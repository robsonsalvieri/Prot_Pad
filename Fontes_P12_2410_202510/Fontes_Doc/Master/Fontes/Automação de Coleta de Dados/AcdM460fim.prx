#INCLUDE "rwmake.ch" 
#INCLUDE "ACDM460FIM.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Funcao   ³ CBM460FIM  º Autor ³ Anderson Rodrigues º Data ³  18/09/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Analisa a rotina e o tipo da NF para verificar se faz o     	º±±
±±º          ³acerto das tabelas para devolucao ou para NF normal        	º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAACD                                                   	º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function CBM460FIM()

If !SuperGetMV("MV_CBPE006",.F.,.F.)
	Return
EndIf

If __cInternet # "AUTOMATICO" .AND. SF2->F2_TIPO # "D"
   AtuSF2CB7()
ElseIf __cInternet # "AUTOMATICO" .AND. SF2->F2_TIPO == "D"
   AtuCB0()   
Endif
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±º Funcao   ³ AtuSF2CB7   º Autor ³ Anderson Rodrigues º Data ³ 08/08/03 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Atualiza as tabelas SF2 e CB7 com os dados da Ordem de      º±±
±±º          ³Separacao - Somente quando a NF e gerada atraves do Protheusº±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAACD                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function AtuSF2CB7()
Local cUltTipoExp
Local nX         := 0
Local nQtdeVol   := 0
Local aOrdSep    := {}
Local aVolume	 := {}

SD2->(DbSetOrder(3))
If ! SD2->(DbSeek(xFilial("SD2")+SF2->F2_DOC+SF2->F2_SERIE+SF2->F2_CLIENTE+SF2->F2_LOJA))   
   Return
Endif   
While ! SD2->(EOF()) .and. xFilial("SD2")  == SD2->D2_FILIAL;
   .and. SD2->D2_DOC     == SF2->F2_DOC;
   .and. SD2->D2_SERIE   == SF2->F2_SERIE;
   .and. SD2->D2_CLIENTE == SF2->F2_CLIENTE ;
	.and. SD2->D2_LOJA    == SF2->F2_LOJA
	If Empty(SD2->D2_ORDSEP)
	   SD2->(DbSkip())
	   Loop
	Endif
	If cPaisLoc =="ARG" .And. FindFunction("fOrdSepara")
		fOrdSepara(@aOrdSep)
	Else
		SC9->(DbSetOrder(7))
		If SC9->(DbSeek(xFilial("SC9")+SD2->D2_COD+SD2->D2_LOCAL+SD2->D2_NUMSEQ));
		.And. SC9->C9_NFISCAL+SC9->C9_SERIENF == SF2->F2_DOC+SF2->F2_SERIE
			aadd(aOrdSep,{SD2->D2_ORDSEP,SC9->C9_ITEM,SD2->D2_PEDIDO})
		EndIf
	EndIf		
	SD2->(DbSkip())
Enddo
If Empty(aOrdSep)
   Return
Endif

For nX := 1 to Len(aOrdSep)
   CB7->(DbSetOrder(1))
   If ! CB7->(DBSeek(xFilial('CB7')+aOrdSep[nX][1]))
      MsgBox(I18N(STR0001,{aOrdSep[nX][1]}),STR0002,"OK") // "Ordem de Separacao #1 nao encontrada na tabela CB7, Verifique !!!"
      Return
   Endif	
   cUltTipoExp:= CBUltExp(CB7->CB7_TIPEXP)
   RecLock("CB7",.F.)
   CB7->CB7_NOTA  := SF2->F2_DOC
   //CB7->CB7_SERIE := SF2->F2_SERIE
   SerieNfId ("CB7",1,"CB7_SERIE",,,,SF2->F2_SERIE)
   CB7->CB7_VOLEMI:= " "
   CB7->CB7_NFEMIT:= " "
   If "03" $ cUltTipoExp
      CB7->CB7_STATUS := "9"  // FINALIZADO   
   ElseIf "03" $ CB7->CB7_TIPEXP 
      CB7->CB7_STATUS := "5"  // gerado nota fiscal
   EndIf
   CB7->(MsUnLock())
   CBLogNF(CB7->CB7_ORDSEP,aOrdSep[nX][2],aOrdSep[nX][3])
   CB9->(DbSetOrder(1))
   CB9->(DBSeek(xFilial('CB9')+CB7->CB7_ORDSEP))
   While CB9->(!Eof() .and. xFilial('CB9')+CB7->CB7_ORDSEP == CB9_FILIAL+CB9_ORDSEP)
      If !Empty(CB9->CB9_VOLUME)
        If Ascan(aVolume,{|x|x == CB9->CB9_VOLUME}) == 0
	 		 Aadd(aVolume, CB9->CB9_VOLUME)
   	 	EndIf
         CB9->(DbSkip())
         Loop
      Endif
      If CB6->(DbSeek(xFilial('CB6')+CB9->CB9_VOLUME)) .AND. !Empty(CB6->CB6_NOTA+CB6->CB6_SERIE) 
         nQtdeVol:= nQtdeVol+1                  
      EndIf	      
      CB9->(DbSkip())
   Enddo
Next

For nX := 1 to Len(aVolume)    
	CB6->(DbSetOrder(1))       
	If CB6->(DbSeek(xFilial('CB6')+aVolume[nX])) .AND. !Empty(CB6->CB6_NOTA+CB6->CB6_SERIE) 
		nQtdeVol++  
	EndIf
Next

If nQtdeVol > 0
	RecLock("SF2",.F.)
	SF2->F2_VOLUME1 := nQtdeVol	 // grava quantidade de volumes na nota
	SF2->(MsUnlock())
EndIf	   
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Funcao   ³ AtuCB0   º Autor ³ Anderson Rodrigues º Data ³  18/09/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Acerto do CB0 na Geracao da NF de devolucao via Protheus,   º±±
±±º          ³Somente se a etiqueta existir e estiver rejeitada pelo C.Q     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAACD                                                       º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function AtuCB0()
Local nX			:= 0
Local aItens		:= {}
Local cCbcQEnd	:= SuperGetMv("MV_CBCQEND",.F.,Space(TamSx3("CB0_LOCALI")[1]))

SD2->(DbSetOrder(3))
If ! SD2->(DbSeek(xFilial("SD2")+SF2->F2_DOC+SF2->F2_SERIE+SF2->F2_CLIENTE+SF2->F2_LOJA))   
   Return
Endif   

While ! SD2->(EOF()) .and. xFilial("SD2")  == SD2->D2_FILIAL;
	.and. SD2->D2_DOC     == SF2->F2_DOC;
	.and. SD2->D2_SERIE   == SF2->F2_SERIE;
	.and. SD2->D2_CLIENTE == SF2->F2_CLIENTE ;
	.and. SD2->D2_LOJA    == SF2->F2_LOJA
	aadd(aItens,SD2->({D2_DOC,D2_SERIE,D2_NFORI,D2_SERIORI,D2_CLIENTE,D2_LOJA,D2_COD,D2_LOCAL,D2_LOCALIZ,D2_NUMSEQ}))
	SD2->(DbSkip())
Enddo

If Empty(aItens)
   Return
Endif

For nX := 1 to Len(aItens)
   CB0->(DbSetOrder(6))
   If CB0->(DbSeek(xFilial("CB0")+aItens[nX,03]+aItens[nX,04]+aItens[nX,05]+aItens[nX,06]+aItens[nX,07]))
	  If CB0->CB0_LOCAL == AlmoxCQ() .and. CB0->CB0_LOCALI # cCbcQEnd
         DigitaEti(nX,aItens)
      Endif   
   Endif
Next
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Funcao   ³DigitaEti º Autor ³ Anderson Rodrigues º Data ³  18/09/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Informacao da etiqueta para gravacao dos dados da Nota      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function DigitaEti(nX,aItens)
Local cEtiqueta := Space(20)

@ 01,001 TO 170,326 DIALOG oDlg TITLE OemToAnsi(STR0003+aItens[nX,07]) // "Ajuste da Etiqueta - Produto "
@ 06,008 TO 076,156

@ 15,018 SAY STR0004 // "Etiqueta"
@ 15,047 GET cEtiqueta Valid VldEtiq(cEtiqueta,nX,aItens) SIZE 70,150

@ 050,047 BMPBUTTON TYPE 01 ACTION OK(nX,aItens)
@ 050,087 BMPBUTTON TYPE 02 ACTION Close(oDlg)
Activate Dialog oDlg Centered
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Funcao   ³ VldEtiq  º Autor ³ Anderson Rodrigues º Data ³  18/09/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Validacao da etiqueta    	                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function VldEtiq(cEtiqueta,nX,aItens)
Local aEtiqueta := {}
Local cCbEndCQ	:= GetMV("MV_CBENDCQ")

CB0->(DbSetOrder(1))
If ! CB0->(DbSeek(xFilial("CB0")+cEtiqueta))
	MsgBox(STR0005,STR0006,"Stop") // "Etiqueta nao encontrada" ### "Atencao"
	Return .f.
Endif
If ascan(aEtiqueta,{|x|x[1] == CB0->CB0_CODETI}) > 0
	MsgBox(STR0007,STR0006,"Stop") //"Etiqueta ja informada" ### "Atencao"
	Return .f.
Endif
If ! Empty(CB0->CB0_NFSAI) .and. ! Empty(CB0->CB0_NFENT)
	MsgBox(STR0008,STR0006,"Stop") // "Etiqueta ja possui NF de devolucao" ### "Atencao"
	Return .f.
Endif
If CB0->(CB0_NFENT+CB0_SERIEE+CB0_FORNEC+CB0_LOJAFO) # aItens[nX,03]+aItens[nX,04]+aItens[nX,05]+aItens[nX,06]
	MsgBox(STR0009,STR0006,"Stop") //"Etiqueta Invalida" ### "Atencao"
	Return .f.
Endif
If CB0->CB0_CODPRO # aItens[nX,07]
   MsgBox(STR0009,STR0006,"Stop") //"Etiqueta Invalida" ### "Atencao"
	Return .f.
Endif
//Se o local da etiqueta não for Arm.CQ OU (MV_CBENDCQ não vazio e o endereço for de inspeção)
If CB0->CB0_LOCAL != AlmoxCQ() .Or. (!Empty(cCbEndCQ) .And. CB0_LOCAL+Alltrim(CB0->CB0_LOCALI)+";" $ cCbEndCQ) 
	MsgBox(STR0009,STR0006,"Stop") //"Etiqueta Invalida" ### "Atencao"
	Return .F.
Endif
aadd(aEtiqueta,{cEtiqueta})
Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Funcao   ³   OK     º Autor ³ Anderson Rodrigues º Data ³  18/09/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Confirmacao da gravacao dos dados da Nota na etiqueta      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function OK(nX,aItens)
RecLock("CB0",.f.)
CB0->CB0_NFSAI := aItens[nX,01]
CB0->CB0_SERIES:= aItens[nX,02]
CB0->(CbLog("10",{CB0_CODPRO,CB0_QTDE,CB0_LOTE,CB0_NFENT,CB0_SERIEE,CB0_FORNEC,CB0_LOJAFO,CB0_LOCAL,CB0_CODETI,"Nota"}))
CB0->(MsUnlock())
Close(oDlg)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ CBLogNF	³ Autor ³ Aecio Ferreira Gomes  ³ Data ³ 02/07/09         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Grava Log dos Eventos da Expedicao após emissão da nota fiscal de  ³±±
±±³			 ³ de saida.                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ M460FIM                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function CBLogNF(cOrdSep,cItemSp,cPedido)
Local nX
Local cStatus
Local aLogExp:={}

CB7->(DbSetOrder(1))
CB7->(DbSeek(xFilial("CB7")+cOrdSep))
If Empty(CB7->CB7_STATUS) .or. CB7->CB7_STATUS == "0"
	cStatus:= STR0010 // "Nao iniciado"
ElseIf CB7->CB7_STATUS == "1"
	cStatus:= STR0011 // "Em separacao"
ElseIf CB7->CB7_STATUS == "2"
	cStatus:= STR0012 // "Separacao finalizada"
ElseIf CB7->CB7_STATUS == "3"
	cStatus:= STR0013 // "Em processo de embalagem"
ElseIf CB7->CB7_STATUS == "4"
	cStatus:= STR0014 // "Embalagem Finalizada"
ElseIf CB7->CB7_STATUS == "5"
	cStatus:= STR0015 // "Nota gerada"
ElseIf CB7->CB7_STATUS == "6"
	cStatus:= STR0016 // "Nota impressa"
ElseIf CB7->CB7_STATUS == "7"
	cStatus:= STR0017 // "Volume impresso"
ElseIf CB7->CB7_STATUS == "8"
	cStatus:= STR0018 // "Em processo de embarque"
ElseIf CB7->CB7_STATUS == "9"
	cStatus:= STR0019 // "Finalizado"
EndIf

CB9->(DBSetOrder(11))
If CB9->(DbSeek(xFilial("CB9")+cOrdSep+cItemSp+cPedido))
	CB8->(DbSetOrder(1))                                               	
  	If CB8->(DbSeek(xFilial("CB8")+cOrdSep+CB9->CB9_ITESEP+CB9->CB9_SEQUEN+CB9->CB9_PROD))
		//Alimenta array do log...
		aadd(aLogExp,{	CB9->CB9_PROD,;
			CB9->CB9_QTESEP,;
			CB9->CB9_LOTECT,;
			CB9->CB9_NUMLOT,;
			CB9->CB9_LOCAL,;
			CB9->CB9_LCALIZ,;
			CB9->CB9_CODETI,;
			CB8->CB8_OP,;
			CB7->CB7_NOTA,;
			CB7->CB7_SERIE,;
			CB7->CB7_CLIENT,;
			CB7->CB7_LOJA,;
			cOrdSep,;
			CB9->CB9_VOLUME,;
			CB9->CB9_SUBVOL,;
			cStatus})
	EndIf
	CB9->(DbSkip())
EndIf

CBG->(DbSetOrder(3))
For nX :=1 to Len(aLogExp)
	CBLog("09",{aLogExp[nX,01],aLogExp[nX,02],aLogExp[nX,03],aLogExp[nX,04],aLogExp[nX,05],aLogExp[nX,06],aLogExp[nX,07],aLogExp[nX,08],aLogExp[nX,09],;
		aLogExp[nX,10],aLogExp[nX,11],aLogExp[nX,12],aLogExp[nX,13],aLogExp[nX,14],aLogExp[nX,15],aLogExp[nX,16]})
Next 
Return
