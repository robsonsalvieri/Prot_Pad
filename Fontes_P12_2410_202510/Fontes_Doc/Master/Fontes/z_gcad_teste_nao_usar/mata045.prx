#INCLUDE "PROTHEUS.CH"
#INCLUDE "APWIZARD.CH"
#INCLUDE "FWBROWSE.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "MATA045.CH"

Static aAllUser
Static aAllGroup
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA045  ³ Autor ³Edson Mar/ Rodrigo Sart³ Data ³ 29.06.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastro de Usuarios Mmessenger                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void MATA045(void)                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MATA045()
Local lUser		:= .T.
Local aUser		:= {}
Local aCodUser	:= {}
Local aEmail	:= {}
Local aGroup	:= {}
Local aCodGroup := {}
Local cUser		:= ""
Local cGroup	:= ""
Local cEmailOri := CriaVar("AN_EMAIL")
Local cCodUserOr:= CriaVar("AN_USER")
Local cCodGroupO:= CriaVar("AN_GRUSER")
Local nUser		:= 0
Local nGroup	:= 0
Local nEmail	:= 0
Local oUser,oGroup,oDlg,oButton1,oButton2,oButton3,oButton4,oButton5,oEmail
Local bListRefresh := {|| A045LRefre(@oUser,@oGroup,@oEmail,@aUser,aAllUser,@aGroup,aAllGroup,@aCodUser,@aCodGroup,@aEmail)}
Local aPages    := {"HEADER"}
Local aTitles   := {OemToAnsi(STR0014),OemToAnsi(STR0015)} // "Usuarios SIGA"###"Nao-Usuarios"
Local cEmail	:=cEmailOri
Local cCodUser	:=cCodUserOr
Local cCodGroup	:=cCodGroupO
Local aAllUsAux	:= {}
Local aAllGrpAux	:= {}
Local aKeyValues	:= {"USR_CODIGO","USR_NOME","USR_EMAIL","USR_MSBLQL"}


Private lEmail2:=.F.
PRIVATE lGrp:=.F.
PRIVATE cSeekMail:=Criavar("AN_USER")+CriaVar("AN_GRUSER")

DEFINE FWMBROWSE oMBrowse ALIAS "SAN"

If aAllUser == Nil .Or. aAllGroup == Nil
	aAllUsAux	:= FWSFAllUsers(,aKeyValues)
	aAllUser := {}
	aEval(aAllUsAux,{|x| If(x[6]=="2",aAdd(aAllUser,x),NIL)} ) // Só Adiciona no array usuários ativos
	aAllUsAux 	:= {} // Limpa array auxiliar
	aAllGroup := {}
	aAllGrpAux	:= FWSFAllGrps(,{"GR__MSBLQL"})
	aEval(aAllGrpAux,{|x| If(x[5]=="2",aAdd(aAllGroup,x),NIL)} ) // Só Adiciona no array grupos ativos
	aAllGrpAux	:= {} // Limpa array auxiliar
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                            ³
//³    1 -Pesquisa e Posiciona em um Banco de Dados              ³
//³    2 -Simplesmente Mostra os Campos                          ³
//³    3 -Inclui registros no Bancos de Dados                    ³
//³    4 -Altera o registro corrente                             ³
//³    5 -Estorna registro selecionado gerando uma contra-partida³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aRotina := MenuDef()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta tela de manutencao de grupos / usuarios                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
A045LRefre(,,,@aUser,aAllUser,@aGroup,aAllGroup,@aCodUser,@aCodGroup,@aEmail)

cUser	:= 0
cGroup	:= 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Chamada dos Ajustes				    				    	   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaHelp()

DEFINE MSDIALOG oDlg TITLE STR0005 FROM 0,0 TO 300,417 OF oMainWnd PIXEL //"Usuarios m-messenger"
@ 2,43 BITMAP oBitmap RESOURCE "MMESSAGE" SIZE 200,90 Of oDlg PIXEL NOBORDER

oFolder := TFolder():New(2.3,.3,aTitles,aPages,oDlg,,,, .F., .F.,206,120,)

@01,05 SAY STR0006 PIXEL OF oFolder:aDialogs[1] //"Usuarios"

@10,05 LISTBOX oUser VAR cUser ITEMS aUser PIXEL SIZE 150,40 OF oFolder:aDialogs[1]
oUser:bGotFocus  := {|| lUser := .T. ,lGrp:=.F.,lEmail2:=.F.}
oUser:bChange    := {|| cGroup := 0,nUser:=oUser:nAT,oGroup:Refresh()}
oUser:bldblClick := {|| cGroup := 0,nUser:=oUser:nAT,oGroup:Refresh()}

@55,05 SAY STR0007 PIXEL OF oFolder:aDialogs[1] //"Grupos"

@64,05 LISTBOX oGroup VAR cGroup ITEMS aGroup PIXEL SIZE 150,40 OF oFolder:aDialogs[1]
oGroup:bGotFocus  := {|| lUser := .F. ,lGrp:=.T.,lEmail2:=.F.}
oGroup:bChange    := {|| cUser := 0,nGroup:=oGroup:nAT,oUser:Refresh()}
oGroup:bldblClick := {|| cUser := 0,nGroup:=oGroup:nAT,oUser:Refresh()}


@10,158 BUTTON oButton1 PROMPT "&"+STR0002 SIZE 44, 11 OF oFolder:aDialogs[1] PIXEL ; //"Incluir"
	ACTION (lEmail2:=.F.,FWExecView("Inclusão","MATA045",3),(Eval(bListRefresh)))

@25,158 BUTTON oButton2 PROMPT "&"+STR0001 SIZE 44, 11 OF oFolder:aDialogs[1] PIXEL ; //"Visualizar"
	ACTION (lEmail2:=.F.,If(A045Seek(lUser,.F.,aCodUser,aCodGroup,aEmail,nUser,nGroup,nEmail),FWExecView(STR0001,"MATA045",1),NIL))

@40,158 BUTTON oButton3 PROMPT "&"+STR0003 SIZE 44, 11 OF oFolder:aDialogs[1] PIXEL ; //"Alterar"
	ACTION (lEmail2:=.F.,If(A045Seek(lUser,.F.,aCodUser,aCodGroup,aEmail,nUser,nGroup,nEmail),FWExecView("Alteração","MATA045",4),NIL))

@55,158 BUTTON oButton4 PROMPT "&"+STR0004 SIZE 44, 11 OF oFolder:aDialogs[1] PIXEL ; //"Excluir"
	ACTION (lEmail2:=.F.,If(A045Seek(lUser,.F.,aCodUser,aCodGroup,aEmail,nUser,nGroup,nEmail),FWExecView("Exclusão","MATA045",5),NIL),(Eval(bListRefresh)))

@70,158 BUTTON oButton5 PROMPT "&"+STR0018 SIZE 44, 11 OF oFolder:aDialogs[1] PIXEL ; //"Sair"
	ACTION oDlg:End()


@01,05 SAY STR0016 PIXEL OF oFolder:aDialogs[2] //"E-mails"

@10,05 LISTBOX oEmail VAR cEmail ITEMS aEmail PIXEL SIZE 150,094 OF oFolder:aDialogs[2]
oEmail:bChange := {||nEmail:=oEmail:nAT,cCodUser:=cCodUserOr,cCodGroup:=cCodGroupO,lGrp:=.F.}
oGroup:bldblClick := {||nEmail:=oEmail:nAT,cEmail:=cEmailOri,cCodUser:=cCodUserOr,cCodGroup:=cCodGroupO}

@10,158 BUTTON oButton1 PROMPT "&"+STR0002 SIZE 44, 11 OF oFolder:aDialogs[2] PIXEL ; //"Incluir"
	ACTION (lEmail2:=.T.,FWExecView("Inclusão","MATA045",3),(Eval(bListRefresh)))

@25,158 BUTTON oButton2 PROMPT "&"+STR0001 SIZE 44, 11 OF oFolder:aDialogs[2] PIXEL ; //"Visualizar"
	ACTION (lEmail2:=.T.,If(A045Seek(.F.,.T.,aCodUser,aCodGroup,aEmail,nUser,nGroup,nEmail),FWExecView(STR0001,"MATA045",1),NIL))

@40,158 BUTTON oButton3 PROMPT "&"+STR0003 SIZE 44, 11 OF oFolder:aDialogs[2] PIXEL ; //"Alterar"
	ACTION (lEmail2:=.T.,If(A045Seek(.F.,.T.,aCodUser,aCodGroup,aEmail,nUser,nGroup,nEmail),FWExecView("Alteração","MATA045",4),NIL))

@55,158 BUTTON oButton4 PROMPT "&"+STR0004 SIZE 44, 11 OF oFolder:aDialogs[2] PIXEL ; //"Excluir"
	ACTION (lEmail2:=.T.,If(A045Seek(.F.,.T.,aCodUser,aCodGroup,aEmail,nUser,nGroup,nEmail),FWExecView("Exclusão","MATA045",5),NIL),(Eval(bListRefresh)))

@70,158 BUTTON oButton5 PROMPT "&"+STR0018 SIZE 44, 11 OF oFolder:aDialogs[2] PIXEL ; //"Sair"
	ACTION oDlg:End()

ACTIVATE DIALOG oDlg CENTERED

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Menu Funcional

@return aRotina - Estrutura
			[n,1] Nome a aparecer no cabecalho
			[n,2] Nome da Rotina associada
			[n,3] Reservado
			[n,4] Tipo de Transação a ser efetuada:
				1 - Pesquisa e Posiciona em um Banco de Dados
				2 - Simplesmente Mostra os Campos
				3 - Inclui registros no Bancos de Dados
				4 - Altera o registro corrente
				5 - Remove o registro corrente do Banco de Dados
				6 - Alteração sem inclusão de registros
				7 - Cópia
			[n,5] Nivel de acesso
			[n,6] Habilita Menu Funcional

@since 21/05/2012
@version P11.6
/*/
//-------------------------------------------------------------------
Static Function MenuDef()
Local aRotina := {}

ADD OPTION aRotina TITLE STR0001		ACTION "VIEWDEF.MATA045"	OPERATION MODEL_OPERATION_VIEW		ACCESS 0 //"Visualizar"	
ADD OPTION aRotina TITLE STR0002	 	ACTION "VIEWDEF.MATA045"	OPERATION MODEL_OPERATION_INSERT	ACCESS 0 //"Incluir"		
ADD OPTION aRotina TITLE STR0003	 	ACTION "VIEWDEF.MATA045"	OPERATION MODEL_OPERATION_UPDATE	ACCESS 0 //"Alterar"		
ADD OPTION aRotina TITLE STR0004 		ACTION "VIEWDEF.MATA045"	OPERATION MODEL_OPERATION_DELETE	ACCESS 3 //"Excluir"	
	
Return aRotina

//-------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Efetua selecao de usuarios
@since 21/05/2012
@version P11.6
/*/
//-------------------------------------------------------------------
Static Function ModelDef()
Local oStruSAN := FWFormStruct(1,'SAN',{|cCampo| !AllTrim(cCampo) $ 'AN_EVENTO|AN_DESCEVE|AN_FORMULA|AN_BFILTRO|AN_FILIAL'})
Local oStruSAN2:= FWFormStruct(1,'SAN',{|cCampo|  !AllTrim(cCampo) $ 'AN_GRUSER|AN_USER|AN_DESCRI|AN_EMAIL'})
Local oModel

If Type("lEmail2") == 'U'
	Private lEmail2 := .F.
Endif

If Type("lGrp") == 'U'
	Private lGrp := .F.
Endif

If lEmail2 // verifica se é entrada de email
	oStruSAN:SetProperty("AN_EMAIL", MODEL_FIELD_OBRIGAT, .T.)
Else
	If lGrp // verifica se é grupo ou usuario
		oStruSAN:SetProperty("AN_GRUSER", MODEL_FIELD_OBRIGAT, .T.)
	Else
		oStruSAN:SetProperty("AN_USER", MODEL_FIELD_OBRIGAT, .T.)
	EndIf
EndIf

oModel := MPFormModel():New('MATA045',,{|oModel|A045PosVld(oModel)})
oModel:AddFields('SANMASTER',,oStruSAN)
oModel:GetModel('SANMASTER'):SetDescription(STR0005)		//"Usuarios m-Messenger"

oModel:AddGRID('SANDETAIL','SANMASTER',oStruSAN2)
oModel:GetModel('SANDETAIL'):SetDescription("STR0006")

oModel:SetRelation( 'SANDETAIL', {{'AN_FILIAL','xFilial("SAN")'},{'AN_USER','AN_USER'},{'AN_GRUSER','AN_GRUSER'},{'AN_EMAIL','AN_EMAIL'}}, SAN->(IndexKey( 1 )) )
oModel:GetModel('SANDETAIL'):SetUniqueline({'AN_EVENTO'})
oModel:GetModel( 'SANDETAIL' ):SetUseOldGrid( .T. )
oModel:SetPrimaryKey({"AN_FILIAL","AN_USER","AN_GRUSER","AN_EMAIL","AN_EVENTO"})
 
Return oModel

//-------------------------------------------------------------------
/*{Protheus.doc} ViewDef
Interface do modelo de dados de cadastro de Usuarios M-Messenger

@since 21/05/2012
@version P11.6
*/
//-------------------------------------------------------------------
Static Function ViewDef()
Local oModel   := FWLoadModel('MATA045') 
Local oStruSAN
Local oStruSAN2:= FWFormStruct(2,'SAN',{|cCampo| !AllTrim(cCampo) $ 'AN_GRUSER|AN_USER|AN_DESCRI|AN_EMAIL'})
Local oView    := FWFormView():New() 


If lEmail2 // verifica se é entrada de email
	oStruSAN := FWFormStruct(2,'SAN',{|cCampo| AllTrim(cCampo) $ 'AN_EMAIL'})
Else
	If lGrp // verifica se é grupo ou usuario
		oStruSAN := FWFormStruct(2,'SAN',{|cCampo| AllTrim(cCampo) $ 'AN_GRUSER|AN_DESCRI'})
	Else
		oStruSAN := FWFormStruct(2,'SAN',{|cCampo| AllTrim(cCampo) $ 'AN_USER|AN_DESCRI'})
	EndIf
EndIf

oView:SetModel(oModel) 
oView:CreateHorizontalBox("MAIN",20)
oView:CreateHorizontalBox("DETAIL",80)

oView:AddField('VIEW_SAN', oStruSAN, 'SANMASTER')
oView:SetOwnerView('VIEW_SAN','MAIN')
	
oView:AddGrid('GRID_SAN', oStruSAN2, 'SANDETAIL')
oView:SetOwnerView('GRID_SAN','DETAIL')
	
Return oView

Static Function A045PosVld(oModel)
Local lRet		:=.T.
Local oModFiel 	:= oModel:GetModel('SANMASTER')
Local oModGrid	:= oModel:GetModel('SANDETAIL')
Local nX        := 0


If lEmail2 // verifica se é entrada de email
	lRet := Iif(Empty(oModFiel:GetValue("AN_EMAIL")),.F.,.T.)
Else
	If lGrp // verifica se é grupo ou usuario
		lRet := Iif(Empty(oModFiel:GetValue("AN_GRUSER")),.F.,.T.)
	Else
		lRet := Iif(Empty(oModFiel:GetValue("AN_USER")),.F.,.T.)
	EndIf
EndIf

If oModel:GetOperation() == MODEL_OPERATION_INSERT
	For nX := 1 to oModGrid:Length()
		oModGrid:GoLine(nX)
		lRet := !SAN->(MsSeek(xFilial('SAN')+oModFiel:GetValue('AN_USER')+oModFiel:GetValue('AN_GRUSER')+oModFiel:GetValue('AN_EMAIL')+oModGrid:GetValue('AN_EVENTO')))
		If !lRet
			Help( " ", 1, "JAGRAVADO" )
			Exit			
		Endif
	Next nX
EndIf
	
Return lRet
/*
admin
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static function A045Commit(oModel)
Return FWformcommit(oModel)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A045Seek  ³ Autor ³ Edson / Rodrigo       ³ Data ³ 29/06/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua pesquisa de existencia de usuario / grupo           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA045                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A045Seek(lUser,lEmail,aCodUser,aCodGroup,aEmail,nUser,nGroup,nEmail,cCodUser,cCodGroup,cEmail)
Local lSeek	:= .F.
Local cSeek	:= ""
                                                      
dbSelectArea("SAN")
If lEmail
	If nEmail > 0
		cEmail:=aEmail[nEmail]
		dbSetOrder(1)
		lSeek := dbSeek(xFilial()+cSeekMail+cEmail)
	EndIf
Else		
	If lUser .And. Len(aCodUser) > 0
		If nUser > 0
			cSeek := aCodUser[nUser]
			dbSetOrder(1)
			lSeek := dbSeek(xFilial()+cSeek)
		EndIf
	ElseIf !lUser .And. Len(aCodGroup) > 0
		If nGroup> 0
			cSeek := aCodGroup[nGroup]
			dbSetOrder(2)
			lSeek := dbSeek(xFilial()+cSeek)
		EndIf
	EndIf
EndIf    

Return lSeek

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A045LRefre³ Autor ³ Edson / Rodrigo       ³ Data ³ 29/06/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua refresh na tela de manutencao de usuarios           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA045                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A045LRefre(oUser,oGroup,oEmail,aUser,aAllUser,aGroup,aAllGroup,aCodUser,aCodGroup,aEmail)
Local aArea		:= GetArea()
Local aAreaSAN		:= SAN->(GetArea())
Local nAcho		:= 0
Local nX			:=0

aGroup 				:= {}
aCodGroup			:= {}
aUser				:= {}
aCodUser			:= {}
aEmail				:= {}

For nx	:= 1 to Len(aAllUser)
	dbSelectArea("SAN")
	dbSetOrder(1)
	If dbSeek(xFilial()+aAllUser[nx,2])
		aAdd(aUser,aAllUser[nx,3])
		aAdd(aCodUser,aAllUser[nx,2])
	EndIf
Next

For nx	:= 1 to Len(aAllGroup)
	dbSelectArea("SAN")
	dbSetOrder(2)
	If dbSeek(xFilial()+aAllGroup[nx,2])
		aAdd(aGroup,aAllGroup[nx,3])
		aAdd(aCodGroup,aAllGroup[nx,2])
	EndIf
Next

dbSelectArea("SAN")
dbSetOrder(1)
dbSeek(xFilial()+cSeekMail)
While !Eof() .And. xFilial()+cSeekMail == AN_FILIAL+AN_USER+AN_GRUSER
	// Inclui e-mail no array
	nAcho:=ASCAN(aEmail,AN_EMAIL)
	If nAcho == 0
		aAdd(aEmail,AN_EMAIL)
	EndIf
	dbSkip()
End
If oUser != Nil
	oUser:SetItems(aUser)
EndIf
If oGroup != Nil
	oGroup:SetItems(aGroup)
EndIf
If oEmail != Nil
	oEmail:SetItems(aEmail)
EndIf

RestArea(aAreaSAN)
RestArea(aArea)
Return .T.


//-------------------------------------------------------------------
/*{Protheus.doc} MA045VALN
preenche o campo descroção apartir do usuário/grupo

@since 21/05/2012
@version P11.6
*/
//-------------------------------------------------------------------
Function MA045VALN(cCampo)
Local oModel := FWModelActive()
Local oView := FWViewActive()
local cUsuEmail:=oModel:GetValue ('SANMASTER',cCampo )
Local lIsInMVC	  := ValType(oModel) <> "U"
Local oField  	  := If(lIsInMVC,oModel:GetModel('SANMASTER'),NIL)
Local lRet := .T.
	
If lGrp
	oModel:SetValue('SANMASTER','AN_DESCRI',GrpRetName(cUsuEmail))
Else
	oModel:SetValue('SANMASTER','AN_DESCRI',UsrRetName(cUsuEmail))
EndIf

PswOrder(1)
If PswSeek( Iif(lIsInMVC,oField:GetValue("AN_USER"),M->AN_USER),.T.)  
	If PswRet()[1,17]
		lRet := .F.				
		Help('',1,'USERBLQ',,'Usuario bloqueado',1,0)
	EndIf
EndIf

If lRet .and. !empty(M->AN_USER) .and. empty(M->AN_GRUSER)
	If SAN->(dbSeek(xFilial("SAN")+Iif(lIsInMVC,oField:GetValue("AN_USER"),M->AN_USER)))
		lRet := .F.				
		Help('',1,'A045USER',,'Usuario ja cadastrado',1,0)	
	EndIf
ElseIf lRet .and. empty(M->AN_USER) .and. !empty(M->AN_GRUSER)
	SAN->(DbSetOrder(2))
	If SAN->(dbSeek(xFilial("SAN")+Iif(lIsInMVC,oField:GetValue("AN_GRUSER"),M->AN_GRUSER)))
		lRet := .F.				
		Help('',1,'A045USER',,'  Grupo ja cadastrado',1,0)	
	EndIf
EndIf
	
If !lRet
	oView:Refresh()
EndIf


Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} MA045INID
inicializador padrão do campo descrição

@since 21/05/2012
@version P11.6
*/
//-------------------------------------------------------------------
Function MA045INID()
local cRet:=""
Local oModel := FWModelActive()
If oModel:getOperation() <> 3 // inclusão
	If lGrp
		cRet:=GrpRetName(oModel:GetValue ('SANMASTER','AN_GRUSER'))
	Else
		cRet:=UsrRetName(oModel:GetValue ('SANMASTER','AN_USER' ))
	EndIf
EndIf
	
Return cRet
//-------------------------------------------------------------------
/*{Protheus.doc} MA045VALGR
Valida o nome do grupo de Usuarios

@since 21/05/2012
@version P11.6
*/
//-------------------------------------------------------------------
Function MA045VALGR()
local cGrupo	:=""
Local lRet		:=.T.
Local oModel 	:= FWModelActive()
	
cGrupo:=GrpRetName(oModel:GetValue ('SANMASTER','AN_GRUSER'))

If Empty(cGrupo)
	lRet:=.F.
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} AjustaHelp()
Ajuste de Help
@author Manuela Cavalcante
@since 05/05/2016
@version 1.0
@return 
/*/
//-------------------------------------------------------------------
Static Function AjustaHelp()
Local aHelpPor
Local aHelpSpa
Local aHelpEng

aHelpPor :=	{"Usuario já cadastrado"}
aHelpSpa :=	{"El usuario ya registrado"}
aHelpEng :=	{"User already registered"}
PutHelp("PA045USER",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Verifique se o usuario ja esta cadastrado"}
aHelpSpa :=	{"Compruebe que el usuario ya suscrito"}
aHelpEng :=	{"Verify that the User already subscribed"}
PutHelp("SA045USER",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Usuario bloqueado"}
aHelpSpa :=	{"Bloquedo usuario"}
aHelpEng :=	{"Bloquedo user"}
PutHelp("PUSERBLQ",aHelpPor,aHelpEng,aHelpSpa,.F.)

aHelpPor :=	{"Verifique o cadastro do usuario"}
aHelpSpa :=	{"Compruebe el registro del usuario"}
aHelpEng :=	{"Check the User 's registration"}
PutHelp("SUSERBLQ",aHelpPor,aHelpEng,aHelpSpa,.F.)

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} Ma045DscEv()
Inicializador padrão do campo Descrição do Evento (AN_DESCEVE)
@author Leonardo Magalhães
@since 09/10/2018
@version 1.0
@return 
/*/
//-------------------------------------------------------------------

Function Ma045DscEv()
Local oModel 	:= FWModelActive()
Local lIsInMVC	:= ValType(oModel) <> "U" .And. oModel:IsActive()
Local oField	:= If(lIsInMVC, oModel:GetModel("SANDETAIL"), NIL)
Local cRet		:= "" 

If lIsInMVC
	If oModel:GetOperation() <> MODEL_OPERATION_INSERT
		If ValType(oField) <> "U" .And. oField:Length() == 0
			cRet := Posicione("SX5", 1, xFilial("SX5") + "E3" + SAN->AN_EVENTO, "X5_DESCRI")
		EndIf
	EndIf
Else
	cRet := If(!Inclui, Posicione("SX5", 1, XFILIAL("SX5") + "E3" + SAN->AN_EVENTO, "X5_DESCRI"), "") 
EndIf
	
Return cRet
