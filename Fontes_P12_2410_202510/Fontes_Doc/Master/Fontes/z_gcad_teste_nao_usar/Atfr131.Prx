#Include "Atfr130.ch"
#include "Protheus.ch"

Static _oATFR1311

// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres./////

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ATFR131  ³ Autor ³ Wagner Mobile Costa   ³ Data ³ 11.11.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Raz„o Analitico do Ativo (Baseado no SN4)                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ ATFR131                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³ Uso      ³ SIGAATF                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ATFR131()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL cString := "SN4"
LOCAL cDesc1  := OemToAnsi(STR0001)   //"Este relatorio ir  imprimir o Raz„o Auxiliar do Ativos"
LOCAL cDesc2  := OemToAnsi(STR0002)   //"Imobilizados (RAZAO) por Conta na moeda corrente."
LOCAL cDesc3  := ""
LOCAL wnrel
LOCAL cCol1
LOCAL cCol2

PRIVATE aReturn  := { OemToAnsi(STR0040), 1,OemToAnsi(STR0041), 2, 2, 1, "",1 }
PRIVATE aLinha   := { }
PRIVATE cPerg    :="AFR131"
PRIVATE nomeprog :="ATFR131"
PRIVATE nLastKey := 0
PRIVATE titulo   := OemToAnsi(STR0003)  //"Razao Auxiliar"
PRIVATE cabec1   := ""
PRIVATE cabec2   := ""
PRIVATE tamanho  := "M"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                                  ³
//³ mv_par01         // da Conta                                        ³
//³ mv_par02         // ate a Conta                                     ³
//³ mv_par03         // Da Data                                         ³
//³ mv_par04         // Ate a Data                                      ³
//³ mv_par05         // da Moeda                                        ³
//³ mv_par06         // Salta pagina na quebra de conta ? (Sim/Nao)     ³
//³ mv_par07         // Quebra Mes a Mes ? (Sim/Nao)                    ³
//³ mv_par08         // Pagina Inicial         							³
//³ mv_par09         // Pagina Final           							³
//³ mv_par10         // Numero Pag p/Reiniciar 							³		
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte( "AFR131",.F. )

SET DECIMALS TO 4

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Direciona para definicao dos parametros da impressao do relatorio         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel :="ATFR131"
wnrel :=SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,"",,Tamanho)

If nLastKey == 27
	Return
End

SetDefault( aReturn,cString )

If nLastKey == 27
	Return
End

cAtfMoeda := GETMV("MV_ATFMOEDA")
cSimbolo  := AllTrim( GETMV("MV_SIMB" + Str(mv_par05+1,1,0) ) )

cCol1   := OemToAnsi(STR0004)+AllTrim(GETMV("MV_SIMB1"))  //"Valor em "
cCol1   := Space(17-Len(cCol1))+cCol1
cCol2   := OemToAnsi(STR0005) + cSimbolo   //"Valor "
cCol2   := Space(13-Len(cCol2))+cCol2

titulo  += OemToAnsi(STR0006) + cSimbolo  //" em "
cabec1  := OemToAnsi(STR0007)+cCol1+OemToAnsi(STR0008)+cCol2+OemToAnsi(STR0009)  //"  Data       Historico       "###" Refer.  "###"            Acrescimo           Decrescimo       Saldo  Atual"


RptStatus({|lEnd| FR131Imp(@lEnd,wnRel,cString)},Titulo)

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ATFR131  ³ Autor ³ Wagner Mobile Costa   ³ Data ³ 11.11.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Raz„o Analitico do Ativo                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ ATFR131(lEnd,WnRel,Titulo)                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd    - A‡Æo do Codeblock                                ³±±
±±³          ³ wnRel   - T¡tulo do relat¢rio                              ³±±
±±³          ³ cString - Mensagem                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function FR131Imp(lEnd,WnRel,cString)

Local cConta,aSaldos[5],nMoeda
Local nFirst:=0
Local lValor
Local nTotAcresc := 0
Local nTotDimin  := 0
Local cRefere    := " "
Local nValorX    := 0
Local nMoedaX    := 0
Local aMeses     := {OemToAnsi(STR0010),OemToAnsi(STR0011),OemToAnsi(STR0012),OemToAnsi(STR0013),OemToAnsi(STR0014),OemToAnsi(STR0015),;  //"Jan"###"Fev"###"Mar"###"Abr"###"Mai"###"Jun"
                     OemToAnsi(STR0016),OemToAnsi(STR0017),OemToAnsi(STR0018),OemToAnsi(STR0019),OemToAnsi(STR0020),OemToAnsi(STR0021)}   //"Jul"###"Ago"###"Set"###"Out"###"Nov"###"Dez"

Local aMesExt    := {OemToAnsi(STR0022),OemToAnsi(STR0023),OemToAnsi(STR0024),OemToAnsi(STR0025),OemToAnsi(STR0026),OemToAnsi(STR0027),;  //"Janeiro"###"Fevereiro"###"Marco"###"Abril"###"Maio"###"Junho"
							OemToAnsi(STR0028),OemToAnsi(STR0029),OemToAnsi(STR0030),OemToAnsi(STR0031),OemToAnsi(STR0032),OemToAnsi(STR0033)}   //"Julho"###"Agosto"###"Setembro"###"Outubro"###"Novembro"###"Dezembro"

Local aTotMeses  := {}
Local dMesAtual  := cTod("")
Local nTacMes    := 0
Local nTdimMes   := 0
Local CbTxt
Local cbCont
Local limite:=132
Local cGrupoPass := GetMv("MV_GRPASS")
Local lInverte   := .F.
Local lCtb       := CtbInUse(), cAliasQry := "TMP", cTipoSn3, aCampos := SN5->(DbStruct())

cbtxt   := SPACE( 10 )
cbcont  := 0
li      := 80
m_pag   := mv_par08

titulo  := OemToAnsi(STR0034)+ cSimbolo  //"Razao Auxiliar em "

Aadd(aCampos, { "N5_TIPOSN3", "C", 2, 0 })

cAliasQry := GetNextAlias()

If _oATFR1311 <> Nil
	_oATFR1311:Delete()
	_oATFR1311 := Nil
Endif

_oATFR1311 := FWTemporaryTable():New( cAliasQry )  
_oATFR1311:SetFields(aCampos) 
_oATFR1311:AddIndex("1", {"N5_CONTA","N5_TIPOSN3","N5_DATA","N5_TIPO"})

//------------------
//Criação da tabela temporaria
//------------------
_oATFR1311:Create()  

AtfGerRazao(mv_par03, mv_par04, mv_par01, mv_par02, cAliasQry)
(cAliasQry)->(DbGoTop())

cMoeda  := Str(mv_par05+1,1)
nMoeda  := mv_par05+1

SetRegua((cAliasQry)->(RecCount()))

While ! (cAliasQry)->(Eof()) .And. (cAliasQry)->N5_CONTA<=mv_par02
    
	IF lEnd
		@PROW()+1,001 PSAY OemToAnsi(STR0035)  //"CANCELADO PELO OPERADOR"
		Exit
	End
   	lInverte := .F.
	cConta 	 := (cAliasQry)->N5_CONTA
	cTipoSn3 := (cAliasQry)->N5_TIPOSN3
	nFirst 	 := 0
          
 	If lCtb
		dbSelectArea("CT1")
		dbSetOrder(1)
		If dbSeek(xFilial("CT1")+cConta)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ lInverte ‚ .T. se conta pertence ao grupo de contas Credoras (cGru-³
			//³ poPass),I1_NORMAL = "C", mas o saldo e devedor                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If (CT1->CT1_NORMAL=="1" .And. SUBS(cConta,1,1)$cGrupoPass)
				lInverte := .T.
			Endif
		Endif
 	Else
		dbSelectArea("SI1")
		dbSetOrder(1)
		If dbSeek(xFilial("SI1")+cConta)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ lInverte ‚ .T. se conta pertence ao grupo de contas Credoras (cGru-³
			//³ poPass),I1_NORMAL = "C", mas o saldo e devedor                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If (SI1->I1_NORMAL=="D" .And. SUBS(cConta,1,1)$cGrupoPass)
				lInverte := .T.
			Endif
		Endif
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Calcula Saldo Inicial.O saldo inicial nao muda de sinal, pois  ³
	//³na virada anual j  vem c/ o sinal correto de acordo com a condi-³
	//³‡„o da conta (I1_NORMAL).                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea(cAliasQry)
	IF (cAliasQry)->N5_TIPO == "0"
		aSaldos[1] := (cAliasQry)->N5_VALOR1
		aSaldos[2] := (cAliasQry)->N5_VALOR2
		aSaldos[3] := (cAliasQry)->N5_VALOR3
		aSaldos[4] := (cAliasQry)->N5_VALOR4
		aSaldos[5] := (cAliasQry)->N5_VALOR5
		(cAliasQry)->( dbSkip())
	Else
		Afill( aSaldos, 0 )
	End
    
	aTotMeses := aClone( aSaldos )

	While !(cAliasQry)->( Eof() ) .and. 	(cAliasQry)->N5_CONTA == cConta .And.;
											(cAliasQry)->N5_TIPOSN3 == cTipoSn3
        
		If li > 58
			If m_pag > mv_par09
				m_pag := mv_par10
			EndIf	
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,IIF(aReturn[4]==1,15,18))
		End
        
		IF lEnd
			@PROW()+1,001 PSAY OemToAnsi(STR0035)  //"CANCELADO PELO OPERADOR"
			Exit
		End
        
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Movimentacao da regua                                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IncRegua()
        
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se nao ultrapassou o intervalo solicitado                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF (cAliasQry)->N5_DATA > mv_par04
			(cAliasQry)->(dbSkip())
			Loop
		Endif
        
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Acumula o saldo anterior. Neste caso, considero o lInverte pois es-³
		//³ tou gerando o saldo anterior lendo os registros de movimentos do   ³
		//³ SN5 e n„o o registro de saldo inicial.                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF (cAliasQry)->N5_DATA < mv_par03
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ N5_tipo == 0 +- Saldo Inicial ( D/C)        ³
			//³            1  + Imobilizacao                ³
			//³            2  + Ampliacao                   ³
			//³            3  + Reavaliacao                 ³
			//³            4  + Depreciacao                 ³
			//³            5  - Baixa                       ³
			//³            6  + Corr.Monet.                 ³
			//³            7  + Corr.Monet.Depr.            ³
			//³            8  - Transf.de                   ³
			//³            9  + Transf.para                 ³
			//³            A  - Imob Capital                ³
			//³            B  + Imob Capital                ³
			//³            C  + Baixa Capital               ³
			//³            D  - Baixa Capital c/Prejuizo    ³
			//³            E  - Amplia‡„o Capital           ³
			//³            F  + Amplia‡„o Capital Prejuizo  ³
			//³            G  + Transf DE Patrimonio        ³
			//³            H  - Transf DE Patrimonio Prej.  ³
			//³            I  - Transf P/ Patrimonio        ³
			//³            J  + Transf P/ Patrimonio Prej.  ³
			//³            K  + Deprec Acelerada Positiva   ³
			//³            L  - Deprec Acelerada Negativa   ³
			//³            M  + Equiv Patrimonial Positiva  ³
			//³            N  - Equiv Patrimonial Negativa  ³
			//³            O  - Correcao de capital  "SCA"  ³
			//³            P  +/- Invent rio                ³
			//³            Q  + Aquis por Transferencia     ³
			//³            R  - Baixa por Aquis por Transf  ³
			//³            S  - Transf de bem entre Filiais ³
			//³            T  + Transf p/ bem entre Filiais ³
			//³            U  + Transf de Patr entre Filiais³
			//³            V  - Transf p/ Patr entre Filiais³			
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lInverte
				If (cAliasQry)->N5_TIPO $ "1234679BCFGJKPQSV"
					aSaldos [ 1 ]        -= (cAliasQry)->N5_VALOR1
					aSaldos [ nMoeda ]   -= &(cAliasQry + "->N5_VALOR"+cMoeda)
					aTotMeses [ 1 ]      -= (cAliasQry)->N5_VALOR1
					aTotMeses [ nMoeda ] -= &(cAliasQry + "->N5_VALOR"+cMoeda)
				Else
					aSaldos [ 1 ]        += (cAliasQry)->N5_VALOR1
					aSaldos [ nMoeda ]   += &(cAliasQry + "->N5_VALOR"+cMoeda)
					aTotMeses [ 1 ]      += (cAliasQry)->N5_VALOR1
					aTotMeses [ nMoeda ] += &(cAliasQry + "->N5_VALOR"+cMoeda)
				Endif
			Else
				If (cAliasQry)->N5_TIPO $ "1234679BCFGJKPQTU"
					aSaldos [ 1 ]        += (cAliasQry)->N5_VALOR1
					aSaldos [ nMoeda ]   += &(cAliasQry + "->N5_VALOR"+cMoeda)
					aTotMeses [ 1 ]      += (cAliasQry)->N5_VALOR1
					aTotMeses [ nMoeda ] += &(cAliasQry + "->N5_VALOR"+cMoeda)
				Else
					aSaldos [ 1 ]        -= (cAliasQry)->N5_VALOR1
					aSaldos [ nMoeda ]   -= &(cAliasQry + "->N5_VALOR"+cMoeda)
					aTotMeses [ 1 ]      -= (cAliasQry)->N5_VALOR1
					aTotMeses [ nMoeda ] -= &(cAliasQry + "->N5_VALOR"+cMoeda)
				Endif
			Endif

			(cAliasQry)->( dbSkip( ) )
			Loop
		Endif
		
		IF nFirst == 0
			nFirst++   
			If lCtb
				dbSelectArea("CT1")
				dbSetOrder( 1 )
				dbSeek(cFilial+cConta)
				@ li    ,  0    PSAY OemToAnsi(STR0036)+Mascara(cConta)+STR0042 + cTipoSn3 + " - " + CT1->CT1_DESC01  //"CONTA: " //" - TIPO - "
			Else
				dbSelectArea("SI1")
				SI1->( dbSetOrder( 1 ) )
				SI1->( dbSeek( cFilial+cConta ) )
				@ li    ,  0    PSAY OemToAnsi(STR0036)+Mascara(cConta)+STR0042 + cTipoSn3 + " - " + SI1->I1_DESC  //"CONTA: " //" - TIPO - "
			EndIf				
			@++li   ,  9    PSAY OemToAnsi(STR0037)  //"SALDO ANTERIOR"
			@ li    , 25    PSAY aSaldos[1]         PICTURE PesqPict("SN5","N5_VALOR1"      ,21)
			@ li    ,112    PSAY aSaldos[nMoeda]    PICTURE PesqPict("SN5","N5_VALOR"+cMoeda,20)
			li++
			dbSelectArea ( cAliasQry )
		EndIf
        
		lValor:=.F.
		If lInverte  
			If (cAliasQry)->N5_TIPO $"1234679BCFGJKPQSV"
				lValor := .T.
				aSaldos [ 1 ]        -= (cAliasQry)->N5_VALOR1
				aSaldos [ nMoeda ]   -= &(cAliasQry + "->N5_VALOR"+cMoeda)
				aTotMeses [ 1 ]      -= (cAliasQry)->N5_VALOR1
				aTotMeses [ nMoeda ] -= &(cAliasQry + "->N5_VALOR"+cMoeda)
			Else
				aSaldos [ 1 ]        += (cAliasQry)->N5_VALOR1
				aSaldos [ nMoeda ]   += &(cAliasQry + "->N5_VALOR"+cMoeda)
				aTotMeses [ 1 ]      += (cAliasQry)->N5_VALOR1
				aTotMeses [ nMoeda ] += &(cAliasQry + "->N5_VALOR"+cMoeda)
			Endif
		Else
			If (cAliasQry)->N5_TIPO $"1234679BCFGJKPQTU"
				lValor := .T.
				aSaldos [ 1 ]        += (cAliasQry)->N5_VALOR1
				aSaldos [ nMoeda ]   += &(cAliasQry + "->N5_VALOR"+cMoeda)
				aTotMeses [ 1 ]      += (cAliasQry)->N5_VALOR1
				aTotMeses [ nMoeda ] += &(cAliasQry + "->N5_VALOR"+cMoeda)
			Else
				aSaldos [ 1 ]        -= (cAliasQry)->N5_VALOR1
				aSaldos [ nMoeda ]   -= &(cAliasQry + "->N5_VALOR"+cMoeda)
				aTotMeses [ 1 ]      -= (cAliasQry)->N5_VALOR1
				aTotMeses [ nMoeda ] -= &(cAliasQry + "->N5_VALOR"+cMoeda)
			Endif
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao da movimentacao identificada                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cRefere := aMeses[Month((cAliasQry)->N5_DATA)]+"/"+Subs(StrZero(Year((cAliasQry)->N5_DATA),4),3,2)
		nValorX := &(cAliasQry + "->N5_VALOR"+cMoeda)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³  Imprime o valor registrado da ufir se o valor da ufir for registrada e            ³
		//³  se a moeda do relat¢rio for ufir.                                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (cAliasQry)->N5_TAXA # 0 .and. (mv_par05 + 1) = Val(cAtfMoeda)
			nMoedaX := (cAliasQry)->N5_TAXA
		Else    
			If nValorX == 0
				nMoedaX := IIF(aSaldos[nMoeda] <> 0, aSaldos[1] / aSaldos[nMoeda], 0)
			Else
				nMoedaX := (cAliasQry)->N5_VALOR1 / nValorX     //),X3Decimal("N5_VALOR"+cMoeda))
			Endif
		Endif

		@ li,  0 PSAY (cAliasQry)->N5_DATA
		@ li, 11 PSAY AFDescSal( (cAliasQry)->N5_TIPO )
		If lInverte
			If (cAliasQry)->N5_TIPO=="B"
				@ li, 25 PSAY (cAliasQry)->N5_VALOR1*(-1) PICTURE  PesqPict("SN5","N5_VALOR1",21)
			Else
				@ li, 25 PSAY (cAliasQry)->N5_VALOR1 PICTURE  PesqPict("SN5","N5_VALOR1",21)
			Endif
		Else
			If (cAliasQry)->N5_TIPO == "A"
				@ li, 25 PSAY (cAliasQry)->N5_VALOR1*(-1) PICTURE  PesqPict("SN5","N5_VALOR1",21)
			Else
				@ li, 25 PSAY (cAliasQry)->N5_VALOR1 PICTURE  PesqPict("SN5","N5_VALOR1",21)
			Endif
		Endif
		@ li, 47 PSAY cRefere
		@ li, 55 PSAY nMoedaX  PICTURE PesqPict("SM2","M2_MOEDA"+cMoeda,11)

		If lValor
			@ li, 69    PSAY nValorX    PICTURE PesqPict("SN5","N5_VALOR"+cMoeda,20)
			nTotAcresc += nValorX
			nTacMes += nValorx
		Else
			@ li, 90    PSAY nValorX    PICTURE PesqPict("SN5","N5_VALOR"+cMoeda,20)
			nTotDimin  += nValorX
			nTdimMes += nValorx
		EndIf
		If lInverte
			If (cAliasQry)->N5_TIPO=="B"
				@ li,112   PSAY aSaldos[nMoeda]*(-1) PICTURE PesqPict("SN5","N5_VALOR"+cMoeda,20)
			Else
				@ li,112   PSAY aSaldos[nMoeda] PICTURE PesqPict("SN5","N5_VALOR"+cMoeda,20)
			Endif
		Else
			If (cAliasQry)->N5_TIPO=="A"
				@ li,112   PSAY aSaldos[nMoeda]*(-1) PICTURE PesqPict("SN5","N5_VALOR"+cMoeda,20)
			Else
				@ li,112   PSAY aSaldos[nMoeda] PICTURE PesqPict("SN5","N5_VALOR"+cMoeda,20)
			Endif		
		Endif
		li++
		dMesAtual   := (cAliasQry)->N5_DATA
		cContaAtual := (cAliasQry)->N5_CONTA
		dbSelectArea(cAliasQry)
		dbSkip()

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Trata a quebra por mes                                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If mv_par07 == 1 .and. nFirst # 0

			If Month((cAliasQry)->N5_DATA)	# Month(dMesAtual)  .or.;
				Year((cAliasQry)->N5_DATA)	# Year(dMesAtual)	.or.;
				cContaAtual # (cAliasQry)->N5_CONTA
				
				li ++
				@ li,  0    PSAY    OemToAnsi(STR0038)+Iif(!Empty(dMesAtual), aMesExt[Month(dMesAtual)], "")    //"TOTAL DO MES: "
				@ li, 25    PSAY    aTotMeses[1]        PICTURE PesqPict("SN5","N5_VALOR1"      ,21)
				@ li, 69    PSAY    nTacMes             PICTURE PesqPict("SN5","N5_VALOR"+cMoeda,20)
				@ li, 90    PSAY    nTdimMes            PICTURE PesqPict("SN5","N5_VALOR"+cMoeda,20)
				@ li,112    PSAY    aTotMeses[nMoeda]   PICTURE PesqPict("SN5","N5_VALOR"+cMoeda,20)
				li++
				@ li, 25    PSAY    "---------------------"
				@ li, 69    PSAY    "--------------------"
				@ li, 90    PSAY    "--------------------"
				@ li,112    PSAY    "--------------------"
				li ++
			Endif
		Endif
	Enddo
    
	afill(aTotMeses ,0)
	nTacMes := 0
	nTdimMes := 0
    
	IF nFirst == 0
		IF li > 58
			If m_pag > mv_par09
				m_pag := mv_par10
			EndIf	
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,IIF(aReturn[4]==1,15,18))
		Endif
		nFirst++
		If lCtb
			dbSelectArea( "CT1" )
			dbSetOrder( 1 )
			dbSeek( cFilial+cConta )
			@ li    , 0    PSAY OemToAnsi(STR0036)+Mascara(cConta)+STR0042 + cTipoSn3 + " - " + CT1->CT1_DESC01  //"CONTA: " //" - TIPO - "
		Else
			dbSelectArea( "SI1" )
			SI1->(dbSetOrder( 1 ))
			SI1->(dbSeek( cFilial+cConta ))	
			@ li    , 0    PSAY OemToAnsi(STR0036)+Mascara(cConta)+STR0042 + cTipoSn3 + " - " + SI1->I1_DESC  //"CONTA: " //" - TIPO - "
		EndIf
		@ ++li  , 9     PSAY OemToAnsi(STR0037)  //"SALDO ANTERIOR"
		@ li    , 25    PSAY aSaldos[1]         PICTURE PesqPict("SN5","N5_VALOR1"      ,21)
		@ li    ,112    PSAY aSaldos[nMoeda]    PICTURE PesqPict("SN5","N5_VALOR"+cMoeda,20)
		li++
		dbSelectArea ( cAliasQry )
	End

	IF nFirst != 0
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Somente mostra o total da conta se nao houver quebra por mes. ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If mv_par07 # 1
			@ li, 25    PSAY "---------------------"
			@ li, 69    PSAY "--------------------"
			@ li, 90    PSAY "--------------------"
			@ li,112    PSAY "--------------------"
			li++
			@ li, 00    PSAY OemToAnsi(STR0039)  //"TOTAL DA CONTA "
			@ li, 25    PSAY aSaldos[1]         PICTURE     PesqPict("SN5","N5_VALOR1"      ,21)
			@ li, 69    PSAY nTotAcresc         PICTURE     PesqPict("SN5","N5_VALOR"+cMoeda,20)
			@ li, 90    PSAY nTotDimin          PICTURE     PesqPict("SN5","N5_VALOR"+cMoeda,20)
			@ li,112    PSAY aSaldos[nMoeda]    PICTURE     PesqPict("SN5","N5_VALOR"+cMoeda,20)
		Endif
		li++
		@ li, 0 PSAY    Replicate("-",Limite)
		li++
		nTotAcresc := 0
		nTotDimin  := 0
	Endif
    
	IF MV_PAR06 == 1
		li:=60
	Endif
	dbSelectArea ( cAliasQry )
Enddo

IF li != 80
	Roda(cbcont,cbtxt,Tamanho)
Endi

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura posi‡”es originais                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Set Device To Screen
dbSelectArea( "SN5" )
SN5->( dbSetOrder( 1 ) )

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	Ourspool(wnrel)
Endif
MS_FLUSH()

//Deleta tabela temporaria no banco de dados
If _oATFR1311 <> Nil
	_oATFR1311:Delete()
	_oATFR1311 := Nil
Endif

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtfGerRazao³ Autor ³ Wagner Mobile Costa  ³ Data ³ 13/11/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Grava o arquivo temporario de saldos por conta/tipo        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cConta    = Conta Contabil                                 ³±±
±±³          ³ dData     = Data do saldo                                  ³±±
±±³          ³ cTipo     = Tipo do saldo                                  ³±±
±±³          ³ cTipoSn3  = Origem do tipo de saldo gerado p/ bem (01/02)  ³±±
±±³          ³ nValorn   = Valores em cada uma das moedas                 ³±±
±±³          ³ cSinal    = Operacao a ser feita                           ³±±
±±³          ³ nTaxa     = Taxa media para apresentacao no razao          ³±±
±±³          ³ cAliasQry = Alias temporario para atualizacao dos valores  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAATF                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtfGerRazao(dDataIni, dDataFim, cContaIni, cContaFim, cAliasSn5)

Local cAliasQry
Local cFilterUser:=aReturn[7]


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicia a leitura do SN4                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cAliasQry := SelDados(	"SN4", "N4_FILIAL = '" + xFilial("SN4") + "' .And. "+;
						"N4_CONTA >= '" + cContaIni + "' .And. " +;
						"N4_CONTA <= '" + cContaFim + "' .And. " +;
						"DTOS(N4_DATA) <= '" + Dtos(dDataFim) + "'", .F.)

While !Eof()

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Movimenta a regua                                                   ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	CarregaSel("SN4")
    
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Posiciona no SN1                                                    ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    dbSelectArea("SN1")
    dbSeek(xFilial("SN1")+SN4->N4_CBASE+SN4->N4_ITEM)

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Posiciona no SN3                                                    ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    dbSelectArea("SN3")
    dbSeek(xFilial("SN3")+SN4->N4_CBASE+SN4->N4_ITEM+SN4->N4_TIPO)
    dbSelectArea("SN4")
	 nTaxaDepr := SN4->N4_TXMEDIA
	
    If SN4->N4_OCORR == "01"        // Baixa
        If SN1->N1_PATRIM $ "N "    // Bem Normal
            cCodSn5 := "5"
        Elseif SN1->N1_PATRIM $ "CAS" // Bem Patrimonial
            cCodSn5 := "C"
        Else
            cCodSn5 := "D"           // Patrimonial Prejuizo
        End
    End

    If SN4->N4_OCORR == "05"        // Arquisi‡„o
        If SN1->N1_PATRIM $ "N "    // Bem Normal
            cCodSn5 := "1"
        Elseif SN1->N1_PATRIM $ "CAS" // Bem Patrimonial
            cCodSn5 := "A"
        Else
            cCodSn5 := "B"           // Patrimonial Prejuizo
        End
    End

    If SN4->N4_OCORR == "09"        // Amplia‡„o
        If SN1->N1_PATRIM $ "N "    // Bem Normal
            cCodSn5 := "2"
        Elseif SN1->N1_PATRIM $ "CAS" // Bem Patrimonial
            cCodSn5 := "E"
        Else
            cCodSn5 := "F"           // Patrimonial Prejuizo
        End
    End

    If SN4->N4_OCORR == "03"        // Transferencia DE
        If SN1->N1_PATRIM $ "N "    // Bem Normal
            cCodSn5 := "8"
        Elseif SN1->N1_PATRIM $ "CAS" // Bem Patrimonial
            cCodSn5 := "G"
        Else
            cCodSn5 := "H"           // Patrimonial Prejuizo
        End
    End

    If SN4->N4_OCORR == "04"        // Transferencia DE
        If SN1->N1_PATRIM $ "N "    // Bem Normal
            cCodSn5 := "9"
        Elseif SN1->N1_PATRIM $ "CAS" // Bem Patrimonial
            cCodSn5 := "I"
        Else
            cCodSn5 := "J"           // Patrimonial Prejuizo
        End
    End

    If SN4->N4_OCORR == "06"        // Deprecia‡„o
        cCodSn5 := "4"
    End

    If SN4->N4_OCORR == "07"        // Corre‡„o do bem
		If SN1->N1_PATRIM $ " NP"
      	cCodSn5 := "6"
		Else
			cCodSn5 := "O"	
		EndIf
    End

    If SN4->N4_OCORR == "08"        // Corre‡„o
        cCodSn5 := "7"
    End

	If SN4->N4_OCORR == "13"
		cCodSn5 := "P"
	EndIf

	If SN4->N4_OCORR == "15"
		cCodSn5 := "Q"
	Endif
	
	If SN4->N4_OCORR == "16"
		cCodSn5 := "R"
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Considera filtro do usuario                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Empty(cFilterUser) .Or. (&cFilterUser)
		If ! Empty(SN4->N4_CONTA)
			(cAliasSn5)->(dbSeek( SN4->N4_CONTA+SN4->N4_TIPO+Dtos(SN4->N4_DATA)+cCodSn5 ))
			If !Found()
				(cAliasSn5)->(Reclock(cAliasSn5,.T.))
				(cAliasSn5)->N5_CONTA  	:= SN4->N4_CONTA
				(cAliasSn5)->N5_DATA   	:= SN4->N4_DATA
				(cAliasSn5)->N5_TIPO   	:= cCodSn5
				(cAliasSn5)->N5_TIPOSN3	:= SN4->N4_TIPO
			Else
				(cAliasSn5)->(Reclock(cAliasSn5))
			EndIf
		
			(cAliasSn5)->N5_TAXA := SN4->N4_TXMEDIA
			(cAliasSn5)->N5_VALOR1 += SN4->N4_VLROC1
			(cAliasSn5)->N5_VALOR2 += SN4->N4_VLROC2
			(cAliasSn5)->N5_VALOR3 += SN4->N4_VLROC3
			(cAliasSn5)->N5_VALOR4 += SN4->N4_VLROC4
			(cAliasSn5)->N5_VALOR5 += SN4->N4_VLROC5
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Caso as contas sejam zeradas, ser„o autom ticamente deletadas             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			IF (cAliasSn5)->N5_VALOR1 == 0 .and. ;
				(cAliasSn5)->N5_VALOR2 == 0 .and. ;
				(cAliasSn5)->N5_VALOR3 == 0 .and. ;
				(cAliasSn5)->N5_VALOR4 == 0 .and. ;
				(cAliasSn5)->N5_VALOR5 == 0
				(cAliasSn5)->(Reclock(cAliasSn5,.F.,.T.))
				(cAliasSn5)->(dbDelete())
			EndIf
			msUnlock()
		Endif
	Endif	
   dbSelectArea(cAliasQry)
   dbSkip()
End

RemoveSel("SN4")

Return .T.