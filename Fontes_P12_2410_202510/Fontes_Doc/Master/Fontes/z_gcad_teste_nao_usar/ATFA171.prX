#Include "Protheus.ch"
#Include "Atfa171.ch"


// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.
//***********************************
// Controle de multiplas moedas  	*
//***********************************
Static lMultMoed := .T.
STATIC lIsRussia	:= If(cPaisLoc$"RUS",.T.,.F.) // CAZARINI -  Flag to indicate if is Russia location

/*/
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北谀哪哪哪哪穆哪哪哪哪哪哪履哪哪哪履哪哪哪哪哪哪哪哪哪哪哪履哪哪穆哪哪哪哪哪勘?
北Funo	 ?ATFA171	  ?Autor ?Claudio D de Souza    ?Data ?25/09/06 潮?
北媚哪哪哪哪呐哪哪哪哪哪哪聊哪哪哪聊哪哪哪哪哪哪哪哪哪哪哪聊哪哪牧哪哪哪哪哪幢?
北Descrio ?Clculo de Depreciao Acelerada                             潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北Uso		 ?Generico 										            潮?
北滥哪哪哪哪牧哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪俦?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌?
*/
Function ATFA171
//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪?
//?Define Variaveis                                                          ?
//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪?
Local cCadastro := OemtoAnsi(STR0001) //"Efetuar a Acelerao"
Local aSays := {}, aButtons := {}

Private aRotina := MenuDef()

nOpca := 0

dbSelectArea("SN3")
dbSetOrder(2) 

Pergunte("AFA171",.F.)

AADD(aSays,STR0005)
AADD(aSays,STR0006)
AADD(aSays,STR0007)
AADD(aSays,STR0008)

AADD(aButtons, { 5,.T.,{|| Pergunte("AFA171",.T. ) } } )
AADD(aButtons, { 1,.T.,{|| If(Af171VPerg().And.Af171Vld(), (FechaBatch(), Processa({|lEnd| af171Proc()})), Nil ) }} )
AADD(aButtons, { 2,.T.,{|| FechaBatch() }} )

FormBatch( cCadastro, aSays, aButtons )

Return

/*/
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北谀哪哪哪哪穆哪哪哪哪哪哪履哪哪哪履哪哪哪哪哪哪哪哪哪哪哪履哪哪穆哪哪哪哪哪勘?
北Funo	 ?Af171VPerg ?Autor ?Claudio D de Souza    ?Data ?25/09/06 潮?
北媚哪哪哪哪呐哪哪哪哪哪哪聊哪哪哪聊哪哪哪哪哪哪哪哪哪哪哪聊哪哪牧哪哪哪哪哪幢?
北Descrio ?Validar as perguntas                              				 潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北Uso		 ?ATFA171 													             	 潮?
北滥哪哪哪哪牧哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪俦?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌?
*/
Static Function Af171VPerg
Local dNextDepr 	:= (GetMv("MV_ULTDEPR")) + 1
Local lRet 			:= .F.

Do Case
Case mv_par05 <= 1
	Aviso(STR0011,STR0013, {"Ok"})  // "Para efetuar a acelera玢o, o fator deve ser maior que 1"
Case	Left(Dtos(mv_par06),6) != Left(Dtos(dNextDepr),6) .Or.;
		Left(Dtos(mv_par07),6) != Left(Dtos(dNextDepr),6)
	Aviso(STR0011,STR0014 + Str(Month(dNextDepr),2)+"/"+Str(Year(dNextDepr),4), {"Ok"})  // "Para efetuar a acelera玢o, o perodo deve estar dentro do ms/ano do prximo clculo de deprecia玢o: "
Case	dDataBase != mv_par07
	Aviso(STR0011,STR0015 + Dtoc(mv_par07), {"Ok"}) // "Para efetuar a acelera玢o, a data base do sistema deve igual a "
OtherWise
	lRet := .T.	
EndCase

Return lRet

/*/
苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北谀哪哪哪哪穆哪哪哪哪哪履哪哪哪履哪哪哪哪哪哪哪哪哪哪哪履哪哪穆哪哪哪哪哪勘?
北Funo	 AF171Proc ?Autor ?Claudio D de Souza	  ?Data ?5/09/06  潮?
北媚哪哪哪哪呐哪哪哪哪哪聊哪哪哪聊哪哪哪哪哪哪哪哪哪哪哪聊哪哪牧哪哪哪哪哪幢?
北Descrio ?Calculo de depreciao Acelerada							  		  潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北Sintaxe e ?AF171Proc             									  			  潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北?Uso		 ?															  				  潮?
北滥哪哪哪哪牧哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪俦?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌?
*/

Function AF171Proc()

Local oDlg1, oQtda
Local oMark:=0
Local lInverte := .f.
Local cAlias    := Alias()
Local cIndex:=""
Local cChave:=""
Local nSavOrd1:=IndexOrd()
Local aCampos := {}
Local aSize 

Private cMarca   := GetMark( )
Private cArquivo	:= ""
Private nTotal 	:= 0
Private nHdlPrv	:= 0
Private cPadrao   := ""
Private nQtdBens  := 0 
//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪?
//?Carrega as perguntas selecionadas:                            ?
//?mv_par01  - Do Bem ? 														?
//?mv_par02  - Ate o Bem ? 												   ?
//?mv_par03  - Do Item do Bem ? 								         ?
//?mv_par04  - Ate o Item do Bem ?											?
//?mv_par05  - Fator ?                                           ?
//?mv_par06  - Data inicial ?                                    ?
//?mv_par07  - Data Final ?                                      ?
//?mv_par08  - Da Conta ?                                        ?
//?mv_par09  - Ate a Conta ?                                     ?
//?mv_par10  - Do Centro de Custo ?                              ?
//?mv_par11  - Ate o Centro de Custo ?                           ?
//?mv_par12  - Do Item Contabil ?                                ?
//?mv_par13  - Ate o Item Contabil ?                             ?
//?mv_par13  - Da Classe de Valor ?                              ?
//?mv_par14  - Ate a Classe de Valor ?                           ?
//?mv_par16  - Mostra Lanc. Cont. ?                              ?
//?mv_par17  - Aglut Lactos ?                                    ?
//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪?

//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪?
//?Verifica o numero do Lote do modulo Ativo Fixo 		         ?
//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪?
cLoteAtf := LoteCont("ATF")

aCampos := {}
AADD(aCampos,{"N3_OK","","  ",""})
dbSelectArea("SX3")
dbSeek ("SN3")
Do While SX3->(!EOF()) .And. (SX3->x3_arquivo == "SN3")
	IF (cNivel >= SX3->x3_nivel .and. SX3->X3_context # "V") .Or.;
		(SX3->X3_PROPRI == "U" .AND. SX3->X3_CONTEXT!="V" .AND. SX3->X3_TIPO<>'M') .Or.;
		Alltrim(SX3->X3_CAMPO) $ "N3_CBASE#N3_ITEM"
		AADD(aCampos,{SX3->X3_CAMPO,"",X3Titulo(),SX3->X3_PICTURE})
	Endif
	SX3->(dbSkip())
Enddo

dbSelectArea("SN3")
dbSetOrder(2)

//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪目
//?Monta expresso do Filtro para seleo							  ?
//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪馁
cIndex := CriaTrab(nil,.f.)
cChave  := IndexKey( )
IndRegua("SN3",cIndex,cChave,,Af171ChecF(),STR0010) //"Selecionando Registros..."
nIndex := RetIndex("SN3")
dbSelectArea("SN3")
#IFNDEF TOP
	dbSetIndex(cIndex+OrdBagExt())
#ENDIF
dbSetOrder(nIndex+1)
DbGoTop()
If BOF() .and. EOF()
	RetIndex("SN3")
	dbSetOrder(nSavOrd1)
	Set Filter to
	FErase (cIndex+OrdBagExt())
	Return
EndIf

nQtdBens := 0	  // quantidade de Bens,mostrado no rodap?do browse
dbGotop()

nOpca := 0
aF171Marca("SN3",cMarca)
//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪目
//?Faz o calculo automatico de dimensoes de objetos     ?
//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪馁
aSize := MSADVSIZE()	

DEFINE MSDIALOG oDlg1 TITLE OemToAnsi(STR0011) From aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL //"Calculo de Depreciao Acelerada"
oDlg1:lMaximized := .T.
	
oPanel := TPanel():New(0,0,'',oDlg1,, .T., .T.,, ,40,40,.T.,.T. )
oPanel:Align := CONTROL_ALIGN_TOP

@1.5, .8 Say OemToAnsi(STR0012) OF oPanel //"Tot de Bens a Depre..."
@1.5, 12 Say oQtda VAR nQtdBens OF oPanel SIZE 50,10
oMark := MsSelect():New(cAlias,"N3_OK",,aCampos,@lInverte,@cMarca,{35,oDlg1:nLeft,oDlg1:nBottom,oDlg1:nRight})
oMark:bMark := {| | Af171Display(cMarca,lInverte,oQtda)}
oMark:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

ACTIVATE MSDIALOG oDlg1 ON INIT EnchoiceBar(oDlg1,{|| nOpca := 1,oDlg1:End()},{|| nOpca := 0,ODlg1:End()})

If nOpca # 1
	dbSetOrder(1)
	RetIndex("SN3")
	dbSetOrder(nSavOrd1)
	Set Filter to
	FErase (cIndex+OrdBagExt())
	Return
Endif

SN1->(DbSetOrder(1))

dbGoTop() // O SN3 esta filtrado

While !(SN3->(EOF()))

	If SN3->N3_OK == cMarca
		SN1->(MsSeek(xFilial("SN1")+SN3->N3_CBASE+SN3->N3_ITEM))
		If Empty(SN1->N1_DTBLOQ) .Or. Dtos(SN1->N1_DTBLOQ) < Dtos(dDataBase)
			Af171Acele()   //CalCula Aceleracao
		Endif	
	Endif	

	SN3->(dbSkip())
	
End

If nQtdBens != 0 .and. nHdlPrv > 0 .And. mv_par16 != 3
	RodaProva(nHdlPrv,nTotal)
	lDigita   := IIf(mv_par16 = 1,.T.,.f.)
	lAglutina := IIf(mv_par17 = 1, .t.,.f.)
	cA100Incl(cArquivo,nHdlPrv,1,cLoteAtf,lDigita,lAglutina)
End

//Apagar Indices temporarios
//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪目
//?Restaura os ndices 										 ?
//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪馁
RetIndex("SN3")
dbSetOrder(nSavOrd1)
Set Filter to
FErase (cIndex+OrdBagExt())
Return

/*/
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北谀哪哪哪哪穆哪哪哪哪哪履哪哪哪履哪哪哪哪哪哪哪哪哪哪哪履哪哪穆哪哪哪哪哪勘?
北Funo	 Af171ChecF?Autor ?Claudio D de Souza    ?Data ?25/09/06 潮?
北媚哪哪哪哪呐哪哪哪哪哪聊哪哪哪聊哪哪哪哪哪哪哪哪哪哪哪聊哪哪牧哪哪哪哪哪幢?
北Descrio Retorna Condio para Indice filtrado da rotina   			  潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北Sintaxe	 Af171ChecF() 												           潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北?Uso		 ATFA171													              潮?
北滥哪哪哪哪牧哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪俦?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌?
*/
Function Af171ChecF()
Local cFiltro := ""

	cFiltro := 'N3_FILIAL == "' + xFilial("SN3")
	cFiltro += '" .AND. N3_CBASE   >= "' + mv_par01
	cFiltro += '" .AND. N3_CBASE   <= "' + mv_par02
	cFiltro += '" .AND. N3_ITEM    >= "' + mv_par03
	cFiltro += '" .AND. N3_ITEM    <= "' + mv_par04
	cFiltro += '" .AND. N3_CCONTAB >= "' + mv_par08
	cFiltro += '" .AND. N3_CCONTAB <= "' + mv_par09
	cFiltro += '" .AND. N3_CUSTBEM >= "' + mv_par10
	cFiltro += '" .AND. N3_CUSTBEM <= "' + mv_par11
	cFiltro += '" .AND. N3_SUBCCON >= "' + mv_par12
	cFiltro += '" .AND. N3_SUBCCON <= "' + mv_par13
	cFiltro += '" .AND. N3_CLVLCON >= "' + mv_par14
	cFiltro += '" .AND. N3_CLVLCON <= "' + mv_par15
	cFiltro += '" .AND. Val(N3_BAIXA) == 0 '
	cFiltro += ' .AND.  N3_CCONTAB <> " " .And. N3_CCDEPR <> " " .And. N3_CDEPREC <> " "' 
	
	If lIsRussia
		cFiltro += ' .AND. N3_OPER  == "1" ' // CAZARINI - 22.01.2017 - Asset Into Operation?
	Endif
    If ExistBlock("AF171Filtr")
    	cFilAux := ExecBlock( "AF171Filtr", .F., .F., {cFiltro} )
    	If Valtype( cFilAux ) == "C"
    		cFiltro := cFilAux
    	EndIf
    Endif 	

Return cFiltro

/*
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北谀哪哪哪哪穆哪哪哪哪哪履哪哪哪履哪哪哪哪哪哪哪哪哪哪哪履哪哪穆哪哪哪哪哪勘?
北Funo	 af171Marca?Autor ?Claudio D de Souza    ?Data ?25/09/06 潮?
北媚哪哪哪哪呐哪哪哪哪哪聊哪哪哪聊哪哪哪哪哪哪哪哪哪哪哪聊哪哪牧哪哪哪哪哪幢?
北Descrio ?Marcar todos os bens filtrados							        潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北Sintaxe	 ?Af171Marca(ExpN1,ExpD1,ExpD2) 							        潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北?Uso		 ?Atfa171													              潮?
北滥哪哪哪哪牧哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪俦?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌?
*/
STATIC Function Af171Marca(cAlias,cMarca)
LOCAL lSavTTS
LOCAL nRec
LOCAL cAliasAnt := Alias()

lSavTTS := __TTSInUse
__TTSInUse := .F.

dbSelectArea(cAlias)
nRec := Recno()
DbGoTop()
While !Eof()
	SN1->(MsSeek(xFilial("SN1") + SN3->(N3_CBASE + N3_ITEM)))
	If SN1->N1_STATUS != "2"
		RecLock("SN3")
		SN3->N3_OK := cMarca
		nQtdBens++
	Else
		RecLock("SN3")
		SN3->N3_OK := " "		
	EndIf
	/*
	 * Tratamento da sele玢o de bens na deprecia玢o acelerada,
	 * No selecionar os bens que possuirem a classifica玢o 
	 * patrimonial como "Custos de Transa玢o"
	 */
	If SN1->N1_PATRIM == "T"
		RecLock("SN3")
		SN3->N3_OK := " "	
	EndIf
	dbSkip()
EndDo
dbGoto(nRec)

__TTSInUse := lSavTTS
dbSelectArea(cAliasAnt)
Return

/*
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北谀哪哪哪哪穆哪哪哪哪哪哪履哪哪哪履哪哪哪哪哪哪哪哪哪哪哪履哪哪穆哪哪哪哪哪勘?
北Funo	 Af171Display?Autor ?Claudio D de Souza 	 ?Data ?25/09/06 潮?
北媚哪哪哪哪呐哪哪哪哪哪哪聊哪哪哪聊哪哪哪哪哪哪哪哪哪哪哪聊哪哪牧哪哪哪哪哪幢?
北Descrio Atualiza tela de seleao de registros       	             	 潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北Sintaxe	 Af171Display( cMarca,lInverte,oQtda,cArquivo) 				    潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北?Uso		 ?ATFA171														             潮?
北滥哪哪哪哪牧哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪俦?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌?
*/
Function af171Display(cMarca,lInverte,oQtda,cArquivo)
Local cFieldMarca := "N3_OK"

SN1->(MsSeek(xFilial("SN1") + SN3->(N3_CBASE + N3_ITEM)))

If IsMark(cFieldMarca,cMarca,lInverte)
	If SN1->N1_STATUS != "2"
		nQtdBens++
		oQtda:Refresh()
	Else
		RecLock("SN3",.F.)
		SN3->N3_OK := " "
		MsUnlock()
		Help(" ",1,"AF171BLQ")	//"Este bem est?bloqueado, a deprecia玢o no poder?ser acelerada."		
	EndIf

	/*
	 * Tratamento da sele玢o de bens com classifica玢o patrimonial como "Custos de Transa玢o"
	 * No permitir a sele玢o.
	 */
	If SN1->N1_PATRIM == "T"
		SN3->(RecLock("SN3",.F.))
		SN3->N3_OK := " "
		SN3->(MsUnlock())
		Help(" ",1,"AF171CUSTR")
	EndIf
Else
	nQtdBens--
	nQtdBens:= Iif(nQtdBens<0,0,nQtdBens)
	oQtda:Refresh()
EndIf

Return

/*/
苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北谀哪哪哪哪穆哪哪哪哪哪哪哪履哪哪穆哪哪哪哪哪哪哪哪哪哪哪履哪穆哪哪哪哪哪勘?
北Funo    ?Af171Acele   Autor ? Alice Y Yamamoto    Data?07/05/98 潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪聊哪哪牧哪哪哪哪哪哪哪哪哪哪哪聊哪牧哪哪哪哪哪幢?
北Descrio ?Calcula a nova depreciaao Acumulada                       潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北Sintaxe	 ?Af171Acele( )                                              潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北?Uso		 ?ATFA171                                                    潮?
北滥哪哪哪哪牧哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪俦?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌?
*/

Function Af171Acele()
Local aArea     := GetArea()
//********************************
// Controle de multiplas moedas  *
//********************************
Local __nQuantas := If(lMultMoed,AtfMoedas(),5)
Local nX

Private aValDepr := If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0})
/*Private nValDepr1 := 0
Private nValDepr2 := 0 
Private nValDepr3 := 0 
Private nValDepr4 := 0 
Private nValDepr5 := 0*/
Private nDepr1		:= 0

DbSelectArea("SN3")
//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪?
//?A data  de incio de depreciao deve ser a data em que o bem ?
//?realmente comeou a ser depreciado.                           ?
//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪?
nDiasParcial := (mv_par07 - mv_par06) + 1
	
//nValDepr1 := 0; nValDepr2  := 0; nValDepr3 := 0; nValDepr4 := 0
//nValDepr5 := 0
nDiferenca := 0

//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪目
//?nValDepr1  - Ultima depreciacao Mensal do Bem em Real            ?
//?nValDepr3  - Ultima Depreciacao Mensal do Bem em UFIR            ?
//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪馁

//********************************
// Controle de multiplas moedas  *
//********************************

aValDepr[1] := 0
If Abs(N3_VRDACM1 + N3_VRCDA1) < Abs(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1)
	IF SN3->N3_TXDEPR1 > 0
		nTaxa 		:= Abs((SN3->N3_TXDEPR1 * mv_par05)- SN3->N3_TXDEPR1) * (( nDiasParcial / Day(LastDay(dDataBase)))) / 1200
		aValDepr[1]  	:= Round(Abs(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1) * nTaxa, X3Decimal("N3_VORIG1"))
		nDiferenca 	:= Abs(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1) - (aValDepr[1] + Abs(N3_VRDACM1+ N3_VRCDA1))
		If Round(nDiferenca,X3Decimal("N3_VORIG1")) <= 0
			nDepr1  := Abs(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1)-Abs(N3_VRDACM1 + N3_VRCDA1)
		Endif
	Endif
Endif

For nX := 2 to __nQuantas
	cMoed := Alltrim(Str(nX))
	aValDepr[nX] := 0
	If Abs(N3_VRDACM2) < Abs(&("N3_VORIG"+cMoed)+&("N3_AMPLIA"+cMoed))
		IF SN3->&("N3_TXDEPR"+cMoed) > 0
			nTaxa 		:= Abs((SN3->&("N3_TXDEPR"+cMoed) * mv_par05)- SN3->&("N3_TXDEPR"+cMoed)) * (( nDiasParcial / Day(LastDay(dDataBase)))) / 1200
			aValDepr[nX]  	:= Round(Abs(&("N3_VORIG"+cMoed)+&("N3_AMPLIA"+cMoed))*nTaxa, X3Decimal("N3_VORIG"+cMoed))
			nDiferenca 	:= Abs(&("N3_VORIG"+cMoed)+&("N3_AMPLIA"+cMoed))-(aValDepr[nX] + Abs(&("N3_VRDACM"+cMoed)))
			If Round(nDiferenca,X3Decimal("N3_VORIG2")) <= 0
				aValDepr[nX] := Abs(&("N3_VORIG"+cMoed)+&("N3_AMPLIA"+cMoed)) - &("N3_VRDACM"+cMoed)
			Endif
		Endif
	Endif
	

Next
//谀哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪?
//?Trata os resduos de depreciaao.                                     ?
//滥哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪?
If nDepr1 != 0
	aValDepr[1] := nDepr1
Endif

Af171Grava()

RestArea(aArea)

Return

/*/
苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北谀哪哪哪哪穆哪哪哪哪哪哪哪履哪哪穆哪哪哪哪哪哪哪哪哪哪哪履哪穆哪哪哪哪哪勘?
北Funo    ?Af171AGrava  Autor ? Alice Y Yamamoto    Data?14/05/98 潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪聊哪哪牧哪哪哪哪哪哪哪哪哪哪哪聊哪牧哪哪哪哪哪幢?
北Descrio ?Atualiza os arquivos SN3,SN4,SN5                           潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北Sintaxe	 ?Af171Grava( )                                              潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北?Uso		 ?ATFA171                                                    潮?
北滥哪哪哪哪牧哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪俦?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌?
*/
Function Af171Grava()
Local aArea    := GetArea()
Local lPadrao
Local dNextDepr 	:= (GetMv("MV_ULTDEPR")) + 1
Local lSoma			:= .F.
//********************************
// Controle de multiplas moedas  *
//********************************
Local __nQuantas := If(lMultMoed,AtfMoedas(),5)
Local nX
Local lOnOff := .T.

lOnOff := MV_PAR18 == 1

dbSelectArea("SN3")
RecLock("SN3")

If aValDepr[1] != 0
	
	//Verifica se j?h?aceleracao no mes corrente.
	If !Empty(SN3->N3_DTACELE) .and. ( Left(DTOS(mv_par07),6) == Left(DTOS(dNextDepr),6) .And. Left(DTOS(SN3->N3_DTACELE),6) == Left(DTOS(dNextDepr),6))
		lSoma := .T.
	Endif

	SN3->N3_INDICE1 := (SN3->N3_TXDEPR1 * mv_par05)
	//Se estiver no mesmo mes e ja tenha ocorrido outra depreciacao acelerada, o valor sera somado.
	If lSoma
		SN3->N3_VRDMES1 += aValDepr[1]
	Else
		SN3->N3_VRDMES1 := aValDepr[1]	
	Endif	
	SN3->N3_VRDACM1 += aValDepr[1] 
	SN3->N3_VRDBAL1 += aValDepr[1] 
	// Se a data da ultima aceleracao, estiver dentro do proximo mes de calculo de depreciacao, soma o valor de 
	// aceleracao do mes, senao substitui.
	SN3->N3_VLACEL1 := If(Left(DTOS(SN3->N3_DTACELE),6) == Left(DTOS(dNextDepr),6), SN3->N3_VLACEL1 + aValDepr[1], aValDepr[1])
	SN3->N3_DTACELE := mv_par07
EndIf

//********************************
// Controle de multiplas moedas  *
//********************************
For nX:=2 to __nQuantas
	cMoed := Alltrim(Str(nX))
	If aValDepr[nX] != 0
		SN3->&(If(nX>9,"N3_INDIC","N3_INDICE")+cMoed) := (SN3->&(If(nX>9,"N3_TXDEP","N3_TXDEPR")+cMoed) * mv_par05)
		//Se estiver no mesmo mes e ja tenha ocorrido outra depreciacao acelerada, o valor sera somado.
		If lSoma
			SN3->&(If(nX>9,"N3_VRDME","N3_VRDMES")+cMoed) += aValDepr[nX]
		Else
			SN3->&(If(nX>9,"N3_VRDME","N3_VRDMES")+cMoed) := aValDepr[nX]
		Endif
		SN3->&(If(nX>9,"N3_VRDAC","N3_VRDACM")+cMoed) += aValDepr[nX]
		SN3->&(If(nX>9,"N3_VRDBA","N3_VRDBAL")+cMoed) += aValDepr[nX]
		// Se a data da ultima aceleracao, estiver dentro do proximo mes de calculo de depreciacao, soma o valor de 
		// aceleracao do mes, senao substitui.
		SN3->&(If(nX>9,"N3_VLACE","N3_VLACEL")+cMoed) := If(Left(DTOS(SN3->N3_DTACELE),6) == Left(DTOS(dNextDepr),6), SN3->&(If(nX>9,"N3_VLACE","N3_VLACEL")+cMoed) + aValDepr[nX], aValDepr[nX])
		SN3->N3_DTACELE := mv_par07
	EndIf

Next
msUnLock()

//22/07/2010
PcoIniLan("000365") 
//********************************
// Controle de multiplas moedas  *
//********************************
cPadrao := "840"

If aValDepr[1] != 0
	Reclock("SN4",.T.)
		SN4->N4_FILIAL 	:= xFilial("SN4")
		SN4->N4_CBASE  	:= SN3->N3_CBASE
		SN4->N4_ITEM   	:= SN3->N3_ITEM
		SN4->N4_TIPO   	:= SN3->N3_TIPO
		SN4->N4_OCORR  	:= IIf(aValDepr[1] > 0, "10", "11")   // 10 aCeleracao Positiva
		SN4->N4_TIPOCNT	:= "4"  //DEPREC ACUM
		SN4->N4_CONTA  	:= SN3->N3_CCDEPR
		SN4->N4_DATA   	:= dDataBase
		SN4->N4_VLROC1 	:= Abs(aValDepr[1])
		SN4->N4_VLROC2 	:= Abs(aValDepr[2])
		SN4->N4_VLROC3 	:= Abs(aValDepr[3])
		SN4->N4_VLROC4 	:= Abs(aValDepr[4])
		SN4->N4_VLROC5 	:= Abs(aValDepr[5])		
		SN4->N4_CCUSTO 	:= SN3->N3_CCCDEP
		SN4->N4_SUBCTA 	:= SN3->N3_SUBCCDE
		SN4->N4_CLVL   	:= SN3->N3_CLVLCDE
		SN4->N4_SEQ    	:= SN3->N3_SEQ
		SN4->N4_TXDEPR	:= nTaxa
		SN4->N4_TPSALDO	:= SN3->N3_TPSALDO
	msUnLock()
	
	dbSelectArea("SN4")
	dbSetOrder(1)
	Reclock("SN4",.T.)
		SN4->N4_FILIAL := xFilial("SN4")
		SN4->N4_CBASE  := SN3->N3_CBASE
		SN4->N4_ITEM   := SN3->N3_ITEM
		SN4->N4_TIPO   := SN3->N3_TIPO
		SN4->N4_OCORR  := IIf(aValDepr[1] > 0, "10", "11")   // 10 aCeleracao Positiva
		SN4->N4_TIPOCNT:= "3"  //despesa de depreciacao
		SN4->N4_CONTA  := SN3->N3_CDEPREC
		SN4->N4_DATA   := dDataBase
		
		If lMultMoed
			AtfMultMoe("SN4","N4_VLROC",{|x| Abs(aValDepr[X]) })
		Else
			SN4->N4_VLROC1 := Abs(aValDepr[1])
			SN4->N4_VLROC2 := Abs(aValDepr[2])
			SN4->N4_VLROC3 := Abs(aValDepr[3])
			SN4->N4_VLROC4 := Abs(aValDepr[4])
			SN4->N4_VLROC5 := Abs(aValDepr[5])
		EndIf
		
		SN4->N4_TXDEPR	:= nTaxa
		SN4->N4_CCUSTO := SN3->N3_CCDESP
		SN4->N4_SUBCTA := SN3->N3_SUBCDEP
		SN4->N4_CLVL   := SN3->N3_CLVLDEP
		SN4->N4_SEQ    := SN3->N3_SEQ
		SN4->N4_LA		 := iIf(lOnOff,"S","N")
		SN4->N4_ORIGEM := Funname()
		SN4->N4_LP		 := cPadrao
		SN4->N4_TPSALDO	:= SN3->N3_TPSALDO
	msUnLock()
	
	//22/07/2010
	PcoDetLan("000365","01","ATFA171")
	ATFSaldo(	SN3->N3_CCDEPR , dDataBase, IIf(aValDepr[1] > 0,"K","L"),;
	Abs(aValDepr[1]), Abs(aValDepr[2]), Abs(aValDepr[3]), Abs(aValDepr[4]),;
	Abs(aValDepr[5]), "+", SN3->N3_TXDEPR1 * mv_par05, SN3->N3_SUBCCDE,, SN3->N3_CLVLCDE,;
	SN3->N3_CCCDEP,"4", aValDepr )
	
	ATFSaldo(	SN3->N3_CDEPREC, dDataBase,IIf(aValDepr[1] > 0,"K","L"),;
	Abs(aValDepr[1]), Abs(aValDepr[2]), Abs(aValDepr[3]), Abs(aValDepr[4]),;
	Abs(aValDepr[5]),"+",SN3->N3_TXDEPR1 * mv_par05,SN3->N3_SUBCDEP,,SN3->N3_CLVLDEP,;
	SN3->N3_CCDESP,"3", aValDepr )
	
	If lOnOff
		lPadrao := VerPadrao(cPadrao)
	
		If mv_par16 != 3
			If nHdlPrv <= 0
				nHdlPrv := HeadProva(cLoteAtf,"ATFA171",Substr(cUsername,1,6),@cArquivo)
			Endif
			nTotal  += DetProva(nHdlPrv,cPadrao,"ATFA171",cLoteAtf)
		Endif
	EndiF
	
EndIf

PcofinLan("000365")

RestArea(aArea)

Return

/*/
苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北谀哪哪哪哪穆哪哪哪哪哪履哪哪哪履哪哪哪哪哪哪哪哪哪哪哪履哪哪穆哪哪哪哪哪勘?
北Programa  MenuDef   ?Autor ?Ana Paula N. Silva     ?Data ?9/11/06 潮?
北媚哪哪哪哪呐哪哪哪哪哪聊哪哪哪聊哪哪哪哪哪哪哪哪哪哪哪聊哪哪牧哪哪哪哪哪幢?
北Descrio ?Utilizacao de menu Funcional                               潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北Retorno   Array com opcoes da rotina.                                 潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北ParametrosParametros do array a Rotina:                               潮?
北?         ?. Nome a aparecer no cabecalho                             潮?
北?         ?. Nome da Rotina associada                                 潮?
北?         ?. Reservado                                                潮?
北?         ?. Tipo de Transao a ser efetuada:                        潮?
北?         ?	1 - Pesquisa e Posiciona em um Banco de Dados     潮?
北?         ?   2 - Simplesmente Mostra os Campos                       潮?
北?         ?   3 - Inclui registros no Bancos de Dados                 潮?
北?         ?   4 - Altera o registro corrente                          潮?
北?         ?   5 - Remove o registro corrente do Banco de Dados        潮?
北?         ?. Nivel de acesso                                          潮?
北?         ?. Habilita Menu Funcional                                  潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪穆哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北?  DATA   ?Programador   Manutencao efetuada                         潮?
北媚哪哪哪哪呐哪哪哪哪哪哪哪呐哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪幢?
北?         ?              ?                                           潮?
北滥哪哪哪哪牧哪哪哪哪哪哪哪牧哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪哪俦?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌?
/*/
Static Function MenuDef()
	Local aRotina		:={ {STR0004, "AllwaysTrue",0,3}} //"Parametros"
Return(aRotina)
/*
苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
北赏屯屯屯屯脱屯屯屯屯屯送屯屯屯淹屯屯屯屯屯屯屯屯屯退屯屯屯淹屯屯屯屯屯屯槐?
北Programa  Af171Vld  Autor  Microsiga           ?Data ? 13/09/11   罕?
北掏屯屯屯屯拓屯屯屯屯屯释屯屯屯贤屯屯屯屯屯屯屯屯屯褪屯屯屯贤屯屯屯屯屯屯贡?
北Desc.     Valida se trabalha com data virada de saldos                罕?
北?         ?                                                           罕?
北掏屯屯屯屯拓屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯贡?
北Uso       ?AP                                                         罕?
北韧屯屯屯屯拖屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯急?
北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北?
哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌?
*/
Static Function Af171Vld()
Local lSldAtf
Local lRet := .T.
Local dUltDepr 	:= GetMv("MV_ULTDEPR")

lSldAtf 	:= GetNewPar("MV_ATFRSLD",.F.)

//se tem o parametro MV_ATFRSLD (REFAZ SALDO) CONFIGURADO COM .T. e controla data de virada
If lSldAtf
	dDataVir :=  AtfGetSN0("13","VIRADAATIVO")
	If !Empty(dDataVir)
		dDataVir := STOD(dDataVir)
		
		//data da virada deve ser sempre no primeiro dia do ano
		If ( DTOS(dDataVir) != "19800101" .And. Year(dDataBase) != Year(dDataVir) ) .Or.  dDataBase < dDataVir
			Aviso(STR0017,STR0018+;  //"Atencao"##"O calculo de depreciacao acelerada somente pode ser efetuado aps a  virada anual no exercicio vigente. "
							STR0019+CRLF+;  //"No Ativo, a virada ocorre aps o clculo de 31 de Dezembro. "
							STR0020+ cFilAnt+" "+CRLF+;  //"Filial : "
							STR0021+DtoC(dDataVir)+Space(10)+;   //"Ultima Virada : "
							STR0022+DtoC(dUltDepr), {"Ok"})  //"Ultimo Calculo Depreciacao : "
			lRet := .F.		// Invalidar o processo.
		EndIf
	Else
		Aviso(STR0017,STR0023+;  //"Atencao"##"No encontrada a data da virada anual e o parametro MV_ATFRSLD esta ativo. Verifique! "
						STR0019+Space(50)+CRLF+;  //"No Ativo, a virada ocorre aps o clculo de 31 de Dezembro. "
						STR0020+ cFilAnt+" ", {"Ok"})  //"Filial : "
		lRet := .F.		// Invalidar o processo.
	EndIf
EndIf
  		
Return(lRet)
