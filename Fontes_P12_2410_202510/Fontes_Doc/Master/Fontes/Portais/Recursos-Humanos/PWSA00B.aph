<%
Local aMenu 	:= f_VldMenu(HttpSession->cMVGPEGFP)
Local aMnuUsr 	:= {}
Local lPula 	:= .F.
Local nI 		:= 0
Local nJ 		:= 0
Local nTamI 	:= 0
Local nY 		:= 0
Local nTamY 	:= 0
Local nPosUsr 	:= 0
Local cFilMenu	:= ""

nTamI := Len(aMenu)

If ExistBlock( "APDMENU" ) 
	aMnuUsr := ExecBlock( "APDMENU", .F., .F. ) 
	If Len(aMnuUsr) > 0 .And. !Empty(aMnuUsr[1][1])
		cFilMenu := cFilAnt
	EndIf

	/*
	// Exemplo de Ponto de Entrada para inibir itens de menu por usuário portal
	User Function ApdMenu()
	Local aMnuUsr := {}

		// Estrutura de cada linha do array
		// 1o Elemento = Filial (Branco pra todas)
		// 2o Elemento = Código do Usuário Genérico do Portal
		// 3o Elemento = Lista com códigos dos itens do menu
		Aadd(aMnuUsr, {"D MG 01 ", "000008", {"000091"}}) 
		Aadd(aMnuUsr, {"D MG 01 ", "000012", {"000092", "000105"}})
		Aadd(aMnuUsr, {"M SP 01 ", "000013", {"000093", "000094", "000095" }})

	Return aMnuUsr
	*/
EndIf 
%>

<html>
<head>
<meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1">
<link href="styles/estilo.css" rel="stylesheet" type="text/css">
<script language="JavaScript">
function DinMenu( x )
{
	if ( document.getElementById( x ).style.display == "none" )
		document.getElementById( x ).style.display = '';
	else
		document.getElementById( x ).style.display = "none";

}
</script>
</head>

<body>

<ul id="primary-nav">
	<% For nI := 1 To nTamI 
		 If Len(aMnuUsr) > 0 
		 	If ( nPosUsr := Ascan(aMnuUsr, {|x| Alltrim(x[1]) = Alltrim(cFilMenu) .And. x[2] == httpsession->usr_info[1]:cusercode}) ) > 0
				lPula := .F.
				For nJ := 1 To Len(aMnuUsr[nPosUsr][3])
					If aMnuUsr[nPosUsr][3][nJ] == aMenu[nI][1]:cCODE
						lPula := .T.
						Exit
					EndIf
				Next nJ
				If lPula
					Loop
				EndIf
		 	EndIf 
		EndIf
		
		If !Empty( aMenu[nI][1]:cPROCEDURECALL ) %>
		<li>
			<a href="<%=aMenu[nI][1]:CPROCEDURECALL%>" target="principal" class="link_menu">
				<img src="imagens-rh/transparente.gif" alt="imagem flecha" id='imgflecha<%=AllTrim(str(nI))%>'>&nbsp;<%=aMenu[nI][1]:CDESCRIPTION%>
			</a>
		</li>
		<% Else %>
		<li>
			<a href="javascript:DinMenu('S<%=aMenu[nI][1]:CCODE%>');" class="link_menu">
				<img src="imagens-rh/transparente.gif" alt="imagem flecha" id='imgflecha<%=AllTrim(str(nI))%>'>&nbsp;<%=aMenu[nI][1]:CDESCRIPTION%>
			</a>
			<ul id="S<%=aMenu[nI][1]:CCODE%>" style="display:none">

				<% nTamY := Len( aMenu[nI][2] ) 
				For nY := 1 To nTamY

					If Len(aMnuUsr) > 0 
						If ( nPosUsr := Ascan(aMnuUsr, {|x| Alltrim(x[1]) = Alltrim(cFilMenu) .And. x[2] == httpsession->usr_info[1]:cusercode}) ) > 0
							lPula := .F.
							For nJ := 1 To Len(aMnuUsr[nPosUsr][3])
								If aMnuUsr[nPosUsr][3][nJ] == aMenu[nI][2][nY]:cCODE
									lPula := .T.
									Exit
								EndIf
							Next nJ
							If lPula
								Loop
							EndIf
						EndIf 
					EndIf %>

					<li class="menuparent-item">
						<a href="<%=aMenu[nI][2][nY]:CPROCEDURECALL%>" class="sub_link_menu" target="principal">
							<img src="imagens-rh/transparente.gif" alt="imagem flecha" id='imgflecha<%=AllTrim(str(nI))%>'>
							<%=aMenu[nI][2][nY]:CDESCRIPTION%>
						</a>
					</li>
				<% Next nY %>
			</ul>
		</li>
		<% EndIf %>	
	<% Next nI %>
</ul>
</body>
</html>
