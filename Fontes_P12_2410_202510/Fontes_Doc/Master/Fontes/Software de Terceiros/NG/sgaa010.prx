#INCLUDE "SGAA010.ch"
#include "Protheus.ch"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ SGAA010  ³ Autor ³ Thiago Olis Machado   ³ Data ³11/08/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa para cadastrar legislacao em vigor.                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³TA0 - Legislacao                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SGAA010()
Local aNGBEGINPRM := NGBEGINPRM()
Local aCores := {}

If Amiin(56,35) //Verifica se o usuário possui licença para acessar a rotina.


Private lAleat := .T.
Private aRotina := MenuDef( .F. )
Private lVisual := .T.
Private nTotal1 := 0
Private cCadastro := OemtoAnsi(STR0036) //"Requisitos Legais e Outros"
Private aAvaliacao := {}
Private cCalend  := ""
Private lCheckTJE := AliasIndic("TJE")

If Len(aSMenu) == 0
	aSMenu := { {STR0008, "SG010SITE()" }} //"Ir ao Site Relacionado"
Endif

aCores  := {{"Sg010Chk()"	,"BR_VERDE"   },;
		 		{"!Sg010Chk()"	,"BR_VERMELHO"}}

dbSelectArea("TA0")
dbSetOrder(1)
mBrowse( 6, 1,22,75,"TA0",,,,,,aCores)

EndIf

NGRETURNPRM(aNGBEGINPRM)
Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SG010SITE ³ Autor ³ Thiago Olis Machado   ³ Data ³11/08/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para chamar o site relacionado ao cadastro.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³TA0 - Legislacao                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SG010SITE()
Local cSite := AllTrim(M->TA0_SITE)
If At("http://",cSite) == 0 .and. At("https://",cSite) == 0
	cSite := "http://"+cSite
Endif
ShellExecute("open",cSite,"","",5)

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ SG010CA  ³ Autor ³ Thiago Olis Machado   ³ Data ³11/08/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para montar uma tela para cadastro.                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³TA0 - Legislacao                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg010Ca(cAlias, nReg, nOpcX)
Local x, i, zx
Local aNGBEGINPRM := If(!IsInCallStack("SGAA010"),NGBEGINPRM(,"SGAA010",,.f.),{})

//Variaveis da tela
Local nOpca := 0
Local oDlg010, oMenu, oEnc, oFolder
Local oPanel, oPanelTA0, oPanelObr, oPanelAca, oPanelTCL, oPanelMAca
Local oPnlLegObr, oPnlLegAca, oPnlLegTCL, oPnlLegTCM, oPanelRA
Local aPages:= {}, aTitles:= {}//Variaveis do folder
Local oFontB := TFont():New("Arial",,-12,,.T.)
Local aButtons := { {"PARAMETROS",{||Sg010Qdo()},STR0010 }} //"Relac. Doc."

//Variaveis de tamanho de tela
Local aSize := MsAdvSize(,.f.,430)
Local nHeightObr := If((Alltrim(GetTheme()) == "FLAT") .Or. SetMdiChild(), 60, 48)
Local nHeightAca := 25

//Variaveis da NewGetDados de TJE - Treinamentos
Local aNoFieldRA := {}
Local cKey, cWhileRA, cAliasTJE
Local oGetRA
Local aHeadRA:= {}, aColsRA	  := {}
//Indicacao de integracao com o treinamento
Local lTreinamento := If(SuperGetMv("MV_NGMDTTR",.F.,"2") == "1",.T.,.F.)

//Variaveis da getdados
Local cGetTCL, cGetTCM
Private oGetPla, oGetRes
Private aHeadPla := {}, aColsPla := {}
Private aHeadRes := {}, aColsRes := {}
//Variaveis dos Memos
Private oMemoObr, oMemoAcoes
Private cMemoObr := "", cMemoAcoes := ""

//Variaveis da Enchoice
Private aGets := {}, aTela := {}
Private aAlt := {}, lAlterou := .f.

Inclui := (nOpcx == 3)
Altera := (nOpcx == 4)

//Cria Folders
Aadd(aTitles,OemToAnsi(STR0038))//"Requisito"
Aadd(aPages,"Header 1")
Aadd(aTitles,OemToAnsi(STR0039))//"Atendimento"
Aadd(aPages,"Header 2")
Aadd(aTitles,OemToAnsi(STR0040))//"Controles"
Aadd(aPages,"Header 3")
Aadd(aTitles,OemToAnsi(STR0041))//"Responsáveis"
Aadd(aPages,"Header 4")
IF	lCheckTJE .And. nModulo == 35
	Aadd(aTitles,OemToAnsi(STR0120))//"Treinamentos"
	Aadd(aPages,"Header 5")
Endif

Define MsDialog oDlg010 Title OemToAnsi(cCadastro) From aSize[7],0 To aSize[6],aSize[5] Of oMainWnd Pixel

	oPanel := TPanel():New(0, 0, Nil, oDlg010, Nil, .T., .F., Nil, Nil, 0, 0, .T., .F. )
	oPanel:Align := CONTROL_ALIGN_ALLCLIENT
	oPanel:CoorsUpdate()

	oFolder := TFolder():New(7.5,0,aTitles,aPages,oPanel,,,,.f.,.f.,333,194,)
	oFolder:Align := CONTROL_ALIGN_ALLCLIENT

	If nOpcx <> 3
		cMemoObr   := TA0->TA0_OBRIGA
		cMemoAcoes := TA0->TA0_ACOES
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Folder 01                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oPanelTA0 := TPanel():New(900,900,,oFolder:aDialogs[1],,,,,,200,200,.F.,.F.)
	oPanelTA0:Align := CONTROL_ALIGN_ALLCLIENT

	RegToMemory("TA0",Inclui)
	dbSelectArea("TA0")
	For i := 1 To FCount()
		zx := "M->" + FieldName(i)
		aAdd(aAlt, &zx.)
	Next i

	oEnc:= MsMGet():New(cAlias,nReg,nOpcx,,,,,{0,0,89,(oPanel:nWidth/2)},,,,,,oPanelTA0)
	oEnc:oBox:Align:= CONTROL_ALIGN_ALLCLIENT

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Folder 02                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oPnlLegObr := TPanel():New(900,900,,oFolder:aDialogs[2],,,,,RGB(67,70,87),200,200,.F.,.F.)
	oPnlLegObr:Align := CONTROL_ALIGN_TOP
	oPnlLegObr:nHeight := 25

	@ 002,002 SAY STR0042 SIZE 200,20 Font oFontB Color RGB(255,255,255) OF oPnlLegObr PIXEL//"Obrigações"

	oPanelObr := TPanel():New(0,0,,oFolder:aDialogs[2],,,,,,800,800,.F.,.F.)
	oPanelObr:Align := CONTROL_ALIGN_ALLCLIENT
	oPanelObr:CoorsUpdate()

	oMemoObr:= tMultiget():New(005,005,{|u| If(Pcount()>0,cMemoObr:=u,cMemoObr)},oPanelObr,((oPanel:nWidth/2)-13),((oPanel:nHeight/2)-nHeightObr),,.T.,,,,.T.,,,,,,If(nOpcx == 2 .Or. nOpcx == 5,.T.,.F.),,,,,.T.)
	oMemoObr:lWordWrap := .T.

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Folder 03                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oPanelAca := TPanel():New(900,900,,oFolder:aDialogs[3],,,,,,200,200,.F.,.F.)
	oPanelAca:Align := CONTROL_ALIGN_TOP
	oPanelAca:nHeight := (oPanel:nHeight/4)

	oPnlLegAca := TPanel():New(900,900,,oPanelAca,,,,,RGB(67,70,87),200,200,.F.,.F.)
	oPnlLegAca:Align := CONTROL_ALIGN_TOP
	oPnlLegAca:nHeight := 25

	@ 002,002 SAY STR0043 SIZE 200,20 Font oFontB Color RGB(255,255,255) OF oPnlLegAca PIXEL//"Ações"

	oPanelMAca := TPanel():New(0,0,,oPanelAca,,,,,,800,800,.F.,.F.)
	oPanelMAca:nHeight := (oPanel:nHeight/2)
	oPanelMAca:Align := CONTROL_ALIGN_TOP
	oPanelMAca:CoorsUpdate()

	oMemoAcoes:= tMultiget():New(005,005,{|u|if(Pcount()>0,cMemoAcoes:=u,cMemoAcoes)},oPanelMAca,((oPanel:nWidth/2)-13),((oPanelMAca:nHeight/4)-nHeightAca),,.T.,,,,.T.,,,,,,If(nOpcx == 2 .Or. nOpcx == 5,.T.,.F.),,,,,.T.)
	oMemoAcoes:lWordWrap := .T.

	cGetTCL := "TCL->TCL_FILIAL == '"+xFilial("TCL")+"' .AND. TCL->TCL_CODLEG == '"+TA0->TA0_CODLEG+"'"
	FillGetDados( nOpcx, "TCL", 1, "TA0->TA0_CODLEG", {|| }, {|| .T.},{"TCL_CODLEG"},,,,{|| NGMontaAcols("TCL", TA0->TA0_CODLEG,cGetTCL)},,aHeadPla,aColsPla)

	If Empty(aColsPla) .Or. nOpcx == 3
		aColsPla := BlankGetd(aHeadPla)
	EndIf

	oPanelTCL := TPanel():New(900,900,,oFolder:aDialogs[3],,,,,,200,200,.F.,.F.)
	oPanelTCL:Align := CONTROL_ALIGN_ALLCLIENT

	oPnlLegTCL := TPanel():New(900,900,,oPanelTCL,,,,,RGB(67,70,87),200,200,.F.,.F.)
	oPnlLegTCL:Align := CONTROL_ALIGN_TOP
	oPnlLegTCL:nHeight := 25

	@ 002,002 SAY STR0044 SIZE 200, 20 Font oFontB Color RGB(255,255,255) OF oPnlLegTCL PIXEL//"Planos de Ações do Requisito"
	PutFileInEof("TCL")
	oGetPla := MsNewGetDados():New(0,0,600,600,IIF(!Inclui.And.!Altera,0,GD_INSERT+GD_UPDATE+GD_DELETE),{|| SGA010VLD("TCL",,oGetPla)},"AllwaysTrue()",,,,9999,,,,oPanelTCL,aHeadPla,aColsPla)
	oGetPla:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Folder 04                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cGetTCM := "TCM->TCM_FILIAL == '"+xFilial("TCM")+"' .AND. TCM->TCM_CODLEG == '"+TA0->TA0_CODLEG+"'"
	FillGetDados( nOpcx, "TCM", 1, "TA0->TA0_CODLEG", {|| }, {|| .T.},{"TCM_CODLEG"},,,,{|| NGMontaAcols("TCM", TA0->TA0_CODLEG,cGetTCM)},,aHeadRes,aColsRes)

	If Empty(aColsRes) .Or. nOpcx == 3
		aColsRes := BlankGetd(aHeadRes)
	EndIf

	oPnlLegTCM := TPanel():New(900,900,,oFolder:aDialogs[4],,,,,RGB(67,70,87),200,200,.F.,.F.)
	oPnlLegTCM:Align := CONTROL_ALIGN_TOP
	oPnlLegTCM:nHeight := 25

	@ 002,002 SAY STR0045 SIZE 200, 20 Font oFontB Color RGB(255,255,255) OF oPnlLegTCM Pixel//"Responsáveis pelo Requisito"
	PutFileInEof("TCM")
	oGetRes := MsNewGetDados():New(0,0,800,800,IIF(!Inclui.And.!Altera,0,GD_INSERT+GD_UPDATE+GD_DELETE),{|| SGA010VLD("TCM",,oGetRes)},"AllwaysTrue()",,,,9999,,,,oFolder:aDialogs[4],aHeadRes,aColsRes)
	oGetRes:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Folder 05                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lCheckTJE .And. nModulo == 35
		If !lTreinamento //Verifica integracao com SIGATRM
			oFolder:aDialogs[5]:Disable()
		EndIf
		//Carrega variaveis do FillGetDados
		cAliasTJE	:= "TJE"
		cWhileRA	:= "TJE->TJE_FILIAL == xFilial('TJE') .AND. TA0->TA0_CODLEG == TJE->TJE_CODLEG"
		cKey 		:= "M->TA0_CODLEG"
		aNoFieldRA	:= {"TJE_FILIAL","TJE_CODLEG"}

		oPanelRA := TPanel():New(900,900,,oFolder:aDialogs[5],,,,,RGB(67,70,87),200,200,.F.,.F.)
		oPanelRA:Align := CONTROL_ALIGN_ALLCLIENT

		    FillGetDados( nOpcx, cAliasTJE, 1, cKey,/*bSeekWhile*/,{|| .T.},aNoFieldRA,/*aYesFields*/,/*lOnlyYes*/,/*cQuery*/,;
		    			{|| NGMontaAcols(cAliasTJE, &cKey,cWhileRA)},/*lEmpty*/,aHeadRA, aColsRA)

			PutFileInEof("TJE")
		    oGetRA := MsNewGetDados():New(0,0,800,800,IIF(!Inclui.And.!Altera,0,GD_INSERT+GD_UPDATE+GD_DELETE),{|| SGA010VLD("TJE",,oGetRA)},;
		    				/*cTudoOk*/,/*cIniCpos*/,/*aAlter*/,/*nFreeze*/,9999,/*cFieldOk*/,/*cSuperDel*/,/*cDelOk*/,oPanelRA,aHeadRA,aColsRA,{||fChange(oGetRA)})
		    oGetRA:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

		If Empty(aColsRA) .Or. nOpcx == 3
			aColsRA := BlankGetd(aHeadRA)
		EndIf
	EndIf

	If Len(aSMenu) > 0
		NGPOPUP(aSMenu,@oMenu,oPanel)
		oPanel:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oPanel)}
		oPanelTA0:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oPanelTA0)}
	Endif

ACTIVATE MSDIALOG oDlg010 ON INIT EnchoiceBar(oDlg010,{|| nOpca:=1,If(!fTudoOk(oGetPla,oGetRes,oGetRA),nOpca :=0,oDlg010:End())},{|| nOpca := 0, oDlg010:End()},,aButtons) Centered

If nOpca == 1
	Sg010Grava(nOPCx,cAlias,oGetRA)
Endif

NGRETURNPRM(aNGBEGINPRM)

Return nOPCa

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Sg010Qdo  ³ Autor ³ Thiago Machado        ³ Data ³ 18.05.04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta uma tela para escolha da visualizacao ou Relacionamento±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg010Qdo()
Local oDlgQdo,oRadio
Local nRadio := 1,nOpc := 1
Local lGrava := .F.

Define MsDialog oDlgQdo From 03.5,6 To 150,320 Title STR0016 Pixel  //"Atenção"
Define FONT oBold NAME "Courier New" SIZE 0, -13 BOLD
@ 0, 0 BITMAP oBmp RESNAME "PROJETOAP" oF oDlgQdo SIZE 35,250 NOBORDER WHEN .F. PIXEL

@ 05,040 Say OemToAnsi(STR0017) Size 117,7 Of oDlgQdo Pixel Font oBold  //"O que deseja fazer ?"

@ 20,048 Radio oRadio Var nRadio Items STR0018,STR0019,STR0020 3d Size 105,10 Of oDlgQdo Pixel  //"Relacionar um documento"###"Visualizar documento relacionado"###"Apagar um Documento Relacionamento"

Define sButton From 055,090 Type 1 Enable Of oDlgQdo Action (lGrava := .t.,oDlgQdo:End())
Define sButton From 055,120 Type 2 Enable Of oDlgQdo Action (lGrava := .f.,oDlgQdo:End())

Activate MsDialog oDlgQdo Centered

If lGrava
	If nRadio == 1
		Sg010RelQdo()
	ElseIf nRadio == 2
		Sg010VieQdo()
	Else
		M->TA0_DOCTO  := " "
		M->TA0_DOCFIL := " "
	EndIf
Endif

Return .f.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Sg010RelQdo Autor ³ Thiago Machado        ³ Data ³ 18.05.04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relaciona um procedimento a um documento QDO               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg010RelQdo()
Local lRet := .f.
lRet := ConPad1( , , , "QDT1",,,.f.)
If lRet
	M->TA0_DOCTO  := QDH->QDH_DOCTO
	M->TA0_DOCFIL := QDH->QDH_FILIAL
EndIf
Return .F.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Sg010VieQdo Autor ³ Thiago Machado        ³ Data ³ 18.05.04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Visualiza um documento QDO                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Sg010VieQdo()
Local _lRet := .f.

If !Empty(M->TA0_DOCTO)
	If QDOVIEW( , M->TA0_DOCTO ) //Visualiza documento Word...
		_lRet := .t.
	Endif
Else
	MsgInfo(STR0046) //"Nao existe documento associado a este Requisito"
Endif

Return _lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Sg010Aval ³ Autor ³ Thiago Machado        ³ Data ³ 09.06.04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Inclui no Array as Avaliacoes relacionadas                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg010Aval(aAlt)
Local cMensagem := ""
Local aAspecto := {}, aImpacto := {}
Local x,y,j
Local cUser  := PadR( Upper( SubStr( cUsuario, 7, 15 ) ), 15 )
Local lSituac:= NGCADICBASE("TAB_SITUAC","A","TAB",.F.)//Verifica se existe situacao de desempenho

If !Empty( M->TA0_DOCTO )
	If M->TA0_DOCTO == TA0->TA0_DOCTO
		SgPostEve( "2",dDataBase,cUser,M->TA0_DOCTO )
	EndIf
EndIf

DbSelectArea("TAM")
DbSetOrder(1)
If !DbSeek(xFilial("TAM")+M->TA0_CODLEG+DtoS(dDataBase)+Time())
	RecLock("TAM",.T.)
	DbSelectArea("TA0")
	For j := 1 To Fcount()
		cCampoTA0 := "TAM"+SubStr(FieldName(j),4,10)
		dbSelectArea("SX3")
		dbSetOrder(02)
		If dbSeek(cCampoTA0)
			dbSelectArea("TAM")
			nx := aAlt[j]
			ny := "TAM->" + cCampoTA0
			&ny := nx
		Endif
		dbSelectArea("TA0")
	Next j
	TAM->TAM_FILIAL := xFilial("TAM")
	TAM->TAM_DTALTE := dDataBase
	TAM->TAM_HRALTE := Time()
	TAM->(MsUnLock())
EndIf

DbSelectArea("TAJ")
DbSetOrder(2)
TAJ->(Dbseek(xFilial("TAJ")+M->TA0_CODLEG))
While !Eof() .And. xFilial("TAJ") == TAJ->TAJ_FILIAL .And. TAJ->TAJ_CODLEG == M->TA0_CODLEG
	If (aScan(aAspecto, {|x| x[1] == TAJ->TAJ_CODASP})) == 0
		aAdd( aAspecto,{ TAJ->TAJ_CODASP })
	EndIf

	TAJ->(DbSkip())
End

DbSelectArea("TAI")
DbSetOrder(2)
TAI->(Dbseek(xFilial("TAI")+M->TA0_CODLEG))
While !Eof() .And. xFilial("TAI") == TAI->TAI_FILIAL .And. TAI->TAI_CODLEG == M->TA0_CODLEG

	If (aScan(aImpacto, {|x| x[1] == TAI->TAI_CODIMP})) == 0
		aAdd( aImpacto,{TAI->TAI_CODIMP} )
	EndIf

	TAI->(DbSkip())
End

For x:=1 To Len(aAspecto)
	dbSelectArea("TAB")
	dbSetOrder(4)
	dbSeek(xFilial("TAB")+aAspecto[x][1])
	While !eof() .and. xFilial("TAB")+aAspecto[x][1] == TAB->TAB_FILIAL+TAB->TAB_CODASP
		If lSituac .and. TAB->TAB_SITUAC == "1"

			If (aScan(aAvaliacao, {|x| x[1] == TAB->TAB_ORDEM})) == 0
				aAdd( aAvaliacao,{ TAB->TAB_ORDEM, NGSEEK("TAF","001"+TAB->TAB_CODNIV,2,"TAF->TAF_NOMNIV"), TAB->TAB_CODASP, ;
											NGSEEK("TA4",TAB->TAB_CODASP,1,"TA4->TA4_DESCRI"), TAB->TAB_CODIMP, ;
											NGSEEK("TAE",TAB->TAB_CODIMP,1,"TAE->TAE_DESCRI"), TAB->TAB_CODCLA, TAB->TAB_DTRESU, "S" } )
			EndIf
		Endif
		dbSelectArea("TAB")
		dbSetOrder(4)
		dbSkip()
	End
Next x

For y:=1 To Len(aImpacto)
	dbSelectArea("TAB")
	dbSetOrder(5)
	dbSeek(xFilial("TAB")+aImpacto[y][1])
	While !eof() .and. xFilial("TAB")+aImpacto[y][1] == TAB->TAB_FILIAL+TAB->TAB_CODIMP
		If lSituac .and. TAB->TAB_SITUAC == "1"

			If (aScan(aAvaliacao, {|x| x[1] == TAB->TAB_ORDEM})) == 0
				aAdd( aAvaliacao,{ TAB->TAB_ORDEM, NGSEEK("TAF","001"+TAB->TAB_CODNIV,2,"TAF->TAF_NOMNIV"), TAB->TAB_CODASP, ;
											NGSEEK("TA4",TAB->TAB_CODASP,1,"TA4->TA4_DESCRI"), TAB->TAB_CODIMP, ;
											NGSEEK("TAE",TAB->TAB_CODIMP,1,"TAE->TAE_DESCRI"), TAB->TAB_CODCLA, TAB->TAB_DTRESU, "S" } )
			EndIf
		Endif
		dbSelectArea("TAB")
		dbSetOrder(5)
		dbSkip()
	End
Next y

If Len(aAvaliacao) > 0
	If Len(aAvaliacao) == 1
		cMensagem := STR0074+cValToChar(Len(aAvaliacao))+STR0075+CHR(10)+CHR(13)+STR0076 //"Existe "###" Avaliação a ser avaliada."###"Deseja visualizá-la?"
	Else
		cMensagem := STR0022+cValToChar(Len(aAvaliacao))+STR0023+CHR(13)+CHR(13)+STR0024//"Existem "###" Avaliações a serem Reavaliadas."###"Deseja visualizá-las?"
	Endif
	If MsgYesNo(cMensagem,STR0016) //###"Atenção"
		MsgRun(OemToAnsi(STR0025),OemToAnsi(STR0026),{|| Sg010ReAva( aAvaliacao )})  //"Selecionando Avaliações a serem Reavaliadas..."###"Aguarde"
	EndIf
EndIf

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Sg010ReAva³ Autor ³ Thiago Machado        ³ Data ³ 09.06.04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta tela para mostrar as Avaliacoes.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Sg010ReAva( aAvaliacao )
Local oBtn1, oBtn2, oBtn3
Local cDemanda := M->TA0_CODLEG
Private oDlgAva, oAspecto, oImpacto, cAspecto := " ",cImpacto := " ",lFirst := .t.

Define MsDialog oDlgAva From 000,000 To 305,625 Title STR0016 Pixel//"Atenção"
Define FONT oBold NAME "Courier New" SIZE 0, -13 BOLD

@ 05,040 Say OemToAnsi(STR0047) Size 187,7 Of oDlgAva Pixel Font oBold //"Avaliações relacionadas com o Requisito "
@ 05,200 Say OemToAnsi(cDemanda) Color CLR_BLUE Pixel Font oBold

DEFINE SBUTTON oBtn1 FROM 003,282 TYPE 11 ENABLE OF oDlgAva Action Sg010_Reavalia(.t.)
oBtn1:cCaption := STR0032//"Reavalia"
oBtn1:cToolTip := STR0032//"Reavalia"

DEFINE SBUTTON oBtn2 FROM 016,282 TYPE 03 ENABLE OF oDlgAva Action Sg010_Reavalia(.f.)
oBtn2:cCaption := STR0028//"Não Afeta"
oBtn2:cToolTip := STR0028//"Não Afeta"

DEFINE SBUTTON oBtn3 FROM 029,282 TYPE 02 ENABLE OF oDlgAva Action oDlgAva:End()

@ 012,006 ListBox oList;
			 Fields Header;
			 STR0029,STR0030,STR0031; //"Ordem"###"Identificação"###"Data Resultado"
			 ColSizes 50,130,50;
			 On Change ( Sg010Change(aAvaliacao));
			 Size 270,130 Of oDlgAva Pixel;
		 	 On DblClick( Sg010_Reavalia( .t. ) )

bList := { || 	If(oList:nAt > Len( aAvaliacao ) .Or. oList:nAt = 0 ,;
					{ " "," "," "," "," "," "," "," " },;
					{ aAvaliacao[oList:nAt,1], aAvaliacao[oList:nAt,2], aAvaliacao[oList:nAt,8] }) }

oList:SetArray(aAvaliacao)
oList:bLine:= bList
oList:GoTop()

@ 143,007 Say OemToAnsi(STR0048) Of oDlgAva Size 45,15 Pixel Color CLR_BLUE//"Aspecto"
@ 143,037 Say oAspecto Var cAspecto Size 148, 08 Of oDlgAva Pixel

@ 143,190 Say OemToAnsi(STR0049) Of oDlgAva Size 45,15 Pixel Color CLR_BLUE//"Impacto"
@ 143,217 Say oImpacto Var cImpacto Size 148, 08 Of oDlgAva Pixel

Activate MsDialog oDlgAva Centered

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Sg010_Reavalia³ Autor ³ Thiago Machado        ³ Data ³ 11.06.04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Mostra a tela de Avaliacao selecionada                    	   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                  	      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg010_Reavalia(lChama)

	If !lChama
		nOpcao := 1
	EndIf

	CursorWait()
	DbSelectArea( "TAB" )
	DbSetOrder( 1 )
	Dbseek( xFilial( "TAB" )+aAvaliacao[oList:nAt,1] )

	If lChama
		nOpcao := Sg110Pro( Alias(), Recno(),4 )
	EndIf
	If nOpcao == 1
		aDel( aAvaliacao, oList:nAt )
		aSize( aAvaliacao, Len( aAvaliacao )-1 )
	EndIf

	oList:Refresh()
	CursorArrow()

	If Len( aAvaliacao ) <= 0 // Fecha a tela caso não tenha mais avaliações!
		oDlgAva:End()
	EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Sg010Change³ Autor ³ Thiago Machado        ³ Data ³ 11.06.04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Mostra label do Aspecto e Impacto.                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Sg010Change(aAvaliacao)

cAspecto := aAvaliacao[oList:nAt,4]
cImpacto := aAvaliacao[oList:nAt,6]
If lFirst
	lFirst := .f.
Else
  oAspecto:Refresh()
  oImpacto:Refresh()
EndIf
Return .t.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fTudoOk   ºAutor  ³Roger Rodrigues     º Data ³  19/12/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Realiza Verificacoes da rotina                              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SGAA010                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fTudoOk(oObjTCL,oObjTCM,oObjTJE)

If !Obrigatorio(aGets,aTela)
	Return .F.
EndIf
If M->TA0_DTVENC < M->TA0_DTVIGE .And. !Empty(M->TA0_DTVENC)
	Help(" ",1,STR0033,,STR0034,3,1) //"ATENÇÃO"###"Campo Data de Vencimento deve ser maior que a Data de Vigência"
	Return .F.
EndIf

If !SGA010Vld("TCL",.T.,oObjTCL)
	Return .F.
EndIf
If !SGA010Vld("TCM",.T.,oObjTCM)
	Return .F.
EndIf
If nModulo == 35
If lCheckTJE .AND. !SGA010Vld("TJE",.T.,oObjTJE)
	Return .F.
EndIf
EndIf
Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Sg010Grava ³ Autor ³ Thiago Machado        ³ Data ³ 11.06.04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Mostra label do Aspecto e Impacto.                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Sg010Grava(nOpcx,cAlias,oObjTJE)
Local aMemos :=	{	{"TCK_MMOBS"	, "TCK_OBSERV"	},;
							{"TCK_MMNOT"	, "TCK_NOTAS"	},;
							{"TCK_MMNCON"	, "TCK_NAOCON"	}}
Local i,k,nPos,ny,nx,zy,nInd
Local aColsPla	:= oGetPla:aCols
Local aHeadPla	:= oGetPla:aHeader
Local aColsRes	:= oGetRes:aCols
Local aHeadRes	:= oGetRes:aHeader
Local aColsOK
Local aHeadOK
Local nCodPla	:= aScan( aHeadPla,	{ |x| Trim( Upper(x[2]) ) == "TCL_CODPLA"})
Local nCodRes	:= aScan( aHeadRes,	{ |x| Trim( Upper(x[2]) ) == "TCM_RESPON"})
Local nCalend
Local nCurso
Local nTurma

If lCheckTJE .And. nModulo == 35
	aColsOK	:= aClone(oObjTJE:aCols)
	aHeadOK	:= aClone(oObjTJE:aHeader)
	nCalend	:= aScan( aHeadOK,	{ |x| Trim( Upper(x[2]) ) == "TJE_CALEND"})
	nCurso	:= aScan( aHeadOK,	{ |x| Trim( Upper(x[2]) ) == "TJE_CURSO"})
	nTurma	:= aScan( aHeadOK,	{ |x| Trim( Upper(x[2]) ) == "TJE_TURMA"})
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se Confirmou e a opcao incluir ou alterar       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOPCx == 3 .or. nOPCx == 4
	dbSelectArea(cALIAS)
	RecLock(cAlias,Inclui)
	For ny := 1 To FCOUNT()
		nx := "m->" + FieldName(ny)
		If "_FILIAL"$Upper(nx)
			&nx. := xFilial(cALIAS)
		Endif
		FieldPut(ny, &nx.)
	Next ny
	TA0->TA0_OBRIGA := cMemoObr
	TA0->TA0_ACOES  := cMemoAcoes
	&nx. := ""
	MsUnLock(cAlias)

	//Deleta Planos de Acao Relacionados
	dbSelectArea("TCL")
	dbSetorder(1)
	dbSeek(xFilial("TCL")+TA0->TA0_CODLEG)
	While !Eof() .And. TCL->TCL_FILIAL == xFilial("TCL") .And. TCL->TCL_CODLEG == TA0->TA0_CODLEG
		RecLock("TCL",.F.)
		dbDelete()
		MsUnlock("TCL")

		dbSelectArea("TCL")
		dbSkip()
	End
	//Coloca os deletados por primeiro
	ASORT(aColsPla,,, { |x, y| x[Len(aColsPla[1])] .and. !y[Len(aColsPla[1])] } )
	For i:=1 To Len(aColsPla)
		If !aColsPla[i][Len(aColsPla[i])] .and. !Empty(aColsPla[i][nCodPla])
			dbSelectArea("TCL")
			dbSetorder(1)
			If !dbSeek(xFilial("TCL")+TA0->TA0_CODLEG+aColsPla[i][nCodPla])
				RecLock("TCL",.T.)
				For k:=1 to FCount()
					If "_FILIAL"$Upper(FieldName(k))
						FieldPut(k, xFilial("TCL"))
					ElseIf "_CODLEG"$Upper(FieldName(k))
						FieldPut(k, TA0->TA0_CODLEG)
					ElseIf "_CODPLA"$Upper(FieldName(k))
						FieldPut(k, aColsPla[i][nCodPla])
					Else
						If (nPos := aScan(aHeadPla, {|x| AllTrim(Upper(x[2])) == AllTrim(Upper(FieldName(j))) })) > 0
							FieldPut(k, aColsPla[i][nPos])
						Endif
					Endif
				Next k
				MsUnlock("TCL")
			EndIf
		ElseIf !Empty(aColsPla[i][nCodPla])
			dbSelectArea("TCL")
			dbSetorder(1)
			If dbSeek(xFilial("TCL")+TA0->TA0_CODLEG+aColsPla[i][nCodPla])
				RecLock("TCL",.F.)
				dbDelete()
				MsUnlock("TCL")
			EndIf
		EndIf
	Next k

	//Deleta Responsaveis Relacionados
	dbSelectArea("TCM")
	dbSetorder(1)
	dbSeek(xFilial("TCM")+TA0->TA0_CODLEG)
	While !Eof() .And. TCM->TCM_FILIAL == xFilial("TCM") .And. TCM->TCM_CODLEG == TA0->TA0_CODLEG
		RecLock("TCM",.F.)
		dbDelete()
		MsUnlock("TCM")

		dbSelectArea("TCM")
		dbSkip()
	End
	//Coloca os deletados por primeiro
	ASORT(aColsRes,,, { |x, y| x[Len(aColsRes[1])] .and. !y[Len(aColsRes[1])] } )
	For i:=1 To Len(aColsRes)
		If !aColsRes[i][Len(aColsRes[i])] .and. !Empty(aColsRes[i][nCodRes])
			dbSelectArea("TCM")
			dbSetorder(1)
			If !dbSeek(xFilial("TCM")+TA0->TA0_CODLEG+aColsRes[i][nCodRes])
				RecLock("TCM",.T.)
				For k:=1 to FCount()
					If "_FILIAL"$Upper(FieldName(k))
						FieldPut(k, xFilial("TCM"))
					ElseIf "_CODLEG"$Upper(FieldName(k))
						FieldPut(k, TA0->TA0_CODLEG)
					ElseIf "_RESPON"$Upper(FieldName(k))
						FieldPut(k, aColsRes[i][nCodRes])
					Else
						If (nPos := aScan(aHeadRes, {|x| AllTrim(Upper(x[2])) == AllTrim(Upper(FieldName(j))) })) > 0
							FieldPut(k, aColsRes[i][nPos])
						Endif
					Endif
				Next k
				MsUnlock("TCM")
			EndIf
		ElseIf !Empty(aColsRes[i][nCodRes])
			dbSelectArea("TCM")
			dbSetorder(1)
			If dbSeek(xFilial("TCM")+TA0->TA0_CODLEG+aColsRes[i][nCodRes])
				RecLock("TCM",.F.)
				dbDelete()
				MsUnlock("TCM")
			EndIf
		EndIf
	Next k

	If lCheckTJE .And. nModulo == 35
	    //Treinamento
		//Coloca os deletados por primeiro
		ASORT(aColsOK,,, { |x, y| x[Len(aColsOK[1])] .and. !y[Len(aColsOK[1])] } )
		For i:=1 To Len(aColsOK)
			If !aTail(aColsOK[i]) .and. !Empty(aColsOK[i][nCalend])
				dbSelectArea("TJE")
				dbSetorder(1)
				If !dbSeek(xFilial("TJE")+TA0->TA0_CODLEG+aColsOK[i][nCalend]+aColsOK[i][nCurso]+aColsOK[i][nTurma])
					RecLock("TJE",.T.)
					For k:=1 to FCount()
						If "_FILIAL"$Upper(FieldName(k))
							FieldPut(k, xFilial("TJE"))
						ElseIf "_CODLEG"$Upper(FieldName(k))
							FieldPut(k, TA0->TA0_CODLEG)
						ElseIf "_CALEND"$Upper(FieldName(k))
							FieldPut(k, aColsOK[i][nCalend])
						ElseIf "_CURSO"$Upper(FieldName(k))
							FieldPut(k, aColsOK[i][nCurso])
						ElseIf "_TURMA"$Upper(FieldName(k))
							FieldPut(k, aColsOK[i][nTurma])
						Else
							If (nPos := aScan(aHeadOK, {|x| AllTrim(Upper(x[2])) == AllTrim(Upper(FieldName(j))) })) > 0
								FieldPut(k, aColsOK[i][nPos])
							Endif
						Endif
					Next k
					MsUnlock("TJE")
				EndIf
			ElseIf aTail(aColsOK[i]) .AND. !Empty(aColsOK[i][nCalend])
				dbSelectArea("TJE")
				dbSetorder(1)
				If dbSeek(xFilial("TJE")+TA0->TA0_CODLEG+aColsOK[i][nCalend]+aColsOK[i][nCurso]+aColsOK[i][nTurma])
					RecLock("TJE",.F.)
					dbDelete()
					MsUnlock("TJE")
				EndIf
			EndIf
		Next i
	EndIf
	If nOpcx == 3 .Or. nOpcx == 4
		SGAA010WF(nOpcx)//Envia Workflow para responsaveis
	EndIf

	//Se for alteracao
	DbSelectArea(cALIAS)
	If nOpcx == 4
		//Verifica se o registro foi alterado de fato
		For zy := 1 To Len(aAlt)
			nx := "M->" + FieldName(zy)
			If &nx. <> aAlt[zy]
				lAlterou := .t.
			EndIf
		Next zy
		If lAlterou
			MsgRun(OemToAnsi(STR0014),OemToAnsi(STR0015),{|| Sg010Aval(aAlt)})  //"Carregando Avaliações"###"Aguarde..."
		EndIf
	Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se Confirmou                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOpcx  <> 2
	//Ponto de entrada para gravacao de campos adicionais ou consistencias
	If ExistBlock("SGAA010G")
		ExecBlock("SGAA010G",.F.,.F.)
	Endif
	//Gravacao de historico de legislacao
	If AliasIndic( "TE8" )
		SGAA010HIS()
	EndIf
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se Confirmou e a opcao excluir                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOPCx == 5
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a Integridade Referencial                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If NGVALSX9("TA0",{"TCL","TCK","TCM","TAQ","TAM","TJE"},.T.)
		//Deleta Eventos Relacionados
		dbSelectArea("TAQ")
		dbSetorder(3)
		dbSeek(xFilial("TAQ")+"4"+TA0->TA0_CODLEG)
		While !eof() .and. xFilial("TAQ")+"4"+TA0->TA0_CODLEG == TAQ->TAQ_FILIAL+TAQ->TAQ_TIPO+TAQ->TAQ_ORDEM
			RecLock("TAQ",.F.)
			dbDelete()
			MsUnlock("TAQ")
			dbSelectArea("TAQ")
			dbSkip()
		End

		If TCK->(FieldPos("TCK_CODLEG")) > 0
			//Deleta Protocolos Relacionados
			dbSelectArea("TCK")
			dbSetorder(1)
			dbSeek(xFilial("TCK")+TA0->TA0_CODLEG)
			While !Eof() .And. TCK->TCK_FILIAL == xFilial("TCK") .And. TCK->TCK_CODLEG == TA0->TA0_CODLEG
				RecLock("TCK",.F.)
				For nInd := 1 To Len(aMemos)
					MSMM(&("TCK->"+aMemos[nInd,1]),,,,2)
				Next nInd
				TCK->(dbDelete())
				MsUnlock("TCK")

				dbSelectArea("TCK")
				dbSkip()
			End
			//Deleta Planos de Acao Relacionados
			dbSelectArea("TCL")
			dbSetorder(1)
			dbSeek(xFilial("TCL")+TA0->TA0_CODLEG)
			While !Eof() .And. TCL->TCL_FILIAL == xFilial("TCL") .And. TCL->TCL_CODLEG == TA0->TA0_CODLEG
				RecLock("TCL",.F.)
				dbDelete()
				MsUnlock("TCL")

				dbSelectArea("TCL")
				dbSkip()
			End
			//Deleta Responsaveis Relacionados
			dbSelectArea("TCM")
			dbSetorder(1)
			dbSeek(xFilial("TCM")+TA0->TA0_CODLEG)
			While !Eof() .And. TCM->TCM_FILIAL == xFilial("TCM") .And. TCM->TCM_CODLEG == TA0->TA0_CODLEG
				RecLock("TCM",.F.)
				dbDelete()
				MsUnlock("TCM")

				dbSelectArea("TCM")
				dbSkip()
			End
			//Deleta Treinamentos Relacionados
			If lCheckTJE .And. nModulo == 35
				dbSelectArea("TJE")
				dbSetorder(1)
				dbSeek(xFilial("TJE")+TA0->TA0_CODLEG)
				While !Eof() .And. TJE->TJE_FILIAL == xFilial("TJE") .And. TJE->TJE_CODLEG == TA0->TA0_CODLEG
					RecLock("TJE",.F.)
					dbDelete()
					MsUnlock("TJE")

					dbSelectArea("TJE")
					dbSkip()
				End
			EndIf
		EndIf

		dbSelectArea(cAlias)
		RecLock(cAlias,.F.)
		DbDelete()
		MsUnlock(cAlias)
		EvalTrigger()// Processa Gatilhos
	EndIf
Endif
lRefresh := .t.

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ Rafael Diogo Richter  ³ Data ³11/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³    1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef( lMdi )
Local aRotina := 	{}
Default lMdi := .T.

aRotina := 	{	{ STR0001 , "AxPesqui"  , 0 , 1},;//"Pesquisar"
					{ STR0002 , "Sg010Ca"   , 0 , 2},;//"Visualizar"
					{ STR0003 , "Sg010Ca"   , 0 , 3},;//"Incluir"
					{ STR0004 , "Sg010Ca"   , 0 , 4},;//"Alterar"
					{ STR0005 , "Sg010Ca"   , 0 , 5, 3},;//"Excluir"
					{ STR0006 , "MsDocument"	, 0 , 6},;//"Conhecimento"
					{ STR0051 , "SG010Leg"   , 0 , 7}}//"Legenda"


If !lMdi .And. TCK->(FieldPos("TCK_CODLEG")) > 0
	AAdd( aRotina,{ STR0035 , "SG010Pro"   , 0 , 4} )//"Protocolos"
EndIf

Return aRotina

//---------------------------------------------------------------------
/*/{Protheus.doc} Sg010Pro
Programa para cadastro de Protocolos

@return lOk, Boolean, Retorna verdadeiro caso a tela tenha sido confirmada

@sample Sg010Pro( "TAF", 1, 3 )

@param cAlias, Caracter, Alias da tabela
@param nReg, Numérico, Número do registro
@param nOpcx, Numérico, Número da operação que está sendo realizada

@author Rafael Diogo Richter
@since 23/07/2008
/*/
//---------------------------------------------------------------------
Function Sg010Pro( cAlias, nReg, nOpcx )

	Local oDlgPro
	Local lOk := .F.

	//Variaveis de tamanho de tela e objetos
	Local aSize		:= {}
	Local aObjects	:= {}
	Local aInfo		:= {}
	Local aPosObj	:= {}

	//Variaveis da GetDados
	Local cGetTCK
	Local aColsTCK := {}
	Local aHeadTCK := {}

	//Variável de botões adicionais na Enchoice
	Local aButtons := {}

	Private oGetTCK
	Private nTCK

	//Executa PE para adição de novos botões na Enchoice
	If ExistBlock( "SGAA0101" )
		aButtons := ExecBlock( "SGAA0101", .F., .F. )
	EndIf

	//Define os tamanhos
	aSize := MsAdvSize( , .F., 430 )
	Aadd( aObjects, { 500, 500, .T., .T. } )
	aInfo := { aSize[1], aSize[2], aSize[3], aSize[4], 0, 0 }
	aPosObj := MsObjSize( aInfo, aObjects, .T. )

	Define MsDialog oDlgPro Title STR0052 From aSize[7], 0 To aSize[6], aSize[5] Of oMainWnd PIXEL //"Controle de Protocolos"

	cGetTCK := "TCK->TCK_FILIAL == '" + xFilial( "TCK" ) + "' .AND. TCK->TCK_CODLEG == '" + TA0->TA0_CODLEG + "'"
	FillGetDados( nOpcx, "TCK", 1, "TA0->TA0_CODLEG", {|| }, {|| .T.}, {"TCK_CODLEG"}, , , , {|| NGMontaAcols( "TCK", TA0->TA0_CODLEG, cGetTCK ) }, , aHeadTCK, aColsTCK )

	nTCK := Len( aColsTCK )
	If Empty( aColsTCK ) .Or. nOpcx == 3
		aColsTCK := BlankGetd( aHeadTCK )
	EndIf

	n := Len( aColsTCK )
	oGetTCK := MsNewGetDados():New( aPosObj[1,2], aPosObj[1,2], aPosObj[1,3], aPosObj[1,4], ( GD_INSERT + GD_UPDATE + GD_DELETE ), { || SG010VLIN() }, "AllWaysTrue()", , , , 9999, , , , oDlgPro, aHeadTCK, aColsTCK )
	oGetTCK:oBrowse:Default()
	oGetTCK:oBrowse:Refresh()
	oGetTCK:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

	Activate Dialog oDlgPro On Init ( EnchoiceBar( oDlgPro, {|| lOk := .T., IIf( SG010VLIN(.T.), oDlgPro:End(), lOk := .F. ) }, {|| lOk:= .F., oDlgPro:End()}, , aButtons ) ) Centered

	If lOk
		Sg010GrvPro()
	Endif

Return lOk

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SG010VLIN ³ Autor ³ Rafael Diogo Richter  ³ Data ³23/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida se toda a GetDados esta OK.                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Valida a linha digitada no GetDados.                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function SG010VLIN(lFim)
Local i,f
Local nAt := oGetTCK:nAt
Local aColsTCK := oGetTCK:aCols
Local aHeadTCK := oGetTCK:aHeader
Local nCodPro := GDFIELDPOS("TCK_CODPRO", aHeadTCK)
Local nDtEntr := GDFIELDPOS("TCK_DTENTR", aHeadTCK)
Local nStatus := GDFIELDPOS("TCK_STATUS", aHeadTCK)
Default lFim := .F.

If Len(aColsTCK) == 1
	If aColsTCK[1][Len(aColsTCK[1])]
		Return .T.
	Endif
Endif

//Percorre aCols
For f:=1 to Len(aColsTCK)
	If !aColsTCK[f][Len(aColsTCK[f])]
		If lFim .or. f == nAt
			//Percorre aHeader
			For i:= 1 to len(aHeadTCK)
				//Verifica se os campos obrigatórios estão preenchidos
				If Empty(aColsTCK[f][i]) .AND. Trim(Upper(aHeadTCK[i][2])) $ "TCK_CODPRO/TCK_DTENTR/TCK_STATUS/TCK_RESPON"
					//Mostra mensagem de Help
					Help(1," ","OBRIGAT2",,aHeadTCK[i][1],3,0)
					Return .F.
				ElseIf aColsTCK[f][nStatus] == "2" .and. Trim(Upper(aHeadTCK[i][2])) == "TCK_CODLIC" .and. Empty(aColsTCK[f][i])
					//Mostra mensagem de Help
					Help(1," ","OBRIGAT2",,aHeadTCK[i][1],3,0)
					Return .F.
				ElseIf aColsTCK[f][nStatus] == "3" .and. Trim(Upper(aHeadTCK[i][2])) == "TCK_NAOCON" .and. Empty(aColsTCK[f][i])
					//Mostra mensagem de Help
					Help(1," ","OBRIGAT2",,aHeadTCK[i][1],3,0)
					Return .F.
				Endif
			Next f
		Endif
		//Verifica se é somente LinhaOk
		If f <> nAt
			If aColsTCK[f][nCodPro] == aColsTCK[nAt][nCodPro] .and. aColsTCK[f][nDtEntr] == aColsTCK[nAt][nDtEntr]
				Help(" ",1,"JAEXISTINF")
				Return .F.
			Endif
		Endif
	Endif
Next i

PutFileInEof("TCK")

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Sg010GrvPro³ Autor ³ Rafael Diogo Richter ³ Data ³23/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Faz validacao antes de gravar.                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Sg010GrvPro()
Local i, k, nInd, nPos
Local lAchou
Local aMemos := {	{"TCK_MMOBS" , "TCK_OBSERV"},;
						{"TCK_MMNOT" , "TCK_NOTAS"	},;
						{"TCK_MMNCON", "TCK_NAOCON"}}
Local aColsTCK := oGetTCK:aCols
Local aHeadTCK := oGetTCK:aHeader

Local nCodPro := GDFIELDPOS("TCK_CODPRO", aHeadTCK)
Local nDesPro := GDFIELDPOS("TCK_DESPRO", aHeadTCK)
Local nOrgao  := GDFIELDPOS("TCK_ORGAO" , aHeadTCK)
Local nDtEntr := GDFIELDPOS("TCK_DTENTR", aHeadTCK)
Local nResp   := GDFIELDPOS("TCK_RESPON", aHeadTCK)
Local nStatus := GDFIELDPOS("TCK_STATUS", aHeadTCK)
Local nNotas  := GDFIELDPOS("TCK_NOTAS" , aHeadTCK)
Local nNaoCon := GDFIELDPOS("TCK_NAOCON", aHeadTCK)
Local nQtDias := GDFIELDPOS("TCK_QTDIAS", aHeadTCK)
Local nDtAler := GDFIELDPOS("TCK_DTALER", aHeadTCK)

For k := 1 To Len(aColsTCK)
	If !aColsTCK[k][Len(aColsTCK[k])]
		dbSelectArea("TCK")
		dbSetorder(1)
		If dbSeek(xFilial("TCK")+TA0->TA0_CODLEG+aColsTCK[k][nCodPro]+DTOS(aColsTCK[k][nDtEntr]))
			RecLock("TCK",.F.)
			lAchou := .T.
			If TCK->TCK_STATUS <> aColsTCK[k][nStatus]
				If TCK->TCK_STATUS == "3"//Deleta memo
					MSMM(&("TCK->TCK_MMNCON"),,,,2)
				EndIf
				If aColsTCK[k][nStatus] == "3"
					SGAW040(1,TA0->TA0_CODLEG,aColsTCK[k][nCodPro],aColsTCK[k][nDesPro],aColsTCK[k][nOrgao],aColsTCK[k][nDtEntr],aColsTCK[k][nResp],aColsTCK[k][nStatus],aColsTCK[k][nNaoCon])
				EndIf
			EndIf
			If (Len(AllTrim(MSMM(TCK->TCK_MMNOT,,,,3))) > Len(AllTrim(aColsTCK[k][nNotas])) .and. ;
					AllTrim(aColsTCK[k][nNotas]) <> AllTrim(MSMM(TCK->TCK_MMNOT,,,,3))) .or. ;
				 (Len(AllTrim(MSMM(TCK->TCK_MMNOT,,,,3))) < Len(AllTrim(aColsTCK[k][nNotas])) .and. ;
					AllTrim(MSMM(TCK->TCK_MMNOT,,,,3)) <> AllTrim(aColsTCK[k][nNotas]))
				SGAW050(1,TA0->TA0_CODLEG,aColsTCK[k][nCodPro],aColsTCK[k][nDesPro],aColsTCK[k][nOrgao],aColsTCK[k][nDtEntr],aColsTCK[k][nResp],aColsTCK[k][nStatus],aColsTCK[k][nNotas])
			EndIf
		Else
			RecLock("TCK",.T.)
			lAchou := .F.
			If aColsTCK[k][nStatus] == "3"
				SGAW040(2,TA0->TA0_CODLEG,aColsTCK[k][nCodPro],aColsTCK[k][nDesPro],aColsTCK[k][nOrgao],aColsTCK[k][nDtEntr],aColsTCK[k][nResp],aColsTCK[k][nStatus],aColsTCK[k][nNaoCon])
			EndIf
			If !Empty(aColsTCK[k][nNotas])
				SGAW050(2,TA0->TA0_CODLEG,aColsTCK[k][nCodPro],aColsTCK[k][nDesPro],aColsTCK[k][nOrgao],aColsTCK[k][nDtEntr],aColsTCK[k][nResp],aColsTCK[k][nStatus],aColsTCK[k][nNotas])
			EndIf
		EndIf
		dbSelectArea("TCK")
		For i:=1 to FCount()
			If "_FILIAL"$Upper(FieldName(i))
				FieldPut(i, xFilial("TCK"))
			ElseIf "_CODLEG"$Upper(FieldName(i))
				FieldPut(i, TA0->TA0_CODLEG)
			ElseIf (nPos := GDFIELDPOS(FieldName(i), aHeadTCK)) > 0
				FieldPut(i, aColsTCK[k][nPos])
			Endif
		Next i
		If (NGCADICBASE("TCK_QTDIAS","A","TCK",.F.))
			If (aColsTCK[k][nStatus] == "1" .And. aColsTCK[k][nQtDias] != 0)
				dbSelectArea("TAQ")
				dbSetorder(3)
				//Caso alterou a data de alerta e o registro ja esta gravado, exclui o anterior
				If (dbSeek(xFilial("TAQ")+"7"+TA0->TA0_CODLEG+aColsTCK[k][nCodPro]+DTOS(aColsTCK[k][nDtEntr])) .And. (TAQ->TAQ_DATA != aColsTCK[k][nDtAler]))
					RecLock("TAQ",.F.)
					dbDelete()
					MsUnlock("TAQ")
				EndIf
				If (FunName() == "SGAA010")
					SgPostEve("7",aColsTCK[k][nDtAler],"*              ",TA0->TA0_CODLEG+aColsTCK[k][nCodPro]+DTOS(aColsTCK[k][nDtEntr]))
				EndIf
			EndIf
		EndIf
		dbSelectArea("TCK")
		MsUnLock("TCK")

		For nInd := 1 To Len(aMemos)
			cMemo := aColsTCK[k][GDFieldPos(aMemos[nInd,2],aHeadTCK)]
			If !Empty(cMemo)
				If !lAchou
					MSMM(,TamSx3(aMemos[nInd,2])[1],,cMemo,1,,,"TCK",aMemos[nInd,1])
				Else
					MSMM(&("TCK->"+aMemos[nInd,1]),TamSx3(aMemos[nInd,2])[1],,cMemo,1,,,"TCK",aMemos[nInd,1])
				EndIf
			EndIf
		Next nInd
	ElseIf !Empty(aColsTCK[k][nCodPro]) .and. !Empty(aColsTCK[k][nDtEntr])
		dbSelectArea("TCK")
		dbSetorder(1)
		If dbSeek(xFilial("TCK")+TA0->TA0_CODLEG+aColsTCK[k][nCodPro]+DTOS(aColsTCK[k][nDtEntr]))
			For nInd := 1 To Len(aMemos)
				MSMM(&("TCK->"+aMemos[nInd,1]),,,,2)
			Next nInd
			dbSelectArea("TCK")
			RecLock("TCK",.F.)
			dbDelete()
			MsUnlock("TCK")
		EndIf
	EndIf
Next k

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SG010Leg ³ Autor ³ Rafael Diogo Richter  ³ Data ³06/08/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Cria uma janela contendo a legenda da mBrowse              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SGAA010		                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SG010Leg()
BrwLegenda(cCadastro,STR0051,{{"ENABLE",STR0060},; //"Legenda"###"Em Dia"
									{"BR_VERMELHO",STR0061}}) //"Atrasado"
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ SG010Chk ³ Autor ³ Rafael Diogo Richter  ³ Data ³06/08/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para validar o Semaforo.                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SG010Chk()
Local lRet := .F.

If TA0->TA0_DTVENC >= dDataBase .Or. Empty(TA0->TA0_DTVENC)
	lRet := .T.
EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ SG010Mos ³ Autor ³ Rafael Diogo Richter  ³ Data ³06/08/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para filtrar aleatoriamente os requisitos            ³±±
±±³          ³em dia/atrasados.                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SG010Mos()
lAleat := !lAleat

If !lAleat
	DbClearFilter()
	DbSeek(xFilial("TA0"))
Else
	MsgRun(STR0037,STR0015,{ || SG010Fil() }) //"Selecionando Requisitos"###"Aguarde..."
Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ SG010Fil ³ Autor ³ Rafael Diogo Richter  ³ Data ³06/08/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para filtrar aleatoriamente os requisitos            ³±±
±±³          ³em dia/atrasados.                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SG010Fil()
Local cFiltro := "TA0->TA0_DTVENC >= dDataBase .Or. Empty(TA0->TA0_DTVENC)"

dbSelectArea("TA0")
Set Filter to &(cFiltro)
dbSeek(xFilial("TA0"))

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SG010STAT ºAutor  ³Roger Rodrigues     º Data ³  22/12/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida campo de status                                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SGAA010                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function SG010STAT()
Local nAt := oGetTCK:nAT
Local aColsTCK := oGetTCK:aCols
Local nCodLic := GDFIELDPOS("TCK_CODLIC", oGetTCK:aHeader)
Local nNaoCon := GDFIELDPOS("TCK_NAOCON", oGetTCK:aHeader)
Local nDtAler := GDFIELDPOS("TCK_DTALER", oGetTCK:aHeader)
Local nDtEntr := GDFIELDPOS("TCK_DTENTR", oGetTCK:aHeader)
Local nQtDias := GDFIELDPOS("TCK_QTDIAS", oGetTCK:aHeader)

If !Pertence("123")
	Return .F.
Else
	If M->TCK_STATUS != "2"
		aColsTCK[nAt][nCodLic] := Space(TAMSX3("TCK_CODLIC")[1])
	ElseIf M->TCK_STATUS != "3"
		aColsTCK[nAt][nNaoCon] := Space(TAMSX3("TCK_NAOCON")[1])
	Endif
Endif

If (NGCADICBASE("TCK_QTDIAS","A","TCK",.F.))
	If  M->TCK_STATUS != "1"
		aColsTCK[nAt][nDtAler] := Space(TAMSX3("TCK_DTALER")[1])
		aColsTCK[nAt][nQtDias] := 0
	ElseIf aColsTCK[nAt][nQtDias] <> 0
		aColsTCK[nAt][nDtAler] :=	DaySub(aColsTCK[nAt][nDtEntr],aColsTCK[nAt][nQtDias])
	Endif
EndIf

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Sg010WStat³ Autor ³ Rafael Diogo Richter  ³ Data ³06/08/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para verificar se o campo pode ser editado ou nao    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg010WStat()
Local nAt := oGetTCK:nAT
Local aColsTCK := oGetTCK:aCols
Local nCodPro := GDFIELDPOS("TCK_CODPRO", oGetTCK:aHeader)
Local nDtEntr := GDFIELDPOS("TCK_DTENTR", oGetTCK:aHeader)

If nAt <= nTCK
	Return .F.
ElseIf Empty(aColsTCK[nAt][nCodPro])
	Return .F.
ElseIf (NGCADICBASE("TCK_DTENTR","A","TCK",.F.))
	If Empty(aColsTCK[nAt][nDtEntr])
		Return .F.
	EndIf
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Sg010WPro ³ Autor ³ Rafael Diogo Richter  ³ Data ³06/08/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para verificar se o campo pode ser editado ou nao    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg010WPro()
Local nAt := oGetTCK:nAT

If nAt <= nTCK
	Return .F.
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Sg010VPro ³ Autor ³ Rafael Diogo Richter  ³ Data ³19/08/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para validar o protocolo digitado.                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg010VPro()
Local i
Local nAt := oGetTCK:nAT
Local aColsTCK := oGetTCK:aCols
Local nCodPro := GDFIELDPOS("TCK_CODPRO", oGetTCK:aHeader)
Local nStatus := GDFIELDPOS("TCK_STATUS", oGetTCK:aHeader)

If nCodPro > 0 .And. nStatus > 0
	For i := 1 To Len(aColsTCK)
		If !aColsTCK[i][Len(aColsTCK[i])]
			If i == nAt
				Loop
			EndIf
			If M->TCK_CODPRO == aColsTCK[i][nCodPro] .And. aColsTCK[i][nStatus] <> "1"
			   Help(" ",1,STR0033,,STR0062,3,1) //"ATENÇÃO"###"Protocolo já finalizado"
			   Return .F.
			EndIf
		EndIf
	Next
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Sg010VDt ³ Autor ³ Rafael Diogo Richter  ³ Data ³19/08/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para validar a data digitada.                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg010VDt()
Local f
Local nAt := oGetTCK:nAt
Local aColsTCK := oGetTCK:aCols
Local nCodPro := GDFIELDPOS("TCK_CODPRO", oGetTCK:aHeader)
Local nDtEntr := GDFIELDPOS("TCK_DTENTR", oGetTCK:aHeader)
Local nDtAler := GDFIELDPOS("TCK_DTALER", oGetTCK:aHeader)
Local nDias   := GDFIELDPOS("TCK_QTDIAS", oGetTCK:aHeader)
Local nStatus := GDFIELDPOS("TCK_STATUS", oGetTCK:aHeader)

If nCodPro > 0 .And. nDtEntr > 0
	//Percorre aCols
	For f = 1 to Len(aColsTCK)
		If !aColsTCK[f][Len(aColsTCK[f])]
			If f <> nAt
				If aColsTCK[f][nCodPro] == aColsTCK[nAt][nCodPro] .and. aColsTCK[f][nDtEntr] == M->TCK_DTENTR
					Help(" ",1,"JAEXISTINF")
					Return .F.
				Endif
			Endif
		Endif
	Next i
Endif

If (NGCADICBASE("TCK_QTDIAS","A","TCK",.F.)) .AND.aColsTCK[nAt][nStatus] == "1"

	aColsTCK[nAt][nDtAler] := 	DaySub(M->TCK_DTENTR,aColsTCK[nAt][nDias] )

Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGA010Vld ³ Autor ³ Rafael Diogo Richter  ³ Data ³02/12/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida se toda a GetDados esta OK.                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function SGA010VLD(cAlias,lFim,oObj)
Local f, nAt  := oObj:nAt
Local aColsOk := oObj:aCols
Local aHeadOk := oObj:aHeader
Local nCodVer := 1, nCodVer2 := 0, nCodVer3 := 0
Local lRet    := .T., lCheck := .T.

Default lFim  := .F.

If cAlias == "TCL"
	nCodVer  := aScan( aHeadOk, { |x| Trim( Upper(x[2]) ) == "TCL_CODPLA"})
ElseIf cAlias == "TCM"
	nCodVer  := aScan( aHeadOk, { |x| Trim( Upper(x[2]) ) == "TCM_RESPON"})
ElseIf cAlias == "TJE" .And. nModulo == 35 //Validacao para TJE, das chaves Calendario+Curso+Turma
	nCodVer  := aScan( aHeadOk, { |x| Trim( Upper(x[2]) ) == "TJE_CALEND"})
	nCodVer2 := aScan( aHeadOk, { |x| Trim( Upper(x[2]) ) == "TJE_CURSO"})
	nCodVer3 := aScan( aHeadOk, { |x| Trim( Upper(x[2]) ) == "TJE_TURMA"})
EndIf
//Verifica aCols de Plano de Acao
If Len(aColsOk) == 1
	If ( Empty(aColsOk[1][nCodVer]) .AND. If( nCodVer2 > 0 , Empty(aColsOk[1][nCodVer2]) , .T. ) .AND. ;
					If( nCodVer3 > 0 , Empty(aColsOk[1][nCodVer3]) , .T. ) ) .or. aColsOk[1][Len(aColsOk[1])]
		lRet := .T.
		lCheck := .F.
	Endif
Endif
If lCheck
	//Percorre aCols
	For f = 1 to Len(aColsOk)
		If !aTail(aColsOk[f])
			If lFim .or. f == nAt
				//VerIfica se os campos obrigatórios estão preenchidos
				If Empty(aColsOk[f][nCodVer])
					//Mostra mensagem de Help
					Help(1," ","OBRIGAT2",,aHeadOk[nCodVer][1],3,0)
					lRet := .F.
					Exit
				Endif
				If nCodVer2 > 0 .AND. Empty(aColsOk[f][nCodVer2])
					//Mostra mensagem de Help
					Help(1," ","OBRIGAT2",,aHeadOk[nCodVer2][1],3,0)
					lRet := .F.
					Exit
				EndIf
				If nCodVer3 > 0 .AND. Empty(aColsOk[f][nCodVer3])
					//Mostra mensagem de Help
					Help(1," ","OBRIGAT2",,aHeadOk[nCodVer3][1],3,0)
					lRet := .F.
					Exit
				EndIf
			Endif
			//Verifica se é somente LinhaOk
			If f <> nAt .and. !aTail(aColsOk[nAt])
				If aColsOk[f][nCodVer] == aColsOk[nAt][nCodVer] .AND. ;
					If( nCodVer2 > 0 , aColsOk[f][nCodVer2] == aColsOk[nAt][nCodVer2] , .T. ) .AND. ;
					If( nCodVer3 > 0 , aColsOk[f][nCodVer3] == aColsOk[nAt][nCodVer3] , .T. )
					Help(" ",1,"JAEXISTINF")
					lRet := .F.
					Exit
				Endif
			Endif
			If cAlias == "TJE" .And. nModulo == 35 .AND. ;//Verifica a existencia das chaves Calendario+Curso+Turma na GetDados da TJE
				!ExistCpo("RA2",aColsOk[nAt][nCodVer] + IF( nCodVer2 > 0 ,aColsOk[nAt][nCodVer2], "" ) + IF( nCodVer3 > 0 ,aColsOk[nAt,nCodVer3], "" ))
				lRet := .F.
				Exit
			Endif
		Endif
	Next f
Endif

PutFileInEof(cAlias)
Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGAA010WF ³ Autor ³ Rafael Diogo Richter  ³ Data ³05/03/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Envia e-mail para os responsaveis                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGASGA                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SGAA010WF(nOpcx)
Local cMailMsg
Local lImp := .F.

cMailMsg := '<html>'
cMailMsg += '<head>'
cMailMsg += '<title>Informacoes do Requisito</title>'
cMailMsg += '<meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1">'
cMailMsg += '<style type="text/css">'
cMailMsg += '<!--'
cMailMsg += 'body,td,th {'
cMailMsg += '	color: #425166;'
cMailMsg += '}'
cMailMsg += 'body {'
cMailMsg += '	background-color: #999999;'
cMailMsg += '}'
cMailMsg += '.style14 {'
cMailMsg += '	font-family: Verdana, Arial, Helvetica, sans-serif;'
cMailMsg += '	font-style: italic;'
cMailMsg += '	font-weight: bold;'
cMailMsg += '	color: #3E4143;'
cMailMsg += '}'
cMailMsg += '.style21 {'
cMailMsg += '	font-size: 10px;'
cMailMsg += '	color: #3E4143;'
cMailMsg += '}'
cMailMsg += '.style44 {'
cMailMsg += '	font-family: Verdana, Arial, Helvetica, sans-serif;'
cMailMsg += '	font-size: 11px;'
cMailMsg += '	font-weight: bold;'
cMailMsg += '	color: #3E4143;'
cMailMsg += '}'
cMailMsg += '.style46 {font-family: Verdana, Arial, Helvetica, sans-serif; font-size: 10px; color: #3E4143; }'
cMailMsg += '.style48 {font-family: Verdana, Arial, Helvetica, sans-serif; font-style: italic; font-weight: bold; color: #3E4143; font-size: 10pt; }'
cMailMsg += '.style49 {font-size: 10pt}'
cMailMsg += '.style56 {'
cMailMsg += '	font-family: Verdana, Arial, Helvetica, sans-serif;'
cMailMsg += '	font-weight: bold;'
cMailMsg += '	font-size: 12px;'
cMailMsg += '	color: #3E4143;'
cMailMsg += '	font-style: italic;'
cMailMsg += '}'
cMailMsg += '.style57 {'
cMailMsg += '	font-family: Verdana, Arial, Helvetica, sans-serif;'
cMailMsg += '	font-weight: bold;'
cMailMsg += '	font-size: 11px;'
cMailMsg += '}'
cMailMsg += '.style59 {color: #3E4143}'
cMailMsg += '-->'
cMailMsg += '</style></head>'
cMailMsg += ''
cMailMsg += '<body>'
cMailMsg += '<table width="540" border="1" align="center" cellpadding="0" cellspacing="0" bordercolor="#98ACBC" bgcolor="#E0E6EB">'
cMailMsg += '  <tr bgcolor="#F7F8F8">'
cMailMsg += '    <td height="35" bgcolor="#BDCAD5"><span class="style48">&nbsp;&nbsp;&nbsp;&nbsp;Informa&ccedil;&otilde;es do Requisito</span>      </td>'
cMailMsg += '  </tr>'
cMailMsg += '  <tr>'
cMailMsg += '    <td height="35"><span class="style46">      <br>'
cMailMsg += '      &nbsp;&nbsp;&nbsp;&nbsp;<strong>Requisito: </strong>'+TA0->TA0_CODLEG+'<br>'
cMailMsg += '      <br>'
cMailMsg += '      &nbsp;&nbsp;&nbsp;&nbsp;<strong>Tema: </strong>'+AllTrim(TA0->TA0_EMENTA)+'<br>'
cMailMsg += '      <br>'
If !Empty(TA0->TA0_TIPO)
	cMailMsg += '      &nbsp;&nbsp;&nbsp;&nbsp;<strong>Tipo Requisito: </strong>'+TA0->TA0_TIPO+'<br>'
	cMailMsg += '      <br>'
EndIf
If !Empty(TA0->TA0_DESCRI)
	cMailMsg += '&nbsp;&nbsp;&nbsp;&nbsp;<strong>Descri&ccedil;&atilde;o: </strong>'+AllTrim(TA0->TA0_DESCRI)+'<br>'
	cMailMsg += '<br>'
EndIf
If !Empty(TA0->TA0_ORIGEM)
	cMailMsg += '&nbsp;&nbsp;&nbsp;&nbsp;<strong>Origem: </strong>'
	If TA0->TA0_ORIGEM == "1"
		cMailMsg += STR0077 //"Federal"
	ElseIf TA0->TA0_ORIGEM == "2"
		cMailMsg += STR0078 //"Estadual"
	ElseIf TA0->TA0_ORIGEM == "3"
		cMailMsg += STR0079 //"Municipal"
	ElseIf TA0->TA0_ORIGEM == "4"
		cMailMsg += STR0080 //"Acionista"
	ElseIf TA0->TA0_ORIGEM == "5"
		cMailMsg += STR0081 //"Comunidade"
	ElseIf TA0->TA0_ORIGEM == "6"
		cMailMsg += STR0082 //"Outros"
	EndIf
	cMailMsg += '<br>'
EndIf
cMailMsg += '<br>'
If !Empty(TA0->TA0_CODRES)
	cMailMsg += '&nbsp;&nbsp;&nbsp;&nbsp;<strong>Resolu&ccedil;&atilde;o: </strong>'+TA0->TA0_CODRES+'<br>'
	cMailMsg += '<br>'
EndIf
If !Empty(TA0->TA0_NUMRES)
	cMailMsg += '&nbsp;&nbsp;&nbsp;&nbsp;<strong>N&ordm; Resolu&ccedil;&atilde;o: </strong>'+TA0->TA0_NUMRES+'<br>'
	cMailMsg += '<br>'
EndIf
If !Empty(TA0->TA0_OREMIS)
	cMailMsg += '&nbsp;&nbsp;&nbsp;&nbsp;<strong>&Oacute;rg&atilde;o Emissor: </strong>'+TA0->TA0_OREMIS+'<br>'
	cMailMsg += '<br>'
EndIf
If !Empty(TA0->TA0_DTEMIS)
	cMailMsg += '&nbsp;&nbsp;&nbsp;&nbsp;<strong>Data Emiss&atilde;o: </strong>'+DTOC(TA0->TA0_DTEMIS)+'<br>'
	cMailMsg += '<br>'
EndIf
If !Empty(TA0->TA0_DTPUBL)
	cMailMsg += '&nbsp;&nbsp;&nbsp;&nbsp;<strong>Data Publica&ccedil;&atilde;o: </strong>'+DTOC(TA0->TA0_DTPUBL)+'<br>'
	cMailMsg += '<br>'
EndIf
If !Empty(TA0->TA0_SITE)
	cMailMsg += '&nbsp;&nbsp;&nbsp;&nbsp;<strong>Site: </strong>'+AllTrim(TA0->TA0_SITE)+'<br>'
	cMailMsg += '<br>'
EndIf
If !Empty(TA0->TA0_DTVENC)
	cMailMsg += '&nbsp;&nbsp;&nbsp;&nbsp;<strong>Vencimento: </strong>'+DTOC(TA0->TA0_DTVENC)+'<br>'
	cMailMsg += '<br>'
EndIf
If !Empty(TA0->TA0_DTVIGE)
	cMailMsg += '&nbsp;&nbsp;&nbsp;&nbsp;<strong>Vig&ecirc;ncia: </strong>'+DTOC(TA0->TA0_DTVIGE)+'<br>'
	cMailMsg += '<br>'
EndIf
If !Empty(TA0->TA0_EST)
	cMailMsg += '&nbsp;&nbsp;&nbsp;&nbsp;<strong>Estado: </strong>'+TA0->TA0_EST+'<br>'
	cMailMsg += '<br>'
EndIf
cMailMsg += '    </span></td>'
cMailMsg += '  </tr>'
If !Empty(TA0->TA0_OBRIGA)
	cMailMsg += '  <tr bgcolor="#F7F8F8">'
	cMailMsg += '    <td height="35" bgcolor="#BDCAD5"><div align="right" class="style14">'
	cMailMsg += '      <div align="left" class="style49"><span style="text-decoration:none">&nbsp;&nbsp;&nbsp;&nbsp;Atendimento</span></div>'
	cMailMsg += '    </div></td>'
	cMailMsg += '  </tr>'
	cMailMsg += '  <tr align="left" valign="top">'
	cMailMsg += '    <td bgcolor="#E0E6EB"><span class="style44">      </span><span class="style46"><br>'
	cMailMsg += '&nbsp;&nbsp;&nbsp;&nbsp;<strong>Obriga&ccedil;&otilde;es: </strong>'+AllTrim(TA0->TA0_OBRIGA)+'<br>'
	cMailMsg += '<br>'
	cMailMsg += '</span>      <br>      <div align="center" class="style21"></div></td>'
	cMailMsg += '  </tr>'
EndIf
If !Empty(TA0->TA0_ACOES)
	cMailMsg += '  <tr bgcolor="#F7F8F8">'
	cMailMsg += '    <td height="35" bgcolor="#BDCAD5"><div align="right" class="style14">'
	cMailMsg += '        <div align="left" class="style49"><span style="text-decoration:none">&nbsp;&nbsp;&nbsp;&nbsp;Controles</span></div>'
	cMailMsg += '    </div></td>'
	cMailMsg += '  </tr>'
	cMailMsg += '  <tr align="left" valign="top">'
	cMailMsg += '    <td bgcolor="#E0E6EB"><span class="style44"> </span><span class="style46"><br>'
	cMailMsg += '&nbsp;&nbsp;&nbsp;&nbsp;<strong>A&ccedil;&otilde;es: </strong>'+AllTrim(TA0->TA0_ACOES)+'<br>'
	cMailMsg += '      </span> <br>'
	lImp := .T.
EndIf
dbSelectArea("TCL")
dbSetOrder(1)
If dbSeek(xFilial("TCL")+TA0->TA0_CODLEG)
	If !lImp
		cMailMsg += '  <tr bgcolor="#F7F8F8">'
		cMailMsg += '    <td height="35" bgcolor="#BDCAD5"><div align="right" class="style14">'
		cMailMsg += '        <div align="left" class="style49"><span style="text-decoration:none">&nbsp;&nbsp;&nbsp;&nbsp;Controles</span></div>'
		cMailMsg += '    </div></td>'
		cMailMsg += '  </tr>'
		cMailMsg += '  <tr align="left" valign="top">'
		cMailMsg += '    <td bgcolor="#E0E6EB"><span class="style44"> </span><span class="style46">'
		cMailMsg += '      </span> <br>'
	EndIf
	cMailMsg += '      <table width="98%"  border="1" align="center" cellpadding="0" cellspacing="0" bordercolor="#BDCAD5">'
	cMailMsg += '        <tr bgcolor="#BDCAD5">'
	cMailMsg += '          <td height="20" colspan="2" scope="col">'
	cMailMsg += '            <div align="left" class="style56">&nbsp;&nbsp;&nbsp;&nbsp;Planos de A&ccedil;&otilde;es do Requisito</div></td>'
	cMailMsg += '        </tr>'
	cMailMsg += '        <tr bgcolor="#D3DDE4">'
	cMailMsg += '          <td height="20" bgcolor="#D3DDE4" scope="col"><div align="left" class="style57 style59">&nbsp;&nbsp;&nbsp;&nbsp;Plano</div></td>'
	cMailMsg += '          <td height="20" bgcolor="#D3DDE4" scope="col"><div align="left" class="style57">&nbsp;&nbsp;&nbsp;&nbsp;<span class="style59">Descri&ccedil;&atilde;o</span></div></td>'
	cMailMsg += '        </tr>'
	While !Eof() .And. TCL->TCL_FILIAL == xFilial("TCL") .And. TCL->TCL_CODLEG == TA0->TA0_CODLEG
		cMailMsg += '        <tr bgcolor="#E8ECF0" class="style46">'
		cMailMsg += '          <td height="20" bgcolor="#E8ECF0" scope="col"><div align="left" class="style59">&nbsp;&nbsp;&nbsp;&nbsp;'+TCL->TCL_CODPLA+'</div></td>'
		dbSelectArea("TAA")
		dbSetorder(1)
		dbSeek(xFilial("TAA")+TCL->TCL_CODPLA)
		cMailMsg += '          <td height="20" bgcolor="#E8ECF0" scope="col"><div align="left" class="style59">&nbsp;&nbsp;&nbsp;&nbsp;'+AllTrim(TAA->TAA_NOME)+'</div></td>'
		cMailMsg += '        </tr>'
		TCL->(dbSkip())
	End
	cMailMsg += '      </table>'
	cMailMsg += '      <br>'
	cMailMsg += '      <div align="center" class="style21"></div></td>'
	cMailMsg += '  </tr>'
EndIf
dbSelectArea("TCM")
dbSetOrder(1)
If dbSeek(xFilial("TCM")+TA0->TA0_CODLEG)
	cMailMsg += '  <tr bgcolor="#F7F8F8">'
	cMailMsg += '    <td height="35" bgcolor="#BDCAD5"><div align="right" class="style14">'
	cMailMsg += '        <div align="left" class="style49"><span style="text-decoration:none">&nbsp;&nbsp;&nbsp;&nbsp;Respons&aacute;veis pelo Requisito </span></div>'
	cMailMsg += '    </div></td>'
	cMailMsg += '  </tr>'
	cMailMsg += '  <tr align="left" valign="top">'
	cMailMsg += '    <td bgcolor="#E0E6EB"> <span class="style46"><br>'
	cMailMsg += '<br>'
	cMailMsg += '    </span>'
	cMailMsg += '      <table width="98%"  border="1" align="center" cellpadding="0" cellspacing="0" bordercolor="#BDCAD5">'
	cMailMsg += '        <tr bgcolor="#BDCAD5">'
	cMailMsg += '          <td width="30%" height="20" scope="col"><div align="left" class="style57 style59">&nbsp;&nbsp;&nbsp;&nbsp;Respons&aacute;vel</div></td>'
	cMailMsg += '          <td width="70%" height="20" bgcolor="#BDCAD5" scope="col"><div align="left" class="style57">&nbsp;&nbsp;&nbsp;&nbsp;<span class="style59">Descri&ccedil;&atilde;o</span></div></td>'
	cMailMsg += '        </tr>'
	dbSelectArea("TCM")
	dbSetOrder(1)
	dbSeek(xFilial("TCM")+TA0->TA0_CODLEG)
	While !Eof() .And. TCM->TCM_FILIAL == xFilial("TCM") .And. TCM->TCM_CODLEG == TA0->TA0_CODLEG
		cMailMsg += '        <tr bgcolor="#E8ECF0" class="style46">'
		cMailMsg += '          <td width="30%" height="20" scope="col"><div align="left" class="style59">&nbsp;&nbsp;&nbsp;&nbsp;'+TCM->TCM_RESPON+'</div></td>'
		dbSelectArea("QAA")
		dbSetorder(1)
		dbSeek(xFilial("QAA")+TCM->TCM_RESPON)
		cMailMsg += '          <td width="70%" height="20" bgcolor="#E8ECF0" scope="col"><div align="left" class="style59">&nbsp;&nbsp;&nbsp;&nbsp;'+QAA->QAA_NOME+'</div></td>'
		cMailMsg += '        </tr>'
		TCM->(dbSkip())
	End
EndIf
cMailMsg += '      </table>'
cMailMsg += '      <span class="style46"><br>'
cMailMsg += '            </span> <br>'
cMailMsg += '      <div align="center" class="style21"></div></td>'
cMailMsg += '  </tr>'
cMailMsg += '</table>'
cMailMsg += '</body>'
cMailMsg += '</html>'

dbSelectArea("TCM")
dbSetOrder(1)
dbSeek(xFilial("TCM")+TA0->TA0_CODLEG)
While !Eof() .And. TCM->TCM_FILIAL == xFilial("TCM") .And. TCM->TCM_CODLEG == TA0->TA0_CODLEG

	dbSelectArea("QAA")
	dbSetorder(1)
	If dbSeek(xFilial("QAA")+TCM->TCM_RESPON)
		If !Empty(QAA->QAA_EMAIL)
			NGSendMail( , Alltrim(QAA->QAA_EMAIL)+Chr(59), , , If(nOpcx==3,STR0063,STR0064)+TA0->TA0_CODLEG, "", cMailMsg)//"Inclusao do Requisito: "###"Alteracao do Requisito: "
		Endif
	EndIf

	TCM->(dbSkip())
End

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGA010DTVAL Autor ³ Jackson Machado	    ³ Data ³11/07/2011³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Função para validação das datas                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGASGA                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SGA010DTVAL()
Local lRet := .T.
Local cMsg := cSol := cHelp := ""
Local cVar := ReadVar()

If cVar == "M->TA0_DTEMIS" .or. cVar == "M->TA0_DTPUBL"

	If M->TA0_DTEMIS > dDataBase .Or. M->TA0_DTPUBL > dDataBase
		cHelp := "DATAMAIOR"
		If cVar == "M->TA0_DTEMIS"
			cMsg := STR0127 //"A data de Emissão não pode ser maior do que a data atual!"
			cSol := STR0129 //"Informe uma Data de Emissão menor do que a atual."
		Else
			cMsg := STR0128 //"A data de Publicação não pode ser maior do que a data atual!"
			cSol := STR0130 //"Informe uma Data de Publicação menor do que a atual."
		EndIf
		lRet := .F.
	EndIf

	If lRet .and. M->TA0_DTEMIS > M->TA0_DTPUBL .and. !Empty(M->TA0_DTEMIS) .and. !Empty(M->TA0_DTPUBL)
		lRet := .F.
		If cVar == "M->TA0_DTEMIS"
			cHelp := "DATAMAIOR"
			cMsg := STR0065//"Data de Emissão maior que Data de Publicação."
			cSol := STR0066//"Informe uma Data de Emissão menor que a Data de Publicação."
		Else
			cHelp := "DATAMENOR"
			cMsg := STR0083 //"Data de Publicação menor que Data de Emissão."
			cSol := STR0084 //"Informe uma Data de Publicação maior que a Data de Emissão."
		Endif
	Endif
Endif

If lRet .and. (cVar == "M->TA0_DTVENC" .or. cVar == "M->TA0_DTPUBL")
	If M->TA0_DTVENC < M->TA0_DTPUBL .and. !Empty(M->TA0_DTVENC) .and. !Empty(M->TA0_DTPUBL)
		lRet := .F.
		If cVar == "M->TA0_DTVENC"
			cHelp := "DATAMENOR"
			cMsg := STR0067//"Data de Vigência Fim menor que Data de Publicação."
			cSol := STR0068//"Informe uma Data de Vigência maior que a Data de Publicação."
		Else
			cHelp := "DATAMAIOR"
			cMsg := STR0085 //"Data de Publicação maior que Data de Emissão."
			cSol := STR0086 //"Informe uma Data de Publicação menor que a Data de Vigência."
		Endif
	Endif
Endif

If lRet .and. (cVar == "M->TA0_DTVENC" .or. cVar == "M->TA0_DTVIGE")
	If M->TA0_DTVENC < M->TA0_DTVIGE .and. !Empty(M->TA0_DTVENC) .and. !Empty(M->TA0_DTVIGE)
		lRet := .F.
		If cVar == "M->TA0_DTVENC"
			cHelp := "DATAMENOR"
			cMsg := STR0069//"Data de Fim da Vigência menor que Data de Início da Vigência."
			cSol := STR0070//"Informe uma Data de Fim da Vigência maior que a Data de Início da Vigência."
		Else
			cHelp := "DATAMAIOR"
			cMsg := STR0087 //"Data de Início da Vigência maior que Data de Fim da Vigência."
			cSol := STR0088 //"Informe uma Data de Início da Vigência menor que a Data de Fim da Vigência."
		Endif
	Endif
Endif

If lRet .and. (cVar == "M->TA0_DTPUBL" .or. cVar == "M->TA0_DTVIGE")
	If M->TA0_DTVIGE < M->TA0_DTPUBL .and. !Empty(M->TA0_DTPUBL) .and. !Empty(M->TA0_DTVIGE)
		lRet := .F.
		If cVar == "M->TA0_DTVIGE"
			cHelp := "DATAMENOR"
			cMsg := STR0071//"Data de Vigência Início menor que Data de Publicação."
			cSol := STR0068//"Informe uma Data de Vigência maior que a Data de Publicação."
		Else
			cHelp := "DATAMAIOR"
			cMsg := STR0089 //"Data de Publicação maior que Data de Início da Vigência."
			cSol := STR0090 //"Informe uma Data de Publicação menor que a Data de Início da Vigência."
		Endif
	Endif
Endif

If !lRet
	ShowHelpDlg(cHelp,{cMsg},2,{cSol},2)
Endif

Return lRet
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SGA010WHENºAutor  ³Roger Rodrigues     º Data ³  22/12/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³When dos campos da tela                                     º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SGAA010                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function SGA010WHEN(cCampo)
Local nAt := oGetTCK:nAT
Local aColsTCK := oGetTCK:aCols
Local nStatus := GDFIELDPOS("TCK_STATUS", oGetTCK:aHeader)

If cCampo == "TCK_CODLIC"
	If nAt <= nTCK
		Return .F.
	Endif
	If aColsTCK[nAt][nStatus] != "2"
		Return .F.
	Endif
ElseIf cCampo == "TCK_NAOCON"
	If nAt <= nTCK
		Return .F.
	Endif
	If aColsTCK[nAt][nStatus] != "3"
		Return .F.
	Endif
Endif

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fChange   ºAutor  ³Guilehrme Benkendorfº Data ³  08/01/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Posiciona RA2 e cCalend na MSNewGetDados                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SGAA010                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fChange(oObj)

Local nPosRA2 := 3, nPosCal := 1
Local nAt     := oObj:oBrowse:nAt
Local aColsOK := aClone(oObj:aCols)
Local aHeadOK := aClone(oObj:aHeader)

nPosCal := aScan( aHeadOK, { |x| Trim( Upper(x[2]) ) == "TJE_CALEND"})
nPosRA2 := aScan( aHeadOK, { |x| Trim( Upper(x[2]) ) == "TJE_CURSO"})

If !Empty(aColsOK[nAt,nPosRA2])
	dbSelectArea("RA2")
	dbSetOrder(1)
	dbSeek(xFilial("RA2")+aColsOK[nAt,nPosCal]+aColsOK[nAt,nPosRA2])
EndIf

cCalend := aColsOK[nAt,nPosCal]

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SGA010CPS ºAutor  ³Guilherme Benkendorfº Data ³  08/01/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida Campos da TJE - Chamados no X3_VALID                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SGAA010                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function SGA010CPS(nCampo,nAux)
Local lRet := .T.
Default nCampo := 0

If nCampo == 1
	lRet := EXISTCPO('RA2',M->TJE_CALEND)
	If lRet
		cCalend := M->TJE_CALEND
	EndIf
ElseIf nCampo == 2 .And. !Empty(cCalend)
	lRet := EXISTCPO('RA2',cCalend+M->TJE_CURSO)
ElseIf nCampo == 3 .And. !Empty(cCalend)
	lRet := EXISTCPO('RA2',cCalend+RA2->RA2_CURSO+M->TJE_TURMA)//Posiciona RA2 no gatilha TJE_CURSO,001
EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGA010QTDIAS()³ Autor ³Jefferson Guermandi³ Data ³ 07/01/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida a cada linha incluida na GetDados                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SGA010QTDIAS()
Local lRET := .T.
Local nAt := oGetTCK:nAT
Local aColsTCK := oGetTCK:aCols
Local nDtEntr := GDFIELDPOS("TCK_DTENTR", oGetTCK:aHeader)
Local nDtAler := GDFIELDPOS("TCK_DTALER", oGetTCK:aHeader)
Local nStatus := GDFIELDPOS("TCK_STATUS", oGetTCK:aHeader)

If Empty(aColsTCK[nAt][nDtEntr])

	lRet := .F.
	ShowHelpDlg(STR0016, {STR0122},5,{STR0123 ,""},1)//Atenção###"Data de Entrega não preenchida."###"Preencha primeiramente a data de entrega."

ElseIF aColsTCK[nAt][nStatus] <> "1"

	lRet := .F.
	ShowHelpDlg(STR0016, {STR0126},5,{STR0121 ,""},1) //Atenção###"Status diferente de Em Análise."###"Status deve ser Em Análise para editar a quantidade de dias para emitir."

ElseIf M->TCK_QTDIAS < 0

	lRet := .F.
	ShowHelpDlg(STR0016, {STR0124},5,{STR0125 ,""},1)//Atenção###"Valor Negativo."###"Este campo não permite valores negativos."
Else
	aColsTCK[nAt][nDtAler] :=	DaySub(aColsTCK[nAt][nDtEntr],M->TCK_QTDIAS)
EndIf

Return lRet


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGA010WHQD()³ Autor ³Thiago H. dos Santos ³ Data ³ 07/01/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ When para o campo quantidade de dias                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SGA010WHQD()
Local lRET := .T.
Local nAt := oGetTCK:nAT
Local aColsTCK := oGetTCK:aCols
Local nStatus := GDFIELDPOS("TCK_STATUS", oGetTCK:aHeader)


If !(aColsTCK[nAt][nStatus] $ " ,1")
	lRet := .F.
EndIf

Return lRet
//---------------------------------------------------------------------
/*/{Protheus.doc} SGAA010HIS()
Gravacao de historico de legislacao

@author  Elynton Fellipe Bazzo
@since   02/05/2013
@version P11
@return  .T.
/*/
//---------------------------------------------------------------------
Static Function SGAA010HIS()

	Local i, nx, lAlterou := .F.
	Local dData := dDataBase, cHora := Time()

	//Verifica se o registro foi alterado de fato
	dbSelectArea( "TA0" )
	For i := 1 To Len( aAlt )
		nx := "M->" + FieldName(i)
		If &nx. <> aAlt[i]
			lAlterou := .T.
		EndIf
	Next i
	//Grava Historico de alteracao
	If lAlterou
		dbSelectArea( "TE8" )
		dbSetOrder( 01 )
		If dbSeek( xFilial( "TE8" )+TA0->TA0_CODLEG+DTOS( dData )+cHora )
			RecLock( "TE8",.F. )
		Else
			RecLock( "TE8",.T. )
		Endif
		For i:=1 to FCount()
			If "_FILIAL"$Upper(FieldName(i))
				FieldPut(i, xFilial("TE8"))
			ElseIf "_DATA"$Upper(FieldName(i))
				FieldPut(i, dData)
			ElseIf "_HORA"$Upper(FieldName(i))
				FieldPut(i, cHora)
			Else
				FieldPut(i, &("TA0->TA0"+Substr(FieldName(i),4)))
			Endif
		Next i
		MsUnlock( "TE8" )
	Endif

Return .T.
