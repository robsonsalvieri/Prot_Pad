#INCLUDE "MNTA650.ch"
#include "protheus.ch"

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTA650
Conciliação manual das notas fiscais

@author Evaldo Cevinscki Jr.
@since 06/02/06
@version P12

@return .T.
/*/
//------------------------------------------------------------------------------
Function MNTA650()

	Local aArea       := GetArea()
	Local aNGBEGINPRM := {}
	Local nSizeFil    := 0
	Local oTmpTbl

	If !FindFunction( 'MNTAMIIN' ) .Or. MNTAMIIN( 19, 95 )

		aNGBEGINPRM := NGBeginPrm()
		nSizeFil    := FwSizeFilial()

		Private asMenu
		Private cAliTRB2 	:= GetNextAlias()
		Private aSize	 	:= MsAdvSize(,.F.,420)
		Private cCadastro	:= OemtoAnsi(STR0001) //"Conciliação de NF's Manual"

		aDBFB := {}
		aAdd(aDBFB,{"ASSI"      , "C", 2,0})
		aAdd(aDBFB,{"NF"        , "C", TamSX3("TQN_NOTFIS")[1],0})
		aAdd(aDBFB,{"VLABA"     , "N",15,3})
		aAdd(aDBFB,{"VLUNIT"    , "N", TAMSX3("TQN_VALUNI")[1]/*9*/,TAMSX3("TQN_VALUNI")[2]/*3*/})
		aAdd(aDBFB,{"VLNEG"     , "N", TAMSX3("TQH_PRENEG")[1]/*9*/,TAMSX3("TQH_PRENEG")[2]/*3*/})
		aAdd(aDBFB,{"COMB"    	, "C", TAMSX3("TQN_CODCOM")[1]/*3*/,0})
		aAdd(aDBFB,{"LITROS"    , "N", 9,3})
		aAdd(aDBFB,{"PLACA"     , "C", 8,0})
		aAdd(aDBFB,{"FROTA"     , "C", 16,0})
		aAdd(aDBFB,{"CCUSTO"    , "C", 20,0})
		aAdd(aDBFB,{"CTRAB"     , "C", 6,0})
		aAdd(aDBFB,{"ITEMCTA"   , "C", 20,0})
		aAdd(aDBFB,{"CLASSE"    , "C", 20,0})
		aAdd(aDBFB,{"PROD"    	, "C", 30,0})
		aAdd(aDBFB,{"NABAST"   	, "C", 15,0})
		aAdd(aDBFB,{"DTABAS"    , "D", 8,0})
		aAdd(aDBFB,{"HRABAS"    , "C", 5,0})
		aAdd(aDBFB,{"KM"        , "N", 9,0})
		aAdd(aDBFB,{"DA4_NOME"  , "C", 40,0})
		aAdd(aDBFB,{"FILIAL"    , "C", nSizeFil,0})
		aAdd(aDBFB,{"POSTO"     , "C", 30,0})
		aAdd(aDBFB,{"DTPGTO"    , "D", 8,0})
		aAdd(aDBFB,{"RECNO"     , "N", 10,0})
		aAdd(aDBFB,{"NUMPC"     , "C", TamSX3("C7_NUM")[1],0})

		//Intancia classe FWTemporaryTable
		oTmpTbl:= FWTemporaryTable():New( cAliTRB2, aDBFB )
		//Adiciona os Indices
		oTmpTbl:AddIndex("Ind01", {"NABAST"})
		oTmpTbl:AddIndex("Ind02", {"DTABAS"})
		oTmpTbl:AddIndex("Ind03", {"FILIAL","FROTA","DTABAS","HRABAS"})
		//Cria a tabela temporaria
		oTmpTbl:Create()

		MNTA650PTE()

		oTmpTbl:Delete()

		NGRETURNPRM( aNGBEGINPRM )

	EndIf

	RestArea( aArea )

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA650C2  ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³ 06/02/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Testa a igualdade dos valores                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA650                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MNA650C2(nValor, nQtLitros)

	local lRETVH := .T.

	If Mv_Par01 <> nValor
		lRETVH := .F.
		HELP(" ",1,STR0010,,STR0012,3,1)  //"ATENÇÃO"###"Valor incorreto."
	EndIf
	If !Empty(Mv_Par06)
		If Mv_Par02 <> nQtLitros
			lRETVH := .F.
			HELP(" ",1,STR0010,,STR0012,3,1)  //"ATENÇÃO"###"Valor incorreto."
		EndIf
	EndIf

Return lRETVH

//---------------------------------------------------------------------
/*/{Protheus.doc} MNA650INI
Monta o arquivo temporario inicial mostrado no browse.

@param [dDeData]  , Caracter, De Data.
@param [dAteData] , Carcater, Até Data.
@param cCodPos    , Caracter, Código do posto.
@param cLoja	  , Caracter, Loja utilizada.
@param cCNPJ	  , Caracter, CNPJ utilizado.
@param [nQtLitros], Numérico, Quantidade de litros.
@param [nValorNF] , Numérico, Valor da nota fiscal.
@param lRefaz	  , Lógico  , Indica se vai carregar novamente o conteúdo.

@author Evaldo Cevinscki Jr.
@since 06/02/06
@version P12

@return .T.
/*/
//------------------------------------------------------------------------------
Function MNA650INI(dDeData, dAteData, cCodPos, cLoja, cCNPJ, nQtLitros, nValorNF, lRefaz )

	Local cCondicao     := ""
	Local lMNT656VL		:= ExistBlock("MNT656VL")
	Local lMNTA6501		:= ExistBlock( "MNTA6501" )
	Local cComb		    := Space(TamSX3("TQN_CODCOM")[1])
	Local lAferiHod		:= .F.
	Local xAferiHod

	Private aMark    	:= {}
	Private lInverte 	:= .F.
	Private cMarca   	:= GetMark()
	Private oMark

	Default lRefaz 	    := .F.

	//Limpa o conteúdo caso já tenha trazido resultado
	If lRefaz .Or. (cAliTRB2)->(RecCount()) > 0
		dbselectArea(cAliTRB2)
		Zap
		//Limpa caso tenha algum registro marcado
		nTotalCL := 000.00
		nTotalC  := 000.00
		nSelec   := 000.000
	EndIf

	aAdd(aMark,{"ASSI"      ,NIL," "     ,    })
	aAdd(aMark,{"NF"        ,NIL,STR0013 ,"@!"})                //"Nota Fiscal     "
	aAdd(aMark,{"VLABA"     ,NIL,STR0014 ,"@E 99,999,999,999.999"}) //"Valor Abastecido"
	aAdd(aMark,{"VLUNIT"    ,NIL,STR0015 ,'@E 99,999.'+Replicate('9',TAMSX3("TQN_VALUNI")[2]) })     //"Valor Unitario"
	aAdd(aMark,{"VLNEG"     ,NIL,STR0016 ,'@E 99,999.'+Replicate('9',TAMSX3("TQH_PRENEG")[2]) })     //"Valor Negociado"
	aAdd(aMark,{"LITROS"    ,NIL,STR0017 ,"@E 99,999.999"})     //"Litros"
	aAdd(aMark,{"PLACA"     ,NIL,STR0018 ,"@!"})                //"Placa"
	aAdd(aMark,{"FROTA"     ,NIL,STR0054 ,"@!"})                //"Frota"
	aAdd(aMark,{"DTABAS"    ,NIL,STR0019 ,"99/99/99"})          //"Dt.Abastec."
	aAdd(aMark,{"HRABAS"    ,NIL,STR0020 ,"99:99"})             //"Hr.Abastec."
	aAdd(aMark,{"KM"        ,NIL,STR0021 ,"@E 999,999,999"})    //"KM"
	aAdd(aMark,{"DTPGTO"    ,NIL,STR0045 ,"99/99/99"})          //"Data Pagamento"
	aAdd(aMark,{"DA4_NOME"  ,NIL,STR0022 ,"@!"})                //"Motorista"
	aAdd(aMark,{"POSTO"     ,NIL,STR0046 ,"@!"})                //"Nome Fantasia"

	DbselectArea("TQN")
	ProcRegua(TQN->(reccount()))

	If cFiliais == 1
		If Empty(cCodPos) .And. !Empty(cCNPJ)
			DbselectArea("TQN")
			DbSetorder(13)
			If DbSeek(cCNPJ)
				cCondicao := "TQN->TQN_CNPJ == cCNPJ"
			Endif
		Else
			DbselectArea("TQN")
			DbSetorder(10)
			If DbSeek(cCodPos+cLoja,.t.)
				cCondicao := "TQN->TQN_POSTO == cCodPos .and. TQN->TQN_LOJA == cLoja  .and. TQN->TQN_DTABAS <= dAteData"
			Endif
		Endif
	Else
		If Empty(cCodPos) .And. !Empty(cCNPJ)
			DbselectArea("TQN")
			DbSetorder(12)
			If DbSeek(xFilial("TQN")+cCNPJ)
				cCondicao := "TQN->TQN_FILIAL == xFilial('TQN')	.and. TQN->TQN_CNPJ == cCNPJ"
			Endif
		Else
			DbselectArea("TQN")
			DbSetorder(03)
			If DbSeek(xFilial("TQN")+cCodPos+cLoja,.t.)
				cCondicao := "TQN->TQN_FILIAL == xFilial('TQN')	.and. TQN->TQN_POSTO == cCodPos .and. TQN->TQN_LOJA  == cLoja .and. TQN->TQN_DTABAS <= dAteData"
			Endif
		Endif
	Endif

	If !EoF() .And. !Empty(cCondicao)
		While !EoF() .And. &(cCondicao)
			IncProc()
			If TQN->TQN_AUTO == "2"   //Abastecimento Manual

				If !Empty(TQN->TQN_DTCON)
					DbSelectArea("TQN")
					DbSkip()
					Loop
				EndIf

				If TQN->TQN_DTABAS < dDeData .Or. TQN->TQN_DTABAS > dAteData
					DbSelectArea("TQN")
					DbSkip()
					Loop
				EndIf

				If lMNT656VL //Verifica se nao deve atualizar estoque e odometro
					xAferiHod := ExecBlock( 'MNT656VL', .F., .F., { TQN->TQN_FROTA } )
					lAferiHod := IIf( ValType(xAferiHod) == 'A', xAferiHod[1], xAferiHod)
					If lAferiHod
						dbSelectArea("TQN")
						dbSkip()
						Loop
					EndIf
				EndIf

				If !Empty(cCodPos) .And. Empty(cLoja)
					DbSelectArea("TQF")
					DbSetOrder(1)
					If DbSeek(xFilial("TQF")+cCodPos)
						cLoja := TQF->TQF_LOJA
					EndIf
				EndIf

				If !Empty(cCNPJ) .And. (Empty(cLoja) .Or. Empty(cCodPos))
					DbSelectArea("TQF")
					DbSetOrder(2)
					If DbSeek(TQN->TQN_FILIAL+cCNPJ)

						If Empty(cCodPos)
							cCodPos := TQF->TQF_CODIGO
						EndIf

						If Empty(cLoja)
							cLoja   := TQF->TQF_LOJA
						EndIf

					EndIf
				EndIf
				DbSelectArea("TQF")
				DbSetOrder(1)
				If DbSeek(xFilial("TQF")+cCodPos+cLoja)
					If TQF->TQF_TIPPOS == '2'
						DbSelectArea("TQN")
						DbSkip()
						Loop
					EndIf
				EndIf

				/*/ Ponto de entrada que retorna True or False dependendo do retorno do filtro do código do combustível na tela
				de conciliação manual, criado com o intuito de tornar mais fácil do usuário identificar os abastecimentos em browse. /*/
				If lMNTA6501
					If !ExecBlock( "MNTA6501", .F., .F. )
						dbSelectArea( "TQN" )
						dbSkip()
						Loop
					EndIf
				EndIf

				RecLock((cAliTRB2), .T.)

				(cAliTRB2)->VLABA     := TQN->TQN_VALTOT
				(cAliTRB2)->VLUNIT    := TQN->TQN_VALUNI
				(cAliTRB2)->LITROS    := TQN->TQN_QUANT
				(cAliTRB2)->PLACA     := TQN->TQN_PLACA
				(cAliTRB2)->FROTA     := TQN->TQN_FROTA
				(cAliTRB2)->KM        := TQN->TQN_HODOM
				(cAliTRB2)->DTABAS    := TQN->TQN_DTABAS
				(cAliTRB2)->HRABAS    := TQN->TQN_HRABAS
				(cAliTRB2)->NF        := TQN->TQN_NOTFIS
				(cAliTRB2)->FILIAL    := TQN->TQN_FILIAL
				(cAliTRB2)->DTPGTO    := TQN->TQN_DTPGMT
				(cAliTRB2)->RECNO     := TQN->(RecNo())

				DbSelectArea("DA4")
				DbSetOrder(1)
				If DbSeek(TQN->TQN_FILIAL+TQN->TQN_CODMOT)
					(cAliTRB2)->DA4_NOME  := DA4->DA4_NOME
				EndIf

				DbSelectArea("TQF")
				DbSetOrder(1)
				If DbSeek(xFilial("TQF")+TQN->TQN_POSTO+TQN->TQN_LOJA)
					(cAliTRB2)->POSTO := TQF->TQF_NREDUZ
				EndIf

				DbSelectArea("TQH")
				DbSetOrder(1)
				DbSeek(xFilial("TQH")+cCodPos+cLoja+cComb)
				cComb  := TQH->TQH_CODCOM
				dDtTQH := TQH->TQH_DTNEG
				Do While !Eof() .and. xFilial("TQH") == TQH->TQH_FILIAL .and.;
				TQH->TQH_CODPOS == cCodPos .and.;
				TQH->TQH_LOJA   == cLoja   .and.;
				TQH->TQH_CODCOM == cComb

					If dDtTQH <= TQH->TQH_DTNEG
						If TQH->TQH_DTNEG <= (cAliTRB2)->DTABAS
							(cAliTRB2)->VLNEG := TQH->TQH_PRENEG
							dDtTQH := TQH->TQH_DTNEG
						EndIf
					EndIf

					DbSelectArea("TQH")
					DbSkip()
					Loop
				EndDo
				MsUnLock(cAliTRB2)
			EndIf

			DbselectArea("TQN")
			DbSkip()
		End
	EndIf

	DbSelectArea(cAliTRB2)
	If Reccount() == 0
		Help(" ",1,"NGATENCAO",,STR0047+Chr(13)+Chr(10)+STR0048,3,1) //"Nao existe dados para montar a tela de Conciliação"###" Manual."
	Else
		MNA650MO(nQtLitros, nValTot)
	EndIf

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA650MO  ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³ 06/02/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Mostra a tela de conciliaçao de NF's                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA650                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

function MNA650MO(nQtLitros, nValTot)

	Local aArea     := GetArea()
	Local nX
	Local aMark    	:= {}
	Local lInverte 	:= .F.
	Local cMarca   	:= GetMark()

	Private nTotalI            := 000.00
	Private nTotalIL           := 000.00
	Private oTotalC,  nTotalC  := 000.00
	Private oTotalCL, nTotalCL := 000.00
	Private oSelec,   nSelec   := 000.000
	Private oMenu

	aAdd(aMark,{"ASSI"      ,NIL," "     ,    })
	aAdd(aMark,{"NF"        ,NIL,STR0013 ,"@!"})                //"Nota Fiscal     "
	aAdd(aMark,{"VLABA"     ,NIL,STR0014 ,"@E 99,999,999,999.999"}) //"Valor Abastecido"
	aAdd(aMark,{"VLUNIT"    ,NIL,STR0015 ,'@E 99,999.'+Replicate('9',TAMSX3("TQN_VALUNI")[2]) })     //"Valor Unitario"
	aAdd(aMark,{"VLNEG"     ,NIL,STR0016 ,'@E 99,999.'+Replicate('9',TAMSX3("TQH_PRENEG")[2]) })     //"Valor Negociado"
	aAdd(aMark,{"LITROS"    ,NIL,STR0017 ,"@E 99,999.999"})     //"Litros"
	aAdd(aMark,{"PLACA"     ,NIL,STR0018 ,"@!"})                //"Placa"
	aAdd(aMark,{"FROTA"     ,NIL,STR0054 ,"@!"})                //"Frota"
	aAdd(aMark,{"DTABAS"    ,NIL,STR0019 ,"99/99/99"})          //"Dt.Abastec."
	aAdd(aMark,{"HRABAS"    ,NIL,STR0020 ,"99:99"})             //"Hr.Abastec."
	aAdd(aMark,{"KM"        ,NIL,STR0021 ,"@E 999,999,999"})    //"KM"
	aAdd(aMark,{"DTPGTO"    ,NIL,STR0045 ,"99/99/99"})          //"Data Pagamento"
	aAdd(aMark,{"DA4_NOME"  ,NIL,STR0022 ,"@!"})                //"Motorista"
	aAdd(aMark,{"POSTO"     ,NIL,STR0046 ,"@!"})                //"Nome Fantasia"

	nTotalI  := nValTot
	nTotalIL := nQtLitros

	DbSelectarea(cAliTRB2)
	DbGotop()
	Define DIALOG oDlg1 FROM 7,10 TO 37,135 TITLE STR0023  //"Conciliaçao de NFs Manual"
	@ 04,010 Button STR0024  Of oDlg1 Size 40,09 Pixel Action (MNA650PE())  //"Pesquisar"
	@ 04,055 Button STR0025 Of oDlg1 Size 40,09 Pixel Action (MNA650VI())  //"Visualizar"

	oMark := MsSelect():New((cAliTRB2),"ASSI",,aMark,@lInverte,@cMarca,{16,8,195,490})
	oMark:oBrowse:lHasMark = .t.
	oMark:oBrowse:lCanAllMark := .t.
	oMark:bMark := { || MNA650MA(cMarca) }
	oMark:oBrowse:bAllMark := { || Processa({ || MNA650VE(cMarca) }) }

	@ 200,010 Say STR0026      Size 55,10 Of oDlg1 Pixel color CLR_BLUE  //"Total Informado"
	@ 200,050 Say nTotalI                Size 40,10 Of oDlg1 Pixel Picture "@E 99,999,999,999.999"

	@ 200,100 Say STR0017               Size 55,10 Of oDlg1 Pixel color CLR_BLUE  //"Litros"
	@ 200,140 Say nTotalIL               Size 40,10 Of oDlg1 Pixel Picture "@E 999,999,999.999"

	@ 200,190 Say STR0027      Size 55,10 Of oDlg1 Pixel color CLR_BLUE  //"Total Calculado"
	@ 200,230 Say oTotalC   Var nTotalC  Size 50,10 Of oDlg1 Pixel Picture "@E 99,999,999,999.999"

	@ 200,300 Say STR0017               Size 55,10 Of oDlg1 Pixel color CLR_BLUE  //"Litros"
	@ 200,320 Say oTotalCL  Var nTotalCL Size 40,10 Of oDlg1 Pixel Picture "@E 999,999,999.999"

	@ 200,360 Say STR0028   Size 55,10 Of oDlg1 Pixel Color CLR_BLUE  //"Items Selecionados"
	@ 200,410 Say oSelec    Var nSelec   Size 40,10 Of oDlg1 Pixel Picture "@E 999,999"

	@ 200,450 Button STR0029         Size 40,09 Of oDlg1 Pixel Action (MNA650CN(nTotalI, nTotalIL, nTotalC, nTotalCL )) //"Conciliar"

	@ 210,450 Button STR0030          Size 40,09 Of oDlg1 Pixel Action (MNA650CR())  //"Corrigir"

	/*---------------------------------------------------------------
	|Ponto de entrada que disponibiliza o botão 'Ações Relacionadas'
	|com a opção de adição de novos botões para implemetação específica.
	/----------------------------------------------------------------*/
	If ExistBlock( "MNTA6502" )

		aUsrBut := ExecBlock( "MNTA6502",.F.,.F. ) //Ponto de entrada que retorna um array com as opções que serão inseridas no menu

		If ValType(aUsrBut) == "A"
			oMenu := TMenu():New(0,0,500,500,.T.)
			// Cria botão que será usado no Menu
			oTButton1 := TButton():New( 04,100,"Ações Relacionadas",oDlg1,,60,09,,,.F.,.T.,.F.,,.F.,,,.F. )

			// Define botão no Menu
			oTButton1:SetPopupMenu(oMenu)

			// Adiciona itens no Menu
			For nX := 1 To Len( aUsrBut )
				fFunction	:= aUsrBut[nX][2]
				oTMenuIte1	:= TMenuItem():New(oDlg1,aUsrBut[nX][1],,,,{||&( fFunction )},,,,,,,,,.T.)
				oMenu:Add(oTMenuIte1)
			Next nX

		EndIf
	EndIf

	NGPOPUP(asMenu,@oMenu)
	oDlg1:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg1)}

	ACTIVATE DIALOG oDlg1 Centered

	RestArea(aArea)

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA650MA  ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³ 06/02/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Marca a linha e atualiza os dados no rodape da tela         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA650                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MNA650MA(cMarca)
	Local cFieldMarca := "ASSI"

	If IsMark(cFieldMarca,cMarca,.f.)
		nVlAba  := (cAliTRB2)->VLABA
		nLitros := (cAliTRB2)->LITROS
		nRecno  := Recno()
		nSelec  := 0

		DbSelectArea(cAliTRB2)
		DbGotop()
		Do While !Eof()
			If !Empty( (cAliTRB2)->ASSI )
				nSelec ++
			EndIf
			Dbskip()
		EndDo
		If nSelec > 1
			DbSelectArea(cAliTRB2)
			If DbSeek( cValToChar(nVlAba) )
				RecLock((cAliTRB2),.f.)
				(cAliTRB2)->ASSI := Space(02)
				MsUnLock(cAliTRB2)
			EndIf
		EndIf

		DbGoTo(nRecno)
		nTotalC  += nVlAba
		nTotalCL += nLitros
		oMark:oBrowse:Refresh()
		oTotalC:Refresh()
		oTotalCL:Refresh()
		oSelec:Refresh()
	Else
		nSelec--
		nTotalC  -= (cAliTRB2)->VLABA
		nTotalCL -= (cAliTRB2)->LITROS
		oTotalC:Refresh()
		oTotalCL:Refresh()
		oSelec:Refresh()
		oMark:oBrowse:Refresh()
	EndIf
Return .t.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA650VI  ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³ 06/02/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Visualiza consulta                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA650                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

function MNA650VI()

	Local aArea := (cAliTRB2)->( GetArea() )

	DbSelectArea('TQN')
	DbGoto( (cAliTRB2)->RECNO )
	NGCAD01( 'TQN', Recno(), 1 )

	RestArea( aArea )

return .t.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNA650CN
Conciliaçao das NF's

@param nTotalI  , Numérico, Numero total.
@param nTotalIL , Numérico, Numero total.
@param nTotalC  , Numérico, Número total de litros.
@param nTotalCL , Numérico, Número total de litros.

@author Evaldo Cevinscki Jr.
@since 06/02/06
@version P12

@return .T.
/*/
//------------------------------------------------------------------------------
Function MNA650CN(nTotalI, nTotalIL, nTotalC, nTotalCL)

	Local aArea     := FWGetArea()
	Local aItC7     := {}
    Local aSC7      := {}
    Local nConcilia := 0
    Local nLimMaxQ  := nTotalI + GetMv("MV_NGTOLVL")
    Local nLimMinQ  := nTotalI - GetMv("MV_NGTOLVL")
    Local nLimMaxL  := nTotalIL + GetMv("MV_NGTOLVL")
    Local nLimMinL  := nTotalIL - GetMv("MV_NGTOLVL")
    Local x, y, z
    Local lRet      := .T.
    Local cItemCta  := Space( FWTamSX3( 'T9_ITEMCTA' )[1] )
	Local cCCusto   := Space( FWTamSX3( 'TQN_CCUSTO' )[1] )
    Local cNGAGLPC  := SuperGetMv( 'MV_NGAGLPC', .F., '1' )
	Local cNGMNTFI  := SuperGetMv( 'MV_NGMNTFI', .F., 'N' )
    

    If (nTotalC > nLimMaxQ .Or. nTotalC < nLimMinQ) .Or. (nTotalCL > nLimMaxL .Or. nTotalCL < nLimMinL)
        HELP(" ",1,STR0010,,STR0049,3,1) //"ATENÇÃO"###"O Total Calculado esta diferente do Total Informado!"
        Return .F.
    EndIf

	ProcRegua(0)
	dbSelectArea(cAliTRB2)
	dbGoTop()
	While !EoF()

		If Empty((cAliTRB2)->ASSI)
			dbSelectArea(cAliTRB2)
			dbSkip()
			Loop
		EndIf

		dbSelectArea("TQN")
		dbSetOrder(1)
		If dbSeek((cAliTRB2)->FILIAL+(cAliTRB2)->FROTA+DtoS((cAliTRB2)->DTABAS)+(cAliTRB2)->HRABAS)

			If Empty( MV_PAR14 )
				cCCusto := TQN->TQN_CCUSTO
			Else
				cCCusto := MV_PAR14
			EndIf

			If Empty( MV_PAR15 )

				dbSelectArea( 'ST9' )
				dbSetOrder( 1 )
				If msSeek( FWxFilial( 'ST9' ) + (cAliTRB2)->FROTA )
				
					cItemCta := ST9->T9_ITEMCTA

				EndIf
				
			Else
				
				cItemCta := MV_PAR15
				
			EndIf

			If cNGMNTFI == 'S' .Or. cNGMNTFI == 'P'
				
				dbSelectArea("SB1")
				dbSetOrder(1)
				dbSeek( xFilial( "SB1" ) + MV_PAR10 )
				aAdd( aSC7, { SB1->B1_COD, SB1->B1_UM, TQN->TQN_QUANT, Round( TQN->TQN_VALUNI, FWTamSX3( 'C7_PRECO' )[2] ),;
					TQN->TQN_POSTO, TQN->TQN_LOJA, MV_PAR11, TQN->TQN_FILIAL, cCCusto, TQN->( RecNo() ), cItemCta, MV_PAR13 } )

			EndIf

		EndIf

		dbSelectArea(cAliTRB2)
		dbSkip()

	End

	If cNGMNTFI == 'S' .Or. cNGMNTFI == 'P'

		aSort( aSC7,,, { |x,y| x[5]+X[6] < y[5]+Y[6] } )

		For x := 1 To Len(aSC7)
			//Aglutina Pedido de Compras  2 = Não
			If cNGAGLPC == "2"
				aAdd(aItC7,{aSC7[x][1],aSC7[x][2],aSC7[x][3],aSC7[x][4],aSC7[x][9],{aSC7[x][10]},aSC7[x][11],aSC7[x][12]})
			Else //Aglutina Pedido de Compras 1 = Sim
				If MV_PAR12 == 1 //Aglutina por C.C.
					nPos := aScan( aItC7, { |y| y[1]+y[5]+AllTrim(Str(y[4])) == aSC7[x][1]+aSC7[x][9]+AllTrim(Str(aSC7[x][4])) } )
					If nPos > 0
						aItC7[nPos][3] += aSC7[x][3]
						aAdd(aItC7[nPos][6],aSC7[x][10])
					Else
						aAdd(aItC7,{aSC7[x][1],aSC7[x][2],aSC7[x][3],aSC7[x][4],aSC7[x][9],{aSC7[x][10]},aSC7[x][11],aSC7[x][12]})
					EndIf
				Else
					
					//Não Aglutina por C.C.
					nPos := aScan( aItC7, { |y| y[1]+AllTrim(Str(y[4])) == aSC7[x][1]+AllTrim(Str(aSC7[x][4])) } )
					
					If nPos > 0
						aItC7[nPos][3] += aSC7[x][3]
						aAdd(aItC7[nPos][6],aSC7[x][10])
					Else

						aAdd( aItC7, { aSC7[x,1], aSC7[x,2], aSC7[x,3], aSC7[x,4], Nil, { aSC7[x,10] }, aSC7[x,11], aSC7[x,12] } )
					
					EndIf

				EndIf
			EndIf
		Next x

		If Len(aSC7) > 0
			If Empty(MV_PAR03) .And. !Empty(MV_PAR05)
				If NGIFDBSEEK('TQF',MV_PAR05,2)
					MV_PAR03 := TQF->TQF_CODIGO
					MV_PAR04 := TQF->TQF_LOJA
				EndIf
			EndIf
			//Aglutina Pedido de Compras  2 = Não
			If cNGAGLPC == "2"
				For y := 1 To Len(aItC7)
					MV_PAR03 := TQF->TQF_CODIGO
					MV_PAR04 := TQF->TQF_LOJA
				    lRet := NgGeraSC7(MV_PAR03,MV_PAR04,MV_PAR11,aSC7[1][8],{aItC7[y]},"MNTA650")
					ConfirmSX8()

					If lRet
						For z := 1 To Len(aItC7[y][6])
							
							dbSelectArea( 'TQN' )
							msGoTo( aItC7[Y,6,Z] )

							dbSelectArea(cAliTRB2)
							dbSetOrder(3)
							If dbSeek(TQN->TQN_FILIAL+TQN->TQN_FROTA+DTOS(TQN->TQN_DTABAS)+TQN->TQN_HRABAS)
								
								RecLock( 'TQN', .F. )

								TQN->TQN_DTCON  := dDATABASE
								TQN->TQN_NOTFIS := (cAliTRB2)->NF
								TQN->TQN_NUMSC7 := SC7->C7_NUM
								TQN->TQN_ITEMSC := SC7->C7_ITEM
					
								//Adicionada condição para utilizar o que for informado no MV_PAR12 (CC)
								//Caso não seja preenchido ele mantém o conteúdo da TQN
								If !Empty((cAliTRB2)->CCUSTO)
									TQN->TQN_CCUSTO := (cAliTRB2)->CCUSTO
								EndIf
								If !Empty((cAliTRB2)->CTRAB)
									TQN->TQN_CENTRA := (cAliTRB2)->CTRAB
								EndIf
								MsUnLock()
								nConcilia++
							EndIf
						Next z
					EndIf
				Next y
			Else
				//Aglutina Pedido de Compras  1 = Sim
			    lRet := NgGeraSC7(MV_PAR03,MV_PAR04,MV_PAR11,aSC7[1][8],aItC7,"MNTA650")
				ConfirmSX8()

				If lRet
					
					For y := 1 To Len(aItC7)
						
						For z := 1 To Len(aItC7[y][6])
							
							dbSelectArea("TQN")
							dbSetOrder(1)
							dbGoTo(aItC7[y][6][z])

							dbSelectArea(cAliTRB2)
							dbSetOrder(3)
							If dbSeek(TQN->TQN_FILIAL+TQN->TQN_FROTA+DTOS(TQN->TQN_DTABAS)+TQN->TQN_HRABAS)
								
								RecLock( 'TQN', .F. )
								
									TQN->TQN_DTCON  := dDATABASE
									TQN->TQN_NOTFIS := (cAliTRB2)->NF
									TQN->TQN_NUMSC7 := SC7->C7_NUM
									TQN->TQN_ITEMSC := fGetItSC7( SC7->C7_FILIAL, SC7->C7_NUM, aItC7[Y,1], aItC7[Y,5], aItC7[Y,4] )
						
									//Adicionada condição para utilizar o que for informado no MV_PAR12 (CC)
									//Caso não seja preenchido ele mantém o conteúdo da TQN
									If !Empty((cAliTRB2)->CCUSTO)
										TQN->TQN_CCUSTO := (cAliTRB2)->CCUSTO
									EndIf

									If !Empty((cAliTRB2)->CTRAB)
										TQN->TQN_CENTRA := (cAliTRB2)->CTRAB
									EndIf

								MsUnLock()

								nConcilia++

							EndIf

						Next z

					Next y

				EndIf
			EndIf
		EndIf
	Else
		//Se não houver integração apenas confirma a conciliação
		dbSelectArea(cAliTRB2)
		dbGoTop()
		Do while !EoF()

			If Empty((cAliTRB2)->ASSI)
				dbSelectArea(cAliTRB2)
				dbSkip()
				Loop
			EndIf

			dbSelectArea("TQN")
			dbSetOrder(1)
			If dbSeek((cAliTRB2)->FILIAL+(cAliTRB2)->FROTA+DtoS((cAliTRB2)->DTABAS)+(cAliTRB2)->HRABAS)
				RecLock("TQN",.F.)
				TQN->TQN_DTCON  := dDATABASE
				TQN->TQN_NOTFIS := (cAliTRB2)->NF
				MsUnLock()
				nConcilia++
			EndIf

			dbSelectArea(cAliTRB2)
			dbSkip()
		End
	EndIf

	MsgInfo(STR0050+AllTrim(STR(nConcilia))+STR0051) //"Conciliação de "###" Nota(´s) Fiscal(´s) Concluída(´s) com Sucesso!"

	FWRestArea( aArea )
	
	oDlg1:End()

	/*--------------------------------------------------+
	| Desaloca consumo de memória p/ melhor performance |
	+--------------------------------------------------*/
	aSize( aArea, 0 )
	aArea := Nil

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA650CR  ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³ 06/02/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Abre a tela de correçao de dados                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA650                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

function MNA650CR()

	local nCrVlUnit   := 000.00
	local cNF         := " "
	local oPnlPai
	Private nCoLitros := 000000
	private oDlg2
	private oMenu

	nCrVlUnit  := (cAliTRB2)->VLUNIT
	nCoLitros  := (cAliTRB2)->LITROS
	cNF        := (cAliTRB2)->NF

	Define DIALOG oDlg2 FROM 7,10 TO 18,91 TITLE STR0030  //"Corrigir"

	oPnlPai := TPanel():New(00,00,,oDlg2,,,,,,0,0,.F.,.F.)
	oPnlPai:Align := CONTROL_ALIGN_ALLCLIENT

	@  08,010 Say STR0017      Size 55,10 Of oPnlPai Pixel  //"Litros"
	@  08,050 MsGet nCoLitros   Size 40,10 Of oPnlPai Pixel Picture "@E 99,999.999" HASBUTTON Valid NGMAQUEZERO(nCoLitros)

	@  26,010 Say STR0033 Size 55,10 Of oPnlPai Pixel  //"Nota Fiscal"
	@  26,050 MsGet cNF         Size 40,10 Of oPnlPai Pixel Picture "@!"

	NGPOPUP(asMenu,@oMenu)
	oPnlPai:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oPnlPai)}

	ACTIVATE DIALOG oDlg2 On Init EnchoiceBar(oDlg2,{||nOpcao:=1,If(!MNA650CO(nCrVlUnit,nCoLitros,cNF),;
	nOpcao := 0,oDlg2:End())},{|| oDlg2:End()}) Centered

return .t.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA650CO  ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³ 06/02/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Corrige os dados nas tabelas                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA650                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MNA650CO(nCrVlUnit,nCrLitros,cNF)
	Local aArea := GetArea()

	DbSelectArea("TQN")
	DbSetOrder(1)
	If DbSeek((cAliTRB2)->FILIAL+(cAliTRB2)->FROTA+DtoS((cAliTRB2)->DTABAS)+(cAliTRB2)->HRABAS)

		If !Empty( (cAliTRB2)->ASSI )
			nTotalC  -= (cAliTRB2)->VLABA
			nTotalCL -= (cAliTRB2)->LITROS
		EndIf

		RecLock("TQN",.F.)
		TQN->TQN_QUANT  := nCrLitros
		TQN->TQN_VALTOT := nCrVlUnit * nCrLitros
		TQN->TQN_NOTFIS := cNF
		TQN->TQN_USUARI := SUBSTR(cUSUARIO,7,15)
		MsUnLock()

		RestArea(aArea)
		RecLock((cAliTRB2),.F.)
		(cAliTRB2)->LITROS := nCrLitros
		(cAliTRB2)->NF     := cNF
		(cAliTRB2)->VLABA  := nCrVlUnit * nCrLitros
		(cAliTRB2)->( MsUnLock() )

		If !Empty( (cAliTRB2)->ASSI )
			nTotalC  += (cAliTRB2)->VLABA
			nTotalCL += (cAliTRB2)->LITROS
		EndIf

	EndIf

	oMark:oBrowse:Refresh()
	oTotalC:Refresh()
	oTotalCL:Refresh()
	oSelec:Refresh()

	oDlg2:End()

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA650PE  ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³ 06/02/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Pesquisa especifica de NF                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA650                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNA650PE()

	Local dPeDtAbas := StoD("")
	Private oDlg3
	Private oMenu

	Define Dialog oDlg3 From 7,10 To 12,40 Title STR0034  //"Pesquisa"

	@  05, 05 Say    STR0035  Size 55,10 Of oDlg3 Pixel  //"Data Abastecimento"
	@  04, 60 MsGet dPeDtAbas Size 45,10 Picture '99/99/99' Of oDlg3 Pixel HasButton
	@  20, 60 Button STR0036  Size 40,09 Of oDlg3 Pixel Action (MNA650POS( DtoC(dPeDtAbas) )) //"OK"

	NGPOPUP(asMenu,@oMenu)
	oDlg3:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg3)}

	Activate Dialog oDlg3

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNA650POS ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³ 06/02/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Posiciona no registro                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA650                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MNA650POS(dPeDtAbas)

	dPeDtAbas := CtoD(dPeDtAbas)

	If !empty(dPeDtAbas)

		DbSelectArea(cAliTRB2)
		dbsetorder(2)
		DbSeek(dPeDtAbas,.T.)

	ENDIF

	oDlg3:End()

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNT650CGC ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³ 13/02/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Valida o CNPJ na pergunta                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA650                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MNT650CGC()

	local lRET := .T.

	If !Empty(Mv_Par05)
		If	!Cgc(Mv_Par05)
			lRET := .F.
		EndIf
	EndIf

Return lRET
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNT650POS ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³ 21/03/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Limpa o campo CNPJ                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA650                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNT650POS()

	If Empty(Mv_Par05) .And. Empty(Mv_Par03)
		HELP(" ",1,STR0010,,STR0011,3,1)  //"ATENÇÃO"###"Preencha o Codigo do Posto ou o CNPJ."
		Return .F.
	EndIf

	dbSelectArea("TQF")
	dbSetOrder(2) //TQF_FILIAL+TQF_CNPJ
	If dbSeek(xFilial("TQF")+MV_PAR05)
		If TQF->TQF_TIPPOS == "2"
			MsgInfo(STR0087) //"Posto Externo selecionado, informe um posto interno."
			Return .F.
		EndIf
	EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNA650VE   ³ Autor ³Thiago Olis Machado   ³ Data ³05/05/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Inverte a marcacao                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MNTA650                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNA650VE(cMarca)

	Dbselectarea(cAliTRB2)
	DbGotop()
	Procregua(LastRec())
	While !Eof()
		IncProc(STR0052+(cAliTRB2)->NF) //"Marcando e/ou Desmarcando Nota Fiscal "
		RecLock((cAliTRB2),.F.)
		If (cAliTRB2)->ASSI = "  "
			(cAliTRB2)->ASSI := cMarca
			nSelec ++
			nTotalC  += (cAliTRB2)->VLABA
			nTotalCL += (cAliTRB2)->LITROS
			oMark:oBrowse:Refresh()
			oTotalC:Refresh()
			oTotalCL:Refresh()
			oSelec:Refresh()
		Else
			nSelec --
			(cAliTRB2)->ASSI := "  "
			nTotalC  -= (cAliTRB2)->VLABA
			nTotalCL -= (cAliTRB2)->LITROS
			oTotalC:Refresh()
			oTotalCL:Refresh()
			oSelec:Refresh()
		EndIf

		(cAliTRB2)->( MsUnLock() )
		DbSkip()
	End
	DbGotop()
Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNT650LJ  ³ Autor ³Marcos Wagner Junior   ³ Data ³ 03/08/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Valida o parametro de Loja                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Mntc650 	                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNT650LJ()

	Local lRet := .T.

	dbSelectArea("TQF")
	dbSetOrder(1)
	If Empty(MV_PAR03) .And. !Empty(MV_PAR04)
		MsgStop(STR0038)//"Informe o Codigo do Posto"
		MV_PAR04 := Space(TamSX3("TQN_POSTO")[1])
		lRet := .F.
	ElseIf !Empty(MV_PAR03) .And. Empty(MV_PAR04)
		MsgStop(STR0039)//"Informe o Codigo da Loja"
		lRet := .F.
	ElseIf !Empty(MV_PAR03) .And. !Empty(MV_PAR04)
		If !ExistCpo("TQF",MV_PAR03+MV_PAR04)
			lRet := .F.
		ElseIf dbSeek(xFilial("TQF")+MV_PAR03+MV_PAR04)
			If TQF->TQF_TIPPOS == "2"
				MsgInfo(STR0087) //"Posto Interno selecionado, informe um posto externo."
				lRet := .F.
			EndIf
		EndIf
	EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNT650CNPJ³ Autor ³Marcos Wagner Junior   ³ Data ³ 06/08/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Busca o CNPJ do Posto/Loja informado                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Mntc650 	                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNT650CNPJ()

	If Empty(MV_PAR03)
		MV_PAR04 := Space(TamSX3("TQN_LOJA")[1])
		MV_PAR05 := Space(TamSX3("TQN_CNPJ")[1])
	Else
		dbSelectArea('TQF')
		dbSetOrder(1) //TQF_FILIAL+TQF_CODIGO+TQF_LOJA
		If dbSeek( xFilial('TQF') + MV_PAR03 + MV_PAR04 )
			MV_PAR04 := TQF->TQF_LOJA
			MV_PAR05 := TQF->TQF_CNPJ
		EndIf
	EndIf

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNT650DT
Valida o parametro ate data

@param nPar, Numérico, Determina o campo que está sendo validado.

@author Soraia de Carvalho
@since 25/07/06
@version P12

@return .T.
/*/
//------------------------------------------------------------------------------
Function MNT650DT(nPar)

	Local lRet := .T.

	If nPar == 1
		If MV_PAR02 < MV_PAR01
			MsgStop(STR0088) //"Data final não pode ser inferior à data inicial."
			lRet := .F.
		EndIf
		If MV_PAR02 > dDataBase .And. lRet
			MsgStop(STR0089) //"Até Data não pode ser maior que a data atual."
			lRet := .F.
		EndIf
	ElseIf nPar == 2
		If MV_PAR07 < MV_PAR02
			MsgStop(STR0090) //"Data de emissão nf não pode ser menor que maior data de abastecimento(Até Data)!"
			lRet := .F.
		EndIf
		If MV_PAR07 > dDataBase .And. lRet
			MsgStop(STR0091) //"Data de emissão nf não pode ser maior que a data atual!"
			lRet := .F.
		EndIf
	ElseIf nPar == 3
		If MV_PAR10 < MV_PAR07
			MsgStop(STR0092) //"Data de pagamento não pode ser menor que a data de emissão da nota fiscal!"
			lRet := .F.
		EndIf
	EndIf

Return lRet

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTA650PTE
Montagem da primeira tela.
@type function

@author Diego de Oliveira
@since 14/06/18
@version P12

@return .T.
/*/
//------------------------------------------------------------------------------
Static Function MNTA650PTE()

	//Campos que complementam a tela de Pedidos de Compra
	Local aFixe   := {{STR0072,"C7_NUM"},{STR0097,"C7_EMISSAO"},{STR0069,"C7_FORNECE"}} //"Número PC"###"Data Emissão"###"Fornecedor"

	//Bloco de confirmação da tela
	Local aCoors := FWGetDialogSize( oMainWnd )
	Local oDlgPrinc, oFWLayer

	Private oBrowseUp,oRelacSC7
	Private oBrowseInf, oBrowse

	Define MsDialog oDlgPrinc Title cCadastro From aCoors[1],aCoors[2] To aCoors[3],aCoors[4] OF oMainWnd Pixel  //"Conciliaçao de NF's Manual"
	oDlgPrinc:lEscClose  := .F.

	// Cria o conteiner onde serão colocados os browses
	oFWLayer := FWLayer():New()
	oFWLayer:Init( oDlgPrinc, .F., .T. )

	// Define Painel Superior
	oFWLayer:AddLine( 'UP', 55, .F. )
	oFWLayer:AddCollumn( 'ALL', 100, .T., 'UP' )
	oPanelUp := oFWLayer:GetColPanel( 'ALL', 'UP' )

	// FWmBrowse Superior
	oBrowseUp:= FWMBrowse():New()
	oBrowseUp:SetOwner( oPanelUp )
	oBrowseUp:SetDescription( STR0084 ) //"Pedidos de Compra"
	oBrowseUp:SetMenuDef( 'MNTA650' )
	oBrowseUp:SetSeeAll( .F. )
	oBrowseUp:SetDoubleClick({|x| SetAltera(.F.), SetInclui(.F.),SC7->C7_NUM, Processa( { |lEnd|  MATA121("SC7",RecNo(),2) }) })
	oBrowseUp:SetFilterDefault("C7_ORIGEM == 'MNTA650' .And. C7_QUJE==0 .And. C7_QTDACLA==0")
	oBrowseUp:SetFields( aFixe )
	oBrowseUp:SetAlias( 'SC7' )
	oBrowseUp:SetProfileID( '1' )
	oBrowseUp:DisableDetails()
	oBrowseUp:SetChgAll(.F.)
	oBrowseUp:Activate()

	// Painel Inferior
	oFWLayer:AddLine( 'DOWN', 45, .F. )
	oFWLayer:AddCollumn( 'LEFT' ,  100, .T., 'DOWN' )
	oPanel := oFWLayer:GetColPanel( 'LEFT' , 'DOWN' )

	oBrowse:= FWMBrowse():New()
	oBrowse:SetOwner( oPanel )
	oBrowse:SetDescription( "Abastecimentos" ) //"Abastecimentos"
	oBrowse:SetMenuDef('')
	oBrowse:DisableDetails()
	oBrowse:SetDoubleClick({|x| SetAltera(.F.), SetInclui(.F.),TQN->TQN_NUMSC7, Processa( { |lEnd| NGCAD01("TQN",Recno(),2)} ) })
	oBrowse:SetAlias( 'TQN' )
	oBrowse:SetFilterDefault( "TQN->TQN_NUMSC7 != ' ' " )
	oBrowse:SetProfileID( '2' )
	oBrowse:SetChgAll(.F.)
	oBrowse:Activate()

	// Relacionamento entre os Painéis
	oRelacSC7:= FWBrwRelation():New()
	oRelacSC7:AddRelation( oBrowseUp, oBrowse, { { 'TQN_FILIAL', 'FWxFilial( "TQN" )' }, { 'TQN_NUMSC7', 'C7_NUM' }, { 'TQN_ITEMSC', 'C7_ITEM' } } )
	oRelacSC7:Activate()

	Activate MsDialog oDlgPrinc Center

Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} fChangeFil
Função de troca de linha.

@author Diego de Oliveira
@since 22/05/18
@version P12

@return Nil.
/*/
//------------------------------------------------------------------------------
Static Function fChangeFil()

	oBrowseInf:SetFilter( "TQN->TQN_NUMSC7", SC7->C7_NUM, SC7->C7_NUM )

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTA650STE
Montagem da segunda tela.
@type function

@author Diego de Oliveira
@since 14/06/18
@version P12

@return .T.
/*/
//------------------------------------------------------------------------------
Function MNTA650STE()

	//Variáveis do Pergunte
	Local cPerg 	  := PadR("MNA650", Len(Posicione("SX1", 1, "MNA650", "X1_GRUPO")))
	Local dDeData   := StoD("")
	Local dAteData  := StoD("")
	Local cCodPos   := Space(TamSX3("TQN_POSTO")[1])
	Local cLoja     := Space(TamSX3("TQN_LOJA")[1])
	Local cCNPJ     := Space(TamSX3("TQN_CNPJ")[1])
	Local nQtLitros := 000000

	//Variáveis números parte inferior da tela
	Private nTotalI  := 000.00
	Private nTotalIL := 000.00
	Private nTotalC  := 000.00
	Private nTotalCL := 000.00
	Private nSelec   := 000.000
	Private oTotalC
	Private oTotalCL
	Private oSelec

	//Variáveis de Marcação
	Private cMarca   	  := GetMark()

	If GetNewPar("MV_NGMNTFI","N") = "S" .Or. GetNewPar("MV_NGMNTFI","N") = "P"
		cPerg := PadR( "MNA650FI" , Len(Posicione("SX1", 1, "MNA650FI", "X1_GRUPO")) )
	Endif

	If Pergunte(cPerg,.T.)
		dDeData   := Mv_Par01
		dAteData  := Mv_Par02
		cCodPos   := Mv_Par03
		cLoja     := Mv_Par04
		cCNPJ     := Mv_Par05
		nQtLitros := Mv_Par06
		nValTot   := Mv_Par07
		nQtdNF    := Mv_Par08
		cFiliais  := Mv_Par09

		//Chama funcao com acompanhamento de barra de progressao
		Processa({|lEnd| MNA650INI(dDeData, dAteData, cCodPos, cLoja, cCNPJ, nQtLitros, nValTot)})
	EndIf

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} fBotExc
Função do botão de exclusão.

@author Diego de Oliveira
@since 29/05/18
@version P12

@return Nil.
/*/
//------------------------------------------------------------------------------
Function fBotExc()

	Local lRet       := .F.
	Local cAliasTQN := GetNextAlias()

	If MsgNoYes(STR0094+; //"Serão deletados os vínculos entre o(s) abastecimento(s) e o Pedido de Compras"
		Space(1)+SC7->C7_NUM+Space(1),STR0095) //". Deseja continuar?"###"Atenção"
		//Botão de Exclusão
		Processa( { |lEnd| lRet := NgGeraSC7(SC7->C7_FORNECE,SC7->C7_LOJA,;
				SC7->C7_COND,TQN->TQN_FILIAL,;
				{{SC7->C7_PRODUTO,; //Formato do Array [1] Produto
				SC7->C7_UM,;        //[2] Unidade de Medida
				SC7->C7_QUANT,;     //[3] Quantidade
				SC7->C7_PRECO}};    //[4] Preco Unitario
				,"MNTA650",5 ) } )

		If lRet
			dbselectArea("TQN")
			BeginSQL Alias cAliasTQN

				SELECT
					TQN_FROTA,
					TQN_DTABAS,
					TQN_HRABAS
				FROM %table:TQN% TQN
				WHERE TQN_AUTO = '2'
					AND TQN_DTCON = %exp:DtoS(SC7->C7_EMISSAO)%
					AND TQN_POSTO = %exp:SC7->C7_FORNECE%
					AND TQN_LOJA = %exp:SC7->C7_LOJA%
					AND TQN_FILIAL = %xFilial:TQN%
					AND TQN_NUMSC7 = %exp:SC7->C7_NUM%
					AND %NotDel%
					ORDER BY TQN_DTABAS
			EndSQL

			dbSelectArea("TQN")
			dbSetOrder(1) //TQN_FILIAL+TQN_FROTA+DTOS(TQN_DTABAS)+TQN_HRABAS
			dbSelectArea(cAliasTQN)
			While !EoF()
				If TQN->(dbSeek(xFilial("TQN") + (cAliasTQN)->TQN_FROTA + (cAliasTQN)->TQN_DTABAS + (cAliasTQN)->TQN_HRABAS ))
					//Limpa os dados do Pedido de Compra no registro de Abastecimento
					RecLock("TQN",.F.)
					TQN->TQN_DTCON  := StoD("")
					TQN->TQN_NUMSC7 := Space(Len(SC7->C7_NUM))
					TQN->TQN_ITEMSC := Space(Len(SC7->C7_ITEM))
					MsUnLock()
				EndIf

				dbSelectArea(cAliasTQN)
				dbSkip()
			EndDo
			( cAliasTQN )->( dbCloseArea() )
			MsgInfo(STR0084+Space(1)+AllTrim(SC7->C7_NUM)+Space(1)+STR0096) //"Pedido de compras "###" excluído."
		
		EndIf
	
	EndIf

	oBrowseUp:Refresh()
	oBrowseUp:ChangeTopBot(.T.)

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Definição do Menu.

@author Diego de Oliveira
@since 13/08/18

@return aRotina array com o Menu
/*/
//---------------------------------------------------------------------
Static Function MenuDef()

	Local aRotina := {}

		aRotina := {{'Excluir'		, "fBotExc()" 	 ,0,5},; //"Excluir"
					{'Incluir Conc.', "MNTA650STE()" ,0,3,0,.F.}}  //"'Incluir Conc.'

Return aRotina

//---------------------------------------------------------------------
/*/{Protheus.doc} fGetItSC7
Busca item do pedido de compra.
@type function

@author Alexandre Santos
@since 21/12/2022

@param cFilSC7, string , Filial do pedido de compra.
@param cNumSC7, string , Número do pedido de compra.
@param cProdut, string , Produto
@param cCCusto, string , Centro de Custos.
@param nVlUnit, integer, Valor unitário.

@return string, Item do pedido de compra.
/*/
//---------------------------------------------------------------------
Static Function fGetItSC7( cFilSC7, cNumSC7, cProdut, cCCusto, nVlUnit )

	Local cAlsSC7 := GetNextAlias()
	Local cRetSC7 := ''
	Local cWhere  := '%%'

	If MV_PAR12 == 1

		cWhere := '%AND SC7.C7_CC = ' + ValToSQL( cCCusto ) + '%'

	EndIf

	BeginSQL Alias cAlsSC7

		SELECT
			SC7.C7_ITEM
		FROM
			%table:SC7% SC7
		WHERE
			SC7.C7_FILIAL  = %exp:cFilSC7% AND
			SC7.C7_NUM     = %exp:cNumSC7% AND
			SC7.C7_PRODUTO = %exp:cProdut% AND
			SC7.C7_PRECO   = %exp:nVlUnit% AND
			SC7.%NotDel%
			%exp:cWhere%

	EndSQL

	If (cAlsSC7)->( !EoF() )

		cRetSC7 := (cAlsSC7)->C7_ITEM

	EndIf

	(cAlsSC7)->( dbCloseArea() )

Return cRetSC7
