#INCLUDE "MNTR270.CH"
#INCLUDE "PROTHEUS.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR270  ³ Autor ³ Deivys Joenck         ³ Data ³ 19/09/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Baixas de Bens                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR270()
	
	Local oReport
	Local aArea := GetArea()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Guarda conteudo e declara variaveis padroes ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aNGBEGINPRM := NGBEGINPRM(2)

	Private cTRB := GetNextAlias()
	Private vVETHORAS := {}
	Private lTIPMOD := NGIFDICIONA("SX3","T9_TIPMOD",2,.F.)

	SetKey( VK_F9, { | | NGVersao( "MNTR270" , 2 ) } )

	If FindFunction("TRepInUse") .And. TRepInUse()
		//-- Interface de impressao
		oReport := ReportDef()
		oReport:SetLandscape() //Default Paisagem
		oReport:PrintDialog()
	Else
		MNTR270R3()
	EndIf
	RestArea(aArea)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve variaveis armazenadas (NGRIGHTCLICK)                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³ Elisangela Costa      ³ Data ³ 21/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Define as secoes impressas no relatorio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR270                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function ReportDef()
	Local oReport
	Local oSection1
	Local oSection2
	Local oCell

	//LAYOUT
	/*        1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         0         1
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123
	________________________________________________________________________________________________________________________________________________________________________________________________________________________
	Bens Baixados
	________________________________________________________________________________________________________________________________________________________________________________________________________________________
	Familia  Descricao
	________________________________________________________________________________________________________________________________________________________________________________________________________________________
	XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

	Bem               Tp. Mod.   Descricao             Fabric. Nome Fabricante                           D.Compra  D.Baixa          Contador             Custo   Custo/Contador  Motivo Descricao
	________________________________________________________________________________________________________________________________________________________________________________________________________________________
	XXXXXXXXXXXXXXXX  XXXXXXXXXX XXXXXXXXXXXXXXXXXXXX  XXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99/99/99  99/99/99  999.999.999.999  9.999.999.999,99  999.999.999,999  XXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	XXXXXXXXXXXXXXXX  XXXXXXXXXX XXXXXXXXXXXXXXXXXXXX  XXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99/99/99  99/99/99  999.999.999.999  9.999.999.999,99  999.999.999,999  XXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

	Familia  Descricao
	________________________________________________________________________________________________________________________________________________________________________________________________________________________
	XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

	Bem               Tp. Mod.   Descricao             Fabric. Nome Fabricante                           D.Compra  D.Baixa          Contador             Custo   Custo/Contador  Motivo Descricao
	________________________________________________________________________________________________________________________________________________________________________________________________________________________
	XXXXXXXXXXXXXXXX  XXXXXXXXXX XXXXXXXXXXXXXXXXXXXX  XXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99/99/99  99/99/99  999.999.999.999  9.999.999.999,99  999.999.999,999  XXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	XXXXXXXXXXXXXXXX  XXXXXXXXXX XXXXXXXXXXXXXXXXXXXX  XXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99/99/99  99/99/99  999.999.999.999  9.999.999.999,99  999.999.999,999  XXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	*/

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao do componente de impressao                                      ³
	//³                                                                        ³
	//³TReport():New                                                           ³
	//³ExpC1 : Nome do relatorio                                               ³
	//³ExpC2 : Titulo                                                          ³
	//³ExpC3 : Pergunte                                                        ³
	//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
	//³ExpC5 : Descricao                                                       ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport := TReport():New("MNTR270",OemToAnsi(STR0001),"MNT27R",{|oReport| ReportPrint(oReport)},;
	STR0001+". "+STR0002+" "+STR0003)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ mv_par01     // De  Centro de Custo                          ³
	//³ mv_par02     // Ate Centro de Custo                          ³
	//³ mv_par03     // De  Centro Trabalho                          ³
	//³ mv_par04     // Ate Centro Trabalho                          ³
	//³ mv_par05     // De  Familia                                  ³
	//³ mv_par06     // Ate Familia                                  ³
	//³ mv_par07     // De  Data Inicio                              ³
	//³ mv_par08     // Ate Data Fim                                 ³
	//³ mv_par09     // Tipo de Custo (1=Medio, 2=Standard)          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Pergunte(oReport:uParam,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da secao utilizada pelo relatorio                               ³
	//³                                                                        ³
	//³TRSection():New                                                         ³
	//³ExpO1 : Objeto TReport que a secao pertence                             ³
	//³ExpC2 : Descricao da seçao                                              ³
	//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
	//³        sera considerada como principal para a seção.                   ³
	//³ExpA4 : Array com as Ordens do relatório                                ³
	//³ExpL5 : Carrega campos do SX3 como celulas                              ³
	//³        Default : False                                                 ³
	//³ExpL6 : Carrega ordens do Sindex                                        ³
	//³        Default : False                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da celulas da secao do relatorio                                ³
	//³                                                                        ³
	//³TRCell():New                                                            ³
	//³ExpO1 : Objeto TSection que a secao pertence                            ³
	//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
	//³ExpC3 : Nome da tabela de referencia da celula                          ³
	//³ExpC4 : Titulo da celula                                                ³
	//³        Default : X3Titulo()                                            ³
	//³ExpC5 : Picture                                                         ³
	//³        Default : X3_PICTURE                                            ³
	//³ExpC6 : Tamanho                                                         ³
	//³        Default : X3_TAMANHO                                            ³
	//³ExpL7 : Informe se o tamanho esta em pixel                              ³
	//³        Default : False                                                 ³
	//³ExpB8 : Bloco de código para impressao.                                 ³
	//³        Default : ExpC2                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSection1 := TRSection():New(oReport,STR0010 ,{(cTRB),"ST6"}) //Familia
	TRCell():New(oSection1,"(cTRB)->FAMILIA",(cTRB),STR0010 ,"@!",06) //"Familia"
	TRCell():New(oSection1,"T6_NOME"     ,"ST6",STR0011 ,/*Picture*/,/*Tamanho*/) //"Descrição"
	TRPosition():New(oSection1,"ST6",1,{|| xFilial("ST6") + (cTRB)->FAMILIA})

	oSection2 := TRSection():New(oSection1,STR0019,{(cTRB),"TQR","ST7","TPJ"}) //"Baixas"
	oCell := TRCell():New(oSection2,"(cTRB)->CODBEM" ,(cTRB),STR0012,"@!",16,/*lPixel*/,/*{|| code-block de impressao }*/) //"Bem"
	If lTIPMOD
		oCell := TRCell():New(oSection2,"(cTRB)->TIPMOD" ,(cTRB),STR0020,"@!",10,/*lPixel*/,/*{|| code-block de impressao }*/) //"Modelo"
		oCell := TRCell():New(oSection2,"TQR_DESMOD"  ,"TQR",STR0011,/*Picture*/,20,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Descrição"
	EndIf
	oCell := TRCell():New(oSection2,"(cTRB)->FABRICA",(cTRB),STR0021,"@!",06,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Fabricante"
	oCell := TRCell():New(oSection2,"T7_NOME"     ,"ST7",STR0011,/*Picture*/,40,/*lPixel*/,/*{|| code-block de impressao }*/) //"Descrição"
	oCell := TRCell():New(oSection2,"(cTRB)->DTCOMPR",(cTRB),STR0013,"99/99/9999",12,/*lPixel*/,/*{|| code-block de impressao }*/) //"Data Compra"
	oCell := TRCell():New(oSection2,"(cTRB)->DTBAIXA",(cTRB),STR0014,"99/99/9999",12,/*lPixel*/,/*{|| code-block de impressao }*/) //"Data Baixa"
	oCell := TRCell():New(oSection2,"(cTRB)->CONTADO",(cTRB),STR0015,"@E 999,999,999,999",14,/*lPixel*/,/*{|| code-block de impressao }*/) //"Contador"
	oCell := TRCell():New(oSection2,"(cTRB)->VLRCUST",(cTRB),STR0016,"@E 9,999,999,999.99",14,/*lPixel*/,/*{|| code-block de impressao }*/) //"Custo"
	oCell := TRCell():New(oSection2,"(cTRB)->VLRCUST/(cTRB)->CONTADO",(cTRB),STR0017,"@E 999,999,999.999",16,/*lPixel*/,/*{|| code-block de impressao }*/) //"Custo/Contador"
	oCell := TRCell():New(oSection2,"(cTRB)->MOTIVO"              ,(cTRB),STR0018,"@!",6,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Motivo"
	oCell := TRCell():New(oSection2,"TPJ_DESMOT"  ,"TPJ",STR0011,/*Picture*/,30,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Descrição"
	//Definicao para imprimir os cabecalhos de campos numericos da esquerda para a direita

	oSection2:Cell("(cTRB)->CONTADO"):SetHeaderAlign("RIGHT")
	oSection2:Cell("(cTRB)->VLRCUST"):SetHeaderAlign("RIGHT")
	oSection2:Cell("(cTRB)->VLRCUST/(cTRB)->CONTADO"):SetHeaderAlign("RIGHT")
	If lTIPMOD
		TRPosition():New(oSection2,"TQR",1,{|| xFilial("TQR") + (cTRB)->TIPMOD})
	EndIf
	TRPosition():New(oSection2,"ST7",1,{|| xFilial("ST7") + (cTRB)->FABRICA})
	TRPosition():New(oSection2,"TPJ",1,{|| xFilial("TPJ") + (cTRB)->MOTIVO})

Return oReport

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR270R3 ³ Autor ³ Deivys Joenck         ³ Data ³ 19/09/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio de Baixas de Bens                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR270                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR270R3()

	Local WNREL      := "MNTR270"
	Local LIMITE     := 220
	Local cDESC1     := STR0001 //"Relatorio dos Bens Baixados."
	Local cDESC2     := STR0002 //"O usuario pode selecionar quais os campos que deverao ser mostrados,"
	Local cDESC3     := STR0003 //"bem como informar parametros de selecao para a impressao."
	Local cSTRING    := "ST9"

	Private NOMEPROG := "MNTR270"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0004,1,STR0005,1,2,1,"",1}   //"Zebrado"###"Administracao"
	Private TITULO   := STR0001 //"Relatorio dos Bens Baixados."
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1, CABEC2
	Private cPERG := "MNT27R"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica as perguntas selecionadas                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ mv_par01     // De  Centro de Custo                          ³
	//³ mv_par02     // Ate Centro de Custo                          ³
	//³ mv_par03     // De  Centro Trabalho                          ³
	//³ mv_par04     // Ate Centro Trabalho                          ³
	//³ mv_par05     // De  Familia                                  ³
	//³ mv_par06     // Ate Familia                                  ³
	//³ mv_par07     // De  Data Inicio                              ³
	//³ mv_par08     // Ate Data Fim                                 ³
	//³ mv_par09     // Tipo de Custo (1=Medio, 2=Standard)          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	Pergunte(cPERG,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("ST9")
		Return
	EndIf
	SetDefault(aRETURN,cSTRING)
	RptStatus({|lEND| MNTR270IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)
	DbSelectArea("ST9")
Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR270IMP³ Autor ³ Deivys Joenck         ³ Data ³ 19/09/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR270                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR270IMP(lEND,WNREL,TITULO,TAMANHO)
	Local cRODATXT := ""
	Local nCNTIMPR := 0
	Local cARQTR270

	Private aVETINR := {}

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Contadores de linha e pagina                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private li := 80 ,m_pag := 1
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se deve comprimir ou nao                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nTIPO  := IIF(aRETURN[4]==1,15,18)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta os Cabecalhos                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	CABEC1 := STR0008
	CABEC2 := If(lTIPMOD,STR0006,STR0023)
	//"Bem             Modelo    Descricao             Fabricante Descrição                             Data Compra  Data Baixa          Contador             Custo   Custo/Contador  Motivo Descricao"
	//"Bem               Fabric. Nome Fabricante                           Data Compra  Data Baixa          Contador             Custo   Custo/Contador  Motivo Descricao"

	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         0         1
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345
	------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Familia  Descricao
	Bem               Tp. Mod.   Descricao             Fabric. Nome Fabricante                           D.Compra  D.Baixa          Contador             Custo   Custo/Contador  Motivo Descricao
	------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	XXXXXXXXXXXXXXXX  XXXXXXXXXX XXXXXXXXXXXXXXXXXXXX  XXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99/99/99  99/99/99  999.999.999.999  9.999.999.999,99  999.999.999,999  XXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	XXXXXXXXXXXXXXXX  XXXXXXXXXX XXXXXXXXXXXXXXXXXXXX  XXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99/99/99  99/99/99  999.999.999.999  9.999.999.999,99  999.999.999,999  XXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

	*/
	aDBFR270 := {{"CODBEM" ,"C",16,0},;
				 {"FAMILIA","C",06,0},;
				 {"CONTADO","N",14,0},;
				 {"FABRICA","C",06,0},;
				 {"DTCOMPR","D",08,0},;
				 {"DTBAIXA","D",08,0},;
				 {"VLRCUST","N",14,2},;
				 {"MOTIVO" ,"C",06,0}}

	If lTIPMOD
		aAdd(aDBFR270,{"TIPMOD" ,"C",10,0})
	EndIf

	//Cria Tabela Temporária
	oARQTR270 := NGFwTmpTbl(cTRB,aDBFR270,{{"FAMILIA","CODBEM"}})

	Processa({|lEND| MTR270TJTS()},STR0009)

	DbSelectArea(cTRB)
	DbGotop()
	While !Eof()
		NGSOMALI(58)
		cCODFAMI := (cTRB)->FAMILIA
		@ Li,000 Psay (cTRB)->FAMILIA
		DbSelectArea("ST6")
		DbSetOrder(1)
		DbSeek(xFILIAL("ST6")+(cTRB)->FAMILIA)
		@ Li,009 Psay ST6->T6_NOME
		NGSOMALI(58)
		While !Eof() .And. (cTRB)->FAMILIA == cCODFAMI
			@LI,000 Psay (cTRB)->CODBEM
			If lTIPMOD
				@LI,018 Psay (cTRB)->TIPMOD
				@LI,029 Psay NGSEEK("TQR",(cTRB)->TIPMOD,1,"TQR_DESMOD")
				@LI,051 Psay (cTRB)->FABRICA
				@LI,062 Psay NGSEEK("ST7",(cTRB)->FABRICA,1,"T7_NOME")
				@LI,104 Psay (cTRB)->DTCOMPR               Picture '99/99/99'
				@LI,117 Psay (cTRB)->DTBAIXA               Picture '99/99/99'
				@LI,130 Psay (cTRB)->CONTADO               Picture '@E 999,999,999,999'
				@LI,147 Psay (cTRB)->VLRCUST               Picture '@E 9,999,999,999.99'
				@LI,165 Psay (cTRB)->VLRCUST/(cTRB)->CONTADO  Picture '@E 999,999,999.999'
				@LI,182 Psay (cTRB)->MOTIVO
				@LI,189 Psay SubStr(NGSEEK("TPJ",(cTRB)->MOTIVO,1,"TPJ_DESMOT"),1,30)
			Else
				@LI,018 Psay (cTRB)->FABRICA
				@LI,031 Psay NGSEEK("ST7",(cTRB)->FABRICA,1,"T7_NOME")
				@LI,073 Psay (cTRB)->DTCOMPR               Picture '99/99/99'
				@LI,086 Psay (cTRB)->DTBAIXA               Picture '99/99/99'
				@LI,099 Psay (cTRB)->CONTADO               Picture '@E 999,999,999,999'
				@LI,116 Psay (cTRB)->VLRCUST               Picture '@E 9,999,999,999.99'
				@LI,134 Psay (cTRB)->VLRCUST/(cTRB)->CONTADO  Picture '@E 999,999,999.999'
				@LI,151 Psay (cTRB)->MOTIVO
				@LI,158 Psay SubStr(NGSEEK("TPJ",(cTRB)->MOTIVO,1,"TPJ_DESMOT"),1,30)
			EndIf
			NGSOMALI(58)
			DbSelectArea(cTRB)
			DbSkip()
		End
	End
	RODA(nCNTIMPR,cRODATXT,TAMANHO)
	RetIndex("ST9")
	Set Filter To
	Set Device To Screen
	If aRETURN[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()
	DbSelectArea(cTRB)

	//Deleta o arquivo temporario fisicamente
	oARQTR270:Delete()

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ReportPrint³ Autor ³  Elisangela Costa     ³ Data ³ 21/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR270                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint(oReport)

	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(1):Section(1)
	Local cARQTR270
	
	Private aVETINR := {}

	aDBFR270 := {{"CODBEM" ,"C",16,0},;
				 {"FAMILIA","C",06,0},;
				 {"CONTADO","N",14,0},;
				 {"FABRICA","C",06,0},;
				 {"DTCOMPR","D",08,0},;
				 {"DTBAIXA","D",08,0},;
				 {"VLRCUST","N",14,2},;
				 {"MOTIVO" ,"C",06,0}}
	If lTIPMOD
		aAdd(aDBFR270,{"TIPMOD" ,"C",10,0})
	EndIf

	//Cria Tabela Temporária 
	oARQTR270 := NGFwTmpTbl(cTRB,aDBFR270,{{"FAMILIA","CODBEM"}})

	Processa({|lEND| MTR270TJTS()},STR0009)

	DbSelectArea(cTRB)
	DbGotop()
	oReport:SetMeter(RecCount())
	While !Eof() .And. !oReport:Cancel()

		oSection1:Init()
		oSection1:PrintLine()
		oSection2:Init()

		cCODFAMI := (cTRB)->FAMILIA
		DbSelectArea(cTRB)
		While !Eof() .And. !oReport:Cancel() .And. (cTRB)->FAMILIA == cCODFAMI

			oReport:IncMeter()
			oSection2:PrintLine()
			DbSelectArea(cTRB)
			Dbskip()
		End
		oSection1:Finish()
		oSection2:Finish()
	End

	//Deleta o arquivo temporario fisicamente
	oARQTR270:Delete()

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MTR270TJTS³ Autor ³ Deivys Joenck         ³ Data ³ 19/09/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR270                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MTR270TJTS()

	DbSelectArea("ST9")
	DbSetOrder(04)
	DbSeek(xFILIAL("ST9")+MV_PAR05,.T.)
	Procregua(LastRec())
	While !Eof() .And. ST9->T9_FILIAL  == xFILIAL("ST9") .And.;
	ST9->T9_CODFAMI <= MV_PAR06

		IncProc()
		If ST9->T9_SITBEM != 'I'
			DbSkip()
			Loop
		EndIf
		If ST9->T9_DTBAIXA < MV_PAR07 .Or. ST9->T9_DTBAIXA > MV_PAR08
			DbSkip()
			Loop
		EndIf

		// PROCURA OS CUSTO DA ORDENS NORMAL DO BEM
		nTOTOS  := 0.00

		DbSelectArea("STJ")
		DbSetOrder(2)
		DBSeek(xFILIAL("STJ")+"B"+ST9->T9_CODBEM)
		While !Eof() .And. STJ->TJ_FILIAL == xFILIAL("STJ") .And.;
		STJ->TJ_TIPOOS = "B" .And. STJ->TJ_CODBEM == ST9->T9_CODBEM

			If STJ->TJ_SITUACA <> "L" .Or. STJ->TJ_TERMINO == 'N'
				DbSkip()
				Loop
			EndIf

			If STJ->TJ_CCUSTO < MV_PAR01 .Or. STJ->TJ_CCUSTO > MV_PAR02
				DbSkip()
				Loop
			EndIf

			If STJ->TJ_CENTRAB < MV_PAR03 .Or. STJ->TJ_CENTRAB > MV_PAR04
				DbSkip()
				Loop
			EndIf

			nVALOR := 0.00
			DbSelectArea("STL")
			DbSetOrder(1)
			If DbSeek(xFILIAL("STL")+STJ->TJ_ORDEM+STJ->TJ_PLANO)
				While !Eof() .And. STL->TL_FILIAL == xFILIAL("STL") .And.;
				STL->TL_ORDEM  == STJ->TJ_ORDEM .And. STL->TL_PLANO  == STJ->TJ_PLANO

					If Alltrim(STL->TL_SEQRELA) <> "0"
						vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
						STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
						STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
						nQTDHORAS := vVETHORAS[1]

						If MV_PAR09 = 2 //STANDARD
							aVETCUST := NGCUSTSTAN(STL->TL_CODIGO,STL->TL_TIPOREG)
							nCUSTO   := aVETCUST[1] //Custo standard
							nCUSTO   := nCUSTO * nQTDHORAS
						Else
							nCUSTO   := STL->TL_CUSTO
						EndIf
						nVALOR += nCUSTO
					EndIf

					DbSelectArea("STL")
					DbSkip()
				End
			EndIf

			nTOTOS := nTOTOS+nVALOR

			DbSelectArea("STJ")
			DbSkip()
		End

		// PROCURA OS CUSTO DA ORDENS HISTORICO DO BEM
		DbSelectArea("STS")
		DbSetOrder(02)
		DbSeek(xFILIAL("STS")+"B"+ST9->T9_CODBEM)
		While !Eof() .And. STS->TS_FILIAL == xFILIAL("STS") .And.;
		STS->TS_TIPOOS = "B" .And. STS->TS_CODBEM == ST9->T9_CODBEM

			If STS->TS_SITUACA <> "L" .Or. STS->TS_TERMINO == 'N'
				DbSkip()
				Loop
			EndIf

			If STS->TS_CCUSTO < MV_PAR01 .Or. STS->TS_CCUSTO > MV_PAR02
				DbSkip()
				Loop
			EndIf

			If STS->TS_CENTRAB < MV_PAR03 .Or. STS->TS_CENTRAB > MV_PAR04
				DbSkip()
				Loop
			EndIf

			nVALOR := 0.00
			DbSelectArea("STT")
			DbSetOrder(1)
			If DbSeek(xFILIAL("STT")+STS->TS_ORDEM+STS->TS_PLANO)
				While !Eof() .And. STT->TT_FILIAL == xFILIAL("STT") .And.;
				STT->TT_ORDEM == STS->TS_ORDEM .And. STT->TT_PLANO  == STS->TS_PLANO

					If Alltrim(STT->TT_SEQRELA) <> "0"
						vVETHORAS := NGTQUATINS(STT->TT_CODIGO,STT->TT_TIPOREG,STT->TT_USACALE,;
						STT->TT_QUANTID,STT->TT_TIPOHOR,STT->TT_DTINICI,;
						STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)
						nQTDHORAS := vVETHORAS[1]

						If MV_PAR09 = 2 //STANDARD
							aVETCUST := NGCUSTSTAN(STT->TT_CODIGO,STT->TT_TIPOREG)
							nCUSTO   := aVETCUST[1] //Custo standard
							nCUSTO   := nCUSTO * nQTDHORAS
						Else
							nCUSTO   := STT->TT_CUSTO
						EndIf
						nVALOR += nCUSTO
					EndIf
					DbSelectArea("STT")
					DbSkip()
				End
			EndIf

			nTOTOS := nTOTOS+nVALOR
			DbSelectArea("STS")
			DbSkip()
		End
		DbSelectArea(cTRB)
		(cTRB)->(DbAppend())
		(cTRB)->CODBEM  := ST9->T9_CODBEM
		(cTRB)->FAMILIA := ST9->T9_CODFAMI
		(cTRB)->CONTADO := ST9->T9_CONTACU
		If lTIPMOD
			(cTRB)->TIPMOD  := ST9->T9_TIPMOD
		EndIf
		(cTRB)->FABRICA := ST9->T9_FABRICA
		(cTRB)->DTCOMPR := ST9->T9_DTCOMPR
		(cTRB)->DTBAIXA := ST9->T9_DTBAIXA
		(cTRB)->VLRCUST := nTOTOS
		(cTRB)->MOTIVO  := ST9->T9_MTBAIXA
		DbSelectArea("ST9")
		DbSkip()
	End
Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} fCriaTRB
Cria TRB
@author eduardo.izola
@since 16/02/2017
@version undefined
@param cAlias, characters, Alias Tabela
@param aFields, array, Array de campos
@param aIndex, array, Indice TRB
@type function
/*/
//---------------------------------------------------------------------
Static Function fCriaTRB(cAlias,aFields,aIndex)

	Local i

	oTempTable := FWTemporaryTable():New( cAlias , aFields )
	For i := 1 To Len(aIndex)
		oTempTable:AddIndex("ind"+cValToChar(i), aIndex[i] )
	Next i
	oTempTable:Create()

Return oTempTable
