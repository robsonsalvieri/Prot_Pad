#INCLUDE "MNTR380.ch"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR380   ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 21/09/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio de Bens transferidos entre filiais                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/      
Function MNTR380()  

	WNREL      := "MNTR380"
	LIMITE     := 182
	cDESC1     := STR0001 //"O relatorio apresentará um quadro comparativo da última inspeção "
	cDESC2     := STR0002 //"com a anterior. "
	cDESC3     := ''
	cSTRING    := "TQF" 

	Private NOMEPROG := "MNTR380"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0003,1,STR0004,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private TITULO   := STR0005 //"Divergencias entre Inspeção Anterior X Inspeção Atual"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2 
	Private aVETINR := {}    
	Private cPerg := "MNR380"   
	Private aPerg :={}

	SetKey( VK_F9, { | | NGVersao( "MNTR380" , 1 ) } )

	Pergunte(cPERG,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("TR2")  
		Return
	EndIf     
	SetDefault(aReturn,cSTRING)
	RptStatus({|lEND| MNTR380IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0009,STR0010) //"Aguarde..."###"Processando Registros..."
	Dbselectarea("TR2")  

Return .T.    

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNR380IMP | Autor ³ Marcos Wagner Junior  ³ Data ³14/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR380                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR380IMP(lEND,WNREL,TITULO,TAMANHO)
	Local nI := 0
	Private cRODATXT := ""
	Private nCNTIMPR := 0     
	Private li := 80 ,m_pag := 1    
	Private cNomeOri
	Private aUltimaIns := {}, aPrimeiIns := {}
	Private cBanda := ''
	Private lInspecoes := .f.

	nTIPO  := IIf(aReturn[4]==1,15,18)

	CABEC1 := STR0011 //"N.Fogo       Filial                 Vida   Sulco    Posicao    Filial                 Vida   Sulco    Posicao   Divergência encontrada"
	CABEC2 := STR0012 //"             ---------------Inspeção Anterior--------------    ----------------Inspeção Final----------------"

	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         0         1         2  
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	**************************************************************************************************************************
	Relatorio de Pneus    
	*******************************************************************************************************************************************************************************************************************************
	Filial de Origem: xx - xxxxxxxxxx
	*******************************************************************************************************************************************************************************************************************************
	---------------Inspeção Anterior--------------    ----------------Inspeção Final----------------
	Ordem: xxxxxx / Plano: xxxxxx (99/99/99)          Ordem: xxxxxx / Plano: xxxxxx (99/99/99)
	N.Fogo       Filial                 Vida   Sulco    Posicao    Filial                 Vida   Sulco    Posicao   Divergência encontrada
	xxxxxxxxxx   xx - xxxxxxxxxxxxxxx   xx     999.99   xxxxxx     xx - xxxxxxxxxxxxxxx   xx     999.99   xxxxxx
	*/

	cAliasQry3 := GetNextAlias()
	cQuery3 := " SELECT * "
	cQuery3 += " FROM "+RetSqlName("TR9")+" TR9 "
	cQuery3 += " WHERE TR9.TR9_FILIAL >= '"+MV_PAR01+"' AND TR9.TR9_FILIAL <= '" +MV_PAR02+"' 
	cQuery3 += " AND   TR9.TR9_DTINSP >= '"+DTOS(MV_PAR03)+"'"
	cQuery3 += " AND   TR9.TR9_STATUS = '4' "
	cQuery3 += " AND   TR9.D_E_L_E_T_ <> '*' "
	cQuery3 += " ORDER BY TR9.TR9_FROTA, TR9.TR9_DTINSP, TR9.TR9_HRINSP "
	cQuery3 := ChangeQuery(cQuery3)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery3),cAliasQry3, .F., .T.)

	aRetorno := {}
	SetRegua(LastRec())                                                                 
	DbGoTop()
	While !Eof()
		nPos := aSCAN(aRetorno,{|x| x[1] = (cAliasQry3)->TR9_FROTA})
		If nPos == 0
			Aadd(aRetorno,{(cAliasQry3)->TR9_FROTA,(cAliasQry3)->R_E_C_N_O_,1})
		Else
			lInspecoes := .t.
			If aRetorno[nPos][3] == 1
				Aadd(aRetorno,{(cAliasQry3)->TR9_FROTA,R_E_C_N_O_,2})
				aRetorno[nPos][3] := 2
			Else
				aRetorno[nPos][2] := aRetorno[nPos+1][2]
				aRetorno[nPos+1][2] := (cAliasQry3)->R_E_C_N_O_
			Endif
		Endif
		dbSkip()
	End
	(cAliasQry3)->(dbCloseArea())

	nCont := 0
	If lInspecoes
		cAliasQry := GetNextAlias()
		cQuery := " SELECT TRC.*,TR9.TR9_FILIAL,TR9.TR9_ORDEM,TR9.TR9_PLANO,TR9.TR9_CICLO,TR9.TR9_STATUS,"
		cQuery += " TR9.TR9_FROTA,TR9.TR9_DTINSP,TR9.TR9_DTDIGI,TR9.TR9_HRDIGI,TR9.TR9_HRINSP "
		cQuery += " FROM "+RetSqlName("TRC")+" TRC ,"+RetSqlName("TR9")+" TR9 "
		cQuery += " WHERE TR9.TR9_FILIAL >= '"+MV_PAR01+"' AND TR9.TR9_FILIAL <= '" +MV_PAR02+"' 
		cQuery += " AND   TR9.TR9_DTINSP >= '"+DTOS(MV_PAR03)+"'"
		cQuery += " AND   TR9.TR9_FILIAL = TRC.TRC_FILIAL "
		cQuery += " AND   TR9.TR9_ORDEM  = TRC.TRC_ORDEM "
		cQuery += " AND   TR9.TR9_CICLO  = TRC.TRC_CICLO "
		cQuery += " AND   TRC.D_E_L_E_T_ <> '*' "
		cQuery += " AND   TR9.D_E_L_E_T_ <> '*' "
		cQuery += " AND   TR9.R_E_C_N_O_ IN ("
		For nI := 1 to Len(aRetorno)
			If aRetorno[nI][3] > 1
				If nCont > 0
					cQuery += ","
				Endif
				cQuery += AllTrim(Str(aRetorno[nI][2]))
				nCont++
			Endif
		Next
		cQuery += ")"
		cQuery += " ORDER BY TRC.TRC_EIXO, TRC.TRC_ORDEM "
		cQuery := ChangeQuery(cQuery)
		dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

		nRegistro := 0
		dbGoTop()
		While !Eof()
			nRegistro++
			For nI := 1 to 10

				If nI <= 9
					cPosicao := "(cAliasQry)->TRC_LOCPN"+AllTrim(Str(nI))
					cPneu    := "(cAliasQry)->TRC_NFOGO"+AllTrim(Str(nI))
				Else
					cPosicao := "(cAliasQry)->TRC_LOCP"+AllTrim(Str(nI))
					cPneu    := "(cAliasQry)->TRC_NFOG"+AllTrim(Str(nI))
				Endif
				cBanda := '  '
				If !Empty(&cPneu)
					nSulco   := MNTR380SUL(nI)
					nCont := 0
					dbSelectArea("TQV")
					dbSetOrder(01)
					If dbSeek((cAliasQry)->TRC_FILIAL+&cPneu)
						While !Eof() .AND. TQV->TQV_FILIAL == (cAliasQry)->TRC_FILIAL .AND. Alltrim(TQV->TQV_CODBEM) == Alltrim(&cPneu)
							If DToS(TQV->TQV_DTMEDI) <= (cAliasQry)->TR9_DTINSP .And. TQV->TQV_HRMEDI <= (cAliasQry)->TR9_HRINSP 
								dMenorData := TQV->TQV_DTMEDI
								cBanda := TQV->TQV_BANDA
								nCont++
							Endif
							dbSkip()
						End				
					Endif
					If nCont = 0
						dbSelectArea("TQS")
						dbSetOrder(01)
						If dbSeek(xFilial("TQS")+&cPneu)
							cBanda := TQS->TQS_BANDAA
						Endif
					Endif
					cBanda := MNTR380BAN(cBanda)
					If MOD(nRegistro,2) > 0
						Aadd(aPrimeiIns,{&cPneu,(cAliasQry)->TRC_FILIAL,cBanda,nSulco,&cPosicao,(cAliasQry)->TR9_ORDEM,;
						(cAliasQry)->TR9_PLANO,(cAliasQry)->TR9_FROTA,(cAliasQry)->TR9_DTINSP})
					Else
						Aadd(aUltimaIns,{&cPneu,(cAliasQry)->TRC_FILIAL,cBanda,nSulco,&cPosicao,(cAliasQry)->TR9_ORDEM,;
						(cAliasQry)->TR9_PLANO,(cAliasQry)->TR9_FROTA,(cAliasQry)->TR9_DTINSP})
					Endif
				Endif
			Next
			DbSelectArea(cAliasQry)
			DbSkip()
		End
		(cAliasQry)->(dbCloseArea())
	Endif

	aDiverge := {}
	For nI := 1 to Len(aUltimaIns)
		nPos := aSCAN(aPrimeiIns,{|x| x[1] = aUltimaIns[nI][1]})
		If nPos > 0
			If aUltimaIns[nI][2] != aPrimeiIns[nPos][2]
				Aadd(aDiverge,{aUltimaIns[nI][1],aUltimaIns[nI][2],aUltimaIns[nI][3],aUltimaIns[nI][4],aUltimaIns[nI][5],;
				aPrimeiIns[nPos][2],aPrimeiIns[nPos][3],aPrimeiIns[nPos][4],aPrimeiIns[nPos][5],STR0013,; //"Filial diferente"
				aPrimeiIns[nPos][6],aPrimeiIns[nPos][7],aPrimeiIns[nPos][8],aUltimaIns[nPos][6],aUltimaIns[nPos][7],;
				aPrimeiIns[nPos][9],aUltimaIns[nPos][9]})
			Endif
			If aUltimaIns[nI][3] != aPrimeiIns[nPos][3]
				Aadd(aDiverge,{aUltimaIns[nI][1],aUltimaIns[nI][2],aUltimaIns[nI][3],aUltimaIns[nI][4],aUltimaIns[nI][5],;
				aPrimeiIns[nPos][2],aPrimeiIns[nPos][3],aPrimeiIns[nPos][4],aPrimeiIns[nPos][5],STR0022,aPrimeiIns[nPos][6],; //"Banda diferente"
				aPrimeiIns[nPos][7],aPrimeiIns[nPos][8],aUltimaIns[nPos][6],aUltimaIns[nPos][7],aPrimeiIns[nPos][9],aUltimaIns[nPos][9]})
			Endif
			If aUltimaIns[nI][4] == aPrimeiIns[nPos][4] .And. aUltimaIns[nI][3] == aPrimeiIns[nPos][3]
				Aadd(aDiverge,{aUltimaIns[nI][1],aUltimaIns[nI][2],aUltimaIns[nI][3],aUltimaIns[nI][4],aUltimaIns[nI][5],;
				aPrimeiIns[nPos][2],aPrimeiIns[nPos][3],aPrimeiIns[nPos][4],aPrimeiIns[nPos][5],STR0015,aPrimeiIns[nPos][6],; //"Sulco igual"
				aPrimeiIns[nPos][7],aPrimeiIns[nPos][8],aUltimaIns[nPos][6],aUltimaIns[nPos][7],aPrimeiIns[nPos][9],aUltimaIns[nPos][9]})
			Endif
			If aUltimaIns[nI][5] != aPrimeiIns[nPos][5]
				Aadd(aDiverge,{aUltimaIns[nI][1],aUltimaIns[nI][2],aUltimaIns[nI][3],aUltimaIns[nI][4],aUltimaIns[nI][5],;
				aPrimeiIns[nPos][2],aPrimeiIns[nPos][3],aPrimeiIns[nPos][4],aPrimeiIns[nPos][5],STR0016,aPrimeiIns[nPos][6],; //"Posição diferente"
				aPrimeiIns[nPos][7],aPrimeiIns[nPos][8],aUltimaIns[nPos][6],aUltimaIns[nPos][7],aPrimeiIns[nPos][9],aUltimaIns[nPos][9]})
			Endif
		Else
			Aadd(aDiverge,{aUltimaIns[nI][1],aUltimaIns[nI][2],aUltimaIns[nI][3],aUltimaIns[nI][4],aUltimaIns[nI][5],;
			'','','','',STR0017,aUltimaIns[nI][6],aUltimaIns[nI][7],aUltimaIns[nI][8],; //"Pneu não encontrado na inspeção anterior"
			aUltimaIns[nI][6],aUltimaIns[nI][7],'',aUltimaIns[nI][9]})
		Endif
		aDiverge := aSort(aDiverge,,,{ |x,y| x[1] < y[1] } )
	Next

	cBemPai := ''
	cBemAntigo := ''
	If Len(aDiverge) > 0
		For nI := 1 to Len(aDiverge)
			NgSomaLi(58)
			If nI == 1 .OR. aDiverge[nI][13] != cBemPai .OR. aDiverge[nI][1] != cBemAntigo
				If aDiverge[nI][13] != cBemPai
					@ Li,014		 PSay STR0018+aDiverge[nI][11]+STR0019+aDiverge[nI][12]+' ('+DTOC(STOD(aDiverge[nI][16]))+')' //"Ordem: "###" / Plano: "
					@ Li,120		 PSay STR0018+aDiverge[nI][14]+STR0019+aDiverge[nI][15]+' ('+DTOC(STOD(aDiverge[nI][17]))+')' //"Ordem: "###" / Plano: "
					NgSomaLi(58)
					NgSomaLi(58)
				Endif
				@ Li,000		 PSay aDiverge[nI][1]
				If !Empty(aDiverge[nI][6])
					@ Li,013 	 Psay aDiverge[nI][6]
					DbSelectArea("SM0")
					SM0->(DbSetOrder(1))	
					If MsSeek(SM0->M0_CODIGO+aDiverge[nI][6])   
						@ Li,021   Psay SM0->M0_FILIAL
					Endif
					@ Li,061		 PSay aDiverge[nI][7]
					@ Li,070		 PSay aDiverge[nI][8]
					@ Li,079		 PSay aDiverge[nI][9]
				Endif
				@ Li,118 	 Psay aDiverge[nI][2]
				DbSelectArea("SM0")
				SM0->(DbSetOrder(1))	
				If MsSeek(SM0->M0_CODIGO+aDiverge[nI][2])   
					@ Li,126   Psay SM0->M0_FILIAL
				Endif
				@ Li,167		 PSay aDiverge[nI][3]
				@ Li,174		 PSay aDiverge[nI][4]
				@ Li,182		 PSay aDiverge[nI][5]
			Endif
			@ Li,193		 PSay aDiverge[nI][10]
			cBemAntigo := aDiverge[nI][1]
			cBemPai := aDiverge[nI][13]
		Next
	Else
		MsgInfo(STR0020,STR0021) //"Não existem dados para montar o relatório."###"ATENÇÃO"
		Return .f.
	Endif

	RODA(nCNTIMPR,cRODATXT,TAMANHO)       

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve a condicao original do arquivo principal             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex('ST9')
	Set Filter To
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |MNR380FL  | Autor ³Marcos Wagner Junior   ³ Data ³ 07/12/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o |Valida codigos de Filial, Ate Filial 					        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR380                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNR380FL(nOpc,cParDe,cParAte)

	Local nTamAte := Posicione("SX1",1,"MNR380"+Space(4)+'02',"X1_TAMANHO")

	If Empty(cParDe) .AND. cParAte == Replicate('Z', nTamAte )
		Return .t.
	Else
		If nOpc == 1  
			If Empty(cParDe)
				Return .t.
			Else
				lRet := IIf(Empty(cParDe),.t.,ExistCpo('SM0',SM0->M0_CODIGO+cParDe))
				If !lRet
					Return .f.
				EndIf      
			Endif
		Elseif nOpc == 2  
			If cParAte == Replicate('Z', nTamAte )
				Return .t.
			Else
				lRet := IIF(ATECODIGO('SM0',SM0->M0_CODIGO+cParDe,SM0->M0_CODIGO+cParAte,03),.T.,.F.)
				If !lRet
					Return .f.
				EndIf
			Endif
		EndIf
	EndIf          

Return .t.                

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR380SUL³ Autor ³Marcos Wagner Junior   ³ Data ³ 07/12/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Busca a menor das 3 medicoes de sulco do pneu               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR380                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR380SUL(nNumero)
	Local nX := 0
	Local nMedida := 1000
	Local nMedMin := 1000

	For nX := 1 to 3
		cMedida := '(cAliasQry)->TRC_PN'+Alltrim(Str(nNumero))+'M'+Alltrim(Str(nX))
		nMedMin := &cMedida
		nMedida := MIN(nMedida,nMedMin)
	Next

Return nMedida

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR380BAN³ Autor ³Marcos Wagner Junior   ³ Data ³ 10/12/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna a descricao da Banda					                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR380                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR380BAN(cBan)

	If cBan = '1'
		cBanda := 'OR'
	ElseIf cBan = '2'
		cBanda := 'R1'
	ElseIf cBan = '3'
		cBanda := 'R2'
	ElseIf cBan = '4'
		cBanda := 'R3'
	ElseIf cBan = '5'
		cBanda := 'R4'
	Else
		cBanda := '  '
	Endif

Return cBanda
