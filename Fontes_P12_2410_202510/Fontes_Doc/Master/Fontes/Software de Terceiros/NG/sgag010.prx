#Include "Protheus.ch"
#Include "SGAG010.ch"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ SGAG010  ³ Autor ³ Rafael Diogo Richter  ³ Data ³11/01/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa para gerar graficos dos criterios de avaliacao.    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SGAG010()
Local aNGBEGINPRM	:= NGBEGINPRM()
Local oTempTRB
Private cCadastro := OemToAnsi(STR0001)  //"Grafico de Criterios de Avaliacao"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                             ³
//³ mv_par01     // De Data                                          ³
//³ mv_par02     // Ate Data                                         ³
//³ mv_par03     // Criterio de Avaliacao                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Pergunte("SGG010",.T.)
	cAliasTRB := GetNextAlias()
	//Cria Estrutura da tabela
	aCampos := {}
	aADD(aCampos, {"OPCAO"	, "C" , 03	, 0})
	aADD(aCampos, {"NOME"	, "C" , 30	, 0})
	aADD(aCampos, {"QUANT"	, "N" , 09	, 0})

	oTempTRB := FWTemporaryTable():New( cAliasTRB, aCampos )
	oTempTRB:AddIndex( "1", {"OPCAO"} )
	oTempTRB:AddIndex( "2", {"NOME"} )
	oTempTRB:AddIndex( "3", {"QUANT"} )
	oTempTRB:Create()

	//Processa as Opcoes do Criterio de Avaliacao
	Processa({ || SGG010PROC() })

	//Verifica se foram carregados registros
	If (cAliasTRB)->(RecCount()) == 0
		MsgStop(STR0015) //"Não existem dados para montar a consulta."
	Else
		//Monta tela com registros
		SGC10CON()
	Endif
	//Deleta o arquivo temporario fisicamente
	oTempTRB:Delete()
	dbSelectArea("TA6")
Endif

NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGG010PROC³ Autor ³ Rafael Diogo Richter  ³ Data ³11/01/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Processa o Criterio de Avaliacao do Aspecto      	   	      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³TA6 - Criterios de Avaliacao do Aspecto                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SGG010PROC()

//Processa o Criterio de Avaliacao do Aspecto(TA6)
DbSelectArea("TA6")
DbSetOrder(01)
If DbSeek(xFilial("TA6")+MV_PAR03)

	DbSelectArea("TA7")
	DbSetOrder(01)
	DbSeek(xFilial("TA7")+TA6->TA6_CODAVA)
	ProcRegua(TA7->(Recno()))
	While !Eof() .And. TA7->TA7_FILIAL = xFILIAL("TA7") .And. TA7->TA7_CODAVA = TA6->TA6_CODAVA
		IncProc()
		DbSelectArea("TAD")
		DbSetOrder(02)
		If DbSeek(xFilial("TAD")+TA7->TA7_CODAVA+TA7->TA7_CODOPC)
			While !Eof() .And. TAD->TAD_FILIAL = xFILIAL("TAD") .And. TAD->TAD_CODAVA = TA7->TA7_CODAVA ;
				.And. TAD->TAD_CODOPC = TA7->TA7_CODOPC

				DbSelectArea("TAB")
				DbSetOrder(01)
				If DbSeek(xFilial("TAB")+TAD->TAD_ORDEM)
					If TAB->TAB_DTRESUL < MV_PAR01 .Or. TAB->TAB_DTRESUL > MV_PAR02
						DbSelectArea("TAD")
						Dbskip()
						Loop
					EndIf
				Else
					DbSelectArea("TAD")
					DbSkip()
					Loop
				EndIf

				If !(cAliasTRB)->(DBSEEK(TAD->TAD_CODOPC))
					(cAliasTRB)->(DbAppend())
					(cAliasTRB)->OPCAO := TAD->TAD_CODOPC
					(cAliasTRB)->NOME  := TA7->TA7_OPCAO
					(cAliasTRB)->QUANT := 1
				Else
					(cAliasTRB)->QUANT += 1
				EndIf

				DbSelectArea("TAD")
				DbSkip()
			End
		Else
			If !(cAliasTRB)->(DBSEEK(TA7->TA7_CODOPC))
				(cAliasTRB)->(DbAppend())
				(cAliasTRB)->OPCAO := TA7->TA7_CODOPC
				(cAliasTRB)->NOME  := TA7->TA7_OPCAO
				(cAliasTRB)->QUANT := 0
			EndIf
		EndIf

		//HISTORICO DAS OPCOES DE CRITERIO DE AVALIACAO
		DbSelectArea("TAP")
		DbSetOrder(02)
		If DbSeek(xFilial("TAP")+TA7->TA7_CODAVA+TA7->TA7_CODOPC)
			While !Eof() .And. TAP->TAP_FILIAL = xFILIAL("TAP") .And. TAP->TAP_CODAVA = TA7->TA7_CODAVA ;
				.And. TAP->TAP_CODOPC = TA7->TA7_CODOPC

				DbSelectArea("TAB")
				DbSetOrder(01)
				If DbSeek(xFilial("TAB")+TAP->TAP_ORDEM)
					If TAB->TAB_DTRESUL < MV_PAR01 .Or. TAB->TAB_DTRESUL > MV_PAR02
						DbSelectArea("TAD")
						Dbskip()
						Loop
					EndIf
				Else
					DbSelectArea("TAP")
					DbSkip()
					Loop
				EndIf

				If !(cAliasTRB)->(DBSEEK(TAP->TAP_CODOPC))
					(cAliasTRB)->(DbAppend())
					(cAliasTRB)->OPCAO := TAP->TAP_CODOPC
					(cAliasTRB)->NOME  := TA7->TA7_OPCAO
					(cAliasTRB)->QUANT := 1
				Else
					(cAliasTRB)->QUANT += 1
				EndIf

				DbSelectArea("TAP")
				DbSkip()
			End
		Else
			If !(cAliasTRB)->(DBSEEK(TA7->TA7_CODOPC))
				(cAliasTRB)->(DbAppend())
				(cAliasTRB)->OPCAO := TA7->TA7_CODOPC
				(cAliasTRB)->NOME  := TA7->TA7_OPCAO
				(cAliasTRB)->QUANT := 0
			EndIf
		EndIf
		DbSelectArea("TA7")
		DbSkip()
	End
End

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SGC10CON  ºAutor  ³Roger Rodrigues     º Data ³  05/04/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Monta tela com criterios de avaliacao                       º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SGAC010                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function SGC10CON()
Local oDlg10
Local cTitulo := STR0008 + AllTrim(MV_PAR03) + " - "+ Substr(NGSEEK("TA6",MV_PAR03,1,"TA6->TA6_DESCRI"),1,25) //"Opcoes do criterio de avaliacao "
Local aSize		:= MsAdvSize( .T. , .F. , 430 )
Local aBtnG010		:= {{"",{|| SG010GRAFI()},STR0009,STR0009}}

Define MsDialog oDlg10 Title cTitulo From 9,0 To 28,80 Of oMainWnd

dbSelectArea(cAliasTRB)
@ 02.5,00 Listbox oList Fields	OPCAO , NOME, PADL(Transform(QUANT,"@E 999,999.99"),09) ;
				          			FieldSizes 60,80,80	;
						          	Size 310,95			;
									HEADERS STR0010, STR0011, STR0012 //"Opcao"###"Descricao"###"Quantidade"
oList:Align := CONTROL_ALIGN_ALLCLIENT
dbSelectArea(cAliasTRB)
dbGoTop()

//@ 012,065 Button STR0009 Of oDlg10 Size 40,11 Action (SG010GRAFI()) //"&Grafico"

Activate MsDialog oDlg10 On Init EnchoiceBar(oDlg10,{||oDlg10:End()},{||oDlg10:End()},,aBtnG010) Centered

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SG010GRAFI³ Autor ³ Rafael Diogo Richter  ³ Data ³11/01/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa para montar o Grafico dos Criterios de Avaliacao   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³TB5 - Licenciamento do Fornecedor por Produto               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SG010GRAFI()

NGGRAFICO(" "+cCadastro," ",cCadastro,STR0016+TA6->TA6_DESCRI,"",; //"Criterios de Avaliacao "
                     {Dtoc(MV_PAR01)+STR0017+Dtoc(MV_PAR02)},"A",cAliasTRB) //" a "
DbselectArea(cAliasTRB)
Dbgotop()

Return .t.