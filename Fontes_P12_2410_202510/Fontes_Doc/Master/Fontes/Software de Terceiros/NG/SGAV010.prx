#include "Protheus.ch"

/*/


Ŀ
Funo     SGAV010   Autor  Rafael Diogo Richter   Data 13/10/2008
Ĵ
Descrio Indicador das despesas com residuos.                        
Ĵ
Tabelas   TAX - Definicao de Residuos                                 
          SB1 - Produtos                                              
          TAV - Processos do Residuo                                  
          ST9 - Bens                                                  
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function SGAV010(dDataDe,dDataAte,cResDe,cResAte,cGrupoDe,cGrupoAte,cLocalDe,cLocalAte,cGerDe,cGerAte,cGeracao)
Local nRet := 0
Local cAliasQry, cQuery

cAliasQry := GetNextAlias()

cQuery := " SELECT SUM(D1_TOTAL) RESULT FROM "+RetSQLName("SD1")+" SD1 "
cQuery += " JOIN "+RetSQLName("TAX")+" TAX ON (TAX.TAX_FILIAL = '" + xFilial("TAX") + "' AND TAX.TAX_CODRES = SD1.D1_COD "
If cGeracao <> Nil
	If cGeracao $ '12'
		cQuery += "	AND TAX.TAX_TPGERA = '" + cGeracao + "' "
	EndIf
EndIf
If cGerDe <> Nil .And. cGerAte <> Nil
	cQuery += "	AND TAX.TAX_CODFON BETWEEN '" + cGerDe + "' AND '" + cGerAte + "' "
EndIf
cQuery += "	AND TAX.D_E_L_E_T_ = ' ') "
If cLocalDe <> Nil .And. cLocalAte <> Nil
	cQuery += " JOIN "+RetSQLName("TAV")+" TAV ON (TAV.TAV_FILIAL = '" + xFilial("TAV") + "' AND TAV.TAV_CODNIV = (SELECT MAX(TAV2.TAV_CODNIV) FROM "+RetSqlName("TAV")+" TAV2 "
	cQuery += "	WHERE TAV2.TAV_FILIAL = '" + xFilial("TAV") + "' AND TAV2.TAV_CODRES = SD1.D1_COD "
	cQuery += "	AND TAV2.TAV_CODNIV BETWEEN '" + cLocalDe + "' AND '" + cLocalAte + "' "
	cQuery += "	AND TAV2.D_E_L_E_T_ = ' ') "
	cQuery += "	AND TAV.TAV_CODRES = SD1.D1_COD "
	cQuery += "	AND TAV.D_E_L_E_T_ = ' ') "
EndIf
cQuery += " JOIN "+RetSQLName("SB1")+" SB1 ON (SB1.B1_FILIAL = '" + xFilial("SB1") + "' AND SB1.B1_COD = SD1.D1_COD "
If cGrupoDe <> Nil .And. cGrupoAte <> Nil
	cQuery += "	AND SB1.B1_GRUPO BETWEEN '" + cGrupoDe + "' AND '" + cGrupoAte + "' "
EndIf
cQuery += "	AND SB1.D_E_L_E_T_ = ' ') "
cQuery += " WHERE SD1.D1_FILIAL = '" + xFilial("SD1") + "' "
If cResDe <> Nil .And. cResAte <> Nil
	cQuery += "	AND SD1.D1_COD BETWEEN '" + cResDe + "' AND '" + cResAte + "' "
EndIf
If dDataDe <> Nil .And. dDataAte <> Nil
	cQuery += " 	AND SD1.D1_EMISSAO BETWEEN '" + DTOS(dDataDe) + "' AND '" + DTOS(dDataAte) + "' "
EndIf
cQuery += " 	AND SD1.D_E_L_E_T_ = ' ' "

cQuery := ChangeQuery(cQuery)
MPSysOpenQuery( cQuery , cAliasQry )

dbSelectArea(cAliasQry)
nRet := (cAliasQry)->RESULT

Return nRet