#INCLUDE "MNTA540.ch"
#include "Protheus.ch"

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTA540
Cadastro de Sinistros

@author Marcos Wagner Junior
@since 15/02/07
/*/
//---------------------------------------------------------------------

Function MNTA540()

	Local aNGBEGINPRM := {}

	If !FindFunction( 'MNTAmIIn' ) .Or. MNTAmIIn( 95 )

		aNGBEGINPRM := NGBEGINPRM()

		Private aRotina,cCadastro,aChkDel,aChoice := {}
		Private lStatus := .f.,lGuincho := .t.
		Private cAtivChg := Alltrim(GetMV("MV_ATIVCHG"))
		Private cAtivSai := Alltrim(GetMV("MV_ATIVSAI"))
		Private lNaoIntTMS := SuperGetMv("MV_NGMNTMS",.F.,"N") == "N"
		Private bNGGRAVA
		Private cFilTRH := xFilial("TRH")

		aCAMPOSN := {'TRH_FLGCOM'}

		aCHOICE  := NGCAMPNSX3("TRH",aCAMPOSN)

		aRotina := MenuDef()

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Define o cabecalho da tela de atualizacoes                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cCadastro := OemtoAnsi(STR0004) //"Cadastro de Sinistros"
		aCHKDEL := {}
		bNGGrava := {|| MNTASINGRA()}

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Endereca a funcao de BROWSE                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Dbselectarea("TRH")
		Dbsetorder(1)
		cFilMbrTRH := "TRH_STATUS = '01'"
		MBROWSE(6,1,22,75,"TRH",,,,,,,,,,,,,,cFilMbrTRH)
		Set Key VK_F9 To
		Dbselectarea("TRH")
		Set Filter TO
		Dbsetorder(1)
		Dbseek(xFILIAL("TRH"))

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Devolve variaveis armazenadas (NGRIGHTCLICK) 					  	  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		NGRETURNPRM(aNGBEGINPRM)

	EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNA540HORA³ Autor ³Marcos Wagner Junior   ³ Data ³ 08/02/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao de hora												        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA540                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNA540HORA()

If dDATABASE == M->TRH_DTACID
	If	M->TRH_HRACID > TIME()
		Msgstop(STR0005,STR0006) //"Hora digitada não poderá ser maior que a hora do sistema"###"Atenção"
		Return .f.
	Endif
Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA540GRA ³ Autor ³Marcos Wagner Junior  ³ Data ³ 08/02/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Consistencia de gravacao                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MNTA540                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA540GRA()

Local cParamSera := SuperGetMv("MV_NG1SERA")

Private cVerific
Private cOs


If Inclui
	DbSelectArea("TRP")
	DbSetOrder(01)
  	RecLock("TRP",.t.)
  	TRP->TRP_FILIAL := xFilial("TRP")
  	TRP->TRP_NUMSIN := M->TRH_NUMSIN
  	TRP->TRP_DTOCOR := dDATABASE
  	TRP->TRP_HROCOR := TIME()
  	TRP->TRP_STATUS := "01"
  	TRP->TRP_CODUSU := SUBSTR(cUsuario,7,15)
   MsUnLock("TRP")

	M->TRH_FLGCOM := '1'
	If M->TRH_EVENTO == '1' .AND. M->TRH_FLGDAN == '1'
		dbSelectArea("TRT")
		dbSetOrder(02)
		If !dbSeek(xFilial("TRT")+M->TRH_NUMSIN)
			cVerific := If(NGVERIFY("STJ"),"0",0)
			cRetornoOs := NGGERAOS('C',M->TRH_DTDIG,M->TRH_CODBEM,cParamSera,cVerific,'N','N','N')
			If cRetornoOs[1][1] == 'N'
				MsgStop(cRetornoOs[1][2],STR0006)//"Atenção"
			Else
				If NGCADICBASE("TRH_ORDEM","A","TRH",.F.)
					RecLock("TRH",.f.)
					TRH->TRH_ORDEM := cRetornoOs[1][3]
					MsUnLock("TRH")
				Endif
				DbSelectArea("STJ")
				DbSetOrder(01)
				If DbSeek(xFilial("STJ")+cRetornoOs[1][3]+'000000')
			  		RecLock("STJ",.f.)
			  		MsMM(,80,,M->TRH_DANOS2,1,,,"STJ","TJ_MMSYP")
			   	MsUnLock("STJ")
				Endif

				DbSelectArea("TRT")
				DbSetOrder(01)
				If !DbSeek(xFilial("TRT")+cRetornoOs[1][3]+'000000')
			  		RecLock("TRT",.t.)
					TRT->TRT_FILIAL := xFilial("TRT")
					TRT->TRT_NUMOS  := cRetornoOs[1][3]
					TRT->TRT_PLANO  := '000000'
					TRT->TRT_NUMSIN := M->TRH_NUMSIN
					TRT->TRT_PARECE := M->TRH_DANOS2
			   	MsUnLock("TRT")
			  	Endif
			Endif
		Endif
	Endif
Endif

If nOPCAO != 2
	nSMM := If(nOPCAO == 3 .OR. nOPCAO == 4,1,2)
	If(nOPCAO = 3,MSMM(,,,M->TRH_DESCRI,nSMM,,,"TRH","TRH_MMSYP"),;
	              MSMM(cCODMMSYP,,,M->TRH_DESCRI,nSMM,,,"TRH","TRH_MMSYP"))
	If(nOPCAO = 3,MSMM(,,,M->TRH_DANOS2,nSMM,,,"TRH","TRH_MMDAN2"),;
	              MSMM(cCODMMDAN2,,,M->TRH_DANOS2,nSMM,,,"TRH","TRH_MMDAN2"))
	EvalTrigger()  // Processa Gatilhos
Endif

MNTA545INA()

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA540ACI ³ Autor ³Marcos Wagner Junior  ³ Data ³ 08/02/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Limpa os campos referentes ao animal, caso nao seja '07'    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MNTA540                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA540ACI()

If M->TRH_TIPACI != '07'
	M->TRH_NOMPRO := Space(Len(M->TRH_NOMPRO))
	M->TRH_TELPRO := Space(Len(M->TRH_TELPRO))
	M->TRH_TIPANI := Space(Len(M->TRH_TIPANI))
	M->TRH_QTDANI := 0
	M->TRH_ENDPRO := Space(Len(M->TRH_ENDPRO))
	M->TRH_UFPRO  := Space(Len(M->TRH_UFPRO))
	M->TRH_CIDPRO := Space(Len(M->TRH_CIDPRO))
	M->TRH_VALANI := 0
Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA540EVE ³ Autor ³Marcos Wagner Junior  ³ Data ³ 08/02/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Limpa os campo de Gravidade caso nao seja '01'			     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MNTA540                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA540EVE()
Local nX := 0

If FunName() == "MNTA545" .AND. cEventoAn != M->TRH_EVENTO
	If !Empty(cEventoAn)
		For nX := 1 to Len(aSVCOLS[1])
		   If (!aSVCOLS[1][nx][Len(aSVCOLS[1][nx])]) .AND. (!Empty(aSVCOLS[1][nx][nTRICODDOC]))
   			MsgStop(STR0010,STR0006)//"Há documentos relacionados ao Evento que deverão ser deletados do folder 'Documentos'!"###"Atenção"
   			Return .f.
			Endif
		Next
	Else
		aSVCOLS[1] := BLANKGETD(aSVHEADER[1])
	Endif
Endif

If M->TRH_EVENTO != '1' .AND. M->TRH_EVENTO != '2'
	M->TRH_CAUPRE := Space(Len(M->TRH_CAUPRE))
Endif
/*If M->TRH_EVENTO != '02'
	M->TRH_VEIROU := Space(Len(M->TRH_VEIROU))
	M->TRH_RECVEL := Space(Len(M->TRH_RECVEL))
	M->TRH_SEQUES := Space(Len(M->TRH_SEQUES))
	M->TRH_MOTLIB := Space(Len(M->TRH_MOTLIB))
Endif*/
If M->TRH_EVENTO = '2'
	M->TRH_VEIROU := '2'
	M->TRH_RECVEL := '1'
Endif

cEventoAn := M->TRH_EVENTO

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ Rafael Diogo Richter  ³ Data ³02/02/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Local aRotina :=	{ { STR0001, "AxPesqui"  , 0 , 1},; //"Pesquisar"
      		        { STR0002, "NG540FOLD" , 0 , 2},; //"Visualizar"
		              { STR0003, "NG540FOLD" , 0 , 3}}  //"Incluir"

Return aRotina

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG540FOLD ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 03/03/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao para montatgem de folder                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA540                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG540FOLD(cALIAS1,nREG,nOPCX)
	Local y, nx, ny
	Local nCONTROL  := 0
	Local aPAGES    := {}
	Local aTITLES   := {}
	Local cGet,oGet

	Private aSize := MsAdvSize(,.f.,430)
	Private lOK       := .F.
	Private nOPCAO    := nOPCX
	Private aSVATELA1 := {}, aSVAGETS1 := {}
	Private aSVATELA2 := {}, aSVAGETS2 := {}
	Private INCLUI    := (nOPCAO == 3)
	Private ALTERA    := (nOPCAO == 4)
	Private nCONTROGD := 1
	Private oENC01,oENC02,oDLG
	Private cStatus
	Private cVerific
	Private cOs, lGETD := .F.
	Private oMenu
	Private aNoFields := {}
	Private aDimens   := {}
	Private cCODMMSYP  := TRH->TRH_MMSYP
	Private cCODMMDAN2 := TRH->TRH_MMDAN2
	Private aObjects   := {}

	aAdd( aObjects , { 200 , 200 , .T. , .F. } )
	aInfo   := { aSize[ 1 ] , aSize[ 2 ] , aSize[ 3 ] , aSize[ 4 ] , 0 , 0 }
	aPosObj := MsObjSize( aInfo , aObjects , .T. )
	aDimens := { 13 , 0 , 89 , aPosObj[ 1 , 4 ] }

	DbSelectArea("TRH")
	AADD(aTITLES,Oemtoansi(STR0007))  //"Sinistro"
	AADD(aPAGES,"HEADER 1")
	nCONTROL++

	If nOPCX # 2 .OR. nOPCX # 5 // Visual
	lGETD := .T.
	EndIf

	If nOpcx = 4 .or. nOpcx = 5
		If TRH->TRH_FLGCOM = '3'
			MsgStop(STR0009) //"Sinistro já finalizado"
			Return .f.
		EndIf
	EndIf

	Define MsDialog oDLG Title cCADASTRO From aSize[7],0 To aSize[6],aSize[5] Of oMAINWND Pixel
	oFOLDER := TFOLDER():New(1,0,aTITLES,aPAGES,oDLG,,,,.F.,.F.,320,200,)
	oFOLDER:aDIALOGS[1]:oFONT := oDLG:oFONT

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Enchoice 01                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	aTELA := {}
	aGETS := {}

	Aadd(aNoFields,"TRH_NOMPRO")
	Aadd(aNoFields,"TRH_ENDPRO")
	Aadd(aNoFields,"TRH_CIDPRO")
	Aadd(aNoFields,"TRH_UFPRO")
	Aadd(aNoFields,"TRH_TELPRO")
	Aadd(aNoFields,"TRH_TIPANI")
	Aadd(aNoFields,"TRH_QTDANI")
	Aadd(aNoFields,"TRH_VALANI")
	aChoice  := NGCAMPNSX3("TRH",aNoFields)

	DbSelectArea("TRH")
	RegtoMemory("TRH",(nOPCAO == 3))
	If nOPCAO == 3
		M->TRH_FLGCOM := '1'
	Endif
	oENC01:= MsMGet():New("TRH",nREG,nOPCAO,,,,aChoice,aDimens,,,,,,oFOLDER:aDIALOGS[1],,,.F.,"aSVATELA1")
	oENC01:oBox:Align := CONTROL_ALIGN_ALLCLIENT
	oENC01:oBOX:bGOTFOCUS := {|| NGENTR545("TRH")}
	oFOLDER:bSETOPTION    := {|x| NGSAIEN545("TRH",x)}
	oEnc01:oBox:bRClicked:= { |o,x,y| oMenu:Activate(x,y,	oEnc01)}

	aSVATELA1 := aCLONE(aTELA)
	aSVAGETS1 := aCLONE(aGETS)

	cStatus := M->TRH_STATUS

	@ 1000,1000 MsGet oGET Var cGET Picture "@!" Size 1,01 Of oFOLDER:aDIALOGS[1]

	NGPOPUP(asMenu,@oMenu)
	oDlg:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDLG)}
	oENC01:oBox:bRClicked := { |o,x,y| oMenu:Activate(x,y,oDLG)}

	Activate MsDialog oDLG On Init (ENCHOICEBAR(oDLG,{|| lOK:=.T.,If(MNTA540CHK(),oDLG:End(),lOK := .F.)},{|| nOK:= 0,MNTA540CAN()}),,;
											AlignObject(oDlg,{oFolder},1))

	If lOK
		DbSelectArea("TRH")
		If Altera .Or. nOpcx == 5
			RecLock("TRH",.F.)
		ElseIf Inclui
			dbSelectArea("TRH")
			dbSetOrder(1)
			If dbSeek(M->TRH_FILIAL+M->TRH_NUMSIN)
				M->TRH_NUMSIN := GetSXENUM("TRH","TRH_NUMSIN",M->TRH_FILIAL+x2path("TRH"))
			EndIf
			ConfirmSX8()
			RecLock("TRH",.T.)
			MNTW055()
			cFilAnt := M->TRH_FILIAL
		Endif

		DbSelectArea("TRH")
		If nOpcx == 5
			DbDelete()
		Else
			For ny := 1 To FCOUNT()
				nx := "m->" + FieldName(ny)
				If "_FILIAL"$Upper(nx)
				&nx. := cFilTRH
				Endif
				FieldPut(ny, &nx.)
			Next ny
		Endif
		MsUnlock("TRH")

		MNTA540GRA()

	Endif

Return .t.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTA540CHK
Consistência do botão OK.

@type function

@source MNTA540.prx

@author Marcos Wagner Junior
@since 03/03/08

@sample MNTA540CHK()

@return Logico, Indica se haverá gravação do sinistro.
/*/
//---------------------------------------------------------------------
Function MNTA540CHK()
	Local aArea 	:= GetArea()
	Local cQryAlias := ""
	Local cQuery	:= ""
	Local lMNTA5400 := ExistBlock( "MNTA5400" ) //Ponto de entrada para validar a inclusão
	Local lRet		:= .T.

	//Se Visualização ou Exclusão não deverá validar
	If !( nOpcao == 2 .OR. nOpcao == 5 )

		If !OBRIGATORIO(aSVAGETS1,aSVATELA1)
			RestArea(aArea)
			oFolder:nOption := 1
			lRet := .F.
		Endif

		If lRet .And. ( !MNTA545OBR() .OR. !MNTA545GUI() .OR. !MNTA545VLD() )
			oFolder:nOption := 1
			lRet := .F.
		Endif
		If lRet
			MNTASINGRA()

			cQryAlias := GetNextAlias()
			cQuery := " SELECT COUNT(*) AS TOTAL FROM " + RetSqlName("TRH")
			cQuery += " WHERE  TRH_FILIAL = '"+xFilial("TRH")+"' AND TRH_CODBEM = '"+M->TRH_CODBEM+"'""
			cQuery += "    AND TRH_DTACID = '"+DtoS(M->TRH_DTACID)+"'  AND TRH_HRACID = '"+M->TRH_HRACID+"'"
			cQuery += "    AND D_E_L_E_T_ = ' ' "
			cQuery := ChangeQuery(cQuery)
			dbUseArea( .T., "TOPCONN", TCGenQry(,,cQuery), cQryAlias, .F., .T.)

			If (cQryAlias)->TOTAL > 0
				MsgInfo(STR0019 + Chr(13) +; //"Não é possível cadastrar um sinistro para um bem com hora"
						STR0020, STR0006) //"e data já cadastrados."###"Atenção"
				lRet := .F.
			EndIf

			(cQryAlias)->(dbCloseArea())
		EndIf
		If lRet .And. lMNTA5400
			lRet := ExecBlock( "MNTA5400", .F., .F. )
		EndIf
	EndIf
	RestArea(aArea)

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA540CAN ³Autor ³ Marcos Wagner Junior  ³ Data ³ 03/03/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Desfaz a numera‡ao sequencial no cancelamento da inclusao  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA540                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTA540CAN()

RollBackSX8()
oDLG:End()

Return .t.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT540VALD()
Valida os danos do sinistro, desde que o parâmetro MV_NG1SERA esteja preenchido.

@author Diego de Oliveira
@since 05/06/2015
@version MP11
/*/
//---------------------------------------------------------------------
Function MNT540VALD()

	Local cNG1Sera := SuperGetMV( "MV_NG1SERA",.F.,"" )

	dbSelectArea("ST4")
	dbSetOrder(1)
	If dbSeek( xFilial( "ST4" ) + cNG1Sera )

		dbSelectArea("STE")
		dbSetOrder(1)
		If dbSeek( xFilial( "STE" ) + ST4->T4_TIPOMAN )
			If STE->TE_CARACTE <> "C" .And. M->TRH_FLGDAN == '1'
				MsgStop(STR0017) //"O serviço informado no parâmetro MV_NG1SERA deve ser do tipo corretivo."
				Return .F.
			EndIf
		EndIf
	ElseIf !Empty(cNG1Sera)
		MsgStop(STR0018) //"O serviço informado não existe no cadastro de serviços. Informe um serviço cadastrado ou inclua um novo."
		Return .F.
	EndIf

	If M->TRH_FLGDAN == '1' .AND. Empty(cNG1Sera)
		M->TRH_VALDAN := 0
		MsgStop(STR0016) //"Não foi informado o código do serviço para a O.S. O parâmetro MV_NG1SERA deve ser configurado para inclusão de sinistros com danos."
		Return .F.
	ElseIf M->TRH_FLGDAN == '2'
		M->TRH_VALDAN := 0
	EndIf

Return .T.
