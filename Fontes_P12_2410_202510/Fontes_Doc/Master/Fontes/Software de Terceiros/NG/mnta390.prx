#INCLUDE "mnta390.ch"
#INCLUDE "PROTHEUS.CH"  

/*/


Ŀ
Funo     MNTA390   Autor  Paulo Pego             Data           
Ĵ
Descrio  Programa para Retorno das OS de servico de Acompanhamento  
Ĵ
Objetivo   Este Programa Atualiza a posicao do contador e Var.Dia do  
           bem basedo na nova leitura e data do contador              
ٱ


/*/
Function MNTA390() 
Local aTMPFIELD, bTMPFUNC, cTMPBRW

//Ŀ
//Guarda conteudo e declara variaveis padroes 
//
Local aNGBEGINPRM := NGBEGINPRM()
Local lOk := .T.

Private asMenu

asMenu := NGRIGHTCLICK("MNTA390")

Private aRotina := MenuDef()
Private aIndTQA    := {}
Private bFiltraBrw := {|| Nil}
Private cCadastro  := OemtoAnsi(STR0009) //"Retorno Acompanhamento"
Private aPerg := {}
Private cPerg := "MNA390"
Private TIPOACOM
Private TIPOACOM2 
Private cCondicao

If "A" $ Alltrim(SuperGetMv("MV_NGLANEX",.F.,"-1"))
	MsgStop(STR0029,STR0028)//"Parmetro MV_NGLANEX restringe lanamentos do tipo Contador."
	lOk := .F.
EndIf

If lOk
	bNGGRAVA := {||MNTA390OB()}
	aPOS1    := {15,1,95,315}
	
	MNTA390PAR(.F.)
	
	mBrowse( 6, 1,22,75,"TQA",,'Empty(TQA->TQA_POSCON)')
	aEval(aIndTQA,{|x| Ferase(x[1]+OrdBagExt())})
	ENDFILBRW("TQA",aIndTQA)
	
	//Ŀ
	// Devolve a condicao original do arquivo principal             
	//
	dbSelectArea("TQA")
	Set Filter To
	dbSetOrder(1)
	dbSeek(xFILIAL("TQA"))
EndIf

//Ŀ
//Retorna conteudo de variaveis padroes       
//
NGRETURNPRM(aNGBEGINPRM)

Return

/*/


Ŀ
Funo    MNTA390PAR Autor  Felipe N. Welter       Data           
Ĵ
Descrio  Filtra o browse                                            
Ĵ
Parametros 1.lPerg - Chama a tela de parametros para filtro           
Ĵ
Uso        MNTA390                                                    
ٱ


/*/
Function MNTA390PAR(lPerg)

dbSelectArea("TQA")
//cCondicao := 'TQA_FILIAL == "'+ xFilial("TQA")+'".And. '
cCondicao := 'TQA_SITUAC == "L" .And. TQA_PLANO <> "000000" .And. TQA_TERMIN == "N"'

If lPerg
	
	If PERGUNTE("MNA390",.T.)
		cCondicao += ' .And. TQA_PLANO >= "'+MV_PAR01+'" .And. TQA_PLANO <= "'+MV_PAR02+'" '
		cCondicao += ' .And. DTOS(TQA_DTREAL) >= "'+DTOS(MV_PAR03)+'" .And. DTOS(TQA_DTREAL) <= "'+DTOS(MV_PAR04)+'" '
		cCondicao += ' .And. TQA_ORDEM >= "'+MV_PAR05+'" .And. TQA_ORDEM <= "'+MV_PAR06+'" '
		cCondicao += ' .And. TQA_CCUSTO >= "'+MV_PAR07+'" .And. TQA_CCUSTO <= "'+MV_PAR08+'" '
	EndIf
	
EndIf

bFiltraBrw := {|| FilBrowse("TQA",@aIndTQA,@cCondicao) }
Eval(bFiltraBrw)

Return

/*


Ŀ
Funo    MNTA390DT  Autor Felipe N. Welter        Data  20/12/08 
Ĵ
Descrio Valida os parametros de/ate Data de Abastecimento           
Ĵ
 Uso      MNTA390                                                     
ٱ


*/
Function MNTA390DT()

If MV_PAR04 < MV_PAR03
	MsgStop("Data final no pode ser inferior  data inicial!")
	Return .F.
EndIf

Return .T.

/*/


Ŀ
Funo     NG390RET  Autor  Paulo Pego             Data           
Ĵ
Descrio  Programa para Retorno das OS de servico de Acompanhamento  
Ĵ
Uso        MNTA390                                                    
ٱ


/*/
Function NG390RET(cAlias,nReg,nOpcx)
Local aRotold := aClone(aRotina)
Local cGERAPREV := AllTrim(GETMv("MV_NGGERPR"))

//DECLARADO A VARIAVEL AROTINA NOVAMENTE PARA PODER USAR NGCAD01, CASO SEJA RETIRADO
//NAO PODE MAS USAR O NGCAD01 POIS OCORRERA ERRO DE ACESSO A ARRAY DA aROTINA.
//DEVE SER REFEITO O PROGRAMA NOVAMENTE

aRotina := {{STR0004, "AxPesqui"  , 0 , 1}   ,;  //"Pesquisar"
            { STR0005, "NG390VIS" , 0 , 2, 0},;  //"Visualizar"
            { STR0006, "NG390Ret" , 0 , 4, 0},;  //"Retorno"
            { STR0006, "NG390Ret" , 0 , 4, 0} }

TIPOACOM  := .F.
TIPOACOM2 := .F.

dbSelectArea("TQA")
nINDATU := IndexOrd()

If !Empty(TQA->TQA_POSCON)
   Help(" ",1,"RETJAEXIST")
   Return .f.
Endif

nRecSTP := Recno()

dbSelectArea("ST9")
dbSetOrder(1)
dbSeek(xFILIAL("ST9")+TQA->TQA_CODBEM)
If ST9->T9_TEMCONT <> "N"
   TIPOACOM := .T.
EndIf

//FindFunction remover na release GetRPORelease() >= '12.1.027'
If FindFunction("MNTCont2")
	TIPOACOM2 := MNTCont2(xFILIAL("TPE"), TQA->TQA_CODBEM)
Else
   dbSelectArea("TPE")
   dbSetOrder(1)
   TIPOACOM2 := dbSeek(xFILIAL("TPE")+TQA->TQA_CODBEM)
EndIf

aCHOICE := {"TQA_ORDEM" ,"TQA_CODBEM","TQA_POSCON","TQA_DTLEI1",;
            "TQA_HORAC1","TQA_POSCO2","TQA_DTLEI2","TQA_HORAC2"}

nRet := NGCAD01(cAlias,nReg,4)
aRotina := aClone(aRotold)

If nRet == 1

   dbSelectArea("TQA")
   nIndexSTP  := IndexOrd()
   nRecSTP390 := Recno()

   If TIPOACOM
      NGTRETCON(TQA->TQA_CODBEM,TQA->TQA_DTLEI1,TQA->TQA_POSCON,TQA->TQA_HORAC1,1,,.T.)
   Endif

   If TIPOACOM2
      NGTRETCON(TQA->TQA_CODBEM,TQA->TQA_DTLEI2,TQA->TQA_POSCO2,TQA->TQA_HORAC2,2,,.F.)
   Endif

   dbSelectArea("TQA")
   dbGoto(nRecSTP390)
   Reclock("TQA",.F.)
   TQA->TQA_RETORN := "S"
   MsUnlock("TQA")

   //GERA O.S AUTOMATICA POR CONTADOR
   If (cGERAPREV = "S" .Or. cGERAPREV = "C") .And. (!Empty(TQA->TQA_POSCON) .Or. !Empty(TQA->TQA_POSCO2))
      If NGCONFOSAUT(cGERAPREV)
         NGGEROSAUT(TQA->TQA_CODBEM,If(!Empty(TQA->TQA_POSCON),TQA->TQA_POSCON,TQA->TQA_POSCO2))
      EndIf
   EndIf
EndIf
dbSelectArea("TQA")
dbSetOrder(nINDATU)
lRefresh := .t.

Return

/*/


Ŀ
Funo    MNTA390OB  Autor  Paulo Pego             Data           
Ĵ
Descrio  Verifica a obrigatoriedade,contadores e variacao dia       
Ĵ
 Uso       MNTA390                                                    
ٱ


/*/
Function MNTA390OB()

//CHECA OS CAMPOS OBRIGATORIOS
If !NGCONTRET(M->TQA_DTLEI1,M->TQA_POSCON,M->TQA_HORAC1,TIPOACOM ,;
               M->TQA_DTLEI2,M->TQA_POSCO2,M->TQA_HORAC2,TIPOACOM2,.T.)
   Return .F.
EndIf
//CHECA SE O CONTADOR E VALIDO

If TIPOACOM
   If !NGCHKHISTO(TQA->TQA_CODBEM,M->TQA_DTLEI1,M->TQA_POSCON,M->TQA_HORAC1,1,,.T.)
      Return .F.
   Endif
   If !NGVALIVARD(TQA->TQA_CODBEM,M->TQA_POSCON,M->TQA_DTLEI1,M->TQA_HORAC1,1,.t.)
      Return .F.
   Endif
Endif
If TIPOACOM2
   If !NGCHKHISTO(TQA->TQA_CODBEM,M->TQA_DTLEI2,M->TQA_POSCO2,M->TQA_HORAC2,2,,.T.)
      Return .F.
   Endif
   If !NGVALIVARD(TQA->TQA_CODBEM,M->TQA_POSCO2,M->TQA_DTLEI2,M->TQA_HORAC2,2,.t.)
      Return .F.
   Endif
Endif

Return .t.

/*/


Ŀ
Funo    NG390LEG   Autor  Felipe N. Welter       Data  29/06/09 
Ĵ
Descrio Cria uma janela contendo a legenda da mBrowse               
Ĵ
Uso       MNTA390                                                     
ٱ


/*/
Function NG390LEG()
BrwLegenda(cCadastro,STR0025,{{"BR_VERDE",Oemtoansi(STR0026)},; //"Legenda"##"Ordem Encerrada"
                              {"BR_VERMELHO",Oemtoansi(STR0027)}}) //"Aguardando Retorno"
Return .T.

/*/


Ŀ
Programa  MenuDef    Autor  Ricardo Dal Ponte      Data 29/11/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()

Local aRotina := {{ STR0004, "PesqBrw"  , 0 , 1}   ,;  //"Pesquisar"
						{ STR0005, "NGCAD01" , 0 , 2, 0},;  //"Visualizar"
						{ STR0006, "NG390Ret" , 0 , 4, 0},;  //"Retorno"
						{ STR0025, "NG390LEG" , 0 , 6, 0}} //"Legenda"
Return(aRotina)