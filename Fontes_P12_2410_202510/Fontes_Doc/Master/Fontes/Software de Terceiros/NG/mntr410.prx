#INCLUDE "MNTR410.ch"
#INCLUDE "PROTHEUS.CH"

/*/


Ŀ
Funo    MNTR410    Autor  Marcos Wagner Junior   Data  01/03/07 
Ĵ
Descrio  Relatorio de Infracoes Recebidas   			  					  
Ĵ
 Uso      Generico                                                    
ٱ


*/
Function MNTR410()

	Private NOMEPROG := "MNTR410"
	Private TAMANHO  := "P"
	Private aRETURN  := {STR0004,1,STR0005,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private TITULO   := STR0006 //"Relatrio de Infraes Recebidas"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private aVETINR  := {}
	Private cPERG    := "MNR410"
	Private aPerg    := {}
	Private CABEC1
	Private CABEC2

	WNREL      := "MNTR410"
	LIMITE     := 080
	cDESC1     := STR0001 //"O relatrio apresentar as quantidades de infraes recebidas no perodo. "
	cDESC2     := STR0002 //"Poder apresentar as seguintes estatsticas: infraes por operaes, "
	cDESC3     := STR0003 //"infraes por estado, infraes por rodovia, infraes por horrio. "
	cSTRING    := "TRX"

	SetKey( VK_F9, { | | NGVersao( "MNTR410" , 1 ) } )

	Pergunte(cPERG,.F.)

	//Envia controle para a funcao SETPRINT
	WNREL := SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")

	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("TRX")
		Return
	EndIf

	SetDefault(aReturn,cSTRING)
	RptStatus({|lEND| MNTR410IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0018,STR0019) //"Aguarde..."###"Processando Registros..."
	Dbselectarea("TRX")

Return .T.

/*/


Ŀ
Funo    |MNR410IMP | Autor  Marcos Wagner Junior   Data  01/03/07 
Ĵ
Descrio Chamada do Relatrio                                        
Ĵ
 Uso      MNTR410                                                     
ٱ


/*/
Function MNTR410IMP(lEND,WNREL,TITULO,TAMANHO)
Local nI := 0
Private cRODATXT := ""
Private nCNTIMPR := 0
Private li := 80 ,m_pag := 1
Private cNomeOri
Private aLinhas := {}
Private cDataIni := SubStr(DTOS(MV_PAR01),7,2) + "/" + SubStr(DTOS(MV_PAR01),5,2) + "/" + SubStr(DTOS(MV_PAR01),1,4)
Private cDataFim := SubStr(DTOS(MV_PAR02),7,2) + "/" + SubStr(DTOS(MV_PAR02),5,2) + "/" + SubStr(DTOS(MV_PAR02),1,4)
Private cDataPar := ('('+ cDataIni + STR0020 + cDataFim+')')  //" A "
Private nBusca, nSoma, nTotal

nTIPO  := IIf(aReturn[4]==1,15,18)

CABEC1 := ""
CABEC2 := ""

cAliasQry := GetNextAlias()
cQuery := " SELECT  TRX.TRX_UFINF, TRX.TRX_DTREC, TRX.TRX_DTINFR, TRX.TRX_RODOVI, "
cQuery += " 		  TRX.TRX_RHINFR, TRX.TRX_PLACA, TRX.TRX_CODINF, TRX.TRX_OPERAC "
cQuery += " FROM " + RetSqlName("TRX")+" TRX"
cQuery += " WHERE TRX.D_E_L_E_T_ <> '*' "
cQuery += "	AND   TRX.TRX_DTINFR  >= '"+DTOS(MV_PAR01)+"'"
cQuery += "	AND   TRX.TRX_DTINFR  <= '"+DTOS(MV_PAR02)+"'"
cQuery += "	AND   TRX.TRX_RHINFR  >= '"+MV_PAR03+"'"
cQuery += "	AND   TRX.TRX_RHINFR  <= '"+MV_PAR04+"'"
cQuery += "	AND   TRX.TRX_CODBEM  >= '"+MV_PAR05+"'"
cQuery += "	AND   TRX.TRX_CODBEM  <= '"+MV_PAR06+"'"
cQuery += "	AND   TRX.TRX_CODMO   >= '"+MV_PAR07+"'"
cQuery += "	AND   TRX.TRX_CODMO   <= '"+MV_PAR08+"'"
If MV_PAR09 = 2
	cQuery += " ORDER BY TRX.TRX_UFINF "
ElseIf MV_PAR09 = 3
	cQuery += " ORDER BY TRX.TRX_RODOVI "
Endif
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

If !Eof()
	nTotal := 0
	If MV_PAR09 = 4//HORARIO
		AADD(aLinhas,{STR0021+cDataPar,STR0022,0}) //"INFRAES POR HORA "###"00 HRS AS 01:59 HRS"
		AADD(aLinhas,{STR0021+cDataPar,STR0023,0})  //"INFRAES POR HORA "###"02 HRS AS 03:59 HRS"
		AADD(aLinhas,{STR0021+cDataPar,STR0024,0}) //"INFRAES POR HORA "###"04 HRS AS 05:59 HRS"
		AADD(aLinhas,{STR0021+cDataPar,STR0025,0}) //"INFRAES POR HORA "###"06 HRS AS 07:59 HRS"
		AADD(aLinhas,{STR0021+cDataPar,STR0026,0}) //"INFRAES POR HORA "###"08 HRS AS 09:59 HRS"
		AADD(aLinhas,{STR0021+cDataPar,STR0027,0}) //"INFRAES POR HORA "###"10 HRS AS 11:59 HRS"
		AADD(aLinhas,{STR0021+cDataPar,STR0028,0}) //"INFRAES POR HORA "###"12 HRS AS 13:59 HRS"
		AADD(aLinhas,{STR0021+cDataPar,STR0029,0})  //"INFRAES POR HORA "###"14 HRS AS 15:59 HRS"
		AADD(aLinhas,{STR0021+cDataPar,STR0030,0}) //"INFRAES POR HORA "###"16 HRS AS 17:59 HRS"
		AADD(aLinhas,{STR0021+cDataPar,STR0031,0}) //"INFRAES POR HORA "###"18 HRS AS 19:59 HRS"
		AADD(aLinhas,{STR0021+cDataPar,STR0032,0}) //"INFRAES POR HORA "###"20 HRS AS 21:59 HRS"
		AADD(aLinhas,{STR0021+cDataPar,STR0033,0})												 //"INFRAES POR HORA "###"22 HRS AS 23:59 HRS"
	Endif

	While !Eof()
		nSoma := 0
		If MV_PAR10 == 1 .OR. MV_PAR10 == 2//Ambas ou Sofridas
			MNTR410TPM()
		ElseIf MV_PAR10 == 3//Recebidas
	   	If !Empty((cAliasQry)->TRX_DTREC)
				MNTR410TPM()
	   	Endif
		Endif

		If MV_PAR09 = 1//OPERACAO
			cDescricao := ''
         If (cAliasQry)->TRX_OPERAC = '1'
         	cDescricao := STR0051 //"Coleta"
         ElseIf (cAliasQry)->TRX_OPERAC = '2'
         	cDescricao := STR0052 //"Transporte"
         ElseIf (cAliasQry)->TRX_OPERAC = '3'
         	cDescricao := STR0053 //"Entrega"
         ElseIf (cAliasQry)->TRX_OPERAC = '4'
         	cDescricao := STR0055 //"Terceiro"
         ElseIf (cAliasQry)->TRX_OPERAC = '5'
         	cDescricao := STR0054 //"Outros"
         Endif
			If !Empty(cDescricao)
				nBusca := aScan(aLinhas, {|x| x[2] == cDescricao})
				If nBusca == 0
			   	AADD(aLinhas,{STR0034+cDataPar,cDescricao,nSoma}) //"INFRAES POR OPERAO "
			 	Else
			 		aLinhas[nBusca][3] := aLinhas[nBusca][3] + nSoma
				Endif
				nTotal := nTotal + nSoma
			Endif
		ElseIf MV_PAR09 = 2//ESTADO
			nBusca := aScan(aLinhas, {|x| x[2] == (cAliasQry)->TRX_UFINF})
			If nBusca == 0
		   	AADD(aLinhas,{STR0035+cDataPar,(cAliasQry)->TRX_UFINF,nSoma}) //"INFRAES POR ESTADO "
		 	Else
		 		aLinhas[nBusca][3] := aLinhas[nBusca][3] + nSoma
			Endif
			nTotal := nTotal + nSoma
		ElseIf MV_PAR09 = 3//RODOVIA
			nBusca := aScan(aLinhas, {|x| x[2] == (cAliasQry)->TRX_RODOVI})
			If !Empty((cAliasQry)->TRX_RODOVI)
				If nBusca == 0
			   	AADD(aLinhas,{STR0036+cDataPar,(cAliasQry)->TRX_RODOVI,nSoma}) //"INFRAES POR RODOVIA "
			 	Else
			 		aLinhas[nBusca][3] := aLinhas[nBusca][3] + nSoma
				Endif
				nTotal := nTotal + nSoma
			Endif
		ElseIf MV_PAR09 = 4//HORARIO
			If (cAliasQry)->TRX_RHINFR >= '00:00' .AND. (cAliasQry)->TRX_RHINFR <= '01:59'
	      	aLinhas[1][3] := aLinhas[1][3] + nSoma
	      ElseIf (cAliasQry)->TRX_RHINFR >= '02:00' .AND. (cAliasQry)->TRX_RHINFR <= '03:59'
	      	aLinhas[2][3] := aLinhas[2][3] + nSoma
	      ElseIf (cAliasQry)->TRX_RHINFR >= '04:00' .AND. (cAliasQry)->TRX_RHINFR <= '05:59'
	      	aLinhas[3][3] := aLinhas[3][3] + nSoma
	      ElseIf (cAliasQry)->TRX_RHINFR >= '06:00' .AND. (cAliasQry)->TRX_RHINFR <= '07:59'
	      	aLinhas[4][3] := aLinhas[4][3] + nSoma
	      ElseIf (cAliasQry)->TRX_RHINFR >= '08:00' .AND. (cAliasQry)->TRX_RHINFR <= '09:59'
	      	aLinhas[5][3] := aLinhas[5][3] + nSoma
	      ElseIf (cAliasQry)->TRX_RHINFR >= '10:00' .AND. (cAliasQry)->TRX_RHINFR <= '11:59'
	      	aLinhas[6][3] := aLinhas[6][3] + nSoma
	      ElseIf (cAliasQry)->TRX_RHINFR >= '12:00' .AND. (cAliasQry)->TRX_RHINFR <= '13:59'
	      	aLinhas[7][3] := aLinhas[7][3] + nSoma
	      ElseIf (cAliasQry)->TRX_RHINFR >= '14:00' .AND. (cAliasQry)->TRX_RHINFR <= '15:59'
	      	aLinhas[8][3] := aLinhas[8][3] + nSoma
	      ElseIf (cAliasQry)->TRX_RHINFR >= '16:00' .AND. (cAliasQry)->TRX_RHINFR <= '17:59'
	      	aLinhas[9][3] := aLinhas[9][3] + nSoma
	      ElseIf (cAliasQry)->TRX_RHINFR >= '18:00' .AND. (cAliasQry)->TRX_RHINFR <= '19:59'
	      	aLinhas[10][3] := aLinhas[10][3] + nSoma
	      ElseIf (cAliasQry)->TRX_RHINFR >= '20:00' .AND. (cAliasQry)->TRX_RHINFR <= '21:59'
	      	aLinhas[11][3] := aLinhas[11][3] + nSoma
	      ElseIf (cAliasQry)->TRX_RHINFR >= '22:00' .AND. (cAliasQry)->TRX_RHINFR <= '23:59'
	      	aLinhas[12][3] := aLinhas[12][3] + nSoma
			Endif
		   nTotal := nTotal + nSoma
	   Endif
	   dbSelectArea(cAliasQry)
		dbSkip()
	End
Else
   MsgInfo(STR0037,STR0038) //"No existem dados para montar o relatrio!"###"ATENO"
   Return .F.
EndIf
(cAliasQry)->(dbCloseArea())

nCont := 0
If Len(aLinhas) > 0
	NgSomaLi(58)
	@ Li,000		 PSay aLinhas[1][1]
	@ Li,053		 PSay STR0039 //"QTDE"
	@ Li,063		 PSay '%'
	NgSomaLi(58)
	@ Li,000 	 Psay Replicate("-",080)
	NgSomaLi(58)
	For nI := 1 to Len(aLinhas)
		If (MV_PAR09 == 4) .OR. (MV_PAR09 != 4 .AND. aLinhas[nI][3] > 0)
	  		@ Li,000	 PSay aLinhas[nI][2]
	  		@ Li,053	 PSay aLinhas[nI][3]
			If (aLinhas[nI][3]/nTotal)*100	== 100
		   	@ Li,063 Psay '100.00%'
		 	ElseIf (aLinhas[nI][3]/nTotal)*100 >= 10
		 	   @ Li,063	Psay Transform(((aLinhas[nI][3]/nTotal)*100),"@E 99.99")+"%"
		   ElseIf (aLinhas[nI][3]/nTotal)*100 > 0
		 	   @ Li,063	Psay '0'+AllTrim(Transform(((aLinhas[nI][3]/nTotal)*100),"@E 99.99")+"%")
		 	Else
		   	@ Li,063 Psay '00.00%'
		   Endif
			NgSomaLi(58)
			nCont++
	   Endif
	Next
	If nCont == 0
	   MsgInfo(STR0037,STR0038) //"No existem dados para montar o relatrio!"###"ATENO"
	   Return .F.
	EndIf
	@ Li,000 	 Psay Replicate("-",080)
	NgSomaLi(58)
	@ Li,000		 PSay STR0040 //"TOTAL"
	@ Li,053	 	 PSay nTotal
	@ Li,063	 	 PSay '100.00%'
Else
	MsgInfo(STR0037,STR0038) //"No existem dados para montar o relatrio!"###"ATENO"
	Return .F.
Endif



RODA(nCNTIMPR,cRODATXT,TAMANHO)
//Ŀ
// Devolve a condicao original do arquivo principal             
//
RetIndex('TRX')
Set Filter To
Set Device To Screen
If aReturn[5] == 1
   Set Printer To
   dbCommitAll()
   OurSpool(WNREL)
EndIf
MS_FLUSH()

Return Nil

/*/


Ŀ
Funo    |MNTR410DT | Autor  Marcos Wagner Junior   Data  01/03/07 
Ĵ
Descrio Chamada do Relatrio                                        
Ĵ
 Uso      MNTR410                                                     
ٱ


/*/
Function MNTR410DT()

If !Empty(MV_PAR01) .AND. !Empty(MV_PAR02)
	If MV_PAR01 > MV_PAR02
		MsgStop(STR0041,STR0038)  //"De Data no poder ser maior que At Data!"###"Ateno"
		Return .f.
	Endif
Endif

Return .t.

/*/


Ŀ
Funo    |MNTR410TPM| Autor  Marcos Wagner Junior   Data  01/03/07 
Ĵ
Descrio Verifica o Parametro Tipo de Multa para sumarizar           
Ĵ
 Uso      MNTR410                                                     
ٱ


/*/
Function MNTR410TPM()

If MV_PAR11 == 1
	nSoma := 1
ElseIf MV_PAR11 == 2
	dbSelectArea("TSH")
	dbSetOrder(01)
	If dbSeek(xFilial("TSH")+(cAliasQry)->TRX_CODINF)
		If TSH->TSH_FLGTPM = '1'
			nSoma := 1
		Endif
	Endif
ElseIf MV_PAR11 == 3
	dbSelectArea("TSH")
	dbSetOrder(01)
	If dbSeek(xFilial("TSH")+(cAliasQry)->TRX_CODINF)
		If TSH->TSH_FLGTPM = '2'
			nSoma := 1
		Endif
	Endif
Endif

Return .t.

/*


Ŀ
Funo    |MNR410CC  | Autor Marcos Wagner Junior    Data  17/09/07 
Ĵ
Descrio |Valida os codigos De/Ate Motorista		                    
Ĵ
 Uso      MNTR410                                                     
ٱ


*/
Function MNR410CC(nOpc,cParDe,cParAte,cTabela)

If (Empty(cParDe) .AND. cParAte = 'ZZZZZZ')
	Return .t.
Else
	If nOpc == 1
		If Empty(cParDe)
			Return .t.
		Else
			lRet := IIf(Empty(cParDe),.t.,ExistCpo(cTabela,cParDe))
		   If !lRet
		      Return .f.
		   EndIf
		Endif
	ElseIf nOpc == 2
		If (cParAte == 'ZZZZZZ')
			Return .t.
		Else
	      lRet := IIF(ATECODIGO(cTabela,cParDe,cParAte,10),.T.,.F.)
	      If !lRet
	         Return .f.
	      EndIf
	   EndIf
	EndIf
Endif

Return .t.