#INCLUDE "mdta290.ch"
#Include "Protheus.ch"

#DEFINE _nVERSAO 4 //Versao do fonte
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MDTA290  ³ Autor ³Denis Hyroshi de Souza ³ Data ³28/07/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Resultados dos exames do Funcionario.                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDTA290

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Armazena variaveis p/ devolucao (NGRIGHTCLICK) 						  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

PRIVATE aRotina := MenuDef()
Private cCliMdtPs := Space(Len(SA1->A1_COD+SA1->A1_LOJA))
PRIVATE cCadastro := OemtoAnsi(STR0004) //"Resultados dos Exames do Funcionario"
PRIVATE cAlias    := "TM0"
PRIVATE cNUMFIC   := space(09)
PRIVATE cPrograma := "MDTA290"
Private lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )
Private aVetinr := {}

aCHKDEL := { }

If FindFunction("MDTRESTRI") .AND. !MDTRESTRI(cPrograma)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve variaveis armazenadas (NGRIGHTCLICK) 			 			  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	NGRETURNPRM(aNGBEGINPRM)
	Return .F.
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lSigaMdtps
	dbSelectArea("SA1")
	dbSetOrder(1)
	mBrowse( 6, 1,22,75,"SA1")
Else
	dbSelectArea("TM0")
	dbSetOrder(1)
	mBrowse( 6, 1,22,75,"TM0",,,,,,fCorFun() )
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve variaveis armazenadas (NGRIGHTCLICK) 					 	  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MDT290SRA  ³ Autor ³ Denis                   ³ Data ³29/06/10  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Filtra os funcionarios do cliente                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MDT290SRA()                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                                ³±±
±±³          ³                                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³  SIGAMDT                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function MDT290SRA()

Local aArea	:= GetArea()
Local oldROTINA := aCLONE(aROTINA)
Local oldCad := cCadastro
cCliMdtPs := SA1->A1_COD+SA1->A1_LOJA

nSizeSA1 := If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1]))
nSizeLo1 := If((TAMSX3("A1_LOJA")[1]) < 1,2,(TAMSX3("A1_LOJA")[1]))

aRotina :=	{{ STR0001  , "AxPesqui"   , 0 , 1 },; //"Pesquisar"
             { STR0002  , "NGCAD01"    , 0 , 2 },; //"Visualizar"
             { STR0003  , "MDT290LOTE" , 0 , 2 },; //"Result. Exames"
             { STR0025  , "GpLegend"   , 0 , 6, 0, .F.} } //"Legenda"

dbSelectArea("TM0")
Set Filter To TM0->TM0_CLIENT == SubStr(cCliMdtPs,1,nSizeSA1) .and. TM0->TM0_LOJA == SubStr(cCliMdtPs,nSizeSA1+1,nSizeLo1)
dbSetOrder(1)
mBrowse( 6, 1,22,75,"TM0",,,,,,fCorFun() )

dbSelectArea("TM0")
Set Filter To

aROTINA := aCLONE(oldROTINA)
RestArea(aArea)
cCadastro := oldCad

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDT290LOTE³ Autor ³Denis Hyroshi de Souza ³ Data ³28/07/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Resultado dos Exames                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MDT290LOTE()
Local oDLG3,oCbx1, oMenu3
Local aSize   := MsAdvSize(,.f.,430)
Local aInfo   := {aSize[4],aSize[5],aSize[6],0,0}
Local oDlgHCO
Local nLeft
Local oPnlExam
Local oTempTRB
Private dDataResul := stod(space(8))
Private cCodiResul := space(4)
Private dIndiResul := space(8)
Private cNomeResul := space(30)
Private cMarca := GetMark(), lInverte := .F.
Private aIndRes := {" ","NORMAL","ALTERADO"}
Private aVetinr := {}
aDBF := {}
AADD(aDBF,{"TM5_OK"    ,"C",02,0})
AADD(aDBF,{"TM5_EXAME" ,"C",If(TAMSX3("TM4_EXAME")[1] < 1, 6, TAMSX3("TM4_EXAME")[1]),0})
AADD(aDBF,{"TM5_NOMEXA","C",30,0})
AADD(aDBF,{"TM5_DTPROG","D",08,0})
AADD(aDBF,{"TM5_DTRESU","D",08,0})
AADD(aDBF,{"TM5_INDRES","C",08,0})
AADD(aDBF,{"TM5_CODRES","C",04,0})
AADD(aDBF,{"TM5_RESULT","C",30,0})

aTRB1 := {}
AADD(aTRB1,{"TM5_OK"    ,NIL," "    ,})
AADD(aTRB1,{"TM5_EXAME" ,NIL,STR0016,}) //"Exame"
AADD(aTRB1,{"TM5_NOMEXA",NIL,STR0019,}) //"Descricao do Exame"
AADD(aTRB1,{"TM5_DTPROG",NIL,STR0020,}) //"Dt. Programacao"
AADD(aTRB1,{"TM5_DTRESU",NIL,STR0021,}) //"Dt. Resultado"
AADD(aTRB1,{"TM5_INDRES",NIL,STR0022,}) //"Tipo Resultado"
AADD(aTRB1,{"TM5_RESULT",NIL,STR0023,}) //"Descr. Resultado"

//Cria TRB
oTempTRB := FWTemporaryTable():New( "TRBEX", aDBF )
oTempTRB:AddIndex( "1", {"TM5_DTPROG","TM5_EXAME"} )
oTempTRB:Create()

If !(Alltrim(GetTheme()) == "FLAT") .And. !SetMdiChild()
	aSize[6] := aSize[6]-50
	aSize[5] := aSize[5]-30
	aSize[4] := aSize[4]-02
	nLeft := 2
EndIf

DEFINE MSDIALOG oDlg3 TITLE OemToAnsi(STR0004+" - "+Alltrim(TM0->TM0_NOMFIC)) From 9,0 To 45,85 OF oMainWnd //"Resultados dos Exames do Funcionario"

oPnlExam							:=	tPanel():New(00,00,,oDlg3,,.F.)
oPnlExam:Align					:= CONTROL_ALIGN_ALLCLIENT

@ 05,10   SAY OemToAnsi(STR0005) PIXEL OF oPnlExam //"Ficha Medica"
@ 05,70   MSGET MDTHideCpo( TM0->TM0_NUMFIC, "TM0_NUMFIC" ) Picture "@!" PIXEL OF oPnlExam SIZE 40,08 WHEN .f.
@ 05,140  SAY OemToAnsi(STR0006) PIXEL OF oPnlExam				//"Nome Funcionario"
@ 05,190  MSGET MDTHideCpo( TM0->TM0_NOMFIC, "TM0_NOMFIC" ) Picture "@!" PIXEL OF oPnlExam SIZE 100,08 WHEN .f.

@ 20,10   SAY OemToAnsi(STR0007) PIXEL OF oPnlExam //"Data do Resultado"
@ 20,70  MSGET dDataResul Picture "99/99/99" PIXEL OF oPnlExam SIZE 45,08 WHEN .t. HasButton
@ 20,140  SAY OemToAnsi(STR0008) PIXEL OF oPnlExam                    //"Resultado"
@ 20,190  MSGET cCodiResul Picture "@!" PIXEL OF oPnlExam Size 30,08 Valid A290VALRES(cCodiResul,1) F3 "TMU" When .T. HasButton
@ 20,225  MSGET cNomeResul Picture "@!" PIXEL OF oPnlExam Size 99,08 When .F.

@ 35,10   SAY OemToAnsi(STR0024) PIXEL OF oPnlExam  //"Ind. Resultado"
@ 35,70   COMBOBOX oCbx1 VAR dIndiResul ITEMS aIndRes  SIZE 40,10 PIXEL OF oPnlExam

@ 45,10 TO 213,330 LABEL  STR0009 PIXEL OF oPnlExam //"Exames"

@ 55,35  BUTTON STR0010 OF oPnlExam SIZE 40,10 PIXEL ACTION (A290MOSTRA()) //"&Mostrar"
@ 55,80  BUTTON STR0011 OF oPnlExam SIZE 40,10 PIXEL ACTION (A290ALTERA()) //"&Alterar"
@ 55,125 BUTTON STR0012 OF oPnlExam SIZE 40,10 PIXEL ACTION (A290GRAVAR()) //"&Gravar"

oMARK								:= MsSelect():NEW("TRBEX","TM5_OK",,aTRB1,@lINVERTE,@cMARCA,{70,15,209,325},,,oPnlExam)
oMARK:bMARK						:= {|| MDTA290MAQ(cMarca,lInverte)}
oMARK:oBROWSE:lHASMARK		:= .T.
oMARK:oBROWSE:lCANALLMARK	:= .T.
oMARK:oBROWSE:bALLMARK		:= {|| MDTA290INV(cMarca) }

NgPopUp(asMenu,@oMenu3)
oDlg3:bRClicked:= { |o,x,y| oMenu3:Activate(x,y,oDlg3)}
ACTIVATE MSDIALOG oDlg3 ON INIT EnchoiceBar(oDlg3,{|| nOpca := 1,oDlg3:End()},{|| nOpca := 2,oDlg3:End()})CENTERED

Dbselectarea("TRBEX")
oTempTRB:Delete()
Return .t.
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MDTA290MAQ³ Autor ³Denis Hyroshi de Souza ³ Data ³ 25/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Inverte a marcacao do browse                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MDTA290MAQ(cMarca,lInverte)
Return .t.
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MDTA290INV³ Autor ³Denis Hyroshi de Souza ³ Data ³ 25/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Inverte a marcacao do browse                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MDTA290INV(cMarca)
Local aArea := GetArea()

Dbselectarea("TRBEX")
Dbgotop()
While !eof()
	TRBEX->TM5_OK := IF(TM5_OK == "  ",cMARCA,"  ")
	Dbskip()
End

RestArea(aArea)
Return .t.
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³A290VALRES³ Autor ³Denis Hyroshi de Souza ³ Data ³ 25/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Verifica se o codigo do resultado existe na tabela TMU     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function A290VALRES(cResCod,nTipoCpo)
Local aArea := GetArea()

If !Empty(cResCod)
	If !ExistCpo("TMU",cResCod)
		Return .f.
	Endif

	Dbselectarea("TMU")
	Dbsetorder(1)
	Dbseek(xFilial("TMU")+cResCod)
	If nTipoCpo == 2
		cDescResul := Substr(TMU->TMU_RESULT,1,30)
	Else
		cNomeResul := Substr(TMU->TMU_RESULT,1,30)
	Endif
EndIf
RestArea(aArea)
lRefresh := .t.
Return .t.
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³A290MOSTRA³ Autor ³Denis Hyroshi de Souza ³ Data ³ 25/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Carrega arquivo temporario com os exames abertos do funcion.³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function A290MOSTRA()

Dbselectarea("TM5")
Dbsetorder(1)
Dbseek(xFilial("TM5")+TM0->TM0_NUMFIC)
While !eof() .and. xFilial("TM5")+TM0->TM0_NUMFIC == TM5->TM5_FILIAL+TM5->TM5_NUMFIC

	If Empty(TM5->TM5_DTRESU)
		Dbselectarea("TM4")
		Dbsetorder(1)
		If Dbseek(xFilial("TM4")+TM5->TM5_EXAME)
			Dbselectarea("TRBEX")
			DbGotop()
			If !Dbseek(DtoS(TM5->TM5_DTPROG)+TM5->TM5_EXAME)
				TRBEX->(DbAppend())
				TRBEX->TM5_OK     := cMarca
				TRBEX->TM5_EXAME  := TM4->TM4_EXAME
				TRBEX->TM5_NOMEXA := TM4->TM4_NOMEXA
				TRBEX->TM5_DTPROG := TM5->TM5_DTPROG
				TRBEX->TM5_DTRESU := dDataResul
				TRBEX->TM5_INDRES := dIndiResul
				TRBEX->TM5_CODRES := cCodiResul
				TRBEX->TM5_RESULT := cNomeResul
			Else
				RecLock("TRBEX",.f.)
				If Empty(TRBEX->TM5_DTRESU)
					TRBEX->TM5_DTRESU := dDataResul
				Endif
				If Empty(TRBEX->TM5_INDRES)
					TRBEX->TM5_INDRES := dIndiResul
				Endif
				If Empty(TRBEX->TM5_CODRES)
					TRBEX->TM5_CODRES := cCodiResul
					TRBEX->TM5_RESULT := cNomeResul
				Endif
				Msunlock("TRBEX")
			Endif
		Endif
	Endif
	Dbselectarea("TM5")
	Dbskip()
End

Dbselectarea("TRBEX")
If Reccount() < 1
	MsgStop(STR0013) //"Não foi encontrado nenhum exame pendente."
	Return .t.
Endif
Dbselectarea("TRBEX")
DbGotop()
oMARK:oBROWSE:REFRESH(.T.)
Return .t.
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³A290ALTERA³ Autor ³Denis Hyroshi de Souza ³ Data ³ 25/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Altera  arquivo temporario com os exames abertos do funcion.³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function A290ALTERA()
Local nOpcc := 0
Local oDlg2,oCbx1, oMenu2
Local cCodigo    := TRBEX->TM5_CODRES
Local dDtresul   := TRBEX->TM5_DTRESU
Local dIndRes    := TRBEX->TM5_INDRES
Private cDescResul := TRBEX->TM5_RESULT

Dbselectarea("TRBEX")
If Reccount() < 1
	MsgStop(STR0014) //"Não é possível alterar. Arquivo Vazio"
	Return .t.
Endif

DEFINE MSDIALOG oDlg2 TITLE OemToAnsi(STR0015+Alltrim(TM0->TM0_NOMFIC)) From 0,0 To 12,70 OF oMainWnd //"Alteração de Exame - "

@ 2.6,1   SAY OemToAnsi(STR0016) //"Exame"
@ 2.6,8   MSGET TRBEX->TM5_EXAME Picture "@!" Size 35,10 When .F.
@ 2.6,15  MSGET TRBEX->TM5_NOMEXA Picture "@!" Size 100,08 When .F.

@ 3.7,1  SAY OemToAnsi(STR0017) //"Data da Programacao"
@ 3.7,8  MSGET TRBEX->TM5_DTPROG Picture "99/99/99" Size 45,08 When .F. HasButton
@ 3.7,15 SAY OemToAnsi(STR0007) //"Data do Resultado"
@ 3.7,21 MSGET dDtresul Picture "99/99/99" SIZE 45,10 Valid A290Valid(dDtresul) WHEN .t. HasButton

@ 4.8,1  SAY OemToAnsi(STR0008)                       //"Resultado"
@ 4.8,8  MSGET cCodigo    Picture "@!" Size 30,08 Valid A290VALRES(cCodigo,2) F3 "TMU" When .T. HasButton
@ 4.8,15 MSGET cDescResul Picture "@!" Size 120,08 When .F.

@ 5.9,1   SAY OemToAnsi(STR0024) //"Ind. Resultado"
@ 5.9,8   COMBOBOX oCbx1 VAR dIndRes ITEMS aIndRes SIZE 40,08 OF oDlg2

NgPopUp(asMenu,@oMenu2)
oDlg2:bRClicked:= { |o,x,y| oMenu2:Activate(x,y,oDlg2)}
ACTIVATE MSDIALOG oDlg2 ON INIT EnchoiceBar(oDlg2,{||nOpcc:=1,oDlg2:End()},{||oDlg2:End()}) CENTERED

If nOpcc = 1
	Dbselectarea("TRBEX")
	RecLock("TRBEX",.f.)
	TRBEX->TM5_DTRESU := dDtresul
	TRBEX->TM5_INDRES := dIndRes
	TRBEX->TM5_CODRES := cCodigo
	TRBEX->TM5_RESULT := cDescResul
	Msunlock("TRBEX")
	oMARK:oBROWSE:REFRESH(.T.)
Endif
Return .t.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³A290ALTERA³ Autor ³Denis Hyroshi de Souza ³ Data ³ 25/07/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Altera  arquivo temporario com os exames abertos do funcion.³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function A290GRAVAR()
Dbselectarea("TRBEX")
If Reccount() < 1
	MsgStop(STR0018) //"Não é possível gravar. Arquivo Vazio"
	Return .t.
Endif

Dbselectarea("TRBEX")
Dbgotop()
While !eof()
	If Empty(TRBEX->TM5_OK) .or. Empty(TRBEX->TM5_CODRES) .or. Empty(TRBEX->TM5_DTRESU) .or. Empty(TRBEX->TM5_INDRES)
		Dbskip()
		Loop
	Endif

	Dbselectarea("TM5")
	Dbsetorder(1)
	If Dbseek(xFilial("TM5")+TM0->TM0_NUMFIC+DTOS(TRBEX->TM5_DTPROG)+TRBEX->TM5_EXAME)
		RecLock("TM5",.f.)
		TM5->TM5_CODRES := TRBEX->TM5_CODRES
		TM5->TM5_DTRESU := TRBEX->TM5_DTRESU
		TM5->TM5_INDRES := If(Alltrim(TRBEX->TM5_INDRES)=="NORMAL","1","2")
		Msunlock("TM5")
	Endif

	Dbselectarea("TRBEX")
	Reclock("TRBEX",.F.)
	Dbdelete()
	Msunlock("TRBEX")
	Dbskip()
End
Dbselectarea("TRBEX")
Pack
DbGotop()
oMARK:oBROWSE:REFRESH(.T.)
Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ Rafael Diogo Richter  ³ Data ³29/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMDT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados         ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )
Local aRotina

If lSigaMdtps
	aRotina := { { STR0001,   "AxPesqui"  , 0 , 1},; //"Pesquisar"
	             { STR0002,   "NGCAD01"   , 0 , 2},; //"Visualizar"
	             { STR0026,   "MDT290SRA" , 0 , 4} } //"Funcionários"
Else
	aRotina :=	{{ STR0001  , "AxPesqui"   , 0 , 1 },; //"Pesquisar"
                 { STR0002  , "NGCAD01"    , 0 , 2 },; //"Visualizar"
                 { STR0003  , "MDT290LOTE" , 0 , 2 },; //"Result. Exames"
                 { STR0025  , "GpLegend"   , 0 , 6, 0, .F.} } //"Legenda"
Endif

Return aRotina

//---------------------------------------------------------------------
/*/{Protheus.doc} A290Valid
Valida data do resultado dos exames.

@author Rodrigo Soledade
@since 16/09/2013
@return logical
@version MP11
/*/
//---------------------------------------------------------------------
Static Function A290Valid(dDtresul)

	Local lRet := .T.

	If Vazio( dDtresul )
		ShowHelpDlg( "NVAZIO" ,	{ STR0027 } , 1 , ;//"Este campo não pode ser vazio."
								{ STR0028 } , 1 )//"Preencha o valor da data de resultado."
		lRet := .F.
	EndIf

Return lRet