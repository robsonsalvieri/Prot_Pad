#Include "MNTR890.ch"
#Include "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR890   ³ Autor ³ Evaldo Cevinscki Jr.  ³ Data ³17/10/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio de Abastecimentos c/ Custos e Consumo             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR890()

    Local aNGBeginPrm := {}

    If !FindFunction( 'MNTAmIIn' ) .Or. MNTAmIIn( 19, 95 )

        aNGBeginPrm := NGBeginPrm()

        Private WNREL       := "MNTR890"
        Private LIMITE      := 220
        Private cDESC1      := STR0001 //"Relatório de Abastecimentos c/ Custos e Consumo"
        Private cDESC2      := " "
        Private cDESC3      := " "
        Private cSTRING     := "TQN"

        Private NOMEPROG := "MNTR890"
        Private TAMANHO  := "G"
        Private aRETURN  := {STR0002,1,STR0003,1,2,1,"",1} //"Zebrado"###"Administracao"
        Private nTIPO    := 0
        Private nLASTKEY := 0
        Private aVETINR  := {}
        Private cPERG    := "MNR890"
        Private aPerg    := {}
        Private aHelp    := {}
        Private CABEC1,CABEC2

        /*---------------------------------------------------------------
        Vetor utilizado para armazenar retorno da função MNT045TRB,
        criada de acordo com o item 18 (RoadMap 2013/14)
        ---------------------------------------------------------------*/
        Private vFilTRB := MNT045TRB()

        SetKey(VK_F4, {|| MNT045FIL( vFilTRB[2] )})

        Pergunte(cPERG,.F.)

        TITULO := STR0001 //"Relatório de Abastecimentos c/ Custos e Consumo"

        //+----------------------------------------+
        //| Envia controle para a funcao SETPRINT  |
        //+----------------------------------------+
        WNREL := SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")

        SetKey(VK_F4, {|| })

        If nLASTKEY = 27
            Set Filter To
            dbSelectArea("TQN")

            MNT045TRB( .T., vFilTRB[1], vFilTRB[2])

            Return
        EndIf
        SetDefault(aReturn,cSTRING)

        RptStatus({|lEND| MNTR890IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0019,STR0020)   //"Aguarde..."###"Processando Registros..."

        dbSelectArea("TQN")

        MNT045TRB( .T., vFilTRB[1], vFilTRB[2])

        NGReturnPrm( aNGBeginPrm )

    EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNR890IMP
Chamada do Relatório

@return Nil

@sample
MNR890IMP()

@author Evaldo Cevinscki Jr.
@since 17/10/2007
@version 1.0
/*/
//---------------------------------------------------------------------
Function MNTR890IMP(lEND,WNREL,TITULO,TAMANHO)

    Local nTotKmPerc  := 0
	Local nTotQtd     := 0
	Local nTotVal     := 0
	Local nTotKmF     := 0
	Local nTotQtdF    := 0
	Local nTotValF    := 0
	Local nQtdFroF    := 0
	Local nTotalG  	  := 0
	Local nTotKmG  	  := 0
	Local nTotQtG	  := 0
	Local nTotVaG	  := 0
	Local nQtdFroG	  := 0
	Local nKmPerc	  := 0
	Local cCodFrota   := ""
	Local cCodFam     := ""
	Local cCodTipMo   := ""
	Local cQuebra     := ""
	Local nMoedaRel   := 1
	Local cFam        := ""
	Local cMod        := ""
	Local cOldQuery   := ""
    Local lMMoeda     := NGCADICBASE("TL_MOEDA","A","STL",.F.) // Multi-Moeda
    Local oARQTRAB    := Nil
    Local lTemReg     := .F. //Variavel que valida se existe registros para montar o relatorio

    Private aVETINR   := {}
    Private cAliasQry := ""
	Private cAliasAux := ""
    Private cRODATXT  := ""
    Private nCNTIMPR  := 0
    Private li        := 80
	Private m_pag     := 1

    Store 0 To nTotKmF, nTotQtdF, nTotValF, nQtdFroF

    If lMMoeda .And. FindFunction("MNTR750MD")
        nMoedarel := MNTR750MD()
    Endif

    nTIPO  := IIF( aReturn[4] == 1, 15, 18 )

    If MV_PAR11 = 1 .OR. MV_PAR11 = 2
        cCABEC1 := STR0021 + Space( 8 )  + STR0031 + Space( 12 ) + STR0032 + Space( 7 ) + STR0033  //" Frota             Família Desc.Família         Modelo     Desc.Modelo          Média Km/L  Custo Médio/L  Tot.Consumo L    Tot.Custo"
    ElseIf MV_PAR11 = 3
        cCABEC1 := STR0022 + Space( 13 ) + STR0031 + Space( 12 ) + STR0032 + Space( 7 ) + STR0033 //"Família    Desc.Família         Qtd.Frotas  Média Km/L  Custo Médio/L  Tot.Consumo L    Tot.Custo"
    ElseIf MV_PAR11 = 4
        cCABEC1 := STR0023 + Space( 13 ) + STR0031 + Space( 12 ) + STR0032 + Space( 7 ) + STR0033 //"Modelo     Desc.Modelo          Qtd.Frotas  Média Km/L  Custo Médio/L  Tot.Consumo L    Tot.Custo"
    EndIf

    CABEC1 := cCABEC1
    CABEC2 := " "
    /*
    //Se Mv_Par11 = 1 ou 2
    1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7
    01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
    *****************************************************************************************************************************************************************************
    Relatório de Abastecimentos c/ Custos e Consumo
    *****************************************************************************************************************************************************************************
    Frota             Família Desc.Família         Modelo     Desc.Modelo          Média Km/L  Custo Médio/L  Tot.Consumo L    Tot.Custo   Tot.Km    Custo/Km  Média L /Km
    *****************************************************************************************************************************************************************************
    Se MV_PAR11 = 1
    Filial: XX - XXXXXXXXXXXXXXXXXXXX   Familia: XXXXXX - XXXXXXXXXXXXXXXXXXXXX
    SenaoSe MV_PAR11 = 2
    Filial: XX - XXXXXXXXXXXXXXXXXXXX   Tipo Modelo: XXXXXXXXXX - XXXXXXXXXXXXXXXXXXXXX
    xxxxxxxxxxxxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxx xxxxxxxxxxxxxxxxxxxx    999.999        999.999    999,999.999  999,999.999  999.999    999.9999
    xxxxxxxxxxxxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxx xxxxxxxxxxxxxxxxxxxx    999.999        999.999    999,999.999  999,999.999  999.999    999.9999
    xxxxxxxxxxxxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxx xxxxxxxxxxxxxxxxxxxx    999.999        999.999    999,999.999  999,999.999  999.999    999.9999
    Se MV_PAR11 = 1
    Total Família
    Total Filial
    SenaoSe MV_PAR11 = 2
    Total Modelo
    Total Filial
    */
    /*
    //Se Mv_Par11 = 3 ou 4
    1         2         3         4         5         6         7         8         9         0         1         2         3
    0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
    *************************************************************************************************************************************
    Relatório de Abastecimentos c/ Custos e Consumo
    *************************************************************************************************************************************
    Família    Desc.Família         Qtd.Frotas  Média Km/L  Custo Médio/L  Tot.Consumo L    Tot.Custo   Tot.Km    Custo/Km  Média L /Km
    Modelo     Desc.Modelo          Qtd.Frotas  Média Km/L  Custo Médio/L  Tot.Consumo L    Tot.Custo   Tot.Km    Custo/Km  Média L /Km
    *************************************************************************************************************************************
    Filial: XX - XXXXXXXXXXXXXXXXXXXX
    xxxxxxyyyy  xxxxxxxxxxxxxxxxxxxx     9,999     999.999        999.999    999,999.999  999,999.999  999.999    999.9999
    xxxxxxyyyy  xxxxxxxxxxxxxxxxxxxx     9,999     999.999        999.999    999,999.999  999,999.999  999.999    999.9999
    xxxxxxyyyy  xxxxxxxxxxxxxxxxxxxx     9,999     999.999        999.999    999,999.999  999,999.999  999.999    999.9999
    Total
    */

	//Define ordenação que será aplicada a query
	If MV_PAR11 = 1 .Or. MV_PAR11 = 3
        aOrderQry := {{"TQN","TQN_FILIAL"},{"ST9","T9_TPCONTA"},{"ST9","T9_CODFAMI"},{"ST9","T9_TIPMOD"},{"TQN","TQN_FROTA"}}
    ElseIf MV_PAR11 = 2 .Or. MV_PAR11 = 4
        aOrderQry := {{"TQN","TQN_FILIAL"},{"ST9","T9_TPCONTA"},{"ST9","T9_TIPMOD"},{"ST9","T9_CODFAMI"},{"TQN","TQN_FROTA"}}
    EndIf

    cAliasQry := GetNextAlias()

    cQuery := " SELECT"
	cQuery +=     " TQN.TQN_FILIAL,"
	cQuery +=     " TQN.TQN_FROTA ,"
	cQuery +=     " ST9.T9_TPCONTA,"
	cQuery +=     " TQN.TQN_DTABAS,"
	cQuery +=     " TQN.TQN_HRABAS,"
    cQuery +=     " TQN.TQN_FILORI,"
    If !lMMoeda
        cQuery += " TQN.TQN_QUANT AS QTD, TQN.TQN_VALTOT AS VALT, "
    Else
        cQuery += " TQN.TQN_QUANT AS QTD, TQN.TQN_VALTOT AS VALT, ST9.T9_FILIAL, TQN.TQN_MOEDA, "
    EndIf
    cQuery +=     " ST9.T9_CODFAMI,"
	cQuery +=     " ST9.T9_TIPMOD,"
    cQuery +=     " (SELECT TQR.TQR_DESMOD FROM " + RetSqlName("TQR") + " TQR
	cQuery +=     " WHERE TQR.TQR_TIPMOD = ST9.T9_TIPMOD
	cQuery +=            " AND TQR.D_E_L_E_T_ = ' ' "
    cQuery +=            " AND " + NGMODCOMP('ST9', 'TQR')
    cQuery +=     " ) AS MODELO,"
    cQuery +=     " (SELECT ST6.T6_NOME FROM " + RetSqlName("ST6") + " ST6"
	cQuery +=      " WHERE ST6.T6_CODFAMI = ST9.T9_CODFAMI
	cQuery +=            " AND ST6.D_E_L_E_T_ = ' ' "
    cQuery +=            " AND " + NGMODCOMP('ST9', 'ST6')
    cQuery +=     " ) AS FAMILIA "
    cQuery += " FROM " + RetSqlName("ST9") + " ST9," + RetSqlName("TQN") + " TQN "
    cQuery += " WHERE TQN.TQN_DTABAS BETWEEN " + ValToSql(DtoS(MV_PAR09)) + " AND " + ValToSql(DtoS(MV_PAR10))

	cQuery +=   " AND TQN.TQN_FILIAL BETWEEN " + ValToSql(xFilial("TQN",MV_PAR01)) + " AND " + ValToSql(xFilial("TQN",MV_PAR02))

    cQuery +=       " AND " + NGMODCOMP('ST9', 'TQN')
    cQuery +=       " AND TQN.TQN_FROTA BETWEEN " + ValToSql(MV_PAR07) + " AND " + ValToSql(MV_PAR08)
    cQuery +=       " AND TQN.TQN_FROTA = ST9.T9_CODBEM "
    cQuery +=       " AND ST9.T9_CODFAMI BETWEEN " + ValToSql(MV_PAR03) + " AND " + ValToSql(MV_PAR04)
    cQuery +=       " AND ST9.T9_TIPMOD BETWEEN " + ValToSql(MV_PAR05) + " AND " + ValToSql(MV_PAR06)
	cQuery +=       " AND ST9.D_E_L_E_T_ = ' '"
	cQuery +=       " AND TQN.D_E_L_E_T_ = ' '"
    If Len(aOrderQry) > 0
        cQuery += " ORDER BY " + MNT890ORDQ(aOrderQry, ",")
    EndIf

    cOldQuery := cQuery
    cQuery := ChangeQuery(cQuery)
    dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .F. )

    cAliasAux := GetNextAlias()

	aDBFAux := {}
	aAdd( aDBFAux,{ "TQN_FILIAL", "C", FwSizeFilial(), 0 }) //Função para tratar tamanho do campo
    aAdd( aDBFAux, { 'TQN_FILORI', 'C', FwSizeFilial(), 0 } )
    aAdd( aDBFAux,{ "TQN_FROTA" , "C", 16, 0 })
	aAdd( aDBFAux,{ "TQN_DTABAS", "D", 08, 0 })
	aAdd( aDBFAux,{ "TQN_HRABAS", "C", 05, 0 })
    aAdd( aDBFAux,{ "T9_TPCONTA", "C", 20, 0 })
    aAdd( aDBFAux,{ "TQN_CODCOM", "C", 03, 0 })
    aAdd( aDBFAux,{ "QTD"       , "N", 14, 2 }) //TQN->TQN_QUANT
    aAdd( aDBFAux,{ "VALT" 		, "N", 16, 2 }) //TQN->TQN_VALTOT
    aAdd( aDBFAux,{ "T9_CODFAMI", "C", 06, 0 })
    aAdd( aDBFAux,{ "T9_TIPMOD" , "C", 10, 0 })
    aAdd( aDBFAux,{ "MODELO" 	, "C", 20, 0 }) //TQR->TQR_TIPMOD
    aAdd( aDBFAux,{ "FAMILIA" 	, "C", 40, 0 }) //ST6->T6_CODFAMI
    aAdd( aDBFAux,{ "TQM_UM" 	, "C", 02, 0 })

    //Cria Indice
    aIndAux   := { aClone( MNT890ORDQ(aOrderQry, ",", .F.) ) }
    //Cria Tabela Temporário
    oARQTRAB := NGFwTmpTbl(cAliasAux, aDBFAux, aIndAux)

    If !lMMoeda
        SqlToTRB(cOldQuery, aDBFAux, cAliasAux)
    Else
        MNT890CALM()
    Endif

    SetRegua(LastRec())
    dbGoTop()

    If ( cAliasAux )->( Eof() )
        MsgInfo(STR0024,STR0025)   //"Não existem dados para montar o relatório."###"ATENÇÃO"
        Return .F.
    Endif

    cTipCont := " "
    cUM      := " "

    While ( cAliasAux )->( !Eof() )

        If MNT045STB( (cAliasAux)->TQN_FROTA, vFilTRB[2] )
            dbSkip()
            Loop
        EndIf

        nKmPerc := 0
        nTotQtd := 0
        nTotVal := 0

        cQuebra  := IIf(MV_PAR11 = 1 .Or. MV_PAR11 = 3,(cAliasAux)->T9_CODFAMI,(cAliasAux)->T9_TIPMOD)
        cQuebFtr := IIf(MV_PAR11 = 1 .Or. MV_PAR11 = 3,'(cAliasAux)->T9_CODFAMI','(cAliasAux)->T9_TIPMOD')
        cQuebCab := IIf(MV_PAR11 = 1 .Or. MV_PAR11 = 3,STR0026,STR0027) //'     Família: '###'     Modelo : '
        cFilQuer := (cAliasAux)->TQN_FILIAL //IIF(NGSX2MODO("TQN")=="C",SM0->M0_CODFIL,(cAliasAux)->TQN_FILIAL)

        dbSelectArea("SM0")
        SM0->(DbSetOrder(1))

        MsSeek(cEMPANT+cFilQuer)

        SomaLinha()
		
        //Se a filial estiver toda compartilhada não apresenta filial e descrição da filial
		If FWModeAccess("TQN",1)=="E"
			If MV_PAR11 = 1 .OR. MV_PAR11 = 2
				@ Li,000 PSay STR0028 + " : " + cFilQuer + " - " + AllTrim(FWFilialName(cEmpAnt, cFilQuer, 1)) + cQuebCab + cQuebra  //"Filial"
			Else
				@ Li,000 PSay STR0028 + " : " + cFilQuer + " - " + SM0->M0_FILIAL //"Filial"
			EndIf
		EndIf

        dbSelectArea(cAliasAux)
        While !Eof() .And. (cAliasAux)->TQN_FILIAL = cFilQuer .And. &cQuebFtr = cQuebra

            // Filtro direto na TQN, definido na tela de configuração da impressão.
            If ( !Empty( aReturn[7] ) )

                dbSelectArea( 'TQN' )
                dbSetOrder( 1 )
                If !MsSeek( (cAliasAux)->TQN_FILIAL + (cAliasAux)->TQN_FROTA + DtoS( (cAliasAux)->TQN_DTABAS ) + (cAliasAux)->TQN_HRABAS )

                    dbSelectArea(cAliasAux)
                    (cAliasAux)->( dbSkip() )

                    Loop

                EndIf

            EndIf

            IncRegua()

            nKmPerc  := 0
            nTotQtd  := 0
            nTotVal  := 0

            /*
            1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7
            01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
            *****************************************************************************************************************************************************************************
            Relatório de Abastecimentos c/ Custos e Consumo
            *****************************************************************************************************************************************************************************
            Frota             Família Desc.Família         Modelo     Desc.Modelo     |    Média Km/L  Custo Médio/L  Tot.Consumo L    Tot.Custo   Tot.Km    Custo/Km  Média L /Km
            *****************************************************************************************************************************************************************************
            Se MV_PAR11 = 1
            Filial: XX - XXXXXXXXXXXXXXXXXXXX   Familia: XXXXXX - XXXXXXXXXXXXXXXXXXXXX
            SenaoSe MV_PAR11 = 2
            Filial: XX - XXXXXXXXXXXXXXXXXXXX   Tipo Modelo: XXXXXXXXXX - XXXXXXXXXXXXXXXXXXXXX
            xxxxxxxxxxxxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxx xxxxxxxxxxxxxxxx 999,999,999.99        999.999    999,999.999  999,999.999  999.999    999.9999
            xxxxxxxxxxxxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxx xxxxxxxxxxxxxxxxxxxx    999.999        999.999    999,999.999  999,999.999  999.999    999.9999
            xxxxxxxxxxxxxxxx  xxxxxx  xxxxxxxxxxxxxxxxxxxx xxxxxxxxxx xxxxxxxxxxxxxxxxxxxx    999.999        999.999    999,999.999  999,999.999  999.999    999.9999
            */

            If (cAliasAux)->TQN_FILIAL <> cFilQuer .Or. &cQuebFtr <> cQuebra
                
                SomaLinha()

                @ Li,000 pSay STR0029+SubStr(cQuebCab,6,Len(cQuebCab))+cQuebra
                @ Li,082 pSay ( nTotKmPerc / nTotQtdF ) Picture '@E 999,999.999'        // Total de media ho/l
                @ Li,095 pSay ( nTotValF / nTotQtdF )   Picture '@E 99,999,999.999'     // Total de custo medio/l
                @ Li,112 pSay nTotQtdF                  Picture '@E 99,999,999.999'     // Total de tot. consumo/l
                @ Li,128 pSay nTotValF                  Picture '@E 999,999,999,999.99' // Total de tot. custo
                @ Li,148 pSay nTotKmPerc                Picture '@E 9,999,999.99'       // Total de tot. ho
                @ Li,162 pSay ( nTotValF / nTotKmPerc )	Picture '@E 9,999,999,999.9999' // Total de custo/ho
                @ Li,182 pSay ( nTotQtdF / nTotKmPerc )	Picture '@E 999,999.999'        // Total de media l/ho

                nTotKmPerc := 0
                nTotKmF    := 0
                nTotQtdF   := 0
                nTotValF   := 0

            EndIf

            cCodFrota := (cAliasAux)->TQN_FROTA
			While !Eof() .And. (cAliasAux)->TQN_FROTA == cCodFrota

                // Reaplica o filtro para os registros do veiculo.
                If ( !Empty( aReturn[7] ) )
    
                    dbSelectArea( 'TQN' )
                    dbSetOrder( 1 )
                    If !MsSeek( (cAliasAux)->TQN_FILIAL + (cAliasAux)->TQN_FROTA + DtoS( (cAliasAux)->TQN_DTABAS ) + (cAliasAux)->TQN_HRABAS )
    
                        dbSelectArea(cAliasAux)
                        (cAliasAux)->( dbSkip() )
    
                        Loop
    
                    EndIf
    
                EndIf

				nKmPerc    += NGCONTPER( (cAliasAux)->TQN_FROTA, DToS( (cAliasAux)->TQN_DTABAS ), (cAliasAux)->TQN_HRABAS, , FWxFilial( 'STP', (cAliasAux)->TQN_FILORI ) )
				nTotQtd    += (cAliasAux)->QTD
				nTotVal    += (cAliasAux)->VALT

				cFam := SubStr((cAliasAux)->FAMILIA,1,20)
				cMod := SubStr((cAliasAux)->MODELO,1,20)

				cCodFrota := (cAliasAux)->TQN_FROTA
				cCodFam   := (cAliasAux)->T9_CODFAMI
				cCodTipMo := (cAliasAux)->T9_TIPMOD
                cQuebra  := IIf(MV_PAR11 = 1 .Or. MV_PAR11 = 3,(cAliasAux)->T9_CODFAMI,(cAliasAux)->T9_TIPMOD)

                lTemReg := .T.

				dbSelectArea(cAliasAux)
            	dbSkip()
			End

            ++nQtdFroF
			++nQtdFroG
            nTotKmPerc += nKmPerc
            nTotKmF    += nKmPerc
			nTotQtdF   += nTotQtd
			nTotValF   += nTotVal

			If MV_PAR11 == 1 .OR. MV_PAR11 == 2
                
                SomaLinha()

                @ Li,000 pSay cCodFrota	            Picture '@!'                    // Frota
                @ Li,018 pSay cCodFam	            Picture '@!'                    // Família
                @ Li,026 pSay cFam	                Picture '@!'                    // Desc.Família
                @ Li,047 pSay cCodTipMo	            Picture '@!'                    // Modelo
                @ Li,058 pSay cMod		            Picture '@!'                    // Desc. Modelo
                @ Li,083 pSay ( nKmPerc / nTotQtd )	Picture '@E 99,999.999'         // Media Km/L
                @ Li,095 pSay ( nTotVal / nTotQtd )	Picture '@E 99,999,999.999'     // Custo Medio/L
                @ Li,111 pSay nTotQtd               Picture '@E 999,999,999.999'    // Tot.Consumo L
                @ Li,128 pSay nTotVal               Picture '@E 999,999,999,999.99' // Tot. Custo
                @ Li,148 pSay nKmPerc               Picture '@E 9,999,999.99'       // Tot. KM
                @ Li,162 pSay ( nTotVal / nKmPerc ) Picture '@E 9,999,999,999.9999' // Custo/KM
                @ Li,182 pSay ( nTotQtd / nKmPerc ) Picture '@E 999,999,999.999'    // Media KM/L

            EndIf

        EndDo

        If !lTemReg

            MsgInfo(STR0024,STR0025)   //"Não existem dados para montar o relatório."###"ATENÇÃO"
            Return .F.

        Endif

        If (MV_PAR11 = 1 .OR. MV_PAR11 = 2) //Imprime totais

            If (cAliasAux)->TQN_FILIAL <> cFilQuer .Or. &cQuebFtr <> cQuebra .OR. (cAliasAux)->(Eof())
                
                SomaLinha()
                
                @ Li,000 pSay STR0029+SubStr(cQuebCab,6,Len(cQuebCab))+cQuebra
                @ Li,082 pSay ( nTotKmPerc / nTotQtdF ) Picture '@E 999,999.999'        // Total de media ho/l
                @ Li,095 pSay ( nTotValF / nTotQtdF )   Picture '@E 99,999,999.999'     // Total de custo medio/l
                @ Li,112 pSay nTotQtdF                  Picture '@E 99,999,999.999'     // Total de tot. consumo/l
                @ Li,128 pSay nTotValF                  Picture '@E 999,999,999,999.99' // Total de tot. custo
                @ Li,148 pSay nTotKmPerc                Picture '@E 9,999,999.99'       // Total de tot. ho
                @ Li,162 pSay ( nTotValF / nTotKmPerc ) Picture '@E 9,999,999,999.9999' // Total de custo/ho
                @ Li,182 pSay ( nTotQtdF / nTotKmPerc )	Picture '@E 999,999,999.999'    // Total de media l/ho

				nTotalG	   += nTotKmPerc
				nTotKmG	   += nTotKmF
				nTotQtG	   += nTotQtdF
				nTotVaG	   += nTotValF
                nTotKmPerc := 0
                nTotKmF    := 0
                nTotQtdF   := 0
                nTotValF   := 0
                nQtdFroF   := 0

                SomaLinha()

            EndIf

			If (cAliasAux)->TQN_FILIAL <> cFilQuer .OR. (cAliasAux)->(Eof())
				
                SomaLinha()
				
                //Se for todo compartilhado não traz a descrição da filial
				If FWModeAccess("TQN",1)=="E"
					
                    @ Li,000 pSay STR0029+STR0028+" : "+cFilQuer+" - "+AllTrim(FWFilialName(cEmpAnt, cFilQuer, 1)) //"Totais "###"Filial"
                    @ Li,082 pSay ( nTotKmG / nTotQtG )	Picture '@E 999,999.999'        // Total de media ho/l
                    @ Li,095 pSay ( nTotVaG / nTotQtG )	Picture '@E 99,999,999.999'     // Total de custo medio/l
                    @ Li,112 pSay nTotQtG				Picture '@E 99,999,999.999'     // Total de tot. consumo/l
                    @ Li,128 pSay nTotVaG				Picture '@E 999,999,999,999.99' // Total de tot. custo
                    @ Li,148 pSay nTotKmG				Picture '@E 9,999,999.99'       // Total de tot. ho
                    @ Li,162 pSay ( nTotVaG / nTotKmG )	Picture '@E 9,999,999,999.9999' // Total de custo/ho

                EndIf

			EndIf

        Else  // para sintetico, mvpar11 = 3 ou 4

            /*
            1         2         3         4         5         6         7         8         9         0         1         2         3
            0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
            *************************************************************************************************************************************
            Relatório de Abastecimentos c/ Custos e Consumo
            *************************************************************************************************************************************
            3	Família    Desc.Família         Qtd.Frotas  Média Km/L  Custo Médio/L  Tot.Consumo L    Tot.Custo   Tot.Km    Custo/Km  Média L /Km
            4	Modelo     Desc.Modelo          Qtd.Frotas  Média Km/L  Custo Médio/L  Tot.Consumo L    Tot.Custo   Tot.Km    Custo/Km  Média L /Km
            *************************************************************************************************************************************
            Filial: XX - XXXXXXXXXXXXXXXXXXXX
            xxxxxxyyyy  xxxxxxxxxxxxxxxxxxxx     9,999     999.999        999.999    999,999.999  999,999.999  999.999    999.9999
            xxxxxxyyyy  xxxxxxxxxxxxxxxxxxxx     9,999     999.999        999.999    999,999.999  999,999.999  999.999    999.9999
            xxxxxxyyyy  xxxxxxxxxxxxxxxxxxxx     9,999     999.999        999.999    999,999.999  999,999.999  999.999    999.9999
            Total
            */

            If (cAliasAux)->TQN_FILIAL <> cFilQuer .Or. &cQuebFtr <> cQuebra
                
                SomaLinha()

                @ Li,000 pSay cQuebra                          Picture '@!'                    // Familia
                @ Li,011 pSay IIf( MV_PAR11 == 3, cFam, cMod ) Picture '@!'                    // Desc. Familia/Desc. Modelo
                @ Li,043 pSay nQtdFroF                         Picture '@E 999,999,999'        // Qtd. Frotas
                @ Li,056 pSay ( nTotKmPerc / nTotQtdF )        Picture '@E 99,999.999'         // Média Km/L
                @ Li,068 pSay ( nTotValF / nTotQtdF )          Picture '@E 99,999,999.999'     // Custo Medio/L
                @ Li,084 pSay nTotQtdF                         Picture '@E 999,999,999.999'    // Tot. Consumo/L
                @ Li,101 pSay nTotValF                         Picture '@E 999,999,999,999.99' // Tot. Custo
                @ Li,121 pSay nTotKmPerc                       Picture '@E 99,999,999,999.99'  // Tot Km.
                @ Li,140 pSay ( nTotValF / nTotKmPerc )        Picture '@E 9,999,999,999.9999' // Custo/Km
                @ Li,160 pSay ( nTotQtdF / nTotKmPerc )        Picture '@E 999,999,999.999'    // Media L/Km

				nTotalG	   += nTotKmPerc
				nTotKmG	   += nTotKmF
				nTotQtG	   += nTotQtdF
				nTotVaG	   += nTotValF
                nTotKmPerc := 0
                nTotKmF    := 0
                nTotQtdF   := 0
                nTotValF   := 0
				nQtdFroF   := 0

            EndIf

            If (cAliasAux)->TQN_FILIAL <> cFilQuer .OR. (cAliasAux)->(Eof())
               
                SomaLinha() 
                
				If FWModeAccess( 'TQN', 1 ) == 'E'

               		@ Li,000 pSay STR0029+STR0028+" : "+cFilQuer+" - "+AllTrim(FWFilialName(cEmpAnt, cFilQuer, 1)) //"Totais "###"Filial"
                    @ Li,043 pSay nQtdFroG 				Picture '@E 999,999,999'        // Total Qtd. Frotas
                    @ Li,056 pSay nTotKmG/nTotQtG 		Picture '@E 99,999.999'         // Total Média Km/L
                    @ Li,068 pSay nTotVaG/nTotQtG		Picture '@E 99,999,999.999'     // Total Custo Medio/L
                    @ Li,084 pSay nTotQtG 				Picture '@E 999,999,999.999'    // Total Tot. Consumo L
                    @ Li,101 pSay nTotVaG 				Picture '@E 999,999,999,999.99' // Total Tot. Custo
                    @ Li,121 pSay nTotKmG 				Picture '@E 99,999,999,999.99'  // Total Tot Km.
                    @ Li,140 pSay nTotVaG/nTotKmG 		Picture '@E 9,999,999,999.9999' // Total Custo/Km
                    @ Li,160 pSay nTotQtG/nTotKmG		Picture '@E 999,999,999.999'    // Total Media L/Km

                EndIf

                nTotKmF     := 0
                nTotQtdF    := 0
                nTotValF    := 0
                nQtdFroF    := 0

            EndIf

        EndIf

        SomaLinha()

    EndDo

    oARQTRAB:Delete()
    (cALIASQRY)->(dbCloseArea())

    Roda(nCNTIMPR,cRODATXT,TAMANHO)

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Devolve a condicao original do arquivo principal             ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    RetIndex('TQN')
    Set Filter To
    Set Device To Screen
    If aReturn[5] == 1
        Set Printer To
        dbCommitAll()
        OurSpool(WNREL)
    EndIf
    MS_FLUSH()

Return Nil
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNT890DT  ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³ 17/10/07   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Valida o parametro ate data                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR890                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNT890DT()

    If  MV_PAR10 < MV_PAR09
        MsgStop(STR0030)   //"Data final não pode ser inferior à data inicial!"
        Return .F.
    EndIf

Return .T.
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SomaLinha³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ SomaLinha()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR630                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function Somalinha()

    Li++
  
    If Li > 58
        Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
    EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT890CALM
Define ordenacao padrao para tabela.

@author Hugo R. Pereira
@since 09/07/2012
@version MP10
@return Nil

/*/
//---------------------------------------------------------------------
Static Function MNT890ORDQ(aOrderQry, cTok, lPrefix)

    Local xOrdem := ""
    Local nIndOrd

    Default lPrefix := .T.

    For nIndOrd := 1 To Len(aOrderQry)
        If lPrefix
        	xOrdem += aOrderQry[nIndOrd][1] + "." + aOrderQry[nIndOrd][2] + If( Len(aOrderQry) != nIndOrd , cTok, "")
        Else
        	If ValType( xOrdem ) <> "A"
        		xOrdem := {}
        	EndIf
        	aAdd( xOrdem, aOrderQry[nIndOrd][2] )
        EndIf
    Next nIndOrd

Return xOrdem

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT890CALM
Efetua cálculo do total de abastecimento, considerando campo Moeda (TQN_MOEDA).

@author Hugo R. Pereira
@since 09/07/2012
@version MP10
@return Nil

/*/
//---------------------------------------------------------------------
Static Function MNT890CALM()

    Local cChgKey := ""
    Local nSumTot := 0
    Local nSumQtd := 0

    dbSelectArea(cAliasQry)
    dbGoTop()
    While .T.
        If !Eof()
            If cChgKey != MNT890CKEY(cAliasQry)
                If !Empty(cChgKey)
                    MNT890GRVX( .F., nSumTot, nSumQtd )
                Endif
                MNT890GRVX( .T. )
                cChgKey    := MNT890CKEY(cAliasQry)
                nSumTot    := 0
                nSumQtd    := 0
            Endif

            nSumTot += xMoeda((cAliasQry)->VALT, Val( (cAliasQry)->TQN_MOEDA ), nMoedarel, STOD((cAliasQry)->TQN_DTABAS), TAMSX3("TQN_VALTOT")[2])
            nSumQtd += (cAliasQry)->QTD

            dbSelectArea(cAliasQry)
            dbSkip()
        Else
            MNT890GRVX( .F., nSumTot, nSumQtd )
            Exit
        Endif
    End

    dbSelectArea(cAliasAux)
    dbGoTop()

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT890CKEY
Retorna chave para verificacao de sua possivel alteracao.

@author Hugo R. Pereira
@since 09/07/2012
@version MP10
@return Nil

/*/
//---------------------------------------------------------------------
Static Function MNT890CKEY(cAlias)
Return (cAlias)->TQN_FILIAL + (cAlias)->TQN_FROTA + (cAlias)->T9_CODFAMI + (cAlias)->T9_TIPMOD + (cAlias)->T9_TPCONTA + (cAlias)->TQN_CODCOM + (cAlias)->T9_FILIAL

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT890GRVX
Grava registro em tabela auxiliar.

@author Hugo R. Pereira
@since 09/07/2012
@version MP10
@return Nil

/*/
//---------------------------------------------------------------------
Static Function MNT890GRVX(lNew, nSumTot, nSumQtd)

    Local aArea := GetArea()
    Local cField, nField

    Default nSumTot := 0
    Default nSumQtd := 0

    dbSelectArea(cAliasAux)
    If lNew
        RecLock(cAliasAux, .T.)
        For nField := 1 to FCount()
            cField := FieldName(nField)
            x := &( "(cAliasQry)->" + cField )
            FieldPut(nField, x)
        Next nField
    Else
        RecLock(cAliasAux, .F.)
        (cAliasAux)->VALT := nSumTot
        (cAliasAux)->QTD  := nSumQtd
    Endif

    (cAliasAux)->(MsUnlock())

    RestArea(aArea)

Return
