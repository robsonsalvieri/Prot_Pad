#INCLUDE "MNTC825.ch"
#include "protheus.ch"

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC825
Consulta de cadastro de postos de combustivel
@author Heverson Vitoreti
@since 09/01/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC825()
	//+-----------------------------------------------------------------------+
	//| Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  |	 
	//+-----------------------------------------------------------------------+
	Local aNGBEGINPRM := NGBEGINPRM()

	Local oTempTable 
	Local aPesq := {}

	Private aRotina := MenuDef()

	Private cCadastro  := OemtoAnsi(STR0003)  //"Consulta de Postos"
	Private lGera := .T.
	Private cTRBB := GetNextAlias()

	aPos1      := {15,1,95,315 }

	aDBFB := {}
	Aadd(aDBFB,{"CNPJ"      ,"C", 14,0})
	Aadd(aDBFB,{"CIDADE"    ,"C", 20,0})
	Aadd(aDBFB,{"TIPPOS"    ,"C", 15,0})
	Aadd(aDBFB,{"NFAN"      ,"C", 30,0})
	Aadd(aDBFB,{"CODIGO"    ,"C", TAMSX3("TQF_CODIGO")[1],0,})
	Aadd(aDBFB,{"LOJA"      ,"C", TAMSX3("TQF_LOJA")[1],0,})
	Aadd(aDBFB,{"CONVENIO"  ,"C", 10,0})
	Aadd(aDBFB,{"ATIVO"     ,"C", 03,0})
	Aadd(aDBFB,{"DIASPRAZO" ,"N", 03,0})
	Aadd(aDBFB,{"COMBUST"   ,"C", 03,0})
	Aadd(aDBFB,{"DESCOMB"   ,"C", 20,0})
	Aadd(aDBFB,{"PRECOATUAL","N", TAMSX3("TQH_PRENEG")[1]/*9*/,TAMSX3("TQH_PRENEG")[2]/*3*/})
	Aadd(aDBFB,{"DTNEG"     ,"D", 08,0})

	//Intancia classe FWTemporaryTable
	oTempTable := FWTemporaryTable():New( cTRBB, aDBFB )

	//Cria indices 
	oTempTable:AddIndex( "Ind01" , {"CNPJ"} 	 	 	 	     )
	oTempTable:AddIndex( "Ind02" , {"CODIGO"}	 	 	 	 	 )
	oTempTable:AddIndex( "Ind03" , {"CODIGO","LOJA","COMBUST"}   )

	//Cria a tabela temporaria
	oTempTable:Create()

	aTRBB := {{STR0016 ,"CNPJ"      , "C", 14,0, X3Picture('TQF_CNPJ') },;      //"CNPJ"
			  {STR0017 ,"CODIGO"    , "C", TAMSX3("TQF_CODIGO")[1], 0, "@!"},;  //"Codigo"
			  {STR0018 ,"LOJA"      , "C", TAMSX3("TQF_LOJA")[1]  , 0, "@!"},;  //"Loja"
			  {STR0019 ,"NFAN"      , "C", 30,0, "@!"    },;                    //"Nome Fantasia"
			  {STR0020 ,"TIPPOS"    , "C", 15,0, "@!"    },;                    //"Tipo Posto"
			  {STR0021 ,"CIDADE"    , "C", 20,0, "@!"    },;                    //"Cidade"
			  {STR0022 ,"CONVENIO"  , "C", 10,0, "@!"    },;                    //"Convenio"
			  {STR0023 ,"ATIVO"     , "C", 03,0, "@!"    },;                    //"Ativo"
			  {STR0024 ,"DIASPRAZO" , "N", 03,0, "@ 999" },;                    //"Dias Prazo"
			  {STR0036 ,"COMBUST"   , "C", 03,0, "@!"    },;                    //"Combustível"
			  {STR0041 ,"DESCOMB"   , "C", 20,0, "@!"    },;                    //"Descricao do Combustível"
			  {STR0025 ,"PRECOATUAL", "N",TAMSX3("TQH_PRENEG")[1]/*9*/,TAMSX3("TQH_PRENEG")[2]/*3*/,;
			  '@E 999.'+Replicate('9',TAMSX3("TQN_VALUNI")[2])}}                //"Preço Atual "

	if pergunte("MNC825",.T.)
		Processa({ |lEnd| MNC825INI()},STR0030)
		If lGera
			DbSelectarea(cTRBB)
			DbGotop()

			//Cria Array para montar a chave de pesquisa
			aAdd( aPesq , { STR0016 ,{{"", "C", 255, 0, "", "@!"} }} )
			aAdd( aPesq , { STR0017 ,{{"", "C", 255, 0, "", "@!"} }} )
			aAdd( aPesq , { STR0017 + " + " + STR0018 + " + " + STR0036, {{"","C" , 255 , 0 ,"","@!"} }} )
			
			oBrowse:= FWMBrowse():New()
			oBrowse:SetDescription(cCadastro)
			oBrowse:SetTemporary(.T.)
			oBrowse:SetAlias(cTRBB)
			oBrowse:SetFields(aTRBB)
			oBrowse:SetSeek(.T.,aPesq)
			oBrowse:Activate()
			
		Endif
	endif

	oTempTable:Delete()

	DbSelectArea("TQF")
	DbSetOrder(01)
	Dbseek(xFilial("TQF"))

	//+-----------------------------------------------------------------------+
	//| Devolve variaveis armazenadas (NGRIGHTCLICK)                          |
	//+-----------------------------------------------------------------------+
	NGRETURNPRM(aNGBEGINPRM)

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNC825INI
Monta o arquivo temporario inicial mostrado no browse 
@author Heverson Vitoreti
@since 09/01/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC825INI()

	local cTipPos := Str(mv_par07,1)
	local dUltDt  := "  /  /  "

	dUltDt := CtoD(dUltDt)

	DbselectArea("TQF")
	DbSetorder(01)
	Dbseek(xFilial("TQF"))
	ProcRegua(LastRec())
	do while !EoF() .and. xFilial("TQF") == TQF->TQF_FILIAL

		IncProc()
		dbselectarea(cTRBB)
		if !empty(Mv_Par04)
			if Mv_Par04 <> TQF->TQF_CNPJ
				DbselectArea("TQF")
				DbSkip()
				loop
			endif
		else
			if !empty(mv_par01)
				if mv_par01 <> TQF->TQF_Codigo
					DbselectArea("TQF")
					DbSkip()
					loop
				endif
			endif

		endif
		if !empty(mv_par02)
			if mv_par02 <> TQF->TQF_LOJA
				DbselectArea("TQF")
				DbSkip()
				Loop
			endif
		endif

		If TQF->TQF_ATIVO <> Str(mv_par06,1)
			DbselectArea("TQF")
			DbSkip()
			loop
		Endif

		If cTipPos <> "4"
			if TQF->TQF_TIPPOS <> cTipPos
				DbselectArea("TQF")
				DbSkip()
				loop
			endif
		EndIf

		if !empty(mv_par03)
			if mv_par03 <> TQF->TQF_NREDUZ
				DbselectArea("TQF")
				DbSkip()
				loop
			endif
		endif

		DbSelectArea("TQH")
		DbSetOrder(01)
		IF DbSeek(xFilial("TQH")+TQF->TQF_CODIGO+TQF->TQF_LOJA+IIF(Empty(Mv_Par05),'',Mv_Par05))
			While !EoF() .and. xFilial("TQH") == TQF->TQF_FILIAL
				If TQH->TQH_CODPOS <> TQF->TQF_CODIGO .or. TQH->TQH_LOJA <> TQF->TQF_LOJA .or.;
				(TQH->TQH_CODCOM <> Mv_Par05 .AND. !Empty(Mv_Par05))
					DbSelectArea("TQH")
					DbSkip()
					Loop
				EndIf

				dbSelectArea(cTRBB)
				dbSetOrder(03)
				If !dbSeek(TQF->TQF_CODIGO+TQF->TQF_LOJA+TQH->TQH_CODCOM)
					RecLock((cTRBB), .T.)
					(cTRBB)->CNPJ := TQF->TQF_CNPJ
					(cTRBB)->CODIGO := TQF->TQF_CODIGO
					(cTRBB)->LOJA := TQF->TQF_LOJA
					(cTRBB)->NFAN := TQF->TQF_NREDUZ
					if Str(mv_par06,1) == "1"
						(cTRBB)->ATIVO := STR0008  //"Sim"
					elseif Str(mv_par06,1) == "2"
						(cTRBB)->ATIVO := STR0009  //"Nao"
					Endif

					if TQF->TQF_TIPPOS == "1"
						(cTRBB)->TIPPOS := STR0011  //"Conveniado"
					elseif TQF->TQF_TIPPOS == "2"
						(cTRBB)->TIPPOS := STR0012  //"Posto Interno"
					elseif TQF->TQF_TIPPOS == "3"
						(cTRBB)->TIPPOS := STR0026	 //"Nao Conveniado"
					Endif

					if TQF->TQF_CONVEN == "1"
						(cTRBB)->CONVENIO := STR0027 //"Repom"
					elseif TQF->TQF_CONVEN == "2"
						(cTRBB)->CONVENIO := STR0028 //"CTF"
					elseif TQF->TQF_CONVEN == "3"
						(cTRBB)->CONVENIO := STR0029 //"EssoCard"
					elseif TQF->TQF_CONVEN == "4"
						(cTRBB)->CONVENIO := STR0032 //"Posto Comum"
					elseif TQF->TQF_CONVEN == "5"
						(cTRBB)->CONVENIO := STR0038 //"GoodCard"
					elseif TQF->TQF_CONVEN == "6"
						(cTRBB)->CONVENIO := STR0039 //"Ticket"
					elseif TQF->TQF_CONVEN == "7"
						(cTRBB)->CONVENIO := STR0040 //"ExcelBR"
					Endif

					DbSelectArea("TQG")
					DbSetOrder(01)
					IF DbSeek(xFilial("TQG")+TQF->TQF_CODIGO+TQF->TQF_LOJA)
						While !Eof() .AND. TQG->TQG_FILIAL == xFilial("TQG") .AND. TQG->TQG_CODPOS == TQF->TQF_CODIGO;
						.AND. TQG->TQG_LOJA == TQF->TQF_LOJA
							(cTRBB)->DIASPRAZO := TQG->TQG_PRAZO
							dbSkip()
						End
					ENDIF
					(cTRBB)->COMBUST    := TQH->TQH_CODCOM
					(cTRBB)->DESCOMB    := NGSEEK("TQM",TQH->TQH_CODCOM,1,'TQM_NOMCOM')
					(cTRBB)->PRECOATUAL := TQH->TQH_PRENEG
					(cTRBB)->DTNEG      := TQH->TQH_DTNEG

					dbselectarea("SA2")
					Dbsetorder(01)
					if dbseek(xfilial("SA2")+TQF->TQF_CODIGO+TQF->TQF_LOJA)
						(cTRBB)->CIDADE := SA2->A2_MUN
					ENDIF
				Else
					RecLock((cTRBB), .F.)
					If TQH->TQH_DTNEG > (cTRBB)->DTNEG
						(cTRBB)->PRECOATUAL := TQH->TQH_PRENEG
						(cTRBB)->DTNEG      := TQH->TQH_DTNEG
					EndIf
				Endif
				MsUnLock(cTRBB)

				DbSelectArea("TQH")
				DbSkip()
			End
		ENDIF

		DbselectArea("TQF")
		DbSkip()
	END
	DbselectArea(cTRBB)
	DbGoTop()
	If Reccount() = 0
		MsgInfo(STR0034,STR0035)//"Não existem dados para montar a tela de consulta"##"ATENÇÃO"
		lGera := .f.
	EndIf
Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNC825PA
Reprocessa o browse de acordo com os parametros  
@author Heverson Vitoreti
@since 09/01/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC825PA()

	If !Pergunte("MNC825",.T.)
		Return
	EndIf

	DbSelectArea(cTRBB)
	Zap

	Processa({ |lEnd| MNC825INI() }, STR0030 )  //"Aguarde ..Processando Arquivo de Postos"
	DbSelectArea(cTRBB)
	DbGoTop()
Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTC825VI
Visualiza consulta
@author Heverson Vitoreti
@since 09/01/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTC825VI()

	DbSelectArea("TQF")
	DbSetOrder(01)
	DbSeek(xFilial("TQF")+(cTRBB)->CODIGO+(cTRBB)->LOJA)
	NGCAD01("TQF",Recno(),1)

	DbSelectArea(cTRBB)

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNC825VAL
Visualiza Consulta
@author Soraia de Carvalho
@since 22/03/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC825VAL()

	If !Empty(MV_PAR01)
		DbSelectArea("TQF")
		DbSetOrder(01)
		If DbSeek(xFilial("TQF")+Mv_Par01)
			//      Mv_Par02 := TQF->TQF_LOJA
			Mv_Par03 := SPACE(40)
			Mv_Par04 := SPACE(14)
		EndIf
	EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNC825VP
- 
@author Heverson Vitoreti 
@since 27/03/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNC825VP()

Return If(Empty(mv_par01),.T.,ExistCpo('TQF',Mv_par01)) .AND. MNC825VAL()

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT825LOJA
Valida o parametro de Loja  
@author Soraia de Carvalho
@since 07/06/06
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function  MNT825LOJA()

	If !Empty(MV_PAR02) .And. Empty(MV_PAR01)
		MsgStop(STR0033) //"Informe o Codigo do Posto"
		MV_PAR02 := "    "
		Return .F.
	EndIf
	If !Empty(MV_PAR04)
		DbSelectArea("TQF")
		DbSetOrder(02)
		DbSeek (xFilial("TQF")+MV_PAR04)
		MV_PAR01 := TQF->TQF_CODIGO
		MV_PAR02 := TQF->TQF_LOJA
	EndIf
	If !Empty(MV_PAR02) .And. !Empty(MV_PAR01)
		If !ExistCpo("TQF",MV_PAR01+MV_PAR02)
			Return .F.
		EndIf
	EndIf
Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Utilizacao de Menu Funcional.   
@author Rafael Diogo Richter
@since 02/02/2008
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Static Function MenuDef()
	Local aRotina :=	{{STR0001 ,"MNTC825VI" ,0,2},;  //"Visualizar"
	{STR0002 ,"MNC825PA" ,0,3,0}}  						//"Parametros"

	//+------------------------------------------------------------------------------+
	//|  Parametros do array a Rotina:                               				 |
	//|         		1. Nome a aparecer no cabecalho                              | 
	//|          		2. Nome da Rotina associada                                  |
	//|          		3. Reservado                                                 |
	//|          		4. Tipo de Transação a ser efetuada:                         |
	//|        					1 - Pesquisa e Posiciona em um Banco de Dados        | 
	//|       				    2 - Simplesmente Mostra os Campos                    |  
	//|        				    3 - Inclui registros no Bancos de Dados              | 
	//|         			    4 - Altera o registro corrente                       |    
	//|          			    5 - Remove o registro corrente do Banco de Dados     |    
	//|          		5. Nivel de acesso                                           | 
	//|         		6. Habilita Menu Funcional                                   |
	//+------------------------------------------------------------------------------+

Return aRotina
