#INCLUDE "mnta430.ch"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTA430  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 13/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para OS DE HISTORICOS                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA430

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Guarda conteudo e declara variaveis padroes ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNGBEGINPRM := NGBEGINPRM()

PRIVATE aRotina := MenuDef()
Private  aCRA      :=  {STR0001,STR0002,STR0003 }  //"Confirma"###"Redigita"###"Abandona"
Private cCadastro  := OemtoAnsi(STR0007) //"Cadastro da Bens"
Private flgservico := .f.   // O TS_SERVICO tambem e' usado no MNTA420
Private lecodbem   := .f.

cCadastro := OemtoAnsi(STR0008) //"O.S. de Historico"

dbSelectArea("ST9")
mBrowse( 6, 1,22,75,"ST9")

DbSelectArea("ST9")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna conteudo de variaveis padroes       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
NGRETURNPRM(aNGBEGINPRM)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NG430INC ³ Autor ³ Inacio Luiz Kolling   ³ Data ³ 13/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de Inclusao de OS bem, servico Historicos           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MNTA430                                                    ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG430INC(cAlias,nReg,nOpcx)
Local LVar01:=1,nLinhas:=0,cSaveMenuh, bCampo
Local GetList:={}, nOpcao:=3,lGravaOK := .T., oDlg, oGet,i, oMenu, oEnchoice
Private dDATAINI := dDataBase, cBLOQITE := "S", cBLOQFUN := "S", cBLOQFER := "S"
Private aTELA[0][0],aGETS[0],aHeader[0],nUsado:=0,cBEMATU := ST9->T9_CODBEM

DbSelectArea("ST9")
nORDST9 := IndexOrd()
DbsetOrder(1)
Dbseek(xFILIAL("ST9")+cBEMATU)

DbSelectArea("STS")
Inclui := .t.
bCampo := { |nCPO| Field(nCPO) }

For i := 1 TO FCount()
   M->&(EVAL(bCampo,i)) := FieldGet(i)
   If Inclui
      If ValType(M->&(EVAL(bCampo,i))) == "C"
         M->&(EVAL(bCampo,i)) := SPACE(LEN(M->&(EVAL(bCampo,i))))
      Elseif ValType(M->&(EVAL(bCampo,i))) == "N"
         M->&(EVAL(bCampo,i)) := 0
      Elseif ValType(M->&(EVAL(bCampo,i))) == "D"
         M->&(EVAL(bCampo,i)) := cTod("  /  /  ")
      Elseif ValType(M->&(EVAL(bCampo,i))) == "L"
         M->&(EVAL(bCampo,i)) := .F.
      ENDIF
   Else
      M->&(EVAL(bCampo,i)) := STS->&(EVAL(bCampo,i))
   Endif
Next i

aFIELD := {"TS_ORDEM","TS_CODBEM","TS_NOMBEM","TS_SERVICO","TS_DTMRINI"}

M->TS_CODBEM  := cBEMATU
M->TS_SERVICO := "HISTOR"

aPos1 := {  15,  1, 140,315 }
Continua := .F.
DbSelectArea("STJ")

M->TS_ORDEM := Space(6)
M->TS_ORDEM := GETSXENUM("STJ", "TJ_ORDEM") //NEXTOSMAN()
//ConfirmSX8()
//Tratamento para evitar duplicação de número de O.S. em base
DbSelectArea("STJ")
DbSetOrder(1)

If DbSeek(xFilial("STJ") + M->TS_ORDEM)
	ConfirmSx8()
	M->TS_ORDEM := GETSXENUM("STJ","TJ_ORDEM")
EndIf

DbSelectArea("STS")

_OK := .t.
nOpca := 0

DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0008) From 9,0 To 28,80 OF oMainWnd //"O.S. de Historico"
	oDlg:lMaximized := .T.

	oEnchoice := Msmget():New("STS",nReg,nOpcx,,,,aFIELD,aPOS1,,3)
		oEnchoice:oBox:Align := CONTROL_ALIGN_ALLCLIENT

	If Len(asMenu) > 0
		NGPOPUP(asMenu,@oMenu)
		oDlg:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg)}
		oEnchoice:oBox:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg)}
	EndIf

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpca:=1,oDlg:End()},{||oDlg:End()})

If nOpcA == 1
   M->TS_SERVICO := "HISTOR"
   M->TS_CODBEM  := cBEMATU
   lGravaOk := C430GRAVA(cAlias,nOpcao)
   If !lGravaOk
      Help(" ",1,"A430NAOREG")
   Else
    //Processa Gatilhos
     EvalTrigger()
    // If __lSX8
        ConfirmSX8()
   //  Endif
   EndIf
Else //if __lSX8
   RollbackSx8()
Endif

DbSelectArea("ST9")
DbsetOrder(nORDST9)
DbSelectArea(cAlias)
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³C430GRAVA ³ Autor ³ Inacio Luiz Kolling   ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava o registro                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA430                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function C430GRAVA(cAlias,nOpcao)

Local lOk := .T.
Local cUsaIntPc := AllTrim(GetMV("MV_NGMNTPC"))
Private nCOPIAOS  := GetMV("MV_NGCOPOS")

If AllTrim(GetNewPar("MV_NGINTER","N")) == "M"  //Mensagem Unica
	M->TJ_ORDEM := M->TS_ORDEM
	M->TJ_CODBEM := ST9->T9_CODBEM
	M->TJ_OBSERVA := ''
	M->TJ_SITUACA := "L"
	M->TJ_TERMINO := "N"
	M->TJ_POSCON2 := TPE->TPE_POSCON
	M->TJ_POSCONT := ST9->T9_POSCONT
	M->TJ_USUARIO := If(Len(STJ->TJ_USUARIO) > 15,cUsername,Substr(cUsuario,7,15))
	M->TJ_DTORIGI := M->TS_DTMRINI
	M->TJ_SERVICO := "HISTOR"
	M->TJ_INTPRJ  := ''
	M->TJ_INTTSK  := ''
	lOk := NGMUMntOrd(STJ->(RecNo()),3,.T.)
	If !lOk
		Return .F.
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Gera ordem de Producao para a OS                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cUsaIntPc == "S"

	cCODPRO := If(FindFunction("NGProdMNT"), NGProdMNT("M")[1], GetMV("MV_PRODMNT")) //Ira verificar apenas o primeiro Produto Manutencao do parametro
	dDataIni := MNT420DTOP(M->TS_DTMRINI)
	dDataFim := MNT420DTOP(M->TS_DTMRINI)
	cCusto := NgFilTPN(ST9->T9_CODBEM,M->TS_DTMRINI,SubStr(Time(),1,5),,xFilial("ST9"))[2] //Buscar o C.C. do bem na TPN
	If !GERAOPNEW(cCODPRO,1,M->TS_ORDEM,dDataIni,dDataFim,,,cCusto,'PLANO 000001')
		Return .F.
	EndIf

Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Grava no arquivo de Ordem Servico - STJ                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

DbSelectArea("ST9")
nORDST9 := IndexOrd()
DbsetOrder(1)
Dbseek(xFILIAL("ST9")+M->TS_CODBEM)

DbSelectArea("TPE")
DbsetOrder(1)
Dbseek(xFILIAL("TPE")+M->TS_CODBEM)

DbSelectArea("ST4")
DbsetOrder(1)
Dbseek(xFILIAL("ST4")+"HISTOR")

DbSelectArea( "STJ" )
RecLock("STJ",.T.)
STJ->TJ_FILIAL  := xFilial('STJ')
STJ->TJ_ORDEM   := M->TS_ORDEM
STJ->TJ_PLANO   := "000001"
STJ->TJ_CODBEM  := ST9->T9_CODBEM
STJ->TJ_TIPOOS  := "B"
STJ->TJ_SERVICO := "HISTOR"
STJ->TJ_DTORIGI := M->TS_DTMRINI
STJ->TJ_CCUSTO  := st9->t9_CCUSTO
STJ->TJ_SITUACA := "L"
STJ->TJ_TERMINO := "N"
STJ->TJ_USUARIO := If(Len(STJ->TJ_USUARIO) > 15,cUsername,Substr(cUsuario,7,15))
STJ->TJ_CENTRAB := st9->t9_CENTRAB
STJ->TJ_PRIORID := st9->t9_PRIORID
STJ->TJ_DTMPINI := M->TS_DTMRINI
STJ->TJ_HOMPINI := "00:00"
STJ->TJ_DTMPFIM := M->TS_DTMRINI
STJ->TJ_HOMPFIM := "00:00"
STJ->TJ_LUBRIFI := ST4->T4_LUBRIFI
STJ->TJ_TIPO    := ST4->T4_TIPOMAN
STJ->TJ_CODAREA := ST4->T4_CODAREA
STJ->TJ_POSCONT := ST9->T9_POSCONT
STJ->TJ_HORACO1 := Time()
STJ->TJ_POSCON2 := TPE->TPE_POSCON
STJ->TJ_HORACO2 := Time()
STJ->TJ_SEQRELA := '000'
STJ->TJ_TERCEIR := '1'
MsUnlock("STJ")

If FindFunction("MNTW215")
	MNTW215(,{M->TS_ORDEM})
Endif

//Grava campos de usuario
M430GRCUSA()

If nCOPIAOS = 2
   NGCOPIAOS(M->TS_ORDEM,"000001",.T.)
EndIf

DbSelectArea("ST9")
DbsetOrder(nORDST9)
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³CHKSER    ³ Autor ³ Inacio Luiz Kolling   ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consite so campo servico                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA430                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SERA430()
If !ST4->(Dbseek(xFilial('ST4') + m->TS_SERVICO))
   Help(" ",1,"SERVNAOEXI")
   Return .f.
Endif
Return .t.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³M430GRCUSA  ³ Autor ³Elisangela Costa       ³ Data ³13/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Grava os campos de usuaruio do STS relacionados ao STJ        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function M430GRCUSA()
Local aAREATU    := GetArea()
Local aCposAlter := {} //Array com os campos de usuario
Local nI         := 0
Local nInd       := 0
Local cCampo     := ""
Local cUsado     := ""
Local cPropri    := ""
Local aHeadSTS   := {}

aHeadSTS := NGHeader("STS")
For nInd := 1 To Len(aHeadSTS)

	cCampo  := aHeadSTS[nInd,2]
	cUsado 	:= aHeadSTS[nInd,7]
	cPropri := Posicione("SX3",2,cCampo,"X3_PROPRI")

	If X3USO(cUsado) .And. cPropri == "U"
		Aadd(aCposAlter,cCampo)
	EndIf

Next nInd

If Len(aCposAlter) > 0
   DbSelectArea("STJ")
   For nI := 1 To Len(aCposAlter)
      nONDERL := At("_",aCposAlter[nI])
      If nONDERL > 0
         cCAMIGUA   := Alltrim(Substr(aCposAlter[nI],nONDERL+1,Len(aCposAlter[nI])))
         cCAMPSTS   := "M->TS_"+cCAMIGUA
         cCAMPSTJ   := "STJ->TJ_"+cCAMIGUA
         cFILPOS430 := "TJ_"+cCAMIGUA
         If FieldPos(cFILPOS430) > 0
            RecLock("STJ",.F.)
            &cCAMPSTJ := &cCAMPSTS
            MsUnLock("STJ")
         Endif
      Endif
   Next nI
EndIf
RestArea(aAREATU)
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Ricardo Dal Ponte     ³ Data ³29/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³	  1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()

Local aRotina    := {{STR0004,"AxPesqui",0,1},;  //"Pesquisar"
                      {STR0005,"MNTA080CAD( 'ST9' , ST9->( Recno() ) , 2 )",0,2},;  //"Visual."
                      {STR0006,"NG430INC",0,4,0}} //"Incluir"
Return(aRotina)
