#Include "MNTR615.CH"
#Include "PROTHEUS.CH"

#DEFINE _nVERSAO 2 //Versao do fonte
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR615  ³ Autor ³ NG Informatica        ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relacao De Estruturas de Bens                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR615()
    
	Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)
	Local WNREL      := "MNTR615"
	Local LIMITE     := 132
	Local cDESC1     := STR0001 //"O objetivo deste relat¢rio ‚ apresentar a estrutura dos Bens selecio- "
	Local cDESC2     := STR0002 //"dos. O asterisco (*) apos o codigo do componente, indica que existem  "
	Local cDESC3     := STR0003 //"componentes nao apresentados na forma de estrutura por problema de espaco."
	Local cSTRING    := "ST9"
    
	Private NOMEPROG := "MNTR615"
	Private TAMANHO  := "M"
	Private aRETURN  := {STR0007,1,STR0008,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private TITULO   := STR0009 //"Estruturas de Bens"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2
    
    /*---------------------------------------------------------------
    Vetor utilizado para armazenar retorno da função MNTTRBSTB,
    criada de acordo com o item 18 (RoadMap 2013/14)
    ---------------------------------------------------------------*/
	Private vFilTRB := MNT045TRB()
    
	SetKey(VK_F4, {|| MNT045FIL( vFilTRB[2] )})
    
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Variaveis utilizadas para parametros                         ³
    //³ mv_par01     // De  Centro de Custos                         ³
    //³ mv_par02     // Ate Centro de Custos                         ³
    //³ mv_par03     // De Centro de Trabalho                        ³
    //³ mv_par04     // Ate Centro de Trabalho                       ³
    //³ mv_par05     // De Familia de Bens                           ³
    //³ mv_par06     // Ate Familia de Bens                          ³
    //³ mv_par07     // De  Bem                                      ³
    //³ mv_par08     // Ate Bem                                      ³
    //³ mv_par09     // Controle de quebra                           ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private cPERG := "MNT615"
	Pergunte(cPERG,.F.)
    
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Envia controle para a funcao SETPRINT                        ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WNREL := SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
    
	SetKey(VK_F4, {|| })
    
	If nLastKey = 27
		Set Filter To
		dbSelectArea("ST9")
        
		MNT045TRB( .T., vFilTRB[1], vFilTRB[2])
        
		Return
	EndIf
    
	SetDefault(aRETURN,cSTRING)
	RptStatus({|lEND|MNTR615IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)
    
	dbSelectArea("ST9")
    
	MNT045TRB( .T., vFilTRB[1], vFilTRB[2])
    
	NGRETURNPRM(aNGBEGINPRM)
    
Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR615IMP³ Autor ³ Waldemiro L. Lustosa  ³ Data ³ 13.11.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR615                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR615IMP(lEND,WNREL,TITULO,TAMANHO)
    
	Local cRODATXT  := ""
	Local nCntImpr  := 0
	Local lImpRel   := .F.
    
	Private cUltimoReg, cCondicao, nAtual
	Private li := 80 ,m_pag := 1
    
	cQuebraCusto := Space(09)
	cQuebraTraba := Space(06)
	cQuebraFamil := Space(06)
	nPosicao     := 0
	cBem         := Space(16)
    
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Verifica se deve comprimir ou nao                            ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nTIPO  := IIF( aRETURN[4] == 1, 15, 18 )
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Monta os Cabecalhos                                          ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	CABEC1 := STR0010 //"Nivel                 Nome                                    Dt Entrada   Local  Nome Localizacao"
	CABEC2 := " "
    
	/*/
	1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
    ************************************************************************************************************************************
	Nivel                       Nome                                     Dt Entrada  Local  Nome Localizacao
    ************************************************************************************************************************************
	Centro De Custo.....: xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	Centro De Trabalho..: xxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	Familia.............: xxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    
	Bem..: xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    
	xxxxxxxxxxxxxxxx            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  dd/mm/aaaa  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxxxxxxxxxxxx          xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  dd/mm/aaaa  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxxxxxxxxxxxx        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  dd/mm/aaaa  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxxxxxxxxxxxx      xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  dd/mm/aaaa  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxxxxxxxxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  dd/mm/aaaa  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  dd/mm/aaaa  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxxxxxxxxxxxx *      xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  dd/mm/aaaa  xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    
	/*/
    
	dbSelectArea("ST9")
	dbSetOrder(3)
	dbSeek(xFilial("ST9") + MV_PAR01, .T.)
        
	SetRegua(LastRec())
    
	While !EoF() .And. ST9->T9_FILIAL == xFilial("ST9") .And.;
			ST9->T9_CCUSTO <= MV_PAR02
        
		IncRegua()
		If lEND
			@ PROW()+1,001 Psay STR0011 //"CANCELADO PELO OPERADOR"
			Exit
		EndIf
        
		If LI > 58
			CABEC(TITULO,CABEC1,CABEC2,NOMEPROG,TAMANHO,nTIPO)
		EndIf
        
		If ST9->T9_CENTRAB < MV_PAR03 .OR. ST9->T9_CENTRAB > MV_PAR04
			dbSkip()
			Loop
		EndIf
        
		If ST9->T9_CODFAMI < MV_PAR05 .OR. ST9->T9_CODFAMI > MV_PAR06
			dbSkip()
			Loop
		EndIf
        
		If ST9->T9_CODBEM  < MV_PAR07 .OR. ST9->T9_CODBEM  > MV_PAR08
			dbSkip()
			Loop
		EndIf
        
        
        
		dbSelectArea("STC")
		dbSetOrder(01)
		If dbSeek( xFilial("STC") + ST9->T9_CODBEM )
			dbSetOrder(01)
			dbSeek( xFilial("STC") + ST9->T9_CODBEM )
		Else
			dbSelectArea("ST9")
			dbSkip()
			Loop
		EndIf
			
		If MNT045STB( ST9->T9_CODBEM, vFilTRB[2] )
				dbSelectArea('ST9')
			dbSkip()
			Loop
		EndIf
        
		lImpRel := .T.
        
		If cQuebraCusto = " "
			cQuebraCusto = ST9->T9_CCUSTO
			cQuebraTraba = ST9->T9_CENTRAB
			cQuebraFamil = ST9->T9_CODFAMI
			QUEBRA(cQuebraCusto,cQuebraTraba,cQuebraFamil)
		Else
			If cQuebraCusto != ST9->T9_CCUSTO                        .OR.;
					(cQuebraTraba != ST9->T9_CENTRAB .AND. MV_PAR09 = 2) .OR.;
					(cQuebraFamil != ST9->T9_CODFAMI .AND. MV_PAR09 = 3)
				cQuebraCusto = ST9->T9_CCUSTO
				cQuebraTraba = ST9->T9_CENTRAB
				cQuebraFamil = ST9->T9_CODFAMI
				li = 59
				Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
				QUEBRA(cQuebraCusto,cQuebraTraba,cQuebraFamil)
			EndIf
		EndIf
        
		NGSOMALI(58)
		@ LI,000 Psay STR0012 //"Bem:"
		@ LI,007 Psay ST9->T9_CODBEM
		@ LI,025 Psay ST9->T9_NOME
		Li++
        
		nORDEM := 1
		nATUAL := ST9->(Recno())
		dbSelectArea("STC")
		dbSetOrder(01)
		cCOD := xFilial("STC")+ST9->T9_CODBEM
		EXPLODE(cCOD,nORDEM)
		dbSelectArea("ST9")
		dbGoTo(nATUAL)
		dbSkip()
	EndDo
        
	If lImpRel
		Roda(nCNTIMPR,cRODATXT,TAMANHO)
	Else
		MsgInfo(STR0017, STR0016)
		Return .F.
	EndIf
    
	RetIndex("ST9")
	RetIndex("STC")
    
	Set Filter To
	Set Device To Screen
    
	If aRETURN[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
    
	MS_FLUSH()
    
Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Quebra   ³ Autor ³ NG Informatica Ltdaa  ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Impressao do Cabecalho de Quebra                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR615                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function QUEBRA(cQuebraCusto,cQuebraTraba,cQuebraFamil)

    @ LI,000 Psay STR0013 //"Centro De Custo = "
    @ LI,022 Psay cQuebraCusto
    
    dbSelectArea("SI3")
    dbSetOrder(01)
    dbSeek(xFilial("SI3") + cQUEBRACUSTO)
    
    @ LI,032 Psay SI3->I3_DESC
    NGSOMALI(58)
    @ LI,000 Psay STR0014 //"Centro De Trabalho = "
    @ LI,022 Psay cQuebraTraba
    
    dbSelectArea("SHB")
    dbSetOrder(01)
    dbSeek(xFilial("SHB")+cQUEBRATRABA)
    
    @ LI,032 Psay SHB->HB_NOME
    NGSOMALI(58)
    @ LI,000 Psay STR0015 //"Familia = "
    @ LI,022 Psay cQuebraFamil
    
    dbSelectArea("ST6")
    dbSetOrder(01)
    dbSeek(xFilial("ST6")+cQUEBRAFAMIL)
    
    @ LI,032 Psay ST6->T6_NOME
    NGSOMALI(58)
    
    dbSelectArea("ST9")
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ Explode  ³ Autor ³ NG Informatica Ltda   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Faz a explosao de uma estrutura                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR615                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function EXPLODE(cCod,nORDEM)

    Local nREG1
    Local cCod1
    Local cOldCod := "ZZ" + Replicate('Z', TAMSX3('TC_CODBEM')[1])
    
    While !EoF() .And. STC->TC_FILIAL + STC->TC_CODBEM == cCod
        
        If STC->TC_TIPOEST = 'B'
        
            If nORDEM <= 6
                @ LI,nORDEM+nORDEM Psay STC->TC_COMPONE
            Else
                @ LI,004 Psay STC->TC_COMPONE
                @ LI,021 Psay "*"
            EndIf
            
            dbSelectArea("ST9")
            dbSetOrder(01)
            dbSeek(xFilial("ST9")+STC->TC_COMPONE)
            
            @ LI,028 Psay SubStr(ST9->T9_NOME,1,39)
            @ LI,069 Psay STC->TC_DATAINI
            @ LI,081 Psay STC->TC_LOCALIZ
            
            dbSelectArea("TPS")
            dbSetOrder(01)
            dbSeek(xFilial("TPS")+STC->TC_LOCALIZ)
            
            @ LI,088 Psay TPS->TPS_NOME
            
            dbSelectArea("ST9")
            dbSetOrder(03)
            dbSelectArea("STC")
            
            Li++
            If Li > 58
                CABEC(TITULO,CABEC1,CABEC2,NOMEPROG,TAMANHO,nTIPO)
                QUEBRA(cQuebraCusto,cQuebraTraba,cQuebraFamil)
            EndIf
            
            dbSelectArea("STC")
            cCOD1 := xFilial("STC") + STC->TC_COMPONE
            nREG1 := STC->(Recno())
            If dbSeek(cCOD1) .AND. (cCOD1 != cOldCod)
                cOldCod := cCOD1
                EXPLODE(cCOD1,++nORDEM)
                --nORDEM
            EndIf
            
            STC->(dbGoTo(nREG1))
        EndIf
        STC->(dbSkip())
    EndDo
    
Return Nil