#INCLUDE "SGAA300.ch"
#include "Protheus.ch"

/*/


Ŀ
Funo     SGAA300   Autor  Thiago Olis Machado    Data 13/04/2005
Ĵ
Descrio Programa para cadastrar objetivos e metas ambientais        
Ĵ
Tabelas   TBH - Objetivos Ambientais                                  
          TBI - Metas Ambientais                                      
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function SGAA300()

	Local aNGBEGINPRM := NGBEGINPRM( )

	If Amiin(56,35) //Verifica se o usurio possui licena para acessar a rotina.

	Private aRotina := MenuDef()

	Private cCadastro := OemtoAnsi(STR0007) //"Objetivos e Metas Ambientais"
	Private aChkDel := {}, bNgGrava
	Private nTotal1 := 0
	Private lVisual := .F.

	//Verifica se o UPDSGA17 foi aplicado
	If !SG90UPDVL()
		Return .F.
	Endif

	Dbselectarea("TBH")
	dbSetOrder(1)
	Dbgotop()

	mBrowse( 6, 1,22,75,"TBH",,'TBH->TBH_PRAZO < dDataBase',,,,,,,,,,,,"TBH_SITUAC = '1' And TBH_FILIAL = " + ValToSql( xFilial("TBH") ) )

	EndIf
	//Ŀ
	//Retorna conteudo de variaveis padroes       
	//
	NGRETURNPRM(aNGBEGINPRM)

Return .t.

/*/


Ŀ
Funo     Sg300Cad  Autor  Thiago Olis Machado    Data 13/04/2005
Ĵ
Descrio Funcao de cadastro                                          
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function Sg300Cad(cAlias,nRecno,nOpcx)
Local cTitulo := cCadastro
Local oMainWnd	 // Objeto para montar a janela de cadastro
Local oDlg
Local lVisual := .t.
Local nControl:= 0,nOK := 0,lGetd := .t.
Local aPages:= {},aTitles:= {},aVar:= {}
Local Ni,cGet
Local aButtons := {}
Local aArea := GetArea()
Local oMenu, oPnl1, oPnl2 , oPnlAll
Private lRetorno := IsInCallStack("SGAA310")
Private oFolder2
Private oEnc01,oGet01
Private oGet,aHeader,nItens
Private aSvATela := {}, aSvAGets := {}, aTela := {}
Private aSvHeader := {{},{},{},{}}, aSvCols := {{},{},{},{}}
Private aQdgDoc  := {}
Private oQdg
Private bQdgLine1
Private bQdgLine2
Private oNGOk      := LoadBitmap( GetResources(), "ENABLE" )
Private oNGNao	    := LoadBitmap( GetResources(), "BR_VERMELHO" )
Private aGantt     := {}
Private aConfig    := {}
Private aDescricao := {}
Private aCorBarras := {}
Private oGantt
Private bRfshGantt
Private aCmbSeek := {}
Private oCBX:= NIL
Private aCombo := {}
Private aTskCmb := {}
Private nTsk:= NIL
Private oCombo2:= NIL
Private aCombo2:= {}
Private l_Internal:= .F.
Private nFldAtu, nFldAnt

Private aSize := MsAdvSize()
Private aPos  := {14,1,297,232}//Posio do GANTT - No remover sem ter certeza, necessrio para o PMSGANTT.PRW

oGantt:= NIL
bRfshGantt      := ''

Aadd(aTitles,OemToAnsi(STR0008))   //"Metas"
Aadd(aPages,"Header 1")
nControl++
Aadd(aTitles,OemToAnsi(STR0009))   //"Grfico de Gantt"
Aadd(aPages,"Header 2")
nControl++
//Verifica se nao esta sendo chamado pelo Retorno
If !lRetorno .and. nOpcx <> 5 .and. nOpcx <> 2
	AAdd( aButtons, { "PMSSETABOT"   , { || IIF(Sg300Situac(nOpcx),oDlg:End(),nil) }, STR0010 } ) //"Aprovar / Finalizar"
Endif

Define MsDialog oDlg Title cTitulo From 0,0 To aSize[6],aSize[5] Color CLR_BLACK,CLR_WHITE Pixel Of oMainWnd

	oPnlAll := TPanel():New(01,01,,oDlg,,,,,,10,10,.F.,.F.)
	oPnlAll:Align := CONTROL_ALIGN_ALLCLIENT

	oPnl1 := TPanel():New(01,01,,oPnlAll,,,,,,1,083,.F.,.F.)
	oPnl1:Align := CONTROL_ALIGN_TOP

	aTela := {}
	aGets := {}
	Dbselectarea("TBH")
	RegToMemory("TBH",(nOpcx == 3))
	oEnc01:= MsMGet():New("TBH",nRecno,nOpcx,,,,,{0,0,1,1},,,,,,oPnl1,,,.f.,"aSvATela")
	oEnc01:oBox:bGotFocus := {|| NgEntraEnc("TBH")}
	oEnc01:ObOX:Align := CONTROL_ALIGN_ALLCLIENT
	aSvATela := aClone(aTela)
	aSvAGets := aClone(aGets)

oPnl2 := TPanel():New(01,01,,oPnlAll,,,,,,1,212,.F.,.F.)
	oPnl2:Align := CONTROL_ALIGN_ALLCLIENT

	oFolder2 := TFolder():New(98,4,aTitles,aPages,oPnl2,,,,.T.,.F.,1000,1000)
	oFolder2:Align := CONTROL_ALIGN_ALLCLIENT
	oFolder2:aDialogs[1]:oFont := oDlg:oFont
	oFolder2:aDialogs[2]:oFont := oDlg:oFont

//Ŀ
// Folder 01                                            
//
Sg300Folder01(nOpcx)

//Ŀ
// Folder 02                                            
//
oFolder2:bSetOption := { |nAt| ;
							 	 nFldAtu:=nAt;
								,nFldAnt:=oFolder2:nOption;
								,IIF(Sg300Gantt(nFldAtu,nFldAnt,oDlg),(oGantt:=PmsGantt(aGantt,aConfig,,,oFolder2:aDialogs[2],aPos,aDescricao,,,,@oGantt,l_Internal,,);
								,oFolder2:aDialogs[2]:Refresh()),.f.)}

NGPOPUP(aSMenu,@oMenu)
oDlg:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg)}
oEnc01:oBox:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg)}

Activate Dialog oDlg On Init EnchoiceBar(oDlg,{|| lOk:=.T.,If(Sg300Obrig(nOpcx),oDlg:End(),lOk := .f.)},{|| nOk:= 0,oDlg:End()},,aButtons) Centered

DbSelectArea("TBH")
RestArea(aArea)

Return .t.

/*/


Ŀ
Funo    Sg300Folder01Autor   Thiago Olis Machado    Data 13/04/2005
Ĵ
Descrio Funcao de cadastro                                             
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ



/*/
Function Sg300Folder01(nOpcx)
Local aArea := GetArea()
Local oPnlLst, oPnlBtn, oPnlLgnd

oPnlLgnd := TPanel():New(01,01,,oFolder2:aDialogs[1],,,,,,1,20,.F.,.F.)
	oPnlLgnd:Align := CONTROL_ALIGN_BOTTOM

	@ 06,040 Bitmap oOk Resource "Enable" Size 8,8 Pixel Of oPnlLgnd Noborder Design//When .F.
	@ 06,052 Say OemToAnsi(STR0015) Size 37,7 Of oPnlLgnd Pixel  //"Em Dia"
	@ 06,140 Bitmap oNao Resource "BR_VERMELHO" Size 8,8 Pixel Of oPnlLgnd Noborder When .F.
	@ 06,152 Say OemToAnsi(STR0016) Size 37,7 Of oPnlLgnd Pixel  //"Atrasada"


oPnlBtn := TPanel():New(01,01,,oFolder2:aDialogs[1],,,,,,25,1,.F.,.F.)
	oPnlBtn:Align := CONTROL_ALIGN_RIGHT

	@ 100,08 BTNBMP oPesq Resource "pesquisa" Size 24,24 OF oPnlBtn Noborder Pixel Action Sg300BuMe(nOpcx) When nOpcx != 2
	oPesq:cToolTip := OemToAnsi(STR0017)  //"Buscar"

	@ 140,08 BTNBMP oInc Resource "bmpincluir" Size 24,24 Pixel Of oPnlBtn Noborder Pixel Action Sg300InMe(nOpcx) When nOpcx != 2
	oInc:cToolTip := OemToAnsi(STR0003)  //"Incluir"

	@ 180,08 BTNBMP oAlt Resource "note" Size 24,24 Pixel Of oPnlBtn Noborder Pixel Action Sg300AlMe(nOpcx) When nOpcx != 2
	oAlt:cToolTip := OemToAnsi(STR0004)  //"Alterar"

	@ 220,08 BTNBMP oExc Resource "excluir" Size 24,24 Pixel Of oPnlBtn Noborder Pixel Action Sg300ExMe(nOpcx) When nOpcx != 2
	oExc:cToolTip := OemToAnsi(STR0005)  //"Excluir"

oPnlLst := TPanel():New(01,01,,oFolder2:aDialogs[1],,,,,,1,1,.F.,.F.)
	oPnlLst:Align := CONTROL_ALIGN_ALLCLIENT

	@ 10,007 ListBox oQdg Fields;
			Header  " "	,;//Semaforo
			STR0011		,;//"Codigo"
			STR0012		,;//"Meta"
			STR0013		,;//"Prazo"
			Size aSize[4]-20,162 Of oPnlLst Pixel,;
			On DblClick Sg300ViMe( aQdgDoc, oQdg:nAt, 2 )

	oQdg:Align := CONTROL_ALIGN_ALLCLIENT

	If nOpcx <> 3
		Sg300CarArray()
	EndIf
	bQdgLine1 := { || { If(aQDgDoc[oQDg:nAt,4] < dDataBase , oNGNao , oNGOk  ) ,;
							aQDGDoc[oQDg:nAt,2],aQDGDoc[oQDg:nAt,3],aQDGDoc[oQDg:nAt,4]}}

	bQdgLine2 := { || { oNGNao, Space( 06 ), Space ( 70 ), Space( 08 ) } }

	oQdg:SetArray( aQDGDoc )
	IIF(Len(aQdgDoc)>0,oQdg:bLine:= bQdgLine1,oQdg:bLine:= bQdgLine2)

	oQdg:GoTop()
	oQdg:Refresh()
	oQdg:cToolTip := OemToAnsi(STR0014)	  //"Duplo click visualiza cadastro de metas"

RestArea(aArea)
Return .t.
/*/


Ŀ
Funo     Sg300InMe    Autor  Thiago Olis Machado    Data 20/04/2005
Ĵ
Descrio Funcao de inclusao de metas ambientais                         
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/
Function Sg300InMe(nOpcx)
Local i, dDtIni

If nOpcx == 3 .or. nOpcx == 4
	SetInclui()
	If Sg090Ca( "TAA", 1, 3 ) == 1
		If TAA->TAA_TPMETA == "3"
			MsgStop(STR0027,STR0023)//"No  possvel relacionar uma meta de tipo 3=N/A."#"Atencao"
			oQdg:GoTop()
			oQdg:Refresh()
			Return .F.
		Endif
		dbSelectArea("TAA")
//		dbGoBottom()
		Aadd(aQDGDoc,{"",TAA->TAA_CODPLA,TAA->TAA_NOME,TAA->TAA_DTFIPR})
		aQdGDoc:= aSort( aQdGDoc,,,{ |x,y| x[4] < y[4] } )
		oQdg:SetArray( aQDGDoc )
		oQdg:bLine:= bQdgLine1
		dDtIni := CTOD("  /  /  ")
		For i := 1 To Len(aQdgDoc)
			dbSelectArea("TAA")
			dbSetOrder(1)
			dbSeek(xFilial("TAA")+aQdGDoc[i][2])
			If TAA->TAA_DTINPR < dDtIni .Or. Empty(dDtIni)
				dDtIni := TAA->TAA_DTINPR
			EndIf
		Next
		M->TBH_ABERTU := dDtIni
		If Len(aQdgDoc) > 0
			M->TBH_FECHAM := aQdGDoc[Len(aQdgDoc)][4]
		Else
			M->TBH_FECHAM := CTOD("  /  /  ")
		Endif
		oEnc01:oBox:Refresh()
	EndIf
	If(nOpcX == 3,SetInclui(),SetAltera())
EndIf
oQdg:GoTop()
oQdg:Refresh()

Return .t.

/*/


Ŀ
Funo     Sg300BuMe    Autor  Thiago Olis Machado    Data 20/04/2005
Ĵ
Descrio Funcao de busca das metas ambientais ja cadastradas            
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/
Function Sg300BuMe(nOpcx)
Local nPos
Local i, dDtIni

dbSelectArea("TAA")
dbGoTop()

If nOpcx == 3 .or. nOpcx == 4
	lRet := ConPad1( , , , "TAA",,,.f.)
	nPos := aScan( aQdGDoc, { |x| x[2] == TAA->TAA_CODPLA })
	If lRet .and. nPos > 0
		MsgStop(STR0024,STR0023)
		Sg300BuMe(nOpcx)
		Return .F.
	EndIf
	If lRet
		Aadd(aQDGDoc,{"",TAA->TAA_CODPLA,TAA->TAA_NOME,TAA->TAA_DTFIPR})
		aQdGDoc:= aSort( aQdGDoc,,,{ |x,y| x[4] < y[4] } )
		oQdg:SetArray( aQDGDoc )
		oQdg:bLine:= bQdgLine1
		dDtIni := CTOD("  /  /  ")
		For i := 1 To Len(aQdgDoc)
			dbSelectArea("TAA")
			dbSetOrder(1)
			dbSeek(xFilial("TAA")+aQdGDoc[i][2])
			If TAA->TAA_DTINPR < dDtIni .Or. Empty(dDtIni)
				dDtIni := TAA->TAA_DTINPR
			EndIf
		Next
		M->TBH_ABERTU := dDtIni
		If Len(aQdgDoc) > 0
			M->TBH_FECHAM := aQdGDoc[Len(aQdgDoc)][4]
		Else
			M->TBH_FECHAM := CTOD("  /  /  ")
		Endif
		oEnc01:oBox:Refresh()
	EndIf
EndIf
oQdg:GoTop()
oQdg:Refresh()
Return .t.

/*/


Ŀ
Funo     Sg300ExMe    Autor  Thiago Olis Machado    Data 20/04/2005
Ĵ
Descrio Funcao de exclusao de metas ambientais                         
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/
Function Sg300ExMe(nOpcx)
Local i, dDtIni

If Len( aQDGDoc ) <= 0
	MsgStop(STR0025,STR0023)
	Return .F.
EndIf

If nOpcx == 3 .or. nOpcx == 4
	dbSelectArea("TBK")
	dbSetOrder(1)
	If dbSeek(xFilial("TBK")+M->TBH_CODOBJ+aQdGDoc[oQdg:nAt][2])
		MsgInfo(STR0034,STR0035) //"J existem resultados lanados para as metas. No  possvel excluir o objetivo"###"Ateno"
		Return .F.
	Endif
	If MsgYesNo(STR0018,STR0019) //"Confirmar excluso?"
		aDel( aQDGDoc,oQdg:nAt)
		aSize( aQDGDoc, Len( aQDGDoc )-1)
		If Len( aQdgDoc ) > 0
			aQdGDoc:= aSort( aQdGDoc,,,{ |x,y| x[4] < y[4] } )
			oQdg:SetArray( aQDGDoc )
			oQdg:bLine:= bQdgLine1
			dDtIni := CTOD("  /  /  ")
			For i := 1 To Len(aQdgDoc)
				dbSelectArea("TAA")
				dbSetOrder(1)
				dbSeek(xFilial("TAA")+aQdGDoc[i][2])
				If TAA->TAA_DTINPR < dDtIni .Or. Empty(dDtIni)
					dDtIni := TAA->TAA_DTINPR
				EndIf
			Next
			M->TBH_ABERTU := dDtIni
			M->TBH_FECHAM := aQdGDoc[Len(aQdgDoc)][4]
		Else
			aQdGDoc := {}
			M->TBH_ABERTU := CTOD("  /  /  ")
			M->TBH_FECHAM := CTOD("  /  /  ")
		EndIf
		oEnc01:oBox:Refresh()
	EndIf
EndIf

Return .t.

/*/


Ŀ
Funo     Sg300AlMe    Autor  Thiago Olis Machado    Data 20/04/2005
Ĵ
Descrio Funcao de alteracao de metas ambientais                        
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/

Function Sg300AlMe(nOpcx)
Local nPos
Local i, dDtIni

If Len( aQDGDoc ) <= 0
	MsgStop(STR0025,STR0023)//"No h metas cadastradas."###"Ateno!"
	Return .F.
EndIf

If nOpcx == 3 .or. nOpcx == 4
	DbSelectArea("TAA")
	DbSetOrder(1)
	If DbSeek(xFilial("TAA")+ aQdGDoc[oQdg:nAt][2] )
		If Sg090Ca( "TAA", Recno(), 4 ) == 1
			nPos := aScan( aQdgDoc, { |x| x[2] == TAA->TAA_CODPLA } )
			If nPos > 0
				If TAA->TAA_TPMETA == "3"
					MsgStop(STR0027,STR0023)//"No  possvel relacionar uma meta de tipo 3=N/A."#"Atencao"
					aDel(aQdgDoc, nPos)
					aSize(aQdgDoc, Len(aQdgDoc)-1)
				Else
					aQdgDoc[nPos][3] := TAA->TAA_NOME
					aQdgDoc[nPos][4] := TAA->TAA_DTFIPR
					aQdGDoc:= aSort( aQdGDoc,,,{ |x,y| x[4] < y[4] } )
					oQdg:SetArray( aQDGDoc )
					oQdg:bLine:= bQdgLine1
				Endif
			EndIf
		EndIf
	EndIf
	aQdGDoc:= aSort( aQdGDoc,,,{ |x,y| x[4] < y[4] } )
	dDtIni := CTOD("  /  /  ")
	For i := 1 To Len(aQdgDoc)
		dbSelectArea("TAA")
		dbSetOrder(1)
		dbSeek(xFilial("TAA")+aQdGDoc[i][2])
		If TAA->TAA_DTINPR < dDtIni .Or. Empty(dDtIni)
			dDtIni := TAA->TAA_DTINPR
		EndIf
	Next
	M->TBH_ABERTU := dDtIni
	If Len(aQdgDoc) > 0
		M->TBH_FECHAM := aQdGDoc[Len(aQdgDoc)][4]
	Else
		M->TBH_FECHAM := CTOD("  /  /  ")
	Endif
EndIf

oQdg:Refresh()

Return .t.

/*/


Ŀ
Funo     Sg300Gantt   Autor  Thiago Olis Machado    Data 20/04/2005
Ĵ
Descrio Funcao de montagem do Gantt                                    
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/

Function Sg300Gantt(nFldAtu,nFldAnt,oDlg)
Local i
Default nFldAtu:= 1 // folder atual
Default nFldAnt:= 1 // folder anterior

If nFldAtu == 2 // If oFolder2:nOption == 2
	aGantt     := {}
	aConfig    := {}
	aDescricao := {}
	aCorBarras := {}
	If oGantt <> NIl
		l_Internal := .T.
		oGantt:hide()
		MsFreeObj(@oGantt,.t.)
		oGantt:End()
		oGantt:=NIL

		aCmbSeek := {}
		oCBX:= NIL
		aCombo := {}
		aTskCmb := {}
		nTsk:= NIL
		oCombo2:= NIL
		aCombo2:= {}

		oFolder2:Refresh()
	Else
		l_Internal:= .F.
	Endif
Else
	l_Internal:= .T.
	If oGantt <> Nil
		oGantt:End()
		oGantt:=NIL
	EndIf
EndIf

aCorBarras := LoadCorBarra( "MV_PMSACOR" )
aRGB := ValorCorBarra( "2" ,aCorBarras ,2 )
aGantt := {}

For i:= 1 To Len( aQDgDoc )
	DbSelectArea("TAA")
	DbSetOrder(1)
	If DbSeek(xFilial("TAA")+aQdgDoc[i][2])
		cView	:= "PmsDispBox({	{'"+STR0028+"','"+TAA->TAA_MAT+"'},"+; //'Recurso '
									"	{'"+STR0029+"','"+"'},"+; //'Descricao'
									"	{'"+STR0030+"','"+DtoC(TAA->TAA_DTINPR)+"'},"+; //'Data Inicial'
									"	{'"+STR0031+"','"+DtoC(TAA->TAA_DTFIPR)+"'}})" //'Data Final'

		aAdd(aGantt,{ {aQdgDoc[i][2],aQdgDoc[i][3]},{{TAA->TAA_DTINPR,"08:00",TAA->TAA_DTFIPR,"18:00",,ValorCorBarra( "1" ,aCorBarras ),RGB( 0, 128, 128 ),2,CLR_BLACK}},cView,,"P" ,})
	EndIf
Next i
aConfig	:= {,.T.,.T.,.T.,.T.,.T.,.F.,"1",.F.}
aDescricao := {{STR0011,35},{STR0020,140}}//"Codigo"###"Descricao"

If Len(aGantt) = 0 .and. nFldAtu == 2
	MsgStop(STR0021) //"Objetivo sem Metas informadas."
	oDlg:Refresh()
	Return .f.
EndIf

Return .t.

/*/


Ŀ
Funo    Sg300CarArray Autor  Thiago Olis Machado    Data 22/04/2005
Ĵ
Descrio Funcao de carga do array                                       
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/
Function Sg300CarArray()
Local i, dDtIni

DbSelectArea("TBI")
DbSetOrder(1)
DbSeek(xFilial("TBI")+M->TBH_CODOBJ)
Do While !Eof() .and. xFilial("TBI") == TBI->TBI_FILIAL .and.;
	TBI->TBI_OBJETI == M->TBH_CODOBJ

	DbSelectArea("TAA")
	DbSetOrder(1)
	If DbSeek(xFilial("TAA")+ TBI->TBI_META) .and. TAA->TAA_STATUS == "1"
		//Adiciona metas no array
		Aadd(aQDGDoc,{"",TBI->TBI_META,TAA->TAA_NOME,TAA->TAA_DTFIPR})

		aQdGDoc:= aSort( aQdGDoc,,,{ |x,y| x[4] < y[4] } )
		oQdg:bLine:= bQdgLine1
		dDtIni := CTOD("  /  /  ")
		For i := 1 To Len(aQdgDoc)
			dbSelectArea("TAA")
			dbSetOrder(1)
			dbSeek(xFilial("TAA")+aQdGDoc[i][2])
			If TAA->TAA_DTINPR < dDtIni .Or. Empty(dDtIni)
				dDtIni := TAA->TAA_DTINPR
			EndIf
		Next
		//Modifica datas do objetivo
		M->TBH_ABERTU := dDtIni
		M->TBH_FECHAM := aQdGDoc[Len(aQdgDoc)][4]
		oEnc01:oBox:Refresh()
	EndIf

	DbSelectArea("TBI")
	DbSkip()
Enddo
oQdg:GoTop()
oQdg:Refresh()

Return .T.

/*/


Ŀ
Funo     Sg300Obrig   Autor  Thiago Olis Machado    Data 22/04/2005
Ĵ
Descrio Checa campos obrigatorios e grava registro.                    
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/
Function Sg300Obrig(nOpcx)
Local aOldArea := GetArea() // Guarda variaveis de alias e indice
Local lTipFla := NGCADICBASE( "TBH_FLAG" , "A" , "TBH" , .F. )
Local i

If !Obrigatorio(aGets,aTela)
	Return .f.
Endif

//Ŀ
//Manipula a tabela TBH
//

DbSelectArea('TBH')
DbSetOrder(1)
If DbSeek(xFilial('TBH')+M->TBH_CODOBJ)
	RecLock('TBH',.f.)
Else
	RecLock('TBH',.t.)
EndIf

If nOpcx <> 5
	TBH->TBH_FILIAL := xFilial('TBH')
	TBH->TBH_CODOBJ := M->TBH_CODOBJ
	TBH->TBH_DESCRI := M->TBH_DESCRI
	TBH->TBH_SITUAC := M->TBH_SITUAC
	TBH->TBH_PRAZO  := M->TBH_PRAZO
	TBH->TBH_ABERTU := M->TBH_ABERTU
	TBH->TBH_FECHAM := M->TBH_FECHAM
	TBH->TBH_RESPON := M->TBH_RESPON
	TBH->TBH_PRIORI := M->TBH_PRIORI
	If lTipFla
		TBH->TBH_FLAG := M->TBH_FLAG
		TBH->TBH_TIPO := M->TBH_TIPO
	EndIf
Else
	TBH->TBH_SITUAC := "4"
EndIf
MsUnlock("TBH")

If nOpcx == 5
	//Cancela todos os planos de ao relacionados
	dbSelectArea("TBI")
	dbSetOrder(1)
	If dbSeek(xFilial("TBI")+M->TBH_CODOBJ)
		If MsgYesNo(STR0032, STR0019) //"Deseja cancelar todos Planos de Ao relacionados ao Objetivo tambm?"###"Atencao"
			While !Eof() .and. xFilial("TBI") == TBI->TBI_FILIAL .and. TBI->TBI_OBJETI == M->TBH_CODOBJ
				dbSelectArea("TAA")
				dbSetOrder(1)
				If dbSeek(xFilial("TAA")+TBI->TBI_META)
					RecLock("TAA",.F.)
					TAA->TAA_STATUS := "3"
					MsUnlock("TAA")
					//Atualiza Data dos objetivos
					SG90ATOBJ(TAA->TAA_CODPLA)
				Endif
				dbSelectArea("TBI")
				dbSkip()
			End
		EndIf
	Endif
Else
	For i:= 1 To Len( aQdgDoc )
		DbSelectArea('TBI')
		DbSetOrder(1)
		If !DbSeek(xFilial('TBI')+M->TBH_CODOBJ+aQdgDoc[i][2])
			RecLock('TBI',.t.)
		Else
			RecLock('TBI',.f.)
		Endif
		TBI->TBI_FILIAL := xFilial('TBI')
		TBI->TBI_OBJETI := M->TBH_CODOBJ
		TBI->TBI_META   := aQdgDoc[i,2]
		MsUnLock('TBI')
	Next

	dbSelectArea("TBI")
	dbSetOrder(1)
	dbSeek(xFilial("TBI")+M->TBH_CODOBJ)
	While !Eof() .And. TBI->TBI_OBJETI == M->TBH_CODOBJ
		nPos := Ascan(aQdgDoc, { |X| X[2] == TBI->TBI_META})
		If nPos == 0
			RecLock('TBI',.f.)
			DbDelete()
			MsUnLock('TBI')
		EndIf
		TBI->(DbSkip())
	End
Endif

RestArea(aOldArea)

If Len( aQDGDoc ) <= 0
	MsgStop(STR0025,STR0023)
	Return .F.
EndIf

Return .t.

/*/


Ŀ
Funo     Sg300Situac  Autor  Thiago Olis Machado    Data 22/04/2005
Ĵ
Descrio Muda a situacao do objetivo.                                   
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/
Function Sg300Situac(nOpcao)
Local lRet := .F.

If nOpcao = 4
	DbSelectArea("TBH")
	DbSetOrder(1)
	If DbSeek(xFilial("TBH")+M->TBH_CODOBJ)
		RecLock("TBH",.f.)
		If M->TBH_SITUAC == "1"
			TBH->TBH_SITUAC := "2"
		ElseIf M->TBH_SITUAC == "2"
			TBH->TBH_SITUAC := "3"
		EndIf
		MsUnLock("TBH")
		lRet := .T.
	EndIf
Else
	MsgAlert(STR0026) //"Opo apenas disponvel no modo de alterao."
EndIf

Return lRet

/*/


Ŀ
 Funo    Sg300Leg  Autor  Thiago Olis Machado    Data  02.06.05 
Ĵ
 Descrio Cria uma janela contendo a legenda da mBrowse              
Ĵ
 Uso       Sgaa300		                                              
ٱ


/*/
Function Sg300Leg()
BrwLegenda(cCadastro,STR0006,{{"ENABLE",OemToAnsi(STR0015)},; //"Legenda"###"Em Dia"
                              {"BR_VERMELHO",OemToAnsi(STR0016)}})      //"Atrasada"
Return .T.

/*/


Ŀ
 Funo   Sg300ViMe  Autor  Thiago Olis Machado    Data 03/05/2005
Ĵ
 Descrio Visualiza o plano de acao.                                 
Ĵ
 Uso       Sgaa300		                                              
ٱ


/*/

Function Sg300ViMe( aQDG,nPos,nOpcx )
Local aArea := GetArea()

If Len( aQDG ) > 0
	DbSelectArea("TAA")
	DbSetOrder(1)
	If DbSeek(xFilial("TAA")+aQDG[nPos,2])
		Sg090Ca( "TAA", Recno(), nOpcx )
	EndIf
EndIf

oQDG:Refresh()
RestArea(aArea)
Return .t.

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 29/11/2006
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaSGA                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          	  1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
Local aRotina :=	{ { STR0001    , "AxPesqui"  , 0 , 1},;     	//"Pesquisar"
					  { STR0002    , "Sg300Cad"  , 0 , 2},;    		//"Visualizar"
                      { STR0003    , "Sg300Cad"  , 0 , 3},;    		//"Incluir"
                      { STR0004    , "Sg300Cad"  , 0 , 4},;    		//"Alterar"
                      { STR0033    , "Sg300Cad"  , 0 , 5, 3},;   	// //"Cancelar"
                      { STR0006    , "Sg300Leg"  , 0 , 5, 3, .F.}}	//"Legenda"

Return aRotina
//---------------------------------------------------------------------------------------------------
/*{Protheus.doc} SGA300VLD
Funo para Validao do campos datas

@return

@sample
SGA300VLD()

@author Juliani Schlickmann Damasceno
@since 05/11/2013
@version 1.0
*/
//---------------------------------------------------------------------------------------------------
Function SGA300VLD()

Local dPrazo := M->TBH_PRAZO
Local lRet := .T.

//No permite que a data do prazo seja maior que a data da base
if !empty(dPrazo) .AND. (dPrazo < dDataBase)
	ShowHelpDlg( STR0035 , { STR0036 } , 2 , { STR0037 } , 2 )//Titulo , Problema , Linhas Problema , Solucao , Linhas da Solucao
	lRet := .F.
EndIf

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} SGA300SXB
Funo para realizar filtro do F3

@type function

@source SGAA300.prx

@author Jean Pytter da costa
@since 01/02/2017

@sample SGA300SXB()

@return Logico, Indica se todas validaes esto corretas.
/*/
//---------------------------------------------------------------------
Function SGA300SXB()

Return If( IsInCallStack( "SGAA300" ) .Or. IsInCallStack( "SGAA310" ), TAA->TAA_TPMETA <> "3" .And. TAA->TAA_PERCEN < 100 .And. TAA->TAA_STATUS <> '3',;
 				TAA->TAA_PERCEN < 100 .And. TAA->TAA_STATUS <> '3')