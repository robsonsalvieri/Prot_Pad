#INCLUDE "MDTC310.ch"
#Include "Protheus.ch"

#DEFINE _nVERSAO 2 //Versao do fonte
/*


Ŀ
Funo     MDTC310   Autor  Inacio Luiz Kolling    Data 06/06/2000
Ĵ
Descrio  Consulta Geral de Programacao de Exames                    
Ĵ
 Uso       Generico                                                   
ٱ


*/
FUNCTION MDTC310()
//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  	  
//
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)


lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )

aRotina := MenuDef()
cPROGRAMA := "MDTC310"

If FindFunction("MDTRESTRI") .AND. !MDTRESTRI(cPrograma)
	//Ŀ
	// Devolve variaveis armazenadas (NGRIGHTCLICK) 			 			  
	//
	NGRETURNPRM(aNGBEGINPRM)
	Return .F.
Endif

If lSigaMdtps
	cCadastro := OemtoAnsi(STR0006)  //"Clientes"

	DbSelectArea("SA1")
	DbSetOrder(1)

	mBrowse( 6, 1,22,75,"SA1")

Else

	cCadastro := OemToAnsi(STR0004) //"Programa de Exames"

	//Ŀ
	// Endereca a funcao de BROWSE                                  
	//

	mBrowse( 6, 1,22,75,"TMW")

	//Ŀ
	// Recupera a Ordem Original do arquivo principal               
	//
	dbSelectArea("TMW")
	dbSetOrder(1)

Endif

//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK)                          
//
NGRETURNPRM(aNGBEGINPRM)

Return .T.
/*


Ŀ
Funo    MDC310EXA  Autor  Inacio Luiz Kolling    Data 06/06/2000
Ĵ
Descrio  Monta um browse com os Exames                              
Ĵ
 Uso       MDTC310                                                    
ٱ


*/
Function MDC310EXA
Local OldRot
Local cFilOld := cFilAnt //Salva filial corrente
Local lTMW_Filial := If( Empty(xFilial("TMW")) .and. !Empty(xFilial("SRA")) , .t. , .f. ) //Indica se o PCMSO  para varias filiais

OldRot := aCLONE(aROTINA)

aRotina := { { STR0002  ,"NGCAD01"    , 0 , 2}}  //"Visualizar"

M->TM5_PCMSO := TMW->TMW_PCMSO

dbSelectArea("TM5")
dbSetOrder(1)

aNGFIELD  := {}
bGRAVAFIL := {}
aFilRelat := {}

dbSelectArea("TM5")
dbSetOrder(1)

cKey   := M->TM5_PCMSO
bWHILE := {|| !eof() .AND. TM5->TM5_PCMSO == M->TM5_PCMSO}
bFOR   := {|| TM5_FILIAL == xFilial("TM5") .and. TM5_PCMSO == M->TM5_PCMSO }
cTRBPG := GetNextAlias()

If lTMW_Filial
	If !Empty(xFilial("TM5"))
		cFilInic := TMW->TMW_FILDE
		cUltFil  := If( Empty(TMW->TMW_FILATE) , Replicate("Z",Len(TMW->TMW_FILATE)) , TMW->TMW_FILATE )
		aAreaSM0 := SM0->(GetArea())
		dbSelectArea("SM0")
		dbGoTop()
		If Empty(cFilInic)
			dbSeek(cEmpAnt)
		Else
			dbSeek(cEmpAnt+cFilInic)
		Endif
		cFilAnt := SM0->M0_CODFIL
		While !Eof() .and. SM0->M0_CODIGO == cEmpAnt .and. SM0->M0_CODFIL <= cUltFil
			aAdd( aFilRelat , SM0->M0_CODFIL )
			dbSkip()
		End
		RestArea(aAreaSM0)

		bGRAVAFIL := {|| MDT310PRC("TM5",cTRBPG,cKey,bWHILE,bFOR,aFilRelat)}
	Endif
Else
	If Empty(xFilial("TM5"))
		bFOR := {|| TM5_FILIAL == xFilial("TM5") .and. TM5_PCMSO == M->TM5_PCMSO .and. (EMPTY(TM5_FILFUN) .OR. TM5_FILFUN == cFilAnt) }
	Endif
Endif

dbSelectArea("TM5")
DbSetOrder(3)

NGCONSULTA(cTRBPG, cKey, bWHILE, bFOR, aRotina,{}, aNGFIELD,,, bGRAVAFIL )

dbSelectArea("TM5")
dbSetOrder(1)

aRotina := aCLONE(OldRot)
cFilAnt := cFilOld //Retorna a filial corrente

Return
/*


Ŀ
Funo    MDT310PRC  Autor  Denis                  Data           
Ĵ
Descrio Processa o arquivo de historico                             
Ĵ
 Uso       MDTC310                                                    
ٱ


*/
Function MDT310PRC(cALI,cMestre,cKey02,bWHILE,bFOR,aFilRelat)
Local nX
Local cFilOld := cFilAnt
Local cFilIni

If Len(aFilRelat) > 1
	cFilIni := aFilRelat[1]
	For nX := 2 to Len(aFilRelat)
		cFilAnt := aFilRelat[nX]
		dbSelectArea(cALI)
		dbSetOrder(3)
		dbseek(xFilial(cALI,aFilRelat[nX])+cKEY02)
		DbEval( {|| fNGGRAVA(cALI,cMestre,cFilIni)},bFOR,bWHILE)
		dbSelectArea(cMESTRE)
	Next nX
Endif

cFilAnt := cFilOld
Return .T.

/*


Ŀ
Funo     fNGGRAVA  Autor  Denis                  Data           
Ĵ
Descrio  GRAVA                                                      
Ĵ
 Uso       MDTC310                                                    
ٱ


*/
Static Function fNGGRAVA(cALI,cTRB,cFilIni)
Local i

DbSelectArea(cTRB)
DbAppend()

For i := 1 To FCOUNT()
    ny := FieldName(i)
    DbSelectArea(cALI)
    If FieldPos( ny ) == 0
       DbSelectArea(cTRB)
       Loop
    Endif
    DbSelectArea(cTRB)
    ny := cTRB + "->" + FieldName(i)
    nx := cALI + "->" + FieldName(i)
    &ny. := &nx.
Next
ny := cTRB + "->" + "TM5_FILIAL"
&ny. := xFilial("TM5",cFilIni)
DbselectArea(cALI)
Return

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 29/11/2006
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaMDT                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()

Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )
Local aRotina

If lSigaMdtps
	aRotina := { { STR0002,   "NGCAD01"   , 0 , 2},; //"Visualizar"
	             { StrTran(STR0005, "'", ""),   "MDTC310P"  , 0 , 4} } //"PCMSO's"
Else
	aRotina :=  { { STR0002   ,"NGCAD01"  , 0 , 2},; //"Visualizar"
                  { STR0003   ,"MDC310EXA" , 0 , 2} } //"Exames"
Endif

Return aRotina
/*


Ŀ
Funo    MDC310EXA  Autor  Inacio Luiz Kolling    Data 06/06/2000
Ĵ
Descrio  Monta um browse com os Exames                              
Ĵ
 Uso       MDTC310                                                    
ٱ


*/
Function MDTC310P()

Local aArea	:= GetArea()
Local oldROTINA := aCLONE(aROTINA)
Local oldCad := cCadastro
Private nSizeSA1  := If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1]))
Private nSizeLo1  := If((TAMSX3("A1_LOJA")[1]) < 1,6,(TAMSX3("A1_LOJA")[1]))

cCliMdtPs := SA1->A1_COD+SA1->A1_LOJA

aRotina :=  { { STR0001   ,"AxPesqui"  , 0 , 1},; //"Pesquisar"
              { STR0002   ,"NGCAD01"  , 0 , 2},; //"Visualizar"
              { STR0003   ,"MDC310EXA" , 0 , 2} } //"Exames"

cCadastro := OemToAnsi(STR0004) //"Programa de Exames"

//Ŀ
// Endereca a funcao de BROWSE                                  
//
dbSelectArea("TMW")
Set Filter To SubSTR(TMW->TMW_CCATE,1,nSizeSA1+nSizeLo1) == cCliMdtps
dbSetOrder(1)
mBrowse( 6, 1,22,75,"TMW")

//Ŀ
// Recupera a Ordem Original do arquivo principal               
//
dbSelectArea("TMW")
dbSetOrder(1)
Set Filter To

Return .T.