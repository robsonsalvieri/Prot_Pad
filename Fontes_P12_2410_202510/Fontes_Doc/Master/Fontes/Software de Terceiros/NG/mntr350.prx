#INCLUDE "MNTR350.CH"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR350  ³ Autor ³ Deivys Joenck         ³ Data ³ 15/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Analise de Recapadoras                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR350()

	Local aNGBEGINPRM := NGBEGINPRM()

	Local oReport
	Local aArea := GetArea()

	Private cTRB1 := GetNextAlias()
	Private cTRB2 := GetNextAlias()

	Private nSizeForn := TAMSX3("A2_COD")[1] //Considera tamanho do codigo do fornecedor pelo SX3

	If FindFunction("TRepInUse") .And. TRepInUse()
		//-- Interface de impressao
		oReport := ReportDef()
		oReport:SetPortrait() //Default Retrato
		oReport:PrintDialog()
	Else
		MNTR350R3()
	EndIf
	RestArea(aArea)

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³Elisangela Costa       ³ Data ³  28/06/06³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Define as secoes impressas no relatorio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR350                                                    ³±±
±±|__________|____________________________________________________________|±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function ReportDef()

	Local oReport
	Local oSection1
	Local oSection2

	//LAYOUT
	/*
	1         2         3         4         5         6         7         8         9        10       110       120
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123012345678901230123
	______________________________________________________________________________________________________________________________________________
	Performance dos Fornecedores de Recapagem de Pneus
	______________________________________________________________________________________________________________________________________________
	Fornecedor       Descricao                               Familia  Descricao                       Reformas   Media Km
	______________________________________________________________________________________________________________________________________________
	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx       999  9.999.999
	xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx       999  9.999.999
	xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx       999  9.999.999


	xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx       999  9.999.999
	xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx       999  9.999.999
	xxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx       999  9.999.999


	*/

	//+------------------------------------------------------------------------+
	//|Criacao do componente de impressao                                      |
	//|                                                                        |
	//|TReport():New                                                           |
	//|ExpC1 : Nome do relatorio                                               |
	//|ExpC2 : Titulo                                                          |
	//|ExpC3 : Pergunte                                                        |
	//|ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  |
	//|ExpC5 : Descricao                                                       |
	//|                                                                        |
	//+------------------------------------------------------------------------+
	oReport := TReport():New("MNTR350",OemToAnsi(STR0001),"MNT350",{|oReport| ReportPrint(oReport)},STR0001+". "+STR0002+" "+STR0003)
	oReport:SetTotalInLine(.F.)

	//+--------------------------------------------------------------+
	//| Variaveis utilizadas para parametros!                        |
	//| mv_par01     // De  Servico                                  |
	//| mv_par02     // Ate Servico                                  |
	//| mv_par03     // De  Familia                                  |
	//| mv_par04     // Ate Familia                                  |
	//| mv_par05     // De  Data                                     |
	//| mv_par06     // Ate Data                                     |
	//| mv_par07     // De  Fornecedor                               |
	//| mv_par08     // Ate Fornecedor                               |
	//+--------------------------------------------------------------+
	Pergunte(oReport:uParam,.F.)

	//+-------------------------------------------------------------------------+
	//| Criacao da secao utilizada pelo relatorio                               |
	//|                                                                         |
	//| TRSection():New                                                         |
	//| ExpO1 : Objeto TReport que a secao pertence                             |
	//| ExpC2 : Descricao da seçao                                              |
	//| ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   |
	//|         sera considerada como principal para a seção.                   |
	//| ExpA4 : Array com as Ordens do relatório                                |
	//| ExpL5 : Carrega campos do SX3 como celulas                              |
	//|         Default : False                                                 |
	//| ExpL6 : Carrega ordens do Sindex                                        |
	//|         Default : False                                                 |
	//|                                                                         |
	//+-------------------------------------------------------------------------+

	//+-------------------------------------------------------------------------+
	//| Criacao da celulas da secao do relatorio                                |
	//|                                                                         |
	//| TRCell():New                                                            |
	//| ExpO1 : Objeto TSection que a secao pertence                            |
	//| ExpC2 : Nome da celula do relatório. O SX3 será consultado              |
	//| ExpC3 : Nome da tabela de referencia da celula                          |
	//| ExpC4 : Titulo da celula                                                |
	//|         Default : X3Titulo()                                            |
	//| ExpC5 : Picture                                                         |
	//|         Default : X3_PICTURE                                            |
	//| ExpC6 : Tamanho                                                         |
	//|         Default : X3_TAMANHO                                            |
	//| ExpL7 : Informe se o tamanho esta em pixel                              |
	//|         Default : False                                                 |
	//| ExpB8 : Bloco de código para impressao.                                 |
	//|         Default : ExpC2                                                 |
	//|                                                                         |
	//+-------------------------------------------------------------------------+

	oSection1 := TRSection():New(oReport,STR0016,{(cTRB2),"SA2","ST6"}) // Fornecedores
	oSection1:SetHeaderBreak() 											// Sempre que houver quebra imprime o cabeçalho da seção
	TRCell():New(oSection1,"CODFORN" ,"    ",STR0011,"@!"  ,nSizeForn ,/*lPixel*/,{||NGRCODFOR()})   // "Fornecedor"
	TRCell():New(oSection1,"A2_NOME" ,"SA2" ,STR0012,"@!"  ,40,/*lPixel*/)                           // "Descrição"
	TRCell():New(oSection1,"CODFAMI" ,"    ",STR0013,"@!"  ,6 ,/*lPixel*/,{||NGRCODFAM()})           // "Familia"
	TRCell():New(oSection1,"T6_NOME" ,"ST6" ,STR0012,"@!"  ,40,/*lPixel*/)                           // "Descrição"
	TRCell():New(oSection1,"REFORMA" ,"    ",STR0014,"999" ,3 ,/*lPixel*/,{||NGRETVAL(1)})           // "Reforma"
	TRCell():New(oSection1,"TOTKM"   ,"    ",STR0015,"@E 99,999,999",9 ,/*lPixel*/,{||NGRETVAL(2)})  // "Media Km"

	//Definicao para imprimir os cabecalhos de campos numericos da esquerda para a direita
	oSection1:Cell("REFORMA"):SetHeaderAlign("RIGHT")
	oSection1:Cell("TOTKM"):SetHeaderAlign("RIGHT")
	TRPosition():New(oSection1,"ST6",1,{|| xFilial("ST6")+cFAMILIA})
	TRPosition():New(oSection1,"SA2",1,{|| xFilial("SA2")+cFORNECE})

	oBreak := TRBreak():New(oSection1,oSection1:Cell("CODFORN"),STR0008,.F.) //"Total do Forncecedor"
	TRFunction():New(oSection1:Cell("REFORMA"),/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
	TRFunction():New(oSection1:Cell("TOTKM"  ),/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)

Return oReport

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR350R3³ Autor ³ Deivys Joenck         ³ Data ³ 15/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio de Analise de Recapadoras                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR350R3()

	Local WNREL      := "MNTR350"
	Local LIMITE     := 132
	Local cDESC1     := STR0001 // "Analise de Performance dos Fornecedores de Recapagem de Pneus."
	Local cDESC2     := STR0002 // "O usuario pode selecionar quais os campos que deverao ser mostrados,"
	Local cDESC3     := STR0003 // "bem como informar parametros de selecao para a impressao."
	Local cSTRING    := "STL"

	Private NOMEPROG := "MNTR350"
	Private TAMANHO  := "M"
	Private aRETURN  := {STR0004,1,STR0005,1,2,1,"",1}  // "Zebrado"###"Administracao"
	Private TITULO   := STR0001 						// "Analise de Performance dos Fornecedores de Recapagem de Pneus."
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1, CABEC2

	//+--------------------------------------------------------------+
	//| Verifica as perguntas selecionadas                           |
	//+--------------------------------------------------------------+

	//+--------------------------------------------------------------+
	//| Variaveis utilizadas para parametros!                        |
	//| mv_par01     // De  Servico                                  |
	//| mv_par02     // Ate Servico                                  |
	//| mv_par03     // De  Familia                                  |
	//| mv_par04     // Ate Familia                                  |
	//| mv_par05     // De  Data                                     |
	//| mv_par06     // Ate Data                                     |
	//| mv_par07     // De  Fornecedor                               |
	//| mv_par08     // Ate Fornecedor                               |
	//+--------------------------------------------------------------+
	Private cPERG := "MNT350"
	Pergunte(cPERG,.F.)

	//+--------------------------------------------------------------+
	//| Envia controle para a funcao SETPRINT                        |
	//+--------------------------------------------------------------+
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("STL")
		Return
	EndIf

	SetDefault(aRETURN,cSTRING)
	RptStatus({|lEND| MNTR350IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)
	DbSelectArea("STL")

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR350IMP³ Autor ³ Deivys Joenck         ³ Data ³ 15/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR350                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR350IMP(lEND,WNREL,TITULO,TAMANHO)

	Local cRODATXT  := ""
	Local nCNTIMPR  := 0
	Local aARRAY350 := {}
	Local nPOS1 := 0
	Local nPOS2 := 0
	Local lDadosImp := .F. //Verifica se deve ser impresso o relatório

	Local oARQT350R1
	Local oARQT350R2

	// Variaveis para controle do cursor de progressao do relatorio
	Local nTOTREGS := 0,nMULT := 1,nPOSANT := 4,nPOSATU := 4,nPOSCNT := 0

	// Contadores de linha e pagina
	Private li := 80 ,m_pag := 1
	Private aVETINR := {}

	// Verifica se deve comprimir ou nao
	nTIPO  := IIF(aRETURN[4]==1,15,18)

	// Monta os Cabecalhos
	CABEC1 := STR0006 // "Fornec. Descricao"
	CABEC2 := STR0007 // "   Familia Decricao                            Reformas       Media Km"

	/*/
	1         2         3         4         5         6         7         8         9
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789
	******************************************************************************************
	Fornec.                Descricao
	Familia       		   Descricao                           Reformas       Media Km
	******************************************************************************************
	xxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxxxx  			   xxxxxxxxxxxxxxxxxxxxxxxx                 999       9.999.999
	xxxxxxxx  			   xxxxxxxxxxxxxxxxxxxxxxxx                 999       9.999.999

	TOTAL DO FORNECEDOR       										999       9.999.999
	/*/

	aDBFR350R1 := {{"CBEM"  ,"C",16,0},;
				   {"CONTA" ,"N",09,0},;
				   {"FORNEC","C",nSizeForn,0},;
				   {"FAMI"  ,"C",06,0}}

	//Cria Tabela Temporária
	oARQT350R1 := NGFwTmpTbl(cTRB1,aDBFR350R1,{{"CBEM","CONTA"}})

	aDBFR350R2 := {{"FORNECE","C",nSizeForn,0},;
				   {"FAMILIA","C",06,0},;
				   {"CODBEM" ,"C",16,0},;
				   {"TOTKM"  ,"N",09,0},;
				   {"REFORMA","N",03,0}}

	//Cria Tabela Temporária
	oARQT350R2 := NGFwTmpTbl(cTRB2,aDBFR350R2,{ {"FORNECE","FAMILIA","CODBEM"}})

	Processa({|lEND| MNTR350ST9()},STR0009)  // "Processando Arquivo..."
	Processa({|lEND| MNTR350TRB1()},STR0010) // "Selecionando Registros..."

	nTOTKM   := 0
	nREFORMA := 0
	lPRI350  := .T.
	DbSelectArea(cTRB2)
	DbGotop()
	SetRegua(LastRec())
	While !Eof()

		cFORNECE := (cTRB2)->FORNECE
		nTOTFORN := 0
		nTOTREFO := 0
		If lPRI350
			lPRI350 := .F.
			NGSOMALI(58)
		EndIf
		@Li,000 Psay (cTRB2)->FORNECE
		@LI,023 Psay NGSEEK('SA2',(cTRB2)->FORNECE,1,'A2_NOME')
		NGSOMALI(58)

		DbSelectArea(cTRB2)
		While !Eof() .And. (cTRB2)->FORNECE = cFORNECE

			nTOTKM   := 0
			nREFORMA := 0
			cFAMILIA := (cTRB2)->FAMILIA
			@LI,000 Psay (cTRB2)->FAMILIA
			@LI,023 Psay NGSEEK('ST6',(cTRB2)->FAMILIA,1,'Substr(T6_NOME,1,30)')
			DbSelectArea(cTRB2)
			While !Eof() .And. (cTRB2)->FORNECE = cFORNECE  .And. (cTRB2)->FAMILIA = cFAMILIA
				lDadosImp := .T.
				IncRegua()
				nTOTKM   := nTOTKM + (cTRB2)->TOTKM
				nREFORMA := nREFORMA + (cTRB2)->REFORMA
				DbSelectArea(cTRB2)
				DbSkip()
			End
			@LI,065 Psay nREFORMA          Picture "999"
			@LI,073 Psay nTOTKM / nREFORMA Picture '@E 99,999,999'

			nTOTFORN := nTOTFORN + nTOTKM
			nTOTREFO := nTOTREFO + nREFORMA
			NGSOMALI(58)
		End
		NGSOMALI(58)
		@LI,000 Psay STR0008 //"TOTAL DO FORNECEDOR"
		@LI,063 Psay nTOTREFO Picture "99999"
		@LI,073 Psay nTOTFORN / nTOTREFO Picture '@E 99,999,999'
		NGSOMALI(58)
		NGSOMALI(58)
	End
	If !lDadosImp
		MsgInfo(STR0017) //"Não há dados para impressão no relatório."
		Return .F.
	EndIf

	RODA(nCNTIMPR,cRODATXT,TAMANHO)

	//Deleta o arquivo temporario fisicamente
	oARQT350R1:Delete()
	oARQT350R2:Delete()

	RetIndex("ST9")
	RetIndex("STJ")
	RetIndex("STL")
	RetIndex("STS")
	RetIndex("STT")

	Set Filter To
	Set Device To Screen
	If aRETURN[5] == 1
		Set Printer To
		dbCommitAll()
		Ourspool(WNREL)
	EndIf
	MS_FLUSH()

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ReportPrint³ Autor ³ Elisangela Costa      ³ Data ³  28/06/06³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ ReportDef                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ReportPrint(oReport)

	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)
	Local oARQT350R1
	Local oARQT350R2
	Local lDadosImp := .F. // Indentifica caso não há dados para serem apresentados

	Private aVETINR := {}

	aDBFR350R1 := {{"CBEM"  ,"C",16,0},;
				   {"CONTA" ,"N",09,0},;
				   {"FORNEC","C",nSizeForn,0},;
				   {"FAMI"  ,"C",06,0}}

	//Cria Tabela Temporária
	oARQT350R1 := NGFwTmpTbl(cTRB1,aDBFR350R1,{{"CBEM","CONTA"}})

	aDBFR350R2 := {{"FORNECE","C",nSizeForn,0},;
				   {"FAMILIA","C",06,0},;
				   {"CODBEM" ,"C",16,0},;
				   {"TOTKM"  ,"N",09,0},;
				   {"REFORMA","N",03,0}}

	//Cria Tabela Temporária
	oARQT350R2 := NGFwTmpTbl(cTRB2,aDBFR350R2,{{"FORNECE","FAMILIA","CODBEM"}})

	Processa({|lEND| MNTR350ST9() }, STR0009) // "Processando Arquivo..."
	Processa({|lEND| MNTR350TRB1()}, STR0010) // "Selecionando Registros..."

	oSection1:Init()
	DbSelectArea(cTRB2)
	DbGotop()
	oReport:SetMeter(RecCount())
	While !Eof() .And. !oReport:Cancel()
		lDadosImp := .T.
		cFORNECE  := (cTRB2)->FORNECE
		cFORNECE2 := " "
		nTOTFORN  := 0
		nTOTREFO  := 0
		DbSelectArea(cTRB2)
		While !Eof() .And. !oReport:Cancel() .And. (cTRB2)->FORNECE = cFORNECE
			nTOTKM   := 0
			nREFORMA := 0
			cFAMILIA := (cTRB2)->FAMILIA
			While !Eof() .And. !oReport:Cancel() .And. (cTRB2)->FORNECE = cFORNECE .And. (cTRB2)->FAMILIA = cFAMILIA
				oReport:IncMeter()
				nTOTKM   += (cTRB2)->TOTKM
				nREFORMA += (cTRB2)->REFORMA
				DbSelectArea(cTRB2)
				DbSkip()
			End
			nTOTFORN  += nTOTKM
			nTOTREFO  += nREFORMA
			If cFORNECE = cFORNECE2
				oSection1:Cell("CODFORN"):Hide()
				oSection1:Cell("A2_NOME"):Hide()
			Else
				oSection1:Cell("CODFORN"):Show()
				oSection1:Cell("A2_NOME"):Show()
			EndIf
			cFORNECE2 := (cTRB2)->FORNECE
			oSection1:PrintLine()
		End
		oSection1:Cell("CODFORN"):Show()
		oSection1:Cell("A2_NOME"):Show()
	End
	If !lDadosImp
		MsgInfo(STR0017) //"Não há dados para impressão no relatório."
		Return .F.
	EndIf
	oSection1:Finish()

	//Deleta o arquivo temporario fisicamente
	oARQT350R1:Delete()
	oARQT350R2:Delete()

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR350ST9
Processa os arquivos

@type function

@source MNTR350.prx

@author Jean Pytter da costa
@since 20/05/2019

@sample MNTR350()

@return Logico.
/*/
//---------------------------------------------------------------------
Static Function MNTR350ST9()

	Local aCont1Men := {}
	Local cSerRef   := AllTrim( SuperGetMv( "MV_NGSEREF" ) )
	Local aServRef 	:= StrTokArr( cSerRef, ';' )
	Local cQryRef   := GetNextAlias()
	Local cModSTJ := '%' + NGMODCOMP( 'ST9', 'STJ' ) + '%'
	Local cModSTS := '%' + NGMODCOMP( 'ST9', 'STS' ) + '%'

	BeginSQL Alias cQryRef

		SELECT
			ST9.T9_CODFAMI AS CODFAMI,
			STJ.TJ_CODBEM  AS CODBEM ,
			STJ.TJ_SERVICO AS SERVICO,
			STJ.TJ_DTMRINI AS DTMRINI,
			STJ.TJ_HOMRINI AS HOMRINI,
			STL.TL_CODIGO  AS CODIINS
		FROM
			%table:ST9% ST9
		INNER JOIN
			%table:STJ% STJ
				ON %exp:cModSTJ% AND
				   STJ.TJ_CODBEM = ST9.T9_CODBEM AND
				   STJ.%NotDel% AND ST9.%NotDel%
		INNER JOIN
			%table:STL% STL
				ON STL.TL_ORDEM  = STJ.TJ_ORDEM  AND
		     	   STL.TL_PLANO  = STJ.TJ_PLANO  AND
				   STL.TL_FILIAL = STJ.TJ_FILIAL AND
				   RTRIM( STL.TL_SEQRELA ) <> %exp:'0'% AND
				   STL.%NotDel%
		WHERE
			ST9.T9_FILIAL = %xFilial:ST9% AND
				ST9.T9_CATBEM = %exp:'3'% AND STJ.TJ_TIPOOS = %exp:'B'% AND
				STL.TL_TIPOREG = %exp:'T'% AND STJ.TJ_SITUACA = %exp:'L'% AND
				STJ.TJ_TERMINO = %exp:'S'% AND
				( STJ.TJ_SERVICO BETWEEN %exp:MV_PAR01% AND %exp:MV_PAR02% ) AND
				( ST9.T9_CODFAMI BETWEEN %exp:MV_PAR03% AND %exp:MV_PAR04% ) AND
				( STJ.TJ_DTORIGI BETWEEN %exp:MV_PAR05% AND %exp:MV_PAR06% ) AND
				( STL.TL_CODIGO BETWEEN %exp:MV_PAR07% AND %exp:MV_PAR08% )
		UNION
			SELECT
				ST9.T9_CODFAMI AS CODFAMI,
				STS.TS_CODBEM  AS CODBEM ,
				STS.TS_SERVICO AS SERVICO,
				STS.TS_DTMRINI AS DTMRINI,
				STS.TS_HOMRINI AS HOMRINI,
				STT.TT_CODIGO  AS CODIINS
			FROM
				%table:ST9% ST9
			INNER JOIN
				%table:STS% STS
					ON %exp:cModSTS% AND
					   STS.TS_CODBEM = ST9.T9_CODBEM AND
					   STS.%NotDel% AND ST9.%NotDel%
			INNER JOIN
				%table:STT% STT
					ON STT.TT_ORDEM  = STS.TS_ORDEM  AND
					   STT.TT_PLANO  = STS.TS_PLANO  AND
					   STT.TT_FILIAL = STS.TS_FILIAL AND
					   RTRIM( STT.TT_SEQRELA ) <> %exp:'0'% AND
					   STT.%NotDel%
			WHERE
				ST9.T9_FILIAL = %xFilial:ST9% AND
				ST9.T9_CATBEM = %exp:'3'% AND STS.TS_TIPOOS = %exp:'B'% AND
					STT.TT_TIPOREG = %exp:'T'% AND STS.TS_SITUACA = %exp:'L'% AND
					STS.TS_TERMINO = %exp:'S'% AND
					( STS.TS_SERVICO BETWEEN %exp:MV_PAR01% AND %exp:MV_PAR02% ) AND
					( ST9.T9_CODFAMI BETWEEN %exp:MV_PAR03% AND %exp:MV_PAR04% ) AND
					( STS.TS_DTORIGI BETWEEN %exp:MV_PAR05% AND %exp:MV_PAR06% ) AND
					( STT.TT_CODIGO BETWEEN %exp:MV_PAR07% AND %exp:MV_PAR08% )
	EndSQL

	dbSelectArea( cQryRef )
	While ( cQryRef )->( !Eof() )

		If aScan(aServRef, {|x| x == AllTrim(( cQryRef )->SERVICO)}) > 0

			//Procura o lançamentos do contador aproximado ou exato
			aCont1Men := NGACUMEHIS( ( cQryRef )->CODBEM , StoD( ( cQryRef )->DTMRINI ), ( cQryRef )->HOMRINI, 1,"P")

			DbSelectArea(cTRB1)
			RecLock(cTrb1,.T.)
				(cTRB1)->CBEM   := ( cQryRef )->CODBEM
				(cTRB1)->CONTA  := aCont1Men[2]
				(cTRB1)->FORNEC := ( cQryRef )->CODIINS
				(cTRB1)->FAMI   := ( cQryRef )->CODFAMI
			MsUnLock()
		EndIf

		dbSelectArea( cQryRef )
		DbSkip()
	End

	( cQryRef )->( dbCloseArea() )

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR350TRB1
Grava arquivo auxiliar

@type static function

@source MNTR350.prx

@author Deivys Joenck
@since 15/08/2001

@sample MNTR350TRB1()

@return Logico.
/*/
//---------------------------------------------------------------------
Static Function MNTR350TRB1()

	Local nContBem
	Local nCont1
	Local cCODBE

	dbSelectArea(cTrb1)
	DbGotop()
	While !Eof()
		IncProc()
		cCODBE := (cTRB1)->CBEM
		nContBem := 1
		While !Eof() .And. (cTRB1)->CBEM = cCODBE
			If nContBem == 1
				nDif := (cTRB1)->CONTA - fGetContI(cCODBE)
				nRef := 1
			Else
				nDif := (cTRB1)->CONTA - nCONT1
				nRef++
			EndIf
			DbSelectArea(cTRB2)
			lRecLock := !DbSeek((cTRB1)->FORNEC+(cTRB1)->FAMI+(cTRB1)->CBEM)
			RecLock(cTRB2, lRecLock)
				(cTRB2)->FORNECE := (cTRB1)->FORNEC
				(cTRB2)->FAMILIA := (cTRB1)->FAMI
				(cTRB2)->CODBEM  := (cTRB1)->CBEM
				(cTRB2)->TOTKM   := nDif
				(cTRB2)->REFORMA := nRef
			MsUnlock()
			nCONT1 := (cTRB1)->CONTA
			nContBem ++
			DbSelectArea(cTRB1)
			dbSkip()
		EndDo
	EndDo

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³NGRCODFAM ³ Autor ³Elisangela Costa       ³ Data ³  28/06/06³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna o codigo da Familia                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³REPORTDEF                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function NGRCODFAM()
Return cFAMILIA

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³NGRCODFOR ³ Autor ³Elisangela Costa       ³ Data ³  28/06/06³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna o codigo do fornecedor                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³REPORTDEF                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function NGRCODFOR()
Return cFORNECE

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³NGRCODFAM ³ Autor ³Elisangela Costa       ³ Data ³  28/06/06³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna o codigo do fornecedor                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³REPORTDEF                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÝÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function NGRETVAL(nCAMPORET)

	Local nVALRET

	If nCAMPORET == 1
		nVALRET := nREFORMA
	ElseIf nCAMPORET == 2
		nVALRET := nTOTKM / nREFORMA
	ElseIf nCAMPORET == 3
		nVALRET := nTOTREFO
	Else
		nVALRET := nTOTFORN
	EndIf

Return nVALRET

//--------------------------------------------------------
/*/{Protheus.doc} fGetContI
Busca contador inicial do bem
@author douglas.constancio
@since 20/07/2017
@version undefined
@param cBem, characters, codigo do bem
@type function
/*/
//--------------------------------------------------------
Static Function fGetContI(cBem)

	Local aArea := GetArea()
	Local nContIni := 0

	dbSelectArea("STP")
	dbSetOrder(8)
	If dbSeek(xFilial("STP") + cBem + 'I')
		nContIni := STP->TP_ACUMCON
	EndIf

	RestArea(aArea)

Return nContIni
