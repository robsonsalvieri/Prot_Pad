#INCLUDE "MDTC700.ch"
#Include "Protheus.ch"

#DEFINE _nVERSAO 1 //Versao do fonte

//-------------------------------------------------------------------
/*/{Protheus.doc} MDTC700
Consulta Geral de Epi's Entregues aos funcionarios. 

O programa inicia com a tabela de epi's por fornecedor, o usuário 
poderá visualizar informações do EPI, ou se clicar no botao 
funcionário, verificará os funcionários que receberão o EPI.

@type    function
@author  Thiago Machado  
@since   09/08/2000
@sample  MDTC700()

@return  Lógico, Sempre verdadeiro
/*/
//-------------------------------------------------------------------
FUNCTION MDTC700()

//Guarda conteudo e declara variaveis padroes
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

Private aRotina := MenuDef()

cCadastro := OemToAnsi(STR0004) //"Epi's Entregues"

//Endereca a funcao de BROWSE  
mBrowse( 6, 1,22,75,"SRA")


//Recupera a Ordem Original do arquivo principal  
dbSelectArea("SRA")
dbSetOrder(1)

//Retorna conteudo de variaveis padroes
NGRETURNPRM(aNGBEGINPRM)
Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MDC700DIA
Monta um browse com os Diagnosticos 

@type    function
@author  Thiago Machado  
@since   09/08/2000
@sample  MDC700DIA

@return Nil
/*/
//-------------------------------------------------------------------
Function MDC700DIA

Local OldRot
Local OldCad     := cCadastro
Local nInd       := 3
  
cCadastro := OemToAnsi(STR0004) //"Epi's Entregues"

OldRot := aCLONE(aROTINA)

aRotina := { { STR0002, "NGCAD01", 0, 2 } } //"Visualizar"

M->TNF_MAT := SRA->RA_MAT

dbSelectArea("TNF")

aNGFIELD := {}


nInd := NGRETORDEM("TNF","TNF_FILIAL+TNF_MAT+TNF_CODEPI+DTOS(TNF_DTENTR)+TNF_HRENTR",.F.)

dbSelectArea("TNF")

cKey := M->TNF_MAT

bWHILE := {|| !eof() .AND. TNF->TNF_MAT == M->TNF_MAT}

bFOR   := {|| TNF_FILIAL  == xFilial("TNF") .and. ;
				TNF_MAT == M->TNF_MAT}


dbSelectArea("TNF")
DbSetOrder(nInd)

NGCONSULTA("TRBT", cKey, bWHILE, bFOR, aRotina,{}, aNGFIELD)

dbSelectArea("SRA")
dbSetOrder(1)

aRotina := aCLONE(OldRot)
cCadastro := OldCad

Return
//-------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Utilização de Menu Funcional

@type    function
@author  Rafael Diogo Richter
@since   29/11/2006
@sample  MenuDef()
@param   param, param_type, param_descr

@Obs     Parametros do array a Rotina:
	1. Nome a aparecer no cabecalho
	2. Nome da Rotina associada
	3. Reservado
	4. Tipo de Transação a ser efetuada:
		1 - Pesquisa e Posiciona em um Banco de Dados
		2 - Simplesmente Mostra os Campos
		3 - Inclui registros no Bancos de Dados
		4 - Altera o registro corrente
		5 - Remove o registro corrente do Banco de Dados
	5. Nivel de acesso
	6. Habilita Menu Funcional

@return  aRotina, Array, Opções da rotina
/*/
//-------------------------------------------------------------------
Static Function MenuDef()

Local aRotina
						 
aRotina := { { STR0002, "NGCAD01", 0, 2 },; //"Visualizar"
				{ STR0003, "MDC700DIA", 0, 3 } } //"EPI's"

Return aRotina
