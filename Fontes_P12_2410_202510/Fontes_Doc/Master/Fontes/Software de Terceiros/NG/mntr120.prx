#INCLUDE "MNTR120.ch"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR120  ³ Autor ³ Elisangela Costa      ³ Data ³28/05/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Impressao de Solicitacao de Servico                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³TQB - Solicitacao de Servico                                ³±±
±±³Tabelas   ³ST9 - Cadastro de Bens                                      ³±±
±±³          ³TAF - Estrutura Organizacional                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR120(aListaSS,lLocComp)

	Local aNGBEGINPRM := NGBEGINPRM()

	Default aListaSS := {}
	Default lLocComp := .T.

	Private cPerg := 'MNT120'
	Private lTEMFACI := NGINTESTORG()//Verifica se tem Estrutura Organizacional
	// [LGPD] Se as funcionalidades, referentes à LGPD, podem ser utilizadas
	Private lLgpd := FindFunction( 'FWPDCanUse' ) .And. FwPdCanUse( .T. )

    /*---------------------------------------------------------------
    Vetor utilizado para armazenar retorno da função MNTTRBSTB,
    criada de acordo com o item 18 (RoadMap 2013/14)
    ---------------------------------------------------------------*/
	Private vFilTRB := MNT045TRB()

	SetKey(VK_F4, {|| MNT045FIL( vFilTRB[2] )})

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para qarametros!                        ³
	//³ mv_par01     De C.Custo                                      ³
	//³ mv_par02     Ate C.Custo                                     ³
	//³ mv_par03     De C.Trabalho                                   ³
	//³ mv_par04     Ate C.Trabalho                                  ³
	//³ mv_par05     De Familia de Bens                              ³
	//³ mv_par06     Ate Familia de Bens                             ³
	//³ mv_par07     De Bem                                          ³
	//³ mv_par08     Ate Bem                                         ³
	//³ mv_par09     De Localizacao                                  ³
	//³ mv_par10     Ate Localizacao                                 ³
	//³ mv_par11     De Solicitacao                                  ³
	//³ mv_par12     Ate Solicitacao                                 ³
	//³ mv_par13     De Data Abertura                                ³
	//³ mv_par14     Ate Data Abertura                               ³
	//³ mv_par15     Tipo (Bem, Localização, Todos)                  ³
	//³ mv_par16     Tipo de Impressao (Em Disco, Via Spool          ³
	//³ mv_par17     Lista (Abertas, Encerradas ou Todas             ³
	//³ mv_par18     Listar Localização Completa                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If Len(aListaSS) > 0
		MV_PAR18 := If(lLocComp,1,2)
		MNT120CHRE(aListaSS)
	Else
		If Pergunte(cPerg,.T.)
			MNT120CHRE()
		EndIf
	EndIf
	MNT045TRB( .T., vFilTRB[1], vFilTRB[2])

	NGRETURNPRM(aNGBEGINPRM)

Return Nil


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNT120CHRE³ Autor ³ Elisangela Costa      ³ Data ³28/05/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Chamada do relatorio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MNTR120                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNT120CHRE(aListaSS)

	Default aListaSS := {}

	Private oPrint := TMSPrinter():New(OemToAnsi(STR0001))  //"Solicitacao de Servico"

	oPrint:Setup()

	MNTR120IMP(oPrint,aListaSS)

	If Len(aListaSS) > 0
		oPrint:Preview()
	Else
		If MV_PAR16 == 2
			oPrint:Print()
		Else
			oPrint:Preview()
		Endif
	Endif
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR120IMP³ Autor ³ Elisangela Costa      ³ Data ³28/05/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Impressao do relatorio                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MNT120CHRE                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR120IMP(oPrint,aListaSS)

	Local i
	Local cCondicao   := ""
	Local cOldFil     := cFilAnt

	Default aListaSS  := {}

	Private lin := 0
	Private oFont09  := TFont():New("Courier New",09,09,,.F.,,,,.F.,.F.)
	Private oFont09B := TFont():New("Courier New",09,09,,.T.,,,,.F.,.F.)
	Private oFont11  := TFont():New("Courier New",11,11,,.F.,,,,.F.,.F.)
	Private oFont13  := TFont():New("Courier New",13,13,,.T.,,,,.F.,.F.)

	If Len(aListaSS) == 0
		If MV_PAR17 == 1
			cCondicao := 'TQB->TQB_SOLUCA == "A" .And.'
		ElseIf MV_PAR17 == 2
			cCondicao := 'TQB->TQB_SOLUCA == "E" .And.'
		EndIf
		cCondicao := cCondicao + 'TQB->TQB_CCUSTO >= MV_PAR01 .And. TQB->TQB_CCUSTO <= MV_PAR02 .And. '
		cCondicao := cCondicao + 'TQB->TQB_CENTRA >= MV_PAR03 .And. TQB->TQB_CENTRA <= MV_PAR04 .And. '
		cCondicao := cCondicao + 'TQB->TQB_DTABER >= MV_PAR13 .And. TQB->TQB_DTABER <= MV_PAR14'

		dbSelectArea("TQB")
		dbSetOrder(01)
		dbSeek(xFilial("TQB") + MV_PAR11,.T.)
		While !Eof() .And. TQB->TQB_FILIAL == xFilial("TQB") .And. TQB->TQB_SOLICI <= MV_PAR12

			If &(cCondicao)

				If MNT045STB( TQB->TQB_CODBEM, vFilTRB[2] )
					dbSelectArea("TQB")
					dbSkip()
					Loop
				EndIf

				If (MV_PAR15 == 1 .and. TQB->TQB_TIPOSS <> "B") .or. (MV_PAR15 == 2 .and. (!lTEMFACI .or. TQB->TQB_TIPOSS <> "L"))
					dbSelectArea("TQB")
					dbSkip()
					Loop
				Endif

				If TQB->TQB_TIPOSS == "B"
					If TQB->TQB_CODBEM < MV_PAR07 .Or. TQB->TQB_CODBEM > MV_PAR08
						dbSelectArea("TQB")
						dbSkip()
						Loop
					EndIf

					dbSelectArea("ST9")
					dbSetOrder(01)
					If dbSeek(xFilial("ST9") + TQB->TQB_CODBEM)
						If ST9->T9_CODFAMI < MV_PAR05 .Or. ST9->T9_CODFAMI > MV_PAR06
							dbSelectArea("TQB")
							dbSkip()
							Loop
						EndIf
					Else
						dbSelectArea("TQB")
						dbSkip()
						Loop
					EndIf
				Else

					If TQB->TQB_CODBEM < MV_PAR09 .Or. TQB->TQB_CODBEM > MV_PAR10
						dbSelectArea("TQB")
						dbSkip()
						Loop
					EndIf
				EndIf

				//Imprime solicitacao
				fImpSS(oPrint)
			EndIf
			dbSelectArea("TQB")
			dbSkip()
		End
	Else
		For i:=1 to Len(aListaSS)
			If !Empty(aListaSS[i][1])
				cFilAnt := aListaSS[i][1]
			Endif
			dbSelectArea("TQB")
			dbSetOrder(1)
			If dbSeek(xFilial("TQB") + aListaSS[i][2])
				fImpSS(oPrint)
			Endif
			cFilAnt := cOldFil
		Next i
	Endif
Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fImpSS    ºAutor  ³Roger Rodrigues     º Data ³  04/04/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Imprime a ss posicionada                                    º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³MNTR120                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fImpSS(oPrint)

	Local i 		:= 0
	Local cIndCon	:=	""	//	1=Bem e 2=Localizacao
	Local cEstrut	:=	""	// Estrutura da Localizao
	Local lFacili	:= AllTrim(SuperGetMv('MV_NG1FAC',.F.,'2')) == '1'
	Local nMaxCarac	:= 84	//	Quantidade maxima de caracteres em cada linha

	Private nImpLin	:=	0	//	Linha a ser impressa a Estrutura Completa

	oPrint:StartPage()
	lin := 9000
	SomaLinha(, .T.)

	oPrint:Say(lin + 20,1040,STR0009,oFont09B)  //"Bem / Localização"
	oPrint:Line(lin,100,Lin + 70,100)  //Linha Vertical
	oPrint:Line(lin,2300,Lin + 70,2300)
	Somalinha(70)
	oPrint:Line(lin,100,lin,2300)    //Linha Horizontal

	//Indentificacao da localizacao ou bem
	oPrint:Say(lin + 30,120,STR0010 + TQB->TQB_CODBEM,oFont09) //"Ident...: "

	If TQB->TQB_TIPOSS == "B"
		cIndCon := "1"
	Else
		cIndCon := "2"
	EndIf

	If mv_par18 == 1 //Se lista localização completa

		cEstrut := MNTR120EL( cIndCon ) //Retorna a estrutura da localizacao em forma de texto.

		If Len( cEstrut ) > nMaxCarac // Verifica se o tamanho da localizacao e maior que 80 caracteres
			If TQB->TQB_TIPOSS == "B"
				nLinhas := MlCount(AllTrim(ST9->T9_NOME),80) //Conta a qtd de linhas do campo 'T9_NOME' e verifica quando pula a linha
			Else
				nLinhas := MlCount(AllTrim(cEstrut),94 ) //Conta a qtd de linhas da localização 'TAF_NOMNIV' e verifica quando pula a linha
			EndIf
			For i := 1 To nLinhas //Percorre a quantidade de linhas.
				If NGSEEK("TQB",TQB->TQB_SOLICI,1,"TQB_TIPOSS") == "B"
					oPrint:Say(lin + 30 + nImpLin,625,MemoLine(ST9->T9_NOME,80,i),oFont09) //Imprime o nome do bem, conforme 'T9_NOME'.
				Else
					oPrint:Say(lin + 30 + nImpLin,500,MemoLine(cEstrut,94,i),oFont09) //Imprime o nome da localização, conforme 'TAF_NOMNIV'.
				EndIf
				oPrint:Line(lin,100,Lin + 70 + nImpLin,100) //Linha Vertical
				oPrint:Line(lin,2300,Lin + 70 + nImpLin,2300) //Linha Vertical
				If nLinhas > 1
					Somalinha(40) //Pula linha para não sobrescrever a descrição da localização
				EndIf
			Next i
		Else
			nImpLin := 0
			oPrint:Say(lin + 30 + nImpLin,625,cEstrut,oFont09)
		EndIf

	ElseIf TQB->TQB_TIPOSS == "B"
		cEstrut := AllTrim(NGSEEK("ST9",TQB->TQB_CODBEM,1,"T9_NOME"))
		nLinhas := MlCount(cEstrut, 80)
		For i := 1 To nLinhas
			oPrint:Say(lin + 30,650,MemoLine(cEstrut,80,i),oFont09)
			oPrint:Line(lin,100,Lin + 70 + nImpLin,100) //Linha Vertical
			oPrint:Line(lin,2300,Lin + 70 + nImpLin,2300) //Linha Vertical
			If nLinhas > 1
				Somalinha(40) //Pula linha para não sobrescrever a descrição da localização
			EndIf
		Next i
	Else
		cEstrut := AllTrim(MNTR120EL(cIndCon))
		nLinhas := MlCount(cEstrut, 95)
		For i := 1 To nLinhas
			oPrint:Say(lin + 30,650,MemoLine(cEstrut,80,i),oFont09)
			oPrint:Line(lin,100,Lin + 70 + nImpLin,100) //Linha Vertical
			oPrint:Line(lin,2300,Lin + 70 + nImpLin,2300) //Linha Vertical
			If nLinhas > 1
				Somalinha(40) //Pula linha para não sobrescrever a descrição da localização
			EndIf
		Next i
	EndIf

	//Impressao do tipo
	If TQB->TQB_TIPOSS = "B"
		oPrint:Say(lin + 95,120,STR0011,oFont09) //"Tipo.: Bem"
	Else
		oPrint:Say(lin + 95,120,STR0012,oFont09) //"Tipo.: Localização"
	EndIf

	//Impressao do Centro de Custo
	oPrint:Say(lin + 128 + nImpLin,120,STR0013 + TQB->TQB_CCUSTO,oFont09) //"C.Custo.: "
	oPrint:Say(lin + 128 + nImpLin,650,NGSEEK('CTT',TQB->TQB_CCUSTO,1,'CTT_DESC01'),oFont09)

	//Impressao do Centro de Trabalho
	oPrint:Say(lin + 158 + nImpLin,120,STR0014 + TQB->TQB_CENTRA,oFont09) //"C.Trab..: "
	oPrint:Say(lin + 158 + nImpLin,650,NGSEEK('SHB',TQB->TQB_CENTRA,1,'HB_NOME'),oFont09)

	//Impressao da Familia
	If TQB->TQB_TIPOSS = "B"
		dbSelectArea("ST9")
		dbSetOrder(01)
		If dbSeek(xFilial("ST9") + TQB->TQB_CODBEM)
			oPrint:Say(lin + 188 + nImpLin,120,STR0015 + ST9->T9_CODFAMI,oFont09) //"Familia.: "
			oPrint:Say(lin + 188 + nImpLin,650,NGSEEK("ST6",ST9->T9_CODFAMI,1,"T6_NOME"),oFont09)
		EndIf
	Else
		If lFacili
			dbSelectArea("TAF")
			dbSetOrder(6)
			If dbSeek(xFilial("TAF") + "X" + "1" + TQB->TQB_CODBEM)
				oPrint:Say(lin + 188 + nImpLin,120,STR0015 + TAF->TAF_CODFAM,oFont09) //"Familia.: "
				oPrint:Say(lin + 188 + nImpLin,650,NGSEEK("ST6",TAF->TAF_CODFAM,1,"T6_NOME"),oFont09)
			EndIf
		Else
			oPrint:Say(lin + 188 + nImpLin,120,STR0015,oFont09) //"Familia.: "
		EndIf
	EndIf

	oPrint:Line(lin + nImpLin,100,Lin + 240 + nImpLin,100) //Linha vertical
	oPrint:Line(lin + nImpLin,2300,Lin + 240 + nImpLin,2300)
	Somalinha(245)
	oPrint:Line(lin + nImpLin,100,lin + nImpLin,2300)    //Linha Horizontal


	oPrint:Say(lin + 20 + nImpLin,980,STR0016,oFont09B) //"Descrição do Serviço Solicitado"

	oPrint:Line(lin + nImpLin,100,Lin + 70 + nImpLin,100)  //Linha Vertical
	oPrint:Line(lin + nImpLin,2300,Lin + 70 + nImpLin,2300)
	Somalinha(70)
	oPrint:Line(lin + nImpLin,100,lin + nImpLin,2300)    //Linha Horizontal

	//Impressao do campo memo
	cMemo   := MSMM(TQB->TQB_CODMSS,,,,3)
	nLinhas := MlCount(cMemo,106)
	For i:= 1 To nLinhas
		oPrint:Say(lin + 20 + nImpLin,130,MemoLine(cMemo,106,i),oFont09)
		oPrint:Line(lin + nImpLin,100,Lin + 50 + nImpLin,100)
		oPrint:Line(lin + nImpLin,2300,Lin + 50 + nImpLin,2300)
		SomaLinha()
	Next i

	oPrint:Line(lin + nImpLin,100,Lin + 50 + nImpLin,100)  //Linha Vertical
	oPrint:Line(lin + nImpLin,2300,Lin + 50 + nImpLin,2300)
	SomaLinha()
	oPrint:Line(lin + nImpLin,100,lin + nImpLin,2300)    //Linha Horizontal

	If nImpLin > 0
		Somalinha(nImpLin)
	EndIf

	oPrint:Say(lin + 20,1100,STR0006,oFont09B)       //"Solicitante"
	oPrint:Line(lin,100,Lin + 70,100) //Linha Vertical
	oPrint:Line(lin,2300,Lin + 70,2300)

	Somalinha(70)

	oPrint:Line(lin,100,lin,2300)   //Linha Horizontal
	If lFacili
		fPrinSolit(TQB->TQB_CDSOLI)
	Else
		oPrint:Say(lin + 30,120,STR0007 + SubStr(TQB->TQB_USUARI,1,40),oFont09) //Solicitante //"Nome..: "
		oPrint:Say(lin + 30,1000,STR0008 + AllTrim(TQB->TQB_RAMAL),oFont09)  //"Ramal..: "
		oPrint:Line(lin,100,Lin + 90,100)  //Linha Vertical
		oPrint:Line(lin,2300,Lin + 90,2300)
		Somalinha(90)
		oPrint:Line(lin,100,lin,2300)    //Linha Horizontal
	Endif

	If lFacili
		fPrinAten(TQB->TQB_SOLICI)
		//fPrinInsum()
	Else
		oPrint:Say(lin + 20,1100,STR0034,oFont09B)       //"Executante"
		oPrint:Line(lin,100,Lin + 70,100) //Linha Vertical
		oPrint:Line(lin,2300,Lin + 70,2300)
		Somalinha(70)
		oPrint:Line(lin,100,lin,2300)   //Linha Horizontal
		oPrint:Say(lin + 30,120,STR0007 + SubStr(NGSEEK("TQ4",TQB->TQB_CDEXEC,1,"TQ4->TQ4_NMEXEC"),1,40),oFont09) //"Executante" //"Nome..: "
	Endif
	oPrint:Say(lin + 20,1000,STR0017,oFont09B)  //"Descrição da Solução da S.S"
	oPrint:Line(lin,100,Lin + 70,100)  //Linha Vertical
	oPrint:Line(lin,2300,Lin + 70,2300)
	Somalinha(70)
	oPrint:Line(lin,100,lin,2300)    //Linha Horizontal

                //Se ss ja encerrada, imprime solucao
	If	!Empty(TQB->TQB_CODMSO) .And. TQB->TQB_SOLUCA == "E"
                    //Impressao da solucao
		cMemo   := MSMM(TQB->TQB_CODMSO,,,,3)
		nLinhas := MlCount(cMemo,106)
		For i := 1 To nLinhas
			oPrint:Say(lin + 20,130,MemoLine(cMemo,106,i),oFont09)
			oPrint:Line(lin,100,lin + 50,100)
			oPrint:Line(lin,2300,lin + 50,2300)
			Somalinha()
		Next i

		oPrint:Line(lin,100,lin + 50,100)  //Linha Vertical
		oPrint:Line(lin,2300,lin + 50,2300)  //Linha Vertical
		Somalinha()
		oPrint:Line(lin,100,lin,2300)    //Linha Horizontal
	Else
		For i := 1 To 8
			oPrint:Line(lin,100,lin + 50,100)
			oPrint:Line(lin,2300,lin + 50,2300)
			oPrint:Line(lin,120,lin,2280)
			If i == 7 .And. (lin + 050) > 2810
				lin += 050
				Exit
			EndIf
			Somalinha()
		Next i

		oPrint:Line(lin,100,lin,2300)    //Linha Horizontal
	EndIf

	oPrint:EndPage()
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³SOMALINHA ³ Autor ³Elisangela Costa       ³ Data ³29/10/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Impressao do Relatorio                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Somalinha(nLinhas, lFirst)
	// Defaults
	Default lFirst := .F.

	If nLinhas == Nil
		Lin += 50
	Else
		Lin += nLinhas
	EndIf

	If lin > 2810 .Or. lFirst
		If !lFirst
			oPrint:Line(lin,100,lin,2300)
			oPrint:EndPage()
			oPrint:StartPage()
		EndIf

		lin := 100
		oPrint:Box(lin,100,560,2300)

		//------------------------------------------------------------------------------
		// Localização do Logo da Empresa
		oPrint:SayBitMap(120,120,NGLOCLOGO(),370,150)
		//------------------------------------------------------------------------------

		oPrint:Line(lin,500,300,500)  //Linha vertical
		oPrint:Say(lin + 45,520,"SIGA/MNTR120",oFont11)
		oPrint:Say(lin + 45,1040,STR0002 + TQB->TQB_SOLICI,oFont13) //"Solicitação de Serviço: "
		oPrint:Say(lin + 85,1040,STR0003 + " " + Transform(ddatabase,"99/99/9999") + " - " + Transform(Time(),"99:99"),oFont13)//"Emissão............:"
		Somalinha(200)
		oPrint:Line(lin,100,lin,2300) //Linha Hizontal

		If lFirst // Apenas imprime uma vez por S.S.
			oPrint:Say(lin + 60,120,STR0004 + Transform(Dtoc(TQB->TQB_DTABER),"99/99/9999"),oFont11) 	   //"Data Abertura S.S..: "
			oPrint:Say(lin + 60,1450,STR0005 + Transform(TQB->TQB_HOABER,"99:99"),oFont11)   //"Hora Abertura S.S..: "

			Somalinha(60)
			oPrint:Say(lin + 60,120,STR0031 + If(TQB->TQB_SOLUCA == "E",Transform(Dtoc(TQB->TQB_DTFECH),"99/99/9999"),"___/___/___"),oFont11) 	   //"Data de Encerramento: "
			oPrint:Say(lin + 60,1211,STR0032 + If(TQB->TQB_SOLUCA == "E",Transform(TQB->TQB_HOFECH,"99:99"),"___:___"),oFont11)   //"Hora de Encerramento: "
			Somalinha(60)
			oPrint:Say(lin + 60,120,STR0033 + If(TQB->TQB_SOLUCA == "E",NTOH(HTON(TQB->TQB_TEMPO)),"___:___"),oFont11) //"Tempo da S.S........: "
			Somalinha(140)
		Else
			Somalinha(020)
		EndIf
	EndIf
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG120VLO  ³ Autor ³ Elisangela Costa      ³ Data ³28/05/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida parametro de Localizacao                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MNTR120                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG120VLOC(cPARAME1,cPARAME2,nTIPO)

	Local lTEMFACI := NGINTESTORG()

	If !lTEMFACI
		Return .T.
	Else
		If nTIPO = 1
			If Empty(cPARAME1)
				Return .T.
			Else
				dbSelectArea("TAF")
				dbSetOrder(07)
				If !dbSeek(xFilial("TAF") + "X" + "2" + cPARAME1)
					MsgInfo(STR0021,STR0022) //"Codigo da localização invalido."###"ATENÇÃO"
					Return .F.
				EndIf
			EndIf
		Else
			If cPARAME2 = "ZZZ"
				IIF( AteCodigo('TAF',cPARAME1,cPARAME2,16), .T., .F.)
			Else
				If Empty(cPARAME2)
					Help(" ",1,"ATEINVALID")
					Return .f.
				Elseif cPARAME2 < cPARAME1
					Help(" ",1,"DEATEINVAL")
					Return .f.
				EndIf

				dbSelectArea("TAF")
				dbSetOrder(07)
				If !dbSeek(xFilial("TAF") + "X" + "2" + cPARAME2)
					MsgInfo(STR0021,STR0022) //"Codigo da localização invalido."###"ATENÇÃO"
					Return .F.
				EndIf
			EndIf
		EndIf
	EndIf
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MNTR120EL ³ Autor ³ Wagner S. de Lacerda  ³ Data ³27/04/2010³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Retorna a estrutura da localizacao em forma de texto       ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cIndCon -> Obrigatorio                                     ³±±
±±³          ³            Inica se a O.S. e' Bem ou Localizacao           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR120                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR120EL(cIndCon) // MNTR 120 Estrutura da Localização

	Local aLocal		:=	{}	//	Armazenamento da Localizacao completa
	Local cLocal		:=	""	//	Localizacao completa e ordenada do Pai ao Nivel
	Local cCodNiv		:=	""	//	Codigo do Nivel
	Local cNomNiv		:=	""	//	Nome do Nivel
	Local cAuxCodNiv	:=	""	//	Variavel auxiliar para armazenar o Codigo do Nivel do registro encontrado dentro do while
	Local cAuxNomNiv	:=	""	//	Variavel auxiliar para armazenar o Nome do Nivel do registro encontrado dentro do while
	Local nCont 		:= 0	//	Variavel contador para o For
	Local cNivel		:=	""	//	Nivel do Bem/Localizacao
	Local nTamTAF       := FWTamSX3( 'TAF_CODNIV' )[1]
	Local cNivel0       := IIf( nTamTAF > 3, '000000', '000' )
	Local cNivel1       := IIf( nTamTAF > 3, '000001', '001' )

	dbSelectArea("TAF")
	dbSetOrder(8)
	If cIndCon == "2" .And. dbSeek(xFilial("TAF") + TQB->TQB_CODBEM)
		cNivel := TAF->TAF_CODNIV
	Else
		cNivel := TQB->TQB_CODBEM
	EndIf

	dbSelectArea("TAF")
	dbSetOrder(7)
	If dbSeek(xFilial("TAF") + "X" + cIndCon + cNivel)	//	Usado MNT + Ind Conteudo + Nivel
		cCodNiv	:= TAF->TAF_CODNIV
		cNomNiv	:= TAF->TAF_NOMNIV

		cAuxCodNiv := cCodNiv

		//	Verifica se Listar a Localizacao Completa == Sim .E. se o Nivel da Localização <> Pai (primeiro)
		If MV_PAR18 == 1 .and. cCodNiv != cNivel1

			// Verifica se o Arquivo nao esta vazio .E. se o Codigo do Nivel <> Pai (primeiro)
			While cAuxCodNiv != cNivel0

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Descritivo da logica:                                             ³
				//³                                                                   ³
				//³ Sabendo que o nivel superior e' o codigo do nivel de um registro, ³
				//³ para encontrar o pai basta buscar pelo registro que possua o      ³
				//³ mesmo codigo (Nivel Superior == Codigo do Nivel)                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

				dbSelectArea("TAF")
				dbSetOrder(8)
				If dbSeek(xFilial("TAF") + cAuxCodNiv)	// Nivel
					cAuxNomNiv := TAF->TAF_NOMNIV
					cAuxCodNiv := TAF->TAF_NIVSUP
				Else
					cAuxNomNiv := ""
				EndIf

				If !Empty(cAuxNomNiv) .and. cAuxCodNiv <> cCodNiv
					aAdd(aLocal, AllTrim(cAuxNomNiv))
				EndIf

				dbSkip()
			End

			nCont := Len(aLocal)

			// Ordena a estrutura ate o ultimo registro antes da localizacao final
			While nCont > 1
				cLocal += aLocal[nCont] + " > "
				nCont--
			EndDo
		EndIf
	EndIf

	If cIndCon == "1"

		cNomNiv := NGSEEK('ST9',TQB->TQB_CODBEM,1,'T9_NOME')

	Else

		cNomNiv := NGSEEK( 'TAF', 'X2' + SubStr( cNivel, 1, nTamTAF ), 7, 'TAF_NOMNIV' )

	EndIf

	cLocal += AllTrim(cNomNiv)	// Seta ou adiciona a localizacao final

Return cLocal

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fPrinAten ³ Autor ³ Jackson Machado       ³ Data ³26/04/2012³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Imprime os atendentes da Solicitação                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cSolicitacao -> Obrigatorio                                ³±±
±±³          ³            Código da S.S. que está sendo impressa          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR120                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fPrinAten(cSolicitacao)

	Local nTamCli := TAMSX3("A2_COD")[1]
	Local nHrTot  := 0
	Local lCanImp := .F.
	Local aOfusc  := {}
	Local lT1Nome := .F.
	Local lA2Nome := .F.

	If lLgpd
		// [LGPD] Caso o usuário não possua acesso ao(s) campo(s), deve-se ofuscá-lo(s)
		aOfusc  := FwProtectedDataUtil():UsrAccessPDField( __CUSERID, { 'T1_NOME', 'A2_NOME' } )
		lT1Nome := Ascan( aOfusc, { |x|  AllTrim(x) == 'T1_NOME' } ) == 0
		lA2Nome := Ascan( aOfusc, { |x|  AllTrim(x) == 'A2_NOME' } ) == 0
	EndIf

	// Verifica se há Atendentes ou Terceiros para imprimir
	dbSelectArea("TUR")
	dbSetOrder(1)
	lCanImp := ( dbSeek(xFilial("TUR") + cSolicitacao + "2") .Or. dbSeek(xFilial("TUR") + cSolicitacao + "3") )

	//----------------------------------------
	// Imprime Atendentes e Terceiros
	//----------------------------------------
	dbSelectArea("TUR")
	dbSetOrder(1)
	If dbSeek(xFilial("TUR") + cSolicitacao) .And. lCanImp
		oPrint:Say(lin + 20,1100,STR0037,oFont09B)       //"Atendente(s)"
		oPrint:Line(lin,100,Lin + 140,100) //Linha Vertical
		oPrint:Line(lin,2300,Lin + 140,2300)

		Somalinha(70)

		oPrint:Line(lin,100,lin,2300)   //Linha Horizontal

		//Cabeçalho
		oPrint:Say(lin + 30,120,STR0039,oFont09) //"Filial"
		oPrint:Say(lin + 30,260,STR0042,oFont09) //"Recurso"
		oPrint:Say(lin + 30,490,STR0038,oFont09) //"Codigo"
		oPrint:Say(lin + 30,720,STR0041,oFont09) //"Loja"
		oPrint:Say(lin + 30,860,STR0007,oFont09) //"Nome"
		oPrint:Say(lin + 30,1560,STR0040,oFont09) //"Hr.Reliz."

		Somalinha(70)

		oPrint:Line(lin,100,lin,2300)    //Linha Horizontal

		While !Eof() .AND. TUR->TUR_FILIAL + TUR->TUR_SOLICI == xFilial("TUR") + cSolicitacao
			lEntra := .F.

			If TUR->TUR_TIPO $ "2/3"
				//Corpo
				oPrint:Say(lin + 30,120,TUR->TUR_FILATE,oFont09)
				oPrint:Say(lin + 30,260,If(TUR->TUR_TIPO == "2",STR0043,STR0044),oFont09)//"Funcionário"###"Terceiro"
				oPrint:Say(lin + 30,490,TUR->TUR_CODATE,oFont09)
				oPrint:Say(lin + 30,720,TUR->TUR_LOJATE,oFont09)
				If TUR->TUR_TIPO == "2"

					If lT1Nome

						oPrint:Say(lin + 30, 860, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize( ST1->T1_NOME ), 1, 40), oFont09)

					Else

						oPrint:Say(lin + 30,860,SubStr(NGSEEK("ST1",TUR->TUR_CODATE,1,"ST1->T1_NOME",TUR->TUR_FILATE),1,40),oFont09)

					EndIf

					oPrint:Say(lin + 30,1560,NTOH(HTON(TUR->TUR_HRREAL)),oFont09)
					nHrTot += HTON(TUR->TUR_HRREAL)
				ElseIf TUR->TUR_TIPO == "3"

					If lA2Nome

						oPrint:Say(lin + 30,860,SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize( SA2->A2_NOME ), 1, 40), oFont09)

					Else

						oPrint:Say(lin + 30,860,SubStr(NGSEEK("SA2",SubStr(TUR->TUR_CODATE,1,nTamCli) + TUR_LOJATE,1,"SA2->A2_NOME",TUR->TUR_FILATE),1,40),oFont09)

					EndIf

				Endif
				oPrint:Line(lin,100,Lin + 220,100)  //Linha Vertical
				oPrint:Line(lin,2300,Lin + 220,2300)
				Somalinha(70)
			Endif
			dbSelectArea("TUR")
			dbSkip()
		EndDo

		Somalinha(60)

		oPrint:Say(lin + 30,120,STR0045,oFont09)//"Total de Horas dos Funcionários:"
		oPrint:Say(lin + 30,1560,cValToChar(NTOH(nHrTot)),oFont09)

		Somalinha(90)

		oPrint:Line(lin,100,lin,2300)    //Linha Horizontal
	Endif

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³fPrinSolit³ Autor ³ Jackson Machado       ³ Data ³26/04/2012³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Imprime o solicitante da Solicitação                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cSolicitante -> Obrigatorio                                ³±±
±±³          ³            Código do solicitante da S.S.                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR120                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fPrinSolit(cSolicitante)

	Local oldInc  := IIf(Type("INCLUI") == "L", Inclui, Nil)
	Local aOfusc  := {}
	Local lNomUsr := .F.
	Local lEmail  := .F.
	Local lTel1   := .F.
	Local lTel2   := .F.

	If lLgpd
		// [LGPD] Caso o usuário não possua acesso ao(s) campo(s), deve-se ofuscá-lo(s)
		aOfusc  := FwProtectedDataUtil():UsrAccessPDField( __CUSERID, { 'TUF_NOMUSR', 'TUF_EMAUSR', 'TUF_TE1USR', 'TUF_TE2USR' } )
		lNomUsr := Ascan( aOfusc, { |x|  AllTrim(x) == 'TUF_NOMUSR' } ) == 0
		lEmail  := Ascan( aOfusc, { |x|  AllTrim(x) == 'TUF_EMAUSR' } ) == 0
		lTel1   := Ascan( aOfusc, { |x|  AllTrim(x) == 'TUF_TE1USR' } ) == 0
		lTel2   := Ascan( aOfusc, { |x|  AllTrim(x) == 'TUF_TE2USR' } ) == 0
	EndIf

	Inclui := .F.

	dbSelectArea("TUF")
	dbSetOrder(1)
	dbSeek(xFilial("TUF") + cSolicitante)

	If lNomUsr
		oPrint:Say(lin + 30,120,STR0007 + FwProtectedDataUtil():ValueAsteriskToAnonymize( AllTrim(MNT909REL("TUF_NOMUSR",.F.,cSolicitante) )), oFont09)
	Else
		oPrint:Say(lin + 30,120,STR0007 + AllTrim(MNT909REL("TUF_NOMUSR",.F.,cSolicitante)), oFont09) //"Nome...: "
	EndIf

	oPrint:Say(lin + 30,1000,"Depart" + ".: " + AllTrim(MNT909REL("TUF_DEPUSR",.F.,cSolicitante)), oFont09) //"Depart.: "

	Somalinha(90)

	oPrint:Say(lin + 30,120,STR0008 + AllTrim(MNT909REL("TUF_RAMUSR",.F.,cSolicitante)), oFont09)  //"Ramal...: "

	If lEmail
		oPrint:Say(lin + 30,1000,"Email" + "..: " + FwProtectedDataUtil():ValueAsteriskToAnonymize( AllTrim(MNT909REL("TUF_EMAUSR",.F.,cSolicitante) )), oFont09) //"Email..: "
	Else
		oPrint:Say(lin + 30,1000,"Email" + "..: " + AllTrim(MNT909REL("TUF_EMAUSR",.F.,cSolicitante)), oFont09) //"Email..: "
	Endif

	Somalinha(90)

	If lTel1
		oPrint:Say(lin + 30,120,"Tel.01" + ".: " + FwProtectedDataUtil():ValueAsteriskToAnonymize( AllTrim(MNT909REL("TUF_TE1USR",.F.,cSolicitante) )), oFont09) //"Tel.01.: "
	Else
		oPrint:Say(lin + 30,120,"Tel.01" + ".: " + AllTrim(MNT909REL("TUF_TE1USR",.F.,cSolicitante)), oFont09) //"Tel.01.: "
	EndIf

	If lTel2
		oPrint:Say(lin + 30,1000,"Tel.02" + ".: " + FwProtectedDataUtil():ValueAsteriskToAnonymize( AllTrim(MNT909REL("TUF_TE2USR",.F.,cSolicitante) )), oFont09)  //"Tel.02.: "
	Else
		oPrint:Say(lin + 30,1000,"Tel.02" + ".: " + AllTrim(MNT909REL("TUF_TE2USR",.F.,cSolicitante)), oFont09)  //"Tel.02.: "
	EndIf

	oPrint:Line(lin-180,100,Lin + 90,100)  //Linha Vertical
	oPrint:Line(lin-180,2300,Lin + 90,2300)

	Somalinha(90)
	oPrint:Line(lin,100,lin,2300)    //Linha Horizontal

	INCLUI := oldInc

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR120VDT
Função para fazer as validações de data no campo "Até Data".

@author Diego de Oliveira
@since  17/08/2016
@version MP12
@Return Nil
/*/
//---------------------------------------------------------------------
Function MNTR120VDT()

Local lRet := .T.

If Empty(MV_PAR14)
	MsgInfo(STR0046) //"Campo 'Até Data' não pode ser vazio"
	lRet := .F.
ElseIf MV_PAR14 < MV_PAR13
	MsgInfo(STR0047) //"O campo 'Até Data' não pode ser menor que o campo 'De Data'"
	lRet := .F.
EndIf

Return lRet
