#INCLUDE "MNTR565.ch"
#INCLUDE "PROTHEUS.CH"

/*/


Ŀ
Funo    MNTR565    Autor  Marcos Wagner Junior   Data  21/09/06 
Ĵ
Descrio Relatorio de Situacao da Inspecao apos apos algum acerto    
Ĵ
 Uso      Generico                                                    
ٱ


*/
Function MNTR565()

	WNREL      := "MNTR565"
	LIMITE     := 132
	cDESC1     := STR0001 //"O relatrio apresentar a situao dos "
	cDESC2     := STR0002 //"veculos inspecionados "
	cDESC3     := ""
	cSTRING    := "SCP"

	Private NOMEPROG := "MNTR565"
	Private TAMANHO  := "M"
	Private aRETURN  := {STR0003,1,STR0004,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private TITULO   := STR0005 //"Situacao de Inspecao de Pneus"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2
	Private aVETINR := {}
	Private cPERG := "MNR565"
	Private aPerg :={}

	SetKey( VK_F9, { | | NGVersao( "MNTR565" , 1 ) } )

	Pergunte(cPERG,.F.)
	//Ŀ
	// Envia controle para a funcao SETPRINT                        
	//
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("TR9")
		Return
	EndIf
	SetDefault(aReturn,cSTRING)
	RptStatus({|lEND| MNTR565IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0009,STR0010) //"Aguarde..."###"Processando Registros..."
	Dbselectarea("TR9")

Return .T.

/*/


Ŀ
Funo    |MNR565IMP | Autor  Marcos Wagner Junior   Data 14/09/2006
Ĵ
Descrio Chamada do Relatrio                                        
Ĵ
 Uso      MNTR565                                                     
ٱ


/*/
Function MNTR565IMP(lEND,WNREL,TITULO,TAMANHO)
	Private cRODATXT := ""
	Private nCNTIMPR := 0
	Private li := 80 ,m_pag := 1
	Private cNomeOri

	nTIPO  := IIf(aReturn[4]==1,15,18)

	CABEC1 := STR0018 //"Veiculo           Descricao                                 O.S.Insp  Status do Bem              Qtde Pneus  Status Inspecao"

	CABEC2 := ""

	/*
	1         2         3         4         5         6         7         8         9         0         1         2
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456
	*******************************************************************************************************************************
	Relatorio de Pneus
	*******************************************************************************************************************************

	*******************************************************************************************************************************
	Filial: xx - xxxxxxxxxxxxxxx

	Veiculo           Descricao                                 O.S.Insp  Status do Bem              Qtde Pneus  Status Inspecao
	xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  xxxxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxx 99          xxxxxxxxxx

	*/
	MNR565NIN()

	cAliasQry := GetNextAlias()
	cQuery := " SELECT TR9.TR9_FILIAL, ST9.T9_CODBEM, ST9.T9_NOME, ST9.T9_SITBEM, ST9.T9_STATUS,"
	cQuery += " TR9.TR9_QTDATU, TR9.TR9_STATUS,TR9.TR9_ORDEM"
	cQuery += " FROM " + RetSqlName("ST9")+" ST9," + RetSqlName("TR9")+" TR9," + RetSqlName("STJ")+" STJ"
	cQuery += " WHERE TR9.TR9_FILIAL  >= '"+MV_PAR01+"' AND TR9.TR9_FILIAL <= '" +MV_PAR02+"'
	cQuery += " AND   TR9.TR9_DTINSP >=  '"+DTOS(MV_PAR03)+"'"
	cQuery += " AND " + NGMODCOMP("STJ","TR9","=") + " "
	cQuery += " AND " + NGMODCOMP("STJ","ST9","=") + " "
	cQuery += " AND STJ.TJ_CODBEM = ST9.T9_CODBEM AND ST9.T9_SITBEM = 'A' "
	cQuery += " AND STJ.TJ_ORDEM  = TR9.TR9_ORDEM "
	cQuery += " AND STJ.TJ_PLANO  = TR9.TR9_PLANO "
	cQuery += " AND STJ.D_E_L_E_T_ <> '*' "
	cQuery += " AND ST9.D_E_L_E_T_ <> '*' "
	cQuery += " AND TR9.D_E_L_E_T_ <> '*' "
	cQuery += " AND TR9.TR9_CICLO = "
	cQuery += " (SELECT MAX(TR.TR9_CICLO) FROM " + RetSqlName("TR9")+" TR," + RetSqlName("STJ")+" TJ "
	cQuery += " WHERE TR9.TR9_FILIAL=TR.TR9_FILIAL "
	cQuery += " AND TR9.TR9_ORDEM = TR.TR9_ORDEM AND TR9.TR9_PLANO=TR.TR9_PLANO AND "
	cQuery += "  TR.TR9_FILIAL = TJ.TJ_FILIAL AND TR.TR9_ORDEM = TJ.TJ_ORDEM AND "
	cQuery += "  TR.TR9_PLANO=TJ.TJ_PLANO AND TR.D_E_L_E_T_<>'*' AND TJ.D_E_L_E_T_<>'*') "
	cQuery += " ORDER BY ST9.T9_FILIAL, ST9.T9_CODBEM"

	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

	SetRegua(LastRec())

	DbGoTop()
	While !Eof()

		IncRegua()
		If lEnd
			@ Prow()+1,001 PSay "CANCELADO PELO OPERADOR"
			Exit
		EndIf

		If	cNomeOri <> (cAliasQry)->TR9_FILIAL
			DbSelectArea("SM0")
			SM0->(DbSetOrder(1))
			If MsSeek(SM0->M0_CODIGO+(cAliasQry)->TR9_FILIAL)
				NgSomaLi(58)
				@ Li,000   Psay (cAliasQry)->TR9_FILIAL + " - " + SM0->M0_FILIAL
				NgSomaLi(58)
				NgSomaLi(58)
			Endif
		EndIf

		DbSelectArea(cAliasQry)
		cNomeOri := (cAliasQry)->TR9_FILIAL

		@ Li,000 PSay (cAliasQry)->T9_CODBEM
		@ Li,018	PSay SubStr((cAliasQry)->T9_NOME,1,40)

		@ Li,060	PSay (cAliasQry)->TR9_ORDEM

		DbSelectArea("TQY")
		DbSetOrder(01)
		If MsSeek(xFilial("TQY")+(cAliasQry)->T9_STATUS)
			@ Li,070	PSay SubStr(TQY->TQY_DESTAT,1,25)
		Endif

		@ Li,097	PSay (cAliasQry)->TR9_QTDATU

		If (cAliasQry)->TR9_STATUS = '1'
			@ Li,109 PSay STR0014 //"Iniciado"
		ElseIf (cAliasQry)->TR9_STATUS = '2'
			@ Li,109	PSay STR0015 //"Inconsistente"
		ElseIf (cAliasQry)->TR9_STATUS = '3'
			@ Li,109	PSay STR0016 //"Pendente de Atualizao"
		ElseIf (cAliasQry)->TR9_STATUS = '4'
			@ Li,109	PSay STR0017 //"Finalizado"
		Endif

		NgSomaLi(58)

		DbSelectArea(cAliasQry)
		DbSkip()
	End

	(cALIASQRY)->(dbCloseArea())

	RODA(nCNTIMPR,cRODATXT,TAMANHO)

	//Ŀ
	// Devolve a condicao original do arquivo principal             
	//
	RetIndex('TR9')
	Set Filter To
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()


Return Nil
/*/


Ŀ
Funo    |MNR565NIN | Autor  Evaldo Cevinscki Jr.   Data 20/11/2007
Ĵ
Descrio Imprime Inspecoes ainda nao iniciadas                       
Ĵ
 Uso      MNTR565                                                     
ٱ


/*/
Function MNR565NIN(cFil)

	Local cNGSINSP	:= AllTrim(GetMv("MV_NGSINSP"))

	cAliasQry2 := GetNextAlias()
	cQuery := " Select STJ.TJ_FILIAL, STJ.TJ_CODBEM,STJ.TJ_ORDEM,ST9.T9_NOME,ST9.T9_STATUS,ST9.T9_SITBEM, "
	cQuery += " (Select Count(*) From "+ RetSqlName("STC")+" STC Where STC.TC_CODBEM=STJ.TJ_CODBEM AND STC.D_E_L_E_T_<>'*')QTDPN
	cQuery += " From "+ RetSqlName("STJ")+" STJ,"+ RetSqlName("ST9")+" ST9"
	cQuery += " Where TJ_SERVICO = '"+cNGSINSP+"' AND STJ.D_E_L_E_T_<>'*' "
	cQuery += " AND STJ.TJ_FILIAL BETWEEN '"+MV_PAR01+"' AND '" +MV_PAR02+"' AND TJ_ORDEM NOT IN"
	cQuery += "(SELECT TR9_ORDEM FROM " + RetSqlName("TR9")+" TR9 WHERE TR9.TR9_FILIAL=STJ.TJ_FILIAL "
	cQuery += " AND TR9.D_E_L_E_T_<>'*') AND STJ.TJ_FILIAL= ST9.T9_FILIAL AND STJ.TJ_CODBEM=ST9.T9_CODBEM AND ST9.D_E_L_E_T_<>'*'"
	cQuery += " AND STJ.TJ_TERMINO ='N' AND STJ.TJ_SITUACA ='L' "
	cQuery += " ORDER BY STJ.TJ_FILIAL, STJ.TJ_CODBEM,STJ.TJ_ORDEM"

	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry2, .F., .T.)

	SetRegua(LastRec())

	DbGoTop()
	While !Eof()

		IncRegua()

		If	cNomeOri <> (cAliasQry2)->TJ_FILIAL
			DbSelectArea("SM0")
			SM0->(DbSetOrder(1))
			If MsSeek(SM0->M0_CODIGO+(cAliasQry2)->TJ_FILIAL)
				NgSomaLi(58)
				@ Li,000   Psay (cAliasQry2)->TJ_FILIAL + " - " + SM0->M0_FILIAL
				NgSomaLi(58)
				NgSomaLi(58)
			Endif
		EndIf

		DbSelectArea(cAliasQry2)
		cNomeOri := (cAliasQry2)->TJ_FILIAL

		@ Li,000	PSay (cAliasQry2)->TJ_CODBEM
		@ Li,018	PSay SubStr((cAliasQry2)->T9_NOME,1,40)
		@ Li,060	PSay (cAliasQry2)->TJ_ORDEM

		DbSelectArea("TQY")
		DbSetOrder(01)
		If MsSeek(xFilial("TQY")+(cAliasQry2)->T9_STATUS)
			@ Li,070		 PSay SubStr(TQY->TQY_DESTAT,1,25)
		Endif

		@ Li,097		 PSay (cAliasQry2)->QTDPN
		@ Li,109		 PSay STR0019 //"No Iniciado"

		NgSomaLi(58)
		DbSelectArea(cAliasQry2)
		DbSkip()
	End

	(cALIASQRY2)->(dbCloseArea())

Return .t.
