#INCLUDE "Mntc790.ch" 
#INCLUDE "PROTHEUS.CH"  

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTC790
Consulta de Ordens de Servico.
@type function

@author Elisangela Costa
@since  03/11/2005

@return boolean, true.
/*/
//-------------------------------------------------------------------
 Function MNTC790()

	Local aCores := {}
	
	Private aRotina   := MenuDef()
	Private cCadastro := OemtoAnsi( STR0001 ) //"Ordem de Servico"

	dbSelectArea( 'STJ' )
	dbSetOrder( 2 )

	If ExistBlock( 'MNTC7902' )

		aCores := ExecBlock( 'MNTC7902', .F., .F. )

	EndIf
	
	mBrowse( 6, 1, 22, 75, 'STJ', , , , , , aCores )

	dbSelectArea( 'STJ' )
	dbSetOrder( 1 )

 Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Menu da rotina MNTC790.
@type function

@author Ricardo Dal Ponte
@since  29/11/2006

@return array, Array contendo as opções do Menu.
/*/
//-------------------------------------------------------------------
 Static Function MenuDef()

	Local aRotina := {{ STR0002, 'AXPESQUI'  , 0, 1 },;    // "Pesquisar"
					  { STR0003, 'NGCAD01'   , 0, 2 },;    // "Visual."
					  { STR0004, 'MNTCOSDE'  , 0, 3, 0 },; // "Detalhes"
					  { STR0005, 'MNTCOCOR'  , 0, 4, 0 },; // "Ocorren."
					  { STR0006, 'MNTC550A'  , 0, 4, 0 },; // "Problemas"  
					  { STR0007, 'NGATRASOS' , 0, 4, 0 },; // "Motivo Atraso"
					  { STR0008, 'MNTC550B'  , 0, 4 } }    // "Etapas"               
							
	If AllTrim( SuperGetMV( 'MV_NGMNTES' ) ) == 'S'
	
		aAdd( aRotina, { STR0009,'MNTC290(STJ->TJ_ORDEM)', 0, 7 } )  // "Sol. Compra"
	
	EndIf
	
	lPyme := Iif( Type( '__lPyme' ) <> 'U', __lPyme, .F. )
	
	If !lPyme
	
		aAdd( aRotina, { STR0010, 'MsDocument', 0, 4 } )  // "Conhecimento"    
	
	EndIf

	If ExistBlock( 'MNTC7901' )
		
		aRotina := ExecBlock( 'MNTC7901', .F., .F., { aRotina } )
	
	EndIf

 Return ( aRotina )

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTC790Vld
Valid. do grupo de perguntas MNC790.
@type function

@author Alexandre Santos
@since  29/11/2023

@param 	cParam , string, Parâmetro que será validado.
@return boolean, Indica se o parâmetro foi validado.
/*/
//-------------------------------------------------------------------
Function MNTC790Vld( cParam )

	Local lRet := .T.

	Do Case

		Case cParam == '01' // De Filial

			If !Empty( MV_PAR01 )

                lRet := ExistCPO( 'SM0', SM0->M0_CODIGO + MV_PAR01 )

            EndIf

            If lRet .And. !Empty( MV_PAR02 ) .And.;
                !( MV_PAR02 == Replicate( 'Z', FwSizeFilial() ) )

                lRet := AteCodigo( 'SM0', SM0->M0_CODIGO + MV_PAR01, SM0->M0_CODIGO + MV_PAR02, 10 )

            EndIf

		Case cParam == '02' // Até Filial

			If !( MV_PAR02 == Replicate( 'Z', FwSizeFilial() ) )
			
				lRet := AteCodigo( 'SM0', SM0->M0_CODIGO + MV_PAR01, SM0->M0_CODIGO + MV_PAR02, 10 )
				
			EndIf

		Case cParam == '03' // De Bem

			If !Empty( MV_PAR03 )

                lRet := ExistCPO( 'ST9', MV_PAR03, 1 )

            EndIf

            If lRet .And. !Empty( MV_PAR04 ) .And.;
                !( MV_PAR04 == Replicate( 'Z', FWTamSX3( 'T9_CODBEM' )[1] ) )

                lRet := AteCodigo( 'ST9', MV_PAR03, MV_PAR04, 10 )

            EndIf

		Case cParam == '04' // Até Bem

			If !( MV_PAR04 == Replicate( 'Z', FWTamSX3( 'T9_CODBEM' )[1] ) )
			
				lRet := AteCodigo( 'ST9', MV_PAR03, MV_PAR04, 10 )
				
			EndIf

		Case cParam == '05' // De Família

			If !Empty( MV_PAR05 )

                lRet := ExistCPO( 'ST6', MV_PAR05, 1 )

            EndIf

            If lRet .And. !Empty( MV_PAR06 ) .And.;
                !( MV_PAR06 == Replicate( 'Z', FWTamSX3( 'T6_CODFAMI' )[1] ) )

                lRet := AteCodigo( 'ST6', MV_PAR05, MV_PAR06, 10 )

            EndIf

		Case cParam == '06' // Até Família

			If !( MV_PAR06 == Replicate( 'Z', FWTamSX3( 'T6_CODFAMI' )[1] ) )
			
				lRet := AteCodigo( 'ST6', MV_PAR05, MV_PAR06, 10 )
				
			EndIf

		Case cParam == '07' // De Modelo

			If !Empty( MV_PAR07 )

                lRet := ExistCPO( 'TQR', MV_PAR07, 1 )

            EndIf

            If lRet .And. !Empty( MV_PAR08 ) .And.;
                !( MV_PAR08 == Replicate( 'Z', FWTamSX3( 'TQR_TIPMOD' )[1] ) )

                lRet := AteCodigo( 'TQR', MV_PAR07, MV_PAR08, 10 )

            EndIf

		Case cParam == '08' // Até Modelo

			If !( MV_PAR08 == Replicate( 'Z', FWTamSX3( 'TQR_TIPMOD' )[1] ) )
			
				lRet := AteCodigo( 'TQR', MV_PAR07, MV_PAR08, 10 )
				
			EndIf

		Case cParam == '09' // De Centro de Custo

			If !Empty( MV_PAR09 )

                lRet := ExistCPO( 'CTT', MV_PAR09, 1 )

            EndIf

            If lRet .And. !Empty( MV_PAR10 ) .And.;
                !( MV_PAR10 == Replicate( 'Z', FWTamSX3( 'CTT_CUSTO' )[1] ) )

                lRet := AteCodigo( 'CTT', MV_PAR09, MV_PAR10, 10 )

            EndIf

		Case cParam == '10' // Até Centro de Custo

			If !( MV_PAR10 == Replicate( 'Z', FWTamSX3( 'CTT_CUSTO' )[1] ) )
			
				lRet := AteCodigo( 'CTT', MV_PAR09, MV_PAR10, 10 )
				
			EndIf

		Case cParam == '11' // De Centro de Trabaho

			If !Empty( MV_PAR11 )

                lRet := ExistCPO( 'SHB', MV_PAR11, 1 )

            EndIf

            If lRet .And. !Empty( MV_PAR12 ) .And.;
                !( MV_PAR12 == Replicate( 'Z', FWTamSX3( 'HB_COD' )[1] ) )

                lRet := AteCodigo( 'SHB', MV_PAR11, MV_PAR12, 10 )

            EndIf

		Case cParam == '12' // Até Centro de Trabalho

			If !( MV_PAR12 == Replicate( 'Z', FWTamSX3( 'HB_COD' )[1] ) )
			
				lRet := AteCodigo( 'SHB', MV_PAR11, MV_PAR12, 10 )
				
			EndIf

		Case cParam == '13' // De Ordem de Serviço

			If !Empty( MV_PAR13 )

                lRet := ExistCPO( 'STJ', MV_PAR13, 1 )

            EndIf

            If lRet .And. !Empty( MV_PAR14 ) .And.;
                !( MV_PAR14 == Replicate( 'Z', FWTamSX3( 'TJ_ORDEM' )[1] ) )

                lRet := AteCodigo( 'STJ', MV_PAR13, MV_PAR14, 10 )

            EndIf

		Case cParam == '14' // Até Ordem de Serviço

			If !( MV_PAR14 == Replicate( 'Z', FWTamSX3( 'TJ_ORDEM' )[1] ) )
			
				lRet := AteCodigo( 'STJ', MV_PAR13, MV_PAR14, 10 )
				
			EndIf

		Case cParam == '15' // De Serviço

			If !Empty( MV_PAR15 )

                lRet := ExistCPO( 'ST4', MV_PAR15, 1 )

            EndIf

            If lRet .And. !Empty( MV_PAR16 ) .And.;
                !( MV_PAR16 == Replicate( 'Z', FWTamSX3( 'T4_SERVICO' )[1] ) )

                lRet := AteCodigo( 'ST4', MV_PAR15, MV_PAR16, 10 )

            EndIf

		Case cParam == '16' // Até Serviço

			If !( MV_PAR16 == Replicate( 'Z', FWTamSX3( 'T4_SERVICO' )[1] ) )
			
				lRet := AteCodigo( 'ST4', MV_PAR15, MV_PAR16, 10 )
				
			EndIf

		Case cParam == '17' // De Data

			lRet := NaoVazio( MV_PAR17 )

            If lRet .And. !Empty( MV_PAR18 ) .And.;
                MV_PAR17 > MV_PAR18

                Help( '', 1, 'DATAINVALI' )

                lRet := .F.

            EndIf

		Case cParam == '18' // Até Data

			If MV_PAR17 > MV_PAR18

                Help( '', 1, 'DATAINVALI' )

                lRet := .F.

            EndIf

		Case cParam == '19' // Tipo de O.S.

			lRet := NaoVazio()

	End Case
	
Return lRet
