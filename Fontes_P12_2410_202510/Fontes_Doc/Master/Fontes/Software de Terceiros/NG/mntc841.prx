#include "Protheus.ch"
#include "MNTC841.CH"

/*


Ŀ
Funo    MNTC841    Autor Inacio Luiz Kolling     Data 19/01/2006
Ĵ
Descrio Consulta/relatorio de abastecimento ja conciliados/a conci- 
          lar/Ambos                                                   
Ĵ
 Uso      MNTC841                                                     
ٱ


*/
Function MNTC841()

	Local aArea := GetArea(),cMESAN := Space(1)
	Local aInd := {}
	Local oTmpTRB
	// [LGPD] Se as funcionalidades, referentes  LGPD, podem ser utilizadas
	Local lLgpd := FindFunction( 'FWPDCanUse' ) .And. FwPdCanUse( .T. )
	Local aOfusc := {}
	Local lCnpj := .F.
	Local lRazaoSo := .F.

	If !FindFunction( 'MNTAmIIn' ) .Or. MNTAmIIn( 95 )

		Private nSizeFil  := If(FindFunction("FWSizeFilial"),FwSizeFilial(),Len(TQN->TQN_FILIAL))
		Private cCadastro := OemtoAnsi(STR0004)
		Private cPerg     := PadR( "MNC841", Len(Posicione("SX1", 1, "MNC841", "X1_GRUPO")) )
		Private cTRBB := GetNextAlias()

		If lLgpd
			// [LGPD] Caso o usurio no possua acesso ao(s) campo(s), deve-se ofusc-lo(s)
			aOfusc := FwProtectedDataUtil():UsrAccessPDField( __CUSERID, { 'TQF_CNPJ', 'A2_NOME'} )

			lCnpj := Ascan( aOfusc, { |x| AllTrim(x) == 'TQF_CNPJ' } ) == 0
			lRazaoSo := Ascan( aOfusc, { |x| AllTrim(x) == 'A2_NOME' } ) == 0

		EndIf

		aDBFB := {}

		Aadd(aDBFB,{"FILIAL"  ,"C", nSizeFil,0})
		Aadd(aDBFB,{"NUMABST" ,"C", 15,0})
		Aadd(aDBFB,{"NFABAST" ,"C", 06,0})
		Aadd(aDBFB,{"VLRABAS" ,"N", TAMSX3("TQN_VALTOT")[1], TAMSX3("TQN_VALTOT")[2]})
		Aadd(aDBFB,{"DTABAST" ,"D", 08,0})
		Aadd(aDBFB,{"ESCALAA" ,"C", 08,0})
		Aadd(aDBFB,{"FROTAAB" ,"C", 16,0})
		Aadd(aDBFB,{"DTVENCA" ,"D", 08,0})
		Aadd(aDBFB,{"DTCONC"  ,"D", 08,0})
		Aadd(aDBFB,{"CNPJABA" ,"C", 14,0})
		Aadd(aDBFB,{"NOMFANA" ,"C", 30,0})
		Aadd(aDBFB,{"HORAABS" ,"C", 05,0})
		Aadd(aDBFB,{"POSTOAB" ,"C", TAMSX3("TQF_CODIGO")[1],0,})
		Aadd(aDBFB,{"LOJAABA" ,"C", TAMSX3("TQF_LOJA")[1],0,})
		Aadd(aDBFB,{"ABASTE"  ,"C", 10,0})
		Aadd(aDBFB,{"CCUSTO"  ,"C", TamSX3("CTT_CUSTO")[1],0})

		// Definicao dos indice(s) temporario(s)
		aInd := {{"FILIAL","POSTOAB","LOJAABA","NFABAST","DTABAST","HORAABS"},{"POSTOAB","LOJAABA","NFABAST","DTABAST","HORAABS"},{"DTABAST","POSTOAB"}}

		oTmpTRB := FWTemporaryTable():New(cTRBB, aDBFB)
		oTmpTRB:AddIndex("Ind01", aInd[1])
		oTmpTRB:AddIndex("Ind02", aInd[2])
		oTmpTRB:AddIndex("Ind03", aInd[3])
		oTmpTRB:Create()

		If Pergunte("MNC841",.T.)
			Processa({ |lEnd| MNTC841PRO()},STR0017)

			cCLASSI := If(mv_par09 = 1,STR0011,If(mv_par09 = 2,STR0012,STR0011+"/"+STR0012))

			DbSelectarea(cTRBB)
			DbGotop()
			If Reccount() > 0
				Define DIALOG oDlg1 FROM 7,10 TO 35,110 TITLE STR0004+" "+cCLASSI
				@ 04,020 Button STR0001 Of oDlg1 Size 40,09 Pixel Action (MNTC841PE())
				@ 04,065 Button STR0002 Of oDlg1 Size 40,09 Pixel Action (MNTC841VI())
				@ 04,110 Button STR0003 Of oDlg1 Size 40,09 Pixel Action (MNTC841RE())

				@ 1,0.3 ListBox oList Fields FILIAL,; //listbox
				NUMABST,;
				NFABAST,;
				PADL(Transform(VLRABAS,"@E 999,999,999,999.999"),18),;
				DTABAST,;
				ESCALAA,;
				FROTAAB,;
				POSTOAB,;
				DTVENCA,;
				If(!Empty(CNPJABA),PADL(Transform(CNPJABA,X3Picture('TQF_CNPJ') ),18),CNPJABA),;
				NOMFANA,;
				DTCONC,;
				If(ABASTE = "1",STR0043,STR0042),; //"Automtico"###"Manual"
				CCUSTO;
				FieldSizes 20,50,30,40,35,40,35,45,40,35,35,40,45,70 ;
				Size 388,180;
				HEADERS STR0045,; //"Filial"
				STR0018,;
				STR0019,;
				STR0020,;
				STR0021,;
				STR0023,;
				STR0024,;
				STR0031,; //"Posto"
				STR0025,;
				STR0026,;
				STR0046,; //"Razo Social"
				STR0047,; //"Dt.Conciliao"
				STR0048,; //"Abastecimento"
				STR0049 //"Centro Custo"

				// oList:aObfuscatedCols  um array para setar quais campos devem ser ofuscados conforme a ListBox
				If lLgpd
					// Inicialmente aObfuscatedCols apresenta valor nil para o array, portanto, precisa indicar os valores
					oList:aObfuscatedCols := {.F., .F., .F., .F., .F., .F., .F., .F., .F., lCnpj, lRazaoSo}
				EndIf

				ACTIVATE DIALOG oDlg1
			Else
				MsgInfo(STR0027,STR0016)
			Endif

		EndIf

		//Deleta arquivos temporarios
		oTmpTRB:Delete()

		RestArea(aArea)
	
	EndIf

Return .T.

/*


Ŀ
Funo    MNTC841PRO Autor Inacio Luiz Kolling     Data 19/01/2006
Ĵ
Descrio Monta o arquivo temporario                                  
Ĵ
 Uso      MNTC841                                                     
ٱ


*/
Function MNTC841PRO()
	Local cCONDI := " "

	If mv_par09 = 1
		cCONDI := "AND TQN_DTCON<>' '"
	ElseIf mv_par09 = 2
		cCONDI := "AND TQN_DTCON =' '"
	Else
		cCONDI := " "
	EndIf

	cAliasQry := "TETQN"

	cQuery := " SELECT * "
	cQuery += " FROM " + RetSQLName("TQN")
	If Mv_Par09 = 1
		cQuery += " WHERE TQN_DTCON BETWEEN '"+(Dtos(MV_PAR03))+"' AND '"+(Dtos(MV_PAR04))+"' "
	Else
		cQuery += " WHERE TQN_DTABAS BETWEEN '"+(Dtos(MV_PAR01))+"' AND '"+(Dtos(MV_PAR02))+"' "
	EndIf
	cQuery += " AND (TQN_POSTO>= '"+(MV_PAR05)+"' AND TQN_LOJA>= '"+MV_PAR06+"') "
	cQuery += " AND (TQN_POSTO<= '"+(MV_PAR07)+"' AND TQN_LOJA<= '"+MV_PAR08+"') "+(cCONDI)+ " AND "
	cQuery += " TQN_FILIAL BETWEEN '"+(MV_PAR10)+"' AND '"+(MV_PAR11)+"' "
	cQuery += " AND D_E_L_E_T_ = ' ' "
	If MV_PAR12 = 1 //Abastecimento Manual.
		cQuery += " AND TQN_AUTO = '2' "
	ElseIf MV_PAR12 = 2 //Abastecimento Automtico.
		cQuery += " AND TQN_AUTO = '1' "
	Endif
	cQuery += " ORDER BY TQN_POSTO,TQN_LOJA,TQN_NOTFIS,TQN_DTABAS,TQN_HRABAS "

	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

	ProcRegua(LastRec())
	DbSelectArea(cALIASQRY)
	While !Eof()
		IncProc(STR0050,STR0051) //"Aguarde..."###"Processando Registros..."

		RecLock((cTRBB), .T.)
		(cTRBB)->FILIAL  := (cAliasQry)->TQN_FILIAL
		(cTRBB)->NUMABST := (cAliasQry)->TQN_NABAST
		(cTRBB)->NFABAST := (cAliasQry)->TQN_NOTFIS
		(cTRBB)->VLRABAS := (cAliasQry)->TQN_VALTOT
		(cTRBB)->DTABAST := STOD((cAliasQry)->TQN_DTABAS)
		(cTRBB)->ESCALAA := (cAliasQry)->TQN_ESCALA
		(cTRBB)->FROTAAB := (cAliasQry)->TQN_FROTA
		(cTRBB)->DTVENCA := STOD((cAliasQry)->TQN_DTPGMT)
		(cTRBB)->CNPJABA := (cAliasQry)->TQN_CNPJ
		(cTRBB)->NOMFANA := NGSEEK("SA2",(cAliasQry)->TQN_POSTO+(cAliasQry)->TQN_LOJA,1,"A2_NOME")
		(cTRBB)->HORAABS := (cAliasQry)->TQN_HRABAS
		(cTRBB)->POSTOAB := (cAliasQry)->TQN_POSTO
		(cTRBB)->LOJAABA := (cAliasQry)->TQN_LOJA
		(cTRBB)->DTCONC  := STOD((cAliasQry)->TQN_DTCON)
		(cTRBB)->ABASTE  := (cAliasQry)->TQN_AUTO
		(cTRBB)->CCUSTO  := (cAliasQry)->TQN_CCUSTO

		MsUnLock(cTRBB)
		DbSelectArea(cALIASQRY)
		DbSkip()
	End
	DBCloseArea()

Return .T.
/*


Ŀ
Funo    MNTC841PE  Autor Inacio Luiz Kolling     Data 19/01/2006
Ĵ
Descrio Pesquisa especifica de abastecimento                        
Ĵ
 Uso      MNTC841                                                     
ٱ


*/
Function MNTC841PE()
	Local dDTABST := dDataBase, cCIDAD := Space(15)
	Local cPOSTOP := Space(Len(tqn->tqn_posto))
	Private oDlg5

	Define MsDialog oDlg5 From 300,120 To 420,400 Title OemToAnsi(STR0001) Pixel

	@ 1,1 Say OemtoAnsi(STR0021)
	@ 1,6 MsGet dDTABST Picture '@!' SIZE 35,10 Valid Naovazio(dDTABST)
	@ 2,1 Say OemtoAnsi(STR0031)
	@ 2,6 MsGet cPOSTOP Picture '@!' SIZE 35,10 Valid If(!Empty(cPOSTOP),NaoVazio(cPOSTOP) .And.;
	ExistCpo("TQF",cPOSTOP),.T.) F3 "TQF"
	@4,25 Button "&OK" Size 30,10 Action MNTC841POS(dDTABST,cPOSTOP)

	Activate MsDialog oDlg5 Centered

Return .T.

/*


Ŀ
Funo    MNTC841POS Autor Inacio Luiz Kolling     Data 19/01/2006
Ĵ
Descrio Posiciona no registro                                       
Ĵ
 Uso      MNTC841                                                     
ٱ


*/
Function MNTC841POS(vDTAB,cVPOST)
	DbSelectArea(cTRBB)
	dbsetorder(3)
	DbSeek(Dtos(vDTAB)+cVPOST,.T.)
	oDlg5:End()
Return .T.

/*


Ŀ
Funo    MNTC841VI  Autor Inacio Luiz Kolling     Data 19/01/2006
Ĵ
Descrio Visualiza cadastro abastecimento                            
Ĵ
 Uso      MNTC841                                                     
ٱ


*/
Function MNTC841VI()
	Local cCADAX := cCadastro
	cCadastro := NGSX2NOME("TQN")+' - '+STR0002
	DbSelectArea("TQN")
	DbSetOrder(01)
	DbSeek((cTRBB)->FILIAL+(cTRBB)->FROTAAB+Dtos((cTRBB)->DTABAST)+(cTRBB)->HORAABS)
	NGCAD01("TQN",Recno(),1)
	DbSelectArea(cTRBB)

	cCadastro := cCADAX
Return .t.

/*


Ŀ
Funo    MNTC841RE  Autor Inacio Luiz Kolling     Data 19/01/2006
Ĵ
Descrio Impressao dos abastecimentos                                
Ĵ
 Uso      MNTC841                                                     
ٱ


*/
Function MNTC841RE()
	Local WNREL      := "MNTC841"
	Local cDESC1     := STR0032 //"Relatorio dos abastecimentos."
	Local cDESC2     := "  "
	Local cDESC3     := "  "
	Local cSTRING    := "TQN"

	Private NOMEPROG := "MNTC841"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0029,1,STR0030,1,2,1,"",1}  //"Zebrado"###"Administracao"
	Private TITULO   := STR0032+" "+cCLASSI
	Private nTIPO    := 0
	Private CABEC1,CABEC2

	//Ŀ
	// Envia controle para a funcao SETPRINT                        
	//

	WNREL := SETPRINT(cSTRING,WNREL,,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")

	If nLASTKEY = 27
		Set Filter To
		DbSelectarea(cTRBB)
		Return
	Endif
	SETDEFAULT(aRETURN,cSTRING)

	RPTSTATUS({|lEND|MNTC841IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)
Return NIL

/*


Ŀ
Funo    MNTC841IMP  Autor Inacio Luiz Kolling    Data 19/01/2006
Ĵ
Descrio  Impressao do relatorio                                     
Ĵ
 Uso       MNTC841                                                    
ٱ


*/
Function MNTC841IMP(lEND,WNREL,TITULO,TAMANHO)
	Local cRODATXT := ""
	Local nCNTIMPR := 0
	Local cNomeFrota := ' '
	// [LGPD] Se as funcionalidades, referentes  LGPD, podem ser utilizadas
	Local lLgpd := FindFunction( 'FWPDCanUse' ) .And. FwPdCanUse( .T. )
	Local aOfusc := {}
	Local lCnpj := .F. 
	Local lRazaoSo := .F.

	//Local nTotNF := 0, cNF := ' '
	Private li := 80 ,m_pag := 1

	If lLgpd
		// [LGPD] Caso o usurio no possua acesso ao(s) campo(s), deve-se ofusc-lo(s)
		aOfusc := FwProtectedDataUtil():UsrAccessPDField( __CUSERID, { 'TQN_CNPJ', 'A2_NOME'} )

		lCnpj := Ascan( aOfusc, { |x| AllTrim(x) == 'TQN_CNPJ' } ) == 0
		lRazaoSo := Ascan( aOfusc, { |x| AllTrim(x) == 'A2_NOME' } ) == 0

	EndIf

	nTIPO      := IIf(aRETURN[4]==1,15,18)
	CABEC1     := STR0052 //"Filial        N. Abastec.      N. Fiscal  Vlr Abastec  Dt. Abast.  Escala  Frota            Nome da Frota                   Posto     Dt. Venc  CNPJ                Razo Social          Dt.Conc.  Tipo        C. Custo"
	CABEC2     := " "
	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         0         1         2
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	************************************************************************************************************************************************************************************************************************************
	Filial        N. Abastec.      N. Fiscal  Vlr Abastec  Dt. Abast.  Escala  Frota            Nome da Frota                   Posto     Dt. Venc  CNPJ                Razo Social          Dt.Conc.  Tipo        C. Custo
	************************************************************************************************************************************************************************************************************************************
	XXXXXXXXXXXX  999999999999999  XXXXXX      99.999,999  99/99/99    XXXXX   XXXXXXXXXXXXXXXX-XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XXXXXXXX  99/99/99  NN.NNN.NNN/NNNN-99  XXXXXXXXXXXXXXXXXXXX  99/99/99  XXXXXXXXXX  XXXXXXXXXXXXXXXXXXXX
	*/

	Dbselectarea(cTRBB)
	DbSetOrder(2)
	nRECTR := Recno()
	Dbgotop()
	Setregua(Lastrec())
	nTotal := 0
	While !Eof()
		cPosto := (cTRBB)->POSTOAB
		nPosto := 0

		While !Eof() .And. (cTRBB)->POSTOAB == cPosto
			nNota := 0
			lNota := .t.
			cNota := (cTRBB)->NFABAST

			While !Eof() .and. (cTRBB)->POSTOAB == cPosto .and. (cTRBB)->NFABAST == cNota

				nNota  += (cTRBB)->VLRABAS
				nPosto += (cTRBB)->VLRABAS
				nTotal += (cTRBB)->VLRABAS
				NGSOMALI(58)
				IncRegua()
				@ Li,000 PSAY (cTRBB)->FILIAL  Picture "@!"
				@ Li,012 PSAY (cTRBB)->NUMABST Picture "999999999999999"
				@ Li,029 PSAY (cTRBB)->NFABAST Picture "@!"
				@ Li,033 PSAY (cTRBB)->VLRABAS Picture "@E 999,999,999,999.999"
				@ Li,053 PSAY (cTRBB)->DTABAST Picture "99/99/9999"
				@ Li,066 PSAY (cTRBB)->ESCALAA Picture "@!"
				cNomeFrota := ' '
				
				DbSelectArea("ST9")
				DbSetOrder(1)
				If DbSeek((cTRBB)->FILIAL+(cTRBB)->FROTAAB)
					cNomeFrota := ST9->T9_NOME
				EndIf
				@ Li,074 PSAY (cTRBB)->FROTAAB+" "+SubStr(cNomeFrota,1,20)
				@ Li,113 PSAY Substr( (cTRBB)->POSTOAB, 1, 16) Picture "@!"
				@ Li,130 PSAY (cTRBB)->DTVENCA Picture "99/99/9999"
				If !Empty((cTRBB)->CNPJABA)
					If lCnpj
						// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
						@ Li,141 PSAY FwProtectedDataUtil():ValueAsteriskToAnonymize( (cTRBB)->CNPJABA ) Picture "@!"
					Else
						@ Li,141 PSAY (cTRBB)->CNPJABA Picture X3Picture( 'TQF_CNPJ' )
					EndIf
				Endif
				If lRazaoSo
					// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
					@ Li,161 PSAY FwProtectedDataUtil():ValueAsteriskToAnonymize( (cTRBB)->NOMFANA, 1, 16 ) Picture "@!"
				Else
					@ Li,161 PSAY Substr( (cTRBB)->NOMFANA, 1, 16 ) Picture "@!"
				EndIf
				@ Li,179 PSAY (cTRBB)->DTCONC Picture "99/99/9999"
				If (cTRBB)->ABASTE = "1"
					@ Li,191 PSAY STR0043 Picture "@!"		    //"Automtico"
				Else
					@ Li,191 PSAY STR0042 Picture "@!" //"Manual"
				Endif
				@ Li,202 PSAY (cTRBB)->CCUSTO Picture "@!"

				DbSelectArea(cTRBB)
				Dbskip()
			End
			If (cTRBB)->POSTOAB <> cPosto .Or. (cTRBB)->NFABAST <> cNota //nNota <> (cTRBB)->VLRABAS
				NGSOMALI(58)
				@ Li,000 PSAY STR0053 //"Total da Nota:"
				@ Li,032 PSAY nNota Picture "@E 999,999,999,999.999"
				NGSOMALI(58)
			EndIF
		End

		If nPosto > 0
			NGSOMALI(58)
			@ Li,000 PSAY STR0054 //"Total do Posto:"
			@ Li,032 PSAY nPosto Picture "@E 999,999,999,999.999"
			NGSOMALI(58)
		EndIF
	End

	If nTotal > 0
		NGSOMALI(58)
		@ Li,000 PSAY STR0055 //"Total Geral:"
		@ Li,032 PSAY nTotal Picture "@E 999,999,999,999.999"
		NGSOMALI(58)
	EndIf

	RODA(nCNTIMPR,cRODATXT,TAMANHO)
	RETINDEX("TQN")
	Set Filter To
	Set Device To SCREEN
	If aRETURN[5] == 1
		Set Printer To
		dbCommitAll()
		OURSPOOL(WNREL)
	Endif
	MS_FLUSH()

	Dbselectarea(cTRBB)
	Dbgoto(nRECTR)

Return NIL
/*


Ŀ
Funo    MNT841DT   Autor Soraia de Carvalho      Data  25/07/06 
Ĵ
Descrio Valida o parametro ate data                                 
Ĵ
 Uso      MNTC841                                                     
ٱ


*/
Function MNT841DT(nVal)
	Local dDtIni,dDtFim

	If nVal == 1
		dDtIni := MV_PAR01
		dDtFim := MV_PAR02
	Else
		dDtIni := MV_PAR03
		dDtFim := MV_PAR04
	EndIf

	If  dDtFim < dDtIni
		MsgStop(STR0056)  //"Data final no pode ser inferior  data inicial!"
		Return .F.
	EndIf

Return .T.
/*


Ŀ
Funo    MNT841FL   Autor Evaldo Cevinscki Jr.    Data  16/10/06 
Ĵ
Descrio Valida o parametro filial                                   
Ĵ
 Uso      MNTC841                                                     
ٱ


*/
Function MNT841FL(nOpc)
	Local cVERFL

	cVERFL := Mv_Par10

	If Empty(mv_par10) .And. mv_par11 = Replicate('Z', nSizeFil)
		Return .t.
	Else
		If nOpc == 1
			lRet := IIf(Empty(Mv_Par10),.t.,ExistCpo('SM0',SM0->M0_CODIGO+Mv_par10))
			If !lRet
				Return .f.
			EndIf
		EndIf

		If nOpc == 2
			lRet := IIF(ATECODIGO('SM0',SM0->M0_CODIGO+Mv_par10,SM0->M0_CODIGO+Mv_Par11,02),.T.,.F.)
			If !lRet
				Return .f.
			EndIf
		EndIf
	EndIf

Return .t.
/*/


Ŀ
Funo    MNT841ATE  Autor  Evaldo Cevinscki Jr.   Data  16/10/06 
Ĵ
Descrio Validaao do codigo do Posto                                
Ĵ
 Uso      MNTC841                                                     
ٱ


/*/
Function MNT841ATE(ALIAS,PAR1,PAR2,TAM)
	If Empty(par2)
		Help(" ",2,STR0057,,STR0058,3,1) //"ATENO"###"Cdigo do Posto final no pode ser vazio."
		Return .f.
	Elseif par2 < par1
		Help(" ",1,STR0057,,STR0059,3,1) //"ATENO"###"Cdigo do Posto final informado  invlido."
		Return .f.
	Endif

	If par2 = replicate('Z',Len(PAR2))
		Return .t.
	Else
		If !Atecodigo('TQF',Par1+Mv_Par06,Par2+Mv_Par08,08)
			Return .f.
		Endif
	Endif
Return .t.
/*


Ŀ
Funo    MNT841LJ   Autor Evaldo Cevinscki Jr.    Data  16/10/06 
Ĵ
Descrio Valida o parametro de Loja                                  
Ĵ
 Uso      Mntc841 	                                                  
ٱ


*/
Function  MNT841LJ()

	If Empty(MV_PAR05) .And. !Empty(MV_PAR06)
		MsgStop(STR0060)  //"Informe o Codigo do Posto"
		MV_PAR06 := Space(Len(MV_PAR06))
		Return .F.
	EndIf
	If !Empty(MV_PAR05) .And. Empty(MV_PAR06)
		MsgStop(STR0061)  //"Informe o Codigo da Loja"
		Return .F.
	EndIf
	If !Empty(MV_PAR05) .And. !Empty(MV_PAR06)
		IF !ExistCpo("TQF",MV_PAR05+MV_PAR06)
			Return .F.
		EndIf
	EndIf

Return .T.
