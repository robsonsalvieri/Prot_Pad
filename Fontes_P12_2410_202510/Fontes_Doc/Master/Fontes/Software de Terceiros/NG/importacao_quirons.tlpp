#include 'tlpp-core.th'
#include 'totvs.ch'

namespace importacao_quirons

//---------------------------------------------------------------------
/*/{Protheus.doc} fAtestadoMedico
Retorna todos os atestados médicos cadastrados

@author Gabriel Sokacheski
@since 20/03/2024

/*/
//---------------------------------------------------------------------
@Get( '/api/rh/v2/sicknote' )
Function fAtestadoMedico()

  Local cEmp            as character
  Local cFil            as character
  Local cQuery          as character
  Local cWhere          as character
  Local cResultado      as character
  Local cAfastamento    as character
  Local cDataChave      as character

  Local jHeaders        as json
  Local jResultado      as json
  Local jQueryParams    as json

  Local lRpc            as logical

  Local nPagina         as numeric
  Local nAtestado       as numeric
  Local nTamanhoPagina  as numeric

  Local oQuery          as object

  jQueryParams := oRest:getQueryRequest()
  jHeaders := jsonObject():New()
  jHeaders[ 'Content-Type' ] := 'application/json'

  cEmp            := FWGrpCompany()
  cFil            := FWCodFil()
  nPagina         := jQueryParams[ 'page' ]; nPagina := Val( nPagina )
  nTamanhoPagina  := jQueryParams[ 'pageSize' ]; nTamanhoPagina := Val( nTamanhoPagina )

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TNY' ) + " TNY"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TM0' ) + " TM0 ON"
  cQuery += CRLF + "TM0_NUMFIC = TNY_NUMFIC"
  cQuery += CRLF + "AND TM0_FILIAL = '" + FwXFilial( 'TM0' ) + "'"
  cQuery += CRLF + "AND TM0.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'SRA' ) + " SRA ON"
  cQuery += CRLF + "RA_MAT = TM0_MAT"
  cQuery += CRLF + "AND RA_FILIAL = TM0_FILFUN"
  cQuery += CRLF + "AND SRA.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TNP' ) + " TNP ON"
  cQuery += CRLF + "TNP_EMITEN = TNY_EMITEN"
  cQuery += CRLF + "AND TNP_FILIAL = '" + FwXFilial( 'TNP' ) + "'"
  cQuery += CRLF + "AND TNP.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "LEFT JOIN " + RetSqlName( 'TYZ' ) + " TYZ ON"
  cQuery += CRLF + "TYZ_NATEST = TNY_NATEST"
  cQuery += CRLF + "AND TYZ_FILIAL = '" + FwXFilial( 'TYZ' ) + "'"
  cQuery += CRLF + "AND TYZ.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "TNY.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TNY_FILIAL = '" + FwXFilial( 'TNY' ) + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'branchId'                           , 'TNY_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'employeeId'                         , 'RA_MAT'      , .T., .T. )
  oQuery:addMapFields( 'employeeId2'                        , 'TM0_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'employeeId3'                        , 'RA_FILIAL'   , .T., .T. )
  oQuery:addMapFields( 'erpId'                              , 'TNY_NUMFIC'  , .T., .T. )
  oQuery:addMapFields( 'type'                               , 'TNY_CODAFA'  , .T., .T. )
  oQuery:addMapFields( 'date'                               , 'TNY_DTINIC'  , .T., .T. )
  oQuery:addMapFields( 'internationalDiseaseClassification' , 'TNY_CID'     , .T., .T. )
  oQuery:addMapFields( 'classEntityRegistrationCode'        , 'TNP_NUMENT'  , .T., .T. )
  oQuery:addMapFields( 'doctorName'                         , 'TNP_NOME'    , .T., .T. )
  oQuery:addMapFields( 'classEntityState'                   , 'TNP_UF'      , .T., .T. )
  oQuery:addMapFields( 'typeOfAbsence'                      , 'TNY_CODAFA'  , .T., .T. )
  oQuery:addMapFields( 'absenceStartDate'                   , 'TYZ_DTSAID'  , .T., .T. )
  oQuery:addMapFields( 'absenceEndDate'                     , 'TYZ_DTALTA'  , .T., .T. )
  oQuery:addMapFields( 'typeOfAllowance'                    , 'TNY_CODABO'  , .T., .T. )
  oQuery:addMapFields( 'absenceStartHour'                   , 'TNY_HRINIC'  , .T., .T. )
  oQuery:addMapFields( 'absenceEndHour'                     , 'TNY_HRFIM'   , .T., .T. )
  oQuery:addMapFields( 'length'                             , 'TNY_QTDIAS'  , .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TNY_FILIAL, TNY_NUMFIC' )

  oQuery:SetPageSize( nTamanhoPagina )
  oQuery:setPage( nPagina )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nAtestado := 1 To Len( jResultado[ 'items' ] )

    cDataChave := FWTimeStamp(;
        1,;
        fDatas( jResultado[ 'items', nAtestado, 'date' ] ),;
        IIf( !Empty( jResultado[ 'items', nAtestado, 'absenceStartHour' ] ), jResultado[ 'items', nAtestado, 'absenceStartHour' ], '00:00:00.000' );
      )

    jResultado[ 'items', nAtestado, 'companyId' ] := cEmp

    jResultado[ 'items', nAtestado, 'erpId' ] := cEmp + '|' + jResultado[ 'items', nAtestado, 'branchId' ] + '|';
    + jResultado[ 'items', nAtestado, 'erpId' ] + '|' + SubStr( cDataChave, 1, 8 ) + '|' + SubStr( cDataChave, 9 )

    jResultado[ 'items', nAtestado, 'employeeId' ] := cEmp + '|' + jResultado[ 'items', nAtestado, 'employeeId3' ] + '|' +;
    jResultado[ 'items', nAtestado, 'employeeId' ]

    cAfastamento := Posicione( 'RCM', 1, FwXFilial( 'RCM' ) + jResultado[ 'items', nAtestado, 'type' ], 'RCM_CODSEF' )

    If 'Q' $ cAfastamento
      jResultado[ 'items', nAtestado, 'type' ] := 2 // Licença maternidade
    ElseIf 'U' $ cAfastamento
      jResultado[ 'items', nAtestado, 'type' ] := 3 // Aposentadoria por invalidez
    Else
      jResultado[ 'items', nAtestado, 'type' ] := 1 // Atestado
    EndIf

    If !Empty( jResultado[ 'items', nAtestado, 'date' ] )

      jResultado[ 'items', nAtestado, 'date' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nAtestado, 'date' ] ),;
        IIf( !Empty( jResultado[ 'items', nAtestado, 'absenceStartHour' ] ), jResultado[ 'items', nAtestado, 'absenceStartHour' ], '00:00:00.000' );
      )

    EndIf

    If !Empty( jResultado[ 'items', nAtestado, 'absenceStartDate' ] )

      jResultado[ 'items', nAtestado, 'absenceStartDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nAtestado, 'absenceStartDate' ] ),;
        IIf( !Empty( jResultado[ 'items', nAtestado, 'absenceStartHour' ] ), jResultado[ 'items', nAtestado, 'absenceStartHour' ], '00:00:00.000' );
      )

    EndIf

    If !Empty( jResultado[ 'items', nAtestado, 'absenceEndDate' ] )

      jResultado[ 'items', nAtestado, 'absenceEndDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nAtestado, 'absenceEndDate' ] ),;
        IIf( !Empty( jResultado[ 'items', nAtestado, 'absenceEndHour' ] ), jResultado[ 'items', nAtestado, 'absenceEndHour' ], '00:00:00.000' );
      )

    EndIf

    If Empty( jResultado[ 'items', nAtestado, 'internationalDiseaseClassification' ] )
      jResultado[ 'items', nAtestado, 'internationalDiseaseClassification' ] := Nil
    EndIf

    If Empty( jResultado[ 'items', nAtestado, 'classEntityRegistrationCode' ] )
      jResultado[ 'items', nAtestado, 'classEntityRegistrationCode' ] := Nil
    EndIf

    If Empty( jResultado[ 'items', nAtestado, 'classEntityState' ] )
      jResultado[ 'items', nAtestado, 'classEntityState' ] := Nil
    EndIf

    If Empty( jResultado[ 'items', nAtestado, 'typeOfAbsence' ] )
      jResultado[ 'items', nAtestado, 'typeOfAbsence' ] := Nil
    Else
      jResultado[ 'items', nAtestado, 'typeOfAbsence' ] := cEmp + '|' + FwXFilial( 'RCM' ) + '|' + jResultado[ 'items', nAtestado, 'typeOfAbsence' ]
    EndIf

    If Empty( jResultado[ 'items', nAtestado, 'absenceStartDate' ] )
      jResultado[ 'items', nAtestado, 'absenceStartDate' ] := Nil
    EndIf

    If Empty( jResultado[ 'items', nAtestado, 'absenceEndDate' ] )
      jResultado[ 'items', nAtestado, 'absenceEndDate' ] := Nil
    EndIf

    If Empty( jResultado[ 'items', nAtestado, 'typeOfAllowance' ] )
      jResultado[ 'items', nAtestado, 'typeOfAllowance' ] := Nil
    Else
      jResultado[ 'items', nAtestado, 'typeOfAllowance' ] := cEmp + '|' + FwXFilial( 'SP6' ) + '|' + jResultado[ 'items', nAtestado, 'typeOfAllowance' ]
    EndIf

    If Empty( jResultado[ 'items', nAtestado, 'allowanceStartDate' ] )
      jResultado[ 'items', nAtestado, 'allowanceStartDate' ] := Nil
    EndIf

    If Empty( jResultado[ 'items', nAtestado, 'allowanceEndDate' ] )
      jResultado[ 'items', nAtestado, 'allowanceEndDate' ] := Nil
    EndIf

    jResultado[ 'items', nAtestado ]:delName( 'absenceStartHour' )
    jResultado[ 'items', nAtestado ]:delName( 'absenceEndHour' )
    jResultado[ 'items', nAtestado ]:delName( 'employeeId2' )
    jResultado[ 'items', nAtestado ]:delName( 'employeeId3' )

  Next nAtestado

  jResultado[ 'totalAmount' ] := Len( jResultado[ 'items' ] ) + jResultado[ 'remainingRecords' ]
  jResultado[ 'amount' ] := Len( jResultado[ 'items' ] )

  If lRpc
    RpcClearEnv()
  EndIF

  oRest:setHeaderResponse( jHeaders )
  oRest:setResponse( jResultado )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fAtestadoAso
Retorna todos os atestados de saúde ocupacionais

@author Gabriel Sokacheski
@since 27/03/2024

/*/
//---------------------------------------------------------------------
@Get( '/api/rh/v2/medicalconsultationng' )
Function fAtestadoAso()

  Local cEmp                as character
  Local cFil                as character
  Local cQuery              as character
  Local cWhere              as character
  Local cResultado          as character

  Local jHeaders            as json
  Local jResultado          as json
  Local jQueryParams        as json

  Local lRpc                as logical

  Local nExame              as numeric
  Local nPagina             as numeric
  Local nAtestado           as numeric
  Local nTamanhoPagina      as numeric

  Local oQuery              as object

  jQueryParams := oRest:getQueryRequest()
  jHeaders := jsonObject():New()
  jHeaders[ 'Content-Type' ] := 'application/json'

  cEmp            := FWGrpCompany()
  cFil            := FWCodFil()
  nPagina         := jQueryParams[ 'page' ]; nPagina := Val( nPagina )
  nTamanhoPagina  := jQueryParams[ 'pageSize' ]; nTamanhoPagina := Val( nTamanhoPagina )

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TMY' ) + " TMY"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TM0' ) + " TM0 ON"
  cQuery += CRLF + "TM0_NUMFIC = TMY_NUMFIC"
  cQuery += CRLF + "AND TM0_FILIAL = '" + FwXFilial( 'TM0' ) + "'"
  cQuery += CRLF + "AND TM0.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "LEFT JOIN " + RetSqlName( 'SRA' ) + " SRA ON"
  cQuery += CRLF + "RA_MAT = TM0_MAT"
  cQuery += CRLF + "AND RA_FILIAL = TM0_FILFUN"
  cQuery += CRLF + "AND SRA.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TMK' ) + " TMK ON"
  cQuery += CRLF + "TMK_CODUSU = TMY_CODUSU"
  cQuery += CRLF + "AND TMK_FILIAL = '" + FwXFilial( 'TMK' ) + "'"
  cQuery += CRLF + "AND TMK.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "TMY.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TMY_FILIAL = '" + FwXFilial( 'TMY' ) + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'branchId'         , 'TMY_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'employeeId'       , 'RA_MAT'      , .T., .T. )
  oQuery:addMapFields( 'employeeId2'      , 'TM0_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'employeeId3'      , 'TM0_NUMFIC'  , .T., .T. )
  oQuery:addMapFields( 'employeeId4'      , 'RA_FILIAL'   , .T., .T. )
  oQuery:addMapFields( 'id'               , 'TMY_NUMASO'  , .T., .T. )
  oQuery:addMapFields( 'type'             , 'TMY_NATEXA'  , .T., .T. )
  oQuery:addMapFields( 'expectedDate'     , 'TMY_DTPROG'  , .T., .T. )
  oQuery:addMapFields( 'issueDate'        , 'TMY_DTEMIS'  , .T., .T. )
  oQuery:addMapFields( 'situation'        , 'TMY_INDPAR'  , .T., .T. )
  oQuery:addMapFields( 'doctor'           , 'TMK_NOMUSU'  , .T., .T. )
  oQuery:addMapFields( 'crm'              , 'TMK_NUMENT'  , .T., .T. )
  oQuery:addMapFields( 'state'            , 'TMK_UF'      , .T., .T. )
  oQuery:addMapFields( 'referentialExam'  , 'TMY_INDEXA'  , .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TMY_FILIAL, TMY_NUMFIC, TMY_NUMASO' )

  oQuery:SetPageSize( nTamanhoPagina )
  oQuery:setPage( nPagina )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nAtestado := 1 To Len( jResultado[ 'items' ] )

    jResultado[ 'items', nAtestado, 'companyId' ] := cEmp

    jResultado[ 'items', nAtestado, 'erpId' ] := cEmp + '|' + jResultado[ 'items', nAtestado, 'branchId' ] + '|' +;
    jResultado[ 'items', nAtestado, 'id' ]

    If Empty( jResultado[ 'items', nAtestado, 'employeeId' ] )
      jResultado[ 'items', nAtestado, 'employeeId' ] := cEmp + '|' + jResultado[ 'items', nAtestado, 'employeeId2' ] + '|' +;
      jResultado[ 'items', nAtestado, 'employeeId3' ]
    Else
      jResultado[ 'items', nAtestado, 'employeeId' ] := cEmp + '|' + jResultado[ 'items', nAtestado, 'employeeId4' ] + '|' +;
      jResultado[ 'items', nAtestado, 'employeeId' ]
    EndIf

    Do Case
      Case jResultado[ 'items', nAtestado, 'referentialExam' ] == '2'
        jResultado[ 'items', nAtestado, 'referentialExam' ] := 1
      Otherwise
        jResultado[ 'items', nAtestado, 'referentialExam' ] := 0
    EndCase

    jResultado[ 'items', nAtestado, 'examList' ] := fBusExaAso(;
      jResultado[ 'items', nAtestado, 'id' ] + Space( TamSX3( 'TMY_NUMASO' )[ 1 ] - Len( jResultado[ 'items', nAtestado, 'id' ] ) ),;
      jResultado[ 'items', nAtestado, 'referentialExam' ];
    )

    If jResultado[ 'items', nAtestado, 'type' ] == '1' // Admissional
      jResultado[ 'items', nAtestado, 'type' ] := 0
    ElseIf jResultado[ 'items', nAtestado, 'type' ] == '2' // Periódico
      jResultado[ 'items', nAtestado, 'type' ] := 1
    ElseIf jResultado[ 'items', nAtestado, 'type' ] == '3' // Mudança de Risco
      jResultado[ 'items', nAtestado, 'type' ] := 3
    ElseIf jResultado[ 'items', nAtestado, 'type' ] == '4' // Retorno ao Trabalho
      jResultado[ 'items', nAtestado, 'type' ] := 2
    ElseIf jResultado[ 'items', nAtestado, 'type' ] == '5' // Demissional
      jResultado[ 'items', nAtestado, 'type' ] := 4
    Else // Pontual
      jResultado[ 'items', nAtestado, 'type' ] := 5
    EndIf

    If !Empty( jResultado[ 'items', nAtestado, 'expectedDate' ] )

      jResultado[ 'items', nAtestado, 'expectedDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nAtestado, 'expectedDate' ] ),;
        '00:00:00.000';
      )

    EndIf

    If !Empty( jResultado[ 'items', nAtestado, 'issueDate' ] )

      jResultado[ 'items', nAtestado, 'issueDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nAtestado, 'issueDate' ] ),;
        '00:00:00.000';
      )

    EndIf

    If jResultado[ 'items', nAtestado, 'situation' ] == '1'
      jResultado[ 'items', nAtestado, 'situation' ] := 0
    ElseIf jResultado[ 'items', nAtestado, 'situation' ] == '2'
      jResultado[ 'items', nAtestado, 'situation' ] := 1
    Else
      jResultado[ 'items', nAtestado, 'situation' ] := 2
    EndIf

    If Empty( jResultado[ 'items', nAtestado, 'issueDate' ] )
      jResultado[ 'items', nAtestado, 'issueDate' ] := Nil
    EndIf

    If Empty( jResultado[ 'items', nAtestado, 'crm' ] )
      jResultado[ 'items', nAtestado, 'crm' ] := Nil
    EndIf

    If Empty( jResultado[ 'items', nAtestado, 'state' ] )
      jResultado[ 'items', nAtestado, 'state' ] := Nil
    EndIf

    For nExame := 1 To Len( jResultado[ 'items', nAtestado, 'examList' ] )
      jResultado[ 'items', nAtestado, 'examList', nExame, 'doctor' ] := jResultado[ 'items', nAtestado, 'doctor' ]
      jResultado[ 'items', nAtestado, 'examList', nExame, 'crm' ] := jResultado[ 'items', nAtestado, 'crm' ]
      jResultado[ 'items', nAtestado, 'examList', nExame, 'state' ] := jResultado[ 'items', nAtestado, 'state' ]
    Next nExame

    If Empty( jResultado[ 'items', nAtestado, 'examList' ] )
      jResultado[ 'items', nAtestado, 'examList' ] := Nil
    EndIf

    jResultado[ 'items', nAtestado ]:delName( 'employeeId2' )
    jResultado[ 'items', nAtestado ]:delName( 'employeeId3' )
    jResultado[ 'items', nAtestado ]:delName( 'employeeId4' )
    jResultado[ 'items', nAtestado ]:delName( 'referentialExam' )

  Next nAtestado

  jResultado[ 'totalAmount' ] := Len( jResultado[ 'items' ] ) + jResultado[ 'remainingRecords' ]
  jResultado[ 'amount' ] := Len( jResultado[ 'items' ] )

  If lRpc
    RpcClearEnv()
  EndIF

  oRest:setHeaderResponse( jHeaders )
  oRest:setResponse( jResultado )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fBusExaAso
Busca os exames do aso

@author Gabriel Sokacheski
@since 19/04/2024

@param cAso, código do aso
@param cTipoExame, conteúdo do campo TMY_INDEXA

@return jResultado, lista dos exames
/*/
//---------------------------------------------------------------------
Static Function fBusExaAso( cAso, cTipoExame )

  Local cQuery      as character
  Local cWhere      as character
  Local cResultado  as character

  Local jResultado  as json

  Local nExame      as numeric

  Local oQuery      as object

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TM5' ) + " TM5"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TM4' ) + " TM4 ON"
  cQuery += CRLF + "TM4_EXAME = TM5_EXAME"
  cQuery += CRLF + "AND TM4_FILIAL = '" + FwXFilial( 'TM4' ) + "'"
  cQuery += CRLF + "AND TM4.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "TM5.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TM5_FILIAL = '" + FwXFilial( 'TM5' ) + "'"
  cWhere += CRLF + "AND TM5_NUMASO = '" + cAso + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'examDate'              , 'TM5_DTRESU', .T., .T. )
  oQuery:addMapFields( 'examDescription'       , 'TM4_NOMEXA', .T., .T. )
  oQuery:addMapFields( 'referentialExam'       , 'TM5_ORIGEX', .T., .T. )
  oQuery:addMapFields( 'result'                , 'TM5_INDRES', .T., .T. )
  oQuery:addMapFields( 'occupationalActivity'  , 'TM5_ORIGEX', .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TM5_FILIAL, TM5_NUMASO' )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nExame := 1 To Len( jResultado[ 'items' ] )

    If !Empty( jResultado[ 'items', nExame, 'examDate' ] )

      jResultado[ 'items', nExame, 'examDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nExame, 'examDate' ] ),;
        '00:00:00.000';
      )

    EndIf

    If jResultado[ 'items', nExame, 'result' ] == '1'
      jResultado[ 'items', nExame, 'result' ] := 0
    ElseIf jResultado[ 'items', nExame, 'result' ] == '2'
      jResultado[ 'items', nExame, 'result' ] := 1
    EndIf

    jResultado[ 'items', nExame, 'occupationalActivity' ] := 1
    jResultado[ 'items', nExame, 'referentialExam' ] := cTipoExame

  Next nExame

Return jResultado[ 'items' ]

//---------------------------------------------------------------------
/*/{Protheus.doc} fCredenciados
Retorna todos os credenciados que realizam exames

@author Gabriel Sokacheski
@since 01/04/2024

/*/
//---------------------------------------------------------------------
@Get( '/api/mdt/v1/accredited' )
Function fCredenciados()

  Local cEmp                  as character
  Local cFil                  as character
  Local cQuery                as character
  Local cWhere                as character
  Local cResultado            as character

  Local jHeaders              as json
  Local jQueryParams          as json
  Local jResultado            as json

  Local lRpc            as logical

  Local nExame                as numeric
  Local nPagina               as numeric
  Local nFornecedor           as numeric
  Local nTamanhoPagina        as numeric

  Local oQuery      as object

  jQueryParams := oRest:getQueryRequest()
  jHeaders := jsonObject():New()
  jHeaders[ 'Content-Type' ] := 'application/json'

  cEmp            := FWGrpCompany()
  cFil            := FWCodFil()
  nPagina         := jQueryParams[ 'page' ]; nPagina := Val( nPagina )
  nTamanhoPagina  := jQueryParams[ 'pageSize' ]; nTamanhoPagina := Val( nTamanhoPagina )

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'SA2' ) + " SA2"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cQuery += CRLF + "AND EXISTS ( SELECT 1 FROM " + RetSqlName( 'TMD' ) + " TMD WHERE SA2.A2_COD = TMD.TMD_FORNEC AND SA2.A2_LOJA = TMD.TMD_LOJA AND TMD.D_E_L_E_T_ != '*' )"

  cWhere := "D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND A2_FILIAL = '" + FwXFilial( 'SA2' ) + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'branchId'       , 'A2_FILIAL' , .T., .T. )
  oQuery:addMapFields( 'name'           , 'A2_NOME'   , .T., .T. )
  oQuery:addMapFields( 'documentNumber' , 'A2_CGC'    , .T., .T. )
  oQuery:addMapFields( 'id'             , 'A2_COD'    , .T., .T. )
  oQuery:addMapFields( 'store'          , 'A2_LOJA'   , .T., .T. )
  oQuery:addMapFields( 'contractStart'  , 'A2_DTINIV' , .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'A2_FILIAL, A2_CGC' )

  oQuery:SetPageSize( nTamanhoPagina )
  oQuery:setPage( nPagina )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nFornecedor := 1 To Len( jResultado[ 'items' ] )

    jResultado[ 'items', nFornecedor, 'companyId' ] := cEmp

    jResultado[ 'items', nFornecedor, 'exams' ] := fBusValExa(;
      jResultado[ 'items', nFornecedor, 'id' ] + Space( TamSX3( 'A2_COD' )[ 1 ] - Len( jResultado[ 'items', nFornecedor, 'id' ] ) ),;
      jResultado[ 'items', nFornecedor, 'store' ] + Space( TamSX3( 'A2_LOJA' )[ 1 ] - Len( jResultado[ 'items', nFornecedor, 'store' ] ) );
    )

    jResultado[ 'items', nFornecedor, 'erpId' ] := cEmp + '|' + jResultado[ 'items', nFornecedor, 'branchId' ] + '|' +;
    jResultado[ 'items', nFornecedor, 'id' ] +  '|' + jResultado[ 'items', nFornecedor, 'store' ]

    If !Empty( jResultado[ 'items', nFornecedor, 'contractStart' ] )

      jResultado[ 'items', nFornecedor, 'contractStart' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nFornecedor, 'contractStart' ] ),;
        '00:00:00.000';
      )

    Else

      For nExame := 1 To Len( jResultado[ 'items', nFornecedor, 'exams' ] )

        If Empty( jResultado[ 'items', nFornecedor, 'contractStart' ] )
          jResultado[ 'items', nFornecedor, 'contractStart' ] := jResultado[ 'items', nFornecedor, 'exams', nExame, 'contractStart' ]
        ElseIf jResultado[ 'items', nFornecedor, 'exams', nExame, 'contractStart' ] < jResultado[ 'items', nFornecedor, 'contractStart' ]
          jResultado[ 'items', nFornecedor, 'contractStart' ] := jResultado[ 'items', nFornecedor, 'exams', nExame, 'contractStart' ]
        EndIf

        jResultado[ 'items', nFornecedor, 'exams', nExame ]:delName( 'contractStart' )

      Next nExame

      jResultado[ 'items', nFornecedor, 'contractStart' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nFornecedor, 'contractStart' ] ),;
        '00:00:00.000';
      )

    EndIf

    If Empty( jResultado[ 'items', nFornecedor, 'documentNumber' ] )
      jResultado[ 'items', nFornecedor, 'documentNumber' ] := ''
    EndIf

    jResultado[ 'items', nFornecedor ]:delName( 'id' )
    jResultado[ 'items', nFornecedor ]:delName( 'store' )

  Next nFornecedor

  jResultado[ 'totalAmount' ] := Len( jResultado[ 'items' ] ) + jResultado[ 'remainingRecords' ]
  jResultado[ 'amount' ] := Len( jResultado[ 'items' ] )

  If lRpc
    RpcClearEnv()
  EndIF

  oRest:setHeaderResponse( jHeaders )
  oRest:setResponse( jResultado )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fBusValExa
Busca os valores dos exames

@author Gabriel Sokacheski
@since 22/04/2024

@param cExame, código do exame

@return jResultado, lista dos valores
/*/
//---------------------------------------------------------------------
Static Function fBusValExa( cFornecedor, cLoja )

  Local cQuery      as character
  Local cWhere      as character
  Local cResultado  as character

  Local nExame      as numeric

  Local jResultado  as json

  Local oQuery      as object

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TMD' ) + " TMD"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TM4' ) + " TM4 ON"
  cQuery += CRLF + "TM4_EXAME = TMD_EXAME"
  cQuery += CRLF + "AND TM4_FILIAL = '" + FwXFilial( 'TM4' ) + "'"
  cQuery += CRLF + "AND TM4.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "TMD.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TMD_FILIAL = '" + FwXFilial( 'TMD' ) + "'"
  cWhere += CRLF + "AND TMD_FORNEC = '" + cFornecedor + "'"
  cWhere += CRLF + "AND TMD_LOJA = '" + cLoja + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'name'         , 'TM4_NOMEXA', .T., .T. )
  oQuery:addMapFields( 'price'        , 'TMD_VALEXA', .T., .T. )
  oQuery:addMapFields( 'eSocialCode'  , 'TM4_NOMEXA', .T., .T. )
  oQuery:addMapFields( 'contractStart', 'TMD_DTINIC', .T., .T. )

  oQuery:setIsCaseSensitive( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TMD_FILIAL, TMD_FORNEC, TMD_LOJA' )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nExame := 1 To Len( jResultado[ 'items' ] )
    jResultado[ 'items', nExame, 'eSocialCode' ] := ''
  Next nExame

Return jResultado[ 'items' ]

//---------------------------------------------------------------------
/*/{Protheus.doc} fFichaMedica
Retorna todas as fichas médicas

@author Gabriel Sokacheski
@since 02/04/2024

/*/
//---------------------------------------------------------------------
@Get( '/api/mdt/v1/medicalrecords' )
Function fFichaMedica()

  Local cEmp            as character
  Local cFil            as character
  Local cQuery          as character
  Local cWhere          as character
  Local cResultado      as character

  Local jHeaders        as json
  Local jResultado      as json
  Local jQueryParams    as json

  Local lRpc            as logical

  Local nPagina         as numeric
  Local nFicha          as numeric
  Local nTamanhoPagina  as numeric

  Local oQuery          as object

  jQueryParams := oRest:getQueryRequest()
  jHeaders := jsonObject():New()
  jHeaders[ 'Content-Type' ] := 'application/json'

  cEmp            := FWGrpCompany()
  cFil            := FWCodFil()
  nPagina         := jQueryParams[ 'page' ]; nPagina := Val( nPagina )
  nTamanhoPagina  := jQueryParams[ 'pageSize' ]; nTamanhoPagina := Val( nTamanhoPagina )

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TM0' ) + " TM0"

  cQuery += CRLF + "LEFT JOIN " + RetSqlName( 'SRA' ) + " SRA ON"
  cQuery += CRLF + "RA_MAT = TM0_MAT"
  cQuery += CRLF + "AND RA_FILIAL = TM0_FILFUN"
  cQuery += CRLF + "AND SRA.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "TM0.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TM0_FILIAL = '" + FwXFilial( 'TM0' ) + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'branchId'               , 'TM0_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'employeeId'             , 'RA_MAT'      , .T., .T. )
  oQuery:addMapFields( 'erpId'                  , 'TM0_NUMFIC'  , .T., .T. )
  oQuery:addMapFields( 'erpId2'                 , 'RA_FILIAL'   , .T., .T. )
  oQuery:addMapFields( 'candidate'              , 'TM0_CANDID'  , .T., .T. )
  oQuery:addMapFields( 'registration'           , 'TM0_MAT'     , .T., .T. )
  oQuery:addMapFields( 'name'                   , 'TM0_NOMFIC'  , .T., .T. )
  oQuery:addMapFields( 'bloodDonator'           , 'TM0_DOADOR'  , .T., .T. )
  oQuery:addMapFields( 'bloodType'              , 'TM0_SANGUE'  , .T., .T. )
  oQuery:addMapFields( 'factor'                 , 'TM0_FATORH'  , .T., .T. )
  oQuery:addMapFields( 'birthday'               , 'TM0_DTNASC'  , .T., .T. )
  oQuery:addMapFields( 'gender'                 , 'TM0_SEXO'    , .T., .T. )
  oQuery:addMapFields( 'weight'                 , 'TM0_PESO'    , .T., .T. )
  oQuery:addMapFields( 'height'                 , 'TM0_ALTURA'  , .T., .T. )
  oQuery:addMapFields( 'smoker'                 , 'TM0_FUMA'    , .T., .T. )
  oQuery:addMapFields( 'eyeHeight'              , 'TM0_ALTOLH'  , .T., .T. )
  oQuery:addMapFields( 'nippleLine'             , 'TM0_LMAMIL'  , .T., .T. )
  oQuery:addMapFields( 'pubicHeight'            , 'TM0_ALTPUB'  , .T., .T. )
  oQuery:addMapFields( 'kneeHeight'             , 'TM0_ALTJOE'  , .T., .T. )
  oQuery:addMapFields( 'elbowHeight'            , 'TM0_ALTCOT'  , .T., .T. )
  oQuery:addMapFields( 'armSize'                , 'TM0_TAMBRA'  , .T., .T. )
  oQuery:addMapFields( 'forearmSize'            , 'TM0_TAMANT'  , .T., .T. )
  oQuery:addMapFields( 'handSize'               , 'TM0_TAMMAO'  , .T., .T. )
  oQuery:addMapFields( 'shoesSize'              , 'TM0_NUMCAL'  , .T., .T. )
  oQuery:addMapFields( 'eyeColor'               , 'TM0_COROLH'  , .T., .T. )
  oQuery:addMapFields( 'skinColor'              , 'TM0_CORPEL'  , .T., .T. )
  oQuery:addMapFields( 'hairColor'              , 'TM0_CORCAB'  , .T., .T. )
  oQuery:addMapFields( 'tamagistPeriod'         , 'TM0_QTTEMP'  , .T., .T. )
  oQuery:addMapFields( 'amountConsumed'         , 'TM0_QTCIG'   , .T., .T. )
  oQuery:addMapFields( 'historic'               , 'TM0_DESCRI'  , .T., .T. )
  oQuery:addMapFields( 'individualRegistration' , 'TM0_CPF'     , .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TM0_FILIAL, TM0_NUMFIC' )

  oQuery:SetPageSize( nTamanhoPagina )
  oQuery:setPage( nPagina )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nFicha := 1 To Len( jResultado[ 'items' ] )

    jResultado[ 'items', nFicha, 'companyId' ] := cEmp

    If Empty( jResultado[ 'items', nFicha, 'employeeId' ] ) // Candidato
      jResultado[ 'items', nFicha, 'employeeId' ] := cEmp + '|' + jResultado[ 'items', nFicha, 'branchId' ] + '|' + ;
      jResultado[ 'items', nFicha, 'erpId' ]
    Else // Funcionário
      jResultado[ 'items', nFicha, 'employeeId' ] := cEmp + '|' + jResultado[ 'items', nFicha, 'erpId2' ] + '|' + ;
      jResultado[ 'items', nFicha, 'employeeId' ]
    EndIf

    jResultado[ 'items', nFicha, 'erpId' ] := cEmp + '|' + jResultado[ 'items', nFicha, 'branchId' ] + '|' + ;
    jResultado[ 'items', nFicha, 'erpId' ]

    If !Empty( jResultado[ 'items', nFicha, 'candidate' ] ) .And. Empty( jResultado[ 'items', nFicha, 'registration' ] )
      jResultado[ 'items', nFicha, 'candidate' ] := .T.
    Else
      jResultado[ 'items', nFicha, 'candidate' ] := .F.
    EndIf

    If jResultado[ 'items', nFicha, 'bloodDonator' ] == '1'
      jResultado[ 'items', nFicha, 'bloodDonator' ] := .T.
    Else
      jResultado[ 'items', nFicha, 'bloodDonator' ] := .F.
    EndIf

    If jResultado[ 'items', nFicha, 'bloodType' ] == '1' .And. jResultado[ 'items', nFicha, 'factor' ] == '1'
      jResultado[ 'items', nFicha, 'bloodType' ] := 0 // A+
    ElseIf jResultado[ 'items', nFicha, 'bloodType' ] == '1' .And. jResultado[ 'items', nFicha, 'factor' ] == '2'
      jResultado[ 'items', nFicha, 'bloodType' ] := 1 // A-
    ElseIf jResultado[ 'items', nFicha, 'bloodType' ] == '2' .And. jResultado[ 'items', nFicha, 'factor' ] == '1'
      jResultado[ 'items', nFicha, 'bloodType' ] := 2 // B+
    ElseIf jResultado[ 'items', nFicha, 'bloodType' ] == '2' .And. jResultado[ 'items', nFicha, 'factor' ] == '2'
      jResultado[ 'items', nFicha, 'bloodType' ] := 3 // B-
    ElseIf jResultado[ 'items', nFicha, 'bloodType' ] == '3' .And. jResultado[ 'items', nFicha, 'factor' ] == '1'
      jResultado[ 'items', nFicha, 'bloodType' ] := 4 // AB+
    ElseIf jResultado[ 'items', nFicha, 'bloodType' ] == '3' .And. jResultado[ 'items', nFicha, 'factor' ] == '2'
      jResultado[ 'items', nFicha, 'bloodType' ] := 5 // AB-
    ElseIf jResultado[ 'items', nFicha, 'bloodType' ] == '4' .And. jResultado[ 'items', nFicha, 'factor' ] == '1'
      jResultado[ 'items', nFicha, 'bloodType' ] := 6 // O+
    ElseIf jResultado[ 'items', nFicha, 'bloodType' ] == '4' .And. jResultado[ 'items', nFicha, 'factor' ] == '2'
      jResultado[ 'items', nFicha, 'bloodType' ] := 7 // O-
    Else
      jResultado[ 'items', nFicha, 'bloodType' ] := 8 // N/A
    EndIf

    If !Empty( jResultado[ 'items', nFicha, 'birthday' ] )

      jResultado[ 'items', nFicha, 'birthday' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nFicha, 'birthday' ] ),;
        '00:00:00.000';
      )

    EndIf

    If jResultado[ 'items', nFicha, 'gender' ] == '1'
      jResultado[ 'items', nFicha, 'gender' ] := 0
    ElseIf jResultado[ 'items', nFicha, 'gender' ] == '2'
      jResultado[ 'items', nFicha, 'gender' ] := 1
    Else
      jResultado[ 'items', nFicha, 'gender' ] := Nil
    EndIf

    If jResultado[ 'items', nFicha, 'smoker' ] == '1'
      jResultado[ 'items', nFicha, 'smoker' ] := .T.
    Else
      jResultado[ 'items', nFicha, 'smoker' ] := .F.
    EndIf

    If jResultado[ 'items', nFicha, 'eyeColor' ] == '1'
      jResultado[ 'items', nFicha, 'eyeColor' ] := 0 // Azul
    ElseIf jResultado[ 'items', nFicha, 'eyeColor' ] == '3'
      jResultado[ 'items', nFicha, 'eyeColor' ] := 1 // Castanho
    ElseIf jResultado[ 'items', nFicha, 'eyeColor' ] == '4'
      jResultado[ 'items', nFicha, 'eyeColor' ] := 2 // Preto
    ElseIf jResultado[ 'items', nFicha, 'eyeColor' ] == '5'
      jResultado[ 'items', nFicha, 'eyeColor' ] := 10 // Outro
    Else
      jResultado[ 'items', nFicha, 'eyeColor' ] := Nil
    EndIf

    If jResultado[ 'items', nFicha, 'skinColor' ] == '1'
      jResultado[ 'items', nFicha, 'skinColor' ] := 0 // Branco
    ElseIf jResultado[ 'items', nFicha, 'skinColor' ] == '2'
      jResultado[ 'items', nFicha, 'skinColor' ] := 1 // Preto
    ElseIf jResultado[ 'items', nFicha, 'skinColor' ] == '3'
      jResultado[ 'items', nFicha, 'skinColor' ] := 2 // Pardo
    ElseIf jResultado[ 'items', nFicha, 'skinColor' ] == '4'
      jResultado[ 'items', nFicha, 'skinColor' ] := 3 // Amarelo
    ElseIf jResultado[ 'items', nFicha, 'skinColor' ] == '5'
      jResultado[ 'items', nFicha, 'skinColor' ] := 4 // Indígena
    Else
      jResultado[ 'items', nFicha, 'skinColor' ] := Nil
    EndIf

    If jResultado[ 'items', nFicha, 'hairColor' ] == '1'
      jResultado[ 'items', nFicha, 'hairColor' ] := 0 // Loiro
    ElseIf jResultado[ 'items', nFicha, 'hairColor' ] == '2'
      jResultado[ 'items', nFicha, 'hairColor' ] := 2 // Castanho
    ElseIf jResultado[ 'items', nFicha, 'hairColor' ] == '3'
      jResultado[ 'items', nFicha, 'hairColor' ] := 2 // Castanho
    ElseIf jResultado[ 'items', nFicha, 'hairColor' ] == '4'
      jResultado[ 'items', nFicha, 'hairColor' ] := 2 // Castanho
    ElseIf jResultado[ 'items', nFicha, 'hairColor' ] == '5'
      jResultado[ 'items', nFicha, 'hairColor' ] := 3 // Preto
    ElseIf jResultado[ 'items', nFicha, 'hairColor' ] == '6'
      jResultado[ 'items', nFicha, 'hairColor' ] := 2 // Ruivo
    ElseIf jResultado[ 'items', nFicha, 'hairColor' ] == '7'
      jResultado[ 'items', nFicha, 'hairColor' ] := 4 // Cinza
    ElseIf jResultado[ 'items', nFicha, 'hairColor' ] == '8'
      jResultado[ 'items', nFicha, 'hairColor' ] := 5 // Branco
    ElseIf jResultado[ 'items', nFicha, 'hairColor' ] == '9'
      jResultado[ 'items', nFicha, 'hairColor' ] := 7 // Outro
    Else
      jResultado[ 'items', nFicha, 'hairColor' ] := Nil
    EndIf

    jResultado[ 'items', nFicha ]:delName( 'registration' )
    jResultado[ 'items', nFicha ]:delName( 'factor' )

    If Empty( jResultado[ 'items', nFicha, 'historic' ] )
      jResultado[ 'items', nFicha, 'historic' ] := Nil
    EndIf

    If Empty( jResultado[ 'items', nFicha, 'individualRegistration' ] )
      jResultado[ 'items', nFicha, 'individualRegistration' ] := Nil
    EndIf

    jResultado[ 'items', nFicha ]:DelName( 'erpId2' )

  Next nFicha

  jResultado[ 'totalAmount' ] := Len( jResultado[ 'items' ] ) + jResultado[ 'remainingRecords' ]
  jResultado[ 'amount' ] := Len( jResultado[ 'items' ] )

  If lRpc
    RpcClearEnv()
  EndIF

  oRest:setHeaderResponse( jHeaders )
  oRest:setResponse( jResultado )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fRisco
Retorna todos os riscos

@author Gabriel Sokacheski
@since 02/04/2024

/*/
//---------------------------------------------------------------------
@Get( '/api/mdt/v1/risks' )
Function fRisco()

  Local cEmp              as character
  Local cFil              as character
  Local cQuery            as character
  Local cWhere            as character
  Local cResultado        as character

  Local jHeaders          as json
  Local jResultado        as json
  Local jQueryParams      as json
  Local jResultadoCC      as json
  Local jResultadoDepto   as json
  Local jResultadoAgente  as json
  Local jResultadoFuncao  as json
  Local jResultadoTarefa  as json
  Local jResultadoMedAge  as json

  Local lRpc              as logical

  Local nRisco            as numeric
  Local nPagina           as numeric
  Local nTamanhoPagina    as numeric

  Local oQuery            as object

  jQueryParams := oRest:getQueryRequest()
  jHeaders := jsonObject():New()
  jHeaders[ 'Content-Type' ] := 'application/json'

  cEmp            := FWGrpCompany()
  cFil            := FWCodFil()
  nPagina         := jQueryParams[ 'page' ]; nPagina := Val( nPagina )
  nTamanhoPagina  := jQueryParams[ 'pageSize' ]; nTamanhoPagina := Val( nTamanhoPagina )

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TN0' ) + " TN0"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TMA' ) + " TMA ON"
  cQuery += CRLF + "TMA_AGENTE = TN0_AGENTE"
  cQuery += CRLF + "AND TMA_FILIAL = '" + FwXFilial( 'TMA' ) + "'"
  cQuery += CRLF + "AND TMA.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TN7' ) + " TN7 ON"
  cQuery += CRLF + "TN7_FONTE = TN0_FONTE"
  cQuery += CRLF + "AND TN7_FILIAL = '" + FwXFilial( 'TN7' ) + "'"
  cQuery += CRLF + "AND TN7.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "LEFT JOIN " + RetSqlName( 'TNE' ) + " TNE ON"
  cQuery += CRLF + "TNE_CODAMB = TN0_CODAMB"
  cQuery += CRLF + "AND TNE_FILIAL = '" + FwXFilial( 'TNE' ) + "'"
  cQuery += CRLF + "AND TNE.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "LEFT JOIN " + RetSqlName( 'CTT' ) + " CTT ON"
  cQuery += CRLF + "CTT_CUSTO = TN0_CC"
  cQuery += CRLF + "AND CTT_FILIAL = '" + FwXFilial( 'CTT' ) + "'"
  cQuery += CRLF + "AND CTT.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "LEFT JOIN " + RetSqlName( 'SQB' ) + " SQB ON"
  cQuery += CRLF + "QB_DEPTO = TN0_DEPTO"
  cQuery += CRLF + "AND QB_FILIAL = '" + FwXFilial( 'SQB' ) + "'"
  cQuery += CRLF + "AND SQB.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "LEFT JOIN " + RetSqlName( 'SRJ' ) + " SRJ ON"
  cQuery += CRLF + "RJ_FUNCAO = TN0_CODFUN"
  cQuery += CRLF + "AND RJ_FILIAL = '" + FwXFilial( 'SRJ' ) + "'"
  cQuery += CRLF + "AND SRJ.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "LEFT JOIN " + RetSqlName( 'TN5' ) + " TN5 ON"
  cQuery += CRLF + "TN5_CODTAR = TN0_CODTAR"
  cQuery += CRLF + "AND TN5_FILIAL = '" + FwXFilial( 'TN5' ) + "'"
  cQuery += CRLF + "AND TN5.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "LEFT JOIN " + RetSqlName( 'V3F' ) + " V3F ON"
  cQuery += CRLF + "V3F_CODIGO = TN0_UNIMED"
  cQuery += CRLF + "AND V3F_FILIAL = '" + FwXFilial( 'V3F' ) + "'"
  cQuery += CRLF + "AND V3F.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "TN0.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TN0_FILIAL = '" + FwXFilial( 'TN0' ) + "'"
  cWhere += CRLF + "AND TN0_DTRECO >= '" + DtoS( FirstYDate( YearSub( Date(), 20 ) ) ) + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'branchId'               , 'TN0_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'erpId'                  , 'TN0_NUMRIS'  , .T., .T. )
  oQuery:addMapFields( 'recognitionDate'        , 'TN0_DTRECO'  , .T., .T. )
  oQuery:addMapFields( 'evaluationDate'         , 'TN0_DTAVAL'  , .T., .T. )
  oQuery:addMapFields( 'eliminationDate'        , 'TN0_DTELIM'  , .T., .T. )
  oQuery:addMapFields( 'eSocialCode'            , 'TMA_ESOC'    , .T., .T. )
  oQuery:addMapFields( 'agentDescription'       , 'TMA_NOMAGE'  , .T., .T. )
  oQuery:addMapFields( 'group'                  , 'TMA_GRISCO'  , .T., .T. )
  oQuery:addMapFields( 'evaluation'             , 'TMA_AVALIA'  , .T., .T. )
  oQuery:addMapFields( 'generatingSource'       , 'TN7_NOMFON'  , .T., .T. )
  oQuery:addMapFields( 'environment'            , 'TNE_NOME'    , .T., .T. )
  oQuery:addMapFields( 'costCenterErpId'        , 'TN0_CC'      , .T., .T. )
  oQuery:addMapFields( 'costCenterBranch'       , 'CTT_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'costCenterDescription'  , 'CTT_DESC01'  , .T., .T. )
  oQuery:addMapFields( 'departmentErpId'        , 'TN0_DEPTO'   , .T., .T. )
  oQuery:addMapFields( 'departmentBranch'       , 'QB_FILIAL'   , .T., .T. )
  oQuery:addMapFields( 'departmentDescription'  , 'QB_DESCRIC'  , .T., .T. )
  oQuery:addMapFields( 'occupationErpId'        , 'TN0_CODFUN'  , .T., .T. )
  oQuery:addMapFields( 'occupationBranch'       , 'RJ_FILIAL'   , .T., .T. )
  oQuery:addMapFields( 'occupationDescription'  , 'RJ_DESC'     , .T., .T. )
  oQuery:addMapFields( 'taskErpId'              , 'TN0_CODTAR'  , .T., .T. )
  oQuery:addMapFields( 'taskBranch'             , 'TN5_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'taskDescription'        , 'TN5_NOMTAR'  , .T., .T. )
  oQuery:addMapFields( 'measurementValue'       , 'TN0_QTAGEN'  , .T., .T. )
  oQuery:addMapFields( 'measurementInstrument'  , 'V3F_DESCRI'  , .T., .T. )
  oQuery:addMapFields( 'training'               , 'TN0_NUMRIS'  , .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TN0_FILIAL, TN0_NUMRIS' )

  oQuery:SetPageSize( nTamanhoPagina )
  oQuery:setPage( nPagina )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nRisco := 1 To Len( jResultado[ 'items' ] )

    jResultado[ 'items', nRisco, 'companyId' ] := cEmp

    jResultado[ 'items', nRisco, 'person' ] := fBusExpRis(;
      jResultado[ 'items', nRisco, 'eliminationDate' ],;
      jResultado[ 'items', nRisco, 'evaluationDate' ],;
      jResultado[ 'items', nRisco, 'costCenterErpId' ] + Space( TamSX3( 'TN0_CC' )[ 1 ] - Len( jResultado[ 'items', nRisco, 'costCenterErpId' ] ) ),;
      jResultado[ 'items', nRisco, 'occupationErpId' ] + Space( TamSX3( 'TN0_CODFUN' )[ 1 ] - Len( jResultado[ 'items', nRisco, 'occupationErpId' ] ) ),;
      jResultado[ 'items', nRisco, 'departmentErpId' ] + Space( TamSX3( 'TN0_DEPTO' )[ 1 ] - Len( jResultado[ 'items', nRisco, 'departmentErpId' ] ) ),;
      jResultado[ 'items', nRisco, 'taskErpId' ] + Space( TamSX3( 'TN0_CODTAR' )[ 1 ] - Len( jResultado[ 'items', nRisco, 'taskErpId' ] ) );
    )

    jResultado[ 'items', nRisco, 'controlMeasure' ] := fBusMedRis( jResultado[ 'items', nRisco, 'erpId' ] )

    jResultado[ 'items', nRisco, 'ipe' ] := fBusEpiRis( jResultado[ 'items', nRisco, 'erpId' ] )

    jResultado[ 'items', nRisco, 'exam' ] := fBusExaRis( jResultado[ 'items', nRisco, 'erpId' ] )

    //-------------------------------------
    // Tratamento das informações do risco
    //-------------------------------------

    jResultado[ 'items', nRisco, 'erpId' ] := cEmp + '|' + jResultado[ 'items', nRisco, 'branchId' ] + '|' + ;
    jResultado[ 'items', nRisco, 'erpId' ]

    If !Empty( jResultado[ 'items', nRisco, 'recognitionDate' ] )

      jResultado[ 'items', nRisco, 'recognitionDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nRisco, 'recognitionDate' ] ),;
        '00:00:00.000';
      )

    EndIf

    If !Empty( jResultado[ 'items', nRisco, 'eliminationDate' ] )

      jResultado[ 'items', nRisco, 'eliminationDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nRisco, 'eliminationDate' ] ),;
        '00:00:00.000';
      )

    EndIf

    If !Empty( jResultado[ 'items', nRisco, 'evaluationDate' ] )

      jResultado[ 'items', nRisco, 'evaluationDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nRisco, 'evaluationDate' ] ),;
        '00:00:00.000';
      )

    EndIf

    Do Case
      Case jResultado[ 'items', nRisco, 'group' ] == '1'
        jResultado[ 'items', nRisco, 'group' ] := 0 // Físico
      Case jResultado[ 'items', nRisco, 'group' ] == '2'
        jResultado[ 'items', nRisco, 'group' ] := 1 // Químico
      Case jResultado[ 'items', nRisco, 'group' ] == '3'
        jResultado[ 'items', nRisco, 'group' ] := 2 // Biológico
      Case jResultado[ 'items', nRisco, 'group' ] == '4'
        jResultado[ 'items', nRisco, 'group' ] := 3 // Ergonômico
      Case jResultado[ 'items', nRisco, 'group' ] $ '5/6'
        jResultado[ 'items', nRisco, 'group' ] := 4 // Mecânico ou Acidente
      Case jResultado[ 'items', nRisco, 'group' ] == '7'
        jResultado[ 'items', nRisco, 'group' ] := 5 // Perigoso      
    EndCase

    jResultadoAgente := jsonObject():new()
    jResultadoAgente[ 'eSocialCode' ] := jResultado[ 'items', nRisco, 'eSocialCode' ]
    jResultadoAgente[ 'description' ] := jResultado[ 'items', nRisco, 'agentDescription' ]
    jResultadoAgente[ 'group' ] := jResultado[ 'items', nRisco, 'group' ]

    Do Case
      Case jResultado[ 'items', nRisco, 'evaluation' ] == '1'
        jResultadoAgente[ 'evaluation' ] := 0 // Quantitativa
      Case jResultado[ 'items', nRisco, 'evaluation' ] == '2'
        jResultadoAgente[ 'evaluation' ] := 1 // Qualitativa
      Otherwise
        jResultadoAgente[ 'evaluation' ] := 1
    EndCase

    jResultado[ 'items', nRisco, 'riskAgent' ] := jResultadoAgente

    FreeObj( jResultadoAgente )

    If jResultado[ 'items', nRisco, 'costCenterErpId' ] != '*'

      jResultado[ 'items', nRisco, 'costCenterErpId' ] := cEmp + '|' + jResultado[ 'items', nRisco, 'costCenterBranch' ] + '|' + ;
      jResultado[ 'items', nRisco, 'costCenterErpId' ]

      jResultadoCC := jsonObject():new()
      jResultadoCC[ 'erpId' ] := jResultado[ 'items', nRisco, 'costCenterErpId' ]
      jResultadoCC[ 'description' ] := jResultado[ 'items', nRisco, 'costCenterDescription' ]

      jResultado[ 'items', nRisco, 'costCenter' ] := { jResultadoCC }

      FreeObj( jResultadoCC )

    Else

      jResultado[ 'items', nRisco, 'costCenter' ] := {}

    EndIf

    If jResultado[ 'items', nRisco, 'departmentErpId' ] != '*'

      jResultado[ 'items', nRisco, 'departmentErpId' ] := cEmp + '|' + jResultado[ 'items', nRisco, 'departmentBranch' ] + '|' + ;
      jResultado[ 'items', nRisco, 'departmentErpId' ]

      jResultadoDepto := jsonObject():new()
      jResultadoDepto[ 'erpId' ] := jResultado[ 'items', nRisco, 'departmentErpId' ]
      jResultadoDepto[ 'description' ] := jResultado[ 'items', nRisco, 'departmentDescription' ]

      jResultado[ 'items', nRisco, 'department' ] := { jResultadoDepto }

      FreeObj( jResultadoDepto )

    Else

      jResultado[ 'items', nRisco, 'department' ] := {}

    EndIf

    If jResultado[ 'items', nRisco, 'occupationErpId' ] != '*'

      jResultado[ 'items', nRisco, 'occupationErpId' ] := cEmp + '|' + jResultado[ 'items', nRisco, 'occupationBranch' ] + '|' + ;
      jResultado[ 'items', nRisco, 'occupationErpId' ]

      jResultadoFuncao := jsonObject():new()
      jResultadoFuncao[ 'erpId' ] := jResultado[ 'items', nRisco, 'occupationErpId' ]
      jResultadoFuncao[ 'description' ] := jResultado[ 'items', nRisco, 'occupationDescription' ]

      jResultado[ 'items', nRisco, 'occupation' ] := { jResultadoFuncao }

      FreeObj( jResultadoFuncao )

    Else

      jResultado[ 'items', nRisco, 'occupation' ] := {}

    EndIf

    If jResultado[ 'items', nRisco, 'taskErpId' ] != '*'

      jResultado[ 'items', nRisco, 'taskErpId' ] := cEmp + '|' + jResultado[ 'items', nRisco, 'taskBranch' ] + '|' + ;
      jResultado[ 'items', nRisco, 'taskErpId' ]

      jResultadoTarefa := jsonObject():new()
      jResultadoTarefa[ 'erpId' ] := jResultado[ 'items', nRisco, 'taskErpId' ]
      jResultadoTarefa[ 'description' ] := jResultado[ 'items', nRisco, 'taskDescription' ]

      jResultado[ 'items', nRisco, 'task' ] := { jResultadoTarefa }

      FreeObj( jResultadoTarefa )

    Else

      jResultado[ 'items', nRisco, 'task' ] := {}

    EndIf

    If !Empty( jResultado[ 'items', nRisco, 'measurementInstrument' ] )
      jResultado[ 'items', nRisco, 'measurementInstrument' ] := AllTrim( SubStr(;
        jResultado[ 'items', nRisco, 'measurementInstrument' ],;
        At( '-', jResultado[ 'items', nRisco, 'measurementInstrument' ] ) + 1,;
        Len( jResultado[ 'items', nRisco, 'measurementInstrument' ] );
      ) )
    EndIf

    If !Empty( jResultado[ 'items', nRisco, 'measurementInstrument' ] )

      jResultadoMedAge := jsonObject():new()
      jResultadoMedAge[ 'value' ] := jResultado[ 'items', nRisco, 'measurementValue' ]
      jResultadoMedAge[ 'instrument' ] := jResultado[ 'items', nRisco, 'measurementInstrument' ]

      jResultado[ 'items', nRisco, 'measurement' ] := { jResultadoMedAge }

      FreeObj( jResultadoMedAge )

    Else

      jResultado[ 'items', nRisco, 'measurement' ] := {}

    EndIf

    //------------------------------------
    // Deleta as propriedades não pedidas
    //------------------------------------
    jResultado[ 'items', nRisco ]:delName( 'evaluationDate' )
    jResultado[ 'items', nRisco ]:delName( 'eSocialCode' )
    jResultado[ 'items', nRisco ]:delName( 'agentDescription' )
    jResultado[ 'items', nRisco ]:delName( 'group' )
    jResultado[ 'items', nRisco ]:delName( 'costCenterErpId' )
    jResultado[ 'items', nRisco ]:delName( 'costCenterBranch' )
    jResultado[ 'items', nRisco ]:delName( 'costCenterDescription' )
    jResultado[ 'items', nRisco ]:delName( 'departmentErpId' )
    jResultado[ 'items', nRisco ]:delName( 'departmentBranch' )
    jResultado[ 'items', nRisco ]:delName( 'departmentDescription' )
    jResultado[ 'items', nRisco ]:delName( 'occupationErpId' )
    jResultado[ 'items', nRisco ]:delName( 'occupationBranch' )
    jResultado[ 'items', nRisco ]:delName( 'occupationDescription' )
    jResultado[ 'items', nRisco ]:delName( 'taskErpId' )
    jResultado[ 'items', nRisco ]:delName( 'taskBranch' )
    jResultado[ 'items', nRisco ]:delName( 'taskDescription' )
    jResultado[ 'items', nRisco ]:delName( 'measurementValue' )
    jResultado[ 'items', nRisco ]:delName( 'measurementInstrument' )
    jResultado[ 'items', nRisco ]:delName( 'evaluation' )

    If Empty( jResultado[ 'items', nRisco, 'eliminationDate' ] )
      jResultado[ 'items', nRisco, 'eliminationDate' ] := Nil
    EndIf

    If Empty( jResultado[ 'items', nRisco, 'environment' ] )
      jResultado[ 'items', nRisco, 'environment' ] := Nil
    EndIf

    If Empty( jResultado[ 'items', nRisco, 'person' ] )
      jResultado[ 'items', nRisco, 'person' ] := {}
    EndIf

    If Empty( jResultado[ 'items', nRisco, 'controlMeasure' ] )
      jResultado[ 'items', nRisco, 'controlMeasure' ] := {}
    EndIf

    If Empty( jResultado[ 'items', nRisco, 'ipe' ] )
      jResultado[ 'items', nRisco, 'ipe' ] := {}
    EndIf

    If Empty( jResultado[ 'items', nRisco, 'exam' ] )
      jResultado[ 'items', nRisco, 'exam' ] := {}
    EndIf

    If Empty( jResultado[ 'items', nRisco, 'measurement' ] )
      jResultado[ 'items', nRisco, 'measurement' ] := {}
    EndIf

    jResultado[ 'items', nRisco, 'training' ] := {}

  Next nRisco

  jResultado[ 'totalAmount' ] := Len( jResultado[ 'items' ] ) + jResultado[ 'remainingRecords' ]
  jResultado[ 'amount' ] := Len( jResultado[ 'items' ] )

  If lRpc
    RpcClearEnv()
  EndIF

  oRest:setHeaderResponse( jHeaders )
  oRest:setResponse( jResultado )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fBusEpiRis
Busca os epis necessários ao risco

@author Gabriel Sokacheski
@since 08/04/2024

@param, cRisco, número do risco

@return aEpi, contém o C.A. dos epis
/*/
//---------------------------------------------------------------------
Static Function fBusEpiRis( cRisco )

  Local aEpi as array

  Local cEpi as character

  cEpi := GetNextAlias()

  BeginSQL Alias cEpi

    SELECT 
      DISTINCT TN3.TN3_NUMCAP, TL0.TL0_NUMCAP
    FROM 
      %table:TNX% TNX
        INNER JOIN %table:TN3% TN3 ON
          TNX.TNX_EPI = TN3.TN3_CODEPI
          AND %xFilial:TN3% = TN3.TN3_FILIAL
          AND TN3.%NotDel%
        LEFT JOIN %table:TL0% TL0 ON
          TNX.TNX_EPI = TL0.TL0_EPIGEN
          AND %xFilial:TL0% = TL0.TL0_FILIAL
          AND TL0.%NotDel%
    WHERE
      TNX.TNX_FILIAL = %xFilial:TNX%
      AND TNX.TNX_NUMRIS = %exp:cRisco%
      AND TNX.%NotDel%

  EndSQL

  dbSelectArea( cEpi )
  ( cEpi )->( dbGoTop() )

  aEpi := {}

  While ( cEpi )->( !Eof() )

    If !Empty( ( cEpi )->( TN3_NUMCAP ) )
      aAdd( aEpi, AllTrim( ( cEpi )->( TN3_NUMCAP ) ) )
    ElseIf !Empty( ( cEpi )->( TL0_NUMCAP ) )
      aAdd( aEpi, AllTrim( ( cEpi )->( TL0_NUMCAP ) ) )
    EndIf

    ( cEpi )->( dbSkip() )

  End

  ( cEpi )->( dbCloseArea() )

Return aEpi

//---------------------------------------------------------------------
/*/{Protheus.doc} fBusMedRis
Busca as medidas de controle do risco

@author Gabriel Sokacheski
@since 08/04/2024

@param, cRisco, número do risco

@return jMedida, lista das medidas
/*/
//---------------------------------------------------------------------
Static Function fBusMedRis( cRisco )

  Local cQuery  as character
  Local cWhere  as character
  Local cMedida as character

  Local jMedida as json

  Local nMedida as numeric

  Local oQuery  as object

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TJF' ) + " TJF"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TO4' ) + " TO4 ON"
  cQuery += CRLF + "TO4_CONTRO = TJF_MEDCON"
  cQuery += CRLF + "AND TO4_FILIAL = '" + FwXFilial( 'TO4' ) + "'"
  cQuery += CRLF + "AND TO4.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "TJF.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TJF_FILIAL = '" + FwXFilial( 'TJF' ) + "'"
  cWhere += CRLF + "AND TJF_NUMRIS = '" + cRisco + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'description'  , 'TO4_NOMCTR'  , .T., .T. )
  oQuery:addMapFields( 'type'         , 'TO4_TIPCTR'  , .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TJF_NUMRIS' )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cMedida := oQuery:getJsonResponse()
  endif

  jMedida := jsonObject():new()
  jMedida:fromJson( cMedida )

  For nMedida := 1 To Len( jMedida[ 'items' ] )

    Do Case
      Case jMedida[ 'items', nMedida, 'type' ] == '1'
        jMedida[ 'items', nMedida, 'type' ] := 0 // Proteção Individual
      Case jMedida[ 'items', nMedida, 'type' ] == '2'
        jMedida[ 'items', nMedida, 'type' ] := 1 // Proteção Coletiva
      Case jMedida[ 'items', nMedida, 'type' ] == '3'
        jMedida[ 'items', nMedida, 'type' ] := 2 // Monitoramento Biológico
      Case jMedida[ 'items', nMedida, 'type' ] == '4'
        jMedida[ 'items', nMedida, 'type' ] := 3 // Medidas Administrativas
      Case jMedida[ 'items', nMedida, 'type' ] == '5'
        jMedida[ 'items', nMedida, 'type' ] := 4 // Engenharia
    EndCase

  Next nMedida

Return jMedida[ 'items' ]

//---------------------------------------------------------------------
/*/{Protheus.doc} fBusExpRis
Busca as medidas de controle do risco

@author Gabriel Sokacheski
@since 08/04/2024

@param dEliminacao, eliminação do risco
@param dAvaliacao, avaliação do risco
@param cCentroCusto, centro de custo do risco
@param cFuncao, função do risco
@param cDepartamento, departamento do risco
@param cTarefa, tarefa do risco

@return jExposto, funcionários expostos ao risco
/*/
//---------------------------------------------------------------------
Static Function fBusExpRis( dEliminacao, dAvaliacao, cCentroCusto, cFuncao, cDepartamento, cTarefa )

  Local cQuery  as character
  Local cWhere  as character
  Local cExposto as character

  Local jExposto as json

  Local nExposto as numeric

  Local oQuery  as object

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'SRA' ) + " SRA"

  cQuery += CRLF + "LEFT JOIN " + RetSqlName( 'TN6' ) + " TN6 ON"
  cQuery += CRLF + "TN6_MAT = RA_MAT"
  cQuery += CRLF + "AND TN6_FILIAL = '" + FwXFilial( 'TN6' ) + "'"
  cQuery += CRLF + "AND TN6.D_E_L_E_T_ != '*'"
  cQuery += CRLF + "AND ("
  cQuery += CRLF + "( TN6_DTTERM = '" + Space( TamSX3( 'TN6_DTTERM' )[ 1 ] ) + "' AND '" + DtoS( fDatas( dEliminacao ) ) + "' = '" + Space( TamSX3( 'TN0_DTELIM' )[ 1 ] ) + "' )"
  cQuery += CRLF + "OR ("
  cQuery += CRLF + "TN6_DTTERM != '" + Space( TamSX3( 'TN6_DTTERM' )[ 1 ] ) + "' AND '" + DtoS( fDatas( dEliminacao ) ) + "' != '" + Space( TamSX3( 'TN0_DTELIM' )[ 1 ] ) + "'"
  cQuery += CRLF + "AND NOT("
  cQuery += CRLF + "( TN6_DTINIC < '" + DtoS( fDatas( dAvaliacao ) ) + "' AND TN6_DTTERM < '" + DtoS( fDatas( dAvaliacao ) ) + "' )"
  cQuery += CRLF + "OR ( TN6_DTINIC > '" + DtoS( fDatas( dEliminacao ) ) + "' AND TN6_DTTERM > '" + DtoS( fDatas( dEliminacao ) ) + "' )"
  cQuery += CRLF + ")"
  cQuery += CRLF + ")"
  cQuery += CRLF + "OR ("
  cQuery += CRLF + "TN6_DTTERM != '" + Space( TamSX3( 'TN6_DTTERM' )[ 1 ] ) + "'"
  cQuery += CRLF + "AND '" + DtoS( fDatas( dEliminacao ) ) + "' = '" + Space( TamSX3( 'TN0_DTELIM' )[ 1 ] ) + "'"
  cQuery += CRLF + "AND NOT ( TN6_DTINIC < '" + DtoS( fDatas( dAvaliacao ) ) + "' AND TN6_DTTERM < '" + DtoS( fDatas( dAvaliacao ) ) + "' )"
  cQuery += CRLF + ")"
  cQuery += CRLF + "OR ("
  cQuery += CRLF + "TN6_DTTERM = '" + Space( TamSX3( 'TN6_DTTERM' )[ 1 ] ) + "'"
  cQuery += CRLF + "AND '" + DtoS( fDatas( dEliminacao ) ) + "' != '" + Space( TamSX3( 'TN0_DTELIM' )[ 1 ] ) + "'"
  cQuery += CRLF + "AND NOT ( '" + DtoS( fDatas( dAvaliacao ) ) + "' < TN6_DTINIC AND '" + DtoS( fDatas( dAvaliacao ) ) + "' < TN6_DTINIC )"
  cQuery += CRLF + ")"
  cQuery += CRLF + ")"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "SRA.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND RA_FILIAL = '" + FwXFilial( 'SRA' ) + "'"
  cWhere += CRLF + "AND ( RA_ADMISSA <= '" + DtoS( fDatas( dEliminacao ) ) + "' OR RA_DEMISSA >= '" + DtoS( fDatas( dEliminacao ) ) + "' )"
  cWhere += CRLF + "AND ( TN6_CODTAR = '" + cTarefa + "' OR '*" + Space( TamSX3( 'TN6_CODTAR' )[ 1 ] - 1 ) + "' = '" + cTarefa + "' )"
  cWhere += CRLF + "AND ( RA_DEPTO = '" + cDepartamento + "' OR '*" + Space( TamSX3( 'RA_DEPTO' )[ 1 ] - 1 ) + "' = '" + cDepartamento + "' )"
  cWhere += CRLF + "AND ( RA_CODFUNC = '" + cFuncao + "' OR '*" + Space( TamSX3( 'RA_CODFUNC' )[ 1 ] - 1 ) + "' = '" + cFuncao + "' )"
  cWhere += CRLF + "AND ( RA_CC = '" + cCentroCusto + "' OR '*" + Space( TamSX3( 'RA_CC' )[ 1 ] - 1 ) + "' = '" + cCentroCusto + "' )"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'erpId'                  , 'RA_MAT'      , .T., .T. )
  oQuery:addMapFields( 'erpId2'                 , 'RA_FILIAL'   , .T., .T. )
  oQuery:addMapFields( 'name'                   , 'RA_NOME'     , .T., .T. )
  oQuery:addMapFields( 'individualRegistration' , 'RA_CIC'      , .T., .T. )
  oQuery:addMapFields( 'birthday'               , 'RA_NASC'     , .T., .T. )
  oQuery:addMapFields( 'gender'                 , 'RA_SEXO'     , .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'RA_NOME' )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cExposto := oQuery:getJsonResponse()
  endif

  jExposto := jsonObject():new()
  jExposto:fromJson( cExposto )

  For nExposto := 1 To Len( jExposto[ 'items' ] )

    jExposto[ 'items', nExposto, 'erpId' ] := FWGrpCompany() + '|' + jExposto[ 'items', nExposto, 'erpId2' ] + '|' +;
    jExposto[ 'items', nExposto, 'erpId' ]

    Do Case
      Case jExposto[ 'items', nExposto, 'gender' ] == 'M'
        jExposto[ 'items', nExposto, 'gender' ] := 0 // Masculino
      Case jExposto[ 'items', nExposto, 'gender' ] == 'F'
        jExposto[ 'items', nExposto, 'gender' ] := 1 // Feminino
      Otherwise
        jExposto[ 'items', nExposto, 'gender' ] := Nil
    EndCase

    If !Empty( jExposto[ 'items', nExposto, 'birthday' ] )

      jExposto[ 'items', nExposto, 'birthday' ] := FWTimeStamp(;
        5,;
        fDatas( jExposto[ 'items', nExposto, 'birthday' ] ),;
        '00:00:00.000';
      )

    EndIf

    If Empty( jExposto[ 'items', nExposto, 'individualRegistration' ] )
      jExposto[ 'items', nExposto, 'individualRegistration' ] := Nil
    EndIf

    jExposto[ 'items', nExposto ]:DelName( 'erpId2' )

  Next nExposto

Return jExposto[ 'items' ]

//---------------------------------------------------------------------
/*/{Protheus.doc} fBusExaRis
Busca os exames do risco

@author Gabriel Sokacheski
@since 08/04/2024

@param, cRisco, número do risco

@return jExame, lista das medidas
/*/
//---------------------------------------------------------------------
Static Function fBusExaRis( cRisco )

  Local cQuery          as character
  Local cWhere          as character
  Local cExame          as character

  Local jExame          as json
  Local jPeriodicidade  as json

  Local nExame          as numeric

  Local oQuery          as object

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TN2' ) + " TN2"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TM4' ) + " TM4 ON"
  cQuery += CRLF + "TM4_EXAME = TN2_EXAME"
  cQuery += CRLF + "AND TM4_FILIAL = '" + FwXFilial( 'TM4' ) + "'"
  cQuery += CRLF + "AND TM4.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TMQ' ) + " TMQ ON"
  cQuery += CRLF + "TMQ_FAIXA = TN2_FAIXA"
  cQuery += CRLF + "AND TMQ_FILIAL = '" + FwXFilial( 'TMQ' ) + "'"
  cQuery += CRLF + "AND TMQ.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "TN2.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TN2_FILIAL = '" + FwXFilial( 'TN2' ) + "'"
  cWhere += CRLF + "AND TN2_NUMRIS = '" + cRisco + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'description'      , 'TM4_NOMEXA'  , .T., .T. )
  oQuery:addMapFields( 'type'             , 'TM4_INDRES'  , .T., .T. )
  oQuery:addMapFields( 'admission'        , 'TN2_TIPOEX'  , .T., .T. )
  oQuery:addMapFields( 'periodic'         , 'TN2_TIPOEX'  , .T., .T. )
  oQuery:addMapFields( 'backToWork'       , 'TN2_TIPOEX'  , .T., .T. )
  oQuery:addMapFields( 'dismissal'        , 'TN2_TIPOEX'  , .T., .T. )
  oQuery:addMapFields( 'changeOfFunction' , 'TN2_TIPOEX'  , .T., .T. )
  oQuery:addMapFields( 'fromAge'          , 'TMQ_1DE'     , .T., .T. )
  oQuery:addMapFields( 'toAge'            , 'TMQ_1ATE'    , .T., .T. )
  oQuery:addMapFields( 'periodicity'      , 'TMQ_1QTMES'  , .T., .T. )
  oQuery:addMapFields( 'postAdmission'    , 'TMQ_POSADM'  , .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TM4_NOMEXA' )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cExame := oQuery:getJsonResponse()
  endif

  jExame := jsonObject():new()
  jExame:fromJson( cExame )

  For nExame := 1 To Len( jExame[ 'items' ] )

    Do Case
      Case jExame[ 'items', nExame, 'type' ] == '1'
        jExame[ 'items', nExame, 'type' ] := 0 // Normal
      Case jExame[ 'items', nExame, 'type' ] == '2'
        jExame[ 'items', nExame, 'type' ] := 1 // IBMP
      Case jExame[ 'items', nExame, 'type' ] == '3'
        jExame[ 'items', nExame, 'type' ] := 2 // Itens de exame
      Case jExame[ 'items', nExame, 'type' ] == '4'
        jExame[ 'items', nExame, 'type' ] := 3 // Audiométrico
      Otherwise
        jExame[ 'items', nExame, 'type' ] := Nil
    EndCase

    Do Case
      Case jExame[ 'items', nExame, 'admission' ] == '1'
        jExame[ 'items', nExame, 'admission' ] := .T.
      Case jExame[ 'items', nExame, 'admission' ] == '6'
        jExame[ 'items', nExame, 'admission' ] := .T.
      Case jExame[ 'items', nExame, 'admission' ] == '8'
        jExame[ 'items', nExame, 'admission' ] := .T.
      Case jExame[ 'items', nExame, 'admission' ] == '9'
        jExame[ 'items', nExame, 'admission' ] := .T.
      Case Len( AllTrim( jExame[ 'items', nExame, 'admission' ] ) ) == 2
        jExame[ 'items', nExame, 'admission' ] := .T.
      Case !Empty( jExame[ 'items', nExame, 'admission' ] )
        jExame[ 'items', nExame, 'admission' ] := .F.
      Otherwise
        jExame[ 'items', nExame, 'admission' ] := .F.
    EndCase

    Do Case
      Case jExame[ 'items', nExame, 'periodic' ] == '10'
        jExame[ 'items', nExame, 'periodic' ] := .F.
      Case jExame[ 'items', nExame, 'periodic' ] == '15'
        jExame[ 'items', nExame, 'periodic' ] := .F.
      Case jExame[ 'items', nExame, 'periodic' ] == '16'
        jExame[ 'items', nExame, 'periodic' ] := .F.
      Case jExame[ 'items', nExame, 'periodic' ] == '2'
        jExame[ 'items', nExame, 'periodic' ] := .T.
      Case jExame[ 'items', nExame, 'periodic' ] == '6'
        jExame[ 'items', nExame, 'periodic' ] := .T.
      Case jExame[ 'items', nExame, 'periodic' ] == '7'
        jExame[ 'items', nExame, 'periodic' ] := .T.
      Case jExame[ 'items', nExame, 'periodic' ] == '8'
        jExame[ 'items', nExame, 'periodic' ] := .T.
      Case !Empty( jExame[ 'items', nExame, 'periodic' ] )
        jExame[ 'items', nExame, 'periodic' ] := .F.
      Otherwise
        jExame[ 'items', nExame, 'periodic' ] := .F.
    EndCase

    Do Case
      Case jExame[ 'items', nExame, 'backToWork' ] == '4'
        jExame[ 'items', nExame, 'backToWork' ] := .T.
      Case jExame[ 'items', nExame, 'backToWork' ] == '12'
        jExame[ 'items', nExame, 'backToWork' ] := .T.
      Case jExame[ 'items', nExame, 'backToWork' ] == '16'
        jExame[ 'items', nExame, 'backToWork' ] := .T.
      Case !Empty( jExame[ 'items', nExame, 'backToWork' ] )
        jExame[ 'items', nExame, 'backToWork' ] := .F.
      Otherwise
        jExame[ 'items', nExame, 'backToWork' ] := .F.
    EndCase

    Do Case
      Case jExame[ 'items', nExame, 'dismissal' ] == '5'
        jExame[ 'items', nExame, 'dismissal' ] := .T.
      Case jExame[ 'items', nExame, 'dismissal' ] == '7'
        jExame[ 'items', nExame, 'dismissal' ] := .T.
      Case jExame[ 'items', nExame, 'dismissal' ] == '8'
        jExame[ 'items', nExame, 'dismissal' ] := .T.
      Case jExame[ 'items', nExame, 'dismissal' ] == '9'
        jExame[ 'items', nExame, 'dismissal' ] := .T.
      Case jExame[ 'items', nExame, 'dismissal' ] == '11'
        jExame[ 'items', nExame, 'dismissal' ] := .T.
      Case jExame[ 'items', nExame, 'dismissal' ] == '14'
        jExame[ 'items', nExame, 'dismissal' ] := .T.
      Case jExame[ 'items', nExame, 'dismissal' ] == '15'
        jExame[ 'items', nExame, 'dismissal' ] := .T.
      Case !Empty( jExame[ 'items', nExame, 'dismissal' ] )
        jExame[ 'items', nExame, 'dismissal' ] := .F.
      Otherwise
        jExame[ 'items', nExame, 'dismissal' ] := .F.
    EndCase

    Do Case
      Case jExame[ 'items', nExame, 'changeOfFunction' ] == '3'
        jExame[ 'items', nExame, 'changeOfFunction' ] := .T.
      Case Len( AllTrim( jExame[ 'items', nExame, 'changeOfFunction' ] ) ) == 2
        jExame[ 'items', nExame, 'changeOfFunction' ] := .T.
      Case !Empty( jExame[ 'items', nExame, 'changeOfFunction' ] )
        jExame[ 'items', nExame, 'changeOfFunction' ] := .F.
      Otherwise
        jExame[ 'items', nExame, 'changeOfFunction' ] := .F.
    EndCase

    jPeriodicidade := jsonObject():new()
    jPeriodicidade[ 'fromAge' ] := jExame[ 'items', nExame, 'fromAge' ]
    jPeriodicidade[ 'toAge' ] := jExame[ 'items', nExame, 'toAge' ]
    jPeriodicidade[ 'periodicity' ] := jExame[ 'items', nExame, 'periodicity' ]
    jPeriodicidade[ 'postAdmission' ] := jExame[ 'items', nExame, 'postAdmission' ]

    jExame[ 'items', nExame, 'periodicity' ] := jPeriodicidade

    FreeObj( jPeriodicidade )

    jExame[ 'items', nExame ]:DelName( 'fromAge' )
    jExame[ 'items', nExame ]:DelName( 'toAge' )
    jExame[ 'items', nExame ]:DelName( 'postAdmission' )
    jExame[ 'items', nExame ]:DelName( 'periodicity' )

  Next nExame

Return jExame[ 'items' ]

//---------------------------------------------------------------------
/*/{Protheus.doc} fQuestionario
API de questionários

@author Gabriel Sokacheski
@since 11/04/2024

/*/
//---------------------------------------------------------------------
@Get( '/api/mdt/v1/quizzes' )
Function fQuestionario()

  Local cEmp            as character
  Local cFil            as character
  Local cQuery          as character
  Local cWhere          as character
  Local cResultado      as character

  Local jHeaders        as json
  Local jResultado      as json
  Local jQueryParams    as json

  Local lRpc            as logical

  Local nPagina         as numeric
  Local nQuestionario   as numeric
  Local nTamanhoPagina  as numeric

  Local oQuery          as object

  jQueryParams := oRest:getQueryRequest()
  jHeaders := jsonObject():New()
  jHeaders[ 'Content-Type' ] := 'application/json'

  cEmp            := FWGrpCompany()
  cFil            := FWCodFil()
  nPagina         := jQueryParams[ 'page' ]; nPagina := Val( nPagina )
  nTamanhoPagina  := jQueryParams[ 'pageSize' ]; nTamanhoPagina := Val( nTamanhoPagina )

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TMG' ) + " TMG"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cQuery += CRLF + "AND EXISTS ( SELECT 1 FROM " + RetSqlName( 'TMH' ) + " TMH where TMH.TMH_QUESTI = TMG.TMG_QUESTI AND TMH.D_E_L_E_T_ != '*' )"

  cWhere := "TMG.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TMG_FILIAL = '" + FwXFilial( 'TMG' ) + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'branchId'     , 'TMG_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'erpId'        , 'TMG_QUESTI'  , .T., .T. )
  oQuery:addMapFields( 'description'  , 'TMG_NOMQUE'  , .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TMG_QUESTI' )

  oQuery:SetPageSize( nTamanhoPagina )
  oQuery:setPage( nPagina )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nQuestionario := 1 To Len( jResultado[ 'items' ] )

    jResultado[ 'items', nQuestionario, 'companyId' ] := cEmp

    jResultado[ 'items', nQuestionario, 'questionsGroup' ] := fBusPerQue(;
      jResultado[ 'items', nQuestionario, 'erpId' ] + Space( TamSX3( 'TMG_QUESTI' )[ 1 ] - Len( jResultado[ 'items', nQuestionario, 'erpId' ] ) );
    )

    jResultado[ 'items', nQuestionario, 'erpId' ] := cEmp + '|' + jResultado[ 'items', nQuestionario, 'branchId' ] + '|' + ;
    jResultado[ 'items', nQuestionario, 'erpId' ]

  Next nQuestionario

  jResultado[ 'totalAmount' ] := Len( jResultado[ 'items' ] ) + jResultado[ 'remainingRecords' ]
  jResultado[ 'amount' ] := Len( jResultado[ 'items' ] )

  If lRpc
    RpcClearEnv()
  EndIF

  oRest:setHeaderResponse( jHeaders )
  oRest:setResponse( jResultado )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fBusPerQue
Busca as perguntas do questionário

@author Gabriel Sokacheski
@since 11/04/2024

@cQuestionario, código do questionário

@return jResultado, lista de perguntas
/*/
//---------------------------------------------------------------------
Static Function fBusPerQue( cQuestionario )

  Local aOpcao         as array

  Local cQuery          as character
  Local cWhere          as character
  Local cOpcao          as character
  Local cResultado      as character

  Local jResultado      as json

  Local nPosicao        as numeric
  Local nPergunta       as numeric

  Local oQuery          as object

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TMH' ) + " TMH"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "TMH.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TMH_FILIAL = '" + FwXFilial( 'TMH' ) + "'"
  cWhere += CRLF + "AND TMH_QUESTI = '" + cQuestionario + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'question' , 'TMH_PERGUN'  , .T., .T. )
  oQuery:addMapFields( 'answers'  , 'TMH_RESPOS'  , .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TMH_PERGUN' )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nPergunta := 1 To Len( jResultado[ 'items' ] )

    aOpcao := {}
    cOpcao  := jResultado[ 'items', nPergunta, 'answers' ]
    nOpcao  := 0

    While ( nPosicao := At( '=', jResultado[ 'items', nPergunta, 'answers' ] ) ) > 0

      nOpcao += 1
      cOpcao := SubStr( jResultado[ 'items', nPergunta, 'answers' ], nPosicao + 1 )

      If At( ';', cOpcao ) > 0
        cOpcao := SubStr( cOpcao, 1, At( ';', cOpcao ) - 1 )
        jResultado[ 'items', nPergunta, 'answers' ] := SubStr( jResultado[ 'items', nPergunta, 'answers' ], At( ';', jResultado[ 'items', nPergunta, 'answers' ] ) + 1 )
      Else
        jResultado[ 'items', nPergunta, 'answers' ] := ''
      EndIf
 
      aAdd( aOpcao, jsonObject():new() )
      aOpcao[ nOpcao ][ 'description' ] := cOpcao

    End

    jResultado[ 'items', nPergunta, 'answers' ] := aOpcao

    If Empty( jResultado[ 'items', nPergunta, 'answers' ] )
      jResultado[ 'items', nPergunta, 'answers' ] := {}
    EndIf

  Next nPergunta

Return jResultado[ 'items' ]

//---------------------------------------------------------------------
/*/{Protheus.doc} fRespostas
API de respostas dos questionários

@author Gabriel Sokacheski
@since 12/04/2024

/*/
//---------------------------------------------------------------------
@Get( '/api/mdt/v1/clinicalquizzes' )
Function fRespostas()

  Local cEmp            as character
  Local cFil            as character
  Local cQuery          as character
  Local cWhere          as character
  Local cResultado      as character

  Local jHeaders        as json
  Local jResultado      as json
  Local jQueryParams    as json

  Local lRpc            as logical

  Local nPagina         as numeric
  Local nQuestionario   as numeric
  Local nTamanhoPagina  as numeric

  Local oQuery          as object

  jQueryParams := oRest:getQueryRequest()
  jHeaders := jsonObject():New()
  jHeaders[ 'Content-Type' ] := 'application/json'

  cEmp            := FWGrpCompany()
  cFil            := FWCodFil()
  nPagina         := jQueryParams[ 'page' ]; nPagina := Val( nPagina )
  nTamanhoPagina  := jQueryParams[ 'pageSize' ]; nTamanhoPagina := Val( nTamanhoPagina )

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TMG' ) + " TMG"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TMI' ) + " TMI ON"
  cQuery += CRLF + "TMI_QUESTI = TMG_QUESTI"
  cQuery += CRLF + "AND TMI_FILIAL = '" + FwXFilial( 'TMI' ) + "'"
  cQuery += CRLF + "AND TMI.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TM0' ) + " TM0 ON"
  cQuery += CRLF + "TM0_NUMFIC = TMI_NUMFIC"
  cQuery += CRLF + "AND TM0_FILIAL = '" + FwXFilial( 'TM0' ) + "'"
  cQuery += CRLF + "AND TM0.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "LEFT JOIN " + RetSqlName( 'SRA' ) + " SRA ON"
  cQuery += CRLF + "RA_MAT = TM0_MAT"
  cQuery += CRLF + "AND RA_FILIAL = '" + FwXFilial( 'SRA' ) + "'"
  cQuery += CRLF + "AND SRA.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cQuery += CRLF + "AND EXISTS ( SELECT 1 FROM " + RetSqlName( 'TMH' ) + " TMH where TMH.TMH_QUESTI = TMG.TMG_QUESTI AND TMH.D_E_L_E_T_ != '*' )"

  cQuery += CRLF + "GROUP BY TMI.TMI_FILIAL, SRA.RA_MAT, SRA.RA_FILIAL, TM0.TM0_FILIAL, TMG.TMG_QUESTI, TMI.TMI_DTREAL, TMG.TMG_NOMQUE, TM0.TM0_NUMFIC"

  cWhere := "TMG.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TMG_FILIAL = '" + FwXFilial( 'TMG' ) + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'branchId'         , 'TMI_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'employeeId'       , 'RA_MAT'      , .T., .T. )
  oQuery:addMapFields( 'employeeId2'      , 'RA_FILIAL'   , .T., .T. )
  oQuery:addMapFields( 'employeeId3'      , 'TM0_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'erpId'            , 'TMG_QUESTI'  , .T., .T. )
  oQuery:addMapFields( 'realizationDate'  , 'TMI_DTREAL'  , .T., .T. )
  oQuery:addMapFields( 'quiz'             , 'TMG_NOMQUE'  , .T., .T. )
  oQuery:addMapFields( 'registration'     , 'TM0_NUMFIC'  , .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TMG_QUESTI' )

  oQuery:SetPageSize( nTamanhoPagina )
  oQuery:setPage( nPagina )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nQuestionario := 1 To Len( jResultado[ 'items' ] )

    If !Empty( jResultado[ 'items', nQuestionario, 'employeeId' ] ) // Funcionário
      jResultado[ 'items', nQuestionario, 'employeeId' ] := cEmp + '|' + jResultado[ 'items', nQuestionario, 'employeeId2' ] + '|' +;
      jResultado[ 'items', nQuestionario, 'employeeId' ]
    Else // Candidato
      jResultado[ 'items', nQuestionario, 'employeeId' ] := cEmp + '|' + jResultado[ 'items', nQuestionario, 'employeeId3' ] + '|' +;
      jResultado[ 'items', nQuestionario, 'registration' ]
    EndIf

    jResultado[ 'items', nQuestionario, 'companyId' ] := cEmp

    jResultado[ 'items', nQuestionario, 'response' ] := fBusResQue(;
      jResultado[ 'items', nQuestionario, 'erpId' ] + Space( TamSX3( 'TMG_QUESTI' )[ 1 ] - Len( jResultado[ 'items', nQuestionario, 'erpId' ] ) ),;
      DtoS( fDatas( jResultado[ 'items', nQuestionario, 'realizationDate' ] ) ),;
      jResultado[ 'items', nQuestionario, 'registration' ] + Space( TamSX3( 'TM0_NUMFIC' )[ 1 ] - Len( jResultado[ 'items', nQuestionario, 'registration' ] ) );
    )

    jResultado[ 'items', nQuestionario, 'erpId' ] := cEmp + '|' + jResultado[ 'items', nQuestionario, 'branchId' ] + '|' + ;
    jResultado[ 'items', nQuestionario, 'erpId' ]

    If !Empty( jResultado[ 'items', nQuestionario, 'realizationDate' ] )

      jResultado[ 'items', nQuestionario, 'realizationDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nQuestionario, 'realizationDate' ] ),;
        '00:00:00.000';
      )

    EndIf

    jResultado[ 'items', nQuestionario ]:delName( 'registration' )
    jResultado[ 'items', nQuestionario ]:delName( 'employeeId2' )
    jResultado[ 'items', nQuestionario ]:delName( 'employeeId3' )

  Next nQuestionario

  jResultado[ 'totalAmount' ] := Len( jResultado[ 'items' ] ) + jResultado[ 'remainingRecords' ]
  jResultado[ 'amount' ] := Len( jResultado[ 'items' ] )

  If lRpc
    RpcClearEnv()
  EndIF

  oRest:setHeaderResponse( jHeaders )
  oRest:setResponse( jResultado )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fBusResQue
Busca as respostas do questionário

@author Gabriel Sokacheski
@since 12/04/2024

@param cQuestionario, código do questionário
@param cRealizacao, data da realização do questionário
@param cFicha, ficha do funcionário

@return jResultado, lista de respostas
/*/
//---------------------------------------------------------------------
Static Function fBusResQue( cQuestionario, cRealizacao, cFicha )

  Local cQuery          as character
  Local cWhere          as character
  Local cResultado      as character

  Local jResultado      as json

  Local nResposta       as numeric

  Local oQuery          as object

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TMI' ) + " TMI"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "TMI.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TMI_FILIAL = '" + FwXFilial( 'TMI' ) + "'"
  cWhere += CRLF + "AND TMI_QUESTI = '" + cQuestionario + "'"
  cWhere += CRLF + "AND TMI_DTREAL = '" + cRealizacao + "'"
  cWhere += CRLF + "AND TMI_NUMFIC = '" + cFicha + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'order'        , 'TMI_QUESTA', .T., .T. )
  oQuery:addMapFields( 'option'       , 'TMI_RESPOS', .T., .T. )
  oQuery:addMapFields( 'description'  , 'TMI_DESCRI', .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TMI_QUESTI, TMI_NUMFIC' )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nResposta := 1 To Len( jResultado[ 'items' ] )

    If Empty( jResultado[ 'items', nResposta, 'description' ] )
      jResultado[ 'items', nResposta, 'description' ] := ''
    EndIf

    If !Empty( jResultado[ 'items', nResposta, 'option' ] ) .And. jResultado[ 'items', nResposta, 'option' ] != '#'
      jResultado[ 'items', nResposta, 'option' ] := Val( jResultado[ 'items', nResposta, 'option' ] ) - 1
    Else
      jResultado[ 'items', nResposta, 'option' ] := Nil
    EndIf

    If !Empty( jResultado[ 'items', nResposta, 'order' ] )
      jResultado[ 'items', nResposta, 'order' ] := Val( jResultado[ 'items', nResposta, 'order' ] )
    EndIf

  Next nResposta


Return jResultado[ 'items' ]

//---------------------------------------------------------------------
/*/{Protheus.doc} fDatas
Converte as datas retornadas pelo json

@author Gabriel Sokacheski
@since 16/04/2024

@param cData, data retornada pelo json

/*/
//---------------------------------------------------------------------
Static Function fDatas( cDataJson )

  Local cDia  as character
  Local cMes  as character
  Local cAno  as character
  Local cData as character

  Local dRetorno as date

  cAno  := SubStr( cDataJson, 1, At( '-', cDataJson ) - 1 )
  cDataJson := SubStr( cDataJson, At( '-', cDataJson ) + 1 )
  cMes  := SubStr( cDataJson, 1, At( '-', cDataJson ) - 1 )
  cDia  := SubStr( cDataJson, At( '-', cDataJson ) + 1 )

  If Len( cMes ) < 2
    cMes := '0' + cMes
  EndIf

  If Len( cDia ) < 2
    cDia := '0' + cDia
  EndIf

  cData := cAno + cMes + cDia

  dRetorno := StoD( cData )

Return dRetorno

//---------------------------------------------------------------------
/*/{Protheus.doc} fEpi
Retorna todos os epis cadastrados

@author Gabriel Sokacheski
@since 16/04/2024

/*/
//---------------------------------------------------------------------
@Get( '/api/mdt/v1/ipe-deliveries' )
Function fEpi()

  Local cEmp            as character
  Local cFil            as character
  Local cQuery          as character
  Local cWhere          as character
  Local cResultado      as character
  Local cDataChave      as character

  Local jHeaders        as json
  Local jResultado      as json
  Local jQueryParams    as json

  Local lRpc            as logical

  Local nPagina         as numeric
  Local nEpi            as numeric
  Local nTamanhoPagina  as numeric

  Local oQuery          as object

  jQueryParams := oRest:getQueryRequest()
  jHeaders := jsonObject():New()
  jHeaders[ 'Content-Type' ] := 'application/json'

  cEmp            := FWGrpCompany()
  cFil            := FWCodFil()
  nPagina         := jQueryParams[ 'page' ]; nPagina := Val( nPagina )
  nTamanhoPagina  := jQueryParams[ 'pageSize' ]; nTamanhoPagina := Val( nTamanhoPagina )

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TNF' ) + " TNF"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'SRA' ) + " SRA ON"
  cQuery += CRLF + "RA_MAT = TNF_MAT"
  cQuery += CRLF + "AND RA_FILIAL = '" + FwXFilial( 'SRA' ) + "'"
  cQuery += CRLF + "AND RA_SITFOLH != 'D'"
  cQuery += CRLF + "AND RA_DEMISSA = '" + Space( FwTamSX3( 'RA_DEMISSA' )[ 1 ] ) + "'"
  cQuery += CRLF + "AND SRA.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "LEFT JOIN " + RetSqlName( 'TN3' ) + " TN3 ON"
  cQuery += CRLF + "TN3_CODEPI = TNF_CODEPI"
  cQuery += CRLF + "AND TN3_FORNEC = TNF_FORNEC"
  cQuery += CRLF + "AND TN3_LOJA = TNF_LOJA"
  cQuery += CRLF + "AND TN3_NUMCAP = TNF_NUMCAP"
  cQuery += CRLF + "AND TN3_FILIAL = '" + FwXFilial( 'TN3' ) + "'"
  cQuery += CRLF + "AND TN3.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "LEFT JOIN " + RetSqlName( 'TL0' ) + " TL0 ON"
  cQuery += CRLF + "TL0_EPIFIL = TNF_CODEPI"
  cQuery += CRLF + "AND TL0_FORNEC = TNF_FORNEC"
  cQuery += CRLF + "AND TL0_LOJA = TNF_LOJA"
  cQuery += CRLF + "AND TL0_NUMCAP = TNF_NUMCAP"
  cQuery += CRLF + "AND TL0_FILIAL = '" + FwXFilial( 'TL0' ) + "'"
  cQuery += CRLF + "AND TL0.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "TNF.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TNF_FILIAL = '" + FwXFilial( 'TNF' ) + "'"
  cWhere += CRLF + "AND TNF_DTENTR >= '" + DtoS( FirstYDate( YearSub( Date(), 10 ) ) ) + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'branchId'             , 'TNF_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'employeeId'           , 'RA_FILIAL'   , .T., .T. )
  oQuery:addMapFields( 'erpId'                , 'TNF_FORNEC'  , .T., .T. )
  oQuery:addMapFields( 'erpId2'               , 'TNF_LOJA'    , .T., .T. )
  oQuery:addMapFields( 'erpId3'               , 'TNF_CODEPI'  , .T., .T. )
  oQuery:addMapFields( 'erpId4'               , 'TNF_MAT'     , .T., .T. )
  oQuery:addMapFields( 'ca'                   , 'TNF_NUMCAP'  , .T., .T. )
  oQuery:addMapFields( 'deliveryDate'         , 'TNF_DTENTR'  , .T., .T. )
  oQuery:addMapFields( 'deliveryHour'         , 'TNF_HRENTR'  , .T., .T. )
  oQuery:addMapFields( 'returnDate'           , 'TNF_DTDEVO'  , .T., .T. )
  oQuery:addMapFields( 'caExpirationDate'     , 'TN3_DTVENC'  , .T., .T. )
  oQuery:addMapFields( 'caExpirationDate2'    , 'TL0_DTVENC'  , .T., .T. )
  oQuery:addMapFields( 'deliveryAmount'       , 'TNF_QTDENT'  , .T., .T. )
  oQuery:addMapFields( 'reason'               , 'TNF_MOTIVO'  , .T., .T. )
  oQuery:addMapFields( 'amountReturned'       , 'TNF_QTDEVO'  , .T., .T. )
  oQuery:addMapFields( 'lastMaintenanceDate'  , 'TNF_DTMANU'  , .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TNF_FILIAL, TNF_DTENTR, TNF_HRENTR' )

  oQuery:SetPageSize( nTamanhoPagina )
  oQuery:setPage( nPagina )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nEpi := 1 To Len( jResultado[ 'items' ] )

    cDataChave := FWTimeStamp(;
      1,;
      fDatas( jResultado[ 'items', nEpi, 'deliveryDate' ] ),;
      IIf( !Empty( jResultado[ 'items', nEpi, 'deliveryHour' ] ), jResultado[ 'items', nEpi, 'deliveryHour' ], '00:00:00.000' );
    )

    jResultado[ 'items', nEpi, 'employeeId' ] := cEmp + '|' + jResultado[ 'items', nEpi, 'employeeId' ] + '|' + ;
    jResultado[ 'items', nEpi, 'erpId4' ]

    jResultado[ 'items', nEpi, 'companyId' ] := cEmp

    jResultado[ 'items', nEpi, 'erpId' ] := cEmp + '|' + jResultado[ 'items', nEpi, 'branchId' ] + '|' +;
    jResultado[ 'items', nEpi, 'erpId' ] + '|' + jResultado[ 'items', nEpi, 'erpId2' ] + '|' + jResultado[ 'items', nEpi, 'erpId3' ];
    + '|' + jResultado[ 'items', nEpi, 'ca' ] + '|' + jResultado[ 'items', nEpi, 'erpId4' ] + '|' + SubStr( cDataChave, 1, 8 );
    + '|' + SubStr( cDataChave, 9 )

    Do Case
      Case jResultado[ 'items', nEpi, 'reason' ] == '1'
        jResultado[ 'items', nEpi, 'reason' ] := 0
      Case jResultado[ 'items', nEpi, 'reason' ] == '2'
        jResultado[ 'items', nEpi, 'reason' ] := 1
      Case jResultado[ 'items', nEpi, 'reason' ] == '3'
        jResultado[ 'items', nEpi, 'reason' ] := 2
      Case jResultado[ 'items', nEpi, 'reason' ] == '4'
        jResultado[ 'items', nEpi, 'reason' ] := 3
      Case jResultado[ 'items', nEpi, 'reason' ] == '5'
        jResultado[ 'items', nEpi, 'reason' ] := 4
      Case jResultado[ 'items', nEpi, 'reason' ] == '6'
        jResultado[ 'items', nEpi, 'reason' ] := 5
      Case jResultado[ 'items', nEpi, 'reason' ] == '7'
        jResultado[ 'items', nEpi, 'reason' ] := 6
      Case jResultado[ 'items', nEpi, 'reason' ] == '8'
        jResultado[ 'items', nEpi, 'reason' ] := 8
      Case jResultado[ 'items', nEpi, 'reason' ] == 'X'
        jResultado[ 'items', nEpi, 'reason' ] := 9
      Otherwise
        jResultado[ 'items', nEpi, 'reason' ] := Nil
    EndCase

    If !Empty( jResultado[ 'items', nEpi, 'deliveryDate' ] )

      jResultado[ 'items', nEpi, 'deliveryDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nEpi, 'deliveryDate' ] ),;
        IIf( !Empty( jResultado[ 'items', nEpi, 'deliveryHour' ] ), jResultado[ 'items', nEpi, 'deliveryHour' ], '00:00:00.000' );
      )

    EndIf

    If !Empty( jResultado[ 'items', nEpi, 'returnDate' ] )

      jResultado[ 'items', nEpi, 'returnDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nEpi, 'returnDate' ] ),;
        '00:00:00.000';
      )

    Else

      jResultado[ 'items', nEpi, 'returnDate' ] := Nil

    EndIf

    If !Empty( jResultado[ 'items', nEpi, 'caExpirationDate' ] )

      jResultado[ 'items', nEpi, 'caExpirationDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nEpi, 'caExpirationDate' ] ),;
        '00:00:00.000';
      )

    ElseIf !Empty( jResultado[ 'items', nEpi, 'caExpirationDate2' ] )

      jResultado[ 'items', nEpi, 'caExpirationDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nEpi, 'caExpirationDate2' ] ),;
        '00:00:00.000';
      )

    Else

      jResultado[ 'items', nEpi, 'caExpirationDate' ] := Nil

    EndIf

    If !Empty( jResultado[ 'items', nEpi, 'lastMaintenanceDate' ] )

      jResultado[ 'items', nEpi, 'lastMaintenanceDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nEpi, 'lastMaintenanceDate' ] ),;
        '00:00:00.000';
      )

    Else

      jResultado[ 'items', nEpi, 'lastMaintenanceDate' ] := Nil

    EndIf

    jResultado[ 'items', nEpi ]:delName( 'erpId2' )
    jResultado[ 'items', nEpi ]:delName( 'erpId3' )
    jResultado[ 'items', nEpi ]:delName( 'erpId4' )
    jResultado[ 'items', nEpi ]:delName( 'caExpirationDate2' )
    jResultado[ 'items', nEpi ]:delName( 'deliveryHour' )

  Next nEpi

  jResultado[ 'totalAmount' ] := Len( jResultado[ 'items' ] ) + jResultado[ 'remainingRecords' ]
  jResultado[ 'amount' ] := Len( jResultado[ 'items' ] )

  If lRpc
    RpcClearEnv()
  EndIF

  oRest:setHeaderResponse( jHeaders )
  oRest:setResponse( jResultado )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fVacinas
Retorna todos os epis cadastrados

@author Gabriel Sokacheski
@since 17/04/2024

/*/
//---------------------------------------------------------------------
@Get( '/api/mdt/v1/vaccines' )
Function fVacinas()

  Local cEmp            as character
  Local cFil            as character
  Local cQuery          as character
  Local cWhere          as character
  Local cResultado      as character

  Local jVacina         as json
  Local jHeaders        as json
  Local jResultado      as json
  Local jQueryParams    as json

  Local lRpc            as logical

  Local nPagina         as numeric
  Local nVacinacao      as numeric
  Local nTamanhoPagina  as numeric

  Local oQuery          as object

  jQueryParams := oRest:getQueryRequest()
  jHeaders := jsonObject():New()
  jHeaders[ 'Content-Type' ] := 'application/json'

  cEmp            := FWGrpCompany()
  cFil            := FWCodFil()
  nPagina         := jQueryParams[ 'page' ]; nPagina := Val( nPagina )
  nTamanhoPagina  := jQueryParams[ 'pageSize' ]; nTamanhoPagina := Val( nTamanhoPagina )

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TL9' ) + " TL9"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TM0' ) + " TM0 ON"
  cQuery += CRLF + "TM0_NUMFIC = TL9_NUMFIC"
  cQuery += CRLF + "AND TM0_FILIAL = '" + FwXFilial( 'TM0' ) + "'"
  cQuery += CRLF + "AND TM0.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "LEFT JOIN " + RetSqlName( 'SRA' ) + " SRA ON"
  cQuery += CRLF + "RA_MAT = TM0_MAT"
  cQuery += CRLF + "AND RA_FILIAL = TM0_FILFUN"
  cQuery += CRLF + "AND SRA.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TL6' ) + " TL6 ON"
  cQuery += CRLF + "TL6_VACINA = TL9_VACINA"
  cQuery += CRLF + "AND TL6_FILIAL = '" + FwXFilial( 'TL6' ) + "'"
  cQuery += CRLF + "AND TL6.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TL7' ) + " TL7 ON"
  cQuery += CRLF + "TL7_VACINA = TL9_VACINA"
  cQuery += CRLF + "AND TL7_FILIAL = '" + FwXFilial( 'TL7' ) + "'"
  cQuery += CRLF + "AND TL7.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TL8' ) + " TL8 ON"
  cQuery += CRLF + "TL8_VACINA = TL9_VACINA"
  cQuery += CRLF + "AND TL8_DOSEID = TL9_DOSE"
  cQuery += CRLF + "AND TL8_FILIAL = '" + FwXFilial( 'TL8' ) + "'"
  cQuery += CRLF + "AND TL8.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "TL9.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TL9_FILIAL = '" + FwXFilial( 'TL9' ) + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'branchId'             , 'TL9_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'employeeId'           , 'RA_MAT'      , .T., .T. )
  oQuery:addMapFields( 'employeeId2'          , 'TM0_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'employeeId3'          , 'TM0_NUMFIC'  , .T., .T. )
  oQuery:addMapFields( 'employeeId4'          , 'RA_FILIAL'   , .T., .T. )
  oQuery:addMapFields( 'erpId'                , 'TL9_NUMFIC'  , .T., .T. )
  oQuery:addMapFields( 'erpId2'               , 'TL9_DTPREV'  , .T., .T. )
  oQuery:addMapFields( 'erpId3'               , 'TL9_VACINA'  , .T., .T. )
  oQuery:addMapFields( 'date'                 , 'TL9_DTREAL'  , .T., .T. )
  oQuery:addMapFields( 'status'               , 'TL9_INDVAC'  , .T., .T. )
  oQuery:addMapFields( 'dose'                 , 'TL9_DOSE'    , .T., .T. )
  oQuery:addMapFields( 'description'          , 'TL6_DESVAC'  , .T., .T. )
  oQuery:addMapFields( 'gender'               , 'TL6_SEXO'    , .T., .T. )
  oQuery:addMapFields( 'fromAge'              , 'TL7_IDADEI'  , .T., .T. )
  oQuery:addMapFields( 'toAge'                , 'TL7_IDADEF'  , .T., .T. )
  oQuery:addMapFields( 'reinforcement'        , 'TL8_REPETI'  , .T., .T. )
  oQuery:addMapFields( 'reinforcementInterval', 'TL8_INIDOS'  , .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TL9_FILIAL, TL9_DTPREV' )

  oQuery:SetPageSize( nTamanhoPagina )
  oQuery:setPage( nPagina )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nVacinacao := 1 To Len( jResultado[ 'items' ] )

    jResultado[ 'items', nVacinacao, 'companyId' ] := cEmp

    If Empty( jResultado[ 'items', nVacinacao, 'employeeId' ] )
      jResultado[ 'items', nVacinacao, 'employeeId' ] :=  cEmp + '|' + jResultado[ 'items', nVacinacao, 'employeeId2' ] + '|' +;
      jResultado[ 'items', nVacinacao, 'employeeId3' ]
    Else
      jResultado[ 'items', nVacinacao, 'employeeId' ] :=  cEmp + '|' + jResultado[ 'items', nVacinacao, 'employeeId4' ] + '|' +;
      jResultado[ 'items', nVacinacao, 'employeeId' ]
    EndIf

    jResultado[ 'items', nVacinacao, 'erpId' ] := cEmp + '|' + jResultado[ 'items', nVacinacao, 'branchId' ] + '|' + ;
    jResultado[ 'items', nVacinacao, 'erpId' ] + '|' + SubStr( FWTimeStamp( 1, fDatas( jResultado[ 'items', nVacinacao, 'erpId2' ] ), '00:00:00.000' ), 1, 8 );
    + '|' + jResultado[ 'items', nVacinacao, 'erpId3' ]

    Do Case
      Case jResultado[ 'items', nVacinacao, 'status' ] == '1'
        jResultado[ 'items', nVacinacao, 'status' ] := 1
      Case jResultado[ 'items', nVacinacao, 'status' ] == '2'
        jResultado[ 'items', nVacinacao, 'status' ] := 0
      Case jResultado[ 'items', nVacinacao, 'status' ] == '3'
        jResultado[ 'items', nVacinacao, 'status' ] := 2
      Otherwise
        jResultado[ 'items', nVacinacao, 'status' ] := Nil
    EndCase

    Do Case
      Case jResultado[ 'items', nVacinacao, 'gender' ] == '1'
        jResultado[ 'items', nVacinacao, 'gender' ] := 0
      Case jResultado[ 'items', nVacinacao, 'gender' ] == '2'
        jResultado[ 'items', nVacinacao, 'gender' ] := 1
      Case jResultado[ 'items', nVacinacao, 'gender' ] == '3'
        jResultado[ 'items', nVacinacao, 'gender' ] := 2
    EndCase

    If !Empty( jResultado[ 'items', nVacinacao, 'date' ] )

      jResultado[ 'items', nVacinacao, 'date' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nVacinacao, 'date' ] ),;
        '00:00:00.000';
      )

    Else

      jResultado[ 'items', nVacinacao, 'date' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nVacinacao, 'erpId2' ] ),;
        '00:00:00.000';
      )

    EndIf

    jResultado[ 'items', nVacinacao, 'dose' ] := Val( jResultado[ 'items', nVacinacao, 'dose' ] )
    jResultado[ 'items', nVacinacao, 'fromAge' ] := Val( jResultado[ 'items', nVacinacao, 'fromAge' ] )
    jResultado[ 'items', nVacinacao, 'toAge' ] := Val( jResultado[ 'items', nVacinacao, 'toAge' ] )

    jVacina := jsonObject():new()

    jVacina[ 'description' ] := jResultado[ 'items', nVacinacao, 'description' ]
    jVacina[ 'gender' ] := jResultado[ 'items', nVacinacao, 'gender' ]
    jVacina[ 'fromAge' ] := jResultado[ 'items', nVacinacao, 'fromAge' ]
    jVacina[ 'toAge' ] := jResultado[ 'items', nVacinacao, 'toAge' ]

    Do Case
      Case jResultado[ 'items', nVacinacao, 'reinforcement' ] == '1'
        jVacina[ 'reinforcement' ] := 1
    EndCase

    jVacina[ 'reinforcementInterval' ] := jResultado[ 'items', nVacinacao, 'reinforcementInterval' ]

    jResultado[ 'items', nVacinacao, 'vaccine' ] := jVacina

    FreeObj( jVacina )

    jResultado[ 'items', nVacinacao ]:delName( 'erpId2' )
    jResultado[ 'items', nVacinacao ]:delName( 'erpId3' )
    jResultado[ 'items', nVacinacao ]:delName( 'employeeId2' )
    jResultado[ 'items', nVacinacao ]:delName( 'employeeId3' )
    jResultado[ 'items', nVacinacao ]:delName( 'employeeId4' )
    jResultado[ 'items', nVacinacao ]:delName( 'description' )
    jResultado[ 'items', nVacinacao ]:delName( 'gender' )
    jResultado[ 'items', nVacinacao ]:delName( 'fromAge' )
    jResultado[ 'items', nVacinacao ]:delName( 'toAge' )
    jResultado[ 'items', nVacinacao ]:delName( 'reinforcement' )
    jResultado[ 'items', nVacinacao ]:delName( 'reinforcementInterval' )

  Next nVacinacao

  jResultado[ 'totalAmount' ] := Len( jResultado[ 'items' ] ) + jResultado[ 'remainingRecords' ]
  jResultado[ 'amount' ] := Len( jResultado[ 'items' ] )

  If lRpc
    RpcClearEnv()
  EndIF

  oRest:setHeaderResponse( jHeaders )
  oRest:setResponse( jResultado )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fDocumentos
Retorna todos os laudos cadastrados

@author Gabriel Sokacheski
@since 18/04/2024

/*/
//---------------------------------------------------------------------
@Get( '/api/mdt/v1/documents' )
Function fDocumentos()

  Local cEmp            as character
  Local cFil            as character
  Local cQuery          as character
  Local cWhere          as character
  Local cResultado      as character

  Local jHeaders        as json
  Local jResultado      as json
  Local jQueryParams    as json
  Local jCentroCusto    as json
  Local jResponsavel    as json

  Local lRpc            as logical

  Local nPagina         as numeric
  Local nLaudo          as numeric
  Local nTamanhoPagina  as numeric

  Local oQuery          as object

  jQueryParams := oRest:getQueryRequest()
  jHeaders := jsonObject():New()
  jHeaders[ 'Content-Type' ] := 'application/json'

  cEmp            := FWGrpCompany()
  cFil            := FWCodFil()
  nPagina         := jQueryParams[ 'page' ]; nPagina := Val( nPagina )
  nTamanhoPagina  := jQueryParams[ 'pageSize' ]; nTamanhoPagina := Val( nTamanhoPagina )

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TO0' ) + " TO0"

  cQuery += CRLF + "LEFT JOIN " + RetSqlName( 'CTT' ) + " CTT ON"
  cQuery += CRLF + "CTT_CUSTO = TO0_CC"
  cQuery += CRLF + "AND CTT_FILIAL = '" + FwXFilial( 'CTT' ) + "'"
  cQuery += CRLF + "AND CTT.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TMK' ) + " TMK ON"
  cQuery += CRLF + "TMK_CODUSU = TO0_CODUSU"
  cQuery += CRLF + "AND TMK_FILIAL = '" + FwXFilial( 'TL6' ) + "'"
  cQuery += CRLF + "AND TMK.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "TO0.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TO0_FILIAL = '" + FwXFilial( 'TO0' ) + "'"
  cWhere += CRLF + "AND TO0_DTINIC >= '" + DtoS( FirstYDate( YearSub( Date(), 20 ) ) ) + "'"
  cWhere += CRLF + "AND ( '" + DtoS( dDataBase ) + "' <= TO0_DTVALI"
  cWhere += CRLF + "OR TO0_DTVALI = '" + Space( TamSX3( 'TO0_DTVALI' )[ 1 ] ) + "' )"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'branchId'               , 'TO0_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'erpId'                  , 'TO0_LAUDO'   , .T., .T. )
  oQuery:addMapFields( 'objective'              , 'TO0_NOME'    , .T., .T. )
  oQuery:addMapFields( 'type'                   , 'TO0_TIPREL'  , .T., .T. )
  oQuery:addMapFields( 'finality'               , 'TO0_FINALI'  , .T., .T. )
  oQuery:addMapFields( 'startDate'              , 'TO0_DTINIC'  , .T., .T. )
  oQuery:addMapFields( 'endDate'                , 'TO0_DTFIM'   , .T., .T. )
  oQuery:addMapFields( 'expirationDate'         , 'TO0_DTVALI'  , .T., .T. )
  oQuery:addMapFields( 'costCenter'             , 'TO0_CC'      , .T., .T. )
  oQuery:addMapFields( 'branchIdCC'             , 'CTT_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'description'            , 'CTT_DESC01'  , .T., .T. )
  oQuery:addMapFields( 'responsible'            , 'TO0_CODUSU'  , .T., .T. )
  oQuery:addMapFields( 'branchIdRe'             , 'TMK_FILIAL'  , .T., .T. )
  oQuery:addMapFields( 'name'                   , 'TMK_NOMUSU'  , .T., .T. )
  oQuery:addMapFields( 'individualRegistration' , 'TMK_CIC'     , .T., .T. )
  oQuery:addMapFields( 'documentNumber'         , 'TMK_NUMENT'  , .T., .T. )
  oQuery:addMapFields( 'documentState'          , 'TMK_UF'      , .T., .T. )
  oQuery:addMapFields( 'entityType'             , 'TMK_ENTCLA'  , .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TO0_FILIAL, TO0_DTINIC' )

  oQuery:SetPageSize( nTamanhoPagina )
  oQuery:setPage( nPagina )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nLaudo := 1 To Len( jResultado[ 'items' ] )

    jResultado[ 'items', nLaudo, 'companyId' ] := cEmp

    jResultado[ 'items', nLaudo, 'erpId' ] := cEmp + '|' + jResultado[ 'items', nLaudo, 'branchId' ] + '|' + ;
    jResultado[ 'items', nLaudo, 'erpId' ]

    Do Case
      Case jResultado[ 'items', nLaudo, 'type' ] == '1'
        jResultado[ 'items', nLaudo, 'type' ] := 0 // ppra
      Case jResultado[ 'items', nLaudo, 'type' ] == '3'
        jResultado[ 'items', nLaudo, 'type' ] := 1 // lt pericial
      Case jResultado[ 'items', nLaudo, 'type' ] == '4'
        jResultado[ 'items', nLaudo, 'type' ] := 2 // ltcat
      Case jResultado[ 'items', nLaudo, 'type' ] == '6'
        jResultado[ 'items', nLaudo, 'type' ] := 3 // pgr
      Case jResultado[ 'items', nLaudo, 'type' ] == '7'
        jResultado[ 'items', nLaudo, 'type' ] := 4 // pcmat
      Case jResultado[ 'items', nLaudo, 'type' ] == '8'
        jResultado[ 'items', nLaudo, 'type' ] := 5 // ppr
      Case jResultado[ 'items', nLaudo, 'type' ] == '2'
        jResultado[ 'items', nLaudo, 'type' ] := 7 // pcmso
      Case jResultado[ 'items', nLaudo, 'type' ] == 'A'
        jResultado[ 'items', nLaudo, 'type' ] := 8 // pae
      Otherwise
        jResultado[ 'items', nLaudo, 'type' ] := Nil
    EndCase

    Do Case
      Case jResultado[ 'items', nLaudo, 'finality' ] == '1'
        jResultado[ 'items', nLaudo, 'finality' ] := 1
      Case jResultado[ 'items', nLaudo, 'finality' ] == '2'
        jResultado[ 'items', nLaudo, 'finality' ] := 0
      Case jResultado[ 'items', nLaudo, 'finality' ] == '3'
        jResultado[ 'items', nLaudo, 'finality' ] := 2
    EndCase

    If !Empty( jResultado[ 'items', nLaudo, 'startDate' ] )

      jResultado[ 'items', nLaudo, 'startDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nLaudo, 'startDate' ] ),;
        '00:00:00.000';
      )

    EndIf

    If !Empty( jResultado[ 'items', nLaudo, 'endDate' ] )

      jResultado[ 'items', nLaudo, 'endDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nLaudo, 'endDate' ] ),;
        '00:00:00.000';
      )

    Else

      jResultado[ 'items', nLaudo, 'endDate' ] := Nil

    EndIf

    If !Empty( jResultado[ 'items', nLaudo, 'expirationDate' ] )

      jResultado[ 'items', nLaudo, 'expirationDate' ] := FWTimeStamp(;
        5,;
        fDatas( jResultado[ 'items', nLaudo, 'expirationDate' ] ),;
        '00:00:00.000';
      )

    Else

      jResultado[ 'items', nLaudo, 'expirationDate' ] := Nil

    EndIf

    If !Empty( jResultado[ 'items', nLaudo, 'costCenter' ] )

      jCentroCusto := jsonObject():new()

      jCentroCusto[ 'erpId' ] := cEmp + '|' + jResultado[ 'items', nLaudo, 'branchIdCC' ] + '|' +;
      jResultado[ 'items', nLaudo, 'costCenter' ]

      jCentroCusto[ 'description' ] := jResultado[ 'items', nLaudo, 'description' ]

      jResultado[ 'items', nLaudo, 'costCenter' ] := jCentroCusto

      FreeObj( jCentroCusto )

    Else

      jResultado[ 'items', nLaudo ]:delName( 'costCenter' )

    EndIf

    jResponsavel := jsonObject():new()

    jResponsavel[ 'erpId' ] := cEmp + '|' + jResultado[ 'items', nLaudo, 'branchIdRe' ] + '|' +;
    jResultado[ 'items', nLaudo, 'responsible' ]

    jResponsavel[ 'name' ] := jResultado[ 'items', nLaudo, 'name' ]
    jResponsavel[ 'gender' ] := 1

    If !Empty( jResultado[ 'items', nLaudo, 'individualRegistration' ] )
      jResponsavel[ 'individualRegistration' ] := jResultado[ 'items', nLaudo, 'individualRegistration' ]
    EndIf

    jResponsavel[ 'birthday' ] := FWTimeStamp( 5, CtoD( '01/01/1991' ), '00:00:00.000' )

    jResponsavel[ 'gender' ] := 0

    jResultado[ 'items', nLaudo, 'responsible' ] := jResponsavel

    FreeObj( jResponsavel )

    If Empty( jResultado[ 'items', nLaudo, 'entityType' ] )

      jResultado[ 'items', nLaudo, 'entityType' ] := Nil

    Else

      Do Case
        Case AllTrim( jResultado[ 'items', nLaudo, 'entityType' ] ) == 'CRM'
          jResultado[ 'items', nLaudo, 'entityType' ] := 0
        Case AllTrim( jResultado[ 'items', nLaudo, 'entityType' ] ) == 'CREA'
          jResultado[ 'items', nLaudo, 'entityType' ] := 1
        Otherwise
          jResultado[ 'items', nLaudo, 'entityType' ] := 5
      EndCase

    EndIf

    jResultado[ 'items', nLaudo, 'entityDescription' ] := 'Outros'

    If Empty( jResultado[ 'items', nLaudo, 'documentNumber' ] )
      jResultado[ 'items', nLaudo, 'documentNumber' ] := Nil
    EndIf

    If Empty( jResultado[ 'items', nLaudo, 'documentState' ] )
      jResultado[ 'items', nLaudo, 'documentState' ] := Nil
    EndIf

    If jResultado[ 'items', nLaudo, 'type' ] != 1 // LT Pericial
      jResultado[ 'items', nLaudo ]:delName( 'finality' )
    EndIf

    jResultado[ 'items', nLaudo ]:delName( 'branchIdCC' )
    jResultado[ 'items', nLaudo ]:delName( 'description' )
    jResultado[ 'items', nLaudo ]:delName( 'branchIdRe' )
    jResultado[ 'items', nLaudo ]:delName( 'name' )
    jResultado[ 'items', nLaudo ]:delName( 'individualRegistration' )

  Next nLaudo

  jResultado[ 'totalAmount' ] := Len( jResultado[ 'items' ] ) + jResultado[ 'remainingRecords' ]
  jResultado[ 'amount' ] := Len( jResultado[ 'items' ] )

  If lRpc
    RpcClearEnv()
  EndIF

  oRest:setHeaderResponse( jHeaders )
  oRest:setResponse( jResultado )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fFuncao
Retorna todas as funções cadastradas

@author Gabriel Sokacheski
@since 19/04/2024

/*/
//---------------------------------------------------------------------
@Get( '/api/mdt/v1/occupation' )
Function fFuncao()

  Local cEmp            as character
  Local cFil            as character
  Local cQuery          as character
  Local cWhere          as character
  Local cResultado      as character
  Local cSubQuery1      as character
  Local cSubQuery2      as character

  Local jHeaders        as json
  Local jResultado      as json
  Local jQueryParams    as json

  Local lRpc            as logical

  Local nPagina         as numeric
  Local nFuncao         as numeric
  Local nTamanhoPagina  as numeric

  Local oQuery          as object

  jQueryParams := oRest:getQueryRequest()
  jHeaders := jsonObject():New()
  jHeaders[ 'Content-Type' ] := 'application/json'

  cEmp            := FWGrpCompany()
  cFil            := FWCodFil()
  nPagina         := jQueryParams[ 'page' ]; nPagina := Val( nPagina )
  nTamanhoPagina  := jQueryParams[ 'pageSize' ]; nTamanhoPagina := Val( nTamanhoPagina )

  cSubQuery1 := "SELECT DISTINCT COUNT( TN3_NUMCAP ) + COUNT( TL0_NUMCAP )"
  cSubQuery1 += CRLF + "FROM " + RetSqlName( 'TNB' ) + " TNB "
  cSubQuery1 += CRLF + "INNER JOIN " + RetSqlName( 'TN3' ) + " TN3 ON"
  cSubQuery1 += CRLF + "TNB.TNB_CODEPI = TN3.TN3_CODEPI"
  cSubQuery1 += CRLF + "AND TN3.TN3_FILIAL = '" + FwXFilial( 'TN3' ) + "'"
  cSubQuery1 += CRLF + "AND TN3.D_E_L_E_T_ != '*'"
  cSubQuery1 += CRLF + "LEFT JOIN " + RetSqlName( 'TL0' ) + " TL0 ON"
  cSubQuery1 += CRLF + "TNB.TNB_CODEPI = TL0.TL0_EPIGEN"
  cSubQuery1 += CRLF + "AND TL0.TL0_FILIAL = '" + FwXFilial( 'TL0' ) + "'"
  cSubQuery1 += CRLF + "AND TL0.D_E_L_E_T_ != '*'"
  cSubQuery1 += CRLF + "WHERE TNB.TNB_CODFUN = SRJ.RJ_FUNCAO"
  cSubQuery1 += CRLF + "AND TNB.TNB_FILIAL = '" + FwXFilial( 'TNB' ) + "'"
  cSubQuery1 += CRLF + "AND TNB.D_E_L_E_T_ != '*'"

  cSubQuery2 := "SELECT DISTINCT COUNT( TON_CODEXA )"
  cSubQuery2 += CRLF + "FROM " + RetSqlName( 'TON' ) + " TON"
  cSubQuery2 += CRLF + "WHERE TON.TON_CODFUN = SRJ.RJ_FUNCAO"
  cSubQuery2 += CRLF + "AND TON.TON_FILIAL = '" + FwXFilial( 'TON' ) + "'"
  cSubQuery2 += CRLF + "AND TON.D_E_L_E_T_ != '*'"

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'SRJ' ) + " SRJ"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "SRJ.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND RJ_FILIAL = '" + FwXFilial( 'SRJ' ) + "'"
  cWhere += CRLF + "AND ( (" + cSubQuery1 + ") > 0 OR (" + cSubQuery2 + ") > 0 )"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'branchId' , 'RJ_FILIAL' , .T., .T. )
  oQuery:addMapFields( 'erpId'    , 'RJ_FUNCAO' , .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'RJ_FILIAL, RJ_FUNCAO' )

  oQuery:SetPageSize( nTamanhoPagina )
  oQuery:setPage( nPagina )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nFuncao := 1 To Len( jResultado[ 'items' ] )

    jResultado[ 'items', nFuncao, 'companyId' ] := cEmp

    jResultado[ 'items', nFuncao, 'ipe' ] := fBusEpiFun(;
      jResultado[ 'items', nFuncao, 'erpId' ] + Space( TamSX3( 'RJ_FUNCAO' )[ 1 ] - Len( jResultado[ 'items', nFuncao, 'erpId' ] ) );
    )

    jResultado[ 'items', nFuncao, 'exams' ] := fBusExaFun(;
      jResultado[ 'items', nFuncao, 'erpId' ] + Space( TamSX3( 'RJ_FUNCAO' )[ 1 ] - Len( jResultado[ 'items', nFuncao, 'erpId' ] ) );
    )

    jResultado[ 'items', nFuncao, 'erpId' ] := cEmp + '|' + jResultado[ 'items', nFuncao, 'branchId' ] + '|' + ;
    jResultado[ 'items', nFuncao, 'erpId' ]

  Next nFuncao

  jResultado[ 'totalAmount' ] := Len( jResultado[ 'items' ] ) + jResultado[ 'remainingRecords' ]
  jResultado[ 'amount' ] := Len( jResultado[ 'items' ] )

  If lRpc
    RpcClearEnv()
  EndIF

  oRest:setHeaderResponse( jHeaders )
  oRest:setResponse( jResultado )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fBusEpiFun
Busca os epis da função

@author Gabriel Sokacheski
@since 19/04/2024

@param, cFuncao, código da função

@return aEpi, lista de epis utilizados na função filtrada
/*/
//---------------------------------------------------------------------
Static Function fBusEpiFun( cFuncao )

  Local aEpi as array

  Local cEpi as character

  cEpi := GetNextAlias()

  BeginSQL Alias cEpi

    SELECT 
      DISTINCT TN3.TN3_NUMCAP, TL0.TL0_NUMCAP
    FROM 
      %table:TNB% TNB
        INNER JOIN %table:TN3% TN3 ON
          TNB.TNB_CODEPI = TN3.TN3_CODEPI
          AND %xFilial:TN3% = TN3.TN3_FILIAL
          AND TN3.%NotDel%
        LEFT JOIN %table:TL0% TL0 ON
          TNB.TNB_CODEPI = TL0.TL0_EPIGEN
          AND %xFilial:TL0% = TL0.TL0_FILIAL
          AND TL0.%NotDel%
    WHERE
      TNB.TNB_FILIAL = %xFilial:TNB%
      AND TNB.TNB_CODFUN = %exp:cFuncao%
      AND TNB.%NotDel%

  EndSQL

  dbSelectArea( cEpi )
  ( cEpi )->( dbGoTop() )

  aEpi := {}

  While ( cEpi )->( !Eof() )

    If !Empty( ( cEpi )->( TN3_NUMCAP ) )
      aAdd( aEpi, AllTrim( ( cEpi )->( TN3_NUMCAP ) ) )
    ElseIf !Empty( ( cEpi )->( TL0_NUMCAP ) )
      aAdd( aEpi, AllTrim( ( cEpi )->( TL0_NUMCAP ) ) )
    EndIf

    ( cEpi )->( dbSkip() )

  End

  ( cEpi )->( dbCloseArea() )

Return aEpi

//---------------------------------------------------------------------
/*/{Protheus.doc} fBusExaFun
Busca os exames da função

@author Gabriel Sokacheski
@since 19/04/2024

@param, cFuncao, código da função

/*/
//---------------------------------------------------------------------
Static Function fBusExaFun( cFuncao )

  Local cQuery          as character
  Local cWhere          as character
  Local cResultado      as character

  Local jResultado      as json
  Local jPeriodicidade  as json

  Local nExame          as numeric

  Local oQuery          as object

  cQuery := "SELECT DISTINCT #QueryFields# FROM " + RetSqlName( 'TON' ) + " TON"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TM4' ) + " TM4 ON"
  cQuery += CRLF + "TM4_EXAME = TON_CODEXA"
  cQuery += CRLF + "AND TM4_FILIAL = '" + FwXFilial( 'TM4' ) + "'"
  cQuery += CRLF + "AND TM4.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "INNER JOIN " + RetSqlName( 'TMQ' ) + " TMQ ON"
  cQuery += CRLF + "TMQ_FAIXA = TON_FAIXA"
  cQuery += CRLF + "AND TMQ_FILIAL = '" + FwXFilial( 'TMQ' ) + "'"
  cQuery += CRLF + "AND TMQ.D_E_L_E_T_ != '*'"

  cQuery += CRLF + "WHERE #QueryWhere#"

  cWhere := "TON.D_E_L_E_T_ != '*'"
  cWhere += CRLF + "AND TON_FILIAL = '" + FwXFilial( 'TON' ) + "'"
  cWhere += CRLF + "AND TON_CODFUN = '" + cFuncao + "'"

  oQuery := fwAdapterBaseV2():new( 'GET', .T. )
  oQuery:addMapFields( 'description'      , 'TM4_NOMEXA', .T., .T. )
  oQuery:addMapFields( 'type'             , 'TM4_INDRES', .T., .T. )
  oQuery:addMapFields( 'admission'        , 'TON_TIPOEX', .T., .T. )
  oQuery:addMapFields( 'periodic'         , 'TON_TIPOEX', .T., .T. )
  oQuery:addMapFields( 'backToWork'       , 'TON_TIPOEX', .T., .T. )
  oQuery:addMapFields( 'dismissal'        , 'TON_TIPOEX', .T., .T. )
  oQuery:addMapFields( 'changeOfFunction' , 'TON_TIPOEX', .T., .T. )
  oQuery:addMapFields( 'fromAge'          , 'TMQ_1DE'   , .T., .T. )
  oQuery:addMapFields( 'toAge'            , 'TMQ_1ATE'  , .T., .T. )
  oQuery:addMapFields( 'periodicity'      , 'TMQ_1QTMES', .T., .T. )
  oQuery:addMapFields( 'gender'           , 'TMQ_SEXO'  , .T., .T. )
  oQuery:addMapFields( 'postAdmission'    , 'TMQ_POSADM', .T., .T. )

  oQuery:setIsCaseSensitive( .T. )
  oQuery:setUseSpaces( .T. )

  oQuery:SetQuery( cQuery )
  oQuery:SetWhere( cWhere )
  oQuery:setOrder( 'TM4_NOMEXA' )

  If oQuery:execute()
    oQuery:fillGetResponse()
    cResultado := oQuery:getJsonResponse()
  endif

  jResultado := jsonObject():new()
  jResultado:fromJson( cResultado )

  For nExame := 1 To Len( jResultado[ 'items' ] )

    Do Case
      Case jResultado[ 'items', nExame, 'type' ] == '1'
        jResultado[ 'items', nExame, 'type' ] := 0 // Normal
      Case jResultado[ 'items', nExame, 'type' ] == '2'
        jResultado[ 'items', nExame, 'type' ] := 1 // IBMP
      Case jResultado[ 'items', nExame, 'type' ] == '3'
        jResultado[ 'items', nExame, 'type' ] := 2 // Itens de exame
      Case jResultado[ 'items', nExame, 'type' ] == '4'
        jResultado[ 'items', nExame, 'type' ] := 3 // Audiométrico
      Otherwise
        jResultado[ 'items', nExame, 'type' ] := Nil
    EndCase

    Do Case
      Case jResultado[ 'items', nExame, 'admission' ] == '1'
        jResultado[ 'items', nExame, 'admission' ] := .T.
      Case jResultado[ 'items', nExame, 'admission' ] == '6'
        jResultado[ 'items', nExame, 'admission' ] := .T.
      Case jResultado[ 'items', nExame, 'admission' ] == '8'
        jResultado[ 'items', nExame, 'admission' ] := .T.
      Case jResultado[ 'items', nExame, 'admission' ] == '9'
        jResultado[ 'items', nExame, 'admission' ] := .T.
      Case Len( AllTrim( jResultado[ 'items', nExame, 'admission' ] ) ) > 1
        jResultado[ 'items', nExame, 'admission' ] := .T.
      Case !Empty( jResultado[ 'items', nExame, 'admission' ] )
        jResultado[ 'items', nExame, 'admission' ] := .F.
    EndCase

    Do Case
      Case jResultado[ 'items', nExame, 'periodic' ] == '10'
        jResultado[ 'items', nExame, 'periodic' ] := .F.
      Case jResultado[ 'items', nExame, 'periodic' ] == '15'
        jResultado[ 'items', nExame, 'periodic' ] := .F.
      Case jResultado[ 'items', nExame, 'periodic' ] == '16'
        jResultado[ 'items', nExame, 'periodic' ] := .F.
      Case jResultado[ 'items', nExame, 'periodic' ] == '2'
        jResultado[ 'items', nExame, 'periodic' ] := .T.
      Case jResultado[ 'items', nExame, 'periodic' ] == '6'
        jResultado[ 'items', nExame, 'periodic' ] := .T.
      Case jResultado[ 'items', nExame, 'periodic' ] == '7'
        jResultado[ 'items', nExame, 'periodic' ] := .T.
      Case jResultado[ 'items', nExame, 'periodic' ] == '8'
        jResultado[ 'items', nExame, 'periodic' ] := .T.
      Case !Empty( jResultado[ 'items', nExame, 'periodic' ] )
        jResultado[ 'items', nExame, 'periodic' ] := .F.
    EndCase

    Do Case
      Case jResultado[ 'items', nExame, 'backToWork' ] == '4'
        jResultado[ 'items', nExame, 'backToWork' ] := .T.
      Case jResultado[ 'items', nExame, 'backToWork' ] == '12'
        jResultado[ 'items', nExame, 'backToWork' ] := .T.
      Case jResultado[ 'items', nExame, 'backToWork' ] == '16'
        jResultado[ 'items', nExame, 'backToWork' ] := .T.
      Case !Empty( jResultado[ 'items', nExame, 'backToWork' ] )
        jResultado[ 'items', nExame, 'backToWork' ] := .F.
    EndCase

    Do Case
      Case jResultado[ 'items', nExame, 'dismissal' ] == '5'
        jResultado[ 'items', nExame, 'dismissal' ] := .T.
      Case jResultado[ 'items', nExame, 'dismissal' ] == '7'
        jResultado[ 'items', nExame, 'dismissal' ] := .T.
      Case jResultado[ 'items', nExame, 'dismissal' ] == '8'
        jResultado[ 'items', nExame, 'dismissal' ] := .T.
      Case jResultado[ 'items', nExame, 'dismissal' ] == '9'
        jResultado[ 'items', nExame, 'dismissal' ] := .T.
      Case jResultado[ 'items', nExame, 'dismissal' ] == '11'
        jResultado[ 'items', nExame, 'dismissal' ] := .T.
      Case jResultado[ 'items', nExame, 'dismissal' ] == '14'
        jResultado[ 'items', nExame, 'dismissal' ] := .T.
      Case jResultado[ 'items', nExame, 'dismissal' ] == '15'
        jResultado[ 'items', nExame, 'dismissal' ] := .T.
      Case !Empty( jResultado[ 'items', nExame, 'dismissal' ] )
        jResultado[ 'items', nExame, 'dismissal' ] := .F.
    EndCase

    Do Case
      Case jResultado[ 'items', nExame, 'changeOfFunction' ] == '3'
        jResultado[ 'items', nExame, 'changeOfFunction' ] := .T.
      Case Len( AllTrim( jResultado[ 'items', nExame, 'changeOfFunction' ] ) ) > 1
        jResultado[ 'items', nExame, 'changeOfFunction' ] := .T.
      Case !Empty( jResultado[ 'items', nExame, 'changeOfFunction' ] )
        jResultado[ 'items', nExame, 'changeOfFunction' ] := .F.
    EndCase

    Do Case
      Case jResultado[ 'items', nExame, 'gender' ] == '1'
        jResultado[ 'items', nExame, 'gender' ] := 1
      Case jResultado[ 'items', nExame, 'gender' ] == '2'
        jResultado[ 'items', nExame, 'gender' ] := 2
      Case jResultado[ 'items', nExame, 'gender' ] == '3'
        jResultado[ 'items', nExame, 'gender' ] := 3
    EndCase

    jPeriodicidade := jsonObject():new()
    jPeriodicidade[ 'fromAge' ] := jResultado[ 'items', nExame, 'fromAge' ]
    jPeriodicidade[ 'toAge' ] := jResultado[ 'items', nExame, 'toAge' ]
    jPeriodicidade[ 'periodicity' ] := jResultado[ 'items', nExame, 'periodicity' ]
    jPeriodicidade[ 'gender' ] := jResultado[ 'items', nExame, 'gender' ]
    jPeriodicidade[ 'postAdmission' ] := jResultado[ 'items', nExame, 'postAdmission' ]

    jResultado[ 'items', nExame, 'periodicity' ] := jPeriodicidade

    jResultado[ 'items', nExame ]:delName( 'fromAge' )
    jResultado[ 'items', nExame ]:delName( 'toAge' )
    jResultado[ 'items', nExame ]:delName( 'gender' )
    jResultado[ 'items', nExame ]:delName( 'postAdmission' )

    FreeObj( jPeriodicidade )

  Next nExame

Return jResultado[ 'items' ]
