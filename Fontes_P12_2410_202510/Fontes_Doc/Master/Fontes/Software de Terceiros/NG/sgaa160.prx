#INCLUDE "SGAA160.ch"
#Include "Protheus.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ SGAA160  ³ Autor ³ Thiago Olis Machado   ³ Data ³11/08/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa para Controle de Eventos.                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³TAQ - Eventos.                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         Atualizacoes Sofridas Desde a Construcao Inicial.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SGAA160()
Local oBold,oBtn1,oBtn2,oBtn3,oBtn4,oCheck
Local cUser := PadR( Upper( SubStr( cUsuario, 7, 15 ) ), 15 )
Private aEventos := SgCarTAQ(cUser)
Private lUmaVez := .f.
Private oDlgEve
Private oList
Private cCadastro := ''
Private cPendencia
Private aAvaliacao := {}
Private aRotina := {}

Private AsMenu := {}, aChkDel := {}, bNgGrava,aChoice := {},aVarNao := {}

SetKey( VK_F9, { | | NGVersao( "SGAA160" , 1 ) } )

If Len( aEventos ) == 0
	Return .t.
EndIf

If aEventos[1][4] == 'N'
	Return .t.
EndIf

Define MsDialog oDlgEve From 000,000 To 305,705 Title STR0001 Pixel    //"Eventos"
Define FONT oBold NAME "Courier New" Size 0, -13 BOLD

@ 02,005 Say OemToAnsi(STR0002) Size 187,7 Of oDlgEve Pixel Font oBold    //"Usuário :"
@ 02,050 Say OemToAnsi( cUser ) Color CLR_BLUE Pixel Font oBold

Define sButton oBtn1 From 003,322 Type 11 Enable Of oDlgEve Action Sg160_Rea(.t.)

Define sButton oBtn2 From 016,322 Type 17

Define sButton oBtn3 From 029,322 Type 03 Enable Of oDlgEve Action Sg160_Rea(.f.)

Define sButton oBtn4 From 042,322 Type 02 Enable Of oDlgEve Action Sg160Grava(cUser)


@ 012,006 ListBox oList Var cList;
			 Fields Header;
			 STR0003,STR0004,STR0005;  //"Pendencia"###"Descricao"###"Data do Evento"
			 ColSizes 50,170,40;
			 Size 310,130 Of oDlgEve Pixel

bList := { || 	If(oList:nAt > Len( aEventos ) .Or. oList:nAt = 0 ,;
					{ " "," "," "," "," "," " },;
					{ aEventos[oList:nAt,1], aEventos[oList:nAt,2], aEventos[oList:nAt,5] }) }

oList:SetArray( aEventos )
oList:bLine:= bList
oList:GoTop()

@145,006 CheckBox oCheck Var lUmaVez Prompt OemToAnsi(STR0006) Size 112,7 Of ODlgEve Pixel   //"Nao mostrar essa mensagem na tela inicial"
Activate MsDialog oDlgEve Centered

Return .t.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	  ³ SgPostEve  ³ Autor ³ Thiago Olis Machado     ³ Data ³ 12/08/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao  ³ Grava Pendencias                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		  ³ SGAA100                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function SgPostEve(cTipo,dData,cUser,cOrdem)

DbSelectArea("TAQ")
DbSetOrder(1)
If DbSeek(xFilial("TAQ")+cUser+Dtos( dData )+cOrdem)
	RecLock("TAQ",.f.)
Else
	RecLock("TAQ",.t.)
EndIf

TAQ->TAQ_FILIAL := xFilial("TAQ")
TAQ->TAQ_USUARI := cUser
TAQ->TAQ_TIPO   := cTipo
TAQ->TAQ_ORDEM  := cOrdem
TAQ->TAQ_DATA   := dData
TAQ->TAQ_PENDEN := Sg160Pen()
TAQ->TAQ_DESC := Sg160Des( cTipo, cOrdem )

MsUnLock("TAQ")
Return .t.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	  ³ Sg160Pen   ³ Autor ³ Thiago Olis Machado     ³ Data ³ 12/08/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao  ³ Grava Pendencias                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		  ³ SIGASGA                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Sg160Pen()
	
	Local cAlsTAQ := GetNextAlias()	
	Local cResult := ''

	BeginSQL Alias cAlsTAQ

		SELECT
			MAX( TAQ.TAQ_PENDEN ) AS RESULT
		FROM
			%table:TAQ% TAQ
		WHERE
			TAQ.TAQ_FILIAL = %xFilial:TAQ% AND
			TAQ.%NotDel%

	EndSQL

	cResult := StrZero( ( Val( (cAlsTAQ)->RESULT ) + 1 ), 6)

	(cAlsTAQ)->( dbCloseArea() )

Return cResult

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	  ³ Sg160Des  ³ Autor ³ Thiago Olis Machado     ³ Data ³ 12/08/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao  ³ Grava Pendencias                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		  ³ SIGASGA                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function Sg160Des(cTipo, cOrdem)
Local cDesc := ""

If cTipo == '1'
	cDesc := STR0007  //"Reavaliar Aspectos x Impactos"
ElseIf cTipo == '2'
	cDesc := STR0008  //"Revisar Documentos"
ElseIf cTipo == '3'
	cDesc := STR0009  //"Realizar Plano de Acao"
ElseIf cTipo == '4'
	cDesc := "O Requisito está prestes a vencer ou ja venceu"
ElseIf cTipo == '5'
	cDesc := "O Requisito está esgotando o seu prazo de vigência ou ja esgotou"
ElseIf cTipo == '6'
	cDesc := STR0012 //"O Almoxarifado não foi informado para o Destino"
ElseIf cTipo == '7'
   DbSelectArea("TCK")
	DbSetOrder(1)
	If (Dbseek(xFilial("TCK") + cOrdem ))
   	cDesc := STR0013 + " " + AllTrim(TCK->TCK_CODPRO) + " " + STR0014 + " " + AllTrim(TCK->TCK_CODLEG)  // Alerta do protocolo XXXX relacionado ao requisito XXXXX
   EndIf
EndIf

Return cDesc

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	  ³ SgCarTAQ  ³ Autor ³ Thiago Olis Machado     ³ Data ³ 12/08/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao  ³ Carrega num array todas as pendencias do usuario.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		  ³ SIGASGA                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function SgCarTAQ(cUser)
Local aEvento  := {}
Local cEmenta := ""

DbSelectArea("TA0")
TA0->(DbSetOrder(1))

TAQ->(DbSeek(xFilial("TAQ") ))
While TAQ->(!Eof()) .and. xFilial("TAQ") == TAQ->TAQ_FILIAL

	If TA0->(DbSeek(xFilial("TA0")+TAQ->TAQ_ORDEM)) .And. TA0->TA0_SITUAC != "2" //Se for um evento ativo

		If Empty(TAQ->TAQ_USUFIM)
			If TAQ->TAQ_USUARI == cUser .or. ALLTRIM(TAQ->TAQ_USUARI) == "*"

				// Caso for alerta de protocolo de requisito tipo 7 exibir caso a data de alerta >= database
				If TAQ->TAQ_TIPO == "7"
					If TAQ->TAQ_DATA <= dDataBase
						aAdd( aEvento,{ TAQ->TAQ_PENDEN, TAQ->TAQ_DESC, TAQ->TAQ_ORDEM, TAQ->TAQ_MOSTRA, TAQ->TAQ_DATA, TAQ->TAQ_TIPO } )
					EndIf
				Else
					cEmenta := Alltrim(TAQ->TAQ_DESC)

					If TAQ->TAQ_TIPO $ "4,5"
						If TA0->(DbSeek(xFilial("TA0")+TAQ->TAQ_ORDEM))
							If ( TA0->TA0_DTVENC - TA0->TA0_DTVCTO ) > dDataBase
								TAQ->(DbSkip())
								Loop
							EndIf
							cEmenta := STR0015+": "+ Alltrim(TA0->TA0_EMENTA)
						Endif
					Endif

					aAdd( aEvento,{ TAQ->TAQ_PENDEN, cEmenta, TAQ->TAQ_ORDEM, TAQ->TAQ_MOSTRA, TAQ->TAQ_DATA, TAQ->TAQ_TIPO } )

				Endif

			EndIf
		EndIf

	EndIf
	TAQ->(DbSkip())
End
Return aEvento

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	  ³Sg160Grava ³ Autor ³ Thiago Olis Machado     ³ Data ³ 12/08/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao  ³ Grava no Usuario que ele nao quer ver as mensagens.           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		  ³ SIGASGA                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function Sg160Grava(cUser)

If TAQ->(DbSeek(xFilial("TAQ")+cUser))
	If lUmaVez
		RecLock("TAQ",.f.)
		TAQ->TAQ_MOSTRA := 'N'
		MsUnLock("TAQ")
	EndIf
EndIf
oDlgEve:End()
Return .t.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	  ³ Sg160_Rea ³ Autor ³ Thiago Olis Machado     ³ Data ³ 12/08/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao  ³ Mostra os cadastros para o usuario baixar as pendencias.      ³±±
±±³			  ³ 																			      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		  ³ SIGASGA                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Sg160_Rea(lChama)
Local nOpcao := 0
Private lCheckTJE := AliasIndic("TJE")

Setaltera(.T.)

If Len(aEventos) == 0
	MsgAlert("A lista de eventos está vazia.","Atenção!")
	Return .F.
EndIf

If !lChama
	nOpcao := 1
EndIf
CursorWait()
If aEventos[oList:nAt,6] == '1'
	DbSelectArea("TAB")
	DbSetOrder(1)
	Dbseek(xFilial("TAB")+PadR( aEventos[oList:nAt,3], TamSX3("TAB_ORDEM")[1] ) )
	If lChama
		nOpcao := Sg110Pro(Alias(),Recno(),4)
	EndIf
ElseIf aEventos[oList:nAt,6] == '2'
	If lChama
		If QdoView( , PadR( aEventos[oList:nAt,3], 16 ) )
			nOpcao := 1
		EndIf
	EndIf
ElseIf aEventos[oList:nAt,6] == '3'
	aRotina := {	{ "Pesquisar" , "AxPesqui"  , 0 , 1},;
                  { "Visualizar" , "Sg090Ca"   , 0 , 2},;
                  { "Incluir" , "Sg090Ca"   , 0 , 3},;
                  { "Alterar" , "Sg090Ca"   , 0 , 4},;
                  { "Excluir" , "Sg090Ca"   , 0 , 5, 3}}
	cCadastro := OemtoAnsi("Plano de Acao")

	DbSelectArea("TAA")
	DbSetOrder(1)
	Dbseek(xFilial("TAA")+PadR( aEventos[oList:nAt,3], TamSX3("TAA_CODPLA")[1] ) )
	If lChama
		nOpcao := Sg090Ca(Alias(),Recno(),4)
	EndIf
ElseIf aEventos[oList:nAt,6] == '4'
	aRotina := {	{ "Pesquisar" , "AxPesqui"  , 0 , 1},;
 						{ "Visualizar" , "Sg010Ca"   , 0 , 2},;
                  { "Incluir" , "Sg010Ca"   , 0 , 3},;
                  { "Alterar" , "Sg010Ca"   , 0 , 4},;
                  { "Excluir" , "Sg010Ca"   , 0 , 5, 3}}
	cCadastro := OemtoAnsi("Requisitos Legais e Outros")

	DbSelectArea("TA0")
	DbSetOrder(1)
	Dbseek(xFilial("TA0")+PadR( aEventos[oList:nAt,3], TamSX3("TA0_CODLEG")[1] ) )
	If lChama
		INCLUI := .F.
		ALTERA := .T.
		nOpcao := Sg010Ca(Alias(),Recno(),4)
	EndIf
ElseIf aEventos[oList:nAt,6] == '5'

	aRotina := {	{ "Pesquisar" , "AxPesqui"  , 0 , 1},;
 						{ "Visualizar" , "Sg010Ca"   , 0 , 2},;
                  { "Incluir" , "Sg010Ca"   , 0 , 3},;
                  { "Alterar" , "Sg010Ca"   , 0 , 4},;
                  { "Excluir" , "Sg010Ca"   , 0 , 5, 3}}
	cCadastro := OemtoAnsi("Requisitos Legais e Outros")

	DbSelectArea("TA0")
	DbSetOrder(1)
	Dbseek(xFilial("TA0")+PadR( aEventos[oList:nAt,3], TamSX3("TA0_CODLEG")[1] ) )
	If lChama
		INCLUI := .F.
		ALTERA := .T.
		nOpcao := Sg010Ca(Alias(),Recno(),4)
	EndIf
ElseIf aEventos[oList:nAt,6] == '6'

	DbSelectArea("TB2")
	DbSetOrder(1)
	Dbseek(xFilial("TB2")+PadR( aEventos[oList:nAt,3], TamSX3("TB2_CODDES")[1] ) )
	If lChama
		INCLUI := .F.
		ALTERA := .T.
		nOpcao := If(SGA170INC(Alias(),Recno(),4),1,0)
	EndIf
ElseIf aEventos[oList:nAt,6] == '7'
	DbSelectArea("TA0")
	DbSetOrder(1)
	Dbseek(xFilial("TA0")+PadR( aEventos[oList:nAt,3], TamSX3("TA0_CODLEG")[1] ) )

	DbSelectArea("TCK")
	DbSetOrder(1)
	Dbseek(xFilial("TCK")+PadR( aEventos[oList:nAt,3], TamSX3("TCK_CODLEG")[1]+TamSX3("TCK_CODPRO")[1]+TamSX3("TCK_DTENTR")[1] ) )
	If lChama
		If TCK->TCK_DTALER <= dDataBase .And. TCK->TCK_STATUS == "1"
			INCLUI := .F.
			ALTERA := .T.
			nOpcao := If(SG010PRO(Alias(),Recno(),4),1,0)
		EndIf
	EndIf
Endif

If nOpcao == 1
	cPendencia := aEventos[oList:nAt][1]
	TAQ->(DbSelectArea("TAQ"))
	TAQ->(DbSetOrder(2))
	If TAQ->(DbSeek(xFilial("TAQ")+cPendencia))
		RecLock("TAQ",.f.)
		TAQ->TAQ_USUFIM := PadR( Upper( SubStr( cUsuario, 7, 15 ) ), 15 )
		MsUnLock("TAQ")
	EndIf
	aDel( aEventos,oList:nAt)
	aSize( aEventos, Len( aEventos )-1)
EndIf

oList:Refresh()
CursorArrow()
Return .t.
