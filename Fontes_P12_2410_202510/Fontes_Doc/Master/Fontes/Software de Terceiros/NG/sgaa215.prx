#Include "Protheus.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ SGAA215  ³ Autor ³ Rafael Diogo Richter  ³ Data ³26/09/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para Retorno das Opcoes dos CheckLists Executados ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßPßßßßßß
/*/
Function SGAA215()

	Local i
	Local nx
	Local nI         := 0
	Local GetList    := {}
	Local aPosCpo    := {}
	Local aExc       := { { 'TBS_OK'    , 'X3_TITULO' , 'Ok'          }, ;
						  { 'TBS_OK'    , 'X3_TAMANHO', 3             }, ;
						  { 'TBR_RESPOS', 'X3_VALID'  , 'ValidaInf()' }  }
	Local aCampos    := { 'TBS_OK', 'TBT_OPCAO', 'TBE_DESOPC', 'TBR_RESPOS' }
	Local nFld       := 0
	Local cField     := ''
	Local aTField    := {}
	Local nUsado     := 0
	Local nCnt       := 0

	Private nItens   := 0
	Private aHeader  := {}
	Private oDlg3, oGet, cCodAnt, nnOPCA, nOk, oPnl3
	Private aRotina := MenuDef()

	SetKey( VK_F9, { | | NGVersao( "SGAA215" , 1 ) } )

	ccALIAS := ALIAS()

	DbSelectArea("TBN")
	DbSetOrder(1)
	DbSeek(xFilial('TBN')+(cAliasCE)->TBQ_ORDEM+(cAliasCE)->TBQ_PLANO)

	cordem	 := TBN->TBN_CODORD
	cplano	 := TBN->TBN_CODPLAN
	csequenc := TBN->TBN_SEQUEN
	ccodchk	 := (cAliasCE)->TBQ_CHKLIS

	DbSelectArea("TBD") //Checklist do Plano Emergencial
	DbSetOrder(1)
	If DbSeek(xFilial('TBD')+ccodchk) .And. !Empty((cAliasCE)->TBQ_OK)

		If TBD->TBD_TIPOPC <> '1'

			If TBD->TBD_TIPOPC = '2'
				ctopcao  := "Exclusiva"
				ltpopcao := '2'
			Else
				ctopcao := "Varias "
				ltpopcao := '3'
			EndIf

			cnomchk := substr(TBD->TBD_DESCHK,1,80)

			M->TBR_RESPOS := SPACE(10)
			M->TBT_TIPRES := "2"
			LEOPCAO       := .F.
			nOpcx         := 3

			DbSelectArea('TBE') //Opção do Checklist
			DbSetOrder(1)
			If DbSeek(xFilial('TBE')+ccodchk)

				nOpcx   := 3

				//Montando aHeader
				aHeader := NGHeadExc( aCampos, .F., , , aExc )
				nUsado  := Len(aHeader)

				nOk     := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TBS_OK"     }) //Histórico dos Checklist Exec
				nnOPCA  := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TBT_OPCAO"  }) //Opções do Checklist da Sim
				ndesOpc	:= aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TBE_DESOPC" }) //Opção do Checklist
				nRESPOS := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TBR_RESPOS" }) //Opções dos Checklist Execut

				DbSetOrder(1)

				DbSelectArea('TBE')
				DbSetOrder(1)
				DbSeek(xFilial('TBE')+ccodchk)

				While TBE->(!Eof()) .And. TBE->TBE_FILIAL == xFilial('TBE') .And. ;
					ccodchk == TBE->TBE_CODCHK
					nCnt++
					TBE->(Dbskip())
				EndDo

				//Se houverem opções do CheckList
				If nCnt > 0

					aCols := Array(nCnt,nUsado+1)

					DbSelectArea('TBE')
					DbSetOrder(1)
					DbSeek(xFilial('TBE')+ccodchk)

					nCnt := 0

					While TBE->(!Eof()) .And. TBE->TBE_FILIAL == xFilial('TBE') .And.;
						ccodchk == TBE->TBE_CODCHK

						nCnt++

						//Montando aCols
						DbSelectArea("TBR")
						DbSetOrder(1)
						DbSeek(xFilial("TBR")+cordem+cplano+ccodchk)
						cRes := ''
						While TBR->(!Eof()) .And. TBR->TBR_ORDEM == cOrdem .And.;
							TBR->TBR_PLANO == cPlano .And. TBR->TBR_CHKLIS == ccodchk

							If AllTrim(TBR->TBR_OPCAO) == AllTrim(TBE->TBE_CODOPC)
								cRes := TBR->TBR_RESPOS
								Exit
							EndIf

							TBR->(DbSkip())

						EndDo

						aCOLS[nCnt][nnOPCA]  := TBE->TBE_CODOPC
						aCOLS[nCnt][nDesOpc] := TBE->TBE_DESOPC
						aCOLS[nCnt][nOK]     := SPACE(01)

						If TBE->TBE_TIPRES = '1'
							DbSelectArea("TBR")
							DbSetOrder(2)
							If DbSeek(xFilial("TBR")+cordem+ccodchk+TBE->TBE_CODOPC)
								aCOLS[nCnt][nOK] := 'X'
							Endif
						Endif

						If !Empty(cRes)
							aCOLS[nCnt][nOK] := 'X'
							aCOLS[nCnt][nRESPOS] := cRes
						Else
							aCOLS[nCnt][nRESPOS] := Space(10)
						EndIf

						nItens++
						lRefresh := .T.

						TBE->(DbSkip())

					EndDo

				EndIf

				//Variaveis do Rodape do Modelo 2
				nLinGetD:=0
				//Titulo da Janela
				cTitulo := "Retorno Opções CheckList"

				cTudoOk  :="Sg215Tudo()"
				cLinhaOk :="Sg215LinOk()"

				While .T.

					nOPCAO := 1
					DEFINE MSDIALOG oDlg3 TITLE OemToAnsi(cTitulo) From 6.5,10 To 31,90 OF oMainWnd

						oPnl3 := tPanel():New(01,01,,oDlg3,,,,CLR_BLACK,CLR_WHITE,100,100)
						oPnl3:Align := CONTROL_ALIGN_ALLCLIENT

						@ 0.2,0.2		SAY OemToAnsi("Ordem") Of oPnl3
						@ 0.2,3.3		MSGET cordem When .f. Of oPnl3
						@ 0.2,10   		SAY OemToAnsi("Plano") Of oPnl3
						@ 0.2,12.2  	MSGET cplano  When .f. Of oPnl3
						@ 0.2,19   		SAY OemToAnsi("Opcao") Of oPnl3
						@ 0.2,21.4  	MSGET ctopcao When .f. Of oPnl3

						@ 1.2,0.2   	SAY OemToAnsi("CheckList") Of oPnl3
						@ 1.2,3.3   	MSGET ccodchk  When .f. Of oPnl3
						@ 1.2,10   		SAY OemToAnsi("Nome") Of oPnl3
						@ 1.2,12.2  	MSGET SubStr(cnomchk,1,40) When .f. size 200,8 Of oPnl3

						oGet := MSGetDados():New(30,2,163,317,nOpcx,"Sg215LinOk","Sg215Tudo","",.F.,{"TBS_OK","TBR_RESPOS"},,,nItens,,,,,oPnl3)

					ACTIVATE MSDIALOG oDlg3 ON INIT EnchoiceBar(oDlg3,{||nopcao:=1,If(!Sg215Tudo(),nOPCAO := 0,oDlg3:End())},{||oDlg3:End()})

					If nOPCAO == 1

						DbSelectArea(cAliasOC)
						If DbSeek((cAliasCE)->TBQ_CHKLIS)
							While !eof() .and. (cAliasOC)->TBE_CODCHK == (cAliasCE)->TBQ_CHKLIS
								RecLock((cAliasOC),.F.)
								DbDelete()
								MsUnLock(cAliasOC)

								DbSelectArea(cAliasOC)
								Dbskip(1)
							End
						Endif

						For nx := 1 to nItens
							If !empty(aCols[nx][nOK])
								DbSelectArea(cAliasOC)
								If !DbSeek(ccodchk+aCols[nx][nnOPCA])
									DbSelectArea("TBE")
									DbSetOrder(1)
									If DbSeek(xFilial('TBE')+ccodchk+aCols[nx][nnOPCA])
									DbSelectArea(cAliasOC)
									RecLock((cAliasOC),.T.)
									DbSelectArea("TBE")
									For i := 1 TO FCount()
										x   := "TBE->" + FieldName(i)
										y   := "(cAliasOC)->" + FieldName(i)
										Replace &y. with &x.
									Next i

									(cAliasOC)->TBE_RESPOS := aCols[nx][nRESPOS]
									(cAliasOC)->TBE_OK     := 'XX'

									MsUnLock(cAliasOC)
									Endif
								Endif
							Endif
						Next
					EndIf

					Exit

				EndDo
			Endif
		Endif
	Else
		DbSelectArea(cAliasOC)
		DbSetOrder(1)
		If DbSeek(ccodchk)
			While !Eof() .And. (cAliasOC)->TBE_CODCHK = ccodchk
				(cAliasOC)->TBE_RESPOS := Space(Len((cAliasOC)->TBE_RESPOS))
				(cAliasOC)->TBE_OK     := Space(2)
				Dbskip()
			End
		Endif
	Endif

	DbSelectArea(ccALIAS)

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Sg215Tudo³ Autor ³ Rafael Diogo Richter  ³ Data ³30/09/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consistencia final                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg215Tudo()

	Local yx
	For yx := 1 to nItens
		dbSelectArea("TBE")
		dbSetOrder(1)
		If dbSeek(xFilial("TBE")+cCodChk+aCols[yx][nnOPCA])
			If !Empty(aCols[yx][nOK]) .and. TBE->TBE_TIPRES == '2' .and. Empty(aCols[yx][nRESPOS])
			MsgInfo("Informe a resposta do item "+AllTrim(aCols[yx][nnOPCA]),"Atenção")
		Return .f.
			Endif
		EndIf
	Next

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Sg215LinOk³ Autor ³ Rafael Diogo Richter  ³ Data ³30/09/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consistencia da linha                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Sg215LinOk

	If n > nItens
	Return .f.
	Endif

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ValidaOk ³ Autor ³ Rafael Diogo Richter  ³ Data ³30/09/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Marca,  desmarca consistencia de leitura de campos          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ValidaOk

	Local xx := 0
	nn := n

	If !Empty(aCols[nn][nOK])
		M->TBS_OK := space(1)
	Else
		M->TBS_OK := 'X'
	Endif

	aCols[nn][nOK] := M->TBS_OK
	lRefresh := .t.
	If ltpopcao = "2"
	If !Empty(M->TBS_OK)
		For xx := 1 to nItens
			If xx <> nn
			If !empty(aCols[xx][nOK])
				HELP(" ",1,"NGMUOPCEXC") //"Somente uma opcao podera ser marcada na etapa exclusiva.. "###"Atencao"

				M->TBS_OK := space(1)
				aCols[nn][nOK] := M->TBS_OK
				lRefresh := .t.
				n := nn
				Return .f.
			Endif
			Endif
		Next
	Else
		DbSelectArea("TBE")
			DbSetOrder(1)
			If DbSeek(xFilial("TBE")+cCodChk)
			If TBE->TBE_TIPRES == '2'
				aCols[nn][nRESPOS] := Space(len(TBT->TBT_CONDIN))
				lRefresh := .t.
		Endif
		EndIf
	Endif
	Else
		DbSelectArea("TBE")
		DbSetOrder(1)
		If DbSeek(xFilial("TBE")+cCodChk)
		If Empty(M->TBS_OK) .and. TBE->TBE_TIPRES == '2'
			aCols[nn][nRESPOS] := Space(len(TBT->TBT_CONDIN))
			lRefresh := .t.
		Endif
		EndIf
	Endif

	ValidaInf()

Return .F.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ValidaInf ³ Autor ³ Rafael Diogo Richter  ³ Data ³30/09/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Consistencia de leitura de campos                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ValidaInf

	DbSelectArea("TBE")
	DbSetOrder(1)
	If DbSeek(xFilial("TBE")+ccodchk+aCols[n][2])
		If TBE->TBE_TIPRES == '1' .or.  Empty(aCols[n][nOK]) .and. TBE->TBE_TIPRES == '2'
		Return .f.
		EndIf
	Endif

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef  ³ Autor ³ Rafael Diogo Richter  ³ Data ³29/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Utilizacao de Menu Funcional.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()

	Local aRotina :=	{{ "Pesquisar" , "AxPesqui" , 0 , 1},;
						 { "Visualizar", "NGCAD02"  , 0 , 2},;
						 { "Incluir"   , "SGAA215"  , 0 , 3}}

Return aRotina

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³   Sg215Inf   ³ Autor ³ Rafael Diogo Richter  ³ Data ³25/05/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para validar o GetDados.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Sg215Inf()

	dbSelectArea("TBE")
	dbSetOrder(1)
	dbSeek(xFilial("TBE")+(cAliasCE)->TBQ_CHKLIS+aCols[n][nnOPCA])

	If Empty(aCols[n][nOK]) .Or. TBE->TBE_TIPRES == "1"
	Return .F.
	Endif

Return .T.