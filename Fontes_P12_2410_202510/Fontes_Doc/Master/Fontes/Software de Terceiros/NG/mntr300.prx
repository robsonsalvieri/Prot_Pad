#INCLUDE "MNTR300.CH"
#INCLUDE "PROTHEUS.CH"

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTR300
Custo por Familia de Bens

@author  Deivys Joenck
@since   13/08/01
@version P11/P12
/*/
//-------------------------------------------------------------------
Function MNTR300()

	// Guarda conteudo e declara variaveis padroes
	Local aNGBEGINPRM := NGBEGINPRM()
	Local aArea       := GetArea()
	Local oReport

	Private cTRB      := GetNextAlias()
	Private vVETHORAS := {}

	//+--------------------------------------------------------------+
	//| Variaveis utilizadas para parametros!                        |
	//| mv_par01     // De  Familia                                  |
	//| mv_par02     // Ate Familia                                  |
	//| mv_par03     // De  Frota                                    |
	//| mv_par04     // Ate Frota                                    |
	//| mv_par05     // De  Data                                     |
	//| mv_par06     // Ate Data                                     |
	//| mv_par07     // Tipo Relatorio (Resumido/Detalhado)          |
	//| mv_par08     // Tipo Moeda                                   |
	//| mv_par09     // De  Grupo Lubrific.?                         |
	//| mv_par10     // Ate Grupo Lubrific.?                         |
	//| mv_par11     // Tipo de Custo (1-Medio,2-Stantard)           |
	//+--------------------------------------------------------------+

	If FindFunction("TRepInUse") .And. TRepInUse()
		//-- Interface de impressao

		Pergunte("MNT300", .T. )

		oReport := ReportDef()
		oReport:SetPortrait() //Default Retrato
		oReport:PrintDialog()
	Else
		MNTR300R3()
	EndIf
	RestArea(aArea)

    // Retorna conteudo de variaveis padroes
	NGRETURNPRM(aNGBEGINPRM)
Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} ReportDef
Define as secoes impressas no relatorio

@author  Elisangela Costa
@since   22/06/06
@version P11/P12
@return  oReport, objeto de responsvel pela impresso
/*/
//-------------------------------------------------------------------
Static Function ReportDef()
	Local oReport
	Local oSection1, oSection2
	Local oCell, oCell2
	Local oTotaliz

	Pergunte( "MNT300", .F. )

	//LAYOUT DO RELATORIO RESUMIDO
	/*
	1         2         3         4         5         6         7         8         9        10       110       120
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789
	___________________________________________________________________________________________________________________________________________
	Custo por Familia de Bens
	___________________________________________________________________________________________________________________________________________

	Familia Descricao                 QTD O.S M.D.O Propria M.D.O Terceiro Mat. Proprio Mat. Terceiro   Oleo Lub.  Vl. Total R$    Vl. Total $
	___________________________________________________________________________________________________________________________________________

	xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx     999     99,999.99      99,999.99    99,999.99     99,999.99   99,999.99 99,999,999.99  99,999,999.99
	xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx     999     99,999.99      99,999.99    99,999.99     99,999.99   99,999.99 99,999,999.99  99,999,999.99

	TOTAL GERAL          QTD O.S M.D.O Propria M.D.O Terceiro Mat. Proprio  Mat. Terceiro  Oleo Lub.  Vl. Total R$    Vl. Total $
	_________________________________________________________________________________________________________________________________________

	999         99,999.99      99,999.99    99,999.99     99,999.99   99,999.99 99,999,999.99  99,999,999.99


	*/


	//LAYOUT DO RELATORIO DETALHADO
	/*
	1         2         3         4         5         6         7         8         9        10       110       120
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123012345678901230123
	______________________________________________________________________________________________________________________________________________
	Custo por Familia de Bens
	______________________________________________________________________________________________________________________________________________
	Familia Descricao
	______________________________________________________________________________________________________________________________________________
	XXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

	Bem         Descricao                 QTD O.S M.D.O Propria M.D.O Terceiro Mat. Proprio Mat. Terceiro   Oleo Lub.  Vl. Total R$    Vl. Total $
	______________________________________________________________________________________________________________________________________________
	xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx     999     99,999.99      99,999.99    99,999.99     99,999.99   99,999.99 99,999,999.99  99,999,999.99
	xxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx     999     99,999.99      99,999.99    99,999.99     99,999.99   99,999.99 99,999,999.99  99,999,999.99

	TOTAL FAMILIA
	______________________________________________________________________________________________________________________________________

	999         99,999.99      99,999.99    99,999.99     99,999.99   99,999.99 99,999,999.99  99,999,999.99



	TOTAL GERAL
	______________________________________________________________________________________________________________________________________

	999         99,999.99      99,999.99    99,999.99     99,999.99   99,999.99 99,999,999.99  99,999,999.99


	*/

	//Ŀ
	//Criacao do componente de impressao                                      
	//                                                                        
	//TReport():New                                                           
	//ExpC1 : Nome do relatorio                                               
	//ExpC2 : Titulo                                                          
	//ExpC3 : Pergunte                                                        
	//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
	//ExpC5 : Descricao                                                       
	//                                                                        
	//
	oReport := TReport():New("MNTR300",OemToAnsi(STR0001),"MNT300",{|oReport| ReportPrint(oReport)},STR0001+". "+STR0002+" "+STR0008)
	oReport:ParamReadOnly()

	//Ŀ
	//Criacao da secao utilizada pelo relatorio                               
	//                                                                        
	//TRSection():New                                                         
	//ExpO1 : Objeto TReport que a secao pertence                             
	//ExpC2 : Descricao da seao                                              
	//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
	//        sera considerada como principal para a seo.                   
	//ExpA4 : Array com as Ordens do relatrio                                
	//ExpL5 : Carrega campos do SX3 como celulas                              
	//        Default : False                                                 
	//ExpL6 : Carrega ordens do Sindex                                        
	//        Default : False                                                 
	//                                                                        
	//
	//Ŀ
	//Criacao da celulas da secao do relatorio                                
	//                                                                        
	//TRCell():New                                                            
	//ExpO1 : Objeto TSection que a secao pertence                            
	//ExpC2 : Nome da celula do relatrio. O SX3 ser consultado              
	//ExpC3 : Nome da tabela de referencia da celula                          
	//ExpC4 : Titulo da celula                                                
	//        Default : X3Titulo()                                            
	//ExpC5 : Picture                                                         
	//        Default : X3_PICTURE                                            
	//ExpC6 : Tamanho                                                         
	//        Default : X3_TAMANHO                                            
	//ExpL7 : Informe se o tamanho esta em pixel                              
	//        Default : False                                                 
	//ExpB8 : Bloco de cdigo para impressao.                                 
	//        Default : ExpC2                                                 
	//                                                                        
	//

	//Defenicao das secoes do relatorio
	oReport:SetTotalInLine(.F.)
	If MV_PAR07 == 1  //RESUMIDO

		oSection1 := TRSection():New(oReport,STR0024,{(cTRB),"ST6"}) //Familias
		TRCell():New(oSection1,"CODFAMI",     ,STR0012 ,"@!"              ,6,/*lPixel*/,{||NGRCODFAM()}) //"Familia"
		TRCell():New(oSection1,"T6_NOME","ST6",STR0013 ,"@!"              ,25,/*lPixel*/)                //"Descrio"
		TRCell():New(oSection1,"REFORM" ,     ,STR0014 ,"9999"            ,4,/*lPixel*/ ,{||NGRETVAL(1)}) //"Quant. O.S"
		TRCell():New(oSection1,"MDOPRO" ,     ,STR0015 ,"@E 9,999,999.99" ,12,/*lPixel*/ ,{||NGRETVAL(2)}) //"M.D.O Propria"
		TRCell():New(oSection1,"MDOTER" ,     ,STR0016 ,"@E 9,999,999.99" ,12,/*lPixel*/ ,{||NGRETVAL(3)}) //"M.D.O Terceiro"
		TRCell():New(oSection1,"MATPRO" ,     ,STR0017 ,"@E 9,999,999.99" ,12,/*lPixel*/ ,{||NGRETVAL(4)}) //"Mat. Proprio"
		TRCell():New(oSection1,"MATTER" ,     ,STR0018 ,"@E 9,999,999.99" ,12,/*lPixel*/ ,{||NGRETVAL(5)}) //"Mat. Terceiro"
		TRCell():New(oSection1,"OLEO"   ,     ,STR0019 ,"@E 9,999,999.99" ,12,/*lPixel*/ ,{||NGRETVAL(6)}) //"Oleo Lub."
		TRCell():New(oSection1,"VALR"   ,     ,STR0020 ,"@E 99,999,999.99",10,/*lPixel*/,{||NGRETVAL(7)}) //"Vl. Total R$"
		TRCell():New(oSection1,"TOTMOE" ,     ,STR0021 ,"@E 99,999,999.99",10,/*lPixel*/,{||NGRETVAL(8)}) //"VL. Total R$"
		//Definicao para imprimir os cabecalhos de campos numericos da esquerda para a
		//direita
		oSection1:Cell("REFORM"):SetHeaderAlign("RIGHT")
		oSection1:Cell("MDOPRO"):SetHeaderAlign("RIGHT")
		oSection1:Cell("MDOTER"):SetHeaderAlign("RIGHT")
		oSection1:Cell("MATPRO"):SetHeaderAlign("RIGHT")
		oSection1:Cell("MATTER"):SetHeaderAlign("RIGHT")
		oSection1:Cell("OLEO"):SetHeaderAlign("RIGHT")
		oSection1:Cell("VALR"):SetHeaderAlign("RIGHT")
		oSection1:Cell("TOTMOE"):SetHeaderAlign("RIGHT")
		TRPosition():New(oSection1,"ST6",1,{|| xFilial("ST6") + cCODFAMI})

		oTotaliz := TRFunction():New(oSection1:Cell("REFORM"),"TOTREF"   ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)
		oTotaliz := TRFunction():New(oSection1:Cell("MDOPRO"),"TOTMPR"   ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)
		oTotaliz := TRFunction():New(oSection1:Cell("MDOTER"),"TOTTER"   ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)
		oTotaliz := TRFunction():New(oSection1:Cell("MATPRO"),"TOTMAP"   ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)
		oTotaliz := TRFunction():New(oSection1:Cell("MATTER"),"TOTMTE"   ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)
		oTotaliz := TRFunction():New(oSection1:Cell("OLEO")  ,"TOTOLE"   ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)
		oTotaliz := TRFunction():New(oSection1:Cell("VALR")  ,"TOTVTO"   ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)
		oTotaliz := TRFunction():New(oSection1:Cell("TOTMOE"),"TOTMOET"  ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)

	Else //DETALHADO
		oSection1 := TRSection():New(oReport,STR0012,{(cTRB),"ST6"}) //Familia
		oSection1:SetHeaderBreak() // sempre que houver quebra imprime o cabealho da seo
		TRCell():New(oSection1,"(cTRB)->FAMILI",(cTRB),STR0012,"@!" ,6,/*lPixel*/,/*{|| code-block de impressao }*/) //"Familia"
		TRCell():New(oSection1,"T6_NOME"    ,"ST6",STR0013,"@!" ,40,/*lPixel*/)   //"Descrio"
		TRPosition():New(oSection1,"ST6",1,{|| xFilial("ST6") + (cTRB)->FAMILI})

		oSection2 := TRSection():New(oReport,STR0025,{(cTRB)}) //Bens
		oCell := TRCell():New(oSection2,"(cTRB)->CODBEM",(cTRB) ,STR0022,"@!"           ,10,/*lPixel*/,/*{|| code-block de impressao }*/) //"Bem"
		oCell := TRCell():New(oSection2,"(cTRB)->NOMBEM",(cTRB) ,STR0013,"@!"           ,20,/*lPixel*/,/*{|| code-block de impressao }*/) //"Descrio"
		oCell := TRCell():New(oSection2,"(cTRB)->REFORM",(cTRB) ,STR0014,"9999"         ,3 ,/*lPixel*/,/*{|| code-block de impressao }*/) //"Quant. O.S"
		oCell := TRCell():New(oSection2,"(cTRB)->MDOPRO",(cTRB) ,STR0015,"@E 9,999,999.99",12,/*lPixel*/,/*{|| code-block de impressao }*/)  //"M.D.O Propria"
		oCell := TRCell():New(oSection2,"(cTRB)->MDOTER",(cTRB) ,STR0016,"@E 9,999,999.99",12,/*lPixel*/,/*{|| code-block de impressao }*/)  //"M.D.O Terceiro"
		oCell := TRCell():New(oSection2,"(cTRB)->MATPRO",(cTRB) ,STR0017,"@E 9,999,999.99",12,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Mat. Proprio"
		oCell := TRCell():New(oSection2,"(cTRB)->MATTER",(cTRB) ,STR0018,"@E 9,999,999.99",12,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Mat. Terceiro"
		oCell := TRCell():New(oSection2,"(cTRB)->OLEO"  ,(cTRB) ,STR0019,"@E 9,999,999.99",12,/*lPixel*/,/*{|| code-block de impressao }*/)  //"Oleo Lub."
		oCell := TRCell():New(oSection2,"VALR"       ,      ,STR0020, "@E 99,999,999.99",13,/*lPixel*/,{||NGRETVAL(7)})               //"Vl. Total R$"
		oCell := TRCell():New(oSection2,"(cTRB)->MOEDA" ,(cTRB) ,STR0021,"@E 99,999,999.99",13,/*lPixel*/,/*{|| code-block de impressao }*/) //"VL. Total R$"
		//Definicao para imprimir os cabecalhos de campos numericos da esquerda para a
		//direita
		oSection2:Cell("(cTRB)->REFORM"):SetHeaderAlign("RIGHT")
		oSection2:Cell("(cTRB)->MDOPRO"):SetHeaderAlign("RIGHT")
		oSection2:Cell("(cTRB)->MDOTER"):SetHeaderAlign("RIGHT")
		oSection2:Cell("(cTRB)->MATPRO"):SetHeaderAlign("RIGHT")
		oSection2:Cell("(cTRB)->MATTER"):SetHeaderAlign("RIGHT")
		oSection2:Cell("(cTRB)->OLEO"):SetHeaderAlign("RIGHT")
		oSection2:Cell("VALR"):SetHeaderAlign("RIGHT")
		oSection2:Cell("(cTRB)->MOEDA"):SetHeaderAlign("RIGHT")

		oBreak := TRBreak():New(oSection1,oSection1:Cell("(cTRB)->FAMILI"),STR0023,.F.)//"TOTAL FAMILIA"
		TRFunction():New(oSection2:Cell("(cTRB)->REFORM"),/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
		TRFunction():New(oSection2:Cell("(cTRB)->MDOPRO"),/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
		TRFunction():New(oSection2:Cell("(cTRB)->MDOTER"),/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
		TRFunction():New(oSection2:Cell("(cTRB)->MATPRO"),/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
		TRFunction():New(oSection2:Cell("(cTRB)->MATTER"),/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
		TRFunction():New(oSection2:Cell("(cTRB)->OLEO")  ,/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
		TRFunction():New(oSection2:Cell("VALR")       ,/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)
		TRFunction():New(oSection2:Cell("(cTRB)->MOEDA") ,/*cId*/,"SUM",oBreak,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.F./*lEndReport*/,.F./*lEndPage*/)

		oTotaliz := TRFunction():New(oSection2:Cell("(cTRB)->REFORM"),"TOTREF"   ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)
		oTotaliz := TRFunction():New(oSection2:Cell("(cTRB)->MDOPRO"),"TOTMPR"   ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)
		oTotaliz := TRFunction():New(oSection2:Cell("(cTRB)->MDOTER"),"TOTTER"   ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)
		oTotaliz := TRFunction():New(oSection2:Cell("(cTRB)->MATPRO"),"TOTMAP"   ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)
		oTotaliz := TRFunction():New(oSection2:Cell("(cTRB)->MATTER"),"TOTMTE"   ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)
		oTotaliz := TRFunction():New(oSection2:Cell("(cTRB)->OLEO")  ,"TOTOLE"   ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)
		oTotaliz := TRFunction():New(oSection2:Cell("VALR")       ,"TOTVTO"   ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)
		oTotaliz := TRFunction():New(oSection2:Cell("(cTRB)->MOEDA") ,"TOTMOET"  ,"SUM",/*oBreak*/,/*cTitle*/,/*cPicture*/,/*uFormula*/,.F./*lEndSection*/,.T./*lEndReport*/,.F./*lEndPage*/)
	EndIf

Return oReport

/*/


Ŀ
Funo     MNTR300R3 Autor  Deivys Joenck          Data  13/08/01 
Ĵ
Descrio  Custo por Familia de Bens                                  
Ĵ
 Uso       MNTR300                                                    
ٱ


*/
Function MNTR300R3()

	Local WNREL      := "MNTR300"
	Local LIMITE     := 132
	Local cDESC1     := STR0001 //"Custo por Familia de Bens"
	Local cDESC2     := STR0002
	Local cDESC3     := STR0008
	Local cSTRING    := "STJ"

	Private NOMEPROG := "MNTR300"
	Private TAMANHO  := "M"
	Private aRETURN  := {STR0003,1,STR0004,1,2,1,"",1}  //"Zebrado"###"Administracao"
	Private TITULO   := STR0001 //"Custo Por Familia de Bens."
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private cPERG    := "MNT300"
	Private CABEC1, CABEC2

	Pergunte(cPERG,.F.)

	//Ŀ
	// Envia controle para a funcao SETPRINT                        
	//
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("STJ")
		Return
	EndIf
	SetDefault(aRETURN,cSTRING)
	RptStatus({|lEND| MNTR300IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)
	DbSelectArea("STJ")
Return Nil

/*/


Ŀ
Funo    MNTR300IMP Autor  Deivys Joenck          Data  13/08/01 
Ĵ
Descrio  Chamada do Relatrio                                       
Ĵ
 Uso       MNTR300                                                    
ٱ


/*/
Function MNTR300IMP(lEND,WNREL,TITULO,TAMANHO)
	Local cRODATXT := ""
	Local nCNTIMPR := 0

	//Ŀ
	// Contadores de linha e pagina                                 
	//
	Private li := 80 ,m_pag := 1
	Private aVETINR := {}
	//Ŀ
	// Verifica se deve comprimir ou nao                            
	//
	nTIPO  := IIF(aRETURN[4]==1,15,18)

	/*/
	1         2         3         4         5         6         7         8         9         0         1         2         3
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901
	************************************************************************************************************************************
	Familia Descricao               Quant.     Mao Obra   Mao Obra   Material   Material  Oleo Lub.     Valor Total     Valor Total
	O.S     Propria    Terceiro   Proprio    Terceiro                R$              R$
	************************************************************************************************************************************
	xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx  999   99,999.99  99,999.99  99,999.99  99,999.99  99,999.99   99,999,999.99   99,999,999.99
	xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx  999   99,999.99  99,999.99  99,999.99  99,999.99  99,999.99   99,999,999.99
	xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxx  999   99,999.99  99,999.99  99,999.99  99,999.99  99,999.99   99,999,999.99
	/*/
	aDBFR300 := {{"FAMILI","C",06,0},;
				 {"CODBEM","C",16,0},;
				 {"NOMBEM","C",40,0},;
				 {"MDOPRO","N",12,2},;
				 {"MDOTER","N",12,2},;
				 {"MATPRO","N",12,2},;
				 {"MATTER","N",12,2},;
				 {"OLEO"  ,"N",12,2},;
				 {"MOEDA" ,"N",12,2},;
				 {"REFORM","N",04,0}}

	//Cria Tabela Temporria
	oARQTR300 := NGFwTmpTbl(cTRB,aDBFR300,{{"FAMILI","CODBEM"}})

	Processa({|lEND| MTR300TSTJ()},STR0011) //"Selecionando Registros..."

	If MV_PAR07 == 1
		CABEC1 := STR0005 //"Familia Descricao             Quant.     Mao Obra    Mao Obra    Material    Material    Oleo Lub.      Valor Total     Valor Total"
		CABEC2 := STR0006 //"                                 O.S      Propria    Terceiro     Proprio    Terceiro                   R$              R$"
		NGSOMALI(58)
		nTOTGVALR   := 0
		nTOTGREFORM := 0
		nTOTGMDOPRO := 0.00
		nTOTGMDOTER := 0.00
		nTOTGMATPRO := 0.00
		nTOTGMATTER := 0.00
		nTOTGOLEO   := 0.00
		nTOTGMOEDA  := 0.00

		DbSelectArea(cTRB)
		DbGotop()
		While !Eof()
			nVALR   := 0
			nREFORM := 0
			nMDOPRO := 0.00
			nMDOTER := 0.00
			nMATPRO := 0.00
			nMATTER := 0.00
			nOLEO   := 0.00
			nTOTMOE := 0.00
			cFAMILI := (cTRB)->FAMILI

			@ Li,000 Psay (cTRB)->FAMILI
			DbSelectArea("ST6")
			DbSetOrder(1)
			DbSeek(xFILIAL("ST6")+(cTRB)->FAMILI)
			@ Li,008 Psay SubStr(ST6->T6_NOME,1,25)
			While !Eof() .And. (cTRB)->FAMILI == cFAMILI
				nMDOPRO := nMDOPRO+(cTRB)->MDOPRO
				nMDOTER := nMDOTER+(cTRB)->MDOTER
				nMATPRO := nMATPRO+(cTRB)->MATPRO
				nMATTER := nMATTER+(cTRB)->MATTER
				nOLEO   := nOLEO  +(cTRB)->OLEO
				nREFORM := nREFORM+(cTRB)->REFORM
				nTOTMOE := nTOTMOE+(cTRB)->MOEDA
				DbSelectArea(cTRB)
				DbSkip()
			End
			nVALR := nMDOPRO+nMDOTER+nMATPRO+nMATTER+nOLEO
			@ Li,035 Psay nREFORM Picture "9999"
			@ Li,041 Psay nMDOPRO Picture "@E 9,999,999.99"
			@ Li,052 Psay nMDOTER Picture "@E 9,999,999.99"
			@ Li,063 Psay nMATPRO Picture "@E 9,999,999.99"
			@ Li,074 Psay nMATTER Picture "@E 9,999,999.99"
			@ Li,085 Psay nOLEO   Picture "@E 9,999,999.99"
			@ Li,097 Psay nVALR   Picture "@E 99,999,999.99"
			@ Li,113 Psay nTOTMOE Picture "@E 99,999,999.99"
			NGSOMALI(58)
			nTOTGREFORM := nTOTGREFORM+nREFORM
			nTOTGMDOPRO := nTOTGMDOPRO+nMDOPRO
			nTOTGMDOTER := nTOTGMDOTER+nMDOTER
			nTOTGMATPRO := nTOTGMATPRO+nMATPRO
			nTOTGMATTER := nTOTGMATTER+nMATTER
			nTOTGOLEO   := nTOTGOLEO  +nOLEO
			nTOTGVALR   := nTOTGVALR  +nVALR
			nTOTGMOEDA  := nTOTGMOEDA +nTOTMOE
		End
		NGSOMALI(58)
		@ Li,016 Psay STR0009     Picture "@!"  //"TOTAL"
		@ Li,035 Psay nTOTGREFORM Picture "9999"
		@ Li,041 Psay nTOTGMDOPRO Picture "@E 9,999,999.99"
		@ Li,052 Psay nTOTGMDOTER Picture "@E 9,999,999.99"
		@ Li,063 Psay nTOTGMATPRO Picture "@E 9,999,999.99"
		@ Li,074 Psay nTOTGMATTER Picture "@E 9,999,999.99"
		@ Li,085 Psay nTOTGOLEO   Picture "@E 9,999,999.99"
		@ Li,097 Psay nTOTGVALR   Picture "@E 99,999,999.99"
		@ Li,113 Psay nTOTGMOEDA  Picture "@E 99,999,999.99"
	EndIf

	If MV_PAR07 == 2
		CABEC1 := STR0005 //"Familia Descricao             Quant.     Mao Obra    Mao Obra    Material    Material    Oleo Lub.      Valor Total     Valor Total"
		CABEC2 := STR0010 //"  Bem       Descricao            O.S      Propria    Terceiro     Proprio    Terceiro                   R$              R$"
		NGSOMALI(58)
		nTOTGVALR   := 0
		nTOTGREFORM := 0
		nTOTGMDOPRO := 0.00
		nTOTGMDOTER := 0.00
		nTOTGMATPRO := 0.00
		nTOTGMATTER := 0.00
		nTOTGOLEO   := 0.00
		nTOTGMOEDA  := 0.00

		DbSelectArea(cTRB)
		DbGotop()
		While !Eof()
			nTOTVALR   := 0
			nTOTREFORM := 0
			nTOTMDOPRO := 0.00
			nTOTMDOTER := 0.00
			nTOTMATPRO := 0.00
			nTOTMATTER := 0.00
			nTOTOLEO   := 0.00
			nTOTMOEDA  := 0.00

			cFAMILI := (cTRB)->FAMILI
			@ Li,000 Psay (cTRB)->FAMILI
			DbSelectArea("ST6")
			DbSetOrder(1)
			DbSeek(xFILIAL("ST6")+(cTRB)->FAMILI)
			@ Li,008 Psay SubStr(ST6->T6_NOME,1,25)
			NGSOMALI(58)

			While !Eof() .And. (cTRB)->FAMILI == cFAMILI
				cCODBEM := (cTRB)->CODBEM
				nVALR   := (cTRB)->MDOPRO+(cTRB)->MDOTER+(cTRB)->MATPRO+(cTRB)->MATTER+(cTRB)->OLEO
				@ Li,002 Psay Substr((cTRB)->CODBEM,1,10)
				@ Li,012 Psay Substr((cTRB)->NOMBEM,1,20)
				@ Li,035 Psay (cTRB)->REFORM Picture "9999"
				@ Li,041 Psay (cTRB)->MDOPRO Picture "@E 9,999,999.99"
				@ Li,052 Psay (cTRB)->MDOTER Picture "@E 9,999,999.99"
				@ Li,063 Psay (cTRB)->MATPRO Picture "@E 9,999,999.99"
				@ Li,074 Psay (cTRB)->MATTER Picture "@E 9,999,999.99"
				@ Li,085 Psay (cTRB)->OLEO   Picture "@E 9,999,999.99"
				@ Li,097 Psay nVALR       Picture "@E 99,999,999.99"
				@ Li,113 Psay (cTRB)->MOEDA  Picture "@E 99,999,999.99"

				NGSOMALI(58)
				nTOTREFORM := nTOTREFORM+(cTRB)->REFORM
				nTOTMDOPRO := nTOTMDOPRO+(cTRB)->MDOPRO
				nTOTMDOTER := nTOTMDOTER+(cTRB)->MDOTER
				nTOTMATPRO := nTOTMATPRO+(cTRB)->MATPRO
				nTOTMATTER := nTOTMATTER+(cTRB)->MATTER
				nTOTOLEO   := nTOTOLEO  +(cTRB)->OLEO
				nTOTVALR   := nTOTVALR  +nVALR
				nTOTMOEDA  := nTOTMOEDA +(cTRB)->MOEDA
				DbSelectArea(cTRB)
				DbSkip()
			End
			nTOTVALR := nTOTMDOPRO+nTOTMDOTER+nTOTMATPRO+nTOTMATTER+nTOTOLEO
			@ Li,016 Psay STR0007    Picture "@!"  //"TOTAL"
			@ Li,035 Psay nTOTREFORM Picture "9999"
			@ Li,041 Psay nTOTMDOPRO Picture "@E 9,999,999.99"
			@ Li,052 Psay nTOTMDOTER Picture "@E 9,999,999.99"
			@ Li,063 Psay nTOTMATPRO Picture "@E 9,999,999.99"
			@ Li,074 Psay nTOTMATTER Picture "@E 9,999,999.99"
			@ Li,085 Psay nTOTOLEO   Picture "@E 9,999,999.99"
			@ Li,097 Psay nTOTVALR   Picture "@E 99,999,999.99"
			@ Li,113 Psay nTOTMOEDA  Picture "@E 99,999,999.99"
			NGSOMALI(58)
			NGSOMALI(58)
			nTOTGREFORM := nTOTGREFORM+nTOTREFORM
			nTOTGMDOPRO := nTOTGMDOPRO+nTOTMDOPRO
			nTOTGMDOTER := nTOTGMDOTER+nTOTMDOTER
			nTOTGMATPRO := nTOTGMATPRO+nTOTMATPRO
			nTOTGMATTER := nTOTGMATTER+nTOTMATTER
			nTOTGOLEO   := nTOTGOLEO  +nTOTOLEO
			nTOTGVALR   := nTOTGVALR  +nTOTVALR
			nTOTGMOEDA  := nTOTGMOEDA +nTOTMOEDA
		End
		@ Li,016 Psay STR0009     Picture "@!"  //"TOTAL"
		@ Li,035 Psay nTOTGREFORM Picture "9999"
		@ Li,041 Psay nTOTGMDOPRO Picture "@E 9,999,999.99"
		@ Li,052 Psay nTOTGMDOTER Picture "@E 9,999,999.99"
		@ Li,063 Psay nTOTGMATPRO Picture "@E 9,999,999.99"
		@ Li,074 Psay nTOTGMATTER Picture "@E 9,999,999.99"
		@ Li,085 Psay nTOTGOLEO   Picture "@E 9,999,999.99"
		@ Li,097 Psay nTOTGVALR   Picture "@E 99,999,999.99"
		@ Li,113 Psay nTOTGMOEDA  Picture "@E 99,999,999.99"
	EndIf

	RODA(nCNTIMPR,cRODATXT,TAMANHO)

	//Deleta o arquivo temporario fisicamente
	oARQTR300:Delete()

	//Ŀ
	// Devolve a condicao original do arquivo principam             
	//
	RetIndex("STJ")
	Set Filter To
	Set Device To Screen
	If aRETURN[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()
Return Nil

/*


Ŀ
Funo    ReportPrint Autor  Elisangela Costa       Data  22/06/06 
Ĵ
Descrio Chamada do Relatrio                                         
Ĵ
 Uso      ReportDef                                                    
ٱ


*/
Static Function ReportPrint(oReport)

	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)
	Private aVETINR := {}

	If !Empty(oReport:uParam)
		Pergunte(oReport:uParam,.F.)
	EndIf

	aDBFR300 := {{"FAMILI","C",06,0},;
				 {"CODBEM","C",16,0},;
				 {"NOMBEM","C",40,0},;
				 {"MDOPRO","N",12,2},;
				 {"MDOTER","N",12,2},;
				 {"MATPRO","N",12,2},;
				 {"MATTER","N",12,2},;
				 {"OLEO"  ,"N",12,2},;
				 {"MOEDA" ,"N",12,2},;
				 {"REFORM","N",04,0}}

	//Cria Tabela Temporria
	oARQTR300 := NGFwTmpTbl(cTRB,aDBFR300,{{"FAMILI","CODBEM"}})

	Processa({|lEND| MTR300TSTJ()},STR0011) //"Selecionando Registros..."

	If MV_PAR07 == 1 //RESUMIDO
		DbSelectArea(cTRB)
		DbGotop()
		oReport:SetMeter(RecCount())
		While !Eof() .And. !oReport:Cancel()

			cCODFAMI := (cTRB)->FAMILI

			nVALR    := 0
			nREFORM  := 0
			nMDOPRO  := 0.00
			nMDOTER  := 0.00
			nMATPRO  := 0.00
			nMATTER  := 0.00
			nOLEO    := 0.00
			nTOTMOE  := 0.00

			DbSelectArea(cTRB)
			While !Eof() .And. !oReport:Cancel() .And. (cTRB)->FAMILI = cCODFAMI
				oReport:IncMeter()

				nMDOPRO := nMDOPRO+(cTRB)->MDOPRO
				nMDOTER := nMDOTER+(cTRB)->MDOTER
				nMATPRO := nMATPRO+(cTRB)->MATPRO
				nMATTER := nMATTER+(cTRB)->MATTER
				nOLEO   := nOLEO  +(cTRB)->OLEO
				nREFORM := nREFORM+(cTRB)->REFORM
				nTOTMOE := nTOTMOE+(cTRB)->MOEDA

				DbSelectArea(cTRB)
				Dbskip()
			End
			nVALR := nMDOPRO+nMDOTER+nMATPRO+nMATTER+nOLEO
			oSection1:Init()
			oSection1:PrintLine()
		End
		oSection1:Finish()
	Else
		oSection1:Init()
		DbSelectArea(cTRB)
		DbGotop()
		oReport:SetMeter(RecCount())
		While !Eof() .And. !oReport:Cancel()

			oSection1:PrintLine()
			oSection2:Init()

			cCODFAMI := (cTRB)->FAMILI

			DbSelectArea(cTRB)
			While !Eof() .And. !oReport:Cancel() .And. (cTRB)->FAMILI = cCODFAMI
				oReport:IncMeter()
				nVALR    := (cTRB)->MDOPRO+(cTRB)->MDOTER+(cTRB)->MATPRO+(cTRB)->MATTER+(cTRB)->OLEO
				oSection2:PrintLine()
				DbSelectArea(cTRB)
				Dbskip()
			End
			oSection2:Finish()
		End
		oSection1:Finish()
	EndIf

	//Deleta o arquivo temporario fisicamente
	oARQTR300:Delete()

Return .T.


/*/


Ŀ
Funo    MTR300TSTJ Autor  Deivys Joenck          Data  13/08/01 
Ĵ
Descrio Chamada do Relatrio                                        
Ĵ
 Uso      MNTR300                                                     
ٱ


/*/
Static Function MTR300TSTJ()

	Local nQTDHORAS,nMDOPRO,nMDOPR2,nMDOTER,nMDOTE2
	Local nMATPRO,nMATPR2,nMATTER,nMATTE2,nOLEO
	Local nOLE2,nMOEDA,aVETCUST,nCUSTO,vVETHORAS
	Local lPRO,lFOUND,lAchou,cMOEDA

	nQTDHORAS 	:= 0.00
	aVETCUST 	:= {}
	vVETHORAS 	:= {}
	lAchou		:= .F.

	DbSelectArea("ST9")
	DbSetOrder(4)
	DbSeek(xFILIAL("ST9")+MV_PAR01,.T.)
	ProcRegua(LastRec())
	While !Eof() .And. ST9->T9_FILIAL == xFILIAL("ST9") .And.;
	ST9->T9_CODFAMI <= MV_PAR02

		IncProc()
		If ST9->T9_CODBEM < MV_PAR03 .Or. ST9->T9_CODBEM > MV_PAR04
			DbSkip()
			Loop
		EndIf

		DbSelectArea("STS")
		DbSetOrder(2)
		DBSEEK(xFILIAL("STS")+"B"+ST9->T9_CODBEM,.T.)

		WHILE !EOF() .AND. STS->TS_FILIAL == xFILIAL("STS") .AND.;
		STS->TS_TIPOOS = "B" .And. STS->TS_CODBEM <= ST9->T9_CODBEM

			If STS->TS_TERMINO != "S" .Or. STS->TS_SITUACA != "L"
				DbSkip()
				Loop
			EndIf

			If STS->TS_DTMRFIM < MV_PAR05 .Or. STS->TS_DTMRFIM > MV_PAR06
				DbSkip()
				Loop
			EndIf

			nMDOPRO := 0.00     //MAO DE OBRA PROPRIA (TIPOREG == "M")
			nMDOPR2 := 0.00     //MAO DE OBRA PROPRIA (TIPOREG == "M")
			nMDOTER := 0.00     //MAO DE OBRA DE TERCEIRO (DESC.PRODUTO == "SERVICO")
			nMDOTE2 := 0.00     //MAO DE OBRA DE TERCEIRO (DESC.PRODUTO == "SERVICO")
			nMATPRO := 0.00     //MATERIAL PROPRIO (MOVIMENTA ESTOQUE)
			nMATPR2 := 0.00     //MATERIAL PROPRIO (MOVIMENTA ESTOQUE)
			nMATTER := 0.00     //MATERIAL TERCEITO (NAO MOVIMENTA ESTOQUE)
			nMATTE2 := 0.00     //MATERIAL TERCEITO (NAO MOVIMENTA ESTOQUE)
			nOLEO   := 0.00     //PNEU (DESC.PROD == PNEU OU PROTETOR OU CAMARA)
			nOLE2   := 0.00     //PNEU (DESC.PROD == PNEU OU PROTETOR OU CAMARA)

			DbSelectArea("STT")
			DbSetOrder(1)
			DbSeek(xFILIAL("STT")+STS->TS_ORDEM+STS->TS_PLANO)
			While !Eof() .And. STT->TT_FILIAL == xFILIAL("STT") .And.;
			STT->TT_ORDEM  == STS->TS_ORDEM  .And. STT->TT_PLANO  == STS->TS_PLANO

				If Alltrim(STT->TT_SEQRELA) <> "0"
					cMOEDA := "0"
					nMOEDA := 0.00     //VALOR DOS INSUMOS EM OUTRA MOEDA
					lAchou := .F.

					If MV_PAR08 == 1
						cMOEDA := "SM2->M2_MOEDA2"
					ElseIf MV_PAR08 == 2
						cMOEDA := "SM2->M2_MOEDA3"
					ElseIf MV_PAR08 == 3
						cMOEDA := "SM2->M2_MOEDA4"
					ElseIf MV_PAR08 == 4
						cMOEDA := "SM2->M2_MOEDA5"
					EndIf

					dbSelectArea("SM2")
					dbSetOrder(1)
					dbSeek(STT->TT_DTINICI,.T.)
					While !BoF() .And. !lAchou
						If !Empty(SM2->M2_DATA) .And. SM2->M2_DATA <= STT->TT_DTINICI
							lAchou := .T.
							nMOEDA := &(cMOEDA)
						EndIf
						dbSkip(-1)
					End

					If MV_PAR11 = 2 //STANDARD
						aVETCUST := NGCUSTSTAN(STT->TT_CODIGO,STT->TT_TIPOREG)
						nCUSTO   := aVETCUST[1] //Custo standard

						vVETHORAS := NGTQUATINS(STT->TT_CODIGO,STT->TT_TIPOREG,STT->TT_USACALE,;
						STT->TT_QUANTID,STT->TT_TIPOHOR,STT->TT_DTINICI,;
						STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)
						nQTDHORAS := vVETHORAS[1]

						nCUSTO   := nCUSTO * nQTDHORAS
					Else
						nCUSTO   := STT->TT_CUSTO
					EndIf

					If STT->TT_TIPOREG == "M"
						nMDOPRO := nMDOPRO+nCUSTO
						nMDOPR2 := nMDOPR2+(nCUSTO / nMOEDA)
					ElseIf STT->TT_TIPOREG == "T"
						nMDOTER := nMDOTER+nCUSTO
						nMDOTE2 := nMDOTE2+(nCUSTO / nMOEDA)
					ElseIf STT->TT_TIPOREG == "P"
						DbSelectArea("SB1")
						DbSetOrder(1)
						DbSeek(xFILIAL("SB1")+SubStr(STT->TT_CODIGO,1,15))
						If SB1->B1_GRUPO >= MV_PAR09 .And. SB1->B1_GRUPO <= MV_PAR10 .And. !Empty(SB1->B1_GRUPO)
							nOLEO := nOLEO+nCUSTO
							nOLE2 := nOLE2+(nCUSTO / nMOEDA)
						Else
							lPRO   := .T.
							lFOUND := .F.
							DbSelectArea("SD1")
							DbSetOrder(4)
							DbSeek(xFILIAL("SD1")+STT->TT_NUMSEQ)

							While !Eof() .And. SD1->D1_FILIAL == xFILIAL("SD1") .And.;
							SD1->D1_NUMSEQ == STT->TT_NUMSEQ

								If Trim(STT->TT_CODIGO) == Trim(SD1->D1_COD)
									lFOUND := .T.
								EndIf
								DbSkip()
							End
							If lFOUND
								DbSelectArea("SF4")
								DbSetOrder(1)
								If DbSeek(xFILIAL("SF4")+SD1->D1_TES)
									If SF4->F4_ESTOQUE == "N"
										lPRO := .F.
									EndIf
								EndIf
							EndIf
							nMATPRO := nMATPRO+If(lPRO,nCUSTO,0.00)
							nMATPR2 := nMATPR2+If(lPRO,nCUSTO /nMOEDA,0.00)
							nMATTER := nMATTER+If(lPRO,0.00,nCUSTO)
							nMATTE2 := nMATTE2+If(lPRO,0.00,nCUSTO / nMOEDA)
						EndIf
					EndIf
				EndIf
				DbSelectArea("STT")
				DbSkip()
			End
			DbSelectArea(cTRB)
			If !DbSeek(ST9->T9_CODFAMI+ST9->T9_CODBEM)
				(cTRB)->(DbAppend())
				(cTRB)->FAMILI := ST9->T9_CODFAMI
				(cTRB)->CODBEM := ST9->T9_CODBEM
				(cTRB)->NOMBEM := ST9->T9_NOME
				(cTRB)->MDOPRO := nMDOPRO
				(cTRB)->MDOTER := nMDOTER
				(cTRB)->MATPRO := nMATPRO
				(cTRB)->MATTER := nMATTER
				(cTRB)->OLEO   := nOLEO
				(cTRB)->REFORM := 1
				(cTRB)->MOEDA  := nMDOPR2+nMDOTE2+nMATPR2+nMATTE2+nOLE2
			Else
				(cTRB)->MDOPRO := (cTRB)->MDOPRO + nMDOPRO
				(cTRB)->MDOTER := (cTRB)->MDOTER + nMDOTER
				(cTRB)->MATPRO := (cTRB)->MATPRO + nMATPRO
				(cTRB)->MATTER := (cTRB)->MATTER + nMATTER
				(cTRB)->OLEO   := (cTRB)->OLEO   + nOLEO
				(cTRB)->REFORM := (cTRB)->REFORM + 1
				(cTRB)->MOEDA  := (cTRB)->MOEDA+nMDOPR2+nMDOTE2+nMATPR2+nMATTE2+nOLE2
			EndIf
			DbSelectArea("STS")
			DbSkip()
		End

		DbSelectArea("STJ")
		DbSetOrder(2)
		DBSEEK(xFILIAL("STJ")+"B"+ST9->T9_CODBEM,.T.)

		WHILE !EOF() .AND. STJ->TJ_FILIAL == xFILIAL("STJ") .AND.;
		STJ->TJ_TIPOOS = "B" .And. STJ->TJ_CODBEM <= ST9->T9_CODBEM

			If STJ->TJ_TERMINO != "S" .Or. STJ->TJ_SITUACA != "L"
				DbSkip()
				Loop
			EndIf

			If STJ->TJ_DTMRFIM < MV_PAR05 .OR. STJ->TJ_DTMRFIM > MV_PAR06
				DbSkip()
				Loop
			EndIf

			nMDOPRO := 0.00     //MAO DE OBRA PROPRIA (TIPOREG == "M")
			nMDOPR2 := 0.00     //MAO DE OBRA PROPRIA (TIPOREG == "M")
			nMDOTER := 0.00     //MAO DE OBRA DE TERCEIRO (DESC.PRODUTO == "SERVICO")
			nMDOTE2 := 0.00     //MAO DE OBRA DE TERCEIRO (DESC.PRODUTO == "SERVICO")
			nMATPRO := 0.00     //MATERIAL PROPRIO (MOVIMENTA ESTOQUE)
			nMATPR2 := 0.00     //MATERIAL PROPRIO (MOVIMENTA ESTOQUE)
			nMATTER := 0.00     //MATERIAL TERCEITO (NAO MOVIMENTA ESTOQUE)
			nMATTE2 := 0.00     //MATERIAL TERCEITO (NAO MOVIMENTA ESTOQUE)
			nOLEO   := 0.00     //PNEU (DESC.PROD == PNEU OU PROTETOR OU CAMARA)
			nOLE2   := 0.00     //PNEU (DESC.PROD == PNEU OU PROTETOR OU CAMARA)

			DbSelectArea("STL")
			DbSetOrder(1)
			DbSeek(xFILIAL("STL")+STJ->TJ_ORDEM+STJ->TJ_PLANO)
			While !Eof() .And. STL->TL_FILIAL == xFILIAL("STL") .And.;
			STL->TL_ORDEM == STJ->TJ_ORDEM .And. STL->TL_PLANO  == STJ->TJ_PLANO

				If Alltrim(STL->TL_SEQRELA) <> "0"
					cMOEDA := "0"
					nMOEDA := 0.00     //VALOR DOS INSUMOS EM OUTRA MOEDA
					lAchou := .F.

					If MV_PAR08 == 1
						cMOEDA := "SM2->M2_MOEDA2"
					ElseIf MV_PAR08 == 2
						cMOEDA := "SM2->M2_MOEDA3"
					ElseIf MV_PAR08 == 3
						cMOEDA := "SM2->M2_MOEDA4"
					ElseIf MV_PAR08 == 4
						cMOEDA := "SM2->M2_MOEDA5"
					EndIf

					dbSelectArea("SM2")
					dbSetOrder(1)
					dbSeek(STL->TL_DTINICI,.T.)
					While !BoF() .And. !lAchou
						If !Empty(SM2->M2_DATA) .And. SM2->M2_DATA <= STL->TL_DTINICI
							lAchou := .T.
							nMOEDA := &(cMOEDA)
						EndIf
						dbSkip(-1)
					End

					If MV_PAR11 = 2 //STANDARD
						aVETCUST := NGCUSTSTAN(STL->TL_CODIGO,STL->TL_TIPOREG)
						nCUSTO   := aVETCUST[1] //Custo standard

						vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
						STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
						STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
						nQTDHORAS := vVETHORAS[1]

						nCUSTO   := nCUSTO * nQTDHORAS
					Else
						nCUSTO   := STL->TL_CUSTO
					EndIf

					If STL->TL_TIPOREG == "M"
						nMDOPRO := nMDOPRO+nCUSTO
						nMDOPR2 := nMDOPR2+(nCUSTO / nMOEDA)
					ElseIf STL->TL_TIPOREG == "T"
						nMDOTER := nMDOTER+nCUSTO
						nMDOTE2 := nMDOTE2+(nCUSTO / nMOEDA)
					ElseIf STL->TL_TIPOREG == "P"
						DbSelectArea("SB1")
						DbSetOrder(1)
						DbSeek(xFILIAL("SB1")+SubStr(STL->TL_CODIGO,1,15))
						If SB1->B1_GRUPO >= MV_PAR09 .And. SB1->B1_GRUPO <= MV_PAR10 .And. !Empty(SB1->B1_GRUPO)
							nOLEO := nOLEO+nCUSTO
							nOLE2 := nOLE2+(nCUSTO / nMOEDA)
						Else
							lPRO   := .T.
							lFOUND := .F.
							DbSelectArea("SD1")
							DbSetOrder(4)
							DbSeek(xFILIAL("SD1")+STL->TL_NUMSEQ)
							While !Eof() .And. SD1->D1_FILIAL == xFILIAL("SD1") .And.;
							SD1->D1_NUMSEQ == STL->TL_NUMSEQ

								If Trim(STL->TL_CODIGO) == TRIM(SD1->D1_COD)
									lFOUND := .T.
								EndIf
								DbSkip()
							End
							If lFOUND
								DbSelectArea("SF4")
								DbSetOrder(1)
								If DbSeek(xFILIAL("SF4")+SD1->D1_TES)
									If SF4->F4_ESTOQUE == "N"
										lPRO := .F.
									EndIf
								EndIf
							EndIf
							nMATPRO := nMATPRO+If(lPRO,nCUSTO,0.00)
							nMATPR2 := nMATPR2+If(lPRO,nCUSTO / nMOEDA,0.00)
							nMATTER := nMATTER+If(lPRO,0.00,nCUSTO)
							nMATTE2 := nMATTE2+If(lPRO,0.00,nCUSTO / nMOEDA)
						EndIf
					EndIf
				EndIf
				DbSelectArea("STL")
				DbSkip()
			End

			DbSelectArea(cTRB)
			If !DbSeek(ST9->T9_CODFAMI+ST9->T9_CODBEM)
				(cTRB)->(DbAppend())
				(cTRB)->FAMILI := ST9->T9_CODFAMI
				(cTRB)->CODBEM := ST9->T9_CODBEM
				(cTRB)->NOMBEM := ST9->T9_NOME
				(cTRB)->MDOPRO := nMDOPRO
				(cTRB)->MDOTER := nMDOTER
				(cTRB)->MATPRO := nMATPRO
				(cTRB)->MATTER := nMATTER
				(cTRB)->OLEO   := nOLEO
				(cTRB)->REFORM := 1
				(cTRB)->MOEDA  := nMDOPR2+nMDOTE2+nMATPR2+nMATTE2+nOLE2
			Else
				(cTRB)->MDOPRO := (cTRB)->MDOPRO + nMDOPRO
				(cTRB)->MDOTER := (cTRB)->MDOTER + nMDOTER
				(cTRB)->MATPRO := (cTRB)->MATPRO + nMATPRO
				(cTRB)->MATTER := (cTRB)->MATTER + nMATTER
				(cTRB)->OLEO   := (cTRB)->OLEO   + nOLEO
				(cTRB)->REFORM := (cTRB)->REFORM + 1
				(cTRB)->MOEDA  := (cTRB)->MOEDA+nMDOPR2+nMDOTE2+nMATPR2+nMATTE2+nOLE2
			EndIf
			DbSelectArea("STJ")
			DbSkip()
		End
		DbSelectArea("ST9")
		DbSkip()
	End

/*/


Ŀ
Funo    NGRETVAL   Autor  Elisangela Costa       Data  22/06/06 
Ĵ
Descrio Retorna o valor dos campos da secao 1 dos relatorio         
Ĵ
 Uso      REPORTDEF                                                   
ٱ


/*/
Static Function NGRETVAL(nCAM)

	Local nVALORCAM := 0

	If nCAM = 1
		nVALORCAM := nREFORM
	ElseIf nCAM = 2
		nVALORCAM := nMDOPRO
	ElseIf nCAM = 3
		nVALORCAM := nMDOTER
	ElseIf nCAM = 4
		nVALORCAM := nMATPRO
	ElseIf nCAM = 5
		nVALORCAM := nMATTER
	ElseIf nCAM = 6
		nVALORCAM := nOLEO
	ElseIf nCAM = 7
		nVALORCAM := nVALR
	Else
		nVALORCAM := nTOTMOE
	EndIf
Return nVALORCAM

/*/


Ŀ
Funo    NGRCODFAM  Autor  Elisangela Costa       Data  22/06/06 
Ĵ
Descrio Retorna o codigo da familia corrente                        
Ĵ
 Uso      REPORTDEF                                                   
ٱ


/*/
Static Function NGRCODFAM()
Return cCODFAMI
