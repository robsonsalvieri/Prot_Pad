#INCLUDE "PROTHEUS.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTV021    ³ Autor ³ Thiago Olis Machado   ³ Data ³01/03/2010³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Custo total de pneus em relacao aos parametros               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ De_Data    - Data inicio                                     ³±±
±±³          ³ Ate_Data   - Ate data                                        ³±±
±±³          ³ De_Ccusto  - De centro de custo                              ³±±
±±³          ³ Ate_Ccusto - Ate centro de custo                             ³±±
±±³          ³ De_CenTra  - De centro de trabalho                           ³±±
±±³          ³ Ate_CenTra - Ate sentro de trabalho                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna   ³ nCusto  - Custo total                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTV021(De_Data,Ate_Data,De_Ccusto,Ate_Ccusto,De_CenTra,Ate_CenTra,De_Familia,Ate_Familia,Con_Val)
Local aAreaOLD   := GetArea(),nCusto := 0
Local De_CcustoL := If(De_Ccusto = Nil,Space(NGSEEKDIC("SX3","TQN_CCUSTO",2,"X3_TAMANHO")),De_Ccusto)
Local De_CenTraL := If(De_CenTra = Nil,Space(NGSEEKDIC("SX3","TQN_CENTRA",2,"X3_TAMANHO")),De_CenTra)
Local De_FamiliaL := If(De_Familia = Nil,Space(NGSEEKDIC("SX3","T9_CODFAMI",2,"X3_TAMANHO")),De_Familia)

// Variáveis de Histórico de Indicadores
Local lMV_HIST 	:= NGI6MVHIST()
Local aParams 	:= {}
Local cCodIndic := "MNTV021"
Local nResult 	:= 0
Local cNGSEREF	:= GetMv("MV_NGSEREF")
Local aServRef 	:= StrTokArr( cNGSEREF, ';' )
Local cNGSECON	:= GetMv("MV_NGSECON")
Local aServCon 	:= StrTokArr( cNGSECON, ';' )
Local nX

Default Con_Val := "1"

// Armazena os Parâmetros
If lMV_HIST
	aParams := {}
	aAdd(aParams, {"DE_DATA"    , De_Data})
	aAdd(aParams, {"ATE_DATA"   , Ate_Data})
	aAdd(aParams, {"DE_CCUSTO"  , De_Ccusto})
	aAdd(aParams, {"ATE_CCUSTO" , Ate_Ccusto})
	aAdd(aParams, {"DE_CENTRA"  , De_CenTra})
	aAdd(aParams, {"ATE_CENTRA" , Ate_CenTra})
	aAdd(aParams, {"DE_FAMILIA" , De_Familia})
	aAdd(aParams, {"ATE_FAMILIA", Ate_Familia})
	aAdd(aParams, {"CON_VAL"    , Con_Val})
	NGI6PREPPA(aParams, cCodIndic)
EndIf

If ValType(De_Data) != "D" .or. ValType(Ate_Data) != "D"
	NGI6PREPVA(cCodIndic, nResult)
	Return nResult
Endif

cAliasQry := GetNextAlias()
// Query
If lMV_HIST
	cQuery := "SELECT * "
Else
	cQuery := "SELECT STL.TL_CUSTO, ST9.T9_CODBEM, ST9.T9_FILIAL "
EndIf
cQuery += " FROM "+RetSqlName("STL")+" STL "

cQuery += " INNER JOIN "+RetSqlName("STJ")+" STJ ON "
cQuery += " ( STJ.TJ_ORDEM = STL.TL_ORDEM AND STJ.TJ_PLANO = STL.TL_PLANO AND"
If ValType(De_CcustoL) == "C" .and. ValType(Ate_Ccusto) == "C"
	cQuery += " STJ.TJ_CCUSTO >= '"+De_CcustoL+"' AND STJ.TJ_CCUSTO <= '"+Ate_Ccusto+"' AND "
Endif
If ValType(De_CenTraL) == "C" .and. ValType(Ate_CenTra) == "C"
	cQuery += " STJ.TJ_CENTRAB >= "+ValToSQL(De_CenTraL)+" AND STJ.TJ_CENTRAB <= "+ValToSQL(Ate_CenTra)+" AND "
Endif
cQuery += " (STJ.TJ_SERVICO = '"+GetMv("MV_NGSEREF")+"' OR STJ.TJ_SERVICO = '"+GetMv("MV_NGSECON")+"' ) AND "
cQuery += " STJ.TJ_SITUACA = 'L' AND "
cQuery += " STJ.TJ_FILIAL = '"+xFilial("STJ")+"' AND STJ.D_E_L_E_T_ <> '*' "
cQuery += " AND STJ.D_E_L_E_T_ <> '*' )"
cQuery += " INNER JOIN "+RetSqlName("ST9")+" ST9 ON"
cQuery += " ( ST9.T9_CODBEM = STJ.TJ_CODBEM AND ST9.T9_CATBEM = '3' "
If ValType(De_FamiliaL) == "C" .and. ValType(Ate_Familia) == "C"
	cQuery += " AND ST9.T9_CODFAMI BETWEEN '"+De_FamiliaL+"' AND '"+Ate_Familia+"'"
EndIf
cQuery += " AND ST9.D_E_L_E_T_ <> '*' )"
cQuery += " WHERE STL.TL_SEQRELA <> '0  ' AND "
If ValType(De_Data) == "D"
	cQuery += " STL.TL_DTINICI >= '"+Dtos(De_Data)+"' AND "
Endif
If ValType(Ate_Data) == "D"
	cQuery += " STL.TL_DTINICI <= '"+Dtos(Ate_Data)+"' AND "
Endif
cQuery += " STL.TL_FILIAL = '"+xFilial("STL")+"' AND STL.D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)
NGI6PREPDA(cAliasQry, cCodIndic)

dbSelectArea(cAliasQry)
dbGoTop()
While (cAliasQry)->(!Eof())
   nCusto += (cAliasQry)->(TL_CUSTO)
   If Con_Val == "1"
	   dbSelectArea("ST9")
	   dbSetOrder(1)
	   If dbSeek((cAliasQry)->T9_FILIAL+(cAliasQry)->T9_CODBEM) .AND. ;
	   		ST9->T9_DTCOMPR >= De_Data .AND. ST9->T9_DTCOMPR <= Ate_Data
	   		nCusto += ST9->T9_VALCPA
	   	EndIf
	EndIf
   (cAliasQry)->(DbSkip())
End
(cAliasQry)->(dbCloseArea())

cAliasQry := GetNextAlias()

//Preenche a variavel cNGSEREF com todos os serviços do parametro MV_NGSEREF
If !Empty( cNGSEREF )
	For nX := 1 To Len(aServRef)
		If nX == 1
			cNGSEREF := "'"+aServRef[nX]+"'"
		Else
			cNGSEREF += ",'"+aServRef[nX]+"'"
		EndIf
	Next nX
Else

	cNGSEREF := "' '"

EndIf

//Preenche a variavel cNGSECON com todos os serviços do parametro MV_NGSECON
If !Empty(cNGSECON)

	For nX := 1 To Len(aServCon)
		If nX == 1
			cNGSECON := "'"+aServCon[nX]+"'"
		Else
			cNGSECON += ",'"+aServCon[nX]+"'"
		EndIf
	Next nX

Else

	cNGSECON := "' '"

EndIf

// Query
If lMV_HIST
	cQuery := "SELECT * "
Else
	cQuery := "SELECT STT.TT_CUSTO, ST9.T9_CODBEM, ST9.T9_FILIAL "
EndIf
cQuery += " FROM "+RetSqlName("STT")+" STT "
cQuery += " INNER JOIN "+RetSqlName("STS")+" STS ON "
cQuery += " ( STS.TS_ORDEM = STT.TT_ORDEM AND STS.TS_PLANO = STT.TT_PLANO AND"
If ValType(De_CcustoL) == "C" .and. ValType(Ate_Ccusto) == "C"
	cQuery += " STS.TS_CCUSTO >= '"+De_CcustoL+"' AND STS.TS_CCUSTO <= '"+Ate_Ccusto+"' AND "
Endif
If ValType(De_CenTraL) == "C" .and. ValType(Ate_CenTra) == "C"
	cQuery += " STS.TS_CENTRAB >= "+ValToSQL(De_CenTraL)+" AND STS.TS_CENTRAB <= "+ValToSQL(Ate_CenTra)+" AND "
Endif
cQuery += " (STS.TS_SERVICO IN ("+cNGSEREF+") OR STS.TS_SERVICO IN ("+cNGSECON+") ) AND "
cQuery += " STS.TS_SITUACA = 'L' AND "
cQuery += " STS.TS_FILIAL = '"+xFilial("STS")+"' AND STS.D_E_L_E_T_ <> '*' "
cQuery += " AND STS.D_E_L_E_T_ <> '*' )"
cQuery += " INNER JOIN "+RetSqlName("ST9")+" ST9 "
cQuery += " ON (ST9.T9_CODBEM = STS.TS_CODBEM AND ST9.T9_CATBEM = '3' "
If ValType(De_FamiliaL) == "C" .and. ValType(Ate_Familia) == "C"
	cQuery += " AND ST9.T9_CODFAMI BETWEEN '"+De_FamiliaL+"' AND '"+Ate_Familia+"'"
EndIf
cQuery += " AND ST9.T9_FILIAL = "+ValToSql(xFilial("ST9"))
cQuery += " AND ST9.D_E_L_E_T_ <> '*' )"
cQuery += " WHERE STT.TT_SEQRELA <> '0  ' AND "
If ValType(De_Data) == "D"
	cQuery += " STT.TT_DTINICI >= '"+Dtos(De_Data)+"' AND "
Endif
If ValType(Ate_Data) == "D"
	cQuery += " STT.TT_DTINICI <= '"+Dtos(Ate_Data)+"' AND "
Endif
cQuery += " STT.TT_FILIAL = '"+xFilial("STT")+"' AND STT.D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)
NGI6PREPDA(cAliasQry, cCodIndic)

While (cAliasQry)->(!Eof())
   nCusto += (cAliasQry)->(TT_CUSTO)
   If Con_Val == "1"
	   dbSelectArea("ST9")
	   dbSetOrder(1)
	   If dbSeek((cAliasQry)->T9_FILIAL+(cAliasQry)->T9_CODBEM) .AND. ;
	   		ST9->T9_DTCOMPR >= De_Data .AND. ST9->T9_DTCOMPR <= Ate_Data
	   		nCusto += ST9->T9_VALCPA
	   	EndIf
	EndIf
   (cAliasQry)->(DbSkip())
End

(cAliasQry)->(dbCloseArea())

// RESULTADO
nResult := nCusto
NGI6PREPVA(cCodIndic, nResult)

RestArea(aAreaOLD)
Return nResult
