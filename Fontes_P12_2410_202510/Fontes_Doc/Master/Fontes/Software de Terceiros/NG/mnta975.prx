#INCLUDE "MNTA975.ch"
#Include "Protheus.ch"
#Include "ap5mail.ch"

/*/


Ŀ
Funo     MNTA975   Autor  Marcos Wagner Junior   Data  03/09/07 
Ĵ
Descrio  Rotina para Solicitacao de Servicos ao Cartorio				  
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function MNTA975

//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 							  
//
Local bKeyF9,bKeyF10,bKeyF11,bKeyF12
Local aOldMenu
Local aNGCAD02 := {}
Private asMenu

bKeyF9 := SetKey(VK_F9)
bKeyF10 := SetKey(VK_F10)
bKeyF11 := SetKey(VK_F11)
bKeyF12 := SetKey(VK_F12)
SetKey( VK_F9, { | | NGVersao( "MNTA975" , 2 ) } )
SETKEY(VK_F10,Nil)
SETKEY(VK_F11,Nil)
SETKEY(VK_F12,Nil)

aOldMenu := ACLONE(asMenu)
asMenu := NGRIGHTCLICK("MNTA975")

aNGCAD02:={;
		If(Type("aCHOICE") == "A",ACLONE(aCHOICE),{}),;
		If(Type("aVARNAO") == "A",ACLONE(aVARNAO),{}),;
		If(Type("aGETNAO") == "A",ACLONE(aGETNAO),{}),;
		If(Type("cGETWHILE") == "C",cGETWHILE,NIL),;
		If(Type("cGETMAKE") == "C",cGETMAKE,NIL),;
		If(Type("cGETKEY") == "C",cGETKEY,NIL),;
		If(Type("cGETALIAS") == "C",cGETALIAS,NIL),;
		If(Type("cTUDOOK") == "C",cTUDOOK,NIL),;
		If(Type("cLINOK") == "C",cLINOK,NIL),;
		If(Type("aRELAC") == "A",ACLONE(aRELAC),{}),;
		If(Type("aCHKDEL") == "A",ACLONE(aCHKDEL),{}),;
		If(Type("bngGRAVA") == "A",ACLONE(bngGRAVA),{}),;
		If(Type("aNGBUTTON") == "A",ACLONE(aNGBUTTON),{})}




//Ŀ
// Define o cabecalho da tela de atualizacoes                   
//
Private aROTINA := MenuDef()
Private cCadastro := STR0006 //"Solicitao de Servios ao Cartrio"
Private aCHKDEL := {}, bNGGRAVA
Private nTotal   := 0
Private cPerg := "MNTA975"
Private aChoice := {}
Private cNao := '2',cSim := '1'

aGETNAO := {{'TSY_NUMREQ'}}
cGETWHILE := "TSX_FILIAL == xFilial('TSX') .and. TSY_NUMREQ == M->TSX_NUMREQ"
cGETMAKE  := "TSX->TSX_NUMREQ"
cGETKEY   := "M->TSX_NUMREQ"
cGETALIAS := "TSY"

cTUDOOK   := AllwaysTrue()
cLINOK    := AllwaysTrue()

DbSelectArea("TSX")
DbGotop()
mBrowse(6,1,22,75,"TSX",,,,,,MNA975COR())

//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK) 							  	  
//
SETKEY(VK_F9,bKeyF9)
SETKEY(VK_F10,bKeyF10)
SETKEY(VK_F11,bKeyF11)
SETKEY(VK_F12,bKeyF12)

asMenu := ACLONE(aOldMenu)

aCHOICE := ACLONE(aNGCAD02[1])
aVARNAO := ACLONE(aNGCAD02[2])
AGETNAO := ACLONE(aNGCAD02[3])
If(aNGCAD02[4] != NIL,cGETWHILE := aNGCAD02[4],)
If(aNGCAD02[5] != NIL,cGETMAKE := aNGCAD02[5],)
If(aNGCAD02[6] != NIL,cGETKEY := aNGCAD02[6],)
If(aNGCAD02[7] != NIL,cGETALIAS := aNGCAD02[7],)
If(aNGCAD02[8] != NIL,cTUDOOK := aNGCAD02[8],)
If(aNGCAD02[9] != NIL,cLINOK := aNGCAD02[9],)
aRELAC := ACLONE(aNGCAD02[10])
aCHKDEL := ACLONE(aNGCAD02[11])
bngGRAVA := ACLONE(aNGCAD02[12])
aNGBUTTON := ACLONE(aNGCAD02[13])

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MNT975IN
Incluir

@author  Marcos Wagner Junior
@since   03/09/07
@version P12
/*/
//-------------------------------------------------------------------
Function MNT975IN(cAlias, nReg, nOpcx)

	Local bCampo
	Local i
	Local x
	Local cKey
	Local aNAO
	Local nI
	Local oFont := TFont():New("Arial",9,15,.T.,.T.,,,,.F.)
	Local aNoFields := {}
	Local aNgHeader	:= {}
	Local nTamTot		:= 0
	Local nInd			:= 0
	Local cCampo		:= ""
	Local cRelacao		:= ""
	// Release menor ou igual a 23
	Local lRPORel23 := GetRPORelease() <= '12.1.023'

	Private oDlg
	Private oGet
	Private oMenu
	Private oTotal
	Private nTSYSERVIC
	Private nTSYQTDE
	Private nTSYTOTAL
	Private nTSYDESERV
	Private cMEMODES := ''
	Private aTELA[0][0]
	pRIVATE aGETS[0]
	Private aSize := MsAdvSize(,.f.,430)
	Private aObjects := {}
	Private aHeader
	Private aCols

	If nOpcx != 3 .And. lRPORel23
		M->TSX_OBSERV := MsMM(TSX->TSX_MMOBS)
	Endif

	If nOpcx = 4
		If TSX->TSX_STATUS = '1'
			MsgStop(STR0007,STR0008)	 //"Solicitao no poder ser alterada pois j foi enviada!"###"Ateno"
			Return .f.
		Endif
	Endif

	Aadd(aObjects,{040,040,.t.,.t.})
	Aadd(aObjects,{050,050,.t.,.t.})
	Aadd(aObjects,{090,090,.t.,.t.})

	aInfo := {aSize[1],aSize[2],aSize[3],aSize[4],0,0}
	aPosObj := MsObjSize(aInfo, aObjects,.f.)

	bCampo  := {|nCPO| Field(nCPO)}

	dbSelectArea("TSX")
	dbSetOrder(1)
	FOR i := 1 TO FCount()
		M->&(EVAL(bCampo,i)) := &(EVAL(bCampo,i))
		If nOpcx == 3 //INCLUIR
		IF     ValType(M->&(EVAL(bCampo,i))) == "C"
				M->&(EVAL(bCampo,i)) := SPACE(LEN(M->&(EVAL(bCampo,i))))
		ELSEIF ValType(M->&(EVAL(bCampo,i))) == "N"
				M->&(EVAL(bCampo,i)) := 0
		ELSEIF ValType(M->&(EVAL(bCampo,i))) == "D"
				M->&(EVAL(bCampo,i)) := cTod("  /  /  ")
		ELSEIF ValType(M->&(EVAL(bCampo,i))) == "L"
				M->&(EVAL(bCampo,i)) := .F.
		ELSEIF ValType(M->&(EVAL(bCampo,i))) == "M"
				M->&(EVAL(bCampo,i)) := SPACE(50)
		ENDIF
		Endif
	Next i

	If nOpcx == 3

		aNgHeader := NGHeader("TSX")
		nTamTot := Len(aNgHeader)
		For nInd := 1 To nTamTot
			cCampo 		:= aNgHeader[nInd,2]
			cRelacao	:= Posicione("SX3",2,cCampo,"X3_RELACAO")
			If !Empty(cRelacao)
				cX  := "m->" + Trim(cCampo)
				cY  := Trim(cRelacao)
				&cX.:= &cY.
			Endif
		Next nInd

	Endif

	aHeader := {}
	aCols := {}
	//Monta o aHEADER e o aCOLS da Getdados
	aAdd(aNoFields, 'TSY_NUMREQ')
	cQuery := "SELECT * FROM "+RetSqlName("TSY")+" TSY WHERE TSY.TSY_FILIAL = '"+xFilial("TSY")+"' AND TSY.TSY_NUMREQ = '"+M->TSX_NUMREQ+"'"+;
				" AND TSY.D_E_L_E_T_ = ' '"
	FillGetDados( 4, "TSY", 1, xFilial("TSY")+M->TSX_NUMREQ, {|| "TSY_FILIAL + TSY_NUMREQ"}, {|| .T.},aNoFields,,,cQuery)

	If Empty(aCols)
	aCols := BlankGetd(aHeader)
	EndIf

	nTSYSERVIC  := aSCAN(aHEADER,{|x| Trim(Upper(x[2])) == "TSY_SERVIC"})
	nTSYDESERV  := aSCAN(aHEADER,{|x| Trim(Upper(x[2])) == "TSY_DESERV"})
	nTSYVALOR   := aSCAN(aHEADER,{|x| Trim(Upper(x[2])) == "TSY_VALOR"})
	nTSYQTDE    := aSCAN(aHEADER,{|x| Trim(Upper(x[2])) == "TSY_QTDE"})
	nTSYTOTAL   := aSCAN(aHEADER,{|x| Trim(Upper(x[2])) == "TSY_TOTAL"})

	aPos1 := {15,1,78,315}
	nOpca := 0

	DEFINE MSDIALOG oDlg TITLE Ccadastro From aSize[7],0 To aSize[6],aSize[5] OF oMainWnd PIXEL
		aPosObj[1][3] += 35
		aPosObj[2][1] += 35
		aPosObj[3][3]  -= 10
	oEnchoice := Msmget():New("TSX", RecNo(), nOpcx,,,,,aPosObj[1],,3)
	oEnchoice:oBox:Align := CONTROL_ALIGN_TOP

		oGet := MSGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[3,3],aPosObj[2,4],nOpcx,"MNTA975LIN(1)","MNTA975LIN(1)",,.T.,,,,3000)

	@ aPosObj[3,3],aPosObj[1,1] Say OemToAnsi(STR0009) of oDlg Pixel Font oFont //"Valor Total"
	@ aPosObj[3,3],aPosObj[1,3] Say oTotal Var nTotal Size 50,10 Of oDlg Pixel Picture '@E 999,999,999.99' Font oFont

		MNTA975TOG()

		NGPOPUP(asMenu,@oMenu)
		oDlg:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg)}
		oEnchoice:oBox:bRClicked := { |o,x,y| oMenu:Activate(x,y,oDlg)}

	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| IF(MNT975ALL(nOpcx),(nOpca:=1,oDlg:End()),nOpca := 0)},{||nOpca:= 0,oDlg:End()}) CENTERED

	If nOpcx == 3
		If nOpca = 0
			RollBackSX8()
		Else
			ConfirmSX8()
		Endif
	Endif

	If nOpcx != 2
		If nOpca == 1
			DbSelectArea("TSX")
			DbSetOrder(01)
			If dbSeek(xFilial("TSX")+M->TSX_NUMREQ)
				RecLock("TSX",.F.)
			Else
				RecLock("TSX",.T.)
			Endif
			TSX->TSX_FILIAL := xFilial("TSX")
			TSX->TSX_NUMREQ := M->TSX_NUMREQ
			TSX->TSX_DTSOLI := M->TSX_DTSOLI
			TSX->TSX_REQUIS := M->TSX_REQUIS
			TSX->TSX_DEPTO  := M->TSX_DEPTO
			TSX->TSX_EMPRES := M->TSX_EMPRES
			TSX->TSX_CCUSTO := M->TSX_CCUSTO
			TSX->TSX_NROCPJ := M->TSX_NROCPJ
			TSX->TSX_FORNEC := M->TSX_FORNEC
			TSX->TSX_LOJA   := M->TSX_LOJA
			If !lRPORel23 // Caso seja verso superior a 12.1.23
				TSX->TSX_OBSERV := M->TSX_OBSERV
			EndIf
			MsUnLock("TSX")

			If lRPORel23
				MNTA975BNG()
				nSMM := If(nOpcx == 3 .Or. nOpcx == 4,1,2)
				RecLock("TSX",.F.)
				MSMM(M->TSX_MMOBS,,,cMEMODES,nSMM,,,"TSX","TSX_MMOBS")
				MsUnLock("TSX")
			EndIf

			For nI := 1 to Len(aCols)
				If aCols[nI][Len(aCols[nI])]
					DbSelectArea("TSY")
					DbSetOrder(01)
					If dbSeek(xFilial("TSY")+M->TSX_NUMREQ+aCols[nI][nTSYSERVIC])
						RecLock("TSY",.F.)
						dbDelete()
						MsUnLock("TSY")
					Endif
				ElseIf !Empty(aCols[nI][nTSYSERVIC]) .AND. !aCols[n][Len(aCols[n])]
					DbSelectArea("TSY")
					DbSetOrder(01)
					If dbSeek(xFilial("TSY")+M->TSX_NUMREQ+aCols[nI][nTSYSERVIC])
						RecLock("TSY",.F.)
					Else
						RecLock("TSY",.T.)
					Endif
					TSY->TSY_FILIAL := xFilial("TSY")
					TSY->TSY_NUMREQ := M->TSX_NUMREQ
					TSY->TSY_SERVIC := aCols[nI][nTSYSERVIC]
					TSY->TSY_QTDE   := aCols[nI][nTSYQTDE]
					TSY->TSY_TOTAL  := aCols[nI][nTSYTOTAL]
					MsUnLock("TSY")
				Endif
			Next
			If MsgYesNo(STR0010,STR0008) //"Deseja enviar a solicitao?"###"ATENO"
				MNTA975REL()
				DbSelectArea("TSX")
				RecLock("TSX",.F.)
				TSX->TSX_STATUS := '1'
				MsUnLock("TSX")
			Else
				DbSelectArea("TSX")
				RecLock("TSX",.F.)
				TSX->TSX_STATUS := '2'
				MsUnLock("TSX")
			Endif
		Endif
	Endif

Return nOPCA

/*


Ŀ
Funo    MNT975ALL  Autor Marcos Wagner Junior    Data  03/09/07 
Ĵ
Descrio Grava dados na TSX, TSY  											  
Ĵ
 Uso      |MNTA975                                                     
ٱ


*/
Function MNT975ALL(nOpcx)
Local nI, lNaoVazio := .f.

If !Obrigatorio(aGets,aTela)
	Return .f.
EndIf

For nI := 1 to Len(aCols)
	If !aCols[nI][Len(aCols[nI])]
		If !Empty(aCols[nI][nTSYSERVIC]) .AND. !Empty(aCols[nI][nTSYQTDE])
			lNaoVazio := .t.
		Endif
	Endif
Next
If !lNaoVazio
	MsgStop(STR0011,STR0008) //"Informe pelo menos um servio com quantidade maior que 0!"###"ATENO"
	Return .f.
Endif

If nOpcx = 5
	Return .t.
Endif

If nOpcx = 4
	Return .t.
Endif

Return .t.

/*/


Ŀ
Funcao    MNA975COR  Autor Marcos Wagner Junior    Data  03/09/07 
Ĵ
Descrio  Funcao p/ definir cores das Multas					           
Ĵ
Retorno   aCores                                                      
ٱ


/*/
Function MNA975COR()
Local aCores := {}

aCores := {{"NGSEMAFARO('TSX->TSX_STATUS = cNao')" , 'BR_VERMELHO'},;
           {"NGSEMAFARO('TSX->TSX_STATUS = cSim')" , 'BR_VERDE'}}

Return(aCores)

/*/


Ŀ
Funcao    MNC975LEG  Autor Marcos Wagner Junior    Data  03/09/07 
Ĵ
Descrio  Legenda do Semaforo											        
Ĵ
Retorno   aCores                                                      
ٱ


/*/
Function MNA975LEG()

BrwLegenda(cCadastro,STR0005,{{"BR_VERMELHO",STR0012},; //"Legenda"###"Solicitao no enviada"
										  {"BR_VERDE" 	 ,STR0013}}) //"Solicitao enviada"

Return .t.

/*/


Ŀ
Funo    MNTA975BNG Autor  Marcos Wagner Junior   Data  04/09/07 
Ĵ
Descrio  Consistencia final antes da gravacao							  
Ĵ
 Uso       MNTA975                                                    
ٱ


/*/
Function MNTA975BNG()

cMEMODES := M->TSX_OBSERV

Return .t.

/*/


Ŀ
Funo    MNTA975REL Autor  Marcos Wagner Junior   Data  05/09/07 
Ĵ
Descrio Relatorios gerados a partir da SS ao Cartorio		           
Ĵ
 Uso      MNTA975	                                                  
ٱ


*/
Function MNTA975REL()

WNREL      := "MNTA975"
LIMITE     := 132
cDESC1     := ""
cDESC2     := ""
cDESC3     := ""
cSTRING    := "TR4"

Private NOMEPROG := "MNTA975"
Private TAMANHO  := "P"
Private aRETURN  := {STR0014,1,STR0015,1,2,1,"",1} //"Zebrado"###"Administracao"
Private TITULO   := STR0016 //"Relatrio"

Processa({|lEnd| MNTA975IMP()()}) // MONTE TELA PARA ACOMPANHAMENTO DO PROCESSO.

Return .T.

/*/


Ŀ
Funo    |MNTA975IMP| Autor  Marcos Wagner Junior   Data  05/09/07 
Ĵ
Descrio Chamada do Relatrio                                        
Ĵ
 Uso      MNTA975                                                     
ٱ


/*/
Function MNTA975IMP()
Local nI, nVia
Local cStartPath := AllTrim(GetSrvProfString("Startpath",""))
Private oFont06b := TFont():New("ARIAL",09,09,,.T.,,,,.F.,.F.)
Private oFont09  := TFont():New("ARIAL",09,09,,.F.,,,,.F.,.F.)
Private oFont11  := TFont():New("ARIAL",11,11,,.T.,,,,.F.,.F.)
Private oPrint := TMSPrinter():New(OemToAnsi(TITULO))
Private aMatriz := {}

If Substr(cStartPath,Len(cStartPath),1) <> "\"
	cStartPath := cStartPath+"\"
Endif

cAliasQry := GetNextAlias()
cQuery := " SELECT TSY.TSY_QTDE, TSY.TSY_TOTAL, TS4.TS4_DESCRI "
cQuery += " FROM " + RetSqlName("TSY")+" TSY, " + RetSqlName("TS4")+" TS4 "
cQuery += " WHERE TSY.TSY_NUMREQ  = '"+M->TSX_NUMREQ+"'"
cQuery += " AND   TSY.TSY_SERVIC  = TS4.TS4_CODSDP "
cQuery += " AND   TSY.D_E_L_E_T_ <> '*' "
cQuery += " AND   TS4.D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)
Dbgotop()

nTotal := 0
If !Eof()
	While !Eof()
		AADD(aMatriz,{(cAliasQry)->TS4_DESCRI,Str((cAliasQry)->TSY_QTDE),Str((cAliasQry)->TSY_TOTAL)})
		nTotal += (cAliasQry)->TSY_TOTAL
		dbSkip()
	End
	AADD(aMatriz,{STR0009,STR0017,Str(nTotal)}) //"VALOR TOTAL"###"R$"
Endif

For nVia := 1 to 2
	nColFim := 1325
	lin := 150
	oPrint:StartPage()
	oPrint:Box(lin,100,830,nColFim)

	cLogo := NGLOCLOGO()

	If File(cLogo)
		oPrint:SayBitMap(lin+5,105,cLogo,250,130)
	EndIf

	oPrint:Say(lin+20,415 ,STR0018,oFont11) //"SOLICITAO DE SERVIOS - CARTRIO"
	oPrint:Say(lin+85,415 ,STR0019,oFont11) //"3 OFCIO DE NOTAS"
	If nVia = 1
		oPrint:Say(lin+100,1041,STR0020,oFont06b) //"1 VIA CARTRIO"
	Else
		oPrint:Say(lin+100,1066,STR0021,oFont06b)	 //"2 VIA CLIENTE"
	Endif

	lin += 140
	oPrint:Line(lin,100,lin,nColFim)

	For nI := 1 to 9
		oPrint:Line(lin,100,lin,nColFim)
	   If nI <> 4 .AND. nI <> 9
	   	oPrint:Line(lin,725,lin+60,725)
	   Endif
		lin += 60
	Next

	oPrint:Say(lin+10-60*9,110,STR0022,oFont09) //"DATA:"
	oPrint:Say(lin+10-60*8,110,STR0023,oFont09) //"REQUISITANTE:"
	oPrint:Say(lin+10-60*7,110,STR0024,oFont09) //"DEPARTAMENTO DO REQUISITANTE:"
	oPrint:Say(lin+10-60*6,500,STR0025,oFont09) //"DADOS PARA CONTABILIZAO:"
	oPrint:Say(lin+10-60*5,110,STR0026,oFont09) //"EMPRESA"
	oPrint:Say(lin+10-60*4,110,STR0027,oFont09) //"CENTRO DE CUSTO"
	oPrint:Say(lin+10-60*3,110,STR0028,oFont09) //"FILIAL"
	oPrint:Say(lin+10-60*2,110,STR0029,oFont09) //"NMERO CPJ"
	oPrint:Say(lin+10-60*1,550,STR0030,oFont09) //"DADOS DA SOLICITAO"

	oPrint:Say(lin+10-60*9,735,DTOC(M->TSX_DTSOLI),oFont09)
	oPrint:Say(lin+10-60*8,735,M->TSX_REQUIS,oFont09)
	oPrint:Say(lin+10-60*7,735,M->TSX_DEPTO,oFont09)
	oPrint:Say(lin+10-60*5,735,M->TSX_EMPRES,oFont09)
	oPrint:Say(lin+10-60*4,735,M->TSX_CCUSTO,oFont09)
	oPrint:Say(lin+10-60*3,735,M->TSX_FILIAL,oFont09)
	oPrint:Say(lin+10-60*2,735,M->TSX_NROCPJ,oFont09)

	For nI := 1 to Len(aMatriz)
		oPrint:Line(lin,100,lin+60,100)
		oPrint:Line(lin,725,lin+60,725)
		oPrint:Line(lin,900,lin+60,900)
		oPrint:Line(lin,nColFim,lin+60,nColFim)

		oPrint:Say(lin+10,110 ,aMatriz[nI][1],oFont09)
		If nI <> Len(aMatriz)
			oPrint:Say(lin+10,685 ,aMatriz[nI][2],oFont09)
		Else
			oPrint:Say(lin+10,793 ,aMatriz[nI][2],oFont09)
		Endif
		//oPrint:Say(lin+10,1150,aMatriz[nI][3],oFont09)
		oPrint:Say(lin+10,1150,Transform(Val(AllTrim(aMatriz[nI][3])),"@E 999,999.99"),oFont09)

		lin += 60
		oPrint:Line(lin,100,lin,nColFim)
	Next

	oPrint:Say(lin+10,110 ,STR0031,oFont09) //"OBSERVAO:"


	nLinhas := MlCount(M->TSX_OBSERV,50)
	If nLinhas > 0
		For nI := 1 to nLinhas
		   oPrint:Say(lin+10,360,Memoline(M->TSX_OBSERV,50,nI),oFont09)
		   oPrint:Line(lin,nColFim,lin+60,nColFim)
		   oPrint:Line(lin,100,lin+60,100)
		   lin += 60
		Next ix
	Else
	   oPrint:Line(lin,nColFim,lin+120,nColFim)
	   oPrint:Line(lin,100,lin+120,100)
	   lin += 120
	Endif

	oPrint:Box(lin,100,lin+190,nColFim)
	oPrint:Say(lin+10,110 ,STR0032,oFont09) //"VISTO DO GERENTE"
	oPrint:Line(lin+60,725,lin+190,725)
	oPrint:Say(lin+10,735 ,STR0033,oFont09) //"VISTO CARTRIO"
	lin += 60
	oPrint:Line(lin,100,lin,nColFim)
	oPrint:EndPage()
Next

oPrint:Preview()

Return Nil

/*/


Ŀ
Funo    |MNTA975LIN| Autor  Marcos Wagner Junior   Data  05/09/07 
Ĵ
Descrio Validacao da linha da GetDados                              
Ĵ
 Uso      MNTA975                                                     
ٱ


/*/
Function MNTA975LIN(nOpc)

Local nI

MNTA975TOG()
If aCols[n][Len(aCols[n])]
	Return .t.
Endif

If nOpc = 1
	cServico := aCols[n][nTSYSERVIC]
Else
	cServico := M->TSY_SERVIC
Endif

For nI := 1 to Len(aCols)
	If nI != n
		If aCols[nI][nTSYSERVIC] = cServico .And. !aCols[nI][Len(aCols[n])]
			Help(" ",1,STR0008,,STR0034,3,1) //"ATENO"###"Servio j informado"
		   Return .f.
		Endif
	Endif
Next

Return .t.

/*/


Ŀ
Funo    |MNTA975SER| Autor  Marcos Wagner Junior   Data  05/09/07 
Ĵ
Descrio Carrega descricao do Servico		                          
Ĵ
 Uso      MNTA975                                                     
ٱ


/*/
Function MNTA975SER()

If !Empty(M->TSY_SERVIC)
	aCols[n][nTSYDESERV] := NGSEEK("TS4",M->TSY_SERVIC,1,"TS4_DESCRI")
	aCols[n][nTSYVALOR]  := NGSEEK("TS6",M->TSX_FORNEC+M->TSX_LOJA+M->TSY_SERVIC+TS6->TS6_DOCTO,1,"TS6_VALOR")
Else
	aCols[n][nTSYDESERV] := ''
	aCols[n][nTSYVALOR]  := 0
Endif

Return .t.

/*/


Ŀ
Funo    |MNTA975TOT| Autor  Marcos Wagner Junior   Data  05/09/07 
Ĵ
Descrio Carrega Total do Servico				                          
Ĵ
 Uso      MNTA975                                                     
ٱ


/*/
Function MNTA975TOT(nOpc)

If nOpc = 1
	nQtde := M->TSY_QTDE
Else
	nQtde := aCols[n][nTSYQTDE]
Endif

aCols[n][nTSYTOTAL] := nQtde * aCols[n][nTSYVALOR]

MNTA975TOG()

Return .t.

/*/


Ŀ
Funo    |MNTA975TOG| Autor  Marcos Wagner Junior   Data  05/09/07 
Ĵ
Descrio Carrega Total Geral					                          
Ĵ
 Uso      MNTA975                                                     
ٱ


/*/
Function MNTA975TOG()

Local nI

nTotal := 0
For nI := 1 to Len(aCols)
	If !aCols[nI][Len(aCols[nI])]
		nTotal += aCols[nI][nTSYTOTAL]
	Endif
Next
oTotal:Refresh()

Return .t.
/*/


Ŀ
Funo     MenuDef   Autor  Denis Hyroshi Souza    Data 01/02/2008
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaMDT                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
Local aROTINA := { { STR0001 	,"AxPesqui"  , 0 , 1},; //"Pesquisar"
					{ STR0002	,"MNT975IN"  , 0 , 2},; //"Visualizar"
					{ STR0003   ,"MNT975IN"  , 0 , 3},; //"Incluir"
					{ STR0004   ,"MNT975IN"  , 0 , 4},; //"Alterar"
                    { STR0005   ,"MNA975LEG" , 0 , 6,,.F.}} //"Legenda"
Return aRotina
