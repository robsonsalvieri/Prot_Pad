#INCLUDE "MNTC740.CH"
#INCLUDE "PROTHEUS.CH"

/*/


Ŀ
Funo     MNTC740   Autor  Inacio Luiz Kolling    Data  15/06/99 
Ĵ
Descrio  Consulta Geral de Movimentacao do Bem na Estrutura         
Ĵ
 Uso       Generico                                                   
ٱ


*/
Function MNTC740(cBem)

    Local aNGBEGINPRM  := NGBEGINPRM(,,,,.T.)

    Local aAlias := {}

    Private cBemC       := cBem
    Private aRotina     := MenuDef()
    Private cCadastro   := OemToAnsi(STR0005)  //"Consulta Movimentacao"
    Private lCORRET     := .F.
    Private INCLUI      := .F.
    Private aIndST9     := {}
    Private lFilBrw     := .T.
    Private cFilterNew  := " ", condST9 := " "

    /*----------------------------------------------------------------
    Variveis utilizadas para parmetros:

    MV_PAR01 - De  data inicio
    MV_PAR02 - At data fim
    ----------------------------------------------------------------*/
    OemToAnsi(cCadastro)
    dbSelectArea("ST9")
    dbSetOrder(01)
    If cBemC <> Nil
        lFilBrw := .f.
        dbSeek(xFilial("ST9")+cBemC)
        Set Filter To xFilial("ST9") = ST9->T9_FILIAL .And. cBemC  = ST9->T9_CODBEM
    Else
        bFiltraBrw := {|| Nil}
        condST9    := 'ST9->T9_FILIAL = "'+xFilial("ST9")+'" .And. C740FILBRW()'
        bFiltraBrw := {|| FilBrowse("ST9",@aIndST9,@condST9)}
        Eval(bFiltraBrw)
    EndIf

    mBrowse(6,1,22,75,"ST9")

    dbSelectArea("ST9")
    dbSetOrder(01)
    If cBemC <> Nil
        Set Filter To
    Else
        aEval( aIndST9, {|x| FErase(x[1] + OrdBagExt() )})
        ENDFILBRW("ST9", aIndST9)
    EndIf

    NGRETURNPRM(aNGBEGINPRM)
Return .T.

/*


Ŀ
Funo    MNC740PAR  Autor  Inacio Luiz Kolling    Data  02/08/97 
Ĵ
Descrio  Monta a tela de parametros                                 
Ĵ
 Uso       MNTC740                                                    
ٱ


*/
Function MNC740PAR()

    Pergunte("MNT74C",.T.)

    cFilterNew := 'ST9->T9_FILIAL = "' + xFilial("ST9") + '" .And. C740FILBR2()'
    NGFILBROINI("ST9", aIndST9, cFilterNew)
    dbSelectArea("ST9")
    dbGotop()

Return .T.

/*


Ŀ
Funo    MNC740CON  Autor  Inacio Luiz Kolling    Data  02/08/97 
Ĵ
Descrio  Monta um browse das manutencoes do Bem                     
Ĵ
 Uso       MNTC740                                                    
ٱ


*/
Function MNC740CON

    Local oDLG2
    Local OLDROT
    Local cCONDICAO
    Local nREGST9 := ST9->(Recno())

    //Ŀ
    //Variaveis de Largura/Altura da Janela
    //
    Local aSize    := MsAdvSize(, .F., 430)
    Local nLargura := aSize[5]
    Local nAltura  := aSize[6]
	Local oTempTMP := Nil

    Private cCADASTRO
    Private aDBFMov   := {}

    Pergunte("MNT74C",.F.)

    nRECNO    := Recno()
    dMV_PAR01 := MV_PAR01
    dMV_PAR02 := MV_PAR02
    OLDROT    := aClone(aRotina)
    cCODBEM   := ST9->T9_CODBEM

    aROTINA := {{STR0002, "VISUAC740", 0, 1},;       //"Visualizar"
                {STR0007, "MNC740BE" , 0, 4, 0},;   //"Visual. Bem"
                {STR0008, "MNC740OS" , 0, 4, 0},;   //"Ordem Serv."
                {STR0009, "MNC740IMP", 0, 4, 0}}    //"Imprimir"

    //Nome da Alias utilizadas pelos ListBox
    Private cTrbMov  := "TRBW" //GetNextAlias()

    //Cria tabelas temporarias para os ListBox
    oTempTMP := CriaTRBMov()

    //Cria tabelas temporarias para os ListBox
    LoadTrbMov(cCODBEM)

    dbSelectArea(cTrbMov)
    dbGotop()
    If EoF()
        Help("",1,"NGMSEMOVIM")
        (cTrbMov)->(dbCloseArea())
        Return
    EndIf

    dbSelectArea("ST9")
    dbSeek(xFILIAL("ST9")+cCODBEM)
    cBEM := Alltrim(ST9->T9_CODBEM)+" - "+ST9->T9_NOME+"     "+STR0073+"  "+str(st9->t9_contacu,9)
    cCADASTRO := Oemtoansi(STR0006) + " " + cBEM //"Movimentacao do Bem"

    Define MsDialog oDLG2 Title cCADASTRO From aSize[7],0 To nAltura,nLargura Of oMainWnd Pixel

		oPnlSup := TPanel():New(0,0,,oDLG2,,,,CLR_WHITE,,0,17,.F.,.F.)
		oPnlSup:Align := CONTROL_ALIGN_TOP

		@ 02,001 Button STR0020 Of oPnlSup Size 40,12 Pixel Action VisualTrb("STZ",cTrbMov)    //"&Visualizar"
		@ 02,050 Button STR0021 Of oPnlSup Size 40,12 Pixel Action NG080FOLD("ST9",nREGST9,1) //"Visualizar &Bem"
		@ 02,100 Button STR0022 Of oPnlSup Size 40,12 Pixel Action MNC740OS()                  //"&O.S."
		@ 02,150 Button STR0023 Of oPnlSup Size 40,12 Pixel Action MNC740IMP()                 //"&Imprimir"
		@ 02,200 Button STR0024 Of oPnlSup Size 40,12 Pixel Action oDLG2:End()                 //"&Sair"

		//ListBox do Historico de Movimentacoes
		dbSelectArea(cTrbMov)
		@ 0,0 Listbox oListMov ;
					Fields (cTrbMov)->TZ_BEMPAI,(cTrbMov)->TZ_LOCALIZ,(cTrbMov)->TZ_DATAMOV,(cTrbMov)->TZ_HORAENT,;
					(cTrbMov)->TZ_DATASAI,(cTrbMov)->TZ_HORASAI,(cTrbMov)->KMENTRADA,(cTrbMov)->KMSAIDA,;
					If(Empty((cTrbMov)->TZ_DATASAI),0,(cTrbMov)->KMSAIDA - (cTrbMov)->KMENTRADA),(cTrbMov)->T8_NOME;
					Headers STR0010,STR0011,STR0013,STR0076,STR0014,STR0077,STR0079,STR0080,STR0078,STR0081 ; //'Bem Pai','Localizao','Dt. Entrada','Hr. Entrada','Dt. Sada','Hr. Sada','Contador Pneu','Km Entrada','Km Sada',"Motivo"
				Of oDLG2 Size nLargura,285 FieldSizes Nil,35,35,35,35,35 On DblClick VisualTrb("STZ",cTrbMov)

		oListMov:cAlias    := cTrbMov
		oListMov:Align := CONTROL_ALIGN_ALLCLIENT

		oPnlInf := TPanel():New(0,0,,oDLG2,,,,CLR_WHITE,,0,18,.F.,.F.)
		oPnlInf:Align := CONTROL_ALIGN_BOTTOM

		@ 05,03 Say    STR0025 Of oPnlInf  Pixel   //"Componente"
		@ 05,40 Say    cBEM    Of oPnlInf  Pixel

    Activate Dialog oDLG2

    dbSelectArea(cTrbMov)
    dbGotop()
    Use

    dbSelectArea("ST9")
    DbGoto(nREGST9)
    aRotina := aCLONE(OLDROT)

	oTempTMP:Delete()

Return .T.

/*/


Ŀ
Funcao    CriaTRBMov Autor  Vitor Emanuel Batista  Data 27/07/2010
Ĵ
Descricao Cria TRB do ListBox do Historico de Movimentacao            
Ĵ
 Uso      MNTC125                                                     
ٱ


/*/
Static Function CriaTRBMov()

	Local oTempTMP := Nil

    aAdd(aDBFMov,{ "TZ_BEMPAI"  , "C" , Len(STZ->TZ_BEMPAI)  , 0 })
    aAdd(aDBFMov,{ "TZ_LOCALIZ" , "C" , Len(STZ->TZ_LOCALIZ) , 0 })
    aAdd(aDBFMov,{ "TZ_DATAMOV" , "D" , 08 , 0 })
    aAdd(aDBFMov,{ "TZ_HORAENT" , "C" , 05 , 0 })
    aAdd(aDBFMov,{ "TZ_DATASAI" , "D" , 08 , 0 })
    aAdd(aDBFMov,{ "TZ_HORASAI" , "C" , 05 , 0 })
    aAdd(aDBFMov,{ "KMSAIDA"    , "N" , 12 , 0 })
    aAdd(aDBFMov,{ "KMENTRADA"  , "N" , 12 , 0 })
    aAdd(aDBFMov,{ "DIFERENCA"  , "N" , 12 , 0 })
    aAdd(aDBFMov,{ "T8_NOME"    , "C" , 20 , 0 })
    aAdd(aDBFMov,{ "RECNO"      , "N" , 16 , 0 })

	oTempTMP := NGFwTmpTbl(cTrbMov,aDBFMov,{{'TZ_BEMPAI'}})

Return oTempTMP

/*/


Ŀ
Funcao    LoadTrbMov Autor  Vitor Emanuel Batista  Data 27/07/2010
Ĵ
Descricao Carrega dados da TRB do Historico de Movimentacao           
Ĵ
Parametro cPneu - Indica o Pneu a carregar os dados                   
Ĵ
 Uso      MNTC125                                                     
ٱ


/*/
Static Function LoadTrbMov(cPneu)
    Local cQuery

    dbSelectArea(cTrbMov)
    ZAP

    cQuery := " SELECT TZ_BEMPAI,TZ_LOCALIZ,TZ_DATAMOV,TZ_HORAENT,TZ_DATASAI,"
    cQuery += "        TZ_HORASAI,T8_NOME,STZ.R_E_C_N_O_ as RECNO,
    cQuery += " (SELECT MAX(TP_ACUMCON) FROM "+RetSqlName("STP") + " STP1 "
    cQuery += "  WHERE STP1.D_E_L_E_T_ <> '*' AND TP_FILIAL = "+ValToSql(xFilial("STP"))
    cQuery += "    AND TP_CODBEM = TZ_CODBEM AND TP_DTLEITU||TP_HORA <= TZ_DATAMOV||TZ_HORAENT) AS KMENTRADA,
    cQuery += " (SELECT MAX(TP_ACUMCON) FROM "+RetSqlName("STP") + " STP2 "
    cQuery += "  WHERE STP2.D_E_L_E_T_ <> '*' AND TP_FILIAL = "+ValToSql(xFilial("STP"))
    cQuery += "    AND TP_CODBEM = TZ_CODBEM AND TP_DTLEITU||TP_HORA <= TZ_DATASAI||TZ_HORASAI) AS KMSAIDA
    cQuery += " FROM " + RetSqlName("STZ") + " STZ"
    cQuery += " LEFT JOIN " + RetSqlName("ST8") + " ST8 ON ST8.D_E_L_E_T_ <> '*' AND T8_FILIAL = "+ValToSql(xFilial("ST8"))
    cQuery += "     AND T8_CODOCOR = TZ_CAUSA"
    cQuery += " WHERE STZ.D_E_L_E_T_ <> '*' AND TZ_FILIAL = "+ValToSql(xFilial("STZ"))
    cQuery += "   AND TZ_CODBEM = "+ValToSql(cPneu)
    cQuery += "   AND TZ_DATAMOV >= "+ValToSql(MV_PAR01) + " AND TZ_DATAMOV <= "+ValToSql(MV_PAR02)
    cQuery += " ORDER BY TZ_FILIAL,TZ_CODBEM,TZ_DATAMOV,TZ_HORAENT"

    SqlToTrb(cQuery,aDBFMov,cTrbMov)

    (cTrbMov)->(dbGotop())

Return

/*/


Ŀ
Funcao    VisualTrb  Autor  Vitor Emanuel Batista  Data 02/08/2010
Ĵ
Descricao Visualiza registro de qualquer ListBox pelo NGCAD01         
Ĵ
Parametro cAlias- Tabela do Banco de dados                            
          cTrb  - Tabela temporaria do ListBox                        
Ĵ
 Uso      MNTC125                                                     
ٱ


/*/
Static Function VisualTrb(cAlias,cTrb)

    Local aOldRot  := aCLONE(aRotina)
    aRotina := { {STR0002,"NGCAD01" , 0, 1},;    //"Visual."
                    {STR0028,"OSDETALH" , 0, 2},;    //"Detalh."
                    {STR0029,"OSHISTOCO", 0, 4},;    //"Ocorren."
                    {STR0030,"OshistPro", 0, 4},;    //"proBlem."
                    {STR0070,"NGATRASOS", 0, 4, 0},; //"Motivo Atraso"
                    {STR0031,"OshistEta", 0, 4}}      //"Etapas"

    If (cTrb)->RECNO > 0
        dbSelectArea(cAlias)
        dbGoTo((cTrb)->RECNO)
        If !Eof()
            NGCAD01(cAlias,Recno(),2)
        EndIf
    EndIf

    aRotina := aClone(aOldRot)

Return

/*


Ŀ
Funo    MNC740OS   Autor  Inacio Luiz Kolling    Data  02/08/97 
Ĵ
Descrio  Monta um browse das O. S. manutencoes do Bem               
Ĵ
 Uso       MNTC740                                                    
ٱ


*/
Function MNC740OS()

    Local OLDROT
    Private cCADASTRO := Oemtoansi(STR0027)  //"Ordens de Servico"

    OLDROT  := aCLONE(aROTINA)
    aRotina := { {STR0002, "NGCAD01", 0, 1},;    //"Visual."
                    { STR0028, "OSDETALH" , 0, 2},;    //"Detalh."
                    { STR0029, "OSHISTOCO", 0, 4},;    //"Ocorren."
                    { STR0030, "OshistPro", 0, 4},;    //"proBlem."
                    { STR0070, "NGATRASOS", 0, 4, 0},; //"Motivo Atraso"
                    { STR0031, "OshistEta", 0, 4}}      //"Etapas"

    cKey     := "B" + cCODBEM

    bWHILE   := {|| !EoF() .And. STJ->TJ_TIPOOS  == 'B' .And. STJ->TJ_CODBEM  == cCODBEM }
    bFOR     := {|| STJ->TJ_FILIAL  == xFilial("STJ") .And. STJ->TJ_SITUACA <> 'C' }

    bWHILE2  := {|| !EoF() .And. STS->TS_TIPOOS  == 'B' .And. STS->TS_CODBEM  == cCODBEM}
    bFOR2    := {|| STS->TS_FILIAL  == xFilial("STS") .And. STS->TS_SITUACA <> 'C' }

    bDBFFUNC := {|| NG510PROC( "TJ_", "TS_","STS", cKey, bWHILE2, bFOR2)}

    dbSelectArea("STS")
    dbSetOrder(02)

    dbSelectArea("STJ")
    dbSetOrder(02)

    NGCONSULTA("TRBJ",cKey,bWHILE,bFOR,aRotina,{},bDBFFUNC)

    dbSelectArea("STS")
    dbSetOrder(01)

    dbSelectArea("STJ")
    dbSetOrder(01)

    aRotina := aCLONE(OLDROT)

Return .T.

/*/


Ŀ
Funo    MNC740IMP  Autor  Incio Luiz Kolling    Data  16/06/99 
Ĵ
Descrio Impressao das Movimentacoes do Bens                         
Ĵ
 Uso       Generico                                                   
ٱ

/*/
Function MNC740IMP()

    Local cSTRING    := "STF"
    Local cDESC1     := STR0037 //"Relatorio das movimentacoes do bem. Para cada bem, serao mostrados"
    Local cDESC2     := STR0038 //"As Orden de Servico. A selecao podera ser feita atraves do parametros"
    Local cDESC3     := STR0039 //"da consulta mais parametros adicionais"
    Local WNREL      := "MNTC740"

    Private aRETURN  := {STR0040,1,STR0041,1,2,1,"",1}  //"Zebrado"###"Administracao"
    Private nLASTKEY := 0
    Private cPERG    := "MNT740"
    Private TAMANHO  := "M"
    Private TITULO   := STR0042 //"Movimentacoes do Bem"
    Private NOMEPROG := "MNTC740"

    //Ŀ
    // Variaveis utilizadas para parametros                         
    // mv_par01                Lista Dados Cadastrais ( Sim/Nao)    
    // mv_par02                Lista Carateristicas   ( Sim/Nao)    
    //
    Pergunte(cPERG,.F.)

    //Ŀ
    // Envia controle para a funcao SETPRINT                        
    //
    wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
    If nLastKey = 27
        Set Filter To
        dbSelectArea("ST0")
        Return
    EndIf
    SetDefault(aRETURN,cString)
    RptStatus({|lEnd| C740Imp(@lEnd,wnRel,titulo,tamanho)},titulo)
    dbSelectArea("ST0")

Return Nil

/*/


Ŀ
Funo     C740Imp   Autor  Waldemiro L. Lustosa   Data  13.11.95 
Ĵ
Descrio  Chamada do Relatrio                                       
Ĵ
 Uso       MNTC740                                                    
ٱ


/*/
Static Function C740Imp(lEnd,wnRel,titulo,tamanho)

    Local cRodaTxt 	:= ""
    Local nCntImpr 	:= 0
    Local cBem      := Space(16)
    Local cOldST9 	:= ""
	Local cTRB2		:= GetNextAlias()
	Local oTempTRB

    //Ŀ
    // Contadores de linha e pagina                                 
    //
    Private li := 80 ,m_pag := 1
    //Ŀ
    // Variaveis locais exclusivas deste programa                   
    //
    Private ntipo        := 0
    Private cQuebraServi := Space(06)
    Private cQuebraSeque := 0
    Private cChave       := Space(16)

    //Ŀ
    // Verifica se deve comprimir ou nao                            
    //
    nTIPO := IIF(aRETURN[4]==1,15,18)

    //Ŀ
    // Monta os Cabecalhos                                          
    //
    Private CABEC1       := STR0082 //Bem Pai         Nome do Bem Pai            Local.  Dt.Entr.   Dt.Saida   Causa Remocao            Cont.Entr Cont.Said Tot.Rodad
    Private CABEC2       := "" //Space(097)+STR0075  //"Cont.Entr Cont.Saida Dif.Cont."

    /*/
    0         1         2         3         4         5         6         7         8         9         0         1         2
    0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
    ************************************************************************************************************************************
    Bem Pai                Nome do Bem Pai           Local.  Dt.Entr.   Dt.Saida   Causa Remocao           Cont.Entr Cont.Said Tot.Rodad

    ************************************************************************************************************************************
    Bem : XXXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXX

    XXXXXXXXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXX  XXXXXX  99/99/9999 99/99/9999 XXXXXXXXXXXXXXXXXXXXXXXX 999999999 999999999 999999999
    999999999 999999999 999999999

                 XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XXXXXXXXXXXXXXX      XX

    Ordem    Plano    Servico  Nome do Servico                           Seq. Dt.Emiss.   Valor da O.S.
    XXXXXX   XXXXXX   XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  999  99/99/99   999.999.999,99
    XXXXXX   XXXXXX   XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  999  99/99/99   999.999.999,99
    /*/
    aDBF2 := { {"ORDEM"   , "C", 06,0} ,;
               {"PLANO"   , "C", 06,0} ,;
               {"SERVICO" , "C", 06,0} ,;
               {"SEQRELA" , "C", 03,0} ,;
               {"DTEMISS" , "D", 08,0} ,;
               {"VALOROS" , "N", 20,2},;
               {"ARQUIVO" , "C", 03,0}}

	oTempTRB := FWTemporaryTable():New( cTRB2, aDBF2 )
	oTempTRB:AddIndex( "1", {"ORDEM","PLANO"} )
	oTempTRB:Create()

    dbSelectArea("ST9")
    cOldST9 := dbFilter()
    Set Filter To

    dbSelectArea("TRBW")
    dbGotop()
    SetRegua(LastRec())
    While !Eof()

        IncRegua()
        SOMALINHA()
        @ Li,000 Psay TRBW->TZ_BEMPAI

        dbSelectArea("ST9")
        dbSetOrder(01)
        dbSeek(xFILIAL("ST9")+TRBW->TZ_BEMPAI)
        @ Li,023 Psay Substr(ST9->T9_NOME,1,25)
        @ Li,049 Psay TRBW->TZ_LOCALIZ
        @ Li,057 Psay TRBW->TZ_DATAMOV Picture '99/99/99'
        @ Li,068 Psay TRBW->TZ_DATASAI Picture '99/99/99'
        @ Li,079 Psay Substr(TRBW->T8_NOME,1,24)
        @ Li,103 Psay TRBW->KMENTRADA Picture '999999999'
        @ Li,113 Psay TRBW->KMSAIDA Picture '999999999'
        @ Li,123 Psay If(Empty(TRBW->TZ_DATASAI),0,TRBW->KMSAIDA - TRBW->KMENTRADA) Picture '999999999'

        SOMALINHA()
        If MV_PAR01 == 1
            SOMALINHA()
            @ Li,000 Psay STR0046 //"Fornecedor:"
            @ Li,012 Psay ST9->T9_FORNECE + " - " + ST9->T9_LOJA
            @ Li,018 Psay ST9->T9_LOJA

            dbSelectArea("SA2")
            dbSetOrder(01)
            dbSeek(xFILIAL("SA2")+ST9->T9_FORNECE)
            @ Li,028 Psay Substr(SA2->A2_NOME,1,40)
            @ Li,069 Psay STR0047 //"Fabricante:"
            @ Li,081 Psay ST9->T9_FABRICA

            dbSelectArea("ST7")
            dbSetOrder(01)
            dbSeek(xFILIAL("ST7")+ST9->T9_FABRICA)
            @ Li,090 Psay Substr(ST7->T7_NOME,1,40)
            SOMALINHA()
            @ Li,000 Psay STR0048 //"Modelo....:"
            @ Li,012 Psay ST9->T9_MODELO
            @ Li,034 Psay STR0049 //"Serie..:"
            @ Li,042 Psay ST9->T9_SERIE
            @ Li,059 Psay STR0050 //"Dt.Compra.:"
            @ Li,071 Psay ST9->T9_DTCOMPR Picture '99/99/99'
            @ Li,087 Psay STR0051 //"Cod.Imob:"
            @ Li,098 Psay Substr(ST9->T9_CODIMOB,1,13)
            @ Li,111 Psay STR0052 //"Prazo Gar.:"
            @ Li,123 Psay ST9->T9_PRGARAN Picture '999999'
            @ Li,131 Psay ST9->T9_UNGARAN

            SOMALINHA()

            @ Li,000 Psay STR0053 //"Dt Garant.:"
            @ Li,012 Psay ST9->T9_DTGARAN Picture '99/99/99'
            @ Li,034 Psay STR0054 //"Prior..:"
            @ Li,045 Psay ST9->T9_PRIORID
            @ Li,059 Psay STR0055 //"Cod.Estoq.:"
            @ Li,071 Psay ST9->T9_CODESTO
            @ Li,088 Psay STR0056 //"Tem Estr:"
            @ Li,099 Psay ST9->T9_ESTRUTU
            @ Li,112 Psay STR0057 //"Contador..:"
            @ Li,123 Psay ST9->T9_POSCONT Picture '999999999'

            SOMALINHA()

            @ Li,000 Psay STR0058 //"Dt Acomp..:"
            @ Li,012 Psay ST9->T9_DTULTAC Picture '99/99/99'
            @ Li,034 Psay STR0059 //"Var/Dia:"
            @ Li,042 Psay ST9->T9_VARDIA Picture '999999'
            @ Li,059 Psay STR0060 //"Limite Con:"
            @ Li,071 Psay ST9->T9_LIMICON Picture '999999999'
            @ Li,087 Psay STR0061 //"Sit.Bem.:"
            @ Li,098 Psay ST9->T9_SITBEM
            @ Li,111 Psay STR0062 //"Sit.Manut.:"
            @ Li,123 Psay ST9->T9_SITMAN

            SOMALINHA()

            @ Li,000 Psay STR0063 //"Custo Hora:"
            @ Li,012 Psay ST9->T9_CUSTOHO  Picture '@E 999,999.99'
            @ Li,034 Psay STR0064 //"Local..:"
            @ Li,042 Psay ST9->T9_LOCAL
            @ Li,059 Psay STR0065 //"Tipo(PCP).:"
            @ Li,071 Psay ST9->T9_FERRAME
            @ Li,085 Psay STR0066 //"Recur/Ferr:"
            @ Li,097 Psay ST9->T9_RECFERR
            @ Li,116 Psay STR0067 //"Chapa:"
            @ Li,123 Psay ST9->T9_CHAPA
        EndIf

        If MV_PAR02 == 1
            dbSelectArea("STB")
            dbSetOrder(01)
            If dbSeek(xFILIAL("STB")+TRBW->TZ_BEMPAI)
                lCARACT := .T.
                SOMALINHA()
                While !EoF() .And. STB->TB_FILIAL == xFILIAL("STB") .And.;
                        STB->TB_CODBEM == TRBW->TZ_BEMPAI

                    SOMALINHA()
                    If lCARACT
                        @ Li,000 Psay STR0068 //"Caracter..:"
                        lCARACT := .F.
                    EndIf

                    @ Li,013 Psay STB->TB_CARACTE

                    @ Li,022 Psay Substr( NGSEEK("TPR", STB->TB_CARACTE, 1, "TPR->TPR_NOME"), 1, TAMSX3("TB_NOME")[1])

                    @ Li,064 Psay NGRetSX3Box( "TB_CONDOP", STB->TB_CONDOP) Picture '@!'
                    @ Li,071 Psay STB->TB_DETALHE Picture '@!'

                    If !Empty( STB->TB_INFO02 )
                        @ Li,088 Psay STB->TB_INFO02 Picture '@!'
                    Else
                        @ Li,088 Psay '-' Picture '@!'
                    EndIf

                    @ Li,105 Psay STB->TB_UNIDADE Picture '@!'

                    dbSelectArea("STB")
                    dbSkip()
                EndDo
            EndIf
        EndIf
        // PROCESSA AS ORDENS DE SERVICO NORMAL DO BEM PAI

        dbSelectArea("STJ")
        dbSetOrder(02)
        dbSeek(xFILIAL("STJ")+"B"+TRBW->TZ_BEMPAI)
        cBEMPAI := ""
        While !Eof() .And. STJ->TJ_FILIAL == xFILIAL("STJ") .And.;
                STJ->TJ_TIPOOS == "B" .And. STJ->TJ_CODBEM == TRBW->TZ_BEMPAI

            If cBEMPAI != TRBW->TZ_BEMPAI
                If STJ->TJ_SITUACA <> 'C'
                    dbSelectArea(cTRB2)
                    (cTRB2)->(DbAppend())
                    Replace ORDEM   With STJ->TJ_ORDEM
                    Replace PLANO   With STJ->TJ_PLANO
                    Replace SERVICO With STJ->TJ_SERVICO
                    Replace SEQRELA With STJ->TJ_SEQRELA
                    Replace DTEMISS With STJ->TJ_DTORIGI
                    Replace VALOROS With STJ->TJ_CUSTMDO + STJ->TJ_CUSTMAT + STJ->TJ_CUSTTER
                    Replace ARQUIVO With "STJ"
                EndIf
                cBEMPAI := TRBW->TZ_BEMPAI
            EndIf
            dbSelectArea("STJ")
            dbSkip()
        EndDo
        // PROCESSA AS ORDENS DE SERVICO HISTORICO DO BEM PAI

        dbSelectArea("STS")
        dbSetOrder(02)
        dbSeek(xFILIAL("STS")+"B"+TRBW->TZ_BEMPAI)
        cBEMPAI := ""
        While !Eof() .And. STS->TS_FILIAL == xFILIAL("STS") .And.;
                STS->TS_TIPOOS == "B" .And. STS->TS_CODBEM == TRBW->TZ_BEMPAI

            If cBEMPAI != TRBW->TZ_BEMPAI
                If STS->TS_SITUACA <> 'C'
                    dbSelectArea(cTRB2)
                    (cTRB2)->(DbAppend())
                    Replace ORDEM   With STS->TS_ORDEM
                    Replace PLANO   With STS->TS_PLANO
                    Replace SERVICO With STS->TS_SERVICO
                    Replace SEQRELA With STS->TS_SEQRELA
                    Replace DTEMISS With STS->TS_DTORIGI
                    Replace VALOROS With STS->TS_CUSTMDO + STS->TS_CUSTMAT + STS->TS_CUSTTER
                    Replace ARQUIVO With "STS"
                EndIf
                cBEMPAI := TRBW->TZ_BEMPAI
            EndIf
            dbSelectArea("STS")
            dbSkip()
        End
        dbSelectArea(cTRB2)
        dbGotop()
        SOMALINHA()
        lPRIN := .T.

        While !EoF()
            SOMALINHA()
            If lPRIN
                @ Li,000 PSAY STR0069 //"Ordem    Plano    Servico  Nome do Servico                           Seq. Dt.Emiss.   Valor da O.S."
                SOMALINHA()
                lPRIN := .F.
            Endif
            If TRBW->TZ_BEMPAI != cBEM
                @ Li,000 Psay (cTRB2)->ORDEM
                @ Li,009 Psay (cTRB2)->PLANO
                @ Li,018 Psay (cTRB2)->SERVICO
                dbSelectArea("ST4")
                dbSetOrder(01)
                dbSeek(xFILIAL('ST4')+(cTRB2)->SERVICO)
                @ Li,027 Psay Substr(ST4->T4_NOME,1,40)
                @ Li,069 Psay (cTRB2)->SEQRELA Picture '@!'
                @ Li,074 Psay (cTRB2)->DTEMISS Picture '99/99/99'
                @ Li,085 Psay (cTRB2)->VALOROS Picture '@E 999,999,999.99'
                dbSelectArea(cTRB2)
                dbSkip()
            EndIf
        EndDo

        SOMALINHA()
        @ Li,000 Psay Replicate("*",132)
        SOMALINHA()

        dbSelectArea(cTRB2)
        Zap
        dbSelectArea("TRBW")
        dbSkip()
    EndDo

    Roda(nCntImpr,cRodaTxt,Tamanho)
    Set Filter To
    Set Device To Screen

    If aRETURN[5] = 1
        Set Printer To
        dbCommitAll()
        OurSpool(wnrel)
    EndIf
    MS_FLUSH()

	oTempTRB:Delete()

    dbSelectArea("ST9")
    Set Filter To &(cOldST9)

    dbSelectArea("TRBW")
    dbGotop()

Return Nil

/*/


Ŀ
 Funo    SOMALINHA Autor  NG Informatica Ltda    Data    /06/97 
Ĵ
 Descrio Incrementa Linha e Controla Salto de Pagina                
Ĵ
 Uso       MNTC740                                                    
ٱ

/*/
Static Function SOMALINHA()

    li++
    If LI > 58
        CABEC( TITULO, CABEC1, CABEC2, NOMEPROG, TAMANHO, nTIPO)
        LI := PROW() + 1
        @ LI,000 Psay STR0026 + ".:" //"Bem :"
        @ LI,006 Psay cCODBEM

        dbSelectArea("ST9")
        dbSetOrder(01)
        dbSeek(xFILIAL("ST9")+cCODBEM)
        @ LI,023 Psay ST9->T9_NOME
        LI := PRow() + 2
    EndIf

Return .T.

/*/


Ŀ
Programa  MenuDef    Autor  Ricardo Dal Ponte      Data 29/11/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
                1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()

    Local aRotina := { {STR0001,"AxPesqui" , 0, 1},;   //"Pesquisa"
                        {STR0002,"NG080FOLD", 0, 2},;    //"Visualizar"
                        {STR0003,"MNC740PAR", 0, 3, 0},; //"Parametros"
                        {STR0004,"MNC740CON", 0, 4, 0}}  //"Consulta"
Return(aRotina)

/*/


Ŀ
 Funo   C740FILBRW Autor  Evaldo Cevinscki Jr.   Data  05/03/10 
Ĵ
 DescrioFiltro dos bens que possuem alguma movimentacao - STZ       
Ĵ
 Uso       MNTC740                                                    
ٱ

/*/
Function C740FILBRW()
    Local aArea := GetArea()

    dbSelectArea("STZ")
    dbSetOrder(02)
    If dbSeek(xFilial("STZ") + ST9->T9_CODBEM)
        lRetFil := .t.
    Else
        lRetFil := .f.
    EndIf

    RestArea(aArea)
Return lRetFil

/*/


Ŀ
 Funo   C740FILBR2 Autor  Evaldo Cevinscki Jr.   Data  05/03/10 
Ĵ
 DescrioFiltro dos bens que possuem alguma movimentacao no periodo  
          informado no botao Parametro                                
Ĵ
 Uso       MNTC740                                                    
ٱ

/*/
Function C740FILBR2()

    Local aArea := GetArea()
    Local lRetFil := .f.

    Pergunte("MNT74C",.F.)

    dbSelectArea("STZ")
    dbSetOrder(02)
    dbSeek(xFilial("STZ")+ST9->T9_CODBEM+DTOS(MV_PAR01),.T.)
    While !Eof() .And. STZ->TZ_FILIAL == xFILIAL("STZ") .And.;
            STZ->TZ_CODBEM == ST9->T9_CODBEM .And. STZ->TZ_DATAMOV <= MV_PAR02
        lRetFil := .t.
        dbSelectArea("STZ")
        dbSkip()
    EndDo

    If IsInCallStack("MNC740IMP")
        Pergunte("MNT740",.F.)
    EndIf

    RestArea(aArea)

Return lRetFil