#INCLUDE "MNTA955.ch"
#Include "Protheus.ch"

/*/


Ŀ
Funo     MNTA955   Autor  Marcos Wagner Junior   Data  23/08/07 
Ĵ
Descrio  Cadastro de Bem x Servico x Fornecedor					  
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function MNTA955()

//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  
//
Local aNGBEGINPRM := NGBEGINPRM()

Private aRotina := MenuDef()

Private cCadastro := STR0006 //"Cadastro de Bem x Servio x Fornecedor"
Private aCHKDEL   := {}
Private bNGGRAVA

DbSelectArea("TSV")
DbSetOrder(1)
mBrowse( 6, 1,22,75,"TSV")

//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK)                          
//
NGRETURNPRM(aNGBEGINPRM)

Return .t.

/*/


Ŀ
Funo    MNTA955CAD Autor  Marcos Wagner Junior   Data 28/03/2007
Ĵ
Descrio Atribui a filial do motorista				                  
Ĵ
 Uso      MNTA955	                                                  
ٱ


/*/
Function MNTA955CAD(cAlias,nRecno,nOpc)

// Release menor ou igual a 23
Local lRPORel23 := GetRPORelease() <= '12.1.023'

Private cMEMODES  := ''
Private lModoTSV  := If(NGSX2MODO("TSV") = 'C',.t.,.f.)
Private cNGFilCad := xFilial("TS1")
Private cFilCad   := cNGFilCad
Private nValorTS6 := 0
Private cOldFornec := ''
Private cOldLoja   := ''

If nOpc != 3 .And. lRPORel23
   M->TSV_OBSERV := MsMM(TSV->TSV_MMOBS)
   cCDDESC := TSV->TSV_MMOBS
Endif

bNGGrava := {|| MNTA955BNG()}

nRET := NGCAD01("TSV",nRecno,nOpc)

If nRET = 1 .And. nOpc != 2 .And. lRPORel23
	nSMM := If(nOpc = 3 .Or. nOpc = 4,1,2)
   If(nOpc = 3,MSMM(,TamSX3("TSV_MMOBS")[1],,cMEMODES,nSMM,,,"TSV","TSV_MMOBS"),;
	            MSMM(cCDDESC,,,cMEMODES,nSMM,,,"TSV","TSV_MMOBS"))
	If nOpc = 5
 		dbSelectArea("SYP")
 		dbSetOrder(01)
 		dbSeek(xFilial("SYP")+TSV->TSV_MMOBS)
 		While !Eof() .AND. SYP->YP_FILIAL = xFilial("SYP") .AND. TSV->TSV_MMOBS = SYP->YP_CHAVE
 		   Reclock("SYP",.f.)
 		   dbDelete()
 		   MsUnLock("SYP")
 			dbSkip()
 		End
 	Endif
Endif

//Para funcionar o IniBrw
ST9->(dbSetOrder(1))
Return .T.

/*/


Ŀ
Funo    MNTA955DAT Autor  Marcos Wagner Junior   Data  23/08/07 
Ĵ
Descrio  Consistencia de Datga Inicio x Data Fim						  
Ĵ
 Uso       MNTA955                                                    
ٱ


/*/
Function MNTA955DAT()
Local lTroFil := .f.
If !Empty(M->TSV_DTINIC) .AND. !Empty(M->TSV_DTFIM)
	If M->TSV_DTINIC > M->TSV_DTFIM
	   MsgStop(STR0007,STR0008) //"Data Fim dever ser igual ou maior que Data Inicio!"###"Ateno"
		Return .f.
	Endif
Endif

aRetFil := NgFilTPN(M->TSV_CODBEM,M->TSV_DTINIC,Time())
If aRetFil[1] <> nil
   cFilCad := aRetFil[1]
Else
   If NGIFDBSEEK("ST9",M->TSV_PLACA,14,.F.)
      cFilCad := POSICIONE('ST9',14,M->TSV_PLACA,'T9_FILIAL')
   Else
      If NGIFDBSEEK("TMS",M->TSV_CODBEM,5,.F.)
         cFilCad := NGSEEK('TMS',M->TSV_CODBEM,5,'DA3_FILIAL')
      ElseIf NGIFDBSEEK("TS3",M->TS1_CODBEM,1,.F.)
         cFilCad := NGSEEK('TS3',M->TSV_CODBEM,1,'TS3_FILIAL')
      Endif
   Endif
Endif

If lModoTSV
   If !Empty(cFilCad)
      lTroFil := .t.
   Endif
Else
   If Empty(cFilCad)
      lTroFil := .t.
   Endif
Endif

If lTroFil
   cFilCad := xFilial("TSV")
Endif
Return .t.

/*/


Ŀ
Funo    MNTA955BNG Autor  Marcos Wagner Junior   Data  23/08/07 
Ĵ
Descrio  Consistencia final antes da gravacao							  
Ĵ
 Uso       MNTA955                                                    
ٱ


/*/
Function MNTA955BNG()
Local aArea  := TSV->(GetArea())
Local nRecno := TSV->(Recno())
Local lRet := .f.

dbSelectArea("TSV")
dbSetOrder(1)
If !dbSeek(xFilial("TSV")+M->TSV_CODBEM+M->TSV_SERVIC+M->TSV_FORNEC+M->TSV_LOJA+DTOS(M->TSV_DTINIC))
	lRet := .t.
Else
	If !INCLUI
		lRet := !(nRecno != Recno())
	EndIf
Endif

If !lRet
	Help(" ",1,"JAGRAVADO")
EndIf

cMEMODES := M->TSV_OBSERV
RestArea(aArea)
Return lRet

/*/


Ŀ
Funo    MNTA955SER Autor  Marcos Wagner Junior   Data  23/08/07 
Ĵ
Descrio  Filtra apenas os servicos gravados na TS6				  
Ĵ
 Uso       MNTA955                                                    
ٱ


/*/
Function MNTA955SER(nOp)
Local aOldArea := GetArea()

If nOp = 1
	cServico := TS4->TS4_CODSDP
Else
	cServico := M->TSV_SERVIC
Endif

/*If !Empty(M->TSV_SERVIC) .And.  "'" $ AllTrim(M->TSV_SERVIC)
	MsgStop(STR0009,STR0008) //"Servio informado no est cadastrado para esse Fornecedor/Loja!"###"Ateno"
	Return .F.
EndIf     */
cAliasQry := GetNextAlias()
cQuery := " SELECT DISTINCT(TS6.TS6_SERVIC) "
cQuery += " FROM " + RetSqlName("TS6") + " TS6 "
cQuery += " WHERE TS6.TS6_SERVIC = "+VALTOSQL(cServico)
If !Empty(M->TSV_FORNEC)
	cQuery += " AND TS6.TS6_FORNEC = "+VALTOSQL(M->TSV_FORNEC)
Endif
If !Empty(M->TSV_LOJA)
	cQuery += " AND TS6.TS6_LOJA = "+VALTOSQL(M->TSV_LOJA)
Endif
cQuery += " AND TS6.D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry,.F.,.T.)
dbGotop()

If nOp = 1
	If Eof()
		(cAliasQry)->(DbCloseArea())
		RestArea(aOldArea)
		Return .f.
	Else
		(cAliasQry)->(DbCloseArea())
		RestArea(aOldArea)
		Return .t.
	Endif
Else
	If Eof()
		RestArea(aOldArea)
		If !Empty(M->TSV_FORNEC) .AND. !Empty(M->TSV_LOJA)
			MsgStop(STR0009,STR0008) //"Servio informado no est cadastrado para esse Fornecedor/Loja!"###"Ateno"
		Else
			MsgStop(STR0010,STR0008)		 //"Servio informado no est cadastrado na rotina de Cadastro de Servicos por Fornecedor!"###"Ateno"
		Endif
		(cAliasQry)->(DbCloseArea())
		Return .f.
	Else
		(cAliasQry)->(DbCloseArea())
		RestArea(aOldArea)
		Return .t.
	Endif
Endif

/*/


Ŀ
Funo    MNTA955FOR Autor  Marcos Wagner Junior   Data  23/08/07 
Ĵ
Descrio  Filtra apenas os fornecedores gravados na TS6				  
Ĵ
 Uso       MNTA955                                                    
ٱ


/*/
Function MNTA955FOR(nOp)

Local aOldArea := GetArea()

cAliasQry := GetNextAlias()
cQuery := " SELECT DISTINCT(TS6.TS6_SERVIC), R_E_C_N_O_ "
cQuery += " FROM " + RetSqlName("TS6") + " TS6 "
cQuery += " WHERE   TS6.TS6_FORNEC = "+VALTOSQL(M->TSV_FORNEC)
If !Empty(M->TSV_SERVIC)
	cQuery += " AND  TS6.TS6_SERVIC = "+VALTOSQL(M->TSV_SERVIC)
Endif
If nOp = 2
	cQuery += " AND TS6.TS6_LOJA = "+VALTOSQL(M->TSV_LOJA)
Endif
cQuery += " AND   TS6.D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry,.F.,.T.)
dbGotop()

If Eof()
	If nOp = 1
		If !Empty(M->TSV_FORNEC)
		   MsgStop(STR0012,STR0008) //"Fornecedor/Loja informado no est cadastrado para esse Servio!"###"Ateno"
	 	Endif
	Else
		If !Empty(M->TSV_LOJA)
		   MsgStop(STR0012,STR0008)	 //"Fornecedor/Loja informado no est cadastrado para esse Servio!"###"Ateno"
		Endif
	Endif
	M->TSV_FORNEC := Space(Len(M->TSV_FORNEC))
	M->TSV_LOJA := Space(Len(M->TSV_LOJA))
	M->TSV_QTDSER := 0
	M->TSV_VALOR := 0
   cOldFornec := M->TSV_FORNEC
	cOldLoja   := M->TSV_LOJA
Else
	If nValorTS6 == TS6->TS6_VALOR
		If !Empty(cOldFornec) .AND. !Empty(cOldLoja)
			dbSelectArea("TS6")
			dbGoTo((cAliasQry)->R_E_C_N_O_)
			M->TSV_QTDSER := 0
			M->TSV_VALOR := 0
		Endif
	ElseIf nValorTS6 != TS6->TS6_VALOR .OR. M->TSV_FORNEC != cOldFornec .OR. M->TSV_LOJA != cOldLoja
		M->TSV_QTDSER := 0
		M->TSV_VALOR := 0
	Endif
	nValorTS6 := TS6->TS6_VALOR
	If M->TSV_FORNEC != cOldFornec .OR. M->TSV_LOJA != cOldLoja
		cOldFornec := M->TSV_FORNEC
		cOldLoja   := M->TSV_LOJA
	Endif
	(cAliasQry)->(DbCloseArea())
	RestArea(aOldArea)
	Return .t.
Endif
(cAliasQry)->(DbCloseArea())
RestArea(aOldArea)
Return .t.

/*/


Ŀ
Funo    MNTA955QTD Autor  Marcos Wagner Junior   Data  17/12/07 
Ĵ
Descrio  Calcula o Valor														  
Ĵ
 Uso       MNTA955                                                    
ٱ


/*/
Function MNTA955QTD()

If !Empty(M->TSV_FORNEC) .AND. !Empty(M->TSV_LOJA) .AND. !Empty(M->TSV_SERVIC)
	dbSelectArea("TS6")
	dbSetOrder(01)
	If dbSeek(xFilial("TS6")+M->TSV_FORNEC+M->TSV_LOJA+M->TSV_SERVIC)
		M->TSV_VALOR := M->TSV_QTDSER * TS6->TS6_VALOR//nValorTS6
	Endif
Endif

Return .t.

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 02/02/2008
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaMNT                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
Local aRotina :=	{{STR0001	,"AxPesqui", 0 , 1},; //"Pesquisar"
                    {STR0002,"MNTA955CAD" , 0 , 2},; //"Visualizar"
                    {STR0003	,"MNTA955CAD" , 0 , 3},; //"Incluir"
                    {STR0004	,"MNTA955CAD" , 0 , 4, 0},; //"Alterar"
                    {STR0005	,"MNTA955CAD" , 0 , 5, 3} } //"Excluir"

Return aRotina