#INCLUDE "mdta285.ch"
#Include "Protheus.ch"
/*/


Ŀ
Funo     MDTA285   Autor Denis Hyroshi de Souza  Data 28/07/2003
Ĵ
Descrio Exames necessarios no novo ambiente de trabalho do Funciona.
ٱ


/*/
Function MDTA285
//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  	  	  
//
Local aNGBEGINPRM := NGBEGINPRM()

Private nSizeCli := If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1]))
Private nSizeLoj := If((TAMSX3("A1_LOJA")[1]) < 1,2,(TAMSX3("A1_LOJA")[1]))
Private cCliMdtPs  := Space(Len(SA1->A1_COD+SA1->A1_LOJA))
Private lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )

PRIVATE aRotina := MenuDef()

PRIVATE cCadastro := OemtoAnsi(STR0004) //"Exames por Mudanca de Funcao ou de Centro de Custo"
PRIVATE cAlias    := "SRA"
PRIVATE cNUMFIC   := space(09)
PRIVATE cPrograma := "MDTA285"

Private cAlias2 := "SI3"
Private cDescr  := "SI3->I3_DESC"
PRIVATE nSizeSI3, nSizeSRJ, nSizeSQB
nSizeSI3 := If((TAMSX3("I3_CUSTO")[1]) < 1,9,(TAMSX3("I3_CUSTO")[1]))
nSizeSRJ := If((TAMSX3("RJ_FUNCAO")[1]) < 1,5,(TAMSX3("RJ_FUNCAO")[1]))
nSizeSQB := If((TAMSX3("QB_DEPTO")[1]) < 1,9,(TAMSX3("QB_DEPTO")[1]))

aCHKDEL := { }

If FindFunction("MDTRESTRI") .AND. !MDTRESTRI(cPrograma)
	//Ŀ
	// Devolve variaveis armazenadas (NGRIGHTCLICK) 			 			  
	//
	NGRETURNPRM(aNGBEGINPRM)
	Return .F.
Endif

//Ŀ
// Endereca a funcao de BROWSE                                  
//
If lSigaMdtps
	dbSelectArea("SA1")
	dbSetOrder(1)
	mBrowse( 6, 1,22,75,"SA1")
Else
	dbSelectArea("SRA")
	dbSetOrder(1)
	mBrowse( 006 , 001 , 022 , 075 , "SRA" , NIL , NIL , NIL , NIL , NIL , fCriaCor() )
Endif

//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK)                          
//
NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*/


Ŀ
Funo     MDT285SRA   Autor  Denis                    Data 29/06/10  
Ĵ
Descrio  Filtra os funcionarios do cliente                              
Ĵ
Sintaxe    MDT285SRA()                                                    
Ĵ
Parametros                                                                
                                                                          
Ĵ
Retorno                                                                   
Ĵ
Uso         SIGAMDT                                                       
ٱ

/*/
Function MDT285SRA()

Local aArea	:= GetArea()
Local oldROTINA := aCLONE(aROTINA)
Local oldCad := cCadastro
cCliMdtPs := SA1->A1_COD+SA1->A1_LOJA

nSizeSA1 := If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1]))
nSizeLo1 := If((TAMSX3("A1_LOJA")[1]) < 1,2,(TAMSX3("A1_LOJA")[1]))

aRotina :=	{{STR0001,"AxPesqui",0,1},; //"Pesquisar"
			 {STR0002,"NGCAD01" ,0,2},; //"Visualizar"
			 {STR0003,"MDT285EX",0,2},; //"Exames"
			 {STR0035, "GpLegend" , 0 , 6,,.F. }} //"Legenda"

dbSelectArea("SRA")
Set Filter To SubStr(SRA->RA_CC,1,nSizeSA1+nSizeLo1) == cCliMdtps
dbSetOrder(1)
mBrowse( 006 , 001 , 022 , 075 , "SRA" , NIL , NIL , NIL , NIL , NIL , fCriaCor() )

dbSelectArea("SRA")
Set Filter To

aROTINA := aCLONE(oldROTINA)
RestArea(aArea)
cCadastro := oldCad

Return

/*/


Ŀ
Funo    MDT285EX   Autor Denis Hyroshi de Souza  Data 28/07/2003
Ĵ
Descrio Mostrar os exames que necessitara o funcionario no novo amb.
ٱ


/*/
Function MDT285EX()

	Local oDlg2, oMenu2, oPanelDlg2
	Local nOpcc:=0
	Local cF3_CTT
	Local cF3_SRJ
	Local oMARKT, oTemp285
	Local cBuscaEx   := Space(40)
	Local aParam     := {}
	Local dtIni      := dDataBase
	Local dtFim      := dDataBase + 1
	Local lMVContab  := Alltrim( GETMV("MV_MCONTAB") ) == "CTB"

	Private lPrestSX6 := SuperGetMv( "MV_NGMDTPS", .F., "N" ) == "S"
	Private cCustoCod := Space(nSizeSI3)
	Private cCustoNom := Space(25)
	Private cFuncaCod := Space(nSizeSRJ)
	Private cFuncaNom := Space(20)
	Private cDepCod   := Space(nSizeSQB)
	Private cDepNome  := Space(30)
	Private cPcmso := " "
	Private cMARCA := GetMark()
	Private lInverte := .F.
	Private aTarFun := {}
	Private lDepto := NGCADICBASE( "TN0_DEPTO" , "A" , "TN0" , .F. )

	aDBF := {}
	AADD(aDBF,{ "TN5_OK"     , "C" ,02, 0 })
	AADD(aDBF,{ "TN5_MARK"   , "C" ,02, 0 })
	AADD(aDBF,{ "TN5_CODTAR" , "C" ,06, 0 })
	AADD(aDBF,{ "TN5_NOMTAR" , "C" ,40, 0 })

	oTemp285 := FWTemporaryTable():New( "TRB285", aDBF )
	oTemp285:AddIndex( "1", {"TN5_CODTAR"} )
	oTemp285:AddIndex( "2", {"TN5_NOMTAR"} )
	oTemp285:AddIndex( "3", {"TN5_MARK","TN5_CODTAR"} )
	oTemp285:Create()

	aTRBT := {}
	AADD(aTRBT,{ "TN5_OK"    ,NIL," ",})
	AADD(aTRBT,{ "TN5_CODTAR",NIL,STR0037,}) //"Tarefa"
	AADD(aTRBT,{ "TN5_NOMTAR",NIL,STR0038,}) //"Nome Tarefa"

	cSeekRis  := xFilial("TN5")
	cWhileRis := '.T.'
	nIndRis := 1
	cSeekTarFun  := xFilial("TN6")
	cIndTarFun := 1
	If lSigaMdtPs
		cSeekRis  := xFilial("TN5")+cCliMdtPs
		cWhileRis := 'TN5->TN5_CLIENT+TN5->TN5_LOJA == cCliMdtPs'
		nIndRis   := 3
		cSeekTarFun  := xFilial("TN6")+cCliMdtPs
		cIndTarFun := 3
	Endif

	dbSelectArea("TN5")
	dbSetOrder(nIndRis)
	dbSeek(cSeekRis)
	While !eof() .and. TN5->TN5_FILIAL == xFilial("TN5") .and. &(cWhileRis)
		TRB285->(DbAppend())
		TRB285->TN5_CODTAR := TN5->TN5_CODTAR
		TRB285->TN5_NOMTAR := TN5->TN5_NOMTAR

		DbSelectArea("TN6")
		DbSetOrder(cIndTarFun)
		If DbSeek(cSeekTarFun + TN5->TN5_CODTAR + SRA->RA_MAT)
			If TN6->TN6_DTINIC >= dtIni .And. TN6->TN6_DTINIC < dtFim .Or. ;
				TN6->TN6_DTINIC < dtIni .and. (TN6->TN6_DTTERM >= dtIni .OR. Empty(TN6->TN6_DTTERM))
				TRB285->TN5_OK := cMarca
				TRB285->TN5_MARK := Space(2)
			Endif
		Else
			TRB285->TN5_MARK := "ZZ"
		Endif

		dbSelectArea("TN5")
		dbSkip()
	EndDo

	If lSigaMdtps
		cCliMdtPs := Substr(SRA->RA_CC,1,nSizeCli+nSizeLoj)
	Endif

	//Verifica se o funcionario esta demitido
	If !SitMdtFunc(SRA->RA_MAT,.f.,.t.,.t.)
		oTemp285:Delete()
		Return
	Endif

	If lMVContab
		cAlias2 := "CTT"
		cDescr  := "CTT->CTT_DESC01"
	Endif

	cF3_CTT := cAlias2
	cF3_SRJ := "SRJ"
	If lSigaMdtps
		cF3_CTT := "MDTPS2"
		cF3_SRJ := "TOS"
	Endif

	cCustoCod := SRA->RA_CC
	cCustoNom := IF(&(cAlias2+"->(DBSEEK(xFILIAL(cAlias2)+SRA->RA_CC))"),Substr(&cDescr,1,25),Space(25))
	cFuncaCod := SRA->RA_CODFUNC
	cFuncaNom := IF(SRJ->(DBSEEK(xFILIAL("SRJ")+SRA->RA_CODFUNC)),Substr(SRJ->RJ_DESC,1,20),Space(20))
	If lDepto //Caso utilize departamento
		cDepCod   := SRA->RA_DEPTO
		cDepNome:= NGSEEK( 'SQB',cDepCod,01,'QB_DESCRIC' )
	Endif

	Dbselectarea("TM0")
	Dbsetorder(3)
	If !Dbseek(SRA->RA_FILIAL+SRA->RA_MAT)
		MsgStop(STR0005,STR0006) //"Funcionrio no possui Ficha Mdica cadastrada."###"ATENCAO"
		oTemp285:Delete()
		Return .t.
	Endif

	DbselectArea("TMW")
	DbsetOrder(1)
	Dbgobottom()
	Do While !BOF()
		If TMW->TMW_FILIAL != xFilial("TMW")
			Dbskip(-1)
			Loop
		Endif
		If dDataBase >= TMW->TMW_DTINIC .AND. dDataBase <= TMW->TMW_DTFIM
			cPcmso := TMW->TMW_PCMSO
			Exit
		Endif
		DbSkip(-1)
	End

	aExaInd  := {"1="+STR0039,"2="+STR0040,"3="+STR0041} //"Codigo"###"Nome"###"Marcados"
	aAdd(aParam,{"TN5_OK",cMARCA,"TN5_MARK"})
	cIndExam := "1"

	DbSelectArea("TRB285")
	DbGotop()

	DEFINE MSDIALOG oDlg2 TITLE STR0007 From 0,0 To 470,550 OF oMainWnd Pixel //"Selecione o Novo Ambiente"

	oPanelDlg2:= tPanel():New(01,01,,oDlg2,,,,,,0,0)
		oPanelDlg2:Align := CONTROL_ALIGN_ALLCLIENT

	@ 7,7  SAY OemToAnsi(STR0008) Of oPanelDlg2 Pixel //"Novo Centro de Custo"
	@ 5,65 MSGET cCustoCod Picture "@!" Size 70,9  Valid (ExistCpo(cAlias2,cCustoCod) .and. NGNOMECC285(cCustoCod,1)) F3 cF3_CTT When .T. Of oPanelDlg2 Pixel HasButton
	@ 5,140 MSGET cCustoNom Picture "@!" Size 110,9 When .F. Of oPanelDlg2 Pixel

	@ 22,7  SAY OemToAnsi(STR0010) Of oPanelDlg2 Pixel //"Nova Funcao"
	@ 21,65  MSGET cFuncaCod Picture "@!" Size 40,9  Valid (ExistCpo("SRJ",cFuncaCod) .and. NGNOMECC285(cFuncaCod,2)) F3 cF3_SRJ When .T. Of oPanelDlg2 Pixel HasButton
	@ 21,140 MSGET cFuncaNom Picture "@!" Size 110,9 When .F. Of oPanelDlg2 Pixel

	If lDepto//Caso utilize departamento.
		@ 37,7  SAY OemToAnsi(STR0070) Of oPanelDlg2 Pixel //"Novo Departamento"
		@ 36,65  MSGET cDepCod Picture "@!" Size 40,9  Valid (ExistCpo("SQB",cDepCod) .and. NGNOMECC285(cDepCod,3)) F3 "SQB" When .T. Of oPanelDlg2 Pixel HasButton
		@ 36,140 MSGET cDepNome Picture "@!" Size 110,9 When .F. Of oPanelDlg2 Pixel
	EndIf

	@ 55,7 TO 195,272 LABEL STR0042 OF oPanelDlg2 Pixel //"Novas Tarefas"
	@ 65,15  SAY OemToAnsi(STR0043) Pixel OF oPanelDlg2 //"Ordenar Por"
	@ 63,52  COMBOBOX oCbx1 VAR cIndExam ITEMS aExaInd Valid MDT285IND(@oMARKT,"TRB285") SIZE 45,60 Pixel OF oPanelDlg2 WHEN .t.

	@ 65,105 Say OemToAnsi(STR0044) Pixel OF oPanelDlg2 //"Localizar Tarefa"
	@ 63,152 MsGet oGetTarr Var cBuscaEx Valid MDT285SRCT(cBuscaEx,@oMARKT) Picture '@!' Size 100,8 Pixel OF oPanelDlg2

	oMARKT := MsSelect():NEW("TRB285","TN5_OK",,aTRBT,@lINVERTE,@cMARCA,{115,15,220,265})
	oMARKT:oBROWSE:lHASMARK := .T.
	oMARKT:oBROWSE:lCANALLMARK := .T.
	oMARKT:bMARK := {|| MDTA285INV("TRB285",aParam,.f.,@oMARKT)}
	oMARKT:oBROWSE:bALLMARK := {|| MDTA285INV("TRB285",aParam,.t.,) }

	NgPopUp(asMenu,@oMenu2)
	oDlg2:bRClicked:= { |o,x,y| oMenu2:Activate(x,y,oDlg2)}
	ACTIVATE MSDIALOG oDlg2 ON INIT EnchoiceBar(oDlg2,{||nOpcc:=1,If(!MDT285CHK(),nOpcc:=0,oDlg2:End())},{||oDlg2:End()}) CENTERED

	If nOpcc == 1
		DbSelectArea("TRB285")
		DbGotop()
		While !Eof()
			If !Empty(TRB285->TN5_OK)
				aAdd(aTarFun,TRB285->TN5_CODTAR)
			Endif
			DbSelectArea("TRB285")
			DbSkip()
		End

		If !MDT285SEEK()
			MsgStop(STR0011+Chr(13)+; //"No h necessidade de realizar nenhum exame por motivo de"
					STR0012,STR0006) //"Mudana de Funo ou Mudana de Centro de Custo."###"ATENCAO"
		Endif
	Endif

	oTemp285:Delete()

Return .t.

/*/

Ŀ
 Funo   MDT285SEEK Autor Denis Hyroshi de Souza  Data  25/07/03 
Ĵ
 DescrioBusca os exames necessario p/ o novo ambiente               
ٱ

/*/
Function MDT285SEEK()
Local aExames := {},i
Local OldRot,nOpca, xx
Local nContador := 0,nCnt := 0,nUsed,nColumn,nOld
Local lGrava := .f.
Local aArea := GetArea()
Local oDLG3, oMenu3, aParam := {}
Local oPanelDlg3, oPanelMark, oPnlTOT, oTempTable
Local lDepto := NGCADICBASE( "TN0_DEPTO" , "A" , "TN0" , .F. )
private oGet ,oDLG1 ,oCbx1 ,oCbx2, cCodCBO
Private aTarefas := aClone(aTarfun)
Private aRiscos  := {}
Private lDelExa := If( SuperGetMv("MV_NGALMDF",.F.,"1") == "2", .t. , .f. )

OldRot := aCLONE(aROTINA)
aROTINA := {}

lInverte:= .f.
cMARCA  := GetMark()
lQuery := .t.

dbSelectArea( "TM4" )

aDBF := {}
AADD(aDBF,{"TM4_OK"    ,"C",02,0})
AADD(aDBF,{"TM4_EXAME" ,"C",If(TAMSX3("TM4_EXAME")[1] < 1, 6, TAMSX3("TM4_EXAME")[1]),0})
AADD(aDBF,{"TM4_NOMEXA","C",30,0})

aTRB1 := {}
AADD(aTRB1,{"TM4_OK"    ,NIL," "                 ,})
AADD(aTRB1,{"TM4_EXAME" ,NIL,STR0023             ,}) //"Exame"
AADD(aTRB1,{"TM4_NOMEXA",NIL,STR0024,}) //"Descricao do Exame"
	
oTempTable := FWTemporaryTable():New( "TRBEX", aDBF )
oTempTable:AddIndex( "1", {"TM4_EXAME"} )
oTempTable:Create()

aRiscos := MDTRETRIS(,,,,,If( Len( aTarefas ) < 1 , Nil , aTarefas ),,,,.T.,{cCustoCod,cFuncaCod,cDepCod})[ 1 ]

aExames := MDTRETEXA(cFuncaCod,"M",aRiscos,cCliMdtps,,.T.,.T.,.T.,TM0->TM0_NUMFIC)//Busca todos os exames necessarios para admissao
For i := 1 to Len(aExames)//Grava no TRB e verifica fornecedor
	Dbselectarea("TRBEX")
	dbGoTop()
	If !dbSeek(aExames[i][1])
		RecLock("TRBEX",.T.)
		TRBEX->TM4_OK     := fGetMarca( cMarca , aExames[i][1] , aExames[i][3] , TM0->TM0_NUMFIC )
		TRBEX->TM4_EXAME  := aExames[i][1]
		TRBEX->TM4_NOMEXA := aExames[i][2]
		MsUnlock("TRBEX")
	Endif
Next i

dbSelectArea("TRBEX")
If Reccount() = 0

	If lDelExa
		MDT285FNEX() //Verifica existencia de exames periodicos
	Endif

	dbSelectArea("TRBEX")
	Use
	aRotina := aCLONE(OldRot)
	RestArea(aArea)
	lRefresh := .t.
	Return .f.
Endif
dbGoTop()
nOpca := 2

aAdd(aParam,{"TM4_OK",cMARCA})

DEFINE MSDIALOG oDlg3 TITLE OemToAnsi(cCadastro) From 0,0 To 470,760 Of oMainWnd Pixel

oPnlTOT := TPanel():New( , , , oDlg3 , , , , , , , , .F. , .F. )
	oPnlTOT:Align := CONTROL_ALIGN_ALLCLIENT

oPanelDlg3:= tPanel():New(01,01,,oPnlTOT,,,,,,,90)
	oPanelDlg3:Align := CONTROL_ALIGN_TOP

oPanelMark:= tPanel():New(100,07,,oPnlTOT,,,,,,367,110)

@ 7,7    SAY OemToAnsi(STR0013) Pixel Of oPanelDlg3 //"Matricula"
@ 5,65   MSGET MDTHideCpo( TM0->TM0_MAT, "TM0_MAT" )  Picture "@!" Size 35,10 When .f. Pixel Of oPanelDlg3
@ 22,7   SAY OemToAnsi(STR0014) Pixel Of oPanelDlg3 //"Nome Funcionario"
@ 20,65  MSGET MDTHideCpo( TM0->TM0_NOMFIC, "TM0_NOMFIC" ) Picture "@!" Size 155,10  When .f. Pixel Of oPanelDlg3
@ 37,7   SAY OemToAnsi(STR0008) Pixel Of oPanelDlg3 //"Novo Centro de Custo"
@ 35,65  MSGET cCustoCod  Picture "@!" Size 100,10 When .f. Pixel Of oPanelDlg3
@ 35,175 MSGET cCustoNom  Picture "@!" Size 200,10 When .f. Pixel Of oPanelDlg3
@ 52,7   SAY OemToAnsi(STR0010) Pixel Of oPanelDlg3 //"Nova Funcao"
@ 50,65  MSGET cFuncaCod  Picture "@!" Size 25,10 When .f. Pixel Of oPanelDlg3
@ 50,175 MSGET cFuncaNom  Picture "@!" Size 100,10 When .f. Pixel Of oPanelDlg3
If lDepto//Caso utilize Departamento.
	@ 67,7   SAY OemToAnsi(STR0070) Pixel Of oPanelDlg3 //"Nova Funcao"
	@ 65,65  MSGET cDepCod  Picture "@!" Size 35,10 When .f. Pixel Of oPanelDlg3
	@ 65,175 MSGET cDepNome  Picture "@!" Size 100,10 When .f. Pixel Of oPanelDlg3
EndIf
@ 78,7  BUTTON STR0048 Of oPanelDlg3 SIZE 40,12 PIXEL ACTION (MDT285CONS(oMARK),oMARK:oBROWSE:REFRESH(.T.))

oMARK := MsSelect():NEW("TRBEX","TM4_OK",,aTRB1,@lINVERTE,@cMARCA,{90,20,190,265},,,oPanelMark)
oMARK:bMARK := {|| MDTA285MAQ(cMarca,lInverte)}
oMARK:oBROWSE:lHASMARK := .T.
oMARK:oBROWSE:lCANALLMARK := .T.
oMARK:oBROWSE:bALLMARK := {|| MDTA285INV("TRBEX",aParam,.t.,) }
oMARK:oBROWSE:ALIGN := CONTROL_ALIGN_ALLCLIENT
NgPopUp(asMenu,@oMenu3)
oPnlTOT:bRClicked := { |o,x,y| oMenu3:Activate(x,y,oPnlTOT)}
ACTIVATE MSDIALOG oDlg3 On Init EnchoiceBar(oDlg3,{|| nOpca := 1,oDlg3:End()},{|| nOpca := 2,oDlg3:End()}) Centered

If nOpca == 1
	If lDelExa
		MDT285FNEX() //Verifica existencia de exames periodicos
	Endif
	NG285PROC()
Endif

oTempTable:Delete()
aRotina := aCLONE(OldRot)
RestArea(aArea)
lRefresh := .t.
Return .t.
/*/

Ŀ
 Funo   MDT285CHK  Autor Denis Hyroshi de Souza  Data  25/07/03 
Ĵ
 Descrio Utilizado para verificar se houve alteracao de ambiente trab
ٱ

/*/
Function MDT285CHK()
/*If cCustoCod == SRA->RA_CC .and. cFuncaCod == SRA->RA_CODFUNC
	Msgstop(STR0019+Chr(13)+; //"O Novo Centro de Custo e a Nova Funo continuam sendo iguais "
			STR0020+Chr(13)+Chr(13)+; //"situao que o funcionrio se encontra atualmente."
			STR0021+Chr(13)+; //"Para realizao desta rotina,  necessrio que pelo menos um desses"
			STR0022,STR0006) //"campos (Novo Centro de Custo / Nova Funo) seja diferente da situao atual."###"ATENCAO"

	Return .f.
Endif*/
Return .t.
/*/

Ŀ
 Funo   MDTA285INV Autor Denis Hyroshi de Souza  Data  25/07/03 
Ĵ
 Descrio Inverte a marcacao do browse                               
           aParam - Parametros para alterar marcacao                  
 			           [1] - Campo definido para marcacao                
           			[2] - Marca definida para alteracao               
           			[3] - Campo definid marca secundaria              
ٱ

/*/
Function MDTA285INV(cAreaTrb,aParam,lAll,oMARKT)
Local aArea := GetArea()

If lAll
	Dbselectarea(cAreaTrb)
	Dbgotop()
	While !eof()
		&(cAreaTrb + "->" + aParam[1][1]) := IF(!Empty(&(aParam[1][1])),Space(2),aParam[1][2])
		If cAreaTrb <> "TRBEX"
			&(cAreaTrb + "->" + aParam[1][3]) := IF(!Empty(&(aParam[1][1])),Space(2),"ZZ")
		Endif
		Dbskip()
	End
Else
	If ValType(aParam) != "U"
		&(cAreaTrb + "->" + aParam[1][3]) := IF(!Empty(&(aParam[1][1])),Space(2),"ZZ")
	Endif
	If ValType(oMARKT) != "U"
		If cIndExam == "3"
			oMARKT:oBROWSE:REFRESH(.T.)
		Endif
	Endif
Endif

RestArea(aArea)
Return .t.
/*/

Ŀ
 Funo   MDTA285MAQ Autor Denis Hyroshi de Souza  Data  25/07/03 
Ĵ
 Descrio Inverte a marcacao do browse                               
ٱ

/*/
Function MDTA285MAQ(cMarca,lInverte)
Return .t.
/*/

Ŀ
 Funo   NG285PROC  Autor Denis Hyroshi de Souza  Data  25/07/03 
Ĵ
 Descrio Grava os exames                                            
Ĵ
 Uso       Generico                                                   
ٱ

/*/
Function NG285PROC()
Local cCodCBO := ""

Dbselectarea("TRBEX")
Dbgotop()
While !eof()
	If Empty(TRBEX->TM4_OK)
		Dbskip()
		Loop
	Endif

	aFornec := {{"     "," "}}
	aFornec := NGUTILFOR(TRBEX->TM4_EXAME)

	Dbselectarea("SRJ")
	Dbsetorder(1)
	Dbseek(xFilial("SRJ")+cFuncaCod)
	cCodCBO := SRJ->RJ_CBO
	If !Empty(SRJ->RJ_CODCBO)
		cCodCBO := SRJ->RJ_CODCBO
	Endif

	Dbselectarea("TM5")
	Dbsetorder(1)
	If !Dbseek(xFilial("TM5")+TM0->TM0_NUMFIC+DTOS(dDataBase)+TRBEX->TM4_EXAME)
		RecLock("TM5",.t.)
		TM5->TM5_FILIAL  := xFilial("TM5")
		TM5->TM5_NUMFIC  := TM0->TM0_NUMFIC
		TM5->TM5_EXAME   := TRBEX->TM4_EXAME
		TM5->TM5_DTPROG  := dDataBase
		TM5->TM5_FORNEC  := aFornec[1][1]
		TM5->TM5_LOJA    := aFornec[1][2]
		TM5->TM5_FILFUN  := SRA->RA_FILIAL
		TM5->TM5_MAT     := SRA->RA_MAT
		TM5->TM5_ORIGEX  := "2"
		TM5->TM5_PCMSO   := cPcmso
		TM5->TM5_INDRES  := "1"
		TM5->TM5_NATEXA  := "3"
		TM5->TM5_CC      := cCustoCod
		TM5->TM5_CODFUN  := cFuncaCod
		TM5->TM5_CBO     := cCodCBO
		TM5->TM5_TNOTRA  := SRA->RA_TNOTRAB
		Msunlock('TM5')
	Endif

	Dbselectarea("TRBEX")
	Dbskip()
End
Return .t.
/*/

Ŀ
 Funo   A285GUIA   Autor Denis Hyroshi de Souza  Data  25/07/03 
Ĵ
 Descrio Imprimi guia de prestacao de servico                       
ٱ

/*/
Function A285GUIA()
Local oDlg,oScr,oCbox,oCLi,oFOr, oMenu
Local nOpcc := 0
Local aExamesPS := {}
Local cFornec  := Space(If((TAMSX3("A2_COD")[1]) < 1,6,(TAMSX3("A2_COD")[1])))
Local cCliente := Space(If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1])))
Local aOpcCbox := {STR0033,STR0034} //"Em Disco"###"Via Spool"
Local cCbox := STR0033 //"Em Disco"
Local odtv, dDtval := CTOD("  /  /  ")

NG285PROC()

Dbselectarea("TRBEX")
Dbgotop()
While !eof()
	If Empty(TRBEX->TM4_OK)
		Dbskip()
		Loop
	Endif
	aAdd(aExamesPS,{TRBEX->TM4_EXAME})
	Dbskip()
End
If Len(aExamesPS) == 0
	Dbselectarea("TRBEX")
	Dbgotop()
	MsgStop(STR0025+; //"No ser possvel imprimir a Guia de Prestao de Servios, pois "
			STR0026,STR0027) //"no existe nenhum exame selecionado."###"AVISO"
	Return
Endif

DEFINE MSDIALOG oDlg TITLE STR0028 From 0,0 To 16,35 OF oMainWnd //"Parmetros do Relatrio"

@ 0,0   SCROLLBOX oScr VERTICAL SIZE 122,139 OF oDlg BORDER
@ 05,5  SAY STR0029 Of oScr Pixel //"Tipo Impresso"
@ 05,45 Combobox oCbox VAR cCbox ITEMS aOpcCbox SIZE 40,40 Pixel OF oScr
@ 25,5  SAY STR0030 Of oScr Pixel //"Fornecedor"
@ 25,45 MsGet oFor VAR cFornec Size 50,08 Picture "@!" F3 "SA2" Of oScr Pixel Valid Existcpo("SA2",cFornec) When .t.
@ 45,5  SAY STR0031 Of oScr Pixel //"Data Validade"
@ 45,45 MsGet oDtv VAR dDtVal Size 50,08 Picture "99/99/9999" Of oScr Pixel When .t.

DEFINE SBUTTON FROM 90, 60 TYPE 1 ENABLE OF oScr ACTION EVAL({|| nOpcc := 1,oDlg:End()})
DEFINE SBUTTON FROM 90, 90 TYPE 2 ENABLE OF oScr ACTION oDlg:End()

NgPopUp(asMenu,@oMenu)
oDlg:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg)}
ACTIVATE MSDIALOG oDlg CENTERED

If nOpcc == 1
	If lPrestSX6
		lDbskip := .t.
		If !Empty(Substr(TM0->TM0_CC,1,nSizeCli))
			cCliente := Substr(TM0->TM0_CC,1,nSizeCli)
			lDbskip := .f.
		Endif
		If !Empty(Substr(SRA->RA_CC,1,nSizeCli)).and. lDbskip
			cCliente := Substr(SRA->RA_CC,1,nSizeCli)
			lDbskip := .f.
		Endif
	Endif
Endif

Dbselectarea("TRBEX")
Dbgotop()
Return .t.


Function NGNOMECC285(cCod,nTable)

Local aArea := GetArea()
Local lDepto := NGCADICBASE( "TN0_DEPTO" , "A" , "TN0" , .F. )

If nTable == 1
	dbSelectArea(cAlias2)
	dbSetOrder(1)
	dbSeek(xFilial(cAlias2)+cCod)
	cCustoNom := Substr(&cDescr,1,25)
ElseIf nTable == 2
	dbSelectArea("SRJ")
	dbSetOrder(1)
	dbSeek(xFilial("SRJ")+cCod)
	cFuncaNom := Substr(SRJ->RJ_DESC,1,25)
Else
	If lDepto .And. nTable == 3
		dbSelectArea("SQB")
		dbSetOrder(1)
		dbSeek(xFilial("SQB")+cCod)
		cDepNome := Substr(SQB->QB_DESCRIC,1,30)
EndIf
EndIf
RestArea(aArea)

Return .t.

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 29/11/2006
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaMDT                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )
Local aRotina

If lSigaMdtps
	aRotina := { { STR0001,   "AxPesqui"  , 0 , 1},; //"Pesquisar"
	             { STR0002,   "NGCAD01"   , 0 , 2},; //"Visualizar"
	             { STR0036,   "MDT285SRA" , 0 , 4} } //"Funcionrios"
Else
	aRotina :=	{{STR0001,"AxPesqui",0,1},; //"Pesquisar"
                 {STR0002,"NGCAD01" ,0,2},; //"Visualizar"
                 {STR0003,"MDT285EX",0,2},; //"Exames"
				 {STR0035, "GpLegend" , 0 , 6,,.F. }} //"Legenda"
Endif
Return aRotina

/*/


Ŀ
Funo     fExaAlInd Autor  Denis                  Data 27/11/07  
Ĵ
Descrio  Troca indice                                               
ٱ


/*/
Static Function MDT285IND(oMARKT,cAliasTMP)

dbSelectArea(cAliasTMP)
If cIndExam == "1"
	dbSetOrder(1)
	oGetTarr:Enable()
ElseIf cIndExam == "2"
	dbSetOrder(2)
	oGetTarr:Enable()
ElseIf cIndExam == "3"
	dbSetOrder(3)
	oGetTarr:Disable()
Endif

TRB285->(dbGoTop())
oMARKT:oBROWSE:REFRESH(.T.)

Return .t.

/*/


Ŀ
Funo    fLocTarefa Autor  Denis                  Data 27/11/07  
Ĵ
Descrio  Localiza tarefa                                            
ٱ


/*/
Static Function MDT285SRCT(cBuscaEx,oMARKT)
Local cOldTa := Alias()
Local aArea1 := TRB285->(GetArea())
Local lAchou := .f.

If Empty(cBuscaEx)
	Return .t.
Endif

dbSelectArea("TRB285")
dbGoTop()
If dbSeek(Alltrim(cBuscaEx),.t.)
	oMARKT:oBROWSE:REFRESH(.T.)
	oMARKT:oBROWSE:SetFocus()
	lAchou := .t.
Else
	RestArea(aArea1)
Endif

If !lAchou
	MsgInfo(STR0045,STR0006) //"No foi possvel localizar esta tarefa."###"Ateno"
	Return .t.
Endif

dbSelectArea(cOldTa)

Return .t.

/*/


Ŀ
Funo    |MDT285FNEX| Autor  Hugo Rizzo Pereira     Data 24/01/11  
Ĵ
Descrio  Cancela exames periodicos pendentes                        
ٱ


/*/
Function MDT285FNEX
Local nOpc := 3, nInd
Local aExamesPer := {}

Dbselectarea("TM0")
Dbsetorder(3)
If DbSeek(SRA->RA_FILIAL + SRA->RA_MAT)

	Dbselectarea("TM5")
	Dbsetorder(1)
	If DbSeek(xFilial("TM5") + TM0->TM0_NUMFIC)

		While !Eof() .And. TM5->TM5_FILIAL == xFilial("TM5") .And. ;
				 TM5->TM5_NUMFIC == TM0->TM0_NUMFIC
			If TM5->TM5_NATEXA == "2"
				If Empty(TM5->TM5_DTRESU)
					aAdd(aExamesPer,{TM5->TM5_DTPROG,TM5->TM5_EXAME})
				Endif
			Endif

			DbSelectArea("TM5")
			DbSkip()
		End
	Endif

Endif

If Len(aExamesPer) > 0
	While nOpc == 3
		nOpc := Aviso(STR0006,STR0046 + CHR(13) + ;
						  STR0047, ;
					  {STR0015,STR0016,STR0002})
		If nOpc == 1
				For nInd := 1 to Len(aExamesPer)
					DbSelectArea("TM5")
					DbSetOrder(1)
					If DbSeek(xFilial("TM5") + TM0->TM0_NUMFIC + DTOS(aExamesPer[nInd][1]) + aExamesPer[nInd][2])
						RecLock("TM5",.F.)
						DbDelete()
						Msunlock('TM5')
					Endif
				Next nInd
		ElseIf nOpc == 3
			MDT285VEXA() //Visualizar exames periodicos pendentes
		Endif
	End
Endif

Return .T.

/*/


Ŀ
Funo    |MDT285VEXA| Autor  Hugo Rizzo Pereira     Data 24/01/11  
Ĵ
Descrio  Browse para visualizacao dos exames periodicos             
ٱ


/*/
Function MDT285VEXA
Local aIndSTP 	 := {}
Local aArea 	 := GetArea()
Private aRotina := {{ "Visualizar" ,"NGCAD01" , 0, 2}}

DbSelectArea("TM0")
DbSetOrder(3)
DbSeek(SRA->RA_FILIAL+SRA->RA_MAT)

DbSelectArea("TM5")
DbSetOrder(8)
DbSeek(xFilial("TM5")+TM0->TM0_NUMFIC)

cCondistp := "TM5->TM5_FILIAL ='" + xFilial("TM5") + "' .And. TM5->TM5_NUMFIC = TM0->TM0_NUMFIC"
cCondistp += " .And. Empty(TM5->TM5_DTRESU) .And. TM5->TM5_NATEXA == '2'"

bFiltraBrw := {|| FilBrowse("TM5",@aIndSTP,@cCondistp)}
Eval(bFiltraBrw)

mBrowse(6, 1,22,75,"TM5",,,,,,)

EndFilBrw( "TM5" , aIndSTP )
RestArea(aArea)

Return .T.

/*/

Ŀ
 Funo   A280ADDEX  Autor Denis Hyroshi de Souza  Data  25/07/03 
Ĵ
 Descrio Inclui exame na lista de exames p/ GUIA                    
ٱ

/*/
Function MDT285ADEX
Local oDlg,oScr, oMenu
Local nOpcb := 0
Local cCodExa := Space( If(TAMSX3("TM4_EXAME")[1] < 1, 6, TAMSX3("TM4_EXAME")[1]) )

DEFINE MSDIALOG oDlg TITLE STR0050 From 0,0 To 8,28 OF oMainWnd //"Inclusao de Exame"

@ 00,00 SCROLLBOX oScr VERTICAL SIZE 122,180 OF oDlg BORDER
@ 10.5,18 SAY STR0023 Of oScr Pixel //"Exame"
@ 10,43 MsGet cCodExa Size 50,08 Picture "@!" F3 "TM4" Of oScr Pixel Valid MDT285VDEX(cCodExa) When .t. HASBUTTON

DEFINE SBUTTON FROM 40, 30 TYPE 1 ENABLE OF oScr ACTION EVAL({|| nOpcb := 1,oDlg:End()})
DEFINE SBUTTON FROM 40, 60 TYPE 2 ENABLE OF oScr ACTION oDlg:End()

NgPopUp(asMenu,@oMenu)
oDlg:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg)}
ACTIVATE MSDIALOG oDlg CENTERED

If nOpcb == 1
	Dbselectarea("TM4")
	Dbsetorder(1)
	If Dbseek(xFilial("TM4") + cCodExa)
		Dbselectarea("TRBEX")
		DbGotop()
		If !Dbseek(cCodExa)
			TRBEX->(DbAppend())
			TRBEX->TM4_OK     := cMarca
			TRBEX->TM4_EXAME  := TM4->TM4_EXAME
			TRBEX->TM4_NOMEXA := TM4->TM4_NOMEXA
		Endif
	Endif
Endif

Dbselectarea("TRBEX")
Dbgotop()
Return .t.

/*/

Ŀ
 Funo   A280ADDEX  Autor Denis Hyroshi de Souza  Data  25/07/03 
Ĵ
 Descrio Valida o codigo do Exame                                   
ٱ

/*/
Function MDT285VDEX(cCodExa)

If !Existcpo("TM4",cCodExa)
	Return .f.
Endif

If lSigaMdtps
	If !MdtTemTOL(/*Cliente*/,/*Loja*/,cCodExa,.T.)
		Return .f.
	Endif
Endif

Dbselectarea("TRBEX")
DbGotop()
If Dbseek(cCodExa)
	MsgStop(STR0049,STR0027) //"Este exame j est incluido na lista de exames."###"AVISO"
	Return .f.
Endif

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MDT285CONS
Chama consulta padro da TM4,e adiciona na TRB

@author Guilherme Freudenburg
@since 27/02/2014
@return
/*/
//---------------------------------------------------------------------
Static Function MDT285CONS(oMARK)

Local lRet

	lRet:=ConPad1( , , , "TM4",,,.f.)//Chama consulta padro

	If lRet
		dbSelectArea( "TRBEX" )
		dbSetOrder( 1 )
		If !dbSeek( TM4->TM4_EXAME )
				RecLock("TRBEX",.T.)
				TRBEX->TM4_OK     := cMarca
				TRBEX->TM4_EXAME  := TM4->TM4_EXAME
				TRBEX->TM4_NOMEXA := TM4->TM4_NOMEXA
				MsUnlock("TRBEX")
		Endif
	Endif
    dbSelectArea("TRBEX")
	oMARK:oBrowse:SetFocus()
Return
//---------------------------------------------------------------------
/*/{Protheus.doc} fGetMarca
Retorna a marcao a ser realizada

@author Guilherme Freudenburg
@since 27/02/2014
@return
/*/
//---------------------------------------------------------------------
Static Function fGetMarca( cMarca , cExame , cFaixa , cFicha )

	Local cRet := cMarca
	Local nIdade := MdtCalcIda( SRA->RA_NASC )
	Local lResul := .F.

	Private aPeriodic := { { 0 , 0 , 0 , 0 } }

	aPeriodic := NGUTILPER( cFaixa , nIdade , TM0->TM0_SEXO )

	dbSelectArea( "TM5" )
	dbSetOrder( 6 ) //TM5_FILIAL+TM5_NUMFIC+TM5_EXAME+DTOS(TM5_DTPROG)
	If dbSeek( xFilial( "TM5" ) + cFicha + cExame )
		While TM5->( !Eof() ) .And. cExame == TM5->TM5_EXAME .And. cFicha == TM5->TM5_NUMFIC
			If !Empty( TM5->TM5_DTRESU )
				lResul := .T.
				dProxExam := NGUTILPRX( TM5->TM5_DTRESU , aPeriodic[ 1 , 1 ] )
				If dProxExam > dDataBase
					cRet := Space( Len( cMarca ) )
					Exit
				EndIf
			EndIf
			TM5->(dbSkip())
		End
	Else
		lResul := .T.
	EndIf

	If !lResul//Caso no tenha nenhum resultado utiliza a data de Admisso
		dProxExam := NGUTILPRX( SRA->RA_ADMISSA , aPeriodic[ 1 , 1 ] )
		If dProxExam > dDataBase
			cRet := Space( Len( cMarca ) )
		EndIf
	EndIf

Return cRet
