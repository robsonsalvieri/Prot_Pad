#INCLUDE "MNTA156.ch"
#INCLUDE "PROTHEUS.CH"

Static lRel12133 := GetRPORelease() >= '12.1.033'

Static cQryFamBem
Static cQryTipMod
Static cQryCount1
Static cQryCount2
Static cQryCount3

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTA156
Gera o cadastro de Manutenção Padrão utilizando uma manutenção Padrão
já cadastrada (TPF,TP5,TPG,TPH,TPM,TP2),
para um intervalo de familia (De, Ate)
@author Inácio Luiz Kolling
@since 11/08/2008
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTA156()

	Local oProc156

	If MNTAmIIn( 19, 95 )

		Private APOS1
		Private ACAMPOS
		Private NN
		Private PP
		Private VL
		Private CCODFAM
		Private CSERVIC
		Private cSEQUEN
		Private cSEQ
		Private LTP2
		Private nSEQUEN
		Private lIndTPF 	:= NgVerify("TPF")
		Private cCadastro   := STR0005
		aPos1           := {15,1,78,315}

		If Pergunte( 'MNT156', .T. )

			oProc156 := MsNewProcess():New ( { |lEnd| MNTA156TP9( @oProc156 ) }, STR0018, , .T. ) // Copiando...
       		oProc156:Activate()
		
		EndIf

	Endif

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTA156TP9
Processa o TP9 e faz copia da manutencao
@author Inacio Luiz Kolling
@since 12/08/2008
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTA156TP9( oProc156 )

	Local aBind     := {}
	Local aCampos   := TPF->( dbStruct() )
	Local cAlsST6   := GetNextAlias()
	Local cAlsTQR   := GetNextAlias()
	Local cAlsTPF   := GetNextAlias()
	Local nTamMod   := FWTamSX3( 'TPF_TIPMOD' )[1]
	Local nQtdeAt   := 0
	Local nQtdeTo   := 0
	Local nQtdTo2   := 0
	Local nQtdAt2   := 0
	Local nQtdReg   := 0
	Local nInd1     := 0
	Local oTmp156

	Private cAlsMan := GetNextAlias()

	oTmp156 := FWTemporaryTable():New( cAlsMan, aCampos )
	oTmp156:AddIndex( '01', { 'TPF_CODFAM', 'TPF_SERVIC', 'TPF_TIPMOD' } )
	oTmp156:Create()

	oProc156:SetRegua1( ( nQtdeTo := fQtdeQuery( 1 ) ) )

	If Trim( MV_PAR07 ) == '*' .Or. Trim( MV_PAR08 ) == '*'

		nQtdTo2 := 1

	EndIf

	oProc156:SetRegua2( ( nQtdTo2 := fQtdeQuery( 2 ) * ( fQtdeQuery( 3 ) + nQtdTo2 ) ) )

	BeginSQL Alias cAlsTPF

		SELECT
			R_E_C_N_O_
		FROM
			%table:TPF% TPF 
		WHERE
			TPF.TPF_FILIAL = %xFilial:TPF%  AND 
			TPF.TPF_CODFAM = %exp:MV_PAR01% AND 
			TPF.TPF_SERVIC = %exp:MV_PAR02% AND 
			TPF.TPF_SEQREL BETWEEN %exp:MV_PAR03% AND %exp:MV_PAR04% AND 
			TPF.%NotDel%

	EndSQL

	While (cAlsTPF)->( !EoF() )
		
		nQtdeAt++
		oProc156:IncRegua1( STR0019 + cValToChar( nQtdeAt ) + STR0020 + cValToChar( nQtdeTo ) ) // Copiando manut. padrão XX de XX

		dbSelectArea( 'TPF' )
		dbGoTo( (cAlsTPF)->R_E_C_N_O_ )

		nQtdAt2 := 0
		cCODFAM := TPF->TPF_CODFAM
		cSERVIC := TPF->TPF_SERVIC
		cTIPMOD := TPF->TPF_TIPMOD
		cSEQUEN := TPF->TPF_SEQREL
		
		oTmp156:Zap()

		RecLock( cAlsMan, .T. )

			For nInd1 := 1 To FCount()

				cField := FieldName( nInd1 )

				nFdPos := (cAlsMan)->( Fieldpos( cField ) )

				cValue := 'TPF->' + FieldName( nInd1 )

				(cAlsMan)->( FieldPut( nFdPos, &cValue. ) )

			Next nInd1

		MsUnLock()

		If Empty( cQryFamBem )

			cQryFamBem := "SELECT "
			cQryFamBem +=	"T6_CODFAMI "
			cQryFamBem += "FROM "
			cQryFamBem +=	RetSQLName( 'ST6' )
			cQryFamBem += "WHERE "
			cQryFamBem +=	"T6_FILIAL  = ? AND "
			cQryFamBem +=	"T6_CODFAMI BETWEEN ? AND ? AND "
			cQryFamBem +=	"D_E_L_E_T_ = ? "

			cQryFamBem := ChangeQuery( cQryFamBem )

		EndIf

		aBind := {}
		aAdd( aBind, FwxFilial( 'ST6' ) )
		aAdd( aBind, MV_PAR05 )
		aAdd( aBind, MV_PAR06 )
		aAdd( aBind, Space( 1 ) )

		dbUseArea( .T., 'TOPCONN', TcGenQry2( , , cQryFamBem, aBind ), cAlsST6, .T., .T. )

		While (cAlsST6)->( !EoF() )

			If Empty( cQryTipMod )

				cQryTipMod := "SELECT "
				cQryTipMod +=	"TQR_TIPMOD "
				cQryTipMod += "FROM "
				cQryTipMod +=	RetSQLName( 'TQR' )
				cQryTipMod += "WHERE "
				cQryTipMod +=	"TQR_FILIAL = ? AND "
				cQryTipMod +=	"TQR_TIPMOD BETWEEN ? AND ? AND "
				cQryTipMod +=	"D_E_L_E_T_ = ? "

				cQryTipMod := ChangeQuery( cQryTipMod )

			EndIf

			aBind := {}
			aAdd( aBind, FwxFilial( 'TQR' ) )
			aAdd( aBind, MV_PAR07 )
			aAdd( aBind, MV_PAR08 )
			aAdd( aBind, Space( 1 ) )

			dbUseArea( .T., 'TOPCONN', TcGenQry2( , , cQryTipMod, aBind ), cAlsTQR, .T., .T. )
			
			While (cAlsTQR)->( !EoF() )

				nQtdAt2++
				oProc156:IncRegua2( STR0021 + cValToChar( nQtdAt2 ) + STR0020 + cValToChar( nQtdTo2 ) ) // Processando XX de XX

				fCadManPad( (cAlsST6)->T6_CODFAMI, (cAlsTQR)->TQR_TIPMOD, @nQtdReg )

				(cAlsTQR)->( dbSkip() )

			End

			If Trim( MV_PAR07 ) == '*' .Or. Trim( MV_PAR08 ) == '*'

				nQtdAt2++
				oProc156:IncRegua2( STR0021 + cValToChar( nQtdAt2 ) + STR0020 + cValToChar( nQtdTo2 ) ) // Processando XX de XX

				fCadManPad( (cAlsST6)->T6_CODFAMI, PadR( '*', nTamMod ), @nQtdReg )

			EndIf

			(cAlsTQR)->( dbCloseArea() )

			(cAlsST6)->( dbSkip() )

		End

		(cAlsST6)->( dbCloseArea() )

		(cAlsTPF)->( dbSkip() )

	End

	(cAlsTPF)->( dbCloseArea() )

	oTmp156:Delete()

	MsgInfo( STR0008 + '  ' + Str( nQtdReg, 10 ), STR0007 )

	FWFreeArray( aBind )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fProcManut
Processa o TP5,TPG,TPH,TPM,TP2 e faz as copias
@author Inacio Luiz Kolling
@since 12/08/2008
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Static Function fProcManut( cNovFam, cNovMod )

	Local nInd1    := 0
	Local oTmpTbl2 
	Local oTmpTbl3 
	Local oTmpTbl4 
	Local oTmpTbl5 
	Local oTmpTbl6 

	Private cTRBA := GetNextAlias() //Alias Tab. 2
	Private cTRBB := GetNextAlias() //Alias Tab. 3
	Private cTRBC := GetNextAlias() //Alias Tab. 4
	Private cTRB1 := GetNextAlias() //Alias Tab. 5
	Private cTRBD := GetNextAlias() //Alias Tab. 6

	/*----------------------+
	| Tarefas da Manutenção |
	+----------------------*/
	dbSelectArea( 'TP5' )
	dbSetOrder( 3 ) // TP5_FILIAL + TP5_CODFAM + TP5_TIPMOD + TP5_SERVIC + TP5_SEQREL + TP5_TAREFA
	If msSeek( FWxFilial( 'TP5' ) + cCODFAM + cTIPMOD + cSERVIC + cSEQUEN )
		
		aCampos  := TP5->( dbStruct() )
		oTmpTbl2 := FWTemporaryTable():New( cTRBA, aCampos )
		oTmpTbl2:AddIndex( '01', { 'TP5_CODFAM', 'TP5_TIPMOD', 'TP5_SERVIC' } )
		oTmpTbl2:Create()

		dbSelectArea( 'TP5' )
		dbSetOrder( 3 ) // TP5_FILIAL + TP5_CODFAM + TP5_TIPMOD + TP5_SERVIC + TP5_SEQREL + TP5_TAREFA
		While TP5->( !EoF() ) .And. TP5->TP5_filial == FWxFilial( 'TP5' ) .And.	TP5->TP5_CODFAM == cCODFAM .And.;
			TP5->TP5_TIPMOD == cTIPMOD .And. TP5->TP5_SERVIC == cSERVIC .And. TP5->TP5_SEQREL == cSEQUEN

			dbSelectArea( cTRBA )
			RecLock( cTRBA, .T. )
				
				For nInd1 := 1 TO FCount()

					nn := FieldName( nInd1 )

					pp := (cTRBA)->(fieldpos(nn))

					vl := TP5->( FieldGet( nInd1 ) )

					(cTRBA)->(FieldPut(pp,vl))

				Next nInd1

			MsUnLock()

			TP5->( dbSkip() )

		End

		dbSelectArea( cTRBA )
		dbGoTop()
		While (cTRBA)->( !EoF() )

			dbSelectArea( 'TP5' )
			dbSetOrder( 3 ) // TP5_FILIAL + TP5_CODFAM + TP5_TIPMOD + TP5_SERVIC + TP5_SEQREL + TP5_TAREFA
			If !msSeek( FWxFilial( 'TP5' ) + cNovFam + cNovMod + cSERVIC + cSEQUEN + (cTRBA)->TP5_TAREFA )
			
				RecLock( 'TP5', .T. )

					For nInd1 := 1 TO FCount()

						nn := FieldName( nInd1 )

						pp := TP5->( fieldpos(nn) )


						If nn == 'TP5_CODFAM'
							
							TP5->TP5_CODFAM := cNovFam

						ElseIf nn == 'TP5_TIPMOD'

							TP5->TP5_TIPMOD := cNovMod
						
						Else

							vl := (cTRBA)->( fieldget( nInd1) )

							TP5->(FieldPut(pp,vl))

						EndIf

					Next nInd1

				TP5->( MsUnLock() )

			EndIf

			(cTRBA)->( dbSkip() )

		End

		oTmpTbl2:Delete()

	EndIf

	/*----------------------+
	| Insumos da Manutenção |
	+----------------------*/
	dbSelectArea( 'TPG' )
	dbSetOrder( 3 ) // TPG_FILIAL + TPG_CODFAM + TPG_TIPMOD + TPG_SERVIC + TPG_SEQREL + TPG_TAREFA + TPG_TIPORE + TPG_CODIGO
	If msSeek( FWxFilial( 'TPG' ) + cCODFAM + cTIPMOD + cSERVIC + cSEQUEN )

		aCampos  := TPG->( dbStruct() )

		oTmpTbl3 := FWTemporaryTable():New( cTRBB, aCampos )
		oTmpTbl3:AddIndex( '01', { 'TPG_CODFAM', 'TPG_TIPMOD', 'TPG_SERVIC' } )
		oTmpTbl3:Create()

		While TPG->( !EoF() ) .And. TPG->TPG_FILIAL == FWxFilial( 'TPG' ) .And.	TPG->TPG_CODFAM == cCODFAM .And.;
			TPG->TPG_TIPMOD == cTIPMOD .And. TPG->TPG_SERVIC == cSERVIC .And. TPG->TPG_SEQREL == cSEQUEN

			RecLock( cTRBB, .T. )
				
				For nInd1 := 1 TO FCount()

					nn := FieldName( nInd1 )

					pp := (cTRBB)->(fieldpos(nn))

					vl := TPG->( fieldget( nInd1 ) )

					(cTRBB)->(FieldPut(pp,vl))

				Next nInd1

			MsUnLock()

			TPG->( dbSkip() )

		End

		dbSelectArea( cTRBB )
		dbGoTop()
		While (cTRBB)->( !EoF() )
			
			dbSelectArea( 'TPG' )
			dbSetOrder( 3 ) // TPG_FILIAL + TPG_CODFAM + TPG_TIPMOD + TPG_SERVIC + TPG_SEQREL + TPG_TAREFA + TPG_TIPORE + TPG_CODIGO
			If !msSeek( FWxFilial( 'TPG' ) + cNovFam + cNovMod + cSERVIC + cSEQUEN + (cTRBB)->TPG_TAREFA +;
				(cTRBB)->TPG_TIPORE + (cTRBB)->TPG_CODIGO )

				RecLock( 'TPG', .T. )

					For nInd1 := 1 To FCount()

						nn := FieldName( nInd1 )
						pp := TPG->(fieldpos(nn))

						If nn == 'TPG_CODFAM'

							TPG->TPG_CODFAM := cNovFam

						ElseIf nn == 'TPG_TIPMOD'

							TPG->TPG_TIPMOD := cNovMod

						Else

							vl := (cTRBB)->( fieldget( nInd1 ) )

							TPG->(FieldPut(pp,vl))

						EndIf

					Next nInd1

				MsUnLock()

			EndIf

			(cTRBB)->( dbSkip() )

		End

		oTmpTbl3:Delete()

	EndIf

	/*---------------------+
	| Etapas da Manutenção |
	+---------------------*/
	dbSelectArea( 'TPH' )
	dbSetOrder( 6 ) // TPH_FILIAL + TPH_CODFAM + TPH_TIPMOD + TPH_SERVIC + TPH_SEQREL + TPH_TAREFA + TPH_ETAPA
	If msSeek( FWxFilial( 'TPH' ) + cCODFAM + cTIPMOD + cSERVIC + cSEQUEN )

		aCampos := TPH->( dbStruct() )
		lTP2    := .F.

		oTmpTbl4:= FWTemporaryTable():New( cTRBC, aCampos )
		oTmpTbl4:AddIndex( '01', { 'TPH_CODFAM', 'TPH_TIPMOD', 'TPH_SERVIC' } )
		oTmpTbl4:Create()

		While TPH->( !EoF() ) .And. TPH->TPH_FILIAL == FWxFilial( 'TPH' ) .And.	TPH->TPH_CODFAM == cCODFAM .And.;
			TPH->TPH_TIPMOD == cTIPMOD .And. TPH->TPH_SERVIC == cSERVIC .And. TPH->TPH_SEQREL == cSEQUEN

			RecLock( cTRBC, .T. )
				
				For nInd1 := 1 TO FCount()

					nn := FieldName( nInd1 )

					pp := (cTRBC)->(fieldpos(nn))

					vl := TPH->( fieldget( nInd1 ) )

					(cTRBC)->(FieldPut(pp,vl))

				Next nInd1

			MsUnLock()

			/*---------------------------------+
			| Opções para etapas da Manutenção |
			+---------------------------------*/
			dbSelectArea( 'TP2' )
			dbSetOrder( 3 ) // TP2_FILIAL + TP2_CODFAM + TP2_TIPMOD + TP2_SERVIC + TP2_SEQREL + TP2_TAREFA + TP2_ETAPA + TP2_OPCAO
			If msSeek( FWxFilial( 'TP2' ) + cCODFAM + cTIPMOD + cSERVIC + cSEQUEN )
				
				If !lTP2

					aCampos := TP2->( dbStruct() )

					oTmpTbl5:= FWTemporaryTable():New( cTRB1, aCampos )
					oTmpTbl5:AddIndex( '01' , { 'TP2_CODFAM', 'TP2_SERVIC', 'TP2_TIPMOD' } )
					oTmpTbl5:Create()

					lTP2 := .T.

				EndIf

				
				While TP2->( !EoF() ) .And. TP2->TP2_FILIAL == FWxFilial( 'TP2' ) .And. TP2->TP2_CODFAM == cCODFAM .And. TP2->TP2_TIPMOD == cTIPMOD .And.;
					TP2->TP2_SERVIC == cSERVIC .And. TP2->TP2_SEQREL == cSEQUEN .And. TP2->TP2_TAREFA == TPH->TPH_TAREFA .And. TP2->TP2_ETAPA == TPH->TPH_ETAPA

					RecLock( cTRB1, .T. )

						For nInd1 := 1 To FCount()

							nn := FieldName( nInd1 )

							pp := (cTRB1)->(fieldpos(nn))

							vl := TP2->( fieldget( nInd1 ) )

							(cTRB1)->(FieldPut(pp,vl))

						Next nInd1

					MsUnLock()

					TP2->( dbSkip() )

				End

			Endif

			TPH->( dbSkip() )

		End

		dbSelectArea( cTRBC )
		dbGoTop()
		While (cTRBC)->( !EoF() )

			dbSelectArea( 'TPH' )
			dbSetOrder( 6 ) // TPH_FILIAL + TPH_CODFAM + TPH_TIPMOD + TPH_SERVIC + TPH_SEQREL + TPH_TAREFA + TPH_ETAPA
			If !msSeek( FWxFilial( 'TPH' ) + cNovFam + cNovMod + cSERVIC + cSEQUEN + (cTRBC)->TPH_TAREFA +;
				(cTRBC)->TPH_ETAPA )
				
				RecLock( 'TPH', .T. )

					For nInd1 := 1 TO FCount()

						nn := FieldName( nInd1 )
						pp := TPH->(fieldpos(nn))

						If nn == 'TPH_CODFAM'

							TPH->TPH_CODFAM := cNovFam

						ElseIf nn == 'TPH_TIPMOD'

							TPH->TPH_TIPMOD := cNovMod

						Else

							vl := (cTRBC)->( fieldget( nInd1 ) )
							TPH->(FieldPut(pp,vl))

						EndIf

					Next nInd1

				TPH->( MsUnLock() )

			EndIf

			(cTRBC)->( dbSkip() )

		End

		oTmpTbl4:Delete()//Deleta Tabela Temporária 4

		If lTP2

			dbSelectArea( cTRB1 )
			dbGoTop()
			While (cTRB1)->( !EoF() )

				dbSelectArea( 'TP2' )
				dbSetOrder( 3 ) // TP2_FILIAL + TP2_CODFAM + TP2_TIPMOD + TP2_SERVIC + TP2_SEQREL + TP2_TAREFA + TP2_ETAPA + TP2_OPCAO
				If !msSeek( FWxFilial( 'TP2' ) + cNovFam + cNovMod + cSERVIC + cSEQUEN + (cTRB1)->TP2_TAREFA +;
					(cTRB1)->TP2_ETAPA + (cTRB1)->TP2_OPCAO )

					RecLock( 'TP2', .T. )

						For nInd1 := 1 To FCount()
						
							nn := FieldName( nInd1 )
							pp := TP2->(fieldpos(nn))

							If nn == 'TP2_CODFAM'

								TP2->TP2_CODFAM := cNovFam

							ElseIf nn == 'TP2_TIPMOD'

								TP2->TP2_TIPMOD := cNovMod

							Else

								vl := (cTRB1)->( fieldget( nInd1 ) )

								TP2->(FieldPut(pp,vl))

							EndIf

						Next nInd1

					TP2->( MsUnLock() )

				EndIf

				(cTRB1)->( dbSkip() )

			End

			oTmpTbl5:Delete()

		EndIf

	EndIf

	/*---------------------------+
	| Dependências da Manutenção |
	+---------------------------*/
	dbSelectArea( 'TPM' )
	dbSetOrder( 2 ) // TPM_FILIAL + TPM_CODFAM + TPM_TIPMOD + TPM_SERVIC + TPM_SEQREL + TPM_TAREFA + TPM_DEPEND
	If msSeek( FWxFilial( 'TPM' ) + cCODFAM + cTIPMOD + cSERVIC + cSEQUEN )
		
		aCampos  := TPM->( dbStruct() )

		oTmpTbl6:= FWTemporaryTable():New( cTRBD, aCampos )
		oTmpTbl6:AddIndex( '01', { 'TPM_CODFAM', 'TPM_SERVIC', 'TPM_TIPMOD' } )
		oTmpTbl6:Create()

		While TPM->( !EoF() ) .And. TPM->TPM_FILIAL == FWxFilial( 'TPM' ) .And.	TPM->TPM_CODFAM == cCODFAM .And. TPM->TPM_TIPMOD == cTIPMOD .And.;
			TPM->TPM_SERVIC == cSERVIC .And. TPM->TPM_SEQREL == cSEQUEN

			RecLock( cTRBD, .T. )

				For nInd1 := 1 TO FCount()

					nn := FieldName( nInd1 )

					pp := (cTRBD)->(fieldpos(nn))

					vl := TPM->( fieldget( nInd1 ) )

					(cTRBD)->(FieldPut(pp,vl))

				Next nInd1

			MsUnLock()

			TPM->( dbSkip() )

		End

		dbSelectArea( cTRBD )
		dbGoTop()
		While (cTRBD)->( !EoF() )

			dbSelectArea( 'TPM' )
			dbSetOrder( 2 ) // TPM_FILIAL + TPM_CODFAM + TPM_TIPMOD + TPM_SERVIC + TPM_SEQREL + TPM_TAREFA + TPM_DEPEND
			If !msSeek( FWxFilial( 'TPM' ) + cNovFam + cNovMod + cSERVIC + cSEQUEN + (cTRBD)->TPM_TAREFA +;
				(cTRBD)->TPM_DEPEND )

				RecLock( 'TPM', .T. )

				For nInd1 := 1 TO FCount()

					nn := FieldName( nInd1 )
					pp := TPM->(fieldpos(nn))

					If nn == 'TPM_CODFAM'

						TPM->TPM_CODFAM := cNovFam

					ElseIf nn == 'TPM_TIPMOD'

						TPM->TPM_TIPMOD := cNovMod

					Else

						vl := (cTRBD)->( fieldget( nInd1 ) )

						TPM->(FieldPut(pp,vl))

					EndIf

				Next nInd1

				TPM->( MsUnLock() )

			EndIf

			(cTRBD)->( dbSkip() )

		End

		oTmpTbl6:Delete()

	EndIf

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTA156DS
Consistencia De sequencia
@author Inacio Luiz Kolling
@since 14/08/2008
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTA156DS()

	Local lRet03 := .T., cSeqP := If(lIndTPF,MV_PAR03,Str(MV_PAR03,3))

	DbSelectArea("TPF" )
	Dbsetorder(1)

	If Empty(mv_par03)
	Else
		If !ExistCpo("TPF",MV_PAR01+MV_PAR02+cSeqP)
			lRet03 := .F.
		Endif
	Endif
Return lRet03
//---------------------------------------------------------------------
/*/{Protheus.doc} MNTA156AS
Consistencia de Ate seguencia
@author Inacio Luiz Kolling
@since 14/08/2008
@version undefined
@type function
/*/
//---------------------------------------------------------------------
Function MNTA156AS()
	Local lRet04 := .T., cSeqP := If(lIndTPF,MV_PAR04,Str(MV_PAR04,3))
	If mv_par04 = "ZZZ"
	Else
		If !NAOVAZIO(MV_PAR04)
			lRet04 := .F.
		Else
			If mv_par04 < mv_par03
				MsgInfo(STR0012,STR0013)
				lRet04 := .F.
			Else
				If !ExistCpo("TPF",MV_PAR01+MV_PAR02+cSeqP)
					lRet04 := .F.
				Endif
			Endif
		Endif
	Endif
Return lRet04

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT156FAN
Validacao ao informar uma familia
@author Taina A. Cardoso
@since 26/08/11
@version undefined

@type function
/*/
//---------------------------------------------------------------------
Function MNT156FAN()

	If !ExistCpo('ST6',MV_PAR01)
		Return .F.
	Else
		dbSelectArea("TPF")
		dbSetOrder(1)
		If !dbSeek(xFilial("TPF")+MV_PAR01)
			MsgInfo(STR0015,STR0016) //"Esta família não possui nenhuma manutenção padrão" ## "ATENCAO"
			Return .F.
		EndIf
	EndIf
Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MNT156MOD
Valida o tipo modelo(Tipo Modelo? - MV_PAR07) informado no pergunte MNT156

@type   Function

@author Eduardo Mussi
@since  30/07/2021

@Param  lVldAte, boolean, Indica se a validação é do parâmetro ATÉ.

@return Lógico , define se o tipo modelo informado é valido
/*/
//-------------------------------------------------------------------
Function MNT156MOD( lVldAte )
	
	Local lRet      := .T.

	Default lVldAte := .F.

	If !lVldAte

		lRet := Empty( MV_PAR07 ) .Or. AllTrim( MV_PAR07 ) == '*' .Or. ExistCPO( 'TQR', MV_PAR07, 1 )

	Else

		lRet := AllTrim( MV_PAR08 ) == '*' .Or. AteCodigo( 'TQR', MV_PAR07, MV_PAR08, FWTamSX3( 'TQR_TIPMOD' )[1] )

	EndIf

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} fQtdeQuery
Retorna quantidade de registros que serão processados.
@type function

@author Alexandre Santos
@since 08/01/2024

@param nOpCount, string, Indica qual query será utilizada.
@return integer , Quantidade de registros processados.
/*/
//---------------------------------------------------------------------
Static Function fQtdeQuery( nOpCount )

    Local aBind     := {}
	Local aArea     := FwGetArea()
    Local cAlsSTJ   := GetNextAlias()
    Local nResult   := 0

	Do Case

		Case nOpCount == 1
			
			If Empty( cQryCount1 )

				cQryCount1 := "SELECT "
				cQryCount1 +=     "COUNT( 1 ) AS QTDE_REG " 
				cQryCount1 += "FROM "
				cQryCount1 +=    RetSQLName( 'TPF' ) + " TPF "
				cQryCount1 += "WHERE "
				cQryCount1 +=    "TPF.TPF_FILIAL = ? AND "
				cQryCount1 +=    "TPF.TPF_CODFAM = ? AND "
				cQryCount1 +=    "TPF.TPF_SERVIC = ? AND "
				cQryCount1 +=    "TPF.TPF_SEQREL BETWEEN ? AND ? AND "
				cQryCount1 +=    "TPF.D_E_L_E_T_ = ' ' "

				cQryCount1 := ChangeQuery( cQryCount1 )

			EndIf

			aAdd( aBind, FWxFilial( 'TPF' ) )
			aAdd( aBind, MV_PAR01 )
			aAdd( aBind, MV_PAR02 )
			aAdd( aBind, MV_PAR03 )
			aAdd( aBind, MV_PAR04 )
			aAdd( aBind, Space( 1 ) )

			dbUseArea( .T., 'TOPCONN', TcGenQry2( , , cQryCount1, aBind ), cAlsSTJ, .T., .T. )

		Case nOpCount == 2

			If Empty( cQryCount2 )

				cQryCount2 := "SELECT "
				cQryCount2 +=		"COUNT( 1 ) AS QTDE_REG " 
				cQryCount2 += "FROM "
				cQryCount2 +=		RetSQLName( 'ST6' )
				cQryCount2 += "WHERE "
				cQryCount2 +=		"T6_FILIAL  = ? AND "
				cQryCount2 +=		"T6_CODFAMI BETWEEN ? AND ? AND "
				cQryCount2 +=		"D_E_L_E_T_ = ? "

				cQryCount2 := ChangeQuery( cQryCount2 )

			EndIf

			aAdd( aBind, FwxFilial( 'ST6' ) )
			aAdd( aBind, MV_PAR05 )
			aAdd( aBind, MV_PAR06 )
			aAdd( aBind, Space( 1 ) )

			dbUseArea( .T., 'TOPCONN', TcGenQry2( , , cQryCount2, aBind ), cAlsSTJ, .T., .T. )

		Case nOpCount == 3

			If Empty( cQryCount3 )

				cQryCount3 := "SELECT "
				cQryCount3 +=		"COUNT( 1 ) AS QTDE_REG " 
				cQryCount3 += "FROM "
				cQryCount3 +=		RetSQLName( 'TQR' )
				cQryCount3 += "WHERE "
				cQryCount3 +=		"TQR_FILIAL = ? AND "
				cQryCount3 +=		"TQR_TIPMOD BETWEEN ? AND ? AND "
				cQryCount3 +=		"D_E_L_E_T_ = ? "

				cQryCount3 := ChangeQuery( cQryCount3 )

			EndIf

			aAdd( aBind, FwxFilial( 'TQR' ) )
			aAdd( aBind, MV_PAR07 )
			aAdd( aBind, MV_PAR08 )
			aAdd( aBind, Space( 1 ) )

			dbUseArea( .T., 'TOPCONN', TcGenQry2( , , cQryCount3, aBind ), cAlsSTJ, .T., .T. )

	End Case

    If (cAlsSTJ)->( !EoF() )

        nResult := (cAlsSTJ)->QTDE_REG
            
    EndIf

    (cAlsSTJ)->( dbCloseArea() )

	FwRestArea( aArea )

    FWFreeArray( aBind )
	FWFreeArray( aArea )
    
Return nResult

//---------------------------------------------------------------------
/*/{Protheus.doc} fCadManPad
Cadastra as novas manutenções padrão.
@type function

@author Alexandre Santos
@since 08/01/2024

@param cFamili, string , Família do bem.
@param cCodMod, string , Tipo modelo.
@param nQtdReg, integer, QUantidade de manutenções inclusas.

@return integer , Quantidade de registros processados.
/*/
//---------------------------------------------------------------------
Static Function fCadManPad( cFamili, cCodMod, nQtdReg )

	Local cField := ''
	Local cValue := ''
	Local nFdPos := 0
	Local nInd1  := 0

    dbSelectArea( 'TPF' )
	dbSetOrder( 4 ) // TPF_FILIAL + TPF_CODFAM + TPF_TIPMOD + TPF_SERVIC + TPF_SEQREL
	If !msSeek( FWxFilial( 'TPF' ) + cFamili + cCodMod + MV_PAR02 + cSEQUEN )

		RecLock( 'TPF', .T. )

			For nInd1 := 1 To FCount()

				cField := FieldName( nInd1 )
				nFdPos := TPF->( fieldpos( cField ) )

				If cField == 'TPF_CODFAM'

					TPF->TPF_CODFAM := cFamili

				ElseIf cField == 'TPF_TIPMOD'

					TPF->TPF_TIPMOD := cCodMod

				Else

					cValue := '(cAlsMan)->' + cField
					
					TPF->( FieldPut( nFdPos, &cValue. ) )

				EndIf

			Next nInd1

		MsUnLock()

		nQtdReg++

		fProcManut( cFamili, cCodMod )

	EndIf
    
Return
