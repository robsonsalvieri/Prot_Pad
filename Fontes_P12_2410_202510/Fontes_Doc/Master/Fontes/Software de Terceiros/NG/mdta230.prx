#INCLUDE "Mdta230.ch"
#Include "Protheus.ch"

#DEFINE _nVERSAO 2 //Versao do fonte
/*/


Ŀ
Funo     MDTA230   Autor  Thiago Machado         Data  23/07/01 
Ĵ
Descrio  Programa de  Registro dos Locais Avaliados no Laudo.       
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function MDTA230

//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 						  
//
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)
Private aVetinr := {}

PRIVATE aRotina := MenuDef()
Private lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )

//Ŀ
// Define o cabecalho da tela de atualizacoes                   
//
PRIVATE cCadastro  := STR0022  //"Locais Avaliados no Laudo"
PRIVATE aCHKDEL := {}, bNGGRAVA
PRIVATE nSizeCli,nSizeLoj
Private cPrograma := "MDTA230"
Private cCliMdtPs

nSizeCli := If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1]))
nSizeLoj := If((TAMSX3("A1_LOJA")[1]) < 1,2,(TAMSX3("A1_LOJA")[1]))

If !( "TO5_CODFUN" $ NGSEEKDIC( "SX2" , "TO5" , 1 , "X2_UNICO" ) )
	 	If !NGINCOMPDIC( "UPDMDT87" , "THVZJN" )
			//Ŀ
			// Devolve variaveis armazenadas (NGRIGHTCLICK) 					  	  
			//
			NGRETURNPRM(aNGBEGINPRM)
			Return .F.
	 	EndIf
EndIf

//Se for prestador de servio
If lSigaMdtPS
	DbSelectArea("SA1")
	DbSetOrder(1)
	mBrowse( 6, 1,22,75,"SA1")
Else
	MDT230CAD()
Endif

//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK) 					  	  
//

NGRETURNPRM(aNGBEGINPRM)

Return .T.

/*/


Ŀ
Funo     MDT230CAD Autor                         Data           
Ĵ
Descrio  Monta um browse dos laudos.                                
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function MDT230CAD()

Local aArea := GetArea()
Local oldROTINA := aCLONE(aROTINA)
LOCAL cFiltraSRJ			//Variavel para filtro
LOCAL aIndexSRJ	:= {}		//Variavel Para Filtro
Local uRet
Local oldCad := cCadastro

PRIVATE aTrocaF3   := {}, aNGFIELD := {}
Private aCHKDEL := {}, bNGGRAVA

If lSigaMdtPS
	aRotina := MenuDef( .F. )
Endif

aPos1 := {  15,  1, 95,315 }

DbSelectArea("TO0")
//Se for prestador de servio faz filtro de laudos por cliente
If lSigaMdtPS
	Set Filter To TO0->TO0_CLIENT+TO0->TO0_LOJA == SA1->A1_COD+SA1->A1_LOJA
	cCliMdtPs := SA1->A1_COD+SA1->A1_LOJA
Endif
DbSetOrder(1)

mBrowse( 6, 1,22,75,"TO0")

aROTINA := aCLONE(oldROTINA)
RestArea(aArea)
cCadastro := oldCad
Return uRet
/*/


Ŀ
Funo    |MDT230ZZIB| Autor  Denis Hyroshi Souza    Data  24/03/05 
Ĵ
Descrio  Inicializa o browse do campo TO5_NOMPOS                    
ٱ


/*/
Function MDT230ZZIB()
Local aArea  := GetArea()
Local cDesc  := " "

If Alltrim(TO5->TO5_CODPOS) == "*"
	cDesc := "TODOS"
Else
	cDesc := Posicione("ZZI",1,xFilial("ZZI")+TO5->TO5_CODPOS,"ZZI_DESC")
Endif

RestArea(aArea)
Return cDesc

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 29/11/2006
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaMDT                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados         
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef( lMdtPs )
Local aRotina

Default lMdtPs := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )

If lMdtPs
	aRotina :=	{ { STR0001, "AxPesqui"  , 0 , 1},;  //"Pesquisar"
                  { STR0002, "NGCAD01"   , 0 , 2},;  //"Visualizar"
                  { STR0019, "MDT230CAD" , 0 , 4} }  //"Laudos"
Else
	aRotina :=  { { STR0001, "AxPesqui"  , 0 , 1},;   //"Pesquisar"
                  { STR0002, "NGCAD01"   , 0 , 2},;   //"Visualizar"
                  { STR0018, "MDT230LO"  , 0 , 4, 3} } //"Ambientes Aval."
Endif

Return aRotina
/*/


Ŀ
Funo    MDT230LO   Autor  Andre Perez Alvarez    Data  03/07/07 
Ĵ
Descrio  Mostra os Locais do Laudo                                  
ٱ


/*/
Function MDT230LO(cAlias,nReg,nOpcx)

LOCAL LVar01:=1,nLinhas:=0,bCampo,cSaveMenuh
LOCAL GetList:={},nSavRec
LOCAL oDlg, i, oPnlTop , oPnlAll, oPnlTOT
LOCAL aOldCOLS
LOCAL nNew, nOld, nDif, xx
Local aSize := MsAdvSize( .T. , .F. , 430 )
//Variaveis de GetDados
Local lAltProg := If(INCLUI .Or. ALTERA, .T.,.F.)
Local aHeader := {} , aCOLS := {}
nSavRec := RecNo()
Private cFilTO5 := xFilial("TO5")
Private cFilSRJ := xFilial("SRJ")
Private oGet, oMenu
Private nCont := 0

// Monta a GetDados
If fMDT230Reg(@aHeader , @aCols , nOpcx)
	aCols := BLANKGETD(aHeader)
Endif

PutFileInEof( "TO5" )

nOpca := 0
DEFINE MSDIALOG oDlg TITLE STR0006 From aSize[ 7 ],0 To aSize[ 6 ],aSize[ 5 ] OF oMainWnd Pixel   //"Laudo x Locais Avaliados"
	//Painel principal
	oPnlTOT := TPanel():New( , , , oDlg , , , , , , , , .F. , .F. )
		oPnlTOT:Align := CONTROL_ALIGN_ALLCLIENT

	oPnlTop := TPanel():New(0, 0, Nil, oPnlTOT, Nil, .T., .F., Nil, Nil, 0, 60, .T., .F. )
		oPnlTop:Align := CONTROL_ALIGN_TOP

		@ 05 , 05  SAY OemtoAnsi(STR0007) OF oPnlTop Pixel  //"Laudo"
		@ 04 , 25  MSGET TO0->TO0_LAUDO SIZE 40,10 WHEN .F. OF oPnlTop Pixel
		@ 05 , 70  SAY OemToAnsi(STR0008) OF oPnlTop Pixel//"Nome Laudo"
		@ 04 , 107 MSGET TO0->TO0_NOME SIZE 150,10 WHEN .F. OF oPnlTop Pixel

		@ 35 , 05 BUTTON "&"+STR0009 SIZE 49,12 PIXEL ACTION ( MDT230BU( oGet ) ) OF oPnlTop Pixel  //"Locais"

	oPnlAll := TPanel():New(0, 0, Nil, oPnlTOT, Nil, .T., .F., Nil, Nil, 0, 60, .T., .F. )
		oPnlAll:Align := CONTROL_ALIGN_ALLCLIENT

		dbSelectArea("TO5")
		PutFileInEof("TO5")
		oGet := MsNewGetDados():New(0,0,( aSize[ 5 ] / 2 ),( aSize[ 6 ] / 2 ) - 60, IIF(!lAltProg,0,GD_INSERT+GD_UPDATE+GD_DELETE),;
	 					{|| NG230LinOk(oGet)},{|| NG230TudOk(oGet)},/*cIniCpos*/,/*aAlterGDa*/,;
	   				/*nFreeze*/, 9999 ,/*cFieldOk */,/*cSuperDel*/,/*cDelOk */,oPnlAll,aHeader,aCols)
		oGet:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

	NGPOPUP(asMenu,@oMenu,oPnlTop)
	oPnlTop:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oPnlTop)}

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpca:=1,if(oGet:TudoOk(),oDlg:End(),nOpca := 0)},{||oDlg:End(),nOpca := 0})

If nOpcA == 1
   Begin Transaction
		lGravaOk := NG230GRAV("TO5",nOpca)
		If !lGravaOk
			MsgStop(STR0010,STR0011) //"No foi possvel completar a operao."  //"Aviso"
		Else
		  //Processa Gatilhos
		   EvalTrigger()
		EndIf
	End Transaction

	If __lSX8
		ConfirmSX8()
	Endif

ElseIf __lSX8
	nNew := Len(oGet:aCols)
	nDif := nNew-nOld
	If nNew > nOld
		For xx := 1 To nDif
			If __lSX8
				RollBackSX8()
			Endif
		Next xx
	Endif
Endif

Return .T.
/*


Ŀ
Funo    NG230LinOk Autor  Andre Perez Alvarez    Data  04/07/07 
Ĵ
Descrio  Critica se a linha digitada esta' Ok                       
Ĵ
Parametros ExpTM = Objeto a ser verificado.                           
Ĵ
 Uso       MDTA230                                                    
ٱ


*/
Function NG230LinOk(o)

//Variaveis auxiliares
Local f
Local aColsOk 	:= {}, aHeadOk := {}
Local nPosCod 	:= 1, nAt := 1, nPosSec := 3
Local nPosQTD1  ,nPosUN1, nPosQTD2, nPosUN2
Local nCols, nHead

//Salva o aCols e aHeader de acordo com a posicao, o nAt da GetDados posicionada e o cdigo de acordo com sua posicao
aColsOk	:= aClone( o:aCols )
aHeadOk	:= aClone( o:aHeader )
nAt		:= n//oGet:nAt
nPosCod	:= aScan( aHeadOk , { | x | AllTrim( Upper( x[ 2 ] ) ) == "TO5_CODAMB" } )
nPosSec	:= aScan( aHeadOk , { | x | AllTrim( Upper( x[ 2 ] ) ) == "TO5_CODFUN" } )

nPosQTD1	:= aSCAN( aHeadOk, { |x| Trim( Upper( x[ 2 ] ) ) == "TO5_QTD1"	}	)
nPosUN1		:= aSCAN( aHeadOk, { |x| Trim( Upper( x[ 2 ] ) ) == "TO5_UNIME1"	}	)
nPosQTD2	:= aSCAN( aHeadOk, { |x| Trim( Upper( x[ 2 ] ) ) == "TO5_QTD2"	}	)
nPosUN2		:= aSCAN( aHeadOk, { |x| Trim( Upper( x[ 2 ] ) ) == "TO5_UNIME2"	}	)


//Percorre aCols
For f:= 1 to Len( aColsOk )
	If !aColsOk[ f , Len( aColsOk[ f ] ) ]
		If f == nAt//Caso seja final ou linha atual
			//Verifica se os campos obrigatrios esto preenchidos
			If Empty( aColsOk[ f , nPosCod ] )
				//Mostra mensagem de Help
				Help( 1 , " " , "OBRIGAT2" , , aHeadOk[ nPosCod , 1 ] , 3 , 0 )
				Return .F.
			Endif
			If Empty( aColsOk[ f , nPosSec ] ) .And. Empty( aColsOk[ f , nPosCod ] )
				//Mostra mensagem de Help
				Help( 1 , " " , "OBRIGAT2" , , aHeadOk[ nPosCod , 1 ] , 3 , 0 )
				Return .F.
			EndIf

			If ( nPosQTD1 > 0 .And. nPosUN1 > 0 ) .And. !Empty( aColsOk[ f , nPosQTD1 ] ) .AND. Empty( aColsOk[ f , nPosUN1 ] )
				msgStop(STR0020,STR0015)  //'Foi informado um valor para o campo "Quantidade 1", no entanto no foi informado a "Unidade de Medida 1".'//"ATENO"
				Return .F.
			Endif
			If ( nPosQTD2 > 0 .And. nPosUN2 > 0 ) .And. !Empty( aColsOk[ f , nPosQTD2 ] ) .AND. Empty( aColsOk[ f , nPosUN2 ] )
				msgStop(STR0021,STR0015)  //'Foi informado um valor para o campo "Quantidade 2", no entanto no foi informado a "Unidade de Medida 2".'//"ATENO"
				Return .F.
			Endif
		Endif
		//Verifica se  somente LinhaOk
		If f <> nAt .and. !aColsOk[ nAt , Len( aColsOk[ nAt ] ) ]
			If aColsOk[ f , nPosCod ] == aColsOk[ nAt , nPosCod ] .And. aColsOk[ f , nPosSec ] == aColsOk[ nAt , nPosSec ]
				//Mostra mensagem de Help
				Help( " " , 1 , "JAEXISTINF" , , aHeadOk[ nPosCod , 1 ] )
				Return .F.
			Endif
		Endif
	Endif
Next f

//Posiciona tabelas em fim de arquivo
PutFileInEof( "TO5" )
PutFileInEof( "TNE" )

Return .t.
/*


Ŀ
Funo    NG230TudOk Autor  Andre Perez Alvarez    Data  04/07/07 
Ĵ
Descrio  Critica se a nota toda esta' Ok                            
Ĵ
Parametros ExpTM = Objeto a ser verificado.                           
Ĵ
 Uso       MDTA230                                                    
ٱ


*/
Function NG230TudOk(o)
Private nColuna := Len(o:aCols[n])
ASORT(aCols,,, { |x, y| x[nColuna] .and. !y[nColuna] } )
Return .t.
/*/


Ŀ
Funo    NG230EXCC  Autor Andre Perez Alvarez     Data  04/07/07 
Ĵ
Descrio  Valida exclusao do registro                                
ٱ


/*/
Function NG230EXCC()
Return .t.
/*/


Ŀ
Funo    NG230GRAV  Autor  Andre Perez Alvarez    Data  04/07/07 
Ĵ
Descrio  Grava os dados na TO5                                      
Ĵ
 Uso       MDTA230                                                    
ٱ


/*/
Function NG230GRAV(cAlias,nOpcao)
Local nx ,ny ,i ,nMaxArray
Local aRegTO5 := {}
Local aBACK := aCLONE(oGet:aCOLS)
Local aColsOk := aClone(oGet:aCols)
Local aHeadOk := aClone(oGet:aHeader)
Local nPosFun
Local lSequen := X3Usado( "TO5_SEQUEN" )
dbSelectArea(cAlias)

//----------------------------------------------------------
// verifica se o ultimo elemento do array esta em branco
//----------------------------------------------------------
aColsOk := {}
aEVAL(aBACK, {|x| If( !Empty(x[1]),  AAdd(aColsOk,x), NIL) })
nMaxArray := Len(aColsOk)

If nMaxArray <= 0
   Return .F.
Endif

cSeekTO5 := cFilTO5+TO0->TO0_LAUDO
nIndTO5  := 1
If lSigaMdtPs
	cSeekTO5 := cFilTO5+SA1->A1_COD+SA1->A1_LOJA+TO0->TO0_LAUDO
	nIndTO5  := 4
Endif

nPosChave	:= aSCAN( aHeadOk, { |x| Trim( Upper( x[ 2 ] ) ) == "TO5_CODAMB"})
nPOSFUN		:= aSCAN( aHeadOk, { |x| Trim( Upper( x[ 2 ] ) ) == "TO5_CODFUN"})

For nx = 1 to nMaxArray

    If aColsOk[nx][Len(aColsOk[nx])]

		If lSigaMdtPs

			dbSelectArea("TO5")
			dbSetOrder(nIndTO5)   //TO5_FILIAL+TO5_LAUDO+TO5_SEQUEN
			If dbSeek( cSeekTO5+aColsOk[nx][nPosChave] )
				RecLock("TO5",.F.,.T.)
				dbDelete()
				MSUNLOCK("TO5")
			EndIf

		Else

			lContLoop := .t.
			dbSelectArea("TO5")
			dbSetOrder(nIndTO5)   //TO5_FILIAL+TO5_LAUDO+TO5_SEQUEN
			dbSeek( cSeekTO5 )
			While !eof() .and. cSeekTO5 == TO5->(TO5_FILIAL+TO5_LAUDO) .and. lContLoop
				If aColsOk[nx][nPosChave] == TO5->TO5_CODAMB .And. aColsOk[nx][nPosFun] == TO5->TO5_CODFUN .And. aScan(aRegTO5, {|x| X == TO5->(Recno()) }) == 0
					RecLock("TO5",.F.,.T.)
					dbDelete()
					MSUNLOCK("TO5")
					lContLoop := .f.
				Endif
				dbSelectArea("TO5")
				dbSkip()
			End

		Endif

		dbSelectArea("TO5")
		Loop

	Endif

    If !empty(aColsOk[nx][nPosChave])

		lIncRegTO5 := .t. //Indica se foi inclusao em modo Nao Prestador
		If lSigaMdtPs
			lIncRegTO5 := .f.
			dbSelectArea("TO5")
			dbSetOrder(nIndTO5)
			If dbSeek( cSeekTO5+aColsOk[nx][nPosChave]+If( lSequen , "" , aColsOk[nx][nPosFun] ) )
				RecLock("TO5",.F.)
			Else
				RecLock("TO5",.T.)
			Endif
		Else
			nRecTO5   := 0
			dbSelectArea("TO5")
			dbSetOrder(nIndTO5)   //TO5_FILIAL+TO5_LAUDO+TO5_SEQUEN
			dbSeek( cSeekTO5 )
			While !eof() .and. cSeekTO5 == TO5->(TO5_FILIAL+TO5_LAUDO) .and. lIncRegTO5
				If aColsOk[nx][nPosChave] == TO5->TO5_CODAMB .And. aColsOk[nx][nPosFun] == TO5->TO5_CODFUN .and. aScan(aRegTO5, {|x| X == TO5->(Recno()) }) == 0
					lIncRegTO5 := .f.
					nRecTO5   := TO5->(Recno())
					aAdd( aRegTO5, TO5->(Recno()) )
				Endif
				dbSelectArea("TO5")
				dbSkip()
			End
			If nRecTO5 > 0
				TO5->(dbGoTo(nRecTO5))
				RecLock("TO5",.F.)
				lIncRegTO5 := .f.
			Else
				RecLock("TO5",.T.)
				lIncRegTO5 := .t.
				aAdd( aRegTO5, TO5->(Recno()) )
			Endif
		Endif

		//----------------------------------------------------------
		// Atualiza dados
		//----------------------------------------------------------
		TO5->TO5_FILIAL := cFilTO5
		TO5->TO5_LAUDO := TO0->TO0_LAUDO
		If lSigaMdtPs
			TO5->TO5_CLIENT := SA1->A1_COD
			TO5->TO5_LOJA   := SA1->A1_LOJA
		Endif
		dbSelectArea("TO5")
		dbSetOrder(1)
		FOR i := 1 TO FCount()
			If "_FILIAL" $ FieldName(i) .OR. "_LAUDO" $ FieldName(i) .OR. "_NOMAMB" $ FieldName(i) .OR.;
				"_DESCFU" $ FieldName(i) .OR. "_CLIENT" $ FieldName(i) .OR. "_LOJA" $ FieldName(i)  .OR.;
				"_SEQUEN" $ FieldName(i) .OR. aSCAN(aHeadOk, {|x| AllTrim(Upper(X[2])) == AllTrim(Upper(FieldName(i))) }) < 1
				Loop
			EndIf
			x   := "M->" + FieldName(i)
			&x. := aColsOk[nx][aSCAN(aHeadOk, {|x| AllTrim(Upper(X[2])) == AllTrim(Upper(FieldName(i))) })]
			y   := "TO5->" + FieldName(i)
			&y := &x
		Next i
		If !lSigaMdtPs .and. lIncRegTO5 .And. lSequen
			TO5->TO5_SEQUEN := GETSXENUM("TO5","TO5_SEQUEN")
		Endif
		MSUNLOCK("TO5")
		dbSetOrder(nIndTO5)
	Endif
Next nx

If !lSigaMdtPs
	Dbselectarea("TO5")
	Dbsetorder(nIndTO5)
	Dbseek( cSeekTO5 )
	While !eof() .and. TO5_FILIAL+TO5_LAUDO == cFilTO5+TO0->TO0_LAUDO
		If aScan(aRegTO5, {|x| X == TO5->(Recno()) }) == 0
			RecLock("TO5",.F.,.T.)
			dbDelete()
			MsunLock("TO5")
		Endif

		Dbskip()
	End
Else
	Dbselectarea("TO5")
	Dbsetorder(nIndTO5)
	Dbseek( cSeekTO5 )
	While !eof() .and. TO5_FILIAL+TO5_LAUDO == cFilTO5+TO0->TO0_LAUDO .and. SA1->A1_COD+SA1->A1_LOJA == TO5->TO5_CLIENT+TO5->TO5_LOJA

		If aSCAN(aColsOk,{|x| AllTrim(x[nPosChave]) == AllTrim(TO5->TO5_CODAMB) .And. AllTrim(x[nPosFun]) == AllTrim(TO5->TO5_CODFUN) .and. !x[Len(x)] }) == 0
			RecLock("TO5",.F.,.T.)
			dbDelete()
			MsunLock("TO5")
		Endif

		Dbskip()
	End
Endif

aCols := aClone( aColsOk )

Return .T.
/*/


Ŀ
Funo    MDT230BU   Autor Andre Perez Alvarez     Data  04/08/07 
Ĵ
Descrio  Mostra um markbrowse com todos os Riscos para o usuario    
           poder seleciona-los de uma soh vez.                        
ٱ


/*/
Static Function MDT230BU( oGet )

Local aArea     := GetArea()
Local aAreaTN0  := TN0->(GetArea())
Local nSizeTNE  := IIf((TAMSX3("TNE_NOME")[1]) < 1, 20, (TAMSX3("TNE_NOME")[1]))
Local nSizeAMB  := TamSX3("TNE_CODAMB")[1]
Local cAlsCPY   := GetNextAlias()
Local cFilTNE   := xFilial("TNE")
Local oDlgF
Local oPnlTop
Local oFont
Local oPnl
Local oPnlTOT
Local oPnlTop2
Local oMARK1
Local oTempTRB
Local aDescIdx	 := {}
Local cPesquisar :=Space( 200 )

Private cMarca   := GetMark()
Private aCbxPesq //ComboBox com indices de pesquisa
Private cCbxPesq := ""
Private oCbxPesq //ComboBox de Pesquisa
Private OldCols  := aCLONE(oGet:aCols)

lInverte:= .f.

lQuery := .t.

dbSelectArea("TO5")

aDBF := {}
AADD(aDBF,{ "TNE_OK"    , "C",02      , 0 })
AADD(aDBF,{ "TNE_CODAMB", "C",nSizeAMB, 0 })
AADD(aDBF,{ "TNE_NOME"  , "C",nSizeTNE, 0 })

aTRB1 := {}
AADD(aTRB1,{ "TNE_OK"    , NIL, " "    ,})
AADD(aTRB1,{ "TNE_CODAMB", NIL, STR0012,})  //"Ambiente"
AADD(aTRB1,{ "TNE_NOME"  , NIL, STR0013,})  //"Nome"

//Cria TRB
oTempTRB := FWTemporaryTable():New( cAlsCPY, aDBF )
oTempTRB:AddIndex( "1", {"TNE_CODAMB"} )
oTempTRB:AddIndex( "2", {"TNE_NOME"} )
oTempTRB:AddIndex( "3", {"TNE_OK"} )
oTempTRB:Create()

dbSelectArea("TNE")

dbSelectArea(cAlsCPY)
Processa({|lEnd| fBuscaReg( cAlsCPY , oGet , cFilTNE )},STR0031,STR0032)

Dbselectarea(cAlsCPY)
Dbgotop()
If (cAlsCPY)->(Reccount()) <= 0
	oTempTRB:Delete()
	RestArea(aAreaTN0)
	RestArea(aArea)
	lRefresh := .t.
	Msgstop(STR0014,STR0015)  //"No existem ambientes cadastrados." //"ATENO"
	Return .t.
Endif

nOpt := 2
DEFINE FONT oFont NAME "Arial" SIZE 0,-12

DEFINE MSDIALOG oDlgF TITLE OemToAnsi(STR0009) From 08,15 To 490,560 OF oMainWnd Pixel  //"Locais"

		oPnlTOT := TPanel():New( , , , oDlgF , , , , , , , , .F. , .F. )
			oPnlTOT:Align := CONTROL_ALIGN_ALLCLIENT

		//Painel do Texto superior
		oPnl 		:= TPanel():New( 01 , 01 , , oPnlTOT , , , , CLR_BLACK , CLR_WHITE , 0 , 55 , .T. , .F. )
			oPnl:Align	:= CONTROL_ALIGN_TOP

		@ 10,08 TO 45 , 265 OF oPnl Pixel
		@ 18,15 SAY STR0016 SIZE 200,10 of oPnl PIXEL FONT oFont //"Estes so os locais cadastrados no sistema."
		@ 28,15 SAY STR0017 SIZE 200,10 of oPnl PIXEL FONT oFont //"Selecione aqueles que foram avaliados no laudo."

	//--- PESQUISAR
	//Define as opcoes de Pesquisa
	aCbxPesq := aClone( aDescIdx )
	aAdd( aCbxPesq , STR0025 ) //"Cdigo+Descrio"
	aAdd( aCbxPesq , STR0026 ) //"Descrio+Cdigo"
	aAdd( aCbxPesq , STR0027 ) //"Marcados"
	cCbxPesq := aCbxPesq[ 1 ]

	//Painel dos botes de pesquisa
	oPnlTop := TPanel():New(0, 0, Nil, oPnlTOT, Nil, .T., .F., Nil, Nil, 0, 50, .T., .F. )
		oPnlTop:Align := CONTROL_ALIGN_TOP

	oCbxPesq := TComboBox():New( 010 , 005 , { | u | If( PCount() > 0 , cCbxPesq := u , cCbxPesq ) } , ;
									aCbxPesq , 200 , 08 , oPnlTop , , { | | } ;
									, , , , .T. , , , , , , , , , "cCbxPesq" )
									oCbxPesq:bChange := { | | fIndexSet( cAlsCPY , aCbxPesq , @cPesquisar , oMARK1 ) }

	oPesquisar := TGet():New( 025 , 005 , { | u | If( PCount() > 0 , cPesquisar := u , cPesquisar ) } , oPnlTop , 200 , 008 , "" , { | | .T. } , CLR_BLACK , CLR_WHITE , ,;
								.F. , , .T. /*lPixel*/ , , .F. , { | | cCbxPesq <> aCbxPesq[ Len( aCbxPesq ) ] }/*bWhen*/ , .F. , .F. , , .F. /*lReadOnly*/ , .F. , "" , "cPesquisar" , , , , .F. /*lHasButton*/ )

	oBtnPesq := TButton():New( 010 , 210 , STR0001 , oPnlTop , { | | fTRBPes( cAlsCPY , oMARK1 , cPesquisar) } , ;//"Pesquisar" //"Pesquisar"
								60 , 10 , , , .F. , .T. , .F. , , .F. , , , .F. )

	//Painel do Mark
	oPnlTop2 := TPanel():New(0, 0, Nil, oPnlTOT, Nil, .T., .F., Nil, Nil, 0, 105, .T., .F. )
		oPnlTop2:Align := CONTROL_ALIGN_TOP

	oMARK1 := MsSelect():NEW(cAlsCPY,"TNE_OK",,aTRB1,@lINVERTE,@cMARCA,{100,5,254,281},,,oPnlTop2)
	oMARK1:oBROWSE:ALIGN			:= CONTROL_ALIGN_ALLCLIENT
	oMARK1:bMARK := {|| MDT230MK(cMarca,lInverte)}
	oMARK1:oBROWSE:lHASMARK := .T.
	oMARK1:oBROWSE:lCANALLMARK := .T.
	oMARK1:oBROWSE:bALLMARK := {|| MDTA230INV(cMarca , cAlsCPY ) }

ACTIVATE MSDIALOG oDlgF ON INIT EnchoiceBar(oDlgF,{|| nOpt := 1,oDlgf:End()},{|| nOpt := 2,oDlgf:End()}) CENTERED

If nOpt <> 2
	Processa( { | lEnd | MDT230CPY( cAlsCPY , oGet ) } )
EndIf

oTempTRB:Delete()

RestArea(aAreaTN0)
RestArea(aArea)
lRefresh := .t.
Return
/*/

Ŀ
 Funo   MDT230MK   Autor Andre Perez Alvarez     Data  19/07/07 
Ĵ
 Descrio                                                            
ٱ

/*/
Function MDT230MK(cMarca,lInverte)
Return
/*/

Ŀ
 Funo   MDTA230INV Autor Andre Perez Alvarez     Data  19/07/07 
Ĵ
 Descrio Inverte a marcacao do browse                               
ٱ

/*/
Function MDTA230INV(cMarca , cAlsCPY )
Local aArea := GetArea()

Dbselectarea(cAlsCPY)
Dbgotop()
While !eof()
	(cAlsCPY)->TNE_OK := IF( (cAlsCPY)->TNE_OK == Space(2) , cMARCA , Space(2) )
	Dbskip()
End

RestArea(aArea)
Return .t.
/*/

Ŀ
 Funo   MDT230CPY Autor Andre Perez Alvarez     Data  20/07/07 
Ĵ
 Descrio Copia os EPI'selecionados no markbrowse para o a GetDados  
ٱ

/*/
//---------------------------------------------------------------------
/*/{Protheus.doc} MDT230CPY
Copia os EPI'selecionados no markbrowse para o a GetDados

@author Andre Perez Alvarez
@since 20/07/07
@edit Guilherme Benkendorf - 19/11/13
@version 11
@return .T./.F.
/*/
//---------------------------------------------------------------------
Static Function MDT230CPY( cAlsCPY , oGet )
Local nCols, nPosCod
Local aColsOk := {}
Local aHeadOk := {}
Local aColsTp := {}

	aColsOk := aClone(oGet:aCols)
	aHeadOk := aClone(oGet:aHeader)
	aColsTp := BLANKGETD(aHeadOk)

	nPosCod	:= aScan( aHeadOk , { | x | AllTrim( Upper( x[ 2 ] ) ) == "TO5_CODAMB" } )
	nPosDes	:= aScan( aHeadOk , { | x | AllTrim( Upper( x[ 2 ] ) ) == "TO5_NOMAMB" } )

	For nCols := Len(aColsOk) To 1 Step -1 //Deleta do aColsOk os registros - no marcados; no estiver encontrado
		dbSelectArea(cAlsCPY)
		dbSetOrder(1)
		If !dbSeek(aColsOK[nCols,nPosCod]) .OR. Empty((cAlsCPY)->TNE_OK)
			aDel(aColsOk,nCols)
			aSize(aColsOk,Len(aColsOk)-1)
		EndIf
	Next nCols

	dbSelectArea(cAlsCPY)
	dbGoTop()
	While (cAlsCPY)->(!Eof())
		If !Empty((cAlsCPY)->TNE_OK) .AND. aScan( aColsOk , {|x| x[nPosCod] == (cAlsCPY)->TNE_CODAMB } ) == 0
			aAdd(aColsOk,aClone(aColsTp[1]))
			aColsOk[Len(aColsOk),nPosCod] := (cAlsCPY)->TNE_CODAMB
			aColsOk[Len(aColsOk),nPosDes] := (cAlsCPY)->TNE_NOME
		EndIf
		(cAlsCPY)->(dbSkip())
	End

	If Len(aColsOK) <= 0
		aColsOK := aClone(aColsTp)
	EndIf

	aSort(aColsOK,,,{ | x , y | If(Empty(x[2]), x[2] > y[2], x[2] < y[2] ) } ) //Ordena por plano
	oGet:aCols := aClone(aColsOK)
	oGet:oBrowse:Refresh()

Return .T.
/*/

Ŀ
 Funo   MDT230DES  Autor Andre Perez Alvarez     Data  05/08/07 
Ĵ
 Descrio Mostra a descricao da funcao no momento da montagem da     
           da GetDados                                                
ٱ

/*/
Function MDT230DES()

Local cDesc	:= ""

If Alltrim( TO5->TO5_CODFUN ) == "*"
	cDesc := "TODAS"
Else
	cDesc := Posicione( "SRJ" , 1 , xFilial( "SRJ" ) + TO5->TO5_CODFUN , "RJ_DESC" )
Endif

Return cDesc

/*/


Ŀ
Funo    MDT230AMB  Autor  Denis Hyroshi          Data  10/08/06 
Ĵ
Descrio  Valida para buscar locais somente do cliente em questao    
ٱ


/*/
Function MDT230AMB()
Local aAreaXXX := GetArea()
Local aAreaTNE := TNE->(GetArea())
Local lRet     := .t.

If Type("cCliMdtPs") == "C"
	If !Empty(cCliMdtPs)
		Dbselectarea("TNE")
		Dbsetorder(3)
		IF !Dbseek(xFilial("TNE")+cCliMdtPs+M->TO5_CODAMB)
			MsgStop(STR0023,STR0024)  //"Este ambiente no pertence ao cliente."  //"ATENO"
			lRet := .f.
		Endif
	Endif
Endif

RestArea(aAreaTNE)
RestArea(aAreaXXX)
Return lRet
//---------------------------------------------------------------------
/*/{Protheus.doc} fMDT230Reg
Funcao para retornar todos os Locais relacionados ao Laudo posicionado

@author Guilherme Benkendorf
@since 18/11/13
@version 11
@return .T./.F.
/*/
//---------------------------------------------------------------------
Static Function fMDT230Reg( aHeader , aCols , nOpcx )
	Local nInd
	Local cKeyGet
	Local cWhileGet
	Local aNoFields := {}

	aAdd(aNoFields,"TO5_FILIAL")
	aAdd(aNoFields,"TO5_LAUDO")
	aAdd(aNoFields,"TO5_SEQUEN")

	If lSigaMdtPs
		aAdd(aNoFields,"TO5_CLIENT")
		aAdd(aNoFields,"TO5_LOJA")
		nInd		:= 4
		cKeyGet 	:= "SA1->A1_COD+SA1->A1_LOJA+TO0->TO0_LAUDO"
		cWhileGet 	:= "TO5->TO5_FILIAL == '" + cFilTO5 + "' .And. TO5->TO5_LAUDO == TO0->TO0_LAUDO"
		cWhileGet	+= " .And. "
		cWhileGet	+= "TO5->TO5_CLIENT == '" + SA1->A1_COD + "' .And. TO5->TO5_LOJA == '" + SA1->A1_LOJA + "'"
	Else
		nInd			:= 1
		cKeyGet		:= "TO0->TO0_LAUDO"
		cWhileGet 	:= "TO5->TO5_FILIAL == '" + cFilTO5 + "' .And. TO5->TO5_LAUDO == '" + TO0->TO0_LAUDO + "'"
	EndIf

	//Monta aCols e aHeader
	dbSelectArea("TO5")
	dbSetOrder(nInd)
	FillGetDados( nOpcx, "TO5", 1, cKeyGet, {|| }, {|| .T.},aNoFields,,,,;
						{|| NGMontaAcols("TO5",&cKeyGet,cWhileGet)},,aHeader, aCols)

Return Empty(aCols)
//---------------------------------------------------------------------
/*/{Protheus.doc} fTRBPes
Funcao de Pesquisar no Browse.

@samples fTRBPes()

@return Sempre verdadeiro

@param cAliasTRB1	- Alias do MarkBrowse ( Obrigatrio )
@param oMark 		- Objeto do MarkBrowse ( Obrigatrio )

@author Guilherme Freudenburg
@since 05/03/2014
/*/
//---------------------------------------------------------------------
Static Function fTRBPes(cAliasTRB , oMark , cPesquisar )

	Local nRecNoAtu := 1//Variavel para salvar o recno
	Local lRet		:= .T.

	//Posiciona no TRB e salva o recno
	dbSelectArea( cAliasTRB )
	nRecNoAtu := RecNo()

	dbSelectArea( cAliasTRB )
	If dbSeek( AllTrim( cPesquisar ) )
		//Caso exista a pesquisa, posiciona
		oMark:oBrowse:SetFocus()
	Else
		//Caso nao exista, retorna ao primeiro recno e exibe mensagem
		dbGoTo( nRecNoAtu )
		ApMsgInfo( STR0028 , STR0024) //"Valor no encontrado."###"Ateno"
		oPesquisar:SetFocus()
		lRet := .F.
	EndIf

	// Atualiza markbrowse
	oMark:oBrowse:Refresh(.T.)

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} fIndexSet
Seta o indice para pesquisa.

@return

@param cAliasTRB	- Alias do TRB ( Obrigatrio )
@param aCbxPesq	- Indices de pesquisa do markbrowse. ( Obrigatrio )
@param cPesquisar	- Valor da Pesquisa ( Obrigatrio )
@param oMark		- Objeto do MarkBrowse ( Obrigatrio )

@author Guilherme Freudenburg
@since 05/03/2014
/*/
//---------------------------------------------------------------------
Static Function fIndexSet( cAliasTRB , aCbxPesq , cPesquisar , oMark )

	Local nIndice := fIndComb( aCbxPesq ) // Retorna numero do indice selecionado

	// Efetua ordenacao do alias do markbrowse, conforme indice selecionado
	dbSelectArea( cAliasTRB )
	dbSetOrder( nIndice )
	dbGoTop()

	// Se o indice selecionado for o ultimo [Marcados]
	If nIndice == Len( aCbxPesq )
		cPesquisar := Space( Len( cPesquisar ) ) // Limpa campo de pesquisa
		oPesquisar:Disable()              // Desabilita campo de pesquisa
		oBtnPesq:Disable()              // Desabilita botao de pesquisa
		oMark:oBrowse:SetFocus()     // Define foco no markbrowse
	Else
		oPesquisar:Enable()               // Habilita campo de pesquisa
		oBtnPesq:Enable()               // Habilita botao de pesquisa
		oBtnPesq:SetFocus()             // Define foco no campo de pesquisa
	Endif

	oMark:oBrowse:Refresh()

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} fIndComb
Retorna o indice, em numero, do item selecionado no combobox

@return nIndice - Retorna o valor do Indice

@param aIndMrk - Indices de pesquisa do markbrowse. ( Obrigatrio )

@author Guilherme Freudenburg
@since 05/03/2014
/*/
//---------------------------------------------------------------------
Static Function fIndComb( aIndMrk )

	Local nIndice := aScan( aIndMrk , { | x | AllTrim( x ) == AllTrim( cCbxPesq ) } )

	// Se o indice nao foi encontrado nos indices pre-definidos, apresenta mensagem
	If nIndice == 0
		ShowHelpDlg( STR0024 ,	{ STR0029 } , 1 , ; //"Ateno"###"ndice no encontrado."
									{ STR0030 } , 1 ) //"Contate o administrador do sistema."
		nIndice := 1
	Endif

Return nIndice
//---------------------------------------------------------------------
/*/{Protheus.doc} fBuscaReg
Funcao para retornar todos os Ambientes

@author
@since
@return
/*/
//---------------------------------------------------------------------
Static Function fBuscaReg( cAliasTRB , oGet , cFilTNE )

	Local nPosCod := 1
	Local nIndex  := 1
	Local cSeek   := cFilTNE
	Local cWhile  := " TNE->TNE_FILIAL == '" + cFilTNE + "'"
	Local aArea   := GetArea()
	Local aColsOK := aClone(oGet:aCols)
	Local aHeadOk := aClone(oGet:aHeader)

	If lSigaMdtPs
		nIndex := 3
		cSeek  += cCliMdtPs
		cWhile += " TNE->TNE_CODCLI == SA1->A1_COD .And. TNE->TNE_LOJACL == SA1->A1_LOJA"
	EndIf
	nPosCod	:= aScan( aHeadOk , { | x | AllTrim( Upper( x[ 2 ] ) ) == "TO5_CODAMB" } )

	dbSelectArea( "TNE" )
	dbSetOrder( nIndex )
	If dbSeek( cSeek )
		While TNE->(!Eof()) .AND. &( cWhile )
			RecLock( cAliasTRB , .T. )
			(cAliasTRB)->TNE_OK     := If( aScan( aColsOk , {|x| x[nPosCod] == TNE->TNE_CODAMB } ) > 0, cMarca , " " )
			(cAliasTRB)->TNE_CODAMB := TNE->TNE_CODAMB
			(cAliasTRB)->TNE_NOME   := TNE->TNE_NOME
			(cAliasTRB)->(MsUnLock())
			TNE->(dbSkip())
		End
	EndIf

	RestArea(aArea)

Return