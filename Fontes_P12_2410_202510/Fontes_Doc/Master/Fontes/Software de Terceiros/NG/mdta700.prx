#INCLUDE "Mdta700.ch"
#Include "Protheus.ch"
#DEFINE _nVERSAO 1 //Versao do fonte
/*/


Ŀ
Funo     MDTA700   Autor Denis Hyroshi de Souza  Data  18/08/03 
Ĵ
Descrio  Programa de Cadastro da SIPAT (Semana Interna de Prevencao 
            ao Acidente do Trabalho)                                   
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function MDTA700
//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 						  
//
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

Private lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )
Private cCadastro := ""
Private aRotina := MenuDef()

If lSigaMdtps
	cCadastro := OemtoAnsi(STR0014)  //"Clientes"

	nSizeSA1 := If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1]))
	nSizeLoj := If((TAMSX3("A1_LOJA")[1]) < 1,2,(TAMSX3("A1_LOJA")[1]))	
	
	DbSelectArea("SA1")
	DbSetOrder(1)
	
	mBrowse( 6, 1,22,75,"SA1")

Else

	Private cPrograma := "MDTA700"
	cCadastro := OemtoAnsi(STR0001) //"Cadastro da SIPAT"
	Private aChkDel := {}, bNgGrava := {}
	Private aTROCAF3 := {}
	
	aCHKDEL := { {'TLH->TLH_SIPAT' , "TLI", 1}}
	
	dbSelectArea("TLH")
	dbSetOrder(1)
	mBrowse(6,1,22,75,"TLH")

Endif

//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK) 					  	  
//
NGRETURNPRM(aNGBEGINPRM)

Return
/*/


Ŀ
Funo    MDT700PROG Autor Denis Hyroshi de Souza  Data  11/07/2007
Ĵ
Descrio  Cadastro da Programacao da CIPA                            
ٱ


/*/
Function MDT700PROG(cAlias,nRecno,cOpcaoX)
Local aAreaXXX := GetArea()
Local aAreaTLH := TLH->(GetArea())

If lSigaMdtps
	
	aCHOICE := {}	
	aVARNAO := {}
	aGETNAO := { { "TLI_SIPAT" , "M->TLH_SIPAT"},;
	             { "TLI_NSIPAT", " "           },; 
	             { "TLI_CLIENT", "SA1->A1_COD" },;
	             { "TLI_LOJA"  , "SA1->A1_LOJA"} }
	cGETWHILE := "TLI_FILIAL == xFilial('TLI') .and. TLI_SIPAT == M->TLH_SIPAT"
	cGETMAKE  := "M->TLH_SIPAT"
	cGETKEY   := "M->TLH_SIPAT + DTOS(M->TLI_DTPROG) + M->TLI_HRPROG"
Else
	aCHOICE := {}
	aVARNAO := {}
	aGETNAO := { { "TLI_SIPAT" , "M->TLH_SIPAT"},;
	             { "TLI_NSIPAT" , " "          } }
	cGETWHILE := "TLI_FILIAL == xFilial('TLI') .and. TLI_SIPAT == M->TLH_SIPAT"
	cGETMAKE  := "M->TLH_SIPAT"
	cGETKEY   := "M->TLH_SIPAT + DTOS(M->TLI_DTPROG) + M->TLI_HRPROG"
Endif

cGETALIAS := "TLI"
cTUDOOK   := "MDT700TuOk()"
cLINOK    := "MDT700LiOk()"

NGCAD02(cAlias,nRecno,4)

bNgGrava := {}
RestArea(aAreaTLH)
RestArea(aAreaXXX)
Return
/*


Ŀ
Funo    MDT700LiOk Autor Denis Hyroshi de Souza  Data  11/07/07 
Ĵ
Descrio  Consistencia da linha da getdados                          
ٱ


/*/
Function MDT700LiOk()

Local nX
Local xx := 0
Local nPos1 := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TLI_DTPROG" })
Local nPos2 := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TLI_HRPROG" })
Local nPos3 := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TLI_CODRES" })

If aCols[n,Len(aCols[n])]
	Return .t.
Endif

//Valida Data Evento
If !ValDataTLI(aCols[n,nPos1])
	Return .f.
Endif

//Valida Responsavel
If !ValResp700(aCols[n,nPos3])
	Return .f.
Endif

For nX := 1 to Len(aCOLS)
	If nx <> n .and. !aCols[nX,Len(aCols[nX])]
		If aCols[nX][nPos1] == aCols[n][nPos1] .and. aCols[nX][nPos2] == aCols[n][nPos2]
			xx++
		Endif
	EndIf    
Next

If xx > 0
	Help(" ",1,"JAEXISTINF")
	Return .f.
Endif

Return .t.

/*/


Ŀ
Funo    MDT700TuOk Autor Denis Hyroshi de Souza  Data 11/07/2007
Ĵ
Descrio  Critica se todas linhas estao OK                           
Ĵ
 Uso                                                                  
ٱ


/*/
Function MDT700TuOk()
Private nColuna := Len(aCols[n])
ASORT(aCols,,, { |x, y| x[nColuna] .and. !y[nColuna] } )
Return .t.

/*


Ŀ
Funo    SubF3TLI   Autor Denis Hyroshi de Souza  Data  11/07/07 
Ĵ
Descrio  Consistencia para o campo responsavel                      
ٱ


/*/
Function SubF3TLI()
Local nPosInd := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TLI_INDRES" })

aTROCAF3 := {}

If nPosInd > 0
	M->TLI_INDRES := aCols[n,nPosInd]
Endif

If M->TLI_INDRES == "2"
	If lSigaMdtps
		AADD(aTROCAF3,{"TLI_CODRES","MDTPS1"})
	Else
		AADD(aTROCAF3,{"TLI_CODRES","SRA"})	
	Endif
Else
	AADD(aTROCAF3,{"TLI_CODRES","TMK"})
EndIf

Return WhenCodRes()
/*/


Ŀ
Funo    ValResp700 Autor Denis Hyroshi de Souza  Data  11/07/07 
Ĵ
Descrio Consiste o codigo do responsavel                            
ٱ


/*/
Function ValResp700(cTLI_CODRES)
Local aAreaXXX := GetArea()

nPos1 := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TLI_INDRES" })
nPos2 := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TLI_NOMRES" })
nPos3 := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TLI_CODRES" })

If nPos2 > 0
	M->TLI_NOMRES := aCOLS[n][nPos2]
Endif
If nPos1 > 0
	M->TLI_INDRES := aCols[n,nPos1]
Endif

If M->TLI_INDRES == "1"
	M->TLI_NOMRES  := Space(40)
	cCODRES := Substr(cTLI_CODRES,1,Len(TMK->TMK_CODUSU))
	dbSelectArea("TMK")
	dbSetOrder(1)
	If !dbSeek(xFilial("TMK")+cCODRES)
		MsgStop(STR0002,STR0003) //"Cdigo de Responsvel inexistente."###"Ateno"
		RestArea(aAreaXXX)
		Return .f.
	EndIf
	M->TLI_NOMRES := TMK->TMK_NOMUSU
ElseIf M->TLI_INDRES == "2"
	M->TLI_NOMRES  := Space(40)
	cCODRES := Substr(cTLI_CODRES,1,Len(SRA->RA_MAT))
	dbSelectArea("SRA")
	dbSetOrder(1)
	If !dbSeek(xFilial("SRA")+cCODRES)
		MsgStop(STR0002,STR0003) //"Cdigo de Responsvel inexistente."###"Ateno"
		RestArea(aAreaXXX)
		Return .f.
	EndIf
	If lSigaMdtps
		If SubStr(SRA->RA_CC,1,nSizeSA1+nSizeLoj) <> cCliMdtps
			MsgStop(STR0015,STR0003) //"O cdigo de responsvel no pertence ao cliente."###"Ateno"
			RestArea(aAreaXXX)
			Return .f.		
		Endif
	Endif
	M->TLI_NOMRES := SRA->RA_NOME
ElseIf M->TLI_INDRES == "3"
	aCols[n,nPos3] := Space(Len(aCols[n,nPos3]))
EndIf

If nPos2 > 0
   aCOLS[n][nPos2] := M->TLI_NOMRES
EndIf

RestArea(aAreaXXX)
Return .t.
/*/


Ŀ
Funo    A700NECBRW Autor Denis Hyroshi de Souza  Data  18/08/03 
Ĵ
Descrio Retorna o nome da necessidade legal                         
ٱ


/*/
Function GetNomeTLI(lRelX3)
Local cRet := Space(40)
Local aArea := GetArea()
Local cTab := Space(3)
Local cCpo := Space(10)
Local nTam := 12

If lRelX3
	Return cRet
Endif

If TLI->TLI_INDRES == "1"
	cTab := "TMK"
	cCpo := "TMK->TMK_NOMUSU"
	nTam := 12
ElseIf TLI->TLI_INDRES == "2"
	cTab := "SRA"
	cCpo := "SRA->RA_NOME"
	nTam := 6
Else
	If !Empty(TLI->TLI_NOMRES)
		cRet := TLI->TLI_NOMRES
	Endif
	Return cRet	
Endif

dbSelectArea(cTab)
dbSetOrder(1)
dbSeek(xFilial(cTab)+Substr(TLI->TLI_CODRES,1,nTam))
cRet := Substr(&cCpo,1,20)

RestArea(aArea)
Return cRet
/*/


Ŀ
Funo    Val700Even Autor Denis Hyroshi de Souza  Data 11/07/2007
Ĵ
Descrio  Consistencia antes de gravar de um evento da SIPAT         
ٱ


/*/
Function Val700Even()
Local aArea := GetArea()

If !ValResp700(M->TLI_CODRES)
	Return .f.
Endif

If lSigaMdtps
	lRet := ExistChav("TLI",cCliMdtps+M->TLI_SIPAT+DTOS(M->TLI_DTPROG)+M->TLI_HRPROG,6)
Else
	lRet := ExistChav("TLI",M->TLI_SIPAT+DTOS(M->TLI_DTPROG)+M->TLI_HRPROG)
Endif

Restarea(aArea)
Return lRet
/*/


Ŀ
Funo    ValDataTLI Autor Denis Hyroshi de Souza  Data 11/07/2007
Ĵ
Descrio  Validacao data evento                                      
ٱ


/*/
Function ValDataTLI(dTLI_DTPROG)

If !Empty(dTLI_DTPROG)
	If dTLI_DTPROG < M->TLH_DTINI .or. dTLI_DTPROG > M->TLH_DTFIM
		MsgStop(STR0004+; //"A data do evento deve estar dentro do perodo da SIPAT ("
				DtoC(M->TLH_DTINI)+STR0005+DtoC(M->TLH_DTFIM)+").",STR0003) //"  "###"Ateno"
		Return .f.
	Endif
Endif

Return .t.
/*/


Ŀ
Funo    Val700Data Autor Denis Hyroshi de Souza  Data 11/07/2007
Ĵ
Descrio  Validacao data fim da SIPAT                                
ٱ


/*/
Function Val700Data()

If !Empty(M->TLH_DTFIM)
	If M->TLH_DTFIM < M->TLH_DTINI
		MsgStop(STR0006,STR0003) //"A Data Fim no pode ser anterior  Data Inicio."###"Ateno"
		Return .f.
	Endif
Endif

Return .t.

/*/


Ŀ
Funo    ValHoFiTLI Autor Denis Hyroshi de Souza  Data 11/07/2007
Ĵ
Descrio  Validacao da hora fim                                      
ٱ


/*/
Function ValHoFiTLI(cTLI_HRFIM)

If Alltrim(cTLI_HRFIM) <> ":"

	If !NgValHora(cTLI_HRFIM)
		Return .f.
	Endif
	
	nPos3 := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TLI_HRPROG" })
	
	If nPos3 > 0
		M->TLI_HRPROG := aCols[n,nPos3]
	Endif
	
	If cTLI_HRFIM < M->TLI_HRPROG
		MsgStop(STR0007,STR0003) //"A Hora Fim no pode ser anterior  Hora Inicio."###"Ateno"
		Return .f.
	Endif
Endif

Return .t.
/*/


Ŀ
Funo     MenuDef   Autor  Denis Hyroshi de Souza Data 11/07/2007
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaMDT                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()

Local lPyme      := If(Type("__lPyme") <> "U",__lPyme,.F.)
Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )
Local aRotina
						 
If lSigaMdtps
	aRotina := { { STR0008,   "AxPesqui"  , 0 , 1},; //"Pesquisar"
	             { STR0009,   "NGCAD01"   , 0 , 2},; //"Visualizar"
	             { STR0016,   "MDT700TLH" , 0 , 4} } //"SIPAT"
Else
	aRotina :=	{ { STR0008, "AxPesqui"  , 0 , 1},; //"Pesquisar"
	              { STR0009, "NGCAD01"   , 0 , 2},; //"Visualizar"
	              { STR0010, "NGCAD01"   , 0 , 3},; //"Incluir"
	              { STR0011, "NGCAD01"   , 0 , 4},; //"Alterar"
	              { STR0012, "NGCAD01"   , 0 , 5, 3},; //"Excluir"
	              { STR0013, "MDT700PROG", 0 , 4} } //"Pro&gramacao"
	
	If !lPyme
		aAdd(aRotina, { STR0017, "MsDocument", 0, 4 } )  //"Conhecimento"
	EndIf
Endif

Return aRotina
/*/


Ŀ
Funo    WhenCodRes Autor Denis Hyroshi de Souza  Data 11/07/2007
Ĵ
Descrio  When do campo TLI_CODRES                                   
ٱ


/*/
Function WhenCodRes()
Local nPosInd := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TLI_INDRES" })
If nPosInd > 0
	If aCols[n,nPosInd] == "3" .or. Empty(aCols[n,nPosInd])
		Return .f.
	Endif
Endif
Return .t.
/*/


Ŀ
Funo    WhenNomRes Autor Denis Hyroshi de Souza  Data 11/07/2007
Ĵ
Descrio  When do campo TLI_NOMRES                                   
ٱ


/*/
Function WhenNomRes()
Local nPosInd := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TLI_INDRES" })
If nPosInd > 0
	If aCols[n,nPosInd] == "3" .And. !Empty(aCols[n,nPosInd])
		Return .T.
	Endif
Endif
Return .f.
/*/


Ŀ
Funo    ValdIndRes Autor Denis Hyroshi de Souza  Data 11/07/2007
Ĵ
Descrio  Validacao do campo TLI_INDRES                              
ٱ


/*/
Function ValdIndRes(cIndResp)
Local nPosCod := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TLI_CODRES" })
Local nPosNom := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TLI_NOMRES" })

aCols[n,nPosCod] := Space(TAMSX3("TLI_CODRES")[1])
aCols[n,nPosNom] := Space(TAMSX3("TLI_NOMRES")[1])

Return .t.

/*/


Ŀ
Funo    MDT700TLH  Autor Andre Perez Alvarez     Data 17/10/2007
Ĵ
Descrio Browse com SIPAT do Cliente(Prestador)                      
ٱ


/*/
Function MDT700TLH(cAlias,nRecno,cOpcaoX)
Local aArea	:= GetArea()
Local oldROTINA := aCLONE(aROTINA)
Local oldCad := cCadastro
Local lPyme  := If(Type("__lPyme") <> "U",__lPyme,.F.)

cCliMdtPs := SA1->A1_COD+SA1->A1_LOJA

aRotina :=	{ 	{ STR0008, "AxPesqui"  , 0 , 1},; //"Pesquisar"
				{ STR0009, "NGCAD01"   , 0 , 2},; //"Visualizar"
				{ STR0010, "NGCAD01"   , 0 , 3},; //"Incluir"
				{ STR0011, "NGCAD01"   , 0 , 4},; //"Alterar"
				{ STR0012, "NGCAD01"   , 0 , 5, 3},; //"Excluir"
				{ STR0013, "MDT700PROG", 0 , 4} } //"Pro&gramacao"

If !lPyme
	aAdd(aRotina, { STR0017, "MsDocument", 0, 4 } )  //"Conhecimento"
EndIf

Private cPrograma := "MDTA700"
Private cCadastro := OemtoAnsi(STR0001) //"Cadastro da SIPAT"
Private aChkDel := {}, bNgGrava := {}
Private aTROCAF3 := {}

aCHKDEL := { {'TLH->TLH_SIPAT' , "TLI", 1}}

aCHOICE := {}

dbSelectArea("TLH")
Set Filter To TLH->(TLH_CLIENT+TLH_LOJA) == cCliMdtps
dbSetOrder(5)
mBrowse(6,1,22,75,"TLH")

dbSelectArea("TLH")
Set Filter To

aROTINA := aCLONE(oldROTINA)
RestArea(aArea)
cCadastro := oldCad

Return

/*/


Ŀ
Funo    MDT700VSIP Autor  Denis                  Data  25/10/07 
Ĵ
Descrio  Valida campo TLH_MANDAT                                    
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function MDT700VSIP()
Local lPrest := .F.

If Type("cCliMdtPs") == "C"
	If !Empty(cCliMdtPs)
		lPrest := .T.
	Endif
Endif

If lPrest
	Return If(Vazio(),.t.,ExistCpo('TNN',cCliMdtps+M->TLH_MANDAT,3))
Else
	Return If(Vazio(),.t.,ExistCpo('TNN',M->TLH_MANDAT))
Endif
Return .t.