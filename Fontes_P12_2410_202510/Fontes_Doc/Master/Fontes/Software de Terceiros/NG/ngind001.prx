#INCLUDE "NGIND001.ch"
#INCLUDE "Protheus.ch"
#INCLUDE "TBICONN.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NGIND001 ³ Autor ³ Denis Hyroshi de Souza³ Data ³01/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Indicadores                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT, SigaMDT e SigaSGA                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
ITENS PARA A VERSAO 2
-Grafico
-Avaliacao detalhada
-Pesquisa detalhada
-Usuarios com acesso apenas aos indicadores de seu interesse

/*/
Function NGIND001(lTipoExec,nFoldOption)
	
	default lTipoExec := .F.

	//Valida se as tabelas dos indicadores foram criadas corretamente
	MNTVldInd()

	If !lTipoExec .And. fNewInd()
		NGIND014()
	Else
		fNGIND001( lTipoExec,nFoldOption )
	EndIf

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} fNGIND001
Responsável por criar tela de indicadores SIGAMNT modelo não PO-UI

@type   Function

@author Denis Hyroshi de Souza
@since  01/07/2008
@param  lTipoExec, Lógico, Define se é um processo automático
@param  nFoldOption, Numérico, Folder posicionado

/*/
//-------------------------------------------------------------------
Static Function fNGIND001( lTipoExec, nFoldOption )
	
	Local aPages		:= {}
	Local aTitles		:= {}
	Local nControl		:= 0
	Local nYY			:= 0
	Local nXX			:= 0
	Local nInd1			:= 0
	Local nConWid		:= 0
	Local nConHei		:= 0
	Local nQtdPorLin	:= 0
	Local nQtdLinAtu	:= 0
	Local nSkipLin 		:= 0
	Local nSaySize		:= 0
	Local nIndTZ5		:= 0
	Local oPnlInd
	Local oScroll
	Local oPnlBtns
	Local oMainWnd
	Local oContainer
	Local oFont    		:= TFont():New("Arial",,-10,.F.,.F.)
	Local oFont12  		:= TFont():New("Arial",,-12,.T.,.T.)
	Local lAutoInd 		:= .F.
	Local lUpd 			:= .F.
	Local lNGIND00A		:= ExistBlock("NGIND00A")

	Default nFoldOption := 1

	Private nColIni
	Private nLinObj, nColObj

	Private nOptionF := nFoldOption
	Private cPerg := "NGIND1"
	Private lSimParam := .F.

	pergunte(cPerg,.f.)

	Private lCpo2_TZ5 := NGCADICBASE("TZ5_FORCON","D","TZ5",.F.)
	Private lRepliPar := .f. //Indica se ao alterar o conteudo de um parametro, o mesmo parametro nos demais indicadores serao alterados automaticamente
	Private Mv_parFo := Mv_par01
	Private Mv_parGr := Mv_par02
	Private oDlgInd, oFolder1
	Private cCadastro := OemtoAnsi(STR0004) //"Indicadores"
	Private cPrograma := "NGIND001"
	Private oMenu
	Private aNgCadBtn := {}
	aAdd(aNgCadBtn,{"PARAMETROS",{|| fNG001Conf() },STR0005,STR0005}) //"Opções"###"Opções"
	aAdd(aNgCadBtn,{"SDUSETDEL" ,{|| fNG001Lege() },STR0006,STR0007}) //"Legenda do Resultado"###"Legenda"

	If Empty(Mv_parFo)
		Mv_parFo := 1
	EndIf

	If Empty(Mv_parGr)
		Mv_parGr := 2
	EndIf

	If !AliasInDic("TZ9")
		Mv_parGr := 2
	EndIf

	If !NGCADICBASE("TZ4_RELACA","D","TZ4",.F.)
		If !NGINCOMPDIC("UPDINDIC","XXXXXXXXX / 2009")
			Return
		Endif
	EndIf
	If NGCADICBASE("TZ5_ATIVO","A","TZ5",.F.)
		lUpd := .T.
	Endif
	nPosMod := 0
	If Type("aAmbientes") == "A"
		nPosMod := aScan( aAmbientes, {|x| x[1] == nModulo } )
		If nPosMod > 0
			cCadastro := cCadastro + STR0008 + aAmbientes[nPosMod,3] //" do Módulo de "
		Endif
	Endif
	If nPosMod == 0
		If cModulo == "MNT"
			cCadastro := STR0021//cCadastro + STR0009 //" do Módulo de Manutenção de Ativos"
		ElseIf cModulo == "MDT"
			cCadastro := cCadastro + STR0010 //" do Módulo de Medicina e Segurança do Trabalho"
		ElseIf cModulo == "SGA"
			cCadastro := cCadastro + STR0011 //" do Módulo de Gestão Ambiental"
		Else
			cCadastro := cCadastro + STR0012 + cModulo //" do Módulo "
		Endif
	Endif

	If Valtype(lTipoExec) == "L"
		If lTipoExec
			If Type("aCadTipo") == "A" .and. Type("aVarParm") == "A" .and. Type("aIndParm") == "A" .and. Type("aRetFunc") == "A" .and. ;
				Type("oIndNG") == "A" .and. Type("nIndNG") == "A" .and. Type("lIndNG") == "A"

				lAutoInd := .t.
			Endif
		Endif
	Endif

	If !lAutoInd
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//Carrega Classificação dos Indicadores
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Private aCadTipo := {}
		Private aVarParm := {} //Parametros por variavel
		Private aIndParm := {} //Parametros comuns do indicador
		Private aRetFunc := {} //Resultados dos calculos das variaveis
		Private oIndNG
		Private nIndNG
		Private lIndNG

		nMaxCol  := 0
		dbSelectArea("TZ1")
		dbSetOrder(1)
		dbSeek(xFilial("TZ1")+Str(nModulo,2))
		While !eof() .and. xFilial("TZ1") == TZ1->TZ1_FILIAL .and. TZ1->TZ1_MODULO == Str(nModulo,2)

			//Carrega Indicadores
			aIndTZ5 := {}
			dbSelectArea("TZ5")
			nIndTZ5 := NGRETORDEM("TZ5","TZ5_FILIAL+TZ5_CODCLA+TZ5_MODULO")
			dbSetOrder(nIndTZ5)
			dbSeek(xFilial("TZ5")+TZ1->TZ1_CODCLA+Str(nModulo,2))
			While !eof() .and. xFilial("TZ5") == TZ5->TZ5_FILIAL .and. TZ1->TZ1_CODCLA == TZ5->TZ5_CODCLA .and. TZ5->TZ5_MODULO == Str(nModulo,2)
				If lUpd
					If TZ5->TZ5_ATIVO == "2"
						dbSelectArea("TZ5")
						dbSkip()
						Loop
					Endif
				Endif
				cMemoTZ5 := Alltrim(TZ5->TZ5_FORMUL)
				If lCpo2_TZ5
					cMemoTZ5 := Alltrim(TZ5->TZ5_FORMUL)+Alltrim(TZ5->TZ5_FORCON)
				Endif
				cTempFor := StrTran(cMemoTZ5, Chr(10) , "")
				cTem2For := StrTran(cTempFor, "@" , "" )
				cTem2For := StrTran(cTem2For, "#" , "" )

				//Dados da tabela TZ7 (Parametros Fixos e Obrigatorios)
				aColsVal := {}
				aColsObr := {}
				dbSelectArea("TZ7")
				dbSetOrder(1)
				dbSeek(xFilial("TZ7")+Str(nModulo,2)+TZ5->TZ5_CODIND)
				While !Eof() .and. xFilial("TZ7")+TZ5->TZ5_CODIND == TZ7->TZ7_FILIAL+TZ7->TZ7_CODIND .and. TZ7->TZ7_MODULO == Str(nModulo,2)

					If TZ7->TZ7_TIPO == '1'

						If aScan( aColsVal, {|x| x[2] == TZ7->TZ7_CODPAR } ) == 0
							dbSelectArea("TZ4")
							dbSetOrder(1)
							If dbSeek(xFilial("TZ4")+Str(nModulo,2)+TZ7->TZ7_CODPAR)
								cTipoTemp := TZ4->TZ4_TIPO
								If !Empty(Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO"))
									cTipoTemp := Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO")
								Endif
								xTempVal := Alltrim(TZ7->TZ7_CONTEU)
								If cTipoTemp $ "D/4"
									xTempVal := StoD(xTempVal)
								ElseIf cTipoTemp $ "N/2"
									xTempVal := Val(xTempVal)
								ElseIf cTipoTemp $ "L/3"
									xTempVal := If(xTempVal==".F.",.f.,.t.)
								ElseIf cTipoTemp $ "6"
									aTipoTZ4 := fRetCombo(Alltrim(TZ4->TZ4_OPCOES))
									nIND := aScan(aTipoTZ4,{|x| Upper(Substr(x,1,1)) == Substr(xTempVal,1,1)})
									If nIND > 0
										xTempVal := Substr(xTempVal,1,1)
									Else
										xTempVal := " "
									Endif
								Endif
								aAdd( aColsVal , { TZ7->TZ7_CODPAR , Alltrim(TZ4->TZ4_DESCRI) , xTempVal } )
							Endif
						Endif

					ElseIf TZ7->TZ7_TIPO == '2'

						If aScan( aColsObr, {|x| x == TZ7->TZ7_CODPAR } ) == 0
							dbSelectArea("TZ4")
							dbSetOrder(1)
							If dbSeek(xFilial("TZ4")+Str(nModulo,2)+TZ7->TZ7_CODPAR)
								aAdd( aColsObr , TZ7->TZ7_CODPAR )
							Endif
						Endif

					Endif

					dbSelectArea("TZ7")
					dbSkip()
				End

				cFormatoInd := ""
				nValor1 := nil
				nValor2 := nil
				If TZ5->TZ5_TIPVAL == "2"
					cFormatoInd := "@!"
					If !Empty(TZ5->TZ5_VALOR1)
						nValor1 := Alltrim(TZ5->TZ5_VALOR1)
					Endif
					If !Empty(TZ5->TZ5_VALOR2)
						nValor2 := Alltrim(TZ5->TZ5_VALOR2)
					Endif
				Else
					NGINDPICT( TZ5->TZ5_TAMMED, TZ5->TZ5_DECMED, @cFormatoInd, ",", ".")
					If Empty(cFormatoInd)
						cFormatoInd := "@E 999,999,999.99"
					Else
						cFormatoInd := "@E " + cFormatoInd
					Endif
					If !Empty(TZ5->TZ5_VALOR1)
						nValor1 := Val(TZ5->TZ5_VALOR1)
					Endif
					If !Empty(TZ5->TZ5_VALOR2)
						nValor2 := Val(TZ5->TZ5_VALOR2)
					Endif
				Endif

				cTmpUni := ""
				dbSelectArea("SAH")
				dbSetOrder(1)
				If dbSeek(xFilial("SAH")+TZ5->TZ5_UNIMED)
					If !Empty(SAH->AH_UMRES)
						cTmpUni := "  " + Alltrim(SAH->AH_UMRES)
					Else
						cTmpUni := "  " + Alltrim(TZ5->TZ5_UNIMED)
					Endif
				Endif
				aADD( aIndTZ5 , { TZ5->TZ5_CODIND , TZ5->TZ5_NOME , cTempFor , cTem2For , {} ,;
								aClone(aColsVal) , aClone(aColsObr) , MSMM(TZ5->TZ5_DESCRI) , cFormatoInd ,;
								TZ5->TZ5_TIPRES, nValor1, nValor2 , cTmpUni , TZ5->TZ5_TIPVAL } )

				//Adiciona no vetor aVarParm os parametros por Formula e por Variavel
				NgIndParm( cTempFor , TZ5->TZ5_CODIND, .F.)

				dbSelectArea("TZ5")
				dbSkip()
			End

			If Len(aIndTZ5) > 0
				aSort( aIndTZ5 ,,, { |x,y| x[2]+x[1] < y[2]+y[1] } )
				aADD( aCadTipo , { TZ1->TZ1_CODCLA , TZ1->TZ1_DESCRI , aIndTZ5 } )
				If Len(aIndTZ5) > nMaxCol
					nMaxCol := Len(aIndTZ5)
				Endif
			Endif

			dbSelectArea("TZ1")
			dbSkip()

		End
		If nMaxCol > 0
			oIndNG:=Array(Len(aCadTipo),nMaxCol,7)
			nIndNG:=Array(Len(aCadTipo),nMaxCol)
			lIndNG:=Array(Len(aCadTipo),nMaxCol)
		Endif
	Endif

	If Len(aCadTipo) == 0
		MsgInfo(STR0013) //"Não existe nenhum Indicador para ser apresentado."
		Return .f.
	Endif

	//Carregando Classificação dos Indicadores
	For nInd1 := 1 To Len(aCadTipo)
		Aadd(aTitles,OemToAnsi(aCadTipo[nInd1,2]))
		Aadd(aPages,"Header "+Alltrim(Str(nInd1,6)))
	Next nInd1
	nControl := Len(aCadTipo)

	//Tamanho da tela
	Private aAC := {STR0014,STR0015},aCRA:= {STR0015,STR0016,STR0014} //"Abandona"###"Confirma"###"Confirma"###"Redigita"###"Abandona"
	Private aHeader[0],Continua,nUsado:=0
	Private aSize := MsAdvSize(,.f.,430), aObjects := {}
	Aadd(aObjects,{200,200,.t.,.f.})
	aInfo := {aSize[1],aSize[2],aSize[3],aSize[4],0,0}
	aPosObj := MsObjSize(aInfo, aObjects,.t.)

	nQtdLinha := 90
	nColDif   := 50
	If aSize[5] > 1200
		nQtdLinha := 150
		nColDif   := 75
	ElseIf aSize[5] > 1000
		nQtdLinha := 120
		nColDif   := 65
	Endif

	If aSize[6] > 900
		aPosObj[1,3] := aPosObj[1,3] * ( aSize[6] / 560 )
	ElseIf aSize[6] > 650
		aPosObj[1,3] := aPosObj[1,3] * ( aSize[6] / 590 )
	Else
		aPosObj[1,3] := aPosObj[1,3] * ( aSize[6] / 610 )
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³TELA INICIAL                                                                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nOpca := 0
	DEFINE MSDIALOG oDlgInd TITLE OemToAnsi(cCadastro) From aSize[7],0 To aSize[6],aSize[5] OF oMainWnd PIXEL
	oDlgInd:LESCCLOSE := .f.

	// Painel principal do Dialog
	oPnlInd := TPanel():New(01, 01, , oDlgInd, , , , CLR_BLACK, CLR_WHITE, 100, 020)
	oPnlInd:Align := CONTROL_ALIGN_ALLCLIENT

	//-- Folder
	oFolder1 := TFolder():New(1,0,aTitles,aPages,oPnlInd,,,,.f.,.f.,aPosObj[1,4],aPosObj[1,3],)//382,208
	oFolder1:Align := CONTROL_ALIGN_ALLCLIENT

	For nInd1 := 1 To Len(aCadTipo)
		oFolder1:aDialogs[nInd1]:oFont := oDlgInd:oFont
		oFolder1:bChange := { || NgInd1Fol() }
	Next nInd1

	//-- Painel dos Botões
	oPnlBtns := TPanel():New(01, 01, , oPnlInd, , , , CLR_BLACK, CLR_WHITE, 100, 020)
	oPnlBtns:Align := CONTROL_ALIGN_BOTTOM

		@ 005,010 Button oBtnMark Prompt STR0017 Size 45,11 Of oPnlBtns Pixel Action fNgChkAll(1) //"Marcar Todos"
		oBtnMark:cToolTip := STR0018 //"Selecionar os indicadores da pasta selecionada"
		oBtnMark:Show()

		@ 005,010 Button oBtnDemk Prompt STR0019 Size 45,11 Of oPnlBtns Pixel Action fNgChkAll(2) //"Desmarcar Todos"
		oBtnDemk:cToolTip := STR0020 //"Desmarcar os indicadores da pasta selecionada"
		oBtnDemk:Hide()

		@ 005,060 Button oBtnPara Prompt STR0021 Size 45,11 Of oPnlBtns Pixel Action fNgChkPar(1) //"Parâmetros"
		oBtnPara:cToolTip := STR0022 //"Edita os parâmetros de seleção dos indicadores assinalados"

		@ 005,110 Button oBtnCalc Prompt STR0023 Size 45,11 Of oPnlBtns Pixel Action Processa({|lEnd| fNgCalc() } , STR0024) //"Calcular"###"Calculando Indicadores..."
		oBtnCalc:cToolTip := STR0025 //"Calcula todos os indicadores assinalados"

		@ 005,160 Button oBtnRelt Prompt STR0026 Size 45,11 Of oPnlBtns Pixel Action Processa({|lEnd| NgInd1Rel(,,.t.) } , STR0027) //"Relatório"###"Imprimindo..."
		oBtnRelt:cToolTip := STR0028 //"Relatório dos indicadores assinalados"

		If lNGIND00A
			ExecBlock("NGIND00A",.F.,.F.,{005,160,oPnlBtns})
		Endif

	//-- Monta Abas
	nConWid := 140
	nConHei := 150
	nQtdPorLin := Int( (oDlgInd:nClientWidth * 0.50) / (nConWid+030) )
	nColIni := ( ( (oDlgInd:nClientWidth * 0.50) - (nQtdPorLin * nConWid) )  / nQtdPorLin )
	For nXX := 1 to Len(aCadTipo)
		nLinObj := 5

		nQtdLinAtu := 0

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Cria um scrollbox para cada tipo de indicador  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oScroll := TScrollBox():New(oFolder1:aDialogs[nXX], 0, 0, 0, 0, .T., .F., .F.)
		oScroll:nClrPane := CLR_WHITE
		oScroll:Align := CONTROL_ALIGN_ALLCLIENT
		oScroll:CoorsUpdate()

		For nYY := 1 to Len(aCadTipo[nXX,3])
			If !lAutoInd
				If aCadTipo[nXX,3,nYY,14] == "2"
					nIndNG[nXX,nYY] := "0:00"
				Else
					nIndNG[nXX,nYY] := 0
				Endif
				lIndNG[nXX,nYY] := .F.
			Endif

			cStrXX := Alltrim( Str(nXX) )
			cStrYY := Alltrim( Str(nYY) )

			nSkipLin := 0

			nQtdLinAtu++
			If nQtdLinAtu > nQtdPorLin
				nQtdLinAtu := 1
				nLinObj += If(Mv_parGr == 1, (nConHei + 030), 060)
			EndIf
			If nQtdLinAtu == 1
				nColObj := nColIni * 0.30
			Else
				nColObj += (nConWid + nColIni)
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Titulo Indicador                               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nSaySize := CalcFieldSize("C",Len(aCadTipo[nXX,3,nYY,2]),0,"@!",aCadTipo[nXX,3,nYY,2],oFont12)
			@ nLinObj, nColObj SAY oIndNG[nXX,nYY,1] Prompt Space(40) PIXEL OF oScroll Font oFont12 SIZE nSaySize,008
			oIndNG[nXX,nYY,1]:bSetGet := ;
				&("{|u| If(pCount() == 0, aCadTipo["+cStrXX+",3,"+cStrYY+",2], aCadTipo["+cStrXX+",3,"+cStrYY+",2]:=u)}")
			oIndNG[nXX,nYY,1]:SetText(aCadTipo[nXX,3,nYY,2])
			oIndNG[nXX,nYY,1]:BRCLICKED := &("{ |o,x,y| fNg01RCli(x,y,o,"+cStrXX+","+cStrYY+") }")
			oIndNG[nXX,nYY,1]:cToolTip := aCadTipo[nXX,3,nYY,8]
			oIndNG[nXX,nYY,1]:bLClicked := &("{|u| lIndNG["+cStrXX+","+cStrYY+"] := !lIndNG["+cStrXX+","+cStrYY+"], NgInd1Fol() }")

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³CheckBox para indicar se a Formula sera calculada ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			oIndNG[nXX,nYY,4] := TCheckBox():New(nLinObj,(nColObj-010),"",,oScroll,10,08,,,,,,,,.T.)
			oIndNG[nXX,nYY,4]:bSetGet   := &("{|u| If(PCount()==0,lIndNG["+cStrXX+","+cStrYY+"],lIndNG["+cStrXX+","+cStrYY+"]:=u)}")
			//oIndNG[nXX,nYY,4]:bLClicked := &("{|| fNgInd1Ck("+cStrXX+","+cStrYY+",.f.) }")
			oIndNG[nXX,nYY,4]:bLClicked := {|| NgInd1Fol() }

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Formula Indicador                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Mv_parFo == 2
				nLinFor := ( Len(aCadTipo[nXX,3,nYY,4]) / nQtdLinha )
				nModFor := ( Len(aCadTipo[nXX,3,nYY,4]) % nQtdLinha )
				If nModFor <> 0 .or. nLinFor == 0
					nLinFor++
				Endif
				nSkipLin += 13
				@ nLinObj+nSkipLin, nColObj SAY oIndNG[nXX,nYY,2] Prompt Space(40) SIZE (nQtdLinha*3.11),(8*nLinFor) PIXEL OF oScroll Font oFont
				oIndNG[nXX,nYY,2]:bSetGet := ;
					&("{|u| If(pCount() == 0, aCadTipo["+cStrXX+",3,"+cStrYY+",4], aCadTipo["+cStrXX+",3,"+cStrYY+",4]:=u)}")
				oIndNG[nXX,nYY,2]:SetText(aCadTipo[nXX,3,nYY,4])
				oIndNG[nXX,nYY,2]:cToolTip := STR0029 //"Fórmula do indicador"
				If nLinFor > 1
					//nSkipLin += (nLinFor-1)*7
				Endif
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Indicador Gráfico                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Mv_parGr == 1
				If NGIFDBSEEK("TZA", aCadTipo[nXX,3,nYY,1], 2)
					oContainer := TPanel():New(nLinObj+nSkipLin+015, nColObj, , oScroll, , , , CLR_BLACK, CLR_WHITE, nConWid, nConHei, .F., .F.)
						oIndNG[nXX,nYY,7] := TNGIndicator():New(/*nTop*/, /*nLeft*/, 1/*nZoom*/, oContainer/*oParent*/, /*nWidth*/, /*nHeight*/, ;
																/*nClrFooter*/, /*cContent*/, /*cStyle*/, .F./*lScroll*/, .T./*lCenter*/)
						oIndNG[nXX,nYY,7]:SetFields(NGI7Fields())
						oIndNG[nXX,nYY,7]:Indicator()
						oIndNG[nXX,nYY,7]:Refresh(.F.)
						// Carrega Fórmula
						NGI7LoaFrm(oIndNG[nXX,nYY,7]/*oObjIndic*/, aCadTipo[nXX,3,nYY,1]/*cCodFormul*/, /*cCodFilial*/, /*cCodModulo*/, .F./*lShowMsg*/)
						oIndNG[nXX,nYY,7]:CanConfig(.F.)
						oIndNG[nXX,nYY,7]:CanZoom(.F.)
						// Se reabrindo tela, atualiza o valor
						If lAutoInd
							oIndNG[nXX,nYY,7]:SetValue(nIndNG[nXX,nYY])
						EndIf
						oIndNG[nXX,nYY,7]:Refresh(.T.)
						// Informações
						oIndNG[nXX,nYY,7]:SetCodeBlock(3, {|oObjIndic| NGI7Info(oObjIndic) })
						// Detalhes
						oIndNG[nXX,nYY,7]:SetCodeBlock(4, {|oObjIndic| NGI7Detail(oObjIndic) })
						// Legenda
						oIndNG[nXX,nYY,7]:SetCodeBlock(5, {|oObjIndic| NGI7Legend(oObjIndic) })
				EndIf
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Resultado Indicador                            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			@ nLinObj+nSkipLin+015, nColObj SAY oIndNG[nXX,nYY,3] Prompt Space(40) SIZE 100,8 PIXEL OF oScroll Font oFont12
			If aCadTipo[nXX,3,nYY,14] <> "2"
				oIndNG[nXX,nYY,3]:SetText( Alltrim( TransForm( 0 , aCadTipo[nXX,3,nYY,9]) )  + aCadTipo[nXX,3,nYY,13] )
			Else
				oIndNG[nXX,nYY,3]:SetText( "0:00"  + aCadTipo[nXX,3,nYY,13] )
			Endif
			//Se reabrindo tela, verifica se o resultado esta Ok ou Fora do Padrao
			If lAutoInd
				oIndNG[nXX,nYY,3]:cToolTip := NgInd1Det(nXX,nYY,aCadTipo[nXX,3,nYY,5])
				//Verifica se está dentro dos padrões
				If aCadTipo[nXX,3,nYY,14] <> "2"
					If nIndNG[nXX,nYY] <> 0
						oIndNG[nXX,nYY,3]:SetText( Alltrim( TransForm(nIndNG[nXX,nYY],aCadTipo[nXX,3,nYY,9]) ) + aCadTipo[nXX,3,nYY,13] )
					Endif
				Else
					If nIndNG[nXX,nYY] <> "0:00"
						oIndNG[nXX,nYY,3]:SetText( nIndNG[nXX,nYY] + aCadTipo[nXX,3,nYY,13] )
					Endif
				Endif
				If oIndNG[nXX,nYY,6] == 2
					oIndNG[nXX,nYY,3]:nClrText := CLR_GRAY
				ElseIf oIndNG[nXX,nYY,6] == 3
					oIndNG[nXX,nYY,3]:nClrText := CLR_BLACK
				ElseIf oIndNG[nXX,nYY,6] == 4
					oIndNG[nXX,nYY,3]:nClrText := CLR_HRED
				Else
					oIndNG[nXX,nYY,3]:nClrText := CLR_HGRAY
					oIndNG[nXX,nYY,6] := 1
				Endif
			Else
				oIndNG[nXX,nYY,3]:nClrText := CLR_HGRAY
				oIndNG[nXX,nYY,6] := 1
			Endif
			// Esconde o texto simples quando o Indicador Gráfico está sendo mostrado
			If Mv_parGr == 1
				// Caso tenha carregado a fórmula no Indicador Gráfico, esconde o texto
				If ValType(oIndNG[nXX,nYY,7]) == "O"
					oIndNG[nXX,nYY,3]:Hide()
				EndIf
			EndIf
		Next nYY

		nLinObj += 4
		@ nLinObj, 012 SAY "" PIXEL OF oScroll Font oFont12
	Next nXX

	If nOptionF <= Len(aPages)
		oFolder1:SetOption(nOptionF)
	EndIf

	ACTIVATE MSDIALOG oDlgInd ON INIT EnchoiceBar(oDlgInd,{|| nOpca := 1,oDlgInd:End()},{|| nOpca := 2,oDlgInd:End()},,aNgCadBtn)

	If nOpca == 3
		NGIND001(.T.,nOptionF)
		Return .T.
	EndIf
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NgInd1Det ³ Autor ³ Denis Hyroshi Souza   ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Exibe as respostas dos parametros                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NgInd1Det(nStrXX,nStrYY,aParSave)
Local cRet := " "
Local nXX

For nXX := 1 to Len( aParSave )

	If nXX == 1
		cRet := STR0030 //"Parâmetros informados para o cálculo do indicador"
		cRet += Chr(13) + Chr(10)
		cRet += "--------------------------------------------------------------"
	Endif

	cRet += Chr(13) + Chr(10)

	cRet += aParSave[nXX, 2] + ": "
	cValType := ValType( aParSave[nXX, 11] )

	If cValType $ "D/4"
		cRet += DtoC( aParSave[nXX, 11] )
	ElseIf cValType $ "N/2"
		cRet += cValToChar( aParSave[nXX, 11] )
	ElseIf cValType $ "L/3"
		If aParSave[nXX, 11]
			cRet += ".T."
		ElseIf !aParSave[nXX, 11]
			cRet += ".F."
		Else
			cRet += STR0031 //"Nulo"
		Endif
	ElseIf cValType $ "C/1"
		cRet += aParSave[nXX, 11]
	Else
		cRet += STR0031 //"Nulo"
	Endif
Next nXX

Return cRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fNg01RCli ³ Autor ³ Denis Hyroshi Souza   ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Opcoes do Click da Direita                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function fNg01RCli(x,y,o,nXX,nYY)
Local aSMenu := {}

aAdd(aSMENU , { STR0021 , "fNgInd1Ck("+Str(nXX)+","+Str(nYY)+",.t.)" } ) //"Parâmetros"
aAdd(aSMENU , { STR0032   , "NgInd1Rel("+Str(nXX)+","+Str(nYY)+",.f.)" } ) //"Detalhes"
If Len( aCadTipo[nXX,3,nYY,5] ) > 0
	aAdd(aSMENU , { STR0023   , "NgIndCalc("+Str(nXX)+","+Str(nYY)+")" } ) //"Calcular"
Endif

NGPOPUP(aSMenu,@oMenu)
oMenu:Activate(x,y,o)

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NgIndCalc ³ Autor ³ Denis Hyroshi Souza   ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula apenas o indicador selecionado com o right click   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NgIndCalc(__nXX,__nYY)
Local aIndBck := aClone(lIndNG)
Local nPosX,nPosY

For nPosX := 1 to Len(lIndNG)
	For nPosY := 1 to Len(lIndNG[nPosX])
		If ValType(lIndNG[nPosX,nPosY]) == "L"
			lIndNG[nPosX,nPosY] := .f.
		Endif
	Next nYYY
Next nXXX

lIndNG[__nXX,__nYY] := .t.
Processa({|lEnd| fNgCalc() } , STR0024) //"Calculando Indicadores..."
lIndNG := aClone(aIndBck)
lIndNG[__nXX,__nYY] := .t.
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fNgInd1Ck ³ Autor ³ Denis Hyroshi Souza   ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida o campo CheckBox                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function fNgInd1Ck(__nXX,__nYY,lOpenPar)
If lIndNG[__nXX,__nYY] .or. lOpenPar
	//oIndNG[__nXX,__nYY,3]:Enable()
	If fNgChkPar(2,__nXX,__nYY)
		If lOpenPar
			lIndNG[__nXX,__nYY] := .t.
			oIndNG[__nXX,__nYY,4]:Refresh()
		Endif
	Endif
Else
	//oIndNG[__nXX,__nYY,3]:Disable()
Endif
Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Ng1DetInd ³ Autor ³ Denis Hyroshi Souza   ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Detalha o resultado do indicador                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ng1DetInd(__nXX,__nYY)
MsgInfo(STR0033) //"Detalhe"
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fNgChkAll ³ Autor ³ Denis Hyroshi Souza   ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Seleciona todos os indicadores                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function fNgChkAll(nTipo)
Local nXXX,nYYY

If nTipo == 1
	oBtnMark:Hide()
	oBtnDemk:Show()
Else
	oBtnMark:Show()
	oBtnDemk:Hide()
Endif

nXXX := oFolder1:nOption

If nXXX <= Len(lIndNG)
	For nYYY := 1 to Len(lIndNG[nXXX])
		If ValType(lIndNG[nXXX,nYYY]) == "L"
			If nTipo == 1
				lIndNG[nXXX,nYYY] := .t.
			Else
				lIndNG[nXXX,nYYY] := .f.
			Endif
		Endif
	Next nYYY
Endif

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fNgChkPar³ Autor ³ Denis Hyroshi Souza   ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Apresenta tela de parametros                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fNgChkPar(nTipoPar,__nXX,__nYY)

Local aRet := aClone( fNgParam(nTipoPar,__nXX,__nYY) )
Local nXX, nPosX, nPosY, nX1, nX2
Local aIndBck

//Armazena as respostas dos parametros no vetor aCadTipo[ X , 3 , Y , 5 ]
If aRet[1]

	//Verificando quais indicadores foram exibidos na tela de parametros
	For nXX := 1 To Len(aRet[2])
		nPosX := aRet[2,nXX,1]
		nPosY := aRet[2,nXX,2]

		//Verifica se houve alteracao na tela de parametros
		lDiffPar := .t.
		If Len(aCadTipo[ nPosX , 3 , nPosY , 5 ]) == Len(aRet[2,nXX,5]) .and. Len(aRet[2,nXX,5]) > 0
			lDiffPar := .f.
			For nX1 := 1 To Len(aCadTipo[ nPosX , 3 , nPosY , 5 ])
				nPosDiff := aScan( aRet[2,nXX,5], {|x| x[12] == aCadTipo[ nPosX , 3 , nPosY , 5 , nX1 , 12] } )
				If nPosDiff > 0
					If ValType( aCadTipo[ nPosX , 3 , nPosY , 5 , nX1 , 11] ) == ValType(aRet[2,nXX,5,nPosDiff,11])
						If aCadTipo[ nPosX , 3 , nPosY , 5 , nX1 , 11] != aRet[2,nXX,5,nPosDiff,11]
							lDiffPar := .t.
							Exit
						Endif
					Else
						lDiffPar := .t.
						Exit
					Endif
				Else
					lDiffPar := .t.
					Exit
				Endif
			Next nX1
		Endif

		aCadTipo[ nPosX , 3 , nPosY , 5 ] := aClone( aRet[2,nXX,5] )

		If !aRet[3]
			If lDiffPar .and. (oIndNG[nPosX,nPosY,6] == 3 .or. oIndNG[nPosX,nPosY,6] == 4)
				oIndNG[nPosX,nPosY,6]:= 2
				oIndNG[nPosX,nPosY,3]:nClrText := CLR_GRAY
				oIndNG[nPosX,nPosY,3]:Refresh()
			Endif
		Endif
    	//oIndNG[ nPosX , nPosY ,3]:cToolTip := NgInd1Det(nPosX , nPosY , aCadTipo[ nPosX , 3 , nPosY , 5 ] )
	Next nXX

	//Se o usuario clicou em Calcular, somente ira calcular os indicadores que foram exibidos
	If aRet[3]
		aIndBck := aClone(lIndNG)
		For nPosX := 1 to Len(lIndNG)
			For nPosY := 1 to Len(lIndNG[nPosX])
				If ValType(lIndNG[nPosX,nPosY]) == "L"
					lIndNG[nPosX,nPosY] := .f.
				Endif
			Next nYYY
		Next nXXX
		For nXX := 1 To Len(aRet[2])
			nPosX := aRet[2,nXX,1]
			nPosY := aRet[2,nXX,2]
			lIndNG[nPosX,nPosY] := .t.
		Next nXX
		Processa({|lEnd| fNgCalc() } , STR0024) //"Calculando Indicadores..."
		lIndNG := aClone(aIndBck)
	Endif

Endif

Return aRet[1]

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fNgParam ³ Autor ³ Denis Hyroshi Souza   ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Apresenta tela de parametros                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fNgParam(nTipoPar,__nXX,__nYY)
Local aParamTZ3 := {}
Local aParamRec := {}
Local aTemp     := {}
Local aPages    := {}
Local aTitles   := {}
Local nPos,nXXX,nYYY,nInd1,nYY,nXX
Local oFont     := TFont():New("Arial",,-10,.F.,.F.)
Local oPnlPai, oPnlBottom
Local cPicture  := ""
Local cValid    := ""
Local lIni		 := .F.
Local lVal		 := .F.
Local cCodInd
Local nAumenta := 25
Local nVarAum

Local nAltura := GetScreenRes()[2]
Local nLargura := GetScreenRes()[1]

Private aIndChk   := {}

If NGCADICBASE("TZ8_VALOR","A","TZ8",.F.)
	lIni := .T.
Endif
If NGCADICBASE("TZ4_VALID","A","TZ4",.F.)
	lVal := .T.
Endif

nVarAum := SetVarNameLen( nAumenta )
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// Parametros de todas as variaveis
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nTipoPar == 1
	For nXXX := 1 to Len(lIndNG)
		For nYYY := 1 to Len(lIndNG[nXXX])
			If ValType(lIndNG[nXXX,nYYY]) == "L"
				If lIndNG[nXXX,nYYY]
					aAdd(aIndChk,{nXXX,nYYY,"","",{}})
				Endif
			Endif
		Next nYYY
	Next nXXX
Else
	aAdd(aIndChk,{__nXX,__nYY,"","",{}})
Endif
If Len(aIndChk) == 0
	MsgInfo(STR0034) //"Nenhum indicador foi selecionado para seleção de parâmetros."
	SetVarNameLen( nVarAum )
	Return {.f.,{},.F.}
Endif

For nXXX := 1 To Len(aIndChk)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	// Parametros da variavel selecionada
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//	If Len( aCadTipo[aIndChk[nXXX,1],3] < aIndChk[nXXX,2]
//		Loop
//	Endif

	aParamRec := {}
	nPos3 := aScan( aIndParm, {|x| x[1] == aCadTipo[aIndChk[nXXX,1],3,aIndChk[nXXX,2],1] } )
	cCodInd	 := aCadTipo[aIndChk[nXXX,1],3,aIndChk[nXXX,2],1]
	If nPos3 > 0
		aParamRec := aClone( aIndParm[nPos3,2] )
	Endif

	If Len(aParamRec) == 0
		MsgInfo(STR0035+Alltrim(aCadTipo[aIndChk[nXXX,1],3,aIndChk[nXXX,2],2])+STR0036) //"As variáveis da fórmula "###" não possuem parâmetros em comum."
		lIndNG[aIndChk[nXXX,1],aIndChk[nXXX,2]] := .f.
		//oIndNG[aIndChk[nXXX,1],aIndChk[nXXX,2],3]:Disable()
		SetVarNameLen( nVarAum )
		Return {.f.,{},.F.}
	Endif

	aTemp := {}
	For nPos := 1 To Len(aParamRec)
		dbSelectArea("TZ4")
		dbSetOrder(1)
		dbSeek(xFilial("TZ4")+Str(nModulo,2)+aParamRec[nPos,1])

		nPos3 := aScan( aCadTipo[aIndChk[nXXX,1],3,aIndChk[nXXX,2],6] , {|x| x[1] == aParamRec[nPos,1] } )
		If nPos3 > 0
			Loop
		Endif

		nPosObr := aScan( aCadTipo[aIndChk[nXXX,1],3,aIndChk[nXXX,2],7] , {|x| x == aParamRec[nPos,1] } )
		lParObr := If( nPosObr > 0 , .t. , .f. )

		If !Empty(TZ4->TZ4_CAMPOS)
			If !Empty(TZ4->TZ4_PICTUR)
				cPicture := Alltrim(TZ4->TZ4_PICTUR)
			Else
		  		cPicture := Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_PICTURE")
			Endif

			If lVal
				cValid := Alltrim(TZ4->TZ4_VALID)
			Endif

			cValTemp := Space(Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TAMANHO"))
			nTamanho := Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TAMANHO")
			cIni_Pad := nil
			If lIni
				dbSelectArea("TZ8")
				dbSetOrder(1)
				dbSeek(xFilial("TZ8")+Str(nModulo,2)+SubStr(AllTrim(cUsuario),7,15)+cCodInd+TZ4->TZ4_CODPAR)
				If !Empty(TZ8->TZ8_VALOR)
					If Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO") == "D"
						cValTemp := StoD(SubStr(TZ8->TZ8_VALOR,1,nTamanho))
					ElseIf Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO") == "N"
						cValTemp := Val(SubStr(TZ8->TZ8_VALOR,1,nTamanho))
					ElseIf Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO") == "L"
						cValTemp := If(SubStr(TZ8->TZ8_VALOR,1,nTamanho) == ".F.",.f.,.t.)
					ElseIf Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO") == "C"
						cValTemp := SubStr(TZ8->TZ8_VALOR,1,nTamanho)
					Endif
				Else
					If Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO") == "D"
						cValTemp := StoD(Space(8))
					ElseIf Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO") == "N"
						cValTemp := 0
					ElseIf Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO") == "L"
						cValTemp := .F.
					ElseIf Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO") == "C"
						cValTemp := cValTemp
					Endif
				Endif
			Else
				If Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO") == "D"
					cValTemp := StoD(Space(8))
				ElseIf Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO") == "N"
					cValTemp := 0
				ElseIf Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO") == "L"
					cValTemp := .F.
				ElseIf Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO") == "C"
					cValTemp := cValTemp
				Endif
			Endif
			If !Empty(TZ4->TZ4_RELACA)
				cIni_Pad := &(Alltrim(TZ4->TZ4_RELACA))
				If ValType(cIni_Pad) == ValType(cValTemp)
					cValTemp := cIni_Pad
				Else
					MsgInfo(STR0087 + " " + aParamRec[nPos,1]) //"Problema no inicializador padrão do tipo de parâmetro:"
				Endif
			Endif
			aAdd( aTemp , { 1,;									//1.Tipo CAMPO
							Alltrim(TZ4->TZ4_DESCRI),; //2.Descricao
							Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO"),;						//3.Tipo Var
							Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TAMANHO"),;					//4.Tamanho
							Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_DECIMAL"),;					//5.Decimal
							cPicture,;			//6.Picture
							TZ4->TZ4_TABELA,;					//7.Tabela
							Alltrim(TZ4->TZ4_CAMPOS);			//8.Campo
							,/*9.oSay*/,/*10.oGet*/,cValTemp,aParamRec[nPos,1],Alltrim(TZ4->TZ4_F3),.f.,lParObr,Space(10),{},cValid } )
							//11.Valor,12.TZ4_CODPAR, 13.TZ4_F3,14.FIXO OU NAO, 15.Obrigatorio 16. Opcoes 17. Clone Opcoes 18. Valid
		Else
			cValTemp := Space(TZ4->TZ4_TAMANH)
			nTamanho := TZ4->TZ4_TAMANH
			cIni_Pad := nil
			If lVal
				cValid := Alltrim(TZ4->TZ4_VALID)
			Endif
			If lIni
				dbSelectArea("TZ8")
				dbSetOrder(1)
				dbSeek(xFilial("TZ8")+Str(nModulo,2)+SubStr(AllTrim(cUsuario),7,15)+cCodInd+TZ4->TZ4_CODPAR)
				If !Empty(TZ8->TZ8_VALOR)
					If TZ4->TZ4_TIPO == "4"
						cValTemp := StoD(SubStr(TZ8->TZ8_VALOR,1,nTamanho))
					ElseIf TZ4->TZ4_TIPO == "2"
						cValTemp := Val(SubStr(TZ8->TZ8_VALOR,1,nTamanho))
					ElseIf TZ4->TZ4_TIPO == "3"
						cValTemp := If(SubStr(TZ8->TZ8_VALOR,1,nTamanho) == ".F.",.F.,.T.)
					ElseIf Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO") == "1"
						cValTemp := SubStr(TZ8->TZ8_VALOR,1,nTamanho)
					Endif

					aTipoTZ4 := {}
					If TZ4->TZ4_TIPO == "6"
						aTipoTZ4 := fRetCombo(Alltrim(TZ4->TZ4_OPCOES))
						nIND := aScan(aTipoTZ4,{|x| Upper(Substr(x,1,1)) == Substr(TZ8->TZ8_VALOR,1,1)})
						If nIND > 0
							cValTemp := Substr(TZ8->TZ8_VALOR,1,1)
						Else
							cValTemp := " "
						Endif
					Endif
				Else
					If TZ4->TZ4_TIPO == "4"
						cValTemp := StoD(Space(8))
					ElseIf TZ4->TZ4_TIPO == "2"
						cValTemp := 0
					ElseIf TZ4->TZ4_TIPO == "3"
						cValTemp := .F.
					ElseIf Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO") == "1"
						cValTemp := cValTemp
					Endif

					aTipoTZ4 := {}
					If TZ4->TZ4_TIPO == "6"
						aTipoTZ4 := fRetCombo(Alltrim(TZ4->TZ4_OPCOES))
					Endif
				Endif
			Else
				If TZ4->TZ4_TIPO == "4"
					cValTemp := StoD(Space(8))
				ElseIf TZ4->TZ4_TIPO == "2"
					cValTemp := 0
				ElseIf TZ4->TZ4_TIPO == "3"
					cValTemp := .F.
				ElseIf Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO") == "1"
					cValTemp := cValTemp
				Endif

				aTipoTZ4 := {}
				If TZ4->TZ4_TIPO == "6"
					aTipoTZ4 := fRetCombo(Alltrim(TZ4->TZ4_OPCOES))
				Endif
			Endif
			If !Empty(TZ4->TZ4_RELACA)
				cIni_Pad := &(Alltrim(TZ4->TZ4_RELACA))
				If ValType(cIni_Pad) == ValType(cValTemp)
					cValTemp := cIni_Pad
				Else
					MsgInfo(STR0087 + " " + aParamRec[nPos,1]) //"Problema no inicializador padrão do tipo de parâmetro:"
				Endif
			Endif
			aAdd( aTemp , { 2,;									//1.Tipo VARIAVEL
							Alltrim(TZ4->TZ4_DESCRI),; //2.Descricao
							TZ4->TZ4_TIPO,;						//3.Tipo Var
							TZ4->TZ4_TAMANH,;					//4.Tamanho
							TZ4->TZ4_DECIMA,;					//5.Decimal
							Alltrim(TZ4->TZ4_PICTUR),;			//6.Picture
											,;					//7.Tabela
											;					//8.Campo
							,/*9.oSay*/,/*10.oGet*/,cValTemp,aParamRec[nPos,1],Alltrim(TZ4->TZ4_F3),.f.,lParObr,Alltrim(TZ4->TZ4_OPCOES),aClone(aTipoTZ4),cValid } )
							//11.Valor,12.TZ4_CODPAR, 13.TZ4_F3,14.FIXO OU NAO, 15.Obrigatorio 16. Opcoes 17. Clone Opcoes 18. Valid
		Endif
	Next nPos

	For nPos := 1 To Len(aCadTipo[aIndChk[nXXX,1],3,aIndChk[nXXX,2],6])
		cCod_Par := aCadTipo[aIndChk[nXXX,1],3,aIndChk[nXXX,2],6,nPos,1]
		If lIni
			dbSelectArea("TZ8")
			dbSetOrder(1)
			dbSeek(xFilial("TZ8")+Str(nModulo,2)+SubStr(AllTrim(cUsuario),7,15)+cCodInd+TZ4->TZ4_CODPAR)
			If !Empty(TZ8->TZ8_VALOR)
				cValTemp := TZ8->TZ8_VALOR
			Else
				cValTemp := aCadTipo[aIndChk[nXXX,1],3,aIndChk[nXXX,2],6,nPos,3]
			Endif
		Else
			cValTemp := aCadTipo[aIndChk[nXXX,1],3,aIndChk[nXXX,2],6,nPos,3]
		Endif

		dbSelectArea("TZ4")
		dbSetOrder(1)
		dbSeek(xFilial("TZ4")+Str(nModulo,2)+cCod_Par)

		nPosObr := aScan( aCadTipo[aIndChk[nXXX,1],3,aIndChk[nXXX,2],7] , {|x| x == cCod_Par } )
		lParObr := If( nPosObr > 0 , .t. , .f. )

		If !Empty(TZ4->TZ4_CAMPOS)
			If !Empty(TZ4->TZ4_PICTUR)
				cPicture := Alltrim(TZ4->TZ4_PICTUR)
			Else
		  		cPicture := Alltrim(Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_PICTURE"))
			Endif

			If lVal
				cValid := Alltrim(TZ4->TZ4_VALID)
			Endif

			aAdd( aTemp , { 1,;									//1.Tipo CAMPO
							Alltrim(TZ4->TZ4_DESCRI),; //2.Descricao
							Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TIPO"),;						//3.Tipo Var
							Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_TAMANHO"),;					//4.Tamanho
							Posicione("SX3",2,TZ4->TZ4_CAMPOS,"X3_DECIMAL"),;					//5.Decimal
							cPicture,;			//6.Picture
							TZ4->TZ4_TABELA,;					//7.Tabela
							Alltrim(TZ4->TZ4_CAMPOS);			//8.Campo
							,/*9.oSay*/,/*10.oGet*/,cValTemp,cCod_Par,Alltrim(TZ4->TZ4_F3),.t.,lParObr,Space(10),{}, cValid } )
												//11.Valor,12.TZ4_CODPAR, 13.TZ4_F3,14.FIXO OU NAO, 15.Obrigatorio 16. Opcoes 17. Clone Opcoes 18. Valid
		Else
			aTipoTZ4 := {}
			If lIni
				If !Empty(TZ8->TZ8_VALOR)
					If TZ4->TZ4_TIPO == "6"
						aTipoTZ4 := fRetCombo(Alltrim(TZ4->TZ4_OPCOES))
						nIND := aScan(aTipoTZ4,{|x| Upper(Substr(x,1,1)) == Substr(TZ8->TZ8_VALOR,1,1)})
						If nIND > 0
							cValTemp := Substr(TZ8->TZ8_VALOR,1,1)
						Else
							cValTemp := " "
						Endif
					Endif
				Else
					If TZ4->TZ4_TIPO == "6"
						aTipoTZ4 := fRetCombo(Alltrim(TZ4->TZ4_OPCOES))
					Endif
				Endif
			Else
				If TZ4->TZ4_TIPO == "6"
					aTipoTZ4 := fRetCombo(Alltrim(TZ4->TZ4_OPCOES))
				Endif
			Endif

			If lVal
				cValid := Alltrim(TZ4->TZ4_VALID)
			Endif
			aAdd( aTemp , { 2,;									//1.Tipo VARIAVEL
							Alltrim(TZ4->TZ4_DESCRI),; //2.Descricao
							TZ4->TZ4_TIPO,;						//3.Tipo Var
							TZ4->TZ4_TAMANH,;					//4.Tamanho
							TZ4->TZ4_DECIMA,;					//5.Decimal
							Alltrim(TZ4->TZ4_PICTUR),;			//6.Picture
											,;					//7.Tabela
											;					//8.Campo
							,/*9.oSay*/,/*10.oGet*/,cValTemp,cCod_Par,Alltrim(TZ4->TZ4_F3),.t.,lParObr,Alltrim(TZ4->TZ4_OPCOES),aClone(aTipoTZ4),cValid } )
												//11.Valor,12.TZ4_CODPAR, 13.TZ4_F3,14.FIXO OU NAO, 15.Obrigatorio 16. Opcoes 17. Clone Opcoes 18. Valid
		Endif
	Next nPos

	aIndChk[nXXX,3] := aCadTipo[aIndChk[nXXX,1],1]
	aIndChk[nXXX,4] := aCadTipo[aIndChk[nXXX,1],2]
	aIndChk[nXXX,5] := aClone(aTemp)
Next nXXX

For nXXX := Len(aIndChk) To 1 Step -1
	If Empty(aIndChk[nXXX,3])
		aDel(aIndChk, nXXX )
		aSize(aIndChk, Len(aIndChk)-1 )
	Endif
Next nXXX

If Len(aIndChk) == 0
	MsgInfo(STR0034) //"Nenhum indicador foi selecionado para seleção de parâmetros."
	SetVarNameLen( nVarAum )
	Return {.f.,{},.f.}
Endif

//Carregando Classificação dos Indicadores
nTipo := 9999999
For nInd1 := 1 To Len(aIndChk)
	If nTipo <> aIndChk[nInd1,1]
		Aadd(aTitles, Alltrim(aIndChk[nInd1,4]) )
		Aadd(aPages,"Header "+Alltrim(Str(Len(aTitles),6)))
		nTipo := aIndChk[nInd1,1]
	Endif
Next nInd1
nControl := Len(aTitles)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³TELA DE PARAMETROS                                                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOpcb := 0
Private cValidPar := ""
DEFINE MSDIALOG oDlgP Title STR0037 From 0,0 To ( (nAltura /5.8)*4 ), ( (nLargura/5)*2 ) Of oMainWnd PIXEL //"Configuração de Parâmetros"
//oDlgP:LESCCLOSE := .f.
oPnlBottom := TPanel():New(00,00,,oDlgP,,,,,,,20,.F.,.F.)
oPnlBottom:Align := CONTROL_ALIGN_BOTTOM
oPnlPai := TPanel():New(00,00,,oDlgP,,,,,,56,41,.F.,.F.)
oPnlPai:Align := CONTROL_ALIGN_ALLCLIENT
oFolder := TFolder():New(0,0,aTitles,aPages,oPnlPai,,,,.T.,, 200 , 217 )
oFolder:Align := CONTROL_ALIGN_ALLCLIENT

nTipo := 9999999
nIndi := 9999999
nContFold := 0
For nXX := 1 To Len(aIndChk)

	If nTipo <> aIndChk[nXX,1]
		nContFold++

		oFolder:aDialogs[nContFold]:oFont := oDlgP:oFont
		nLinObj := 5
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Cria um scrollbox para cada tipo de indicador  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		//@ 0,0 SCROLLBOX oScroll VERTICAL SIZE 195,280 OF oFolder:aDialogs[nContFold]// BORDER
		oScroll := TScrollBox():New(oFolder:aDialogs[nContFold],01,01,(nAltura/4),(nLargura/5.2), .T., .T., .T.)
	Endif

	If nTipo <> aIndChk[nXX,1] .or. nIndi <> aIndChk[nXX,2]
		If nTipo == aIndChk[nXX,1]
			nLinObj += 20
		Endif
		@ nLinObj, 5 TO nLinObj+(Len(aIndChk[nXX,5])*11)+15, ( nLargura ) / 6 LABEL Alltrim(aCadTipo[aIndChk[nXX,1],3,aIndChk[nXX,2],2]) OF oScroll PIXEL
	Endif

	For nYY := 1 to Len(aIndChk[nXX,5])
		If nYY == 1
			nLinObj += 11
		Else
			nLinObj += 11
		Endif

		//Cria campo para receber o valor do parametro
		nPosA := aScan( aCadTipo[aIndChk[nXX,1],3,aIndChk[nXX,2],5] , {|x| x[12] == aIndChk[nXX,5,nYY,12] } )
		If nPosA > 0
			aIndChk[nXX,5,nYY,11] := aCadTipo[aIndChk[nXX,1],3,aIndChk[nXX,2],5,nPosA,11]
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Titulo Parametro                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aIndChk[nXX,5,nYY,15]
	 		@ nLinObj, 010 SAY aIndChk[nXX,5,nYY,9] Prompt Space(40) PIXEL OF oScroll Font oFont Color CLR_HBLUE
	 		If !(aIndChk[nXX,5,nYY,3] $ "L/3")
		 		If Empty(cValidPar)
			 		cValidPar += "Empty(_aIndChk["+Str(nXX)+",5,"+Str(nYY)+",11])"
			 	Else
				 	cValidPar += " .or. Empty(_aIndChk["+Str(nXX)+",5,"+Str(nYY)+",11])"
			 	Endif
			Endif
		Else
			@ nLinObj, 010 SAY aIndChk[nXX,5,nYY,9] Prompt Space(40) PIXEL OF oScroll Font oFont
		Endif

 		Private &(Alltrim(aCadTipo[aIndChk[nXX,1],3,aIndChk[nXX,2],1])+"_"+AllTrim(aIndChk[nXX,5,nYY,12])) := aIndChk[nXX,5,nYY,11]
		aIndChk[nXX,5,nYY,9]:bSetGet := ;
			&("{|u| If(pCount() == 0, aIndChk["+Str(nXX)+",5,"+Str(nYY)+",2], aIndChk["+Str(nXX)+",5,"+Str(nYY)+",2]:=u)}")
    	aIndChk[nXX,5,nYY,9]:SetText(aIndChk[nXX,5,nYY,2])

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Resultado Parametro                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aIndChk[nXX,5,nYY,3] == "L" .or. aIndChk[nXX,5,nYY,3] == "3"
			aIndChk[nXX,5,nYY,10] := TCheckBox():New(nLinObj,150,"",,oScroll,10,10,,,,,,,,.T.)
		ElseIf aIndChk[nXX,5,nYY,3] == "6"
			aIndChk[nXX,5,nYY,10] := TComboBox():New(nLinObj,150,,aIndChk[nXX,5,nYY,17],60,/*60*/10,oScroll,,,,,,.T.,,,,/*bWhen*/,,,,;
							Alltrim(aCadTipo[aIndChk[nXX,1],3,aIndChk[nXX,2],1])+"_"+AllTrim(aIndChk[nXX,5,nYY,12]) /*ReadVar*/)
		Else
			aIndChk[nXX,5,nYY,10] := TGet():New( nLinObj, 150, ,;
							oScroll, 60, 7, aIndChk[nXX,5,nYY,6] , , , , , , ,.T., , ,/*bWhen*/, , , ,/*lReadOnly*/ , , , ;
	 					      Alltrim(aCadTipo[aIndChk[nXX,1],3,aIndChk[nXX,2],1])+"_"+AllTrim(aIndChk[nXX,5,nYY,12]) /*ReadVar*/,,,,.T.)
	 		If !Empty(aIndChk[nXX,5,nYY,13])
				aIndChk[nXX,5,nYY,10]:cF3 := aIndChk[nXX,5,nYY,13]
			Endif
		Endif
		cVarTexto := "aIndChk["+Str(nXX)+",5,"+Str(nYY)+",11]"
    	aIndChk[nXX,5,nYY,10]:bSetGet := ;
    		&("{|u| If(pCount() == 0, "+cVarTexto+", "+cVarTexto+":=u ) , If(pCount() == 0, "+Alltrim(aCadTipo[aIndChk[nXX,1],3,aIndChk[nXX,2],1])+"_"+AllTrim(aIndChk[nXX,5,nYY,12])+", "+Alltrim(aCadTipo[aIndChk[nXX,1],3,aIndChk[nXX,2],1])+"_"+AllTrim(aIndChk[nXX,5,nYY,12])+":=u )}")
    	If nTipoPar == 1 .AND. Len(aIndChk[nXX,5,nYY]) > 17
	    	aIndChk[nXX,5,nYY,10]:bValid := &('{|| If(NG001VALID("'+aIndChk[nXX,5,nYY,18]+'","'+AllTrim(aIndChk[nXX,5,nYY,12])+'"),NgRepInd1("'+aIndChk[nXX,5,nYY,12]+'",@aIndChk,'+Str(nXX)+','+Str(nYY)+'),.F. ) }')//&("{|| NgRepInd1('"+aIndChk[nXX,5,nYY,12]+"',@aIndChk,"+Str(nXX)+","+Str(nYY)+") }")
    	Endif

    	If aIndChk[nXX,5,nYY,14]
	    	aIndChk[nXX,5,nYY,10]:Disable()
    	Endif

	Next nYY

	nTipo := aIndChk[nXX,1]
	nIndi := aIndChk[nXX,2]

Next nXX
If nTipoPar == 1
	@ 6,010 checkbox oCheck Var lRepliPar Prompt "" Size 150,16 OF oPnlBottom PIXEL
	@ 3,020 Say STR0038 PIXEL OF oPnlBottom //"Replicar o conteúdo informado para os parâmetros"
	@ 8,020 Say STR0039 PIXEL OF oPnlBottom //"de todos os indicadores apresentados nesta janela?"
Endif
@ 4,180 Button oBtnClc Prompt STR0023 Size 40,11 Of oPnlBottom Pixel Action {|| nOpcb := 3, If(!NGIN001OK(cValidPar,aIndChk),nOpcb := 0,oDlgP:End())} //"Calcular"
oBtnClc:cToolTip := STR0040 //"Calcula os indicadores"

ACTIVATE MSDIALOG oDlgP ON INIT EnchoiceBar(oDlgP,{|| nOpcb := 1, If(!NGIN001OK(cValidPar,aIndChk),nOpcb := 0,oDlgP:End())},;
												  {|| nOpcb := 2,oDlgP:End()}) CENTERED

If lIni
	If nOpcb == 1 .or. nOpcb == 3
		For nXX := 1 To Len(aIndChk)
			cCodInd := aCadTipo[aIndChk[nXX,1],3,aIndChk[nXX,2],1]
			For nYY := 1 To Len(aIndChk[nXX,5])
				dbSelectArea("TZ8")
				dbSetOrder(1)
				If dbSeek(xFilial("TZ8")+Str(nModulo,2)+SubStr(AllTrim(cUsuario),7,15)+cCodInd+aIndChk[nXX,5,nYY,12])
					RecLock("TZ8",.F.)
					If aIndChk[nXX,5,nYY,3] $ "4/D"
						TZ8->TZ8_VALOR  := DTOS(aIndChk[nXX,5,nYY,11])
					ElseIf aIndChk[nXX,5,nYY,3] $ "3/L"
						TZ8->TZ8_VALOR  := If(aIndChk[nXX,5,nYY,11],".T.",".F.")
					ElseIf aIndChk[nXX,5,nYY,3] $ "2/N"
						TZ8->TZ8_VALOR  := AllTrim(Str(aIndChk[nXX,5,nYY,11]))
					Else
						TZ8->TZ8_VALOR  := aIndChk[nXX,5,nYY,11]
					Endif
					TZ8->(MsUnLock())
				Else
					RecLock("TZ8",.T.)
					TZ8->TZ8_FILIAL := xFilial("TZ8")
					TZ8->TZ8_MODULO := Str(nModulo,2)
					TZ8->TZ8_USER	 := SubStr(AllTrim(cUsuario),7,15)
					TZ8->TZ8_IND	 := cCodInd
					TZ8->TZ8_CODPAR := aIndChk[nXX,5,nYY,12]
					If aIndChk[nXX,5,nYY,3] $ "4/D"
						TZ8->TZ8_VALOR  := DTOS(aIndChk[nXX,5,nYY,11])
					ElseIf aIndChk[nXX,5,nYY,3] $ "3/L"
						TZ8->TZ8_VALOR  := If(aIndChk[nXX,5,nYY,11],".T.",".F.")
					ElseIf aIndChk[nXX,5,nYY,3] $ "2/N"
						TZ8->TZ8_VALOR  := AllTrim(Str(aIndChk[nXX,5,nYY,11]))
					Else
						TZ8->TZ8_VALOR  := aIndChk[nXX,5,nYY,11]
					Endif
					TZ8->(MsUnLock())
				Endif
			Next nYY
		Next nXX
	Endif
Endif

If nTipoPar == 2 .and. (nOpcb == 0 .or. nOpcb == 2) .and. Len(aIndChk) == 1
	lIndNG[aIndChk[1,1],aIndChk[1,2]] := .f.
	//oIndNG[aIndChk[1,1],aIndChk[1,2],3]:Disable()
Endif

If nOpcb != 1 .and. nOpcb != 3
	SetVarNameLen( nVarAum )
	Return {.f.,{},.f.}
Endif

SetVarNameLen( nVarAum )
lSimParam := .T.
Return {.t.,aIndChk,(nOpcb==3)}

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NgRepInd1³ Autor ³ Denis Hyroshi Souza   ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Replica o conteudo do parametro para outros indicadores    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NgRepInd1(cCodPar,aIndChk,_nXX_,_nYY_)
Local nInd,nInd1

If lRepliPar
	For nInd := 1 To Len(aIndChk)
		For nInd1 := 1 To Len(aIndChk[nInd,5])
			If cCodPar == aIndChk[nInd,5,nInd1,12] .and. !aIndChk[nInd,5,nInd1,14]
				aIndChk[nInd,5,nInd1,11] := aIndChk[_nXX_,5,_nYY_,11]
				&(Alltrim(aCadTipo[aIndChk[nInd,1],3,aIndChk[nInd,2],1])+"_"+AllTrim(aIndChk[nInd,5,nInd1,12])) := aIndChk[_nXX_,5,_nYY_,11]
			Endif
		Next nInd1
	Next nInd
Endif

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NgIndParm³ Autor ³ Denis Hyroshi Souza   ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Busca os parametros das variaveis                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NGIN001OK(cValidPar,aIndChk)

Private _aIndChk := aClone(aIndChk)

If &(cValidPar)
	Help(" ",1,"OBRIGAT",,Space(40),3,0)
	Return .f.
Endif

Return .t.

//-------------------------------------------------------------------
/*/{Protheus.doc} NgIndParm
Busca os parametros das variaveis
@author  Denis Hyroshi Souza
@since   10/07/2008
@version p12

@param cForm, Caractere, Fórmula do Indicador
@param cCodIndic, Caractere, Código do Indicador
@param lComuns, Lógico, se pega apenas os parametros em comum dos mntv

/*/
//-------------------------------------------------------------------
Function NgIndParm(cForm , cCodIndic, lComuns)
Local aRet1    := {}
Local aRet2    := {}
Local aParVar  := {}
Local aTabVar  := {}
Local cMemoTmp := cForm
Local cMemoRes := ""
Local nPos1, nPos2, nPos3

Default lComuns := .T.

// Verifica se pode executar a função, pois ela função pode ser chamada por outro fonte
If Type("aIndParm") <> "A" .Or. Type("aVarParm") <> "A" // Verifica existência de variáveis PRIVATEs obrigatórias
	Return
EndIf

//---------------------------------------------------------------
// Varre formula separando as @variaveis@
//---------------------------------------------------------------
While !Empty(cMemoTmp)
	nPos1 := AT("@",cMemoTmp)
	If nPos1 > 0
		nPos2 := AT("@", Substr(cMemoTmp,nPos1+1) )
		If nPos2 > 0
			aAdd( aTabVar, Substr( cMemoTmp , nPos1+1 , nPos2-1 ) )
			cMemoRes += Substr( cMemoTmp , 1 , nPos1 - 1) + '1'
			cMemoTmp := Substr( cMemoTmp , nPos1+nPos2+1 ) // Pega a partir do fecha variavel '@XXXXXXXX@'
		Else
			Exit
		Endif
	Else
		cMemoRes += cMemoTmp
		Exit
	EndIf
End

//----------------------------------------------------------------
// Busca os parametros das variaveis
//----------------------------------------------------------------
For nPos1 := 1 To Len(aTabVar)

	aParVar := {}
	aRet1   := {}
	aRetUtil := {}
	dbSelectArea("TZ3")
	dbSetOrder(1)
	dbSeek(xFilial("TZ3")+Str(nModulo,2)+PadR(aTabVar[nPos1],12))
	While !Eof() .and. xFilial("TZ3")+PadR(aTabVar[nPos1],12) == TZ3->TZ3_FILIAL+TZ3->TZ3_CODVAR .and. TZ3->TZ3_MODULO == Str(nModulo,2)
		cDescPar := NGSEEK("TZ4",Str(nModulo,2)+TZ3->TZ3_CODPAR,1,"TZ4->TZ4_DESCRI")
		aAdd( aRet1   , { TZ3->TZ3_CODVAR , TZ3->TZ3_ORDEM , TZ3->TZ3_CODPAR , cDescPar } )
		aAdd( aParVar , { TZ3->TZ3_CODPAR , cDescPar } )
		dbSelectArea("TZ3")
		dbSkip()
	End
	If nPos1 == 1
		aRet2 := aClone(aParVar)
	Else
	 	If lComuns
			For nPos2 := Len(aRet2) To 1 Step -1
				nPos3 := aScan( aParVar, {|x| x[1] == aRet2[nPos2,1] } )
				If nPos3 == 0
					aDel(aRet2, nPos2 )
					aSize(aRet2, Len(aRet2)-1 )
				Endif
			Next nPos2
		Else
			For nPos2 := 1 to Len(aParVar)
				nPos3 := aScan( aRet2, {|x| x[1] == aParVar[nPos2,1] } )
				If nPos3 == 0
					aAdd( aRet2, aParVar[nPos2] )
				Endif
			Next
		EndIf
	Endif

	nPos3 := aScan( aVarParm, {|x| x[1] == PadR(aTabVar[nPos1],12) } )
	If nPos3 == 0
		For nPos2 := 1 To Len(aRet1)
			aAdd( aVarParm, aRet1[nPos2] )
		Next nPos2
	Endif

Next nPos1


aAdd( aIndParm , {cCodIndic , aRet2 } )
aSort( aVarParm ,,, { |x,y| x[1]+x[2] < y[1]+y[2] } )

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} fNgCalc
Calcula os indicadores
@author  Denis Hyroshi Souza
@since   10/07/2008
@version p12
/*/
//-------------------------------------------------------------------
Static Function fNgCalc()

	Local nXXX, nYYY, nX
	Local nCont    := 0
	Local cMsg     := ""
	Local nQtdeInd := 0
	Local aParams  := {}
	Local nPosR    := 0
	Local nValTemp := 0
	Local oNGKPI
	Local xValTemp
	Local lRPORel17:= GetRPORelease() <= '12.1.017'

	Private cIndAtu := ""

	If !lRPORel17 // Caso seja uma versão maior que 12.1.17
		oNGKPI := NGKPI():New()

		// Determina a operação selecionada da Classe.
		oNGKPI:setOperation(4)

	EndIf

	If !lSimParam
		MsgInfo(STR0088, STR0042) // "Preencha os parâmetros antes de calcular." ## "Atenção"
		Return .F.
	Endif

	For nXXX := 1 to Len(lIndNG)
		For nYYY := 1 to Len(lIndNG[nXXX])
			nQtdeInd++
		Next nYYY
	Next nXXX

	ProcRegua(nQtdeInd)

	// Verifica se a formula esta correta(sintaxe)
	For nXXX := 1 to Len(lIndNG)
		For nYYY := 1 to Len(lIndNG[nXXX])
			IncProc()
			If ValType(lIndNG[nXXX,nYYY]) == "L"
				If lIndNG[nXXX,nYYY]
					cIndAtu := aCadTipo[nXXX,3,nYYY,1] // Alimenta a veriável de verificacao do indicador (Metrorec)
					// Verifica se a formula está correta
					If !NGCHKFORM( aCadTipo[nXXX,3,nYYY,3] , .f.)
						// Se a formula estiver com erro
						lIndNG[nXXX,nYYY] := .f.
						// oIndNG[nXXX,nYYY,3]:Disable()
						nCont++
						cMsg += Chr(13) + Chr(10) + "-" + Alltrim(aCadTipo[nXXX,3,nYYY,2]) + " ("+Alltrim(aCadTipo[nXXX,2])+")"
					Else
						//--------------------
						// Histórico
						//--------------------
						// Inicializa o Processo de Armazenar os dados do Cálculo
						NGI6MVHIST(2)

						//--------------------
						// Calcula a formula
						//--------------------
						If !lRPORel17 // Caso seja release maior que 12.1.17

							For nX := 1 To Len(aCadTipo[nXXX][3][nYYY][5])
								aAdd( aParams, { Alltrim(aCadTipo[nXXX][3][nYYY][5][nX][12]), aCadTipo[nXXX][3][nYYY][5][nX][11]} )
							Next nX

							// Cálculo do Indicador
							oNGKPI:setIndParams(aParams) // Carrega os valores para a classe

							xValTemp :=  oNGKPI:getKPI(cIndAtu)  // Executa o cálculo do indicador

							// Exibe Error da Classe se Houver.
							If xValTemp == Nil
								oNGKPI:showHelp()
								xValTemp := 0
							EndIf

							nIndNG[nXXX,nYYY] := xValTemp
						Else
							//Calcula a formula
							nValTemp := fCalcForm( aCadTipo[nXXX,3,nYYY,3] , nXXX , nYYY )
							If ValType(nValTemp) <> "N"
								Return .f.
							Endif
							If aCadTipo[nXXX,3,nYYY,14] == "2"
								nIndNG[nXXX,nYYY] := Alltrim(NTOH(nValTemp))
							Else
								nIndNG[nXXX,nYYY] := nValTemp
							Endif

						Endif

						oIndNG[nXXX,nYYY,3]:cToolTip := NgInd1Det(nXXX,nYYY,aCadTipo[nXXX,3,nYYY,5])

						//--------------------
						// Histórico
						//--------------------
						// Confirma o Histórico
						NGI6MVHIST(3, .T.)
						// Armazena o Resultado do Cálculo
						If !lRPORel17 // Caso seja release maior que 12.1.17
							NGI6HISTRE(aCadTipo[nXXX,3,nYYY,1]/*cFormula*/, IIf(aCadTipo[nXXX,3,nYYY,14] == "2", HtoN(xValTemp), xValTemp)/*nResultado*/)
						Else
							NGI6HISTRE(aCadTipo[nXXX,3,nYYY,1]/*cFormula*/, nValTemp/*nResultado*/)
						EndIf
					Endif
					If aCadTipo[nXXX,3,nYYY,14] == "2"
						oIndNG[nXXX,nYYY,3]:SetText( nIndNG[nXXX,nYYY] + aCadTipo[nXXX,3,nYYY,13] )
					Else
						oIndNG[nXXX,nYYY,3]:SetText( Alltrim( TransForm(nIndNG[nXXX,nYYY],aCadTipo[nXXX,3,nYYY,9]) )  + aCadTipo[nXXX,3,nYYY,13] )
					Endif
					If ValType(oIndNG[nXXX,nYYY,7]) == "O"
						oIndNG[nXXX,nYYY,7]:SetValue(nIndNG[nXXX,nYYY])
					EndIf

					// Verifica se está dentro dos padrões
					lIndPadrao := .t.
					If aCadTipo[nXXX,3,nYYY,10] == "5" .and. aCadTipo[nXXX,3,nYYY,11] <> nil .and. aCadTipo[nXXX,3,nYYY,12] <> nil
						If aCadTipo[nXXX,3,nYYY,14] != "2"
							If nIndNG[nXXX,nYYY] < aCadTipo[nXXX,3,nYYY,11] .or. nIndNG[nXXX,nYYY] > aCadTipo[nXXX,3,nYYY,12]
								lIndPadrao := .f.
							Endif
						Else
							If HtoM(nIndNG[nXXX,nYYY]) < HtoM(aCadTipo[nXXX,3,nYYY,11]) .or. HtoM(nIndNG[nXXX,nYYY]) > HtoM(aCadTipo[nXXX,3,nYYY,12])
								lIndPadrao := .f.
							Endif
						Endif
					ElseIf aCadTipo[nXXX,3,nYYY,10] == "6" .and. aCadTipo[nXXX,3,nYYY,11] <> nil .and. aCadTipo[nXXX,3,nYYY,12] <> nil
						If aCadTipo[nXXX,3,nYYY,14] != "2"
							If nIndNG[nXXX,nYYY] >= aCadTipo[nXXX,3,nYYY,11] .and. nIndNG[nXXX,nYYY] <= aCadTipo[nXXX,3,nYYY,12]
								lIndPadrao := .f.
							Endif
						Else
							If HtoM(nIndNG[nXXX,nYYY]) >= HtoM(aCadTipo[nXXX,3,nYYY,11]) .and. HtoM(nIndNG[nXXX,nYYY]) <= HtoM(aCadTipo[nXXX,3,nYYY,12])
								lIndPadrao := .f.
							Endif
						Endif
					ElseIf aCadTipo[nXXX,3,nYYY,10] == "1" .and. aCadTipo[nXXX,3,nYYY,11] <> nil
						If aCadTipo[nXXX,3,nYYY,14] != "2"
							If nIndNG[nXXX,nYYY] <= aCadTipo[nXXX,3,nYYY,11]
								lIndPadrao := .f.
							Endif
						Else
							If HtoM(nIndNG[nXXX,nYYY]) <= HtoM(aCadTipo[nXXX,3,nYYY,11])
								lIndPadrao := .f.
							Endif
						Endif
					ElseIf aCadTipo[nXXX,3,nYYY,10] == "2" .and. aCadTipo[nXXX,3,nYYY,11] <> nil
						If aCadTipo[nXXX,3,nYYY,14] != "2"
							If nIndNG[nXXX,nYYY] < aCadTipo[nXXX,3,nYYY,11]
								lIndPadrao := .f.
							Endif
						Else
							If HtoM(nIndNG[nXXX,nYYY]) < HtoM(aCadTipo[nXXX,3,nYYY,11])
								lIndPadrao := .f.
							Endif
						Endif
					ElseIf aCadTipo[nXXX,3,nYYY,10] == "3" .and. aCadTipo[nXXX,3,nYYY,11] <> nil
						If aCadTipo[nXXX,3,nYYY,14] != "2"
							If nIndNG[nXXX,nYYY] >= aCadTipo[nXXX,3,nYYY,11]
								lIndPadrao := .f.
							Endif
						Else
							If HtoM(nIndNG[nXXX,nYYY]) >= HtoM(aCadTipo[nXXX,3,nYYY,11])
								lIndPadrao := .f.
							Endif
						Endif
					ElseIf aCadTipo[nXXX,3,nYYY,10] == "4" .and. aCadTipo[nXXX,3,nYYY,11] <> nil
						If aCadTipo[nXXX,3,nYYY,14] != "2"
							If nIndNG[nXXX,nYYY] > aCadTipo[nXXX,3,nYYY,11]
								lIndPadrao := .f.
							Endif
						Else
							If HtoM(nIndNG[nXXX,nYYY]) > HtoM(aCadTipo[nXXX,3,nYYY,11])
								lIndPadrao := .f.
							Endif
						Endif
					ElseIf aCadTipo[nXXX,3,nYYY,10] == "7" .and. aCadTipo[nXXX,3,nYYY,11] <> nil
						If aCadTipo[nXXX,3,nYYY,14] != "2"
							If nIndNG[nXXX,nYYY] <> aCadTipo[nXXX,3,nYYY,11]
								lIndPadrao := .f.
							Endif
						Else
							If HtoM(nIndNG[nXXX,nYYY]) <> HtoM(aCadTipo[nXXX,3,nYYY,11])
								lIndPadrao := .f.
							Endif
						Endif
					ElseIf aCadTipo[nXXX,3,nYYY,10] == "8" .and. aCadTipo[nXXX,3,nYYY,11] <> nil
						If aCadTipo[nXXX,3,nYYY,14] != "2"
							If nIndNG[nXXX,nYYY] == aCadTipo[nXXX,3,nYYY,11]
								lIndPadrao := .f.
							Endif
						Else
							If HtoM(nIndNG[nXXX,nYYY]) == HtoM(aCadTipo[nXXX,3,nYYY,11])
								lIndPadrao := .f.
							Endif
						Endif
					Endif

					If lIndPadrao
						oIndNG[nXXX,nYYY,3]:nClrText := CLR_BLACK
						oIndNG[nXXX,nYYY,6] := 3
					Else
						oIndNG[nXXX,nYYY,3]:nClrText := CLR_HRED
						oIndNG[nXXX,nYYY,6] := 4
					Endif
					oIndNG[nXXX,nYYY,3]:Refresh()
				Endif
			Endif
		Next nYYY
	Next nXXX

	If !lRPORel17 // Caso seja release maior que 12.1.17
		// Elimina o objeto da classe da memória.
		oNGKPI:Free()
	EndIf

	If nCont == 1
		MsgInfo(STR0041+cMsg,STR0042) // "O seguinte indicador está com inconsistência na fórmula e não será calculado."###"Atenção"
	ElseIf nCont > 1
		MsgInfo(STR0043+cMsg,STR0042) // "Os seguintes indicadores estão com inconsistências na fórmula e não serão calculados."###"Atenção"
	Endif

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fCalcForm³ Autor ³ Denis Hyroshi Souza   ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Executa a formula do indicador                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fCalcForm( cTxtFormula , nXXX , nYYY )
	Local nRet := 0.000, nRetTemp := 0.000
	Local cMemoTmp := cTxtFormula
	Local cVarNao  := ""
	Local aTabVar  := {}
	Local aTabExp  := {}
	Local lRet     := .t.
	Local aArea    := GetArea()
	Local nPos1,nPos2,nXX,nYY,nVIR

	//Ordenando a variavel aRetFunc pelo codigo CODVAR
	aSort( aRetFunc ,,, { |x,y| x[1] < y[1] } )

	//Ordenando as respostas dos parametros pelo codigo do parametro (CODPAR)
	//aSort( aCadTipo[nXXX,3,nYYY,5] ,,, { |x,y| x[12] < y[12] } )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Varre formula separando e calculando as @variaveis@          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cMemoRes := ""
	While !Empty(cMemoTmp)
		nPos1 := AT("@",cMemoTmp)
		If nPos1 > 0
			nPos2 := AT("@", Substr(cMemoTmp,nPos1+1) )
			If nPos2 > 0
				cCodigoVar := Substr( cMemoTmp , nPos1+1 , nPos2-1 )
				aAdd( aTabVar, cCodigoVar )
				cMemoRes += Substr( cMemoTmp , 1 , nPos1 - 1)

				//Verifica se a Variavel ja foi calculada nas mesma condicoes (numero e resposta de parametros)
				cValorVar := fVarCalc( PadR(cCodigoVar,12) , nXXX , nYYY )

				// Se ja foi calcula, adiciona o valor da variavel calculada na expressao (formula)
				// OU se estiver utilizando o Histórico de Indicadores, obriga a calcular novamente
				If ( ValType(cValorVar) != "N" .And. ValType(cValorVar) != "C" ) .Or. NGI6MVHIST(1)
					//Senão, executa a variavel (funcao) passando os parametros informados
					//Cria a expressao para executar a variavel (funcao)
					dbSelectArea("TZ2")
					dbSetOrder(1)
					dbSeek(xFilial("TZ2")+Str(nModulo,2)+PadR(cCodigoVar,12))
					cExpVar := Alltrim(TZ2->TZ2_FUNCAO)+"("
					If TZ2->TZ2_TIPO == "2"
						cExpVar := "U_"+cExpVar
					Endif
					nVirgula:= 1
					nPosR := aScan( aVarParm, {|x| x[1] == PadR(cCodigoVar,12) } )
					If nPosR > 0
						For nXX := nPosR To Len(aVarParm)
							If aVarParm[nXX,1] <> PadR(cCodigoVar,12)
								Exit
							Endif
							While nVirgula < Val(aVarParm[nXX,2])
								cExpVar += ","
								nVirgula++
							End
							nPosA := aScan( aCadTipo[nXXX,3,nYYY,5], {|x| x[12] == aVarParm[nXX,3] } )
							If nPosA > 0
								If aCadTipo[nXXX,3,nYYY,5,nPosA,3] == "C" .or. aCadTipo[nXXX,3,nYYY,5,nPosA,3] == "1"
									If !Empty(aCadTipo[nXXX,3,nYYY,5,nPosA,11])
										cExpVar += "'"+aCadTipo[nXXX,3,nYYY,5,nPosA,11]+"'"
									Endif
								ElseIf aCadTipo[nXXX,3,nYYY,5,nPosA,3] == "6"
									If !Empty(aCadTipo[nXXX,3,nYYY,5,nPosA,11])
										cExpVar += "'"+aCadTipo[nXXX,3,nYYY,5,nPosA,11]+"'"
									Endif
								ElseIf aCadTipo[nXXX,3,nYYY,5,nPosA,3] == "N" .or. aCadTipo[nXXX,3,nYYY,5,nPosA,3] == "2"
									nTam := aCadTipo[nXXX,3,nYYY,5,nPosA,4]
									nDec := aCadTipo[nXXX,3,nYYY,5,nPosA,5]
									cExpVar += Alltrim( Str( aCadTipo[nXXX,3,nYYY,5,nPosA,11] , nTam , nDec ) )
								ElseIf aCadTipo[nXXX,3,nYYY,5,nPosA,3] == "D" .or. aCadTipo[nXXX,3,nYYY,5,nPosA,3] == "4"
									If !Empty(aCadTipo[nXXX,3,nYYY,5,nPosA,11])
										cExpVar += "STOD('"+DtoS(aCadTipo[nXXX,3,nYYY,5,nPosA,11])+"')"
									Endif
								ElseIf aCadTipo[nXXX,3,nYYY,5,nPosA,3] == "L" .or. aCadTipo[nXXX,3,nYYY,5,nPosA,3] == "3"
									If aCadTipo[nXXX,3,nYYY,5,nPosA,11]
										cExpVar += ".T."
									Else
										cExpVar += ".F."
									Endif
								Endif
							Endif
						Next nXX
					Endif
					cExpVar += ")"

					//Executa a variavel (funcao)
					cValorVar := &(cExpVar)

					If ValType(cValorVar) == "N"
						//Adiciona os resultados no vetor que armazena todos os resultados das variaveis
						aVarTemp := {}
						For nYY := 1 To Len(aCadTipo[nXXX,3,nYYY,5])
							aAdd( aVarTemp , { aCadTipo[nXXX,3,nYYY,5,nYY,12] , aCadTipo[nXXX,3,nYYY,5,nYY,11] } )
						Next nYY
						aAdd( aRetFunc, { PadR(cCodigoVar,12) , aVarTemp , cValorVar } )
					ElseIf ValType(cValorVar) == "C"
						cValorVar := Alltrim(cValorVar)
						nPos2Dot := RAT(":",cValorVar)
						If nPos2Dot == Len(cValorVar)-2 .and. fValHr01(cValorVar)
							//Adiciona os resultados no vetor que armazena todos os resultados das variaveis
							aVarTemp := {}
							For nYY := 1 To Len(aCadTipo[nXXX,3,nYYY,5])
								aAdd( aVarTemp , { aCadTipo[nXXX,3,nYYY,5,nYY,12] , aCadTipo[nXXX,3,nYYY,5,nYY,11] } )
							Next nYY
							aAdd( aRetFunc, { PadR(cCodigoVar,12) , aVarTemp , cValorVar } )
						Else
							cTipoVal := STR0046 //"caracter"
							MsgStop(STR0048+Alltrim(cCodigoVar)+STR0049+cTipoVal+"."+Chr(13) + Chr(10)+; //"A Variavél "###" está retornando um valor tipo "
									STR0050+Alltrim(TZ2->TZ2_FUNCAO)+"(), "+STR0051,STR0042) //"Verificar o retorno da Função " //"pois deverá retornar valor numérico ou hora."###"Atenção"
							Return .f.
						Endif
					Else
						cTipoVal := STR0044 //"não numérico"
						If ValType(cValorVar) == "D"
							cTipoVal := STR0045 //"data"
						ElseIf ValType(cValorVar) == "L"
							cTipoVal := STR0047 //"lógico"
						Endif
						MsgStop(STR0048+Alltrim(cCodigoVar)+STR0049+cTipoVal+"."+Chr(13) + Chr(10)+; //"A Variavél "###" está retornando um valor tipo "
								STR0050+Alltrim(TZ2->TZ2_FUNCAO)+"(), "+STR0051,STR0042) //"Verificar o retorno da Função " //"pois deverá retornar valor numérico ou hora."###"Atenção"
						Return .f.
					Endif
				Endif

				//Adiciona valor calculado na expressao
				If ValType(cValorVar) == "C"
					cValorVar := HTON(cValorVar)
				Endif
				cMemoRes += Alltrim(Str(cValorVar))
				cMemoTmp := Substr( cMemoTmp , nPos1+nPos2+1 ) //Pega a partir do fecha variavel '@XXXXXXXX@'
			Else
				Exit
			Endif
		Else
			cMemoRes += cMemoTmp
			Exit
		EndIf
	End

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Calcula a expressao                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cMemoRes := StrTran(cMemoRes, "#" , "" )
	nRet := &(cMemoRes)
	If ExistBlock("NGIND00B") .and. ValType(nRet) == "N"
		nRetTemp := ExecBlock("NGIND00B",.F.,.F.,{nRet,cIndAtu,cTxtFormula,aRetFunc,nXXX,nYYY,aCadTipo})
		If ValType(nRetTemp) == "N"
			nRet := nRetTemp
		Endif
	Endif
	RestArea(aArea)

Return nRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fVarCalc ³ Autor ³ Denis Hyroshi Souza   ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se a Variavel ja foi calculado com os mesmos      ³±±
±±³          ³ parametros e conteudo de parametros                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fVarCalc( cCodigoVar , nXXX , nYYY )
Local nXX,nYY,nPosR
Local lFoiCalc := .f.
Local cValorVar := nil

nPosR := aScan( aRetFunc, {|x| x[1] == cCodigoVar } )
If nPosR > 0

	For nXX := nPosR To Len(aRetFunc)
		If aRetFunc[nXX,1] <> cCodigoVar
			Exit
		Endif
		lFoiCalc := .f.
		For nYY := 1 To Len(aRetFunc[nXX,2])

			nPosDiff := aScan( aCadTipo[nXXX,3,nYYY,5], {|x| x[12] == aRetFunc[nXX,2,nYY,1] } )
			If nPosDiff > 0
				If ValType( aCadTipo[nXXX,3,nYYY,5,nPosDiff,11] ) == ValType(aRetFunc[nXX,2,nYY,2])
					If aCadTipo[nXXX,3,nYYY,5,nPosDiff,11] != aRetFunc[nXX,2,nYY,2]
						lFoiCalc := .f.
						Exit
					Else
						lFoiCalc := .t.
					Endif
				Else
					lFoiCalc := .f.
					Exit
				Endif
			Else
				lFoiCalc := .f.
				Exit
			Endif

		Next nYY

		If lFoiCalc
			//Resultado ja calculado
			cValorVar := aRetFunc[nXX,3]
			Exit
		Endif
	Next nXX

Endif

Return cValorVar

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fNG001Lege³ Autor ³ Denis Hyroshi Souza  ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Tela da legenda do resultado                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function fNG001Lege()
Local oFont12  := TFont():New("Arial",,-12,.T.,.T.)
Local oFont    := TFont():New("Arial",,-12,.F.,.F.)

Define MsDialog oDlg1New Title OemToAnsi(STR0052) From 0,0 To 09,50 OF oMainWnd //"Legenda do Resultado do Indicador"

@ 008,010 Say "9,99   "+STR0053 Of oDlg1New Pixel Color CLR_HGRAY Font oFont12 //"Indicador não foi calculado"
@ 020,010 Say "9,99   "+STR0054 Of oDlg1New Pixel Color CLR_GRAY Font oFont12 //"Resultado desatualizado após alterações nos parâmetros"
@ 032,010 Say "9,99   "+STR0055 Of oDlg1New Pixel Color CLR_BLACK Font oFont12 //"Resultado desejado"
@ 044,010 Say "9,99   "+STR0056 Of oDlg1New Pixel Color CLR_HRED Font oFont12 //"Resultado insatisfatório"

//@ 017,040 Say "Indicador não foi calculado"  Of oDlg1New Pixel Font oFont
//@ 029,040 Say "Resultado desatualizado após alterações nos parâmetros" Of oDlg1New Pixel Font oFont
//@ 041,040 Say "Resultado desejado" Of oDlg1New Pixel Font oFont
//@ 053,040 Say "Resultado insatisfatório" Of oDlg1New Pixel Font oFont

Activate MsDialog oDlg1New Centered

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fNG001Conf³ Autor ³ Denis Hyroshi Souza  ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Configura tela de indicadores                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function fNG001Conf()
Local _Mv_par01 := Mv_parFo
Local _Mv_par02 := Mv_parGr
If pergunte(cPerg,.t.)
	Mv_parFo := Mv_par01
	Mv_parGr := Mv_par02
	If _Mv_par01 <> Mv_parFo .Or. _Mv_par02 <> Mv_parGr
		nOpca := 3
		nOptionF := oFolder1:nOption
		oDlgInd:End()
	Endif
Endif

Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} NgInd1Rel
Detalhes do indicador

@param __nXX	, Numérico	, Posição no array.
@param __nYY	, Numérico	, Posição no array.
@param lTodosInd, Lógico	, Determina a impressão de todos os indicadores.

@author Denis Hyroshi Souza
@since 10/07/2008
@version P12

@return Vazio
/*/
//------------------------------------------------------------------------------
Function NgInd1Rel(__nXX,__nYY,lTodosInd)
Local aArea := GetArea()
Local nXXX,nYYY,LinhaCorrente,nXX
Local nPosX,nPosY
Local aIndRel := {}
Local aPerg := {}
Local cSMCOD := If(FindFunction("FWGrpCompany"),FWGrpCompany(),SM0->M0_CODIGO)
Local cSMFIL := If(FindFunction("FWCodFil"),FWCodFil(),SM0->M0_CODFIL)

Local cTempPath  := GetTempPath()
Local cTmpImg    := "TempTNGIndicador"
Local cTmpExt    := ".PNG"
Local cTmpBitmap := ""

Private cPerg1 := "NGIN01"

If lTodosInd
	If !pergunte(cPerg1,.t.)
		Return .f.
	Endif
Else
	pergunte(cPerg1,.f.)
	Mv_par01 := 1
	Mv_par02 := 1
	Mv_par03 := 1
	Mv_par04 := 1
	Mv_par05 := 1
	Mv_par06 := 1
Endif
// Se o Indicador Gráfico estiver desabilitado, não irá imprimir os gráficos
If Mv_parGr == 2
	Mv_par06 := 2
EndIf

oFtR10b := TFont():New("Arial",10,10,,.T.,,,,.F.,.F.)
oFtR10  := TFont():New("Arial",10,10,,.F.,,,,.F.,.F.)
oFtR12b := TFont():New("Arial",12,12,,.T.,,,,.F.,.F.)
oFtR13  := TFont():New("Arial",13,13,,.T.,,,,.F.,.F.)
oFtR08  := TFont():New("Arial",09,09,,.F.,,,,.F.,.F.)

//|-------------------------------------------|
//| Parametros de todas as variaveis          |
//|-------------------------------------------|
If lTodosInd
	For nXXX := 1 to Len(lIndNG)
		For nYYY := 1 to Len(lIndNG[nXXX])
			If ValType(lIndNG[nXXX,nYYY]) == "L"
				If lIndNG[nXXX,nYYY]
					aAdd(aIndRel,{nXXX,nYYY})
				Endif
			Endif
		Next nYYY
	Next nXXX
Else
	aAdd(aIndRel,{__nXX,__nYY})
Endif
If Len(aIndRel) == 0
	MsgInfo(STR0064) //"Nenhum indicador foi selecionado."
	Return
Endif
oPrintRel := FWMsPrinter():New(OemToAnsi(cCadastro))
oPrintRel:SetPortrait() //retrato

cLogo := NGLOCLOGO()

//Ordenando a variavel aRetFunc pelo codigo CODVAR
aSort( aRetFunc ,,, { |x,y| x[1] < y[1] } )

lin := 9999
For nXXX := 1 to Len(aIndRel)
	nPosX := aIndRel[nXXX,1]
	nPosY := aIndRel[nXXX,2]

	cTmpBitmap := cTempPath + Alltrim(aCadTipo[nPosX,3,nPosY,2]) + cTmpExt

	//TITULO INDICADOR
	If Mv_par06 == 1
		If ( lin + 800 ) > 3000 //Verifica a exsitência de espaço para impressão da imagem
			Somalinha(800,lTodosInd)
		EndIf
	EndIf
	Somalinha(,lTodosInd)
	oPrintRel:Line(lin,200,lin,2350)
	Somalinha(30,lTodosInd)
	oPrintRel:Say(lin,200,Alltrim( aCadTipo[nPosX,3,nPosY,2] ),oFtR12b) //Titulo Indicador

	// Imprime a Imagem do Indicador
	If Mv_par06 == 1
		Somalinha(20,lTodosInd)
		If ValType(oIndNG[nPosX,nPosY,7]) == "O"
			
			If File( cTmpBitmap )
				FErase( cTmpBitmap )
			EndIf

			oIndNG[nPosX,nPosY,7]:oTPPanel:SaveToPng(0, 0, oIndNG[nPosX,nPosY,7]:oTPPanel:nClientWidth, oIndNG[nPosX,nPosY,7]:oTPPanel:nClientHeight, cTmpBitmap)

			// Aguarda até que a imagem seja copiada corretamente para pasta temporária
			While !File( cTmpBitmap )
				Sleep( 500 ) // thread corrente deixa de ser executada por milisegundos
			End While

			oPrintRel:SayBitMap(lin, 300, cTmpBitmap, 600, 600)
			Somalinha(600,lTodosInd)

		EndIf
	EndIf

	Somalinha(80,lTodosInd)
	cTempImp := " "
	If aCadTipo[nPosX,3,nPosY,14] <> "2"
		cTempImp := Alltrim( TransForm(nIndNG[nPosX,nPosY],aCadTipo[nPosX,3,nPosY,9]) )
	Else
		cTempImp := nIndNG[nPosX,nPosY]
	Endif
	oPrintRel:Say(lin,300, STR0065 ,oFtR10b) //"Resultado:"
	If oIndNG[nPosX,nPosY,6] == 1
		oPrintRel:Say(lin,550,cTempImp + aCadTipo[nPosX,3,nPosY,13] ,oFtR12b,,CLR_HGRAY) //Resultado / Unidade Medida
	ElseIf oIndNG[nPosX,nPosY,6] == 2
		oPrintRel:Say(lin,550,cTempImp + aCadTipo[nPosX,3,nPosY,13] ,oFtR12b,,CLR_GRAY)  //Resultado / Unidade Medida
	ElseIf oIndNG[nPosX,nPosY,6] == 3
		oPrintRel:Say(lin,550,cTempImp + aCadTipo[nPosX,3,nPosY,13] ,oFtR12b,,CLR_BLACK) //Resultado / Unidade Medida
	ElseIf oIndNG[nPosX,nPosY,6] == 4
		oPrintRel:Say(lin,550,cTempImp + aCadTipo[nPosX,3,nPosY,13] ,oFtR12b,,CLR_HRED)  //Resultado / Unidade Medida
	Endif

	//META
	If Mv_par03 == 1
		cTipoCmb := ""
		If aCadTipo[nPosX,3,nPosY,10] == "1" .and. aCadTipo[nPosX,3,nPosY,11] <> nil
			cTipoCmb := STR0066 //"Maior que: "
			cTipoCmb += Alltrim( TransForm(aCadTipo[nPosX,3,nPosY,11],aCadTipo[nPosX,3,nPosY,9]) )
		ElseIf aCadTipo[nPosX,3,nPosY,10] == "2" .and. aCadTipo[nPosX,3,nPosY,11] <> nil
			cTipoCmb := STR0067 //"Maior que ou igual a: "
			cTipoCmb += Alltrim( TransForm(aCadTipo[nPosX,3,nPosY,11],aCadTipo[nPosX,3,nPosY,9]) )
		ElseIf aCadTipo[nPosX,3,nPosY,10] == "3" .and. aCadTipo[nPosX,3,nPosY,11] <> nil
			cTipoCmb := STR0068 //"Menor que: "
			cTipoCmb += Alltrim( TransForm(aCadTipo[nPosX,3,nPosY,11],aCadTipo[nPosX,3,nPosY,9]) )
		ElseIf aCadTipo[nPosX,3,nPosY,10] == "4" .and. aCadTipo[nPosX,3,nPosY,11] <> nil
			cTipoCmb := STR0069 //"Menor que ou igual a: "
			cTipoCmb += Alltrim( TransForm(aCadTipo[nPosX,3,nPosY,11],aCadTipo[nPosX,3,nPosY,9]) )
		ElseIf aCadTipo[nPosX,3,nPosY,10] == "5" .and. aCadTipo[nPosX,3,nPosY,11] <> nil .and. aCadTipo[nPosX,3,nPosY,12] <> nil
			cTipoCmb := STR0070 //"Dentro do intervalo: "
			cTipoCmb += Alltrim( TransForm(aCadTipo[nPosX,3,nPosY,11],aCadTipo[nPosX,3,nPosY,9]) )
			cTipoCmb += STR0071 //" a "
			cTipoCmb += Alltrim( TransForm(aCadTipo[nPosX,3,nPosY,12],aCadTipo[nPosX,3,nPosY,9]) )
		ElseIf aCadTipo[nPosX,3,nPosY,10] == "6" .and. aCadTipo[nPosX,3,nPosY,11] <> nil .and. aCadTipo[nPosX,3,nPosY,12] <> nil
			cTipoCmb := STR0072 //"Fora do intervalo: "
			cTipoCmb += Alltrim( TransForm(aCadTipo[nPosX,3,nPosY,11],aCadTipo[nPosX,3,nPosY,9]) )
			cTipoCmb += STR0071 //" a "
			cTipoCmb += Alltrim( TransForm(aCadTipo[nPosX,3,nPosY,12],aCadTipo[nPosX,3,nPosY,9]) )
		ElseIf aCadTipo[nPosX,3,nPosY,10] == "7" .and. aCadTipo[nPosX,3,nPosY,11] <> nil
			cTipoCmb := STR0073 //"Igual a: "
			cTipoCmb += Alltrim( TransForm(aCadTipo[nPosX,3,nPosY,11],aCadTipo[nPosX,3,nPosY,9]) )
		ElseIf aCadTipo[nPosX,3,nPosY,10] == "8" .and. aCadTipo[nPosX,3,nPosY,11] <> nil
			cTipoCmb := STR0074 //"Diferente de: "
			cTipoCmb += Alltrim( TransForm(aCadTipo[nPosX,3,nPosY,11],aCadTipo[nPosX,3,nPosY,9]) )
		Endif

		If !Empty(cTipoCmb)
			cTipoCmb += aCadTipo[nPosX,3,nPosY,13]
			Somalinha(80,lTodosInd)
			oPrintRel:Say(lin,300, STR0075 ,oFtR10b) //"Meta:"
			oPrintRel:Say(lin,550,cTipoCmb,oFtR10)
		Endif
	Endif

	//DESCRICAO
	If Mv_par01 == 1
		cDesVar := Alltrim( aCadTipo[nPosX,3,nPosY,8] )
	    nLinhasMemo := MLCOUNT(cDesVar,100)
	    For LinhaCorrente := 1 To nLinhasMemo
		    If !Empty( MemoLine(cDesVar,100,LinhaCorrente) )
				If LinhaCorrente == 1
					lPrimFun := .f.
					Somalinha(80,lTodosInd)
					oPrintRel:Say(lin,300, STR0076 ,oFtR10b) //"Descrição:"
				Else
			        Somalinha(,lTodosInd)
				Endif
				oPrintRel:Say(lin,550,MemoLine(cDesVar,100,LinhaCorrente),oFtR10)
	        Endif
	    Next LinhaCorrente
	Endif

	//FORMULA
	If Mv_par02 == 1
		cDesVar := Alltrim( aCadTipo[nPosX,3,nPosY,4] )
	    nLinhasMemo := MLCOUNT(cDesVar,75)
	    For LinhaCorrente := 1 To nLinhasMemo
		    If !Empty( MemoLine(cDesVar,75,LinhaCorrente) )
				If LinhaCorrente == 1
					lPrimFun := .f.
					Somalinha(80,lTodosInd)
					oPrintRel:Say(lin,300, STR0077 ,oFtR10b) //"Fórmula:"
				Else
			        Somalinha(,lTodosInd)
				Endif
				oPrintRel:Say(lin,550,MemoLine(cDesVar,75,LinhaCorrente),oFtR10)
	        Endif
	    Next LinhaCorrente
	Endif

	//FUNCOES (VARIAVEIS)
	If Mv_par04 == 1
		cFormula := aCadTipo[nPosX,3,nPosY,1]
		lPrimFun := .t.
		dbSelectArea("TZ6")
		dbSetOrder(1)
		dbSeek(xFilial("TZ6")+Str(nModulo,2)+cFormula)
		While !Eof() .and. xFilial("TZ6")+cFormula == TZ6->TZ6_FILIAL+TZ6->TZ6_CODIND .and. TZ6->TZ6_MODULO == Str(nModulo,2)
			dbSelectArea("TZ2")
			dbSetOrder(1)
			If dbSeek(xFilial("TZ2")+Str(nModulo,2)+TZ6->TZ6_CODVAR)
				Somalinha(,lTodosInd)
				If lPrimFun
					lPrimFun := .f.
					Somalinha(20,lTodosInd)
					oPrintRel:Say(lin,300, STR0078 ,oFtR10b) //"Variáveis:"
				Endif
				cResFunc := fVarCalc( TZ2->TZ2_CODVAR , nPosX , nPosY )
				If ValType(cResFunc) == "C"
					cResFunc := " =  " + Alltrim( cResFunc )
				ElseIf ValType(cResFunc) == "N"
					cResFunc := " =  " + Alltrim( Str( cResFunc ) )
				Else
					cResFunc := " =  "
				Endif
				oPrintRel:Say(lin,550, Alltrim( TZ2->TZ2_CODVAR ) + " (" + AllTrim(TZ2->TZ2_TITULO) + ") " ,oFtR10b)
				oPrintRel:Say(lin,1290, cResFunc ,oFtR10b)
				cDesVar := Alltrim( MSMM(TZ2->TZ2_DESCRI) )
			    nLinhasMemo := MLCOUNT(cDesVar,95)
			    For LinhaCorrente := 1 To nLinhasMemo
			        If !Empty( MemoLine(cDesVar,95,LinhaCorrente) )
				        Somalinha(,lTodosInd)
			        	oPrintRel:Say(lin,600,MemoLine(cDesVar,95,LinhaCorrente),oFtR10)
			        Endif
			    Next LinhaCorrente
			Endif
			dbSelectArea("TZ6")
			dbSkip()
		End
	Endif

	//PARAMETROS
	If Mv_par05 == 1
		For nXX := 1 to Len( aCadTipo[nPosX,3,nPosY,5] )
			If nXX == 1
				Somalinha(80,lTodosInd)
				oPrintRel:Say(lin,300, STR0079 ,oFtR10b) //"Parâmetros:"
			Else
				Somalinha(,lTodosInd)
			Endif
			cRet := aCadTipo[nPosX,3,nPosY,5][nXX, 2] + ": "
			cValType := ValType( aCadTipo[nPosX,3,nPosY,5][nXX, 11] )
			If cValType $ "D/4"
				cRet += DtoC( aCadTipo[nPosX,3,nPosY,5][nXX, 11] )
			ElseIf cValType $ "N/2"
				cRet += cValtoChar( aCadTipo[nPosX,3,nPosY,5][nXX, 11] )
			ElseIf cValType $ "L/3"
				If aCadTipo[nPosX,3,nPosY,5][nXX, 11]
					cRet += ".T."
				ElseIf !aCadTipo[nPosX,3,nPosY,5][nXX, 11]
					cRet += ".F."
				Else
					cRet += STR0031 //"Nulo"
				Endif
			ElseIf cValType $ "C/1"
				cRet += aCadTipo[nPosX,3,nPosY,5][nXX, 11]
			Else
				cRet += STR0031 //"Nulo"
			Endif
			oPrintRel:Say(lin,550, cRet ,oFtR10)
		Next nXX
	Endif
Next nXXX

If lin <> 9999
	If Mv_par06 == 1
		SomaLinha( 60 , lTodosInd)
	EndIf
	oPrintRel:Line(lin+60,200,lin+60,2350)
	oPrintRel:EndPage()
Endif

oPrintRel:Preview()

RestArea(aArea)
Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} Somalinha
Função responsável por pular linha.

@param nMaisLin	, Numérico	, Espaçamento de linhas que será pulado.
@param lTodosInd, Lógico	, Determina a impressão de todos os indicadores.

@author Guilherme Freudenburg
@since 01/02/2018
@version P12

@return Vazio
/*/
//------------------------------------------------------------------------------
Static Function Somalinha(nMaisLin,lTodosInd)
If ValType(nMaisLin) == "N"
	lin += nMaisLin
Else
	lin += 60
Endif
If lin > 3000
	If lin <> 9999
		oPrintRel:EndPage()
	Endif

	oPrintRel:StartPage()
	If lTodosInd
		lin := 250
		If File(cLogo)
			oPrintRel:SayBitMap(200,200,cLogo,335,185)
		EndIf
		oPrintRel:Say(lin,600,cCadastro,oFtR13)
		oPrintRel:Say(lin-30,2100,STR0080+DtoC(dDataBase),oFtR08) //"Data: "
		oPrintRel:Say(lin+10,2100,STR0081+Substr(Time(),1,5),oFtR08) //"Hora: "

		lin := 450
	Else
		lin := 200
	Endif
Endif
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NgIndAuto ³ Autor ³ Denis Hyroshi Souza  ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula automaticamente o indicador                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NgIndAuto(nModInd,cCodInd,aDadosInd,lMsg)
Local nXXX,nYYY,nPos1,nPos2,nXX,nYY,nVIR
Local nCont := 0
Local cMsg  := ""
Local nQtdeInd := 0
Local aAreaXXX := GetArea()
Local aAreaTZ5 := TZ5->(GetArea())
Local cTempFor
Local xRet := 0.000, nRet := 0.000
Local cMemoRes,cMemoTmp
Local aRet1
Local cExpVar,nVirgula
Local lCpo2_TZ5 := NGCADICBASE("TZ5_FORCON","D","TZ5",.F.)

Default nModInd := nModulo
Default lMsg    := .f.

If Valtype(cCodInd) <> "C"
	If lMsg
		MsgStop(STR0082) //"O codigo do indicador inválido."
	Endif
	Return
Endif
If Valtype(aDadosInd) <> "A"
	If lMsg
		MsgStop(STR0083) //"Matriz com os parâmetros inválida."
	Endif
	Return
Endif

dbSelectArea("TZ5")
dbSetOrder(1)
If !dbSeek( xFilial("TZ5") + Str(nModInd,2) + cCodInd )
	If lMsg
		MsgStop(STR0082) //"O codigo do indicador inválido."
	Endif
	Return
Endif

cMemoTZ5 := Alltrim(TZ5->TZ5_FORMUL)
If lCpo2_TZ5
	cMemoTZ5 := Alltrim(TZ5->TZ5_FORMUL)+Alltrim(TZ5->TZ5_FORCON)
Endif
cTempFor := StrTran(cMemoTZ5, Chr(10) , "")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//Verifica se a formula esta correta(sintaxe)
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !NGCHKFORM( cTempFor , .f.)
	//Se a formula estiver com erro
Else
	//--------------------
	// Histórico
	//--------------------
	// Inicializa o Processo de Armazenar os dados do Cálculo
	NGI6MVHIST(2)

	//Calcula a formula
	cMemoTmp := cTempFor
	cVarNao  := ""
	aTabVar  := {}
	aTabExp  := {}
	lRet     := .t.
	aArea    := GetArea()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Varre formula separando e calculando as @variaveis@          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cMemoRes := ""
	While !Empty(cMemoTmp)
		nPos1 := AT("@",cMemoTmp)
		If nPos1 > 0
			nPos2 := AT("@", Substr(cMemoTmp,nPos1+1) )
			If nPos2 > 0
				cCodigoVar := Substr( cMemoTmp , nPos1+1 , nPos2-1 )
				aAdd( aTabVar, cCodigoVar )
				cMemoRes += Substr( cMemoTmp , 1 , nPos1 - 1)

				aRet1   := {}
				dbSelectArea("TZ3")
				dbSetOrder(1)
				dbSeek(xFilial("TZ3")+Str(nModInd,2)+PadR(cCodigoVar,12))
				While !Eof() .and. xFilial("TZ3")+PadR(cCodigoVar,12) == TZ3->TZ3_FILIAL+TZ3->TZ3_CODVAR .and. TZ3->TZ3_MODULO == Str(nModInd,2)
					aAdd( aRet1 , { TZ3->TZ3_ORDEM , TZ3->TZ3_CODPAR } )
					dbSelectArea("TZ3")
					dbSkip()
				End
				aSort( aRet1 ,,, { |x,y| x[1] < y[1] } )

				//Executa a variavel (funcao) passando os parametros informados
				//Cria a expressao para executar a variavel (funcao)
				dbSelectArea("TZ2")
				dbSetOrder(1)
				dbSeek(xFilial("TZ2")+Str(nModInd,2)+PadR(cCodigoVar,12))
				cExpVar := Alltrim(TZ2->TZ2_FUNCAO)+"("
				If TZ2->TZ2_TIPO == "2"
					cExpVar := "U_"+cExpVar
				Endif
				nVirgula:= 1
				For nXX := 1 To Len(aRet1)
					While nVirgula < Val(aRet1[nXX,1])
						cExpVar += ","
						nVirgula++
					End
					nPosA := aScan( aDadosInd, {|x| x[1] == Alltrim(aRet1[nXX,2]) } )
					If nPosA > 0
						If ValType(aDadosInd[nPosA,2]) == "C"
							If !Empty(aDadosInd[nPosA,2])
								cExpVar += "'" + Alltrim( aDadosInd[nPosA,2] ) + "'"
							Endif
						ElseIf ValType(aDadosInd[nPosA,2]) == "N"
							cExpVar += Alltrim( Str( aDadosInd[nPosA,2] ) )
						ElseIf ValType(aDadosInd[nPosA,2]) == "D"
							If !Empty(aDadosInd[nPosA,2])
								cExpVar += "STOD('" + DtoS(aDadosInd[nPosA,2]) + "')"
							Endif
						ElseIf ValType(aDadosInd[nPosA,2]) == "L"
							If aDadosInd[nPosA,2]
								cExpVar += ".T."
							Else
								cExpVar += ".F."
							Endif
						Endif
					Endif
				Next nXX
				cExpVar += ")"

				//Executa a variavel (funcao)
				cValorVar := &(cExpVar)
				If ValType(cValorVar) != "N"
					If ValType(cValorVar) != "C"
						If lMsg
							MsgStop(STR0084+Alltrim(cCodigoVar)+STR0085,STR0042) //"A Variavél "###" está retornando um conteúdo diferente de númerico ou hora."###"Atenção"
						Endif
						Return
					Else
						cValorVar := Alltrim(cValorVar)
						nPos2Dot := RAT(":",cValorVar)
						If nPos2Dot != Len(cValorVar)-2 .or. !fValHr01(cValorVar)
							If lMsg
								MsgStop(STR0084+Alltrim(cCodigoVar)+STR0085,STR0042) //"A Variavél "###" está retornando um conteúdo diferente de númerico ou hora."###"Atenção"
							Endif
							Return
						EndIf
					EndIf
				EndIf

				//Adiciona valor calculado na expressao
				If ValType(cValorVar) == "C"
					cValorVar := HTON(cValorVar)
				Endif
				cMemoRes += Alltrim(Str(cValorVar))
				cMemoTmp := Substr( cMemoTmp , nPos1+nPos2+1 ) //Pega a partir do fecha variavel '@XXXXXXXX@'
			Else
				Exit
			Endif
		Else
			cMemoRes += cMemoTmp
			Exit
		EndIf
	End

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Calcula a expressao                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cMemoRes := StrTran(cMemoRes, "#" , "" )
	xRet := &(cMemoRes)

	If ValType(xRet) <> "N" .Or. Val( cValToChar( xRet ) ) < 0
		xRet := 0.000
	EndIf

	nRet := xRet

	//--------------------
	// Histórico
	//--------------------
	// Confirma o Histórico
	NGI6MVHIST(3, .T.)
	// Armazena o Resultado do Cálculo
	NGI6HISTRE(cCodInd/*cFormula*/, nRet/*nResultado*/)
Endif

RestArea(aAreaTZ5)
RestArea(aAreaXXX)

Return nRet


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³  fRetCombo³ Autor ³ Denis Hyroshi de Souza³ Data ³07/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se a formula esta correta e faz a gravacao         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fRetCombo(cVar)
Local aArray1 := RetSx3Box(cVar,,,1)
Local nCont,aArray2 := {}

For nCont := 1 To Len(aArray1)
	AADD(aArray2,aArray1[nCont][1])
Next nCont

Return aClone(aArray2)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NgInd1Fol ³ Autor ³ Denis Hyroshi Souza   ³ Data ³10/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao ao alterar de Folder                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NgInd1Fol()
Local nXXX,nYYY,lTemDesMk := .f.

nXXX := oFolder1:nOption

If nXXX <= Len(lIndNG)
	For nYYY := 1 to Len(lIndNG[nXXX])
		If ValType(lIndNG[nXXX,nYYY]) == "L"
			If !lIndNG[nXXX,nYYY]
				lTemDesMk := .t.
				Exit
			Endif
		Endif
	Next nYYY
Endif

If !lTemDesMk
	oBtnMark:Hide()
	oBtnDemk:Show()
Else
	oBtnMark:Show()
	oBtnDemk:Hide()
Endif

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NG001VALID ³ Autor ³ Jackson Machado        ³ Data ³19/10/2012³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Execucao da validacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG001VALID(cExec,cVariavel)
Local nX
Local nPosFol := 0, nPosFor := 0, nChk := 0
Local nAt, cAt
Local cRead := ReadVar()

nAt := At("_",cRead)
cAt := SubStr(cRead,1,nAt-1)

For nX := 1 To Len(aCadTipo)
	If (nPos := aScan(aCadTipo[nX][3],{|x| AllTrim(UPPER(x[1])) == AllTrim(UPPER(cAt))})) > 0
		nPosFol := nX
		nPosFor := nPos
	EndIF
Next nX

If nPosFol > 0 .AND. nPosFor > 0
	nChk := aScan(aIndChk,{|x| x[1] == nPosFol .AND. x[2] == nPosFor })
EndIf

If nChk > 0
	For nX := 1 To Len(aIndChk[nChk][5])
		Private cVarVal := "M->"+aIndChk[nChk][5][nX][12]
		If aIndChk[nChk][5][nX][12] $ ReadVar()
			&(cVarVal) := &(cRead)
		Else
			&(cVarVal) := aIndChk[nChk][5][nX][11]
		EndIf
	Next nX
Else
	cExec := ""
EndIf

Return If(!Empty(cExec),&(cExec),.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fValHr01  ³ Autor ³ Denis Hyroshi Souza   ³ Data ³07/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida hora                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fValHr01(cValorVar)
Local lRETVH := .T.,xh,xm,cTemp,posv
Local lPROVH := .F.
Local nPadraoX := cValorVar

If Empty(nPadraoX)
   lPROVH := .F.
Else
   posv := At(":",nPadraoX)
   If posv = 0
      lPROVH := .T.
   Else
      cTemp := Alltrim(Substr(nPadraoX,1,posv-1)+Substr(nPadraoX,posv+1))
      If Empty(cTemp)
	      lPROVH := .T.
      Else
	      If Len(cTemp) == 1
		      nPadraoX := "0:0"+cTemp
		      posv := 2
	      ElseIf Len(cTemp) == 2
		      nPadraoX := "0:"+cTemp
		      posv := 2
	      Else
		      nPadraoX := Substr( cTemp, 1, Len(cTemp)-2 )+":"+Substr(cTemp,Len(cTemp)-1)
		      posv := Len(nPadraoX)-2
	      Endif

	      cHorav   := SubStr( nPadraoX ,1,(posv-1))
	      cMinutov := SubStr( nPadraoX ,(posv+1),2)
	      If Empty(cHorav) .Or. Empty(cMinutov) .Or. Len(Alltrim(cMinutov)) < 2
	         lPROVH := .T.
	      Else
	         For xh := 1 To Len(cHorav)
	            cCARACHOR := Substr(cHorav,xh,1)
	            If Empty(cCARACHOR) .Or. !Isdigit(cCARACHOR)
	              lPROVH := .T.
	              Exit
	            Endif
	         Next xh
	         If !lPROVH
	            For xm := 1 To Len(cMinutov)
	               cCARACMIN := Substr(cMinutov,xm,1)
	               If Empty(cCARACMIN) .Or. !Isdigit(cCARACMIN)
	                 lPROVH := .T.
	                 Exit
	               Endif
	            Next xm
	         Endif

	         If !lPROVH
	            nHorav   := Val(cHorav)
	            nMinutov := Val(cMinutov)
	            If !(nMinutov < 60)
	               lPROVH := .T.
	            Endif
	         Endif
	      Endif
      Endif
   Endif
Endif

If lPROVH
   lRETVH := .F.
Endif

Return lRETVH

//-------------------------------------------------------------------
/*/{Protheus.doc} fNewInd
Verificar se o ambiente está apto a utilizar PO-UI e verifica se 
o usuário tem interesse em utilizar a versão com PO-UI.

@type   Function

@author Eduardo Mussi
@since  14/11/2023

@return Lógico, define que será utilizado PO-UI( NGIND014 )
/*/
//-------------------------------------------------------------------
Static Function fNewInd()
	
	Local lRet := .F. 

	// Verifica se o ambiente está preparado para utilizar PO-UI
	If FindFunction('AmIOnRestEnv') .And. AmIOnRestEnv()
	
		// 'Há uma nova versão dos indicadores gráficos. Deseja utilizar?' ## 'Atenção'
		lRet := MsgYesNo( STR0090, STR0042 ) 

	EndIf

Return lRet

