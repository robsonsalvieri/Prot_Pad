#INCLUDE "PROTHEUS.CH"
#INCLUDE "MNTR990.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ MNTR990  ³ Autor ³ Inacio Luiz Kolling ³Data³23/11/07 09:20³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Mapa de manutencao por bem   (H. previstas X Custo previsto)³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAMNT                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR990()
	//------------------------------------------------
	// Armazena variaveis p/ devolucao (NGRIGHTCLICK)
	//------------------------------------------------
    Local aNGBEGINPRM := NGBEGINPRM()

    Private cPathSiga := GetPvProfString(GetEnvServer(),"StartPath","ERROR",GetADV97())
    Private oPrint,li := 0
    Private aCores    := {{'A',CLR_BLUE},;
                        {'B',CLR_GREEN},;
                        {'C',CLR_HRED},;
                        {'D',CLR_BLACK},;
                        {'E',CLR_RED}}
    Private nSizeCC   := If((TAMSX3("CTT_CUSTO")[1]) < 1,20,(TAMSX3("CTT_CUSTO")[1]))

    oFont09B := TFont():New("Arial",09,09,,.T.,,,,.F.,.F.)
    oFont13  := TFont():New("Courier New",13,13,,.T.,,,,.F.,.F.)
    oFont20  := TFont():New("Courier New",20,20,,.T.,,,,.F.,.F.)
    oFont30  := TFont():New("Courier New",30,30,,.T.,,,,.F.,.F.)
    oFont40  := TFont():New("Courier New",40,40,,.T.,,,,.F.,.F.)
    cPergT   := PadR( "MNT990", Len(Posicione("SX1", 1, "MNT990", "X1_GRUPO")) )

    If !Pergunte(cPergT,.t.)
        Return
    Endif

    oPrint  := TMSPrinter():New(OemToAnsi(STR0001))
    limpbol := oPrint:Setup()

    If !limpbol
        Return
    Endif

    oPrint:SetLandscape()
    oPrint:SetPaperSize(9)

    Processa({|lEnd| MNTR990IMP(oPrint)},STR0002)

    DbSelectArea("ST9")

	//-----------------------------------------------
	//  Devolve variaveis armazenadas (NGRIGHTCLICK)
	//-----------------------------------------------
    NGRETURNPRM(aNGBEGINPRM)

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR990IMP³ Autor ³ Inacio Luiz Kolling ³Data³26/11/07 09:30³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Processo e impressao                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR990                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR990IMP(oPrint)
Local   nV1,nV2
Private dDataIn := MV_PAR07
Private nPag    := 0, lTemPrv := .f.
Private dDtaAux := dDataIn
Private nTamAx  := (MV_PAR08 - MV_PAR07)+1
Private cDef0107 := Posicione( 'SX1' , 1 , cPergT + '07' , 'X1Def01()' )
Private cDef0207 := Posicione( 'SX1' , 1 , cPergT + '07' , 'X1Def02()' )
Private cDef0109 := Posicione( 'SX1' , 1 , cPergT + '09' , 'X1Def01()' )
Private cDef0209 := Posicione( 'SX1' , 1 , cPergT + '09' , 'X1Def02()' )

If mv_par10 = 1
   aMatMa := Array(nTamAx,6)
   /*
     [1] - Dia mes ano
     [2] - Data manutencao
     [3] - Horas Previstas
     [4] - Custos
     [5] - Cor
     [6] - Semana
  */
Else
   aMatMa := {}
   /*
     [1] - Semana
     [2] - Ano
     [3] - Horas Previstas
     [4] - Custos
   */
Endif

Store 0 To nV1,nV2,nDMes

DbSelectArea("STF")
Dbsetorder(1)
DbSeek(xFilial("STF")+mv_par01,.T.)
ProcRegua(LastRec())
While !Eof() .And. STF->TF_FILIAL == xfilial("STF") .And.;
   STF->TF_CODBEM <= mv_par02

   lTemPrv := .f.
   nRECNO  := Recno()

   IncProc(OemtoAnsi(STR0003+" "+Alltrim(STF->TF_CODBEM)+" "+STR0004+" "+;
                                 Alltrim(STF->TF_SERVICO)+" "+STR0005+" "+STF->TF_SEQRELA))
   DbSelectArea("ST9")
   Dbsetorder(1)
   DbSeek(xFilial("ST9")+STF->TF_CODBEM)
   If ST9->T9_SITMAN <> "A" .Or. ST9->T9_SITBEM <> "A" .Or. STF->TF_ATIVO = "N" .Or. STF->TF_PERIODO = "E"
      DbSelectArea("STF")
      DbSkip()
      Loop
   EndIf
   If ST9->T9_CCUSTO < mv_par03 .Or. ST9->T9_CCUSTO > mv_par04 .Or.;
      ST9->T9_CENTRAB < mv_par05 .Or. ST9->T9_CENTRAB > mv_par06
   	dbSelectArea("STF")
      dbSkip()
      Loop
   EndIf

   DbSelectArea("ST4")
   Dbsetorder(1)
   DbSeek(xFilial("ST4")+STF->TF_SERVICO)

   dDataIX := dDtaAux
   nV1     := 0
   If mv_par10 = 1
      For nV1 := 1 To nTamAx
         nSemaA := NGSEMANANO(dDataIX)
         aMatMa[nV1] := {dDataIX,Ctod('  /  /  '),'00:00',0,'D',nSemaA}
         dDataIX ++
      Next nV1
   Else
      aMatMa := {}
      For nV1 := 1 To nTamAx
         nSemaA := NGSEMANANO(dDataIX)
         nPosMD := Ascan(aMatMa,{|x| x[1]+x[2] = Str(nSemaA,2)+Str(Year(dDataIX),4)})
         If nPosmD = 0
            Aadd(aMatMa,{Str(nSemaA,2),Str(Year(dDataIX),4),'00:00',0})
         Endif
         dDataIX ++
      Next nV1
   Endif

   dULT   := STF->TF_DTULTMA  //Data de Ultima Manutencao
   dULTAC := ST9->T9_DTULTAC  //Data de Ultimo Acompanhamento do Contador do Bem
   cTIPO  := STF->TF_TIPACOM  //Tipo de Acompanhamento da Manutencao
   nQTD   := STF->TF_TEENMAN  //Quantidade de Tempo da Manutencao
   cUND   := STF->TF_UNENMAN  //Unidade de Medida da Manutencao
   nULT   := STF->TF_CONMANU  //Contador da Ultima Manutencao
   nINC   := STF->TF_INENMAN  //Incremento da Manutencao
   nPOS   := ST9->T9_CONTACU  //Contador Acumulado do Bem
   nVAR   := ST9->T9_VARDIA   //Variacao dia do Bem

   If cTIPO = "S"
      DbSelectArea("TPE")
      Dbsetorder(1)
      If DbSeek(xFilial("TPE")+STF->TF_CODBEM)
         dULTAC := TPE->TPE_DTULTA
         nPOS   := TPE->TPE_CONTAC
         nVAR   := TPE->TPE_VARDIA
      Else
         DbSelectArea("STF")
         DbSkip()
         Loop
      EndIf
   EndIf
   lOK := .T.

   If cTIPO == "T"
      lOK := If(Empty(nQTD) .Or. Empty(cUND) .Or. Empty(dULT),.F.,.T.)
   ElseIf cTIPO $  "C/P/S"
      lOK := If(Empty(nINC) .Or. Empty(nVAR) .Or. Empty(dULTAC) .Or. Empty(nPOS) .Or. Empty(nULT),.F.,.T.)
   ElseIf cTIPO == "A"
      lOK := If(Empty(nINC) .Or. Empty(nVAR) .Or. Empty(dULTAC) .Or. Empty(nPOS) .Or. Empty(nULT) .Or.;
                Empty(nQTD) .Or. Empty(cUND) .Or. Empty(dULT),.F.,.T.)
   ElseIf cTIPO == "F"
      If STF->( FieldPos("TF_CONPREV") ) <> 0
         nULT := STF->TF_CONPREV
         lOK  := If(Empty(nINC) .Or. Empty(nVAR) .Or. Empty(dULTAC) .Or. Empty(nPOS) .Or. Empty(nULT),.F.,.T.)
      EndIf
   Else
      lOK := .F.
   EndIf

   If lOK
      MNTR990STF()
   EndIf

   If lTemPrv
      MNTR990MI()
   Endif

   DbSelectArea("STF")
   Dbsetorder(1)
   Dbgoto(nRECNO)
   DbSkip()
End

oPrint:EndPage()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a condicao original do arquivo principal             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RetIndex('STJ')
Set Filter To
oPrint:Preview()

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR990STF³ Autor ³ In cio Luiz Kolling   ³ Data ³23/11/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Calcula as data da proxima manutencao por tempo e projeta as³±±
±±³          ³proximas manutencao.                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR990                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR990STF()
Local dPROX, dMIN, dMAX, lOK:= .F.,dDTLMANT,dDTLMANC
Local PXX,nPosD,nPosMD,vDatPrg := {}

Store 0 To PXX,nPosD,nPosMD

dMAX     := Ctod("  /  /  ")
dATEDA   := MV_PAR08
cCONDSTJ := "(stj->tj_dtmpini >= MV_PAR07 .And. stj->tj_dtmpini <= MV_PAR08) .And. "
cCONDSTJ += "(stj->tj_dtorigi) >= MV_PAR07 .And. stj->tj_dtorigi <= MV_PAR08 .And. "
cCONDSTJ += "stj->tj_situaca == 'L' .And. stj->tj_termino = 'N' .And. "
cCONDSTJ += "Val(stj->tj_plano) > 0"

DbSelectArea("STJ")
DbSetOrder(2)
DbSeek(xFilial("STJ")+"B"+STF->TF_CODBEM+STF->TF_SERVICO+STF->TF_SEQRELA)
ProcRegua(Reccount())
While !Eof() .And. STJ->TJ_FILIAL == xFilial("STJ") .And. STJ->TJ_TIPOOS == "B" .And.;
   STJ->TJ_CODBEM  == STF->TF_CODBEM .And. STJ->TJ_SERVICO == STF->TF_SERVICO .And.;
   STJ->TJ_SEQRELA == STF->TF_SEQRELA
   IncProc(OemtoAnsi(STR0006))
   lOK  := .T.
   dMAX := MAX(dMAX, If(Empty(STJ->TJ_DTMRFIM),STJ->TJ_DTMPFIM,STJ->TJ_DTMRFIM) )
   If &(cCONDSTJ)
      dDTGRA := STJ->TJ_DTMPINI
      If mv_par10 = 1
         nPosMD := Ascan(aMatMa,{|x| x[1] = dDTGRA})
         If nPosMD > 0 .And. nPosMD <= nTamAx
            vRetHC := MNTR990HC(STJ->TJ_ORDEM,STJ->TJ_PLANO)
            aMatMa[nPosMD,2] := dDTGRA
            aMatMa[nPosMD,3] := NGSOMAHCAR(vRetHC[1],aMatMa[nPosMD,3])
            aMatMa[nPosMD,4] += vRetHC[2]

            If dDTGRA < Date()
               aMatMa[nPosMD,5] := "C"
            ElseIf dDTGRA = Date()
               aMatMa[nPosMD,5] := "A"
            Else
               aMatMa[nPosMD,5] := "B"
            Endif
            lTemPrv := .t.
         Endif
      Else
         vRetHC := MNTR990HC(STJ->TJ_ORDEM,STJ->TJ_PLANO)
         nSemaA := NGSEMANANO(dDTGRA)
         nPosMD := Ascan(aMatMa,{|x| x[1]+x[2] = Str(nSemaA,2)+Str(Year(dDTGRA),4)})
         If nPosMD > 0
            aMatMa[nPosMD,3] := NGSOMAHCAR(vRetHC[1],aMatMa[nPosMD,3])
            aMatMa[nPosMD,4] += vRetHC[2]
         Endif
         lTemPrv := .t.
      Endif
   EndIf
   DbSelectArea("STJ")
   DbSkip()
End

DbSelectArea("STF")

If cTIPO = "T"    // Tempo
   dPROX   := dULT
   dDTLMAN := dPROX
Else
   If cTIPO = "A" // Tempo/Contador
      dDTLMANT := dULT   //Data da Ultima Manutencao P/Tempo
      dDTLMANC := dULTAC //Data da Ultima Manutencao P/Contador

      If dDTLMANT = dDTLMANC
         /*
         dDATATEM := NGPROXMAN(dULT,"T", nQTD, cUND, nULT, nINC, nPOS, nVAR)
         dDATACON := NGPROXMAN(dULTAC,"C", nQTD, cUND, nULT, nINC, nPOS, nVAR)
         */

         dDATATEM := NGPROXMANT(dULT,nQTD,cUND)
         dDATACON := NGPROXMANC(dULTAC,nULT,nINC,nPOS,nVAR)

         If dDATATEM < dDATACON
            cTIPO   := "T"
            dPROX   := dDTLMANT
            dDTLMAN := dDTLMANT
         Else
            cTIPO   := "C"
            dPROX   := dDTLMANC
            dDTLMAN := dDTLMANC
         EndIf
      Else
         If dDTLMANT < dDTLMANC
            cTIPO   := "T"
            dPROX   := dDTLMANT
            dDTLMAN := dDTLMANT
         Else
            cTIPO   := "C"
            dPROX   := dDTLMANC
            dDTLMAN := dDTLMANC
         EndIf
      EndIf
   Else        // Contador/Producao/Contador Fixo/Segundo Contador
      dPROX   := dULTAC
      dDTLMAN := dPROX
   EndIf
EndIf

dDTUTIL := Ctod("  /  /  ")
dDTUTIX := Ctod("  /  /  ")
lIMPDAT := .F.  //Considera data de ultima Manutencao

If dPROX < MV_PAR07
   ProcRegua(dPROX - MV_PAR07)
   lPrimT := .T.
   While dPROX < MV_PAR07
      IncProc(OemtoAnsi(STR0007))

      /*
      If cTIPO = "T"
         dPROX   := NGPROXMAN(dPROX, cTIPO, (nQTD*(-1)), cUND, nULT, (nINC*(-1)) , nPOS, nVAR)
         dDTUTIX := dPROX
      Else
         dPROX   := NGPROXMAN(dDTLMAN, cTIPO, (nQTD*(-1)), cUND, nULT, (nINC*(-1)) , nPOS, nVAR)
         dDTUTIX := dPROX
         nULT    := nULT - nINC
      EndIf
      */

      If cTIPO = "T"
         If lPrimT
           dPROX   := NGPROXMANT(dPROX,(nQTD*(-1)),cUND)
           lPrimT     := .F.
         Else
            dPROX := dDTUTIX-NGINCMANUNI(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA)
         EndiF
         dDTUTIX := dPROX
      Else
          dPROX   := NGPROXMANC(dDTLMAN,nULT,(nINC*(-1)),nPOS,nVAR)
         dDTUTIX := dPROX
         nULT    := nULT - nINC
      EndIf

      If dPROX <= MV_PAR07
         If cTIPO = "T"
            If dPROX < MV_PAR07
               If Empty(dDTUTIL)
                  dDTUTIX := dDTUTIL
                  dDTUTIL := dDTLMAN
               Else
                  dDTUTIL := dDTUTIL
               EndIf
            Else
               If Empty(dDTUTIL)
                  dDTUTIX := dDTUTIL
                  dDTUTIL := dPROX
               Else
                  dDTUTIX := Ctod("  /  /  ")
                  dDTUTIL := dPROX
               EndIf
            EndIf
         Else
            If dPROX < MV_PAR07
               If Empty(dDTUTIL)
                  dDATAMAT := NGPROXMAN(dDTLMAN, cTIPO, nQTD, cUND, nULT, nINC, nPOS, nVAR)
                  nULT     := nULT + nINC
                  If dDATAMAT = dDTLMAN
                     lIMPDAT := .T.
                     dDTUTIL := dDATAMAT
                  Else
                     dDTUTIL := dDTLMAN
                  EndIf
               Else
                  If dDTUTIL < dDTLMAN
                     lIMPDAT := .T.
                     dDTUTIL := dDTUTIL
                     nULT    := nULT + nINC
                  Else
                     dDTUTIL := dDTUTIL
                     nULT    := nULT + nINC
                  EndIf
               EndIf
            Else
               If dPROX < dDTLMAN
                  lIMPDAT := .T.
                  dDTUTIX := If(!Empty(dDTUTIL),Ctod("  /  /  "),dDTUTIL)
                  dDTUTIL := dPROX
               Else
                  dDTUTIX := dDTUTIL
                  dDTUTIL := dPROX
               EndIf
            EndIf
         EndIf
         Exit
      Endif
      dDTUTIL := dPROX
   End
Else
   ProcRegua(Max((dPROX - MV_PAR07),(dATEDA-dPROX)))
   lPrimT := .T.
   While dPROX < MV_PAR07 .And. dPROX < dATEDA
      IncProc(OemtoAnsi(STR0007))
      /*
      If cTIPO = "T"
         dPROX   := NGPROXMAN(dPROX, cTIPO, nQTD, cUND, nULT, nINC, nPOS, nVAR)
         dDTUTIX := dPROX
      Else
         dPROX   := NGPROXMAN(dDTLMAN, cTIPO, nQTD, cUND, nULT, nINC, nPOS, nVAR)
         dDTUTIX := dPROX
         nULT    := nULT + nINC
      EndIf
      */

      If cTIPO = "T"
         If lPrimT
           dPROX   := NGPROXMANT(dPROX,nQTD,cUND)
           lPrimT     := .F.
         Else
            dPROX := dDTUTIX+NGINCMANUNI(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA)
         EndiF
         dDTUTIX := dPROX
      Else
         dPROX   := NGPROXMANC(dDTLMAN,nULT,nINC,nPOS,nVAR)

         dDTUTIX := dPROX
         nULT    := nULT + nINC
      EndIf

      If dPROX >= MV_PAR07
         If cTIPO = "T"
            If dPROX > MV_PAR07
               If Empty(dDTUTIL)
                  dDTUTIX := dDTUTIL
                  dDTUTIL := dPROX
               Else
                  dDTUTIX := dDTUTIL
                  dDTUTIL := dPROX
               EndIf
            Else
               If Empty(dDTUTIL)
                  dDTUTIX := dDTUTIL
                  dDTUTIL := dPROX
               Else
                  dDTUTIX := Ctod("  /  /  ")
                  dDTUTIL := dPROX
               EndIf
            EndIf
         Else
            If dDTUTIL > MV_PAR07 .Or. Empty(dDTUTIL)
               If Empty(dDTUTIL)
                  dDATAMAT := NGPROXMAN(dPROX, cTIPO, (nQTD*(-1)), cUND, nULT, (nINC*(-1)) , nPOS, nVAR)
                  nULT     := nULT - nINC
                  If dDATAMAT = dDTLMAN
                     lIMPDAT := .T.
                     dDTUTIL := dDATAMAT
                  Else
                     If dDATAMAT = dPROX
                        nULT    := nULT + nINC
                        dDTUTIX := dDTUTIL
                        dDTUTIL := dPROX
                     Else
                        dDTUTIL := dDTLMAN
                     EndIf
                  EndIf
               Else
                  nULT := nULT - nINC
               EndIf
            Else
               dDTUTIX := dDTUTIL
               dDTUTIL := dPROX
            EndIf
         EndIf
         Exit
      Endif
      dDTUTIL := dPROX
   End
Endif

If Empty(dDTUTIX) .And. dDTLMAN = MV_PAR07

   lPrimT := .T.
   ProcRegua(MV_PAR07-dPROX)
   While dPROX <= MV_PAR07
      IncProc(OemtoAnsi(STR0007))

      /*
      If cTIPO = "T"
         dPROX   := NGPROXMAN(dPROX,cTIPO,nQTD,cUND,nULT,nINC,nPOS,nVAR)
         dDTUTIX := dPROX
      Else
         dPROX   := NGPROXMAN(dDTLMAN,cTIPO,nQTD,cUND,nULT,nINC,nPOS,nVAR)
         dDTUTIX := dPROX
         nULT    := nULT + nINC
      EndIf
      */

      If cTIPO = "T"
         If lPrimT
           dPROX  := NGPROXMANT(dPROX,nQTD,cUND)
           lPrimT := .F.
         Else
            dPROX := dDTUTIX+NGINCMANUNI(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA)
         EndiF
         dDTUTIX := dPROX
      Else
         dPROX   := NGPROXMANC(dDTLMAN,nULT,nINC,nPOS,nVAR)
         dDTUTIX := dPROX
         nULT    := nULT + nINC
      EndIf

      If dPROX > MV_PAR07
         dDTUTIL := dPROX
         dDTUTIX := dDTLMAN
         Exit
      EndIf
      dDTUTIL := dPROX
   End
EndIf

lPrimT := .T.
If Empty(dDTUTIL)
   If cTIPO = "T"
     If lPrimT
        dPROX  := NGPROXMANT(dPROX,nQTD,cUND)
        lPrimT := .F.
     Else
        dPROX := dDTUTIL+NGINCMANUNI(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA)
     EndiF
     dDTUTIL := dPROX
   Else
     dDTUTIL   := NGPROXMANC(dDTLMAN,nULT,nINC,nPOS,nVAR)
     nULT    := nULT + nINC
   EndIf
EndIf

lPrimT := .T.
ProcRegua(dATEDA-dDTUTIL)
While dDTUTIL <= dATEDA
   IncProc(OemtoAnsi(STR0008))
   dDTGRA := dDTUTIL
   If !NGTEMOSPREVE(STF->TF_CODBEM,STF->TF_SERVICO,;
      STF->TF_SEQRELA,dDTGRA,.F.)
      If mv_par10 = 1
         nPosMD := Ascan(aMatMa,{|x| x[1] = dDTGRA})
         If nPosMD > 0 .And. nPosMD <= nTamAx
            vRetHC := MNTR990HG(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA)
            aMatMa[nPosMD,2] := dDTGRA
            aMatMa[nPosMD,3] := NGSOMAHCAR(vRetHC[1],aMatMa[nPosMD,3])
            aMatMa[nPosMD,4] += vRetHC[2]
            If dDTGRA < Date()
               aMatMa[nPosMD,5] := "C"
            ElseIf dDTGRA = Date()
               aMatMa[nPosMD,5] := "A"
            Else
               aMatMa[nPosMD,5] := "B"
            Endif
            lTemPrv := .t.
         Endif
      Else
         vRetHC := MNTR990HG(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA)
         nSemaA := NGSEMANANO(dDTGRA)
         nPosMD := Ascan(aMatMa,{|x| x[1]+x[2] = Str(nSemaA,2)+Str(Year(dDTGRA),4)})
         If nPosMD > 0
            aMatMa[nPosMD,3] := NGSOMAHCAR(vRetHC[1],aMatMa[nPosMD,3])
            aMatMa[nPosMD,4] += vRetHC[2]
         Endif
         lTemPrv := .t.
      EndIf
   EndIf

   /*
   If cTIPO = "T"
      dDTUTIL := NGPROXMAN(dDTUTIL,cTIPO,nQTD,cUND,nULT,nINC,nPOS,nVAR)
   Else
      dDTUTIL := NGPROXMAN(dDTLMAN,cTIPO,nQTD,cUND,nULT,nINC,nPOS,nVAR)
      nULT    := nULT + nINC
   EndIf
   */

   If cTIPO = "T"
      If lPrimT
         dPROX  := NGPROXMANT(dDTUTIL,nQTD,cUND)
         lPrimT := .F.
      Else
         dPROX := dDTUTIL+NGINCMANUNI(STF->TF_CODBEM,STF->TF_SERVICO,STF->TF_SEQRELA,,,dDTUTIL)
      EndiF
      dDTUTIL := dPROX
   Else
      dDTUTIL := NGPROXMANC(dDTLMAN,nULT,nINC,nPOS,nVAR)
      nULT    := nULT + nINC
   EndIf
End
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR990HC ³ Autor ³ In cio Luiz Kolling   ³ Data ³26/11/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Calcula as horas prevista dos insumos (STL)                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR990                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR990HC(vOrde,vPlan)
Local cHoSTL := "00:00",nCustSTL := 0.00
dbselectArea('STL')
dbsetOrder(1)
If dbseek(xFILIAL('STL')+vOrde+vPlan)
   While !Eof() .And. STL->TL_FILIAL = XFILIAL("STL") .And.;
      STL->TL_ORDEM = vOrde .And. STL->TL_PLANO = vPlan
      If STL->TL_SEQRELA = "0  "
         If stl->tl_tiporeg $ "ETMF"
            cHoSTL := NGSOMAHCAR(Ntoh(STL->TL_QUANTID),cHoSTL)
         Endif
         nCustSTL += STL->TL_CUSTO
      Endif
      dbskip()
   End
Endif
Return {cHoSTL,nCustSTL}

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR990HG ³ Autor ³ In cio Luiz Kolling   ³ Data ³26/11/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Calcula as horas prevista dos insumos (STG)                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR990                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR990HG(vBem,vServ,vSeqR)
Local cHoSTG := "00:00",nCustSTG := 0.00
dbselectArea('STG')
dbsetOrder(1)
If dbseek(xFILIAL('STG')+vBem+vServ+vSeqR)
   While !Eof() .And. STG->TG_FILIAL = XFILIAL("STG") .And.;
      STG->TG_CODBEM = STF->TF_CODBEM .And.;
      STG->TG_SEQRELA = vSeqR
      If stg->tg_tiporeg $ "ETMF"
         cHoSTG := NGSOMAHCAR(Ntoh(STG->TG_QUANTID),cHoSTG)
      Endif
      nCustSTG += NGCALCUSTI(stg->tg_codigo,stg->tg_tiporeg,;
                             stg->tg_quantid,stg->tg_local,,,,stg->tg_quanrec)
      dbselectArea('STG')
      dbskip()
   End
Endif
Return {cHoSTG,nCustSTG}

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR990LIN³ Autor ³ In cio Luiz Kolling   ³ Data ³26/11/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Cabecalho e incrementador                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR990                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR990LIN(linhas)
    Local cTitA
    Default linhas := 50
    li += linhas
    If li >= 2500

        MNTR990LOG()

        MNTR990LIN(5)
        oPrint:Say(li-10,1000,STR0001,oFont40)

        MNTR990LIN(65)

        oPrint:Say(li-10,2770,Dtoc(MV_PAR07)+" "+STR0009+" "+Dtoc(MV_PAR08),oFont13)

        MNTR990LIN(70)

        oPrint:Say(li   ,450 ,STR0003+" "+Alltrim(STF->TF_CODBEM)+" - "+NGSEEK("ST9",STF->TF_CODBEM,1,"T9_NOME"),oFont13)
        cTitA := IIf( mv_par09 == 2, cDef0207, cDef0107 )
        oPrint:Say(li-30,2870,cTitA,oFont13,,aCores[5,2])
        MNTR990LIN()
        oPrint:Say(li-30,2765,STR0010+" "+Dtoc(Date()),oFont13)
        oPrint:Say(li   ,450 ,STR0004+" "+STF->TF_SERVICO+" - "+NGSEEK("ST4",STF->TF_SERVICO,1,"T4_NOME"),oFont13)
        oPrint:Say(li   ,1900,STR0005+" "+STF->TF_SEQRELA,oFont13)
        oPrint:Say(li+10,2850,STR0011+" "+Time(),oFont13)

        oPrint:Box(li+60,10,li+2120,3285)
        MNTR990LIN(270)
    Endif
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR990MI ³ Autor ³ In cio Luiz Kolling   ³ Data ³26/11/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Impressao do mapa                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR990                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR990MI()
Local nV1,nPF,nSF,aRetDA := {},vRetDN := {},vRetAN := {},lLeng := .t.
Local vVetDS := {STR0012,STR0013,STR0014,STR0015,STR0016,STR0017,STR0018}
Local cHoraA := '00:00',nHoraA

Store 0 To nV1,nPF,nSF,nHoraA

nQtdA := Year(MV_PAR08) - Year(MV_PAR07)
If nQtdA > 0
   nAnoA := Year(MV_PAR07)
   For nV1 := 1 To nQtdA+1
      If Ascan(vRetAN,{|x| x = nAnoA}) = 0
         Aadd(vRetAN,nAnoA)
      Endif
      vRetDN := {}
      vRetDN := NGRETDSANO(nAnoA)
      Aadd(aRetDA,vRetDN)
      nAnoA ++
   Next nV1
Else
   aRetDA := NGRETDSANO(Year(MV_PAR07))
   Aadd(vRetAN,Year(MV_PAR07))
Endif

li    := 4000
nPag  := 0
nSemA := 0
nSemX := 0
nMesX := 0
nAnoX := 0
nQtdL := 1
nColS := 200
nInc  := 1
nInI  := 1

If mv_par09 = 2
   For nV1 := 1 To Len(aMatMa)
      If aMatMa[nV1,3] <> '00:00' .Or. !Empty(aMatMa[nV1,4])
         nInI := nV1
         Exit
      Endif
   Next nV1
Endif

If mv_par10 = 1
   MNTR990LIN()
   For nPF := nInI To Len(aMatMa)
      lLeng := .f.
      If nQtdL = 1
         MNTR990LIN(40)
      Endif

      nMesS := Month(aMatMa[nPF,1])
      nAnoS := Year(aMatMa[nPF,1])
      If nMesS <> nMesX
         nMesX := nMesS
         nLinM := If(nQtdL = 1,250,319)
         cMesE := MESEXTENSO(nMesS)

         If nQtdL = 8
            cMesE := SubStr(cMesE,1,3)+"/"+SubStr(Alltrim(Str(nAnoS,4)),3,2)
         ElseIf nQtdL = 7 .And. Len(cMesE) > 5
            cMesE += "/"+SubStr(Alltrim(Str(nAnoS,4)),3,2)
         Else
            cMesE += " "+Str(nAnoS,4)
         Endif

         nColP := If(nQtdL = 8,170,20)
         If nAnoS = Year(Date()) .And. nMesS = Month(Date())
            oPrint:Say(li-nLinM,nColS-nColP,cMesE,oFont30,,aCores[1,2])
         Else
            oPrint:Say(li-nLinM,nColS-nColP,cMesE,oFont30)
         Endif

      Endif

      If nQtdL = 1
         oPrint:Line(li-150,10,li-150,3280)
      Endif

      If nQtdL = 1
         MNTR990LIN(80)
      Endif

      If nSemX <> aMatMa[nPF,6]
         nSemX   := aMatMa[nPF,6]
         cSemana := If(nQtdL = 8,chr(170)+STR0019,chr(170)+STR0020)

         If nAnoS = Year(Date()) .And. nMesS = Month(Date()) .And. ;
            nSemX = NGSEMANANO(Date())
            oPrint:Say (li-230,nColS-160,Str(aMatMa[nPF,6],2)+cSemana,oFont20,,aCores[1,2])
         Else
            oPrint:Say (li-230,nColS-160,Str(aMatMa[nPF,6],2)+cSemana,oFont20)
         Endif
         nPoA := Ascan(vRetAN,{|x| x = Year(aMatMa[nPF,1])})
         If nQtdA > 0
            nPDA := Ascan(aRetDA[nPoA],{|x| x = aMatMa[nPF,1]})
         Else
            nPDA := Ascan(aRetDA,{|x| x = aMatMa[nPF,1]})
         Endif

         If nPDA > 0
            nColP := If(nQtdL = 1,180,190)
            oPrint:Box(li-230,nColS-(nColP+2),li-150,(nColS+5)-nColP)
         Endif
      Endif

      oPrint:Box(li-150,nColS-190,li   ,nColS+200)
      oPrint:Line(li-90,nColS-190,li-90,nColS+200)
      oPrint:Line(li-50,nColS-190,li-50,nColS+200)
      oPrint:Line(li-50,nColS    ,li   ,nColS)

      nCorC := Ascan(aCores,{|x| x[1] = aMatMa[nPF,5]})
      If aMatMa[nPF,1] = Date()
         nCorC := 1
      Endif

      oPrint:Say(li-150,nColS-40,vVetDS[Dow(aMatMa[nPF,1])],oFont13,,aCores[nCorC,2])
      oPrint:Say(li-90 ,nColS-40,Str(Day(aMatMa[nPF,1]),2) ,oFont13,,aCores[nCorC,2])

      nCor  := Ascan(aCores,{|x| x[1] = aMatMa[nPF,5]})
      nColP := (8-Len(Alltrim(aMatMa[nPF,3])))*10
      oPrint:Say(li-45,(nColS-140)+nColP,aMatMa[nPF,3],oFont09B,,aCores[nCor,2])
      nColP := Len(Space(13-Len(Alltrim((Str(aMatMa[nPF,4],12,2))))))*10
      oPrint:Say(li-45,(nColS-30)+nColP,Transform(aMatMa[nPF,4],"@E 9,999,999.99"),oFont09B,,aCores[nCor,2])

      nQtdL ++
      nColS += 390
      If nQtdL = 9
         nQtdL := 1
         nColS := 200
         MNTR990LIN(190)
      Endif

      nInc ++
      If nInc = 49 .And. nPF <= Len(aMatMa)
         lLeng := .t.
         MNTR990LE()
         nQtdL := 1
         nColS := 200
         nInc  := 1
         li    := 4000
         MNTR990LIN()
      Endif

      If nPF = Len(aMatMa) .And. !lLeng
         MNTR990LIN(190)
         MNTR990LE()
      Endif
   Next nPF
Else
   MNTR9902LI()
   ProcRegua(Len(aMatMa))
   For nPF := nInI To Len(aMatMa)
      lLeng := .f.

      If nQtdL = 1
         MNTR9902LI(40)
      Endif

      nAnoS  := Val(aMatMa[nPF,2])
      nSemX  := Val(aMatMa[nPF,1])
      cHoraA := aMatMa[nPF,3]
      nHoraA := aMatMa[nPF,4]

      If nAnoS <> nAnoX
         nAnoX := Val(aMatMa[nPF,2])
         nLinM := If(nQtdL = 1,80,230)
         nColP := If(nQtdL = 8,170,170)

         If nQtdL = 1
            oPrint:Box(li-60,nColS-(nColP+2),li+10,(nColS+2)-nColP)
         Else
            oPrint:Box(li-220,nColS-195,li-130,nColS-190)
         Endif

         oPrint:Say(li-nLinM,nColS-50,Str(nAnoX,4),oFont30)
      Endif

      If nQtdL = 1
         MNTR9902LI(150)
      Endif

      oPrint:Box(li-140,nColS-190,li-20,nColS+275)
      oPrint:Line(li-75,nColS-190,li-75,nColS+275)
      oPrint:Line(li-75,nColS+30 ,li-20,nColS+30)

      nCorC := 4
      If Val(aMatMa[nPF,2]) = Year(Date()) .And. NGSEMANANO(Date()) = nSemX
         nCorC := 1
      Endif

      nColP := If (nSemX > 9,0,40)
      oPrint:Say(li-140,nColS-nColP,Str(nSemX,2)+chr(170),oFont20,,aCores[nCorC,2])

      nCor  := 4
      If Val(aMatMa[nPF,2]) = Year(Date()) .And. NGSEMANANO(Date()) = nSemX
         If cHoraA <> '00:00' .Or. nHoraA > 0
            nCor  := 1
         Endif
      ElseIf Val(aMatMa[nPF,2]) <= Year(Date()) .And. nSemX < NGSEMANANO(Date())
         If cHoraA <> '00:00' .Or. nHoraA > 0
            nCor  := 3
         Endif
      ElseIf cHoraA <> '00:00' .Or. nHoraA > 0
         nCor := 2
      Endif

      nColP := (11-Len(Alltrim(cHoraA)))*10
      oPrint:Say(li-70,(nColS-130)+nColP,cHoraA,oFont09B,,aCores[nCor,2])
      nColP := Len(Space(16-Len(Alltrim((Str(nHoraA,13,2))))))*10
      oPrint:Say(li-70,(nColS-20)+nColP,Transform(nHoraA,"@E 99,999,999.99"),oFont09B,,aCores[nCor,2])

      nQtdL ++
      nColS += 465
      If nQtdL = 8
         nQtdL := 1
         nColS := 200
         MNTR9902LI(30)
      Endif

      nInc ++
      If nInc = 57 .And. nPF <= Len(aMatMa)
         lLeng := .t.
         MNTR990LE(2)
         nQtdL := 1
         nColS := 200
         nInc  := 1
         li    := 4000
         MNTR9902LI()
      Endif

      If nPF = Len(aMatMa) .And. !lLeng
         MNTR9902LI(30)
         MNTR990LE(2)
      Endif
   Next nPF
Endif
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR990LE ³ Autor ³ In cio Luiz Kolling   ³ Data ³26/11/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Impressao da legenda                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR990                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR990LE(nTip)
Local nSomL := If(nTip = Nil,0,150)
oPrint:Say((li+nSomL)-190,50,STR0021                        ,oFont13)
oPrint:Box((li+nSomL)-145,20,(li+nSomL)-20,350)
oPrint:Say((li+nSomL)-140,50,STR0022                        ,oFont09B,,aCores[3,2])
If nTip = Nil
   oPrint:Say((li+nSomL)-100,50 ,STR0023+" "+DtoC(Date())   ,oFont09B,,aCores[1,2])
Else
   If nTip = 2
      oPrint:Say((li+nSomL)-100,50 ,STR0025                 ,oFont09B,,aCores[1,2])
   Else
      oPrint:Say((li+nSomL)-100,50 ,STR0023+" "+DtoC(Date()),oFont09B,,aCores[1,2])
   Endif
Endif
oPrint:Say((li+nSomL)-60 ,50 ,STR0024                       ,oFont09B,,aCores[2,2])
Return .t.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR9902LI³ Autor ³ In cio Luiz Kolling   ³ Data ³26/11/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Cabecalho e incrementador por semana                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR990                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR9902LI(linhas)
    Local cTitA
    Default linhas := 50
    li += linhas
    If li >= 2500

        MNTR990LOG()

        MNTR9902LI(15)
        oPrint:Say(li-10,1000,STR0001,oFont40)

        MNTR9902LI(65)

        oPrint:Say(li-40,2770,Dtoc(MV_PAR07)+" "+STR0009+" "+Dtoc(MV_PAR08),oFont13)
        cTitA := IIf( mv_par09 == 2, cDef0209, cDef0109 )
        oPrint:Say(li+10,2870,cTitA,oFont13,,aCores[5,2])
        MNTR9902LI(70)

        oPrint:Say(li-20,2765,STR0010+" "+Dtoc(Date()),oFont13)
        oPrint:Say(li+25,2850,STR0011+" "+Time(),oFont13)
        oPrint:Box(li+70,10,li+2120,3285)
        MNTR9902LI(90)
        oPrint:Say(li,20  ,STR0003+" "+Alltrim(STF->TF_CODBEM)+" - "+NGSEEK("ST9",STF->TF_CODBEM,1,"T9_NOME"),oFont13)
        oPrint:Say(li,1600,STR0004+" "+STF->TF_SERVICO+" - "+NGSEEK("ST4",STF->TF_SERVICO,1,"T4_NOME"),oFont13)
        oPrint:Say(li,2890,STR0005+" "+STF->TF_SEQRELA,oFont13)
        oPrint:Line(li+60,10,li+60,3280)
        MNTR9902LI(100)
    Endif
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR990LOG³ Autor ³ In cio Luiz Kolling   ³ Data ³26/11/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³Impressao do logo da empresa                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR990                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR990LOG()
li := 10
nPag ++
oPrint:EndPage()
oPrint:StartPage()

cFileLogo := NGLOCLOGO()

If File(cFileLogo)
	oPrint:SayBitMap(55,55,cFileLogo,300,165)
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³MNTR990VCCºAutor  ³Wagner S. de Lacerdaº Data ³  29/07/2010 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Valida Ate Centro de Custo.                                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MNTR740                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR990VCC()

Local cZzCC := Replicate("Z",nSizeCC)

If mv_par04 <> cZzCC
	If ExistCpo("CTT",mv_par04)
		If !AteCodigo("CTT",mv_par03,mv_par04,nSizeCC)
			Return .F.
		EndIf
	Else
		Return .F.
	EndIf
EndIf

Return .T.
