#INCLUDE "MDTA075.ch"
#Include "Protheus.ch"

/*


Ŀ
Funo     MDTA075   Autor  Marcio Costa            Data  19/12/99 
Ĵ
Descrio  Programa de Cadastro de Usuarios Atendentes do Ambulatorio  
Ĵ
Uso        Generico                                                    
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                      
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                    
Ĵ
Elisangela C. 10/01/02000450Alteracao na Function NG075EXC,essa func.
                            nao estava deixado excluir medico que nao
                            possuia agenda, e tambem deixava excluir 
              |        |      |medico que ja possuia atendiment.efetuad.|
Ĵ
Andre E.Perez |13/10/05|003106|Alteracao na Function NG075AGE. Foi      
Alvarez       |        |      |incluida a opcao "Transferencia" de      
              |              consulta nela. Outras funcoes foram in-  
              |              cluidas para auxiliar nessa tarefa. Sao  
              |              elas: NG075MUD,VAL075DIA,ValidA075,      
              |              RESTORE075                               
ٱ


*/
Function MDTA075
//-----------------------------------------------
// Armazena variaveis p/ devolucao (NGRIGHTCLICK)
//-----------------------------------------------
Local aNGBEGINPRM := NGBEGINPRM()

Private cPROGRAMA := "MDTA075"
Private cCliMdtPs := Space(10)
PRivate cPerg := "MDT076    "
Private lTLF_DTAGEN := .t.
Private nSizeCODFUN := If((TAMSX3("RJ_FUNCAO")[1]) < 1,5,(TAMSX3("RJ_FUNCAO")[1]))
//---------------------------------------------------------
// Define Array contendo as Rotinas a executar do programa
// ----------- Elementos contidos por dimensao ------------
// 1. Nome a aparecer no cabecalho
// 2. Nome da Rotina associada
// 3. Usado pela rotina
// 4. Tipo de Transao a ser efetuada
//    1 - Pesquisa e Posiciona em um Banco de Dados
//    2 - Simplesmente Mostra os Campos
//    3 - Inclui registros no Bancos de Dados
//    4 - Altera o registro corrente
//    5 - Remove o registro corrente do Banco de Dados
//-------------------------------------------------------
PRIVATE aRotina := MenuDef()
PRIVATE lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )

//-----------------------------------------------
// Define o cabecalho da tela de atualizacoes
//-----------------------------------------------
PRIVATE cCadastro := OemtoAnsi(STR0007) //"Atendentes Ambulatorio"
PRIVATE aCHKDEL := {}, bNGGRAVA
Private look := .f.

pergunte(cPerg,.f.)

If AMiIn( 35 ) // Somente autorizado para SIGAMDT
	If FindFunction("MDTA076") .And. AliasInDic( "TY9" )
		ShowHelpDlg( STR0076 , ; //"ATENO"
				{ STR0167 } , 2 , ; //"A rotina de Agenda Mdica(MDTA075) foi descontinuada. "
				{ STR0168 } , 2 ) //"Voc ser direcionado para a Agenda Mdica Mod. 2(MDTA076)."

		MDTA076()
	Else

		//-----------------------------------------------------------------
		// aCHKDEL array que verifica a INTEGRIDADE REFERENCIAL na exclu-
		// so do registro.
		//
		// 1 - Chave de pesquisa
		// 2 - Alias de pesquisa
		// 3 - Ordem de pesquisa
		//------------------------------------------------------------------
		aCHKDEL := { {'TML->TML_CODUSU'    , "TMJ", 1}}

		If FindFunction("MDTRESTRI") .AND. !MDTRESTRI(cPrograma)
			//-----------------------------------------------
			// Devolve variaveis armazenadas (NGRIGHTCLICK)
			//-----------------------------------------------
			NGRETURNPRM(aNGBEGINPRM)
			Return .F.
		Endif
		If SuperGetMv("MV_NG2SEG",.F.,"2") == "1" .AND. !(SuperGetMV("MV_MDTPS",.F.,"N") == "S")
			dbSelectArea("TMK")
			dbSetOrder(1)
		EndIf

		//-----------------------------------------------
		// Endereca a funcao de BROWSE
		//-----------------------------------------------
		DbSelectArea("TML")
		DbSetOrder(1)
		mBrowse( 6, 1,22,75,"TML")
	EndIf
EndIf
//-----------------------------------------------
//  Devolve variaveis armazenadas (NGRIGHTCLICK)
//-----------------------------------------------
NGRETURNPRM(aNGBEGINPRM)
Return .T.

/*/


Ŀ
Funo     NG075AGE  Autor  Marcio Luiz da Costa   Data 21/03/2000
Ĵ
Descrio  AGENDA medica                                              
Ĵ
 Uso       Generico                                                   
ٱ

/*/
Function NG075AGE(lPesq)
Local nPos
Local aBkpMenu    := {}
Local oldDlg      := If(Type("oDlg") == "O",oDlg,Nil)
//Ŀ
// Define Array contendo as Rotinas a executar do programa      
// ----------- Elementos contidos por dimensao ------------     
// 1. Nome a aparecer no cabecalho                              
// 2. Nome da Rotina associada                                  
// 3. Usado pela rotina                                         
// 4. Tipo de Transao a ser efetuada                          
//    1 - Pesquisa e Posiciona em um Banco de Dados             
//    2 - Simplesmente Mostra os Campos                         
//    3 - Inclui registros no Bancos de Dados                   
//    4 - Altera o registro corrente                            
//    5 - Remove o registro corrente do Banco de Dados          
//
__lPesq := lPesq
If ValType(lPesq) <> "L"
	__lPesq := .F. //Define se esta funcao foi chamada pelo botao de pesquisa de consulta
Endif

oldROTINA := aCLONE(aROTINA)
look := .f.
PRIVATE cCadant := cCadastro
PRIVATE cCodusu := TML->TML_CODUSU
PRIVATE aRotina

If __lPesq
	aRotina :=	{ { STR0008, "AxPesqui"  , 0 , 1},;  //"Pesquisar "
	                 { STR0002, "NGCAD01"   , 0 , 2},;  //"Visualizar"
				  	 { STR0004, "MDT075CAD" , 0 , 4},;  //"Alterar"
					 { STR0005, "MDT075CAD" , 0 , 5, 3} } //"Excluir"
Else
	aRotina := { { STR0008, "AxPesqui"  ,0 ,1},; 	//"Pesquisar "
                 { STR0002, "NGCAD01"   ,0 ,2},; 	//"Visualizar"
                 { STR0003, "MDT075INC"   ,0 ,3},; 	//"Incluir"
                 { STR0004, "MDT075PRE"   ,0 ,4},; 	//"Alterar"
                 { STR0005, "MDT075PRE"   ,0 ,5 ,3}} //"Excluir"

  	If !lSigaMdtPS
		aAdd( aRotina , { STR0013,	"NG075MUD"  ,0 ,4} ) //"Transferencia"
		If SuperGetMv("MV_NG2AUDI",.F.,"2") == "1"
			aAdd( aRotina , { STR0163,"MDTA991('TMJ',{'TMJ_CODUSU','TMJ_USERGI'},{'"+cCodusu+"',STOD('"+DTOS(MDTDATALO("TML->TML_USERGI",,,.F.))+"')})" , 0 , 3 } )//"Hist. Exc."
		EndIf
	Endif
Endif


//Ŀ
// Define o cabecalho da tela de atualizacoes                   
//
cCadastro := STR0009 //"Agenda Medica"

cCadastro := OemtoAnsi(cCadastro)

#IFNDEF WINDOWS
        //Ŀ
        // Recupera o desenho padrao de atualizacoes                    
        //
        ScreenDraw("SMT050", 3, 0, 0, 0)

        //Ŀ
        // Display de dados especificos deste Programa                  
        //
        SetColor("b/w,,,")
        @ 3, 1 Say cCadastro
#ENDIF
        //Ŀ
        // Campos que serao mostrados na tela de agendamento de consulta
        //

aCHOICE := { "TMJ_CODUSU"   ,;
             "TMJ_NOMUSU"   ,;
             "TMJ_DTCONS"   ,;
             "TMJ_HRCONS"   ,;
             "TMJ_NUMFIC"   ,;
             "TMJ_NOMFIC"   ,;
             "TMJ_MOTIVO"   ,;
             "TMJ_NATEXA"   ,;
             "TMJ_NOMOTI"   ,;
             "TMJ_EXAME"    ,;
             "TMJ_NOMEXA"   ,;
             "TMJ_OBSCON"    }

If lSigaMdtPS
	aAdd( aCHOICE , "TMJ_CODCLI" )
	aAdd( aCHOICE , "TMJ_LOJCLI" )
	aAdd( aCHOICE , "TMJ_NOMCLI" )
Endif

If FindFunction( "MDTA076" )
	If TMJ->( FieldPos( "TMJ_QTDHRS" ) ) > 0
		aAdd( aCHOICE , "TMJ_QTDHRS" )
	EndIf
EndIf
If (nPos := aScan(aSMenu,{|x| "MDTRELHIS" $ x[2]})) > 0
	aBkpMenu := aClone(aSMenu)
	aSMenu[nPos,2] := MDT075TRO(@aSMenu,nPos,"TMJ")
EndIf

//Ŀ
//aCHKDEL array que verifica a INTEGRIDADE REFERENCIAL na exclu-
//so do registro.                                              
//                                                              
//1 - Chave de pesquisa                                         
//2 - Alias de pesquisa                                         
//3 - Ordem de pesquisa                                         
//
aCHKDEL := { }
//Ŀ
// Endereca a funcao de BROWSE                                  
//
bNGGRAVA  := {|| MDT075VAL(.f.)}
DbSelectArea("TMJ")
DbSetOrder(1)
If !__lPesq
	If lSigaMdtPS
		SET FILTER TO TMJ_FILIAL == xFilial('TMJ') .AND. TMJ_CODUSU == cCodusu
		bNGGRAVA  := {|| MDT075VAL(.t.,.t.)}
	Else
		SET FILTER TO TMJ_FILIAL == xFilial('TMJ') .AND. TMJ_CODUSU == cCodusu .AND. EMPTY(TMJ_DTATEN)
	Endif
Endif

//Valida a existencia do calendario (arquivo SH7)
If !NGIFDBSEEK("SH7",TML->TML_CALEND,1)
	ShowHelpDlg(STR0076,	{STR0156+Space(01)+STR0157+": "+TML->TML_CALEND},3,;//"ATENO"  //"Calendrio do atentende no existe no cadastro de calendrios."##"Calendrio"
								{STR0158},3)  //"Ajustar o cadastro com calendrio vlido ou cadastrar o calendrio que est invlido."
Else
	If ExistBlock("MDTA0751")  //Ponto de entrada
		ExecBlock("MDTA0751",.F.,.F.)
	Endif


	mBrowse( 6, 1,22,75,"TMJ")
EndIf

aROTINA   := aCLONE(oldROTINA)
cCadastro := cCadant

DbSelectArea("TMJ")
Set Filter To
DbSetOrder(1)
dBSEEK(XFILIAL("TMJ"))
If Len(aBkpMenu) > 0
	aSMenu := aClone(aBkpMenu)
EndIf
bNGGRAVA  := {}
If ValType(oldDlg) == "O"
	oDlg      := oldDlg
EndIf
Return .T.

/*/


Ŀ
Funo     NG075EXC  Autor  Marcio Luiz da Costa   Data 21/03/2000
Ĵ
Descrio  Exclui o medico  da tabela de Atendentes e todas as        
           consultas marcadas para este medico da tabela - TMJ.       
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function NG075EXC(cAlias,nReg,nOpcx)

If FindFunction("MDTRESTRI") .AND. NGCADICBASE("TMK_USUARI","A","TMK",.F.) .AND. !MDTRESTUS(MDTDATALO("TML->TML_USERGI",.F.))
	Return .F.
Endif

If nOpcx == 5 .AND. Funname() == "MDTA075" // Excluir. Valida se o usurio possui agenda para excluir
	If fValidExc(TML->TML_CODUSU)
		Return .F.
	EndIf
Endif

look := .t.

bNGGRAVA := {|x| CHKDEL075()}
nRet :=  NGCAD01("TML", nReg, 5)
bNGGRAVA := {}

If nRet = 1
   DbSelectArea("TMJ")
   DbSetOrder(1)
   IF DBSEEK(XFILIAL("TMJ")+ TML->TML_CODUSU)
        DbSelectArea("TMT")
        DbSetOrder(2)
        IF !DBSEEK(XFILIAL("TMT")+ TML->TML_CODUSU)
           DbSelectArea("TMJ")
           Do While !EOF()                        .AND. ;
             TMJ->TMJ_FILIAL == xFilial("TML")   .AND. ;
             TMJ->TMJ_CODUSU == TML->TML_CODUSU
               RecLock("TMJ",.F.)
               DBDELETE()
               MSUNLOCK("TMJ")
               DbSkip()
           EndDo
           DbSelectArea("TML")
           RecLock("TML",.F.)
           DBDELETE()
           MSUNLOCK("TML")
        endif
   ELSE
       DbSelectArea("TML")
       RecLock("TML",.F.)
         DBDELETE()
       MSUNLOCK("TML")
   ENDIF
   DbSelectArea("TML")
   DbSetOrder(1)
Endif

Return .t.

/*/


Ŀ
Funo     MD075VAL  Autor  Marcio Luiz da Costa   Data 21/03/2000
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function MD075VAL()
Return .t.
/*/


Ŀ
Funo    MDT075VAL  Autor Denis Hyroshi de Souza  Data 06/04/2004
Ĵ
 Uso       Verifica se o atendente ja tem consulta para esse horario  
ٱ


/*/
Function MDT075VAL(lFiltro,lSemTRB)
Local cMsg
Local aArea := GetArea()
Local cCodUsu := ""  // Codigo do usuario corrente (memoria)
Local cCodCal := ""  // Codigo do calendario

//utilizado na Query
//--------------
Local cAliasTMJ := GetNextAlias()
Local cTabTMJ := RetSqlName("TMJ")
//--------------

// Recebe o usuario, da memoria
cCodUsu := M->TMJ_CODUSU

// Busca o codigo do calendario na TML
dbSelectArea("TML")
dbSetOrder(1)
If dbSeek(xFilial("TML")+cCodUsu)
	cCodCal := TML->TML_CALEND
EndIf

Default lSemTRB := .f.
Private aAreaTMJ := TMJ->(Getarea())

if lFiltro .and. (Altera .Or. Inclui)
	nRecnoTMJ := TMJ->(Recno())
	dDtCons := M->TMJ_DTCONS
	cHrCons := M->TMJ_HRCONS
	If Empty(dDtCons)
		MsgStop(STR0038) //"Campo Data Consulta  obrigatrio."
		Return .f.
	Endif
	If Empty(cHrCons)
		MsgStop(STR0039) //"Campo Hora Consulta  obrigatrio."
		Return .f.
	Endif
	If !NGVALHORA(cHrCons,.t.)
		Return .f.
	Endif

	dbSelectArea("TMJ")
	Set Filter To

	If ! ( MDTDIS075() .and. VALHR075(.f.,cCaleMed,dDtCons,cHrCons,'I',M->TMJ_NUMFIC,M->TMJ_NATEXA,TMJ->TMJ_HRCONS) )
		Return .f.
	Endif

	If !lSemTRB
		dbSelectArea("TMJ")
		dbSetOrder(6) //TMJ_FILIAL+TMJ_NUMFIC
		Set Filter To  TMJ_FILIAL == xFilial("TMJ") .and. TMJ_NUMFIC == TRBK->NUMFIC .and. TMJ_CODUSU == M->TMJ_CODUSU
	Else
		SET FILTER TO TMJ_FILIAL == xFilial('TMJ') .AND. TMJ_CODUSU == cCodusu
	Endif

	Dbselectarea("TMJ")
	Dbsetorder(11)  //TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS)+TMJ_HRCONS+TMJ_NUMFIC
	If Dbseek(xFilial("TMJ") + M->TMJ_CODUSU + DtoS(dDtCons) +  cHrCons + M->TMJ_NUMFIC)
		If nRecnoTMJ <> TMJ->(Recno())
			msgStop( STR0023 ) //"Ja existe consulta marcada nesse horario para este funcionrio."
			Return .f.
		Endif
	Endif
ElseIf Inclui
	If !CompoeSesmt(M->TMJ_CODUSU,.f.,.f.)
		cMsg := STR0040 //"No ser possvel incluir consulta na agenda, pois o usurio no compe mais o Sesmt."
		HELP(" ",1,STR0027,STR0027,Alltrim(Memoline(cMsg,50,1))+CHR(13)+CHR(10)+Memoline(cMsg,50,2),3,4) //"ATENCAO"###"ATENCAO"
		Return .f.
	Endif

	RESTORE075(1)	//Excluir filtro das consultas

	//verifica se existe algum registro igual ao que esta sendo incluso
	cQuery := "SELECT COUNT( * ) AS CONT "
	cQuery += "FROM " + cTabTMJ + " TMJ "
	cQuery += "WHERE TMJ.TMJ_FILIAL = "+ ValToSql( xFilial( "TMJ" ) ) +" AND TMJ.TMJ_CODUSU = "+ ValToSql( cCodUsu ) +" AND "
	cQuery += "TMJ.TMJ_DTCONS = "+ ValToSql( DTOS( M->TMJ_DTCONS ) ) +" AND "
	cQuery += 		"TMJ.TMJ_HRCONS = "+ ValToSql( M->TMJ_HRCONS ) +" AND TMJ.D_E_L_E_T_ <> '*'"
	cQuery := ChangeQuery(cQuery)

	MPSysOpenQuery( cQuery , cAliasTMJ )

	DbSelectArea( cAliasTMJ )
	If ( cAliasTMJ )->CONT > 0
		MsgStop(STR0012) //"Ja existe consulta marcada nesse horario para este atendente."
		RestArea(aArea)

		RESTORE075(2)	//Atualizar filtro das consultas
		Return .f.
	Endif
	RESTORE075(2)	//Atualizar filtro das consultas
Endif

If INCLUI

	If FindFunction( "MDTA076" ) .And. AliasInDic( "TY9" )
		//Verifica se horario informado termina em 0 ou 5
		If !MDT076HOR( M->TMJ_HRCONS )
			Return .F.
		Endif

		If !MDT076INT( "TMJ", .T. )//Chama funo para verificar possiveis interferencias de horarios
			Return .F.
		EndIf

	EndIf
	If !MDT75VLHRC(cCodCal,M->TMJ_DTCONS,M->TMJ_HRCONS,"I",.T.,IsInCallStack( "MDTA076" ))
		RestArea(aArea)
		Return .F.
	EndIf
EndIf

	If ExistBlock("MDTA0752")  //Ponto de entrada
		ExecBlock("MDTA0752",.F.,.F.)
	Endif

RestArea(aArea)
Return .t.
/*/


Ŀ
Funo     CHKDEL075 Autor  Denis Hyroshi de Souza Data 15/08/2002
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function CHKDEL075()

IF look
	DbSelectArea("TMJ")
	DbSetOrder(1)
	IF DBSEEK(XFILIAL("TMJ")+ TML->TML_CODUSU)
	    DbSelectArea("TMT")
	    DbSetOrder(2)
	    IF DBSEEK(XFILIAL("TMT")+ TML->TML_CODUSU)
       		Help(" ",1,"NGSMEDATEN") //"Esse medico nao pode ser excluido pois possui atendimento efetuado"
	        Return.f.
	    ENDIF
	ENDIF
ENDIF
look := .f.
Return .t.

/*/


Ŀ
Funo     NG075PSAGE Autor                         Data           
Ĵ
 Uso                                                                  
ٱ

/*/
Function NG075PSAGE

	Local GetList         := {},xx1,ic,iz,nx23,nCont
	Local oPanel
	Local OldROT := aClone(aRotina)

	Local oCbx
	Local aFind := {STR0041,STR0042} //"R.G. do Funcionrio"###"Nome do Funcionrio"
	Local cFind := aFind[2]
	Local cValPesq := Space(15)
	Local bValPesq
	Local nEpoch := Set( _SET_EPOCH , 1950 )	//Altera o Set Epoch para 1950
	Local nFld
	Local aFields
	Local cField
	Local cValidHrC

	Private cExaEEG := Alltrim(NGSEEK("TM4","000038",1,"TM4->TM4_NOMEXA"))
	Private cExaECG := Alltrim(NGSEEK("TM4","000041",1,"TM4->TM4_NOMEXA"))

	Private aRotina  := {	{STR0001 ,"AxPesqui",0 ,1},; //"Pesquisar"
							{STR0002,"NGCAD02" ,0 ,2},; //"Visualizar"
							{STR0003   ,"MDTA075" ,0 ,3},; //"Incluir"
							{STR0004   ,"MDTA075" ,0 ,4}} //"Alterar"

	Private cFICHANOME := Space(9)
	Private lProValor := .f.
	Private oDlg,oGet,xx
	Private oCPO01,oCPO02,oCPO03,oCPO04,oCPO05,oCPO06,oCPO07
	Private nItens := 0

	Private cFunc075      := Space(5)
	Private cSeto075      := Space(20)
	Private nSizeSA1      := If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1]))
	Private nSizeLo1      := If((TAMSX3("A1_LOJA")[1]) < 1,2,(TAMSX3("A1_LOJA")[1]))
	Private nSizeSA2      := If((TAMSX3("A2_COD")[1]) < 1,6,(TAMSX3("A2_COD")[1]))
	Private nSizeLo2      := If((TAMSX3("A2_LOJA")[1]) < 1,2,(TAMSX3("A2_LOJA")[1]))
	Private nSizeCli,nSizeLoj
	nSizeCli := If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1]))
	nSizeLoj := If((TAMSX3("A1_LOJA")[1]) < 1,2,(TAMSX3("A1_LOJA")[1]))
	Private aSemana       := {STR0043,STR0044,STR0045,STR0046,STR0047,STR0048,STR0049} //"DOMINGO"###"SEGUNDA-FEIRA"###"TERA-FEIRA"###"QUARTA-FEIRA"###"QUINTA-FEIRA"###"SEXTA-FEIRA"###"SBADO"

	//Campos da tela
	Private M->TM0_CODFUN := Space(5)
	Private M->TMJ_DTCONS := dDataBase
	nDIA := DOW(M->TMJ_DTCONS)
	Private cDiaSemana    := aSemana[nDIA]
	Private M->TMJ_CODUSU := TML->TML_CODUSU
	Private cCaleMed      := TML->TML_CALEND
	Private cNomeMed      := NGSEEK("TMK",M->TMJ_CODUSU,1,"TMK->TMK_NOMUSU")
	Private cPer01De      := "  :  "
	Private cPer02De      := "  :  "
	//Private cPer03De      := "  :  "
	Private cPer01Ate     := "  :  "
	Private cPer02Ate     := "  :  "
	//Private cPer03Ate     := "  :  "
	Private nTotDia       := 0
	Private nTotTarde     := 0
	Private nTotlimDia    := 0
	Private nTotlimTar    := 0
	Private nIntervalo    := 10
	Private aCols         := {}

	//Variaveis de controle
	Private lConSA2       := .t.
	Private lConSA1       := .f.
	Private lWhenEpi      := .t.
	Private lPcmsoZ       := .f.
	Private LENUMFIC      := .T.
	Private INCLUI        := .T.
	Private aHorarios     := { }
	Private aHorMedic     := { }

	//Valida a existencia do calendario (arquivo SH7)
	If !NGIFDBSEEK("SH7",TML->TML_CALEND,1)
		ShowHelpDlg(STR0076,	{STR0156+Space(01)+STR0157+": "+TML->TML_CALEND},3,;//"ATENO"  //"Calendrio do atentende no existe no cadastro de calendrios."##"Calendrio"
									{STR0158},3)  //"Ajustar o cadastro com calendrio vlido ou cadastrar o calendrio que est invlido."
		aRotina := aClone(OldROT)
		Set( _SET_EPOCH , nEpoch )
		Return .F.
	EndIf

	NGHORA075(TML->TML_CALEND,M->TMJ_DTCONS)

	aMedicos := fHorMedic(M->TMJ_DTCONS)
	nMedicosDia := aMedicos[1]
	nMedicosTar := aMedicos[2]
	nIntervalo  := aMedicos[3]

	If nIntervalo > 0 .and. Alltrim(cPer01De) <> ":" .and. Alltrim(cPer01Ate) <> ":"
		nDif := HTOM(cPer01Ate) - HTOM(cPer01De)
		If nDif > 0
			nConsulta := nDif / nIntervalo
			//If Mod(nDif,nIntervalo) <> 0
			nConsulta := Int(nDif / nIntervalo)+1
			//Endif
			nTotlimDia := nConsulta * nMedicosDia
		Endif
	Endif
	If nIntervalo > 0 .and. Alltrim(cPer02De) <> ":" .and. Alltrim(cPer02Ate) <> ":"
		nDif := HTOM(cPer02Ate) - HTOM(cPer02De)
		If nDif > 0
			nConsulta := nDif / nIntervalo
			//If Mod(nDif,nIntervalo) <> 0
			nConsulta := Int(nDif / nIntervalo)+1
			//Endif
			nTotlimTar := nConsulta * nMedicosTar
		Endif
	Endif
	nItens := nTotlimDia+nTotlimTar

	aALIAS := "TMJ"
	DbSelectArea("TMJ")
	DbGOBOTTOM()
	DbSKIP()

	cValidHrC := 'VALHR075(.t.,cCaleMed,M->TMJ_DTCONS,M->TMJ_HRCONS,"I",aCols[n,nFicha],aCols[n,nMotiv],aCols[n,nHORAC]) .and. SAVEMDT075(1)'

	nUsado  := 0
	aHeader := {}
	aExc    := { { 'TMJ_HRCONS', 'X3_VALID',  cValidHrC                                          }, ;
				 { 'TMJ_NUMFIC', 'X3_VALID', 'fMDT075TM0() .and. SAVEMDT075(2)'                  }, ;
				 { 'TMJ_NUMFIC', 'X3_F3'   , 'TM0'                                               }, ;
				 { 'TM0_NOMFIC', 'X3_VALID', 'MDT075NOME() .and. SAVEMDT075(3)'                  }, ;
				 { 'TMJ_MOTIVO', 'X3_VALID', 'EXISTCPO("TMS",M->TMJ_MOTIVO) .and. SAVEMDT075(7)' }, ;
				 { 'TMJ_NATEXA', 'X3_VALID', 'MDT075NATX() .and. SAVEMDT075(4)'                  }, ;
				 { 'TMJ_OBSCON', 'X3_VALID', 'SAVEMDT075(6)'                                     }, ;
				 { 'TOL_CLIENT', 'X3_VALID', 'EXISTCPO("SA1",M->TOL_CLIENT)'                     }, ;
				 { 'TOL_CLIENT', 'X3_F3'   , 'SA1'                                               }, ;
				 { 'TOL_LOJACL', 'X3_VALID', 'EXISTCPO("SA1",aCols[n,nCLIEN]+M->TOL_LOJACL)'     }  }


	aCampos := { 'TMJ_HRCONS', 'TMJ_NUMFIC', 'TM0_NOMFIC', 'TMJ_MOTIVO', 'TMJ_NATEXA', ;
				 'TMJ_NOMEXA', 'TMJ_OBSCON', 'TOL_CLIENT', 'TOL_LOJACL', 'TOL_NOMECL'  }

	aHeader := NGHeadExc( aCampos, .F., .F., .F., aExc )

	DbSelectArea("SX3")
	DbSetOrder(1)

	cALIAS := "TMJ"
	dbSelectArea("TMJ")
	dbSetOrder(1)
	dbSeek(xFilial("TMJ")+TML->TML_CODUSU+DTOS(M->TMJ_DTCONS))
	nCnt := 0
	While !EOF() .And. TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS) == xFilial("TMJ")+TML->TML_CODUSU+DTOS(M->TMJ_DTCONS)
		nCnt++
		dbSkip()
	End

	Private nFICHA := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TMJ_NUMFIC"})
	Private nNOFIC := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TM0_NOMFIC"})
	Private nMOTIVO := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TMJ_MOTIVO"})
	//Private nFUNCC := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TM0_NOMEFU"})
	Private nNOEXA := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TMJ_NOMEXA"})
	Private nHORAC := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TMJ_HRCONS"})
	Private nMOTIV := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TMJ_NATEXA"})
	//Private nNOMOT := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TMJ_NOMOTI"})
	Private nOBSER := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TMJ_OBSCON"})
	Private nCLIEN := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TOL_CLIENT"})
	Private nLOJAC := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TOL_LOJACL"})
	Private nNOMEC := aSCAN(aHEADER,{|x| TRIM(UPPER(x[2])) == "TOL_NOMECL"})

	Monta_Agenda(.f.)

	nOpcx := 3
	nOpca := 1

	Private aSize := MsAdvSize(,.f.,430), aObjects := {}
	Aadd(aObjects,{050,050,.t.,.t.})
	Aadd(aObjects,{020,020,.t.,.t.})
	Aadd(aObjects,{100,100,.t.,.t.})
	aInfo := {aSize[1],aSize[2],aSize[3],aSize[4],0,0}
	aPosObj := MsObjSize(aInfo, aObjects,.t.)

	DEFINE MSDIALOG oDLG FROM aSize[7],0 To aSize[6]-10,aSize[5]-20 TITLE cCadastro PIXEL

	@ 020,020 MSPANEL oPanel OF oDlg
	oPanel:Align := CONTROL_ALIGN_TOP
	oPanel:nHeight := 220

	@ 005,008 say OemtoAnsi(STR0052) of oPanel Pixel //"Data"
	@ 005,040 Msget M->TMJ_DTCONS Picture '99/99/9999' Valid (Naovazio(M->TMJ_DTCONS).and. fMDT075TMK(2)) SIZE 43,8 of oPanel Pixel
	@ 005,090 Msget cDiaSemana Picture '@!' SIZE 50,8 When .f. of oPanel Pixel

	@ 020,008 say OemtoAnsi(STR0023) of oPanel Pixel //"Medico"
	@ 020,040 Msget M->TMJ_CODUSU Picture '@!' Valid (EXISTCPO("TMK",M->TMJ_CODUSU) .and. fMDT075TMK(1)) F3 "TMK" SIZE 60,8 When .f. of oPanel Pixel
	@ 020,110 say OemtoAnsi(STR0018) of oPanel Pixel //"Nome"
	@ 020,130 Msget cNomeMed SIZE 100,8 When .f. of oPanel Pixel
	@ 035,008 say OemtoAnsi(STR0053) of oPanel Pixel //"Calendario"
	@ 035,040 Msget cCaleMed SIZE 30,8 When .f. of oPanel Pixel
	@ 035,080 say OemtoAnsi(STR0054) of oPanel Pixel //"Intervalo entre consultas"
	@ 035,145 Msget nIntervalo Picture '999' Valid Positivo(nIntervalo) .and. Monta_Agenda(.t.) SIZE 25,8 When .f. of oPanel Pixel
	@ 035,171 say OemtoAnsi(STR0055) of oPanel Pixel //"(minutos)"


	@ 50,010 BUTTON STR0056  OF oPanel SIZE 58,10 PIXEL ACTION (NG075AGE3()) //"Histrico Consultas"
	@ 63,010 BUTTON STR0057      OF oPanel SIZE 58,10 PIXEL ACTION (MDT075PERF()) //"Consulta Perfil"
	@ 76,010 BUTTON STR0058 OF oPanel SIZE 58,10 PIXEL ACTION (MDT075ALFI(.t.)) //"Alterar Ficha Medica"
	@ 89,010 BUTTON STR0059  OF oPanel SIZE 58,10 PIXEL ACTION (MDT075Trans()) //"Transferir consulta"

	bValPesq := {|| If ( cFind==STR0041, cValPesq:=Space(15), cValPesq:=Space(40) ) } //"R.G. do Funcionrio"

	//
	//Pesquisa Consulta
	//
	Private lPrimeiro := .F.
	@ 051,080 TO 98,230 LABEL STR0060 of oPanel Pixel //"Pesquisa de Consultas"

	@ 060,087 say OemtoAnsi(STR0061) of oPanel Pixel //"Objeto de Pesquisa: "
	@ 057.8,140  COMBOBOX oCbx VAR cFind ITEMS aFind SIZE 70,3 Of oPanel Pixel Valid Eval(bValPesq)
	@ 074,087 say OemtoAnsi(STR0062) of oPanel Pixel //"Valor a procurar: "
	@ 073.4,140 Msget cValPesq SIZE 70,6 When .T. of oPanel Pixel

	@ 085,087 BUTTON STR0063 OF oPanel SIZE 45,10 PIXEL ACTION (MDT075Pesq(cFind,cValPesq)) //"Localizar"

	@ 003,240 TO 36,390 LABEL STR0064 of oPanel Pixel //"Horario de Atendimento"
	@ 012,248 say OemtoAnsi(STR0065) of oPanel Pixel //"1 Perodo"
	@ 012,280 Msget cPer01De Picture '@!' SIZE 30,8 When .f. OF oPanel PIXEL
	@ 012,312 say OemtoAnsi(STR0066) of oPanel Pixel //"as"
	@ 012,320 Msget cPer01Ate Picture '@!' SIZE 30,8 When .f. OF oPanel PIXEL
	@ 024,248 say OemtoAnsi(STR0067) of oPanel Pixel //"2 Perodo"
	@ 024,280 Msget cPer02De Picture '@!' SIZE 30,8 When .f. OF oPanel PIXEL
	@ 024,312 say OemtoAnsi(STR0066) of oPanel Pixel //"as"
	@ 024,320 Msget cPer02Ate Picture '@!' SIZE 30,8 When .f. OF oPanel PIXEL
	//@ 036,248 say OemtoAnsi("3 Perodo") of oPanel Pixel
	//@ 036,280 Msget cPer03De Picture '@!' SIZE 30,8 When .f. OF oPanel PIXEL
	//@ 036,310 say OemtoAnsi("as") of oPanel Pixel
	//@ 036,320 Msget cPer03Ate Picture '@!' SIZE 30,8 When .f. OF oPanel PIXEL

	@ 051,240 TO 98,390 LABEL STR0068 of oPanel Pixel //"Atendimentos no Dia"
	@ 060,248 say OemtoAnsi(STR0069) of oPanel Pixel //"Quantidade:"
	@ 060,282 say OemtoAnsi(STR0070) of oPanel Pixel //"Manh"
	@ 060,300 Msget oCPO02 Var nTotDia   Picture '999' SIZE 30,8 When .f. OF oPanel PIXEL
	@ 060,335 say OemtoAnsi(STR0071) of oPanel Pixel //"Tarde"
	@ 060,350 Msget oCPO03 Var nTotTarde Picture '999' SIZE 30,8 When .f. OF oPanel PIXEL
	@ 072,248 say OemtoAnsi(STR0072) of oPanel Pixel //"Limite Aten.:"
	@ 072,282 say OemtoAnsi(STR0070) of oPanel Pixel //"Manh"
	@ 072,300 Msget oCPO04 Var nTotlimDia Picture '999' SIZE 30,8 When .f. OF oPanel PIXEL
	@ 072,335 say OemtoAnsi(STR0071) of oPanel Pixel //"Tarde"
	@ 072,350 Msget oCPO05 Var nTotlimTar Picture '999' SIZE 30,8 When .f. OF oPanel PIXEL
	@ 084,248 say OemtoAnsi(STR0073) of oPanel Pixel //"Mdicos"
	@ 090,248 say OemtoAnsi(STR0074) of oPanel Pixel //"Disponveis"
	@ 084,282 say OemtoAnsi(STR0070) of oPanel Pixel //"Manh"
	@ 084,300 Msget oCPO06 Var nMedicosDia Picture '999' SIZE 30,8 When .f. OF oPanel PIXEL
	@ 084,335 say OemtoAnsi(STR0071) of oPanel Pixel //"Tarde"
	@ 084,350 Msget oCPO07 Var nMedicosTar Picture '999' SIZE 30,8 When .f. OF oPanel PIXEL

	oGet := MSGetDados():New(90,1,300,315,nOpcx,"MDTA075LOK","AllwaysTrue()",,.f.,,1,,nItens,,,,"NG075EXCC(.t.)")
	oGet:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

	ACTIVATE MSDIALOG oDlg ON INIT ( EnchoiceBar(oDlg,{||nOpca:=2,if(oGet:TudoOk(),oDlg:End(),nOpca := 1)},{||nOpca:=1,oDlg:End()})) CENTERED

	If Type("M->TMJ_DTCONS") == "D"
		If !Empty(M->TMJ_DTCONS)
			DelReserv(" ",M->TMJ_DTCONS,"99:99")
		Endif
	Endif

	aRotina := aClone(OldROT)

	Set( _SET_EPOCH , nEpoch )

	If nOpca == 1
		Return NIL
	Endif

Return .t.

/*/


Ŀ
Funo    MDT075DIA  Autor                         Data           
Ĵ
Descrio                                                             
ٱ


/*/
Function MDT075DIA()
nDIA := DOW(M->TMJ_DTCONS)
If nDIA > 0
	cDiaSemana  := aSemana[nDIA]
	aMedicos := fHorMedic(M->TMJ_DTCONS)
	nMedicosDia := aMedicos[1]
	nMedicosTar := aMedicos[2]
	nIntervalo  := aMedicos[3]
	If .f. //M->TMJ_DTCONS < dDataBase
		nMedicosDia := fTotConDia(M->TMJ_CODUSU,M->TMJ_DTCONS,nMedicosDia,1)
		nMedicosTar := fTotConDia(M->TMJ_CODUSU,M->TMJ_DTCONS,nMedicosTar,2)
	Endif
	nTotlimDia  := 0
	nTotlimTar  := 0
	If nIntervalo > 0 .and. Alltrim(cPer01De) <> ":" .and. Alltrim(cPer01Ate) <> ":"
		nDif := HTOM(cPer01Ate) - HTOM(cPer01De)
		If nDif > 0
			nConsulta := nDif / nIntervalo
			//If Mod(nDif,nIntervalo) <> 0
			nConsulta := Int(nDif / nIntervalo)+1
			//Endif
			nTotlimDia := nConsulta * nMedicosDia
		Endif
	Endif
	If nIntervalo > 0 .and. Alltrim(cPer02De) <> ":" .and. Alltrim(cPer02Ate) <> ":"
		nDif := HTOM(cPer02Ate) - HTOM(cPer02De)
		If nDif > 0
			nConsulta := nDif / nIntervalo
			//If Mod(nDif,nIntervalo) <> 0
			nConsulta := Int(nDif / nIntervalo)+1
			//Endif
			nTotlimTar := nConsulta * nMedicosTar
		Endif
	Endif
	nItens := nTotlimDia+nTotlimTar
Endif

lRefresh := .t.
Return .t.
/*/


Ŀ
Funo    fMDT075TMK Autor Denis Hyroshi de Souza  Data           
Ĵ
Descrio  Altera o nome do Medico                                    
ٱ


/*/
Function fMDT075TMK(nTipo)
Local aArea := GetArea()
Local aAreaTML := TML->(GetArea())

If nTipo <> 2

	Dbselectarea("TMK")
	Dbsetorder(1)
	Dbseek(xFilial("TMK")+M->TMJ_CODUSU)
	cNomeMed := TMK->TMK_NOMUSU

	Dbselectarea("TML")
	Dbsetorder(1)
	If !Dbseek(xFilial("TML")+M->TMJ_CODUSU)
		RestArea(aAreaTML)
		RestArea(aArea)
		MsgStop(Alltrim(cNomeMed)+" "+STR0075,STR0076) //"no tem horrio de atendimento cadastrado."###"ATENO"
		Return .f.
	Endif
	NGHORA075(TML->TML_CALEND,M->TMJ_DTCONS)
	cCaleMed := TML->TML_CALEND
Else
	Dbselectarea("TML")
	Dbsetorder(1)
	Dbseek(xFilial("TML")+M->TMJ_CODUSU)
	NGHORA075(TML->TML_CALEND,M->TMJ_DTCONS)
	cCaleMed := TML->TML_CALEND
	MDT075DIA()
Endif

Monta_Agenda(.t.)

oCPO02:REFRESH(.T.)
oCPO03:REFRESH(.T.)
oCPO04:REFRESH(.T.)
oCPO05:REFRESH(.T.)
oCPO06:REFRESH(.T.)
oCPO07:REFRESH(.T.)
oGet:FORCEREFRE()
RestArea(aAreaTML)
RestArea(aArea)
lRefresh := .t.
Return .t.
/*/


Ŀ
Funo    fMDT075TM0 Autor Denis Hyroshi de Souza  Data           
Ĵ
Descrio  Valida campo Ficha Medica                                  
ٱ


/*/
Function fMDT075TM0()
Local aArea := GetArea()
Local l2ConCanc := 0
Local nSizeCli := If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1]))
Local nSizeLoj := If((TAMSX3("A1_LOJA")[1]) < 1,2,(TAMSX3("A1_LOJA")[1]))

If !Empty(cFunc075)
	If M->TMJ_NUMFIC == PadR(cFunc075,Len(M->TMJ_NUMFIC))
		If !Empty(NGSEEK("SRJ",cFunc075,1,"SRJ->RJ_DESC"))
			M->TMJ_NUMFIC := TM0->TM0_NUMFIC
		Endif
	Endif
Endif
If !Empty(cSeto075)
	If M->TMJ_NUMFIC == PadR(cSeto075,Len(M->TMJ_NUMFIC))
		If !Empty(NGSEEK("CTT",cSeto075,1,"CTT->CTT_DESC01"))
			M->TMJ_NUMFIC := TM0->TM0_NUMFIC
		Endif
	Endif
Endif

Dbselectarea("TM0")
Dbsetorder(1)
If !Dbseek(xFilial("TM0")+M->TMJ_NUMFIC)
	Help(" ",1,"REGNOIS")
	RestArea(aArea)
	Return .f.
Endif

dbSelectArea("TMJ")
dbSetOrder(11)
IF dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+aCOLS[n][nHORAC]+aCOLS[n][nFICHA])
	If !EMPTY(TMJ_DTATEN)
		MsgStop(STR0077) //"Esta consulta no pode ser alterada, pois j foi realizada."
		RestArea(aArea)
		Return .f.
	Endif
Endif

If NGSEEK("SRA", TM0->TM0_MAT ,1,'SRA->RA_SITFOLH') == "D"
	cRG__TM0  := TM0->TM0_RG
	If !MDT075ALFI(.f.,M->TMJ_NUMFIC,1)
		cRG__TM0  := nil
		Return .f.
	Endif
	cRG__TM0  := nil
Endif

If NGSEEK("SA1", Substr(TM0->TM0_CC,1,nSizeCli+nSizeLoj) ,1,'SA1->A1_MSBLQL') == "1"
	If MsgYesNo(STR0078) //"Este funcionrio trabalha em um cliente inativo. Deseja ajustar o cadastro do funcionrio?"
		If !MDT075ALFI(.f.,M->TMJ_NUMFIC,1)
			Return .f.
		Endif
	Else
		Return .f.
	Endif
Endif
If NGSEEK("CTT", TM0->TM0_CC ,1,'CTT->CTT_MSBLQL') == "1"
	If MsgYesNo(STR0079) //"Este funcionrio trabalha em um setor bloqueado. Deseja ajustar o cadastro do funcionrio?"
		If !MDT075ALFI(.f.,M->TMJ_NUMFIC,1)
			Return .f.
		Endif
	Else
		Return .f.
	Endif
Endif

dbSelectArea("TMJ")
dbSetOrder(11)
IF dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+aCOLS[n][nHORAC]+M->TMJ_NUMFIC)
   Help(" ",1,"JAEXISTINF")
   RestArea(aArea)
   Return .f.
Endif

l2AchoCon := .f.
Dbselectarea("TOL")
Dbsetorder(2)
Dbseek(xFilial("TOL")+TM0->TM0_CLIENT+TM0->TM0_LOJA)
While !eof() .and. xFilial("TOL")+TM0->TM0_CLIENT+TM0->TM0_LOJA == TOL->(TOL_FILIAL+TOL_CLIENT+TOL_LOJACL) .and. !l2AchoCon
	//TOL->TOL_DTINIC <= dDataBase .and. TOL->TOL_DTTERM >= dDataBase .and.
	If TOL->TOL_STATUS $ "1/3" .and. TOL->TOL_TIPCON == "1"
		l2AchoCon := .t.
	ElseIf TOL->TOL_STATUS == "4"
		l2ConCanc := 4
	ElseIf TOL->TOL_STATUS == "2"
		l2ConCanc := 2
	Endif
	Dbselectarea("TOL")
	Dbskip()
End
If !l2AchoCon
	Dbselectarea("TOL")
	Dbsetorder(2)
	Dbseek(xFilial("TOL")+TM0->TM0_CLIENT)
	cUniTemp := TOL->TOL_LOJACL
	While !eof() .and. xFilial("TOL")+TM0->TM0_CLIENT+cUniTemp == TOL->(TOL_FILIAL+TOL_CLIENT+TOL_LOJACL) .and. !l2AchoCon
		//TOL->TOL_DTINIC <= dDataBase .and. TOL->TOL_DTTERM >= dDataBase .and.
		If TOL->TOL_STATUS $ "1/3" .and. TOL->TOL_TIPCON == "1"
			l2AchoCon := .t.
		ElseIf TOL->TOL_STATUS == "4"
			l2ConCanc := 4
		ElseIf TOL->TOL_STATUS == "2"
			l2ConCanc := 2
		Endif
		Dbselectarea("TOL")
		Dbskip()
	End
	If !l2AchoCon
		cMsgStop := STR0080+Alltrim(NGSEEK("SA1",TM0->TM0_CLIENT+TM0->TM0_LOJA,1,"SA1->A1_NOME"))+; //"O cliente "
					STR0081 //" no possui contrato."
		If l2ConCanc == 4
			cMsgStop := STR0080+Alltrim(NGSEEK("SA1",TM0->TM0_CLIENT+TM0->TM0_LOJA,1,"SA1->A1_NOME"))+; //"O cliente "
						STR0082 //" est com o contrato cancelado."
		ElseIf l2ConCanc == 2
			cMsgStop := STR0080+Alltrim(NGSEEK("SA1",TM0->TM0_CLIENT+TM0->TM0_LOJA,1,"SA1->A1_NOME"))+; //"O cliente "
						STR0083 //" est com o contrato pendente."
		Endif
		MsgInfo(cMsgStop)
	Endif
Endif

aCOLS[n][nNOFIC] := TM0->TM0_NOMFIC
aCOLS[n][nCLIEN] := TM0->TM0_CLIENT
aCOLS[n][nLOJAC] := TM0->TM0_LOJA
aCOLS[n][nNOMEC] := NGSEEK("SA1",TM0->TM0_CLIENT+TM0->TM0_LOJA,1,"SA1->A1_NOME")
If !Empty(aCOLS[n][nMOTIV])
	aCOLS[n][nNOEXA] := Busca_Perfil(TM0->TM0_NUMFIC,aCOLS[n][nMOTIV])
Endif
//aCOLS[n][nFUNCC] := NGSEEK("SRJ",TM0->TM0_CODFUN,1,"SRJ->RJ_DESC")
lRefresh := .t.
RestArea(aArea)
Return .t.

/*/


Ŀ
Funo    MDT075NOME Autor Denis Hyroshi de Souza  Data           
Ĵ
Descrio  Valida campo Ficha Medica                                  
ٱ


/*/
Function MDT075NOME()
Local aArea := GetArea()
Local oDlg3
Local oMARK, oTempTable
Local l2ConCanc := 0
Private cMARCA  := GetMark()

If !NaoVazio(M->TM0_NOMFIC)
	Return .f.
Endif

cFICHANOME := aCOLS[n][nFICHA]

lInverte:= .f.
lQuery := .t.

aDBF := {}
AADD(aDBF,{"OKID"  ,"C",02,0})
Aadd(aDBF,{"SITUAC","C",01,0})
Aadd(aDBF,{"NUMFIC","C",09,0})
Aadd(aDBF,{"NOMFIC","C",40,0})
Aadd(aDBF,{"NUMRG" ,"C",15,0})
Aadd(aDBF,{"DTNASC","D",08,0})
Aadd(aDBF,{"NOMFUN","C",40,0})
Aadd(aDBF,{"CLIENT","C",NSizeSA1,0})
Aadd(aDBF,{"LOJA"  ,"C",nSizeLo1,0})
Aadd(aDBF,{"NOMCLI","C",40,0})

aTRB1 := {}
AADD(aTRB1,{"OKID"	,NIL," ",})
AADD(aTRB1,{"SITUAC",NIL,STR0084,}) //"Status"
AADD(aTRB1,{"NUMFIC",NIL,STR0085,}) //"Ficha"
AADD(aTRB1,{"NOMFIC",NIL,STR0018,}) //"Nome"
AADD(aTRB1,{"NUMRG" ,NIL,STR0086,}) //"R.G."
Aadd(aTRB1,{"DTNASC",NIL,STR0087,})  //"Data Nasc."
AADD(aTRB1,{"NOMFUN",NIL,STR0088,}) //"Funo"
AADD(aTRB1,{"CLIENT",NIL,STR0089,}) //"Cliente"
AADD(aTRB1,{"LOJA"  ,NIL,STR0090,}) //"Loja"
AADD(aTRB1,{"NOMCLI",NIL,STR0091,}) //"Nome Cliente"

oTempTable := FWTemporaryTable():New( TRBK, aDBF )
oTempTable:AddIndex( "1", { "NUMFIC" } )
oTempTable:Create()

Dbselectarea("TM0")
Dbsetorder(2)
Dbseek(xFilial("TM0")+Alltrim(M->TM0_NOMFIC))
While !eof() .and. xFilial("TM0") == TM0->TM0_FILIAL .and. ;
	Alltrim(M->TM0_NOMFIC) == Substr(TM0->TM0_NOMFIC,1,Len(Alltrim(M->TM0_NOMFIC)))

	dbSelectArea("SRA")
	dbSetOrder(1)
	dbSeek(xFilial("SRA",TM0->TM0_FILFUN)+TM0->TM0_MAT)

	Dbselectarea("TRBK")
	Dbgotop()
	If !Dbseek(TM0->TM0_NOMFIC+DTOS(TM0->TM0_DTNASC))
		RecLock("TRBK",.t.)
		TRBK->OKID   := "  "
		TRBK->NUMFIC := TM0->TM0_NUMFIC
		TRBK->NOMFIC := TM0->TM0_NOMFIC
		TRBK->NUMRG  := TM0->TM0_RG
		TRBK->DTNASC := TM0->TM0_DTNASC
		TRBK->CLIENT := TM0->TM0_CLIENT
		TRBK->LOJA   := TM0->TM0_LOJA
		TRBK->NOMCLI := Posicione("SA1",1,xFilial("SA1")+TM0->TM0_CLIENT+TM0->TM0_LOJA,"A1_NOME")
		TRBK->NOMFUN := Posicione("SRJ",1,xFilial("SRJ")+TM0->TM0_CODFUN,"RJ_DESC")
		TRBK->SITUAC := SRA->RA_SITFOLH
		Msunlock("TRBK")
	Else
		If TM0->TM0_NUMFIC > TRBK->NUMFIC
			RecLock("TRBK",.f.)
			TRBK->OKID   := "  "
			TRBK->NUMFIC := TM0->TM0_NUMFIC
			TRBK->NOMFIC := TM0->TM0_NOMFIC
			TRBK->NUMRG  := TM0->TM0_RG
			TRBK->DTNASC := TM0->TM0_DTNASC
			TRBK->CLIENT := TM0->TM0_CLIENT
			TRBK->LOJA   := TM0->TM0_LOJA
			TRBK->NOMCLI := Posicione("SA1",1,xFilial("SA1")+TM0->TM0_CLIENT+TM0->TM0_LOJA,"A1_NOME")
			TRBK->NOMFUN := Posicione("SRJ",1,xFilial("SRJ")+TM0->TM0_CODFUN,"RJ_DESC")
			TRBK->SITUAC := SRA->RA_SITFOLH
			Msunlock("TRBK")
		Endif
	Endif

	Dbselectarea("TM0")
	Dbskip()
End

Dbselectarea("TM0")
Dbsetorder(1)

Dbselectarea("TRBK")
Dbgotop()
If TRBK->(Reccount()) <= 0
	oTempTable:Delete()
	RestArea(aArea)
	lRefresh := .t.
	Msgstop(STR0092,STR0076) //"No existem funcionrios que iniciam por estas letras."###"ATENO"
	Return .f.
Endif

nOpca := 2
DEFINE MSDIALOG oDlg3 TITLE OemToAnsi(STR0093) From 11,2 To 26,120 OF oMainWnd //"Selecione o funcionrio"
oMARK := MsSelect():NEW("TRBK","OKID",,aTRB1,@lINVERTE,@cMARCA,{20,05,110,463})
oMARK:bMARK := {|| MDTA075MAQ(cMarca,lInverte,TRBK->(Recno())) .and. oMARK:oBROWSE:REFRESH(.T.)}
oMARK:oBROWSE:lHASMARK := .T.
oMARK:oBROWSE:lCANALLMARK := .F.
ACTIVATE MSDIALOG oDlg3 ON INIT EnchoiceBar(oDlg3,{|| nOpca := 1,oDlg3:End()},{|| nOpca := 2,oDlg3:End()})

lAltFun := .f.
If nOpca == 1

	Dbselectarea("TRBK")
	Dbgotop()
	While !eof()
		If Empty(TRBK->OKID)
			Dbskip()
			Loop
		Endif

		dbSelectArea("TM0")
		/*
		cTM0_MAT := NGSEEK("TM0", TRBK->NUMFIC ,1,'TM0->TM0_MAT')
		If NGSEEK("SRA", cTM0_MAT ,1,'SRA->RA_SITFOLH') == "D"
			If !MsgYesNo("Este funcionrio est demitido. Confirma o agendamento?")
				dbSelectArea("TRBK")
				Use
				RestArea(aArea)
				Return .f.
			Endif
		Endif
		*/

		dbSelectArea("TMJ")
		dbSetOrder(11)
		IF dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+aCOLS[n][nHORAC]+aCOLS[n][nFICHA])
			If !EMPTY(TMJ_DTATEN)
				MsgStop(STR0077) //"Esta consulta no pode ser alterada, pois j foi realizada."
				dbSelectArea("TRBK")
				Use
				RestArea(aArea)
				Return .f.
			Endif
		Endif

		cTM0_MAT := NGSEEK("TM0", TRBK->NUMFIC ,1,'TM0->TM0_MAT')
		If NGSEEK("SRA", cTM0_MAT ,1,'SRA->RA_SITFOLH') == "D"
			cRG__TM0  := NGSEEK("TM0", TRBK->NUMFIC ,1,'TM0->TM0_RG')
			If !MDT075ALFI(.f.,TRBK->NUMFIC,2)
				dbSelectArea("TRBK")
				Use
				RestArea(aArea)
				cRG__TM0  := nil
				Return .f.
			Endif
			cRG__TM0  := nil
		Endif

		If NGSEEK("SA1", TRBK->CLIENT+TRBK->LOJA ,1,'SA1->A1_MSBLQL') == "1"
			If MsgYesNo(STR0078) //"Este funcionrio trabalha em um cliente inativo. Deseja ajustar o cadastro do funcionrio?"
				If !MDT075ALFI(.f.,TRBK->NUMFIC,2)
					dbSelectArea("TRBK")
					Use
					RestArea(aArea)
					Return .f.
				Endif
			Else
				dbSelectArea("TRBK")
				Use
				RestArea(aArea)
				Return .f.
			Endif
		Endif

		cSetorTM0 := NGSEEK("TM0",TRBK->NUMFIC,1,"TM0->TM0_CC")
		If NGSEEK("CTT", cSetorTM0 ,1,'CTT->CTT_MSBLQL') == "1"
			If MsgYesNo(STR0079) //"Este funcionrio trabalha em um setor bloqueado. Deseja ajustar o cadastro do funcionrio?"
				If !MDT075ALFI(.f.,TRBK->NUMFIC,2)
					dbSelectArea("TRBK")
					Use
					RestArea(aArea)
					Return .f.
				Endif
			Else
				dbSelectArea("TRBK")
				Use
				RestArea(aArea)
				Return .f.
			Endif
		Endif

		l2AchoCon := .f.
		Dbselectarea("TOL")
		Dbsetorder(2)
		Dbseek(xFilial("TOL")+TRBK->CLIENT+TRBK->LOJA)
		While !eof() .and. xFilial("TOL")+TRBK->CLIENT+TRBK->LOJA == TOL->(TOL_FILIAL+TOL_CLIENT+TOL_LOJACL) .and. !l2AchoCon
			//TOL->TOL_DTINIC <= dDataBase .and. TOL->TOL_DTTERM >= dDataBase .and.

			If TOL->TOL_STATUS $ "1/3" .and. TOL->TOL_TIPCON == "1"
				l2AchoCon := .t.
			ElseIf TOL->TOL_STATUS == "4"
				l2ConCanc := 4
			ElseIf TOL->TOL_STATUS == "2"
				l2ConCanc := 2
			Endif

			Dbselectarea("TOL")
			Dbskip()
		End
		If !l2AchoCon
			Dbselectarea("TOL")
			Dbsetorder(2)
			Dbseek(xFilial("TOL")+TRBK->CLIENT)
			cUniTemp := TOL->TOL_LOJACL
			While !eof() .and. xFilial("TOL")+TRBK->CLIENT+cUniTemp == TOL->(TOL_FILIAL+TOL_CLIENT+TOL_LOJACL) .and. !l2AchoCon
				//TOL->TOL_DTINIC <= dDataBase .and. TOL->TOL_DTTERM >= dDataBase .and.
				If TOL->TOL_STATUS $ "1/3" .and. TOL->TOL_TIPCON == "1"
					l2AchoCon := .t.
				ElseIf TOL->TOL_STATUS == "4"
					l2ConCanc := 4
				ElseIf TOL->TOL_STATUS == "2"
					l2ConCanc := 2
				Endif
				Dbselectarea("TOL")
				Dbskip()
			End
			If !l2AchoCon
				cMsgStop := STR0080+Alltrim(TRBK->NOMCLI)+STR0081 //"O cliente "###" no possui contrato."
				If l2ConCanc == 4
					cMsgStop := STR0080+Alltrim(TRBK->NOMCLI)+STR0082 //"O cliente "###" est com o contrato cancelado."
				ElseIf l2ConCanc == 2
					cMsgStop := STR0080+Alltrim(TRBK->NOMCLI)+STR0083 //"O cliente "###" est com o contrato pendente."
				Endif
				MsgInfo(cMsgStop)
			Endif
		Endif

		lAltFun := .t.
		aCOLS[n][nNOFIC] := TRBK->NOMFIC
		aCOLS[n][nFICHA] := TRBK->NUMFIC
		aCOLS[n][nCLIEN] := TRBK->CLIENT
		aCOLS[n][nLOJAC] := TRBK->LOJA
		aCOLS[n][nNOMEC] := TRBK->NOMCLI
		If !Empty(aCOLS[n][nMOTIV])
			aCOLS[n][nNOEXA] := Busca_Perfil(TRBK->NUMFIC,aCOLS[n][nMOTIV])
		Endif
		cTM0_CODFUN := NGSEEK("TM0",TRBK->NUMFIC,1,"TM0->TM0_CODFUN")
		//aCOLS[n][nFUNCC] := NGSEEK("SRJ",cTM0_CODFUN,1,"SRJ->RJ_DESC")
		Dbselectarea("TRBK")
		Dbskip()
	End
Endif
If !lAltFun
	oTempTable:Delete()
	RestArea(aArea)
	Return .f.
Endif

oTempTable:Delete()
RestArea(aArea)
Return .t.

/*/


Ŀ
Funo    MDTA075LOK Autor  Incio Luiz Kolling    Data           
Ĵ
Descrio  Critica se a linha digitada esta' Ok                       
Ĵ
 Uso                                                                  
ٱ


/*/
FUNCTION MDTA075LOK(o)
Local lRET := .T.
Local nX
Local aArea := GetArea()
Local nSalvo := n,nPosInc := 0,nFor

//Exclui os horarios reservados do usuario
DelReserv(M->TMJ_CODUSU,M->TMJ_DTCONS,aCOLS[n][nHORAC])

If Empty(aCols[n,nMOTIV]) .and. !Empty(aCols[n,nFICHA])
	MsgStop(STR0094) //"O campo Natureza  obrigatrio."
	Return .f.
Endif

If !VALHR075(.f.,cCaleMed,M->TMJ_DTCONS,aCOLS[n][nHORAC],'I')
	Return .f.
Endif

dbSelectArea("TMJ")
dbSetOrder(1)
dbSeek(xFilial("TMJ")+TML->TML_CODUSU+DTOS(M->TMJ_DTCONS))
aCntTMJ := {}
While !EOF() .And. TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS) == xFilial("TMJ")+TML->TML_CODUSU+DTOS(M->TMJ_DTCONS)

	If Empty(TMJ->TMJ_NUMFIC) .and. TMJ->TMJ_USURES <> SubStr(cUsuario,7,15) .and. !Empty(TMJ->TMJ_USURES)
		dbSkip()
		loop
	Endif

	nPosInc := 0
	For nFor := 1 to Len(aCols)
		If aCOLS[nFor][nHORAC] == TMJ->TMJ_HRCONS
			nPosInc := 1
			Exit
		Endif
	Next nFor

	If nPosInc > 0
		aADD(aCntTMJ,{ TMJ->TMJ_HRCONS , TMJ->TMJ_NUMFIC , TMJ->TMJ_NATEXA })
	Endif

	dbSelectArea("TMJ")
	dbSkip()
End

aCntFOR := {}
For nX := 1 to Len(aCols)
	If !Empty(aCOLS[nX][nFICHA]) .and. !acols[nX][len(Acols[nX])]
		aADD(aCntFOR,{ aCOLS[nX][nHORAC] , aCOLS[nX][nFICHA] , aCOLS[nX][nMOTIV] })
	Endif
Next nX

If Len(aCntFOR) <> Len(aCntTMJ)
	Monta_Agenda(.t.)
Elseif Len(aCntFOR) == Len(aCntTMJ)
	ASORT(aCntFOR,,,{|x,y| x[1]+x[2]+x[3] < y[1]+y[2]+y[3] })
	ASORT(aCntTMJ,,,{|x,y| x[1]+x[2]+x[3] < y[1]+y[2]+y[3] })

	lDiffArray := .f.
	For nX := 1 To Len(aCntFOR)
		If aCntFOR[nX,1] <> aCntTMJ[nX,1] .or. aCntFOR[nX,2] <> aCntTMJ[nX,2] .or. aCntFOR[nX,3] <> aCntTMJ[nX,3]
			lDiffArray := .t.
			Exit
		Endif
	Next nX

	If lDiffArray
		Monta_Agenda(.t.)
	Endif
Endif

n := nSalvo
RestArea(aArea)
oGet:oBrowse:nAt := nSalvo
oGet:oBrowse:Refresh()
Return lRET
/*/


Ŀ
Funo    VAL075DIA  Autor  Marcio Costa           Data  24/01/00 
Ĵ
Descrio  Verifica se o dia cai em dia de trabalho do medico.        
ٱ


/*/
Function NGHORA075(cCALEND,dDataCon)
Local nlo
Local nDIASEM := Dow(dDataCon)
Local aCalend := NGCALENDAH(cCALEND)
Local lDiaPer := .F.

cPer01De  := "  :  "
cPer02De  := "  :  "
//cPer03De  := "  :  "
cPer01Ate := "  :  "
cPer02Ate := "  :  "
//cPer03Ate := "  :  "

dbSelectArea("TLF")
dbSetorder(1)
If dbSeek(xFilial("TLF")+DTOS(dDataCon))
	If !Empty(TLF->TLF_HRINID) .AND. !Empty(TLF->TLF_HRFIMD) .AND. !Empty(TLF->TLF_HRINIT) .AND. !Empty(TLF->TLF_HRFIMT)
		cPer01De  := TLF->TLF_HRINID
		cPer01Ate := TLF->TLF_HRFIMD

		cPer02De  := TLF->TLF_HRINIT
		cPer02Ate := TLF->TLF_HRFIMT
		lDiaPer := .T.
	Endif
Endif

If !lDiaPer
	For nlo := 1 To Len(aCalend[nDIASEM,2])
		If nlo == 1
			cPer01De  := aCALEND[nDIASEM,2,nlo,1]
			cPer01Ate := aCALEND[nDIASEM,2,nlo,2]
		Elseif nlo == 2
			cPer02De  := aCALEND[nDIASEM,2,nlo,1]
			cPer02Ate := aCALEND[nDIASEM,2,nlo,2]
//		Elseif nlo == 3
//			cPer03De  := aCALEND[nDIASEM,2,nlo,1]
//			cPer03Ate := aCALEND[nDIASEM,2,nlo,2]
		Endif
	Next nlo
Endif

Return .t.
/*/


Ŀ
Funo    NG075AGE3  Autor                         Data           
Ĵ
Descrio  Historico                                                  
ٱ

/*/
Function NG075AGE3(lPesq)

Local aArea := GetArea()
Local aAreaTML := TML->(GetArea())
Local dConsultas := M->TMJ_DTCONS
Local cCodMed := M->TMJ_CODUSU
Default lPesq := .F.

Dbselectarea("TML")
Dbsetorder(1)
Dbseek(xFilial("TML")+M->TMJ_CODUSU)

NG075AGE(lPesq)

RestArea(aAreaTML)
RestArea(aArea)
M->TMJ_DTCONS := dConsultas
M->TMJ_CODUSU := cCodMed

fMDT075TMK(1)

lRefresh := .t.
Return .t.
/*/


Ŀ
Funo    A075INIHRC Autor                         Data           
Ĵ
Descrio                                                             
ٱ

/*/
Function A075INIHRC()
Local xxaa
Local nhora := -1

If Type("nIntervalo") == "N" .and. Type("aCols") == "A"
	If nIntervalo > 0
		For xxaa := 1 to Len(aCols)
			If xxaa <> n
				If HTOM(aCOLS[xxaa][1]) > nhora .and. HTOM(aCOLS[xxaa][1]) <> 0
					nhora := HTOM(aCOLS[xxaa][1])
				Endif
			Endif
		Next xxaa
		If nhora <> -1
			Return MTOH(nhora+nIntervalo)
		Else
			Return "  :  "
		Endif
	Endif
Endif

Return If(INCLUI,SUBSTR(TIME(),1,5),TMJ->TMJ_HRCONS)

/*/


Ŀ
 Funo   MDTA075MAQ Autor Denis Hyroshi de Souza  Data  25/07/03 
Ĵ
 Descrio Inverte a marcacao do browse                               
ٱ


/*/

Function MDTA075MAQ(cMarca,lInverte,nRegs)
Local aArea := GetArea()

Dbselectarea("TRBK")
Dbgotop()
While !eof()
	If nRegs == TRBK->(Recno())
		TRBK->OKID := cMARCA
	Else
		TRBK->OKID := "  "
	Endif
	Dbskip()
End
RestArea(aArea)
Return .t.

/*/


Ŀ
Funo    MDT075DIA  Autor                         Data           
Ĵ
Descrio SALVA A CONSULTA                                            
ٱ


/*/
Function SAVEMDT075(nCampo1)
Local aArea := GetArea()
Local aAreaTMJ := TMJ->(GetArea())
Local lret := .t.,lRetNat := .f.
Local lIncNovo := .f.,nFor,xxx
Local nContaCons := 0
Local nSalvo := n

If nCampo1 <> 1
	//Valida Horario contra o calendario
	If !VALHR075(.f.,cCaleMed,M->TMJ_DTCONS,aCOLS[n][1],'I')
		Return .f.
	Endif
Else
	//Valida Horario contra o calendario
	If !VALHR075(.f.,cCaleMed,M->TMJ_DTCONS,M->TMJ_HRCONS,'I')
		Return .f.
	Endif
	//Valida Hora
	If !NGVALHORA(M->TMJ_HRCONS,.t.)
		Return .f.
	Endif
	If Alltrim(M->TMJ_HRCONS) <> ":"
		For nFor := 1 to Len(aCols)
			If n <> nFor
				If M->TMJ_HRCONS == aCOLS[nFor][nHORAC] .and. !Empty(aCOLS[nFor][nFICHA])
					nContaCons++
				Endif
			Endif
		Next nFor
		If M->TMJ_HRCONS <= "12:00"
			If nContaCons == nMedicosDia
				MsgStop(STR0095+Alltrim(Str(nMedicosDia,4))+STR0096) //"Excedeu o limite de consultas para este horrio. Existem "###" mdicos disponveis para o perodo da manh."
				Return .f.
			Endif

			If Alltrim(cPer01De) <> ":" .and. MOD(HTOM(M->TMJ_HRCONS)-HTOM(cPer01De),nIntervalo) <> 0
				MsgStop(STR0097+Str(nIntervalo,3)+STR0098) //"O horrio da consulta dever ser marcado no intervalo de "###"min de atendimento."
				Return .f.
			Endif
		Else
			If nContaCons == nMedicosTar
				MsgStop(STR0095+Alltrim(Str(nMedicosTar,4))+STR0099) //"Excedeu o limite de consultas para este horrio. Existem "###" mdicos disponveis para o perodo da tarde."
				Return .f.
			Endif

			If Alltrim(cPer01De) <> ":" .and. MOD(HTOM(M->TMJ_HRCONS)-HTOM(cPer02De),nIntervalo) <> 0
				MsgStop(STR0097+Str(nIntervalo,3)+STR0098) //"O horrio da consulta dever ser marcado no intervalo de "###"min de atendimento."
				Return .f.
			Endif
		Endif
	Endif
Endif

If Empty(M->TMJ_CODUSU) .or. Empty(M->TMJ_DTCONS)
	MsgStop(STR0100,STR0076) //"Os Campos Data e Mdico so obrigatrios."###"ATENO"
	return .f.
Endif

If nCampo1 == 1 .or. nCampo1 == 2
	dDtTempC := CTOD("//")
	If nCampo1 == 1
		If !Empty(M->TMJ_HRCONS)
			dDtTempC := M->TMJ_HRCONS
		Endif
	Else
		If !Empty(aCOLS[n][nHORAC])
			dDtTempC := aCOLS[n][nHORAC]
		Endif
	Endif

	If !Empty(dDtTempC) .and. .f. //AGENDA TECNICA
		//Verifica se o usuario esta agendado em servicos tecnicos nas empresas.
		Dbselectarea("TOO")
		Dbsetorder(1)
		Dbseek(xFilial("TOO")+M->TMJ_CODUSU)
		While !eof() .and. xFilial("TOO")+M->TMJ_CODUSU == TOO->TOO_FILIAL+TOO->TOO_CODUSU
		    If M->TMJ_DTCONS < TOO->TOO_DATAAG .or. (M->TMJ_DTCONS == TOO->TOO_DATAAG .and. dDtTempC < TOO->TOO_HORA)
		    	Dbskip()
		    	Loop
		    Endif
		    If M->TMJ_DTCONS > TOO->TOO_DATAFI .or. (M->TMJ_DTCONS == TOO->TOO_DATAFI .and. dDtTempC > TOO->TOO_HORAFI)
		    	Dbskip()
		    	Loop
		    Endif

		    MsgInfo(STR0150+; // "A consulta no poder ser agendada para este horrio, pois o mdico estar alocado em cliente."
		    	Chr(10)+STR0151,STR0076) // "Ver 'Agenda Servios Tcnicos'."###"ATENO"
			RestArea(aAreaTMJ)
			RestArea(aArea)
			Return .f.

			Dbskip()
		End
	Endif
Endif

dbSelectArea("TMJ")
dbSetOrder(11)
If nCampo1 == 1
	If !Empty(M->TMJ_HRCONS) .and. !Empty(aCOLS[n][nFICHA])
		IF !dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+M->TMJ_HRCONS+aCOLS[n][nFICHA])
			IF !dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+aCOLS[n][nHORAC]+aCOLS[n][nFICHA])
				lIncNovo := .t.
			Else
				RecLock("TMJ",.f.)
				TMJ->TMJ_HRCONS := M->TMJ_HRCONS
				MsUnLock("TMJ")
			Endif
		Else
			Help(" ",1,"JAEXISTINF")
			lret := .f.
		Endif
	Endif
ElseIf nCampo1 == 2
	If !Empty(aCOLS[n][nHORAC]) .and. !Empty(M->TMJ_NUMFIC)
		IF !dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+aCOLS[n][nHORAC]+M->TMJ_NUMFIC)
			IF !dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+aCOLS[n][nHORAC]+aCOLS[n][nFICHA]) .or. ;
				Empty(aCOLS[n][nFICHA])

				lIncNovo := .t.
			Else
				RecLock("TMJ",.f.)
				TMJ->TMJ_NUMFIC := M->TMJ_NUMFIC
				TMJ->TMJ_MAT    := NGSEEK("TM0",M->TMJ_NUMFIC,1,"TM0->TM0_MAT")
				MsUnLock("TMJ")
			Endif
		Else
			Help(" ",1,"JAEXISTINF")
			lret := .f.
		Endif
	Endif
ElseIf nCampo1 == 3
	If !Empty(aCOLS[n][nHORAC]) .and. !Empty(aCOLS[n][nFICHA])
		IF !dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+aCOLS[n][nHORAC]+aCOLS[n][nFICHA])
			IF !dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+aCOLS[n][nHORAC]+cFICHANOME) .or. ;
				Empty(cFICHANOME)

				lIncNovo := .t.
			Else
				RecLock("TMJ",.f.)
				TMJ->TMJ_NUMFIC := aCOLS[n][nFICHA]
				TMJ->TMJ_MAT    := NGSEEK("TM0",aCOLS[n][nFICHA],1,"TM0->TM0_MAT")
				MsUnLock("TMJ")
			Endif
		Else
			Help(" ",1,"JAEXISTINF")
			aCOLS[n][nFICHA] := cFICHANOME
			lret := .f.
		Endif
	Endif
ElseIf nCampo1 == 4
	If !Empty(aCOLS[n][nHORAC]) .and. !Empty(aCOLS[n][nFICHA]) .and. !Empty(M->TMJ_NATEXA)
		IF dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+aCOLS[n][nHORAC]+aCOLS[n][nFICHA])
			RecLock("TMJ",.f.)
			TMJ->TMJ_NATEXA := M->TMJ_NATEXA
			MsUnLock("TMJ")
		Else
			lIncNovo := .t.
		Endif
	Endif
ElseIf nCampo1 == 5
	If !Empty(aCOLS[n][nHORAC]) .and. !Empty(aCOLS[n][nFICHA])
		IF dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+aCOLS[n][nHORAC]+aCOLS[n][nFICHA])
			RecLock("TMJ",.f.)
			TMJ->TMJ_EXAME := M->TMJ_EXAME
			MsUnLock("TMJ")
		Endif
	Endif
ElseIf nCampo1 == 6
	If !Empty(aCOLS[n][nHORAC]) .and. !Empty(aCOLS[n][nFICHA])
		IF dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+aCOLS[n][nHORAC]+aCOLS[n][nFICHA])
			RecLock("TMJ",.f.)
			TMJ->TMJ_OBSCON := M->TMJ_OBSCON
			MsUnLock("TMJ")
		Endif
	Endif
ElseIf nCampo1 == 7
	If !Empty(aCOLS[n][nHORAC]) .and. !Empty(aCOLS[n][nFICHA]) .and. !Empty(M->TMJ_MOTIVO)
		IF dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+aCOLS[n][nHORAC]+aCOLS[n][nFICHA])
			RecLock("TMJ",.f.)
			TMJ->TMJ_MOTIVO := M->TMJ_MOTIVO
			MsUnLock("TMJ")
		Else
			lIncNovo := .t.
		Endif
	Endif
Endif


If lIncNovo
	dDtTempC := CTOD("//")
	If nCampo1 == 1
		If !Empty(M->TMJ_HRCONS)
			dDtTempC := M->TMJ_HRCONS
		Endif
	Else
		If !Empty(aCOLS[n][nHORAC])
			dDtTempC := aCOLS[n][nHORAC]
		Endif
	Endif

	If !Empty(dDtTempC)
		aTotExame  := A075TOTATE()
		nTotDia    := aTotExame[1]
		nTotTarde  := aTotExame[2]

		oCPO02:REFRESH(.T.)
		oCPO03:REFRESH(.T.)
		oCPO04:REFRESH(.T.)
		oCPO05:REFRESH(.T.)
		oCPO06:REFRESH(.T.)
		oCPO07:REFRESH(.T.)

		nConsDiaHr := 0

		dbSelectArea("TMJ")
		dbSetOrder(1)
		dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+dDtTempC)
		While !EOF() .And. TMJ->(TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS)+TMJ_HRCONS) == xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+dDtTempC
			If Empty(TMJ->TMJ_NUMFIC) .and. (TMJ->TMJ_USURES == SubStr(cUsuario,7,15) .or. Empty(TMJ->TMJ_USURES))
				dbskip()
				loop
			Endif
			nConsDiaHr++
			dbskip()
		End

		If (dDtTempC <= "12:00" .and. nConsDiaHr >= nMedicosDia) .or. ;
			(dDtTempC > "12:00" .and. nConsDiaHr >= nMedicosTar)

			MsgStop(STR0101) //"Excedeu o limite de consultas para este horrio."
			lret := .f.
			Monta_Agenda(.t.)
			lRetNat := .t.
			If nCampo1 == 4
				M->TMJ_NATEXA := " "
			Endif
		Endif
	Endif

	If lret
		//Exclui os horarios reservados do usuario
		DelReserv(M->TMJ_CODUSU,M->TMJ_DTCONS,dDtTempC)

		RecLock("TMJ",.T.)
		TMJ->TMJ_FILIAL := xFilial("TMJ")
		TMJ->TMJ_CODUSU := M->TMJ_CODUSU
		TMJ->TMJ_DTCONS := M->TMJ_DTCONS
		TMJ->TMJ_DTPROG := M->TMJ_DTCONS
		//TMJ->TMJ_SEQUEN := cSequeTMJ
		TMJ->TMJ_NUMFIC := If(nCampo1 == 2,M->TMJ_NUMFIC,aCOLS[n][nFICHA])
		TMJ->TMJ_MAT    := NGSEEK("TM0",TMJ->TMJ_NUMFIC,1,"TM0->TM0_MAT")
		TMJ->TMJ_HRCONS := If(nCampo1 == 1,M->TMJ_HRCONS,aCOLS[n][nHORAC])
		TMJ->TMJ_NATEXA := If(nCampo1 == 4,M->TMJ_NATEXA,aCOLS[n][nMOTIV])
		TMJ->TMJ_MOTIVO := If(nCampo1 == 7,M->TMJ_MOTIVO,aCOLS[n][nMOTIVO])
		TMJ->TMJ_OBSCON := aCOLS[n][nOBSER]
		TMJ->TMJ_USURES := SubStr(cUsuario,7,15)
		TMJ->TMJ_DTARES := dDataBase
		TMJ->TMJ_HORRES := Substr(Time(),1,5)
		TMJ->TMJ_EXAME  := "NR7"
		MSUNLOCK("TMJ")
	Endif
Endif
aTotExame  := A075TOTATE()
nTotDia    := aTotExame[1]
nTotTarde  := aTotExame[2]

If nCampo1 == 4 .and. lret
	aCOLS[n][nNOEXA] := Busca_Perfil(aCOLS[n][nFICHA],M->TMJ_NATEXA)
Endif

If nCampo1 == 1 .and. M->TMJ_HRCONS <> aCOLS[n][nHORAC]
	nPosScan := aSCAN(aCOLS,{|x| x[nHORAC] == M->TMJ_HRCONS .and. Empty(x[nFICHA]) })
	If nPosScan == 0
		For xxx := Len(aCOLS) to 1
			If aCOLS[xxx,nHORAC] < M->TMJ_HRCONS .and. Empty(aCols[xxx,nFICHA])
				nPosScan := xxx
				Exit
			Endif
		Next xxx
	Endif
	If nPosScan == 0
		For xxx := 1 to Len(aCOLS)
			If aCOLS[xxx,nHORAC] > M->TMJ_HRCONS .and. Empty(aCols[xxx,nFICHA])
				nPosScan := xxx
				Exit
			Endif
		Next xxx
	Endif
	If nPosScan > 0
		aTemp := aClone(aCOLS[nPosScan])
		aCOLS[nPosScan] := aClone(aCOLS[n])
		aTemp[nHORAC] := aCOLS[n][nHORAC]
		aCOLS[nPosScan,nHORAC] := M->TMJ_HRCONS
		aCOLS[n] := aClone(aTemp)
		M->TMJ_HRCONS := aCOLS[n][nHORAC]
	Endif
Endif

oCPO02:REFRESH(.T.)
oCPO03:REFRESH(.T.)
oCPO04:REFRESH(.T.)
oCPO05:REFRESH(.T.)
oCPO06:REFRESH(.T.)
oCPO07:REFRESH(.T.)

RestArea(aAreaTMJ)
RestArea(aArea)

n := nSalvo
oGet:oBrowse:nAt := nSalvo
oGet:oBrowse:Refresh()

If lRetNat
	Return .t.
Endif
Return lret

/*/


Ŀ
Funo    NG075ALTC  Autor                         Data           
Ĵ
Descrio                                                             
ٱ


/*/
Function NG075ALTC()
pergunte("MDT075",.T.)
Return .t.

/*/


Ŀ
Funo    NG075ALTM  Autor                         Data           
Ĵ
Descrio                                                             
ٱ


/*/
Function NG075ALTM()
//pergunte("MDT076",.T.)
AxCadastro("TLF",STR0102, "MDT075DEL()","MDT075ALT()") //"Disponibilidade de Mdicos"
Return
/*


Ŀ
Funo    MDT075DEL   Autor  Marco Aurelio         Data  15/10/01 
Ĵ
Descrio  Rotina de validacao da exclusao da disponibilidade de medico
Ĵ
Parametros Nenhum                                                     
Ĵ
Retorno    ExpL1 -> Validacao OK                                      
Ĵ
Uso        MDTA075                                                    
ٱ


*/
Function MDT075DEL()

	Local lRet := .t.
	Local cArea := GetArea()

	dbSelectArea("TMJ")
	dbSetOrder(6)

	//Pesquisa existencia do registro no cadastro de Candidatos nao permitindo a exclusao do Convenio caso encontre
	If dbSeek(xFilial('TMJ')+DTOS(TLF->TLF_DTAGEN))
		cError := AllTrim(FwX2Nome("TMJ")) + " (TMJ)"
		HELP(" ",1,"MA10SC",,cError,5,1)
		lRet := .F.
	Endif

	RestArea(cArea)

Return lRet

/*


Ŀ
Funo    MDT075ALT   Autor  Denis Hyroshi de Souza Data  12/02/07 
Ĵ
Descrio  Altera disponibilidade de medicos                           
Ĵ
Parametros Nenhum                                                     
Ĵ
Retorno    ExpL1 -> Validacao OK                                      
Ĵ
Uso        MDTA075                                                    
ٱ


*/
Function MDT075ALT()

Local nManha, nTarde

If Altera

	nManha := fTotConDia(TML->TML_CODUSU,M->TLF_DTAGEN,0,1)
	If nManha > M->TLF_QTDDIA
		MsgStop(STR0103+Alltrim(Str(nManha,9))+STR0104+; //"Na Agenda Mdica foi disponibilizado "###" mdicos, portanto, a quantidade disponvel "
			STR0105,STR0076) //"de manh no poder ser inferior  quantidade disponibilizada anteriormente."###"ATENO"
		Return .f.
	Endif

	nTarde := fTotConDia(TML->TML_CODUSU,M->TLF_DTAGEN,0,2)
	If nTarde > M->TLF_QTDTAR
		MsgStop(STR0103+Alltrim(Str(nTarde,9))+STR0104+; //"Na Agenda Mdica foi disponibilizado "###" mdicos, portanto, a quantidade disponvel "
			STR0106,STR0076) //" tarde no poder ser inferior  quantidade disponibilizada anteriormente."###"ATENO"
		Return .f.
	Endif

Endif

Return .t.

/*/


Ŀ
Funo    NG075EXCC  Autor                         Data           
Ĵ
Descrio Funcao de exclusao da consulta na grade agenda              
ٱ


/*/
Function NG075EXCC(lMsgExc)
Local aCols2 := aClone(aCOls)
Local nSalvo := n
Local lOutroUser := .f.
Local aArea := GetArea()

Default lMsgExc := .t.

dbSelectArea("TMJ")
dbSetOrder(11)
IF dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+aCOLS2[n][nHORAC]+aCOLS2[n][nFICHA])
	If !EMPTY(TMJ_DTATEN)
		MsgStop(STR0107) //"Esta consulta no pode ser excluda, pois j foi realizada."
		Return .f.
	Endif
Endif

If lMsgExc
	If !MsgYesNo(STR0108) //"Deseja realmente excluir a consulta agendada?"
		Return .f.
	Endif
Endif

dbSelectArea("TMJ")
dbSetOrder(1)
dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+acols[n][nHORAC])
While !EOF() .And. TMJ->(TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS)+TMJ_HRCONS) == xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+acols[n][nHORAC]
	IF TMJ->TMJ_NUMFIC == acols[n][nFicha]
		If Empty(TMJ->TMJ_NUMFIC) .and. (TMJ->TMJ_USURES != SubStr(cUsuario,7,15) .and. !Empty(TMJ->TMJ_USURES))
			lOutroUser := .t.
			dbSkip()
			loop
		Endif
		RecLock("TMJ",.f.)
		Dbdelete()
		MsUnLock("TMJ")
		Exit
	Endif
	dbskip()
End
If lOutroUser
	MsgStop(STR0109) //"Este horrio est reservado para outro usurio do sistema."
	RestArea(aArea)
	Return .f.
Endif

aCOLS[n][nFICHA] := Space(Len(aCOLS[n][nFICHA]))
aCOLS[n][nNOFIC] := Space(Len(aCOLS[n][nNOFIC]))
aCOLS[n][nNOEXA] := Space(Len(aCOLS[n][nNOEXA]))
aCOLS[n][nMOTIV] := Space(Len(aCOLS[n][nMOTIV]))
aCOLS[n][nMOTIVO] := Space(Len(aCOLS[n][nMOTIVO]))
aCOLS[n][nOBSER] := Space(Len(aCOLS[n][nOBSER]))
aCOLS[n][nCLIEN] := Space(Len(aCOLS[n][nCLIEN]))
aCOLS[n][nLOJAC] := Space(Len(aCOLS[n][nLOJAC]))
aCOLS[n][nNOMEC] := Space(30)
aCOLS[n][nUsado+1] := .F.

aTotExame  := A075TOTATE()
nTotDia    := aTotExame[1]
nTotTarde  := aTotExame[2]
oCPO02:REFRESH(.T.)
oCPO03:REFRESH(.T.)
oCPO04:REFRESH(.T.)
oCPO05:REFRESH(.T.)
oCPO06:REFRESH(.T.)
oCPO07:REFRESH(.T.)
oGet:FORCEREFRE()

n := nSalvo
oGet:oBrowse:nAt := n
oGet:oBrowse:Refresh()
Return .t.

/*/


Ŀ
Funo    A075TOTATE Autor                         Data           
Ĵ
Descrio                                                             
ٱ


/*/
Function A075TOTATE()
Local nTot1 := 0
Local nTot2 := 0
Local aArea := GetArea()
Local aAreaTMJ := TMJ->(GetArea())

dbSelectArea("TMJ")
dbSetOrder(6)
dbSeek(xFilial("TMJ")+DTOS(M->TMJ_DTCONS))
While !EOF() .And. TMJ_FILIAL+DTOS(TMJ_DTCONS) == xFilial("TMJ")+DTOS(M->TMJ_DTCONS)
	If TMJ->TMJ_HRCONS <= "12:00"
		nTot1++
	Else
	    nTot2++
	Endif
    dbSkip()
End

RestArea(aAreaTMJ)
RestArea(aArea)
Return {nTot1,nTot2}

/*/


Ŀ
Funo    MDT075PERF Autor Denis Hyroshi de Souza  Data 07/06/2004
Ĵ
Descrio  Mostra Perfil do Funcionaro                                
ٱ


/*/
Function MDT075PERF()
Local oDLG3,oCbx1, oTempTable
Local aArea := GetArea()
Local lTemPerCusto := .f.
Local nTamExa := If(TAMSX3("TM4_EXAME")[1] < 1, 6, TAMSX3("TM4_EXAME")[1])

Private oBrw400

aDBF2 := {}
AADD(aDBF2,{ "TON_CODEXA" , "C" ,nTamExa, 0 })
AADD(aDBF2,{ "TON_DESEXA" , "C" ,30, 0 })

oTempTable := FWTemporaryTable():New( TRBCLI, aDBF2 )
oTempTable:AddIndex( "1", {"TON_CODEXA"} )
oTempTable:Create()

Dbselectarea("TM0")
Dbsetorder(1)
Dbseek(xFilial("TM0")+aCOLS[n][nFICHA])

cSeekTON := TM0->TM0_CODFUN+Substr(TM0->TM0_CC,1,nSizeCli+nSizeLoj)

DbSelectArea("TL2")
DbSetOrder(4) //TL2_FILIAL+TL2_CUSTO+TL2_CODFUN+TL2_CODEXA
If DbSeek(xFilial("TL2")+TM0->TM0_CC+TM0->TM0_CODFUN)
	lTemPerCusto := .t.
Endif

If !lTemPerCusto
	DbSelectArea("TL2")
	DbSetOrder(4) //TL2_FILIAL+TL2_CUSTO+TL2_CODFUN+TL2_CODEXA
	If DbSeek(xFilial("TL2")+TM0->TM0_CC+Space(nSizeCODFUN))
		lTemPerCusto := .t.
	Endif
Endif

If lTemPerCusto .and. !Empty(aCOLS[n][nMOTIV])
	DbSelectArea("TL2")
	DbSetOrder(1)
	DbSeek(xFilial("TL2")+TM0->TM0_CC)
	While !eof() .and. TL2->(TL2_FILIAL+TL2_CUSTO) == xFilial("TL2")+TM0->TM0_CC

		If !Empty(TL2->TL2_CODFUN) .AND. TL2->TL2_CODFUN != TM0->TM0_CODFUN
			DbSelectArea("TL2")
	   		DbSkip()
	   		Loop
		Endif

		If  (aCOLS[n][nMOTIV] == "1" .and. !(TL2->TL2_TIPOEX $ "1 -6 -8 -9 -10-11-12-13-14-15")) .or. ;
			(aCOLS[n][nMOTIV] == "2" .and. !(TL2->TL2_TIPOEX $ "2 -6 -7 -8 -11-12-13-14")) .or. ;
			(aCOLS[n][nMOTIV] == "3" .and. !(TL2->TL2_TIPOEX $ "3 -10-11-12-13-14-15")) .or. ;
			(aCOLS[n][nMOTIV] == "4" .and. !(TL2->TL2_TIPOEX $ "4 -11-12")) .or. ;
			(aCOLS[n][nMOTIV] == "5" .and. !(TL2->TL2_TIPOEX $ "5 -7 -8 -9 -11-14-15"))

			DbSelectArea("TL2")
			DbSkip()
			Loop
		Endif

		Dbselectarea("TM4")
		Dbsetorder(01)
		If Dbseek(xFilial("TM4")+TL2->TL2_CODEXA)
			Dbselectarea("TRBCLI")
			Dbgotop()
			If !Dbseek(TL2->TL2_CODEXA)
				TRBCLI->(RecLock("TRBCLI",.t.))
				TRBCLI->TON_CODEXA := TL2->TL2_CODEXA
				TRBCLI->TON_DESEXA := Substr(TM4->TM4_NOMEXA,1,40)
				MsUnLock("TRBCLI")
			Endif
		Endif
		DbSelectArea("TL2")
		Dbskip()
	End
ElseIf !Empty(aCOLS[n][nMOTIV])
	DbSelectArea("TON")
	DbSetOrder(1)
	DbSeek(xFilial("TON")+cSeekTON)
	While !eof() .and. TON->(TON_FILIAL+TON_CODFUN+TON_CLIENT+TON_LOJA) == xFilial("TON")+cSeekTON
		If  (aCOLS[n][nMOTIV] == "1" .and. !(TON->TON_TIPOEX $ "1 -6 -8 -9 -10-11-12-13-14-15")) .or. ;
			(aCOLS[n][nMOTIV] == "2" .and. !(TON->TON_TIPOEX $ "2 -6 -7 -8 -11-12-13-14")) .or. ;
			(aCOLS[n][nMOTIV] == "3" .and. !(TON->TON_TIPOEX $ "3 -10-11-12-13-14-15")) .or. ;
			(aCOLS[n][nMOTIV] == "4" .and. !(TON->TON_TIPOEX $ "4 -11-12")) .or. ;
			(aCOLS[n][nMOTIV] == "5" .and. !(TON->TON_TIPOEX $ "5 -7 -8 -9 -11-14-15"))

			DbSelectArea("TON")
			DbSkip()
			Loop
		Endif

		Dbselectarea("TM4")
		Dbsetorder(01)
		If Dbseek(xFilial("TM4")+TON->TON_CODEXA)

			If lTemPerCusto
				DbSelectArea("TL2")
				DbSetOrder(1)
				If !DbSeek(xFilial("TL2")+TM0->TM0_CC+TON->TON_CODEXA)
					DbSelectArea("TON")
					DbSkip()
					Loop
				Endif
			Endif

			Dbselectarea("TRBCLI")
			Dbgotop()
			If !Dbseek(TON->TON_CODEXA)
				TRBCLI->(RecLock("TRBCLI",.t.))
				TRBCLI->TON_CODEXA := TON->TON_CODEXA
				TRBCLI->TON_DESEXA := Substr(TM4->TM4_NOMEXA,1,40)
				MsUnLock("TRBCLI")
			Endif
		Endif
		DbSelectArea("TON")
		Dbskip()
	End
Endif

Dbselectarea("TRBCLI")
Dbgotop()
If TRBCLI->(Reccount()) <= 0
	oTempTable:Delete()
	lRefresh := .t.
	Msgstop(STR0125,STR0076) //"No existem exames para o perfil do funcionrio."###"ATENO"
	RestArea(aArea)
	Return .f.
Endif

DEFINE MSDIALOG oDlg3 TITLE OemToAnsi(STR0057) From 8,0 To 25,70 OF oMainWnd //"Consulta Perfil"

@ 1.3,1 SAY OemToAnsi(STR0126) //"Estes so os exames necessrios  funo exercida pelo funcionrio."
@ 2.3,0.8 LISTBOX oBrw400 FIELDS	TRBCLI->TON_CODEXA											,;
									TRBCLI->TON_DESEXA											;
						FieldSizes 40,70											;
						Size 260,90												;
						HEADERS STR0019											,; //"Exame"
								STR0127 //"Nome Exame"

ACTIVATE MSDIALOG oDlg3 ON INIT EnchoiceBar(oDlg3,{|| oDlg3:End()},{|| oDlg3:End()})

oTempTable:Delete()
RestArea(aArea)
Return .t.
/*/


Ŀ
Funo    Monta_Agenda Autor Denis Hyroshi de Souza  Data 07/06/2004
Ĵ
Descrio Monta agenda com os horarios vagos para agendamento de exame
ٱ


/*/
Static Function Monta_Agenda(_RefreshGet)
Local nCNT02
Local nMedic
Local cHoraCons
Local nFor
Local nCont
Local lRet := .f.

aCols := {}
nCNT02 := nTotlimDia+nTotlimTar
nItens := nTotlimDia+nTotlimTar
nMedic := 1
cHoraCons := cPer01De
For nCont := 1 To nCNT02
	If nCont == nTotlimDia+1
		nMedic := 1
		cHoraCons := cPer02De
		//If Alltrim(cPer02De) == ":" .and. "14:00" >= cPer01De .and. "14:00" <= cPer01Ate
		//	cHoraCons := "14:00"
		//Endif
	Endif
	If Alltrim(cHoraCons) == ":"
		Exit
	Endif

	If nCont <= nTotlimDia
		If nMedic > nMedicosDia
			nMedic := 1
			cHoraCons := MTOH(HTOM(cHoraCons)+nIntervalo)
		Endif
		If cHoraCons > cPer01Ate
			nCont := nTotlimDia
			Loop
		Endif
	Elseif nCont <= nTotlimDia+nTotlimTar
		If nMedic > nMedicosTar
			nMedic := 1
			cHoraCons := MTOH(HTOM(cHoraCons)+nIntervalo)
		Endif
		If cHoraCons > cPer02Ate
			nCont := nTotlimDia+nTotlimTar
			Loop
		Endif
	Endif

	aADD(aCols,{;
				cHoraCons,;
				SPACE(09),;
				SPACE(02),;
				SPACE(40),;
				SPACE(01),;
				SPACE(20),;
				SPACE(80),;
				SPACE(50),;
				SPACE(nSizeSA1),;
				SPACE(nSizeLo1),;
				SPACE(40),;
				.F.})
	nMedic++
	lRet := .t.
Next nCOnt

dbSelectArea("TMJ")
dbSetOrder(1)
dbSeek(xFilial("TMJ")+TML->TML_CODUSU+DTOS(M->TMJ_DTCONS))
nCnt := 0
While !EOF() .And. TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS) == xFilial("TMJ")+TML->TML_CODUSU+DTOS(M->TMJ_DTCONS)
	nCnt++

	nPosInc := 0

	For nFor := 1 to Len(aCols)
		If Empty(aCOLS[nFor][nFICHA]) .and. aCOLS[nFor][nHORAC] == TMJ->TMJ_HRCONS
			nPosInc := nFor
			Exit
		Endif
	Next nFor

	If .f.//nPosInc == 0
		For nFor := 1 to Len(aCols)
			If Empty(aCOLS[nFor][nFICHA]) .and. aCOLS[nFor][nHORAC] > TMJ->TMJ_HRCONS
				nPosInc := nFor
				Exit
			Endif
		Next nFor
	Endif

	If nPosInc > 0
		dbSelectArea("TM0")
		dbSetOrder(1)
		dbSeek(xFilial("TM0")+TMJ->TMJ_NUMFIC)
		aCOLS[nPosInc][nFICHA] := TMJ->TMJ_NUMFIC
		aCOLS[nPosInc][nNOFIC] := Padr(TM0->TM0_NOMFIC,40)
		aCOLS[nPosInc][nMOTIVO] := TMJ->TMJ_MOTIVO
		aCOLS[nPosInc][nHORAC] := TMJ->TMJ_HRCONS
		aCOLS[nPosInc][nMOTIV] := TMJ->TMJ_NATEXA
		If !Empty(aCOLS[nPosInc][nMOTIV])
			aCOLS[nPosInc][nNOEXA] := Busca_Perfil(TMJ->TMJ_NUMFIC,aCOLS[nPosInc][nMOTIV])
		Endif
		//aCOLS[nPosInc][nFUNCC] := NGSEEK("SRJ",TM0->TM0_CODFUN,1,"SRJ->RJ_DESC")
		aCOLS[nPosInc][nOBSER] := TMJ->TMJ_OBSCON
		aCOLS[nPosInc][nCLIEN] := TM0->TM0_CLIENT
		aCOLS[nPosInc][nLOJAC] := TM0->TM0_LOJA
		aCOLS[nPosInc][nNOMEC] := NGSEEK("SA1",aCOLS[nPosInc][nCLIEN]+aCOLS[nPosInc][nLOJAC],1,"SA1->A1_NOME")
		aCOLS[nPosInc][nUsado+1] := .F.
	Endif

	dbSelectArea("TMJ")
	dbSkip()
End
aTotExame  := A075TOTATE()
nTotDia    := aTotExame[1]
nTotTarde  := aTotExame[2]

//Se nao montou agenda.
If !lRet
	dbSelectArea("TMJ")
	dbSetOrder(1)
	dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS))
	nCnt := 0
	While !EOF() .And. TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS) == xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)
	    nCnt++
	    dbSkip()
	End

	If nCNT > 0
		aCols:=Array(nCnt,nUsado+1)

		dbSelectArea("TMJ")
		dbSetOrder(1)
		dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS))
		nCnt := 0
		While !EOF() .And. TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS) == xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)
			dbSelectArea("TM0")
			dbSetOrder(1)
			dbSeek(xFilial("TM0")+TMJ->TMJ_NUMFIC)
			nCnt++
			aCOLS[nCnt][nFICHA] := TMJ->TMJ_NUMFIC
			aCOLS[nCnt][nNOFIC] := PadR(TM0->TM0_NOMFIC,40)
			aCOLS[nCnt][nMOTIVO] := TMJ->TMJ_MOTIVO
			aCOLS[nCnt][nHORAC] := TMJ->TMJ_HRCONS
			aCOLS[nCnt][nMOTIV] := TMJ->TMJ_NATEXA
			If !Empty(aCOLS[nCnt][nMOTIV])
				aCOLS[nCnt][nNOEXA] := Busca_Perfil(TMJ->TMJ_NUMFIC,aCOLS[nCnt][nMOTIV])
			Endif
			//aCOLS[nCnt][nFUNCC] := NGSEEK("SRJ",TM0->TM0_CODFUN,1,"SRJ->RJ_DESC")
			aCOLS[nCnt][nOBSER] := TMJ->TMJ_OBSCON
			aCOLS[nCnt][nCLIEN] := TM0->TM0_CLIENT
			aCOLS[nCnt][nLOJAC] := TM0->TM0_LOJA
			aCOLS[nCnt][nNOMEC] := NGSEEK("SA1",aCOLS[nCnt][nCLIEN]+aCOLS[nCnt][nLOJAC],1,"SA1->A1_NOME")
			aCOLS[nCnt][nUsado+1] := .F.

			dbSelectArea("TMJ")
			dbSkip()
		End
		aTotExame  := A075TOTATE()
		nTotDia    := aTotExame[1]
		nTotTarde  := aTotExame[2]
	Else
		aCols:=Array(1,nUsado+1)
		aCOLS[1][nFICHA] := SPACE(09)
		aCOLS[1][nNOFIC] := SPACE(40)
		aCOLS[1][nNOEXA] := SPACE(80)
		aCOLS[1][nHORAC] := cPer01De
		//aCOLS[1][nFUNCC] := SPACE(40)
		aCOLS[1][nMOTIVO] := SPACE(02)
		aCOLS[1][nMOTIV] := SPACE(01)
		aCOLS[1][nOBSER] := SPACE(50)
		aCOLS[1][nCLIEN] := SPACE(nSizeSA1)
		aCOLS[1][nLOJAC] := SPACE(nSizeLo1)
		aCOLS[1][nNOMEC] := Space(40)
		aCOLS[1][nUsado+1] := .F.

		aTotExame  := A075TOTATE()
		nTotDia    := aTotExame[1]
		nTotTarde  := aTotExame[2]
	Endif
Endif

If _RefreshGet
	oGet:FORCEREFRE()
	oCPO02:REFRESH(.T.)
	oCPO03:REFRESH(.T.)
Endif
Return lRet


/*/


Ŀ
Funo    Busca_Perfil Autor                       Data           
Ĵ
Descrio                                                             
ٱ


/*/
Function Busca_Perfil(cFicha,cNatExa)
Local aArea := GetArea()
Local aAreaTM0 := TM0->(GetArea())
Local ltemexames := .f.
Local lTemPerCusto := .f.
Local cExames := ""

Dbselectarea("TM0")
Dbsetorder(1)
Dbseek(xFilial("TM0")+cFicha)

DbSelectArea("TL2")
DbSetOrder(4) //TL2_FILIAL+TL2_CUSTO+TL2_CODFUN+TL2_CODEXA
If DbSeek(xFilial("TL2")+TM0->TM0_CC+TM0->TM0_CODFUN)
	lTemPerCusto := .t.
Endif

If !lTemPerCusto
	DbSelectArea("TL2")
	DbSetOrder(4) //TL2_FILIAL+TL2_CUSTO+TL2_CODFUN+TL2_CODEXA
	If DbSeek(xFilial("TL2")+TM0->TM0_CC+Space(nSizeCODFUN))
		lTemPerCusto := .t.
	Endif
Endif

DbSelectArea("TON")
DbSetOrder(1)
If DbSeek(xFilial("TON")+TM0->TM0_CODFUN+Substr(TM0->TM0_CC,1,nSizeSA1+nSizeLo1))
	ltemexames := .t.
Endif

If lTemPerCusto
	DbSelectArea("TL2")
	DbSetOrder(1)
	DbSeek(xFilial("TL2")+TM0->TM0_CC)
	While !eof() .and. TL2->(TL2_FILIAL+TL2_CUSTO) == xFilial("TL2")+TM0->TM0_CC

		If !Empty(TL2->TL2_CODFUN) .AND. TL2->TL2_CODFUN != TM0->TM0_CODFUN
			DbSelectArea("TL2")
	   		DbSkip()
	   		Loop
		Endif

		If  (cNatExa == "1" .and. !(TL2->TL2_TIPOEX $ "1 -6 -8 -9 -10-11-12-13-14-15")) .or. ;
			(cNatExa == "2" .and. !(TL2->TL2_TIPOEX $ "2 -6 -7 -8 -11-12-13-14")) .or. ;
			(cNatExa == "3" .and. !(TL2->TL2_TIPOEX $ "3 -10-11-12-13-14-15")) .or. ;
			(cNatExa == "4" .and. !(TL2->TL2_TIPOEX $ "4 -11-12")) .or. ;
			(cNatExa == "5" .and. !(TL2->TL2_TIPOEX $ "5 -7 -8 -9 -11-14-15"))

			DbSelectArea("TL2")
			DbSkip()
			Loop
		Endif

		Dbselectarea("TM4")
		Dbsetorder(01)
		If Dbseek(xFilial("TM4")+TL2->TL2_CODEXA)
			If Empty(cExames)
				cExames += Alltrim(Substr(TM4->TM4_NOMEXA,1,30))
			Else
				cExames += "/"+Alltrim(Substr(TM4->TM4_NOMEXA,1,30))
			Endif
		Endif
		DbSelectArea("TL2")
		Dbskip()
	End
Elseif ltemexames
	DbSelectArea("TON")
	DbSetOrder(1)
	DbSeek(xFilial("TON")+TM0->TM0_CODFUN+Substr(TM0->TM0_CC,1,nSizeSA1+nSizeLo1))
	While !eof() .and. TON->(TON_FILIAL+TON_CODFUN+TON_CLIENT+TON_LOJA) == xFilial("TON")+TM0->TM0_CODFUN+Substr(TM0->TM0_CC,1,nSizeSA1+nSizeLo1)
		If  (cNatExa == "1" .and. !(TON->TON_TIPOEX $ "1 -6 -8 -9 -10-11-12-13-14-15")) .or. ;
			(cNatExa == "2" .and. !(TON->TON_TIPOEX $ "2 -6 -7 -8 -11-12-13-14")) .or. ;
			(cNatExa == "3" .and. !(TON->TON_TIPOEX $ "3 -10-11-12-13-14-15")) .or. ;
			(cNatExa == "4" .and. !(TON->TON_TIPOEX $ "4 -11-12")) .or. ;
			(cNatExa == "5" .and. !(TON->TON_TIPOEX $ "5 -7 -8 -9 -11-14-15"))

			DbSelectArea("TON")
			DbSkip()
			Loop
		Endif

		Dbselectarea("TM4")
		Dbsetorder(01)
		If Dbseek(xFilial("TM4")+TON->TON_CODEXA)
			If Empty(cExames)
				cExames += Alltrim(Substr(TM4->TM4_NOMEXA,1,30))
			Else
				cExames += "/"+Alltrim(Substr(TM4->TM4_NOMEXA,1,30))
			Endif
		Endif
		DbSelectArea("TON")
		Dbskip()
	End
Endif

cExames := PADR(cExames,80)

RestArea(aAreaTM0)
RestArea(aArea)
Return cExames

/*/


Ŀ
Funo    MDT075Trans Autor Andre E. Perez Alvarez  Data 14/12/2006
Ĵ
Descrio  Transfere consulta para outro horario e dia.                
ٱ


/*/
Function MDT075Trans()

Local aArea := Getarea()
Local oDlg, lRet := .f.

If Empty(aCOLS[n][nFICHA])
	msgStop(STR0022)
	Return .F.
Endif

Private dDtCons := M->TMJ_DTCONS,;
      	cHrCons := aCOLS[n][nHORAC]


DEFINE MSDIALOG oDlg FROM  0,0 TO 230,500 TITLE STR0016 PIXEL //"Transferencia de Consulta"

@ 010,008 Say OemToAnsi(STR0017) Size 47,07 Of oDlg Pixel //"Ficha Medica"
@ 010,045 MsGet MDTHideCpo( aCOLS[n][nFICHA], "TM0_NUMFIC" ) Of oDlg Pixel Size 40,08 Picture "@!" When .f.
@ 021,008 Say OemToAnsi(STR0018) Size 47,07 Of oDlg Pixel //"Nome"
@ 021,045 MsGet MDTHideCpo( aCOLS[n][nNOFIC], "TM0_NOMFIC" ) Of oDlg Pixel Size 130,08 Picture "@!" When .f.

@ 4.2,01 TO 7.7,15 LABEL STR0036 OF oDlg  //"Dia e Horrio Atual:"
@ 4.2,16 TO 7.7,30 LABEL STR0037 OF oDlg  //"Transferir Para Dia e Horrio:"

@ 075,018 Say OemToAnsi(STR0024) Size 47,07 Of oDlg Pixel //"Data Consulta"
@ 075,055 MsGet M->TMJ_DTCONS Of oDlg Pixel Size 50,08 Picture "99/99/9999" When .f.
@ 086,018 Say OemToAnsi(STR0025) Size 47,07 Of oDlg Pixel //"Hora Consulta"
@ 086,055 MsGet aCOLS[n][nHORAC] Of oDlg Pixel Size 50,08 Picture "99:99" When .f.

@ 075,119+018 Say OemToAnsi(STR0024) Size 47,07 Of oDlg Pixel Color CLR_HBLUE  //"Data Consulta"
@ 075,119+055 MsGet dDtCons Of oDlg Pixel Size 50,08 Picture "99/99/9999" When .T.
@ 086,119+018 Say OemToAnsi(STR0025) Size 47,07 Of oDlg Pixel  Color CLR_HBLUE  //"Hora Consulta"
@ 086,119+055 MsGet cHrCons Of oDlg Pixel Size 50,08 Picture "99:99" Valid NGVALHORA(cHrCons,.t.) When .T.

DEFINE SBUTTON FROM 40, 180 TYPE 1 ENABLE OF oDlg ACTION EVAL({|| lRET := ValidA075(),If(lRET,oDlg:End(),nil)})
DEFINE SBUTTON FROM 40, 210 TYPE 2 ENABLE OF oDlg ACTION oDlg:END()
ACTIVATE MSDIALOG oDlg CENTERED

If lRet

	dbSelectArea("TMJ")
	dbSetOrder(11)  //TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS)+TMJ_HRCONS+TMJ_NUMFIC
	If dbSeek( xFilial("TMJ") +  M->TMJ_CODUSU + DtoS(M->TMJ_DTCONS) +  aCOLS[n][nHORAC] + aCOLS[n][nFICHA] ) //linha do aCOLS ja esta gravado
		RecLock("TMJ", .F.)
		TMJ->TMJ_DTCONS := dDtCons
		TMJ->TMJ_HRCONS := cHrCons
		MsUNlock("TMJ")
	Else	//linha do aCOLS nao esta gravado
		RecLock("TMJ", .T.)
		TMJ->TMJ_FILIAL := xFilial("TMJ")
		TMJ->TMJ_CODUSU := M->TMJ_CODUSU
		TMJ->TMJ_DTCONS := dDtCons
		TMJ->TMJ_DTPROG := cHrCons
		TMJ->TMJ_NUMFIC := aCOLS[n][nFICHA]
		TMJ->TMJ_MAT    := NGSEEK("TM0",aCOLS[n][nFICHA],1,"TM0->TM0_MAT")
		TMJ->TMJ_HRCONS := aCOLS[n][nHORAC]
		TMJ->TMJ_NATEXA := aCOLS[n][nMOTIV]
		TMJ->TMJ_MOTIVO := aCOLS[n][nMOTIVO]
		TMJ->TMJ_OBSCON := aCOLS[n][nOBSER]
		TMJ->TMJ_EXAME  := "NR7"
		MsUNlock("TMJ")
	Endif

	Monta_Agenda(.t.)

Endif
RestArea(aArea)
Return .T.

/*/


Ŀ
Funo    |ValidA075 |Autor  Andre E. Perez Alvarez  Data 14/12/2002
Ĵ
Descrio Validacao de inclusao                                       
ٱ


/*/
Function ValidA075()

Local cCalend := ""
Local aArea   := GetArea()

dbSelectArea("TML")
dbSetOrder(1)
If dbSeek(xFilial("TML")+cCodMed)
	cCalend := TML->TML_CALEND
Endif

If Empty(dDtCons)
	RestArea(aArea)
	Return .f.
Endif
If dDtCons < ddatabase
	msgStop( STR0164 )
	Return .f.
Endif
If !NGVALHORA(cHrCons,.t.)
	RestArea(aArea)
	Return .f.
Endif

If ! ( MDTDIS075() .and. VALHR075(.f.,cCaleMed,dDtCons,cHrCons,'I',aCOLS[n][nFICHA],aCOLS[n][nMotiv],aCOLS[n][nHORAC]) )
	RestArea(aArea)
	Return .f.
Endif

Dbselectarea("TMJ")
Dbsetorder(11)  //TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS)+TMJ_HRCONS+TMJ_NUMFIC
If Dbseek(xFilial("TMJ") + M->TMJ_CODUSU + DtoS(dDtCons) +  cHrCons + aCOLS[n][nFICHA])
	msgStop( STR0023 ) //"Ja existe consulta marcada nesse horario para este funcionrio."
	RestArea(aArea)
	Return .f.
Endif

If !MDT75VLHRC(cCalend,DDTCONS,CHRCONS,"I",.T.)
	RestArea(aArea)
	Return .F.
EndIf

RestArea(aArea)

Return .t.

/*/


Ŀ
Funo    |MDTDIS075 |Autor  Andre E. Perez Alvarez  Data 14/12/2006
Ĵ
Descrio Verifica limite de consultas no periodo, e se o medico esta 
          alocado em cliente nesse dia e horario.                     
ٱ


/*/
Function MDTDIS075()

Local aArea      := GetArea()
Local aAreaTMJ   := TMJ->(GetArea())
Local lRet       := .t.
Local nContaCons := 0
Local aHrMedSv   := {nMedicosDia, nMedicosTar, nIntervalo}

//Carrega a quantidade de medicos disponiveis para a data da transferencia da consulta.
aMedicos := fHorMedic(dDtCons)
nMedicosDia := aMedicos[1]
nMedicosTar := aMedicos[2]
nIntervalo  := aMedicos[3]

//Verifica quantas consultas ja existem para a data e horario que o usuario deseja transferir
dbSelectArea("TMJ")
dbSetOrder(1)
dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(dDtCons)+cHrCons)
While !EOF() .And. TMJ->(TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS)+TMJ_HRCONS) == xFilial("TMJ")+M->TMJ_CODUSU+DTOS(dDtCons)+cHrCons
	nContaCons++
	dbskip()
End

//Se for de Manha
If cHrCons <= "12:00"
	If nMedicosDia == 0
		lRet := .f.
	Endif
	If nContaCons == nMedicosDia .and. nMedicosDia > 0
		MsgStop(STR0095+Alltrim(Str(nMedicosDia,4))+STR0096) //"Excedeu o limite de consultas para este horrio. Existem "###" mdicos disponveis para o perodo da manh."
		lRet := .f.
	Endif

	If Alltrim(cPer01De) <> ":" .and. MOD(HTOM(cHRCONS)-HTOM(cPer01De),nIntervalo) <> 0 .and. lRet
		MsgStop(STR0097+Str(nIntervalo,3)+STR0098) //"O horrio da consulta dever ser marcado no intervalo de "###"min de atendimento."
		lRet := .f.
	Endif
Else
//Se for de Tarde
	If nMedicosTar == 0
		lRet := .f.
	Endif
	If nContaCons == nMedicosTar .and. nMedicosTar > 0
		MsgStop(STR0095+Alltrim(Str(nMedicosTar,4))+STR0099) //"Excedeu o limite de consultas para este horrio. Existem "###" mdicos disponveis para o perodo da tarde."
		lRet := .f.
	Endif
	If Alltrim(cPer01De) <> ":" .and. MOD(HTOM(cHRCONS)-HTOM(cPer02De),nIntervalo) <> 0 .and. lRet
		MsgStop(STR0097+Str(nIntervalo,3)+STR0098) //"O horrio da consulta dever ser marcado no intervalo de "###"min de atendimento."
		lRet := .f.
	Endif
Endif

RestArea(aAreaTMJ)
RestArea(aArea)

//Restaura os valores do dia atual
nMedicosDia := aHrMedSv[1]
nMedicosTar := aHrMedSv[2]
nIntervalo  := aHrMedSv[3]

Return lret

/*/


Ŀ
Funo    |NG075EX2  |Autor  Andre E. Perez Alvarez  Data 14/12/2006
Ĵ
Descrio Deleta a consulta transferida.                              
ٱ


/*/
Function NG075EX2()
Local aCols2 := aClone(aCOls)

dbSelectArea("TMJ")
dbSetOrder(1)
IF dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+acols2[n][nHORAC])
	If !EMPTY(TMJ_DTATEN)
		MsgStop(STR0107) //"Esta consulta no pode ser excluda, pois j foi realizada."
		Return .f.
	Endif
Endif

dbSelectArea("TMJ")
dbSetOrder(1)
dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+acols[n][nHORAC])
While !EOF() .And. TMJ->(TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS)+TMJ_HRCONS) == xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+acols[n][nHORAC]
	IF TMJ->TMJ_NUMFIC == acols[n][nFicha]
		RecLock("TMJ",.f.)
		Dbdelete()
		MsUnLock("TMJ")
		Exit
	Endif
	dbskip()
End

aCOLS[n][nFICHA] := Space(Len(aCOLS[n][nFICHA]))
aCOLS[n][nNOFIC] := Space(Len(aCOLS[n][nNOFIC]))
aCOLS[n][nNOEXA] := Space(Len(aCOLS[n][nNOEXA]))
aCOLS[n][nMOTIV] := Space(Len(aCOLS[n][nMOTIV]))
aCOLS[n][nMOTIVO] := Space(Len(aCOLS[n][nMOTIVO]))
aCOLS[n][nOBSER] := Space(Len(aCOLS[n][nOBSER]))
aCOLS[n][nCLIEN] := Space(Len(aCOLS[n][nCLIEN]))
aCOLS[n][nLOJAC] := Space(Len(aCOLS[n][nLOJAC]))
aCOLS[n][nNOMEC] := Space(30)
aCOLS[n][nUsado+1] := .F.

aTotExame  := A075TOTATE()
nTotDia    := aTotExame[1]
nTotTarde  := aTotExame[2]
oCPO02:REFRESH(.T.)
oCPO03:REFRESH(.T.)
oCPO04:REFRESH(.T.)
oCPO05:REFRESH(.T.)
oCPO06:REFRESH(.T.)
oCPO07:REFRESH(.T.)
oGet:FORCEREFRE()

Return .t.

/*


ͻ
Programa    MDT075ALFI      Andre E. Perez Alvarez        15-12-06   
͹
Desc.      Altera a Ficha Medica                                      
͹
Uso        MDTA075                                                    
ͼ


*/
Function MDT075ALFI(lBotao_,c_NUMFIC,nTipoOp)
Local aAreaXXX := GetArea()
Local aAreaTm0 := TM0->(GetArea())
Local lRet    := .t.
Local nX, i
Local lFunDemitido := .f.

Default nTipoOp := 0

If lBotao_
	If !Pergunte("MDT75A    ",.T.)
		lRet := .f.
	Endif
Else
	Mv_par01 := c_NUMFIC
Endif

If lRet

	If !ExistCpo("TM0",Mv_par01)
		lRet := .f.
	Endif

	If lRet
		dbSelectArea("TM0")
		dbSetOrder(1)
		dbSeek(xFilial("TM0")+Mv_par01)

		dbSelectArea("SA1")
		dbSetOrder(1)
		dbSeek(xFilial("SA1")+TM0->TM0_CLIENT+TM0->TM0_LOJA)

		cCliMdtPs := TM0->TM0_CLIENT+TM0->TM0_LOJA

		If SitFunFicha(Mv_par01,.f.,.f.,.f.)

			bNGGRAVA  := {|| CHK2TA005()}

			Inclui := .f.
			Altera := .t.
			cFichaGrv := TM0->TM0_MAT
			cDepenGrv := TM0->TM0_NUMDEP
			cSetorGrv := TM0->TM0_CC
			cFuncaGrv := TM0->TM0_CODFUNC
			cClieTM0  := TM0->TM0_CLIENT
			cLojaTM0  := TM0->TM0_LOJA
			cRG__TM0  := TM0->TM0_RG

			Begin Transaction

				If NGCAD01("TM0",TM0->(Recno()),4) == 1

					//Se alterou a empresa
					If cClieTM0 <> TM0->TM0_CLIENT
						aTM0area := TM0->(getarea())

						dbSelectArea("SRA")
						dbSetOrder(1)
						If dbSeek(TM0->TM0_FILFUN+TM0->TM0_MAT) .and. !Empty(TM0->TM0_MAT)
							RecLock("SRA",.f.)
							SRA->RA_DEMISSA := dDataBase
							SRA->RA_SITFOLH := "D"
							SRA->(MsUnLock())

							For i := 1 To FCount()
								x  := "m->" + FieldName(i)
								y  := "SRA->" + FieldName(i)
								&x := &y
							Next i

							cMat_D   := GETSXENUM('SRA','RA_MAT')
							CONFIRMSX8()
							cFicha_D := GETSXENUM('TM0','TM0_NUMFIC')
							CONFIRMSX8()

							RecLock("SRA",.t.)
							For i := 1 To FCount()
								If FieldName(i) == "RA_FILIAL" .OR. FieldName(i) == "RA_MAT" .OR. FieldName(i) == "RA_CC" .OR.;
									FieldName(i) == "RA_MAT" .OR. FieldName(i) == "RA_DEMISSA" .OR. FieldName(i) == "RA_SITFOLH" .OR.;
									FieldName(i) == "RA_CODFUNC" .OR. FieldName(i) == "RA_ADMISSA"

									Loop
								EndIf
								x := "m->" + FieldName(i)
								y := "SRA->" + FieldName(i)
								&y := &x
							Next i
							SRA->RA_FILIAL  := cFilAnt
							SRA->RA_MAT     := cMat_D
							SRA->RA_CC      := TM0->TM0_CC
							SRA->RA_CODFUNC := TM0->TM0_CODFUN
							SRA->RA_ADMISSA := dDataBase
							SRA->(Msunlock())

							dbSelectArea("TM0")
							dbSetOrder(1)
							RecLock('TM0',.T.)
							TM0->TM0_FILIAL  := xfilial('TM0')
							TM0->TM0_FILFUN  := SRA->RA_FILIAL
							TM0->TM0_NUMFIC  := cFicha_D
							TM0->TM0_DTIMPL  := SRA->RA_ADMISSA
							TM0->TM0_NOMFIC  := SRA->RA_NOME
							TM0->TM0_SANGUE  := "5"
							TM0->TM0_FATORH  := "1"
							TM0->TM0_DOADOR  := "2"
							TM0->TM0_MAT     := SRA->RA_MAT
							TM0->TM0_DTNASC  := SRA->RA_NASC
							TM0->TM0_RG      := SRA->RA_RG
							TM0->TM0_CODFUN  := SRA->RA_CODFUNC
							TM0->TM0_CC      := SRA->RA_CC
							TM0->TM0_CLIENT  := Substr(SRA->RA_CC,1,nSizeCli)
							TM0->TM0_LOJA    := Substr(SRA->RA_CC,nSizeCli+1,nSizeLoj)
							TM0->TM0_SEXO    := If(SRA->RA_SEXO = "M",'1','2')
							TM0->TM0_FUMA    := "2"
							TM0->(MsUnLock())

							a2TM0area := TM0->(getarea())

							dbSelectArea("SRE")
							dbSetOrder(1)
							RecLock("SRE",.t.)
							SRE->RE_FILIAL  := xFilial("SRE")
							SRE->RE_DATA    := dDataBase
							SRE->RE_EMPD    := cEmpAnt
							SRE->RE_FILIALD := cFilAnt
							SRE->RE_MATD    := cFichaGrv
							SRE->RE_CCD     := cSetorGrv
							SRE->RE_EMPP    := cEmpAnt
							SRE->RE_FILIALP := cFilAnt
							SRE->RE_MATP    := SRA->RA_MAT
							SRE->RE_CCP     := SRA->RA_CC
							MsUnlock("SRE")

							dbSelectArea("SR7")
							RecLock("SR7",.T.)
							SR7->R7_FILIAL   := cFilAnt
							SR7->R7_MAT      := SRA->RA_MAT
							SR7->R7_DATA     := dDataBase
							SR7->R7_TIPO     := "001"
							SR7->R7_FUNCAO   := SRA->RA_CODFUNC
							SR7->R7_DESCFUN  := Posicione("SRJ",1,xFilial("SRJ")+SRA->RA_CODFUNC,"RJ_DESC")
							SR7->R7_USUARIO  := STR0148 //"Sistema"
							SR7->(MsUnLock())

							RestArea(aTM0area)
							If !Eof()
								RecLock("TM0",.f.)
								TM0->TM0_CODFUN  := cFuncaGrv
								TM0->TM0_CC      := cSetorGrv
								TM0->TM0_CLIENT  := cClieTM0
								TM0->TM0_LOJA    := cLojaTM0
								TM0->(MsUnLock())
							Endif
							Restarea(a2TM0area)
						Endif

					//Se nao alterou a empresa
					Else
						dbSelectArea("SRA")
						dbSetOrder(1)
						If dbSeek(TM0->TM0_FILFUN+TM0->TM0_MAT) .and. !Empty(TM0->TM0_MAT)

							//Salva SRA
							aAreaFUN := SRA->(GetArea())
							If cFuncaGrv <> TM0->TM0_CODFUNC
								M->RA_CODFUNC := TM0->TM0_CODFUNC
								M->RA_TIPOPGT := SRA->RA_TIPOPGT
								M->RA_CATFUNC := SRA->RA_CATFUNC
								fGravaSr3(dDataBase,"001",SRA->RA_SALARIO)
								//Restaura SRA
								RestArea(aAreaFUN)
							Endif

							If cSetorGrv <> TM0->TM0_CC
								Dbselectarea("SRE")
								Dbsetorder(1)
								RecLock("SRE",.t.)
								SRE->RE_FILIAL  := xFilial("SRE")
								SRE->RE_DATA    := dDataBase
								SRE->RE_EMPD    := SM0->M0_CODIGO
								SRE->RE_FILIALD := SM0->M0_CODFIL
								SRE->RE_MATD    := SRA->RA_MAT
								SRE->RE_CCD     := SRA->RA_CC
								SRE->RE_EMPP    := SM0->M0_CODIGO
								SRE->RE_FILIALP := SM0->M0_CODFIL
								SRE->RE_MATP    := SRA->RA_MAT
								SRE->RE_CCP     := TM0->TM0_CC
								MsUnlock("SRE")
								//Restaura SRA
								RestArea(aAreaFUN)
							Endif

							SRA->(RecLock("SRA",.f.))
							SRA->RA_NOME	:= TM0->TM0_NOMFIC
							SRA->RA_SEXO	:= If(TM0->TM0_SEXO <> "2","M","F")
							SRA->RA_ESTCIVI	:= If(!Empty(TM0->TM0_ESTCIV),TM0->TM0_ESTCIV,SRA->RA_ESTCIVI)
							SRA->RA_NASC	:= If(!Empty(TM0->TM0_DTNASC),TM0->TM0_DTNASC,SRA->RA_NASC)
							SRA->RA_CC		:= TM0->TM0_CC
							SRA->RA_CODFUNC	:= TM0->TM0_CODFUN
							SRA->RA_RG  	:= If(!Empty(TM0->TM0_RG)   ,TM0->TM0_RG   ,SRA->RA_RG)
							SRA->RA_NUMCP  	:= If(!Empty(TM0->TM0_NUMCP),TM0->TM0_NUMCP,SRA->RA_NUMCP)
							SRA->RA_SERCP	:= If(!Empty(TM0->TM0_SERCP),TM0->TM0_SERCP,SRA->RA_SERCP)
							SRA->RA_UFCP	:= If(!Empty(TM0->TM0_UFCP) ,TM0->TM0_UFCP ,SRA->RA_UFCP)
							SRA->(MsUnlock())

							dbSelectArea("TM5")
							dbSetOrder(1)
							dbSeek(xFilial("TM5")+TM0->TM0_NUMFIC)
							While !Eof() .and. xFilial("TM5")+TM0->TM0_NUMFIC == TM5->TM5_FILIAL+TM5->TM5_NUMFIC
								RecLock("TM5",.f.)
								TM5->TM5_CC      := SRA->RA_CC
								TM5->TM5_CODFUN  := SRA->RA_CODFUNC
								TM5->(MsUnLock())
								dbSkip()
							End

						Endif
	                Endif

					For nX := 1 To Len(aCols)
						If Mv_par01 == aCols[nX,nFICHA]
							dbSelectArea("TMJ")
							dbSetOrder(11)
							If dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DtoS(M->TMJ_DTCONS)+aCOLS[nX,nHORAC]+aCols[nX,nFICHA])
								RecLock("TMJ",.f.)
								TMJ->TMJ_NUMFIC := TM0->TM0_NUMFIC
								TMJ->TMJ_MAT    := TM0->TM0_MAT
								TMJ->(MsUnLock())
							Endif
							aCols[nX,nFICHA] := TM0->TM0_NUMFIC
							aCols[nX,nNOFIC] := TM0->TM0_NOMFIC
							aCols[nX,nCLIEN] := TM0->TM0_CLIENT
							aCols[nX,nLOJAC] := TM0->TM0_LOJA
							aCOLS[nX,nNOMEC] := NGSEEK("SA1",aCOLS[nX][nCLIEN]+aCOLS[nX][nLOJAC],1,"SA1->A1_NOME")
							//aCOLS[nX,nFUNCC] := NGSEEK("SRJ",TM0->TM0_CODFUN,1,"SRJ->RJ_DESC")
						Endif
					Next nX
				Else
					lRet := .f.
				Endif

			End Transaction

			bNGGRAVA := {}
        Else
			lRet := .f.
			lFunDemitido := .t.
        Endif
	Endif
Endif

If lFunDemitido
	If MsgYesNo(STR0128+Chr(13)+Chr(10)+; //"No ser possvel selecion-lo, pois o funcionrio est demitido."
				STR0129) //"Deseja realizar a admisso em outra empresa/unidade, aproveitando as informaes j cadastradas?"

		c_Mv_par01 := Mv_par01
		// Gera nova ficha, pois funcionario estava demitido
		If !MDT075ITM0() //Nao gerou ficha
			lRet := .f.
		Else //Gerou ficha
			lRet := .t.

			If !lBotao_ // Se for setor ou unidade inativa e o funcionario esta demitido
				If nTipoOp == 1
					M->TMJ_NUMFIC := TM0->TM0_NUMFIC
				ElseIf nTipoOp == 2
					Dbselectarea("TRBK")
					If !Eof() .and. !Bof()
						RecLock("TRBK",.f.)
						TRBK->NOMFIC := TM0->TM0_NOMFIC
						TRBK->NUMFIC := TM0->TM0_NUMFIC
						TRBK->CLIENT := TM0->TM0_CLIENT
						TRBK->LOJA   := TM0->TM0_LOJA
						TRBK->NOMCLI := Posicione("SA1",1,xFilial("SA1")+TM0->TM0_CLIENT+TM0->TM0_LOJA,"A1_NOME")
						MsUnLock("TRBK")
					Endif
				Endif
			Endif

			For nX := 1 To Len(aCols)
				If c_Mv_par01 == aCols[nX,nFICHA]
					dbSelectArea("TMJ")
					dbSetOrder(11)  //TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS)+TMJ_HRCONS+TMJ_NUMFIC
					If dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DtoS(M->TMJ_DTCONS)+aCOLS[nX,nHORAC]+aCols[nX,nFICHA])
						RecLock("TMJ",.f.)
						TMJ->TMJ_NUMFIC := TM0->TM0_NUMFIC
						TMJ->TMJ_MAT    := TM0->TM0_MAT
						TMJ->(MsUnLock())
					Endif
					aCols[nX,nFICHA] := TM0->TM0_NUMFIC
					aCols[nX,nNOFIC] := TM0->TM0_NOMFIC
					aCols[nX,nCLIEN] := TM0->TM0_CLIENT
					aCols[nX,nLOJAC] := TM0->TM0_LOJA
					aCOLS[nX,nNOMEC] := NGSEEK("SA1",aCOLS[nX][nCLIEN]+aCOLS[nX][nLOJAC],1,"SA1->A1_NOME")
					//aCOLS[nX,nFUNCC] := NGSEEK("SRJ",TM0->TM0_CODFUN,1,"SRJ->RJ_DESC")
				Endif
			Next nX
		Endif
	Else
		lRet := .f.
	Endif
Endif

RestArea(aAreaTm0)
RestArea(aAreaXXX)
Inclui := .t.
Return lRet

/*/


Ŀ
 Funo   fTotConDia   Autor Denis                   Data  15/12/06 
Ĵ
 Descrio Busca a quantidade de medicos disponiveis em dias anteriores
ٱ


/*/

Static Function fTotConDia(vTMJ_CODUSU,vTMJ_DTCONS,nQuantPar,nTipo)

Local nX
Local nConsultas := nQuantPar
Local aArea := GetArea()
Local aHrsDia := {}
Local aHrsTar := {}
Local nPosScan

dbSelectArea("TMJ")
dbSetOrder(1)
dbSeek(xFilial("TMJ")+vTMJ_CODUSU+DTOS(vTMJ_DTCONS))
While !EOF() .And. TMJ->(TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS)) == xFilial("TMJ")+vTMJ_CODUSU+DTOS(vTMJ_DTCONS)

	If TMJ->TMJ_HRCONS <= "12:00" .and. nTipo == 1
		nPosScan := aSCAN(aHrsDia,{|x| x[1] == TMJ->TMJ_HRCONS })
		If nPosScan > 0
			aHrsDia[nPosScan,2] += 1
		Else
			aADD(aHrsDia,{TMJ->TMJ_HRCONS,1})
		Endif
	ElseIf TMJ->TMJ_HRCONS > "12:00" .and. nTipo == 2
		nPosScan := aSCAN(aHrsTar,{|x| x[1] == TMJ->TMJ_HRCONS })
		If nPosScan > 0
			aHrsTar[nPosScan,2] += 1
		Else
			aADD(aHrsTar,{TMJ->TMJ_HRCONS,1})
		Endif
	Endif

	dbSelectArea("TMJ")
	dbskip()
End

If nTipo == 1
	For nX := 1 To Len(aHrsDia)
		If aHrsDia[nX,2] > nConsultas
			nConsultas := aHrsDia[nX,2]
		Endif
	Next nX
Else
	For nX := 1 To Len(aHrsTar)
		If aHrsTar[nX,2] > nConsultas
			nConsultas := aHrsTar[nX,2]
		Endif
	Next nX
Endif

RestArea(aArea)
Return nConsultas

/*/


Ŀ
 Funo   MDTDIASEMA   Autor Denis                   Data  15/12/06 
Ĵ
 Descrio Busca o dia da semana                                       
ٱ


/*/
Function MDTDIASEMA(dData)

Local aSemana := {STR0043,STR0044,STR0045,STR0046,STR0047,STR0048,STR0049} //"DOMINGO"###"SEGUNDA-FEIRA"###"TERA-FEIRA"###"QUARTA-FEIRA"###"QUINTA-FEIRA"###"SEXTA-FEIRA"###"SBADO"
Local nDIA := DOW(dData)

Return If(nDIA>0,aSemana[nDIA],Space(20))

/*/


Ŀ
 Funo   MDTDIASEMA   Autor Denis                   Data  15/12/06 
Ĵ
 Descrio Retorna a quantidade de medico manha, tarde, ou intervalo   
ٱ


/*/
Function fHorMedic(dAgenda)
Local aRet := {0,0,0}
Local aArea := GetArea()
Private dTMJ_DTCONS := dAgenda

Set Century ON

dbSelectArea("TLF")
dbSetorder(1)
If dbSeek(xFilial("TLF")+DTOS(dTMJ_DTCONS))

	aRet := {TLF->TLF_QTDDIA,TLF->TLF_QTDTAR,If( TLF->TLF_INTERV == 0 , 10 , TLF->TLF_INTERV )}

ElseIf MsgYesNo(STR0131+DTOC(dTMJ_DTCONS)+STR0132+; //"A disponibilidade de mdicos para a data "###" no foi informada. "
				STR0133) //"Deseja informar agora?"

	lTLF_DTAGEN := .f.
	aRelac := {{"TLF_DTAGEN","dTMJ_DTCONS"},{"TLF_SEMANA","MDTDIASEMA(dTMJ_DTCONS)"}}
	aCHOICE := nil
		aCHOICE := { "TLF_DTAGEN"   ,;
		             "TLF_SEMANA"   ,;
		             "TLF_QTDDIA"   ,;
		             "TLF_QTDTAR"   ,;
		             "TLF_INTERV"    }
	If Type("bNGGRAVA") <> "U"
		__bNGGRAVA := bNGGRAVA
		bNGGRAVA := nil
	Endif

	_Inclui := Inclui
	_Altera := Altera
	Inclui := .t.
	Altera := .f.
	If NgCad01("TLF",TLF->(Recno()),3) == 1
		aRet := {TLF->TLF_QTDDIA,TLF->TLF_QTDTAR,If( TLF->TLF_INTERV == 0 , 10 , TLF->TLF_INTERV )}
	Endif
	Inclui := _Inclui
	Altera := _Altera
	lTLF_DTAGEN := .t.
	If Type("__bNGGRAVA") <> "U"
		bNGGRAVA := __bNGGRAVA
	Endif
	aRelac := {}
	aCHOICE := { "TMJ_CODUSU"   ,;
	             "TMJ_NOMUSU"   ,;
	             "TMJ_DTCONS"   ,;
	             "TMJ_HRCONS"   ,;
	             "TMJ_NUMFIC"   ,;
	             "TMJ_NOMFIC"   ,;
	             "TMJ_MOTIVO"   ,;
	             "TMJ_NATEXA"   ,;
	             "TMJ_NOMOTI"   ,;
	             "TMJ_EXAME"    ,;
	             "TMJ_NOMEXA"   ,;
	             "TMJ_CODCLI"   ,;
	             "TMJ_LOJCLI"   ,;
	             "TMJ_NOMCLI"   ,;
	             "TMJ_OBSCON"    }
Endif

Set Century OFF
RestArea(aArea)
Return aRet

/*/


Ŀ
 Funo   VALHR075     Autor Denis                   Data  13/02/07 
Ĵ
 Descrio Valida Hora consulta                                        
ٱ


/*/
Function VALHR075(lPergunta,cCaleMed,dDTCONS,cHRCONS,cTipo,cNumFicha, cNatureza,c2HRCONS)
Local lRet
Local aAreaXXX := GetArea()
Local aAreaTLF := TLF->(GetArea())
Local lTLF := .f.

Default lPergunta := .f.

dbSelectArea("TLF")
dbSetorder(1)
If dbSeek(xFilial("TLF")+DTOS(dDTCONS))
	If !Empty(TLF->TLF_HRINID) .AND. !Empty(TLF->TLF_HRFIMD) .AND. !Empty(TLF->TLF_HRINIT) .AND. !Empty(TLF->TLF_HRFIMT)
		lTLF := .t.
		If (cHRCONS < TLF->TLF_HRINID .or. cHRCONS > TLF->TLF_HRFIMD) .and. ;
			(cHRCONS < TLF->TLF_HRINIT .or. cHRCONS > TLF->TLF_HRFIMT)

			MsgInfo(STR0134+DTOC(dDTCONS)+"."+Chr(13)+Chr(10)+; //"Horario invalido para o dia "
					STR0135+Chr(13)+Chr(10)+; //"Horarios disponiveis:"
					TLF->TLF_HRINID+STR0136+TLF->TLF_HRFIMD+Chr(13)+Chr(10)+; //" as "
					TLF->TLF_HRINIT+STR0136+TLF->TLF_HRFIMT,STR0149) //" as "###"NAO CONFORMIDADE"

			lRet := .f.
		Else
			lRet := .t.
		Endif
	Endif
Endif

If !lTLF
	If !Empty(cHRCONS) .and. (cHRCONS == cPer01Ate .or. cHRCONS == cPer02Ate)// .or. cHRCONS == cPer03Ate)
		lRet := NGVALHRCALE(cCaleMed,dDTCONS,cHRCONS,"F")
	Else
		lRet := NGVALHRCALE(cCaleMed,dDTCONS,cHRCONS,cTipo)
	Endif
Endif

If ValType(cNumFicha) == "C" .and. ValType(cNatureza) == "C"
	If fTemEegEcg(cNumFicha, cNatureza, dDTCONS, cHRCONS, c2HRCONS)
		If lPergunta
			If MsgYesNo(STR0137+; //"O intervalo entre duas consultas que necessitam dos exames ECG ou EEG deve ser de no mnimo 30 minutos. "
						STR0138) //"Deseja agendar para o prximo horrio vago?"

				//Verifica proximo horario disponivel para atendimentos especiais (ECG/EEG)
				// Retorna a posicao do aCols disponivel
				nPosTroca := fProxAtend(cHRCONS,cNumFicha,c2HRCONS)
				If nPosTroca > 0 .and. Len(aCOLS) >= nPosTroca

					cFichaAn := aCOLS[n][nFICHA]
					cNatxaAn := aCOLS[n][nMotiv]
					cMotivo  := aCOLS[n][nMotivo]

					//Deletar linha atual e incluir no proximo horario vago
					NG075EXCC(.f.)

					dbSelectArea("TM0")
					dbSetOrder(1)
					dbSeek(xFilial("TM0")+cFichaAn)

					aCOLS[nPosTroca][nFICHA] := TM0->TM0_NUMFIC
					aCOLS[nPosTroca][nNOFIC] := TM0->TM0_NOMFIC
					aCOLS[nPosTroca][nCLIEN] := TM0->TM0_CLIENT
					aCOLS[nPosTroca][nLOJAC] := TM0->TM0_LOJA
					aCOLS[nPosTroca][nNOMEC] := NGSEEK("SA1",TM0->TM0_CLIENT+TM0->TM0_LOJA,1,"SA1->A1_NOME")
					aCOLS[nPosTroca][nNOEXA] := Busca_Perfil(TM0->TM0_NUMFIC,cNatxaAn)
					aCOLS[nPosTroca][nMOTIV] := cNatxaAn
					aCOLS[nPosTroca][nMOTIVO] := cMotivo

					oGet:oBrowse:nAt := nPosTroca
					oGet:oBrowse:Refresh()

					M->TMJ_HRCONS := aCOLS[nPosTroca][nHORAC]

					MsgInfo(STR0139+aCOLS[nPosTroca][nHORAC]) //"A consulta foi agendanda para o horrio - "

					Return .t.
				Else
					MsgInfo(STR0140) //"No existe horrio livre para a agenda deste dia."
					Return .f.
				Endif
			Else
				Return .f.
			Endif
		Else
			MsgStop(STR0141+; //"Este horrio no est disponvel para consultas que necessitam dos exames ECG ou EEG. "
					STR0142) //"O intervalo entre duas consultas desta natureza deve ser de no mnimo 30 minutos."
			lRet := .f.
		Endif
	Endif
Endif

RestArea(aAreaTLF)
RestArea(aAreaXXX)

Return lRet
/*


ͻ
Programa    AgendaWhen      Denis Hyroshi de Souza       14-02-07   
͹
Desc.      When do campo NUMFIC e NOMFIC                              
͹
Uso        MDTA075                                                    
ͼ


*/
Function AgendaWhen(nTipo)

Local nConsDiaHr := 0
Local lOutroUser := .f.
Local lret       := .t.
Local nSalvo

//Se no for chamado pelo programa de AGENDA MEDICA, retornar sempre VERDADEIRO
If Type("cPrograma") == "C" .and. Type("aCols") == "A"
	If cPROGRAMA != "MDTA075"
		Return .t.
	Endif
Else
	Return .t.
Endif

nSalvo     := n

//Se ja estiver preenchido os campos Ficha ou Nome
If (nTipo == 1 .and. !Empty(acols[n][nFICHA])) .or. (nTipo == 2 .and. !Empty(acols[n][nNOFIC]))
	Return .t.
Endif

//Exclui os horarios reservados do usuario
DelReserv(M->TMJ_CODUSU,M->TMJ_DTCONS,acols[n][nHORAC])

dbSelectArea("TMJ")
dbSetOrder(1)
dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+acols[n][nHORAC])
While !eof() .And. TMJ->(TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS)+TMJ_HRCONS) == xFilial("TMJ")+;
											M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+acols[n][nHORAC]

	//Verifica se tem registro reservado por outro usuario do sistema
	If Empty(TMJ->TMJ_NUMFIC) .and. TMJ->TMJ_USURES <> SubStr(cUsuario,7,15) .and. !Empty(TMJ->TMJ_USURES)
		lOutroUser := .t.
	Endif

	//Acumula a quantidade de consultas para o horario
	nConsDiaHr++

	dbskip()
End

If (acols[n][nHORAC] <= "12:00" .and. nConsDiaHr >= nMedicosDia) .or. ;
	(acols[n][nHORAC] > "12:00" .and. nConsDiaHr >= nMedicosTar)

	lret := .f.
	If lOutroUser
		MsgStop(STR0109) //"Este horrio est reservado para outro usurio do sistema."
	Else
		MsgStop(STR0101) //"Excedeu o limite de consultas para este horrio."
		Monta_Agenda(.t.)
		n := nSalvo
		oGet:oBrowse:nAt := nSalvo
		oGet:oBrowse:Refresh()
	Endif
Endif

If lret
	RecLock("TMJ",.T.)
	TMJ->TMJ_FILIAL := xFilial("TMJ")
	TMJ->TMJ_CODUSU := M->TMJ_CODUSU
	TMJ->TMJ_DTCONS := M->TMJ_DTCONS
	TMJ->TMJ_DTPROG := M->TMJ_DTCONS
	TMJ->TMJ_NUMFIC := Space(9)
	TMJ->TMJ_HRCONS := aCOLS[n][nHORAC]
	TMJ->TMJ_USURES := SubStr(cUsuario,7,15)
	TMJ->TMJ_DTARES := dDataBase
	TMJ->TMJ_HORRES := Substr(Time(),1,5)
	TMJ->TMJ_EXAME  := "NR7"
	TMJ->(MsUnLock())
Endif

Return lret

/*


ͻ
Programa    DelReserv       Denis Hyroshi de Souza       14-02-07   
͹
Desc.      Exclui os horarios reservados do usuario                   
͹
Uso        MDTA075                                                    
ͼ


*/
Function DelReserv(cMedico,dDataCon,cHoraCon)

dbSelectArea("TMJ")
dbSetOrder(12)
dbSeek(xFilial("TMJ")+SubStr(cUsuario,7,15)+Space(9)+DTOS(dDataCon))
While !EOF() .And. TMJ->(TMJ_FILIAL+TMJ_USURES+TMJ_NUMFIC+DTOS(TMJ_DTCONS)) == xFilial("TMJ")+;
												SubStr(cUsuario,7,15)+Space(9)+DTOS(dDataCon)

	RecLock("TMJ",.f.)
	dbDelete()
	TMJ->(MsUnLock())

	dbSelectArea("TMJ")
	dbskip()
End

Return
/*/


ͻ
Programa  MDT075Pesq Autor| Andre Perez          | Data |  14-02-07  
͹
Desc.      Pesquisa uma consulta.                                     
͹
Uso        MDTA075                                                    
ͼ

/*/
Function MDT075Pesq(cFind,cValPesq)

Local aArea := GetArea()
Local aAreaTM0 := TM0->(GetArea())
Local aAreaTMJ := TMJ->(GetArea())
Local oDlg3
Local oMARK, oTempTable
Local l2ConCanc := 0

Local cQuery
Local cAliasTM0 := GetNextAlias()
Local cTabTM0 := RetSqlName("TM0")

Private cMARCA  := GetMark()

If !NaoVazio(cValPesq)
	Return .f.
Endif

lInverte:= .f.

aDBF := {}
AADD(aDBF,{"OKID"  ,"C",02,0})
Aadd(aDBF,{"NUMFIC","C",09,0})
Aadd(aDBF,{"NOMFIC","C",40,0})
Aadd(aDBF,{"NUMRG" ,"C",15,0})
Aadd(aDBF,{"DTNASC","D",08,0})
Aadd(aDBF,{"NOMFUN","C",40,0})
Aadd(aDBF,{"CLIENT","C",NSizeSA1,0})
Aadd(aDBF,{"LOJA"  ,"C",nSizeLo1,0})
Aadd(aDBF,{"NOMCLI","C",40,0})

aTRB1 := {}
AADD(aTRB1,{"OKID"	,NIL," ",})
AADD(aTRB1,{"NUMFIC",NIL,STR0017,}) //"Ficha Medica"
AADD(aTRB1,{"NOMFIC",NIL,STR0018,}) //"Nome"
AADD(aTRB1,{"NUMRG" ,NIL,STR0086,}) //"R.G."
Aadd(aTRB1,{"DTNASC",NIL,STR0087,})  //"Data Nasc."
AADD(aTRB1,{"NOMFUN",NIL,STR0088,}) //"Funo"
AADD(aTRB1,{"CLIENT",NIL,STR0089,}) //"Cliente"
AADD(aTRB1,{"LOJA"  ,NIL,STR0090,}) //"Loja"
AADD(aTRB1,{"NOMCLI",NIL,STR0091,}) //"Nome Cliente"

oTempTable := FWTemporaryTable():New( TRBK, aDBF )
oTempTable:AddIndex( "1", {"NUMFIC"} )
oTempTable:Create()

If cFind == STR0041 //"R.G. do Funcionrio"

	cQuery := "SELECT TM0_NUMFIC,TM0_NOMFIC,TM0_RG,TM0_DTNASC,TM0_CLIENT,TM0_LOJA "
	cQuery += "FROM " + cTabTM0 + " "
	cQuery += "WHERE D_E_L_E_T_ != '*' AND "
	cQuery += "TM0_FILIAL = '" + xFilial("TM0") + "' AND "
	cQuery += "TM0_RG = '" + cValPesq + "' "

	cQuery := ChangeQuery(cQuery)
	MPSysOpenQuery( cQuery , cAliasTM0 )

	dbSelectArea(cAliasTM0)
	dbGotop()
    While !Eof()

		dbSelectArea("TM0")
		dbSetOrder(1)
		dbSeek(xFilial("TM0")+(cAliasTM0)->TM0_NUMFIC)

		Dbselectarea("TRBK")
		Dbgotop()
		If !Dbseek( (cAliasTM0)->TM0_NOMFIC + (cAliasTM0)->TM0_DTNASC )
			RecLock("TRBK",.t.)
			TRBK->OKID   := "  "
			TRBK->NUMFIC := (cAliasTM0)->TM0_NUMFIC
			TRBK->NOMFIC := (cAliasTM0)->TM0_NOMFIC
			TRBK->NUMRG  := (cAliasTM0)->TM0_RG
			TRBK->DTNASC := StoD( (cAliasTM0)->TM0_DTNASC )
			TRBK->CLIENT := (cAliasTM0)->TM0_CLIENT
			TRBK->LOJA   := (cAliasTM0)->TM0_LOJA
			TRBK->NOMCLI := Posicione("SA1",1,xFilial("SA1")+(cAliasTM0)->TM0_CLIENT+(cAliasTM0)->TM0_LOJA,"A1_NOME")
			TRBK->NOMFUN := Posicione("SRJ",1,xFilial("SRJ")+TM0->TM0_CODFUN,"RJ_DESC")
			Msunlock("TRBK")
		Else
			If (cAliasTM0)->TM0_NUMFIC > TRBK->NUMFIC
				RecLock("TRBK",.f.)
				TRBK->OKID   := "  "
				TRBK->NUMFIC := (cAliasTM0)->TM0_NUMFIC
				TRBK->NOMFIC := (cAliasTM0)->TM0_NOMFIC
				TRBK->NUMRG  := (cAliasTM0)->TM0_RG
				TRBK->DTNASC := StoD( (cAliasTM0)->TM0_DTNASC )
				TRBK->CLIENT := (cAliasTM0)->TM0_CLIENT
				TRBK->LOJA   := (cAliasTM0)->TM0_LOJA
				TRBK->NOMCLI := Posicione("SA1",1,xFilial("SA1")+(cAliasTM0)->TM0_CLIENT+(cAliasTM0)->TM0_LOJA,"A1_NOME")
				TRBK->NOMFUN := Posicione("SRJ",1,xFilial("SRJ")+TM0->TM0_CODFUN,"RJ_DESC")
				Msunlock("TRBK")
			Endif
		Endif

    	dbSelectArea(cAliasTM0)
    	dbSkip()
    End
	(cAliasTM0)->( dbCloseArea() )

Else

	cValPesq := AllTrim(cValPesq)

	cQuery := "SELECT TM0_NUMFIC,TM0_NOMFIC,TM0_RG,TM0_DTNASC,TM0_CLIENT,TM0_LOJA "
	cQuery += "FROM " + cTabTM0 + " "
	cQuery += "WHERE D_E_L_E_T_ != '*' AND "
	cQuery += "TM0_FILIAL = '" + xFilial("TM0") + "' AND "
	cQuery += "TM0_NOMFIC Like '" + cValPesq + "%' "

	cQuery := ChangeQuery(cQuery)
	MPSysOpenQuery( cQuery , cAliasTM0 )

	dbSelectArea(cAliasTM0)
	dbGotop()
    While !Eof()

		dbSelectArea("TM0")
		dbSetOrder(1)
		dbSeek(xFilial("TM0")+(cAliasTM0)->TM0_NUMFIC)

		Dbselectarea("TRBK")
		Dbgotop()
		If !Dbseek( (cAliasTM0)->TM0_NOMFIC + (cAliasTM0)->TM0_DTNASC )
			RecLock("TRBK",.t.)
			TRBK->OKID   := "  "
			TRBK->NUMFIC := (cAliasTM0)->TM0_NUMFIC
			TRBK->NOMFIC := (cAliasTM0)->TM0_NOMFIC
			TRBK->NUMRG  := (cAliasTM0)->TM0_RG
			TRBK->DTNASC := StoD( (cAliasTM0)->TM0_DTNASC )
			TRBK->CLIENT := (cAliasTM0)->TM0_CLIENT
			TRBK->LOJA   := (cAliasTM0)->TM0_LOJA
			TRBK->NOMCLI := Posicione("SA1",1,xFilial("SA1")+(cAliasTM0)->TM0_CLIENT+(cAliasTM0)->TM0_LOJA,"A1_NOME")
			TRBK->NOMFUN := Posicione("SRJ",1,xFilial("SRJ")+TM0->TM0_CODFUN,"RJ_DESC")
			Msunlock("TRBK")
		Else
			If (cAliasTM0)->TM0_NUMFIC > TRBK->NUMFIC
				RecLock("TRBK",.f.)
				TRBK->OKID   := "  "
				TRBK->NUMFIC := (cAliasTM0)->TM0_NUMFIC
				TRBK->NOMFIC := (cAliasTM0)->TM0_NOMFIC
				TRBK->NUMRG  := (cAliasTM0)->TM0_RG
				TRBK->DTNASC := StoD( (cAliasTM0)->TM0_DTNASC )
				TRBK->CLIENT := (cAliasTM0)->TM0_CLIENT
				TRBK->LOJA   := (cAliasTM0)->TM0_LOJA
				TRBK->NOMCLI := Posicione("SA1",1,xFilial("SA1")+(cAliasTM0)->TM0_CLIENT+(cAliasTM0)->TM0_LOJA,"A1_NOME")
				TRBK->NOMFUN := Posicione("SRJ",1,xFilial("SRJ")+TM0->TM0_CODFUN,"RJ_DESC")
				Msunlock("TRBK")
			Endif
		Endif

    	dbSelectArea(cAliasTM0)
    	dbSkip()
    End
	(cAliasTM0)->( dbCloseArea() )

Endif

Dbselectarea("TRBK")
Dbgotop()
If TRBK->(Reccount()) <= 0
	oTempTable:Delete()
	RestArea(aArea)
	RestArea(aAreaTM0)
	If cFind == STR0041 //"R.G. do Funcionrio"
		Msgstop(STR0143,STR0076)	 //"No existem funcionrios com este R.G."###"ATENO"
	Else
		Msgstop(STR0092,STR0076) //"No existem funcionrios que iniciam por estas letras."###"ATENO"
 	Endif
 	Return .F.
Endif

nOpca := 2
DEFINE MSDIALOG oDlg3 TITLE OemToAnsi(STR0093) From 11,02 To 26,120 OF oMainWnd //"Selecione o funcionrio"
oMARK := MsSelect():NEW("TRBK","OKID",,aTRB1,@lINVERTE,@cMARCA,{20,05,110,463})
oMARK:bMARK := {|| MDTA075MAQ(cMarca,lInverte,TRBK->(Recno())) .and. oMARK:oBROWSE:REFRESH(.T.)}
oMARK:oBROWSE:lHASMARK := .T.
oMARK:oBROWSE:lCANALLMARK := .F.
ACTIVATE MSDIALOG oDlg3 ON INIT EnchoiceBar(oDlg3,{|| nOpca := 1,oDlg3:End()},{|| nOpca := 2,oDlg3:End()})

If nOpca == 1
	Dbselectarea("TRBK")
	Dbgotop()
	While !eof()
		If Empty(TRBK->OKID)
			Dbskip()
			Loop
		Endif

		dbSelectArea("TMJ")
		dbSetOrder(6) //TMJ_FILIAL+TMJ_NUMFIC
		Set Filter To  TMJ_FILIAL == xFilial("TMJ") .and. TMJ_NUMFIC == TRBK->NUMFIC .and. TMJ_CODUSU == M->TMJ_CODUSU
		NG075AGE3(.T.)
		Exit
	End
Endif

oTempTable:Delete()

dbSelectArea("TMJ")
dbSetOrder(1)
Set Filter To

RestArea(aAreaTMJ)
RestArea(aAreaTM0)
RestArea(aArea)
Return .T.
/*/


ͻ
Programa  MDT075HR   Autor| Andre Perez          | Data |  14-02-07  
͹
Desc.      Valida os campos de hora da quantidade de medicos.         
͹
Uso        MDT075HR                                                   
ͼ

/*/
Function MDT075HR(cHrIni,cHrFim)

If cHrFim < cHrIni
	msgStop(STR0144) //"A hora final do perdo deve ser maior ou igual  hora inicial."
	Return .F.
Endif

Return .T.
/*/


ͻ
Programa  MDT075CAD  Autor| Denis                | Data |  14-02-07  
͹
Desc.      Alteracao e exclusao da consulta medica                    
͹
Uso        MDT075HR                                                   
ͼ

/*/
Function MDT075CAD(cAlias_,nRecno_,nOpcxx)
Local RotinaOld := aClone(aRotina)

aRotina :=	  { { STR0008, "AxPesqui"  , 0 , 1},;  //"Pesquisar "
				{ STR0002, "NGCAD01"   , 0 , 2},;  //"Visualizar"
				{ STR0003, "NGCAD01"   , 0 , 3},;  //"Incluir"
				{ STR0004, "NGCAD01"   , 0 , 4},;  //"Alterar"
				{ STR0005, "NGCAD01"   , 0 , 5, 3} } //"Excluir"

If nOpcxx == 3
	bNGGRAVA  := {|| MDT075VAL(.t.)}
Endif


If SuperGetMV("MV_NG2SEG",.F.,"2") == "1" .AND. NGCADICBASE("TMK_USUARI","A","TMK",.F.) .AND. !(SuperGetMV("MV_MDTPS",.F.,"N") == "S")
	aArea := GetArea()
	dbSelectArea("TMK")
	dbSetOrder(1)
	dbSeek(xFilial("TMK")+TMJ->TMJ_CODUSU)
	If AllTrim(cUserName) <> AllTrim(MDTDATALO("TMJ->TMJ_USERGI",.F.)) .AND. AllTrim(MDTDATALO("TMJ->TMJ_USERGI",.F.)) <> AllTrim(cUserName)  .AND. ;
			AllTrim(cUserName) <> AllTrim(TMK->TMK_USUARI) .AND. AllTrim(TMK->TMK_USUARI) <> AllTrim(cUserName)
		ShowHelpDlg("SEMPERM",	{STR0159}	,2,;//"Usurio sem permisso para alterar o registro."
								{STR0160}	,2)//"Acesse o sistema com o usurio de incluso/alterao do registro."
		Return .F.
	Endif
	RestArea(aArea)
Endif
NGCAD01("TMJ",TMJ->(Recno()),nOpcxx+1)
bNGGRAVA  := {|| MDT075VAL(.f.)}

aRotina := aClone(RotinaOld)
Return

/*/


ͻ
Programa  fTemEegEcg Autor| Denis                | Data |  14-02-07  
͹
Desc.      Verifica se tem EEG e ECG no intervalo de 30 minutos       
͹
Uso        MDT075HR                                                   
ͼ


/*/
Function fTemEegEcg(cFichaM, cNatCon, dDtaCons, cHoraPrev, cHoraAnt)

Local aAreaXXX   := GetArea()
Local aAreaTMJ   := TMJ->(GetArea())
Local cHoraIni   := MTOH(HTOM(cHoraPrev)-29)
Local cHoraFim   := MTOH(HTOM(cHoraPrev)+29)
Local cPerExames := " "
Local lRet       := .f.

// Se SIGAMDT for da Engemed
If FindFunction("ENG2XFUN")

	cPerExames := Alltrim(Busca_Perfil(cFichaM,cNatCon))
	If (!Empty(cExaECG) .and. cExaECG $ cPerExames) .or. ;
		(!Empty(cExaEEG) .and. cExaEEG $ cPerExames)

		dbSelectArea("TMJ")
		dbSetOrder(6)
		dbSeek(xFilial("TMJ")+DTOS(dDtaCons)+cHoraIni,.t.)
		While !EOF() .And. TMJ_FILIAL+DTOS(TMJ_DTCONS) == xFilial("TMJ")+DTOS(dDtaCons) .and. ;
			TMJ->TMJ_HRCONS <= cHoraFim .and. !lRet

			If Empty(TMJ->TMJ_NATEXA) .or. (TMJ->TMJ_HRCONS == cHoraAnt .and. TMJ->TMJ_NUMFIC == cFichaM)
				dbSelectArea("TMJ")
				dbSkip()
				loop
			Endif

			cPerExames := Alltrim(Busca_Perfil(TMJ->TMJ_NUMFIC,TMJ->TMJ_NATEXA))

			If !Empty(cExaECG) .and. cExaECG $ cPerExames
				lRet := .t.
			Endif
			If !Empty(cExaEEG) .and. cExaEEG $ cPerExames
				lRet := .t.
			Endif

			dbSelectArea("TMJ")
			dbSkip()
		End

	Endif

Endif

RestArea(aAreaTMJ)
RestArea(aAreaXXX)
Return lRet

/*/


ͻ
Programa  fProxAtend Autor| Denis                | Data |  14-02-07  
͹
Desc.      Verifica se tem EEG e ECG no intervalo de 30 minutos       
͹
Uso        MDT075HR                                                   
ͼ


/*/
Function fProxAtend(Chorario,cNumFicha,c2HRCONS)

Local cHoraIni    := MTOH(HTOM(Chorario)-29)
Local cHrUltAtend := ""
Local cFichaAtend := ""
Local nPosScan,nX,nZZZ
Local nPosTroca   := 0

dbSelectArea("TMJ")
dbSetOrder(6)
dbSeek(xFilial("TMJ")+DTOS(M->TMJ_DTCONS)+cHoraIni,.t.)
While !EOF() .And. TMJ->TMJ_FILIAL == xFilial("TMJ") .and. TMJ->TMJ_DTCONS == M->TMJ_DTCONS
	cPerExames := Alltrim(Busca_Perfil(TMJ->TMJ_NUMFIC,TMJ->TMJ_NATEXA))

	If (!Empty(cExaECG) .and. cExaECG $ cPerExames) .or. (!Empty(cExaEEG) .and. cExaEEG $ cPerExames)
		cHrUltAtend := TMJ->TMJ_HRCONS
		cFichaAtend := TMJ->TMJ_NUMFIC
		Exit
	Endif

	dbSelectArea("TMJ")
	dbSkip()
End

nPosScan := aSCAN(aCOLS,{|x| x[nHORAC] == cHrUltAtend .and. x[nFICHA] == cFichaAtend })

//Verifica proximo horario disponivel
For nX := nPosScan To Len(aCols)
	If Empty(aCols[nX,nFICHA])
		If HTOM(aCols[nX,nHORAC]) >= HTOM(cHrUltAtend)+30 .and. aCols[nX,nHORAC] > Chorario
		    lTemApos := .f.
			For nZZZ := nX To Len(aCols)
				If HTOM(aCOLS[nZZZ][nHORAC]) >= HTOM(aCols[nX,nHORAC])+30
					Exit
				Endif
				If !Empty(aCols[nZZZ,nFICHA]) .and. !Empty(aCols[nZZZ,nMOTIV]) .and. (aCols[nZZZ,nHORAC] <> c2HRCONS .or. aCols[nZZZ,nFICHA] == cNumFicha)
					cPerExames := Alltrim(Busca_Perfil(aCols[nZZZ,nFICHA],aCols[nZZZ,nMOTIV]))
					If (!Empty(cExaECG) .and. cExaECG $ cPerExames) .or. (!Empty(cExaEEG) .and. cExaEEG $ cPerExames)
						lTemApos := .t.
					Endif
				Endif
			Next nZZZ

			If !lTemApos
				nPosTroca := nX
				Exit
			Endif
		Endif
	Else
		If !Empty(aCols[nX,nMOTIV])
			cPerExames := Alltrim(Busca_Perfil(aCols[nX,nFICHA],aCols[nX,nMOTIV]))
			If (!Empty(cExaECG) .and. cExaECG $ cPerExames) .or. (!Empty(cExaEEG) .and. cExaEEG $ cPerExames)
				cHrUltAtend := aCols[nX,nHORAC]
			Endif
		Endif
	Endif

Next nX

Return nPosTroca

/*/


ͻ
Programa  MDT075NATX Autor| Denis                | Data |  14-02-07  
͹
Desc.      Validacao do campo natureza do exame                       
͹
Uso        MDT075                                                     
ͼ


/*/
Function MDT075NATX()

If Empty(M->TMJ_NATEXA)
	Return .t.
Endif

If M->TMJ_NATEXA <> aCOLS[n][nMOTIV]

	nJaexist := 0
	dbSelectArea("TMJ")
	dbSetOrder(7)
	dbSeek(xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+aCOLS[n][nFICHA])
	While !Eof() .and. xFilial("TMJ")+M->TMJ_CODUSU+DTOS(M->TMJ_DTCONS)+aCOLS[n][nFICHA] == ;
		TMJ->TMJ_FILIAL+TMJ->TMJ_CODUSU+DTOS(TMJ->TMJ_DTCONS)+TMJ->TMJ_NUMFIC

		If TMJ->TMJ_NATEXA == M->TMJ_NATEXA
			nJaexist++
		Endif

		dbSkip()
	End

	If nJaexist > 0
		Help(" ",1,"JAEXISTINF")
		Return .f.
	Endif

Endif

//Verifica se o atendimento  para exames EEG ou ECG, e se ja existe atendimento recente
//            Ficha          , Natureza     , Data,        , Hora
If fTemEegEcg(aCols[n,nFicha], M->TMJ_NATEXA, M->TMJ_DTCONS, aCOLS[n][nHORAC], aCOLS[n][nHORAC])
	If MsgYesNo(STR0137+; //"O intervalo entre duas consultas que necessitam dos exames ECG ou EEG deve ser de no mnimo 30 minutos. "
				STR0138) //"Deseja agendar para o prximo horrio vago?"

		//Verifica proximo horario disponivel para atendimentos especiais (ECG/EEG)
		// Retorna a posicao do aCols disponivel
		nPosTroca := fProxAtend(aCOLS[n][nHORAC],aCols[n,nFicha],aCOLS[n][nHORAC])
		If nPosTroca > 0 .and. Len(aCOLS) >= nPosTroca

			cFichaAn := aCOLS[n][nFICHA]

			//Deletar linha atual e incluir no proximo horario vago
			NG075EXCC(.f.)

			dbSelectArea("TM0")
			dbSetOrder(1)
			dbSeek(xFilial("TM0")+cFichaAn)

			aCOLS[nPosTroca][nFICHA] := TM0->TM0_NUMFIC
			aCOLS[nPosTroca][nNOFIC] := TM0->TM0_NOMFIC
			aCOLS[nPosTroca][nCLIEN] := TM0->TM0_CLIENT
			aCOLS[nPosTroca][nLOJAC] := TM0->TM0_LOJA
			aCOLS[nPosTroca][nNOMEC] := NGSEEK("SA1",TM0->TM0_CLIENT+TM0->TM0_LOJA,1,"SA1->A1_NOME")
			aCOLS[nPosTroca][nNOEXA] := Busca_Perfil(TM0->TM0_NUMFIC,M->TMJ_NATEXA)
			aCOLS[nPosTroca][nMOTIV] := M->TMJ_NATEXA
			aCOLS[nPosTroca][nMOTIVO] := M->TMJ_MOTIVO

			oGet:oBrowse:nAt := nPosTroca
			oGet:oBrowse:Refresh()

			MsgInfo(STR0139+aCOLS[nPosTroca][nHORAC]) //"A consulta foi agendanda para o horrio - "

			Return .t.
		Else
			MsgInfo(STR0140) //"No existe horrio livre para a agenda deste dia."
			Return .f.
		Endif
	Else
		Return .f.
	Endif
Endif

Return .t.
/*/


ͻ
Programa  MDT075ITM0 Autor| Denis                | Data |  14-02-07  
͹
Desc.      Inclui Ficha Medica aproveitando dados de func. demitido   
͹
Uso        MDT075                                                     
ͼ


/*/
Function MDT075ITM0()

Private aRelac := {}

dbSelectArea("TM0")
dbSetOrder(1)
dbSeek(xFilial("TM0")+Mv_par01)

Private cTM0_NOMFIC := TM0->TM0_NOMFIC
Private cTM0_DTNASC := TM0->TM0_DTNASC
Private cTM0_SEXO   := TM0->TM0_SEXO
Private cTM0_RG     := TM0->TM0_RG
Private cTM0_ESTCIV := TM0->TM0_ESTCIV
Private cTM0_NUMCP  := TM0->TM0_NUMCP
Private cTM0_SERCP  := TM0->TM0_SERCP
Private cTM0_UFCP   := TM0->TM0_UFCP
Private cTM0_SANGUE := TM0->TM0_SANGUE
Private cTM0_FATORH := TM0->TM0_FATORH
Private cTM0_PESO   := TM0->TM0_PESO
Private cTM0_ALTURA := TM0->TM0_ALTURA
Private cTM0_NUMCAL := TM0->TM0_NUMCAL
Private cTM0_TIPFIS := TM0->TM0_TIPFIS
Private cTM0_FUMA   := TM0->TM0_FUMA
Private cTM0_QTCIG  := TM0->TM0_QTCIG
Private cTM0_QTTEMP := TM0->TM0_QTTEMP
Private cTM0_COROLH := TM0->TM0_COROLH
Private cTM0_CORCAB := TM0->TM0_CORCAB
Private cTM0_CORPEL := TM0->TM0_CORPEL
Private cTM0_DOADOR := TM0->TM0_DOADOR
Private cTM0_DTDOAC := TM0->TM0_DTDOAC
Private cTM0_DESCRI := TM0->TM0_DESCRI

aADD(aRelac , {"TM0_NOMFIC","cTM0_NOMFIC"} )
aADD(aRelac , {"TM0_DTNASC","cTM0_DTNASC"} )
aADD(aRelac , {"TM0_SEXO"  ,"cTM0_SEXO"  } )
aADD(aRelac , {"TM0_RG"    ,"cTM0_RG"    } )
aADD(aRelac , {"TM0_ESTCIV","cTM0_ESTCIV"} )
aADD(aRelac , {"TM0_NUMCP" ,"cTM0_NUMCP" } )
aADD(aRelac , {"TM0_SERCP" ,"cTM0_SERCP" } )
aADD(aRelac , {"TM0_UFCP"  ,"cTM0_UFCP"  } )
aADD(aRelac , {"TM0_SANGUE","cTM0_SANGUE"} )
aADD(aRelac , {"TM0_FATORH","cTM0_FATORH"} )
aADD(aRelac , {"TM0_PESO"  ,"cTM0_PESO"  } )
aADD(aRelac , {"TM0_ALTURA","cTM0_ALTURA"} )
aADD(aRelac , {"TM0_NUMCAL","cTM0_NUMCAL"} )
aADD(aRelac , {"TM0_TIPFIS","cTM0_TIPFIS"} )
aADD(aRelac , {"TM0_FUMA"  ,"cTM0_FUMA"  } )
aADD(aRelac , {"TM0_QTCIG" ,"cTM0_QTCIG" } )
aADD(aRelac , {"TM0_QTTEMP","cTM0_QTTEMP"} )
aADD(aRelac , {"TM0_COROLH","cTM0_COROLH"} )
aADD(aRelac , {"TM0_CORCAB","cTM0_CORCAB"} )
aADD(aRelac , {"TM0_CORPEL","cTM0_CORPEL"} )
aADD(aRelac , {"TM0_DOADOR","cTM0_DOADOR"} )
aADD(aRelac , {"TM0_DTDOAC","cTM0_DTDOAC"} )
aADD(aRelac , {"TM0_DESCRI","cTM0_DESCRI"} )

//Inclui nova ficha
Return MDT005F3()

/*/


Ŀ
Funo    |NG075MUD   Autor Andre E.P.Alvarez       Data 13/10/2005
Ĵ
Descrio Transferencia de Consulta                                   
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function NG075MUD()
Local aArea := Getarea()

dbSelectArea("TMK")
dbSetOrder(1)
dbSeek(xFilial("TMK")+TMJ->TMJ_CODUSU)
If SuperGetMV("MV_NG2SEG",.F.,"2") == "1" .AND. NGCADICBASE("TMK_USUARI","A","TMK",.F.) .AND. !(SuperGetMV("MV_MDTPS",.F.,"N") == "S")
	If AllTrim(cUserName) <> AllTrim(MDTDATALO("TMJ->TMJ_USERGI",.F.)) .AND. AllTrim(MDTDATALO("TMJ->TMJ_USERGI",.F.)) <> AllTrim(cUserName)  .AND. ;
			AllTrim(cUserName) <> AllTrim(TMK->TMK_USUARI) .AND. AllTrim(TMK->TMK_USUARI) <> AllTrim(cUserName)
		ShowHelpDlg("SEMPERM",	{STR0159}	,2,;//"Usurio sem permisso para alterar o registro."
								{STR0160}	,2)//"Acesse o sistema com o usurio de incluso/alterao do registro."
		Return .F.
	Endif
Endif
RestArea(aArea)

NG160MUD("MDTA075")

RestArea(aArea)
Return .t.

/*/

Ŀ
Funo    |ValidA175 |Autor  Andre E.P. Alvarez      Data 13/10/2005
Ĵ
Descrio Validacao de inclusao                                       
ٱ


/*/
Function ValidA175()

Local cCalend := ""
Local aArea   := GetArea()

dbSelectArea("TML")
dbSetOrder(1)
If dbSeek(xFilial("TML")+cCodMed)
	cCalend := TML->TML_CALEND
Endif

If !ExistCpo("TMK",cCodMEd)
	RestArea(aArea)
	Return .f.
Endif
If Empty(dDtCons)
	RestArea(aArea)
	Return .f.
Endif
If dDtCons < ddatabase
	msgStop( STR0164 )
	Return .f.
Endif
If !NGVALHORA(cHrCons,.t.)
	RestArea(aArea)
	Return .f.
Endif

If !MDT75VLHRC(cCalend,DDTCONS,CHRCONS,"I",.T.)
	RestArea(aArea)
	Return .F.
EndIf

RESTORE075(1)	//Excluir filtro das consultas

If IsInCallStack( "MDTA076" ) .Or. ( IsInCallStack( "MDTA410" ) .And. ALTERA )

	//bkp dos valores antigos
	__cMedico := TMJ->TMJ_CODUSU
	__dDtCons := TMJ->TMJ_DTCONS
	__cHrCons := TMJ->TMJ_HRCONS
	__cNumFic := TMJ->TMJ_NUMFIC

	//chama funo para verificar se vai possuir interferencias de horrios
	If !MDT076INT( "TMJ", .T. )
		Return .F.
	EndIf
Else

	Dbselectarea("TMJ")
	Dbsetorder(1)
	If Dbseek(xFilial("TMJ")+cCodMed+Dtos(dDtCons)+cHrCons)
		MsgStop(STR0028+Chr(13)+Chr(13)+; //"Ja existe consulta marcada para esta data e hora na agenda do "
				STR0023+Space(1)+Alltrim(cNomMed),STR0027) //"medico: "###"ATENCAO"
		RESTORE075(2)//Atualizar filtro das consultas
		RestArea(aArea)
		Return .f.
	Endif
EndIf

RESTORE075(2)	//Atualizar filtro das consultas

RestArea(aArea)

Return .t.

/*/


Ŀ
Funo    |RESTORE075|Autor  Andre E.P. Alvarez      Data 13/10/2005
Ĵ
Descrio Restaura o filtro das consultas clinicas                    
ٱ


/*/
Function RESTORE075(nTipo)

If nTipo == 1
	DbSelectArea("TMJ")
	Set Filter To
	DbSeek("TMJ")
ElseIf Type("cCodusu") == "C"
	DbSelectArea("TMJ")
	SET FILTER TO TMJ_FILIAL == xFilial('TMJ')	.AND. ;
  				  TMJ_CODUSU == cCodusu   		.AND. ;
        		  EMPTY(TMJ_DTATEN)
Endif

RestArea(aAreaTMJ)

Return .t.

/*/


Ŀ
Funo    VAL075DIA() Autor  Andre E.P. Alvarez     Data  13/10/05 
Ĵ
Descrio  Verifica se o dia cai em dia de trabalho do medico.         
ٱ


/*/
Function VAL075DIA()

Local nDia,nHora,nMinuto,nInd,nConsulta, xyz
Local aAreaTML := TML->(GetArea())

Dbselectarea("TML")
Dbsetorder(1)
If Dbseek(xFilial("TML")+cCodMed)
	cCalendario :=TML->TML_CALEND
Endif

aDia := NGMDT_H7(cCalendario)

If Len(aDia) == 0 .or. Type("aDia") != "A"
	MsgStop(STR0032+Chr(13)+Chr(13)+; //"Problema: O calendario na qual a agenda do medico baseia-se, nao foi encontrado."
			STR0033+;	 //"Solucao: Verificar no cadastro de Calendarios se o codigo"
			" '"+cCalendario+"' "+;
			STR0034,STR0027) //"ATENCAO" //"existe na tabela. Caso nao exista, cadastra-lo."

	RestArea(aAreaTML)
	Return .f.
Endif

nDia := If(DOW(dDtCons)==1,7,DOW(dDtCons)-1) //Dia da Semana 1=Segunda,2=Terca,3=Quarta,...,7=Domingo
nInd := At(":",cHrCons)

nHora   := val(Substr(cHrCons,1,nInd-1))
nMinuto := val(Substr(cHrCons,nInd+1))
nConsulta := (nHora*60)+nMinuto

RestArea(aAreaTML)

For xyz := 1 to Len(aDia[nDia][4])
	nOutIni := aDia[nDia][4][xyz][1]
	nOutFim := aDia[nDia][4][xyz][2]
	If (nConsulta > nOutIni .and. nConsulta < nOutFim) .or. (nConsulta == 0 .and. nOutIni == 0)
		MsgStop(Alltrim(cNomMed)+" "+STR0030+Chr(13)+Chr(13)+; //"nao pode atender consultas nesse horario."
				STR0031,STR0027) //"Verificar o calendario de atendimento do Medico."###"ATENCAO"
		Return .f.
	Endif
Next xyz

Return .t.

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 29/11/2006
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaMDT                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
Local aRotina
Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .t. , .f. )

aRotina :=	{ { STR0001, 	"AxPesqui"  , 0 , 1},; //"Pesquisar"
              { STR0002,	"NGCAD01"   , 0 , 2},; //"Visualizar"
              { STR0003,   "NGCAD01"   , 0 , 3,183},; //"Incluir"
              { STR0004,   "NG075ALT"  , 0 , 4,184},; //"Alterar"
              { STR0005,   "NG075EXC"  , 0 , 5,185}} //"Excluir"

//Acessos do perfil do usurio:
//183 - Incluir Agenda Mdica
//184 - Alterar Agenda Mdica
//185 - Excluir Agenda Mdica

If lSigaMdtPS
	aADD(aRotina,{STR0006,"NG075PSAGE"  , 0 , 2}) //"A&genda"
	aADD(aRotina,{STR0145,"NG075ALTM", 0 , 2}) //"Qtde.Medicos"
Else
	aADD(aRotina,{STR0006,"NG075AGE"  , 0 , 3}) //"A&genda"
Endif

Return aRotina

/*/


Ŀ
Funo     CodCliTMJ  Autor  Denis Hyroshi          Data  13/10/05 
Ĵ
Descrio  Busca Codigo do Cliente da Ficha que esta na TMJ            
ٱ


/*/
Function CodCliTMJ()
Local aArea    := GetArea()
Local aAreaTM0 := TM0->(GetArea())
Local cRet     := Space(Len(TM0->TM0_CLIENT))

dbSelectArea("TM0")
dbSetOrder(1)
If dbSeek(xFilial("TM0")+TMJ->TMJ_NUMFIC)
	cRet := TM0->TM0_CLIENT
Endif

RestArea(aAreaTM0)
RestArea(aArea)
Return cRet
/*/


Ŀ
Funo     LojCliTMJ  Autor  Denis Hyroshi          Data  13/10/05 
Ĵ
Descrio  Busca Loja   do Cliente da Ficha que esta na TMJ            
ٱ


/*/
Function LojCliTMJ()
Local aArea    := GetArea()
Local aAreaTM0 := TM0->(GetArea())
Local cRet     := Space(Len(TM0->TM0_LOJA))

dbSelectArea("TM0")
dbSetOrder(1)
If dbSeek(xFilial("TM0")+TMJ->TMJ_NUMFIC)
	cRet := TM0->TM0_LOJA
Endif

RestArea(aAreaTM0)
RestArea(aArea)
Return cRet
/*/


Ŀ
Funo     NomCliTMJ  Autor  Denis Hyroshi          Data  13/10/05 
Ĵ
Descrio  Busca Nome   do Cliente da Ficha que esta na TMJ            
ٱ


/*/
Function NomCliTMJ()
Local aArea    := GetArea()
Local aAreaTM0 := TM0->(GetArea())
Local aAreaSA1 := SA1->(GetArea())
Local cRet     := Space(40)

dbSelectArea("TM0")
dbSetOrder(1)
If dbSeek(xFilial("TM0")+TMJ->TMJ_NUMFIC)
	cCodSA1 := TM0->TM0_CLIENT + TM0->TM0_LOJA
	dbSelectArea("SA1")
	dbSetOrder(1)
	If dbSeek(xFilial("SA1")+cCodSA1)
		cRet := SA1->A1_NOME
	Endif
Endif

RestArea(aAreaTM0)
RestArea(aAreaSA1)
RestArea(aArea)
Return cRet
/*/


Ŀ
Funo     NG075ALT  Autor Roger Rodrigues         Data 24/03/2010
Ĵ
Descrio  Altera calendrio do mdico                                
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function NG075ALT(cAlias,nReg,nOpcx)
Private cOldCalend := TML->TML_CALEND

_bNGGRAVA := bNGGRAVA
//Verifica se o calendrio foi alterado
bNGGRAVA  := {|| MDT75VLCAL()}
//Chama tela de cadastro
nRet :=  NGCAD01("TML", nReg, nOpcx)
bNGGRAVA := _bNGGRAVA

If nRet = 1
	//Verifica se o calendrio foi alterado
	If cOldCalend <> TML->TML_CALEND
		Processa({|| ALTCAL75()})
	Endif
	//Atualiza calenadario do usuario
	If NGCADICBASE("TMK_CALEND","A","TMK",.F.)
		dbSelectArea("TMK")
		dbSetOrder(1)
		If dbSeek(xFilial("TMK")+TML->TML_CODUSU) .and. TMK->TMK_CALEND != TML->TML_CALEND
			RecLock("TMK",.F.)
			TMK->TMK_CALEND := TML->TML_CALEND
			MsUnlock("TMK")
		Endif
	Endif
Endif

Return .t.

/*


ͻ
Programa  ALTCAL75  Autor  Roger Rodrigues      Data   26/03/10   
͹
Desc.     Altera os agendamentos de acordo com o novo calendario      
                                                                      
͹
Uso       MDTA075                                                     
ͼ


*/
Static Function ALTCAL75()
Local aAgenda := {},i,j,k,aCalend := {}

//Carrega exames futuros
cHora := Substr(Time(),1,5)
#IFDEF TOP
	cTMJ := GetNextAlias()
	cQuery := "SELECT TMJ.TMJ_DTCONS, TMJ.TMJ_HRCONS "
	cQuery += "FROM "+RetSqlName("TMJ")+" TMJ "
	cQuery += "WHERE TMJ.D_E_L_E_T_ <> '*' AND TMJ.TMJ_FILIAL = '"+xFilial("TMJ")+"' AND "
	cQuery += "TMJ.TMJ_CODUSU = '"+TML->TML_CODUSU+"' AND "
	cQuery += "TMJ.TMJ_DTCONS||TMJ.TMJ_HRCONS >= '"+DTOS(dDataBase)+cHora+"' "
	cQuery += "ORDER BY TMJ.TMJ_DTCONS+TMJ.TMJ_HRCONS"
	cQuery := ChangeQuery(cQuery)
	MPSysOpenQuery( cQuery , cTMJ )
	dbSelectArea(cTMJ)
	dbGoTop()
	While !eof()
		aAdd(aAgenda,{STOD((cTMJ)->TMJ_DTCONS),(cTMJ)->TMJ_HRCONS,"00:00"})
		//Calcula as horas para a proxima consulta
		If Len(aAgenda) > 1
			n := Len(aAgenda)
			aAgenda[n][3] := NGCALCHCAR(aAgenda[n-1][1],aAgenda[n-1][2],aAgenda[n][1],aAgenda[n][2])
		End
		dbSelectArea(cTMJ)
		dbSkip()
	End
	(cTMJ)->(dbCloseArea())
#ELSE
	dbSelectArea("TMJ")
	dbSetOrder(1)
	dbSeek(xFilial("TMJ")+TML->TML_CODUSU)
	While !eof() .and. xFilial("TMJ")+TML->TML_CODUSU == TMJ->TMJ_FILIAL+TMJ->TMJ_CODUSU .and. ;
								DTOS(TMJ->TMJ_DTCONS)+TMJ->TMJ_HRCONS >= DTOS(dDataBase)+cHora
		aAdd(aAgenda,{TMJ->TMJ_DTCONS,TMJ->TMJ_HRCONS,"00:00"})
		//Calcula as horas para a proxima consulta
		If Len(aAgenda) > 1
			n := Len(aAgenda)
			aAgenda[n][3] := NGCALCHCAR(aAgenda[n-1][1],aAgenda[n-1][2],aAgenda[n][1],aAgenda[n][2])
		End
		dbSelectArea("TMJ")
		dbSkip()
	End
#ENDIF
dbSelectArea("TML")
//Carrega Calendario
aCalend := NGCALENDAH(TML->TML_CALEND)
ProcRegua(Len(aAgenda))
For i:=1 to Len(aAgenda)
	//Procura proximo horario disponivel
	IncProc()
	If i==1
		dData:= aAgenda[i][1]
		cHora:= aAgenda[i][2]
	Else
		cHora := MTOH(HTOM(cHora)+HTOM(aAgenda[i][3]))
		While HTOM(cHora) > HTOM("23:59")
			cHora := MTOH(HTOM(cHora)-(24*60))
			dData ++
		End
	Endif
	nDIA := DOW(dData)
	lAchou := .F.
	lProprio := .F.
	j := nDia
	While !lAchou .and. j <= 7
		//Verifica se o dia tem horas
		If aCalend[j][1] <> "00:00"
			For k:=1 to Len(aCalend[j][2])
				If j==nDia
					If cHora >= aCalend[j][2][k][1] .and. cHora < aCalend[j][2][k][2]
						lAchou := .T.
						lProprio := .T.
						Exit
					ElseIf aCalend[j][2][k][1] >= cHora
						cHora := aCalend[j][2][k][1]
						lAchou := .T.
						Exit
					Endif
				Else
					cHora := aCalend[j][2][k][1]
					lAchou := .T.
					Exit
				Endif
			Next k
		Endif
		If lAchou
			Exit
		Endif
		//Incrementa contador e verifica se j percorreu o dia
		j++
		If j > 7
			j := 1
		ElseIf j == nDia
			Exit
		Endif
		dData ++
	End
	If !lProprio
		//Ajusta horario
		dbSelectArea("TMJ")
		dbSetOrder(1)
		While dbSeek(xFilial("TMJ")+TML->TML_CODUSU+DTOS(dData)+cHora)
			cHora := MTOH(HTOM(cHora)+1)
			If cHora > "23:59"
				cHora := "00:00"
				dData ++
			Endif
			dbSelectArea("TMJ")
			dbSkip()
		End
	Endif
	//Grava novo horario
	dbSelectArea("TMJ")
	dbSetOrder(1)
	If dbSeek(xFilial("TMJ")+TML->TML_CODUSU+DTOS(aAgenda[i][1])+aAgenda[i][2])
		Reclock("TMJ",.F.)
		TMJ->TMJ_DTCONS := dData
		TMJ->TMJ_HRCONS := cHora
		MsUnlock("TMJ")
	Endif
Next i

Return

/*


ͻ
Programa  MDT75VLCALAutor  Roger Rodrigues      Data   26/03/10   
͹
Desc.     Valida calendrio na alteracao                              
                                                                      
͹
Uso       MDTA075                                                     
ͼ


*/
Function MDT75VLCAL()
Local aCalend := {}, i, lOk := .F.
aCalend := NGCALENDAH(M->TML_CALEND)
For i:=1 to Len(aCalend)
	If aCalend[i][1] <> "00:00"
		lOk := .T.
		Exit
	Endif
Next i
If !lOk
	ShowHelpDlg(STR0076,{STR0146},1) //"ATENO"###"O calendrio informado no possui horas cadastradas."
	Return .F.
Endif
If cOldCalend <> M->TML_CALEND
	If !MsgYesNo(STR0147) //"Deseja mesmo alterar o caledrio do usurio? Isto reprogramar todos seus exames."
		Return .F.
	Endif
Endif

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MDT75VLHRC
Valida a Hora da Consulta de acordo com o calendario.

@Param lFonte076 - Verifica se esta sendo chamado do MDTA076

@author Wagner S. de Lacerda
@since 28/06/2010

@return
/*/
//---------------------------------------------------------------------
Function MDT75VLHRC(cVCALEND,dVDATA,cVHORA,cVTIPO,lMESAG,lFonte076)

Local nlo     := 0
Local c1LI    := chr(13)+chr(10)
Local cMENSAC := Space(10)
Local aCALEND := NGCALENDAH(cVCALEND)
Local nDIASEM := Dow(dVDATA)
Local lMES    := If(lMESAG <> Nil,lMESAG,.T.)
Local lRet    := .T.

Default lFonte076 := .F.

If aCALEND[nDIASEM,1] = "00:00"
   cMENSAC := STR0152+" ( "+Alltrim(DIAEXTENSO(dVDATA))+" )"+c1LI;
             +STR0153+" "+Alltrim(cVCALEND)+" - "+sh7->h7_descri
Else
   lFORAH := .F.
   For nlo := 1 To Len(aCALEND[nDIASEM,2])
      If cVTIPO = "I"
         If cVHORA >= aCALEND[nDIASEM,2,nlo,1] .And. cVHORA < aCALEND[nDIASEM,2,nlo,2]
            lFORAH := .T.
            Exit
         Endif
      Else
         If cVHORA > aCALEND[nDIASEM,2,nlo,1] .And. cVHORA <= aCALEND[nDIASEM,2,nlo,2]
            lFORAH := .T.
            Exit
         Endif
      Endif
   Next nlo
   If !lFORAH
      cMENSAC := STR0154+" "+Dtoc(dVDATA)+c1LI+STR0135+c1LI
      nlo := 0
      For nlo := 1 To Len(aCALEND[nDIASEM,2])
         cMENSAC := cMENSAC+aCALEND[nDIASEM,2,nlo,1]+" "+STR0066+" "+ aCALEND[nDIASEM,2,nlo,2]+c1LI
      Next nlo
   Endif
Endif

If lFonte076
	If !Empty(cMENSAC) .And. lMES
		cMENSAC := cMENSAC+c1LI
		ShowHelpDlg(STR0076,{cMENSAC},1,{STR0165},1) //"ATENO"###"Informar outra Data ou Horrio do turno do Mdico."
		lRet := .F.
	Endif
Else
	If !Empty(cMENSAC) .And. lMES
		cMENSAC := cMENSAC+c1LI+STR0155 // "Deseja confirmar ?"
		lRet := MsgYesNo(cMENSAC,STR0149) // "NAO CONFORMIDADE"
	Endif
EndIf

Return lRet
//---------------------------------------------------------------------
/*/{Protheus.doc} MDT075PRE
Define funcao de alteracao/exclusao para validacao do usuario (SBIS)
Uso MDTA075

@return Nil

@sample
MDT075PRE(cAlias,nReg,nOpcx)

@author Jackson Machado
@since 21/08/2012
/*/
//---------------------------------------------------------------------
Function MDT075PRE(cAlias,nReg,nOpcx)
Local aArea := GetArea()

If nOpcx == 4
	If ExistBlock("MDTA075CAD")  //Ponto de entrada
		ExecBlock("MDTA075CAD",.F.,.F.)
	Endif
Endif
dbSelectArea("TMK")
dbSetOrder(1)
dbSeek(xFilial("TMK")+TMJ->TMJ_CODUSU)
If nOpcx == 5 .Or. nOpcx == 4// Apenas os usurios que incluiram podem excluir ou alterar...
	If NGCADICBASE("TMK_USUARI","A","TMK",.F.) .AND. SuperGetMV("MV_NG2SEG",.F.,"2") == "1" .AND. AllTrim(cUserName) <> AllTrim(MDTDATALO("TMJ->TMJ_USERGI",.F.)) .AND.;
			AllTrim(MDTDATALO("TMJ->TMJ_USERGI",.F.)) <> AllTrim(cUserName)  .AND. AllTrim(cUserName) <> AllTrim(TMK->TMK_USUARI) .AND.;
			AllTrim(TMK->TMK_USUARI) <> AllTrim(cUserName) .AND. !(SuperGetMV("MV_MDTPS",.F.,"N") == "S")
		ShowHelpDlg("SEMPERM",	{STR0159}	,2,;//"Usurio sem permisso para alterar o registro."
								{STR0160}	,2)//"Acesse o sistema com o usurio de incluso/alterao do registro."
		RestArea(aArea)
		Return .F.
	Endif
EndIf
If nOpcx == 5 .AND. FindFunction("MDTEXCSBI") .AND. NGCADICBASE("TMK_USUARI","A","TMK",.F.) .AND. !MDTEXCSBI(MDTDATALO("TMJ->TMJ_USERGI"))
	RestArea(aArea)
	Return .F.
Endif
RestArea(aArea)

NGCAD01(cAlias,nReg,nOpcx)
RestArea(aArea)
Return .T.

//---------------------------------------------------------------------
/*{Protheus.doc} MDT075INC
Define funcao de incluso para validacao do usuario (SBIS)
Uso MDTA075

@return Nil

@sample
MDT075INC(cAlias,nReg,nOpcx)

@author Guilherme Freudenburg
@since 18/06/2013
/*/
//---------------------------------------------------------------------
Function MDT075INC(cAlias,nReg,nOpcx)

	Local aArea := GetArea()
	Local aChoice := {}
	Local aSize := MsAdvSize(,.f.,430)
	Local oDlgTMJ

	Private cNewHrCons	:= ""

	If ExistBlock("MDTA075INC")  //Ponto de entrada
		ExecBlock("MDTA075INC",.F.,.F.)
	Endif

	If FindFunction( "MDTA076" ) .And. AliasInDic( "TY9" )

		//Atribui valor as variveis de tela
		dbSelectarea("TMJ")
		RegToMemory("TMJ",.T.)

		//Monta a Tela
		Define MsDialog oDlgTMJ Title OemToAnsi( STR0166 ) From aSize[ 7 ] , 0 To aSize[ 6 ] , aSize[ 5 ] Of oMainWnd Pixel //"Agenda Mdica"

			//Campos que no vao ser mostrados no browse
			aNao := { "TMJ_MAT", "TMJ_CONVOC", "TMJ_DTPROG", "TMJ_DTATEN", "TMJ_PCMSO", "TMJ_ATEENF", "TMJ_CODENF",;
				 "TMJ_DESENF", "TMJ_DTENFE", "TMJ_HRENFE", "TMJ_INDENF", "TMJ_HRCHGD", "TMJ_HRAGUA", "TMJ_HRSAID" }

			aChoice  := NGCAMPNSX3( cAlias,aNao )

			oPnlPai := TPanel():New(00,00,,oDlgTMJ,,,,,,0,0,.F.,.F.)
			oPnlPai:Align   := CONTROL_ALIGN_ALLCLIENT
			oEnchoice:= MsMGet():New( cAlias,,nOpcx,,,,aChoice,{0,0,aSize[ 6 ]/2,aSize[ 5 ]/2},,,,,,oPnlPai )

		//Ativacao do Dialog
		Activate MsDialog oDlgTMJ On Init EnchoiceBar( oDlgTMJ,{ ||  nOpca := 1, If( MDT076TDOK( "TMJ", TML->TML_CALEND ), oDlgTMJ:End(), nil ) },; //confirm
															{ || nOpcao := 0,oDlgTMJ:End() } )CENTERED //cancel
	Else
		NGCAD01("TMJ",nReg,nOpcx)
	EndIf

	RestArea(aArea)
Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MDT075TRO
Troca a tabela de consulta do click da direita
Uso MDTA075 e MDTA110

@return Nil

@sample
MDT075TRO(aSMenu,1)

@author Jackson Machado
@since 05/11/2012
/*/
//---------------------------------------------------------------------
Function MDT075TRO(aCMenu,nPosicao,cTabela)
Local nAt
Local cVariavel := aCMenu[nPosicao][2]
Local cTmpVar   := ""

If ValType(cTabela) == "C"
	nAt       := RAT("MDTRELHIS",cVariavel)
	cTmpVar   := SubStr(cVariavel,1,nAt+Len("MDTRELHIS"))
	cTmpVar   += "'"+cTabela+"'"
	cTmpVar   += ")"
	cVariavel := cTmpVar
EndIf

Return cVariavel
//---------------------------------------------------------------------
/*/{Protheus.doc} fValidExc
Verifica se existe agenda cadastrada para o mdico para permitir excluir.

@author Alessandro Smaha
@since 23/05/2013
@return lLogico
/*/
//---------------------------------------------------------------------
Static Function fValidExc(cAtendente)

Local lRetOk := .F.
Local aArea  := GetArea()

DbSelectArea("TMJ")
TMJ->(DbSetOrder(1)) // TMJ_FILIAL+TMJ_CODUSU+DTOS(TMJ_DTCONS)+TMJ_HRCONS
TMJ->(DbSeek(TMK->(xFilial("TMJ")+cAtendente)))

If TMJ->(!Eof())
	ShowHelpDlg(STR0076,	{STR0161},3,; // "Ateno"###"Existe agenda cadastrada para este atendente."
								{STR0162},3)  // "Excluir as agendas para permitir excluir o atendente."
	lRetOk := .T.
EndIf

RestArea(aArea)

Return lRetOk
