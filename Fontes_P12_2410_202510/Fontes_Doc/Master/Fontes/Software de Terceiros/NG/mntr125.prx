#INCLUDE "MNTR125.ch"
#INCLUDE "PROTHEUS.CH"
/*/


Ŀ
Funo    MNTR125    Autor  Inacio Luiz Kolling    Data 15/10/2007
Ĵ
Descrio Redimento de pneus recapados                                
Ĵ
 Uso      Generico                                                    
ٱ


*/      
Function MNTR125()

	WNREL   := "MNTR125"
	LIMITE  := 232
	cDESC1  := STR0001 //"Redimento de pneus recapados"
	cDESC2  := " "
	cDESC3  := " "
	cSTRING := "TQS"

	Private NOMEPROG := "MNTR125"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0002,1,STR0003,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private aVETINR  := {}
	Private cPerg    := "MNR125"
	Private CABEC1
	Private CABEC2
	Private lOper    := If(Alltrim(GetMv("MV_NGOPER")) == "S",.T.,.F.)
	Private cContab  := GetMv("MV_MCONTAB")
	Private vCampoCC := {}

	If cContab == "CTB"
		vCampoCC := {"CTT","CTT_CUSTO","CTT_OPERAC"}
	ElseIf cContab == "CON"
		vCampoCC := {"SI3","I3_CUSTO","I3_OPERAC"}
	EndIf

	SetKey( VK_F9, { | | NGVersao( "MNTR125" , 1 ) } )

	Pergunte(cPERG,.F.)  

	TITULO := STR0001

	// Envia controle para a funcao SETPRINT
	WNREL := SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("TQS")
		Return
	EndIf     
	SetDefault(aReturn,cSTRING)
	RptStatus({|lEND| MNTR125IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0012,STR0013) //"Aguarde..."###"Processando Registros..."
	Dbselectarea("TQS")

Return .T.    

/*/


Ŀ
Funo    MNR125IMP  Autor  Inacio Luiz Kolling    Data 15/10/2007
Ĵ
Descrio Chamada do Relatrio                                        
Ĵ
 Uso      MNTR125                                                     
ٱ


/*/
Function MNTR125IMP(lEND,WNREL,TITULO,TAMANHO)

	Local vBanda     := {'OR','R1','R2','R3','R4'}
	Private cRODATXT := ""
	Private nCNTIMPR := 0     
	Private li       := 80, m_pag := 1

	nTIPO  := IIf(aReturn[4]==1,15,18)                                                                                                                                                                                               
	CABEC1 := Space(108)+STR0017
	CABEC2 := STR0018

	/*
	1         2         3         4         5         6         7         8         9         1         2         3         4         5         6         7         8         9         1         2
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567
	****************************************************************************************************************************************************************************************************************
	Redimento de pneus recapados
	****************************************************************************************************************************************************************************************************************
	-----------------Rendimento Real-----------------     -----------------Redimento Previsto--------------
	Familia  Medida   Modelo       Cod. Pneu          Num. Fogo    DOT   Vida  Sulco   Status                       OR        R1        R2        R3        R4            OR        R1        R2        R3        R4
	****************************************************************************************************************************************************************************************************************
	xxxxxx   xxxxxx   xxxxxxxxxx   xxxxxxxxxxxxxxxx   xxxxxxxxxx   xxxx   xx   999999  xxxxxxxxxxxxxxxxxxxx  999999999 999999999 999999999 999999999 999999999     999999999 999999999 999999999 999999999 999999999

	*/                              

	cAliasQry := GetNextAlias()
	cQuery := " SELECT TQS.TQS_FILIAL, ST9.T9_CODFAMI, TQS.TQS_MEDIDA, ST9.T9_TIPMOD,"
	cQuery += "        TQS.TQS_CODBEM, TQS.TQS_NUMFOG, TQS.TQS_DOT, TQS.TQS_BANDAA,"
	cQuery += "        TQS.TQS_SULCAT, TQS.TQS_KMOR, TQS.TQS_KMR1, TQS.TQS_KMR2,"
	cQuery += "        TQS.TQS_KMR3, TQS.TQS_KMR4, TQS_DESENH, ST9.T9_FABRICA, "
	cQuery += "        (SELECT TQY.TQY_DESTAT FROM "+RetSqlName("TQY")+" TQY WHERE TQY.TQY_CATBEM = '3' AND TQY.TQY_STATUS = ST9.T9_STATUS "
	cQuery += "        AND TQY.D_E_L_E_T_ <> '*' AND TQY_FILIAL = " + ValToSql(xFilial("TQY")) + ") STATUSP "
	If lOper
		cQuery += " ,TSZ.TSZ_DESSER"
	EndIf	
	cQuery += " FROM " + RetSqlName("TQS")+" TQS "
	cQuery += " JOIN " + RetSqlName("ST9")+" ST9 ON TQS.TQS_CODBEM = ST9.T9_CODBEM"
	cQuery += " JOIN " + RetSQLName(vCampoCC[1])+" CDC ON (ST9.T9_CCUSTO = CDC."+vCampoCC[2]+") "
	If lOper
		cQuery += " JOIN "+RetSQLName("TSZ")+" TSZ ON (CDC."+vCampoCC[3]+" = TSZ.TSZ_CODSER ) "
	EndIf
	cQuery += " AND ST9.T9_CODFAMI >= '"+mv_par01+"' AND ST9.T9_CODFAMI <= '" +mv_par02+"'
	cQuery += " AND ST9.T9_TIPMOD >= '"+mv_par03+"' AND ST9.T9_TIPMOD <= '" +mv_par04+"'
	cQuery += " AND ST9.T9_STATUS >= '"+mv_par09+"' AND ST9.T9_STATUS <= '" +mv_par10+"'
	cQuery += " WHERE TQS.TQS_FILIAL >= '"+mv_par07+"' AND TQS.TQS_FILIAL <= '" +mv_par08+"'
	cQuery += " AND TQS.TQS_MEDIDA >= '"+mv_par05+"' AND TQS.TQS_MEDIDA <= '" +mv_par06+"'
	cQuery += " AND TQS.TQS_CODBEM >= '"+mv_par11+"' AND TQS.TQS_CODBEM <= '" +mv_par12+"'
	//cQuery += " AND TQS.TQS_EIXO  <> ' ' AND TQS.TQS_EIXO <> 'R'"
	cQuery += " AND TQS.D_E_L_E_T_ <> '*' "
	cQuery += " AND ST9.D_E_L_E_T_ <> '*' "
	If lOper
		cQuery += " AND TSZ.D_E_L_E_T_<>'*'
		cQuery += " ORDER BY TSZ.TSZ_DESSER, TQS.TQS_FILIAL,ST9.T9_CODFAMI,TQS.TQS_MEDIDA,ST9.T9_TIPMOD,TQS.TQS_CODBEM"
	Else
		cQuery += " ORDER BY TQS.TQS_FILIAL,ST9.T9_CODFAMI,TQS.TQS_MEDIDA,ST9.T9_TIPMOD,TQS.TQS_CODBEM"
	Endif

	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)                                 

	SetRegua(LastRec())                                                             
	DbGoTop()           

	If Eof()
		MsgInfo(STR0014,STR0015) //"No existem dados para montar o relatrio."###"ATENO"
		Return .F.
	Endif

	cBemQuer := ''
	cOpeQuer := ''
	If lOper
		cCond := "(cAliasQry)->TQS_FILIAL = cFilQuer .AND. (cAliasQry)->TSZ_DESSER = cOpeQuer"
	Else
		cCond := "(cAliasQry)->TQS_FILIAL = cFilQuer"
	Endif

	While !Eof()
		cFilQuer := (cAliasQry)->TQS_FILIAL
		DbSelectArea("SM0")
		SM0->(DbSetOrder(1))
		MsSeek(cEMPANT+cFilQuer)

		NgSomaLi(58)

		If lOper
			NgSomaLi(58)
			@ Li,000 PSay STR0022 //"Operao:"
			@ Li,010 PSay (cAliasQry)->TSZ_DESSER
			cOpeQuer := (cAliasQry)->TSZ_DESSER
			NgSomaLi(58)
		EndIf  

		@ Li,000 PSay STR0016+" : "+cFilQuer+" - "+SM0->M0_FILIAL
		NgSomaLi(58)
		DbSelectArea(cAliasQry)
		While !Eof() .And. &cCond
			IncRegua()
			If cBemQuer != (cAliasQry)->TQS_CODBEM
				NgSomaLi(58)       
				@ Li,000 PSay (cAliasQry)->T9_CODFAMI Picture "@!"
				@ Li,009 PSay (cAliasQry)->TQS_MEDIDA Picture "@!"
				@ Li,018 PSay (cAliasQry)->T9_TIPMOD  Picture "@!"
				@ Li,031 PSay (cAliasQry)->TQS_CODBEM Picture "@!"
				@ Li,050 PSay (cAliasQry)->TQS_NUMFOG Picture "@!"
				@ Li,063 PSay (cAliasQry)->TQS_DOT    Picture "@!"

				If !Empty((cAliasQry)->TQS_BANDAA)
					@ Li,070 PSay vBanda[Val((cAliasQry)->TQS_BANDAA)]
				Endif

				@ Li,075 PSay (cAliasQry)->TQS_SULCAT Picture "@E 999.99"
				@ Li,083 PSay SubStr((cAliasQry)->STATUSP,1,22)  Picture "@!"

				DbSelectArea("TQX")
				TQX->(DbSetOrder(1))
				MsSeek(Xfilial("TQX")+(cAliasQry)->TQS_MEDIDA+(cAliasQry)->T9_TIPMOD)

				nB0 := 0
				nB1 := 0
				nB2 := 0
				nB3 := 0
				nB4 := 0
				cAliasQry2 := GetNextAlias()
				cQuery2 := " SELECT TQV.TQV_CODBEM, TQV.TQV_SULCO, TQV.TQV_BANDA "
				cQuery2 += " FROM " + RetSqlName("TQV")+" TQV "
				cQuery2 += " WHERE TQV.TQV_CODBEM = '"+(cAliasQry)->TQS_CODBEM+"'"
				cQuery2 += " AND   TQV.D_E_L_E_T_ <> '*' "
				cQuery2 += " ORDER BY TQV.TQV_CODBEM, TQV.TQV_DTMEDI, TQV.TQV_HRMEDI, TQV.TQV_BANDA "
				cQuery2 := ChangeQuery(cQuery2)
				dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery2),cAliasQry2, .F., .T.)
				dbSelectArea(cAliasQry2)
				While !Eof() .AND. (cAliasQry2)->TQV_CODBEM = (cAliasQry)->TQS_CODBEM
					If (cAliasQry2)->TQV_BANDA = '1'
						nB0 := (cAliasQry2)->TQV_SULCO
					ElseIf (cAliasQry2)->TQV_BANDA = '2'
						nB1 := (cAliasQry2)->TQV_SULCO
					ElseIf (cAliasQry2)->TQV_BANDA = '3'
						nB2 := (cAliasQry2)->TQV_SULCO
					ElseIf (cAliasQry2)->TQV_BANDA = '4'
						nB3 := (cAliasQry2)->TQV_SULCO
					ElseIf (cAliasQry2)->TQV_BANDA = '5'
						nB4 := (cAliasQry2)->TQV_SULCO
					Endif
					dbSkip()
				End
				(cAliasQry2)->(dbCloseArea())

				If nB0 != 0
					@ Li,105 PSay (cAliasQry)->TQS_KMOR / (TQX->TQX_SULCOO-nB0)   Picture "999999999"
				Else
					@ Li,105 PSay 0   Picture "999999999"
				Endif
				If nB1 != 0
					@ Li,115 PSay (cAliasQry)->TQS_KMR1 / (TQX->TQX_SULCOO-nB1)   Picture "999999999"
				Else
					@ Li,115 PSay 0   Picture "999999999"
				Endif
				If nB2 != 0
					@ Li,125 PSay (cAliasQry)->TQS_KMR2 / (TQX->TQX_SULCOO-nB2)   Picture "999999999"
				Else
					@ Li,125 PSay 0   Picture "999999999"
				Endif
				If nB3 != 0
					@ Li,135 PSay (cAliasQry)->TQS_KMR3 / (TQX->TQX_SULCOO-nB3)   Picture "999999999"
				Else
					@ Li,135 PSay 0   Picture "999999999"
				Endif
				If nB3 != 0
					@ Li,145 PSay (cAliasQry)->TQS_KMR4 / (TQX->TQX_SULCOO-nB4)   Picture "999999999"
				Else
					@ Li,145 PSay 0   Picture "999999999"
				Endif

				DbSelectArea("TQU")
				TQU->(DbSetOrder(1))
				MsSeek(Xfilial("TQU")+(cAliasQry)->TQS_DESENH+(cAliasQry)->T9_FABRICA)

				@ Li,159 PSay TQX->TQX_KMESPO / TQX->TQX_SULCOO         Picture "999999999"
				@ Li,169 PSay TQX->TQX_KMESPO * (TQU->TQU_KMESP1 / 100) Picture "999999999"
				@ Li,179 PSay TQX->TQX_KMESPO * (TQU->TQU_KMESP2 / 100) Picture "999999999"
				@ Li,189 PSay TQX->TQX_KMESPO * (TQU->TQU_KMESP3 / 100) Picture "999999999"
				@ Li,199 PSay TQX->TQX_KMESPO * (TQU->TQU_KMESP4 / 100) Picture "999999999"
				cBemQuer := (cAliasQry)->TQS_CODBEM
			Endif

			DbSelectArea(cAliasQry)
			DbSkip()
		End
		NgSomaLi(58)
	End 

	(cALIASQRY)->(dbCloseArea())

	RODA(nCNTIMPR,cRODATXT,TAMANHO)       

	// Devolve a condicao original do arquivo principal
	RetIndex('TQS')
	Set Filter To
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

Return Nil

/*/


Ŀ
Funo     MNR125ST   Autor Marcos Wagner Junior   Data  27/02/08 
Ĵ
Descrio  Valida os codigos De... , Ate... Status do Bem				  
ٱ


/*/
Function MNR125ST(nOpc,cParDe,cParAte)

	Local lRet := .t.

	If (Empty(cParDe) .AND. cParAte = 'ZZ')
		Return .t.
	Else
		If nOpc == 1
			If Empty(cParDe)
				Return .t.
			Else		
				dbSelectArea("TQY")
				dbSetOrder(01)
				If dbSeek(xFilial("TQY")+cParDe)
					If TQY->TQY_CATBEM != '3'
						MsgStop(STR0021)//"O Status digitado no  um status de pneu!"
						lRet := .f.
					Endif
				Else
					lRet := ExistCpo("TQY",cParDe)
				Endif
				If !lRet
					Return .f.
				EndIf 
			Endif 
		ElseIf nOpc == 2      
			If (cParAte == 'ZZ')
				Return .t.
			Else
				dbSelectArea("TQY")
				dbSetOrder(01)
				If dbSeek(xFilial("TQY")+cParAte)
					If TQY->TQY_CATBEM != '3'
						MsgStop(STR0021)//"O Status digitado no  um status de pneu!"
						lRet := .f.
					Endif
				Else
					lRet := ExistCpo("TQY",cParAte)
				Endif
				If !lRet
					Return .f.
				EndIf		
				lRet := IIF(ATECODIGO("TQY",cParDe,cParAte,10),.T.,.F.)
			EndIf
		EndIf    
	Endif

Return lRet

/*


Ŀ
Funo    MNT125PNEU Autor Marcos Wagner Junior    Data 14/07/2008
Ĵ
Descrio Valida o parametro De/Ate Pneu                              
Ĵ
 Uso      MNTR125                                                     
ٱ


*/
Function MNT125PNEU(nOpc,cParDe,cParAte)

	Local aOldArea  := GetArea()
	Local lRet 		:= .T.

	If (Empty(cParDe) .AND. cParAte = 'ZZZZZZZZZZZZZZZZZZ')
		Return .t.
	Else
		If nOpc == 1
			If Empty(cParDe)
				Return .t.
			Else
				dbSelectArea("ST9")
				dbSetOrder(01)
				If dbSeek(xFilial("ST9")+cParDe)
					If ST9->T9_CATBEM != '3'
						MsgInfo(STR0023,STR0015) //"O Bem digitado no  um Pneu!"###"ATENO"
						lRet := .f.
					Endif
				Else
					lRet := ExistCpo("ST9",cParDe)
				Endif
			Endif 
		ElseIf nOpc == 2      
			If (cParAte == 'ZZZZZZZZZZZZZZZZZZ') 
				Return .t.
			Else
				dbSelectArea("ST9")
				dbSetOrder(01)
				If dbSeek(xFilial("ST9")+cParAte)
					If ST9->T9_CATBEM != '3'
						MsgInfo(STR0023,STR0015) //"O Bem digitado no  um Pneu!"###"ATENO"
						lRet := .f.
					Endif
				Endif
				If lRet
					lRet := IIF(ATECODIGO("ST9",cParDe,cParAte,12),.T.,.F.)
				Endif
			EndIf
		EndIf    
	Endif

	RestArea(aOldArea)

Return lRet