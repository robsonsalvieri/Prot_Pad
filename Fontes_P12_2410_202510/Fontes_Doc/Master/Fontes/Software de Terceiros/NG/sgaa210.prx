#INCLUDE "SGAA210.ch"
#INCLUDE "Protheus.ch"

/*/


Ŀ
Funo     SGAA210   Autor  Rafael Diogo Richter   Data 12/07/2005
Ĵ
Descrio  Programa para Retorno das OS                               
Ĵ
Objetivo                                                              
ٱ


/*/
Function SGAA210(cPlano)

//Ŀ
//Guarda conteudo e declara variaveis padroes 
//
Local aNGBEGINPRM := NGBEGINPRM( )

//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 						     
//
Local aOldMenu
Local aNGCAD02 := {}

Default cPlano := ""

Private asMenu
Private lTipo := .F.

Private aTrocaF3 := {}

aOldMenu := ACLONE(asMenu)

asMenu := NGRIGHTCLICK("SGAA210")

aNGCAD02:={;
		If(Type("aCHOICE")   == "A",ACLONE(aCHOICE),{}),;
		If(Type("aVARNAO")   == "A",ACLONE(aVARNAO),{}),;
		If(Type("aGETNAO")   == "A",ACLONE(aGETNAO),{}),;
		If(Type("cGETWHILE") == "C",cGETWHILE,NIL),;
		If(Type("cGETMAKE")  == "C",cGETMAKE,NIL),;
		If(Type("cGETKEY")   == "C",cGETKEY,NIL),;
		If(Type("cGETALIAS") == "C",cGETALIAS,NIL),;
		If(Type("cTUDOOK")   == "C",cTUDOOK,NIL),;
		If(Type("cLINOK")    == "C",cLINOK,NIL),;
		If(Type("aRELAC")    == "A",ACLONE(aRELAC),{}),;
		If(Type("aCHKDEL")   == "A",ACLONE(aCHKDEL),{}),;
		If(Type("bngGRAVA")  == "A",ACLONE(bngGRAVA),{}),;
		If(Type("aNGBUTTON") == "A",ACLONE(aNGBUTTON),{})}

Private aROTINA := MenuDef()

Private cCadastro := STR0010  //"Retorno das Ordens de Simulao"
Private LCORRET := .F.
//Ŀ
// Endereca a funcao de BROWSE                                  
//

Dbselectarea("TBN")
Dbseek(xFILIAL('TBN'))

If !Empty(cPlano)
	Set Filter To &("TBN_SITUAC == '3' .And. TBN_TERMIN == '2' .And. TBN_PLAEME == '" + cPlano +"'")
Else
	Set Filter To  TBN_SITUAC == "3" .And. TBN_TERMIN == '2'
EndIf

If Eof()
   Help("",1,"ARQVAZIO")
Else
   mBrowse(6,1,22,75,"TBN",,'TBN->TBN_DATINP < dDataBase')
Endif

//Ŀ
// Devolve a condicao original do arquivo principal             
//
Dbselectarea("TBN")
Set Filter To
Dbsetorder(1)

Dbselectarea("TBN")
Dbsetorder(1)
Dbseek(xFILIAL('TBN'))

//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK) 					 	 		  
//

asMenu  := ACLONE(aOldMenu)
aCHOICE := ACLONE(aNGCAD02[1])
aVARNAO := ACLONE(aNGCAD02[2])
AGETNAO := ACLONE(aNGCAD02[3])
If(aNGCAD02[4] != NIL,cGETWHILE := aNGCAD02[4],)
If(aNGCAD02[5] != NIL,cGETMAKE  := aNGCAD02[5],)
If(aNGCAD02[6] != NIL,cGETKEY   := aNGCAD02[6],)
If(aNGCAD02[7] != NIL,cGETALIAS := aNGCAD02[7],)
If(aNGCAD02[8] != NIL,cTUDOOK   := aNGCAD02[8],)
If(aNGCAD02[9] != NIL,cLINOK    := aNGCAD02[9],)
aRELAC    := ACLONE(aNGCAD02[10])
aCHKDEL   := ACLONE(aNGCAD02[11])
bngGRAVA  := ACLONE(aNGCAD02[12])
aNGBUTTON := ACLONE(aNGCAD02[13])

//Ŀ
//Retorna conteudo de variaveis padroes       
//
NGRETURNPRM(aNGBEGINPRM)
Return .T.

/*/


Ŀ
Funo     Sg210Fin  Autor  Rafael Diogo Richter   Data 12/07/2005
Ĵ
Descrio Programa para Finalizacao da Os                             
Ĵ
Tabelas   TBN - Ordem do Plano de Simulacao                           
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/

Function Sg210Fin(cAlia,nRecno,nOpcx)
Local nRet := 0, cEmergencia := '', cSeq := ''

cEmergencia := TBN->TBN_PLAEME
cSeq := TBN->TBN_SEQUEN

aChoice := {"TBN_CODORD","TBN_CODPLA","TBN_DATINR","TBN_DATFIN","TBN_OBSERV"}
nRet := NgCad01("TBN",nRecno,4)
If nRet = 1
   DbSelectArea("TBF")
   DbSetOrder(1)
   If DbSeek(xFilial("TBF")+cEmergencia+cSeq)
	   RecLock("TBF",.f.)
	   TBF->TBF_DATPLA := TBN->TBN_DATINR
	   MsUnLock("TBF")
	EndIf
   DbSelectArea("TBN")
   RecLock("TBN",.f.)
   TBN->TBN_TERMIN := '1'
   MsUnLock("TBN")
EndIf
Return .T.

/*/


Ŀ
Funo     Sg210Fin  Autor  Rafael Diogo Richter   Data 12/07/2005
Ĵ
Descrio Programa para Finalizacao da Os                             
Ĵ
Tabelas   TBN - Ordem do Plano de Simulacao                           
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function Sg210Leg()
BrwLegenda(cCadastro,STR0006,{{"ENABLE",Oemtoansi(STR0008)},; //"Legenda"###"Em Dia"
                              {"BR_VERMELHO",STR0009}} )  //"Atrasada"
Return .t.

/*/


Ŀ
Funo     Sg210Chk  Autor  Rafael Diogo Richter   Data 21/10/2005
Ĵ
Descrio Programa para Cadastro dos CheckLists                       
Ĵ
Tabelas   TBN - Ordem do Plano de Simulacao                           
Ĵ
 Uso       SigaSGA                                                    
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Function Sg210Chk()

	Local OLDROT, nINDETA := 0, nfor := 0
	Local aSize := {}, aObjects := {}, aInfo := {}, aPosObj := {}

	Local oPnlAll
	Local oTempCE, oTempOC

	Local lTipUsu := NGCADICBASE("TBQ_TIPUSU","A","TBQ",.F.)

	Private cAliasCE	:= GetNextAlias() //Tabela 'Checklists Executados' 'TQB'
	Private cAliasOC	:= GetNextAlias() //Tabela 'Opo do  Checklist'   'TBE'
	Private cARQTRAZ, cARQTRAK
	Private lTipQue	:= NGCADICBASE( "TBQ_TIPQUE" , "A" , "TBQ" , .F. )

	DbSelectArea("TBN")

	lINVERTE  := .F.
	cMARCA    := GetMark()
	nORDEM  := TBN->TBN_CODORD
	nPLANO  := TBN->TBN_CODPLA
	OLDROT  := aCLONE(aROTINA)

	DbSelectArea("TBQ")
	nTAMQAA      := If((TAMSX3("QAA_MAT")[1]) < 1,16,(TAMSX3("QAA_MAT")[1]))

	aDBF := {}
	AADD(aDBF,{"TBQ_OK"     ,"C",02, 0 })
	AADD(aDBF,{"TBQ_ORDEM"  ,"C",06, 0 })
	AADD(aDBF,{"TBQ_PLANO"  ,"C",06, 0 })
	If lTipQue
		AADD(aDBF,{"TBQ_TIPQUE"  ,"C",01,0})
		AADD(aDBF,{"TBQ_DESTIP"  ,"C",15,0})
	EndIf
	AADD(aDBF,{"TBQ_CHKLIS"  ,"C",06, 0 })
	AADD(aDBF,{"TBQ_SEQCHK" ,"C",03, 0 })
	AADD(aDBF,{"TBQ_DESCHK","C",60, 0 })
	If lTipUsu
		AADD(aDBF,{"TBQ_TIPUSU","C",1, 0 })
	EndIf
	AADD(aDBF,{"TBQ_USUARI","C",nTAMQAA,TAMSX3("TBQ_USUARI")[2] })

	oTempCE := FWTemporaryTable():New( cAliasCE, aDBF )
	oTempCE:AddIndex( "1", {"TBQ_ORDEM","TBQ_PLANO","TBQ_CHKLIS"} )
	oTempCE:Create()

	aTRBQ := {}
	AADD(aTRBQ,{"TBQ_OK"     ,Nil," "    ,})
	If lTipQue
		AADD(aTRBQ,{"TBQ_DESTIP"  ,Nil,"Tipo",})//"Tipo Chk."
	EndIf
	AADD(aTRBQ,{"TBQ_CHKLIS"  ,Nil,STR0004,})//"CheckList"
	AADD(aTRBQ,{"TBQ_SEQCHK" ,Nil,STR0027,})//"Sequncia"
	AADD(aTRBQ,{"TBQ_DESCHK",Nil,STR0028,})//"Descrio"
	AADD(aTRBQ,{"TBQ_USUARI",Nil,STR0019,})//"Executante"

	aDBF3 := TBE->(DbStruct())
	AADD(aDBF3,{"TBE_RESPOS","C",10,0})
	AADD(aDBF3,{"TBE_OK"    ,"C",2,0})

	oTempOC := FWTemporaryTable():New( cAliasOC, aDBF3 )
	oTempOC:AddIndex( "1", {"TBE_CODCHK","TBE_CODOPC"} )
	oTempOC:Create()

	dbSelectArea("TBQ")
	dbSetOrder(3)
	dbSeek(xFILIAL("TBQ")+TBN->TBN_CODORD+TBN->TBN_CODPLA)
	While !Eof() .And. TBQ->TBQ_FILIAL == xFILIAL("TBQ") .And.;
		TBQ->TBQ_ORDEM == TBN->TBN_CODORD .And. TBQ->TBQ_PLANO == TBN->TBN_CODPLA

		dbSelectArea("TBD")
		dbSetOrder(1)
		dbSeek(xFILIAL('TBD')+TBQ->TBQ_CHKLIS)
		(cAliasCE)->(DbAppend())
		(cAliasCE)->TBQ_OK     := If(Empty(TBQ->TBQ_OK),"  ",cMARCA)
		(cAliasCE)->TBQ_ORDEM  := TBQ->TBQ_ORDEM
		(cAliasCE)->TBQ_PLANO  := TBQ->TBQ_PLANO
		If lTipQue
			(cAliasCE)->TBQ_TIPQUE := TBQ->TBQ_TIPQUE
			(cAliasCE)->TBQ_DESTIP := NGRETSX3BOX( "TBQ_TIPQUE" , TBQ->TBQ_TIPQUE )
		EndIf
		(cAliasCE)->TBQ_CHKLIS := TBQ->TBQ_CHKLIS
		(cAliasCE)->TBQ_DESCHK := TBD->TBD_DESCHK
		If lTipUsu
			(cAliasCE)->TBQ_TIPUSU := TBQ->TBQ_TIPUSU
		EndIf
		(cAliasCE)->TBQ_USUARI := TBQ->TBQ_USUARI
		(cAliasCE)->TBQ_SEQCHK := TBQ->TBQ_SEQCHK
		MsUnlock(cAliasCE)


		dbSelectArea("TBR")
	dbSetOrder(2)
		dbSeek(xFilial("TBR")+TBN->TBN_CODORD+TBQ->TBQ_CHKLIS)
		While !Eof() .And. TBR->TBR_ORDEM == TBN->TBN_CODORD .And. TBR->TBR_CHKLIS == TBQ->TBQ_CHKLIS
			dbSelectArea(cAliasOC)
		RecLock((cAliasOC),.T.)
			(cAliasOC)->TBE_CODCHK	:= TBR->TBR_CHKLIS
			(cAliasOC)->TBE_CODOPC	:= TBR->TBR_OPCAO
			(cAliasOC)->TBE_RESPOS	:= TBR->TBR_RESPOS
			(cAliasOC)->TBE_TIPRES	:= TBR->TBR_TIPRES
			(cAliasOC)->TBE_CONDOP	:= TBR->TBR_OPERAD
			(cAliasOC)->TBE_CONDIN	:= TBR->TBR_CONDIN
			(cAliasOC)->TBE_OK		:= TBR->TBR_OK
			MsUnlock(cAliasOC)
			dbSelectArea("TBR")
			dbSetOrder(2)
			dbSkip()
		EndDo

		dbSelectArea("TBQ")
		dbSkip()

	EndDo

	dbSelectArea(cAliasCE)
	dbGotop()
	nOPCA   := 0

	//Definicao de tamanho de tela e objetos
	aSize := MsAdvSize(,.f.,430)
	Aadd(aObjects,{030,030,.t.,.t.})
	Aadd(aObjects,{100,100,.t.,.t.})
	aInfo := {aSize[1],aSize[2],aSize[3],aSize[4],0,0}
	aPosObj := MsObjSize(aInfo, aObjects,.t.)

	Define MsDialog oDLG1 Title STR0012 From aSize[7],0 To aSize[6],aSize[5] Of oMAINWND Pixel //"CheckLists Executados"

	oPnlAll := TPanel():New(01,01,,oDLG1,,,,,,10,10,.F.,.F.)
	oPnlAll:Align := CONTROL_ALIGN_ALLCLIENT

	oPanelTop := TPanel():New(01,01,,oPnlAll,,,,,,10,10,.F.,.F.)
	oPanelTop:nHeight := 50
	oPanelTop:Align := CONTROL_ALIGN_TOP
	@ 0.4,5  Button STR0013 Of oPnlAll Size 40,12 Action (SgProcChk('TBN',Recno(),1))  //"Incluir"
	@ 0.4,20 Button STR0005 Of oPnlAll Size 40,12 Action (SgProcChk('TBN',Recno(),2))  //"Excluir"
	@ 0.4,35 Button STR0014 Of oPnlAll Size 40,12 Action (Sg210Excu())  //"Executante"
	@ 0.4,50 Button STR0015 Of oPnlAll Size 40,12 Action (SG210BCON())  //"Conhecimento"

	oPanelBot := TPanel():New(01,01,,oPnlAll,,,,,,150,350,.F.,.F.)
	oPanelBot:Align := CONTROL_ALIGN_ALLCLIENT

		oMARK       := MsSelect():NEW(cAliasCE,"TBQ_OK",,aTRBQ,@lINVERTE,@cMARCA,{35,1,150,330},,,oPanelBot)
		oMARK:oBROWSE:Align := CONTROL_ALIGN_ALLCLIENT
		If lTipQue
			oMARK:bMARK := {| | If( (cAliasCE)->TBQ_TIPQUE == "1" , SGAA215(cMARCA,lINVERTE) , fNewQuest() )}
		Else
			oMARK:bMARK := {| | SGAA215(cMARCA,lINVERTE)}
		EndIf
		oMARK:oBROWSE:lHASMARK = .T.
		oMARK:oBROWSE:lCANALLMARK := .T.
		oMARK:oBROWSE:bALLMARK := {|| Sg210Inv(cMARCA) }

		If (cAliasCE)->(Eof())
			oMARK:oBrowse:Disable()
		Endif


	Activate MsDialog oDLG1 On Init EnchoiceBar(oDLG1,{|| nOPCA := 1,If(!SGAA210Efi(),nOPCA  := 0,oDLG1:End())},{||oDLG1:End()})

	If nOpca == 1
		Sg210GChk()
	EndIf

	oTempCE:Delete()
	oTempOC:Delete()

Return Nil

/*/


Ŀ
Funcao    Sg210Excu  Autor  Rafael Diogo Richter   Data 22/09/2005
Ĵ
Descrio  Funcao do executante do CheckList                          
Ĵ
Retorno                                                               
ٱ


/*/
Function Sg210Excu()

	Local oDlg7
	Local lTipUsu	:= NGCADICBASE( "TBQ_TIPUSU", "A", "TBQ", .F. )
	Local aTipo		:= { "1=" + NGRETSX3BOX( "TBQ_TIPUSU", '1' ), "2=" + NGRETSX3BOX( "TBQ_TIPUSU", '2' ) }

	Private lAccsUsu
	Private cCodUsu
	Private cNomUsu

	Private oGetNom
	Private oGetNum
	Private oSay

	If Empty((cAliasCE)->TBQ_OK)
		Help(" ",1,STR0016,,STR0017,3,1)  //"ATENO"##"Nao Foi Selecionada o CheckList Como Executado"
		Return
	EndIf

	//Verifica se o usurio corrente pre
	If FindFunction( "MDTVldFldAcc")
		lAccsUsu := MDTVldFldAcc( "TBQ_USUARI" ) .And. SGAF3QAASRA("TBQ_USUARI","TBQ_TIPUSU")
	Else
		lAccsUsu := SGAF3QAASRA("TBQ_USUARI","TBQ_TIPUSU")
	EndIf

	M->TBQ_TIPUSU := IIf( lTipUsu .And. !Empty((cAliasCE)->TBQ_TIPUSU), (cAliasCE)->TBQ_TIPUSU, '1')
	cNomUsu := Space(40)
	M->TBQ_USUARI := (cAliasCE)->TBQ_USUARI

	cCodUsu := MDTHideCpo( M->TBQ_USUARI, "TBQ_USUARI" )
	cNomUsu := MDTHideCpo( cNomUsu, IIf( M->TBQ_TIPUSU == "1", "QAA_NOME", "RA_NOME" ) )

	Sg210Usu( cCodUsu )
	
	nExec := 0
	Define Msdialog oDlg7 From 0,0 To 150,370 Title Oemtoansi(STR0018) Pixel  //"Executante do CheckList"

		@ 10,10  Say STR0033  Pixel //"Tipo Usu."
		@ 10,40 Combobox M->TBQ_TIPUSU Items aTipo Size 80,7 OF oDlg7 On Change (cCodUsu:=Space(Len(M->TBQ_USUARI)),cNomUsu:='',fCriaCheck( oDlg7 ) ) When lTipUsu  Pixel

		fCriaCheck( oDlg7 )

		Define SButton From 60, 120 Type 1 Enable Of oDlg7 Action ( nExec:=1,oDlg7:End() )
		Define SButton From 60, 150 Type 2 Enable Of oDlg7 Action ( nExec:=2,oDlg7:End() )

	Activate Msdialog oDlg7 Centered

	If nExec == 1
		DbSelectArea(cAliasCE)
		nRECNO := Recno()
		Reclock((cAliasCE),.f.)
		(cAliasCE)->TBQ_USUARI := cCodUsu
		If lTipUsu
			(cAliasCE)->TBQ_TIPUSU := M->TBQ_TIPUSU
		EndIf
		MsUnlock(cAliasCE)
		DbGotop()
		oMARK:oBROWSE:Refresh()
		DbGoto(nRECNO)
	EndIf

	DbSelectArea(cAliasCE)

Return

/*/


Ŀ
Funcao     Sg210Usu  Autor  Rafael Diogo Richter   Data 22/09/2005
Ĵ
Descrio  Consistencia do Usuario/Executante                    	  
Ĵ
Retorno                                                               
ٱ


/*/
Static Function Sg210Usu( cCodUsu )

If !Empty( cCodUsu )

	If M->TBQ_TIPUSU == '1'
		DbSelectArea( "QAA" )
		DbSetOrder(1)
		If !DbSeek( xFilial( "QAA" ) + cCodUsu )
			Help( " ", 1, "REGNOIS" )
			Return .F.
		Else
			cNomUsu	 := QAA->QAA_NOME
			lRefresh := .T.
		EndIf
	Else
		DbSelectArea( "SRA" )
		DbSetOrder(1)
		If !DbSeek( xFilial("SRA") + ALLTRIM( cCodUsu ) )
			Help( " ", 1, "REGNOIS" )
			Return .F.
		Else
			cNomUsu := SRA->RA_NOME
			lRefresh := .T.
		EndIf
	EndIf

	If Type( "oGetNom" ) == "O"

		oGetNom:cText := cNomUsu
		oGetNom:Refresh()

	EndIf

EndIf

Return

/*/


Ŀ
Funcao     Sg210Inv  Autor  Rafael Diogo Richter   Data 22/09/2005
Ĵ
Descrio  Inverte Marcadores                                    	  
Ĵ
Retorno                                                               
ٱ


/*/
Function Sg210Inv()
Local nREG := (cAliasCE)->(Recno())

DbSelectArea(cAliasCE)
DbGotop()
While !Eof()
   (cAliasCE)->TBQ_OK := IIF(TBQ_OK == "  ",cMARCA,"  ")
   If Empty((cAliasCE)->TBQ_OK)
      (cAliasCE)->TBQ_USUARI := Space(Len(TBQ->TBQ_USUARI))
   EndIf
   DbSkip()
End

(cAliasCE)->(DbGoto(nREG))
lREFRESH := .T.
Return Nil

/*/


Ŀ
Funcao     Sg210Inv  Autor  Rafael Diogo Richter   Data 22/09/2005
Ĵ
Descrio  Consistencia final dos CheckLists e Respostas         	  
Ĵ
Retorno                                                               
ٱ


/*/
Function SGAA210Efi()
Dbselectarea(cAliasCE)
Dbgotop()
While !Eof()
   If !Empty((cAliasCE)->TBQ_OK)
      Dbselectarea("TBD")
      Dbsetorder(1)
      If Dbseek(xfilial("TBD")+(cAliasCE)->TBQ_CHKLIS)
         If TBD->TBD_TIPOPC <> "1"
            Dbselectarea(cAliasOC)
            Dbsetorder(1)
            If Dbseek((cAliasCE)->TBQ_CHKLIS)
               lREPOS := .F.
               While !Eof() .And. (cAliasOC)->TBE_CODCHK = (cAliasCE)->TBQ_CHKLIS
                  If !Empty((cAliasOC)->TBE_OK)
                     lREPOS := .T.
                     Exit
                  Endif
                  Dbskip()
               End

               If !lREPOS
                  Msginfo(STR0022+" "+(cAliasCE)->TBQ_CHKLIS,STR0023)  //"Nao foi selecionado as respostas das opcoes do CheckList"##"No Conformidade"
                  Return .f.
               Endif
            Else
               Msginfo(STR0022+" "+(cAliasCE)->TBQ_CHKLIS,STR0023)  //"Nao foi selecionado as respostas das opcoes do CheckList"##"No Conformidade"
               Return .f.
            Endif
         Endif
      Endif
   Endif
   Dbselectarea(cAliasCE)
   Dbskip()
End
Return .t.

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 29/11/2006
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaSGA                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
Local lPyme := Iif(Type("__lPyme") <> "U",__lPyme,.F.)
Local aRotina :=	{{STR0001,"PesqBrw" , 0, 1},; //"Pesquisar"
                   {STR0002,"NGCAD01", 0, 2},; //"Visualizar"
                   {STR0003,"Sg210Fin", 0, 4},; //"Finalizar"
                   {STR0004,"Sg210Chk", 0, 4},; //"CheckList"
                   {STR0024,"Sg210Exc", 0, 5},; //"Cancelar"
                   {STR0006,"Sg210Leg", 0, 6},; //"Legenda"
                   { STR0026, "Sg210Hist"	, 0 , 3 }} //"Histrico"


If !lPyme
   AAdd( aRotina, { STR0015, "MsDocument", 0, 4 } )  //"Conhecimento"
EndIf

Return aRotina

/*/


Ŀ
Funcao    SG210BCON  Autor Felipe Nathan Welter    Data  20/01/10 
Ĵ
Descrio Funcionalidade do botao do conhecimento                     |
Ĵ
Retorno   Nenhum                                                      
ٱ


/*/
Static Function SG210BCON()
Local lTipQue := NGCADICBASE( "TBQ_TIPQUE" , "A" , "TBQ" , .F. )

dbSelectArea("TBQ")
dbSetOrder(01)
If dbSeek(xFilial("TBQ") + (cAliasCE)->TBQ_ORDEM + (cAliasCE)->TBQ_PLANO + If(lTipQue,(cAliasCE)->TBQ_TIPQUE,"") + (cAliasCE)->TBQ_CHKLIS)
	MsDocument('TBQ',Recno(),4)
Else
	MsgInfo(STR0025)  //"Nenhum CheckList selecionado."
EndIf

Return .T.

/*/


Ŀ
Funo      Sg210GChk   Autor  Rafael Diogo Richter   Data 15/05/2009
Ĵ
Descrio Funcao para gravar os CheckLists.                              
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/
Function Sg210GChk()
Local lTipUsu := NGCADICBASE("TBQ_TIPUSU","A","TBQ",.F.)
Local lTipQue := NGCADICBASE("TBQ_TIPQUE","A","TBQ",.F. )

dbSelectArea("TBR")
dbSetOrder(1)
dbSeek(xFilial("TBR")+TBN->TBN_CODORD+TBN->TBN_CODPLA)
While !Eof() .And. TBR->TBR_FILIAL == xFilial("TBR") .And. TBR->TBR_ORDEM == TBN->TBN_CODORD .And.;
		TBR->TBR_PLANO == TBN->TBN_CODPLA

	Reclock("TBR",.F.)
	dbDelete()
	MsUnlock("TBR")
	dbSelectArea("TBR")
	dbSkip()
End

dbSelectArea(cAliasCE)
dbgotop()
While !Eof()
   If !Empty((cAliasCE)->TBQ_OK)
      Dbselectarea("TBD")
      Dbsetorder(1)
      If Dbseek(xfilial("TBD")+(cAliasCE)->TBQ_CHKLIS)
         If TBD->TBD_TIPOPC <> "1"
            dbSelectArea(cAliasOC)
            dbSetOrder(1)
            dbseek((cAliasCE)->TBQ_CHKLIS)
				While !Eof() .And. (cAliasOC)->TBE_CODCHK = (cAliasCE)->TBQ_CHKLIS
					If !Empty((cAliasOC)->TBE_OK)
						dbSelectArea("TBR")
						dbSetOrder(1)
						If !dbSeek(xFilial("TBR")+TBN->TBN_CODORD+TBN->TBN_CODPLA+(cAliasOC)->TBE_CODCHK+(cAliasOC)->TBE_CODOPC)
							Reclock("TBR",.T.)
						Else
							Reclock("TBR",.F.)
						EndIf
						TBR->TBR_FILIAL	:= xFilial("TBR")
						TBR->TBR_ORDEM	:= TBN->TBN_CODORD
						TBR->TBR_PLANO	:= TBN->TBN_CODPLA
						TBR->TBR_CHKLIS	:= (cAliasOC)->TBE_CODCHK
						TBR->TBR_OPCAO	:= (cAliasOC)->TBE_CODOPC
						TBR->TBR_RESPOS	:= (cAliasOC)->TBE_RESPOS
						If lTipUsu
							TBR->TBR_TIPUSU   := (cAliasCE)->TBQ_TIPUSU
						EndIf
						TBR->TBR_USUARI	:= (cAliasCE)->TBQ_USUARI
						TBR->TBR_TIPRES	:= (cAliasOC)->TBE_TIPRES
						TBR->TBR_OPERAD	:= (cAliasOC)->TBE_CONDOP
						TBR->TBR_CONDIN	:= (cAliasOC)->TBE_CONDIN
						TBR->TBR_OK		:= (cAliasOC)->TBE_OK
						MsUnlock("TBR")
					EndIf
					dbSelectArea(cAliasOC)
					dbSkip()
     			End
         EndIf
      EndIf
   Else
		dbSelectArea("TBR")
		dbSetOrder(1)
		dbSeek(xFilial("TBR")+TBN->TBN_CODORD+TBN->TBN_CODPLA+(cAliasCE)->TBQ_CHKLIS)
		While !Eof() .And. TBR->TBR_FILIAL == xFilial("TBR") .And. TBR->TBR_ORDEM == TBN->TBN_CODORD .And.;
				TBR->TBR_PLANO == TBN->TBN_CODPLA .And. TBR->TBR_CHKLIS == (cAliasCE)->TBQ_CHKLIS

			Reclock("TBR",.F.)
			dbDelete()
			MsUnlock("TBR")

			dbSelectArea("TBR")
			dbSkip()
		End
   EndIf
   dbSelectArea("TBQ")
   dbSetOrder(1)
   If dbSeek(xFilial("TBQ")+TBN->TBN_CODORD+TBN->TBN_CODPLA+If(lTipQue,(cAliasCE)->TBQ_TIPQUE,"")+(cAliasCE)->TBQ_CHKLIS)
	   Reclock("TBQ",.F.)
	   TBQ->TBQ_OK			:= (cAliasCE)->TBQ_OK
	   TBQ->TBQ_USUARI	:= (cAliasCE)->TBQ_USUARI
		If lTipUsu
			TBQ->TBQ_TIPUSU   := (cAliasCE)->TBQ_TIPUSU
		EndIf
	   MsUnlock("TBQ")
	EndIf

	dbSelectArea(cAliasCE)
	dbSkip()
End

Return .T.

/*/


Ŀ
Funo      Sg210Exc    Autor  Rafael Diogo Richter   Data 27/05/2009
Ĵ
Descrio Funcao para cancelar a Ordem de Simulacao                      
Ĵ
 Uso       SigaSGA                                                       
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    F.O    Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


/*/
Function Sg210Exc()

dbSelectArea("TBN")
Reclock("TBN",.F.)
TBN->TBN_SITUACA := "1"
TBN->TBN_TERMINO := "1"
TBN->TBN_DATFIN := dDataBase
MsUnlock("TBN")

Return .T.
/*/


Ŀ
Funo    fCorLeg        Autor Jackson Machado         Data 05/04/2013
Ĵ
Descrio  Define o sinaleiro para a situacao das ordens de servico       
Ĵ
Uso        SGAA210                                                        
ٱ

/*/
Static Function fCorLeg()

Local aCores := { 	{ "TBN->TBN_SITUAC == '2'"    ,  'BR_VERMELHO'	},;  //Pendente
				 	{ "TBN->TBN_SITUAC == '3'"    ,  'BR_VERDE'	    },;  //Finalizada
					{ "TBN->TBN_SITUAC == '1'"    ,  'BR_AZUL'  	} }  //Cancelada

Return aCores
/*/


Ŀ
Funo     SG210RLEG     Autor Jackson Machado         Data 05/04/2013
Ĵ
Descrio  Cria uma janela contendo a legenda da mBrowse                  
Ĵ
Uso        SGAA210                                                        
ٱ

/*/
Function SG210RLEG()

BrwLegenda(cCadastro,STR0006,{{"BR_VERMELHO" ,OemToAnsi(STR0029)},;//"Pendente"
								{"BR_VERDE",OemToAnsi(STR0030)},;    //"Finalizada"
                              	{"BR_AZUL" ,OemToAnsi(STR0031)}})    //"Cancelada"

Return .T.
/*/


Ŀ
Funo    Sg210Hist      Autor Jackson Machado         Data 05/04/2013
Ĵ
Descrio  Monta um browse com as ordens de inspecao finalizadas.         
Ĵ
Sintaxe    Sg210Hist( cALIAS, nREG, nOPCX )                               
Ĵ
Parametros Arg1 - Alias da tabela                                         
           Arg2 - Numero do registro                                      
           Arg3 - Opcao escolhida                                         
Ĵ
Retorno    Booleano                                                       
Ĵ
Uso        SGAA210                                                        
ٱ

/*/
//---------------------------------------------------------------------
/*/{Protheus.doc} Sg210Hist
Monta um browse com as ordens de inspecao finalizadas.
@parameter
	Arg1 - Alias da tabela
	Arg2 - Numero do registro
	Arg3 - Opcao escolhida

@return
Booleano

@author Jackson Machado
@since 05/04/2013
@version 1.0
/*/
//---------------------------------------------------------------------
Function Sg210Hist( cALIAS, nREG, nOPCX )
Local nRecno := TBN->( Recno() )
Local aOldRotina := Aclone(aRotina)
Local aTBNIndex := {}
Local cFilterOld := TBN->( dbFilter() )
Private bBrwFiltra := {|| Nil }

aRotina :=	{ { STR0001, "AxPesqui"	, 0 , 1 },; //"Pesquisar"
              { STR0002, "NGCAD01"	, 0 , 2 },; //"Visualizar"
              { STR0032, "SG210REA"	, 0 , 4 },; //"Reabrir"
              { STR0006, "SG210RLEG" , 0 , 8 } } //"Legenda"

Begin Sequence

	//Ŀ
	//Filtra somente ordens de inspecao finalizadas e canceladas
	//
	dbSelectArea("TBN")
	Set Filter To

	cFiltraRh := 'TBN_TERMIN != "2" .and.'
	cFiltraRh += 'TBN_FILIAL == "'+xFilial("TBN")+'"'
	bBrwFiltra := { || FilBrowse("TBN",@aTBNIndex,@cFiltraRH) }
	Eval( bBrwFiltra )

	TBN->(dbGoTop())

	mBrowse(6,1,22,75,"TBN",,,,,,fCorLeg() )

	//Ŀ
	//Filtra somente as ordens de inspecao pendentes
	//
	dbSelectArea("TBN")
	Set Filter to &(cFilterOld)
	//Eval( bBrwFiltra )
	dbGoTo(nRecno)

End Sequence

aRotina := Aclone(aOldRotina)

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} Sg210Rea
Reabre a O.S. setada no browse

@return

@author Jackson Machado
@since 05/04/2013
@version 1.0
/*/
//---------------------------------------------------------------------
Function Sg210Rea()

	dbSelectArea( "TBN" )
	RecLock( "TBN" )
	TBN->TBN_SITUACA := "3"
	TBN->TBN_TERMINO := "2"
	TBN->TBN_DATFIN  := STOD("")
	TBN->TBN_DATINR  := STOD("")
	TBN->( MsUnLock() )

Return
//---------------------------------------------------------------------
/*/{Protheus.doc} fNewQuest
Marcao quando novo questionrio

@return

@author Jackson Machado
@since 05/04/2013
@version 1.0
/*/
//---------------------------------------------------------------------
Static Function fNewQuest()

	Private cQuestSGA := (cAliasCE)->TBQ_CHKLIS //Variavel que identifica qual o questionrio

	dbSelectArea( "TJ1" )
	dbSetOrder( 2 )
	If dbSeek( xFilial( "TJ1" ) + (cAliasCE)->TBQ_CHKLIS + (cAliasCE)->TBQ_ORDEM  )

		RegToMemory( "TJ1" , .F. )

		MDTB001C( "TJ1" , TJ1->( Recno() ) , 4 )

	Else

		aRotSetOpc( "TJ2" , 0 , 3 )

	EndIf

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} fCriaCheck
Criao da tela de executante de checklist.

@author Cau Girardi Petri
@since 10/03/2022

@param nNomUsu	, Numrico, Nome do Usurio.
@param cCodUsu	, String  , Cdigo do Usurio.
@param lAccsUsu	, Lgico  , Variavel logica para verificar se troca F3.
@param oDlg7	, Objeto  , Tela pai.

@return NIL
/*/
//-------------------------------------------------------------------

Static Function fCriaCheck( oDlg7 )

	oSay := TSay():New( 25 , 10 , { || Oemtoansi( STR0019 ) }, oDlg7 , , , , , , .T. , , , 30 , 10 ) //"Executante "

	oGetNum  := TGet():New( 25 , 40 , { |u| IIf( PCount() > 0, cCodUsu := u, cCodUsu ) },;
				oDlg7 , 80 , 7 , '@!', { || Sg210Usu( cCodUsu ) }, , , , , , .T. , , , { || lAccsUsu } , , , , , , IIf( M->TBQ_TIPUSU == "1" , "QAA" , "SRA" ) , , , , , .T. ,)

	oSay := TSay():New( 40 , 10 , { || Oemtoansi( STR0020 ) }, oDlg7 , , , , , , .T. , , , 30 , 10 ) //"Nome "

	oGetNom  := TGet():New( 40 , 40 , { |u| IIf( PCount() > 0, cNomUsu := u, cNomUsu ) },;
										 oDlg7 , 140 , 7 , , , , , , , , .T. , , , { || .F. } )

Return
