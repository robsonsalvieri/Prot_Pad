#INCLUDE "MNTR545.ch"
#INCLUDE "PROTHEUS.CH"

//-----------------------------------------------------------------------
/*/{Protheus.doc} MNTR545
Relatorio de Divergncias nas Inspees de Pneus.

@author Marcos Wagner Junior
@since 14/12/2006

@param aPergA, Array, contedo das perguntas
@return boolean
/*/
//-----------------------------------------------------------------------
Function MNTR545(aPergA)

	// Armazena variaveis p/ devolucao (NGRIGHTCLICK)
	Local aNGBEGINPRM := NGBEGINPRM()
	Local lRet := .T.

	WNREL      := "MNTR545"
	LIMITE     := 132
	cDESC1     := STR0001 //"O relatrio apresentar as divergncias "
	cDESC2     := STR0002 //"encontradas na comparao da inspeo atual "
	cDESC3     := STR0003 //"com a situao da estrutura dos pneus no sistema."
	cSTRING    := "TR9"

	Private NOMEPROG := "MNTR545"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0004,1,STR0005,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private TITULO   := STR0006 //"Divergncias Encontradas nas Inspees de Pneus"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2
	Private aVETINR := {}
	Private cPERG := "MNR545"
	Private aPerg :={}
	Private aImprime := {}

	Private nSizeFil := If(FindFunction("FWSizeFilial"),FwSizeFilial(),Len(ST9->T9_FILIAL))

	Default aPergA	 := {}

	Pergunte(cPERG,.F.)
	// Envia controle para a funcao SETPRINT
	If Len(aPergA) == 0
		wnrel:=SetPrint(cString,wnrel,cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	Else
		//Atribui os contedos do pergunte quando vir na chamada da funo
		MV_PAR01 := aPergA[1]
		MV_PAR02 := aPergA[2]
		MV_PAR03 := aPergA[3]
		wnrel:=SetPrint(cString,wnrel,,titulo,cDesc1,cDesc2,cDesc3,.F.,"")
	EndIf

	If nLastKey == 27

		dbSelectArea("TR9")
		Return
	EndIf

	SetDefault(aReturn,cString)
	RptStatus({|lEND| lRet := MNTR545IMP(@lEND,wnrel,titulo,TAMANHO)},STR0010,STR0011) //"Aguarde..."###"Processando Registros..."

	dbselectarea("TR9")

	// Devolve variveis armazenadas (NGRIGHTCLICK)
	NGRETURNPRM(aNGBEGINPRM)

Return lRet

/*/


Ŀ
Funo    |MNR545IMP | Autor  Marcos Wagner Junior   Data 14/12/2006
Ĵ
Descrio Chamada do Relatrio                                        
Ĵ
 Uso      MNTR545                                                     
ٱ


/*/
Function MNTR545IMP(lEND,WNREL,TITULO,TAMANHO)
	Local nCiclo := 0
	Local nX := 0
	Local lImprime := .f.
	Local nTotInc  := 0
	Local nIncEst := 0, nIncLoc := 0, nIncDup := 0, nIncVei := 0, nIncSul := 0
	Local lRet := .T.
	Private cRODATXT := ""
	Private nCNTIMPR := 0
	Private li := 80 ,m_pag := 1
	Private cNomeOri := "  "
	Private nTotPneu := 0
	Private nAcertados := 0
	Private nCont := 0
	Private nGerPneu := 0
	Private nGerAcer := 0
	Private cNomFil, cBem, nI
	Private nIncons := 0
	Private aPneusQry, aPosicQry,aSulcoQry, aPNs := {}
	Private lCicIgual := .F.

	nTIPO  := IIf(aReturn[4]==1,15,18)

	CABEC1 := STR0013 //"Ciclo  Veculo           Posio  Sulco    N.Fogo      Dt. Insp.  Dt. Digit.  Hora  Km.        Veculo           Posio  Sulco   Filial                          Divergncia"
	CABEC2 := ""



	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9       0         1         2
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678903456789012345678901234567890
	***************************************************************************************************************************************************************************************************************************
	Relatorio de Pneus
	***************************************************************************************************************************************************************************************************************************
	--------------------------------- Inspeo Atual -----------------------------------------     -------------------------------------------------------- Situao SGF ---------------------------------------------------------

	Filial.: xxxxxxxxxxxx - xxxxxxxxxxxxxxx

	Ciclo  Veculo           Posio  Sulco    N.Fogo      Dt. Insp.  Dt. Digit.  Hora  Km.        Veculo           Posio  Sulco   Filial                          Divergncia

	x      xxxxxxxxxxxxxxxx  xxxxxx   xxx,xx   xxxxxxxxxx  99/99/99   99/99/99    99:99 999999     xxxxxxxxxxxxxxxx  xxxxxx   xxx,xx  xxxxxxxxxxxx - xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	x      xxxxxxxxxxxxxxxx  xxxxxx   xxx,xx   xxxxxxxxxx  99/99/99   99/99/99    99:99 999999     xxxxxxxxxxxxxxxx  xxxxxx   xxx,xx  xxxxxxxxxxxx - xxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	*/

	cAliasQry := GetNextAlias()
	cQuery := " SELECT TRC.*,TR9.TR9_FILIAL,TR9.TR9_ORDEM,TR9.TR9_PLANO,TR9.TR9_CICLO,TR9.TR9_STATUS,"
	cQuery += " TR9.TR9_FROTA,TR9.TR9_DTINSP,TR9.TR9_DTDIGI,TR9.TR9_HRDIGI,TR9.TR9_KMATU,STJ1.TJ_CODBEM,"
	cQuery += " (select count(*) from "+RetSqlName("TRC")+" where trc_filial=trc.trc_filial and trc_ordem=trc.trc_ordem and trc_plano=trc.trc_plano and trc_ciclo=trc.trc_ciclo and D_E_L_E_T_ <> '*')eixos "
	cQuery += " FROM "+RetSqlName("TRC")+" TRC ,"+RetSqlName("TR9")+" TR9 "
	cQuery += " INNER JOIN " + RetSqlName('STJ') + " STJ1 ON "
	cQuery += NGMODCOMP('STJ','TR9','=', , ,'STJ1.TJ_FILIAL', 'TR9.TR9_FILIAL')
	cQuery += " AND TR9.TR9_ORDEM = STJ1.TJ_ORDEM "
	cQuery += " WHERE TRC.TRC_CICLO IN(SELECT MAX(TR9.TR9_CICLO)"
	cQuery += " FROM "+RetSqlName("TR9")+" TR9,"+RetSqlName("STJ")+" STJ "
	cQuery += " WHERE TR9.TR9_ORDEM = TRC.TRC_ORDEM"
	cQuery += " AND TR9.TR9_FILIAL  >= '"+MV_PAR01+"' AND TR9.TR9_FILIAL <= '" +MV_PAR02+"'
	cQuery += " AND TR9.TR9_DTINSP >=  '"+DTOS(MV_PAR03)+"'"
	cQuery += " AND TR9.TR9_FILIAL = TRC.TRC_FILIAL"
	cQuery += " AND TR9.D_E_L_E_T_ <> '*' "
	cQuery += " AND TR9.TR9_FILIAL = STJ.TJ_FILIAL AND TR9.TR9_ORDEM = STJ.TJ_ORDEM AND "
	cQuery += " TR9.TR9_PLANO=STJ.TJ_PLANO AND STJ.TJ_TERMINO='N' AND STJ.TJ_SITUACA<>'C' AND STJ.D_E_L_E_T_<>'*' "
	cQuery += " GROUP BY TR9.TR9_ORDEM)"
	cQuery += " AND TRC.D_E_L_E_T_ <> '*' "
	cQuery += " AND TR9.D_E_L_E_T_ <> '*'"
	cQuery += " AND TR9.TR9_ORDEM = TRC.TRC_ORDEM"
	cQuery += " AND TR9.TR9_CICLO = TRC.TRC_CICLO"
	cQuery += " ORDER BY TR9.TR9_FILIAL,TR9.TR9_FROTA"
	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

	SetRegua(LastRec())

	If !Eof()
		aPneus    := {TRC->TRC_NFOGO1,TRC->TRC_NFOGO2,TRC->TRC_NFOGO3,TRC->TRC_NFOGO4,;
		TRC->TRC_NFOGO5,TRC->TRC_NFOGO6,TRC->TRC_NFOGO7,TRC->TRC_NFOGO8,;
		TRC->TRC_NFOGO9,TRC->TRC_NFOG10}

		DbGoTop()
		While !Eof()

			aPneusQry := {(cAliasQry)->TRC_NFOGO1,(cAliasQry)->TRC_NFOGO2,(cAliasQry)->TRC_NFOGO3,(cAliasQry)->TRC_NFOGO4,;
			(cAliasQry)->TRC_NFOGO5,(cAliasQry)->TRC_NFOGO6,(cAliasQry)->TRC_NFOGO7,(cAliasQry)->TRC_NFOGO8,;
			(cAliasQry)->TRC_NFOGO9,(cAliasQry)->TRC_NFOG10}

			aPosicQry := {(cAliasQry)->TRC_LOCPN1,(cAliasQry)->TRC_LOCPN2,(cAliasQry)->TRC_LOCPN3,(cAliasQry)->TRC_LOCPN4,;
			(cAliasQry)->TRC_LOCPN5,(cAliasQry)->TRC_LOCPN6,(cAliasQry)->TRC_LOCPN7,(cAliasQry)->TRC_LOCPN8,;
			(cAliasQry)->TRC_LOCPN9,(cAliasQry)->TRC_LOCP10}

			cFil   := (cAliasQry)->TR9_FILIAL
			cOrdem := (cAliasQry)->TR9_ORDEM
			cPlano := (cAliasQry)->TR9_PLANO
			cCic   := (cAliasQry)->TR9_CICLO

			IncRegua()
			If lEnd
				@ Prow()+1,001 PSay STR0014 //"CANCELADO PELO OPERADOR"
				Exit
			EndIf

			nI := 1
			aImprime := {}
			If (cAliasQry)->TRC_CICLO = '1'
				While nI < 11
					If !Empty(aPneusQry[nI])
						DbSelectArea("TQS")
						DbSetOrder(01)
						If DbSeek( FWxFilial('TQS', (cAliasQry)->TRC_FILIAL) + aPneusQry[nI] )
							nIncons := nIncons + MNR545DUP(aPosicQry[nI],aPneusQry[nI],nI,TQS->TQS_SULCAT, (cAliasQry)->TJ_CODBEM)//Verifica se pneu ja foi informado em outra inspecao
							nIncons := nIncons + MNR545EST(aPosicQry[nI],aPneusQry[nI],nI)//Verifica se pneu esta na estrutura do veiculo
							nIncons := nIncons + MNR545LOC(aPosicQry[nI],aPneusQry[nI],nI)//Verifica se pneu esta em local diferente do sistema
							nIncons := nIncons + MNR545SUL(aPosicQry[nI],aPneusQry[nI],nI)//Verifica se pneu esta com sulco maior que o registrado no sistema
							nIncons := nIncons + MNR545VEI(aPosicQry[nI],aPneusQry[nI],nI)//Verifica se pneu consta em outro veiculo
							nIncons := nIncons + MNR545FIL(aPneusQry[nI],aPosicQry[nI],nI)//Verifica se pneu esta em outra filial
							nIncons := nIncons + MNR545MED(TQS->TQS_MEDIDA,aPosicQry[nI],aPneusQry[nI])//Verifica se pneu tem a medida diferente dos demais do mesmo eixo
							nIncons := nIncons + MNR545FAM((cAliasQry)->TRC_FILIAL,aPneusQry[nI],aPosicQry[nI],nI)//Verifica se o pneus eh de familia diferente da estrutura para esta posicao
						Endif
						aADD(aPNs,{aPneusQry[nI]})
					Else
						If AllTrim((cAliasQry)->TRC_EIXO) == "RESERVA" .And. !Empty(aPosicQry[nI])
							MNR545RES(aPosicQry[nI],aPneusQry[nI],nI)
						EndIf
					Endif
					nI++
				End
				nTotInc += nIncons
			EndIf

			lCicIgual := .f.

			If (cAliasQry)->TRC_CICLO > '1'
				nIncons := 0
				nI	:= 1

				For nCiclo := 1 to Val((cAliasQry)->TRC_CICLO) - 1
					nEixo := 1
					cCiclo := AllTrim(Str(nCiclo)) + Space(Len(TRC->TRC_CICLO)-Len(AllTrim(Str(nCiclo))))

					DbSelectArea("TRC")
					DbSetOrder(1)
					If DbSeek((cAliasQry)->TRC_FILIAL+(cAliasQry)->TRC_ORDEM+(cAliasQry)->TRC_PLANO+cCiclo+Str(nEixo,3))

						While nI < 11
							nIncEst := 0
							nIncLoc := 0
							nIncDup := 0
							nIncVei := 0
							nIncSul := 0
							If !Empty(aPneusQry[nI])
								DbSelectArea("TQS")
								DbSetOrder(01)
								If DbSeek( FWxFilial( 'TQS', (cAliasQry)->TRC_FILIAL ) + aPneusQry[nI] )
									nIncEst := MNR545EST(aPosicQry[nI],aPneusQry[nI],nI)//Verifica se pneu esta na estrutura do veiculo
									nIncLoc := MNR545LOC(aPosicQry[nI],aPneusQry[nI],nI)//Verifica se pneu esta em local diferente do sistema --posicional
									nIncDup := MNR545DUP(aPosicQry[nI],aPneusQry[nI],nI,TQS->TQS_SULCAT,(cAliasQry)->TJ_CODBEM)//Verifica se pneu ja foi informado em outra inspecao
									nIncVei := MNR545VEI(aPosicQry[nI],aPneusQry[nI],nI)//Verifica se pneu consta em outro veiculo
									nIncSul := MNR545SUL(aPosicQry[nI],aPneusQry[nI],nI)//Verifica se sulco eh maior que o do sistema
									nIncons := nIncons + MNR545FIL(aPneusQry[nI],aPosicQry[nI],nI)//Verifica se pneu esta em outra filial
									nIncons := nIncons + MNR545MED(TQS->TQS_MEDIDA,aPosicQry[nI],aPneusQry[nI])//Verifica se pneu tem a medida diferente dos demais do mesmo eixo
									nIncons := nIncons + MNR545FAM((cAliasQry)->TRC_FILIAL,aPneusQry[nI],aPosicQry[nI],nI)//Verifica se o pneus eh de familia diferente da estrutura para esta posicao
									nCicloIgual := MNR545CDUP() //VERIFICA SE EXISTEM 2 INSPECOES IGUAIS

									If nCicloIgual == 1 .And. ( nIncDup == 0 .And. nIncEst == 0 .And. nIncVei == 0 .And. nIncSul == 0 )
										lCicIgual := .T.
									Else
										nIncons += nIncEst + nIncLoc + nIncDup	+ nIncVei + nIncSul
									EndIf

									If nCicloIgual == 1 .And. nIncDup == 0 .And. (nIncEst == 1 .Or. nIncVei == 1) .And. nIncSul == 0
										MNR545ARR(aPosicQry[nI],aPneusQry[nI],9,,TQS->TQS_SULCAT,nI)//2 ciclos iguais com divergencia de pneu nao encontrado na estrutura
										nIncons += nIncEst + nIncLoc + nIncDup	+ nIncVei + nIncSul
									EndIf
									aADD(aPNs,{aPneusQry[nI]})
								EndIf
							Else
								If AllTrim((cAliasQry)->TRC_EIXO) == "RESERVA" .And. !Empty(aPosicQry[nI])
									MNR545RES(aPosicQry[nI],aPneusQry[nI],nI)
								EndIf
							EndIf
							nI++
						End
						nEixo++
						lCicIgual := .t.
						nTotInc += nIncons
					EndIf
				Next nCiclo
			Elseif nIncons = 0
				lCicIgual := .t.
			Endif

			If lCicIgual = .f. .or. nIncons > 0
				If	cNomeOri <> (cAliasQry)->TRC_FILIAL
					DbSelectArea("SM0")
					SM0->(DbSetOrder(1))
					If MsSeek(SM0->M0_CODIGO+(cAliasQry)->TRC_FILIAL)
						NgSomaLi(58)
						@ Li,000   Psay (cAliasQry)->TRC_FILIAL + " - " + SM0->M0_FILIAL
						NgSomaLi(58)
						NgSomaLi(58)
						@ Li,000   Psay STR0015 //"--------------------------------- Inspeo Atual -----------------------------------------     -------------------------------------------------------- Situao SGF -------------------------------------------------------"
						NgSomaLi(58)
						NgSomaLi(58)
					Endif
				EndIf

				DbSelectArea(cAliasQry)
				cNomeOri := (cAliasQry)->TRC_FILIAL

				For nX := 1 to Len(aImprime)
					@ Li,000   Psay aImprime[nX,1]
					@ Li,007   Psay aImprime[nX,2]
					@ Li,025   Psay aImprime[nX,3]
					@ Li,034   Psay aImprime[nX,12]
					@ Li,043   Psay aImprime[nX,4]
					@ Li,055   Psay stod( aImprime[nX,5] ) Picture "99/99/9999"
					@ Li,066   Psay stod( aImprime[nX,6] ) Picture "99/99/9999"
					@ Li,078   Psay aImprime[nX,7]
					@ Li,084   Psay aImprime[nX,14] Picture "@E 999999"

					@ Li,095   Psay aImprime[nX,8]
					@ Li,113   Psay aImprime[nX,9]
					@ Li,122   Psay aImprime[nX,13]

					dbSelectArea("SM0")
					SM0->(DbSetOrder(1))
					If MsSeek(SM0->M0_CODIGO+aImprime[nX,10])
						@ Li,130 Psay SubStr(aImprime[nX,10] + " - " + SM0->M0_FILIAL,1,30)
					Else
						@ Li,130 Psay aImprime[nX,10]
					Endif
					@ Li,162   Psay SubStr(aImprime[nX,11], 1, 60)
					NgSomaLi(58)
					lImprime := .t.
				Next
			Endif
			cOrd := (cAliasQry)->TR9_ORDEM
			cFrota := (cAliasQry)->TR9_FROTA
			DbSelectArea(cAliasQry)
			DbSkip()
			If cOrd <> (cAliasQry)->TR9_ORDEM
				dbSelectArea("TR9")
				dbSetOrder(01)
				dbSeek(cFIL+cORDEM+cPlano+cCic)
				If nTotInc == 0
					If !lImprime
						RecLock("TR9",.F.)
						TR9->TR9_STATUS := '3'
						MsUnlock("TR9")
					EndIf
				Else
					RecLock("TR9",.F.)
					TR9->TR9_STATUS := '2'
					MsUnlock("TR9")
				EndIf
				lImprime := .f.
				nTotInc  := 0
			EndIf
			If cFrota <> (cAliasQry)->TR9_FROTA
				vCompB := NGESTRU(cFrota)
				aPneuF := {}
				For nX := 1 To Len(vCompB)
					If NGIFDBSEEK("TQS",vCompB[nX],1,.f.)
						Aadd(aPneuF,{vCompB[nX],TQS->TQS_NUMFOG,NGSEEK("STC",cFrota+vCompB[nX]+"B",1,"TC_LOCALIZ")})
					EndIf
				Next nX
				For nX := 1 To Len(aPneuF)
					nPosPN := Ascan(aPNs,{|x| x[1] == aPneuF[nX,2]})
					If nPosPN = 0
						@ Li,000   Psay "*"
						@ Li,095   Psay cFrota
						@ Li,113   Psay aPneuF[nX,3]
						@ Li,162   Psay STR0026 + Space( 1 ) + AllTrim( aPneuF[nX,2] ) + Space( 1 ) + STR0025 + cFrota
						NgSomaLi(58)
					EndIf
				Next nX

				aPNs := {}
			EndIf

			dbSelectArea(cAliasQry)
		End
	Else
		MsgStop(STR0027) //'No h dados para imprimir no relatrio'
		lRet := .F.
	EndIf

	If lRet
		If !lImprime .And. !Empty((cAliasQry)->TR9_CICLO) .And. nTotInc == 0
			MsgInfo(STR0028+(cAliasQry)->TR9_CICLO+STR0029+TR9->TR9_STATUS) //'Status do ciclo X alterado para Y'
		Else
			RODA(nCNTIMPR,cRODATXT,TAMANHO)
		EndIf
	EndIf

	(cAliasQry)->(dbCloseArea())

	//Ŀ
	// Devolve a condicao original do arquivo principal             
	//
	RetIndex('TR9')
	Set Device To Screen
	If aReturn[5] == 1
		If lRet
			Set Printer To
			dbCommitAll()
			OurSpool(WNREL)
		EndIf
	EndIf
	MS_FLUSH()


Return lRet


/*/


Ŀ
Funo    |MNR545MED | Autor  Marcos Wagner Junior   Data 18/12/2006
Ĵ
Descrio Verifica se a medida do pneu eh igual aos outros pneus do   
			 mesmo eixo																  
Ĵ
 Uso      MNTR545                                                     
ٱ


/*/
Function MNR545MED(cMedida,cPosicao,cNumfogo)

	Local nI := 1

	While nI < 11
		dbSelectArea("TQS")
		dbSetOrder(01)
		If !Empty(aPneusQry[nI])
			If dbSeek((cAliasQry)->TRC_FILIAL+aPneusQry[nI])
				If cMedida != TQS->TQS_MEDIDA
					MNR545ARR(cPosicao,cNumfogo,6,,TQS->TQS_SULCAT,nI)
					Return 1
				Endif
			Endif
		Endif
		nI++
	End

Return 0

/*/


Ŀ
Funo    |MNR545FIL | Autor  Marcos Wagner Junior   Data 18/12/2006
Ĵ
Descrio Verifica se o pneu	consta em outra filial						  
Ĵ
 Uso      MNTR545                                                     
ٱ


/*/
Function MNR545FIL(cNumfogo,cPosicao,nI)

	Local cQryAlias := GetNextAlias()
	Local cQryTQS := ""

	Local cCodBem := ""

	//--------------------------------------------------
	// Busca o Cdigo do Bem pelo Nmero do Fogo
	//--------------------------------------------------
	// SELECT
	cQryTQS := "SELECT TQS.TQS_CODBEM "
	// FROM
	cQryTQS += "FROM " + RetSQLName("TQS") + " TQS "
	// WHERE                                                      \
	cQryTQS += "WHERE TQS.TQS_NUMFOG = " + ValToSQL(cNumfogo) + " AND TQS.D_E_L_E_T_ <> '*' "
	// ORDER BY
	cQryTQS += "ORDER BY TQS.TQS_CODBEM "

	// Utiliza a Tabela
	cQryTQS := ChangeQuery(cQryTQS)
	dbUseArea(.T., "TOPCONN", TCGenQry(,,cQryTQS), cQryAlias, .F., .T.)

	// Recebe o Cdigo do Bem
	dbSelectArea(cQryAlias)
	dbGoTop()
	If !Eof()
		cCodBem := (cQryAlias)->TQS_CODBEM
	EndIf
	dbCloseArea()

	//--------------------------------------------------
	// Verifica Inconsistncia com o Bem
	//--------------------------------------------------
	If !Empty(cCodBem)
		dbSelectArea("ST9")
		dbSetOrder(16)
		If dbSeek(cCodBem+"A")
			While !Eof() .And. cCodBem == ST9->T9_CODBEM .And. ST9->T9_SITBEM == "A"
				If FWxFilial('ST9', (cAliasQry)->TRC_FILIAL) != ST9->T9_FILIAL
					MNR545ARR(cPosicao,cNumfogo,5,,,nI)
					Return 1
				EndIf

				dbSelectArea("ST9")
				dbSkip()
			End
		EndIf
	EndIf

Return 0

/*/


Ŀ
Funo    |MNR545VEI | Autor  Marcos Wagner Junior   Data 18/12/2006
Ĵ
Descrio Verifica se o pneu	consta em outro veiculo						  
Ĵ
 Uso      MNTR545                                                     
ٱ


/*/
Function MNR545VEI(cPosicao,cNumfogo,nI)
	Local aArea := GetArea()

	DbSelectArea("TR9")
	DbSetOrder(01)
	If DbSeek((cAliasQry)->TRC_FILIAL+(cAliasQry)->TRC_ORDEM)
		DbSelectArea("TQS")
		DbSetOrder(01)
		If DbSeek((cAliasQry)->TRC_FILIAL+cNumfogo)
			If TR9->TR9_PLACA != TQS->TQS_PLACA .And. !Empty(TQS->TQS_PLACA)
				MNR545ARR(cPosicao,cNumfogo,4,,TQS->TQS_SULCAT,nI)
				RestArea(aArea)
				Return 1
			Endif
		Endif
	Endif
	RestArea(aArea)

Return 0

/*/


Ŀ
Funo    |MNR545SUL | Autor  Marcos Wagner Junior   Data 18/12/2006
Ĵ
Descrio Verifica se o pneu eh maior que o registrado no sistema	  
Ĵ
 Uso      MNTR545                                                     
ٱ


/*/
Function MNR545SUL(cPosicao,cNumfogo,nI)

	nMedida := MNTR545MME(nI)
	If nMedida > TQS->TQS_SULCAT
		MNR545ARR(cPosicao,cNumfogo,3,nMedida,TQS->TQS_SULCAT,nI)
		Return 1
	Endif

Return 0

/*/


Ŀ
Funo    |MNR545LOC | Autor  Marcos Wagner Junior   Data 18/12/2006
Ĵ
Descrio Verifica se o pneu esta posicionado em local diferente do	  
          informado.																  
Ĵ
 Uso      MNTR545                                                     
ٱ


/*/
Function MNR545LOC(cPosicao,cNumfogo,nI)
	Local aArea := GetArea()

	If TQS->TQS_POSIC != cPosicao
		If AllTrim((cAliasQry)->TRC_EIXO) == "RESERVA" .AND. TQS->TQS_EIXO = "R"
			RestArea(aArea)
			Return 0
		EndIf
		MNR545ARR(cPosicao,cNumfogo,2,,TQS->TQS_SULCAT,nI)
		RestArea(aArea)
		Return 1
	Endif
	RestArea(aArea)
Return 0
/*/


Ŀ
Funo    |MNR545EST | Autor  Marcos Wagner Junior   Data 18/12/2006
Ĵ
Descrio Verifica se o pneu esta com a placa em branco 				  
Ĵ
 Uso      MNTR545                                                     
ٱ


/*/
Function MNR545EST(cPosicao,cNumfogo,nI)
	Local aArea := GetArea()

	If Empty(TQS->TQS_PLACA)
		MNR545ARR(cPosicao,cNumfogo,1,,TQS->TQS_SULCAT,nI)
		RestArea(aArea)
		Return 1
	Endif
	RestArea(aArea)
Return 0

/*/


Ŀ
Funo    |MNR545FAM | Autor  Marcos Wagner Junior   Data 18/12/2006
Ĵ
Descrio Verifica se a familia do pneu eh diferente da definida na	  
          estrutura para esta posicao.										  
Ĵ
 Uso      MNTR545                                                     
ٱ


/*/
Function MNR545FAM(cFilNG,cPneu,cPosicao,nI)
	Local aArea := GetArea()

	dbSelectArea("ST9")
	dbSetOrder(01)
	If dbSeek(cFilNG+cPneu)
		dbSelectArea("TQ1")
		dbSetOrder(01)
		If dbSeek(ST9->T9_FILIAL+ST9->T9_CODFAMI+ST9->T9_TIPMOD)
			If ST9->T9_CODFAMI != TQ1->TQ1_DESENH
				DbSelectArea("TQS")
				DbSetOrder(1)
				DbSeek(ST9->T9_FILIAL+ST9->T9_CODBEM)
				MNR545ARR(cPosicao,cPneu,7,,TQS->TQS_SULCAT,nI)
				RestArea(aArea)
				Return 1
			Endif
		Endif
	Endif
	RestArea(aArea)
Return 0

/*/


Ŀ
Funo    |MNR545ARR | Autor  Marcos Wagner Junior   Data 18/12/2006
Ĵ
Descrio Alimenta Array de inconsistencias que sera impresso			  
Ĵ
 Uso      MNTR545                                                     
ٱ


/*/
Function MNR545ARR(cPosicao,cNumfogo,nDiverg,nSulcoIn,nSulcoSis,nI)

	Local cVeiculo,cDatains,cDatadig,cHoradig,cDiverg,cPosic := " ",cVeiSis := " ",nKmAtu := 0
	If nSulcoIn = Nil
		nSulcoIn := MNTR545MME(nI)
		//nSulcoIn := 0
	EndIf
	If nSulcoSis = Nil
		nSulcoSis := 0
	EndIf

	cVeiculo := (cAliasQry)->TR9_FROTA
	cDatains := (cAliasQry)->TR9_DTINSP
	cDatadig := (cAliasQry)->TR9_DTDIGI
	cHoradig := (cAliasQry)->TR9_HRDIGI
	nKmAtu   := (cAliasQry)->TR9_KMATU

	DbSelectArea("TQS")
	DbSetOrder(01)
	If DbSeek((cAliasQry)->TRC_FILIAL+cNumfogo)
		cPosic := TQS->TQS_POSIC
		If !Empty(TQS->TQS_PLACA)
			DbSelectArea("ST9")
			DbSetOrder(14)
			If DbSeek(TQS->TQS_PLACA)
				cVeiSis := ST9->T9_CODBEM
			Endif
		EndIf
	Endif

	If nDiverg = 1
		cDiverg := STR0016 //"Pneu no encontrado na estrutura do veculo"
	Elseif nDiverg = 2
		cDiverg := STR0017 //"Pneu em local diferente do informado"
	Elseif nDiverg = 3
		cDiverg := STR0018 //"Pneu com sulco maior do que o registrado no sistema"
	Elseif nDiverg = 4
		cDiverg := STR0019 //"Pneu consta em outro veculo"
	Elseif nDiverg = 5
		cDiverg := STR0020 //"Pneu consta em outra filial"
	Elseif nDiverg = 6
		cDiverg := STR0021 //"Pneu com medida diferente dos demais pneus do mesmo eixo"
	Elseif nDiverg = 7
		cDiverg := STR0022 //"Pneu de famlia diferente da defin. na estrut.para posio"
	Elseif nDiverg = 8
		cDiverg := STR0012 //"Pneu consta na Inspeo de outra frota"
	Elseif nDiverg = 9
		cDiverg := STR0023 //"2 Ciclos iguais, atualize a estrutura"
	Elseif nDiverg = 10
		cDiverg := STR0024 //"Step no foi informado"
	Elseif nDiverg = 11
		cDiverg := STR0030 //"Pneu consta na inspeo de outra Ordem de Servio"
	Endif

	aADD(aImprime,{(cAliasQry)->TRC_CICLO,cVeiculo,cPosicao,cNumfogo,cDatains,;
	cDatadig,cHoradig,cVeiSis,cPosic,(cAliasQry)->TRC_FILIAL,cDiverg,nSulcoIn,nSulcoSis,nKmAtu})

Return .t.
/*/


Ŀ
Funo    |MNTR545MME| Autor  Marcos Wagner Junior   Data 18/12/2006
Ĵ
Descrio Das 3 medidas de sulco informadas, pega a menor delas    	  
Ĵ
 Uso      MNTA750                                                     
ٱ


/*/
Function MNTR545MME(nI)
	Local nMedida := 10000
	Local i

	cMed1 := "(cAliasQry)->TRC_PN"+AllTrim(Str(nI))+"M1"
	cMed2 := "(cAliasQry)->TRC_PN"+AllTrim(Str(nI))+"M2"
	cMed3 := "(cAliasQry)->TRC_PN"+AllTrim(Str(nI))+"M3"

	For i:= 1 to 3
		cMedida := "cMed"+Str(i,1)
		nMedMin := &cMedida
		nMedida := MIN(nMedida,&nMedMin)
	Next

Return nMedida

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNR545DUP
Verifica se o pneu ja foi informado em outra inspecao

@type Function

@author Evaldo Cevinscki Jr.
@since 23/11/2006
@param cPosicao, caractere, posio do Pneu na estrutura
@param cNumFogo, caractere, nmero de fogo do pneu
@param nI, numerico, ciclo de inspeo
@param nSulco, numerico, sulco do Pneu
@param cBemPai, caractere, Bem pai do Pneu na estrutura

@return numerico, retorna sempre 0  
/*/ 
//------------------------------------------------------------------------------
Function MNR545DUP( cPosicao, cNumfogo, nI, nSulco, cBemPai )
	Local aArea := GetArea()
	Local lDup := .f.

	cAliasQry3 := GetNextAlias()
	cQuery := " SELECT MAX(TRC.TRC_CICLO),TRC.TRC_FILIAL,TRC.TRC_ORDEM,TRC.TRC_PLANO, STJ.TJ_CODBEM "
	cQuery += " FROM "+RetSqlName("TRC")+" TRC,"+RetSqlName("STJ")+" STJ WHERE '"+cNumfogo+"' IN "
	cQuery += " (TRC.TRC_NFOGO1,TRC.TRC_NFOGO2,TRC.TRC_NFOGO3,TRC.TRC_NFOGO4,TRC.TRC_NFOGO5 "
	cQuery += " ,TRC.TRC_NFOGO6,TRC.TRC_NFOGO7,TRC.TRC_NFOGO8,TRC.TRC_NFOGO9,TRC.TRC_NFOG10) "
	cQuery += " AND TRC.TRC_FILIAL = STJ.TJ_FILIAL AND TRC.TRC_ORDEM = STJ.TJ_ORDEM AND "
	cQuery += " TRC.TRC_PLANO=STJ.TJ_PLANO AND STJ.TJ_TERMINO='N' AND STJ.TJ_SITUACA<>'C' AND TRC.D_E_L_E_T_<>'*' AND STJ.D_E_L_E_T_<>'*' "
	cQuery += " AND TRC.TRC_ORDEM <> '"+(cAliasQry)->TRC_ORDEM+"' AND TRC.TRC_CICLO = "
	cQuery += " (SELECT MAX(TR9.TR9_CICLO) FROM "+RetSqlName("TR9")+" TR9,"+RetSqlName("STJ")+" TJ "
	cQuery += " WHERE TRC.TRC_FILIAL=TR9.TR9_FILIAL "
	cQuery += " AND TRC.TRC_ORDEM = TR9.TR9_ORDEM AND TRC.TRC_PLANO=TR9.TR9_PLANO AND "
	cQuery += " TR9.TR9_FILIAL = TJ.TJ_FILIAL AND TR9.TR9_ORDEM = TJ.TJ_ORDEM AND "
	cQuery += " TR9.TR9_PLANO=TJ.TJ_PLANO AND TJ.TJ_TERMINO='N' AND TJ.TJ_SITUACA<>'C' AND TR9.D_E_L_E_T_<>'*' AND TJ.D_E_L_E_T_<>'*') "
	cQuery += " GROUP BY TRC.TRC_CICLO,TRC.TRC_FILIAL,TRC.TRC_ORDEM,TRC.TRC_PLANO, STJ.TJ_CODBEM "
	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry3, .F., .T.)

	SetRegua(LastRec())

	DbGoTop()
	While !Eof()
		lDup := .t.
		If cBemPai == (cAliasQry3)->TJ_CODBEM
			
			MNR545ARR(cPosicao,cNumfogo,11,,nSulco,nI)
		
		Else

			MNR545ARR(cPosicao,cNumfogo,8,,nSulco,nI)

		EndIf
		DbSelectArea(cAliasQry3)
		DbSkip()
	End
	(cALIASQRY3)->(dbCloseArea())
	If lDup
		RestArea(aArea)
		Return 1
	EndIf
	RestArea(aArea)
Return 0
/*/


Ŀ
Funo    |MNR545CDUP| Autor  Evaldo Cevinscki Jr.   Data 23/11/2006
Ĵ
Descrio VERIFICA SE EXISTEM 2 INSPECOES IGUAIS                      
Ĵ
 Uso      MNTR545                                                     
ٱ


/*/
Function MNR545CDUP()
	Local nEixos := 0

	cAliasQry4 := GetNextAlias()
	cQuery := " Select TRC_FILIAL,TRC_ORDEM,TRC_PLANO,TRC_EIXO,TRC_QTDPNE,TRC_NFOGO1,TRC_LOCPN1,TRC_NFOGO2, "
	cQuery += " TRC_LOCPN2,TRC_NFOGO3,TRC_LOCPN3,TRC_NFOGO4,TRC_LOCPN4,TRC_NFOGO5,TRC_LOCPN5,"
	cQuery += " TRC_NFOGO6,TRC_LOCPN6,TRC_NFOGO7,TRC_LOCPN7,TRC_NFOGO8,TRC_LOCPN8,TRC_NFOGO9,"
	cQuery += " TRC_LOCPN9,TRC_NFOG10,TRC_LOCP10 "
	cQuery += " from " + RetSqlName("TRC") "
	cQuery += " where trc_filial='"+(cAliasQry)->TRC_FILIAL+"' AND TRC_ORDEM = '"+(cAliasQry)->TRC_ORDEM+"' "
	cQuery += " group by TRC_FILIAL,TRC_ORDEM,TRC_PLANO,TRC_EIXO,TRC_QTDPNE,TRC_NFOGO1,TRC_LOCPN1,TRC_NFOGO2,"
	cQuery += " TRC_LOCPN2,TRC_NFOGO3,TRC_LOCPN3,TRC_NFOGO4,TRC_LOCPN4,TRC_NFOGO5,TRC_LOCPN5,"
	cQuery += " TRC_NFOGO6,TRC_LOCPN6,TRC_NFOGO7,TRC_LOCPN7,TRC_NFOGO8,TRC_LOCPN8,TRC_NFOGO9,"
	cQuery += " TRC_LOCPN9,TRC_NFOG10,TRC_LOCP10"
	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry4, .F., .T.)

	SetRegua(LastRec())
	cEixo := "ZZ"
	DbGoTop()
	While !Eof()
		If (cAliasQry4)->TRC_EIXO <> cEixo
			cEixo := (cAliasQry4)->TRC_EIXO
			nEixos++
		EndIf
		DbSelectArea(cAliasQry4)
		DbSkip()
	End
	(cAliasQry4)->(dbCloseArea())
	If nEixos = (cAliasQry)->EIXOS
		Return 1
	EndIf

Return 0
/*/


Ŀ
Funo    |MNR545RES | Autor  Evaldo Cevinscki Jr.   Data 20/12/2007
Ĵ
Descrio Verifica se os Steps foram informados na inspecao           
Ĵ
 Uso      MNTR545                                                     
ٱ


/*/
Function MNR545RES(cPosicao,cNumfogo,nI)
	Local aArea := GetArea()

	MNR545ARR(cPosicao,cNumfogo,10,,,nI)

	RestArea(aArea)
Return .t.
