#INCLUDE "Mntr775.ch"
#INCLUDE "PROTHEUS.CH"

#DEFINE _nVERSAO 3 //Versao do fonte
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR775  ³ Autor ³ Denis Hyroshi de Souza³ Data ³ 19/09/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Resumo do plano de manutencao                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MNTR775()
	Local oReport
	Local aArea := GetArea()

	//+---------------------------------------------+
	//| Guarda conteudo e declara variaveis padroes |
	//+---------------------------------------------+
	Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)

	Private cTRB2	  := GetNextAlias()
	Private Titulo    := STR0001 //"Resumo do Plano de Manutencao"
	Private TitDescr  := STR0002 //"Sera apresentado as quantidades de Ordens de Servicos abertos, finalizados, cancelados, etc."
	Private nomeprog  := "MNTR775"
	Private aVETINR   := {}
	Private aDBFR775  := {}
	Private vINDR775  := {}
	Private oARQTR775                      	
	Private aPerg     := {}

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros!                        ³
	//³ mv_par01     // De Plano                                     ³
	//³ mv_par02     // Ate Plano                                    ³
	//³ mv_par03     // De Data Manutencao                           ³
	//³ mv_par04     // Ate Data Manutencao                          ³ 
	//³ mv_par05     // De Centro de Custo                           ³
	//³ mv_par06     // Ate Centro de Custo                          ³ 
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	//Modelo Release 4
	If FindFunction("TRepInUse") .And. TRepInUse()
		//-- Interface de impressao
		oReport := ReportDef()
		oReport:SetPortrait() //Default Retrato  
		oReport:PrintDialog()
	Else
		//Modelos Padrao
		MNTR775PAD()
	EndIf
	//+---------------------------------------------+
	//| Retorna conteudo de variaveis padroes       |
	//+---------------------------------------------+
	NGRETURNPRM(aNGBEGINPRM)
	RestArea(aArea)
Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR775  ³ Autor ³ Denis Hyroshi de Souza³ Data ³ 19/09/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio padrao                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function MNTR775PAD()

	Local cString    := "STJ"
	Local cdesc1     := STR0003 //"Relatorio de Resumo do Plano de Manutencao       "
	Local cdesc2     := STR0004 //"Sera apresentado as quantidades de Ordens de Ser-"
	Local cdesc3     := STR0005 //"vicos abertos, finalizados, cancelados, etc.     "
	Local wnrel      := "MNTR775"
	Local aPerg      := {}

	Private aReturn  := { STR0006, 1,STR0007, 1, 2, 1, "",1 } //"Zebrado"###"Administracao"
	Private nLastKey := 0
	Private cperg    := "MNT775"
	Private Tamanho  := "M"
	Private lmaqui   := .f.

	Pergunte(cperg,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	wnrel:=SetPrint(cString,wnrel,cperg,titulo,cDesc1,cDesc2,cDesc3,.F.,"")

	If nLastKey = 27
		Set Filter To
		Return
	Endif

	SetDefault(aReturn,cString)
	RptStatus({|lEnd| R775Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R775Imp  ³ Autor ³ Denis Hyroshi de Souza³ Data ³ 19/09/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR775                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R775Imp(lEnd,wnRel,titulo,tamanho)

	Local cRodaTxt := ""
	Local nCntImpr := 0

	Private li := 80 ,m_pag := 1
	Private CABEC1 := STR0008 //"Plano   Nome Plano                                Dt. Plano  Dt.Inicio  Dt. Fim    OS Abertas  Finalizadas  Canceladas  Pendentes"
	Private CABEC2 := " "
	nTipo := IIF(aReturn[4]==1,15,18)

	/*/

	1         2         3         4         5         6         7         8
	012345678901234567890123456789012345678901234567890123456789012345678901234567890
	____________________________________________________________________________________________________________________________________
	Plano........: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 
	Data Plano...: 99/99/99        De.: 99/99/99     Ate.: 99/99/99

	Número de O.S. Em aberto...: 999999
	Finalizadas.: 999999
	Canceladas..: 999999
	Pendentes...: 999999


	Plano........: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 
	Data Plano...: 99/99/99        De.: 99/99/99     Ate.: 99/99/99

	Número de O.S. Em aberto...: 999999
	Finalizadas.: 999999
	Canceladas..: 999999
	Pendentes...: 999999


	1         2         3         4         5         6         7         8         9         0         1         2         3
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	____________________________________________________________________________________________________________________________________
	Plano   Nome Plano                                Dt. Plano  Dt.Inicio  Dt. Fim    OS Abertas  Finalizadas  Canceladas  Pendentes
	___________________________________________________________________________________________________________________________________
	XXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  99/99/99   99/99/99   99/99/99       999999       999999      999999     999999

	Plano........: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 
	Data Plano...: 99/99/99        De.: 99/99/99     Ate.: 99/99/99

	Número de O.S. Em aberto...: 999999
	Finalizadas.: 999999
	Canceladas..: 999999
	Pendentes...: 999999
	/*/

	Processa({|lEND| MTR775TJTS(.t.)},STR0009) //"Selecionando Registros..."

	//+----------------------+
	//| Inicio da Impressao  |
	//+----------------------+

	dbSelectArea(cTRB2)
	DbGoTop()
	SetRegua(LastRec())
	While !Eof()

		IncRegua()

		dbSelectArea("STI")
		dbSetOrder(01)
		dbSeek(xFilial("STI")+(cTRB2)->PLANO)

		NGSOMALI(58)
		@ Li,000 Psay (cTRB2)->PLANO
		@ Li,008 Psay STI->TI_DESCRIC
		@ Li,050 Psay STI->TI_DATAPLA
		@ Li,061 Psay STI->TI_DATAINI
		@ Li,072 Psay STI->TI_DATAFIM
		@ Li,087 Psay (cTRB2)->EM_ABERTO	Picture "999999"
		@ Li,100 Psay (cTRB2)->ENCERRADAS	Picture "999999"
		@ Li,112 Psay (cTRB2)->CANCELADAS	Picture "999999"
		@ Li,123 Psay (cTRB2)->PENDENTES	Picture "999999"

		dbSelectArea(cTRB2)
		(cTRB2)->(dbSkip())
	End

	Roda(nCntImpr,cRodaTxt,Tamanho)

	//Deleta o arquivo temporario fisicamente
	oARQTR775:Delete()
	
	//+---------------------------------------------------+
	//| Devolve a condicao original do arquivo principam  |
	//+---------------------------------------------------+
	RetIndex("SI3")
	Set Filter To

	Set device to Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	Endif
	MS_FLUSH()
	
Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³  Denis                ³ Data ³ 22/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Define as secoes impressas no relatorio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR775                                                    ³±±
±±|__________|____________________________________________________________|±± 
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function ReportDef()
	Static oReport
	Static oSection1  

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao do componente de impressao                                      ³
	//³                                                                        ³
	//³TReport():New                                                           ³
	//³ExpC1 : Nome do relatorio                                               ³
	//³ExpC2 : Titulo                                                          ³
	//³ExpC3 : Pergunte                                                        ³
	//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
	//³ExpC5 : Descricao                                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport := TReport():New("MNTR775",OemToAnsi(Titulo),"MNT775",{|oReport| ReportPrint(oReport)},Titulo+". "+TitDescr)
	Pergunte(oReport:uParam,.F.)

	//Defenicao das secoes do relatorio
	oReport:SetTotalInLine(.F.)
	oSection1 := TRSection():New(oReport,STR0019,{(cTRB2),"STI"}) //"Resumo"
	TRCell():New(oSection1,/*"(cTRB2)->PLANO"*/"cPlano"		     ,(cTRB2),STR0010	,"@!"		,6	,/*lPixel*/,{|| cPlano}) //"Plano"
	TRCell():New(oSection1,"TI_DESCRIC"			                 ,"STI"	 ,STR0011	,"@!"		,30	,/*lPixel*/) //"Nome Plano"
	TRCell():New(oSection1,"TI_DATAPLA"			                 ,"STI"	 ,STR0012	,"99/99/9999"	,10	,/*lPixel*/) //"Dt. Plano"
	TRCell():New(oSection1,"TI_DATAINI"			                 ,"STI"	 ,STR0013	,"99/99/9999"	,10	,/*lPixel*/) //"Dt. Inic."
	TRCell():New(oSection1,"TI_DATAFIM"			                 ,"STI"  ,STR0014	,"99/99/9999"	,10	,/*lPixel*/) //"Dt. Fim"
	TRCell():New(oSection1,/*"(cTRB2)->EM_ABERTO"*/"cAberto"	 ,(cTRB2),STR0015	,"999999"	,6	,/*lPixel*/,{|| cAberto}) //"OS Abertas"
	TRCell():New(oSection1,/*"(cTRB2)->ENCERRADAS"*/"cEncerradas",(cTRB2),STR0016	,"999999"	,6	,/*lPixel*/,{|| cEncerradas}) //"OS Finalizadas"
	TRCell():New(oSection1,/*"(cTRB2)->CANCELADAS"*/"cCanceladas",(cTRB2),STR0017	,"999999"	,6	,/*lPixel*/,{|| cCanceladas}) //"OS Canceladas"
	TRCell():New(oSection1,/*"(cTRB2)->PENDENTES"*/"cPendentes"	 ,(cTRB2),STR0018	,"999999"	,6	,/*lPixel*/,{|| cPendentes}) //"OS Pendentes"

	//Definicao para imprimir os cabecalhos de campos numericos da esquerda para a direita
	//oSection1:Cell("REFORM"):SetHeaderAlign("RIGHT") 
	TRPosition():New(oSection1,"STI",1,{|| xFilial("STI")+(cTRB2)->PLANO})

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ReportPrint³ Autor ³Denis Hyroshi de Souza ³ Data ³ 22/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ReportDef                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/      
Static Function ReportPrint(oReport)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da secao utilizada pelo relatorio                               ³
	//³                                                                        ³
	//³TRSection():New                                                         ³
	//³ExpO1 : Objeto TReport que a secao pertence                             ³
	//³ExpC2 : Descricao da seçao                                              ³
	//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
	//³        sera considerada como principal para a seção.                   ³
	//³ExpA4 : Array com as Ordens do relatório                                ³
	//³ExpL5 : Carrega campos do SX3 como celulas                              ³
	//³        Default : False                                                 ³
	//³ExpL6 : Carrega ordens do Sindex                                        ³
	//³        Default : False                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao da celulas da secao do relatorio                                ³
	//³                                                                        ³
	//³TRCell():New                                                            ³
	//³ExpO1 : Objeto TSection que a secao pertence                            ³
	//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
	//³ExpC3 : Nome da tabela de referencia da celula                          ³
	//³ExpC4 : Titulo da celula                                                ³
	//³        Default : X3Titulo()                                            ³
	//³ExpC5 : Picture                                                         ³
	//³        Default : X3_PICTURE                                            ³
	//³ExpC6 : Tamanho                                                         ³
	//³        Default : X3_TAMANHO                                            ³
	//³ExpL7 : Informe se o tamanho esta em pixel                              ³
	//³        Default : False                                                 ³
	//³ExpB8 : Bloco de código para impressao.                                 ³
	//³        Default : ExpC2                                                 ³
	//³                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	Private cPlano, cAberto, cCanceladas, cEncerradas, cPendentes
	Processa({|lEND| MTR775TJTS(.f.)},STR0009)    //"Selecionando Registros..."

	dbSelectArea(cTRB2)
	DbGotop()
	oSection1:Init()
	oReport:SetMeter(RecCount())
	While !Eof() .And. !oReport:Cancel()
		oReport:IncMeter()  
		cPlano      := (cTRB2)->PLANO
		cAberto     := (cTRB2)->EM_ABERTO
		cCanceladas := (cTRB2)->CANCELADAS
		cEncerradas := (cTRB2)->ENCERRADAS
		cPendentes  := (cTRB2)->PENDENTES
		oSection1:PrintLine()

		(cTRB2)->(dbSkip())
	End
	oSection1:Finish()   

	//Deleta o arquivo temporario fisicamente 
	oARQTR775:Delete()
	
Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MTR775TJTS ³ Autor ³Denis Hyroshi de Souza ³ Data ³ 22/06/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Grava registros no arquivo temporario                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MTR775TJTS(lPad775)
	
	Local aArea := GetArea()

	aDBFR775  := {}
		AAdd(aDBFR775,{"PLANO"	   , "C", 06, 0})
		AAdd(aDBFR775,{"EM_ABERTO" , "N", 06, 0})
		AAdd(aDBFR775,{"CANCELADAS", "N", 06, 0})
		AAdd(aDBFR775,{"ENCERRADAS", "N", 06, 0})
		AAdd(aDBFR775,{"PENDENTES" , "N", 06, 0})
	
	//Cria Tabela Temporária 
	oARQTR775 := NGFwTmpTbl(cTRB2,aDBFR775,{{"PLANO"}})

	//+--------------------------------------------+
	//| Possiciona os arquivos para processamento  |
	//+--------------------------------------------+

	dbSelectArea("STJ")
	dbSetOrder(05)  // PLANO ORDEM BEM SERVICO
	dbSeek(xFilial("STJ")+MV_PAR01+MV_PAR05,.t.)
	If lPad775
		nReg := LastRec() - Recno()
	Endif

	dbSelectArea("STS")
	dbSetOrder(03)  // PLANO ORDEM
	dbSeek(xFilial("STS")+MV_PAR01,.t.)
	If lPad775
		nReg := nReg + (LastRec() - Recno())
		SetRegua(nReg)
	Endif

	//+--------------------------------------------+
	//| Processa arquivo de O.S.                   |
	//+--------------------------------------------+
	dbSelectArea("STJ")
	While !Eof() .And. STJ->TJ_FILIAL == xFilial("STJ") .And. STJ->TJ_PLANO <= MV_PAR02

		If lPad775
			IncRegua()
		Endif
		
		If STJ->TJ_CCUSTO < MV_PAR05 .Or. STJ->TJ_CCUSTO > MV_PAR06
			dbSelectArea("STJ")
			dbSkip()
			Loop
		EndIf

		If STJ->TJ_DTORIGI < MV_PAR03 .Or. STJ->TJ_DTORIGI > MV_PAR04
			dbSelectArea("STJ")
			dbSkip()
			Loop
		EndIf

		dbSelectArea(cTRB2)
		If !dbSeek(STJ->TJ_PLANO)
			RecLock((cTRB2),.T.)
			(cTRB2)->PLANO  := STJ->TJ_PLANO
		Else
			RecLock((cTRB2),.F.)
		Endif

		If STJ->TJ_SITUACA == "C"
			(cTRB2)->CANCELADAS += 1
		ElseIf STJ->TJ_SITUACA == "P"
			(cTRB2)->PENDENTES  += 1
		ElseIf STJ->TJ_SITUACA == "L"
			If STJ->TJ_TERMINO == "S"
				(cTRB2)->ENCERRADAS += 1
			Else
				(cTRB2)->EM_ABERTO += 1
			Endif
		EndIf

		(cTRB2)->(MsUnLock())

		dbSelectArea("STJ")
		dbSkip()
	End

	//+--------------------------------------------+
	//| Processa arquivo de O.S. HISTORICO         |
	//+--------------------------------------------+
	dbSelectArea("STS")
	While !Eof() .And. STS->TS_FILIAL == xFilial("STS") .And. STS->TS_PLANO <= MV_PAR02

		If lPad775
			IncRegua()
		Endif

		If STS->TS_DTORIGI < MV_PAR03 .Or. STS->TS_DTORIGI > MV_PAR04
			dbSelectArea("STS")
			dbSkip()
			Loop
		EndIf

		dbSelectArea(cTRB2)
		If !dbSeek(STS->TS_PLANO)
			RecLock((cTRB2),.T.)
			(cTRB2)->PLANO  := STS->TS_PLANO
		Else
			RecLock((cTRB2),.F.)
		Endif

		If STS->TS_SITUACA == "C"
			(cTRB2)->CANCELADAS += 1
		ElseIf STS->TS_SITUACA == "P"
			(cTRB2)->PENDENTES  += 1
		ElseIf STS->TS_SITUACA == "L"
			If STS->TS_TERMINO == "S"
				(cTRB2)->ENCERRADAS += 1
			Else
				(cTRB2)->EM_ABERTO += 1
			Endif
		EndIf

		(cTRB2)->(MsUnLock())

		dbSelectArea("STS")
		dbSkip()
	End

	//+--------------------------------------------+
	//| Retorna aos indices primarios              |
	//+--------------------------------------------+
	dbSelectArea("STJ")
	dbSetOrder(01)
	dbSelectArea("STS")
	dbSetOrder(01)

	RestArea(aArea)
	
Return