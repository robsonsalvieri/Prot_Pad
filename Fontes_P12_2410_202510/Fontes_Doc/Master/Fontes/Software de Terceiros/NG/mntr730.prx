#INCLUDE "MNTR730.CH"
#INCLUDE "PROTHEUS.CH"
/*


Ŀ
Funo     MNTR730   Autor  Deivys Joenck          Data  19/11/01 
Ĵ
Descrio  Relatorio de paradas do equipamento                        
Ĵ
OBSERVACAO Reviso tcnica 14/10/2002                                 
Ĵ
 Uso       Generico                                                   
ٱ


*/
Function MNTR730()

	Local vVETHORAS  := {}
	Local WNREL      := "MNTR730"
	Local LIMITE     := 132
	Local cDESC1     := STR0001 //"Relatorio das Paradas dos Bens nas Manutencoes."
	Local cDESC2     := STR0002 //"Usar os Parametros para Selecionar o C.Custo, o Bem, o "
	Local cDESC3     := STR0003 //"Servico e o Periodo de Paradas que Deseja Imprimir."
	Local cSTRING    := "ST9"

	Private NOMEPROG := "MNTR730"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0004,1,STR0005,1,2,1,"",1}  //"Zebrado"###"Administracao"
	Private TITULO   := STR0006 //"Paradas de Equipamentos para Manutencoes"
	Private nTIPO    := 0
	Private nSOMA    := 0,nHORAS := 0,lPROBLEMA := .F.
	Private CABEC1, CABEC2

	//Ŀ
	// Variaveis utilizadas para Parametros!                        
	// mv_par01     // De Data Parada                               
	// mv_par02     // Ate Data Parada                              
	// mv_par03     // De C.Custo                                   
	// mv_par04     // Ate C.Custo                                  
	// mv_par05     // Do Bem                                       
	// mv_par06     // Ate Bem                                      
	// mv_par07     // Do Servico                                   
	// mv_par08     // Ate Servico                                  
	// mv_par09     // Sequencia                                    
	// mv_par10     // Lista Obs. Mot. Parada                       
	// mv_par11     // Considera Calendario                         
	// mv_par12     // Tipo de Relatorio                            
	//
	Private cPERG := "MNT730"
	PERGUNTE(cPERG,.F.)

	//Ŀ
	// Envia controle para a funcao SETPRINT                        
	//

	WNREL := SETPRINT(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")

	If nLASTKEY = 27
		SET FILTER TO
		RETURN
	Endif
	SETDEFAULT(aRETURN,cSTRING)

	If MV_PAR12 = 1
		TITULO := TITULO + " - " + STR0027 //"Analitico"
	ElseIf MV_PAR12 = 2
		TITULO := TITULO + " - " + STR0028 //"Sintetico"
	Endif

	RPTSTATUS({|lEND|MNTR730IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)
RETURN NIL

/*/


Ŀ
Funo    MNTR730IMP  Autor  Deivys Joenck         Data  19/11/01 
Ĵ
Descrio  Impressao do relatorio                                     
ٱ
 Uso       MNTR730                                                    
ٱ


/*/
Function MNTR730IMP(lEND,WNREL,TITULO,TAMANHO)

	Local cRODATXT := ""
	Local nCNTIMPR := 0
	Local nMULT    := 1
	Local oTempTable //Obj. Tabela Temporaria

	Private cTRB	:= GetNextAlias()

	Private li := 80 ,m_pag := 1




	aTRAB      := {}
	dDTPARINI  := dDataBase
	nTOHRPAR   := 0.00
	nTOHRESP   := 0.00
	nTOHRMAN   := 0.00
	nTOHRHOM   := 0.00
	nTHRDISP   := 0.00
	nMVPAR     := Val(mv_par09)
	nTIPO      := IIf(aRETURN[4]==1,15,18)

	//                                        1         2         3         4         5         6         7         8         9        10        11        12        13              14       15         16
	//                         01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	//CABEC1     := STR0007 //"   Bem              Descricao                 O.Serv. Manutencao                     Dt.Parada   Hr/Par  Hr/Esp  Hr/Man    Hr/h"
	If mv_par12 == 1
		CABEC1 := STR0029 //"   Bem              Descricao                 Ordem   Manutencao                Desc. Servio               Contador   Dt.Par.Ini.  Hr.Par.Ini  Dt.Par.Fim  Hr.Par.Fim  Hr/Par   Hr/Esp    Hr/Man     Hr/h"
	Else
		CABEC1 := STR0007 //  "   Bem              Descricao                                             Manutencao                                                                                    Hr/Par   Hr/Esp    Hr/Man     Hr/h"
	EndIf
	CABEC2    := " "

	/*
	1         2         3         4         5         6         7         8         9        10        11        12        13              14       15         16
	01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	*************************************************************************************************************************************
	Bem              Descricao                 Ordem  Manutencao                                            Contador   Dt.Parada   Hr/Par   Hr/Esp    Hr/Man     Hr/h
	*************************************************************************************************************************************
	Centro de Custo..: xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx                     9999999999  99/99/9999 9999:99  9999:99   9999:99   999:99
	Obs: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx                                   99/99/9999 9999:99  9999:99   9999:99  9999:99

	Total da Manut.: Paradas:    999  Hr/Dis: 99999:99  Perc.: 999,9%           999999:99 99999.99 999999:99 99999:99

	Total do Bem:    Paradas:    999  Hr/Dis: 99999:99  Perc.: 999,9%           999999:99 99999.99 999999:99 99999:99

	Total do C.Custo:Paradas:    999  *Media: HP x HD:    9999999,99*           999999:99 99999.99 999999:99 99999:99

	Total Geral:     Paradas:    999  *Media: HP x HD:   99999999,99*           999999:99 99999.99 999999:99 99999:99
	*/

	aDBF := {{"CUSTO"  ,"C",Len(stj->tj_ccusto),0},;
			 {"CODBEM" ,"C",16,0},;
			 {"ORDEM"  ,"C",06,0},;
			 {"SERVIC" ,"C",06,0},;
			 {"SEQREL" ,"C",03,0},;
			 {"POSCONT","N",09,0},;
			 {"DTPRIN" ,"D",08,0},;
			 {"HRPRIN" ,"C",05,0},;
			 {"DTPRFI" ,"D",08,0},;
			 {"HRPRFI" ,"C",05,0},;
			 {"HORPAR" ,"N",12,2},;
			 {"HORESP" ,"N",12,2},;
			 {"HORMAN" ,"N",12,2},;
			 {"HORHOM" ,"N",12,2},;
			 {"HORDIS" ,"N",12,2},;
			 {"ARQUIV" ,"C",03,0}}

	//Instancia classe FWTemporaryTable
	oTempTable  := FWTemporaryTable():New( cTRB, aDBF )
	//Cria indices
	oTempTable:AddIndex( "Ind01" , {"CUSTO","CODBEM","SERVIC","SEQREL"}  )
	//Cria a tabela temporaria
	oTempTable:Create()


	PROCESSA({|lEND| MNTR730STJ()},STR0016+STR0017) //"Processando Arquivo..."..NORMAIS
	If !lPROBLEMA
		PROCESSA({|lEND| MNTR730STS()},STR0016+STR0018) //"Processando Arquivo..."..HISTORICO
	Endif
	If !lPROBLEMA
		//Inicio da impressao

		Dbselectarea(cTRB)
		Dbgotop()
		Store 0.00 To nTOTHRPAR,nTOTHRESP,nTOTHRMAN,nTOTHRHOM,nTOTPARAD,nTOTHRDIS,nTOHDPCC
		SETREGUA(LASTREC())
		While !Eof()
			NGSOMALI(58)
			cCUSTO := (cTRB)->CUSTO
			Store 0.00 To nTOCHRPAR,nTOCHRESP,nTOCHRMAN,nTOCHRHOM,nTOCPARAD,nTOCHRDIS,nHDISPCC

			@ Li,000 PSAY STR0011 //"Centro de Custo..:"
			@ Li,019 PSAY Alltrim((cTRB)->CUSTO)+" "+NGSEEK('SI3',(cTRB)->CUSTO,1,'SUBSTR(I3_DESC,1,30)')
			NGSOMALI(58)

			Dbselectarea(cTRB)
			While !Eof() .And. (cTRB)->CUSTO == cCUSTO
				NGSOMALI(58)
				cCODBEM  := (cTRB)->CODBEM
				Store 0.00 To nTOMHRHOM,nTOMHRPAR,nTOMHRESP,nTOMHRMAN,nTOMPARAD,nTOMHRDIS

				If mv_par12 = 1
					@ Li,003 PSAY (cTRB)->CODBEM
					@ Li,020 PSAY NGSEEK('ST9',(cTRB)->CODBEM,1,'SUBSTR(T9_NOME,1,25)')
				Else
					@ Li,003 PSAY (cTRB)->CODBEM
					@ Li,020 PSAY NGSEEK('ST9',(cTRB)->CODBEM,1,'SUBSTR(T9_NOME,1,45)')
				EndIf

				Dbselectarea(cTRB)
				nHDISPCC += (cTRB)->HORDIS

				While !Eof() .And. (cTRB)->CUSTO == cCUSTO .And. (cTRB)->CODBEM == cCODBEM
					cSERVIC  := (cTRB)->SERVIC
					cSEQREL  := (cTRB)->SEQREL
					nTTHRDIS := (cTRB)->HORDIS
					Store 0.00 To nTTHRHOM,nTTHRPAR,nTTHRESP,nTTHRMAN,nPARADAS

					While !Eof() .And. (cTRB)->CUSTO == cCUSTO .And. (cTRB)->CODBEM == cCODBEM .And.;
					(cTRB)->SERVIC == cSERVIC .And. (cTRB)->SEQREL == cSEQREL

						INCREGUA()

						// -----------------------------------
						// IMPRESSAO DO RELATORIO ANALITICO
						// -----------------------------------
						If mv_par12 = 1
							@ Li,046 PSAY (cTRB)->ORDEM

							If Alltrim((cTRB)->SEQREL) == "0"
								@ Li,054 PSAY NGSEEK('ST4',(cTRB)->SERVIC,1,'SUBSTR(T4_NOME,1,25)')
							ELSE
								@ Li,054 PSAY NGSEEK('STF',(cTRB)->CODBEM+(cTRB)->SERVIC+(cTRB)->SEQREL,;
								1,'SUBSTR(TF_NOMEMAN,1,25)')
								@ Li,080 PSAY NGSEEK('ST4',(cTRB)->SERVIC,1,'SUBSTR(T4_NOME,1,25)')
							Endif

							Dbselectarea(cTRB)
							@ Li,108 PSAY (cTRB)->POSCONT Picture "999999999"
							@ Li,119 PSAY (cTRB)->DTPRIN  Picture "99/99/9999"
							@ Li,132 PSAY (cTRB)->HRPRIN  Picture "99:99"
							@ Li,144 PSAY (cTRB)->DTPRFI  Picture "99/99/9999"
							@ Li,156 PSAY (cTRB)->HRPRFI  Picture "99:99"

							@ Li,168 PSAY If((cTRB)->HORPAR < 0,"-"+NtoH((cTRB)->HORPAR * -1)," "+NtoH((cTRB)->HORPAR)) //NGRHODSEXN((cTRB)->HORPAR,If(MV_PAR11 = 1,"D","S"))  Picture "@E 99999.99"
							@ Li,177 PSAY If((cTRB)->HORESP < 0,"-"+NtoH((cTRB)->HORESP * -1)," "+NtoH((cTRB)->HORESP)) //NGRHODSEXN((cTRB)->HORESP,If(MV_PAR11 = 1,"D","S"))  Picture "@E 99999.99"
							@ Li,187 PSAY If((cTRB)->HORMAN < 0,"-"+NtoH((cTRB)->HORMAN * -1)," "+NtoH((cTRB)->HORMAN)) //NGRHODSEXN((cTRB)->HORMAN,If(MV_PAR11 = 1,"D","S"))  Picture "@E 99999.99"
							@ Li,196 PSAY If((cTRB)->HORHOM < 0,"-"+NtoH((cTRB)->HORHOM * -1)," "+NtoH((cTRB)->HORHOM)) //NGRHODSEXN((cTRB)->HORHOM,"D")  //Picture "@R 9999:99"

							If MV_PAR10 == 1
								vStjM := NGCADICBASE('TJ_MMSYP','A','STJ')
								vStsM := NGCADICBASE('TS_MMSYP','A','STS')
								cOBS := If((cTRB)->ARQUIV = 'STJ',If(vStjM[1],NGMEMOSYP(NGSEEK('STJ',(cTRB)->ORDEM,1,'TJ_MMSYP')),;
								NGSEEK('STJ',(cTRB)->ORDEM,1,'TJ_OBSERVA')),;
								If(vStsM[1],NGMEMOSYP(NGSEEK('STS',(cTRB)->ORDEM,1,'TS_MMSYP')),;
								NGSEEK('STS',(cTRB)->ORDEM,1,'TS_OBSERVA')))
								NG2IMPMEMO(cOBS,67,046,"Obs.: ",.F.,.T.)
							Endif
							NGSOMALI(58)
						EndIF

						// -----------------------------------
						// IMPRESSAO DO RELATORIO SINTETICO
						// -----------------------------------
						If mv_par12 = 2 .And. nPARADAS = 0

							If Alltrim((cTRB)->SEQREL) == "0"
								@ Li,074 PSAY NGSEEK('ST4',(cTRB)->SERVIC,1,'SUBSTR(T4_NOME,1,41)')
							Else
								@ Li,074 PSAY NGSEEK('STF',(cTRB)->CODBEM+(cTRB)->SERVIC+(cTRB)->SEQREL,;
								1,'SUBSTR(TF_NOMEMAN,1,41)')
							Endif
							dbselectarea(cTRB)

						End If

						nTTHRHOM := nTTHRHOM + (cTRB)->HORHOM
						nTTHRPAR := nTTHRPAR + (cTRB)->HORPAR
						nTTHRESP := nTTHRESP + (cTRB)->HORESP
						nTTHRMAN := nTTHRMAN + (cTRB)->HORMAN
						nPARADAS := nPARADAS + 1

						Dbselectarea(cTRB)
						Dbskip()
					End

					NGSOMALI(58)

					@ Li,080 PSAY STR0015 //"Total de Man:"
					@ Li,099 PSAY STR0012
					@ Li,109 PSAY nPARADAS Picture "@E 999"
					@ Li,114 PSAY STR0009 //"Hr/Dis:"
					@ Li,122 PSAY NGRETHORDDH(nTTHRDIS)[1] //Picture "@E 99999.99"
					@ Li,132 PSAY STR0010 //"Per:"
					@ Li,138 PSAY (HtoM(NtoH(nTTHRPAR))*100) / (NGRETHORDDH(nTTHRDIS)[2] * 60) Picture "@E 9999.999"
					@ Li,147 PSAY "%"

					@ Li,168 PSAY If(nTTHRPAR < 0,"-"+NtoH(nTTHRPAR * -1)," "+NtoH(nTTHRPAR)) //NGRHODSEXN(nTTHRPAR,If(MV_PAR11 = 1,"D","S")) Picture "@E 99999.99"
					@ Li,177 PSAY If(nTTHRESP < 0,"-"+NtoH(nTTHRESP * -1)," "+NtoH(nTTHRESP)) //NGRHODSEXN(nTTHRESP,If(MV_PAR11 = 1,"D","S")) Picture "@E 99999.99"
					@ Li,187 PSAY If(nTTHRMAN < 0,"-"+NtoH(nTTHRMAN * -1)," "+NtoH(nTTHRMAN)) //NGRHODSEXN(nTTHRMAN,If(MV_PAR11 = 1,"D","S")) Picture "@E 99999.99"
					@ Li,196 PSAY If(nTTHRHOM < 0,"-"+NtoH(nTTHRHOM * -1)," "+NtoH(nTTHRHOM)) //NGRHODSEXN(nTTHRHOM,"D") Picture "@E 9999.99"

					nTOMHRHOM += nTTHRHOM
					nTOMHRPAR += nTTHRPAR
					nTOMHRESP += nTTHRESP
					nTOMHRMAN += nTTHRMAN
					nTOMPARAD += nPARADAS
					nTOMHRDIS += nTTHRDIS

					NGSOMALI(58)
				End
				@ Li,080 PSAY STR0008 //"Total de Paradas:"
				@ Li,099 PSAY STR0012
				@ Li,109 PSAY nTOMPARAD Picture "@E 999"
				@ Li,114 PSAY STR0009 //"Hr/Dis:"
				@ Li,122 PSAY NGRETHORDDH(nTTHRDIS)[1] //Picture "@E 99999.99"
				@ Li,132 PSAY STR0010 //"Per:"
				@ Li,138 PSAY (HtoM(NtoH(nTOMHRPAR))*100) / (NGRETHORDDH(nTTHRDIS)[2] * 60) Picture "@E 9999.999"
				@ Li,147 PSAY "%"

				@ Li,168 PSAY If(nTOMHRPAR < 0,"-"+NtoH(nTOMHRPAR * -1)," "+NtoH(nTOMHRPAR)) //NGRHODSEXN(nTOMHRPAR,If(MV_PAR11 = 1,"D","S")) Picture "@E 99999.99"
				@ Li,177 PSAY If(nTOMHRESP < 0,"-"+NtoH(nTOMHRESP * -1)," "+NtoH(nTOMHRESP)) //NGRHODSEXN(nTOMHRESP,If(MV_PAR11 = 1,"D","S")) Picture "@E 99999.99"
				@ Li,187 PSAY If(nTOMHRMAN < 0,"-"+NtoH(nTOMHRMAN * -1)," "+NtoH(nTOMHRMAN)) //NGRHODSEXN(nTOMHRMAN,If(MV_PAR11 = 1,"D","S")) Picture "@E 99999.99"
				@ Li,196 PSAY If(nTOMHRHOM < 0,"-"+NtoH(nTOMHRHOM * -1)," "+NtoH(nTOMHRHOM)) //NGRHODSEXN(nTOMHRHOM,"D") Picture "@E 9999.99"
				NGSOMALI(58)
				nTOCHRPAR += nTOMHRPAR
				nTOCHRESP += nTOMHRESP
				nTOCHRMAN += nTOMHRMAN
				nTOCHRHOM += nTOMHRHOM
				nTOCPARAD += nTOMPARAD
				nTOCHRDIS += nTOMHRDIS
			End
			NGSOMALI(58)
			@ Li,080 PSAY STR0013
			@ Li,099 PSAY STR0012
			@ Li,109 PSAY nTOCPARAD Picture "@E 999"

			@ Li,114 PSAY STR0019 //"*Media.: HP x HD:"
			@ Li,135 PSAY HtoM(NtoH(nTOCHRPAR)) / (NGRETHORDDH(nHDISPCC)[2] * 60) Picture "@E 9999999.999*"

			@ Li,168 PSAY If(nTOCHRPAR < 0,"-"+NtoH(nTOCHRPAR * -1)," "+NtoH(nTOCHRPAR)) //NGRHODSEXN(nTOCHRPAR,If(MV_PAR11 = 1,"D","S")) Picture "@E 99999.99"
			@ Li,177 PSAY If(nTOCHRESP < 0,"-"+NtoH(nTOCHRESP * -1)," "+NtoH(nTOCHRESP)) //NGRHODSEXN(nTOCHRESP,If(MV_PAR11 = 1,"D","S")) Picture "@E 99999.99"
			@ Li,187 PSAY If(nTOCHRMAN < 0,"-"+NtoH(nTOCHRMAN * -1)," "+NtoH(nTOCHRMAN)) //NGRHODSEXN(nTOCHRMAN,If(MV_PAR11 = 1,"D","S")) Picture "@E 99999.99"
			@ Li,196 PSAY If(nTOCHRHOM < 0,"-"+NtoH(nTOCHRHOM * -1)," "+NtoH(nTOCHRHOM)) //NGRHODSEXN(nTOCHRHOM,"D") Picture "@E 9999.99"
			NGSOMALI(58)
			nTOTHRPAR += nTOCHRPAR
			nTOTHRESP += nTOCHRESP
			nTOTHRMAN += nTOCHRMAN
			nTOTHRHOM += nTOCHRHOM
			nTOTPARAD += nTOCPARAD
			nTOTHRDIS += nTOCHRDIS
			nTOHDPCC  += nHDISPCC
		End
		If nTOTPARAD > 0.00 .Or. nTOTHRPAR > 0.00 .Or.  nTOTHRESP > 0.00 .Or.;
		nTOTHRMAN > 0.00 .Or. nTOTHRHOM > 0.00
			NGSOMALI(58)
			@ Li,080 PSAY STR0014
			@ Li,099 PSAY STR0012
			@ Li,109 PSAY nTOTPARAD Picture "@E 999"

			@ Li,114 PSAY STR0019 //"*Media.: HP x HD:"
			@ Li,134 PSAY HtoM(NtoH(nTOCHRPAR)) / (NGRETHORDDH(nTOHDPCC)[2] * 60) Picture "@E 99999999.999*"

			@ Li,168 PSAY If(nTOTHRPAR < 0,"-"+NtoH(nTOTHRPAR * -1)," "+NtoH(nTOTHRPAR)) //NGRHODSEXN(nTOTHRPAR,If(MV_PAR11 = 1,"D","S")) Picture "@E 99999.99"
			@ Li,177 PSAY If(nTOTHRESP < 0,"-"+NtoH(nTOTHRESP * -1)," "+NtoH(nTOTHRESP)) //NGRHODSEXN(nTOTHRESP,If(MV_PAR11 = 1,"D","S")) Picture "@E 99999.99"
			@ Li,187 PSAY If(nTOTHRMAN < 0,"-"+NtoH(nTOTHRMAN * -1)," "+NtoH(nTOTHRMAN)) //NGRHODSEXN(nTOTHRMAN,If(MV_PAR11 = 1,"D","S")) Picture "@E 99999.99"
			@ Li,196 PSAY If(nTOTHRHOM < 0,"-"+NtoH(nTOTHRHOM * -1)," "+NtoH(nTOTHRHOM)) //NGRHODSEXN(nTOTHRHOM,"D") Picture "@E 9999.99"
		Endif

		RODA(nCNTIMPR,cRODATXT,TAMANHO)
		RETINDEX("ST9")
		SET FILTER TO
		SET DEVICE TO SCREEN
		If aRETURN[5] == 1
			SET PRINTER TO
			dbCommitAll()
			OURSPOOL(WNREL)
		Endif
		MS_FLUSH()
	Endif

	oTempTable:Delete()//Deleta arquivo temporario

RETURN NIL

/*/


Ŀ
Funo    MNTR730STJ  Autor Incio Luiz Kolling    Data 14/10/2002
Ĵ
 DescrioProcessa as ordem de servico - NORMAIS ( STJ )              
ٱ
 Uso       MNTR730                                                    
ٱ


/*/
Static Function MNTR730STJ()
	cCONDSTJ1 := 'stj->tj_termino = "S" .And. stj->tj_situaca = "L" .And. '
	cCONDSTJ1 := cCONDSTJ1 + '(stj->tj_servico >= MV_PAR07 .And. stj->tj_servico <= MV_PAR08) .And.'
	cCONDSTJ1 := cCONDSTJ1 + '(stj->tj_ccusto  >= MV_PAR03 .And. stj->tj_ccusto  <= MV_PAR04)'

	cCONDSTL := 'Val(stl->tl_seqrela) > 0 .And. (stl->tl_tiporeg = "M" .Or. stl->tl_tiporeg = "E")'

	Dbselectarea("STJ")
	Dbsetorder(2)
	Dbseek(xFILIAL("STJ")+"B"+MV_PAR05,.T.)
	Procregua(LASTREC())
	While !Eof() .And. STJ->TJ_FILIAL == xFILIAL("STJ") .And.;
	STJ->TJ_TIPOOS = "B" .And. STJ->TJ_CODBEM <= MV_PAR06

		Incproc()
		cCODBEM := stj->tj_codbem
		dbSelectArea("ST9")
		dbSetOrder(1)
		If !dbSeek(xFilial("ST9")+cCODBEM)
			Dbselectarea("STJ")
			Dbskip()
			Loop
		Else
			If !MNTR730CAL(cCODBEM,"STJ")
				lPROBLEMA := .T.
				Exit
			Endif
			Dbselectarea("STJ")
			Dbsetorder(2)
			While !Eof() .And. STJ->TJ_FILIAL == xFILIAL("STJ") .And.;
			STJ->TJ_TIPOOS == "B" .And. STJ->TJ_CODBEM == cCODBEM

				If &(cCONDSTJ1)
					cCONDSTJ2 := If(Empty(stj->tj_dtprini),;
					'(STJ->TJ_DTMRINI >= MV_PAR01 .and. STJ->TJ_DTMRFIM <= MV_PAR02) .OR.'+;
					'(STJ->TJ_DTMRINI <= MV_PAR01 .and. STJ->TJ_DTMRFIM >= MV_PAR02) .OR.'+;
					'(STJ->TJ_DTMRINI >= MV_PAR01 .and. STJ->TJ_DTMRFIM >= MV_PAR02 .and. STJ->TJ_DTMRINI <= MV_PAR02) .OR.'+;
					'(STJ->TJ_DTMRINI <  MV_PAR01 .and. STJ->TJ_DTMRFIM >= MV_PAR01 .and. STJ->TJ_DTMRFIM <= MV_PAR02)',;
					'(STJ->TJ_DTPRINI >= MV_PAR01 .and. STJ->TJ_DTPRFIM <= MV_PAR02) .OR.'+;
					'(STJ->TJ_DTPRINI <= MV_PAR01 .and. STJ->TJ_DTPRFIM >= MV_PAR02) .OR.'+;
					'(STJ->TJ_DTPRINI >= MV_PAR01 .and. STJ->TJ_DTPRFIM >= MV_PAR02 .and. STJ->TJ_DTPRINI <= MV_PAR02) .OR.'+;
					'(STJ->TJ_DTPRINI <  MV_PAR01 .and. STJ->TJ_DTPRFIM >= MV_PAR01 .and. STJ->TJ_DTPRFIM <= MV_PAR02)')

					If &(cCONDSTJ2)

						If !Empty(MV_PAR09)
							If nMVPAR != Val(STJ->TJ_SEQRELA)
								Dbskip()
								Loop
							Endif
						Endif

						nTOHRHOM := 0.00
						Dbselectarea("STL")
						Dbsetorder(1)
						Dbseek(xFILIAL("STL")+STJ->TJ_ORDEM+STJ->TJ_PLANO)
						While !Eof() .And. STL->TL_FILIAL == xFILIAL("STL") .And.;
						STL->TL_ORDEM == STJ->TJ_ORDEM .And. STL->TL_PLANO == STJ->TJ_PLANO

							If &(cCONDSTL)

								If FindFunction( 'NGCalcHour' )

									nQTDHORAS := NGCalcHour( STL->TL_CODIGO, { STL->TL_DTINICI, STL->TL_HOINICI,;
									                         STL->TL_DTFIM, STL->TL_HOFIM }, STL->TL_USACALE )

								Else

									vVETHORAS := NGTQUATINS(STL->TL_CODIGO,STL->TL_TIPOREG,STL->TL_USACALE,;
									STL->TL_QUANTID,STL->TL_TIPOHOR,STL->TL_DTINICI,;
									STL->TL_HOINICI,STL->TL_DTFIM,STL->TL_HOFIM,STL->TL_UNIDADE)
									nQTDHORAS := vVETHORAS[1]

								EndIf

								nTOHRHOM += nQTDHORAS
							Endif
							Dbselectarea("STL")
							Dbskip()
						End

						MNTR730CHP(STJ->TJ_DTPRINI,STJ->TJ_HOPRINI,STJ->TJ_DTMRINI,;
						STJ->TJ_HOMRINI,STJ->TJ_DTPRFIM,STJ->TJ_HOPRFIM,;
						STJ->TJ_DTMRFIM,STJ->TJ_HOMRFIM)

						MNTR730TRB(STJ->TJ_CCUSTO,STJ->TJ_CODBEM,STJ->TJ_ORDEM,STJ->TJ_SERVICO,;
						STJ->TJ_SEQRELA,'STJ',STJ->TJ_POSCONT,STJ->TJ_HOPRINI,STJ->TJ_DTPRFIM,STJ->TJ_HOPRFIM)

					Endif
				Endif
				Dbselectarea("STJ")
				Dbskip()
			End
		EndIf

	End
Return .t.

/*/


Ŀ
Funo    MNTR730STS  Autor Incio Luiz Kolling    Data 14/10/2002
Ĵ
 DescrioProcessa as ordem de servico - NORMAIS ( STS )              
ٱ
 Uso       MNTR730                                                    
ٱ


/*/
Static Function MNTR730STS()
	cCONDSTS1 := 'sts->ts_termino = "S" .And. sts->ts_situaca = "L" .And. '
	cCONDSTS1 := cCONDSTS1 + '(sts->ts_servico >= MV_PAR07 .And. sts->ts_servico <= MV_PAR08) .And. '
	cCONDSTS1 := cCONDSTS1 + '(sts->ts_ccusto >= MV_PAR03 .And. sts->ts_ccusto <= MV_PAR04)'

	cCONDSTT := 'Val(stt->tt_seqrela) > 0 .And. (stt->tt_tiporeg = "M" .Or. stt->tt_tiporeg = "E")'

	Dbselectarea("STS")
	Dbsetorder(2)
	Dbseek(xFILIAL("STS")+"B"+MV_PAR05,.T.)
	Procregua(LASTREC())
	While !Eof() .And. STS->TS_FILIAL == xFILIAL("STS") .And.;
	STS->TS_TIPOOS = "B" .And. STS->TS_CODBEM <= MV_PAR06

		Incproc()
		cCODBEM := sts->ts_codbem
		dbSelectArea("ST9")
		dbSetOrder(1)
		If !dbSeek(xFilial("ST9")+cCODBEM)
			Dbselectarea("STJ")
			Dbskip()
			Loop
		Else
			If !MNTR730CAL(cCODBEM,"STS")
				lPROBLEMA := .T.
				Exit
			Endif
			Dbselectarea("STS")
			Dbsetorder(2)
			While !Eof() .And. STS->TS_FILIAL == xFILIAL("STS") .And.;
			STS->TS_TIPOOS == "B" .And. STS->TS_CODBEM == cCODBEM

				If &(cCONDSTS1)
					cCONDSTS2 := If(Empty(sts->ts_dtprini),;
					'(STS->TS_DTMRINI >= MV_PAR01 .and. STS->TS_DTMRFIM <= MV_PAR02) .OR.'+;
					'(STS->TS_DTMRINI <= MV_PAR01 .and. STS->TS_DTMRFIM >= MV_PAR02) .OR.'+;
					'(STS->TS_DTMRINI >= MV_PAR01 .and. STS->TS_DTMRFIM >= MV_PAR02 .and. STS->TS_DTMRINI <= MV_PAR02) .OR.'+;
					'(STS->TS_DTMRINI <  MV_PAR01 .and. STS->TS_DTMRFIM >= MV_PAR01 .and. STS->TS_DTMRFIM <= MV_PAR02)',;
					'(STS->TS_DTPRINI >= MV_PAR01 .and. STS->TS_DTPRFIM <= MV_PAR02) .OR.'+;
					'(STS->TS_DTPRINI <= MV_PAR01 .and. STS->TS_DTPRFIM >= MV_PAR02) .OR.'+;
					'(STS->TS_DTPRINI >= MV_PAR01 .and. STS->TS_DTPRFIM >= MV_PAR02 .and. STS->TS_DTPRINI <= MV_PAR02) .OR.'+;
					'(STS->TS_DTPRINI <  MV_PAR01 .and. STS->TS_DTPRFIM >= MV_PAR01 .and. STS->TS_DTPRFIM <= MV_PAR02)')

					If &(cCONDSTS2)
						If !Empty(MV_PAR09)
							If nMVPAR != Val(STS->TS_SEQRELA)
								Dbskip()
								Loop
							Endif
						Endif

						nTOHRHOM := 0.00
						Dbselectarea("STT")
						Dbsetorder(1)
						Dbseek(xFILIAL("STT")+STS->TS_ORDEM+STS->TS_PLANO)
						While !Eof() .And. STT->TT_FILIAL == xFILIAL("STT") .And.;
						STT->TT_ORDEM == STS->TS_ORDEM .And. STT->TT_PLANO == STS->TS_PLANO

							If &(cCONDSTT)

								If FindFunction( 'NGCalcHour' )

									nQTDHORAS := NGCalcHour( STT->TT_CODIGO, { STT->TT_DTINICI, STT->TT_HOINICI,;
									                         STT->TT_DTFIM, STT->TT_HOFIM }, STT->TT_USACALE )

								Else

									vVETHORAS := NGTQUATINS(STT->TT_CODIGO,STT->TT_TIPOREG,STT->TT_USACALE,;
									STT->TT_QUANTID,STT->TT_TIPOHOR,STT->TT_DTINICI,;
									STT->TT_HOINICI,STT->TT_DTFIM,STT->TT_HOFIM,STT->TT_UNIDADE)
									nQTDHORAS := vVETHORAS[1]

								EndIf

								nTOHRHOM += nQTDHORAS
							Endif
							Dbselectarea("STT")
							Dbskip()
						End

						MNTR730CHP(STS->TS_DTPRINI,STS->TS_HOPRINI,STS->TS_DTMRINI,;
						STS->TS_HOMRINI,STS->TS_DTPRFIM,STS->TS_HOPRFIM,;
						STS->TS_DTMRFIM,STS->TS_HOMRFIM)

						MNTR730TRB(STS->TS_CCUSTO,STS->TS_CODBEM,STS->TS_ORDEM,STS->TS_SERVICO,;
						STS->TS_SEQRELA,'STS',STS->TS_POSCONT,STS->TS_HOPRINI,STS->TS_DTPRFIM,STS->TS_HOPRFIM)
					Endif
				Endif
				Dbselectarea("STS")
				Dbskip()
			End
		Endif
	End
Return .t.

/*/


Ŀ
Funo    MNTR730CAL  Autor Incio Luiz Kolling    Data 14/10/2002
Ĵ
 DescrioCalcula as horas de disponiveis do calendario do bem        
ٱ
 Uso       MNTR730                                                    
ٱ


/*/
Static Function MNTR730CAL(cVBEM,cARQP)
	Local i := 0
	Dbselectarea("ST9")
	Dbsetorder(1)
	If !Dbseek(xFILIAL("ST9")+cVBEM)
		Msginfo(STR0020+" "+Alltrim(cVBEM)+" "+STR0021+" "+cARQP+" "+STR0022+" "+STR0023+" ST9.",STR0024)
		Return .f.
	Endif

	//Apura total de horas disponivel no periodo
	nPOS := aSCAN(aTRAB,{|X| X[1] == ST9->T9_CALENDA})
	If nPOS > 0
		nTHRDISP := aTRAB[nPOS][2]
	ELSE
		aDIAMAN := NG_H7(ST9->T9_CALENDA)
		If Empty(aDIAMAN)
			Msginfo(STR0025+" "+ST9->T9_CALENDA+" ";
			+STR0021+" "+STR0020+" "+Alltrim(cVBEM)+" "+STR0022+" "+STR0023+" SH7.",STR0024)
			Return .f.
		Endif
		dDIA  := MV_PAR01
		nDIAS := (MV_PAR02 - MV_PAR01) + 1
		nOCO  := MONTH(dDIA)
		nSOMA := 0
		FOR i := 1 TO nDIAS
			nSEM  := If(DOW(dDIA)==1,7,DOW(dDIA)-1)
			nSOMA += HTOM(aDIAMAN[nSEM][03])
			dDIA  ++
		NEXT
		nSOMA := MTOH(nSOMA)
		nPOS  := AT(":",nSOMA)
		If nPOS >0
			nPOS1 := SUBSTR(nSOMA,1,(nPOS-1))
			nPOS2 := SUBSTR(nSOMA,(nPOS+1))
			nTHRDISP := VAL(nPOS1) + (VAL(nPOS2)/100)
			nTHRDISP := If(nTHRDISP<0,0,nTHRDISP)
			AADD(aTRAB,{ST9->T9_CALENDA,nTHRDISP})
		Endif
	Endif
Return .t.

//-------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} MNTR730CHP
Calcula as horas de parada.
@type static

@author Incio Luiz Kolling
@since 14/10/2002

@sample MNTR730CHP( 24/04/2019, '21:00', 24/04/2019, '21:00', 25/04/2019, '00:53', 25/04/2019, '00:53' )

@param  dDTPRINI, Data    , Data de Parada Inicio.
@param  cHOPRINI, Caracter, Hora de Parada Inicio.
@param  dDTMRINI, Data    , Data Real Inicio da Manuteno.
@param  cHOMRINI, Caracter, Hora Real Inicio da Manuteno.
@param  dDTPRFIM, Data    , Data de Parada Fim.
@param  cHOPRFIM, Caracter, Hora de Parada Fim.
@param  dDTMRFIM, Data    , Data Real Fim da Manuteno.
@param  cHOMRFIM, Caracter, Hora Real Fim da Manuteno.
@return .T.
/*/
//-------------------------------------------------------------------------------------------------------
Static Function MNTR730CHP(dDTPRINI,cHOPRINI,dDTMRINI,cHOMRINI,dDTPRFIM,;
	cHOPRFIM,dDTMRFIM,cHOMRFIM)
	//Calcula Horas Paradas
	dDTINI    := dDTPRINI
	cHRINI    := cHOPRINI
	dDTPARINI := dDTPRINI
	dDTFIM    := dDTPRFIM
	cHRFIM    := cHOPRFIM

	nTOHRPAR := 0
	If !Empty(dDTINI) .and. !Empty(cHRINI) //Verifica se h tempo de parada para a O.S.
		If MV_PAR11 == 2 //Calcula as horas de parada considerando calendario do bem
			nTOHRPAR := TimeWork( dDTINI, cHRINI, dDTFIM, cHRFIM, ST9->T9_CALENDA )
		Else
			CALHORDAT()
			nTOHRPAR := nHORAS
		EndIf
	EndIf

	//Calcula Horas Espera
	dDTINI := dDTPRINI
	cHRINI := cHOPRINI

	If EMPTY(dDTPRINI)
		dDTINI := dDTMRINI
		cHRINI := cHOMRINI
	Endif
	dDTFIM := dDTMRINI
	cHRFIM := cHOMRINI

	If MV_PAR11 == 2 //Calcula as horas de espera considerando calendario do bem
		nTOHRESP := TimeWork( dDTINI, cHRINI, dDTFIM, cHRFIM, ST9->T9_CALENDA )
	Else
		CALHORDAT()
		nTOHRESP := nHORAS
	EndIf

	//Calcula Horas Manutencao
	dDTINI := dDTMRINI
	cHRINI := cHOMRINI
	dDTFIM := dDTMRFIM
	cHRFIM := cHOMRFIM

	If MV_PAR11 == 2 //Calcula as horas da manutencao considerando calendario do bem
		nTOHRMAN := TimeWork( dDTINI, cHRINI, dDTFIM, cHRFIM, ST9->T9_CALENDA )
	Else
		CALHORDAT()
		nTOHRMAN := nHORAS
	EndIf

Return .t.

/*/


Ŀ
Funo    MNTR730TRB  Autor Incio Luiz Kolling    Data 14/10/2002
Ĵ
 DescrioGrava o arquivo temporario                                  
ٱ
 Uso       MNTR730                                                    
ٱ


/*/
Static Function MNTR730TRB(cCCUSTO,cVBEM,cORDEM,cSERV,nSEQU,cARQ,nPOSC,cHrPrIni,dDtPrFim,cHrPrFim)

	Dbselectarea(cTRB)
	(cTRB)->(DbAppend())
	(cTRB)->CUSTO   := cCCUSTO
	(cTRB)->CODBEM  := cVBEM
	(cTRB)->ORDEM   := cORDEM
	(cTRB)->SERVIC  := cSERV
	(cTRB)->SEQREL  := nSEQU
	(cTRB)->DTPRIN  := dDTPARINI
	(cTRB)->HORPAR  := nTOHRPAR
	(cTRB)->HORESP  := nTOHRESP
	(cTRB)->HORMAN  := nTOHRMAN
	(cTRB)->HORHOM  := nTOHRHOM
	(cTRB)->HORDIS  := nTHRDISP
	(cTRB)->ARQUIV  := cARQ
	(cTRB)->POSCONT := nPOSC
	(cTRB)->HRPRIN  := cHrPrIni
	(cTRB)->DTPRFI  := dDtPrFim
	(cTRB)->HRPRFI  := cHrPrFim

Return .t.

/*/


Ŀ
Funo    CALHORDAT   Autor  Deivys Joenck         Data  19/11/01 
Ĵ
 Descrio Calcula horas entre duas datas                             
ٱ
 Uso       MNTR730                                                    
ٱ


/*/
STATIC Function CALHORDAT()
	nSOMA  := 0
	nHORAS := 0
	hINI   := (HTOM(cHRINI)/60)
	hFIM   := (HTOM(cHRFIM)/60)

	If dDTFIM > dDTINI
		nSOMA := ((dDTFIM-dDTINI)*24)
	Endif
	nHORAS := (hFIM+nSOMA)-hINI
RETURN .T.

/*/


Ŀ
Funo     NGCHKCART  Autor  Deivys Joenck         Data  27/11/01 
Ĵ
 Descrio Checa se foram digitados caracteres no parametro de sequen 
ٱ
 Uso       MNTR730                                                    
ٱ


/*/
Function NGCHKCART()
	Local x := 0
	If !Empty(MV_PAR09)
		For x:=1 To Len(AllTrim(MV_PAR09))
			If !Isdigit(Substr(MV_PAR09,x,1))
				return .F.
			Endif
		Next
	Endif
Return .T.