#INCLUDE "MNTA960.CH"
#INCLUDE "PROTHEUS.CH"

/*


ͻ
Programa   MNTA960  Autor  Felipe N. Welter     Data   14/05/08   
͹
Desc.      Programa que possibilita escolher de forma dinamica os     
           itens que serao mostrados com o recurso click da direita   
͹
Uso        Generico                                                   
ͼ


*/
Function MNTA960()
//Ŀ
//Guarda conteudo e declara variaveis padroes 
//
Local aNGBEGINPRM := NGBEGINPRM()

Private aRotina := MenuDef()
Private cCampoDes

Private cGETWHILE := "TQD_FILIAL == xFilial('TQD') .And. TQD_PROGRA == M->TQC_PROGRA"
Private cGETMAKE  := "TQC->TQC_PROGRA"
Private cGETKEY   := "M->TQC_PROGRA+M->TQD_SEQUEN+M->TQD_FUNCAO"
Private cGETALIAS := "TQD"

cTUDOOK   := "AllwaysTrue()"
cLINOK    := "MNTA960LIN()"

aCHOICE := {}
aVARNAO := {}
aRELAC  := {}

aGETNAO := {{"TQD_PROGRA" ,"M->TQC_PROGRA"}}
             //nao aparece  //valor que recebe

aAdd( aGETNAO, { 'TQD_DESESP' ,'M->TQD_DESPOR' })
aAdd( aGETNAO, { 'TQD_DESING' ,'M->TQD_DESPOR' })
cCampoDes := 'TQD_DESPOR'

bNGGRAVA  := {|| MNTA960INC() }
cCadastro := OemtoAnsi(STR0001) //"Programas do Click da Direita"

aCHKDEL := {}

//Ŀ
// Endereca a funcao de BROWSE                                  
//
dbSelectArea("TQC")
dbSetOrder(1)
dbSelectArea("TQD")
dbSetOrder(2)

mBrowse( 6, 1,22,75,"TQC")

//Ŀ
//Retorna conteudo de variaveis padroes       
//
NGRETURNPRM(aNGBEGINPRM)
Return


/*/


Ŀ
Funo    MNTA960INC Autor Felipe N. Welter        Data  19/05/08 
Ĵ
Descrio  Verificacao antes de gravar no sistema (botao de OK)       
Ĵ
 Uso       MNTA960                                                    
ٱ


/*/
Static Function MNTA960INC()

Local nSequen := aSCAN( aHEADER, { |x| AllTrim( Upper(x[2]) ) == "TQD_SEQUEN"})
Local nCnt1, nCnt2, nX, nDif
Local lFlag := lResp := .F.
Local aSequen := {}

nX := 1

If ALTERA .Or. INCLUI
	For nCnt1 := 1 To Len(aCols)
		If !(aCols[nCnt1][Len(aCols[nCnt1])])
			AADD(aSequen,nX)
			nX++
		EndIf
	Next

	For nCnt1 := 1 To Len(aSequen)
		For nCnt2 := 1 To Len(aCols)
			If aSequen[nCnt1] == Val(aCols[nCnt2][nSequen]) .And. !aCols[nCnt2][Len(aCols[nCnt2])]
				aSequen[nCnt1] := 0
			EndIf
		Next
	Next


	If !VerifArray(aSequen)
		If APMSGYESNO(STR0011+chr(13)+chr(10)+STR0012)
		//"O campo 'Sequencia' dos itens no apresenta uma sequncia ordenada."###"Deseja que o sistema ordene?"
			lResp := .T.
		EndIf
	EndIf

		While !VerifArray(aSequen)
		If lResp

			For nCnt1 := 1 To Len(aSequen)
				If aSequen[nCnt1] != 0
					For nCnt2 := 1 To Len(aCols)
						If (Val(aCols[nCnt2][nSequen]) > aSequen[nCnt1]) .And. !aCols[nCnt2][Len(aCols[nCnt2])]
							nDif := Val(aCols[nCnt2][nSequen]) - aSequen[nCnt1]
							aCols[nCnt2][nSequen] = CValToChar(Val(aCols[nCnt2][nSequen]) - nDif)
							If Len(aCols[nCnt2][nSequen]) == 1
								aCols[nCnt2][nSequen] := "0" + aCols[nCnt2][nSequen]
							EndIf
							lFlag := .T.
						EndIf
						If lFlag
							Exit
						EndIf
					Next
				If lFlag
					Exit
				EndIf
				EndIf
			Next

			lFlag := .F.
			nX := 1

			For nCnt1 := 1 To Len(aSequen)
				aSequen[nCnt1] := nX
				nX++
			Next

			For nCnt1 := 1 To Len(aSequen)
				For nCnt2 := 1 To Len(aCols)
					If aSequen[nCnt1] == Val(aCols[nCnt2][nSequen]) .And. !aCols[nCnt2][Len(aCols[nCnt2])]
						aSequen[nCnt1] := 0
					EndIf
				Next
			Next
		Else
			Exit
		EndIf
		End

EndIf

Return .T.

/*/


Ŀ
Funo    VerifArray Autor Felipe N. Welter        Data  19/05/08 
Ĵ
Descrio  Verifica se as posicoes do array estao todas OK            
Ĵ
Parametros1.aSequen - Array para verificacao								  
Ĵ
 Uso       MNTA960                                                    
ٱ


/*/
Static Function VerifArray(aSequen)

Local nCont

For nCont := 1 To Len(aSequen)
	If aSequen[nCont] != 0
		Return .F.
	EndIf
Next

Return .T.

/*/


Ŀ
Funo    MNTA960LIN Autor Felipe N. Welter        Data  14/05/08 
Ĵ
Descrio  Validacao da linha informada em TQD                        
Ĵ
 Uso       MNTA960                                                    
ٱ


/*/
Function MNTA960LIN()

Local nCont, nVal := nTot := 0
Local lRETSeq := lRETDes := lRET := .T.
Local nDesc := aSCAN( aHEADER, { |x| AllTrim( Upper(x[2]) ) == cCampoDes})
Local nSequen := aSCAN( aHEADER, { |x| AllTrim( Upper(x[2]) ) == "TQD_SEQUEN"})

If !(aCols[n][Len(aCols[n])]) //se nao estiver marcada para deletar

	For nCont := 1 To Len(aCols)
		If nCont <> n

			If Iif(Len(AllTrim(aCols[n][nSequen])) == 1,"0"+AllTrim(aCols[n][nSequen]),AllTrim(aCols[n][nSequen])) ==;
					 AllTrim(aCols[nCont][nSequen]) .And. !(aCols[nCont][Len(aCols[nCont])])
				lRETSeq := .F.
			EndIf

		EndIf
	Next

	If !lRETSeq
		ShowHelpDlg("NGATENCAO",{STR0013},2,{STR0014},2)
		//Help(" ",1,"NGATENCAO",,STR0013+Chr(13)+Chr(10)+; //"O valor de Sequencia j foi informado."
		//	  STR0014,3,1) //"Digite um valor diferente ou delete este item."
	EndIf


	If Empty(Trim(aCols[n][nDesc]))  //verifica se a descricao esta vazia
		lRETDes := .F.
	EndIf

	If !lRETDes
		Help(" ",1,"NGATENCAO",,STR0005,3,1) //"O campo Descricao deve ser preenchido."
	EndIf

	lRET := (lRETDes .And. lRETSeq)

EndIf

If Empty(aCols[n][nSequen])
	nVal := Val(ProxSequen(nSequen))
	If !aCols[n][Len(aCols[n])]
		aCols[n][nSequen] := CValToChar(nVal)
	EndIf
EndIf

If Len(Trim(aCols[n][nSequen])) == 1
	aCols[n][nSequen] := "0"+aCols[n][nSequen]
EndIf

Return lRET


/*/


Ŀ
Funo    ProxSequen Autor Felipe N. Welter        Data  19/05/08 
Ĵ
Descrio  Retorna o proximo valor para o campo TQD_SEQUEN (Sequencia)
Ĵ
Parametros 1.nSequen - Posicao do Campo no aCOLS							  
Ĵ
 Uso       MNTA960OLIN                                                
ٱ


/*/
Static Function ProxSequen(nSequen)

Local nCont,nCont2
Local lFlag := .F.
Local aArrayC := {}
Local aArrayN := {}

For nCont := 1 To Len(aCols)
	AADD(aArrayN,nCont)
	If !aCols[nCont][Len(aCols[nCont])]
		AADD(aArrayC,Val(aCols[nCont][nSequen]))
	EndIf
Next

For nCont := 1 To Len(aArrayN)
	For nCont2 := 1 To Len(aArrayC)
		If aArrayN[nCont] == aArrayC[nCont2]
			lFlag := .T.
		EndIf
	Next
	If !lFlag
		Return AllTrim(CValToChar(aArrayN[nCont]))
	Else
		lFlag := .F.
	EndIf
Next

Return 0


/*/


Ŀ
Funo     MNTA960I  Autor Felipe N. Welter        Data  20/05/08 
Ĵ
Descrio  Inclusao no cadastro de um 'programa' (TQC->TQC_PROGRA)    
Ĵ
 Uso       MNTA960                                                    
ٱ


/*/
Function MNTA960I()

Local nRET := 0

dbSelectArea("TQC")
dbSetOrder(1)
nRET := NGCAD02("TQC",,3)

Return nRET

/*/


Ŀ
Funo     MNTA960A  Autor Felipe N. Welter        Data  20/05/08 
Ĵ
Descrio  Alteracao do cadastro de um 'programa' (TQC->TQC_PROGRA)   
Ĵ
 Uso       MNTA960                                                    
ٱ


/*/
Function MNTA960A()

Local nRET := 0

dbSelectArea("TQC")
dbSetOrder(1)
If dbSeek(xFilial("TQC")+TQC->TQC_PROGRA)
   nRET := NGCAD02("TQC",Recno(),4)
EndIf

Return nRET

/*/


Ŀ
Programa  MenuDef    Autor  Felipe N. Welter       Data  20/05/08 
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function MenuDef()

Local aRotina := {{STR0006  ,"AxPesqui" , 0 , 1}   ,; //"Pesquisar"
                   {STR0007  ,"NGCAD02"  , 0 , 2}   ,; //"Visualizar"
                   {STR0008   ,"MNTA960I" , 0 , 3}   ,; //"Incluir"
                   {STR0009   ,"MNTA960A" , 0 , 4, 0},; //"Alterar"
                   {STR0010   ,"NGCAD02"  , 0 , 5, 3} } //"Excluir"
Return(aRotina)
