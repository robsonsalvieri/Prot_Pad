#INCLUDE "MNTr930.ch"
#include "Protheus.ch"
#Include "Topconn.ch"

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTR930
Lista abastecimento por filial

@author Soraia de Carvalho
@since 17/01/06
/*/
//------------------------------------------------------------------------------
Function MNTR930()

	//Armazena variaveis p/ devolucao (NGRIGHTCLICK)
	Local aNGBEGINPRM := NGBEGINPRM()
	Local aPesq 	  := {}
	// [LGPD] Se as funcionalidades, referentes à LGPD, podem ser utilizadas
	Local lLgpd := FindFunction( 'FWPDCanUse' ) .And. FwPdCanUse( .T. )
	Local aOfuCampos := {}
	Local aOfusc := {}

	If !FindFunction( 'MNTAmIIn' ) .Or. MNTAmIIn( 19, 95 )

		Private aRotina   := {{STR0001,"MNR930PES", 0, 1},;   //"Pesquisar"
							{STR0002,"MNR930VI" , 0, 2},;   //"Visualizar"
							{STR0003,"MNR930PA" , 0, 3, 0},; //"Parametros"
							{STR0004,"MNR930IM" , 0, 3, 0}}  //"Imprimir"

		Private cCadastro := OemtoAnsi(STR0005)   //"Relatorio de abastecimento por Filial"
		Private cPerg     := "MNR930"
		Private aPerg 	  :={}
		Private oTempTable
		Private cTRBB 	  := GetNextAlias()
		Private lOper 	  := If(AllTrim(GetNewPar("MV_NGOPER","N")) == "S",.T.,.F.)
		Private lKmPrimAb
		Private cContab	  := GetMv("MV_MCONTAB")
		Private vCampoCC  := {"CTT","CTT_CUSTO","CTT_OPERAC","CTT_FILIAL"}
		Private lPrimAbas := .f.
		Private nSizeFil  := If(FindFunction("FWSizeFilial"),FwSizeFilial(),Len(TQN->TQN_FILIAL))

		If lLgpd

			// [LGPD] Caso o usuário não possua acesso ao(s) campo(s), deve-se ofuscá-lo(s)
			aOfusc := FwProtectedDataUtil():UsrAccessPDField( __CUSERID, { 'TQF_NREDUZ', 'TQF_CNPJ', 'DA4_NOME', 'TQF_CIDADE'} )

			// Caso TQF_CNPJ não esteja dentro de aOfusc, ele deve ser ofuscado 
			If Ascan( aOfusc, { |x| AllTrim(x) == 'TQF_CNPJ' } ) == 0
				// aOfuCampos é o array utilizado no oBrowse para ser ofuscado
				Aadd( aOfuCampos, 'CNPJ')
			EndIf

			If Ascan( aOfusc, { |x| AllTrim(x) == 'DA4_NOME' } ) == 0
				Aadd( aOfuCampos, 'NOMMOT')
			EndIf

			If Ascan( aOfusc, { |x| AllTrim(x) == 'TQF_NREDUZ' } ) == 0
				Aadd( aOfuCampos, 'POSTO')
			EndIf

			If Ascan( aOfusc, { |x| AllTrim(x) == 'TQF_CIDADE' } ) == 0
				Aadd( aOfuCampos, 'CODMUN')
			EndIf
		
		EndIf

		If pergunte("MNR930",.T.)


			If cContab == "CON"
				vCampoCC := {"SI3","I3_CUSTO","I3_OPERAC","I3_FILIAL"}
			EndIf

			aPos1      := {15,1,95,315 }

			aDBFB := {}
				Aadd(aDBFB,{"VEIC  " , "C", 16,0})
				Aadd(aDBFB,{"PLACA"  , "C", 08,0})
				Aadd(aDBFB,{"DATABA" , "D", 08,0})
				Aadd(aDBFB,{"HRAABA" , "C", 05,0})
				Aadd(aDBFB,{"KM    " , "N", 09,0})
				Aadd(aDBFB,{"LITROS" , "N", 09,3})
				Aadd(aDBFB,{"VALUN " , "N", TAMSX3("TQN_VALUNI")[1]/*9*/,TAMSX3("TQN_VALUNI")[2]/*3*/})
				Aadd(aDBFB,{"VALABA" , "N", 30,3})
				Aadd(aDBFB,{"KMPERC" , "N", 09,0})
				Aadd(aDBFB,{"MEDIA " , "N", 15,2})
				Aadd(aDBFB,{"MEDESP" , "N", 09,2})
				Aadd(aDBFB,{"POSTO " , "C", 30,0})
				Aadd(aDBFB,{"CODMOT" , "C", 06,0})
				Aadd(aDBFB,{"NOMMOT" , "C", 40,0})
				Aadd(aDBFB,{"CODMUN" , "C", 15,0})
				Aadd(aDBFB,{"CNPJ"   , "C", 14,0})
				Aadd(aDBFB,{"TIPSER" , "C", 40,0})
				Aadd(aDBFB,{"CODBEM" , "C", 16,0})
				Aadd(aDBFB,{"CCUSTO" , "C", 20,0})
				Aadd(aDBFB,{"NOMFL"  , "C", 15,0})
				Aadd(aDBFB,{"USUARIO", "C", 15,0})
				Aadd(aDBFB,{"ORDENA" , "C", 08,0})
				Aadd(aDBFB,{"FILIAL" , "C", nSizeFil,0})
				Aadd(aDBFB,{"PRIMAB" , "L", 01,0})
				Aadd(aDBFB,{"CODCOM" , "C", 03,0})
				Aadd(aDBFB,{"UM"     , "C", 02,0})
				Aadd(aDBFB,{"TPCONTA", "C", 06,0})
				Aadd(aDBFB,{"TPMEDIA", "C", 01,0})

			//Instancia classe FWTemporaryTable
			oTempTable  := FWTemporaryTable():New( cTRBB, aDBFB )

			If lOper
				oTempTable:AddIndex( "Ind01" , {"TIPSER","FILIAL","CODCOM","VEIC","DATABA"} )
				oTempTable:AddIndex( "Ind02" , {"TIPSER","FILIAL","CODCOM","VEIC","ORDENA"} )
				aAdd(aPesq, {STR0013 + " + " + STR0055 + " + " + STR0058 + " + " + STR0015 + " + " + STR0018, {{"", "C", 255, 0, "", "@!"}}})
				aAdd(aPesq, {STR0013 + " + " + STR0055 + " + " + STR0058 + " + " + STR0015 + " + " + STR0057, {{"", "C", 255, 0, "", "@!"}}})
			Else
				oTempTable:AddIndex( "Ind01" , {"FILIAL","CODCOM","VEIC","DATABA"} )
				oTempTable:AddIndex( "Ind02" , {"FILIAL","CODCOM","VEIC","ORDENA"} )
				aAdd( aPesq, {STR0055 + " + " + STR0058 + " + " + STR0015 + " + " + STR0018, {{"", "C", 255, 0, "", "@!"}}})
				aAdd( aPesq, {STR0055 + " + " + STR0058 + " + " + STR0015 + " + " + STR0057, {{"", "C", 255, 0, "", "@!"}}})
			EndIf

			oTempTable:AddIndex("Ind03", {"CNPJ"})
			oTempTable:AddIndex("Ind04", {"VEIC"})
			oTempTable:AddIndex("Ind05", {"TIPSER","NOMFL","VEIC"}  )

			aAdd( aPesq, {STR0056 , {{"", "C", 14,0, "", X3Picture('TQF_CNPJ')}}})
			aAdd( aPesq, {STR0015 , {{"", "C", 16,0, "", "@!"}}})
			aAdd( aPesq, {STR0013 + " + " + STR0012 + " + " + STR0015, {{"", "C", 255, 0, "", "@!"}}})
			oTempTable:Create()

			aTRBB := {	{STR0012 ,"NOMFL"     ,"C", 02, 0, "@!"},;              //"Filial"
						{STR0055 ,"FILIAL"    ,"C", nSizeFil,0, "@!"},;         //"Cod. Filial"
						{STR0056 ,"CNPJ"      ,"C", 14, 0, "@!",,,,,, 'CNPJ'},; //"CNPJ"
						{STR0057 ,"ORDENA"    ,"C", 08, 0, "@!"},;              //"Ordena"
						{STR0058 ,"CODCOM"    ,"C", 03, 0, "@!"},;              //"Cod. Combustível"
						{STR0013 ,"TIPSER"    ,"C", 40, 0, "@!"},;              //"Operação"
						{STR0014 ,"USUARIO"   ,"C", 15, 0, "@!"},;              //"Usuario"
						{STR0015 ,"VEIC"      ,"C", 16, 0, "@!" },;             //"Veículo"
						{STR0016 ,"PLACA"     ,"C", 16, 0, "@!" },;             //"Placa"
						{STR0017 ,"NOMMOT"    ,"C", 40, 0, "@!",,,,,, 'NOMMOT' },;             //"Motorista"
						{STR0018 ,"DATABA"    ,"D", 08, 0, "@ 99/99/99" },;     //"Dat.Abst"
						{STR0019 ,"KM"        ,"N", 09, 0, "@R 999999999" },;   //"Contador"
						{STR0020 ,"LITROS"    ,"N", 09, 3, "@E 99,999.999" },;  //"Quantidade"
						{STR0021 ,"VALUN"     ,"N", TAMSX3("TQN_VALUNI")[1]/*9*/,TAMSX3("TQN_VALUNI")[2]/*3*/,;
						'@E 99,999.'+Replicate('9', TAMSX3("TQN_VALUNI")[2]) },;     //"Vlr.Unit"
						{STR0022 ,"VALABA"    ,"N", 30, 3, "@E 999,999,999,999.99" },;//"Vlr.Abast"
						{STR0023 ,"KMPERC"    ,"N", 06, 0, "@R 999999999" },; //"Qtd.Cont.Trab."
						{STR0024 ,"POSTO"     ,"C", 30, 0, "@!",,,,,, 'POSTO'},;            //"Posto"
						{STR0025 ,"MEDIA"     ,"N", 15, 3, "@E 99,999,999,999.999"},; //"Media"
						{STR0026 ,"MEDESP"    ,"N", 09, 2, "@E 999,999.99"},; //"Med.Esperada"
						{STR0027 ,"CODMUN"    ,"C", 15, 0, "@!",,,,,, 'CODMUN'}} //"Cidade"

			Processa({ |lEnd| MNR930INI()}, STR0010,STR0011 ) //"Aguarde..."###"Processando Registros..."
			DbSelectarea(cTRBB)
			DbGotop()
			If MV_PAR07 = 1
				DbSetOrder(1)
			Else
				DbSetOrder(2)
			EndIf

			oBrowse:= FWMBrowse():New()
			oBrowse:SetDescription(cCadastro)
			oBrowse:SetTemporary(.T.)
			oBrowse:SetAlias(cTRBB)
			oBrowse:SetFields(aTRBB)
			oBrowse:SetSeek(.T.,aPesq)
			If lLgpd
				// SetObfuscFields ofusca os campos do array especificado
				oBrowse:SetObfuscFields( aOfuCampos )
			EndIf
			oBrowse:Activate()

			oTempTable:Delete()

			DbSelectArea("TQN")
			DbSetOrder(01)
			Dbseek(xFilial("TQN"))
		Endif

	EndIf

	//Devolve variaveis armazenadas (NGRIGHTCLICK)
	NGRETURNPRM(aNGBEGINPRM)

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNTR903INI
Monta o arquivo temporario inicial mostrado no browse

@author Soraia de Carvalho
@since 17/01/06
/*/
//------------------------------------------------------------------------------
Function MNR930INI()
	Local aArea	   := GetArea()
	Local cFil     := "  "
	Local cNomeFil := " "
	Local lMMoeda  := NGCADICBASE("TL_MOEDA","A","STL",.F.) // Multi-Moeda

	Private cFiliIni := Mv_Par03
	Private dData
	Private cVeiculo
	Private nKMPerc
	Private nKM 	 :=0
	Private nABASVEI := 0
	lKmPrimAb := If(MV_PAR08 == 1,.T.,.F.)

	cAliasQry := "TETQN"

	cQuery := " SELECT A.TQN_FILIAL,A.TQN_PLACA,A.TQN_FROTA,A.TQN_DTABAS,A.TQN_HRABAS,A.TQN_HODOM,"
	cQuery += " A.TQN_QUANT,A.TQN_VALUNI,A.TQN_VALTOT,A.TQN_CODMOT,A.TQN_USUARI,A.TQN_CCUSTO,B.T9_CODBEM,B.T9_TPCONTA, "
	cQuery += " B.T9_CCUSTO,b.t9_CODFAMI,F.TQF_NREDUZ,F.TQF_CIDADE,F.TQF_CNPJ, F.TQF_CONVEN,A.TQN_CODCOM,TQM.TQM_UM,TQM.TQM_CONVEN"

	If lMMoeda
		cQuery += " ,A.TQN_MOEDA "
	Endif

	If lOper
		cQuery += " ,E.TSZ_DESSER"
	EndIf
	
	cQuery += " ,C.T6_TIPO1,C.T6_UNIDAD1,C.T6_MEDIA1"

	cQuery += " FROM " + RetSQLName("TQN") + " A "
	cQuery += " JOIN "+RetSQLName("ST9")+" B ON (b.t9_CODBEM = A.TQN_FROTA "

	cQuery += " AND " + NGMODCOMP("ST9","TQN","=") + ")"

	cQuery += " JOIN "+RetSQLName("ST6")+" C ON (C.t6_CODFAMI = b.t9_CODFAMI "

	cQuery += " AND " + NGMODCOMP("ST6","ST9","=") + ")"

	cQuery += " JOIN "+RetSQLName(vCampoCC[1])+" D ON (A.TQN_CCUSTO = D."+vCampoCC[2]

	If NGSX2MODO("TQN") <> 	'E'
		cQuery += " AND " + NGMODCOMP("TQN",vCampoCC[1],"=") + ")"
	Else
		cQuery += " AND " + NGMODCOMP(vCampoCC[1],"TQN","=") + ")"
	EndIf

	If lOper
		cQuery += "	LEFT JOIN " + RetSqlName("TSZ") + " E ON ("
		cQuery += " E.TSZ_CODSER = D."+vCampoCC[3]
		cQuery += " AND " + NGMODCOMP("TSZ",vCampoCC[1],"=") + ")"
		cQuery += " AND E.D_E_L_E_T_ <> '*'"
	EndIf
	cQuery += " JOIN "+RetSQLName("TQF")+" F ON (F.TQF_CODIGO = A.TQN_POSTO AND F.TQF_LOJA = A.TQN_LOJA "

	cQuery += " AND " + NGMODCOMP("TQF","TQN","=") + ")"

	cQuery += " LEFT JOIN "+RetSqlName("TQM")+" TQM ON (TQM.TQM_CODCOM = A.TQN_CODCOM AND TQM.TQM_CONVEN = F.TQF_CONVEN "

	cQuery += " AND " + NGMODCOMP("TQM","TQN","=") + ")"

	cQuery += " WHERE "

	If NGSX2MODO("TQN") == 	'E'
		cQuery += "A.TQN_FILIAL >= '"+cFILIINI+"' AND A.TQN_FILIAL <= '"+MV_PAR04+"'  AND "
	EndIf

	cQuery += " A.TQN_DTABAS>='"+DTOS(MV_PAR01)+"' "
	cQuery += " AND A.TQN_DTABAS<='"+DTOS(MV_PAR02)+"' AND A.TQN_FROTA BETWEEN '"+MV_PAR05+"' AND '"+MV_PAR06+"' "
	cQuery += " AND A.D_E_L_E_T_<>'*' AND B.D_E_L_E_T_<>'*' AND C.D_E_L_E_T_<>'*' "
	cQuery += " AND D.D_E_L_E_T_<>'*' AND F.D_E_L_E_T_<>'*' AND TQM.D_E_L_E_T_ <> '*' "
	cQuery += " ORDER BY A.TQN_FILIAL,A.TQN_PLACA"
	cQuery := ChangeQuery(cQuery)

	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

	ProcRegua(LastRec())
	dData := " "
	cVeiculo	:= " "
	nKM 		:= 0
	DbSelectArea(cALIASQRY)
	While !EoF()

		IncProc(STR0010,STR0011) //"Aguarde..."###"Processando Registros..."

		cFrota	:= (cAliasQry)->TQN_FROTA
		cFilBem	:= (cAliasQry)->TQN_FILIAL
		cHorAbas	:= (cAliasQry)->TQN_HRABAS
		dDatAbas	:= (cAliasQry)->TQN_DTABAS
		lPrimAbas := NGPRIABAS(cFrota,dDatAbas,cHorAbas) //verifica se eh o 1a abast. do veiculo
		If lPrimAbas .And. !lKmPrimAb
			nKm := 0
		Else
			nKM := NGCONTPER(cFrota,dDatAbas,cHorAbas)
		EndIf
		RestArea(aArea)

		RecLock((cTRBB), .T.)

		If (cAliasQry)->TQN_FILIAL <> cFil
			cFil := (cAliasQry)->TQN_FILIAL
			DbSelectArea("SM0")
			SM0->(DbSetOrder(1))
			If MsSeek(SM0->M0_CODIGO+(cAliasQry)->TQN_FILIAL)
				(cTRBB)->NOMFL := SM0->M0_FILIAL
				cNomeFil := SM0->M0_FILIAL
			EndIf
		Else
			(cTRBB)->NOMFL := cNomeFil
		EndIf
		(cTRBB)->FILIAL := (cAliasQry)->TQN_FILIAL
		(cTRBB)->CODBEM := (cAliasQry)->T9_CODBEM
		(cTRBB)->CCUSTO := (cAliasQry)->TQN_CCUSTO
		(cTRBB)->MEDESP := Posicione("TT8",1,xFilial("TT8", (cAliasQry)->TQN_FILIAL ) + (cAliasQry)->T9_CODBEM +;
							(cAliasQry)->TQN_CODCOM,"TT8->TT8_MEDIA") 
		If lOper
			(cTRBB)->TIPSER := (cAliasQry)->TSZ_DESSER
		EndIf
		(cTRBB)->POSTO  := (cAliasQry)->TQF_NREDUZ
		(cTRBB)->CODMUN := (cAliasQry)->TQF_CIDADE
		(cTRBB)->CNPJ   := (cAliasQry)->TQF_CNPJ
		(cTRBB)->NOMMOT := Posicione('DA4',1,xFilial("DA4", (cAliasQry)->TQN_FILIAL) + (cAliasQry)->TQN_CODMOT,'DA4_NOME', )
		(cTRBB)->VEIC   := (cAliasQry)->TQN_FROTA
		(cTRBB)->PLACA  := (cAliasQry)->TQN_PLACA
		(cTRBB)->DATABA := STOD((cAliasQry)->TQN_DTABAS)
		(cTRBB)->KM     := (cAliasQry)->TQN_HODOM
		(cTRBB)->LITROS := (cAliasQry)->TQN_QUANT
		(cTRBB)->VALUN  := If( lMMoeda, xMoeda((cAliasQry)->TQN_VALUNI,Val((cAliasQry)->TQN_MOEDA),nMoedarel,(cTRBB)->DATABA,TAMSX3("TQN_VALUNI")[2]), (cAliasQry)->TQN_VALUNI )
		(cTRBB)->VALABA := If( lMMoeda, xMoeda((cAliasQry)->TQN_VALTOT,Val((cAliasQry)->TQN_MOEDA),nMoedarel,(cTRBB)->DATABA,TAMSX3("TQN_VALTOT")[2]), (cAliasQry)->TQN_VALTOT )
		(cTRBB)->KMPERC := nKM
		(cTRBB)->CODMOT := (cAliasQry)->TQN_CODMOT
		(cTRBB)->TPMEDIA := Posicione('ST6',1, xFilial("ST6", (cAliasQry)->TQN_FILIAL) + (cAliasQry)->T9_CODFAMI,'T6_MEDIA1')
		If (cTRBB)->TPMEDIA == "1" .Or. Empty((cTRBB)->TPMEDIA)
			(cTRBB)->MEDIA  := If(lPrimAbas,0,((cTRBB)->KMPERC / (cAliasQry)->TQN_QUANT))
			(cTRBB)->TPCONTA:= If((cTRBB)->TPMEDIA == "1",AllTrim((cAliasQry)->T6_TIPO1)+"/"+(cAliasQry)->T6_UNIDAD1,SubStr((cAliasQry)->T9_TPCONTA,1,2))
		ElseIf (cTRBB)->TPMEDIA == "2"
			(cTRBB)->MEDIA  := If(lPrimAbas,0,((cAliasQry)->TQN_QUANT / (cTRBB)->KMPERC))
			(cTRBB)->TPCONTA:= AllTrim((cAliasQry)->T6_UNIDAD1)+"/"+(cAliasQry)->T6_TIPO1
		EndIf
		(cTRBB)->TPCONTA := AllTrim((cTRBB)->TPCONTA)
		(cTRBB)->USUARIO:= ALLTRIM((cAliasQry)->TQN_USUARI)
		(cTRBB)->ORDENA := INVERTE(STOD((cAliasQry)->TQN_DTABAS))
		(cTRBB)->HRAABA := (cAliasQry)->TQN_HRABAS
		(cTRBB)->PRIMAB := lPrimAbas
		(cTRBB)->CODCOM := (cAliasQry)->TQN_CODCOM
		(cTRBB)->UM     := (cAliasQry)->TQM_UM
		MsUnLock(cTRBB)
		DbSelectArea(cALIASQRY)
		DbSkip()
	End
	(cALIASQRY)->(dbCloseArea())

	DbSelectArea(cTRBB)
	DbGoTop()
	If Reccount() = 0
		MsgInfo(STR0028,STR0029) //"Não existe dados para montar a tela de consulta."###"ATENÇÃO"
		Return .F.
	EndIf

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNR930PA
Reprocessa o browse de acordo com os parametros

@author Soraia de Carvalho
@since 17/01/06
/*/
//------------------------------------------------------------------------------
Function MNR930PA()

	If Pergunte("MNR930",.T.)

		DbSelectArea(cTRBB)
		Zap

		Processa({ |lEnd| MNR930INI() }, STR0030 )  // //"Aguarde ..Processando Arquivo de Filiais"

		DbSelectArea(cTRBB)
		DbGotop()
		If MV_PAR07 = 1
			DbSetOrder(1)
		Else
			DbSetOrder(2)
		EndIf

	EndIf

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNR930vi
Visualiza consulta

@author Soraia de Carvalho
@since 17/01/06
/*/
//------------------------------------------------------------------------------
Function MNR930VI()

	Local aAlias := GetArea()

	DbSelectArea("TQN")
	DbSetOrder(01)
	DbSeek((cTRBB)->FILIAL+(cTRBB)->VEIC+DTOS((cTRBB)->DATABA)+(cTRBB)->HRAABA)
	NgCad01("TQN",Recno(),1)

	Restarea(aAlias)

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR930  ³ Autor ³ Soraia de Carvalho    ³ Data ³17/01/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio de Abastecimento por Filiais                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³TQF - Estrutura Organizacional                              ³±±
±±³          ³TQN - Usuarios.                                             ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNR930IM()
	Local cString    := "TQN"
	Local cDesc1     := STR0005 //"Relatorio de abastecimento por filial"
	Local cDesc2     := STR0031  //"O usuario podera utilizar a opcao Parametros para"
	Local cDesc3     := STR0032 //"a obtencao da selecao desejada."
	Local wnRel      := "MNTR930"

	Private aReturn  := { STR0033, 1,STR0034 , 2, 2, 1, "",1 }     //### //"Administracao"###"Zebrado"
	Private nLastKey := 0
	Private Titulo   := STR0035 //"Relatório de Abastecimento por Filial"
	Private Tamanho  := "G"
	LIMITE  := 220

	DbSelectArea("TQN")

	//Envia controle para a funcao SETPRINT
	DbSelectArea(cTRBB)
	DbGotop()
	If Reccount() > 0
		wnRel := SetPrint(cString,wnRel,,Titulo,cDesc1,cDesc2,cDesc3,.f.,"")
		If nLastKey = 27
			Set Filter To
			DbSelectArea("TQN")
			Return
		Endif
		SetDefault(aReturn,cString)
		RptStatus({|lEnd| R930Imp(@lEnd,wnRel,Titulo,Tamanho)},Titulo)
		DbSelectArea("TQN")
	Else
		MsgInfo(STR0036,STR0029) //"Nao exite dados para imprimir o relatório."###"ATENÇÃO"
	EndIf

Return Nil
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R930IMP  ³ Autor ³ Soraia de Carvalho    ³ Data ³17/01/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Chamada do Relatorio.                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R930Imp(lEnd,wnRel,Titulo,Tamanho)
	Local cRodaTxt	:= ''
	Local nCntImpr	:= 0, cTpMed := ""
	Local cTpConta	:= ""
	Local nLenMedia	:= 0
	Local nMoedarel	:= 1
	Local nTamValUni:= TAMSX3( 'TQN_VALUNI' )[ 2 ]
	Local cPicValUni:= '@E 9,999,999.' + Replicate( '9', nTamValUni )
	// [LGPD] Se as funcionalidades, referentes à LGPD, podem ser utilizadas
	Local lLgpd := FindFunction( 'FWPDCanUse' ) .And. FwPdCanUse( .T. )
	Local lCidade := .F.
	Local lNreduz := .F.
	Local lNomeMot:= .F. 
	Local aOfusc := {}

	Private li := 80 ,m_pag := 1
	Private NomeProg	:= "MNTR930"
	Private Cabec1	:= "" //STR0037 //"Veiculo          Data       Posto                     Motorista                               KM  Lts Abast. Val.Unit.   Vlr.Abst.     KMPerc.      Media Média Esp. Usuario         Cidade            C.Custo"
	Private Cabec2	:= ""
	Private cCabec1	:= STR0037 //"Veiculo          Data       Posto                     Motorista                         Contador  Qtd.Abast. Val.Unit.   Vlr.Abst.  Cont.Trab. Média Km/l  Média Esp. Usuario         Cidade            C.Custo"
	Private cCabec2	:= ""
	Private cCabec930	:= " "
	Private cOPER		//CODIGO DA OPERACAO
	Private cCODFL	:= "  " //NOME FILIAL
	Private cComb		:= Space(3)
	Private cTp		:= Space(1)
	Private lVerOpT	:= .F.
	Private nQtdMd	:= 0
	Private nTOTLIT	:= 0000.00
	Private nTOTVLABA	:= 000.00
	Private nTOTKM	:= 000000
	Private nTOTMD	:= 00000.00
	Private nOpTOTLIT	:= 0, nOpTOTVLABA := 0, nOpTOTKM := 0
	Private lPrim		:= .f., nLtPri := 0 , nLtPriT := 0

	If lLgpd
		// [LGPD] Caso o usuário não possua acesso ao(s) campo(s), deve-se ofuscá-lo(s)
		aOfusc := FwProtectedDataUtil():UsrAccessPDField( __CUSERID, { 'TQF_CIDADE', 'TQF_NREDUZ', 'DA4_NOME' } )
		lCidade := Ascan( aOfusc, { |x| AllTrim(x) == 'TQF_CIDADE' } ) == 0
		lNreduz:= Ascan( aOfusc, { |x| AllTrim(x) == 'TQF_NREDUZ' } ) == 0
		lNomeMot:= Ascan( aOfusc, { |x| AllTrim(x) == 'DA4_NOME' } ) == 0
	EndIf

	// Caso o update de multi-moeda esteja aplicado e haja a funcao de criacao da pergunta
	If NGCADICBASE("TL_MOEDA","A","STL",.F.) .And. FindFunction("MNTR750MD")
		nMoedarel := MNTR750MD()
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se deve comprimir ou nao                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nTipo  := IIf(aReturn[4]==1,15,18)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta os Cabecalhos                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	/*/
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         0         1         2
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567
	************************************************************************************************************************************************************************************************************************************
	Veiculo          Data       Posto                     Motorista                               KM  Lts Abast. Val.Unit.   Vlr.Abst.     KMPerc.      Media Média Esp. Usuario         Cidade            C.Custo
	Veiculo          Data       Posto                     Motorista                         Contador  Qtd.Abast. Val.Unit.   Vlr.Abst.  Cont.Trab. Média Km/l  Média Esp. Usuario         Cidade            C.Custo
	************************************************************************************************************************************************************************************************************************************
	Operação:

	xxxxxxxxxxxxxxxx 99/99/9999 xxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 999,999,999 999,999,999 99,999.999 999,999.99 999,999,999 99,999.999 99,999.999 xxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxx

	/*/

	//----------------------------------------
	// Define o indice de leitura do arquivo
	//----------------------------------------

	DbSelectArea(cTRBB)
	If MV_PAR07 = 1
		DbSetOrder(1)
	Else
		DbSetOrder(2)
	EndIf

	SetRegua(LastRec())
	DbGoTop()

	While !Eof()

		If lEnd
			@ Prow()+1,001 PSay STR0038  //"CANCELADO PELO OPERADOR"
			Exit
		EndIf
		IncRegua()
		If lOper
			cCond := "(cTRBB)->NOMFL <> cCODFL .Or. (cTRBB)->TIPSER <> cOPER .Or. (cTRBB)->CODCOM <> cComb .Or. (cTRBB)->TPCONTA <> cTp"
		Else
			cCond := "(cTRBB)->NOMFL <> cCODFL .Or. (cTRBB)->CODCOM <> cComb .Or. (cTRBB)->TPCONTA <> cTp"
		EndIf
		If &cCond
			//Veiculo          Data       Posto                     Motorista                         Contador  Qtd.Abast. Val.Unit.   Vlr.Abst.  Cont.Trab. Média Km/l  Média Esp. Usuario         Cidade            C.Custo
			cTpConta  := AllTrim((cTRBB)->TPCONTA)
			cCabec930 := SubStr(cCABEC1,1,149)+(cTRBB)->TPCONTA+SubStr(cCABEC1,155,65)
			
			nLenMedia := Len(cTpConta)-1
			SomaLinha()
			SomaLinha()
			@ Li,000 Psay cCabec930
			SomaLinha()
			@ Li,000 Psay __PrtThinLine()
			If lOper
				SomaLinha()
				@ Li,000 PSay STR0039 //"Operação:"
				@ Li,010 PSay (cTRBB)->TIPSER
				cOPER := (cTRBB)->TIPSER
			EndIf

			SomaLinha()
			@ Li,000 PSay STR0040 //"Filial:"
			@ Li,010 PSay (cTRBB)->NOMFL
			cCODFL := (cTRBB)->NOMFL
			SomaLinha()
			@ Li,000 PSay STR0052 //"Combustível:"
			@ Li,013 PSay NGSEEK('TQM',(cTRBB)->CODCOM,1,'TQM_NOMCOM')
			cComb := (cTRBB)->CODCOM
			cTp   := (cTRBB)->TPCONTA
			SomaLinha()
			@ Li,000 Psay REPLICATE(chr(45),220)
			//SomaLinha()
		EndIf
		If lOper
			If (cTRBB)->TIPSER = cOPER  //Imprime por operacao
				SomaLinha()
				@ Li,000 PSay (cTRBB)->VEIC
				@ Li,017 PSay (cTRBB)->DATABA
				If lNreduz
					// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
					@ Li,028 PSay FwProtectedDataUtil():ValueAsteriskToAnonymize( SubStr((cTRBB)->POSTO,1,25) )
				Else
					@ Li,028 PSay SubStr((cTRBB)->POSTO,1,25)
				EndIf
				If lNomeMot
					// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
					@ Li,054 PSay FwProtectedDataUtil():ValueAsteriskToAnonymize( SubStr((cTRBB)->NOMMOT,1,30) )
				Else
					@ Li,054 PSay SubStr((cTRBB)->NOMMOT,1,30)
				EndIf
				@ Li,084 PSay (cTRBB)->KM     Picture "@E 999,999,999"
				@ Li,098 PSay (cTRBB)->LITROS Picture "@E 99,999.999" // "@E 999,999,999"
				@ Li,108 - nTamValUni PSay (cTRBB)->VALUN Picture cPicValUni
				@ Li,112 PSay (cTRBB)->VALABA Picture "@E 999,999,999,999.99"
				@ Li,130 PSay (cTRBB)->KMPERC Picture "@E 999,999,999"
				@ Li,141 + nLenMedia PSay (cTRBB)->MEDIA Picture "@E 9,999.999"
				@ Li,157 Psay (cTRBB)->MEDESP Picture "@E 9,999.999"
				@ Li,168 Psay (cTRBB)->USUARIO
				If lCidade
					// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
					@ Li,181 PSay FwProtectedDataUtil():ValueAsteriskToAnonymize( (cTRBB)->CODMUN )
				Else
					@ Li,181 PSay (cTRBB)->CODMUN
				EndIf
				@ Li,202 PSay (cTRBB)->CCUSTO
				If (cTRBB)->PRIMAB .And. lKmPrimAb
					@ Li,220 PSay "*"
					lPrim := .T.
					nLtPri += (cTRBB)->LITROS
					nLtPriT += (cTRBB)->LITROS
				EndIf

				nOpTOTLIT   += (cTRBB)->LITROS
				nOpTOTVLABA += (cTRBB)->VALABA
				nOpTOTKM    += (cTRBB)->KMPERC
				cTpMed      := (cTRBB)->TPMEDIA

				nTOTLIT   += (cTRBB)->LITROS
				nTOTVLABA += (cTRBB)->VALABA
				nTOTKM    += (cTRBB)->KMPERC
				nTOTMD    += (cTRBB)->MEDIA
				nQtdMd++
			EndIf
		Else
			SomaLinha()
			@ Li,000 PSay (cTRBB)->VEIC
			@ Li,017 PSay (cTRBB)->DATABA
			If lNreduz
				// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
				@ Li,028 PSay FwProtectedDataUtil():ValueAsteriskToAnonymize( SubStr((cTRBB)->POSTO,1,25) )
			Else
				@ Li,028 PSay SubStr((cTRBB)->POSTO,1,25)
			EndIf
			If lNomeMot
				// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
				@ Li,054 PSay FwProtectedDataUtil():ValueAsteriskToAnonymize( SubStr((cTRBB)->NOMMOT,1,30) )
			Else
				@ Li,054 PSay SubStr((cTRBB)->NOMMOT,1,30)
			EndIf
				@ Li,084 PSay (cTRBB)->KM     Picture "@E 999,999,999"
				@ Li,098 PSay (cTRBB)->LITROS Picture "@E 99,999.999" // "@E 999,999,999"
				@ Li,108 - nTamValUni PSay (cTRBB)->VALUN Picture cPicValUni
				@ Li,112 PSay (cTRBB)->VALABA Picture "@E 999,999,999,999.99"
				@ Li,130 PSay (cTRBB)->KMPERC Picture "@E 999,999,999"
				@ Li,141 + nLenMedia PSay (cTRBB)->MEDIA Picture "@E 9,999.999"
				@ Li,157 Psay (cTRBB)->MEDESP Picture "@E 9,999.999"
				@ Li,168 Psay (cTRBB)->USUARIO
			If lCidade
				// ValueAsteriskToAnonymize substitui o valor do campo para ******* 
				@ Li,181 PSay FwProtectedDataUtil():ValueAsteriskToAnonymize( (cTRBB)->CODMUN )
			Else
				@ Li,181 PSay (cTRBB)->CODMUN
			EndIf
			@ Li,202 PSay (cTRBB)->CCUSTO
			If (cTRBB)->PRIMAB .And. lKmPrimAb
				@ Li,226 PSay "*"
				lPrim := .t.
				nLtPri += (cTRBB)->LITROS
				nLtPriT += (cTRBB)->LITROS
			EndIf

			nOpTOTLIT   += (cTRBB)->LITROS
			nOpTOTVLABA += (cTRBB)->VALABA
			nOpTOTKM    += (cTRBB)->KMPERC
			cTpMed      := (cTRBB)->TPMEDIA

			nTOTLIT   += (cTRBB)->LITROS
			nTOTVLABA += (cTRBB)->VALABA
			nTOTKM    += (cTRBB)->KMPERC
			nTOTMD    += (cTRBB)->MEDIA
			nQtdMd++

		EndIf
		DbSelectArea(cTRBB)
		DbSkip()
		//totais operacao
		If lOper
			If (cTRBB)->TIPSER <> cOPER .Or. (cTRBB)->CODCOM <> cComb .Or. (cTRBB)->TPCONTA <> cTp
				SomaLinha()
				SomaLinha()
				@ Li,055 Psay STR0043+AllTrim(cOPER)  //"Total Operação: "
				@ Li,097 Psay nOpTOTLIT   Picture "@E 999,999.999" //"@E 99,999,999,999"
				@ Li,118 Psay nOpTOTVLABA Picture "@E 999,999,999.99"
				@ Li,129 Psay nOpTOTKM    Picture "@E 999,999,999,999"
				If !Empty(cTpMed)
					If cTpMed == "1"
						@ Li,143+nLenMedia Psay (nOpTOTKM / (nOpTOTLIT - nLtPri)) Picture "@E 9,999.999"
					ElseIf cTpMed == "2"
						@ Li,143+nLenMedia Psay ((nOpTOTLIT - nLtPri) / nOpTOTKM ) Picture "@E 9,999.999"
					EndIf
				Else
					@ Li,143+nLenMedia Psay (nOpTOTKM / (nOpTOTLIT - nLtPri)) Picture "@E 9,999.999"
				EndIf
				SomaLinha()
				If lPrim .And. lKmPrimAb
					SomaLinha()
					@ Li,000 Psay STR0053 //"* Primeiro abastecimento do veículo, portanto a média de Km percorrido não é calculada."
					lPrim := .f.
				EndIf
				nOpTOTLIT   := 0
				nOpTOTVLABA := 0
				nOpTOTKM    := 0
				nLtPri      := 0
				cTpMed      := " "
			EndIf
		Else
			If (cTRBB)->CODCOM <> cComb .Or. (cTRBB)->TPCONTA <> cTp
				SomaLinha()
				SomaLinha()

				@ Li,055 Psay STR0054 //"Total: "
				@ Li,097 Psay nOpTOTLIT   Picture "@E 999,999.999" //"@E 99,999,999,999"
				@ Li,114 Psay nOpTOTVLABA Picture "@E 999,999,999,999.99"

				SomaLinha()
				If lPrim .And. lKmPrimAb
					SomaLinha()
					@ Li,000 Psay STR0053 //"* Primeiro abastecimento do veículo, portanto a média de Km percorrido não é calculada."
					lPrim := .f.
				EndIf
				nOpTOTLIT   := 0
				nOpTOTVLABA := 0
				nOpTOTKM    := 0
				nLtPri      := 0
			EndIf
		EndIf
	EndDo

	If (cTRBB)->(RecCount()) > 0

		SomaLinha()
		SomaLinha()
		@ Li,055 Psay STR0041 //"Total Geral:"
		@ Li,114 Psay nTOTVLABA Picture "@E 999,999,999,999.99"

		If lPrim	.And. lKmPrimAb
			SomaLinha()
			@ Li,000 Psay STR0053 //"* Primeiro abastecimento do veículo, portanto a média de Km percorrido não é calculada."
		EndIf
	EndIf

	Roda(nCntImpr,cRodaTxt,Tamanho)

	//Devolve a condicao original do arquivo principal
	RetIndex('TQN')

	Set Filter To

	Set device to Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	Endif
	MS_FLUSH()

Return NIL
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |MNR930FL  | Autor ³Marcos Wagner Junior   ³ Data ³ 01/08/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o |Valida codigos de Filial, Ate Filial 					        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR140                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNR930FL(nOpc,cParDe,cParAte)

	Local nSizeFil := TamSX3("TQN_FILIAL")[1]

	If Empty(cParDe) .AND. (cParAte == Replicate('Z', nSizeFil))
		Return .t.
	Else
		If nOpc == 1
			If Empty(cParDe)
				Return .t.
			Else
				lRet := IIf(Empty(cParDe),.t.,ExistCpo('SM0',SM0->M0_CODIGO+cParDe))
				If !lRet
					Return .f.
				EndIf
			Endif
		Elseif nOpc == 2
			If (cParAte == Replicate('Z', nSizeFil))
				Return .t.
			Else
				lRet := IIF(ATECODIGO('SM0',SM0->M0_CODIGO+cParDe,SM0->M0_CODIGO+cParAte,10),.T.,.F.)
				If !lRet
					Return .f.
				EndIf
			Endif
		EndIf
	EndIf

Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNR930Pes ³ Autor ³ Rafael Diogo Richter ³ Data ³30/05/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Monta a tela de pesquisa especifica.                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGF                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNR930Pes()

	Local cORDEM930 := Space(Len(TQN->TQN_FROTA))

	Define MsDialog oDlg5 From 300,120 To 390,330 Title OemToAnsi(STR0001) Pixel  //"Pesquisar"

	@ 0.5,1 say OemtoAnsi(STR0015) //"Veículo"
	@ 0.5,5 Msget cORDEM930 Picture '@!' SIZE 40,10

	Define sButton From 030,046 Type 1 Enable Of oDlg5 Action MNR930Rec(cORDEM930)
	Define sButton From 030,075 Type 2 Enable Of oDlg5 Action oDlg5:End()

	Activate MsDialog oDlg5 Centered

	DbSelectArea(cTRBB)
	If MV_PAR07 = 1
		DbSetOrder(1)
	Else
		DbSetOrder(2)
	EndIf


Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNR930Rec ³ Autor ³ Thiago Olis Machado  ³ Data ³30/05/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Monta a tela de pesquisa especifica.                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGF                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNR930Rec(cORDEM930)
	Local i,n:= 1

	DbSelectArea(cTRBB)
	DbSetOrder(4)
	n:= len(AllTrim(cordem930))
	For i := n To 16
		cOrdem930 := StrZero(Val(AllTrim(cOrdem930)),i)
		If DbSeek(cORDEM930)
			Exit
		EndIf
	Next i
	oDlg5:End()

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNT930DT  ³ Autor ³Soraia de Carvalho     ³ Data ³ 25/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Valida o parametro ate data                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR930                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNT930DT()

	If  MV_PAR02 < MV_PAR01
		MsgStop(STR0042)  //"Data final não pode ser inferior à data inicial!"
		Return .F.
	EndIf

Return .T.
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³ SomaLinha³ Autor ³ Inacio Luiz Kolling   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ SomaLinha()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR630                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Static Function Somalinha()
	Li++
	If Li > 58 .And. Li <> 81
		Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
		@ Li,000 Psay cCabec930
		SomaLinha()
		@ Li,000 Psay __PrtThinLine()
		Somalinha()
	EndIf
	If Li == 81
		Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
	EndIf
Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNR930BEM
Validação do parâmetro de Bem

@author Elynton Fellipe Bazzo
@since 23/02/15
@version P11/P12
@return Boolean lRet: indica retorno lógico da validação
/*/
//---------------------------------------------------------------------
Function MNR930BEM()

	Local lRet := .T.

	lRet := If( AteCodigo( "ST9",mv_par05,mv_par06 ),.T.,.F. )

Return lRet
