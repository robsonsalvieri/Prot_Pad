#INCLUDE "mntr135.ch"
#INCLUDE "PROTHEUS.CH"
/*/


Ŀ
Funo    MNTR135    Autor  Evaldo Cevinscki Jr.   Data 16/10/2007
Ĵ
Descrio Relatorio de Estimativa de Km a Rodar por Tipo de Modelo    
Ĵ
 Uso      Generico                                                    
ٱ


*/      
Function MNTR135()                                      

	WNREL   := "MNTR135"
	LIMITE  := 132
	cDESC1  := STR0001 //"Relatrio de Estimativa de Km a Rodar por Tipo de Modelo"
	cDESC2  := STR0016 //"e por Centro de Custos, quebrando por filial."
	cDESC3  := " "
	cSTRING := "ST9"

	Private NOMEPROG := "MNTR135"
	Private TAMANHO  := "M"
	Private aRETURN  := {STR0002,1,STR0003,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private aVETINR  := {}
	Private cPERG    := "MNR135"
	Private CABEC1,CABEC2            
	SetKey( VK_F9, { | | NGVersao( "MNTR135" , 1 ) } )

	Pergunte(cPERG,.F.)  

	TITULO := STR0001 //"Relatrio de Estimativa de Km a Rodar por Tipo de Modelo"

	//Envia controle para a funcao SETPRINT
	WNREL := SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("ST9")
		Return
	EndIf     
	SetDefault(aReturn,cSTRING)
	RptStatus({|lEND| MNTR135IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0009,STR0010)  //"Aguarde..."###"Processando Registros..."
	Dbselectarea("ST9")

Return .T.    

/*/


Ŀ
Funo    MNR135IMP  Autor  Evaldo Cevinscki Jr.   Data 16/10/2007
Ĵ
Descrio Chamada do Relatrio                                        
Ĵ
 Uso      MNTR135                                                     
ٱ


/*/
Function MNTR135IMP(lEND,WNREL,TITULO,TAMANHO)
	Local cTipMod := " "
	Private cRODATXT := ""
	Private nCNTIMPR := 0     
	Private li       := 80, m_pag := 1

	nTIPO  := IIf(aReturn[4]==1,15,18)                                                                                                                                                                                               
	CABEC1 := STR0011+AllTrim(Str(MV_PAR05))+STR0012 //"Tipo Modelo  Desc.Modelo            Fabricante            Variao Dia Mdia(Km)  Estimativa Km nos prximos "###" Dias"
	CABEC2 := ""   

	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3       
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	*************************************************************************************************************************************
	Relatrio de Estimativa de Km a Rodar por Modelo de Veculo
	**************************************************************************************************************************************
	Tipo Modelo  Desc.Modelo            Fabricante            Centro de Custo       Variao Dia Mdia  Kms a Rodar prximos n Dias  
	**************************************************************************************************************************************
	Filial: XX - XXXXXXXXXXXXXXXXXXXX
	xxxxxxxxxx   xxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx	          999,999                       999,999
	xxxxxxxxxx   xxxxxxxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxx	          999,999                       999,999

	*/                              

	cAliasQry := GetNextAlias()  
	cQuery := " SELECT ST9.T9_FILIAL,ST9.T9_TIPMOD,"
	cQuery += " (SELECT TQR.TQR_DESMOD FROM "+RetSqlName("TQR")+" TQR WHERE TQR.TQR_TIPMOD = ST9.T9_TIPMOD AND TQR.D_E_L_E_T_ <> '*') AS MODELO,"
	cQuery += " (SELECT ST7.T7_NOME FROM "+RetSqlName("ST7")+" ST7 WHERE ST7.T7_FABRICA = 
	cQuery += " (SELECT TQR.TQR_FABRIC FROM "+RetSqlName("TQR")+" TQR WHERE TQR.TQR_TIPMOD = ST9.T9_TIPMOD AND TQR.D_E_L_E_T_ <> '*')
	cQuery += " AND ST7.D_E_L_E_T_ <> '*') AS FABRICA,"
	cQuery += " (AVG(ST9.T9_VARDIA)) AS VARDIA,(AVG(ST9.T9_VARDIA) * "+Alltrim(Str(mv_par05))+") AS ESTIMATIVA,ST9.T9_CCUSTO " 
	cQuery += " FROM "+RetSqlName("ST9")+" ST9 "
	cQuery += " WHERE ST9.T9_TIPMOD <> ' ' AND ST9.T9_TEMCONT <> 'N' "  
	If MV_PAR06 <> 5
		cQuery += " AND ST9.T9_CATBEM = '"+AllTrim(Str(mv_par06))+"' " 
	EndIf	
	cQuery += " AND ST9.T9_TIPMOD BETWEEN '"+mv_par03+"' AND '" +mv_par04+"' "
	cQuery += " AND ST9.T9_FILIAL BETWEEN '"+mv_par01+"' AND '" +mv_par02+"' "
	cQuery += " AND ST9.T9_SITBEM ='A' AND ST9.D_E_L_E_T_ <> '*' "
	cQuery += " GROUP BY ST9.T9_FILIAL,ST9.T9_TIPMOD,ST9.T9_CCUSTO "
	cQuery += " ORDER BY ST9.T9_FILIAL,ST9.T9_TIPMOD,ST9.T9_CCUSTO"

	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)                                 

	SetRegua(LastRec())                                                             
	DbGoTop()           

	If Eof()
		MsgInfo(STR0013,STR0014)  //"No existem dados para montar o relatrio."###"ATENO"
		Return .F.
	Endif

	While !Eof()
		cFilQuer := (cAliasQry)->T9_FILIAL
		DbSelectArea("SM0")
		SM0->(DbSetOrder(1))
		MsSeek(cEMPANT+cFilQuer)

		NgSomaLi(58)
		@ Li,000 PSay STR0015+" : "+cFilQuer+" - "+SM0->M0_FILIAL //"Filial"
		DbSelectArea(cAliasQry)
		While !Eof() .And. (cAliasQry)->T9_FILIAL = cFilQuer
			IncRegua()
			NgSomaLi(58)
			If cTipMod <> (cAliasQry)->T9_TIPMOD        
				cTipMod := (cAliasQry)->T9_TIPMOD        
				@ Li,000 PSay (cAliasQry)->T9_TIPMOD Picture "@!"
				@ Li,013 PSay SubStr((cAliasQry)->MODELO,1,20) Picture "@!"
				@ Li,036 PSay SubStr((cAliasQry)->FABRICA,1,20)  Picture "@!"
			EndIf   
			@ Li,058 PSay (cAliasQry)->T9_CCUSTO  Picture "@!"
			@ Li,088 PSay (cAliasQry)->VARDIA Picture "@E 999,999.99"
			@ Li,119 PSay (cAliasQry)->ESTIMATIVA Picture "@E 999,999.99"      
			DbSelectArea(cAliasQry)
			DbSkip()
		End
		NgSomaLi(58)
	End 
	
	(cALIASQRY)->(dbCloseArea())

	RODA(nCNTIMPR,cRODATXT,TAMANHO)       

	//Devolve a condicao original do arquivo principal
	RetIndex('ST9')
	Set Filter To
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

Return Nil