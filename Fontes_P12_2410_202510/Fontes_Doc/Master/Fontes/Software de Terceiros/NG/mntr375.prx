#Include "RwMake.ch"
#INCLUDE "MNTR375.ch"
#include "Protheus.ch"

#DEFINE _nVERSAO 1 //Versao do fonte
/*


ͻ
Programa   MNTR375  Autor  Vinicius Vallado     Data   03/24/06   
͹
Desc.      Relatorio de Abastecimentos por Unidade de Negocio         
͹
Uso        SigaSGF                                                    
ͼ


*/
Function MNTR375() 

	// Armazena variaveis p/ devolucao (NGRIGHTCLICK)
	Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)
	Local oTempTable	    //Tabela Temporaria
	Local aPesq := {}

	Private aRotina := MenuDef()					 

	Private cCadastro := OemtoAnsi(STR0005)            //"Relat. de Abast. Unid. de Neg."
	Private cPerg 	 := "MNR375"
	Private aPerg 	 :={}
	Private cTRBB	 := GetNextAlias()

	aPos1 := {15,1,95,315}

	aDBFB := {}                          
	aAdd(aDBFB,{"NNOTA"  ,"C", TamSX3("TQN_NOTFIS")[1],0})
	aAdd(aDBFB,{"CODPOS" ,"C", 8,0})              
	aAdd(aDBFB,{"POSTO"  ,"C",30,0})
	aAdd(aDBFB,{"LOJA"   ,"C", 4,0})
	aAdd(aDBFB,{"DTVENC" ,"D", 8,0})
	aAdd(aDBFB,{"VALNOTA","N",15,3})
	aAdd(aDBFB,{"LITRAG" ,"N", 9,3})
	aAdd(aDBFB,{"DTABAS" ,"D", 8,3})
	aAdd(aDBFB,{"UN"     ,"C",40,0})
	aAdd(aDBFB,{"CNPJUN" ,"C",14,0})
	aAdd(aDBFB,{"NABAST" ,"C",15,0})
	aAdd(aDBFB,{"TIPABAS","C",01,0})     
	aAdd(aDBFB,{"FILIAL" ,"C", TamSX3("TQN_FILIAL")[1],0})

	//Intancia classe FWTemporaryTable
	oTempTable  := FWTemporaryTable():New( cTRBB, aDBFB )
	//Cria indices
	oTempTable:AddIndex( "Ind01" , {"NNOTA"}  )
	oTempTable:AddIndex( "Ind02" , {"UN","POSTO","CODPOS","TIPABAS","DTABAS"} )
	oTempTable:AddIndex( "Ind03" , {"CNPJUN"} )
	oTempTable:AddIndex( "Ind04" , {"CODPOS"} )
	//Cria a tabela temporaria
	oTempTable:Create()

	aTRBB := {{STR0020  ,"NNOTA"   ,"C",TamSX3("TQN_NOTFIS")[1] ,0 ,"@!" },; //"Nota"
	{STR0021,"POSTO"   ,"C", 30,0 ,"@!" },; 	 	 	 	 	 	 		 //""Posto"
	{STR0034,"CODPOS"   ,"C", 08,0 ,"@!" },;				         		 //"Cod. Posto" 
	{STR0056,"TIPABAS" ,"C", 01,0 ,"@!" },;				         			 //"Tipo Abast." 
	{STR0022,"LOJA"    ,"C", 04,0 ,"@!" },;						 			 //"Loja"
	{STR0023,"DTABAS"  ,"D", 08,0 ,"99/99/99" },; 				 			 //"Data Abastecimento"
	{STR0024,"DTVENC"  ,"D", 08,0 ,"99/99/99" },;				     		 //"Data Vencimento"
	{STR0025,"LITRAG"  ,"N", 09,3 ,"@E 99,999.999"},;			     		 //"Litragem"
	{STR0026,"VALNOTA" ,"N", 15,3 ,"@E 99,999,999,999.999" },;	 			 //"Valor Total"
	{STR0027,"UN"      ,"C", 30,0 ,"@!"},; 						 			 //"Unid. Neg."
	{STR0028,"CNPJUN"  ,"C", 14,0 ,X3Picture('TQF_CNPJ') }}	     			 //"CNPJ"


	If Pergunte("MNR375",.T.)
		Processa({|IEnd| MNTR975INI()},STR0029,STR0030) 	 //"Aguarde..."###"Processando Registros..."
		DbSelectArea(cTRBB) 
		DbGotop() 

		//Cria Array para montar a chave de pesquisa
		aAdd( aPesq, {STR0020 , {{"", "C", TamSX3("TQN_NOTFIS")[1] , 0, "", "@!"}}})
		aAdd( aPesq, {STR0027 + " + " + STR0021 + " + " + STR0034 + " + " + STR0056 + " + " + STR0023,{{"","C" , 255 , 0 ,"","@!"} }} )
		aAdd( aPesq, {STR0028, {{"", "C", 255, 0, "", X3Picture('TQF_CNPJ')}}})
		aAdd( aPesq, {STR0034, {{"", "C", 255, 0, "", "@!"}}})

		oBrowse:= FWMBrowse():New()
		oBrowse:SetDescription(cCadastro)
		oBrowse:SetTemporary(.T.)
		oBrowse:SetAlias(cTRBB)
		oBrowse:SetFields(aTRBB)
		oBrowse:SetSeek(.T.,aPesq)
		oBrowse:Activate()
	EndIf

	oTempTable:Delete()//Deleta Tabela Temporaria
	DbSelectArea("TQN")
	DbSetOrder(01)
	Dbseek(xFilial("TQN"))  

	//Devolve variaveis armazenadas (NGRIGHTCLICK)
	NGRETURNPRM(aNGBEGINPRM)

Return .T.	
/*


ͻ
Programa  MNTR975INIAutor  Vinicius Vallado             03/22/06   
͹
Desc.      Monta o arquivo temporario inicial mostrado no browse                                                          
͹
Uso       MNTR375                                                     
ͼ


*/          
Function MNTR975INI()

	Local _cGetDB := TcGetDb()		
	Private cFiliIni



	If Mv_Par07 == "  "
		cFiliIni := IF(NgSX2Modo("TQN")=='C','  ','01') 
	Else
		cFiliIni := Mv_Par07
	EndIf

	If MV_PAR09 == 1
		cTipAbas := "TQN_AUTO = '1'"
	ElseIf MV_PAR09 == 2
		cTipAbas :=	"TQN_AUTO = '2'"
	ElseIf MV_PAR09 == 3
		cTipAbas :=	"(TQN_AUTO = '1' OR TQN_AUTO = '2')"	
	EndIf

	cAliasQry := "TETQN"

	cQuery := " SELECT TQN_FILIAL,TQN_NOTFIS,TQN_QUANT,TQN_VALTOT,TQN_NABAST,TQN_DTABAS,TQN_AUTO,"
	cQuery += " TQN_CCUSTO,TQN_POSTO,TQN_LOJA,TQN_CNPJ,TQN_DTPGMT,"
	If Upper(_cGetDB) == "ORACLE"
		cQuery += " (SELECT TQF_NREDUZ FROM "+RetSQLName("TQF")+" WHERE TQF_CODIGO=TQN_POSTO AND TQF_LOJA=TQN_LOJA "
		If NGSX2MODO("TQF") == NGSX2MODO("TQN")
			cQuery += " AND TQF_FILIAL = TQN_FILIAL "
		Else
			cQuery += " AND TQN_FILIAL = '"+xFilial("TQN")+"' AND TQF_FILIAL = '"+xFilial("TQF")+"' "
		EndIf
		cQuery += " AND D_E_L_E_T_ = ' ' AND ROWNUM=1)POSTO "
	Else
		cQuery += " (SELECT TQF_NREDUZ FROM "+RetSQLName("TQF")+" WHERE TQF_CODIGO=TQN_POSTO AND TQF_LOJA=TQN_LOJA "
		If NGSX2MODO("TQF") == NGSX2MODO("TQN")
			cQuery += " AND TQF_FILIAL = TQN_FILIAL "
		Else
			cQuery += " AND TQN_FILIAL = '"+xFilial("TQN")+"' AND TQF_FILIAL = '"+xFilial("TQF")+"' "
		EndIf
		cQuery += " AND D_E_L_E_T_ = ' ' GROUP BY TQF_NREDUZ HAVING COUNT(*)=1)POSTO
	Endif
	cQuery += " FROM " + RetSQLName("TQN")
	cQuery += " WHERE TQN_FILIAL BETWEEN '"+cFILIINI+"' AND '"+MV_PAR08+"' AND TQN_DTABAS BETWEEN '"+DTOS(MV_PAR01)+"' "
	cQuery += " AND '"+DTOS(MV_PAR02)+"' "
	cQuery += " AND (TQN_POSTO>= '"+(MV_PAR03)+"' AND TQN_LOJA>= '"+MV_PAR04+"')  "
	cQuery += " AND (TQN_POSTO<= '"+(MV_PAR05)+"' AND TQN_LOJA<= '"+MV_PAR06+"')  "
	cQuery += " AND " +cTipAbas+ " AND D_E_L_E_T_ = ' ' "
	If Upper(_cGetDB) == "ORACLE"
		cQuery += " AND SUBSTR(TQN_CCUSTO,1,2) BETWEEN '"+(Mv_Par10)+"' AND '"+(Mv_Par11)+"' "
	Else 
		cQuery += " AND SUBSTRING(TQN_CCUSTO,1,2) BETWEEN '"+(Mv_Par10)+"' AND '"+(Mv_Par11)+"' "
	EndIf
	cQuery += " ORDER BY TQN_FILIAL,TQN_PLACA,TQN_DTABAS,TQN_HRABAS "

	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

	ProcRegua(LastRec())
	DbSelectArea(cALIASQRY)
	While !Eof()  
		IncProc(STR0029,STR0030) //"Aguarde..."###"Processando Registros..."

		cUnNeg := SubStr((cAliasQry)->TQN_CCUSTO,1,2) 
		RecLock((cTRBB), .T.)
		DbSelectArea("TSW")  
		DbSetOrder(01)   
		If MsSeek(xFilial("TSW")+cUnNeg)
			(cTRBB)->UN := TSW->TSW_DUNNEG
		EndIf

		(cTRBB)->POSTO   := (cAliasQry)->POSTO
		(cTRBB)->CODPOS  := (cAliasQry)->TQN_POSTO
		(cTRBB)->LOJA    := (cAliasQry)->TQN_LOJA
		(cTRBB)->CNPJUN  := (cAliasQry)->TQN_CNPJ
		(cTRBB)->FILIAL  := (cAliasQry)->TQN_FILIAL
		(cTRBB)->NNOTA   := (cAliasQry)->TQN_NOTFIS
		(cTRBB)->LITRAG  := (cAliasQry)->TQN_QUANT
		(cTRBB)->VALNOTA := (cAliasQry)->TQN_VALTOT                                                                                     
		(cTRBB)->NABAST  := (cAliasQry)->TQN_NABAST 
		(cTRBB)->DTABAS  := STOD((cAliasQry)->TQN_DTABAS)
		(cTRBB)->TIPABAS := (cAliasQry)->TQN_AUTO
		(cTRBB)->DTVENC  := STOD((cAliasQry)->TQN_DTPGMT)
		MsUnLock(cTRBB)
		DbSelectArea(cALIASQRY) 
		DbSkip() 
	End
	DBCloseArea()

	DbSelectArea(cTRBB)
	DbGoTop()
	If Reccount() = 0
		MsgInfo(STR0031,STR0032) //"No existem dados para montar a tela de consulta."###"ATENO"
		Return .F.
	EndIf 	
Return .T. 	     
/*


ͻ
Programa  MNTR375PESAutor  Vinicius Vallado     Data   03/24/06   
͹
Desc.     Realiza a Pesquisa                                          
͹
Uso        AP                                                         
ͼ


*/
Function MNTR375PES()

	Local cPosto := Space(Len(TQF->TQF_CODIGO))
	Local cCnpj  := Space(Len(TQF->TQF_CNPJ))
	Private oDlg5

	Define MsDialog oDlg5 From 300,130 To 400,490 Title OemToAnsi(STR0033) Pixel //"Pesquisa"

	@ 0.5,0.5 Say OemtoAnsi(STR0034) //"Codigo do Posto"
	@ 3.5,50 Get cPosto Picture '@!' SIZE 60,10 F3 "TQF" 
	@ 1.8,2 Say OemtoAnsi(STR0028) //"CNPJ"
	@ 1.6,6 Get cCnpj Picture X3Picture('TQF_CNPJ') VALID if(!Empty(cCnpj),Cgc(cCnpj),.T.) Size 60,10 Of oDlg5
	@ 3.7,33 Button "&OK" Size 30,10 Action MNTR375POS(cPosto,cCnpj)

	Activate MsDialog oDlg5 Centered

	DbSelectArea(cTRBB)
	DbSetOrder(01)

Return .T. 
/*


ͻ
Programa  MNTR375POSAutor  Microsiga            Data   03/27/06   
͹
Desc.                                                                 
͹
Uso        AP                                                         
ͼ


*/
Function MNTR375POS(cPosto,cCnpj)

	Local aAlias := GetArea()

	If !empty(cPosto)
		DbSelectArea(cTRBB)
		dbSetOrder(4)
		DbSeek(cPosto,.t.)
	Else 
		IF!empty(cCnpj)
			dbSelectArea(cTRBB)
			dbSetOrder(3)
			DbSeek(cCnpj,.t.)	
		EndIf
	EndIf

	oDlg5:End()

	Restarea(aAlias)

Return .T.  
/*


ͻ
Programa  MNTR375VISAutor  Microsiga            Data   03/27/06   
͹
Desc.     Visualiza so dados do registro selecionado.                 
͹
Uso        MNTR375                                                    
ͼ


*/
Function MNTR375VIS()

	Local aAlias := GetArea()

	DbSelectArea("TQN")
	DbSetOrder(04)
	//DbSeek(xFilial("TQN")+(cTRBB)->NABAST)
	DbSeek((cTRBB)->FILIAL + (cTRBB)->NABAST)
	NGCAD01("TQN",Recno(),1)

	DbSelectArea("TQN")
	Restarea(aAlias)

Return .t.      
/*


Ŀ
Funo    MNTR375PAR Autor Vinicius Vallado        Data  03/27/06 
Ĵ
Descrio Reprocessa o browse de acordo com os parametros             
Ĵ
 Uso      MNTR375                                                     
ٱ


*/
Function MNTR375PAR()

	If !Pergunte("MNR375",.T.)
		Return
	EndIf 

	DbSelectArea(cTRBB)
	Zap

	Processa({ |lEnd| MNTR975INI()}, STR0035)  //"Aguarde ...Processando Registros..."

	DbSelectArea(cTRBB)
	DbGotop()

RETURN .T.
/*        


Ŀ
Funo    MNTR375IMP Autor  Vinicius Vallado       Data 03/27/06  
Ĵ
Descrio Relatorio de Abastecimento por Unidade de Negocio 	   	  
Ĵ
 Uso       SigaMNT                                                    
Ĵ 


*/
Function MNTR375IMP()

	Local cString    := "TQN"
	Local cDesc1     := STR0036 //"Relatorio de abastecimento por Unidade de Negocio"
	Local cDesc2     := STR0037  //"O usuario podera utilizar a opcao Parametros para"
	Local cDesc3     := STR0038 //"a obtencao da selecao desejada."
	Local wnRel      := "MNTR375"

	Private aReturn  := { STR0039, 1,STR0040 , 2, 2, 1, "",1 }     //"Administracao"###"Zebrado"
	Private nLastKey := 0
	Private Titulo   := STR0036 //"Relatorio de Abastecimento por Unidade de Negocio"
	Private Tamanho  := "M"

	DbSelectArea("TQN")

	//Ŀ
	// Envia controle para a funcao SETPRINT     
	//

	Pergunte(cPerg,.f.)
	cPerg := " "
	wnRel := SetPrint(cString,wnRel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.f.,"")
	cPerg := "MNR375"

	If nLastKey = 27
		Set Filter To
		DbSelectArea("TQN")
		Return
	Endif

	SetDefault(aReturn,cString)

	RptStatus({|lEnd| MNTR375IM(@lEnd,wnRel,Titulo,Tamanho)},Titulo)

	DbSelectArea("TQN")

Return Nil

/*/


Ŀ
Funo    MNTR375IM  Autor  Vinicius Vallado       Data 03/27/06  
Ĵ
Descrio Chamada do Relatorio.                                       
Ĵ
 Uso       SigaMNT                                                    
Ĵ 


/*/
Function MNTR375IM(lEnd,wnRel,Titulo,Tamanho)
	Local cRodaTxt  := ""
	Local nCntImpr  := 0  
	Private li := 80 ,m_pag := 1
	Private NomeProg := "MNTR375"
	Private Cabec1   := STR0041 //"N.Fiscal     Data Abastecimento  Litragem          Valor Total   Data Vencimento"
	Private Cabec2   := ""                
	Private cUN := ""
	Private cPosto := ""
	Private cCodPo := ""
	Private cTabas := ""
	Private lVerOpT  := .F.
	Private nQtdMd   := 0
	Private nTOTLIT  := 0000000.00 
	Private nTOTVL   := 0000000.00
	Private nTOTLITUN:= 0000000.00 
	Private nTOTVLUN := 0000000.00
	Private nTOTLITPT:= 00000.000 
	Private nTOTVLPT := 0000000.00
	Private nTOTALL  := 00000.00
	Private nTOTALV  := 0000000.00

	//Ŀ
	// Verifica se deve comprimir ou nao                            
	//
	nTipo  := IIf(aReturn[4]==1,15,18)

	//Ŀ
	// Monta os Cabecalhos                                          
	//
	/*/
	1         2         3         4         5         6         7         8   
	012345678901234567890123456789012345678901234567890123456789012345678901234567890123
	************************************************************************************
	N.Fiscal     Data Abastecimento  Litragem          Valor Total   Data Vencimento
	************************************************************************************

	------------------------------------------------------------------------------------
	Unidade de Negocio:
	Posto:
	Codigo do Posto: 
	CNPJ:   
	Abastecimento:
	------------------------------------------------------------------------------------

	999999          99/99/99       99,999.999   99,999,999,999.999      99/99/9999

	Sub Total U.N:    99,999.999   99,999,999,999.999

	Total U.N:        99,999.999   99,999,999,999.999

	Total do Posto:   99,999.999   99,999,999,999.999


	/*/                                                

	//Ŀ
	// Define o indice de leitura do arquivo                    
	//

	DbSelectArea(cTRBB)
	DbSetOrder(02)

	SetRegua(LastRec())
	DbGoTop()

	While !Eof()

		If lEnd
			@ Prow()+1,001 PSay STR0042  //"CANCELADO PELO OPERADOR"
			Exit
		EndIf 
		DbSelectArea(cTRBB)
		DbSetOrder(02)
		IncRegua()
		If (cTRBB)->UN <> cUN 
			nTOTLITUN := 0
			nTOTVLUN  := 0 
			nTOTLIT   := 0
			nTOTVL    := 0
		EndIf

		If (cTRBB)->UN <> cUN .Or. (cTRBB)->TIPABAS <> cTabas .OR. (cTRBB)->POSTO <> cPOSTO;
		.Or. (cTRBB)->CODPOS <> cCodPo
			NgSomaLi(58)

			NgSomaLi(58)
			@ Li,000 Psay REPLICATE(chr(45),132)
			NgSomaLi(58)
			@ Li,000 PSay STR0043 //"Unidade de Negocio:"
			@ Li,019 PSay (cTRBB)->UN 
			cUN := (cTRBB)->UN        
			cTabas := (cTRBB)->TIPABAS      

			NgSomaLi(58)
			@ Li,000 PSay STR0044 //"Posto:"
			@ Li,006 PSay (cTRBB)->POSTO 
			cPosto := (cTRBB)->POSTO
			cCodPo := (cTRBB)->CODPOS

			NgSomaLi(58)
			@ Li,000 PSay STR0045 //"Cdigo do Posto:"
			@ Li,017 PSay (cTRBB)->CODPOS

			NgSomaLi(58)
			@ Li,000 PSay STR0046 //"CNPJ:"
			@ Li,005 PSay (cTRBB)->CNPJUN PICTURE X3Picture('TQF_CNPJ')

			NgSomaLi(58)
			@ Li,000 PSay STR0047 //"Abastecimento:"
			If (cTRBB)->TIPABAS == '1'
				@ Li,014 Psay STR0006  //"Automtico"
			ElseIf (cTRBB)->TIPABAS == '2'
				@ Li,014 Psay STR0007       //"Manual"
			EndIf       

			NgSomaLi(58)
			@ Li,000 Psay REPLICATE(chr(45),132)
			NgSomaLi(58)
			nTOTLITUN := 0
			nTOTVLUN  := 0
			nTOTLITPT := 0
			nTOTVLPT  := 0
			//EndIf

			While !Eof() .and. (cTRBB)->UN == cUN .And. (cTRBB)->TIPABAS == cTabas .AND. (cTRBB)->POSTO == cPOSTO;
			.And. (cTRBB)->CODPOS == cCodPo
				//Quebra por Unidade de Negocio e tipo de abastecimento
				NgSomaLi(58)
				@ Li,000 PSay (cTRBB)->NNOTA
				@ Li,016 Psay (cTRBB)->DTABAS
				@ Li,031 PSay (cTRBB)->LITRAG  Picture "@E 99,999.999"
				@ Li,044 PSay (cTRBB)->VALNOTA Picture "@E 99,999,999,999.999"
				@ Li,068 PSay (cTRBB)->DTVENC
				//Totalizadores
				nTOTLIT   += (cTRBB)->LITRAG
				nTOTVL    += (cTRBB)->VALNOTA
				nTOTLITUN += (cTRBB)->LITRAG
				nTOTVLUN  += (cTRBB)->VALNOTA
				nTOTLITPT += (cTRBB)->LITRAG
				nTOTVLPT  += (cTRBB)->VALNOTA     	  
				cPosto := (cTRBB)->POSTO
				cCodPo := (cTRBB)->CODPOS
				DbSelectArea(cTRBB)
				DbSkip()  
			End

			//If (cTRBB)->TIPABAS <> cTabas  
			NgSomaLi(58)
			NgSomaLi(58)     
			@ Li,013 Psay STR0048 //"Total do Posto:"
			@ Li,031 Psay nTOTLITUN   Picture "@E 99,999.999"
			@ Li,044 Psay nTOTVLUN    Picture "@E 99,999,999,999.999"
			nTOTALL+=nTOTLITUN
			nTOTALV+=nTOTVLUN
			nTOTLITUN := 0           
			nTOTVLUN  := 0
			//EndIf	   
			If (cTRBB)->UN <> cUN  
				NgSomaLi(58)
				NgSomaLi(58)   
				@ Li,013 Psay STR0049 //"Total U.N:"
				@ Li,030 Psay nTOTLIT Picture "@E 999,999.999" 
				@ Li,044 Psay nTOTVL Picture  "@E 99,999,999,999.999"
				//nTOTALL+=nTOTLIT
				//nTOTALV+=nTOTVL	                   
				nTOTLIT  := 0
				nTOTVL  := 0
			EndIf
		EndIf  
		//	DbSelectArea(cTRBB)		
		//	DbSkip()  
	End
	NgSomaLi(58)
	NgSomaLi(58)   
	@ Li,013 Psay STR0050 //"Total Geral:"
	@ Li,030 Psay nTOTALL Picture "@E 999,999.999"
	@ Li,043 Psay nTOTALV Picture "@E 999,999,999,999.999"		   

	Roda(nCntImpr,cRodaTxt,Tamanho)

	//Ŀ
	// Devolve a condicao original do arquivo principal             
	// 

	RetIndex('TQN')

	Set Filter To

	Set device to Screen

	If aReturn[5] = 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	Endif
	MS_FLUSH()

Return NIL 
/*


Ŀ
Funo    MNTR375LOJ Autor Elisangela Costa        Data  06/01/06 
Ĵ
Descrio Valida o parametro de Loja                                  
Ĵ
 Uso      MNTR375	                                                  
ٱ


*/
Function MNTR375LOJ()

	If Empty(MV_PAR03) .And. !Empty(MV_PAR04)
		MsgStop(STR0051)  //"Informe o Codigo do Posto"
		MV_PAR04 := Space(Len(MV_PAR04))
		Return .F. 
	EndIf    
	If !Empty(MV_PAR03) .And. Empty(MV_PAR04)
		MsgStop(STR0052)  //"Informe o Codigo da Loja"
		Return .F. 
	EndIf  
	If !Empty(MV_PAR03) .And. !Empty(MV_PAR04)
		IF !ExistCpo("TQF",MV_PAR03+MV_PAR04)
			Return .F.
		EndIf	
	EndIf

Return .T.  

/*/


Ŀ
Funo    MNTR375ATE Autor  Heverson Vitoreti      Data  18/04/06 
Ĵ
Descrio Validaao do codigo do Posto                                
Ĵ
 Uso      MNTR375                                                     
ٱ


/*/
Function MNTR375ATE(ALIAS,PAR1,PAR2,TAM)

	If Empty(par2)
		Help(" ",2,STR0032,,STR0053,3,1) //"ATENO"###"Cdigo do Posto final no pode ser vazio."
		Return .f.
	Elseif par2 < par1
		Help(" ",1,STR0032,,STR0054,3,1) //"ATENO"###"Cdigo do Posto final informado  invlido."
		Return .f.
	Endif

	If par2 = replicate('Z',Len(PAR2))
		Return .t.
	Else  
		If !Atecodigo('TQF',Par1+Mv_Par04,Par2+Mv_Par06,08)
			Return .f.	
		Endif
	Endif 

Return .t. 

/*


Ŀ
Funo    MNTR975FL  Autor Soraia de Carvalho      Data  25/07/06 
Ĵ
Descrio Valida o parametro ate data                                 
Ĵ
 Uso      MNTR375                                                     
ٱ


*/
Function MNTR975FL(nOpc)
	Local cVERFL

	cVERFL := Mv_Par07

	If Empty(mv_par07)
		Return .t.
	Else
		If nOpc == 1
			lRet := IIf(Empty(Mv_Par07),.t.,ExistCpo('SM0',SM0->M0_CODIGO+Mv_par07))
			If !lRet
				Return .f.
			EndIf
		EndIf

		If nOpc == 2
			lRet := IIF(ATECODIGO('SM0',SM0->M0_CODIGO+Mv_par07,SM0->M0_CODIGO+Mv_Par08,02),.T.,.F.)
			If !lRet
				Return .f.
			EndIf
		EndIf
	EndIf

Return .t.   
/*


Ŀ
Funo    MNTR375DT  Autor Soraia de Carvalho      Data  25/07/06 
Ĵ
Descrio Valida o parametro ate data                                 
Ĵ
 Uso      MNTR375                                                     
ٱ


*/
Function MNTR375DT()

	If  MV_PAR02 < MV_PAR01
		MsgStop(STR0055)  //"Data final no pode ser inferior  data inicial!"
		Return .F.  
	EndIf

Return .T.

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 02/02/2008
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaMNT                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ 
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
	Local aRotina :=	{		{STR0001  ,"MNTR375PES()",0,1},; //"Pesquisar"
	{STR0002 ,"MNTR375VIS()",0,2},; //"Visualizar"
	{STR0003 ,"MNTR375PAR()",0,3,0},; //"Parametros"
	{STR0004   ,"MNTR375IMP()",0,3,0}}  //"Imprimir"

Return aRotina
