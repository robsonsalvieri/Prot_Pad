#Include "MNTR605.ch"
#Include "Protheus.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTR605
description
@author  NG Informatica
@since   06/97
@version P12
/*/
//-------------------------------------------------------------------
Function MNTR605(cBEM,cCCUSTO,cCENTRAB,cCODFAM)

    Local aNGBEGINPRM := NGBEGINPRM()
    Local cString	  := If(IsInCallStack("MNTA120"),"STF","ST9")
    Local cDesc1	  := STR0001 //"Relatorio das manutencoes cadastradas para o Bem. Atraves da utilizacao"
    Local cDesc2	  := STR0002 //"dos Parametros o usuario podera selecionar aquelas manutencoes que"
    Local cDesc3	  := STR0003 //"deseja analisar."
    Local wnrel	      := "MNTR605"

    Private aReturn	 := { STR0004, 1,STR0005, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
    Private nLastKey := 0
    Private cPerg    := "MNT605    "
    Private Tamanho	 := "G"
    Private titulo   := STR0006 //"Manutencoes do Bem"

    //Vetor utilizado para armazenar retorno da função MNT045TRB,
    //criada de acordo com o item 18 (RoadMap 2013/14)
    Private vFilTRB := MNT045TRB()

    SetKey(VK_F4, {|| MNT045FIL( vFilTRB[2] )})

    //³ Variaveis utilizadas para parametros                         ³
    //³ mv_par01             // De  Centro de Custos                 ³
    //³ mv_par02             // Ate Centro de Custos                 ³
    //³ mv_par03             // De  Centro de Trabalhos              ³
    //³ mv_par04             // Ate Centro de Trabalhos              ³
    //³ mv_par05             // De  Familia                          ³
    //³ mv_par06             // Ate Familia                          ³
    //³ mv_par07             // De  Bem                              ³
    //³ mv_par08             // Ate Bem                              ³
    //³ mv_par09             // Salta Pagina /CT/FM/BEM              ³
    Pergunte(cPerg,.f.)
    If (cBEM <> Nil) .And. (cCCUSTO <> Nil) .And. (cCENTRAB <> Nil) .And. (cCODFAM <> Nil)
        cPerg := Nil
        MV_PAR01 := cCCUSTO
        MV_PAR02 := cCCUSTO
        MV_PAR03 := cCENTRAB
        MV_PAR04 := cCENTRAB
        MV_PAR05 := cCODFAM
        MV_PAR06 := cCODFAM
        MV_PAR07 := cBEM
        MV_PAR08 := cBEM
        MV_PAR09 := 3
    EndIf

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Envia controle para a funcao SETPRINT                        ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    wnrel := SetPrint( cString, wnrel, cPerg, titulo, cDesc1, cDesc2, cDesc3, .F., "" )

    SetKey(VK_F4, {|| })

    If nLastKey = 27
        Set Filter To
        dbSelectArea("ST0")

        MNT045TRB( .T., vFilTRB[1], vFilTRB[2])

        Return Nil
    Endif

    SetDefault(aReturn,cString)
    RptStatus({|lEnd| R605Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

    dbSelectArea("ST0")

    MNT045TRB( .T., vFilTRB[1], vFilTRB[2])

    NGRETURNPRM(aNGBEGINPRM)

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ R605Imp  ³ Autor ³ Waldemiro L. Lustosa  ³ Data ³ 13.11.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR605                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function R605Imp(lEnd,wnRel,titulo,tamanho)

    Local cRodaTxt  := ""
    Local nCntImpr  := 0
    Local nAtual
    Local nTamPro   := TamSX3("TG_CODIGO")[1] - 15
    Local lAtivo    := .F.
    Local lInativo  := .F.
    Local lImpRel   := .F.

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Contadores de linha e pagina                                 ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    Private li := 00 ,m_pag := 1
    Private Cabec1			:= " "
    Private Cabec2			:= " "
    Private nomeprog        := "MNTR605"
    Private ntipo           := 0
    Private cQuebraCusto    := Space(09)
    Private cQuebraTraba    := Space(06)
    Private cQuebraFamil    := Space(06)
    Private cQuebraBem      := Space(16)
    Private cChave			:= Space(16)

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Verifica se deve comprimir ou nao                            ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    nTipo  := IIF( aReturn[4] == 1, 15, 18)

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Monta os Cabecalhos                                          ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    //           1         2         3         4         5         6         7         8         9         0         1         2         3		  4          5         6
    // 01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789
    //
    // Bem...: xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  Familia...: xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    // Pai...: xxxxxxxxxxxxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  Calend....: xxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    // Prior.: xxx     Contador: xxxxxxxxx 		Dt Acomp.: dd/mm/aaaa 	 Var/Dia...: xxxxxx  Limite Contador: xxxxxxxxx 		Sit.Bem: xxxx   Sit.Manut: xxxx
    //               Contador 2: xxxxxxxxx			Dt Acomp.: dd/mm/aaaa 	 Var/Dia...: xxxxxx  Limite Contador: xxxxxxxxx
    //
    //    Servico: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  		  Sequencia: xxx  		Nome Manut: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    //       Tipo: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx      	      Prior: xxx
    //
    //                         Descricao....: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    //                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    //                                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    //                         Periodo......: xxxxxxxxxx                     						Area: xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    //                         Dt.Ult.Manut.: xx/xx/xxxx              Parada: xxx      Parada Antes: xxx xxx              Depois: xxx xxx        N Util: Adianta		Incremento Entre Manut: xxxxxxxxx
    //                         Tipo Acompan.: xxxxxxxx              Tempo Entre Manut: xxx xxx            Calendario: xxx
    //                         Cont.Ult.Man.: xxxxxxxxx             		              Periodo Acomp.: xxx xxx
    //                         Cont.Bem.....: xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    //
    //                         -----------------------------------------------------------------------------------------------TAREFAS--------------------------------------------------------------------------------------------
    //
    //                         xxxxxx  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    //                                 Dependencias: xxxxxx xx,xx xxxxxx xx,xx xxxxxx xx,xx xxxxxx xx,xx xxxxxx xx,xx xxxxxx xx,xx
    //                                               xxxxxx xx,xx xxxxxx xx,xx xxxxxx xx,xx xxxxxx xx,xx xxxxxx xx,xx xxxxxx xx,xx
    //
    //                                 ---------------------------------------------------------------------------------------ETAPAS---------------------------------------------------------------------------------------------
    //                                 xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    //                                 xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    //                                 xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
    //
    //                                             -----------------------------------------------------------------OPCOES-----------------------------------------------------------------
    //                                             Opcao.......... Tipo Resposta... Operador.. Informacao da Condição....... Tipo Manutencao..... Bem................ Serviço.... Sequencia
    //                                             xxxxxxxxxxxxxxx xxxxxxxxx        xx         xxxxxxxxxx                    xxxxxxxxxx           xxxxxxxxxxxxxxxx    xxxxxx      999
    //                                             xxxxxxxxxxxxxxx xxxxxxxxx        xx         xxxxxxxxxx                    xxxxxxxxxx           xxxxxxxxxxxxxxxx    xxxxxx      999
    //
    //                                 ---------------------------------------------------------------INSUMOS----------------------------------------------------------------------
    //                                 Nome.............. Codigo............... Descricao.............................  Quantidade...... Consumo... Unidades... Reserva.... Destino
    //                                 xxxxxxxxxxxxx 	  xxxxxxxxxxxxxxx 	   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 		xxx 	 xxxxxx,xx 		  xxx 	  SIM 			Substit
    //                                 xxxxxxxxxxxxx 	  xxxxxxxxxxxxxxx 	   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx 		xxx 	 xxxxxx,xx 		  xxx    NAO 		    Apoio
    //                                 xxxxxxxxxxxxx 	  xxxxxxxxxxxxxxx 	   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx		xxx 	 xxxxxx,xx 		  xxx    SIM 	 	 	Troca
    //
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Define o indice de leitura do arquivo de Bens            ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

    dbSelectArea("ST9")
    dbSetOrder(02)
    dbSeek(xFilial('ST9') + MV_PAR01, .T.)

    SetRegua(LastRec())

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Correr ST9 para ler as Estruturas                        ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    While !Eof() .And. ST9->T9_FILIAL == xFilial("ST9") .And.;
            ST9->T9_CCUSTO >= MV_PAR01  .And. ST9->T9_CCUSTO <= MV_PAR02

        IncRegua()

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ Selecao dos registros conforme os parametros             ³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

        dbSelectArea("STF")
        dbSetOrder(01)
        If !dbSeek(xFilial("STF")+ST9->T9_CODBEM)
            dbSelectArea("ST9")
            dbSkip()
            Loop
        EndIf

        dbSelectArea("ST9")
        If ST9->T9_CENTRAB < MV_PAR03 .OR. ST9->T9_CENTRAB > MV_PAR04
            dbSkip()
            Loop
        EndIf

        If ST9->T9_CODFAMI < MV_PAR05 .OR. ST9->T9_CODFAMI > MV_PAR06
            dbSkip()
            Loop
        EndIf

        If ST9->T9_CODBEM  < MV_PAR07 .OR. ST9->T9_CODBEM  > MV_PAR08
            dbSkip()
            Loop
        EndIf

        If MNT045STB( ST9->T9_CODBEM, vFilTRB[2] )
            dbSkip()
            Loop
        EndIf

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ Verifica se o Bem tem Bem Pai                            ³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

        dbSelectArea("STC")
        dbSetOrder(03)

        cChave := ST9->T9_CODBEM

        While .T.
            dbSeek(xFilial('STC')+cChave)
            If Found()
                cChave := STC->TC_CODBEM
                dbSkip()
                Loop
            EndIf
            Exit
        EndDo

        lInativo := .F.
        lAtivo   := .F.
        dbSelectArea("STF")
        dbSeek(xFilial('STF')+ST9->T9_CODBEM)
        While !Eof() .And. STF->TF_CODBEM = ST9->T9_CODBEM .And. STF->TF_FILIAL == xFilial("STF")
            If MV_PAR10 == 2 .And. STF->TF_ATIVO = "N"
                lInativo := .T.
                exit
            ElseIf MV_PAR10 == 1 .And. STF->TF_ATIVO == "S"
                lAtivo := .T.
                exit
            ELseIf MV_PAR10 == 3
                exit
            EndIf
            dbSelectArea("STF")
            dbSkip()
        End

        If MV_PAR10 == 2 .And. !lInativo
            dbSelectArea("ST9")
            dbSkip()
            Loop
        ElseIf MV_PAR10 == 1 .And. !lAtivo
            dbSelectArea("ST9")
            dbSkip()
            Loop
        EndIf

        lImpRel := .T.

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ Impressao da Primeira Linha da Estrutura                 ³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        If cQuebraCusto = " "
            cQuebraCusto := ST9->T9_CCUSTO
            cQuebraTraba := ST9->T9_CENTRAB
            cQuebraFamil := ST9->T9_CODFAMI
            Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
            Quebra(cQuebraCusto,cQuebraTraba,cQuebraFamil)
        Else
            If cQuebraCusto != ST9->T9_CCUSTO  .OR.;
                    (cQuebraTraba != ST9->T9_CENTRAB .AND. MV_PAR09 = 1) .Or.;
                    (cQuebraFamil != ST9->T9_CODFAMI .AND. MV_PAR09 = 2) .Or.;
                    MV_PAR09      = 3

                cQuebraCusto := ST9->T9_CCUSTO
                cQuebraTraba := ST9->T9_CENTRAB
                cQuebraFamil := ST9->T9_CODFAMI
                Li = 59
                Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
                Quebra(cQuebraCusto,cQuebraTraba,cQuebraFamil)
            EndIf
        EndIf

        nAtual := ST9->(Recno())  // guarda o endereco do Bem Atual

        @ Li,000 Psay STR0008 //"Bem...:"
        @ Li,008 Psay ST9->T9_CODBEM
        @ Li,026 Psay ST9->T9_NOME
        dbSelectArea("ST6")
        dbSetOrder(01)
        dbSeek(xFilial('ST6')+ST9->T9_CODFAMI)
        @ Li,082 Psay STR0009 //"Familia...:"
        @ Li,094 Psay ST9->T9_CODFAMI
        @ Li,102 Psay ST6->T6_NOME
        MNTR605LIN()

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ Impressao da Segunda  Linha do Bem                       ³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        If ST9->T9_CODBEM != cChave
            dbSelectArea("ST9")
            dbSetOrder(01)
            dbSeek(xFilial('ST9') + cChave)
            @ Li,000 Psay STR0010 //"Pai...:"
            @ Li,008 Psay cChave
            @ Li,026 Psay ST9->T9_NOME
        EndIf

        dbSelectArea("ST9")
        dbSetOrder(02)
        dbGoTo(nAtual)
        @ Li,082 Psay STR0011 //"Calendario:"
        @ Li,094 Psay ST9->T9_CALENDA

        dbSelectArea("SH7")
        dbSetOrder(01)
        dbSeek(xFilial('SH7')+ST9->T9_CALENDA)
        @ Li,098 Psay SH7->H7_DESCRI

        MNTR605LIN()

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ Impressao da Terceira Linha do Bem                       ³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        @ Li,000 Psay STR0012 //"Prior.:"
        @ Li,008 Psay ST9->T9_PRIORID
        @ Li,030 Psay STR0013 //"Contador:"
        @ Li,040 Psay cValtoChar(ST9->T9_POSCONT)
        @ Li,059 Psay STR0014 //"Dt.Acomp.:"
        @ Li,070 Psay ST9->T9_DTULTAC Picture '99/99/9999'
        @ Li,082 Psay STR0015 //"Var/Dia...:"
        @ Li,094 Psay cValToChar(ST9->T9_VARDIA)
        @ Li,134 Psay STR0016 //"Limite Contador:"
        @ Li,151 Psay cValToChar(ST9->T9_LIMICON)
        @ Li,168 Psay STR0017 //"Sit.Bem:"
        If ST9->T9_SITBEM = "A"
            @ Li,177 Psay STR0018 //"Ativo"
        ElseIf ST9->T9_SITBEM = "I"
            @ Li,177 Psay STR0019 //"Inativo"
        ElseIf ST9->T9_SITBEM = "T"
            @ Li,177 Psay STR0076 //"Tranferido"
        EndIf
        @ Li,201 Psay STR0020 //"Sit.Manut:"

        If ST9->T9_SITMAN = "A"
            @ Li,213 Psay STR0018 //"Ativo"
        Else
            @ Li,213 Psay STR0019 //"Inativo"
        EndIf

        MNTR605LIN()
        dbSelectArea("TPE")
        dbSetOrder(01)
        dbSeek(xFilial("TPE")+ST9->T9_CODBEM)
        @ Li,028 Psay "Contador 2:"
        @ Li,040 Psay cValtoChar(TPE->TPE_POSCON)
        @ Li,059 Psay STR0014 //"Dt.Acomp.:"
        @ Li,070 Psay TPE->TPE_DTULTA Picture '99/99/9999'
        @ Li,082 Psay STR0015 //"Var/Dia...:"
        @ Li,094 Psay cValtoChar(TPE->TPE_VARDIA)
        @ Li,134 Psay STR0016 //"Limite Cont:"
        @ Li,151 Psay cValToChar(TPE->TPE_LIMICO)
        MNTR605LIN()

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ Impressao de manutencoes do bem                          ³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        dbSelectArea("ST9")
        dbSetOrder(01)
        dbSeek(xFilial("ST9")+STF->TF_CODBEM)
        cCodBem1 := STF->TF_CODBEM
        nPERACOM := ST9->T9_PERACOM
        cUNIACOM := ST9->T9_UNIACOM

        If ST9->T9_TEMCONT <> 'N' .And. (ST9->T9_TEMCONT == 'I' .Or.;
                ST9->T9_TEMCONT == 'P')

            cBEMAUX := Space(Len(ST9->T9_CODBEM))
            If ST9->T9_TEMCONT == 'I'
                cBEMAUX := NGBEMIME(STF->TF_CODBEM)
            ElseIf ST9->T9_TEMCONT == 'P'
                cBEMAUX := NGBEMPAI(STF->TF_CODBEM)
            EndIf

            If !Empty(cBEMAUX)
                dbSelectArea( "ST9" )
                dbSetOrder(01)
                If dbSeek(xFilial("ST9")+cBEMAUX)
                    nPERACOM := ST9->T9_PERACOM
                    cUNIACOM := ST9->T9_UNIACOM
                EndIf
            EndIf
        EndIf

        dbSelectArea("ST9")
        dbSetOrder(01)
        dbSeek(xFilial("ST9")+STF->TF_CODBEM)

        dbSelectArea("STF")
        dbSeek(xFilial('STF')+ST9->T9_CODBEM)
        While !Eof() .And. STF->TF_CODBEM = ST9->T9_CODBEM .And. STF->TF_FILIAL == xFilial("STF")

            If MV_PAR10 == 2 .And. STF->TF_ATIVO = "S"
                dbSkip()
                Loop
            ElseIf MV_PAR10 == 1 .And. STF->TF_ATIVO == "N"
                dbSkip()
                Loop
            EndIf
            MNTR605LIN()
            dbSelectArea("ST4")
            dbSetOrder(01)
            dbSeek(xFilial('ST4')+STF->TF_SERVICO)

            // linha do servico
            @ Li,017 Psay STR0021 //"Servico:"
            @ Li,026 Psay STF->TF_SERVICO
            @ Li,033 Psay Substr(ST4->T4_NOME,1,40)

            @ Li,089 Psay STR0022 //"Sequencia:"
            @ Li,100 Psay STF->TF_SEQRELA Picture "@!"
            @ Li,119 Psay STR0023 //"Nome Manut:"
            @ Li,131 Psay STF->TF_NOMEMAN

            MNTR605LIN()
            dbSelectArea("STE")
            dbSetOrder(01)
            dbSeek(xFilial('STE')+STF->TF_TIPO)

            dbSelectArea("STF")
            @ Li,020 Psay STR0024 //"Tipo:"
            @ Li,026 Psay STF->TF_TIPO
            @ Li,033 Psay STE->TE_NOME
            @ Li,093 Psay STR0025 //"Prior:"
            @ Li,100 Psay STF->TF_PRIORID
            MNTR605LIN()

            // linha da descricao da manutencao
            NGIMPMEMO(STF->TF_DESCRIC,70,23,STR0026,.F.,.F.,"MNTR605LIN()",58)

            // linha do periodo/tipo de manutencao
            @ Li,037  Psay STR0027 //"Periodo......:"
            If STF->TF_PERIODO = "R"
                @ Li,052  Psay STR0028 //"REPETITIVO"
            Else
                @ Li,052  Psay STR0029 //"UNICO"
            EndIf

            dbSelectArea("STD")
            dbSetOrder(01)
            dbSeek(xFilial("STD")+STF->TF_CODAREA)

            dbSelectArea("STF")
            @ Li,094 Psay STR0030 //"Area:"
            @ Li,100 Psay STF->TF_CODAREA
            @ Li,107 Psay STD->TD_NOME
            MNTR605LIN()

            @ Li,037 Psay STR0031 //"Dt.Ult.Manut.:"
            @ Li,052 Psay STF->TF_DTULTMA Picture '99/99/9999'

            @ LI,071 Psay STR0032 //"Parada:"
            If STF->TF_PARADA = "S" .or. STF->TF_PARADA = "T"
                @ Li,079 Psay STR0033 //"Sim"
            Else
                @ Li,079 Psay STR0034 //"Nao"
            EndIf
            @ Li,086 Psay STR0035 //"Parada Antes:"
            @ Li,100 Psay STF->TF_TEPAANT Picture "@E 999"
            @ Li,104 Psay STF->TF_UNPAANT
            @ Li,114 Psay STR0036 //"Depois:"
            @ Li,122 Psay STF->TF_TEPADEP Picture "@E 999"
            @ Li,148 Psay STF->TF_UNPADEP
            @ Li,168 Psay STR0037 //"N Util.:"
            @ Li,177 Psay STF->TF_NAOUTIL
            MNTR605LIN()

            @ Li,037 Psay STR0038 //"Tipo Acompan.:"
            @ Li,052 Psay STF->TF_TIPACOM
            @ Li,163 Psay STR0082 //"Manut. Ativa:"
            If STF->TF_ATIVO == "S"
                @ Li,177 Psay STR0033 //"Sim"
            Else
                @ Li,177 Psay STR0034 //"Nao"
            EndIf
            MNTR605LIN()
            MNTR605LIN()
            If STF->TF_TIPACOM = "T"
                @ Li,024 Psay STR0039 //"TEMPO"
                @ Li,065 Psay STR0040 //"Tempo Entre Manut:"
                @ Li,085 Psay STF->TF_TEENMAN Picture "@E 999"
                @ Li,089 Psay STF->TF_UNENMAN
                @ Li,132 Psay STR0041 //"Calendario:"
                @ Li,144 Psay STF->TF_CALENDA//numero calendario
                MNTR605LIN()

            ElseIf STF->TF_TIPACOM = "C"
                @ Li,024 Psay STR0042 //"CONTADOR"
                @ Li,132 Psay STR0041 //"Calendario:"
                @ Li,144 Psay STF->TF_CALENDA
                MNTR605LIN()
                @ Li,024 Psay STR0043 //"Cont.Ult.Man.:"
                @ Li,039 Psay STF->TF_CONMANU
                @ Li,084 Psay STR0045 //"Periodo Acomp.:"
                @ Li,100 Psay nPERACOM Picture "@E 999"
                @ Li,104 Psay cUNIACOM
                @ Li,120 Psay STR0044 //"Incremento Entre Manut:"
                @ Li,144 Psay Alltrim(Transform(STF->TF_INENMAN,"@E 999999999"))
                MNTR605LIN()
                @ Li,024 Psay STR0046 //"Cont.Bem.....:"
                @ Li,039 Psay cCodBem1
                MNTR605LIN()

            ElseIf STF->TF_TIPACOM = "F"
                @ Li,024 Psay STR0047 //"CON FIXO"
                @ Li,132 Psay STR0041 //"Calendario:"
                @ Li,144 Psay STF->TF_CALENDA
                MNTR605LIN()
                @ Li,024 Psay STR0043 //"Cont.Ult.Man.:"
                @ Li,039 Psay STF->TF_CONMANU
                @ Li,120 Psay STR0044 //"Incremento Entre Manut:"
                @ Li,144 Psay Alltrim(Transform(STF->TF_INENMAN,"@E 999999999"))
                @ Li,084 Psay STR0045 //"Periodo Acomp.:"
                @ Li,100 Psay nPERACOM Picture "@E 999"
                @ Li,104 Psay cUNIACOM
                MNTR605LIN()
                @ Li,038 Psay STR0046 //"Cont.Bem.....:"
                @ Li,053 Psay cCodBem1
                MNTR605LIN()

            ElseIf STF->TF_TIPACOM = "A"
                @ Li,024 Psay STR0048 //"AMBOS   "
                @ Li,065 Psay STR0040 //"Tempo Entre Manut:"
                @ Li,085 Psay STF->TF_TEENMAN Picture "@E 999"
                @ Li,089 Psay STF->TF_UNENMAN
                @ Li,120 Psay STR0041 //"Calendario:"
                @ Li,144 Psay STF->TF_CALENDA
                MNTR605LIN()
                @ Li,024 Psay STR0043 //"Cont.Ult.Man.:"
                @ Li,039 Psay STF->TF_CONMANU
                @ Li,120 Psay STR0044 //"Incremento Entre Manut:"
                @ Li,144 Psay Alltrim(Transform(STF->TF_INENMAN,"@E 999999999"))
                @ Li,084 Psay STR0045 //"Periodo Acomp.:"
                @ Li,100 Psay nPERACOM Picture "@E 999"
                @ Li,104 Psay cUNIACOM
                MNTR605LIN()
                @ Li,038 Psay STR0046 //"Cont.Bem.....:"
                @ Li,053 Psay cCodBem1
                MNTR605LIN()

            ElseIf STF->TF_TIPACOM = "S"
                @ Li,024 Psay STR0074 //"SEGUNDO CONTADOR"
                @ Li,120 Psay STR0041 //"Calendario:"
                @ Li,144 Psay STF->TF_CALENDA
                MNTR605LIN()
                @ Li,024 Psay STR0043 //"Cont.Ult.Man.:"
                @ Li,039 Psay STF->TF_CONMANU
                @ Li,120 Psay STR0044 //"Incremento Entre Manut:"
                @ Li,144 Psay Alltrim(Transform(STF->TF_INENMAN,"@E 999999999"))
                @ Li,084 Psay STR0045 //"Periodo Acomp.:"
                @ Li,100 Psay nPERACOM Picture "@E 999"
                @ Li,104 Psay cUNIACOM
                MNTR605LIN()
                @ Li,038 Psay STR0046 //"Cont.Bem.....:"
                @ Li,053 Psay cCodBem1
                MNTR605LIN()
            ElseIf STF->TF_TIPACOM = "P"
                @ Li,024 Psay STR0075 //"PRODUCAO"
                @ Li,120 Psay STR0041 //"Calendario:"
                @ Li,144 Psay STF->TF_CALENDA
                MNTR605LIN()
                @ Li,024 Psay STR0043 //"Cont.Ult.Man.:"
                @ Li,039 Psay STF->TF_CONMANU
                @ Li,120 Psay STR0044 //"Incremento Entre Manut:"
                @ Li,144 Psay Alltrim(Transform(STF->TF_INENMAN,"@E 999999999"))
                @ Li,084 Psay STR0045 //"Periodo Acomp.:"
                @ Li,100 Psay nPERACOM Picture "@E 999"
                @ Li,104 Psay cUNIACOM
                MNTR605LIN()
                @ Li,038 Psay STR0046 //"Cont.Bem.....:"
                @ Li,053 Psay cCodBem1
                MNTR605LIN()
            EndIf


            UltimaTarefa := Space(06)
            lPtarefa     := .T.
            // esta variavel ai em cima controla a ultima ou a primeira tarefa????

            dbSelectArea("ST5")
            dbSetOrder(06) //T5_FILIAL+T5_CODBEM+T5_SERVICO+T5_SEQRELA+STR(T5_SEQUENC,3,0)+T5_TAREFA
            dbSeek(xFilial('ST5')+STF->TF_CODBEM+STF->TF_SERVICO+STF->TF_SEQRELA)
            While !Eof() .And. ST5->T5_FILIAL = xFilial("ST5") .And. ST5->T5_CODBEM = STF->TF_CODBEM;
                    .And. ST5->T5_SERVICO = STF->TF_SERVICO .And. ST5->T5_SEQRELA = STF->TF_SEQRELA

                If lPtarefa
                    MNTR605LIN()
                    @ Li,024 Psay STR0049 //"-----------------------------------------------------------------------------------------------TAREFAS--------------------------------------------------------------------------------------------"
                    lPtarefa := .F.
                EndIf

                If ST5->T5_TAREFA != UltimaTarefa
                    MNTR605LIN()
                    @ Li,024 Psay ST5->T5_TAREFA //Picture "999999"

                    // se tarefa 0 deve imprimir a constante
                    If ALLTRIM(ST5->T5_TAREFA) == "0"
                        @ Li,032 Psay STR0050 //"SEM ESPECIFICACAO DE TAREFA"
                    Else
                        @ Li,032 Psay ST5->T5_DESCRIC
                    EndIf
                    MNTR605LIN()

                    nDeslocamento := 046
                    lPrimeiro = .T.
                    dbSelectArea("STM")
                    dbSetOrder(01)
                    dbSeek(xFilial('STM')+ST5->T5_CODBEM+ST5->T5_SERVICO+ST5->T5_SEQRELA+ST5->T5_TAREFA)
                    While !Eof() .And. STM->TM_FILIAL = xFilial("STM") .And. STM->TM_CODBEM = ST5->T5_CODBEM .And.;
                            STM->TM_SERVICO = ST5->T5_SERVICO .And. STM->TM_SEQRELA = ST5->T5_SEQRELA .And.;
                            STM->TM_TAREFA  = ST5->T5_TAREFA

                        If lPrimeiro = .T.
                            @ Li,032 Psay STR0051 //"Dependencias:"
                        EndIf
                        @ Li,nDeslocamento Psay STM->TM_DEPENDE
                        @ Li,(nDeslocamento + 13) Psay STM->TM_SOBREPO

                        If nDeslocamento = 126
                            nDeslocamento = 046
                        EndIf
                        MNTR605LIN()
                        dbSkip()
                    End

                    lprimeiro = .F.
                    dbSelectArea("STH")
                    dbSetOrder(03)
                    dbSeek(xFilial('STH')+ST5->T5_CODBEM+ST5->T5_SERVICO+ST5->T5_SEQRELA+ST5->T5_TAREFA)
                    While !Eof() .And. STH->TH_FILIAL = xFilial("STH") .And. STH->TH_CODBEM = ST5->T5_CODBEM .And.;
                            STH->TH_SERVICO = ST5->T5_SERVICO .And. STH->TH_SEQRELA = ST5->T5_SEQRELA .And.;
                            STH->TH_TAREFA = ST5->T5_TAREFA

                        If !lprimeiro
                            MNTR605LIN()
                            @ Li,032 Psay STR0052 //"---------------------------------------------------------------------------------------ETAPAS---------------------------------------------------------------------------------------------"
                            MNTR605LIN()
                            lprimeiro = .T.
                        EndIf

                        @ Li,032 Psay STH->TH_ETAPA

                        dbSelectArea("TPA")
                        dbSetOrder(01)
                        dbSeek(xFilial("TPA")+STH->TH_ETAPA)

                        NGIMPMEMO(TPA->TPA_DESCRI,92,40,,.F.,.F.,"MNTR605LIN()",58)

                        If STH->TH_OPCOES <> "S" .And. !Empty(STH->TH_OPCOES)
                            @ Li,044 Psay STR0053 //"-----------------------------------------------------------------OPCOES-----------------------------------------------------------------"
                            MNTR605LIN()
                            @ Li,044 Psay STR0054 //"Opcao      Tipo Res. Con. Informacao Tp. Manut. Bem              Serv.  Seq."
                            MNTR605LIN()
                            dbSelectArea("TP1")
                            dbSetOrder(01)
                            dbSeek(xFilial('TP1')+STH->TH_CODBEM+STH->TH_SERVICO+STH->TH_SEQRELA+STH->TH_TAREFA+STH->TH_ETAPA)
                            While !Eof() .And. TP1->TP1_FILIAL = xFilial("TP1") .And. STH->TH_CODBEM == TP1->TP1_CODBEM;
                                    .And. STH->TH_SERVICO == TP1->TP1_SERVIC .And. STH->TH_SEQRELA == TP1->TP1_SEQREL;
                                    .And. STH->TH_TAREFA  == TP1->TP1_TAREFA .And. STH->TH_ETAPA == TP1->TP1_ETAPA


                                @ Li,044 Psay TP1->TP1_OPCAO

                                If !Empty(TP1->TP1_TIPRES)
                                    ctipores := STR0055 //"Informada"
                                    If TP1->TP1_TIPRES == 'M'
                                        ctipores := STR0056 //"Marcada"
                                    EndIf
                                    @ Li,060 Psay ctipores
                                EndIf

                                If !Empty(TP1->TP1_CONDOP)

                                    ccondop := ALLTRIM(TP1->TP1_CONDOP)

                                    If ccondop == 'I'
                                        ccondop := "="
                                    ElseIf ccondop == '<I'
                                        ccondop := "<="
                                    ElseIf ccondop == '>I'
                                        ccondop := ">="
                                    EndIf

                                    @ Li,077 Psay ccondop

                                EndIf

                                If !Empty(TP1->TP1_CONDIN)
                                    @ Li,088 Psay TP1->TP1_CONDIN
                                EndIf

                                ctpmanu := STR0057 //"Não Gera"
                                If TP1->TP1_TPMANU == 'C'
                                    ctpmanu := STR0058 //"Corretiva"
                                ElseIf TP1->TP1_TPMANU == 'P'
                                    ctpmanu := STR0059 //"Preventiva"
                                ElseIf TP1->TP1_TPMANU == 'O'
                                    ctpmanu := STR0060 //"Outras"
                                EndIf

                                @ Li,118 Psay ctpmanu

                                If !Empty(TP1->TP1_BEMIMN)
                                    @ Li,139 Psay TP1->TP1_BEMIMN
                                EndIf

                                If !Empty(TP1->TP1_SERVMN)
                                    @ Li,159 Psay TP1->TP1_SERVMN
                                EndIf

                                If !Empty(TP1->TP1_SEQUMN)
                                    @ Li,171 Psay TP1->TP1_SEQUMN
                                EndIf

                                MNTR605LIN()
                                dbSelectArea("TP1")
                                dbSkip()
                            End
                            MNTR605LIN()
                        EndIf
                        dbSelectArea("STH")
                        dbSkip()
                    End
                    UltimaTarefa = ST5->T5_TAREFA
                EndIf

                lprimeiro = .F.
                dbSelectArea("STG")
                dbSetOrder(01)
                dbSeek(xFilial('STG')+ST5->T5_CODBEM+ST5->T5_SERVICO+ST5->T5_SEQRELA+ST5->T5_TAREFA)
                While !Eof() .And. STG->TG_FILIAL = xFilial("STG") .And. STG->TG_CODBEM = ST5->T5_CODBEM;
                        .And. STG->TG_SERVICO = ST5->T5_SERVICO .And. STG->TG_SEQRELA = ST5->T5_SEQRELA;
                        .And. STG->TG_TAREFA = ST5->T5_TAREFA

                    If !lprimeiro
                        MNTR605LIN()
                        @ Li,032 Psay Replicate("-",nTamPro)+"-------------------------------------------INSUMOS----------------------------------------------------"
                        MNTR605LIN()
                        @ Li,032 Psay "Nome......... Codigo........."+Replicate(".",nTamPro)+" Descricao............................. Quant. Consumo.. Un. Res Destino"
                        MNTR605LIN()
                        lprimeiro = .T.
                    EndIf

                    Do Case // Nome do Insumo
                    Case STG->TG_TIPOREG = "E"
                        dbSelectArea("ST0")
                        dbSetOrder(01)
                        dbSeek(xFilial("ST0")+Substr(STG->TG_CODIGO,1,3))
                        NomeInsumo = ST0->T0_NOME
                        TipoInsumo = STR0063 //"ESPECIALIDADE"
                    Case STG->TG_TIPOREG = "P"
                        dbSelectArea("SB1")
                        dbSetOrder(01)
                        dbSeek(xFilial('SB1')+STG->TG_CODIGO)
                        NomeInsumo = Substr(SB1->B1_DESC,1,38)
                        TipoInsumo = STR0064 //"PRODUTO"
                    Case STG->TG_TIPOREG = "M"
                        dbSelectArea("ST1")
                        dbSetOrder(01)
                        dbSeek(xFilial('ST1')+Substr(STG->TG_CODIGO,1,6))
                        NomeInsumo = ST1->T1_NOME
                        TipoInsumo = STR0065 //"FUNCIONARIO"
                    Case STG->TG_TIPOREG = "F"
                        dbSelectArea("SH4")
                        dbSetOrder(01)
                        dbSeek(xFilial("SH4")+Substr(STG->TG_CODIGO,1,6))
                        NomeInsumo = SH4->H4_DESCRI
                        TipoInsumo = STR0066 //"FERRAMENTA"
                    Case STG->TG_TIPOREG = "T"
                        dbSelectArea("SA2")
                        dbSetOrder(01)
                        dbSeek(xFilial("SA2")+Substr(STG->TG_CODIGO,1,6))
                        TipoInsumo = STR0067 //"TERCEIRO"
                        NomeInsumo = SA2->A2_NOME
                    EndCase

                    @ Li,032 Psay TipoInsumo
                    @ Li,046 Psay STG->TG_CODIGO
                    @ Li,062+nTamPro Psay NomeInsumo
                    @ Li,103+nTamPro Psay STG->TG_QUANREC Picture "@E 999"
                    @ Li,106+nTamPro Psay STG->TG_QUANTID Picture "@E 999999.99"
                    @ Li,119+nTamPro Psay STG->TG_UNIDADE
                    If STG->TG_RESERVA = "S"
                        @ Li,122+nTamPro Psay STR0033 //"SIM"
                    Else
                        @ Li,122+nTamPro Psay STR0034 //"NAO"
                    EndIf
                    If STG->TG_TIPOREG ="P"
                        Do Case
                        Case STG->TG_DESTINO = "A"
                            @ Li,126+nTamPro Psay STR0068 //"Apoio"
                        Case STG->TG_DESTINO = "S"
                            @ Li,126+nTamPro Psay STR0069 //"Substit"
                        Case STG->TG_DESTINO = "T"
                            @ Li,126+nTamPro Psay STR0070 //"Troca"
                        EndCase
                    EndIf
                    MNTR605LIN()
                    dbSelectArea("STG")
                    dbSkip()
                End
                dbSelectArea("ST5")
                dbSkip()
            End
            dbSelectArea("STF")
            dbSkip()
        EndDo
        dbSelectArea("ST9")
        dbSetOrder(02)
        dbGoTo(nAtual)
        dbSkip()
        MNTR605LIN()
    EndDo

    If lImpRel
        Roda(nCntImpr,cRodaTxt,Tamanho)
    Else
        MsgInfo(STR0084, STR0083)
        Return .F.
    EndIf

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Devolve a condicao original do arquivo principal             ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    RetIndex("ST9")
    RetIndex("STC")
    RetIndex("SHB")
    RetIndex("STF")
    RetIndex("ST4")
    RetIndex("STE")
    RetIndex("STD")
    RetIndex("STG")
    RetIndex("ST5")
    RetIndex("STM")
    RetIndex("STH")
    RetIndex("ST0")
    RetIndex("ST1")
    RetIndex("SB1")
    RetIndex("SH4")
    RetIndex("SA2")
    RetIndex("ST6")
    Set Filter To
    Set device to Screen

    If aReturn[5] = 1
        Set Printer To
        dbCommitAll()
        OurSpool(wnrel)
    EndIf

    MS_FLUSH()

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Quebra   ³ Autor ³ NG Informatica Ltdaa  ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Impressao do Cabecalho de Quebra                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ QUEBRA                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Quebra(cQuebraCusto,cQuebraTraba,cQuebraFamil)

    @ Li,000 Psay STR0071 //"BENS DO CENTRO DE CUSTO = "
    @ Li,026 Psay cQuebraCusto
    @ Li,048 Psay STR0072 //"CENTRO DE TRABALHO = "
    @ Li,069 Psay cQuebraTraba

    If MV_PAR09 >= 2
        @ Li,079 Psay STR0073 //"FAMILIA = "
        @ Li,089 Psay cQuebraFamil
    EndIf

    Li++
    @ Li,000 Psay Replicate("*",220)//************************************************************************************************************************************"
    MNTR605LIN()

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MNTR605LIN³ Autor ³ NG Informatica Ltda   ³ Data ³   /06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Incrementa Linha e Controla Salto de Pagina                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe  ³ MNTR605LIN()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR605                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function MNTR605LIN()

    Li++
    If Li > 58
        Cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
        Quebra(cQuebraCusto,cQuebraTraba,cQuebraFamil)
    EndIf

Return .T.