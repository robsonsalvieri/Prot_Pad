#INCLUDE "MNTR220.CH"
#INCLUDE "PROTHEUS.CH"

//---------------------------------------------------------------------
/*/{Protheus.doc} MNTR220
Imprime o Relatorio Personalizavel
@return
@param
@sample MNTR220()
@author Alexandre Santos
@since 11/07/2017
/*/
//---------------------------------------------------------------------
Function MNTR220()
	Local aArea := GetArea()
	Private aEtapas := {}
	Private nX
	Private oOrdem
	Private oTempTable //Obj. Tab. Temporária
	Private cTRB 		:= GetNextAlias() //Alias Tab. Temp.

	aNGBEGINPRM = NGBEGINPRM(,,,,.T.)
	If FindFunction("TRepInUse") .And. TRepInUse()
		oReport := ReportDef()
		oReport:SetLandscape() //Default Paisagem
		oReport:PrintDialog()
	Else
		MNTR220R3()
	EndIf
	RestArea(aArea)
	NGRETURNPRM(aNGBEGINPRM)
Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} ReportDef
Monta a Impressão do Relatório Personalizavel.
@type static

@author Alexandre Santos
@since 11/07/2017

@param
@return oReport, Objeto, Objeto de impressão TReport
/*/
//---------------------------------------------------------------------
Static Function ReportDef()

	Local oReport
	Local oSecCcusto
	Local oSecFunc
	Local oSecOrdem
	Local oSecEtapa
	Local oBreak

	oReport := TReport():New("MNTR220",OemToAnsi(STR0006),"MNT22R",{|oReport| ReportPrint(oReport)},;
			STR0001+". "+STR0002+" "+STR0003)

	/*---------------------------------------+
	| Variaveis utilizadas para parametros!  |
	| MV_PAR01 - De  Periodo                 |
	| MV_PAR02 - Ate Periodo                 |
	| MV_PAR03 - De  Centro Custo            |
	| MV_PAR04 - Ate Centro Custo            |
	| MV_PAR05 - De  Centro de Trabalho      |
	| MV_PAR06 - Ate Centro de Trabalho      |
	| MV_PAR07 - De  Bem                     |
	| MV_PAR08 - Ate Bem                     |
	| MV_PAR09 - De  Familia                 |
	| MV_PAR10 - Ate Familia                 |
	| MV_PAR11 - De  Etapa                   |
	| MV_PAR12 - Ate Etapa                   |
	| MV_PAR13 - De  Funcionario             |
	| MV_PAR14 - Ate Funcionario             |
	+---------------------------------------*/

	Pergunte(oReport:uParam,.F.)

	/*/
	1         2         3         4         5         6         7         8         9         0         1         2         3         4
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	**************************************************************************************************************************************************************************************
	Código      Centro de custo             Matricula   Funcionário
	********************************************************************************************
	XXXXXX      XXXXXXXXXXXXXXXXXXXXXXXXX   XXXXXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	O.S.        Plano       Tipo      Bem      Nome                          Serviço   Descrição                      Quant.  Unid. Data Inic. Hora Inic. Data Fim   Hora Fim
	***********************************************************************************************************************************************************************
	XXXXXXXXX   XXXXXXXXX   XXXXXX    XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XXXXX   X     99/99/9999 99:99      99/99/9999 99:99
	            XXXXXXXXX   XXXXXX    XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XXXXX   X     99/99/9999 99:99      99/99/9999 99:99
	            XXXXXXXXX   XXXXXX    XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XXXXX   X     99/99/9999 99:99      99/99/9999 99:99
																										Total horas:  XXXXX
				Etapa       Descrição
				************************************************************************************************************************************
				XXXXXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
				XXXXXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	XXXXXXXXX   XXXXXXXXX   XXXXXX    XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XXXXX   X     99/99/9999 99:99      99/99/9999 99:99
	            XXXXXXXXX   XXXXXX    XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XXXXX   X     99/99/9999 99:99      99/99/9999 99:99
																										Total horas:  XXXXX
				Etapa       Descrição
				************************************************************************************************************************************
				XXXXXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
				XXXXXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	XXXXXXXXX   XXXXXXXXX   XXXXXX    XXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXX XXXXXXXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XXXXX   X     99/99/9999 99:99      99/99/9999 99:99
																										Total horas:  XXXXX
				Etapa       Descrição
				************************************************************************************************************************************
				XXXXXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
				XXXXXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
	*/

	//Centro de Custo
	oSecCcusto 	:= TRSection():New(oReport, STR0041, {"cTRB", "SI3"})
		TRCell():New(oSecCcusto, "(cTRB)->CUSTO"	, "cTRB", STR0041, "@!",12)//Centro de Custo
		TRCell():New(oSecCcusto, "I3_DESC"			, "SI3"	, STR0042, "@!",30)//Descrição
		TRPosition():New(oSecCcusto,"SI3",1,{|| xFilial("SI3") + (cTRB)->CUSTO})

	//Funcionário
	oSecFunc 	:= TRSection():New(oReport, STR0044, {"cTRB", "ST1"},,,,,,,,,,12,)
		TRCell():New(oSecFunc, "(cTRB)->CODFUNC"	, "cTRB", STR0043, "@!",12)//Código Funcionário
		TRCell():New(oSecFunc, "T1_NOME"			, "ST1"	, STR0044, "@!",40)//Nome
		TRPosition():New(oSecFunc,"ST1",1,{|| xFilial("ST1") + (cTRB)->CODFUNC})

	//Ordem de Serviço
	oSecOrdem := TRSection():New(oReport,"Detalhes da O.S.",{"cTRB", "ST9", "ST4"},,,,,,,,,,24,)
		oOrdem := TRCell():New(oSecOrdem, "(cTRB)->ORDEM"	, "cTRB", STR0045, "@!"	,12)//Número OS
		TRCell():New(oSecOrdem, "(cTRB)->PLANO"		, "cTRB", STR0046, "@!"	,12)//Plano
		TRCell():New(oSecOrdem, "TJ_TIPOOS"			, "STJ"	, STR0047, "@!"	,12)//Tipo
		TRCell():New(oSecOrdem, "(cTRB)->CODBEM"	, "cTRB", STR0048, "@!"	,12)//Código Bem
		TRCell():New(oSecOrdem, "T9_NOME"			, "ST9"	, STR0049, "@!"	,40)//Descrição Bem
		TRCell():New(oSecOrdem, "(cTRB)->SERVICO"	, "cTRB", STR0050, "@!"	,12)//Código Serviço
		TRCell():New(oSecOrdem, "T4_NOME"			, "ST4"	, STR0042, "@!"	,40)//Descrição Serv.
		TRCell():New(oSecOrdem, "(cTRB)->QUANT"		, "cTRB", STR0051, ""	,08)//Quantidade
		TRCell():New(oSecOrdem, "(cTRB)->UNIDADE"	, "cTRB", STR0052, "@!"	,08)//Unidade
		TRCell():New(oSecOrdem, "(cTRB)->DTINICI"	, "cTRB", STR0053, ""	,14)//Data Inicial
		TRCell():New(oSecOrdem, "(cTRB)->HOINICI"	, "cTRB", STR0054, ""	,14)//Hora Inicial
		TRCell():New(oSecOrdem, "(cTRB)->DTFIM"		, "cTRB", STR0055, ""	,14)//Data Final
		TRCell():New(oSecOrdem, "(cTRB)->HOFIM"		, "cTRB", STR0056, ""	,14)//Hora Final
		oSecOrdem:Cell("(cTRB)->QUANT"):SetHeaderAlign("RIGHT")
		oBreak := TRBreak():New(oSecFunc,".T.", STR0058,.F.)  //"Total de Horas"

		TRFunction():New( oSecOrdem:Cell( '(cTRB)->QUANT' ), /*Id*/, 'TIMESUM', oBreak, /*cTitle*/, /*cPicture*/, /*uFormula*/, .F./*lEndSection*/, .F./*lEndReport*/, .F./*lEndPage*/ )

		TRPosition():New(oSecOrdem	,"ST9"	,1	,{|| xFilial("ST9") + (cTRB)->CODBEM	})
		TRPosition():New(oSecOrdem	,"ST4"	,1	,{|| xFilial("ST4") + (cTRB)->SERVICO	})
		TRPosition():New(oSecOrdem	,"STJ"	,1	,{|| xFilial("STJ") + (cTRB)->ORDEM	})

	//Etapas
	oSecEtapa := TRSection():New(oReport, "Etapas", {"cTRB", "TPA"},,,,,,,,,,24,)
		TRCell():New(oSecEtapa, "ETAPA"		, ""	, STR0057, "@!",12, .T., {||aEtapas[nX]})//Código Etapa
		TRCell():New(oSecEtapa, "TPA_DESCRI", "TPA"	, STR0042, "@!",150)						//Descrição Etapa
		TRPosition():New(oSecEtapa,"TPA",1,{|| xFilial("SI3") + aEtapas[nX]})

Return oReport

//---------------------------------------------------------------------
/*/{Protheus.doc} ReportPrint
Realiza a impressão do relatorio no modelo TReport.
@param 		oReport, Objeto, Instancia de objeto oReport.
@return  	.T. -> Impressao concluida.
			.F. -> Nao imprimiu.
@sample ReportPrint(oReport)
@author Alexandre Santos
@since 11/07/2017
/*/
//---------------------------------------------------------------------
Static Function ReportPrint(oReport)
	Local oSecCcusto 	:= oReport:Section(1)
	Local oSecFunc 		:= oReport:Section(2)
	Local oSecOrdem 	:= oReport:Section(3)
	Local oSecEtapa 	:= oReport:Section(4)
	Local nFor, nOrdem, cFunc, cOrdem, cCCusto
	Private cArqTrab
	Private cTRB 		:= GetNextAlias()
	//Cria tabela temporária
	MontaTRB()
	dbSelectArea(cTRB)
	dbGotop()
	oReport:SetMeter(RecCount())
	While !EOF() .And. !oReport:Cancel()
		cCCusto := (cTRB)->Custo
		oSecCcusto:Init()
		oSecCcusto:PrintLine()
		While !EOF() .And. cCCusto == (cTRB)->CUSTO
			cFunc  := (cTRB)->CODFUNC
			oSecFunc:Init()
			oSecFunc:PrintLine()
			While !EOF() .And. cCCusto == (cTRB)->CUSTO .And. cFunc == (cTRB)->CODFUNC
				oOrdem:lVisible := .T.
				cOrdem  := (cTRB)->ORDEM
				cPlano	:= (cTRB)->PLANO
				nOrdem  := 0
				aEtapas := {}
				While !EOF() .And. cCCusto == (cTRB)->CUSTO .And.;
			 	 cFunc == (cTRB)->CODFUNC .And. cOrdem == (cTRB)->ORDEM .And. cPlano == (cTRB)->PLANO
					nOrdem++
					If nOrdem > 1
						oOrdem:Hide()
					EndIf
					oSecOrdem:Init()
					oSecOrdem:PrintLine()
					If !Empty((cTRB)->ETAPA)
						Aadd(aEtapas,(cTRB)->ETAPA)
					EndIf
					DbSelectArea(cTRB)
					Dbskip()
				End
				If Len(aEtapas) > 0
					oSecEtapa:Init()
					For nFor := 1 To Len(aEtapas)
						nX := nFor
						oSecEtapa:PrintLine()
					Next x
					oSecEtapa:Finish()
				EndIf
				oSecOrdem:Finish()
			End
			oSecFunc:Finish()
		End
		oSecCcusto:Finish()
	End
	oTempTable:Delete()//Deleta tabela temporária
Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MontaTRB
Reaaliza criação e preenchimento da tabela temporaria TRB
@param
@return
@sample MontaTRB()
@author Alexandre Santos
@since 12/07/2017
/*/
//---------------------------------------------------------------------
Static Function MontaTRB()
	aDBF := {}
		Aadd(aDBF,{"CUSTO"  ,"C",20, 0 })
		Aadd(aDBF,{"CODFUNC","C",06, 0 })
		Aadd(aDBF,{"TURNO"  ,"C",03, 0 })
		Aadd(aDBF,{"ORDEM"  ,"C",06, 0 })
		Aadd(aDBF,{"PLANO"  ,"C",06, 0 })
		Aadd(aDBF,{"CODBEM" ,"C",16, 0 })
		Aadd(aDBF,{"SERVICO","C",06, 0 })
		Aadd(aDBF,{"SEQRELA","C",03, 0 })
		Aadd(aDBF,{"QUANT"  ,"N",09, 2 })
		Aadd(aDBF,{"UNIDADE","C",03, 0 })
		Aadd(aDBF,{"DTINICI","D",10, 0 })
		Aadd(aDBF,{"HOINICI","C",05, 0 })
		Aadd(aDBF,{"DTFIM"  ,"D",10, 0 })
		Aadd(aDBF,{"HOFIM"  ,"C",05, 0 })
		Aadd(aDBF,{"TIPOHOR","C",01, 0 })
		Aadd(aDBF,{"ETAPA"  ,"C",06, 0 })
	//Instancia classe FWTemporaryTable
    oTempTable := FWTemporaryTable():New( cTRB, aDBF )
    //Cria indices
    oTempTable:AddIndex( "Ind01" , {"CUSTO","CODFUNC","ORDEM","PLANO"} )
    //Cria a tabela temporaria
    oTempTable:Create()
	Processa({|lEND| MNTR220ST1()},STR0009+STR0010) //"Processando Arquivo..."
	Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTR220R3  ³ Autor ³ Deivys Joenck         ³ Data ³ 11/10/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Atividades de equipe de manutencao                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³OBSERVACAO³ Refeito em 03/10/2002                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR220R3()

	Local WNREL      := "MNTR220"
	Local LIMITE     := 132
	Local nHelp 	 := 0
	Local cDESC1     := STR0001 //"Atividades da Equipe de Manutencao. A equipe de manutencao"
	Local cDESC2     := STR0002 //"e identificada pelo Centro de Custo a que pertence. As ocorrencias"
	Local cDESC3     := STR0003 //"desejadas poderao ser selecionadas atraves da opcao de parametros."
	Local cSTRING    := "ST9"
	Local lCorrige   := .F.

	//³Guarda conteudo e declara variaveis padroes
	Local aNGBEGINPRM := NGBEGINPRM(,,,,.T.)
	Private NOMEPROG := "MNTR220"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0004,1,STR0005,1,2,1,"",1}    //"Zebrado"###"Administracao"
	Private TITULO   := STR0006 //"Atividades da Equipe de Manutencao."
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2

	//+----------------------------------------------+
	//³ Variaveis utilizadas para parametros!        |
	//³ mv_par01     // De  Periodo                  |
	//³ mv_par02     // Ate Periodo                  |
	//³ mv_par03     // De  Centro Custo             |
	//³ mv_par04     // Ate Centro Custo             |
	//³ mv_par05     // De  Centro de Trabalho       |
	//³ mv_par06     // Ate Centro de Trabalho       |
	//³ mv_par07     // De  Bem                      |
	//³ mv_par08     // Ate Bem                      |
	//³ mv_par09     // De  Familia                  |
	//³ mv_par10     // Ate Familia                  |
	//³ mv_par11     // De  Etapa                    |
	//³ mv_par12     // Ate Etapa                    |
	//³ mv_par13     // De  Funcionario              |
	//³ mv_par14     // Ate Funcionario              |
	//+----------------------------------------------+

	Private cPERG := "MNT22R"

	Pergunte(cPERG,.F.)
	// Envia controle para a funcao SETPRINT
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		dbSelectArea("ST9")
		Return
	EndIf
	SetDefault(aReturn,cSTRING)

	RptStatus({|lEND| MNTR220IMP(@lEND,WNREL,TITULO,TAMANHO)},TITULO)
	dbSelectArea("ST9")

	NGRETURNPRM(aNGBEGINPRM)

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR220IMP³ Autor ³ Deivys Joenck         ³ Data ³ 02/10/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada do Relat¢rio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR220                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR220IMP(lEND,WNREL,TITULO,TAMANHO)

	Local cRODATXT := ""
	Local nCNTIMPR := 0
	Local nMULT    := 1,xx
	Local vVETHORAS := {}

	Private li := 80 ,m_pag := 1
	Private lImprime	:= .F.






	nTIPO  := IIf(aReturn[4]==1,15,18)
	CABEC1 := STR0007 //"O.S.   Plano  Tipo          Bem/Localização  Nome                      Servic Descricao            QTD Uni ....Inicio.... ......Fim....."
	CABEC2 := " "

	/*/
	1         2         3         4         5         6         7         8         9         0         1         2         3         4
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	*******************************************************************************************************************************************************
	O.S.   Plano  Tipo          Bem/Localização  Nome                      Servico Descricao           Quant. Unid.   ......Inicio.....    .......Fim......
	*******************************************************************************************************************************************************
	Centro de Custo..: xxxxxxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	Funcionario......: xxxxxx    xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	xxxxxx xxxxxx xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxx xxxxxx xxxxxxxxxxxxxxxxxxxx 999999999 xxx xx/xx/xxxx xx:xx xx/xx/xxxx xx:xx
	Bem Pai..: xxxxxxxxxxxxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

	Etapa  Descricao
	xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	xxxxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
	*/
	MontaTRB()




	dbSelectArea(cTRB)
	dbGotop()
	SetRegua(LastRec())
	cPAI := Space(Len((cTRB)->CODBEM))
	While !Eof()
		NGSOMALI(58)
		cCUSTOR := (cTRB)->CUSTO
		@LI,000 Psay STR0009 //"C.Custo de Custo......:"
		@LI,019 Psay Alltrim((cTRB)->CUSTO)+" - "+NGSEEK('SI3',(cTRB)->CUSTO,1,'I3_DESC')

		dbSelectArea(cTRB)
		While !Eof() .And. (cTRB)->CUSTO == cCUSTOR
			cCODFUNCR := (cTRB)->CODFUNC
			NGSOMALI(58)
			@LI,000 Psay STR0010 // "Funcionario.:"
			@LI,019 Psay Alltrim((cTRB)->CODFUNC)+" - "+NGSEEK('ST1',(cTRB)->CODFUNC,1,'SUBSTR(T1_NOME,1,40)')
			NGSOMALI(58)
			cTHORATRAB   := 0
			dbSelectArea(cTRB)
			While !Eof() .And. (cTRB)->CUSTO == cCUSTOR .And. (cTRB)->CODFUNC == cCODFUNCR
				cORDEM  := (cTRB)->ORDEM
				cPLANO  := (cTRB)->PLANO
				lDiff   := .t.
				aETAPAS := {}
				While !Eof() .And. (cTRB)->CUSTO == cCUSTOR .And. (cTRB)->CODFUNC == cCODFUNCR .And.;
				(cTRB)->ORDEM == cORDEM .And. (cTRB)->PLANO == cPLANO

					IncRegua()
					cPAI := Space(Len((cTRB)->CODBEM))
					dbSelectArea("STC")
					dbSetOrder(03)
					If dbSeek(xFILIAL("STC")+(cTRB)->CODBEM)
						cPAI := STC->TC_CODBEM
					EndIf

					vVETHORAS  := NGRETHORDDH((cTRB)->QUANT)
					cHORAA := vVETHORAS[1]
					cTHORATRAB := NGSOMAHNUM((cTRB)->QUANT,cTHORATRAB)

					NGSOMALI(58)
					dbSelectArea(cTRB)
					If lDiff
						@LI,000 Psay (cTRB)->ORDEM
						@LI,007 Psay (cTRB)->PLANO
					EndIf
					lDiff := .f.
					@LI,014 Psay NGRETSX3BOX('TJ_TIPOOS',NGSEEK('STJ',(cTRB)->ORDEM,1,'TJ_TIPOOS'))
					@LI,028 Psay (cTRB)->CODBEM
					If NGSEEK('STJ',(cTRB)->ORDEM,1,'TJ_TIPOOS') == "B"
						@LI,045 Psay NGSEEK('ST9',(cTRB)->CODBEM,1,'SUBSTR(ST9->T9_NOME,1,24)')
					Else
						@LI,045 Psay NGSEEK('TAF',(cTRB)->CODBEM,8,'SUBSTR(TAF->TAF_NOMNIV,1,24)')
					EndIf
					@LI,071 Psay (cTRB)->SERVICO
					@LI,079 Psay NGSEEK('ST4',(cTRB)->SERVICO,1,'SUBSTR(ST4->T4_NOME,1,18)')

					If Len(cHORAA) <= 5
						@LI,100 Psay If(Len(cHORAA) = 4,"0"+cHORAA,cHORAA)
					Else
						@LI,099 Psay If(Len(cHORAA) = 4,"0"+cHORAA,cHORAA)
					Endif

					@LI,106 Psay (cTRB)->UNIDADE Picture 'H'
					@LI,114 Psay (cTRB)->DTINICI Picture '99/99/9999'
					@LI,126 Psay (cTRB)->HOINICI Picture '99:99'
					@LI,135 Psay (cTRB)->DTFIM   Picture '99/99/9999'
					@LI,146 Psay (cTRB)->HOFIM   Picture '99:99'
					If (cTRB)->CODBEM != cPAI .And. !Empty(cPAI)
						NGSOMALI(58)
						@ LI,014 Psay STR0011 //"Bem Pai..:"
						@ LI,025 Psay cPAI
						@ LI,042 Psay NGSEEK('ST9',cPAI,1,'T9_NOME')
					Endif
					If !Empty((cTRB)->ETAPA)
						Aadd(aETAPAS,{(cTRB)->ETAPA})
					EndIf
					dbSelectArea(cTRB)
					DbSkip()
				End
				// IMPRIME AS ETAPAS
				lPRIN := .T.
				If Len(aETAPAS) > 0
					For xx := 1 To Len(aETAPAS)
						If lPRIN
							NGSOMALI(58)
							NGSOMALI(58)
							@LI,014 Psay STR0012 //"Etapa  Descricao da Etapa"
							lPRIN := .F.
						EndIf
						NGSOMALI(58)
						@LI,014 Psay aETAPAS[xx][1]
						@LI,021 Psay NGSEEK('TPA',aETAPAS[xx][1],1,'TPA_DESCRI')
					Next xx
				EndIf
				If !lPRIN
					NGSOMALI(58)
				EndIf
				dbSelectArea(cTRB)
			End
			NGSOMALI(58)
			@LI,084 Psay STR0028 //"Total..:"
			vVETHORAS := NGRETHORDDH(cTHORATRAB)
			cHORATCC  := vVETHORAS[1]

			If Len(cHORATCC) <= 5
				@LI,100 Psay If(Len(cHORATCC) = 4,"0"+cHORATCC,cHORATCC)
			Else
				@LI,099 Psay If(Len(cHORATCC) = 4,"0"+cHORATCC,cHORATCC)
			EndIf
			NGSOMALI(58)
		End
	End
	If !lImprime
		MsgInfo(STR0039,STR0040) // "Não existem dados para montar o relatório!" ## "ATENÇÃO"
		Return .F.
	Endif
	RODA(nCNTIMPR,cRODATXT,TAMANHO)
	RetIndex("ST9")
	Set Filter To
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	Ms_Flush()
	oTempTable:Delete()//Deleta tabela temporária

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR220ST1³ Autor ³ In cio Luiz Kolling   ³ Data ³01/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Processa arquivo de funcionarios                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR220                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR220ST1()

	cCONDST1 := 'st1->t1_ccusto >= mv_par03 .And. st1->t1_ccusto <= mv_par04'

	cCONDSTJ := 'stj->tj_situaca = "L" '
	cCONDSTJ := cCONDSTJ + '.and. stj->tj_centrab >= mv_par05 .and. stj->tj_centrab <= mv_par06'

	cCONDSTL := 'Alltrim(stl->tl_seqrela) > "0" .And. !Empty(stl->tl_seqrela) .And.'
	cCONDSTL := cCONDSTL + '(stl->tl_dtinici >= mv_par01 .And. stl->tl_dtinici <= mv_par02)'

	cCONDSTS := 'sts->ts_situaca = "L"'
	cCONDSTS := cCONDSTS + '.and. sts->ts_centrab >= mv_par05 .and. sts->ts_centrab <= mv_par06'

	cCONDSTT := 'Val(stt->tt_seqrela) > 0 .And.'
	cCONDSTT := cCONDSTT + '(stt->tt_dtinici >= mv_par01 .And. stt->tt_dtinici <= mv_par02)'

	cCONDST9 := 'ST9->(Found()) .And. st9->t9_codbem >= mv_par07 .And. st9->t9_codbem <= mv_par08'
	cCONDST9 := cCONDST9 +'.and. st9->t9_codfami >= mv_par09 .and. st9->t9_codfami <= mv_par10 '

	cCONDTAF := 'taf->taf_codniv >= mv_par15 .And. taf->taf_codniv <= mv_par16'

	cCONDTPA := 'tpa->tpa_etapa >= mv_par11 .And. tpa->tpa_etapa <= mv_par12'



	dbSelectArea("ST1")
	dbSetOrder(1)
	dbSeek(xFILIAL("ST1")+MV_PAR13,.T.)
	ProcRegua(LastRec())
	While !Eof() .And. xFILIAL("ST1") == ST1->T1_FILIAL .And. ST1->T1_CODFUNC <= MV_PAR14
		Incproc()

		If &(cCONDST1)
			dbSelectArea("STL")
			dbSetOrder(8)
			sCODFUNC := ST1->T1_CODFUNC+Replicate(' ',Len(STL->TL_CODIGO)-Len(ST1->T1_CODFUNC))
			dbSeek(xFILIAL("STL")+"M"+sCODFUNC)
			While !Eof() .And. xFILIAL("STL") == STL->TL_FILIAL .And.;
			STL->TL_TIPOREG == "M" .And. STL->TL_CODIGO == sCODFUNC

				If &(cCONDSTL)
					dbSelectArea("STJ")
					dbSetOrder(01)
					dbSeek(xFILIAL("STJ")+STL->TL_ORDEM+STL->TL_PLANO)

					If &(cCONDSTJ)
						If STJ->TJ_TIPOOS == "B"
							dbSelectArea("ST9")
							dbSetOrder(01)
							dbSeek(xFILIAL("ST9")+STJ->TJ_CODBEM)
						Else
							dbSelectArea("TAF")
							dbSetOrder(08)
							dbSeek(xFILIAL("TAF")+STJ->TJ_CODBEM)
						EndIF


						If (STJ->TJ_TIPOOS == "B" .And. &(cCONDST9)) .Or. (STJ->TJ_TIPOOS != "B" .And. &(cCONDTAF))
							dbSelectArea("TPA")
							dbSetOrder(01)
							dbSeek(xFILIAL("TPA")+STL->TL_ETAPA)


							If &(cCONDTPA)
								MNTR220TRB(STJ->TJ_ORDEM,STJ->TJ_PLANO,STJ->TJ_CODBEM,;
								STJ->TJ_SERVICO,STL->TL_QUANTID,STL->TL_UNIDADE,;
								STL->TL_DTINICI,STL->TL_HOINICI,STL->TL_DTFIM,;
								STL->TL_HOFIM,STL->TL_ETAPA,STL->TL_TIPOHOR,;
								STL->TL_USACALE,STL->TL_CODIGO)
							EndIf
						EndIf
					EndIf
				EndIf
				dbSelectArea("STL")
				dbSkip()
			End

			dbSelectArea("STT")
			dbSetOrder(3)
			sCODFUNC := ST1->T1_CODFUNC+Replicate(' ',Len(STT->TT_CODIGO)-Len(ST1->T1_CODFUNC))
			dbSeek(xFILIAL("STT")+"M"+sCODFUNC)
			While !Eof() .And. xFILIAL("STT") == STT->TT_FILIAL .And.;
			STT->TT_TIPOREG == "M" .And. STT->TT_CODIGO == sCODFUNC

				If &(cCONDSTT)
					dbSelectArea("STS")
					dbSetOrder(01)
					dbSeek(xFILIAL("STS")+STT->TT_ORDEM+STT->TT_PLANO)

					If &(cCONDSTS)
						dbSelectArea("ST9")
						dbSetOrder(01)
						dbSeek(xFILIAL("ST9")+STS->TS_CODBEM)

						If &(cCONDST9)
							dbSelectArea("TPA")
							dbSetOrder(01)
							dbSeek(xFILIAL("TPA")+STT->TT_ETAPA)

							If &(cCONDTPA)
								MNTR220TRB(STS->TS_ORDEM,STS->TS_PLANO,STS->TS_CODBEM,;
								STS->TS_SERVICO,STT->TT_QUANTID,STT->TT_UNIDADE,;
								STT->TT_DTINICI,STT->TT_HOINICI,STT->TT_DTFIM,;
								STT->TT_HOFIM,STT->TT_ETAPA,STT->TT_TIPOHOR,;
								STT->TT_USACALE,STT->TT_CODIGO)
							EndIf
						EndIf
					EndIf
				EndIf
				dbSelectArea("STT")
				dbSkip()
			End

		EndIf
		dbSelectArea("ST1")
		dbSkip()
	End

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR220TRB³ Autor ³ In cio Luiz Kolling   ³ Data ³01/10/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Grava o arquivo temporario                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTR220                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MNTR220TRB(cORDEM,cPLANO,cBEM,cSERV,nQTD,cUNID,dDTINI,;
	cHOINI,dDTFIM,cHOFIM,cETAPA,cTPHOR,cUSACALE,cCODIGO)

	Local vVETHORAS := {}

	lImprime := .T.

	dbSelectArea(cTRB)
	(cTRB)->(DbAppend())
	(cTRB)->CUSTO   := ST1->T1_CCUSTO
	(cTRB)->CODFUNC := ST1->T1_CODFUNC
	(cTRB)->TURNO   := ST1->T1_TURNO
	(cTRB)->ORDEM   := cORDEM
	(cTRB)->PLANO   := cPLANO
	(cTRB)->CODBEM  := cBEM
	(cTRB)->SERVICO := cSERV

	vVETHORAS  := NGTQUATINS(cCODIGO,"M",cUSACALE,nQTD,cTPHOR,dDTINI,cHOINI,dDTFIM,cHOFIM,cUNID)
	(cTRB)->QUANT := vVETHORAS[2]

	(cTRB)->UNIDADE := cUNID
	(cTRB)->DTINICI := dDTINI
	(cTRB)->HOINICI := cHOINI
	(cTRB)->DTFIM   := dDTFIM
	(cTRB)->HOFIM   := cHOFIM
	(cTRB)->TIPOHOR := cTPHOR
	(cTRB)->ETAPA   := cETAPA

Return .T.

//---------------------------------------------------------------------
/*/{Protheus.doc} MNT220VALX1
Valida a sequência dos campos De/Até Código de localização, e chama a função
que Consiste se o codigo da localizacao na estrutura organizacional é válido.

@author Antonio Hardt da Mota
@since 07/03/2013
@version 1.0

/*/
//---------------------------------------------------------------------

Function MNT220VALX1(nPar)
	Local lRet := .T.
	Default nPar := 0

	If nPar == 1 //Caso for a pergunta "De Localização ?"
		If !NGChkEstOr(mv_par15,mv_par16,'L',1)
			lRet = .F.
		EndIf
		If lRet
			If mv_par16 != ' ' .And. mv_par15 > mv_par16
				ShowHelpDlg(STR0034,{STR0035},2,{STR0036},2)//"Atenção"#"Localização inicial maior que localização final."#"Informe uma localização menor."
				lRet := .F.
			EndIf
		EndIf
	ElseIf nPar == 2 //Caso for a pergunta "Até Localização ?"
		If !NGChkEstOr(mv_par15,mv_par16,'L',2)
			lRet = .F.
		EndIf
		If lRet
			If mv_par15 > mv_par16
				ShowHelpDlg(STR0034,{STR0037},2,{STR0038},2)//"Atenção"#"Localização final menor que localização inicial."#"Informe uma localização maior."
				lRet := .F.
			EndIf
		EndIf
	EndIf

Return lRet
