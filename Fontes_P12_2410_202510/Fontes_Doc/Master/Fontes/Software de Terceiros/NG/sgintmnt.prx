#include "Protheus.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGINTMNT  ³ Autor ³ Thiago Olis Machado   ³ Data ³01/07/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa para montagem de telas de retorno de ocorrencia    ³±±
±±³          ³sendo geradas apartir do MNT.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³aProduto     -  Array contendo informacoes do residuo gerado³±±
±±³          ³                    [1] = Codigo do produto utilizado       ³±±
±±³          ³                    [2] = Data da geracao do residuo        ³±±
±±³          ³                    [3] = Hora da geracao do residuo        ³±±
±±³          ³                    [4] = Quantidade utilizada              ³±±
±±³          ³                    [5] = Ordem                             ³±±
±±³          ³                    [6] = Plano                             ³±±
±±³          ³                    [7] = Sequencia                         ³±±
±±³          ³                    [8] = Local do Almoxarifado             ³±±
±±³          ³nOrigem      -  Origem da Geracao         	              ³±±
±±³          ³                    1 = Gestao Ambiental              	  ³±±
±±³          ³                    2 = Manutencao de Ativos          	  ³±±
±±³          ³                    3 = PCP                           	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT,SigaPCP                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SgIntMnt(aResiduo,nOrigem)

	Local i
	Local nResiduo
	Local cDestino := AllTrim(GetNewPar("MV_SGADES"," "))
	Local aOldArea := GetArea()

	Private aRetRes := {}
	Private lMontaTela := .t.

	If !Empty(cDestino)
		dbSelectArea("TB2")
		dbSetOrder(1)
		If dbSeek(xFilial("TB2")+AllTrim(cDestino))
			If TB2->TB2_TIPO <> "1"
				MsgStop("O Destino Padrão de Resíduos não é do tipo Interno. Verificar o Destino definido no parâmetro MV_SGADES.","Atenção!")
				Return .F.
			Else
				If Empty(TB2->TB2_CODALM)
					MsgStop("Código do Almoxarifado não preenchido no Destino Padrão de Resíduos. Verificar o Destino definido no parâmetro MV_SGADES.","Atenção!")
					Return .F.
				EndIf
			EndIf
		EndIf
	EndIf

	For i := 1 To Len( aResiduo )
		nResiduo := 0
		DbSelectArea( 'TC8' )
		DbSetOrder( 01 )
		DbSeek( xFilial( 'TC8' )+aResiduo[i][1] )
		While !Eof() .and. xFilial('TC8') == TC8->TC8_FILIAL .and.;
		TC8->TC8_CODPRO == aResiduo[i][1]

			nResiduo++
			dbSkip()
		End

		If nResiduo > 0 .AND. lMontaTela
			SgIntVaRes( aResiduo,i,nOrigem)
		Endif

	Next i

	RestArea(aOldArea)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGINTUMRES³ Autor ³ Thiago Olis Machado   ³ Data ³01/07/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para testar se o residuo gerado possui quantidade.   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³cProduto     -  Codigo do Produto utilizado no MNT.         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT,SigaPCP                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SgIntUmRes(aResiduo,i,nOrigem)
	Local nQuantidade

	DbSelectArea( 'TC8' )
	DbSetOrder( 01 )
	DbSeek( xFilial( 'TC8' )+aResiduo[i][1])

	If TC8->TC8_QUANT <> 0
		If TC8->TC8_TIPO == '1'
			nQuantidade := aResiduo[i][4]*TC8->TC8_QUANT/100
		Else
			nQuantidade := TC8->TC8_QUANT * aResiduo[i][4]
		EndIf
		aRetRes := SgRetRes(TC8->TC8_CODRES,aResiduo[i][2],aResiduo[i][3],nQuantidade,nOrigem,,,,,aResiduo[i][5],aResiduo[i][6],;
		aResiduo[i][7],aResiduo[i][8],aResiduo[i][9])
		If Len(aRetRes) > 0
			If !aRetRes[1][1]
				MsgInfo(aRetRes[1][2],"Atenção")
			EndIf
		EndIf
	Else
		SgIntUmTel(aResiduo,i,TC8->TC8_CODRES,nOrigem)
	EndIf

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGINTUMTEL³ Autor ³ Thiago Olis Machado   ³ Data ³01/07/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa para montagem de uma tela para reportar a quantida-³±±
±±³          ³de de residuo gerada.                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT,SigaPCP                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SgIntUmTel(aResiduo,i,cResiduo,nOrigem)
	Local oDlg  			 // Objeto para montar a janela de cadastro
	Local cTitulo := "Residuo Gerado" // Titulo da janela
	Local nQuantidade := 0.00
	//Local cDestino := AllTrim(GetNewPar("MV_SGADES"," "))
	Private cDescricao,cDescRes

	SgIntDesc(aResiduo[1][1],cResiduo)

	Define MsDialog oDlg From 03.5,6 To 450,731 Title cTitulo Pixel

	oPnl:=TPanel():New(00,00,,oDlg,,,,,,0,115,.F.,.F.)
	oPnl:Align := CONTROL_ALIGN_ALLCLIENT

	@ 021,005 To 042,370 Label 'Produto Utilizado na O.S '+aResiduo[i][5] Of oPnl Pixel

	/*@ 25,023 Say OemToAnsi("Destino") Size 37,7 Of oDlg Pixel Color CLR_HBLUE
	@ 34,054 MsGet cDestino Size 038,08 Of oDlg Pixel Picture "@!" Valid ExistCpo('TB2',cDestino) */

	@ 37,023 Say OemToAnsi("Produto") Size 37,7 Of oPnl Pixel Color CLR_HBLUE
	@ 36,054 MsGet aResiduo[i][1] Size 058,08 Of oPnl Pixel Picture "@!" When .F.

	@ 37,116 Say OemToAnsi("Descricao") Size 37,7 Of oPnl Pixel
	@ 36,150 MsGet cDescricao Size 188,08 Of oPnl Pixel Picture "@!" When .F.

	@ 061,005 To 127,370 Label 'Residuo Gerado' Of oPnl Pixel

	@ 80,023 Say OemToAnsi("Residuo") Size 37,7 Of oPnl Pixel Color CLR_HBLUE
	@ 79,054 MsGet cResiduo Size 038,08 Of oPnl Pixel Picture "@!" When .F.

	@ 80,112 Say OemToAnsi("Descricao") Size 37,7 Of oPnl Pixel
	@ 79,146 MsGet cDescRes Size 188,08 Of oPnl Pixel Picture "@!" When .F.

	@ 098,023 Say OemToAnsi("Data") Size 37,7 Of oPnl Pixel
	@ 097,054 MsGet aResiduo[i][2] Size 038,08 Of oPnl Pixel Picture "99/99/9999" When .F.

	@ 098,112 Say OemToAnsi("Hora") Size 37,7 Of oPnl Pixel
	@ 097,146 MsGet aResiduo[i][3] Size 038,08 Of oPnl Pixel Picture "99:99" When .F.

	@ 098,269 Say OemToAnsi("Quantidade") Size 37,7 Of oPnl Pixel
	@ 097,300 MsGet nQuantidade Size 038,08 Of oPnl Pixel Picture "@E 999,999.99"

	Activate MsDialog oDlg On Init EnchoiceBar(oDlg,{||nOpca:=1,aRetRes := SgRetRes(cResiduo,aResiduo[i][2],aResiduo[i][3],nQuantidade,nOrigem,,,,,aResiduo[i][5],aResiduo[i][6],aResiduo[i][7],aResiduo[i][8],aResiduo[i][9]),oDlg:End()},{||oDlg:End()}) Center
	If Len(aRetRes) > 0
		If !aRetRes[1][1]
			MsgInfo(aRetRes[1][2],"Atenção")
		EndIf
	EndIf

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGINTDESC ³ Autor ³ Thiago Olis Machado   ³ Data ³04/07/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa para trazer a descricao do produto e do residuo.   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT,SigaPCP                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SgIntDesc(cProduto,cResiduo)
	Local aCod  := {cProduto,cResiduo}
	Local i

	For i:= 1 To 2
		DbSelectArea('SB1')
		DbSetOrder(1)
		DbSeek(xFilial('SB1')+aCod[i])
		If i == 1
			cDescricao := SB1->B1_DESC
		Else
			cDescRes := SB1->B1_DESC
		EndIf
	Next

Return .t.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGINTVARES³ Autor ³ Thiago Olis Machado   ³ Data ³01/07/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa para montagem de um markbrowse para escolha de     |±±
±±³          ³quais residuos ele gerou.                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT,SigaPCP                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SgIntVaRes(aResiduo,i,nOrigem)

	Local oDlg  			 // Objeto para montar a janela de cadastro
	Local cTitulo := "Residuo Gerado" // Titulo da janela
	Local nQuantidade := 0.00
	Local oScr, oPnl
	Local nI
	Local lZero := .F.
	Local cParam := GETNEWPAR("MV_SGAOCEQ", 2)
	//Array para montagem do aHeader
	Local aCampos := { 'TL_ORDEM', 'TL_PLANO', 'B1_COD', 'B1_DESC', 'TL_QUANTID' }
	Local aExc    := { {'B1_COD', 'X3_TITULO', 'Insumo' } }

	Private cDescricao,cDescRes,oMark
	Private aMark := {}, aCols := {}, aHeader := {}
	Private lInverte:= .f.
	Private cMarca  := GetMark()
	Private lQuery  := .t.
	Private aQtdeZero := {}
	Private aVETINR := {}
	Private aDBF1, vIND1, cTRB1
	Private oBtn,oBtn1
	Private oTmpTRBX1
	Private oTmpTRBX2
	Private cTRBX

	DbSelectArea('SB1')
	DbSetOrder(1)
	DbSeek(xFilial('SB1')+aResiduo[i][1])
	cDescricao := SB1->B1_DESC

 	If cParam == "1"//Caso parâmetro seja setado como geração automatica, não traz tela para confirmação
		SgIntAuto(nOrigem,aResiduo,i)
	Else
		If IsInCallStack('MNTA415') .AND. Len(aResiduo) > 1//Verifica se é chamado pelo MNTA415 e o array de Resíduos foi preenchido com mais de um valor
			lMontaTela := .f.
			aDbf := {}
				aAdd(aDbf,{ "TRB_OK"       , "C" ,02, 0 })
				aAdd(aDBF,{ "TRB_ORDEM"    , "C" ,06, 0 })
				aAdd(aDBF,{ "TRB_PLANO"    , "C" ,06, 0 })
				aAdd(aDBF,{ "TRB_CODPRO"   , "C" ,TAMSX3("B1_COD")[1], 0 })
				aAdd(aDBF,{ "TRB_CODRES"   , "C" ,TAMSX3("B1_COD")[1], 0 })
				aAdd(aDBF,{ "TRB_DESCRI"   , "C" ,40, 0 })
				aAdd(aDBF,{ "TRB_QTDEST"   , "N" ,09, 2 })
				aAdd(aDBF,{ "TRB_QUANTI"   , "N" ,09, 2 })
				aAdd(aDBF,{ "TRB_UM"       , "C" ,02, 0 })
				aAdd(aDBF,{ "TRB_DTINIC"   , "D" ,08, 0 })
				aAdd(aDBF,{ "TRB_HOINIC"   , "C" ,05, 0 })
				aAdd(aDBF,{ "TRB_SEQREL"   , "C" ,03, 0 })
				aAdd(aDBF,{ "TRB_FILIAL"   , "C" ,02, 0 })
				aAdd(aDBF,{ "TRB_NUMSEQ"   , "C" ,06, 0 })

			//Variavel recebe GetNextAlias()
			cTRBX := GetNextAlias()
			//Intancia classe FWTemporaryTable
			oTmpTRBX1 := FWTemporaryTable():New( cTRBX, aDBF )
			//Cria indices
			oTmpTRBX1:AddIndex( "Ind01" , {"TRB_ORDEM","TRB_PLANO"} )
			//Cria a tabela temporaria
			oTmpTRBX1:Create()

			aAdd(aMark,{"TRB_OK"       ,NIL," ",})
			aAdd(aMark,{"TRB_CODRES"   ,NIL,"Residuo" ,})
			aAdd(aMark,{"TRB_DESCRI"   ,NIL,"Descricao" ,})
			aAdd(aMark,{"TRB_QTDEST"   ,NIL,"Quant. Est." ,PesqPict("TC8","TC8_QUANT")})
			aAdd(aMark,{"TRB_QUANTI"   ,NIL,"Quant. Real" ,PesqPict("TC8","TC8_QUANT")})
			aAdd(aMark,{"TRB_UM"       ,NIL,"Unidade Medida" ,})

			For nI := 1 To Len(aResiduo)
				dbSelectArea( 'TC8' )
				dbSetOrder( 01 )
				If dbSeek( xFilial( 'TC8' )+aResiduo[nI][1] )
					Aadd(aCols,{aResiduo[nI][5],aResiduo[nI][6],aResiduo[nI][1],NGSEEK("SB1",aResiduo[nI][1],1,"B1_DESC"),aResiduo[nI][4],.f.})
				Endif
			Next

			aHeader := NGHeadExc( aCampos, .T., .F., .F., aExc )

			SgIntMark(aResiduo,i,@lZero)

			SGDESMK()

			dbSelectArea(cTRBX)
			dbGoTop()

			//Caso parâmetro for definido como 3, sempre trará a tela, caso for definido como 2,
			//trará a tela se existir algum valor zerado, caso não, gera automaticamente
			If cParam == "3" .Or. ( cParam == "2" .And. lZero )
				Define MsDialog oDlg From 03.5,6 To 565,710 Title cTitulo Color CLR_HBLUE, CLR_WHITE Pixel

				//Panel criado para correta disposicao da tela
				oPnlPai := TPanel():New( , , , oDlg , , , , , , , , .F. , .F. )
					oPnlPai:Align := CONTROL_ALIGN_ALLCLIENT

					@ 005,005 To 110,346 Label "Insumos utilizados nas O.S." Of oPnlPai Pixel
					oGet := MsNewGetDados():New(017,11,104,340,3,'AllwaysTrue()','AllwaysTrue()',,{},,0,,,,oPnlPai,aHeader,aCols)
					oGet:oBrowse:bChange := { || ChangeGet1() }
					oGet:oBrowse:bGotFocus := { || ChangeGet1()}

					@ 111,005 To 251,346 Label "Residuos Gerados" Of oPnlPai Pixel

					dbselectArea(cTRBX)
					Set Filter To (cTRBX)->TRB_ORDEM == aCols[oGet:nAt][1] .AND. (cTRBX)->TRB_PLANO == aCols[oGet:nAt][2] .AND. (cTRBX)->TRB_CODPRO == aCols[oGet:nAt][3]
					dbGoTop()

					@ 255,307 Button oBtn Prompt "Alt. Quant." Size 40,12 Of oPnlPai Pixel ;
					Action SGRESREAL()

					oMark := MsSelect():New((cTRBX),"TRB_OK",,aMark,@lInverte,@cMarca,{119,11,245,340},,,oPnlPai)
					oMark:oBrowse:lHasMark = .t.
					oMark:oBrowse:lCanAllMark := .t.
					oMark:oBrowse:bChange:={|| SGIMCHBTN() }
					oMark:bMark := { || SgIntValMark(cMarca,aResiduo[i,1],aResiduo[i,4]) }
					oMark:oBrowse:bAllMark	:= {|| SgMarkAll(cMarca,aResiduo[i,1],aResiduo[i,4]) }

					//@ 00,00 ScrollBox oScr Vertical Size 100,100 OF oDlg Border

				Activate MsDialog oDlg On Init EnchoiceBar(oDlg,{||nOpca:=1, If(SgIntGra(nOrigem,aResiduo,i),oDlg:End(),)},{||oDlg:End()}) Center
			Else
				SgIntGra(nOrigem,aResiduo,i,.F.)
			EndIf
		Else
			aDbf := {}
				aAdd(aDbf,{ "TRB_OK"       , "C" ,02, 0 })
				aAdd(aDBF,{ "TRB_CODRES"   , "C" ,TAMSX3("B1_COD")[1], 0 })
				aAdd(aDBF,{ "TRB_DESCRI"   , "C" ,40, 0 })
				aAdd(aDBF,{ "TRB_QTDEST"   , "N" ,09, 2 })
				aAdd(aDBF,{ "TRB_QUANTI"   , "N" ,09, 2 })
				aAdd(aDBF,{ "TRB_UM"       , "C" ,02, 0 })

			//Variavel recebe GetNextAlias()
			cTRBX := GetNextAlias()
			//Intancia classe FWTemporaryTable
			oTmpTRBX2 := FWTemporaryTable():New( cTRBX, aDBF )
			//Cria indices
			oTmpTRBX2:AddIndex( "Ind01" , {"TRB_CODRES","TRB_DESCRI"} )
			//Cria a tabela temporaria
			oTmpTRBX2:Create()

			aAdd(aMark,{"TRB_OK"       ,NIL," ",})
			aAdd(aMark,{"TRB_CODRES"   ,NIL,"Residuo" ,})
			aAdd(aMark,{"TRB_DESCRI"   ,NIL,"Descricao" ,})
			aAdd(aMark,{"TRB_QTDEST"   ,NIL,"Quant. Est." ,PesqPict("TC8","TC8_QUANT")})
			aAdd(aMark,{"TRB_QUANTI"   ,NIL,"Quant. Real" ,PesqPict("TC8","TC8_QUANT")})
			aAdd(aMark,{"TRB_UM"       ,NIL,"Unidade Medida" ,})

			SgIntMark(aResiduo,i,@lZero)

			SGDESMK()

			dbSelectArea(cTRBX)
			dbGoTop()

			//Caso parâmetro for definido como 3, sempre trará a tela, caso for definido como 2,
			//trará a tela se existir algum valor zerado, caso não, gera automaticamente
			If cParam == "3" .Or. ( cParam == "2" .And. lZero )

				Define MsDialog oDlg From 03.5,6 To 545,721 Title cTitulo Pixel

				//Panel que cobre toda a tela
				oPnlScr := TPanel():New(00,00,,oDlg,,,,,,0,115,.F.,.F.)
					oPnlScr:Align := CONTROL_ALIGN_ALLCLIENT

				//Panel de Produto Utilizado na O.S
				oPnlProd := TPanel():New(00,00,,oPnlScr,,,,,,0,50,.F.,.F.)
					oPnlProd:Align := CONTROL_ALIGN_TOP

				//Panel de Residuos Gerados
				oPnlRes := TPanel():New(00,00,,oPnlScr,,,,,,0,172,.F.,.F.)
					oPnlRes:Align := CONTROL_ALIGN_TOP

				oPnlBot := TPanel():New(00,00,,oPnlScr,,,,,,0,100,.F.,.F.)
					oPnlBot:Align := CONTROL_ALIGN_ALLCLIENT

				@ 005,005 To 042,350 Label 'Produto Utilizado na O.S '+aResiduo[i][5] Of oPnlProd Pixel

				@ 023,023 Say OemToAnsi("Produto") Size 37,7 Of oPnlProd Pixel Color CLR_HBLUE
				@ 022,054 MsGet aResiduo[i][1] Size 058,08 Of oPnlProd Pixel Picture "@!" When .F.

				@ 023,116 Say OemToAnsi("Descricao") Size 37,7 Of oPnlProd Pixel
				@ 022,150 MsGet cDescricao Size 188,08 Of oPnlProd Pixel Picture "@!" When .F.

				@ 000,005 To 170,350 Label 'Residuos Gerados' Of oPnlRes Pixel

				@ 005,005 Button oBtn Prompt "Alt. Quant." Size 40,12 Of oPnlBot Pixel ;
				Action SGRESREAL()

				SGDESMK()

				dbSelectArea(cTRBX)
				dbGoTop()

				oMark := MsSelect():New((cTRBX),"TRB_OK",,aMark,@lInverte,@cMarca,{015,11,160,337},,,oPnlRes)
				oMark:oBrowse:lHasMark = .t.
				oMark:oBrowse:lCanAllMark := .t.
				oMark:oBrowse:bChange:={|| SGIMCHBTN() }
				oMark:bMark := { || SgIntValMark(cMarca,aResiduo[i,1],aResiduo[i,4]) }
				oMark:oBrowse:bAllMark	:= {|| SgMarkAll(cMarca,aResiduo[i,1],aResiduo[i,4]) }

				Activate MsDialog oDlg On Init EnchoiceBar(oDlg,{||nOpca:=1, If(SgIntGra(nOrigem,aResiduo,i),oDlg:End(),)},{||oDlg:End()}) Center
			Else
				//Gera automaticamente, caso parâmetro seja 2 e não possua valor zerado
				SgIntGra(nOrigem,aResiduo,i,.F.)
			EndIf
		Endif
		DbSelectArea(cTRBX)
		Use
	EndIf


Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGINTMARK ³ Autor ³ Thiago Olis Machado   ³ Data ³04/07/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Inclusao dos registros de residuo no TRB                    |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT,SigaPCP                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SgIntMark(aResiduo,i,lZero)
	Local nQuantidade := 0
	Local nI
	Local nQtdOco

	If IsInCallStack('MNTA415') .AND. Len(aResiduo) > 1
		For nI := 1 To Len(aResiduo)
			DbSelectArea( 'TC8' )
			DbSetOrder( 01 )
			DbSeek( xFilial( 'TC8' )+aResiduo[nI][1] )
			Do While !Eof() .and. xFilial('TC8') == TC8->TC8_FILIAL .and.;
			TC8->TC8_CODPRO == aResiduo[nI][1]

				(cTRBX)->(DbAppend())
				(cTRBX)->TRB_ORDEM   := aResiduo[nI][5]
				(cTRBX)->TRB_PLANO   := aResiduo[nI][6]
				(cTRBX)->TRB_CODPRO  := TC8->TC8_CODPRO
				(cTRBX)->TRB_CODRES  := TC8->TC8_CODRES
				(cTRBX)->TRB_DTINIC  := aResiduo[nI][2]
				(cTRBX)->TRB_HOINIC  := aResiduo[nI][3]
				(cTRBX)->TRB_SEQREL  := aResiduo[nI][7]
				(cTRBX)->TRB_FILIAL  := aResiduo[nI][8]
				(cTRBX)->TRB_NUMSEQ  := aResiduo[nI][9]

				If TC8->TC8_TIPO == '1'
					nQuantidade := aCols[nI][5]*TC8->TC8_QUANT/100
				Else
					nQuantidade := TC8->TC8_QUANT * aCols[nI][5]
				EndIf
				If fBuscaOco(@nQtdOco,aResiduo,i)
					(cTRBX)->TRB_OK 		:= cMarca
					(cTRBX)->TRB_QUANTI	:= nQtdOco
				Else
					nQtdOco			:= 0
					(cTRBX)->TRB_OK      	:= ''
					(cTRBX)->TRB_QUANTI	:= nQuantidade
				EndIf

				//Caso tenha um valor zerado, indicativo deve ficar como verdadeiro
				If nQuantidade == 0 .And. nQtdOco == 0
					lZero := .T.
				EndIf

				(cTRBX)->TRB_QTDEST  := nQuantidade


				DbSelectArea('SB1')
				DbSetOrder(1)
				If DbSeek(xFilial('SB1')+TC8->TC8_CODRES)
					(cTRBX)->TRB_DESCRI  := SB1->B1_DESC
					(cTRBX)->TRB_UM      := SB1->B1_UM
				EndIf

				If nQuantidade >= 0 .Or. nQtdOco >= 0
					aAdd(aQtdeZero,{TC8->TC8_CODRES})
				Endif

				DbSelectArea('TC8')
				DbSkip()
			EndDo
		Next
	Else
		DbSelectArea( 'TC8' )
		DbSetOrder( 01 )
		DbSeek( xFilial( 'TC8' )+aResiduo[i][1] )
		Do While !Eof() .and. xFilial('TC8') == TC8->TC8_FILIAL .and.;
		TC8->TC8_CODPRO == aResiduo[i][1]

			(cTRBX)->(DbAppend())
			(cTRBX)->TRB_CODRES  := TC8->TC8_CODRES

			If TC8->TC8_TIPO == '1'
				nQuantidade := aResiduo[i][4]*TC8->TC8_QUANT/100
			Else
				nQuantidade := TC8->TC8_QUANT * aResiduo[i][4]
			EndIf

			If fBuscaOco(@nQtdOco,aResiduo,i)
				(cTRBX)->TRB_OK 		:= cMarca
				(cTRBX)->TRB_QUANTI	:= nQtdOco
			Else
				nQtdOco			:= 0
				(cTRBX)->TRB_OK      	:= ''
				(cTRBX)->TRB_QUANTI	:= nQuantidade
			EndIf

			//Caso tenha um valor zerado, indicativo deve ficar como verdadeiro
			If nQuantidade == 0 .And. nQtdOco == 0
				lZero := .T.
			EndIf

			(cTRBX)->TRB_QTDEST  := nQuantidade

			DbSelectArea('SB1')
			DbSetOrder(1)
			If DbSeek(xFilial('SB1')+TC8->TC8_CODRES)
				(cTRBX)->TRB_DESCRI  := SB1->B1_DESC
				(cTRBX)->TRB_UM      := SB1->B1_UM
			EndIf

			If nQuantidade >= 0 .Or. nQtdOco >= 0
				aAdd(aQtdeZero,{TC8->TC8_CODRES})
			Endif

			DbSelectArea('TC8')
			DbSkip()
		EndDo
	Endif

	(cTRBX)->(DbGotop())

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGINTVALMARK³ Autor ³ Thiago Olis Machado   ³ Data ³04/07/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida se possui quantidade gerada.                           |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT,SigaPCP                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SgIntValMark(cMarca,cProdut,nQtd)
	Local cFieldMarca := "TRB_OK"

	If IsMark(cFieldMarca,cMarca,lInverte)
		If (cTRBX)->TRB_QTDEST == 0
			SgIntTeMark()
		EndIf
	Else
		If aSCAN(aQtdeZero,{|x| x[1] == (cTRBX)->TRB_CODRES }) > 0
			RecLock((cTRBX),.f.)
			(cTRBX)->TRB_QUANTI := SGQTDEST(cProdut,(cTRBX)->TRB_CODRES,nQtd)
			(cTRBX)->(MsUnLock())
		Endif
	EndIf

	SGIMCHBTN()

	oMark:oBrowse:Refresh()

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGINTTEMARK ³ Autor ³ Thiago Olis Machado   ³ Data ³04/07/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Monta tela para informacao da quantidade                      |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT,SigaPCP                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SgIntTeMark()
	Local oDlgA
	Local nQuantidade := 0.00
	Local nOpcB := 0

	Define MsDialog oDlgA From 03.5,6 To 150,353 Title "Quantidade Gerada" Pixel

	@ 010,005 To 052,170 Label 'Informe a quantidade gerada do residuo '+SubStr( (cTRBX)->TRB_DESCRI,1,18 ) Of oDlgA Pixel

	@ 28,023 Say OemToAnsi("Quantidade") Size 37,7 Of oDlgA Pixel Color CLR_HBLUE
	@ 27,054 MsGet nQuantidade Size 050,08 Of oDlgA Pixel Picture "@E 999,999.99" Hasbutton

	Activate MsDialog oDlgA On Init EnchoiceBar(oDlgA,{||nOpcB:=1,oDlgA:End()},{||nOpcB:=0,oDlgA:End()}) Center

	If nOpcB == 1
		RecLock((cTRBX),.f.)
		(cTRBX)->TRB_QUANTI := nQuantidade
		(cTRBX)->(MsUnLock())
	Else
		RecLock((cTRBX),.f.)
		(cTRBX)->TRB_OK := ''
		(cTRBX)->(MsUnLock())
	EndIf

Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SGINTGRA    ³ Autor ³ Thiago Olis Machado   ³ Data ³04/07/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Gera os residuos escolhidos                                   |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaMNT,SigaPCP                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SgIntGra(nOrigem,aResiduo,i,lVerOk)

	Default lVerOk := .T.

	DbSelectArea(cTRBX)
	DbClearFilter()
	DbGoTop()
	Do While !Eof()

		If !lVerOk .Or. !Empty( (cTRBX)->TRB_OK )
			If IsInCallStack('MNTA415') .AND. Len(aResiduo) > 1
				aRetRes := SgRetRes((cTRBX)->TRB_CODRES,(cTRBX)->TRB_DTINIC,(cTRBX)->TRB_HOINIC,(cTRBX)->TRB_QUANTI,nOrigem,,,,,(cTRBX)->TRB_ORDEM,;
				(cTRBX)->TRB_PLANO,(cTRBX)->TRB_SEQREL,(cTRBX)->TRB_FILIAL,(cTRBX)->TRB_NUMSEQ)
			Else
				aRetRes := SgRetRes((cTRBX)->TRB_CODRES,aResiduo[i][2],aResiduo[i][3],(cTRBX)->TRB_QUANTI,nOrigem,,,,,aResiduo[i][5],;
				aResiduo[i][6],aResiduo[i][7],aResiduo[i][8],aResiduo[i][9])
			Endif
			If Len(aRetRes) > 0
				If !aRetRes[1][1]
					MsgInfo(aRetRes[1][2],"Atenção")
					Return .f.
				EndIf
			EndIf
		EndIf
		DbSkip()
	EndDo

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SgMarkAll ³ Autor ³ Marcos Wagner Junior  ³ Data ³18/03/2010³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para Inverter a marcacao do browse.                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tabelas   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaSGA                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SgMarkAll(cMarca,cProdut,nQtd)
	Local aArea := GetArea()

	dbSelectArea(cTRBX)
	dbGoTop()
	While !Eof()
		RecLock((cTRBX),.f.)
		(cTRBX)->TRB_OK := IF(TRB_OK == "  ",cMarca,"  ")
		If Empty(TRB_OK)
			If aSCAN(aQtdeZero,{|x| x[1] == (cTRBX)->TRB_CODRES }) > 0
				(cTRBX)->TRB_QUANTI := SGQTDEST(cProdut,(cTRBX)->TRB_CODRES,nQtd)
			Endif
		Else

			If (cTRBX)->TRB_QTDEST == 0
				SgIntTeMark()
			EndIf
		Endif
		(cTRBX)->(MsUnLock())
		dbSkip()
	End

	oMark:oBrowse:Refresh()

	RestArea(aArea)
Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |ChangeGet1³ Autor ³ Marcos Wagner Junior  ³ Data ³ 15/06/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Foco na linha da GetDados							   			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SGINTMNT                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ChangeGet1()

	dbselectArea(cTRBX)
	Set Filter To (cTRBX)->TRB_ORDEM == aCols[oGet:nAt][1] .AND. (cTRBX)->TRB_PLANO == aCols[oGet:nAt][2] .AND. (cTRBX)->TRB_CODPRO == aCols[oGet:nAt][3]
	dbGoTop()

	SGIMCHBTN() //Chama a função para a atualização do botão "Alt. Quant."

	oMark:oBrowse:Refresh()

Return .t.

//---------------------------------------------------------------------------------------------------
/*Static Function SGQTDEST()
Função para voltar a quantidade estimada do resíduo.

@return

@sample
SGQTDEST()

@author Juliani Schlickmann Damasceno
@since 06/01/2014
@version 1.0
*/
//---------------------------------------------------------------------------------------------------
Static Function SGQTDEST(cProdut,cCodRes,nQtd)
	Local nQuantidade
	Local aArea := GetArea()

	DbSelectArea( 'TC8' )
	DbSetOrder( 01 )
	DbSeek( xFilial( 'TC8' )+cProdut+cCodRes)

	If TC8->TC8_QUANT >= 0

		If TC8->TC8_TIPO == '1'
			nQuantidade := nQtd * TC8->TC8_QUANT/100
		Else
			nQuantidade := TC8->TC8_QUANT * nQtd
		EndIf

	EndIf

	RestArea(aArea)

Return nQuantidade

//---------------------------------------------------------------------------------------------------
/*Static Function fBuscaOco()
Função para verificar se há ocorrência em determinada hora e data.
Para não gerar a mesma ocorrência mais vezes.

@return

@sample
fBuscaOco()

@author Juliani Schlickmann Damasceno
@since 06/01/2014
@version 1.0
*/
//---------------------------------------------------------------------------------------------------
Static Function fBuscaOco(nQtdOco,aResiduo,i,cResiduo)

	Local lRet := .F.
	Local cHrOco
	Local dDtOco
	Local aArea := GetArea()
	Default cResiduo := ""

	If ValType( aResiduo ) == "A"
		dDtOco := aResiduo[ i , 2 ]
		cHrOco := aResiduo[ i , 3 ]
	Else
		dDtOco := (cTRBX)->TRB_DTINIC
		cHrOco := (cTRBX)->TRB_HOINIC
	EndIf

	If Empty( cResiduo )
		cResiduo := (cTRBX)->TRB_CODRES
	EndIf

	DbSelectArea('TB0')
	DbSetOrder(2)
	If DbSeek (xFilial('TB0')+cResiduo)
		While TB0->(!EOF()) .And. TB0->TB0_FILIAL == xFilial('TB0') .And. TB0->TB0_CODRES == cResiduo
			If TB0->TB0_DATA == dDtOco .And. TB0->TB0_HORA == cHrOco
				nQtdOco := TB0->TB0_QTDE
				lRet := .T.
				Exit
			EndIf
			TB0->(DbSkip())
		End
	EndIf


	RestArea(aArea)
Return lRet

//---------------------------------------------------------------------------------------------------
/*Function SGRESREAL()
Função para a criação da tela de Alteração da Quantidade de Resíduo.

@return

@sample
SGRESREAL()

@author Juliani Schlickmann Damasceno
@since 07/01/2014
@version 1.0
*/
//---------------------------------------------------------------------------------------------------
Static Function SGRESREAL()
	Local oDlgA
	Local nOpcB := 0
	Local oPnl
	Local nQuantReal := 0.00
	Local nQuant := 0.00
	Local oTButton1,oTButton2

	nQuant := (cTRBX)->TRB_QTDEST
	nQuantReal := (cTRBX)->TRB_QUANTI

	Define MsDialog oDlgA From 03.5,6 To 130,308 Title "Alteração de Quantidade de Resíduo" Pixel

	oPnl:=TPanel():New(00,00,,oDlgA,,,,,,0,115,.F.,.F.)
	oPnl:Align := CONTROL_ALIGN_ALLCLIENT

	@ 010,010 Say OemToAnsi("Quant. Est.") Size 37,7 Of oPnl Pixel Color CLR_HBLUE
	@ 010,041 MsGet nQuant Size 050,08 Of oPnl Pixel Picture "@E 999,999.99" When .F. Hasbutton
	@ 023,010 Say OemToAnsi("Quant. Real") Size 37,7 Of oPnl Pixel Color CLR_HBLUE
	@ 023,041 MsGet nQuantReal Size 050,08 Of oPnl Pixel Picture "@E 999,999.99" Hasbutton

	oTButton1 := tButton():New(045,050,"Confirmar",oPnl,{||nOpcB:=1,oDlgA:End()},40,11,,,,.t.)
	oTButton2 := tButton():New(045,100,"Cancelar",oPnl,{||nOpcB:=0,oDlgA:End()},40,11,,,,.t.)

	Activate MsDialog oDlgA Center


	If nOpcB == 1
		RecLock((cTRBX),.f.)
		(cTRBX)->TRB_QUANTI := nQuantReal
		(cTRBX)->(MsUnLock())
	EndIf


Return .t.

//---------------------------------------------------------------------------------------------------
/*Static Function SGIMCHBTN()
Função para desabilitação do botão Alt. Quant.

@return

@sample
SGIMCHBTN()

@author Juliani Schlickmann Damasceno
@since 09/01/2014
@version 1.0
*/
//---------------------------------------------------------------------------------------------------
Static Function SGIMCHBTN()
	Local lRet := .T.
	Local cParam := GETNEWPAR("MV_SGAOCEQ", 2)

	If Empty((cTRBX)->TRB_OK) .Or. (cParam <> "3" .And. ((cTRBX)->TRB_QTDEST > 0))
		lRet := .F.
		oBtn:Disable()
	Else
		oBtn:Enable()
	EndIf

	oBtn:Refresh()

Return  lRet

//---------------------------------------------------------------------------------------------------
/*Function SgIntAuto()
Função para a geração automática da ocorrência.
@return

@sample
SgIntAuto()

@author Juliani Schlickmann Damasceno
@since 09/01/2014
@version 1.0
*/
//---------------------------------------------------------------------------------------------------
Static Function SgIntAuto(nOrigem,aResiduo,i)

	Local nQuantidade := 0
	Local nI
	Local nQtdOco

	If IsInCallStack('MNTA415').AND. Len(aResiduo) >= 1
		For nI := 1 To Len(aResiduo)
			DbSelectArea( 'TC8' )
			DbSetOrder( 01 )
			DbSeek( xFilial( 'TC8' )+aResiduo[nI][1] )
			Do While !Eof() .and. xFilial('TC8') == TC8->TC8_FILIAL .and.;
			TC8->TC8_CODPRO == aResiduo[nI][1]

				If !fBuscaOco(@nQuantidade,aResiduo,nI,TC8->TC8_CODRES)
					If TC8->TC8_TIPO == '1'
						nQuantidade := aCols[nI][5]*TC8->TC8_QUANT/100
					Else
						nQuantidade := TC8->TC8_QUANT * aResiduo[nI][4]
					EndIf
				EndIf

				aRetRes := SgRetRes(TC8->TC8_CODRES,aResiduo[nI][2],aResiduo[nI][3],nQuantidade,nOrigem,,,,,aResiduo[nI][5],;
				aResiduo[nI][6],aResiduo[nI][7],aResiduo[nI][8],aResiduo[nI][9])



				DbSelectArea('TC8')
				DbSkip()
			EndDo
		Next
	Else
		DbSelectArea( 'TC8' )
		DbSetOrder( 01 )
		DbSeek( xFilial( 'TC8' )+aResiduo[i][1] )
		Do While !Eof() .and. xFilial('TC8') == TC8->TC8_FILIAL .and.;
		TC8->TC8_CODPRO == aResiduo[i][1]

			If !fBuscaOco(@nQuantidade,aResiduo,i,TC8->TC8_CODRES)
				If TC8->TC8_TIPO == '1'
					nQuantidade := aResiduo[i][4]*TC8->TC8_QUANT/100
				Else
					nQuantidade := TC8->TC8_QUANT * aResiduo[i][4]
				EndIf
			EndIf


			aRetRes := SgRetRes(TC8->TC8_CODRES,aResiduo[i][2],aResiduo[i][3],nQuantidade,nOrigem,,,,,aResiduo[i][5],;
			aResiduo[i][6],aResiduo[i][7],aResiduo[i][8],aResiduo[i][9])


			DbSelectArea('TC8')
			DbSkip()
		EndDo
	Endif

Return
//---------------------------------------------------------------------------------------------------
/*/{Protheus.doc} SGDESMK()
Função para marcar os resíduos que possuem quantidade estivada maior que zero.
@return

@sample
SGDESMK()

@author Juliani Schlickmann Damasceno
@since 10/01/2014
@version 1.0
/*/
//---------------------------------------------------------------------------------------------------
Static Function SGDESMK()

	Local cParam := GETNEWPAR("MV_SGAOCEQ", 2)

	dbSelectArea(cTRBX)
	dbGoTop()
	While !Eof()
		If cParam <> "3" .And. ((cTRBX)->TRB_QTDEST > 0)
			RecLock((cTRBX), .F.)
			(cTRBX)->TRB_OK := cMarca
			(cTRBX)->(MsUnLock())
		EndIf
		(cTRBX)->(dbSkip())
	EndDo

	dbSelectArea(cTRBX)
	dbGoTop()

Return