#INCLUDE "MNTA280.ch"
#Include "Protheus.ch"
#INCLUDE 'FWMVCDEF.CH'

//Variaveis da Ordem do Array de Opcoes
#DEFINE __POS_OPCSUP__ 01
#DEFINE __POS_OPCPER__ 02
#DEFINE __POS_OPCTIP__ 03
#DEFINE __POS_OPCPAN__ 04
#DEFINE __TAM_ARROPC__ 04

//Variaveis da Ordem do Array de Perguntas
#DEFINE __POS_NIVSUP__ 01
#DEFINE __POS_CODNIV__ 02
#DEFINE __POS_DESCRI__ 03
#DEFINE __POS_PERGUN__ 04
#DEFINE __POS_TIPO__ 05
#DEFINE __POS_PERGOP__ 06
#DEFINE __POS_MARCAD__ 07
#DEFINE __POS_IMAGEM__ 08
#DEFINE __POS_OBJTXT__ 09
#DEFINE __POS_PAIANT__ 10
#DEFINE __TAM_ARRAY__ 10

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MNTA280  ³ Autor ³ Inacio Luiz Kolling   ³ Data ³24/11/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de inclusao de solicitacao de servico             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 03/01/12 ³ Devido a alteração responsavel por possibilitar que o      ³±±
±±³          ³ MNTA280 aja como rotina automatica, as mensagens de        ³±±
±±³          ³ validações relacionadas ao cadastro, devem sempre ser      ³±±
±±³          ³ através da função Help, para que nao haja conflitos quando ³±±
±±³          ³ houver uma chamada através do MsExecAuto.                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA280(cTipoSS, cCodBem, xRotAuto, nOpcAuto)

	Local aNGBEGINPRM := {}
	Local lNovoBrw    := .F.
	Local l280Auto    := .F.
	Local cCondTmp    := ''
	Local nPosAuto    := 0

	If !FindFunction( 'MNTAmIIn' ) .Or. MNTAmIIn( 19, 95 )

		aNGBEGINPRM := NGBEGINPRM()
		lNovoBrw    := If(TcSrvType() == "AS/400" .OR. TcSrvType() == "iSeries" .OR. Empty(TcSrvType()) .OR. (ExistBlock("MNTA2801") .AND. !ExistBlock("MNTA280D")),.F.,.T.)
		l280Auto    := ValType(xRotAuto) <> "U"

		Private aRotina   := MenuDef()
		Private cCadastro := Oemtoansi(STR0006) //"Solicitacao de Servico"

		//Variaveis para preenchimento automatico dos campos TQB_CODBEM e TQB_TIPOSS
		Private cTipoSS280 := cTipoSS
		Private cCodBem280 := cCodBem

		// Variáveis utilizada quando execução do fonte é feita através de MsExecAuto
		Default nOpcAuto := 3

		If ExistBlock("MNTA2805")
			Private aVarsPE := {} //Variaveis para PE nas Cores / Legenda
		EndIf

		If l280Auto .And. ( nPosAuto := aScan( xRotAuto, { |x| Upper( AllTrim( x[1] ) ) == 'TQB_CODBEM' } ) ) > 0

			cCodBem280 := xRotAuto[nPosAuto,2]
			cTipoSS280 := 'B'

		EndIf

		lPesqSat := SuperGetMv("MV_NGPSATI",.F.,"N") == "S"
		//PESQUISA DE SATISFACAO
		If !l280Auto .And. !NG280SAT() .And. lPesqSat
			MsgInfo(STR0019+"!", STR0020+"!")//"Antes de acessar a tela de Solicitação de Serviços, você deverá responder a Pesquisa de Satisfação"###"AVISO
			//Comentada a função pois há problema no menudef ao acessar a rotina MNTA305 que é em MVC
			//MNTA305()
			Return .F.
		EndIf

		If !l280Auto .And. !NG280SAT() .And. lPesqSat
			// Devolve variaveis armazenadas (NGRIGHTCLICK)
			NGRETURNPRM(aNGBEGINPRM)
			Return .T.
		EndIf
		If MNTUPDFAC(.F.)
			If !MNT280VBA()
				Return .F.
			EndIf
		EndIf

		//-----------------------------------------------
		// Realiza Filtragem do Browse
		//-----------------------------------------------

		//Funcao para corrigir a base quando campo TQB_SOLUCA estiver com o conteudo igual a "N"
		MNT280AJU()

		dbSelectArea("TQB")
		dbSetOrder(12)
		cCond280 := 'TQB->TQB_FILIAL = "'+ xFilial("TQB")+'"'+'.And. '
		cCond280 += '(TQB->TQB_SOLUCA = "A" .Or. TQB->TQB_SOLUCA = "D")'
		If ValType(cTipoSS) == "C" .And. ValType(cCodBem) == "C"
			cCond280 += " .And. TQB->TQB_CODBEM = '"+cCodBem+"' .And. TQB->TQB_TIPOSS = '"+cTipoSS+"'"

			//-----------------------------------------------
			// Não apresentar a tela para informar a filial
			//-----------------------------------------------
			SetBrwCHGAll(.F.)
		EndIf

		If ExistBlock("MNTA2801")
			cCond280 += ExecBlock("MNTA2801",.F.,.F.)
		EndIf

		If !lNovoBrw
			cCond280 := 'TQB->TQB_FILIAL = "'+ xFilial("TQB")+'"'+'.And. '
			cCond280 += '(TQB->TQB_SOLUCA = "A" .Or. TQB->TQB_SOLUCA = "D")'

			If ValType(cTipoSS) == "C" .And. ValType(cCodBem) == "C"
				cCond280 += " .And. TQB->TQB_CODBEM = '"+cCodBem+"' .And. TQB->TQB_TIPOSS = '"+cTipoSS+"'"

				//-----------------------------------------------
				// Não apresentar a tela para informar a filial
				//-----------------------------------------------
				SetBrwCHGAll(.F.)
			EndIf

			If ExistBlock("MNTA2801")
				cCondTmp	:= ExecBlock("MNTA2801",.F.,.F.)
				If ValType( cCondTmp ) == "C"
					cCond280 += cCondTmp
				EndIF
			EndIf

			dbSelectArea("TQB")
			Set Filter To &(cCond280)

		Else
			cCond280 := "TQB_FILIAL = '" + xFilial("TQB") + "' AND "
			cCond280 += "(TQB_SOLUCA = 'A' OR TQB_SOLUCA = 'D')"

			If ValType(cTipoSS) == "C" .And. ValType(cCodBem) == "C"
				cCond280 += "AND TQB_CODBEM = '" + cCodBem + "' AND TQB_TIPOSS = '" + cTipoSS + "'"
			EndIf
			If ExistBlock("MNTA280D")
				cCondTmp	:= ExecBlock("MNTA280D",.F.,.F.)
				If ValType( cCondTmp ) == "C"
					cCond280 += cCondTmp
				EndIF
			EndIf
		EndIf

		If l280Auto // Caso a a chamada seja para Rotina Automatica

			Private aRotAuto := xRotAuto
			MBrowseAuto(nOpcAuto,aRotAuto,"TQB")

		ElseIf ExistBlock("MNTA280C")
			ExecBlock("MNTA280C",.F.,.F.)
		Else
			MBrowse( 6,1,22,75,"TQB",,,,,,MNTA280COR(),,,,,,,,If(lNovoBrw, cCond280, Nil) )
		EndIf

		dbSelectArea("TQB")
		Set Filter To
		dbSetOrder(1)
		If !l280Auto
			dbSeek(xFilial("TQB"))
		EndIf

		// Devolve variaveis armazenadas (NGRIGHTCLICK)
		NGRETURNPRM(aNGBEGINPRM)

	EndIf

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} ³MNTA280IN
Inclusao da solicitacao de servico

@author  Inacio Luiz Kolling
@since   24/11/2003
@version P12
@param nOpcx	, numerico, Opção de operação 1
@param nOperacao, numerico, Opção de operação 2
@param cTitulo	, caracter, Titulo da Solicitação de Serviço
@param nChamada  , numerico, Indica qual rotina chamou a função
				2 - MNTA291
				3 - MNTA400
				4 - MNTA902
/*/
//-------------------------------------------------------------------
Function MNTA280IN( nOpcx, nOperacao, cTitulo, nChamada )

	Local aNGBEGINPRM := NGBEGINPRM(,"MNTA280",,.F.) // Guarda conteudo e declara variaveis padroes
	Local cTexto      := "", cOrdemTx := ""

	//Variaveis de Tela
	Local oPanel, oMenu, oGet280Fol, oFolder280, oGet280Ate
	Local lOk := .F.
	Local aArrGet
	Local lFacilit := MNTINTFAC()

	//Variaveis de tamanho de tela e objetos
	Local aSize := {}, aObjects := {}, aInfo := {}, aPosObj := {}

	Local cCodSS   := ""
	Local cFiltro  := AllTrim(TQB->(dbFilter()))
	Local aArea    := TQB->(GetArea())
	Local nInd     := 0
	Local nTamTAF  := FWTamSX3( 'TAF_CODNIV' )[1]
	Local nI       := 1

	// Release menor ou igual a 17
	Local lRPORel17 := GetRPORelease() <= '12.1.017'

	Local lValidCls := .T.

	// Classe de S.S.
	Private oTQB

	Default nOperacao := 1
	Default cTitulo   := OemToAnsi( STR0006 ) //Solicitacao de Serviço
	Default nChamada  := 1

	Private l280Auto := Type("aRotAuto") == "A" .And. IsInCallStack("MNTA280") // Variável definida para utilização da rotina automatica
	Private oEnchoice//Variavel deve ser private devido ao F11 e F3
	Private aGets := {}, aTela := {}

	If !lRPORel17

		oTQB := MntSR():New()

		// Determina a operação de Inclusão.
		oTQB:setOperation(nOpcx)

		// Não apresenta mensagens condicionais
		oTQB:setAsk(.F.)

		If nOpcx == 3
			// Preenchimento dos campos da classe de negócio
			oTQB:setValue("TQB_FILIAL" , xFilial("TQB"))

			// Retira campo, que não está em tela, das validações
			oTQB:RemoveField('TQB_PRIORI')

			If !Empty( cCodBem280 )

				oTQB:setValue( 'TQB_CODBEM', cCodBem280 )
				oTQB:setValue( 'TQB_TIPOSS', cTipoSS280 )

				If cTipoSS280 == 'B'

					TIPOACOM  := oTQB:HasCounter( 1, cCodBem280 ) // Carrega variável que define se bem tem contador ou não
					TIPOACOM2 := oTQB:HasCounter( 2, cCodBem280 ) // Carrega variável que define se bem tem segundo contador

					oTQB:setValue( 'TQB_CCUSTO', Posicione( 'ST9', 1, xFilial( 'ST9' ) + cCodBem280, 'T9_CCUSTO' ) )
					oTQB:setValue( 'TQB_CENTRA', Posicione( 'ST9', 1, xFilial( 'ST9' ) + cCodBem280, 'T9_CENTRAB' ) )

				Else

					oTQB:setValue( 'TQB_CCUSTO', Posicione( 'TAF', 7, FWxFilial( 'TAF' ) + 'X2' + SubStr( cCodBem280, 1, nTamTAF ), 'TAF_CCUSTO' ) )
					oTQB:setValue( 'TQB_CENTRA', Posicione( 'TAF', 7, FWxFilial( 'TAF' ) + 'X2' + SubStr( cCodBem280, 1, nTamTAF ), 'TAF_CENTRA' ) )

				EndIf

			EndIf
			
			// Transfere os valores do Objeto para a memoria
			oTQB:ClassToMemory()

		ElseIf nOpcx == 4 .Or. nOpcx == 5

			If nOpcx == 4

				// Os métodos que buscam e gravam os campos no objeto não trabalham com campos Memo
				// trecho para que campos Memo de usuário tenha seu valor carregado na alteração
				While nI <= Len( oTQB:oStruct:aStruct ) 
					
					// Condicionais - (Tipo == Memo) (Valor == Nulo) (Visual == Virtual)
					If oTQB:oStruct:aStruct[nI, 3] == 'M' .And. oTQB:oStruct:aMemory[nI, 3] == Nil .And.;
					oTQB:oStruct:aStruct[nI, 9] != 'V'

						oTQB:setValue( oTQB:oStruct:aStruct[nI, 2], &( oTQB:oStruct:aStruct[nI, 1] + '->' + oTQB:oStruct:aStruct[nI, 2] ))

					EndIf

					nI ++

				End

			EndIf

			If nOperacao == 1

				// Retira campo, que não está em tela, das validações
				oTQB:RemoveField( 'TQB_PRIORI' )

			EndIf

			oTQB:Load( { xFilial("TQB") + TQB->TQB_SOLICI } )
			
			// Transfere os valores do Objeto para a memoria
			oTQB:ClassToMemory()

		EndIf

	EndIf

	If !l280Auto
		//Definicao de tamanho de tela e objetos
		aSize := MsAdvSize(,.F.,430)
		Aadd(aObjects,{060,060,.T.,.T.})
		Aadd(aObjects,{040,040,.T.,.T.})
		aInfo := {aSize[1],aSize[2],aSize[3],aSize[4],0,0}
		aPosObj := MsObjSize(aInfo, aObjects,.T.)
	Else
		//Tratamento para execução da Rotina Automatica
		MN280AUTO(nOpcx)
		NGRETURNPRM(aNGBEGINPRM)
		If !lRPORel17
			// Elimina o objeto
			oTQB:Free()
		EndIf

		Return
	EndIf
	cCadastro := cTitulo

	If nOpcx != 2
		If nOperacao == 1//Inclusao
			If nOpcx == 4 .Or. nOpcx == 5
				If nOpcx == 5 //Se a opção for de Exclusão.
					/*/ Ponto de entrada chamado no momento de exclusão de uma S.S., no qual verifica se o usuário
					é o mesmo que realizou a abertura da SS ou se faz parte do grupo de administradores./*/
					If ExistBlock( "MNTA280E" )
						If !ExecBlock( "MNTA280E", .F., .F. ) //Se o Retorno do PE for falso.
							Return .F. //Finaliza a esclusão de registros.
						EndIf
					Else
						If AllTrim(TQB->TQB_CDSOLI) != AllTrim(__cUserID) .And. !FwIsAdmin() //Verifica se o usuario e mesmo que abriu a SS ou se e administrador
							Help(" ",1,STR0020,,STR0024,4,5) // "Aviso" ## "Deleção permitida apenas para o Solicitante da S.S. ou um usuário do grupo de Administradores!"
							Return .F.
						EndIf
					EndIf
					If TQB->TQB_SOLUCA != "A"
						Help(" ",1,STR0020,,STR0025,4,5) // "Aviso" ## "Operação não permitida! A S.S. já foi distribuída!"
						Return .F.
					EndIf
				EndIf
				If !Empty(tqb->tqb_ordem)
					Help(" ",1,STR0009,,STR0006+STR0015+If(nOpcx == 4,STR0017,STR0018)+","+STR0016,4,5)
					Return .F.
				ElseIf AllTrim(GetNewPar("MV_NGMULOS","N")) == "S"
					dbSelectArea("TT7")
					dbSetOrder(1)
					If dbSeek(xFilial("TT7")+TQB->TQB_SOLICI)
						Help(" ",1,STR0009,,STR0006+STR0015+If(nOpcx == 4,STR0017,STR0018)+","+STR0016,4,5)
						Return .F.
					EndIf
				EndIf
			EndIf
		ElseIf nOperacao == 2//Distribuicao
			If nOpcx == 3 .Or. nOpcx == 4
				If lFacilit .And. TQB->TQB_SOLUCA == "D"
					MsgInfo(STR0099) //"Solicitação de Serviço já distribuída."
					Return .F.
				EndIf
			EndIf
			If AllTrim(GetNewPar("MV_NGMULOS","N")) <> "S"
				If !Empty(tqb->tqb_ordem)
					MsgInfo(STR0046,STR0047) //"Distribuição sem acesso, a ordem de serviço já foi gerada." ### "NAO CONFORMIDADE"
					Return .F.
				EndIf
			Else
				dbSelectArea("TT7")
				dbSetOrder(1)
				If dbSeek(xFilial("TT7")+TQB->TQB_SOLICI)
					MsgInfo(STR0046,STR0047) //"Distribuição sem acesso, a ordem de serviço já foi gerada." ### "NAO CONFORMIDADE"
					Return .F.
				EndIf
			EndIf
		ElseIf nOperacao == 3 .Or. nOperacao == 5 // Fechamento ou Cancelamento
			If nOpcx == 3 .Or. nOpcx == 4
				If TQB->TQB_SOLUCA == "E"
					MsgInfo(STR0099)
					Return .F.
				ElseIf TQB->TQB_SOLUCA == "C"
					MsgInfo(STR0099)
					Return .F.
				EndIf
				//PE para que seja validado o cancelamento da SS
				If ExistBlock( "MNTA280H" ) .And. nOperacao == 5
					If !ExecBlock( "MNTA280H", .F., .F. )
						Return .F.
					EndIf
				EndIf
			EndIf
			If lFacilit
				If Val( AllTrim(StrTran(TQB->TQB_TEMPO,":","")) ) == 0
					If IsInCallStack("MNTA291") .And. TQB->TQB_SOLUCA == "D" .And. nOperacao == 5
						If !MsgYesNo(STR0048 + CRLF + CRLF + STR0049) //"Não há reporte de horas realizado para a Solicitação de Serviço." ## "Deseja cancelar mesmo assim?"
							Return .F.
						EndIf
					EndIf
				EndIf
			EndIf
			If nOpcx <> 5
				If AllTrim(GetNewPar("MV_NGMULOS","N")) == "S"
					dbSelectArea("TT7")
					dbSetOrder(1)
					dbSeek(xFilial("TT7")+TQB->TQB_SOLICI)
					While !Eof() .And. TT7->TT7_FILIAL == xFilial("TT7") .And. TT7->TT7_SOLICI == TQB->TQB_SOLICI
						If TT7->TT7_TERMIN == "N" .and. ( TT7->TT7_SITUAC == "L" .Or. TT7->TT7_SITUAC == "P" )
							cOrdemTx += TT7->TT7_ORDEM+chr(13)+chr(10)
						EndIf
						dbSelectArea("TT7")
						dbSkip()
					End
				Else
					If !Empty(TQB->TQB_ORDEM)
						dbSelectArea("STJ")
						dbSetOrder(01)
						If dbSeek(xFilial("STJ")+TQB->TQB_ORDEM)
							If (STJ->TJ_SITUACA = "L" .Or. STJ->TJ_SITUACA = "P" ).And. STJ->TJ_TERMINO = "N"
								cOrdemTx += STJ->TJ_ORDEM+chr(13)+chr(10)
							EndIf
						EndIf
					EndIf
				EndIf
				If !Empty(cOrdemTx)
					//Recebe o texto do cabeçalho
					cTexto := STR0050+chr(13)+chr(10)+STR0052+tqb->tqb_solici+chr(13)+chr(10)+STR0051+chr(13)+chr(10)
					//Recebe as O.S. que precisam ser finalizadas
					cTexto += cOrdemTx
					//Exibe a tela de observação
					NGMSGMEMO(STR0009,cTexto)
					Return
				EndIf
			Else
				If AllTrim(GetNewPar("MV_NGMULOS","N")) <> "S"
					If !Empty(tqb->tqb_ordem)
						MsgInfo(STR0053+STR0054+chr(13)+chr(10)+chr(13)+chr(10)+; //"Exclusao não permitida. " ## "Existe ordem de servico para a solicitacao.."
						STR0052+tqb->tqb_solici+chr(13)+chr(10)+; //"Solicitacao de servico..: "
						STR0051+tqb->tqb_ordem+chr(13)+chr(10),STR0055) //"Ordem de Servico........: " ## "ATENCAO"
						Return .F.
					EndIf
				Else
					dbSelectArea("TT7")
					dbSetOrder(1)
					If dbSeek(xFilial("TT7")+TQB->TQB_SOLICI)
						MsgInfo(STR0053+STR0056+chr(13)+chr(10)+STR0057+; //"Exclusao não permitida. " ## "A Solicitação..." ## "uma ou mais Ordens ..."
						chr(13)+chr(10)+chr(13)+chr(10)+STR0052+tqb->tqb_solici+chr(13)+chr(10),STR0055) //"Solicitacao de servico..: " ## "ATENCAO"
						Return .F.
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf

	dbSelectArea("TQB")

	// Declaracao do aRotina, pois existem rotinas que nao tem todas as opcoes de menu
	aRotina := {{STR0001, "AxPesqui"    ,0, 1},; // "Pesquisar"
	{STR0002, "MNTA280IN(2)",0, 2},; // "Visualizar"
	{STR0003, "MNTA280IN(3)",0, 3},; // "Incluir"
	{STR0004, "MNTA280IN(4)",0, 4},; // "Alterar"
	{STR0005, "MNTA280IN(5)",0, 5,3}}// "Excluir"

	If nOperacao == 5 .And. nOpcx == 4
		M->TQB_DTCANC := dDataBase
		M->TQB_HRCANC := SubStr(Time(),1,5)
	EndIf

	// Inicializa variavéis utilizadas na rotina como private
	MNT280CPO(nOperacao,nOpcx)

	If lRPORel17

		RegToMemory( 'TQB', .T. )
		MNT280REG( nOpcx, a280Relac, a280Memos, .T. )

	// Carrega o relação dos campos listados nos arrays a280Relac e a280Memos para versões que utilizam a classe MNTSR
	ElseIf nOpcx > 0

		// Aciona o relação para os campos listados no array a280Relac
		For nInd := 1 To Len( a280Relac )

			oTQB:setValue( a280Relac[nInd,1], InitPad( a280Relac[nInd,2] ) )

		Next nInd

		// Aciona o relação para os campos do tipo memo listados no array a280Memos
		For nInd := 1 To Len( a280Memos )

			If ExistIni( a280Memos[nInd,2] )
					oTQB:setValue( a280Memos[nInd,2], InitPad( Posicione( 'SX3', 2, a280Memos[nInd,2], 'X3_RELACAO' ) ) )
				EndIf

		Next nInd

		// Transfere os valores do Objeto para a memoria
		oTQB:ClassToMemory()

	EndIf

	lValidCls := fVldFecha(nChamada, nOpcx)

	If lValidCls

		// Enchoice bar sem botão "X" para fechar
		Define MsDialog oDlg280 Title OemToAnsi(cCadastro) From aSize[7],0 To aSize[6],aSize[5] Of oMainWnd Pixel STYLE DS_MODALFRAME
	
	Else

		// Enchoice bar com botão "X" para fechar
		Define MsDialog oDlg280 Title OemToAnsi(cCadastro) From aSize[7],0 To aSize[6],aSize[5] Of oMainWnd Pixel
	
	EndIf
	
	oDlg280:lMaximized := .T.

	//Define painel principal
	oPanel := TPanel():New(0,0,,oDlg280,,,,,,aSize[5],aSize[6],.F.,.F.)
	oPanel:Align := CONTROL_ALIGN_ALLCLIENT

	// Cria Enchoice com informacoes
	dbSelectArea("TQB")
	oEnchoice := MsMGet():New("TQB",TQB->(Recno()),nOpcx,,,,a280Choice,aPosObj[1],,,,,,oPanel,,,.F.)

	If lFacilit .and. !Inclui
		oEnchoice:oBox:Align := CONTROL_ALIGN_TOP

		// Cria Folder com informacoes
		oFolder280 := TFolder():New( 0,0,{STR0058,STR0059},,oPanel,,,,.T.,,0,aSize[6]) // "Follow-Up" ## "Atendentes"
		oFolder280:Align := CONTROL_ALIGN_ALLCLIENT

		// GetDados de Follow-up
		aArrGet := MNT280RFU(TQB->TQB_SOLICI)
		oGet280Fol := MsNewGetDados():New(5,5,500,500,0,"AllWaysTrue()","AllWaysTrue()",,,,9999,,,,oFolder280:aDialogs[1],aArrGet[1],aArrGet[2])
		oGet280Fol:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT
		oGet280Fol:oBrowse:Refresh()

		// GetDados de Atendimentos
		aArrGet := MNT280RAT(TQB->TQB_SOLICI)
		oGet280Ate := MsNewGetDados():New(5,5,500,500,0,"AllWaysTrue()","AllWaysTrue()",,,,9999,,,,oFolder280:aDialogs[2],aArrGet[1],aArrGet[2])
		oGet280Ate:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT
		oGet280Ate:oBrowse:Refresh()

	Else
		oEnchoice:oBox:Align := CONTROL_ALIGN_ALLCLIENT
	EndIf

	If Len(aSMenu) > 0
		NGPOPUP(aSMenu,@oMenu,oPanel)
		oPanel:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oPanel)}
	EndIf

	// Finaliza o MsDialog e faz a gravação
	If !lRPORel17
		Activate MsDialog oDlg280 On Init (EnchoiceBar(oDlg280,{|| If(MNT280TOK(nOperacao, nOpcx, @oTQB, lFacilit),(lOk:=.T.,oDlg280:End()),lOk:=.F.)},{|| If(lValidCls, lOk := lValidCls, (lOk:=lValidCls,oDlg280:End()))},,a280Buttons)) Centered

		cCodSS := MNT280GRV(nOperacao, nOpcx, lOk, a280Memos, , , @oTQB)
	Else
		Activate MsDialog oDlg280 On Init (EnchoiceBar(oDlg280,{|| If(MNT280TOK(nOperacao, nOpcx),(lOk:=.T.,oDlg280:End()),lOk:=.F.)},{|| lOk:=.F.,oDlg280:End()},,a280Buttons)) Centered

		cCodSS := MNT280GRV(nOperacao, nOpcx, lOk, a280Memos)
	EndIf

	If nOpcx != 3
		RestArea(aArea)
	EndIf

	If !Empty(cFiltro)
		dbSelectArea("TQB")
		Set Filter to &(cFiltro)
	EndIf
	NGRETURNPRM(aNGBEGINPRM)
	If !lRPORel17
		FreeObj(oTQB)
	EndIf

Return cCodSS

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTA280CHE
Consistencia final
@author  Inacio Luiz Kolling
@since   24/11/2003
@version p12
/*/
//-------------------------------------------------------------------
Function MNTA280CHE()

	Local nRegTQB := TQB->(RecNo())
	Local lRet 		:= .T.
	Local cMsgAlert  := ""

	// Release menor ou igual a 17
	Local lRPORel17 := IIf(GetRPORelease() <= '12.1.017', .T., .F.)

	If !l280Auto
		// ALERTA DUPLICIDADE DE S.S. -> Bem + Serviço ou Bem + Área da Manutenção (Facilities)
		If !Empty(M->TQB_CDSERV) .And. INCLUI //Valida somente na Inclusao de S.S. (deve possuir um Tipo de Servico ou, no caso do Facilities, uma Área de Manutenção)
			If !lFacilit
				cMsgAlert := STR0027 + CRLF + STR0028 //"Existe pelo menos uma Solicitação de Serviço incluída" ## "para o mesmo bem/localização e serviço desta S.S."
			Else
				cMsgAlert := STR0027 + CRLF + STR0061 // "Existe pelo menos uma Solicitação de Serviço incluída" ## "para o mesmo bem/localização e área de manutenção desta S.S."
			EndIf

			dbSelectArea("TQB")
			dbSetOrder(05)
			dbSeek(xFilial("TQB")+M->TQB_CODBEM,.T.)
			While !Eof() .And. TQB->TQB_FILIAL == xFilial("TQB") .And. TQB->TQB_CODBEM == M->TQB_CODBEM
				If TQB->TQB_CDSERV == M->TQB_CDSERV .And. TQB->TQB_SOLUCA == "A" //somente aguardando analise
					If !APMSGYESNO(cMsgAlert + CRLF + CRLF + STR0029, STR0030)  //"Deseja confirmar a operação?"##"Duplicidade de S.S."
						dbGoTo(nRegTQB)
						lRet := .F.
					EndIf
					Exit
				EndIf
				dbSkip()
			EndDo
			dbGoTo(nRegTQB)
		EndIf
	EndIf

	If lRet .And. ExistBlock("MNTA2802")
		If !ExecBlock("MNTA2802",.F.,.F.)
			lRet := .F.
		EndIf
	EndIf

	cFilSt9 := NGSEEK("ST9",M->TQB_CODBEM,1,"T9_FILIAL")
	If lRet .And. TIPOACOM .And. NGCADICBASE("TQB_POSCON","A","TQB",.F.)
		//Verifica se o veiculo tem viagem escalada
		If !NGCHKTMS(M->TQB_CODBEM,M->TQB_DTABER,M->TQB_HOABER)
			lRet := .F.
		EndIf
	EndIf

	If lRet .And. Inclui .And. !l280Auto //Se for inclusão.
		If !NGOSABRVEN( M->TQB_CODBEM,,.F.,.T.,.T.,,,,,2 ) //Função que alerta a existência de O.S. vencidas.
			lRet := .F.
		EndIf
	EndIf

	If lRPORel17
		If lRet .And. Inclui .And. !l280Auto .And. Empty(M->TQB_DESCSS)//Se for inclusão.
			Help( " ",1, STR0100,, STR0101 + Alltrim(Posicione("SX3",2,"TQB_DESCSS","X3Titulo()")) + " (TQB_DESCSS)" + STR0102,3,1 )
			lRet := .F.
		EndIf
	EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA280LEG³ Autor ³ Thiago Olis Machado   ³ Data ³15/01/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Cria uma janela contendo a legenda do mBrowse.              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Objetivo  ³ MNTA280                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA280LEG()
	Local aLegenda := {{"Br_Vermelho",STR0021},; //"Aguardando Analise"
	{"Br_Verde",STR0022},;  //"Distribuída"
	{"Br_Amarelo",STR0023}} //"Com Atraso Cadastrado"

	If ExistBlock("MNTA2806")
		aLegenPE := aCLONE(aLegenda)
		aLegenPE := ExecBlock("MNTA2806",.F.,.F.)
		aLegenda := aCLONE(aLegenPE)
	EndIf

	BrwLegenda(cCadastro,STR0014,aLegenda) //"Legenda"

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA280COR³ Autor ³ Ricardo Dal Ponte     ³ Data ³06/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Definicao das cores do semafaro                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MNTA280                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA280COR()
	Local aCORES := {                                              ;
	{"MNTA280Atr()",'BR_AMARELO'},;
	{"NGSEMAFARO('TQB->TQB_SOLUCA == "+'"A"'+"')",'BR_VERMELHO'},;
	{"NGSEMAFARO('TQB->TQB_SOLUCA == "+'"D"'+"')",'BR_VERDE'}}

	If ExistBlock("MNTA2805")
		aVarsPE := {}
		aCoresPE := aCLONE(aCORES)
		aCoresPE := ExecBlock("MNTA2805",.F.,.F.)
		aCORES := aCLONE(aCoresPE)
	EndIf

Return (aCORES)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG280BEMLOC³ Autor ³ Inacio Luiz Kolling   ³ Data ³17/02/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Consistencia do bem/localizacao                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³GENERICO                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG280BEMLOC(cTipoSS,lWeb)
	
	Local cMsg     := ''
	Local cFamMod  := ''
	Local lRet     := .T.
	Local lFacilit := MNTINTFAC()
	Local nTamTAF  := FWTamSX3( 'TAF_CODNIV' )[1]
	
	Default cTipoSS:= ""
	Default lWeb  := .F.

	If cTipoSS == "B"
		If !ExistCpo("ST9",M->TQB_CODBEM,1)
			cMsg := STR0037 //"Não existe Bem/Localização relacionado a este código."
			lRet := .F.
		Else
			dbSelectArea("ST9")
			dbSetOrder(1)
			If dbSeek(xFilial("ST9")+M->TQB_CODBEM)
				If ST9->T9_SITBEM == "I"//Bem Inativo
					cMsg := STR0038 //"O Bem está Inativo no sistema."
					lRet := .F.
				ElseIf ST9->T9_SITBEM = "T"//Bem Transferido
					cMsg := STR0039 //"O Bem foi Transferido."
					lRet := .F.
				ElseIf ST9->T9_SITMAN = "I"//Situação do bem Inativa
					cMsg := STR0040 //"Situação da Manutenção do bem está Inativa."
					lRet := .F.
				EndIf
				If lRet .and. FindFunction("MNT280FBEM")
					lRet := MNT280FBEM(M->TQB_CODBEM)
					If !lRet
						cMsg := STR0041 //"Usuário sem permissão para incluir solicitações para este bem."
					EndIf
				EndIf
				
				If lRet
					
					dbSelectArea( 'TAF' )
					dbSetOrder( 6 ) // TAF_FILIAL + TAF_MODMNT + TAF_INDCON + TAF_CODCON
					If dbSeek( xFilial( 'TAF' ) + 'X' + '1' + M->TQB_CODBEM )
						
						If NGValidTUA()
							lRet := MNT902REST( TAF->TAF_CODCON, 'S', 'I', .F., .F. )
						Else
							lRet := .F.
						EndIf

						If !lRet
							cMsg := STR0041 //"Usuário sem permissão para incluir solicitações para este bem."
						EndIf

					EndIf

				EndIf
				
				If lRet
					TIPOACOM := If(ST9->T9_TEMCONT = "S",.T.,.F.)
					//FindFunction remover na release GetRPORelease() >= '12.1.027'
					If FindFunction("MNTCont2")
						TipoAcom2 := MNTCont2(xFilial("TPE"), M->TQB_CODBEM )
					Else
						TipoAcom2 := NGIFDBSEEK("TPE",M->TQB_CODBEM,1)
					EndIf

					cFamMod  := ST9->T9_CODFAMI+ST9->T9_TIPMOD
				ElseIf !lWeb .and. !Empty(cMsg)
					Help(" ",1,STR0055,,cMsg,4,5) //"ATENCAO"
				EndIf
			EndIf
		EndIf
		If lRet
			M->TQB_NOMBEM  := NGSEEK("ST9",M->TQB_CODBEM,1,"T9_NOME")
			M->TQB_CCUSTO  := NGSEEK("ST9",M->TQB_CODBEM,1,"T9_CCUSTO")
			M->TQB_NOMCUS  := NGSEEK("CTT",M->TQB_CCUSTO,1,"CTT_DESC01")
			M->TQB_CENTRA  := NGSEEK("ST9",M->TQB_CODBEM,1,"T9_CENTRAB")
			M->TQB_NOMCTR  := NGSEEK("SHB",M->TQB_CENTRA,1,"HB_NOME")
		EndIf
		lREFRESH := .T.
	Else
		
		cCODBEM := 'X2' + SubStr( M->TQB_CODBEM, 1, nTamTAF )

		If !ExistCPO( 'TAF', cCODBEM, 7 )

			cMsg := STR0037 //"Não existe Bem/Localização relacionado a este código."
			lRet := .F.

		Else
							
			dbSelectArea( 'TAF' )
			dbSetOrder( 2 )
			If msSeek( FWxFilial( 'TAF' ) + '001' + SubStr( M->TQB_CODBEM, 1, nTamTAF ) )
									
				If NGValidTUA()
					lRet := MNT902REST( TAF->TAF_CODNIV, 'S', 'I', .F. )
				EndIf

				If !lRet
					cMsg := STR0042 //"Usuário sem permissão para incluir solicitações para esta localização."
				EndIf

			EndIf

			If !lWeb .and. !lRet .and. !Empty(cMsg)
				Help(" ",1,STR0055,,cMsg,4,5) //"ATENCAO"
			EndIf

		EndIf

		If lRet
			M->TQB_NOMBEM := NGSEEK("TAF",cCODBEM,7,"TAF->TAF_NOMNIV")
			M->TQB_CCUSTO := NGSEEK("TAF",cCODBEM,7,"TAF->TAF_CCUSTO")
			M->TQB_CENTRA := NGSEEK("TAF",cCODBEM,7,"TAF->TAF_CENTRA")
			M->TQB_NOMCUS := NGSEEK("CTT",M->TQB_CCUSTO,1,"CTT->CTT_DESC01")
			M->TQB_NOMCTR := NGSEEK("SHB",M->TQB_CENTRA,1,"SHB->HB_NOME")

			If lFacilit
				cFamMod  := NGSEEK("TAF",cCODBEM,7,"TAF->TAF_CODFAM")
			EndIf
		EndIf
	EndIf

	If Type("M->TQB_POSCON") <>  "U"
		M->TQB_POSCON := 0
		M->TQB_POSCO2 := 0
	EndIf

	//Se tiver Novo Facilities verifica Questionário de Sintomas
	If lRet .and. lFacilit .and. !Empty(cFamMod) .and. !IsInCallStack("MNTA295") .And. ( Type( "l280Auto" ) <> "L" .Or. !l280Auto )
		M->TQB_DESCSS := A280MTRESP({}, M->TQB_DESCSS)
		dbSelectArea("TU3")
		dbSetOrder(2)
		If dbSeek(xFilial("TU3")+cFamMod)
			c280MemoDg := ""
			cMsg := STR0043+CHR(13)+STR0044 //"Existe um questionário para identificação de Sintomas disponível."###"Deseja responder o mesmo?"
			If !lWeb
				If MsgYesNo(cMsg)
					MNT280DIAG(Inclui, Altera, @c280MemoDg)
				EndIf
			EndIf
		EndIf
	EndIf

	dbSelectArea("TQB")
	lREFRESH := .T.

Return If(!lWeb, lRet, {lRet, cMsg, cFamMod})
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG280SAT    ³ Autor ³ Ricardo Dal Ponte     ³ Data ³11/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica pesquisa de satisfacao                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTA280                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG280SAT(cCodUsrSS)
	Local lAchouSS := .F.
	Local cCondic, cAddFiltro
	Local lFacilit := MNTINTFAC()
	Default cCodUsrSS := RetCodUsr()

	dbSelectArea("TQB")
	dbSetOrder(10)

	dbSeek(xFILIAL("TQB")+cCodUsrSS)

	If !lFacilit
		cCondic := 'TQB->TQB_SOLUCA == "E" .And. (Empty(TQB->TQB_PSAP) .Or. Empty(TQB->TQB_PSAN)) .And. MNT305CHKP()'
	Else
		cCondic := 'TQB->TQB_SOLUCA == "E" .and. !Empty(TQB->TQB_SEQQUE) .and. TQB->TQB_SATISF == "2"'
	EndIf

	If ExistBlock("MNTA2804")
		cAddFiltro := ExecBlock("MNTA2804",.F.,.F.)
		If !Empty(cAddFiltro)
			cCondic += ' .And. ('+cAddFiltro+')'
		EndIf
	Else
	EndIf

	While !Bof() .And. TQB->TQB_FILIAL == xFilial("TQB") .And.;
	Alltrim(TQB->TQB_CDSOLI) == Alltrim(cCodUsrSS)
		If &cCondic
			lAchouSS := .T.
			Exit
		EndIf
		TQB->(dbSkip())
	End

	If lAchouSS = .T.
		Return .F.
	EndIf
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Ricardo Dal Ponte     ³ Data ³29/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³    1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()
	Local nX := 0
	Local aROTINA := {	{STR0001, "PesqBrw"     ,0, 1},; //"Pesquisar"
						{STR0002, "MNTA280IN(2)",0, 2},; //"Visualizar"
						{STR0003, "MNTA280IN(3)",0, 3},; //"Incluir"
						{STR0004, "MNTA280IN(4)",0, 4},; //"Alterar"
						{STR0005, "MNTA280IN(5)",0, 5,3},; //"Excluir"
						{STR0014, "MNTA280LEG"  ,0, 5,,.F.},; // Legenda
						{"Conhecimento", "MsDocument"  ,0, 4}}

	If ExistBlock("MNTA280B")
		aRetBt := ExecBlock( 'MNTA280B', .F., .F. )
		If Valtype(aRetBt) <> "A"
			MsgStop(STR0026)
		Else
			For nX := 1 to Len(aRetBt)
				aAdd( aRotina , aClone(aRetBt[nX]))
			Next nX
		EndIf
	EndIf

Return(aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTA280Atr³ Autor ³ Rafael Diogo Richter  ³ Data ³ 25/06/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se a OS da SS esta cadastrada com Atraso          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAMNT                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTA280Atr()
	Local lRet := .F.

	If AllTrim(GetNewPar("MV_NGMULOS","N")) == "S"
		dbSelectArea("TT7")
		dbSetOrder(1)
		dbSeek(xFilial("TT7")+TQB->TQB_SOLICI)
		While !Eof() .And. TT7->TT7_FILIAL == xFilial("TT7") .And. TT7->TT7_SOLICI == TQB->TQB_SOLICI
			If TT7->TT7_TERMIN == "N"
				dbSelectArea("TPL")
				dbSetOrder(1)
				If dbSeek(xFilial("TPL")+TT7->TT7_ORDEM)
					If !Empty(TPL->TPL_DTINIC) .And. !Empty(TPL->TPL_HOINIC)
						lRet := .T.
						Exit
					EndIf
				EndIf
			EndIf
			dbSelectArea("TT7")
			dbSkip()
		End
	Else
		If !Empty(TQB->TQB_ORDEM)
			dbSelectArea("TPL")
			dbSetOrder(1)
			If dbSeek(xFilial("TPL")+TQB->TQB_ORDEM)
				lRet := .T.
			EndIf
		EndIf
	EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³  MNT280AL  ³ Autor ³ Rafael Diogo Richter ³ Data ³21/07/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao para preencher os campos da tela quando chamado a    ³±±
±±³          ³ partir da arvore logica.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MNTA902                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         Atualizacoes Sofridas Desde a Construcao Inicial.              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ F.O  ³  Motivo da Alteracao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNT280AL(nTipVal)

	Local aArea := GetArea()
	Local dDataSS := dDatabase

	If FunName()=='MNTA902' .And. !l280Auto
		M->TQB_TIPOSS := If(lBemAL,"B","L")
		M->TQB_CODBEM := cBemAL
	EndIf

	//Ponto de entrada para alterar a data da inclusao da S.S.
	If ExistBlock("MNTA280F")
		dDataSS := ExecBlock("MNTA280F",.F.,.F.)
	EndIf

	If nTipVal == 1 .And. !l280Auto
		NG280BEMLOC(M->TQB_TIPOSS)
		RestArea(aArea)
		Return cBemAL
	ElseIf nTipVal == 2
		Return dDataSS
	ElseIf nTipVal == 3
		Return Substr(Time(),1,5)
	ElseIf nTipVal == 4
		Return  __cUserID
	EndIf

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³NG280CONT ³ Autor ³Evaldo Cevinscki Jr.   ³ Data ³ 20/04/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validacao dos campos de contador na tela                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MNTA280                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function NG280CONT()
	Local lRet := .F.
	Local cVar280 := ReadVar(),cFilSt9L := NGSEEK("ST9",M->TQB_CODBEM,1,"T9_FILIAL")

	If "TQB_POSCON" $ cVar280
		lRet := If(M->TQB_POSCON > 0,CHKPOSLIM(M->TQB_CODBEM,M->TQB_POSCON,1),If(POSITIVO(),.T.,.F.))
		If lRet
			lRet := If(M->TQB_POSCON > 0,NGCHKHISTO(M->TQB_CODBEM,M->TQB_DTABER,M->TQB_POSCON,M->TQB_HOABER,1,,.T.,cFilSt9L),.T.)
		EndIf

		If lRet .And. M->TQB_POSCON > 0
			vVetCont := NGACUMEHIS(M->TQB_CODBEM,M->TQB_DTABER,M->TQB_HOABER,1,"A")
			nAcumReg := vVetCont[2] + (M->TQB_POSCON - vVetCont[1])
			nNovaVar := NGVARIADT(M->TQB_CODBEM,M->TQB_DTABER,1,nAcumReg,.F.,.T.)
			If !NGCHKLIMVAR(M->TQB_CODBEM,NGSEEK('ST9',M->TQB_CODBEM,1,'T9_CODFAMI'),1,nNovaVar,.T.,.T.)
				lRET := .F.
			EndIf
			If lRet .And. !NGVALIVARD(M->TQB_CODBEM,M->TQB_POSCON,M->TQB_DTABER,M->TQB_HOABER,1,.T.,,cFilSt9L)
				lRET := .F.
			EndIf
		EndIf

	ElseIf "TQB_POSCO2" $ cVar280
		lRet := If(M->TQB_POSCO2 > 0,CHKPOSLIM(M->TQB_CODBEM,M->TQB_POSCO2,2),If(POSITIVO(),.T.,.F.))
		If lRet
			lRet := If(M->TQB_POSCO2 > 0,NGCHKHISTO(M->TQB_CODBEM,M->TQB_DTABER,M->TQB_POSCO2,M->TQB_HOABER,2,,.T.,cFilSt9L),.T.)
		EndIf
		If lRet .And. M->TQB_POSCO2 > 0
			vVetCont := NGACUMEHIS(M->TQB_CODBEM,M->TQB_DTABER,M->TQB_HOABER,2,"A")
			nAcumReg := vVetCont[2] + (M->TQB_POSCO2 - vVetCont[1])
			nNovaVar := NGVARIADT(M->TQB_CODBEM,M->TQB_DTABER,1,nAcumReg,.F.,.T.)
			If !NGCHKLIMVAR(M->TQB_CODBEM,NGSEEK('ST9',M->TQB_CODBEM,1,'T9_CODFAMI'),2,nNovaVar,.T.,.T.)
				lRET := .F.
			EndIf
			If lRet .And. !NGVALIVARD(M->TQB_CODBEM,M->TQB_POSCO2,M->TQB_DTABER,M->TQB_HOABER,2,.T.,,cFilSt9L)
				lRET := .F.
			EndIf
		EndIf
	EndIf
Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} MN280AUTO
Processo para chamada a partir de MsExecAuto.
Rotina Automatica.

@param nOpc Numero da operacao.

@author Hugo R. Pereira
@since 07/05/2013
@version MP11
@return nRetTQB Define se a inclusao foi efetuada com sucesso.
1 Registro incluido.
0 Falha na inclusao.
/*/
//---------------------------------------------------------------------
Static Function MN280AUTO(nOpc)

	Local nRetTQB	:= 0
	Local nOperac	:= 1
	Local aArea     := {}

	// Variáveis utilizadas para gravação do memo
	Local nSMM      := IIf( nOpc == 3 .Or. nOpc == 4, 1, 2 )
	Local cCodMSMM  := IIf( nOpc != 3, TQB->TQB_CODMSS, Nil )
	Local nTamTot   := 0
	Local nInd      := 0
	Local lVal      := .T.

	// Release menor ou igual a 17
	Local lRPORel17 := IIf(GetRPORelease() <= '12.1.017', .T., .F.)

	MN280INIAT()            // Realiza validações iniciais à atribuição de memoria
	MNT280CPO(nOperac,nOpc)
	If !lRPORel17

		// Não apresenta mensagens condicionais
		oTQB:setAsk(.F.)

		nTamTot := Len(aRotAuto)
		For nInd := 1 To nTamTot
			oTQB:SetValue(aRotAuto[nInd][1], aRotAuto[nInd][2])
			If aRotAuto[nInd][1] == "TQB_CODBEM" .And. !Empty( aRotAuto[nInd][2] )
				oTQB:SetValue("TQB_CCUSTO", POSICIONE("ST9", 1, xFilial("ST9") + aRotAuto[nInd][2], "T9_CCUSTO"))
			EndIf
		Next nInd

		If !oTQB:valid()
			oTQB:showHelp()
			lVal := .F.
		EndIf

	Else
		RegToMemory("TQB",.T.)	// Carrega memoria dos campos da tabela TQB
	EndIf

	If !lRPORel17
		// Realiza validações, 'em tela', baseado nas memorias dos campos
		If EnchAuto( "TQB", aRotAuto, {|| MN280EXSOL() .And. MNTA280CHE() .And. lVal }, nOpc )
			oTQB:upsert() // Método para inclusão.
			nRetTQB := 1
		EndIf
	Else
		// Realiza validações, 'em tela', baseado nas memorias dos campos
		If EnchAuto( "TQB", aRotAuto, {|| MN280EXSOL() .And. MNTA280CHE() }, nOpc )
			nRetTQB := AxIncluiAuto("TQB",,,nOpc) // Persiste registro na tabela TQB
		EndIf
	EndIf

	// Caso a inclusao foi efetuada corretamente
	If nRetTQB == 1
		// Gravação do memo para rotina automática.
		MSMM( cCodMSMM,,, M->TQB_DESCSS, nSMM,,, "TQB", "TQB_CODMSS" )
		ConfirmSX8() // Confirma utilização de numero sugerido
		If lFacilit
			MNT280GFU(M->TQB_SOLICI,"01") //Grava Follow-up
		EndIf

		If lRPORel17 .And. M->TQB_SOLUCA == 'A' .And. AllTrim( SuperGetMv( 'MV_NGSSWRK', .F., '' ) ) == 'S'

			// para versões posteriores a 17 o wf é enviado através do método upsert do oTQB
			aArea := GetArea()
			MNTW025( M->TQB_SOLICI ) // Workflow de inclusão de S.S.
			RestArea( aArea )

		EndIf

		If ExistBlock("MNTA2807")
			aArea := GetArea()
			ExecBlock("MNTA2807",.F.,.F.)
			RestArea(aArea)
		EndIf

	Else
		RollBackSX8() // Cancela utilização de numero sugerido
	EndIf

Return nRetTQB

//---------------------------------------------------------------------
/*/{Protheus.doc} MN280INIAT
Consiste campos a serem desconsiderados, caso repassados.

@author Hugo R. Pereira
@since 07/05/2013
@version MP11
@return Nil
/*/
//---------------------------------------------------------------------
Static Function MN280INIAT()

	Local cCpo := 0

	// Retira campo Solicitação caso repassado
	// Tratamento efetuado devido ao AutoIncremento no campo
	If (cCpo := aScan(aRotAuto,{|x| Upper(AllTrim(x[1])) == "TQB_SOLICI" })) > 0
		RollBackSX8()
		aDel(aRotAuto,cCpo)
		aSize(aRotAuto,Len(aRotAuto)-1)
	EndIf

	// Retira campo Filial caso repassado
	// Tratamento efetuado devido à utilização, por default, do retorno de xFilial
	If (cCpo := aScan(aRotAuto,{|x| Upper(AllTrim(x[1])) == "TQB_FILIAL" })) > 0
		aDel(aRotAuto,cCpo)
		aSize(aRotAuto,Len(aRotAuto)-1)
	EndIf

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} MN280EXSOL
Consiste campos obrigatorios.

@author Hugo R. Pereira
@since 07/05/2013
@version MP11
@return Boolean Define se a obrigatoriedade de campos foi obedecida.
.T. Sem inconsistencias, campos obrigatorios preenchidos.
.F. Inconsistencia encontrada.
/*/
//---------------------------------------------------------------------
Static Function MN280EXSOL()

	Local aObgGets := aClone(a280Choice)
	Local aArea    := GetArea()
	Local nField

	// Verifica se todos os campos obrigatorios estão preenchidos
	For nField := 1 to Len(aObgGets)
		If X3Obrigat(aObgGets[nField]) .And. Empty(M->&(aObgGets[nField]))
			Help(" ",1,aObgGets[nField],,,4,5)
			Return .F.
		EndIf
	Next nField

	// Verifica se o numero da solicitação já esta sendo utilizado
	If NGIFDBSEEK("TQB", M->TQB_SOLICI, 1)
		Help(" ",1,STR0020,,STR0045 + Space(1) +  AllTrim(M->TQB_SOLICI) + ".",4,5) // "Aviso" ## "Já existe uma solicitação de serviço com o número"
		Return .F.
	EndIf

	RestArea(aArea)

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MNT280DIAGºAutor  ³Roger Rodrigues     º Data ³  03/03/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Carrega itens para o Questionário de Sintomas da SS         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³MNTA280/MNTI0060                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNT280DIAG(lInclui, lAltera, cMemoDg)
	Local oDlgDiag, oPanelBot
	Local nOpca := 2, nDiv := 1.3
	//Variaveis de tamanho de tela e objetos
	Local aSize := {}, aObjects := {}, aInfo := {}, aPosObj := {}
	Local cOldServ := M->TQB_CDSERV
	Local cOldDesc := M->TQB_NMSERV
	Default lInclui := .F.
	Default lAltera := .F.

	Private oScroll280, oPanelTop
	Private oFont    := TFont():New("Arial",,-10,.F.,.F.)
	Private oFont12  := TFont():New("Arial",,-12,.T.,.T.)
	Private oFont14  := TFont():New("Arial",,-14,.T.,.T.)
	Private cCodBem  := cDescBem :=  cFamMod := ""
	Private aOpcoes  := {}
	Private cSupAtu  := "001", cTipAtu := "1", lPaiPerg := .F.

	//Definicao de tamanho de tela e objetos
	aSize := MsAdvSize(,.F.,430)
	Aadd(aObjects,{100,100,.T.,.T.})
	Aadd(aObjects,{025,025,.T.,.T.})
	aInfo := {aSize[1]/nDiv,aSize[2]/nDiv,aSize[3]/nDiv,aSize[4]/nDiv,0,0}
	aPosObj := MsObjSize(aInfo, aObjects,.T.)

	//Carrega nome do bem
	cCodBem := AllTrim(M->TQB_CODBEM)
	If Empty(cCodBem)
		ShowHelpDlg(STR0055,{STR0062}) //"ATENCAO" ## "Para acessar a tela de Questionário de Sintomas, o código do Bem/Localização deve ser preenchido antes."
		Return .F.
	EndIf

	If M->TQB_TIPOSS == "B"
		dbSelectArea("ST9")
		dbSetOrder(1)
		If dbSeek(xFilial("ST9")+cCodBem)
			cDescBem := Substr(ST9->T9_NOME,1,40)
			cFamMod  := ST9->T9_CODFAMI+ST9->T9_TIPMOD
		EndIf
	Else
		dbSelectArea("TAF")
		dbSetOrder(8)
		If dbSeek(xFilial("TAF")+cCodBem)
			cDescBem := Substr(TAF->TAF_NOMNIV,1,40)
			cFamMod  := TAF->TAF_CODFAM + Space(TamSX3('T9_TIPMOD')[1])
		EndIf
	EndIf

	//Se questionario estiver em branco e for visualizacao, tenta carregar da SS posicionada
	If Empty(cMemoDg) .and. !lInclui
		cMemoDg := MSMM(TQB->TQB_MEMODG)
	EndIf
	//Carrega Questionário de Sintomas da Familia
	Processa({|| aOpcoes := MNT280PERG(cSupAtu,.T.,,cMemoDg)},STR0063) //"Aguarde... Carregando Questionário de Sintomas"
	If Len(aOpcoes) == 0
		ShowHelpDlg(STR0055,{STR0064}) //"ATENÇÃO" ## "Não existe nenhum questionário de sintomas cadastrado para a Família/Tipo Modelo do Bem/Localização selecionado."
		Return .F.
	EndIf

	DEFINE MSDIALOG oDlgDiag TITLE OemToAnsi(cCadastro) From aSize[7],0 To aSize[6]/nDiv,aSize[5]/nDiv OF oMainWnd PIXEL
	oPainelPai := TPanel():New(00,00,,oDlgDiag,,,,,,00,00,.F.,.F.)
	oPainelPai:Align := CONTROL_ALIGN_ALLCLIENT
	oDlgDiag:lEscClose := .F.

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Parte de Cima da Tela               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oPanelTop        := TPanel():New(0,0,,oPainelPai,,,,,,,aPosObj[1,3]-15,.F.,.F.)
	oPanelTop:Align  := CONTROL_ALIGN_TOP

	fMontaTop(,lInclui,lAltera,,aPosObj)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Parte de Baixo da Tela              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oPanelBot := TPanel():New(0,0,,oPainelPai,,,,,,10,10,.F.,.F.)
	oPanelBot:nHeight := 20
	oPanelBot:Align := CONTROL_ALIGN_ALLCLIENT

	@ 3,aPosObj[1,4]-110 Button oBtnAnt Prompt STR0065 Size 40,14 Of oPanelBot Pixel When (cSupAtu != "001") Action (fMontaTop(.T.,lInclui,lAltera,.T.,aPosObj)) //"Anterior"

	@ 3,aPosObj[1,4]-65 Button oBtnProx Prompt STR0066 Size 40,14 Of oPanelBot Pixel When (cTipAtu == "2" .or. cTipAtu == "1"); //"Próximo"
	Action (If(fMontaTop(.T.,lInclui,lAltera,,aPosObj)>0,(nOpca := 1, oDlgDiag:End()),))

	ACTIVATE MSDIALOG oDlgDiag ON INIT EnchoiceBar(oDlgDiag,{|| nOpca := 1,oDlgDiag:End()},{|| nOpca := 2,oDlgDiag:End()}) Centered

	If nOpca == 1 .and. (lInclui .or. lAltera)
		M->TQB_DESCSS := A280MTRESP(aOpcoes, M->TQB_DESCSS)
		cMemoDg := A280GRVPER(aOpcoes)
	Else
		M->TQB_CDSERV := cOldServ
		M->TQB_NMSERV := cOldDesc
	EndIf

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fMarkItem ºAutor  ³Roger Rodrigues     º Data ³  04/03/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Marca item                                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³MNTA280                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fMarkItem(nItem)
	Local i
	Local nAt := aScan(aOpcoes, {|x| x[__POS_OPCSUP__] == cSupAtu})
	Local xConteudo := ""
	Local cCmpo 	:= ""

	For i:= 1 To Len(aOpcoes[nAt,__POS_OPCPER__])
		cCmpo := "aOpcoes["+cValtoChar(nAt)+","+cValToChar(__POS_OPCPER__)+","+cValToChar(i)+","+cValToChar(__POS_IMAGEM__)+"]"
		xConteudo := &cCmpo.

		If ValType(xConteudo) != "U"
			If i == nItem
				aOpcoes[nAt,__POS_OPCPER__,i,__POS_MARCAD__] := 1
				aOpcoes[nAt,__POS_OPCPER__,i,__POS_IMAGEM__]:LoadBitmaps("ngradiook")
			Else
				If aOpcoes[nAt,__POS_OPCPER__,i,__POS_MARCAD__] <> 0
					aOpcoes[nAt,__POS_OPCPER__,i,__POS_MARCAD__] := 0
					aOpcoes[nAt,__POS_OPCPER__,i,__POS_IMAGEM__]:LoadBitmaps("ngradiono")
				EndIf
			EndIf
		EndIf
	Next i

	oScroll280:Refresh()
Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fMontaTop ºAutor  ³Roger Rodrigues     º Data ³  04/03/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Monta parte de cima do questionario                         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³MNTA280                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fMontaTop(lDestroy, lInclui, lAltera, lVolta, aPosObj)

	Local i, nPos, nAt, n2At
	Local cEscolha := ""
	Local lPerg := .F.
	Default lDestroy := .F.
	Default lVolta   := .F.

	nAt := aScan(aOpcoes, {|x| x[__POS_OPCSUP__] == cSupAtu})
	If nAt == 0
		Return 1
	EndIf
	cTipAtu  := aOpcoes[nAt,__POS_OPCTIP__]

	If lDestroy//Remonta tela
		If !lVolta//Proximo
			nPos := aScan(aOpcoes[nAt,__POS_OPCPER__], {|x| x[__POS_MARCAD__] > 0 .and. ValType(x[__POS_IMAGEM__]) != "U"})
			If nPos > 0
				lPaiPerg := ( aOpcoes[nAt,__POS_OPCPER__,nPos,__POS_TIPO__] == "2")
				oPanelTop:FreeChildren()
				If lPaiPerg//Se for pergunta, pega o selecionado
					cEscolha := Substr(aOpcoes[nAt,__POS_OPCPER__,nPos,__POS_DESCRI__],1,1)
				EndIf
				cSupAtu := aOpcoes[nAt,__POS_OPCPER__,nPos,__POS_CODNIV__]
				nAt := aScan(aOpcoes, {|x| x[__POS_OPCSUP__] == cSupAtu})
				//Preenche servico da SS
				M->TQB_CDSERV := Space(TAMSX3("TQB_CDSERV")[1])
				M->TQB_NMSERV := Space(TAMSX3("TQB_NMSERV")[1])
				If nAt == 0
					Return 1
				ElseIf !lPaiPerg
					cTipAtu := aOpcoes[nAt,__POS_OPCTIP__]
				Else
					cTipAtu := '2'
				EndIf
			Else
				ShowHelpDlg(STR0055,{STR0067},1,{STR0068}) // "ATENÇÃO" ## "Não é possível avançar, pois nenhum item foi marcado." ## "Marque uma das opções apresentadas."
				IIf(cTipAtu == '3',cTipAtu := '2',cTipAtu)
				Return 0
			EndIf
		Else//Anterior
			oPanelTop:FreeChildren()
			//Configura item superior
			cSupAtu := aOpcoes[nAt,__POS_OPCPAN__]
			nAt := aScan(aOpcoes, {|x| x[__POS_OPCSUP__] == cSupAtu})
			cTipAtu  := aOpcoes[nAt,__POS_OPCTIP__]
			//Verifica se era uma pergunta
			If (n2At := aScan(aOpcoes, {|x| x[__POS_OPCSUP__] == aOpcoes[nAt,__POS_OPCPAN__]})) > 0
				lPaiPerg := (aOpcoes[n2At,__POS_OPCTIP__] == "2")
				If lPaiPerg//Se for pergunta, pega o selecionado
					cTipAtu := '2'
					nPos := aScan(aOpcoes[n2At,__POS_OPCPER__], {|x| x[__POS_MARCAD__] > 0})
					If nPos > 0
						cEscolha := Substr(aOpcoes[n2At,__POS_OPCPER__,nPos,__POS_DESCRI__],1,1)
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf
	If nAt == 0 .or. Len(aOpcoes[nAt,__POS_OPCPER__]) == 0
		Return 1
	EndIf
	@ 0,0 SCROLLBOX oScroll280 VERTICAL OF oPanelTop BORDER
	oScroll280:Align := CONTROL_ALIGN_ALLCLIENT

	nLinObj := 2
	nTam := 12

	//-------------------------------------------------
	//Titulo do Grupo
	//-------------------------------------------------
	oPanelTmp := TPaintPanel():new(nLinObj-1,-4,aPosObj[1,4]+5,14,oScroll280)
	oPanelTmp:addShape("id=2;type=2;left=0;top=0;width="+Alltrim(Str(aPosObj[1,4]*2.2,5))+";height=24;"+;
	"gradient=1,0,0,0,36,0.0,#808090,0.1,#808090,1.0,#FFFFFF;pen-width=1;"+;
	"pen-color=#B0C4DE;can-move=0;can-mark=0;is-blinker=1;")
	@ 2, 11 SAY oGrpNome Prompt cCodBem+" - "+cDescBem PIXEL OF oPanelTmp Font oFont14 COLOR CLR_WHITE

	nLinObj += 25

	//-------------------------------------------------
	//Titulo Questão
	//-------------------------------------------------
	nPosOld := nLinObj+8
	@ nLinObj, 009 SAY oTitulo Prompt Space(40) PIXEL OF oScroll280 Font oFont12


	//-------------------------------------------------
	//Calcula tamanho do Group Box
	//-------------------------------------------------
	For i:=1 to Len(aOpcoes[nAt,__POS_OPCPER__])
		If !lPaiPerg .or. (cEscolha+";") $ aOpcoes[nAt,__POS_OPCPER__,i,__POS_PERGOP__]
			If aOpcoes[nAt,__POS_OPCPER__,i,__POS_TIPO__] == "2" .or. aOpcoes[nAt,__POS_OPCPER__,i,__POS_TIPO__] == "1"
				lPerg := .T.
				nLinObj += 12
			EndIf
		EndIf
	Next i

	//-------------------------------------------------
	//Apresenta Group Box, se houver perguntas
	//-------------------------------------------------
	If lPerg
		nLinObj += 13
		@ nPosOld,09 TO nLinObj-3,aPosObj[1,4]-160 OF oScroll280 PIXEL
	EndIf

	//Retorna a posicao original, para apresentar os radiobox corretamente
	nLinObj := nPosOld - 8


	//-------------------------------------------------
	//Montan lista de opcoes (radio)
	//-------------------------------------------------
	For i:=1 to Len(aOpcoes[nAt,__POS_OPCPER__])
		If !lPaiPerg .or. (cEscolha+";") $ aOpcoes[nAt,__POS_OPCPER__,i,__POS_PERGOP__]
			If aOpcoes[nAt,__POS_OPCPER__,i,__POS_TIPO__] == "2" .or. aOpcoes[nAt,__POS_OPCPER__,i,__POS_TIPO__] == "1"
				oTitulo:SetText(aOpcoes[nAt,__POS_OPCPER__,i,__POS_PERGUN__])
				nLinObj += 12

				aOpcoes[nAt,__POS_OPCPER__,i,__POS_IMAGEM__] := TBtnBmp2():New( nLinObj*2,26,14,14,;
				If(aOpcoes[nAt,__POS_OPCPER__,i,__POS_MARCAD__]==0,"ngradiono","ngradiook"),,,,{||},oScroll280,,,.T. )

				//@ nLinObj, 22 SAY aOpcoes[nAt,__POS_OPCPER__,i,__POS_OBJTXT__] Prompt Space(30) PIXEL OF oScroll280
				aOpcoes[nAt,__POS_OPCPER__,i,__POS_OBJTXT__] := TSay():New(nLinObj,22,,oScroll280,,,,,,.T.,,,200,10)

				aOpcoes[nAt,__POS_OPCPER__,i,__POS_OBJTXT__]:SetText(aOpcoes[nAt,__POS_OPCPER__,i,__POS_DESCRI__])

				If !lInclui .and. !lAltera
					aOpcoes[nAt,__POS_OPCPER__,i,__POS_IMAGEM__]:lReadOnly := .T.
				Else
					aOpcoes[nAt,__POS_OPCPER__,i,__POS_OBJTXT__]:bLClicked:= &("{|| fMarkItem("+cValToChar(i)+") }")
					aOpcoes[nAt,__POS_OPCPER__,i,__POS_IMAGEM__]:bAction	:= &("{|| fMarkItem("+cValToChar(i)+") }")
				EndIf
			ElseIf aOpcoes[nAt,__POS_OPCPER__,i,__POS_TIPO__] == "3"
				//Preenche servico da SS
				M->TQB_CDSERV := aOpcoes[nAt,__POS_OPCPER__,i,__POS_DESCRI__]
				M->TQB_NMSERV := NGSEEK("TQ3",aOpcoes[nAt,__POS_OPCPER__,i,__POS_DESCRI__],1,"TQ3->TQ3_NMSERV")

				// Pela rotina de árvore o carregamento deve ser direto no objeto pois ainda está criando a tela
				If FwIsInCallStack( 'MNTA902' ) .And. Type( 'oTQB' ) == 'O'

					oTQB:setValue( 'TQB_CDSERV', M->TQB_CDSERV )
					oTQB:setValue( 'TQB_NMSERV', M->TQB_NMSERV )
				
				EndIf
			
			EndIf
		EndIf
	Next i

Return If( lPerg , 0 , 1 )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MNT280PERGºAutor  ³Roger Rodrigues     º Data ³  04/03/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Carrega itens de acordo com o item pai                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³MNTA280                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNT280PERG(cPai,lFirst,cAntPai,cOpcoes)
	Local i, j, k, nTotal := 0, nRecno
	Local nProx
	Local aReturn := {}, aItens := {}, aRet := {}
	Local cDescItem := cPergunta := cFather := cLinha := ""
	Default lFirst := .F.//Indica se eh a primeira chamada
	Default cAntPai:= cPai//Indica quem o era o Pai do Item Pai
	Default cOpcoes:= ""

	If !Empty(cOpcoes)//Quando algum questionario ja foi respondido
		ProcRegua(3)
		//Divide por item pai
		IncProc()
		While (nProx := At("</pai>",cOpcoes)) > 0
			aAdd(aItens, Substr(cOpcoes,1,nProx))
			cOpcoes := Substr(cOpcoes,nProx+6)
		End

		//Extrai informacoes comuns e linhas
		IncProc()
		For i:=1 to Len(aItens)
			nProx := At("</inf>",aItens[i])
			aAdd(aRet, {StrTokArr(Substr(aItens[i],1,nProx-1),';'),{}})
			aItens[i] := Substr(aItens[i],nProx+6)
			While (nProx := At("</lin>",aItens[i])) > 0
				aAdd(aRet[Len(aRet),2], Substr(aItens[i],1,nProx-1))
				aItens[i] := Substr(aItens[i],nProx+6)
			End
		Next i
		aItens := {}
		aReturn:= Array(Len(aRet),__TAM_ARROPC__)

		//Monta Array Principal
		IncProc()
		For i:=1 to Len(aRet)
			For j:=1 to __TAM_ARROPC__
				If j==__POS_OPCPER__
					aReturn[i][j] := {}
				ElseIf j==1
					aReturn[i][j] := aRet[i][1][j]
				Else
					aReturn[i][j] := aRet[i][1][j-1]
				EndIf
			Next j
		Next i
		aItens := {}
		//Monta array com opcoes
		IncProc()
		For i:=1 to Len(aReturn)
			For j:=1 to Len(aRet[i][__POS_OPCPER__])
				aItens := Array(__TAM_ARRAY__)
				cLinha := aRet[i][__POS_OPCPER__][j]
				k := 0
				While (nProx := At("</col>", cLinha)) > 0 .and. k <= __TAM_ARRAY__
					k++
					If Substr(cLinha,(nProx-3),3) == "<c>"
						aItens[k] := Substr(cLinha,1,nProx-4)
					ElseIf Substr(cLinha,(nProx-3),3) == "<n>"
						aItens[k] := Val(Substr(cLinha,1,nProx-4))
					Else
						aItens[k] := Nil
					EndIf
					cLinha := Substr(cLinha,nProx+6)
				End
				aAdd(aReturn[i][__POS_OPCPER__], aItens)
			Next j
		Next i
		aItens := {}
		aRet := {}
	Else//Primeiro questionario
		If lFirst//Para barra de processamento
			dbSelectArea("TU3")
			dbSetOrder(2) //TU3_FILIAL+TU3_CODFAM+TU3_TIPMOD+TU3_NIVSUP+TU3_ORDEM
			dbSeek(xFilial("TU3")+cFamMod+cPai)
			While !Eof() .and. xFilial("TU3")+cFamMod+cPai == TU3->(TU3_FILIAL+TU3_CODFAM+TU3_TIPMOD+TU3_NIVSUP)
				nTotal ++
				dbSelectArea("TU3")
				dbSkip()
			End
		EndIf
		//Carrega array com opcoes
		dbSelectArea("TU3")
		dbSetOrder(2) //TU3_FILIAL+TU3_CODFAM+TU3_TIPMOD+TU3_NIVSUP+TU3_ORDEM
		dbSeek(xFilial("TU3")+cFamMod+cPai)
		If lFirst
			ProcRegua(nTotal)
		EndIf
		While !Eof() .and. xFilial("TU3")+cFamMod+cPai == TU3->(TU3_FILIAL+TU3_CODFAM+TU3_TIPMOD+TU3_NIVSUP)
			If lFirst
				IncProc()
			EndIf
			If TU3->TU3_TIPO != "2"
				If TU3->TU3_TIPO == "1"//Problema
					cPergunta := STR0068 //"Por favor, selecione qual o problema que está ocorrendo:"
					aItens := {NGSEEK("ST8",TU3->TU3_CODOCO,1,"ST8->T8_NOME")}
				Else//servico
					cPergunta := STR0069 //"Abaixo está indicado o tipo de serviço do problema:"
					aItens := {TU3->TU3_CDSERV}
				EndIf
			Else//Pergunta
				cPergunta := AllTrim(Capital(TU3->TU3_PERGUN))
				aItens    := StrTokArr(TU3->TU3_COMBO,';')//Passa opcoes para array
			EndIf
			For i:=1 to Len(aItens)
				If TU3->TU3_TIPO == "1"
					cDescItem := AllTrim(Capital(aItens[i]))
				Else
					cDescItem := AllTrim(aItens[i])
				EndIf
				aAdd(aReturn, Array(__TAM_ARRAY__))
				aReturn[Len(aReturn)][__POS_NIVSUP__] := TU3->TU3_NIVSUP
				aReturn[Len(aReturn)][__POS_CODNIV__] := TU3->TU3_CODNIV
				aReturn[Len(aReturn)][__POS_DESCRI__] := cDescItem
				aReturn[Len(aReturn)][__POS_PERGUN__] := cPergunta
				aReturn[Len(aReturn)][__POS_TIPO__] := TU3->TU3_TIPO
				aReturn[Len(aReturn)][__POS_PERGOP__] := AllTrim(TU3->TU3_PERGOP)
				aReturn[Len(aReturn)][__POS_MARCAD__] := 0
				aReturn[Len(aReturn)][__POS_IMAGEM__] := Nil
				aReturn[Len(aReturn)][__POS_OBJTXT__] := Nil
				aReturn[Len(aReturn)][__POS_PAIANT__] := cAntPai
			Next i
			nRecno := TU3->(Recno())
			aRet := MNT280PERG(TU3->TU3_CODNIV,,TU3->TU3_NIVSUP)
			If Len(aRet) > 0
				For i:=1 to Len(aRet)
					aAdd(aReturn, aRet[i])
				Next
			EndIf
			dbSelectArea("TU3")
			dbSetOrder(2)
			dbGoTo(nRecno)
			dbSkip()
		End

		If Len(aReturn) > 0 .and. lFirst
			//Ordena por item pai
			aSort(aReturn,,,{|x,y| x[__POS_NIVSUP__]+x[__POS_DESCRI__] < y[__POS_NIVSUP__]+y[__POS_DESCRI__]})
			aRet := {}
			For i:=1 to Len(aReturn)
				If cFather != aReturn[i][__POS_NIVSUP__]
					aAdd(aRet, Array(__TAM_ARROPC__))
					aRet[Len(aRet)][__POS_OPCSUP__] := aReturn[i][__POS_NIVSUP__]
					aRet[Len(aRet)][__POS_OPCPER__] := {}
					aRet[Len(aRet)][__POS_OPCTIP__] := aReturn[i][__POS_TIPO__]
					aRet[Len(aRet)][__POS_OPCPAN__] := aReturn[i][__POS_PAIANT__]
					cFather := aReturn[i][__POS_NIVSUP__]
				EndIf
				aAdd(aRet[Len(aRet)][__POS_OPCPER__],aReturn[i])
			Next i
			aReturn := aRet
			aSort(aReturn,,,{|x,y| x[__POS_OPCSUP__] < y[__POS_OPCSUP__]})
		EndIf
	EndIf

Return aReturn

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A280GRVPERºAutor  ³Roger Rodrigues     º Data ³  10/03/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Grava perguntas selecionadas                                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³MNTA280                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A280GRVPER(aOpcoes)
	Local i, j, k, nPos
	Local cOpcoes := ""

	//Verifica quais realmente estao marcados
	If (nPos := aScan(aOpcoes, {|x| x[__POS_OPCSUP__] == "001"})) > 0
		For i:=1 to Len(aOpcoes[nPos,__POS_OPCPER__])
			fCheckMark(aOpcoes[nPos,__POS_OPCPER__,i,__POS_CODNIV__], aOpcoes[nPos,__POS_OPCPER__,i,__POS_MARCAD__])
		Next i
	EndIf

	//Grava opcoes em uma string
	For i:=1 to Len(aOpcoes)
		cOpcoes += aOpcoes[i,__POS_OPCSUP__]+";"+aOpcoes[i,__POS_OPCTIP__]+";"+aOpcoes[i,__POS_OPCPAN__]+";</inf>"
		For j:=1 to Len(aOpcoes[i,__POS_OPCPER__])
			For k:=1 to Len(aOpcoes[i,__POS_OPCPER__,j])
				If ValType(aOpcoes[i,__POS_OPCPER__,j,k]) == "C"
					cOpcoes += AllTrim(aOpcoes[i,__POS_OPCPER__,j,k])+"<c>"
				ElseIf ValType(aOpcoes[i,__POS_OPCPER__,j,k]) == "N"
					cOpcoes += AllTrim(Str(aOpcoes[i,__POS_OPCPER__,j,k]))+"<n>"
				Else
					cOpcoes += "Nil"+"<u>"
				EndIf
				cOpcoes += "</col>"
			Next k
			cOpcoes += "</lin>"
		Next j
		cOpcoes += "</pai>"
	Next i

Return cOpcoes
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fCheckMarkºAutor  ³Roger Rodrigues     º Data ³  15/03/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Verifica se marcacao esta correta                           º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³MNTA280                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fCheckMark(cFather, nMark, cOpc)
	Local i, nPos
	Default cOpc := ""

	//Verifica quais realmente estao marcados
	If (nPos := aScan(aOpcoes, {|x| x[__POS_OPCSUP__] == cFather})) > 0
		For i:=1 to Len(aOpcoes[nPos,__POS_OPCPER__])
			If aOpcoes[nPos,__POS_OPCPER__,i,__POS_TIPO__] == "2" .and. (Empty(cOpc) .or. cOpc $ aOpcoes[nPos,__POS_OPCPER__,i,__POS_PERGOP__])
				If nMark == 0 .or. aOpcoes[nPos,__POS_OPCPER__,i,__POS_MARCAD__] == 0
					aOpcoes[nPos,__POS_OPCPER__,i,__POS_MARCAD__] := 0
					fCheckMark(aOpcoes[nPos,__POS_OPCPER__,i,__POS_CODNIV__], aOpcoes[nPos,__POS_OPCPER__,i,__POS_MARCAD__],;
					Substr(aOpcoes[nPos,__POS_OPCPER__,i,__POS_DESCRI__],1,1))
				ElseIf nMark == 1 .and. aOpcoes[nPos,__POS_OPCPER__,i,__POS_MARCAD__] == 1
					fCheckMark(aOpcoes[nPos,__POS_OPCPER__,i,__POS_CODNIV__], aOpcoes[nPos,__POS_OPCPER__,i,__POS_MARCAD__],;
					Substr(aOpcoes[nPos,__POS_OPCPER__,i,__POS_DESCRI__],1,1))
				EndIf
			EndIf
		Next i
	EndIf

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A280MTRESPºAutor  ³Roger Rodrigues     º Data ³  10/10/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Monta texto com as respostas do questionario                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAMNT                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A280MTRESP(aOpcoes,cMemoDesc,lWeb)
	Local nAt, nAtOpc
	Local cNivPai := "001", cOpcao := ""
	Local cTexto := ""
	Local cCHR := Chr(13)+Chr(10)
	Default cMemoDesc := ""
	Default lWeb := .F.

	If lWeb
		cCHR := "\n"
		cMemoDesc := StrTran(cMemoDesc,Chr(13)+Chr(10),cCHR)
	EndIf

	While (nAt := aScan(aOpcoes, {|x| x[__POS_OPCSUP__] == cNivPai})) > 0
		If (nAtOpc := aScan(aOpcoes[nAt, __POS_OPCPER__], {|x| x[__POS_MARCAD__] > 0 .and. (Empty(cOpcao) .or. cOpcao $ x[__POS_PERGOP__])})) > 0
			If aOpcoes[nAt, __POS_OPCPER__, nAtOpc, __POS_TIPO__] == "3"
				Exit
			Else
				If Empty(cTexto)
					cTexto := STR0070 //"/**Início Questionário"
				EndIf
				cTexto += cCHR+"P: "+Trim(aOpcoes[nAt, __POS_OPCPER__, nAtOpc, __POS_PERGUN__])+cCHR
				cTexto += "R: "+Trim(aOpcoes[nAt, __POS_OPCPER__, nAtOpc, __POS_DESCRI__])
				cNivPai := aOpcoes[nAt, __POS_OPCPER__, nAtOpc, __POS_CODNIV__]
				If aOpcoes[nAt, __POS_OPCPER__, nAtOpc, __POS_TIPO__] == "1"
					cOpcao := ""
				ElseIf aOpcoes[nAt, __POS_OPCPER__, nAtOpc, __POS_TIPO__] == "2"
					cOpcao := Substr(aOpcoes[nAt, __POS_OPCPER__, nAtOpc, __POS_DESCRI__],1,1)
				EndIf
			EndIf
		Else
			Exit
		EndIf
	End

	If !Empty(cTexto)
		cTexto += cCHR+STR0071+"**/" //"Fim Questionário "
	EndIf

	nAt := 0
	If !Empty(cMemoDesc)
		If (nAt := At("/**",cMemoDesc)) > 0 .and. (nAtOpc  := At("**/", cMemoDesc)) > 0
			cTexto := Substr(cMemoDesc,1,nAt-1)+cTexto+Substr(cMemoDesc,nAtOpc+3)
		EndIf
	EndIf
	If nAt == 0 .and. !Empty(cMemoDesc)
		cTexto := Trim(cMemoDesc)+cCHR+cTexto
	EndIf

Return cTexto
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MNT280RESTºAutor  ³Roger Rodrigues     º Data ³  31/05/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Verifica restricao do usuario que esta abrindo a SS         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAMNT                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNT280REST(cCampo)
	Local lRet := .T.
	Local bVal := {||}
	If FindFunction("NGValidTUA") .and. NGCADICBASE(cCampo,"A","TUA",.F.)
		If cCampo == "TUA_INFSER"
			bVal := {|| TUA->TUA_INFSER == "1" .or. Empty(TUA->TUA_INFSER)}
		ElseIf cCampo == "TUA_INCTER"
			bVal := {|| TUA->TUA_INCTER != "1"}
		ElseIf cCampo == "TUA_INFTPS"
			bVal := {|| TUA->TUA_INFTPS == "1" .or. Empty(TUA->TUA_INFTPS)}
		ElseIf cCampo == "TUA_VISBEM"
			bVal := {|| TUA->TUA_VISBEM == "1" .or. Empty(TUA->TUA_VISBEM)}
		ElseIf cCampo == "TUA_TRANSS"
			bVal := {|| TUA->TUA_TRANSS == "1" .or. Empty(TUA->TUA_TRANSS)}
		ElseIf cCampo == "TUA_CANCSS"
			bVal := {|| TUA->TUA_CANCSS == "1" .or. Empty(TUA->TUA_CANCSS)}
		ElseIf cCampo == "TUA_FECHSS"
			bVal := {|| TUA->TUA_FECHSS == "1" .or. Empty(TUA->TUA_FECHSS)}
		ElseIf cCampo == "TUA_DISTSS"
			bVal := {|| TUA->TUA_DISTSS == "1" .or. Empty(TUA->TUA_DISTSS)}
		EndIf
		lRet := NGValidTUA(,bVal)
	EndIf
Return lRet

//------------------------------------------------------------------------------
/*/{Protheus.doc} MNT280WHEN
When dos campos da tela

@author Roger Rodrigues
@since 04/06/11
@return logic, se campo estará aberto para edição

/*/
//------------------------------------------------------------------------------
Function MNT280WHEN( cCampo, lWeb )

	Local lRet      := .T.
	Local lFacilit  := MNTINTFAC()
	Local lLanex    := .F.
	Local lHasCount := .F.

	Default lWeb := .F.

	If cCampo == "TQB_CDSOLI"
		If lWeb .or. lLeabre
			lRet := !MNT280REST("TUA_INCTER")//Verifica se permite inclusao da SS para outra pessoa
		Else
			lRet := .F.
		EndIf
	ElseIf cCampo == "TQB_TPSERV"
		If lWeb .or. lLeabre
			lRet := MNT280REST("TUA_INFTPS")//Verifica se permite informar tipo de servico
		ElseIf Type("lLeFecha") == "L"
			If !lLeFecha
				lRet := Empty(TQB->TQB_CDSERV)
			EndIf
		EndIf
	ElseIf cCampo == "TQB_CODBEM"
		lRet := lLeabre .And. M->TQB_SOLUCA <> "D" .And. (!IsInCallStack('MNTA902') .And. !IsInCallStack('MNTA907'))
	ElseIf cCampo == "TQB_RAMAL" .And. !Empty(TQB->TQB_RAMAL)
		lRet := lLeabre
	ElseIf cCampo == "TQB_DESCSS"
		lRet := lWeb .or. lDigServ
	ElseIf cCampo == "TQB_CDSERV"
		If lFacilit
			If lWeb .or. lLeabre
				lRet := MNT280REST("TUA_INFSER")//Verifica se permite informar servico
			ElseIf Type("lLeFecha") == "L"
				If !lLeFecha
					lRet := Empty(TQB->TQB_CDSERV)
				EndIf
			EndIf
		Else
			lRet := lWeb .or. lDigServ
		EndIf
	ElseIf cCampo == "TQB_FUNEXE"
		lRet := lSSClassi
	ElseIf cCampo == "TQB_DTFECH"
		lRet := lLefecha
	ElseIf cCampo == "TQB_HOFECH"
		lRet := lLefecha
	ElseIf cCampo == "TQB_TEMPO"
		lRet := lLefecha
	ElseIf cCampo == "TQB_DESCSO"
		lRet := lLefecha
	ElseIf cCampo == "TQB_CDEXEC"
		lRet := lSSClassi
	ElseIf cCampo == "TQB_PRIORI"
		lRet := lSSPriori
	ElseIf cCampo == "TQB_POSCON"

		//--------------------------------------------------------------------------------------------------------
		// Descrição do campo: Posição do contador na inclusão da Solicitação de Serviço.
		// Campo fica aberto somente na inclusão da SS
		//---------------------------------------------------------------------------------------------------------
		lHasCount := Type("TIPOACOM") == "L" .And. TIPOACOM
		lLanex    := IIf( FindFunction('NGUSELANEX'), NGUSELANEX( M->TQB_CODBEM ) , ;
						AllTrim( SuperGetMv( 'MV_NGLANEX', .F., '' ) )  ) == "A"
		lRet      := Inclui .And. lHasCount .And. !lLanex

		If Inclui .And. lLanex .And. lHasCount
			// Carrega contador automaticamente somente somente quando é lanex pois o campo sempre ficará fechado
			M->TQB_POSCON := NGTpCont( M->TQB_CODBEM, M->TQB_DTABER, M->TQB_HOABER )
		EndIf

	ElseIf cCampo == "TQB_POSCO2"
		lRet := Type("TIPOACOM2") == "L" .and. TIPOACOM2 .And. Inclui
	EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} MNT280VAL
Valida os campos da tela
@author  Roger Rodrigues
@since   03/10/11
@version p12
/*/
//-------------------------------------------------------------------
Function MNT280VAL(cCampo,lWeb)

	Local lRet  := .T.
	Local cMsg  := ""
	Local xVar  := ""
	Local aArea := GetArea()
	// Release menor ou igual a 17
	Local lRPORel17 := IIf(GetRPORelease() <= '12.1.017', .T., .F.)

	Default cCampo := ReadVar()
	Default lWeb   := .F.

	xVar := &(cCampo)
	If cCampo == "M->TQB_CDSOLI"
		// Posiciona no usuario
		PswOrder(1)
		If PswSeek(xVar,.T.)
			M->TQB_CDSOLI := Substr(PswRet(1)[1,1], 1, 6)
			M->TQB_NMSOLI := Substr(PswRet(1)[1,4], 1, 50)
			M->TQB_RAMAL  := PswRet(1)[1,20]
		Else
			lRet := .F.
			If lWeb
				cMsg := STR0072 // "Não existe usuário relacionado à este código."
			Else
				Help(" ",1,"REGNOIS")
			EndIf
		EndIf
	ElseIf cCampo == "M->TQB_CDSERV"
		If !lRPORel17
			If !Empty(M->TQB_CDSERV)
				If ExistCpo("TQ3",xVar)
					dbSelectArea("TQ3")
					dbSetOrder(1)
					If dbSeek(xFilial("TQ3")+xVar)
						
						PswOrder(2)
						
						If PswSeek(TQ3->TQ3_CDRESP)
							M->TQB_NOMFUN := Padr(SubStr(UsrFullName(PswRet(1)[1][1]), 1, 40), 40)
						EndIf
						
					EndIf

				Else
					lRet := .F.
				EndIf
			EndIF
		Else
			If ExistCpo("TQ3",xVar)
				dbSelectArea("TQ3")
				dbSetOrder(1)
				If dbSeek(xFilial("TQ3")+xVar)
					
					PswOrder(2)
					
					If PswSeek(TQ3->TQ3_CDRESP)
						M->TQB_NOMFUN := Padr(SubStr(UsrFullName(PswRet(1)[1][1]), 1, 40), 40)
					EndIf
					
				EndIf

			Else
				lRet := .F.
			EndIf
		EndIf
	ElseIf cCampo == "M->TQB_TEMPO"
		lRet := MNTA290TEM(xVar)
	ElseIf cCampo == "M->TQB_DTFECH"
		lRet := MNTA290DTF(xVar)
		If lRet .and. !Empty(StrTran(M->TQB_HOFECH,":"," "))
			lRet := MNTA290HOF(1)
		EndIf
	ElseIf cCampo == "M->TQB_HOFECH"
		lRet := MNTA290HOF()
	EndIf

	RestArea(aArea)

Return If(lWeb, cMsg, lRet)

//-------------------------------------------------------------------
/*/{Protheus.doc} MNT280REL
Relacao dos campos da tela

@author  Roger Rodrigues
@since   18/10/11
@version p12

@param cCampo, Caractere, Campo para relação
@param lIniBrw, Lógico,

/*/
//-------------------------------------------------------------------
Function MNT280REL(cCampo,lIniBrw)
	
	Local nPos
	Local cTipo     := ""
	Local cCodigo   := ""
	Local cTipoSS   := ""
	Local cCodResp  := ''
	Local xRetorno  := Space(TAMSX3(cCampo)[1])
	Local cFilOld   := cFilAnt
	Local aArea     := GetArea()
	Local lLeCancel := .F.
	Local lRPORel17 := IIf(GetRPORelease() <= '12.1.017', .T., .F.) // Release menor ou igual a 17
	Local aUserInf  := {}
	Local cInfoUser := ''
	Local nTamTAF   := FWTamSX3( 'TAF_CODNIV' )[1]

	Default lIniBrw := .F.

	If Type("INCLUI") = 'U'
		INCLUI := .F.  // Considera que não é inclusão quando variavel igual a nula
	EndIf

	If cCampo == "TQB_POSCON"

		xRetorno := IIF( ISINCALLSTACK('MNTA291'),'',NGTpCont(M->TQB_CODBEM, M->TQB_DTABER, M->TQB_HOABER) )

	ElseIf cCampo == "TQB_DESCSS"

		If Inclui .and. Type("c280MemoDg") == "C" .and. Type("cTipoSS280") == "C" .And. Type("cCodBem280") == "C" .and. !Empty(c280MemoDg)//Chamadas externas
			xRetorno := A280MTRESP(MNT280PERG("001",.T.,,c280MemoDg))
		ElseIf !Inclui
			xRetorno := MSMM(TQB->TQB_CODMSS,80)
		Else
			xRetorno  := ''
		EndIf

	ElseIf cCampo == "TQB_NOMBEM"

		If lIniBrw .or. Type("M->TQB_CODBEM") != "C"
			cCodigo := TQB->TQB_CODBEM
			cTipoSS := TQB->TQB_TIPOSS
			cCodFil := TQB->TQB_FILIAL
		Else
			cCodigo := M->TQB_CODBEM
			cTipoSS := M->TQB_TIPOSS
			cCodFil := ""
		EndIf

		If !Empty( cCodigo )

			If cTipoSS == 'B'

				xRetorno := Posicione( 'ST9', 1, IIf( !Empty( cCodFil ), xFilial('ST9', cCodFil), xFilial('ST9')) + cCodigo, "T9_NOME")
			
			Else
			
				xRetorno := Posicione( 'TAF', 7, IIf( !Empty( cCodFil ), cCodFil, FWxFilial( 'TAF' ) ) +;
					'X2' + SubStr( cCodigo, 1, nTamTAF ) , 'TAF_NOMNIV' )
			
			EndIf
		
		EndIf

	ElseIf cCampo == "TQB_NOMCUS"

		If !lRPORel17
			If lIniBrw .or. Type("M->TQB_CCUSTO") != "C" .And. !Empty(TQB->TQB_CDSERV)
				cCodigo := TQB->TQB_CCUSTO
				cCodFil := TQB->TQB_FILIAL
			Else
				cCodigo := M->TQB_CCUSTO
				cCodFil := ""
			EndIf
		Else
			If lIniBrw .or. Type("M->TQB_CCUSTO") != "C"
				cCodigo := TQB->TQB_CCUSTO
				cCodFil := TQB->TQB_FILIAL
			Else
				cCodigo := M->TQB_CCUSTO
				cCodFil := ""
			EndIf
		EndIf

		If !Empty(cCodigo)
			xRetorno := Posicione( 'CTT', 1, IIf( !Empty( cCodFil ), cCodFil, xFilial('CTT')) + cCodigo, 'CTT_DESC01' )
		EndIf

	ElseIf cCampo == "TQB_NOMCTR"

		If lIniBrw .or. Type("M->TQB_CENTRA") != "C"
			cCodigo := TQB->TQB_CENTRA
		Else
			cCodigo := M->TQB_CENTRA
		EndIf
		If !Empty(cCodigo)
			xRetorno := Posicione( 'SHB', 1, xFilial( 'SHB' ) + cCodigo, 'HB_NOME' )
		EndIf

	ElseIf cCampo == "TQB_RAMAL"

		If Inclui
			cCodigo := RetCodUsr()
			PswOrder(1)
			If PswSeek(cCodigo,.T.) .And. !Empty( ( cInfoUser := PswRet(1)[1,20] ) )
				xRetorno := Padr( cInfoUser, Len( xRetorno ) )
			EndIf
		Else
			xRetorno := TQB->TQB_RAMAL
		EndIf

	ElseIf cCampo == "TQB_DESCSS"

		If !Inclui
			xRetorno := MSMM(TQB->TQB_CODMSS,80)
		EndIf

	ElseIf cCampo == "TQB_DESCSO"

		If !Inclui
			If !Empty(TQB->TQB_CODMSO)
				xRetorno := MSMM(TQB->TQB_CODMSO,80)
			ElseIf Type("a280Relac") == "A" .and. (nPos := aScan(a280Relac,{|x| x[1] == cCampo})) > 0
				xRetorno := &(a280Relac[nPos][2])
			EndIf
		EndIf

	ElseIf cCampo == "TQB_NOMFUN"

		If !Inclui .and. !Empty(TQB->TQB_CDSERV)

			// Retorna nome do usuário
			aUserInf := {}
			cCodResp := Posicione('TQ3', 1, xFilial( 'TQ3' ) + TQB->TQB_CDSERV, 'TQ3_CDRESP')
			If Type( 'oHashResp' ) == 'O' .And. HmGet( oHashResp, cCodResp , @aUserInf )
				xRetorno := aUserInf[ 1, 2 ]
			Else
				aUserInf := FWSFLoadUser( cCodResp,,,1 ) // Quarto parâmetro busca pelo Id do usuário
				If Len(aUserInf) > 0
					xRetorno := aUserInf[ 4 ]
					If Type( 'oHashResp' ) == 'O'
						HmAdd( oHashResp, { cCodResp, aUserInf[ 4 ], aUserInf[ 5 ] } )
					EndIf
				EndIf
			EndIf
		EndIf

	ElseIf cCampo == "TQB_CDSOLI"

		If Inclui
			xRetorno := RetCodUsr()
		Else
			xRetorno := TQB->TQB_CDSOLI
		EndIf

	ElseIf cCampo == "TQB_NMSOLI"

		If Inclui
			xRetorno := UsrFullName(RetCodUsr())
		Else
			If Type('oHashSol') == 'O'
				If HmGet( oHashSol, TQB->TQB_CDSOLI, @aUserInf )
					xRetorno := aUserInf[ 1, 2 ]
				EndIf
			Else
				xRetorno := UsrFullName(TQB->TQB_CDSOLI)
			EndIf
		EndIf

	ElseIf cCampo == "TQB_EMSOLI"

		If Inclui
			aUserInf := FWSFALLUSERS({RetCodUsr()}, { 'USR_EMAIL' })
			If Len(aUserInf) > 0
				xRetorno := aUserInf[1,3]
			EndIF
		Else
			If Type('oHashSol') == 'O'
				If HmGet( oHashSol, TQB->TQB_CDSOLI, @aUserInf )
					xRetorno := aUserInf[ 1, 3 ]
				EndIf
			Else
				aUserInf := FWSFLoadUser( TQB->TQB_CDSOLI,,,3 ) // Quarto parâmetro busca pelo Id do usuário
				If Len(aUserInf) > 0
					xRetorno := aUserInf[5]
				EndIf
			EndIf
		EndIf

	ElseIf cCampo == "TQB_DTCANC"

		If !lRPORel17 .And. lLeCancel
			xRetorno := dDataBase
		Else
			xRetorno := cToD("")
		EndIf

	ElseIf cCampo == "TQB_HRCANC"

		If lLeCancel
			xRetorno := SubStr(Time(),1,5)
		EndIf

	ElseIf cCampo == "TQB_NMSERV"

		cCodigo  := TQB->TQB_CDSERV
		xRetorno := Posicione("TQ3", 1, IIf( !Empty( TQB->TQB_FILIAL ), TQB->TQB_FILIAL, xFilial('TQ3')) + TQB->TQB_CDSERV, 'TQ3_NMSERV' )

	ElseIf cCampo == "TUM_NOMUSU"

		If INCLUI .And. !lIniBrw
			xRetorno := UsrFullName(RetCodUsr())
		Else
			xRetorno := If(!Empty(TUM->TUM_USUARI), UsrFullName(TUM->TUM_USUARI), Space(TAMSX3("TUM_NOMUSU")[1]))
		EndIf

	ElseIf cCampo == "TUR_DESATE"

		If lIniBrw .or. Type("M->TUR_FILATE") != "C"
			cFilAnt := TUR->TUR_FILATE
			cTipo   := TUR->TUR_TIPO
			cCodigo := TUR->TUR_CODATE
		Else
			cFilAnt := M->TUR_FILATE
			cTipo   := M->TUR_TIPO
			cCodigo := M->TUR_CODATE
		EndIf
		If cTipo == "1"//Equipe
			xRetorno := NGSEEK("TP4",Substr(cCodigo,1,TAMSX3("TP4_CODIGO")[1]),1,"TP4->TP4_DESCRI")
		ElseIf cTipo == "2"//Atendente
			xRetorno := NGSEEK("ST1",Substr(cCodigo,1,TAMSX3("T1_CODFUNC")[1]),1,"ST1->T1_NOME")
		ElseIf cTipo == "3"//Fornecedor
			xRetorno := NGSEEK("SA2",Substr(cCodigo,1,TAMSX3("A2_COD")[1]),1,"SA2->A2_NOME")
		EndIf
		cFilAnt := cFilOld

	ElseIf cCampo == "TUM_NOMATE"

		If !lIniBrw .And. INCLUI .And. !Empty( M->TUM_ATENDE )
			xRetorno := NGSEEK("ST1", M->TUM_ATENDE, 1, "T1_NOME", M->TUM_FILATE)
		ElseIf !Empty( TUM->TUM_ATENDE ) .And. AllTrim( TUM->TUM_CODFOL ) == '04' .And.;
		NGSEEK( 'TUR', TUM->TUM_SOLICI, 1, 'TUR_TIPO', TUM->TUM_FILATE ) == '1'
			xRetorno := NGSEEK( 'TP4', TUM->TUM_ATENDE, 1, 'TP4->TP4_DESCRI', TUM->TUM_FILATE )
		ElseIf lIniBrw .Or. !INCLUI
			xRetorno := NGSEEK("ST1", TUM->TUM_ATENDE, 1, "T1_NOME", TUM->TUM_FILATE)	
		Else
			xRetorno := Space( TAMSX3("TUM_NOMATE")[1] )
		EndIf

	EndIf

	RestArea(aArea)

Return xRetorno

//-------------------------------------------------------------------
/*/{Protheus.doc} MNT280FBEM
Verifica se o usuario pode visualizar todos os bens e filtra de acordo

@author  Roger Rodrigues
@since   28/06/11
@version P12
@obs usado em MNTA280/MNTI005
/*/
//-------------------------------------------------------------------
Function MNT280FBEM(cCodBem)
	Local lRet := .T.
	Local aArea:= GetArea()
	Default cCodBem := ""

	lRet := MNT280REST("TUA_VISBEM")//Verifica se tem restricao para ver todos os bens
	//Se nao tiver permissao, filtra o bem
	If !Empty(cCodBem) .and. !lRet
		dbSelectArea("TAF")
		dbSetOrder(6)
		lRet := dbSeek(xFilial("TAF")+"X"+"1"+cCodBem)
	EndIf

	RestArea(aArea)
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTUPDFAC
Verifica se o update do facilities esta executado

@author  Roger Rodrigues
@since   04/08/11
@version P12
/*/
//-------------------------------------------------------------------
Function MNTUPDFAC(lShowMsg)

	Default lShowMsg := .T.

	If !MNTINTFAC()
		If lShowMsg
			ShowHelpDlg(STR0055, {STR0073},1,{STR0074}) //"ATENÇÃO" ## "O sistema não está utilizando o Módulo Facilities." ## "Favor ativar o parâmetro MV_NG1FAC."
		EndIf
		Return .F.
	EndIf

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MNTINTFAC
Verifica se o Novo Facilities eh utilizado

@author  Roger Rodrigues
@since   28/03/12
@version P12
/*/
//-------------------------------------------------------------------
Function MNTINTFAC()
Return AllTrim(SuperGetMv("MV_NG1FAC",.F.,"2")) == "1"

//-------------------------------------------------------------------
/*/{Protheus.doc} MNT280CPO
Inicializa a280Choice e variaveis dos campos da tela de acordo com
modo de alteração

@author  Roger Rodrigues
@since   12/03/12
@version P12
/*/
//-------------------------------------------------------------------
Function MNT280CPO(nModo, nOpcx)

	Local i, nPos
	Local cRelac
	Local aCamposN2 := {}, aCamposS := {}, aNgButton := {}
	Local aRelac2
	Local lUsaMultOS := AllTrim(GetNewPar("MV_NGMULOS","N")) == "S"
	Local lSigaloc := SuperGetMV( 'MV_NG1LOC', .F., .F. ) // valida se integra com o módulo SIGALOC

	//Define variaveis de campos da tela
	_SetOwnerPrvt("Visual"  , (nOpcx == 2))
	_SetOwnerPrvt("Inclui"  , (nOpcx == 3))
	_SetOwnerPrvt("Altera"  , (nOpcx == 4))
	_SetOwnerPrvt("Exclui"  , (nOpcx == 5))
	_SetOwnerPrvt("lFacilit", MNTINTFAC())

	//Declara variaveis Inclui, Altera, Visual, Exclui
	aRotSetOpc("TQB",TQB->(Recno()),nOpcx)

	//Variavies de configuracao de campos
	_SetOwnerPrvt("a280Choice"   , {})
	_SetOwnerPrvt("a280Memos"    , {})
	_SetOwnerPrvt("a280Relac"    , {})
	_SetOwnerPrvt("a280Buttons" , {})

	//Campos de When e Filtro da Tela
	_SetOwnerPrvt("lLeabre"  , .F.)
	_SetOwnerPrvt("lLefecha" , .F.)
	_SetOwnerPrvt("lLeCancel", .F.)
	_SetOwnerPrvt("lSSPriori", .F.)
	_SetOwnerPrvt("lSSClassi", .F.)
	_SetOwnerPrvt("lDigServ" , .F.)

	//-----------------------
	//Variaveis de Contador
	//-----------------------
	_SetOwnerPrvt("lSH1"     , .F.)
	If Inclui
		// Tratamento para não sobrescrever atribuição da variável caso seja definida anteriormente
		_SetOwnerPrvt("TIPOACOM" , Type("TIPOACOM") == "L" .And. TIPOACOM )
		_SetOwnerPrvt("TIPOACOM2", Type("TIPOACOM2") == "L" .And. TIPOACOM2 )
	Else
		// Em qualquer processo diferente de inclusão as variável são definidas .F. para
		// não permitir editar os campos de contador
		_SetOwnerPrvt("TIPOACOM" , .F. )
		_SetOwnerPrvt("TIPOACOM2", .F.)
	EndIf

	//Variaveis do F11
	_SetOwnerPrvt("cARQUISAI" , "TQB")

	//Variaveis de relacao
	_SetOwnerPrvt("c280MemoDg", If(lFacilit .and. nOpcx != 3, MSMM(TQB->TQB_MEMODG), ""))

	//Define campos da tela e conteudo
	If nModo == 1 //Inclusao - Semelhante ao MNTA280
		If IsInCallStack("MNTC286")
			aCamposN := {"TQB_FUNEXE","TQB_DTFECH","TQB_HOFECH","TQB_TEMPO","TQB_ORDEM","TQB_USUARI","TQB_PSAP","TQB_PSAN",;
			"TQB_PRIORI", "TQB_CDEXEC", "TQB_NMEXEC","TQB_OBSPRA","TQB_OBSATE"}
		Else
			aCamposN := {"TQB_FUNEXE","TQB_DTFECH","TQB_HOFECH","TQB_TEMPO","TQB_DESCSO","TQB_ORDEM","TQB_USUARI","TQB_PSAP","TQB_PSAN",;
			"TQB_PRIORI", "TQB_CDEXEC", "TQB_NMEXEC","TQB_OBSPRA","TQB_OBSATE"}
		EndIf
		If !lFacilit
			aAdd(aCamposN, "TQB_TPSERV")
			aAdd(aCamposN, "TQB_DTCANC")
			aAdd(aCamposN, "TQB_HRCANC")
			aAdd(aCamposN, "TQB_MECANC")
		EndIf
		//Campos que nao irao aparecer em tela
		If ExistBlock("MNTA2803")
			aCamposN2 := ExecBlock("MNTA2803",.F.,.F.)
		EndIf
		//Campos de deverao aparecer em tela
		If ExistBlock("MNTA280A")
			aCamposS := ExecBlock("MNTA280A",.F.,.F.)
		EndIf

		SetKey(VK_F11,{||NGTAFMNT("M->TQB_CODBEM", .T.)})

		If Type("cTipoSS280") == "C" .And. Type("cCodBem280") == "C" .and. nOpcx == 3
			a280Relac := {	{"TQB_CODBEM","'"+cCodBem280+"'"} ,;
			{"TQB_TIPOSS","'"+cTipoSS280+"'"} ,;
			{"lTemp",'&(X3Valid("TQB_CODBEM")) .And. VldUser("TQB_CODBEM")'}}
			If lFacilit
				aAdd(a280Relac, {"TQB_DESCSS","'c280MemoDg'"})
			EndIf
		EndIf

		//Ponto de Entrada para inclusao de botoes na enchoice
		If ExistBlock("MNTA2809")
			aNgButton := ExecBlock( 'MNTA2809', .F., .F., { nOpcx } )
			If ValType(aNgButton) == "A"
				a280Buttons := aClone(aNgButton)
			EndIf
		EndIf
		lLeabre := .T.
		If Inclui .or. TQB->TQB_SOLUCA == "A"
			lDigServ  := .T.
			lSSClassi := .T.
		EndIF

	ElseIf nModo == 2//Distribuicao - Semelhante ao MNTA295
		aCamposN := {"TQB_FUNEXE", "TQB_PSAP", "TQB_PSAN", "TQB_DTFECH", "TQB_HOFECH", "TQB_TEMPO", "TQB_DESCSO", "TQB_OBSPRA","TQB_OBSATE"}

		If !lFacilit
			aAdd(aCamposN, "TQB_TPSERV")
			aAdd(aCamposN, "TQB_DTCANC")
			aAdd(aCamposN, "TQB_HRCANC")
			aAdd(aCamposN, "TQB_MECANC")
		EndIf

		If lUsaMultOS
			aAdd(aCamposN, "TQB_ORDEM")
		EndIf

		//Campos que nao irao aparecer em tela
		If ExistBlock("MNTA2957")
			aCamposN2 := ExecBlock("MNTA2957",.F.,.F.)
		EndIf

		lDigServ  := .T.
		lLEFECHA  := .F.
		lSSClassi := .T.
		lSSPriori := .T.

	ElseIf nModo == 3 .Or. nModo == 5 //Fechamento ou Cancelamento - Semelhante ao MNTA290
		If nOpcx <> 5
			aCamposN := {"TQB_FUNEXE", "TQB_PSAP", "TQB_PSAN", "TQB_USUARI", "TQB_OBSPRA", "TQB_OBSATE", "TQB_ORDEM"}
			If !lFacilit .And. nModo != 5
				aAdd(aCamposN, "TQB_TPSERV")
				aAdd(aCamposN, "TQB_DTCANC")
				aAdd(aCamposN, "TQB_HRCANC")
				aAdd(aCamposN, "TQB_MECANC")
			EndIf

			If nModo == 3
				lLefecha := .T.
			ElseIf nModo == 5
				lLeCancel := .T.

				aAdd(aCamposN, "TQB_TEMPO")
				aAdd(aCamposN, "TQB_DTFECH")
				aAdd(aCamposN, "TQB_HOFECH")
				aAdd(aCamposN, "TQB_DESCSO")

				aAdd(a280Relac, {"TQB_DTCANC", 'dDataBase'})
				aAdd(a280Relac, {"TQB_HRCANC", 'SubStr(Time(),1,5)'})
			EndIf
			lLefecha := .T.
			
			If X3Obrigat('TQB_PRIORI') .And. Empty(TQB->TQB_PRIORI)
				lSSPriori := .T.
			EndIf

			If !Empty(TQB->TQB_ORDEM)
				If NGCADICBASE ("TJ_MMSYP","A","STJ",.F.)
					If ExistBlock("MNTA280G") //Ponto de Entrada para alterar a observação da Solução da SS para mensagem da STL.
						cRelac := ExecBlock("MNTA280G",.F.,.F.)
					Else
						cRelac := 'NGSEEK("STJ",TQB->TQB_ORDEM,1,"NGMEMOSYP(STJ->TJ_MMSYP)")'
					EndIf
				Else
					If ExistBlock("MNTA280G")
						cRelac := ExecBlock("MNTA280G",.F.,.F.) //Ponto de Entrada para alterar a observação da Solução da SS para mensagem da STL.
					Else
						cRelac := 'NGSEEK("STJ",TQB->TQB_ORDEM,1,"TJ_OBSERVA")'
					EndIf
				EndIf
				aAdd(a280Relac, {"TQB_DESCSO",cRelac})
			EndIf
		Else
			aCamposN := {"TQB_PSAP", "TQB_PSAN", "TQB_DTFECH", "TQB_HOFECH", "TQB_TEMPO", "TQB_DESCSO", "TQB_OBSPRA", "TQB_OBSATE"}
		EndIf

		If nModo == 3
			//Retorna campos com relacao
			If ExistBlock("MNTA290A")
				aRelac2 := ExecBlock("MNTA290A",.F.,.F.)
				If Valtype(aRelac2) == "A"
					For i := 1 To Len(aRelac2)
						aAdd(a280Relac,aRelac2[i])
					Next i
				EndIf
			EndIf
		EndIf

		//Campos que nao irao aparecer em tela
		If ExistBlock("MNTA2902")
			aCamposN2 := ExecBlock("MNTA2902",.F.,.F.)
		EndIf

	ElseIf nModo == 4 //Satisfacao - Semelhante ao MNTA305
		aCamposN := {"TQB_FUNEXE"}
		If lFacilit
			aAdd(aCamposN, "TQB_PSAP")
			aAdd(aCamposN, "TQB_PSAN")
			aAdd(aCamposN, "TQB_OBSPRA")
			aAdd(aCamposN, "TQB_OBSATE")
		Else
			aAdd(aCamposN, "TQB_TPSERV")
			aAdd(aCamposN, "TQB_DTCANC")
			aAdd(aCamposN, "TQB_HRCANC")
			aAdd(aCamposN, "TQB_MECANC")
		EndIf
		If lUsaMultOS
			aAdd(aCamposN, "TQB_ORDEM")
		EndIf

	EndIf

	//Caso a integração com o SIGALOC estiver desabilitada os campos não são exibidos.
	If !lSigaloc .and. TQB->(fieldpos("TQB_PROJET")) > 0 .and. TQB->(fieldpos("TQB_OBRA")) > 0 .and. TQB->(fieldpos("TQB_AS")) > 0
		aadd(aCamposN,"TQB_PROJET")
		aadd(aCamposN,"TQB_OBRA")
		aadd(aCamposN,"TQB_AS")
	EndIf

	// Se o campo TEMPO da S.S. estiver visível, carrega o Relação
	If aScan( aCamposN, { | x | AllTrim( x ) == 'TQB_TEMPO' } ) == 0 .and. lFacilit
		
		cRelac := Posicione( 'SX3', 2, 'TQB_TEMPO', 'X3_RELACAO' )
		
		If Empty( cRelac )
		
			aAdd( a280Relac, { 'TQB_TEMPO', 'A291HrsTot(, TQB->TQB_SOLICI)' } )
		
		Else

			aAdd( a280Relac, { 'TQB_TEMPO', cRelac } )

		EndIf
	
	EndIf

	// SEMPRE quando o Modo for DIFERENTE de CANCELAMENTO, esconde os campos de Cancelamento
	If nModo <> 5 .And. TQB->TQB_SOLUCA != 'C'
		aAdd(aCamposN, "TQB_DTCANC")
		aAdd(aCamposN, "TQB_HRCANC")
		aAdd(aCamposN, "TQB_MECANC")
	EndIf

	//Adiciona botao de Questionário de Sintomas
	If lFacilit .and. nOpcx != 5
		aAdd(a280Buttons,{"ng_ico_questionario2", {||MNT280DIAG(Inclui, Altera, @c280MemoDg)}, STR0075, STR0075}) //"Q. Sintomas"
	EndIf

	//Se utilizar Facilities, nao apresentar estes campos
	If lFacilit
		aAdd(aCamposN, "TQB_NOMFUN")
		aAdd(aCamposN, "TQB_CDEXEC")
		aAdd(aCamposN, "TQB_NMEXEC")
	Else
		//Se não utilizar Facilities deverá remover estes campos da tela
		aAdd(aCamposN, "TQB_LOCALI")
		aAdd(aCamposN, "TQB_NOMLOC")
	EndIf
	//Adiciona campos do usuario no array de campos na mostrados
	If (Type("aCamposN2") == "A") .and. !Empty(aCamposN2) .and. Len(aCamposN2) > 0
		For i := 1 To Len(aCamposN2)
			If aScan(aCamposN, {|x| x == aCamposN2[i]}) == 0
				aAdd(aCamposN, aCamposN2[i])
			EndIf
		Next i
	EndIf
	//Retira campos do usuario do array de campos nao mostrados
	For i := 1 To Len(aCamposS)
		If (nPos := aScan(aCamposN, {|x| x == aCamposS[i]}) ) > 0
			aDel(aCamposN, nPos)
			aSize(aCamposN, Len(aCamposN)-1)
		EndIf
	Next i

	a280Choice  := NGCAMPNSX3("TQB",aCamposN)
	If Len(aCamposN2) > 0
		aAdd(a280Choice,"NOUSER")
	EndIf

	aAdd(a280Memos, {"TQB_CODMSS","TQB_DESCSS"})
	aAdd(a280Memos, {"TQB_CODMSO","TQB_DESCSO"})
	aAdd(a280Memos, {"TQB_CDCANC","TQB_MECANC"})

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MNT280REG
Carrega as variaveis da tabela

@author  Roger Rodrigues
@since   13/04/12
@version P12

@param nOpcx, numérico, opção escollhida (Inclusão, Alteração, Exclusão, Visualização)
@param aRelacao, array, relação dos campos
@param aMemos, array, campos tipo Memo
@param [l280Reg], lógico, .T. quando chamado pela rotina mnta280 e .F. para as demais rotinas
/*/
//-------------------------------------------------------------------
Function MNT280REG(nOpcx, aRelacao, aMemos, l280Reg)

	Local cRelac    := ""
	Local cCmpo 	:= ""
	Local xConteudo := ""
	Local nTamTot	:= 0
	Local cCampo	:= ""
	Local cContext	:= ""
	Local nTamanho	:= ""
	Local cTipo		:= ""
	Local nInd		:= 0
	Local aHeadTQB	:= {}
	Local cCmpo2    := ""

	Default nOpcx 		:= 0 // Carrega registro em branco
	Default aRelacao 	:= {}
	Default aMemos 		:= {}
	Default l280Reg     := .F.

	aHeadTQB := NGHeader("TQB")
	nTamTot  := Len(aHeadTQB)

	For nInd := 1 To nTamTot
		cCampo      := aHeadTQB[nInd,2]
		cContext 	:= aHeadTQB[nInd,10]
		nTamanho 	:= aHeadTQB[nInd,4]
		cTipo 		:= aHeadTQB[nInd,8]

		If cCampo == 'TQB_SOLICI' .And. l280Reg // Evita que o número da solicitação seja incrementado mais
			Loop                                // de uma vez, devido a chamada da função RegToMemory chamada na
		EndIf                                   // inclusão de um solicitação de serviço

		If nOpcx > 0
			If cContext == "V" .Or. nOpcx == 3
				cCmpo     := "M->"+Trim(cCampo)
				xConteudo := &cCmpo.
				If ValType(xConteudo) != "U"
					&("M->"+Trim(cCampo)) := CriaVar(Trim(cCampo),.T.)
				Else
					_SetOwnerPrvt(Trim(cCampo),CriaVar(Trim(cCampo),.T.))
				EndIf
			Else
				cCmpo := ("TQB->"+Trim(cCampo))
				cCmpo2 := "M->"+Trim(cCampo)
				If ValType(&cCmpo2) != "U"
					&("M->"+Trim(cCampo))   := &cCmpo
				Else
					_SetOwnerPrvt(Trim(cCampo),&cCmpo)
				EndIf
			EndIf
		Else
			If cTipo == "C"
				cCpo := Space(nTamanho)
			ElseIf cTipo == "N"
				cCpo := 0
			ElseIf cTipo == "D"
				cCpo := STOD("")
			ElseIf cTipo == "L"
				cCpo := .F.
			ElseIf cTipo == "M"
				cCpo := ""
			EndIf
			cCmpo     := "M->"+Trim(cCampo)
			xConteudo := &cCmpo.
			If ValType(xConteudo) != "U"
				&("M->"+Trim(cCampo)) := cCpo
			Else
				_SetOwnerPrvt(Trim(cCampo),cCpo)
			EndIf
		EndIf
	Next nInd

	If nOpcx > 0
		For nInd := 1 To Len(aRelacao)
			cCampo   := "M->" + aRelacao[nInd][1]
			cRelac   := aRelacao[nInd][2]
			&cCampo. := &cRelac
		Next nInd

		For nInd := 1 To Len(aMemos)
			cCampo  := "M->" + aMemos[nInd][2]
			&cCampo := ""
			If ExistIni(aMemos[nInd][2])
				aRelacao := Posicione("SX3",2,aMemos[nInd][2],"X3_RELACAO")
				&cCampo  := InitPad(aRelacao)
			EndIf
		Next nInd
	EndIf

Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} MNT280GRV
Realiza gravacao da tabela

@author  Roger Rodrigues
@since   16/04/12
@version P12
@param nModo, numérico, qual o processo...
			 0 - Distribuição - Alteração
			 1 - Inclusão
			 2 - Distribuição
			 3 - Fechamento
			 4 - Satisfação
			 5 - Cancelamento
@param nOpcx, numérico, qual a operação...
			 1 -
			 2 - Visualização
			 3 - Inclusão
			 4 - Alteração
			 5 - Exclusão
@param lConfirm
@param aMemos
@param cCodFun
@param cCodFilAte
@param oTQB, objeto, objeto da Classe de S.S
@obs usado em MNTA280/MNTA296
/*/
//-------------------------------------------------------------------
Function MNT280GRV(nModo, nOpcx, lConfirm, aMemos, cCodFun, cCodFilAte, oTQB)

	Local i, nRecno, nSMM, nCriticidade:= 1
	Local cCodSS, cVarMemo, cVarCpo, cPriori, cCodFlwUp
	Local aArea := GetArea()
	Local aDbfW045 := {}
	Local lRPORel17 := IIf(GetRPORelease() <= '12.1.017', .T., .F.) // Release menor ou igual a 17

	Private cTrbW045 := GetNextAlias()
	Private cTrbW025 := GetNextAlias()
	Private cTrbW040 := GetNextAlias()

	Default lConfirm := .T.
	Default aMemos := {}

	// Verifica se utiliza a Classe de SS (Release maior que 12.1.17)
	If !lRPORel17 .And. nModo != 0

		If lConfirm .and. nOpcx != 2

			Begin Transaction

				If nModo == 1 .Or. nModo == 4 // Inclusão ou Satisfação
					If nOpcx == 5 .And. nModo == 1
						oTQB:delete() // Método para exclusão.
					Else
						oTQB:upsert() // Método para inclusão.
					EndIf
				ElseIf nModo == 2 // Distribuição

					oTQB:assign() // Método para Distribuição.

				ElseIf nModo == 3 // Fechamento

					oTQB:close() // Método para Fechamento.

				ElseIf nModo == 5 // Cancelamento

					dbSelectArea("TQB")
					RecLock("TQB",.F.)
					TQB->TQB_SOLUCA := "C"
					TQB_HRCANC := M->TQB_HRCANC
					TQB_DTCANC := M->TQB_DTCANC
					MsUnLock("TQB")
					EvalTrigger()  // Processa Gatilhos

					MSMM(,,,M->TQB_MECANC,1,,,"TQB","TQB_CDCANC")

					If !Empty( TQB->TQB_HOABER )
						
						If TQB->TQB_POSCON > 0
							MNT470EXCO( TQB->TQB_CODBEM, TQB->TQB_DTABER , TQB->TQB_HOABER, 1 )
						EndIf
						
						If TQB->TQB_POSCO2 > 0
							MNT470EXCO( TQB->TQB_CODBEM, TQB->TQB_DTABER , TQB->TQB_HOABER, 2 )
						EndIf
					EndIf

				EndIf

				//Posiciona novamente, pois tem RestArea dentro da Classe
				dbSelectArea("TQB")
				dbSetOrder(1)
				dbSeek(xFilial("TQB")+M->TQB_SOLICI)
				cCodSS := M->TQB_SOLICI

				//Processamento na Inclusao
				If nModo == 1

					//Grava questionario realizado
					If lFacilit
						If nOpcx == 3
							MSMM(,,,c280MemoDg,nSMM,,,"TQB","TQB_MEMODG")
						Else
							MSMM(TQB->TQB_MEMODG,,,c280MemoDg,nSMM,,,"TQB","TQB_MEMODG")
						EndIf
					EndIf
					If (nOpcx == 3 .or. nOpcx == 4) .and. lFacilit
						//Calcula criticidade
						If FindFunction("NGCALCRI")
							nCriticidade := NGCALCRI(TQB->TQB_TIPOSS,TQB->TQB_CODBEM,TQB->TQB_CCUSTO,TQB->TQB_DTABER, TQB->TQB_CDSERV)
							dbSelectArea("TQB")
							RecLock("TQB",.F.)
							TQB->TQB_CRITIC := nCriticidade
							MsUnlock("TQB")
							//Preenche criticidade
							cPriori := MNT293CPO(nCriticidade, "TU9_PRIORI")
							dbSelectArea("TQB")
							RecLock("TQB",.F.)
							TQB->TQB_PRIORI := cPriori
							MsUnlock("TQB")
						EndIf
					EndIf

					EvalTrigger() // Processa Gatilhos

					If ExistBLock( 'MNTA2807' ) .And. lFacilit
						aArea := GetArea()
						ExecBlock( 'MNTA2807', .F. , .F. )
						RestArea(aArea)
					EndIf

				//Processamento no Fechamento
				ElseIf nModo == 3
				If NGCADICBASE("ZZY_DTREPR","D","ZZY",.F.) .And. NGCADICBASE("ZZY_SEQUEN","D","ZZY",.F.) .And. ;
					NGCADICBASE("ZZY_SERVIC","D","ZZY",.F.) .And. NGCADICBASE("TJ_STATUS","D","STJ",.F.)
						lCervPetro := .T.
					ElseIf NGCADICBASE("ZZ7_NUMPI","D","ZZ7",.F.)
						lEnercan := .T.
					EndIf
				EndIf

				//Gravacao do Follow-up
				If lFacilit
					If nOpcx == 3//Inclusao
						cCodFlwUp := "01"
					ElseIf nModo < 2 .and. nOpcx == 4//Alteração
						cCodFlwUp := "02"
					ElseIf nModo < 2 .and. nOpcx == 5//Exclusão
						cCodFlwUp := "03"
					ElseIf nModo == 3//Fechamento
						cCodFlwUp := "06"
					ElseIf nModo == 5//Cancelamento
						cCodFlwUp := "07"
					Else//Em ultimo caso eh uma alteracao
						cCodFlwUp := "02"
					EndIf
					//Grava Follow-up
					MNT280GFU(cCodSS,cCodFlwUp,,,,,,,cCodFun,cCodFilAte)
				EndIf
				//Verifica se distribui automaticamente
				If nModo == 1 .and. (nOpcx == 3 .or. nOpcx == 4) .and. lFacilit
					If TQB->TQB_SOLUCA == "A" .and. FindFunction("MNT298AUT")
						If MNT298AUT(TQB->TQB_SOLICI, TQB->TQB_TIPOSS, TQB->TQB_CODBEM, TQB->TQB_CDSERV, TQB->TQB_DTABER, cTrbW040)
							MsgInfo(STR0076 + " '" + AllTrim(TQB->TQB_SOLICI) + "' " + STR0077,STR0055) //"A Solicitação" ## "foi distribuída por processo de Distribuição Automática!" ## "ATENÇÃO"
						EndIf
					EndIf
				EndIf
			End Transaction
		EndIf

		If nOpcx == 2
			cCodSS := TQB->TQB_SOLICI
		EndIf
		If !lConfirm .and. nOPCx == 3
			RollBackSX8()
		EndIf
	Else

		If lConfirm .and. nOpcx != 2

			Begin Transaction

				// Manipula a tabela TQB
				dbSelectArea("TQB")
				dbSetOrder(1)
				If dbSeek(xFilial("TQB")+M->TQB_SOLICI)
					RecLock("TQB",.F.)
				Else
					RecLock("TQB",.T.)
				EndIf

				If nOpcx <> 5
					For i:=1 to FCount()
						If "_FILIAL"$FieldName(i)
							FieldPut(i, xFilial("TQB"))
						Else
							FieldPut(i, &("M->"+FieldName(i)))
						EndIf
					Next i
				Else
					dbDelete()
				EndIf
				MsUnlock("TQB")

		  		dbSelectArea("TQB")
				nSMM := If(nOpcx == 3 .Or. nOpcx == 4,1,2)

				For i:=1 to Len(aMemos)
					cVarCpo := aMemos[i][1]
					cVarMemo:= aMemos[i][2]
					If nOpcx == 3
						MSMM(,,,&cVarMemo,nSMM,,,"TQB",aMemos[i][1])
					Else
						MSMM(&cVarCpo,,,&cVarMemo,nSMM,,,"TQB",aMemos[i][1])
					EndIf
				Next i

				EvalTrigger()
				If nOpcx == 3
					ConfirmSX8()
				EndIf

				cCodSS := TQB->TQB_SOLICI

				//Processamento na Inclusao
				If nModo == 1 .Or. nModo == 0

					If nOpcx == 5

						//Exclui o lancamento de contador relacionado ao bem
						If !Empty( TQB->TQB_HOABER )
                            // Verifica se foi informado primeiro contador
                            If TQB->TQB_POSCON > 0
                               MNT470EXCO( TQB->TQB_CODBEM, TQB->TQB_DTABER , TQB->TQB_HOABER, 1 )
                            EndIf
                            // Verifica se foi informado segundo contador
                            If TQB->TQB_POSCO2 > 0
                               MNT470EXCO( TQB->TQB_CODBEM, TQB->TQB_DTABER , TQB->TQB_HOABER, 2 )
                            EndIf
                        EndIf

						If GetNewPar("MV_NGSSWRK","N") == "S"
							If AllTrim(TQB->TQB_USUARI) != AllTrim(SubStr(UsrFullName(RetCodUsr()),1,30))
								If !ExistBlock("MNTA2808")
									aArea := GetArea()
									MNTW045(TQB->TQB_SOLICI,,, cTrbW045, aDbfW045)
									RestArea(aArea)
								EndIf
							EndIf
						EndIf
					EndIf
					//Grava questionario realizado
					If lFacilit
						If nOpcx == 3
							MSMM(,,,c280MemoDg,nSMM,,,"TQB","TQB_MEMODG")
						Else
							MSMM(TQB->TQB_MEMODG,,,c280MemoDg,nSMM,,,"TQB","TQB_MEMODG")
						EndIf
					EndIf
					If (nOpcx == 3 .or. nOpcx == 4) .and. lFacilit
					//Calcula criticidade
						nCriticidade := NGCALCRI(TQB->TQB_TIPOSS,TQB->TQB_CODBEM,TQB->TQB_CCUSTO,TQB->TQB_DTABER, TQB->TQB_CDSERV)
						dbSelectArea("TQB")
						RecLock("TQB",.F.)
						TQB->TQB_CRITIC := nCriticidade
						MsUnlock("TQB")
						//Preenche criticidade
						If Empty(M->TQB_PRIORI)
							cPriori := MNT293CPO(nCriticidade, "TU9_PRIORI")
							dbSelectArea("TQB")
							RecLock("TQB",.F.)
							TQB->TQB_PRIORI := cPriori
							MsUnlock("TQB")
						EndIf
					EndIf

					EvalTrigger()  // Processa Gatilhos

					If TQB->TQB_SOLUCA == "A" .Or. INCLUI
						If AllTrim(SuperGetMv("MV_NGSSWRK",.F.,"")) == "S"
							aArea := GetArea()
							MNTW025(TQB->TQB_SOLICI,,, cTrbW025)
							RestArea(aArea)
						EndIf
					EndIf
					If ExistBlock("MNTA2807")
						aArea := GetArea()
						ExecBlock("MNTA2807",.F.,.F.)
						RestArea(aArea)
					EndIf
					cFilSt9F := NGSEEK("ST9",TQB->TQB_CODBEM,1,"T9_FILIAL")
					If nOpcx == 3
						If TIPOACOM .And. TQB->TQB_POSCON > 0
							NGTRETCON(TQB->TQB_CODBEM,TQB->TQB_DTABER,TQB->TQB_POSCON,TQB->TQB_HOABER,1,,.F.,,cFilSt9F)
						EndIf
						If TIPOACOM2 .And. TQB->TQB_POSCO2 > 0
							NGTRETCON(TQB->TQB_CODBEM,TQB->TQB_DTABER,TQB->TQB_POSCO2,TQB->TQB_HOABER,2,,.F.,,cFilSt9F)
						EndIf
					EndIf

				ElseIf nModo == 2 //Processamento na Distribuicao

					dbSelectArea( 'TQB' )
					RecLock( 'TQB', .F. )
					TQB->TQB_SOLUCA := 'D'
					TQB->( MsUnLock() )
					EvalTrigger()  // Processa Gatilhos

					If ExistBlock( 'MNTA280J' )
						ExecBlock( 'MNTA280J', .F., .F. )
					EndIf

					If AllTrim( SuperGetMv( 'MV_NGSSWRK', .F., '') ) == 'S'
						aArea := GetArea()
						MNTW040( TQB->TQB_SOLICI, TQB->TQB_CDEXEC, TQB->TQB_CDSERV, cTrbW040 ) //Workflow disparado para o Executante da S.S.
						RestArea( aArea )
					EndIf

				ElseIf nModo == 3 //Processamento no Fechamento
					dbSelectArea("TQB")
					RecLock("TQB",.F.)
					TQB->TQB_SOLUCA := "E"
					MsUnLock("TQB")
					EvalTrigger()  // Processa Gatilhos

					If FieldPos("TQB_PRIOR2") > 0
						If AllTrim(GetNewPar("MV_NGMULOS","N")) == "S"
							dbSelectArea("TT7")
							dbSetOrder(1)
							dbSeek(xFilial("TT7")+TQB->TQB_SOLICI)
							While !Eof() .And. TT7->TT7_FILIAL == xFilial("TT7") .And. TT7->TT7_SOLICI == TQB->TQB_SOLICI
								dbSelectArea("STJ")
								dbSetOrder(1)
								If dbSeek(xFilial("STJ")+TT7->TT7_ORDEM)
									RecLock("STJ",.F.)
									STJ->TJ_PRIORID := TQB->TQB_PRIOR2
									MsUnLock("STJ")
								EndIf
								dbSelectArea("TT7")
								dbSkip()
							End
						Else
							If !Empty(TQB->TQB_ORDEM)
								dbSelectArea("STJ")
								dbSetOrder(1)
								If dbSeek(xFilial("STJ")+TQB->TQB_ORDEM)
									RecLock("STJ",.F.)
									STJ->TJ_PRIORID := TQB->TQB_PRIOR2
									MsUnLock("STJ")
								EndIf
							EndIf
						EndIf

					EndIf

					If AllTrim(SuperGetMv("MV_NGSSWRK",.F.,"")) == "S"
						If AliasInDic("TUQ")
							cContVar := AllTrim(SuperGetMv("MV_NGPESST",.F.,""))
							aQuest := fRetQuesti(cContVar,TQB->TQB_SOLICI,.F.,TQB->TQB_SEQQUE)
							//Verifica se o Questionário está habilitado ou não (1=Sim;2=Não)
							If Len(aQuest) > 0 .And. aQuest[4] <> "1" //Verifica se há questionário de Satisfação cadastrado antes da validação.
								MNT307QUE(.F.,TQB->TQB_SOLICI,.T.)
							EndIf
						EndIf
						dbSelectArea("TQB")
						nRecno := TQB->(Recno())


						Processa({|| MNTW035(nRecno)},"Enviando Workflow...") //Envia workflow para o solicitante da SS;


						dbSelectArea("TQB")
						TQB->(dbGoto(nRecno))
					EndIf
					If NGCADICBASE("ZZY_DTREPR","D","ZZY",.F.) .And. NGCADICBASE("ZZY_SEQUEN","D","ZZY",.F.) .And. ;
					NGCADICBASE("ZZY_SERVIC","D","ZZY",.F.) .And. NGCADICBASE("TJ_STATUS","D","STJ",.F.)
						lCervPetro := .T.
					EndIf
					If ExistBlock("MNTFE290")
						ExecBlock("MNTFE290",.F.,.F.)
					EndIf
					//Processamento no Cancelamento
				ElseIf nModo == 5
					dbSelectArea("TQB")
					RecLock("TQB",.F.)
					TQB->TQB_SOLUCA := "C"
					MsUnLock("TQB")
					EvalTrigger()  // Processa Gatilhos
				EndIf

				//Gravacao do Follow-up
				If lFacilit
					If nOpcx == 3//Inclusao
						cCodFlwUp := "01"
					ElseIf nModo < 2 .and. nOpcx == 4//Alteração
						cCodFlwUp := "02"
					ElseIf nModo < 2 .and. nOpcx == 5//Exclusão
						cCodFlwUp := "03"
					ElseIf nModo == 3//Fechamento
						cCodFlwUp := "06"
					ElseIf nModo == 5//Cancelamento
						cCodFlwUp := "07"
					Else//Em ultimo caso eh uma alteracao
						cCodFlwUp := "02"
					EndIf
					//Grava Follow-up
					MNT280GFU(cCodSS,cCodFlwUp,,,,,,,cCodFun,cCodFilAte)
				EndIf
				//Verifica se distribui automaticamente
				If nModo == 1 .and. (nOpcx == 3 .or. nOpcx == 4) .and. lFacilit
					If TQB->TQB_SOLUCA == "A" .and. FindFunction("MNT298AUT")
						If MNT298AUT(TQB->TQB_SOLICI, TQB->TQB_TIPOSS, TQB->TQB_CODBEM, TQB->TQB_CDSERV, TQB->TQB_DTABER, cTrbW040)
							MsgInfo(STR0076 + " '" + AllTrim(TQB->TQB_SOLICI) + "' " + STR0077,STR0055) //"A Solicitação" ## "foi distribuída por processo de Distribuição Automática!" ## "ATENÇÃO"
						EndIf
					EndIf
				EndIf
			End Transaction
		EndIf

		If nOpcx == 2
			cCodSS := TQB->TQB_SOLICI
		EndIf
		If !lConfirm .and. nOPCx == 3
			RollBackSX8()
		EndIf

		dbSelectArea("TQB")

	EndIf

	// PE para customizações gerais após confirmação
	If ExistBlock("MNTA2909")
		ExecBlock("MNTA2909",.F.,.F., {nModo})
	EndIf

Return cCodSS

//----------------------------------------------------------------------------
/*/{Protheus.doc} MNT280TOK
Realiza verificacoes finais na solicitacao.
@type function

@author  Roger Rodrigues
@since   16/04/12

@sample MNT280TOK( 1, 4, oObj, .F. )

@param nModo    , Define qual o processo a ser executado.
			 	 0 - Distribuição - Alteração
				 1 - Inclusão
			 	 2 - Distribuição
			 	 3 - Fechamento
			 	 4 - Satisfação
			 	 5 - Cancelamento
@param nOpcx   , Numérico, Define a operação a ser executada.
			 	 1 -
			 	 2 - Visualização
			 	 3 - Inclusão
			 	 4 - Alteração
			  	 5 - Exclusão
@param oTQB    , Objeto  , Objeto da Classe de S.S.
@param lFacilit, Lógico  , Define se esta usando Facilits.
@return lRet   , Lógico  , Define se o processo foi realizado corretamente.
/*/
//-----------------------------------------------------------------------------
Function MNT280TOK( nModo, nOpcx, oTQB, lFacilit )

	Local lRet      := .T.
	Local xPE280I   := Nil
	Local lRPORel17 := IIf(GetRPORelease() <= '12.1.017', .T., .F.) // Release menor ou igual a 17

	If !lRPORel17 .And. nModo != 0

		If ExistBlock("MNTA2802")
			If !ExecBlock("MNTA2802",.F.,.F.)
				lRet := .F.
			EndIf
		EndIf

		// Transfere os valores do Objeto para a Memória.
		oTQB:MemoryToClass()

		If nOpcx == 4 .And. nModo == 3
			oTQB:setValue("TQB_SOLUCA", "E") // Define que é fechamento
		ElseIf nOpcx == 4 .And. nModo == 2
			oTQB:setValue("TQB_SOLUCA", "D") // Define que é Distribuição
		EndIf

		//Verifica se os registros são válidos para realizar a inclusão
		If nOpcx !=2 .And. !oTQB:valid()
			oTQB:showHelp()
			lRet := .F.
		EndIf

		If lRet .And. nOpcx !=2 .And. nOpcx != 5
			If nModo == 1 //Inclusão de SS
				lRet := MNTA280CHE()
			ElseIf nModo == 3
				lRet := MNTA290FCH()
			EndIf
		EndIf
	Else
		If nOpcx !=2 .and. nOpcx != 5
			lRet := Obrigatorio(aGets,aTela)
			If lRet
				If nModo == 1 //Inclusão de SS
					lRet := MNTA280CHE()
				ElseIf nModo == 2 .Or. nModo == 0
					lRet := NG295SERV()
				ElseIf nModo == 3
					lRet := MNTA290FCH()
				ElseIf nModo == 4
					lRet := MNTA305OBR()
				ElseIf nModo == 5
					lRet := MNTA290CNC()
				EndIf
			EndIf

			If lRet .And. ExistBlock( 'MNTA280I' )

				// Ponto de entrada que permite incluir novas validações aos processos de solicitação de serviço.
				xPE280I := ExecBlock( 'MNTA280I', .F., .F., { nOpcX } ) //Se o Retorno do PE for falso.

				lRet    := IIf( ValType( xPE280I ) == 'A', xPE280I[1], xPE280I )

			EndIf

		EndIf

	EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} MNT280VBA
Verifica a necessidade de ajuste de base

@author  Jackson Machado
@since   25/04/12
@version P12
/*/
//-------------------------------------------------------------------
Function MNT280VBA(lWeb)
	Local lAjuste   := .F.
	Local cMsg      := STR0078 //"Para utilização das novas rotinas do Facilities, será necessário realizar um ajuste de base."
	Local cMsg2     := CHR(13)+CHR(13)+STR0079 //"Para mais detalhes, contate o administrador de sistemas."
	Local cQry      := ""
	Local cNewAlias := GetNextAlias()
	Private cMarca  := GetMark()
	Default lWeb    := .F.


	cQry := " SELECT R_E_C_N_O_ FROM "+RetSqlName("TQB")+" TQB WHERE "
	cQry += " ( TQB.TQB_CDEXEC <> '' AND TQB.TQB_SOLICI NOT IN "
	cQry += " (SELECT TUR_SOLICI FROM "+RetSqlName("TUR")+" TUR WHERE TUR.D_E_L_E_T_ <> '*' AND TUR.TUR_FILIAL = '"+xFilial("TUR")+"') )"
	cQry += " AND TQB.D_E_L_E_T_ <> '*' "
	cQry += " AND TQB.TQB_FILIAL = '"+xFilial("TQB")+"' "

	cQry := ChangeQuery(cQry)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQry),(cNewAlias), .T., .T.)

	dbSelectArea(cNewAlias)
	dbGoTop()
	While (cNewAlias)->( !Eof() )
		lAjuste := .T.
		Exit
	EndDo
	(cNewAlias)->(dbCloseArea())


	If lAjuste .AND. !lWeb
		If MsgYesNo(cMsg+STR0080+cMsg2) //" Deseja ajustar agora?"
			lAjuste := !fAjutBase(cMarca)
		EndIf
	EndIf

Return If(lWeb,cMsg+cMsg2,!lAjuste)

//-------------------------------------------------------------------
/*/{Protheus.doc} fAjutBase
Tela para ajuste de base

@author  Jackson Machado
@since   25/04/12
@version P12
/*/
//-------------------------------------------------------------------
Static Function fAjutBase(cMarca)
	Local nX, nRec
	Local lRet := .F.
	Local oFont    := TFont():New("Arial",,-11,.F.,.T.)
	//Variaveis de tamanho de tela e objetos
	Local aSize := {}, aObjects := {}, aInfo := {}, aPosObj := {}
	//Variaveis de objeto
	Local oDlg, oPnlPai, oPnlFol
	Local oListFu, oMarkFu
	Local oPnl1Btn, oPnl1Bot
	Local oPnl1Lis, oPnl1Mar
	Local oPnl1Tit, oPnl2Tit
	Local oPnlYY //P.O.G.
	//Variaveis de Folder
	Local oFolder
	Local aTitles := {}
	//Cores
	Local aCores := NGCOLOR("10")
	//Variavies de List
	Local aFuncionarios := {}
	Local aTitulos, aColsSize
	Local bListFu
	//Variavies de Mark
	Local aTRBFUN, aTRBF
	Local aIndFun
	Local oTmpFun
	Private cTRBFun
	Private lInverte := .F.

	//Criacao dos TRB's
	Private aVETINR := {}

	//Mark Funcionário
	aTRBFUN := {}
	aAdd(aTRBFUN,{"OK"      ,"C",02,0})
	aAdd(aTRBFUN,{"FILFUN"  ,"C",02,0})
	aAdd(aTRBFUN,{"MATFUN"  ,"C",06,0})
	aAdd(aTRBFUN,{"NOMFUN"  ,"C",30,0})

	aIndFun	:= {"FILFUN","MATFUN"}
	cTRBFun	:= GetNextAlias()

	//Intancia classe FWTemporaryTable
	oTmpFun := FWTemporaryTable():New( cTRBFun, aTRBFUN )
	//Cria indices
	oTmpFun:AddIndex( "Ind01" , aIndFun )
	//Cria a tabela temporaria
	oTmpFun:Create()


	aTRBF := {}
	aAdd(aTRBF,{ "OK"     ,NIL," "    ,})
	aAdd(aTRBF,{ "FILFUN" ,NIL,"Filial",})
	aAdd(aTRBF,{ "MATFUN" ,NIL,"Matrícula",})
	aAdd(aTRBF,{ "NOMFUN" ,NIL,"Nome",})

	Processa({|lEnd| fCarga(@aFuncionarios) },STR0081,STR0082,.T.) //"Aguarde..." ## "Carregando Informações..."

	If Len(aFuncionarios) == 0
		Return
	EndIf
	//Definicao de tamanho de tela e objetos
	aSize := MsAdvSize(,.F.,430)
	Aadd(aObjects,{030,030,.T.,.T.})
	Aadd(aObjects,{100,100,.T.,.T.})
	aInfo := {aSize[1],aSize[2],aSize[3],aSize[4],0,0}
	aPosObj := MsObjSize(aInfo, aObjects,.T.)

	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0083) From aSize[7],0 To aSize[6],aSize[5] OF oMainWnd PIXEL //"Ajuste de Base"
	oPnlPai := TPanel():New(00,00,,oDlg,,,,,aCores[2],00,00,.F.,.F.)
	oPnlPai:Align := CONTROL_ALIGN_ALLCLIENT

	oPnlFol := TPanel():New(00,00,,oDlg,,,,,/*aCores[2]*/,00,00,.F.,.F.)
	oPnlFol:Align := CONTROL_ALIGN_ALLCLIENT

	//Criando Folders
	Aadd(aTitles,OemToAnsi(STR0084)) //"Funcionários"

	oFolder := TFolder():New(0,0,aTitles,,oPnlFol,,,,.F.,.F.,1000,1000,)
	oFolder:Align := CONTROL_ALIGN_ALLCLIENT
	nTelaX := ( aSize[6]/2.02 ) - 108
	//FOLDER 1
	oPnl1Bot := TPanel():New(00,00,,oFolder:aDialogs[1],,,,,aCores[2],aSize[5],aSize[6],.F.,.F.)
	oPnl1Bot:Align := CONTROL_ALIGN_TOP
	oPnl1Bot:nHeight := 20
	oSay:= tSay():New(01,05,{|| STR0085},oPnl1Bot,,oFont,,,,.T.,aCores[1],aCores[1],200,40) //"Favor indicar qual Funcionário da Manutenção corresponde ao Executante de S.S."

	oPnl1Lis := TPanel():New(00,00,,oFolder:aDialogs[1],,,,,aCores[2],aSize[5]/4-12,aSize[6],.F.,.F.)
	oPnl1Lis:Align := CONTROL_ALIGN_LEFT
	oPnl1Lis:nHeight := aSize[5]/4-12

	oPnl1Tit  := TPanel():New(00,00,,oPnl1Lis,,,,,aCores[2],10,10,.F.,.F.)
	oPnl1Tit:Align := CONTROL_ALIGN_TOP
	oSay:= tSay():New(01,05,{|| STR0086},oPnl1Tit,,oFont,,,,.T.,aCores[1],aCores[1],200,40) //"Executantes da S.S."

	aTitulos  := {STR0087,STR0088,STR0089} //"Filial" ## "Matrícula" ## "Nome"
	aColsSize := {40,40,200}
	oListFu := TWBrowse():New( 22 , 01, 245, 150,,aTitulos,aColsSize,oPnl1Lis,,,,,{||},,,,,,,.F.,,.T.,,.F.,,, )
	oListFu:SetArray(aFuncionarios)
	oListFu:Align   := CONTROL_ALIGN_ALLCLIENT
	bListFu         := { || { ;
	aFuncionarios[oListFu:nAt,1], ;
	aFuncionarios[oListFu:nAt,2], ;
	aFuncionarios[oListFu:nAt,3];
	} }
	oListFu:bLine   := bListFu
	oListFu:bChange := {|| fChange(@aFuncionarios, oListFu, oMarkFu)}
	oListFu:nAt     := 1

	oPnl1Btn := TPanel():New(00,00,,oFolder:aDialogs[1],,,,,aCores[2],14,14,.F.,.F.)
	oPnl1Btn:Align := CONTROL_ALIGN_LEFT
	//Panel para abaixar o botao de inclusão - P.O.G.
	oPnlYY := TPanel():New(0,0,,oPnl1Btn,,,,,aCores[2],0,12,.F.,.F.)
	oPnlYY:Align := CONTROL_ALIGN_TOP

	oBtnIncFun  := TBtnBmp():NewBar("ng_ico_incluir","ng_ico_incluir",,,,{|| fCadastro(@oMarkFu,aFuncionarios,oListFu)},,oPnl1Btn,,,STR0090,,,,,"") //"Incluir Funcionário"
	oBtnIncFun:Align  := CONTROL_ALIGN_TOP

	oPnl1Mar := TPanel():New(00,00,,oFolder:aDialogs[1],,,,,aCores[2],aSize[5],aSize[6],.F.,.F.)
	oPnl1Mar:Align := CONTROL_ALIGN_ALLCLIENT

	oPnl2Tit  := TPanel():New(00,00,,oPnl1Mar,,,,,aCores[2],10,10,.F.,.F.)
	oPnl2Tit:Align := CONTROL_ALIGN_TOP
	oSay:= tSay():New(01,05,{|| STR0091},oPnl2Tit,,oFont,,,,.T.,aCores[1],aCores[1],200,40) //"Funcionários da Manutenção."

	oMarkFu := MsSelect():New((cTRBFun),"OK",,aTRBF,@lInverte,@cMarca,{00,00,1000,1000},,,oPnl1Mar)
	oMarkFu:bMARK               := {|| fMark((cTRBFun)->(FILFUN+MATFUN),@aFuncionarios, oListFu, oMarkFu ) }
	oMarkFu:oBrowse:lHASMARK    := .T.
	oMarkFu:oBrowse:lCANALLMARK := .F.
	oMarkFu:oBrowse:bALLMARK    := {|| .F. }
	oMarkFu:oBrowse:ALIGN       := CONTROL_ALIGN_ALLCLIENT

	(cTRBFun)->(dbGoTop())

	oListFu:Refresh()
	oMarkFu:oBrowse:Refresh()
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| lRet := .T.,If(!fValTrb(aFuncionarios),lRet := .F.,oDlg:End())},{|| lRet := .F., oDlg:End()})

	If lRet
		For nX := 1 To Len(aFuncionarios)
			If !Empty(aFuncionarios[nX,4])
				dbSelectArea("TQB")
				dbSetOrder(11)
				If dbSeek(xFilial("TQB")+aFuncionarios[nX,2])
					While !Eof() .AND. TQB->TQB_CDEXEC == aFuncionarios[nX,2]
						nRec := TQB->(RECNO())
						MNT280GAT(TQB->TQB_SOLICI/*cCodSS*/, "2"/*cTipoAtend*/, xFilial("ST1")/*cFilAtend*/, aFuncionarios[nX,4]/*cCodAtend*/, /*cLojAtend*/, ;
						TQB->TQB_DTABER/*dDtReceb*/, TQB->TQB_HOABER/*cHrReceb*/, TQB->TQB_DTFECH/*dDtFinal*/, TQB->TQB_HOFECH/*cHrFinal*/, TQB->TQB_TEMPO/*cHrRealiz*/)

						dbSelectArea("ST1")
						dbSetOrder(1)
						If dbSeek(xFilial("ST1")+Trim(aFuncionarios[nX,4])) .and. (ST1->T1_TIPATE != "2" .or. ST1->T1_TIPATE != "3")
							RecLock("ST1",.F.)
							ST1->T1_TIPATE := "3"
							MsUnlock("ST1")
						EndIf
						dbSelectArea("TQB")
						dbSetOrder(11)
						dbGoTo(nRec)
						dbSkip()
					End
				EndIf
			EndIf
		Next nX
	EndIf

	oTmpFun:Delete()
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} fValTrb
Cadastro de Funcionarios

@author  Jackson Machado
@since   25/04/12
@version p12
@param aFunc, array, array de funcionários
/*/
//-------------------------------------------------------------------
Static Function fValTrb(aFunc)
	Local nX
	Local lCompTQ4   := NGSX2MODO("TQ4") == "C"

	For nX := 1 To Len(aFunc)
		If Empty(aFunc[nX,4])
			If lCompTQ4
				dbSelectArea("TQB")
				dbSetOrder(11)
				If dbSeek(xFilial("TQB")+aFunc[nX,2])
					ShowHelpDlg(STR0055,{STR0092},2,; //"ATENÇÃO" ## "Um ou mais executantes não foram configurados."
					{STR0093+AllTrim(aFunc[nX,2])+" - "+aFunc[nX,3]},2) //"Favor selecionar o funcionário correspondente do executante "
					Return .F.
				EndIf
			Else
				ShowHelpDlg(STR0055,{STR0092},2,; //"ATENÇÃO" ## "Um ou mais executantes não foram configurados."
				{STR0093+AllTrim(aFunc[nX,2])+" - "+aFunc[nX,3]},2) //"Favor selecionar o funcionário correspondente do executante "
				Return .F.
			EndIf
		EndIf
	Next nX

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fChange   ºAutor  ³Jackson Machado     º Data ³  25/04/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Chamada na alteração das linhas do listbox                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAMNT                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fChange(aArray, oObj, oMark)
	Local cCod
	Local cKey      := aArray[oObj:nAt,4]

	cAliasTMP := cTRBFun
	cCod      := "MATFUN"

	dbSelectArea(cAliasTMP)
	dbGoTo(1)
	While (cAliasTMP)->(!Eof())
		RecLock(cAliasTMP,.F.)
		If cKey == (cAliasTMP)->&(cCod)
			(cAliasTMP)->OK := cMarca
			(cAliasTMP)->(MsUnLock())
		Else
			(cAliasTMP)->OK := Space(2)
		EndIf
		(cAliasTMP)->(MsUnLock())
		(cAliasTMP)->(dbSkip())
	End
	dbSelectArea(cAliasTMP)
	dbGoTo(1)

	oMark:oBrowse:Refresh()

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fMark     ºAutor  ³Jackson Machado     º Data ³  25/04/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Marca a opcao                                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAMNT                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fMark(cChave,aArray, oObj, oMark)
	Local cAliasTMP
	Local cKey, cCod

	cAliasTMP := cTRBFun
	cKey      := "(cTRBFun)->(FILFUN+MATFUN)"
	cCod      := "MATFUN"

	dbSelectArea(cAliasTMP)
	dbGoTo(1)
	While (cAliasTMP)->(!Eof())
		RecLock(cAliasTMP,.F.)
		If &(cKey) == cChave
			(cAliasTMP)->OK := cMarca
			(cAliasTMP)->(MsUnLock())
			aArray[oObj:nAt,4] := (cAliasTMP)->&(cCod)
		Else
			(cAliasTMP)->OK := Space(2)
		EndIf
		(cAliasTMP)->(MsUnLock())
		(cAliasTMP)->(dbSkip())
	End
	dbSelectArea(cAliasTMP)
	dbSetOrder(1)
	dbSeek(cChave)

	oMark:oBrowse:Refresh()
Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fCadastro ºAutor  ³Jackson Machado     º Data ³  25/04/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cadastro de Funcionarios                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAMNT                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fCadastro(oMark,aArray,oObj)
	Local cAliasTMP
	Local aRestArea       := GetArea()
	Private M->T1_TIPOFUN := "N"
	Private cCadastro     := ""

	INCLUI := .T.
	ALTERA := .F.

	cAliasTMP := cTRBFun
	FWExecView( STR0084 , 'MNTA020' , MODEL_OPERATION_INSERT , , { || .T. } )

	Processa({|lEnd| fCarga(,aArray,oObj) },STR0081,STR0082,.T.) //"Aguarde..." ## "Carregando Informações..."

	dbSelectArea(cAliasTMP)
	dbGoTop()
	oMark:oBrowse:nAt := 1
	oMark:oBrowse:Refresh()
	RestArea(aRestArea)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fCarga    ºAutor  ³Jackson Machado     º Data ³  25/04/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Realiza a carga de informacoes                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAMNT                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fCarga(aFunc,aArray,oObj)
	Default aArray := {}

	If ValType(aFunc) == "A"
		ProcRegua(TQ4->(RecCount()))
		dbSelectArea("TQ4")
		dbGoTop()
		dbSeek(xFilial("TQ4"))
		While !Eof() .AND. xFilial("TQ4") == TQ4->TQ4_FILIAL
			IncProc(STR0094) //"Executantes..."
			dbSelectArea("TQB")
			dbSetOrder(11)
			If dbSeek(xFilial("TQB")+TQ4->TQ4_CDEXEC)
				aAdd(aFunc,{TQ4->TQ4_FILIAL,TQ4->TQ4_CDEXEC,TQ4->TQ4_NMEXEC,""})
			EndIf
			dbSelectArea("TQ4")
			dbSkip()
		End
	EndIf

	dbSelectArea(cTRBFun)
	ZAP
	ProcRegua(ST1->(RecCount()))
	dbSelectArea("ST1")
	dbGoTop()
	dbSeek(xFilial("ST1"))
	While !Eof() .AND. xFilial("ST1") == ST1->T1_FILIAL
		IncProc(STR0095) //"Funcionários..."
		dbSelectArea(cTRBFun)
		RecLock(cTRBFun,.T.)
		(cTRBFun)->OK     := If(Len(aArray) > 0 .AND. aArray[oObj:nAt,4] == ST1->T1_CODFUNC,cMarca,"")
		(cTRBFun)->FILFUN := ST1->T1_FILIAL
		(cTRBFun)->MATFUN := ST1->T1_CODFUNC
		(cTRBFun)->NOMFUN := ST1->T1_NOME
		(cTRBFun)->(MsUnLock())
		dbSelectArea("ST1")
		dbSkip()
	End

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNT280GFU
Grava o Follow-Up

@return Nil

@sample
MNT280GFU('000001','01')

@param
cCodSS   - Código da S.S. para gravação (Obrigatório)
cCodFlwUp   - Código do Follow-Up (Obrigatório)
cObservacao - Observação do Follow-Up (N. Obrigatório)
dDtIFlwUp   - Data Início do Follow-Up (N. Obrigatório)
cHrIFlwUp   - Hora Início do Follow-Up  (N. Obrigatório)
dDtFFlwUp   - Data Fim do Follow-Up (N. Obrigatório)
cHrFFlwUp   - Hora Fim do Follow-Up  (N. Obrigatório)
cUsuFlwUp   - Usuário do Follow-Up (N. Obrigatório)
cCodFun     - Atendente do Follow-Up (N. Obrigatório)
cCodFilAte  - Filial Atendente do Follow-Up (N. Obrigatório)

@author Jackson Machado
@since 16/04/12
@version 1.0
/*/
//---------------------------------------------------------------------
Function MNT280GFU(cCodSS,cCodFlwUp,cObservacao,dDtIFlwUp,cHrIFlwUp,dDtFFlwUp,cHrFFlwUp,cUsuFlwUp,cCodFun,cCodFilAte)
	Local aArea := GetArea()
	Local dDataAtu := dDataBase
	Local cHoraAtu := Time()
	Local lInclui  := .F.
	Local cHrTotal := ""
	Default dDtIFlwUp   := dDataAtu
	Default cHrIFlwUp   := cHoraAtu
	Default dDtFFlwUp   := dDataAtu
	Default cHrFFlwUp   := cHoraAtu
	Default cUsuFlwUp   := RetCodUsr()
	Default cObservacao := ""
	Default cCodFun     := ""
	Default cCodFilAte  := ""

	// Calcula o Total de Horas
	If !Empty(dDtIFlwUp) .And. !Empty(cHrIFlwUp) .And. !Empty(dDtFFlwUp) .And. !Empty(cHrFFlwUp)
		cHrTotal := A291CalcHr(dDtIFlwUp, cHrIFlwUp, dDtFFlwUp, cHrFFlwUp)
	EndIf

	//--------------------
	// Gravação
	//--------------------
	BEGIN TRANSACTION // Inicia a Transação
		dbSelectArea("TUM")
		dbSetOrder(1)
		If !dbSeek(xFilial("TUM")+cCodSS+DTOS(dDtIFlwUp)+cHrIFlwUp+DTOS(dDtFFlwUp)+cHrFFlwUp+cCodFlwUp+cUsuFlwUp)
			RecLock("TUM",.T.)
			lInclui := .T.
			TUM->TUM_FILIAL := xFilial("TUM")
			TUM->TUM_SOLICI := cCodSS
			TUM->TUM_CODFOL := cCodFlwUp
			TUM->TUM_DTINIC := dDtIFlwUp
			TUM->TUM_HRINIC := cHrIFlwUp
			TUM->TUM_DTFIM  := dDtFFlwUp
			TUM->TUM_HRFIM  := cHrFFlwUp
		Else
			RecLock("TUM",.F.)
			lInclui := .F.
		EndIf
		TUM->TUM_USUARI := cUsuFlwUp
		TUM->TUM_OBSERV := cObservacao
		TUM->TUM_FILATE := cCodFilAte
		TUM->TUM_ATENDE := cCodFun
		If !Empty(cHrTotal)
			TUM->TUM_HRTOTA := cHrTotal
		EndIf
		TUM->(MsUnLock())
	END TRANSACTION // Encerra a Transação

	If lInclui
		MSMM(,,,cObservacao,1,,,"TUM","TUM_OBSERV")
	Else
		MSMM(&("TUM_OBSERV"),,,cObservacao,3,,,"TUM","TUM_OBSERV")
	EndIf

	RestArea(aArea)
Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNT280RFU
Retorna Follow-up da SS

@return {aHeader,aCols}

@sample
MNT280RFU('000001')

@param
cCodSS   - Código da S.S. para gravação (Obrigatório)
aNao     - Array com campos que nao devem aparecer (N. Obrigatório)
cCodFil  - Filial para Busca dos registros (N. Obrigatório)

@author Roger Rodrigues
@since 16/06/12
@version 1.0
/*/
//---------------------------------------------------------------------
Function MNT280RFU(cCodSS, aNao, cCodFil)
	Local aNGBEGINPRM := NGBEGINPRM()
	Local cWhile := ""
	Local aHeadFol := {}, aColsFol := {}

	Default aNao := {"TUM_SOLICI"}
	Default cCodFil := ""
	Default cCodSS := ""

	cWhile := "TUM->TUM_FILIAL == xFilial( 'TUM' ) .AND. TUM->TUM_SOLICI == '" + cCodSS + "'"

	//Carrega variaveis
	dbSelectArea("TUM")
	dbSetOrder(1)
	FillGetDados( 2,"TUM",1,cCodSS,{||},{||.T.},aNao,,,,{|| NGMontaAcols("TUM",cCodSS,cWhile) },,aHeadFol,aColsFol)
	If Len(aColsFol) == 0
		aColsFol := BlankGetD(aHeadFol)
	EndIf

	NGRETURNPRM(aNGBEGINPRM)
Return {aHeadFol, aColsFol}
//---------------------------------------------------------------------
/*/{Protheus.doc} MNT280GAT
Grava o Atendimento

@return Nil

@param
cCodSS   - Código da S.S. para leitura (Obrigatório)
cCodFlwUp   - Código do Follow-Up (Obrigatório)
cObservacao - Observação do Follow-Up (N. Obrigatório)
dDtIFlwUp   - Data Início do Follow-Up (N. Obrigatório)
cHrIFlwUp   - Hora Início do Follow-Up  (N. Obrigatório)
dDtFFlwUp   - Data Fim do Follow-Up (N. Obrigatório)
cHrFFlwUp   - Hora Fim do Follow-Up  (N. Obrigatório)
cUsuFlwUp   - Usuário do Follow-Up (N. Obrigatório)
cCodFun     - Atendente do Follow-Up (N. Obrigatório)

@author Jackson Machado
@since 16/04/12
@version 1.0
/*/
//---------------------------------------------------------------------
Function MNT280GAT(cCodSS, cTipoAtend, cFilAtend, cCodAtend, cLojAtend, dDtReceb, cHrReceb, dDtFinal, cHrFinal, cHrRealiz, cCodFilTUR)

	// Variáveis auxiliares
	Local aArea := GetArea()
	Local dDataNULL := CTOD("")
	Local cHoraNULL := ""

	Local cSeekFilTUR := xFilial("TUR")

	// Defaults
	Default dDtReceb   := dDataNULL
	Default cHrReceb   := cHoraNULL
	Default dDtFinal   := dDataNULL
	Default cHrFinal   := cHoraNULL
	Default cHrRealiz  := cHoraNULL
	Default cCodFilTUR := ""

	//--- Define o Tamanho do Conteúdo dos campos para gravação
	cCodSS     := PADR(cCodSS    , TAMSX3("TUR_SOLICI")[1], " ")
	cTipoAtend := PADR(cTipoAtend, TAMSX3("TUR_TIPO")[1]  , " ")
	cFilAtend  := PADR(cFilAtend , TAMSX3("TUR_FILATE")[1], " ")
	cCodAtend  := PADR(cCodAtend , TAMSX3("TUR_CODATE")[1], " ")
	cLojAtend  := PADR(cLojAtend , TAMSX3("TUR_LOJATE")[1], " ")
	If !Empty(cHrReceb) .And. Len(cHrReceb) == 5
		cHrReceb += ":00"
	ElseIf AllTrim(cHrReceb) == ":"
		cHrReceb := cHoraNULL
	EndIf
	If !Empty(cHrFinal) .And. Len(cHrFinal) == 5
		cHrFinal += ":00"
	ElseIf AllTrim(cHrFinal) == ":"
		cHrFinal := cHoraNULL
	EndIf
	If !Empty(cHrRealiz) .And. Len(cHrRealiz) == 5
		cHrRealiz += ":00"
	ElseIf AllTrim(cHrRealiz) == ":"
		cHrRealiz := cHoraNULL
	EndIf
	If !Empty(cCodFilTUR)
		cSeekFilTUR := xFilial("TUR", cCodFilTUR)
	EndIf

	//--------------------
	// Gravação
	//--------------------
	BEGIN TRANSACTION // Inicia a Transação
		dbSelectArea("TUR")
		dbSetOrder(1)
		If dbSeek(cSeekFilTUR + cCodSS + cTipoAtend + cFilAtend + cCodAtend + cLojAtend + DTOS(dDtReceb) + cHrReceb)
			RecLock("TUR",.F.)
		Else
			RecLock("TUR",.T.)
			TUR->TUR_FILIAL := xFilial("TUR")
			TUR->TUR_SOLICI := cCodSS
			TUR->TUR_TIPO   := cTipoAtend
			TUR->TUR_FILATE := cFilAtend
			TUR->TUR_CODATE := cCodAtend
			TUR->TUR_LOJATE := cLojAtend
			TUR->TUR_DTRECE := dDtReceb
			TUR->TUR_HRRECE := cHrReceb
		EndIf
		If !Empty(cHrRealiz)
			TUR->TUR_HRREAL := cHrRealiz
		EndIf
		If !Empty(dDtFinal)
			TUR->TUR_DTFINA := dDtFinal
		EndIf
		If !Empty(cHrFinal)
			TUR->TUR_HRFINA := cHrFinal
		EndIf
		TUR->(MsUnLock())
	END TRANSACTION // Encerra a Transação

	RestArea(aArea)
Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNT280RAT
Retorna Atendentes da SS

@return {aHeader,aCols}

@sample
MNT280RAT('000001')

@param
cCodSS   - Código da S.S. para leitura (Obrigatório)
aNao     - Array com campos que nao devem aparecer (N. Obrigatório)
cCodFil  - Filial para Busca dos registros (N. Obrigatório)

@author Roger Rodrigues
@since 16/06/12
@version 1.0
/*/
//---------------------------------------------------------------------
Function MNT280RAT(cCodSS, aNao, cCodFil)
	Local aNGBEGINPRM := NGBEGINPRM()
	Local cWhile := "TUR->TUR_FILIAL == xFilial( 'TUR' ) .AND. TUR->TUR_SOLICI == '" + cCodSS + "'"
	Local aHeadAte := {}, aColsAte := {}
	Default aNao := {"TUR_SOLICI"}
	Default cCodFil := ""

	//Carrega variaveis
	dbSelectArea("TUR")
	dbSetOrder(1)
	FillGetDados( 2,"TUR",1,cCodSS,{||},{||.T.},aNao,,,,{|| NGMontaAcols("TUR",cCodSS,cWhile) },,aHeadAte,aColsAte)
	If Len(aColsAte) == 0
		aColsAte := BlankGetD(aHeadAte)
	EndIf

	NGRETURNPRM(aNGBEGINPRM)
Return {aHeadAte, aColsAte}
//---------------------------------------------------------------------
/*/{Protheus.doc} MNT280RIN
Retorna Insumos da S.S.

@return {aHeader,aCols}

@sample
MNT280RIN('000001')

@param
cCodSS   - Código da S.S. para leitura (Obrigatório)
aNao     - Array com campos que nao devem aparecer (N. Obrigatório)
cCodFil  - Filial para Busca dos registros (N. Obrigatório)

@author Wagner Sobral de Lacerda
@since 18/10/2012
@version 1.0
/*/
//---------------------------------------------------------------------
Function MNT280RIN(cCodSS, aNao, cCodFil)
	Local aNGBEGINPRM := NGBEGINPRM()
	Local cWhile := "TUU->TUU_FILIAL == xFilial( 'TUU' ) .AND. TUU->TUU_SOLICI == '" + cCodSS + "'"
	Local aHeadAte := {}, aColsAte := {}
	Default aNao := {"TUU_SOLICI"}
	Default cCodFil := ""

	//Carrega variaveis
	dbSelectArea("TUU")
	dbSetOrder(1)
	FillGetDados( 2,"TUU",1,cCodSS,{||},{||.T.},aNao,,,,{|| NGMontaAcols("TUU",cCodSS,cWhile) },,aHeadAte,aColsAte)
	If Len(aColsAte) == 0
		aColsAte := BlankGetD(aHeadAte)
	EndIf

	NGRETURNPRM(aNGBEGINPRM)
Return {aHeadAte, aColsAte}
//---------------------------------------------------------------------
/*/{Protheus.doc} MNT280OBS
Função para exibição da observacao do Follow-Up

@return Nil

@sample
MNT280OBS()

@author Jackson Machado
@since 16/04/12
@version 1.0
/*/
//---------------------------------------------------------------------
Function MNT280OBS()

	Local oDlgObs, oObs
	Local cObs    := ""
	Local nOpcObs := 0

	dbSelectArea("TUM")
	cObs := TUM->TUM_OBSERV

	Define Msdialog oDlgObs Title STR0096 From 18,20 To 28,75 Of oMainWnd //"Observação do Follow-up"

	@ 17,8 Say STR0097 Of oDlgObs Pixel  //"Observação"
	@ 25,8 Get oObs Var cObs Of oDlgObs Multiline Size 200,40 Pixel

	Activate Msdialog oDlgObs On Init EnchoiceBar(oDlgObs,{|| nOpcObs := 1 , If(fValObs(cObs,@nOpcObs),oDlgObs:End(),.T.) },{|| oDlgObs:End()})

Return (nOpcObs == 1)
//---------------------------------------------------------------------
/*/{Protheus.doc} fValObs
Função para validação da observação do Follow-Up

@return Nil

@sample
fValObs()

@author Jackson Machado
@since 16/04/12
@version 1.0
/*/
//---------------------------------------------------------------------
Static Function fValObs(cObs,nOpcObs)

	If Empty(cObs)
		MsgInfo(STR0098,STR0055) //"É necessário informar o campo Observação." ## "ATENÇÃO"
		nOpcObs := 2
		Return .F.
	EndIf

	dbSelectArea("TUM")
	RecLock("TUM",.F.)
	TUM->TUM_OBSERV := cObs
	MsUnLock("TUM")

Return .T.
//---------------------------------------------------------------------
/*/{Protheus.doc} MNT280AJU
Função para ajuste de base quando o conteúdo do campo TQB_SOLUCA estiver igual à "N"

@return Nil

@sample
MNT280AJU()

@author Diego de Oliveira
@since 30/11/15
@version 1.0
/*/
//---------------------------------------------------------------------
Function MNT280AJU()

	Local cAliasQr1 := GetNextAlias() //Cria o alias cAliasQr1, para carregar o resultado da query.

	//Query que a quantidade de registros com a variação dia zerada
	cQuery := " SELECT COUNT(*) AS SOLUCA FROM  " + RetSQLName( "TQB" )"
	cQuery += " WHERE TQB_SOLUCA = 'N' "
	cQuery += " AND D_E_L_E_T_ <> '*' "
	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T.,"TOPCONN",TCGENQRY(,,cQuery),cAliasQr1,.F.,.T. )

	dbSelectArea(cAliasQr1)
	dbGoTop()
	If !Eof()

		While (cAliasQr1)->( !EoF() )

			If (cAliasQr1)->SOLUCA > 0
				cQuery := " UPDATE " + RetSQLName( "TQB" )
				cQuery += " SET TQB_SOLUCA = 'A' "
				cQuery += " WHERE TQB_SOLUCA = 'N' "
				cQuery += " AND D_E_L_E_T_ <> '*' "
				TcSqlExec( cQuery )
			EndIf

			(cAliasQr1)->( dbSkip() )

		EndDo

	EndIf

	(cAliasQr1)->(dbCloseArea()) //Fecha a área de trabalho corrente.

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} fVldFecha
Função utilizada para validar se a tela de finalização de S.S.
pode ser fechada ou não.

@type Function

@author João Ricardo Santini Zandoná
@since 10/02/2023
@param nOpcx   , numérico, indica qual a operação que está sendo realizada 
@param nChamada, numérico, indica qual fonte está chamando a função
		1 - Default
		2 - MNTA291
		3 - MNTA400
		4 - MNTA902

@return Logica, informa se a tela pode ser fechada ou não.
/*/
//------------------------------------------------------------------------------
Static Function fVldFecha(nChamada, nOpcx)

	Local lImpede := .F.

	If nOpcx = 4 .And. nChamada != 1 .And. ExistBlock("MNTA280K") //Ponto de Entrada MNTA280 para impedir o fechamento da tela sem finalizar a S.S.
		lImpede := ExecBlock("MNTA280K",.F.,.F., {nChamada})
	EndIf

Return lImpede
