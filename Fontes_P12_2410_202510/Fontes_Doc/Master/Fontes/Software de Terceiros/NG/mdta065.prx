#INCLUDE "MDTA065.ch"
#Include "Protheus.ch"

#DEFINE _nVERSAO 1 //Versao do fonte
/*/


Ŀ
Funo     MDTA065   Autor  Marcio Costa           Data  30/12/99 
Ĵ
Descrio  Programa para cadastrar os Itens dos Exames e o Padrao de  
            normaliade para cada item.                                 
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function MDTA065
//Ŀ
// Armazena variaveis p/ devolucao (NGRIGHTCLICK) 				  		  	  
//
Local aNGBEGINPRM := NGBEGINPRM(_nVERSAO)
Local nTamExa		:= If(TAMSX3("TM4_EXAME")[1] < 1, 6, TAMSX3("TM4_EXAME")[1])

PRIVATE aRotina := MenuDef()

//Ŀ
// Define o cabecalho da tela de atualizacoes                   
//
PRIVATE cCadastro := OemtoAnsi(STR0006) //"Itens do Exame "
PRIVATE aCHKDEL := {}, bNGGRAVA
PRIVATE LEITEM    := .T.
PRIVATE LELIMMAX  := .T.
PRIVATE LELIMMIN  := .T.
PRIVATE LENOMITE  := .T.
PRIVATE cPROGRAMA  := 'MDTA065'

aCHOICE := {}
aVARNAO := {}

aGETNAO := { { "TM8_EXAME" , "M->TM4_EXAME"},;
             { "TM8_NOMEXA" , " "            } }

cGETWHILE := "TM8_FILIAL == xFilial('TM8') .and. TM8_EXAME == M->TM4_EXAME"
cGETMAKE  := "TM4->TM4_EXAME"
cGETKEY   := "M->TM4_EXAME + M->TM8_ITEM"
cGETALIAS := "TM8"
cTUDOOK   := "AllwaysTrue()"
cLINOK    := "AllwaysTrue()"

M->TM8_EXAME := SPACE(nTamExa)

//Ŀ
//aCHKDEL array que verifica a INTEGRIDADE REFERENCIAL na exclu-
//so do registro.                                              
//                                                              
//1 - Chave de pesquisa                                         
//2 - Alias de pesquisa                                         
//3 - Ordem de pesquisa                                         
//

aCHKDEL := { {'TM4->TM4_EXAME'    , "TM5", 2},;
             {'TM4->TM4_EXAME'    , "TM8", 1},;
             {'TM4->TM4_EXAME'    , "TMD", 2},;
             {'TM4->TM4_EXAME'    , "TMB", 2},;
             {'TM4->TM4_EXAME'    , "TN2", 2},;
             {'TM4->TM4_EXAME'    , "TN9", 1},;
             {'TM4->TM4_EXAME'    , "TN8", 2}}

//Ŀ
// Endereca a funcao de BROWSE                                  
//

DbSelectArea("TM4")
DbSetOrder(1)

Set Filter To TM4->TM4_FILIAL == xFilial("TM4") .and. TM4->TM4_INDRES = '3'

mBrowse( 6, 1,22,75,"TM4")
Set Filter To

//Ŀ
// Devolve variaveis armazenadas (NGRIGHTCLICK)                          
//
NGRETURNPRM(aNGBEGINPRM)

Return .T.
/*/


Ŀ
Funo    A065INC    Autor Denis Hyroshi de Souza  Data  26/02/03 
Ĵ
Descrio Inclusao e alteracao                                        
ٱ


/*/
Function A065INC(cAlias,nReg,nOpcx)

Local oDlg, oDlg1, oDlg2, oDlg3, oGet, oCbx, oCbx1, oCbx2, oCbx3, oMenu
Local lKey := .t.
Local aNoFields := {}
Private M->TM4_EXAME  := Space(If( TAMSX3("TM4_EXAME")[1] < 1, 6, TAMSX3("TM4_EXAME")[1] ))
Private M->TM4_NOMEXA := Space(40)
Private M->TM4_DESEXA := Space(100)
Private M->TM4_INDRES := Space(1)
Private M->TM4_ADMISS := Space(1)
Private M->TM4_DEMISS := Space(1)
Private M->TM4_RETORN := Space(1)
dbSelectArea("TM4")
dbSetOrder(1)
nOpcao := nOpcx
cALIAS := "TM8"
bNGGRAVA := {||CHK065GRV()}
//Ŀ
// Monta a entrada de dados do arquivo                          
//
PRIVATE aTELA[0][0],aGETS[0],aHeader[0],nUsado:=0,aCols[0]
bCampo := {|nCPO| Field(nCPO) }

/*
dbSelectArea("SX3")
dbSeek(cAlias)
While !EOF() .And. (x3_arquivo == cAlias)
	IF X3USO(x3_usado) .And. cNivel >= x3_nivel .And.;
		Trim(x3_campo) != "TM8_EXAME"            .And.;
		Trim(x3_campo) != "TM8_NOMEXA"

		nUsado++
		AADD(aHeader,{ TRIM(x3_titulo), x3_campo, x3_picture,;
			x3_tamanho, x3_decimal, x3_valid,;
			x3_usado, x3_tipo, x3_arquivo, x3_context } )
	Endif
	dbSkip()
End

Private aCOLS[1][nUsado+1]

dbSelectArea("SX3")
dbSeek(cAlias)
nUsado:=0
While !EOF() .And. (x3_arquivo == cAlias)
	IF X3USO(x3_usado) .And. cNivel >= x3_nivel .And.;
		Trim(x3_campo) != "TM8_EXAME"            .And.;
		Trim(x3_campo) != "TM8_NOMEXA"

		nUsado++
		IF x3_tipo == "C"
			aCOLS[1][nUsado] := SPACE(x3_tamanho)
		ELSEIF x3_tipo == "N"
    		aCOLS[1][nUsado] := 0
		ELSEIF x3_tipo == "D"
			aCOLS[1][nUsado] := Ctod('  /  /  ')
		ELSEIF x3_tipo == "M"
			aCOLS[1][nUsado] := ""
		ELSE
			aCOLS[1][nUsado] := .F.
		Endif
	Endif
	dbSkip()
End
*/

aResul := {STR0012,STR0013,STR0014,STR0015} // "1=Normal","2=IBMP","3=Itens Exame","4=Audiometrico"
aResul2 := {STR0016,STR0017} // "1=Sim","2=No"
//aCOLS[1][nUsado+1] := .F.
M->TM4_EXAME  := space(If( TAMSX3("TM4_EXAME")[1] < 1, 6, TAMSX3("TM4_EXAME")[1] ))
M->TM4_NOMEXA := space(40)
M->TM4_DESEXA := space(100)
M->TM4_INDRES := "3"
M->TM4_ADMISS := "2"
M->TM4_DEMISS := "2"
M->TM4_RETORN := "2"

If nOpcx = 4
	M->TM4_EXAME  := TM4->TM4_EXAME
	M->TM4_NOMEXA := TM4->TM4_NOMEXA
	M->TM4_DESEXA := TM4->TM4_DESEXA
	If !Empty(TM4->TM4_ADMISS)
		M->TM4_ADMISS := SubStr(aResul2[val(TM4->TM4_ADMISS)],1,1)
	Endif
	If !Empty(TM4->TM4_DEMISS)
		M->TM4_DEMISS := SubStr(aResul2[val(TM4->TM4_DEMISS)],1,1)
	Endif
	If !Empty(TM4->TM4_RETORN)
		M->TM4_RETORN := SubStr(aResul2[val(TM4->TM4_RETORN)],1,1)
	Endif

	lKey := .f.

/*
	aCols	:= MakeGetd( "TM8",TM4->TM4_EXAME,aHeader,"TM8->TM8_FILIAL == xFILIAL('TM8') .AND. TM4->TM4_EXAME == TM8->TM8_EXAME")

	If Empty(aCols)
		aCols := BlankGetD(aHeader)
	EndIf
*/
EndIf

aAdd(aNoFields, 'TM8_EXAME')
aAdd(aNoFields, 'TM8_NOMEXA')

#IFDEF TOP
	cQuery := "SELECT * FROM "+RetSqlName("TM8")+" TM8 WHERE TM8.TM8_FILIAL = '"+xFilial("TM8")+"' AND TM8.TM8_EXAME = '"+TM4->TM4_EXAME+"' AND TM8.D_E_L_E_T_ = ' '"
	FillGetDados( nOpcx, "TM8", 1, xFilial("TM8")+TM4->TM4_EXAME, {|| "TM8->TM8_FILIAL + TM8->TM8_EXAME"}, {|| .T.},aNoFields,,,cQuery)
#ELSE
	cKEY := "TM4->TM4_EXAME"
	cGET := "TM8->TM8_FILIAL == '"+xFilial("TM8")+"' .And. TM8->TM8_EXAME == '" + TM4->TM4_EXAME + "'"
	FillGetDados( nOpcx, "TM8", 1, cKey, {|| }, {|| .T.},aNoFields,,,,{|| NGMontaAcols("TM8",&cKEY,cGET)})
#ENDIF

If Empty(aCols) .Or. nOpcx == 3
   aCols := BlankGetd(aHeader)
EndIf

nOpca := 0
DEFINE MSDIALOG oDlg TITLE cCadastro From 9,12 To 40,109 OF oMainWnd
@ 1.5 ,.8   SAY OemToAnsi(STR0007) Color CLR_HBLUE //"Exame"
@ 1.5 ,5.0  MSGET M->TM4_EXAME  Picture "@!" Valid A065CHAVE(M->TM4_EXAME) SIZE 40,10 WHEN lKey
@ 2.4 ,.8   SAY OemToAnsi(STR0008) Color CLR_HBLUE //"Nome Exame"
@ 2.4 ,5.0  MSGET M->TM4_NOMEXA Picture "@!" Valid SIZE 250,10 WHEN .t.
@ 3.3 ,.8   SAY OemToAnsi(STR0009) //"Desc Exame"
@ 3.3 ,5.0  MSGET M->TM4_DESEXA Picture "@!" SIZE 250,10 WHEN .t.
@ 4.2 ,.8   SAY OemToAnsi(STR0010) Color CLR_HBLUE //"Indic Result"
@ 4.2 ,5.0  COMBOBOX oCbx VAR M->TM4_INDRES ITEMS aResul SIZE 75,60  Of oDlg WHEN .f.

@ 4.2 ,20.8 SAY OemToAnsi(STR0018) // Admissional
@ 4.2 ,25.0 COMBOBOX oCbx1 VAR M->TM4_ADMISS ITEMS aResul2 SIZE 35,60 Of oDlg1 WHEN .t.
@ 5.2 ,.8   SAY OemToAnsi(STR0019) // Demissional
@ 5.2 ,5.0  COMBOBOX oCbx2 VAR M->TM4_DEMISS ITEMS aResul2 SIZE 35,60 Of oDlg2 WHEN .t.
@ 5.2 ,20.8 SAY OemToAnsi(STR0020) // Ret. Trab.
@ 5.2 ,25.0 COMBOBOX oCbx3 VAR M->TM4_RETORN ITEMS aResul2 SIZE 35,60 Of oDlg3 WHEN .t.

dbSelectArea("TM8")

oGet := MSGetDados():New(85,5,230,380,nOpcx,"A065CHKLIN()","AllwaysTrue()","",.t.,,,,300)

NgPopUp(asMenu,@oMenu)
oDlg:bRClicked:= { |o,x,y| oMenu:Activate(x,y,oDlg)}
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpca:=1, If(oGET:TUdoOK(),If(!obrigatorio(aGets,aTela),nOpca := 0,If(!Eval(bNGGRAVA),nOpca := 0,oDlg:End())), nOpca := 0)},{||oDlg:End()})CENTERED
If nOpca == 1
	NG065GRAV()
Endif

bNGGRAVA := {}
Return .T.
/*/


Ŀ
Funo    NG065GRAV  Autor Denis Hyroshi de Souza  Data           
Ĵ
Descrio  Grava os dados no TM8 e TM4                                
Ĵ
 Uso       MDTA630                                                    
ٱ


/*/
Function NG065GRAV()
Local nx ,nMaxArray , aBACK := aCLONE(aCOLS)
Local cAlias := Alias()

DbSelectArea("TM4")
Dbsetorder(1)
If !Dbseek(xFilial("TM4")+M->TM4_EXAME)
	Reclock("TM4",.t.)
Else
	Reclock("TM4",.f.)
EndIf
TM4->TM4_FILIAL := xFilial("TM4")
TM4->TM4_EXAME  := M->TM4_EXAME
TM4->TM4_NOMEXA := M->TM4_NOMEXA
TM4->TM4_DESEXA := M->TM4_DESEXA
TM4->TM4_INDRES := M->TM4_INDRES
TM4->TM4_ADMISS := M->TM4_ADMISS
TM4->TM4_DEMISS := M->TM4_DEMISS
TM4->TM4_RETORN := M->TM4_RETORN
Msunlock("TM4")

//Ŀ
// verifica se o ultimo elemento do array esta em branco        
//
aCOLS := {}
aEVAL(aBACK, {|x| If( !Empty(x[1]),  AAdd(aCOLS,x), NIL) })
nMaxArray := Len(aCols)

nITEM := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TM8_ITEM" })
nDESC := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TM8_NOMITE" })
nUNID := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TM8_UNIDAD" })
nLMIN := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TM8_LIMMIN" })
nLMAX := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TM8_LIMMAX" })

For nx = 1 to nMaxArray
	If !empty(aCols[nx][nITEM]) .and.;
		!aCols[nx][Len(aCols[nx])]

		Dbselectarea("TM8")
		Dbsetorder(1)
		If !Dbseek(xFilial("TM8")+M->TM4_EXAME+aCols[nx][nITEM])
			RecLock("TM8",.T.)
		Else
			RecLock("TM8",.f.)
		EndIf
		TM8->TM8_FILIAL := xFilial("TM8")
		TM8->TM8_EXAME  := M->TM4_EXAME
		TM8->TM8_ITEM   := aCols[nx][nITEM]
		TM8->TM8_NOMITE := aCols[nx][nDESC]
		TM8->TM8_UNIDAD := aCols[nx][nUNID]
		TM8->TM8_LIMMIN := aCols[nx][nLMIN]
		TM8->TM8_LIMMAX := aCols[nx][nLMAX]
		Msunlock("TM8")
	ElseIf !empty(aCols[nx][nITEM]) .and.;
		    aCols[nx][Len(aCols[nx])]

		Dbselectarea("TM8")
		Dbsetorder(1)
		If Dbseek(xFilial("TM8")+M->TM4_EXAME+aCols[nx][nITEM])
			RecLock("TM8",.f.)
			DbDelete()
			MsUnLock("TM8")
		EndIf
	Endif
Next nx
DbSelectArea(cAlias)
Return .T.
/*/


Ŀ
Funo    D065CHK    Autor  Marcio Costa           Data  03/01/00 
Ĵ
Descrio  Consiste a existencia de outro codigo na GetDados          
ٱ


/*/
Function D065CHK()
Local xx := 0, npos, lRET := .T.
Local nX

nPOS := aSCAN( aHEADER, { |x| Trim( Upper(x[2]) ) == "TM8_ITEM"})

If nPOS > 0
   For nX := 1 to Len(aCOLS)
       If aCOLS[nX][nPOS] == M->TM8_ITEM  .And. nX != n .And. !aCols[nx][Len(aCols[nx])]
          xx := xx + 1
       Endif
   Next
Endif

If xx > 0
   Help(" ",1,"JAEXISTINF")
   lRet := .f.
Endif

If M->TM4_INDRES <> "3"
   Help(" ",1,"MSG_INFINV")
   lRet := .f.
Endif


Return lRET
/*/


Ŀ
Funo    D065CHKSEC Autor Denis Hyroshi de Souza  Data  20/11/02 
Ĵ
Descrio  Consiste a existencia de outro codigo na GetDados          
ٱ


/*/
Function D065CHKSEC()
Local lRET := .T.

Dbselectarea("TM8")
Dbsetorder(1)
If Dbseek(xFilial("TM8")+M->TM8_EXAME+M->TM8_ITEM)
	Help(" ",1,"JAEXISTINF")
	lRet := .f.
Endif

Dbselectarea("TM4")
Dbsetorder(1)
Dbseek(xFilial("TM4")+M->TM8_EXAME)
If TM4->TM4_INDRES <> "3"
	Help(" ",1,"MSG_INFINV")
	lRet := .f.
Endif

Return lRET
//
//VERIFICA SE PODE GRAVAR
//
Function CHK065GRV()
Local lRet := .t.

//If !ExistChav("TM4",M->TM4_EXAME)
//	HELP(" ",1,"JAGRAVADO")
//	lRet := .f.
//Endif
If Empty(M->TM4_EXAME)
	HELP(" ",1,"OBRIGAT")
	lRet := .f.
Elseif Empty(M->TM4_NOMEXA)
	HELP(" ",1,"OBRIGAT")
	lRet := .f.
Endif
Return lRet
//
//VERIFICA SE JA EXISTE EXAME GRAVADO COM O CODIGO INFORMADO
//
Function A065CHAVE(cExame_065)
Local LRet := .t.

DbSelectArea("TM4")
DbSetOrder(1)
Set Filter To

If !ExistChav("TM4",cExame_065)
	LRet := .f.
Endif
/*If Empty(cExame_065)
	HELP(" ",1,"NVAZIO")
	LRet := .f.
Endif */

DbSelectArea("TM4")
DbSetOrder(1)
Set Filter To TM4->TM4_FILIAL == xFilial("TM4") .and. TM4->TM4_INDRES = '3'

Return LRet

/*/


Ŀ
Funo    A065LIMMIN Autor Denis Hyroshi de Souza  Data  01/09/03 
Ĵ
Descrio  Retorna o conteudo do campo LIMMIN                         
ٱ


/*/
Function A065LIMMIN()
Local nLMIN := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TM8_LIMMIN" })
Return aCols[n][nLMIN]
/*/


Ŀ
Funo    A065CHKLIN Autor Denis Hyroshi de Souza  Data  04/12/03 
Ĵ
Descrio  Verifica se o conteudo LIMMIN e' memor que o LIMAX         
ٱ


/*/
Function A065CHKLIN()
default n := 1

If acols[n][len(Acols[n])]
	Return .T.
EndIf

If (nMIN := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TM8_LIMMIN" })) < 1
	Return .T.
Endif
If (nMAX := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TM8_LIMMAX" })) < 1
	Return .T.
Endif

If aCols[n][nMAX] < aCols[n][nMIN]
	Msginfo(STR0011) //"O Limite Maximo nao pode ser menor que o Limite Minimo."
	Return .F.
Endif
If (nNOME := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TM8_NOMITE" })) < 1
	Return .T.
Endif

If Empty(aCols[n][nNOME])
	Msginfo(STR0021) //"O Nome Item deve ser informado."
	Return .F.
Endif
If (nITEM := aSCAN(aHEADER, {|x| AllTrim(Upper(X[2])) == "TM8_ITEM" })) < 1
	Return .T.
Endif
If Empty(aCols[n][nITEM])
	Msginfo(STR0022) //"O Cdigo do Item deve ser informado."
	Return .F.
Endif
Return .T.

/*/


Ŀ
Funo     MenuDef   Autor  Rafael Diogo Richter   Data 29/11/2006
Ĵ
Descrio Utilizacao de Menu Funcional.                               
Ĵ
 Uso       SigaMDT                                                    
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    F.O    Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


/*/
Static Function MenuDef()
Local aRotina :=	{ { STR0001, "AxPesqui"  , 0 , 1},; //"Pesquisar"
                    { STR0002,"NGCAD02"   , 0 , 2},; //"Visualizar"
                    { STR0003,   "A065INC"   , 0 , 3},; //"Incluir"
                    { STR0004,   "A065INC"   , 0 , 4},; //"Alterar"
                    { STR0005,   "NGCAD02"   , 0 , 5, 3} } //"Excluir"

Return aRotina
