#INCLUDE "PROTHEUS.CH"
/*


Ŀ
Funo     MNTV029     Autor  Marcos Wagner Junior   Data 01/03/2010
Ĵ
Descrio Quantidade de pontos perdidos pelos motoristas no periodo     
Ĵ
Parametros De_Data    - Data inicio                                     
           Ate_Data   - Ate data                                        
Ĵ
Retorna    nQtdLt  - Litros total                                       
Ĵ
 Uso       GENERICO                                                     
ٱ


*/
Function MNTV029(De_Data,Ate_Data)
Local aAreaOLD   := GetArea(),nQtdPontos := 0

// Variveis de Histrico de Indicadores
Local lMV_HIST := NGI6MVHIST()
Local aParams := {}
Local cCodIndic := "MNTV029"
Local nResult := 0

// Armazena os Parmetros
If lMV_HIST
	aParams := {}
	aAdd(aParams, {"DE_DATA" , De_Data})
	aAdd(aParams, {"ATE_DATA", Ate_Data})
	NGI6PREPPA(aParams, cCodIndic)
EndIf

If ValType(De_Data) != "D" .or. ValType(Ate_Data) != "D"
	NGI6PREPVA(cCodIndic, nResult)
	Return nResult
Endif

cAliasQry := GetNextAlias()
// Query
If lMV_HIST
	cQuery := "SELECT * "
Else
	cQuery := "SELECT TSH.TSH_PONTOS "
EndIf
cQuery += " FROM " + RetSqlName("TSH") + " TSH ," + RetSqlName("TRX") + " TRX "
cQuery += " JOIN " + RetSqlName("DA4") + " DA4 ON TRX.TRX_CODMO = DA4.DA4_COD "
If NGSX2MODO("TRX") == NGSX2MODO("DA4")
	cQuery += " AND TRX.TRX_FILIAL = DA4.DA4_FILIAL "
Else
	cQuery += " AND DA4.DA4_FILIAL = '"+xFilial("DA4")+"'"
Endif
cQuery += " AND DA4.D_E_L_E_T_ <> '*' "
cQuery += " AND DA4.DA4_TIPMOT = '1' "
cQuery += " WHERE TSH.TSH_CODINF = TRX.TRX_CODINF "
If ValType(De_Data) == "D"                                    
	cQuery += " AND TRX.TRX_DTINFR >= '"+Dtos(De_Data)+"' "
Endif
If ValType(Ate_Data) == "D"
	cQuery += " AND TRX.TRX_DTINFR <= '"+Dtos(Ate_Data)+"' "
Endif
If NGSX2MODO("TRX") == NGSX2MODO("TSH")
	cQuery += " AND TRX.TRX_FILIAL = TSH.TSH_FILIAL "
	cQuery += " AND TRX.TRX_FILIAL = '"+xFilial("TRX")+"' "
Else
	cQuery += " AND TRX.TRX_FILIAL = '"+xFilial("TRX")+"'"
	cQuery += " AND TSH.TSH_FILIAL = '"+xFilial("TSH")+"'"
Endif
cQuery += " AND   TRX.D_E_L_E_T_ <> '*' "
cQuery += " AND   TSH.D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)
NGI6PREPDA(cAliasQry, cCodIndic)

dbSelectArea(cAliasQry)
dbGoTop()
While !Eof()
   nQtdPontos += Val((cAliasQry)->TSH_PONTOS)
   dbSkip()
End
(cAliasQry)->(dbCloseArea())

// RESULTADO
nResult := nQtdPontos
NGI6PREPVA(cCodIndic, nResult)

RestArea(aAreaOLD)
Return nResult