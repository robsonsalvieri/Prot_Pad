#INCLUDE "MNTR420.ch"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MNTR420   ³ Autor ³ Marcos Wagner Junior  ³ Data ³ 21/09/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Relatorio de Bens transferidos entre filiais                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/      
Function MNTR420()  

	WNREL      := "MNTR420"
	LIMITE     := 182
	cDESC1     := STR0001 //"O relatorio apresentará um quadro comparativo das divergências "
	cDESC2     := STR0002 //"encontradas na última inspeção em relação com ao que havia no "
	cDESC3     := STR0003 //"sistema anteriormente."
	cSTRING    := "TQF" 

	Private NOMEPROG := "MNTR420"
	Private TAMANHO  := "G"
	Private aRETURN  := {STR0004,1,STR0005,1,2,1,"",1} //"Zebrado"###"Administracao"
	Private TITULO   := STR0006 //"Divergências Estruturais Após Inspeção"
	Private nTIPO    := 0
	Private nLASTKEY := 0
	Private CABEC1,CABEC2 
	Private aVETINR := {}    
	Private cPerg := "MNR420"   
	Private aPerg :={}

	SetKey( VK_F9, { | | NGVersao( "MNTR420" , 1 ) } )

	Pergunte(cPERG,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
	WNREL:=SetPrint(cSTRING,WNREL,cPERG,TITULO,cDESC1,cDESC2,cDESC3,.F.,"")
	If nLASTKEY = 27
		Set Filter To
		DbSelectArea("TR9")  
		Return
	EndIf     
	SetDefault(aReturn,cSTRING)
	RptStatus({|lEND| MNTR420IMP(@lEND,WNREL,TITULO,TAMANHO)},STR0010,STR0011) //"Aguarde..."###"Processando Registros..."
	Dbselectarea("TR9")  

Return .T.    

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    |MNR420IMP | Autor ³ Marcos Wagner Junior  ³ Data ³14/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Chamada do Relat¢rio                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR420                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MNTR420IMP(lEND,WNREL,TITULO,TAMANHO)
	Local nI := 0
	Private cRODATXT := ""
	Private nCNTIMPR := 0     
	Private li := 80 ,m_pag := 1    
	Private aDiverge := {}

	nTIPO  := IIf(aReturn[4]==1,15,18)

	CABEC1 := STR0012 //"N.Fogo       Filial                 Frota              Sulco    Posicao    Filial                 Frota              Sulco    Posicao   Divergência encontrada"
	CABEC2 := STR0013 //"             ----------------------Última Inspeção---------------------    --------------------Estrutura Anterior--------------------"

	/*
	1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7         8         9         0         1         2  
	0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
	**************************************************************************************************************************
	Relatorio de Pneus    
	*******************************************************************************************************************************************************************************************************************************
	----------------------Última Inspeção---------------------    --------------------Estrutura Anterior--------------------
	Ordem: xxxxxx / Plano: xxxxxx (99/99/99)
	N.Fogo       Filial                 Frota              Sulco    Posicao    Filial                 Frota              Sulco    Posicao   Divergência encontrada
	xxxxxxxxxx   xx - xxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxx   999.99   xxxxxx     xx - xxxxxxxxxxxxxxx   xxxxxxxxxxxxxxxx   999.99   xxxxxx
	*/

	cAliasQry3 := GetNextAlias()
	cQuery3 := " SELECT * "
	cQuery3 += " FROM "+RetSqlName("TR9")+" TR9 "
	cQuery3 += " WHERE TR9.TR9_FILIAL >= '"+MV_PAR01+"' AND TR9.TR9_FILIAL <= '" +MV_PAR02+"' 
	cQuery3 += " AND   TR9.TR9_DTINSP >= '"+DTOS(MV_PAR03)+"'"
	cQuery3 += " AND   TR9.TR9_STATUS = '4' "
	cQuery3 += " AND   TR9.D_E_L_E_T_ <> '*' "
	cQuery3 += " ORDER BY TR9.TR9_FROTA, TR9.TR9_DTINSP, TR9.TR9_HRINSP "
	cQuery3 := ChangeQuery(cQuery3)
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery3),cAliasQry3, .F., .T.)

	aRetorno := {}
	SetRegua(LastRec())                                                                 
	DbGoTop()
	While !Eof()
		nPos := aSCAN(aRetorno,{|x| x[1] = (cAliasQry3)->TR9_FROTA})
		If nPos == 0
			Aadd(aRetorno,{(cAliasQry3)->TR9_FROTA,(cAliasQry3)->R_E_C_N_O_})
		Else
			aRetorno[nPos][2] := (cAliasQry3)->R_E_C_N_O_
		Endif
		dbSkip()
	End
	(cAliasQry3)->(dbCloseArea())

	nCont := 0
	If Len(aRetorno) > 0
		cAliasQry := GetNextAlias()
		cQuery := " SELECT * "
		cQuery += " FROM "+RetSqlName("TRC")+" TRC ,"+RetSqlName("TR9")+" TR9 "
		cQuery += " WHERE TR9.TR9_FILIAL = TRC.TRC_FILIAL "
		cQuery += " AND   TR9.TR9_ORDEM  = TRC.TRC_ORDEM "
		cQuery += " AND   TR9.TR9_CICLO  = TRC.TRC_CICLO "
		cQuery += " AND   TRC.D_E_L_E_T_ <> '*' "
		cQuery += " AND   TR9.D_E_L_E_T_ <> '*' "
		cQuery += " AND   TR9.R_E_C_N_O_ IN ("
		For nI := 1 to Len(aRetorno)
			If nCont > 0
				cQuery += ","
			Endif
			cQuery += AllTrim(Str(aRetorno[nI][2]))
			nCont++
		Next
		cQuery += ")"
		cQuery += " ORDER BY TR9.TR9_FILIAL, TR9.TR9_FROTA "
		cQuery := ChangeQuery(cQuery)
		dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),cAliasQry, .F., .T.)

		dbGoTop()
		While !Eof()
			For nI := 1 to 10
				cFilialQry := (cAliasQry)->TRC_FILIAL
				cBemPaiQry := (cAliasQry)->TR9_FROTA
				If nI <= 9
					cPosicaoQry := "(cAliasQry)->TRC_LOCPN"+AllTrim(Str(nI))
					cPneuQry    := "(cAliasQry)->TRC_NFOGO"+AllTrim(Str(nI))
				Else
					cPosicaoQry := "(cAliasQry)->TRC_LOCP"+AllTrim(Str(nI))
					cPneuQry    := "(cAliasQry)->TRC_NFOG"+AllTrim(Str(nI))
				Endif
				If !Empty(&cPneuQry)
					nSulcoQry  := MNTR420SUL(nI)
					nSulcoSis  := '  '
					cBemPaiSis := '  '
					cLocaliSis := '  '
					cFilialSis := '  '

					lAchouTQV  := .f.
					dbSelectArea("TQV")
					dbSetOrder(01)
					If dbSeek((cAliasQry)->TRC_FILIAL+&cPneuQry)
						While !Eof() .AND. TQV->TQV_FILIAL == (cAliasQry)->TRC_FILIAL .AND. AllTrim(TQV->TQV_CODBEM) == AllTrim(&cPneuQry)
							If TQV->TQV_DTMEDI < STOD((cAliasQry)->TR9_DTINSP)
								nSulcoSis := TQV->TQV_SULCO
								lAchouTQV  := .t.
							ElseIf (TQV->TQV_DTMEDI == STOD((cAliasQry)->TR9_DTINSP)) .AND. (TQV->TQV_HRMEDI <= (cAliasQry)->TR9_HRINSP)
								nSulcoSis := TQV->TQV_SULCO
								lAchouTQV  := .t.
							Else
								Exit
							Endif
							dbSkip()
						End				
					Endif
					If !lAchouTQV
						nSulcoSis := nSulcoQry
					Endif

					cMSGSulco  := ''
					cMSGPosica := ''
					cMSGBem    := ''
					cMSGFilial := ''
					lAchouSTZ  := .f.
					dbSelectArea("STZ")
					dbSetOrder(02)
					If dbSeek((cAliasQry)->TRC_FILIAL+&cPneuQry+Space(16-Len(&cPneuQry))+(cAliasQry)->TR9_DTINSP+'E'+(cAliasQry)->TR9_HRINSP)
						cAliasQry2 := GetNextAlias()
						cQuery2 := " SELECT STZ.TZ_BEMPAI, STZ.TZ_LOCALIZ "
						cQuery2 += " FROM "+RetSqlName("STZ")+" STZ "
						cQuery2 += " WHERE STZ.TZ_DATASAI = '"+(cAliasQry)->TR9_DTINSP+"'"
						cQuery2 += " AND   STZ.TZ_HORASAI = '"+(cAliasQry)->TR9_HRINSP+"'"
						cQuery2 += " AND   STZ.TZ_CODBEM  = '"+&cPneuQry+"'"
						cQuery2 += " AND   STZ.TZ_TIPOMOV = 'S'"
						cQuery2 += " AND   STZ.D_E_L_E_T_ <> '*' "
						cQuery2 := ChangeQuery(cQuery2)
						dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery2),cAliasQry2, .F., .T.)
						dbGoTop()
						If !Eof()
							lAchouSTZ := .t.
							cBemPaiSis := (cAliasQry2)->TZ_BEMPAI
							cLocaliSis := (cAliasQry2)->TZ_LOCALIZ
							If cBemPaiSis != cBemPaiQry
								cMSGBem    := STR0014 //"Bem pai do sistema foi alterado em relação a inspeção"
							Endif
							If cLocaliSis != &cPosicaoQry
								cMSGPosica := STR0015 //"Posição do sistema foi alterada em relação a inspeção"
							Endif
						Endif
						(cAliasQry2)->(dbCloseArea())
					Endif
					If !lAchouSTZ
						cBemPaiSis := cBemPaiQry
						cLocaliSis := &cPosicaoQry
					Endif

					lFilialSis := .f.
					cAliasQry2 := GetNextAlias()
					cQuery2 := " SELECT STZ.TZ_FILIAL, STZ.TZ_DATASAI, STZ.TZ_HORASAI "
					cQuery2 += " FROM "+RetSqlName("STZ")+" STZ "
					cQuery2 += " WHERE STZ.TZ_DATASAI = '"+(cAliasQry)->TR9_DTINSP+"'"
					cQuery2 += " AND   STZ.TZ_HORASAI = '"+(cAliasQry)->TR9_HRINSP+"'"
					cQuery2 += " AND   STZ.TZ_CODBEM  = '"+&cPneuQry+"'"
					cQuery2 += " AND   STZ.TZ_TIPOMOV = 'S'"
					cQuery2 += " AND   STZ.D_E_L_E_T_ <> '*' "
					cQuery2 += " ORDER BY STZ.TZ_DATASAI, STZ.TZ_HORASAI "
					cQuery2 := ChangeQuery(cQuery2)
					dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery2),cAliasQry2, .F., .T.)
					While !Eof()
						If (cAliasQry2)->TZ_DATASAI < (cAliasQry)->TR9_DTINSP
							cFilialSis := (cAliasQry2)->TZ_FILIAL
							lFilialSis := .t.
						ElseIf (cAliasQry2)->TZ_DATASAI == (cAliasQry)->TR9_DTINSP .AND. (cAliasQry2)->TZ_HORASAI <= (cAliasQry)->TR9_HRINSP
							cFilialSis := (cAliasQry2)->TZ_FILIAL
							lFilialSis := .t.
						Endif
						dbSkip()
					End
					(cAliasQry2)->(dbCloseArea())
					If !lFilialSis
						cFilialSis := cFilialQry
					Endif

					If cFilialSis != cFilialQry
						cMSGFilial := STR0016 //"Filial do sistema foi alterada em relação a inspeção"
						Aadd(aDiverge,{&cPneuQry,(cAliasQry)->TR9_DTINSP,(cAliasQry)->TR9_ORDEM,(cAliasQry)->TR9_PLANO,cFilialSis,;
						cFilialQry,cBemPaiSis,cBemPaiQry,cLocaliSis,&cPosicaoQry,nSulcoSis,nSulcoQry,cMSGFilial})
					Endif				
					If !Empty(cMSGBem)
						Aadd(aDiverge,{&cPneuQry,(cAliasQry)->TR9_DTINSP,(cAliasQry)->TR9_ORDEM,(cAliasQry)->TR9_PLANO,cFilialSis,;
						cFilialQry,cBemPaiSis,cBemPaiQry,cLocaliSis,&cPosicaoQry,nSulcoSis,nSulcoQry,cMSGBem})
					Endif
					If !Empty(cMSGPosica)
						Aadd(aDiverge,{&cPneuQry,(cAliasQry)->TR9_DTINSP,(cAliasQry)->TR9_ORDEM,(cAliasQry)->TR9_PLANO,cFilialSis,;
						cFilialQry,cBemPaiSis,cBemPaiQry,cLocaliSis,&cPosicaoQry,nSulcoSis,nSulcoQry,cMSGPosica})
					Endif
					If !Empty(nSulcoSis) .AND. nSulcoSis != nSulcoQry
						cMSGSulco := STR0017 //"Sulco do sistema foi alterado em relação a inspeção"
						Aadd(aDiverge,{&cPneuQry,(cAliasQry)->TR9_DTINSP,(cAliasQry)->TR9_ORDEM,(cAliasQry)->TR9_PLANO,cFilialSis,;
						cFilialQry,cBemPaiSis,cBemPaiQry,cLocaliSis,&cPosicaoQry,nSulcoSis,nSulcoQry,cMSGSulco})
					Endif
				Endif
			Next
			DbSelectArea(cAliasQry)
			DbSkip()
		End
		(cAliasQry)->(dbCloseArea())
	Endif

	cBemAntigo := '' 
	cOrdemOld  := ''
	cPlanoOld  := ''
	If Len(aDiverge) > 0
		For nI := 1 to Len(aDiverge)
			NgSomaLi(58)
			If cOrdemOld != aDiverge[nI][3] .OR. cPlanoOld != aDiverge[nI][4]
				If nI != 1
					NgSomaLi(58)
				Endif
				@ Li,022		 PSay STR0018+aDiverge[nI][3]+STR0019+aDiverge[nI][4]+' ('+DTOC(STOD(aDiverge[nI][2]))+')' //"Ordem: "###" / Plano: "
				NgSomaLi(58)
				NgSomaLi(58)
			Endif
			If cBemAntigo != aDiverge[nI][1]
				@ Li,000		 PSay aDiverge[nI][1]
				@ Li,013 	 Psay aDiverge[nI][6] + ' - '
				DbSelectArea("SM0")
				DbSetOrder(01)
				If DbSeek(SM0->M0_CODIGO+aDiverge[nI][6])
					@ Li,018   Psay SM0->M0_FILIAL
				Endif
				@ Li,036		 PSay aDiverge[nI][8]
				@ Li,055		 PSay aDiverge[nI][12]
				@ Li,064		 PSay aDiverge[nI][10]
				@ Li,075		 PSay aDiverge[nI][5] + ' - '
				DbSelectArea("SM0")
				DbSetOrder(01)
				If DbSeek(SM0->M0_CODIGO+aDiverge[nI][5])
					@ Li,080   Psay SM0->M0_FILIAL
				Endif
				@ Li,098		 PSay aDiverge[nI][7]
				@ Li,117		 PSay aDiverge[nI][11]
				@ Li,126		 PSay aDiverge[nI][9]
			Endif
			@ Li,136		 PSay aDiverge[nI][13]
			cBemAntigo := aDiverge[nI][1]
			cOrdemOld  := aDiverge[nI][3]
			cPlanoOld  := aDiverge[nI][4]
		Next
	Else
		MsgInfo(STR0020,STR0021)//"Não existem dados para montar o relatório."###"ATENÇÃO"
		Return .f.
	Endif

	RODA(nCNTIMPR,cRODATXT,TAMANHO)       

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Devolve a condicao original do arquivo principal             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex('ST9')
	Set Filter To
	Set Device To Screen
	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(WNREL)
	EndIf
	MS_FLUSH()

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |MNR420FL  | Autor ³Marcos Wagner Junior   ³ Data ³ 07/12/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o |Valida codigos de Filial, Ate Filial 					        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR420                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNR420FL(nOpc,cParDe,cParAte)

	Local nTamAte := Posicione("SX1",1,"MNR420"+Space(4)+'02',"X1_TAMANHO")

	If Empty(cParDe) .AND. ( cParAte == Replicate('Z', nTamAte ) )
		Return .t.
	Else
		If nOpc == 1  
			If Empty(cParDe)
				Return .t.
			Else
				lRet := IIf(Empty(cParDe),.t.,ExistCpo('SM0',SM0->M0_CODIGO+cParDe))
				If !lRet
					Return .f.
				EndIf      
			Endif
		Elseif nOpc == 2  
			If ( cParAte == Replicate('Z', nTamAte ) )
				Return .t.
			Else
				lRet := IIF(ATECODIGO('SM0',SM0->M0_CODIGO+cParDe,SM0->M0_CODIGO+cParAte,03),.T.,.F.)
				If !lRet
					Return .f.
				EndIf
			Endif
		EndIf
	EndIf          

Return .t.       
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |MNR420CC  | Autor ³Marcos Wagner Junior   ³ Data ³ 21/09/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o |Valida todos codigos De... , Ate..., com excessao da Filial ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR420                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MNR420CC(nOpc,cParDe,cParAte,cTabela)  

	If (Empty(cParDe) .AND. cParAte = 'ZZ' ) .OR. (Empty(cParDe) .AND. cParAte = 'ZZZZZZ' ) 
		Return .t.
	Else
		If nOpc == 1
			If Empty(cParDe)
				Return .t.
			Else		
				lRet := IIf(Empty(cParDe),.t.,ExistCpo(cTabela,cParDe))
				If !lRet
					Return .f.
				EndIf 
			Endif 
		ElseIf nOpc == 2      
			If (cParAte == 'ZZ') .OR. (cParAte == 'ZZZZZZ') 
				Return .t.
			Else
				lRet := IIF(ATECODIGO(cTabela,cParDe,cParAte,10),.T.,.F.)
				If !lRet 
					Return .f.
				EndIf  
			EndIf
		EndIf    
	Endif

Return .t.                                 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MNTR420SUL³ Autor ³Marcos Wagner Junior   ³ Data ³ 07/12/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Busca a menor das 3 medicoes de sulco do pneu               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MNTR420                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MNTR420SUL(nNumero)
	Local nX := 0
	Local nMedida := 1000
	Local nMedMin := 1000

	For nX := 1 to 3
		cMedida := '(cAliasQry)->TRC_PN'+Alltrim(Str(nNumero))+'M'+Alltrim(Str(nX))
		nMedMin := &cMedida
		nMedida := MIN(nMedida,nMedMin)
	Next

Return nMedida
