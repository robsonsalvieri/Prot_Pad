#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"

#include "manufacturing.comex.drawback.smartview.purchaseditems.ch"

namespace totvs.protheus.manufacturing.comex.drawback.smartview

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEDC", tables="SW6, SW8, SW9", name="Itens Comprados", country="ALL", initialRelease="12.1.2210")

/*/{Protheus.doc} ItensCompradosTReportsBusinessObject
Classe para criação do Objeto de Negócio para a listagem dos ItensComprados (Antigo EICIC150)
@author tiago tudisco
@since 06/10/2023
/*/
class ItensCompradosTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass

/*/{Protheus.doc} new
Método de instância da classe
@author tiago tudisco
@since 06/10/2023
/*/ 
method new() class ItensCompradosTReportsBusinessObject
_Super:new()
//Define a Área
self:appendArea("Easy Drawback Control")
 
//Define o nome do Objeto de Negócio
self:setDisplayName(STR0001) // "Itens Comprados"
 
//Define a descrição do Objeto de Negócio
self:setDescription(STR0002)// ""Este relatório emitirá em período determinado, os itens importados.""

//Define qual grupo de pergunte será utilizado no relatório
self:setPergunte("EICIC150")

return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@author tiago tudisco
@since 06/10/2023
/*/ 
method getData(nPage as numeric, oFilter as object) as object class ItensCompradosTReportsBusinessObject
local jParams  as json
local aUserFil as array

//Define a quantidade máxima por página (Default 100)
self:setPageSize(100)
 
jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

MV_PAR01 := AvKey(jParams["MV_PAR01"][1],"W8_FILIAL") // Filial de ?
MV_PAR02 := AvKey(jParams["MV_PAR02"][1],"W8_FILIAL") // Filial até ?
MV_PAR03 := IIF(!Empty(jParams["MV_PAR03"][1]), TEDtConvSM(jParams["MV_PAR03"][1]),"") // Data Registro de ?
MV_PAR04 := IIF(!Empty(jParams["MV_PAR04"][1]), TEDtConvSM(jParams["MV_PAR04"][1]),"") // Data Registro até ?
MV_PAR05 := AvKey(jParams["MV_PAR05"][1],"B1_COD") // Produto de ?
MV_PAR06 := AvKey(jParams["MV_PAR06"][1],"B1_COD") // Produto até ?
MV_PAR07 := if( valtype(jParams["MV_PAR07"][1]) == "N", jParams["MV_PAR07"][1], val(jParams["MV_PAR07"][1])) // Consiste estrutura ?

aUserFil := TEgetUsrFil("SW6", MV_PAR01, MV_PAR02) //Retorna quais filiais serão executadas com base no pergunte Filial de ? / Filial até ?

setPItems(@self:oData, aUserFil)

return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@author tiago tudisco
@since 06/10/2023
/*/ 
method getSchema() as object class ItensCompradosTReportsBusinessObject
local aFieldsSW6 as array
local aFieldsSW8 as array
local aFieldsSW9 as array
local aFieldsSB1 as array
local aFieldsED0 as array
local aFieldsSWN as array
local aFieldsSYT as array

local aTableCustom as array
local aCustomFld as array
local nI as numeric
local nY as numeric

aFieldsSW6 := getCampos("SW6")
aFieldsSW8 := getCampos("SW8")
aFieldsSW9 := getCampos("SW9")
aFieldsSB1 := getCampos("SB1")
aFieldsED0 := getCampos("ED0")
aFieldsSYT := getCampos("SYT")
aFieldsSWN := {"WN_DOC","WN_SERIE","WN_QUANT","WN_LOTECTL","WN_NUMLOTE","WN_DTVALID","WN_TIPO_NF"}

aTableCustom := {"SW6","SW8","SW9"}
For nI := 1 To Len(aTableCustom)
    aCustomFld := TECustomCpo(aTableCustom[nI], .F.)
    If !Empty(aCustomFld)
        For nY := 1 To Len(aCustomFld)
            aAdd(&("aFields"+aCustomFld[nY][5]), aCustomFld[nY][1])
        Next
    EndIf
Next

self:oSchema:aliasToSchema("SW6", aFieldsSW6)
self:oSchema:aliasToSchema("SW8", aFieldsSW8)
self:oSchema:aliasToSchema("SW9", aFieldsSW9)
self:oSchema:aliasToSchema("SB1", aFieldsSB1)
self:oSchema:aliasToSchema("SWN", aFieldsSWN)
self:oSchema:aliasToSchema("SYT", aFieldsSYT)

self:oSchema:addProperty("WKEMPFIL"         , STR0003  , "string"  , STR0003   , "WKEMPFIL"    ) //"Empresa"
self:oSchema:addProperty("W8_VLMOE"         , STR0004  , "number"  , STR0004   , "W8_VLMOE"     ) //"Valor Moeda"
self:oSchema:addProperty("W8_VLREAL"        , STR0005  , "number"  , STR0005   , "W8_VLREAL"    ) //"Valor Real"
self:oSchema:addProperty("NFORNECEDOR"      , STR0006  , "string"  , STR0006   , "NFORNECEDOR"  ) //"Nome Fornecedor"
self:oSchema:addProperty("NFABRICANTE"      , STR0007  , "string"  , STR0007   , "NFABRICANTE"  ) //"Nome Fabricante"
self:oSchema:addProperty("DESC_MODAL_AC"    , STR0008  , "string"  , STR0008   , "DESC_MODAL_AC") //"Descrição Modalidade A/C"
self:oSchema:addProperty("W6_DESC_TIPOREG"  , STR0009  , "string"  , STR0009   , "W6_DESC_TIPOREG") //"Desc Tipo de Registro"
self:oSchema:addProperty("WN_DESC_TIPONF"   , STR0012  , "string"  , STR0012   , "WN_DESC_TIPONF" ) //"Desc Tipo NF"

return self:oSchema


/*/{Protheus.doc} setPItems
@author Tiago Tudisco
@since 14/09/2023
/*/ 
Static Function setPItems(oDados, aUserFil)
local cBkpFil:= cFilAnt as character
local nI                as numeric
local aCamposQry        as array

aCamposQry := getCampos("ALL")
For nI := 1 To Len(aUserFil)
    cFilAnt := aUserFil[nI]
    TrataDados(@oDados, aCamposQry)
Next

If cFilAnt <> cBkpFil
    cFilAnt := cBkpFil
EndIf

Return

Static Function TrataDados(oDados, aCamposQry)
Local jItens     as json
Local cAlias     as character
Local aCustomFld as Array
Local nI         as numeric

aCustomFld := TECustomFld({"SW6","SW8","SW9"}) //Pega os campos customizados
cALias := getNextAlias()
getQryPed(cAlias, aCustomFld, aCamposQry)

While (cAlias)->(!Eof())

    jItens := JsonObject():new()
    
    jItens["WKEMPFIL"]       := AllTrim(FwCodFil()) + "-" + FwFilName(FwCodEmp(), FwCodFil())
    jItens["W8_VLMOE"]      := (cAlias)->(W8_VLMOE)
    jItens["W8_VLREAL"]     := (cAlias)->(W8_VLMOE) * (cAlias)->(W6_TX_US_D)
    jItens["NFORNECEDOR"]   := (cAlias)->(NFORNECEDOR)
    jItens["NFABRICANTE"]   := (cAlias)->(NFABRICANTE)
    jItens["DESC_MODAL_AC"] := (cAlias)->(DESC_MODAL_AC)
    jItens["W6_DESC_TIPOREG"]:=(cAlias)->(W6_DESC_TIPOREG)
    jItens["WN_DOC"]        := (cAlias)->(WN_DOC)
    jItens["WN_SERIE"]      := (cAlias)->(WN_SERIE)
    jItens["WN_QUANT"]      := (cAlias)->(WN_QUANT)
    jItens["WN_LOTECTL"]    := (cAlias)->(WN_LOTECTL)
    jItens["WN_NUMLOTE"]    := (cAlias)->(WN_NUMLOTE)
    jItens["WN_DTVALID"]    := IIF(!Empty((cAlias)->(WN_DTVALID)),FwTimeStamp( 5, (cAlias)->(WN_DTVALID)),)
    jItens["WN_TIPO_NF"]    := (cAlias)->(WN_TIPO_NF)
    jItens["WN_DESC_TIPONF"]:= (cAlias)->(WN_DESC_TIPONF)

    For nI := 1 to len(aCamposQry)
        cTipo := GetSx3Cache(aCamposQry[nI], "X3_TIPO")
        If cTipo == "D"
            jItens[aCamposQry[nI]] := IIF(!Empty((cAlias)->&(aCamposQry[nI])),FwTimeStamp( 5, (cAlias)->&(aCamposQry[nI])),)
        Else
            jItens[aCamposQry[nI]] := (cAlias)->&(aCamposQry[nI])
        EndIf
    Next

    //Tratamento para campos customizados
    For nI := 1 to len(aCustomFld)
        If aCustomFld[nI][2] == "D"
            jItens[aCustomFld[nI][1]] := IIF(!Empty((cAlias)->&(aCustomFld[nI][1])),FwTimeStamp( 5, (cAlias)->&(aCustomFld[nI][1])),)
        Else
            jItens[aCustomFld[nI][1]] := (cAlias)->&(aCustomFld[nI][1])
        EndIf
    Next

    oDados:appendData(jItens)
    FreeObj(jItens)

    (cAlias)->(dbSkip())
End

(cAlias)->(dbCloseArea())
Return

Static Function getQryPed(cAlias, aCustomFld, aCamposQry)
Local cCustomFld:= ""
Local cCamposQry:= ""
Local cQry := ""
Local aQryInfo := {}
Local aSetField := {}

Local cSuspensao:= STR0010 //"SUSPENSÃO"
Local cIsencao  := STR0011 //"ISENÇÃO"


If !Empty(aCustomFld)
    cCustomFld := "," + TECustomSql(aCustomFld)
EndIf
cCamposQry := getSQLField(aCamposQry) +','


cQry := "SELECT "
cQry += cCamposQry
cQry +=            "(SW8.W8_QTDE * SW8.W8_PRECO) AS W8_VLMOE,"
cQry +=            "SA2.A2_NREDUZ AS NFORNECEDOR,"
cQry +=            "FAB.A2_NREDUZ AS NFABRICANTE,"
cQry +=            "CASE WHEN W6_TIPOREG <>  ? THEN 'DI' ELSE 'DUIMP' END AS W6_DESC_TIPOREG,"
                   aAdd( aQryInfo , {'2','C'})  
cQry +=            "COALESCE(ED0_AC, ' ') AS ED0_MODAL,"
cQry +=            "CASE WHEN ED0_AC <> ? THEN (CASE WHEN ED0_MODAL = ? THEN ? ELSE ? END) ELSE ' ' END AS DESC_MODAL_AC,"
                   aAdd( aQryInfo , {'','C'})  
                   aAdd( aQryInfo , {'1','C'})  
                   aAdd( aQryInfo , {cSuspensao,'C'})                     
                   aAdd( aQryInfo , {cIsencao,'C'})  
cQry +=            "COALESCE(WN.WN_DOC, ' ') AS WN_DOC,"
cQry +=            "COALESCE(WN.WN_SERIE, ' ') AS WN_SERIE,"
cQry +=            "COALESCE(WN.WN_QUANT, 0) AS WN_QUANT,"
cQry +=            "COALESCE(WN.WN_LOTECTL, ' ') AS WN_LOTECTL,"
cQry +=            "COALESCE(WN.WN_NUMLOTE, ' ') AS WN_NUMLOTE,"
cQry +=            "COALESCE(WN.WN_DTVALID, ' ') AS WN_DTVALID,"
cQry +=            "COALESCE(WN.WN_TIPO_NF, ' ') AS WN_TIPO_NF,"
cQry +=            "CASE WHEN WN.WN_TIPO_NF = ? THEN 'PRIMEIRA' ELSE ( "
                   aAdd( aQryInfo , {'1','C'})  
cQry +=            "     CASE WHEN WN.WN_TIPO_NF = ? THEN 'ÚNICA' ELSE ( "
                   aAdd( aQryInfo , {'3','C'})  
cQry +=            "        CASE WHEN WN.WN_TIPO_NF = ?THEN 'MÃE' ELSE ' ' END "
                   aAdd( aQryInfo , {'5','C'})  
cQry +=            "    ) END "
cQry +=            ") END AS WN_DESC_TIPONF "
cQry +=            cCustomFld
cQry +=     "FROM "
cQry +=     RetSqlName("SW6") + " SW6 "

cQry +=     "INNER JOIN " + RetSqlName("SW9") +" SW9 ON ( "
cQry +=     "   SW9.W9_FILIAL = SW6.W6_FILIAL "
cQry +=     "   AND SW9.W9_HAWB = SW6.W6_HAWB "
cQry +=     ") "
cQry +=     "INNER JOIN " + RetSqlName("SW8") +" SW8 ON ( "
cQry +=     "    SW8.W8_FILIAL = SW9.W9_FILIAL "
cQry +=     "    AND SW8.W8_HAWB = SW9.W9_HAWB "
cQry +=     "    AND SW8.W8_INVOICE = SW9.W9_INVOICE "
cQry +=     ") "
cQry +=     "INNER JOIN " + RetSqlName("SB1") +" SB1 ON ( "
cQry +=     "    SB1.B1_FILIAL = ? "
                 aAdd( aQryInfo , {xFilial('SB1'),'C'})  
cQry +=     "    AND SB1.B1_COD = SW8.W8_COD_I "
cQry +=     "    AND SB1.D_E_L_E_T_ = ?  "
                 aAdd( aQryInfo , {'','C'})  
cQry +=     ") "
cQry +=     "INNER JOIN " + RetSqlName("SA2") +" SA2 ON ( "
cQry +=     "    SA2.A2_FILIAL = ?  "
                 aAdd( aQryInfo , {xFilial('SA2'),'C'})  
cQry +=     "    AND SA2.A2_COD = SW8.W8_FORN "
cQry +=     "    AND SA2.A2_LOJA = SW8.W8_FORLOJ "
cQry +=     "    AND SA2.D_E_L_E_T_ = ?  "
                 aAdd( aQryInfo , {'','C'})  
cQry +=     ") "
cQry +=     "INNER JOIN " + RetSqlName("SA2") +" FAB ON ( " 
cQry +=     "    FAB.A2_FILIAL = ?  "
                 aAdd( aQryInfo , {xFilial('SA2'),'C'})  
cQry +=     "    AND FAB.A2_COD = SW8.W8_FABR "
cQry +=     "    AND FAB.A2_LOJA = SW8.W8_FABLOJ "
cQry +=     "    AND FAB.D_E_L_E_T_ = ?  "
                 aAdd( aQryInfo , {'','C'})  
cQry +=     ") "
cQry +=     "INNER JOIN " + RetSqlName("SYT") +" SYT ON ( "
cQry +=     "    SYT.YT_FILIAL = ? "
                 aAdd( aQryInfo , {xFilial('SYT'),'C'})  
cQry +=     "    AND SYT.YT_COD_IMP = SW6.W6_IMPORT "
cQry +=     "    AND SYT.D_E_L_E_T_ = ?  "
                 aAdd( aQryInfo , {'','C'})  
cQry +=     ") "
cQry +=      getJoinSG1(@aQryInfo) //Pergunta se deve consistir a estrutura
cQry +=     "LEFT JOIN " + RetSqlName("ED0") +" ED0 ON ( "
cQry +=     "    ED0.ED0_FILIAL = ? "
                 aAdd( aQryInfo , {xFilial('ED0'),'C'})  
cQry +=     "    AND ED0.ED0_AC = SW8.W8_AC "
cQry +=     "    AND ED0.D_E_L_E_T_ = ?  "
                 aAdd( aQryInfo , {'','C'})  
cQry +=     ") "
cQry +=     "LEFT JOIN " + RetSqlName("SWN") +" WN ON ( "
cQry +=     "    WN.WN_FILIAL = ? "
                 aAdd( aQryInfo , {xFilial('SWN'),'C'})  
cQry +=     "    AND WN.WN_HAWB = SW8.W8_HAWB "
cQry +=     "    AND WN.WN_PGI_NUM = SW8.W8_PGI_NUM "
cQry +=     "    AND WN.WN_PO_EIC = SW8.W8_PO_NUM "
cQry +=     "    AND WN.WN_CC = SW8.W8_CC "
cQry +=     "    AND WN.WN_SI_NUM = SW8.W8_SI_NUM "
cQry +=     "    AND WN.WN_INVOICE = SW8.W8_INVOICE "
cQry +=     "    AND WN.WN_PRODUTO = SW8.W8_COD_I "
cQry +=     "    AND WN.WN_ITEM = SW8.W8_POSICAO "
cQry +=     "    AND "
cQry +=     "    ( "
cQry +=     "        WN.WN_TIPO_NF = ? OR "  //PRIMEIRA
                     aAdd( aQryInfo , {'1','C'})  
cQry +=     "        WN.WN_TIPO_NF = ? OR "  //UNICA
                     aAdd( aQryInfo , {'3','C'})  
cQry +=     "        WN.WN_TIPO_NF = ? "     //MAE
                     aAdd( aQryInfo , {'5','C'})  
cQry += ") "
cQry +=     "    AND WN.D_E_L_E_T_ = ?  "
                 aAdd( aQryInfo , {'','C'})  
cQry +=     ") "
cQry +=     "WHERE   SW6.W6_FILIAL = ? "
                 aAdd( aQryInfo , {xFilial('SW6'),'C'})  
cQry +=     "    AND ( " 
cQry +=     "       ( "
cQry +=     "            SW6.W6_CURRIER = ?  "
                         aAdd( aQryInfo , {'1','C'})  
cQry +=     "            AND SW6.W6_DIRE <> ? "
                         aAdd( aQryInfo , {'','C'})  
cQry +=     "        ) " 
cQry +=     "        OR ( " 
cQry +=     "            SW6.W6_CURRIER <> ? "
                         aAdd( aQryInfo , {'1','C'})  
cQry +=     "           AND SW6.W6_DI_NUM <> ? "
                        aAdd( aQryInfo , {'','C'})  
cQry +=     "        ) "
cQry += ") AND "
cQry +=          getWhere(@aQryInfo)
cQry +=     "        SW6.D_E_L_E_T_ = ? AND "
                 aAdd( aQryInfo , {'','C'})  
cQry +=     "        SW8.D_E_L_E_T_ = ? AND "
                 aAdd( aQryInfo , {'','C'})  
cQry +=     "        SW9.D_E_L_E_T_ = ?  "
                 aAdd( aQryInfo , {'','C'})  
cQry +=     "ORDER BY SW8.W8_COD_I, SW6.W6_DTREG_D "
  
aAdd( aSetField , {'WN_DTVALID','D',8,0})

comex.generics.setFilterInQueryPreparedStatement(cAlias,cQry,aQryInfo,aSetField)

TESetField(cAlias, aCamposQry)

If !Empty(aCustomFld)
    TECustomCol(cAlias, aCustomFld)
EndIf

Return


Static Function getCampos(cAlias)
Local aRet := {}

If cAlias == "SW6" .Or. cAlias == "ALL"
    aAdd(aRet,"W6_FILIAL")
    aAdd(aRet,"W6_HAWB")
    aAdd(aRet,"W6_IMPORT")
    aAdd(aRet,"W6_DI_NUM")
    aAdd(aRet,"W6_DTREG_D")
    aAdd(aRet,"W6_VLMLEMN")
    aAdd(aRet,"W6_DT_ENTR")
    aAdd(aRet,"W6_TX_US_D")
    aAdd(aRet,"W6_TIPOREG")
EndIf

If cAlias == "SW8" .Or. cAlias == "ALL"
    aAdd(aRet,"W8_FILIAL")
    aAdd(aRet,"W8_INVOICE")
    aAdd(aRet,"W8_FORN")
    aAdd(aRet,"W8_FORLOJ")
    aAdd(aRet,"W8_FABR")
    aAdd(aRet,"W8_FABLOJ")
    aAdd(aRet,"W8_PGI_NUM")
    aAdd(aRet,"W8_POSICAO")
    aAdd(aRet,"W8_PO_NUM")
    aAdd(aRet,"W8_COD_I")
    aAdd(aRet,"W8_UNID")
    aAdd(aRet,"W8_QTDE")
    aAdd(aRet,"W8_PRECO")
    aAdd(aRet,"W8_SEGURO")
    aAdd(aRet,"W8_FRETEIN")
    aAdd(aRet,"W8_INLAND")
    aAdd(aRet,"W8_PACKING")
    aAdd(aRet,"W8_OUTDESP")
    aAdd(aRet,"W8_DESCONT")
    aAdd(aRet,"W8_AC")
    aAdd(aRet,"W8_SEQSIS")
    aAdd(aRet,"W8_ACISEN")
    //(SW8.W8_QTDE * SW8.W8_PRECO) W8_VLMOE, 
EndIf

If cAlias == "SW9" .Or. cAlias == "ALL"
    aAdd(aRet,"W9_FILIAL")
    aAdd(aRet,"W9_HAWB")
    aAdd(aRet,"W9_MOE_FOB")
    aAdd(aRet,"W9_INCOTER")
    aAdd(aRet,"W9_COND_PA")
    aAdd(aRet,"W9_DIAS_PA")
    aAdd(aRet,"W9_TX_FOB")
    aAdd(aRet,"W9_FREINC")
    aAdd(aRet,"W9_SEGINC")
EndIf

If cAlias == "SB1" .Or. cAlias == "ALL"
    aAdd(aRet,"B1_DESC")
EndIf

If cAlias == "ED0" .Or. cAlias == "ALL"
    aAdd(aRet,"ED0_MODAL")
EndIf

If cAlias == "SYT" .Or. cAlias == "ALL"
    aAdd(aRet,"YT_NOME")
    aAdd(aRet,"YT_NOME_RE")
EndIf

Return aRet

Static Function getSQLField(aCustomFld)
Local cRet := ""
Local nI

For nI := 1 to len(aCustomFld)
    If Empty(cRet)
        cRet := aCustomFld[nI]
    Else
        cRet += ", " + aCustomFld[nI]
    EndIf
Next

Return cRet

Static Function TESetField(cAlias, aCustomFld)
Local nI
//COLUMN XXXXXX AS DATE
For nI := 1 to len(aCustomFld)
    If GetSx3Cache(aCustomFld[nI],"X3_TIPO") == "D"
        TCSetField(cAlias, aCustomFld[nI], "D",/*nSize*/,/*nPrecision*/)
    EndIf
Next
Return

Static Function getJoinSG1(aQryInfo)
Local cJoinSG1 := ""
If MV_PAR07 != 2 //Consiste estrutura ? 1-Sim/2-Não
    cJoinSG1 := " INNER JOIN " + RetSQLName("SG1") + " SG1 ON ( "
    cJoinSG1 +=     " SG1.G1_FILIAL = ? "
                    aAdd( aQryInfo , {xFilial("SG1"),'C'})      
    cJoinSG1 +=     " AND SG1.G1_COMP = SW8.W8_COD_I "
    cJoinSG1 +=     " AND SG1.D_E_L_E_T_ = ? "
                    aAdd( aQryInfo , {'','C'})              
    cJoinSG1 += " ) "
EndIf
Return cJoinSG1

/*
Static FUNCTION getWhere
Parâmetro: aQryInfo - parâmetro passado por referência para complementar os parâmeros da query
Retorna: cWhere - string com os filtros para a query
Author: Maurício Frison
Data: 31/10/2024
*/
Static Function getWhere(aQryInfo)
Local cFilRegist:= ""
Local cProduto  := ""
Local cWhere    := ""

//FILTRO DATA REGISTRO
If !Empty(MV_PAR03) //REGISTRO DE
    cFilRegist += " SW6.W6_DTREG_D >= ? AND " //Data Registro de ?
    aAdd( aQryInfo , {MV_PAR03,'D'})  
EndIf
If !Empty(MV_PAR04) //REGISTRO ATE
    cFilRegist += " SW6.W6_DTREG_D <= ? AND " //Data Registro até ?
    aAdd( aQryInfo , {MV_PAR04,'D'})      
EndIf

//FILTRO PRODUTO
If !Empty(MV_PAR05) //PRODUTO DE
    cProduto += " SW8.W8_COD_I >= ? AND " //Produto de ?
    aAdd( aQryInfo , {MV_PAR05,'C'})      
EndIf
If !Empty(MV_PAR06) //PRODUTO ATE
    cProduto += " SW8.W8_COD_I <= ? AND " //Produto até ?
    aAdd( aQryInfo , {MV_PAR06,'C'})          
EndIf

cWhere := cFilRegist + cProduto 
Return cWhere
