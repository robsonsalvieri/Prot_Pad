#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "Average.ch"
#include "manufacturing.comex.import.smartview.importstatus.ch"

#define ENTREGUES 1

namespace totvs.protheus.manufacturing.comex.import.smartview

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEIC", tables="SW0, SW1, SW2, SW3, SW4, SW5, SW6, SW7", name="Status da Importação", country="ALL", initialRelease="12.1.2210")

/*/{Protheus.doc} StatusImportacaoTReportsBusinessObject
Classe para criação do Objeto de Negócio para a listagem de Status do Item ou do Purchase Order (Status da Importação)
Este relatório foi convertido a partir dos relatórios "Status do Item" e "Status do Pedido" (EICIP170/EICIP175)

@author tiago tudisco
@since 03/08/2023
/*/
class StatusImportacaoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass

/*/{Protheus.doc} new
Método de instância da classe
@author tiago tudisco
@since 03/08/2023
/*/ 
method new() class StatusImportacaoTReportsBusinessObject
_Super:new()
//Define a área
self:appendArea("Easy Import Control") //"Easy Import Control"
 
//Define o nome do Objeto de Negócio
self:setDisplayName(STR0116) // "Status da Importação"
 
//Define a descrição do Objeto de Negócio
self:setDescription(STR0117)// "Este relatório visa agilizar a prestação de informações aos departamentos solicitantes, apresentando status do item em todas as fases da importação em que se encontrar pendente."

self:setPergunte("EICIP170") //Indica o pergunte que será utilizado no relatório

return self
 
/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@author tiago tudisco
@since 03/08/2023
/*/ 
method getData(nPage as numeric, oFilter as object) as object class StatusImportacaoTReportsBusinessObject
local jParams  as json
local aUserFil as array
//Define a quantidade máxima por página (Default 100)
self:setPageSize(100)
 
jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

MV_PAR01 := jParams["MV_PAR01"][1] //Filial de ?
MV_PAR02 := jParams["MV_PAR02"][1] //Filial até ??
MV_PAR03 := AvKey(jParams["MV_PAR03"][1],"W1_COD_I")//Produto de ?
MV_PAR04 := AvKey(jParams["MV_PAR04"][1],"W1_COD_I")//Produto até ?
MV_PAR05 := AvKey(jParams["MV_PAR05"][1],"W0__NUM")//Solicit.Import de ?
MV_PAR06 := AvKey(jParams["MV_PAR06"][1],"W0__NUM")//Solicit.Import. até ?
MV_PAR07 := AvKey(jParams["MV_PAR07"][1],"W2_PO_NUM")//Pedido até ?
MV_PAR08 := AvKey(jParams["MV_PAR08"][1],"W2_PO_NUM")//Pedido até ?
MV_PAR09 := IIF(!Empty(jParams["MV_PAR09"][1]), TEDtConvSM(jParams["MV_PAR09"][1]),"") //Data PO de ?
MV_PAR10 := IIF(!Empty(jParams["MV_PAR10"][1]), TEDtConvSM(jParams["MV_PAR10"][1]),"") //Data PO até ?
MV_PAR11 := if( valtype(jParams["MV_PAR11"][1]) == "N", jParams["MV_PAR11"][1], val(jParams["MV_PAR11"][1]))  //Considera Itens Entregues ? 1-Sim/2-Não

aUserFil := TEgetUsrFil("SW6", MV_PAR01, MV_PAR02) //Retorna quais filiais serão executadas com base no pergunte Filial de? / Filial até?
setStatImp(@self:oData, aUserFil)

return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@author tiago tudisco
@since 03/08/2023
/*/ 
method getSchema() as object class StatusImportacaoTReportsBusinessObject
local aFieldsSW0 as array
local aFieldsSW1 as array
local aFieldsSW2 as array
local aFieldsSW3 as array
local aFieldsSW5 as array
local aFieldsSW6 as array
local aFieldsSW7 as array
local aFieldsSA2 as array
local aFieldsSY3 as array
local aFieldsSB1 as array
local aFieldsSYQ as array

aFieldsSW0 := {"W0__NUM","W0__DT"}
aFieldsSW1 := {"W1_CC","W1_DTENTR_",""}
aFieldsSW2 := {"W2_FILIAL","W2_PO_NUM","W2_COMPRA","W2_DT_ALTE"}
aFieldsSW3 := {"W3_PART_N"}
aFieldsSW5 := {"W5_PGI_NUM","W5_SEQ_LI","WP_REGIST","W5_DT_ENTR"}
aFieldsSW6 := {"W6_HAWB","W6_DESP","W6_DT_EMB"}
aFieldsSW7 := {"W7_FORN","W7_FORLOJ","W7_POSICAO","W7_COD_I","W7_QTDE","W7_PRECO","W7_PRECO_R","W7_REG"}
aFieldsSA2 := {"A2_NREDUZ"}
aFieldsSY3 := {"Y3_DESC"}
aFieldsSB1 := {"B1_DESC"}
aFieldsSYQ := {"YQ_DESCR"}

self:oSchema:aliasToSchema("SW0", aFieldsSW0)
self:oSchema:aliasToSchema("SW1", aFieldsSW1)
self:oSchema:aliasToSchema("SW2", aFieldsSW2)
self:oSchema:aliasToSchema("SW3", aFieldsSW3)
self:oSchema:aliasToSchema("SW5", aFieldsSW5)
self:oSchema:aliasToSchema("SW6", aFieldsSW6)
self:oSchema:aliasToSchema("SW7", aFieldsSW7)
self:oSchema:aliasToSchema("SA2", aFieldsSA2)
self:oSchema:aliasToSchema("SY3", aFieldsSY3)
self:oSchema:aliasToSchema("SB1", aFieldsSB1)
self:oSchema:aliasToSchema("SYQ", aFieldsSYQ)

self:oSchema:addProperty("WKFLUXO"  , STR0121 , "string"  , STR0121 , "WKFLUXO") //"Anuente"
self:oSchema:addProperty("WKOBS"    , STR0024 , "string"  , STR0024 , "WKOBS") //"Status"
self:oSchema:addProperty("WKEMPFIL" , STR0122 , "string"  , STR0122 , "WKEMPFIL") //"Empresa"


return self:oSchema

/*/{Protheus.doc} setStatImp
@author Tiago Tudisco
@since 10/08/2023
/*/ 
Static Function setStatImp(oDados, aUserFil)
local cBkpFil:= cFilAnt as character
local cQuery := ''      as character
local nI                as numeric
local aQryInfo :=  {}   as array

For nI := 1 To Len(aUserFil)
    cFilAnt := aUserFil[nI]
    cQuery := getQryImp(aUserFil,@aQryInfo )
    TrataDados(@oDados, cQuery, aQryInfo)
    cQuery := ""
    aQryInfo := {}
Next

If cFilAnt <> cBkpFil
    cFilAnt := cBkpFil
EndIf

Return

/*/{Protheus.doc} getQryImp
@author Tiago Tudisco
@since 10/08/2023
/*/ 
Static Function getQryImp(aUserFil, aQryInfo)
local cQuery := ''      as character

//================|QUERY DADOS SI - SOLICITACAO IMPORTACAO|================//
cQuery += qryImpSI(@aQryInfo)
//====================FIM QUERY DADOS SI============================//

cQuery += " UNION ALL "

//================|QUERY DADOS PO - PURCHASE ORDER|================//
cQuery += qryImpPO(@aQryInfo)
//===================FIM QUERY DADOS PO=======================//

cQuery += " UNION ALL "

//================|QUERY DADOS LI - LICENSA IMPORTACAO|================//
cQuery += qryImpLI(@aQryInfo)
//===================FIM QUERY DADOS LI=======================//

cQuery += " UNION ALL "

//================|QUERY DADOS EMB - EMBARQUE/DESEMBARACO|================//
cQuery += qryImpEMB(@aQryInfo)
//===================FIM QUERY DADOS EMB======================//

Return cQuery

/*/{Protheus.doc} TrataDados
Monta json com os dados do relatório
@author Tiago Tudisco
@since 10/08/2023
/*/ 
Static Function TrataDados(oDados, cQuery, aQryInfo)
Local jItens    as json
Local cAlias    as character
Local cObserv   as character
Local aSetField := {} as array

cALias := getNextAlias()
aAdd( aSetField , {'WKSALDOPRO',AvSX3("W3_QTDE")[2]   , AvSX3("W3_QTDE")[3]   , AvSX3("W3_QTDE")[4]})
aAdd( aSetField , {"WKW6DTEMB"  , AvSX3("W6_DT_EMB")[2] , AvSX3("W6_DT_EMB")[3] , AvSX3("W6_DT_EMB")[4]})
comex.generics.setFilterInQueryPreparedStatement(cAlias,cQuery,aQryInfo,aSetField)
EasyTCFields(cAlias)
cObserv := ""
While (cAlias)->(!Eof())
    //Verificação dos Status
    Do Case
        Case (cAlias)->(WKTIPO) == "SI " //Dados da Solicitação de Importação
            cObserv := (STR0039) //"EM NEGOCIAção

        Case (cAlias)->(WKTIPO) == "PO " //Dados do Purchase Order
            cObserv := getStatPO(cAlias)

        Case (cAlias)->(WKTIPO) == "LI " //Dados da LicenÃ§a de Importação
            cObserv := getStatPLI(cAlias)

        Case (cAlias)->(WKTIPO) == "EMB" //Dados do Embarque/Desembaraço
            cObserv := getStatEMB(cAlias)

    EndCase

    jItens := JsonObject():new()
    jItens["W2_FILIAL"] := (cAlias)->(W2_FILIAL)
    jItens["WKEMPFIL"]  := AllTrim((cAlias)->(W2_FILIAL)) + "-" + FwFilName(FwCodEmp(), (cAlias)->(W2_FILIAL))
    jItens["W2_PO_NUM"] := (cAlias)->(W2_PO_NUM)
    jItens["W6_HAWB"]   := (cAlias)->(W6_HAWB)
    jItens["W1_CC"]     := (cAlias)->(W1_CC)
    jItens["Y3_DESC"]   := (cAlias)->(Y3_DESC)
    jItens["W7_FORN"]   := (cAlias)->(W7_FORN)
    jItens["W7_FORLOJ"] := (cAlias)->(W7_FORLOJ)
    jItens["A2_NREDUZ"] := (cAlias)->(A2_NREDUZ)
    jItens["W7_POSICAO"]:= (cAlias)->(W7_POSICAO)
    jItens["W6_DESP"]   := (cAlias)->(W6_DESP)
    jItens["W7_COD_I"]  := (cAlias)->(W7_COD_I)
    jItens["B1_DESC"]   := (cAlias)->(B1_DESC)
    jItens["W3_PART_N"] := (cAlias)->(W3_PART_N)
    jItens["W2_COMPRA"] := (cAlias)->(W2_COMPRA)
    jItens["W7_QTDE"]   := (cAlias)->(W7_QTDE)
    jItens["W7_PRECO"]  := (cAlias)->(W7_PRECO)
    jItens["W7_PRECO_R"]:= (cAlias)->(W7_PRECO_R)
    jItens["W0__NUM"]   := (cAlias)->(W0__NUM)
    jItens["W5_PGI_NUM"]:= (cAlias)->(W5_PGI_NUM)
    jItens["W5_SEQ_LI"] := (cAlias)->(W5_SEQ_LI)
    jItens["WP_REGIST"] := (cAlias)->(WP_REGIST)
    jItens["W0__DT"]    := IIF(!Empty((cAlias)->(W0__DT)),FwTimeStamp( 5, (cAlias)->(W0__DT)),)
    jItens["W2_DT_ALTE"]:= IIF(!Empty((cAlias)->(W2_DT_ALTE)),FwTimeStamp( 5, (cAlias)->(W2_DT_ALTE)),)
    jItens["W6_DT_EMB"] := IIF(!Empty((cAlias)->(W6_DT_EMB)),FwTimeStamp( 5, (cAlias)->(W6_DT_EMB)),)
    jItens["W1_DTENTR_"]:= IIF(!Empty((cAlias)->(W1_DTENTR_)),FwTimeStamp( 5, (cAlias)->(W1_DTENTR_)),)
    jItens["W7_REG"]    := (cAlias)->(W7_REG)
    jItens["W5_DT_ENTR"]:= IIF(!Empty((cAlias)->(W5_DT_ENTR)),FwTimeStamp( 5, (cAlias)->(W5_DT_ENTR)),)
    jItens["YQ_DESCR"]  := (cAlias)->(YQ_DESCR)
    jItens["WKFLUXO"]   := IF((cAlias)->(WKFLUXO) == '1',STR0123,STR0124) //"Sim"#####"Não"
    jItens["WKOBS"]     := cObserv

    oDados:appendData(jItens)
    FreeObj(jItens)
    cObserv := ""

    (cAlias)->(dbSkip())
End

(cAlias)->(dbCloseArea())
Return

Static Function qryImpSI(aQryInfo)
Local cQuery    := "" as character
Local nLeadTime := EasyGParam("MV_LT_COMP") + EasyGParam("MV_LT_LICE") + EasyGParam("MV_LT_DESE") as numeric

//================|QUERY DADOS SI - SOLICITACAO IMPORTACAO|================//
cQuery := " SELECT	''              AS W6_DTEMB_D, "
cQuery += "         W0.W0_FILIAL	AS W2_FILIAL, "
cQuery += " 		W1.W1_PO_NUM	AS W2_PO_NUM, "
cQuery += " 		' '				AS W6_HAWB, "
cQuery += " 		W1.W1_CC		AS W1_CC, "
cQuery += " 		Y3.Y3_DESC		AS Y3_DESC, "
cQuery += " 		W1.W1_FORN		AS W7_FORN, "
cQuery += " 		W1.W1_FORLOJ	AS W7_FORLOJ, "
cQuery += " 		COALESCE(A2.A2_NREDUZ, ' ') AS A2_NREDUZ, "
cQuery += " 		W1.W1_POSICAO	AS W7_POSICAO, "
cQuery += " 		' '				AS W6_DESP, "
cQuery += " 		W1.W1_COD_I		AS W7_COD_I, "
cQuery += " 		B1.B1_DESC, "
cQuery += " 		COALESCE(A5.A5_CODPRF, ' ')	AS W3_PART_N, "
cQuery += " 		COALESCE(W2.W2_COMPRA, ' ')	AS W2_COMPRA, "
cQuery += " 		B1.B1_ANUENTE   AS WKFLUXO, "
cQuery += " 		W1.W1_SALDO_Q	AS W7_QTDE, "
cQuery += " 		0				AS W7_PRECO, "
cQuery += " 		0				AS W7_PRECO_R, "
cQuery += " 		W1.W1_SI_NUM	AS W0__NUM, "
cQuery += " 		' '				AS W5_PGI_NUM, "
cQuery += " 		' '				AS W5_SEQ_LI, "
cQuery += " 		' '				AS WP_REGIST, "
cQuery += " 		W0.W0__DT		AS W0__DT, "
cQuery += " 		COALESCE(CASE WHEN W2.W2_DT_ALTE IS NULL OR W2.W2_DT_ALTE = ? THEN W2.W2_PO_DT ELSE W2.W2_DT_ALTE END, ' ') AS W2_DT_ALTE, "
                    aAdd( aQryInfo , {'','D'}) 
cQuery += " 		W1_DT_EMB		AS W6_DT_EMB, "
cQuery += " 		W1.W1_DTENTR_	AS W1_DTENTR_, "
cQuery += " 		W1.W1_REG		AS W7_REG, "
cQuery += " 		CASE WHEN W1.W1_DTENTR_ > ? THEN W1.W1_DTENTR_ ELSE ?  END AS W5_DT_ENTR, "
                    aAdd( aQryInfo , {dDataBase,'DD'}) 
                    aAdd( aQryInfo , {dDataBase + nLeadTime,'DD'})                     
cQuery += " 		' '				AS YQ_DESCR, "
//cQuery += " 		'EM NEGOCIAÇÃO' AS WKOBS, "
cQuery += "         0               AS W3_QTDE, "
cQuery += "         0               AS WKSALDOPRO, "
cQuery += "         ' '		        AS W2_HAWB_DA, "
cQuery += "         ' '     		AS W2_VENCDA, "
cQuery += "         ' '	            AS W5_DT_SHIP, "
cQuery += "         ' '             AS W5_DOCTO_F, "
cQuery += "         ' '             AS W6_CHEG, "
cQuery += "         ' '             AS WKW6DTEMB, "
cQuery += "         ' '             AS W5_FLUXO, "
cQuery += "         ' '             AS W5_DEF_REQ, "
cQuery += "         ' '             AS W4_SUFRAMA, "
cQuery += "         ' '             AS W4_DT_SUFR, "
cQuery += "         ' '             AS W4_PORTASN, "
cQuery += "         ' '             AS W4_FLUXO, "
cQuery += "         ' '             AS W4_DTEDCEX, "
cQuery += "         ' '             AS WP_MICRO, "
cQuery += "         ' '             AS WP_TRANSM, "
cQuery += "         ' '             AS WP_VENCTO, "
cQuery += "         ' '	            AS WP_PGI_NUM, "
cQuery += "         ' '	            AS W6_DT_HAWB, "
cQuery += "         ' '	            AS W6_DT_ENCE, "
cQuery += "         ' '             AS W6_DT, "
cQuery += "         ' '             AS W6_DT_AVE, "
cQuery += "         ' '             AS W6_TIPODES, "
cQuery += "         ' '             AS W6_DT_DESE, "
cQuery += "         ' '             AS W6_DTRECDO, "
cQuery += "         ' '             AS Y9_DAP, "
cQuery += "         ' '             AS Y9_SIGLA, "
cQuery += "         ' '             AS W6_DA_DT, "
cQuery += "         ' '             AS W6_DT_ENTR, "
cQuery += "         ' '             AS W6_DI_NUM, "
cQuery += "         ' '             AS W6_DT_DTA, "
cQuery += "         ' '             AS W6_PRVENTR, "
cQuery += " 		'SI'			AS WKTIPO "
cQuery += " FROM " + RetSqlName("SW0") + " W0 INNER JOIN  "
cQuery +=          + RetSqlName("SW1") + " W1 ON (W0.W0_FILIAL = W1.W1_FILIAL AND W0.W0__CC = W1.W1_CC AND W0.W0__NUM = W1.W1_SI_NUM) INNER JOIN "
cQuery +=          + RetSqlName("SB1") + " B1 ON (B1.B1_FILIAL = ? AND W1.W1_COD_I = B1.B1_COD AND B1.D_E_L_E_T_ = ?) LEFT JOIN "
                   aAdd( aQryInfo , {xFilial("SB1"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SA2") + " A2 ON (A2.A2_FILIAL = ? AND A2.A2_COD = W1.W1_FORN AND A2.A2_LOJA = W1.W1_FORLOJ AND A2.D_E_L_E_T_ = ?) LEFT JOIN "
                   aAdd( aQryInfo , {xFilial("SA2"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SY3") + " Y3 ON (Y3.Y3_FILIAL = ? AND Y3.Y3_COD = W1.W1_CC AND Y3.D_E_L_E_T_ = ?) LEFT JOIN "
                   aAdd( aQryInfo , {xFilial("SY3"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SW2") + " W2 ON (W2.W2_FILIAL = ? AND W2.W2_PO_NUM = W1.W1_PO_NUM AND W2.D_E_L_E_T_ = ?) LEFT JOIN	  "
                   aAdd( aQryInfo , {xFilial("SW2"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SYQ") + " YQ ON (YQ.YQ_FILIAL = ? AND YQ.YQ_VIA = W2.W2_TIPO_EM AND YQ.D_E_L_E_T_ = ?) LEFT JOIN "
                   aAdd( aQryInfo , {xFilial("SYQ"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SA5") + " A5 ON (A5.A5_FILIAL = ? AND W1.W1_FORN = A5.A5_FORNECE AND W1.W1_FORLOJ = A5.A5_LOJA AND W1.W1_COD_I = A5.A5_PRODUTO AND W1.W1_FABR = A5.A5_FABR AND W1.W1_FABLOJ = A5.A5_FALOJA AND A5.D_E_L_E_T_ = ?) "
                   aAdd( aQryInfo , {xFilial("SA5"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery += " WHERE "

//SE PREENCHIDO O CAMPO DE FILTRO DE FILIAL
cQuery += "       W1.W1_FILIAL = ? AND " //Filial
                   aAdd( aQryInfo , {xFilial("SW1"),'C'}) 

//----------------------------
//SE PREENCHIDO O CAMPO DE FILTRO DE PRODUTO
If !Empty(MV_PAR03) //Produto de ?
    cQuery += "   W1.W1_COD_I		>= ? AND " //PRODUTO
    aAdd( aQryInfo , {MV_PAR03,'C'}) 
EndIf
If !Empty(MV_PAR04) //Produto até ?
    cQuery += "   W1.W1_COD_I		<= ? AND " //PRODUTO
    aAdd( aQryInfo , {MV_PAR04,'C'}) 
EndIf
//------------------------------------------

cQuery += "    W1.W1_SEQ		= ? AND "
aAdd( aQryInfo , {0,'N'}) 
cQuery += "    W1.W1_SALDO_Q	> ? AND "
aAdd( aQryInfo , {0,'N'}) 

//SE PREENCHIDO OS CAMPOS DE FILTRO DE DATA "
If !Empty(MV_PAR09) //Data Inicial
    cQuery += "   W0.W0__DT >= ? AND " //DT INICIAL 
    aAdd( aQryInfo , {MV_PAR09,'D'}) 
EndIf
If !Empty(MV_PAR10) //Data Final
    cQuery += "   W0.W0__DT <= ? AND " //DT FINAL
    aAdd( aQryInfo , {MV_PAR10,'D'}) 
EndIf
//------------------------------------------

//SE PREENCHIDO OS CAMPOS DE FILTRO DE SI "
If !Empty(MV_PAR05) //SI de ?
    cQuery += "   W0.W0__NUM >= ? AND " //SI
    aAdd( aQryInfo , {MV_PAR05,'C'}) 
EndIf
If !Empty(MV_PAR06) //SI até ?
    cQuery += "   W0.W0__NUM <= ? AND " //SI
    aAdd( aQryInfo , {MV_PAR06,'C'}) 
EndIf

//------------------------------------------

//SE PREENCHIDO OS CAMPOS DE FILTRO DE PO "
If !Empty(MV_PAR07) //Pedido de ?
    cQuery += "   W2.W2_PO_NUM >= ? AND " //PO
    aAdd( aQryInfo , {MV_PAR07,'C'})     
EndIf
If !Empty(MV_PAR08) //Pedido até ?
    cQuery += "   W2.W2_PO_NUM <= ? AND " //PO
    aAdd( aQryInfo , {MV_PAR08,'C'})         
EndIf
//------------------------------------------

cQuery += "    W0.D_E_L_E_T_ = ? AND "
aAdd( aQryInfo , {'','C'})         
cQuery += "    W1.D_E_L_E_T_ = ? "
aAdd( aQryInfo , {'','C'})         
//cQuery += " ORDER BY WKTIPO, W1.W1_FILIAL "
//====================FIM QUERY DADOS SI============================//

Return cQuery

Static Function qryImpPO(aQryInfo)
Local cQuery := "" as character

//================|QUERY DADOS PO - PURCHASE ORDER|================//
cQuery := " SELECT	''              AS W6_DTEMB_D, "
cQuery += "         W0.W0_FILIAL	AS W2_FILIAL, "
cQuery += " 		W3.W3_PO_NUM	AS W2_PO_NUM, "
cQuery += " 		' '				AS W6_HAWB, "
cQuery += " 		W3.W3_CC		AS W1_CC, "
cQuery += " 		Y3.Y3_DESC		AS Y3_DESC, "
cQuery += " 		W3.W3_FORN		AS W7_FORN, "
cQuery += " 		W3.W3_FORLOJ	AS W7_FORLOJ, "
cQuery += " 		COALESCE(A2.A2_NREDUZ, ' ') AS A2_NREDUZ, "
cQuery += " 		W3.W3_POSICAO	AS W7_POSICAO, "
cQuery += " 		' '				AS W6_DESP, "
cQuery += " 		W3.W3_COD_I		AS W7_COD_I, "
cQuery += " 		B1.B1_DESC, "
cQuery += " 		COALESCE(CASE WHEN W3.W3_PART_N = ? THEN A5.A5_CODPRF ELSE W3.W3_PART_N END, ' ') AS W3_PART_N, "
                    aAdd( aQryInfo , {'','C'})       
cQuery += " 		COALESCE(W2.W2_COMPRA, ' ')	AS W2_COMPRA, "
cQuery += " 		W3.W3_FLUXO     AS WKFLUXO, "
cQuery += " 		W3.W3_SALDO_Q	AS W7_QTDE, "
cQuery += " 		W3.W3_PRECO * (CASE WHEN W2.W2_PARID_U < ? THEN ? ELSE W2.W2_PARID_U END) AS W7_PRECO, "
                    aAdd( aQryInfo , {0,'N'})       
                    aAdd( aQryInfo , {1,'N'})       
cQuery += " 		W3.W3_PRECO * W3.W3_SALDO_Q * (CASE WHEN W2.W2_PARID_U < ? THEN ? ELSE W2.W2_PARID_U END) AS W7_PRECO_R, "
                    aAdd( aQryInfo , {0,'N'})       
                    aAdd( aQryInfo , {1,'N'})       
cQuery += " 		W3.W3_SI_NUM	AS W0__NUM, "
cQuery += " 		W3.W3_PGI_NUM	AS W5_PGI_NUM, "
cQuery += " 		' '				AS W5_SEQ_LI, "
cQuery += " 		' '				AS WP_REGIST, "
cQuery += " 		W0.W0__DT		AS W0__DT, "
cQuery += " 		CASE WHEN W2.W2_DT_ALTE = ' ' THEN W2.W2_PO_DT ELSE W2.W2_DT_ALTE END AS W2_DT_ALTE, "
cQuery += " 		W3.W3_DT_EMB	AS W6_DT_EMB, "
cQuery += " 		W1.W1_DTENTR_	AS W1_DTENTR_, "
cQuery += " 		W3.W3_REG		AS W7_REG, "
cQuery += " 		W3.W3_DT_ENTR	AS W5_DT_ENTR, "
cQuery += " 		YQ.YQ_DESCR		AS YQ_DESCR, "
//cQuery += " 		' '				AS WKOBS, "
cQuery += "         W3.W3_QTDE      AS W3_QTDE, "
cQuery += "         CASE WHEN EW0.EW0_NR_PRO IS NOT NULL THEN W3.W3_QTDE - (SELECT SUM(SALDO.EW0_QTDE) EW0_QTDE FROM " + RetSQLName("EW0") + " SALDO WHERE SALDO.EW0_FILIAL = ? AND SALDO.EW0_PO_NUM = W3.W3_PO_NUM AND SALDO.EW0_POSICA = W3.W3_POSICAO AND SALDO.D_E_L_E_T_ = ?) ELSE W3.W3_QTDE END AS WKSALDOPRO, "
                    aAdd( aQryInfo , {xFilial('EW0'),'C'})   
                    aAdd( aQryInfo , {'','C'})   
cQuery += "         W2.W2_HAWB_DA	AS W2_HAWB_DA, "
cQuery += "         W2.W2_VENCDA	AS W2_VENCDA, "
cQuery += "         ' '	            AS W5_DT_SHIP, "
cQuery += "         ' '             AS W5_DOCTO_F, "
cQuery += "         ' '             AS W6_CHEG, "
cQuery += "         ' '             AS WKW6DTEMB, "
cQuery += "         ' '             AS W5_FLUXO, "
cQuery += "         ' '             AS W5_DEF_REQ, "
cQuery += "         ' '             AS W4_SUFRAMA, "
cQuery += "         ' '             AS W4_DT_SUFR, "
cQuery += "         ' '             AS W4_PORTASN, "
cQuery += "         ' '             AS W4_FLUXO, "
cQuery += "         ' '             AS W4_DTEDCEX, "
cQuery += "         ' '             AS WP_MICRO, "
cQuery += "         ' '             AS WP_TRANSM, "
cQuery += "         ' '             AS WP_VENCTO, "
cQuery += "         ' '	            AS WP_PGI_NUM, "
cQuery += "         ' '	            AS W6_DT_HAWB, "
cQuery += "         ' '	            AS W6_DT_ENCE, "
cQuery += "         ' '             AS W6_DT, "
cQuery += "         ' '             AS W6_DT_AVE, "
cQuery += "         ' '             AS W6_TIPODES, "
cQuery += "         ' '             AS W6_DT_DESE, "
cQuery += "         ' '             AS W6_DTRECDO, "
cQuery += "         ' '             AS Y9_DAP, "
cQuery += "         ' '             AS Y9_SIGLA, "
cQuery += "         ' '             AS W6_DA_DT, "
cQuery += "         ' '             AS W6_DT_ENTR, "
cQuery += "         ' '             AS W6_DI_NUM, "
cQuery += "         ' '             AS W6_DT_DTA, "
cQuery += "         ' '             AS W6_PRVENTR, "
cQuery += " 		'PO'			AS WKTIPO "
cQuery += " FROM " + RetSqlName("SW2") + " W2 INNER JOIN "
cQuery +=          + RetSqlName("SW3") + " W3  ON (W2.W2_FILIAL = W3.W3_FILIAL AND W2.W2_PO_NUM = W3.W3_PO_NUM) INNER JOIN  "
cQuery +=          + RetSqlName("SW0") + " W0  ON (W0.W0_FILIAL = ? AND W0.W0__CC = W3.W3_CC AND W0.W0__NUM = W3.W3_SI_NUM AND W0.D_E_L_E_T_ = ?) INNER JOIN  "
                    aAdd( aQryInfo , {xFilial("SW0"),'C'})   
                    aAdd( aQryInfo , {'','C'})   
cQuery +=          + RetSqlName("SW1") + " W1  ON (W1.W1_FILIAL = ? AND W1.W1_CC = W3.W3_CC AND W1.W1_SI_NUM = W3.W3_SI_NUM AND W1.W1_COD_I = W3.W3_COD_I AND W1.W1_REG = W3.W3_REG AND W1.W1_SEQ = W3.W3_SEQ AND W1.D_E_L_E_T_ = ?) INNER JOIN "
                    aAdd( aQryInfo , {xFilial("SW1"),'C'})   
                    aAdd( aQryInfo , {'','C'})   
cQuery +=          + RetSqlName("SA2") + " A2  ON (A2.A2_FILIAL = ? AND A2.A2_COD = W3.W3_FORN AND A2.A2_LOJA = W3.W3_FORLOJ AND A2.D_E_L_E_T_ = ?) INNER JOIN "
                    aAdd( aQryInfo , {xFilial("SA2"),'C'})   
                    aAdd( aQryInfo , {'','C'})   
cQuery +=          + RetSqlName("SY3") + " Y3  ON (Y3.Y3_FILIAL = ? AND Y3.Y3_COD = W3.W3_CC AND Y3.D_E_L_E_T_ = ?) LEFT JOIN "
                    aAdd( aQryInfo , {xFilial("SY3"),'C'})   
                    aAdd( aQryInfo , {'','C'})   
cQuery +=          + RetSqlName("SB1") + " B1  ON (B1.B1_FILIAL = ? AND W3.W3_COD_I = B1.B1_COD AND B1.D_E_L_E_T_ = ?) LEFT JOIN "
                    aAdd( aQryInfo , {xFilial("SB1"),'C'})   
                    aAdd( aQryInfo , {'','C'})   
cQuery +=          + RetSqlName("SA5") + " A5  ON (A5.A5_FILIAL = ? AND W3.W3_FORN = A5.A5_FORNECE AND W3.W3_FORLOJ = A5.A5_LOJA AND W3.W3_COD_I = A5.A5_PRODUTO AND W3.W3_FABR = A5.A5_FABR AND W3.W3_FABLOJ = A5.A5_FALOJA AND A5.D_E_L_E_T_ = ?) LEFT JOIN "
                    aAdd( aQryInfo , {xFilial("SA5"),'C'})   
                    aAdd( aQryInfo , {'','C'})   
cQuery +=          + RetSqlName("SW6") + " W6  ON (W6.W6_FILIAL = ? AND W6.W6_HAWB = W2.W2_HAWB_DA AND W6.D_E_L_E_T_ = ?) LEFT JOIN "
                    aAdd( aQryInfo , {xFilial("SW6"),'C'})   
                    aAdd( aQryInfo , {'','C'})   
cQuery +=          + RetSqlName("EW0") + " EW0 ON (EW0.EW0_FILIAL = ? AND EW0.EW0_PO_NUM = W3.W3_PO_NUM AND EW0.EW0_POSICA = W3.W3_POSICAO AND EW0.D_E_L_E_T_ = ?) LEFT JOIN "
                    aAdd( aQryInfo , {xFilial("EW0"),'C'})   
                    aAdd( aQryInfo , {'','C'})   
cQuery +=          + RetSqlName("SYQ") + " YQ ON (YQ.YQ_FILIAL = ? AND YQ.YQ_VIA = W2.W2_TIPO_EM AND YQ.D_E_L_E_T_ = ?) "
                    aAdd( aQryInfo , {xFilial("SYQ"),'C'})   
                    aAdd( aQryInfo , {'','C'})   
cQuery += " WHERE "

//SE PREENCHIDO O CAMPO DE FILTRO DE FILIAL
cQuery += "       W3.W3_FILIAL = ? AND " //Filial
                  aAdd( aQryInfo , {xFilial("SW3"),'C'})   
//------------------------------------------------
//SE PREENCHIDO O CAMPO DE FILTRO DE PRODUTO
If !Empty(MV_PAR03) //Produto de ?
    cQuery += "    W3.W3_COD_I	>= ? AND " //PRODUTO
    aAdd( aQryInfo , {MV_PAR03,'C'})   
EndIf
If !Empty(MV_PAR04) //Produto até ?
    cQuery += "    W3.W3_COD_I	<= ? AND " //PRODUTO
    aAdd( aQryInfo , {MV_PAR04,'C'})       
EndIf
//------------------------------------------------
cQuery += "    W3.W3_SEQ		= ? AND "
aAdd( aQryInfo , {0,'N'})   
cQuery += "    W3.W3_SALDO_Q > ? AND "
aAdd( aQryInfo , {0,'N'})   
//SE PREENCHIDO OS CAMPOS DE FILTRO DE DATA
If !Empty(MV_PAR09) //Data Inicial
    cQuery += "    W2_PO_DT >= ? AND " //Data De
    aAdd( aQryInfo , {MV_PAR09,'D'})   
EndIf
If !Empty(MV_PAR10) //Data Final
    cQuery += "    W2_PO_DT <= ? AND " //Data Ate
    aAdd( aQryInfo , {MV_PAR10,'D'})   
EndIf
//------------------------------------------------

//SE PREENCHIDO OS CAMPOS DE FILTRO DE SI "
If !Empty(MV_PAR05) //SI de ?
    cQuery += "   W0.W0__NUM >= ? AND " //SI
    aAdd( aQryInfo , {MV_PAR05,'C'})       
EndIf
If !Empty(MV_PAR06) //SI até ?
    cQuery += "   W0.W0__NUM <= ? AND " //SI
    aAdd( aQryInfo , {MV_PAR06,'C'})           
EndIf

//------------------------------------------

//SE PREENCHIDO OS CAMPOS DE FILTRO DE PO "
If !Empty(MV_PAR07) //Pedido de ?
    cQuery += "   W2.W2_PO_NUM >= ? AND " //PO
    aAdd( aQryInfo , {MV_PAR07,'C'})               
EndIf
If !Empty(MV_PAR08) //Pedido até ?
    cQuery += "   W2.W2_PO_NUM <= ? AND " //PO
    aAdd( aQryInfo , {MV_PAR08,'C'})                   
EndIf
//------------------------------------------

cQuery += "    W2.D_E_L_E_T_ = ? AND "
aAdd( aQryInfo , {'','C'})                   
cQuery += "    W3.D_E_L_E_T_ = ? "
aAdd( aQryInfo , {'','C'})                   
//cQuery += " ORDER BY WKTIPO, WKFILIAL "

//===================FIM QUERY DADOS PO=======================//

Return cQuery

Static Function qryImpLI(aQryInfo)
Local cQuery := "" as character

cQuery := " SELECT	''              AS W6_DTEMB_D, "
cQuery += "         W5.W5_FILIAL	AS W2_FILIAL, "
cQuery += " 		W5.W5_PO_NUM	AS W2_PO_NUM, "
cQuery += " 		W5.W5_HAWB		AS W6_HAWB, "
cQuery += " 		W5.W5_CC		AS W1_CC, "
cQuery += " 		Y3.Y3_DESC		AS Y3_DESC, "
cQuery += " 		W5.W5_FORN		AS W7_FORN, "
cQuery += " 		W5.W5_FORLOJ	AS W7_FORLOJ, "
cQuery += " 		COALESCE(A2.A2_NREDUZ, ' ') AS A2_NREDUZ, "
cQuery += " 		W5.W5_POSICAO	AS W7_POSICAO, "
cQuery += " 		' '				AS W6_DESP, "
cQuery += " 		W5.W5_COD_I		AS W7_COD_I, "
cQuery += " 		B1.B1_DESC, "
cQuery += " 		COALESCE(CASE WHEN W3.W3_PART_N = ' ' THEN A5.A5_CODPRF ELSE W3.W3_PART_N END, ' ') AS W3_PART_N, "
cQuery += " 		COALESCE(W2.W2_COMPRA, ' ')	AS W2_COMPRA, "
cQuery += " 		W3.W3_FLUXO     AS WKFLUXO, "
cQuery += " 		W5.W5_SALDO_Q	AS W7_QTDE, "
cQuery += " 		W5.W5_PRECO * (CASE WHEN W2.W2_PARID_U < 0 THEN 1 ELSE W2.W2_PARID_U END) AS W7_PRECO, "
cQuery += " 		W5.W5_PRECO * W5.W5_SALDO_Q * (CASE WHEN W2.W2_PARID_U < 0 THEN 1 ELSE W2.W2_PARID_U END) AS W7_PRECO_R, "
cQuery += " 		W5.W5_SI_NUM	AS W0__NUM, "
cQuery += " 		W5.W5_PGI_NUM	AS W5_PGI_NUM, "
cQuery += " 		W5.W5_SEQ_LI	AS W5_SEQ_LI, "
cQuery += " 		COALESCE(WP.WP_REGIST, ' ')	AS WP_REGIST, "
cQuery += " 		W0.W0__DT		AS W0__DT, "
cQuery += " 		CASE WHEN W2.W2_DT_ALTE = ' ' THEN W2.W2_PO_DT ELSE W2.W2_DT_ALTE END AS W2_DT_ALTE, "
cQuery += " 		W5.W5_DT_EMB	AS W6_DT_EMB, "
cQuery += " 		W1.W1_DTENTR_	AS W1_DTENTR_, "
cQuery += " 		W5.W5_REG		AS W7_REG, "
cQuery += " 		W5.W5_DT_ENTR	AS W5_DT_ENTR, "
cQuery += " 		YQ.YQ_DESCR		AS YQ_DESCR, "
//cQuery += " 		' '				AS WKOBS, "
cQuery += "         W3.W3_QTDE      AS W3_QTDE, "
cQuery += "         0               AS WKSALDOPRO, "
cQuery += "         W2.W2_HAWB_DA	AS W2_HAWB_DA, "
cQuery += "         W2.W2_VENCDA	AS W2_VENCDA, "
cQuery += "         W5.W5_DT_SHIP	AS W5_DT_SHIP, "
cQuery += "         W5.W5_DOCTO_F   AS W5_DOCTO_F, "
cQuery += "         COALESCE(W6.W6_CHEG, ' ')      AS W6_CHEG, "
cQuery += "         COALESCE(W6.W6_DT_EMB, ' ')    AS WKW6DTEMB, "
cQuery += "         W5.W5_FLUXO     AS W5_FLUXO, "
cQuery += "         W5.W5_DEF_REQ   AS W5_DEF_REQ, "
cQuery += "         W4.W4_SUFRAMA   AS W4_SUFRAMA, "
cQuery += "         W4.W4_DT_SUFR   AS W4_DT_SUFR, "
cQuery += "         W4.W4_PORTASN   AS W4_PORTASN, "
cQuery += "         W4.W4_FLUXO     AS W4_FLUXO, "
cQuery += "         W4.W4_DTEDCEX   AS W4_DTEDCEX, "
cQuery += "         COALESCE(WP.WP_MICRO, ' ')     AS WP_MICRO, "
cQuery += "         COALESCE(WP.WP_TRANSM, ' ')    AS WP_TRANSM, "
cQuery += "         COALESCE(WP.WP_VENCTO, ' ')    AS WP_VENCTO, "
cQuery += "         COALESCE(WP.WP_PGI_NUM, ' ')   AS WP_PGI_NUM, "
cQuery += "         COALESCE(W6.W6_DT_HAWB, ' ')	AS W6_DT_HAWB, "
cQuery += "         COALESCE(W6.W6_DT_ENCE, ' ')   AS W6_DT_ENCE, "
cQuery += "         COALESCE(W6.W6_DT, ' ')   AS W6_DT, "
cQuery += "         COALESCE(W6.W6_DT_AVE, ' ')   AS W6_DT_AVE, "
cQuery += "         COALESCE(W6.W6_TIPODES, ' ')   AS W6_TIPODES, "
cQuery += "         COALESCE(W6.W6_DT_DESE, ' ')   AS W6_DT_DESE, "
cQuery += "         COALESCE(W6.W6_DTRECDO, ' ')   AS W6_DTRECDO, "
cQuery += "         ' '             AS Y9_DAP, "
cQuery += "         ' '             AS Y9_SIGLA, "
cQuery += "         COALESCE(W6.W6_DA_DT, ' ')   AS W6_DA_DT, "
cQuery += "         COALESCE(W6.W6_DT_ENTR, ' ')   AS W6_DT_ENTR, "
cQuery += "         COALESCE(W6.W6_DI_NUM, ' ')   AS W6_DI_NUM, "
cQuery += "         COALESCE(W6.W6_DT_DTA, ' ')   AS W6_DT_DTA, "
cQuery += "         COALESCE(W6.W6_PRVENTR, ' ')   AS W6_PRVENTR, "
cQuery += " 		'LI'			AS WKTIPO "
cQuery += " FROM " + RetSqlName("SW5") + " W5 INNER JOIN "
cQuery +=          + RetSqlName("SW4") + " W4 ON (W4.W4_FILIAL = W5.W5_FILIAL AND W4.W4_PGI_NUM = W5.W5_PGI_NUM) INNER JOIN  "
cQuery +=          + RetSqlName("SW2") + " W2 ON (W2.W2_FILIAL = ? AND W2.W2_PO_NUM = W5.W5_PO_NUM AND W2.D_E_L_E_T_ = ?) INNER JOIN  "
                   aAdd( aQryInfo , {xFilial("SW2"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SW3") + " W3 ON (W3.W3_FILIAL = ? AND W3.W3_PO_NUM = W5.W5_PO_NUM AND W3.W3_CC = W5.W5_CC AND W3.W3_SI_NUM = W5.W5_SI_NUM AND W3.W3_COD_I = W5.W5_COD_I AND W3.W3_REG = W5.W5_REG AND W5.W5_SEQ = W3.W3_SEQ AND W5.W5_POSICAO = W3.W3_POSICAO AND W3.W3_FORN = W5.W5_FORN AND W3.W3_FORLOJ = W5.W5_FORLOJ AND W3.D_E_L_E_T_ = ?) INNER JOIN  "
                   aAdd( aQryInfo , {xFilial("SW3"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SW0") + " W0 ON (W0.W0_FILIAL = ? AND W0.W0__CC = W5.W5_CC AND W0.W0__NUM = W5.W5_SI_NUM AND W0.D_E_L_E_T_ = ?) INNER JOIN  "
                   aAdd( aQryInfo , {xFilial("SW0"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SW1") + " W1 ON (W1.W1_FILIAL = ? AND W1.W1_CC = W5.W5_CC AND W1.W1_SI_NUM = W5.W5_SI_NUM AND W1.W1_COD_I = W5.W5_COD_I AND W1.W1_REG = W5.W5_REG AND W1.W1_SEQ = W5.W5_SEQ AND W1.D_E_L_E_T_ = ?) INNER JOIN "
                   aAdd( aQryInfo , {xFilial("SW1"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SA2") + " A2 ON (A2.A2_FILIAL = ? AND A2.A2_COD = W5.W5_FORN AND A2.A2_LOJA = W5.W5_FORLOJ AND A2.D_E_L_E_T_ = ?) INNER JOIN "
                   aAdd( aQryInfo , {xFilial("SA2"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SB1") + " B1 ON (B1.B1_FILIAL = ? AND B1.B1_COD = W5.W5_COD_I AND B1.D_E_L_E_T_ = ?) INNER JOIN  "
                   aAdd( aQryInfo , {xFilial("SB1"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SY3") + " Y3 ON (Y3.Y3_FILIAL = ? AND Y3.Y3_COD = W5.W5_CC AND Y3.D_E_L_E_T_ = ?) LEFT JOIN "
                   aAdd( aQryInfo , {xFilial("SY3"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SW6") + " W6DA ON (W6DA.W6_FILIAL = ? AND W6DA.W6_HAWB = W2.W2_HAWB_DA AND W6DA.D_E_L_E_T_ = ?) LEFT JOIN "
                   aAdd( aQryInfo , {xFilial("SW6"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SW6") + " W6 ON (W6.W6_FILIAL = ? AND W6.W6_HAWB = W5.W5_HAWB AND W6.D_E_L_E_T_ = ?) LEFT JOIN "
                   aAdd( aQryInfo , {xFilial("SW6"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SWP") + " WP ON (WP.WP_FILIAL = ? AND WP.WP_PGI_NUM = W5.W5_PGI_NUM AND WP.WP_SEQ_LI = W5.W5_SEQ_LI AND WP.D_E_L_E_T_ = ?) LEFT JOIN "
                   aAdd( aQryInfo , {xFilial("SWP"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SA5") + " A5 ON (A5.A5_FILIAL = ? AND A5.A5_FORNECE = W5.W5_FORN AND A5.A5_LOJA = W5.W5_FORLOJ AND A5.A5_PRODUTO = W5.W5_COD_I AND A5.A5_FABR = W5.W5_FABR AND A5.A5_FALOJA = W5.W5_FABLOJ AND A5.D_E_L_E_T_ = ?) LEFT JOIN "
                   aAdd( aQryInfo , {xFilial("SA5"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery +=          + RetSqlName("SYQ") + " YQ ON (YQ.YQ_FILIAL = ? AND YQ.YQ_VIA = W2.W2_TIPO_EM AND YQ.D_E_L_E_T_ = ?) "
                   aAdd( aQryInfo , {xFilial("SYQ"),'C'}) 
                   aAdd( aQryInfo , {'','C'}) 
cQuery += " WHERE "

//SE PREENCHIDO O CAMPO DE FILTRO DE FILIAL
cQuery += "       W4.W4_FILIAL = ? AND " //Filial
                  aAdd( aQryInfo , {xFilial("SW4"),'C'}) 
//SE PREENCHIDO O CAMPO DE FILTRO DE PRODUTO "
If !Empty(MV_PAR03) //Produto de ?
    cQuery += "    W5.W5_COD_I   >= ? AND " //Produto
                  aAdd( aQryInfo , {MV_PAR03,'C'})     
EndIf
If !Empty(MV_PAR04) //Produto até ?
    cQuery += "    W5.W5_COD_I	<= ? AND " //PRODUTO
    aAdd( aQryInfo , {MV_PAR04,'C'})     
EndIf
//---------------------------------------------
cQuery += "    W5.W5_SEQ		= ? AND "
aAdd( aQryInfo , {0,'N'})     
cQuery += "    W5.W5_SALDO_Q > ? AND "
aAdd( aQryInfo , {0,'N'})     

//SE PREENCHIDO OS CAMPOS DE FILTRO DE DATA "
If !Empty(MV_PAR09) //Data Inicial
    cQuery += "    W4_PGI_DT >= ? AND " //Data De
    aAdd( aQryInfo , {MV_PAR09,'D'})         
EndIf
If !Empty(MV_PAR10) //Data Final
    cQuery += "    W4_PGI_DT <= ? AND " //Data Ate
    aAdd( aQryInfo , {MV_PAR10,'D'})         
EndIf

//--------------------------

//SE PREENCHIDO OS CAMPOS DE FILTRO DE SI "
If !Empty(MV_PAR05) //SI de ?
    cQuery += "   W0.W0__NUM >= ? AND " //SI
    aAdd( aQryInfo , {MV_PAR05,'C'})             
EndIf
If !Empty(MV_PAR06) //SI até ?
    cQuery += "   W0.W0__NUM <= ? AND " //SI
    aAdd( aQryInfo , {MV_PAR06,'C'})             
EndIf

//------------------------------------------

//SE PREENCHIDO OS CAMPOS DE FILTRO DE PO "
If !Empty(MV_PAR07) //Pedido de ?
    cQuery += "   W2.W2_PO_NUM >= ? AND " //PO
    aAdd( aQryInfo , {MV_PAR07,'C'})                 
EndIf
If !Empty(MV_PAR08) //Pedido até ?
    cQuery += "   W2.W2_PO_NUM <= ? AND " //PO
    aAdd( aQryInfo , {MV_PAR08,'C'})                 
EndIf
//------------------------------------------

cQuery += "    W4.D_E_L_E_T_ = ? AND "
     aAdd( aQryInfo , {'','C'})                 
cQuery += "    W5.D_E_L_E_T_ = ? "
     aAdd( aQryInfo , {'','C'})                 
//cQuery += " ORDER BY WKTIPO, WKFILIAL "

Return cQuery


Static Function qryImpEMB(aQryInfo)
Local cQuery := "" as character

cQuery := " SELECT	W6.W6_DT_EMB    AS W6_DTEMB_D, "
cQuery += "         W7.W7_FILIAL	AS W2_FILIAL, "
cQuery += " 		W7.W7_PO_NUM	AS W2_PO_NUM, "
cQuery += " 		W7.W7_HAWB		AS W6_HAWB, "
cQuery += " 		W7.W7_CC		AS W1_CC, "
cQuery += " 		Y3.Y3_DESC		AS Y3_DESC, "
cQuery += " 		W7.W7_FORN		AS W7_FORN, "
cQuery += " 		W7.W7_FORLOJ	AS W7_FORLOJ, "
cQuery += " 		COALESCE(A2.A2_NREDUZ, ' ') AS A2_NREDUZ, "
cQuery += " 		W7.W7_POSICAO	AS W7_POSICAO, "
cQuery += " 		W6.W6_DESP		AS W6_DESP, "
cQuery += " 		W7.W7_COD_I		AS W7_COD_I, "
cQuery += " 		B1.B1_DESC, "
cQuery += " 		COALESCE(CASE WHEN W3.W3_PART_N = ' ' THEN A5.A5_CODPRF ELSE W3.W3_PART_N END, ' ') AS W3_PART_N, "
cQuery += " 		COALESCE(W2.W2_COMPRA, ' ')	AS W2_COMPRA, "
cQuery += " 		W7.W7_FLUXO     AS WKFLUXO, "
cQuery += " 		W7.W7_QTDE		AS W7_QTDE, "
cQuery += " 		W7.W7_PRECO * (CASE WHEN W2.W2_PARID_U < 0 THEN 1 ELSE W2.W2_PARID_U END) AS W7_PRECO, "
cQuery += " 		W7.W7_PRECO * W7.W7_QTDE * (CASE WHEN W2.W2_PARID_U < 0 THEN 1 ELSE W2.W2_PARID_U END) AS W7_PRECO_R, "
cQuery += " 		W7.W7_SI_NUM	AS W0__NUM, "
cQuery += " 		W5.W5_PGI_NUM	AS W5_PGI_NUM, "
cQuery += " 		W5.W5_SEQ_LI	AS W5_SEQ_LI, "
cQuery += " 		COALESCE(WP.WP_REGIST, ' ')	AS WP_REGIST, "
cQuery += " 		W0.W0__DT		AS W0__DT, "
cQuery += " 		CASE WHEN W2.W2_DT_ALTE = ? THEN W2.W2_PO_DT ELSE W2.W2_DT_ALTE END AS W2_DT_ALTE, "
                    aAdd(aQryInfo , {'','D'})                 
cQuery += " 		CASE WHEN W6.W6_DT_EMB <> ? THEN W6.W6_DT_EMB ELSE (CASE WHEN W6.W6_DT_ETD <> ? THEN W6.W6_DT_ETD ELSE W5.W5_DT_EMB END) END AS W6_DT_EMB, "
                    aAdd(aQryInfo , {'','D'})                 
                    aAdd(aQryInfo , {'','D'})                                     
cQuery += " 		W1.W1_DTENTR_	AS W1_DTENTR_, "
cQuery += " 		W7.W7_REG		AS W7_REG, "
cQuery += " 		W5.W5_DT_ENTR	AS W5_DT_ENTR, "
cQuery += " 		YQ.YQ_DESCR		AS YQ_DESCR, "
//cQuery += " 		' '				AS WKOBS, "
cQuery += "         W3.W3_QTDE      AS W3_QTDE, "
cQuery += "         0               AS WKSALDOPRO, "
cQuery += "         W2.W2_HAWB_DA   AS W2_HAWB_DA, "
cQuery += "         W2.W2_VENCDA	AS W2_VENCDA, "
cQuery += "         W5.W5_DT_SHIP	AS W5_DT_SHIP, "
cQuery += "         W5.W5_DOCTO_F   AS W5_DOCTO_F, "
cQuery += "         W6.W6_CHEG      AS W6_CHEG, "
cQuery += "         W6.W6_DT_EMB    AS WKW6DTEMB, "
cQuery += "         ' '             AS W5_FLUXO, "
cQuery += "         W5.W5_DEF_REQ   AS W5_DEF_REQ, "
cQuery += "         W4.W4_SUFRAMA   AS W4_SUFRAMA, "
cQuery += "         W4.W4_DT_SUFR   AS W4_DT_SUFR, "
cQuery += "         W4.W4_PORTASN   AS W4_PORTASN, "
cQuery += "         W4.W4_FLUXO     AS W4_FLUXO, "
cQuery += "         W4.W4_DTEDCEX   AS W4_DTEDCEX, "
cQuery += "         COALESCE(WP.WP_MICRO, ' ')   AS WP_MICRO, "
cQuery += "         COALESCE(WP.WP_TRANSM, ' ')   AS WP_TRANSM, "
cQuery += "         COALESCE(WP.WP_VENCTO, ' ')   AS WP_VENCTO, "
cQuery += "         COALESCE(WP.WP_PGI_NUM, ' ')   AS WP_PGI_NUM, "
cQuery += "         W6.W6_DT_HAWB	AS W6_DT_HAWB, "
cQuery += "         W6.W6_DT_ENCE   AS W6_DT_ENCE, "
cQuery += "         W6.W6_DT        AS W6_DT, "
cQuery += "         W6.W6_DT_AVE    AS W6_DT_AVE, "
cQuery += "         W6.W6_TIPODES   AS W6_TIPODES, "
cQuery += "         W6.W6_DT_DESE   AS W6_DT_DESE, "
cQuery += "         W6.W6_DTRECDO   AS W6_DTRECDO, "
cQuery += "         COALESCE(Y9.Y9_DAP, ' ')       AS Y9_DAP, "
cQuery += "         COALESCE(Y9.Y9_SIGLA, ' ')     AS Y9_SIGLA, "
cQuery += "         W6.W6_DA_DT     AS W6_DA_DT, "
cQuery += "         W6.W6_DT_ENTR   AS W6_DT_ENTR, "
cQuery += "         W6.W6_DI_NUM    AS W6_DI_NUM, "
cQuery += "         W6.W6_DT_DTA    AS W6_DT_DTA, "
cQuery += "         W6.W6_PRVENTR   AS W6_PRVENTR, "
cQuery += " 		'EMB'			AS WKTIPO "
cQuery += " FROM " + RetSqlName("SW7") + " W7 INNER JOIN "
cQuery +=          + RetSqlName("SW6") + " W6 ON (W6.W6_FILIAL = W7.W7_FILIAL AND W6.W6_HAWB = W7.W7_HAWB) INNER JOIN "
cQuery +=          + RetSqlName("SW2") + " W2 ON (W2.W2_FILIAL = ? AND W2.W2_PO_NUM = W7.W7_PO_NUM AND W2.D_E_L_E_T_ = ?) INNER JOIN "
                    aAdd(aQryInfo , {xFilial("SW2"),'C'})                 
                    aAdd(aQryInfo , {'','C'})                                     
cQuery +=          + RetSqlName("SW3") + " W3 ON (W3.W3_FILIAL = ? AND W3.W3_PO_NUM = W7.W7_PO_NUM AND W3.W3_CC = W7.W7_CC AND W3.W3_SI_NUM = W7.W7_SI_NUM AND W3.W3_COD_I = W7.W7_COD_I AND W3.W3_REG = W7.W7_REG AND W3.W3_SEQ = W7.W7_SEQ AND W3.W3_POSICAO = W7.W7_POSICAO AND W3.W3_FORN = W7.W7_FORN AND W3.W3_FORLOJ = W7.W7_FORLOJ AND W3.D_E_L_E_T_ = ?) INNER JOIN  "
                    aAdd(aQryInfo , {xFilial("SW3"),'C'})                 
                    aAdd(aQryInfo , {'','C'})                                     
cQuery +=          + RetSqlName("SW4") + " W4 ON (W4.W4_FILIAL = ? AND W4.W4_PGI_NUM = W7.W7_PGI_NUM AND W4.D_E_L_E_T_ = ?) INNER JOIN "
                    aAdd(aQryInfo , {xFilial("SW4"),'C'})                 
                    aAdd(aQryInfo , {'','C'})                                     
cQuery +=          + RetSqlName("SW5") + " W5 ON (W5.W5_FILIAL = ? AND W5.W5_PGI_NUM = W7.W7_PGI_NUM AND W5.W5_CC = W7.W7_CC AND W5.W5_SI_NUM = W7.W7_SI_NUM AND W5.W5_COD_I = W7.W7_COD_I AND W5.W5_POSICAO = W7.W7_POSICAO AND W5.W5_FORN = W7.W7_FORN AND W5.W5_REG = W7.W7_REG AND W5.W5_SEQ = W7.W7_SEQ AND W5.W5_PO_NUM = W7.W7_PO_NUM AND W5.D_E_L_E_T_ = ?) INNER JOIN "
                    aAdd(aQryInfo , {xFilial("SW5"),'C'})                 
                    aAdd(aQryInfo , {'','C'})                                     
cQuery +=          + RetSqlName("SW0") + " W0 ON (W0.W0_FILIAL = ? AND W0.W0__CC = W7.W7_CC AND W0.W0__NUM = W7.W7_SI_NUM AND W0.D_E_L_E_T_ = ?) INNER JOIN  "
                    aAdd(aQryInfo , {xFilial("SW0"),'C'})                 
                    aAdd(aQryInfo , {'','C'})                                     
cQuery +=          + RetSqlName("SW1") + " W1 ON (W1.W1_FILIAL = ? AND W1.W1_CC = W7.W7_CC AND W1.W1_SI_NUM = W7.W7_SI_NUM AND W1.W1_COD_I = W7.W7_COD_I AND W1.W1_REG = W7.W7_REG AND W1.W1_SEQ = W7.W7_SEQ AND W1.D_E_L_E_T_ = ?) INNER JOIN "
                    aAdd(aQryInfo , {xFilial("SW1"),'C'})                 
                    aAdd(aQryInfo , {'','C'})                                     
cQuery +=          + RetSqlName("SA2") + " A2 ON (A2.A2_FILIAL = ? AND A2.A2_COD = W7.W7_FORN AND A2.A2_LOJA = W7.W7_FORLOJ AND A2.D_E_L_E_T_ = ?) INNER JOIN "
                    aAdd(aQryInfo , {xFilial("SA2"),'C'})                 
                    aAdd(aQryInfo , {'','C'})                                     
cQuery +=          + RetSqlName("SB1") + " B1 ON (B1.B1_FILIAL = ? AND B1.B1_COD = W7.W7_COD_I AND B1.D_E_L_E_T_ = ?) INNER JOIN "
                    aAdd(aQryInfo , {xFilial("SB1"),'C'})                 
                    aAdd(aQryInfo , {'','C'})                                     
cQuery +=          + RetSqlName("SY3") + " Y3 ON (Y3.Y3_FILIAL = ? AND Y3.Y3_COD = W7.W7_CC AND Y3.D_E_L_E_T_ = ?) LEFT JOIN "
                    aAdd(aQryInfo , {xFilial("SY3"),'C'})                 
                    aAdd(aQryInfo , {'','C'})                                     
cQuery +=          + RetSqlName("SWP") + " WP ON (WP.WP_FILIAL = ? AND WP.WP_PGI_NUM = W7.W7_PGI_NUM AND WP.WP_SEQ_LI = W7.W7_SEQ_LI AND WP.D_E_L_E_T_ = ?) LEFT JOIN "
                    aAdd(aQryInfo , {xFilial("SWP"),'C'})                 
                    aAdd(aQryInfo , {'','C'})                                     
cQuery +=          + RetSqlName("SY9") + " Y9 ON (Y9.Y9_FILIAL = ? AND Y9.Y9_SIGLA = W6.W6_LOCAL AND Y9.D_E_L_E_T_ = ?) LEFT JOIN "
                    aAdd(aQryInfo , {xFilial("SY9"),'C'})                 
                    aAdd(aQryInfo , {'','C'})                                     
cQuery +=          + RetSqlName("SA5") + " A5 ON (A5.A5_FILIAL = ? AND A5.A5_FORNECE = W7.W7_FORN AND A5.A5_LOJA = W7.W7_FORLOJ AND A5.A5_PRODUTO = W7.W7_COD_I AND A5.A5_FABR = W7.W7_FABR AND A5.A5_FALOJA = W7.W7_FABLOJ AND A5.D_E_L_E_T_ = ?) LEFT JOIN "
                    aAdd(aQryInfo , {xFilial("SA5"),'C'})                 
                    aAdd(aQryInfo , {'','C'})                                     
cQuery +=          + RetSqlName("SYQ") + " YQ ON (YQ.YQ_FILIAL = ? AND YQ.YQ_VIA = W6.W6_VIA_TRA AND YQ.D_E_L_E_T_ = ?) "
                    aAdd(aQryInfo , {xFilial("SYQ"),'C'})                 
                    aAdd(aQryInfo , {'','C'})                                     
cQuery += " WHERE "

//SE PREENCHIDO O CAMPO DE FILTRO DE FILIAL
cQuery += "       W7.W7_FILIAL = ? AND " //Filial
                  aAdd(aQryInfo , {xFilial("SW7"),'C'})                 

//SE PREENCHIDO O CAMPO DE FILTRO DE PRODUTO "
If !Empty(MV_PAR03) //Produto de ?
    cQuery += "    W7.W7_COD_I	>= ? AND " //Produto
    aAdd(aQryInfo , {MV_PAR03,'C'})                 
EndIf
If !Empty(MV_PAR04) //Produto até ?
    cQuery += "    W7.W7_COD_I	<= ? AND " //PRODUTO
    aAdd(aQryInfo , {MV_PAR04,'C'})                 
EndIf
//--------------------------------------
cQuery += "    W7.W7_SEQ		= ? AND "
aAdd(aQryInfo , {0,'N'})                 
cQuery += "    W7.W7_SALDO_Q	> ? AND "
aAdd(aQryInfo , {0,'N'})                 

//SE PREENCHIDO OS CAMPOS DE FILTRO DE DATA "
If !Empty(MV_PAR09) //Data de ?
    cQuery += "    W6.W6_DT_HAWB >= ? AND " //Data Inicial
    aAdd(aQryInfo , {MV_PAR09,'D'})                     
EndIf
If !Empty(MV_PAR10) //Data até ?
    cQuery += "    W6.W6_DT_HAWB <= ? AND " //Data Final
    aAdd(aQryInfo , {MV_PAR10,'D'})                     
EndIf
//------------------------------------------

//SE PREENCHIDO OS CAMPOS DE FILTRO DE SI "
If !Empty(MV_PAR05) //SI de ?
    cQuery += "   W0.W0__NUM >= ? AND " //SI
    aAdd(aQryInfo , {MV_PAR05,'C'})                     
EndIf
If !Empty(MV_PAR06) //SI até ?
    cQuery += "   W0.W0__NUM <= ? AND " //SI
    aAdd(aQryInfo , {MV_PAR06,'C'})                     
EndIf

//------------------------------------------

//SE PREENCHIDO OS CAMPOS DE FILTRO DE PO "
If !Empty(MV_PAR07) //Pedido de ?
    cQuery += "   W2.W2_PO_NUM >= ? AND " //PO
    aAdd(aQryInfo , {MV_PAR07,'C'})                         
EndIf
If !Empty(MV_PAR08) //Pedido até ?
    cQuery += "   W2.W2_PO_NUM <= ? AND " //PO
    aAdd(aQryInfo , {MV_PAR08,'C'})                             
EndIf
//------------------------------------------
//FILTRO SE CONSIDERA ITENS ENTREGUES OU NÃO
If MV_PAR11 != ENTREGUES //Considera itens entregues 1-Sim/2-Não
    cQuery += "   W6.W6_DT_ENTR = ? AND "
    aAdd(aQryInfo , {'','D'})                             
EndIf
//------------------------------------------
cQuery += "    W7.D_E_L_E_T_ = ? AND "
aAdd(aQryInfo , {'','C'})                         
cQuery += "    W6.D_E_L_E_T_ = ? "
aAdd(aQryInfo , {'','C'})                         
//cQuery += " ORDER BY WKTIPO, WKFILIAL "

Return cQuery

Static Function getStatPO(cAlias)
Local cRet := "" as character
    
If (cAlias)->(WKSALDOPRO) == 0
    cRet := STR0043//"CONFECÇÃO P.L.I."
Else
    cRet := STR0118 + ALLTRIM(STR((cAlias)->(WKSALDOPRO))) + Space(1) + STR0042 + " / " //"Saldo: "  #### "AGUARDANDO PROFORMA"
    cRet += STR0119 + ALLTRIM(STR(((cAlias)->(W3_QTDE) - (cAlias)->(WKSALDOPRO))))  + Space(1) + STR0043 //"Qtde: "####"CONFECCAO P.L.I."
EndIf

If !Empty((cAlias)->(W2_HAWB_DA))
    If (cAlias)->(W2_VENCDA) < dDataBase
        cRet := STR0097 // "DI NACIONALIZACAO VENCIDA"
    Else
        cRet := STR0101 + (cAlias)->(W2_HAWB_DA) + " " + STR0098 + Alltrim(Str((cAlias)->(W2_VENCDA) - dDataBase)) + STR0099   // "ENTREP."### "VENCERA EM " ### " DIAS A DI NAC."
    EndIf
EndIf

Return cRet

Static Function getStatPLI(cAlias)
Local cRet := "" as character

If Empty((cAlias)->(W2_HAWB_DA))
    If (cAlias)->(W6_DT_EMB) >= dDataBase //Quando executada query LI, o campo W5_DT_EMB foi renomeado para W6_DT_EMB
        cRet := STR0045 + IF(Empty((cAlias)->(W5_DT_SHIP)), STR0046,"") //"AG. EMBARQUE" #####" S/ SHIP.INSTRUCTIONS"
    Else
        cRet := STR0047 + IF(Empty((cAlias)->(W5_DT_SHIP)), STR0046,"") //"NAO EMBARCADO "#####" S/ SHIP.INSTRUCTIONS"
    EndIf
EndIf

If !Empty((cAlias)->(W5_DOCTO_F)) .And. Empty((cAlias)->(W2_HAWB_DA))
    cRet := STR0050 + DTOC((cAlias)->(W6_DT_EMB)) //"EMBARQUE CONFIRMADO P/ "
    IF (cAlias)->(W6_DT_EMB) > dDataBase
        cRet := STR0047 + IF(Empty((cAlias)->W5_DT_SHIP), STR0046, STR0049) // //"NAO EMBARCADO "######"S/ SHIP.INSTRUCTIONS"######"C/ SHIP.INSTRUCTIONS"
    EndIf
EndIf
/*
If !Empty((cAlias)->(W6_HAWB))//Quando executada query LI, o campo W5_HAWB foi renomeado para W6_HAWB
    If Empty((cAlias)->(W6_CHEG)) .And. (cAlias)->(WKW6DTEMB) >= dDataBase .and. Empty((cAlias)->(W2_HAWB_DA))
        cRet = STR0050 + DTOC((cAlias)->WKW6DTEMB) //"EMBARQUE CONFIRMADO P/ "
    EndIf
EndIf
*/
If (cAlias)->(W5_FLUXO) == "5"
    cRet := STR0053 //"AG. PROCESSO DE NACIONALIZACAO"
EndIf

If (cAlias)->(W5_DEF_REQ) == 'S'
    cRet := STR0054//"AG. DEFINICAO DO REQUISITANTE"
EndIf

If (cAlias)->(W6_DT_EMB) > dDataBase //Quando executada query LI, o campo W5_DT_EMB foi renomeado para W6_DT_EMB
    cRet := STR0115 //"EMBARQUE PENDENTE" 
EndIf

If (cAlias)->(W4_SUFRAMA) == "S"
    If Empty((cAlias)->(W4_DT_SUFR))
        cRet := STR0055//"AGUARDANDO ENVIO AO SUFRAMA"
    EndIf
Else
    If (cAlias)->(W4_PORTASN) # "S" .And. (cAlias)->(W4_FLUXO) # "7"
        If (cAlias)->(W4_FLUXO) # "4"
            If !Empty((cAlias)->(WP_PGI_NUM)) .And. Empty((cAlias)->(WP_MICRO)) .And. Empty((cAlias)->(WP_REGIST))
                cRet := STR0056//"AGUARDANDO ENVIO AO ORIENTADOR"
            ElseIf !Empty((cAlias)->(WP_PGI_NUM)) .And. Empty((cAlias)->(WP_TRANSM))
                cRet := STR0057//"AGUARDANDO ENVIO AO ORGAO ANUENTE"
            ElseIf !Empty((cAlias)->(WP_PGI_NUM)) .And. !Empty((cAlias)->(WP_MICRO)) .And. !Empty((cAlias)->(WP_REGIST))
                cRet := STR0045//"AG. EMBARQUE"
            ElseIf !Empty((cAlias)->(WP_PGI_NUM)) .And. !Empty((cAlias)->(WP_MICRO)) .And. Empty((cAlias)->(WP_REGIST))
                cRet := STR0120 //"AGUARD. RETORNO DA L.I."
            ElseIf !Empty((cAlias)->(WP_PGI_NUM)) .And. Empty((cAlias)->(WP_MICRO)) .And. !Empty((cAlias)->(WP_REGIST))
                cRet := STR0045//"AG. EMBARQUE"
            EndIf
        EndIf
    EndIf
EndIf

If (cAlias)->(W4_SUFRAMA) == "S"
    If !Empty((cAlias)->(W4_DT_SUFR))
        If (cAlias)->(W4_PORTASN) # "S" .And. (cAlias)->(W4_FLUXO) # "7"
            If Empty((cAlias)->(W4_DTEDCEX))
                cRet := STR0058 + DTOC((cAlias)->(W4_DT_SUFR)) //"PROC. NA SUFRAMA DESDE "
            EndIf
        EndIf
    EndIf
EndIf

If (cAlias)->(W4_PORTASN) # "S" .And. (cAlias)->(W4_FLUXO) # "7"
    If Empty((cAlias)->(WP_PGI_NUM)) .And. !Empty((cAlias)->(WP_TRANSM)) .And. Empty((cAlias)->(WP_VENCTO))
        cRet := STR0059 + DTOC((cAlias)->(WP_TRANSM)) //"EM ANUENCIA DESDE "
    EndIf
EndIf

If !Empty((cAlias)->(W2_HAWB_DA))
    If (cAlias)->(W2_VENCDA) < dDataBase
        cRet := STR0097// "DI NACIONALIZACAO VENCIDA"
    Else
        cRet := STR0101 + (cAlias)->(W2_HAWB_DA) + " " + STR0098 + Alltrim(Str((cAlias)->(W2_VENCDA) - dDataBase)) + STR0099  //"ENTREP."### "VENCERA EM "" ### " DIAS A DI NAC."
    EndIf
EndIf

Return cRet

Static Function getStatEMB(cAlias)
Local cRet := "" as character

If !Empty((cALias)->(WKW6DTEMB))
    If Empty((cALias)->(W2_HAWB_DA))
        IF Empty((cAlias)->(W6_CHEG))
            cRet := STR0068 + DTOC((cALias)->(WKW6DTEMB)) //"EM TRÃNSITO DESDE "
        Else
            cRet := STR0061 + DTOC((cAlias)->(W6_CHEG)) //"ATRACADO EM "
        EndIf
    Else
        cRet := STR0100 + DTOC((cAlias)->(W6_DT_HAWB)) //"AGUARDANDO NACIONALIZAÇÃO DESDE "
    EndIf
ENDIF

If !Empty((cAlias)->(W6_DT_ENCE))
    cRet := STR0112 //"ENCERRADO"
EndIf

If !Empty((cAlias)->(W6_CHEG))
    If Empty((cAlias)->(W6_DT)) .And. !Empty((cAlias)->(W6_DT_AVE))
        If !Alltrim((cAlias)->(W6_TIPODES)) $ ("2/3/4/02/03/04")    //Diferente de DA
            cRet := STR0062 + DTOC((cAlias)->(W6_DT_AVE)) //"AG. PGTO IMPOSTOS DESDE "
        Else
            cRet := STR0094 //+ DTOC(SW6->W6_DT_AVE) "AG. DECLARACAO DE ADMISSÃO "
        EndIf
    ENDIF
ENDIF

If !Empty((cAlias)->(W6_DT))
    If Empty((cAlias)->(W6_DT_DESE))
        If !Alltrim((cAlias)->(W6_TIPODES)) $ ("2/3/4/02/03/04") .And. Empty((cAlias)->(W2_HAWB_DA)) // DI
            cRet := STR0063 + DTOC((cAlias)->(W6_DT)) //"AG. DESEMBARACO DESDE "
        ElseIf !Alltrim((cAlias)->(W6_TIPODES)) $ ("2/3/4/02/03/04") .And. !Empty((cAlias)->(W2_HAWB_DA)) // DI Nac.
            cRet := STR0100 + DTOC((cAlias)->(W6_DT))//"AG. NACIONALIZACAO DESDE "### + DT_PAGTO IMP.
        Else // DA
            cRet := STR0095 //+ DTOC(SW6->W6_DT+MDias) //"AG. ENTREPOSTAMENTO "
        EndIf
    Else
        If !Alltrim((cAlias)->(W6_TIPODES)) $ ("2/3/4/02/03/04")  //Diferente de DA
            cRet := STR0064 //"AGUARDANDO ENTREGA "
        Else
            cRet := STR0095 //"AG. ENTREPOSTAMENTO "
        EndIf
    EndIf
EndIf

If Empty((cAlias)->(W6_DT)) // && Alterado em 27-04-93 pedido p/ Atilio
    If !Empty((cAlias)->(W6_DTRECDO)) .And. !Empty((cAlias)->(W6_CHEG))
        If !Empty((cAlias)->(Y9_SIGLA)) .And. (cAlias)->(Y9_DAP) $ cSim
            cRet := STR0065 //"MAT. AGUARDANDO NO DAP"
        EndIf
    EndIf
EndIf

If (cAlias)->(W6_DT_EMB) > dDataBase
    cRet := STR0115  //"EMBARQUE PENDENTE" 
EndIf

IF (cAlias)->(WKFLUXO) == "4"
    IF !Empty((cAlias)->(W6_DA_DT))
        cRet := STR0095 //"AG. ENTREPOSTAMENTO"
    ENDIF
ENDIF

If !Alltrim((cAlias)->(W6_TIPODES)) $ ("2/3/4/02/03/04") .And. !Empty((cAlias)->(W6_DT_ENTR)) .And. Empty((cAlias)->(W2_HAWB_DA)) //Diferente de DA
    cRet := STR0067 + DTOC((cAlias)->(W6_DT_ENTR)) //"ENTREGUE EM "
ElseIf Alltrim((cAlias)->(W6_TIPODES)) $ ("2/3/4/02/03/04/14") .And. !Empty((cAlias)->(W2_HAWB_DA))// DA  // GFP - 06/08/2015
    If Empty((cAlias)->(W6_DI_NUM))
        cRet := STR0095 //"AG. ENTREPOSTAMENTO"
    Else
        cRet := STR0066 + DTOC((cAlias)->(W6_DT_DTA)) //"ENTREPOSTADO EM "
    EndIf
EndIf

IIf(Empty((cAlias)->(W6_DTEMB_D)) .And. Empty(cRet) .And. Empty((cAlias)->(W6_PRVENTR)),cRet := STR0096 , "") // "AGUARD. CONFIRMACAO DE EMBARQUE"

If !Empty((cAlias)->(W6_PRVENTR)) .And. Empty((cAlias)->(W6_DT_ENTR)) .And. Empty((cAlias)->(W6_CHEG))
    cRet := STR0113 + DTOC((cAlias)->(W6_PRVENTR)) //"PREVISAO DE ENTREGA EM "
EndIf

Return cRet
