#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "Average.ch"
#include "manufacturing.comex.export.smartview.orderbacklog.ch"

namespace totvs.protheus.manufacturing.comex.export.smartview

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEEC", tables="EE7, EE8, EE9, EEC, EXL", name="Carteira de Pedidos", country="ALL", initialRelease="12.1.2210")
/*/{Protheus.doc} CarteiraPedidosTReportsBusinessObject
Classe para criação do Objeto de Negócio para a listagem da Carteira de Pedidos (Antigo EECPRL13_RDM)
@author tiago tudisco
@since 29/08/2023
/*/
class CarteiraPedidosTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass

/*/{Protheus.doc} new
Método de instância da classe
@author tiago tudisco
@since 29/08/2023
/*/ 
method new() class CarteiraPedidosTReportsBusinessObject
_Super:new()
//Define a Área
self:appendArea("Easy Export Control") //"Easy Export Control"
 
//Define o nome do Objeto de Negócio
self:setDisplayName(STR0001) // "Carteira de Pedidos"
 
//Define a descrição do Objeto de Negócio
self:setDescription(STR0002)// "Este relatório visa apresentar a carteira dos pedidos de exportação, com seus respectivos saldos e Embarques."

self:setPergunte("EECPRL13") //Indica o pergunte que será utilizado no relatório

return self
 
/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@author tiago tudisco
@since 03/08/2023
/*/ 
method getData(nPage as numeric, oFilter as object) as object class CarteiraPedidosTReportsBusinessObject
local jParams  as json
local cAlias   as character
//Define a quantidade máxima por página (Default 100)
self:setPageSize(100)
 
jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

MV_PAR01 := AvKey(jParams["MV_PAR01"][1],"EE7_PEDIDO") //Pedido de ?
MV_PAR02 := AvKey(jParams["MV_PAR02"][1],"EE7_PEDIDO") //Pedido até ?
MV_PAR03 := IIF(!Empty(jParams["MV_PAR03"][1]),DToS(FWDateTimeToLocal(jParams["MV_PAR03"][1])[1]),"") //Data de Entrega de ?
MV_PAR04 := IIF(!Empty(jParams["MV_PAR04"][1]),DToS(FWDateTimeToLocal(jParams["MV_PAR04"][1])[1]),"") //Data de Entrega até ?
MV_PAR05 := AvKey(jParams["MV_PAR05"][1],"EE8_FABR")//Fabricante ?
MV_PAR06 := AvKey(jParams["MV_PAR06"][1],"EE8_FALOJA")//Loja do Fabricante ?
MV_PAR07 := AvKey(jParams["MV_PAR07"][1],"EE7_EXPORT")//Exportador ?
MV_PAR08 := AvKey(jParams["MV_PAR08"][1],"EE7_EXLOJA")//Loja do Exportador ?
MV_PAR09 := AvKey(jParams["MV_PAR09"][1],"EE7_IMPORT")//Importador ?
MV_PAR10 := AvKey(jParams["MV_PAR10"][1],"EE7_IMLOJA")//Loja do Importador ?
MV_PAR11 := AvKey(jParams["MV_PAR11"][1],"A1_PAIS")//País do Importador ?
MV_PAR12 := if( valtype(jParams["MV_PAR12"][1]) == "N", jParams["MV_PAR12"][1], val(jParams["MV_PAR12"][1])) //Pedido sem Saldo ? 1-Sim/2-Não
MV_PAR13 := AvKey(jParams["MV_PAR13"][1],"EE8_UNIDAD")//Unidade do Total ?
MV_PAR14 := AvKey(jParams["MV_PAR14"][1],"EE7_MOEDA")//Moeda do Total ?

cAlias := getNextAlias()
setCartPed(@self:oData)

return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@author tiago tudisco
@since 03/08/2023
/*/ 
method getSchema() as object class CarteiraPedidosTReportsBusinessObject
local aFieldsEE7 as array
local aFieldsEE8 as array
local aFieldsSA1 as array
//local aFieldsSA2 as array
local aFieldsEE9 as array
local aFieldsEEC as array
local aFieldsEXL as array
local aCustomFld as array
local aTableCustom as array
local nI as numeric
local nY as numeric

aFieldsEE7 := {"EE7_PEDIDO", "EE7_PEDFAT", "EE7_DTPEDI", "EE7_IMPORT", "EE7_IMLOJA", "EE7_REFIMP", "EE7_MOEDA", "EE7_FRPREV", "EE7_FRPCOM", "EE7_SEGPRE", "EE7_DESPIN", "EE7_DESCON", "EE7_PRECOA", "EE7_INCOTE", "EE7_CONDPA", "EE7_DIASPA", "EE7_VIA", "EE7_ORIGEM", "EE7_DEST"}
aFieldsEE8 := {"EE8_DTENTR","EE8_FABR", "EE8_FALOJA", "EE8_FORN", "EE8_FOLOJA", "EE8_UNIDAD", "EE8_SLDINI", "EE8_PRECOI", "EE8_PRCINC", "EE8_SLDATU", "EE8_COD_I", "EE8_DTPREM", "EE8_CODAGE", "EE8_PERCOM", "EE8_VLCOM", "EE8_SEQUEN", "EE8_VM_DES", "EE8_DESC"}
aFieldsSA1 := {"A1_NREDUZ"}
//aFieldsSA2 := {""}
aFieldsEE9 := {"EE9_CODAGE", "EE9_PERCOM", "EE9_VLCOM" ,"EE9_NF", "EE9_SERIE", "EE9_SLDINI", "EE9_PRECOI", "EE9_PRCINC", "EE9_UNIDAD"}
aFieldsEEC := {"EEC_PREEMB", "EEC_NRODUE", "EEC_DUEAVR", "EEC_DTDUE", "EEC_NRORUC", "EEC_DTEMBA", "EEC_DTCONH", "EEC_NRCONH", "EEC_VIA", "EEC_ORIGEM", "EEC_DEST", "EEC_CONDPA", "EEC_DIASPA", "EEC_INCOTE", "EEC_PRECOA" ,"EEC_FRPREV", "EEC_FRPCOM", "EEC_SEGPRE", "EEC_DESPIN", "EEC_DESCON", "EEC_PAISDT", "EEC_PAISET"}
aFieldsEXL := {}

aTableCustom := {"EE7", "EE8", "EE9", "EEC", "EXL"}
For nI := 1 To Len(aTableCustom)
    aCustomFld := TECustomCpo(aTableCustom[nI], .F.)
    If !Empty(aCustomFld)
        For nY := 1 To Len(aCustomFld)
            aAdd(&("aFields"+aCustomFld[nY][5]), aCustomFld[nY][1])
        Next
    EndIf
Next

self:oSchema:aliasToSchema("EE7", aFieldsEE7)
self:oSchema:aliasToSchema("EE8", aFieldsEE8)
self:oSchema:aliasToSchema("SA1", aFieldsSA1)
//self:oSchema:aliasToSchema("SA2", aFieldsSA2)
self:oSchema:aliasToSchema("EE9", aFieldsEE9)
self:oSchema:aliasToSchema("EEC", aFieldsEEC)
self:oSchema:aliasToSchema("EXL", aFieldsEXL)

self:oSchema:addProperty("DESCPROD"     , STR0003   , "string"  , STR0003   , "DESCPROD") //"Descrição do Item"
self:oSchema:addProperty("FAB_NREDUZ"   , STR0004   , "string"  , STR0004   , "FAB_NREDUZ") //"Nome Fabricante"
self:oSchema:addProperty("FORN_NREDUZ"  , STR0005   , "string"  , STR0005   , "FORN_NREDUZ") //"Nome Fornecedor"
self:oSchema:addProperty("UNIDTOTAL"    , STR0006   , "number"  , STR0006   , "UNIDTOTAL") //"Unidade Total"
self:oSchema:addProperty("MOEDTOTAL"    , STR0007   , "number"  , STR0007   , "MOEDTOTAL") //"Moeda Total"
self:oSchema:addProperty("SIMBUNIDTOTAL", STR0008   , "string"  , STR0008   , "SIMBUNIDTOTAL") //"Simb Unidade Total"
self:oSchema:addProperty("SIMBMOEDTOTAL", STR0009   , "string"  , STR0009   , "SIMBMOEDTOTAL") //"Simb Moeda Total"
self:oSchema:addProperty("TAXATOTAL"    , STR0010   , "number"  , STR0010   , "TAXATOTAL") //"Taxa Moeda Total"

return self:oSchema

/*/{Protheus.doc} setCartPed
Monta json com os dados do relatório
@author Tiago Tudisco
@since 29/08/2023
/*/ 
Static Function setCartPed(oDados)
Local jItens     as json
Local cAlias     as character
Local aCustomFld as Array
Local nI

aCustomFld := TECustomFld({"EE7", "EE8", "EE9", "EEC", "EXL"}) //Pega os campos customizados
cALias := getNextAlias()
getQryPed(cAlias, aCustomFld)

If Empty(MV_PAR13)//Unidade do Total
    MV_PAR13 := "KG"
EndIf
If Empty(MV_PAR14)//Moeda do Total
    MV_PAR14 := "US$"
EndIf

While (cAlias)->(!Eof())
    jItens := JsonObject():new()

    jItens["EE7_PEDIDO"]    := (cAlias)->(EE7_PEDIDO)
    jItens["EE8_SEQUEN"]    := (cAlias)->(EE8_SEQUEN)
    jItens["EE7_PEDFAT"]    := (cAlias)->(EE7_PEDFAT)
    jItens["EE7_DTPEDI"]    := IIF(!Empty((cAlias)->(EE7_DTPEDI)),FwTimeStamp( 5, (cAlias)->(EE7_DTPEDI)),)
    jItens["EE8_DTENTR"]    := IIF(!Empty((cAlias)->(EE8_DTENTR)),FwTimeStamp( 5, (cAlias)->(EE8_DTENTR)),)
    jItens["EE7_IMPORT"]    := (cAlias)->(EE7_IMPORT)
    jItens["EE7_IMLOJA"]    := (cAlias)->(EE7_IMLOJA)
    jItens["A1_NREDUZ"]     := (cAlias)->(A1_NREDUZ)
    jItens["EE7_REFIMP"]    := (cAlias)->(EE7_REFIMP)
    jItens["EE8_FABR"]      := (cAlias)->(EE8_FABR)
    jItens["EE8_FALOJA"]    := (cAlias)->(EE8_FALOJA)
    jItens["FAB_NREDUZ"]    := (cAlias)->(FAB_NREDUZ)
    jItens["EE8_FORN"]      := (cAlias)->(EE8_FORN)
    jItens["EE8_FOLOJA"]    := (cAlias)->(EE8_FOLOJA)
    jItens["FORN_NREDUZ"]   := (cAlias)->(FORN_NREDUZ)
    jItens["EE8_UNIDAD"]    := (cAlias)->(EE8_UNIDAD)
    jItens["EE8_SLDINI"]    := (cAlias)->(EE8_SLDINI)
    jItens["EE8_PRECOI"]    := (cAlias)->(EE8_PRECOI)
    jItens["EE7_MOEDA"]     := (cAlias)->(EE7_MOEDA)
    jItens["EE8_PRCINC"]    := (cAlias)->(EE8_PRCINC)
    jItens["EE9_SLDINI"]    := (cAlias)->(EE9_SLDINI)
    jItens["EE8_SLDATU"]    := (cAlias)->(EE8_SLDATU)
    jItens["EE8_COD_I"]     := (cAlias)->(EE8_COD_I)
    jItens["DESCPROD"]      := memoline(MSMM((cAlias)->(EE8_DESC),AVSX3("EE8_VM_DES",3)),30,1)
    jItens["EE8_DTPREM"]    := IIF(!Empty((cAlias)->(EE8_DTPREM)),FwTimeStamp( 5, (cAlias)->(EE8_DTPREM)),)
    jItens["EE7_FRPREV"]    := (cAlias)->(EE7_FRPREV)
    jItens["EE7_FRPCOM"]    := (cAlias)->(EE7_FRPCOM)
    jItens["EE7_SEGPRE"]    := (cAlias)->(EE7_SEGPRE)
    jItens["EE7_DESPIN"]    := (cAlias)->(EE7_DESPIN)
    jItens["EE7_DESCON"]    := (cAlias)->(EE7_DESCON)
    jItens["EE7_PRECOA"]    := (cAlias)->(EE7_PRECOA)
    jItens["EE7_INCOTE"]    := (cAlias)->(EE7_INCOTE)
    jItens["EE7_CONDPA"]    := (cAlias)->(EE7_CONDPA)
    jItens["EE7_DIASPA"]    := (cAlias)->(EE7_DIASPA)
    jItens["EE7_VIA"]       := (cAlias)->(EE7_VIA)
    jItens["EE7_ORIGEM"]    := (cAlias)->(EE7_ORIGEM)
    jItens["EE7_DEST"]      := (cAlias)->(EE7_DEST)
    jItens["EE8_CODAGE"]    := (cAlias)->(EE8_CODAGE)
    jItens["EE8_PERCOM"]    := (cAlias)->(EE8_PERCOM)
    jItens["EE8_VLCOM"]     := (cAlias)->(EE8_VLCOM)
    jItens["EEC_PREEMB"]    := (cAlias)->(EEC_PREEMB)
    jItens["EEC_NRODUE"]    := (cAlias)->(EEC_NRODUE)
    jItens["EEC_DUEAVR"]    := IIF(!Empty((cAlias)->(EEC_DUEAVR)),FwTimeStamp(5, (cAlias)->(EEC_DUEAVR)),)
    jItens["EEC_DTDUE"]     := IIF(!Empty((cAlias)->(EEC_DTDUE)),FwTimeStamp( 5, (cAlias)->(EEC_DTDUE)),)
    jItens["EEC_NRORUC,	"]  := (cAlias)->(EEC_NRORUC)
    jItens["EEC_DTEMBA"]    := IIF(!Empty((cAlias)->(EEC_DTEMBA)),FwTimeStamp( 5, (cAlias)->(EEC_DTEMBA)),)
    jItens["EEC_DTCONH,	"]  := IIF(!Empty((cAlias)->(EEC_DTCONH)),FwTimeStamp( 5, (cAlias)->(EEC_DTCONH)),)
    jItens["EEC_NRCONH"]    := (cAlias)->(EEC_NRCONH)
    jItens["EEC_VIA"]       := (cAlias)->(EEC_VIA)
    jItens["EEC_ORIGEM"]    := (cAlias)->(EEC_ORIGEM)
    jItens["EEC_DEST"]      := (cAlias)->(EEC_DEST)
    jItens["EEC_CONDPA"]    := (cAlias)->(EEC_CONDPA)
    jItens["EEC_DIASPA"]    := (cAlias)->(EEC_DIASPA)
    jItens["EEC_INCOTE"]    := (cAlias)->(EEC_INCOTE)
    jItens["EEC_PRECOA"]    := (cAlias)->(EEC_PRECOA)
    jItens["EEC_FRPREV"]    := (cAlias)->(EEC_FRPREV)
    jItens["EEC_FRPCOM"]    := (cAlias)->(EEC_FRPCOM)
    jItens["EEC_SEGPRE"]    := (cAlias)->(EEC_SEGPRE)
    jItens["EEC_DESPIN"]    := (cAlias)->(EEC_DESPIN)
    jItens["EEC_DESCON"]    := (cAlias)->(EEC_DESCON)
    jItens["EEC_PAISDT"]    := (cAlias)->(EEC_PAISDT)
    jItens["EEC_PAISET"]    := (cAlias)->(EEC_PAISET)
    jItens["EE9_CODAGE"]    := (cAlias)->(EE9_CODAGE)
    jItens["EE9_PERCOM"]    := (cAlias)->(EE9_PERCOM)
    jItens["EE9_VLCOM"]     := (cAlias)->(EE9_VLCOM)
    jItens["EE9_NF"]        := (cAlias)->(EE9_NF)
    jItens["EE9_SERIE"]      := (cAlias)->(EE9_SERIE)
    jItens["EE9_PRECOI"]    := (cAlias)->(EE9_PRECOI)
    jItens["EE9_PRCINC"]    := (cAlias)->(EE9_PRCINC)
    jItens["EE9_UNIDAD"]    := (cAlias)->(EE9_UNIDAD)
    jItens["UNIDTOTAL"]     := AVTransUnid((cAlias)->(EE8_UNIDAD), MV_PAR13, (cAlias)->(EE8_COD_I), (cAlias)->(EE8_SLDINI))
    jItens["MOEDTOTAL"]     := EECCalcTaxa((cAlias)->(EE7_MOEDA), MV_PAR14, (cAlias)->(EE8_PRCINC), 2)
    jItens["TAXATOTAL"]     := BuscaTaxa(MV_PAR14, dDataBase)
    jItens["SIMBUNIDTOTAL"] := MV_PAR13
    jItens["SIMBMOEDTOTAL"] := MV_PAR14

    //Tratamento para campos customizados
    For nI := 1 to len(aCustomFld)
        If aCustomFld[nI][2] == "D"
            jItens[aCustomFld[nI][1]] := IIF(!Empty((cAlias)->&(aCustomFld[nI][1])),FwTimeStamp( 5, (cAlias)->&(aCustomFld[nI][1])),)
        Else
            jItens[aCustomFld[nI][1]] := (cAlias)->&(aCustomFld[nI][1])
        EndIf
    Next

    oDados:appendData(jItens)
    FreeObj(jItens)

    (cAlias)->(dbSkip())
End

(cAlias)->(dbCloseArea())
Return

/*/{Protheus.doc} getQryPed
@author Tiago Tudisco
@since 29/08/2023
/*/ 
Static Function getQryPed(cAlias, aCustomFld)
Local cCustomFld:= ""
Local cQry := ""
Local aQryInfo := {}
Local aSetField := {}

If !Empty(aCustomFld)
    cCustomFld := ',' + TECustomSql(aCustomFld) 
EndIf

cQry := "SELECT "
cQry += "  EE7.EE7_PEDIDO, EE7.EE7_PEDFAT, EE7.EE7_DTPEDI, EE8.EE8_DTENTR, EE7.EE7_IMPORT, EE7.EE7_IMLOJA, SA1.A1_NREDUZ, "
cQry += "  EE7.EE7_REFIMP, EE8.EE8_SEQUEN, EE8.EE8_FABR,   EE8.EE8_FALOJA, COALESCE(FABR.A2_NREDUZ,' ') AS FAB_NREDUZ, EE8.EE8_FORN,   EE8.EE8_FOLOJA, FORN.A2_NREDUZ AS FORN_NREDUZ, "
cQry += " EE8.EE8_UNIDAD, EE8.EE8_SLDINI, EE8.EE8_PRECOI, EE7.EE7_MOEDA,  EE8.EE8_PRCINC, EE8.EE8_SLDINI - EE8.EE8_SLDATU AS QTDEEMB, "
cQry += "     EE8.EE8_SLDATU, EE8.EE8_COD_I,  EE8_DESC,       EE8.EE8_DTPREM, EE7.EE7_FRPREV, EE7.EE7_FRPCOM, EE7.EE7_SEGPRE,  EE7.EE7_DESPIN, "
cQry += "     EE7.EE7_DESCON, EE7.EE7_PRECOA, EE7.EE7_INCOTE, EE7.EE7_CONDPA, EE7.EE7_DIASPA, EE7.EE7_VIA,     EE7.EE7_ORIGEM, "
cQry += "     EE7.EE7_DEST,   EE8.EE8_CODAGE, EE8.EE8_PERCOM, EE8.EE8_VLCOM,  COALESCE(EEC.EEC_PREEMB,' ') AS EEC_PREEMB, COALESCE(EEC.EEC_NRODUE,' ') AS EEC_NRODUE,  COALESCE(EEC.EEC_DUEAVR,' ') AS EEC_DUEAVR, "
cQry += "     COALESCE(EEC.EEC_DTDUE,' ') AS EEC_DTDUE,  COALESCE(EEC.EEC_NRORUC,' ') AS EEC_NRORUC, COALESCE(EEC.EEC_DTEMBA,' ') AS EEC_DTEMBA, COALESCE(EEC.EEC_DTCONH,' ') AS EEC_DTCONH, "
cQry += "     COALESCE(EEC.EEC_NRCONH,' ') AS EEC_NRCONH, COALESCE(EEC.EEC_VIA,' ') AS EEC_VIA, COALESCE(EEC.EEC_ORIGEM,' ') AS EEC_ORIGEM, COALESCE(EEC.EEC_DEST,' ') AS EEC_DEST, "
cQry += "     COALESCE(EEC.EEC_CONDPA,' ') AS EEC_CONDPA, COALESCE(EEC.EEC_DIASPA,0) AS EEC_DIASPA, COALESCE(EEC.EEC_INCOTE,' ') AS EEC_INCOTE, COALESCE(EEC.EEC_PRECOA,' ') AS EEC_PRECOA, "
cQry += "     COALESCE(EEC.EEC_FRPREV,0) AS EEC_FRPREV, COALESCE(EEC.EEC_FRPCOM,0) AS EEC_FRPCOM, COALESCE(EEC.EEC_SEGPRE,0) AS EEC_SEGPRE, COALESCE(EEC.EEC_DESPIN,0) AS EEC_DESPIN, "
cQry += "     COALESCE(EEC.EEC_DESCON,0) AS EEC_DESCON, COALESCE(EEC.EEC_PAISDT,' ') AS EEC_PAISDT, COALESCE(EEC.EEC_PAISET,' ') AS EEC_PAISET, "
cQry += "     COALESCE(EE9.EE9_PRECOI,0) AS EE9_PRECOI, COALESCE(EE9.EE9_PRCINC,0) AS EE9_PRCINC, COALESCE(EE9.EE9_UNIDAD,' ') AS EE9_UNIDAD, "
cQry += "     COALESCE(EE9.EE9_SLDINI,0) AS EE9_SLDINI, COALESCE(EE9.EE9_CODAGE,' ') AS EE9_CODAGE, COALESCE(EE9.EE9_PERCOM,0) AS EE9_PERCOM, COALESCE(EE9.EE9_VLCOM,0) AS EE9_VLCOM, COALESCE(EE9.EE9_NF,' ') AS EE9_NF, COALESCE(EE9.EE9_SERIE,' ') AS EE9_SERIE "
cQry +=       cCustomFld
cQry += "     FROM "
cQry +=   RetSqlName("EE8") + " EE8 "
cQry +=  "     INNER JOIN " + RetSqlName("EE7") + " EE7  ON ( "
cQry += "         EE8.EE8_FILIAL = EE7.EE7_FILIAL "
cQry += "         AND EE8.EE8_PEDIDO = EE7.EE7_PEDIDO " 
cQry += "     ) "
cQry += "     INNER JOIN " + RetSqlName("SA1") + " SA1  ON ( "
cQry += "         SA1.A1_FILIAL = ? " // %xFilial:SA1%  
              aAdd( aQryInfo , {xFilial('SA1'),'C'})  
cQry += "         AND SA1.A1_COD = EE7.EE7_IMPORT "
cQry += "         AND SA1.A1_LOJA = EE7.EE7_IMLOJA "
cQry += "         AND SA1.D_E_L_E_T_ = ? " //SA1.%NotDel% 
              aAdd( aQryInfo , {'','C'})  
cQry += "     ) "
cQry += "     INNER JOIN " + RetSqlName("SA2") + " FORN  ON ( "
cQry += "         FORN.A2_FILIAL = ?   " // %xFilial:SA2%
              aAdd( aQryInfo , {xFilial('SA2'),'C'})  
cQry += "         AND FORN.A2_COD = EE8.EE8_FORN  "
cQry += "         AND FORN.A2_LOJA = EE8.EE8_FOLOJA "
cQry += "         AND FORN.D_E_L_E_T_ =  ? " //FORN.%NotDel% 
              aAdd( aQryInfo , {'','C'})  
cQry += "     ) "
cQry += "     LEFT JOIN " + RetSqlName("SA2") + " FABR  ON ( "
cQry += "         FABR.A2_FILIAL = ?   " //%xFilial:SA2%
              aAdd( aQryInfo , {xFilial('SA2'),'C'})  
cQry += "         AND FABR.A2_COD = EE8.EE8_FABR "
cQry += "         AND FABR.A2_LOJA = EE8.EE8_FALOJA  "
cQry += "         AND FABR.D_E_L_E_T_ = ? " //%NotDel% 
              aAdd( aQryInfo , {'','C'})  
cQry += "     ) "
cQry += "     LEFT JOIN " + RetSqlName("EE9") + " EE9  ON ( "
cQry += "         EE9.EE9_FILIAL = ? " //%xFilial:EE9%  
              aAdd( aQryInfo , {xFilial('EE9'),'C'})  
cQry += "         AND EE9.EE9_PEDIDO = EE8.EE8_PEDIDO "
cQry += "         AND EE9.EE9_SEQUEN = EE8.EE8_SEQUEN "
cQry += "         AND EE9.D_E_L_E_T_ = ? " //%NotDel% 
              aAdd( aQryInfo , {'','C'})  
cQry += "     ) "
cQry += "     LEFT JOIN " + RetSqlName("EEC") + " EEC  ON ( "
cQry += "         EE9.EE9_FILIAL = EEC.EEC_FILIAL  "
cQry += "         AND EE9.EE9_PREEMB = EEC.EEC_PREEMB  " 
cQry += "         AND EEC.D_E_L_E_T_ = ? " //%NotDel% 
              aAdd( aQryInfo , {'','C'})  
cQry += "     )  "
cQry += "     LEFT JOIN " + RetSqlName("EXL") + " EXL ON ( "
cQry += "         EXL.EXL_FILIAL = EEC.EEC_FILIAL " 
cQry += "         AND EXL.EXL_PREEMB = EEC.EEC_PREEMB "
cQry += "         AND EXL.D_E_L_E_T_ =? " //%NotDel%
              aAdd( aQryInfo , {'','C'})  
cQry += "     ) "
cQry += "     WHERE   EE7.EE7_FILIAL = ? AND "
              aAdd( aQryInfo , {xFilial('EE7'),'C'})  
cQry +=       getWhere(@aQryInfo)
cQry += "             EE7.D_E_L_E_T_ = ? AND "
              aAdd( aQryInfo , {'','C'})  
cQry += "             EE8.D_E_L_E_T_ = ?  "
              aAdd( aQryInfo , {'','C'})  
cQry += "     ORDER BY "
cQry += "         EE7.EE7_IMPORT, EE7.EE7_IMLOJA, EE7.EE7_PEDIDO, EE8.EE8_COD_I "

aAdd( aSetField , {'EE7_DTPEDI','D',8,0})
aAdd( aSetField , {'EE8_DTENTR','D',8,0})
aAdd( aSetField , {'EE8_DTPREM','D',8,0})
aAdd( aSetField , {'EEC_DUEAVR','D',8,0})
aAdd( aSetField , {'EEC_DTDUE','D',8,0})
aAdd( aSetField , {'EEC_DTEMBA','D',8,0})
aAdd( aSetField , {'EEC_DTCONH','D',8,0})
   
comex.generics.setFilterInQueryPreparedStatement(cAlias,cQry,aQryInfo,aSetField)

If !Empty(aCustomFld)
    TECustomCol(cALias, aCustomFld)
EndIf

Return


/*
Static FUNCTION getWhere
Parâmetro: aQryInfo - parâmetro passado por referência para complementar os parâmeros da query
Retorna: cWhere - string com os filtros para a query
Author: Maurício Frison
Data: 31/10/2024
*/
Static Function getWhere(aQryInfo)
Local cFilPedido:= ""
Local cFilData  := ""
Local cFilFabr  := ""
Local cFilExport:= ""
Local cFilImport:= ""
Local cFilPais  := ""
Local cFilNoSld := ""
Local lFilFabr  := .F.
Local cRet   := ""


//FILTRO PEDIDO
If !Empty(MV_PAR01)
    cFilPedido += " EE7.EE7_PEDIDO >= ? AND " //Pedido de ?
    aAdd( aQryInfo , {MV_PAR01,'C'})
EndIf
If !Empty(MV_PAR02)
    cFilPedido += " EE7.EE7_PEDIDO <= ? AND " //Pedido ate ?
    aAdd( aQryInfo , {MV_PAR02,'C'})
EndIf

//FILTRO DATA DE ENTREGA
If !Empty(MV_PAR03)
    cFilData += " EE8.EE8_DTENTR >= ? AND " //Data de ?
    aAdd( aQryInfo , {MV_PAR03,'D'})
EndIf
If !Empty(MV_PAR04)
    cFilData += " EE8.EE8_DTENTR <= ? AND " //Data ate ?
    aAdd( aQryInfo , {MV_PAR04,'D'})    
EndIf

//FILTRO FABRICANTE
If !Empty(MV_PAR05)
    If !Empty(MV_PAR06)
        lFilFabr := .T.
    EndIf
    cFilFabr += " ((EE8.EE8_FABR = ? " + IIF(lFilFabr, "AND EE8.EE8_FALOJA = ? ","") + ") OR (EE8.EE8_FABR = ' ' AND EE8.EE8_FORN = ? " + IIF(lFilFabr," AND EE8.EE8_FOLOJA = ? ","") + " )) AND "
    aAdd( aQryInfo , {MV_PAR05,'C'})
    IF lFilFabr
       aAdd( aQryInfo , {MV_PAR06,'C'})
    EndIf   

    aAdd( aQryInfo , {MV_PAR05,'C'})
    IF lFilFabr
       aAdd( aQryInfo , {MV_PAR06,'C'})
    EndIf   

EndIf

//FILTRO EXPORTADOR
If !Empty(MV_PAR07)
    cFilExport += " EE7.EE7_EXPORT = ? AND "
    aAdd( aQryInfo , {MV_PAR07,'C'})

    If !Empty(MV_PAR08)
        cFilExport += " EE7.EE7_EXLOJA = ? AND "
        aAdd( aQryInfo , {MV_PAR08,'C'})
    EndIf
EndIf

//FILTRO IMPORTADOR
If !Empty(MV_PAR09)    
    cFilImport += " EE7.EE7_IMPORT = ? AND "
    aAdd( aQryInfo , {MV_PAR09,'C'})
    If !Empty(MV_PAR10)
        cFilImport += " EE7.EE7_IMLOJA = ? AND "
        aAdd( aQryInfo , {MV_PAR10,'C'})
    EndIf
EndIf

//FILTRO PAÍS DO IMPORTADOR
If !Empty(MV_PAR11)
    cFilPais := " SA1.A1_PAIS = ? AND "
    aAdd( aQryInfo , {MV_PAR11,'C'})
EndIf

//FILTRO PEDIDO SEM SALDO
If MV_PAR12 == 2 //Pedido sem Saldo: 1-Sim/2-NÃ£o
    cFilNoSld := " EE8.EE8_SLDATU <> ? AND "
    aAdd( aQryInfo , {0,'N'})
EndIf

cRet := cFilPedido + cFilData + cFilFabr + cFilExport + cFilImport + cFilPais + cFilNoSld
Return cRet

