#include 'totvs.ch'

/*/{Protheus.doc} OFSoEquipmentModelCreatedInDbsMessage
	Classe que representa uma mensagem da entidade EquipmentModel mensagem CreatedInDbs
	
	@type class
	@author Rubens Takahashi
	@since 09/08/2021
/*/
Class OFSoEquipmentModelCreatedInDbsMessage

	public data cName
	public data oSoRequest
	
	data oEquipModel
	data cMessageId
	data cMsgVX5
	data cOriId

	public Method New() CONSTRUCTOR
	public Method GetMessageId()
	public Method SetEquipmentModel()
	public Method Send()
	public Method GetBody()
	public Method GetHeader()
	public Method Delay()
EndClass

/*/{Protheus.doc} New
	Construtor Simples

	@type method
	@author Rubens Takahashi
	@since 09/08/2021
/*/
Method New(oEquipModel) Class OFSoEquipmentModelCreatedInDbsMessage
	::cName := "EquipmentModelCreatedInDbs"
	::oSoRequest := OFSoRequest():New(self)

	::oSoRequest:Set("VK5_ORITAB", "VV2")
	::oSoRequest:Set("VK5_MESSAG", "16")

	if valtype(oEquipModel) != "U"
		::SetEquipmentModel(oEquipModel)
	endif
Return SELF

/*/{Protheus.doc} SetEquipmentModel
	Seta o EquipmentModel que gerará o body da mensagem

	@type method
	@author Rubens Takahashi
	@since 10/08/2021
/*/
Method SetEquipmentModel(oEquipModel as object) Class OFSoEquipmentModelCreatedInDbsMessage
	self:oEquipModel := oEquipModel
	self:oSoRequest:SetBody(self:GetBody())

	self:oSoRequest:Set("VK5_ORIKEY", self:oEquipModel:GetRefId())

Return .t.

/*/{Protheus.doc} GetMessageId
	Retorna o nome da mensagem para ser enviada ao SO onde necessário

	@type method
	@author Rubens Takahashi
	@since 09/08/2021
/*/
Method GetMessageId() Class OFSoEquipmentModelCreatedInDbsMessage
	if valtype(self:cMessageId) == "U"
		self:cMessageId := "equipment-model-" + FWUUIDV4(.t.)
	endif
return self:cMessageId

/*/{Protheus.doc} GetBody
	Retorna o body para a mensagem

	@type method
	@author Rubens Takahashi
	@since 12/08/2021
/*/
Method GetBody() Class OFSoEquipmentModelCreatedInDbsMessage
	oReqBody := OFSoRequestBody():New()
	oReqBody:SetData(self:oEquipModel:toJson())
	oReqBody:SetName(self:cName)
Return oReqBody

/*/{Protheus.doc} GetHeader
	Retorna o header da mensagem

	@type method
	@author Rubens Takahashi
	@since 12/08/2021
/*/
Method GetHeader() Class OFSoEquipmentModelCreatedInDbsMessage
Return self:oSoRequest:GetHeader()

/*/{Protheus.doc} Send
	Envia mensagem para SO

	@type method
	@author Rubens Takahashi
	@since 12/08/2021
/*/
Method Send() Class OFSoEquipmentModelCreatedInDbsMessage
	local oComm := OFSoCommunication():New()
	self:oSoRequest:SetBody(self:GetBody())
	self:oSoRequest:Save()

	jResp := oComm:SendMessage(self, self:oSoRequest)
	//if jResp["code"] == oComm:nCodigoFalhaComunicacao
	//	self:Delay() // atrasa para tentar mais tarde
	//endif
Return 

/*/{Protheus.doc} Delay
	atrasa a mensagem por falha de comunicação

	@type method
	@author Rubens Takahashi
	@since 19/10/2021
/*/
Method Delay() Class OFSoEquipmentModelCreatedInDbsMessage
	Local oMsg := OFDMSExitMessage():New()
	local oJson := JsonObject():New()
	self:oSoRequest:Save()
	oJson['header'] := self:oSoRequest:GetHeader():ToJson()
	oJson['body'] := self:oSoRequest:GetBody():ToJson()
	oMsg:Set("VK6_PAYLOA", oJson:toJson())
	oMsg:Set("VK6_MSGNAM", OFDMSMessageConstants():EquipmentModelCreatedInDbsMessageName())
	oMsg:Set("VK6_CODVK5", self:oSoRequest:GetId())
	oMsg:SetAtrasada()
Return oMsg:Save()
