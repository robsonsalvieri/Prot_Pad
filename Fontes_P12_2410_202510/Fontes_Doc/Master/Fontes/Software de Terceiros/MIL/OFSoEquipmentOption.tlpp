#include "tlpp-core.th"
#include "totvs.ch"

//static lDebug := .f.

/*/{Protheus.doc} OFSoEquipmentOption
	Classe com os dados de Option para comunicação com o Blackbird

	O retorno básico é:
	{
		"name": "EquipmentOptionAddedInDbs",
		"data": {
			"referenceId": "OPT-REF-ID-001",
			"equipmentReferenceId": "EQ-REF-ID-001",
			"code": "EQ-CODE-001",
			"type": "Accessory",
			"description": "Remote start",
			"quantity": 1,
			"salePrice": 11.11,
			"cost": 19.19,
			"estimatedCost": 12.12,
			"serialNumber": "cheerios-123",
			"incentiveAgreementNumber": "YOU-AGREED-123"
		}
	}
	Conforme link de documentação em: https://jdbbmockstordata001.blob.core.windows.net/$web/bb-api-docs/Equipment/latest/OpenApi.html#operation/EquipmentOptionAddedInDbs

	@type function
	@author Rubens Takahashi
	@since 27/07/2022
/*/
class OFSoEquipmentOption from VERegistroSql
	Public Method New()
	Public Method ToJsonApi()
	Public Method ToJson()
	Public Method GetRefId()
	Public Method Fill()
	Public Method Find()
	Public Method FindByRecnoDel()

end class

/*/{Protheus.doc} New 
	Construtor basico

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
method New() class OFSoEquipmentOption
	_Super:New('VQE')

	::AddFields({;
		"R_E_C_N_O_", ;
		"VQE_CODVQD","VQE_BBRFID" })

	::AddRelFields({;
		"VV1.VV1_BBRFID", "VQD.VQD_DESCRI", "VQD.VQD_BASCOD", "VQD.VQD_CODOPC", "VQD.VQD_DIGIMP" })

	//::HasOne('OFSoEquipment'  , { { { || xFilial('VV1') } , 'VV1_FILIAL' } , { 'VV1_CHAINT' , 'VO0_CHAINT' } } )

	::Join("VQD","VQD","VQD.VQD_FILIAL = '" + FWxFilial("VQD") + "' AND VQD.VQD_CODIGO = VQE.VQE_CODVQD AND VQD.D_E_L_E_T_ = ' '")
	::Join("VV1","VV1","VV1.VV1_FILIAL = '" + FWxFilial("VV1") + "' AND VV1.VV1_CHAINT = VQE.VQE_CHAINT AND VV1.D_E_L_E_T_ = ' '")

	//if lDebug
	//	Conout(" ")
	//	Conout( "OFSoEquipmentOption:NEW " + Str(Procline(1),5) + " - " + ProcName(1) )
	//	Conout(" ")
	//endif

return self

/*/{Protheus.doc} Find
	Busca o contato via reference id

	@type method
	@author Rubens Takahashi
	@since 02/07/2021
/*/
Method Find(cRefId) Class OFSoEquipmentOption
	default nOrder := 1
	dbSelectArea("VQE")
	dbSetOrder(nOrder)
	msSeek(cRefId)
Return self:FindByRecno( VE1->(recno()) )

Method FindByRecnoDel(nRecno) Class OFSoEquipmentOption
	self:Select();
		:_From(self:cAlias);
		:AndEq('R_E_C_N_O_', nRecno)

	// busca primeiro registro
	oReg := self:First()

	// limpa a query interna
	self:Clear()
Return oReg

/*/{Protheus.doc} GetRefId
	Retorna o reference id para usar no SO

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
Method GetRefId() Class OFSoEquipmentOption
Return self:Get("VQE_BBRFID")

/*/{Protheus.doc} ToJson
	retorna um json object

	@type method
	@author Rubens Takahashi
	@since 02/07/2021
/*/
Method ToJson() Class OFSoEquipmentOption
Return self:ToJsonApi()

/*/{Protheus.doc} ToJsonApi
	Retorna um JSON para enviar a API

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
Method ToJsonApi() Class OFSoEquipmentOption
	local jJson := JsonObject():New()
	local cCodOpc := ""

	If self:Get("VQD.VQD_DIGIMP") == "0"
		cCodOpc := self:Get("VQE_CODVQD")
	Else
		cCodOpc := self:Get("VQD.VQD_BASCOD")+self:Get("VQD.VQD_CODOPC")
	EndIf

	jJson["code"]                     := FGX_SetVlJson( cCodOpc, .f.)
	jJson["cost"]                     := NIL
	jJson["description"]              := FGX_SetVlJson( self:Get("VQD.VQD_DESCRI"), .f.)
	jJson["equipmentReferenceId"]     := FGX_SetVlJson( iif( ! empty(self:Get("VV1.VV1_BBRFID")) , "EQ-" + self:Get("VV1.VV1_BBRFID") , "" ) , .f. )
	jJson["estimatedCost"]            := NIL
	jJson["incentiveAgreementNumber"] := NIL
	jJson["quantity"]                 := 1
	jJson["referenceId"]              := FGX_SetVlJson( "EO-" + self:Get("VQE_BBRFID"), .f.)
	jJson["salePrice"]                := NIL
	jJson["serialNumber"]             := NIL
	jJson["type"]                     := "Options"

Return jJson

