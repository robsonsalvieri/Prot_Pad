#include "tlpp-core.th"
#include "totvs.ch"
#INCLUDE "FWMVCDEF.CH"

//static lDebug := .f.

/*/{Protheus.doc} OFSoEquipmentModel
	Classe com os dados de Marca (Make) para comunicação com o Blackbird

	O retorno básico é:
	{
		"name": "EquipmentMakeCreatedInDbs",
		"data": {
			"referenceId": "MAKE-REF-ID-001",
			"id": "MAKE-ID-001",
			"name": "John Deere",
			"description": "John Deere Equipment",
			"category": "Tractor"
		}
	}
	Conforme link de documentação em: https://jdbbmockstordata001.blob.core.windows.net/$web/bb-api-docs/Equipment/latest/OpenApi.html#operation/EquipmentMakeCreatedInDbs

	@type function
	@author Rubens Takahashi
	@since 10/01/2022
/*/
class OFSoEquipmentModel from VERegistroSql
	Public Method New()
	Public Method ToJsonApi()
	Public Method ToJson()
	Public Method GetRefId()
	Public Method Fill()
	Public Method FindByRecnoDel()
	Private Method checkForeignKey()
end class

/*/{Protheus.doc} New 
	Construtor basico

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
method New() class OFSoEquipmentModel
	_Super:New('VV2')

	::AddFields({"R_E_C_N_O_", "VV2_BBRFID","VV2_MODVEI", "VV2_DESMOD" })

	::AddRelFields({ "VE1.VE1_BBRFID", "VE1.VE1_BBINTG", "VE1.VE1_BBDEL", "VE1.VE1_BBSYNC", "VE1.R_E_C_N_O_ as VE1_RECNO" })

	::Join("VE1","VE1","VE1.VE1_FILIAL = '" + xFilial('VE1') + "' AND VE1.VE1_CODMAR = VV2_CODMAR AND VE1.D_E_L_E_T_ = ' '")

	::SetPKField("VV2_BBRFID")

	//if lDebug
	//	Conout(" ")
	//	Conout( "OFSoEquipmentModel:NEW " + Str(Procline(1),5) + " - " + ProcName(1) )
	//	Conout(" ")
	//endif

return self

/*/{Protheus.doc} FindByRecnoDel
	Busca pelo recno

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
Method FindByRecnoDel(nRecno) Class OFSoEquipmentModel
	self:Select();
		:_From(self:cAlias);
		:AndEq('R_E_C_N_O_', nRecno)

	// busca primeiro registro
	oReg := self:First()

	// limpa a query interna
	self:Clear()
Return oReg

/*/{Protheus.doc} GetRefId
	Retorna o reference id para usar no SO

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
Method GetRefId() Class OFSoEquipmentModel
Return self:Get("VV2_BBRFID")

/*/{Protheus.doc} ToJson
	retorna um json object

	@type method
	@author Rubens Takahashi
	@since 02/07/2021
/*/
Method ToJson() Class OFSoEquipmentModel
Return self:ToJsonApi()

/*/{Protheus.doc} ToJsonApi
	Retorna um JSON para enviar a API

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
Method ToJsonApi() Class OFSoEquipmentModel
	local jJson := JsonObject():New()

	self:checkForeignKey(self:Get("VE1_RECNO"))

	jJson["category"]        := NIL
	jJson["description"]     := FGX_SetVlJson( self:Get("VV2_DESMOD"), .f. )
	jJson["makeReferenceId"] := FGX_SetVlJson( "EK-" + self:Get("VE1.VE1_BBRFID"), .f. )
	jJson["name"]            := FGX_SetVlJson( self:Get("VV2_MODVEI"), .f. )
	jJson["referenceId"]     := FGX_SetVlJson( "EM-" + self:Get("VV2_BBRFID"), .f.)

	//if lDebug
	//	
	//	Conout( "-----------------------------------------------------------------------")
	//	// Conout( "OFSoEquipmentModel toJsonApi " + Str(Procline(1),5) + " - " + ProcName(1))
	//
	//	nCont := 2
	//	While !Empty(ProcName(nCont))
	//		// Conout( "                       " + Str(Procline(nCont),5) + " - " + ProcName(nCont) )
	//		nCont++
	//	EndDo
	//	Conout( "-----------------------------------------------------------------------")
	//	Conout(" ")
	//
	//endif

Return jJson

Method checkForeignKey(nRecNo) Class OFSoEquipmentModel

	Local oModel

	DbSelectArea("VE1")
	DbGoto(nRecNo)

	if VE1->VE1_BBDEL == "1"

		oModel := FWLoadModel("VEIA031")
		oModel:setOperation(MODEL_OPERATION_UPDATE)

		if oModel:Activate()

			oModel:SetValue("MODEL_VE1", "VE1_BBINTG" , "0" ) // Marca como nao integrado para enviar a JD
			oModel:SetValue("MODEL_VE1", "VE1_BBSYNC" , "0" ) // Marca como nao sincronizado para enviar a JD
			oModel:SetValue("MODEL_VE1", "VE1_BBDEL"  , "0" ) // Desmarca como deletado

			If oModel:VldData()
				oModel:CommitData()
			endif

			oModel:DeActivate()

		EndIf

	endif

	if VE1->VE1_BBINTG == "0"
		VA0310013_TransmitirBlackBird()
	endif

Return
