#include 'totvs.ch'
#Include "FWMVCDEF.CH"
#include "OFSOEQUIPMENTMODELUPDATEDINFIELDSERVICEINTERACTOR.ch"

/*/{Protheus.doc} OFSoEquipmentModelUpdatedInFieldServiceInteractor
	Classe responsavel por trabalhar alteração de modelo em campo
	
	@type class
	@author Vinicius Gati
	@since 07/10/2022
/*/
Class OFSoEquipmentModelUpdatedInFieldServiceInteractor
	Public Method New() CONSTRUCTOR
	Public Method Process()
EndClass

/*/{Protheus.doc} New
	Construtor Simples

	@type method
	@author Vinicius Gati
	@since 07/10/2022
/*/
Method New() Class OFSoEquipmentModelUpdatedInFieldServiceInteractor
Return SELF

/*/{Protheus.doc} Process
	processa a criacao

	@type method
	@author Vinicius Gati
	@since 29/09/2022
/*/
Method Process(jSoRequestBody, jGatewayHeader) Class OFSoEquipmentModelUpdatedInFieldServiceInteractor
	local jData := jSoRequestBody:GetData()
	local oModel := FwLoadModel("VEIA030")
	local oConfig   := OFSoConfig():New()
	local oCfgAtu   := oConfig:GetConfig()

	if empty(jData["makeReferenceId"])
		return OFSoRestErrorResponse():BadRequest("EquipmentModelUpdatedInFieldService", 0, STR0001, "")	// "Dados inválidos detectados, verifique o campo makeReferenceId"
	endif
	oMarca := OFSoEquipmentMake():New()
	oMarca := oMarca:Find(OFXFA0124_WithoutPrefix("EK-", jData["makeReferenceId"]))

	if ! oMarca:Persistido()
		return OFSoRestErrorResponse():BadRequest("EquipmentModelUpdatedInFieldService", 0, STR0002, "")	// "makeReferenceId inválido"
	endif

	oMod := OFSoEquipmentModel():New()
	oMod := oMod:Find(OFXFA0124_WithoutPrefix("EM-", jData["referenceId"]))

	if ! oMod:Persistido()
		return OFSoRestErrorResponse():BadRequest("EquipmentModelUpdatedInFieldService", 0, STR0003, "")	// "ReferenceId inválido"
	endif

	DbSelectArea("VV2")
	DBGoTo(oMod:nRecno)
	if VV2->VV2_CODMAR != oMarca:Get("VE1_CODMAR")
		return OFSoRestErrorResponse():BadRequest("EquipmentModelUpdatedInFieldService", 0, STR0004, "")	// "Não é possível alterar a marca"
	endif

	oModel:SetOperation( MODEL_OPERATION_UPDATE )
	oModel:Activate()
	oModel:SetValue("MODEL_VV2", "VV2_DESMOD", left(FGX_GetFromSoJson(jData["description"]), GetSX3Cache('VV2_DESMOD', "X3_TAMANHO")))
	oModel:SetValue("MODEL_VV2", "VV2_CODMAR", oMarca:Get("VE1_CODMAR"))
	oModel:SetValue("MODEL_VV2", "VV2_CATVEI", oCfgAtu["VV2_CATVEI"])
	oModel:SetValue("MODEL_VV2", "VV2_TIPVEI", oCfgAtu["VV2_TIPVEI"])
	oModel:SetValue("MODEL_VV2", "VV2_ESPVEI", oCfgAtu["VV2_ESPVEI"])
	oModel:SetValue("MODEL_VV2", "VV2_BBSYNC", "1")
	oModel:SetValue("MODEL_VV2", "VV2_BBINTG", "1")
	If oModel:VldData() .and. oModel:CommitData()
		oModel:DeActivate()
		oEquip := OFSoEquipmentModel():New()
		oEquip := oEquip:Find(OFXFA0124_WithoutPrefix("EM-", jData["referenceId"]))
		return OFSoResponseBody():New(200, jSoRequestBody, oEquip:ToJson())
	endif

	aErro := oModel:GetErrorMessage()
	oErro := JsonObject():New()
	oErro["code"] := cValToChar(aErro[05])
	oErro["message"] := 'Field ' + cValToChar(aErro[02]) + ' ' + cValToChar(aErro[06])
	oErro["details"] := cValToChar(aErro[07])
	oErro["trace"] := ""
	oRet := JsonObject():New()
	oRet["errors"] := oErro
	oRet["name"] := "EquipmentUpdatedInFieldService"
	oModel:DeActivate()
Return OFSoResponseBody():New(400, jSoRequestBody, oRet)
