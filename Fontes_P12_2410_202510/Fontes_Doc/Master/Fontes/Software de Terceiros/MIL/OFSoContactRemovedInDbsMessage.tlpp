#include 'totvs.ch'

/*/{Protheus.doc} OFSoContactRemovedInDbsMessage
	Classe que representa uma mensagem de atualizacao de Contact no Protheus para o SO
	
	@type class
	@author Rubens Takahashi
	@since 17/09/2021
/*/
Class OFSoContactRemovedInDbsMessage from OFSoContactCreatedInDbsMessage
	Public Method New() Constructor
	Public Method GetMessageId()
	Public Method GetBody()
	Public Method Delay()
EndClass


/*/{Protheus.doc} New
	Construtor Simpled
	
	@type method
	@author Rubens Takahashi
	@since 17/09/2021dddd
/*/
Method New(oContact) Class OFSoContactRemovedInDbsMessage
	_Super:New(oContact)
	::cName := "ContactRemovedInDbs"

	::oSoRequest:Set("VK5_MESSAG", "12")

Return SELF

/*/{Protheus.doc} GetMessageId
	Retorna o nome da mensagem para ser enviada ao SO onde necessário

	@type method
	@author Rubens Takahashi
	@since 17/09/2021
/*/
Method GetMessageId() Class OFSoContactRemovedInDbsMessage
	if valtype(self:cMessageId) == "U"
		self:cMessageId := "contacts-" + FWUUIDV4(.t.)
	endif
return self:cMessageId


/*/{Protheus.doc} GetBody
	Retorna o nome da mensagem para ser enviada ao SO onde necessário

	@type method
	@author Rubens Takahashi
	@since 26/08/2021
/*/
Method GetBody() Class OFSoContactRemovedInDbsMessage
	Local oJson := self:oSoContact:toJson()
	Local oRemoveBody := JsonObject():New()
	Local oReqBody := OFSoRequestBody():New()
	oRemoveBody["referenceId"] := oJson["referenceId"]
	oReqBody:SetData(oRemoveBody)
	oReqBody:SetName(self:cName)
Return oReqBody

/*/{Protheus.doc} Delay
	atrasa a mensagem por falha de comunicação

	@type method
	@author Vinicius Gati
	@since 19/10/2021
/*/
Method Delay() Class OFSoContactRemovedInDbsMessage
	Local oMsg := OFDMSExitMessage():New()
	local oJson := JsonObject():New()
	oJson['header'] := self:oSoRequest:GetHeader():toJson()
	oJson['body'] := self:oSoRequest:GetBody():toJson()
	oMsg:Set("VK6_PAYLOA", oJson:toJson())
	oMsg:Set("VK6_MSGNAM", OFDMSMessageConstants():ContactRemovedInDbsMessageName())
	oMsg:Set("VK6_CODVK5", self:oSoRequest:GetId())
	oMsg:SetAtrasada()
Return oMsg:Save()
