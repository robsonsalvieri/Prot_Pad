#include 'totvs.ch'

/*/{Protheus.doc} OFSoContactUpdatedInFieldServiceInteractor
	Classe que vai ser responsável por receber dados do OS e integrar ao protheus
	
	@type class
	@author Vinicius Gati
	@since 06/09/2022
/*/
Class OFSoContactUpdatedInFieldServiceInteractor
	Public Method New() CONSTRUCTOR
	Public Method Process()
EndClass

/*/{Protheus.doc} New
	Construtor Simples

	@type method
	@author Vinicius Gati
	@since 06/09/2022
/*/
Method New() Class OFSoContactUpdatedInFieldServiceInteractor
Return SELF

/*/{Protheus.doc} Process

	@type method
	@author Vinicius Gati
	@since 21/10/2021
/*/
Method Process(jSoRequestBody, jGatewayHeader) Class OFSoContactUpdatedInFieldServiceInteractor
	Local oContact := OFSoContact():New()
	local jData := jSoRequestBody:GetData()
	Local oSU5Contact
	local uuid := strtran(jData["referenceId"], "CT-", "")
	oContact := oContact:Find(uuid)
	if oContact:Persistido()
		oSU5Contact := OFContact():New()
		oSU5Contact := oSU5Contact;
			:AndEq("U5_FILIAL", oContact:Get("VK8_U5FIL"));
			:AndEq("U5_CODCONT", oContact:Get("VK8_U5COD"));
			:First()
		if oSU5Contact:Persistido()
			if oSU5Contact:UpdateForSO(jData)
				oContact := oContact:Find(uuid)
				return OFSoResponseBody():New(200, jSoRequestBody, oContact)
			else
				return OFSoInvalidBodyEntityResponse():GetResponse("ContactUpdatedInFieldService", oSU5Contact)
			endif
		else
			return OFSoRestErrorResponse():BadRequest("ContactUpdatedInFieldService", 0, "Contact deleted from SU5 for reference: " + jData["referenceId"] + " not found", "")
		endif
	endif
Return OFDMSDefaultRestResponse():NotFound()

