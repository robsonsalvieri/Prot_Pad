#include "tlpp-core.th"
#include "totvs.ch"

//static lDebug := .f.

/*/{Protheus.doc} OFSoEquipmentMake
	Classe com os dados de Marca (Make) para comunicação com o Blackbird

	O retorno básico é:
{
		"name": "EquipmentMakeCreatedInDbs",
		"data": {
			"referenceId": "MAKE-REF-ID-001",
			"id": "MAKE-ID-001",
			"name": "John Deere",
			"description": "John Deere Equipment",
			"category": "Tractor"
		}
	}
	Conforme link de documentação em: https://jdbbmockstordata001.blob.core.windows.net/$web/bb-api-docs/Equipment/latest/OpenApi.html#operation/EquipmentMakeCreatedInDbs

	@type function
	@author Rubens Takahashi
	@since 10/01/2022
/*/
class OFSoEquipmentMake from VERegistroSql
	Public Method New()
	Public Method ToJsonApi()
	Public Method ToJson()
	Public Method GetRefId()
	Public Method Fill()
	Public Method FindByRecnoDel()
end class

/*/{Protheus.doc} New
	Construtor basico

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
method New() class OFSoEquipmentMake
	_Super:New('VE1')

	::AddFields({"R_E_C_N_O_","VE1_BBRFID","VE1_CODMAR", "VE1_DESMAR" })
	::SetPKField("VE1_BBRFID")

	//if lDebug
	//	Conout(" ")
	//	Conout( "OFSoEquipmentMake:NEW " + Str(Procline(1),5) + " - " + ProcName(1) )
	//	Conout(" ")
	//endif

return self

Method FindByRecnoDel(nRecno) Class OFSoEquipmentMake
	self:Select();
		:_From(self:cAlias);
		:AndEq('R_E_C_N_O_', nRecno)

	// busca primeiro registro
	oReg := self:First()

	// limpa a query interna
	self:Clear()
Return oReg

/*/{Protheus.doc} GetRefId
	Retorna o reference id para usar no SO

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
Method GetRefId() Class OFSoEquipmentMake
Return self:Get("VE1_BBRFID")

/*/{Protheus.doc} ToJson
	retorna um json object

	@type method
	@author Rubens Takahashi
	@since 02/07/2021
/*/
Method ToJson() Class OFSoEquipmentMake
Return self:ToJsonApi()

/*/{Protheus.doc} ToJsonApi
	Retorna um JSON para enviar a API

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
Method ToJsonApi() Class OFSoEquipmentMake
	local jJson := JsonObject():New()

	jJson["category"]    := NIL
	jJson["description"] := NIL
	jJson["id"]          := FGX_SetVlJson( self:Get("VE1_CODMAR"), .f. )
	jJson["name"]        := FGX_SetVlJson( self:Get("VE1_DESMAR"), .f. )
	jJson["referenceId"] := FGX_SetVlJson( "EK-" + self:Get("VE1_BBRFID"), .f.)

	//if lDebug
	//	
	//	Conout( "-----------------------------------------------------------------------")
	//	Conout( "OFSoEquipmentMake toJsonApi " + Str(Procline(1),5) + " - " + ProcName(1))
	//
	//	nCont := 2
	//	While !Empty(ProcName(nCont))
	//		Conout( "                       " + Str(Procline(nCont),5) + " - " + ProcName(nCont) )
	//		nCont++
	//	EndDo
	//	Conout( "-----------------------------------------------------------------------")
	//	Conout(" ")
	//
	//endif

Return jJson
