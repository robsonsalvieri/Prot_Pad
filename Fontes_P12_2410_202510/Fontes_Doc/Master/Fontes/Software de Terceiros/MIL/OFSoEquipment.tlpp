#include "tlpp-core.th"
#include "totvs.ch"
#INCLUDE "FWMVCDEF.CH"

//static lDebug := .f.
static oSqlHlp := DMS_SqlHelper():New()
// static oConfig := OFSoConfig():New()

/*/{Protheus.doc} OFSoEquipment
	Classe com os dados do Equipamento para comunicação com o Blackbird

	O retorno básico é:
	{
		"name": "EquipmentCreatedInDbs",
		"data": {
			"referenceId": "EQ-REF-ID-001",
			"attachedToReferenceId": "EQ-REF-ID-002",
			"builtOn": "2019-09-12T00:00:00Z",
			"cabNumber": "CAB-123",
			"category": "Tractor",
			"customerReferenceId": "CUST-REF-01",
			"deliveredMeterReading": {
				"description": "Odometer",
				"value": 101253.23,
				"units": "Miles",
				"lastUpdatedOn": "2019-09-12T12:01:01Z",
				"lastUpdatedBy": "JDLink"
			},
			"deliveredOn": "2020-01-01T00:00:00Z",
			"engineCode": "ENG-CODE-01",
			"engineSerialNumber": "123459-21566-1156",
			"equipmentId": "EQ-ID-001",
			"equipmentSubType": "TBD",
			"equipmentType": "CustomerEquipment",
			"fixedAssetNumber": "FA-001",
			"fleetNumber": "FLEET-0001",
			"franchiseReferenceId": "FR-REF-ID-001",
			"inventoryStatus": "Sold",
			"isAttachment": true,
			"jdLinkMachineId": "JD-LINK-ID-001",
			"lastMeterReading": {
				"description": "Odometer",
				"value": 101253.23,
				"units": "Miles",
				"lastUpdatedOn": "2019-09-12T13:01:01Z",
				"lastUpdatedBy": "JDLink"
			},
			"licenseDate": "2019-09-12T00:00:00Z",
			"licenseExpiresOn": "2020-09-12T00:00:00Z",
			"licenseNumber": "LIC-123",
			"locationReferenceId": "LOC-REF-ID-001",
			"meters": [
				{
				"referenceId": "METER-READING-001",
				"description": "Hour Meter",
				"value": 101253.23,
				"units": "Hours",
				"lastUpdatedOn": "2019-09-12T14:00:01Z",
				"lastUpdatedBy": "JDLink"
				},
				{
				"referenceId": "METER-READING-002",
				"description": "Watts",
				"value": 101253.23,
				"units": "Watts",
				"lastUpdatedOn": "2019-09-12T15:00:01Z",
				"lastUpdatedBy": "JDLink"
				}
			],
			"makeReferenceId": "EQ-MAK-001",
			"modelReferenceId": "EQ-MOD-001",
			"modelName": "Z225",
			"notes": "Lorem ipsum dolor sit amet, consectetur adipiscing elit, sed do eiusmod tempor incididunt ut labore et dolore magna aliqua.",
			"dealerNotes": "Lorem ipsum dolor sit amet, consectetur adipiscing elit, sed do eiusmod tempor incididunt ut labore et dolore magna aliqua.",
			"additionalNotes": [
				{
				"referenceId": "EQUIPMENTNOTES-REF-ID-001",
				"noteType": "General",
				"notes": "Lorem ipsum dolor sit amet, consectetur adipiscing Lorem ipsum dolor sit amet, consectetur adipiscing elit",
				"createdByUserId": "John Doe",
				"createdOn": "20201-05-04T16:15:14Z",
				"modifiedByUserId": "Jane Doe",
				"modifiedOn": "20201-05-04T16:15:14Z"
				},
				{
				"referenceId": "EQUIPMENTNOTES-REF-ID-002",
				"noteType": "General",
				"notes": "Lorem ipsum dolor sit amet, consectetur adipiscing Lorem ipsum dolor sit amet, consectetur adipiscing elit",
				"createdByUserId": "John Doe",
				"createdOn": "20201-05-04T16:15:14Z",
				"modifiedByUserId": "Jane Doe",
				"modifiedOn": "20201-05-04T16:15:14Z"
				}
			],
			"onYard": false,
			"options": [
				{
				"referenceId": "EQ-OPT-001",
				"code": "EQC-001",
				"type": "Accessory",
				"description": "quis nostrud exercitation",
				"quantity": 2,
				"salePrice": 1.11,
				"cost": 1,
				"esimatedCost": 0.5,
				"serialNumber": "Lucky Charms",
				"incentiveAgreementNumber": "INC-001"
				},
				{
				"referenceId": "EQ-OPT-002",
				"code": "EQC-002",
				"type": "Options",
				"description": "quis nostrud exercitation",
				"quantity": 1,
				"salePrice": 1.11,
				"cost": 1,
				"esimatedCost": 0.5,
				"serialNumber": "Lucky Charms",
				"incentiveAgreementNumber": "INC-001"
				}
			],
			"orderNumber": "ORD-1234",
			"rentedToReferenceId": "CUST-REF-001",
			"serialNumber": "12324-01351-135165",
			"soldByLocationReferenceId": "LOC-REF-ID-001",
			"soldBySalesperson": "Chris June",
			"stockNumber": "STK-001",
			"stockType": "New",
			"stockUnitStatus": "Pending",
			"subCategory": "Farm Stuff",
			"taxCategory": "Taxable",
			"terminalSerialNumber": "T12352",
			"transmissionSerialNumber": "TRAN-12338877718",
			"warrantyType": "Purchased",
			"yearManufactured": "2003"
		}
	}

	Conforme link de documentação em: https://jdbbmockstordata001.blob.core.windows.net/$web/bb-api-docs/Equipment/latest/OpenApi.html#operation/EquipmentCreatedInDbs

	@type function
	@author Rubens Takahashi
	@since 10/01/2022
/*/
class OFSoEquipment from VERegistroSql
	Public Method New()
	Public Method Find()
	Public Method ToJsonApi()
	Public Method ToJson()
	Public Method GetRefId()
	Public Method Fill()
	Public Method FindByRecnoDel()

	Public Method FetchOldMemos()
	Public Method FetchMemoFields()
	Private Method checkForeignKey()
	Private Method enforceCustomerIsCreated()
	Private Method GetDadosDoPedido()

	Static Method GetStockTypeForProtheus()
	Static Method GetStockUnitStatusForProtheus()
	Static Method GetEquipmentTypeForProtheus()
	Static Method GetInventoryStatusForProtheus()
end class

/*/{Protheus.doc} New
	Construtor basico

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
method New() class OFSoEquipment
	_Super:New('VV1')

	::AddFields({;
		"R_E_C_N_O_", "VV1_FILIAL", "VV1_CAMBIO", "VV1_SITVEI" , "VV1_CHAINT", "VV1_CHASSI",;
		"VV1_CODFRO", "VV1_FABMOD", "VV1_NUMMOT", "VV1_BBRFID", "VV1_SERMOT","VV1_BBEQTY",;
		"VV1_BBSTTY", "VV1_BBYARD", "VV1_BBINST", "VV1_BBSTST", "VV1_LOCRID","VV1_CODMAR",;
		"VV1_SOLCID", "VV1_FILENT", "VV1_FILSAI", "VV1_PEDFAB", "VV1_CGCODE","VV1_NUMCMO",;
		"VV1_ESTVEI", "VV1_DATPED", "VV1_DATETG", "VV1_DTDPCP", "VV1_DTEFAB","VV1_VEND",;
		"VV1_ESTVEI", "VV1_DATPED", "VV1_DATETG", "VV1_DTDPCP", "VV1_DTEFAB","VV1_VEND",;
		"VV1_PROATU", "VV1_LJPATU",;
		"VV1_NUMTRA", "VV1_MODVEI", "VV1_SEGMOD", "VV1_CORVEI", "VV1_DTUVEN","VV1_DTHRES","VV1_DTHVAL" })

	::AddFieldsMemo({"VV1_OBSMEM", "VV1_OBSVEI", "VV1_OBSERV"})

	::AddFields({"VE1.VE1_BBRFID", "VE1.VE1_BBINTG", "VE1.VE1_BBDEL", "VE1.VE1_BBSYNC", "VE1.R_E_C_N_O_ AS VE1_RECNO" })
	::AddFields({"VV2.VV2_BBRFID", "VV2.VV2_BBINTG", "VV2.VV2_BBDEL", "VV2.VV2_BBSYNC", "VV2.R_E_C_N_O_ AS VV2_RECNO", "VV2.VV2_DESMOD" })
	//::AddFields({"SB1.B1_COD", "SB1.B1_CODITE" })

	::AddRelFields({"SA3.A3_NOME"})
	::AddRelFields({"VV0.VV0_NUMNFI", "VV0.VV0_SERNFI"})
	::AddRelFields({"VVA.VVA_VALVDA"})

	//::AddFields({;
	//	"VK7.VK7_REFID" })

	::HasOne('OFSoEquipmentMake'  , { { { || xFilial('VE1') } , 'VE1_FILIAL' } , { 'VE1_CODMAR' , 'VV1_CODMAR' } } )
	::HasOne('OFSoEquipmentModel' , { { { || xFilial('VV2') } , 'VV2_FILIAL' } , { 'VV2_CODMAR' , 'VV1_CODMAR' } , { 'VV2_MODVEI' , 'VV1_MODVEI' } , { 'VV2_SEGMOD' , 'VV1_SEGMOD' } } )
	//::HasOne('OFSoProduct'  , { { { || xFilial('SB1') } , 'B1_FILIAL' } , { 'B1_CODITE' , 'VV1_CHAINT' } } )
	//::HasOne('OFSoCustomer'       , { { { || xFilial('VK7') } , 'VV7_FILIAL' } , { 'VK7_A1FIL' , xFilial('VV1') } , { 'VK7_A1COD' , 'VV1_PROATU' } , { 'VK7_A1LOJA' , 'VV1_LJPATU' } } )

	//::LeftJoin("SA3", "SA3", "A3_FILIAL = '" + xFilial("SA3") + "' AND A3_COD = VV1_VEND AND SA3.D_E_L_E_T_ = ' '")
	::LeftJoin("VV0", "VV0", "VV0_FILIAL = VV1_FILSAI AND VV0_NUMTRA = VV1_NUMTRA AND VV0.D_E_L_E_T_ = ' '")
	::LeftJoin("VVA", "VVA", "VVA_FILIAL = VV1_FILSAI AND VVA_NUMTRA = VV1_NUMTRA AND VVA.D_E_L_E_T_ = ' '")
	::LeftJoin("SA3", "SA3", "A3_FILIAL = '" + xFilial("SA3") + "' AND A3_COD = VV0_CODVEN AND SA3.D_E_L_E_T_ = ' '")

	//if lDebug
	//	Conout(" ")
	//	Conout( "OFSoEquipment:NEW " + Str(Procline(1),5) + " - " + ProcName(1) )
	//	Conout(" ")
	//endif

return self

/*/{Protheus.doc} Find
	Busca o contato via reference id

	@type method
	@author Rubens Takahashi
	@since 02/07/2021
/*/
Method Find(cRefId) Class OFSoEquipment
	default nOrder := 1
	dbSelectArea("VV1")
	dbSetOrder(nOrder)
	msSeek(cRefId)
Return self:FindByRecno( VV1->(recno()) )

Method FindByRecnoDel(nRecno) Class OFSoEquipment
	self:Select();
		:_From(self:cAlias);
		:AndEq('R_E_C_N_O_', nRecno)

	// busca primeiro registro
	oReg := self:First()

	// limpa a query interna
	self:Clear()
Return oReg

/*/{Protheus.doc} GetRefId
	Retorna o reference id para usar no SO

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
Method GetRefId() Class OFSoEquipment
Return self:Get("VV1_BBRFID")

/*/{Protheus.doc} ToJson
	retorna um json object

	@type method
	@author Rubens Takahashi
	@since 02/07/2021
/*/
Method ToJson() Class OFSoEquipment
Return self:ToJsonApi()

/*/{Protheus.doc} ToJsonApi
	Retorna um JSON para enviar a API

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
Method ToJsonApi() Class OFSoEquipment
	local jJson := JsonObject():New()
	local jOption
	local cCodOpc := ""
	Local oDados
	local jMeter
	local xAux
	Local cVE1RefId
	Local cVV2RefId
	
	FGX_VV1SB1("CHAINT", self:Get("VV1_CHAINT"))

	VK7->(dbSetOrder(2))

	// The reference id from the customer stored at VK7_REFID.
	// 
	// Can be two ways to send:
	// If this record is equipmentType = "CustomerEquipment": the VK7 positioning will use the VV1_PROATU + VV1_LJPATU.
	// If this record is equipmentType <> "CustomerEquipment": blank.
	// jJson["customerReferenceId"]       := FGX_SetVlJson( self:Get("VK7.VK7_REFID"), .t. ) // The owning customer for the equipment.

	If ! empty( self:Get("VV1_PROATU") )
		self:enforceCustomerIsCreated(self:Get("VV1_PROATU"), self:Get("VV1_LJPATU"))
	EndIf

	jJson["additionalNotes"]       := nil
	jJson["attachedToReferenceId"] := nil
	jJson["builtOn"]               := nil
	jJson["category"]              := FGX_SetVlJson( self:Get("VV1_CGCODE"), .f. )

	if self:Get("VV1_BBEQTY") == "1" .and. ! empty( self:Get("VV1_PROATU") ) .and. VK7->(dbSeek(xFilial("VK7") + xFilial('SA1') + self:Get("VV1_PROATU") + self:Get("VV1_LJPATU") ))
		jJson["customerReferenceId"] := FGX_SetVlJson( iif( ! empty(VK7->VK7_REFID) , "CS-" + VK7->VK7_REFID , "" ) , .f. ) // The owning customer for the equipment.
	else
		jJson["customerReferenceId"] := nil
	endif

	jJson["dealerNotes"]               := FGX_SetVlJson( self:Get("VV1_OBSVEI"), .f. )
	jJson["deliveredMeterReading"]     := nil

	// The time zone independent delivered on date for the equipment.
	// We have to check this information on Protheus to see if is attached to the warranty process or not.
	jJson["deliveredOn"]               := nil

	jJson["engineCode"]                := FGX_SetVlJson( self:Get("VV1_NUMMOT"), .f. )
	jJson["engineSerialNumber"]        := FGX_SetVlJson( self:Get("VV1_SERMOT"), .f. )
	jJson["equipmentId"]               := FGX_SetVlJson( self:Get("VV1_CHAINT"), .f. )
	jJson["equipmentSubType"]          := nil

	// Enum: "CustomerEquipment" "StockUnit" "FixedAsset"
	// The type for the equipment.
	// We will have to create a logic to separate and determine the equipment type in Protheus.

	do case
	case self:Get("VV1_BBEQTY") == "1"
		jJson["equipmentType"] := FGX_SetVlJson( "CustomerEquipment", .f. )
	case self:Get("VV1_BBEQTY") == "2"
		jJson["equipmentType"] := FGX_SetVlJson( "StockUnit", .f. )
	case self:Get("VV1_BBEQTY") == "3"
		jJson["equipmentType"] := FGX_SetVlJson( "FixedAsset", .f. )
	otherwise
		jJson["equipmentType"] := ""
	endcase

	jJson["fixedAssetNumber"]      := nil
	jJson["fleetNumber"]           := FGX_SetVlJson( self:Get("VV1_CODFRO"), .f. )
	jJson["franchiseReferenceId"]  := nil

	do case
	case self:Get("VV1_BBINST") == "1";	jJson["inventoryStatus"]:= "Sold"
	case self:Get("VV1_BBINST") == "2";	jJson["inventoryStatus"]:= "Stock"
	case self:Get("VV1_BBINST") == "3";	jJson["inventoryStatus"]:= "Loaner"
	case self:Get("VV1_BBINST") == "4";	jJson["inventoryStatus"]:= "OnHold"
	case self:Get("VV1_BBINST") == "5";	jJson["inventoryStatus"]:= "Rental"
	case self:Get("VV1_BBINST") == "6";	jJson["inventoryStatus"]:= "Demo"
	case self:Get("VV1_BBINST") == "7";	jJson["inventoryStatus"]:= "Shop"
	case self:Get("VV1_BBINST") == "8";	jJson["inventoryStatus"]:= "Asset"
	otherwise;	jJson["inventoryStatus"]:= nil
	endcase

	jJson["userDates"] := JsonObject():new()
	jJson["userText"]  := JsonObject():new()

	// Quando reservado, procura o ultimo registro de reserva para posicionar no atendimento correto 
	if self:Get("VV1_BBINST") $ "1;4"	// Sold ou onHold

		if self:Get("VV1_BBINST") == "4"
			jJson["userDates"]["userDate1"] := FWTimeStamp(6, CtoD(Left(self:Get("VV1_DTHRES"),6) + "20" + SubStr(self:Get("VV1_DTHRES"),7,2)) , SubStr(self:Get("VV1_DTHRES"),10,8) )
			jJson["userDates"]["userDate2"] := FWTimeStamp(6, CtoD(Left(self:Get("VV1_DTHVAL"),6) + "20" + SubStr(self:Get("VV1_DTHVAL"),7,2)) , Transform(SubStr(self:Get("VV1_DTHVAL"),10,4) + "00" , "@R 99:99:00") )
		else
			jJson["userDates"]["userDate1"] := nil
			jJson["userDates"]["userDate2"] := nil
		endif
/*
		cQuery := "select VV9_FILIAL, VV9_NUMATE, VV9_NOMVIS, VV9_VEND1, A3_NOME"
		cQuery += " from " + retSqlName( "VVA" ) + " VVA"
		cQuery += " join " + retSqlName( "VV9" ) + " VV9 on VV9_FILIAL = VVA_FILIAL and VV9_NUMATE = VVA_NUMTRA and VV9.D_E_L_E_T_ = ' '"
		cQuery += " left join " + retSqlName( "SA3" ) + " SA3 on A3_FILIAL = '" + xFilial("SA3") + "' and A3_COD = VV9_VEND1 and SA3.D_E_L_E_T_ = ' '"
		cQuery += " where VVA_FILIAL = '" + xFilial("VVA") + "'"
		cQuery += " and VVA_CHAINT = '" + self:Get("VV1_CHAINT") + "'"
		cQuery += " and VVA_RESERV = '1'"
		cQuery += " and VVA_DATVAL = '" + DtoS(CtoD(Left(self:Get("VV1_DTHVAL"),6) + "20" + SubStr(self:Get("VV1_DTHVAL"),7,2))) + "'"
		cQuery += " and VVA_HORVAL = '" + SubStr(self:Get("VV1_DTHVAL"),10,4) + "'"
		cQuery += " and VVA.D_E_L_E_T_ = ' '"
*/
		cQuery := "select VV9_FILIAL, VV9_NUMATE, VV9_NOMVIS, A3_NOME, VV9_CODCLI, VV9_LOJA "
		cQuery += " from " + retSqlName( "VVA" ) + " VVA"
		cQuery += " join " + retSqlName( "VV9" ) + " VV9 on VV9_FILIAL = VVA_FILIAL and VV9_NUMATE = VVA_NUMTRA and VV9.D_E_L_E_T_ = ' '"
		cQuery += " left join " + retSqlName( "SA3" ) + " SA3 on A3_FILIAL = '" + xFilial("SA3") + "' and A3_COD = VV9_VEND1 and SA3.D_E_L_E_T_ = ' '"		
		cQuery += " where VVA_FILIAL = '" + xFilial("VVA") + "'"
		cQuery += " and VVA_CHAINT = '" + self:Get("VV1_CHAINT") + "'"
		cQuery += " and VVA.D_E_L_E_T_ = ' '"
		cQuery += " order by VVA.R_E_C_N_O_ desc"

		cQAlias := "TRESERVA"
		dbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), cQAlias, .F., .T. )

		if ! (cQAlias)->(Eof())

			cSQL := "SELECT VK7_REFID "
			cSQL += " FROM " + retSqlName( "VK7" ) + " VK7 "
			cSQL += " JOIN " + retSqlName( "VV9" ) + " VV9 on VV9_FILIAL = '" + xFilial("VV9") + "' and VK7_A1COD = VV9_CODCLI and VK7_A1LOJA = VV9_LOJA and VK7.D_E_L_E_T_ = ' ' "
			cSQL += " WHERE VK7_FILIAL = '" + xFilial("VK7") + "' "
			cSQL += " AND VV9_CODCLI = '"+alltrim((cQAlias)->VV9_CODCLI)+"' "
			cSQL += " AND VV9_LOJA = '"+alltrim((cQAlias)->VV9_LOJA)+"' "
			cSQL += " AND VK7_BBINTG = '1' " 
			cSQL += " AND VK7_BBSYNC = '1' " 
			cSQL += " AND VK7_BBDEL  = '0' "
			cSQL += " AND VV9.D_E_L_E_T_ = ' '"		
			cRefID := FM_SQL(cSQL)

			jJson["userText"]["userText4"] := iif( ! empty((cQAlias)->VV9_FILIAL) , FMX_RmvAcent( alltrim((cQAlias)->VV9_FILIAL) ), NIL )
			jJson["userText"]["userText5"] := iif( ! empty((cQAlias)->VV9_NUMATE) , FMX_RmvAcent( alltrim((cQAlias)->VV9_NUMATE) ), NIL )
			IF !empty(cRefID)
				jJson["userText"]["userText6"] := "CS-" + alltrim(cRefID)
			Else
				jJson["userText"]["userText6"] := iif( ! empty((cQAlias)->VV9_NOMVIS) , FMX_RmvAcent( alltrim((cQAlias)->VV9_NOMVIS) ), NIL )
			Endif
			jJson["userText"]["userText7"] := iif( ! empty((cQAlias)->A3_NOME) , FMX_RmvAcent( alltrim((cQAlias)->A3_NOME) ), NIL )
		else
			jJson["userText"]["userText4"] := nil
			jJson["userText"]["userText5"] := nil
			jJson["userText"]["userText6"] := nil
			jJson["userText"]["userText7"] := nil
		endif
		(cQAlias)->(dbCloseArea())
	else
		jJson["userDates"]["userDate1"] := NIL
		jJson["userDates"]["userDate2"] := NIL
		jJson["userText"]["userText4"] := NIL
		jJson["userText"]["userText5"] := NIL
		jJson["userText"]["userText6"] := NIL
		jJson["userText"]["userText7"] := NIL
	endif
	jJson["jdLinkMachineId"]          := nil
	jJson["lastMeterReading"]       := nil

	cQuery := "SELECT * FROM " + RetSQLName("VO0") + " VO0 WHERE VO0_FILIAL = '" + xFilial("VO0") + "' AND VO0_CHAINT = '" + self:Get("VV1_CHAINT") + "' AND D_E_L_E_T_ = ' ' ORDER BY VO0_DATA DESC, VO0_HORA DESC"
	cQAlias := "TMP_VO0"
	dbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), cQAlias, .F., .T. )
	if !(cQAlias)->(Eof()) //.and. (cQAlias)->VO0_BBINTG == '0'
	
		jMeter := JsonObject():New()
		jMeter["description"]          := FGX_SetVlJson( iif( (cQAlias)->VO0_ORIGEM == "1" , "LANÇAMENTO MANUAL", "ORDEM DE SERVIÇO" ) , .f. )
		jMeter["value"]                := (cQAlias)->VO0_KILOME
		jMeter["units"]                := "Hours"
		jMeter["lastUpdatedOn"]        := FWTimeStamp(6, StoD((cQAlias)->VO0_DATA), Transform(StrZero((cQAlias)->VO0_HORA,4),"@R 99:99:00"))
		jMeter["lastUpdatedBy"]        := "Protheus"
		jJson["lastMeterReading"] := jMeter
	else
		jJson["lastMeterReading"] := nil
	endif
	(cQAlias)->(dbCloseArea())


	jJson["licenseDate"]           := nil
	jJson["licenseExpiresOn"]      := nil
	jJson["licenseNumber"]         := nil

	cVE1RefId := self:checkForeignKey(self:Get("VE1_RECNO"),"VE1")
	cVV2RefId := self:checkForeignKey(self:Get("VV2_RECNO"),"VV2")

	jJson["makeReferenceId"]       := iif( cVE1RefId <> NIL .and. ! empty(cVE1RefId) , FGX_SetVlJson( "EK-" + cVE1RefId , .f. ) , NIL )
	jJson["meters"]                := nil
	jJson["modelName"]             := iif( ! empty(self:Get("VV2.VV2_DESMOD")) , self:Get("VV2.VV2_DESMOD") , NIL )
	jJson["modelReferenceId"]      := iif( cVV2RefId <> NIL .and. ! empty(cVV2RefId) , FGX_SetVlJson( "EM-" + cVV2RefId , .f. ) , NIL )
	jJson["notes"]                 := FGX_SetVlJson( self:Get("VV1_OBSERV"), .f. )
	jJson["onYard"]                := self:Get("VV1_BBYARD") == "1"


	cQuery := "SELECT VQE_CODVQD, VQE_BBRFID, VQD_DESCRI, VQD_DIGIMP, VQD_BASCOD, VQD_CODOPC " +;
		" FROM " + RetSQLName("VQE") + " VQE " +;
		" JOIN " + RetSQLName("VQD") + " VQD ON " +;
		       " VQD.VQD_FILIAL = '" + FWxFilial("VQD") + "'" +;
		   " AND VQD.VQD_CODIGO = VQE.VQE_CODVQD " +;
		   " AND VQD.D_E_L_E_T_ = ' '" +;
		 " WHERE VQE.VQE_FILIAL = '" + FWxFilial("VQE") + "' " +;
		   " AND VQE.VQE_CHAINT = '" + self:Get("VV1_CHAINT") + "' " +;
		   " AND VQE.D_E_L_E_T_ = ' ' "+;
		   " AND VQD.VQD_CODOPC <> ' ' "
	cQAlias := "TMP_VQE"
	
	dbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), cQAlias, .F., .T. )
	
	if !(cQAlias)->(Eof())

		jJson["options"]           := {}
	
		While !(cQAlias)->(Eof())

			If (cQAlias)->VQD_DIGIMP == "0" .OR. Empty((cQAlias)->VQD_CODOPC)
				cCodOpc := (cQAlias)->VQE_CODVQD
			Else
				cCodOpc := (cQAlias)->VQD_BASCOD+(cQAlias)->VQD_CODOPC
			EndIf
	
			jOption := JsonObject():New()
	
			jOption["code"]                     := FGX_SetVlJson( cCodOpc , .f.)	
			jOption["cost"]                     := NIL
			jOption["description"]              := FGX_SetVlJson( (cQAlias)->VQD_DESCRI, .f.)
			jOption["estimatedCost"]            := NIL
			jOption["incentiveAgreementNumber"] := NIL
			jOption["referenceId"]              := FGX_SetVlJson( "EO-" + (cQAlias)->VQE_BBRFID , .f.)
			jOption["quantity"]                 := 1
			jOption["salePrice"]                := NIL
			jOption["serialNumber"]             := NIL
			jOption["type"]                     := "Options"

			aadd( jJson["options"] , jOption )
	
			(cQAlias)->(dbSkip())
		End
	
	else
	
		jJson["options"]           := nil
	
	endif
	(cQAlias)->(dbCloseArea())

	jJson["referenceId"]               := FGX_SetVlJson( iif( ! empty(self:Get("VV1_BBRFID")) , "EQ-" + self:Get("VV1_BBRFID") , "" ) , .f. )
	jJson["serialNumber"]              := FGX_SetVlJson( self:Get("VV1_CHASSI"), .f. )

	// A reference to the location from which the equipment was sold.
	// The reference id from the location where the machine is sold.
	// Can be two ways to send:
	// If this record is equipmentType <> ""CustomerEquipment"": blank.
	// If this record is equipmentType = ""CustomerEquipment"" and VV1_SITVEI = ""1=Vendido"": the positioning will use the VV1_FILSAI."
	jJson["soldByLocationReferenceId"] := nil
	if self:Get("VV1_BBEQTY") $ "1/2" .and. self:get("VV1_SITVEI") $ "0/1"
		IF !Empty(self:Get("VV1_SOLCID")) 	//Quando estiver em branco, retornar NIL
			jJson["soldByLocationReferenceId"] := FGX_SetVlJson( alltrim(cEmpAnt + "-" + self:Get("VV1_SOLCID")) , .t. )
		Else
			jJson["soldByLocationReferenceId"] := nil
		Endif 		
	else
		jJson["soldByLocationReferenceId"] := nil
	endif

	// temporario ... estava dando erro no processamento do dynamics, 
	// conforme email do Brad, isso era um problema na John Deere.
	if empty( self:Get("VV1_DTEFAB") )
		jJson["plannedDeliveryDate"] := nil
	else
		jJson["plannedDeliveryDate"] := FWTimeStamp(6, StoD( self:Get("VV1_DTEFAB")), "00:00:00")
	endif
	jJson["orderNumber"] := IIF( !empty(self:Get("VV1_PEDFAB")) , self:Get("VV1_PEDFAB") , NIL )

	// The ID for the location for the equipment.
	// "The reference id from the location where the machine is stored.
	// 
	// Can be two ways to send:
	// If this record is equipmentType = ""StockUnit"" or ""FixedAsset"": the positioning will use the VV1_LOCRID.
	// If this record is equipmentType = ""CustomerEquipment"": blank."
	jJson["locationReferenceId"] := nil

	oDados := self:GetDadosDoPedido()
	if self:Get("VV1_BBEQTY") <> "1" .and. ! empty(self:Get("VV1_BBEQTY"))
		if Empty(self:Get("VV1_LOCRID")) .and. ! ValType(oDados) == "U" .and. ! empty(oDados["VQ0_FILENT"])
			jJson["locationReferenceId"] := FGX_SetVlJson(alltrim(cEmpAnt + "-" + oDados["VQ0_FILENT"]) , .f.)
		elseif !Empty(self:Get("VV1_LOCRID"))
			jJson["locationReferenceId"] := FGX_SetVlJson(alltrim(cEmpAnt + "-" + self:Get("VV1_LOCRID")) , .f.)
		endif
	endif

	// The salesperson that sold the equipment.
	// We will have to search for the nota fiscal that is related to the sale, pick the sales person code and inform the sales person name.
	if self:get("VV1_SITVEI") == "1"
		jJson["soldBySalesperson"]   := FGX_SetVlJson(self:Get("SA3.A3_NOME"), .f.)
		jJson["customerInvoiceDate"] := FWTimeStamp(6, StoD( self:Get("VV1_DTUVEN")), "00:00:00")
		jJson["salesInvoiceNumber"]  := FGX_SetVlJson(self:Get("VV0.VV0_NUMNFI")+"-"+self:Get("VV0.VV0_SERNFI"), .f.)
		jJson["soldSalePrice"]       := self:Get("VVA.VVA_VALVDA")
		jJson["soldToReferenceId"]   := FGX_SetVlJson( iif( ! empty(VK7->VK7_REFID) , "CS-" + VK7->VK7_REFID , "" ) , .f. )
	else
		jJson["soldBySalesperson"]   := nil
		jJson["customerInvoiceDate"] := nil
		jJson["salesInvoiceNumber"]  := nil
		jJson["soldSalePrice"]       := nil
		jJson["soldToReferenceId"]   := iif( self:Get("VV1_BBINST") == "1", FGX_SetVlJson( iif( ! empty(VK7->VK7_REFID) , "CS-" + VK7->VK7_REFID , "" ) , .f. ), nil )
	endif
	//jJson["stockNumber"]           := FGX_SetVlJson(self:Get("SB1.B1_COD"), .f.)
	jJson["stockNumber"]           := FGX_SetVlJson( SB1->B1_COD , .f. )
	jJson["rentedToReferenceId"]   := nil
	jJson["subCategory"]           := nil
	jJson["taxCategory"]           := nil
	jJson["terminalSerialNumber"]  := nil
	jJson["warrantyType"]          := nil
	
	do case
	case self:Get("VV1_BBSTST") == "1"
		jJson["stockUnitStatus"] := "Pending"
	case self:Get("VV1_BBSTST") == "2"
		jJson["stockUnitStatus"] := "InStock"
	case self:Get("VV1_BBSTST") == "3"
		jJson["stockUnitStatus"] := "Invoiced"
	case self:Get("VV1_BBSTST") == "4"
		jJson["stockUnitStatus"] := "Rental"
	otherwise
		jJson["stockUnitStatus"] := nil
	endcase

	do case
	case self:Get("VV1_BBSTTY") == "1"
		jJson["stockType"]             := "New"
	case self:Get("VV1_BBSTTY") == "2"
		jJson["stockType"]             := "Used"
	case self:Get("VV1_BBSTTY") == "3"
		jJson["stockType"]             := "Demo"
	otherwise
		jJson["stockType"]             := IIF(self:Get("VV1_ESTVEI") == "0", "New", "Used")
	endcase

	jJson["transmissionSerialNumber"]  := FGX_SetVlJson( self:Get("VV1_CAMBIO"), .f. )

	xAux := FGX_SetVlJson( self:Get("VV1_FABMOD"), .f. )
	if valType( xAux ) == "U"
		jJson["yearManufactured"]      := nil
		jJson["modelYear"]             := nil
	else
		jJson["yearManufactured"]      := left( xAux, 4 )
		jJson["modelYear"]             := right( xAux, 4 )
	endif

	if empty( self:Get("VV1_DATPED") )
		jJson["orderDate"]             := nil
	else
		jJson["orderDate"]             := FWTimeStamp(6, StoD( self:Get("VV1_DATPED")), "00:00:00")
	endif

	if ! empty( self:Get("VV1_DTDPCP") )
		jJson["dateReceived"] := FWTimeStamp(6, StoD( self:Get("VV1_DTDPCP") ), "00:00:00")
	else
		jJson["dateReceived"] := nil
	endif

	if ! empty( self:Get("VV1_NUMCMO") )
		jJson["cabNumber"] := self:Get("VV1_NUMCMO")
	else
		jJson["cabNumber"] := nil
	endif

	if self:Get("VV1_BBEQTY") == "2"	// StockUnit
		xAux := FGX_VLRSUGV(	self:Get("VV1_CHAINT"),;
								self:Get("VV1_CODMAR"),;
								self:Get("VV1_MODVEI"),;
								self:Get("VV1_SEGMOD"),;
								self:Get("VV1_CORVEI"),;
								.T.;
							)
		jJson["listPrice"] := xAux
		jJson["saleValue"] := xAux
	else
		jJson["listPrice"] := nil
		jJson["saleValue"] := nil
	endif

	if valType(oDados) != "U"
		jJson["dateMarkedSold"] := iif( ! empty(oDados["VJR_RTLDAT"]) , FGX_SetVlJson( FWTimeStamp(6, StoD( oDados["VJR_RTLDAT"] ), "00:00:00"), .f.) , NIL )
		jJson["userText"]["userText1"]      := iif( ! empty(oDados["VJR_CCGNAM"]) , FMX_RmvAcent( FGX_SetVlJson( oDados["VJR_CCGNAM"], .f.) ), nil )
	else
		jJson["dateMarkedSold"] := nil
		jJson["userText"]["userText1"]      := nil
	endif

	jJson["baseMachineCode"] := FGX_SetVlJson( getBaseCD( self:Get("VV1_CHAINT") ), .f. )
	jJson["totalCost"]       := getCustEquip( self:get("VV1_CHAINT"), self:get("VV1_SITVEI") )

	//if lDebug
	//	
	//	Conout("-----------------------------------------------------------------------")
	//	Conout("OFSoEquipment toJsonApi " + Str(Procline(1),5) + " - " + ProcName(1))
	//
	//	nCont := 2
	//	While !Empty(ProcName(nCont))
	//		Conout( "                       " + Str(Procline(nCont),5) + " - " + ProcName(nCont) )
	//		nCont++
	//	EndDo
	//	Conout("-----------------------------------------------------------------------")
	//	Conout(" ")
	//endif
Return jJson

method FetchMemoFields() class OFSoEquipment

	dbSelectArea('VV1')
	self:_Posiciona()

	self:oJsonData["VV1_OBSVEI"] := VV1->VV1_OBSVEI

	self:FetchOldMemos()
return .t.


method FetchOldMemos() class OFSoEquipment

	if type("INCLUI") == "U"
		INCLUI := .F.
	endif
	if type("ALTERA") == "U"
		ALTERA := .F.
	endif

	If ! Empty( VV1->VV1_OBSMEM )
		self:oJsonData["VV1_OBSMEM"] := VV1->VV1_OBSMEM
		self:oJsonData["VV1_OBSERV"] := ALLTRIM(E_MSMM(VV1->VV1_OBSMEM,TamSx3("VV1_OBSERV")[1]))
	else
		self:oJsonData["VV1_OBSMEM"] := ""
		self:oJsonData["VV1_OBSERV"] := ""
	EndIf
	self:lMemosFetched := .t.

return .t.

Method checkForeignKey(nRecNo,cTable) Class OFSoEquipment

	Local oModel
	Local cRetRefID := ""

	DbSelectArea(cTable)
	DbGoto(nRecNo)

	if cTable == "VE1"
		if VE1->VE1_BBDEL == "1"

			oModel := FWLoadModel("VEIA031")
			oModel:setOperation(MODEL_OPERATION_UPDATE)

			if oModel:Activate()

				oModel:SetValue("MODEL_VE1", "VE1_BBINTG" , "0" ) // Marca como nao integrado para enviar a JD
				oModel:SetValue("MODEL_VE1", "VE1_BBSYNC" , "0" ) // Marca como nao sincronizado para enviar a JD
				oModel:SetValue("MODEL_VE1", "VE1_BBDEL"  , "0" ) // Desmarca como deletado

				If oModel:VldData()
					oModel:CommitData()
				endif

				oModel:DeActivate()

			EndIf

		endif

		if VE1->VE1_BBINTG == "0" .or. empty(VE1->VE1_BBINTG)
			VA0310013_TransmitirBlackBird(cTable,nRecNo,3,.f.)
		endif

		cRetRefID := VE1->VE1_BBRFID

	ElseIf cTable == "VV2"
		if VV2->VV2_BBDEL == "1"

			oModel := FWLoadModel("VEIA030")
			oModel:setOperation(MODEL_OPERATION_UPDATE)

			if oModel:Activate()

				oModel:SetValue("MODEL_VV2", "VV2_BBINTG" , "0" ) // Marca como nao integrado para enviar a JD
				oModel:SetValue("MODEL_VV2", "VV2_BBSYNC" , "0" ) // Marca como nao sincronizado para enviar a JD
				oModel:SetValue("MODEL_VV2", "VV2_BBDEL"  , "0" ) // Desmarca como deletado

				If oModel:VldData()
					oModel:CommitData()
				endif

				oModel:DeActivate()

			EndIf

		endif

		if VV2->VV2_BBINTG == "0" .or. Empty(VV2->VV2_BBINTG)
			VA0300033_TransmitirBlackBird(cTable,nRecNo,3,.f.)
		endif

		cRetRefID := VV2->VV2_BBRFID

	EndIf

Return cRetRefID


Method enforceCustomerIsCreated(cProAtu , cLjPAtu) Class OFSoEquipment

	local cBkpAlias := Alias()
	local oModelSA1
	local oModel402
	local aErro
	local aAreaSA1 := SA1->(getArea())
		
	//Conout("cProAtu -> [" + cProAtu +  "]" )
	//Conout("cLjPAtu -> [" + cLjPAtu + "]" )

	VK7->(dbSetOrder(2))
	if ! VK7->(dbSeek(xFilial("VK7") + xFilial('SA1') + cProAtu + cLjPAtu ))


		dbSelectArea("SA1")
		SA1->(dbSetOrder(1))
		SA1->(dbSeek(xFilial("SA1") + cProAtu + cLjPAtu ))

		oModelSA1 := FWLoadModel( 'CRMA980' )
		oModelSA1:SetOperation( MODEL_OPERATION_UPDATE )
		
		lRet := oModelSA1:Activate()




		//Conout("SA1->A1_COD  -> [" + SA1->A1_COD  + "]")
		//Conout("SA1->A1_LOJA -> [" + SA1->A1_LOJA + "]")
		//Conout("SA1->A1_NOME -> [" + SA1->A1_NOME + "]")

		oModelSA1:SetValue("SA1MASTER","A1_NOME",SA1->A1_NOME)
		If oModelSA1:VldData()
			oModelSA1:CommitData()
		Else
			aErro   := oModelSA1:GetErrorMessage()

			// A estrutura do vetor com erro é:
			//  [1] Id do formulário de origem
			//  [2] Id do campo de origem
			//  [3] Id do formulário de erro
			//  [4] Id do campo de erro
			//  [5] Id do erro
			//  [6] mensagem do erro
			//  [7] mensagem da solução
			//  [8] Valor atribuido
			//  [9] Valor anterior
//			Conout(" ")
//			Conout("ERRO !!!!")
//			Conout(" ")
//			Conout("OFSoEquipment -> enforceCustomerIsCreated ")
//			Conout(" ")

//			Conout( "Id do formulário de origem:" + ' [' + AllToChar( aErro[1]  ) + ']' )
//			Conout( "Id do campo de origem:     " + ' [' + AllToChar( aErro[2]  ) + ']' )
//			Conout( "Id do formulário de erro:  " + ' [' + AllToChar( aErro[3]  ) + ']' )
//			Conout( "Id do campo de erro:       " + ' [' + AllToChar( aErro[4]  ) + ']' )
//			Conout( "Id do erro:                " + ' [' + AllToChar( aErro[5]  ) + ']' )
//			Conout( "Mensagem do erro:          " + ' [' + AllToChar( aErro[6]  ) + ']' )
//			Conout( "Mensagem da solução:       " + ' [' + AllToChar( aErro[7]  ) + ']' )
//			Conout( "Valor atribuido:           " + ' [' + AllToChar( aErro[8]  ) + ']' )
//			Conout( "Valor anterior:            " + ' [' + AllToChar( aErro[9]  ) + ']' )
//			Conout(" ")

		endif

		oModelSA1:DeActivate()

	else
		if VK7->VK7_BBDEL == "1"

			oModel402 := FWLoadModel("OFIA402")
			oModel402:setOperation(MODEL_OPERATION_UPDATE)

			if oModel402:Activate()

				oModel402:SetValue("MODEL_VK7", "VK7_BBINTG" , "0" ) // Marca como nao integrado para enviar a JD
				oModel402:SetValue("MODEL_VK7", "VK7_BBSYNC" , "0" ) // Marca como nao sincronizado para enviar a JD
				oModel402:SetValue("MODEL_VK7", "VK7_BBDEL"  , "0" ) // Desmarca como deletado

				If oModel402:VldData()
					oModel402:CommitData()
				endif

				oModel402:DeActivate()

			EndIf

		endif

		if VK7->VK7_BBINTG == "0"
			OA4020033_TransmitirBlackBird()
		endif
		
	EndIf

	restArea(aAreaSA1)

	if ! empty(cBkpAlias)
		dbselectarea(cBkpAlias)
	endif

Return

/*/{Protheus.doc} GetStockTypeForProtheus
	Get value (dePara) from SO to Protheus

	@type method
	@author Vinicius Gati
	@since 14/10/2022
/*/
Method GetStockTypeForProtheus(cStockType) Class OFSoEquipment
	do case
	case cStockType == "New"
		return "1"
	case cStockType == "Used"
		return "2"
	case cStockType == "Demo"
		return "3"
	endcase
Return " "

/*/{Protheus.doc} GetStockUnitStatusForProtheus
	Get value (dePara) from SO to Protheus for stock unit status

	@type method
	@author Vinicius Gati
	@since 14/10/2022
/*/
Method GetStockUnitStatusForProtheus(cStockUnitStatus) Class OFSoEquipment
	do case
	case cStockUnitStatus == "Pending"
		return "1"
	case cStockUnitStatus == "InStock"
		return "2"
	case cStockUnitStatus == "Invoiced"
		return "3"
	case cStockUnitStatus == "Rental"
		return "4"
	endcase
Return " "

/*/{Protheus.doc} GetEquipmentTypeForProtheus

	@type method
	@author Vinicius Gati
	@since 14/10/2022
/*/
Method GetEquipmentTypeForProtheus(cEquipStatus) Class OFSoEquipment
	do case
	case cEquipStatus == "CustomerEquipment"
		return "1"
	case cEquipStatus == "StockUnit"
		return "2"
	case cEquipStatus == "FixedAsset"
		return "3"
	endcase	
Return "1"

/*/{Protheus.doc} GetInventoryStatusForProtheus

	@type method
	@author Vinicius Gati
	@since 14/10/2022
/*/
Method GetInventoryStatusForProtheus(cStatus) Class OFSoEquipment
	do case
	case cStatus == "Sold"
		return "1"
	case cStatus == "Stock"
		return "2"
	case cStatus == "Loaner"
		return "3"
	case cStatus == "OnHold"
		return "4"
	case cStatus == "Rental"
		return "5"
	case cStatus == "Demo"
		return "6"
	case cStatus == "Shop"
		return "7"
	case cStatus == "Asset"
		return "8"
	endcase
Return " "


/*/{Protheus.doc} GetDadosDoPedido
	Pega os dados do pedido para gerar o json

	@type method
	@author Vinicius Gati
	@since 27/10/2023
/*/
Method GetDadosDoPedido() Class OFSoEquipment
	local aResults
	local cSQL := ""
	cSQL += " SELECT VQ0_FILIAL, VQ0_FILENT, VJR_RTLDAT, VJR_CCGNAM  "
	cSQL += "   FROM " + RetSQLName("VQ0") + " VQ0 "
	cSQL += "   left join " + RetSQLName("VJR") + " VJR "
	cSQL += "      on  VJR_FILIAL = VQ0_FILIAL"
	cSQL += "      and VJR_CODVQ0 = VQ0_CODIGO"
	cSQL += "      and VJR.D_E_L_E_T_  = ' ' "
	cSQL += "  WHERE VQ0_FILIAL = '" + xFilial("VQ0") + "' " 
	cSQL += "    AND VQ0_CHASSI = '" + self:Get("VV1_CHASSI") + "' "
	cSQL += "    AND VQ0_STATUS <> '3' "
	cSQL += "    AND VQ0.D_E_L_E_T_  = ' ' "
	aResults = oSqlHlp:GetSelectJson( { "VQ0_FILENT", "VJR_RTLDAT", "VJR_CCGNAM" }, cSQL)
	if len(aResults) > 0
		return aResults[1]
	endif
return nil


/*/{Protheus.doc} getBaseCD
Recupera o Código Base do Equipamento ( baseMachineCode )
@type function
@version 1.0
@author cristiamRossi
@since 12/18/2023
@param cChaInt, character, Número interno do Veículo
@return character, código base do equipamento
/*/
static function getBaseCD( cChaInt )
	local cAliasAtu := alias()
	local cAlias    := getNextAlias()
	local cBaseCD   := ""

	beginSql alias cAlias
		select VJR_ORDCOD
		from %table:VQ0% VQ0
		join %table:VJR% VJR
			on  VJR_FILIAL = VQ0_FILIAL
			and VJR_CODVQ0 = VQ0_CODIGO
			and VJR.%notdel%
		where VQ0_FILIAL = %xFilial:VQ0%
		and   VQ0_CHAINT = %exp:cChaInt%
		and   VQ0.%notdel%
	endSql

	if ! (cAlias)->( eof() )
		cBaseCD := (cAlias)->VJR_ORDCOD
	endif
	(cAlias)->( dbCloseArea() )

	if ! empty( cAliasAtu )
		dbSelectArea( cAliasAtu )
	endif
return cBaseCD


/*/{Protheus.doc} getCustEquip
Recupera custo do Equipamento ( totalCost )
@type function
@version 1.0
@author cristiamRossi
@since 12/19/2023
@param cChaInt, character, Número interno do Veículo
@param cSitVei, character, Situação do Equipamento
@return numeric, custo do Equipamento
/*/
static function getCustEquip( cChaInt, cSitVei )
local cAliasAtu := alias()
local nCusto    := nil
local aAreaVQ0  := VQ0->( getArea() )

	if cSitVei == "0"
		if FGX_VV1SB1( "CHAINT", cChaInt )
			dbSelectArea("SB2")
			SB2->( dbSetOrder(1) )

			if SB2->( dbSeek( xFilial( "SB2" ) + SB1->B1_COD + VV1->VV1_LOCPAD ) )
				nCusto := SB2->B2_CM1
			endif
		endif
	else
		dbSelectArea("VQ0")
		VQ0->( dbSetOrder(3) )	// VQ0_FILIAL + VQO_CHAINT

		if VQ0->( dbSeek( xFilial( "VQ0" ) + cChaInt ) )
			nCusto := VQ0->VQ0_VALCUS
		endif
		VQ0->( restArea( aAreaVQ0 ) )
		VQ0->( restArea( aAreaVQ0 ) )
	endif

	if ! empty( cAliasAtu )
		dbSelectArea( cAliasAtu )
	endif
return nCusto
