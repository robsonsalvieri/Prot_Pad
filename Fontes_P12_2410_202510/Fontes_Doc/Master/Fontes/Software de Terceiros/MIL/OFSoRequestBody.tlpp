#include 'totvs.ch'
#include "tlpp-core.th"

/*/{Protheus.doc} OFSoRequestBody
    Classe que interpreta um body padrao do SO John deere
    
    @type class
    @author Vinicius Gati
    @since 01/07/2021
/*/
Class OFSoRequestBody
    Public Data jBody
    Public Data cParseMsg

    Public Method New()
    Public Method IsValid()
    Public Method GetData()
    Public Method GetName()
    Public Method SetData()
    Public Method SetName()
    Public Method ToJson()
EndClass

/*/{Protheus.doc} New
    Construtor Simples

    @type method
    @author Vinicius Gati
    @since 01/07/2021
/*/
Method New(cBody) Class OFSoRequestBody
    ::jBody := JsonObject():New()
    if ! empty(cBody)
        ::cParseMsg := ::jBody:fromJson(cBody)
    endif
Return SELF


/*/{Protheus.doc} IsValid
    Returns if the json is a valida request body from SO project

    @type method
    @author Vinicius Gati
    @since 01/07/2021
/*/
Method IsValid() Class OFSoRequestBody
Return Empty(self:cParseMsg) .and. self:jBody:hasProperty("name") .and. self:jBody:hasProperty("data")


/*/{Protheus.doc} GetData
    retorna propriedade Data

    @type method
    @author Vinicius Gati
    @since 01/07/2021
/*/
Method GetData(Params) Class OFSoRequestBody
Return self:jBody["data"]

/*/{Protheus.doc} GetName
    retorna propriedade name que também é a action da url

    @type method
    @author Vinicius Gati
    @since 01/07/2021
/*/
Method GetName() Class OFSoRequestBody
Return self:jBody["name"]


/*/{Protheus.doc} ToJson
    returns the internal json object

    @type method
    @author Vinicius Gati
    @since 02/07/2021
/*/
Method ToJson() Class OFSoRequestBody
Return self:jBody

/*/{Protheus.doc} SetData
    Set Data

    @type method
    @author Vinicius Gati
    @since 02/07/2021
/*/
Method SetData(jData) Class OFSoRequestBody
Return self:jBody["data"] := jData


/*/{Protheus.doc} SetName
    set Name

    @type method
    @author Vinicius Gati
    @since 02/07/2021
/*/
Method SetName(cName as Character) Class OFSoRequestBody
Return self:jBody["name"] := cName
