#include 'totvs.ch'

/*/{Protheus.doc} OFSoLogTextoDinamico
	Classe para controlar um log de texto usando template e valores
	
	@type class
	@author Vinicius Gati
	@since 27/10/2022
/*/
Class OFSoLogTextoDinamico
	data aAttrib
	data oFormLog
	Public Method New() CONSTRUCTOR
	Public Method Template()
	Public Method SetValue()
	Public Method ToString()
	Public Method ToJson()
EndClass

/*/{Protheus.doc} New
	Construtor Simples

	@type method
	@author Vinicius Gati
	@since 27/10/2022
/*/
Method New(oFormLog, jInitialData) Class OFSoLogTextoDinamico
	::aAttrib := DMS_DataContainer():New()
	if jInitialData != nil
		::SetValue(":title:", jInitialData[":title:"])
		::SetValue(":detail:", jInitialData[":detail:"])
		::SetValue(":progress:", jInitialData[":progress:"])
	endif
	::oFormLog := oFormLog
Return SELF

/*/{Protheus.doc} ToJson
	Descricao

	@type method
	@author Vinicius Gati
	@since 28/10/2022
/*/
Method ToJson() Class OFSoLogTextoDinamico
	local oJ := JsonObject():New()
	oJ[":title:"] := self:aAttrib:Gv(":title:")
	oJ[":detail:"] := self:aAttrib:Gv(":detail:")
	oJ[":progress:"] := self:aAttrib:Gv(":progress:")
Return oJ

/*/{Protheus.doc} Template
	Returns a string in template format

	@type method
	@author Vinicius Gati
	@since 27/10/2022
/*/
Method Template() Class OFSoLogTextoDinamico
	cLog := ":title:" + chr(13) + chr(10)
	cLog += " --- " + chr(13) + chr(10)
	cLog += ":detail: " + chr(13) + chr(10)
	cLog += " --- " + chr(13) + chr(10)
	cLog += ":progress:" + chr(13) + chr(10)
Return cLog

/*/{Protheus.doc} ToString
	retorna o teplate em string

	@type method
	@author Vinicius Gati
	@since 27/10/2022
/*/
Method ToString() Class OFSoLogTextoDinamico
	local nX := 1
	cTemplate := self:Template()
	For nX:= 1 to Len(self:aAttrib:aData)
		aData := self:aAttrib:aData[nX]
		cLabel := aData[1]
		cValue := aData[2]
		cTemplate := StrTran(cTemplate,cLabel,cValue)
	Next
Return cTemplate

/*/{Protheus.doc} SetValue

	@type method
	@author Vinicius Gati
	@since 27/10/2022
/*/
Method SetValue(cLabel, cValue) Class OFSoLogTextoDinamico
	self:aAttrib:SetValue(cLabel, cValue)
	if self:oFormLog != nil
		self:oFormLog:SetValue("LOG", self:ToString())
	endif
Return .t.
