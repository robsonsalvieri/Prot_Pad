#include "tlpp-core.th"
#include 'totvs.ch'

/*/{Protheus.doc} OFSoContactCreatedInDbsMessage
	Classe que representa uma mensagem da entidade Contact mensagem CreatedInDbs
	
	@type class
	@author Vinicius Gati
	@since 09/08/2021
/*/
Class OFSoContactCreatedInDbsMessage
	public data cName
	public data oSoRequest
	data oSoContact
	data cMessageId
	data cMsgVX5
	data cOriId

	public Method New() CONSTRUCTOR
	public Method GetMessageId()
	public Method SetContact()
	public Method Send()
	public Method GetBody()
	public Method GetHeader()
	public Method Delay()
EndClass

/*/{Protheus.doc} New
	Construtor Simples

	@type method
	@author Vinicius Gati
	@since 09/08/2021
/*/
Method New(oContact) Class OFSoContactCreatedInDbsMessage
	::cName := "ContactCreatedInDbs"
	::oSoRequest := OFSoRequest():New(self)

	::oSoRequest:Set("VK5_ORITAB", "VK8")
	::oSoRequest:Set("VK5_MESSAG", "10")

	if valtype(oContact) != "U"
		::SetContact(oContact)
	endif
Return SELF

/*/{Protheus.doc} SetContact
	Seta o Contact que gerará o body da mensagem

	@type method
	@author Vinicius Gati
	@since 10/08/2021
/*/
Method SetContact(oSoContact) Class OFSoContactCreatedInDbsMessage
	self:oSoContact := oSoContact
	self:oSoRequest:SetBody(self:GetBody())

	self:oSoRequest:Set("VK5_ORIKEY", self:oSoContact:GetRefId())

Return .t.

/*/{Protheus.doc} GetMessageId
	Retorna o nome da mensagem para ser enviada ao SO onde necessário

	@type method
	@author Vinicius Gati
	@since 09/08/2021
/*/
Method GetMessageId() Class OFSoContactCreatedInDbsMessage
	if valtype(self:cMessageId) == "U"
		self:cMessageId := "contacts-" + FWUUIDV4(.t.)
	endif
return self:cMessageId

/*/{Protheus.doc} GetBody
	Retorna o body para a mensagem

	@type method
	@author Vinicius Gati
	@since 12/08/2021
/*/
Method GetBody() Class OFSoContactCreatedInDbsMessage
	oReqBody := OFSoRequestBody():New()
	oReqBody:SetData(self:oSoContact:toJson())
	oReqBody:SetName(self:cName)
Return oReqBody

/*/{Protheus.doc} GetHeader
	Retorna o header da mensagem

	@type method
	@author Vinicius Gati
	@since 12/08/2021
/*/
Method GetHeader() Class OFSoContactCreatedInDbsMessage
Return self:oSoRequest:GetHeader()

/*/{Protheus.doc} Send
	Envia mensagem para SO

	@type method
	@author Vinicius Gati
	@since 12/08/2021
/*/
Method Send() Class OFSoContactCreatedInDbsMessage
	local oComm := OFSoCommunication():New()
	self:oSoRequest:SetBody(self:GetBody())
	self:oSoRequest:Save()

	jResp := oComm:SendMessage(self, self:oSoRequest)
	//if jResp["code"] == oComm:nCodigoFalhaComunicacao
	//	self:Delay() // atrasa para tentar mais tarde
	//endif
Return 

/*/{Protheus.doc} Delay
	atrasa a mensagem por falha de comunicação

	@type method
	@author Vinicius Gati
	@since 19/10/2021
/*/
Method Delay() Class OFSoContactCreatedInDbsMessage
	Local oMsg := OFDMSExitMessage():New()
	local oJson := JsonObject():New()
	self:oSoRequest:Save()
	oJson['header'] := self:oSoRequest:GetHeader():ToJson()
	oJson['body'] := self:oSoRequest:GetBody():ToJson()
	oMsg:Set("VK6_PAYLOA", oJson:toJson())
	oMsg:Set("VK6_MSGNAM", OFDMSMessageConstants():ContactCreatedInDbsMessageName())
	oMsg:Set("VK6_CODVK5", self:oSoRequest:GetId())
	oMsg:SetAtrasada()
Return oMsg:Save()
