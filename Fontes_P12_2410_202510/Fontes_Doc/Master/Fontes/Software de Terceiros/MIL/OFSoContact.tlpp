#include "tlpp-core.th"
#include "totvs.ch"
#INCLUDE "FWMVCDEF.CH"

//static lDebug := .f.

/*/{Protheus.doc} OFSoContact
	Classe criada para retornar os dados conforme Contact Service do Service Operations

	O retorno básico é:
	{
		"name": "ContactCreatedInDbs",
		"data": {
			"referenceId": "CONTACT-REF-ID-001",
			"customerReferenceId": "CUST-REF-ID-001",
			"vendorReferenceId": "VEND-REF-ID-001",
			"sourceSystem": "DBS",
			"isPrimaryForCustomer": true,
			"isPrimaryForVendor": true,
			"firstName": "Pat",
			"lastName": "Washington",
			"middleName": "Jerry",
			"familiarName": "JW",
			"email": "pat.washington@johndeere.local",
			"email2": "anotheremail@johndeere.local",
			"email3": "thirdemail@johndeere.local",
			"primaryPhone": "8648675309",
			"homePhone": "8648675309",
			"mobilePhone": "8648675309",
			"faxNumber": "8641112222",
			"salutation": "Ind",
			"suffix": "PhD",
			"generation": "Jr.",
			"mailingAddress": {
				"line1": "1 Elm Street",
				"city": "Moline",
				"stateOrProvince": "IA",
				"postalCode": "11223",
				"county": "Moline",
				"country": "USA"
			},
			"physicalAddress": {
				"line1": "1 Elm Street",
				"city": "Moline",
				"stateOrProvince": "IA",
				"postalCode": "11223",
				"county": "Moline",
				"country": "USA"
			}
		}
	}

	Conforme link de documentação em: https://jdbbmockstordata001.blob.core.windows.net/$web/bb-api-docs/Contact/latest/OpenApi.html#operation/ContactCreatedInDbs

	@type function
	@author Rubens Takahashi
	@since 10/01/2022
/*/
class OFSoContact from VERegistroSql
	Private Data aFldMap
	Public Method New()
	Public Method ToJsonApi()
	Public Method ToJson()
	Public Method GetRefId()
	Public Method Fill()
	Public Method Save()
	Public Method FindByRecnoDel()
	Private Method enforceCustomerIsCreated()
end class

/*/{Protheus.doc} New
	Construtor basico

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
method New() class OFSoContact
	_Super:New('VK8')

	::AddFields({;
		"R_E_C_N_O_", ;
		"VK8_CODIGO", "VK8_REFID", "VK8_U5FIL","VK8_U5COD",;
		"VK8_CODVK7", "VK8_PRICUS", "VK8_PRIVEN", "VK8_FSNAME", "VK8_LSNAME", "VK8_EMAIL", "VK8_PPHONE", ;
		"VK8_MPHONE", "VK8_FAXNUM", "VK8_MAEND1", "VK8_MAEND2", "VK8_MAEND3", "VK8_MACIDA", ;
		"VK8_MAESTA", "VK8_MAPAIS", "VK8_MACEP", "VK8_PAEND1", "VK8_PAEND2", "VK8_PAEND3", "VK8_PACIDA", ;
		"VK8_PAESTA", "VK8_PAPAIS", "VK8_PACEP", "VK8_BBINTG", "VK8_BBSYNC", "VK8_BBDINC", "VK8_BBDALT" })

	::AddFields({;
		"VK7.VK7_REFID", "VK7.VK7_BBINTG", "VK7.VK7_BBDEL", "VK7.VK7_BBSYNC", "VK7.R_E_C_N_O_ as VK7_RECNO" })

	::HasOne('OFSoCustomer' , { { { || xFilial('VK7') }, 'VK7_FILIAL' }, { 'VK7_CODIGO' , 'VK8_CODVK7' } } )
	::SetPKField("VK8_REFID")
	//if lDebug
	//	Conout(" ")
	//	Conout( "OFSoContact:NEW " + Str(Procline(1),5) + " - " + ProcName(1) )
	//	Conout(" ")
	//endif
	// ::cFieldFil := "U5_FILIAL" // wrong
	::aFldMap := {}
	aadd(::aFldMap, { "referenceId", "VK8_REFID" })
	aadd(::aFldMap, { "customerReferenceId", "VK7.VK7_REFID" })
	// aadd(::aFldMap, { "vendorReferenceId", xxxxxxxxxxxxx })
	// aadd(::aFldMap, { "sourceSystem", xxxxxxxxxxxxx })
	aadd(::aFldMap, { "isPrimaryForCustomer", "VK8_PRICUS" })
	aadd(::aFldMap, { "isPrimaryForVendor", "VK8_PRIVEN" })
	aadd(::aFldMap, { "firstName", "VK8_FSNAME" })
	aadd(::aFldMap, { "lastName", "VK8_LSNAME" })
	// aadd(::aFldMap, { "familiarName", "VK8_FMNAME" })
	aadd(::aFldMap, { "email", "VK8_EMAIL" })
	// aadd(::aFldMap, { "email2", "VK8_EMAIL2" })
	// aadd(::aFldMap, { "email3", "VK8_EMAIL3" })
	aadd(::aFldMap, { "primaryPhone", "VK8_PPHONE" })
	aadd(::aFldMap, { "homePhone", "VK8_HPHONE" })
	aadd(::aFldMap, { "mobilePhone", "VK8_MPHONE" })
	aadd(::aFldMap, { "faxNumber", "VK8_FAXNUM" })
	// aadd(::aFldMap, { "salutation", "VK8_SALUTA" })
	// aadd(::aFldMap, { "suffix", "VK8_SUFFIX" })
	// aadd(::aFldMap, { "generation", "VK8_GENERA" })

	aadd(::aFldMap, { "mailingAddress.line1", "VK8_MAEND1" }) // "mailingAddress.line1": "1 Elm Street",
	aadd(::aFldMap, { "mailingAddress.line2", "VK8_MAEND2" }) // "mailingAddress.line2": "1 Elm Street",
	aadd(::aFldMap, { "mailingAddress.line3", "VK8_MAEND3" }) // "mailingAddress.line3": "1 Elm Street",
	aadd(::aFldMap, { "mailingAddress.city", "VK8_MACIDA" }) // "mailingAddress.city": "Moline",
	aadd(::aFldMap, { "mailingAddress.stateOrProvince", "VK8_MAESTA" }) // "mailingAddress.stateOrProvince": "IA",
	aadd(::aFldMap, { "mailingAddress.postalCode", "VK8_MACEP" }) // "mailingAddress.postalCode": "11223",
	aadd(::aFldMap, { "mailingAddress.country", "VK8_MAPAIS" }) // "mailingAddress.country": "USA"

	aadd(::aFldMap, { "physicalAddress.line1", "VK8_PAEND1"}) // "1 Elm Street",
	aadd(::aFldMap, { "physicalAddress.line2", "VK8_PAEND2" }) // "physicalAddress.line2": "1 Elm Street",
	aadd(::aFldMap, { "physicalAddress.line3", "VK8_PAEND3" }) // "physicalAddress.line3": "1 Elm Street",
	aadd(::aFldMap, { "physicalAddress.city", "VK8_PACIDA"}) // "Moline",
	aadd(::aFldMap, { "physicalAddress.stateOrProvince", "VK8_PAESTA"}) // "IA",
	aadd(::aFldMap, { "physicalAddress.postalCode", "VK8_PACEP"}) // "11223",
	aadd(::aFldMap, { "physicalAddress.country", "VK8_PAPAIS"}) // "USA"
return self

/*/{Protheus.doc} FindByRecnoDel

	@type method
	@author Rubens Takahashi
	@since 02/07/2021
/*/
Method FindByRecnoDel(nRecno) Class OFSoContact
	self:Select();
		:_From(self:cAlias);
		:AndEq('R_E_C_N_O_', nRecno)

	// busca primeiro registro
	oReg := self:First()

	// limpa a query interna
	self:Clear()
Return oReg

/*/{Protheus.doc} GetRefId
	Retorna o reference id para usar no SO

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
Method GetRefId() Class OFSoContact
//	local cIdWhithSpaces := self:Get("A1_FILIAL") + self:Get("A1_COD") + self:Get("A1_LOJA")
//Return STRTRAN(cIdWhithSpaces, " ", "#")
Return self:Get("VK8_REFID")

/*/{Protheus.doc} ToJson
	retorna um json object

	@type method
	@author Rubens Takahashi
	@since 02/07/2021
/*/
Method ToJson() Class OFSoContact
Return self:ToJsonApi()

/*/{Protheus.doc} ToJsonApi
	Retorna um JSON para enviar a API

	@type function
	@author Rubens Takahashi
	@since 13/01/2022
/*/
Method ToJsonApi() Class OFSoContact
	local jJson := JsonObject():New()
	local jMailAddress := JsonObject():New()
	local jPhysicalAddress := JsonObject():New()

	self:enforceCustomerIsCreated(self:Get("VK7_RECNO"))

	if ! empty(self:Get("VK7.VK7_REFID"))
		jJson["customerReferenceId"] := FGX_SetVlJson(OFXFA0084_WithPrefix("CS-", self:Get("VK7.VK7_REFID")), .f.) //A unique ID to identify the customer to which the contact is related.
	else
		jJson["customerReferenceId"] := nil
	endif
	jJson["referenceId"] := "CT-" + self:Get("VK8_REFID")
	jJson["email"] := FGX_SetVlJson(self:Get("VK8_EMAIL"), .f.)
	jJson["email2"] := NIL
	jJson["email3"] := NIL
	jJson["familiarName"] := NIL
	jJson["faxNumber"] := FGX_SetVlJson(self:get("VK8_FAXNUM"), .f.)
	jJson["firstName"] := self:Get("VK8_FSNAME")
	jJson["generation"] := NIL
	jJson["homePhone"] := FGX_SetVlJson(self:get("VK8_HPHONE"), .f.)
	jJson["isPrimaryForCustomer"] := iif( self:Get("VK8_PRICUS") == "1" , "true" , "false" ) // An indicator that this contact is the primary contact for the related customer.
	jJson["isPrimaryForVendor"] := iif( self:Get("VK8_PRIVEN") == "1" , "true" , "false" ) // An indicator that this contact is the primary contact for the related vendor.
	jJson["lastName"] := self:Get("VK8_LSNAME")
	jJson["mailingAddress"] := jMailAddress
	jJson["middleName"] := NIL
	// jJson["email2"] := FGX_SetVlJson(self:Get("VK8_EMAIL2"), .f.)
	// jJson["email3"] := FGX_SetVlJson(self:Get("VK8_EMAIL3"), .f.)
	jJson["mobilePhone"] := FGX_SetVlJson(self:get("VK8_MPHONE"), .f.)
	jJson["physicalAddress"] := jPhysicalAddress
	jJson["primaryPhone"] := FGX_SetVlJson(self:get("VK8_PPHONE"), .f.)
	jJson["homePhone"] := FGX_SetVlJson(self:get("VK8_HPHONE"), .f.)
	jJson["mobilePhone"] := FGX_SetVlJson(self:get("VK8_MPHONE"), .f.)
	jJson["faxNumber"] := FGX_SetVlJson(self:get("VK8_FAXNUM"), .f.)
	jJson["referenceId"] := FGX_SetVlJson("CT-" + self:Get("VK8_REFID"), .f.)
	jJson["salutation"] := NIL
	jJson["sourceSystem"] := "DBS" // The name/description of the source system.
	jJson["suffix"] := NIL
	jJson["vendorReferenceId"] := NIL //A unique ID to identify the vendor to which the contact is related.

	jMailAddress["city"]            := FGX_SetVlJson( self:get("VK8_MACIDA") , .f. )
	jMailAddress["country"]         := FGX_SetVlJson( self:Get("VK8_MAPAIS") , .f. )
	jMailAddress["county"]          := NIL // O condado do endereço.
	jMailAddress["line1"]           := FGX_SetVlJson( self:get("VK8_MAEND1") , .f. )
	jMailAddress["line2"]           := FGX_SetVlJson( self:get("VK8_MAEND2") , .f. )
	jMailAddress["line3"]           := FGX_SetVlJson( self:get("VK8_MAEND3") , .f. )
	jMailAddress["postalCode"]      := FGX_SetVlJson( self:get("VK8_MACEP") , .f. )
	jMailAddress["stateOrProvince"] := FGX_SetVlJson( self:get("VK8_MAESTA") , .f. )

	jPhysicalAddress["city"]            := FGX_SetVlJson( self:get("VK8_PACIDA") , .f. )
	jPhysicalAddress["country"]         := FGX_SetVlJson( self:Get("VK8_PAPAIS") , .f. )
	jPhysicalAddress["county"]          := NIL // O condado do endereço.
	jPhysicalAddress["line1"]           := FGX_SetVlJson( self:get("VK8_PAEND1") , .f. )
	jPhysicalAddress["line2"]           := FGX_SetVlJson( self:get("VK8_PAEND2") , .f. )
	jPhysicalAddress["line3"]           := FGX_SetVlJson( self:get("VK8_PAEND3") , .f. )
	jPhysicalAddress["postalCode"]      := FGX_SetVlJson( self:get("VK8_PACEP") , .f. )
	jPhysicalAddress["stateOrProvince"] := FGX_SetVlJson( self:get("VK8_PAESTA") , .f. )

	//if lDebug
	//	
	//	Conout( "-----------------------------------------------------------------------")
	//	Conout( "OFSoContact toJsonApi " + Str(Procline(1),5) + " - " + ProcName(1))
	//
	//	nCont := 2
	//	While !Empty(ProcName(nCont))
	//		Conout( "                       " + Str(Procline(nCont),5) + " - " + ProcName(nCont) )
	//		nCont++
	//	EndDo
	//	Conout( "-----------------------------------------------------------------------")
	//	Conout(" ")
	//
	//endif

Return jJson

/*/{Protheus.doc} Fill
	preenche o objeto com valores vindos da request

	@type method
	@author Rubens Takahashi
	@since 01/07/2021
/*/
Method Fill(jData) Class OFSoContact
	// local oArrHlp := DMS_ArrayHelper():New()
	// local cRefId
	local nX := 1
	local cFld
	DBSelectArea("VK8")

	for nX := 1 to len(self:aFldMap)
		cFrom := self:aFldMap[nX, 1]
		cFld := self:aFldMap[nX, 2]
		If "." $ cFrom
			aDados := Strtokarr2(cFrom, ".", .f.)
			jData2 := jData[aDados[1]]
			if jData2 != nil
				self:Set(cFld, jData2[aDados[2]])
			endif
		Else
			self:Set(cFld, jData[cFrom])
		EndIf
	next

	self:Set("VB8_LSNAME", OFXFA0074_SafeConcat({jData["middleName"], self:Get("VB8_LSNAME")}))
Return .t.

/*/{Protheus.doc} Save
	Salva o registro

	@type method
	@author Rubens Takahashi
	@since 01/07/2021
/*/
Method Save() Class OFSoContact
	local nX := 1
	Local oModel := FWLoadModel("OFIA401")
	local lDeuCerto := .f.

	VK8->(dbGoTo(self:nRecno))

	If self:Persistido()
		oModel:setOperation(MODEL_OPERATION_UPDATE)
	else		
		oModel:setOperation(MODEL_OPERATION_INSERT)
	EndIf
	if oModel:Activate()
		for nX := 1 to len(self:aFldMap)
			// cFrom := self:aFldMap[nX, 1]
			cFld := self:aFldMap[nX, 2]
			if left(cFld, 4) == "VK8_"
				if self:Get(cFld) == nil
					self:Set(cFld, criavar(cFld))
				endif
				oModel:SetValue("MODEL_VK8", cFld, self:Get(cFld)) // Marca como nao integrado para enviar a JD
			endif
		next
		If oModel:VldData()
			lVEEvSO := .t.
			lDeuCerto := oModel:CommitData()
			lVEEvSO := .f.
		endif

		if ! lDeuCerto
			aErro := oModel:GetErrorMessage()
			oErro := JsonObject():New()
			oErro["form_id"] :=  cValToChar(aErro[01])
			oErro["field_id"] :=  cValToChar(aErro[02])
			oErro["form_error_id"] :=  cValToChar(aErro[03])
			oErro["field_error_id"] :=  cValToChar(aErro[04])
			oErro["error_id"] :=  cValToChar(aErro[05])
			oErro["error_message"] :=  cValToChar(aErro[06])
			oErro["solution_message"] :=  cValToChar(aErro[07])
			oErro["value_to"] :=  cValToChar(aErro[08])
			oErro["before_from"] :=  cValToChar(aErro[09])
			self:addError("self", oErro:toJson())
		endif
		oModel:DeActivate()
	EndIf
Return lDeuCerto

/*/{Protheus.doc} enforceCustomerIsCreated
	assegura que o cliente foi enviado ao SO

	@type method
	@author Renato Cruz
	@since 01/07/2021
/*/
Method enforceCustomerIsCreated(nRecNo) Class OFSoContact
	Local oModel
	DbSelectArea("VK7")
	DbGoto(nRecNo)
	if VK7->VK7_BBDEL == "1"
		oModel := FWLoadModel("OFIA402")
		oModel:setOperation(MODEL_OPERATION_UPDATE)
		if oModel:Activate()
			oModel:SetValue("MODEL_VK7", "VK7_BBINTG" , "0") // Marca como nao integrado para enviar a JD
			oModel:SetValue("MODEL_VK7", "VK7_BBSYNC" , "0") // Marca como nao sincronizado para enviar a JD
			oModel:SetValue("MODEL_VK7", "VK7_BBDEL"  , "0") // Desmarca como deletado
			If oModel:VldData()
				oModel:CommitData()
			endif
			oModel:DeActivate()
		EndIf
	endif
Return
