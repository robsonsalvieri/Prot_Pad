#include 'totvs.ch'

/*/{Protheus.doc} OFDMSMessage
	Classe que grava tabela VK6, um esquema de service broker porém com necessidades
	menos exigentes.
	
	@type class
	@author Vinicius Gati
	@since 19/10/2021
/*/
Class OFDMSMessage from VERegistroSql
	Public Method New()
	Public Method BeforeCreate()
	Public Method SetRecebida()
	Public Method SetAtrasada()
	Public Method SetTransmitida()
	Public Method SetProcessada()
	Public Method SetFinished()
	Public Method isEntrance()
	Public Method WithInitialStatus()
	Public Method GetPayload()
	Public Method GetDMSRequest()
	Public Method Process()
EndClass

/*/{Protheus.doc} WithInitialStatus
	Faz um IN com os status de recém entrada na tabela, ou seja, com 
	processamento ou envio pendentes dependendo o tipo do registro.

	@type method
	@author Vinicius Gati
	@since 20/10/2021
/*/
Method WithInitialStatus() Class OFDMSMessage
Return self:AndIn("VK6_STATUS", "'0','1'")

/*/{Protheus.doc} New
	Construtor Simples

	@type method
	@author Vinicius Gati
	@since 19/10/2021
/*/
Method New() Class OFDMSMessage
	_Super:New('VK6')
	::AddFields(FGX_CtoArr("VK6_FILIAL VK6_UUID VK6_CODVK5 VK6_MSGNAM VK6_PAYLOA VK6_TIPO VK6_STATUS VK6_DATALT VK6_DATINC"))
	::PermiteAssign(FGX_CtoArr("VK6_CODVK5 VK6_MSGNAM VK6_PAYLOA VK6_TIPO VK6_STATUS"))
Return SELF

/*/{Protheus.doc} BeforeCreate
	antes de criar novo registro

	@type method
	@author Vinicius Gati
	@since 19/10/2021
/*/
Method BeforeCreate() Class OFDMSMessage
	self:Set("VK6_FILIAL", xFilial("VK6"))
	self:Set("VK6_UUID", FwUUIDV4(.t.))
Return _Super:BeforeCreate()

/*/{Protheus.doc} SetRecebida

	@type method
	@author Vinicius Gati
	@since 20/10/2021
/*/
Method SetRecebida() Class OFDMSMessage
	self:Set("VK6_STATUS", "0")
Return

/*/{Protheus.doc} SetAtrasada

	@type method
	@author Vinicius Gati
	@since 20/10/2021
/*/
Method SetAtrasada() Class OFDMSMessage
	self:Set("VK6_STATUS", "1")
Return

/*/{Protheus.doc} SetTransmitida

	@type method
	@author Vinicius Gati
	@since 20/10/2021
/*/
Method SetTransmitida() Class OFDMSMessage
	self:Set("VK6_STATUS", "2")
Return

/*/{Protheus.doc} SetProcessada

	@type method
	@author Vinicius Gati
	@since 20/10/2021
/*/
Method SetProcessada() Class OFDMSMessage
	self:Set("VK6_STATUS", "3")
Return


/*/{Protheus.doc} Process
	Processa a mensagem

	@type method
	@author Vinicius Gati
	@since 20/10/2021
/*/
Method Process() Class OFDMSMessage
	if alltrim(self:Get("VK6_MSGNAM")) == OFDMSMessageConstants():CustomerCreatedInDbsMessageName()
		oCom := OFSoCommunication():New()
		jPayload := self:GetPayload()
//		jRet := oCom:Send(jPayload:Get("header"), jPayload:Get("body"), self:GetDMSRequest())
//		if jRet["code"] > 0
//			return .t.
//		endif
	endif
Return .f.

/*/{Protheus.doc} GetDMSRequest


	@type method
	@author Vinicius Gati
	@since 20/10/2021
/*/
Method GetDMSRequest() Class OFDMSMessage
	local cId := self:Get("VK6_CODVK5")
	local oRequest
	if ! empty(cId)
		oRequest := OFDMSRequest():New()
		oRequest := oRequest:Find(cId)
	endif
Return oRequest

/*/{Protheus.doc} GetPayload


	@type method
	@author Vinicius Gati
	@since 20/10/2021
/*/
Method GetPayload() Class OFDMSMessage
	local jPayload := JsonObject():New()
	jPayload:fromJson(self:Get("VK6_PAYLOA"))
Return jPayload

/*/{Protheus.doc} SetFinished
	Seta o status final dependendo do atual e do tipo

	@type method
	@author Vinicius Gati
	@since 20/10/2021
/*/
Method SetFinished() Class OFDMSMessage
	if self:isEntrance()
		self:SetProcessada()
	else
		self:SetTransmitida()
	endif
Return 

/*/{Protheus.doc} isEntrance
	Verifica se o tipo é de entrada

	@type method
	@author Vinicius Gati
	@since 20/10/2021
/*/
Method isEntrance() Class OFDMSMessage
Return self:Get("VK6_TIPO") == "0" // vai dar exception se não setar o valor e eu acho que tem que dar mesmo
