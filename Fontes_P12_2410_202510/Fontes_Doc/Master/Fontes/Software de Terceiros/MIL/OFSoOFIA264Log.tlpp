#include 'totvs.ch'

/*/{Protheus.doc} OFSoOFIA264Log
	Classe para armazenar e recuperar log de execução da rotina
	
	@type class
	@author Vinicius Gati
	@since 28/10/2022
/*/
Class OFSoOFIA264Log
	data cCodigo
	Public Method New() CONSTRUCTOR
	Public Method Grava()
	Public Method Get()
EndClass

/*/{Protheus.doc} New
	Construtor Simples

	@type method
	@author Vinicius Gati
	@since 28/10/2022
/*/
Method New() Class OFSoOFIA264Log
	::cCodigo := "OFIA264LOG"
Return SELF

/*/{Protheus.doc} Grava
	grava os logs

	@type method
	@author Vinicius Gati
	@since 28/10/2022
/*/
Method Grava(oLog, oLogDet) Class OFSoOFIA264Log
	oJ := JsonObject():New()
	oJ["log"] := oLog:ToJson()
	oJ["log_det"] := oLogDet:ToJson()
		
	VRN->(dbSetOrder(1))
	if VRN->(dbSeek(xFilial("VRN") + self:cCodigo))
		reclock("VRN", .F.)
		VRN->VRN_CONFIG := oJ:ToJson()
		VRN->(MsUnlock())
	else
		reclock("VRN", .T.)
		VRN->VRN_FILIAL := xFilial("VRN")
		VRN->VRN_CONFIG := oJ:ToJson()
		VRN->VRN_CODIGO := self:cCodigo
		VRN->(MsUnlock())
	endif
Return .t.

/*/{Protheus.doc} Get
	busca os logs

	@type method
	@author Vinicius Gati
	@since 28/10/2022
/*/
Method Get() Class OFSoOFIA264Log
	local oConfig
	VRN->(dbSetOrder(1))
	if VRN->(dbSeek(xFilial("VRN") + self:cCodigo))
		oConfig := JsonObject():New()
		oConfig:FromJson(VRN->VRN_CONFIG)
		return {OFSoLogTextoDinamico():New(nil, oConfig["log"]), OFSoLogTextoDinamico():New(nil, oConfig["log_det"])}
	endif
return {OFSoLogTextoDinamico():New(), OFSoLogTextoDinamico():New()}
