#include 'totvs.ch'

/*/{Protheus.doc} OFSoModelService
	Classe principal de controle do PRIM
	
	@type class
	@author Vinicius Gati
	@since 29/09/2022
/*/
Class OFSoModelService
	Public Method New() CONSTRUCTOR
	Public Method Process()
EndClass

/*/{Protheus.doc} New
	Construtor Simples

	@type method
	@author Vinicius Gati
	@since 29/09/2022
/*/
Method New() Class OFSoModelService
Return SELF

/*/{Protheus.doc} Process
	Metodo que processa as requisicoes vindas do Service Operations

	@type method
	@author Vinicius Gati
	@since 29/09/2022
/*/
Method Process(aQueryParams as Array, cBody as Character, cGatewayHeader as Character) Class OFSoModelService
	local jGatewayHeader := JsonObject():New()
	local cParseHead := jGatewayHeader:fromJson(cGatewayHeader)
	Local oSoRequest := OFSoRequestBody():New(cBody)
	local oInteractor := nil

	if ! empty(cParseHead)
		return OFSoInvalidGatewayHeaderResponse():New()
	endif

	if ! oSoRequest:IsValid()
		return OFSoInvalidBodyResponse():New()
	endif

	if oSoRequest:GetName() == "EquipmentModelCreatedInFieldService"
		oInteractor := OFSoEquipmentModelCreatedInFieldServiceInteractor():New()
		oResp := oInteractor:Process(oSoRequest, jGatewayHeader)
		return oResp
	endif

	if oSoRequest:GetName() == "EquipmentModelUpdatedInFieldService"
		oInteractor := OFSoEquipmentModelUpdatedInFieldServiceInteractor():New()
		oResp := oInteractor:Process(oSoRequest, jGatewayHeader)
		return oResp
	endif
Return OFSoInvalidBodyResponse():New()

