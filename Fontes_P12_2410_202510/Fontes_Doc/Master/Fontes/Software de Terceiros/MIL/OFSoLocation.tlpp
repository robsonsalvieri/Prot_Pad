#include 'totvs.ch'

/*/{Protheus.doc} OFSoLocation
	Classe que representará o Location para o SO
	
	@type class
	@author Rubens Takahashi
	@since 01/12/2023
/*/
Class OFSoLocation
	Public data cCodigoEmpresa
	Public data cCodigoFilial

	Public Method New() Constructor
	Public Method toJson()
	Public Method GetId()
	Public Method GetReferenceId()
EndClass

/*/{Protheus.doc} New
	Construtor

	@type method
	@author Rubens Takalashi
	@since 13/08/2021
/*/
Method New(cCodigoEmpresa, cCodigoFilial) Class OFSoLocation
	::cCodigoEmpresa := cCodigoEmpresa
	::cCodigoFilial := cCodigoFilial
Return SELF

/*/{Protheus.doc} GetId
	retorna o id unico do location que no caso é composto da empresa mais filial

	@type method
	@author Vinicius Gati
	@since 13/08/2021
/*/
Method GetId() Class OFSoLocation
Return ::cCodigoEmpresa + ::cCodigoFilial

/*/{Protheus.doc} GetReferenceId
	Descricao

	@type method
	@author Vinicius Gati
	@since 16/08/2021
/*/
Method GetReferenceId() Class OFSoLocation
Return ::cCodigoEmpresa + "-" + ::cCodigoFilial

Method toJson() Class OFSoLocation
	Local cBkpEmpAnt := cEmpAnt
	Local cBkpFilAnt := cFilAnt

	Local jJson := JsonObject():New()
	Local jMailingAddress := JsonObject():New()
	Local jphysicalAddress := JsonObject():New()

	Local oConfig

	Default cCodEmpresa := cEmpAnt
	Default cCodFilial  := cFilAnt

	SM0->(dbSetOrder(1))
	SM0->(dbSeek( self:cCodigoEmpresa + self:cCodigoFilial ))

	cFilAnt := self:cCodigoFilial

	oConfig := OFSoConfig():New()
	oConfig := oConfig:GetConfig()

	jMailingAddress["city"]            := FGX_SetVlJson(SM0->M0_CIDCOB, .f. )
	jMailingAddress["country"]         := "BRA"
	jMailingAddress["county"]          := NIL
	jMailingAddress["line1"]           := FGX_SetVlJson(SM0->M0_ENDCOB, .f. )
	jMailingAddress["line2"]           := FGX_SetVlJson(SM0->M0_COMPCOB, .f. )
	jMailingAddress["line3"]           := FGX_SetVlJson(SM0->M0_BAIRCOB, .f. )
	jMailingAddress["postalCode"]      := FGX_SetVlJson(SM0->M0_CEPCOB, .f. )
	jMailingAddress["stateOrProvince"] := FGX_SetVlJson(SM0->M0_ESTCOB, .f. )

	jPhysicalAddress["city"]            := FGX_SetVlJson(SM0->M0_CIDENT, .f. )
	jPhysicalAddress["country"]         := "BRA"
	jPhysicalAddress["county"]          := NIL
	jPhysicalAddress["line1"]           := FGX_SetVlJson(SM0->M0_ENDENT, .f. )
	jPhysicalAddress["line2"]           := FGX_SetVlJson(SM0->M0_COMPENT, .f. )
	jPhysicalAddress["line3"]           := FGX_SetVlJson(SM0->M0_BAIRENT, .f. )
	jPhysicalAddress["postalCode"]      := FGX_SetVlJson(SM0->M0_CEPENT, .f. )
	jPhysicalAddress["stateOrProvince"] := FGX_SetVlJson(SM0->M0_ESTENT, .f. )

	jJson["branchId"]      := AllTrim(SM0->M0_CODFIL)
	jJson["businessPhone"] := FGX_SetVlJson(SM0->M0_TEL)
	jJson["companyId"]     := cEmpAnt // Codigo do grupo de empresa
	jJson["contracts"]            := NIL
	jJson["email"]         := NIL
	jJson["mailingAddress"] := jMailingAddress
	jJson["mainBranchId"]  := oConfig["MATRIZID"] // Codigo da Filial do Protheus referente a Matriz (cFilAnt)
	jJson["name"]          := AllTrim(SM0->M0_NOMECOM)
	jJson["physicalAddress"] := jPhysicalAddress
	jJson["referenceId"]   := self:GetReferenceId()
	jJson["taxRegionReferenceId"] := NIL

	cFilAnt := cBkpFilAnt
Return jJson

