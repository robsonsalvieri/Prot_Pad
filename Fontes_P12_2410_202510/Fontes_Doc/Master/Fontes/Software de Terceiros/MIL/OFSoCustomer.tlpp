#include "tlpp-core.th"
#include "totvs.ch"

//static lDebug := .f.

/*/{Protheus.doc} OFSoCustomer
	Classe criada para retornar os dados conforme Customer Service do Service Operations

	O retorno básico é:

	{
		"referenceId": "CUST-REF-ID-001",
		"customerNumber": "CUST-DEF-001",
		"billToNumber": "CUST-ABC",
		"billToReferenceId": "CUST-BILLTO-REF-01",
		"customerType": "Business",
		"companyName": "Farmer Jim Bob Joe Billy Jackson",
		"firstName": "",
		"middleName": "",
		"lastName": "",
		"familiarName": "",
		"salutation": "",
		"suffix": "",
		"generation": "",
		"email": "jimbobjoebilly@jacksonfarm.local",
		"email2": "secondemail@jacksonfarm.local",
		"email3": "thirdemail@jacksonfarm.local",
		"primaryPhone": "8648675309",
		"otherPhone": "8648675309",
		"mobilePhone": "8648675309",
		"faxNumber": "8648675309",
		"doingBusinessAs": "Farmer JBJBJ",
		"mailingAddress": {
			"line1": "1 Elm Street",
			"city": "Moline",
			"stateOrProvince": "IA",
			"postalCode": "11223",
			"county": "Moline",
			"country": "USA"
		},
		"physicalAddress": {
			"line1": "1 Elm Street",
			"city": "Moline",
			"stateOrProvince": "IA",
			"postalCode": "11223",
			"county": "Moline",
			"country": "USA"
		},
		"poRequiredIndicator": true,
		"regulatoryDetails": nil,
		"customerKnowledgeCenterId": "ckc-id-001",
		"inactiveIndicator": "A",
		"inactiveReason": "N/A",
		"inactiveDate": "2020-01-01T00:00:00Z",
		"customerGroup": "Alpha Group",
		"currencyCode": "USD",
		"rate": 19.24,
		"rateEffectiveDate": "2019-01-01T00:00:00Z",
		"rateExpirationDate": "2019-12-31T00:00:00Z",
		"franchiseReferenceId": "FR-REF-01"
	}

	Conforme link de documentação em: https://jdbbmockstordata001.blob.core.windows.net/$web/bb-api-docs/Customer/latest/OpenApi.html#operation/CustomerCreatedInDbs

	@type function
	@author Vinicius Gati
	@since 22/01/2020
/*/
class OFSoCustomer from VERegistroSql
	Public Method New()
	Public Method ToJsonApi()
	Public Method ToJson()
	Public Method GetRefId()
	Public Method Fill()
	Public Method FindByRecnoDel()
	Public Method BeforeCreate()
	Public Method SendToSA1()
	Protected Method SplitName()
end class

/*/{Protheus.doc} New
	Construtor basico

	@type function
	@author Vinicius Gati
	@since 30/06/2021
/*/
method New() class OFSoCustomer
	_Super:New('VK7')
	::AddFields({;
		"R_E_C_N_O_", "VK7_CODIGO", "VK7_GROUP", "VK7_CODVK7", "VK7_REFID", "VK7_CNUMB", "VK7_BTNUMB", "VK7_BTREID",;
		"VK7_TYPE", "VK7_DOBUAS", "VK7_CPNAME", "VK7_FSNAME", "VK7_LSNAME",;
		"VK7_EMAIL", "VK7_PPHONE", "VK7_FAXNUM", "VK7_CKCID",;
		"VK7_STATUS", "VK7_STAMOT", "VK7_A1FIL",;
		"VK7_A1COD", "VK7_A1LOJA", "VK7_MAEND1", "VK7_MAEND2", "VK7_MAEND3", "VK7_MACIDA", "VK7_MAESTA", "VK7_MAPAIS",;
		"VK7_MACEP", "VK7_PAEND1", "VK7_PAEND2", "VK7_PAEND3", "VK7_PACIDA", "VK7_PAESTA", "VK7_PAPAIS", "VK7_PACEP",; // "VK7_GENERA", "VK7_EMAIL2", "VK7_EMAIL3", "VK7_OPHONE", "VK7_MPHONE", "VK7_FMNAME", "VK7_SALUTA", "VK7_SUFFIX", "VK7_RATE", "VK7_FRARID",
		"VK7_BBINTG", "VK7_BBSYNC", "VK7_BBDEL", "VK7_BBDINC", "VK7_BBDALT", "VK7_CGC", "VK7_INSCR", "VK7_ACCLAS";
	})
	::AddFieldsData({"VK7_STADAT"/*, "VK7_REFDAT", "VK7_REXDAT"*/ })
	::SetPKField("VK7_REFID")

	// ::AddFields({"VJP.VJP_ID AS VJP_ID", "VJP.R_E_C_N_O_ AS VJP_RECNO" })
	// ::LeftJoin("VJP", "VJP", "VJP_FILIAL='" + xFilial("VJP") + "' AND VJP_A1COD = VK7_A1COD AND VJP_A1LOJA = VK7_A1LOJA AND VJP.D_E_L_E_T_ = ' '")

	//if lDebug
	//	Conout(" ")
	//	Conout( "OFSoCustomer:NEW " + Str(Procline(1),5) + " - " + ProcName(1) )
	//	Conout(" ")
	//endif
	
return self

/*/{Protheus.doc} BeforeCreate
	Descricao

	@type method
	@author Vinicius Gati
	@since 08/08/2022
/*/
Method BeforeCreate() Class OFSoCustomer
	self:Set("VK7_CODIGO", criavar("VK7_CODIGO"))
	self:Set("VK7_FILIAL", criavar("VK7_FILIAL"))
	self:Set("VK7_REFID", criavar('VK7_REFID') )
	confirmSX8()
Return _Super:BeforeCreate()

/*/{Protheus.doc} Find
	Busca o cliente via recno sem filtrar deletados

	@type method
	@author Rubens Takahashi
	@since 02/07/2021
/*/
Method FindByRecnoDel(nRecno) Class OFSoCustomer
	self:Select();
		:_From(self:cAlias);
		:AndEq('R_E_C_N_O_', nRecno)

	// busca primeiro registro
	oReg := self:First()

	// limpa a query interna
	self:Clear()
Return oReg

/*/{Protheus.doc} GetRefId
	Retorna o reference id para usar no SO

	@type function
	@author Vinicius Gati
	@since 30/06/2021
/*/
Method GetRefId() Class OFSoCustomer
Return self:Get("VK7_REFID")

/*/{Protheus.doc} ToJson
	retorna um json object

	@type method
	@author Vinicius Gati
	@since 02/07/2021
/*/
Method ToJson() Class OFSoCustomer
Return self:ToJsonApi()

/*/{Protheus.doc} ToJsonApi
	Retorna um JSON para enviar a API

	@type function
	@author Vinicius Gati
	@since 30/06/2021
/*/
Method ToJsonApi() Class OFSoCustomer
	local jJson := JsonObject():New()
	local jMailAddress := JsonObject():New()
	local jPhysicalAddress := JsonObject():New()

	jJson["billToNumber"] := FGX_SetVlJson(self:Get("VK7_BTNUMB") , .f.) // The A/R account number for the related bill to/invoice customer. If the customer is their own bill to customer then this value may match the customerNumber field or be blank.
	
	if ! empty(self:Get("VK7_BTREID"))
		jJson["billToReferenceId"] := FGX_SetVlJson(OFXFA0084_WithPrefix("CS-", self:Get("VK7_BTREID"))) // The unique reference ID for the A/R account that is the related bill to/invoice customer. If the customer is their own bill to customer then this value may match the referenceId field or be blank.
	else
		jJson["billToReferenceId"] := nil
	endif

	If self:Get("VK7_TYPE") == "2" // Pessoa Juridica
		jJson["companyName"]  := FGX_SetVlJson( self:Get("VK7_CPNAME") , .f. )
	EndIf

	If self:Get("VK7_TYPE") == "1" // Pessoa Fisica
		jJson["companyName"]  := NIL
	EndIf

	jJson["currencyCode"] := "BRL"
	jJson["customerGroup"] := NIL
	jJson["customerKnowledgeCenterId"] := iif( ! empty( self:Get("VK7_CKCID") ), cValToChar( self:Get("VK7_CKCID") ), NIL ) // "ckc-id-001" The customer's knowledge center ID. CKC number

	// The A/R account number for the customer.
	jJson["customerNumber"] := self:Get("VK7_CNUMB")

	If self:Get("VK7_TYPE") == "2" // Pessoa Juridica
		jJson["customerType"] := "Business"
		jJson["companyName"]  := FGX_SetVlJson(self:Get("VK7_CPNAME") , .f.)
		jJson["doingBusinessAs"] := FGX_SetVlJson(self:Get("VK7_DOBUAS") , .f.)
		jJson["firstName"]    := NIL
		jJson["lastName"]     := NIL
	Endif

	If self:Get("VK7_TYPE") == "1" // Pessoa Fisica
		jJson["customerType"] := "Individual"
		jJson["doingBusinessAs"] := NIL
		jJson["firstName"]    := FGX_SetVlJson(self:Get("VK7_FSNAME") , .f.)
		jJson["lastName"]     := FGX_SetVlJson(self:Get("VK7_LSNAME") , .f.)
	EndIf
	jJson["familiarName"] := NIL // FGX_SetVlJson(self:Get("VK7_FMNAME"))
	jJson["middleName"]   := ""

	jJson["email"]        := FGX_SetVlJson( self:Get("VK7_EMAIL") , .f. )
	jJson["email2"]       := NIL
	jJson["email3"]       := NIL

	If self:Get("VK7_TYPE") == "2" // Pessoa Juridica
		jJson["familiarName"] := NIL
	EndIf

	If self:Get("VK7_TYPE") == "1" // Pessoa Fisica
		jJson["familiarName"] := FGX_SetVlJson( self:Get("VK7_FSNAME") , .f. )
	EndIf

	jJson["faxNumber"]    := FGX_SetVlJson( self:Get("VK7_FAXNUM") , .f. )

	If self:Get("VK7_TYPE") == "2" // Pessoa Juridica
		jJson["firstName"]    := NIL
	EndIf

	If self:Get("VK7_TYPE") == "1" // Pessoa Fisica
		jJson["firstName"]    := FGX_SetVlJson( self:Get("VK7_FSNAME") , .f. )
	EndIf

	jJson["franchiseReferenceId"] := nil // The franchise for the customer.
	jJson["generation"]   := NIL
	jJson["inactiveDate"] := FGX_SetVlJson( iif( ! empty(self:Get("VK7_STADAT")) , left(self:Get("VK7_STADAT"),4) + "-" + substr(self:Get("VK7_STADAT"),5,2) + "-" + right(self:Get("VK7_STADAT"),2) + "T" + Time() + "Z", "" ) , .f. ) // The date the customer was made inactive. "2020-01-01T00:00:00Z"
	jJson["inactiveIndicator"] := iif( self:Get("VK7_STATUS") == "2" , "A" , "I" ) //  I = Inactive, A = Active
	jJson["inactiveReason"] := FGX_SetVlJson( self:Get("VK7_STAMOT") , .f. ) // The reason the customer is inactive.

	If self:Get("VK7_TYPE") == "1" // Pessoa Fisica
		jJson["lastName"]     := FGX_SetVlJson( self:Get("VK7_LSNAME") , .f. )
	Endif

	If self:Get("VK7_TYPE") == "2" // Pessoa Juridica
		jJson["lastName"]     := NIL
	EndIf

	jMailAddress["city"]            := FGX_SetVlJson( self:get("VK7_MACIDA") , .f. )
	jMailAddress["country"]         := FGX_SetVlJson( self:Get("VK7_MAPAIS") , .f. )
	jMailAddress["county"]          := NIL // O condado do endereço.
	jMailAddress["line1"]           := FGX_SetVlJson( self:get("VK7_MAEND1") , .f. )
	jMailAddress["line2"]           := FGX_SetVlJson( self:get("VK7_MAEND2") , .f. )
	jMailAddress["line3"]           := FGX_SetVlJson( self:get("VK7_MAEND3") , .f. )
	jMailAddress["postalCode"]      := FGX_SetVlJson( self:get("VK7_MACEP") , .f. )
	jMailAddress["stateOrProvince"] := FGX_SetVlJson( self:get("VK7_MAESTA") , .f. )
	jJson["mailingAddress"] := jMailAddress

	jJson["middleName"]   := NIL
	jJson["mobilePhone"]  := NIL // FGX_SetVlJson( self:Get("VK7_MPHONE") , .f. )
	jJson["otherPhone"]   := NIL // FGX_SetVlJson( self:Get("VK7_OPHONE") , .f. )

	jPhysicalAddress["city"]            := FGX_SetVlJson( self:get("VK7_PACIDA") , .f. )
	jPhysicalAddress["country"]         := FGX_SetVlJson( self:Get("VK7_PAPAIS") , .f. )
	jPhysicalAddress["county"]          := NIL // O condado do endereço.
	jPhysicalAddress["line1"]           := FGX_SetVlJson( self:get("VK7_PAEND1") , .f. )
	jPhysicalAddress["line2"]           := FGX_SetVlJson( self:get("VK7_PAEND2") , .f. )
	jPhysicalAddress["line3"]           := FGX_SetVlJson( self:get("VK7_PAEND3") , .f. )
	jPhysicalAddress["postalCode"]      := FGX_SetVlJson( self:get("VK7_PACEP") , .f. )
	jPhysicalAddress["stateOrProvince"] := FGX_SetVlJson( self:get("VK7_PAESTA") , .f. )
	jJson["physicalAddress"] := jPhysicalAddress

	// An indicator that a PO is required by the customer.
	jJson["poRequiredIndicator"] := NIL

	jJson["primaryPhone"] := FGX_SetVlJson( self:Get("VK7_PPHONE") , .f. )
	jJson["rate"] := NIL // The current default rate for the customer.
	jJson["rateEffectiveDate"] := NIL // The effective date of teh current customer rate. "2019-01-01T00:00:00Z"
	jJson["rateExpirationDate"] := NIL // The expiration date of the current customer rate.
	jJson["referenceId"] := OFXFA0084_WithPrefix("CS-", self:Get("VK7_REFID"))
	jJson["regulatoryDetails"] := nil // Additional regulatory and tax information for the customer.
	jJson["salutation"]   := NIL
	jJson["suffix"]       := NIL
	jJson["userField1"]   := FGX_SetVlJson( self:get("VK7_CGC") , .f. )
	jJson["userField2"]   := FGX_SetVlJson( self:get("VK7_INSCR") , .f. )
	jJson["accountClass"] := FGX_SetVlJson( self:get("VK7_ACCLAS") , .f. )

	//if lDebug
	//
	//	Conout( "-----------------------------------------------------------------------")
	//	Conout( "OFSoCustomer toJsonApi " + Str(Procline(1),5) + " - " + ProcName(1))
	//
	//	nCont := 2
	//	While !Empty(ProcName(nCont))
	//		Conout( "                       " + Str(Procline(nCont),5) + " - " + ProcName(nCont))
	//		nCont++
	//	EndDo
	//	Conout( "-----------------------------------------------------------------------")
	//	Conout(" ")
	//
	//endif

Return jJson

/*/{Protheus.doc} SplitName
	Pega o nome do cliente particionado e retorna como array

	@type method
	@author Rubens Takahashi
	@since 01/07/2021
/*/
Method SplitName(cFullName) class OFSoCustomer
	local cFirstName := ""
	local cMidleName := ""
	local cLastName := ""
	local nPos

	// Retira o Sobrenome
	nPos := RAT(" ",cFullName)
	If nPos <> 0
		cLastName := Right(cFullName,Len(cFullName) - nPos)
		cFullName := AllTrim(Left(cFullName,nPos))
	EndIf

	// Retira o Primeiro Nome
	nPos := AT(" ",cFullName)
	If nPos == 0 .and. Len(cFullName) <> 0
		nPos := Len(cFullName)
	EndIf
	If nPos <> 0
		cFirstName := AllTrim(Left(cFullName,nPos))
		cFullName := AllTrim(SubStr(cFullName,Len(cFirstName)+1))
	EndIf

	// Retira Nome do meio
	If !Empty(cFullName)
		cMidleName := AllTrim(cFullName)
	EndIf
Return {cFirstName, cMidleName, cLastName}

/*/{Protheus.doc} Fill
	preenche o objeto com valores vindos da request

	@type method
	@author Vinicius Gati
	@since 01/07/2021
/*/
Method Fill(jData) Class OFSoCustomer
	self:set("VK7_CNUMB", jData["customerNumber"])
	self:set("VK7_BTNUMB", jData["billToNumber"])
	self:set("VK7_BTREID", iif(left(jData["billToReferenceId"], 3) == "CS-", strtran(jData["billToReferenceId"], "CS-", "") , jData["billToReferenceId"])) // "CUST-BILLTO-REF-" + self:set("A1_COD")
	if jData["customerType"] == "Individual"
		self:set("VK7_TYPE" , "1")
	endif
	if jData["customerType"] == "Business"
		self:set("VK7_TYPE" , "2")
		self:set("VK7_CPNAME", jData["companyName"])
	Endif
	//self:set("VK7_SALUTA", jData["salutation"])
	//self:set("VK7_SUFFIX", jData["suffix"])
	//self:set("VK7_GENERA", jData["generation"])
	self:set("VK7_EMAIL" , jData["email"])
	//self:set("VK7_EMAIL2", jData["email2"])
	//self:set("VK7_EMAIL3", jData["email3"])
	self:set("VK7_PPHONE", jData["primaryPhone"])
	//self:set("VK7_OPHONE", jData["otherPhone"])
	//self:set("VK7_MPHONE", jData["mobilePhone"])
	self:set("VK7_FAXNUM", jData["faxNumber"])
	self:set("VK7_FSNAME", jData["firstName"])
	self:set("VK7_LSNAME", jData["lastName"])
	//self:set("VK7_FMNAME", jData["familiarName"])
	self:set("VK7_DOBUAS", jData["doingBusinessAs"])
	// jData["poRequiredIndicator"] := iif(valtype(jData["poRequiredIndicator"]) == "L", jData["poRequiredIndicator"], jData["poRequiredIndicator"] == 'true')
	// jData["regulatoryDetails"] := nil
	self:set("VK7_STATUS", "2")

	jMailAddress := jData["mailingAddress"]
	self:set("VK7_MAEND1", jMailAddress["line1"])
	self:set("VK7_MAEND2", jMailAddress["line2"])
	self:set("VK7_MAEND3", jMailAddress["line3"])
	self:set("VK7_MACIDA", jMailAddress["city"])
	self:set("VK7_MAESTA", jMailAddress["stateOrProvince"])
	self:set("VK7_MACEP", jMailAddress["postalCode"])
	self:set("VK7_MAPAIS", jMailAddress["country"])

	jPhysicalAddress := jData["physicalAddress"]
	self:set("VK7_PAEND1", jPhysicalAddress["line1"])
	self:set("VK7_PAEND2", jPhysicalAddress["line2"])
	self:set("VK7_PAEND3", jPhysicalAddress["line3"])
	self:set("VK7_PACIDA", jPhysicalAddress["city"])
	self:set("VK7_PAESTA", jPhysicalAddress["stateOrProvince"])
	self:set("VK7_PACEP", jPhysicalAddress["postalCode"])
	self:set("VK7_PAPAIS", jPhysicalAddress["country"])

	self:set("VK7_ACCLAS", jData["accountClass"])
	self:set("VK7_CKCID" , jData["customerKnowledgeCenterId"]) // "ckc-id-001" The customer's knowledge center ID.
Return .t.

/*/{Protheus.doc} SendToSA1
	Grava os dados do registro no SA1 via CRM980

	@type method
	@author Vinicius Gati
	@since 08/08/2022
/*/
Method SendToSA1() Class OFSoCustomer
	local oArHlp  := DMS_ArrayHelper():New()
	local oSA1Mod, oModel, nX
	local lDeuCerto := .F.
	local aFields   := {}
	local aFOrder   := OFXFA0044_SX3CampoComOrdem("SA1")
	local aOrdFlds  := {}
	local aAreaSA1 := SA1->(getArea())

	DBSelectArea("SA1")
	dbSetOrder(1)
	DbSeek(self:Get("VK7_A1FIL") + self:Get("VK7_A1COD") + self:Get("VK7_A1LOJA"))
	oModel := FWLoadModel("CRMA980")

	oModel:SetOperation(4)
	oModel:Activate()

	oSA1Mod := oModel:GetModel("SA1MASTER")

	oTel1 := FGX_ConvFoneToProtheus(self:Get("VK7_PPHONE"))
	oFax := FGX_ConvFoneToProtheus(self:Get("VK7_FAXNUM"))

	aadd(aFields, { "A1_DDD", oTel1["DDD"] })
	aadd(aFields, { "A1_TEL", oTel1["TEL"] })
	aadd(aFields, { "A1_FAX", oFax["TEL"] })

	// --- endereço fisico
	aadd(aFields, { "A1_END", self:Get("VK7_PAEND1") })
	aadd(aFields, { "A1_BAIRRO", self:Get("VK7_PAEND2") })
	aadd(aFields, { "A1_COMPLEM", self:Get("VK7_PAEND3") })
	aadd(aFields, { "A1_MUN", self:Get("VK7_PACIDA") })
	if ! empty(self:Get("VK7_PAESTA"))
		aadd(aFields, { "A1_EST", OFXFA0064_FindFirstState(self:Get("VK7_PAESTA")) })
	endif
	if ! empty(self:Get("VK7_PAPAIS"))
		aadd(aFields, { "A1_PAIS", OFXFA0054_FindFirstCountry(self:Get("VK7_PAPAIS")) })
	endif
	aadd(aFields, { "A1_CEP", self:Get("VK7_PACEP") })

	// --- endereço de correspondencia
	aadd(aFields, { "A1_ENDENT", self:Get("VK7_MAEND1") })
	aadd(aFields, { "A1_BAIRROE", self:Get("VK7_MAEND2") })
	aadd(aFields, { "A1_COMPENT", self:Get("VK7_MAEND3") })
	aadd(aFields, { "A1_MUNE", self:Get("VK7_MACIDA") })
	if ! empty(self:Get("VK7_MAESTA"))
		aadd(aFields, { "A1_ESTE", OFXFA0064_FindFirstState(self:Get("VK7_MAESTA")) })
	endif
	aadd(aFields, { "A1_CEPE", self:Get("VK7_MACEP") })
	aadd(aFields, { "A1_EMAIL", self:Get("VK7_EMAIL") })
	
	if self:get("VK7_TYPE") == "1"
		aadd(aFields, { "A1_NOME", OFXFA0074_SafeConcat({self:Get("VK7_FSNAME") , self:Get("VK7_LSNAME")})})
	else
		aadd(aFields, { "A1_NOME", self:Get("VK7_CPNAME") })
	endif

	if ! Empty(self:Get("VK7_DOBUAS"))
		aadd(aFields, { "A1_NREDUZ", self:Get("VK7_DOBUAS") })
	endif

	// adicionando info da ordem nos campos
	For nX := 1 to Len(aFields)
		aField := aFields[nX]
		oFldData := oArHlp:First(aFOrder, {|j| aField[1] == j['campo']  })
		aadd(aOrdFlds, { oFldData['ordem'], oFldData['tamanho'], aField[1], aField[2] })
	Next
	aSort(aOrdFlds,,, {|x,y| x[1] < y[1] })

	For nX := 1 to Len(aOrdFlds)
		nSize  := aOrdFlds[nX][2]
		cCampo := aOrdFlds[nX][3]
		uVal   := aOrdFlds[nX][4]
		if valtype(uVal) == 'C' .and. len(uVal) > nSize
			uVal := left(uVal, nSize)
		endif
		oSA1Mod:SetValue(cCampo, uVal)
	Next

	If oModel:VldData()
		lVEEvSO := .t.
		lDeuCerto := oModel:CommitData()
		lVEEvSO := .f.
	EndIf

    //Se nao deu certo a inclusao, mostra a mensagem de erro
    If ! lDeuCerto
        //Busca o Erro do Modelo de Dados
        aErro := oModel:GetErrorMessage()
        self:addError(aErro[04], aErro[06])
        //Monta o Texto que será mostrado na tela
//        conout("Id do formulário de origem:"  + ' [' + AllToChar(aErro[01]) + ']')
//        conout("Id do campo de origem: "      + ' [' + AllToChar(aErro[02]) + ']')
//        conout("Id do formulário de erro: "   + ' [' + AllToChar(aErro[03]) + ']')
//        conout("Id do campo de erro: "        + ' [' + AllToChar(aErro[04]) + ']')
//        conout("Id do erro: "                 + ' [' + AllToChar(aErro[05]) + ']')
//        conout("Mensagem do erro: "           + ' [' + AllToChar(aErro[06]) + ']')
//        conout("Mensagem da soluçao: "        + ' [' + AllToChar(aErro[07]) + ']')
//        conout("Valor atribuído: "            + ' [' + AllToChar(aErro[08]) + ']')
//        conout("Valor anterior: "             + ' [' + AllToChar(aErro[09]) + ']')
    EndIf

	oModel:DeActivate()

	restArea(aAreaSA1)

Return lDeuCerto
