#include 'totvs.ch'

/*/{Protheus.doc} OFSoCustomerCreatedInDbsMessage
	Classe que representa uma mensagem da entidade customer mensagem CreatedInDbs
	
	@type class
	@author Vinicius Gati
	@since 09/08/2021
/*/
Class OFSoCustomerCreatedInDbsMessage
	public data cName
	public data oSoRequest
	data oSoCustomer
	data cMessageId
	data cMsgVX5
	data cOriId

	public Method New() CONSTRUCTOR
	public Method GetMessageId()
	public Method SetCliente()
	public Method Send()
	public Method GetBody()
	public Method GetHeader()
	public Method Delay()
EndClass

/*/{Protheus.doc} New
	Construtor Simples

	@type method
	@author Vinicius Gati
	@since 09/08/2021
/*/
Method New(oCustomer) Class OFSoCustomerCreatedInDbsMessage
	::cName := "CustomerCreatedInDbs"
	::oSoRequest := OFSoRequest():New(self)

	::oSoRequest:Set("VK5_ORITAB", "VK7")
	::oSoRequest:Set("VK5_MESSAG", "07")

	if valtype(oCustomer) != "U"
		::SetCliente(oCustomer)
	endif
Return SELF

/*/{Protheus.doc} SetCliente
	Seta o cliente que gerará o body da mensagem

	@type method
	@author Vinicius Gati
	@since 10/08/2021
/*/
Method SetCliente(oSoCustomer as object) Class OFSoCustomerCreatedInDbsMessage
	self:oSoCustomer := oSoCustomer
	self:oSoRequest:SetBody(self:GetBody())

	self:oSoRequest:Set("VK5_ORIKEY", self:oSoCustomer:GetRefId())

Return .t.

/*/{Protheus.doc} GetMessageId
	Retorna o nome da mensagem para ser enviada ao SO onde necessário

	@type method
	@author Vinicius Gati
	@since 09/08/2021
/*/
Method GetMessageId() Class OFSoCustomerCreatedInDbsMessage
	if valtype(self:cMessageId) == "U"
		self:cMessageId := "customers-" + FWUUIDV4(.t.)
	endif
return self:cMessageId

/*/{Protheus.doc} GetBody
	Retorna o body para a mensagem

	@type method
	@author Vinicius Gati
	@since 12/08/2021
/*/
Method GetBody() Class OFSoCustomerCreatedInDbsMessage
	oReqBody := OFSoRequestBody():New()
	oReqBody:SetData(self:oSoCustomer:toJson())
	oReqBody:SetName(self:cName)
Return oReqBody

/*/{Protheus.doc} GetHeader
	Retorna o header da mensagem

	@type method
	@author Vinicius Gati
	@since 12/08/2021
/*/
Method GetHeader() Class OFSoCustomerCreatedInDbsMessage
Return self:oSoRequest:GetHeader()

/*/{Protheus.doc} Send
	Envia mensagem para SO

	@type method
	@author Vinicius Gati
	@since 12/08/2021
/*/
Method Send() Class OFSoCustomerCreatedInDbsMessage
	local oComm := OFSoCommunication():New()
	self:oSoRequest:SetBody(self:GetBody())
	self:oSoRequest:Save()

	jResp := oComm:SendMessage(self, self:oSoRequest)
	//if jResp["code"] == oComm:nCodigoFalhaComunicacao
	//	self:Delay() // atrasa para tentar mais tarde
	//endif
Return 

/*/{Protheus.doc} Delay
	atrasa a mensagem por falha de comunicação

	@type method
	@author Vinicius Gati
	@since 19/10/2021
/*/
Method Delay() Class OFSoCustomerCreatedInDbsMessage
	Local oMsg := OFDMSExitMessage():New()
	local oJson := JsonObject():New()
	self:oSoRequest:Save()
	oJson['header'] := self:oSoRequest:GetHeader():ToJson()
	oJson['body'] := self:oSoRequest:GetBody():ToJson()
	oMsg:Set("VK6_PAYLOA", oJson:toJson())
	oMsg:Set("VK6_MSGNAM", OFDMSMessageConstants():CustomerCreatedInDbsMessageName())
	oMsg:Set("VK6_CODVK5", self:oSoRequest:GetId())
	oMsg:SetAtrasada()
Return oMsg:Save()
