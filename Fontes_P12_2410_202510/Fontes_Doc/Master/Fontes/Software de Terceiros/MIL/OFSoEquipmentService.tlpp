#include 'totvs.ch'

/*/{Protheus.doc} OFSoEquipmentService
	Classe responsável por tratar as mensagens de equipamento vindos do SO
	
	@type class
	@author Vinicius Gati
	@since 13/10/2022
/*/
Class OFSoEquipmentService
	Public Method New() CONSTRUCTOR
	Public Method Process()
EndClass

/*/{Protheus.doc} New
	Construtor Simples

	@type method
	@author Vinicius Gati
	@since 13/10/2022
/*/
Method New() Class OFSoEquipmentService
Return SELF

/*/{Protheus.doc} Process
	Metodo que processa as requisicoes vindas do Service Operations

	@type method
	@author Vinicius Gati
	@since 29/09/2022
/*/
Method Process(aQueryParams as Array, cBody as Character, cGatewayHeader as Character) Class OFSoEquipmentService
	local jGatewayHeader := JsonObject():New()
	local cParseHead := jGatewayHeader:fromJson(cGatewayHeader)
	Local oSoRequest := OFSoRequestBody():New(cBody)
	local oInteractor := nil

	if ! empty(cParseHead)
		return OFSoInvalidGatewayHeaderResponse():New()
	endif

	if ! oSoRequest:IsValid()
		return OFSoInvalidBodyResponse():New()
	endif

	if oSoRequest:GetName() == "EquipmentCreatedInFieldService"
		oInteractor := OFSoEquipmentCreatedInFieldServiceInteractor():New()
		oResp := oInteractor:Process(oSoRequest, jGatewayHeader)
		return oResp
	endif

	if oSoRequest:GetName() == "EquipmentUpdatedInFieldService"
		oInteractor := OFSoEquipmentUpdatedInFieldServiceInteractor():New()
		oResp := oInteractor:Process(oSoRequest, jGatewayHeader)
		return oResp
	endif
Return OFSoInvalidBodyResponse():New()

