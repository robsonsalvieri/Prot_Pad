#INCLUDE "FINA520.CH"
#INCLUDE "PROTHEUS.CH"

/*

Ŀ
 Funao	     FINA520     Autor  Newton Rogerio Ghiraldelli    Data  17/04/2000 
Ĵ
 Descriao    Atualizacao da Tabela de Plano de Vendas                              
Ĵ
 Sintaxe	     FINA520()                                                             
Ĵ
 Parametros   Nao tem                                                               
Ĵ
 Uso		     Generico                                                              
Ĵ
 Observacoes  Nao tem																
ٱ

  ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.  

Ŀ
 Programador     BOPS        BOPS                      Alteracao                  
Ĵ
                                                                                  
ٱ

*/

Function FINA520()

//Ŀ
//                                                                                        
// Definicao de variaveis                                                                 
//                                                                                        
//
//Ŀ
//                                                                                        
// Define Array contendo as Rotinas que serao executada pelo programa                     
//                                                                                        
// --------------------------- Elementos contidos por dimensao -------------------------- 
//                                                                                        
// 1. Nome a aparecer no botao ( cabecalho )                                              
// 2. Nome da Rotina associada                                                            
// 3. Usado pela rotina                                                                   
// 4. Tipo de Transao a ser efetuada                                                    
//    1 - Pesquisa e Posiciona em um Banco de Dados                                       
//    2 - Visualiza os campos do registro corrente                                        
//    3 - Inclui registros no Bancos de Dados                                             
//    4 - Altera o registro corrente                                                      
//    5 - Remove o registro corrente                                                      
//                                                                                        
//

Private aRotina := MenuDef()
//Ŀ
//                                                                                        
// Define o cabecalho da tela de atualizacoes                                             
//                                                                                        
//

Private cCadastro := OemToAnsi(STR0006) //"Cadastro de Planos de Vendas"

//Ŀ
//                                                                                        
// Endereca a funcao de BROWSE                                                            
//                                                                                        
//

mBrowse( 06, 01, 22, 75, "SEN" )

Return nil

/*

Ŀ
 Funao	     FA520DEL    Autor  Newton Rogerio Ghiraldelli    Data  17/04/2000 
Ĵ
 Descriao    Exclusao de registros da tabela Planos de Vendas                      
Ĵ
 Sintaxe	     FA520DEL( cAlias, nReg, nOpc )                                        
Ĵ
 Parametros   cAlias : Alias do Arquivo                                             
              nReg   : Numero do Registro                                           
              nOpc   : Numero da opcao selecionada                                  
Ĵ
 Uso		     Generico                                                              
Ĵ
 Observacoes  Nao tem															    
ٱ

*/

Function FA520DEL( cAlias, nReg, nOpc )

//Ŀ
//                                                                                        
// Definicao de variaveis                                                                 
//                                                                                        
//

Local ni
Local nOpcA
Local oDlg
Local lDeleta	:= .t.
Local bCampo
Local aSize := MsAdvSize()
      
//Ŀ
//                                                                                        
// Monta a entrada de dados do arquivo                                                    
//                                                                                        
//

Private aTELA[0][0]
Private aGETS[0]

oSize := FWDefSize():New(.T.)
oSize:AddObject( "ENCHOICE", 100,100, .T., .T. ) // Adiciona enchoice
oSize:Process()
aPos := {oSize:GetDimension("ENCHOICE","LININI"),oSize:GetDimension("ENCHOICE","COLINI"),oSize:GetDimension("ENCHOICE","LINEND"),oSize:GetDimension("ENCHOICE","COLEND")}

//Ŀ
//                                                                                        
// Verifica se o arquivo est realmente vazio ou se est posicionado em outra filial.     
//                                                                                        
//

If SEN->(Eof())   //.or. SEQ->EQ_FILIAL # xFilial( "SEQ" )
	Help( " " , 1, "ARQVAZIO" )
	Return Nil
Endif

While .t.

		//Ŀ
		//                                                                                  
		// Envia para processamento dos Gets.                                               
		//                                                                                  
		//

		DbSelectArea( cAlias )
		bCampo := { |nCPO| Field( nCPO ) }
		For ni := 1 TO FCount()
			M->&( EVAL( bCampo, ni ) ) := FieldGet( ni )
		Next ni

		nOpca := 1

		DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 TO aSize[6],aSize[5] of oMainWnd PIXEL

		EnChoice( cAlias, nReg, nOpc, , "AC", OemToAnsi( STR0009 ),, aPos ) //"Quanto  excluso?"

		ACTIVATE	MSDIALOG	oDlg;
					ON INIT	EnchoiceBar( oDlg, { || nOpca := 2, oDlg:End() }, { || nOpca := 1, oDlg:End() } )

		DbSelectArea( cAlias )

		If nOpcA == 2

			//Ŀ
			//                                                                               
			// Antes de deletar, verificar se existe arramacao com outros arquivos.          
			//                                                                               
			//

			DbSelectArea( "SEM" ) 
			DbSetOrder(4)

			//Ŀ
			//                                                                               
			// SEM - Arquivo de Contratos CDCI.                                              
			//                                                                               
			//

			If ( dbSeek( xFilial("SEM")+SEN->EN_CODIGO) )
				Help(" ", 1, "FIA520NDEL" )
				lDeleta := .f.
				MsUnlock()
			Endif

			//Ŀ
			//                                                                               
			// Se nao houver amarracao deleta o registro corrente.                           
			//                                                                               
			//

			If lDeleta
				Begin Transaction
						DbSelectArea( cAlias )
						RecLock( cAlias, .f., .t. )
						dbDelete()
				End Transaction
			Endif
		Else
			MsUnlock( )
		Endif
		Exit
Enddo

//Ŀ
//                                                                                        
// Devolve as ordens aos arquivos pesquisados.                                            
//                                                                                        
//
/*
dbSelectArea( "SEM" )
dbSetOrder( 1 )

dbSelectArea( "SEN" )
dbSetOrder( 1 )
*/
dbSelectArea( cAlias )

Return nil



/*

Ŀ
 Funao	     FA520IOC    Autor  Eduardo Motta                 Data  25/04/2000 
Ĵ
 Descriao    Calculo do IOC caso incida sobre o Coeficiente                        
Ĵ
 Sintaxe	     FA520IOC (nTaxa, cTippes, nQtepar, nPrazo, nCare)                     
Ĵ
 Parametros   nTaxa   : Taxa Anual do IOC                                           
              cTippes : Tipo da Pessoa (Fisica ou Juridica)                         
              nQtepar : Quantidade de parcelas                                      
              nPrazo  : Diferenca de dias entre as parcelas                         
              nCare   : Carencia em dias para primeira parcela                      
Ĵ
 Uso		     Generico                                                              
Ĵ
 Observacoes  Nao tem											                    
ٱ

*/
Function FA520IOC(nTaxa, cTippes, nQtepar, nPrazo, nCare)

local nTxioc, nFator1, nFator2, nNumdia

nNumdia := iif(nCare + ((nQtepar - 1) * nPrazo) <= 365,nCare + ((nQtepar - 1) * nPrazo),365)
nFator1 := (val(subs(str(((nTaxa) / 365) / 100,9,7),1,8))) * nNumdia
nFator2 := (nFator1 - 1) * (-1)
nTxioc  := (nFator1 / nFator2) + 1
//nTxioc  := val(str(nFator1 / nFator2,8,6)) + 1

return(nTxioc)

/*

Ŀ
 Funao	     FA520COE    Autor  Eduardo Motta                 Data  25/04/2000 
Ĵ
 Descriao    Calculo do Coeficiente Multiplicador                                  
Ĵ
 Sintaxe	     FA520COE (nTxano, cTipcalc, nQtepar, nPrazo, nCare)                   
Ĵ
 Parametros   cTxano   : Taxa anual do Indice (SEP)                                 
              cTipCalc : Tipo de Calculo (D-Dia, M-Mes, I-Geral)                    
              nQtepar  : Quantidade de parcelas                                     
              nPrazo   : Diferenca de dias entre as parcelas                        
              nCare    : Carencia em dias para primeira parcela                     
Ĵ
 Uso		     Generico                                                              
Ĵ
 Observacoes  Nao tem											                    
ٱ

*/

Function FA520COE(nTxano, cTipcalc, nQtepar, nPrazo, nCare)

local nTxmes, nTxdia, nAux1, nI, nAux2, nTxcoef

nTxmes  := val(subs(str(((((nTxano / 100) + 1) ** (1 / 12)) - 1) * 100,8,5),1,7))
nTxdia  := val(subs(str(((((nTxmes / 100) + 1) ** (1 / 30)) - 1) * 100,10,7),1,9))
nTxcoef := 0.00
do case
   case cTipcalc == "M"
        nTxcoef := nTxmes
   case cTipcalc == "D"
        nTxcoef := nTxdia
   case cTipcalc == "I"
        nAux2 = 0
        for nI = 0 to nQtepar - 1
            nAux1  := 100 / (((nTxdia / 100) + 1) ** (nCare + (nI * nPrazo)))
            nAux2 := nAux2 + nAux1
        next  
        nTxcoef := (100/nAux2)
        //nTxcoef := val(subs(str(100 / nAux2,10,7),1,9))
endcase
return(nTxcoef)


/*


Ŀ
 Funao	     FA520CAL    Autor  Eduardo Motta                 Data  25/04/2000 
Ĵ
 Descriao    Calculo do IOC e do Indice Multiplicador                              
Ĵ
 Sintaxe	     FA520CAL                                                              
Ĵ
 Parametros                                                                         
Ĵ
 Uso		     Generico                                                              
Ĵ
 Observacoes  Nao tem											                    
ٱ


*/

Function FA520CAL

local nCmult,nTxioc, nIndice

nIndice := FA520COE(SEP->EP_Taxa,"I",M->EN_Maxparc,M->EN_Razao,M->EN_Carenc)
nTxioc  := FA520IOC(SEO->EO_Taxa,M->EN_Pessoa,M->EN_Maxparc,M->EN_Razao,M->EN_Carenc)
nCmult  := nIndice*nTxioc
//nCmult  := val(subs(str(nIndice * nTxioc,9,6),1,8))

return(nCmult)


/*


Ŀ
 Funao	     FA520Ind    Autor  Eduardo Motta                 Data  12/06/2000 
Ĵ
 Descriao    Atera o Indice de varios Planos                                       
Ĵ
 Sintaxe	     FA520Ind                                                              
Ĵ
 Parametros                                                                         
Ĵ
 Uso		     Generico                                                              
Ĵ
 Observacoes  Nao tem											                    
ٱ


*/

Function FA520Ind

If !Pergunte("FIA520",.T.)
   Return
EndIf   

/*

mv_par01 ==> plano inicial
mv_par02 ==> plano final
mv_par03 ==> Pessoa fisica/juridica
mv_par04 ==> indice a ser gravado nos planos que pertencem a lista de planos (mv_par01)

*/

SEP->(DbSeek(xFilial("SEP")+mv_par04))
SEN->(DbGoTop())
Do While ! SEN->(Eof())
   If ! (SEN->EN_CODIGO >= mv_par01 .and. SEN->EN_CODIGO <= mv_par02) .or. SEN->EN_PESSOA # Str(mv_par03,1)
      SEN->(DbSkip())
      Loop
   EndIf
   RecLock( "SEN", .f.)
   SEN->EN_INDICE := mv_par04
   SEN->EN_TAXA   := SEP->EP_TAXA
   RegToMemory("SEN",.f.)
   SEN->EN_COEF   := FA520CAL()
   SEN->(MsUnlock())
   SEN->(DbSkip())
EndDo

Return .T.

/*/


Ŀ
Programa  MenuDef    Autor  Ana Paula N. Silva      Data 27/11/06 
Ĵ
Descrio  Utilizacao de menu Funcional                               
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados     
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function MenuDef()
Local aRotina := {	{ OemToAnsi( STR0001 ) ,"AxPesqui", 0, 1,,.F. },; //"Pesquisar"
							{ OemToAnsi( STR0002 ) ,"AxVisual", 0, 2 },; //"Visualizar"
							{ OemToAnsi( STR0003 ) ,"AxInclui", 0, 3 },; //"Incluir"
							{ OemToAnsi( STR0004 ) ,"AxAltera", 0, 4 },; //"Alterar"
							{ OemToAnsi( STR0010 ) ,"FA520Ind", 0, 4 },; //"Alt Indices"
							{ OemToAnsi( STR0005 ) ,"FA520Del", 0, 5 } } //"Excluir"
Return(aRotina)