#include "tlpp-core.th"
#include "backoffice.accounting.accountingtotal.controller.ch"

namespace totvs.protheus.backoffice.accounting.accountingtotal
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingTotal
Classe principal de controle, onde inicializa o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class AccountingTotal
    public method new()
    
    @Get("/api/ctb/accountingtotal/")
    public method getAccountingTotal()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingTotal
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class AccountingTotal
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAccountingTotal
Metodo que inicia a chamada dos totais de lancamentos contabeis

@author Totvs
/*/
//-------------------------------------------------------------------
method getAccountingTotal() class AccountingTotal
    Local oService  := AccountingTotalService():getInstance() as object        
    Local jResponse := JsonObject():new() as json
    Local aTotal    := {} as array
    Local cResponse := STR0001 as character //"Json Inválido"
    Local lResponse := .F. as logical        
        
    If CT2->(FieldPos("CT2_PROCES")) > 0 .And. CT2->(FieldPos("CT2_INCONS")) > 0
        aTotal := oService:getTotal()        
        If ValType(aTotal) == "A" .And. Len(aTotal) > 0            
            jResponse["totais"] := aClone(aTotal)
            oRest:setKeyHeaderResponse("Content-Type", "application/json")
            oRest:setResponse(jResponse)
            lResponse := .T.                            
        EndIf
    Else
        cResponse := STR0002 //"Campos CT2_PROCES e CT2_INCONS não encontrados na base de dados"
        lResponse := .F.
    EndIf

    If !lResponse
        tcSetResponse(.F., "message", cResponse, .F.)
    EndIf
        
    FwFreeArray(aTotal)
    FreeObj(oService)                                    
return
