#Include "BACKOFFICE.RECONCILIATION.TABLESLINK.CONTROLLER.ch"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.reconciliation.tableslink
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} TablesLink
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class TablesLink
    public method new()

    @Get("/api/tc/tableslink/")
    public method getAllTablesLink()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} TablesLink
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class TablesLink
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllTablesLink
Metodo que inicia a chamada das configuracoes de match do conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllTablesLink() class TablesLink
    Local oService  As Object
    Local cReturn   As Character
    Local cFilBkp   As Character    
    Local aFilters  As Array
    Local lReturn   As Logical
    Local cChaveRes As Character
    Local cResponse As Character
    Local cTabOri   As Character
    Local cTabDes   As Character

    //Inicilaiza variaveis
    oService  := TablesLinkService():getInstance()    
    cFilBkp   := cFilAnt
    cChaveRes := STR0001 //"message"
    cResponse := ""
    cTabOri   := oRest:getQueryRequest():GetJsonText("tabori")
    cTabDes   := oRest:getQueryRequest():GetJsonText("tabdes")
    lReturn   := .F.
    
    If ValType(cTabOri)=="C" .And. !Empty(cTabOri) .And. cTabOri <> "null"
        If ValType(cTabDes)=="C" .And. !Empty(cTabDes) .And. cTabDes <> "null" 
            If cTabOri <> cTabDes 
                tcGetHeaders()

                lReturn := oService:getAllTablesLink(cTabOri, cTabDes, @cReturn)
                cResponse := cReturn

                If lReturn
                    cChaveRes := "data"
                EndIf
                
                //Restaura empresa e filial
                cFilAnt := cFilBkp

                //Limpa array
                FWFreeArray(aFilters)
            Else
                cResponse := STR0004+"tabori"+STR0003+"tabdes"+STR0002     //" devem ser diferentes." //" e " //"Os parâmetros "
            EndIf
        Else
            cResponse := STR0006+"tabdes"+STR0005     //" não informado." //"Parâmetro "
        EndIf
    Else
        cResponse := STR0006+"tabori"+STR0005 //" não informado." //"Parâmetro "
    EndIf

    tcSetResponse(.T. ,cChaveRes, cResponse,lReturn)
return
