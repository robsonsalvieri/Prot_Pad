#include "tlpp-core.th"


namespace totvs.protheus.backoffice.reconciliation.LookUp
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} LookUp
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class LookUp
    public method new()

    @Get("/api/tc/lookup/")
    public method getAllLookUp()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} LookUp
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class LookUp
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllLookUp
Metodo que retorna as contas contabeis (Consulta padrão)

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllLookUp() class LookUp
    Local oService  As Object
    Local oField    As Object
    Local jResposta As Json
    Local cFilBkp   As Character
    Local cTable    As Character 
    Local cSearch   As Character 
    Local nPage     As Numeric
    Local nPageSize As Numeric
    Local nAt       As Numeric
    Local aFilters  As Array
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character	

    //Inicilaiza variaveis
    oService  := LookUpService():getInstance()
    oField    := tcTreatsFilter():new()
    jResposta := JsonObject():new()
    cFilBkp   := cFilAnt
    cTable    := tcGetQryParams('table')
    cSearch    := tcGetQryParams('search')
    aFilters  := {}
    nPage     := 1
    nPageSize := 10
    nAt       := 0
    lResponse := .F.
    cChaveRes := "message"
    cResponse :=  "Resposta inválida." //STR0001
    
    tcGetHeaders()
    aFilters := tcGetFilters()
    
    tcGetPageAndPageSize(@nPage, @nPageSize)

    jResposta:= oService:getAllLookUp(cTable, aFilters, nPage, nPageSize,cSearch)

    IIf(ValType(jResposta:GetJsonText("items")) <> "U",; 
        tcAnswerRest(jResposta, .T.),;    
        tcSetResponse(lResponse, cChaveRes, cResponse, .F.))

    //Restaura empresa e filial
    cFilAnt := cFilBkp

    //Limpa array
	FWFreeArray(aFilters)
return
