#include "tlpp-core.th"

namespace totvs.protheus.backoffice.reconciliation.dataexport
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} DataExport
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class DataExport 
    public method new()

    @Get("/api/tc/dataexport/")
    public method getAllDataExports()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} DataExport
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class DataExport
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllDataExports
Metodo que inicia a chamada das configuracoes de match do conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllDataExports() class DataExport
    Local oService  As Object
    Local jResposta As Json
    Local cFilBkp   As Character
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character
    Local aResponse As Array	

    //Inicilaiza variaveis
    oService  := DataExportService():getInstance()
    jResposta := JsonObject():new()
    cFilBkp   := cFilAnt
    lResponse := .F.
    cChaveRes := "message"
    cResponse := "Resposta inválida."
    aResponse := oService:getAllDataExport()

    tcGetHeaders()

    If ValType(aResponse) == "A" .And. Len(aResponse) > 1
        lResponse := aResponse[1]
        cResponse := aResponse[2]
    EndIf 

    tcSetResponse(lResponse, cChaveRes, cResponse, .T.)
    FwFreeArray(aResponse)
    FreeObj(oService)                           

    cFilAnt := cFilBkp
return
