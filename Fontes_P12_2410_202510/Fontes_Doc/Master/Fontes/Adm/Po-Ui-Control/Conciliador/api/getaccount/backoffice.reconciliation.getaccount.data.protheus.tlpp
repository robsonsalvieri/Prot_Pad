#include "tlpp-core.th"

namespace totvs.protheus.backoffice.reconciliation.getaccount
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAccountProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class GetAccountProtheusData from FWAdapterBaseV2
    Public method new()
    Public method getAll()
    static method getData() As object
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAccountProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class GetAccountProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAccountProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class GetAccountProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := GetAccountProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getAll
Metodo responsável pela busca das configuracoes do conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method getAll(cCodEntity as Character,cPlano as Character,cValue as Character, cFilters as Character,;
                nPage as Numeric, nPageSize as Numeric) class GetAccountProtheusData
    Local aArea  := GetArea() As Array
    Local cWhere := GetBranchiesForConditional(cCodEntity) As Character
    Local cCodigo := "" As Character
    Local cDesc := "" As Character
    Local cBloq := "BLOQ" As Character
    Local cConcat:= IIF("MSSQL" $ Alltrim(TcGetDB()), "+", "||") as Character

    Default cFilters  := ""
    Default nPage     := 1
    Default nPageSize := 10

    Do case
        case cCodEntity =='CT1'
            cCodigo := 'CT1_CONTA'
            cDesc   := 'CT1_DESC01'
        case cCodEntity =='CTT'
            cCodigo := 'CTT_CUSTO'
            cDesc   := 'CTT_DESC01'
        case cCodEntity =='CTD'
            cCodigo := 'CTD_ITEM'
            cDesc   := 'CTD_DESC01'
        case cCodEntity =='CTH'
            cCodigo := 'CTH_CLVL'
            cDesc   := 'CTH_DESC01'
        case cCodEntity =='CV0'
            cCodigo := 'CV0_CODIGO'
            cDesc   := 'CV0_DESC'
            cBloq   := 'BLOQUE'
        case cCodEntity =='CQJ'
            cCodigo := 'CQJ_CODEVE'
            cDesc   := 'CQJ_DESC'
    End case

    addMapFields(self,cCodEntity,cCodigo,cDesc)

    ::setPage(nPage)
    ::setPageSize(nPageSize)
    ::SetQuery(getQuery(cCodEntity))

    //Aplica filtros
    If !Empty(cFilters)
        cWhere += " AND ( "+cCodigo+" LIKE '%" + UPPER( cFilters ) + "%' OR "+cDesc+" LIKE '%" + UPPER( cFilters ) +"%' ) "
    EndIf

    If cCodEntity <> 'CQJ'  // filtro bloqueio e classe somente para as entidades padroes
        cWhere += " AND "+cCodEntity+"_"+cBloq+" IN ('2', ' ') "
        cWhere += " AND "+cCodEntity+"_CLASSE='2' "
    EndIf

    //Entidades adicionais
    If !Empty(cPlano) .and. cCodEntity == 'CV0'
        If Empty(cValue)
            cWhere += " AND CV0_PLANO = '"+cPlano+"' "
        Else
            cWhere += " AND CV0_PLANO"+cConcat+"CV0_CODIGO = '"+cValue+"' "
        EndIf
    ElseIf !Empty(cValue) //Entidades
        cWhere += " AND "+cCodigo+" = '"+cValue+"'"
    EndIf
    cWhere += " AND "+cCodEntity+".D_E_L_E_T_ = ' '"

    ::SetWhere(cWhere)
    ::SetOrder(cCodEntity+"."+cCodigo)

    If ::Execute()
        ::FillGetResponse()
    EndIf

    RestArea(aArea)
    FwFreeArray(aArea)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} addMapFields
Realiza o mapeamento dos campos que serão retornados

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function addMapFields(oSelf as Object,cCodEntity As Character,cCodigo As Character,cDesc As Character)

    oSelf:addMapFields("Cod Conta", cCodigo, .T., .F., {cCodigo, "C", TamSx3(cCodigo)[1], 0}, cCodEntity+"."+cCodigo)
    oSelf:addMapFields("Desc", cDesc, .T., .F., {cDesc, "C", TamSx3(cDesc)[1], 0}, cCodEntity+"."+cDesc)

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Realiza a query para busca de informações

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function getQuery(cCodEntity As Character) As Character
    Local cQuery As Character

    cQuery := " SELECT #QueryFields#"
    cQuery += " FROM " + RetSqlName(cCodEntity) + " "+cCodEntity
    cQuery += " WHERE #QueryWhere#"
Return cQuery
