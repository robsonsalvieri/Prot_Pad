#include "tlpp-core.th"
#include "totvs.ch"
#include "fwlibversion.ch"
namespace totvs.protheus.backoffice.reconciliation.userprofile
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} UserProfileProtheusData
Classe responsavel pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class UserProfileProtheusData
Public  method new()
Public  method getData() as Object    
Public  method postProfileByBody()
Public  method getProfile()
Public  method setAlterProfile()
Public  method deleteProfile()
Private method addUserProfile()
Private method findUserProfile()
Private method getAlterProfile()
endclass

/*/{Protheus.doc} UserProfileProtheusData
Metodo construtor

@author Totvs
/*/
method new() class UserProfileProtheusData
return

/*/{Protheus.doc} getData
Metodo para chamada e validacao do tratamento de dados

@author Totvs
/*/
method getData() class UserProfileProtheusData as Object
static __oActiveData as Object

If ValType(__oActiveData) == "U"
    __oActiveData := UserProfileProtheusData():new()
EndIf
return __oActiveData

/*/{Protheus.doc} postProfileByBody
Metodo responsavel pela inclusao dos profiles dos usuarios

@author Totvs
/*/
method postProfileByBody(jBody as Json, cBody as Character, lAlterProfile as Logical,; 
                            aUpdProfile as Array, lDelete as logical, cOrigem as Character) class UserProfileProtheusData 
Local aResponse   := {} as Array
Local aProfile    := {} as Array
Local aAllusers   := {} as Array 

Local cProfile    := jBody:GetJsonText("codcfg") as Character
Local cKeyProfile := "" as Character
Local nI          := 0 as Numeric
Local uRet        := Nil
Local cKey        := "" as Character
Local flagPouiSize := .F. as Logical

If cProfile == "null"
    cKeyProfile := "branches"
ElseIf cProfile == "POUISIZE"
    flagPouiSize := .T.
Else
    cKeyProfile := cProfile 
EndIf

Default cBody         := oRest:getBodyRequest()
Default lAlterProfile := .F.
Default aUpdProfile   := {}
Default lDelete       := .F.
Default cOrigem       := ""

If cOrigem == "CTBA960"
    cKey := "FECHAMENT"
ElseIf cOrigem == "ATFA510"
    cKey := "AGRPATRIMO"
ElseIf cProfile == "POUISIZE"
    cKey := "POUISIZE"
    cKeyProfile := "POUISIZE"
ElseIf cOrigem == "CTBA961"
    cKey := "MONITOFILA"
    cKeyProfile := "MONITOFILA"
Else
    cKey := "CONCILIAD"
EndIf
cProfile := jBody:GetJsonText(cKeyProfile)
If lDelete        
    aAllusers := FwsfAllUsers()
    aAdd(aResponse, .T.)
    aAdd(aResponse, cKeyProfile)    
    
    //Deleta todas as configuracoes dos usuarios
    For nI := 1 To Len(aAllusers)
        ::deleteProfile(aAllusers[nI][2], cKeyProfile)                
    Next nI        
ElseIf AllTrim(cProfile) == "[]"
    /*Retorna o valor em formato de string
    Necessário pois se retornar um array vazio da erro no front*/            
    cKeyProfile   := cKey
    lAlterProfile := .T.
    aAdd(aProfile, cProfile)
    aAdd(aResponse, .T.)
    aAdd(aResponse, cProfile)        
Else
    If AllTrim(cKeyProfile) == "branches" //Chamada da própria classe
        aProfile      := aClone(jBody[cKeyProfile])
        cKeyProfile   := cKey
        lAlterProfile := .T.
        aAdd(aResponse, .T.)
        aAdd(aResponse, aClone(aProfile))    
    Else //Chamadas externas
        uRet := jBody:fromJson(cBody)
        If ValType(uRet) == "U"            
            aAdd(aProfile, cBody)                            
        EndIf
        
        If !lAlterProfile
            lAlterProfile := ::getAlterProfile(aProfile, cKeyProfile, aUpdProfile, cOrigem) //Verifica alteracao do filtro do usuario                  
        EndIf
                
        If lAlterProfile //Se houve alteracao de profile, atualiza json
            ::setAlterProfile(aProfile, aUpdProfile)
        EndIf

        If cOrigem == "CTBA961"
            aAdd(aResponse, .T.)
            aAdd(aResponse, aClone(aProfile))
        EndIf
    EndIf        
EndIf
If lAlterProfile
    ::addUserProfile(aProfile, cKeyProfile, cOrigem)
EndIf
FwFreeArray(aProfile) 
return aResponse

/*/{Protheus.doc} addUserProfile
Metodo responsavel pela gravacao de profiles

@author Totvs
/*/
method addUserProfile(aProfile as Array, cKeyProfile as Character, cOrigem as Character) class UserProfileProtheusData
    Local oProfile := FWProfile():New() as Object
    Local lLibVersion := FwLibVersion() > "20240226" As Logical 
    Local cUserPrf := RetCodUsr() as Character
    Local cKeyProfileTT := If(cKeyProfile == "", "POUISIZE", cKeyProfile) as Character
    Local lSaveFilProfile := .T. as Logical
    Default cOrigem := ""

    If cOrigem $ "CTBA960|ATFA510|CTBA961"
        cProgram := cOrigem
    Else
        cProgram := "CTBA940"
    EndIf
    // 
    if cOrigem == "CTBA960"
        lSaveFilProfile := .F.
    ElseIf cOrigem == "ATFA510"
        lSaveFilProfile := .F.
    EndIf
    // Carrega dados do Profile
    oProfile:SetUser(cUserPrf) 
    oProfile:SetProgram(cProgram)
    oProfile:SetTask("CONFIG") 
    oProfile:SetType(cKeyProfileTT)
    if lLibVersion
        oProfile:SetCompany(cEmpAnt)
        if cKeyProfileTT <> "CONCILIAD" .And. lSaveFilProfile
            oProfile:SetBranch(cFilAnt)
        else
            oProfile:SetBranch("")
        endIf
        oProfile:Load(.T.)
        oProfile:SetProfile(aClone(aProfile))
        oProfile:Save(.T.) 
    else
        oProfile:Load()
        oProfile:SetProfile(aClone(aProfile))
        oProfile:Save() 
    endIf
    oProfile:Destroy()
return

/*/{Protheus.doc} getProfile
Metodo responsavel pela busca dos profiles dos usuarios

@author Totvs
/*/
method getProfile(cParamType as Character, cOrigem as Character,cFontSize as Character) class UserProfileProtheusData

Local aResponse := {} as Array
Default cOrigem := ""

::findUserProfile(cParamType, aResponse, cOrigem,cFontSize) 
return aResponse

/*/{Protheus.doc} findUserProfile
Metodo responsavel pela busca de profiles

@author Totvs
/*/
method findUserProfile(cParamType as Character, aResponse as Array, cOrigem as Character,cFontSize as Character) class UserProfileProtheusData
    Local oProfile  := FWProfile():New() as Object
    Local lLibVersion := FwLibVersion() > "20240226" As Logical 
    Local aFilsProf := {} as Array
    Local cUserPrf  := RetCodUsr() as Character
    Local cKey      := "" as Character
    Local cProgram  := "" as Character
    Local lSaveFilProfile := .T. as Logical
    Default cOrigem := ""
    If cOrigem == "CTBA960"
        cKey := "FECHAMENT"
        cProgram := cOrigem
        lSaveFilProfile := .F.
    ElseIf cOrigem == "ATFA510"
        cKey := "AGRPATRIMO"
        cProgram := cOrigem
        lSaveFilProfile := .F.
    ElseIf cOrigem == "CTBA961"
        cKey := "MONITOFILA"
        cProgram := cOrigem
    Else
        cKey := "CONCILIAD"
        cProgram := "CTBA940"
    EndIf
    cParamType := If(Empty(cParamType), cKey, cParamType)
    oProfile:SetUser(cUserPrf) 
    oProfile:SetProgram(cProgram)
    oProfile:SetTask("CONFIG") 
    oProfile:SetType(cParamType)
    if lLibVersion
        oProfile:SetCompany(cEmpAnt)
        if cParamType <> "CONCILIAD" .And. lSaveFilProfile
            oProfile:SetBranch(cFilAnt)
        else
            oProfile:SetBranch("")
        endIf
        oProfile:Load(.T.) //Carrega as configurações armazenadas no profile
    else
        oProfile:Load() //Carrega as configurações armazenadas no profile
    endIf
    aFilsProf := oProfile:GetProfile()//Retorna um array contendo o profile do usuário
    If ValType(aFilsProf) == "A" .And. Len(aFilsProf) > 0
        aAdd(aResponse, .T.)    
        aAdd(aResponse, aClone(aFilsProf))
    Else
        aAdd(aResponse, .F.)    
        aAdd(aResponse, "")
    EndIf 
    oProfile:Destroy()
return

/*/{Protheus.doc} getAlterProfile
Metodo responsavel pela verificacao de alteracoes
no filtro do usuario e se grava no profile

@author Totvs
/*/
method getAlterProfile(aNewFilter as Array, cKeyProfile as Character, aUpdProfile as Array, cOrigem as Character) class UserProfileProtheusData
    Local aCurrentFilter := {} as Array
    Local oCurrentFilter := JsonObject():new() as Object
    Local oNewFilter     := JsonObject():new() as Object
    Local lAlterPrf      := .F. as Logical
    Local uRet           := Nil

    Local cKeyProfileTT := If(cKeyProfile == "", "POUISIZE", cKeyProfile) as Character

    Default cOrigem := ""

    aCurrentFilter := AClone(::getProfile(cKeyProfileTT,cOrigem))

    If (ValType(aCurrentFilter) == "A" .And. !aCurrentFilter[1]) //Se inclusao de profile, sempre atualiza
        aUpdProfile := {"update", "true"}
        lAlterPrf := .T.    
    ElseIf (ValType(aCurrentFilter) == "A" .And. aCurrentFilter[1]) .And.;
        (ValType(aNewFilter) == "A" .And. Len(aNewFilter) > 0)
        
        uRet := oCurrentFilter:fromJson(aCurrentFilter[2][1]) //Armazena filtros atuais em formato Json    
        If ValType(uRet) == "U"    
            uRet := oNewFilter:fromJson(aNewFilter[1]) //Armazena filtros novos em formato Json
            If ValType(uRet) == "U"
                If cOrigem == "CTBA961"
                    If oNewFilter:getJsonText("atention") <> oCurrentFilter:getJsonText("atention") .Or.;
                        oNewFilter:getJsonText("critical") <> oCurrentFilter:getJsonText("critical") .Or.;
                        oNewFilter:getJsonText("limit") <> oCurrentFilter:getJsonText("limit") .Or.;
                        oNewFilter:getJsonText("timer") <> oCurrentFilter:getJsonText("timer")              
                        aUpdProfile := {"update", "true"}
                        lAlterPrf   := .T.
                    EndIf
                elseif cKeyProfileTT == "POUISIZE"
                    lAlterPrf   := .T.
                Else
                    If oNewFilter:getJsonText("tabori") <> oCurrentFilter:getJsonText("tabori") .Or.;
                        oNewFilter:getJsonText("tabdes") <> oCurrentFilter:getJsonText("tabdes") .Or.;
                        (oNewFilter:getJsonText("totalLedger") <> "null" .And.; 
                        oNewFilter:getJsonText("totalLedger") <> oCurrentFilter:getJsonText("totalLedger"))               
                        aUpdProfile := {"update", "true"}
                        lAlterPrf   := .T.
                    EndIf
                EndIf
            EndIf
        EndIf        
    EndIf
return lAlterPrf

/*/{Protheus.doc} setAlterProfile
Metodo responsavel pela insercao de alteracoes
no profile do usuario

@author Totvs
/*/
method setAlterProfile(aProfile as Array, aUpdate as Array) class UserProfileProtheusData
    Local cProfile   := "" as Character
    Local nPosUpdate := 0 as Numeric

    If ValType(aProfile) == "A" .And. Len(aProfile) > 0 .And.;
        ValType(aUpdate) == "A" .And. Len(aUpdate) > 1
        
        cProfile := Rtrim(aProfile[Len(aProfile)])
        If (nPosUpdate := At('"update"',cProfile)) > 0        
            cProfile := SubStr(cProfile, 1, nPosUpdate-2)
        Else
            cProfile := SubStr(cProfile, 1, Len(cProfile)-1)
        EndIf
        
        cProfile += ',"'+aUpdate[1]+'":"'+aUpdate[2]+'"}'
        
        aProfile[Len(aProfile)] := cProfile     
    EndIf
return


/*/{Protheus.doc} deleteProfile
Metodo responsavel pela exclusao dos profiles dos usuarios

@author Totvs
/*/
method deleteProfile(cUserPrf as Character, cKeyPrf as Character) class UserProfileProtheusData 
    Local oProfile  := FWProfile():New() as Object
    Local lLibVersion := FwLibVersion() > "20240226" As Logical 
    Local lSaveFilProfile := .T. as Logical
    Default cUserPrf := __cUserId
    Default cKeyPrf  := "CONCILIAD"
    
    if cKeyPrf == "FECHAMENT"
        lSaveFilProfile := .F.
    ElseIf cKeyPrf == "AGRPATRIMO"
        lSaveFilProfile := .F.
    EndIf

    oProfile:SetUser(cUserPrf) 
    oProfile:SetProgram("CTBA940")
    oProfile:SetTask("CONFIG") 
    oProfile:SetType(cKeyPrf)
    if lLibVersion
        oProfile:SetCompany(cEmpAnt)
        if cKeyPrf <> "CONCILIAD" .And. lSaveFilProfile
            oProfile:SetBranch(cFilAnt)
        else
            oProfile:SetBranch("") 
        endIf
        oProfile:Delete(.T.)
    else
        oProfile:Delete()     
    endIf
    oProfile:Destroy() 
return 
