#Include "BACKOFFICE.ACCOUNTINGCLOSING.ACCOUNTINGCALENDAR.CONTROLLER.ch"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.accountingclosing.accountingcalendar
using namespace totvs.protheus.backoffice.accountingclosing.util

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingCalendar
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class AccountingCalendar
    public method new()

    @Get("/api/fc/accountingcalendar/")
    public method getAllAccountingCalendar()

    @POST("/api/fc/accountingcalendar/")
    public method postBlockCalendar()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingCalendar
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class AccountingCalendar
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllAccountingCalendar
Metodo que inicia a chamada das configuracoes das definições de fechamento
do Fechamento Contabil

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllAccountingCalendar() class AccountingCalendar
    Local oService  As Object
    Local jResposta As Json
    Local cFilBkp   As Character   
    Local nPage     As Numeric
    Local nPageSize As Numeric    
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character	
    Local cDatIni   As Character
    Local cDatFin   As Character
    Local cType     As Character

    //Inicilaiza variaveis
    oService  := AccountingCalendarService():getInstance()
    jResposta := JsonObject():new()
    cFilBkp   := cFilAnt
    nPage     := 1
    nPageSize := 10
    lResponse := .F.
    cChaveRes := "message"
    cResponse := STR0003 //"Resposta inválida."
    cDatIni   := oRest:getQueryRequest():GetJsonText("dateini")
    cDatFin   := oRest:getQueryRequest():GetJsonText("datefin")
    cType     := oRest:getQueryRequest():GetJsonText("type")
    
    fcGetPageAndPageSize(@nPage, @nPageSize)
    fcGetHeaders()

    jResposta:fromJson(oService:getAllAccountingCalendar(nPage, nPageSize, cDatIni, cDatFin, cType))

    IIf(ValType(jResposta:GetJsonText("items")) <> "U",; 
        fcAnswerRest(jResposta, .T.),;
        fcSetResponse(lResponse, cChaveRes, cResponse, .F.))

    //Restaura empresa e filial
    cFilAnt := cFilBkp

    //Limpa array
	FWFreeArray()
return

//-------------------------------------------------------------------
/*/{Protheus.doc} postBlockCalendar
Metodo que inicia a chamada das configuracoes das definições de fechamento
do Fechamento Contabil

@author Totvs
/*/
//-------------------------------------------------------------------
method postBlockCalendar() class AccountingCalendar
    Local oService  As Object
    Local jResposta As Json
    Local cFilBkp   As Character   
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character	
    Local cBody     As Character
    Local jBody     As Json
    
    //Inicilaiza variaveis
    oService  := AccountingCalendarService():getInstance()
    jResposta := JsonObject():new()
    jBody     := JsonObject():new()
    cFilBkp   := cFilAnt
    lResponse := .F.
    cChaveRes := "message"
    cResponse := ""
    cBody 	  := oRest:GetBodyRequest()

    fcGetHeaders()

    If ValType(cBody) == "C" .And. !Empty(cBody)
        uRet := jBody:fromJson( cBody )
        If ValType(uRet) == "U" .And. ValType(jBody["items"]) <> "U" .And. jBody:hasProperty("items")
            lResponse := oService:postBlockCalendar(jBody, @cResponse)            
        Else
            cResponse := STR0004 //"Conteúdo do body inválido"
        EndIf
    Else
        cResponse := STR0005 //"Body não enviado"
    EndIf

    fcSetResponse(lResponse, cChaveRes, cResponse, lResponse)

    //Restaura empresa e filial
    cFilAnt := cFilBkp

return

