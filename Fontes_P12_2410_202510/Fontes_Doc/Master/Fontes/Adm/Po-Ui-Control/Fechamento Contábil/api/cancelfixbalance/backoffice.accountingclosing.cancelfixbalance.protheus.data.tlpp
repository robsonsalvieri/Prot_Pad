#include "tlpp-core.th"
#include "BACKOFFICE.ACCOUNTINGCLOSING.CANCELFIXBALANCE.PROTHEUS.DATA.CH"

namespace totvs.protheus.backoffice.accountingclosing.cancelfixbalance

//-------------------------------------------------------------------
/*/{Protheus.doc} cancelFixBalanceProtheusData
Classe responsável pela exclusão de registros da tabela QLJ quando não forem reprocessados

@author Totvs
/*/
//-------------------------------------------------------------------
class CancelFixBalanceProtheusData from FWAdapterBaseV2
    Public method new()
    Public method postCancelFixBalance()
    Public method clearFixBalance()
    static method getData() As object
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} CancelFixBalanceProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class CancelFixBalanceProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} CancelFixBalanceProtheusData

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class CancelFixBalanceProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := CancelFixBalanceProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} postCancelFixBalance 
Metodo responsável pela exclusão dos registros da QLJ com base no código UUID enviado

@author Totvs
/*/
//-------------------------------------------------------------------
method postCancelFixBalance(cUUID as Character, cError as Character) class CancelFixBalanceProtheusData

    Local lRet          as Logical
    Local cQryDel       as Character

    lRet    := .F.
    cError  := ""

    cQryDel := " DELETE FROM " + RetSQLName("QLJ") + " WHERE QLJ_FILIAL = '" + xFilial("QLJ") + "' AND QLJ_UUID = '" + cUUID + "' "
    IIf(TcSqlExec(cQryDel) <> 0,;
        (conout(TCSqlError()), cError := STR0001),; //"Ocorreu um erro inesperado ao limpar a tabela de registros com saldos divergentes. Para mais detalhes, consulte o console.log" 
        cError := "")

    IIf(cError <> "", lRet := .F., lRet := .T.)

    UnlockByName("CTBA960_"+cUUID)

return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} clearFixBalance 
Metodo responsável pela exclusão das divergencias que ficaram na QLJ e não foram reprocessadas

@author Totvs
/*/
//-------------------------------------------------------------------
method clearFixBalance(cError as Character) class CancelFixBalanceProtheusData
    Local lRet          as Logical
    Local cQryUUID      as Character
    Local cAliasTmp     as character
    Local cUUID         as Character
    Local cQryDel       as Character
    Local cTableTmp     as Character

    lRet        := .T.
    cError      := ""
    cUUID       := ""
    aUUIDs      := {}
    cAliasTmp   := GetNextAlias()
    cTableTmp  := "TRZ"+cEmpAnt+"0_SP"

    //Verifica se há dados na QLJ
    cQryUUID := " SELECT DISTINCT QLJ_UUID AS UUID FROM " + RetSQLName("QLJ") + " WHERE QLJ_FILIAL = '" + xFilial("QLJ") + "' "

    dbUseArea(.T., "TOPCONN", TcGenQry(,, cQryUUID), cAliasTmp, .T., .F.)

    While (cAliasTmp)->(!EOF())
        cUUID := AllTrim((cAliasTmp)->UUID)
        
        //Se o UUID ainda estiver sendo usado, não será possível trava-lo e não será excluído neste momento.
        If LockByName("CTBA960_"+cUUID)
            cQryDel := " DELETE FROM " + RetSQLName("QLJ") + " WHERE QLJ_FILIAL = '" + xFilial("QLJ") + "' AND QLJ_UUID = '" + cUUID + "' "
            
            IIf(TcSqlExec(cQryDel) <> 0,;
                (conout(TCSqlError()), cError := STR0002),; //""Ocorreu um erro inespeado ao preparar a tabela de saldos a corrigir. Para mais detalhes, consulte o console.log" " 
                cError := "")

            IIf(cError <> "", lRet := .F., lRet := .T.)

            If lRet
                cQryDel := " DELETE FROM " + cTableTmp + " WHERE TRZ_UUID = '" + cUUID + "' "
    
                IIf(TcSqlExec(cQryDel) <> 0,;
                    (conout(TCSqlError()), cError := STR0002),; //"Ocorreu um erro inesperado ao limpar a tabela de registros com saldos divergentes. Para mais detalhes, consulte o console.log" 
                    cError := "")

                IIf(cError <> "", lRet := .F., lRet := .T.)
            EndIf
            
            UnlockByName("CTBA960_"+cUUID)
        EndIf
        (cAliasTmp)->(dbSkip())
    EndDo
    (cAliasTmp)->(dbCloseArea()) //Fecha temporario

return lRet


