#include "tlpp-core.th"

namespace totvs.protheus.backoffice.accountingclosing.closingtables
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} ClosingTablesProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class ClosingTablesProtheusData from FWAdapterBaseV2
    Public method new()
    Public method getTables()
    static method getData() As object
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} ClosingTablesProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class ClosingTablesProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} ClosingTablesProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class ClosingTablesProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := ClosingTablesProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getTables
Metodo responsável pela busca das definições do fechamento contabil

@author Totvs
/*/
//-------------------------------------------------------------------
method getTables() class ClosingTablesProtheusData
    Local aArea  := GetArea() As Array
    Local cWhere := GetBranchiesForConditional("QLF", "CTBA960") As Character

    addMapFields(self)

    ::SetQuery(getQuery())

    cWhere += " AND QLF.D_E_L_E_T_ = ' '"

    ::SetWhere(cWhere)
    ::SetOrder("QLF.QLF_FILIAL, QLF.QLF_MODULE")

    If ::Execute()
        ::FillGetResponse()
    EndIf

    RestArea(aArea)
    FwFreeArray(aArea)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} addMapFields
Realiza o mapeamento dos campos que serão retornados

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function addMapFields(oSelf as Object)

    oSelf:addMapFields("branch" , "branch", .T., .F., {"branch", "C", TamSx3("QLF_FILIAL")[1], 0}, "QLF.QLF_FILIAL")
    oSelf:addMapFields("module" , "module", .T., .F., {"module", "C", TamSx3("QLF_MODULE")[1], 0}, "QLF.QLF_MODULE")
    oSelf:addMapFields("name"   , "name", .T., .F., {"name", "C", TamSx3("QLF_MODNAM")[1], 0}, "QLF.QLF_MODNAM")
    oSelf:addMapFields("tables"  , "tables" , .T., .F., {"tables" , "M", TamSx3("QLF_TABLES")[1], 0}, "QLF.QLF_TABLES") 

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Realiza a query para busca de informações

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function getQuery() As Character
    Local cQuery As Character

    cQuery := " SELECT #QueryFields#"
    cQuery += " FROM " + RetSqlName("QLF") + " QLF "
    cQuery += " WHERE #QueryWhere#"
Return cQuery
