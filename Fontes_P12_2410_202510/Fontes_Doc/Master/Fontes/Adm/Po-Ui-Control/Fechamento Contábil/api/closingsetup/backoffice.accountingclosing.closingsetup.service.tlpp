#include "tlpp-core.th"
#include "backoffice.accountingclosing.closingsetup.service.ch"

namespace totvs.protheus.backoffice.accountingclosing.closingsetup
using namespace totvs.protheus.backoffice.reconciliation.util

/*/{Protheus.doc} ClosingSetupService
    Classe de serviço, realizada instância e chamada de metodos de serviços
    @author Totvs
    @since 09/01/2024
    @version 12.1.2310
/*/
Class ClosingSetupService    	    
    data lResponse As Logical
    data cResponse As Character
    data cResponseKey As Character
    data cMessage As Character

    public method new()
    public method getInstance()    
    public method getClosingSetupModule()
    public method getClosingSetupConfigs()    
    public method getClosingSetupResponse()    
    public method postClosingSetup()
    
    private method closingSetupValidStructJson()
    private method closingSetupClearSql()
    private method setClosingSetupResponse()    
EndClass

/*/{Protheus.doc} new
    Metodo construtor
    @author Totvs
    @since 09/01/2024
    @version 12.1.2310
/*/
Method new() class ClosingSetupService
    ::lResponse := .F.
    ::cResponse := ""
    ::cResponseKey := "message"
    ::cMessage := STR0001 //"Json invalido"
Return

/*/{Protheus.doc} getInstance
    Responsavel por instanciar a classe service
    @author Totvs
    @since 09/01/2024
    @version 12.1.2310
/*/
Method getInstance() class ClosingSetupService
    static __instance As Object

    If ValType(__instance) == "U"
        __instance := ClosingSetupService():new()
    EndIf    
Return __instance

//-------------------------------------------------------------------
/*/{Protheus.doc} getClosingSetupModule
Responsavel por chamar a consulta e parsear o retorno no formato JSON

@author Totvs
/*/
//-------------------------------------------------------------------
method getClosingSetupModule() class ClosingSetupService
    Local oSettings := ClosingSetupProtheusData():getInstance() As Object
    Local jResponse := JsonObject():new() As Json
    
    jResponse["items"] := oSettings:getClosingSetupModule()    
    FreeObj(oSettings)
return jResponse

//-------------------------------------------------------------------
/*/{Protheus.doc} getClosingSetupConfigs
Responsavel por chamar a consulta e parsear o retorno no formato JSON

@author Totvs
/*/
//-------------------------------------------------------------------
method getClosingSetupConfigs(cAlias as Character) class ClosingSetupService
    Local oSettings := ClosingSetupProtheusData():getInstance() As Object
    Local jResponse := JsonObject():new() As Json
    
    jResponse['items'] := oSettings:getClosingSetupConfigs(cAlias)    
    FreeObj(oSettings)
return jResponse

/*/{Protheus.doc} postClosingSetup
    (long_description)
    @author Totvs
    @since 09/01/2024
    @version 12.1.2310   
/*/
Method postClosingSetup() class ClosingSetupService
    Local jBody := JsonObject():new() as Json        
    Local oData := ClosingSetupProtheusData():getInstance() as Object
    Local oBody := oRest:GetBodyRequest() as Object
                
    If ::closingSetupValidStructJson(oBody, jBody) //Validacoes body        
        jBody:FromJSON(DecodeUTF8(jBody:ToJSON(), "cp1252")) //Tratamento para converter o encoding
        ::closingSetupClearSql(jBody) //Limpeza de comandos SQL
        oData:saveClosingSetup(jBody)
        ::setClosingSetupResponse(.T., "", "")                                                          
    Else
        ::setClosingSetupResponse(.F., ::cMessage, "message")                
    EndIf

    FreeObj(oData)
    FreeObj(oBody)
Return

/*/{Protheus.doc} closingSetupValidStructJson
    Valida estrutura de JSON
    @author Totvs
    @since 09/01/2024
    @version 12.1.2310       
/*/  
Method closingSetupValidStructJson(oBody, jBody as Json) class ClosingSetupService
    Local lRet := .T. as Logical
            
    If ValType(oBody) != "C" .Or. ValType(jBody:fromJson(oBody)) != "U"
        ::cMessage := STR0001 //"Json invalido"
        lRet := .F.                        
    ElseIf !jBody:hasProperty("items")
        ::cMessage := STR0002 //"Não foram enviadas todas as propriedades esperadas para a API"
        lRet := .F.
    ElseIf Len(JBody["items"]) <= 0
        ::cMessage := STR0003 //"Foram enviadas propriedades com valor vazio para a API"
        lRet := .F.
    EndIf
Return lRet

/*/{Protheus.doc} closingSetupClearSql
    Valida estrutura de JSON
    @author Totvs
    @since 10/01/2024
    @version 12.1.2310       
/*/ 
Method closingSetupClearSql(jBody as Json) class ClosingSetupService
    Local cAditionalCondition := "" as Character
    Local nI := 0 as Numeric
    Local nX := 0 as Numeric
    
    If ValType(jBody) == "J" .And. Len(JBody["items"]) > 0
        For nI := 1 to Len(jBody["items"])
            For nX := 1 To Len(jBody["items"][nI]["tables"])                
                cAditionalCondition := tcClearSql(jBody["items"][nI]["tables"][nX]["aditionalcondition"])
                jBody["items"][nI]["tables"][nX]["aditionalcondition"] := cAditionalCondition
            Next nX                        
        Next nI
    EndIf
Return

/*/{Protheus.doc} getClosingSetupResponse
    get de retorno do Post
    @author Totvs
    @since 10/01/2024
    @version 12.1.2310       
/*/
Method getClosingSetupResponse() class ClosingSetupService    
Return {::lResponse, ::cResponse, ::cResponseKey}

/*/{Protheus.doc} setClosingSetupResponse
    Set de retorno do Post
    @author Totvs
    @since 10/01/2024
    @version 12.1.2310       
/*/
Method setClosingSetupResponse(lResponse as Logical, cResponse as Character, cResponseKey as Character) class ClosingSetupService
    ::lResponse := lResponse
    ::cResponse := cResponse
    ::cResponseKey := cResponseKey
Return
