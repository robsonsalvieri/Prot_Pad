#Include "BACKOFFICE.ACCOUNTINGCLOSING.CHECKBALANCE.CONTROLLER.ch"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.accountingclosing.checkbalance
using namespace totvs.protheus.backoffice.accountingclosing.util

//-------------------------------------------------------------------
/*/{Protheus.doc} CheckBalance
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class CheckBalance
    public method new()

    @Get("/api/fc/checkbalance/")
    public method getAllCheckBalance()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} CheckBalance
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class CheckBalance
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllCheckBalance
Metodo que inicia a chamada das configuracoes de match do conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllCheckBalance() class CheckBalance
    Local oService  As Object
    Local cFilBkp   As Character        
    Local lReturn   As Logical
    Local cChaveRes As Character
    Local cResponse As Character 
    Local nPage     As Numeric
    Local nPageSize As Numeric
    Local jResposta := JsonObject():New()

    //Inicilaiza variaveis
    oService  := CheckBalanceService():getInstance()    
    cFilBkp   := cFilAnt
    cChaveRes := "message"
    cResponse := ""
    cDatIni   := oRest:getQueryRequest():GetJsonText("dateini")
    cDatFin   := oRest:getQueryRequest():GetJsonText("datefin")
    nPage     := 1
    nPageSize := 10
    lReturn   := .F.
    cCurrency := oRest:getQueryRequest():GetJsonText("currency")
    cUUIDBalance := oRest:getQueryRequest():GetJsonText("uuid")
    
    If ValType(cDatIni)=="C" .And. !Empty(cDatIni) .And. cDatIni <> "null"
        If ValType(cDatFin)=="C" .And. !Empty(cDatFin) .And. cDatFin <> "null" 
            If cDatFin >= cDatIni
                
                fcGetPageAndPageSize(@nPage, @nPageSize)
                jResposta:fromJson(oService:getAllCheckBalance(cDatIni, cDatFin, cCurrency,  nPage, nPageSize, cUUIDBalance)) 

                If(ValType(jResposta:GetJsonText("items")) <> "U")
                    lReturn := .T.
                    cChaveRes := "data" 
                    fcAnswerRest(jResposta, .T.)
                EndIf 

                //Restaura empresa e filial
                cFilAnt := cFilBkp
            Else
                cResponse := STR0001 //"A data final não pode ser menor do que a data inicial."
            EndIf        
        Else
            cResponse := STR0002 //"A data final não foi informada."
        EndIf
    Else
        cResponse := STR0003 //"A data inicial não foi informada."
    EndIf

    fcSetResponse(lReturn, cChaveRes, cResponse, lReturn)

return
