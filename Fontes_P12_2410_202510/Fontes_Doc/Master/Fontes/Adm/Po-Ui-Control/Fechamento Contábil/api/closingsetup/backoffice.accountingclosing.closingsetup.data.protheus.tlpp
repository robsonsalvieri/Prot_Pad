#include "tlpp-core.th"
#include "backoffice.accountingclosing.closingsetup.data.protheus.ch"

namespace totvs.protheus.backoffice.accountingclosing.closingsetup
using namespace totvs.protheus.backoffice.accountingclosing.util
using namespace totvs.protheus.backoffice.reconciliation.util

/*/{Protheus.doc} ClosingSetupProtheusData    
    @author Totvs
    @since 09/01/2024
    @version 12.1.2310
    /*/
class ClosingSetupProtheusData
    Public method new()
    Public method getInstance() as Object
    Public method getClosingSetupModule()
    Public method getClosingSetupConfigs()
    Public method saveClosingSetup() as Array

    private method addTable()
endClass

/*/{Protheus.doc} new
    Metodo construtor
    @author Totvs
    @since 09/01/2024
    @version 12.1.2310
    /*/
method new() class ClosingSetupProtheusData
return

method getInstance() class ClosingSetupProtheusData as Object
    static __instanceData As Object

    If ValType(__instanceData) == "U"
        __instanceData := ClosingSetupProtheusData():new()
    EndIf
return __instanceData

//-------------------------------------------------------------------
/*/{Protheus.doc} getClosingSetupConfigs
Metodo responsavel pela busca dos modulos que possuem LP contabil

@author Totvs
/*/
//-------------------------------------------------------------------
method getClosingSetupModule() class ClosingSetupProtheusData
    Local aArea     := GetArea() As Array
    Local cCvaFil   := GetBranchiesForConditional("CVA", "CTBA960") As Character
    Local cCtlFil   := GetBranchiesForConditional("CTL", "CTBA960") As Character
    Local cAliasSQL := GetNextAlias()
    Local cQuery    := "" As Character
    Local cModulo   := "" As Character
    Local nLen      := 0  As Numeric
    Local nPos      := 0  As Numeric
    Local aRet      := {} As Array
    Local aTables   := {} As Array
    Local aModulo   := RetModName() As Array
     
    cQuery := " SELECT CVA_MODULO, CTL_ALIAS " 
    cQuery += " FROM " + RetSQLName("CVA") + " CVA " 
    cQuery += " INNER JOIN " + RetSQLName("CTL") + " CTL ON " 
    cQuery += " CVA_FILIAL = CTL_FILIAL AND "
    cQuery += " CVA_CODIGO = CTL_LP " 
    cQuery += " WHERE CVA.D_E_L_E_T_ = ' '"
    cQuery += " AND CTL.D_E_L_E_T_ = ' ' "
    If ValType(cCvaFil) == "C" .and. !Empty(cCvaFil)
        cQuery += " AND CTL."+cCtlFil
        cQuery += " AND CVA."+cCvaFil
    EndIf
    cQuery += " GROUP BY CVA_MODULO, CTL_ALIAS "
    cQuery += " ORDER BY CVA_MODULO "
    cQuery := ChangeQuery(cQuery)

    MPSysOpenQuery(cQuery,cAliasSQL)
	DbSelectArea((cAliasSQL))
    While (cAliasSQL)->(!EoF()) 
        If cModulo == (cAliasSQL)->CVA_MODULO
            Aadd(aTables,JsonObject():new())
            nLen := len(aTables)
            aTables[nLen]["table"] :=(cAliasSQL)->CTL_ALIAS
            aTables[nLen]["description"] := FwSX2Util():GetX2Name((cAliasSQL)->CTL_ALIAS)
        Else
            cModulo := (cAliasSQL)->CVA_MODULO
            If len(aTables) > 0
                nLen := len(aRet)
                aRet[nLen]["tables"] := aTables
            EndIf
            aTables := {}
            Aadd(aRet,JsonObject():new())
            nLen := len(aRet)
            aRet[nLen]["module"] := (cAliasSQL)->CVA_MODULO
            nPos := Ascan( aModulo, {|x| x[1] == VAL((cAliasSQL)->CVA_MODULO )} )
            If nPos > 0
                aRet[nLen]["modname"] := aModulo[nPos][2]
                aRet[nLen]["moddescription"] := aModulo[nPos][3]
            EndIf

            Aadd(aTables,JsonObject():new())
            nLen := len(aTables)
            aTables[nLen]["table"] :=(cAliasSQL)->CTL_ALIAS
            aTables[nLen]["description"] :=FwSX2Util():GetX2Name((cAliasSQL)->CTL_ALIAS) 
        EndIf
        
        (cAliasSQL)->(dbSkip())        
    EndDo

    If len(aTables) > 0
        nLen := len(aRet)
        aRet[nLen]["tables"] := aTables
    EndIF
    (cAliasSQL)->(DbCloseArea())

    RestArea(aArea)
    FwFreeArray(aArea)
return aRet

//-------------------------------------------------------------------
/*/{Protheus.doc} getClosingSetupConfigs
Metodo responsavel pela busca dos configuracoes dos modulos

@author Totvs
/*/
//-------------------------------------------------------------------
method getClosingSetupConfigs(cAlias as character) class ClosingSetupProtheusData
    Local aRet :={}
    ::addTable(cAlias,aRet)
return aRet

//-------------------------------------------------------------------
/*/{Protheus.doc} addTable
Metodo responsavel em adicionar as configuracoes dos modulos

@author Totvs
/*/
//-------------------------------------------------------------------
method addTable(cAlias as character,aTable as array) class ClosingSetupProtheusData
    Local cCpUuid     := "" As Character
    Local cCpFilial   := "" As Character
    Local nlen        := 0  As Numeric
    Local nI          := 0  As Numeric
    Local aFields     := {} As Array
    Local aDatefields := {} As Array
    Local aflagfield  := {} As Array
    
    aFields := FWSX3Util():GetAllFields(cAlias) //Retorna os campos de um alias específico da SX3.

    For nI := 1 To Len(aFields)
        Do Case
            Case "_MSUIDT" $  aFields[nI] 
                cCpUuid := aFields[nI]
            Case "_FILIAL" $ aFields[nI]
                cCpFilial := aFields[nI] 
            Case "_DTLANC" $ aFields[nI] .Or. "_LA" $ aFields[nI]
                Aadd(aflagfield,aFields[nI]) // sugestoes para campo flag
            Case FWSX3Util():GetFieldType( aFields[nI] ) == 'D'
                Aadd(aDatefields,aFields[nI])
	    EndCase
    Next nI
    
    If !Empty(cCpUuid) .And. Len(aDatefields) > 0
        Aadd(aTable,JsonObject():new())
        nlen := Len(aTable)
        aTable[nLen]["branchfield"] := cCpFilial
        aTable[nLen]["uuidfield"]   := cCpUuid
        aTable[nLen]["aflagfield"]  := aflagfield
        aTable[nLen]["adatefield"]  := aDatefields
    Else 
        Aadd(aTable,JsonObject():new())
        nLen := Len(aTable)
        aTable[nLen]["error"] := ""
        If Empty(cCpUuid)
            aTable[nLen]["error"] += STR0001 //"|Tabela não possui campo UUID|"
        EndIf
        If Len(aDatefields) < 1
            aTable[nLen]["error"] += STR0002 //"|Tabela não possui campos de Data|"
        EndIf
    EndIf
Return

/*/{Protheus.doc} saveClosingSetup
    Método que grava o JSON na tabela QLF
    @author Totvs
    @since 09/01/2024
    @version 12.1.2310
    @param jBody    
/*/
Method saveClosingSetup(jBody) class ClosingSetupProtheusData     
    Local cCodModule := "" as Character
    Local nI         := 0  as Numeric
    Local lInsert    := .F. as Logical
        
    For nI := 1 to Len(jBody["items"])
        cCodModule := jBody["items"][nI]["QLF_MODULE"]
        lInsert := !QLF->(dbSeek(xFilial("QLF")+cCodModule))
        
        QLF->(Reclock("QLF", lInsert))                                
        QLF->QLF_FILIAL := xFilial("QLF")
        QLF->QLF_MODULE := cCodModule
        QLF->QLF_MODNAM := jBody["items"][nI]["QLF_MODNAM"]
        QLF->QLF_TABLES := fcArrayToObject("tables", jBody["items"][nI]["tables"])
        QLF->QLF_VERSIO := GetJsonVersion(cCodModule)                                                                                                                                    
        QLF->(MsUnlock())                                                    
    Next nI        
Return
