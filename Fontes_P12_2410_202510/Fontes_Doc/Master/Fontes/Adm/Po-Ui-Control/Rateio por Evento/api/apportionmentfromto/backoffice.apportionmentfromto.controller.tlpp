#include "tlpp-core.th"
#include "backoffice.apportionmentfromto.controller.ch"

namespace totvs.protheus.backoffice.apportionmentFromTo
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} ApportionmentFromTo
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class apportionmentFromTo
    private Data nOpcAuto as numeric

    public method new()    

    @Get("/api/ctb/apportionmentfromto/") // Consultar
    public method getAllApportionmentFromTo()

    @Get('api/ctb/apportionmentfromto/:id')
    public method getApportionmentFromToById()

    @Post("/api/ctb/apportionmentfromto/") // Incluir / Alterar
    public method postApportionmentFromTo()

    @Delete("/api/ctb/apportionmentfromto/") // Deletar
    public method deleteApportionmentFromTo()

    private method setApportionmentFromTo()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} ApportionmentFromTo
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class ApportionmentFromTo    
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllApportionmentFromTo
Metodo que inicia a chamada get da Cadastro de De Para

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllApportionmentFromTo(cId as character)  class ApportionmentFromTo
    Local oService  As Object
    Local oField    As Object
    Local jResposta As Json
    Local cFilBkp   As Character
    Local nPage     As Numeric
    Local nPageSize As Numeric
    Local nAt       As Numeric
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character
    Default cId := ""
	
    //Inicilaiza variaveis
    oService  := ApportionmentFromToService():getInstance()
    oField    := tcTreatsFilter():new()
    jResposta := JsonObject():new()
    cFilBkp   := cFilAnt
    nPage     := 1
    nPageSize := 10
    nAt       := 0
    lResponse := .F.
    cChaveRes := "message"
    cResponse := STR0001 //"Resposta inválida."

    tcGetHeaders()
    tcGetPageAndPageSize(@nPage, @nPageSize)

    jResposta:fromJson(oService:getAllApportionmentFromTo(nPage, nPageSize, cId))

    IIf(ValType(jResposta:GetJsonText("items")) <> "U",; 
        tcAnswerRest(jResposta, .T.),;    
        tcSetResponse(lResponse, cChaveRes, cResponse, .F.))

    //Restaura empresa e filial
    cFilAnt := cFilBkp
return

//-------------------------------------------------------------------
/*/{Protheus.doc} postApportionmentFromTo
Metodo que inicia a chamada de Inclusao da Cadastro de De Para

@author Totvs
/*/
//-------------------------------------------------------------------
method postApportionmentFromTo() class ApportionmentFromTo
    ::nOpcAuto := 4
    ::setApportionmentFromTo()
return

//-------------------------------------------------------------------
/*/{Protheus.doc} deleteApportionmentFromTo
Metodo que inicia a chamada de exclusao da Cadastro de De Para

@author Totvs
/*/
//-------------------------------------------------------------------
method deleteApportionmentFromTo() class ApportionmentFromTo
    ::nOpcAuto := 5
    ::setApportionmentFromTo()
return

//-------------------------------------------------------------------
/*/{Protheus.doc} setApportionmentFromTo
Metodo que inicia a chamada do CRUD para Cadastro de De Para

@author Totvs
/*/
//-------------------------------------------------------------------
method setApportionmentFromTo() class ApportionmentFromTo
    Local cFilBkp   := cFilAnt as Character
    Local cChaveRes := "message" as Character
    Local cBody     := oRest:getBodyRequest() as Object    
    Local cResponse := STR0002 as Character //"Conteúdo do corpo inválido."
    Local jBody     := JsonObject():new() as Json    
    Local aResponse := {} as Array    
    Local oService  := ApportionmentFromToService():getInstance() as Object    
    Local lResponse := .F. as Logical        
    Local uRet      := Nil

    If ValType(cBody) == "C"
        uRet := jBody:fromJson(cBody)
        If ValType(uRet) == "U"            
            tcGetHeaders()            
            aResponse := oService:setApportionmentFromTo(jBody, ::nOpcAuto)          
            
            If ValType(aResponse) == "A" .And. Len(aResponse) > 1
                If aResponse[1]
                    lResponse := aResponse[1]
                    cResponse := aResponse[2]                    
                EndIf             
            EndIf        
        Else
            cResponse := STR0003 //"Ocorreu um erro inesperado ao gravar a tabela de DE/PARA"
        EndIf
    EndIf    
    
    tcSetResponse(lResponse, cChaveRes, cResponse, .F.)
    FwFreeArray(aResponse)
    FreeObj(oService)                

    //Restaura empresa e filial
    cFilAnt := cFilBkp
return

//-------------------------------------------------------------------
/*/{Protheus.doc} postApportionmentFromTo
Metodo que inicia a chamada get da Cadastro de Codigo DePara com id

@author Totvs
/*/
//-------------------------------------------------------------------
method getApportionmentFromToById() class ApportionmentFromTo
    Local cId := oRest:getPathParamsRequest()["id"] as Character
    ::getAllApportionmentFromTo(cId)
return
