#include "tlpp-core.th"

namespace totvs.protheus.backoffice.reconciliation.deleteassetgroup
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} DeleteAssetGroup
Classe principal de controle, onde e inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class DeleteAssetGroup
public method new()

@Post("/api/rt/deletedatagroup/")
public method getDeleteAssetGroup()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} DeleteAssetGroup
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class DeleteAssetGroup
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getDeleteAssetGroup
Metodo que inicia a chamada da DeleteAssetGroup

@author Totvs
/*/
//-------------------------------------------------------------------
method getDeleteAssetGroup() class DeleteAssetGroup
Local oService  := DeleteAssetGroupService():getInstance() as Object
Local cBody     := oRest:getBodyRequest() as Object
Local jBody     := JsonObject():new() as Json
Local aResponse := {} as Array
Local cFilBkp   := cFilAnt as Character
Local uRet      := Nil
Local lPadrao   := .F. as Logical
Local lResponse := .F. as Logical
Local cChaveRes := "message" as Character
Local cResponse := "Conteúdo do corpo inválido." as Character

If ValType(cBody) == "C"
    uRet := jBody:fromJson(cBody)
    If ValType(uRet) == "U"
        tcGetHeaders() //Posiciona na filial passada pelo tenantId
        aResponse := oService:DeleteAssetGroupByBody(jBody)

        If ValType(aResponse) == "A" .And. Len(aResponse) > 1
            If aResponse[1]
                lPadrao   := .T.
                lResponse := aResponse[1]
                cResponse := aResponse[2]
            Else
                cResponse := aResponse[2]
            EndIf
        EndIf
    EndIf
EndIf

tcSetResponse(lResponse, cChaveRes, cResponse, lPadrao)
FwFreeArray(aResponse)
FreeObj(oService)

//Restaura empresa e filial
cFilAnt := cFilBkp

Return
