#include "tlpp-core.th"
#include "backoffice.revitalization.getassets.controller.ch"

namespace totvs.protheus.backoffice.revitalization.getassets
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAssets
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class GetAssets

    Data jResposta	as json
    Data Page       as Numeric
	Data PageSize   as Numeric
	Data aFieldsApi as Array
	Data aUrlFilter as Array
    Data jBody      as json
    Data oService   as Object

    public method new()

    @Get("/api/rt/getdata/")
    public method getAllGetAssets()

    @Post("/api/rt/getdata/")
    public method getassetsbyfilter()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAssets
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class GetAssets
	::page       := 1
	::pagesize   := 10
	::aFieldsApi := {}
	::aUrlFilter := {}
    ::oService   := GetAssetsService():getInstance()
	::jResposta  := JsonObject():New()
    ::jBody 	 := JsonObject():New()
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllGetAssetss
Metodo que retorna as contas contabeis (Consulta padrão)

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllGetAssets() class GetAssets
    Local oService  As Object
    Local jResposta As Json
    Local cFilBkp   As Character
    Local cFilters  As Character
    Local cField    As Character
    Local cType     As Character
    Local nPage     As Numeric
    Local nPageSize As Numeric
    Local nAt       As Numeric
    Local aFilters  As Array
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character
    Local cFil As Character
    Local oOpcFilter As Json
    Local cOpcFilter As Character

    //Inicilaiza variaveis
    oService  := GetAssetsService():getInstance()
    jResposta := JsonObject():new()
    cFilBkp   := cFilAnt
    cFilters  := ""
    cField    := tcGetQryParams("field")
    cType     := tcGetQryParams("type")
    cFil     := tcGetQryParams("filial")
    aFilters  := {}
    nPage     := 1
    nPageSize := 10
    nAt       := 0
    lResponse := .F.
    cChaveRes := "message"
    cResponse :=  STR0001 //"Resposta inválida."
    cOpcFilter:= tcGetQryParams("copcfilter")
    

    If ! Empty(cOpcFilter)
        oOpcFilter := JsonObject():new()
        oOpcFilter:FromJSON( cOpcFilter )
    EndIf

    tcGetHeaders()
    aFilters := tcGetFilters()

    If ValType(aFilters) == "A" .And. Len(aFilters) > 0
        If cType == "2" .And. AllTrim(Upper(cField)) == "N1_ITEM" .And. ("," $ aFilters[1][2])
            nAt := At(",", aFilters[1][2])
            cFilters := Padr(SubStr(aFilters[1][2], 1, nAt-1), TamSx3("N1_CBASE")[1])
            cFilters += Padr(SubStr(aFilters[1][2], nAt+1, Len(aFilters[1][2])),TamSx3("N1_ITEM")[1])
        Else
            nAt := At("=", aFilters[1][2])
            cFilters := RTrim(SubStr(aFilters[1][2], nAt+1, Len(aFilters[1][2])))
        EndIf
    EndIf

    tcGetPageAndPageSize(@nPage, @nPageSize)

    jResposta:fromJson(oService:getAllGetAssets(cFilters, nPage, nPageSize, cField, cType,cFil,oOpcFilter))

    IIf(ValType(jResposta:GetJsonText("items")) <> "U",;
        tcAnswerRest(jResposta, .T.),;
        tcSetResponse(lResponse, cChaveRes, cResponse, .F.))

    //Restaura empresa e filial
    cFilAnt := cFilBkp

    //Limpa array
	FWFreeArray(aFilters)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getassetsbyfilter
Metodo que realiza o filtro composto dos itens de ativo

@author Totvs
/*/
//-------------------------------------------------------------------
method getassetsbyfilter() class GetAssets

    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character
    Local jResposta As Json
	Local cBody  := oRest:getBodyRequest() As Object

    jResposta    := JsonObject():new()
    lResponse    := .F.
    cChaveRes    := "message"
    cResponse    :=  STR0001 //"Resposta inválida."

	tcGetPageAndPageSize(@::page, @::pageSize)
	If ValType(cBody) == "C"
		::jBody:fromJson(cBody)
		::jResposta:fromJson(::oService:getassetsbyfilter(::page,::pageSize,::jBody))
	EndIf

    IIf(ValType(::jResposta:GetJsonText("items")) <> "U",;
        tcAnswerRest(::jResposta, .T.),;
        tcSetResponse(lResponse, cChaveRes, cResponse, .F.))
return
