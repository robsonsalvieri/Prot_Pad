#INCLUDE "ATFA220.CH"
#Include "Protheus.ch"
// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.
Static lFWCodFil := .T.

//********************************
// Controle de multiplas moedas  *
//********************************
Static lMultMoed := .T.

/*/


Ŀ
Funo     ATFA220   Autor  Alice Y Yamamoto       Data  05/10/98 
Ĵ
Descrio  Ajuste contabil dos bens inventariados                     
Ĵ
Sintaxe    ATFA220()                                                  
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function ATFA220
//Ŀ
// Define Variaveis                                               
//
Local aSays:={}, aButtons:={}
Local nOpca 		:= 0

Private cCadastro  	:= STR0004     //"Ajust.Cont. de Bens Inventariados"	

//Ŀ
// Variavel incluida para contabilizao em diario, Correlativo   
//
Private cCodDiario := ""
PRIVATE aDiario:= {}

ATFXKERNEL()
 
dbSelectArea("SN8")
dbSetOrder(1 )

Pergunte("AFA220",.F.)
AADD(aSays,STR0005) //"Este programa tem o objetivo de efetuar o ajuste contabil "
AADD(aSays,STR0006) //"dos bens inventariados. Os bens inventariados devem ser a-"
AADD(aSays,STR0007) //"justados antes do proximo calculo."

//Ŀ
// Inicializa o log de processamento                            
//
ProcLogIni( aButtons )

AADD(aButtons, { 5,.T.,{|| Pergunte("AFA220",.T. ) } } )
AADD(aButtons, { 1,.T.,{|| nOpca := 1,	IF( AtfOK(),FechaBatch(),nOpca := 0 ) }} )
AADD(aButtons, { 2,.T.,{|| FechaBatch() }} )

FormBatch( cCadastro, aSays, aButtons ,,,380)

If nOpca == 1     
	//Ŀ
	// Atualiza o log de processamento   
	//
	ProcLogAtu("INICIO")   
	
	If MV_PAR06 == 1 .And. !Empty(xFilial("SN1"))// Seleciona filiais
		Processa({ |lEnd| Afa220Fil(MV_PAR07,MV_PAR08) })
	Else
		Processa({ |lEnd| Afa220Proc() })
	EndIf
			
	//Ŀ
	// Atualiza o log de processamento   
	//
	ProcLogAtu("FIM")
Endif	

Return 
/*


ͻ
Programa  ATFA220 Autor  Alvaro Camillo Neto  Data   21/09/09   
͹
Desc.     Executa o processamento para cada filial                    
                                                                      
͹
Uso        ATFA200                                                  
ͼ


*/
Static Function Afa220Fil(cFilDe,cFilAte)
Local cFilIni 	:= cFIlAnt
Local aArea		:= GetArea()
Local nInc		:= 0
Local aSM0		:= AdmAbreSM0()

For nInc := 1 To Len( aSM0 )
	If aSM0[nInc][1] == cEmpAnt .AND. cFilDe <= aSM0[nInc][2] .AND. aSM0[nInc][2] <= cFilAte
		cFilAnt := aSM0[nInc][2]
		ProcLogAtu("MENSAGEM","EXECUTANDO O PROCESSO DA FILIAL "  + cFilAnt) 
		Afa220Proc()
	EndIf
Next

cFIlAnt := cFilIni
RestArea(aArea)
Return

/*/


Ŀ
Funo	 AFA220Proc Autor  Alice Yamamoto	     Data 05/10/98  
Ĵ
Descrio  Efetua o ajuste contabil e atualiza os campos dos arqs.    
Ĵ
Sintaxe e  AFA220Proc            									  
Ĵ
 Uso		  															  
ٱ


*/
Function AFA220Proc()
Local aArea := { Alias(), IndexOrd() }
//********************************
// Controle de multiplas moedas  *
//********************************
Local __nQuantas := If(lMultMoed,AtfMoedas(),5)
Local aVlrResid := If(lMultMoed, AtfMultMoe(,,{|x| 0}) , {0,0,0,0,0} )
Local aVlrAjust := If(lMultMoed, AtfMultMoe(,,{|x| 0}) , {0,0,0,0,0} )
Local aTaxa     := If(lMultMoed, AtfMultMoe(,,{|x| 0}) , {0,0,0,0,0} )
Local nX
Local lPadrao    := .F.
Local lPrim      := .T.
Local nQtdAnt    := 0       //Qtd anterior ao inventario
Local dData      := Lastday(GetMv("MV_ULTDEPR")+1)
Local cN1TipoNeg := Alltrim(SuperGetMv("MV_N1TPNEG",.F.,"")) // Tipos de N1_PATRIM que aceitam Valor originais negativos
Local cN3TipoNeg := Alltrim(SuperGetMv("MV_N3TPNEG",.F.,"")) // Tipos de N3_TIPO que aceitam Valor originais negativos
Local nK		 := 0	
Local lSeqCorr   :=  UsaSeqCor()
Local cIDMOV	:= "" 
Local cTpSaldo	:= ""
Local lSN3Saldo := .T.
Local cOcorr 	:= ""
Local aDadosComp :={}
Local aValores   := {}
Local lBlqInv	:= SuperGetMv("MV_BLQINVE",.F.,"2") == "1"
Local cStatus	:= "1" 
Local lOnOff	:= .T.
Local lDigita := .T.
Local lAglutina := .T.

Private aRotina  := MenuDef()
Private cMoedaAtf:= Getmv("MV_ATFMOED")
Private cLoteAtf := LoteCont("ATF")
Private Valor    := 0       //Valor a ser contabilizado
Private cPadrao  := "827"
Private cArquivo := ""
Private nTotal   := 0
Private nHdlPrv  := 0

If MV_PAR06 == 1 .And. Empty(xFilial("SN1")) 
	ProcLogAtu("MENSAGEM","TRATAMENTO MULTI FILIAL DESABILITADO: SN1 COMPARTILHADO") 
EndIf

//Ŀ
// Verifica se existe lancamento padrao. 											
//
lPadrao := VerPadrao(cPadrao)
If !lPadrao
	Help(" ",1,"220LPADR")   //O lancto padrao 827 na esta cadastrado
	//Ŀ
	// Atualiza o log de processamento com o erro  
	//
	ProcLogAtu("ERRO","220LPADR",Ap5GetHelp("220LPADR"))

	dbSelectArea( aArea[1] )
	dbSetOrder( aArea[2] )
	Return
EndIf

//Ŀ
// Verifica se existe lancamento padrao. 											
//
If dDatabase > dData .or. dDataBase  <= Getmv("MV_ULTDEPR")                   
	Help(" ",1,"220DATA")     //A database deve ser maior a data do ltimo cl-
	//Ŀ
	// Atualiza o log de processamento com o erro  
	//
	ProcLogAtu("ERRO","220DATA",Ap5GetHelp("220DATA"))

	dbSelectArea( aArea[1] )  //culo e imediatamente subsequente. O inventrio 
	dbSetOrder( aArea[2] )    //no  vlido para meses futuros.
	Return
Endif

//Ŀ
// Carrega as perguntas selecionadas:                       
// mv_par01 - 1 Converter pela Tx. Ult. Calc.               
//            2 Converter pela Tx da DataBase               
//            3 Informar taxa para conversao                
// mv_par02 - Taxa de conversao para a moeda 2              
// mv_par03 - Taxa de conversao para a moeda 3              
// mv_par04 - Taxa de conversao para a moeda 4              
// mv_par05 - Taxa de conversao para a moeda 5              
// mv_par06 - Seleciona Filial ?       				  	     
// mv_par07 - Filial de       								     
// mv_par08 - Filial at	             					   
// mv_par09 - Contabiliza On-Line							     
// mv_par10 - Mostra Lanamentos							     
// mv_par11 - Aglutina Lanamentos						     
//

dbSelectArea("SM2")
If mv_par01 = 2                //conversao pela dDatabase
	dbSeek(dDatabase)
	//********************************
	// Controle de multiplas moedas  *
	//********************************
	If lMultMoed
		aTaxa	:= AtfMultMoe(,,{|x| If(x=1,1,SM2->&( "M2_MOEDA"+Alltrim(Str(x)) ))})
	Else
		aTaxa[2]  := SM2->M2_MOEDA2
		aTaxa[3]  := SM2->M2_MOEDA3
		aTaxa[4]  := SM2->M2_MOEDA4
		aTaxa[5]  := SM2->M2_MOEDA5
	EndIf
ElseIf mv_par01 == 3           //converter por taxa informada
	aTaxa[2] := mv_par02
	ataxa[3] := mv_par03
	ataxa[4] := mv_par04
	ataxa[5] := mv_par05
EndIf

lOnOff := MV_PAR09 == 1
lDigita := MV_PAR10 == 1
lAglutina := MV_PAR11 == 1

dbSelectArea("SN8")
dbSetOrder(1)
dbSeek(xFilial("SN8"),.T.)
ProcRegua( RecCount() )

While !Eof() .And. xFilial("SN8") == SN8->N8_FILIAL

	IncProc()
	
	If !Empty(SN8->N8_DTAJUST)
		dbSelectArea("SN8")
		dbSkip()
		loop
	EndIf
	dbSelectArea("SN1")
	dbSetOrder(1)
	If !dbSeek(xFilial("SN1")+SN8->N8_CBASE+SN8->N8_ITEM)
		dbSelectArea("SN8")
		dbskip()
		loop
	EndIf
	
	If ATFXVerPrj(SN1->N1_CBASE,SN1->N1_ITEM) 
		dbSelectArea("SN8")
		dbskip()
		loop
	EndIf 
	
	nQtdAnt := SN1->N1_QUANTD
	dbSelectArea("SN3")
	dbSetOrder(1)
	If !dbSeek(xFilial("SN3")+SN8->N8_CBASE+SN8->N8_ITEM+SN8->N8_TIPO)
		dbSelectArea("SN8")
		dbskip()
		loop
	EndIf	

	//Ŀ
	// Caso a conversao seja pela taxa do ltimo clculo, levar em consi -
	//deraao as seguintes itens:                                         
	// Aquisicoes anteriores  a 01/01/1996 - converter pela taxa de 0.8287
	// Aquisicoes posteriores a 01/01/1996 - converter pela taxa do dia de
	//aquisicao do bem que  a mesma utilizada no clculo                 
	//
	If mv_par01 == 1  //CONVERSO PELA TAXA DO LTIMO CLCULO
		If DTOS(SN3->N3_AQUISIC) < "19960101"
			aTaxa[2] := Round((SN3->N3_VORIG1+SN3->N3_VRCACM1+SN3->N3_AMPLIA1)/(SN3->N3_VORIG2+SN3->N3_AMPLIA2),X3Decimal("N3_TXDEPR2"))
			aTaxa[3] := 0.8287
			aTaxa[4] := Round((SN3->N3_VORIG1+SN3->N3_VRCACM1+SN3->N3_AMPLIA1)/(SN3->N3_VORIG4+SN3->N3_AMPLIA4),X3Decimal("N3_TXDEPR4"))
			aTaxa[5] := Round((SN3->N3_VORIG1+SN3->N3_VRCACM1+SN3->N3_AMPLIA1)/(SN3->N3_VORIG5+SN3->N3_AMPLIA5),X3Decimal("N3_TXDEPR5"))
		Else
			dbSelectArea("SM2")
			If dbSeek(SN3->N3_AQUISIC)
				//********************************
				// Controle de multiplas moedas  *
				//********************************
				If lMultMoed
				aTaxa	:= AtfMultMoe(,,{|x| If(x=1,1, SM2->&("M2_MOEDA"+Alltrim(Str(x))) ) })
				Else
					aTaxa[2] := SM2->M2_MOEDA2
					aTaxa[3] := SM2->M2_MOEDA3
					aTaxa[4] := SM2->M2_MOEDA4
					aTaxa[5] := SM2->M2_MOEDA5
				EndIf
			Else
				//********************************
				// Controle de multiplas moedas  *
				//********************************
				For nX	:= 2 to __nQuantas
					cMoed	:= Alltrim(str(nX))
					If nX=3
						aTaxa[nX] := 0.8287
					Else						
						If nX>9
							aTaxa[nX] :=Round((SN3->&("N3_VORIG"+cMoed)+SN3->&("N3_VRCAC"+cMoed)+SN3->&("N3_AMPLI"+cMoed))/(SN3->&("N3_VORIG"+cMoed)+SN3->&("N3_AMPLI"+cMoed)),X3Decimal("N3_TXDEP"+cMoed))
						Else
							aTaxa[nX] :=Round((SN3->&("N3_VORIG"+cMoed)+SN3->&("N3_VRCACM"+cMoed)+SN3->&("N3_AMPLIA"+cMoed))/(SN3->&("N3_VORIG"+cMoed)+SN3->&("N3_AMPLIA"+cMoed)),X3Decimal("N3_TXDEPR"+cMoed))
						EndIf
					EndIf
				Next
			Endif
		Endif
	Endif
    
	If lBlqInv 		
		//Ŀ
		// Bloqueia ou desbloqueia o bem de acordo com o local informado.      
		//
		cStatus := "1"	
		If !Empty(SN1->N1_LOCAL)
			dbSelectArea("SNL")
			SNL->(dbSetOrder(1))
			If SNL->(dbSeek( xFilial("SNL") + SN1->N1_LOCAL))
				If SNL->NL_BLOQ == "1"
					cStatus := "3"
				EndIf
			EndIf
		EndIf			
	EndIf	

	Reclock("SN1",.F.)
	SN1->N1_QUANTD := IIf(SN8->N8_QTDINV != nQtdant .and.	SN8->N8_QTDINV != 0, SN8->N8_QTDINV, SN1->N1_QUANTD)
	If lBlqInv 
		SN1->N1_STATUS := cStatus
	EndIf
	MsUnlock()

	Reclock("SN8",.F.)
	SN8->N8_DTAJUST := dDataBase
	MsUnlock()

	//Ŀ
	// Calculo do Valor residual nas 5 moedas                       
	//
	If SN3->N3_TIPO = "05" .Or. SN1->N1_PATRIM $ cN1TipoNeg .Or. SN3->N3_TIPO $ cN3TipoNeg
		//********************************
		// Controle de multiplas moedas  *
		//********************************
		aVlrResid[1] := (SN3->N3_VORIG1+SN3->N3_VRCACM1+SN3->N3_AMPLIA1) + Abs((SN3->N3_VRDACM1+SN3->N3_VRCDA1))
		For nX := 2 to __nQuantas
			cMoed	:= Alltrim(Str(nX))
			If nX>9
				aVlrResid[nX] := (SN3->&("N3_VORIG"+cMoed)+SN3->&("N3_AMPLI"+cMoed)) + Abs(SN3->&("N3_VRDAC"+cMoed))
			Else
				aVlrResid[nX] := (SN3->&("N3_VORIG"+cMoed)+SN3->&("N3_AMPLIA"+cMoed)) + Abs(SN3->&("N3_VRDACM"+cMoed))
			EndIf
		Next
	Else
		//********************************
		// Controle de multiplas moedas  *
		//********************************
		aVlrResid[1] := (SN3->N3_VORIG1+SN3->N3_VRCACM1+SN3->N3_AMPLIA1) - (SN3->N3_VRDACM1+SN3->N3_VRCDA1)
		For nX := 2 to __nQuantas
			cMoed	:= Alltrim(Str(nX))
			If nX>9
				aVlrResid[nX] := (SN3->&("N3_VORIG"+cMoed)+SN3->&("N3_AMPLI"+cMoed)) - Abs(SN3->&("N3_VRDAC"+cMoed))
			Else
				aVlrResid[nX] := (SN3->&("N3_VORIG"+cMoed)+SN3->&("N3_AMPLIA"+cMoed)) - Abs(SN3->&("N3_VRDACM"+cMoed))
			EndIf
		Next
	Endif

	//Ŀ
	// O valor do ajuste (nVlrAjust1)  calculada com a seguinte    
	// frmula : nvlrAjust1 = nVlrResid1 - Valor Inventariado. O va-
	// de ajuste  somado na depreciao acumulada do bem.          
	//
	//********************************
	// Controle de multiplas moedas  *
	//********************************
	aVlrAjust[1] := aVlrResid[1] - SN8->N8_VLRINV
	For nX := 2 to __nQuantas
		cMoed	:= Alltrim(Str(nX))
		aVlrAjust[nX] := Round(aVlrResid[nX] - ( SN8->N8_VLRINV / aTaxa[nX] ),X3Decimal("N3_VORIG"+cMoed))
	Next

	Valor := Abs(aVlrAjust[1])

	Reclock("SN3",.F.)
	//********************************
	// Controle de multiplas moedas  *
	//********************************
	If lMultMoed
		AtfMultMoe("SN3","N3_VRDACM",{|x| SN3->&(If(x>9,"N3_VRDAC","N3_VRDACM")+Alltrim(Str(x))) + aVlrAjust[x] })
	Else
		SN3->N3_VRDACM1 += aVlrAjust[1]
		SN3->N3_VRDACM2 += aVlrAjust[2]
		SN3->N3_VRDACM3 += aVlrAjust[3]
		SN3->N3_VRDACM4 += aVlrAjust[4]
		SN3->N3_VRDACM5 += aVlrAjust[5]
	EndIf

	//********************************
	// Controle de multiplas moedas  *
	//********************************
	If lMultMoed
		AtfMultMoe("SN3","N3_VRDBAL",{|x| SN3->&(If(x>9,"N3_VRDBA","N3_VRDBAL")+Alltrim(Str(x))) + aVlrAjust[x] })
	Else
		SN3->N3_VRDBAL1 += aVlrAjust[1]
		SN3->N3_VRDBAL2 += aVlrAjust[2]
		SN3->N3_VRDBAL3 += aVlrAjust[3]
		SN3->N3_VRDBAL4 += aVlrAjust[4]
		SN3->N3_VRDBAL5 += aVlrAjust[5]
	EndIf

	//********************************
	// Controle de multiplas moedas  *
	//********************************
	If lMultMoed
		AtfMultMoe("SN3","N3_VRDMES",{|x| aVlrAjust[x] })
	Else
		SN3->N3_VRDMES1 := aVlrAjust[1]
		SN3->N3_VRDMES2 := aVlrAjust[2]
		SN3->N3_VRDMES3 := aVlrAjust[3]
		SN3->N3_VRDMES4 := aVlrAjust[4]
		SN3->N3_VRDMES5 := aVlrAjust[5]
	EndIf
	If SN8->N8_VLRINV = 0              //Depreciou totalmente
		SN3->N3_FIMDEPR := SN8->N8_DTAJUST
	EndIf
	MsUnlock()
 	
 	cOcorr 	   := "13"
	aDadosComp := ATFXCompl( &('SM2->M2_MOEDA'+cMoedaAtf) ,0,/*cMotivo*/,/*cBaixa*/,/*cFilOrig*/,/*cSerie*/,/*cNota*/,/*nVenda*/,SN1->N1_LOCAL, SN3->N3_PRODMES )
	If lSN3Saldo
		cTpSaldo := SN3->N3_TPSALDO
	EndIf
	ATFXMOV(cFilAnt,@cIDMOV,SN8->N8_DTAJUST,cOcorr,SN3->N3_CBASE,SN3->N3_ITEM,SN3->N3_TIPO,SN3->N3_BAIXA,SN3->N3_SEQ,SN3->N3_SEQREAV,"4",SN8->N8_QTDINV,cTpSaldo,,aVlrAjust,aDadosComp,,,,,lOnOff,cPadrao)
	
	If lSeqCorr
		aAdd(aDiario,{"SN4",SN4->(recno()),cCodDiario,"N4_NODIA","N4_DIACTB"})
	EndIf
    
    cOcorr 	   := "13"
	aDadosComp := ATFXCompl( &('SM2->M2_MOEDA'+cMoedaAtf) ,0,/*cMotivo*/,/*cBaixa*/,/*cFilOrig*/,/*cSerie*/,/*cNota*/,/*nVenda*/,SN1->N1_LOCAL, SN3->N3_PRODMES )
	If lSN3Saldo
		cTpSaldo := SN3->N3_TPSALDO
	EndIf
	ATFXMOV(cFilAnt,@cIDMOV,SN8->N8_DTAJUST,cOcorr,SN3->N3_CBASE,SN3->N3_ITEM,SN3->N3_TIPO,SN3->N3_BAIXA,SN3->N3_SEQ,SN3->N3_SEQREAV,"3",SN8->N8_QTDINV,cTpSaldo,,aVlrAjust,aDadosComp,,,,,lOnOff,cPadrao)
	
	If lSeqCorr
		aAdd(aDiario,{"SN4",SN4->(recno()),cCodDiario,"N4_NODIA","N4_DIACTB"})
	EndIf
                                 
	If ExistBlock("AF220GRV")
		ExecBlock("AF220GRV",.F.,.F.)
	EndIf

	ATFSALDO(	SN3->N3_CCDEPR ,SN8->N8_DTAJUST,"P",aVlrAjust[1],aVlrAjust[2],aVlrAjust[3],aVlrAjust[4],;
			aVlrAjust[5],"+",&('SM2->M2_MOEDA'+cMoedaAtf), SN3->N3_SUBCCDE,, SN3->N3_CLVLCDE,SN3->N3_CCCDEP,"4", aVlrAjust )
	ATFSALDO(	SN3->N3_CDEPREC,SN8->N8_DTAJUST,"P",aVlrAjust[1],aVlrAjust[2],aVlrAjust[3],aVlrAjust[4],;
			aVlrAjust[5],"+",&('SM2->M2_MOEDA'+cMoedaAtf), SN3->N3_SUBCDEP,, SN3->N3_CLVLDEP,SN3->N3_CCDESP,"3", aVlrAjust )
	IF lPadrao .And. lOnOff
		//Ŀ
		// Envia para lanamento contbil, desde que exista lanamento padro-
		// nizado para o ativo cadastrado. 											 
		//
		If lPrim 	 //Se for 1a vez.
			nHdlPrv := HeadProva(cLoteAtf,"ATFA220",Substr(cUsername,1,6),@cArquivo)
			lPrim := .F.
		Endif
		nTotal += DetProva(nHdlPrv,cPadrao,"ATFA220",cLoteAtf)
	Endif
	dbSelectArea("SN8")
	dbSetorder(1)
	dbSkip()
EndDo

If nTotal != 0 .and. nHdlPrv > 0 .And. lOnOff
	RodaProva(nHdlPrv,nTotal)                                                 
	cA100Incl(cArquivo,nHdlPrv,1,cLoteAtf,lDigita,lAglutina,,,,,,aDiario)
Endif

dbSelectArea( aArea[1] )
dbSetOrder( aArea[2] )

Return
/*/


Ŀ
Funo     AtfOk     Autor  Alice Y. Yamamoto      Data  19.01.99 
Ĵ
Descrio  Mensagem e confirmacao do processamento                    
Ĵ
Sintaxe    AtfOk(void)                                                
ٱ


/*/
Static Function AtfOk()

Local lRet := .T.
                    
//Ŀ
// Disponibilizacao para contabilizao em Diario, Proj Portugal 
// Incluido em 02/06/08 por SI4004 - Anderson Gonalves          
//
If UsaSeqCor()
	cCodDiario := CTBAVerDia()
EndIf

Return(lRet)

/*/


Ŀ
Programa  MenuDef    Autor  Ana Paula N. Silva      Data 29/11/06 
Ĵ
Descrio  Utilizacao de menu Funcional                               
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados     
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function MenuDef()
	Local aRotina:={ {"Parametros", "AllwaysTrue",0,3}}
Return(aRotina)      

/*/


Ŀ
Funo    AdmAbreSM0 Autor  Orizio                 Data  22/01/10 
Ĵ
Descrio Retorna um array com as informacoes das filias das empresas 
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Static Function AdmAbreSM0()
	Local aArea			:= SM0->( GetArea() )
	Local aAux			:= {}
	Local aRetSM0		:= {}
	Local lFWLoadSM0	:= .T.
	Local lFWCodFilSM0 	:= .T.

	If lFWLoadSM0
		aRetSM0	:= FWLoadSM0()
	Else
		DbSelectArea( "SM0" )
		SM0->( DbGoTop() )
		While SM0->( !Eof() )
			aAux := { 	SM0->M0_CODIGO,;
						IIf( lFWCodFilSM0, FWGETCODFILIAL, SM0->M0_CODFIL ),;
						"",;
						"",;
						"",;
						SM0->M0_NOME,;
						SM0->M0_FILIAL }

			aAdd( aRetSM0, aClone( aAux ) )
			SM0->( DbSkip() )
		End
	EndIf

	RestArea( aArea )
Return aRetSM0
