#include 'tlpp-core.th'


namespace tr.BranchCode
using namespace tr.util
//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/BranchCode
Class BranchCodeService

@author José González
@since 02/02/2022
/*/
//-------------------------------------------------------------------

Class BranchCodeService
	Public Method new()

	Public Method BranchCodeServices()
EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/BranchCode
new Constructor de la clase

@author José González
@since 02/02/2022
/*/
//-------------------------------------------------------------------
Method new() Class BranchCodeService
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/BranchCode
retorna las filiales

@Return response As Jobject

@author José González
@since 02/02/2022
/*/
//-------------------------------------------------------------------

Method BranchCodeServices(jParamsRequest,jRestFilter) Class BranchCodeService
Local oResponse As Object
Local oBranchCode as Object
Local cGrupo := ""
Local aFil := {}
Local aFiliales:= {}
local i := 1
local nPos := 1
Local cFiltro := ""
Local cLabel  := ""
oResponse := JsonObject():New()
oBranchCode := JsonObject():New()

If jParamsRequest != Nil .and. !(Empty(jParamsRequest['group']))
	cGrupo := jParamsRequest['group'] 
	cFiltro:= iif(EMPTY(jParamsRequest['filtro']),jRestFilter['filter'],jParamsRequest['filtro'])
EndIf
	
aFil := FWLoadSM0() 

If len(afil) > 0
	For i := 1 to len(afil)
		If afil[i][1] == cGrupo .and. afil[i][11]
			cLabel := afil[i][2] +"-"+alltrim(afil[i][7])
			If Empty(cfiltro) .or. (!EMPTY(cFiltro) .and. (cfiltro) $ UPPER( cLabel ))   
				Aadd(aFiliales,JsonObject():new())
				nPos := Len(aFiliales)
				aFiliales[nPos]['value'] 	:= afil[i][2]		
				aFiliales[nPos]['label'] 	:= afil[i][2] +"-"+alltrim(afil[i][7]) // Código + Descripción
			Endif
		EndIF
	Next
EndIf

If !(Empty(jParamsRequest['filtro']))
	oBranchCode['value'] := aFiliales[Len(aFiliales)]['value']
	oBranchCode['label'] := aFiliales[Len(aFiliales)]['label']
Else
	oBranchCode['items']:= aFiliales
Endif

	oResponse["result"] := .T.
	oResponse["response"] := oBranchCode

Return oResponse
