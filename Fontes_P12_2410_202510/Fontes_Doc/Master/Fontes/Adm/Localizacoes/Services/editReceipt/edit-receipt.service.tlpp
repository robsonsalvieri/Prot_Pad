#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "EDITRECEIPTSERVICE.CH"
#include "FWMVCDEF.CH"
namespace tr.editReceiptDetail

/*/{Protheus.doc} editReceiptDetailService
Declaracion de los metodos de la clase
@type class
@version  1
@author José González
@since 07/10/2021
/*/
Class editReceiptDetailService
	Public Method new()
	Public Method geteditReceiptDetailService()
EndClass

/*/{Protheus.doc} editReceiptDetailService::new
Constructor de la clase
@type method
@version  1
@author José González
@since 07/10/2021
/*/
Method new() Class editReceiptDetailService
Return

/*/{Protheus.doc} editReceiptDetailService::geteditReceiptDetailService
Guarda la Edición de los Cheques en el Recibo de PAgo
@type method
@version  1
@author  José González
@since 07/10/2021
/*/
Method geteditReceiptDetailService (oJParams ) class  editReceiptDetailService
	Local aTemp As Array
	Local cTemp	As Character
	Local aResponse As Array
	Local oResponse := JsonObject():New()
	Local jValidate	:= JsonObject():new()
	Local jErrors := JsonObject():new()
	Local cSerie
	Local cRecibo	
	Local cVersao
	Local cModal	:= PADR(oJParams['receipt']['originalNature'], GetSx3Cache('FJT_NATURE','X3_TAMANHO' ))
	Local cModMod	:= PADR(oJParams['receipt']['modifiedNature'], GetSx3Cache('FJT_NATURE','X3_TAMANHO' ))
	Local lModMod	:= IIF(cModal <> cModMod,.T.,.F.)
	Local lRet		:= .T.
	Local lRetMod	:= .F.
	Local nI		:= 1
	Local aError 	:= {}
	Local aresp := {}
	cTemp := "" 
	aTemp := {} 
	aResponse := {} 
	aModif := {}  
	cRecibo := PADR(oJParams['receipt']['receipt'] 	, GetSx3Cache('FJT_RECIBO','X3_TAMANHO' ))
	cSerie 	:= PADR(oJParams['receipt']['serie']	, GetSx3Cache('FJT_SERIE','X3_TAMANHO' 	))
	cVersao	:= PADR(oJParams['receipt']['version']	, GetSx3Cache('FJT_VERSAO','X3_TAMANHO' ))
	SetFunName("FINA998")
	
oMdlTab := FwLoadModel("FINA887")
DbSelectArea("FJT")
FJT->(DbSetOrder(1))
IF FJT->(MsSeek(xFilial("FJT")+cSerie+cRecibo+cVersao,.T.))
	oMdlTab:SetOperation(MODEL_OPERATION_UPDATE)
	
	oMdlTab:Activate()
	oMdlTab:SetValue('FJT_MASTER', "ASIENTO" 	, oJParams['parameters']['mv_par01'])
	oMdlTab:SetValue('FJT_MASTER', "AGRUPA" 	, oJParams['parameters']['mv_par02'])
	oMdlTab:SetValue('FJT_MASTER', "ONLINE" 	, oJParams['parameters']['mv_par03'])
	oMdlTab:SetValue('FJT_MASTER', "EDITCH" , .T. )		
	IF lModMod
		If oMdlTab:SetValue('FJT_MASTER', "FJT_NATURE" ,cModMod )
			oMdlTab:loadValue('FJT_MASTER', "FJT_NATURE" ,cModal )	
		Else 
			lRetMod := .T.		
		EndIf
	EndIf 

	IF !lRetMod
		For nI:= 1 to len(oJParams['values'])
			If len(oJParams['values']) > 1 .and. nI > 1
				oMdlTab:GetModel('CH_DETAIL' ):AddLine()
			EndIF
			oMdlTab:SetValue('CH_DETAIL', 'CH_NUMERO'	, oJParams['values'][nI]["numero"]   		)
			oMdlTab:SetValue('CH_DETAIL', 'CH_BCOCHQ'	, oJParams['values'][nI]["bcochq"]   		)
			oMdlTab:SetValue('CH_DETAIL', 'CH_AGECHQ'	, oJParams['values'][nI]["agechq"]   		)
			oMdlTab:SetValue('CH_DETAIL', 'CH_CTACHQ'	, oJParams['values'][nI]["ctachq"]   		)
			oMdlTab:SetValue('CH_DETAIL', 'CH_POSTAL'	, oJParams['values'][nI]["postal"]  		)
			oMdlTab:SetValue('CH_DETAIL', 'CH_TERCEIR'	, oJParams['values'][nI]["terceir"] 		)
			oMdlTab:SetValue('CH_DETAIL', 'CH_ENDOSSA'	, oJParams['values'][nI]["endossa"]   		)
			oMdlTab:SetValue('CH_DETAIL', 'CH_CGC'		, oJParams['values'][nI]["cgc"]   			)
			oMdlTab:SetValue('CH_DETAIL', 'CH_EMISSAO'	, STOD(oJParams['values'][nI]["emissao"])  	)
			oMdlTab:SetValue('CH_DETAIL', 'CH_DTVCTO'	, STOD(oJParams['values'][nI]["dtvcto"])   	)
			oMdlTab:SetValue('CH_DETAIL', 'CH_SELDOC'	, oJParams['values'][nI]["seldoc"]   		)
			IF lModMod
				oMdlTab:SetValue('CH_DETAIL', 'CH_NATURE'	, cModMod)
			Else
				oMdlTab:SetValue('CH_DETAIL', 'CH_NATURE'	, cModal)
			EndIf
		Next

		If oMdlTab:VldData()
			If oMdlTab:CommitData()
				lret := .T.
			EndIf
		Endif
	ENDIF 
	
	aError := oMdlTab:GetErrorMessage()
	If Len(aError)>0 .and. alltrim(aError[6]) <> "" 
		lRet := .F.
		jErrors['message'] := STR0001 //!  Se encontraron inconsistencias, revise la información.
		jErrors['detailMessage'] := aError[6] 
	Endif
Endif

jValidate['success']	:=  lRet
jValidate['error']		:=	jErrors
jValidate['returnNature'] := lretmod

oResponse["result"] := .T.
oResponse["response"] := jValidate

return oResponse
