#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE 'FWMVCDEF.CH'
#Include 'VALIDPAYMENTFORMSERVICE.CH'

namespace tr.validPaymentForm

/*/{Protheus.doc} validPaymentFormService
Definicion de metodos de la clase
@type class
@version  1
@author jose.gonzalez
@since 12/4/2021
/*/
Class validPaymentFormService
	Public Method new()
	Public Method getvalidPaymentFormService()
EndClass

/*/{Protheus.doc} validPaymentFormService::new
Inicializa la clase
@type method
@version 1 
@author jose.gonzalez
@since 12/4/2021
/*/
Method new() Class validPaymentFormService
Return


/*/{Protheus.doc} validPaymentFormService::getvalidPaymentFormService
Realiza una validacion de campos en el Objeto FINA887 
@type method
@version  1
@author jose.gonzalez
@since 12/4/2021
/*/
Method getvalidPaymentFormService (oJParams ) class  validPaymentFormService
	Local oMdlTab	As Object
	Local jtotal	:= JsonObject():new()
	Local jresponse := JsonObject():New()
	Local jItems 	:= JsonObject():New()
	Local cSaltoL   := Chr(10)+ Chr(13)
	Local aError 	:= {}	As Array
	Local aErrors 	:= {}	As Array
	Local cCampo 	:= "" 	As Character
	Local cTable 	:= "EL_" As Character
	Local cTitulo 	:= ""	As Character
	Local cValue 	:= ""	As Character
	Local nValue 	:= 0	As Numeric
	Local i 		:= 1	As Numeric
	Local nPos 		:= 1	As Numeric
	Local dValue	As Date
	Local aHeader 	:= {}
	Local aAux		:= {}
	SetFunName("FINA998")

	jItems := oJParams['items']

	oMdlTab:= FwLoadModel("FINA887")
	oMdlTab:SetOperation(MODEL_OPERATION_INSERT)
	oMdlTab:Activate()
	//Limpiamos triggers
	oMdlTab:Getmodel('SEL_DETAIL'):OFORMMODELSTRUCT:ATRIGGERS:= {}
	//Limpiamos las reglas de dependencia
	oMdlTab:GetModel('SEL_DETAIL'):OFORMMODELSTRUCT:ARULES := {}  						
	oMdlTab:aModelStruct[1][4][1][3]:ARULES := {}	

	For i := 1 to len(oJParams['coins'])
		If len(oJParams['coins']) > 1 .and. i > 1
			oMdlTab:GetModel('MOE_DETAIL' ):AddLine()
		EndIF 
		oMdlTab:SetValue('MOE_DETAIL',"MOEDA" 		, alltrim(str(oJParams['coins'][i]['moneda'])))
		oMdlTab:SetValue('MOE_DETAIL',"TASA" 		, oJParams['coins'][i]['tasa'] )
	Next
	
	aHeader := oJParams['header']:GetNames()
	FOR i := 1 to len(aHeader)
			If oMdlTab:GetModel('FJT_MASTER'):GETSTRUCT():GetProperty("FJT_"+UPPER(aHeader[i]),MODEL_FIELD_TIPO ) == 'D'
            	oMdlTab:loadValue('FJT_MASTER', "FJT_"+UPPER(aHeader[i]),STOD(StrTran(oJParams['header'][aHeader[i]],"-","")))
        	Else
    			oMdlTab:loadValue('FJT_MASTER', "FJT_"+UPPER(aHeader[i]),oJParams['header'][aHeader[i]])	
			EndIf
    NEXT

	For i:= 1 to len(jItems)
		dValue := CTOD("")
		cCampo := cTable + UPPER(jItems[i]["property"])
		cTitulo := getTitleType(cCampo, "title")
		cCampoTipo := getTitleType(cCampo, "type")
		oMdlTab:GetModel('SEL_DETAIL'):oFormModelStruct:SetProperty(cCampo,MODEL_FIELD_WHEN) := "" //Limpiamos los WHEN de cada campo
		If cCampoTipo == 'D'
			If !VAZIO(jItems[i]["value"])
				dValue := STOD(jItems[i]["value"]) 
			ELSE
				dValue := CTOD("")
			ENDIF
			IF ( !VAZIO(dValue) .AND.  !(oMdlTab:SetValue('SEL_DETAIL',cCampo ,dValue)) )
				aError := oMdlTab:GetErrorMessage()
				Aadd(aErrors,JsonObject():new())
				nPos := Len(aErrors)
				aErrors[nPos]['message' ]:= cTitulo+" ==> "+	aError[6]
			EndIf
		ElseIF cCampoTipo == 'N'
			If !(jItems[i]["value"] == NIL)
				nValue := jItems[i]["value"]
				If !VAZIO(nValue) .AND. !(oMdlTab:SetValue('SEL_DETAIL',cCampo ,nValue)) 
					aError := oMdlTab:GetErrorMessage()
					Aadd(aErrors,JsonObject():new())
					nPos := Len(aErrors)
					aErrors[nPos]['message' ]:= cTitulo +" ==> "+	aError[6]
				EndIf
			EndIF
		Else
			cValue := IIF(jItems[i]["value"]!=Nil,jItems[i]["value"],"")
			If !VAZIO(cValue) .AND. !(oMdlTab:SetValue('SEL_DETAIL',cCampo ,Alltrim(cValue))) 
				aError := oMdlTab:GetErrorMessage()
				Aadd(aErrors,JsonObject():new())
				nPos := Len(aErrors)
				aErrors[nPos]['message' ] 	:=  cTitulo +" ==> "+	aError[6] + IIF(!VAZIO(aError[7]),cSaltoL+STR0012+cSaltoL+aError[7],"")
			EndIf
		EndIF
		cTitulo := ""
	Next

	//Si no hay errores en validaciones de campo procedera con las validaciones por linea
	IF VAZIO(aError)
		IF !oMdlTab:GetModel('SEL_DETAIL'):VldLineData()//Validacion de linea
			aError := oMdlTab:GetErrorMessage()
			Aadd(aErrors,JsonObject():new())
			nPos := Len(aErrors)
			aErrors[nPos]['message' ]:= aError[6]
		ENDIF
	ENDIF

	jtotal ['errors' ]:= 	aErrors

	If len(aErrors) > 0
		jtotal['success' ] := 	.F.
	Else
		jtotal['success' ] := 	.T.
	EndIf

	oMdlTab:DeActivate()
	jresponse["result"] := .T.
	jresponse["response"] := jtotal

return jresponse

/*/{Protheus.doc} getTitleType
Metodo encargado de obtener los valores como el titulo y el tipo ya sea de un campo en la base de datos o generada en el modelo 
por ejemplo en ARG no existe en la DB la variable EL_VALBASE, pero en el modelo se basa de la FE_VALBASE, por lo cual se cambia y se obtienen los valores
@type function
@version  1
@author luis.aboytes
@since 22/10/2021
/*/
STATIC FUNCTION getTitleType(cCampo,get)
	Local cRet := "" As Character

	IF get == "title"
		IF cPaisLoc == "ARG" .AND. cCampo$"EL_VALBASE|EL_ALIQ"
			cRet := Getsx3Cache( STRTRAN(cCampo,"EL","FE"),'X3_TITULO' )
		ELSE
			cRet := Getsx3Cache( cCampo,'X3_TITULO' )
		ENDIF
	ELSEIF get == "type"
		IF cPaisLoc == "ARG" .AND. cCampo$"EL_VALBASE|EL_ALIQ"
			cRet := Getsx3Cache( STRTRAN(cCampo,"EL","FE"),'X3_TIPO'  )
		ELSE
			cRet := Getsx3Cache( cCampo,'X3_TIPO' )
		ENDIF
	ENDIF
RETURN cRet
