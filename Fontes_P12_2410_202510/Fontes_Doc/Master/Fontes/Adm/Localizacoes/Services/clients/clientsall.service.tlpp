#include 'tlpp-core.th'


namespace tr.Clients
using namespace tr.util
//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/Clients
Class CatalogsService

@author José González
@since 30/06/2021
/*/
//-------------------------------------------------------------------

Class ClientsService
	Public Method new()
	Public Method ClientsServices()
	Public Method Clients2Services()
	Public Method ClientsTotalsServices()
EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/Clients
new
Constructor de la clase

@author José González
@since 30/06/2021
/*/
//-------------------------------------------------------------------
Method new() Class ClientsService
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/Clients
retorna los clientes Paginados

@Return response As Jobject

@author José González
@since 30/06/2021
/*/
//-------------------------------------------------------------------

Method ClientsServices(jQueryRequestParams) Class ClientsService
	Local oClientsAdapter As Object
	Local oResponse As Object
	Local cFil As Character
	Local cFilSelec	As Character
	Local lCanFil	As Logical
	Local jRespons 		:= JsonObject():New()
	Local nX As numeric
	Local lClient	:= .F. As Logical 

	oClientsAdapter := ClientsAdapter():new('GET')

	IF Empty(jQueryRequestParams['page']) .AND. Empty(jQueryRequestParams['pageSize'])
		lClient := .T.
	ENDIF

	//Indica cual pagina debemos retornar
	If !(Empty(jQueryRequestParams['page']))
		oClientsAdapter:setPage(Val(jQueryRequestParams['page']))
	EndIf

	//Indica el tamaño de la pagina
	If !(Empty(jQueryRequestParams['pageSize']))
		oClientsAdapter:setPageSize(50)
	EndIf

	If !(Empty(jQueryRequestParams['filial']))
		cFilSelec := jQueryRequestParams['filial']
	EndIf

	If jQueryRequestParams:hasproperty('client')
		cFil :=jQueryRequestParams['client']
	Else
		cFil :=jQueryRequestParams['filter']
	EndIf

	If !(Empty(jQueryRequestParams['canSetFilial']))
		lCanFil := jQueryRequestParams['canSetFilial'] == "true"
	EndIf
	
	oClientsAdapter:Clients1Adapter("SA1", cFil, cFilSelec, lCanFil,jQueryRequestParams,lClient)

	oResponse := JsonObject():New()
	If oClientsAdapter:lOk
		oResponse["result"] := .T.

		jRespons:fromJson(oClientsAdapter:getJSONResponse())

		If !Empty(cFilSelec) .And. lCanFil
			cFilSelec := xFilial("SA1",cFilSelec)
		Else
			cFilSelec := xFilial("SA1")
		EndIf

		DbSelectArea("SA1")
		DbSetOrder(1)//A1_FILIAL+A1_COD+A1_LOJA
		For nX := 1 to Len(jRespons["items"])
			IF SA1->(MsSeek(cFilSelec + jRespons["items"][nX]['code']))
				jRespons["items"][nX]['name']   := Alltrim(SA1->A1_NOME)
				jRespons["items"][nX]['value'] := jRespons["items"][nX]['code']
				jRespons["items"][nX]['label'] := jRespons["items"][nX]['code'] + ' - ' + jRespons["items"][nX]['name']
				jRespons["items"][nX]['format'] := jRespons["items"][nX]['code'] + ' - ' + jRespons["items"][nX]['name']
			EndIf	

			If jQueryRequestParams:hasproperty('client')
				jRespons := jRespons["items"][nX]
				Exit
			EndIf
		Next
		SA1->(DbCloseArea())

		oResponse["response"] := jRespons
	Else
		oResponse := answerErrorFormat(oClientsAdapter:GetCode(), oClientsAdapter:GetMessage())
	EndIf

	oClientsAdapter:DeActivate()
	oClientsAdapter := nil
Return oResponse

//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/Clients
retorna los clientes 

@Return response As Jobject

@author José González
@since 30/06/2021
/*/
//-------------------------------------------------------------------

Method Clients2Services(jParamsRequest) Class ClientsService
	Local oClientsAdapter As Object
	Local oResponse As Object
	Local oClients as Object
	Local cFil as Character

	oClientsAdapter := ClientsAdapter():new('GET')

	cFil := ""

	If jParamsRequest != Nil .and. !(Empty(jParamsRequest['filial']))
		cFil = jParamsRequest['filial']
	EndIf

	oClients := oClientsAdapter:Clients2Adapter(cFil)

	oResponse := JsonObject():New()
	If oClientsAdapter:lOk
		oResponse["result"] := .T.
		oResponse["response"] := oClients
	Else
		oResponse := answerErrorFormat(400, "Error")
	EndIf

	oClientsAdapter:DeActivate()
	oClientsAdapter := nil

Return oResponse

//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/Clients
retorna el total de los clientes

@Return response As Jobject

@author José González
@since 30/06/2021
/*/
//-------------------------------------------------------------------
Method ClientsTotalsServices(jParamsRequest) Class ClientsService
	Local oClientsAdapter As Object
	Local oResponse As Object
	Local oClients as Object
	Local lSelectFil As Logical

	oClientsAdapter := ClientsAdapter():new('GET')

	lSelectFil := jParamsRequest['canselectfilial'] == "true"

	oClients := oClientsAdapter:ClientsTotalsAdapter(lSelectFil)

	oResponse := JsonObject():New()
	If oClientsAdapter:lOk
		oResponse["result"] := .T.
		oResponse["response"] := oClients
	Else
		oResponse := answerErrorFormat(400, "Error")
	EndIf

	oClientsAdapter:DeActivate()
	oClientsAdapter := nil

Return oResponse
