#INCLUDE "FINA020.CH"
#INCLUDE "PROTHEUS.CH"

Static __nExeDel  := 0
Static _oFina020

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ FINA020  ³ Autor ³ Wagner Xavier         ³ Data ³ 22/05/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de atualiza‡„o de Or‡amentos                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ FINA020()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Gen‚rico 															  	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.          	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador³ Data   ³ BOPS ³  Motivo da Alteracao                  	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Julio W.  ³22.11.99³META  ³ Revis„o do fonte para Protheus            ³±±
±±³           ³        ³      ³                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION FINA020

Private aRotina := MenuDef()

PRIVATE cCadastro := OemToAnsi(STR0009) //"Atualiza‡„o de Or‡amentos"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
mBrowse( 6, 1,22,75,"SE7")

RETURN

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³FA020Delet³ Autor ³ Wagner Xavier         ³ Data ³ 22/05/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa de exclusao de Orcamentos                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³FA020Delet(ExpC1,ExpN1,ExpN2)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FinA020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FUNCTION FA020DELET(cAlias,nReg,nOpc)

Local oDlg, nOpcA, i, bCampo
Local lOk        := .T.
Local aBut020    := {}
Local nX
Local aSize      := {}
Local aObjects   := {}
Local aPosObj    := {}
Local lIntPFS    := SuperGetMV("MV_JURXFIN",,.F.)
Local lFSinc     := SuperGetMV("MV_JFSINC",.F.,'2') == "1"
Local aCampos    := FA020Cmpos()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aTELA[0][0],aGETS[0]

Private aHeader  := {}
Private aCols 	 := {}

Private aColsJan := {}
Private aColsFev := {}
Private aColsMar := {}
Private aColsAbr := {}
Private aColsMai := {}
Private aColsJun := {}
Private aColsJul := {}
Private aColsAgo := {}
Private aColsSet := {}
Private aColsOut := {}
Private aColsNov := {}
Private aColsDez := {}

Private oValRat
Private nValRat  	:= 0
Private nValOrc  	:= 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o arquivo est  realmente vazio ou se       ³
//³ est  posicionado em outra filial.                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (cAlias)->(EOF()) .or. SE7->E7_FILIAL # xFilial("SE7")
   HELP(" " , 1 , "ARQVAZIO")
Else
	While .T.
		nOpca := 0
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Envia para processamento dos Gets          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea( cAlias )
		bCampo := {|nCPO| Field(nCPO) }
		FOR i := 1 TO FCount()
			M->&(EVAL(bCampo,i)) := FieldGet(i)
		NEXT
		nOpca:=1
		dbSelectArea(cAlias)
		If SoftLock( "SE7" )
			Fa020LeRat()
			If !lIntPFS
				aBut020 := {{"BUDGET", {|| Fa020Rateio(2)}, STR0013, STR0027}} //"Visualização do Rateio do orçamento do mês" //"Rateio"
			EndIf

			aSize := MSADVSIZE()
			AAdd( aObjects, { 315,  70, .T., .T. } )
			aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }

			DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL
			aPosObj := MsObjSize( aInfo, aObjects, .T. )
			EnChoice(cAlias, nReg, nOpc,,,, aCampos, aPosObj[1],, 3,,,,,, .T.)
			ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg, {|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()},, aBut020)

			If nOpcA == 2
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Inicio da protecao via TTS                 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				BEGIN TRANSACTION
					DbSelectArea("SEW")
					DbSetOrder(1)
					// Apaga os rateios dos orcamentos
					For nX := 1 To 12
						MsSeek(xFilial("SEW")+SE7->(E7_ANO+StrZero(nX,2)+E7_CMOEDA+E7_NATUREZ))
						While SEW->(!Eof()) .And.;
								SEW->(EW_FILIAL+EW_ANO+EW_MES+EW_MOEDA+EW_NATUREZ) == xFilial("SEW")+SE7->(E7_ANO+StrZero(nX,2)+E7_CMOEDA+E7_NATUREZ)
							If RecLock("SEW",.F.,.T.)
								dbDelete()
							Else
								lOk := .F.
								Exit
							Endif
							DbSkip()
						End
					Next
					dbSelectArea(cAlias)
					If lOk // Apagou todos os rateios para o orcamento
						//Controle de Saldo de Naturezas
						F020AtuNat("-")

						// Fila de sincronização - Jurídico
						If lIntPFS .And. lFSinc
							J170GRAVA("SE7", xFilial("SE7") + SE7->E7_NATUREZ + SE7->E7_ANO + SE7->E7_CMOEDA + SE7->E7_CESCR + SE7->E7_CCUSTO + SE7->E7_CPART + SE7->E7_CRATEIO, "5")
						EndIf

						RecLock(cAlias,.F.,.T.)
						dbDelete()
						MsUnlock()
					Endif
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Final  da protecao via TTS                 ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				END TRANSACTION
			Else
				MsUnlock( )
			Endif
		Endif
		Exit
	Enddo
	dbSelectArea(cAlias)
Endif

RETURN

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³FA020Inclu³ Autor ³ Claudio D. de Souza   ³ Data ³ 03/10/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para inclusao de orcamentos       				     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FA020Inclu(ExpC1,ExpN1,ExpN2) 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo											  ³±±
±±³			 ³ ExpN1 = Numero do registro 										  ³±±
±±³			 ³ ExpN2 = Numero da opcao selecionada 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA020																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FA020Inclu(cAlias,nReg,nOpc)
Local aBut020    := {}
Local nOpca      := 0
Local lIntPFS    := SuperGetMV("MV_JURXFIN",,.F.)
Local lFSinc     := SuperGetMV("MV_JFSINC",.F.,'2') == "1"
Local aCampos    := FA020Cmpos()

Private aHeader := {}
Private aCols 	 := {}

Private aColsJan := {}
Private aColsFev := {}
Private aColsMar := {}
Private aColsAbr := {}
Private aColsMai := {}
Private aColsJun := {}
Private aColsJul := {}
Private aColsAgo := {}
Private aColsSet := {}
Private aColsOut := {}
Private aColsNov := {}
Private aColsDez := {}

Private oValRat
Private nValRat  	:= 0

If !lIntPFS
	aBut020 := {{"BUDGET", {|| Fa020Rateio(3)}, STR0012, STR0027}} //"Ratear orçamento do mês" //"Rateio"
EndIf

nOpca := AxInclui(cAlias,nReg,nOpc,aCampos,,,"FA020VlInc()",,"FA020AXINC('"+cAlias+"')",aBut020)

If nOpca == 1
	F020AtuNat("+")

	// Fila de sincronização - Jurídico
	If lIntPFS .And. lFSinc
		J170GRAVA("SE7", xFilial("SE7") + SE7->E7_NATUREZ + SE7->E7_ANO + SE7->E7_CMOEDA + SE7->E7_CESCR + SE7->E7_CCUSTO + SE7->E7_CPART + SE7->E7_CRATEIO, "3")
	EndIf
Endif
Return nOpca

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³FA020Alter³ Autor ³ Claudio D. de Souza   ³ Data ³ 17/10/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para alteracao de orcamentos       				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FA020Alter(ExpC1,ExpN1,ExpN2) 							 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo									  ³±±
±±³			 ³ ExpN1 = Numero do registro 								  ³±±
±±³			 ³ ExpN2 = Numero da opcao selecionada 						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA020														 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FA020Alter(cAlias,nReg,nOpc)
Local aBut020    := {}
Local nOpca      := 0
Local aOldVlrs   := {}
Local lIntPFS    := SuperGetMV("MV_JURXFIN",,.F.)
Local lFSinc     := SuperGetMV("MV_JFSINC",.F.,'2') == "1"
Local aNoEditCpo := {"E7_NATUREZ", "E7_ANO", "E7_MOEDA", "E7_CESCR", "E7_CCUSTO", "E7_SIGLA", "E7_CRATEIO"}
Local aCampos    := FA020Cmpos()
Local aEditCpo   := FA020EdtCp(aCampos, aNoEditCpo)

Private aHeader  := {}
Private aCols 	 := {}

Private aColsJan := {}
Private aColsFev := {}
Private aColsMar := {}
Private aColsAbr := {}
Private aColsMai := {}
Private aColsJun := {}
Private aColsJul := {}
Private aColsAgo := {}
Private aColsSet := {}
Private aColsOut := {}
Private aColsNov := {}
Private aColsDez := {}

Private oValRat
Private nValRat  	:= 0

aOldVlrs	:= {	SE7->E7_VALJAN1,;
					SE7->E7_VALFEV1,;
					SE7->E7_VALMAR1,;
					SE7->E7_VALABR1,;
					SE7->E7_VALMAI1,;
					SE7->E7_VALJUN1,;
					SE7->E7_VALJUL1,;
					SE7->E7_VALAGO1,;
					SE7->E7_VALSET1,;
					SE7->E7_VALOUT1,;
					SE7->E7_VALNOV1,;
					SE7->E7_VALDEZ1,;
					SE7->E7_ANO}
Fa020LeRat()

If !lIntPFS
	aBut020 := {{"BUDGET", {|| Fa020Rateio(3)}, STR0012, STR0027}} //"Ratear orçamento do mês" //"Rateio"
EndIf

nOpca := AxAltera(cAlias,nReg,nOpc,aCampos,aEditCpo,,,"FA020VlAlt()","FA020AXAlt('"+cAlias+"')",,aBut020)

If nOpca == 1
	//Controle de Saldo de Naturezas
	F020AtuNat("+",aOldVlrs)

	// Fila de sincronização - Jurídico
	If lIntPFS .And. lFSinc
		J170GRAVA("SE7", xFilial("SE7") + SE7->E7_NATUREZ + SE7->E7_ANO + SE7->E7_CMOEDA + SE7->E7_CESCR + SE7->E7_CCUSTO + SE7->E7_CPART + SE7->E7_CRATEIO, "4")
	EndI
Endif

Return nOpca

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³FA020Alter³ Autor ³ Claudio D. de Souza   ³ Data ³ 17/10/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para alteracao de orcamentos       				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FA020Alter(ExpC1,ExpN1,ExpN2) 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo									  ³±±
±±³			 ³ ExpN1 = Numero do registro 								  ³±±
±±³			 ³ ExpN2 = Numero da opcao selecionada 						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA020													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FA020Visua(cAlias,nReg,nOpc)
Local aBut020 := {}
Local nOpca
Local aCampos := FA020Cmpos()
Local lIntPFS := SuperGetMV("MV_JURXFIN",,.F.) // Integração SIGAPFS x SIGAFIN

Private aHeader  := {}
Private aCols 	 := {}

Private aColsJan := {}
Private aColsFev := {}
Private aColsMar := {}
Private aColsAbr := {}
Private aColsMai := {}
Private aColsJun := {}
Private aColsJul := {}
Private aColsAgo := {}
Private aColsSet := {}
Private aColsOut := {}
Private aColsNov := {}
Private aColsDez := {}

Private oValRat
Private nValRat  	:= 0
Private nValOrc  	:= 0

Fa020LeRat()

If !lIntPFS
	aBut020 := {{"BUDGET", {|| Fa020Rateio(2)}, STR0013, STR0027}} //"Visualização do Rateio do orçamento do mês" //"Rateio"
EndIf

nOpca := AxVisual(cAlias,nReg,nOpc,aCampos,,,,aBut020)

Return nOpca
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³FA020AxInc³ Autor ³ Claudio D. de Souza   ³ Data ³ 03/10/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Auxiliar na gravacao do orcamento        				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FA020AxInc(ExpC1)             							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA020													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa020AxInc(cAlias)
	SE7->E7_CMOEDA := StrZero(SE7->E7_MOEDA,2)
	SE7->(FkCommit())
	Fa020GrvRat()
Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³FA020AxAlt³ Autor ³ Claudio D. de Souza   ³ Data ³ 17/10/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Auxiliar na gravacao do orcamento          				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FA020AxAlt(ExpC1)             							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA020													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa020AxAlt(cAlias)
	Fa020GrvRat()
Return .T.

/*/{Protheus.doc} Fa020Rateio
Cadastro de rateio para orçamentos.

@author  Claudio Donizete Souza
@version Protheus 12
@since   03/10/2003
/*/
Function Fa020Rateio(nOpc)

Local oDlg, oTela, oGet, oPanelAll, oPanelBot, bOk, bCancel
Local aCpoMeses  := {"E7_VALJAN1", "E7_VALFEV1", "E7_VALMAR1", "E7_VALABR1", "E7_VALMAI1", "E7_VALJUN1", "E7_VALJUL1", "E7_VALAGO1", "E7_VALSET1", "E7_VALOUT1", "E7_VALNOV1", "E7_VALDEZ1"}
Local cCampos    := "E7_VALJAN1#E7_VALFEV1#E7_VALMAR1#E7_VALABR1#E7_VALMAI1#E7_VALJUN1#E7_VALJUL1#E7_VALAGO1#E7_VALSET1#E7_VALOUT1#E7_VALNOV1#E7_VALDEZ1"
Local cVar       := ""
Local aCampos    := {"EW_CCUSTO", "EW_VALOR", "EW_PERC", "EW_ITEMCTA", "EW_CLVL", "EW_REC_WT", "EW_ALI_WT"}
Local nOpcA      := 0
Local nPosValor  := GdFieldPos("EW_VALOR")
Local nPosPerc   := GdFieldPos("EW_PERC")
Local nPosRec    := GdFieldPos("EW_REC_WT")
Local nPrecis    := TamSX3("EW_PERC")[2]
Local nPerRat    := 0
Local cMatRat    := ""
Local lCopia     := .F.
Local lCria      := .F.

Local aParamBox  := {}
Local aRetParam  := {}
Local aMeses     := {}
Local cMesExt    := ""
Local nX

// Pede para o usuário selecionar o mês que vai ratear.
For nX := 1 to 12
	aAdd(aMeses, StrZero(nX, 2) + "-" + MesExtenso(nX))
Next nX

aAdd(aParamBox, {2, STR0012, aMeses[1], aMeses, 90,, .T.})  // "Ratear orçamento do mês"
If ParamBox(aParamBox, STR0027, @aRetParam)  // "Rateio"
	nX      := AScan(aMeses, AllTrim(aRetParam[1]))
	cVar    := "M->" + aCpoMeses[nX]
	cMesExt := SubStr(aMeses[nX], 4)
	nValOrc := &cVar

	// Atualiza objetos na tela, para ter acesso ao valor informado pelo usuario.
	oTela := GetWndDefault()
	oTela:CommitControls()
	aCols := {}

	// Matriz com os dados do rateio, anterior ao mes atual.
	cMatRat := ("aCols"+Left(SubStr(cCampos,At(Right(cVar,10),cCampos)-5,4),3))

	If cVar != "M->E7_VALJAN1" 				.And.;
		AllTrim(Right(cVar,10)) $ cCampos	.And.;
		!Empty(&(cMatRat)) 						.And.;
		(Len(&(cMatRat)) > 1 .Or. &(cMatRat)[1][3] <> 0) .And.;
		Empty(&("aCols"+Left(Right(cVar,4),3))) .And.;
		nOpc == 3 .And.;
		nValOrc > 0 .And.;
		IW_MSGBOX(STR0014, STR0015, "YESNO" ) //"Copia percentuais de rateio do orçamento do mês anterior?"###"Atenção"

		nValRat := 0
		lCopia  := .T.
		aCols   := aClone(&cMatRat)

		// Recupera dados da matriz de rateios do mes anterior para a matriz de rateios do mes atual.
		For nX := 1 To Len(&(cMatRat))
			aCols[nX][nPosValor] := Round((aCols[nX][nPosPerc] / 100) * nValOrc, nPrecis)
			nValRat += aCols[nX][nPosValor]
			aCols[nX][nPosRec] := 0
		Next nX

		// Retira a diferença de centavos da última linha.
		aTail(aCols)[nPosValor] += (nValOrc - nValRat)
		nValRat := nValOrc
	Else
		nValRat := 0
		lCopia := .F.

		// Se a matriz de dados do mes atual estiver vazia, ou o aCols estiver vazio, cria aCols
		// para ser utilizado no rateio do orcamento do mes atual
		If Empty(&("aCols"+Left(Right(cVar,4),3))) .And. (Empty(aCols) .Or. aCols[1][1] == Nil)
			aCols := {}
			Aadd(aCols, Array(Len(aCampos) + 1))
		Else
			// Restaura aCols referente ao mes posicionado.
			If !Empty(&("aCols"+Left(Right(cVar,4),3)))
				aCols := aClone(&("aCols"+Left(Right(cVar,4),3)))
			Endif

			// Recupera dados da matriz de rateios do mes anterior para a matriz de rateios do mes atual
			For nX := 1 To Len(aCols)
				If !GdDeleted(nX)
					nValRat += aCols[nX][nPosValor]
					nPerRat += aCols[nX][nPosPerc]
				Endif
			Next nX

			// Se o rateio não estiver batendo, recalcula.
			If nOpc == 3 .and. nValRat <> nValOrc .and. round(nPerRat, 4) = 100
				nValRat := RecValor(nValOrc, aCols)
			Endif
		Endif
	Endif

	// Cria aCols e aHeader, apenas uma vez
	If (Empty(aHeader) .Or. !lCopia) .And. (Empty(aCols) .Or. aCols[1][1] == Nil)
		SX3->(DbSetOrder(2))  // X3_CAMPO.
		lCria := .F.
		For nX := 1 To Len(aCampos)
			// Cria aHeader apenas um vez
			If Len(aHeader) < Len(aCampos)
				If SX3->(dbSeek(Pad(aCampos[nX], 10), .F.))
					SX3->(aAdd(aHeader, {AllTrim(X3Titulo()), X3_CAMPO, X3_PICTURE, X3_TAMANHO, X3_DECIMAL, X3_VALID, X3_USADO, X3_TIPO, X3_ARQUIVO, X3_CONTEXT}))
					lCria := .T.
				EndIf
			Endif
		Next
		If lCria
			SX3->(dbSeek("EW_FILIAL", .F.))
			cUsado := SX3->X3_USADO
			aAdd(aHeader, {"Alias WT", "EW_ALI_WT", "", 9, 0,, cUsado, "C", "SEW", "V"})
			aAdd(aHeader, {"Recno WT", "EW_REC_WT", "", 9, 0,, cUsado, "N", "SEW", "V"})
		EndIf

		For nX := 1 To Len(aHeader)
			If Alltrim(aHeader[nX][2]) == "EW_PERC" // Percentual
				aHeader[nX][3] := Tm(0, aHeader[nX][4], aHeader[nX][5])
				aCols[1][nX] := CriaVar("EV_PERC",  .F.)
			ElseIf Alltrim(aHeader[nX][2]) == "EW_REC_WT"
				aCols[1][nX] := 0
			ElseIf Alltrim(aHeader[nX][2]) == "EW_ALI_WT"
				aCols[1][nX] := "SEW"
			Else
				aCols[1][nX] := CriaVar(aHeader[nX][2], .F.)
			Endif
		Next nX

		// Indica que a linha nao esta deletada
		aCols[1][nX] := .F.
	Endif
	nPosValor := GdFieldPos("EW_VALOR")
	nPosPerc  := GdFieldPos("EW_PERC")
	nPosRec   := GdFieldPos("EW_REC_WT")

	// Altera as validacoes em aHeader, devido o novo valor a ser rateado, enviado como parametro as
	// funcoes de calculo do percentual e do valor em funcao do percentual.
	aHeader[nPosPerc][6]  := "Fa020CalcV(" + cVar + ")"
	aHeader[nPosValor][6] := "Fa020CalcP(" + cVar + ")"

	DbSelectArea("SE7")
	DEFINE MSDIALOG oDlg TITLE STR0017 From 9, 0 To 29.2, 80 OF oMainWnd   //"Orçamento por centro de custo"
	oPanelAll := TPanel():New(0, 0, "", oDlg,,,,,, 0, 0, .F., .F.)
	oPanelAll:Align := CONTROL_ALIGN_ALLCLIENT

	oGet := MSGetDados():New(0, 0, 0, 0, nOpc, {|| Fa020LinOk(&cVar, nOpcA) },,, (nOpc == 3),,,,,,,, "Fin020Del", oPanelAll)
	oGet:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

	oPanelBot := TPanel():New(0, 0, "", oPanelAll,,,,,, 0, 23, .T., .F.)
	oPanelBot:Align := CONTROL_ALIGN_BOTTOM

	@ 0.3, 1.4 Say STR0018 + cMesExt + "/" + M->E7_ANO FONT oDlg:oFont of oPanelBot //"Rateio para o orçamento do Mês/Ano: "
	@ 1.0, 1.4 Say STR0019 FONT oDlg:oFont of oPanelBot  // "Valor a Ratear "
	@ 1.0, 7.6 Say             nValOrc Picture PesqPict("SE7", "E7_VALJAN1", 14) FONT oDlg:oFont of oPanelBot
	@ 1.0, 24  Say STR0020 FONT oDlg:oFont of oPanelBot  // "Valor Rateado  "
	@ 1.0, 32  Say oValRat VAR nValRat Picture PesqPict("SEW", "EW_VALOR", 14)   FONT oDlg:oFont of oPanelBot

	bOk     := {|| nOpcA := 1, If(oGet:TudoOk(), oDlg:End(), nOpcA := 0)}
	bCancel := {|| nOpcA := 0, oDlg:End()}
	ACTIVATE MSDIALOG oDlg CENTER ON INIT EnchoiceBar(oDlg, bOk, bCancel)

	If nOpcA == 1
		&("aCols"+Left(Right(cVar,4),3)) := aClone(aCols)
	Endif
Endif

Return

/*/{Protheus.doc} Fa020CalcP
Calcula percentual a partir do valor digitado.

@author  Claudio Donizete Souza
@version Protheus 12
@since   06/10/2003
/*/
Function Fa020CalcP(nValaRat)

LOCAL nX, lRet := .F.
Local nPosValor  := GdFieldPos("EW_VALOR")
Local nPosPerc   := GdFieldPos("EW_PERC")
Local nPrecis    := TamSX3("EW_PERC")[2]

If !Empty(M->EW_VALOR)

	// Calcula o percentual de acordo com o valor digitado.
	aCols[n][nPosPerc] := Round((M->EW_VALOR / nValaRat) * 100, nPrecis)

	// Calcula o valor rateado.
	nValRat := 0
	For nX := 1 To Len(aCols)
		// Se a linha nao estiver excluida, acumula o valor digitado.
		If !aTail(aCols[nX])
			If nX == n
				nValRat += M->EW_VALOR
			Else
				nValRat += aCols[nX][nPosValor]
			Endif
		EndIf
	Next nX
	oValRat:Refresh() // Atualiza o objeto na tela

	lRet := .T.
Else
	Alert(STR0021) //"E' necessario informar um valor maior que zero!"
Endif

Return lRet

/*/{Protheus.doc} Fa020CalcV
Calcula valor a partir do percentual digitado.

@author  Claudio Donizete Souza
@version Protheus 12
@since   06/10/2003
/*/
Function Fa020CalcV(nValaRat)

LOCAL nX, lRet := .F.
Local nPosValor  := GdFieldPos("EW_VALOR")
Local nPosPerc   := GdFieldPos("EW_PERC")
Local nPrecis    := TamSX3("EW_VALOR")[2]
Local nTotPer    := 0
Local nDifer     := 0

If !Empty(M->EW_PERC)

	// Calcula o valor de acordo com o percentual digitado.
	aCols[n][nPosValor] := Round((M->EW_PERC / 100) * nValaRat, nPrecis)

	// Calcula o valor rateado.
	nValRat := 0
	For nX := 1 To Len(aCols)
		// Se a linha nao estiver excluida, acumula o valor digitado.
		If !aTail(aCols[nX])
			If nX = n
				nTotPer += M->EW_PERC
			Else
				nTotPer += aCols[nX][nPosPerc]
			Endif
			nValRat += aCols[nX][nPosValor]
		EndIf
	Next nX

	// Se o total do percentual for 100, joga a diferença de centavos no último item.
	nDifer := (nValaRat - nValRat)
	If round(nTotPer, 4) = 100 .and. nDifer <> 0
		aTail(aCols)[nPosValor] += nDifer
		nValRat += nDifer
	Endif
	oValRat:Refresh() // Atualiza o objeto na tela

	lRet := .T.
Else
	Alert(STR0022) //"E' necessario informar um percentual maior que zero!"
Endif

Return lRet

/*/{Protheus.doc} Fa020LinOk
Analisa a linha digitada

@author  Claudio Donizete Souza
@version Protheus 12
@since   06/10/2003
/*/
Function Fa020LinOk(nValaRat, nOpca)

Local lRet       := .T.
Local nLen       := 0
Local nSoma      := 0
Local nPerc      := 0
Local nAscan     := 0
Local nPosCc     := GdFieldPos("EW_CCUSTO")
Local nPosValor  := GdFieldPos("EW_VALOR")
Local cPic       := PesqPict("SE7", "E7_VALJAN1", 19)
Local nX

// Se o centro de custo já estiver registrado e nao estiver apagado, nao permite novo
// rateio para o mesmo c.c
nAscan := Ascan( aCols, { |e| e[nPosCc] == aCols[n][nPosCC] .And. !e[len(e)] } )
If nAscan > 0 .And. n != nAscan .And. !aCols[n][Len(aCols[1])]
	Alert( STR0023 ) //"Centro de  custo já rateado"
	lRet := .F.
ElseIf Empty(aCols[n][nPosCC]) .And. !aCols[n][Len(aCols[1])] // Se o C.C estiver vazio, avisa o usuário
	Alert( STR0024 ) //"Informe um centro de custo válido"
	lRet := .F.
Else
	nLen := Len(aCols)
	For nX := 1 To nLen
		// Se a linha nao estiver excluída, soma o valor digitado.
		If !aTail(aCols[nX])
			nSoma += aCols[nX][nPosValor]
		EndIf
	Next

	// Se a soma for maior que o valor a ser distribuido ou se foi pressionado
	// Ok e a Soma for diferente do valor distribuido, avisa e invalida o valor
	// digitado
	If Val(Str(nSoma,17,2)) > Val(Str(nValaRat,17,2)) .Or. ;
		(nOpca == 1 .And. Val(Str(nSoma,17,2)) != Val(Str(nValaRat,17,2)))
		Help(" ", 1, "FA020RATEI",,;
		Pad(STR0025, 21) + " " + Trans(nValaRat, cPic) + CRLF +;  // "Valor do orçamento: "
		Pad(STR0026, 21) + " " + Trans(nSoma , cPic), 4, 0)       // "Total Rateado: "

		lRet := .F.
	Endif
EndIf

Return lRet

/*/{Protheus.doc} Fa020GrvRat
Gravação do rateio.

@author  Claudio Donizete Souza
@version Protheus 12
@since   17/10/2003
/*/
Static Function Fa020GrvRat(lCopia)

Local aMatRat    := {}
Local nValRat    := 0
Local nSEWRecNo  := 0
Local aMeses     := {"JAN", "FEV", "MAR", "ABR", "MAI", "JUN", "JUL", "AGO", "SET", "OUT", "NOV", "DEZ"}
Local nPosPerc   := GdFieldPos("EW_PERC")
Local nPosValor  := GdFieldPos("EW_VALOR")
Local nPosCCusto := GdFieldPos("EW_CCUSTO")
Local nPosItCta  := GdFieldPos("EW_ITEMCTA")
Local nPosClVl   := GdFieldPos("EW_CLVL")
Local nPosRecNo  := GdFieldPos("EW_REC_WT")
Local nX, nY
Default lCopia := .F.

DbSelectArea("SEW")
DbSetOrder(1)

// Para os doze meses do ano, grava os rateios informados
For nX := 1 To 12
	aMatRat := &("aCols"+aMeses[nX])
	If ValType(aMatRat) == "A"
		// Grava o rateio para o mes atual
		For nY := 1 To Len(aMatRat)
			nPerc     := aMatRat[nY][nPosPerc]
			nValRat   := aMatRat[nY][nPosValor]
			nSEWRecNo := If(lCopia, 0, aMatRat[nY][nPosRecNo])

			// Se a linha nao estiver excluída.
			If !GdDeleted(nY, aHeader, aMatRat) .and. nValRat <> 0
				// Pesquisa Natureza+Ano+Mes+Ident.
				If nSEWRecNo = 0
					RecLock("SEW", .T.)
					SEW->EW_FILIAL  := xFilial("SEW")
					SEW->EW_NATUREZ := SE7->E7_NATUREZ
					SEW->EW_ANO     := SE7->E7_ANO
					SEW->EW_MES     := StrZero(nX, 2)
					SEW->EW_CCUSTO  := aMatRat[nY][nPosCCusto]  // Centro de Custo
					SEW->EW_ITEMCTA := aMatRat[nY][nPosItCta]   // Item
					SEW->EW_CLVL    := aMatRat[nY][nPosClVl]    // Classe de Valor
					SEW->EW_MOEDA   := SE7->E7_CMOEDA
				Else
					SEW->(msGoTo(nSEWRecNo))
					RecLock("SEW", .F.)
				Endif
				SEW->EW_VALOR   := nValRat
				SEW->EW_PERC    := nPerc
				SEW->(MsUnlock())
			ElseIf nSEWRecNo > 0
				// Se a linha estiver excluída, exclui do SEW.
				SEW->(msGoTo(nSEWRecNo))
				If RecLock("SEW",.F.,.T.)
					dbDelete()
				Endif
				MsUnlock()
				SEW->(MsUnlock())
			Endif
		Next
	Endif
Next nX

Return

/*/{Protheus.doc} Fa020LeRat
Lê dados de rateios já cadastrados.

@author  Claudio Donizete Souza
@version Protheus 12
@since   17/10/2003
/*/
Static Function Fa020LeRat

Local aMeses     := {"JAN", "FEV", "MAR", "ABR", "MAI", "JUN", "JUL", "AGO", "SET", "OUT", "NOV", "DEZ"}
Local aCampos    := {"EW_CCUSTO", "EW_VALOR", "EW_PERC", "EW_ITEMCTA", "EW_CLVL"}
Local cChave     := ""
Local aColsMes   := {}
Local nX

SX3->(dbSetOrder(2))  // X3_CAMPO.
aHeader := {}
For nX := 1 To Len(aCampos)
	SX3->(dbSeek(Pad(aCampos[nX], 10), .F.))
	SX3->(aAdd(aHeader, {AllTrim(X3Titulo()), X3_CAMPO, X3_PICTURE, X3_TAMANHO, X3_DECIMAL, X3_VALID, X3_USADO, X3_TIPO, X3_ARQUIVO, X3_CONTEXT}))

	If aCampos[nX] == "EW_PERC" // Percentual
		aHeader[nX][3] := Tm(0, aHeader[nX][4], aHeader[nX][5])
	Endif
Next nX

SX3->(DbSeek("EW_FILIAL", .F.))
cUsado := SX3->X3_USADO
aAdd(aHeader, {"Alias WT", "EW_ALI_WT", "", 9, 0,, cUsado, "C", "SEW", "V"})
aAdd(aHeader, {"Recno WT", "EW_REC_WT", "", 9, 0,, cUsado, "N", "SEW", "V"})

// Le os rateios cadastrados.
SEW->(dbSetOrder(1))  // EW_FILIAL, EW_ANO, EW_MES, EW_MOEDA, EW_NATUREZ, EW_CCUSTO, EW_ITEMCTA, EW_CLVL.
For nX := 1 To 12
	aColsMes := &("aCols" + aMeses[nX])
	cChave := xFilial("SEW") + SE7->(E7_ANO + StrZero(nX, 2) + E7_CMOEDA + E7_NATUREZ)
	SEW->(MsSeek(cChave, .F.))
	While SEW->(!Eof() .And. EW_FILIAL + EW_ANO + EW_MES + EW_MOEDA + EW_NATUREZ == cChave)
		aAdd(aColsMes, {SEW->EW_CCUSTO, SEW->EW_VALOR, SEW->EW_PERC, SEW->EW_ITEMCTA, SEW->EW_CLVL, "SEW", SEW->(Recno()), .F.})
		SEW->(DbSkip())
	End
Next nX

aCols := aClone(aColsJan)

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³Fa020ValOrc³ Autor ³ Claudio Donizete Souza³ Data ³ 17/10/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida a inclusao do orcamento								³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³Fa020ValOrc³()												³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FA020VlInc() e X3_VALID do campo E7_MOEDA					³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa020ValOrc()
Local aArea    := GetArea()
Local aAreaSE7 := SE7->(GetArea())
Local lIntPFS  := SuperGetMV("MV_JURXFIN",,.F.) // Integração SIGAPFS x SIGAFIN
Local lRet     := .T.

If ! lIntPFS
	SE7->(dbSetOrder(1))  // E7_FILIAL, E7_NATUREZ, E7_ANO, E7_CMOEDA.
	If SE7->(MsSeek(xFilial("SE7") + M->E7_NATUREZ + M->E7_ANO + StrZero(M->E7_MOEDA, 2))) ;
		.Or. ! FreeForUse("SE7", M->E7_NATUREZ + M->E7_ANO + StrZero(M->E7_MOEDA, 2))
		lRet := .F.
		JurMsgErro(STR0055, "Fa020ValOrc", STR0056) // "Já existe registro cadastrado para com essa natureza, ano e moeda!" ##"Altere a natureza, ano ou moeda."
	EndIf
EndIf

RestArea(aAreaSE7)
RestArea(aArea)

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³FIN020DEL  ³ Autor ³ Claudio D. de Souza  ³ Data ³ 29/12/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Atualiza valores do rodape quando deleta linha             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³FIN020DEL()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³.T.                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³Generico                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Fin020del()
Local nPosValor	:= Ascan(aHeader	, {|e| Alltrim(e[2]) == "EW_VALOR" } )

__nExeDel++
// Como esta rotina eh chamada duas vezes pela exclusao na GetDados, controlar
// as chamadas para nao ocorrer erro nos calculos dos dados do rodape
If (__nExeDel%2)==0
	Return .T.
Endif

// Registro deletado -> eh o contrario pois pressionou <DEL> e o arquivo ainda esta com Flag trocado
If !GdDeleted()
	nValRat	-= aCols[n][nPosValor]
Else
	nValRat	+= aCols[n][nPosValor]
EndIf

If Type("oValRat")=="O"
	oValRat:Refresh()
Endif

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Ana Paula N. Silva     ³ Data ³17/11/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados     ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()
Local aRotina := {}
Local aRotinaNew

aAdd( aRotina,	{OemToAnsi(STR0004) ,"AxPesqui", 0 , 1,,.F.}) // "Pesquisar"
aAdd( aRotina,	{OemToAnsi(STR0005) ,"Fa020Visua", 0 , 2})  // "Visualizar"
aAdd( aRotina,	{OemToAnsi(STR0006) ,"Fa020Inclu", 0 , 3})  //"Incluir"
aAdd( aRotina,	{OemToAnsi(STR0007) ,"Fa020Alter", 0 , 4})  // "Alterar"
aAdd( aRotina,	{OemToAnsi(STR0008) ,"FA020Delet", 0 , 5, 3}) // "Excluir"
aAdd( aRotina,	{OemToAnsi(STR0038) ,"FA020Repl" , 0 , 4 })  // "Replicar"

//Ponto de entrada para inclusão de novos itens no menu aRotina
If ExistBlock("FI020ROT")
	aRotinaNew := ExecBlock("FI020ROT",.F.,.F.,aRotina)
	If (ValType(aRotinaNew) == "A")
		aRotina := aClone(aRotinaNew)
	EndIf
EndIf

Return(aRotina)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³F020ATUNAT ³ Autor ³ Claudio D. de Souza  ³ Data ³ 29/12/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Atualiza valores do rodape quando deleta linha             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³FIN020DEL()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³.T.                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³Generico                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function F020ATUNAT(cSinal,aOldVlrs)

Local dData		:= CTOD("")
Local nMes		:= 0
Local cCond 	:= " "
Local lAtuSldNat := .T.
Local aValores	:= {	SE7->E7_VALJAN1,;
							SE7->E7_VALFEV1,;
							SE7->E7_VALMAR1,;
							SE7->E7_VALABR1,;
							SE7->E7_VALMAI1,;
							SE7->E7_VALJUN1,;
							SE7->E7_VALJUL1,;
							SE7->E7_VALAGO1,;
							SE7->E7_VALSET1,;
							SE7->E7_VALOUT1,;
							SE7->E7_VALNOV1,;
							SE7->E7_VALDEZ1}


DEFAULT cSinal := ""
DEFAULT aOldVlrs := {}

SED->(DbSetOrder(1))
SED->(MsSeek(xFilial("SED") + SE7->E7_NATUREZ ))

If cPaisLoc == "BRA"
	cCond := If(SED->ED_COND=="D","P", SED->ED_COND)
EndIf


//Controle de Saldo de Naturezas
If lAtuSldNat
	For nMes := 1 to 12
		//Ultimo dia do mes (data do Orcado)
		dData := (LastDay(Ctod("01"+"/"+StrZero(nMes,2)+"/"+SE7->E7_ANO, "ddmmyy")))
		If !Empty(aOldVlrs)
			//Atualizo o valor anterior para o saldo da natureza (utilizado apenas na alteracao)
			AtuSldNat(SE7->E7_NATUREZ, dData , SE7->E7_MOEDA, "1", cCond, aOldVlrs[nMes], aOldVlrs[nMes],"-",,FunName(),"SE7",SE7->(Recno()),0)
		Endif

		//Atualizo o valor atual para o saldo da natureza
		AtuSldNat(SE7->E7_NATUREZ, dData , SE7->E7_MOEDA, "1", cCond, aValores[nMes], aValores[nMes],cSinal,,FunName(),"SE7",SE7->(Recno()),0)
	Next
Endif

Return



/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³FA020Repl ³ Autor ³ Mauricio Pequim Jr    ³ Data ³ 30/04/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa de Replicacao de Orcamentos                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³FA020Repl(ExpC1,ExpN1,ExpN2)                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FinA020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FUNCTION FA020Repl(cAlias, nReg, nOpc, lA020Auto, aPrgAut)

Static aPergRet := {}

Local lRet       := .F.
Local aArea      := GetArea()
Local cLockName  := "FINA020"
Local cPerg      := "FINSRF"  // Pergunta do Relatorio
Local aParamBox  := {}
Local cTitulo    := STR0028 //"Réplica de Orçamentos"
Local lCentered  := .T.
Local lCanSave   := .T.
Local lUserSave  := .T.
Local dData      := dDataBase

Default lA020Auto := .F.  //Controla se é via execauto
Default aPrgAut   := {}

//Arquivo de semaforo
If LockByName(cLockName, .T., .F.)
	aAdd(aParamBox, {1, STR0036, SE7->E7_ANO,           "", "", "", "", 50, .T.})  // "Replicar do Ano"
	aAdd(aParamBox, {1, STR0037, Soma1(SE7->E7_ANO, 4), "", "", "", "", 50, .T.})  // "Replicar P/ Ano"
	aAdd(aParamBox, {2, STR0038, STR0039, {STR0039, STR0040}, 60, "", .F.})        // "Replicar"###"Todos"###"Todos"###"Selecionar"
	aAdd(aParamBox, {1, STR0041, 0, "@E 999.99",, "", ".T.", 10, .F.})             // "Ajustar Valores (%)"
	aAdd(aParamBox, {2, STR0042, STR0043, {STR0043, STR0044}, 60, "", .F.})        // "Caso exista no ano de Replica"###"Atualizar"###"Atualizar"###"Não atualizar"

	If !lA020Auto //Acessa se programa esta sendo rodado via interface
		lRet := ParamBox(aParamBox, cTitulo, aPergRet,,, lCentered,,, /*oMainDlg*/,, lCanSave, lUserSave)
		aPergRet := AjRetParam(aPergRet, aParamBox)
	Else
		aPergRet := aPrgAut
	Endif

	If lRet .or. lA020Auto
		Processa({|lEnd| F020Replic()}, STR0045) //"Replicando Orçamentos..."
	Endif

	UnLockByName(cLockName, .T., .F.)
	RestArea(aArea)
Else
	MsgAlert(STR0029 + CRLF +;    // "A função de réplica de orçamentos esta sendo utilizada por"
				STR0030 + CRLF +; // "outro usuario. Por questoes de integridade de dados, não"
				STR0031 + CRLF +; // "é permitida a utilização desta rotina por mais de um usuário"
				STR0032, cTitulo) // "simultaneamente. Tente novamente mais tarde."
Endif

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³FA020Repl ³ Autor ³ Mauricio Pequim Jr    ³ Data ³ 30/04/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Programa de Replicacao de Orcamentos                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³F020Replic(ExpC1,ExpN1,ExpN2)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FinA020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function F020Replic()

Local cQuery     := ""
Local cChave     := SE7->(IndexKey(1))
Local aChave     := {}
Local nCT        := 0
Local cMarca     := GetMark()
Local aStru      := SE7->(DbStruct())
Local aCpoBrw    := {}
Local aOldVlrs   := {}
Local cTRBSE7    := GetNextAlias()
Local nCount     := Len(aStru)
Local nOpca      := 0
Local lAtualiza  := .T.	//Atualiza os rateios ja existentes
Local oOdlgKco, oTimer
Local nTimeOut   := SuperGetMv("MV_FATOUT",, 900) * 1000 	// Estabelece 15 minutos para que o usuarios selecione os orcamentos
Local nTimeMsg   := SuperGetMv("MV_MSGTIME",, 120) * 1000 	// Estabelece 02 minutos para exibir a mensagem para o usuário
		                                            	    // informando que a tela fechará automaticamente em XX minutos
Local lIntPFS    := SuperGetMV("MV_JURXFIN",,.F.)
Local lFSinc     := SuperGetMV("MV_JFSINC",.F.,'2') == "1"
Local nOperation := 0
Local bCondicao  := {|| .T.}
Local nOpca      := 1
Local cCampo     := ""
Local xValue
Local nIncrem    := 0
Local nI, nZ

Private ALTERA   := .F.
Private INCLUI   := .T.
Private aHeader  := {}
Private aCols 	 := {}

Private aColsJan := {}
Private aColsFev := {}
Private aColsMar := {}
Private aColsAbr := {}
Private aColsMai := {}
Private aColsJun := {}
Private aColsJul := {}
Private aColsAgo := {}
Private aColsSet := {}
Private aColsOut := {}
Private aColsNov := {}
Private aColsDez := {}

//Abro o arquivo de rateios de orcamentos
//Este sera utilizado na replicacao dos rateios do orcamento
//Como a gravacao deste estara numa transacao, tenho que abrir antes
dbSelectArea("SEW")

cQuery := ""
FOR nCt := 1 To Len(aStru)
	If aStru[nCt,2] <> "M"
		cQuery+= ","+Alltrim(aStru[nCt,1])
	Endif
Next nCt                            //aqui
cQuery := "SELECT "+SubStr(cQuery,2) + " "
cQuery += "FROM "+RetSqlName("SE7")+ " SE7 "
cQuery += "WHERE "
cQuery += "E7_FILIAL='"	+ xFilial("SE7") + "'"
cQuery += " AND E7_ANO= '" + aPergRet[1] + "'"
cQuery += " AND D_E_L_E_T_ = ' '"
cQuery += " ORDER BY " + SqlOrder(cChave)

Aadd(aStru, {"E7_OK","C",2,0})

//Monto array dos campos a serem mostrados no browse
aCpoBrw := F020CpoBrw()

aChave := Strtokarr2( cChave, "+", .F.)

// Criando o Objeto de ArqTemporario
_oFina020 := FwTemporaryTable():New(cTRBSE7)

// Criando a Strutura do objeto
_oFina020:SetFields(aStru)

// Criando o Indicie da Tabela
_oFina020:AddIndex("1",aChave)

//////////////////////////////////
// Criação da tabela temporaria //
//////////////////////////////////
_oFina020:Create()

Processa({|| SqlToTrb(cQuery, aStru, cTRBSE7)}) // Cria arquivo temporario
(cTRBSE7)->(DbGoTop())

//Caso tenha optado por selecionar quais orcamentos serao replicados
//Apresento tela para selecao
If aPergRet[3] == 2

	bCondicao := { || (cTRBSE7)->E7_OK == cMarca }

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz o calculo automatico de dimensoes de objetos     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aSize := MSADVSIZE()

	//Tela de selecao de titulos a serem liquidados
	DEFINE MSDIALOG oDlgKco TITLE cCadastro From aSize[7],0 To aSize[6],aSize[5] OF oMainWnd PIXEL
	oDlgKco:lMaximized := .T.
	oTimer:= TTimer():New((nTimeOut-nTimeMsg),{|| MsgTimer(nTimeMsg,oDlgKco) },oDlgKco) // Ativa timer
	oTimer:Activate()

	oPanel := TPanel():New(0,0,'',oDlgKco,, .T., .T.,, ,15,15,.T.,.T. )
	oPanel:Align := CONTROL_ALIGN_TOP

	@ 003, 005 Say STR0047+ aPergRet[1]+STR0048+ aPergRet[2] FONT oDlgKco:oFont			PIXEL Of oPanel  //"Nro. Liquida‡„o " //"Replicar Orcamentos de " //" para "

	oMark 		:= MsSelect():New(cTRBSE7,"E7_OK","",aCpoBrw,.F.,@cMarca,{50,oDlgKco:nLeft,oDlgKco:nBottom,oDlgKco:nRight})
	oMark:oBrowse:lhasMark := .t.
	oMark:oBrowse:lCanAllmark := .t.
	oMark:oBrowse:bAllMark := { || F020Invert(cTRBSE7,cMarca,.T.,oMark)}
	oMark:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

	ACTIVATE MSDIALOG oDlgKco ON INIT EnchoiceBar(oDlgKco,{|| nOpca := 1,oDlgKco:End()},{|| nOpca := 2,oDlgKco:End()}) VALID (oTimer:End(),.T.)
Endif

If nOpca == 1

	BEGIN TRANSACTION

	While !((cTRBSE7)->(Eof()))

		If (Eval(bCondicao))

			SE7->(dbSetOrder(1))  // E7_FILIAL, E7_NATUREZ, E7_ANO, E7_CMOEDA.
			aOldVlrs := {}

			//Posiciono no Rateio original para replicar rateios do orcamento
			If SE7->(MsSeek(xFilial("SE7")+(cTRBSE7)->(E7_NATUREZ+aPergRet[1]+E7_CMOEDA+E7_CESCR+E7_CCUSTO+E7_CPART+E7_CRATEIO)))
				//Limpo os arrays pois a rotina de leitura do Rateio foi projetada
				//para ler os rateios de uma natureza a cada vez
				//Caso não sejam zerados, ele acumula os rateios
				aHeader  := {}
				aCols    := {}
				aColsJan := {}
				aColsFev := {}
				aColsMar := {}
				aColsAbr := {}
				aColsMai := {}
				aColsJun := {}
				aColsJul := {}
				aColsAgo := {}
				aColsSet := {}
				aColsOut := {}
				aColsNov := {}
				aColsDez := {}

				Fa020LeRat()

				//Guardo valores antigos
				aOldVlrs	:= {	SE7->E7_VALJAN1,;
									SE7->E7_VALFEV1,;
									SE7->E7_VALMAR1,;
									SE7->E7_VALABR1,;
									SE7->E7_VALMAI1,;
									SE7->E7_VALJUN1,;
									SE7->E7_VALJUL1,;
									SE7->E7_VALAGO1,;
									SE7->E7_VALSET1,;
									SE7->E7_VALOUT1,;
									SE7->E7_VALNOV1,;
									SE7->E7_VALDEZ1,;
									SE7->E7_ANO}
			Endif

			//Gravo o registro replicado do orcamento
			If SE7->(MsSeek(xFilial("SE7")+(cTRBSE7)->(E7_NATUREZ+aPergRet[2]+E7_CMOEDA+E7_CESCR+E7_CCUSTO+E7_CPART+E7_CRATEIO)))
				lAtualiza  := (aPergRet[5] == 1)
				nOperation := 4
			Else
				lAtualiza  := .T.
				nOperation := 3
			Endif

			If lAtualiza
				RegToMemory("SE7", .T.)

				For nZ := 1 to nCount
					cCampo := (cTRBSE7)->(FieldName(nZ))
					If !Empty(cCampo) .and. SE7->(FieldPos(cCampo)) > 0 .and. cCampo != "E7_OK"
						If (cTRBSE7)->(ValType(FieldGet(FieldPos(cCampo)))) != "M"
							xValue := (cTRBSE7)->(FieldGet(nZ))
							M->(&cCampo) := xValue
						EndIf
					EndIf
				Next nZ

				// Gravo o ano do orcamento replicado
				M->E7_ANO := aPergRet[2]

				//Atualizo os valores do orcamento
				If aPergRet[4] != 0
					nIncrem := (aPergRet[4] / 100)
					M->E7_VALJAN1 += (M->E7_VALJAN1 * nIncrem)
					M->E7_VALFEV1 += (M->E7_VALFEV1 * nIncrem)
					M->E7_VALMAR1 += (M->E7_VALMAR1 * nIncrem)
					M->E7_VALABR1 += (M->E7_VALABR1 * nIncrem)
					M->E7_VALMAI1 += (M->E7_VALMAI1 * nIncrem)
					M->E7_VALJUN1 += (M->E7_VALJUN1 * nIncrem)
					M->E7_VALJUL1 += (M->E7_VALJUL1 * nIncrem)
					M->E7_VALAGO1 += (M->E7_VALAGO1 * nIncrem)
					M->E7_VALSET1 += (M->E7_VALSET1 * nIncrem)
					M->E7_VALOUT1 += (M->E7_VALOUT1 * nIncrem)
					M->E7_VALNOV1 += (M->E7_VALNOV1 * nIncrem)
					M->E7_VALDEZ1 += (M->E7_VALDEZ1 * nIncrem)
				Endif

				If FA020VlInc()

					// Grava o registro.
					RecLock("SE7", (nOperation == 3))
					For nZ := 1 to nCount
						cCampo := (cTRBSE7)->(FieldName(nZ))
						If !Empty(cCampo) .and. SE7->(FieldPos(cCampo)) > 0 .and. cCampo != "E7_OK"
							If (cTRBSE7)->(ValType(FieldGet(FieldPos(cCampo)))) != "M"
								xValue := M->(&cCampo)
								SE7->(FieldPut(FieldPos(cCampo), xValue))
							EndIf
						EndIf
					Next nZ

					// Fila de sincronização - Jurídico
					If lIntPFS .And. lFSinc
						J170GRAVA("SE7", xFilial("SE7") + SE7->E7_NATUREZ + SE7->E7_ANO + SE7->E7_CMOEDA + SE7->E7_CESCR + SE7->E7_CCUSTO + SE7->E7_CPART + SE7->E7_CRATEIO, cValToChar(nOperation))
					EndIf

					SE7->(MsUnlock())

					// Replica rateio do Orcamento.
					If Len(aHeader) > 0
						Fa020GrvRat(.T.)
					Endif

					// Controle de Saldo de Naturezas
					F020AtuNat("+", aOldVlrs)
				Endif
			Endif
		Endif
		dbselectarea(cTRBSE7)
		DbSkip()
	Enddo

	END TRANSACTION
Endif

// Excluo o TRB
If (_oFina020 <> NIL)
	_oFina020:Delete()
	_oFina020 := NIL
Endif

dbSelectArea("SE7")

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ F020Invert ³ Autor ³ Mauricio Pequim Jr    ³ Data ³ 05/05/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Marca / Desmarca titulos					  	     			 		 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Fina020																	    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function F020Invert(cAlias,cMarca,lTodos,oMark)
Local nReg := (cAlias)->(Recno())

DEFAULT lTodos  := .T.

DbSelectArea(cAlias)
If lTodos
	DbGoTop()
Endif
While !lTodos .Or. (cAlias)->(!Eof())
	If (cAlias)->E7_OK == cMarca
		RecLock(cAlias)
		Replace E7_OK With Space(02)
	Else
		RecLock(cAlias)
		Replace E7_OK With cMarca
		(cAlias)->(MsUnlock())
	Endif
	If lTodos
		(cAlias)->(dbSkip())
	Else
		Exit
	Endif
Enddo
DbGoTo(nReg)

Return(NIL)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ"±±
±±ºPrograma  ³AjRetParamºAutor  ³Alvaro Camillo Neto º Data ³  17/12/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Ajusta as repostas do aParambox                             º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ TOTVS		                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AjRetParam(aRet,aParamBox)
Local nX := 1

IF ValType(aRet) == "A" .AND. Len(aRet) == Len(aParamBox)
	For nX := 1 to Len(aParamBox)
		If aParamBox[nX][1] == 1
			aRet[nX] := aRet[nX]
		ElseIf aParamBox[nX][1] == 2 .AND. ValType(aRet[nX]) == "C"
			aRet[nX] := aScan(aParamBox[nX][4],{|x| Alltrim(x) == aRet[nX]})
		ElseIf aParamBox[nX][1] == 2 .AND. ValType(aRet[nX]) == "N"
			aRet[nX] := aRet[nX]
		Endif
	Next nX
ENDIF

Return aRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ"±±
±±ºPrograma  ³F020CpoBrwºAutor  ³Mauricio Pequim Jr  º Data ³  07/05/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Cria Array dos campos para Browse									  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ TOTVS		                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function F020CpoBrw()

Local aCpoBrowse	:= {}
Local aArea			:= GetArea()

dbSelectArea("SX3")
dbSetOrder(2)

AADD(aCpoBrowse,{"E7_OK"		,, " ","  "})

If MsSeek("E7_FILIAL")
	AADD(aCpoBrowse,{SX3->X3_CAMPO,,SX3->X3_TITULO,SX3->X3_PICTURE})
Endif

If MsSeek("E7_NATUREZ")
	AADD(aCpoBrowse,{SX3->X3_CAMPO,,SX3->X3_TITULO,SX3->X3_PICTURE})
Endif

If MsSeek("E7_MOEDA")
	AADD(aCpoBrowse,{SX3->X3_CAMPO,,SX3->X3_TITULO,SX3->X3_PICTURE})
Endif

RestArea(aArea)

Return (aCpoBrowse)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ"±±
±±ºPrograma  ³F020VldNatºAutor  ³Claudio D. de Souza º Data ³  14/06/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Validacao do campo E7_NATUREZ (SX3)  							  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Cadastro de orcamentos                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function F020VldNat
Local lRet     := .T.
Local lIntPFS  := SuperGetMV("MV_JURXFIN",,.F.) // Integração SIGAPFS x SIGAFIN

DbSelectArea("SED")
SED->(DbSetOrder(1))
SED->(MsSeek(xFilial("SED") + M->E7_NATUREZ))

If SED->ED_TIPO == '1'
	Help( " ", 1, "NATUREZ",, STR0049, 1, 0 ) // "Verifique se a natureza informada está classificada corretamente. Apenas naturezas do titpo analítico serão aceitas para este processo.!"
	lRet := .F.
Endif

If lRet .and. cPaisLoc == "BRA" .and. X3Usado("ED_COND")
	If !SED->ED_COND $ "RD"
		lRet := .F.
		Help( ,, "INVNAT",, STR0050, 1, 0)	//"Código da natureza inválido. É preciso escolher uma natureza em que sua condição (ED_COND) seja igual a R-Receita ou D-Despesa"
	Endif
EndIf

If lRet .And. lIntPFS .And. FindFunction("J235AVlNat") // Valida natureza conforme integração com SIGAPFS
	lRet := J235AVlNat(M->E7_NATUREZ)
EndIf

SED->(dbCloseArea())

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} FA020VlInc()
Validação da inclusão do orçamento

@Return lRet  .T./.F. As informações são válidas ou não.

@author Jorge Luis Branco Martins Junior
@since 29/01/2018
@version 1.0
/*/
//-------------------------------------------------------------------
Function FA020VlInc()
Local lRet := .T.

/*
Valida a chave única da tabela SE7, de forma que só seja permitida a inclusão de mais de um Orçamento
para o mesmo Ano, Natureza e Moeda quando a integração com o SIGAPFS estiver ligada (MV_JURXFIN = .T.).
Para integração ligada a chave válida é a do SX2:
E7_FILIAL+E7_NATUREZ+E7_ANO+E7_CMOEDA+E7_CESCR+E7_CCUSTO+ E7_CPART+E7_CRATEIO
Integração desligada vale a antiga chave: E7_FILIAL+E7_NATUREZ+E7_ANO+E7_CMOEDA
*/

lRet := Fa020ValOrc() .and. FA020VlRat() .and. FA020VlPFS() .and. FA020VlChv()

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} FA020VlAlt()
Valida a alteração do orçamento

@Return lRet  .T./.F. As informações são válidas ou não.

@author Jorge Luis Branco Martins Junior
@since 26/01/2018
@version 1.0
/*/
//-------------------------------------------------------------------
Function FA020VlAlt()
Local lRet := .T.
lRet := lRet .and. FA020VlRat() // Validação referente ao rateio.
lRet := lRet .and. FA020VlPFS() // Validação referente a integração SIGAPFS x SIGAFIN
Return lRet


/*/{Protheus.doc} FA020VlRat()
Valida se os valores digitados batem com os valores do rateio.

@Return lRet  .T./.F. As informações são válidas ou não.

@author Felipe Raposo
@since  08/10/2018
/*/
Static Function FA020VlRat()

Local lRet       := .T.
Local aCpoMeses  := {"E7_VALJAN1", "E7_VALFEV1", "E7_VALMAR1", "E7_VALABR1", "E7_VALMAI1", "E7_VALJUN1", "E7_VALJUL1", "E7_VALAGO1", "E7_VALSET1", "E7_VALOUT1", "E7_VALNOV1", "E7_VALDEZ1"}
Local lRateio    := .F.
Local aMatRat    := {}
Local nPosValor  := GdFieldPos("EW_VALOR")
Local nPosPerc   := GdFieldPos("EW_PERC")
Local cPicVlr    := PesqPict("SE7", "E7_VALJAN1", 19)
Local nPerRat    := 0
Local nMes, nX

// Valida o rateio de todos os meses.
For nMes := 1 To 12
	aMatRat := &("aCols" + SubStr(aCpoMeses[nMes], 7, 3))
	If ValType(aMatRat) == "A" .and. !empty(aMatRat)
		// Calcula o valor total do rateio.
		lRateio := .F.
		nValRat := 0
		nPerRat := 0
		For nX := 1 To Len(aMatRat)
			If !GdDeleted(nX,, aMatRat)
				lRateio := .T.
				nValRat += aMatRat[nX][nPosValor]
				nPerRat += aMatRat[nX][nPosPerc]
			Endif
		Next nX

		// Verifica se houve rateio.
		If lRateio
			// Se há divergência do valor, não deixa continuar.
			nValor := &("M->" + aCpoMeses[nMes])
			If nValRat <> nValor
				// Se o percentual rateado estiver totalizando 100, redistribui os valores.
				If round(nPerRat, 4) = 100
					// Recalcula valores de acordo com os percentuais informados.
					nValRat := RecValor(nValor, aMatRat)
				Else
					Help(" ", 1, "FA020RATEI",,;
					STR0018 + " " + MesExtenso(nMes) + "/" + M->E7_ANO + CRLF + CRLF +;  // "Rateio para o orçamento do Mês/Ano: "
					Pad(STR0025, 21) + " " + Trans(nValor,  cPicVlr) + CRLF +;           // "Valor do orçamento "
					Pad(STR0026, 21) + " " + Trans(nValRat, cPicVlr), 4, 0)              // "Total Rateado "
					lRet := .F.
					Exit
				Endif
			Endif
		Endif
	Endif
Next nMes

Return lRet

/*/{Protheus.doc} RecValor()
Recalcula valores de acordo com os percentuais informados.

@Return nValRat - valor rateado.

@author Felipe Raposo
@since  10/10/2018
/*/
Static Function RecValor(nValor, aMatRat)

Local nValRat    := 0
Local nPerRat    := 0
Local nDifer     := 0
Local nPosValor  := GdFieldPos("EW_VALOR")
Local nPosPerc   := GdFieldPos("EW_PERC")
Local nPrecis    := TamSX3("EW_VALOR")[2]
Local nX

nValRat := 0
For nX := 1 To Len(aMatRat)
	If !GdDeleted(nX,, aMatRat)
		aMatRat[nX][nPosValor] := Round((aMatRat[nX][nPosPerc] / 100) * nValor, nPrecis)
		nValRat += aMatRat[nX][nPosValor]
	Endif
Next nX

// Se o total do percentual for 100, joga a diferença de centavos no último item.
nDifer := (nValor - nValRat)
If nDifer <> 0
	aTail(aMatRat)[nPosValor] += nDifer
	nValRat += nDifer
Endif

Return nValRat

//-------------------------------------------------------------------
/*/{Protheus.doc} FA020VlPFS()
Validação do preenchimento dos campos da integração SIGAPFS x SIGAFIN.
Será executada ao confirmar uma inclusão ou alteração.

@Return lRet  .T./.F. As informações são válidas ou não.

@author Jorge Luis Branco Martins Junior
@since 26/01/2018
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function FA020VlPFS()
Local lRet    := .T.
Local lIntPFS := SuperGetMV("MV_JURXFIN",,.F.) // Integração SIGAPFS x SIGAFIN

If lIntPFS .And. FindFunction("JurVldNCC")
	lRet := JurVldNCC( NIL /*oModel*/, "" /*cModelId*/, "E7_NATUREZ", "E7_CESCR", "E7_CCUSTO", "E7_CPART", "E7_SIGLA", "E7_CRATEIO")
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} FA020VlChv()
Valida a chave única da tabela SE7, de forma que só seja permitida a
inclusão de mais de um Orçamento para o mesmo Ano, Natureza e Moeda
quando a integração com o SIGAPFS estiver ligada (MV_JURXFIN = .T.).

@Return lRet  .T./.F. As informações são válidas ou não.

@author Cristina Cintra
@since 15/08/2018
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function FA020VlChv()
Local lIntPFS := SuperGetMV("MV_JURXFIN",,.F.) // Integração SIGAPFS x SIGAFIN
Local lRet    := .T.

If lIntPFS
	lRet := ExistChav("SE7", M->E7_NATUREZ + M->E7_ANO + StrZero(M->E7_MOEDA, 2) + M->E7_CESCR + M->E7_CCUSTO + M->E7_CPART + M->E7_CRATEIO)
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} FA020Cmpos()
Verifica se a integracao com o SIGAPFS está ativa, para habilitar
os campos da integração na tela.

@Return aCampos Indica os campos que serão exibidos na tela.

@author Jorge Luis Branco Martins Junior
@since 26/01/2018
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function FA020Cmpos()
Local aStruct    := FWFormStruct(1,'SE7'):GetFields()
Local lIntPFS    := SuperGetMV("MV_JURXFIN",,.F.) // Integração SIGAPFS x SIGAFIN
Local nCt        := 0
Local aCampos    := {}
Local cCamposPFS := ""
Local cCampo     := ""
Local lUsado     := .T.
Local lNivel     := .T.

If lIntPFS // Se a integração SIGAFIN x SIGAPFS estiver ativa, não exibe somente o campo de código do participante
	cCamposPFS := "E7_CPART"
Else // Se a integração SIGAFIN x SIGAPFS não estiver ativa, não exibe os campos da integração
	cCamposPFS := "E7_CESCR|E7_DESCR|E7_SIGLA|E7_CPART|E7_DPART|E7_CRATEIO|E7_DRATEIO"
EndIf

For nCt := 1 To Len(aStruct)
	cCampo := aStruct[nCt][3]
	lUsado := X3Usado(cCampo)
	lNivel := cNivel >= GetSx3Cache(cCampo, "X3_NIVEL")

	If lUsado .and. lNivel .And. !(cCampo $ cCamposPFS)
		AADD(aCampos,cCampo)
	EndIf
Next nCt

Return aCampos

//-------------------------------------------------------------------
/*/{Protheus.doc} FA020EdtCp
Função que monta array com os campos editáveis.

@param  cCampos   , array, Campos que serão exibidos na tela
@param  aNoEditCpo, array, Campos que não devem ser editados
@return aEditCpo  , array, Todos os campos que serão editáveis

@author  Jonatas Martins
@since   30/01/2017
/*/
//-------------------------------------------------------------------
Static Function FA020EdtCp(aCampos, aNoEditCpo)
	Local aEditCpo := {}

	Default aCampos     := {}
	Default aNoEditCpo  := {}

	If Empty(aNoEditCpo)
		aEditCpo := aCampos
	Else
		aEval(aCampos, {|cCampo| IIF(aScan(aNoEditCpo, cCampo) == 0,;
									 Aadd(aEditCpo, cCampo), "")})
	EndIf

Return ( aEditCpo )

//-------------------------------------------------------------------
/*/{Protheus.doc} F020When()
Função de When para os seguintes campos da integração com o SIGAPFS:
 - E7_CESCR, E7_CCUSTO, E7_SIGLA e E7_CRATEIO

@param cTipo       Tipo do campo para validação do When
                   "1"=Escritorio,
                   "2"=Centro de Custo,
                   "3"=Código e Sigla do participante do centro de custo,
                   "4"=Tabela de Rateio

@Return lRet       .T./.F. Indica se o campo ficará habilitado ou não.

@author Jorge Luis Branco Martins Junior
@since 26/01/2018
@version 1.0
/*/
//-------------------------------------------------------------------
Function F020When(cTipo)
Local lRet     := .T.
Local lIntPFS  := SuperGetMV("MV_JURXFIN",,.F.) // Integração SIGAPFS x SIGAFIN

If lIntPFS .And. FindFunction("JurWhNatCC")
	lRet := JurWhNatCC(cTipo, "", "E7_NATUREZ", "E7_CESCR", "E7_CCUSTO", "E7_CPART", "E7_CRATEIO")
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} F020VldCC()
Validação do campo E7_CCUSTO

@Return lRet Indica se o valor preenchido é válido.

@author Jorge Luis Branco Martins Junior
@since 25/01/2018
@version 1.0
/*/
//-------------------------------------------------------------------
Function F020VldCC()
Local lRet    := .T.
Local lMVC    := .F. // Indica se a rotina é MVC
Local lIntPFS := SuperGetMV("MV_JURXFIN",,.F.) // Integração SIGAPFS x SIGAFIN

If lIntPFS .And. FindFunction("JVldCTTNS7") // Realizar a validação de centro de custo por escritório, somente se a integração estiver ativa.
	lRet := JVldCTTNS7("E7_CESCR", "E7_CCUSTO",, lMVC)
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} F020VldEsc()
Validação do campo E7_CESCR

@Return lRet Indica se o valor preenchido é válido.

@author Jorge Luis Branco Martins Junior
@since 29/01/2018
@version 1.0
/*/
//-------------------------------------------------------------------
Function F020VldEsc()
Local lRet    := .T.

lRet := ExistCpo('NS7', M->E7_CESCR, 1)

If lRet .And. FindFunction("JAVLDCAMPO")
	lRet := JAVLDCAMPO('','E7_CESCR','NS7','NS7_ATIVO','1')
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} F020VldRat()
Validação do campo E7_CRATEIO

@Return lRet Indica se o valor preenchido é válido.

@author Jorge Luis Branco Martins Junior
@since 29/01/2018
@version 1.0
/*/
//-------------------------------------------------------------------
Function F020VldRat()
Local lRet    := .T.

lRet := ExistCpo('OH6', M->E7_CRATEIO, 1)

If lRet .And. FindFunction("JAVLDCAMPO")
	lRet := JAVLDCAMPO('','E7_CRATEIO','OH6','OH6_ATIVO','1')
EndIf

Return lRet