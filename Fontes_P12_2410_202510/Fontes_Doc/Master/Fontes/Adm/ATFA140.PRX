#Include "Protheus.ch"
#Include "Atfa140.ch"

// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.

/*/

Ŀ
Funo     ATFA140     Autor  Alice Yamamoto         Data  08.06.00 
Ĵ
Descrio  Alterao de Centro de Custo                                 
Ĵ
Uso        SIGAATF                                                      
ٱ


*/
Function ATFA140

Private aPos    :=      {  8,  4, 11, 74 }
Private aRotina := MenuDef()

//Ŀ
// Define o cabealho da tela de atualizaes                                
//
Private cCadastro := OemtoAnsi(STR0004)  //"Alterao de Centro de Custos"  

ATFXKERNEL()

//Ŀ
// Enderea a funo de BROWSE                                               
//
mBrowse( 6, 1,22,75,"SN1") 

Return

/*/

Ŀ
Funo     AF140Cus    Autor  Vincius Barreira      Data  03/08/93 
Ĵ
Descrio  Transferncia de centro de custo                             
Ĵ
 Uso       Generico                                                     
ٱ


*/
Function AF140CUS(cAlias,nReg,nOpc)
Local aSays := {}, aButtons := {}
Local nOpca := 0  

Pergunte("AFA140",.F. )
//Ŀ
// Verifica as perguntas selecionadas                                  
// mv_par01         // Para o Centro de Custo                          
// mv_par02         // Do Cdigo Base                                  
// mv_par03         // Do Cdigo Item                                  
// mv_par04         // At o Cdigo Base                               
// mv_par05         // At o Cdigo Item                               
//
AADD(aSays,OemToAnsi( STR0005 ) )  //"	Este programa tem o objetivo de alterar os Centros "
AADD(aSays,OemToAnsi( STR0006 ) )  //"de Custos dos bens de acordo com os parmetros esco-"
AADD(aSays,OemToAnsi( STR0007 ) )  //"lhidos. O Centro de Custo alterado ser o referente "
AADD(aSays,OemToAnsi( STR0008 ) )  //"a conta contabil do bem."

//Ŀ
// Inicializa o log de processamento                            
//
ProcLogIni( aButtons )

AADD(aButtons, { 5,.T.,{|| Pergunte("AFA140",.T. ) } } )
AADD(aButtons, { 1,.T.,{|| nOpca:= 1, If( Af140Base() .And. Af140CCusto(), FechaBatch(), nOpca:=0 ) }} )
AADD(aButtons, { 2,.T.,{|| FechaBatch() }} )

FormBatch( cCadastro, aSays, aButtons ,,,370)

If nOpca == 1     
	//Ŀ
	// Atualiza o log de processamento   
	//
	ProcLogAtu("INICIO")
	Processa({|lEnd| AF140PROC()} ) 
	//Ŀ
	// Atualiza o log de processamento   
	//
	ProcLogAtu("FIM")	
Endif

/*/

Ŀ
Funo     AF140PROC   Autor  Vincius Barreira      Data  05/01/96 
Ĵ
Descrio  Efetua a alterao dos c.custos e ativa gauge                
Ĵ
 Uso       SIGAATF                                                      
ٱ


*/
Function AF140PROC()

Local lAf140Grv := ExistBlock("AF140GRV")
Local lCcdp	:= GetMv("MV_ATFCCDP",.F.,.F.)

//Ŀ
// Posiciona no primeiro registro                          
//
dbSelectArea("SN1")
dbSetOrder(1)
dbSeek(xFilial("SN1")+mv_par02+mv_par03,.T.)
//Ŀ
// Inicia a proteo do processo via TTS              
//
Begin Transaction

//Ŀ
// Altera no Arquivo de informaes contbeis - SN3   
//
Procregua(SN3->(Recc()))
dbSelectArea("SN3")
dbSetOrder(1)
dbSeek(xFilial("SN3")+mv_par02+mv_par03,.T.)
While !Eof() .and. SN3->N3_FILIAL==xFilial("SN3") .And. SN3->N3_CBASE + SN3->N3_ITEM <= mv_par04 + mv_par05
	Incproc()

	If VAL(SN3->N3_BAIXA) > 0
		dbSkip()
		Loop
	Endif

	RecLock("SN3")
	SN3->N3_CCUSTO := mv_par01
	
	//Caso o N3_CCDESP esteja configurado para receber o mesmo valor, atribui o novo C. Custo
	If lCcdp	
		SN3->N3_CCDESP := mv_par01
	Endif
	
	If lAf140Grv
		ExecBlock("AF140GRV",.F.,.F.)
	Endif

	msUnlock()
	dbSkip()
Enddo
End Transaction

Return

/*/


Ŀ
Funo    Af140Base  Autor  Alice Yamamoto         Data  08.06.00 
Ĵ
Descrio  Verifica se a faixa cdigo  vlida                        
Ĵ
Sintaxe    af140Base()                                                
Ĵ
Parametros                                                            
Ĵ
 Uso       SIGAATF                                                    
ٱ


*/
Function Af140Base()
Local lRet := .T.

If mv_par02+mv_par03 > mv_par04 + mv_par05
	HELP(" ",1,"AFA140Meno")
	lREt := .F.
Endif

Return lRet

/*/


Ŀ
Funo    Af140CCusto Autor  Alice Yamamoto         Data  08.06.00 
Ĵ
Descrio  Verifica se a faixa cdigo  vlida                         
Ĵ
Sintaxe    af140Base()                                                 
Ĵ
Parametros                                                             
Ĵ
 Uso       SIGAATF                                                     
ٱ


*/
Function Af140CCusto()

Return NaoVazio(mv_par01) .And. Ctb105Cc(@mv_par01)

/*/


Ŀ
Programa  MenuDef    Autor  Ana Paula N. Silva      Data 10/12/06 
Ĵ
Descrio  Utilizacao de menu Funcional                               
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados     
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function MenuDef()
Local aRotina := { 	{ OemtoAnsi(STR0001), "AxPesqui"         , 0 , 1},; //"Pesquisar"
                     	{ OemtoAnsi(STR0002), "VIEWDEF.ATFA012 ", 0 , 2},; //"Visualizar"
                     	{ OemtoAnsi(STR0003), "AF140Cus"         , 0 , 4} } //"Alt.C.Custo"
Return(aRotina)
        
