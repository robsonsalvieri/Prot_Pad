#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "msobject.ch"
#include "PROTHEUS.CH"
#include "FWMVCDEF.CH"
#include "backoffice.sv.control.paramutil.ch"

namespace totvs.protheus.backoffice.sv.control.paramutil

//-------------------------------------------------------------------
/*/{Protheus.doc} ControlSvParamUtil
Classe para tratativas de parâmetros a serem utilizados no SmartView.

@author Controladoria
@since 24/04/2024
@version 1.0    
/*/
//-------------------------------------------------------------------
class ControlSvParamUtil
    
    public method new() as object

    @Get("/api/controladoria/smartview/v1/options/:modulo/:objeto/:funcao/:opcao")
    public method getComboParam()

    @Get("/api/controladoria/smartview/v1/options/getBranches")
    public method getBranches()

    @Get("/api/controladoria/smartview/v1/options/getClassification")
    public method getClassification()

    @Get("/api/controladoria/smartview/v1/options/getCoins")
    public method getCoins()

    static method GetOpcoes()       as array
    static method processGetCombo() as character

    static method hiddenParam() as json

    protected method getQueryClassification() as character
    protected method getQueryCoins() as character

    private data cFuncao as character
    private data cModulo as character
    private data cObjeto as character
    private data cData   as character
    private data cOpcao  as character
    private data jPath   as object
    private data aOptions as array
    
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
Construtor da classe

@author Controladoria
@since 24/04/2024
@version 1.0
/*/
//-------------------------------------------------------------------
method new() class ControlSvParamUtil
    ::cFuncao   := ''
    ::cModulo   := ''
    ::cObjeto   := ''
    ::cData     := ''
    ::cOpcao    := ''
    ::jPath     := Nil
    ::aOptions  := {}
return Self

//-------------------------------------------------------------------
/*/{Protheus.doc} hiddenParam
Retorna um json com os parâmetros a serem ocultos.

@return Json

@author Controladoria
@since 24/04/2024
@version 1.0    
/*/
//-------------------------------------------------------------------
method hiddenParam(aParams as array, cPerg as character) as json class ControlSvParamUtil

    local jParams := JsonObject():new() as json
    local oObj    := FWSX1Util():new()  as  object 
    local nI         as numeric
    local nTotParams as numeric
    local nPosParam  as numeric
    local aPerg      as array
  
    default aParams := {}
    default cPerg   := ""    
  
    //Recupera a estrutura dos parâmetros
    if oObj:ExistPergunte(cPerg)
        oObj:AddGroup(Alltrim(cPerg))
        oObj:SearchGroup()

        aPerg := oObj:GetGroup(Alltrim(cPerg))[2]
    endif    

    //Oculta parâmetro da SX1 no relatório
    nTotParams := len(aParams)
    jParams["parameters"] := Array(nTotParams)

    for nI := 1 To nTotParams
        jParams["parameters"][nI] := JsonObject():new()
        jParams["parameters"][nI]["name"]       := aParams[nI]
        jParams["parameters"][nI]["visibility"] := "Hidden"

        nPosParam := AScan(aPerg, {|x| UPPER(Alltrim(x:CX1_VAR01)) == aParams[nI]})

        if nPosParam > 0
            if aPerg[nPosParam]:CX1_TIPO == "C"
                jParams["parameters"][nI]["value"] := ""
            elseif aPerg[nPosParam]:CX1_TIPO == "N"
                jParams["parameters"][nI]["value"] := 0
            elseif aPerg[nPosParam]:CX1_TIPO == "D"
                jParams["parameters"][nI]["value"] := CTOD("  /  /  ")
            endif
        endif        
    next nI
    
return jParams

//-------------------------------------------------------------------
/*/{Protheus.doc} getComboParam
Retorna o combo conforme Path Params passado.

@return oRest:setResponse (com JSON de retorno da api)

@author Controladoria
@since 24/04/2024
@version 1.0   
/*/
//-------------------------------------------------------------------
method getComboParam() class ControlSvParamUtil

    ::jPath := oRest:getPathParamsRequest()
    If (::jPath <> Nil )
        ::cModulo  := ::jPath["modulo"]
        ::cObjeto  := ::jPath["objeto"]
        ::cfuncao  := ::jPath["funcao"]
        ::cOpcao   := ::jPath["opcao" ]  
    EndIf    
    
    ::aOptions := ::GetOpcoes(::cModulo , ::cObjeto, ::cfuncao, ::cOpcao)
    ::cData    := ::processGetCombo(::aOptions)

    aSize(::aOptions, 0)

return oRest:setResponse(::cData)

// ------------------------------------------------------------------ //
/*/{Protheus.doc} GetOpcoes
Busca o array com as opções do combo, conforme definido no
no objeto de negócio.

@param		cModulo Código do módulo (FAT/CRM/TMK)	
@param		cObjeto Nome do objeto de negócio.
@param		cFuncao Função do objeto de negócio.
@param		cOpcao  Parâmetro que será envaido na função.        

@return aReturn: Array com as opções do combo.

@author Controladoria
@since 24/04/2024
@version 1.0    
/*/
// ------------------------------------------------------------------ //
method GetOpcoes(cModulo as character, cObjeto as character, cFuncao as character, cOpcao as character) as array class ControlSvParamUtil

    Local aReturn   := {}  as array
    Local lFound    := .F. as logical
    Local cFunc     := ""  as character
    Local cInName   := "totvs.protheus.backoffice.sv." as character
    Local cFnName   := ".treportsintegratedprovider."  as character

    cFunc   := cInName + cModulo + "." + cObjeto + cFnName + cFuncao
    lFound  := tlpp.ffunc(cFunc)

    If lFound
        aReturn := tlpp.call(cFunc, cOpcao)
    EndIf

return aReturn

// ------------------------------------------------------------------ //
/*/{Protheus.doc} processGetCombo
Processa o array formatando o JSON a ser retornado na API, para
os combos.

@param		aOpcoes Array com as opções que serão retornadas.
        
@return cReturn: JSON formatado para retornar na API.

@author Controladoria
@since 24/04/2024
@version 1.0
/*/
// ------------------------------------------------------------------ //
method processGetCombo(aOpcoes as array) as character class ControlSvParamUtil

    local cReturn := "" as character
    local aReturn := {} as array
    local jReturn := JsonObject():new() as json
    local nX,nPos := 0  as numeric
 
    For nX := 1 to Len(aOpcoes)
        aAdd(aReturn, JsonObject():new())
        nPos := Len(aReturn)

        aReturn[nPos]["key"]    := nX
        aReturn[nPos]["label"]  := Alltrim(aOpcoes[nX])
    Next nX

    jReturn["data"] := aReturn
    cReturn := jReturn:toJson()

return cReturn

//-------------------------------------------------------------------
/*/{Protheus.doc} getBranches
Retorna as filiais do sistema.

@return nil

@author Controladoria
@since 18/09/2024
@version 1.0   
/*/
//-------------------------------------------------------------------
method getBranches() Class ControlSvParamUtil

    local jQueryParams as json
    local jResponse    as json
    local jItem        as json
    local aSM0         as array
    local nSM0         as numeric
    local nPage        as numeric
    local nPageSize    as numeric
    local nCount       as numeric
    local nTotal       as numeric
    local nStart       as numeric
    local cSearch      as character
    local cAliasBase   as character
    local lEmpresa	   as logical 
    local lUnidNeg	   as logical
    local lHelp	       as logical
    local lExibeTela   as logical

    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAliasBase   := ""
    lEmpresa	 := .T.
    lUnidNeg	 := .T.
    lHelp        := .F.
    lExibeTela   := .F.
    aSM0         := AdmGetFil(.T.,lEmpresa,cAliasBase,lUnidNeg,lHelp,lExibeTela)
    nSM0         := 0
    nPage        := 1
    nPageSize    := 100
    nCount       := 1
    nTotal       := Len(aSM0)
    nStart       := 1
    cSearch      := ""

    if (jQueryParams != NIL)
        nPage   := iif(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)
        cSearch := iif(jQueryParams:hasProperty("q"),    AllTrim(jQueryParams["q"]),   "")

        nPage := iif(nPage <= 0, 1, nPage)
    endif

    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["remainingRecords"] := 0
    jResponse["keyProperty"]      := "M0_CODFIL"
    jResponse["descriptor"]       := {"M0_CODFIL": EncodeUTF8(STR0001),"M0_NOMRED": EncodeUTF8(STR0002)}

    if (nPage > 1)
        nStart := (nPage - 1) * nPageSize
    endif

    for nSM0 := nStart to Len(aSM0)

        if (!Empty(cSearch) .and. Upper(cSearch) <> Upper(AllTrim(aSM0[nSM0][1])) .and. Upper(cSearch) <> Upper(AllTrim(aSM0[nSM0][2])))
            loop
        endif

        if (nCount > nPageSize)
            jResponse["remainingRecords"] := nTotal - (nPage * nPageSize)
            jResponse["nextPageUrl"]      := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")
            exit
        endif

        jItem := {"M0_CODFIL": EncodeUTF8(aSM0[nSM0][1]),"M0_NOMRED": EncodeUTF8(AllTrim(aSM0[nSM0][2]))}
        AAdd(jResponse["data"], jItem)

        nCount++
    next nSM0

    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aSM0)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} getClassification
Retorna as classificações patrimoniais da SN0

@return nil

@author Controladoria
@since 06/11/2024
@version 1.0   
/*/
//-------------------------------------------------------------------
method getClassification() Class ControlSvParamUtil

    local jQueryParams as json
    local jResponse    as json
    local jItem        as json
    local nPage        as numeric
    local nPageSize    as numeric
    local nCount       as numeric
    local nTotal       as numeric
    local nStart       as numeric
    local cQuery       as character
    local cSearch      as character
    local cAliasBase   as character

    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAliasBase   := ""
    nPage        := 1
    nPageSize    := 100
    nCount       := 1
    nStart       := 1
    cSearch      := ""

    if (jQueryParams != NIL)
        nPage   := iif(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)
        cSearch := iif(jQueryParams:hasProperty("q"),    AllTrim(jQueryParams["q"]),   "")

        nPage := iif(nPage <= 0, 1, nPage)
    endif

    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["remainingRecords"] := 0
    jResponse["keyProperty"]      := "N0_CHAVE"
    jResponse["descriptor"]       := {"N0_CHAVE": EncodeUTF8(STR0003),"N0_DESC01": EncodeUTF8(STR0004)}

    if (nPage > 1)
        nStart := (nPage - 1) * nPageSize
    endif

    cQuery := self:getQueryClassification()

    cAliasBase := MPSysOpenQuery(cQuery)
    
    dbSelectArea(cAliasBase)
    Count To nTotal

    //Posiciona no registro inicial
    (cAliasBase)->(dbGoTop())

    while (cAliasBase)->(!Eof())  

        if (!Empty(cSearch) .and. Upper(cSearch) <> Upper(AllTrim((cAliasBase)->N0_CHAVE)) .and. Upper(cSearch) <> Upper(AllTrim((cAliasBase)->N0_DESC01)))
            (cAliasBase)->(dbSkip())
            loop
        endif

        jItem := {"N0_CHAVE": EncodeUTF8(AllTrim((cAliasBase)->N0_CHAVE)),"N0_DESC01": EncodeUTF8(AllTrim((cAliasBase)->N0_DESC01))}
        AAdd(jResponse["data"], jItem)

        nCount++
        (cAliasBase)->(dbSkip())
    endDo

    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} getQueryClassification
Retorna a query da SN0 montada

@return nil

@author Controladoria
@since 06/11/2024
@version 1.0   
/*/
//-------------------------------------------------------------------
method getQueryClassification() as character class ControlSvParamUtil

    local cQuery      := "" as character 
    local nParamOrder := 1  as numeric
    local oStatement as object

    cQuery := "SELECT N0_CHAVE, N0_DESC01 "
	cQuery += "FROM "+RetSqlName("SN0")+" SN0 "
    cQuery += "WHERE SN0.N0_TABELA =  ? "
	cQuery += "AND SN0.D_E_L_E_T_ = ? "
	cQuery += "ORDER BY N0_CHAVE"
    
    cQuery := ChangeQuery(cQuery)    
    oStatement := FWExecStatement():New(cQuery)  
    
    oStatement:SetString(nParamOrder++, "07")
    oStatement:SetString(nParamOrder++, " ")

    cQuery := oStatement:GetFixQuery()

    FreeObj(oStatement)

return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} getCoins
Retorna as moedas da CTO

@return nil

@author Controladoria
@since 06/11/2024
@version 1.0   
/*/
//-------------------------------------------------------------------
method getCoins() Class ControlSvParamUtil

    local jQueryParams as json
    local jResponse    as json
    local jItem        as json
    local nPage        as numeric
    local nPageSize    as numeric
    local nCount       as numeric
    local nTotal       as numeric
    local nStart       as numeric
    local cQuery       as character
    local cSearch      as character
    local cAliasBase   as character

    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAliasBase   := ""
    nPage        := 1
    nPageSize    := 100
    nCount       := 1
    nStart       := 1
    cSearch      := ""

    if (jQueryParams != NIL)
        nPage   := iif(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)
        cSearch := iif(jQueryParams:hasProperty("q"),    AllTrim(jQueryParams["q"]),   "")

        nPage := iif(nPage <= 0, 1, nPage)
    endif

    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["remainingRecords"] := 0
    jResponse["keyProperty"]      := "CTO_MOEDA"
    jResponse["descriptor"]       := {"CTO_MOEDA": EncodeUTF8(STR0005),"CTO_SIMB": EncodeUTF8(STR0006), "CTO_DESC": EncodeUTF8(STR0002)}

    if (nPage > 1)
        nStart := (nPage - 1) * nPageSize
    endif

    cQuery := self:getQueryCoins()

    cAliasBase := MPSysOpenQuery(cQuery)
    
    dbSelectArea(cAliasBase)
    Count To nTotal

    //Posiciona no registro inicial
    (cAliasBase)->(dbGoTop())

    while (cAliasBase)->(!Eof())

        if (!Empty(cSearch) .and. Upper(cSearch) <> Upper(AllTrim((cAliasBase)->CTO_MOEDA)) .and. ; 
            Upper(cSearch) <> Upper(AllTrim((cAliasBase)->CTO_SIMB)) .and. ;
            Upper(cSearch) <> Upper(AllTrim((cAliasBase)->CTO_DESC)))
            (cAliasBase)->(dbSkip())
            loop
        endif

        jItem := {"CTO_MOEDA": EncodeUTF8(AllTrim((cAliasBase)->CTO_MOEDA)),;
                  "CTO_SIMB" : EncodeUTF8(AllTrim((cAliasBase)->CTO_SIMB)),; 
                  "CTO_DESC" : EncodeUTF8(AllTrim((cAliasBase)->CTO_DESC))}

        AAdd(jResponse["data"], jItem)

        nCount++
        (cAliasBase)->(dbSkip())
    endDo

    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} getQueryCoins
Retorna a query da CTO montada

@return nil

@author Controladoria
@since 06/11/2024
@version 1.0   
/*/
//-------------------------------------------------------------------
method getQueryCoins() as character class ControlSvParamUtil

    local cQuery      := "" as character 
    local nParamOrder := 1  as numeric
    local oStatement as object

    cQuery := "SELECT CTO_MOEDA, CTO_SIMB, CTO_DESC "
	cQuery += "FROM "+RetSqlName("CTO")+" CTO "
    cQuery += "WHERE CTO_FILIAL = ? "
    cQuery += "AND CTO_MOEDA BETWEEN ? AND ? "
	cQuery += "AND CTO.D_E_L_E_T_ = ? "
	cQuery += "ORDER BY CTO_MOEDA"
    
    cQuery := ChangeQuery(cQuery)    
    oStatement := FWExecStatement():New(cQuery)  
    
    oStatement:SetString(nParamOrder++, FwxFilial("CTO"))
    oStatement:SetString(nParamOrder++, "01")
    oStatement:SetString(nParamOrder++, "05")
    oStatement:SetString(nParamOrder++, " ")

    cQuery := oStatement:GetFixQuery()

    FreeObj(oStatement)

return cQuery
