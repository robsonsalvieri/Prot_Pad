#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#Include "Protheus.ch"   
#include "backoffice.sv.atf.balancetodepreciate.bra.ch"

namespace totvs.protheus.backoffice.sv.atf.balancetodepreciate.treportsintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN3, SN1, CTT, CT1", name="Saldo a Depreciar", country="ALL", customTables="SN1, SN3, CTT")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BalanceToDepreciateSmartViewBusinessObject
Classe para criação do Objeto de Negócio Saldo a Depreciar para o SmartView
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class BalanceToDepreciateSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()             as object
    public method getDescription()  as character
    public method getAreas()        as array
    public method getData()         as object
    public method getSchema()       as object

    //usados na internacionalização
    public method preschema()       as object 
    public method preData()         as object
    public method processData()     as json 

    protected method getQuery() 

    protected data cAlias       as character
    protected data aFields      as array
    protected data aAllFields   as array
    protected data jItens       as json
    protected data oStatement   as object
    protected data oIntegratedProvider as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method new() as object Class BalanceToDepreciateSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Saldo a Depreciar"
    self:setPergunte("ATR050")   //Indica o pergunte que será utilizado no relatório

    ::aAllFields := {}
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getDescription() as character class BalanceToDepreciateSmartViewBusinessObject
return STR0002 //"Objeto contendo informações do relatório Saldo a Depreciar"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getAreas() as array class BalanceToDepreciateSmartViewBusinessObject
return {STR0003} //"Ativo Fixo"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class BalanceToDepreciateSmartViewBusinessObject

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class BalanceToDepreciateSmartViewBusinessObject
    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class BalanceToDepreciateSmartViewBusinessObject

return ::jItens

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@return object: self:oData
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class BalanceToDepreciateSmartViewBusinessObject

    local cDescMoeda01 := AllTrim(SuperGetMv("MV_SIMB1")) as character
    local cDescMoeda02 := AllTrim(SuperGetMv("MV_SIMB2")) as character
    local cDescMoeda03 := AllTrim(SuperGetMv("MV_SIMB3")) as character
    local cDescMoeda04 := AllTrim(SuperGetMv("MV_SIMB4")) as character
    local cDescMoeda05 := AllTrim(SuperGetMv("MV_SIMB5")) as character
    local cMascCta     := SuperGetMv("MV_MASCARA")        as character
    local cCompanyName := "" as character
    local cBranchName  := "" as character
    local nX           := 0  as numeric
    
    CTBsetValueMVPAR(oFilter:getParameters(), self:cPergunte) //Carrega lista de parametros para a memória

    self:oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := self:oIntegratedProvider:getCompanyName()
    cBranchName  := self:oIntegratedProvider:getBranchName()

    self:getQuery(oFilter)
    self:cAlias := self:oStatement:OpenAlias()

    while (self:cAlias)->(!Eof())
	    ::jItens := JsonObject():new()

        For nX := 1 To Len(self:aAllFields)
            if self:aAllFields[nX]:getType() == "date"
                ::jItens[self:aAllFields[nX]:getName()] := totvs.framework.treports.date.stringToTimeStamp((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
            elseif self:aAllFields[nX]:getType() == "string"
                ::jItens[self:aAllFields[nX]:getName()] := AllTrim((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
            else
                ::jItens[self:aAllFields[nX]:getName()] := (self:cAlias)->&(self:aAllFields[nX]:getRealName())
            endif
		Next nX        

        ::jItens[ "N3_CCONTAB_M" ] := EntidadeCTB((self:cAlias)->N3_CCONTAB ,0,0,Nil/*Tamanho*/,.F.,cMascCta,""/*Separador*/,"CT1",,,,.F.)

        ::jItens[ "DESCCONTA"    ] := (self:cAlias)->CT1_DESC01
        ::jItens[ "DESCMOEDA01"  ] := cDescMoeda01
        ::jItens[ "DESCMOEDA02"  ] := cDescMoeda02
        ::jItens[ "DESCMOEDA03"  ] := cDescMoeda03
        ::jItens[ "DESCMOEDA04"  ] := cDescMoeda04
        ::jItens[ "DESCMOEDA05"  ] := cDescMoeda05
        ::jItens[ "NomeEmpresa"  ] := cCompanyName
        ::jItens[ "NomeFilial"   ] := cBranchName

        self:processData()
        self:oData:appendData(::jItens)

        (self:cAlias)->(DBSKIP())
    EndDO

    (self:cAlias)->(DBCloseArea())

    self:oStatement:Destroy()

    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())
    FreeObj(self:oIntegratedProvider)
    FreeObj(self:oStatement)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getStruct
Prepara a estrutura dos campos
 
@param aFields array: array com os campos do relatório
 
@return array: array com a estrutura dos campos
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class BalanceToDepreciateSmartViewBusinessObject
    
    local aFieldsSN1 := {}  as array
    local aFieldsSN3 := {}  as array
    local aFieldsCT1 := {}  as array
    local aFieldsCTT := {}  as array

    aFieldsSN3 := { "N3_TIPO", "N3_CBASE", "N3_ITEM", "N3_CCUSTO", "N3_CCONTAB", "N3_VORIG1", "N3_AMPLIA1", "N3_VRCACM1", "N3_VRDACM1", "N3_VRCDA1",;
		            "N3_VORIG2", "N3_AMPLIA2", "N3_VRDACM2", "N3_VORIG3", "N3_AMPLIA3", "N3_VRDACM3", "N3_VORIG4", "N3_AMPLIA4", "N3_VRDACM4",;
		            "N3_VORIG5", "N3_AMPLIA5", "N3_VRDACM5", "N3_CDEPREC", "N3_CCDEPR"}

    aFieldsSN1 := {"N1_DESCRIC", "N1_PATRIM"}
    aFieldsCT1 := {"CT1_DESC01"}
    aFieldsCTT := {"CTT_DESC01"}

    self:oSchema:aliasToSchema("SN3", aFieldsSN3)
    self:oSchema:aliasToSchema("SN1", aFieldsSN1)
    self:oSchema:aliasToSchema("CT1", aFieldsCT1)
    self:oSchema:aliasToSchema("CTT", aFieldsCTT)

    //Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()

    self:oSchema:addProperty("N3_CCONTAB_M"    	,STR0004    ,"string" ,STR0004  ,"N3_CCONTAB_M",,,,,.F.)     // "Conta Mascara"  
    self:oSchema:addProperty("DESCMOEDA01"    	,STR0005    ,"string" ,STR0005  ,"DESCMOEDA01",,,,.F.)       // "Desc. Moeda 01"
    self:oSchema:addProperty("DESCMOEDA02"    	,STR0006    ,"string" ,STR0006  ,"DESCMOEDA02",,,,.F.)       // "Desc. Moeda 02"
    self:oSchema:addProperty("DESCMOEDA03"    	,STR0007    ,"string" ,STR0007  ,"DESCMOEDA03",,,,.F.)       // "Desc. Moeda 03"
    self:oSchema:addProperty("DESCMOEDA04"    	,STR0008    ,"string" ,STR0008  ,"DESCMOEDA04",,,,.F.)       // "Desc. Moeda 04"
    self:oSchema:addProperty("DESCMOEDA05"    	,STR0009    ,"string" ,STR0009  ,"DESCMOEDA05",,,,.F.)       // "Desc. Moeda 05"
    self:oSchema:addProperty("DESCCONTA"    	,STR0011    ,"string" ,STR0011  ,"DESCCONTA" ,,,,,.F.)       // "Descr. Conta"
    self:oSchema:addProperty("NomeEmpresa"      ,STR0014    ,"string" ,STR0014  ,"NomeEmpresa",,,,.F.)       // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial"       ,STR0015    ,"string" ,STR0015  ,"NomeFilial" ,,,,.F.)       // "Nome Filial"

    self:oSchema:addParameter("MV_PAR17", STR0010, "number", .F.)	//"Ordem ? 1 = Conta | 2 = C. Custo"

    self:setCustomURL("MV_PAR17", "/api/controladoria/smartview/v1/options/atf/balancetodepreciate/SVATFCombo002/1", 1)
        
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Será usado para alterar a Query.

@author Bruno Oliveira (B.O)
@since 25/10/2023
@version 12.1.2310
/*/
//-------------------------------------------------------------------

method getQuery(oFilter as Object) class BalanceToDepreciateSmartViewBusinessObject
    
    local cQuery        := ""                as character
    local nParamOrder   := 1                 as numeric
    local nx, i         := 0                 as numeric 
    local lRealProv	    := .F.               as logical
    local cWhereAux	    := ""                as character    
    local oPerg	        := FWSX1Util():New() as object
    local aPergunte                          as array
    local cChave                             as character
    local aTiposF	    := ATFXTpBem(1,.T.)  as array
    local aTiposG	    := ATFXTpBem(2,.T.)  as array
    local cTipos	    := ""                as character
    local cFilialDe     := ""                as character
    local cFilialAte    := ""                as character
    local lConsidFil    := .F.               as logical

    //Verifica se a nova pergunta realmente foi criada, para não dar error log no cliente
    oPerg:AddGroup("ATR050")
    oPerg:SearchGroup()
    aPergunte := oPerg:GetGroup("ATR050")

    lConsidFil := iif(MV_PAR14 == 1, .T., .F.) //Cons Filiais Abaixo ?
    cFilialDe  := MV_PAR15
    cFilialAte := iif(empty(MV_PAR16), FWxFilial("SN3"), MV_PAR16)

    if MV_PAR11 = 1
        cTipos := "'"
        For nX := 1 to Len(aTiposG)
            cTipos += aTiposG[nX]

            if nX != Len(aTiposG)
                cTipos += "','"
            endif
        Next nX
        cTipos += "'"
    elseif MV_PAR11 = 2
        cTipos := "'"
        For nX := 1 to Len(aTiposF)
            cTipos += aTiposF[nX]

            if nX != Len(aTiposF)
                cTipos += "','"
            endif
        Next nX
        cTipos += "'"
    endif
    
    //Verificação do campo para ativos de custo de provisão  
    lRealProv := SN3->(FieldPos("N3_ATFCPR")) > 0

    cQuery := "SELECT  "
    cQuery += self:getSQLFields()
    cQuery += " FROM "+RetSqlName("SN3")+" SN3 "
    cQuery += " JOIN "+RetSqlName("SN1")+" SN1 ON "
    cQuery += "     SN1.N1_FILIAL =  SN3.N3_FILIAL "
    cQuery += "     AND SN1.N1_CBASE = SN3.N3_CBASE "
    cQuery += "     AND SN1.N1_ITEM = SN3.N3_ITEM "
    cQuery += "     AND SN1.D_E_L_E_T_= ? "
    cQuery += " LEFT JOIN "+RetSqlName("CT1")+" CT1 ON "
    cQuery += "     CT1.CT1_FILIAL =  ? "
    cQuery += "     AND CT1.CT1_CONTA = SN3.N3_CCONTAB "
    cQuery += "     AND CT1.D_E_L_E_T_= ? "
    cQuery += " LEFT JOIN "+RetSqlName("CTT")+" CTT ON "
    cQuery += "     CTT.CTT_FILIAL =  SN3.N3_FILIAL "
    cQuery += "     AND CTT.CTT_CUSTO = SN3.N3_CCUSTO "
    cQuery += "     AND CTT.D_E_L_E_T_= ? "
    cQuery += " WHERE "

    if lConsidFil
        cQuery += " SN3.N3_FILIAL BETWEEN ? AND ? "
    else
        cQuery += " SN3.N3_FILIAL = ? "
    endif

    cQuery += " AND SN3.N3_CCONTAB <> ? "
    cQuery += " AND SN3.N3_CBASE BETWEEN ? AND ? "
    cQuery += " AND SN3.N3_BAIXA = ? "

    //Tratativa realizada para os bens que possuem taxa de depreciação igual a zero
    if Len (aPergunte[2]) > 11
        if MV_PAR12 == 2 // Não considera bens com taxa de depreciação = 0 (zero).
            cQuery += 	" AND SN3.N3_TXDEPR1 <> ?  "
        endif
    else
        cQuery +=	" AND SN3.N3_TXDEPR1 <> ?  "
    endif

    if MV_PAR17 <> 2
        cChave := "N3_FILIAL,N3_CCONTAB,N3_CBASE,N3_ITEM,N3_TIPO,N3_BAIXA"
    elseif MV_PAR17 == 2
        cChave := "N3_FILIAL,N3_CCUSTO,N3_CBASE,N3_ITEM,N3_TIPO,N3_BAIXA"
    endif

    if MV_PAR08 == 2 // Somente bens com saldo a depreciar
        For i := 3 To 7
            if &("mv_par0" + ALLTRIM(STR(i))) == 1	//Se a moeda foi escolhida pelo usuario
                if !Empty( cWhereAux )
                    cWhereAux += " OR "
                endif
                cWhereAux += " ( SN3.N3_VRDACM"+ALLTRIM(STR(i-2))+" < (SN3.N3_VORIG"+ALLTRIM(STR(i-2))+" + SN3.N3_AMPLIA"+ALLTRIM(STR(i-2))+") )"
            endif
        Next i

        if !Empty( cWhereAux )
            cQuery += " AND ( " + cWhereAux + " ) "
        endif
    endif

    if lRealProv .AND. MV_PAR10 == 2
        cQuery += " AND (SN3.N3_ATFCPR = ? OR SN3.N3_ATFCPR = ?)  "
    endif

    cQuery 	+=	" AND (SN3.N3_CDEPREC <> ? "
    cQuery 	+=	" OR  SN3.N3_CDESP <> ? "
    cQuery 	+=  " OR  SN3.N3_CCDEPR <> ? ) "

    if MV_PAR11 == 1 .OR. MV_PAR11 == 2
        cQuery 	+= " AND  SN3.N3_TIPO NOT IN ( ? ) " //cTipos
    endif

    cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    
    cQuery += " AND SN3.D_E_L_E_T_= ? "
    cQuery += " ORDER BY "+cChave

    cQuery := ChangeQuery(cQuery)
    
    self:oStatement := FWExecStatement():New(cQuery)

    self:oStatement:SetString(nParamOrder++, ' ')
    self:oStatement:SetString(nParamOrder++, FWxFilial("CT1"))
    self:oStatement:SetString(nParamOrder++, ' ')   
    self:oStatement:SetString(nParamOrder++, ' ')

    if lConsidFil
        self:oStatement:SetString(nParamOrder++, cFilialDe)
        self:oStatement:SetString(nParamOrder++, cFilialAte)
    else
        self:oStatement:SetString(nParamOrder++, FWxFilial("SN3"))
    endif

    self:oStatement:SetString(nParamOrder++, ' ')
    self:oStatement:SetString(nParamOrder++, MV_PAR01)
    self:oStatement:SetString(nParamOrder++, MV_PAR02)
    self:oStatement:SetString(nParamOrder++, '0')

    if Len (aPergunte[2]) > 11
        if MV_PAR12 == 2 // Não considera bens com taxa de depreciação = 0 (zero)
            self:oStatement:Setnumeric(nParamOrder++, 0)
        endif
    else
        self:oStatement:Setnumeric(nParamOrder++, 0)
    endif

    if lRealProv .AND. MV_PAR10 == 2
        self:oStatement:SetString(nParamOrder++, '2') 
        self:oStatement:SetString(nParamOrder++, ' ') 
    endif 

    self:oStatement:SetString(nParamOrder++, ' ') 
    self:oStatement:SetString(nParamOrder++, ' ') 
    self:oStatement:SetString(nParamOrder++, ' ') 

    if MV_PAR11 == 1 .OR. MV_PAR11 == 2
        self:oStatement:SetUnsafe(nParamOrder++, cTipos)
    endif
    
    self:oStatement:SetString(nParamOrder++, ' ')
    
    self:oStatement:GetFixQuery()

return

//-------------------------------------------------------------------
/*/{Protheus.doc} SVATFCombo002
    Função para retornar combobox de parâmetro
   
    @author Renan Gremes
    @since 06/02/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

Function SVATFCombo002(cOpcao)
    local aRet := {} as array

    if cOpcao == "1"
        aRet := {STR0012, STR0013} //"Conta", "C Custo"
    endif

return aRet
