#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.valuedposition12months.bra.ch"

namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1, SN3", name="Posição Valorizada 12 meses", country="ALL")

//-------------------------------------------------------------------------------
/* 
@author Beatriz Dias Ferreira
@since 16/06/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class ValuedPosition12MonthsSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()            as object
    public method getDescription() as character
    public method getAreas()       as array
    public method getData()        as object
    public method getSchema()      as object

    //usados na internacionalização
    public method preSchema()      as object 
    public method preData()        as object
    public method processData()    as json

    protected method AFR073Per()   as array

    protected data jItens  as json
    protected data oIntegratedProvider as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
Method new() as object Class ValuedPosition12MonthsSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Posicao 12 Meses"
    self:setPergunte("AFR073") //Indica o pergunte que será utilizado no relatório
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Controladoria
@since  DEZ/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getDescription() as character class ValuedPosition12MonthsSmartViewBusinessObject
return STR0002 // "Objeto contendo informações sobre a Posição Valorizada em 12 Meses"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Controladoria
@since  DEZ/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getAreas() as array class ValuedPosition12MonthsSmartViewBusinessObject
Return { STR0003 } //"Ativo Fixo"

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@author Controladoria
@since  DEZ/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class ValuedPosition12MonthsSmartViewBusinessObject
    
    Local cAliasQry         as character
    Local dAquIni           as date 
    Local dAquFim           as date
    Local aPeriodo          as array
    Local cBemIni           as character 
    Local cBemFim           as character
    Local cItemIni          as character
    Local cItemFim          as character
    Local cContaIni         as character
    Local cContaFim         as character
    Local cCCIni            as character
    Local cCCFim            as character
    Local cItCtbIni         as character
    Local cItCtbFim         as character
    Local cClvlIni          as character
    Local cClVlFim          as character
    Local cGrupoIni         as character
    Local cGrupoFim         as character
    Local cMoeda  := "01"   as character
    Local aSelFil           as array
    Local lTodasFil         as logical
    Local aTipo             as array
    Local nEntidade         as numeric
    Local nTipoTotal        as integer
    Local cTipoSLD := "1"   as character
    Local cDescrFil         as character
    Local aSelClass         as array
    Local lRealProv         as logical
    Local cDescTipo	:= ""   as character
    Local cDescSld 	:= ""   as character
    Local cTipDepr 	:= ""   as character
    Local cDescMet	:= ""   as character
    Local cTipoFiscal	    as character
    Local cTipoGerenc       as character
    Local cTipoIncent	    as character
    Local jParams := Nil    as json 
    
    cAliasQry := GetNextAlias()

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "AFR073" )
    jParams := oFilter:getParameters()

    ::oIntegratedProvider := ControlIntegratedProvider():New()

    //Seleção de filiais    
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
    ::oIntegratedProvider:loadFilterBranches({"SN1"})

    If Valtype(::oIntegratedProvider:oFilterBranches["SN1"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["SN1"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["SN1"]
    EndIf

    If !Empty(jParams:SV_CLASSPATR)
        If Valtype(jParams:SV_CLASSPATR) == "A"
            If Valtype(jParams:SV_CLASSPATR[1]) == "A"
                aSelClass := jParams:SV_CLASSPATR[1]
            Else
                aSelClass := jParams:SV_CLASSPATR
            EndIf
        EndIf
    EndIf

    dDataIni  := MV_PAR02 // Data Inicial ?                
    dDatafim  := MV_PAR03 // Data Final ?                  
    cGrupoIni := MV_PAR04 // Do Grupo ?                    
    cGrupoFim := MV_PAR05 // Até Grupo ?                   
    cBemIni   := MV_PAR06 // Código Inicial ?              
    cItemIni  := MV_PAR07 // Item Inicial ?                
    cBemFim   := MV_PAR08 // Código Final ?                
    cItemFim  := MV_PAR09 // Item Final ?                  
    dAquIni   := MV_PAR10 // Da Data de aquisição ?        
    dAquFim   := MV_PAR11 // Até Data de aquisição ?       
    cContaIni := MV_PAR12 // Da Conta ?                    
    cContaFim := MV_PAR13 // Até Conta ?                   
    cCCIni    := MV_PAR14 // Do Centro Custo ?             
    cCCFim    := MV_PAR15 // Ate Centro Custo ?            
    cItCtbIni := MV_PAR16 // Do Item Contábil ?            
    cItCtbFim := MV_PAR17 // Ate Item Contábil ?           
    cClvlIni  := MV_PAR18 // Da Classe de Valor ?          
    cClVlFim  := MV_PAR19 // Ate Classe de Valor ?      
    nTipoTotal:= MV_PAR21 // Exibe Informações ?           
    cMoeda    := iif(Empty(MV_PAR22), cMoeda, StrZero(MV_PAR22,2)) // Moeda ?         StrZero(MV_PAR22,2)              
    nEntidade := iif(Empty(MV_PAR23), 1, MV_PAR23)                 // Entidade ?                    
    cTipoSLD  := iif(Empty(MV_PAR24), cTipoSLD, MV_PAR24)          // Tipo de Saldo ?
    lRealProv := iif(MV_PAR26 == 2, .T., .F.)                      // Exibir Ativos Real. Provis ?  

    If nTipoTotal == 1 //Fiscal
	    aTipo := ATFXTpBem(1,.T.)
    ElseIf nTipoTotal == 2 //Gerencial
    	aTipo := ATFXTpBem(2,.T.)
    ElseIf nTipoTotal == 3 //Incentivada
    	aTipo := ATFXTpBem(3,.T.)
    EndIf
    
    aPeriodo := self:AFR073Per(MV_PAR02,MV_PAR03)
	
    cTipoFiscal	 := ATFXTpBem(1)
    cTipoGerenc  := ATFXTpBem(2)
    cTipoIncent	 := ATFXTpBem(3)    

    //função nativa do sistema, para carregameto das informações do relatório
    ATFGERCOMP(,,,.F.,cAliasQry,dAquIni,dAquFim,aPeriodo,cBemIni,cBemFim,cItemIni,cItemFim,cContaIni,cContaFim,;
              cCCIni,cCCFim,cItCtbIni,cItCtbFim,cClvlIni,cClVlFim,cGrupoIni,cGrupoFim,cMoeda,aSelFil,lTodasFil,Nil,Nil,aTipo,Nil,Nil,nEntidade,cTipoSLD,aSelClass,lRealProv)

    dbSelectArea(cAliasQry)
    (cAliasQry)->(dbGoTop())

    Do While (cAliasQry)->(!EOF())

        //Novo objeto
	    ::jItens := JsonObject():new()
            
        cCodBase    := (cAliasQry)->CBASE		
        cTpSaldo    := (cAliasQry)->TPSALDO
		cDescTipo   := Alltrim(FWGetSX5("G1", (cAliasQry)->TIPO)[1][4])    
	    cDescSld    := Alltrim(FWGetSX5("SL", iif(Empty(cTpSaldo), "1", cTpSaldo))[1][4])    
        cTipDepr    := iif(Empty((cAliasQry)->TPDEPR), "1", (cAliasQry)->TPDEPR)
	    cDescMet    := AllTrim(GetAdvFVal("SN0", "N0_DESC01", FwxFilial("SN0") +"04"+ cTipDepr ,1))
        cDescrFil   := AllTrim(GetAdvFval("SM0", "M0_FILIAL", cEmpAnt + (cAliasQry)->FILIAL)) 

        ::jItens["FILIAL"	] := (cAliasQry)->FILIAL	 
        ::jItens["CBASE" 	] := (cAliasQry)->CBASE 	 
        ::jItens["ITEM"  	] := (cAliasQry)->ITEM  	 
        ::jItens["MOEDA" 	] := (cAliasQry)->MOEDA 	 
        ::jItens["CLASSIF"	] := (cAliasQry)->CLASSIF	 
        ::jItens["TIPO"	 	] := (cAliasQry)->TIPO	 	 
        ::jItens["DESC_SINT"] := (cAliasQry)->DESC_SINT 
        ::jItens["AQUISIC" 	] := totvs.framework.treports.date.dateToTimeStamp((cAliasQry)->AQUISIC)
        ::jItens["DTBAIXA" 	] := totvs.framework.treports.date.dateToTimeStamp((cAliasQry)->DTBAIXA) 
        ::jItens["DTINIPER" ] := totvs.framework.treports.date.dateToTimeStamp((cAliasQry)->DTINIPER)
        ::jItens["DTFIMPER" ] := totvs.framework.treports.date.dateToTimeStamp((cAliasQry)->DTFIMPER) 
        ::jItens["PERIODO" 	] := (cAliasQry)->PERIODO 	 
        ::jItens["CHAPA"	] := (cAliasQry)->CHAPA	    
        ::jItens["GRUPO"	] := (cAliasQry)->GRUPO	    
        ::jItens["CONTA"	] := (cAliasQry)->CONTA	    
        ::jItens["CCUSTO"	] := (cAliasQry)->CCUSTO	 
        ::jItens["SUBCTA"	] := (cAliasQry)->SUBCTA	 
        ::jItens["CLVL"	 	] := (cAliasQry)->CLVL	 	 
        ::jItens["SEQ"	   	] := (cAliasQry)->SEQ	   	 
        ::jItens["SEQREAV"	] := (cAliasQry)->SEQREAV	 
        ::jItens["FLAGBAIXA"] := (cAliasQry)->FLAGBAIXA 
        ::jItens["TPSALDO" 	] := (cAliasQry)->TPSALDO 	 
        ::jItens["TPDEPR"  	] := (cAliasQry)->TPDEPR  	 
        ::jItens["QUANTD"	] := (cAliasQry)->QUANTD	 
        ::jItens["ORIGINAL"	] := (cAliasQry)->ORIGINAL	 
        ::jItens["AMPLIACAO"] := (cAliasQry)->AMPLIACAO 
        ::jItens["ATUALIZ"	] := (cAliasQry)->ATUALIZ	 
        ::jItens["DEPRECACM"] := (cAliasQry)->DEPRECACM 
        ::jItens["DEPRECMES"] := (cAliasQry)->DEPRECMES
        ::jItens["RESIDUAL"	] := (cAliasQry)->RESIDUAL	 
        ::jItens["CORRECACM"] := (cAliasQry)->CORRECACM 
        ::jItens["CORDEPACM"] := (cAliasQry)->CORDEPACM
        ::jItens["VLBAIXAS" ] := (cAliasQry)->VLBAIXAS 
        ::jItens["CDESCTIPO"] := cDescTipo
        ::jItens["CDESCSLD" ] := cDescSld 
        ::jItens["CDESCMET" ] := cDescMet 
        ::jItens["CDESCFIL" ] := cDescrFil	
			
        self:processData()
        self:oData:appendData(::jItens)    
        (cAliasQry)->(dbSkip())

    Enddo

    (cAliasQry)->(dbCloseArea())

    FreeObj(oFilter:getParameters())
    FreeObj(jParams)
    FreeObj(::oIntegratedProvider)
    FreeObj(::jItens)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Metodo para montar o schema do objeto

@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getSchema() as object class ValuedPosition12MonthsSmartViewBusinessObject

    self:oSchema:addProperty("FILIAL"	 ,STR0004 ,"string" ,STR0004 ,"FILIAL"		,,,,.F.) // Filial
    self:oSchema:addProperty("CBASE" 	 ,STR0005 ,"string" ,STR0005 ,"CBASE" 		,,,,.F.) // Codigo Bem
    self:oSchema:addProperty("ITEM"  	 ,STR0006 ,"string" ,STR0006 ,"ITEM"  		,,,,.F.) // Item
    self:oSchema:addProperty("MOEDA" 	 ,STR0007 ,"string" ,STR0007 ,"MOEDA" 		,,,,.F.) // Moeda
    self:oSchema:addProperty("CLASSIF"	 ,STR0008 ,"string" ,STR0008 ,"CLASSIF"		,,,,.F.) // Classificação
    self:oSchema:addProperty("TIPO"	 	 ,STR0009 ,"string" ,STR0009 ,"TIPO"	 	,,,,.F.) // Tipo do Bem
    self:oSchema:addProperty("DESC_SINT" ,STR0010 ,"string" ,STR0010 ,"DESC_SINT"	,,,,.F.) // Desc. Sintetica
    self:oSchema:addProperty("AQUISIC" 	 ,STR0011 ,"date"   ,STR0011 ,"AQUISIC" 	,,,,.F.) // Data Aquisição
    self:oSchema:addProperty("DTBAIXA" 	 ,STR0012 ,"date"   ,STR0012 ,"DTBAIXA" 	,,,,.F.) // Data Baixa
    self:oSchema:addProperty("DTINIPER"  ,STR0013 ,"date"   ,STR0013 ,"DTINIPER" 	,,,,.F.) // Data Inicio Periodo
    self:oSchema:addProperty("DTFIMPER"  ,STR0014 ,"date"   ,STR0014 ,"DTFIMPER" 	,,,,.F.) // Data Fim Periodo
    self:oSchema:addProperty("PERIODO" 	 ,STR0015 ,"string" ,STR0015 ,"PERIODO" 	,,,,.F.) // Periodo
    self:oSchema:addProperty("CHAPA"	 ,STR0016 ,"string" ,STR0016 ,"CHAPA"	 	,,,,.F.) // Chapa
    self:oSchema:addProperty("GRUPO"	 ,STR0017 ,"string" ,STR0017 ,"GRUPO"	 	,,,,.F.) // Grupo
    self:oSchema:addProperty("CONTA"	 ,STR0018 ,"string" ,STR0018 ,"CONTA"	 	,,,,.F.) // Conta Contabil Bem
    self:oSchema:addProperty("CCUSTO"	 ,STR0019 ,"string" ,STR0019 ,"CCUSTO"		,,,,.F.) // Centro de Custo Bem
    self:oSchema:addProperty("SUBCTA"	 ,STR0020 ,"string" ,STR0020 ,"SUBCTA"		,,,,.F.) // Sub Conta Bem
    self:oSchema:addProperty("CLVL"	 	 ,STR0021 ,"string" ,STR0021 ,"CLVL"	 	,,,,.F.) // Classe Valor Bem
    self:oSchema:addProperty("SEQ"	   	 ,STR0022 ,"string" ,STR0022 ,"SEQ"	   		,,,,.F.) // Sequencia
    self:oSchema:addProperty("SEQREAV"	 ,STR0023 ,"string" ,STR0023 ,"SEQREAV"		,,,,.F.) // Sequencia de Reavaliação
    self:oSchema:addProperty("FLAGBAIXA" ,STR0024 ,"string" ,STR0024 ,"FLAGBAIXA"	,,,,.F.) // Flag de baixa
    self:oSchema:addProperty("TPSALDO" 	 ,STR0025 ,"string" ,STR0025 ,"TPSALDO" 	,,,,.F.) // Tipo de Saldo
    self:oSchema:addProperty("TPDEPR"  	 ,STR0026 ,"string" ,STR0026 ,"TPDEPR"  	,,,,.F.) // Tipo de Depreciação
    self:oSchema:addProperty("QUANTD"	 ,STR0027 ,"number" ,STR0027 ,"QUANTD"		,,,,.F.) // Quantidade
    self:oSchema:addProperty("ORIGINAL"	 ,STR0028 ,"number" ,STR0028 ,"ORIGINAL"	,,,,.F.) // Original
    self:oSchema:addProperty("AMPLIACAO" ,STR0029 ,"number" ,STR0029 ,"AMPLIACAO"	,,,,.F.) // Ampliação
    self:oSchema:addProperty("ATUALIZ"	 ,STR0030 ,"number" ,STR0030 ,"ATUALIZ"		,,,,.F.) // Atualização
    self:oSchema:addProperty("DEPRECACM" ,STR0031 ,"number" ,STR0031 ,"DEPRECACM"	,,,,.F.) // Depreciação Acumulada
    self:oSchema:addProperty("DEPRECMES" ,STR0032 ,"number" ,STR0032 ,"DEPRECMES"	,,,,.F.) // Depreciação Acum. Mes
    self:oSchema:addProperty("RESIDUAL"	 ,STR0033 ,"number" ,STR0033 ,"RESIDUAL"	,,,,.F.) // Valor Residual
    self:oSchema:addProperty("CORRECACM" ,STR0034 ,"number" ,STR0034 ,"CORRECACM"	,,,,.F.) // Vlr Corr. Acumulada
    self:oSchema:addProperty("CORDEPACM" ,STR0035 ,"number" ,STR0035 ,"CORDEPACM"	,,,,.F.) // Vlr Cor. Dep. Acumulada
    self:oSchema:addProperty("VLBAIXAS"  ,STR0042 ,"number" ,STR0042 ,"VLBAIXAS"	,,,,.F.) // Valor Baixas

    //Descrições
    self:oSchema:addProperty("CDESCTIPO" ,STR0036 ,"string" ,STR0036 ,"CDESCTIPO"	,,,,.F.) // Descrição Tipo
    self:oSchema:addProperty("CDESCSLD"  ,STR0037 ,"string" ,STR0037 ,"CDESCSLD"	,,,,.F.) // Descrição Saldo
    self:oSchema:addProperty("CDESCMET"  ,STR0038 ,"string" ,STR0038 ,"CDESCMET"	,,,,.F.) // Descrição Tipo Depreciação
    self:oSchema:addProperty("CDESCFIL"  ,STR0039 ,"string" ,STR0039 ,"CDESCFIL"	,,,,.F.) // Descrição Filial

    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0040, "string", .T.) //Filiais
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)   

    //Seleciona Classificação Patrimonial
    self:oSchema:addParameter("SV_CLASSPATR", STR0041, "string", .T.) //Classif. Patrimonial
    self:setCustomURL("SV_CLASSPATR", "/api/controladoria/smartview/v1/options/getClassification", 2)    
       
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author wilton.santos
    @since 05/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class ValuedPosition12MonthsSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author wilton.santos
    @since 05/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class ValuedPosition12MonthsSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 05/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class ValuedPosition12MonthsSmartViewBusinessObject
return ::JItens

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo usado no relatório padrão para trazer o período 
    conforme preenhechimento do parâmetro.
    @author wilton.santos
    @since 05/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method AFR073Per(dDataIni as date, dDataFim as date) as array class ValuedPosition12MonthsSmartViewBusinessObject
    Local aPeriodo := {} as array
    Local dDataAux := FirstDay(dDataIni) as date

    While dDataAux <= LastDay(dDataFim)
        aAdd(aPeriodo,{FirstDay(dDataAux),LastDay(dDataAux)})
        dDataAux := LastDay(dDataAux)+1
    EndDo
Return aPeriodo
