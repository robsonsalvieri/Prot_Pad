#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.assetadvances.bra.ch"

#DEFINE SX1GRUPO "AFR090"

//Cria um novo namespace para sua classe
namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace control.sv.functions.utils
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider

//Ativar ou desativar a utilização da classe
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1,SN3,SN5", name="Adiantamentos do Ativo Fixo", country="ALL",customTables="SN3,SN1")

//-------------------------------------------------------------------------------
/*{Protheus.doc} AssetAdvancesSmartViewBusinessObject
Classe que contêm os dados dos Adiantamentos do Ativo Fixo
Fonte/Perg      ATFR090	/ AFR090
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
Class AssetAdvancesSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()                 as object
    public method getDescription()      as character
    public method getAreas()            as array
    public method getData()             as object
    public method getSchema()           as object

    //usados na internacionalização
    public method preschema()           as object 
    public method preData()             as object
    public method processData()         as json 
    
    protected method getAdiantamentos() as character
    protected method setAdiantamentos() as object
    
    protected data jDados       as json
    protected data cAlias       as character
    protected data oQuery       as object
    protected data aAllFields   as array

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method new() as object Class AssetAdvancesSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)    //"Adiantamentos"
    self:setPergunte( SX1GRUPO )    //Indica o pergunte que será utilizado no relatório
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
@return object: string
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getDescription() as character class AssetAdvancesSmartViewBusinessObject
return STR0002 //"Objeto contendo informações do Adiantamentos do Ativo Fixo"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
@return object: string
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getAreas() as array class AssetAdvancesSmartViewBusinessObject
return {STR0003} //"Ativo Fixo"

//-------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Metodos que determina os campos que estarão disponiveis Smartview
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method getSchema() as object class AssetAdvancesSmartViewBusinessObject

    Local aFieldsSN1        as array
    Local aFieldsSN3        as array

    aFieldsSN1 := {"N1_FILIAL" ,"N1_AQUISIC" ,"N1_CBASE"  ,"N1_ITEM"   ,"N1_PROJETO"}

    aFieldsSN3 := {"N3_CBASE"  ,"N3_ITEM"   ,"N3_CCONTAB","N3_CCUSTO" ,"N3_SUBCTA" ,"N3_CLVL"   ,;
                   "N3_VORIG1" ,"N3_VORIG2" ,"N3_VORIG3" ,"N3_VORIG4" ,"N3_VORIG5" ,"N3_AMPLIA1",;
                   "N3_AMPLIA2","N3_AMPLIA3","N3_AMPLIA4","N3_AMPLIA5","N3_VRCACM1","N3_VRCACM2","N3_VRCACM3",;
                   "N3_VRCACM4","N3_VRCACM5","N3_CDEPREC","N3_CCDEPR" ,"N3_CCORREC"}

    self:oSchema:aliasToSchema("SN1",aFieldsSN1)
    self:oSchema:aliasToSchema("SN3",aFieldsSN3)

    self:aAllFields := self:getStructFields()

return self:oSchema

//-------------------------------------------------------------------------------
/*{Protheus.doc} getData
Tratamento dos dados que serão enviados aos campos do getSchema
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class AssetAdvancesSmartViewBusinessObject

    Local cQuery := "" as character

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), SX1GRUPO )

    //Carrega o bloco de baixas
    cQuery       := self:getAdiantamentos(oFilter)

    self:cAlias  := MPSysOpenQuery(cQuery)
    self:setAdiantamentos(oFilter)

    (self:cAlias)->( DBCloseArea() )

	//Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())

return self:oData

//-------------------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodos usado na Internacionalização
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method preSchema() as object class AssetAdvancesSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Método de instância da classe
@return object: self:oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method preData() as object class AssetAdvancesSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método na Chamado no momento do processamento da query
@return object: self:oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method processData() class AssetAdvancesSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------------------
/*{Protheus.doc} getAdiantamentos
gera a query de filtragem dos dados 
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method getAdiantamentos(oFilter) as character class AssetAdvancesSmartViewBusinessObject

    Local cQuery        := ""     as character
    Local cWhere        := ""     as character
    local nParamOrder   := 1      as numeric
  
    
    cQuery := " SELECT " + self:getSQLFields()
	cQuery += " FROM "+ RetSqlName("SN3") +" SN3 "
	cQuery += " INNER JOIN "+ RetSqlName("SN1") +" SN1 ON SN1.D_E_L_E_T_ = ? "
	cQuery += "   AND SN1.N1_FILIAL  = SN3.N3_FILIAL "
	cQuery += "   AND SN1.N1_CBASE   = SN3.N3_CBASE "
	cQuery += "   AND SN1.N1_ITEM    = SN3.N3_ITEM "
    cQuery += "   AND SN3.D_E_L_E_T_ = ? "
	cQuery += " WHERE SN3.D_E_L_E_T_  = ? "
    cQuery += "   AND SN3.N3_FILIAL   = ? " 
	cQuery += "   AND SN3.N3_TIPO     = ? "
	cQuery += "   AND SN1.N1_AQUISIC >= ? "         // 01,"Da Data ?                    MV_PAR01        D      
	cQuery += "   AND SN1.N1_AQUISIC <= ? "         // 02,"Ate a Data ?                 MV_PAR02        D      
	cQuery += "   AND SN3.N3_CCONTAB >= ? "         // 03,"Da Conta ?                   MV_PAR03        C   CT1
	cQuery += "   AND SN3.N3_CCONTAB <= ? "         // 04,"Ate a Conta ?                MV_PAR04        C   CT1
	cQuery += "   AND SN3.N3_CCUSTO  >= ? "         // 05,"Do Centro Custo ?            MV_PAR05        C   CTT
	cQuery += "   AND SN3.N3_CCUSTO  <= ? "         // 06,"Ate o  Centro Custo ?        MV_PAR06        C   CTT
	cQuery += "   AND SN3.N3_CBASE   >= ? "         // 07,"Do Codigo ?                  MV_PAR07        C   SN1
	cQuery += "   AND SN3.N3_CBASE   <= ? "         // 08,"Ate o Codigo ?               MV_PAR08        C   SN1
	cQuery += "   AND SN1.N1_PROJETO >= ? "         // 09,"Do Projeto ?                 MV_PAR09        C
	cQuery += "   AND SN1.N1_PROJETO <= ? "         // 10,"Ate o Projeto ?              MV_PAR10        C
	cQuery += "   AND SN3.N3_SUBCTA  >= ? "         // 12,"Do Item Contábil ?           MV_PAR12        C   CTD
	cQuery += "   AND SN3.N3_SUBCTA  <= ? "         // 13,"Até o Item Contábil ?        MV_PAR13        C   CTD
	cQuery += "   AND SN3.N3_CLVL    >= ? "         // 14,"Da Classe de Valor ?         MV_PAR14        C   CTH
	cQuery += "   AND SN3.N3_CLVL    <= ? "         // 15,"Até a Classe de Valor ?      MV_PAR15        C   CTH

    // 16,"Considera Itens Baixados ?   MV_PAR16        N   Sim,Não
    If MV_PAR16 == 2
        cWhere := "   AND SN3.N3_BAIXA    = ? "
    Endif	
    // 18,"Exibir Ativos Real. Provis ? MV_PAR18        N   Sim,Não
    If MV_PAR18 == 2
        cWhere += "   AND SN3.N3_ATFCPR  IN (? , ?) "
    EndIf

	cQuery += cWhere

    //Os filtros serão setados na interface do novo TReports    
    cQuery += Iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

    cQuery += " ORDER BY N1_FILIAL,N1_PROJETO,N1_CBASE,N1_ITEM "

    //Objeto executa o comando SQL inúmeras vezes.
    self:oQuery:= FWPreparedStatement():New(ChangeQuery(cQuery))
    
    self:oQuery:setString(nParamOrder++  , Space(1) )      
    self:oQuery:setString(nParamOrder++  , Space(1) )      
    self:oQuery:setString(nParamOrder++  , Space(1) )       
    self:oQuery:setString(nParamOrder++  , FwXFilial("SN3") )
    self:oQuery:setString(nParamOrder++  , "03" )          

    self:oQuery:setDate(nParamOrder++    , MV_PAR01 )       // 01,"Da Data ?                    MV_PAR01        D      
    self:oQuery:setDate(nParamOrder++    , MV_PAR02 )       // 02,"Ate a Data ?                 MV_PAR02        D      
    self:oQuery:setString(nParamOrder++  , MV_PAR03 )       // 03,"Da Conta ?                   MV_PAR03        C   CT1
    self:oQuery:setString(nParamOrder++  , MV_PAR04 )       // 04,"Ate a Conta ?                MV_PAR04        C   CT1
    self:oQuery:setString(nParamOrder++  , MV_PAR05 )       // 05,"Do Centro Custo ?            MV_PAR05        C   CTT
    self:oQuery:setString(nParamOrder++  , MV_PAR06 )       // 06,"Ate o  Centro Custo ?        MV_PAR06        C   CTT
    self:oQuery:setString(nParamOrder++  , MV_PAR07 )       // 07,"Do Codigo ?                  MV_PAR07        C   SN1
    self:oQuery:setString(nParamOrder++  , MV_PAR08 )       // 08,"Ate o Codigo ?               MV_PAR08        C   SN1
    self:oQuery:setString(nParamOrder++  , MV_PAR09 )       // 09,"Do Projeto ?                 MV_PAR09        C
    self:oQuery:setString(nParamOrder++  , MV_PAR10 )       // 10,"Ate o Projeto ?              MV_PAR10        C
    self:oQuery:setString(nParamOrder++  , MV_PAR12 )       // 12,"Do Item Contábil ?           MV_PAR12        C   CTD
    self:oQuery:setString(nParamOrder++  , MV_PAR13 )       // 13,"Até o Item Contábil ?        MV_PAR13        C   CTD
    self:oQuery:setString(nParamOrder++  , MV_PAR14 )       // 14,"Da Classe de Valor ?         MV_PAR14        C   CTH
    self:oQuery:setString(nParamOrder++  , MV_PAR15 )       // 15,"Até a Classe de Valor ?      MV_PAR15        C   CTH


    If MV_PAR16 == 2
        self:oQuery:setString(nParamOrder++  , "0" )  
    Endif	

    If MV_PAR18 == 2
        self:oQuery:setString(nParamOrder++  , "2" )  
        self:oQuery:setString(nParamOrder++  , Space(1) )  
        // self:oQuery:SetUnsafe(3  , "('2',' ')" )  
    EndIf

    //Retorna query com parâmetros tratados e substituídos.
    cQuery := self:oQuery:GetFixQuery()

return cQuery

//-------------------------------------------------------------------------------
/*{Protheus.doc} setAdiantamentos
seta os dados filtrados no objeto oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method setAdiantamentos(oFilter as object) as object class AssetAdvancesSmartViewBusinessObject

    Local nX            := 0   as numeric
    local cId                  as character
    local cRealName            as character

    //Posiciona no registro inicial
    (self:cAlias)->(DBGoTop())

    While !(self:cAlias)->(Eof())
        
        self:jDados := JsonObject():new()

        For nX := 1 to len (self:aAllFields)

            cId := self:aAllFields[nX]:getName()
            cRealName := self:aAllFields[nX]:getRealName()

            If self:aAllFields[nX]:getType() == "date"
                ::jDados[cId] := totvs.framework.treports.date.stringToTimeStamp((self:cAlias)->&(cRealName))
            Elseif self:aAllFields[nX]:getType() == "string"
                ::jDados[cId] := ALLTRIM((self:cAlias)->&(cRealName))
            Else
                ::jDados[cId] := (self:cAlias)->&(cRealName)
            EndIf

        Next nX

        self:processData()
        self:oData:appendData(self:jDados)

        (self:cAlias)->( dbSkip() )

    End

return self:oData
