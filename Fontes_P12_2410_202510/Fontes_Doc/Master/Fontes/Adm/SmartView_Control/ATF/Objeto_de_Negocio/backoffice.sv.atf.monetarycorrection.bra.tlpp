#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#Include "Protheus.ch"   
#include "backoffice.sv.atf.monetarycorrection.bra.ch"

namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN3, SN1", name="Correção Monetária", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} MonetaryCorrectionSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Cadastro Ativos para o SmartView
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class MonetaryCorrectionSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new() as object
    public method getDescription() as character
    public method getAreas() as array
    public method getData() as object
    public method getSchema() as object

    //usados na internacionalização
    public method preschema()      as object 
    public method preData()        as object
    public method processData()    as json 

    protected method getQuerySN3() as character
    protected method getTableTemp()  as object
    
    protected data aFields as array
    protected data aSelFil as array
    protected data jItens  as json
    Protected data oATFR1701
    protected data oIntegratedProvider as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method new() as object Class MonetaryCorrectionSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Correção Monetária"
    self:setPergunte("AFR170") //Indica o pergunte que será utilizado no relatório

    ::aSelFil := {}
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getDescription() as character class MonetaryCorrectionSmartViewBusinessObject
return STR0002 //"Objeto contendo informações do relatório Correção Monetária"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getAreas() as array class MonetaryCorrectionSmartViewBusinessObject
Return {STR0003} //"Ativo Fixo"


//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class MonetaryCorrectionSmartViewBusinessObject

return self:oSchema
//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class MonetaryCorrectionSmartViewBusinessObject
    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class MonetaryCorrectionSmartViewBusinessObject

return ::jItens

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 

 
@return object: self:oData
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class MonetaryCorrectionSmartViewBusinessObject

    Local jParams       := Nil as json    
    Local cMoeda        := SuperGetMV("MV_ATFMOED") as character // Localiza a moeda do ativo
    Local dUltProc 	    := SuperGetMV("MV_ULTDEPR") as date		// Localiza a data do ultimo calculo
    Local dDataProx 	:= LastDay( dUltProc ) + 1  as date
    
    Local cCompanyName  as character
    Local cBranchName   as character

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    CTBsetValueMVPAR(jParams, "AFR170")//Carrega lista de parametros para a memória

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

    //Seleção de filiais
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
    ::oIntegratedProvider:loadFilterBranches({"SN3"})

    If Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1]
    Else
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
    EndIf    

    cQuery := self:getQuerySN3()
    cAlias := MPSysOpenQuery(cQuery)

    SM2->( dbSeek( dDataProx, .T. ) )
    nUfir := &('SM2->M2_MOEDA'+cMoeda)

    self:getTableTemp()

    while (cAlias)->(!Eof())

        dbSelectArea("SN1")
        dbSetOrder(1)
        dbSeek(xFilial("SN1",(cAlias)->N3_FILIAL) + (cAlias)->N3_CBASE + (cAlias)->N3_ITEM)

        dbSelectArea("CT1")
        dbSetOrder(1)
        dbSeek( xFilial("CT1",(cAlias)->N3_FILIAL) + (cAlias)->N3_CCONTAB )
        
        dbSelectArea("cNomeArq")
        cNomeArq->(DBGOTOP(  ))
        cNomeArq->(dbSetOrder(1))
        If	!cNomeArq->(dbSeek((cAlias)->N3_FILIAL + (cAlias)->N3_CCONTAB ) )
            RecLock( "cNomeArq" , .T. )
            cNomeArq->FILIAL := (cAlias)->N3_FILIAL	
            cNomeArq->CONTA  := (cAlias)->N3_CCONTAB
            
            cNomeArq->DESCRIC := CT1->CT1_DESC01
            
            cNomeArq->(MSUNLOCK())
        else
            RecLock( "cNomeArq" , .F. )   
        Endif
         
        // Verificação da classificação de Ativo se sofre depreciação
	    lAtClDepr := AtClssVer(SN1->N1_PATRIM)

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ Saldo e':Valor original+Corre‡„o do Acumul-Deprec.Acumulada        ³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        If lAtClDepr .OR. SN1->N1_PATRIM $ " P"
            cNomeArq->SALDO  += ( (cAlias)->&("(N3_VORIG"+cMoeda+"+N3_AMPLIA"+cMoeda+")") )  // Valor Original em Ufir
            cNomeArq->SALD1  += ( (cAlias)->N3_VORIG1 + (cAlias)->N3_AMPLIA1 + (cAlias)->N3_VRCACM1 )  // Valor Original em Moeda corrente
            cNomeArq->AMPLIA1+= (cAlias)->N3_AMPLIA1
        Else
            cNomeArq->SALDO  -= ( (cAlias)->&("(N3_VORIG"+cMoeda+"+N3_AMPLIA"+cMoeda+")") )  // Valor Original em Ufir
            cNomeArq->SALD1  -= ( (cAlias)->N3_VORIG1 + (cAlias)->N3_VRCACM1 )  // Valor Original em Moeda corrente	
            cNomeArq->AMPLIA1-= (cAlias)->N3_AMPLIA1
        Endif
        
        If CT1->CT1_NORMAL == "1"
            cNomeArq->DEVED += ( (cAlias)->N3_VRCACM1 )  // VrcAcm1: Corre‡„o Acumulada Moeda
            Iif(mv_par03 = 2,cNomeArq->CREDO += ( (cAlias)->N3_VRCDA1  ),;
                            cNomeArq->CREDO += 0 )      // Vrcda1: Corr.Depr.Acum
        Else
            cNomeArq->CREDO += ( (cAlias)->N3_VRCACM1 )  // VrcAcm1: Corre‡„o Acumulada Moeda
            Iif(mv_par03 = 2,cNomeArq->DEVED += ( (cAlias)->N3_VRCDA1  ),;
                            cNomeArq->DEVED += 0)       // Vrcda1: Corr.Depr.Acum
        Endif

        cNomeArq->(MSUNLOCK()) 

        If mv_par03 == 1 // Separa conta de depreciacao? SIM/NAO

            If !Empty((cAlias)->N3_CCDEPR)   //Verifico se a conta de deprec acum	

                dbSelectArea("CT1")
                dbSetOrder(1)
                dbSeek( xFilial("CT1",(cAlias)->N3_FILIAL) + (cAlias)->N3_CCDEPR )
                
                dbSelectArea( "cNomeArq" )
                If	!cNomeArq->(dbSeek((cAlias)->N3_FILIAL +  (cAlias)->N3_CCDEPR ) )
                    RecLock( "cNomeArq" , .T. )
                    cNomeArq->FILIAL := (cAlias)->N3_FILIAL
                    cNomeArq->CONTA := (cAlias)->N3_CCDEPR
                    
                    cNomeArq->DESCRIC := CT1->CT1_DESC01
                   
                Else
                    RecLock( "cNomeArq" )
                Endif

                //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                //³ Saldo e':Valor original+Corre‡„o do Acumul-Deprec.Acumulada        ³
                //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                cNomeArq->SALDO += ( (cAlias)->&('N3_VRDACM'+cMoeda) )  // Valor Original em Ufir
                cNomeArq->SALD1 += ( (cAlias)->N3_VRDACM1 + (cAlias)->N3_VRCDA1 )  // Valor Original em Moeda corrente
                
                If CT1->CT1_NORMAL == "1"
                    cNomeArq->CREDO += ( (cAlias)->N3_VRCDA1  )  // Vrcda1: Corr.Depr.Acum
                Else
                    cNomeArq->DEVED += ( (cAlias)->N3_VRCDA1  )  // Vrcda1: Corr.Depr.Acum
                Endif
                
            Endif
        Endif  
        cNomeArq->(MSUNLOCK())

        (cAlias)->(DBSKIP())   
    EndDO

    (cAlias)->(DBCloseArea())

    dbSelectArea("cNomeArq")
    cNomeArq->(DBGOTOP( ))

    while !(cNomeArq->(Eof()))
        //Novo objeto
	        ::jItens := JsonObject():new()
            ::jItens[ "CONTA" ]         := cNomeArq->CONTA 
            ::jItens[ "DESCRICAO" ]     := cNomeArq->DESCRIC
            ::jItens[ "SALDO" ]         := cNomeArq->SALDO
            ::jItens[ "SALD1" ]         := cNomeArq->SALD1
            ::jItens[ "DEVED" ]         := cNomeArq->DEVED
            ::jItens[ "CREDO" ]         := cNomeArq->CREDO
            ::jItens[ "AMPLIA1" ]       := cNomeArq->AMPLIA1

            ::jItens[ "NomeEmpresa" ]   := cCompanyName
            ::jItens[ "NomeFilial" ]    := cBranchName
            
            self:processData() 
            self:oData:appendData(::jItens)  

        cNomeArq->(DBSKIP())
    Enddo
    cNomeArq->(dbCloseArea())

    //Deleta tabela temporaria do banco de dados
    If ::oATFR1701 <> Nil
        ::oATFR1701:Delete()
        ::oATFR1701 := Nil
    Endif
      
    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(jParams)
    FreeObj(::oIntegratedProvider)
     
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema

@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class MonetaryCorrectionSmartViewBusinessObject

    Local cMoeda                as character
    Local cDescMoedA            as character
    Local cDescMoed1 	        as character
    Local nMoeda         := 0   as numeric

    cMoeda := SuperGetMV("MV_ATFMOED") // Localiza a moeda do ativo
    nMoeda := Val(cMoeda)

    cDescMoedA:= Alltrim(Subs(SuperGetMV("MV_MOEDA"+Alltrim(cMoeda)),1,6))
    cDescMoed1  := Alltrim(Subs(SuperGetMV("MV_MOEDA1"),1,6))

    cDescMoedA:=Iif(Len(cDescMoedA)< 6,cDescMoedA+space(6 - Len(cDescMoedA)),cDescMoedA)
    cDescMoed1:=Iif(Len(cDescMoed1)< 6,cDescMoed1+space(6 - Len(cDescMoed1)),cDescMoed1)

    self:oSchema:addProperty("CONTA"    	,STR0004    ,"string" ,STR0004  ,"MOEDA01",,,,.F.)       // "Moeda 01"
    self:oSchema:addProperty("DESCRICAO"    ,STR0005    ,"string" ,STR0005  ,"MOEDA02",,,,.F.)       // "Moeda 02"
    self:oSchema:addProperty("SALDO"    	,STR0006+cDescMoedA+STR0007     ,"number" ,STR0006+cDescMoedA+STR0007   ,"MOEDA03",,,,.F.)       // "Moeda 03"
    self:oSchema:addProperty("SALD1"    	,STR0008+cDescMoed1+STR0009     ,"number" ,STR0008+cDescMoed1+STR0009   ,"MOEDA04",,,,.F.)       // "Moeda 04"
    self:oSchema:addProperty("DEVED"    	,STR0010    ,"number" ,STR0010  ,"MOEDA05",,,,.F.)       // "Moeda 05"
 
    self:oSchema:addProperty("CREDO"        ,STR0011    ,"number" ,STR0011  ,"DESCMOEDA01",,,,.F.)   // "Desc. Moeda 01"
    self:oSchema:addProperty("AMPLIA1"      ,STR0012    ,"number" ,STR0012  ,"DESCMOEDA02",,,,.F.)   // "Desc. Moeda 02"

    self:oSchema:addProperty("NomeEmpresa"  ,STR0013    ,"string" ,STR0013  ,"NomeEmpresa",,,,.F.)   // Nome Empresa
    self:oSchema:addProperty("NomeFilial"   ,STR0014    ,"string" ,STR0014  ,"NomeFilial" ,,,,.F.)   // Nome Filial   

    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0015, "string", .T.) //Filiais
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuerySN3
    @author Bruno Oliveira
    @since 14/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getQuerySN3() as Character class MonetaryCorrectionSmartViewBusinessObject
    local cQuery  := "" as character
    local oStatement as object
    local nParamOrder := 1 as numeric

    cQuery := " SELECT N3_FILIAL,N3_CBASE, N3_ITEM, N3_CCONTAB, N3_AMPLIA1, N3_VRCACM1, N3_CCDEPR, "
	cQuery += " N3_VORIG1, N3_VORIG2, N3_VORIG3, N3_VORIG4, N3_VORIG5, "
	cQuery += " N3_VRDACM1, N3_VRDACM2, N3_VRDACM3, N3_VRDACM4, N3_VRDACM5, "
	cQuery += " N3_AMPLIA1, N3_AMPLIA2, N3_AMPLIA3, N3_AMPLIA4, N3_AMPLIA5, "
	cQuery += " N3_VRCDA1 "
	cQuery += "	FROM "+RetSqlName("SN3")+" SN3 "
	cQuery += "	WHERE "

    if !empty(self:aSelFil)
        cQuery += "	N3_FILIAL IN ( ? ) "
    else
        cQuery += "	N3_FILIAL =  ?  "
    endif

    If	!Empty( mv_par01 ) // Data Conta
		cQuery += " AND N3_CCONTAB > ? "
		
		If mv_par03 == 1 // Separa conta de depreciacao? SIM/NAO
			cQuery += " AND N3_CCDEPR > ? "
		EndIf
	Endif

    If	!Empty( mv_par02 ) // Ate a Conta
		cQuery += " AND N3_CCONTAB < ? "
		
		If mv_par03 == 1 // Separa conta de depreciacao? SIM/NAO
			cQuery += " AND N3_CCDEPR < ? "
		EndIf
	Endif

	cQuery += " AND SN3.N3_BAIXA = ? "
	cQuery += " AND SN3.D_E_L_E_T_= ? "				
    cQuery += " ORDER BY "  + SqlOrder( SN3->(IndexKey(1)) )
    
    cQuery := ChangeQuery(cQuery)
    
    oStatement := FWExecStatement():New(cQuery)

    if !empty(self:aSelFil)
        oStatement:SetIn(nParamOrder++, ::aSelFil)
    else
        oStatement:SetString(nParamOrder++, FWXFilial("SN3"))
    endif    
    
    If	!Empty( mv_par01 ) // Data Conta
        oStatement:SetString(nParamOrder++, mv_par01)
        If mv_par03 == 1 // Separa conta de depreciacao? SIM/NAO
            oStatement:SetString(nParamOrder++, mv_par01)
        EndIf
    EndIf
    If	!Empty( mv_par02 ) // Data Conta
        oStatement:SetString(nParamOrder++, mv_par02)
        If mv_par03 == 1 // Separa conta de depreciacao? SIM/NAO
            oStatement:SetString(nParamOrder++, mv_par02)
        EndIf
    EndIf

    oStatement:SetString(nParamOrder++, "0")
    oStatement:SetString(nParamOrder++, " ")

    cQuery := oStatement:GetFixQuery()

Return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} getTableTemp
    @author Bruno Oliveira
    @since 14/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getTableTemp() as object class MonetaryCorrectionSmartViewBusinessObject
    Local aCampos  		:= {} as Array
    Local aTam			:= {} as Array

    AADD(aCampos,{"FILIAL","C",FWSizeFilial(),0})
    aTam := TamSX3("N5_CONTA")
    AADD(aCampos,{"CONTA"	,"C"	,aTam[1],aTam[2]	} )
    aTam := TamSX3("CT1_DESC01")
    AADD(aCampos,{"DESCRIC"	,"C"	,aTam[1],aTam[2]	} )
    AADD(aCampos,{"SALDO"	,"N"  ,16   ,4 } )
    AADD(aCampos,{"SALD1"	,"N"  ,16   ,4 } )
    AADD(aCampos,{"DEVED"	,"N"  ,16   ,4 } )
    AADD(aCampos,{"CREDO"	,"N"  ,16   ,4 } )
    AADD(aCampos,{"AMPLIA1"	,"N"  ,16   ,4 } )
    AADD(aCampos,{"REGSM0"	,"N"  ,10   ,0 } )

    ::oATFR1701 := FWTemporaryTable():New( "cNomeArq" )  
    ::oATFR1701:SetFields(aCampos) 
    ::oATFR1701:AddIndex("1", {"FILIAL","CONTA"})
    //------------------
    //Criação da tabela temporaria
    //------------------
    ::oATFR1701:Create()  

Return 
