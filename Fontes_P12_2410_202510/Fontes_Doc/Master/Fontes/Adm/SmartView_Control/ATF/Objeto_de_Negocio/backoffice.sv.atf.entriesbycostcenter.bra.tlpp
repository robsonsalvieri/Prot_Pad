#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.entriesbycostcenter.bra.ch"

//Cria um novo namespace para sua classe
namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace control.sv.functions.utils
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider

//Ativar ou desativar a utilização da classe
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1,SN3", name="Lançamento Por Centro de Custo", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} CentrodeCustoTReportsBusinessObject
Classe para criação do Objeto de Negócio do Lançamento por Centro de Custo para o TReports
Fonte/Perg      ATFR190 / ATR190
@author         Controladoria
@since          14/06/2023
@version        12.1.2310
@Revitalização  Renato July - DEZ/2023
*/
//-------------------------------------------------------------------------------
Class EntriesByCostCenterSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()                 as object
    public method getDescription()      as character
    public method getAreas()            as array
    public method getData()             as object
    public method getSchema()           as object

    //usados na internacionalização
    public method preschema()           as object 
    public method preData()             as object
    public method processData()         as json

    protected method getAdiantamentos() as character
    protected method setAdiantamentos() as object

    protected data jDados               as json
    protected data cAlias               as character
    protected data oQuery               as object
    protected data oIntegratedProvider 	as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author         Controladoria
@since          14/06/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method new() as object class EntriesByCostCenterSmartViewBusinessObject
    _Super:new()
    self:setDisplayName( STR0001 )  //"Lançamento por Centro de Custo" 
    self:setPergunte( "ATR190" )    //Indica o pergunte que será utilizado no relatório
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
@return object: string
@author         Controladoria
@since          14/06/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getDescription() as character class EntriesByCostCenterSmartViewBusinessObject
return STR0002 //"Objeto contendo informações das Lançamento por Centro de Custo" 

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
@return object: string
@author         Controladoria
@since          14/06/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getAreas() as array class EntriesByCostCenterSmartViewBusinessObject
return { STR0003 } //"Ativo Fixo" 

//-------------------------------------------------------------------
/*{Protheus.doc} getStruct
Prepara a estrutura dos campos
@param aCpos array: Array com os campos do relatório
@return array: Array com a estrutura dos campos
@author         Controladoria
@since          14/06/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getSchema() as object class EntriesByCostCenterSmartViewBusinessObject

    Local aFieldsSN1            as array
    Local aFieldsSN3            as array
	Local lCanFilter 	:= .F.  as logical

    //Campos para composição do Schema.
    aFieldsSN1  := { "N1_DESCRIC" , "N1_PATRIM"  }

    aFieldsSN3 := { "N3_FILIAL"  , "N3_CBASE"   , "N3_ITEM"    , "N3_TIPO"    , "N3_CCUSTO"  , "N3_CCONTAB" ,;
                    "N3_TXDEPR1" , "N3_VORIG1"  , "N3_VRCACM1" , "N3_VRDACM1" , "N3_VRCDA1"  , "N3_AMPLIA1" , "N3_VRDMES1" , "N3_INDICE1" , "N3_VLACEL1",;
                    "N3_TXDEPR2" , "N3_VORIG2"  , "N3_VRCACM2" , "N3_VRDACM2" , "N3_VRCDA2"  , "N3_AMPLIA2" , "N3_VRDMES2" , "N3_INDICE2" , "N3_VLACEL2",;
                    "N3_TXDEPR3" , "N3_VORIG3"  , "N3_VRCACM3" , "N3_VRDACM3" , "N3_VRCDA3"  , "N3_AMPLIA3" , "N3_VRDMES3" , "N3_INDICE3" , "N3_VLACEL3",;
                    "N3_TXDEPR4" , "N3_VORIG4"  , "N3_VRCACM4" , "N3_VRDACM4" , "N3_VRCDA4"  , "N3_AMPLIA4" , "N3_VRDMES4" , "N3_INDICE4" , "N3_VLACEL4",;
                    "N3_TXDEPR5" , "N3_VORIG5"  ,"N3_VRCACM5"  , "N3_VRDACM5" , "N3_VRCDA5"  , "N3_AMPLIA5" , "N3_VRDMES5" , "N3_INDICE5" , "N3_VLACEL5"}

    self:oSchema:aliasToSchema("SN1",aFieldsSN1)
    self:oSchema:aliasToSchema("SN3",aFieldsSN3)

    //Seta os parametros manuais no SmartView
    self:addProperty( "NMOEDAC" , STR0004 , "string" , STR0004 , "NMOEDAC",,,,lCanFilter   )
	self:addProperty( "NMOEDAS" , STR0005 , "string" , STR0005 , "NMOEDAS",,,,lCanFilter   )
    self:addProperty( "NomeEmpresa", STR0006, "string", STR0006, "NomeEmpresa",,,,lCanFilter ) // Nome Empresa
    self:addProperty( "NomeFilial",  STR0007, "string", STR0007, "NomeFilial",,,,lCanFilter  ) // Nome Filial
	
    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0008, "string", .T.) //Filiais
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
@return object: self:oData
@author         Controladoria
@since          14/06/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class EntriesByCostCenterSmartViewBusinessObject

    Local cQuery        := ""       as character

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "ATR190" )

    //Carrega o bloco de baixas
    cQuery       := self:getAdiantamentos(oFilter)

    self:cAlias  := MPSysOpenQuery(cQuery)
    self:setAdiantamentos(oFilter)

	//Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())
    IIf(Select(self:cAlias)>0,(self:cAlias)->(DbCloseArea()),MSErase(self:cAlias))

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
@author         Controladoria
@since          14/06/2023
@version        12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class EntriesByCostCenterSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Método de instância da classe
@return object: self:oData
@author         Controladoria
@since          NOV/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method preData() as object class EntriesByCostCenterSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método na Chamado no momento do processamento da query
@return object: self:oData
@author         Controladoria
@since          NOV/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method processData() class EntriesByCostCenterSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------------------
/*{Protheus.doc} getAdiantamentos
gera a query de filtragem dos dados 
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method getAdiantamentos(oFilter) as character class EntriesByCostCenterSmartViewBusinessObject

    Local cQuery                    as character
    Local lAtfCusPrv    := .F.      as logical   
    Local nContaOrder   := 1        as numeric
    Local aSelFil                   as array
    //10 - Exibir Ativos Real. Provis ?     N   Sim/Não
    lAtfCusPrv := AFXAtCsPrv()

    //Salva nome empresa e filial logada
    ::oIntegratedProvider := ControlIntegratedProvider():New()

	::oIntegratedProvider:lMultSelectBranches := .T.
	::oIntegratedProvider:lAllowBranchFilter  := .F.
	::oIntegratedProvider:setBranchPar(oFilter:getParameters())
	::oIntegratedProvider:loadFilterBranches({"CTT"})

	If Valtype(::oIntegratedProvider:oFilterBranches["CTT"][1]) == "A"
		aSelFil := ::oIntegratedProvider:oFilterBranches["CTT"][1]
	Else
		aSelFil := ::oIntegratedProvider:oFilterBranches["CTT"]
	EndIf

    //Select para trabalho.
    cQuery := " SELECT "
    cQuery += " N1_DESCRIC , N1_PATRIM , " 
    cQuery += " N3_FILIAL  , N3_CBASE  , N3_ITEM    , N3_TIPO    , N3_CCUSTO  , N3_CCONTAB , "
    cQuery += " N3_TXDEPR1 , N3_VORIG1 , N3_VRDACM1 , N3_AMPLIA1 , N3_VRDMES1 , N3_VRCDA1  , N3_VRCACM1 , "
    cQuery += " N3_TXDEPR2 , N3_VORIG2 , N3_VRDACM2 , N3_AMPLIA2 , N3_VRDMES2 , "
    cQuery += " N3_TXDEPR3 , N3_VORIG3 , N3_VRDACM3 , N3_AMPLIA3 , N3_VRDMES3 , "
    cQuery += " N3_TXDEPR4 , N3_VORIG4 , N3_VRDACM4 , N3_AMPLIA4 , N3_VRDMES4 , "
    cQuery += " N3_TXDEPR5 , N3_VORIG5 , N3_VRDACM5 , N3_AMPLIA5 , N3_VRDMES5 "

    cQuery += " FROM " + RetSqlName("SN3") + " SN3 "
    cQuery += " INNER JOIN " + RetSqlName("SN1") + " SN1 ON SN1.D_E_L_E_T_  = ? "
    cQuery += "   AND SN1.N1_FILIAL   = SN3.N3_FILIAL "
    cQuery += "   AND SN1.N1_CBASE    = SN3.N3_CBASE "
    cQuery += "   AND SN1.N1_ITEM     = SN3.N3_ITEM  "

    //Seleção de dados
    cQuery += " WHERE SN3.D_E_L_E_T_  = ? "
    if !empty(aSelFil)
        cQuery += "   AND SN3.N3_FILIAL   IN ( ? ) "
    else
        cQuery += "   AND SN3.N3_FILIAL   =  ?  "
    endif    
    cQuery += "   AND SN3.N3_CCUSTO  >= ? "
    cQuery += "   AND SN3.N3_CCUSTO  <= ? "
    cQuery += "   AND SN3.N3_CCONTAB >= ? "
    cQuery += "   AND SN3.N3_CCONTAB <= ? "

    cQuery += "   AND (SN3.N3_CCUSTO  <> ? "
    cQuery += "    OR  SN3.N3_CDEPREC <> ? "
    cQuery += "    OR  SN3.N3_CDESP   <> ? "
    cQuery += "    OR  SN3.N3_CCDEPR  <> ? ) "

    // 05 - Considera Baixas ?      N Sim/Não
	If MV_PAR05 == 1
		cQuery += "   AND ( N3_BAIXA     < ? OR "
		If MV_PAR06 == 4
            cQuery += "( N3_BAIXA     >= ? "
        Else
            cQuery += "( N3_BAIXA      = ? "
        EndIf
		cQuery += "   AND N3_DTBAIXA  >= ? "
		cQuery += "   AND N3_DTBAIXA  <= ? )) "
	Else	
        cQuery += "   AND N3_BAIXA     < ? "
	EndIf

    //10 - Exibir Ativos Real. Provis
    If lAtfCusPrv  .And. MV_PAR10 == 2
        cQuery += "   AND SN3.N3_ATFCPR   <> ? "
    EndIf 

    //Os filtros serão setados na interface do novo TReports     
    cQuery += IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")    

    //Define o campo de ordenação
    cQuery += " ORDER BY N3_FILIAL,N3_CCUSTO,N3_CCONTAB,N3_CBASE,N3_ITEM,N3_TIPO"

    //Objeto executa o comando SQL inúmeras vezes.
    self:oQuery:= FWPreparedStatement():New(ChangeQuery(cQuery))

    self:oQuery:SetString( nContaOrder++ , Space(01))
    self:oQuery:SetString( nContaOrder++ , Space(01))
    if !empty(aSelFil)
        self:oQuery:setIn( nContaOrder++ , aSelFil)
    else
        self:oQuery:SetString( nContaOrder++ , FWXFilial("SN3"))
    endif
    self:oQuery:SetString( nContaOrder++ , MV_PAR01 )
    self:oQuery:SetString( nContaOrder++ , MV_PAR02 )
    self:oQuery:SetString( nContaOrder++ , MV_PAR03 )
    self:oQuery:SetString( nContaOrder++ , MV_PAR04 )

    self:oQuery:SetString( nContaOrder++ ,  Space(1))
    self:oQuery:SetString( nContaOrder++ ,  Space(1))
    self:oQuery:SetString( nContaOrder++ ,  Space(1))
    self:oQuery:SetString( nContaOrder++ ,  Space(1))

    If MV_PAR05 == 1
        self:oQuery:SetString( nContaOrder++ ,  "1")

		If MV_PAR06 == 4
            self:oQuery:SetString( nContaOrder++ ,  "1")
        Else
            self:oQuery:SetString( nContaOrder++ , AllTrim(Str(mv_par06)))
        EndIf

        self:oQuery:SetDate(   nContaOrder++ , MV_PAR07 )
        self:oQuery:SetDate(   nContaOrder++ , MV_PAR08 )
	Else	
        self:oQuery:SetString( nContaOrder++ ,  "1")
	EndIf

    If lAtfCusPrv  .And. MV_PAR10 == 2
        self:oQuery:SetString( nContaOrder++ ,  "1")
    EndIf 

    //Retorna query com parâmetros tratados e substituídos.
    cQuery := self:oQuery:GetFixQuery()

return cQuery

//-------------------------------------------------------------------------------
/*{Protheus.doc} setAdiantamentos
seta os dados filtrados no objeto oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method setAdiantamentos(oFilter as object) as object class EntriesByCostCenterSmartViewBusinessObject

    Local lAtClDepr     := .F.      as logical
    Local cMoeda                    as character
    Local cCentroCus                as character
    Local cConta                    as character 
    Local cMoedaTmp                 as character
	Local cMoedasTMp                as character
    Local cCompanyName              as character
    Local cBranchName               as character

    // Acumuladores de Conta
    Local nCtaVlOri1                as numeric
    Local nCtaDpAcm1                as numeric
    Local nCtaDpMes1                as numeric
    Local nCtaCrDep1                as numeric
    Local nCtaCrMon1                as numeric
    Local nCtaVlOri2                as numeric
    Local nCtaDpAcm2                as numeric
    Local nCtaDpMes2                as numeric
    Local nCtaCrDep2                as numeric
    Local nCtaCrMon2                as numeric
    Local nCtaVlAmp1                as numeric
    Local nCtaVlAmp2                as numeric

    // Acumuladores de Centro de Custo
    Local nValOri1                  as numeric
    Local nDepAcm1                  as numeric
    Local nDepMes1                  as numeric
    Local nCorDep1                  as numeric
    Local nCorMon1                  as numeric
    Local nValOri2                  as numeric
    Local nDepAcm2                  as numeric
    Local nDepMes2                  as numeric
    Local nCorDep2                  as numeric
    Local nCorMon2                  as numeric
    Local nValAmp1                  as numeric
    Local nValAmp2                  as numeric

    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()
    
    cMoeda      := AllTrim(SuperGetMv("MV_ATFMOED"))
    cMoedaTmp	:= cMoeda
	cMoedasTMp	:= SuperGetMv("MV_SIMB"+cMoeda)

    //Posiciona no registro inicial
    (self:cAlias)->(DBGoTop())

    While !(self:cAlias)->(Eof())
        
        cCentroCus  := (self:cAlias)->N3_CCUSTO
        While (self:cAlias)->(!Eof()) .And. (self:cAlias)->N3_CCUSTO == cCentroCus

            self:jDados := JsonObject():new()

            cCentroCus  := (self:cAlias)->N3_CCUSTO
            cConta      := (self:cAlias)->N3_CCONTAB
            While (self:cAlias)->(!Eof()) .And. (self:cAlias)->N3_CCUSTO == cCentroCus .And. (self:cAlias)->N3_CCONTAB == cConta

                cD1_N3FILIAL		    := AllTrim((self:cAlias)->N3_FILIAL)
                cD1_N3CCUSTO		    := AllTrim((self:cAlias)->N3_CCUSTO)
                cD1_N3CCONTAB		    := AllTrim((self:cAlias)->N3_CCONTAB)
                cD1_N3CBASE		        := AllTrim((self:cAlias)->N3_CBASE)
                cD1_N3ITEM		        := AllTrim((self:cAlias)->N3_ITEM)
                cD1_N3TIPO		        := AllTrim((self:cAlias)->N3_TIPO)
                cD1_N1DESCRIC		    := AllTrim((self:cAlias)->N1_DESCRIC)
                cD1_N1PATRIM		    := AllTrim((self:cAlias)->N1_PATRIM)

                lAtClDepr := Iif(FindFunction("AtClssVer"),AtClssVer((self:cAlias)->N1_PATRIM),(self:cAlias)->N1_PATRIM $ "NID")

                //Acumula Conta em Moeda 1
                If lAtClDepr .Or. (self:cAlias)->N1_PATRIM $ " P"
                    nCtaVlOri1 += (self:cAlias)->N3_VORIG1
                    nCtaDpAcm1 += (self:cAlias)->N3_VRDACM1 
                    nCtaVlAmp1 += (self:cAlias)->N3_AMPLIA1
                    nCtaDpMes1 += (self:cAlias)->N3_VRDMES1 
                    nCtaCrDep1 += (self:cAlias)->N3_VRCDA1  
                    nCtaCrMon1 += (self:cAlias)->N3_VRCACM1 
                Else
                    nCtaVlOri1 -= (self:cAlias)->N3_VORIG1
                    nCtaVlAmp1 -= (self:cAlias)->N3_AMPLIA1
                    nCtaDpAcm1 -= (self:cAlias)->N3_VRDACM1 
                    nCtaDpMes1 -= (self:cAlias)->N3_VRDMES1 
                    nCtaCrDep1 -= (self:cAlias)->N3_VRCDA1  
                    nCtaCrMon1 -= (self:cAlias)->N3_VRCACM1 
                Endif

                //Acumula Conta em Moeda do Ativo
                If lAtClDepr .Or. (self:cAlias)->N1_PATRIM $ " P"
                    nCtaVlOri2 += (self:cAlias)->&("N3_VORIG"+cMoeda)
                    nCtaVlAmp2 += (self:cAlias)->&("N3_AMPLIA"+cMoeda)
                    nCtaDpAcm2 += (self:cAlias)->&("N3_VRDACM"+cMoeda)
                    nCtaDpMes2 += (self:cAlias)->&("N3_VRDMES"+cMoeda)
                    nCtaCrDep2 += IIf(cMoeda=="1",(self:cAlias)->N3_VRCDA1,0)  
                    nCtaCrMon2 += IIf(cMoeda=="1",(self:cAlias)->N3_VRCACM1,0) 
                Else
                    nCtaVlOri2 -= (self:cAlias)->&("N3_VORIG"+cMoeda)
                    nCtaVlAmp2 -= (self:cAlias)->&("N3_AMPLIA"+cMoeda)
                    nCtaDpAcm2 -= (self:cAlias)->&("N3_VRDACM"+cMoeda)
                    nCtaDpMes2 -= (self:cAlias)->&("N3_VRDMES"+cMoeda)
                    nCtaCrDep2 -= IIf(cMoeda=="1",(self:cAlias)->N3_VRCDA1,0)  
                    nCtaCrMon2 -= IIf(cMoeda=="1",(self:cAlias)->N3_VRCACM1,0) 
                Endif

                //Acumula Centro de Custo em Moeda 1
                If lAtClDepr .Or. (self:cAlias)->N1_PATRIM $ " P"
                    nValOri1 += (self:cAlias)->N3_VORIG1
                    nValAmp1 += (self:cAlias)->N3_AMPLIA1
                    nDepAcm1 += (self:cAlias)->N3_VRDACM1
                    nDepMes1 += (self:cAlias)->N3_VRDMES1
                    nCorDep1 += (self:cAlias)->N3_VRCDA1 
                    nCorMon1 += (self:cAlias)->N3_VRCACM1
                Else
                    nValOri1 -= (self:cAlias)->N3_VORIG1
                    nValAmp1 -= (self:cAlias)->N3_AMPLIA1
                    nDepAcm1 -= (self:cAlias)->N3_VRDACM1
                    nDepMes1 -= (self:cAlias)->N3_VRDMES1
                    nCorDep1 -= (self:cAlias)->N3_VRCDA1 
                    nCorMon1 -= (self:cAlias)->N3_VRCACM1
                Endif

                //Acumula Centro de Custo em Moeda do Ativo
                If lAtClDepr .Or. (self:cAlias)->N1_PATRIM $ " P"
                    nValOri2 += (self:cAlias)->&("N3_VORIG"+cMoeda)
                    nValAmp2 += (self:cAlias)->&("N3_AMPLIA"+cMoeda)
                    nDepAcm2 += (self:cAlias)->&("N3_VRDACM"+cMoeda)
                    nDepMes2 += (self:cAlias)->&("N3_VRDMES"+cMoeda)
                    nCorDep2 += IIf(cMoeda=="1",(self:cAlias)->N3_VRCDA1,0)
                    nCorMon2 += IIf(cMoeda=="1",(self:cAlias)->N3_VRCACM1,0)
                Else
                    nValOri2 -= (self:cAlias)->&("N3_VORIG"+cMoeda)
                    nValAmp2 -= (self:cAlias)->&("N3_AMPLIA"+cMoeda)
                    nDepAcm2 -= (self:cAlias)->&("N3_VRDACM"+cMoeda)
                    nDepMes2 -= (self:cAlias)->&("N3_VRDMES"+cMoeda)
                    nCorDep2 -= IIf(cMoeda=="1",(self:cAlias)->N3_VRCDA1,0)
                    nCorMon2 -= IIf(cMoeda=="1",(self:cAlias)->N3_VRCACM1,0)
                Endif

                //Popular o jDados
                dbSelectArea(self:cAlias)
                cConta      := (self:cAlias)->N3_CCONTAB
                (self:cAlias)->(dbSkip())
                If (self:cAlias)->N3_CCONTAB <> cConta .Or. (self:cAlias)->N3_CCUSTO <> cCentroCus

                    //Campos da query
                    self:jDados["N3_FILIAL"]		    := cD1_N3FILIAL
                    self:jDados["N3_CCUSTO"]		    := cD1_N3CCUSTO
                    self:jDados["N3_CCONTAB"]		    := cD1_N3CCONTAB
                    self:jDados["N3_CBASE"]		        := cD1_N3CBASE
                    self:jDados["N3_ITEM"]		        := cD1_N3ITEM
                    self:jDados["N3_TIPO"]		        := cD1_N3TIPO
                    self:jDados["N1_DESCRIC"]		    := cD1_N1DESCRIC
                    self:jDados["N1_PATRIM"]		    := cD1_N1PATRIM

                    Do Case
                    Case cMoeda == "1"
                        self:jDados["N3_TXDEPR1"]		    := (self:cAlias)->N3_TXDEPR1
                        self:jDados["N3_VORIG1"]		    := nCtaVlOri1+nCtaCrMon1+nCtaVlAmp1
                        self:jDados["N3_AMPLIA1"]		    := nCtaVlAmp1
                        self:jDados["N3_VRDACM1"]		    := nCtaDpAcm1
                        self:jDados["N3_VRDMES1"]		    := nCtaDpMes1
                        self:jDados["N3_VRCDA1"]		    := nCtaCrDep1
                        self:jDados["N3_VRCACM1"]		    := nCtaCrMon1
                    Case cMoeda == "2"
                        self:jDados["N3_TXDEPR2"]		    := (self:cAlias)->N3_TXDEPR2
                        self:jDados["N3_VORIG2"]		    := nCtaVlOri2+nCtaCrMon2+nCtaVlAmp2
                        self:jDados["N3_AMPLIA2"]		    := nValAmp2
                        self:jDados["N3_VRDACM2"]		    := nDepAcm2
                        self:jDados["N3_VRDMES2"]		    := nDepMes2
                        self:jDados["N3_VRCDA1"]		    := nCorDep2
                        self:jDados["N3_VRCACM1"]		    := nCorMon2
                    Case cMoeda == "3"
                        self:jDados["N3_TXDEPR3"]		    := (self:cAlias)->N3_TXDEPR3
                        self:jDados["N3_VORIG3"]		    := nCtaVlOri2+nCtaCrMon2+nCtaVlAmp2
                        self:jDados["N3_AMPLIA3"]		    := nValAmp2
                        self:jDados["N3_VRDACM3"]		    := nDepAcm2
                        self:jDados["N3_VRDMES3"]		    := nDepMes2
                        self:jDados["N3_VRCDA1"]		    := nCorDep2
                        self:jDados["N3_VRCACM1"]		    := nCorMon2
                    Case cMoeda == "4"
                        self:jDados["N3_TXDEPR4"]		    := (self:cAlias)->N3_TXDEPR4
                        self:jDados["N3_VORIG4"]		    := nCtaVlOri2+nCtaCrMon2+nCtaVlAmp2
                        self:jDados["N3_AMPLIA4"]		    := nValAmp2
                        self:jDados["N3_VRDACM4"]		    := nDepAcm2
                        self:jDados["N3_VRDMES4"]		    := nDepMes2
                        self:jDados["N3_VRCDA1"]		    := nCorDep2
                        self:jDados["N3_VRCACM1"]		    := nCorMon2
                    Case cMoeda == "5"
                        self:jDados["N3_TXDEPR5"]		    := (self:cAlias)->N3_TXDEPR5
                        self:jDados["N3_VORIG5"]		    := nCtaVlOri2+nCtaCrMon2+nCtaVlAmp2
                        self:jDados["N3_AMPLIA5"]		    := nValAmp2
                        self:jDados["N3_VRDACM5"]		    := nDepAcm2
                        self:jDados["N3_VRDMES5"]		    := nDepMes2
                        self:jDados["N3_VRCDA1"]		    := nCorDep2
                        self:jDados["N3_VRCACM1"]		    := nCorMon2
                    EndCase

                    //Campos para a composição do Desing
                    self:jDados["NMOEDAC"]             := cMoedaTmp
                    self:jDados["NMOEDAS"]             := cMoedasTMp
                    self:jDados[ "NomeEmpresa" ]       := cCompanyName
                	self:jDados[ "NomeFilial" ]        := cBranchName

                EndIf

            End

            self:processData()
            self:oData:appendData(self:jDados)

            dbSelectArea(self:cAlias)
            nCtaVlOri1 := nCtaDpAcm1 := nCtaDpMes1 := nCtaCrDep1 := nCtaCrMon1 := 0
            nCtaVlOri2 := nCtaDpAcm2 := nCtaDpMes2 := nCtaCrDep2 := nCtaCrMon2 := 0
            nCtaVlAmp1 := nCtaVlAmp2 := 0

        End

    End

return self:oData
