#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.depreciatedassets.bra.ch"

namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN3,SN1", name="Bens Depreciados", country="ALL", , customTables="SN1,SN3")

//-------------------------------------------------------------------------------
/*{Protheus.doc} DepreciatedAssetsSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Bens Depreciados para o TReports
 
@author Renan Gremes
@since 06/06/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class DepreciatedAssetsSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()             as object
    public method getDescription()  as character
    public method getAreas()        as array
    public method getData()         as object
    public method getSchema()       as object

    public method preschema()       as object 
    public method preData()         as object
    public method processData()     as json
    
    private method AfrSaldo()       as numeric

    protected method getQuery()     as character
    protected method setData()      as object
   
    protected data cAlias           as character
    protected data cBaseDe          as character
    protected data cBaseAte         as character
    protected data cFilDe           as character
    protected data cFilAte          as character
    protected data cMoeda           as character
    protected data cN1TipoNeg       as character
    protected data cN3TipoNeg       as character
    protected data cCompanyName     as character
    protected data cBranchName      as character
    protected data aAllFields       as array
    protected data aSelFil          as array
    protected data lConsFil         as logical
    protected data lAtfCusPrv       as logical
    protected data lExAtRePr        as logical
    protected data jDados           as json
    protected data oQuery           as object
    protected data oIntegratedProvider as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 06/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method new() as object Class DepreciatedAssetsSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) // "Bens Depreciados"
    self:setPergunte("ATR180")   // Indica o pergunte que será utilizado no relatório

    ::lAtfCusPrv := AFXAtCsPrv()
    ::cMoeda     := SuperGetMv("MV_ATFMOED",,"1")         // Moeda configurada no módulo ATF
    ::cN1TipoNeg := Alltrim(SuperGetMv("MV_N1TPNEG",,"")) // Tipos de N1_PATRIM que aceitam Valor originais negativos
    ::cN3TipoNeg := Alltrim(SuperGetMv("MV_N3TPNEG",,"")) // Tipos de N3_TIPO que aceitam Valor originais negativos
    ::aSelFil    := {}
    ::aAllFields := {}
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 06/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getDescription() as character class DepreciatedAssetsSmartViewBusinessObject
return STR0002 // "Objeto contendo informações Saldos a Depreciar por Conta"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 06/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getAreas() as array class DepreciatedAssetsSmartViewBusinessObject
Return {STR0003} // "Ativo Fixo"

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 06/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class DepreciatedAssetsSmartViewBusinessObject    
    
    local cQuery as character

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "ATR180")

    //Definição de parametros
    ::cBaseDe   := MV_PAR01                         // Do Bem ?
    ::cBaseAte  := MV_PAR02                         // Ate o Bem ?
    ::lConsFil  := iif(MV_PAR08 == 1, .T., .F.)     // Cons Filiais Abaixo ?
    ::cFilDe    := MV_PAR09                         // Filial De ?
    ::cFilAte   := iif(Empty(MV_PAR10), FwXFilial("SN3"), MV_PAR10) // Filial Até ?
    ::lExAtRePr := iif(MV_PAR12 == 1, .T., .F.)     // Exibir Ativos Real. Provis ?

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    ::cCompanyName := ::oIntegratedProvider:getCompanyName()
    ::cBranchName  := ::oIntegratedProvider:getBranchName()

    //Seleção de filiais
    If !self:lConsFil
        ::oIntegratedProvider:lMultSelectBranches := .T.
        ::oIntegratedProvider:lAllowBranchFilter  := .T.
        ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
        ::oIntegratedProvider:loadFilterBranches({"SN3"})

        If Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
            ::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1]
        Else
            ::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
        EndIf
    EndIf

    cQuery := self:getQuery(oFilter)
    self:cAlias := MPSysOpenQuery(cQuery)
    self:setData(oFilter)

    //Elimina da memória a instância do objeto informado como parâmetro
    FreeObj(oFilter:getParameters())
    FreeObj(::oIntegratedProvider)

    iif(Select(self:cAlias) > 0, (self:cAlias)->(DbCloseArea()), MSErase(self:cAlias))    
    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.

    @author Controladoria
    @since  DEZ/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method getQuery(oFilter as object) as character class DepreciatedAssetsSmartViewBusinessObject
    
    local cSpace  := space(1) as character
    local cQuery       as character
    local nIndice := 1 as numeric

    //Select    
    cQuery := "SELECT "
    cQuery += self:getSQLFields() //Pega campos do schema
    cQuery += " FROM "+RetSqlName("SN3")+" SN3  "
    cQuery += " JOIN "+RetSqlName("SN1")+" SN1 ON SN1.N1_FILIAL = SN3.N3_FILIAL "
    cQuery += " AND SN1.N1_CBASE = SN3.N3_CBASE "
    cQuery += " AND SN1.N1_ITEM = SN3.N3_ITEM "
    cQuery += " AND SN1.D_E_L_E_T_ = ? "
    cQuery += " WHERE "

    if self:lConsFil
        cQuery += " SN3.N3_FILIAL BETWEEN ? AND ? "
	else
        if !Empty(self:aSelFil)
            cQuery += " SN3.N3_FILIAL IN ( ? ) "
        else
            cQuery += " SN3.N3_FILIAL = ? "
        endif
	endif
	
    if UPPER(AllTrim(::cBaseDe)) == UPPER(AllTrim(::cBaseAte))
		cQuery += " AND SN3.N3_CBASE = ? "
	else
		cQuery += " AND SN3.N3_CBASE BETWEEN ? AND ? "
	endif

    cQuery += " AND SN3.N3_BAIXA = ? AND ( SN3.N3_CDEPREC != ? OR SN3.N3_CDESP != ? OR SN3.N3_CCDEPR != ? ) "

    if self:lAtfCusPrv .And. !self:lExAtRePr
		cQuery += " AND SN3.N3_ATFCPR IN ( ? ) "
	endif

    cQuery += " AND SN3.D_E_L_E_T_ = ? "

    //Os filtros serão setados na interface do novo TReports
    cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

    cQuery  += " ORDER BY SN3.N3_CBASE"
	
	self:oQuery := FWExecStatement():New(ChangeQuery(cQuery))

    //Popula binds da query
    self:oQuery:SetString(nIndice++, cSpace)

    if self:lConsFil
        self:oQuery:SetString(nIndice++, ::cFilDe)
        self:oQuery:SetString(nIndice++, ::cFilAte)
	else
        if !Empty(self:aSelFil)
            self:oQuery:SetIn(nIndice++, ::aSelFil)
        else
            self:oQuery:SetString(nIndice++, FWxFilial("SN3"))
        endif
	endif

    if UPPER(AllTrim(::cBaseDe)) == UPPER(AllTrim(::cBaseAte))
		self:oQuery:SetString(nIndice++, ::cBaseDe)        
	else
		self:oQuery:SetString(nIndice++, ::cBaseDe)
        self:oQuery:SetString(nIndice++, ::cBaseAte)
	endif
    self:oQuery:SetString(nIndice++, "0")
    self:oQuery:SetString(nIndice++, cSpace)
    self:oQuery:SetString(nIndice++, cSpace)
    self:oQuery:SetString(nIndice++, cSpace)
    if self:lAtfCusPrv .And. !self:lExAtRePr
        self:oQuery:SetIn(nIndice++, {" ", "2"})
    endif
    self:oQuery:SetString(nIndice++, cSpace)
	
	cQuery := self:oQuery:GetFixQuery()

return cQuery


//-------------------------------------------------------------------------------
/*{Protheus.doc} setData
    Seta os dados filtrados no objeto oData

    @author Controladoria
    @since DEZ/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------------------

method setData(oFilter as object) as object class DepreciatedAssetsSmartViewBusinessObject
    
    local nX as numeric

    self:aAllFields := self:getStructFields()
    
    //Posiciona no registro inicial
    (self:cAlias)->(dbGoTop())

    While !(self:cAlias)->(Eof())
        //Verifica o saldo para impressao ou nao do bem
        if self:AfrSaldo() <= 0
            ::jDados := JsonObject():new()

            //Popula campos da query
            For nX := 1 To Len(self:aAllFields)
                if !(self:aAllFields[nX]:getName() $ "NomeEmpresa|NomeFilial")
                    if self:aAllFields[nX]:getType() == "date"
                        ::jDados[self:aAllFields[nX]:getName()] := iif(!empty((self:cAlias)->&(self:aAllFields[nX]:getRealName())), FwTimeStamp(5, StoD((self:cAlias)->&(self:aAllFields[nX]:getRealName())), "00:00:00"), nil)
                    elseif self:aAllFields[nX]:getType() == "string"
                        ::jDados[self:aAllFields[nX]:getName()] := AllTrim((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
                    else
                        ::jDados[self:aAllFields[nX]:getName()] := (self:cAlias)->&(self:aAllFields[nX]:getRealName())
                    endif
                endif
            Next nX

            ::jDados["NomeEmpresa"] := ::cCompanyName
            ::jDados["NomeFilial" ] := ::cBranchName

            self:processData()
            self:oData:appendData(::jDados)
        endif

        (self:cAlias)->(dbSkip())        
    EndDo

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Metodo que retorna a Estrutura de dados.
 
@param 
@return object: self:oSchema
 
@author Renan Gremes
@since 06/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class DepreciatedAssetsSmartViewBusinessObject
   
    local aFieldsSN1 as array
    local aFieldsSN3 as array
    local lCanFilter := .F. as logical

    aFieldsSN1 := { "N1_DESCRIC", "N1_PATRIM" }

    aFieldsSN3 := { "N3_FILIAL",  "N3_CBASE",   "N3_ITEM",    "N3_TXDEPR1", "N3_TIPO",    "N3_VORIG1",; 
                    "N3_VORIG2",  "N3_VORIG3",  "N3_VORIG4",  "N3_VORIG5",  "N3_VRCACM1", "N3_VRDACM1",;
                    "N3_VRDACM2", "N3_VRDACM3", "N3_VRDACM4", "N3_VRDACM5", "N3_VRCDA1",  "N3_AMPLIA1",;
                    "N3_AMPLIA2", "N3_AMPLIA3", "N3_AMPLIA4", "N3_AMPLIA5", "N3_TPSALDO", "N3_AQUISIC", "N3_DTBAIXA" }

    //Adiciona campos ao schema
    self:oSchema:aliasToSchema("SN1", aFieldsSN1)
    self:oSchema:aliasToSchema("SN3", aFieldsSN3)

    self:oSchema:addProperty("NomeEmpresa", STR0005, "string", STR0005, "NomeEmpresa",,,,lCanFilter) // Nome Empresa
    self:oSchema:addProperty("NomeFilial",  STR0006, "string", STR0006, "NomeFilial" ,,,,lCanFilter) // Nome Filial   

    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0004, "string", .T.) //Filiais
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@param 
@return object: self:oSchema
 
@author Renan Gremes
@since 06/06/2023
@version 1.0
*/
//-------------------------------------------------------------------

method preSchema() as object class DepreciatedAssetsSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.
 
@param 
@return object: self:oData
 
@author Renan Gremes
@since 06/06/2023
@version 1.0
*/
//-------------------------------------------------------------------

method preData() as object class DepreciatedAssetsSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
 
@param 
@return object: self:oData
 
@author Renan Gremes
@since 06/06/2023
@version 1.0
*/
//-------------------------------------------------------------------

method processData() class DepreciatedAssetsSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} AfrSaldo
Retorna o saldo para impressao ou nao do bem
 
@param cAlias, character, indica alias da consulta
@param cMoeda, character, indica a moeda a ser validada
@param cN1TipoNeg, character, Indica os Tipos de N1_PATRIM que aceitam 
                              valores originais negativos
@param cN3TipoNeg, character, Indica os tipos de N3_TIPO que aceitam 
                              valores originais negativos
 
@return numeric: nSaldo
 
@author Renan Gremes
@since 26/10/2023
@version 1.0
*/
//-------------------------------------------------------------------

method AfrSaldo() as numeric class DepreciatedAssetsSmartViewBusinessObject

    local nSaldo := 0 as numeric

	if ::cMoeda == "1"
		if (self:cAlias)->N3_TIPO = "05" .Or. (self:cAlias)->N1_PATRIM $ ::cN1TipoNeg .Or. (self:cAlias)->N3_TIPO $ ::cN3TipoNeg
			nSaldo := (self:cAlias)->(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1+Abs(N3_VRDACM1+N3_VRCDA1))
		else
			nSaldo := (self:cAlias)->(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1)-((self:cAlias)->N3_VRDACM1+(self:cAlias)->N3_VRCDA1)
		endif
	else
		if (self:cAlias)->N3_TIPO = "05" .Or. (self:cAlias)->N1_PATRIM $ ::cN1TipoNeg .Or. (self:cAlias)->N3_TIPO $ ::cN3TipoNeg
			if ::cMoeda == "2"
				nSaldo := (self:cAlias)->( N3_VORIG2 + N3_AMPLIA2 + Abs(N3_VRDACM2) )
			elseif ::cMoeda == "3"
				nSaldo := (self:cAlias)->( N3_VORIG3 + N3_AMPLIA3 + Abs(N3_VRDACM3) )
			elseif ::cMoeda == "4"
				nSaldo := (self:cAlias)->( N3_VORIG4 + N3_AMPLIA4 + Abs(N3_VRDACM4) )
			elseif ::cMoeda == "5"
				nSaldo := (self:cAlias)->( N3_VORIG5 + N3_AMPLIA5 + Abs(N3_VRDACM5) )
			endif
		else
			if ::cMoeda == "2"
				nSaldo :=(self:cAlias)->( N3_VORIG2 + N3_AMPLIA2 - N3_VRDACM2 )
			elseif ::cMoeda == "3"
				nSaldo :=(self:cAlias)->( N3_VORIG3 + N3_AMPLIA3 - N3_VRDACM3 )
			elseif ::cMoeda == "4"
				nSaldo :=(self:cAlias)->( N3_VORIG4 + N3_AMPLIA4 - N3_VRDACM4 )
			elseif ::cMoeda == "5"
				nSaldo :=(self:cAlias)->( N3_VORIG5 + N3_AMPLIA5 - N3_VRDACM5 )
			endif
		endif
	endif

return nSaldo
