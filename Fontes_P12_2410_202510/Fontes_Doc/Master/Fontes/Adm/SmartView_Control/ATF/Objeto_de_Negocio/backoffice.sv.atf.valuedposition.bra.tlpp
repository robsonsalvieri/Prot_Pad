#include "protheus.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.valuedposition.bra.ch"

//Cria um novo namespace para sua classe
namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace control.sv.functions.utils
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider

//Ativar ou desativar a utilização da classe
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN3,CT1", name="Posicao Valorizada dos Bens", country="ALL",customTables="SN1,SN3,CT1")

//-------------------------------------------------------------------------------
/*{Protheus.doc} ValuedPositionSmartViewBusinessObject
Classe para criação do Objeto de Negócio da Posicao Valorizada dos Bens para o Smart View
Fonte/Perg      ATFR070	/ AFR070
@author         Douglas Silva
@since          16/06/2023
@version        12.1.2310
@Revitalização  Renato July - OUT/2023
*/
//-------------------------------------------------------------------------------
Class ValuedPositionSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()                 as object
    public method getDescription()      as character
    public method getAreas()            as array
    public method getData()             as object
    public method getSchema()           as object

    //usados na internacionalização
    public method preschema()           as object 
    public method preData()             as object
    public method processData()         as json 
    
    protected method getAdiantamentos() as character
    protected method setAdiantamentos() as object
    
    protected data jDados               as json
    protected data aAllFields           as array
    protected data cAlias               as character
    protected data oQuery               as object
    protected data oIntegratedProvider  as object
    protected data aSelFil              as array

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self
@author         
@since          
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method new() as object Class ValuedPositionSmartViewBusinessObject
    _Super:new()
    self:setDisplayName( STR0001 )  //"Posição Valorizada por Conta"
    self:setPergunte( "AFR070" )    //Indica o pergunte que será utilizado no relatório
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
@return object: string
@author         
@since          
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getDescription() as character class ValuedPositionSmartViewBusinessObject
return STR0002 //"Objeto contendo informações Posição Valorizada por Conta"

//-------------------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
@author         
@since          
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method getAreas() as array class ValuedPositionSmartViewBusinessObject
Return { STR0003 } //"Ativo Fixo"

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
@param aCpos array: Array com os campos do relatório
@return array: Array com a estrutura dos campos
@author         
@since          
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getSchema() as object class ValuedPositionSmartViewBusinessObject

    Local aFieldsCT1            as array
    Local aFieldsSN3            as array
    Local aFieldsSN1            as array

    aFieldsCT1  := {;
                    "CT1_DESC01";
                    }
    aFieldsSN1  := {;
                    "N1_PATRIM","N1_FILIAL","N1_CBASE","N1_ITEM";
                    }

    aFieldsSN3  := {;
                    "N3_FILIAL"  , "N3_CBASE"   , "N3_ITEM"    , "N3_TIPO"    , "N3_TIPREAV" , "N3_BAIXA"   , "N3_HISTOR"  , ;
                    "N3_TPSALDO" , "N3_TPDEPR"  , "N3_CCONTAB" , "N3_CUSTBEM" , "N3_CDEPREC" , "N3_CCUSTO"  , "N3_CCDEPR"  , ;
                    "N3_CDESP"   , "N3_CCORREC" , "N3_NLANCTO" , "N3_DLANCTO" , "N3_DINDEPR" , "N3_FIMDEPR" , "N3_DEXAUST" , ;
                    "N3_INDICE1" , "N3_VORIG1"  , "N3_TXDEPR1" , "N3_VRDBAL1" , "N3_VRCACM1" , "N3_VRDACM1" , "N3_VRDMES1" , "N3_VRCDA1" , "N3_AMPLIA1" , "N3_VLACEL1" , ;
                    "N3_INDICE2" , "N3_VORIG2"  , "N3_TXDEPR2" , "N3_VRDBAL2" , "N3_VRCACM2" , "N3_VRDACM2" , "N3_VRDMES2" , "N3_VRCDA2" , "N3_AMPLIA2" , "N3_VLACEL2" , ;
                    "N3_INDICE3" , "N3_VORIG3"  , "N3_TXDEPR3" , "N3_VRDBAL3" , "N3_VRCACM3" , "N3_VRDACM3" , "N3_VRDMES3" , "N3_VRCDA3" , "N3_AMPLIA3" , "N3_VLACEL3" , ;
                    "N3_INDICE4" , "N3_VORIG4"  , "N3_TXDEPR4" , "N3_VRDBAL4" , "N3_VRCACM4" , "N3_VRDACM4" , "N3_VRDMES4" , "N3_VRCDA4" , "N3_AMPLIA4" , "N3_VLACEL4" , ;
                    "N3_INDICE5" , "N3_VORIG5"  , "N3_TXDEPR5" , "N3_VRDBAL5" , "N3_VRCACM5" , "N3_VRDACM5" , "N3_VRDMES5" , "N3_VRCDA5" , "N3_AMPLIA5" , "N3_VLACEL5" , ;
                    "N3_VRCMES1" , "N3_AQUISIC" , "N3_DTBAIXA" , "N3_VRCDM1"  , "N3_VRCDB1"  , "N3_PERDEPR" , "N3_VMXDEPR" , ;
                    "N3_VRCBAL1" , "N3_VLSALV1" , "N3_SUBCDES" , "N3_SUBCCOR" , "N3_BXICMS"  , "N3_SEQREAV" , "N3_DEPREC"  , ;
                    "N3_CALCDEP" , "N3_PRODANO" , "N3_PRODMES" , "N3_OK"      , "N3_SEQ"     , "N3_CCDESP"  , "N3_CCCDEP"  , ;
                    "N3_CCCDES"  , "N3_CCCORR"  , "N3_SUBCTA"  , "N3_SUBCCON" , "N3_SUBCDEP" , "N3_SUBCCDE" , "N3_CODBAIX" , ;
                    "N3_FILORIG" , "N3_CLVL"    , "N3_CLVLCON" , "N3_CLVLDEP" , "N3_CLVLCDE" , "N3_CLVLDES" , "N3_CLVLCOR" , ;
                    "N3_IDBAIXA" , "N3_LOCAL"   , "N3_NOVO"    , "N3_QUANTD"  , "N3_PERCBAI" , "N3_NODIA"   , "N3_DIACTB"  , ;
                    "N3_DTACELE" , "N3_RATEIO"  , "N3_CODRAT"  , "N3_CRIDEPR" , "N3_CALDEPR" , "N3_PRODACM" , "N3_PERCDEP" , ;
                    "N3_CODPOOL" , "N3_VLIMPER" , "N3_CODIND"  , "N3_ATFCPR"  , "N3_ATVORIG" , "N3_INTP";
                    }

    //Adiciona campos ao schema
    self:oSchema:aliasToSchema("CT1",aFieldsCT1)
    self:oSchema:aliasToSchema("SN1",aFieldsSN1)
    self:oSchema:aliasToSchema("SN3",aFieldsSN3)

    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0004, "string", .T.) //Filiais
    self:oSchema:addParameter("SV_CLASSPATR", STR0005, "string", .T.) //Classif. Patrimonial  
    
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)
    self:setCustomURL("SV_CLASSPATR", "/api/controladoria/smartview/v1/options/getClassification", 2)
    //Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
@return object: self:oData
@author         
@since          
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class ValuedPositionSmartViewBusinessObject
    
    Local cQuery        as character

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "AFR070" )

    //Carrega o bloco de baixas
    cQuery       := self:getAdiantamentos(oFilter)

    self:cAlias  := MPSysOpenQuery(cQuery)
    self:setAdiantamentos(oFilter)

    (self:cAlias)->( DBCloseArea() )
	//Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())

return self:oData

//-------------------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodos usado na Internacionalização
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method preSchema() as object class ValuedPositionSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Método de instância da classe
@return object: self:oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method preData() as object class ValuedPositionSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método na Chamado no momento do processamento da query
@return object: self:oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method processData() class ValuedPositionSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------------------
/*{Protheus.doc} getAdiantamentos
gera a query de filtragem dos dados 
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method getAdiantamentos(oFilter as object) as character class ValuedPositionSmartViewBusinessObject

    Local cDelete := space(1)  as character
    Local cQuery               as character
    Local cTipoN3     := "33"  as character
    Local cN3ATFCPR   := " ,2" as character
    Local aClassPat   := {}    as array
    local aSelFilCT1           as array
    Local oJParams             as object
    Local cBaixa      := "0"   as character
    Local nParamOrder := 1     as numeric
    local __nTamFil            as numeric
    local nCount               as numeric
    Local dUltDepr    := GetNewPar("MV_ULTDEPR","19800101")    as date

    oJParams    := oFilter:GetParameters()
    aClassPat   := ojParams['SV_CLASSPATR']

    ::oIntegratedProvider := ControlIntegratedProvider():New()

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
    ::oIntegratedProvider:loadFilterBranches({"SN3"})

    If Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1] 
    Else
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
    EndIf

    if __nTamFil < 1
        __nTamFil := IIf(FWModeAccess("CT1", 01) == "E", Len(FWSM0Layout(, 1)), 0)
        __nTamFil += IIf(FWModeAccess("CT1", 02) == "E", Len(FWSM0Layout(, 2)), 0)
        __nTamFil += IIf(FWModeAccess("CT1", 03) == "E", Len(FWSM0Layout(, 3)), 0)
    EndIf

    aSelFilCT1 := {}
    for nCount := 1 to len(::aSelFil)
        aAdd(aSelFilCT1, Alltrim(SUBSTR(::aSelFil[nCount],1,__nTamFil)))
    next nCount

    //Campos que farão parte do select da query
    //Seleção de dados para processamento
    cQuery := " SELECT  " + self:getSQLFields(.F., , .F.)

    cQuery += " FROM " + RetSQLName("SN1") + " SN1 "
    cQuery += "," + RetSQLName("SN3") + " SN3 "

    //Adiciona Filial corrente
    cQuery += " LEFT JOIN " + RetSQLName("CT1") + " CT1 "
    cQuery += "   ON CT1.D_E_L_E_T_  = ? "
    cQuery += "  AND CT1.CT1_FILIAL  IN ( ? ) "
    cQuery += "  AND CT1.CT1_CONTA   = SN3.N3_CCONTAB " 

    cQuery += " WHERE SN3.D_E_L_E_T_  = ? "
    cQuery += "   AND SN1.D_E_L_E_T_  = ? "
    cQuery += "   AND SN3.N3_FILIAL   IN ( ? ) "
	cQuery	+= "  AND SN3.N3_CCONTAB  BETWEEN ? AND ? "
	cQuery	+= "  AND SN3.N3_CCUSTO   BETWEEN ? AND ? "
    cQuery += "   AND SN3.N3_TIPO     NOT IN ? "

    // 10,Quanto as Baixas ?            (Todas/Nenhuma/Ate Calc./Apos Calc./Mês Calc.)
    // 14,Imprime Total de Baixas ?     (Sim/Não)
	If MV_PAR10 == 2 .And. MV_PAR14 <> 1        // Nao considera nenhuma baixa e não imprime baixas
        cQuery += "   AND SN3.N3_BAIXA     = ? "
	Elseif MV_PAR10 == 3                        // Baixas ate o calculo
        cQuery += "   AND SN3.N3_BAIXA    <> ? "
        cQuery += "   AND SN3.N3_DTBAIXA  <> ? "
        cQuery += "   AND SN3.N3_DTBAIXA   > ? "
	Elseif MV_PAR10 == 4                        // Baixas apos o calculo
        cQuery += "   AND SN3.N3_BAIXA    <> ? "
        cQuery += "   AND SN3.N3_DTBAIXA  <> ? "
        cQuery += "   AND SN3.N3_DTBAIXA  <= ? "
	Elseif MV_PAR10 == 5                        // Mes do Calculo
        cQuery += "   AND SN3.N3_BAIXA    <> ? "
        cQuery += "   AND MONTH(SN3.N3_DTBAIXA)  = ? "
        cQuery += "   AND YEAR(SN3.N3_DTBAIXA)   = ? "
	EndIf

    // 17,Exibir Ativos Real. Provis ? (S/N)
    If MV_PAR17 == 2
        cQuery += "   AND SN3.N3_ATFCPR   IN ? "
    EndIf

    cQuery += "AND SN1.N1_FILIAL = SN3.N3_FILIAL "
    cQuery += "AND SN1.N1_CBASE = SN3.N3_CBASE "
    cQuery += "AND SN1.N1_ITEM = SN3.N3_ITEM "
    
    If Len(aClassPat) > 0 .And. !(Empty(aClassPat[1]))
        cQuery += "AND N1_PATRIM IN ( ? ) "
    EndIf

    //Os filtros serão setados na interface do novo TReports    
    cQuery += Iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(),"")

    //Define o campo de ordenação
    cQuery += " ORDER BY N3_CBASE "

    //Objeto executa o comando SQL inúmeras vezes.
    self:oQuery:= FWPreparedStatement():New(ChangeQuery(cQuery))


    self:oQuery:SetString(nParamOrder++, cDelete)
    self:oQuery:SetIn(nParamOrder++, iif(empty(aSelFilCT1), {FWXFilial("CT1")}, aSelFilCT1))
    self:oQuery:SetString(nParamOrder++, cDelete)
    self:oQuery:SetString(nParamOrder++, cDelete)
    self:oQuery:SetIn(nParamOrder++, iif(empty(::aSelFil), {FWXFilial("SN3")}, ::aSelFil))
    self:oQuery:SetString(nParamOrder++, MV_PAR01)
    self:oQuery:SetString(nParamOrder++, MV_PAR02)
    self:oQuery:SetString(nParamOrder++, MV_PAR03)
    self:oQuery:SetString(nParamOrder++, MV_PAR04)
    self:oQuery:SetUnsafe(nParamOrder++, FormatIn(cTipoN3, ","))
    If MV_PAR10 == 2 .And. MV_PAR14 <> 1        // Nao considera nenhuma baixa e não imprime baixas
        self:oQuery:SetString(nParamOrder++, cBaixa)
	Elseif MV_PAR10 == 3                        // Baixas ate o calculo
        self:oQuery:SetString(nParamOrder++, cBaixa)
        self:oQuery:SetString(nParamOrder++, Space(1))
        self:oQuery:SetUnsafe(nParamOrder++, ValToSQL(dUltDepr))
	Elseif MV_PAR10 == 4                        // Baixas apos o calculo
        self:oQuery:SetString(nParamOrder++, cBaixa)
        self:oQuery:SetString(nParamOrder++, Space(1))
        self:oQuery:SetUnsafe(nParamOrder++, ValToSQL(dUltDepr))
	Elseif MV_PAR10 == 5                        // Mes do Calculo
        self:oQuery:SetString(nParamOrder++, cBaixa)
        self:oQuery:SetString(nParamOrder++, StrZero(Month(dUltDepr),2))
        self:oQuery:SetString(nParamOrder++, StrZero(Year(dUltDepr),4))
	EndIf

    If MV_PAR17 == 2
        self:oQuery:SetUnsafe(nParamOrder++, FormatIn(cN3ATFCPR, ","))
    EndIf

    If Len(aClassPat) > 0 .And. !(Empty(aClassPat[1]))
        self:oQuery:SetIn(nParamOrder++, aClassPat)
    EndIf

    //Retorna query com parâmetros tratados e substituídos.
    cQuery := self:oQuery:GetFixQuery()

return cQuery

//-------------------------------------------------------------------------------
/*{Protheus.doc} setAdiantamentos
seta os dados filtrados no objeto oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method setAdiantamentos(oFilter as object) as object class ValuedPositionSmartViewBusinessObject

    local nX := 0 as numeric

    //Posiciona no registro inicial
    (self:cAlias)->(dbGoTop())

    While !(self:cAlias)->(Eof())
        
        self:jDados := JsonObject():new()

        //Popula campos da query
        For nX := 1 To Len(self:aAllFields)
            if self:aAllFields[nX]:getType() == "date"
                self:jDados[self:aAllFields[nX]:getName()] := iif(!empty((self:cAlias)->&(self:aAllFields[nX]:getRealName())), FwTimeStamp(5, StoD((self:cAlias)->&(self:aAllFields[nX]:getRealName())), "00:00:00"), nil)
            elseif self:aAllFields[nX]:getType() == "string"
                self:jDados[self:aAllFields[nX]:getName()] := ALLTRIM((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
            else
                self:jDados[self:aAllFields[nX]:getName()] := (self:cAlias)->&(self:aAllFields[nX]:getRealName())
            endif
		Next nX

        self:processData()
        self:oData:appendData(self:jDados)

        (self:cAlias)->(dbSkip())

    EndDo

return self:oData
