#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.assetsheet.bra.ch"

namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1,SN2,SN3,SN4,SA2", name="Ficha do Ativo", country="ALL", customTables="SN1,SN2,SN3,SN4,SA2")

//-------------------------------------------------------------------------------
/*{Protheus.doc} CentrodeCustoTReportsBusinessObject
classe para criação do Objeto de Negócio de Ficha do Ativo para o TReports
 
@author Renan Gremes
@since 02/06/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class AssetSheetSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()             as object
    public method getDescription()  as character
    public method getAreas()        as array
    public method getData()         as object
    public method getSchema()       as object

    //usados na internacionalização 
    public method preSchema()       as object 
    public method preData()         as object
    public method processData()     as json

	protected method getQuery()     as character
    protected method setData()      as object
    protected method calcResValue() as array

    protected data cBasede       as character
    protected data cBaseAte      as character
    protected data cItemde       as character
    protected data cItemAte      as character
    protected data cCustode      as character
    protected data cCustoAte     as character
    protected data cContade      as character
    protected data cContaAte     as character
    protected data cPenhora      as character
    protected data cFilDe        as character
    protected data cFilAte       as character
    protected data cCompanyName  as character
    protected data cBranchName   as character
    protected data lConsidBx     as logical
    protected data lConsidFil    as logical
    protected data lExAtRePr     as logical

    protected data cAlias        as character
    protected data dUltDepr      as date
    protected data oQuery        as object
    Protected data aAllFields    as array
    protected data aSelFil       as array
    protected data jItens        as json
    protected data jDados        as json
    protected data oIntegratedProvider as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 02/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class AssetSheetSmartViewBusinessObject    
    _Super:new()
    self:setDisplayName(STR0001) // "Ficha do Ativo"

    self:setPergunte("AFR030")  // Indica o pergunte que será utilizado no relatório

    ::dUltDepr   := SuperGetMV("MV_ULTDEPR")
    ::aSelFil    := {}
    ::aAllFields := {}
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 02/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getDescription() as character class AssetSheetSmartViewBusinessObject
return  STR0002 //"Objeto contendo informações da Ficha do Ativo Fixo"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 02/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class AssetSheetSmartViewBusinessObject
Return {STR0003} //"Ativo Fixo"

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 02/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class AssetSheetSmartViewBusinessObject

    local cQuery as character    

    //Carrega lista de parametros para a memória
	CTBsetValueMVPAR(oFilter:getParameters(), "AFR030")

	::cBaseDe     := MV_PAR01                       // Do Codigo ?
    ::cBaseAte    := MV_PAR02                       // Ate Codigo ?
    ::cItemde     := MV_PAR03                       // Do Item ?
    ::cItemAte    := MV_PAR04                       // Até Item ?
    ::cCustoDe    := MV_PAR05                       // Do Centro de Custo ?
    ::cCustoAte   := MV_PAR06                       // Ate Centro Custo ?
    ::cContaDe    := MV_PAR07                       // Da Conta Contabil ? 
    ::cContaAte   := MV_PAR08                       // Ate Conta Contabil ? 
    ::lConsidBx   := iif(MV_PAR14 == 1, .T., .F.)   // Considera Baixados ? 1=Sim;2=Não
    ::lConsidFil  := iif(MV_PAR18 == 1, .T., .F.)   // Cons Filiais Abaixo ? 1=Sim;2=Não
    ::cFilDe      := MV_PAR19                       // Filial De ?
    ::cFilAte     := MV_PAR20                       // Filial Ate ?
    ::cPenhora    := MV_PAR21                       // Sit. Penhora ?
    ::lExAtRePr   := iif(MV_PAR23 == 1, .T., .F.)   // Exibir Ativos Real. Provis ? 1=Sim;2=Não

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    ::cCompanyName := ::oIntegratedProvider:getCompanyName()
    ::cBranchName  := ::oIntegratedProvider:getBranchName()

    //Seleção de filiais
    If !self:lConsidFil
        ::oIntegratedProvider:lMultSelectBranches := .T.
        ::oIntegratedProvider:lAllowBranchFilter  := .T.
        ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
        ::oIntegratedProvider:loadFilterBranches({"SN3"})

        If Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
            ::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1]
        Else
            ::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
        EndIf
    EndIf

    cQuery := self:getQuery(oFilter)

    self:cAlias := MPSysOpenQuery(cQuery)
    self:setData(oFilter)

    (self:cAlias)->(dbCloseArea())

    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())
    FreeObj(::oIntegratedProvider)
    
return self:oData

//-------------------------------------------------------------------------------
/*{Protheus.doc} setData
    Seta os dados filtrados no objeto oData

    @author Renan Gremes
    @since 28/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------------------

method setData(oFilter as object) as object class AssetSheetSmartViewBusinessObject
    
    local aResValues as array
    local nX         as numeric

    //Posiciona no registro inicial
    (self:cAlias)->(DBGoTop())

    While !(self:cAlias)->(Eof())
        //Adiciona espacos em branco, pois possui bens normais sem preenchimento no campo de penhora.
        if empty( (self:cAlias)->N1_PENHORA ) .and. !((self:cAlias)->N1_PENHORA $ AllTrim( ::cPenhora ))
            ::cPenhora := AllTrim( ::cPenhora ) + (self:cAlias)->N1_PENHORA
        endif

        //Verifica se a penhora esta informada no parâmetro para imprimir
        if (self:cAlias)->N1_PENHORA $ ::cPenhora
            ::jItens := JsonObject():new()

            For nX := 1 To Len(self:aAllFields)
                if self:aAllFields[nX]:getType() == "date"
                    ::jItens[self:aAllFields[nX]:getName()] := iif(!empty((self:cAlias)->&(self:aAllFields[nX]:getRealName())), FwTimeStamp(5, StoD((self:cAlias)->&(self:aAllFields[nX]:getRealName())), "00:00:00"), nil)
                elseif self:aAllFields[nX]:getType() == "string"
                    ::jItens[self:aAllFields[nX]:getName()] := AllTrim((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
                else
                    ::jItens[self:aAllFields[nX]:getName()] := (self:cAlias)->&(self:aAllFields[nX]:getRealName())
                endif
		    Next nX

            //Adiciona data da última depreciação ao json
            ::jItens["MV_ULTDEPR"] := iif(!empty(::dUltDepr), FwTimeStamp(5, ::dUltDepr, "00:00:00"), nil)

            //Adiciona descrição da empresa e filial
            ::jItens["NomeEmpresa"] := ::cCompanyName
            ::jItens["NomeFilial" ] := ::cBranchName
            
            //Calcula os valores residuais
            aResValues := {}
            aResValues := self:calcResValue(::dUltDepr)
            
            //Adiciona valores residuais de cada moeda ao json
            ::jItens["VALRESID1"] := aResValues[1] // Moeda1
            ::jItens["VALRESID2"] := aResValues[2] // Moeda2
            ::jItens["VALRESID3"] := aResValues[3] // Moeda3
            ::jItens["VALRESID4"] := aResValues[4] // Moeda4
            ::jItens["VALRESID5"] := aResValues[5] // Moeda5
            
            self:processData()
            self:oData:appendData(::jItens)
        endif

        (self:cAlias)->(dbSkip())
        
    EndDo

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getStruct
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Renan Gremes
@since 02/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class AssetSheetSmartViewBusinessObject    
    
    local aFieldsSN1 as array
    local aFieldsSN2 as array
    local aFieldsSN3 as array
    local aFieldsSN4 as array
    local aFieldsSX5 as array
    local aFieldsSA2 as array
    local lCanFilter := .F. as logical

	aFieldsSN1 := { "N1_CBASE", "N1_ITEM", "N1_DESCRIC", "N1_LOCAL", "N1_GRUPO", "N1_AQUISIC", "N1_BAIXA", "N1_PROJETO", "N1_NFISCAL",;
                    "N1_NSERIE", "N1_QUANTD", "N1_PLACA", "N1_CSEGURO", "N1_APOLICE", "N1_DTVENC", "N1_TIPOSEG", "N1_FORNEC", "N1_LOJA",;
                    "N1_PENHORA", "N1_CHAPA" }

	aFieldsSN2 := { "N2_HISTOR" }

	aFieldsSN3 := { "N3_HISTOR", "N3_CCONTAB", "N3_CCORREC", "N3_CDEPREC", "N3_CCDEPR", "N3_CDESP", "N3_CCUSTO", "N3_DINDEPR", "N3_DTBAIXA",;
                    "N3_CODIND", "N3_VRCMES1", "N3_VRDMES1", "N3_VRDMES2", "N3_VRDMES3", "N3_VRDMES4", "N3_VRDMES5", "N3_VRCDM1", "N3_VRCBAL1",;
                    "N3_VRDBAL1", "N3_VRDBAL2", "N3_INDICE1", "N3_INDICE2", "N3_INDICE3", "N3_INDICE4", "N3_INDICE5", "N3_VRDBAL3", "N3_VRDBAL4",; 
                    "N3_VRDBAL5", "N3_VRCDB1", "N3_VRCACM1", "N3_VRDACM1", "N3_VRDACM2", "N3_VRDACM3", "N3_VRDACM4", "N3_VRDACM5", "N3_VRCDA1",;
                    "N3_VORIG1", "N3_VORIG2", "N3_VORIG3", "N3_VORIG4", "N3_VORIG5", "N3_TIPO", "N3_TXDEPR1", "N3_TXDEPR2", "N3_TXDEPR3", "N3_TXDEPR4",; 
                    "N3_TXDEPR5", "N3_AMPLIA1", "N3_AMPLIA2", "N3_AMPLIA3", "N3_AMPLIA4", "N3_AMPLIA5" }

	aFieldsSN4 := { "N4_QUANTD" }

	aFieldsSX5 := { "X5_DESCRI" }

	aFieldsSA2 := { "A2_COD", "A2_LOJA", "A2_NOME" }
   
	//Adiciona campos ao schema
    self:oSchema:aliasToSchema("SN1", aFieldsSN1)
    self:oSchema:aliasToSchema("SN2", aFieldsSN2)
	self:oSchema:aliasToSchema("SN3", aFieldsSN3)
	self:oSchema:aliasToSchema("SN4", aFieldsSN4)
	self:oSchema:aliasToSchema("SX5", aFieldsSX5)
	self:oSchema:aliasToSchema("SA2", aFieldsSA2)

    //Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields() 

    //Adiciona campos customizados ao schema
    self:oSchema:addProperty( "MV_ULTDEPR" , STR0004  , "date"   , STR0004 , "MV_ULTDEPR" ,,,,lCanFilter)
    self:oSchema:addProperty( "VALRESID1"  , STR0005  , "number" , STR0005 , "VALRESID1"  ,,,,lCanFilter)
    self:oSchema:addProperty( "VALRESID2"  , STR0006  , "number" , STR0006 , "VALRESID2"  ,,,,lCanFilter)
    self:oSchema:addProperty( "VALRESID3"  , STR0007  , "number" , STR0007 , "VALRESID3"  ,,,,lCanFilter)
    self:oSchema:addProperty( "VALRESID4"  , STR0008  , "number" , STR0008 , "VALRESID4"  ,,,,lCanFilter)
    self:oSchema:addProperty( "VALRESID5"  , STR0009  , "number" , STR0009 , "VALRESID5"  ,,,,lCanFilter)
    self:oSchema:addProperty( "NomeEmpresa", STR0010  , "string" , STR0010 , "NomeEmpresa",,,,lCanFilter) // Nome Empresa
    self:oSchema:addProperty( "NomeFilial" , STR0011  , "string" , STR0011 , "NomeFilial" ,,,,lCanFilter) // Nome Filial

    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0012, "string", .T.) //Filiais
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.

@return object: self:oSchema

@author Renan Gremes
@since 28/12/2023
@version 1.0
/*/
//-------------------------------------------------------------------

method preSchema() as object class AssetSheetSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.

@return object: self:oData

@author Renan Gremes
@since 28/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method preData() as object class AssetSheetSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método chamado antes do appendData

@return object: self:oData

@author Renan Gremes
@since 28/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method processData() class AssetSheetSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.

    @author Renan Gremes
    @since 28/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method getQuery(oFilter as object) as character class AssetSheetSmartViewBusinessObject
	
    local cSpace := space(1) as character
    local cQuery    as character
    local cFields   as character
    local cGroup    as character
    local nParamOrder := 1 as numeric

    //Campos que farão parte do select da query
    cFields := self:getSQLFields(.F., , .F.)
    cFields := StrTran(cFields, "N1_QUANTD", "SUM(N1_QUANTD) N1_QUANTD")
    cFields := StrTran(cFields, "N4_QUANTD", "SUM(N4_QUANTD) N4_QUANTD")    

    //Select
    cQuery := " SELECT "
    cQuery += cFields
    cQuery += " FROM "+RetSqlName("SN1")+" SN1 "
    
    cQuery += " INNER JOIN "+RetSqlName("SN3")+" SN3 ON SN1.N1_FILIAL = SN3.N3_FILIAL " 
    cQuery += " AND SN1.N1_CBASE = SN3.N3_CBASE " 
    cQuery += " AND SN1.N1_ITEM = SN3.N3_ITEM "
    cQuery += " AND SN1.D_E_L_E_T_ = ? " 
    
    cQuery += " LEFT JOIN "+RetSqlName("SN4")+" SN4 ON SN4.N4_FILIAL = SN3.N3_FILIAL " 
    cQuery += " AND SN4.N4_CBASE = SN3.N3_CBASE " 
    cQuery += " AND SN4.N4_ITEM = SN3.N3_ITEM " 
    cQuery += " AND SN4.N4_TIPO = SN3.N3_TIPO " 
    cQuery += " AND SN4.N4_DATA = SN3.N3_DTBAIXA " 
    cQuery += " AND SN4.N4_SEQ = SN3.N3_SEQ "  
    cQuery += " AND SN4.D_E_L_E_T_ = ? " 
    
    cQuery += " LEFT JOIN "+RetSqlName("SN2")+" SN2 ON SN2.N2_FILIAL = SN3.N3_FILIAL " 
    cQuery += " AND SN2.N2_CBASE = SN3.N3_CBASE " 
    cQuery += " AND SN2.N2_ITEM = SN3.N3_ITEM "
    cQuery += " AND SN2.D_E_L_E_T_ = ? " 
    
    cQuery += " LEFT JOIN "+RetSqlName("SA2")+" SA2 ON SA2.A2_FILIAL = ? " 
    cQuery += " AND SA2.A2_COD  = SN1.N1_FORNEC " 
    cQuery += " AND SA2.A2_LOJA = SN1.N1_LOJA " 
    cQuery += " AND SA2.D_E_L_E_T_ = ? " 
    
    cQuery += " LEFT JOIN "+RetSqlName("SX5")+" SX5 ON SX5.X5_TABELA = ? "
    cQuery += " AND SN3.N3_TIPO = X5_CHAVE "
    cQuery += " AND SX5.D_E_L_E_T_= ? "
    
    //Filtro da consulta
    cQuery += " WHERE "

    if self:lConsidFil
        cQuery += " SN1.N1_FILIAL BETWEEN ? AND ? "
        cQuery += " AND SN3.N3_FILIAL BETWEEN ? AND ? "
	else
        if !Empty(self:aSelFil)
            cQuery += " SN1.N1_FILIAL IN ( ? ) "
            cQuery += " AND SN3.N3_FILIAL IN ( ? ) "
        else
            cQuery += " SN1.N1_FILIAL =  ? "
        endif
	endif

    cQuery += " AND SN3.N3_CBASE BETWEEN ? AND ? "
    cQuery += " AND SN3.N3_ITEM BETWEEN ? AND ? "
    cQuery += " AND SN3.N3_CCONTAB BETWEEN ? AND ? "
    cQuery += " AND SN3.N3_CCUSTO BETWEEN ? AND ? "

    if !self:lConsidBx
        cQuery += " AND SN3.N3_BAIXA = ? "
    endif

    cQuery += " AND SN3.N3_CCONTAB <> ? "

    if !self:lExAtRePr
		cQuery += " AND SN3.N3_ATFCPR <> ? "
	endif

    cQuery += " AND SN3.D_E_L_E_T_ = ? "
    
    //Os filtros serão setados na interface do novo TReports
    cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")    

    //Tratamento para agrupamento (group by)
    cGroup := self:getSQLFields(.F., , .F.)
    cGroup := StrTran(cGroup, "N1_QUANTD,", "")
    cGroup := StrTran(cGroup, "N4_QUANTD,", "")
    
    cQuery  += " GROUP BY "+cGroup

    cQuery  += " ORDER BY SN1.N1_CBASE, SN1.N1_ITEM"	
	
	self:oQuery:= FWExecStatement():New(ChangeQuery(cQuery))
    
    self:oQuery:SetString(nParamOrder++, cSpace)
    self:oQuery:SetString(nParamOrder++, cSpace)
    self:oQuery:SetString(nParamOrder++, cSpace)
    self:oQuery:SetString(nParamOrder++, FWxFilial("SA2"))
    self:oQuery:SetString(nParamOrder++, cSpace)
    self:oQuery:SetString(nParamOrder++, "G1")
    self:oQuery:SetString(nParamOrder++, cSpace)

    if self:lConsidFil      
        self:oQuery:SetString(nParamOrder++, ::cFilDe )
        self:oQuery:SetString(nParamOrder++, ::cFilAte)
        self:oQuery:SetString(nParamOrder++, ::cFilDe )
        self:oQuery:SetString(nParamOrder++, ::cFilAte)
    else
        if !Empty(self:aSelFil)
            self:oQuery:SetIn(nParamOrder++, ::aSelFil)
            self:oQuery:SetIn(nParamOrder++, ::aSelFil)  
        else
            self:oQuery:SetString(nParamOrder++, FWxFilial("SN1"))
        endif            
    endif

    self:oQuery:SetString(nParamOrder++, ::cBasede)
    self:oQuery:SetString(nParamOrder++, ::cBaseAte)
    self:oQuery:SetString(nParamOrder++, ::cItemde)
    self:oQuery:SetString(nParamOrder++, ::cItemAte)
    self:oQuery:SetString(nParamOrder++, ::cContaDe)
    self:oQuery:SetString(nParamOrder++, ::cContaAte)
    self:oQuery:SetString(nParamOrder++, ::cCustoDe)
    self:oQuery:SetString(nParamOrder++, ::cCustoAte)

    if !self:lConsidBx
        self:oQuery:SetUnsafe(nParamOrder++, "01")
    endif

    self:oQuery:SetString(nParamOrder++, cSpace)

    if !self:lExAtRePr
        self:oQuery:SetUnsafe(nParamOrder++, "1")
	endif
    
    self:oQuery:SetString(nParamOrder++, cSpace)
	
	cQuery := self:oQuery:GetFixQuery()

return cQuery

//-------------------------------------------------------------------
/*{Protheus.doc} calcResValue
Retorna array com cálculo do valor residual
 
@return array: Array com os cálculos de valores residuais por moeda
 
@author Renan Gremes
@since 03/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method calcResValue() as array class AssetSheetSmartViewBusinessObject
    
    local aResValue := Array(5) as array
    local cMoeda    as character
    local dDtDepr   as date
    local dDtIndepr as date
    local nMoeda    as numeric

    dDtIndepr := StoD( (self:cAlias)->N3_DINDEPR )

    //Valida qual data que irá carregar no cálculo
    if (MesAnoAtf( dDtIndepr) <> MesAnoAtf( ::dUltDepr )) .and. (MesAnoAtf( dDtIndepr ) <> MesAnoAtf( ::dUltDepr+1 ))        
        dDtDepr := ::dUltDepr
    else
        dDtDepr := dDtIndepr
    endif

    //Calcula moeda 1
    aResValue[1] := ((self:cAlias)->N3_VORIG1+(self:cAlias)->N3_VRCACM1+(self:cAlias)->N3_AMPLIA1)-((self:cAlias)->N3_VRDACM1+(self:cAlias)->N3_VRCDA1)			
	
    //Calcula moedas 2 a 5
    For nMoeda := 2 To 5
        cMoeda := cValToChar(nMoeda)
        aResValue[nMoeda] := ((self:cAlias)->&("N3_VORIG"+cMoeda) + (self:cAlias)->N3_VRCACM1 + (self:cAlias)->&("N3_AMPLIA"+cMoeda))-( (self:cAlias)->&("N3_VRDACM"+cMoeda) + xMoeda((self:cAlias)->N3_VRCDA1, 1, nMoeda, dDtDepr) )
	Next nMoeda

return aResValue
