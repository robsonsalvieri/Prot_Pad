#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.transactionsxasset.bra.ch"

namespace totvs.protheus.backoffice.sv.atf.transactionsxasset.treportsintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1,SN3,SN4", name="Movimentos por Bem", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} TransactionsxAssetSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Cadastro Ativos para o SmartView
 
@author Bruno Oliveira (B.0)
@since 18/10/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class TransactionsxAssetSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()             as object
    public method getDescription()  as character
    public method getAreas()        as array
    public method getData()         as object
    public method getSchema()       as object

    //usados na internacionalização
    public method preschema()       as object 
    public method preData()         as object
    public method processData()     as json

    protected method getQuerySN3()  as character
    protected method getQuerySN4()  as character
    protected method GetTPMOV()
    protected method AtfGetMtvo()   as character
    
    protected data jItens               as json
    protected data aAllFields           as array
    protected data oIntegratedProvider  as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Bruno Oliveira (B.O)
@since 18/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method new() as object Class TransactionsxAssetSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Movimentos por Bem"
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira (B.O)
@since 18/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getDescription() as character class TransactionsxAssetSmartViewBusinessObject
return STR0002 //"Objeto contendo informações Cadastro de Ativos"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira (B.O)
@since 18/10/2023
@version 1.0
*/
//-------------------------------------------------------------------

Method getAreas() as array class TransactionsxAssetSmartViewBusinessObject
Return {STR0003} //"Ativo Fixo"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preSchema() as object class TransactionsxAssetSmartViewBusinessObject

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preData() as object class TransactionsxAssetSmartViewBusinessObject
    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 20/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method processData() as json class TransactionsxAssetSmartViewBusinessObject

return ::jItens

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 

 
@return object: self:oData
 
@author Bruno Oliveira (B.O)
@since 18/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class TransactionsxAssetSmartViewBusinessObject

    local cQuery            as character
    local cAliasSN3 := ""   as character
    local cAliasSN4 := ""   as character
    local cOcorre   := ""   as character
    local cMotivo   := ""   as character
    local cCodigo   := ""   as character
    local cTxtCC    := ""   as character
    local cTxtIT    := ""   as character
    local cTxtCL    := ""   as character
    local cId               as character
    local cRealName         as character
    local cCompanyName      as character
    local cBranchName       as character
    local cDescMoeda1       as character
    local cDescMoedaSel     as character
    local cConta            as character
    local cContaAnt         as character
    local cSeq              as character
    local cTipoCnt          as character
    local nX       := 0     as numeric
    local nPosicao := 0     as numeric
    local nDet     := 0     as numeric
    local aMotivo    := {}  as array
    local aTipoOco   := {}  as array
    local aSX5       := {}  as array
    local aMovimento := {}  as array
    local aTotMov    := {}  as array
    local aCodigo    := {}  as array
    local aMovSN4    := {}  as array
    local lAtfCusPrv := AFXAtCsPrv() as logical
    local lTransfDe  := .F. as logical
    local lTransfPa  := .F. as logical
    local lImprime          as logical
    local jParams    := Nil as json

    Private CBASEINI  := "" as character
    Private CBASEFIM  := "" as character
    Private CCONTAINI := "" as character
    Private CCONTAFIM := "" as character
    Private DDATAINI        as date
    Private DDATAFIM        as date
    Private NBEMRPROV := 0  as numeric
    Private NORDEMMOV := 0  as numeric 
    Private NMOEDA    := 1  as numeric
    Private NORDEM    := 1  as numeric
    Private NEXIBECC  := 1  as numeric
    Private NEXIBEITEM := 1 as numeric
    Private NEXIBECLVL := 1 as numeric

    jParams := oFilter:getParameters() //Metodo para retorno do json dos parâmetros
    CTBsetValueMVPAR(jParams, "")      //Carrega lista de parametros para a memória
    
    ::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName    := ::oIntegratedProvider:getCompanyName()
    cBranchName     := ::oIntegratedProvider:getBranchName()

    //Pega a tabela 16 da SX5
    aSX5 := FWGetSX5("16") //[1] FILIAL - [2] TABELA - [3] CHAVE - [4] DESCRICAO

    For nX := 1 To Len(aSX5)
        AAdd( aMotivo, SubStr(aSX5[nX][3],1,2 ) + "-" + SubStr(Capital(aSX5[nX][4]),1,12 ) )
    Next nX

    self:GetTPMOV(aTipoOco)

    For nX := 1 to Len(aTipoOco)
        aAdd(aMovimento, {0, 0, Nil, Nil})  // { Vlr em R$, Vlr M Forte, Desc Movimento}
        aAdd(aTotMov,    {0, 0})       // { Total na M1, Total M Forte }
    Next nX

    //Trata descrição moedas
    cDescMoeda1   := SuperGetMV("MV_MOEDA1")
    cDescMoedaSel := SuperGetMV("MV_MOEDA"+Str(NMOEDA+1,1))

    cQuery := self:getQuerySN3(oFilter)
    cAliasSN3 := MPSysOpenQuery(cQuery)

    dbSelectArea("SN1")
    dbSelectArea("SN3")

    While !(cAliasSN3)->(Eof())
        SN1->(DbGoTo((cAliasSN3)->N1RECNO))
        SN3->(DbGoTo((cAliasSN3)->N3RECNO))
        cConta := SN3->N3_CCONTAB

        //NÃO mostra ativo Custo/Provisao
        If lAtfCusPrv .and. NBEMRPROV == 2 .And. SN3->N3_ATFCPR == "1"
            (cAliasSN3)->(dbSkip())
            Loop
        Endif

        If cCodigo == SN3->N3_CBASE+SN3->N3_ITEM+SN3->N3_TIPO
            If SN3->N3_TIPO == "02"
                nPosicao := Ascan(aCodigo,{|x| x=SN3->N3_CBASE+SN3->N3_ITEM+SN3->N3_TIPO+SN3->N3_SEQREAV})
                If nPosicao > 0
                    cCodigo := SN3->N3_CBASE+SN3->N3_ITEM+SN3->N3_TIPO
                    (cAliasSN3)->(dbSkip())
                    Loop
                EndIf
            Endif
        EndIf

        If SN3->N3_TIPO == "02"
            aAdd(aCodigo, SN3->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQREAV))
        Endif

        SN1->(dbSetOrder(1))
        SN1->(MsSeek(FWxFilial("SN1")+SN3->N3_CBASE+SN3->N3_ITEM))

        SN4->(dbSetOrder(1))
        cBase    := SN3->N3_CBASE+SN3->N3_ITEM+SN3->N3_TIPO
        cSeq     := SN3->N3_SEQ
        cSeqReav := SN3->N3_SEQREAV
        lTransfDe := .F.
        lTransfPa := .F.
        aMovSN4   := {}

        dbSelectArea("SN4")

        cQuery := self:getQuerySN4(oFilter)
        cAliasSN4 := MPSysOpenQuery(cQuery)
    
        While !(cAliasSN4)->(Eof())

            If cSeqReav != (cAliasSN4)->(N4_SEQREAV)
                (cAliasSN4)->(dbSkip())
                Loop
            EndIf

            If (cAliasSN4)->(N4_SEQ) # (cAliasSN3)->(N3_SEQ)
                (cAliasSN4)->(dbSkip())
                Loop
            EndIf

            SN4->(DbGoTo((cAliasSN4)->N4RECNO))

            cMotivo := self:AtfGetMtvo(aMotivo, SN4->N4_Motivo)

            lImprime := .T.

            If (SN4->N4_OCORR $ ("06|08|10|11|12|13|20" )) .And. SN4->N4_TIPOCNT == "4" // Depreciacao|Correcao Depreciacao|Depreciacao Acelerada|Depreciacao Acelerada|Depreciacao Acelerada|Inventario|Depreciacao Gerencial
                lImprime := .F.
            EndIf

            If SN4->N4_OCORR == "07" .And. SN4->N4_TIPOCNT == "1"		// Correcao
                lImprime := .F.
            EndIf

            cTxtCC	:= STR0044  //  "C. Custo:  "
			cTxtIT	:= STR0045	//	"Item Conta:"
			cTxtCL	:= STR0046	//	"Cl.Valor:  "

            If SN4->N4_OCORR == "03"
				cTxtCC	:= STR0047  //  "C.Custo de "
				cTxtIT	:= STR0048	//	"Item de:   "
				cTxtCL	:= STR0049	//	"Cl.Vlr de: "
			ElseIf SN4->N4_OCORR == "04"
				cTxtCC	:= STR0050  //  "p/ C.Custo:"
				cTxtIT	:= STR0051	//	"para Item: "
				cTxtCL	:= STR0052	//	"p/ Cl.Vlr: "
			EndIf

            If lImprime
                cOcorre := aTipoOco[Val(SN4->N4_OCORR)] + IIF(SN4->N4_OCORR $ "03/04",Subst(SN4->N4_CONTA,1,20)," ")
               
			    //TRATAMENTO PARA IMPRIMIR OS REGISTROS ORIGEM E DESTINO NA SEQUENCIA EM QUE OCORRERAM
			    If NORDEMMOV == 2
			    	aAdd(aMovSN4,{SN4->N4_TIPOCNT+STR(SN4->(Recno()),30)+SN4->N4_OCORR,SN4->N4_DATA,cOcorre,cMotivo,SN4->N4_VLROC1,SN4->&("N4_VLROC"+Str(NMOEDA+1,1)),SN4->N4_QUANTD,SN4->N4_TXMEDIA,SN4->N4_TXDEPR,cTxtCC+SN4->N4_CCUSTO,cTxtIT+SN4->N4_SUBCTA,cTxtCL+SN4->N4_CLVL})
			    Else
                    ::jItens := JsonObject():New()

                    For nX := 1 to len(self:aAllFields)
                        cId := self:aAllFields[nX]:getName()
                        cRealName := self:aAllFields[nX]:getRealName()
                        If cId == "N4_MOTIVO" 
                            ::jItens[cId] := cMotivo
                        ElseIf cId == "N4_CCUSTO" .And. NEXIBECC == 1
                            ::jItens[cId] := (cAliasSN4)->&(cRealName)
                        ElseIf cId == "N4_SUBCTA" .And. NEXIBEITEM == 1
                            ::jItens[cId] := (cAliasSN4)->&(cRealName)
                        ElseIf cId == "N4_CLVL" .And. NEXIBECLVL == 1
                            ::jItens[cId] := (cAliasSN4)->&(cRealName)
                        ElseIf cId $ "N3_CBASE|N3_ITEM|N3_TIPO|N1_DESCRIC"
                            ::jItens[cId] := AllTrim((cAliasSN3)->&(cRealName))
                        ElseIf self:aAllFields[nX]:getType() == "date"
                            ::jItens[cId] := totvs.framework.treports.date.stringToTimeStamp((cAliasSN4)->&(cRealName)) 
                        ElseIf self:aAllFields[nX]:getType() == "string"
                            ::jItens[cId] := AllTrim((cAliasSN4)->&(cRealName)) 
                        Else
                            ::jItens[cId] := (cAliasSN4)->&(cRealName)
                        EndIf
                    Next nX

                    ::jItens["COCORREN"    ] := cOcorre
                    ::jItens["NomeEmpresa" ] := cCompanyName
                    ::jItens["NomeFilial"  ] := cBranchName
                    ::jItens["DescMoeda1"  ] := cDescMoeda1
                    ::jItens["DescMoedaSel"] := cDescMoedaSel
                    ::jItens["lTotalizador"] := .F.

                    self:processData()
                    self:oData:appendData(::jItens)
                EndIf
            EndIf

            If SN4->N4_OCORR == "01" .And. SN4->N4_TIPOCNT == "1"  //Baixa
	    		aMovimento[1][1] += SN4->N4_VLROC1
	    		aMovimento[1][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[1][3] := aTipoOco[1] //" BAIXA"
                aMovimento[1][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "02" .And. SN4->N4_TIPOCNT == "1"  //Substitu
	    		aMovimento[2][1] += SN4->N4_VLROC1
	    		aMovimento[2][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[2][3] := aTipoOco[2]  //"SUBSTITUICAO"
                aMovimento[2][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "03" .And. SN4->N4_TIPOCNT == "1" .And. !lTransfDe //Transf de
	    		lTransfDe := .T.
	    		aMovimento[3][1] += SN4->N4_VLROC1
	    		aMovimento[3][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[3][3] := aTipoOco[3]  //" DEPRECIACAO"
                aMovimento[3][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "04" .And. SN4->N4_TIPOCNT == "1" .And. !lTransfPa  //Transf para
	    		lTransfPa := .T.
	    		aMovimento[4][1] += SN4->N4_VLROC1
	    		aMovimento[4][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[4][3] := aTipoOco[4] //"Transfer para"
                aMovimento[4][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "05" .And. SN4->N4_TIPOCNT="1" .And. SN4->N4_TIPO == "01" //Implantac
	    		aMovimento[5][1] := SN4->N4_VLROC1
	    		aMovimento[5][2] := &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[5][3] := aTipoOco[23]  //"Implantacao"
                aMovimento[5][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "06" .And. SN4->N4_TIPOCNT == "3"  //Depreciac
	    		aMovimento[6][1] += SN4->N4_VLROC1
	    		aMovimento[6][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[6][3] := aTipoOco[24]   //"DEPRECIACAO"
                aMovimento[6][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "07" .And. SN4->N4_TIPOCNT == "2" //Corre Bem
	    		aMovimento[7][1] += SN4->N4_VLROC1
	    		aMovimento[7][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[7][3] := aTipoOco[25] //"Correcao Bem"
                aMovimento[7][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "08" .And. SN4->N4_TIPOCNT == "5"  //Corre Depr
	    		aMovimento[8][1] += SN4->N4_VLROC1
	    		aMovimento[8][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[8][3] := aTipoOco[8] //"Correcao da Depr"
                aMovimento[8][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "09" .And. SN4->N4_TIPOCNT == "1"  //Ampliacao
	    		aMovimento[9][1] += SN4->N4_VLROC1
	    		aMovimento[9][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[9][3] := aTipoOco[9]  //"Ampliacao"
                aMovimento[9][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "10" .And. SN4->N4_TIPOCNT == "3"  //Dep Acel  esp SBPS
	    		aMovimento[10][1] += SN4->N4_VLROC1
	    		aMovimento[10][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[10][3] := aTipoOco[10]   //"Acelera positiva"
                aMovimento[10][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "11" .And. SN4->N4_TIPOCNT == "3" //Dep Acel Espe SBPS
	    		aMovimento[11][1] += SN4->N4_VLROC1
	    		aMovimento[11][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[11][3] := aTipoOco[11]  //"Aceleracao Negativa"
                aMovimento[11][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "12" .And. SN4->N4_TIPOCNT == "3"  //Dep. Incentivada Acelerada
	    		aMovimento[10][1] += SN4->N4_VLROC1
	    		aMovimento[10][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[10][3] := aTipoOco[10]   //"Acelera positiva"
                aMovimento[10][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "13" .And. SN4->N4_TIPOCNT == "4" //Ajuste inv
	    		aMovimento[12][1] += SN4->N4_VLROC1
	    		aMovimento[12][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[12][3] := aTipoOco[13]  //"Ajuste Invent"
                aMovimento[12][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "15" .And. SN4->N4_TIPOCNT == "1"  //Baixa Transf
	    		aMovimento[13][1] += SN4->N4_VLROC1
	    		aMovimento[13][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[13][3] := aTipoOco[15]   //"Baixa Transf"
                aMovimento[13][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "16" .And. SN4->N4_TIPOCNT == "1"  //Aquis Trans
	    		aMovimento[14][1] += SN4->N4_VLROC1
	    		aMovimento[14][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[14][3] := aTipoOco[16]  //"Aquis Transf"
                aMovimento[14][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "20" .And. SN4->N4_TIPOCNT == "3"  //Depr. Gerencial
	    		aMovimento[20][1] += SN4->N4_VLROC1
	    		aMovimento[20][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[20][3] := aTipoOco[20]  //"Depr. Gerencial"
                aMovimento[20][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "21" .And. SN4->N4_TIPOCNT == "1"  //Depr. Gerencial
	    		aMovimento[21][1] += SN4->N4_VLROC1
	    		aMovimento[21][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[21][3] := aTipoOco[21]  //"Taxa Perdida"
                aMovimento[21][4] := SN4->N4_OCORR
	    	ElseIf SN4->N4_OCORR == "22" .And. SN4->N4_TIPOCNT == "1"
	    		aMovimento[22][1] += SN4->N4_VLROC1
	    		aMovimento[22][2] += &('SN4->N4_VLROC'+Str(NMOEDA+1,1))
	    		aMovimento[22][3] := aTipoOco[22]  //"Mais/Menos Valia"
                aMovimento[22][4] := SN4->N4_OCORR
	    	EndIf

            //TRATAMENTO PARA IMPRIMIR OS REGISTROS ORIGEM E DESTINO NA SEQUENCIA EM QUE OCORRERAM
            If NORDEMMOV == 2
                aSort(aMovSN4,,,{|x,y| x[1] < y[1]})
                
                If Len(aMovSN4) > 0
                    cTipoCnt := Left(aMovSN4[1][1],1)
        
                    For nDet := 1 to Len(aMovSN4)
                        If cTipoCnt <> Left(aMovSN4[nDet][1],1)
                            cTipoCnt := Left(aMovSN4[nDet][1],1)
                        EndIf

                        ::jItens := JsonObject():New()

                        ::jItens["N4_FILIAL"   ] := SN3->N3_FILIAL
                        ::jItens["N3_CBASE"    ] := AllTrim(SN3->N3_CBASE)
                        ::jItens["N3_ITEM"     ] := AllTrim(SN3->N3_ITEM)
                        ::jItens["N3_TIPO"     ] := SN3->N3_TIPO
                        ::jItens["N4_DATA"     ] := totvs.framework.treports.date.dateToTimeStamp(aMovSN4[nDet][2])
                        ::jItens["N4_OCORR"    ] := aMovSN4[nDet][3]
                        ::jItens["N4_MOTIVO"   ] := aMovSN4[nDet][4]
                        ::jItens["N4_VLROC1"   ] := aMovSN4[nDet][5]
                        ::jItens["N4_VLROC"+Str(NMOEDA+1,1)] := aMovSN4[nDet][6]
                        ::jItens["N4_QUANTD"   ] := aMovSN4[nDet][7]
                        ::jItens["N4_TXMEDIA"  ] := aMovSN4[nDet][8]
                        ::jItens["N4_TXDEPR"   ] := aMovSN4[nDet][9]

                        If NEXIBECC == 1
                            ::jItens["N4_CCUSTO"] := aMovSN4[nDet][10]
                        EndIf

                        If NEXIBEITEM == 1
                            ::jItens["N4_SUBCTA"] := aMovSN4[nDet][11]
                        EndIf

                        If NEXIBECLVL == 1
                            ::jItens["N4_CLVL"  ] := aMovSN4[nDet][12]
                        EndIf

                        ::jItens["COCORREN"    ] := aMovSN4[nDet][3]
                        ::jItens["NomeEmpresa" ] := cCompanyName
                        ::jItens["NomeFilial"  ] := cBranchName
                        ::jItens["DescMoeda1"  ] := cDescMoeda1
                        ::jItens["DescMoedaSel"] := cDescMoedaSel
                        ::jItens["lTotalizador"] := .F.

                        self:processData()
                        self:oData:appendData(::jItens)
                    Next nDet
                EndIf
            EndIf

            (cAliasSN4)->(dbSkip())
        EndDo

        (cAliasSN4)->(dbCloseArea())

        //Totais por Movimento
        For nX := 1 to 20
            If aMovimento[nX][1] != 0
                ::jItens := JsonObject():New()

                ::jItens["N4_FILIAL"   ] := SN3->N3_FILIAL
                ::jItens["N3_CBASE"    ] := AllTrim(SN3->N3_CBASE)
                ::jItens["N3_ITEM"     ] := AllTrim(SN3->N3_ITEM)
                ::jItens["N3_TIPO"     ] := SN3->N3_TIPO
                ::jItens["N1_DESCRIC"  ] := SN1->N1_DESCRIC
                ::jItens["NomeEmpresa" ] := cCompanyName
                ::jItens["NomeFilial"  ] := cBranchName
                ::jItens["N4_OCORR"    ] := aMovimento[nX][4]
                ::jItens["COCORREN"    ] := aMovimento[nX][3]
                ::jItens["N4_VLROC1"   ] := aMovimento[nX][1]
                ::jItens["N4_VLROC"+Str(NMOEDA+1,1)] := aMovimento[nX][2]
                ::jItens["lTotalizador"] := .T.

                self:oData:appendData(::jItens)

                aTotMov[nX][1] += aMovimento[nX][1]
                aTotMov[nX][2] += aMovimento[nX][2]
                aMovimento[nX][1] := 0
                aMovimento[nX][2] := 0
            EndIf
        Next nX

        dbSelectArea("SN3")
        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ cContab e utilizada qdo a ordem for 2 ( CONTA )    ³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        cCodigo    := SN3->N3_CBASE+SN3->N3_ITEM+SN3->N3_TIPO
        cContaAnt  := SN3->N3_CCONTAB
        SN3->(dbSkip())

        If NORDEM == 2 .And. cContaAnt != SN3->N3_CCONTAB //( cContaAnt!= SN3->N3_CCONTAB .Or. (SN3->(!Eof()) .And. SN3->N3_FILIAL == xFilial("SN3") .And. Eval( bCond ) ) )
            For nX := 1 to Len(aTotMov)
                If aTotMov[nX][1] != 0
                    Exit
                Endif
            Next
       
            //Totais por Movimento
            For nX := 1 to Len(aTotMov)
                If aTotMov[nX][1] != 0
                    ::jItens := JsonObject():New()

                    ::jItens["N4_FILIAL"   ] := SN3->N3_FILIAL
                    ::jItens["N3_CBASE"    ] := AllTrim(SN3->N3_CBASE)
                    ::jItens["N3_ITEM"     ] := AllTrim(SN3->N3_ITEM)
                    ::jItens["N3_TIPO"     ] := SN3->N3_TIPO
                    ::jItens["N1_DESCRIC"  ] := SN1->N1_DESCRIC
                    ::jItens["NomeEmpresa" ] := cCompanyName
                    ::jItens["NomeFilial"  ] := cBranchName
                    ::jItens["N4_OCORR"    ] := aMovimento[nX][4]
                    ::jItens["COCORREN"    ] := aMovimento[nX][3]
                    ::jItens["N4_VLROC1"   ] := aTotMov[nX][1]
                    ::jItens["N4_VLROC"+Str(NMOEDA+1,1)] := aTotMov[nX][2]
                    ::jItens["lTotalizador"] := .T.

                    self:oData:appendData(::jItens)

                    aTotMov[nX][1] := 0
                    aTotMov[nX][2] := 0
                Endif
            Next nX
        ElseIf NORDEM ==1
            For nX := 1 to Len(aTotMov)
                aTotMov[nX][1] := 0
                aTotMov[nX][2] := 0
            Next nX
        EndIf

        (cAliasSN3)->(dbSkip())
    EndDo

    SN1->(dbCloseArea())
    SN3->(dbCloseArea())
    SN4->(dbCloseArea())

    (cAliasSN3)->(dbCloseArea())  

    //Elimina da memória.
    FreeObj(jParams)
    FreeObj(::jItens)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
 
@param aCamposQry array: array com os campos do relatório
 
@return array: array com a estrutura dos campos
 
@author Bruno Oliveira (B.O)
@since 18/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class TransactionsxAssetSmartViewBusinessObject

    local aFieldsSN1 := {}  as array
    local aFieldsSN3 := {}  as array
    local aFieldsSN4 := {}  as array
    local lCanFilter := .F. as logical

    aFieldsSN1 := {"N1_DESCRIC"}

    aFieldsSN3 := {"N3_CBASE", "N3_ITEM", "N3_TIPO"}

    aFieldsSN4 := {"N4_FILIAL", "N4_CLVL",    "N4_CCUSTO",  "N4_SUBCTA",;
                   "N4_CONTA",  "N4_SEQ",     "N4_SEQREAV", "N4_DATA",   "N4_MOTIVO",;
                   "N4_QUANTD", "N4_TXMEDIA", "N4_TXDEPR",  "N4_OCORR",  "N4_TIPOCNT",;
                   "N4_VLROC1", "N4_VLROC2",  "N4_VLROC3",  "N4_VLROC4", "N4_VLROC5"}

     
    self:oSchema:aliasToSchema("SN1",aFieldsSN1)
    self:oSchema:aliasToSchema("SN3",aFieldsSN3)
    self:oSchema:aliasToSchema("SN4",aFieldsSN4)

    self:aAllFields := self:getStructFields()
    
    self:oSchema:addProperty( "COCORREN",     STR0004, "string" , STR0004, "COCORREN"    ,,,,lCanFilter)
    self:oSchema:addProperty( "NomeEmpresa",  STR0039, "string" , STR0039, "NomeEmpresa" ,,,,lCanFilter) // Nome Empresa
    self:oSchema:addProperty( "NomeFilial",   STR0040, "string" , STR0040, "NomeFilial"  ,,,,lCanFilter) // Nome Filial
    self:oSchema:addProperty( "DescMoeda1",   STR0041, "string" , STR0041, "DescMoeda1"  ,,,,lCanFilter) // Descr. Moeda 1
    self:oSchema:addProperty( "DescMoedaSel", STR0042, "string" , STR0042, "DescMoedaSel",,,,lCanFilter) // Descr. Moeda Selecionada
    self:oSchema:addProperty( "lTotalizador", STR0043, "boolean", STR0043, "lTotalizador",,,,lCanFilter) // Linha Totalizadora?

    //Seta os parametros manuais no SmartView
    self:oSchema:addParameter("CBASEINI",   STR0005, "string",  .F.) //"Do Bem ?"
	self:oSchema:addParameter("CBASEFIM",   STR0006, "string",  .F.) //"Ate o Bem ?"
    self:oSchema:addParameter("DDATAINI",   STR0007, "date",    .F.) //"A partir da Data ?"
	self:oSchema:addParameter("DDATAFIM",   STR0008, "date",    .F.) //"Ate a Data ?"
    self:oSchema:addParameter("CCONTAINI",  STR0009, "string",  .F.) //"Da conta ?"
	self:oSchema:addParameter("CCONTAFIM",  STR0010, "string",  .F.) //"Ate a conta ?"
    self:oSchema:addParameter("NMOEDA",     STR0013, "number",  .F.) //"Moeda do relatório?"
    self:oSchema:addParameter("NORDEMMOV",  STR0011, "number",  .F.) //"Ordena Movimentos ?"
    self:oSchema:addParameter("NEXIBECC",   STR0056, "number",  .F.) //"Mostra Centro de Custo ?"
    self:oSchema:addParameter("NEXIBEITEM", STR0057, "number",  .F.) //"Mostra Item Contábil ?"
    self:oSchema:addParameter("NEXIBECLVL", STR0058, "number",  .F.) //"Mostra Classe de Valor ?"
    self:oSchema:addParameter("NBEMRPROV",  STR0012, "number",  .F.) //"Exibir Ativos Real. Provis ?"

    //Seta a consulta padrão nos parametros manuais
    self:setCustomURL("CBASEINI",  "api/framework/v1/genericLookupService/smartview/SN1", 2)
    self:setCustomURL("CBASEFIM",  "api/framework/v1/genericLookupService/smartview/SN1", 2)
    self:setCustomURL("CCONTAINI", "api/framework/v1/genericLookupService/smartview/CT1", 2)
    self:setCustomURL("CCONTAFIM", "api/framework/v1/genericLookupService/smartview/CT1", 2)

    //Seta combo do SX1 nos parametros manuais
    self:setCustomURL("NORDEMMOV", "/api/framework/treports/integratedprovider/v1/options/AFR150/MV_PAR08", 1)
    self:setCustomURL("NBEMRPROV", "/api/framework/treports/integratedprovider/v1/options/AFR150/MV_PAR12", 1)
    self:setCustomURL("NMOEDA",    "/api/framework/treports/integratedprovider/v1/options/AFR150/MV_PAR07", 1)
    self:setCustomURL("NEXIBECC",  "/api/framework/treports/integratedprovider/v1/options/AFR150/MV_PAR09", 1)
    self:setCustomURL("NEXIBEITEM","/api/framework/treports/integratedprovider/v1/options/AFR150/MV_PAR10", 1)
    self:setCustomURL("NEXIBECLVL","/api/framework/treports/integratedprovider/v1/options/AFR150/MV_PAR11", 1)

return self:oSchema


//-------------------------------------------------------------------
/*{Protheus.doc} AtfGetMtvo
Retorna a descricao do motivo da baixa 
 
@param aMotBaixa array
       cMotivo character
 
@return cReturn
 
@author Bruno Oliveira (B.O)
@since 18/10/2023
@version 1.0
*/
//-------------------------------------------------------------------

method AtfGetMtvo(aMotBaixa as array, cMotivo as character) as character class TransactionsxAssetSmartViewBusinessObject
	local cReturn 	:= " " as character
	local nPos		:= aScan( aMotBaixa, { |x| Left( x, 02 ) == cMotivo } ) as numeric

	If nPos > 0
		cReturn := aMotBaixa[ nPos ]
	EndIf	
Return cReturn

//-------------------------------------------------------------------
/*{Protheus.doc} GetTPMOV
Retorna array com os tipos de movimentos
 
@param aarray array
 
@return
 
@author Bruno Oliveira (B.O)
@since 18/10/2023
@version 1.0
*/
//-------------------------------------------------------------------

method GetTPMOV( aarray as array)  class TransactionsxAssetSmartViewBusinessObject
    Default aarray := {} 

    AADD( aarray, OemToAnsi(STR0014) ) // "Baixa"                                01 - 01
    AADD( aarray, OemToAnsi(STR0015) ) // "Substituicao"                         02 - 02
    AADD( aarray, OemToAnsi(STR0016) ) // "Transf. de"                           03 - 03
    AADD( aarray, OemToAnsi(STR0017) ) // "Transf. p/"                           04 - 04
    AADD( aarray, OemToAnsi(STR0018) ) // "Implantac no mes"        	         05 - 05
    AADD( aarray, OemToAnsi(STR0019) ) // "Depreciac no mes"                     06 - 06
    AADD( aarray, OemToAnsi(STR0020) ) // "Correcao no mes "                     07 - 07
    AADD( aarray, OemToAnsi(STR0021) ) // "Corr Monet Depre"                     08 - 08
    AADD( aarray, OemToAnsi(STR0022) ) // "Ampliacao"                            09 - 09
    AADD( aarray, OemToAnsi(STR0023) ) // "Depr. Acelerada"                      10 - 10
    AADD( aarray, OemToAnsi(STR0024) ) // "Depr. Incentiv. Neg."                 11 - 11
    AADD( aarray, OemToAnsi(STR0025) ) // "Depr. Incentiv. Pos."                 12 - 12
    AADD( aarray, OemToAnsi(STR0026) ) // "Inventario"                           13 - 13
    AADD( aarray, OemToAnsi(STR0027) ) // "Reservado"                            14 - 14
    AADD( aarray, OemToAnsi(STR0028) ) // "Baixa por Transf"                     15 - 15
    AADD( aarray, OemToAnsi(STR0029) ) // "Aquis por Transf"                     16 - 16
    AADD( aarray, OemToAnsi(STR0030) ) // "Depr. Correc. Monet. Mensal"          17 - 17
    AADD( aarray, OemToAnsi(STR0031) ) // "Depr. Acumul. Correc. Monet. Mensal"  18 - 18
    AADD( aarray, OemToAnsi(STR0032) ) // "Reservado"                            14 - 14
    AADD( aarray, OemToAnsi(STR0033) ) // "Depr. Gerencial"                      20 - 20
    AADD( aarray, OemToAnsi(STR0034) ) // "Taxa Perdida"                         21 - 21
    AADD( aarray, OemToAnsi(STR0035) ) // "Mais/Menos Valia"                     22 - 22
    AADD( aarray, OemToAnsi(STR0036) ) // "Implantac no periodo"        	     23 - 23
    AADD( aarray, OemToAnsi(STR0037) ) // "Depreciac no periodo"                 24 - 24
    AADD( aarray, OemToAnsi(STR0038) ) // "Correcao no periodo"                  24 - 24

Return nil

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuerySN3
Método para montar a query da SN3

@author Renan Gremes
@since 21/11/2024
@version 12.1.2310
/*/
//-------------------------------------------------------------------

method getQuerySN3(oFilter as Object) as Character class TransactionsxAssetSmartViewBusinessObject
    local cQuery        := ""   as character
    local oStatement            as object
    local nParamOrder   := 1    as numeric

    cQuery := "SELECT SN3.R_E_C_N_O_ N3RECNO, N3_SEQ, N3_CBASE, N3_ITEM, N3_TIPO, SN1.R_E_C_N_O_ N1RECNO, N1_DESCRIC "
    cQuery += "FROM "+RetSqlName("SN3")+" SN3 "
    cQuery += " LEFT JOIN "+RetSQLName("SN1")+" SN1 ON N1_FILIAL = N3_FILIAL "
    cQuery += " AND N1_CBASE = N3_CBASE AND N1_ITEM = N3_ITEM AND SN1.D_E_L_E_T_ = ? "
    cQuery += "WHERE N3_FILIAL = ? "
    cQuery += " AND N3_CCONTAB <= ? "
    cQuery += " AND N3_TIPO != ? "
    cQuery += " AND N3_CBASE BETWEEN ? AND ? "
    cQuery += " AND SN3.D_E_L_E_T_ = ? "

    If NORDEM == 1 
        cQuery += " ORDER BY N3_CBASE "
    ElseIf NORDEM == 2
        cQuery += " ORDER BY N3_CCONTAB "
    EndIf

    cQuery := ChangeQuery(cQuery)
    
    oStatement := FWExecStatement():New(cQuery)  

    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetString(nParamOrder++, FWxFilial("SN3"))
    oStatement:SetString(nParamOrder++, CCONTAFIM)
    oStatement:SetString(nParamOrder++, "33")
    oStatement:SetString(nParamOrder++, CBASEINI)  
    oStatement:SetString(nParamOrder++, CBASEFIM)
    oStatement:SetString(nParamOrder++,  " ")    
    
    cQuery := oStatement:GetFixQuery()

    FreeObj(oStatement)

Return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuerySN4
Método para montar a query da SN4

@author Renan Gremes
@since 21/11/2024
@version 12.1.2310
/*/
//-------------------------------------------------------------------

method getQuerySN4(oFilter as Object) as character class TransactionsxAssetSmartViewBusinessObject
    local cQuery        := ""   as character
    local oStatement            as object
    local nParamOrder   := 1    as numeric

    cQuery := "SELECT SN4.R_E_C_N_O_ N4RECNO, "
    cQuery += " N4_FILIAL, N4_CBASE, N4_ITEM, N4_TIPO, "
    cQuery += " N4_CONTA,  N4_SEQ,   N4_SEQREAV,N4_DATA, N4_MOTIVO, "
    cQuery += " N4_QUANTD, N4_TXMEDIA, N4_TXDEPR, N4_OCORR,  N4_TIPOCNT, "
    cQuery += " N4_CCUSTO, N4_SUBCTA,  N4_CLVL, N4_VLROC1, N4_VLROC2, "
    cQuery += " N4_VLROC3, N4_VLROC4,  N4_VLROC5 "
    cQuery += "FROM "+RetSqlName("SN4")+" SN4 "
	cQuery += "WHERE N4_FILIAL = ? "
	cQuery += " AND N4_DATA	BETWEEN ? AND ? "
	cQuery += " AND N4_CONTA BETWEEN ? AND ? "
	cQuery += " AND N4_CBASE = ? "
	cQuery += " AND N4_ITEM = ? "
	cQuery += " AND N4_TIPO = ? "
	cQuery += " AND N4_TPSALDO = ? "
    cQuery += " AND SN4.D_E_L_E_T_ = ?"

    cQuery := ChangeQuery(cQuery)
    
    oStatement := FWExecStatement():New(cQuery)  

    oStatement:SetString(nParamOrder++, FWxFilial("SN4"))
    oStatement:SetDate(nParamOrder++,   DDATAINI)
    oStatement:SetDate(nParamOrder++,   DDATAFIM)
    oStatement:SetString(nParamOrder++, CCONTAINI)
    oStatement:SetString(nParamOrder++, CCONTAFIM)
    oStatement:SetString(nParamOrder++, SN3->N3_CBASE)
    oStatement:SetString(nParamOrder++, SN3->N3_ITEM)
    oStatement:SetString(nParamOrder++, SN3->N3_TIPO)
    oStatement:SetString(nParamOrder++, SN3->N3_TPSALDO)
    oStatement:SetString(nParamOrder++,  " ")
    
    cQuery := oStatement:GetFixQuery()

    FreeObj(oStatement)

Return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} SVATFCombo001
    Função para retornar combobox de parâmetro
   
    @author Renan Gremes
    @since 06/02/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

Function SVATFCombo004(cOpcao as character)
    Local aRet := {} as array

    if cOpcao == "1"
        aRet := {STR0054, STR0055} //"Por Bem", "Por Conta"
    endif

return aRet
