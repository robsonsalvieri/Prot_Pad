#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.depreciationprojection.bra.ch"

//Cria um novo namespace para sua classe
namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

//Ativar ou desativar a utilização da classe
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SNS", name="Projeção de Depreciação", country="ALL", , customTables="SNS")

//-------------------------------------------------------------------------------
/*{Protheus.doc} DepreciationProjectionSmartViewBusinessObject
                Classe que contêm os dados dos Projeção de Depreciação do Ativo Fixo
Fonte/Perg      ATFR350	/ AF350A
@author         Controladoria
@since          19/12/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
Class DepreciationProjectionSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()                     as object
    public method getDescription()          as character
    public method getAreas()                as array
    public method getData()                 as object
    public method getSchema()               as object

    //usados na internacionalização
    public method preschema()               as object 
    public method preData()                 as object
    public method processData()             as json 
    
    protected method getProjDepreciacao()   as character
    protected method setProjDepreciacao()   as object
    
    protected data jDados           as json
    protected data cAlias           as character
    protected data oQuery           as object
    protected data oIntegratedProvider as object

    protected data dDataDe	        as date	
    protected data dDataAte         as date
    protected data nModel           as numeric  
    protected data nTxDe	        as numeric
    protected data nTxAte	        as numeric 	
    protected data nQuebra          as numeric  
    protected data nEntidade        as numeric  
    protected data cCodigo 	        as character
    protected data cRevisao	        as character
    protected data cCompanyName     as character
    protected data cBranchName      as character
    protected data nTipoMov         as numeric
    protected data aAllFields       as array
    protected data aSelFil          as array

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
Method new() as object Class DepreciationProjectionSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)    //"Projeção de Depreciação"
    self:setPergunte( "AF350A" )    //Indica o pergunte que será utilizado no relatório

    ::aSelFil := {}
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
@return object: string
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
Method getDescription() as character class DepreciationProjectionSmartViewBusinessObject
return  STR0002 //"Objeto contendo informações da Projeção de Depreciação do Ativo Fixo"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
@return object: string
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
Method getAreas() as array class DepreciationProjectionSmartViewBusinessObject
Return {STR0003} //"Ativo Fixo"

//-------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Metodos que determina os campos que estarão disponiveis Smartview
@author         Controladoria
@since          19/12/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method getSchema() as object class DepreciationProjectionSmartViewBusinessObject

    local aFieldsSNS as array
    local lCanFilter := .F. as logical

    aFieldsSNS := {"NS_FILIAL", "NS_DATA", "NS_TPMOV", "NS_CBASE", "NS_ITEM", "NS_TIPOCNT", "NS_CONTA",;
                    "NS_CCUSTO", "NS_SUBCTA", "NS_CLVL", "NS_TPDEPR", "NS_VLRMOV1", "NS_VLRMOV2",;
                    "NS_VLRMOV4", "NS_VLRMOV5", "NS_VLRACM1", "NS_VLRACM2", "NS_VLRACM3", "NS_VLRACM4",;
                    "NS_VLRACM5"}

    self:oSchema:aliasToSchema("SNS", aFieldsSNS)

    self:oSchema:addProperty("NS_VLRMOV3",   STR0004, "number", STR0004, "NS_VLRMOV3" )  //Valor Mov. 3

    //Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()

    //Adiciona campos ao schema
    self:oSchema:addProperty("TPMOV_DESC",   STR0005, "string", STR0005, "TPMOV_DESC" ,,,,lCanFilter)  //Descrição Tipo Movto
    self:oSchema:addProperty("TPDEPR_DESC",  STR0006, "string", STR0006, "TPDEPR_DESC",,,,lCanFilter)  //Descrição Tipo Deprec.
    self:oSchema:addProperty("NomeEmpresa",  STR0007, "string", STR0007, "NomeEmpresa",,,,lCanFilter)  //Nome Empresa
    self:oSchema:addProperty("NomeFilial",   STR0008, "string", STR0008, "NomeFilial" ,,,,lCanFilter)  //Nome Filial   

    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0009, "string", .T.) //Filiais
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------------------
/*{Protheus.doc} getData
Tratamento dos dados que serão enviados aos campos do getSchema
@author         Controladoria
@since          19/12/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class DepreciationProjectionSmartViewBusinessObject

    Local cQuery := "" as character

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "AF350A" )

    ::dDataDe	:= MV_PAR01	 // "Período de ?
    ::dDataAte  := MV_PAR02	 // "Período até ?
    ::nModel    := MV_PAR03	 // "Demonstrar ?
    ::nTxDe		:= MV_PAR04	 // "Taxa Depreciação de ?
    ::nTxAte	:= MV_PAR05	 // "Taxa Depreciação até ?
    ::nQuebra  	:= MV_PAR06	 // "Quebra ?
    ::nEntidade := MV_PAR07	 // "Entidade Base ?
    ::cCodigo   := MV_PAR08	 // "Código ?
    ::cRevisao  := MV_PAR09	 // "Revisão ?
    ::nTipoMov 	:= MV_PAR10	 // "Movimentos ?

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    ::cCompanyName := ::oIntegratedProvider:getCompanyName()
    ::cBranchName  := ::oIntegratedProvider:getBranchName()

    //Seleção de filiais    
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
    ::oIntegratedProvider:loadFilterBranches({"SNS"})

    If Valtype(::oIntegratedProvider:oFilterBranches["SNS"][1]) == "A"
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["SNS"][1]
    Else
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["SNS"]
    EndIf            

    //Carrega o bloco de baixas
    cQuery := self:getProjDepreciacao(oFilter)

    self:cAlias  := MPSysOpenQuery(cQuery)
    self:setProjDepreciacao(oFilter)

    (self:cAlias)->( DBCloseArea() )

    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())
    FreeObj(::oIntegratedProvider)

return self:oData

//-------------------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodos usado na Internacionalização
@author         Controladoria
@since          19/12/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method preSchema() as object class DepreciationProjectionSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Método de instância da classe
@return object: self:oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method preData() as object class DepreciationProjectionSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método na Chamado no momento do processamento da query
@return object: self:oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method processData() class DepreciationProjectionSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------------------
/*{Protheus.doc} getProjDepreciacao
Gera a query de filtragem dos dados 
@author         Controladoria
@since          19/12/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method getProjDepreciacao(oFilter) as character class DepreciationProjectionSmartViewBusinessObject

    Local cQuery       := "" as character
    Local cOrder       := "" as character
    Local aTpMov       := "" as character
    Local nParamOrder  := 1  as numeric  

    //Define a quebra do relatório
    If ::nQuebra == 1   // TODAS  
        cOrder  := "NS_FILIAL,NS_DATA"
    Else
        cOrder  := "NS_FILIAL"
    EndIf

    If ::nQuebra == 2   // DATA_MOV
        cOrder += ",NS_DATA"
    Else
        Do Case
        Case ::nEntidade == 1       // CONTA
            cOrder  += ",NS_CONTA"
        Case ::nEntidade == 2       // CCUSTO
            cOrder  += ",NS_CONTA,NS_CCUSTO"
        Case ::nEntidade == 3       // SUBCONTA
            cOrder  += ",NS_CONTA,NS_CCUSTO,NS_SUBCTA"
        Case ::nEntidade == 4       // CLASSE
            cOrder  += ",NS_CONTA,NS_CCUSTO,NS_SUBCTA,NS_CLVL"
        EndCase
    EndIf

    If  ::nModel == 1       // ANALITICO
        cOrder += ",NS_CBASE,NS_ITEM"	
    EndIf
   
    Do Case
        Case ::nTipoMov == 1            // TODOS_MOV
            aTpMov	:= {"0","1","2"}
        Case ::nTipoMov == 2            // SALDO_MOV
            aTpMov	:= {"0"}
        Case ::nTipoMov == 3            // SIMUL_REAL_MOV
            aTpMov	:= {"1"}
        Case ::nTipoMov == 4            // SIMUL_MOD_MOV
            aTpMov	:= {"2"}
        OtherWise
            aTpMov	:= {"0","1","2"}
    EndCase

    cQuery := "	SELECT "
    cQuery += self:getSQLFields() //Pega campos do schema
    cQuery += "	FROM "+ RetSqlName("SNS") +" SNS "
    cQuery += "	WHERE SNS.D_E_L_E_T_  = ? "
    if !Empty(self:aSelFil)
        cQuery += " AND SNS.NS_FILIAL IN ( ? ) "
    endif
    cQuery += "	AND SNS.NS_DATA    >= ? "
    cQuery += "	AND SNS.NS_DATA    <= ? "
    cQuery += "	AND SNS.NS_TXDEPR1 >= ? "
    cQuery += "	AND SNS.NS_TXDEPR1 <= ? "
    cQuery += "	AND SNS.NS_CODIGO   = ? "
    cQuery += "	AND SNS.NS_REVISAO  = ? "
    cQuery += "	AND SNS.NS_TPMOV IN ( ? )"
        
    //Os filtros serão setados na interface do novo SmartView
    cQuery += IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

    cQuery += " ORDER BY " + cOrder

    //Objeto executa o comando SQL inúmeras vezes.
    self:oQuery := FWPreparedStatement():New(ChangeQuery(cQuery))

    //Objeto popula parametros de pergintas para o comando SQL.
    self:oQuery:SetString(nParamOrder++, Space(1))
    If !Empty(self:aSelFil)
        self:oQuery:SetIn(nParamOrder++, ::aSelFil)
    EndIf
    self:oQuery:SetDate(nParamOrder++,    ::dDataDe)
    self:oQuery:SetDate(nParamOrder++,    ::dDataAte)
    self:oQuery:setNumeric(nParamOrder++, ::nTxDe)
    self:oQuery:setNumeric(nParamOrder++, ::nTxAte)
    self:oQuery:SetString(nParamOrder++,  ::cCodigo)
    self:oQuery:SetString(nParamOrder++,  ::cRevisao)
    self:oQuery:SetIn(nParamOrder++,      aTpMov)
    
    //Retorna query com parâmetros tratados e substituídos.
    cQuery := self:oQuery:GetFixQuery()

return cQuery

//-------------------------------------------------------------------------------
/*{Protheus.doc} 
Seta os dados filtrados no objeto oData
@author         Controladoria
@since          19/12/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method setProjDepreciacao(oFilter as object) as object class DepreciationProjectionSmartViewBusinessObject

    local nX    := 0  as numeric
    local nPos  := 0 as numeric
    local aCBOX := {} as array
    local aAux  := {} as array
    
    //Posiciona no registro inicial
    (self:cAlias)->(DBGoTop())

    //Tratamento descrição tipo movimento
    aCBOX := StrTokArr(Posicione("SX3", 2, "NS_TPMOV", "X3_CBOX"),';')
    For nX := 1 To Len(aCBOX)
        aAdd(aAux, StrTokArr(aCBOX[nX], "="))
    Next nX

    While !(self:cAlias)->(Eof())
        
        self:jDados := JsonObject():new()
        
        //Popula campos da query
        For nX := 1 To Len(self:aAllFields)
            if self:aAllFields[nX]:getType() == "date"
                self:jDados[self:aAllFields[nX]:getName()] := iif(!empty((self:cAlias)->&(self:aAllFields[nX]:getRealName())), FwTimeStamp(5, StoD((self:cAlias)->&(self:aAllFields[nX]:getRealName())), "00:00:00"), nil)
            elseif self:aAllFields[nX]:getType() == "string"
                self:jDados[self:aAllFields[nX]:getName()] := AllTrim((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
            else
                self:jDados[self:aAllFields[nX]:getName()] := (self:cAlias)->&(self:aAllFields[nX]:getRealName())
            endif
		Next nX

        nPos := Ascan(aAux, {|x| x[1] == (self:cAlias)->NS_TPMOV})
        
        //Campos customizados
        self:jDados["TPMOV_DESC"]  := aAux[nPos][2]
        self:jDados["TPDEPR_DESC"] := AllTrim(Posicione("SN0", 1, FWxFilial("SN0")+"04"+(self:cAlias)->NS_TPDEPR, "N0_DESC01"))
        self:jDados["NomeEmpresa"] := ::cCompanyName
        self:jDados["NomeFilial" ] := ::cBranchName

        self:processData()
        self:oData:appendData(self:jDados)

        (self:cAlias)->( dbSkip() )

    EndDo

return self:oData
