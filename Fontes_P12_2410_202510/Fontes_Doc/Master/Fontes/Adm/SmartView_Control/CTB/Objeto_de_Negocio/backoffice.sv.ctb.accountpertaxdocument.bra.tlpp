#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.accountpertaxdocument.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGACTB",tables="CT1, CT2", name="Conta por Documento Fiscal", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} CentrodeCustoTReportsBusinessObject
Classe para criação do Objeto de Negócio Conta por Documento Fiscal para o TReports
 
@author Ednilson Queiroz
@since 04/03/2024
@version 1.0
*/
//-------------------------------------------------------------------------------
class AccountPerTaxDocumentSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartViewCtbr420Raz

    public method new()                     as object
    public method getDescription()          as character
    public method getCTBR420RazData()       as object  //Carregado na classe pai
    public method getCTBR420RazSchema()     as object // Carregado na classe pai
    public method handleCTBR420RazParams() //carrega os parametros
    public method processCustomData()       as json
    public method preData()                 as object
    public method preSchema()               as object
    public method processData()             as json

    protected data nTotAtual  as numeric
    protected data cContaAnt  as character
    protected data lPrintZero as logical
    protected data dDataAnt   as date
  
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Ednilson Queiroz
@since 04/03/2024
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class AccountPerTaxDocumentSmartViewBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //"Conta por Documento Fiscal"
    self:setPergunte("CTR420")   //Indica o pergunte que será utilizado no relatório

    ::nTotAtual := 0
    ::cContaAnt := ""
    ::dDataAnt	:= CTOD("  /  /  ")
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz
@since 04/03/2024
@version 1.0
*/
//-------------------------------------------------------------------
method getDescription() as character class AccountPerTaxDocumentSmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre o Conta por Documento Fiscal"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.

@return object: self:oSchema

@author Ednilson Queiroz
@since 04/03/2024
@version 1.0
/*/
//-------------------------------------------------------------------
method preSchema() as object class AccountPerTaxDocumentSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.

@return object: self:oData

@author Ednilson Queiroz
@since 04/03/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preData() as object class AccountPerTaxDocumentSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método chamado antes do appendData

@return object: self:oData

@author Ednilson Queiroz
@since 04/03/2024
@version 1.0*/
//-------------------------------------------------------------------
method processData() class AccountPerTaxDocumentSmartViewBusinessObject
return ::JItens

//-------------------------------------------------------------------
/*{Protheus.doc} getCTBR420RazData
@return object: self:oData

@author Ednilson Queiroz
@since 04/03/2024
@version 1.0*/
//-------------------------------------------------------------------
method getCTBR420RazData() as object class AccountPerTaxDocumentSmartViewBusinessObject
   self:handleCTBR420RazParams()
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCTBR420RazSchema
@return object: self:oData

@author Ednilson Queiroz
@since 04/03/2024
@version 1.0*/
//-------------------------------------------------------------------
method getCTBR420RazSchema() as object class AccountPerTaxDocumentSmartViewBusinessObject
    self:oSchema:addProperty("SALDOANT"     ,STR0013 , "number", STR0013, "SALDOANT"    ,,,,.F. ) //"Saldo Anterior"
    self:oSchema:addProperty("SALDOANT_M"   ,STR0008 , "string", STR0008, "SALDOANT_M"  ,,,,.F. ) //"Saldo Anterior Mascara"
    self:oSchema:addProperty("SALDOATUAL"   ,STR0003 , "number", STR0003, "SALDOATUAL"  ,,,,.F. ) //"Saldo Atual"
    self:oSchema:addProperty("SALDOATUAL_M" ,STR0004 , "string", STR0004, "SALDOATUAL_M",,,,.F. ) //"Saldo Atual Mascara"
    self:oSchema:addProperty("CONTARES"     ,STR0011 , "string", STR0011, "CONTARES"    ,,,,.F. ) //"Conta Reduzida"
    self:oSchema:addProperty("DESCCTA"      ,STR0005 , "string", STR0005, "DESCCTA"     ,,,,.F. ) //"Descrição Conta"
    self:oSchema:addProperty("CONTASINT"    ,STR0006 , "string", STR0006, "CONTASINT"   ,,,,.F. ) //"Conta Sintetica"
    self:oSchema:addProperty("DESCSINT"     ,STR0007 , "string", STR0007, "DESCSINT"    ,,,,.F. ) //"Descrição Sintetica"
    self:oSchema:addProperty("NomeEmpresa"  ,STR0009 , "string", STR0009, "NomeEmpresa" ,,,,.F. ) //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial"   ,STR0010 , "string", STR0010, "NomeFilial"  ,,,,.F. ) //"Nome Filial"

    self:oSchema:addParameter("SV_MULTBRANCH", STR0012, "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} handleCTBR420RazParams
@return object: self:oData

@author Ednilson Queiroz
@since 04/03/2024
@version 1.0*/
//-------------------------------------------------------------------
method handleCTBR420RazParams() class AccountPerTaxDocumentSmartViewBusinessObject
    Local cArqTmp       := ""   as character
    Local cContaIni     := ""   as character
    Local cContaFim     := ""   as character
    Local cMoeda        := ""   as character
    Local dDataIni              as date 
    Local dDataFim              as date 
    Local aSetOfBook    := {}   as array
    Local lNoMov        := .F.  as logical
    Local cSaldo        := ""   as character
    Local cTpRel        := "1"  as character
    Local lAnalitico    := .F.  as logical
    Local cLoteIni      := ""   as character
    Local cLoteFim      := ""   as character
    Local cSbLoteIni    := ""   as character
    Local cSbLoteFim    := ""   as character
    Local cDocIni       := ""   as character
    Local cDocFim       := ""   as character
    Local cFiltCT1      := ""   as character
    Local aSelFil       := {}   as array
    Local lImpDoc0              as logical

    //Parametros utilizados na visão de dados.
    cContaIni    := MV_PAR01
    cContaFim    := MV_PAR02
    dDataIni     := MV_PAR03
    dDataFim     := MV_PAR04
    cMoeda       := iif(Empty(MV_PAR05), "01", MV_PAR05)
    cSaldo       := iif(Empty(MV_PAR06), "1", MV_PAR06)  
    aSetOfBook   := iif(Empty(MV_PAR07), CTBSetOf(""), CTBSetOf(MV_PAR07))
    lNoMov       := iif(MV_PAR08 == 2, .F., .T.)    
    lImpDoc0     := iif(MV_PAR16 == 2, .F., .T.)
    ::lPrintZero := iif(MV_PAR17 == 1, .F., .T.)

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf

    //Os parametros de mv_par09 em diante serão utilizados quando for feito o design do relatório.
    //por enquanto na visão de dados não tem efeito nenhum até o momento serão revistos futuramente.
    ::cArqTmp       :=  cArqTmp    
    ::cContaIni     :=  cContaIni  
    ::cContaFim     :=  cContaFim  
    ::cMoeda        :=  cMoeda     
    ::dDataIni      :=  dDataIni   
    ::dDataFim      :=  dDataFim   
    ::aSetOfBook    :=  aSetOfBook 
    ::lNoMov        :=  lNoMov
    ::cSaldo        :=  AllTrim(cSaldo)
    ::cTpRel        :=  cTpRel     
    ::lAnalitico    :=  lAnalitico 
    ::cLoteIni      :=  cLoteIni   
    ::cLoteFim      :=  cLoteFim   
    ::cSbLoteIni    :=  cSbLoteIni 
    ::cSbLoteFim    :=  cSbLoteFim 
    ::cDocIni       :=  cDocIni    
    ::cDocFim       :=  cDocFim    
    ::cFiltCT1      :=  cFiltCT1   
    ::aSelFil       :=  aSelFil    
    ::lImpDoc0      :=  lImpDoc0

return 

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
@return object: self:oData

@author Ednilson Queiroz
@since 04/03/2024
@version 1.0*/
//-------------------------------------------------------------------
method processCustomData() as json class AccountPerTaxDocumentSmartViewBusinessObject
    Local aSaldo     := {} as array
    Local aSaldoAnt  := {} as array
    Local nTamConta	 := TamSX3("CT1_DESC01")[1] as numeric
    Local TAM_VALOR  := 17 as numeric
    Local nSaldoAtu  := 0  as numeric
    Local nDecimais        as numeric
    Local cPicture         as character
    Local cDesc            as character
    Local cContaSint       as character
    Local cDescSint        as character
    Local cCodRes          as character
    Local cMascara         as character
    Local cSepara    := "" as character

    nDecimais  := DecimalCTB(::aSetOfBook,::cMoeda) 
    cPicture   := Iif(!Empty(::aSetOfBook[4]), "@E 999,999,999,999.99", "")
    cMascara   := Iif(Empty(::aSetOfBook[2]), SuperGetMv("MV_MASCARA"), RetMasCtb(::aSetOfBook[2], @cSepara))
    cContaSint := Ctr400Sint(cArqTmp->CONTA,@cDescSint,::cMoeda,@cDesc,@cCodRes)

    aSaldoAnt 	:= SaldoCT7Fil(cArqTmp->CONTA,::dDataIni,::cMoeda,::cSaldo,"CTBR400",,,::aSelFil)
	aSaldo 		:= SaldoCT7Fil(cArqTmp->CONTA,cArqTmp->DATAL,::cMoeda,::cSaldo,,,,::aSelFil)
    
    If Empty(::cContaAnt) .OR. ::cContaAnt <> cArqTmp->CONTA
        ::nTotAtual := 0
        ::cContaAnt := cArqTmp->CONTA
        ::dDataAnt	:= CTOD("  /  /  ")

        nSaldoAtu   := aSaldo[6]
        ::nTotAtual := nSaldoAtu - cArqTmp->LANCDEB + cArqTmp->LANCCRD
	ElseIf ::cContaAnt == cArqTmp->CONTA
        If ::dDataAnt <> cArqTmp->DATAL 
			If (cArqTmp->LANCDEB <> 0 .Or. cArqTmp->LANCCRD <> 0)
                ::JItens["DATAL"] := totvs.framework.treports.date.dateToTimeStamp(::dDataAnt)
			Endif
			::dDataAnt := cArqTmp->DATAL
		EndIf
        ::nTotAtual := (::nTotAtual) - cArqTmp->LANCDEB + cArqTmp->LANCCRD
    EndIf

    //Contas
    ::JItens["DESCCTA"    ] := cDesc
    ::JItens["CONTASINT"  ] := EntidadeCTB(cContaSint,0,0,nTamConta,.F.,cMascara,cSepara,,,,,.F.)
    ::JItens["DESCSINT"   ] := cDescSint

    //Saldos
    ::JItens["SALDOANT"    ] := aSaldoAnt[6]
    ::JItens["SALDOANT_M"  ] := ValorCTB(aSaldoAnt[6],,,TAM_VALOR,nDecimais,.T.,cPicture,,,,,,,::lPrintZero,.F.)
    ::JItens["SALDOATUAL"  ] := ::nTotAtual
    ::JItens["SALDOATUAL_M"] := ValorCTB(::nTotAtual,,,TAM_VALOR,nDecimais,.T.,cPicture,,,,,,::lPrintZero,.F.,.F.)

    //Dados empresa e filial logada
    ::JItens["NomeEmpresa"] := self:cCompanyName
    ::JItens["NomeFilial" ] := self:cBranchName

Return ::JItens
