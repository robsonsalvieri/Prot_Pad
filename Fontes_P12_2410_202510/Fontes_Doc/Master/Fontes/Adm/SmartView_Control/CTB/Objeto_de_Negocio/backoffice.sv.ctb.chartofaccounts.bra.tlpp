#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.chartofaccounts.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1", name="Plano de Contas", country="ALL",,customTables="CT1")

//-------------------------------------------------------------------------------
/*{Protheus.doc} ChartOfAccountsSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Plano de Contas para o TReports
 
@author Controladoria
@since 07/07/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class ChartOfAccountsSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public method new() 			as object
	public method getData() 		as object
	public method getSchema() 		as object
	public method getAreas() 		as array
	public method getDescription() 	as character

	public method preData()         as object
    public method preSchema()       as object
	public method processData()    as json 

	protected data oQuery1 		as object
	protected data aAllFields   as array
	protected data cAlias       as character
	protected data jDados       as json
	protected data oIntegratedProvider  as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Controladoria
@since 07/07/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class ChartOfAccountsSmartViewBusinessObject

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0001) //"Plano de Contas"
	self:setPergunte("CTR010") //Indica o pergunte que será utilizado no relatório

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Controladoria
@since 07/07/2023
@version 12.1.2310
*/
//------------------------------------------------------------------- 

method getDescription() as character class ChartOfAccountsSmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre os planos de contas"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Controladoria
@since 07/07/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class ChartOfAccountsSmartViewBusinessObject
return {STR0003} //"Contabilidade"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.

@author Controladoria
@since 07/07/2023
@version 12.1.2310
/*/
//-------------------------------------------------------------------

method preSchema() as object class ChartOfAccountsSmartViewBusinessObject
return self:oSchema



//-------------------------------------------------------------------
/*/{Protheus.doc} processData
Metodo que será consumido pelo M.I.
Será usado para alterar a Query antes do getData.

@author Controladoria
@since 07/07/2023
@version 12.1.2310
/*/
//-------------------------------------------------------------------

method processData() as json class ChartOfAccountsSmartViewBusinessObject
return ::jDados


//-------------------------------------------------------------------
/*/{Protheus.doc} preData
Metodo que será consumido pelo M.I.
AJusta o Json conforme alteração no processData e preSchema

@author Controladoria
@since 07/07/2023
@version 12.1.2310
/*/
//-------------------------------------------------------------------

method preData() as object class ChartOfAccountsSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author Controladoria
@since 07/07/2023
@version 12.1.2310
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class ChartOfAccountsSmartViewBusinessObject
	local cQuery     as character
	local cContaDe   as character
	local cContaAte  as character
	local nTpConta   as numeric
	local lContBloq  as logical
	local cMascara   as character
	local cSeparador as character
	local cDelete := space(1) as character
	local nParamOrder := 1    as numeric
	local cCompanyName        as character
	local cBranchName         as character
	local nX		  := 1    as numeric
	Local lcCusto	as logical

	//Carrega lista de parametros para a memória
	CTBsetValueMVPAR(oFilter:getParameters(), "CTR010")

	::oIntegratedProvider := ControlIntegratedProvider():New()
	cCompanyName    := ::oIntegratedProvider:getCompanyName()
	cBranchName     := ::oIntegratedProvider:getBranchName()

	cContaDe  := MV_PAR01                        // da conta
	cContaAte := MV_PAR02                        // ate a conta
	nTpConta  := MV_PAR05                        // 1-Analitica, 2- Sintetica, 3- Todas
	lContBloq := iif( MV_PAR07 == 2 ,.T. , .F. ) // Imprime Bloqueadas?
	lcCusto	  := iif( MV_PAR03 == 1 ,.T. , .F. ) // Imprime Bloqueadas?
	If Empty( MV_PAR08 ) // Mascara
		cMascara := SuperGetMv( "MV_MASCARA" )
	Else
		cMascara := RetMasCtb( MV_PAR08 , @cSeparador )
	EndIf

	cQuery := " SELECT " +  self:getSQLFields(.F., , .F.)
	cQuery += " FROM " +RetSqlName("CT1")+ " CT1"	
	cQuery += " WHERE CT1.CT1_FILIAL =  ? "
	cQuery += " AND CT1.CT1_CONTA >= ? "
	cQuery += " AND CT1.CT1_CONTA <= ? "
    cQuery += " AND CT1.D_E_L_E_T_ = ? "
	If nTpConta == 1     //Analiticas
		cQuery += " AND CT1.CT1_CLASSE = ? "
	Elseif nTpConta == 2 //Sinteticas
		cQuery += " AND CT1.CT1_CLASSE = ? "
	Elseif nTpConta == 3 //Ambas
		cQuery += " AND CT1.CT1_CLASSE IN (?) "
	EndIf
	If lContBloq
		cQuery += " AND CT1.CT1_BLOQ != ? "
	EndIf
	If !lcCusto
		cQuery += " AND CT1.CT1_NCUSTO = ? "
	EndIf
	cQuery += " ORDER BY CT1_CONTA "

	cQuery += IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

	self:oQuery1 := FWPreparedStatement():New(ChangeQuery(cQuery))

	self:oQuery1:SetString(nParamOrder++, FWxFilial("CT1"))
	self:oQuery1:SetString(nParamOrder++, MV_PAR01)
	self:oQuery1:SetString(nParamOrder++, MV_PAR02)
	self:oQuery1:SetString(nParamOrder++, cDelete)
	If nTpConta == 1     //Analiticas
		self:oQuery1:SetString(nParamOrder++, '2')
	Elseif nTpConta == 2 //Sinteticas
		self:oQuery1:SetString(nParamOrder++, '1')
	Elseif nTpConta == 3 //Ambas
		self:oQuery1:Setin(nParamOrder++, {'1','2'})
	EndIf
	If lContBloq
		self:oQuery1:SetString(nParamOrder++, '1')
	EndIf
	If !lcCusto
		self:oQuery1:SetNumeric(nParamOrder++, 0)
	EndIf

	cQuery := self:oQuery1:GetFixQuery()

	self:cAlias  := MPSysOpenQuery(cQuery)
    //Posiciona no registro inicial
    (self:cAlias)->(dbGoTop())

	While !(self:cAlias)->(Eof())

        self:jDados := JsonObject():new()

        //Popula campos da query
        For nX := 1 To Len(self:aAllFields)
			if self:aAllFields[nX]:getType() == "date"
                self:jDados[self:aAllFields[nX]:getName()] := totvs.framework.treports.date.stringToTimeStamp((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
            elseif self:aAllFields[nX]:getType() == "string"
                self:jDados[self:aAllFields[nX]:getName()] := AllTrim((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
			else
				self:jDados[self:aAllFields[nX]:getName()] := (self:cAlias)->&(self:aAllFields[nX]:getRealName())
            endif
		Next nX

		::jDados["NomeEmpresa"] := cCompanyName
		::jDados["NomeFilial" ] := cBranchName
		::jDados["CONTA_M"	  ] := EntidadeCTB(AllTrim((self:cAlias)->CT1_CONTA),0,0,70,.F.,cMascara,cSeparador,,,,,.F.)

        self:processData()
        self:oData:appendData(self:jDados)

        (self:cAlias)->(dbSkip())
    EndDo

	(self:cAlias)->(dbCloseArea())
    
    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())
    FreeObj(::oIntegratedProvider)     

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getStruct
Prepara a estrutura dos campos

@param aCpos array: Array com os campos do relatório

@return array: Array com a estrutura dos campos

@author Controladoria
@since 07/07/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getSchema() as object class ChartOfAccountsSmartViewBusinessObject

	Local aFieldsCT1 := {}  as array
	Local lCanFilter := .F. as logical

	aFieldsCT1 := { "CT1_FILIAL","CT1_CONTA" ,"CT1_DESC01","CT1_DESC02","CT1_DESC03","CT1_DESC04","CT1_DESC05","CT1_CLASSE",;
                    "CT1_NORMAL","CT1_RES"	 ,"CT1_BLOQ"  ,"CT1_DTBLIN","CT1_DTBLFI","CT1_DC"	 ,"CT1_NCUSTO","CT1_CC"	   ,;
                    "CT1_CVD02" ,"CT1_CVD03" ,"CT1_CVD04" ,"CT1_CVD05" ,"CT1_CVC02" ,"CT1_CVC03" ,"CT1_CVC04" ,"CT1_CVC05 ",;
                    "CT1_HP"	,"CT1_ACITEM","CT1_ACCUST","CT1_ACCLVL","CT1_DTEXIS","CT1_DTEXSF","CT1_CTAVM" ,"CT1_CTARED",;
                    "CT1_CTALP"	,"CT1_CTAPON","CT1_BOOK"  ,"CT1_GRUPO" ,"CT1_AGLSLD","CT1_RGNV1" ,"CT1_RGNV2" ,"CT1_RGNV3" ,;
                    "CT1_CCOBRG","CT1_ITOBRG","CT1_CLOBRG","CT1_TRNSEF","CT1_AGLUT" ,"CT1_LALUR" ,"CT1_CTLALU","CT1_TPLALU",;
                    "CT1_LALHIR","CT1_RATEIO","CT1_ESTOUR","CT1_CODIMP","CT1_AJ_INF","CT1_NATCTA","CT1_ACATIV","CT1_ATOBRG",;
                    "CT1_ACET05","CT1_05OBRG","CT1_INDNAT","CT1_SPEDST","CT1_NTSPED","CT1_ACAT01","CT1_AT01OB","CT1_ACAT02",;
                    "CT1_AT02OB","CT1_ACAT03","CT1_AT03OB","CT1_ACAT04","CT1_AT04OB","CT1_TPO01 ","CT1_TPO04 ","CT1_TPO02 ",;
                    "CT1_TPO03"	,"CT1_INTP"	 ,"CT1_CVD01" ,"CT1_CVC01" ,"CT1_MOEDVM","CT1_DIOPS" ,"CT1_PVARC" ,"CT1_CTASUP" }
	
	self:oSchema:aliasToSchema("CT1",aFieldsCT1)
	
	//Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()
	
	self:oSchema:addProperty("NomeEmpresa", STR0004, "string", STR0004, "NomeEmpresa",,,,lCanFilter) // Nome Empresa
    self:oSchema:addProperty("NomeFilial" , STR0005, "string", STR0005, "NomeFilial" ,,,,lCanFilter) // Nome Filial
	self:oSchema:addProperty("CONTA_M"    , STR0006, "string", STR0006, "CONTA_M"    ,,,,lCanFilter) // Cod Conta Mascara

return self:oSchema

