#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.ledger2coin.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT2, CTT, CTD, CTH, CTZ, CTO", name="Razão 2 Moedas", country="ALL", ,)


//-------------------------------------------------------------------------------
/*{Protheus.doc} ledgerbyaccountingitemSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Razão Item Contábil para o TReports
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class ledger2coinSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbgerraz

	public method new() as object
	public method getDescription()      as character
	public method getAreas()            as array
	

    public method preData()            as object
    public method preSchema()          as object
    public method processData()        as json

	public method processCustomData()	as json

	public method ExecDBSkipTemp()     

	public method getCtGerRazSchema()  as object
	public method getCtgGerRazData()   as object
	public method handleCtgGerRazParams()

	protected method getSaldos()
	protected data nSaldoAtu    as numeric
	protected data nSaldoAtu2   as numeric
    protected data lSldAntCC    as logical
    protected data lSldAntIt    as logical
    protected data lSldAntCv    as logical
    protected data aSaldo       as array
    protected data aSaldoAnt    as array
	protected data aSaldo2      as array
    protected data aSaldoAnt2   as array	
	protected data cCoinOption  as character
	protected data jDados    	as json

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method new() as object Class ledger2coinSmartViewBusinessObject
	_Super:new()
	self:setDisplayName(STR0001) //"Razão 2 Moedas"
	self:setPergunte("CTR410") //Indica o pergunte que será utilizado no relatório

	::nSaldoAtu     := 0
	::nSaldoAtu2    := 0
    ::lSldAntCC     := .F.
    ::lSldAntIt     := .F.
    ::lSldAntCv     := .F.
    ::aSaldo        := {}
    ::aSaldoAnt     := {}
    ::aSaldo2       := {}
    ::aSaldoAnt2    := {}	
	::cCoinOption	:= ""

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method processCustomData() as json class ledger2coinSmartViewBusinessObject
	
	local cDtLanc       as character
	local nSaldoAnt 	:= 0   as numeric
	local nSaldoAnt2 	:= 0   as numeric
    local cNormal   	:= ""  as character
    local cPicture 		:= ::aSetOfBook[4] as character    
    local nDecimais 	:= iif(DecimalCTB(::aSetOfBook,::cMoeda) == 0, SuperGetMv("MV_CENT"), DecimalCTB(::aSetOfBook,::cMoeda)) as numeric	
    local TAM_VALOR 	:= 20  as numeric	

	if cArqTmp->CONTA <> ::cContaAnt
		//Pegando os saldos MV_PAR05
		::cCoinOption := "1"
		self:getSaldos()
        ::nSaldoAtu := 0
        ::nSaldoAtu := ::aSaldoAnt[6]
        nSaldoAnt   := ::aSaldo[6]
		
		//Pegando os saldos MV_PAR06
		::cCoinOption := "2"
		self:getSaldos()
		::nSaldoAtu2 := 0
        ::nSaldoAtu2 := ::aSaldoAnt2[6]
        nSaldoAnt2   := ::aSaldo2[6]
	EndIf

	::nSaldoAtu  := ::nSaldoAtu  - cArqTmp->LANCDEB_1 + cArqTmp->LANCCRD_1
	::nSaldoAtu2 := ::nSaldoAtu2 - cArqTmp->LANCDEB + cArqTmp->LANCCRD

	//Moedas
	aCtb2Moeda:= CtbMoeda(::c2Moeda) //MV_PAR05
	aCtbMoeda := CtbMoeda(::cMoeda)  //MV_PAR06
	//Tratamento dos campos de data para timestamp
	cDtLanc := iif(!empty(cArqTMP->DATAL), FwTimeStamp(5, cArqTMP->DATAL, "00:00:00"), nil)
	//Manipulação de conteúdo do json
	::jDados["TPSLDANT"]   := RZValorCTB(nSaldoAnt,,,TAM_VALOR,nDecimais,.T.,cPicture,cNormal,,,,,,::lPrintZero,.F.,,,.F.)
	::jDados["TPSLDATU"]   := RZValorCTB(::nSaldoAtu,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cNormal,,,,,,::lPrintZero,.F.,,,.F.)
	::jDados["TPSLDANT2"]  := RZValorCTB(nSaldoAnt2,,,TAM_VALOR,nDecimais,.T.,cPicture,cNormal,,,,,,::lPrintZero,.F.,,,.F.)
	::jDados["TPSLDATU2"]  := RZValorCTB(::nSaldoAtu2,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cNormal,,,,,,::lPrintZero,.F.,,,.F.)
	::jDados["LANCDEB_1"]  := cArqTmp->LANCDEB_1
	::jDados["LANCDEBTX"]  := aCtb2Moeda[3]
	::jDados["LANCDEBMD"]  := aCtbMoeda[3]
	::jDados["LANCCRD_1"]  := cArqTmp->LANCCRD_1
	::jDados["LANCCRDTX"]  := aCtb2Moeda[3]
	::jDados["LANCCRDMD"]  := aCtbMoeda[3]
	::jDados["TXDEBITO"]   := cArqTMP->TXDEBITO
	::jDados["TXCREDITO"]  := cArqTMP->TXCREDITO
	::jDados["SALDOANT"]   := nSaldoAnt
	::jDados["SALDOANT2"]  := nSaldoAnt2
	::jDados["SALDOSCR"]   := ::nSaldoAtu
	::jDados["SALDOSCR2"]  := ::nSaldoAtu2
	::jDados["NomeEmpresa"]:= ::cCompanyName
    ::jDados["NomeFilial" ]:= ::cBranchName

return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Valida se pula registro na temporária
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method ExecDBSkipTemp(cArqTmp As character) class ledger2coinSmartViewBusinessObject

	Local lOk := .F.

    If !::lNoMov //Se imprime conta sem movimento
        lOk := (::jDados["SALDOSCR2"] == 0 .And. cArqTmp->LANCDEB + cArqTmp->LANCDEB_1 ==0 .And.;
                cArqTmp->LANCCRD + cArqTmp->LANCCRD_1 == 0)
    Endif             

    If ::lNomov .And. (::jDados["SALDOSCR2"]== 0 .And. cArqTmp->LANCDEB + cArqTmp->LANCDEB_1 ==0 .And.;
	                                cArqTmp->LANCCRD + cArqTmp->LANCCRD_1 == 0) 
        If CtbExDtFim("CT1") .And. CT1->(MsSeek(xFilial()+cArqTmp->CONTA))
            lOk := !CtbVlDtFim("CT1",::dDataIni) 		
        EndIf
    EndIf

Return lOk

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerRazSchema
Prepara a estrutura dos campos
 
@return object: Schema com a estrutura dos campos
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getCtGerRazSchema() as object class ledger2coinSmartViewBusinessObject

	local lCanFilter := .F. as logical

	//Adição de novos campos ao schema
	self:oSchema:addProperty("TPSLDANT2" , STR0004, "string", STR0004, "TPSLDANT2" ,,,,lCanFilter) // Conta Sem Movimento
	self:oSchema:addProperty("TPSLDATU2" , STR0005, "string", STR0005, "TPSLDATU2" ,,,,lCanFilter) // Conta Sem Movimento
	self:oSchema:addProperty("LANCDEB_1" , STR0007, "number", STR0007, "LANCDEB_1" ,,,,lCanFilter) // Conta Sem Movimento
	self:oSchema:addProperty("LANCDEBTX" , STR0008, "string", STR0008, "LANCDEBTX",,,,lCanFilter) // Descricao da Conta
	self:oSchema:addProperty("LANCDEBMD" , STR0009, "string", STR0009, "LANCDEBMD",,,,lCanFilter) // Descricao da Conta
	self:oSchema:addProperty("LANCCRD_1" , STR0010, "number", STR0010, "LANCCRD_1" ,,,,lCanFilter) // Conta Sem Movimento
	self:oSchema:addProperty("LANCCRDTX" , STR0011, "string", STR0011, "LANCCRDTX",,,,lCanFilter) // Descricao da Conta
	self:oSchema:addProperty("LANCCRDMD" , STR0012, "string", STR0012, "LANCCRDMD",,,,lCanFilter) // Descricao da Conta
	self:oSchema:addProperty("TXDEBITO"  , STR0013, "number", STR0013, "TXDEBITO" ,,,,lCanFilter) // Conta Sem Movimento
	self:oSchema:addProperty("TXCREDITO" , STR0014, "number", STR0014, "TXCREDITO",,,,lCanFilter) // Descricao da Conta
	self:oSchema:addProperty("SALDOANT"  , STR0015, "number", STR0015, "SALDOANT",,,,lCanFilter) // Saldo Anterior
	self:oSchema:addProperty("SALDOANT2" , STR0016 ,"number", STR0016, "SALDOANT2",,,,lCanFilter) // Saldo Anterior
	self:oSchema:addProperty("SALDOSCR2" , STR0017 ,"number", STR0017 ,"SALDOSCR2",,,,lCanFilter   ) // Saldo
	self:oSchema:addProperty("NomeEmpresa", STR0018 ,"string" ,STR0018,"NomeEmpresa",,,,lCanFilter )  // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial" , STR0019 ,"string" ,STR0019,"NomeFilial" ,,,,lCanFilter )  // "Nome Filial"

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: self
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getDescription() as character class ledger2coinSmartViewBusinessObject
return STR0002 //"Objeto contendo informações Razão  2 Moedas"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: self
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getAreas() as array class ledger2coinSmartViewBusinessObject
Return {STR0003} //"Contabilidade"

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerRazData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getCtgGerRazData(nPage as numeric, oFilter as object) as object class ledger2coinSmartViewBusinessObject
	//Carrega lista de parametros para a memória
	CTBsetValueMVPAR(oFilter:getParameters(), "CTR410")
	self:handleCtgGerRazParams( oFilter )

	//Elimina da memória a instância do objeto informado como parâmetro
    FreeObj(oFilter:getParameters())

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerRazParams
Atribui valores aos atributos da classe principal

@param oFilter, objeto, contém o filtro do TReports
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerRazParams(oFilter as object)  class ledger2coinSmartViewBusinessObject

	local cTipo   := "1"  as character
	local cSaldo  := "1"  as character
	local lSldAnt := .F.  as logical
 
    ::cContaIni    := MV_PAR01                                  //Da Conta ?                    	
    ::cContaFim    := MV_PAR02                                  //Ate Conta ?  
    ::dDataIni     := MV_PAR03                                  //Da Data ?                     	       
    ::dDataFim     := MV_PAR04                                  //Ate a Data ?  
    ::c2Moeda      := MV_PAR05                                  //Moeda Corrente ?
    ::nTipo        := MV_PAR27
    ::lSldAnt      := lSldAnt
    ::cMoeda       := MV_PAR06                                  //Moeda ? 
    ::cSaldo       := Iif(empty(MV_PAR07),cSaldo,Alltrim(MV_PAR07))      //Tipo de Saldo ? 
    ::aSetOfBook   := IIf(empty(MV_PAR08),CTBSetOf(""),CTBSetOf(MV_PAR09))//Cod. Config. Livros ?   
    ::lAnalit      := IIf(MV_PAR09==1,.T.,.F.)                  //Tipo Relatorio ?              	       
    ::lNoMov       := Iif(MV_PAR10==1,.T.,.F.)                  //Impr. Cta S/ Movim ? 
    ::lJunta	   := Iif(MV_PAR11==1,.T.,.F.)                  //Junta C.C. Contabil ?
	::nCodConta	   := MV_PAR12                  				//Impr Cod Conta ?
    ::cCustoIni    := MV_PAR15                                  //Do Centro de Custo ?          	       
    ::cCustoFim    := MV_PAR16                                  //Ate Centro de Custo ? 
    ::cItemIni     := MV_PAR17                                  //Do Item Contabil ?            	       
    ::cItemFim     := MV_PAR18                                  //Ate Item Contabil ? 
    ::cCLVLIni     := MV_PAR19                                  //Da Classe de Valor ?          	       
    ::cCLVLFim     := MV_PAR20                                  //Ate Classe de Valor ?     
    ::lPrintZero   := Iif(MV_PAR28==1,.T.,.F.)                  //Imprime Valor 0,00 ?          	       
    ::cTipo        := cTipo                                     // Razao por Conta
	::lSldAntCC     := (mv_par13 == 1)
    ::lSldAntIt     := (mv_par13 == 2)
    ::lSldAntCv     := (mv_par13 == 3)
	::lRazao		:= .T.
return


//-------------------------------------------------------------------
/*{Protheus.doc} getSaldos
Método para carregar saldos
 
@return object: string
 
@author Renan Gremes
@since 16/09/2024
@version 1.0
*/
//------------------------------------------------------------------- 
method getSaldos() class ledger2coinSmartViewBusinessObject    

	Local cMoedaEsc := ""
	Local aSaldo	:= {} as Array
	Local aSaldoAnt := {} as Array
	
	If ::cCoinOption == "1"
		cMoedaEsc := ::c2Moeda
	Else	
		cMoedaEsc := ::cMoeda
	EndIf

    If ::lSldAntCC
		aSaldo    	:= SaldTotCT3(::cCustoIni,::cCustoFim,cArqTmp->CONTA,cArqTmp->CONTA,cArqTmp->DATAL,cMoedaEsc,::cSaldo,::aSelFil)
		aSaldoAnt 	:= SaldTotCT3(::cCustoIni,::cCustoFim,cArqTmp->CONTA,cArqTmp->CONTA,::dDataIni,cMoedaEsc,::cSaldo,::aSelFil)
	ElseIf ::lSldAntIt
		aSaldo    	:= SaldTotCT4(::cItemIni,::cItemFim,::cCustoIni,::cCustoFim,cArqTmp->CONTA,cArqTmp->CONTA,cArqTmp->DATAL,cMoedaEsc,::cSaldo,::aSelFil)
		aSaldoAnt 	:= SaldTotCT4(::cItemIni,::cItemFim,::cCustoIni,::cCustoFim,cArqTmp->CONTA,cArqTmp->CONTA,::dDataIni,cMoedaEsc,::cSaldo,::aSelFil)
	ElseIf ::lSldAntCv
		aSaldo    	:= SaldTotCTI(::cClVlIni,::cClVlFim,::cItemIni,::cItemFim,::cCustoIni,::cCustoFim,cArqTmp->CONTA,cArqTmp->CONTA,cArqTmp->DATAL,cMoedaEsc,::cSaldo,::aSelFil)
		aSaldoAnt 	:= SaldTotCTI(::cClVlIni,::cClVlFim,::cItemIni,::cItemFim,::cCustoIni,::cCustoFim,cArqTmp->CONTA,cArqTmp->CONTA,::dDataIni,cMoedaEsc,::cSaldo,::aSelFil)		
	Else        
		aSaldo 		:= SaldoCT7(cArqTmp->CONTA,cArqTmp->DATAL,cMoedaEsc,::cSaldo)
		aSaldoAnt	:= SaldoCT7(cArqTmp->CONTA,::dDataIni,cMoedaEsc,::cSaldo,"CTBR400")
	EndIf

	If ::cCoinOption == "1"
		::aSaldo 	:= aSaldo
		::aSaldoAnt :=aSaldoAnt
	Else	
		::aSaldo2 	:= aSaldo
		::aSaldoAnt2 :=aSaldoAnt
	EndIf

return


//-------------------------------------------------------------------
/*{Protheus.doc} preData
Método preData de internacionalização
 
@return object: string
 
@author Ewerton Franklin
@since 28/11/2024
@version 1.0
*/
//------------------------------------------------------------------- 

method preData() as object class ledger2coinSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Método preData de internacionalização
 
@return object: string
 
@author Ewerton Franklin
@since 28/11/2024
@version 1.0
*/
//------------------------------------------------------------------- 

method preSchema() as object class ledger2coinSmartViewBusinessObject
return self:oSchema


//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método processData de internacionalização
 
@return object: string
 
@author Ewerton Franklin
@since 28/11/2024
@version 1.0
*/
//------------------------------------------------------------------- 

method processData() as json class ledger2coinSmartViewBusinessObject
return ::jDados
