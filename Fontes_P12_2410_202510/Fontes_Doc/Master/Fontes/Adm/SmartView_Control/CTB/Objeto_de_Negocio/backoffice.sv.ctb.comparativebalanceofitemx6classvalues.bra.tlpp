#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.comparativebalanceofitemx6classvalues.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CTD, CTH, CTM, CTN", name="Balancete Comparativo de Item x 6 Classe Valores", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} ComparativeBalanceOfItemx6ClassValuesSmartViewBusinessObject
Classe para a criação do Objeto de Negócio do relatório
Balancete Comparativo de Item x 6 Classe Valores para o TReports
 
@author Renan Gremes
@since 14/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class ComparativeBalanceOfItemx6ClassValuesSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartViewCtGerComp

    public method new()                     as object
    public method getDescription()          as character
    public method processCustomData()       as json

    public method processData()             as json
    public method preSchema()               as object
    public method preData()                 as object 

    public method getCtGerCompData()        as object
    public method getCtGerCompSchema()      as object
    
    public method vldExeCtgGerComp()        as logical
    public method ExecDBSkipTemp()          as logical

    public method handleCtGerCompParams()
    public method handleCtGerCompAppend()   as object
    public method handleExeCtGerComp()      as object 

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 14/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class ComparativeBalanceOfItemx6ClassValuesSmartViewBusinessObject
    _Super:new()    
    self:setDisplayName(STR0001) // "Balancete Comparativo de Item x 6 Classe Valores"
    self:setPergunte("CTR300")   // Indica o pergunte que será utilizado no relatório
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 14/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getDescription() as character class ComparativeBalanceOfItemx6ClassValuesSmartViewBusinessObject
return STR0002 // "Objeto contendo informacoes sobre o Balancete Comparativo de Item x 6 Classe Valores"

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preData() as object class ComparativeBalanceOfItemx6ClassValuesSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preSchema() as object class ComparativeBalanceOfItemx6ClassValuesSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method processData() as json class ComparativeBalanceOfItemx6ClassValuesSmartViewBusinessObject
return ::jItens

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerCompData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 14/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getCtGerCompData(nPage as numeric, oFilter as object) as object class ComparativeBalanceOfItemx6ClassValuesSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR300")
    self:handleCtGerCompParams( oFilter )
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerCompSchema
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Renan Gremes
@since 14/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtGerCompSchema() as object class ComparativeBalanceOfItemx6ClassValuesSmartViewBusinessObject
    
    local lCanFilter := .F. as logical
    
    //Adição de novos campos ao schema    
    self:oSchema:addProperty("VLRTOTAL",    STR0005, "string", STR0005, "VLRTOTAL",,,,lCanFilter)     // Valor Total

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Deverá ser chamado no momento do processamento da query
 
@return object: self:oData
 
@author Renan Gremes
@since 14/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method processCustomData() as json class ComparativeBalanceOfItemx6ClassValuesSmartViewBusinessObject
    
    local cPicture  as character
    local nDecimais as numeric    

    nDecimais := DecimalCTB(::aSetOfBook, MV_PAR14)
    cPicture  := ::aSetOfBook[4]

    //Manipulação de conteúdo do json
    ::jItens["VLRTOTAL"]   := ValorCTB(cArqTmp->(COLUNA1+COLUNA2+COLUNA3+COLUNA4+COLUNA5+COLUNA6),,,17,nDecimais,CtbSinalMov(),cPicture, cArqTmp->NORMAL, , , , , ,::lPrintZero,.F./*lSay*/)

return ::jItens

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtGerCompParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Renan Gremes
@since 14/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtGerCompParams( oFilter as object ) class ComparativeBalanceOfItemx6ClassValuesSmartViewBusinessObject    

    local cClVlIni   as character
    local cClVlFim   as character
    local cSaldos    as character
    local cPergClVl  as character
    local cMoeda     as character
    local cSegAte    as character
    local lImpSint   as logical  
    local lVlrZerado as logical
    local lImpAntLP  as logical
    local lPrintZero as logical
    local nDivide    as numeric
    local nCont      := 0 as numeric
    local nPergClVl	 := 4 as numeric //Definido com 4, porque a primeira perg. de cl.valor incia com o mv_par05
    local aSetOfBook as array
    local aClVl      := {} as array
    local aTamClVl   := TAMSX3("CTH_CLVL") as array

    //Roda MV_PAR05 a MV_PAR10 e adiciona no array
    For nCont := 1 to 6
        cPergClVl	:= &("mv_par"+Strzero(nPergClVl+nCont,2))
        if Empty(cPergClVl)
            aAdd(aClVl,space(aTamClVl[1]))
        else
            aAdd(aClVl,cPergClVl)
        endif
    Next nCont
    
    //Define padrão de branco a ZZZ
    cClVlIni     := Space(aTamClVl[1])
    cClVlFim     := Repl("Z",aTamClVl[1])

    //Validação dos valores
    lImpSint     := Iif(MV_PAR11==1 .Or. MV_PAR11==3,.T.,.F.)
    aSetOfBook   := Iif(!Empty(MV_PAR12), CTBSetOf(MV_PAR12), CTBSetOf(""))
    lVlrZerado   := Iif(MV_PAR13==1,.T.,.F.)
    cMoeda       := Iif(Empty(MV_PAR14), "01", MV_PAR14)
    cSaldos      := Iif(!Empty(MV_PAR16), PadR(AllTrim(MV_PAR16), TamSX3("CT2_TPSALD")[1]), "1")
    cSegAte      := MV_PAR17
    lPrintZero   := Iif(MV_PAR25==1,.T.,.F.)
    nDivide      := self:CompVld(MV_PAR26)
    lImpAntLP    := Iif(MV_PAR27==1,.T.,.F.)
   
    //Atribuição dos parâmetros nos atributos  
    self:dDataIni    := MV_PAR01    // Data Inicial 
    self:dDataFim    := MV_PAR02    // Data Final
    self:cItemIni    := MV_PAR03    // Item Inicial
    self:cItemFim    := MV_PAR04    // Item Final
    self:aEntid      := aClVl       // Cl.Valor (01 a 06)
    self:cClVlIni    := cClVlIni    // Cl. Valor "branco"
    self:cClVlFim    := cClVlFim    // Cl. Valor "ZZZ"
    self:lImpSint    := lImpSint    // Imprime Itens: 1=Sintetico;2=Analitico;3=Ambas
    self:aSetOfBook  := aSetOfBook  // Cod. Config. Livros
    self:lVlrZerado  := lVlrZerado  // Saldos Zerados?
    self:cMoeda      := cMoeda      // Moeda?      
    self:cSaldos     := cSaldos     // Saldos? 1=Reais;2=Orcados;3=Gerenciais    
    self:cSegmento   := MV_PAR18    // Filtra Segmento?
    self:cSegIni     := MV_PAR19    // Conteudo Inicial Segmento?
    self:cSegFim     := MV_PAR20    // Conteudo Final Segmento?
    self:cFiltSegm   := MV_PAR21    // Conteudo Contido em?
    self:lPrintZero  := lPrintZero  // Imprime Valor 0.00? 
    self:nDivide     := nDivide     // Divide por?
    self:lImpAntLP   := lImpAntLP   // Posicao Ant. L/P? 1=Sim;2=Nao
    self:dDataLP     := MV_PAR28    // Data Lucros/Perdas?

    //Atribuição de lalores fixos nos atributos
    self:cAlias      := "CTX"
    self:lImpConta   := .F.    
    self:cTpVlr      := "M"
    self:lMeses      := .F.
    self:lEntid      := .T.    
    self:cHeader     := "CTD"
    self:lNImpMov    := .F.
    self:lImpTotS    := .F.
    self:cString     := "CTD"
    self:lFiliais    := .F.    
    self:cFilUSU     := ".T."

return

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtGerCompAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 14/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtGerCompAppend(oFilter as object) as object class ComparativeBalanceOfItemx6ClassValuesSmartViewBusinessObject
    //execucao do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtGerComp
Método para execução do CTGERCOMP

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 14/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleExeCtGerComp(oFilter as object) as object class ComparativeBalanceOfItemx6ClassValuesSmartViewBusinessObject
return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para validar se pula linha no append
 
@return logico: lRet
 
@author Renan Gremes
@since 14/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method ExecDBSkipTemp() as logical class ComparativeBalanceOfItemx6ClassValuesSmartViewBusinessObject
    
    local lRet       := .F. as logical

    //Se pula sintetica e o tipo do item é sintetico    
    if MV_PAR11 == 1					// So imprime Sinteticas
        lRet := (cArqTmp->TIPOITEM == "2")
    elseif MV_PAR11 == 2				// So imprime Analiticas        
        lRet := (cArqTmp->TIPOITEM == "1")
    endif


    if !lRet
        if ::lVlrZerado  .And. (Abs(cArqTmp->COLUNA1)+Abs(cArqTmp->COLUNA2)+Abs(cArqTmp->COLUNA3)+Abs(cArqTmp->COLUNA4)+Abs(cArqTmp->COLUNA5)+Abs(cArqTmp->COLUNA6))==0
            if CtbExDtFim("CTD")
                dbSelectArea("CTD")
                dbSetOrder(1)
                if MsSeek(xFilial()+cArqTmp->ITEM)
                    if !CtbVlDtFim("CTD",MV_PAR01)
                        lRet := .T.
                    endif
                endif
            endif
        endif
    endif

return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} vldExeCtgGerComp
Método para validar se irá executar o gercomp e appendar os dados
 
@return logico: lRet
 
@author Renan Gremes
@since 14/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method vldExeCtgGerComp() as logical class ComparativeBalanceOfItemx6ClassValuesSmartViewBusinessObject

    local lRet := .T. as logical
    local aCtbMoeda   as array

    //Valida a moeda informada
    aCtbMoeda  	:= CtbMoeda(::cMoeda,::nDivide)
    if Empty(aCtbMoeda[1])
        lRet := .F.
    endif

return lRet
