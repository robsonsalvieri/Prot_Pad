#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.ledgerbyaccountingitem.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT2, CTT, CTD, CTH, CTZ, CTO", name="Razão Item Contábil", country="ALL", ,)


//-------------------------------------------------------------------------------
/*{Protheus.doc} ledgerbyaccountingitemSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Razão Item Contábil para o TReports
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class ledgerbyaccountingitemSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbgerraz

    public method new() as object
    public method getDescription()     as character
    public method getAreas()           as array

    public method preData()            as object
    public method preSchema()          as object
    public method processData()        as json

    public method processCustomData()  as json
    public method getCtGerRazSchema()  as object
    public method getCtgGerRazData()   as object
    public method handleCtgGerRazParams() 
  
    protected data oIntegratedProvider as object    
    protected data nSaldoAtu as numeric
    protected data aSaldo    as array
    protected data aSaldoAnt as array
    protected data  cItemAnt as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method new() as object Class ledgerbyaccountingitemSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Razão Item Contábil" 
    self:setPergunte("CTR480") //Indica o pergunte que será utilizado no relatório
    
    ::nSaldoAtu     := 0
    ::cItemAnt      := ""
    ::aSaldo        := {}
    ::aSaldoAnt     := {}
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Método preData de internacionalização
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method preData() as object class ledgerbyaccountingitemSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Método preSchema de internacionalização
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method preSchema() as object class ledgerbyaccountingitemSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método processData de internacionalização
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method processData() as json class ledgerbyaccountingitemSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method processCustomData() as json class ledgerbyaccountingitemSmartViewBusinessObject
    
    local cMoedaDesc    as character
    local cDescItem     as character
    Local cPicture      as character 
    Local nSaldoAnt     as Numeric
    Local nDecimais 	as numeric
    Local TAM_VALOR := 17 as Numeric

    //Carrega os valores default    
    cMoedaDesc  := ::cMoeda
    cDescItem   := " "

    cPicture    := ::aSetOfBook[4]
    nDecimais   := DecimalCTB(::aSetOfBook, ::cMoeda)
  
    If CTD->(dbSeek(FWxFilial("CTD")+cArqTMP->ITEM))
        cDescItem := &("CTD->CTD_DESC" + cMoedaDesc)
    Endif

    //Se trocou item, pega saldo
    if cArqTmp->ITEM <> ::cItemAnt
        ::aSaldoAnt	:= SaldTotCT4(cArqTMP->ITEM,cArqTMP->ITEM,::cCustoIni,::cCustoFim,::cContaIni,::cContaFim,::dDataIni,::cMoeda,::cSaldo,::aSelFil)
	    ::aSaldo	:= SaldTotCT4(cArqTMP->ITEM,cArqTMP->ITEM,::cCustoIni,::cCustoFim,::cContaIni,::cContaFim,cArqTmp->DATAL,::cMoeda,::cSaldo,::aSelFil)
        ::nSaldoAtu := ::aSaldoAnt[6]
        nSaldoAnt   := ::aSaldo[6]
    endif
    nSaldoAnt := ::nSaldoAtu
    ::nSaldoAtu := ::nSaldoAtu - cArqTmp->LANCDEB + cArqTmp->LANCCRD

    //Manipulação de conteúdo do json
    ::jDados["SALDANT"]    := nSaldoAnt
    ::jDados["TPSLDANT"]   := ValorCTB(nSaldoAnt,,,TAM_VALOR,nDecimais,.T.,cPicture,"",,,,,,::lPrintZero,.F.)
    ::jDados["SALDOSCR"]   := ::nSaldoAtu
    ::jDados["TPSLDATU"]   := ValorCTB(::nSaldoAtu,,,TAM_VALOR,nDecimais,.T.,cPicture,"",,,,,,::lPrintZero,.F.)
    ::jDados["DESCITEM"]   := cDescItem
    ::jDados["NomeEmpresa"]:= ::cCompanyName
    ::jDados["NomeFilial" ]:= ::cBranchName
    ::cItemAnt := cArqTmp->ITEM

return

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getDescription() as character class ledgerbyaccountingitemSmartViewBusinessObject
return STR0002 //"Objeto contendo informações Razão Item Contábil"
//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getAreas() as array class ledgerbyaccountingitemSmartViewBusinessObject
Return {STR0003} //"Contabilidade" 

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerRazData
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getCtgGerRazData(nPage as numeric, oFilter as object) as object class ledgerbyaccountingitemSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR480")
    self:handleCtgGerRazParams( oFilter )
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerRazSchema
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getCtGerRazSchema() as object class ledgerbyaccountingitemSmartViewBusinessObject
    local lCanFilter := .F. as logical

    self:oSchema:addProperty("DESCITEM"   , STR0035, "string", STR0035, "DESCITEM" ,,,,lCanFilter) 
    self:oSchema:addProperty("SALDANT"    , STR0038 ,"number", STR0038, "SALDANT" ,,,,lCanFilter)//"Saldo Anterior"
    self:oSchema:addProperty("NomeEmpresa", STR0036 ,"string" ,STR0036,"NomeEmpresa"  ,,,,.F. )  // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial" , STR0037 ,"string" ,STR0037,"NomeFilial"   ,,,,.F. )  // "Nome Filial"
    self:oSchema:addParameter("SV_MULTBRANCH", "Filiais", "string", .T.) //str000
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)  

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerRazParams
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method handleCtgGerRazParams(oFilter as object)  class ledgerbyaccountingitemSmartViewBusinessObject

    local cMoeda :="01"    as character
    local cTipo  :="3"     as character
    local cSaldo :="1"     as character
    local aSelFil:={}      as array

    ::oIntegratedProvider := ControlIntegratedProvider():New()

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    if Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    endif

    ::cItemIni     := MV_PAR01 //Do Item Contabil ?            	       
    ::cItemFim     := MV_PAR02 //Ate Item Contabil ? 
    ::dDataIni     := MV_PAR03 //Da Data ?
    ::dDataFim     := MV_PAR04 //Ate a Data ?
    ::cMoeda       := Iif(empty(MV_PAR05),cMoeda,MV_PAR05) //Moeda ?
    ::cSaldo       := Iif(empty(MV_PAR06),cSaldo,MV_PAR06)//Tipo de Saldo ?
    ::aSetOfBook   := IIf(empty(MV_PAR07),CTBSetOf(""),CTBSetOf(MV_PAR07))//Cod. Config. Livros ?
    ::lAnalit      := IIf(MV_PAR08==1,.T.,.F.) //Tipo Relatorio ?
    ::lNoMov       := Iif(MV_PAR09==1,.T.,.F.)//Impr. Cta S/ Movim ?
    ::nCodConta	   := MV_PAR10 //Imprime Cod. Conta ?
    ::lSldAnt      := .T.
    ::cContaIni    := MV_PAR12 //Da Conta ?
    ::cContaFim    := MV_PAR13 //Ate Conta ?
    ::cCustoIni    := MV_PAR15 //Do Centro de Custo ?
    ::cCustoFim    := MV_PAR16 //Ate Centro de Custo ?
    ::cCLVLIni     := MV_PAR18 //Da Classe de Valor ?
    ::cCLVLFim     := MV_PAR19 //Ate Classe de Valor ?
    ::lCCRes       := iif(MV_PAR24 == 2, .T., .F.)  // Impr Cod C.Custo ?
    ::lItemRes     := iif(MV_PAR25 == 2, .T., .F.)  // Impr Cod Item ?
    ::lClVlRes     := iif(MV_PAR26 == 2, .T., .F.)  // Impr Cod Cl.Valor ?
    ::lPrintZero   := .F.//Imprime Valor 0,00 ?
    ::aSelFil      := aSelFil //Seleciona Filiais ?
    ::cTipo        := cTipo//// Razao por Conta

return
