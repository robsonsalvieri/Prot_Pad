#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.demonstrativeresult.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT2", name="Demonstrativos De Resultado (DRE)", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} demonstrationcashflowSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Demonstrativos De Fluxo de Caixa 
para o SmartView
 
@author Ednilson Queiroz
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
class demonstrativeresultSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions

	public method new()                     as object
	public method getDescription()          as character

	//usados na internacionalizacao
    public method preschema()      			as object 
    public method preData()        			as object
    public method processData()    			as json

	public method getCtgGerPlanData()       as object
	public method getCtGerPlanSchema()      as object

	public method vldExeCtgGerPlan()		as logical

	public method handleCtgGerPlanParams()
	public method handleExeCtgGerPlan()     as object
	public method handleCtgGerPlanAppend()  as object

	public method processCustomData()       as json

	protected data cTpValor as character

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Ednilson Queiroz
@since 04/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class demonstrativeresultSmartViewBusinessObject

	_Super:new()
	self:setDisplayName(STR0001) // Demonstrativos De Fluxo de Caixa
	self:setPergunte("CTR510") //Indica o pergunte que será utilizado no relatório

	::cTpValor := SuperGetMV("MV_TPVALOR")
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return string
 
@author Ednilson Queiroz
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getDescription() as character class demonstrativeresultSmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre o Demonstrativos De Fluxo de Caixa"

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Ednilson Queiroz
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method preData() as object class demonstrativeresultSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Ednilson Queiroz
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method preSchema() as object class demonstrativeresultSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Ednilson Queiroz
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method processData() as json class demonstrativeresultSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Ednilson Queiroz
@since 04/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getCtGerPlanSchema() as object class demonstrativeresultSmartViewBusinessObject
	local lCanFilter := .F. as logical

	///Adição de novos campos ao schema
	self:oSchema:addProperty("SINALMOEDA" ,STR0004, "string", STR0004, "SINALMOEDA" ,,,,lCanFilter) // Sinal Moeda
	self:oSchema:addProperty("dFinal"     ,STR0005, "date"  , STR0005, "dFinal"     ,,,,lCanFilter) // Data Final
	self:oSchema:addProperty("dFinalA"    ,STR0006, "date"  , STR0006, "dFinalA"    ,,,,lCanFilter) // Data Inicial
	self:oSchema:addProperty("NomeEmpresa",STR0007, "string", STR0007, "NomeEmpresa",,,,lCanFilter) // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial" ,STR0008, "string", STR0008, "NomeFilial" ,,,,lCanFilter) // "Nome Filial"

	self:oSchema:addParameter("SV_MULTBRANCH", STR0009, "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Ednilson Queiroz
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class demonstrativeresultSmartViewBusinessObject
	//Carrega lista de parametros para a memória
	CTBsetValueMVPAR(oFilter:getParameters(), "CTR510")
	self:handleCtgGerPlanParams( oFilter )

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Usado para appendar dados ao schema. Chamado no GetData da classe pai

@return object: self:oData

@author Ednilson Queiroz
@since 04/12/2023
@version 1.0*/
//-------------------------------------------------------------------
method processCustomData() as json class demonstrativeresultSmartViewBusinessObject
	Local cPicture  as character
	Local aCtbMoeda as array
	Local aTamVal   := TAMSX3("CT2_VALOR") as array
	Local nSaldoAtu := 0 as numeric
	Local nSaldoAnt := 0 as numeric
	Local nDecimais := 0 as numeric

	aCtbMoeda := CtbMoeda(::cMoedaDsc)
	nDecimais := DecimalCTB(::aSetOfBook, MV_PAR03)
	cPicture  := ::aSetOfBook[4]

	nSaldoAtu := iif(::lMovPeriodo, cArqTmp->(SALDOATU-SALDOANT), cArqTmp->SALDOATU)
	nSaldoAnt := iif(::lMovPeriodo, cArqTmp->MOVPERANT, cArqTmp->SALDOANT)

	::jDados["SALDOATU"] 	:= nSaldoAtu
	::jDados["SALDOATU_M"] 	:= ValorCTB(nSaldoAtu,,,aTamVal[1],nDecimais,.T.,cPicture,cArqTmp->NORMAL,cArqTmp->CONTA,,,::cTpValor,,,.F. )

	::jDados["SALDOANT"] 	:= nSaldoAnt
	::jDados["SALDOANT_M"] 	:= ValorCTB(nSaldoAnt,,,aTamVal[1],nDecimais,.T.,cPicture,cArqTmp->NORMAL,cArqTmp->CONTA,,,::cTpValor,,,.F. )

	::jDados["SINALMOEDA"]  := aCtbMoeda[3]
	::jDados["dFinalA"]     := iif(!empty(::dDataIni), FwTimeStamp(5, ::dDataIni-1, "00:00:00"), nil)
	::jDados["dFinal"]      := iif(!empty(::dDataFim), FwTimeStamp(5, ::dDataFim, "00:00:00"), nil)
	::jDados["NomeEmpresa"] := self:cCompanyName
    ::jDados["NomeFilial" ] := self:cBranchName
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Ednilson Queiroz
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleExeCtgGerPlan(oFilter as object) as object class demonstrativeresultSmartViewBusinessObject
	//execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Ednilson Queiroz
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanAppend(oFilter as object) as object class demonstrativeresultSmartViewBusinessObject
	//execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Ednilson Queiroz
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanParams( oFilter ) class demonstrativeresultSmartViewBusinessObject

	Local cTpSald := "1"  as character
	Local cMoeda  := "01" as character
	Local cCodLivro       as character
	Local cSaldos 	  	  as character
	Local dFinal          as date
	Local dFinalA         as date
	Local lMovPeriodo     as logical
	Local nDivide		  as numeric
	Local aSelFil		  as array

	lMovPeriodo := mv_par13 == 1
	lDemAnt 	:= iif(MV_PAR06 == 1, .T., .F.)
	dFinal      := StoD("")

	If lDemAnt
		dFinalA := MV_PAR20
	Else
		dFinalA :=StoD("")
	Endif

	If MV_PAR17 == 1
		cSaldos := IIf(!empty(MV_PAR18), MV_PAR18, cTpsald)
	Else
		cSaldos := IIf(!empty(MV_PAR15), MV_PAR15, cTpsald)
	EndIf

	::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf
	
	If mv_par09 == 1
		// SE DEVE CONSIDERAR TODO O CALENDARIO
		CTG->(DbSeek(xFilial("CTG")+mv_par01))
		dbSelectArea("CTG")
		dbSetOrder(1)
		MsSeek(xFilial("CTG")+mv_par01,.T.)

		If Empty(mv_par08)
			While CTG->CTG_FILIAL = xFilial("CTG") .And. CTG->CTG_CALEND = mv_par01
				dFinal	:= CTG->CTG_DTFIM
				CTG->(DbSkip())
			EndDo
		Else
			dFinal	:= mv_par08
		EndIf

		If !Empty(dFinalA)
			If CTG->(DbSeek(xFilial() + mv_par01))
				dFinalA		:= MV_PAR20
			EndIf
		Else
			dFinalA   	:= Ctod(Left(Dtoc(dFinal), 6) + Str(Year(dFinal) - 1, 4))
			If Empty ( dFinalA )
				If MONTH(dFinal) == 2
					If Day(dFinal) > 28 .and. Day(dFinal) == 29
						dFinalA := Ctod(Left( STRTRAN ( Dtoc(dFinal) , "29" , "28" ), 6) + Str(Year(dFinal) - 1, 4))
					EndIf
				EndIf
			EndIf
		EndIf

		mv_par01    := dFinal
	Else

		dFinal := mv_par08

		CTG->(dbSetOrder(1))
		CTG->(DbSeek(xFilial("CTG")+mv_par01))
		While CTG->CTG_FILIAL == xFilial("CTG") .And. CTG->CTG_CALEND == mv_par01
			If dFinal >= CTG->CTG_DTINI .and. dFinal <= CTG->CTG_DTFIM
				dFinalA		:= CTG->CTG_DTINI
				If lMovPeriodo
					dFinalA		:= dFinalA - 1
				EndIf
				Exit
			Endif
			CTG->(DbSkip())
		EndDo
		
        If !Empty(dFinalA)
			If CTG->(DbSeek(xFilial() + mv_par01))
				dFinalA		:= MV_PAR20
			EndIf
		Else
			dFinalA   	:= Ctod(Left(Dtoc(dFinal), 6) + Str(Year(dFinal) - 1, 4))
			If Empty ( dFinalA )
				If MONTH(dFinal) == 2
					If Day(dFinal) > 28 .and. Day(dFinal) == 29
						dFinalA := Ctod(Left( STRTRAN ( Dtoc(dFinal) , "29" , "28" ), 6) + Str(Year(dFinal) - 1, 4))
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf
	
	// Tratamento do parâmetro Divide por ?
    if MV_PAR21 == 1 		// Não se aplica
	    nDivide  := 1
    elseif MV_PAR21 == 2	// Divide por cem
		nDivide  := 100
	elseif MV_PAR21 == 3	// Divide por mil
		nDivide  := 1000
	elseif MV_PAR21 == 4	// Divide por milhao
		nDivide  := 1000000
	endif

	cCodLivro       := MV_PAR02
	::dDataIni      := dFinalA+1
	::dDataFim      := dFinal
	::cContaIni     := Space(Len(CriaVar("CT1_CONTA")))
	::cContaFim     := Repl('Z',Len(CriaVar("CT1_CONTA")))
	::cCCIni        := Space(Len(CriaVar("CTT_CUSTO")))
	::cCCFim        := Repl('Z',Len(CriaVar("CTT_CUSTO")))
	::cItemIni      := Space(Len(CriaVar("CTD_ITEM")))
	::cItemFim      := Repl('Z',Len(CriaVar("CTD_ITEM")))
	::cClvlIni      := Space(Len(CriaVar("CTH_CLVL")))
	::cClvlFim      := Repl('Z',Len(CriaVar("CTH_CLVL")))
	::cMoeda        := IIf(!empty(MV_PAR03), MV_PAR03, cMoeda)
	::cSaldos       := PadR(AllTrim(cSaldos), TamSX3("CT2_TPSALD")[1])
	::aSetOfBook    := iIf(!Empty(cCodLivro), CTBSetOf(cCodLivro), CTBSetOf(""))
	::cSegIni       := ""
	::cSegFim       := Repl("Z", 20)
	::lImpAntLP     := IIf(MV_PAR04 == 1, .T., .F.)
	::dDataLP       := Iif(::lImpAntLP, MV_PAR05, CTOD("  /  /  "))
	::nDivide       := nDivide
	::lVlrZerado    := IIf(MV_PAR12 == 1, .T., .F.)
	::cMoedaDsc     := IIf(!empty(MV_PAR14), MV_PAR14, cMoeda)	
	::aSelFil       := aSelFil	
	::lMovPeriodo   := MV_PAR13 == 1
	::lPlGerSint    := .T.
	::lConsSaldo    := IIf(MV_PAR17 == 1, .T., .F.) 
	::lDemDRE       := !Empty(MV_PAR20)
	::dFinalA		:= dFinalA
	::aSetOfBook[9] := ::nDivide

	If ::lMovPeriodo
		self:lPeriodo0 := .T.
		::dPeriodo0 := Ctod(Left(Dtoc(dFinal), 6) + Str(Year(dFinal) - 2, 4)) + 1
	EndIf

return

//-------------------------------------------------------------------
/*{Protheus.doc} vldExeCtgGerPlan
Valida se executa a função CtgGerPlan

@param lRet, lógico, contém o filtro do TReports
 
@author Ednilson Queiroz
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method vldExeCtgGerPlan() class demonstrativeresultSmartViewBusinessObject

	Local lRet := .T.

	if Empty(mv_par02)
		lRet:=.F.
	Elseif !VdSetOfBook( mv_par02 , .F. ) .or. Empty(::aSetOfBook[5])
		lRet := .F.
	Endif

Return lRet
