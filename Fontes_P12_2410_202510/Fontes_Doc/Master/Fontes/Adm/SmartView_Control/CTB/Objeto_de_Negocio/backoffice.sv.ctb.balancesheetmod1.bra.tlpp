#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancesheetmod1.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGACTB",tables="CT1,CT2", name="Balanço Modelo 1", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BalanceSheetMod1SmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balanço Modelo 1 para o TReports
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
class BalanceSheetMod1SmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions
   
    public method new()                     as object
    public method getDescription()          as character

    //usados na internacionalização
    public method preData()                 as object
    public method preSchema()               as object
    public method processData()             as json

    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object
    public method handleCtgGerPlanParams() 

    public method ExecDBSkipTemp()          as logical
    public method processCustomData()       as json

    private method f150Soma() as numeric
    
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author         Gustavo Campos
@since          26/09/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method new() as object class BalanceSheetMod1SmartViewBusinessObject
    _Super:new()

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) // "Balanço Modelo 1"
    self:setPergunte("CTR150") //Indica o pergunte que será utilizado no relatório

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
@return object: string
@author         Gustavo Campos
@since          26/09/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getDescription() as character class BalanceSheetMod1SmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre o Balanço Modelo 1"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.

    @author Renan Gremes
    @since 16/12/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class BalanceSheetMod1SmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema

    @author Renan Gremes
    @since 16/12/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class BalanceSheetMod1SmartViewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.

    @author Renan Gremes
    @since 16/12/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class BalanceSheetMod1SmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Deverá ser chamado no momento do processamento da query
@return object: self:oData
@author         Renato July
@since          NOV/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method processCustomData() as json class BalanceSheetMod1SmartViewBusinessObject
    local lTotGSint := Iif(MV_PAR24 == 2, .T., .F.)	as logical //Define se ira imprimir o total geral pelas contas analiticas ou sinteticas
    local cPicture  as character
    local TAM_VALOR := 17 as numeric
    local nTotDeb   as numeric
    local nTotCrd   as numeric
    local nDecimais as numeric

    nDecimais := DecimalCTB(::aSetOfBook,::cMoeda) //DecimalCTB(aSetOfBook,mv_par08)
    cPicture  := Iif(!Empty(::aSetOfBook[4]),"@E 999,999,999,999.99","")

    nTotDeb := self:f150Soma("D",lTotGSint)
	nTotCrd := self:f150Soma("C",lTotGSint)

    ::jDados["SALDODEB"	 ] := nTotDeb
    ::jDados["SALDODEB_M"] := ValorCTB(nTotDeb,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)
    ::jDados["SALDOCRD"	 ] := nTotCrd
    ::jDados["SALDOCRD_M"] := ValorCTB(nTotCrd,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)

return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
@return array: Array com a estrutura dos campos
@author         Gustavo Campos
@since          26/09/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getCtGerPlanSchema() as object class BalanceSheetMod1SmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
@return object: ::oData
@author         Gustavo Campos
@since          26/09/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceSheetMod1SmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBSetValueMVPAR(oFilter:getParameters(), "CTR150")
    self:handleCtgGerPlanParams( oFilter )
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN
@param oFilter, objeto, contém o filtro do TReports
@return object: ::oData
@author         Gustavo Campos
@since          26/09/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method handleExeCtgGerPlan(oFilter as object) as object class BalanceSheetMod1SmartViewBusinessObject
//execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append
@param oFilter, objeto, contém o filtro do TReports
@return object: ::oData
@author         Gustavo Campos
@since          26/09/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method handleCtgGerPlanAppend(oFilter as object) as object class BalanceSheetMod1SmartViewBusinessObject
//execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados
@param oFilter, objeto, contém o filtro do TReports
@author         Gustavo Campos
@since          26/09/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method handleCtgGerPlanParams( oFilter ) class BalanceSheetMod1SmartViewBusinessObject
   
    local cContaIni         as Character
    local cContaFim         as Character
    local cMoeda  :=  "01"  as Character
    local cTpsald :=  "1"   as Character
    
    local nIniPage          as Numeric
    Local nDivide := 1      as numeric

    local lImpAntLP         as logical
    Local lImpSint          as Logical 
    Local lPrintZero        as Logical
    Local lSldZerado        as Logical
    
    local aSetOfBook        as array
    local aSelFil           as array

    local dDataIni          as Date
    local dDataFim          as Date
    local dDataLP           as Date
    
    ///Parametros do relatorio
    dDataIni    := MV_PAR01                                              // Data Inicial ?                	
    dDataFim    := MV_PAR02                                              // Data Final ?                  	
    cContaIni   := MV_PAR03                                              // Conta Inicial ?               	
    cContaFim   := MV_PAR04                                              // Conta Final ?                 	
    lImpSint    := iIf(MV_PAR05==1 .Or. MV_PAR05==3,.T.,.F.)             // Imprime Contas ?              	
    aSetOfBook  := iIf(!Empty(MV_PAR06),CTBSetOf(MV_PAR06),CTBSetOf("")) // Cod. Config. Livros ?
    lSldZerado  := iIf(MV_PAR07==1,.T.,.F.)                              // Saldos Zerados ?
    cMoeda      := iIf(!empty(MV_PAR08),MV_PAR08,cMoeda)                 // Moeda ?
    nIniPage    := MV_PAR09                                              // Folha  Inicial ?    
    cTpsald     := iif(!Empty(MV_PAR10),PadR(AllTrim(MV_PAR10), TamSX3("CT2_TPSALD")[1]),cTpsald) // Tipo de Saldo ?
    lPrintZero  := Iif(MV_PAR17==1,.T.,.F.)                              // Imprime Valor 0.00  ?
    lImpCod     := Iif(MV_PAR18==1,.T.,.F.)                              // Imprime Codigo ?
    nDivide     := MV_PAR19                                              // Divide por ?
    lImpAntLP   := Iif(MV_PAR21 == 1,.T.,.F.)                             // Posicao Ant. L/P ?
    dDataLP     := MV_PAR22                                               // Data Lucros/Perdas ?   
    aSelFil     := {xFilial('CT2')}
    
    self:dDataIni       := dDataIni
    self:dDataFim       := dDataFim
    self:cAlias         := "CT7"//são obrigatórios
    self:cIdent         := ""//são obrigatórios   
    self:cContaIni      := cContaIni
    self:cContaFim      := cContaFim
    self:cMoeda         := cMoeda
    self:cSaldos        := cTpsald
    self:aSetOfBook     := aSetOfBook
    self:lNImpMov       := .F.
    self:lImpConta      := .F.
    self:lImpAntLP      := lImpAntLP     
    self:dDataLP        := dDataLP  
    self:nDivide        := nDivide
    self:lVlrZerado     := lSldZerado
    self:aSelFil        := aSelFil
    self:lTodasFil      := .F.
    self:lImpSint       := lImpSint
    
return 

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para pular linha
 
@param cArqTmp, caracter, indica o alias do arquivo temporário
 
@return lógico
 
@author Renan Gremes
@since 16/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method ExecDBSkipTemp(cArqTmp As character) as logical class BalanceSheetMod1SmartViewBusinessObject
   
    local lRet := .T.
    
    If MV_PAR05 == 1					// So imprime Sinteticas
		If cArqTmp->TIPOCONTA == "2"
			lRet := .F.
		EndIf
	EndIf

return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} f150Soma
Soma o valor do lançamento
 
@return numérico
 
@author Renan Gremes
@since 16/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method f150Soma(cTipo as character, lTotGSint as logical) as numeric class BalanceSheetMod1SmartViewBusinessObject

    Local nRetValor	:= 0 as numeric
    Local nValor	:= 0 as numeric

	nValor := cArqTmp->SALDOATUCR - cArqTmp->SALDOATUDB

	If MV_PAR05 == 1					// So imprime Sinteticas - Soma Sinteticas
		If cArqTmp->TIPOCONTA == "1" .And. cArqTmp->NIVEL1  
				If nValor < 0 .And. cTipo == "D"
					nRetValor := Abs(nValor)
				ElseIf  nValor > 0 .And. cTipo == "C"
					nRetValor := Abs(nValor)
				EndIf
		EndIf
	Else	// Soma Analiticas
		If MV_PAR05 == 2		//Se imprime so as analiticas
			If cArqTmp->TIPOCONTA == "2"
				If nValor < 0 .And. cTipo == "D"
					nRetValor := Abs(nValor)
				ElseIf nValor > 0 .And. cTipo == "C"
					nRetValor := Abs(nValor)
				EndIf
			EndIf
		ElseIf MV_PAR05 == 3		//Se imprime as analiticas e sinteticas
			If lTotGSint		//Se totaliza pelas sinteticas
				If cArqTmp->TIPOCONTA == "1" .And. cArqTmp->NIVEL1  
					If (nValor) < 0 .And. cTipo == "D"
						nRetValor := Abs(nValor)
					ElseIf  nValor > 0 .And. cTipo == "C"
						nRetValor := Abs(nValor)
					EndIf
				EndIf
			Else				//Se totaliza pelas analiticas
				If cArqTmp->TIPOCONTA == "2"
					If (nValor) < 0 .And. cTipo == "D"
						nRetValor := Abs(nValor)
					ElseIf nValor > 0 .And. cTipo == "C" 
						nRetValor := Abs(nValor)
					EndIf
				EndIf
		    EndIf
		EndIf
	EndIf

return nRetValor
