#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.referentialchartofaccounts.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1,CVN,CVD", name="Plano de Contas Referencial", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} ReferentialChartofAccountsSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Plano de Contas Referencial 
para o TReports
 
@author Douglas Silva
@since 27/04/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class ReferentialChartofAccountsSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()             as object
	public method getData()         as object
	public method getSchema()       as object
	public method getAreas()        as array
	public method getDescription()  as character

    //usados na internacionalização
    public method preschema()       as object 
    public method preData()         as object
    public method processData()     as json 

    PROTECTED data jDados  as json
    protected data oQuery1 as object
    protected data oIntegratedProvider  as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Douglas Silva
@since 27/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method new() as object Class ReferentialChartofAccountsSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //Plano de Contas Referencial

    // Indica o pergunte que será utilizado no relatório
   self:setPergunte("CTBR025")	
    
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Douglas Silva
@since 27/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getDescription() as character class ReferentialChartofAccountsSmartViewBusinessObject
return STR0002 //Objeto contendo informações cadastro de Plano de Contas Referencial

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Douglas Silva
@since 27/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getAreas() as array class ReferentialChartofAccountsSmartViewBusinessObject
Return {STR0003} //Contabilidade

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Douglas Silva
@since 27/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class ReferentialChartofAccountsSmartViewBusinessObject

    local cAlias              as character
    local cQuery              as character
    local cDesc               as character
    local cPlanoRef           as character
    local cVersao             as character
    local cMoeda              as character
    local cCompanyName        as character
    local cBranchName         as character
    local cDelete := space(1) as character
    local nTpRel              as numeric
    local nImpCta             as numeric
    local nParamOrder := 1    as numeric
    local cDtVigI             as date
    local cDtVigF             as date
    local lSoCtaRel           as logical

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTBR025")
    
    ::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

    cPlanoRef	:= MV_PAR01						//Cod. Plano Referencial
	cVersao		:= MV_PAR02						//Versão
	nTpRel		:= MV_PAR03						//Tipo do relatório: 1=Cadastro;2=Relacionamento
	nImpCta		:= MV_PAR04						//Imprime: 1=Analíticas; 2=Sintéticas;3=Ambas 
	lSoCtaRel 	:= iif(MV_PAR06 == 1,.T.,.F.)	//1=Mostra só conta com relacionamento;2=Mostra todas as contas
	cMoeda		:= MV_PAR07						//Descrição em qual moeda?
        
    cMoeda := iif(Empty(cMoeda) .Or. Empty(CtbMoeda(cMoeda)[1]), "01", cMoeda)
    
    cDesc := "CT1_DESC" + cMoeda

    if nTpRel == 1 //Se imprime somente cadastro do plano referencial
        cQuery  := "SELECT DISTINCT "
        cQuery  += " CVN.CVN_CODPLA, "
        cQuery  += " CVN.CVN_VERSAO, "
        cQuery  += " CVN.CVN_DSCPLA, "
        cQuery  += " CVN.CVN_CTAREF, "
        cQuery  += " CVN.CVN_CLASSE, "
        cQuery  += " CVN.CVN_DTVIGI, "
        cQuery  += " CVN.CVN_DTVIGF, "
        cQuery  += " CVN.CVN_DSCCTA, "
        cQuery  += " CVN.CVN_CTASUP, "
        cQuery  += " '' CONTA, "
        cQuery  += " '' DESCR "
		cQuery  += " FROM "+RetSqlName("CVN")+" CVN "
		cQuery  += "WHERE CVN.CVN_FILIAL = ? "
		cQuery  += "AND CVN.CVN_CODPLA = ? "
        cQuery  += "AND CVN.CVN_VERSAO = ? "
        if nImpCta == 1      //Somente contas Analiticas
            cQuery  += "AND CVN.CVN_CLASSE = ? "
        elseif  nImpCta == 2 //Somente contas Sintéticas
            cQuery  += "AND CVN.CVN_CLASSE = ? "
        endif

        //Os filtros serão setados na interface do novo TReports    
        cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

		cQuery  += "AND CVN.D_E_L_E_T_ = ? "
	    cQuery  += "ORDER BY CVN_CODPLA, CVN_VERSAO, CVN_CTAREF "
    else
        cQuery  := "SELECT "
        cQuery  += " TAB.CVN_CODPLA, "
        cQuery  += " TAB.CVN_VERSAO, "
        cQuery  += " TAB.CVN_DSCPLA, "
        cQuery  += " TAB.CVN_CTAREF, "
        cQuery  += " TAB.CVN_CLASSE, "
        cQuery  += " TAB.CVN_DTVIGI, "
        cQuery  += " TAB.CVN_DTVIGF, "
        cQuery  += " TAB.CVN_DSCCTA, "
        cQuery  += " TAB.CVN_CTASUP, "
        cQuery  += " TAB.CVD_CONTA CONTA, "
        cQuery  += " TAB.CT1_DESC DESCR, "
        cQuery  += " TAB.LINHA "
        cQuery  += " FROM ( "
        cQuery  += " SELECT  "
        cQuery  += " 	CVN_CODPLA, CVN_VERSAO, CVN_DSCPLA,  "
        cQuery  += " 	CVN_CTAREF, CVN_CLASSE, CVN_DTVIGI,  "
        cQuery  += " 	CVN_DTVIGF, CVN_DSCCTA, CVN_CTASUP, "
        cQuery  += " 	ISNULL(CVD_CONTA, '') CVD_CONTA, "
        cQuery  += " 	ISNULL("+cDesc+",'') CT1_DESC, "
        cQuery  += " 	ROW_NUMBER() OVER(ORDER BY CVN_CODPLA, CVN_VERSAO DESC) LINHA "
        cQuery  += " FROM "+RetSqlName("CVN")+" CVN "
        if lSoCtaRel //Somente conta com relacionamento
            cQuery  += " 		JOIN "+RetSqlName("CVD")+" CVD ON (CVD_FILIAL = ? AND CVD_CODPLA = CVN_CODPLA AND CVN_VERSAO = CVD_VERSAO AND CVD_CTAREF = CVN_CTAREF AND  CVD.D_E_L_E_T_ = ? ) "
            cQuery  += " 		JOIN "+RetSqlName("CT1")+" CT1 ON (CT1_FILIAL = ? AND CVD_CONTA = CT1_CONTA  AND  CT1.D_E_L_E_T_ = ? )  "	
        else
            cQuery  += " 		LEFT JOIN "+RetSqlName("CVD")+" CVD ON (CVD_FILIAL = ? AND CVD_CODPLA = CVN_CODPLA AND CVN_VERSAO = CVD_VERSAO AND CVD_CTAREF = CVN_CTAREF AND  CVD.D_E_L_E_T_ = ? ) "
            cQuery  += " 		LEFT JOIN "+RetSqlName("CT1")+" CT1 ON (CT1_FILIAL = ? AND CVD_CONTA = CT1_CONTA  AND  CT1.D_E_L_E_T_ = ? )  "	
        endif
        cQuery  += " 		WHERE CVN.CVN_FILIAL = ? "
        cQuery  += " 		AND CVN.CVN_CODPLA = ?"
        cQuery  += " 		AND CVN.CVN_VERSAO = ?"
        if nImpCta == 1      //Somente contas Analiticas
            cQuery  += " 		AND CVN.CVN_CLASSE = ? "
        elseif  nImpCta == 2 //Somente contas Sintéticas
            cQuery  += " 		AND CVN.CVN_CLASSE = ? "
        endif
        
        //Os filtros serão setados na interface do novo TReports    
        cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

        cQuery  += " 		AND CVN.D_E_L_E_T_ = ?) TAB "
        cQuery  += " ORDER BY TAB.CVN_CODPLA, TAB.CVN_VERSAO, TAB.CVN_CTAREF "
    endif

	self:oQuery1:= FWPreparedStatement():New(ChangeQuery(cQuery))    
    
    if nTpRel == 1
        self:oQuery1:SetString(nParamOrder++, FWxFilial("CVN"))
        self:oQuery1:SetString(nParamOrder++, cPlanoRef)
        self:oQuery1:SetString(nParamOrder++, cVersao)
        if nImpCta == 1      //Somente contas Analiticas
            self:oQuery1:SetString(nParamOrder++, "2")
        elseif  nImpCta == 2 //Somente contas Sintéticas
            self:oQuery1:SetString(nParamOrder++, "1")
        endif
        self:oQuery1:SetString(nParamOrder++, cDelete)
    else      
        self:oQuery1:SetString(nParamOrder++, FWxFilial("CVD"))
        self:oQuery1:SetString(nParamOrder++, cDelete)
        self:oQuery1:SetString(nParamOrder++, FWxFilial("CT1"))
        self:oQuery1:SetString(nParamOrder++, cDelete)
        self:oQuery1:SetString(nParamOrder++, FWxFilial("CVN"))
        self:oQuery1:SetString(nParamOrder++, cPlanoRef)
        self:oQuery1:SetString(nParamOrder++, cVersao)
        if nImpCta == 1      //Somente contas Analiticas
            self:oQuery1:SetString(nParamOrder++, "2")
        elseif  nImpCta == 2 //Somente contas Sintéticas
            self:oQuery1:SetString(nParamOrder++, "1")
        endif
        self:oQuery1:SetString(nParamOrder++, cDelete)
    endif    

	cQuery := self:oQuery1:GetFixQuery()

	cAlias := MPSysOpenQuery(ChangeQuery(cQuery))
    
    (cAlias)->(dbGoTop())    

    While !(cAlias)->(Eof())

        If ( nImpCta == 2 .And. (cAlias)->CVN_CLASSE <> "1") .Or. ( nImpCta == 1 .And. (cAlias)->CVN_CLASSE <> "2")
            (cAlias)->( dbSkip() )
            Loop
        Endif

        //Tratamento dos campos de data para timestamp
        cDtVigI := iif(!empty((cAlias)->CVN_DTVIGI), FwTimeStamp(5, StoD((cAlias)->CVN_DTVIGI), "00:00:00"), nil)
        cDtVigF := iif(!empty((cAlias)->CVN_DTVIGF), FwTimeStamp(5, StoD((cAlias)->CVN_DTVIGF), "00:00:00"), nil)

        ::jDados := JsonObject():new()

        ::jDados["CVN_CODPLA"]  := (cAlias)->CVN_CODPLA
        ::jDados["CVN_VERSAO"]  := (cAlias)->CVN_VERSAO
        ::jDados["CVN_DSCPLA"]  := (cAlias)->CVN_DSCPLA
        ::jDados["CVN_CTAREF"]  := (cAlias)->CVN_CTAREF
        ::jDados["CVN_CLASSE"]  := (cAlias)->CVN_CLASSE
        ::jDados["CVN_DTVIGI"]  := cDtVigI
        ::jDados["CVN_DTVIGF"]  := cDtVigF
        ::jDados["CVN_DSCCTA"]  := (cAlias)->CVN_DSCCTA
        ::jDados["CVN_CTASUP"]  := (cAlias)->CVN_CTASUP
        ::jDados["CVD_CONTA" ]  := (cAlias)->CONTA
        ::jDados["CT1_DESC01"]  := (cAlias)->DESCR
        ::jDados["NomeEmpresa"] := cCompanyName
        ::jDados["NomeFilial"]  := cBranchName

        self:processData()
        self:oData:appendData(::jDados)

        (cAlias)->(dbSkip())
  
    EndDo

    (cAlias)->(dbCloseArea())

    //Elimina da memória
    FreeObj(oFilter:getParameters())
    FreeObj(::oIntegratedProvider)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getStruct
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Douglas Silva
@since 27/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class ReferentialChartofAccountsSmartViewBusinessObject

    Local aFieldsCVN as array
    Local aFieldsCT1 as array
    Local aFieldsCVD as array

    aFieldsCT1 := {"CT1_DESC01"}
    aFieldsCVD := {"CVD_CONTA"}
    aFieldsCVN := {"CVN_CODPLA","CVN_VERSAO","CVN_DSCPLA","CVN_CTAREF","CVN_CLASSE","CVN_DTVIGI","CVN_DTVIGF","CVN_DSCCTA","CVN_CTASUP"}

    self:oSchema:aliasToSchema("CT1",aFieldsCT1)
    self:oSchema:aliasToSchema("CVN",aFieldsCVN)
    self:oSchema:aliasToSchema("CVD",aFieldsCVD)

    self:oSchema:addProperty("NomeEmpresa", STR0004, "string", STR0004, "NomeEmpresa",,,,.F.)   //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial" , STR0005, "string", STR0005, "NomeFilial" ,,,,.F.)   //"Nome Filial"

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
@author         Ednilson Queiroz
@since          11/09/2023
@version        12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class ReferentialChartofAccountsSmartViewBusinessObject

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
Metodo que será consumido pelo M.I.
@author         Ednilson Queiroz
@since          11/09/2023
@version        12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class ReferentialChartofAccountsSmartViewBusinessObject
    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
Metodo que será consumido pelo M.I.
@author         Ednilson Queiroz
@since          11/09/2023
@version        12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class ReferentialChartofAccountsSmartViewBusinessObject

return ::jDados
