#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.monthtomonthbalancecomparison.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGACTB",tables="CT1,CT2,CQ1,CQ3,CQ5,CQ7", NAME="Comparativo de Saldo Mes a Mes", country="ALL")

class MonthtoMonthBalanceComparisonSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()            as object
    public method getDescription() as character
    public method getAreas()       as array
    public method getData()        as object
    public method getSchema()      as object
    
    //usados na internacionalização
    public method preschema()      as object 
    public method preData()        as object
    public method processData()    as json 

	protected method setDados()	   as object
	protected method CtCmpSldM()
	protected method CtQryComp()
	protected method compvld()
	protected method criaTabTemp() as array
	

    //parametros que serão alimentados por MV_PAR
    PROTECTED DATA dDataIni   AS Date
	PROTECTED DATA dDataFim   AS Date
	PROTECTED DATA dDataLP    AS Date

	PROTECTED DATA cContaIni  AS character
	PROTECTED DATA cContaFim  AS character
	PROTECTED DATA cCCIni     AS character
	PROTECTED DATA cCCFim     AS character
	PROTECTED DATA cItemIni   AS character
	PROTECTED DATA cItemFim   AS character
	PROTECTED DATA cClVlIni   AS character
	PROTECTED DATA cClVlFim   AS character
	PROTECTED DATA cMoeda     AS character
	PROTECTED DATA cSaldoA    AS character
	PROTECTED DATA cSaldoB    AS character
	PROTECTED DATA cSaldoC    AS character
	
	PROTECTED DATA aSetOfBook AS array
	PROTECTED DATA aMeses     AS array
	PROTECTED DATA aBind      AS array
	
	PROTECTED DATA lVlrZerado AS logical
	PROTECTED DATA lPrintZero AS logical
	PROTECTED DATA lUsaCta    AS logical
	PROTECTED DATA lUsaCusto  AS logical
	PROTECTED DATA lUsaItem   AS logical
	PROTECTED DATA lUsaClVl   AS logical
	PROTECTED DATA lImpDesc   AS logical
	PROTECTED DATA lAcum      AS logical
	PROTECTED DATA lImpAntLP  AS logical

	PROTECTED DATA nDivide    AS Numeric
	PROTECTED DATA nImpPerc   AS Numeric
	PROTECTED DATA nImpValor  AS Numeric
	PROTECTED DATA nDecimais  AS Numeric
	PROTECTED DATA nLenMeses  AS Numeric
	
	PROTECTED DATA nParamOrder AS Integer
	
	PROTECTED DATA jItens     AS Json
	PROTECTED DATA cAlias     AS character

	PROTECTED DATA _oCTBR2951 AS Object
	PROTECTED DATA _oCTBR2952 AS Object
	PROTECTED DATA oQuery     AS Object

endclass

method new() as object class MonthtoMonthBalanceComparisonSmartViewBusinessObject

_Super:new()

//Define o nome do Objeto de Negócio
self:setDisplayName(STR0001) //  "Comparativo de Saldo Mês a Mês"
self:setPergunte("CTR295") //Indica o pergunte que será utilizado no relatório

//seta alguns atributos com valores default
::cSaldoA     := "1"
::cMoeda      := "01"
::aBind       := {}
return self

//Descrição dos detalhes do objeto no smartview
method getDescription() as character class MonthtoMonthBalanceComparisonSmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre o Comparativo de Saldo Mês a Mês"

Method getAreas() as array class MonthtoMonthBalanceComparisonSmartViewBusinessObject
Return {STR0003} //"Contabilidade" 
//-------------------------------------------
// getData
// Preenche o com os valores todo campo configurado no getSchema
//
//-------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class MonthtoMonthBalanceComparisonSmartViewBusinessObject
Local cMoeda        :="01"  as character
Local cTpSaldo      :="1"   as character
Local lImpConta		:= .T.  as logical
Local lImpCusto		:= .T.  as logical
Local lImpItem  	:= .T.	as logical
Local nCont         := 0    as integer
Local aPeriodos     := {}   as array
Local cTabSaldo 			as character
Local cEntid    			as character
Local nMeses		:= 0    as integer

//Preenchimento dos MV_PAR's
CTBsetValueMVPAR(oFilter:getParameters(), 'CTR295')

::dDataIni 		:= MV_PAR01       										// Data Inicial ?                
::dDataFim 		:= MV_PAR02       										// Data Final ?                  
::cContaIni  	:= MV_PAR03       										// Conta Inicial ?               
::cContaFim  	:= MV_PAR04       										// Conta Final ?                 
::cCCIni    	:= MV_PAR05       										// Do Centro de Custo ?          
::cCCFim	   	:= MV_PAR06       										// Ate o C.Custo ?               
::cItemIni      := MV_PAR07       										// Do Item Contabil ?            
::cItemFim      := MV_PAR08       										// Ate o Item Contabil ?         
::cClVlIni      := MV_PAR09       										// Da Classe de Valor ?          
::cClVlFim      := MV_PAR10       										// Ate a Classe de Valor ?       
::cMoeda	    := Iif(Empty(MV_PAR11),cMoeda,MV_PAR11)					// Moeda ?                       
::cSaldoA	    := Iif(Empty(MV_PAR12),cTpSaldo,PadR(AllTrim(MV_PAR12), TamSX3("CT2_TPSALD")[1]))				// Tipo de Saldo ?               
::cSaldoB		:= Substr(MV_PAR13,1,1)									// Saldo a Comparar ?            
::cSaldoC		:= Substr(MV_PAR13,2,1)									// Saldo a Comparar ?            
::aSetOfBook    := Iif(!Empty(MV_PAR14),CTBSetOf(MV_PAR14),CTBSetOf(""))// Config. Livros ?              
::lVlrZerado	:= Iif(MV_PAR15==1,.T.,.F.)       						// Saldos Zerados ?              
::lAcum         := Iif(MV_PAR16==2,.T.,.f.)       						// Comparar ?                    
nPagina			:= MV_PAR17       										// Folha Inicial ?               
lImpConta       := iif(MV_PAR18==2,.F.,.T.)       						// Imprime Cod. Conta ?          
lImpCusto       := iif(MV_PAR19==2,.F.,.T.)       						// Imprime Cod. C.C ?            
lImpItem        := iif(MV_PAR20==2,.F.,.T.)       						// Imprime Cod. Item ?           
::lPrintZero	:= Iif(MV_PAR21==1,.T.,.F.)       						// Imprime Valor 0,00 ?          
::lImpAntLP     := Iif(MV_PAR23 == 1,.T.,.F.)  							// Posicao Ant. L/P ?            
::dDataLP       := MV_PAR24       										// Data Lucros/Perdas ?          
::nImpPerc		:= MV_PAR25       										// Imp.Var.Percentual ?          
::nImpValor		:= MV_PAR26       										// Imp. Dif. em Valor ?          
::lImpDesc 		:= Iif(MV_PAR27 == 1,.T.,.F.)       					// Imp.Descricoes ?              
::lUsaCta		:= Iif(MV_PAR28 == 1,.T.,.F.)           				// Usa Conta ?                   
::lUsaCusto		:= Iif(MV_PAR29 == 1,.T.,.F.)       					// Usa C.Custo ?                 
::lUsaItem		:= Iif(MV_PAR30 == 1,.T.,.F.)       					// Usa Item Contabil ?           
::lUsaClVl		:= Iif(MV_PAR31 == 1,.T.,.F.)       					// Usa Classe de Valor ?    
self:compvld(MV_PAR22)       											// Divide por ?           


// Tratando o Periodo 
//------------------------------------------------------------------------------------
::aMeses := {}
aPeriodos 		:= ctbPeriodos(::cMoeda, ::dDataIni, ::dDataFim, .T., .F.)

For nCont := 1 to len(aPeriodos)
	//Se a Data do periodo eh maior ou igual a data inicial solicitada no relatorio.
	If aPeriodos[nCont][1] >= ::dDataIni .And. aPeriodos[nCont][2] <= ::dDataFim
		If nMeses <= 12
			AADD(::aMeses,{StrZero(nMeses,2),aPeriodos[nCont][1],aPeriodos[nCont][2]})
		EndIf
		nMeses += 1
	EndIf
Next

If Len( ::aMeses ) == 0
	AADD(::aMeses,{'01',::dDataIni,::dDataFim})
EndIf	

::nLenMeses	:= Len(::aMeses)
cTabSaldo := "CT7"
cEntid    := ""

DO CASE

CASE ::lUsaCta  //  Conta = "Sim"
	
	// Somente Conta
	If !::lUsaCusto .and. !::lUsaItem .and. !::lUsaClVl
		cTabSaldo := "CT7"

	// Conta+CCusto
	ElseIf ::lUsaCusto .and. !::lUsaItem .and. !::lUsaClVl
		cTabSaldo := "CT3"

	// Conta+CCusto+Item  ou  Conta+Item
	ElseIf ::lUsaItem .and. !::lUsaClVl
		cTabSaldo := "CT4"

	// Conta+CCusto+Item+Cl.Valor  ou  Conta+CCusto+Cl.Valor  ou  Conta+Item+Cl.Valor  ou  Conta+Cl.Valor
	ElseIf ::lUsaClVl
		cTabSaldo := "CTI"
	EndIf

CASE !::lUsaCta  //  Conta = "Nao"

	// Somente C.Custo
	If ::lUsaCusto .and. !::lUsaItem .and. !::lUsaClVl
		cTabSaldo := "CTU"
		cEntid    := "CTT"
		
	// Somente Item
	ElseIf !::lUsaCusto .and. ::lUsaItem .and. !::lUsaClVl
		cTabSaldo := "CTU"
		cEntid    := "CTD"

	// Somente Classe de Valor
	ElseIf !::lUsaCusto .and. !::lUsaItem .and. ::lUsaClVl
		cTabSaldo := "CTU"
		cEntid    := "CTH"

	// C.Custo+Item
	ElseIf ::lUsaCusto .and. ::lUsaItem .and. !::lUsaClVl
		cTabSaldo := "CTV"

	// C.Custo+Classe
	ElseIf ::lUsaCusto .and. !::lUsaItem .and. ::lUsaClVl
		cTabSaldo := "CTW"

	// Item+Classe
	ElseIf !::lUsaCusto .and. ::lUsaItem .and. ::lUsaClVl
		cTabSaldo := "CTX"

	// C.Custo+Item+Classe
	ElseIf ::lUsaCusto .and. ::lUsaItem .and. ::lUsaClVl
		cTabSaldo := "CTY"
	EndIf
	
ENDCASE


//chamada do metodo para criação da temporaria 
self:CtCmpSldM(oFilter,cTabSaldo,cEntid)
					
self:setDados(oFilter)


dbSelectArea("cArqTmp")
cArqTMP->(DBCloseArea())

If ::_oCTBR2951 <> Nil
	::_oCTBR2951:Delete()
	::_oCTBR2951 := Nil
Endif

return self:oData
//-------------------------------------------------------------------
/*/{Protheus.doc} getSchema
    Modela os campos
    @author wilton.santos
    @since 28/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method getSchema() as object class MonthtoMonthBalanceComparisonSmartViewBusinessObject

self:oSchema:addProperty("CONTA"	,STR0004, "string" ,STR0004,"CONTA"	   	,,,,.F.) // Codigo da Conta
self:oSchema:addProperty("CTASUP"	,STR0005, "string" ,STR0005,"CTASUP"	,,,,.F.) // Codigo da Conta Superior
self:oSchema:addProperty("CTARES"	,STR0006, "string" ,STR0006,"CTARES"	,,,,.F.) // Codigo Reduzido da Conta
self:oSchema:addProperty("DESCCTA"	,STR0007, "string" ,STR0007,"DESCCTA"	,,,,.F.) // Descricao da Conta
self:oSchema:addProperty("NORMAL"	,STR0008, "string" ,STR0008,"NORMAL"	,,,,.F.) // Situacao da Conta
self:oSchema:addProperty("TIPOCONTA",STR0009, "string" ,STR0009,"TIPOCONTA"	,,,,.F.) // Conta Analitica / Sintetica
self:oSchema:addProperty("CUSTO"	,STR0010, "string" ,STR0010,"CUSTO"		,,,,.F.) // Codigo do Centro de Custo
self:oSchema:addProperty("CCSUP"	,STR0011, "string" ,STR0011,"CCSUP"		,,,,.F.) // Codigo do Centro de Custo Superior
self:oSchema:addProperty("CCRES"	,STR0012, "string" ,STR0012,"CCRES"		,,,,.F.) // Codigo Reduzido do Centro de Custo
self:oSchema:addProperty("DESCCC" 	,STR0013, "string" ,STR0013,"DESCCC" 	,,,,.F.) // Descricao do Centro de Custo
self:oSchema:addProperty("TIPOCC"  	,STR0014, "string" ,STR0014,"TIPOCC"  	,,,,.F.) // Centro de Custo Analitico / Sintetico
self:oSchema:addProperty("ITEM"		,STR0015, "string" ,STR0015,"ITEM"		,,,,.F.) // Codigo do Item
self:oSchema:addProperty("ITSUP"	,STR0016, "string" ,STR0016,"ITSUP"		,,,,.F.) // Codigo do Item Superior
self:oSchema:addProperty("ITEMRES" 	,STR0017, "string" ,STR0017,"ITEMRES" 	,,,,.F.) // Codigo Reduzido do Item
self:oSchema:addProperty("DESCITEM" ,STR0018, "string" ,STR0018,"DESCITEM" 	,,,,.F.) // Descricao do Item
self:oSchema:addProperty("TIPOITEM"	,STR0019, "string" ,STR0019,"TIPOITEM"	,,,,.F.) // Item Analitica / Sintetica
self:oSchema:addProperty("CLVL"		,STR0020, "string" ,STR0020,"CLVL"		,,,,.F.) // Codigo da Classe de Valor
self:oSchema:addProperty("CLSUP"	,STR0021, "string" ,STR0021,"CLSUP"	   	,,,,.F.) // Codigo da Classe de Valor Superior
self:oSchema:addProperty("CLVLRES"	,STR0022, "string" ,STR0022,"CLVLRES"	,,,,.F.) // Cod. Red. Classe de Valor
self:oSchema:addProperty("DESCCLVL" ,STR0023, "string" ,STR0023,"DESCCLVL"  ,,,,.F.) // Descricao da Classe de Valor
self:oSchema:addProperty("TIPOCLVL"	,STR0024, "string" ,STR0024,"TIPOCLVL"	,,,,.F.) // Classe de Valor Analitica / Sintetica
self:oSchema:addProperty("ORDEM"	,STR0025, "string" ,STR0025,"ORDEM"		,,,,.F.) // Ordem
self:oSchema:addProperty("GRUPO"	,STR0026, "string" ,STR0026,"GRUPO"		,,,,.F.) // Grupo Contabil
self:oSchema:addProperty("IDENTIFI"	,STR0027, "string" ,STR0027,"IDENTIFI"	,,,,.F.) // Identificador
self:oSchema:addProperty("ESTOUR"  	,STR0028, "string" ,STR0028,"ESTOUR"  	,,,,.F.) // Define se eh conta estourada
self:oSchema:addProperty("NIVEL1"	,STR0029, "boolean",STR0029,"NIVEL1"	,,,,.F.) // Logico para identificar se eh de nivel 1 -> tot

self:oSchema:addProperty("COLA1"	,STR0030 ,"number",STR0030 ,"COLA1"	,,,,.F.) // Saldo A Periodo 1
self:oSchema:addProperty("COLA2"   	,STR0031 ,"number",STR0031 ,"COLA2" ,,,,.F.) // Saldo A Periodo 2
self:oSchema:addProperty("COLA3"   	,STR0032 ,"number",STR0032 ,"COLA3" ,,,,.F.) // Saldo A Periodo 3
self:oSchema:addProperty("COLA4" 	,STR0033 ,"number",STR0033 ,"COLA4" ,,,,.F.) // Saldo A Periodo 4
self:oSchema:addProperty("COLA5" 	,STR0034 ,"number",STR0034 ,"COLA5" ,,,,.F.) // Saldo A Periodo 5
self:oSchema:addProperty("COLA6"  	,STR0035 ,"number",STR0035 ,"COLA6" ,,,,.F.) // Saldo A Periodo 6
self:oSchema:addProperty("COLA7"	,STR0036 ,"number",STR0036 ,"COLA7"	,,,,.F.) // Saldo A Periodo 7
self:oSchema:addProperty("COLA8"   	,STR0037 ,"number",STR0037 ,"COLA8" ,,,,.F.) // Saldo A Periodo 8
self:oSchema:addProperty("COLA9"   	,STR0038 ,"number",STR0038 ,"COLA9" ,,,,.F.) // Saldo A Periodo 9
self:oSchema:addProperty("COLA10" 	,STR0039 ,"number",STR0039 ,"COLA10",,,,.F.) // Saldo A Periodo 10
self:oSchema:addProperty("COLA11" 	,STR0040 ,"number",STR0040 ,"COLA11",,,,.F.) // Saldo A Periodo 11
self:oSchema:addProperty("COLA12"  	,STR0041 ,"number",STR0041 ,"COLA12",,,,.F.) // Saldo A Periodo 12

self:oSchema:addProperty("COLB1"	,STR0042 ,"number",STR0042 ,"COLB1"	,,,,.F.) // Saldo B Periodo 1
self:oSchema:addProperty("COLB2"   	,STR0043 ,"number",STR0043 ,"COLB2" ,,,,.F.) // Saldo B Periodo 2
self:oSchema:addProperty("COLB3"   	,STR0044 ,"number",STR0044 ,"COLB3" ,,,,.F.) // Saldo B Periodo 3
self:oSchema:addProperty("COLB4" 	,STR0045 ,"number",STR0045 ,"COLB4" ,,,,.F.) // Saldo B Periodo 4
self:oSchema:addProperty("COLB5" 	,STR0046 ,"number",STR0046 ,"COLB5" ,,,,.F.) // Saldo B Periodo 5
self:oSchema:addProperty("COLB6"   	,STR0047 ,"number",STR0047 ,"COLB6" ,,,,.F.) // Saldo B Periodo 6
self:oSchema:addProperty("COLB7"	,STR0048 ,"number",STR0048 ,"COLB7"	,,,,.F.) // Saldo B Periodo 7
self:oSchema:addProperty("COLB8"   	,STR0049 ,"number",STR0049 ,"COLB8" ,,,,.F.) // Saldo B Periodo 8
self:oSchema:addProperty("COLB9"   	,STR0050 ,"number",STR0050 ,"COLB9" ,,,,.F.) // Saldo B Periodo 9
self:oSchema:addProperty("COLB10" 	,STR0051 ,"number",STR0051 ,"COLB10",,,,.F.) // Saldo B Periodo 10
self:oSchema:addProperty("COLB11" 	,STR0052 ,"number",STR0052 ,"COLB11",,,,.F.) // Saldo B Periodo 11
self:oSchema:addProperty("COLB12"  	,STR0053 ,"number",STR0053 ,"COLB12",,,,.F.) // Saldo B Periodo 12

self:oSchema:addProperty("COLC1"	,STR0054 ,"number",STR0054 ,"COLC1"	,,,,.F.) // Saldo C Periodo 1
self:oSchema:addProperty("COLC2"   	,STR0055 ,"number",STR0055 ,"COLC2" ,,,,.F.) // Saldo C Periodo 2
self:oSchema:addProperty("COLC3"   	,STR0056 ,"number",STR0056 ,"COLC3" ,,,,.F.) // Saldo C Periodo 3
self:oSchema:addProperty("COLC4" 	,STR0057 ,"number",STR0057 ,"COLC4" ,,,,.F.) // Saldo C Periodo 4
self:oSchema:addProperty("COLC5" 	,STR0058 ,"number",STR0058 ,"COLC5" ,,,,.F.) // Saldo C Periodo 5
self:oSchema:addProperty("COLC6"   	,STR0059 ,"number",STR0059 ,"COLC6" ,,,,.F.) // Saldo C Periodo 6
self:oSchema:addProperty("COLC7"	,STR0060 ,"number",STR0060 ,"COLC7"	,,,,.F.) // Saldo C Periodo 7
self:oSchema:addProperty("COLC8"   	,STR0061 ,"number",STR0061 ,"COLC8" ,,,,.F.) // Saldo C Periodo 8
self:oSchema:addProperty("COLC9"   	,STR0062 ,"number",STR0062 ,"COLC9" ,,,,.F.) // Saldo C Periodo 9
self:oSchema:addProperty("COLC10" 	,STR0063 ,"number",STR0063 ,"COLC10",,,,.F.) // Saldo C Periodo 10
self:oSchema:addProperty("COLC11" 	,STR0064 ,"number",STR0064 ,"COLC11",,,,.F.) // Saldo C Periodo 11
self:oSchema:addProperty("COLC12"  	,STR0065 ,"number",STR0065 ,"COLC12",,,,.F.) // Saldo C Periodo 12

self:oSchema:addProperty("TOTLA"  	,STR0066 ,"number",STR0066  ,"TOTLA",,,,.F.) // Total Linha A
self:oSchema:addProperty("TOTLB"  	,STR0067 ,"number",STR0067  ,"TOTLB",,,,.F.) // Total Linha B
self:oSchema:addProperty("TOTLC"  	,STR0068 ,"number",STR0068  ,"TOTLC",,,,.F.) // Total Linha C

self:oSchema:addProperty("DESCSLDA"	,STR0069 ,"string",STR0069 ,"DESCSLDA",,,,.F.) // Descr. Saldo A
self:oSchema:addProperty("DESCSLDB"	,STR0070 ,"string",STR0070 ,"DESCSLDB",,,,.F.) // Descr. Saldo B
self:oSchema:addProperty("DESCSLDC"	,STR0071 ,"string",STR0071 ,"DESCSLDC",,,,.F.) // Descr. Saldo C

return self:oSchema
//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author wilton.santos
    @since 28/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class MonthtoMonthBalanceComparisonSmartViewBusinessObject

return self:oSchema
//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author wilton.santos
    @since 28/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class MonthtoMonthBalanceComparisonSmartViewBusinessObject
    
return self:oData
//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 28/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class MonthtoMonthBalanceComparisonSmartViewBusinessObject

return ::JItens
//-------------------------------------------------------------------
/*/{Protheus.doc} CtCmpSldM
    Será usado para criar a temporaria
    
	@author wilton.santos
    @since 28/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method CtCmpSldM(oFilter as object,cAlias as character,cEntid as character) as variant class MonthtoMonthBalanceComparisonSmartViewBusinessObject
Local aSaveArea 	:= GetArea()     		 as array
Local aChave        := {}     				 as array
Local cEntidIni		:= "" as character
Local cEntidFim		:= "" as character
Local cEntidIni1	:= "" as character
Local cEntidFim1	:= "" as character
Local cEntidIni2	:= "" as character
Local cEntidFim2	:= "" as character
Local nTRB			:= 1  as numeric
Local nCont			:= 0  as numeric
Local nX			:= 0  as numeric
Local aStruTMP		:= {}
Local nColunas	    := 0 as numeric
Local aTamVlr	 	:= TAMSX3("CT2_VALOR") as array 
Local cCampoQry     := "" 				   as character
Local cOrdQry     	:= "" 				   as character
Local cQuery 			  				   as character
Local nMes  			  				   as numeric
Local cQuerySldA          				   as character
Local cQuerySldB          				   as character
Local cQuerySldC          				   as character
Local cCampUSU      :=" " 				   as character
Local cFilUSU       :=" " 				   as character
Local cArqTmp       :=" " 				   as character

Local cSaldoA		:= ::cSaldoA  as character
Local cSaldoB       := ::cSaldoB  as character
Local cSaldoC		:= ::cSaldoC  as character

Local lSldA			:= .F. as logical
Local lSldB			:= .F. as logical
Local lSldC			:= .F. as logical

Local cAlias			   as character
local nParamOrder   := 1   as integer

Private cPlanoRef	:= ::aSetOfBook[11] as array
Private cVersao		:= ::aSetOfBook[12] as array


lSldA				:= !Empty(::cSaldoA) 
lSldB				:= !Empty(::cSaldoB) 
lSldC				:= !Empty(::cSaldoC) 
cEntid		       	:= Iif(cEntid == Nil,'',cEntid) 

//Tratamento para filtro de usuario  -WILTON
If Empty(cFilUSU)
	cFilUSU := ".T." 
EndIf

/// TRATAMENTO PARA OBTENÇÃO DO SALDO DAS CONTAS ANALITICAS
Do Case
Case cAlias == "CT7"
	aChave	:= {"CONTA"}
	
	cCampUSU  := ""	
	
	cQuery := "SELECT CT1_CONTA CONTA, CT1_RES CTARES, CT1_DESC"+::cMoeda+" DESCCTA, CT1_CLASSE TIPOCONTA, CT1_CTASUP CTASUP, "
	cQuery += cCampUSU									//// ADICIONA OS CAMPOS NA QUERY
	If lSldA
		cQuery += self:CtQryComp("CT1",::nLenMeses,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLA",,,@nParamOrder)
	EndIf
	If lSldB
		If lSldA
			cQuery += ", "
		EndIf
		cQuery += self:CtQryComp("CT1",::nLenMeses,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLB",,,@nParamOrder)
	EndIf
	If lSldC
		If lSldA .or. lSldB
			cQuery += ", "
		EndIf
		cQuery += self:CtQryComp("CT1",::nLenMeses,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLC",,,@nParamOrder)
	EndIf
	
	cQuery += " FROM "+RetSqlName("CT1")+" ARQ "
	cQuery += " WHERE ARQ.CT1_FILIAL = ? "
				aAdd(self:aBind,{nParamOrder++,FwxFilial("CT1")})

	cQuery += " AND ARQ.CT1_CONTA BETWEEN ? AND ? "
				aAdd(self:aBind,{nParamOrder++,::cContaIni})
				aAdd(self:aBind,{nParamOrder++,::cContaFim})
	cQuery += " AND ARQ.CT1_CLASSE =  ? 	"
				aAdd(self:aBind,{nParamOrder++,'2'})
	
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ.CT1_BOOK LIKE ?"
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})	
	Endif
	
	cQuery += " AND ARQ.D_E_L_E_T_ = ? "
	aAdd(self:aBind,{nParamOrder++,space(1)})
	
	If !::lVlrZerado
		cQuery += " AND ( "
		If lSldA
			cQuery += self:CtQryComp("CT1",::nLenMeses,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		If lSldB
			If lSldA
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CT1",::nLenMeses,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		If lSldC
			If lSldA .or. lSldB
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CT1",::nLenMeses,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		cQuery += " ) "
	EndIf
	
Case cAlias == "CT3"
	aChave	:= {"CONTA","CUSTO"}
	
	cCampUSU  := ""										//// DECLARA VARIAVEL COM OS CAMPOS DO FILTRO DE USUÁRIO
	
	cQuery := "SELECT CT1_CONTA CONTA, CT1_RES CTARES, CT1_DESC"+::cMoeda+" DESCCTA, CT1_CLASSE TIPOCONTA, CT1_CTASUP CTASUP, "
	cQuery += " 	  CTT_CUSTO CUSTO,  CTT_RES CCRES, CTT_DESC"+::cMoeda+" DESCCC, CTT_CLASSE TIPOCC, CTT_CCSUP CCSUP, "
	cQuery += cCampUSU									//// ADICIONA OS CAMPOS NA QUERY
	
	If lSldA
		cQuery += self:CtQryComp("CTT",::nLenMeses,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLA",,,@nParamOrder)
	EndIf
	If lSldB
		If lSldA
			cQuery += ", "
		EndIf
		cQuery += self:CtQryComp("CTT",::nLenMeses,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLB",,,@nParamOrder)
	EndIf
	If lSldC
		If lSldA .or. lSldB
			cQuery += ", "
		EndIf
		cQuery += self:CtQryComp("CTT",::nLenMeses,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLC",,,@nParamOrder)
	EndIf
	
	cQuery += " FROM "+RetSqlName("CT1")+" ARQ, "+RetSqlName("CTT")+" ARQ2 "
	cQuery += " WHERE ARQ2.CTT_FILIAL = ? "
		aAdd(self:aBind,{nParamOrder++,FwxFilial("CTT")})

	cQuery += " AND ARQ2.CTT_CUSTO BETWEEN ? AND ? "
		aAdd(self:aBind,{nParamOrder++,::cCCIni})
		aAdd(self:aBind,{nParamOrder++,::cCCFim})

	cQuery += " AND ARQ2.CTT_CLASSE = ? 	"
		aAdd(self:aBind,{nParamOrder++,'2' })
	
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ2.CTT_BOOK LIKE ? 	"
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
	
	cQuery += " AND ARQ.CT1_FILIAL = ?	"
		aAdd(self:aBind,{nParamOrder++,FwxFilial("CT1")})

	cQuery += " AND ARQ.CT1_CONTA BETWEEN ? AND ? "
		aAdd(self:aBind,{nParamOrder++,::cContaIni})
		aAdd(self:aBind,{nParamOrder++,::cContaFim})
	
	cQuery += " AND ARQ.CT1_CLASSE = ? 	"
		aAdd(self:aBind,{nParamOrder++,'2' })
	
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ.CT1_BOOK LIKE ? "
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
	
	cQuery += " AND ARQ.D_E_L_E_T_ = ?  	"
	aAdd(self:aBind,{nParamOrder++,space(1)})
	
	cQuery += " AND ARQ2.D_E_L_E_T_ = ?  	"
	aAdd(self:aBind,{nParamOrder++,space(1)})
	
	If !::lVlrZerado
		cQuery += " AND ( "
		If lSldA
			cQuery += self:CtQryComp("CTT",::nLenMeses,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		If lSldB
			If lSldA
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CTT",::nLenMeses,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		If lSldC
			If lSldA .or. lSldB
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CTT",::nLenMeses,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		cQuery += " ) "
	EndIf
	
Case cAlias == "CT4"
	
	If ::lUsaCusto
		aChave := {"CONTA","CUSTO","ITEM"}
	Else
		aChave := {"CONTA","ITEM"}
	EndIf
	
	cCampUSU  := ""										//// DECLARA VARIAVEL COM OS CAMPOS DO FILTRO DE USUÁRIO
	
	cQuery := "SELECT CT1_CONTA CONTA, CT1_RES CTARES, CT1_DESC"+::cMoeda+" DESCCTA, CT1_CLASSE TIPOCONTA, CT1_CTASUP CTASUP, "
	If ::lUsaCusto
		cQuery += " 	  CTT_CUSTO CUSTO,  CTT_RES CCRES, CTT_DESC"+::cMoeda+" DESCCC, CTT_CLASSE TIPOCC, CTT_CCSUP CCSUP, "
	EndIf
	cQuery += " 	  CTD_ITEM ITEM, CTD_RES ITEMRES, CTD_DESC"+::cMoeda+" DESCITEM, CTD_CLASSE TIPOITEM, CTD_ITSUP ITSUP, "
	cQuery += cCampUSU									//// ADICIONA OS CAMPOS NA QUERY
	
	If lSldA
		cQuery += self:CtQryComp("CTD",::nLenMeses,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLA",,,@nParamOrder)
	EndIf
	If lSldB
		If lSldA
			cQuery += ", "
		EndIf
		cQuery += self:CtQryComp("CTD",::nLenMeses,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLB",,,@nParamOrder)
	EndIf
	If lSldC
		If lSldA .or. lSldB
			cQuery += ", "
		EndIf
		cQuery += self:CtQryComp("CTD",::nLenMeses,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLC",,,@nParamOrder)
	EndIf
	
	If ::lUsaCusto
		cQuery += " FROM "+RetSqlName("CT1")+" ARQ, "+RetSqlName("CTT")+" ARQ2, "+RetSqlName("CTD")+" ARQ3 "
	Else
		cQuery += " FROM "+RetSqlName("CT1")+" ARQ, "+RetSqlName("CTD")+" ARQ3 "
	EndIf
	
	cQuery += " WHERE "
	
	If ::lUsaCusto
		cQuery += "     ARQ2.CTT_FILIAL = ? 	"
		aAdd(self:aBind,{nParamOrder++,FwxFilial("CTT")})
	
		cQuery += " AND ARQ2.CTT_CUSTO BETWEEN ? AND ? "
		aAdd(self:aBind,{nParamOrder++,::cCCIni})
		aAdd(self:aBind,{nParamOrder++,::cCCFim})

		cQuery += " AND ARQ2.CTT_CLASSE =  ? "
		aAdd(self:aBind,{nParamOrder++,'2'})
		
		If !Empty(::aSetOfBook[1])
			cQuery += " AND ARQ2.CTT_BOOK LIKE ? "
			aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
		Endif
		
		cQuery += " AND "
	
	EndIf
	
	cQuery += "     ARQ.CT1_FILIAL = ?	"
	aAdd(self:aBind,{nParamOrder++,FwxFilial("CT1")})
	
	cQuery += " AND ARQ.CT1_CONTA BETWEEN ? AND ? 	"
	aAdd(self:aBind,{nParamOrder++,::cContaIni})
	aAdd(self:aBind,{nParamOrder++,::cContaFim})
	
	cQuery += " AND ARQ.CT1_CLASSE = ?  	"
	aAdd(self:aBind,{nParamOrder++,'2'})
	
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ.CT1_BOOK LIKE ? "
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
	
	cQuery += " AND ARQ3.CTD_FILIAL = ? "
	aAdd(self:aBind,{nParamOrder++,FwxFilial("CTD")})

	cQuery += " AND ARQ3.CTD_ITEM BETWEEN ? AND ?	"
	aAdd(self:aBind,{nParamOrder++,::cItemIni})
	aAdd(self:aBind,{nParamOrder++,::cItemFim})
	
	cQuery += " AND ARQ3.CTD_CLASSE = ?  	"
	aAdd(self:aBind,{nParamOrder++,'2'})
	
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ3.CTD_BOOK LIKE ? "
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
	
	If ::lUsaCusto
		cQuery += " AND ARQ2.D_E_L_E_T_ = ?  	"
		aAdd(self:aBind,{nParamOrder++,space(1)})
	EndIf
	
	cQuery += " AND ARQ.D_E_L_E_T_ = ?  	"
	aAdd(self:aBind,{nParamOrder++,space(1)})
	
	cQuery += " AND ARQ3.D_E_L_E_T_ = ?  	"
	aAdd(self:aBind,{nParamOrder++,space(1)})
	
	If !::lVlrZerado
		cQuery += " AND ( "
		If lSldA
			cQuery += self:CtQryComp("CTD",::nLenMeses,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		If lSldB
			If lSldA
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CTD",::nLenMeses,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		If lSldC
			If lSldA .or. lSldB
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CTD",::nLenMeses,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		cQuery += " ) "
	EndIf
	
Case cAlias == "CTI"
	
	aChave := {"CONTA"}
	
	If ::lUsaCusto 
		AADD(aChave,"CUSTO") 
	EndIf

	If ::lUsaItem  
		AADD(aChave,"ITEM")   
	EndIf
			
	AADD(aChave,"CLVL") 
				
	cCampUSU  := "" //// DECLARA VARIAVEL COM OS CAMPOS DO FILTRO DE USUÁRIO
			
	cQuery := "SELECT CT1_CONTA CONTA, CT1_RES CTARES, CT1_DESC"+::cMoeda+" DESCCTA, CT1_CLASSE TIPOCONTA, CT1_CTASUP CTASUP, "
	If ::lUsaCusto
		cQuery += " 	  CTT_CUSTO CUSTO,  CTT_RES CCRES, CTT_DESC"+::cMoeda+" DESCCC, CTT_CLASSE TIPOCC, CTT_CCSUP CCSUP, "
	EndIf
	If ::lUsaItem
		cQuery += " 	  CTD_ITEM ITEM, CTD_RES ITEMRES, CTD_DESC"+::cMoeda+" DESCITEM, CTD_CLASSE TIPOITEM, CTD_ITSUP ITSUP, "
	EndIf
	cQuery += " 	  CTH_CLVL CLVL, CTH_RES CLVLRES, CTH_DESC"+::cMoeda+" DESCCLVL, CTH_CLASSE TIPOCLVL, CTH_CLSUP CLSUP, "
	cQuery += cCampUSU									//// ADICIONA OS CAMPOS NA QUERY
	
	cQuerySldA :=""
	cQuerySldB :=""
	cQuerySldC :=""
	
	For nCont :=1 to ::nLenMeses
		
		If lSldA
			cQuerySldA += self:CtQryComp("CTH",nCont,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLA",nCont,,@nParamOrder)
			If nCont !=	::nLenMeses
				cQuerySldA +=", "
			Endif
		EndIf
		If lSldB
			cQuerySldB += self:CtQryComp("CTH",nCont,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLB",nCont,,@nParamOrder)
			If nCont !=	::nLenMeses
				cQuerySldB +=", "
			Endif
		EndIf
		If lSldC
			cQuerySldC += self:CtQryComp("CTH",nCont,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLC",nCont,,@nParamOrder)
			If nCont !=	::nLenMeses
				cQuerySldC +=", "
			Endif
		EndIf
	Next
	
	cQuery +=cQuerySldA  + Iif(!Empty(cQuerySldB),", " + cQuerySldB,"")+ Iif(!Empty(cQuerySldC),", " + cQuerySldC,"")
	// ARQ  = Conta
	// ARQ4 = Classe
	cQuery += " FROM "+RetSqlName("CT1")+" ARQ, "+RetSqlName("CTH")+" ARQ4 "
	
	If ::lUsaCusto
		// ARQ2 = C.Custo
		cQuery += ", "+RetSqlName("CTT")+" ARQ2 "
	EndIf
	
	If ::lUsaItem
		// ARQ3 = Item
		cQuery += ", "+RetSqlName("CTD")+" ARQ3 "
	EndIf
	
	cQuery += " WHERE "
	
	If ::lUsaCusto
		cQuery += "     ARQ2.CTT_FILIAL = ?	"
		aAdd(self:aBind,{nParamOrder++,FwxFilial("CTT")})
	
		cQuery += " AND ARQ2.CTT_CUSTO BETWEEN ? AND ? "
		aAdd(self:aBind,{nParamOrder++,::cCCIni})
		aAdd(self:aBind,{nParamOrder++,::cCCFim})

		cQuery += " AND ARQ2.CTT_CLASSE = ?  	"
		aAdd(self:aBind,{nParamOrder++,'2'})
		
		If !Empty(::aSetOfBook[1])
			cQuery += " AND ARQ2.CTT_BOOK LIKE ? 	"
			aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
		Endif

		cQuery += " AND "
	EndIf
	
	cQuery += "     ARQ.CT1_FILIAL = ? "
	aAdd(self:aBind,{nParamOrder++,FwxFilial("CT1")})
	
	cQuery += " AND ARQ.CT1_CONTA BETWEEN ? AND ? "
	aAdd(self:aBind,{nParamOrder++,::cContaIni})
	aAdd(self:aBind,{nParamOrder++,::cContaFim})

	cQuery += " AND ARQ.CT1_CLASSE = ?  	"
	aAdd(self:aBind,{nParamOrder++,'2'})
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ.CT1_BOOK LIKE ? "
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
	If ::lUsaItem
		cQuery += " AND ARQ3.CTD_FILIAL = ? 	"
		aAdd(self:aBind,{nParamOrder++,FwxFilial("CTD")})

		cQuery += " AND ARQ3.CTD_ITEM BETWEEN ? AND ? "
		aAdd(self:aBind,{nParamOrder++,::cItemIni})
		aAdd(self:aBind,{nParamOrder++,::cItemFim})

		cQuery += " AND ARQ3.CTD_CLASSE = ?  	"
		aAdd(self:aBind,{nParamOrder++,'2'})
		
		If !Empty(::aSetOfBook[1])
			cQuery += " AND ARQ3.CTD_BOOK LIKE ? "
			aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
		Endif

	EndIf
	cQuery += " AND ARQ4.CTH_FILIAL = ?  	"
	aAdd(self:aBind,{nParamOrder++,FwxFilial("CTH")})
	
	cQuery += " AND ARQ4.CTH_CLVL BETWEEN ? AND ?	"
	aAdd(self:aBind,{nParamOrder++,::cClVlIni})
	aAdd(self:aBind,{nParamOrder++,::cClVlFim})

	cQuery += " AND ARQ4.CTH_CLASSE = ?  	"
	aAdd(self:aBind,{nParamOrder++,'2'})

	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ4.CTH_BOOK LIKE ? "
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
	
	cQuery += " AND ARQ.D_E_L_E_T_ = ? 	"
	aAdd(self:aBind,{nParamOrder++,space(1)})

	cQuery += " AND ARQ4.D_E_L_E_T_ = ?  	"
	aAdd(self:aBind,{nParamOrder++,space(1)})

	If ::lUsaCusto
		cQuery += " AND ARQ2.D_E_L_E_T_ = ?  	"
		aAdd(self:aBind,{nParamOrder++,space(1)})
	EndIf
	
	If ::lUsaItem
		cQuery += " AND ARQ3.D_E_L_E_T_ = ?  	"
		aAdd(self:aBind,{nParamOrder++,space(1)})
	EndIf
	
	If !::lVlrZerado
		cQuery += " AND ( "
		If lSldA
			cQuery += self:CtQryComp("CTH",::nLenMeses,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",1,.T.,@nParamOrder)
		EndIf
		If lSldB
			If lSldA
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CTH",::nLenMeses,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",1,.T.,@nParamOrder)
		EndIf
		If lSldC
			If lSldA .or. lSldB
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CTH",::nLenMeses,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",1,.T.,@nParamOrder)
		EndIf
		cQuery += " ) "
	EndIf
			
Case cAlias == "CTU"
			
	// Centro de Custo
	If cEntid == "CTT"
		cEntidIni	:= ::cCCIni
		cEntidFim	:= ::cCCFim
		cCampoQry   := "CTT_CUSTO CUSTO, CTT_RES CCRES, CTT_DESC"+::cMoeda+" DESCCC, CTT_CLASSE TIPOCC, CTT_CCSUP CCSUP, "
		cOrdQry		:= "CTT_CUSTO"
		aChave      := {"CUSTO"}
		
		// Item Contabil
	ElseIf cEntid == "CTD"
		cEntidIni	:= ::cItemIni
		cEntidFim	:= ::cItemFim
		cCampoQry   := "CTD_ITEM ITEM, CTD_RES ITEMRES, CTD_DESC"+::cMoeda+" DESCITEM, CTD_CLASSE TIPOITEM, CTD_ITSUP ITSUP, "
		cOrdQry		:= "CTD_ITEM"
		aChave      := {"ITEM"}
		
		// Classe de Valor
	ElseIf cEntid == "CTH"
		cEntidIni	:= ::cClVlIni
		cEntidFim	:= ::cClVlFim
		cCampoQry   := "CTH_CLVL CLVL, CTH_RES CLVLRES, CTH_DESC"+::cMoeda+" DESCCLVL, CTH_CLASSE TIPOCLVL, CTH_CLSUP CLSUP, "
		cOrdQry		:= "CTH_CLVL"
		aChave      := {"CLVL"}
	EndIf
	cCampUSU  := ""										//// DECLARA VARIAVEL COM OS CAMPOS DO FILTRO DE USUÁRIO
	
	cQuery := "SELECT " + cCampoQry
	cQuery += cCampUSU									//// ADICIONA OS CAMPOS NA QUERY
	
	If lSldA
		For nMes := 1 to ::nLenMeses
			cQuery += "  	(SELECT COALESCE( SUM(CQ9_CREDIT) - SUM(CQ9_DEBITO) ,0) "
			cQuery += " 		 	FROM "+RetSqlName("CQ9")+" CQ9 "
			cQuery += "   			WHERE CQ9_FILIAL = ? "
			cQuery += "   			AND CQ9_MOEDA = ? "
			cQuery += "   			AND CQ9_TPSALD = ? "
			cQuery += "   			AND CQ9_IDENT = ? "
			cQuery += " 			AND CQ9_CODIGO	= ARQ."+cOrdQry+" "
			
			aAdd(self:aBind,{nParamOrder++,FwxFilial("CQ9")})
			aAdd(self:aBind,{nParamOrder++,::cMoeda})
			aAdd(self:aBind,{nParamOrder++,cSaldoA})
			aAdd(self:aBind,{nParamOrder++,cEntid})

			If ::lAcum
				cQuery += "    			AND CQ9_DATA <= ? "
				aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,3])})
			Else
				cQuery += "    			AND CQ9_DATA BETWEEN ? AND ? "
				aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,2])})
				aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,3])})
			EndIf

			If ::lImpAntLP .and. ::dDataLP >= ::aMeses[nMes,2]
				cQuery += "	AND CQ9_LP <> ? "
				aAdd(self:aBind,{nParamOrder++,'Z'})
			Endif
			cQuery += "   			AND CQ9.D_E_L_E_T_ = ?) COLA"+ALLTRIM(STR(nMes))
			aAdd(self:aBind,{nParamOrder++,space(1)})
			If nMes < ::nLenMeses
				cQuery += ","
			Endif
		Next
	EndIf
	If lSldB
		If lSldA
			cQuery += ","
		EndIf
		For nMes := 1 to ::nLenMeses
			cQuery += "  	(SELECT SUM(CQ9_CREDIT) - SUM(CQ9_DEBITO) "
			cQuery += " 		 	FROM "+RetSqlName("CQ9")+" CQ9 "
			cQuery += "   			WHERE CQ9_FILIAL = ? "
			cQuery += "   			AND CQ9_MOEDA = ? "
			cQuery += "   			AND CQ9_TPSALD = ? "
			cQuery += "   			AND CQ9_IDENT = ? "
			cQuery += " 			AND CQ9_CODIGO	= ARQ."+cOrdQry+" "

			aAdd(self:aBind,{nParamOrder++,FwxFilial("CQ9")})
			aAdd(self:aBind,{nParamOrder++,::cMoeda})
			aAdd(self:aBind,{nParamOrder++,cSaldoB})
			aAdd(self:aBind,{nParamOrder++,cEntid})

			If ::lAcum
				cQuery += "    			AND CQ9_DATA <= ? "
				aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,3])})
			Else
				cQuery += "    			AND CQ9_DATA BETWEEN ? AND ? "
				aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,2])})
				aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,3])})
			EndIf

			If ::lImpAntLP .and. ::dDataLP >= ::aMeses[nMes,2]
				cQuery += "	AND CQ9_LP <> ? "
				aAdd(self:aBind,{nParamOrder++,'Z'})
			Endif
			cQuery += "   			AND CQ9.D_E_L_E_T_ = ?) COLB"+ALLTRIM(STR(nMes))
			aAdd(self:aBind,{nParamOrder++,space(1)})
			If nMes < ::nLenMeses
				cQuery += ","
			Endif
		Next
	EndIf
	If lSldC
		If lSldA .or. lSldB
			cQuery += ","
		EndIf
		For nMes := 1 to ::nLenMeses
			cQuery += "  	(SELECT SUM(CQ9_CREDIT) - SUM(CQ9_DEBITO) "
			cQuery += " 		 	FROM "+RetSqlName("CQ9")+" CQ9 "
			cQuery += "   			WHERE CQ9_FILIAL = ? "
			cQuery += "   			AND CQ9_MOEDA = ? "
			cQuery += "   			AND CQ9_TPSALD = ? "
			cQuery += "   			AND CQ9_IDENT = ? "
			cQuery += " 			AND CQ9_CODIGO	= ARQ."+cOrdQry+" "
			
			aAdd(self:aBind,{nParamOrder++,FwxFilial("CQ9")})
			aAdd(self:aBind,{nParamOrder++,::cMoeda})
			aAdd(self:aBind,{nParamOrder++,cSaldoC})
			aAdd(self:aBind,{nParamOrder++,cEntid})
			
			If ::lAcum
				cQuery += "    			AND CQ9_DATA <= ? "
				aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,3])})
			Else
				cQuery += "    			AND CQ9_DATA BETWEEN ? AND ? "
				aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,2])})
				aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,3])})
			EndIf
			If ::lImpAntLP .and. ::dDataLP >= ::aMeses[nMes,2]
				cQuery += "	AND CQ9_LP <> ? "
				aAdd(self:aBind,{nParamOrder++,'Z'})
			Endif
			cQuery += "   			AND CQ9.D_E_L_E_T_ = ?) COLC"+ALLTRIM(STR(nMes))
			aAdd(self:aBind,{nParamOrder++,space(1)})
			If nMes < ::nLenMeses
				cQuery += ","
			Endif
		Next
	EndIf
			
	cQuery += "	FROM "+RetSqlName(cEntid)+" ARQ "
	cQuery += "	WHERE ARQ."+cEntid+"_FILIAL = ? "
	aAdd(self:aBind,{nParamOrder++,FwxFilial(cEntid)})

	cQuery += "	AND ARQ."+cOrdQry+" BETWEEN ? AND ? "
	aAdd(self:aBind,{nParamOrder++,cEntidIni})
	aAdd(self:aBind,{nParamOrder++,cEntidFim})
	
	cQuery += "	AND ARQ."+cEntid+"_CLASSE = ? "
	aAdd(self:aBind,{nParamOrder++,'2'})
			
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ."+cEntid+"_BOOK LIKE ? 	"
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
	
	cQuery += " AND ARQ.D_E_L_E_T_ = ? "
	aAdd(self:aBind,{nParamOrder++,space(1)})

	If !::lVlrZerado
		cQuery += " AND ( "
		
		If lSldA
			For nMes := 1 to ::nLenMeses
				cQuery += "  	(SELECT ROUND(SUM(CQ9_CREDIT),2) - ROUND(SUM(CQ9_DEBITO),2) "
				cQuery += " 		 	FROM "+RetSqlName("CQ9")+" CQ9 "
				cQuery += "   			WHERE CQ9_FILIAL = ? "
				cQuery += "   			AND CQ9_MOEDA = ? "
				cQuery += "   			AND CQ9_TPSALD = ? "
				cQuery += "   			AND CQ9_IDENT = ? "
				cQuery += " 			AND CQ9_CODIGO	= ARQ."+cOrdQry+" "

				aAdd(self:aBind,{nParamOrder++,FwxFilial("CQ9")})
				aAdd(self:aBind,{nParamOrder++,::cMoeda})
				aAdd(self:aBind,{nParamOrder++,cSaldoA})
				aAdd(self:aBind,{nParamOrder++,cEntid})

				If ::lAcum
					cQuery += "    			AND CQ9_DATA <= ? "
					aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,3])})
				Else
					cQuery += "    			AND CQ9_DATA BETWEEN ? AND ? "
					aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,2])})
					aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,3])})
				EndIf
				If ::lImpAntLP .and. ::dDataLP >= ::aMeses[nMes,2]
					cQuery += "	AND CQ9_LP <> ? "
					aAdd(self:aBind,{nParamOrder++,'Z'})
				Endif
				cQuery += "   			AND CQ9.D_E_L_E_T_ = ?) <> 0 "
				aAdd(self:aBind,{nParamOrder++,space(1)})
				If nMes < ::nLenMeses
					cQuery += " OR "
				EndIf
			Next
		EndIf
		If lSldB
			If lSldA
				cQuery += " OR "
			EndIf
			For nMes := 1 to ::nLenMeses
				cQuery += "  	(SELECT ROUND(SUM(CQ9_CREDIT),2) - ROUND(SUM(CQ9_DEBITO),2) "
				cQuery += " 		 	FROM "+RetSqlName("CQ9")+" CQ9 "
				cQuery += "   			WHERE CQ9_FILIAL = ? "
				cQuery += "   			AND CQ9_MOEDA = ? "
				cQuery += "   			AND CQ9_TPSALD = ? "
				cQuery += "   			AND CQ9_IDENT = ? "
				cQuery += " 			AND CQ9_CODIGO	= ARQ."+cOrdQry+" "
				
				aAdd(self:aBind,{nParamOrder++,FwxFilial("CQ9")})
				aAdd(self:aBind,{nParamOrder++,::cMoeda})
				aAdd(self:aBind,{nParamOrder++,cSaldoB})
				aAdd(self:aBind,{nParamOrder++,cEntid})
				
				If ::lAcum
					cQuery += "    			AND CQ9_DATA <= ? "
					aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,3])})
				Else
					cQuery += "    			AND CQ9_DATA BETWEEN ? AND ? "
					aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,2])})
					aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,3])})

				EndIf
				If ::lImpAntLP .and. ::dDataLP >= ::aMeses[nMes,2]
					cQuery += "	AND CQ9_LP <> ? "
					aAdd(self:aBind,{nParamOrder++,'Z'})
				Endif
				cQuery += "   			AND CQ9.D_E_L_E_T_ = ?) <> 0 "
				aAdd(self:aBind,{nParamOrder++,space(1)})
				If nMes < ::nLenMeses
					cQuery += " OR "
				Endif
			Next
		EndIf
		If lSldC
			If lSldA .or. lSldB
				cQuery += " OR "
			EndIf
			For nMes := 1 to ::nLenMeses
				cQuery += "  	(SELECT ROUND(SUM(CQ9_CREDIT),2) - ROUND(SUM(CQ9_DEBITO),2) "
				cQuery += " 		 	FROM "+RetSqlName("CQ9")+" CQ9 "
				cQuery += "   			WHERE CQ9_FILIAL = ? "
				cQuery += "   			AND CQ9_MOEDA = ? "
				cQuery += "   			AND CQ9_TPSALD = ? "
				cQuery += "   			AND CQ9_IDENT = ? "
				cQuery += " 			AND CQ9_CODIGO	= ARQ."+cOrdQry+" "
				
				aAdd(self:aBind,{nParamOrder++,FwxFilial("CQ9")})
				aAdd(self:aBind,{nParamOrder++,::cMoeda})
				aAdd(self:aBind,{nParamOrder++,cSaldoC})
				aAdd(self:aBind,{nParamOrder++,cEntid})
				
				If ::lAcum
					cQuery += "    			AND CQ9_DATA <= ? "
					aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,3])})
				Else
					cQuery += "    			AND CQ9_DATA BETWEEN ? AND ? "
					aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,2])})
					aAdd(self:aBind,{nParamOrder++,DTOS(::aMeses[nMes,3])})

				EndIf
				If ::lImpAntLP .and. ::dDataLP >= ::aMeses[nMes,2]
					cQuery += "	AND CQ9_LP <> ? "
					aAdd(self:aBind,{nParamOrder++,'Z'})
				Endif
				cQuery += "   			AND CQ9.D_E_L_E_T_ = ?) <> 0 "
				aAdd(self:aBind,{nParamOrder++,space(1)})
				If nMes < ::nLenMeses
					cQuery += " OR "
				Endif
			Next
		EndIf
				
		cQuery += " ) "
	EndIf
		
Case cAlias == "CTV"
			
	aChave      := {"CUSTO","ITEM"}
	
	cEntidIni1	:=	::cCCIni
	cEntidFim1	:=	::cCCFim
	cEntidIni2	:=	::cItemIni
	cEntidFim2	:=	::cItemFim
			
	cCampUSU  := "" //// DECLARA VARIAVEL COM OS CAMPOS DO FILTRO DE USUÁRIO
	
	cQuery := " SELECT CTT_CUSTO CUSTO, CTT_RES CCRES, CTT_DESC"+::cMoeda+" DESCCC, CTT_CLASSE TIPOCC, CTT_CCSUP CCSUP, "
	cQuery += " 	    CTD_ITEM ITEM, CTD_RES ITEMRES, CTD_DESC"+::cMoeda+" DESCITEM, CTD_CLASSE TIPOITEM, CTD_ITSUP ITSUP, "
			
	cQuery += cCampUSU									//// ADICIONA OS CAMPOS NA QUERY
			
	If lSldA
		cQuery += self:CtQryComp("CTV",::nLenMeses,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLA",,,@nParamOrder)
	EndIf
	If lSldB
		If lSldA
			cQuery += ", "
		EndIf
		cQuery += self:CtQryComp("CTV",::nLenMeses,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLB",,,@nParamOrder)
	EndIf
	If lSldC
		If lSldA .or. lSldB
			cQuery += ", "
		EndIf
		cQuery += self:CtQryComp("CTV",::nLenMeses,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLC",,,@nParamOrder)
	EndIf
			
	cQuery += " FROM "+RetSqlName("CTT")+" ARQ2, "+RetSqlName("CTD")+" ARQ3 "
	cQuery += " WHERE ARQ2.CTT_FILIAL = ? "
	aAdd(self:aBind,{nParamOrder++,FwxFilial("CTT")})

	cQuery += " AND ARQ2.CTT_CUSTO BETWEEN ? AND ? "
	aAdd(self:aBind,{nParamOrder++,::cCCIni})
	aAdd(self:aBind,{nParamOrder++,::cCCFim})
	
	cQuery += " AND ARQ2.CTT_CLASSE = ?  	"
	aAdd(self:aBind,{nParamOrder++,'2'})
	
	
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ2.CTT_BOOK LIKE ? "
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
	
	cQuery += " AND ARQ3.CTD_FILIAL = ?	"
	aAdd(self:aBind,{nParamOrder++,FwxFilial("CTD")})

	cQuery += " AND ARQ3.CTD_ITEM BETWEEN ? AND ? 	"
	aAdd(self:aBind,{nParamOrder++,::cItemIni})
	aAdd(self:aBind,{nParamOrder++,::cItemFim})

	cQuery += " AND ARQ3.CTD_CLASSE = ?  	"
	aAdd(self:aBind,{nParamOrder++,'2'})
	
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ3.CTD_BOOK LIKE ? "
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
	
	cQuery += " AND ARQ2.D_E_L_E_T_ = ?  	"
	aAdd(self:aBind,{nParamOrder++,space(1)})

	cQuery += " AND ARQ3.D_E_L_E_T_ = ?  	"
	aAdd(self:aBind,{nParamOrder++,space(1)})
			
	If !::lVlrZerado
		cQuery += " AND ( "
		If lSldA
			cQuery += self:CtQryComp("CTV",::nLenMeses,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		If lSldB
			If lSldA
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CTV",::nLenMeses,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		If lSldC
			If lSldA .or. lSldB
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CTV",::nLenMeses,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		cQuery += " ) "
	EndIf
						
Case cAlias == "CTW"
			
	aChave := {"CUSTO","CLVL"}	
	
	cCampUSU  := ""										//// DECLARA VARIAVEL COM OS CAMPOS DO FILTRO DE USUÁRIO
	
	cQuery := "SELECT CTT_CUSTO CUSTO, CTT_RES CCRES, CTT_DESC"+::cMoeda+" DESCCC, CTT_CLASSE TIPOCC, CTT_CCSUP CCSUP, "
	cQuery += "       CTH_CLVL CLVL, CTH_RES CLVLRES, CTH_DESC"+::cMoeda+" DESCCLVL, CTH_CLASSE TIPOCLVL, CTH_CLSUP CLSUP, "
	
	cQuery += cCampUSU									//// ADICIONA OS CAMPOS NA QUERY
			
	If lSldA
		cQuery += self:CtQryComp("CTW",::nLenMeses,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLA",,,@nParamOrder)
	EndIf
	If lSldB
		If lSldA
			cQuery += ", "
		EndIf
		cQuery += self:CtQryComp("CTW",::nLenMeses,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLB",,,@nParamOrder)
	EndIf
	If lSldC
		If lSldA .or. lSldB
			cQuery += ", "
		EndIf
		cQuery += self:CtQryComp("CTW",::nLenMeses,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLC",,,@nParamOrder)
	EndIf
			
	cQuery += " FROM "+RetSqlName("CTT")+" ARQ2, "+RetSqlName("CTH")+" ARQ4 "
	
	cQuery += " WHERE ARQ2.CTT_FILIAL = ? 	"
	aAdd(self:aBind,{nParamOrder++,FwxFilial("CTT")})
	
	cQuery += " AND ARQ2.CTT_CUSTO BETWEEN ? AND ? "
	aAdd(self:aBind,{nParamOrder++,::cCCIni})
	aAdd(self:aBind,{nParamOrder++,::cCCFim})
	
	cQuery += " AND ARQ2.CTT_CLASSE = ? 	"
	aAdd(self:aBind,{nParamOrder++,'2'})
			
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ2.CTT_BOOK LIKE ? "
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
			
	cQuery += " AND ARQ4.CTH_FILIAL = ?	"
	aAdd(self:aBind,{nParamOrder++,FwxFilial("CTH")})

	cQuery += " AND ARQ4.CTH_CLVL BETWEEN ? AND ? "
	aAdd(self:aBind,{nParamOrder++,::cClVlIni})
	aAdd(self:aBind,{nParamOrder++,::cClVlFim})

	cQuery += " AND ARQ4.CTH_CLASSE = ?  	"
	aAdd(self:aBind,{nParamOrder++,'2'})
			
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ4.CTH_BOOK LIKE ? "
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
			
	cQuery += " AND ARQ2.D_E_L_E_T_ = ?  	"
	aAdd(self:aBind,{nParamOrder++,space(1)})
	
	cQuery += " AND ARQ4.D_E_L_E_T_ = ?  	"
	aAdd(self:aBind,{nParamOrder++,space(1)})
			
	If !::lVlrZerado
		cQuery += " AND ( "
		If lSldA
			cQuery += self:CtQryComp("CTW",::nLenMeses,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		If lSldB
			If lSldA
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CTW",::nLenMeses,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		If lSldC
			If lSldA .or. lSldB
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CTW",::nLenMeses,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		cQuery += " ) "
	EndIf
			
Case cAlias == "CTX"
			
	aChave := {"ITEM","CLVL"}
				
	cCampUSU  := ""										//// DECLARA VARIAVEL COM OS CAMPOS DO FILTRO DE USUÁRIO
	
	cQuery := "SELECT CTD_ITEM ITEM, CTD_RES ITEMRES, CTD_DESC"+::cMoeda+" DESCITEM, CTD_CLASSE TIPOITEM, CTD_ITSUP ITSUP, "
	cQuery += "       CTH_CLVL CLVL, CTH_RES CLVLRES, CTH_DESC"+::cMoeda+" DESCCLVL, CTH_CLASSE TIPOCLVL, CTH_CLSUP CLSUP, "
	
	cQuery += cCampUSU									//// ADICIONA OS CAMPOS NA QUERY
			
	If lSldA
		cQuery += self:CtQryComp("CTX",::nLenMeses,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLA",,,@nParamOrder)
	EndIf
	If lSldB
		If lSldA
			cQuery += ", "
		EndIf
		cQuery += self:CtQryComp("CTX",::nLenMeses,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLB",,,@nParamOrder)
	EndIf
	If lSldC
		If lSldA .or. lSldB
			cQuery += ", "
		EndIf
		cQuery += self:CtQryComp("CTX",::nLenMeses,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLC",,,@nParamOrder)
	EndIf
	
	cQuery += " FROM "+RetSqlName("CTD")+" ARQ3, "+RetSqlName("CTH")+" ARQ4 "
	cQuery += " WHERE ARQ3.CTD_FILIAL = ?	"
	aAdd(self:aBind,{nParamOrder++,FwxFilial("CTD")})

	cQuery += " AND ARQ3.CTD_ITEM BETWEEN ? AND ?  	"
	aAdd(self:aBind,{nParamOrder++,::cItemIni})
	aAdd(self:aBind,{nParamOrder++,::cItemFim})

	cQuery += " AND ARQ3.CTD_CLASSE = ? 	"
	aAdd(self:aBind,{nParamOrder++,'2'})
			
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ3.CTD_BOOK LIKE ? "
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
			
	cQuery += " AND ARQ4.CTH_FILIAL = ?  	"
	aAdd(self:aBind,{nParamOrder++,FwxFilial("CTH")})

	cQuery += " AND ARQ4.CTH_CLVL BETWEEN ? AND ?  	"
	aAdd(self:aBind,{nParamOrder++,::cClVlIni})
	aAdd(self:aBind,{nParamOrder++,::cClVlFim})

	cQuery += " AND ARQ4.CTH_CLASSE = ?  	"
	aAdd(self:aBind,{nParamOrder++,'2'})
			
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ4.CTH_BOOK LIKE ? "
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
			
	cQuery += " AND ARQ3.D_E_L_E_T_ = ?  	"
	aAdd(self:aBind,{nParamOrder++,space(1)})

	cQuery += " AND ARQ4.D_E_L_E_T_ = ?  	"
	aAdd(self:aBind,{nParamOrder++,space(1)})
	
	If !::lVlrZerado
		cQuery += " AND ( "
		If lSldA
			cQuery += self:CtQryComp("CTX",::nLenMeses,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		If lSldB
			If lSldA
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CTX",::nLenMeses,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		If lSldC
			If lSldA .or. lSldB
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CTX",::nLenMeses,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		cQuery += " ) "
	EndIf
			
Case cAlias == "CTY"
			
	aChave := {"CUSTO","ITEM","CLVL"}
				
	cCampUSU  := ""										//// DECLARA VARIAVEL COM OS CAMPOS DO FILTRO DE USUÁRIO
	
	cQuery := "SELECT CTH_CLVL CLVL, CTH_RES CLVLRES, CTH_DESC"+::cMoeda+" DESCCLVL, CTH_CLASSE TIPOCLVL, CTH_CLSUP CLSUP, "
	cQuery += " 	  CTT_CUSTO CUSTO,  CTT_RES CCRES, CTT_DESC"+::cMoeda+" DESCCC, CTT_CLASSE TIPOCC, CTT_CCSUP CCSUP, "
	cQuery += " 	  CTD_ITEM ITEM, CTD_RES ITEMRES, CTD_DESC"+::cMoeda+" DESCITEM, CTD_CLASSE TIPOITEM, CTD_ITSUP ITSUP, "
	cQuery += cCampUSU									//// ADICIONA OS CAMPOS NA QUERY
			
	If lSldA
		cQuery += self:CtQryComp("CTY",::nLenMeses,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLA",,,@nParamOrder)
	EndIf
	If lSldB
		If lSldA
			cQuery += ", "
		EndIf
		cQuery += self:CtQryComp("CTY",::nLenMeses,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLB",,,@nParamOrder)
	EndIf
	If lSldC
		If lSldA .or. lSldB
			cQuery += ", "
		EndIf
		cQuery += self:CtQryComp("CTY",::nLenMeses,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP,"COLC",,,@nParamOrder)
	EndIf
			
	cQuery += " FROM "+RetSqlName("CTH")+" ARQ4, "+RetSqlName("CTT")+" ARQ2, "+RetSqlName("CTD")+" ARQ3 "
	cQuery += " WHERE ARQ2.CTT_FILIAL = ?  	"
	aAdd(self:aBind,{nParamOrder++,FwxFilial("CTT")})

	cQuery += " AND ARQ2.CTT_CUSTO BETWEEN ? AND ? "
	aAdd(self:aBind,{nParamOrder++,::cCCIni})
	aAdd(self:aBind,{nParamOrder++,::cCCFim})

	cQuery += " AND ARQ2.CTT_CLASSE = ?  	"
	aAdd(self:aBind,{nParamOrder++,'2'})
			
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ2.CTT_BOOK LIKE  ? 	"
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
			
	cQuery += " AND ARQ3.CTD_FILIAL = ?  	"
	aAdd(self:aBind,{nParamOrder++,FwxFilial("CTD")})

	cQuery += " AND ARQ3.CTD_ITEM BETWEEN ? AND ?  	"
	aAdd(self:aBind,{nParamOrder++,::cItemIni})
	aAdd(self:aBind,{nParamOrder++,::cItemFim})

	cQuery += " AND ARQ3.CTD_CLASSE = ?  	"
	aAdd(self:aBind,{nParamOrder++,'2'})
			
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ3.CTD_BOOK LIKE ? "
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
			
	cQuery += " AND ARQ4.CTH_FILIAL = ?  	"
	aAdd(self:aBind,{nParamOrder++,FwxFilial("CTH")})

	cQuery += " AND ARQ4.CTH_CLVL BETWEEN ? AND ? 	"
	aAdd(self:aBind,{nParamOrder++,::cClVlIni})
	aAdd(self:aBind,{nParamOrder++,::cClVlFim})

	cQuery += " AND ARQ4.CTH_CLASSE = ?  	"
	aAdd(self:aBind,{nParamOrder++,'2'})
			
	If !Empty(::aSetOfBook[1])
		cQuery += " AND ARQ4.CTH_BOOK LIKE ? "
		aAdd(self:aBind,{nParamOrder++,"%"+::aSetOfBook[1]+"%"})
	Endif
			
	cQuery += " AND ARQ4.D_E_L_E_T_ = ? "
	aAdd(self:aBind,{nParamOrder++,space(1)})

	cQuery += " AND ARQ2.D_E_L_E_T_ = ? "
	aAdd(self:aBind,{nParamOrder++,space(1)})

	cQuery += " AND ARQ3.D_E_L_E_T_ = ? "
	aAdd(self:aBind,{nParamOrder++,space(1)})
			
	If !::lVlrZerado
		cQuery += " AND ( "
		If lSldA
			cQuery += self:CtQryComp("CTY",::nLenMeses,::cMoeda,cSaldoA,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		If lSldB
			If lSldA
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CTY",::nLenMeses,::cMoeda,cSaldoB,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		If lSldC
			If lSldA .or. lSldB
				cQuery += " OR "
			EndIf
			cQuery += self:CtQryComp("CTY",::nLenMeses,::cMoeda,cSaldoC,::lAcum,::aMeses,::lImpAntLP,::dDataLP," <> 0 ",,,@nParamOrder)
		EndIf
		cQuery += " ) "
	EndIf
			
EndCase
		
		If ! Empty( cQuery )
			cQuery := ChangeQuery(cQuery)
			
			self:oQuery := FWExecStatement():New(cQuery)

			//aBind[1] = numero da sequencia do Bind
			//aBind[2] = parametro substituido (todos estão ou devem estar em string)
			For nX:=1 to len(self:aBind)
				self:oQuery:SetString(self:aBind[nX,1], self:aBind[nX,2])
			Next nX
			
			cQuery := self:oQuery:GetFixQuery()

			self:cAlias  := MPSysOpenQuery(cQuery)	
			
			For nColunas := 1 to ::nLenMeses
				If lSldA
					TcSetField((::cAlias),"COLA"+Str(nColunas,Iif(nColunas>9,2,1)),"N",aTamVlr[1],aTamVlr[2])
				EndIf
				If lSldB
					TcSetField((::cAlias),"COLB"+Str(nColunas,Iif(nColunas>9,2,1)),"N",aTamVlr[1],aTamVlr[2])
				EndIf
				If lSldC
					TcSetField((::cAlias),"COLC"+Str(nColunas,Iif(nColunas>9,2,1)),"N",aTamVlr[1],aTamVlr[2])
				EndIf
			Next
		EndIf
		
		If !Empty(::aSetOfBook[5])				// Indica qual o Plano Gerencial Anexado
			aChave := {"CONTA"}
		Endif
		//preenchimento do array com os campos
		aCampos := self:criaTabTemp()

		::_oCTBR2951 := FWTemporaryTable():New( "cArqTmp" )  
		::_oCTBR2951:SetFields(aCampos) 
		::_oCTBR2951:AddIndex("1", aChave)

		If !Empty(::aSetOfBook[5])				// Indica qual o Plano Gerencial Anexado
			::_oCTBR2951:AddIndex("2", {"ORDEM"})
		Endif
					
		//-----------------------------
		//Criação da tabela temporaria
		//-----------------------------
		::_oCTBR2951:Create()
		
		dbSelectArea("cArqTmp")
		
		If  Empty(::aSetOfBook[5])				/// SÓ HÁ QUERY SEM O PLANO GERENCIAL
			//// SE FOR DEFINIÇÃO TOP
			If Select((self:cAlias)) > 0		/// E O ALIAS TRBTMP ESTIVER ABERTO (INDICANDO QUE A QUERY FOI EXECUTADA)
				dbSelectArea((self:cAlias))
				aStruTMP := dbStruct()			/// OBTEM A ESTRUTURA DO TMP
				
				dbSelectArea((self:cAlias))
				dbGoTop()						/// POSICIONA NO 1º REGISTRO DO TMP
				
				While (self:cAlias)->(!Eof())			/// REPLICA OS DADOS DA QUERY (TRBTMP) PARA P/ O TEMPORARIO EM DISCO
					
					RecLock("cArqTMP",.T.)
					For nTRB := 1 to Len(aStruTMP)
						If Subs(aStruTmp[nTRB][1],1,4) $ "COLA/COLB/COLC" .And. ::nDivide > 1
							Field->&(aStruTMP[nTRB,1])	:= (((self:cAlias)->&(aStruTMP[nTRB,1])))/::nDivide
						Else
							Field->&(aStruTMP[nTRB,1]) := (self:cAlias)->&(aStruTMP[nTRB,1])
						EndIf
					Next
					cArqTMP->(MsUnlock())
					
					(self:cAlias)->(dbSkip())
				Enddo
				
				dbSelectArea((self:cAlias))
				(self:cAlias)->( DBCloseArea() )					/// FECHA O TRBTMP (RETORNADO DA QUERY)
			Endif
		EndIf
		
		dbSelectArea("cArqTmp")
		dbSetOrder(1)
		
RestArea(aSaveArea)

Return cArqTmp
//-------------------------------------------------------------------
/*/{Protheus.doc} CtQryComp
    
    Grava os dados na tabela temporaria
    @author wilton.santos
    @since 28/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method CtQryComp(cArqBase as character,nFim as integer,cMoeda as character,cSaldo as character,lAcum as logical,;
				aMeses as array,lImpAntLP as logical,dDataLP as date,cFinal as character,nInicio as integer,;
				lUnico as logical, nParamOrder as integer) class MonthtoMonthBalanceComparisonSmartViewBusinessObject
Local nMes              as integer
Local lUsaCusto   as logical
Local lUsaItem as logical
Local cQuery   := "" as character
Local cAlias := "" as character

DEFAULT nInicio := 1   
DEFAULT lUnico := .F.  

If cArqBase == "CT1"
	dbSelectArea("CQ1")
	cAlias := "CQ1"
ElseIf cArqBase == "CTT"
	dbSelectArea("CQ3")
	cAlias := "CQ3"
ElseIf cArqBase == "CTD"
	dbSelectArea("CQ5")
	cAlias := "CQ5"
ElseIf cArqBase == "CTH"
	dbSelectArea("CQ7")
	cAlias := "CQ7"
ElseIf cArqBase == "CTV"// Custo+Item
	dbSelectArea("CQ5")
	cAlias := "CQ5"
ElseIf cArqBase == "CTW"// Custo+Classe
	dbSelectArea("CQ7")
	cAlias := "CQ7"
ElseIf cArqBase == "CTX"// Item+Classe
	dbSelectArea("CQ7")
	cAlias := "CQ7"
ElseIf cArqBase == "CTY"  // Custo+Item+Classe
	dbSelectArea("CQ7")
	cAlias := "CQ7"
EndIf

If cAlias $ "CQ7/CQ5"  // CTD = Conta+Custo+Item  ou  Conta+Item
	lUsaCusto := ::lUsaCusto
	If cAlias == "CQ7"  // Conta+CCusto+Item+Cl.Valor  ou  Conta+CCusto+Cl.Valor  ou  Conta+Item+Cl.Valor  ou  Conta+Cl.Valor
		lUsaItem := ::lUsaItem
	EndIf
EndIf

// Tabelas:
// ARQ  -> CT1
// ARQ2 -> CTT
// ARQ3 -> CTD
// ARQ4 -> CTH       
if (lUnico)
 nInicio:=nFim
EndIf 

For nMes := nInicio to nFim
	cQuery += "  	(SELECT COALESCE( SUM("+cAlias+"_CREDIT) - SUM("+cAlias+"_DEBITO), 0) "
	cQuery += " 		 	FROM "+RetSqlName(cAlias)+" "+cAlias
	cQuery += "   			WHERE "+cAlias+"_FILIAL = ? "
	cQuery += "   			AND "+cAlias+"_MOEDA = ? "
	cQuery += "   			AND "+cAlias+"_TPSALD = ? "

	aAdd(self:aBind,{nParamOrder++,FwxFilial(cAlias)})
	aAdd(self:aBind,{nParamOrder++,cMoeda})
	aAdd(self:aBind,{nParamOrder++,cSaldo})
   
   If cArqBase == "CT1"      // Conta
		cQuery += "  			AND CQ1_CONTA = ARQ.CT1_CONTA "

   ElseIf cArqBase == "CTT"   // Conta+Custo
		cQuery += "  			AND CQ3_CONTA = ARQ.CT1_CONTA "
		cQuery += "   			AND CQ3_CCUSTO = ARQ2.CTT_CUSTO "

   ElseIf cArqBase == "CTD"  // Conta+Custo+Item  ou  Conta+Item
		cQuery += "   			AND CQ5_ITEM = ARQ3.CTD_ITEM "
		If lUsaCusto
			cQuery += "   			AND CQ5_CCUSTO = ARQ2.CTT_CUSTO "
		EndIf
		cQuery += "  			AND CQ5_CONTA = ARQ.CT1_CONTA "

   ElseIf cArqBase == "CTH"  // Conta+CCusto+Item+Cl.Valor  ou  Conta+CCusto+Cl.Valor  ou  Conta+Item+Cl.Valor  ou  Conta+Cl.Valor
		cQuery += "   			AND CQ7_CLVL = ARQ4.CTH_CLVL "
		If lUsaItem
			cQuery += "   			AND CQ7_ITEM = ARQ3.CTD_ITEM "
		EndIf
  		If lUsaCusto
			cQuery += "   			AND CQ7_CCUSTO = ARQ2.CTT_CUSTO "
		EndIf
		cQuery += "  			AND CQ7_CONTA = ARQ.CT1_CONTA "

   ElseIf cArqBase == "CTV"  // Custo+Item
		cQuery += "   			AND CQ5_CCUSTO = ARQ2.CTT_CUSTO "
		cQuery += "  			AND CQ5_ITEM = ARQ3.CTD_ITEM "

	ElseIf cArqBase == "CTW"  // Custo+Classe
		cQuery += "   			AND CQ7_CCUSTO = ARQ2.CTT_CUSTO "
		cQuery += "  			AND CQ7_CLVL = ARQ4.CTH_CLVL "

	ElseIf cArqBase == "CTX"  // Item+Classe
		cQuery += "  			AND CQ7_ITEM = ARQ3.CTD_ITEM "
		cQuery += "   			AND CQ7_CLVL = ARQ4.CTH_CLVL "

	ElseIf cArqBase == "CTY"  // Custo+Item+Classe
		cQuery += "   			AND CQ7_CCUSTO = ARQ2.CTT_CUSTO "
		cQuery += "   			AND CQ7_ITEM = ARQ3.CTD_ITEM "
		cQuery += "   			AND CQ7_CLVL = ARQ4.CTH_CLVL "
	EndIf
	
	If lAcum .and. (nMes == 1)
		cQuery += "   			AND "+cAlias+"_DATA <= ? "
		aAdd(self:aBind,{nParamOrder++,DTOS(aMeses[nMes,3])})
	Else 
		If lUnico
			cQuery += "   			AND "+cAlias+"_DATA BETWEEN ? AND ? "			
			aAdd(self:aBind,{nParamOrder++,DTOS(aMeses[1,2])})
			aAdd(self:aBind,{nParamOrder++,DTOS(aMeses[nMes,3])})
		Else
			cQuery += "   			AND "+cAlias+"_DATA BETWEEN ? AND ? "
			aAdd(self:aBind,{nParamOrder++,DTOS(aMeses[nMes,2])})
			aAdd(self:aBind,{nParamOrder++,DTOS(aMeses[nMes,3])})
		EndIf
	EndIf
	If lImpAntLP .And. dDataLP >= aMeses[nMes,2]
		cQuery += "	AND "+cAlias+"_LP <> ? "
		aAdd(self:aBind,{nParamOrder++,'Z'})
	Endif

	cQuery += "   			AND "+cAlias+".D_E_L_E_T_ = ?) " 	
	aAdd(self:aBind,{nParamOrder++,space(1)})

	cQuery += cFinal			/// NOME DA COLUNA "COL" OU CLAUSULA WHERE "<> 0"

	If Left(cFinal,3) == "COL"
		cQuery += ALLTRIM(STR(nMes))
		If nMes < nFim 
			cQuery += ", "
		EndIf
	Else
		If nMes < nFim 
			cQuery += " OR "
		EndIf	
	EndIf
Next

//bind

Return cQuery
//-------------------------------------------------------------------
/*/{Protheus.doc} CtbPeriodos
    Ajusta o parametro nDivide vindo do objeto de negocio
    @author gustavo.campos
    @since 30/08/2023
    @version 12.1.2210
/*/ 
//-------------------------------------------------------------------
method compvld(nDivide as Numeric) class MonthtoMonthBalanceComparisonSmartViewBusinessObject
    
    If nDivide == 2			// Divide por cem
	   ::nDivide := 100
    ElseIf nDivide == 3		// Divide por mil
	   ::nDivide := 1000
    ElseIf nDivide == 4		// Divide por milhao
	   ::nDivide := 1000000
	Else
	   ::nDivide := 1	   
    EndIf

return
//-------------------------------------------------------------------
/*/{Protheus.doc} setDados
    Trata a query
    @author wilton.santos
    @since 28/12/2023
    @version 12.1.2310
/*/ 
//-------------------------------------------------------------------
method setDados(oFilter) as object class MonthtoMonthBalanceComparisonSmartViewBusinessObject
Local cMascCta              as character
Local cMascCus              as character
Local cMascIte              as character
Local cMascClv              as character
Local cConta                as character
Local cCusto                as character
Local cItem                 as character
Local cClvl                 as character
Local cSepara  			    as character
Local cPicture  			as character
Local nTotLinhaA	:= 0 	as numeric 
Local nTotLinhaB	:= 0 	as numeric 
Local nTotLinhaC	:= 0 	as numeric 
Local cDescSLA		:="" 	as character 
Local cDescSLB		:="" 	as character 
Local cDescSLC		:="" 	as character 
Local lTemSLDA				as logical
Local lTemSLDB				as logical
Local lTemSldC				as logical
//Tratamento para impressão
//-----------------------------------------------------------------------
cDescSLA	:= ALLTRIM(Tabela("SL", ::cSaldoA, .F.))
cDescSLB	:= ALLTRIM(Tabela("SL", ::cSaldoB, .F.))
cDescSLC	:= ALLTRIM(Tabela("SL", ::cSaldoC, .F.))

lTemSLDA	:= !Empty(::cSaldoA)
lTemSLDB	:= !Empty(::cSaldoB)
lTemSldC	:= !Empty(::cSaldoC)

cPicture    := Iif(Empty(::aSetOfBook[4]),"@E 9,999,999,999.99","")

//coletando cada maskara
cMascCta   := IIF(Empty(::aSetOfBook[2]),SuperGetMv("MV_MASCARA"),RetMasCtb(::aSetOfBook[2],@cSepara))//Mascara da Conta
cMascCus   := IIF(Empty(::aSetOfBook[6]),SuperGetMv("MV_MASCCUS"),RetMasCtb(::aSetOfBook[6],@cSepara))//Mascara do Centro de Custo
cMascIte   := IIF(Empty(::aSetOfBook[7]),SuperGetMv("MV_MASCCTD"),RetMasCtb(::aSetOfBook[7],@cSepara))//Mascara do Item Contabil  
cMascClv   := IIF(Empty(::aSetOfBook[8]),SuperGetMv("MV_MASCCTH"),RetMasCtb(::aSetOfBook[8],@cSepara))//Mascara da Classe Valor

If Select("cArqTmp") > 0

	dbSelectArea("cArqTmp")
	cArqTmp->(DBGoTop())
	While !cArqTmp->(Eof())
		
		cConta := EntidadeCTB(cArqTMP->CONTA ,0,0,Nil/*Tamanho*/,.F.,cMascCta,""/*Separador*/,"CT1",,,,.F.)
		cCusto := EntidadeCTB(cArqTMP->CUSTO ,0,0,Nil/*Tamanho*/,.F.,cMascCus,""/*Separador*/,"CTT",,,,.F.)
		cItem  := EntidadeCTB(cArqTMP->ITEM  ,0,0,Nil/*Tamanho*/,.F.,cMascIte,""/*Separador*/,"CTD",,,,.F.)
		cClvl  := EntidadeCTB(cArqTMP->CLVL  ,0,0,Nil/*Tamanho*/,.F.,cMascClv,""/*Separador*/,"CTH",,,,.F.)
		
		//Calcula o Total das Linhas
		If lTemSldA
			nTotLinhaA	:= IIf (::lAcum,COLA12,COLA1+COLA2+COLA3+COLA4+COLA5+COLA6+COLA7+COLA8+COLA9+COLA10+COLA11+COLA12)
		EndiF
		If lTemSldB
			nTotLinhaB	:= IIf (::lAcum,COLB12,COLB1+COLB2+COLB3+COLB4+COLB5+COLB6+COLB7+COLB8+COLB9+COLB10+COLB11+COLB12)
		EndIf
		If lTemSldC
			nTotLinhaC	:= IIf (::lAcum,COLC12,COLC1+COLC2+COLC3+COLC4+COLC5+COLC6+COLC7+COLC8+COLC9+COLC10+COLC11+COLC12)
		EndIf

		//Novo objeto
		::jItens := JsonObject():new()

		::jItens["CONTA"	] := cConta	
		::jItens["CTASUP"	] := cArqTMP->CTASUP	
		::jItens["CTARES"	] := cArqTMP->CTARES	
		::jItens["DESCCTA"	] := cArqTMP->DESCCTA	
		::jItens["NORMAL"	] := cArqTMP->NORMAL	
		::jItens["TIPOCONTA"] := cArqTMP->TIPOCONTA
		::jItens["CUSTO"	] := cCusto	
		::jItens["CCSUP"	] := cArqTMP->CCSUP	
		::jItens["CCRES"	] := cArqTMP->CCRES	
		::jItens["DESCCC" 	] := cArqTMP->DESCCC 	
		::jItens["TIPOCC"  	] := cArqTMP->TIPOCC  	
		::jItens["ITEM"		] := cItem		
		::jItens["ITSUP"	] := cArqTMP->ITSUP	
		::jItens["ITEMRES" 	] := cArqTMP->ITEMRES 	
		::jItens["DESCITEM" ] := cArqTMP->DESCITEM 
		::jItens["TIPOITEM"	] := cArqTMP->TIPOITEM	
		::jItens["CLVL"		] := cClvl	
		::jItens["CLSUP"	] := cArqTMP->CLSUP	
		::jItens["CLVLRES"	] := cArqTMP->CLVLRES	
		::jItens["DESCCLVL" ] := cArqTMP->DESCCLVL 
		::jItens["TIPOCLVL"	] := cArqTMP->TIPOCLVL	
		::jItens["ORDEM"	] := cArqTMP->ORDEM	
		::jItens["GRUPO"	] := cArqTMP->GRUPO	
		::jItens["IDENTIFI"	] := cArqTMP->IDENTIFI	
		::jItens["ESTOUR"  	] := cArqTMP->ESTOUR  	
		::jItens["NIVEL1"	] := cArqTMP->NIVEL1	
		::jItens["COLA1"	] := cArqTMP->COLA1	
		::jItens["COLA2"   	] := cArqTMP->COLA2   	
		::jItens["COLA3"   	] := cArqTMP->COLA3   	
		::jItens["COLA4" 	] := cArqTMP->COLA4 	
		::jItens["COLA5" 	] := cArqTMP->COLA5 	
		::jItens["COLA6"  	] := cArqTMP->COLA6  	
		::jItens["COLA7"	] := cArqTMP->COLA7	
		::jItens["COLA8"   	] := cArqTMP->COLA8   	
		::jItens["COLA9"   	] := cArqTMP->COLA9   	
		::jItens["COLA10" 	] := cArqTMP->COLA10 	
		::jItens["COLA11" 	] := cArqTMP->COLA11 	
		::jItens["COLA12"  	] := cArqTMP->COLA12  	
		::jItens["COLB1"	] := cArqTMP->COLB1	
		::jItens["COLB2"   	] := cArqTMP->COLB2   	
		::jItens["COLB3"   	] := cArqTMP->COLB3   	
		::jItens["COLB4" 	] := cArqTMP->COLB4 	
		::jItens["COLB5" 	] := cArqTMP->COLB5 	
		::jItens["COLB6"   	] := cArqTMP->COLB6   	
		::jItens["COLB7"	] := cArqTMP->COLB7	
		::jItens["COLB8"   	] := cArqTMP->COLB8   	
		::jItens["COLB9"   	] := cArqTMP->COLB9   	
		::jItens["COLB10" 	] := cArqTMP->COLB10 	
		::jItens["COLB11" 	] := cArqTMP->COLB11 	
		::jItens["COLB12"  	] := cArqTMP->COLB12  	
		::jItens["COLC1"	] := cArqTMP->COLC1	
		::jItens["COLC2"   	] := cArqTMP->COLC2   	
		::jItens["COLC3"   	] := cArqTMP->COLC3   	
		::jItens["COLC4" 	] := cArqTMP->COLC4 	
		::jItens["COLC5" 	] := cArqTMP->COLC5 	
		::jItens["COLC6"   	] := cArqTMP->COLC6   	
		::jItens["COLC7"	] := cArqTMP->COLC7	
		::jItens["COLC8"   	] := cArqTMP->COLC8   	
		::jItens["COLC9"   	] := cArqTMP->COLC9   	
		::jItens["COLC10" 	] := cArqTMP->COLC10 	
		::jItens["COLC11" 	] := cArqTMP->COLC11 	
		::jItens["COLC12"  	] := cArqTMP->COLC12  	
		::jItens["TOTLA"  	] := nTotLinhaA  	
		::jItens["TOTLB"  	] := nTotLinhaB  	
		::jItens["TOTLC"  	] := nTotLinhaC  	
		::jItens["DESCSLDA"	] := cDescSLA  	
		::jItens["DESCSLDB"	] := cDescSLB  	
		::jItens["DESCSLDC"	] := cDescSLC  	

		self:processData()
		self:oData:appendData(::jItens)

		nTotLinhaA	:= 0
		nTotLinhaB	:= 0
		nTotLinhaC	:= 0

		cArqTMP->(DbSkip())
			
	ENDDO

EndIF
return self:oData
//-------------------------------------------------------------------
/*/{Protheus.doc} setDados
    Preenche o array com os campos da tabela temporaria
    @author wilton.santos
    @since 04/01/2024
    @version 12.1.2310
/*/ 
//-------------------------------------------------------------------
method criaTabTemp() as array class MonthtoMonthBalanceComparisonSmartViewBusinessObject
Local aTamConta		:= TAMSX3("CT1_CONTA")   			 as array
Local aTamCtaRes	:= TAMSX3("CT1_RES")     			 as array
Local aTamCC        := TAMSX3("CTT_CUSTO")   			 as array
Local aTamCCRes 	:= TAMSX3("CTT_RES")     			 as array
Local aTamItem  	:= TAMSX3("CTD_ITEM")    			 as array
Local aTamItRes 	:= TAMSX3("CTD_RES")     			 as array
Local aTamClVl  	:= TAMSX3("CTH_CLVL")    			 as array
Local aTamCvRes 	:= TAMSX3("CTH_RES")     			 as array
Local aCampos     							 			 as array
Local nTamCta 		:= Len(CriaVar("CT1_DESC"+::cMoeda)) as numeric
Local nTamItem		:= Len(CriaVar("CTD_DESC"+::cMoeda)) as numeric
Local nTamCC  		:= Len(CriaVar("CTT_DESC"+::cMoeda)) as numeric
Local nTamClVl		:= Len(CriaVar("CTH_DESC"+::cMoeda)) as numeric
Local nTamGrupo		:= Len(CriaVar("CT1_GRUPO")) 		 as numeric
Local aTamVal		:= TAMSX3("CT2_VALOR") 				 as array 
Local nDecimal      := DecimalCTB(::aSetOfBook,::cMoeda) as numeric

::nDecimais := Iif(nDecimal!= 0 ,nDecimal,aTamVal[2]) 
 

aCampos := {	{ "CONTA"	   	, "C", aTamConta[1] 	, 0 },;  		// Codigo da Conta
				{ "CTASUP"		, "C", aTamConta[1] 	, 0 },;			// Codigo da Conta Superior
				{ "CTARES"		, "C", aTamCtaRes[1]	, 0 },;  		// Codigo Reduzido da Conta
				{ "DESCCTA"	   	, "C", nTamCta			, 0 },;  		// Descricao da Conta
				{ "NORMAL"		, "C", 01				, 0 },;			// Situacao da Conta
				{ "TIPOCONTA"	, "C", 01				, 0 },;			// Conta Analitica / Sintetica
				{ "CUSTO"		, "C", aTamCC[1]		, 0 },; 	 	// Codigo do Centro de Custo
				{ "CCSUP"		, "C", aTamCC[1]		, 0 },;			// Codigo do Centro de Custo Superior
				{ "CCRES"		, "C", aTamCCRes[1]		, 0 },;  		// Codigo Reduzido do Centro de Custo
				{ "DESCCC" 	   	, "C", nTamCC			, 0 },;  		// Descricao do Centro de Custo
				{ "TIPOCC"  	, "C", 01				, 0 },;			// Centro de Custo Analitico / Sintetico
				{ "ITEM"		, "C", aTamItem[1]		, 0 },; 	 	// Codigo do Item
				{ "ITSUP"		, "C", aTamItem[1]		, 0 },;			// Codigo do Item Superior
				{ "ITEMRES" 	, "C", aTamItRes[1]		, 0 },;  		// Codigo Reduzido do Item
				{ "DESCITEM" 	, "C", nTamItem			, 0 },;  		// Descricao do Item
				{ "TIPOITEM"	, "C", 01				, 0 },;			// Item Analitica / Sintetica
				{ "CLVL"		, "C", aTamClVl[1]		, 0 },; 	 	// Codigo da Classe de Valor
				{ "CLSUP"	   	, "C", aTamClVl[1] 		, 0 },;			// Codigo da Classe de Valor Superior
				{ "CLVLRES"	   	, "C", aTamCVRes[1]		, 0 },; 		// Cod. Red. Classe de Valor
				{ "DESCCLVL"   	, "C", nTamClVl			, 0 },;  		// Descricao da Classe de Valor
				{ "TIPOCLVL"	, "C", 01				, 0 },;			// Classe de Valor Analitica / Sintetica
				{ "ORDEM"		, "C", 10				, 0 },;			// Ordem
				{ "GRUPO"		, "C", nTamGrupo		, 0 },;			// Grupo Contabil
				{ "IDENTIFI"	, "C", 01				, 0 },;
				{ "ESTOUR"  	, "C", 01				, 0 },;			// Define se eh conta estourada
				{ "NIVEL1"		, "L", 01				, 0 }}			// Logico para identificar se eh de nivel 1 -> total do relatorio

	aAdd(aCampos, { "COLA1"		, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo A Periodo 1
	aAdd(aCampos, { "COLA2"   	, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo A Periodo 2
	aAdd(aCampos, { "COLA3"   	, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo A Periodo 3
	aAdd(aCampos, { "COLA4" 	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo A Periodo 4
	aAdd(aCampos, { "COLA5" 	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo A Periodo 5
	aAdd(aCampos, { "COLA6"  	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo A Periodo 6
	aAdd(aCampos, { "COLA7"		, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo A Periodo 7
	aAdd(aCampos, { "COLA8"   	, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo A Periodo 8
	aAdd(aCampos, { "COLA9"   	, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo A Periodo 9
	aAdd(aCampos, { "COLA10" 	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo A Periodo 10
	aAdd(aCampos, { "COLA11" 	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo A Periodo 11
	aAdd(aCampos, { "COLA12"  	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo A Periodo 12

	aAdd(aCampos, { "COLB1"		, "N", aTamVal[1]+2, ::nDecimais} )	// Saldo B Periodo 1
	aAdd(aCampos, { "COLB2"   	, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo B Periodo 2
	aAdd(aCampos, { "COLB3"   	, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo B Periodo 3
	aAdd(aCampos, { "COLB4" 	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo B Periodo 4
	aAdd(aCampos, { "COLB5" 	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo B Periodo 5
	aAdd(aCampos, { "COLB6"   	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo B Periodo 6
	aAdd(aCampos, { "COLB7"		, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo B Periodo 7
	aAdd(aCampos, { "COLB8"   	, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo B Periodo 8
	aAdd(aCampos, { "COLB9"   	, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo B Periodo 9
	aAdd(aCampos, { "COLB10" 	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo B Periodo 10
	aAdd(aCampos, { "COLB11" 	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo B Periodo 11
	aAdd(aCampos, { "COLB12"  	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo B Periodo 12

	aAdd(aCampos, { "COLC1"		, "N", aTamVal[1]+2, ::nDecimais} )	// Saldo C Periodo 1
	aAdd(aCampos, { "COLC2"   	, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo C Periodo 2
	aAdd(aCampos, { "COLC3"   	, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo C Periodo 3
	aAdd(aCampos, { "COLC4" 	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo C Periodo 4
	aAdd(aCampos, { "COLC5" 	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo C Periodo 5
	aAdd(aCampos, { "COLC6"   	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo C Periodo 6
	aAdd(aCampos, { "COLC7"		, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo C Periodo 7
	aAdd(aCampos, { "COLC8"   	, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo C Periodo 8
	aAdd(aCampos, { "COLC9"   	, "N", aTamVal[1]+2, ::nDecimais} ) 	// Saldo C Periodo 9
	aAdd(aCampos, { "COLC10" 	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo C Periodo 10
	aAdd(aCampos, { "COLC11" 	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo C Periodo 11
	aAdd(aCampos, { "COLC12"  	, "N", aTamVal[1]+2, ::nDecimais} )  // Saldo C Periodo 12

return aCampos
