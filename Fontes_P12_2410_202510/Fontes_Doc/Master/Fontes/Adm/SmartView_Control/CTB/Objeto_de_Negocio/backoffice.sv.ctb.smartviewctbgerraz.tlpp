#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.ctb.smartviewctbgerraz.ch"

static cArqtmp := " " as character

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGACTB", tables="CT1, CT2, CT3, CTT, CTD, CTH, CTZ, CTO", name="Classe Pai - SmartviewCtbgerraz", country="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} SmartviewCtbgerraz
    Essa classe visa implementar os relatorios origens:
    CTBR400
    CTBR480
    CTBR440
    CTBR410
    CTBR490

    @author wilton.santos
    @since 15/08/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------

class SmartviewCtbgerraz from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new()                as object
    public method getData()            as object
    public method getSchema()          as object    
    public method getDescription()     as character
    public method getAreas()           as array

    //usados na internacionalizacao
    public method preschema()          as object 
    public method preData()            as object
    public method processData()        as json
    public method processCustomData()  as Json
    
    public method getCtGerRazSchema()  as object
    public method getCtgGerRazData()   as object
    public method getCtgGerRazAppend() as object
    public method handleCtgGerRazParams() 
    public method handleCtgGerRazAppend() as json
    public method impCompl()           as object
    
    protected method exeCtgGerRaz()       as object
    protected method getEntidadeCTB()
    protected method getQueryEntidades()  as character
    protected method setCtgGerRazParams()    

    public method ExecDBSkipTemp()
    
    //atributos - na ordem que está sendo chamado na função
    protected data oMeter       as object   
    protected data oText        as object
    protected data oDlg         as object
    protected data lEnd         as logical
    protected data cArqTmpNome  as character
    protected data cContaIni    as character
    protected data cContaFim    as character
    protected data cCustoIni    as character
    protected data cCustoFim    as character
    protected data cItemIni     as character
    protected data cItemFim     as character
    protected data cCLVLIni     as character
    protected data cCLVLFim     as character
    protected data cMoeda       as character
    protected data cMoedaDesc   as character
    protected data cContaAnt    as character
    protected data cClVlAnt     as character
    protected data cMascCtaMV   as character
    protected data cMascCusMV   as character
    protected data cMascIteMV   as character
    protected data cMascClvMV   as character
    protected data dDataIni     as date
    protected data dDataFim     as date
    protected data aSetOfBook   as array
    protected data lNoMov       as logical
    protected data cSaldo       as character
    protected data lJunta       as logical
    protected data cTipo        as character
    protected data lAnalit      as logical
    protected data c2Moeda      as character
    protected data nTipo        as numeric
    protected data cUFilter     as character
    protected data lSldAnt      as logical
    protected data aSelFil      as array
    protected data lExterno     as logical
    protected data lCancel      as logical
    protected data nCodConta    as numeric
    protected data lClVlRes     as logical
    protected data lCCRes       as logical
    protected data lItemRes     as logical
    protected data jParams      as json
    
    //tratamentos especificos
    protected data jDados        as json
    protected data jEntidadesCT1 as json
    protected data jEntidadesCTT as json
    protected data jEntidadesCTH as json
    protected data jEntidadesCTD as json

    //atributos para tratamento de impressão  
    protected data lPrintZero     as logical //se imprime valor 0,00

    protected data oIntegratedProvider as object
    protected data cCompanyName        as character
    protected data cBranchName         as character

	protected data lRazao              as logical //Tratativa para que no CTBGerRaz entre no CtbQryRaz (.F.) ou CtbRazao (.T.)
    protected data lRazCC              as logical //Tratativa para que no CTBGerRaz entre no nas condições do Razao Centro de Custo CTBR440
	
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author wilton.santos
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
method new() as object class SmartviewCtbgerraz
    _Super:new()
    //alias da temporaria
    ::cArqTmpNome := " "
    ::cContaAnt   := " "
    ::cClVlAnt    := " "
    ::cMascCtaMV  := SuperGetMv("MV_MASCARA")
    ::cMascCusMV  := SuperGetMv("MV_MASCCUS")
    ::cMascIteMV  := SuperGetMv("MV_MASCCTD")
    ::cMascClvMV  := SuperGetMv("MV_MASCCTH")
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} getAreas
    Define o nome da Area, sem a necessidade de declarar no fonte que estiver herdando
    @author wilton.santos
    @since 15/08/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getAreas() as array class SmartviewCtbgerraz
return {STR0001} //"Contabilidade"

//-------------------------------------------------------------------
/*/{Protheus.doc} getDescription
    Define um descrição padrão
    @author wilton.santos
    @since 15/08/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getDescription() as character class SmartviewCtbgerraz
return STR0002 //"Objeto contendo informações sobre o relatorio"

//-------------------------------------------------------------------
/*/{Protheus.doc} getData
    Atualiza o objeto oData ao ser executado
    @author wilton.santos
    @since 15/08/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class SmartviewCtbgerraz

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), self:cPergunte)
    self:jParams := oFilter:getParameters()

    self:oIntegratedProvider := ControlIntegratedProvider():New()
    self:cCompanyName := self:oIntegratedProvider:getCompanyName()
    self:cBranchName  := self:oIntegratedProvider:getBranchName()
    
    self:getCtgGerRazData(nPage, oFilter)
    self:setCtgGerRazParams(oFilter)
    self:exeCtgGerRaz(oFilter)
    self:getCtgGerRazAppend(oFilter,nPage)

    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())
    FreeObj(self:jParams)
    FreeObj(self:oIntegratedProvider)

return self:oData
//-------------------------------------------------------------------
/*/{Protheus.doc} getSchema
    Define um schema padrão quando for chamado o metodo getCtGerRazSchema() na classe filha
    @author wilton.santos
    @since 15/08/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getSchema() as object class SmartviewCtbgerraz

    //CTBGERRAZ
    self:oSchema:addProperty("CONTA"       ,STR0003 ,"string" ,STR0003 ,"CONTA"       ) // Codigo da Conta
    self:oSchema:addProperty("CONTA_RES"   ,STR0036 ,"string" ,STR0036 ,"CONTA_RES"   ) // Codigo da Conta Reduzido
    self:oSchema:addProperty("CONTA_M"     ,STR0004 ,"string" ,STR0004 ,"CONTA_M"     ) // Cod Conta Masc
    self:oSchema:addProperty("DESC_CONTA"  ,STR0035 ,"string" ,STR0035 ,"DESC_CONTA"  ) // Descricao da Conta
    self:oSchema:addProperty("XPARTIDA"    ,STR0006 ,"string" ,STR0006 ,"XPARTIDA"    ) // Contra Partida
    self:oSchema:addProperty("XPARTIDA_RES",STR0037 ,"string" ,STR0037 ,"XPARTIDA_RES") // Contra Partida Reduzido
    self:oSchema:addProperty("XPARTIDA_M"  ,STR0007 ,"string" ,STR0007 ,"XPARTIDA_M"  ) // Contra Partida Masc
    self:oSchema:addProperty("TIPO"        ,STR0008 ,"string" ,STR0008 ,"TIPO"        ) // Tipo
    self:oSchema:addProperty("LANCDEB"     ,STR0009 ,"number" ,STR0009 ,"LANCDEB"     ) // Debito 
    self:oSchema:addProperty("LANCDEB_M"   ,STR0010 ,"string" ,STR0010 ,"LANCDEB"     ) // Debito Masc
    self:oSchema:addProperty("LANCCRD"     ,STR0011 ,"number" ,STR0011 ,"LANCCRD"     ) // Credito
    self:oSchema:addProperty("LANCCRD_M"   ,STR0012 ,"string" ,STR0012 ,"LANCCRD"     ) // Credito Masc
    self:oSchema:addProperty("SALDOSCR"    ,STR0013 ,"number" ,STR0013 ,"SALDOSCR"    ) // Saldo
    self:oSchema:addProperty("TPSLDANT"    ,STR0015 ,"string" ,STR0015 ,"TPSLDANT"    ) // Sinal do Saldo Anterior
    self:oSchema:addProperty("TPSLDATU"    ,STR0016 ,"string" ,STR0016 ,"TPSLDATU"    ) // Sinal do Saldo Atual
    self:oSchema:addProperty("HISTORICO"   ,STR0017 ,"string" ,STR0017 ,"HISTORICO"   ) // Historico
    self:oSchema:addProperty("CCUSTO"      ,STR0018 ,"string" ,STR0018 ,"CCUSTO"      ) // Centro de Custo
    self:oSchema:addProperty("CCUSTO_RES"  ,STR0038 ,"string" ,STR0038 ,"CCUSTO_RES"  ) // Centro de Custo Reduzido
    self:oSchema:addProperty("CCUSTO_M"    ,STR0019 ,"string" ,STR0019 ,"CCUSTO_M"    ) // Centro de Custo Masc
    self:oSchema:addProperty("ITEM"        ,STR0020 ,"string" ,STR0020 ,"ITEM"        ) // Item Contabil
    self:oSchema:addProperty("ITEM_RES"    ,STR0039 ,"string" ,STR0039 ,"ITEM_RES"    ) // Item Contabil Reduzido
    self:oSchema:addProperty("ITEM_M"      ,STR0021 ,"string" ,STR0021 ,"ITEM_M"      ) // Item Contabil Masc
    self:oSchema:addProperty("CLVL"        ,STR0022 ,"string" ,STR0022 ,"CLVL"        ) // Classe de Valor
    self:oSchema:addProperty("CLVL_RES"    ,STR0040 ,"string" ,STR0040 ,"CLVL_RES"    ) // Classe de Valor Reduzido
    self:oSchema:addProperty("CLVL_M"      ,STR0023 ,"string" ,STR0023 ,"CLVL_M"      ) // Classe de Valor Masc
    self:oSchema:addProperty("DATAL"       ,STR0024 ,"date"   ,STR0024 ,"DATAL"       ) // Data Lancamento
    self:oSchema:addProperty("LOTE"        ,STR0025 ,"string" ,STR0025 ,"LOTE"        ) // Lote
    self:oSchema:addProperty("SUBLOTE"     ,STR0026 ,"string" ,STR0026 ,"SUBLOTE"     ) // Sublote
    self:oSchema:addProperty("DOC"         ,STR0027 ,"string" ,STR0027 ,"DOC"         ) // Documento
    self:oSchema:addProperty("LINHA"       ,STR0028 ,"string" ,STR0028 ,"LINHA"       ) // Linha
    self:oSchema:addProperty("SEQLAN"      ,STR0029 ,"string" ,STR0029 ,"SEQLAN"      ) // Sequencia do Lancamento
    self:oSchema:addProperty("SEQHIST"     ,STR0030 ,"string" ,STR0030 ,"SEQHIST"     ) // Sequencia do Historico
    self:oSchema:addProperty("EMPORI"      ,STR0031 ,"string" ,STR0031 ,"EMPORI"      ) // Empresa Origem
    self:oSchema:addProperty("FILORI"      ,STR0032 ,"string" ,STR0032 ,"FILORI"      ) // Filial Origem
    self:oSchema:addProperty("FILIAL"      ,STR0034 ,"string" ,STR0034 ,"FILIAL"      ) // Filial

    self:getCtGerRazSchema()

return self:oSchema

// Se pula o registro na tabela temporaria
method ExecDBSkipTemp()  class SmartviewCtbgerraz
    local lRet := .F.
return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} getCtgGerRazAppend
    Define o dados que serão appendados na temporaria gerada na ctgerRaz,
    podendo serem sobrepostos no handleCtgGerRazAppend 
    @author wilton.santos
    @since 28/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getCtgGerRazAppend(oFilter as object, nPage as numeric) as object class SmartviewCtbgerraz

    local cMascCta           as character
    local cMascCus           as character
    local cMascIte           as character
    local cMascClv           as character
    local cCodConta          as character
    local cDescConta         as character
    local cCodXPartida       as character
    local cCodClVl           as character
    local cCodCusto          as character
    local cCodItem           as character
    local cCodContaM         as character
    local cCodXPartidaM      as character
    local cCodClVlM          as character
    local cCodCustoM         as character
    local cCodItemM          as character
    local cPicture           as character
    local cSeparaCta         as character
    local cSeparaCus         as character
    local cSeparaIte         as character
    local cSeparaClv         as character
    local cFilOld := cFilAnt as character
    local nDecimais          as numeric
    local TAM_VALOR  := 17   as numeric
    local aArea      := {}   as array
    local aEntidades := {}   as array

    aArea     := getArea()
    nDecimais := DecimalCTB(::aSetOfBook,::cMoeda) //DecimalCTB(aSetOfBook,mv_par08)
    cPicture  := Iif(!Empty(::aSetOfBook[4]),"@E 999,999,999,999.99","")

    //Coletando mascara das entidades
    cMascCta   := Iif(Empty(::aSetOfBook[2]),iif(empty(::cMascCtaMV),nil,::cMascCtaMV),RetMasCtb(::aSetOfBook[2],@cSeparaCta)) //Mascara da Conta
    cMascCus   := Iif(Empty(::aSetOfBook[6]),iif(empty(::cMascCusMV),nil,::cMascCusMV),RetMasCtb(::aSetOfBook[6],@cSeparaCus)) //Mascara do Centro de Custo
    cMascIte   := Iif(Empty(::aSetOfBook[7]),iif(empty(::cMascIteMV),nil,::cMascIteMV),RetMasCtb(::aSetOfBook[7],@cSeparaIte)) //Mascara do Item Contabil    
    cMascClv   := Iif(Empty(::aSetOfBook[8]),iif(empty(::cMascClvMV),nil,::cMascClvMV),RetMasCtb(::aSetOfBook[8],@cSeparaClv)) //Mascara da Classe Valor

    //Popula array entidades
    aAdd(aEntidades, {"CONTA",    cMascCta, "CT1", cSeparaCta})
    aAdd(aEntidades, {"XPARTIDA", cMascCta, "CT1", cSeparaCta})
    aAdd(aEntidades, {"CCUSTO",   cMascCus, "CTT", cSeparaCus})
    aAdd(aEntidades, {"ITEM",     cMascIte, "CTD", cSeparaIte})
    aAdd(aEntidades, {"CLVL",     cMascClv, "CTH", cSeparaClv})

    //Formata a entidade com a mascara
    self:getEntidadeCTB(aEntidades)

    //Abre tabelas para recuperar código que será impresso (Normal/Reduzido)
    dbSelectArea("CT1")
	CT1->(dbSetOrder(1))

    dbSelectArea("CTH")
    CTH->(dbSetOrder(1))

    dbSelectArea("CTT")
	CTT->(dbSetOrder(1))

    dbSelectArea("CTD")
	CTD->(dbSetOrder(1))
    
    cArqTmp->(DBGoTop())

    While !cArqTmp->(Eof())

        //Salva os códigos padrão (normal)
        cCodConta    := AllTrim(cArqTMP->CONTA)
        cCodXPartida := AllTrim(cArqTMP->XPARTIDA)
        cCodClVl     := AllTrim(cArqTmp->CLVL)
        cCodCusto    := AllTrim(cArqTMP->CCUSTO)
        cCodItem     := AllTrim(cArqTMP->ITEM)

        cFilAnt := cArqTmp->FILORI

        //Tratamento para codigo da entidade conforme parâmetros
        //Conta
        if CT1->(dbSeek(FWxFilial("CT1")+cArqTMP->CONTA))
            cDescConta := AllTrim(&("CT1->CT1_DESC" + ::cMoedaDesc))

            if ::nCodConta == 2
                cCodConta := AllTrim(CT1->CT1_RES)
            elseif ::nCodConta == 3
                cCodConta := AllTrim(CT1->CT1_CODIMP)
            endif
        endif

        //Contra partida
        if CT1->(dbSeek(FWxFilial("CT1")+cArqTMP->XPARTIDA))
            if ::nCodConta == 2                    
                cCodXPartida := AllTrim(CT1->CT1_RES)
            elseif ::nCodConta == 3
                cCodXPartida := AllTrim(CT1->CT1_CODIMP)
            endif
        endif

        //Classe de Valor
        if ::lClVlRes
            if cArqTmp->CLVL <> CTH->CTH_CLVL
                if CTH->(dbSeek(FWxFilial("CTH")+cArqTmp->CLVL))
                    cCodClVl := AllTrim(CTH->CTH_RES)
                endif
            endif
        endif
        
        //Centro de custo
        if ::lCCRes
            if cArqTMP->CCUSTO <> CTT->CTT_CUSTO
                if CTT->(dbSeek(FWxFilial("CTT")+cArqTMP->CCUSTO))
                    cCodCusto := AllTrim(CTT->CTT_RES)
                endif
            endif
        endif
        
        //Item contábil
        if ::lItemRes
            if cArqTMP->ITEM <> CTD->CTD_ITEM
                if CTD->(dbSeek(FWxFilial("CTD")+cArqTMP->ITEM))
                    cCodItem := AllTrim(CTD->CTD_RES)
                endif
            endif
        endif

        //Tratamento para mascara
        cCodContaM    := Iif(::jEntidadesCT1:HasProperty(cCodConta),    ::jEntidadesCT1:GetJsonObject(cCodConta),    AllTrim(EntidadeCTB(cCodConta,,,20,.F.,cMascCta,cSeparaCta,"CT1",,.F.,,.F.)))
        cCodXPartidaM := Iif(::jEntidadesCT1:HasProperty(cCodXPartida), ::jEntidadesCT1:GetJsonObject(cCodXPartida), AllTrim(EntidadeCTB(cCodXPartida,,,20,.F.,cMascCta,cSeparaCta,"CT1",,.F.,,.F.)))
        cCodCustoM    := Iif(::jEntidadesCTT:HasProperty(cCodCusto),    ::jEntidadesCTT:GetJsonObject(cCodCusto),    AllTrim(EntidadeCTB(cCodCusto,,,20,.F.,cMascCus,cSeparaCus,"CTT",,.F.,,.F.)))
        cCodItemM     := Iif(::jEntidadesCTD:HasProperty(cCodItem),     ::jEntidadesCTD:GetJsonObject(cCodItem),     AllTrim(EntidadeCTB(cCodItem,,,20,.F.,cMascIte,cSeparaIte,"CTD",,.F.,,.F.)))
        cCodClVlM     := Iif(::jEntidadesCTH:HasProperty(cCodClVl),     ::jEntidadesCTH:GetJsonObject(cCodClVl),     AllTrim(EntidadeCTB(cCodClVl,,,20,.F.,cMascClv,cSeparaClv,"CTH",,.F.,,.F.)))

        ::jDados := {;
                    "CONTA"       : AllTrim(cArqTMP->CONTA)    ,;
                    "CONTA_RES"   : iif(::nCodConta == 2 .Or. ::nCodConta == 3, cCodConta, ""),;
                    "CONTA_M"     : cCodContaM                 ,;
                    "DESC_CONTA"  : cDescConta                 ,;
                    "XPARTIDA"    : AllTrim(cArqTMP->XPARTIDA) ,; 
                    "XPARTIDA_RES": iif(::nCodConta == 2 .Or. ::nCodConta == 3, cCodXPartida, ""),;
                    "XPARTIDA_M"  : cCodXPartidaM              ,; 
                    "TIPO"        : AllTrim(cArqTMP->TIPO)     ,; 
                    "LANCDEB"     : cArqTMP->LANCDEB           ,; 
                    "LANCDEB_M"   : AllTrim(RZValorCTB(cArqTMP->LANCDEB,,,TAM_VALOR-2,nDecimais,.T.,cPicture,"1",,,,,,::lPrintZero,.F.)),;
                    "LANCCRD"     : cArqTMP->LANCCRD           ,; 
                    "LANCCRD_M"   : AllTrim(RZValorCTB(cArqTMP->LANCCRD,,,TAM_VALOR-2,nDecimais,.T.,cPicture,"2",,,,,,::lPrintZero,.F.)),;
                    "SALDOSCR"    : cArqTMP->SALDOSCR          ,; 
                    "TPSLDANT"    : AllTrim(cArqTMP->TPSLDANT) ,; 
                    "TPSLDATU"    : AllTrim(cArqTMP->TPSLDATU) ,; 
                    "HISTORICO"   : AllTrim(cArqTMP->HISTORICO),; 
                    "CCUSTO"      : AllTrim(cArqTMP->CCUSTO)   ,; 
                    "CCUSTO_RES"  : iif(::lCCRes, cCodCusto, ""),;
                    "CCUSTO_M"    : cCodCustoM                 ,; 
                    "ITEM"        : AllTrim(cArqTMP->ITEM)     ,; 
                    "ITEM_RES"    : iif(::lItemRes, cCodItem, ""),;
                    "ITEM_M"      : cCodItemM                  ,; 
                    "CLVL"        : AllTrim(cArqTmp->CLVL)     ,; 
                    "CLVL_RES"    : iif(::lClVlRes, cCodClVl, ""),;
                    "CLVL_M"      : cCodClVlM                  ,; 
                    "DATAL"       : totvs.framework.treports.date.dateToTimeStamp(cArqTMP->DATAL),; 
                    "LOTE"        : AllTrim(cArqTMP->LOTE)     ,; 
                    "SUBLOTE"     : AllTrim(cArqTMP->SUBLOTE)  ,; 
                    "DOC"         : AllTrim(cArqTMP->DOC)      ,; 
                    "LINHA"       : AllTrim(cArqTMP->LINHA)    ,; 
                    "SEQLAN"      : AllTrim(cArqTMP->SEQLAN)   ,; 
                    "SEQHIST"     : AllTrim(cArqTMP->SEQHIST)  ,; 
                    "EMPORI"      : cArqTMP->EMPORI            ,; 
                    "FILORI"      : cArqTMP->FILORI            ,; 
                    "FILIAL"      : cArqTMP->FILIAL             ;
                    }
        
        self:processData()
        self:processCustomData()

        //Salva conta para verificar se trocou
        ::cContaAnt  := cArqTmp->CONTA
        ::cClVlAnt   := cArqTmp->CLVL
    
        self:oData:appendData(self:handleCtgGerRazAppend())
        
        If  self:lAnalit .Or. ( self:cPergunte =='CTR400' .And. ALLTRIM(STR(MV_PAR08)) $ '1|2' )
            self:ImpCompl()
        EndIf

        //Se pula o registro
        If self:ExecDBSkipTemp(cArqTmp)
            cArqTmp->(DbSkip())
            Loop
        EndIf
        
        cArqTmp->(DbSkip())
      
    EndDo

    cFilAnt := cFilOld    

    //tratamento para deletar a temporaria
    dbSelectArea("cArqTmp")
    Set Filter To
    dbCloseArea()
    If Select("cArqTmp") == 0
        FErase(cArqTmp+GetDBExtension())
        FErase("cArqInd"+OrdBagExt())
    EndIf
    
    FreeObj(::jEntidadesCT1)
    FreeObj(::jEntidadesCTT)
    FreeObj(::jEntidadesCTD)
    FreeObj(::jEntidadesCTH)

    CT1->(dbCloseArea())
    CTH->(dbCloseArea())
	CTT->(dbCloseArea())
	CTD->(dbCloseArea())

    restarea(aArea)

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} impCompl
    Método utilizado para complementar histórico (CT2_DC = '4')
    
    @author Renan Gremes
    @since 13/03/2025
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method impCompl() as object class SmartviewCtbgerraz

    local jDados                as json
    Local oPrepared     := NIL	as Object
    local nParamOrder   := 1	As Numeric

    //Implementar query
    _oQryImpH := "SELECT CT2_DC,CT2_LINHA,CT2_FILIAL,CT2_DATA,CT2_LOTE,CT2_SBLOTE,CT2_DOC,"
    _oQryImpH += " CT2_SEQLAN,CT2_EMPORI,CT2_FILORI,CT2_MOEDLC,CT2_SEQHIS, "
    _oQryImpH += " CT2_HIST FROM "
    _oQryImpH += RetSqlName("CT2")
    _oQryImpH += " WHERE CT2_FILIAL = ?"
    _oQryImpH += " AND CT2_DATA     = ?"
    _oQryImpH += " AND CT2_LOTE     = ?"
    _oQryImpH += " AND CT2_SBLOTE   = ?"
    _oQryImpH += " AND CT2_DOC      = ?"
    _oQryImpH += " AND CT2_SEQLAN   = ?"
    _oQryImpH += " AND CT2_EMPORI   = ?"
    _oQryImpH += " AND CT2_FILORI   = ?"
    _oQryImpH += " AND CT2_MOEDLC   = ?"
    _oQryImpH += " AND CT2_DC       = ?"
    _oQryImpH += " AND D_E_L_E_T_   = ?"
    _oQryImpH += " ORDER BY CT2_SEQHIS"
    _oQryImpH := ChangeQuery(_oQryImpH) 

    oPrepared := FWExecStatement():New(_oQryImpH) 

    oPrepared:SetString(nParamOrder++, cArqTMP->FILIAL)
    oPrepared:SetDate(nParamOrder++,   cArqTMP->DATAL)		
    oPrepared:SetString(nParamOrder++, cArqTMP->LOTE)
    oPrepared:SetString(nParamOrder++, cArqTMP->SUBLOTE)
    oPrepared:SetString(nParamOrder++, cArqTMP->DOC)
    oPrepared:SetString(nParamOrder++, cArqTMP->SEQLAN)
    oPrepared:SetString(nParamOrder++, cArqTMP->EMPORI)
    oPrepared:SetString(nParamOrder++, cArqTMP->FILORI)
    oPrepared:SetString(nParamOrder++, '01')
    oPrepared:SetString(nParamOrder++, '4')
    oPrepared:SetString(nParamOrder++, Space(1))

    QCT2IMPH := oPrepared:OpenAlias()

    //Appendar json
    If (QCT2IMPH)->(!Eof())
    
        While !(QCT2IMPH)->(Eof())

            jDados := {;
                "CONTA"       : AllTrim(cArqTMP->CONTA)    ,; 
                "CCUSTO"      : AllTrim(cArqTMP->CCUSTO)   ,; 
                "ITEM"        : AllTrim(cArqTMP->ITEM)     ,; 
                "CLVL"        : AllTrim(cArqTMP->CLVL)     ,; 
                "TIPO"        : (QCT2IMPH)->CT2_DC     ,; 
                "HISTORICO"   : AllTrim((QCT2IMPH)->CT2_HIST),; 
                "DATAL"       : totvs.framework.treports.date.dateToTimeStamp(cArqTMP->DATAL),; 
                "LOTE"        : AllTrim(cArqTMP->LOTE)     ,; 
                "SUBLOTE"     : AllTrim(cArqTMP->SUBLOTE)  ,; 
                "DOC"         : AllTrim(cArqTMP->DOC)      ,; 
                "LINHA"       : AllTrim((QCT2IMPH)->CT2_LINHA)    ,; 
                "SEQLAN"      : AllTrim((QCT2IMPH)->CT2_SEQLAN)   ,; 
                "SEQHIST"     : AllTrim((QCT2IMPH)->CT2_SEQHIS)  ,; 
                "EMPORI"      : cArqTMP->EMPORI            ,; 
                "FILORI"      : cArqTMP->FILORI            ,; 
                "FILIAL"      : cArqTMP->FILIAL             ;
                }

            self:oData:appendData(jDados)
            (QCT2IMPH)->(dbSkip())

        endDo

    EndIf

    (QCT2IMPH)->(dbCloseArea())
    
    oPrepared:Destroy()
    oPrepared := nil 
    dbSelectArea("cArqTmp")

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} setCtgGerRazParams
    Define um valor default para os atributos abaixo
    @author wilton.santos
    @since 28/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method setCtgGerRazParams(oFilter as object) class SmartviewCtbgerraz

    //valores defalult
    ::c2Moeda 	 := ""
    ::nTipo		 := 1
    ::cUFilter	 := ""
    ::lSldAnt	 := .F.
    ::aSelFil 	 := {}
    ::lExterno 	 := .F.
    ::lEnd 	 	 := .F.
    ::lCancel 	 := .F.
    ::cCustoIni  := Space(TamSX3("CTT_CUSTO")[1])
    ::cCustoFim  := Replicate("Z" , TamSX3("CTT_CUSTO")[1])
    ::cItemIni   := Space(TamSX3("CTD_ITEM")[1])
    ::cItemFim   := Replicate("Z" , TamSX3("CTD_ITEM")[1])
    ::cCLVLIni   := Space(TamSX3("CTH_CLVL")[1])
    ::cCLVLFim   := Replicate("Z" , TamSX3("CTH_CLVL")[1])
    ::nCodConta  := 1
    ::lClVlRes   := .F.
    ::lCCRes     := .F.
    ::lItemRes   := .F.
    ::cMoedaDesc := "01"
    ::lRazao     := .F.
    ::lRazCC     := .F.

    self:handleCtgGerRazParams(oFilter)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} exeCtgGerRaz
    Executa a CtgerRaz conforme atributos preenchidos na classe filha
    @author wilton.santos
    @since 28/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method exeCtgGerRaz(oFilter as object) as object class SmartviewCtbgerraz

    local cArqTmpNome := "" as character
    
    CTBGerRaz(  ,;
                ,;
                ,;
                ::lEnd,;
                @cArqtmp,;
                ::cContaIni,; 
                ::cContaFim,; 
                ::cCustoIni,; 
                ::cCustoFim,; 
                ::cItemIni,; 
                ::cItemFim,; 
                ::cCLVLIni,; 
                ::cCLVLFim,; 
                ::cMoeda,; 
                ::dDataIni,; 
                ::dDataFim,; 
                ::aSetOfBook,;
                ::lNoMov,; 
                ::cSaldo,; 
                ::lJunta,; 
                ::cTipo,; 
                ::lAnalit,; 
                ::c2Moeda,; 
                ::nTipo,; 
                ::cUFilter,; 
                ::lSldAnt,; 
                ::aSelFil,; 
                ::lExterno,; 
                ::lCancel,;
                ,;
                @cArqTmpNome,;
                ::lRazao,;
                ::lRazCC)
                
    ::cArqTmpNome := cArqTmpNome

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} handleCtgGerRazAppend
    Complementa os dados que serão retornados no getData().
    @author wilton.santos
    @since 16/08/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method handleCtgGerRazAppend() as json class SmartviewCtbgerraz
return ::jDados

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Executa a query conforme parametrizacao
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method preSchema() as object class SmartviewCtbgerraz
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que  consumido pelo M.I.
    Ajusta o Json conforme alteracao no processData e preSchema
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method preData() as object class SmartviewCtbgerraz    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que  consumido pelo M.I.
    usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method processData() as json class SmartviewCtbgerraz
return ::jDados

//-------------------------------------------------------------------
/*/{Protheus.doc} processCustomData
    Metodo que será consumido pelas classes filhas
    
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method processCustomData() as json class SmartviewCtbgerraz    
Return ::jDados

//-------------------------------------------------------------------
/*/{Protheus.doc} getEntidadeCTB
    Metodo que retornará as máscaras das entidades
    
    @author Renan Gremes
    @since 19/08/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method getEntidadeCTB(aEntidades as array) class SmartviewCtbgerraz

    local cQuery             as character
    local cMascara           as character
    local cSepara            as character
    local cCodEntidade       as character
    local cMascaraEntidade   as character
    local cAliasEntidade     as character
    local cEntidadeBkp := "" as character
    local nPosEntidade := 0  as numeric

    cQuery := self:getQueryEntidades(aEntidades)
    MPSysOpenQuery(cQuery, "QRYTMP")
    DbSelectArea("QRYTMP")

    ::jEntidadesCT1 := JsonObject():new()
    ::jEntidadesCTT := JsonObject():new()
    ::jEntidadesCTD := JsonObject():new()
    ::jEntidadesCTH := JsonObject():new()

    While QRYTMP->(!Eof())
        //Só faz o aScan se trocou de entidade
        If cEntidadeBkp <> QRYTMP->ENTIDADE
            nPosEntidade     := aScan(aEntidades,{|x| x[1] == AllTrim(QRYTMP->ENTIDADE)})
        EndIf

        If nPosEntidade > 0
            cMascaraEntidade := aEntidades[nPosEntidade][2]
            cAliasEntidade   := aEntidades[nPosEntidade][3]
            cSepara          := aEntidades[nPosEntidade][4]

            cCodEntidade := AllTrim(QRYTMP->CONTEUDO)
        Else
            cCodEntidade := ""
        EndIf
        
        //Só adiciona no json, se não for vazio o código da entidade
        If !Empty(cCodEntidade)
            //Pega a mascara da entidade
            cMascara := AllTrim(EntidadeCTB(cCodEntidade,,,20,.F.,cMascaraEntidade,cSepara,cAliasEntidade,,.F.,,.F.))
            
            //Popula o array da entidade com a mascara
            &("self:jEntidades"+cAliasEntidade)[cCodEntidade] := cMascara
        EndIf

        //Salva a entidade para verificação no aScan
        cEntidadeBkp := QRYTMP->ENTIDADE
        
        QRYTMP->(DbSkip())        
    EndDo

    QRYTMP->(DbCloseArea())

return

//-------------------------------------------------------------------
/*/{Protheus.doc} getEntidadeCTB
    Metodo que retornará as máscaras das entidades
    
    @author Renan Gremes
    @since 19/08/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method getQueryEntidades(aEntidades as array) as character class SmartviewCtbgerraz

    local cQuery      := "" as character
    local nI          := 0  as numeric
    local oQuery            as object

    Default aEntidades := {}

    For nI := 1 To Len(aEntidades)
        cQuery += "SELECT "+aEntidades[nI][1]+" AS CONTEUDO, '"+aEntidades[nI][1]+"' AS ENTIDADE FROM "+::cArqTmpNome+CRLF
        cQuery += "GROUP BY "+aEntidades[nI][1]+CRLF

        If nI < Len(aEntidades)
            cQuery += "UNION " +CRLF
        EndIf
    Next nI

    oQuery := FWPreparedStatement():New(cQuery)
    cQuery := oQuery:GetFixQuery()
    
    FreeObj(oQuery)

return cQuery
