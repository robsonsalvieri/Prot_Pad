#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.accountx6branches.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT3, CT7", name="Conta X 6 Filiais", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} AccountX6BranchesSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Conta X 6 Filiais
para o TReports
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class AccountX6BranchesSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartViewCtGerComp
    public method new()                    as object 
    public method getDescription()         as character
    public method getAreas()               as array
    public method getCtGerCompData()       as object
    public method getCtGerCompSchema()     as object

    public method handleCtGerCompParams()
    public method handleCtGerCompAppend()  as object
    public method handleExeCtGerComp()     as object

    protected method Set6Fils()            as array // Carrega array com 6 filiais para ctgercomp

    protected data jDados as json    
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class AccountX6BranchesSmartViewBusinessObject

    _Super:new()

    self:setDisplayName(STR0001) //"Conta X 6 Filiais"
    self:setPergunte("CTR250") //Indica o pergunte que será utilizado no relatório

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 09/11/2023
@version 12.1.2310
*/
//------------------------------------------------------------------- 

method getDescription() as character class AccountX6BranchesSmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre o relatorio de Conta X 6 Filiais"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class AccountX6BranchesSmartViewBusinessObject
return {STR0003} //"Contabilidade"

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerCompData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtGerCompData(nPage as numeric, oFilter as object) as object class AccountX6BranchesSmartViewBusinessObject
    //Carrega lista de parametros para a memória    
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR250")

    self:handleCtGerCompParams(oFilter)

    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerCompSchema
Prepara a estrutura dos campos
 
@return array: array com a estrutura dos campos
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtGerCompSchema() as object class AccountX6BranchesSmartViewBusinessObject

    self:setCustomURL("MV_PAR05",  "api/framework/v1/genericLookupService/smartview/SM0", 2)
    self:setCustomURL("MV_PAR06",  "api/framework/v1/genericLookupService/smartview/SM0", 2)
    self:setCustomURL("MV_PAR07",  "api/framework/v1/genericLookupService/smartview/SM0", 2)
    self:setCustomURL("MV_PAR08",  "api/framework/v1/genericLookupService/smartview/SM0", 2)
    self:setCustomURL("MV_PAR09",  "api/framework/v1/genericLookupService/smartview/SM0", 2)
    self:setCustomURL("MV_PAR10",  "api/framework/v1/genericLookupService/smartview/SM0", 2)

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtGerCompParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtGerCompParams( oFilter as object ) class AccountX6BranchesSmartViewBusinessObject
    
    Local cAlias      := "CT7"                                      as character
    Local cIdent      := ""                                         as character
    Local dDataIni    := MV_PAR01                                   as date
    Local dDataFim    := MV_PAR02                                   as date
    Local cContaIni   := MV_PAR03                                   as character
    Local cContaFim   := MV_PAR04                                   as character
    Local lImpSint    := iif(MV_PAR11==1 .OR. MV_PAR11==3,.T.,.F.)  as logical
    Local aSetOfBook  := iif(empty(MV_PAR12),CTBSetOf(MV_PAR12),CTBSetOf("")) as array
    Local lVlrZerado  := iif(MV_PAR13==1,.T.,.F.)                   as logical
    Local cMoeda      := iif(empty(MV_PAR14),"01",MV_PAR14)         as character
    Local cSaldos     := iif(Empty(MV_PAR16),"1",MV_PAR16)          as character
    Local lPrintZero  := iif(MV_PAR23==1,.T.,.F.)                   as logical
    Local lImpConta   := iif(MV_PAR24==1,.T.,.F.)                   as logical
    Local nDivide     := iif(empty(MV_PAR25),1,MV_PAR25)            as numeric
    Local lImpAntLP   := iif(MV_PAR27==1,.T.,.F.)                   as logical
    Local dDataLP     := MV_PAR28                                   as date
    Local cTpVlr      := "M"                                        as character

    //Variaveis fixas no relatorio 
    Local lMeses      := .F.                                        as logical
    Local aMeses      := {}                                         as array
    Local cHeader     := ""                                         as character
    Local lNImpMov    := .F.                                        as logical
    Local cString     := ""                                         as character
    Local aFilAux     := {}                                         as array

    //Preenche array de afiliais
    aFilAux := self:Set6Fils()

    //setar os parametros da classe CtGerComp conforme os MV_PARxx
    self:cAlias      := cAlias
    self:cIdent      := cIdent
    self:dDataIni    := dDataIni
    self:dDataFim    := dDataFim
    self:cContaIni   := cContaIni
    self:cContaFim   := cContaFim
    self:lImpSint    := lImpSint
    self:aSetOfBook  := aSetOfBook
    self:cMoeda      := cMoeda
    self:lVlrZerado  := lVlrZerado
    self:cSaldos     := cSaldos
    self:lImpConta   := lImpConta
    self:nDivide     := nDivide
    self:lImpAntLP   := lImpAntLP
    self:dDataLP     := dDataLP
    self:cTpVlr      := cTpVlr
    self:lMeses      := lMeses
    self:aMeses      := aMeses
    self:cHeader     := cHeader
    self:lNImpMov    := lNImpMov
    self:lImpTotS    := .F.
    self:cString     := cString
    self:lFiliais    := .T. 
    self:aFiliais    := aFilAux
    self:lPrintZero  := lPrintZero

    self:processData()

return

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtGerCompAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtGerCompAppend(oFilter as object) as object class AccountX6BranchesSmartViewBusinessObject
    //execucao do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtGerComp
Método para execução do CTGERCOMP

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleExeCtGerComp(oFilter as object) as object class AccountX6BranchesSmartViewBusinessObject
    //execucao da CtGerComp
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} Set6Fils
Método para montar array com as 6 filiais

@param nenhum
 
@return array: aFilAux
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method Set6Fils() as array class AccountX6BranchesSmartViewBusinessObject

    Local aFiliais := {} as array
    
    aAdd(aFiliais, iif(!empty(MV_PAR05), PadR(MV_PAR05, TamSX3("CT1_FILIAL")[1]), ""))
    aAdd(aFiliais, iif(!empty(MV_PAR06), PadR(MV_PAR06, TamSX3("CT1_FILIAL")[1]), ""))
    aAdd(aFiliais, iif(!empty(MV_PAR07), PadR(MV_PAR07, TamSX3("CT1_FILIAL")[1]), ""))
    aAdd(aFiliais, iif(!empty(MV_PAR08), PadR(MV_PAR08, TamSX3("CT1_FILIAL")[1]), ""))
    aAdd(aFiliais, iif(!empty(MV_PAR09), PadR(MV_PAR09, TamSX3("CT1_FILIAL")[1]), ""))
    aAdd(aFiliais, iif(!empty(MV_PAR10), PadR(MV_PAR10, TamSX3("CT1_FILIAL")[1]), ""))    

    aFilAux := aClone(aFiliais)

Return aFilAux
