#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.ctb.smartviewctbgercmp.ch"

static cArqtmp  := " " as character

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGACTB", tables="CT1, CT7", name="Classe Pai - SmartViewctbgercmp", country="ALL")

class SmartViewctbgercmp from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new()                 as object
    public method getData()             as object
    public method getSchema()           as object    
    public method getDescription()      as character
    public method getAreas()            as array
    
    //usados na internacionalizacao
    public method preschema()      as object 
    public method preData()        as object
    public method processData()    as json 
    public method itemGetData()    as json

    public method getctbgercmpSchema()  as object
    public method getctbgercmpData()    as object
    public method getctbgercmpAppend()  as object
    public method handlectbgercmpParams() 
    public method handlectbgercmpAppend()   //popula pra tabela temporaria conforme parametros
    public method handleExectbgercmp()      //passar pra ctbgercmp os atributos carregados

    public method VldExectbgercmp() as Logical
    public method CTBGerCmp()
    public method CtbPeriodos()
    public method CompVld()

    public method execDBSkipTemp()
    
    protected method exectbgercmp()    as object
    protected method setctbgercmpParams()   //carrega os atributos default da ctbgercmp

    
    //atributos - na ORDEM que esta sendo chamado na função
    protected data oMeter         as object
    protected data oText          as object
    protected data oDlg           as object
    protected data lEnd           as logical

    protected data cArqtmp        as character
    protected data dDataIni       as date
    protected data dDataFim       as date
    protected data cAlias         as character
    protected data cIdent         as character
    protected data cContaIni      as character
    protected data cContaFim      as character
    protected data cCCIni         as character
    protected data cCCFim         as character
    protected data cItemIni       as character
    protected data cItemFim       as character
    protected data cClvlIni       as character
    protected data cClVlFim       as character
    protected data cMoeda         as character
    protected data cTpSld1        as character
    protected data cTpSld2        as character

    protected data aSetOfBook     as array
    protected data cSegmento      as character
    protected data cSegIni        as character
    protected data cSegFim        as character
    protected data cFiltSegm      as character
    protected data cSegAte        as character

    protected data lVariacao0     as logical
    protected data bVariacao      as CodeBlock
    
    protected data nGrupo         as numeric  
    protected data nDivide        as numeric
    protected data lCt1Sint       as logical
    protected data cString        as character
    protected data cFilUSU        as character
    protected data lEntSint       as logical
    protected data aSelFil        as array

    protected data lTodasFil      as logical
    protected data lPrintZero     as logical
   
    protected data jItens         as json
    protected data jParams        as json

    protected data lCtb380        as logical
    protected data lCtb210        as logical

    protected data oIntegratedProvider as object
    protected data cCompanyName        as character
    protected data cBranchName         as character
endclass

method new() as object class SmartViewctbgercmp
    _Super:new()
return self
//-------------------------------------------------------------------
/*/{Protheus.doc} getAreas
    Define o nome da Area, sem a necessidade de declarar no fonte que estiver herdando
    @author bruno.osilva (B.O)
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getAreas() as array class SmartViewctbgercmp
return {STR0001} // STR0001 "Contabilidade"
//-------------------------------------------------------------------
/*/{Protheus.doc} getDescription
    Define um descrição padrão
    @author bruno.osilva (B.O)
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getDescription() as character class SmartViewctbgercmp
return STR0002+STR0003  // STR0002+STR0003 "Objeto contendo informacoes sobre" + "RELATORIO" 
//-------------------------------------------------------------------
/*/{Protheus.doc} getData
    Atualiza o objeto oData ao ser executado
    @author bruno.osilva (B.O)
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class SmartViewctbgercmp

    //Verifica se o oFilter nao esta vindo de outro lugar e pega os parametros preenchidos
    if oFilter <> NIL        
        //Carrega lista de parametros para a memória
        CTBsetValueMVPAR(oFilter:getParameters(), self:cPergunte )
        self:jParams := oFilter:getParameters()  
    EndIf

    self:oIntegratedProvider := ControlIntegratedProvider():New()
    self:cCompanyName := self:oIntegratedProvider:getCompanyName()
    self:cBranchName  := self:oIntegratedProvider:getBranchName()

    self:setctbgercmpParams()           //Carrega propriedades e valores default
    self:CompVld(::nDivide)

    If self:VldExectbgercmp()
        self:exectbgercmp( oFilter )        //Executa ctbgercmp com as propriedades passadas
        self:getctbgercmpAppend(oFilter)    //Adiciona ao objeto do smartview
    EndIf

    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())
    FreeObj(self:jParams)
    FreeObj(self:oIntegratedProvider)

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} getSchema
    Define um schema padrao quando for chamado o metodo getctbgercmpSchema() na classe filha
    @author bruno.osilva (B.O)
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getSchema() as object class SmartViewctbgercmp

    //Posicoes 2 e 4 sao descricoes dos campos.

    self:oSchema:addProperty("CONTA"    	,STR0004    ,"string" ,STR0004  ,"CONTA")       // Codigo da Conta
    self:oSchema:addProperty("CONTA_M"    	,STR0005    ,"string" ,STR0005  ,"CONTA_M")       // Cod Conta Mascara
    self:oSchema:addProperty("NORMAL"		,STR0006    ,"string" ,STR0006  ,"NORMAL")      // Situacao    
    self:oSchema:addProperty("CTARES"		,STR0007    ,"string" ,STR0007  ,"CTARES")      // Codigo Reduzido da Conta    
    self:oSchema:addProperty("DESCCTA"		,STR0008    ,"string" ,STR0008  ,"DESCCTA")     // Descricao da Conta
    self:oSchema:addProperty("CUSTO"		,STR0009    ,"string" ,STR0009  ,"CUSTO")       // Codigo do Centro de Custo    
    self:oSchema:addProperty("CUSTO_M"		,STR0010    ,"string" ,STR0010  ,"CUSTO")       // Cod. do CCusto Mascara
    self:oSchema:addProperty("CCRES"		,STR0011    ,"string" ,STR0011  ,"CCRES")       // Codigo reduzido do Centro de Custo
    self:oSchema:addProperty("DESCCC"		,STR0012    ,"string" ,STR0012  ,"DESCCC")      // Descrição do Centro de Custo    
    self:oSchema:addProperty("ITEM"			,STR0013    ,"string" ,STR0013  ,"ITEM")        // Codigo do Item Contabil   
    self:oSchema:addProperty("ITEM_M"		,STR0014    ,"string" ,STR0014  ,"ITEM")        // Codigo do Item Mascara   
    self:oSchema:addProperty("ITEMRES"		,STR0015    ,"string" ,STR0015  ,"ITEMRES")     // Codigo Reduzido do Item    
    self:oSchema:addProperty("DESCITEM"		,STR0016    ,"string" ,STR0016  ,"DESCITEM")    // Descricao do Item    
    self:oSchema:addProperty("CLVL"			,STR0017    ,"string" ,STR0017  ,"CLVL")        // Codigo da Classe de Valor    
    self:oSchema:addProperty("CLVL_M"		,STR0018    ,"string" ,STR0018  ,"CLVL_M")      // Codigo da ClVl Mascara   
    self:oSchema:addProperty("CLVLRES"		,STR0019    ,"string" ,STR0019  ,"CLVLRES")     // Cod. Red. Classe de Valor    
    self:oSchema:addProperty("DESCCLVL"		,STR0020    ,"string" ,STR0020  ,"DESCCLVL")    // Descricao da Classe de Valor    

    self:oSchema:addProperty("MOVIMENTO1"	,STR0021    ,"number" ,STR0021  ,"MOVIMENTO1")     // Movimento Tipo de Saldo 01  
    self:oSchema:addProperty("MOVIMENTO2"	,STR0022    ,"number" ,STR0022  ,"MOVIMENTO2")     // Movimento Tipo de Saldo 02   
    self:oSchema:addProperty("MOVIMENTO1_M"	,STR0023    ,"string" ,STR0023  ,"MOVIMENTO1_M")     // Movimento Tipo de Saldo 01  
    self:oSchema:addProperty("MOVIMENTO2_M"	,STR0024    ,"string" ,STR0024  ,"MOVIMENTO2_M")     // Movimento Tipo de Saldo 02  
    self:oSchema:addProperty("VARIACAO"		,STR0025    ,"number" ,STR0025  ,"VARIACAO")     // Variacao   
    self:oSchema:addProperty("VARIACAO_M"	,STR0026    ,"string" ,STR0026  ,"VARIACAO_M")     // Variacao  

    self:oSchema:addProperty("TIPOCONTA"	,STR0027    ,"string" ,STR0027  ,"TIPOCONTA")   // Tipo de Conta
    self:oSchema:addProperty("TIPOCC"		,STR0028    ,"string" ,STR0028  ,"TIPOCC")      // Tipo de Centro de Custo
    self:oSchema:addProperty("TIPOITEM"		,STR0029    ,"string" ,STR0029  ,"TIPOITEM")    // Tipo de Item Contabil
    self:oSchema:addProperty("TIPOCLVL"		,STR0030    ,"string" ,STR0030  ,"TIPOCLVL")    // Tipo de Classe de Valor   
    self:oSchema:addProperty("CCSUP"		,STR0031    ,"string" ,STR0031  ,"CCSUP")       // Codigo do Centro de Custo Superior     
    self:oSchema:addProperty("ITSUP"		,STR0032    ,"string" ,STR0032  ,"ITSUP")       // Codigo do Item Superior    
    self:oSchema:addProperty("CLSUP"		,STR0033    ,"string" ,STR0033  ,"CLSUP")       // Codigo da Clvl Superior    
    self:oSchema:addProperty("ORDEM"		,STR0034    ,"string" ,STR0034  ,"ORDEM")       // ORDEM    
    self:oSchema:addProperty("GRUPO"		,STR0035    ,"string" ,STR0035  ,"GRUPO")       // Grupo Contabil    
    self:oSchema:addProperty("TOTVIS"		,STR0036    ,"string" ,STR0036  ,"TOTVIS")      // Resultado da Visão    
    self:oSchema:addProperty("SLDENT"		,STR0037    ,"string" ,STR0037  ,"SLDENT")      // Saldo entidade    
    self:oSchema:addProperty("FATSLD"		,STR0038    ,"string" ,STR0038  ,"FATSLD")      // Fator Entidade   
    self:oSchema:addProperty("VISENT"		,STR0039    ,"string" ,STR0039  ,"VISENT")      // Visualiza Entidade    
    self:oSchema:addProperty("IDENTIFI"		,STR0040    ,"string" ,STR0040  ,"IDENTIFI")    // Identificador
    self:oSchema:addProperty("NIVEL1"		,STR0041    ,"boolean",STR0041  ,"NIVEL1")      // Nivel      
    self:oSchema:addProperty("FILIAL"		,STR0042    ,"string" ,STR0042  ,"FILIAL")      // Filial 

    self:oSchema:addProperty("COLUNA_1"		,STR0043    ,"number" ,STR0043  ,"COLUNA_1")      // "Coluna 1"  
    self:oSchema:addProperty("COLUNA_2"		,STR0044    ,"number" ,STR0044  ,"COLUNA_2")      // "Coluna 2"
    self:oSchema:addProperty("COLUNA_1_M"	,STR0045    ,"string" ,STR0045  ,"COLUNA_1_M")      // "Coluna 1"  
    self:oSchema:addProperty("COLUNA_2_M"	,STR0046    ,"string" ,STR0046  ,"COLUNA_2_M")      // "Coluna 2"

    self:getctbgercmpSchema()

return self:oSchema

/* Se pula o registro da tabela cArqTemp*/
method ExecDBSkipTemp() class SmartViewctbgercmp
    Local lRet := .F. as Logical
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} getctbgercmpAppend
    Define o dados que serão appendados na temporaria gerada na ctbgercmp,
    podendo serem sobrepostos no handlectbgercmpAppend 
    @author bruno.osilva (B.O)
    @since 28/08/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getctbgercmpAppend(oFilter as object) as object class SmartViewctbgercmp

    local cMascCta           as character
    local cMascCus           as character
    local cMascIte           as character
    local cMascClv           as character
    local cConta             as character
    local cCusto             as character
    local cItem              as character
    local cClvl              as character
    local cPicture           as character
    local nDecimais          as numeric
    local aArea := {}        as array
    Local aTamVal := TAMSX3("CT2_VALOR") as array
    Local cSepara            as character
    aArea := getArea()
    nDecimais := DecimalCTB(::aSetOfBook,::cMoeda) //DecimalCTB(aSetOfBook,mv_par08)
    cPicture  := Iif(!Empty(::aSetOfBook[4]),"@E 999,999,999,999.99","")
    
    //coletando cada mascara
    cMascCta   := IIF(Empty(::aSetOfBook[2]),SuperGetMv("MV_MASCARA"),RetMasCtb(::aSetOfBook[2],@cSepara))//Mascara da Conta
    cMascCus   := IIF(Empty(::aSetOfBook[6]),SuperGetMv("MV_MASCCUS"),RetMasCtb(::aSetOfBook[6],@cSepara))//Mascara do Centro de Custo
    cMascIte   := IIF(Empty(::aSetOfBook[7]),SuperGetMv("MV_MASCCTD"),RetMasCtb(::aSetOfBook[7],@cSepara))//Mascara do Item Contabil    
    cMascClv   := IIF(Empty(::aSetOfBook[8]),SuperGetMv("MV_MASCCTH"),RetMasCtb(::aSetOfBook[8],@cSepara))//Mascara da Classe Valor
    
    cArqTmp->(DBGoTop())

    While !cArqTmp->(Eof())

        //Tratamento para mascara
        cConta := EntidadeCTB(cArqTMP->CONTA ,0,0,Nil/*Tamanho*/,.F.,cMascCta,""/*Separador*/,"CT1",,,,.F.)
        cCusto := EntidadeCTB(cArqTMP->CUSTO ,0,0,Nil/*Tamanho*/,.F.,cMascCus,""/*Separador*/,"CTT",,,,.F.)
        cItem  := EntidadeCTB(cArqTMP->ITEM  ,0,0,Nil/*Tamanho*/,.F.,cMascIte,""/*Separador*/,"CTD",,,,.F.)
        cClvl  := EntidadeCTB(cArqTMP->CLVL  ,0,0,Nil/*Tamanho*/,.F.,cMascClv,""/*Separador*/,"CTH",,,,.F.)
  
        //Metodo para pular determinado registro
        If self:ExecDBSkipTemp(cArqTmp)
            cArqTmp->(DbSkip())
            Loop
        EndIf

        //NovO objeto
	    ::jItens := JsonObject():new()

        ::JItens["CONTA"]           :=  cArqTMP->CONTA
        ::JItens["CONTA_M"]         :=  cConta
        ::JItens["NORMAL"]          :=  cArqTMP->NORMAL
        ::JItens["CTARES"]          :=  cArqTMP->CTARES
        ::JItens["DESCCTA"]         :=  cArqTMP->DESCCTA
        ::JItens["CUSTO"]           :=  cArqTMP->CUSTO
        ::JItens["CUSTO_M"]         :=  cCusto
        ::JItens["CCRES"]           :=  cArqTMP->CCRES
        ::JItens["DESCCC"]          :=  cArqTMP->DESCCC
        ::JItens["ITEM"]            :=  cArqTMP->ITEM
        ::JItens["ITEM_M"]          :=  cItem
        ::JItens["ITEMRES"]         :=  cArqTMP->ITEMRES
        ::JItens["DESCITEM"]        :=  cArqTMP->DESCITEM
        ::JItens["CLVL"]            :=  cArqTMP->CLVL
        ::JItens["CLVL_M"]          :=  cClvl
        ::JItens["CLVLRES"]         :=  cArqTMP->CLVLRES
        ::JItens["DESCCLVL"]        :=  cArqTMP->DESCCLVL
        ::JItens["MOVIMENTO1"]      :=  cArqTMP->MOVIMENTO1
        ::JItens["MOVIMENTO2"]      :=  cArqTMP->MOVIMENTO2
        ::JItens["MOVIMENTO1_M"]    :=  ValorCTB(cArqTmp->MOVIMENTO1,,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
        ::JItens["MOVIMENTO2_M"]    :=  ValorCTB(cArqTmp->MOVIMENTO2,,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
        ::JItens["VARIACAO"]        :=  cArqTMP->VARIACAO
        ::JItens["VARIACAO_M"]      :=  ValorCTB(Abs(cArqTMP->VARIACAO),,,7,nDecimais,.F.,cPicture,cArqTmp->NORMAL, , , , , ,::lPrintZero,.F.,.T.)+"%"                                   
        ::JItens["TIPOCONTA"]       :=  cArqTMP->TIPOCONTA
        ::JItens["TIPOCC"]          :=  cArqTMP->TIPOCC
        ::JItens["TIPOITEM"]        :=  cArqTMP->TIPOITEM
        ::JItens["TIPOCLVL"]        :=  cArqTMP->TIPOCLVL
        ::JItens["CCSUP"]           :=  cArqTMP->CCSUP
        ::JItens["ITSUP"]           :=  cArqTMP->ITSUP
        ::JItens["CLSUP"]           :=  cArqTMP->CLSUP
        ::JItens["ORDEM"]           :=  cArqTMP->ORDEM
        ::JItens["GRUPO"]           :=  cArqTMP->GRUPO
        ::JItens["TOTVIS"]          :=  cArqTMP->TOTVIS
        ::JItens["SLDENT"]          :=  cArqTMP->SLDENT
        ::JItens["FATSLD"]          :=  cArqTMP->FATSLD
        ::JItens["VISENT"]          :=  cArqTMP->VISENT
        ::JItens["IDENTIFI"]        :=  cArqTMP->IDENTIFI
        ::JItens["NIVEL1"]          :=  cArqTMP->NIVEL1
        ::JItens["FILIAL"]          :=  cArqTMP->FILIAL
        ::JItens["COLUNA_1"]        :=  cArqTMP->COLUNA_1
        ::JItens["COLUNA_2"]        :=  cArqTMP->COLUNA_2
        ::JItens["COLUNA_1_M"]      :=  ValorCTB(Abs(cArqTMP->COLUNA_1),,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
        ::JItens["COLUNA_2_M"]      :=  ValorCTB(cArqTMP->COLUNA_2,,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
                                        
        self:processData()

        self:itemGetData()

        self:oData:appendData(::JItens)
        cArqTmp->(DbSkip())
    EndDo

    self:handlectbgercmpAppend(oFilter)

    //tratamento para deletar a temporaria
    dbSelectArea("cArqTmp")
    Set Filter To
    dbCloseArea()

    If Select("cArqTmp") == 0
        FErase(cArqTmp+GetDBExtension())
        FErase("cArqInd"+OrdBagExt())
    EndIf

    dbselectArea("CT1")

    restarea(aArea)
return self:oData
//-------------------------------------------------------------------
/*/{Protheus.doc} setctbgercmpParams
    Define um valor default para os atributos abaixo
    @author bruno.osilva (B.O)
    @since 28/08/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method setctbgercmpParams(oFilter as object) class SmartViewctbgercmp

    //Default funcao ctbgercmp
    ::cMoeda		:= "01"
    ::lTodasFil     := .F.
    ::cArqTmp		:= ""
    ::cIdent		:= "" 
    ::lCt1Sint 	    := .F. 
    ::lEntSint      := .F.
    ::aSelFil       := {}
    ::lTodasFil     := .F.
    ::lPrintZero    := .T.
    //Valida se a classe chamada é a statementOfmonterayVariation
    //UTILIZADO SOMENTE NA CLASSE FILHA statementOfmonterayVariation , pois ha validações especificas na  funcao ctbgercmp()
    ::lCtb380  := .F.
     //Valida se o relatorio é o CTBR210
    ::lCtb210  := .F.
    self:handlectbgercmpParams()

return 
//-------------------------------------------------------------------
/*/{Protheus.doc} exectbgercmp
    Chama o methodo ctbgercmp conforme atributos preenchidos na classe filha
    @author bruno.osilva (B.O)
    @since 28/08/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method exectbgercmp(oFilter as object) as object class SmartViewctbgercmp
    If ::lCtb380 
        self:CtbGerCmp( "",;
                        "",;
                        "",;
                        "",;
                        @cArqtmp,;
                        ::dDataIni,;
                        ::dDataFim,;
                        ::cAlias,;
                        ::cContaIni,;
                        ::cContaFim,;
                        ::cCCIni,;
                        ::cCCFim,;
                        ::cItemIni,;
                        ::cItemFim,;
                        ::cClvlIni,;
                        ::cClVlFim,;
                        ::cMoeda,;
                        ::cTpSld1,;
                        ::cTpSld2,;
                        ::aSetOfBook,;
                        ::cSegmento,;
                        ::cSegIni,;
                        ::cSegFim,;
                        ::cFiltSegm,;
                        ::cSegAte,;
                        ::lVariacao0,;
                        ::nDivide,;
                        ::nGrupo,;
                        ::bVariacao,;
                        ::cIdent,;
                        ::lCt1Sint,;
                        ::cString,;
                        ::cFilUSU,;
                        ::lEntSint,;
                        ::aSelFil,;
                        ::lTodasFil;
            )
        self:handleExectbgercmp()
    Endif
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} exectbgercmp
    Executa a ctbgercmp conforme atributos preenchidos na classe filha
    @author bruno.osilva (B.O)
    @since 28/08/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method CTBGerCmp(oMeter,oText,oDlg,lEnd,cArqtmp,;
					dDataIni,dDataFim,cAlias,cContaIni,cContaFim,cCCIni,cCCFim,cItemIni,cItemFim,cClvlIni,cClVlFim,;
					cMoeda,cTpSld1,cTpSld2,aSetOfBook,cSegmento,cSegIni,cSegFim,cFiltSegm,;
					cSegAte,lVariacao0,nDivide,nGrupo,bVariacao,cIdent,lCt1Sint,cString,cFilUSU,lEntSint,aSelFil,lTodasFil)  class SmartViewctbgercmp

Local lFWCodFil 	:= FindFunction("FWCodFil")

Local aTamConta		:= TAMSX3("CT1_CONTA")
Local aTamCtaRes	:= TAMSX3("CT1_RES")
Local aTamCC        := TAMSX3("CTT_CUSTO")
Local aTamCCRes 	:= TAMSX3("CTT_RES")
Local aTamItem  	:= TAMSX3("CTD_ITEM")
Local aTamItRes 	:= TAMSX3("CTD_RES")
Local aTamClVl  	:= TAMSX3("CTH_CLVL")
Local aTamCvRes 	:= TAMSX3("CTH_RES")
Local aTamVal		:= TAMSX3("CT2_VALOR")
Local aCtbMoeda		:= {}
Local aSaveArea 	:= GetArea()
Local aCampos

Local cChave

Local cEntidIni		:= ""
Local cEntidFim		:= ""
Local cFilDe		:=  xFilial(cAlias)
Local cFilAte		:=  xFilial(cAlias)
Local nTamCta 		:= Len(CriaVar("CT1_DESC"+cMoeda))
Local nTamItem		:= Len(CriaVar("CTD_DESC"+cMoeda))
Local nTamCC  		:= Len(CriaVar("CTT_DESC"+cMoeda))
Local nTamClVl		:= Len(CriaVar("CTH_DESC"+cMoeda))
Local nTamGrupo		:= Len(CriaVar("CT1_GRUPO"))
Local nDecimais		:= 0
Local nInicio		:= Val(cMoeda)
Local nFinal		:= Val(cMoeda)
Local nTamFilial 	:= IIf( lFWCodFil, FWGETTAMFILIAL, TamSx3( "CT2_FILIAL" )[1] )
Local lCusto		:= CtbMovSaldo("CTT")//Define se utiliza C.Custo
Local lItem 		:= CtbMovSaldo("CTD")//Define se utiliza Item
Local lClVl			:= CtbMovSaldo("CTH")//Define se utiliza Cl.Valor
Local lAtSldBase	:= Iif(SuperGetMv("MV_ATUSAL")== "S",.T.,.F.)

Local nPos			:= 0
Local nDigitos		:= 0

Local cCodMasc		:= ""

Local aChave		:= {}
Local nTamCt		:= aTamConta[1]
Local cTableNam1 	:= ""

Private cPlanoRef	:= aSetOfBook[11]
Private cVersao		:= aSetOfBook[12]

lVariacao0	:= Iif(lVariacao0 == Nil,.F.,lVariacao0)

dMinData := CTOD("")

Default cIdent		:= ""
Default lCt1Sint 	:= .F.
Default cMoeda		:= "01"					//// SE NAO FOR INFORMADA A MOEDA ASSUME O PADRAO 01
DEFAULT lEntSint	:= .F.					//// SE IRA CALCULAR O SALDO DAS SINTETICAS PARA 2ª ENTIDADE (C.CUSTO). BOPS 91037
DEFAULT aSelFil     := {}
DEFAULT lTodasFil   := .F.
DEFAULT cArqTmp		:= ""
DEFAULT nDivide     := 1

nGrupo		:=	Iif(nGrupo == Nil,2,nGrupo)
// Retorna Decimais
aCtbMoeda := CTbMoeda(cMoeda)
nDecimais := aCtbMoeda[5]

//Se utiliza o plano referencial, desconsidera os filtros das entidades dos relatórios.
If !Empty(cPlanoRef) .And. !Empty(cVersao)
	//Se o relatório não possuir conta, o plano referencial e a versão serão desconsiderados.
	//Será considerado cód. config. livros em branco.
	If cAlias $ "CTU/CTV/CTW/CTX/CVY" .Or. ::lCtb380 
		//"Plano referencial não disponível nesse relatório. O relatório será processado desconsiderando a configuração de livros."
		cPlanoRef		:= ""
		cVersao			:= ""
		aSetOfBook		:= CTBSetOf("")
	EndIf
Endif

aCampos := {{ "CONTA"		, "C", nTamCt, 0 },;  			// Codigo da Conta
{ "NORMAL"		, "C", 01			, 0 },;			// Situacao
{ "CTARES"		, "C", aTamCtaRes[1], 0 },;  			// Codigo Reduzido da Conta
{ "DESCCTA"	, "C", nTamCta		, 0 },;  			// Descricao da Conta
{ "SUPERIOR"	, "C", nTamCt, 0 },;  			// Codigo da Conta
{ "CUSTO"		, "C", aTamCC[1]	, 0 },; 	 		// Codigo do Centro de Custo
{ "CCRES"		, "C", aTamCCRes[1], 0 },;  			// Codigo Reduzido do Centro de Custo
{ "DESCCC" 	, "C", nTamCC		, 0 },;  			// Descricao do Centro de Custo
{ "ITEM"		, "C", aTamItem[1]	, 0 },; 	 		// Codigo do Item
{ "ITEMRES" 	, "C", aTamItRes[1], 0 },;  			// Codigo Reduzido do Item
{ "DESCITEM" 	, "C", nTamItem		, 0 },;  			// Descricao do Item
{ "CLVL"		, "C", aTamClVl[1]	, 0 },; 	 		// Codigo da Classe de Valor
{ "CLVLRES"	, "C", aTamCVRes[1], 0 },; 		 	// Cod. Red. Classe de Valor
{ "DESCCLVL"   , "C", nTamClVl		, 0 },;  			// Descricao da Classe de Valor
{ "MOVIMENTO1"	, "N", aTamVal[1]+2, nDecimais},; 	// Movimento Tipo de Saldo 01
{ "MOVIMENTO2"	, "N", aTamVal[1]+2	, nDecimais},; 	// Movimento Tipo de Saldo 02
{ "VARIACAO" 	, "N", aTamVal[1]+2, nDecimais},; 	// Variacao
{ "TIPOCONTA"	, "C", 01			, 0 },;			// Conta Analitica / Sintetica
{ "TIPOCC"  	, "C", 01			, 0 },;			// Centro de Custo Analitico / Sintetico
{ "TIPOITEM"	, "C", 01			, 0 },;			// Item Analitica / Sintetica
{ "TIPOCLVL"	, "C", 01			, 0 },;			// Classe de Valor Analitica / Sintetica
{ "CCSUP"		, "C", aTamCC[1]	, 0 },;			// Codigo do Centro de Custo Superior
{ "ITSUP"		, "C", aTamItem[1]	, 0 },;			// Codigo do Item Superior
{ "CLSUP"	    , "C", aTamClVl[1] , 0 },;			// Codigo da Classe de Valor Superior
{ "ORDEM"		, "C", 10			, 0 },;			// Ordem
{ "GRUPO"		, "C", nTamGrupo	, 0 },;			// Grupo Contabil
{ "TOTVIS"		, "C", 01			, 0 },;
	{ "SLDENT"		, "C", 01			, 0 },;
	{ "FATSLD"		, "C", 01			, 0 },;
	{ "VISENT"		, "C", 01			, 0 },;
	{ "IDENTIFI"	, "C", 01			, 0 },;
	{ "NIVEL1"		, "L", 01			, 0 },;				// Logico para identificar se
{ "FILIAL"		, "C", nTamFilial		, 0 }}				// eh de nivel 1 -> usado como


If bVariacao <> Nil
	Aadd(aCampos, { "COLUNA_1"	, "N", aTamVal[1]+2, nDecimais})
	Aadd(aCampos, { "COLUNA_2"	, "N", aTamVal[1]+2, nDecimais})
Endif

/// TRATAMENTO PARA OBTENÇÃO DO SALDO DAS CONTAS ANALITICAS
Do Case
Case cAlias  == "CT7"
	cEntidIni	:= cContaIni
	cEntidFim	:= cContaFim
	cCodMasc	:= aSetOfBook[2]
	If nGrupo == 2
		cChave 	:= "CONTA"
		aChave	:= {"CONTA"}
	Else									// Indice por Grupo -> Totaliza por grupo
		cChave := "CONTA+GRUPO"
		aChave	:= {"CONTA","GRUPO"}
	Endif

EndCase

If !Empty(aSetOfBook[5])				// Indica qual o Plano Gerencial Anexado
	cChave		:= "CONTA"
	aChave		:= {"CONTA"}
Endif

//-------------------
//Criação do objeto
//-------------------
_oTempTable := FWTemporaryTable():New("cArqTmp")
_oTempTable:SetFields( aCampos )
lCriaInd := .T.
_oTempTable:AddIndex("1", aChave)
If !Empty(aSetOfBook[5])				// Indica qual o Plano Gerencial Anexado
	_oTempTable:AddIndex("2", {"ORDEM"})
Endif

//------------------
//Criação da tabela
//------------------
_oTempTable:Create()

cTableNam1 := _oTempTable:GetRealName()

dbSelectArea("cArqTmp")

If !Empty(cSegmento)
	If Len(aSetOfBook) == 0 .or. Empty(aSetOfBook[1])
		Return(cArqTmp)
	Endif
	dbSelectArea("CTM")
	dbSetOrder(1)
	If MsSeek(xFilial()+cCodMasc)
		While !Eof() .And. CTM->CTM_FILIAL == xFilial() .And. CTM->CTM_CODIGO == cCodMasc
			nPos += Val(CTM->CTM_DIGITO)
			If CTM->CTM_SEGMEN == strzero(val(cSegmento),2)
				nPos -= Val(CTM->CTM_DIGITO)
				nPos ++
				nDigitos := Val(CTM->CTM_DIGITO)
				Exit
			EndIf
			dbSkip()
		EndDo
	Else
		Return(cArqTmp)
	EndIf
EndIf

dbSelectArea("cArqTmp")
dbSetOrder(1)

If !Empty(aSetOfBook[5]) // Indica qual o Plano Gerencial Anexado
	// Monta Arquivo Lendo Plano Gerencial
	// Neste caso a filtragem de entidades contabeis é desprezada!
	If cAlias == 'CT7'
		CtPlGerSld(oMeter,oText,oDlg,lEnd,dDataIni,dDataFim,cMoeda,aSetOfBook,cAlias,,,,cTpSld1,cTpSld2,,,1,nDivide)
		dbSetOrder(2)
	EndIf
Else
	Do Case
	Case cAlias $ 'CT7/CTU'	//Comparativo de 2 Tipos de Saldos(Somente 1 Entidade)
		CtSldSoEnt(oMeter,oText,oDlg,lEnd,dDataIni,dDataFim,cEntidIni,cEntidFim,cMoeda,cTpSld1,cTpSld2,aSetOfBook,cSegmento,cSegIni,cSegFim,cFiltSegm,cAlias,lCusto,lItem,lClVl,lAtSldBase,nInicio,nFinal,cFilDe,cFilate,nDivide,lVariacao0,nGrupo,bVariacao,cIdent,aSelFil,lTodasFil)    
	EndCase
EndIf

RestArea(aSaveArea)
Return cArqTmp

//-------------------------------------------------------------------
/*/{Protheus.doc} CtbPeriodos
    Ajusta o parâmetro nDivide vindo do objeto de negócio
    @author bruno.osilva (B.O)
    @since 30/08/2023
    @version 12.1.2210
/*/ 
//-------------------------------------------------------------------
method CompVld(nDivide as Numeric) class SmartViewctbgercmp

    If nDivide == 2			// Divide por cem
	   ::nDivide := 100
    ElseIf nDivide == 3		// Divide por mil
	    ::nDivide := 1000
    ElseIf nDivide == 4		// Divide por milhao
	    ::nDivide := 1000000
    else
        ::nDivide := 1        
    EndIf

return

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Executa a query conforme parametrizacao
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method preSchema() as object class SmartViewctbgercmp

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que  consumido pelo M.I.
    Ajusta o Json conforme alteracao no processData e preSchema
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method preData() as object class SmartViewctbgercmp
    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que  consumido pelo M.I.
    usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method processData() as json class SmartViewctbgercmp

return ::JItens
