#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.entitybyledger.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT2", name="Razão por Entidade", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} EntitybyLedgerSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Razão por entidades
 
@author wilton.santos
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
class EntitybyLedgerSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()            as object
    public method getDescription() as character
    public method getAreas()       as array
    public method getData()        as object
    public method getSchema()      as object
    
    //usados na internacionalização
    public method preschema()      as object 
    public method preData()        as object
    public method processData()    as json
    
    protected method execQuery()   as character //executar a procedure dos Saldos 
    protected method loadEntity()  as array     //carrega quantas entidades tem configurada no ambiente
    protected method execProc()                 //executar a procedure dos Saldos 
    protected method sCtbCfCubo()  as array     //executar a procedure dos Saldos 

    //parametros que serão alimentados por MV_PAR
    protected data dDataIni	      as date
    protected data dDataFim	      as date
    protected data cMoeda	      as character
    protected data cSaldo	      as character
    protected data cMoedaDsc      as character
    protected data cNomeConf      as character
    protected data cCompanyName   as character
    protected data cBranchName    as character
    protected data cTpValor       as character
    protected data nMaxLin        as numeric
    protected data nEntidades     as numeric
    protected data nQtdEntid      as numeric
    protected data nQtEntAdic     as numeric
    protected data jItens         as json
    protected data aEntidade      as array
    protected data aStruct        as array
    protected data aEntidades     as array
    protected data aSelFil        as array
    protected data oIntegratedProvider  as object
    private   data oQry           as object
    public data cAlias            as character

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author wilton.santos
@since 26/07/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class EntitybyLedgerSmartViewBusinessObject

    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //  "Razão por Entidade"
    self:setPergunte("CTR403") //Indica o pergunte que será utilizado no relatório

    //seta alguns atributos com valores default
    ::cAlias      := ""
    ::cSaldo      := "1"
    ::cMoeda      := "01"
    ::nMaxLin     := 55
    ::aStruct     := {}
    ::aEntidades  := {}
    ::aSelFil     := {}
    ::cNomeConf   := "CTBR403"
    ::cTpValor    := SuperGetMV("MV_TPVALOR")
    ::nQtEntAdic  := CtbQtdEntd()

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método seta a descrição do objeto
 
@return object: string
 
@author wilton.santos
@since 26/07/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getDescription() as character class EntitybyLedgerSmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre o Razão por Entidade"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método seta a área do objeto
 
@return object: string
 
@author wilton.santos
@since 26/07/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getAreas() as array class EntitybyLedgerSmartViewBusinessObject
return {STR0003} //"Contabilidade" 

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método seta a área do objeto
 
@return object: string
 
@author wilton.santos
@since 26/07/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class EntitybyLedgerSmartViewBusinessObject
    local cQuery    := "" as character
    local cMascCta        as character
    local cMascCus        as character
    local cMascIte        as character
    local cMascClv        as character
    local cAgrupamento    as character
    local nSaldoAnt := 0  as numeric
    local nSaldoAtu := 0  as numeric
    local nDecimais       as numeric
    local nX              as numeric
    local TAM_VALOR := 19 as numeric
    local aTamVal   := TAMSX3("CT2_VALOR") as array
    local aSetOfBook       as array
    local aNiveis    := {} as array
    local aEntParIni := {} as array
    local aEntParFim := {} as array

    private lCusto := .F. as logical
    private lItem  := .F. as logical
    private lClVl  := .F. as logical
    private cCpoQuebra := "" as character

    CTBsetValueMVPAR(oFilter:getParameters(), "CTR403")

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    ::cCompanyName := ::oIntegratedProvider:getCompanyName()
    ::cBranchName  := ::oIntegratedProvider:getBranchName()

    //Seleção de filiais    
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf

    aSetOfBook := CTBSetOf( "" )

    ::dDataIni	 := MV_PAR01       	//Data inicial ?
    ::dDataFim	 := MV_PAR02       	//Data final ?
    ::cMoeda	 := MV_PAR03       	//Moeda ?
    ::cSaldo	 := MV_PAR04       	//Tipo de Saldo ?
    ::nMaxLin    := MV_PAR05       	//Num.linhas p/ o Razao ?
    ::cMoedaDsc  := MV_PAR06       	//Descricao na moeda ?
    ::nEntidades := Val(MV_PAR07)   //Impr. Ate Entidade ?

    ::nEntidades := iif(::nEntidades == 0, 4, ::nEntidades)

    //preenche a  quantidade de Entidades
    ::aEntidade := self:loadEntity(oFilter)

    //executa a query
    cQuery := self:execQuery(oFilter)

    ::cAlias := MPSysOpenQuery(cQuery, NIL, ::aStruct)

    nDecimais := DecimalCTB(aSetOfBook,::cMoeda) 
    cPicture  := Iif(!Empty(aSetOfBook[4]),"@E 999,999,999,999.99","")

    //coletando as 4 mascaras
    cMascCta   := IIF(Empty(aSetOfBook[2]),SuperGetMv("MV_MASCARA"),RetMasCtb(aSetOfBook[2],@cSepara))//Mascara da Conta

    //se nivel 02
    If ( lCusto := ::nEntidades >= 2 )
        cMascCus   := IIF(Empty(aSetOfBook[6]),SuperGetMv("MV_MASCCUS"),RetMasCtb(aSetOfBook[6],@cSepara))//Mascara do Centro de Custo
    EndIf
    //se nivel 03
    If ( lItem := ::nEntidades >=3 )
        cMascIte   := IIF(Empty(aSetOfBook[7]),SuperGetMv("MV_MASCCTD"),RetMasCtb(aSetOfBook[7],@cSepara))//Mascara do Item Contabil    
    EndIf
    //se nivel 04
    If ( lClVl := ::nEntidades >= 4 )
        cMascClv   := IIF(Empty(aSetOfBook[8]),SuperGetMv("MV_MASCCTH"),RetMasCtb(aSetOfBook[8],@cSepara))//Mascara da Classe Valor
    EndIf

    dbSelectArea(::cAlias)
    (::cAlias)->(DBGoTop())

    While !(::cAlias)->(Eof())

        cConta := EntidadeCTB((::cAlias)->NIV01 ,0,0,Nil/*Tamanho*/,.F.,cMascCta,""/*Separador*/,"CT1",,,,.F.)
        
        if lCusto
            cCusto := EntidadeCTB((::cAlias)->NIV02 ,0,0,Nil/*Tamanho*/,.F.,cMascCus,""/*Separador*/,"CTT",,,,.F.)
        endif

        if lItem
            cItem  := EntidadeCTB((::cAlias)->NIV03 ,0,0,Nil/*Tamanho*/,.F.,cMascIte,""/*Separador*/,"CTD",,,,.F.)
        endif

        if lClVl
            cClvl  := EntidadeCTB((::cAlias)->NIV04 ,0,0,Nil/*Tamanho*/,.F.,cMascClv,""/*Separador*/,"CTH",,,,.F.)
        endif

        ::jItens := JsonObject():new()
        ::jItens["CT2_FILIAL"] := (::cAlias)->CT2_FILIAL
        ::jItens["CT2_DATA"  ] := totvs.framework.treports.date.stringToTimeStamp((::cAlias)->CT2_DATA)
        ::jItens["CT2_LOTE"  ] := (::cAlias)->CT2_LOTE
        ::jItens["CT2_SBLOTE"] := (::cAlias)->CT2_SBLOTE
        ::jItens["CT2_DOC"   ] := (::cAlias)->CT2_DOC
        ::jItens["CT2_LINHA" ] := (::cAlias)->CT2_LINHA
        ::jItens["NIV01"     ] := (::cAlias)->NIV01
        ::jItens["NIV01_M"   ] := cConta

        if lCusto
            ::jItens["NIV02"     ] := (::cAlias)->NIV02
            ::jItens["NIV02_M"   ] := cCusto
        endif

        if lItem
            ::jItens["NIV03"     ] := (::cAlias)->NIV03
            ::jItens["NIV03_M"   ] := cItem
        endif

        if lClVl
            ::jItens["NIV04"     ] := (::cAlias)->NIV04
            ::jItens["NIV04_M"   ] := cClvl
        endif

        //Monta as entidades adicionais
        if ::nEntidades > 4
            for nX := 5 to ::nEntidades
                if nX > ::nQtEntAdic //Monta apenas até a 9
                    exit
                endif
                ::jItens["NIV0"+cValtoChar(nX)] := (::cAlias)->&("NIV0"+cValtoChar(nX))
            next nX
        endif

        ::jItens["VLRDEB"    ] := (::cAlias)->VLRDEB
        ::jItens["VLRDEB_M"  ] := ValorCTB((::cAlias)->VLRDEB,,,TAM_VALOR - 2,nDecimais,.T.,cPicture,,,,,,,.T.,.F.)
        ::jItens["VLRCRED"   ] := (::cAlias)->VLRCRED
        ::jItens["VLRCRED_M" ] := ValorCTB((::cAlias)->VLRCRED,,,TAM_VALOR - 2,nDecimais,.T.,cPicture,,,,,,,.T.,.F.)

        //Pega saldos
        aNiveis := StrToKArr(cCpoQuebra, "+")

		aEntParIni := {}
		aEntParFim := {}

		aEval(aNiveis,{|x| AADD(aEntParIni,(::cAlias)->&(x))})
		aEntParFim := aClone(aEntParIni)

		aSaldoAnt := CtbSldCubo(aEntParIni,aEntParFim,MV_PAR01,MV_PAR02,MV_PAR03,MV_PAR04,::aSelFil, .F.)
		nSaldoAnt := aSaldoAnt[6]
		nSaldoAtu := aSaldoAnt[6]		
            
        nSaldoAtu := nSaldoAtu - (::cAlias)->VLRDEB + (::cAlias)->VLRCRED
        
        ::jItens["SALDOANT"  ] := nSaldoAnt
        ::jItens["SALDOANT_M"] := ValorCTB(nSaldoAnt,,,aTamVal[1],nDecimais,.T.,cPicture,,,,,::cTpValor,,.T.,.F.)

        ::jItens["SALDOATU"  ] := nSaldoAtu
        ::jItens["SALDOATU_M"] := ValorCTB(nSaldoAtu,,,aTamVal[1],nDecimais,.T.,cPicture,,,,,::cTpValor,,.T.,.F.)

        ::jItens["NomeEmpresa"] := ::cCompanyName
        ::jItens["NomeFilial"]  := ::cBranchName

        //Trata agrupamento
        cAgrupamento := ""
        aEval(aNiveis,{|x| cAgrupamento += (::cAlias)->&(x)})

        ::jItens["AGRUPAMENTO"] := cAgrupamento

        self:processData()
        self:oData:appendData(::jItens)

        (::cAlias)->(DbSkip())

    EndDo

    (::cAlias)->(DBCloseArea())
    FreeObj(::jItens)

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} execQuery
    Executa a query conforme parametrização
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getSchema() as object class EntitybyLedgerSmartViewBusinessObject
    Local lCanFilter := .F. as logical
    Local cEntidade  := "" as character
    Local nY := 0 as numeric

    self:oSchema:addProperty("CT2_FILIAL" , STR0020, "string", STR0020, "CT2_FILIAL")  //Filial
    self:oSchema:addProperty("CT2_DATA"   , STR0021, "date"  , STR0021, "CT2_DATA")    //Data Lançamento
    self:oSchema:addProperty("CT2_LOTE"   , STR0022, "string", STR0022, "CT2_LOTE")    //Lote
    self:oSchema:addProperty("CT2_SBLOTE" , STR0004, "string", STR0004, "CT2_SBLOTE")  //Sublote
    self:oSchema:addProperty("CT2_DOC"    , STR0005, "string", STR0005, "CT2_DOC")     //Documento
    self:oSchema:addProperty("CT2_LINHA"  , STR0006, "string", STR0006, "CT2_LINHA")   //Linha
    self:oSchema:addProperty("NIV01"      , STR0007, "string", STR0007, "NIV01"      ,,,,lCanFilter)  //Conta Contábil
    self:oSchema:addProperty("NIV01_M"    , STR0008, "string", STR0008, "NIV01_M"    ,,,,lCanFilter)  //Conta Cont. c/ Mask
    self:oSchema:addProperty("NIV02"      , STR0009, "string", STR0009, "NIV02"      ,,,,lCanFilter)  //Centro de Custo
    self:oSchema:addProperty("NIV02_M"    , STR0010, "string", STR0010, "NIV02_M"    ,,,,lCanFilter)  //Centro Custo. c/ Mask
    self:oSchema:addProperty("NIV03"      , STR0011, "string", STR0011, "NIV03"      ,,,,lCanFilter)  //Item Contábil
    self:oSchema:addProperty("NIV03_M"    , STR0012, "string", STR0012, "NIV03_M"    ,,,,lCanFilter)  //Item Cont. c/ Mask
    self:oSchema:addProperty("NIV04"      , STR0013, "string", STR0013, "NIV04"      ,,,,lCanFilter)  //Classe de Valor
    self:oSchema:addProperty("NIV04_M"    , STR0014, "string", STR0014, "NIV04_M"    ,,,,lCanFilter)  //Classe Vlr. c/ Mask

    //Adiciona entidades adicionais de acordo com a base
    For nY := 5 To ::nQtEntAdic
        cEntidade := StrZero(nY, 2)
        self:oSchema:addProperty("NIV"+cEntidade, STR0015+cEntidade, "string", STR0015+cEntidade, "NIV"+cEntidade ,,,,lCanFilter)  //Entidade
    next nY

    self:oSchema:addProperty("VLRDEB"     , STR0016, "number", STR0016, "VLRDEB"     ,,,,lCanFilter)  //Valor Debito
    self:oSchema:addProperty("VLRDEB_M"   , STR0017, "string", STR0017, "VLRDEB_M"   ,,,,lCanFilter)  //Vlr Debito c/ Masc.
    self:oSchema:addProperty("VLRCRED"    , STR0018, "number", STR0018, "VLRCRED"    ,,,,lCanFilter)  //Valor Credito
    self:oSchema:addProperty("VLRCRED_M"  , STR0019, "string", STR0019, "VLRCRED_M"  ,,,,lCanFilter)  //Vlr. Credito c/ Masc
    self:oSchema:addProperty("SALDOANT"   , STR0023, "number", STR0023, "SALDOANT"   ,,,,lCanFilter)  //Saldo Anterior
    self:oSchema:addProperty("SALDOANT_M" , STR0024, "string", STR0024, "SALDOANT_M" ,,,,lCanFilter)  //Saldo Anterior Masc
    self:oSchema:addProperty("SALDOATU"   , STR0025, "number", STR0025, "SALDOATU"   ,,,,lCanFilter)  //Saldo Atual
    self:oSchema:addProperty("SALDOATU_M" , STR0026, "string", STR0026, "SALDOATU_M" ,,,,lCanFilter)  //Saldo Atual Masc
    self:oSchema:addProperty("NomeEmpresa", STR0027, "string", STR0027, "NomeEmpresa",,,,lCanFilter)  //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial" , STR0028, "string", STR0028, "NomeFilial" ,,,,lCanFilter)  //"Nome Filial"
    self:oSchema:addProperty("AGRUPAMENTO", STR0030, "string", STR0030, "AGRUPAMENTO",,,,lCanFilter)  //"Agrupamento"

    self:oSchema:addParameter("SV_MULTBRANCH", STR0029, "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} execQuery
    Monta a query conforme parametrização

    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method execQuery(oFilter as object) as character class EntitybyLedgerSmartViewBusinessObject
    local cQry		     := "" as character
    local cQryDeb	     := "" as character
    local cQryCre	     := "" as character
    local cQryEntDeb     := "" as character
    local cQryEntCre     := "" as character
    local cQryOrder	     := "" as character
    local cCodEntInicial := "" as character
    local cCodEntFinal   := "" as character
    local cDelete        := Space(1) as character
    local nTamCT2_DEB	 := TamSx3("CT2_DEBITO")[1] as numeric
    local nTamCT2_CC 	 := TamSx3("CT2_CCD")[1]    as numeric
    local nTamCT2_IT	 := TamSx3("CT2_ITEMD")[1]  as numeric
    local nTamCT2_CL	 := TamSx3("CT2_CLVLDB")[1] as numeric
    local nX, nY      := 0  as numeric
    local nParamOrder := 1  as numeric
    local aEntdIni    := {} as array
    local aEntdFim    := {} as array

    aParCubo := self:sCtbCfCubo(cValtoChar(::nEntidades),::cNomeConf,,oFilter)

    For nX := 1 To Len(aParCubo) 
    	AADD( aEntdIni,If(MsAscii(aParCubo[nX][1])== 13,"",aParCubo[nX][1]))
    	AADD( aEntdFim,If(MsAscii(aParCubo[nX][2])== 13,"",aParCubo[nX][2]))
    Next nX

    For nY := 1 To ::nEntidades
        If nY > ::nQtEntAdic //Monta apenas até a 9
            Exit
        EndIf
    	If nY == 1
    		cQryDeb += "CT2_DEBITO NIV01,"
    		cQryCre += "CT2_CREDIT NIV01,"
            cCpoQuebra += "NIV01+"
    		cQryOrder += "NIV01,"
    		cQryEntDeb += "AND CT2_DEBITO BETWEEN ? AND ? "  + CRLF 
    		cQryEntCre += "AND CT2_CREDIT BETWEEN ? AND ? " + CRLF
    	ElseIf nY == 2		
    		cQryDeb += "CT2_CCD NIV02,"
    		cQryCre += "CT2_CCC NIV02,"
            cCpoQuebra += "NIV02+"   
    		cQryOrder += "NIV02,"		
    		cQryEntDeb += "AND CT2_CCD BETWEEN ? AND ? " + CRLF
    		cQryEntCre += "AND CT2_CCC BETWEEN ? AND ? " + CRLF
    	ElseIf nY == 3		
    		cQryDeb += "CT2_ITEMD NIV03,"
    		cQryCre += "CT2_ITEMC NIV03,"
            cCpoQuebra += "NIV03+"                                                           
    		cQryOrder += "NIV03,"	
    		cQryEntDeb += "AND CT2_ITEMD BETWEEN ? AND ? " + CRLF   
    		cQryEntCre += "AND CT2_ITEMC BETWEEN ? AND ? " + CRLF		
    	ElseIf nY == 4		
    		cQryDeb += "CT2_CLVLDB NIV04,"
    		cQryCre += "CT2_CLVLCR NIV04,"
            cCpoQuebra += "NIV04+"                                    
    		cQryOrder += "NIV04,"		
    		cQryEntDeb += "AND CT2_CLVLDB BETWEEN ? AND ? " + CRLF   
    		cQryEntCre += "AND CT2_CLVLCR BETWEEN ? AND ? " + CRLF				
    	Else
    		cQryDeb += "CT2_EC"+StrZero(nY,2)+"DB NIV"+StrZero(nY,2)+","
    		cQryCre += "CT2_EC"+StrZero(nY,2)+"CR NIV"+StrZero(nY,2)+","
            cCpoQuebra +="NIV"+StrZero(nY,2)+"+"
    		cQryOrder += "NIV"+StrZero(nY,2)+","
    		cQryEntDeb += "AND "+"CT2_EC"+StrZero(nY,2)+"DB BETWEEN ? AND ? " + CRLF  
    		cQryEntCre += "AND "+"CT2_EC"+StrZero(nY,2)+"CR BETWEEN ? AND ? " + CRLF			
    	EndIF		

    Next nY

    cQryOrder += "CT2_FILIAL,CT2_DATA,CT2_LOTE,CT2_SBLOTE,CT2_DOC,CT2_LINHA"

    cQry += "SELECT CT2_FILIAL,CT2_DATA,CT2_LOTE,CT2_SBLOTE,CT2_DOC,CT2_LINHA,"
    cQry += cQryDeb + "CT2_VALOR VLRDEB, 0 VLRCRED" + CRLF
    cQry += " FROM " + RetSqlName("CT2") + CRLF
    cQry += " WHERE "
    if !empty(::aSelFil)
        cQry += " CT2_FILIAL IN ( ? ) "
    else
        cQry += " CT2_FILIAL =  ? "
    endif  
    cQry += " AND CT2_DATA >= ? AND CT2_DATA <= ?
    cQry += " AND CT2_TPSALD = ?
    cQry += " AND CT2_MOEDLC = ?
    cQry += " AND (CT2_DC = ? OR CT2_DC = ?) AND CT2_VALOR <> ? " + cQryEntDeb + " AND D_E_L_E_T_ = ? "   

    If oFilter:hasFilter()
        cQry += " AND " + oFilter:getSQLExpression()
    Endif

    cQry += " UNION ALL " + CRLF

    cQry += " SELECT CT2_FILIAL,CT2_DATA,CT2_LOTE,CT2_SBLOTE,CT2_DOC,CT2_LINHA,"
    cQry += cQryCre + "0 VLRDEB, CT2_VALOR VLRCRED"+ CRLF
    cQry += " FROM " + RetSqlName("CT2") + CRLF
    cQry += " WHERE "
    if !empty(::aSelFil)
        cQry += " CT2_FILIAL IN ( ? ) "
    else
        cQry += " CT2_FILIAL =  ? "
    endif
    cQry += " AND CT2_DATA >= ? AND CT2_DATA <= ?
    cQry += " AND CT2_TPSALD = ?
    cQry += " AND CT2_MOEDLC = ?
    cQry += " AND (CT2_DC = ? OR CT2_DC = ?) AND CT2_VALOR <> ? " + cQryEntCre + " AND D_E_L_E_T_ = ? " 

    If oFilter:hasFilter()
        cQry += " AND " + oFilter:getSQLExpression()
    Endif

    cQry += " ORDER BY " + CRLF
    cQry += cQryOrder

    ::oQry := FWPreparedStatement():New(ChangeQuery(cQry))
    
    //Popula BINDs da primeira parte da query    
    if !empty(::aSelFil)
        ::oQry:SetIn(nParamOrder++,     ::aSelFil)
    else
        ::oQry:SetString(nParamOrder++, FWXFilial("CT2"))
    endif  
    ::oQry:SetDate(nParamOrder++,   self:dDataIni)
    ::oQry:SetDate(nParamOrder++,   self:dDataFim)
    ::oQry:SetString(nParamOrder++, self:cSaldo)
    ::oQry:SetString(nParamOrder++, self:cMoeda)
    ::oQry:SetString(nParamOrder++, "1")
    ::oQry:SetString(nParamOrder++, "3")
    ::oQry:SetNumeric(nParamOrder++, 0)

    For nY := 1 To ::nEntidades
        If nY > ::nQtEntAdic //Monta apenas até a 9
            Exit
        EndIf
    	If nY == 1
    		cCodEntInicial	:= If(Empty(aEntdIni[nY]),Space(nTamCT2_DEB),aEntdIni[nY]) 
    		cCodEntFinal	:= If(Empty(aEntdFim[nY]),Repl('z',TamSx3('CT2_DEBITO')[1]),aEntdFim[nY])

            ::oQry:SetString(nParamOrder++, cCodEntInicial)
            ::oQry:SetString(nParamOrder++, cCodEntFinal)
    	ElseIf nY == 2
    		cCodEntInicial	:= If(Empty(aEntdIni[nY]),Space(nTamCT2_CC),aEntdIni[nY]) 
    		cCodEntFinal	:= If(Empty(aEntdFim[nY]),Repl('z',TamSx3('CT2_CCD')[1]),aEntdFim[nY])

            ::oQry:SetString(nParamOrder++, cCodEntInicial)
            ::oQry:SetString(nParamOrder++, cCodEntFinal)
    	ElseIf nY == 3
    		cCodEntInicial	:= If(Empty(aEntdIni[nY]),Space(nTamCT2_IT),aEntdIni[nY]) 
    		cCodEntFinal	:= If(Empty(aEntdFim[nY]),Repl('z',TamSx3('CT2_ITEMD')[1]),aEntdFim[nY])

            ::oQry:SetString(nParamOrder++, cCodEntInicial)
            ::oQry:SetString(nParamOrder++, cCodEntFinal)
    	ElseIf nY == 4
    		cCodEntInicial	:= If(Empty(aEntdIni[nY]),Space(nTamCT2_CL),aEntdIni[nY]) 
    		cCodEntFinal	:= If(Empty(aEntdFim[nY]),Repl('z',TamSx3('CT2_CLVLDB')[1]),aEntdFim[nY])

            ::oQry:SetString(nParamOrder++, cCodEntInicial)
            ::oQry:SetString(nParamOrder++, cCodEntFinal)
    	Else
    		cCodEntInicial	:= If(Empty(aEntdIni[nY]),Space(TamSx3("CT2_EC"+StrZero(nY,2)+"DB")[1]),aEntdIni[nY]) 
    		cCodEntFinal	:= If(Empty(aEntdFim[nY]),Repl('z',TamSx3("CT2_EC"+StrZero(nY,2)+"DB")[1]),aEntdFim[nY])

            ::oQry:SetString(nParamOrder++, cCodEntInicial)
            ::oQry:SetString(nParamOrder++, cCodEntFinal)
    	EndIF

    Next nY

    ::oQry:SetString(nParamOrder++, cDelete)

    //Popula BINDs da segunda parte da query
    if !empty(::aSelFil)
        ::oQry:SetIn(nParamOrder++,     ::aSelFil)
    else
        ::oQry:SetString(nParamOrder++, FWXFilial("CT2"))
    endif 
    ::oQry:SetDate(nParamOrder++,   self:dDataIni)
    ::oQry:SetDate(nParamOrder++,   self:dDataFim)
    ::oQry:SetString(nParamOrder++, self:cSaldo)
    ::oQry:SetString(nParamOrder++, self:cMoeda)
    ::oQry:SetString(nParamOrder++, "2")
    ::oQry:SetString(nParamOrder++, "3")
    ::oQry:SetNumeric(nParamOrder++, 0)

    For nY := 1 To ::nEntidades
        If nY > ::nQtEntAdic //Monta apenas até a 9
            Exit
        EndIf
    	If nY == 1
    		cCodEntInicial	:= If(Empty(aEntdIni[nY]),Space(nTamCT2_DEB),aEntdIni[nY]) 
    		cCodEntFinal	:= If(Empty(aEntdFim[nY]),Repl('z',TamSx3('CT2_DEBITO')[1]),aEntdFim[nY])

            ::oQry:SetString(nParamOrder++, cCodEntInicial)
            ::oQry:SetString(nParamOrder++, cCodEntFinal)
    	ElseIf nY == 2
    		cCodEntInicial	:= If(Empty(aEntdIni[nY]),Space(nTamCT2_CC),aEntdIni[nY]) 
    		cCodEntFinal	:= If(Empty(aEntdFim[nY]),Repl('z',TamSx3('CT2_CCD')[1]),aEntdFim[nY])

            ::oQry:SetString(nParamOrder++, cCodEntInicial)
            ::oQry:SetString(nParamOrder++, cCodEntFinal)
    	ElseIf nY == 3
    		cCodEntInicial	:= If(Empty(aEntdIni[nY]),Space(nTamCT2_IT),aEntdIni[nY]) 
    		cCodEntFinal	:= If(Empty(aEntdFim[nY]),Repl('z',TamSx3('CT2_ITEMD')[1]),aEntdFim[nY])

            ::oQry:SetString(nParamOrder++, cCodEntInicial)
            ::oQry:SetString(nParamOrder++, cCodEntFinal)
    	ElseIf nY == 4
    		cCodEntInicial	:= If(Empty(aEntdIni[nY]),Space(nTamCT2_CL),aEntdIni[nY]) 
    		cCodEntFinal	:= If(Empty(aEntdFim[nY]),Repl('z',TamSx3('CT2_CLVLDB')[1]),aEntdFim[nY])

            ::oQry:SetString(nParamOrder++, cCodEntInicial)
            ::oQry:SetString(nParamOrder++, cCodEntFinal)
    	Else
    		cCodEntInicial	:= If(Empty(aEntdIni[nY]),Space(TamSx3("CT2_EC"+StrZero(nY,2)+"DB")[1]),aEntdIni[nY]) 
    		cCodEntFinal	:= If(Empty(aEntdFim[nY]),Repl('z',TamSx3("CT2_EC"+StrZero(nY,2)+"DB")[1]),aEntdFim[nY])

            ::oQry:SetString(nParamOrder++, cCodEntInicial)
            ::oQry:SetString(nParamOrder++, cCodEntFinal)
    	EndIF

    Next nY

    ::oQry:SetString(nParamOrder++, cDelete)

    cQry := ::oQry:GetFixQuery()

    FreeObj(::oQry)
                        
return cQry

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    Adiciona campos ao schema

    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method preSchema() as object class EntitybyLedgerSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    Ajusta o Json conforme alteração no processData e preSchema

    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method preData() as object class EntitybyLedgerSmartViewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.

    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method processData() as json class EntitybyLedgerSmartViewBusinessObject
return ::JItens

//-------------------------------------------------------------------
/*/{Protheus.doc} loadEntity
    Carrega as entidades configurada no ambiente
    Será usado para alterar a Query antes de ser criada a temporária.

    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method loadEntity(oFilter as object) as array class EntitybyLedgerSmartViewBusinessObject
    local nTam_CPOCHV 	:= 0 as numeric
    local cQuery 		:= "" as character
    local cDelete       := Space(1) as character
    local cAlias403 	:= getNextAlias() as character
    local oQuery as object

    private aEntidades	:= {} as array
    private aEstrCT0	:= {} as array
    private aParCubo	:= {} as array

    cQuery := " SELECT CT0_ID, CT0_CPOCHV, CT0_DSCRES, CT0_F3ENTI "
    cQuery += " FROM "+RetSqlName("CT0")
    cQuery += " WHERE CT0_FILIAL = ? "
    cQuery += " AND CT0_CPOCHV != ? "
    cQuery += " AND D_E_L_E_T_ = ? "

    oQuery := FWPreparedStatement():New(ChangeQuery(cQuery))

    oQuery:SetString(1, FWxFilial("CT0"))
    oQuery:SetString(2, cDelete)
    oQuery:SetString(3, cDelete)

    cQuery := oQuery:GetFixQuery()
    cAlias403 := MPSysOpenQuery(cQuery)
        
    While (cAlias403)->(!Eof())
        nTam_CPOCHV := TamSx3( (cAlias403)->CT0_CPOCHV)[1]
        AADD( aEstrCT0,{ (cAlias403)->CT0_ID , nTam_CPOCHV , (cAlias403)->CT0_DSCRES , (cAlias403)->CT0_F3ENTI } )	
        AADD( ::aEntidades , Space( nTam_CPOCHV ) )		// Parametro entidade inicio
        AADD( ::aEntidades , Space( nTam_CPOCHV ) )		// Parametro entidade fim

        (cAlias403)->(DbSkip())
    EndDo
        
    (cAlias403)->(dbCloseArea())
        
    DbSelectArea("CT0") 
    DbSetOrder(1)
    CtbParCubo(.T.,::cNomeConf)

    FreeObj(oQuery)

return ::aEntidades

//-------------------------------------------------------------------
/*/{Protheus.doc} sCtbCfCubo
    Copia da função CtbCfCubo
    Função que determina o range das entidades a serem filtradas na query

    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method sCtbCfCubo(cCodSel as character, cArquivo as character, lWhenConta as logical, oFilter as object) as array class EntitybyLedgerSmartViewBusinessObject
    local nX,nY		:= 0 as numeric
    local nEscolha	:= Val(cCodSel) as numeric
    local aEntidRet	:= {} as array

    Default cArquivo := "CTBPARCUBO" 
    Default lWhenConta := .t.

    nY := 1
    For nX := 1 TO nEscolha
        if nX > ::nQtEntAdic //Monta apenas até a 9
            Exit
        endif
        ::aEntidades[nY]    := ""
        ::aEntidades[nY+1]  := "zzzzz"
        AADD(aEntidRet,{ ::aEntidades[nY], ::aEntidades[nY+1] })
        nY:= nY+2
    Next nX

return aEntidRet
