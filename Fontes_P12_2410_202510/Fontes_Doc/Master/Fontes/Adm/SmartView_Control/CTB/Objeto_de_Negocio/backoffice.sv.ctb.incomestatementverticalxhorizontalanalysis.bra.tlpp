#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.incomestatementverticalxhorizontalanalysis.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CTS", name="Demonstrativo Analise Vertical x Analise Horizontal", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} IncomeStatementVerticalxHorizontalAnalysisSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do 
Demonstrativo Analise Vertical x Analise Horizontal para o TReports
 
@author Bruno Oliveira (BO)
@since 13/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
class IncomeStatementVerticalxHorizontalAnalysisSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions
    public method new()                     as object
    public method getDescription()          as character
    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object

    public method processData()             as json
    public method preSchema()               as object
    public method preData()                 as object 

    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object

    public method vldExeCtgGerPlan()        as logical
    public method processCustomData()       as json

    //atributos para calculo de porcetagem
    protected data lTotalArqTemp            as logical
    protected data aTotalArqTemp            as array
    protected data nTotMes                  as numeric
    protected data nTotAtu                  as numeric 
    protected data nTotVisM                 as numeric 
    protected data nTotVisA                 as numeric

    protected data cTpValor                 as character
    
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Bruno Oliveira (BO)
@since 13/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class IncomeStatementVerticalxHorizontalAnalysisSmartViewBusinessObject

    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //"Demonstrativo Analise Vertical x Analise Horizontal"
    self:setPergunte("CTR542") //Indica o pergunte que será utilizado no relatório

    ::lTotalArqTemp  := .F.
    ::aTotalArqTemp  := {}
    ::nTotMes        := 0
    ::nTotAtu        := 0
    ::nTotVisM       := 0
    ::nTotVisA       := 0
    ::cTpValor       := SuperGetMV("MV_TPVALOR")
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira (BO)
@since 13/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getDescription() as character class IncomeStatementVerticalxHorizontalAnalysisSmartViewBusinessObject
return STR0002 //"Objeto contendo informacoes sobre o Demonstrativo Analise Vertical x Analise Horizontal"

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 05/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method processData() as json class IncomeStatementVerticalxHorizontalAnalysisSmartViewBusinessObject    
    //Execução antes do append
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 05/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preSchema() as object class IncomeStatementVerticalxHorizontalAnalysisSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 05/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preData() as object class IncomeStatementVerticalxHorizontalAnalysisSmartViewBusinessObject 
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Bruno Oliveira (BO)
@since 13/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getCtGerPlanSchema() as object class IncomeStatementVerticalxHorizontalAnalysisSmartViewBusinessObject

    self:oSchema:addProperty("VERTATU"    , STR0003, "string", STR0003, "VERTATU"    ,,,,.F.) // "Vertical Atual"
    self:oSchema:addProperty("VERTANT"    , STR0004, "string", STR0004, "VERTANT"    ,,,,.F.) // "Vertical Anterior"
    self:oSchema:addProperty("HORIZABS"   , STR0005, "string", STR0005, "HORIZABS"   ,,,,.F.) // "Horizontal Absoluto"
    self:oSchema:addProperty("HORIZREL"   , STR0006, "string", STR0006, "HORIZREL"   ,,,,.F.) // "Horizontal Relativos"
    self:oSchema:addProperty("NomeEmpresa", STR0007, "string", STR0007, "NomeEmpresa",,,,.F.) // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial" , STR0008, "string", STR0008, "NomeFilial" ,,,,.F.) // "Nome Filial"

    self:oSchema:addParameter("SV_MULTBRANCH", STR0009, "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 13/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleExeCtgGerPlan(oFilter as object) as object class IncomeStatementVerticalxHorizontalAnalysisSmartViewBusinessObject
//execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 13/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanAppend(oFilter as object) as object class IncomeStatementVerticalxHorizontalAnalysisSmartViewBusinessObject
//execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 13/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class IncomeStatementVerticalxHorizontalAnalysisSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR542")//Carrega lista de parametros para a memória
   
    self:handleCtgGerPlanParams( oFilter )
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Usado para appendar dados ao schema. Chamado no GetData da classe pai

@return object: self:oData

@author Bruno Oliveira (BO)
@since 13/12/2023
@version 1.0*/
//-------------------------------------------------------------------
method processCustomData() as json class IncomeStatementVerticalxHorizontalAnalysisSmartViewBusinessObject
    Local aTotal 		:= {} as array
    Local nTotal		:= 0  as numeric
    Local nTotVisA		:= 0  as numeric
    Local nTotVisM		:= 0  as numeric
    Local aTamVal		:= TamSX3( "CT2_VALOR" ) as array
    Local nDecimais 	:= DecimalCTB( Self:aSetOfBook, MV_PAR03 ) as numeric
    Local cPicture as character

    cPicture 	:= Self:aSetOfBook[4]
    If !Empty( cPicture ) .And. Len( Trans( 0, cPicture ) ) > 15
        cPicture := ""
    EndIf

    If ! ::lTotalArqTemp 
        nRecnoTMP   := cArqTmp->(Recno())
        cArqTmp->(dbGoTop())
        While !cArqTmp->(Eof())
            If MV_PAR10 <= 2 .AND. cArqTmp->TIPOCONTA == "1"
                nTotal := aScan( aTotal, { |x| x[1] = cArqTmp->CONTA } )
                If nTotal = 0
                    Aadd( aTotal, { cArqTmp->CONTA, 0, 0 } )
                    nTotal := Len( aTotal )
                EndIf

                aTotal[nTotal][2]	+= cArqTmp->SALDOANT
                aTotal[nTotal][3]	+= cArqTmp->SALDOATU
            EndIf

            If cArqTmp->TOTVIS == "1"
                nTotVisM := cArqTmp->SALDOANT
                nTotVisA := cArqTmp->SALDOATU
            EndIf
            cArqTmp->(DbSkip())
        EndDo

        ::lTotalArqTemp  := .T.
        ::aTotalArqTemp  := aTotal
        ::nTotVisM       := nTotVisM
        ::nTotVisA       := nTotVisA
        If Len(aTotal) = 0
            ::aTotalArqTemp  := { {"", 0, 0 }}
        Endif

        dbSelectArea("cArqTmp")
        cArqTmp->(dbGoTo(nRecnoTMP))
    EndIf

    nTotal := aScan(::aTotalArqTemp, { |x| x[1] = cArqTmp->SUPERIOR })

    If MV_PAR10 == 1		//Se considerar o % na sintetica local                     
        If (::nTotMes == 0 .AND. ::nTotAtu == 0) .AND. (Empty( cArqTmp->SUPERIOR ) .OR. cArqTmp->TIPOCONTA == "1") 
            ::nTotMes := cArqTmp->SALDOANT
            ::nTotAtu := cArqTmp->SALDOATU
        EndIf
    ElseIf MV_PAR10 == 2	//Se considerar o % do total em relacao a conta de nivel 1
        If nTotal > 0 
            If cArqTmp->TIPOCONTA == "2" 
                ::nTotMes := ::aTotalArqTemp[nTotal][2]
                ::nTotAtu := ::aTotalArqTemp[nTotal][3]
            Else 
                ::nTotMes := cArqTmp->SALDOANT
                ::nTotAtu := cArqTmp->SALDOATU				
            EndIf    	
                
        Else				
            ::nTotMes := cArqTmp->SALDOANT
            ::nTotAtu := cArqTmp->SALDOATU				
        EndIf					

    EndIf

    If MV_PAR10 < 3
        ::jDados["VERTATU"] := Transform((cArqTmp->(SALDOATU)  / ::nTotAtu) * 100, "@E 9999.99")    + " %"
        ::jDados["VERTANT"] := Transform((cArqTmp->(SALDOANT) / ::nTotMes) * 100, "@E 9999.99")     + " %"

    else
        ::jDados["VERTATU"] := Transform((cArqTmp->(SALDOATU) / ::nTotVisA) * 100 , "@E 9999.99")   + " %"
        ::jDados["VERTANT"] := Transform((cArqTmp->(SALDOANT) / ::nTotVisM) * 100  , "@E 9999.99")  + " %"      
    Endif    

    ::jDados["HORIZABS"] := ValorCTB(cArqTmp->(MOVIMENTO),,,aTamVal[1],nDecimais,.T.,cPicture, cArqTmp->NORMAL,cArqTmp->CONTA,,,::cTpValor,,,.F.,,.F.)
    ::jDados["HORIZREL"] := Transform(cArqTmp->( MOVIMENTO / SALDOATU * 100 ), "@E 9999.99")  + " %"
    
    ::jDados["NomeEmpresa"] := self:cCompanyName
    ::jDados["NomeFilial" ] := self:cBranchName

Return ::jDados


method vldExeCtgGerPlan() as logical class IncomeStatementVerticalxHorizontalAnalysisSmartViewBusinessObject
    Local lRet      := .T.  as Logical

    //Valida se o livro é valido!
    If !ct040Valid(MV_PAR02)
        lRet := .F.    
	EndIf
    //Valida se o livro é valido e se está vazio!
    If !VdSetOfBook( MV_PAR02 , .T. )
        lRet := .F.  
    EndIf

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Renan Gremes
@since 01/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanParams( oFilter ) class IncomeStatementVerticalxHorizontalAnalysisSmartViewBusinessObject
   
    local cContaIni         as character
    local cContaFim         as character
    local cItemIni          as character
    local cItemFim          as character
    local cMoeda  :=  "01"  as character
    local cTpsald :=  "1"   as character
    
    local nIniPage          as numeric
    local lImpAntLP         as logical

    local aSetOfBook        as array
    local aSelFil           as array
    local dDataIni          as date
    local dDataFim          as date
    local dDataLP           as date

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf
   
    If MV_PAR04 == 2 
        dDataIni  	:= MV_PAR05
        dDataFim	:= MV_PAR06
        dPeriodo0	:= CtbPeriodos( MV_PAR03, dDataIni, dDataFim, .F., .F. )[1][2]    
    Else
        dDataIni  	:= CtoD( "01/" + Subs( DtoC( MV_PAR01 ), 4 ) )
        dDataFim		:= MV_PAR01
        dPeriodo0 	:= CtoD( Str( Day( LastDay( MV_PAR01 ) ), 2 ) + "/" + Subs( DtoC( MV_PAR01 ), 4 ) )
    EndIf	
  
    cContaIni   := ""                                       // Conta Inicial ?               
    cContaFim   := Repl('Z',Len( CT1->CT1_CONTA ))          // Conta Final ?  
    cItemIni    := ""                                       // Item Contabil Inicial ?
    cItemFim    := Repl('Z',Len( CTD->CTD_ITEM ))           // Item Contabil Final?   
    cCCIni      := ""                                       // CC Inicial ?
    cCCFim      := Repl('Z',Len( CTT->CTT_CUSTO ))          // CC Final? 
    cClvlIni    := ""                                       // Classe Inicial ?
    cClVlFim    := Repl('Z',Len( CTH->CTH_CLVL ) )          // Classe Final?                         
    aSetOfBook  := Iif(!Empty(MV_PAR02), CTBSetOf(MV_PAR02), CTBSetOf("")) // Cod. Config. Livros ?                               
    cMoeda      := Iif(!Empty(MV_PAR03), MV_PAR03, cMoeda)  // Moeda                                            
    nIniPage    := MV_PAR07                                 // Folha Inicial
    cTpSald     := Iif(!Empty(MV_PAR11), PadR(AllTrim(MV_PAR11), TamSX3("CT2_TPSALD")[1]), cTpsald) // Tipo de Saldo
    lImpAntLP   := .T.                                      // Posição Ant. L/P?
    dDataLP     := Iif(MV_PAR04 == 2, MV_PAR05, MV_PAR01)   // Data Lucros/Perdas?          

    // Atributos com conteúdo fixado estão obedecendo a chamado do relatorio principal.
    self:dDataIni       := dDataIni
    self:dDataFim       := dDataFim
    self:cContaIni      := cContaIni
    self:cContaFim      := cContaFim
    self:cCCIni         := cCCIni  
    self:cCCFim         := cCCFim 
    self:cItemIni       := cItemIni  
    self:cItemFim       := cItemFim
    self:cClvlIni       := cClvlIni  
    self:cClVlFim       := cClVlFim  
    self:cAlias         := ""//são obrigatórios
    self:cIdent         := ""//são obrigatórios           
    self:aSetOfBook     := aSetOfBook   
    self:cMoeda         := cMoeda
    self:cSaldos        := cTpsald
    self:cSegmento      := Space(2)   
    self:cSegIni        := Space(20)
    self:cSegFim        := Repl( "Z", 20 )
    self:cFiltSegm      := Space(30)
    self:lImpAntLP      := lImpAntLP     
    self:dDataLP        := dDataLP  
    self:aSelFil        := aSelFil
    self:aGeren         := nil
    self:dPeriodo0      := dPeriodo0
    self:cRecDesp       := ""
    self:lNImpMov       := nil
    self:lImpConta      := nil
    self:nGrupo         := nil
    self:cHeader        := nil
    self:nDivide        := nil
    self:lVlrZerado     := nil
    self:cFiltroEnt     := nil
    self:cCodFilEnt     := nil
    self:cSegmentoG     := nil
    self:cSegIniG       := nil
    self:cSegFimG       := nil
    self:cFiltSegmG     := nil
    self:lUsGaap        := nil
    self:cMoedConv      := nil
    self:cConsCrit      := nil
    self:dDataConv      := nil
    self:nTaxaConv      := nil
    self:aGeren         := nil
    self:lImpSint       := .T. 
    self:lNImpMov       := nil
    self:cFilUSU        := ""
    self:lRecDesp0      := nil
    self:dDtZeraRD      := ""
    self:lMovPeriodo    := nil
    self:dDtCorte       := nil
    self:lPlGerSint     := nil
    self:lConsSaldo     := nil
    self:lCompEnt       := nil
    self:cArqAux        := nil
    self:lUsaNmVis      := nil
    self:cNomeVis       := nil
    self:lCttSint       := nil
    self:lTodasFil      := nil
    self:cQuadroCTB     := nil
    self:aEntidades     := nil
    self:cCodEntidade   := nil
    self:lDemDRE        := nil
    self:dFinalA        := nil

return
