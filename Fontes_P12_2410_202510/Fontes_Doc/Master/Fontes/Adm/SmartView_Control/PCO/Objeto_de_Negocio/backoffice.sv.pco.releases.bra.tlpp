#include "msobject.ch"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.pco.releases.bra.ch"

namespace totvs.protheus.pco.smartviewsintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPCO", tables="AKD,AK1,AK5,AK6,AKF,AL2,CTT,CTD,CTH", name="Lançamentos PCO", country="ALL",customTables="AKD")

//-------------------------------------------------------------------------------
/*{Protheus.doc} CentrodeCustoTReportsBusinessObject
Classe para criação do Objeto de Negócio de budgetaccounts para o TReports
 
@author Douglas Rodrigues da Silva
@since 22/06/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
Class ReleasesSmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()            as object
    public method getDescription() as character
    public method getAreas()       as array
    public method getData()        as object
    public method getSchema()      as object

    //usados na internacionalização
    public method preschema()      as object
    public method preData()        as object
    public method processData()    as json

    protected data cAlias          as character
    protected data oQuery1         as object
    protected data jItens          as json

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
  
@author Douglas Rodrigues da Silva
@since 22/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method new() as object Class ReleasesSmartviewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Lançamentos" 
    self:setPergunte("PCR400")   //Indica o pergunte que será utilizado no relatório
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Douglas Rodrigues da Silva
@since 22/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getDescription() as character class ReleasesSmartviewBusinessObject
return STR0002 

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
@author Douglas Rodrigues da Silva
@since 22/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getAreas() as array class ReleasesSmartviewBusinessObject
Return { STR0003 }

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author Renan Gremes
    @since 14/02/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preSchema() as object class ReleasesSmartviewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author Renan Gremes
    @since 14/02/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preData() as object class ReleasesSmartviewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author Renan Gremes
    @since 14/02/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method processData() as json class ReleasesSmartviewBusinessObject
return ::jItens

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Douglas Rodrigues da Silva
@since 22/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class ReleasesSmartviewBusinessObject

    local cQuery            as character
    local dDataDe           as character
    local dDataAte          as character
    local cContaDe          as character
    local cContaAte         as character
    local cClasseDe         as character
    local cClasseAte        as character
    local cOperDe           as character
    local cOperAte          as character
    local cTpSald           as character
    local cProcessDe        as character
    local cProcessAte       as character
    local nParamOrder := 1  as numeric
    local nOrdem      := 1  as numeric
    Local cGetDB	  := Alltrim(UPPER(TcGetDb())) as character
    Local cGrup      := IIf("SQL" $ cGetDB, "+", "||") as character
    Local cOrdem      := "AKD_CO"+cGrup+"AKD_DATA" as character

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "PCR400")

    //Elimina da memória a instância do objeto informado como parâmetro
    FreeObj(oFilter:getParameters())

    dDataDe     := MV_PAR01
    dDataAte    := MV_PAR02
    cContaDe    := MV_PAR03
    cContaAte   := MV_PAR04
    cClasseDe   := MV_PAR05
    cClasseAte  := MV_PAR06
    cOperDe     := MV_PAR07
    cOperAte    := MV_PAR08
    cTpSald     := MV_PAR09
    cProcessDe  := MV_PAR10
    cProcessAte := MV_PAR11
    nOrdem      := Iif(MV_PAR12 < 1, 1,MV_PAR12)

    If nOrdem == 1
        cOrdem := " AKD_CO"+cGrup+"AKD_DATA "
    ElseIf nOrdem == 2
        cOrdem := " AKD_CO"+cGrup+"AKD_CLASSE"+cGrup+"AKD_OPER "
    ElseIf nOrdem == 3
        cOrdem := " AKD_CLASSE"+cGrup+"AKD_OPER "
    ElseIf nOrdem == 4
        cOrdem := " AKD_OPER "
    ElseIf nOrdem == 5
        cOrdem := " AKD_DATA "+cGrup+" AKD_CO "+cGrup+" AKD_CLASSE "+cGrup+" AKD_OPER "
    EndIf
    cQuery :=  " SELECT  " + cOrdem + " AGRUPAMENTO, "
    cQuery  +=   self:getSQLFields() //Pega campos do schema    
    cQuery += " FROM "+RetSqlName("AKD")+"  AKD "
    cQuery += " WHERE "

    cQuery += " AKD.AKD_FILIAL =  ? "
    cQuery += " AND AKD_DATA	BETWEEN ? AND ? "
    cQuery += " AND AKD_CO		BETWEEN ? AND ? "
    cQuery += " AND AKD_CLASSE	BETWEEN ? AND ? "
    cQuery += " AND AKD_OPER	BETWEEN ? AND ? "
    cQuery += " AND AKD_TPSALD = ? "
    cQuery += " AND AKD_PROCES	BETWEEN ? AND ? "
    cQuery += " AND AKD_STATUS != ?"
    cQuery += " AND AKD.D_E_L_E_T_ = ?"

    cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")    

	self:oQuery1 := FWPreparedStatement():New(ChangeQuery(cQuery))

    self:oQuery1:SetString(nParamOrder++, FWxFilial("AKD"))
    self:oQuery1:SetDate(nParamOrder++,   dDataDe)
	self:oQuery1:SetDate(nParamOrder++,   dDataAte)
    self:oQuery1:SetString(nParamOrder++, cContaDe)
	self:oQuery1:SetString(nParamOrder++, cContaAte)
    self:oQuery1:SetString(nParamOrder++, cClasseDe)
	self:oQuery1:SetString(nParamOrder++, cClasseAte)
    self:oQuery1:SetString(nParamOrder++, cOperDe)
	self:oQuery1:SetString(nParamOrder++, cOperAte)
    self:oQuery1:SetString(nParamOrder++, cTpSald)
	self:oQuery1:SetString(nParamOrder++, cProcessDe)
    self:oQuery1:SetString(nParamOrder++, cProcessAte)
    self:oQuery1:SetString(nParamOrder++, '3')
    self:oQuery1:SetString(nParamOrder++, Space(1))

    cQuery := self:oQuery1:GetFixQuery()

    //Define a query do Objeto de Negócio
    self:setQuery(cQuery)
    self:processData()
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getStruct
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Douglas Rodrigues da Silva
@since 22/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getSchema() as object class ReleasesSmartviewBusinessObject
    
    local aFieldsAKD := {}  as array
    
    aFieldsAKD := {"AKD_FILIAL", "AKD_STATUS", "AKD_LOTE"  , "AKD_ID"    , "AKD_DATA"  , "AKD_CO"    , "AKD_CLASSE",; 
                   "AKD_OPER"  , "AKD_TPSALD", "AKD_TIPO"  , "AKD_HIST"  , "AKD_IDREF" , "AKD_PROCES", "AKD_CHAVE" ,;
                   "AKD_ITEM"  , "AKD_SEQ"   , "AKD_USER"  , "AKD_COSUP" , "AKD_VALOR1", "AKD_VALOR2", "AKD_VALOR3",; 
                   "AKD_VALOR4", "AKD_VALOR5", "AKD_CODPLA", "AKD_VERSAO", "AKD_CC"    , "AKD_ITCTB" , "AKD_CLVLR" ,;
                   "AKD_LCTBLQ", "AKD_FILORI", "AKD_UNIORC"}

    self:oSchema:aliasToSchema("AKD",aFieldsAKD)

    self:oSchema:addProperty("AGRUPAMENTO", STR0004, "string", STR0004, "AGRUPAMENTO") //Ordenacão

return self:oSchema
