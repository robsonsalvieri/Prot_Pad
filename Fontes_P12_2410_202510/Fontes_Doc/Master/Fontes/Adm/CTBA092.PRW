#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "CTBA092.CH"

/*


Ŀ
Programa  CTBA092     Autor Miguel Angel Rojas G.      Data  02.12.13 
Ĵ
Descrio  MANTENIMIENTO FUNCIONES                                        
Ĵ
Sintaxe    CTBA092                                                        
Ĵ
Parametros                                                                
Ĵ
 Uso       GENERAL                                                        
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                 
Ĵ
Programador  Data    BOPS       Motivo da Alteracao                     
Ĵ
Miguel Rojas10/02/14            Indices utilizados en DbSetOrder        
Ĵ
Miguel Rojas25/02/14           Validacion de tabla CWN_PAR1             
Ĵ
Gpe StaCruz 14/05/14           Se agregaron 2 parametros para el tipo   
                               AXMOEDA                                  
Ĵ
M.Camargo   29/04/15           Se modifica validacion CTB92T1T para     
                               validar todos los tipos de menmnicos    
ٱ


*/

Function CTBA092()
Local oBrowse
Private cMnemonico	:= "M_"
Private lGuarda		:=.f.	
Private cRetSix 		:=""
Private cTabla  		:=""
Private lValido 		:= .F.
Private lModifica 	:= .T.  //--- Variable para validacines en modificaciones
oBrowse := FWMBrowse():New()
oBrowse:SetAlias("CWN")
oBrowse:SetDescription(STR0018)//"Funciones"
oBrowse:SetMenuDef("CTBA092")
oBrowse:DisableDetails()
oBrowse:Activate()
Return NIL

/*


Ŀ
Funo    MenuDef    Autor  Miguel Angel Rojas G.  Data 10.12.2013
Ĵ
Descrio  Crea un Menu Estandar (ABC) sin usar aRotina               
Ĵ
Sintaxe    MenuDef()                                                     
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno   Menu Estandar                                               
Ĵ
 Uso       CTBA092                                                    
ٱ


*/

Static Function MenuDef()
Return FWMVCMenu( "CTBA092" ) // Genera un Menu Estandar en MVC sin Necesidad de aRotina.

/*


Ŀ
Funcion   ModelDef   Autor Miguel Angel. Rojas G.  Data 03/12/2013
Ĵ
Desc.     Crea la estructura del modelo de datos llama                
          funciones para validar antes de guardar y al modificar      
Ĵ
Sintaxe    MenuDef()                                                     
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno    ExpO1 = Modelo de datos                                    
Ĵ
 Uso       CTBA092                                                    
ٱ


*/

Static Function ModelDef()
Local oStruCWN := FWFormStruct( 1, "CWN" )	
Local oModel	
//--- Objeto Constructor del Modelo de Datos
oModel := MPFormModel():New( "CTBA092", { | oMdl | CTBA092PRE( oMdl ) } , { | oMdl | CTBA092POS( oMdl ) },/*bCommit*/,/*bCancel*/ )
//--- Agrega un Modelo para la captura de datos
oModel:AddFields( "FUNCIONES", /*es el encabezado*/, oStruCWN )
//--- Descripcin del Modelo de Datos
oModel:SetDescription( STR0018) //Funciones
//--- Descripcin de los componente del Modelo de Datos
oModel:GetModel( "FUNCIONES" ):SetDescription( STR0018 ) // Funcioones
oModel:SetPrimaryKey( { "FUNCIONES", "CWN_FILIAL","CWN_CODFUN" } )
Return oModel

/*


Ŀ
Funcion   ViewDef    Autor Miguel Angel. Rojas G.  Data 03/12/2013
Ĵ
Desc.      Genera la vista de los datos de acuerdo al  modelo         
                                                                      
Ĵ
Sintaxe    ViewDef()                                                     
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno   ExpO1 = Vista de datos                                      
Ĵ
 Uso       CTBA092                                                    
ٱ


*/

Static Function ViewDef()
Local oModel   := FWLoadModel( "CTBA092" )
Local oStruCWN := FWFormStruct( 2, "CWN")
Local oView
oView := FWFormView():New()
oView:SetModel(oModel)
oView:AddField( "VIEW_CWN", oStruCWN, "FUNCIONES" )
oView:CreateHorizontalBox( "PANTALLA" , 100 )
oView:SetOwnerView( "VIEW_CWN", "PANTALLA" )
oView:SetViewAction( "BUTTONOK" ,{ |oView| CTB92GD( oView ) } )
oView:SetViewAction( "BUTTONCANCEL" ,{ |oView| CTB92CR( oView ) } )
Return oView
/*


Ŀ
Funcion   CTB92LBOX  Autor Miguel Angel. Rojas G.  Data 03/12/2013
Ĵ
Desc.     Llenado del ComboBox del campo CWN_TIPO con las opciones    
          especificadas en CTB092.CH                                  
Ĵ
Sintaxe    CTB92LBOX()                                                   
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno   ExpC1 = Lista de valores para el campo CWN_TIPO             
Ĵ
 Uso       CTBA092                                                    
ٱ


*/
Function CTB92LBOX() 
Local cRetorno:=STR0004+";"+STR0005    // Conversion de Monedas , Apuntador
Return (cRetorno)

/*


Ŀ
Funcion   CTB92SIX   Autor Miguel Angel. Rojas G.  Data 03/12/2013
Ĵ
Desc.     Agrega datos a la Consulta SIXFUN, muestra los indices      
          de la tabla indicada en el mnemonico                        
Ĵ
Sintaxe    CTB92SIX(ExpC1)                                               
Ĵ
Parametros ExpC1 = Tipo de valor a buscar "I"=Indice                  
                                                                      
Ĵ
Retorno    Expl1 = .T. Si se ejecuto la funcion                       
Ĵ
 Uso      Filtro Consulta SIXFUN                                      
ٱ


*/

Function CTB92SIX(cVar)
Local lRet    := .F.
Local nUsado  := 0  
Local nOpcA   := 0 
Local nSX3Campo := 0
Local aColIX	:= {} 
Local aHeadIX 	:= {}
Local oDlgCons 
Local oGetCons
Local cTabSix 			:= "" 	
Local cValor 			:= ""
Local cPar				:= ""
Local cModulo 			:= Alltrim(FunName())
Local cDescripcion	:=""
Default cVar 	:= "I" 	

If cModulo =="CTBA092"
    cValor		:= "T"
	cPar 		:= "CWN_PAR1"
	
	//---Llama a la funcion pra obtener el nombre de la tabla.
     IF !Empty(Rtrim(M->&(cPar)))
		cTabSix	:= CTB92CWJ(cValor,cPar)
	EndIf	
Else
   	cTabSix 	:= cTabla
EndIf   	

cRetSix :=""
If !Empty(cTabSix)
	aColsIX 	:= {}
	aHeadIX 	:= {}
	nUsado 		:= 0
	 	
	Aadd(aHeadIX, {STR0001  	, "cIndice"   , "@!", 003, 0,,, "C",, "V",,,,"V"}) //"Indice"
	Aadd(aHeadIX, {STR0003	, "cDescricao", "@!", 070, 0,,, "C",, "V",,,,"V"}) //"Descripcin"
	Aadd(aHeadIX, {STR0002 	, "cChave"    , "@!", 160, 0,,, "C",, "V",,,,"V"}) //"Llave"
	
	//---Seleccionamos la tabla SIX para buscar los ndices de las tablas.
 	DbSelectArea("SIX")
 	SIX->(DbSeek(cTabSix))
 	While !Eof() .And. (SIX->INDICE == cTabSix)

		cDescripcion := SixDescricao()		
  		aAdd(aColsIX, { SIX->ORDEM,cDescripcion, SIX->CHAVE, SIX->INDICE, .F. })
  		SIX->(DbSkip())
  	Enddo

	//---Genera la Ventana que muestra la informacin generada
	Define MsDialog oDlgCons Title OemToAnsi (STR0001) From 000, 000 To 500, 850 Of oMainWnd PIXEL   //Indice
	oGetCons := MsNewGetDados():New(000, 000, 500, 850, 0,,,,,, 99999,,,, oDlgCons, aHeadIX, aColsIX)
	oGetCons:oBrowse:align := CONTROL_ALIGN_ALLCLIENT
	Activate MsDialog oDlgCons Centered ON INIT EnchoiceBar (oDlgCons, {	|| nOpcA := 1, oDlgCons:End()},	{|| nOpcA := 0, oDlgCons:End()} )
		
	//---Retorna el Orden o la llave depende del parmetro 
	IF cVar=="I"
		cRetSix := oGetCons:aCols[oGetCons:nAt, 1]		// Orden
	ELSE
		cRetSix := oGetCons:aCols[oGetCons:nAt, 3]		// Llave
	ENDIF
Else
	 Aviso( STR0006,STR0025, {"Ok"})   // Aviso, Tabla vacia, indice invalido
EndIF	 	
	lRet := .T.
Return(lRet)

/*


Ŀ
Funcion   CTB92RET   Autor Miguel Angel. Rojas G.  Data 11/12/2013
Ĵ
Desc.     Llenado de consulta SIXFUN                                  
                                                                      
Ĵ
Sintaxe    CTB92RET()                                                    
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno                                                               
Ĵ
 Uso       Retorno Consulta SIXFUN                                    
ٱ


*/
Function CTB92RET()
Local cRetorno := cRetSix
Return(cRetorno)

/*


Ŀ
Funcion   CTB92CESP  Autor Miguel Angel. Rojas G.  Data 11/12/2013
Ĵ
Desc.     Determina que consulta se ejecutara de acuerdo al valor     
          en CWN_TIPO y el campo en que se encuentra                  
Ĵ
Sintaxe    CTB92ESP()                                                    
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno                                                               
Ĵ
 Uso       Filtro consulta CTB921                                     
ٱ


*/
Function CTB92CESP()
Local cCadena 	:=""
Local cVar 	:= Alltrim(ReadVar())
Local cTipo 	:= M->CWN_TIPO
If cTipo == "1"
	 CONPAD1(,,,"CWJFIL")
	 cRetSix:= CWJ->CWJ_CODMNE 
ELSE
	DO CASE
		CASE cVar == "M->CWN_PAR1"
			CONPAD1(,,,"CWJTAB")
			cRetSix:= CWJ->CWJ_CODMNE
			cTabla := CWJ->CWJ_TABLA
		CASE cVar == "M->CWN_PAR2"
			CONPAD1(,,,"SIXFUN")					
		CASE cVar == "M->CWN_PAR3"
			//cCadena :=  rTrim(oModel:GetValue( "FUNCIONES","CWN_PAR3" ))
			CONPAD1(,,,"CWJ")	
			cCadena := alltrim(m->cwn_par3)  
			If Altera .AND. SubStr(cCadena,Len(cCadena),1)<>"+" .AND.!Empty(cCadena)
				cCadena := "+" +Alltrim( CWJ->CWJ_CODMNE)
		    Else
		    	cCadena := cCadena + Alltrim( CWJ->CWJ_CODMNE)
		    Endif	
			cRetSix:= cCadena+"+" //GSA
			
		CASE cVar == "M->CWN_PAR4"
		    CONPAD1(,,,"CWJ")
			cRetSix:= CWJ->CWJ_CODMNE
		ENDCASE		 
ENDIF
Return(.t.)

/*


Ŀ
Funcion   CTB92CRET  Autor Miguel Angel. Rojas G.  Data 11/12/2013
Ĵ
Desc.     Regresa el valor seleccionado de la consulta especifica     
                                                                      
Ĵ
Sintaxe    CTB92CRET()                                                   
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno    ExpC1 = Valor de retorno de SIXFUN                         
Ĵ
 Uso       Retorno consulta CTB921                                    
ٱ


*/

Function CTB92CRET()
Local cRetorno := cRetSix
Return(cRetorno)

/*


Ŀ
Funcion   CTB092POS  Autor Miguel Angel. Rojas G.  Data 11/12/2013
Ĵ
Desc.     Valida la informacion al hacer clic en Confirmar            
                                                                      
Ĵ
Sintaxe    CTB092POS(ExpO1)                                              
Ĵ
Parametros ExpO1 = Modelo de Datos                                    
                                                                      
Ĵ
Retorno    ExpL1 = Si .T. guarda los datos de acuerdo a la validacion 
Ĵ
 Uso       CTBA092                                                    
ٱ


*/

Static Function CTBA092POS( oModel )
Local lRet   	:= .T.	
Local cVar		:= ""
Local cTipo 	:= ""
Local nFin 	:= 5 
Local ni		:= 0
Local nVal		:= 0
Local cPar 	:= ""
Local nOperation := oModel:GetOperation()
Local cFuncion	:=""
lGuarda		:=.f.     
If nOperation == MODEL_OPERATION_INSERT .OR. nOperation == MODEL_OPERATION_UPDATE
	cTipo := alltrim(M->CWN_TIPO)
	//---Identifica si es xMoeda o Posicione
	If cTipo =="2"
   		nFin := 4
   		//---Si es Posicione, el campo CWN_PAR5 debe estar vaco y no editable.
	ELSE
	    cPar := &(M->CWN_TIPDAT)
	    IF cPar <>1
	    	Help( ,, STR0006,,STR0020,1, 0 )	//Aviso, La Funcin Conversin de Monedas solo acepta Tipo de resultado Numrico
   			lRet := .f.
	    EndIF
	ENDIF
	
//---Verifica que no esten vacos los campos parametro n de acuerdo a la regla de negocio.
	If lRet
		FOR ni:= 3 to nFin
			cVar :="CWN_PAR"+Alltrim(Str(nI))
			cPar :=(M->&(cVar))
			If Empty(cPar)
				Help( ,, STR0006,, STR0007+Alltrim(Str(ni))+" ("+cVar+")"+STR0008,1, 0 ) // Aviso, El campo parametro, no se relleno
				ni:= nFin
				lRet :=.f.
			EndIF
		Next
	   lGuarda := .t.
	EndIF
	//--- Borro el ultimo signo + del campo CWN_PAR3 antes de G<uardar
	cVar:= Rtrim(M->CWN_PAR3)
	If SubStr(cVar,Len(cVar),1)=="+"
	   cVar :=	SubStr(cVar,1,Len(cVar)-1)
	   oModel:LoadValue("FUNCIONES","CWN_PAR3",cVar)
	EndIf   	
EndIf
//---Confirmar si desea eliminar el registro
If nOperation ==	MODEL_OPERATION_DELETE
	lValido:= .f.
	cVar := CWN->CWN_CODFUN
	cPar := "4"
	DbSelectArea("CWJ")
	CWJ ->(DBSETORDER(2))   //CWJ_FILIAL+CWJ_TIPDAT+CWJ_DATVIN		
	If CWJ->(DbSeek(xFilial("CWJ")+cPar+cVar))
		Help( ,, STR0006,,STR0019 ,1, 0 )  // Aviso,Esta funcin esta siendo utilizada en algn Mnemnico y no permite ser Eliminada
 		lRet := .F.
 	Else	
    	lRet := MsgNoYes(STR0011)		//Esta seguro de Eliminar el registro?
		If !lRet
			Help( ,, STR0006,, STR0012,1, 0 ) //Aviso,No se hicieron cambios
		EndIf
	EndIF			
EndIf
Return lRet
  

/*


Ŀ
Funcion   CTB092PRE  Autor  Miguel Angel. Rojas G  Data 11/12/2013
Ĵ
Desc.     Si esta en modificar valida cuales campos se pueden editar  
                                                                      
Ĵ
Sintaxe    CTB92PRE(ExpO1)                                               
Ĵ
Parametros ExpO1 = Objeto Modelo de datos                             
                                                                      
Ĵ
Retorno                                                               
Ĵ
 Uso       CTBA092                                                    
ٱ


*/

Static Function CTBA092PRE( oModel )
Local lRet   		:= .T.
Local cTipDat 		:= "4"
Local cFuncion		:= ""


If Altera .AND. !lValido
	lValido := .t.
	cFuncion := CWN->CWN_CODFUN
	DbSelectArea("CWJ")
	CWJ ->(DBSETORDER(2))		//CWJ_FILIAL+CWJ_TIPDAT+CWJ_DATVIN		
	If CWJ->(DbSeek(xFilial("CWJ")+cTipDat+cFuncion))
		Help( ,, STR0006,,OemToAnsi(STR0010) ,1, 0 )     //Aviso, La Funcion esta en uso de algun mnemonico, solo permitir cambios en la Descripcin y en la Ayuda	
 		lRet := .F.
 		lModifica := .F.
 	Else
 		lModifica := .T.	
 	EndIf	
EndIf	

Return lRet

/*


Ŀ
Funcion   CTB92M     Autor  Miguel Angel. Rojas G  Data 11/12/2013
Ĵ
Desc.     Activa o desactiva los campos al modificar, dependiendo si  
          esta en uso la funcion                                      
Ĵ
Sintaxe    CTB92M()                                                      
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno   ExpL1: .T. activa los campos para edicion                   
Ĵ
 Uso       CTBA092                                                    
ٱ


*/

Function CTB92M()
If !Altera
	lModifica := .T.
endif
Return (lModifica)

/*


Ŀ
Funcion   CTB92ADVPL Autor Miguel Angel. Rojas G.  Data 11/12/2013
Ĵ
Desc.     Genera la Sintaxis en ADVPL dependiendo del valor CWN_TIPO  
          1 xMoeda, 2 Posicione                                       
Ĵ
Sintaxe    CTB92ADVPL(ExpC1)                                             
Ĵ
Parametros ExpC1 = Tipo de Funcion a realizar                         
                                                                      
Ĵ
Retorno    ExpC2 = Funcion con sintaxis ADPLV                         
Ĵ
 Uso       CTBA092                                                    
ٱ


*/

Function CTB92ADVPL(cTipo)
Local cFuncion	:= ""
Local nI 		:=1
Local nFin		:=7
Local cPar		:= ""
Local cVar		:= ""
Local cValor	:= ""
Local cAux		:= ""
Local nJ		:=1
		
If cTipo =="1"
	cFuncion := "xMoeda ("					
	For ni := 1 to nFin
		cPar :="CWN_PAR"+Alltrim(Str(nI))
		cAux :=Alltrim(M->&(cPar))
		cVar := ""
		If nI<7
			If Substr(cAux,1,2) $ "M_"
				cVar += CTB92INT(cAux) // Interpretar mnemnico
				cVar += ","
			Else
				cVar += cAux + "," 
			EndIf
		Else
			cVar :=cAux+")"
		endIF
		
		
		cFuncion += cVar
	Next	
Else
	cFuncion	:="Posicione ("
	nFin := 4
	FOR ni:= 1 to nFin
		cPar :="CWN_PAR"+Alltrim(Str(nI))
		DO CASE
			CASE nI 	== 1
				cValor := "T"
				cAux	:= allTrim(CTB92CWJ(cValor,cPar)) 
				cVar := "'"+cAux+"',"
			CASE nI	 == 2
				cAux := Alltrim(M->&(cPar))	
				cVAR := cAux+","	
			CASE nI	 == 3			
				cValor :="C"
				cAux	:= allTrim(CTB92PAR3(cPar))
				cVar 	:= "xFilial("+SubStr(cFuncion,12,4)+"')+"+cAux+","
			CASE nI	 == 4
				cValor :="C"
				cAux	:= allTrim(CTB92CWJ(cValor,cPar))
				cVar := "'"+cAux+"' )"
		ENDCASE
			cFuncion += cVar			
	Next	
EndIf
Return (cFuncion)

/*


Ŀ
Funcion   CTB92CWJ   Autor Miguel Angel. Rojas G.  Data 11/12/2013
Ĵ
Desc.     Busca en la Tabla CWJ el contenido de los campos CWJ_TABLA  
          o CWJ_CAMPO dependiendo del parametro dado                  
Ĵ
Sintaxe    CTB92CWJ(ExpC1,ExpC2)                                         
Ĵ
Parametros ExpC1 = Tipo de dato a retornar Campo o Tabla              
           ExpC2 = Campo a evaluar                                    
Ĵ
Retorno    ExpC3 =  Nombre del campo o Tabla                          
Ĵ
 Uso       Filtro consulta CTB921                                     
ٱ


*/

Function CTB92CWJ(cValor,cPar)
Local cBuscar 	:= Rtrim(M->&(cPar))
DbSelectArea("CWJ")
CWJ ->(DBSETORDER(1)) //CWJ_FILIAL+CWJ_CODMNE	
If CWJ ->(	Dbseek(xFilial("CWJ")+cBuscar))
    If cValor =='T'
 		cBuscar := ALLTRIM(CWJ->CWJ_TABLA)
 	Else
 		cBuscar := ALLTRIM(CWJ->CWJ_CAMPO)
	EndIF
EndIf	
Return (cBuscar)

Static Function CTB92INT(cClave)

	Local aArea := getarea()
	Local cTp	 := ""
	
	dbSelectArea("CWJ")
	CWJ->(dbSetOrder(1))
	If CWJ->(Dbseek(xFilial("CWJ")+cClave))
		cTp := CWJ->CWJ_TIPDAT
		
		Do Case
			Case cTp == "1"
				cRet := CWJ->CWJ_TABLA +"->" + CWJ->CWJ_CAMPO 
			Case cTp == "2"
				cRet := CWJ->TABLA
			Case cTp $ "3|4"
				If cTp == "3"
					cRet :=  POSICIONE("CWK",1,xFilial("CWK")+CWJ->CWJ_DATVIN,"CWK_ADVPL")	
				Else
					cRet :=	CWJ->CWJ_DATVIN		
				EndIf
			Case cTp == "5"  
				cRet := CWJ->CWJ_FUNRPO
			Case cTp == "6"
				cRet := CWJ->CWJ_VALOR
		End Case				
	EndIf	
	RestArea(aArea)
	
Return cRet 
/*


Ŀ
Funcion   CTB92T1T   Autor Miguel Angel. Rojas G.  Data 11/12/2013
Ĵ
Desc.     Valida los valores admitidos en todos los campos parametro  
          1 al 5, cuando CWN_TIPO =1 (xMoeda)                         
Ĵ
Sintaxe    CTB92T1T()                                                    
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno                                                               
Ĵ
 Uso       CTBA092                                                    
ٱ


*/
 
Function CTB92T1T
Local cVar 	:= Upper(&(ReadVar()))
Local cAux		:= ""
Local lEsNum	:= .f.
Local lRet		:= .t.
Local ni		:= 0
Local cValor	:= "C"
Local nVal 	:= Len(rTrim(cVAr))
Local cPar		:=  Alltrim(ReadVar())
	Local cTpMne	:= ""
	Local cTyp	:= "U"
	Local cFor	:= ""
	Local aArea 	:= getArea()

If Substr(cVar,1,2)<>cMnemonico
    if cPar =="M->CWN_PAR4" 
       lEsNum:=.t.
    endif   
 	For ni := 1 to nVal
		 If SubStr(cVar,nI,1) $("1/2/3/4/5/6/7/8/9/0")
	   			lEsNum := .t.
		Else
			lRet := .f.
			ni := nVal
			Help( ,, STR0006,,STR0013,1, 0 )   // Aviso, No es Mnemnico o Numrico				
		EndIf			
	Next
Else
   &(Readvar()):= cVar	
EndIF
	
	cTpMne := POSICIONE("CWJ",1, xFilial("CWJ")+ cVar ,"CWJ_TIPDAT")
	
	If cTpMne $ "1" // tipo Campo
		// si no es mnemnico de tipo funcin
		DbSelectArea("SX3")
		SX3 ->(DBSETORDER(2))  //X3_CAMPO	 
		If !lEsNum
			If cPar =="M->CWN_PAR4" .AND. lRet		
				cPar	:="CWN_PAR4"
				cAux	:= allTrim(CTB92CWJ(cValor,cPar))	 				
				SX3 ->(DbSeek(cAux))
				If SX3->X3_TIPO<>"D"
					lRet := .f.
					Help( ,, STR0006,,STR0016,1, 0 ) // Aviso, Solo acepta Mnemnicos de tipo DATE
				EndIf
			Else
				If lRet
					cPar	:=SubStr(cPar,4,8)
					cAux	:= allTrim(CTB92CWJ(cValor,cPar))	 		
					SX3 ->(DbSeek(cAux))
					If SX3->X3_TIPO<>"N"
						lRet := .f.
						Help( ,, STR0006,,STR0017,1, 0 )  // Aviso,El Mnemonico seleccionado no es de tipo Numero
					EndIf			
				EndIf		
			EndIf
	ElseIf cTpMne == "3" // Tipo Frmula
		cFor := POSICIONE("CWJ",1, xFilial("CWJ")+ cVar, "CWJ_DATVIN")
		cTyp := POSICIONE("CWK",1, xFilial("CWK")+ cFor, "CWK_TIPDAT")
		
		if cTyp $ "1"
			lRet := .T.
		Else
			lRet := .f.
			Help( ,, STR0006,,STR0017,1, 0 )  // Aviso,El Mnemonico seleccionado no es de tipo Numero
		EndIF
	ElseIf cTpMne $ "2" // Tipo Tabla
		lRet := .f.
		Help( ,, STR0006,,STR0017,1, 0 )  // Aviso,El Mnemonico seleccionado no es de tipo Numero
	ElseIf !Empty(cTpMen) // Cualquier otro tipo de menmnico
		lRet := .T.
	EndIf
	
	EndIf
If lEsNum .AND. cPar =="M->CWN_PAR4"
    lRet := .F.
    Help( ,, STR0006,,STR0016,1, 0 )   //Aviso, Solo acepta Mnemonicos de tipo DATE 
EndIf 

Return(lRet)		

/*


Ŀ
Funcion   CTB92T21   Autor Miguel Angel Rojas G.   Data 11/12/2013
Ĵ
Desc.     Valida los valores admitidos en el campo parametro 1 y 4    
          cuando CWN_TIPO =2 (xPosicione)                             
Ĵ
Sintaxe    CTB92T21()                                                    
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno    ExpL1 = .t. Valida si no, manda un mensaje de error        
Ĵ
 Uso       TABLA CWN Validacion CWN_PAR1 y CWN_PAR4                   
ٱ


*/

Function CTB92T21
Local oModel := FWModelActive()
Local cVar := Alltrim(&(ReadVar()))
Local lRet	:= .t.
Local cPar	:= Alltrim(ReadVar())
Local cTa2 :=""

If Substr(cVar,1,2)<>cMnemonico
	DO CASE
	   CASE cPar == "M->CWN_PAR1"
	      	DbSelectArea("SX2")
			SX2 ->(DBSETORDER(1))		//X2_CHAVE
			If !SX2 ->(DbSeek(cVar))
				lRet := .f.
				Help( ,, STR0006,,STR0024+cVar+STR0022,1, 0 ) //Aviso, No existe la tabla: , En el diccionario de datos
			EndIf			
	   CASE cPar == "M->CWN_PAR4"   		 
	   		lRet:=CTB92BC(cVar)	   					
	EndCase
Else
	If !CWJ ->(Dbseek(xFilial("CWJ")+cVar))
		lRet := .f.
		Help( ,, STR0006,,STR0014,1, 0 )	//Aviso, No es Mnemonico
	Else
	   	If CWJ->CWJ_TIPDAT !='2' .AND. cPar =="M->CWN_PAR1"
	   	   Help( ,, STR0006,,STR0026,1, 0 )	//Aviso, El Mnemonico no es tipo Tabla
	   	   lRet := .F.                        //La validacion es solo para el parametro 1
	   	EndIf   
	EndIf
EndIF

If cPar == "M->CWN_PAR1"
	oModel:LoadValue("FUNCIONES","CWN_PAR2","")
	oModel:LoadValue("FUNCIONES","CWN_PAR4","")
EndIF	
Return(lRet)	

/*


Ŀ
Funcion   CTB92T22   Autor Miguel Angel Rojas G.   Data 11/12/2013
Ĵ
Desc.     Valida los valores admitidos en el campo parametro 2        
          cuando CWN_TIPO =2 (xPosicione)                             
Ĵ
Sintaxe    CTB92T22()                                                    
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno    ExpL1 = .t. Valida si no, manda un mensaje de error        
Ĵ
 Uso       TABLA CWN Validacion CWN_PAR2                              
ٱ


*/

Function CTB92T22
Local cVar 	:= Alltrim(&(ReadVar()))
Local lRet		:= .f.
Local cValor	:= "T"
Local cPar 	:= "CWN_PAR1" 
Local cTabSix	:= ""
 //---Llama a la funcion para obtener el nombre de la tabla.
If !Empty(Rtrim(M->&(cPar)))
    cTabSix := CTB92CWJ(cValor,cPar)
	//---Seleccionamos la tabla SIX para buscar los ndices de las tablas.
 	DbSelectArea("SIX")
	SIX->(DbSeek(cTabSix))
 	While !Eof() .And. (SIX->INDICE == cTabSix)
  		If cVar == Alltrim(SIX->ORDEM)
  		    lRet := .t.
  		EndIf    
  		SIX->(DbSkip())
  	Enddo
Else
	lRet:= .t.  	
EndIF
If !lRet		
	Help( ,, STR0006,,STR0015,1, 0 )  //Aviso, El indice no existe en la tabla SIX 
EndIF
Return(lRet)	

/*


Ŀ
Funcion   CTB92T23   Autor Miguel Angel  Rojas G.  Data 11/12/2013
Ĵ
Desc.     Valida los valores admitidos en el campo parametro 3        
          cuando CWN_TIPO =2 (xPosicione)                             
Ĵ
Sintaxe    CTB92T23()                                                    
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno    ExpL1 = .t. Valida si no, manda un mensaje de error        
Ĵ
 Uso       X3_VALID del campo CWN_PAR3                                
ٱ


*/
Function CTB92T23
Local lRet		:= .t.	
Local cCadena	:= ""
Local nPos		:= 1
Local nI		:= 1
Local nTam		:= TamSX3("CWN_PAR3")[1]
Local nMenm	:= TamSX3("CWJ_CODMNE")[1]
Local nCampo	:= 10
Local nLen 	:= 0
Local nCant	:= 0
Local oModel 	:= FWModelActive()
Local cVarMem	:= "M->"
Local oView 	:= FWViewActive()
Local cVar		:=""
Local aValores:={}
Local cValor:=''


DbSelectArea("CWJ")
CWJ ->(DBSETORDER(1))    // CWJ_FILIAL+CWJ_CODMNE
cVar:=alltrim(&(readvar()))
aValores:=separa(cVar,"+")
For ni:= 1 to len(aValores)
   
    if !empty(aValores[ni])
	    if substr(aValores[ni],1,2)==cMnemonico
	    	cValor:=padr(aValores[ni],nMenm)
	    	If !CWJ ->(	Dbseek(xFilial("CWJ")+cValor))
	    		Help( ,, STR0006,,STR0014+": "+cValor,1, 0 ) // Aviso, No es Mnemonico
	    		lRet:= .f.
	    	endif
	    else
	    	if substr(aValores[ni],1,3)==cVarMem //Si es variable de memoria
	    		cValor:=padr(substr(aValores[ni],4,len(aValores[ni])),10)
	    	   	lRet := CTB92BC(cValor) //busca el campo en sx3
	    	   	If !lRet
	    	   		Help( ,, STR0006,,STR0021+cValor+STR0022,1, 0 )   // Aviso, No existe el campo: , En el diccionario de datos
	    	   	EndIf	
	    	else   	
	    		cValor :=padr(substr(aValores[ni],1,len(aValores[ni])),10)
	    		lRet := CTB92BC(cValor)
	    		If !lRet
	    	   		Help( ,, STR0006,,STR0021+cValor+STR0022,1, 0 )   // Aviso, No existe el campo: , En el diccionario de datos
	    	   	EndIf
	    	endif
	    endif	   	
    endif
Next
if !lRet
	M->CWN_PAR3:=SPACE(ntam)
endif

Return (lRet)

/*


Ŀ
Funcion   CTB92CWJFUN   Autor Miguel A. Rojas G.   Data 11/12/2013
Ĵ
Desc.     Filtra los valores a seleccionar en la consulta CWJ         
                                                                      
Ĵ
Sintaxe    CTB92CWJFUN()                                                 
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno    ExpC1 = Cadena a evaluar en  filtro consulta CWJ           
Ĵ
 Uso       Filtro para la Consulta CWJ                                
ٱ


*/
Function CTB92CWJFUN()
Local cVar 	:= ReadVar()
Local cFiltro  := "CWJ->CWJ_TIPDAT=='1'"
Local cPar		:='T'
If cVar == "M->CWN_PAR4"
    IF Empty(cTabla)  
        cTabla := rtrim(M->CWN_PAR1)
        If SubStr(cTabla,1,2)==cMnemonico
           cTabla := CTB92CWJ(cPar,cVar)
        EndiF
	 Endif
	cFiltro += " .AND. CWJ->CWJ_TABLA =='"+cTabla+"'"
EndIf	
Return(cFiltro)
/*


Ŀ
Funcion   CTB92TIPDAT   Autor Miguel A. Rojas G.   Data 11/12/2013
Ĵ
Desc.     Hace editable o no el campo CWN_TIPDAT dependiendo del      
          valor contenido en CWN_TIPO                                 
Ĵ
Sintaxe    CTB92TIPDAT()                                                
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno    ExpL1 = .T. Permite editar .F. Bloquea                     
Ĵ
 Uso       Filtro para la Consulta CWJ                                
ٱ


*/

Function CTB92TIPDAT()
Local cTipo 	:= getMemvar("CWN_TIPO") 
Local oView 	:= FWViewActive()
Local lRet		:= .T.

If oView!=Nil
 	   If cTipo =="1"
 	   		lRet := .F.
 	   Else
 	       lRet := .T.
 	       CTB92M()
 	  EndIF    
EndIf
Return lRet

/*


Ŀ
Funcion   CTB92PAR3     Autor Miguel A. Rojas G.   Data 20/12/2013
Ĵ
Desc.     Llena el campo CWN_PAR3 si es Posicione                     
                                                                      
Ĵ
Sintaxe    CTB92PAR3(ExpC2)                                              
Ĵ
Parametros ExpC1 = Contiene El nombre del campo a evaluar             
                                                                      
Ĵ
Retorno    ExpL1 = .T. Permite editar .F. Bloquea                     
Ĵ
 Uso                                                                  
ٱ


*/

Function CTB92PAR3(cPar)
Local oModel 	:= FWModelActive()
Local cVar	:= Rtrim(M->&(cPar))
Local nI	:= 1
Local nLen	:= Len(cVar)
Local nPoS := 1
Local lRet := .f.
Local cBuscar		:= ""
Local cFuncion		:= ""
Local claTabla	 	:= ""
Local nCantidad 	:= 0
Local nPar3		:= TamSX3("CWN_PAR3")[1]
Local cMemVar		:= "M->"
Local cAux			:= ""

If nPar3 == nLen
	cVar := cVar+"+" 
	nLen := Len(cVar)
EndIF

For ni := 1 to nLen
    If SubStr(cVar,nI,1) == "+"
        nCantidad := nI-nPos
        cBuscar := SubStr(cVar,nPos,nCantidad)
        lRet:= .t.
        nPos := ni+1
    EndIF
   
    If lRet
    	cAux := cBuscar    
		If Substr(cBuscar,1,2)==cMnemonico
			DbSelectArea("CWJ")
			CWJ ->(DBSETORDER(1))		//CWJ_FILIAL+CWJ_CODMNE
			CWJ ->(	Dbseek(xFilial("CWJ")+cBuscar))
		    cBuscar := ALLTRIM(CWJ->CWJ_CAMPO)			
		Else
			If Substr(cBuscar,1,3)==cMemVar
				cBuscar := SubStr(cAux,4,Len(cAux))
			EndIF 
		EndIf		
		
		DbSelectArea("SX3")
		SX3 ->(DBSETORDER(2))	 //X3_CAMPO	
		SX3 ->(DbSeek(cBuscar))
		claTabla := SX3->X3_ARQUIVO
		If SX3->X3_TIPO=="D"
		   If SubStr(cAux,1,3) == cMemVar
		   		cBuscar := "DTOS("+cAux+")"
		   Else
		   		cBuscar :="DTOS("+claTabla+"->"+cBuscar+")"
		   EndIF		
		Else
		  If SubStr(cAux,1,3) <> cMemVar
		      cBuscar := claTabla+"->"+cBuscar
		  ELse
		      cBuscar := cAux
		  EndIf
		      	
		EndIf
		If SubStr(cVar,nI,1) =="+" .AND. nI<nLen 
		    cBuscar += "+"
		EndIf      
		
		cFuncion += cBuscar
		lRet := .f.	
		
	EndIF
Next
Return (cFuncion)

/*


Ŀ
Funcion   CTB92BC       Autor Miguel A. Rojas G.   Data 20/12/2013
Ĵ
Desc.     Busca en la SX3 si existe el campo indicado                 
                                                                      
Ĵ
Sintaxe    CTB92BC(ExpC1)                                                
Ĵ
Parametros ExpC1 = Contiene El nombre del campo a buscar              
                                                                      
Ĵ
Retorno    ExpL1 = .T. si lo encontro .F. si no                       
Ĵ
 Uso       CTBA094                                                    
ٱ


*/

Function CTB92BC(cCampo)
Local lEncuentra := .T.
DbSelectArea("SX3")
SX3 ->(DBSETORDER(2))   //X3_CAMPO
If !SX3 ->(DbSeek(cCampo))
	lEncuentra := .f.
	Help( ,, STR0006,,STR0021+cCampo+STR0022,1, 0 )   // Aviso, No existe el campo: , En el diccionario de datos
EndIf
Return lEncuentra


/*


Ŀ
Funcion   CBT92CL       Autor Miguel A. Rojas G.   Data 20/12/2013
Ĵ
Desc.     Limpia los campos de parametros si se hace un cambio        
          en el tipo de funcion                                       
Ĵ
Sintaxe    CBT92CL()                                                     
Ĵ
Parametros                                                            
                                                                      
Ĵ
Retorno    ExpL1 = .T. si limpia los campos                           
Ĵ
 Uso       Validacion en el campo CWN_TIPO                            
ٱ


*/

Function CBT92CL()
Local oModel := FWModelActive()
Local lRet := .t.
Local cTipo  := M->CWN_TIPO

If Inclui
	oModel:LoadValue("FUNCIONES","CWN_PAR1","")
	oModel:LoadValue("FUNCIONES","CWN_PAR2","")
	oModel:LoadValue("FUNCIONES","CWN_PAR3","")
	oModel:LoadValue("FUNCIONES","CWN_PAR4","")
	oModel:LoadValue("FUNCIONES","CWN_PAR5","")
	if cTipo =="1"
		oModel:LoadValue("FUNCIONES","CWN_TIPDAT","1")
	EndIf
EndIF

Return lRet

/*


Ŀ
Funcion   CTB92GD    Autor  Miguel Angel. Rojas G  Data 11/12/2013
Ĵ
Desc.     Si la validacion es correcta lGuarda =.t. guarda la funcion 
          en codigo ADVPL en el campo CWN_ADVPL                       
Ĵ
Sintaxe    CTB92GD(ExpO1)                                                
Ĵ
Parametros ExpO1 = Objeto Vista                                       
                                                                      
Ĵ
Retorno                                                               
Ĵ
 Uso       CTBA092                                                    
ٱ


*/

Function CTB92GD(oView)
Local cTipo
Local cVar 

If Altera .OR. Inclui
	//--- Identifica el tipo de funcion 1 o 2
	cTipo := (M->CWN_TIPO)
	//--- Guarda el valor de CODFUN para buscarlo en la tabla CWN
	cVar := GetMemVar("CWN_CODFUN")  
	If lGuarda    //--- Si la validacion es correcta guarda la funcion
		cFuncion:= CTB92ADVPL(cTipo)
		DbSelectArea("CWN") 
		IF CWN->(DBSeek(xFilial("CWN")+cVar))			
			//---BLOQUEA EL REGISTRO
		   	RECLOCK("CWN",.F.)
		    //---ACTUALIZA INFORMACION     				
		    CWN->CWN_ADVPL := cFuncion		    
		    If CWN->CWN_TIPO =="2"
		       CWN->CWN_PAR5 := ""
		    ENDIF   
		    //---DESBLOQUEA REGISTRO
		    CWN->(MSUNLOCK())
		  EndIf	
	Endif 
EndIF	        
Return .t.


/*


Ŀ
Funcion   CTB92CR    Autor  Miguel Angel. Rojas G  Data 11/12/2013
Ĵ
Desc.      Bandera para validacion si la funcion esta en uso.         
                                                                      
Ĵ
Sintaxe    CTB92CR(ExpO1)                                                
Ĵ
Parametros ExpO1 = Objeto Vista                                       
                                                                      
Ĵ
Retorno                                                               
Ĵ
 Uso       CTBA092                                                    
ٱ


*/

Function CTB92CR(oView)
lValido := .f.	        
Return .t.

