#INCLUDE 'tlpp-core.th'

NAMESPACE totvs.protheus.backoffice.fin.general

class FlowControl
	public method new() constructor    
	
	static method newException() as object
	static method setRestError(oError as object)
	static method execSql(cCommandSql as character)
	static method logErrorMessage(oError as object, cGroupLogMsg as character , cCategoryLogMsg as character )
	
endClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
	@author guilherme.sordi@totvs.com.br
	@since 08/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method new() Class FlowControl
return Self

//-------------------------------------------------------------------
/*/{Protheus.doc} newException
	@description Cria uma exceção com todos os dados necessários para retornar em uma requisição HTTP.
	@author guilherme.sordi@totvs.com.br
	@since 08/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method newException(cDescription as character, nCode as numeric, cDetailedMessage as character	) as object Class FlowControl
    local oError := ErrorClass():New() as object
	local jError := JsonObject():new() as json
	
	default nCode := 0
	default cDetailedMessage := ""

	if nCode == 0
    	oError:description := cDescription
	else
		jError["code"] := cValToChar(nCode)
		jError["message"] := cDescription
		jError["detailedMessage"] := cDetailedMessage
		oError:description := jError:toJson()
	endIf

return oError

//-------------------------------------------------------------------
/*/{Protheus.doc} setRestError
	@description Passa para o objeto Rest os detalhes do erro recebido no objeto oError (classe ErrorClass)
	@author guilherme.sordi@totvs.com.br
	@since 08/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method setRestError(oError as object) class FlowControl
	local cDescription := oError:description
	local jError := JsonObject():new()
	
	oRest:setKeyHeaderResponse('Content-Type','application/json')	

	if left(cDescription,1) == "{"
		jError:fromJson(cDescription)
		oRest:setResponse(jError)
		oRest:setStatusCode(val(jError['code']))
	else
		oRest:setResponse({"code": "500", "message": "Internal Server Error", "detailedMessage": oError:description})
		oRest:setStatusCode(500)
	endIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} execSql
	@description Executa TcSqlExec e gera newException caso ocorra erro. 
	@author rafael.rondon
	@since 18/09/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method execSql(cCommandSql as character) class FlowControl
	if TcSqlExec(cCommandSql) < 0
		throw FlowControl():newException(TcSqlError())
	endif
return


//-------------------------------------------------------------------
/*/{Protheus.doc} logErrorMessage
	@description Loga mensagem de erro no console
	@author rafael.rondon
	@since 18/09/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method logErrorMessage(oError as object, cGroupLogMsg as character , cCategoryLogMsg as character ) class FlowControl

	local aMessage	:= {} as array

	default cGroupLogMsg 		:= FunName()
	default cCategoryLogMsg 	:= 'FlowControlLogErrorMessage'
	
	aAdd(aMessage, {"Description", oError:description})
	aAdd(aMessage, {"ErrorStack", oError:errorStack})

	FwLogMsg("INFO", /*cTransactionId*/, cGroupLogMsg, cCategoryLogMsg, "", FunName(), '', /*nMensure*/, /*nElapseTime*/, aMessage)

return
