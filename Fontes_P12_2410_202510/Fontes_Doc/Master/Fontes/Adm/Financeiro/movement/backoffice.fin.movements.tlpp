#include 'protheus.ch'
#include 'tlpp-core.th'
#include 'tlpp-rest.th'

namespace totvs.protheus.backoffice.fin.movements

//-------------------------------------------------------------------
/*/{Protheus.doc} Movements
	@author @rodrigo.oliveira
	@since 15/03/2025
	@type class
	@version 12.1.2410
    @see https://tdn.totvs.com.br/pages/viewpage.action?pageId=927532033
/*/
//-------------------------------------------------------------------
class Movements

	public method new(jParam as json) as object
	public method clear()
	public method getWriteOffsByBill() as json
	public method getCompDocumentJsonBySE5(nRecno as numeric) as json
	public method getCompDocumentKeyBySE5(nRecno as numeric) as character
	public method getCompDocumentJson(cIdFk as character, cRecPag as character) as json
	public method getCompDocumentKey(cIdFk as character, cRecPag as character) as character
	public method getIdComp(nRecSE as numeric, cRecPag as character, cSeq as character) as character
	public method setIdDocKey(cChvTit as character, cRecPag as character)
	public method setIdDocRec(nRecSE as numeric, cAls as character)
	public method setFullBillKey(lFullKey as logical)

	private method setParameters(jParam as json)
	private method setPayReceivable(cRecPag as character)
	private method getWriteOffsByBillStatement() as object 
	private method getCompStatement(cIdFK as character) as object
	private method getIdCompStatement(cSeq as character) as object	
	private method getDateFilter() as character
	private method aliastodata() as json

	private data oStWriteOffsByBill as object
	private data oStComp as object
	private data oStIdComp as object	
	private data jGetPost as json
	private data jGetComp as json
	private data jGetIdComp as json

	protected data jParameters as json
	protected data cIdDoc as character
	protected data cPayReceivable as character
	protected data lFullBillKey as logical
endClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
	@author @rodrigo.oliveira
	@since 21/03/2025
	@type method
	@version 12.1.2410

	@param jParameters, Json, parâmetro com informações para filtro
		das informações

		Estrutura do jParameters
			jParameters['branch']         	
			jParameters['initialDate']      
			jParameters['finalDate']        
			jParameters['filterOrigBranch'] 
			jParameters['removeReversed'] 	
			jParameters['lcompensated']		
			jParameters['ldateAvailability']
			jParameters['addFilterQuery']
/*/
//-------------------------------------------------------------------
method new(jParameters as json) as object class Movements
	self:clear()
	If ValType(jParameters) == "J"
		self:setParameters(jParameters)
	EndIf
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} clear
	@author @rodrigo.oliveira
	@since 21/03/2025
	@type method
	@version 12.1.2410
/*/
//-------------------------------------------------------------------
method clear() class Movements
	If self:oStWriteOffsByBill != Nil
		FreeObj(self:oStWriteOffsByBill)
	EndIf
	If self:oStComp	!= Nil
		FreeObj(self:oStComp)
	EndIf
	If self:oStIdComp != Nil
		FreeObj(self:oStIdComp)
	EndIf
	self:jParameters := JsonObject():New()
	self:jGetPost := JsonObject():New()
	self:jGetComp := JsonObject():New()
	self:jGetIdComp := JsonObject():New()
	self:cIdDoc	:= ""
	self:cPayReceivable	:= ""
	self:lFullBillKey := .F.
return

//-------------------------------------------------------------------
/*/{Protheus.doc} setIdDocRec

 	Define o ID da FK7 a partir do título posicionado
	ou do RECNO das tabelas SE1, SE2 ou SE5

	@author @rodrigo.oliveira
	@since 21/03/2025
	@type method
	@version 12.1.2410
	@param nRecno, Numeric, RECNO do título / movimentação posicionado
	@param cAls, Character, identificador do Alias do registro
/*/
//-------------------------------------------------------------------
method setIdDocRec(nRecno as numeric, cAls as character) class Movements
	Local aAreaSE := {} as array
	Local aAreaAtu := {} as array
	Local cChaveFK7	:= '' as character
	Local cRecPag := 'P' as character

	Default nRecno 	:= 0
	Default cAls	:= 'SE5'
   
	If !Empty(nRecno)
		aAreaAtu := FwGetArea()
		aAreaSE	:= (cAls)->(FwGetArea())
		(cAls)->(DbGoTo(nRecno))
	EndIf

	If cAls == 'SE5'
		cChaveFK7	:= xFilial("SE5", SE5->E5_FILORIG) + '|' + SE5->E5_PREFIXO + '|' + SE5->E5_NUMERO + '|' + SE5->E5_PARCELA + '|' + SE5->E5_TIPO + '|' + SE5->E5_CLIFOR + '|' + SE5->E5_LOJA
		If SE5->E5_TABORI == 'FK1'
			cRecPag	:= 'R'
		EndIf
	ElseIf cAls == 'SE2'
		cChaveFK7	:= SE2->E2_FILIAL + '|' + SE2->E2_PREFIXO + '|' + SE2->E2_NUM + '|' + SE2->E2_PARCELA + '|' + SE2->E2_TIPO + '|' + SE2->E2_FORNECE + '|' + SE2->E2_LOJA
	ElseIf cAls == 'SE1'
		cChaveFK7	:=  SE1->E1_FILIAL + '|' + SE1->E1_PREFIXO + '|' + SE1->E1_NUM + '|' + SE1->E1_PARCELA + '|' + SE1->E1_TIPO + '|' + SE1->E1_CLIENTE + '|' + SE1->E1_LOJA
		cRecPag	:= 'R'
	EndIf
	
	self:setIdDocKey(cChaveFK7, cRecPag)
	
	If !Empty(nRecno)
		FwRestArea(aAreaSE)
		FwRestArea(aAreaAtu)
		FwFreeArray(aAreaSE)
		FwFreeArray(aAreaAtu)
	EndIf
return

/*/{Protheus.doc} setIdDocKey()

	Define o ID da FK7 do título atual pela chave do título

	@author @rodrigo.oliveira
	@since 21/03/2025
	@type method
	@version 12.1.2410
	@param cChaveFK7, Character, Chave do titulo no formato FK7_CHAVE
	@param cRecPag, Character, identificador da carteira do título posicionado
/*/
Method setIdDocKey(cChaveFK7 as character, cRecPag as character) Class Movements
	Local cAls	:= 'SE1' as character

	default cChaveFK7 := ""
	default cRecPag := "R"

    If cRecPag == 'P'
		cAls	:= 'SE2'
	EndIf
	
	self:setPayReceivable(cRecPag)
	::cIdDoc    := FINGRVFK7(cAls, cChaveFK7)
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} setPayReceivable
	@author @rodrigo.oliveira
	@since 21/03/2025
	@type method
	@version 12.1.2410
	@param cRecPag, Character, identificador da carteira do título posicionado
/*/
//-------------------------------------------------------------------
method setPayReceivable(cRecPag as character) class Movements
	default cRecPag := "R"
	::cPayReceivable	:= cRecPag
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} setFullBillKey

	Set no atributo 'lFullBillKey' para determinar 
	como deve ser composta a chave do título a receber.
	Pode ser a chave completa (prefixo+numero+parcela+tipo+clifor+loja) 
	ou respeitando a chave-legado do E5_DOCUMEN da carteira a 
	receber (sem o clifor).

	@author @fabio.casagrande
	@since 28/04/2025
	@type method
	@version 12.1.2410
	@param lFullKey, logical, .T. = chave completa/.F. = chave legado (E5_DOCUMEN)
/*/
//-------------------------------------------------------------------
method setFullBillKey(lFullKey as logical) class Movements
	default lFullKey := .T.
	::lFullBillKey	:= lFullKey
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getWriteOffsByBill

	Retorna dados da(s) baixa(s) de um titulo a receber via JSON

	@author @rodrigo.oliveira
	@since 21/03/2025
	@type method
	@version 12.1.2410
	@return jResult, Json, Array de retorno das informações do(s) título(s)

	Estrutura do jParameters:
		jResult['document'][n]{
			['FK7_FILIAL']       //Filial do título
			['FK7_PREFIX']     //Numero do título
			['FK7_NUM']        //Parcela do título
			['FK7_PARCEL']    //Parcela do título
			['FK7_TIPO']        //Tipo do título
			['FK7_CLIFOR']    //Cliente/Fornecedor do título
			['FK7_LOJA']        //Loja
			['FK7_IDFK7']      //ID do título
			['E5_DATA']         //Data da baixa
			['E5_DTDISPO']   //Data da disponibilidade
			['E5_TIPODOC']  //Tipo da movimentação
			['E5_MOTBX']     //Motivo da baixa
			['E5_MOEDA']    //Moeda da baixa
			['E5_VALOR']     //Valor da baixa
			['E5_SEQ']         //Sequencia da baixa
			['FK_IDFK']        //Identificador da baixa
			['FK_IDCOMP'] } //Identificador da compensação

/*/
//-------------------------------------------------------------------
method getWriteOffsByBill() as json class Movements   
    local jResult 	:= JsonObject():new() as json
    local cAlias 	:= '' as character   

    cAlias := self:getWriteOffsByBillStatement():openAlias()
    jResult := self:aliastoData(cAlias)
    (cAlias)->(dbCloseArea())

return jResult

//-------------------------------------------------------------------
/*/{Protheus.doc} getCompDocumentJsonBySE5
    Recebe o RECNO da SE5 (ou o registro da SE5 posicionado) e
    retorna os dados do título de contrapartida da compensação em um JSON

	@author @rodrigo.oliveira
	@since 21/03/2025
	@type method
	@version 12.1.2410
	@param nRecno, Numeric, RECNO da movimentação (SE5) posicionado
/*/
//-------------------------------------------------------------------
method getCompDocumentJsonBySE5(nRecno as numeric) as json class Movements
    local jResult := JsonObject():new() as json
	local aAreaAtu as array
	local aAreaSE5 as array

	default nRecno := 0
    
	If !Empty(nRecno)
		aAreaAtu := FwGetArea()
		aAreaSE5 := SE5->(FwGetArea())
		SE5->(DbGoTo(nRecno))
	EndIf

	::setPayReceivable(if(SE5->E5_TABORI == 'FK2', 'P', 'R'))

	jResult	:= ::getCompDocumentJson(SE5->E5_IDORIG, ::cPayReceivable)

	If !Empty(nRecno)
		FwRestArea(aAreaSE5)
		FwRestArea(aAreaAtu)
		FwFreeArray(aAreaSE5)
		FwFreeArray(aAreaAtu)
	EndIf
return jResult

//-------------------------------------------------------------------
/*/{Protheus.doc} getCompDocumentJson
    Recebe o IDFK e retorna os dados do título de
	contrapartida da compensação em um JSON

	@author @rodrigo.oliveira
	@since 21/03/2025
	@type method
	@version 12.1.2410
	@param cIdFK, Character, Id da FK1 / FK2 da movimentação posicionada
	@param cRecPag, Character, Carteira (R=Receber/P=Pagar)
/*/
//-------------------------------------------------------------------
method getCompDocumentJson(cIdFk as character, cRecPag as character) as json class Movements
    local jResult := JsonObject():new() as json
    local cAlias as character

	default cIdFk := ""
	default cRecPag := "R"

	::setPayReceivable(cRecPag)

    cAlias := self:getCompStatement(cIdFk):openAlias()
    jResult := self:aliastoData(cAlias, .T.)
    (cAlias)->(dbCloseArea())

return jResult

//-------------------------------------------------------------------
/*/{Protheus.doc} getCompDocumentKey
    Recebe o IDFK1/IDFK2 da movimentação e retorna os dados do título de
	contrapartida em ums string no formato de pesquisa da tabela (SE1/SE2)

	@author @rodrigo.oliveira
	@since 21/03/2025
	@type method
	@version 12.1.2410

	@param cIdFK, Character, Id da FK1 / FK2 da movimentação posicionada
	@param cRecPag, Character, Carteira (R=Receber/P=Pagar)

	@return cDocumentKey, Character, Chave do título compensado
/*/
//-------------------------------------------------------------------
method getCompDocumentKey(cIdFK as character, cRecPag as character) as character class Movements
	local cDocumentKey as character
	local cAlias as character

	default cIdFk := ""
	default cRecPag := "R"

	::setPayReceivable(cRecPag)

	cAlias := self:getCompStatement(cIdFk):openAlias()

	cDocumentKey := (cAlias)->FK7_PREFIX + (cAlias)->FK7_NUM + (cAlias)->FK7_PARCEL + (cAlias)->FK7_TIPO
	If ::cPayReceivable == 'P' .Or. ::lFullBillKey
		cDocumentKey += (cAlias)->FK7_CLIFOR
	EndIf
	cDocumentKey += (cAlias)->FK7_LOJA

	(cAlias)->(dbCloseArea())

return cDocumentKey

//-------------------------------------------------------------------
/*/{Protheus.doc} getCompDocumentKeyBySE5
    Recebe o RECNO da SE5 (ou o registro da SE5 posicionado) e
    retorna os dados do título de contrapartida no formado E5_DOCUMEN
    Carteira a Receber: PREFIXO+NUMERO+PARCELA+TIPO+LOJA
	Carteira a PAGAR: PREFIXO+NUMERO+PARCELA+TIPO+FORNECEDOR+LOJA

	@author @rodrigo.oliveira
	@since 21/03/2025
	@type method
	@version 12.1.2410
	@param nRecno, Numeric, RECNO do título / movimentação posicionado
/*/
//-------------------------------------------------------------------
method getCompDocumentKeyBySE5(nRecno as numeric) as character class Movements
	local cDocumentKey as character
	local aAreaAtu as array
	local aAreaSE5 as array

	default nRecno := 0
    
	If !Empty(nRecno)
		aAreaAtu := FwGetArea()
		aAreaSE5 := SE5->(FwGetArea())
		SE5->(DbGoTo(nRecno))
	EndIf

	::setPayReceivable(if(SE5->E5_TABORI == 'FK2', 'P', 'R'))

	cDocumentKey	:= self:getCompDocumentKey(SE5->E5_IDORIG, ::cPayReceivable)

	If !Empty(nRecno)
		FwRestArea(aAreaSE5)
		FwRestArea(aAreaAtu)
		FwFreeArray(aAreaSE5)
		FwFreeArray(aAreaAtu)
	EndIf
	
return cDocumentKey

//-------------------------------------------------------------------
/*/{Protheus.doc} getWriteOffsByBillStatement

	Executa query do metodo 'getWriteOffsByBill'

	@author @rodrigo.oliveira
	@since 21/03/2025
	@type method
	@version 12.1.2410
/*/
//-------------------------------------------------------------------
method getWriteOffsByBillStatement() as object class Movements
    local cQuery := "" as character
    local nParam := 1 as numeric
	local cChaveJson := "" as character
	local cAls	:= "" as character
	   
	cChaveJson	:= ::cPayReceivable+cValToChar(::jParameters['lcompensated'])+cValToChar(::jParameters['filterOrigBranch'])+;
					cValToChar(::jParameters['initialDate'])+cValToChar(::jParameters['finalDate'])+cValToChar(::jParameters['ldateAvailability'])

	if self:oStWriteOffsByBill == NIL .Or. !(self:jGetPost:HasProperty(cChaveJson))
		//Remove a propriedade anterior do JSON e insere a atual
		If Len(self:jGetPost:GetNames()) > 0
			self:jGetPost:DelName(self:jGetPost:GetNames()[1])
		EndIf
		self:jGetPost[cChaveJson] := .T.

		cAls	:= 'FK2'

		If ::cPayReceivable == 'P'
			cQuery	:= "Select FK2.FK2_IDFK2 IDFK, FK2.FK2_IDCOMP IDCOMP, FK2.FK2_FILORI FILORI, "
			cQuery	+= "FK7.FK7_FILTIT, FK7.FK7_PREFIX, FK7.FK7_NUM, FK7.FK7_PARCEL, FK7.FK7_TIPO, FK7.FK7_CLIFOR, FK7.FK7_LOJA, FK7.FK7_IDDOC, "
			cQuery	+= "FK2.FK2_DATA DATABX, FK2.FK2_DTDISP DTDISP, FK2.FK2_DOC DOC, FK2.FK2_TPDOC TPDOC, FK2.FK2_VALOR VAL, "
			cQuery	+= "FK2.FK2_MOEDA MOEDA, FK2.FK2_VLMOE2 VLMOE2, FK2.FK2_MOTBX MOTBX, FK2.FK2_SEQ SEQBX "
			cQuery 	+= "From " + RetSqlName('FK2') + " FK2 "
			cQuery 	+= "Inner Join " + RetSqlName('FK7') + " FK7 "
			cQuery 	+= "On FK2_IDDOC = FK7.FK7_IDDOC "
			cQuery 	+= "And FK7.D_E_L_E_T_ = ? " //#1
			If ::jParameters['removeReversed']
				cQuery	+= "Left Join " + RetSqlName(cAls) + " FK2EST "
				If ::jParameters['filterOrigBranch']
					cQuery += "On FK2EST.FK2_FILORI = FK2.FK2_FILORI"
				Else
					cQuery += "On FK2EST.FK2_FILIAL = FK2.FK2_FILIAL"		
				EndIf
				cQuery	+= " 	And FK2EST.FK2_IDDOC = FK2.FK2_IDDOC"
				cQuery	+= " 	And FK2EST.FK2_SEQ = FK2.FK2_SEQ"
				cQuery	+= " 	And FK2EST.FK2_TPDOC = ?"	//#2
				cQuery	+= "	And FK2EST.D_E_L_E_T_ = ?"	//#3
			EndIf
			If ::jParameters['filterOrigBranch']
				cQuery += " WHERE FK2.FK2_FILORI = ? " //#4
			Else
				cQuery += " WHERE FK2.FK2_FILIAL = ? " //#4			
			EndIf
			cQuery	+= "  And FK2.FK2_IDDOC = ?"	//#5
			If ::jParameters['lcompensated']
				cQuery	+= " And FK2.FK2_MOTBX = ?" //#6
			EndIf
			If ::jParameters:HasProperty('initialDate') .And. !Empty(::jParameters['initialDate'])
				cQuery	+= "? "		//#7
			EndIf
			cQuery	+= " And FK2.D_E_L_E_T_ = ? "	//#8
			If ::jParameters:HasProperty('addFilterQuery') .And. !Empty(::jParameters['addFilterQuery'])
				cQuery	+= "? "      				//#9
			EndIf
			If ::jParameters['removeReversed']
				cQuery	+= "  	And FK2EST.FK2_SEQ IS NULL"
			EndIf
			cQuery	+= " ORDER BY FK2.FK2_IDDOC, FK2.FK2_SEQ"
		Else
			cAls	:= 'FK1'

			cQuery	:= "Select FK1.FK1_IDFK1 IDFK, FK1.FK1_IDCOMP IDCOMP, FK1.FK1_FILORI FILORI, "
			cQuery	+= "FK7.FK7_FILTIT, FK7.FK7_PREFIX, FK7.FK7_NUM, FK7.FK7_PARCEL, FK7.FK7_TIPO, FK7.FK7_CLIFOR, FK7.FK7_LOJA, FK7.FK7_IDDOC, "
			cQuery	+= "FK1.FK1_DATA DATABX, FK1.FK1_DTDISP DTDISP, FK1.FK1_DOC DOC, FK1.FK1_TPDOC TPDOC, FK1.FK1_VALOR VAL, "
			cQuery	+= "FK1.FK1_MOEDA MOEDA, FK1.FK1_VLMOE2 VLMOE2, FK1.FK1_MOTBX MOTBX, FK1.FK1_SEQ SEQBX "
			cQuery	+= "From " + RetSqlName("FK1") + " FK1 "
			cQuery 	+= "Inner Join " + RetSqlName('FK7') + " FK7 "
			cQuery 	+= "On FK1_IDDOC = FK7_IDDOC "
			cQuery 	+= "And FK7.D_E_L_E_T_ = ? " //#1
			
			If ::jParameters['removeReversed']
				cQuery	+= "Left Join " + RetSqlName(cAls) + " FK1EST "
				If ::jParameters['filterOrigBranch']
					cQuery	+= "	On FK1EST.FK1_FILORI = FK1.FK1_FILORI"
				Else
					cQuery	+= "	On FK1EST.FK1_FILIAL = FK1.FK1_FILIAL"
				EndIf
				cQuery	+= " 	And FK1EST.FK1_IDDOC = FK1.FK1_IDDOC"
				cQuery	+= " 	And FK1EST.FK1_SEQ = FK1.FK1_SEQ"
				cQuery	+= " 	And FK1EST.FK1_TPDOC = ?"	//#2
				cQuery	+= "	And FK1EST.D_E_L_E_T_ = ?"	//#3
			EndIf
			If ::jParameters['filterOrigBranch']
				cQuery += " WHERE FK1.FK1_FILORI = ? " //#4
			Else
				cQuery += " WHERE FK1.FK1_FILIAL = ? " //#4	
			EndIf
			cQuery	+= " And FK1.FK1_IDDOC = ?"	//#5
			If ::jParameters['lcompensated']
				cQuery	+= " And FK1.FK1_MOTBX = ?" //#6
			EndIf
			If ::jParameters:HasProperty('initialDate') .And. !Empty(::jParameters['initialDate'])
				cQuery	+= "? "		//#7
			EndIf
			cQuery	+= " And FK1.D_E_L_E_T_ = ? "	//#8
			If ::jParameters:HasProperty('addFilterQuery') .And. !Empty(::jParameters['addFilterQuery'])
				cQuery += "? "      				//#9
			EndIf
			If ::jParameters['removeReversed']
				cQuery	+= "  	And FK1EST.FK1_SEQ IS NULL"
			EndIf
			cQuery	+= " ORDER BY FK1.FK1_IDDOC, FK1.FK1_SEQ"
		EndIf

		cQuery	:= ChangeQuery(cQuery)
		self:oStWriteOffsByBill := FWExecStatement():new(cQuery)  

	endIf

	self:oStWriteOffsByBill:SetString(nParam++, ' ') //#1
	If ::jParameters['removeReversed']
		self:oStWriteOffsByBill:SetString(nParam++, 'ES') //#2
		self:oStWriteOffsByBill:SetString(nParam++, ' ') //#3
	EndIf
	If ::jParameters['filterOrigBranch']
		self:oStWriteOffsByBill:SetString(nParam++, ::jParameters['branch']) //#4
	Else
		self:oStWriteOffsByBill:SetString(nParam++, xFilial('FK7', ::jParameters['branch'])) //#4
	EndIf
	self:oStWriteOffsByBill:SetString(nParam++, ::cIdDoc ) //#5
	If ::jParameters['lcompensated']
		self:oStWriteOffsByBill:SetString(nParam++, 'CMP') //#6
	EndIf
	If ::jParameters:HasProperty('initialDate') .And. !Empty(::jParameters['initialDate'])
		self:oStWriteOffsByBill:SetUnsafe(nParam++, self:getDateFilter()) //#7
	EndIf
	self:oStWriteOffsByBill:SetString(nParam++, ' ') //#8
	If ::jParameters:HasProperty('addFilterQuery') .And. !Empty(::jParameters['addFilterQuery'])
		self:oStWriteOffsByBill:SetUnsafe(nParam++, ::jParameters['addFilterQuery']) //#9
	EndIf

return self:oStWriteOffsByBill

//-------------------------------------------------------------------
/*/{Protheus.doc} setParameters
	@author rodrigo.oliveira
	@since 22/03/2024
	@type method
	@version 12.1.2310
	@param jParameters, Json, Parâmetros recebidos e definidos no
		momento da construção da classe
/*/
//-------------------------------------------------------------------
method setParameters(jParameters as json) class Movements

	if !(jParameters:HasProperty('filterOrigBranch'))
        jParameters['filterOrigBranch'] := .F.
	endif
	if !(jParameters:HasProperty('removeReversed'))
        jParameters['removeReversed'] 	:= .T.      
	endif
	if !(jParameters:HasProperty('lcompensated'))
		jParameters['lcompensated']		:= .T.
	endif
	if !(jParameters:HasProperty('ldateAvailability'))
    	jParameters['ldateAvailability']:= .F.
	endif
	
    self:jParameters := jParameters
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getDateFilter

	@author @rodrigo.oliveira
	@since 21/03/2025
	@type method
	@version 12.1.2410
	@return cRet, Character, String com a instrução de filtro por
		data na query das baixas de título
/*/
//-------------------------------------------------------------------
method getDateFilter() as character class Movements
	Local cRet	:= '' as character
	Local ldateAvailability	:= .F. as logical
	Local cAls	:= 'FK1' as character

	cRet				:= ' And '
	ldateAvailability	:= ::jParameters['dateAvailability']

	If ::cPayReceivable == 'P'
		cAls := 'FK2'
	EndIf
	If ::jParameters:HasProperty('finalDate') .And. !Empty(::jParameters['finalDate'])
		If ldateAvailability
			cRet	+= cAls + '.' + cAls + '_DTDISP'
		Else
			cRet	+= cAls + '.' + cAls + '_DATA'
		EndIf
		cRet	+= " BETWEEN '" + Dtos(::jParameters['initialDate']) + "' And '" + Dtos(::jParameters['finalDate']) + "' "
	Else
		If ldateAvailability
			cRet	+= cAls + '.' + cAls + '_DTDISP'
		Else
			cRet	+= cAls + '.' + cAls + '_DATA'
		EndIf
		cRet += " = '" + Dtos(::jParameters['initialDate']) + "' "
	EndIf

return cRet

//-------------------------------------------------------------------
/*/{Protheus.doc} aliastodata
	@author @rodrigo.oliveira
	@since 21/03/2025
	@type method
	@version 12.1.2410
	@param cTemp, Character, Alias temporário com dados das baixas do título
	@param lNeg, Logical, Define se o retorno será apenas do título (.T.) ou
		da baixa
	@return jRet, Json, Dados da query no formato de array
/*/
//-------------------------------------------------------------------
method aliastodata(cTemp as character, lNeg as logical) as json class Movements
	Local jRet 	:= JsonObject():New() as json
	Local jAux  as json
	Local nI	:= 0 as numeric

	Default cTemp	:= ""
	Default lNeg	:= .F.

	jRet['document'] := {}

	(cTemp)->(DbGoTop())
	While !(cTemp)->(EoF())
		nI++
		
		jAux	:= JsonObject():New()
			
		jAux['FK7_FILIAL'] 		:= (cTemp)->FK7_FILTIT
		jAux['FK7_PREFIX']		:= (cTemp)->FK7_PREFIX
		jAux['FK7_NUM']			:= (cTemp)->FK7_NUM
		jAux['FK7_PARCEL']		:= (cTemp)->FK7_PARCEL
		jAux['FK7_TIPO']		:= (cTemp)->FK7_TIPO
		If ::cPayReceivable == 'P' .Or. (::cPayReceivable == 'R' .And. (::lFullBillKey .Or. (!lNeg .And. (cTemp)->TPDOC == 'ES')))
			jAux['FK7_CLIFOR']	:= (cTemp)->FK7_CLIFOR
		EndIf
		jAux['FK7_LOJA']		:= (cTemp)->FK7_LOJA
		If !lNeg
			jAux['FK7_IDDOC']	:= (cTemp)->FK7_IDDOC
			jAux['E5_DATA'] 	:= (cTemp)->DATABX
			jAux['E5_DTDISPO']	:= (cTemp)->DTDISP
			jAux['E5_TIPODOC']	:= (cTemp)->TPDOC
			jAux['E5_MOTBX']	:= (cTemp)->MOTBX
			jAux['E5_MOEDA']	:= (cTemp)->MOEDA
			jAux['E5_VALOR']	:= (cTemp)->VAL
			jAux['E5_SEQ']		:= (cTemp)->SEQBX
			jAux['FK_IDFK']		:= (cTemp)->IDFK
			jAux['FK_IDCOMP']	:= (cTemp)->IDCOMP
			aAdd(jRet['document'], jAux)
		Else
			aAdd(jRet['document'], jAux)
		EndIf
		FreeObj(jAux)
		(cTemp)->(DbSkip())
	EndDo

return jRet

//-------------------------------------------------------------------
/*/{Protheus.doc} getCompStatement

    Executa query dos metodos 'getCompDocumentJsonBySE5' e 'getCompDocumentKey'.
    A partir do recno da SE5, obtem o E5_IDORIG e posiciona na respectiva FK1. 
    a partir da FK1, obtem o FK1_IDCOMP e posiciona na FK7. 
    A query deve retornar os dados da FK7 do título de contrapartida (FK1_IDCOMP)

	@author @rodrigo.oliveira
	@since 21/03/2025
	@type method
	@version 12.1.2410
	@param cIdFK, Character, Id da FK1 / FK2 da movimentação posicionada
	@return, Object, Objeto construído pela classe FWExecStatement
/*/
//-------------------------------------------------------------------
method getCompStatement(cIdFK as character) as object class Movements
    local cQuery as character
	local cChaveNeg as character

	default cIdFK := ""

	cChaveNeg	:= ::cPayReceivable

	if self:oStComp == NIL .Or. !(self:jGetComp:HasProperty(cChaveNeg))
		//Remove a propriedade anterior do JSON e insere a atual
		If Len(self:jGetComp:GetNames()) > 0
			self:jGetComp:DelName(self:jGetComp:GetNames()[1])
		endif
		self:jGetComp[cChaveNeg] := .T.

		cQuery 	:= "SELECT FK7.FK7_FILTIT, FK7.FK7_PREFIX, FK7.FK7_NUM, FK7.FK7_PARCEL, FK7.FK7_TIPO, FK7.FK7_CLIFOR, FK7.FK7_LOJA "	
		if ::cPayReceivable == 'P'
			cQuery	+= "FROM " + RetSqlName("FK7") + " FK7 "
			cQuery	+= "INNER JOIN " + RetSqlName("FK2") + " FK2 ON FK2_IDCOMP = FK7_IDDOC And FK2.D_E_L_E_T_ = ? "
			cQuery	+= " WHERE FK7.D_E_L_E_T_ = ? AND FK2.FK2_IDFK2 = ?"
		else
			cQuery	+= "FROM " + RetSqlName("FK7") + " FK7 "
			cQuery	+= "INNER JOIN " + RetSqlName("FK1") + " FK1 ON FK1_IDCOMP = FK7_IDDOC And FK1.D_E_L_E_T_ = ? "
			cQuery	+= " WHERE FK7.D_E_L_E_T_ = ? AND FK1.FK1_IDFK1 = ?"
		endif
		cQuery		:= ChangeQuery(cQuery)
		self:oStComp   := FWExecStatement():new(cQuery)
	endIf
	self:oStComp:setString(1, ' ')
	self:oStComp:setString(2, ' ')
    self:oStComp:setString(3, cIdFK)
return self:oStComp

//-------------------------------------------------------------------
/*/{Protheus.doc} getIdComp
    Retorna o ID da compensação referente ao título e sequencia da 
	baixa informado.
    Carteira a Receber: Retorna o conteúdo do campo FK1_IDCOMP
	Carteira a Pagar:  Retorna o conteúdo do campo FK2_IDCOMP

	@author @fabio.casagrande
	@since 22/04/2025
	@type method
	@version 12.1.2410

	@param nRecno, Numeric, RECNO do título a pagar/receber
	@param cRecPag,  Character, Carteira (R=Receber/P=Pagar)
	@param cSeq, Character, Sequencia da baixa por compensação
	@return cIdComp, Character, ID da compensação
/*/
//-------------------------------------------------------------------
method getIdComp(nRecno as numeric, cRecPag as character, cSeq as character) as character class Movements
	local cIdComp := "" as character
	local cAls := "SE2" as character
	local aAreaAtu := {} as array
	local aAreaTit := {} as array

	default nRecno := 0
	default cRecPag := "P"
	default cSeq := ""

	If cRecPag == "R"
		cAls := "SE1"		
	EndIf

	If !Empty(nRecno)
		aAreaAtu := FwGetArea()
		aAreaTit := (cAls)->(FwGetArea())
		(cAls)->(DbGoTo(nRecno))
	EndIf

	self:setIdDocRec((cAls)->(RECNO()), cAls)
	self:setPayReceivable(cRecPag)

    cIdComp := self:getIdCompStatement(cSeq):ExecScalar("IDCOMP")

	If !Empty(nRecno)
		FwRestArea(aAreaTit)
		FwRestArea(aAreaAtu)
		FwFreeArray(aAreaTit)
		FwFreeArray(aAreaAtu)
	EndIf
	
return cIdComp

//-------------------------------------------------------------------
/*/{Protheus.doc} getIdCompStatement

    Executa query do metodo 'getIdComp' para localizar o ID do título 
	de contrapartida da compensação (FK1_IDCOMP ou FK2_IDCOMP).

	@author @fabio.casagrande
	@since 22/04/2025
	@type method
	@version 12.1.2410
	
	@param cSeq, Character, Sequencia da baixa por compensação
	@return oStIdComp, Object, Objeto construído pela classe FWExecStatement
/*/
//-------------------------------------------------------------------
method getIdCompStatement(cSeq as character) as object class Movements
    local cQuery := "" as character
	local cChave := "" as character
	local nParam := 1 as Numeric

	default cSeq := ""	

	cChave := ::cPayReceivable + cValToChar(Empty(cSeq))

	if self:oStIdComp == NIL .Or. !(self:jGetIdComp:HasProperty(cChave))
		//Remove a propriedade anterior do JSON e insere a atual
		If Len(self:jGetIdComp:GetNames()) > 0
			self:jGetIdComp:DelName(self:jGetIdComp:GetNames()[1])
		endif
		self:jGetIdComp[cChave] := .T.

		if ::cPayReceivable == 'R'
			cQuery  += "SELECT FK1.FK1_IDCOMP IDCOMP "
			cQuery  += "FROM "+RetSqlName("FK1")+" FK1 "
			cQuery	+= "Left Join " + RetSqlName('FK1') + " FK1EST "
			cQuery	+= "	On FK1EST.FK1_FILIAL = FK1.FK1_FILIAL"
			cQuery	+= " 	And FK1EST.FK1_IDDOC = FK1.FK1_IDDOC"
			cQuery	+= " 	And FK1EST.FK1_SEQ = FK1.FK1_SEQ"
			cQuery	+= " 	And FK1EST.FK1_TPDOC = ?"	//#1
			cQuery	+= "	And FK1EST.D_E_L_E_T_ = ?"	//#2

			cQuery  += "WHERE FK1.FK1_IDDOC = ? " //#3
			If !Empty(cSeq)
				cQuery  += "AND FK1.FK1_SEQ  = ? " //#4
			EndIf
			cQuery += " AND FK1.FK1_MOTBX = ? " //#5
			cQuery += " AND FK1.FK1_RECPAG = ? " //#6
			cQuery	+= "  	And FK1EST.FK1_SEQ IS NULL"
			cQuery += " AND FK1.D_E_L_E_T_  = ? " //#7
		else
			cQuery  += "SELECT FK2.FK2_IDCOMP IDCOMP "
			cQuery  += "FROM "+RetSqlName("FK2")+" FK2 "
			cQuery	+= "Left Join " + RetSqlName('FK2') + " FK2EST "
			cQuery	+= "	On FK2EST.FK2_FILIAL = FK2.FK2_FILIAL"
			cQuery	+= " 	And FK2EST.FK2_IDDOC = FK2.FK2_IDDOC"
			cQuery	+= " 	And FK2EST.FK2_SEQ = FK2.FK2_SEQ"
			cQuery	+= " 	And FK2EST.FK2_TPDOC = ?"	//#1
			cQuery	+= "	And FK2EST.D_E_L_E_T_ = ?"	//#2

			cQuery  += "WHERE FK2.FK2_IDDOC = ? " //#3
			If !Empty(cSeq)
				cQuery  += "AND FK2.FK2_SEQ  = ? " //#4
			EndIf
			cQuery += " AND FK2.FK2_MOTBX = ? " //#5
			cQuery += " AND FK2.FK2_RECPAG = ? " //#6
			cQuery	+= "  	And FK2EST.FK2_SEQ IS NULL"
			cQuery += " AND FK2.D_E_L_E_T_  = ? " //#7
		endif
		cQuery := ChangeQuery(cQuery)
		self:oStIdComp := FWExecStatement():new(cQuery)  
	endIf

	self:oStIdComp:setString(nParam++, 'ES') //#1
	self:oStIdComp:setString(nParam++, ' ') //#2

	self:oStIdComp:setString(nParam++, ::cIdDoc) //#3
	If !Empty(cSeq)
		self:oStIdComp:SetString(nParam++, cSeq) //#4
	EndIf
	self:oStIdComp:setString(nParam++, 'CMP') //#5
	self:oStIdComp:setString(nParam++, ::cPayReceivable) //#6
	self:oStIdComp:setString(nParam++, ' ') //#7

return self:oStIdComp
