#include 'protheus.ch'

#include 'tlpp-core.th'
#include 'tlpp-rest.th'

namespace totvs.protheus.backoffice.fin.abstract

//-------------------------------------------------------------------
/*/{Protheus.doc} TafIntegration
    @description Classe abstrata para a implementação de consultas à base
    retornando um JSON. Extende FWAdapterBaseV2.
    A classe derivada pode implementar loadMapFields, getQuery, getWhere e getOrder para definir
    a consulta. Pode-se usar setCalcData para definir campos calculados.
    Exemplo: totvs.protheus.backoffice.fin.taf.integration.PayableBillsData
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type class
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
class DefaultGetModel from FWAdapterBaseV2
    public method new() as object
    public method clear()
    public method processData(nPage as numeric)

    protected method loadMapFields()
    protected method setCalcData()
    protected method rowToJson()
    protected method newException(cDescription as character) as object
    protected method getQuery() as character
    protected method getWhere() as character
    protected method getOrder() as character
    protected method getMapFieldsToString() as character

    protected data lOK as logical
    protected data jCalcData as json
    protected data cAlias as character
    protected data aFields as array
    protected data lEmpty as logical
endClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method new() as object class DefaultGetModel
    _Super:new("GET")
    self:clear()
    self:loadMapFields()
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} clear
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method clear() class DefaultGetModel
    self:lOK := .T.
    self:jCalcData := JsonObject():new()
    self:cAlias := ""
    self:aFields := {}
    self:lEmpty := .T.
return

//-------------------------------------------------------------------
/*/{Protheus.doc} rowToJson
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method rowToJson(cAlias as Character, aFields as Array, lEmpty as Logical) class DefaultGetModel
    local nX := 1 as numeric
    local aProperties := {} as array

    self:cAlias := cAlias
    self:aFields := aFields
    self:lEmpty := lEmpty

    _Super:rowToJson(cAlias, aFields, lEmpty)

    self:setCalcData()

    aProperties := self:jCalcData:getNames()
    for nX := 1 to len(aProperties)
        cProperty := aProperties[nX]            
        self:oJsonObj:setProp( cProperty, self:jCalcData[cProperty], lEmpty)
    next
return

//-------------------------------------------------------------------
/*/{Protheus.doc} newException
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method newException(cDescription as character) as object class DefaultGetModel    
	local oError := ErrorClass():New() 
	oError:description := cDescription
return oError

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData(nPage as numeric) class DefaultGetModel
    default nPage := 1

    self:oJsonObj:oJsonObj := JsonObject():new()    
    self:oJsonObj:nItem := 1
      
    self:setPage(nPage)

    self:SetQuery( self:getQuery() )
    self:SetWhere( self:getWhere() ) 
    self:SetOrder( self:getOrder() )

    if !self:Execute()
        throw self:newException(self:cMessage)
    endIf
    
    self:FillGetResponse()

    self:oJsonObj:cFieldsQry := ""
    self:oJsonObj:cFieldsSubQry := ""

return

//-------------------------------------------------------------------
/*/{Protheus.doc} getMapFieldsToString
    @description Transforma o vetor de campos mapeados em uma string para ser usada
    na cláusula SELECT da consulta, permitindo excluir determinados campos (para que sejam
    calculados via SQL). 
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getMapFieldsToString(aFieldsToExclude as array) as character class DefaultGetModel
    local cFields := "" as character
    local cFieldName := "" as character
    local nX := 1 as numeric
    local aFields := self:oJsonObj:aMapFields as array
    local lConsiderField := .T. as logical

    for nX := 1 to len(aFields)
        cFieldName := aFields[nX][2]
        lConsiderField := aScan(aFieldsToExclude, {|cFieldToExclude| allTrim(upper(cFieldToExclude)) == allTrim(upper(cFieldName))}) == 0
        if lConsiderField
            if empty(cFields)
                cFields += cFieldName
            else
                cFields += ", " + cFieldName
            endIf
        endIf
    next
return cFields


//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
    @description Método abstrato. Para exemplos de implementação, veja a classe totvs.protheus.backoffice.fin.taf.integration.PayableBillsData. 
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getQuery() as character class DefaultGetModel
    
return ""

//-------------------------------------------------------------------
/*/{Protheus.doc} getWhere
    @description Método abstrato. Para exemplos de implementação, veja a classe totvs.protheus.backoffice.fin.taf.integration.PayableBillsData.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getWhere() as character class DefaultGetModel
    
return ""

//-------------------------------------------------------------------
/*/{Protheus.doc} getOrder
    @description Método abstrato. Para exemplos de implementação, veja a classe totvs.protheus.backoffice.fin.taf.integration.PayableBillsData.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getOrder() as character class DefaultGetModel
    
return ""

//-------------------------------------------------------------------
/*/{Protheus.doc} setCalcData
    @description Método abstrato. Para exemplos de implementação, veja a classe totvs.protheus.backoffice.fin.taf.integration.PayableBillsData.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method setCalcData() class DefaultGetModel

return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadMapFields
    @description Método abstrato. Para exemplos de implementação, veja a classe totvs.protheus.backoffice.fin.taf.integration.PayableBillsData.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method loadMapFields() class DefaultGetModel

return
