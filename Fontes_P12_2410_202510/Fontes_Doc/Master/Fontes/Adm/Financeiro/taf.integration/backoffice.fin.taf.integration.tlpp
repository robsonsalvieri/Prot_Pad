#include 'protheus.ch'
#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'backoffice.fin.taf.integration.ch'

namespace totvs.protheus.backoffice.fin.taf.integration

//-------------------------------------------------------------------
/*/{Protheus.doc} TafIntegration
    @description Classe responsável pela integração do Financeiro com o TAF.
    Tem potencial para substituir o extrator no futuro.
    Consome as classes PayableBills e PayableWriteOff que retornam os dados
    de títulos a pagar a baixas a pagar respectivamente.
    No futuro, poderão ser implementadas novas classes relacionadas ao contas a receber.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type class
	@version 12.1.2310
    @see https://tdn.totvs.com/pages/viewpage.action?pageId=831829942
/*/
//-------------------------------------------------------------------
class TafIntegration
    public method new() as object
    public method setParameters()
    public method setPageSize()
    public method getBills() as json
    public method getWriteOff() as json
    public method clear() as json

    protected method validateEnvironment()
    protected method validateParameters()
    protected method newException() as object
    protected method logError()

    protected data oPayableBills as object
    protected data oPayableWriteOff as object

    protected data jParameters as json
    protected data aMandatoryParameters as array
    protected data jResult as json
    protected data lValidatedEnvironment as logical
endClass


//-------------------------------------------------------------------
/*/{Protheus.doc} new
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method new() as object class TafIntegration
    self:clear()
    self:lValidatedEnvironment := .F.
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} clear
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method clear() class TafIntegration
    self:jParameters := JsonObject():new()
    self:jResult := JsonObject():new()
    self:aMandatoryParameters := {"branch", "initialDate", "finalDate", "typeDatePayable"/*, "typeDateReceivable"*/}

    self:oPayableBills := PayableBillsData():new()
    self:oPayableWriteOff := PayableWriteOffData():new()
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getBills
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getBills(nPage as numeric) as json class TafIntegration   
    local jResult := JsonObject():new() as json
    
    default nPage := 1      

    try
        self:validateEnvironment()
        self:validateParameters() 
        self:oPayableBills:processData(nPage)
        jResult:fromJson(self:oPayableBills:getJSONResponse())
    catch oError
        self:logError(oError)
        jResult['code'] := "500"
        jResult['message'] := STR0001 //"Não foi possível obter os dados da requisição de títulos do Financeiro."
        jResult['detailedMessage'] := oError:description
    endTry
return jResult

//-------------------------------------------------------------------
/*/{Protheus.doc} getWriteOff
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getWriteOff(nPage as numeric) as json class TafIntegration 
    local jResult := JsonObject():new() as json
    
    default nPage := 1      

    try
        self:validateEnvironment()
        self:validateParameters() 
        self:oPayableWriteOff:processData(nPage)
        jResult:fromJson(self:oPayableWriteOff:getJSONResponse())
    catch oError
        self:logError(oError)
        jResult['code'] := "500"
        jResult['message'] := STR0001 //"Não foi possível obter os dados da requisição de títulos do Financeiro."
        jResult['detailedMessage'] := oError:description
    endTry
return jResult

//-------------------------------------------------------------------
/*/{Protheus.doc} validateParameters
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method validateParameters() class TafIntegration
    local nX := 1 as numeric
    local cParameter := "" as character

    for nX := 1 to len(self:aMandatoryParameters)
        cParameter := self:aMandatoryParameters[nX]
        if !self:jParameters:hasProperty(cParameter)
            throw self:newException(STR0003 + ': ' + cParameter) //Parâmetro obrigatório não informado
        endIf
    next nX
return

//-------------------------------------------------------------------
/*/{Protheus.doc} setParameters
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method setParameters(jParameters as json) class TafIntegration
    self:jParameters := jParameters
    self:oPayableBills:setParameters(self:jParameters)
    self:oPayableWriteOff:setParameters(self:jParameters)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} setPageSize
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method setPageSize(nPageSize as numeric) class TafIntegration
    self:oPayableBills:setPageSize(nPageSize)
    self:oPayableWriteOff:setPageSize(nPageSize)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} newException
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method newException(cDescription as character) as object class TafIntegration    
	local oError := ErrorClass():New() 
	oError:description := cDescription
return oError

//-------------------------------------------------------------------
/*/{Protheus.doc} logError
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method logError(oError as object) class TafIntegration
    if oError != NIL    
        FWLogMsg("WARN", , "SIGAFIN","TafIntegration", procName(1), , , , , {{"description", oError:description}, /*{"errorEnv", oError:errorEnv},*/ {"errorStack", oError:errorStack}})
    endIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} validateEnvironment
    @description Verifica se o RPO contem as versões adequadas dos fontes
    que possuem funções que o componente vai consumir.
    @author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
    @see https://jiraproducao.totvs.com.br/browse/DSTFC-8526
/*/
//-------------------------------------------------------------------
method validateEnvironment() class TafIntegration
    if self:lValidatedEnvironment
        return
    endIf    

    if GetRPORelease() <= "12.1.2410" 
        if !FindFunction("FTemBorImp") ; //FINA989 >= 01/04/2024 , chamada na classe PayableWriteOffData
            .or. !FindFunction("FTemSFT") ; //FINXREINF >= 15/09/2023, chamada na classe PayableBillsData
            .or. !FindFunction("AModNot") //FINXREINF >= 15/09/2023, chamada na classe PayableBillsData

            throw self:newException(STR0004) //"Para utilizar a integração Financeiro X TAF, atualize o módulo Financeiro com o pacote de expedição contínua mais recente."
        endIf
    endIf    

    self:lValidatedEnvironment := .T.
return



