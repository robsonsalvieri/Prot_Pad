#Include "PROTHEUS.ch"
#Include "fileio.ch"
#Include "FINA580.CH"

STATIC lPanelFin	:= NIL
STATIC lValSE2Sit	:= .F.
STATIC lFinVDoc		:= NIL
STATIC lFa580Lib	:= NIL
STATIC lF580BROW	:= NIL
STATIC lFina580		:= NIL
STATIC lFina580A	:= NIL
STATIC lManBrow		:= NIL
STATIC lF580LbA		:= NIL
STATIC lf580AutVl	:= NIL
STATIC lF580Chav	:= NIL
STATIC lF580FAUT	:= NIL
STATIC lF580Legen	:= NIL
STATIC lF580EdLeg	:= NIL
STATIC lF580ADDB	:= NIL
STATIC lF565Can 	:= NIL
STATIC lF580VLDPA	:= NIL
STATIC _oFINA5801
//Parâmetro que verifica se será Realizado validação por 1 = Data Contábil (E2_EMIS1) / 2 = Emissão (E2_EMISSAO)
STATIC lVerEmis1	:= Nil


//------------------------------------------------------------------------------------------
// __lLoad580 tem que ser a ultima, para carregar as statics declaradas anteriormente
Static __lLoad580 := F580IniStat()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ FiNA580  ³ Autor ³ Fernando Dourado      ³ Data ³ 19/03/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Liberacao dos  Titulos a Pagar                     		  ³±±         
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ FiNA580()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function FINA580(nPosArotina as Numeric) 

Local cSetFilter	As Character
Local cTipoLib		As Character	//Parâmetro de ativação de controle de Alçadas
Local lCtlIpag		As Logical

Private aRotina		As Array
Private cMarca		As Character
Private cCadastro	As Character
Private cFilAux		As Character

cSetFilter	:= SE2->(DBFILTER()) // Salva o filtro
cTipoLib	:= SuperGetMV( "MV_FINCTAL", .F., "1" )	//Parâmetro de ativação de controle de Alçadas
lCtlIpag	:= SuperGetMv("MV_CTLIPAG")

If !lCtlIpag
    MSGAlert(OemToAnsi( STR0008 ))//"Esta rotina só poderá ser executada, quando o parâmetro MV_CTLIPAG For 'T'")
	Return 	
Endif

//----------------------------------------
// Inicializa staticas 
F580IniStat(!__lLoad580)

aRotina		:= MenuDef( cTipoLib )
cMarca		:= GetMark()
cCadastro	:= OemToAnsi( STR0003 )
cFilAux		:= cFilAnt

DEFAULT nPosArotina := 0

If nPosArotina > 0 // Sera executada uma opcao diretamento de aRotina, sem passar pela mBrowse
	dbSelectArea("SE2")
	bBlock := &( "{ |a,b,c,d,e| " + aRotina[ nPosArotina,2 ] + "(a,b,c,d,e) }" )
	Eval( bBlock, Alias(), (Alias())->(Recno()),nPosArotina)
Else	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de entrada para ser utilizado antes do Browse          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lF580BROW
		ExecBlock( "F580BROW",.F.,.F.)
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Endereca a Fun‡„o de BROWSE                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	mBrowse( 6, 1,22,75,"SE2",,,,,, Fa580Leg("SE2"))

Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Recupera a Integridade dos dados                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RetIndex("SE2")
dbSetOrder(1)

If !Empty(cSetFilter)   
	Set Filter to &cSetFilter  //Restaura o filtro, para que o usuario que o usuario não perca o filtro do fina750
Else
	Set Filter to
EndIf

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fA580Man ³ Autor ³ Fernando Dourado      ³ Data ³ 19/03/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Liberacao Manual                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Fa580Man()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION FA580MAN(cAlias,nReg,nOpc,lAuto,aTit)

Local nOpcA		:= 0
Local lRet		:= .T.
Local lJustCP	:= CposJust() 
Local aBut580	:= {}             
Local cFilAtu	:= cFilAnt
Local lBlqPA  := SuperGetMv("MV_BLQPAPC",.F.,.F.)
Local cPedBql  := ''

Private aSE2FI2 := {}

If lFa580Lib
	lRet := ExecBlock("FA580LIB",.F.,.F.)
EndIf


If dDataBase < iif(lVerEmis1,SE2->E2_EMIS1,SE2->E2_EMISSAO)
	MsgInfo(STR0100,"FA580DATA") //"Não é possivel liberar um título em uma data inferior a sua emissão."
	lRet := .F.	
Endif

//Controle de validacao de documentos obrigatorios
If lFinVDoc == NIL
	lFinVDoc := ( GetNewPar("MV_FINVDOC","2")=="1" )
Endif

//Verifica se situacao atual permite liberacao
If lValSE2Sit
	If !Empty(SE2->E2_DATASUS) .Or. !Empty(SE2->E2_DATACAN)
		Aviso( "FA580VLD", STR0023, {"OK"} ) //"Situação atual do título não permite o processo de liberação."
		lRet := .F.	
	EndIf                  
End If

//Documentos Apresentados
IF lFinVDoc
	cFilAnt := SE2->E2_FILORIG
	If !CN062ValDocs("01",.F.)
		lRet := .F.
	EndIf
	cFilAnt := cFilAtu
EndIf		
	 
//Verifica vinculo como pedido de compras
If lBlqPA
cPedBql := ValidPedido(SE2->E2_NUM, SE2->E2_PREFIXO) 
	If  !(Empty(cPedBql))
		Aviso( "PEDVLD",STR0098 + "'"+cPedBql+"' " + STR0099, {"OK"},3 )
		lRet := .F.
	EndIf
EndIf		 

If lRet
	If Empty(SE2->E2_DATALIB) .AND. (SE2->E2_SALDO+SE2->E2_SDACRES-SE2->E2_SDDECRE) > GETMV("MV_VLMINPG")
		dbSelectArea(cAlias)
		
		//Botoes adicionais na EnchoiceBar
		If lJustCP // Adiciona botao para justificativa
			Aadd(aBut580,{'BAIXATIT',{||Fa580JUST("E2_DATALIB")},STR0042})		//"Justificativa"
		Endif						
		
		If !lAuto
			If lPanelFin  //Chamado pelo Painel Financeiro					   
				RegToMemory(cAlias,.T.,,,FunName())                                       
				oPanelDados := FinWindow:GetVisPanel()
				oPanelDados:FreeChildren()		
				aDim := DLGinPANEL(oPanelDados)		
				nOpca := AxVisual(cAlias,nReg,2,,,,,,,,.T.,,.T.,.T.,aDim)
			Else
				nOpca := AxVisual(cAlias,nReg,2,,,,,aBut580,,,,,.T.)
			Endif	
		Else 
			nOpcA := 1 //KASSIA
			dbSelectArea("SE2")
			DbSetOrder(1)//E2_FILIAL, E2_PREFIXO, E2_NUM, E2_PARCELA, E2_TIPO, E2_FORNECE, E2_LOJA, R_E_C_N_O_, D_E_L_E_T_
			DbSeek(xFilial("SE2")+aTit[1,1]+aTit[1,2]+aTit[1,3]+aTit[1,4]+aTit[1,5]+aTit[1,6])
		Endif
					
		If nOpcA == 1
			Begin Transaction
				dbSelectArea( cAlias )
				RecLock(cAlias,.F.,.t.)
					SE2->E2_DATALIB := dDataBase
					SE2->E2_USUALIB := cUsername
					SE2->E2_STATLIB := "03"
					SE2->E2_CODAPRO := Fa006User( __cUserId, .F., 2 )
				MsUnlock()
				
				//Envio de e-mail pela rotina de checklist de documentos obrigatorios
				IF lFinVDoc
					cFilAnt := SE2->E2_FILORIG
					CN062ValDocs("01",.F.,.T.)
					cFilAnt := cFilAtu
				EndIf
				
				If lFina580
					Execblock("FINA580",.f.,.f.,{.T.})
				Endif
			End Transaction

			If lFina580A
				Execblock("FINA580A",.f.,.f.)
			Endif

			F580GrvFI2()

		EndIf
	ElseIF SE2->E2_SALDO <= 0
	    MSGAlert(STR0017) //"Titulo totalmente baixado"
	ElseIF !Empty(SE2->E2_DATALIB) .OR. (SE2->E2_SALDO+SE2->E2_SDACRES-SE2->E2_SDDECRE) <= GETMV("MV_VLMINPG")
	    MSGAlert(STR0015) //"Titulo liberado para pagamento."
	Endif
EndIf
dbSelectArea( cAlias )
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fA580Can ³ Autor ³ Fernando Dourado      ³ Data ³ 19/03/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cancelar a Liberacao                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Fa580Can()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION FA580CAN(cAlias,nReg,nOpc)

Local nOpcC := 0
Local lGrvMov := .T.

//valida o cancelamento de título tipo PA
If SE2->E2_TIPO $ MVPAGANT .And. lF580VLDPA  
	lRet := ExecBlock( "F580VLDPA",.F.,.F.)			
EndIf 

If !Empty(SE2->E2_DATALIB)
	If Empty(SE2->E2_BAIXA) .And. Empty(SE2->E2_NUMBOR) .And. Empty(SE2->E2_IMPCHEQ)        
		dbSelectArea(cAlias)
	
		If lPanelFin  //Chamado pelo Painel Financeiro			
			dbSelectArea("SE2")
			RegToMemory("SE2",.T.,,,FunName())                                       
			oPanelDados := FinWindow:GetVisPanel()
			oPanelDados:FreeChildren()
			aDim := DLGinPANEL(oPanelDados)	   
			nOpcC := AxVisual(cAlias,nReg,nOpc,,,,,,,,.T.,,.T.,.T.,aDim)
		Else
			nOpcC := AxVisual(cAlias,nReg,nOpc)
		Endif
		
		If nOpcC == 1
			//valida se o processo de cancelamento da liberação para baixa continuará ou não.
			If lF565Can
				lGrvMov := Execblock("F580CAN",.F.,.F.)
			Endif
			
			If lGrvMov
							
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se existe movimento este TES                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				Begin Transaction
					dbSelectArea( cAlias )
					RecLock(cAlias,.F.,.t.)
					SE2->E2_DATALIB := ctod("  /  /  ")
					SE2->E2_USUALIB := " "
					SE2->E2_STATLIB := "01"
					MsUnlock()
				End Transaction
				
			Endif
		EndIf
	Else
	    MsGAlert(OemToAnsi( STR0012 ))//"Atenção!!!Título já Baixado"
	Endif
Endif
dbSelectArea( cAlias )
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ fA580Aut ³ Autor ³ Fernando Dourado      ³ Data ³ 19/03/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Liberacao Automática                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Fa580Aut()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fA580Aut(cAlias,cCampo,nOpcE)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Vari veis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local oDlg1
Local lInverte	:= .f.
Local nOpca		:= 0
Local cChave	:= ""
Local aCampos	:= {}
Local lOk		:= .F.
Local oValor	:= 0
Local oQtdTit 	:= 0
Local nValor 	:= 0
Local nQtdTit  	:= 0
Local aSize    	:= {}
Local aObjects 	:= {}
Local aInfo   	:= {}
Local aPosObj 	:= {}
Local nPos		:= 2.7
//Gestao
Local lQuery 	:= IfDefTopCTB() // verificar se pode executar query (TOPCONN)
Local nX		:= 0
Local aSelFil	:= {}
Local aTmpFil	:= {} 
Local lGestao	:= FWSizeFilial() > 2	// Indica se usa Gestao Corporativa
Local lSE2Access := Iif( lGestao, FWModeAccess("SE2",1) == "E", FWModeAccess("SE2",3) == "E") //Verifica SE2 totalmente compartilhado
Local lBlqPA    := SuperGetMv("MV_BLQPAPC",.F.,.F.)
Local aFields   := {}
Local nQtdReg   := 0   // Quantida de registros retornados na query
Local nTamPic   := 0   // define o tamanho da mascara da picture 
Local cPictdin  := ""  // Picture dinamico

Default cCampo := ""


If lPanelFin
	lOk := PergInPanel("FIN580",.T.)
Else
	lOk := pergunte("FIN580",.T.)
Endif
If !lOk
	Return .T.
Endif

//Gestao
//Controle de validacao de documentos obrigatorios
If lFinVDoc == NIL
	lFinVDoc := ( GetNewPar("MV_FINVDOC","2")=="1" )
Endif

//Selecao de filiais
If lQuery .and. lSE2Access .and. mv_par12 == 1 .And. Len( aSelFil ) <= 0 
	aSelFil := AdmGetFil(.F.,.T.,"SE2")
	If Len( aSelFil ) <= 0
		Return
	EndIf	
Else
	aSelFil := { cFilAux }	 
EndIf

cChave		:= "E2_FILIAL+DTOS(E2_VENCREA)+E2_NOMFOR+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO"
If lF580Chav
	cChave:= ExecBlock( "F580CHAV",.F.,.F.,{cChave})
EndIf
dbSelectArea( cAlias )

cAliasSE2	:= "QRYSE2"
cQuery	:= ""
aStru	:= SE2->(DbStruct())
aFields := aClone(aStru)
aEval(aStru,{|x| cQuery += ","+AllTrim(x[1])})

cQuery	:= "SELECT "+SubStr(cQuery,2)
cQuery	+=         ","+"SE2.R_E_C_N_O_ RECNO "
cQuery	+= "FROM "+RetSqlName("SE2")+ " SE2 "
cQuery	+= "WHERE " + FA580ChecF(aSelFil,aTmpFil)
If lBlqPA
	cQuery += "AND SE2.E2_NUM NOT IN (SELECT FIE_NUM FROM "+RetSqlName("FIE")+","+RetSqlName("SC7")+" WHERE FIE_PEDIDO = C7_NUM AND C7_CONAPRO <> 'L' AND  FIE_PREFIX = E2_PREFIXO)"
Endif
cQuery	+= " AND SE2.D_E_L_E_T_ = ' ' "
cQuery	+= " ORDER BY " + SqlOrder(cChave)

Aadd(aStru, {"RECNO","N",10,0})

If _oFINA5801 <> Nil
	_oFINA5801:Delete()
	_oFINA5801	:= Nil
Endif	

//Cria o Objeto do FwTemporaryTable
_oFINA5801 := FwTemporaryTable():New(cAliasSe2)

//Cria a estrutura do alias temporario
_oFINA5801:SetFields(aStru)

//Cria o índice do alias temporario
_oFINA5801:AddIndex("1", TTFtIndex(StrToKarr(cChave,"+")))
	
//Criando a Tabela Temporaria
_oFINA5801:Create()

Processa({||SqlToTrb(cQuery, aStru, cAliasSe2)}) // Cria arquivo temporario

dbGoTop()

If !Eof()
	nOpcA	:= 0
	aCampos := {}
	nQtdReg := (cAliasSe2)->( LASTREC())	
	nTamPic := Len(AllTrim(Str(nQtdReg)))
	cPictdin := Replicate("9" , IIf( nTamPic > 3 , nTamPic, 3) )  
	AADD(aCampos,{"E2_OK","","  ","", "0"})
	For nX := 1 to len(aFields)
		cCampo	:= aFields[nX][1]
		if X3USO(GetSx3Cache( cCampo, "X3_USADO" ) ) .and. GetSx3Cache( cCampo, "X3_CONTEXT" ) != "V" .AND. cNivel >= GetSx3Cache(cCampo,"X3_NIVEL") .OR.;
			(AllTrim(cCampo) = "E2_DATALIB" .or. (AllTrim(cCampo) == "E2_FILIAL" .and. Len(aSelFil) > 1))
			AADD(aCampos,{cCampo,"",FwX3Titulo(cCampo),GetSx3Cache(cCampo,"X3_PICTURE"),GetSx3Cache(cCampo,"X3_ORDEM") })
		endif
	Next nX
	ASORT(aCampos, , , { | x,y | x[5] < y[5] } )	
	FwFreeArray(aFields)

	//Ponto de entrada para manipulacao dos campos a serem apresentados no browse de marcacao
	If lManBrow
		aCampos := ExecBlock("F580MNBR",.f.,.f.,aCampos)
	Endif

	dbSelectArea(cAliasSe2)
	If mv_par11 <> 2
		Fa580Marca(cAliasSe2,cMarca,oValor,oQtdTit,@nValor,@nQtdTit)
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Calculo das dimensoes da Janela                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aSize    := MsAdvSize()
	aObjects := {}
	AAdd( aObjects, { 015, 015, .T., .F. } )
	AAdd( aObjects, { 120, 100, .T., .T. } )

	aInfo   := { aSize[ 1 ],aSize[ 2 ],aSize[ 3 ],aSize[ 4 ],02,02 }
	aPosObj := MsObjSize( aInfo, aObjects, .F. )

	DEFINE MSDIALOG oDlg1 TITLE OemToAnsi(STR0003) From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
	oDlg1:lMaximized := .T.
	
	@ nPos , 01	Say STR0018 SIZE 50,12 //"Valor Selecionado "
	@ nPos , 24	Say STR0019 SIZE 50,12 //"Títulos Selecionados "
	@ nPos , 10	Say oValor		VAR nValor		Picture "@E 9,999,999,999,999.99" 
	@ nPos , 34	Say oQtdTit 	VAR nQtdTit 	Picture cPictdin  

	oMark:= MsSelect():New(cAliasSe2,"E2_OK","E2_DATALIB",aCampos,@lInverte,@cMarca	,{aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4]})
	oMark:bMark := {||Fa580Exibe(cAliasSE2,cMarca,oValor,oQtdTit,@nValor,@nQtdTit)}	
	oMark:oBrowse:lhasMark		:= .t.
	oMark:oBrowse:lCanAllmark	:= .t.
	oMark:oBrowse:bAllMark		:= { || FA580Inverte(cMarca,cAliasSe2,oValor,oQtdTit,@nValor,@nQtdTit,.T.)}
	
	If lf580AutVl 
		nValor := ExecBlock("F580AUTVL", .f.,.f.)
		oValor:Refresh()
	EndIf
	
	If lPanelFin  //Chamado pelo Painel Financeiro					
		ACTIVATE MSDIALOG oDlg1 ON INIT FaMyBar(oDlg1,{|| nOpca := 1,oDlg1:End()},;
													{|| nOpca := 0,ODlg1:End()} )		 			
    Else
		ACTIVATE MSDIALOG oDlg1 ON INIT EnchoiceBar(	oDlg1,{|| nOpca := 1,oDlg1:End()},;
										 		   {|| nOpca := 0,ODlg1:End()} ) CENTER
	Endif
			
	dbSelectArea(cAliasSe2)
	
	If nOpca == 1			
		MsUnlockall()
		dbgotop()		
		Processa( {|| ProcessReg(cAliasSe2,nQtdReg)})
	EndIf
Else
	Help(" ",1,"RECNO")
Endif	
#IFDEF TOP
	DbSelectArea(cAliasSe2)
	DbCloseArea()      
	
	//Gestao
  	For nX := 1 TO Len(aTmpFil)
		CtbTmpErase(aTmpFil[nX])   
    Next
	
#ELSE
	RetIndex("SE2")
	If !Empty(cIndex)
		fErase(cIndex+OrdBagExt())
	Endif
	Set Filter to
#ENDIF

Return (.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³fa580blank³ Autor ³ Fernando Dourado 	    ³ Data ³ 21/03/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Limpa o campo E2_OK para efetuar marca‡„o. 				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³fa580blank() 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³FINA580													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fa580Blank()

Reclock("SE2")
Replace E2_OK with "  "
MsUnlock()

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³fa580ChecF³ Autor ³ Fernando Dourado 	    ³ Data ³ 21/03/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna Condi‡„o para Indice Condicional					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³fa580ChecF() 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Fa580ChecF(aSelFil,aTmpFil)

Local cFiltro	 := ""
Local cTmpSE2Fil := ""
Local lMdAcsSE2	:= FwModeAccess("SE2",3,cEmpAnt) = "C"

DEFAULT aSelFil := {}
DEFAULT aTmpFil := {}

If Len(aSelFil) > 1
	cFiltro += "E2_FILIAL " + GetRngFil( aSelFil, "SE2", .T., @cTmpSE2Fil )
	aAdd(aTmpFil, cTmpSE2Fil)
ElseIf !lMdAcsSE2
	cFiltro	:= "E2_FILIAL = '"+aSelFil[1]+"'"
Else
	cFiltro	:= "E2_FILIAL = '" + SE2->E2_FILIAL + "' "
EndIf

cFiltro	+= " AND (E2_FORNECE>='"+mv_par01+"' AND E2_FORNECE<='"+mv_par02+"'"
cFiltro	+= " AND E2_PORTADO>='"+mv_par03+"' AND E2_PORTADO<='"+mv_par04+"'"
cFiltro	+= " AND E2_VENCTO >='"+DTOS(mv_par05)+"'"
cFiltro	+= " AND E2_VENCTO <='"+Dtos(mv_par06)+"'"
cFiltro	+= " AND E2_VALOR>="+ALLTRIM(STR(mv_par07,17,2))
cFiltro	+= " AND E2_VALOR<="+ALLTRIM(STR(mv_par08,17,2))
cFiltro	+= " AND E2_TIPO>='"+mv_par09+"' AND E2_TIPO<='"+mv_par10+"'"
cFiltro	+= " AND E2_TIPO NOT IN " + FormatIn(MVABATIM,"|")
cFiltro	+= " AND E2_SALDO>0)"
cFiltro	+= " AND ((E2_DATALIB='"+dtos(ctod(""))+"')"
cFiltro	+= " OR (E2_SALDO+E2_SDACRES-E2_SDDECRE<="+ALLTRIM(STR(GetMv('MV_VLMINPG'),17,2))
cFiltro	+= " AND E2_SALDO>0"
cFiltro	+= " AND E2_DATALIB='"+dTos(ctod(""))+"'))"

If lValSE2Sit
	cFiltro	+= " AND E2_DATASUS = '" + dTos(ctod("")) + "'"
	cFiltro	+= " AND E2_DATACAN = '" + dTos(ctod("")) + "'"		
EndIf

If lF580FAUT
	cFiltro:= ExecBlock( "F580FAUT",.F.,.F.,{cFiltro})
EndIf

Return cFiltro

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³fA580Par	³ Autor ³ Fernando Dourado 	  ³ Data ³ 19/03/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Ativa Parƒmetros do Programa										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ 																			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Gen‚rico 																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fA580Par()
pergunte("FIN580",.T.)
Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³Fa580Marca³ Autor ³ Alessandro B. Freire  ³ Data ³ 21/11/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Trata o valor	para marcar e desmarcar item					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Fa580Marca(ExpN1,ExpD1,ExpD2) 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA580																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Fa580Marca(cAlias,cMarca,oValor,oQtdTit,nValor,nQtdTit)
Local aArea := GetArea()
Local lRet	:= .T.
Local lPrimeiro :=.T.
Local cFilAtu	:= cFilAnt

//Controle de validacao de documentos obrigatorios
If lFinVDoc == NIL
	lFinVDoc := ( GetNewPar("MV_FINVDOC","2")=="1" )
Endif

dbSelectArea(cAlias)
DbGoTop()
While !Eof()

	//Verifica se a data da liberacao esta menor que a emissao/Dt. Contábilo
	If dDataBase < iif(lVerEmis1,(cAlias)->E2_EMIS1,(cAlias)->E2_EMISSAO)
		lRet := .F.
	Else
		lRet := .T.	
	Endif
	
	// Documentos Apresentados
	IF lFinVDoc
		lRet	:= .T.
		SE2->(dbGoTo((cAlias)->RECNO))
		cFilAnt := SE2->E2_FILORIG
		If !CN062ValDocs("01",.T.,,,@lPrimeiro)
			lRet := .F.
		EndIf
		cFilAnt := cFilAtu
	EndIf	
			 
	If lRet			
		RecLock(cAlias)
		(cAlias)->E2_OK := cMarca
		MsUnLock()
		nValor += Round(NoRound(xMoeda((cAlias)->(E2_SALDO+E2_SDACRES-E2_SDDECRE),(cAlias)->E2_MOEDA,1,,3),3),2)
		nQtdTit++
	EndIf
	dbSkip()
EndDo
RestArea(aArea)
Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³FA580Inver³ Autor ³ Fernando Dourado		  ³ Data ³ 19/03/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Marca / Desmarca todos os titulos								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA580																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Fa580Inverte(cMarca,cAlias,oValor,oQtdTit,nValor,nQtdTit,lTodos)

Local aArea			:= GetArea()
Local lRet			:= .T.
Local lPrimeiro		:= .T.
Local bWhile		:= {||xFilial("SE2") == (cAlias)->E2_FILIAL}
Local lQuery 		:= IfDefTopCTB() // verificar se pode executar query (TOPCONN)
Local cFilAtu		:= cFilAnt

Default lTodos	:= .F.

//Controle de validacao de documentos obrigatorios
If lFinVDoc == NIL
	lFinVDoc := ( GetNewPar("MV_FINVDOC","2")=="1" )
Endif

dbSelectArea(cAlias)
DbGoTop()

//Gestao
If lQuery .and. mv_par12 == 1
	bWhile	:= {||.T.}
Endif

While !Eof() .and. Eval(bWhile)

	//Verifica se a data da liberacao esta menor que a emissao/Dt. Contábilo
	If dDataBase < iif(lVerEmis1,(cAlias)->E2_EMIS1,(cAlias)->E2_EMISSAO)
		lRet := .F.
	Else
		lRet := .T.	
	Endif

	// Documentos Apresentados
	IF lFinVDoc
		lRet	:= .T.
		SE2->(dbGoTo((cAlias)->RECNO))
		cFilAnt := SE2->E2_FILORIG
		If !CN062ValDocs("01",.T.,.F.,lTodos,@lPrimeiro)
			lRet := .F.
		EndIF
		cFilAnt := cFilAtu
	EndIf 

	If lRet
		RecLock(cAlias)
		IF (cAlias)->E2_OK == cMarca
			(cAlias)->E2_OK	:= "  "
			nValor -= Round(NoRound(xMoeda((cAlias)->(E2_SALDO+E2_SDACRES-E2_SDDECRE),(cAlias)->E2_MOEDA,1,,3),3),2)
			nQtdTit--
		Else
			(cAlias)->E2_OK	:= cMarca
			nValor += Round(NoRound(xMoeda((cAlias)->(E2_SALDO+E2_SDACRES-E2_SDDECRE),(cAlias)->E2_MOEDA,1,,3),3),2)
			nQtdTit++
		Endif
		MsUnlock()
	EndIf
	dbSkip()
Enddo
RestArea(aArea)

nQtdTit:= Iif(nQtdTit<0,0,nQtdTit)
oValor:Refresh()
oQtdTit:Refresh()
oMark:oBrowse:Refresh(.t.)

Return Nil

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³FA580Exibe³ Autor ³ Mauricio Pequim Jr.   ³ Data ³ 19/05/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Exibe Totais de titulos selecionados						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FA580Exibe()												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA580													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Fa580Exibe(cAlias,cMarca,oValor,oQtdTit,nValor,nQtdTit)

Local lRet := .T.
Local cFilAtu	:= cFilAnt

//Controle de validacao de documentos obrigatorios
If lFinVDoc == NIL
	lFinVDoc := ( GetNewPar("MV_FINVDOC","2")=="1" )
Endif

// Documentos Apresentados
If (cAlias)->E2_OK == cMarca

	//Verifica se a data da liberacao esta menor que a emissao/Dt. Contábilo
	If dDataBase < iif(lVerEmis1,(cAlias)->E2_EMIS1,(cAlias)->E2_EMISSAO)
		MsgInfo(STR0100,"FA580DATA") //"Não é possivel liberar o título em uma data inferior a sua emissão."
		lRet := .F.
	Endif

	IF lFinVDoc
		lRet	:= .T.
		SE2->(dbGoTo((cAlias)->RECNO))
		cFilAnt := SE2->E2_FILORIG
		If !CN062ValDocs("01",.F.)
			lRet := .F.
		EndIf
		cFilAnt := cFilAtu
	EndIf
		 
EndIf

If lRet
	If (cAlias)->E2_OK == cMarca
		nValor += Round(NoRound(xMoeda((cAlias)->(E2_SALDO+E2_SDACRES-E2_SDDECRE),(cAlias)->E2_MOEDA,1,,3),3),2)
		nQtdTit++
	Else
		nValor -= Round(NoRound(xMoeda((cAlias)->(E2_SALDO+E2_SDACRES-E2_SDDECRE),(cAlias)->E2_MOEDA,1,,3),3),2)
		nQtdTit--
	Endif
	nQtdTit:= Iif(nQtdTit<0,0,nQtdTit)
	oValor:Refresh()
	oQtdTit:Refresh()
Else
	RecLock(cAlias)
	IF (cAlias)->E2_OK == cMarca
		(cAlias)->E2_OK	:= "  "
	Else
		(cAlias)->E2_OK	:= cMarca
	Endif
	MsUnlock()
	nQtdTit:= Iif(nQtdTit<0,0,nQtdTit)
	oValor:Refresh()
	oQtdTit:Refresh()
	oMark:oBrowse:Refresh(.t.)
EndIf

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³FA580Leg	  ³ Autor ³ Mauricio Pequim Jr. ³ Data ³ 04.12.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cria uma janela contendo a legenda da mBrowse ou retorna a ³±±
±±³          ³ para o BROWSE                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Fina580                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FA580Leg(cAlias, nReg)

Local aLegenda   := {}
Local uRetorno   := .T. 
Local cTipoLib   := SuperGetMV( "MV_FINCTAL", .T., "1" )
Local cVlrMinimo := ALLTRIM(STR(GetMv('MV_VLMINPG'),17,2))
Local aTmp     := {}

//Utiliza liberação simples
If cTipoLib == "1" 
	aLegenda := { { "BR_VERDE"	  , STR0015 },;	//"Titulo liberado para pagamento."
				  { "BR_AZUL"	  , STR0016 },;	//"Titulo aguardando liberacao."
				  { "BR_VERMELHO" , STR0017 },;	//"Titulo totalmente baixado."
				  { "BR_VIOLETA"  , STR0020 },; //"Titulo suspenso."						
				  { "BR_LARANJA"  , STR0021 } } //"Titulo cancelado."
	If lF580Legen
  		aTmp := aClone(ExecBlock( "F580LEGEN", .f., .f. ))
 	
 		If Len(aTmp) >= Len(aLegenda)
	  		aLegenda := aClone(aTmp)	
  		EndIf
	EndIf
					   
	If nReg = Nil	// Chamada direta da funcao onde nao passa, via menu Recno eh passado
		uRetorno := {}
		Aadd(uRetorno, { ' E2_SALDO <= 0', aLegenda[3][1] } )
		 
		If lValSE2Sit	
			Aadd(uRetorno, { '!EMPTY(E2_DATASUS) .AND. !EMPTY(E2_USUASUS)' , aLegenda[4][1] })
			Aadd(uRetorno, { '!EMPTY(E2_DATACAN) .AND. !EMPTY(E2_USUACAN)' , aLegenda[5][1] })		 
		EndIf
		
		Aadd(uRetorno, { '!EMPTY(E2_DATALIB) .OR.  (SE2->E2_SALDO+SE2->E2_SDACRES-SE2->E2_SDDECRE) <= '+cVlrMinimo, aLegenda[1][1] })
		Aadd(uRetorno, { ' EMPTY(E2_DATALIB) .AND. (SE2->E2_SALDO+SE2->E2_SDACRES-SE2->E2_SDDECRE)  > '+cVlrMinimo, aLegenda[2][1] })
		
	Else
		BrwLegenda(cCadastro, STR0014, aLegenda) // "Legenda"
	Endif

//Utiliza liberação por controle de alçadas de fundo fixo.
Else
	//Cores para a legenda
	aLegenda := { {"BR_AZUL"	, "Bloqueado (Esperando outros níveis)"	},;	 //"Bloqueado (Esperando outros níveis)"
	              {"BR_VERMELHO", STR0016	},;	 //"Esperando aprovação do usuário"
	              {"BR_VERDE"   , STR0015	},;	 //"Movimento liberado pelo usuário"
	              {"BR_PRETO"   , "Movimento bloqueado pelo usuário"	},;  //"Movimento bloqueado pelo usuário"
	              {"BR_CINZA"   , "Movimento liberado por outro usuário"	} }  //"Movimento liberado por outro usuário"   
 
	If lF580Legen
 		aTmp := aClone(ExecBlock( "F580LEGEN", .f., .f. ))
   		If Len(aTmp) >= Len(aLegenda)
	  		aLegenda := aClone(aTmp)	
   		EndIf
	EndIf	
	//Verifica se não foi chamada do menu
	If nReg == nil  
		uRetorno := {}
		//Legenda de status da movimentação do caixinha
		aAdd( uRetorno, { "E2_STATLIB == '01' .Or.( Empty(E2_STATLIB) .And. Empty(E2_DATALIB) )" , "BR_VERMELHO" } )	//Esperando aprovação do usuário
		aAdd( uRetorno, { "E2_STATLIB == '02'", "BR_AZUL"	   } )	//Bloqueado (esperando outros níveis)
		aAdd( uRetorno, { "E2_STATLIB == '03' .Or. ( Empty(E2_STATLIB) .And. !Empty(E2_DATALIB) )", "BR_VERDE" } )	//Movimento liberado pelo usuário
		aAdd( uRetorno, { "E2_STATLIB == '04'", "BR_PRETO"	   } )	//Movimento bloqueado pelo usuário
		aAdd( uRetorno, { "E2_STATLIB == '05'", "BR_CINZA"	   } )	//Movimento liberado por outro usuário 
	Else                         
		BrwLegenda( cCadastro, STR0014, aLegenda )  //"Legenda"                                                       
	EndIf 
	
	If lF580EdLeg
	aTmp:={}
	aTmp := aClone(ExecBlock( "F580EDLEG", .f., .f. ))

	If ValType(uRetorno) = "A"
		If Len(aTmp) >= Len(uRetorno)
			uRetorno := aClone(aTmp)	
	   		EndIf
   		EndIf
	EndIf
EndIf

Return uRetorno

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Ana Paula N. Silva     ³ Data ³28/11/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados         ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef( cTipoLib )

Default cTipoLib := "1"

//Verifica se parâmetro de Liberação por alçadas está ativo. ( MV_FINCTAL = 2 )  
If cTipoLib == "1"
    
	//Menu para Liberação Simples
	aRotina	:= {{ STR0013, "AxPesqui" , 0 , 1, ,.F.},; 	//"Pesquisar"
				{ STR0007, "fA580Man" , 0 , 2, ,.F.},; 	//"Manual"
				{ STR0002, "fA580Aut" , 0 , 4, ,.F.},; 	//"Automatica" 	
				{ STR0005, "fA580Can" , 0 , 2, ,.F.},; 	//"Cancelar "						
				{ STR0014, "fA580Leg" , 0 , 2, ,.F.} } 	//"Legenda" 	 

Else

	//Menu para liberação com controle de alçada de fundo fixo
	aRotina := { { STR0013, "AxPesqui"   , 0, 1 },;			//"Pesquisar"
                 { STR0061, "Fa581Saldo" , 0, 3 },;			//"Consulta Saldos"
                 { STR0062, "Fa580Liber" , 0, 3 },;			//"Libera Mov."
           	     { STR0057, "Fa580Anula" , 0, 3 },;			//"Estornar"
                 { STR0058, "Fa580Super" , 0, 3 },;			//"Superior"
                 { STR0059, "Fa580Transf", 0, 3 },;			//"Transfere Sup."
                 { STR0060, "Fa580Aus"   , 0, 3 },;			//"Ausência Temp."
                 { STR0090, "Fa580Desbl" , 0, 3 },;			//"Desbloqueio"                 
                 { STR0014, "Fa580Leg"   , 0, 2, , .F. } }	//"Legenda"	

EndIf

If lValSE2Sit
	aAdd( aRotina, { STR0022, "FA580Sit" , 0 , 6, , .F. } )//"Situacoes" 
EndIf
				
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada para adicionar rotinas e botoes na aRotina ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lF580ADDB
	aRotina := ExecBlock( "F580ADDB",.F.,.F.,{aRotina})
EndIf				
					
Return(aRotina)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³FinA290T   ³ Autor ³ Marcelo Celi Marques ³ Data ³ 26.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada semi-automatica utilizado pelo gestor financeiro   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FinA580T(aParam)
	cRotinaExec := "FINA580"
	ReCreateBrow("SE2",FinWindow)      		
	FinA580(aParam[1])  		
	ReCreateBrow("SE2",FinWindow)      	
	dbSelectArea("SE2")
	
	INCLUI := .F.
	ALTERA := .F.
	
Return .T.	

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FA580SIT  ºAutor  ³Fabricio Romera     º Data ³  06/21/10    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Rotina de alteracao da situacao do titulo (Suspensao		   º±±
±±º          ³e cancelamento do titulo).                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ FINA580() - Liberacao P/ Baixa                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function FA580SIT(cAlias, nRecNo, nOpc)

Local cPrefixo  := Space(TamSX3("E2_PREFIXO")[2])
Local cNumero   := Space(TamSX3("E2_NUM")[2])
Local cTipo     := Space(TamSX3("E2_TIPO")[2])
Local cParcela  := Space(TamSX3("E2_PARCELA")[2])
Local cFornLoja := Space(TamSX3("E2_PREFIXO")[2]+TamSX3("E2_PREFIXO")[2])
Local cSitAtual := Space(30)      
Local cDtCancel := Space(16)      
Local aSitNova  := {}
Local nSitNova  := "01"      
Local nSitAtual := 0
Local aCoord    := {}
Local lOk       := .F.
Local lJustCP 	:= CposJust() 
Local aBut580   := {}             
Local bOk		:= {|| lOk := .T., oDlg:End()}			  
Local cCampo     := ""
Local bChangeSit := { }
Local oDlg
Local oPanel                      
Local oGetDtCan
Local oGetGhost
Local oForn

//Verifica se o controle de alçadas está ativo. MV_FINCTAL = 2
//Se estiver ativo, termina.
If SuperGetMV( "MV_FINCTAL", .F., "1" ) == "2"
	Help( " ", 1, "Fa580Sit", , STR0074, 1, 0 )	//"Não é possível alterar a situação do título com o controle de alçadas ativo."
	Return	
EndIf

If lValSE2Sit
	bChangeSit := { || cDtCancel := CTOD(""), oDlg:CtrlRefresh(), aSE2FI2 := {}, cCampo := "", If(nSitNova == "03", cCampo:= "E2_DATACAN", cCampo:= ""), If(nSitNova == "02", cCampo:= "E2_DATASUS",), aSE2FI2 := BuildSE2FI2(cCampo) }
EndIf

Private aSE2FI2 := {}
	
	//Valida se titulo pode entrar no processo
	If ! ValidAltSE2(nRecNo,,,.T.)
		Return
	End If	
                               	
	//Carrega Informacoes do Titulo
	DbSelectArea("SE2")
	DbGoTo(nRecNo)  
	cPrefixo  := AllTrim( SE2->(E2_PREFIXO) )
	cNumero   := AllTrim( SE2->(E2_NUM) )  
	cTipo     := AllTrim( SE2->(E2_TIPO) ) 
	cParcela  := AllTrim( SE2->(E2_PARCELA) )
	cFornLoja := AllTrim( SE2->(E2_FORNECE + "/" + E2_LOJA + " - " + E2_NOMFOR) )

	If lValSE2Sit
		cDtCancel := IIf ( !Empty(SE2->(E2_LIMCAN)), SE2->(E2_LIMCAN) , CTOD("") )
	End If
	                                   
	If lValSE2Sit	   
		//Verifica Situacao Atual do Titulo
		If SE2->( !Empty(E2_USUASUS) .And. !Empty(E2_DATASUS) )
			nSitAtual := 2
		ElseIf SE2->( !Empty(E2_USUACAN) .And. !Empty(E2_DATACAN) )		
			nSitAtual := 3
		Else
			nSitAtual := 1
		End If
	End If
	
	//Carrega opcoes do combo de situacoes 
	aAdd( aSitNova, STR0052 )	//"01=Em Aprovação"
	aAdd( aSitNova, STR0053 )	//"02=Suspenso"
	aAdd( aSitNova, STR0054 )	//"03=Cancelado"
	
	cSitAtual := Right( aSitNova[nSitAtual], Len(aSitNova[nSitAtual]) - 3 )
	cSitAtual := Padc(cSitAtual, 40)            
	
	nSitNova := "0" + AllTrim( Str(nSitAtual) ) 
		
	//Botoes adicionais na EnchoiceBar
	If lJustCP // Adiciona botao para justificativa
		Aadd(aBut580,{'BAIXATIT',{||Fa580JUST(cCampo)},STR0042})		//"Justificativa"		
        dbSelectArea("SE2")
       	DbGoTo(nRecNo)
		RegToMemory("SE2",.F.,,,FunName())
		If lValSE2Sit			
			If(nSitNova == "03", cCampo:= "E2_DATACAN", cCampo:= "")
			If(nSitNova == "02", cCampo:= "E2_DATASUS",)
		End If	
		aSE2FI2 := BuildSE2FI2(cCampo)
	Endif						

	
	//Carrega array de coordenadas dos objetos
	aCoord := {000,000,250,680,; //Tela
	           012,008,031,009,; //Say Prefixo
	           010,030,030,009,; //Get Prefixo
	           012,070,031,009,; //Say Numero
	           010,095,070,009,; //Get Numero
	           012,170,031,009,; //Say Tipo
	           010,185,030,009,; //Get Tipo 
	           012,222,031,009,; //Say Parcela
	           010,250,025,009,; //Get Parcela
	           032,008,045,009,; //Say Fornecedor/Loja
	           030,065,145,009,; //Get Fornecedor/Loja
	           052,008,040,009,; //Say Situacao Atual
	           050,050,090,009,; //Get Situacao Atual
	           072,008,040,009,; //Say Nova Situacao
	           070,050,090,009,; //Cbo Nova Situacao
	           090,181,030,009,; //Button Ok
	           090,214,030,009,; //Button Cancelar 
	           072,155,060,009,; //Say Data Cancelamento
	           070,195,050,009}; //Get Data Cancelamento           

	DEFINE MSDIALOG oDlg TITLE STR0022 FROM aCoord[1],aCoord[2] TO aCoord[3],aCoord[4] OF oDlg PIXEL
	
	oPanel       := TPanel():New(0,0,"",oDlg,NIL,.T.,.F.,NIL,NIL,0,0,.T.,.F.)	
	oPanel:Align := CONTROL_ALIGN_ALLCLIENT            
	
	@ aCoord[5] ,aCoord[6] 	SAY   STR0025 /*"Prefixo"*/					SIZE aCoord[7], aCoord[8]	PIXEL OF oPanel 
	@ aCoord[9] ,aCoord[10]	MSGET cPrefixo  PICTURE "@!" 				SIZE aCoord[11],aCoord[12] 	PIXEL OF oPanel WHEN .F. CENTERED
	
	@ aCoord[13],aCoord[14] SAY   STR0026 /*"Número"*/  				SIZE aCoord[15],aCoord[16]	PIXEL OF oPanel 
	@ aCoord[17],aCoord[18]	MSGET cNumero   PICTURE "@!" 				SIZE aCoord[19],aCoord[20] 	PIXEL OF oPanel WHEN .F. CENTERED
	
	@ aCoord[21],aCoord[22]	SAY   STR0027 /*"Tipo"*/    				SIZE aCoord[23],aCoord[24]	PIXEL OF oPanel 
	@ aCoord[25],aCoord[26]	MSGET cTipo     PICTURE "@!" 				SIZE aCoord[27],aCoord[28] 	PIXEL OF oPanel WHEN .F. CENTERED
	
	@ aCoord[29],aCoord[30]	SAY   STR0028 /*"Parcela"*/ 				SIZE aCoord[31],aCoord[32]	PIXEL OF oPanel 
	@ aCoord[33],aCoord[34]	MSGET cParcela  PICTURE "@!" 				SIZE aCoord[35],aCoord[36] 	PIXEL OF oPanel WHEN .F. CENTERED

	@ aCoord[37],aCoord[38]	SAY   STR0029 /*"Fornecedor/Loja"*/ 		SIZE aCoord[39],aCoord[40]	PIXEL OF oPanel 
	@ aCoord[41],aCoord[42]	MSGET oForn VAR cFornLoja WHEN .F. CENTERED SIZE aCoord[43],aCoord[44]  PIXEL OF oPanel OBFUSCATED RetGlbLGPD("E2_NOMFOR") PICTURE "@!"	
	
	@ aCoord[45],aCoord[46]	SAY   STR0030 /*"Situação Atual"*/ 			SIZE aCoord[47],aCoord[48]	PIXEL OF oPanel 
	@ aCoord[49],aCoord[50]	MSGET cSitAtual PICTURE "@!" 				SIZE aCoord[51],aCoord[52] 	PIXEL OF oPanel WHEN .F. CENTERED

	If lValSE2Sit
		@ aCoord[53],aCoord[54] SAY   STR0031 /*"Nova Situação"*/ 			SIZE aCoord[55],aCoord[56]	PIXEL OF oPanel 
		@ aCoord[57],aCoord[58] COMBOBOX oCombo VAR nSitNova ITEMS aSitNova	SIZE aCoord[59],aCoord[60] 	PIXEL OF oPanel WHEN .T. ON CHANGE 	If(lJustCP, Eval(bChangeSit), cDtCancel := CTOD(""))
		
		@ aCoord[69],aCoord[70]	SAY   STR0032 /*"Dt. Limite Canc."*/		SIZE aCoord[71],aCoord[72]	PIXEL OF oPanel 
		@ aCoord[73],aCoord[74]	MSGET oGetDtCan VAR cDtCancel PICTURE "99/99/9999" SIZE aCoord[75],aCoord[76] PIXEL OF oPanel WHEN nSitNova = "03" HASBUTTON CENTERED
	EndIf
	@ aCoord[73],aCoord[74]	GET oGetGhost SIZE 0,0 PIXEL OF oPanel 
	
	ACTIVATE MSDIALOG oDlg CENTER ON INIT EnchoiceBar(oDlg,;
													{||If( ValidAltSE2(nRecNo, nSitNova, cDtCancel, .F.), Eval(bOk),)},; //OK
													{||oDlg:End()},, aBut580)                              			     //Cancel
	                                                        
	If lOk	      
		                                   
		If nSitAtual = Val(nSitNova)
			Return
		End If
		
		AltSitSE2(nRecNo, nSitNova, nSitAtual, cDtCancel)										

 		// Adiciona botao para envio de instrucoes de cobranca
		F580GrvFI2()
		
	End If

Return
      
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AltSitSE2  ºAutor  ³Fabricio Romera    º Data ³  06/21/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Carrega informacoes do titulo para montar tela principal.   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³FINA580SIT                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AltSitSE2(nRecNo, nSitNova, nSitAtual, dDtCancel)
				
DbSelectArea("SE2")
DbGoTo(nRecNo)

Reclock("SE2", .F.)      
    
	If lValSE2Sit
		SE2->(E2_USUASUS) := ""
		SE2->(E2_DATASUS) := CTOD("")
		SE2->(E2_USUACAN) := ""
		SE2->(E2_DATACAN) := CTOD("") 	
		SE2->(E2_LIMCAN)  := CTOD("") 	
	EndIf
	                 
	//Se titulo cancelado, sit. aguardando liberacao
	If nSitAtual = 3
		SE2->(E2_USUALIB) := ""	
	    SE2->(E2_DATALIB) := CTOD("") 		
	EndIf		    
	
	If Val(nSitNova) = 2 //Suspenso          
	    SE2->(E2_DATALIB) := CTOD("") 		
		If lValSE2Sit
			SE2->(E2_USUASUS) := cUserName
			SE2->(E2_DATASUS) := dDataBase
		EndIf
	ElseIf Val(nSitNova) = 3 //Cancelado    
		SE2->(E2_USUALIB) := cUserName					
	    SE2->(E2_DATALIB) := dDataBase
	    If lValSE2Sit
			SE2->(E2_USUACAN) := cUserName
			SE2->(E2_DATACAN) := dDataBase
			SE2->(E2_LIMCAN)  := dDtCancel
		EndIf
	EndIf

MsUnlock()

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma   ValidAltSE2ºAutor  ³Fabricio Romera    º Data ³  06/21/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida informacoes do titulo para o processo de mudanca de  º±±
±±º          ³situacao do titulo                                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³FINA580SIT                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ValidAltSE2(nRecno, nSitNova, dDtCancel, lEntrada)
Local aAreaSE2	:= SE2->(GetArea())
Local lRet		:= .F.      
Local cMsg		:= ""                                             
    
//Validacoes para entrada do titulo no processo de alteracao de situacao
If lEntrada
                                                       	                            
	DbSelectArea("SE2")
	DbGoTo(nRecNo)     
			                                                       	
	//Verifica se titulo foi baixado
	If !Empty(E2_BAIXA)	
	    cMsg := STR0034 + IIf(SE2->E2_SALDO <= 0, STR0035, STR0036) ; 	//"Titulo baixado " , "totalmente", "parcialmente" ;
	         +  STR0037			    									//", situação não pode ser alterada."
   		Aviso(STR0038, cMsg, {"Ok"}, 1, STR0033 )						//"Situação de Título"; "Aviso"

		Return lRet                                   		
	EndIf
	
	//Verifica se titulo foi associado a cheque
	If !Empty(E2_NUMBCO)
		cMsg := STR0039													//"Título associado à cheque, situação não pode ser alterada."
   		Aviso(STR0038, cMsg, {"Ok"}, 1, STR0033 ) // "Aviso"
		Return lRet
	EndIf
	
	//Verifica bordero
	If !Empty(E2_NUMBOR)
		cMsg := STR0040													//"Título associado à borderô, situação não pode ser alterada."
   		Aviso(STR0038, cMsg, {"Ok"}, 1, STR0033 ) // "Aviso"
		Return lRet
	EndIf
			
Endif		
                                                       
//Valida se data de cancelamento foi informada
If nSitNova == "03" .And. Empty(dDtCancel)
	cMsg := STR0041	//"Data limite de cancelamento não informada."
   		Aviso(STR0038, cMsg, {"Ok"}, 1, STR0033 ) // "Aviso"
	Return lRet
ElseIf nSitNova == "03" .And. Empty(SE2->E2_DATALIB) .AND. (SE2->E2_SALDO+SE2->E2_SDACRES-SE2->E2_SDDECRE) >= GETNEWPAR("MV_VLMINPG",0)
	cMsg := STR0050	//"Título deve estar liberado para efetuar o cancelamento."
   		Aviso(STR0038, cMsg, {"Ok"}, 1, STR0033) // "Aviso"
	Return lRet		
EndIf

lRet := .T.

RestArea(aAreaSE2)

Return lRet


/*           
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³Fa580JUST   ³Autor  ³ Fabricio Romera     ³ Data ³ 30/07/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Desc.     ³Historico para liberacao de titulos e mudanca de situacao do³±±
±±³          ³titulo (suspensao e cancelamento)                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA580									            	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Fa580JUST(cCampo)
Local lRet			:= .T.
Local nMaxLenAnt	:= 10
Local nMaxLenAtu	:= 10
Local nOpcA			:= 0
Local oGetDados
Local lCposJust := CposJust()
Private aHead	:= {}

If lCposJust
		
	If Empty(aSE2FI2)		
		aSE2FI2	:= BuildSE2FI2(cCampo)
	Endif
	
	If Len(aSE2FI2) > 0
		aHead	:=	{}
		AAdd(aHead, {STR0043 ,"SEQ","",2 ,0,,Nil,"C",,,,,".F.",,,,})			//"Seq"
		AAdd(aHead, {STR0042 ,"HISTOR" ,"",80,0,,Nil,"M",,,,,"FA580MEMO()",,,,})//"Justificativa"
		AAdd(aHead, {STR0044 ,"VALANT","",nMaxLenAnt,0,,Nil,"C",,,,,".F.",,,,})	//"Valor Anterior"
		AAdd(aHead, {STR0045 ,"VALATU","",nMaxLenAtu,0,,Nil,"C",,,,,".F.",,,,})	//"Valor Atual"
		AAdd(aHead, {STR0046 ,"NOMCPO","",10,0,,Nil,"C",,,,,".F.",,,,})			//"Nome Campo "
		AAdd(aHead, {STR0047 ,"TIPCPO","",1 ,0,,Nil,"C",,,,,".F.",,,,})			//"Tipo Campo "
		DEFINE FONT oBold NAME "Arial" SIZE 0, -13 BOLD
		DEFINE MSDIALOG oDlg FROM 88 ,22  TO 350,619 TITLE STR0048 Of oMainWnd PIXEL	//"Histórico"
		@ 14 ,03   TO 40 ,296 LABEL '' OF oDlg PIXEL
		@ 19 ,10   SAY STR0049 Of oDlg PIXEL SIZE 280 ,30 FONT oBold COLOR CLR_BLUE		//"A justificativa deverá conter no mínimo 5 caracteres."
		
		oGetDados := MsNewGetDados():New(45,3,120,296,GD_UPDATE,/*"fa050juLok"*/,/*"fa050juTOk"*/,,,,Len(aSE2FI2),,,,oDlg,aHead,aSE2FI2)
		
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpca:=1,If(oGetDados:TudoOk(),oDlg:End(),nOpca := 0)},{||oDlg:End()}) CENTER
		
		If nOpca==1
			aSE2FI2:=aClone(oGetDados:aCols)
		Else
			lRet := .F.
		EndIf
	EndIf
Endif

Return lRet


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³BuildSE2FI2 ³Autor  ³ Marcelo Pimentel    ³ Data ³ 23/11/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna os dados do array aSE2FI2                          ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ BuildSE2FI2                                                ³±±
±±³          ³    aCpos  : nomes dos campos que serao avaliados           ³±±
±±³          ³    aDados : dados dos campos de acpos                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA050                	            					  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function BuildSE2FI2(cCampo)
Local aItems 		:= {}
Local cRet			:=  ""
Local cItems		:=  ""
Local lCposJust := CposJust()                       

If cCampo = "" 
	Return {}
End If

If lCposJust
	//Monta Interface para incluir as JUSTIFICATIVAS
	dbSelectArea("FI2")
	DbSetOrder(3)
	If DbSeek(xFilial("FI2")+"2"+M->E2_NUMBOR+M->E2_PREFIXO+M->E2_NUM+M->E2_PARCELA+M->E2_TIPO+M->E2_FORNECE+M->E2_LOJA)
		While !Eof() .and. FI2->FI2_NUMBOR+FI2->FI2_PREFIX+FI2->FI2_TITULO+FI2->FI2_PARCEL+FI2->FI2_TIPO+FI2->FI2_CODFOR+FI2->FI2_LOJFOR == ;
			M->E2_NUMBOR+M->E2_PREFIXO+M->E2_NUM+M->E2_PARCELA+M->E2_TIPO+M->E2_FORNECE+M->E2_LOJA .AND. ;
			xFilial("FI2") == FI2->FI2_FILIAL
			If FI2->FI2_CAMPO = cCampo
				cRet := MSMM(FI2->FI2_HISTOR,80)
				Aadd(aItems,{FI2->FI2_SEQ,cRet,FI2->FI2_VALANT,FI2->FI2_VALNOV,FI2->FI2_CAMPO,FI2->FI2_TIPCPO,.F.})
			End If
			dbSkip()
		EndDo
	EndIf
		
	cItems	:= StrZero(Len(aItems)+1,2)
	Aadd(aItems,{cItems," " ,DtoC(M->&(cCampo)),DtoC(dDataBase),cCampo,"D",.F.})

Endif
Return aItems

/*/               
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³F050GrvFI2  ³Autor  ³ Marcelo Pimentel    ³ Data ³ 23/11/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava a tabela FI2 com as justificativas                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ F050GrvFI2: O SE2 deve estar poscionado, os dados que serao³±±
±±³          ³    gravados devem estar na variavel publica aItemsFI2.     ³±±
±±³          ³    aItemsFI2[x][1]: Justificativa                          ³±±
±±³          ³    aItemsFI2[x][2]: Titulo do campo (nao utilizado)        ³±±
±±³          ³    aItemsFI2[x][3]: Valor anterior                         ³±±
±±³          ³    aItemsFI2[x][4]: Novo valor                             ³±±
±±³          ³    aItemsFI2[x][5]: Nome do campo                          ³±±
±±³          ³    aItemsFI2[x][6]: Tipo do campo                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA050						            				  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function F580GrvFI2(lGravaLog)
Local nX		:=	1
Local aArea 	:= GetArea()
Local cJustific	:= ""
Local lCposJust := CposJust()
DEFAULT lGravaLog := .F.                                         

If lGravaLog 
	lCposJust := .T.
EndIf	

If lCposJust .and. Type('aSE2FI2') == "A" .And. Len(aSE2FI2) > 0
	FI2->(DbSetOrder(3))
	For nX:=1 To Len(aSE2FI2)
		If !Empty(aSE2FI2[nX][2])
			cChave	:=	xFilial("FI2")+"2"+SE2->(E2_NUMBOR+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA)+aSE2FI2[nX][1]+"2"
			// Pesquisa pela ocorrencia nao gerada (FI2_GERADO = 2 - Ja esta na chave)
			If FI2->(DbSeek(cChave))
				RecLock('FI2',.F.)
				Replace FI2_DTGER WITH dDataBase
				MsUnLock()
			Else
				RecLock('FI2',.T.)
				Replace FI2_FILIAL 	WITH xFilial("FI2")
				Replace FI2_CARTEI  WITH "2"
				Replace FI2_GERADO  WITH "2"
				Replace FI2_NUMBOR 	WITH SE2->E2_NUMBOR
				Replace FI2_PREFIX	WITH SE2->E2_PREFIXO
				Replace FI2_TITULO	WITH SE2->E2_NUM
				Replace FI2_PARCEL	WITH SE2->E2_PARCELA
				Replace FI2_TIPO  	WITH SE2->E2_TIPO
				Replace FI2_CODFOR	WITH SE2->E2_FORNECE
				Replace FI2_LOJFOR	WITH SE2->E2_LOJA
				Replace FI2_DTOCOR	WITH dDataBase
				Replace FI2_VALANT	WITH aSE2FI2[nX][3]
				Replace FI2_VALNOV	WITH aSE2FI2[nX][4]
				Replace FI2_CAMPO 	WITH aSE2FI2[nX][5]
				Replace FI2_TIPCPO	WITH aSE2FI2[nX][6]
				Replace FI2_SEQ  	WITH aSE2FI2[nX][1]
				cJustific := __CUSERID + " -  " + Dtoc(dDatabase) + " / " + Substr(Time(),1,5) + STR0050 	
				MsUnLock()
				
				MSMM(FI2_HISTOR,,,cJustific+aSE2FI2[nX][2],1,,,"FI2","FI2_HISTOR")
			Endif
		Endif
	Next nX
Endif
RestArea(aArea)
Return(.T.)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³FA580MEMO ³ Autor ³ Fabricio Romera       ³ Data ³ 23.11.05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida se campo MEMO para modo de alteracao ou visualizacao ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³             		    									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA580           										  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FA580MEMO()
Local lRet	:= .T.
/*
FI2->(dbsetorder(3))
If FI2->(DbSeek(xFilial("FI2")+"2"+M->E2_NUMBOR+M->E2_PREFIXO+M->E2_NUM+M->E2_PARCELA+M->E2_TIPO+M->E2_FORNECE+M->E2_LOJA+StrZero(n,2)))
	lRet := .F.
EndIf
*/
Return lRet    

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Função    ³ ValSE2Situac ³ Autor ³ Danilo Dias      ³ Data ³ 07/12/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descrição ³ Valida campos incluídos na tabela SE2 para controle       ³±±
±±³           ³ de situação  										   	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe	  ³ lValSE2Sit    										      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		  ³ FINA580           										  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ValSE2Situac() As Logical

Local aArea    As Array
Local aAreaSE2 As Array
Local aCampos  As Array
Local nPos	   As Numeric
Local lRet 	   As Logical

aArea 	 := GetArea()     
aAreaSE2 := {}
aCampos  := {}	
nPos	 := 0
lRet	 := .T.
If Select( "SE2" ) > 0
	aAreaSE2 := SE2->(GetArea())
EndIf

aCampos := FWSX3Util():GetAllFields("SE2")
nPos := aScan( aCampos, { |aX3Cpo| aX3Cpo $ "E2_USUASUS|E2_DATASUS|E2_USUACAN|E2_DATACAN|E2_LIMCAN|" } )

If nPos > 0
	lRet := .T.
Else
	lRet := .F.
EndIf

If Len( aAreaSE2 ) > 0
	RestArea( aAreaSE2 )
EndIf
RestArea( aArea )
	
Return lRet       


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ FaSldAbat   ºAutor  ³ Wilson P.Godoi  º Data ³ 16/01/2014  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Procura os Abatimentos na SE2 para Atualizar o Saldo do    º±±
±±º          ³ Aprovador corretamente.                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ FINA580                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function FaSldAbat(lAbat)
Local nSalvRec 	:= SE2->(RecNO())
Local cNum	  	:= SE2->E2_NUM
Local cPrefixo 	:= SE2->E2_PREFIXO
Local cParcela 	:= SE2->E2_PARCELA
Local cFornece 	:= SE2->E2_FORNECE
Local cLoja	  	:= SE2->E2_LOJA
Local cTipo		:= SE2->E2_TIPO
Local nTotAbat 	:= 0
Local nValPadrao:= 0
Local dAnter 	:= ""	
Local cMoedaSE2	:= IIF(Empty(SE2->E2_MOEDA),"1",AllTrim(Str(SE2->E2_MOEDA,2)))
Local cDescMoeda:= ""
Local dDataCan := ""                              
PRIVATE dBaixa	  := CriaVar("E2_BAIXA")

	dbSelectArea("SE2")
	
	*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	*³Carrega as variaveis utilizadas para receber os dados do titulo		   ³
	*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nSalvRec 	:= SE2->(RecNO())
	cNum	  	:= SE2->E2_NUM
	cPrefixo 	:= SE2->E2_PREFIXO
	cParcela 	:= SE2->E2_PARCELA
	cFornece 	:= SE2->E2_FORNECE
	cLoja	  	:= SE2->E2_LOJA
	cTipo		:= SE2->E2_TIPO
	nTotAbat 	:= 0
	nValPadrao 	:= 0
	dAnter 		:= dBaixa	
	cMoedaSE2	:= IIF(Empty(SE2->E2_MOEDA),"1",AllTrim(Str(SE2->E2_MOEDA,2)))
	cDescMoeda  := ""
	dDataCan	:= SE2->E2_DATACAN //FRS - Sit. Titulo

	*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	*³Soma Titulos Abatimentos												³
	*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nTotAbat:=SumAbatPag( cPrefixo, cNum, cParcela, cFornece, SE2->E2_MOEDA, "S", dBaixa, cLoja )
	SE2->( dbGoTo( nSalvRec ) )
	*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	*³Recebe os dados do titulo a ser baixado								³
	*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lCont := .F.
    
	nValTot		:= SE2->E2_VLCRUZ
	nValEstrang := SE2->E2_SALDO-nTotAbat
	nTotal 		:= nValEstrang

Return ntotal 


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ Fa580Liber  ºAutor  ³ Danilo Dias     º Data ³ 11/05/2011  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Liberação de movimentos pelo aprovador usando regras de    º±±
±±º          ³ alçada por fundo fixo.                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ FINA580                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Fa580Liber( cAlias, nReg )
       
Local aArea     := GetArea()
Local cUsrAprov := __cUserId
Local cCodAprov := SE2->E2_CODAPRO
Local cCodAprSis:= ""
Local cCodUsrApr:= ""  
Local cCodGrupo := ""
Local cNivelApr := "" 
Local cMoeda    := strzero(SE2->E2_MOEDA,TamSx3("FRP_MOEDA")[1]) //cValToChar(SE2->E2_MOEDA)//alterado por Fernando Radu Muscalu em 01/09/2011
Local dDtRef    := dDataBase  
Local dEmissao  := SE2->E2_EMISSAO
Local nTotal    := SE2->E2_VALOR
Local nNumTit   := SE2->E2_NUM
Local nSldAprov := 0 
Local lRet      := .T.    
Local nOpc      := 0
Local nOpca		:= 0
Local lValMaxMin  := .T.
Local lAprovVal := .F. 
Local lAbat     := .T.
Local cNum	  	:= SE2->E2_NUM
Local cPrefixo 	:= SE2->E2_PREFIXO
Local cParcela 	:= SE2->E2_PARCELA
Local cFornece 	:= SE2->E2_FORNECE
Local cLoja	  	:= SE2->E2_LOJA
Local nMoedTit	:= SE2->E2_MOEDA

//Caso o título fosse gerado antes do preenchimento do parâmetro, busca o aprovador novamente na liberação.
//Evitando travar o processo de aprovação.
if empty(cCodAprov)
	cCodAprov := FA050Aprov(nMoedTit)
	Reclock("SE2", .F.)
		SE2->E2_CODAPRO	:= cCodAprov
	SE2->(MsUnlock())
endif

dbSelectArea("FRP")
FRP->(dbSetOrder(2)) //FRP_FILIAL+FRP_USER+FRP_MOEDA
FRP->(dbSeek(xFilial("FRP")+cUsrAprov+cMoeda))
cCodAprSis := FRP->FRP_COD
//Obtenho o codigo de usuario do sistema do aprovador cadastrado na solicitacao
cCodUsrApr := GetAdvFVal("FRP","FRP_USER",xFilial("FRP")+cUsrAprov+cMoeda,1,"",.T.)

//Verifico se o aprovador cadastrado na solicitacao faz parte de um grupo de aprovadores
dbSelectArea("FRR")
FRR->(dbSetOrder(2)) //FRR_FILIAL+FRR_USER+FRR_MOEDA
If FRR->(dbSeek(xFilial("FRR")+cUsrAprov+cMoeda))
	cCodGrupo	:= FRR->FRR_COD
	cNivelApr	:= FRR->FRR_NIVEL 
                      
	//Verifico se o usuario da aprovacao faz parte do mesmo grupo e valido se o nivel eh igual ou superior
	FRR->(dbSetOrder(1)) //FRR_FILIAL+FRR_COD+FRR_MOEDA+FRR_ITEM
	If FRR->(dbSeek(xFilial("FRR")+cCodGrupo+cMoeda))
		While FRR->(!Eof()) .And. FRR->(FRR_FILIAL+FRR_COD+FRR_MOEDA) == xFilial("FRR")+cCodGrupo+cMoeda

			If FRR->FRR_CODGES == cCodAprSis

				If Val(FRR->FRR_NIVEL) >= Val(cNivelApr)
					lAprovVal := .T.
				EndIf

				Exit
			EndIf
		
			FRR->(dbSkip())
		EndDo
	EndIf
EndIf

//Verifica se o gestor faz parte de grupo isento do processo de analise do valor max e min
If lAprovVal
 	If GetAdvFVal("FRR","FRR_AUTLIM",xFilial("FRR")+cCodGrupo+cMoeda,1,"",.T.) == "2"
 		lValMaxMin := .F.
	EndIf
EndIf

//Busca dados do Gestor Aprovador do Movimento
dbSelectArea("FRP")
FRP->(dbSetOrder(1))
	
If Empty(cCodAprov)
	Help( " ", 1, "Fa580Liber", , STR0075, 1, 0 )	//"Título a liberar não possui aprovador."	
Else
	FRP->(dbSeek( xFilial("FRP") + cCodAprov + cMoeda )) 
	//Verifica se usuário é o aprovador
	If cUsrAprov != FRP->FRP_USER			
    	Help( " ", 1, "Fa580Liber", , STR0076, 1, 0 )	//"Usuario não é o aprovador para esse titulo"
    ElseIf FRP->FRP_MSBLQL  == '1'   					// Aprovador bloqueado
		Help( " ", 1, "Fa580Liber", ,STR0097, 1, 0 )    // "Gestor Aprovador não tem permissao para executar este processo"
	ElseIf SE2->E2_SALDO == 0
		Help( " ", 1, "Fa580Liber", , STR0094, 1, 0 )	//"Título sem saldo para liberação para baixa."
	ElseIf  SE2->E2_SALDO < FRP->FRP_LIMMIN .AND. lValMaxMin
		Help( " ", 1, "Fa580Liber", , STR0095, 1, 0 )	//"Valor do título não atinge limite minimo estipulado para o Gestor."
	ElseIf SE2->E2_SALDO > FRP->FRP_LIMMAX .AND. lValMaxMin
		Help( " ", 1, "Fa580Liber", , STR0096, 1, 0 )	//"Valor do título ultrapassa limite máximo estipulado para o Gestor."
	Else 
		FRP->(dbSetOrder(2))
		If FRP->(dbSeek( xFilial("FRP") + cUsrAprov + cMoeda ))
					
			//Título aguardando liberação
			If SE2->E2_STATLIB == "01" .Or. ( Empty(SE2->E2_STATLIB) .And. Empty(SE2->E2_DATALIB) )

				If lRet                                                                                       
					nOpca:= 1
					nOpc := Fa581CfMov( STR0077, , "SE2", nNumTit, dEmissao, dDtRef, nTotal, "E2_VALOR", nOpca )	//"Liberação de Título a Pagar"

					If nOpc == 1 .and. lRet	//Título aprovado
						
						Begin Transaction
							
						//Verifica se tem Abatimento ou Desconto
						nTotal:= FaSldAbat(lAbat)							



							//Verifica saldo do aprovador
							lRet := Fa550ValSld( cUsrAprov, cCodAprov, cMoeda, dDtRef, nTotal, @nSldAprov )
							If lRet                      
								//Atualiza saldo do aprovador
								lRet := FXALCGrava( cUsrAprov, cCodAprov, cMoeda, "2", dDtRef, nTotal )
							EndIf
								
							If lRet
								//Se tem saldo libera Título
								RecLock( "SE2", .F. )
									SE2->E2_STATLIB := "03"
									SE2->E2_DATALIB := dDataBase
									SE2->E2_USUALIB := cUserName		
								MsUnlock() 
	
								dbSetOrder(1)
								dbSeek(xFilial("SE2")+cPrefixo+cNum+cParcela)
								While !SE2->(Eof()) .And. E2_FILIAL==xFilial("SE2") .And. SE2->E2_NUM == cNum .And.  SE2->E2_PREFIXO == cPrefixo .And.  SE2->E2_PARCELA == cParcela;
										          .And. SE2->E2_FORNECE == cFornece .And. SE2->E2_LOJA == cLoja
						    		//Atualiza status do Movimento Abatimentos
									IF SE2->E2_TIPO $ MVABATIM
										RecLock( "SE2", .F. )
											SE2->E2_STATLIB := "03"
											SE2->E2_DATALIB := dDataBase
											SE2->E2_USUALIB := cUserName		
										MsUnlock() 
									EndIf
									SE2->(DbSkip())     
								EndDo  
							EndIf
						End Transaction
					ElseIf nOpc == 2 .and. lRet	//Bloquear Título
						RecLock( "SE2", .F. )
						SE2->E2_STATLIB  := "04"		
						MsUnlock()	
					EndIf
				EndIf
			   
			//Título bloqueado aguardando outros níveis
			ElseIf SE2->E2_STATLIB == "02"                                    
				Help( " ", 1, "Fa580Liber", , STR0078, 1, 0 )	//"Título está bloqueado aguardando outros níveis."
				
			//Título já liberado           
			ElseIf SE2->E2_STATLIB == "03" .Or. SE2->E2_STATLIB == "05" .Or. !Empty(SE2->E2_DATALIB)
				Help( " ", 1, "Fa580Liber", , STR0079, 1, 0 )	//"Título já foi liberado."
				
			//Título bloqueado pelo usuário
			ElseIf SE2->E2_STATLIB == "04"                                    
				Help( " ", 1, "Fa580Liber", , STR0080, 1, 0 )	//"Título foi bloqueado por usuário."
			EndIf
		
		Else
			Help( " ", 1, "Fa580Liber", , STR0081, 1, 0 )	//"Usuário não é um Gestor Financeiro ou não tem permissão para a moeda deste título."
		EndIf
	EndIf
EndIf 

RestArea( aArea )

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ Fa580Anula   ºAutor  ³ Danilo Dias    º Data ³ 11/05/2011  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Estornar a liberação do título corrente.                   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ FINA580                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Fa580Anula( cAlias, nReg )
                 
Local aArea     := GetArea()
Local lRet      := .T.
Local cUsrAprov := __cUserId
Local cCodAprov := SE2->E2_CODAPRO
Local dDtRef    := SE2->E2_DATALIB
Local dEmissao  := SE2->E2_EMISSAO
Local nTotal    := SE2->E2_VALOR  
Local nNumTit   := SE2->E2_NUM
Local cMoeda    := strzero(SE2->E2_MOEDA,TamSx3("FRP_MOEDA")[1])//cValToChar(SE2->E2_MOEDA)//alterado por Fernando Radu Muscalu em 01/09/2011
Local nOpc      := 0
Local lAbat     := .F.  
Local cNum	  	:= SE2->E2_NUM
Local cPrefixo 	:= SE2->E2_PREFIXO
Local cParcela 	:= SE2->E2_PARCELA
Local cFornece 	:= SE2->E2_FORNECE
Local cLoja	  	:= SE2->E2_LOJA

//Verifica se título foi pago, mesmo parcialmente
If Empty(SE2->E2_BAIXA) .And. Empty(SE2->E2_NUMBOR) .And. Empty(SE2->E2_IMPCHEQ)   
	
	//Título liberado, termina.
	If SE2->E2_STATLIB == "03" .Or. SE2->E2_STATLIB == "05"
		        
	    //Busca dados do Gestor Aprovador do Movimento
		dbSelectArea("FRP")
		FRP->(dbSetOrder(2))
		
		If FRP->(dbSeek( xFilial() + cUsrAprov + cMoeda )) 
		
	    	//Verifica se usuário é o aprovador
	    	If FRP->FRP_MSBLQL  == '2' //Aprovador debloqueado
		    	//Verifica se usuário é o aprovador
				If FRP->FRP_COD == cCodAprov		
					nOpc := Fa581CfMov( STR0082, .F., "SE2", nNumTit, dEmissao, dDtRef, nTotal, "E2_VALOR" )	//"Cancelamento da Liberação de Título a Pagar"
					
					If nOpc == 1	//Confirma estorno
						Begin Transaction
				        
						//Verifica se tem Abatimento ou Desconto
						nTotal:= FaSldAbat(lAbat)							

				    	//Atualiza o saldo do aprovador
						lRet := FXALCGrava( cUsrAprov, cCodAprov, cMoeda, "3", dDtRef, nTotal )

						If lRet				
							//Atualiza status do Movimento
							RecLock( "SE2", .F. )
								SE2->E2_STATLIB := "01"
								SE2->E2_DATALIB := CTOD("  /  /  ")
								SE2->E2_USUALIB := ""
							MsUnlock()     

							dbSetOrder(1)
							dbSeek(xFilial("SE2")+cPrefixo+cNum+cParcela)
							While !SE2->(Eof()) .And. E2_FILIAL==xFilial("SE2") .And. SE2->E2_NUM == cNum .And.  SE2->E2_PREFIXO == cPrefixo .And.  SE2->E2_PARCELA == cParcela;
									          .And. SE2->E2_FORNECE == cFornece .And. SE2->E2_LOJA == cLoja
					    		//Atualiza status do Movimento Abatimentos
								IF SE2->E2_TIPO $ MVABATIM
									RecLock( "SE2", .F. )
										SE2->E2_STATLIB := "01"
										SE2->E2_DATALIB := CTOD("  /  /  ")
										SE2->E2_USUALIB := ""
									MsUnlock()
								EndIf
								SE2->(DbSkip())     
							EndDo

						EndIf				
						End Transaction 
					EndIf       
			    Else
			    	Help( " ", 1, "Fa580Anula", , STR0076, 1, 0 )	//"Usuário não é o aprovador para esse título."
			    EndIf
			Else
				Help( " ", 1, "Fa580Liber", , STR0097 , 1, 0 ) // Aprovador esta como bloqueado
			EndIf	
		Else
			Help( " ", 1, "Fa580Anula", , STR0081, 1, 0 )	//"Usuário não é um Gestor Financeiro ou não tem permissão para a moeda deste título."
		EndIf
	Else
		Help( " ", 1, "Fa580Anula", , STR0083, 1, 0 )	//"Título não foi liberado."	
	EndIf
	
Else
	Help( " ", 1, "Fa580Anula", , STR0089, 1, 0 )	//"Título teve pagamento realizado. Não é possível Anular a liberação."	
EndIf
	
RestArea( aArea )

Return   

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ Fa580Super ºAutor  ³ Danilo Dias      º Data ³ 11/05/2011  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Libera título a pagar pelo aprovador superior do aprovador º±±
±±º          ³ original do título.                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ FINA580                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Fa580Super( cAlias, nReg )
                 
Local aArea     := GetArea()
Local cUsrAprov := __cUserId
Local cCodAprov := SE2->E2_CODAPRO
Local cCodSup   := Fa006User( cUsrAprov, .F., 2, SE2->E2_MOEDA)	//Retorna o código de gestor do usuário logado
Local cMOeda    := strzero(SE2->E2_MOEDA,TamSx3("FRP_MOEDA")[1])//cValToChar(SE2->E2_MOEDA)//alterado por Fernando Radu Muscalu em 01/09/2011
Local dDtRef    := dDataBase
Local dEmissao  := SE2->E2_EMISSAO
Local nTotal    := SE2->E2_VALOR  
Local nNumTit   := SE2->E2_NUM
Local nSldAprov := 0 
Local lRet      := .T.  
 
//Verifica se usuário é superior do aprovador do Movimento
dbSelectArea("FRS")
FRS->(dbSetOrder(3))
	
If FRS->(dbSeek( xFilial() + "2" + cCodAprov + cCodSup ))
	
	cCodAprov := cCodSup  
		
		//Movimento aguardando liberação
		If SE2->E2_STATLIB == "01" .Or. ( Empty(SE2->E2_STATLIB) .And. Empty(SE2->E2_DATALIB) )
	            
            dbSelectArea("FRP")
            FRP->(dbSetOrder(1))
	    	FRP->(dbGoTop())
		    	
	    	If FRP->(dbSeek (xFilial() + cCodSup + cMoeda)) 
	    		If FRP->FRP_MSBLQL  == '2' //Aprovador debloqueado
					nOpc := Fa581CfMov( STR0084, , "SE2", nNumTit, dEmissao, dDtRef, nTotal, "E2_VALOR" )	//"Liberação de título pelo Gestor Superior"
	    		Else
	    			Help( " ", 1, "Fa580Liber", , STR0097 , 1, 0 ) // Aprovador esta como bloqueado
					nOpc := 0
				End	
	    	Else
	    		Help( " ", 1, "Fa580Transf", , STR0085, 1, 0 )	//"Superior não é um Gestor Financeiro."
	    		nOpc := 0
	    	EndIf
	            
            If nOpc == 1
	 			//Verifica saldo do gestor
			    lRet := Fa550ValSld( cUsrAprov, cCodAprov, cMoeda, dDtRef, nTotal, @nSldAprov )   	
				    
				If lRet
					Begin Transaction
						
					lRet := FXALCGrava( cUsrAprov, cCodAprov, cMoeda, "2", dDtRef, nTotal )
						
					If lRet
						//Se tem saldo libera título
						RecLock( "SE2", .F. )
						SE2->E2_STATLIB := "05"
						SE2->E2_CODAPRO := cCodSup
						SE2->E2_DATALIB := dDtRef
						SE2->E2_USUALIB := cUserName		
						MsUnlock()
					EndIf
						
					End Transaction
				EndIf
			ElseIf nOpc == 2
				RecLock( "SE2", .F. )
				SE2->E2_STATLIB := "04"
				SE2->E2_CODAPRO := cCodSup	
				MsUnlock()	
			EndIf
		   
		//Título bloqueado aguardando outros níveis
		ElseIf SE2->E2_STATLIB == "02"                                    
			Help( " ", 1, "Fa580Super", , STR0078, 1, 0 )	//"Título está bloqueado aguardando outros níveis."
			
		//Título já liberado           
		ElseIf SE2->E2_STATLIB == "03" .Or. SE2->E2_STATLIB == "05" .Or. !Empty(SE2->E2_DATALIB)
			Help( " ", 1, "Fa580Super", , STR0079, 1, 0 )	//"Título já foi liberado."
			
		//Título bloqueado pelo usuário
		ElseIf SE2->E2_STATLIB == "04"                                    
			Help( " ", 1, "Fa580Super", , STR0080, 1, 0 )	//"Título foi bloqueado por usuário."
		EndIf
Else
	Help( " ", 1, "Fa580Super", , STR0086, 1, 0 )	//"Apenas um superior do aprovador pode liberar um Título através dessa opção."
EndIf
	
RestArea( aArea )

Return 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ Fa580Transf ºAutor  ³ Danilo Dias     º Data ³ 11/05/2011  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Transfere a responsabilidade da liberação para o superior  º±±
±±º          ³ do aprovador original.                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ FINA580                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Fa580Transf( cAlias, nReg )
                 
Local aArea     := GetArea()
Local cUsrAprov := __cUserId
Local cCodAprov := SE2->E2_CODAPRO
Local cMOeda    := strzero(SE2->E2_MOEDA,TamSx3("FRP_MOEDA")[1])//cValToChar(SE2->E2_MOEDA)//alterado por Fernando Radu Muscalu em 01/09/2011
Local dDtRef    := dDataBase
Local dEmissao  := SE2->E2_EMISSAO
Local nTotal    := SE2->E2_VALOR  
Local nNumTit   := SE2->E2_NUM
 
//Verifica se usuário é gestor financeiro
dbSelectArea("FRP")
FRP->(dbSetOrder(2))
	
If FRP->(dbSeek( xFilial() + cUsrAprov + cMoeda ))
		
	If FRP->FRP_MSBLQL  == '2' //Aprovador desbloqueado
	 		
		//Movimento aguardando liberação
		If SE2->E2_STATLIB == "01" .Or. ( Empty(SE2->E2_STATLIB) .And. Empty(SE2->E2_DATALIB) )
		            
            //Verifica se usuário é o aprovador
			If FRP->FRP_COD == cCodAprov  	
	
			    dbSelectArea("FRS")
			    FRS->(dbSetOrder(1))                               
				    
			    //Verifica se há superior vinculado na tabela FRS - Amarração Gestor X Superior
			    If FRS->(dbSeek( xFilial() + "2" + cCodAprov + "01" ))
				    	
			    	FRP->(dbSetOrder(1))
			    	FRP->(dbGoTop())
			    	If FRP->(dbSeek (xFilial() + FRS->FRS_CODSUP + cMoeda))
			    		nOpc := Fa581CfMov( STR0087, , "SE2", nNumTit, dEmissao, dDtRef, nTotal, "E2_VALOR" )	//"Transferência de Aprovador do Título"
			    	Else
			    		Help( " ", 1, "Fa580Transf", , STR0085, 1, 0 )	//"Superior não é um Gestor Financeiro."
			    		nOpc := 0
			    	EndIf
		    	
			    	If nOpc == 1			    
						RecLock( "SE2", .F. )
						SE2->E2_CODAPRO := FRS->FRS_CODSUP
						SE2->E2_DATALIB := CTOD("  /  /  ")
						SE2->E2_USUALIB := ""		
						MsUnlock()
				 	ElseIf nOpc == 2
				 		RecLock( "SE2", .F. )              
				 		SE2->E2_STATLIB := "04"
						SE2->E2_CODAPRO := FRS->FRS_CODSUP
						SE2->E2_DATALIB := CTOD("  /  /  ")
						SE2->E2_USUALIB := ""		
						MsUnlock()
				 	EndIf
				Else
			   		Help( " ", 1, "Fa580Transf", , STR0088, 1, 0 )	//"Usuário não possui superior vinculado."		
				EndIf
						        
	   		Else
		    	Help( " ", 1, "Fa580Transf", , STR0076, 1, 0 )	//"Usuário não é o aprovador deste Título."
		    EndIf
			    
		//Título bloqueado
		ElseIf SE2->E2_STATLIB == "02" .Or. SE2->E2_STATLIB == "04"                                    
			Help( " ", 1, "Fa580Transf", , STR0080, 1, 0 )	//"Título foi bloqueado por usuário."
			
		//Título já liberado           
		ElseIf SE2->E2_STATLIB == "03" .Or. SE2->E2_STATLIB == "05" .Or. !Empty(SE2->E2_DATALIB)
			Help( " ", 1, "Fa580Transf", , STR0079, 1, 0 )	//"Título já foi liberado."
		EndIf                                                                        
	Else
		Help( " ", 1, "Fa580Transf", , STR0097 , 1, 0 ) // Gestor esta como bloqueado
	Endif	

Else
	Help( " ", 1, "Fa580Transf", , STR0081, 1, 0 )	//"Usuário não é um Gestor Financeiro ou não tem permissão para a moeda deste Movimento."
EndIf
	
RestArea( aArea )

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ Fa580Aus   ºAutor  ³ Danilo Dias      º Data ³ 11/05/2011  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Transfere a responsabilidade da liberação por ausência     º±±
±±º          ³ temporária.                                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ FINA580                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Fa580Aus()

Local aArea     := GetArea() 
Local aAreaSE2  := SE2->(GetArea()) 
Local cMarca    := GetMark()
Local cCodSup   := Fa006User( __cUserID, .F., 2 )	//Retorna o código de gestor do usuário logado

//Carrega dialog para seleção dos títulos do aprovador a transferir
nOpc := Fa581Mark( "SE2", "E2_OK", cMarca, cCodSup )
	
//Se confirmado altera o código do aprovador do título
If nOpc == 1
	SE2->(dbGoTop())
		
	While SE2->(!Eof())
		If SE2->E2_OK = cMarca
			RecLock( "SE2", .F. )
			SE2->E2_CODAPRO := cCodSup
			SE2->E2_DATALIB := CTOD("  /  /  ")
			SE2->E2_USUALIB := ""		
			MsUnlock()
		EndIf
		SE2->(dbSkip())
	EndDo
EndIf
	
SE2->(dbClearFilter())

RestArea(aAreaSE2)
RestArea(aArea)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ Fa580Desbl   ºAutor  ³ Marcos R. Pires º Data ³ 03/10/2011 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Desbloqueia o titulo desde que o usuário seja  o aprovador º±±
±±º          ³ corrente ou o aprovador superior                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ FINA580                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Fa580Desbl( cAlias, nReg )
Local aArea     := GetArea()
Local cUsrAprov := __cUserId
Local cCodAprov := SE2->E2_CODAPRO
Local dDtRef    := dDataBase
Local cMoeda    := StrZero(SE2->E2_MOEDA,TamSx3("FRP_MOEDA")[1])
Local dEmissao  := SE2->E2_EMISSAO
Local nTotal    := SE2->E2_VALOR  
Local nNumTit   := SE2->E2_NUM
Local lContinua	:= .T.
Local lAchou	:= .F.
Local nOpc      := 0
Local cCodApUsr	:= "" //Codigo do aprovador do utilizador do sistema
    
//Verifica se o titulo está bloqueado
If lContinua .And. SE2->E2_STATLIB != "04"
	lContinua := .F.
	Help( " ", 1, "Fa580Desbl", , STR0091, 1, 0 ) //"Titulo não bloqueado"
EndIf

If lContinua .AND. FRP->FRP_MSBLQL  == '1' //Aprovador bloqueado
	Help( " ", 1, "Fa580Liber", , STR0097 , 1, 0 ) // Aprovador esta como bloqueado
	lContinua := .F.
EndIf

//Verifica se o usuário esta cadastrado na FRP
dbSelectArea("FRP")
dbSetOrder(2) //FRP_FILIAL+FRP_USER+FRP_MOEDA
If FRP->(dbSeek(xFilial("FRP") + cUsrAprov + cMoeda))
	cCodApUsr := FRP->FRP_COD
Else
	lContinua := .F.
	Help( " ", 1, "Fa580Liber", , STR0081, 1, 0 )	//"Usuário não é um Gestor Financeiro ou não tem permissão para a moeda deste título."
EndIf
 
//Verifica se o Usuário logado é o aprovador corrente ou o aprovador superior
If lContinua
	If cCodAprov != cCodApUsr
		dbSelectArea("FRS")
		dbSetOrder(2) //FRS_FILIAL+FRS_CODAPR
		If dbSeek(xFilial("FRS") + cCodAprov)
			While FRS->(!Eof()) .And. FRS->FRS_FILIAL == xFilial("FRS") .And. FRS->FRS_CODAPR == cCodAprov
				If FRS->FRS_CODSUP == cCodApUsr
					lAchou	:= .T.
				EndIf
			FRS->(dbSkip())
			EndDo
		Else
			lContinua := .F.
			Help( " ", 1, "Fa580Desbl", , STR0088, 1, 0 )	//"Usuário não possui superior vinculado."
		EndIf
		If !lAchou
			lContinua := .F.
			Help( " ", 1, "Fa580Desbl", , STR0092, 1, 0 )	//"Usuário não é o aprovador superior do aprovador do titulo"
		EndIf
	EndIf
EndIf

If lContinua
	nOpc := Fa581CfMov( STR0093, .F. , "SE2", nNumTit, dEmissao, dDtRef, nTotal, "E2_VALOR" ) //"Desbloqueio do Título"
EndIf

//Efetua o desbloqueio
If lContinua .And. nOpc == 1
	RecLock( "SE2", .F. )
	SE2->E2_STATLIB := "01"
	MsUnlock()
EndIf
    
RestArea( aArea )

Return Nil

//----------------------------------------------------------------------
/*/{Protheus.doc} ValidPedido()
Verifica se existe titulo vinculado a um pedido de compras.
@author Leonardo Bratti
@since 05/12/2017
@version 1.0
@return .T.
/*/
//----------------------------------------------------------------------
Static Function ValidPedido(cNumTit As Character , cPrefixo As Character) As Character
Local cAliasFIE  As Character
Local cPedBql    As Character
Local cQuery     As Character 
Local aArea      As Array

Default cNumTit  := ''
Default cPrefixo := ''

cAliasFIE := '' 
cPedBql := ''
cQuery := '' 
aArea := GetArea()
cAliasFIE := GetNextAlias()

cQuery := "SELECT FIE_FILIAL , FIE_PEDIDO , FIE_NUM , FIE_FORNEC "   	  	
cQuery += "FROM " + RetSqlname("FIE")+ " FIE , " + RetSqlname("SC7") +  " SC7 " 
cQuery += "WHERE FIE.FIE_FILIAL = '" + xFilial("FIE") + "' AND "
cQuery += "SC7.C7_FILIAL = '" + xFilial("SC7") + "' AND FIE.FIE_PEDIDO = SC7.C7_NUM " 
cQuery += "AND FIE_NUM = '" + cNumTit + "' AND FIE_PREFIX = '" + cPrefixo + "' AND SC7.C7_CONAPRO <> 'L' " 
cQuery += "AND SC7.D_E_L_E_T_ = ' '  AND FIE.D_E_L_E_T_ = ' '   "
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasFIE,.T.,.T.)	

If (cAliasFIE)->(!Eof())
	cPedBql := (cAliasFIE)->FIE_PEDIDO			 
EndIf

RestArea(aArea)
(cAliasFIE)->(dbCloseArea())
Return cPedBql

//----------------------------------------------------------------------
/*/{Protheus.doc} ProcessReg()
processa a liberaçã dos registros marcados.
@author Rene Julian
@since 23/11/2020
@version 1.0
@return 
/*/
//----------------------------------------------------------------------
Static Function ProcessReg(cAliasSe2,nQtdReg)
Local cFilAtu	:= cFilAnt
Local lLiber	:= .T.      

ProcRegua(nQtdReg)	

While !Eof()	
	IncProc()
	lLiber := .T.
	If lF580LbA		
		lLiber := ExecBlock("F580LbA",.F.,.F.)
	EndIf	 
	IF lFinVDoc
		SE2->(dbGoTo((cAliasSe2)->RECNO))
		cFilAnt := SE2->E2_FILORIG
		If !CN062ValDocs("01",.F.)
			lLiber := .F.
		EndIf
		cFilAnt := cFilAtu
	EndIf			 				 
	If (cAliasSe2)->E2_OK == cMarca .And. Empty((cAliasSe2)->E2_DATALIB) .And. lLiber
		BEGIN TRANSACTION
			dbSelectArea(cAliasSe2)
			#IFDEF TOP
				If (cAliasSe2)->(FieldPos("RECNO")) > 0
					DbSelectArea("SE2")
					MsGoto((cAliasSe2)->RECNO)
					DbSelectArea(cAliasSe2)
				Endif	
			#ENDIF
			RecLock("SE2")
			SE2->E2_DATALIB	:= dDataBase
			SE2->E2_USUALIB   := cUsername
			SE2->E2_STATLIB := "03"
			SE2->E2_CODAPRO := Fa006User( __cUserId, .F., 2 )
			MsUnlock()
			
			//Envio de e-mail pela rotina de checklist de documentos obrigatorios
			IF lFinVDoc
				cFilAnt := SE2->E2_FILORIG
				CN062ValDocs("01",.F.)
				cFilAnt := cFilAtu
			EndIf
			
			If lFina580
				Execblock("FINA580",.f.,.f.,{.T.})
			Endif
		END TRANSACTION		

		If lFina580A
			Execblock("FINA580A",.f.,.f.)
		Endif

	EndIf
	dbSelectArea(cAliasSe2)
	DbSkip()	
Enddo

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} F580IniStat
Inicializa variaveis statics

@author Fabio Zanchim
@since  07/07/2022
@version 12
/*/
//-------------------------------------------------------------------
Static Function F580IniStat(lExecute)

    Default lExecute := Select("SIX")>0 .And.;
                        Select("SX2")>0 .And.;
                        Select("SX3")>0 
	If lExecute		
		lValSE2Sit 	:= ValSE2Situac()//Gestao
		lPanelFin 	:= IsPanelFin()
		lFa580Lib	:= ExistBlock("FA580LIB")
		lF580BROW	:= ExistBlock("F580BROW")
		lFina580	:= ExistBlock("FINA580")
		lFina580A	:= ExistBlock("FINA580A")
		lManBrow	:= ExistBlock("F580MNBR")
		lF580LbA	:= ExistBlock("F580LbA")
		lf580AutVl	:= ExistBlock("F580AUTVL")
		lF580Chav	:= ExistBlock("F580CHAV")
		lF580FAUT	:= ExistBlock("F580FAUT")
		lF580Legen	:= ExistBlock("F580LEGEN") 
		lF580EdLeg	:= ExistBlock("F580EDLEG")
		lF580ADDB	:= ExistBlock("F580ADDB")
		lF565Can 	:= Existblock("F580CAN")
		lF580VLDPA	:= ExistBlock("F580VLDPA")
		lVerEmis1	:= Alltrim(SuperGetMV("MV_F580DT",.F.,"1")) == "1"
	Endif

Return(lExecute)
