#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include 'backoffice.sv.fin.bankstatement.ch'

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="FK5", customTables="FK5", name="Extrato Bancário", country="ALL")
//-------------------------------------------------------------------
/*/{Protheus.doc} BankStatementSmartViewBusinessObject
Classe para criação do Objeto de Negócio para geração do extrato bancário

@author Pâmela Bernardo
@since 10/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
class BankStatementSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object
    public method mySetSchema()

    protected data oStBanks as object
    
    protected data nCurrency as numeric
    protected data lBalancePerBranch as logical
    protected data nFilterConciliation as numeric
    protected data lConvertByRateOfTheDay as logical
    protected data nTypeOpeningBalanceConversion as numeric
    protected data nOpeningBalance as numeric
    protected data nCurrentBalance as numeric
    protected data dInitialDate as date
    protected data dFinalDate as date
    protected data cBankCode as character 
    protected data cBankBranchCode as character 
    protected data cBankAccountCode as character
    protected data lFix24Ok as logical
    protected data lFixDateOk as logical
    protected data lFint035 as logical
    protected data cLinkTdn as character

    protected data nTEntConc  as numeric
    protected data nTSaidaCon as numeric
    protected data nTEntSCon  as numeric
    protected data nTSaiSConc as numeric

    protected data nCreditLimit as numeric
    protected data nBankCurrency as numeric
    protected data aBanksList as array

    protected method validEnvironment()
    protected method loadStatement()
    protected method initializeBusinessObject()
    protected method handleData() as object
    protected method getConversionDateOpeningBalance() as date
    protected method loadParameters()
    protected method loadDefaultParameters()
    protected method convertCurrencyMovement() as numeric
    protected method posPositionAlias()
    protected method loadOpeningBalance()
    protected method loadBankData()
endclass
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Pâmela Bernardo
@since 10/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method new() class BankStatementSmartViewBusinessObject
    _Super:new()    
    self:lFint035 := FWSX1Util():ExistPergunte("FINT035")
    self:initializeBusinessObject()

    self:setMainTable("FK5")
    self:setBranchFilter(.T., .T.)

    self:nTEntConc     := 0 
    self:nTSaidaCon    := 0 
    self:nTEntSCon     := 0 
    self:nTSaiSConc    := 0 

    self:nCurrentBalance := 0
    
    self:nCreditLimit := 0 
    self:nBankCurrency := 1
    self:aBanksList := {}
    self:lFixDateOk := GetRpoRelease() > "12.1.2410" .or. DToS(GetApoInfo('FINXFIX.PRW')[4]) >= "20240523"
    self:lFix24Ok := GetMv("MV_FINFIX",.F.,' ') >= '24'
    self:cLinkTdn := "https://tdn.totvs.com/pages/viewpage.action?pageId=759953066"

return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject()
    @author fabioh.andrade@totvs.com.br
    @since 02/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class BankStatementSmartViewBusinessObject    
    self:setDisplayName(STR0002) //"Extrato Bancário"
    self:setDescription(STR0002) //"Extrato Bancário"

    if self:lFint035
        self:setPergunte("FINT035")
    else
        self:setPergunte("FIN470")
    endIf
return

//-------------------------------------------------------------------
/*{Protheus.doc} validEnvironment()
    @description Além da validação padrão do FinIntegratedProvider, valida data do fonte e execução do Fix 24
    @author guilherme.sordi@totvs.com.br
    @since 09/12/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method validEnvironment() class BankStatementSmartViewBusinessObject
    local lOk := _Super:validEnvironment()
    
    if lOk .and. !self:lFixDateOk
        FwLogMsg("WARN",, STR0015,,, , STR0016 , , ,) //Smart View; Ambiente desatualizado: Necessário aplicar o pacote de expedição contínua backoffice mais recente.
        self:setErrorStatus(400, STR0015, STR0016) 
        lOk := .F.
    endIf

    if lOk .and. !self:lFix24Ok
        FwLogMsg("WARN",, STR0015,,, , STR0017 , , ,) //Smart view; Verificar consistência do migrador de dados (FKs) [FIX 24]
        self:setErrorStatus(400, STR0015, STR0017) 
        lOk := .F.
    endIf

    if lOk .and. !self:lFint035
        FwLogMsg("WARN",, STR0015,,, , STR0016 + " " + STR0018 + " " + self:cLinkTdn , , ,) //Smart View; Ambiente desatualizado: Necessário aplicar o pacote de expedição contínua backoffice mais recente.; O grupo de perguntas FINT035 não foi encontrado. Para mais informações acesse:
        self:setErrorStatus(400, STR0015, STR0016 + " " + STR0018 + " " + self:cLinkTdn)
        lOk := .F.
    endIf
return lOk

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
Atribui ao objeto oData o conteúdo do Alias e campos calculados.
@type method
@param oFilter, objeto, contém o filtro do TReports
@author Fábio Henrique Andrade
@since 19/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData() as object class BankStatementSmartViewBusinessObject 
    Local nSaldofim       := 0  as numeric
    Local nVlrEntr        := 0  as numeric
    Local nVlrSaida       := 0  as numeric
    Local nMovementValue  := 0  as numeric
    Local cDocument       := "" as character
    Local cConcilia       := "" as character       
    Local nDecSald        := 0  as numeric

    nDecSald       := TamSX3("FK5_VALOR")[2]
    nMovementValue := (self:cAlias)->FK5_VALOR
    nMovementValue := self:convertCurrencyMovement(nMovementValue)

    cConcilia   := ""

    If (self:cAlias)->FK5_RECPAG == "R"
        nVlrEntr := nMovementValue
        self:nCurrentBalance += nMovementValue
        If !Empty((self:cAlias)->FK5_DTCONC)
            self:nTEntConc  += nMovementValue
            cConcilia       := "X"
        Else 
            self:nTEntSCon += nMovementValue
        Endif
    Else
        nVlrSaida := nMovementValue
        self:nCurrentBalance -= nMovementValue
        If !Empty((self:cAlias)->FK5_DTCONC)
            self:nTSaidaCon += nMovementValue
            cConcilia       := "X"
        Else 
            self:nTSaiSConc += nMovementValue
        Endif
    EndIf

    If !Empty((self:cAlias)->FK7_NUM)
        cDocument := (self:cAlias)->FK7_FILTIT + " " + (self:cAlias)->FK7_PREFIX + " " + (self:cAlias)->FK7_NUM + " " + (self:cAlias)->FK7_PARCEL + " " + (self:cAlias)->FK7_TIPO
    ElseIf (self:cAlias)->TPDOC $ 'CH/ES' .and. !Empty((self:cAlias)->NUMCH)
        cDocument := STR0013+(self:cAlias)->NUMCH //"Cheque nº: "
    ElseIf (self:cAlias)->TPDOC $ 'BL/ES' .and. !Empty((self:cAlias)->LOTE)
        cDocument := STR0014 + ": " +(self:cAlias)->LOTE //"Lote: "
    ElseIf (self:cAlias)->TPDOC $ 'TR/TE' .and. !Empty((self:cAlias)->NUMCH)
        cDocument := (self:cAlias)->NUMCH
    Elseif !Empty((self:cAlias)->FK5_DOC)
        cDocument := (self:cAlias)->FK5_DOC
    EndIf
            
    self:jData := { "FK5_VALOR":    nMovementValue,;
                    "FK5_DOC":      cDocument,;
                    "SALDOINI":     self:nOpeningBalance,;
                    "ENTRADA":      nVlrEntr,;
                    "SAIDA":        nVlrSaida,;
                    "SALDOATU":     Round(self:nCurrentBalance, nDecSald),;
                    "TOTENTCONC":   self:nTEntConc,;
                    "TOTSAIDACONC": self:nTSaidaCon,;
                    "TOTENTSCONC":  self:nTEntSCon,;
                    "TOTSAISCONC":  self:nTSaiSConc,;
                    "LIMITE":       self:nCreditLimit,;
                    "SALDOFIM":     nSaldofim,;  
                    "CONCILIADO":   cConcilia }  

return


//-------------------------------------------------------------------
/*{Protheus.doc} mySetSchema
Retorna a estrutura dos campos e conjunto de parametros

@return object: self:oSchema

@author Pâmela Bernardo
@since 12/05/2023
@version 1.0
*/
//-------------------------------------------------------------------

method mySetSchema()  class BankStatementSmartViewBusinessObject
    Local aFieldsFK5 := {} as array

    aFieldsFK5 := { "FK5_FILIAL", "FK5_BANCO", "FK5_AGENCI", "FK5_CONTA", ;
                    "FK5_DTDISP", "FK5_DTCONC", "FK5_RECPAG", "FK5_VALOR",;
                    "FK5_TXMOED", "FK5_MOEDA", "FK5_HISTOR", "FK5_DOC"}

    self:oSchema:aliasToSchema("FK5", aFieldsFK5)
    self:finAddProperty("SALDOINI"     , STR0003, "number" ) //"Saldo Inicial"
    self:finAddProperty("ENTRADA"      , STR0004, "number" ) //"Entrada"
    self:finAddProperty("SAIDA"        , STR0005, "number" ) //"Saída"
    self:finAddProperty("SALDOATU"     , STR0006, "number" ) //"Saldo atual"
    self:finAddProperty("SALDOFIM"     , STR0007, "number" ) //"Saldo Disponível"
    self:finAddProperty("TOTENTCONC"   , STR0008, "number" ) //"Total de Entrada Conciliados"
    self:finAddProperty("TOTSAIDACONC" , STR0009, "number" ) //"Total de Saída Conciliados"
    self:finAddProperty("TOTENTSCONC"  , STR0010, "number" ) //"Total de Entrada Sem Conciliação"
    self:finAddProperty("TOTSAISCONC"  , STR0011, "number" ) //"Total de Saída Sem Conciliação"
    self:finAddProperty("LIMITE"       , STR0012, "number" ) //"Limite de Crédito"    
    self:finAddProperty("CONCILIADO"   , ""     , "string" ) //"Conciliado - sem STR criado, pois não há cabecalho na impressão.

return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
Retorna a query a ser executada na função getData

@return character: cQuery

@author Pâmela Bernardo
@since 09/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method loadStatement() class BankStatementSmartViewBusinessObject
    Local cQuery        := ""           as character
    Local nParamOrder   := 1            as numeric

    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "FK5_DTDISP" ) )
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "FK5_DTCONC" ) )
    
    cQuery := " SELECT ? , FK5_FILORI FILORI, "
    cQuery += " FK5_IDMOV IDMOV, FK5_IDDOC IDDOC, FK5_TPDOC TPDOC, FK5_NUMCH NUMCH, FK5_LOTE LOTE, "
    cQuery += " COALESCE(FK7_FILTIT,'') FK7_FILTIT, COALESCE(FK7_PREFIX,'') FK7_PREFIX, COALESCE(FK7_NUM,'') FK7_NUM, COALESCE(FK7_PARCEL,'') FK7_PARCEL, COALESCE(FK7_TIPO,'') FK7_TIPO "

    cQuery += " FROM " + RetSQLName("FK5") + " FK5 "
    cQuery += " LEFT JOIN " + RetSQLName("FK7") + " FK7 "
    cQuery += " ON " + fwJoinFilial("FK7", "FK5") + " 
    cQuery += " AND FK7_IDDOC = CASE WHEN FK5_IDFK7 <> ' ' THEN FK5_IDFK7 ELSE FK5_IDDOC END "
    cQuery += " AND FK7.D_E_L_E_T_ = ? "
    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += " AND FK5.FK5_BANCO = ? "
    cQuery += " AND FK5.FK5_AGENCI = ? "
    cQuery += " AND FK5.FK5_CONTA =  ? "
    cQuery += " AND FK5.FK5_DTDISP BETWEEN ? AND ? "

    If self:nFilterConciliation == 2
        cQuery += " AND FK5_DTCONC <> ? "
    ElseIf self:nFilterConciliation == 3
        cQuery += " AND FK5_DTCONC = ? "
    EndIf

    cQuery += " AND FK5.D_E_L_E_T_ = ?  "

    cQuery += self:getFilterToSQL()
    
    if valType(self:aMainTableSelectedBranches) == "A" .and. len(self:aMainTableSelectedBranches) > 1
        cQuery += "	ORDER BY FK5_DTDISP, FK5_BANCO, FK5_AGENCI, FK5_CONTA, FK5_NUMCH, FK5_DOC, FK5.R_E_C_N_O_ "
    else
        cQuery += "	ORDER BY FK5_FILIAL, FK5_DTDISP, FK5_BANCO, FK5_AGENCI, FK5_CONTA, FK5_NUMCH, FK5_DOC, FK5.R_E_C_N_O_ "
    endIf

    self:oStatement := FWExecStatement():New(ChangeQuery(cQuery))

	self:oStatement:SetUnsafe(nParamOrder++, self:getAllFieldsToSQL())
    self:oStatement:SetString(nParamOrder++, " ")
    self:setStatementBranchFilter(@nParamOrder)
    self:oStatement:SetString(nParamOrder++, self:cBankCode)
    self:oStatement:SetString(nParamOrder++, self:cBankBranchCode)
    self:oStatement:SetString(nParamOrder++, self:cBankAccountCode)
    self:oStatement:SetDate(nParamOrder++, self:dInitialDate)
    self:oStatement:SetDate(nParamOrder++, self:dFinalDate)

    If self:nFilterConciliation == 2 .OR. self:nFilterConciliation == 3
        self:oStatement:SetString(nParamOrder++, " ")
    EndIf

    self:oStatement:SetString(nParamOrder++, " ")

return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadOpeningBalance
    @description Carrega o saldo inicial e a data que deve ser considerada como saldo inicial 
    (para o cenário em que a conversão de moeda do limite de crédito deve ser feita na data do saldo inicial).
    @author guilherme.sordi@totvs.com.br
    @since 08/11/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method loadOpeningBalance() class BankStatementSmartViewBusinessObject
    local oBanks := Banks():new() as object
    local aBalances := {} as array
    local nX as numeric
    local nY as numeric
    local lUseBalanceDateToCurrencyConvertion := self:nTypeOpeningBalanceConversion == 1 as logical

    self:nOpeningBalance := 0
    
    oBanks:setBranchFilter(self:aMultiBranches)
    oBanks:setCurrency(self:nCurrency)
    if !lUseBalanceDateToCurrencyConvertion
        oBanks:setCurrencyConvertionDate(self:getConversionDateOpeningBalance())
    endIf

    for nX := 1 to len(self:aBanksList)
        aBalances := oBanks:getBankBalances(self:aBanksList[nX], self:dInitialDate)
        for nY := 1 to len(aBalances)
            if self:nFilterConciliation == 1
                self:nOpeningBalance += aBalances[nY]["e8_salatua"]
            elseIf self:nFilterConciliation == 2
                self:nOpeningBalance += aBalances[nY]["e8_salreco"]
            elseIf self:nFilterConciliation == 3
                self:nOpeningBalance += (aBalances[nY]["e8_salatua"] - aBalances[nY]["e8_salreco"])
            endIf
        next    
    next
    
    self:nCurrentBalance := self:nOpeningBalance
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadBankData
    @description Carrega a lista dos bancos conforme filtro do smart view, e o limite de crédito e moeda do
    primeiro cadastro encontrado na SA6.
    Carrega o primeiro encontrado na SA6 considerando que alguns ambientes podem ter o nmesmo banco/agência/conta
    cadastro em mais de uma filial com a tabela SA6 exclusiva. Nesse caso, consideramos os registros de todas as filiais
    para manter o comportamento do legado.
    @author guilherme.sordi@totvs.com.br
    @since 08/11/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method loadBankData() class BankStatementSmartViewBusinessObject
    local cQuery as character
    local cAlias as character
    local nParamOrder := 1 as numeric

    self:aBanksList := {}
    self:nCreditLimit := 0
    self:nBankCurrency := 1

    if self:oStBanks == NIL
        cQuery := " SELECT SA6.R_E_C_N_O_ RECNO, A6_LIMCRED, A6_MOEDA "
        cQuery += " FROM " + RetSQLName("SA6") + " SA6 "
        cQuery += " WHERE "
        cQuery += " A6_FILIAL IN (?) "
        cQuery += " AND SA6.A6_COD = ? "
        cQuery += " AND SA6.A6_AGENCIA = ? "
        cQuery += " AND SA6.A6_NUMCON =  ? "
        cQuery += " AND SA6.D_E_L_E_T_ = ?  "

        self:oStBanks := FWExecStatement():New(ChangeQuery(cQuery))
    endIf
     
    self:oStBanks:SetIn(nParamOrder++, self:getAliasBranchFilter("SA6"))
    self:oStBanks:SetString(nParamOrder++, self:cBankCode)
    self:oStBanks:SetString(nParamOrder++, self:cBankBranchCode)
    self:oStBanks:SetString(nParamOrder++, self:cBankAccountCode)
    self:oStBanks:SetString(nParamOrder++, " ")

    cAlias := self:oStBanks:openAlias()
    
    self:nBankCurrency := Max((cAlias)->A6_MOEDA, 1)
    self:nCreditLimit := xMoeda((cAlias)->A6_LIMCRED, self:nBankCurrency, self:nCurrency, dDatabase, self:nDecimals) 

    while !(cAlias)->(eof())
        aAdd(self:aBanksList, (cAlias)->RECNO)
        (cAlias)->(dbSkip())
    endDo
    (cAlias)->(dbCloseArea())

return

//----------------------------------------------------------------
/*/{Protheus.doc} getConversionDateOpeningBalance
Função que determinará a data de conversão do saldo inicial (SE8)

@author Pâmela Bernardo
@since  02/06/21
@version 12
/*/
//-----------------------------------------------------------------
method getConversionDateOpeningBalance() as date class BankStatementSmartViewBusinessObject	
    local dConversionDate := dDatabase as date

    If self:nTypeOpeningBalanceConversion == 2
        dConversionDate := dDataBase
    ElseIf self:nTypeOpeningBalanceConversion == 3
        dConversionDate := self:dInitialDate
    ElseIf self:nTypeOpeningBalanceConversion == 4
        dConversionDate := self:dFinalDate
    EndIf

Return dConversionDate

//-------------------------------------------------------------------
/*/{Protheus.doc} loadDefaultParameters
    @author fabioh.andrade@totvs.com.br
    @since 02/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class  BankStatementSmartViewBusinessObject
    self:nCurrency := 0
    self:nFilterConciliation := 1
    self:lConvertByRateOfTheDay := .T.
    self:lBalancePerBranch := .T.
    self:nTypeOpeningBalanceConversion := 1        
    self:dInitialDate := dDataBase
    self:dFinalDate := dDataBase
return

//----------------------------------------------------------------
/*/{Protheus.doc} loadParameters
    Carrega os parâmetros do relatório

    @author guilherme.sordi@totvs.com.br
    @since  11/08/2023
    @version 12.1.23
*/
//-----------------------------------------------------------------
method loadParameters() class BankStatementSmartViewBusinessObject
    if self:oCurrentFilter <> NIL .and. self:lFint035
        self:cBankCode := MV_PAR01
        self:cBankBranchCode := MV_PAR02
        self:cBankAccountCode := MV_PAR03
        self:dInitialDate := MV_PAR04
        self:dFinalDate := MV_PAR05
        self:nFilterConciliation := MV_PAR06 //1 = Todos, 2 = Conciliados, 3 = Não conciliados
        self:nCurrency := MV_PAR07 //Moeda na qual os dados serão apresentados para o usuário
        self:lConvertByRateOfTheDay := MV_PAR08 == 1 // 1 = Taxa do dia, 2 = Taxa do movimento
        self:nTypeOpeningBalanceConversion := MV_PAR09 // Taxa da... 1= Data do saldo, 2 = Data base, 3 = Data inicial, 4 = Data final
    endIf
return

//----------------------------------------------------------------
/*/{Protheus.doc}convertCurrencyMovement
    Converte o valor do movimento para a moeda do relatório. 

    @author guilherme.sordi@totvs.com.br
    @since  14/08/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method convertCurrencyMovement(nValue as numeric) as numeric class BankStatementSmartViewBusinessObject
    local dConversao := dDatabase as numeric
    local nTaxaContratada := 0 as numeric
    local nTaxaOrigem := 0 as numeric
    local nTaxaDestino := 0 as numeric

    if self:lConvertByRateOfTheDay
        dConversao := dDataBase
        nTaxaContratada := 0
    else
        dConversao := (self:cAlias)->FK5_DTDISP
        nTaxaContratada := (self:cAlias)->FK5_TXMOED
    endIf

    if self:nBankCurrency == 1
        nTaxaDestino := nTaxaContratada
    else
        nTaxaOrigem := nTaxaContratada
    endIf
    
    nValue := xMoeda(nValue, self:nBankCurrency, self:nCurrency, dConversao, self:nDecimals, nTaxaOrigem, nTaxaDestino)
return nValue

//-------------------------------------------------------------------
/*{Protheus.doc} posPositionAlias
	Método chamado imediatamente antes de iniciar o While do 
	MyAliasToData(), validando se existe registro para movimentos e saldos bancarios
	e posicionada no primeiro registro.
	@author Francisco Oliveira
	@since 07/03/2025
	@version 12.1.2410
*/
//-------------------------------------------------------------------
method posPositionAlias() class BankStatementSmartViewBusinessObject

    self:loadBankData()
    self:loadOpeningBalance()
    
    If (self:cAlias)->(EOF())

        self:jData := JSONObject():new()
   
        self:jData := { "FK5_BANCO":    self:cBankCode ,;
                        "FK5_AGENCI":   self:cBankBranchCode ,;
                        "FK5_CONTA":    self:cBankAccountCode ,;
                        "FK5_VALOR":    0 ,;
                        "FK5_DOC":      "",;
                        "SALDOINI":     self:nOpeningBalance ,;
                        "ENTRADA":      0 ,;
                        "SAIDA":        0 ,;
                        "SALDOATU":     self:nCurrentBalance,;
                        "TOTENTCONC":   self:nTEntConc,;
                        "TOTSAIDACONC": self:nTSaidaCon,;
                        "TOTENTSCONC":  self:nTEntSCon,;
                        "TOTSAISCONC":  self:nTSaiSConc,;
                        "LIMITE":       self:nCreditLimit,;
                        "CONCILIADO":   "" }  

        self:oData:appendData(self:jData)
    
    Endif
return 
