#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.billsrecbysalesrep.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE1,SED,SA1,SA3", name="Títulos a receber por vendedor", country="ALL", customTables="SE1,SED,SA1,SA3")
//-------------------------------------------------------------------
/*/{Protheus.doc} BillsReceivableBySalesRepSmartViewBusinessObject
Classe para criação do Objeto de Negócio
@author guilherme.sordi@totvs.com.br
@since 23/05/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
class BillsReceivableBySalesRepSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object
    
    protected method initializeBusinessObject()
    protected method loadDefaultParameters()
    protected method loadParameters()
    protected method mySetSchema()
    protected method myAliasToData()
    protected method handleData()
    protected method appendRenegotiatedBills()
    protected method convertCurrencyValue() as numeric
    protected method getAccessoryValues() as numeric
    protected method getDeduction() as numeric
    protected method getInterestValue() as numeric
    protected method getFineValue() as numeric
    protected method getDiscountValue() as numeric
    protected method getFormatedText() as json
    protected method getFieldstoSQL() as character

    protected method preOpenQuery()
    protected method prepareStatements()

    protected method getHasFO1() as character
    protected method getTrackerQuery() as character
    protected method getSalespersonView() as character
    
    protected method getMainQuery() as character
    protected method getMainQueryWhere() as character
    protected method handleMainQueryParameters()
    protected method handleMainQueryWhere()
    
    protected method getRenegotiatedBillsQuery() as character
    protected method getRenegotiatedBillsQueryWhere() as character
    protected method handleRenegotiatedBillsQueryParameters()
    protected method setRenegotiatedBillsStatementBranchFilter()

    protected data oStRenegotiatedBills as object

    protected data cCustomerFrom as character
    protected data cCustomerTo as character
    protected data cCustomerUnitFrom as character
    protected data cCustomerUnitTo as character
    protected data dIssueFrom as character
    protected data dIssueTo as character
    protected data dDuoFrom as character
    protected data dDuoTo as character
    protected data cSalespersonFrom as character
    protected data cSalespersonTo as character
    protected data cBillClassFrom as character
    protected data cBillClassTo as character

    protected data lCalculateFineValueBySIGALOJA as logical
    protected data lAddIncreaseAndDecreaseToFineValueCalculation as logical
    protected data lProportionalDiscount as logical
    protected data lForecastOfComission as logical

    protected data nCurrency as numeric
    protected data cCurrentSalespersonCode as character
    protected data nCountValidRenegotiatedBills as numeric
    protected data nCurrentParameterOrder as numeric
    protected data jValuesToComission as json
    protected data aDefaultTables as array
endClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
    @author guilherme.sordi@totvs.com.br
    @since 23/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method new() as object class BillsReceivableBySalesRepSmartViewBusinessObject
    _Super:new()
    self:initializeBusinessObject()
    self:setCompleteData(.T.)
    self:setMainTable("SE1")
    self:setBranchFilter(.T., .T.)

    self:nCurrency := 1
    self:cCurrentSalespersonCode := ""
    self:nCountValidRenegotiatedBills := 1
    self:nCurrentParameterOrder := 1
    self:aDefaultTables := {}
    self:lForecastOfComission := .F.
    
    self:lCalculateFineValueBySIGALOJA := ( SuperGetMv("MV_JURTIPO",,"") == "L" ) .Or. ( SuperGetMv("MV_LJINTFS", ,.F.) ) 
    self:lAddIncreaseAndDecreaseToFineValueCalculation := SuperGetMv("MV_JURTIN",,.F.)
    self:lProportionalDiscount := SuperGetMV("MV_DESCFIN",,"I") == "P"
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initilizeBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 23/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class BillsReceivableBySalesRepSmartViewBusinessObject
    self:setDisplayName(STR0002) // "Títulos a receber por vendedor"
    self:setDescription(STR0002) // "Títulos a receber por vendedor"
    self:setPergunte("FINR137")
return

//-------------------------------------------------------------------
/*{Protheus.doc} mySetSchema
    ReDefine torna a estrutura dos campos e conjunto de parametros
    @author guilherme.sordi@totvs.com.br
    @since 23/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method mySetSchema() class BillsReceivableBySalesRepSmartViewBusinessObject
    local aFieldsSE1 := {} as array
    local aFieldsSA1 := {} as array
    local aFieldsSA3 := {} as array

    aFieldsSE1 := { "E1_FILIAL", "E1_PREFIXO", "E1_NUM", "E1_PARCELA", "E1_TIPO", "E1_CLIENTE", "E1_LOJA", ;
        "E1_NATUREZ", "E1_EMISSAO", "E1_VENCTO", "E1_VENCREA", "E1_VALOR", "E1_MOEDA", "E1_TXMOEDA", "E1_VLCRUZ", "E1_SALDO", ;
        "E1_ACRESC","E1_SDACRES", "E1_SDDECRE", "E1_HIST", "E1_JUROS", "E1_MULTA", "E1_BAIXA", "E1_ORIGEM", "E1_PORCJUR", "E1_DESCONT" }

    aFieldsSA1 := { "A1_COD", "A1_LOJA", "A1_CGC", "A1_NOME", "A1_NREDUZ", "A1_TIPO", "A1_PESSOA" }
    
    aFieldsSA3 := { "A3_COD", "A3_NOME", "A3_NREDUZ"}

    self:oSchema:aliasToSchema("SE1", aFieldsSE1)
    self:oSchema:aliasToSchema("SED", "ED_DESCRIC")
    self:oSchema:aliasToSchema("SA1", aFieldsSA1)
    self:oSchema:aliasToSchema("SA3", aFieldsSA3)
    self:oSchema:aliasToSchema("FK7", "FK7_IDDOC")

    self:aDefaultTables := {"SE1", "SED", "SA1", "SA3", "FK7"}
    
    self:finAddProperty("SALESPERSON_DATA", STR0003, "string", "A3_NOME") //"Dados do vendedor"
    self:finAddProperty("CUSTOMER_DATA", STR0004, "string", "A1_NOME") //"Dados do cliente"
    self:finAddProperty("BILL_DATA", STR0005, "string", "E1_NUM") //"Dados do título"
    self:finAddProperty("DAYS_LATE", STR0006, "number") //"Dias de atraso"
    self:finAddProperty("ACCESSORY_VALUES", STR0008, "number") //"Valores acessórios"
    self:finAddProperty("NET_AMOUNT", STR0009, "number") //"Saldo líquido"
    self:finAddProperty("CLASSIFICATION", STR0010, "string") //Classificação

    FwFreeArray(aFieldsSE1)
    FWFreeArray(aFieldsSA1)
    FWFreeArray(aFieldsSA3)
    
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preOpenQuery
    @author guilherme.sordi@totvs.com.br
    @since 23/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method preOpenQuery() class BillsReceivableBySalesRepSmartViewBusinessObject    
    self:prepareStatements()
return


//-------------------------------------------------------------------
/*{Protheus.doc} prepareStatements
    Monta as queries e cria os objetos ExecStatement que serão usados pelo objeto de negócio
    @author guilherme.sordi@totvs.com.br
    @since 23/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method prepareStatements() class BillsReceivableBySalesRepSmartViewBusinessObject
    self:addFieldsToStruct( { "E1_EMISSAO", "E1_VENCTO", "E1_VENCREA", "E1_BAIXA" } )

    self:oStatement := FWExecStatement():New( self:getMainQuery() )
    self:oStatement:setFields(self:aStruct)
    self:handleMainQueryParameters()
    self:oStRenegotiatedBills := FWExecStatement():New( self:getRenegotiatedBillsQuery() )
    self:oStRenegotiatedBills:setFields(self:aStruct)
return

//-------------------------------------------------------------------
/*{Protheus.doc} getMainQuery
    Monta a query principal do objeto de negócio
    @author guilherme.sordi@totvs.com.br
    @since 23/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getMainQuery() as character class BillsReceivableBySalesRepSmartViewBusinessObject
    local cQuery := "" as character

    cQuery := " SELECT ? "
    cQuery += " , SE1.R_E_C_N_O_ SE1RECNO, VW.VENDEDOR, A3_COMIS COMIS, E3_DATA DATACOM, " 
    cQuery += " COALESCE(HASFO1.HASFO1, '2') HASFO1, '1' ORIGINAL_BILL "

    cQuery += " FROM " + RetSQLName("SE1") + " SE1 "
    cQuery += " JOIN " + RetSQLName("SED") + " SED "
    cQuery += " ON " + FwJoinFilial("SE1","SED") 
    cQuery += " AND E1_NATUREZ = ED_CODIGO "
    cQuery += " AND SED.D_E_L_E_T_ = ? "

    cQuery += " JOIN " + RetSQLName("SA1") + " SA1 "
    cQuery += " ON " + FwJoinFilial("SE1","SA1") 
    cQuery += " AND E1_CLIENTE = A1_COD "
    cQuery += " AND E1_LOJA = A1_LOJA "
    cQuery += " AND SA1.D_E_L_E_T_ = ? "

    cQuery += " JOIN " + self:getSalespersonView() + " VW "
    cQuery += " ON SE1.R_E_C_N_O_ = VW.R_E_C_N_O_ "

    cQuery += " JOIN " + RetSQLName("SA3") + " SA3 "
    cQuery += " ON " + FwJoinFilial("SE1","SA3") 
    cQuery += " AND VW.VENDEDOR = A3_COD "
    cQuery += " AND SA3.D_E_L_E_T_ = ? "

    cQuery += " JOIN " + RetSQLName("FK7") + " FK7 "
    cQuery += " ON E1_FILIAL = FK7_FILTIT "
    cQuery += " AND E1_PREFIXO = FK7_PREFIX "
    cQuery += " AND E1_NUM = FK7_NUM "
    cQuery += " AND E1_PARCELA = FK7_PARCEL "
    cQuery += " AND E1_TIPO = FK7_TIPO "
    cQuery += " AND E1_CLIENTE = FK7_CLIFOR "
    cQuery += " AND E1_LOJA = FK7_LOJA "
    cQuery += " AND FK7.D_E_L_E_T_ = ? "
    cQuery += " AND FK7_ALIAS = ? "
    
    cQuery += " LEFT JOIN HASFO1 "
    cQuery += " ON FK7.FK7_IDDOC = HASFO1.FO1_IDDOC "

    cQuery += " LEFT JOIN " + RetSQLName("SE3") + " SE3"
    cQuery += " ON " + FwJoinFilial("SE1","SE3") 
    cQuery += " AND E1_PREFIXO = E3_PREFIXO "
    cQuery += " AND E1_NUM = E3_NUM "
    cQuery += " AND E1_PARCELA = E3_PARCELA "
    cQuery += " AND SE3.D_E_L_E_T_ = ? "

    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += " AND SE1.D_E_L_E_T_ = ?  "

    If self:lForecastOfComission
        cQuery += " AND (SE1.E1_SALDO >= ? OR HASFO1.HASFO1 IS NOT NULL) "
    Else 
        cQuery += " AND (SE1.E1_SALDO > ? OR HASFO1.HASFO1 IS NOT NULL) "
    EndIf

    cQuery += self:getMainQueryWhere()

    cQuery += self:getFilterToSQL()

    cQuery += "ORDER BY " + SqlOrder(SA3->(IndexKey( 1 ))) + ", " + SqlOrder(SE1->(IndexKey( 1 )))

    cQuery := changeQuery(cQuery)

    cQuery := "WITH HASFO1 AS ( "+ self:getHasFO1() +" ) " + cQuery

return cQuery

//-------------------------------------------------------------------
/*{Protheus.doc} getRenegotiatedBillsQuery
    Monta a query que vai buscar os filhos da liquidação. Precisa ter os mesmos campos da query principal
    para reaproveitar todo o códido criado para os campos calculados.
    @author guilherme.sordi@totvs.com.br
    @since 23/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getRenegotiatedBillsQuery() as character class BillsReceivableBySalesRepSmartViewBusinessObject
    local cQuery := "" as character

    cQuery := " SELECT ? "
    cQuery += " , SE1.R_E_C_N_O_ SE1RECNO, A3_COMIS COMIS, E3_DATA DATACOM, "
    cQuery += " COALESCE(HASFO1.HASFO1, '2') HASFO1, '2' ORIGINAL_BILL "

    cQuery += " FROM " + RetSQLName("SE1") + " SE1 "
    cQuery += " JOIN " + RetSQLName("SED") + " SED "
    cQuery += " ON " + FwJoinFilial("SE1","SED") 
    cQuery += " AND E1_NATUREZ = ED_CODIGO "
    cQuery += " AND SED.D_E_L_E_T_ = ? "

    cQuery += " JOIN " + RetSQLName("SA1") + " SA1 "
    cQuery += " ON " + FwJoinFilial("SE1","SA1") 
    cQuery += " AND E1_CLIENTE = A1_COD "
    cQuery += " AND E1_LOJA = A1_LOJA "
    cQuery += " AND SA1.D_E_L_E_T_ = ? "

    cQuery += " JOIN " + RetSQLName("SA3") + " SA3 "
    cQuery += " ON " + FwJoinFilial("SE1","SA3")
    cQuery += " AND A3_COD = ? "
    cQuery += " AND SA3.D_E_L_E_T_ = ? "

    cQuery += " JOIN " + RetSQLName("FK7") + " FK7 "
    cQuery += " ON E1_FILIAL = FK7_FILTIT "
    cQuery += " AND E1_PREFIXO = FK7_PREFIX "
    cQuery += " AND E1_NUM = FK7_NUM "
    cQuery += " AND E1_PARCELA = FK7_PARCEL "
    cQuery += " AND E1_TIPO = FK7_TIPO "
    cQuery += " AND E1_CLIENTE = FK7_CLIFOR "
    cQuery += " AND E1_LOJA = FK7_LOJA "
    cQuery += " AND FK7.D_E_L_E_T_ = ? "
    cQuery += " AND FK7_ALIAS = ? "

    cQuery += " LEFT JOIN HASFO1 "
    cQuery += " ON FK7.FK7_IDDOC = HASFO1.FO1_IDDOC "

    cQuery += " JOIN TRACKER ON SE1.R_E_C_N_O_ = E1RECNO_FILHO "

    cQuery += " LEFT JOIN " + RetSQLName("SE3") + " SE3"
    cQuery += " ON " + FwJoinFilial("SE1","SE3")
    cQuery += " AND E1_PREFIXO = E3_PREFIXO "
    cQuery += " AND E1_NUM = E3_NUM "
    cQuery += " AND E1_PARCELA = E3_PARCELA "
    cQuery += " AND SE3.D_E_L_E_T_ = ? "

    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += " AND SE1.D_E_L_E_T_ = ?  " 

    If self:lForecastOfComission
        cQuery += " AND (SE1.E1_SALDO >= ? OR HASFO1.HASFO1 IS NOT NULL) "
    Else 
        cQuery += " AND (SE1.E1_SALDO > ? OR HASFO1.HASFO1 IS NOT NULL) "
    EndIF

    cQuery += " AND IDDOC_PAI = ? "

    cQuery += self:getRenegotiatedBillsQueryWhere()

    cQuery += self:getFilterToSQL()
    
    cQuery := changeQuery(cQuery)
    
    cQuery := "WITH HASFO1 AS ( "+ self:getHasFO1() +" ), TRACKER AS (" + self:getTrackerQuery() + ") " + cQuery

return cQuery


//-------------------------------------------------------------------
/*{Protheus.doc} getMainQueryWhere
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getMainQueryWhere() as character class BillsReceivableBySalesRepSmartViewBusinessObject
    local cQuery := "" as character
    
    cQuery += " AND SE1.E1_CLIENTE BETWEEN ? AND ? "
    cQuery += " AND SE1.E1_LOJA BETWEEN ? AND ? "
    cQuery += " AND SE1.E1_EMISSAO BETWEEN ? AND ? "
    cQuery += " AND SE1.E1_VENCTO BETWEEN ? AND ? "
    cQuery += " AND VW.VENDEDOR BETWEEN ? AND ? "
    cQuery += " AND SE1.E1_NATUREZ BETWEEN ? AND ? "

return cQuery

//-------------------------------------------------------------------
/*{Protheus.doc} handleMainQueryParameters
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleMainQueryParameters() class BillsReceivableBySalesRepSmartViewBusinessObject
    self:nCurrentParameterOrder := 1

    //getHasFO1()
    self:oStatement:setString(self:nCurrentParameterOrder++, ' ')

    self:oStatement:setUnsafe(self:nCurrentParameterOrder++, self:getFieldsToSQL())
    self:oStatement:setString(self:nCurrentParameterOrder++, ' ')
    self:oStatement:setString(self:nCurrentParameterOrder++, ' ')
    self:oStatement:setString(self:nCurrentParameterOrder++, ' ')
    self:oStatement:setString(self:nCurrentParameterOrder++, ' ')
    self:oStatement:setString(self:nCurrentParameterOrder++, 'SE1') 
    self:oStatement:setString(self:nCurrentParameterOrder++, ' ')
    self:setStatementBranchFilter(@self:nCurrentParameterOrder++)
    self:oStatement:setString(self:nCurrentParameterOrder++, ' ')
    self:oStatement:SetNumeric(self:nCurrentParameterOrder++, 0)

    self:handleMainQueryWhere()
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleMainQueryWhere
    @author guilhermed.santos@totvs.com.br
    @since 03/12/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method handleMainQueryWhere() class BillsReceivableBySalesRepSmartViewBusinessObject
    self:oStatement:setString(self:nCurrentParameterOrder++, self:cCustomerFrom)
    self:oStatement:setString(self:nCurrentParameterOrder++, self:cCustomerTo)
    self:oStatement:setString(self:nCurrentParameterOrder++, self:cCustomerUnitFrom)
    self:oStatement:setString(self:nCurrentParameterOrder++, self:cCustomerUnitTo)
    self:oStatement:setDate(self:nCurrentParameterOrder++, self:dIssueFrom)
    self:oStatement:setDate(self:nCurrentParameterOrder++, self:dIssueTo)
    self:oStatement:setDate(self:nCurrentParameterOrder++, self:dDuoFrom)
    self:oStatement:setDate(self:nCurrentParameterOrder++, self:dDuoTo)
    self:oStatement:setString(self:nCurrentParameterOrder++, self:cSalespersonFrom)
    self:oStatement:setString(self:nCurrentParameterOrder++, self:cSalespersonTo)
    self:oStatement:setString(self:nCurrentParameterOrder++, self:cBillClassFrom)
    self:oStatement:setString(self:nCurrentParameterOrder++, self:cBillClassTo)
return

//-------------------------------------------------------------------
/*{Protheus.doc} getRenegotiatedBillsQueryWhere
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getRenegotiatedBillsQueryWhere() as character class BillsReceivableBySalesRepSmartViewBusinessObject
return ""

//-------------------------------------------------------------------
/*{Protheus.doc} handleRenegotiatedBillsQueryParameters
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleRenegotiatedBillsQueryParameters() class BillsReceivableBySalesRepSmartViewBusinessObject
    self:nCurrentParameterOrder := 1

    //getHasFO1()
    self:oStRenegotiatedBills:setString(self:nCurrentParameterOrder++, ' ')

    //getTrackerQuery()
    self:oStRenegotiatedBills:setString(self:nCurrentParameterOrder++, ' ')

    //RenegotiatedBillsQuery
    self:oStRenegotiatedBills:setUnsafe(self:nCurrentParameterOrder++, self:getFieldsToSQL())
    self:oStRenegotiatedBills:setString(self:nCurrentParameterOrder++, ' ')
    self:oStRenegotiatedBills:setString(self:nCurrentParameterOrder++, ' ')
    self:oStRenegotiatedBills:setString(self:nCurrentParameterOrder++, self:cCurrentSalespersonCode)
    self:oStRenegotiatedBills:setString(self:nCurrentParameterOrder++, ' ')
    self:oStRenegotiatedBills:setString(self:nCurrentParameterOrder++, ' ')
    self:oStRenegotiatedBills:setString(self:nCurrentParameterOrder++, 'SE1')
    self:oStRenegotiatedBills:setString(self:nCurrentParameterOrder++, ' ')
    self:setRenegotiatedBillsStatementBranchFilter(@self:nCurrentParameterOrder++)
    self:oStRenegotiatedBills:setString(self:nCurrentParameterOrder++, ' ')
    self:oStRenegotiatedBills:setNumeric(self:nCurrentParameterOrder++, 0)
    self:oStRenegotiatedBills:setString(self:nCurrentParameterOrder++, (self:cAlias)->FK7_IDDOC)
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadDefaultParameters
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class BillsReceivableBySalesRepSmartViewBusinessObject
    self:cCustomerFrom := ""
    self:cCustomerTo := ""
    self:cCustomerUnitFrom := ""
    self:cCustomerUnitTo := ""
    self:dIssueFrom := cToD("  /  /    ")
    self:dIssueTo := cToD("  /  /    ")
    self:dDuoFrom := cToD("  /  /    ")
    self:dDuoTo := cToD("  /  /    ")
    self:cSalespersonFrom := ""
    self:cSalespersonTo := ""
    self:cBillClassFrom := ""
    self:cBillClassTo := ""
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class BillsReceivableBySalesRepSmartViewBusinessObject
    self:cCustomerFrom := MV_PAR01
    self:cCustomerTo := MV_PAR03
    self:cCustomerUnitFrom := MV_PAR02
    self:cCustomerUnitTo := MV_PAR04
    self:dIssueFrom := MV_PAR05
    self:dIssueTo := MV_PAR06
    self:dDuoFrom := MV_PAR07
    self:dDuoTo := MV_PAR08
    self:cSalespersonFrom := MV_PAR09
    self:cSalespersonTo := MV_PAR10
    self:cBillClassFrom := MV_PAR11
    self:cBillClassTo := MV_PAR12
return

//-------------------------------------------------------------------
/*{Protheus.doc} getSalespersonView
    Cria uma view em SQL que transforma o relacionamento entre 
    as tabelas SE1 e SA3 em 1 para n.

    @return character: View pronta para ser usada
    @author guilherme.sordi@totvs.com.br
    @since 23/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getSalespersonView() as character class BillsReceivableBySalesRepSmartViewBusinessObject
    local cQuery := "" as character
    local nX     := 0  as numeric
    local cVend  := "" as character

    for nX := 1 to 5
        cVend := "E1_VEND" + cValToChar(nX)

        cQuery += if( nX > 1, " UNION ", "")

        cQuery += "SELECT "+ cVend +" VENDEDOR, R_E_C_N_O_ "
        cQuery += "FROM "+ retSQLName("SE1") +" SE1 "
        cQuery += "WHERE SE1."+ cVend +" <> '' "
    next nX

    cQuery := "("+ cQuery +")"

return cQuery

//-------------------------------------------------------------------
/*{Protheus.doc} myAliasToData
    Percorre os resultados da query principal montando o objeto oData.
    Faz backup da área porque as funções do legado que calculam valores acessórios, abatimentos etc. exigem
    que o ponteiro esteja posicionado no título em questão.
    @author guilherme.sordi@totvs.com.br
    @since 23/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method myAliasToData() class BillsReceivableBySalesRepSmartViewBusinessObject    
    local aArea := {} as array
    local aAreaSE1 := {} as array
    local lIsValidRecord := .T. as logical

    self:setCompleteData(.T.)

    (self:cAlias)->(DBGoTop())    

    if (self:cAlias)->(Eof()) 
        return
    endIf    
    
    aArea := getArea()
    aAreaSE1 := SE1->(getArea())

    While !(self:cAlias)->(Eof())     
    
        self:cCurrentSalespersonCode := (self:cAlias)->VENDEDOR
        self:nCountValidRenegotiatedBills := 0

        if (self:cAlias)->HASFO1 == '1'
            self:appendRenegotiatedBills()
        endIf

        lIsValidRecord := (!self:lForecastOfComission .and. (self:cAlias)->E1_SALDO > 0 .or. self:nCountValidRenegotiatedBills > 0) .or.;
        (self:lForecastOfComission .and. ((self:cAlias)->E1_SALDO >= 0 .or. self:nCountValidRenegotiatedBills >= 0) .and. ((self:cAlias)->DATACOM == Nil .or. Empty((self:cAlias)->DATACOM)))

        if lIsValidRecord
            self:jData := JSONObject():new() 
            self:processAndAppend()
        endIf
        (self:cAlias)->(DbSkip())
    Enddo 

    restArea(aAreaSE1)
    restArea(aArea)
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    Atribui ao objeto oData o conteúdo do Alias e campos calculados.
    @author guilherme.sordi@totvs.com.br
    @since 23/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleData() class BillsReceivableBySalesRepSmartViewBusinessObject
    local lCreditBill      := .F. as logical
    local nDaysLate        := 0   as numeric
    local nNetAmount       := 0   as numeric
    local nAccessoryValues := 0   as numeric
    local nDeduction       := 0   as numeric
    local nInterest        := 0   as numeric
    local nDiscount        := 0   as numeric
    local nFineValue       := 0   as numeric
    local nBillValue       := 0   as numeric
    local nBillBalance     := 0   as numeric
    local jFormatedText as json

    jFormatedText := self:getFormatedText()

    lCreditBill := ((::cAlias)->E1_TIPO $ MVRECANT + "|" + MV_CRNEG)    
    
    nBillValue := (self:cAlias)->E1_VALOR
    nNetAmount := nBillBalance := (self:cAlias)->E1_SALDO

    SE1->(DbGoTo((::cAlias)->SE1RECNO))

    if !lCreditBill
        If dDatabase > (::cAlias)->E1_VENCREA
            nDaysLate := dateDiffDay((::cAlias)->E1_VENCTO, dDatabase)
        endIf

        nAccessoryValues := self:getAccessoryValues()        
        nNetAmount += nAccessoryValues
        
        nDeduction := self:getDeduction()
        If STR(nNetAmount,17,2) == STR(nDeduction,17,2)
            nNetAmount := 0
        Else
            nNetAmount -= nDeduction
        Endif

        nInterest := self:getInterestValue()  
        nFineValue := self:getFineValue()
        nDiscount := self:getDiscountValue()

        self:jValuesToComission := {"nBalance": nNetAmount, "nInterest": nInterest, "nFineValue": nFineValue, "nDiscount": nDiscount}
        
        nNetAmount += nInterest
        nNetAmount += nFineValue
        nNetamount -= nDiscount
    endIf
    
    if lCreditBill
        nBillValue *= -1
        nBillBalance *= -1
        nNetAmount *= -1
    endIf

    self:jData := {;
        "SALESPERSON_DATA": jFormatedText["cSalespersonData"],;
        "CUSTOMER_DATA": jFormatedText["cCustomerData"],;
        "BILL_DATA": jFormatedText["cBillData"],;
        "E1_VALOR": self:convertCurrencyValue(nBillValue),;
        "E1_SALDO": self:convertCurrencyValue(nBillBalance),;
        "E1_SDACRES": self:convertCurrencyValue((self:cAlias)->E1_SDACRES),;
        "E1_SDDECRE": self:convertCurrencyValue((self:cAlias)->E1_SDDECRE),;
        "E1_JUROS": self:convertCurrencyValue(nInterest),;
        "E1_MULTA": self:convertCurrencyValue(nFineValue),;
        "E1_DESCONT": self:convertCurrencyValue(nDiscount),;
        "ACCESSORY_VALUES": self:convertCurrencyValue(nAccessoryValues),;
        "NET_AMOUNT": self:convertCurrencyValue(nNetAmount),;
        "DAYS_LATE": nDaysLate}

    if (self:cAlias)->ORIGINAL_BILL == '1'
        self:jData["CLASSIFICATION"] := STR0011 //Título original
    else
        self:jData["CLASSIFICATION"] := STR0012 //Título renegociado
    endIf
 
return


//-------------------------------------------------------------------
/*{Protheus.doc} appendRenegotiatedBills
    Carrega para o oData todos os títulos filhos de um processo de liquidação.
    Faz a busca recursiva, retornando filhos de todos os níveis.
    @author guilherme.sordi@totvs.com.br
    @since 23/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method appendRenegotiatedBills() class BillsReceivableBySalesRepSmartViewBusinessObject
    local cRenegotiatedBillsAlias := "" as character
    local cBackupAlias := "" as character

    self:handleRenegotiatedBillsQueryParameters()
    cRenegotiatedBillsAlias := self:oStRenegotiatedBills:openAlias()    

    cBackupAlias := self:cAlias
    self:cAlias := cRenegotiatedBillsAlias

    while !((self:cAlias)->(eof()))    

        if (self:cAlias)->HASFO1 == '1'
            self:appendRenegotiatedBills()
        endIf

        self:jData := JSONObject():new() 
        self:processAndAppend()
        self:nCountValidRenegotiatedBills++
        (self:cAlias)->( dbSkip() )
    endDo
    (self:cAlias)->( DBCloseArea() )

    self:cAlias := cBackupAlias
return

//-------------------------------------------------------------------
/*{Protheus.doc} getHasFO1
@author guilherme.sordi@totvs.com.br
@since 23/05/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method getHasFO1() as character class BillsReceivableBySalesRepSmartViewBusinessObject
    local cQuery := "" as character
	cQuery += " SELECT FO1_IDDOC, '1' HASFO1 "
	cQuery += " FROM " + RetSQLName("FO1") + " FO1 "
	cQuery += " WHERE D_E_L_E_T_ = ? "
	cQuery += " GROUP BY FO1_IDDOC "
    cQuery := changeQuery(cQuery)
return cQuery


//-------------------------------------------------------------------
/*{Protheus.doc} getTrackerQuery
    Relaciona títulos pai de liquidação com títulos filhos de liquidação
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getTrackerQuery() as character class BillsReceivableBySalesRepSmartViewBusinessObject
    local cQuery := "" as character
    
    cQuery += " SELECT FO1.FO1_IDDOC IDDOC_PAI, SE1.R_E_C_N_O_ E1RECNO_FILHO "
    cQuery += " FROM " + RetSQLName("FO1") + " FO1 "
    cQuery += " JOIN " + RetSQLName("FO2") + " FO2 "
    cQuery += "     ON FO1_FILIAL = FO2_FILIAL "
    cQuery += "     AND FO1_PROCES = FO2_PROCES "
    cQuery += "     AND FO1_VERSAO = FO2_VERSAO "
    cQuery += " JOIN " + RetSQLName("SE1") + " SE1 "
    cQuery += "     ON FO2_FILIAL = E1_FILIAL "
    cQuery += "     AND FO2_PREFIX = E1_PREFIXO  "
    cQuery += "     AND FO2_NUM = E1_NUM "
    cQuery += "     AND FO2_PARCEL = E1_PARCELA "
    cQuery += "     AND FO2_TIPO = E1_TIPO "
    cQuery += "     AND SE1.D_E_L_E_T_ = ? "

    cQuery := changeQuery(cQuery)
return cQuery

//-------------------------------------------------------------------
/*{Protheus.doc} convertCurrencyValue
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method convertCurrencyValue(nValue as numeric, jCurrencyVariables as json, lConverttoCurrency1 as logical) as numeric class BillsReceivableBySalesRepSmartViewBusinessObject

    local dConvertion := dDatabase as numeric
    local nContractedRate := 0 as numeric
    local nOriginRate := 0 as numeric
    local nDestinationRate := 0 as numeric
    local nMovimentCurrency := 1 as numeric

    default lConverttoCurrency1 := .F.

    if jCurrencyVariables == NIL
        if "E1_EMISSAO" $ self:getFieldsToCompleteData()
            jCurrencyVariables := {"dConvertion": (self:cAlias)->E1_EMISSAO, "nContractedRate": (self:cAlias)->E1_TXMOEDA, "nMovimentCurrency": (self:cAlias)->E1_MOEDA}
        else                
            jCurrencyVariables := {"dConvertion": (self:cAlias)->C5_EMISSAO, "nContractedRate": (self:cAlias)->C5_TXMOEDA, "nMovimentCurrency": (self:cAlias)->C5_MOEDA}
        endIf
    endIf

    dConvertion := jCurrencyVariables["dConvertion"]
    nContractedRate := jCurrencyVariables["nContractedRate"]
    nMovimentCurrency := jCurrencyVariables["nMovimentCurrency"]

    if nMovimentCurrency == 1
        nDestinationRate := nContractedRate
    else
        nOriginRate := nContractedRate
    endIf
    
    if lConverttoCurrency1
        nValue := xMoeda(nValue, 1, nMovimentCurrency, dConvertion, self:nDecimals, nDestinationRate,nOriginRate)
    else
        nValue := xMoeda(nValue, nMovimentCurrency, self:nCurrency, dConvertion, self:nDecimals, nOriginRate, nDestinationRate)
    endIf
return nValue

//-------------------------------------------------------------------
/*{Protheus.doc} getAccessoryValues
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getAccessoryValues() as numeric class BillsReceivableBySalesRepSmartViewBusinessObject
    local nAccessoryValues := 0 as numeric
    local lSettled := .F. as logical
    local cPortfolio := "R" as character

    nAccessoryValues := FValAcess( (self:cAlias)->E1_PREFIXO, (self:cAlias)->E1_NUM, (self:cAlias)->E1_PARCELA, (self:cAlias)->E1_TIPO, (self:cAlias)->E1_CLIENTE, (self:cAlias)->E1_LOJA,;
        (self:cAlias)->E1_NATUREZ, lSettled, /*cCodVa*/, cPortfolio, /*dDtBaixa*/, /*aValAces*/, /*nMoedaTit*/, (self:cAlias)->E1_MOEDA, (self:cAlias)->E1_MOEDA, /*cIdFKD*/, /*lRetroativ*/ ) 
        
    nAccessoryValues += (self:cAlias)->E1_SDACRES - (self:cAlias)->E1_SDDECRE

return nAccessoryValues

//-------------------------------------------------------------------
/*{Protheus.doc} getDeduction
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getDeduction() as numeric class BillsReceivableBySalesRepSmartViewBusinessObject
    local nDeduction := 0 as numeric
    local cPortfolio := "R" as character

    SomaAbat((self:cAlias)->E1_PREFIXO, (self:cAlias)->E1_NUM, (self:cAlias)->E1_PARCELA, cPortfolio, (self:cAlias)->E1_MOEDA, dDatabase, (self:cAlias)->E1_CLIENTE, (self:cAlias)->E1_LOJA)

return nDeduction

//-------------------------------------------------------------------
/*{Protheus.doc} getInterestValue
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getInterestValue(nValue as numeric) as numeric class BillsReceivableBySalesRepSmartViewBusinessObject
    local nInterestValue := 0 as numeric
    local lRetroactiveView := .F. as logical
    local dLastPosting := (self:cAlias)->E1_BAIXA as date
    local lJurDevido := .F. as logical

    default nValue := (self:cAlias)->E1_SALDO

    if (self:cAlias)->E1_VENCREA >= dDatabase
        return 0
    endIf

    nInterestValue := fa070Juros((self:cAlias)->E1_MOEDA, nValue, "SE1", dLastPosting, (self:cAlias)->E1_MOEDA, lJurDevido, lRetroactiveView, dDatabase)

return nInterestValue

//-------------------------------------------------------------------
/*{Protheus.doc} getFineValue
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getFineValue() as numeric class BillsReceivableBySalesRepSmartViewBusinessObject
    local nFineValue := 0 as numeric
    local lIntegrationTINxProtheus := .F. as logical

    lIntegrationTINxProtheus := AllTrim((self:cAlias)->E1_ORIGEM) == "FINI055"

    if self:lCalculateFineValueBySIGALOJA    
        nFineValue := LojxRMul(,,,(self:cAlias)->E1_SALDO,(self:cAlias)->E1_ACRESC,(self:cAlias)->E1_VENCREA,dDataBase,,(self:cAlias)->E1_MULTA,,(self:cAlias)->E1_PREFIXO,(self:cAlias)->E1_NUM,(self:cAlias)->E1_PARCELA,(self:cAlias)->E1_TIPO,(self:cAlias)->E1_CLIENTE,(self:cAlias)->E1_LOJA,"SE1",.T.)    
    elseIf lIntegrationTINxProtheus
        if self:lAddIncreaseAndDecreaseToFineValueCalculation
            nFineValue := ( (self:cAlias)->E1_PORCJUR / 100 ) * ( (self:cAlias)->E1_SALDO + (self:cAlias)->E1_SDACRES - (self:cAlias)->E1_SDDECRE )
        else
            nFineValue := ( (self:cAlias)->E1_PORCJUR / 100 ) * (self:cAlias)->E1_SALDO
        endIf    
    endif

return nFineValue

//-------------------------------------------------------------------
/*{Protheus.doc} getDiscountValue
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getDiscountValue(nValue as numeric) as numeric class BillsReceivableBySalesRepSmartViewBusinessObject
    local nDiscountValue := 0 as numeric

    default nValue := (self:cAlias)->E1_SALDO

    if self:lProportionalDiscount .or. empty((self:cAlias)->E1_BAIXA) .or. (self:cAlias)->E1_BAIXA > dDatabase
        nDiscountValue := FaDescFin("SE1", dDatabase, nValue, (self:cAlias)->E1_MOEDA, /*lVerBxado*/, /*lTemGEM*/)
    EndIf
return nDiscountValue

//-------------------------------------------------------------------
/*{Protheus.doc} getDiscountValue
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getFormatedText() as json class BillsReceivableBySalesRepSmartViewBusinessObject
    local jFormatedText := JsonObject():New() as json

    jFormatedText["cCGC"] := self:transformCGC((self:cAlias)->A1_CGC,"A1_CGC")
    jFormatedText["cSalespersonData"] := allTrim((self:cAlias)->A3_COD) + ' - ' + allTrim((self:cAlias)->A3_NOME)
    jFormatedText["cCustomerData"] := allTrim((self:cAlias)->A1_COD) + '/' + allTrim((self:cAlias)->A1_LOJA) + ' - ' + allTrim((self:cAlias)->A1_NOME) + ' ('+ allTrim((self:cAlias)->A1_NREDUZ) +')'
    
    if "E1_NUM" $ self:getFieldsToCompleteData()
        jFormatedText["cBillData"] := allTrim((self:cAlias)->E1_PREFIXO) + '-' + allTrim((self:cAlias)->E1_NUM) + '-' + allTrim((self:cAlias)->E1_PARCELA) + '-' + allTrim((self:cAlias)->E1_TIPO)
        jFormatedText["cSalesOrderData"] := NIL
    else
        jFormatedText["cBillData"] := NIL
        jFormatedText["cSalesOrderData"] := allTrim((self:cAlias)->C6_NUM) + "-" + allTrim((self:cAlias)->C6_ITEM)
    end
    
return jFormatedText

//-------------------------------------------------------------------
/*{Protheus.doc} getFieldsToSQL
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getFieldsToSQL(aTables as array) as character class BillsReceivableBySalesRepSmartViewBusinessObject
    local cFields := "" as character
    local lContactTableName := .T. as logical
    local lOnlyCustomFields := .F.

    default aTables := self:aDefaultTables

    cFields := self:getSQLFields(lContactTableName, aTables, lOnlyCustomFields) + " "
    
    self:setFieldsToCompleteData(cFields)
return cFields


//-------------------------------------------------------------------
/*/{Protheus.doc} BillsReceivableBySalesRepSmartViewBusinessObject
    O método getSQLBranchFilter adiicona o filtro de filial com bind parameters.
    Esse método preenche os bind parameters no objeto self:oStRenegotiatedBills.
    Sugestão: Passar o nParamOrder como referência para continuar a numeração dos parâmetros
    da query no método chamador.
    @author guilherme.sordi@totvs.com.br
    @since 25/10/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method setRenegotiatedBillsStatementBranchFilter(nParamOrder as numeric) class BillsReceivableBySalesRepSmartViewBusinessObject
    default nParamOrder := 1
    if self:lAllowBranchFilter .and. self:lMultSelectBranches
        if self:lEmptyMultiBranches
            self:oStRenegotiatedBills:SetString(nParamOrder++, fwXFilial(self:cMainTable))
        else
            self:oStRenegotiatedBills:SetIn(nParamOrder++, self:aMainTableSelectedBranches)
        endIf
    endIf
return
