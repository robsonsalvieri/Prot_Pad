#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.listofissuedchecks.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SEF,SA6", customTables="SEF,SA6", name="Relação de cheques", country="ALL")

/*/{Protheus.doc} ListOfIssuedChecksSmartViewBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
class ListOfIssuedChecksSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object
    public method mySetSchema() as object

    protected data nCurrencyCheck as numeric
    protected data lSumValue as logical
    protected data cGroupByComplement as character

    protected data lOnlyPayable as logical
    protected data lOnlyReceivable as logical
    protected data cBankCodeFrom as character
    protected data cBankCodeTo as character
    protected data cBankBranchFrom as character
    protected data cBankBranchTo as character
    protected data cBankAccountFrom as character
    protected data cBankAccountTo as character
    protected data cCheckNumberFrom as character
    protected data cCheckNumberTo as character
    protected data dIssueFrom as date
    protected data dIssueTo as date
    protected data nCurrency as date
    protected data nParam as numeric
    protected data lConvertCurrency as logical  
    protected data lOnlyApprovedNo as logical  
    protected data lOnlyApprovedYes as logical  

    protected method initializeBusinessObject()
    protected method loadStatement()
    protected method handleData()
    protected method loadParameters()
    protected method loadDefaultParameters()
    protected method convertCurrency() as numeric
    protected method getApprovedText() as character
    protected method getNameText() as character
    protected method getWhereSEF() as character
    protected method setBindSEF()
endclass


/*/{Protheus.doc} new
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
method new() class ListOfIssuedChecksSmartViewBusinessObject
    _Super:new()

    self:initializeBusinessObject()
    self:setAllTrim(.F.)
    self:setCompleteData(.T.)
    self:setAllowFilterByBranch(.T.)

    self:nCurrencyCheck := 1
    self:nParam := 1
    self:lSumValue := !SuperGetMV("MV_LEGF191", .F., .T.) //Bloqueia a edição do valor nominal do cheque quando já existe um título vinculado ao mesmo registro do cheque.
    self:cGroupByComplement := ""
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject

@author Fábio Andrade
@since 05/01/2024
@version 1.0
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class ListOfIssuedChecksSmartViewBusinessObject
    self:setDisplayName(STR0001)  //Relação de Cheques 
    self:setDescription(STR0001)  //Relação de Cheques 
    
    self:setPergunte("FINT013")
return


/*/{Protheus.doc} mySetSchema
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
method mySetSchema() class ListOfIssuedChecksSmartViewBusinessObject

    local aFieldsSEF   := {} as array

    aFieldsSEF := { "EF_FILIAL", "EF_BANCO", "EF_AGENCIA", "EF_CONTA", "EF_NUM" ,"EF_VALOR" ,"EF_DATA" ,"EF_VENCTO",;
    "EF_EMITENT","EF_HIST","EF_LIBER", "EF_ALINEA1", "EF_CART" }

    self:finAddProperty("APPROVED"  , STR0002, "string")   //"Situação do cheque"
    self:finAddProperty("NAME"      , STR0005, "string", "EF_CLIENTE") //"Cliente emitente / Beneficiário"
    self:finAddProperty("PORTFOLIO" , STR0007, "string")  //"Carteira"
    self:finAddProperty("CHECK_DATA", STR0010, "string") //"Dados do cheque"

    self:oSchema:aliasToSchema("SEF", aFieldsSEF )

    FwFreeArray(aFieldsSEF)

return


/*/{Protheus.doc} loadStatement
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
method loadStatement() class ListOfIssuedChecksSmartViewBusinessObject
    local cQuery  := "" as character
    local cFields := "" as character
    local cGroupBy := "" as character

    self:loadFilterBranches({"SEF","SA6"})

    self:aStruct := {}
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "EF_DATA" ) )
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "EF_VENCTO" ) )    

    cFields  := self:getAllFieldsToSQL()
    cGroupBy := cFields + ", EF_CLIENTE, EF_BENEF "
    cFields  += ", EF_CLIENTE CLIENTE, EF_BENEF BENEF "

    cQuery := "SELECT ? "

    if self:lSumValue
        cQuery += ", CASE WHEN EF_CART = 'R' THEN SUM(EF_VALOR) ELSE MAX(EF_VALOR) END VALOR "
    else
        cQuery += ", EF_VALOR VALOR "
        cGroupBy += ', EF_VALOR '
    endIf        
    cQuery += " , COALESCE(A6_MOEDA,1) A6_MOEDA "

    cQuery += " FROM " + RetSqlName("SEF") + " SEF "
    cQuery += " LEFT JOIN " + RetSqlName("SA6") + " SA6 "
    if self:lEmptyMultiBranches
        cQuery += " ON SA6.A6_FILIAL = ? "
    else
        cQuery += " ON SA6.A6_FILIAL IN ( ? ) "
    endIf
    cQuery += " AND SA6.A6_COD = SEF.EF_BANCO "
    cQuery += " AND SA6.A6_AGENCIA = SEF.EF_AGENCIA "
    cQuery += " AND SA6.A6_NUMCON = SEF.EF_CONTA"

    cQuery += " AND SA6.D_E_L_E_T_ = ? "
    
    if self:lEmptyMultiBranches
        cQuery += " WHERE EF_FILIAL = ? "    
    else
        cQuery += " WHERE EF_FILIAL IN ( ? ) "
    endIf       
	cQuery += " AND EF_BANCO BETWEEN ? AND ? "    
	cQuery += " AND EF_AGENCIA BETWEEN ? AND ? "  
	cQuery += " AND EF_CONTA BETWEEN ? AND ?  "   
	cQuery += " AND EF_NUM BETWEEN ? AND ?  "     
	cQuery += " AND EF_DATA BETWEEN ? AND ?  "  

    if self:lOnlyPayable
        cQuery += " AND EF_CART IN ( ? ) "  
    endIf

    if self:lOnlyReceivable
        cQuery += " AND EF_CART = ? "  
    endIf

    if self:lOnlyApprovedYes
        cQuery += " AND EF_LIBER = ? "
    endIf

    if self:lOnlyApprovedNo
        cQuery += " AND EF_LIBER IN ( ? ) "
    endIf

    if !self:lConvertCurrency .and. !self:lOnlyReceivable
        cQuery += " AND (A6_MOEDA IS NULL OR A6_MOEDA = ?) "
    EndIf

    cQuery += self:getWhereSEF()

    cQuery += " AND EF_NUM <> ? "
    cQuery += " AND SEF.D_E_L_E_T_ = ? "

    cQuery += self:getFilterToSQL()

    cQuery += " GROUP BY " + cGroupBy + ', A6_MOEDA'

    cQuery += " ORDER BY " + SqlOrder(SEF->(IndexKey( 1 )))

    cQuery := ChangeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)

    self:oStatement:setUnsafe(self:nParam++, cFields)

    if self:lEmptyMultiBranches
        self:oStatement:SetString(self:nParam++, self:oFilterBranches["SA6"][1])
        self:oStatement:SetString(self:nParam++, ' ')
        self:oStatement:SetString(self:nParam++, self:oFilterBranches["SEF"][1]) 
    else
        self:oStatement:SetIn(self:nParam++, self:oFilterBranches["SA6"][1])
        self:oStatement:SetString(self:nParam++, ' ')
        self:oStatement:SetIn(self:nParam++, self:oFilterBranches["SEF"][1]) 
    endIf

    self:oStatement:SetString(self:nParam++, self:cBankCodeFrom) 
    self:oStatement:SetString(self:nParam++, self:cBankCodeTo) 
    self:oStatement:SetString(self:nParam++, self:cBankBranchFrom) 
    self:oStatement:SetString(self:nParam++, self:cBankBranchTo) 
    self:oStatement:SetString(self:nParam++, self:cBankAccountFrom) 
    self:oStatement:SetString(self:nParam++, self:cBankAccountTo) 
    self:oStatement:SetString(self:nParam++, self:cCheckNumberFrom) 
    self:oStatement:SetString(self:nParam++, self:cCheckNumberTo) 
    self:oStatement:SetDate(self:nParam++, self:dIssueFrom) 
    self:oStatement:SetDate(self:nParam++, self:dIssueTo) 

    if self:lOnlyPayable
        self:oStatement:SetIn(self:nParam++, {' ', 'P'})
    endIf
    if self:lOnlyReceivable
        self:oStatement:SetString(self:nParam++, 'R')
    endIf

    if self:lOnlyApprovedYes
        self:oStatement:SetString(self:nParam++, 'S')
    endIf
    if self:lOnlyApprovedNo
        self:oStatement:SetIn(self:nParam++, {' ', 'N'})
    endIf

    if !self:lConvertCurrency .and. !self:lOnlyReceivable
        self:oStatement:setNumeric(self:nParam++, self:nCurrency)   
    endIf

    self:setBindSEF()

    self:oStatement:SetString(self:nParam++, ' ')
    self:oStatement:SetString(self:nParam++, ' ')
    
return

/*/{Protheus.doc} handleData
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
method handleData() class ListOfIssuedChecksSmartViewBusinessObject
    local nCheckValue := 0 as numeric
    local xDuoDate := nil
    local cPortfolio := ""
    local cCheckData := ""

    self:nCurrencyCheck := SA6->A6_MOEDA
    nCheckValue := self:convertCurrency((self:cAlias)->VALOR)
    
    if (self:cAlias)->EF_CART == 'R'
        xDuoDate := self:varToTimeStamp((self:cAlias)->EF_VENCTO)
        cPortfolio := STR0009 //"Receber"
    else
        xDuoDate := NIL
        cPortfolio := STR0008 //"Pagar"
    endIf

    cCheckData := (self:cAlias)->EF_BANCO + "-" + (self:cAlias)->EF_AGENCIA + "-" + (self:cAlias)->EF_CONTA + "-" + (self:cAlias)->EF_NUM

    self:jData := {; 
        "CHECK_DATA": cCheckData,;
        "EF_VALOR": nCheckValue,;
        "EF_VENCTO": xDuoDate,;
        "PORTFOLIO": cPortfolio,;
        "APPROVED": self:getApprovedText(),;
        "NAME": self:getNameText();
        }
return

/*/{Protheus.doc} loadParameters
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
method loadParameters() class ListOfIssuedChecksSmartViewBusinessObject   
    if self:oCurrentFilter != NIL
        self:lOnlyPayable := MV_PAR01 == 1
        self:lOnlyReceivable := MV_PAR01 == 2
        self:cBankCodeFrom := MV_PAR02
        self:cBankCodeTo := MV_PAR03
        self:cBankBranchFrom := MV_PAR04
        self:cBankBranchTo := MV_PAR05
        self:cBankAccountFrom := MV_PAR06
        self:cBankAccountTo := MV_PAR07
        self:cCheckNumberFrom := MV_PAR08
        self:cCheckNumberTo := MV_PAR09
        self:dIssueFrom := MV_PAR10
        self:dIssueTo := MV_PAR11
        self:nCurrency := MV_PAR12
        self:lConvertCurrency := MV_PAR13 == 2
        self:lOnlyApprovedNo := MV_PAR14 == 2
        self:lOnlyApprovedYes := MV_PAR14 == 1
    endIf
return

/*/{Protheus.doc} loadDefaultParameters
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
method loadDefaultParameters() class ListOfIssuedChecksSmartViewBusinessObject
    self:lOnlyPayable := .F.
    self:lOnlyReceivable := .F.
    self:cBankCodeFrom := ""
    self:cBankCodeTo := ""
    self:cBankBranchFrom := ""
    self:cBankBranchTo := ""
    self:cBankAccountFrom := ""
    self:cBankAccountTo := ""
    self:cCheckNumberFrom := ""
    self:cCheckNumberTo := ""
    self:dIssueFrom := dDataBase
    self:dIssueTo := dDataBase
    self:nCurrency := dDataBase
    self:lConvertCurrency := .F. 
    self:lOnlyApprovedNo := .F.
    self:lOnlyApprovedYes := .F.
return

/*/{Protheus.doc} convertCurrency
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
method convertCurrency(nValor as numeric) as numeric class ListOfIssuedChecksSmartViewBusinessObject    
    local dConversao := (self:cAlias)->EF_DATA as date    

    nValor := xMoeda(nValor, self:nCurrencyCheck, self:nCurrency, dConversao, self:nDecimals)
return nValor

/*/{Protheus.doc} getApprovedText
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
method getApprovedText() as character class ListOfIssuedChecksSmartViewBusinessObject
    local cApproved := "N/A" as character
    
    if (self:cAlias)->EF_CART <> 'R'
        if (self:cAlias)->EF_LIBER == 'N'
            cApproved := STR0004 //"Bloqueado"
        else
            cApproved := STR0003 //"Liberado
        endIf
    endIf
return cApproved

/*/{Protheus.doc} getNameText
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
method getNameText() as character class ListOfIssuedChecksSmartViewBusinessObject
    local cName := "" as character
    
    if (self:cAlias)->EF_CART <> 'R'
        cName := (self:cAlias)->BENEF
    else
        cName := (self:cAlias)->CLIENTE + ' ' + (self:cAlias)->EF_EMITENT      
    endIf
    cName := allTrim(cName)
return cName

/*/{Protheus.doc} getWhereSEF
    @author guilherme.sordi@totvs.com.br
    @since 02/09/2023
    @version 12.1.2210
*/
method getWhereSEF() as character class ListOfIssuedChecksSmartViewBusinessObject
    local cWhere := "" as character
    cWhere := " AND EF_IMPRESS IN ( ? ) "
    cWhere += " AND EF_ALINEA1 = ? "
return cWhere

/*/{Protheus.doc} setBindSEF
    @author fabioh.andrade@totvs.com.br
    @since 04/09/2024
    @version 12.1.2210
*/
method setBindSEF() class ListOfIssuedChecksSmartViewBusinessObject
    self:oStatement:SetIn(self:nParam++, {' ', 'S', 'N'})
    self:oStatement:SetString(self:nParam++, ' ')
return 
