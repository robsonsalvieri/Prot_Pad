#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.fin.dailyfinancialstatement.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SA6, FK5", name="Movimento Financeiro Diário", country="ALL", customTables="SA6")

//-------------------------------------------------------------------
/*/{Protheus.doc} DailyFinancialStatementSmartViewBusinessObject
Classe para criação do Objeto de Negócio para listagem de Movimento Financeiro Diário

@author Fábio Henrique Andrade
@since 10/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
class DailyFinancialStatementSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider    
    public method new() as object
    public method mySetSchema() as object
    public method myAliasToData() as object

    protected data oStFinancialMovements as object
    protected data dReferenceDate as date
    protected data nCurrency as numeric
    protected data nBankCurrency as numeric
    protected data lConvertCurrencyRate as logical
    protected data lSumCreditLimit as logical

    protected data cMovAlias as character
    protected data nInflow as numeric
    protected data nOutflow as numeric
    protected data nInvestments as numeric
    
    protected method loadStatement() 
    protected method loadParameters()
    protected method loadDefaultParameters()
    protected method updateSummary()
    protected method initializeBusinessObject()
    protected method loadFinancialMovementsStatement()
    protected method loadFinancialMovements()
    protected method processFinancialMovements()
    protected method convertCurrency() as numeric
    protected method getMovementCurrencyRate() as numeric
    protected method getOriginCurrencyRate() as numeric
    protected method getDestinyCurrencyRate() as numeric
    protected method getBankBalances() as array
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Fábio Henrique Andrade
@since 10/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method new() class DailyFinancialStatementSmartViewBusinessObject
    _Super:new()

    self:initializeBusinessObject()
    self:setCompleteData(.T.)
    self:setMainTable("SA6")
    self:setBranchFilter(.T., .T.)

    self:nCurrency      := 1
    self:nBankCurrency  := 1
    self:dReferenceDate := dDatabase
    self:nInflow        := 0
    self:nOutflow       := 0
    self:nInvestments   := 0
    self:cMovAlias      := ""
return self


//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject

@author matheus.monteiro@totvs.com.br
@since 09/01/2024
@version 1.0
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class DailyFinancialStatementSmartViewBusinessObject
    self:setDisplayName(STR0002) //"Movimento Financeiro Diário"
    self:setDescription(STR0002) //"Movimento Financeiro Diário"
    self:setPergunte("FINR530")
return

//-------------------------------------------------------------------
/*{Protheus.doc} mySetSchema
Retorna a estrutura dos campos e conjunto de parametros

@return object: self:oSchema

@author matheus.monteiro@totvs.com.br
@since 09/01/2024
@version 1.0
*/
//-------------------------------------------------------------------
method mySetSchema() class DailyFinancialStatementSmartViewBusinessObject
    Local aFieldsSA6 as array

    aFieldsSA6 := { "A6_FILIAL", "A6_COD", "A6_AGENCIA", "A6_NUMCON", "A6_NREDUZ", "A6_LIMCRED", "A6_MOEDA" }

    self:oSchema:aliasToSchema("SA6", aFieldsSA6)

    self:finAddProperty("SALDOINI" , STR0003, "number")   //"Saldo Inicial"
    self:finAddProperty("ENTRADA"  , STR0004, "number")    //"Entrada"
    self:finAddProperty("SAIDA"    , STR0005, "number")      //"Saída"
    self:finAddProperty("APLICACAO", STR0006, "number")  //"Aplicações"
    self:finAddProperty("SALDOFIM" , STR0007, "number")   //"Saldo Disponível"
    self:finAddProperty("FILIAL_SALDO" , STR0008, "string") //"Filial do saldo"

    FwFreeArray(aFieldsSA6)
return self:oSchema


//-------------------------------------------------------------------
/*{Protheus.doc} myAliasToData
Atribui ao objeto oData o conteúdo do Alias e campos calculados.
@param oFilter, objeto, contém o filtro do TReports
@author Fábio Henrique Andrade
@since 19/07/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method myAliasToData(oFilter as object) as object class DailyFinancialStatementSmartViewBusinessObject
    local nClosingBalance := 0 as numeric
    local nOpeningBalance := 0 as numeric
    local aBalances := {} as array
    local nX := 1 as numeric

    (self:cAlias)->(DBGoTop())

    while !(self:cAlias)->(Eof())

        self:nBankCurrency := Max((self:cAlias)->A6_MOEDA, 1)

        aBalances := self:getBankBalances()
        for nX := 1 to len(aBalances)
            nOpeningBalance := aBalances[nX]['e8_salatua']

            if self:lSumCreditLimit
                nOpeningBalance += xMoeda((self:cAlias)->A6_LIMCRED, self:nBankCurrency, self:nCurrency, self:dReferenceDate, self:nDecimals)
            endIf
            
            self:loadFinancialMovements(aBalances[nX]['cMovFilter'])

            nClosingBalance := nOpeningBalance + self:nInflow + self:nInvestments - self:nOutflow

            self:jData := {"A6_COD": (self:cAlias)->A6_COD,;
                "A6_AGENCIA": (self:cAlias)->A6_AGENCIA,;
                "A6_NUMCON": (self:cAlias)->A6_NUMCON,;
                "A6_NREDUZ": (self:cAlias)->A6_NREDUZ,;
                "FILIAL_SALDO": aBalances[nX]['cGroupBranch'],;
                "SALDOINI": nOpeningBalance,;
                "ENTRADA": self:nInflow,;
                "SAIDA": self:nOutflow,;
                "APLICACAO": self:nInvestments,;
                "SALDOFIM": nClosingBalance ,;
                "A6_FILIAL": (self:cAlias)->A6_FILIAL,;
                "A6_LIMCRED": (self:cAlias)->A6_LIMCRED,;
                "A6_MOEDA": self:nBankCurrency}

            self:processAndAppend()
        next

        (self:cAlias)->(DbSkip())
    endDo
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} loadStatement
    Cria o objeto self:oStatement da classe FWExecStatement com a query principal 
    do objeto de negócio.
    @author guilherme.sordi@totvs.com.br
    @since 22/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadStatement() class DailyFinancialStatementSmartViewBusinessObject
    local cQuery        := "" as character
    local nParamOrder   := 1 as numeric

    cQuery := " SELECT ?, SA6.R_E_C_N_O_ SA6_RECNO "
    cQuery += " FROM " + RetSQLName("SA6") + " SA6 "
    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()

    If !self:lConvertCurrencyRate
        cQuery += " AND SA6.A6_MOEDA = ? "
    EndIf

    cQuery += " AND SA6.D_E_L_E_T_ = ?  "
    cQuery += " ? "

    self:oStatement := FWExecStatement():New(ChangeQuery(cQuery))
     
    self:oStatement:SetUnsafe(nParamOrder++, self:getAllFieldsToSQL())
    self:setStatementBranchFilter(@nParamOrder)

	If !self:lConvertCurrencyRate
        self:oStatement:SetNumeric(nParamOrder++, self:nCurrency)
    EndIf  

    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetUnsafe(nParamOrder++, self:getFilterToSQL())
return 

/*{Protheus.doc} updateSummary
    @author guilherme.sordi@totvs.com.br
    @since 11/11/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method updateSummary() class DailyFinancialStatementSmartViewBusinessObject
    self:nInflow += self:convertCurrency((self:cMovAlias)->ENTRADA)
    self:nOutflow += self:convertCurrency((self:cMovAlias)->SAIDA)
    self:nInvestments += self:convertCurrency((self:cMovAlias)->APLICACAO)
return 

/*/{Protheus.doc} convertCurrency
    @author guilherme.sordi@totvs.com.br
    @since 11/10/2023
    @version 12.1.2210
*/
method convertCurrency(nValue as numeric) as numeric class DailyFinancialStatementSmartViewBusinessObject
    nValue := round(xMoeda(nValue, self:nBankCurrency, self:nCurrency, (self:cMovAlias)->FK5_DTDISP, self:nDecimals, self:getOriginCurrencyRate(), self:getDestinyCurrencyRate()), self:nDecimals-1 )
return nValue

/*/{Protheus.doc} getOriginCurrencyRate
    @description Um título moeda 2 baixado em um banco moeda 2, no contas a receber, vai gravar 1 no campo 
    FK5_TXMOED. Nesse caso, ignoramos o conteúdo desse campo e mandamos 0 como taxa do movimento na 
    função xMoeda para que ela considere a taxa da SM2 da data do movimento.
    @author guilherme.sordi@totvs.com.br
    @since 11/10/2023
    @version 12.1.2210
*/
method getMovementCurrencyRate() as numeric class DailyFinancialStatementSmartViewBusinessObject
    local nCurrencyRate := (self:cMovAlias)->FK5_TXMOED as numeric
    if nCurrencyRate == 1
        nCurrencyRate := 0
    endIf
return nCurrencyRate

/*/{Protheus.doc} getOriginCurrencyRate
    @author guilherme.sordi@totvs.com.br
    @since 11/10/2023
    @version 12.1.2210
*/
method getOriginCurrencyRate() as numeric class DailyFinancialStatementSmartViewBusinessObject
    local nCurrencyRate as numeric    
    if self:nBankCurrency > 1 
        nCurrencyRate := self:getMovementCurrencyRate()
    endIf
return nCurrencyRate

/*/{Protheus.doc} getDestinyCurrencyRate
    @description Em caso de movimento oriundo de um título com taxa contratada,
    estamos supondo que a moeda do relatório é a moeda de origem do movimento.
    
    Se o objeto de negócio está em moeda maior que um, o movimento bancário está
    em moeda 1, a taxa do movimento é uma taxa contratada ou informada (diferente de SM2),
    e a moeda de origem do movimento é diferente da moeda do objeto de negócio,
    não se deve usar a taxa do movimento (FK5_TXMOED) como taxa de destino da conversão.
    O certo seria
    Opção 1 - converter para a moeda de origem do movimento (do título) usando a taxa
    do movimento e depois converter para a moeda do objeto de negócio usando a SM2 ou
    Opção 2 - converter diretamente da moeda 1 para a moeda do objeto de negócio desconsiderando
    a taxa do movimento (afinal, essa taxa foi contratada para a moeda do título e não para a 
    moeda do objeto de negócio).
    Será necessário uma análise mais profunda da regra de negócio para se estabelecer um padrão 
    que atenda todas as necessidades.
    @author guilherme.sordi@totvs.com.br
    @since 11/10/2023
    @version 12.1.2210
*/
method getDestinyCurrencyRate() as numeric class DailyFinancialStatementSmartViewBusinessObject
    local nCurrencyRate as numeric
    if self:nBankCurrency == 1
        nCurrencyRate := self:getMovementCurrencyRate()
    endIf
return nCurrencyRate

//-------------------------------------------------------------------
/*/{Protheus.doc} loadDefaultParameters
    Inicializa as propriedades de parâmetros do objeto de negócio
    para não correr riscos de error.log.
    Será implementado no objeto de negócio.
    @author guilherme.sordi@totvs.com.br
    @since 08/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class DailyFinancialStatementSmartViewBusinessObject
    self:dReferenceDate := dDatabase
    self:nCurrency := 1
    self:lSumCreditLimit := .F.
	self:lConvertCurrencyRate := .F.
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadParameters
    Carrega os parâmetros da consulta antes de chamar o getQuery().
    Será implementado no objeto de negócio, de acordo com a necessidade e
    os parâmetros utilizados.
    @author guilherme.sordi@totvs.com.br
    @since 26/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class DailyFinancialStatementSmartViewBusinessObject
    if self:oCurrentFilter != NIL
		self:dReferenceDate := MV_PAR01
        self:nCurrency := MV_PAR02
        self:lSumCreditLimit := MV_PAR03 == 1
		self:lConvertCurrencyRate := MV_PAR04 == 1
    endIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadFinancialMovements
    Carrega a movimentação financeira da FK5 para o banco posicionado no self:cAlias
    @author guilherme.sordi@totvs.com.br
    @since 06/11/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method loadFinancialMovements(cFilter as character) class DailyFinancialStatementSmartViewBusinessObject
    local nParamOrder := 1 as numeric
    default cFilter := ""

    self:loadFinancialMovementsStatement()

    self:oStFinancialMovements:SetDate(nParamOrder++, self:dReferenceDate)
    self:oStFinancialMovements:SetString(nParamOrder++, " ")
    self:oStFinancialMovements:SetString(nParamOrder++, (self:cAlias)->A6_FILIAL)
    self:oStFinancialMovements:SetString(nParamOrder++, (self:cAlias)->A6_COD)
    self:oStFinancialMovements:SetString(nParamOrder++, (self:cAlias)->A6_AGENCIA)
    self:oStFinancialMovements:SetString(nParamOrder++, (self:cAlias)->A6_NUMCON)
    self:oStFinancialMovements:SetString(nParamOrder++, " ")
    self:oStFinancialMovements:SetUnsafe(nParamOrder++, self:getFilterToSQL())
    self:oStFinancialMovements:SetUnsafe(nParamOrder++, cFilter)
    
    self:processFinancialMovements()
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadFinancialMovementsStatement
    Prepara o objeto self:oStFinancialMovements da classe FWExecStatement, 
    que será responsável pelo consulta da movimentação financeira no banco de dados.
    @author guilherme.sordi@totvs.com.br
    @since 06/11/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method loadFinancialMovementsStatement() class DailyFinancialStatementSmartViewBusinessObject
    local cQuery as character
    local cReferenceDate := DtoS(self:dReferenceDate) as character

    if self:oStFinancialMovements == NIL
        cQuery := " SELECT "
        cQuery += " COALESCE((CASE WHEN FK5.FK5_RECPAG = 'R' AND FK5.FK5_TPDOC <> 'AP' THEN FK5.FK5_VALOR ELSE 0 END), 0) ENTRADA, "
        cQuery += " COALESCE((CASE WHEN FK5.FK5_RECPAG = 'P' AND FK5.FK5_TPDOC <> 'AP' THEN FK5.FK5_VALOR ELSE 0 END), 0) SAIDA, "
        cQuery += " COALESCE((CASE WHEN FK5.FK5_TPDOC = 'AP' THEN (CASE WHEN FK5.FK5_RECPAG = 'P' THEN - FK5.FK5_VALOR ELSE FK5.FK5_VALOR END)ELSE 0 END),0) APLICACAO,"
        cQuery += " COALESCE(FK5.FK5_TXMOED,0) AS FK5_TXMOED, COALESCE(FK5.FK5_DTDISP, '" + cReferenceDate + "') AS FK5_DTDISP, FK5.FK5_VLMOE2 "

        cQuery += " FROM " + RetSqlName("SA6") + " SA6 "
        
        cQuery += " JOIN " + RetSqlName("FK5") + " FK5 ON " + FwJoinFilial("SA6", "FK5")
        cQuery += " AND FK5.FK5_BANCO = SA6.A6_COD  "
        cQuery += " AND FK5.FK5_AGENCI = SA6.A6_AGENCIA "
        cQuery += " AND FK5.FK5_CONTA = SA6.A6_NUMCON "
        cQuery += " AND FK5.FK5_DTDISP = ? "
        cQuery += " AND FK5.D_E_L_E_T_ = ?  "
        
        cQuery += " WHERE "
        cQuery += " SA6.A6_FILIAL = ?  "
        cQuery += " AND SA6.A6_COD = ?  "
        cQuery += " AND SA6.A6_AGENCIA = ?  "
        cQuery += " AND SA6.A6_NUMCON = ?  "
        cQuery += " AND SA6.D_E_L_E_T_ = ?  "

        cQuery += " ? "
        cQuery += " ? "

        self:oStFinancialMovements := FWExecStatement():new(ChangeQuery(cQuery))
    endIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} processFinancialMovements
    Processa o retorno da query de movimentos, somando separadamente entrada, saída e apropriação de investimentos e empréstimos.
    É feito em um loop em vez de agrupamento na query por causa da conversão de saldos.
    @author guilherme.sordi@totvs.com.br
    @since 06/11/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method processFinancialMovements() class DailyFinancialStatementSmartViewBusinessObject
    self:nInflow := 0
    self:nOutflow := 0
    self:nInvestments := 0

    self:cMovAlias := self:oStFinancialMovements:openAlias()
    
    (self:cMovAlias)->(DBGoTop())
    while !(self:cMovAlias)->(Eof())
        self:updateSummary()
        (self:cMovAlias)->(DbSkip())
    endDo
    (self:cMovAlias)->(DbCloseArea())

    self:cMovAlias := ""
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getBankBalances
    @description Carrega uma lista de saldos bancários para o determinado cadastro de banco,
    conforme compartilhamento das tabelas SA6, SE8 e FK5.
    O resultado respeita o filtro de filiais feito no Smart View e a moeda do objeto de negócio
    definida pelo Smart View, fazendo as conversões necessárias do saldo.
    @author guilherme.sordi@totvs.com.br
    @since 08/11/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method getBankBalances() as array class DailyFinancialStatementSmartViewBusinessObject
    local oBanks := Banks():new() as object
    local aBalances := {} as array

    oBanks:setBranchFilter(self:aMultiBranches)
    oBanks:setCurrency(self:nCurrency)
    aBalances := oBanks:getBankBalances((self:cAlias)->SA6_RECNO, self:dReferenceDate)
return aBalances
