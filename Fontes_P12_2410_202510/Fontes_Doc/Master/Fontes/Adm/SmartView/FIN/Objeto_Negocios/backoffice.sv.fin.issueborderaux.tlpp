#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.issueborderaux.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE1,SEA,SA6,FK7,SA1,SA2", customTables="SEA,SE1,SA1,SE2,SA2", name="Emissão de Borderôs", country="ALL")
//-------------------------------------------------------------------
/*/{Protheus.doc} IssueBorderauxSmartViewBusinessObject
Classe para criação do Objeto de Negócio
@author Fábio Henrique Andrade
@since 16/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
class IssueBorderauxSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object   

    protected data nOrdemQryParamsPagar as numeric
    protected data nOrdemQryParamsReceber as numeric
    private data oStFk1 as object
    private data oStFk2 as object
    private data oObjAb as object
    protected data nDecimaisMoeda as numeric
    protected data dConversaoMoeda as date

    protected data nMoeda as numeric
    protected data lListarOutrasMoedas as logical
    protected data cCarteiraBordero as character
    protected data cAliasSE as character
    protected data cSimboloMoeda as character
    protected data aBackupArea as array
    protected data aBackupAreaSE as array
    protected data lShowInstructions as logical

    protected data lHasGroupItem7 as logical
    protected data lShowSettledBill as logical
    protected data lOnlyCnab as logical
    protected data lOnlyOnline as logical
    
    protected method initializeBusinessObject()
    protected method loadDefaultParameters()

    protected method mySetSchema()
    protected method loadStatement() 
    protected method getMainQueryRec() as character
    protected method getMainQueryPay() as character
    protected method getWherePay() as character
    protected method getWhereRec() as character
    protected method getSelectComplementPay() as character
    protected method getSelectComplementRec() as character

    protected method loadStatementPay() as character
    protected method loadStatementRec() as character
    protected method setQryParamsPagar()
    protected method setQryParamsReceber()
    protected method setBindParametersRec()
    protected method setBindParametersPay()
    protected method handleData()
    protected method loadParameters()
    protected method getValorTitulo() as numeric
    protected method buscaValorMovimentoBaixado() as numeric
    protected method loadHasGroupItem7()
    protected method preWhileMyAliasToData()
    protected method postWhileMyAliasToData()
    protected method getLocalizationFieldsPay() as character
    protected method getCalculatedTaxes() as numeric

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Fábio Henrique Andrade
@since 16/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method new() as object class IssueBorderauxSmartViewBusinessObject    
    self:loadHasGroupItem7()
    
    _Super:new()

    self:initializeBusinessObject()
    self:setCompleteData(.T.)
    self:setAllTrim(.T.)
    self:setAllowSmartViewFilter(.F.)
    self:setMainTable("SEA")
    self:setBranchFilter(.T., .T.)

    ::nOrdemQryParamsReceber := 1
    ::nOrdemQryParamsPagar := 1

    ::dConversaoMoeda := dDatabase
    ::nDecimaisMoeda := self:nDecimals

    self:cSimboloMoeda := ""
    self:aBackupArea := {}
    self:aBackupAreaSE := {}
    self:lShowInstructions := .F.

return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 09/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class IssueBorderauxSmartViewBusinessObject
    self:setDisplayName(STR0002) //"Títulos enviados ao banco"
    self:setDescription(STR0003) //"Emissão relatorio de Borderos para envio ao banco"
    self:setPergunte("FINT005") 
return

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 09/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadStatement() class IssueBorderauxSmartViewBusinessObject

    self:loadCustomFieldsPayAndRec()

    If ::cCarteiraBordero == "P"
        self:loadStatementPay()
    ElseIf ::cCarteiraBordero == "R"
        self:loadStatementRec()
    EndIf

return 

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 09/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method mySetSchema() class IssueBorderauxSmartViewBusinessObject
    Local aFieldsSEA := {} As Array
    Local aFieldsSA6 := {} As Array
    Local aFieldsSE1 := {} As Array                                                                               

    aFieldsSEA := { "EA_FILIAL", "EA_NUMBOR", "EA_PREFIXO", "EA_NUM", "EA_PARCELA",;
                    "EA_TIPO", "EA_FORNECE", "EA_LOJA", "EA_PORTADO", "EA_AGEDEP", "EA_NUMCON",; 
                    "EA_DATABOR"}
    aFieldsSA6 := { "A6_COD", "A6_NOME", "A6_AGENCIA", "A6_BAIRRO", "A6_MUN", "A6_EST" }
    aFieldsSE1 := { "E1_SALDO", "E1_CLIENTE", "E1_LOJA", "E1_EMISSAO", "E1_VENCTO", "E1_VENCREA", "E1_MOEDA"}

    self:oSchema:aliasToSchema("SEA", aFieldsSEA )
    self:oSchema:aliasToSchema("SA6", aFieldsSA6 )
    self:oSchema:aliasToSchema("SE1", aFieldsSE1 )
    self:oSchema:aliasToSchema("SA1", "A1_NOME" )
    self:oSchema:aliasToSchema("SEB", "EB_DESCRI" )
    self:oSchema:aliasToSchema("FRV", "FRV_DESCRI" )

    self:setPropDisplayName("EB_DESCRI", STR0024) //Ocorrência
    self:setPropDisplayName("FRV_DESCRI", STR0025) //Situação
    self:setPropDisplayName("A1_NOME", STR0019) //"Nome Cli/Forn."
    self:setPropDisplayName("E1_CLIENTE", STR0020) //"Cod. Cli/Forn."

    self:finAddProperty("VALOR", STR0004, "number") //Valor Titulo
    self:finAddProperty("SIMB_MOED", STR0005, "string") //Símbolo Moeda
    self:finAddProperty("DADOS_BANCO"  , STR0009, "string", "A6_COD")    //"Dados Banco"
    self:finAddProperty("DADOS_CLIFOR" , STR0010, "string", "A1_NOME")   //"Dados Cli/Forn."
    self:finAddProperty("BORTYPE", STR0021, "string") //"Modalidade"
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatementRec
Retorna a query a ser executada na função getData 
para ::cCarteiraBordero = "R"

@author Fábio Henrique Andrade Silva
@since 16/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadStatementRec(oFilter as object) as character class IssueBorderauxSmartViewBusinessObject
    Local cQuery  := "" as character
    Local lQueryUnion := .T. as logical 

    cQuery := self:getMainQueryRec()
    
    if self:lShowInstructions
        cQuery += " UNION ALL "
        cQuery += self:getMainQueryRec( lQueryUnion )
    endIf
    cQuery := ChangeQuery( cQuery )

    self:oStatement := FWExecStatement():New( cQuery )
    
    self:setBindParametersRec()

    if self:lShowInstructions
        self:setBindParametersRec( lQueryUnion )
    endIf

return

//-------------------------------------------------------------------
/*{Protheus.doc} getMainQueryRec
Retorna a string da query a ser concatenada no método loadStatementRec 
para ::cCarteiraBordero = "R"

@author Fábio Henrique Andrade Silva
@since 19/09/2024
@version 12.1.2410
*/
//-------------------------------------------------------------------
method getMainQueryRec(lQueryUnion as logical) as character class IssueBorderauxSmartViewBusinessObject
    local cQuery    := "" as character
    default lQueryUnion := .F.
    
    cQuery := " SELECT DISTINCT ? " 
    cQuery += " FROM " + RetSqlName("SEA") + " SEA " 
    cQuery += " INNER JOIN "
    cQuery += " " + RetSqlName("SA6") + " SA6 " 
    cQuery += " ON " + FWJoinFilial("SA6", "SEA")
    cQuery += " AND SA6.A6_COD     = SEA.EA_PORTADO "
    cQuery += " AND SA6.A6_AGENCIA = SEA.EA_AGEDEP "
    cQuery += " AND SA6.A6_NUMCON  = SEA.EA_NUMCON "
    cQuery += " AND SA6.D_E_L_E_T_ = ? "
    
    cQuery += " INNER JOIN " + RetSqlName("SE1") + " SE1 "
    cQuery += " ON " + FWJoinFilial("SE1", "SEA")
    cQuery += " AND SE1.E1_PREFIXO = SEA.EA_PREFIXO "
    cQuery += " AND SE1.E1_NUM     = SEA.EA_NUM "
    cQuery += " AND SE1.E1_PARCELA = SEA.EA_PARCELA "
    cQuery += " AND SE1.E1_TIPO    = SEA.EA_TIPO "
    cQuery += " AND SE1.E1_NUMBOR  = SEA.EA_NUMBOR "
    cQuery += " AND SE1.D_E_L_E_T_ = ? "
    
    cQuery += " INNER JOIN " + RetSqlName("SA1") + " SA1 " 
    cQuery += " ON " + FWJoinFilial("SA1", "SE1")
    cQuery += " AND SA1.A1_COD  = SE1.E1_CLIENTE "
    cQuery += " AND SA1.A1_LOJA = SE1.E1_LOJA "
    cQuery += " AND SA1.D_E_L_E_T_ = ? "
    
    cQuery += " INNER JOIN " + RetSqlName("FK7") + " FK7 "
    cQuery += " ON  FK7.FK7_FILTIT = SE1.E1_FILIAL "
    cQuery += " AND FK7.FK7_PREFIX = SE1.E1_PREFIXO "
    cQuery += " AND FK7.FK7_NUM = SE1.E1_NUM "
    cQuery += " AND FK7.FK7_PARCEL = SE1.E1_PARCELA "
    cQuery += " AND FK7.FK7_TIPO = SE1.E1_TIPO "
    cQuery += " AND FK7.FK7_CLIFOR = SE1.E1_CLIENTE "
    cQuery += " AND FK7.FK7_LOJA = SE1.E1_LOJA "
    cQuery += " AND FK7.D_E_L_E_T_ = ? "
    cQuery += " AND FK7_ALIAS = ? "

    cQuery += " LEFT JOIN " + RetSqlName("SEB") + " SEB "
    cQuery += " ON " + FWJoinFilial("SEB", "SE1")
    cQuery += " AND SEB.EB_BANCO = SE1.E1_PORTADO "
    cQuery += " AND SEB.EB_REFBAN = SE1.E1_OCORREN "
    cQuery += " AND SEB.EB_TIPO = ? "
    cQuery += " AND SEB.D_E_L_E_T_ = ? "

    cQuery += " LEFT JOIN " + RetSqlName("FRV") + " FRV "
    cQuery += " ON " + FWJoinFilial("FRV", "SE1")
    cQuery += " AND SE1.E1_SITUACA = FRV.FRV_CODIGO "
    cQuery += " AND FRV.D_E_L_E_T_ = ? "

    if lQueryUnion
        cQuery += " INNER JOIN " + RetSqlName("FI2") + " FI2 "
	    cQuery += " ON " + FWJoinFilial("FI2", "SEA")
	    cQuery += " AND FI2.FI2_CARTEI = ? "
	    cQuery += " AND FI2.FI2_NUMBOR = SEA.EA_NUMBOR "
		cQuery += " AND FI2.FI2_PREFIX = SEA.EA_PREFIXO "
		cQuery += " AND FI2.FI2_TITULO = SEA.EA_NUM "
		cQuery += " AND FI2.FI2_PARCEL = SEA.EA_PARCELA "
		cQuery += " AND FI2.FI2_TIPO = SEA.EA_TIPO "
		cQuery += " AND FI2.FI2_CODCLI = SE1.E1_CLIENTE "
		cQuery += " AND FI2.FI2_LOJCLI = SE1.E1_LOJA "
        cQuery += " AND FI2.D_E_L_E_T_ = ? "
    endIf

    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += " AND SEA.EA_CART = ? "

    if self:lOnlyCnab        
        cQuery += " AND SEA.EA_BORAPI <> ? "
    endIf

    if self:lOnlyOnline
        cQuery += " AND SEA.EA_BORAPI = ? "
        cQuery += " AND SEA.EA_TRANSF = ? "
    endIf

    cQuery += self:getWhereRec()

    cQuery += " AND SEA.D_E_L_E_T_ = ? "
    
    if (!self:lShowInstructions) .OR. (self:lShowInstructions .AND. lQueryUnion)
        cQuery += " ORDER BY " + SqlOrder(SEA->(IndexKey( 1 )))
    endIf

return cQuery

//-------------------------------------------------------------------
/*{Protheus.doc} setBindParametersRec
Executa o parser dos binds da query para ::cCarteiraBordero = "R"

@author Fábio Henrique Andrade Silva
@since 19/09/2024
@version 12.1.2410
*/
//-------------------------------------------------------------------
method setBindParametersRec(lQueryUnion as logical) class IssueBorderauxSmartViewBusinessObject
    local cFields := "" as character
    default lQueryUnion := .F.

    self:loadFilterBranches( { "SEA" } )
    self:addFieldsToStruct( { "EA_DATABOR", "E1_EMISSAO", "E1_VENCTO", "E1_VENCREA", "E2_BAIXA" } )
                                                                                         
    cFields := " EA_FILIAL, EA_NUMBOR, EA_PREFIXO, EA_NUM, EA_PARCELA, EA_TIPO, EA_FORNECE, EA_LOJA, EA_PORTADO, EA_AGEDEP, EA_NUMCON, EA_DATABOR, "
    cFields += " A6_COD, A6_NOME, A6_AGENCIA, A6_BAIRRO, A6_MUN, A6_EST, A1_NOME, SA1.R_E_C_N_O_ CLIFORRECN, "
    cFields += " E1_SALDO, SEA.R_E_C_N_O_ AS RECNOSEA, EA_BORAPI, EA_TRANSF, "
    cFields += " E1_CLIENTE, E1_LOJA, E1_EMISSAO, E1_VENCTO, E1_VENCREA, E1_MOEDA, SE1.R_E_C_N_O_ TITNRECNO, "
    cFields += " FK7_IDDOC, EB_DESCRI, FRV_DESCRI "

    cFields += self:getSelectComplementRec(lQueryUnion)

    cFields += self:commaAtBeginning(self:cSelectCustomFieldsRec)

    self:oStatement:SetUnsafe(::nOrdemQryParamsReceber++, cFields)  
    self:oStatement:SetString(::nOrdemQryParamsReceber++, " ")

    self:oStatement:SetString(::nOrdemQryParamsReceber++, " ")

    self:oStatement:SetString(::nOrdemQryParamsReceber++, " ")

    self:oStatement:SetString(::nOrdemQryParamsReceber++, " ")
    self:oStatement:SetString(::nOrdemQryParamsReceber++, "SE1")

    self:oStatement:SetString(::nOrdemQryParamsReceber++, "E")
    self:oStatement:SetString(::nOrdemQryParamsReceber++, " ")

    self:oStatement:SetString(::nOrdemQryParamsReceber++, " ")

    if lQueryUnion
        self:oStatement:SetString(::nOrdemQryParamsReceber++, "1")
        self:oStatement:SetString(::nOrdemQryParamsReceber++, " ")
    endIf
    
    self:setStatementBranchFilter(@self:nOrdemQryParamsReceber)

    self:oStatement:SetString(::nOrdemQryParamsReceber++, self:cCarteiraBordero)
    
    if self:lOnlyCnab        
        self:oStatement:SetString(::nOrdemQryParamsReceber++, 'S')
    endIf

    if self:lOnlyOnline
        self:oStatement:SetString(::nOrdemQryParamsReceber++, 'S')
        self:oStatement:SetString(::nOrdemQryParamsReceber++, 'S')
    endIf

    self:setQryParamsReceber()

    self:oStatement:SetString(::nOrdemQryParamsReceber++, " ")
return 

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatementRec
Método que será sobrescrito pelas classes filhas para completarem os parametros 
da query de borderos a receber

@author Fábio Henrique Andrade Silva
@since 20/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method setQryParamsReceber() class IssueBorderauxSmartViewBusinessObject

    self:oStatement:SetString(::nOrdemQryParamsReceber++, MV_PAR02)
    self:oStatement:SetString(::nOrdemQryParamsReceber++, MV_PAR03)

    If !::lListarOutrasMoedas
        self:oStatement:SetNumeric(::nOrdemQryParamsReceber++, ::nMoeda)
    EndIf    

    if !self:lShowSettledBill
        self:oStatement:SetNumeric(::nOrdemQryParamsReceber++, 0)
    endIf

return 

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatementPay
Retorna a query a ser executada na função getData 
para ::cCarteiraBordero = "P"

@author Fábio Henrique Andrade Silva
@since 19/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadStatementPay() class IssueBorderauxSmartViewBusinessObject
    Local cQuery  := "" as character
    Local lQueryUnion := .T. as logical 

    cQuery := self:getMainQueryPay()
    
    if self:lShowInstructions
        cQuery += " UNION ALL "
        cQuery += self:getMainQueryPay( lQueryUnion )
    endIf
    cQuery := ChangeQuery( cQuery )

    self:oStatement := FWExecStatement():New( cQuery )
    
    self:setBindParametersPay()

    if self:lShowInstructions
        self:setBindParametersPay( lQueryUnion )
    endIf
       
return

//-------------------------------------------------------------------
/*{Protheus.doc} getMainQueryPay
Retorna a string da query a ser concatenada no método loadStatementRec 
para ::cCarteiraBordero = "P"

@author Fábio Henrique Andrade Silva
@since 19/09/2024
@version 12.1.2410
*/
//-------------------------------------------------------------------
method getMainQueryPay(lQueryUnion as logical) as character class IssueBorderauxSmartViewBusinessObject
    local cQuery    := "" as character
    default lQueryUnion := .F.
    
    cQuery := " SELECT DISTINCT ? "                                                                               
    cQuery += " FROM " + RetSQLName("SEA") + " SEA " 
    
    cQuery += " INNER JOIN " + RetSQLName("SA6") + " SA6 " 
    cQuery += " ON " + FWJoinFilial("SA6", "SEA")
    cQuery += " AND SA6.A6_COD     = SEA.EA_PORTADO "
    cQuery += " AND SA6.A6_AGENCIA = SEA.EA_AGEDEP "
    cQuery += " AND SA6.A6_NUMCON  = SEA.EA_NUMCON "
    cQuery += " AND SA6.A6_COD     = SEA.EA_PORTADO "
    cQuery += " AND SA6.D_E_L_E_T_ = ? "
    
    cQuery += " INNER JOIN " + RetSQLName("SE2") + " SE2 " 
    cQuery += " ON " + FWJoinFilial("SE2", "SEA") 
    cQuery += " AND SE2.E2_PREFIXO = SEA.EA_PREFIXO "
    cQuery += " AND SE2.E2_NUM     = SEA.EA_NUM "
    cQuery += " AND SE2.E2_PARCELA = SEA.EA_PARCELA "
    cQuery += " AND SE2.E2_TIPO    = SEA.EA_TIPO "
    cQuery += " AND SE2.E2_FORNECE = SEA.EA_FORNECE "
    cQuery += " AND SE2.E2_LOJA    = SEA.EA_LOJA "
    cQuery += " AND SE2.E2_NUMBOR  = SEA.EA_NUMBOR "
    cQuery += " AND SE2.D_E_L_E_T_ = ? "
    
    cQuery += " INNER JOIN " + RetSQLName("SA2") + " SA2 " 
    cQuery += " ON " + FWJoinFilial("SA2", "SE2")
    cQuery += " AND SA2.A2_COD  = SE2.E2_FORNECE "
    cQuery += " AND SA2.A2_LOJA = SE2.E2_LOJA "
    cQuery += " AND SA2.D_E_L_E_T_ = ? "
    
    cQuery += " INNER JOIN " + RetSQLName("FK7") + " FK7 "
    cQuery += " ON  FK7.FK7_FILTIT = SE2.E2_FILIAL "
    cQuery += " AND FK7.FK7_PREFIX = SE2.E2_PREFIXO"
    cQuery += " AND FK7.FK7_NUM = SE2.E2_NUM"
    cQuery += " AND FK7.FK7_PARCEL = SE2.E2_PARCELA"
    cQuery += " AND FK7.FK7_TIPO = SE2.E2_TIPO"
    cQuery += " AND FK7.FK7_CLIFOR = SE2.E2_FORNECE"
    cQuery += " AND FK7.FK7_LOJA = SE2.E2_LOJA"
    cQuery += " AND FK7.D_E_L_E_T_ = ? "
    cQuery += " AND FK7_ALIAS = ? "
    
    cQuery += " INNER JOIN " + RetSQLName("SED") + " SED "
    cQuery += " ON " + FWJoinFilial("SED", "SE2")
    cQuery += " AND SE2.E2_NATUREZ = SED.ED_CODIGO "
    cQuery += " AND SED.D_E_L_E_T_ = ? "

    if lQueryUnion
        cQuery += " INNER JOIN " + RetSqlName("FI2") + " FI2 "
	    cQuery += " ON " + FWJoinFilial("FI2", "SEA")
	    cQuery += " AND FI2.FI2_CARTEI = ? "
	    cQuery += " AND FI2.FI2_NUMBOR = SEA.EA_NUMBOR "
		cQuery += " AND FI2.FI2_PREFIX = SEA.EA_PREFIXO "
		cQuery += " AND FI2.FI2_TITULO = SEA.EA_NUM "
		cQuery += " AND FI2.FI2_PARCEL = SEA.EA_PARCELA "
		cQuery += " AND FI2.FI2_TIPO = SEA.EA_TIPO "
		cQuery += " AND FI2.FI2_CODFOR = SE2.E2_FORNECE "
		cQuery += " AND FI2.FI2_LOJFOR = SE2.E2_LOJA "
        cQuery += " AND FI2.D_E_L_E_T_ = ? "
    endIf

    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += " AND SEA.EA_CART = ? "

    if self:lOnlyCnab
        cQuery += " AND SEA.EA_BORAPI <> ? "
    endIf

    if self:lOnlyOnline
        cQuery += " AND SEA.EA_BORAPI = ? "
    endIf

    cQuery += self:getWherePay()

    cQuery += " AND SEA.D_E_L_E_T_ = ? "
    
    if (!self:lShowInstructions) .OR. (self:lShowInstructions .AND. lQueryUnion)
        cQuery += " ORDER BY " + SqlOrder(SEA->(IndexKey( 1 )))
    endIf

return cQuery

//-------------------------------------------------------------------
/*{Protheus.doc} setBindParametersRec
Executa o parser dos binds da query para ::cCarteiraBordero = "R"

@author Fábio Henrique Andrade Silva
@since 19/09/2024
@version 12.1.2410
*/
//-------------------------------------------------------------------
method setBindParametersPay(lQueryUnion as logical) class IssueBorderauxSmartViewBusinessObject
    local cFields := "" as character
    default lQueryUnion := .F.

    self:loadFilterBranches({"SEA"})
    self:addFieldsToStruct({ "EA_DATABOR", "E1_EMISSAO", "E1_VENCTO", "E1_VENCREA" })
            
    cFields := " EA_FILIAL, EA_NUMBOR, EA_PREFIXO, EA_NUM, EA_PARCELA, EA_TIPO, EA_FORNECE, EA_LOJA, EA_PORTADO, EA_AGEDEP, EA_NUMCON, EA_DATABOR, "
    cFields += " A6_COD, A6_NOME, A6_AGENCIA, A6_BAIRRO, A6_MUN, A6_EST, A2_NOME AS A1_NOME, SA2.R_E_C_N_O_ CLIFORRECN, "
    cFields += " E2_SALDO AS E1_SALDO, SEA.R_E_C_N_O_ AS RECNOSEA, EA_BORAPI, EA_TRANSF, "
    cFields += " E2_FORNECE AS E1_CLIENTE, E2_LOJA AS E1_LOJA, E2_EMISSAO AS E1_EMISSAO, E2_VENCTO AS E1_VENCTO, "
    cFields += " E2_VENCREA AS E1_VENCREA, E2_MOEDA AS E1_MOEDA, SE2.R_E_C_N_O_ TITNRECNO, "
    cFields += " FK7_IDDOC, '' EB_DESCRI, '' FRV_DESCRI "
    
    cFields += self:getLocalizationFieldsPay()
    
    cFields += self:getSelectComplementPay(lQueryUnion)
    
    cFields += self:commaAtBeginning(self:cSelectCustomFieldsPay)

    self:oStatement:setUnsafe(::nOrdemQryParamsPagar++, cFields)
    
    self:oStatement:SetString(::nOrdemQryParamsPagar++, " ")
    
    self:oStatement:SetString(::nOrdemQryParamsPagar++, " ")
    
    self:oStatement:SetString(::nOrdemQryParamsPagar++, " ")
    
    self:oStatement:SetString(::nOrdemQryParamsPagar++, " ")
    self:oStatement:SetString(::nOrdemQryParamsPagar++, "SE2")
    
    self:oStatement:SetString(::nOrdemQryParamsPagar++, " ")
    
    if lQueryUnion
        self:oStatement:SetString(::nOrdemQryParamsPagar++, "2")
        self:oStatement:SetString(::nOrdemQryParamsPagar++, " ")
    endIf

    if self:lEmptyMultiBranches
        self:oStatement:SetString(::nOrdemQryParamsPagar++, self:oFilterBranches["SEA"][1])
    else
        self:oStatement:SetIn(::nOrdemQryParamsPagar++, self:oFilterBranches["SEA"][1])
    endIf
    self:oStatement:SetString(::nOrdemQryParamsPagar++, self:cCarteiraBordero)
   
    if self:lOnlyCnab        
        self:oStatement:SetString(::nOrdemQryParamsPagar++, "S")
    endIf

    if self:lOnlyOnline
        self:oStatement:SetString(::nOrdemQryParamsPagar++, "S")
    endIf

    self:setQryParamsPagar()

    self:oStatement:SetString(::nOrdemQryParamsPagar++, " ")

return 

//-------------------------------------------------------------------
/*{Protheus.doc} setQryParamsPagar
Método que será sobrescrito pelas classes filhas para completarem os parametros 
da query de borderos a pagar

@author Fábio Henrique Andrade Silva
@since 20/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method setQryParamsPagar() class IssueBorderauxSmartViewBusinessObject

    self:oStatement:SetString(::nOrdemQryParamsPagar++, MV_PAR02)
    self:oStatement:SetString(::nOrdemQryParamsPagar++, MV_PAR03)

    If !::lListarOutrasMoedas
        self:oStatement:SetNumeric(::nOrdemQryParamsPagar++, ::nMoeda)
    EndIf    

    if !self:lShowSettledBill
        self:oStatement:SetNumeric(::nOrdemQryParamsPagar++, 0)
    endIf  

return 

//-------------------------------------------------------------------
/*{Protheus.doc} preWhileMyAliasToData
    @author guilherme.sordi@totvs.com.br
    @since 18/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method preWhileMyAliasToData() class IssueBorderauxSmartViewBusinessObject    
    self:cSimboloMoeda := SuperGetMV("MV_SIMB" + Alltrim(cValToChar(self:nMoeda)))
    self:aBackupArea := getArea()
    If self:cCarteiraBordero == "P"    
        aBackupAreaSE := SE2->(getArea())
    Else
        aBackupAreaSE := SE1->(getArea())
    EndIf

return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
Atribui ao objeto oData o conteúdo do Alias e campos calculados.
@param oFilter, objeto, contém o filtro do TReports
@author Fábio Henrique Andrade
@since 19/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData() class IssueBorderauxSmartViewBusinessObject
    Local cBankData             := "" as character
    Local cCustomerSupplierData := "" as character
    local cType                 := "" as character

    if (self:cAlias)->EA_BORAPI == "S"
        cType := STR0023
    else
        cType := STR0022
    endIf

    cBankData := (::cAlias)->EA_PORTADO +"-"+(::cAlias)->EA_AGEDEP +"-"+(::cAlias)->EA_NUMCON
    cCustomerSupplierData := (::cAlias)->E1_CLIENTE+"/"+(::cAlias)->E1_LOJA+"-"+alltrim((::cAlias)->A1_NOME)

    self:jData := {;
        "VALOR": self:getValorTitulo(),;
        "SIMB_MOED": self:cSimboloMoeda,;
        "DADOS_BANCO": cBankData,;
        "DADOS_CLIFOR": cCustomerSupplierData,;
        "BORTYPE": cType;
    }
 
return

//-------------------------------------------------------------------
/*{Protheus.doc} postWhileMyAliasToData
    @author guilherme.sordi@totvs.com.br
    @since 18/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method postWhileMyAliasToData() class IssueBorderauxSmartViewBusinessObject
    restArea(self:aBackupAreaSE)
    restArea(self:aBackupArea)
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters

Carrega os parâmetros informados pelo usuário para as variáveis públicas MV_PAR
e atualiza as propriedades de filtro da classe.

As propriedades de filtro da classe ajudam a compreesão do código (boas práticas de código limpo).

self:lHasGroupItem7
Em novembro de 2023 foram criadas 3 novas perguntas e a pergunta "outras moedas" foi alterada para ficar igual 
aos outros objetos de negócio do financeiro (1 = Não listar, 2 = Converter)

@param oFilter, objeto, contém o filtro do TReports
@author Fábio Henrique Andrade
@since 16/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadParameters() class IssueBorderauxSmartViewBusinessObject
    Local aCarteiras := {"P","R"}

    self:nMoeda := MV_PAR04
    self:lListarOutrasMoedas := MV_PAR05 == 1
    self:cCarteiraBordero := aCarteiras[MV_PAR01]            

    If self:cCarteiraBordero == "P"
        self:cAliasSE := "SE2"
    Else
        self:cAliasSE := "SE1"
    EndIf

    if self:lHasGroupItem7 
        self:lListarOutrasMoedas := MV_PAR05 == 2
        self:lShowSettledBill := MV_PAR06 == 1
        self:lOnlyCnab := MV_PAR07 == 1
        self:lOnlyOnline := MV_PAR07 == 2
    endIf

    FwFreeArray(aCarteiras)
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadDefaultParameters()
    @author guilherme.sordi
    @since 09/11/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class IssueBorderauxSmartViewBusinessObject
    self:nMoeda := 1
    self:lListarOutrasMoedas := .F.
    self:cCarteiraBordero := "P"
    self:cAliasSE := "SE2"
    self:lShowSettledBill := .T.
    self:lOnlyCnab := .F.
    self:lOnlyOnline := .F.
return

/*/{Protheus.doc} getValorTitulo()
    Retorna o valor do titulo
    @author Francisco Oliveira
    @since 13/04/2023
    @version 12.1.2210
/*/
method getValorTitulo() as numeric class IssueBorderauxSmartViewBusinessObject
	Local nValor    := 0   As Numeric
	Local nAbat     := 0   As Numeric
    Local nVA       := 0   As Numeric
    Local nTotalMov := 0   As Numeric
	Local nImpoCalc := 0   As Numeric
	Local cTitPai   := ""  As Character
    Local nRecno := (::cAlias)->TITNRECNO as numeric
	
	(::cAliasSE)->(DbGoto(nRecno))	
    
    nTotalMov := self:buscaValorMovimentoBaixado()
	
	If ::cCarteiraBordero == "R"

        nVA := FValAcess(SE1->E1_PREFIXO, SE1->E1_NUM, SE1->E1_PARCELA, SE1->E1_TIPO, SE1->E1_CLIENTE, SE1->E1_LOJA,SE1->E1_NATUREZ, .F., /*cCodVa*/, "R")
        
        If nTotalMov == 0 .And. SE1->E1_SALDO > 0 .And. SE1->E1_SALDO != SE1->E1_VALOR .And. !Empty(SE1->E1_BAIXA)
			nValor := SE1->(E1_SALDO+E1_SDACRES) - SE1->E1_SDDECRE
		Else
			nValor := (SE1->(E1_VALOR+E1_ACRESC) - SE1->E1_DECRESC)
		EndIf

        nValor += nVA
        nValor := xMoeda(nValor, SE1->E1_MOEDA, ::nMoeda, SE1->E1_EMISSAO, ::nDecimaisMoeda, SE1->E1_TXMOEDA)
		
		cTitPai := SE1->(E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO+E1_CLIENTE+E1_LOJA)		
		nAbat := FinTotAb("SE1", cTitPai, SE1->E1_FILORIG, ::nMoeda, ::dConversaoMoeda, ::nDecimaisMoeda, ::oObjAb, MVABATIM, "|")
	Else

        nVA := FValAcess(SE2->E2_PREFIXO,SE2->E2_NUM,SE2->E2_PARCELA,SE2->E2_TIPO,SE2->E2_FORNECE,SE2->E2_LOJA,SE2->E2_NATUREZ, .F., "", "P")
    
        If nTotalMov == 0 .And. SE2->E2_SALDO > 0 .And. SE2->E2_SALDO != SE2->E2_VALOR .And. !Empty(SE2->E2_BAIXA)
		    nValor := SE2->(E2_SALDO+E2_SDACRES) - SE2->E2_SDDECRE
		Else
			nValor := (SE2->(E2_VALOR+E2_ACRESC) - SE2->E2_DECRESC)
		EndIf
        
        nValor += nVA
        nValor := xMoeda(nValor, SE2->E2_MOEDA, ::nMoeda, SE2->E2_EMISSAO, ::nDecimaisMoeda, SE2->E2_TXMOEDA)
        

        //Total de impostos calculado, previsão de retenção.
        If !SE2->E2_TIPO $ MVPAGANT
            nImpoCalc := self:getCalculatedTaxes()
        EndIf
		
		cTitPai := SE2->(E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA)				
		nAbat := FinTotAb("SE2", cTitPai, SE2->E2_FILORIG, ::nMoeda, ::dConversaoMoeda, ::nDecimaisMoeda, ::oObjAb, MVABATIM, "|")
	Endif
	
    nValor -= (nTotalMov + nImpoCalc + nAbat)

Return nValor

//------------------------------------
/*/{Protheus.doc} buscaValorMovimentoBaixado
Busca o total de movimento baixado, antes da geração do borderô
@author Sivaldo Oliveira
@since 15/04/2021
@version 12.1.2210
/*/
//------------------------------------
method buscaValorMovimentoBaixado() as numeric class IssueBorderauxSmartViewBusinessObject
	Local nValue      := 0                           as numeric
	Local cQuery      := ""                          as character
	Local cTblTmp     := ""                          as character
    Local nParamOrder := 1                           as numeric
    Local cIDDocFK    := (::cAlias)->FK7_IDDOC       as character
    Local dDataBord   := (::cAlias)->EA_DATABOR		 as date
    Local aMotBxNotIn :={'PCC', 'IRF', 'ISS', 'IMR'} as array
    Local oStatement                                 as object
        
    If ::cCarteiraBordero == "P"
        oStatement := self:oStFk2
        if oStatement == NIL
            cQuery := "SELECT FK2.FK2_VALOR VALOR, FK2.FK2_MOEDA MOEDA, FK2.FK2_TXMOED TXMOEDA, "
            cQuery += "FK2.FK2_DATA DATABX "
            cQuery += "FROM " + RetSqlName("FK2") + " FK2 "
            cQuery += "WHERE FK2.FK2_IDDOC = ? "
            cQuery += "AND FK2.FK2_DATA < ? "
            cQuery += "AND FK2.FK2_MOTBX NOT IN (?) "
            cQuery += "AND FK2.D_E_L_E_T_ = ? "
            cQuery += "AND FK2.FK2_SEQ NOT IN (SELECT FK2EST.FK2_SEQ 
            cQuery += "FROM " + RetSqlName("FK2") + " FK2EST "
            cQuery += "WHERE FK2EST.FK2_IDDOC = FK2.FK2_IDDOC "
            cQuery += "AND FK2EST.FK2_DATA < ? "
            cQuery += "AND FK2EST.D_E_L_E_T_ = ? "
            cQuery += "AND FK2EST.FK2_TPDOC = ? ) "
            
            cQuery := ChangeQuery(cQuery)
            oStatement := FWExecStatement():New(cQuery)
            self:oStFk2 := oStatement
        endIf
    Else            
        oStatement := self:oStFk1
        if oStatement == NIL
            cQuery := "SELECT FK1.FK1_VALOR VALOR, FK1.FK1_MOEDA MOEDA, FK1.FK1_TXMOED TXMOEDA, "
            cQuery += "FK1.FK1_DATA DATABX "
            cQuery += "FROM " + RetSqlName("FK1") + " FK1 "
            cQuery += "WHERE FK1.FK1_IDDOC = ? "
            cQuery += "AND FK1.FK1_DATA < ? "
            cQuery += "AND FK1.FK1_MOTBX NOT IN (?) "
            cQuery += "AND FK1.D_E_L_E_T_ = ? "
            cQuery += "AND FK1.FK1_SEQ NOT IN (SELECT FK1EST.FK1_SEQ 
            cQuery += "FROM " +  RetSqlName("FK1") + " FK1EST "
            cQuery += "WHERE FK1EST.FK1_IDDOC = FK1.FK1_IDDOC "
            cQuery += "AND FK1EST.FK1_DATA < ? "
            cQuery += "AND FK1EST.D_E_L_E_T_ = ? "
            cQuery += "AND FK1EST.FK1_TPDOC = ? ) "
        
            cQuery := ChangeQuery(cQuery)
            oStatement := FWExecStatement():New(cQuery)
            self:oStFk1 := oStatement
        endIf
    EndIf
    
    oStatement:SetString(nParamOrder++, cIdDocFK)
    oStatement:SetDate(nParamOrder++, dDataBord)
    oStatement:SetIn(nParamOrder++, aMotBxNotIn)
    oStatement:SetString(nParamOrder++, ' ')
    oStatement:SetDate(nParamOrder++, dDataBord)
    oStatement:SetString(nParamOrder++, ' ')
    oStatement:SetString(nParamOrder++, 'ES')

    cTblTmp := oStatement:OpenAlias()
    
    While !(cTblTmp)->(Eof())       
        
        nValue += xMoeda((cTblTmp)->VALOR, val((cTblTmp)->MOEDA), ::nMoeda, STOD((cTblTmp)->DATABX), ::nDecimaisMoeda, (cTblTmp)->TXMOEDA)
        
        (cTblTmp)->(DbSkip())
    EndDo
    
    (cTblTmp)->(DbCloseArea())
Return nValue


//-------------------------------------------------------------------
/*{Protheus.doc} self:getWherePay()
    @author guilherme.sordi
    @since 09/11/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method getWherePay() as character class IssueBorderauxSmartViewBusinessObject
    local cWhere := "" as character

    cWhere += " AND SEA.EA_NUMBOR BETWEEN ? AND ? "

    If !::lListarOutrasMoedas    
        cWhere += " AND SE2.E2_MOEDA = ? "
    EndIf

    if !self:lShowSettledBill
        cWhere += " AND SE2.E2_SALDO <> ? "
    endIf

return cWhere


//-------------------------------------------------------------------
/*{Protheus.doc} self:getWhereRec()
    @author guilherme.sordi
    @since 09/11/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method getWhereRec() as character class IssueBorderauxSmartViewBusinessObject
    local cWhere := "" as character

    cWhere += " AND SEA.EA_NUMBOR BETWEEN ? AND ? "

    If !::lListarOutrasMoedas    
        cWhere += " AND SE1.E1_MOEDA = ? "
    EndIf

    if !self:lShowSettledBill
        cWhere += " AND SE1.E1_SALDO <> ? "
    endIf

return cWhere


//-------------------------------------------------------------------
/*{Protheus.doc} loadHasGroupItem7()
    @author guilherme.sordi
    @since 09/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadHasGroupItem7() class IssueBorderauxSmartViewBusinessObject
    local oGroup := FWSX1Util():New() as object
    local aGroup := {} as array
    local aGroupItems := {} as array

    oGroup:addGroup("FINT005")
    oGroup:searchGroup()
    aGroup := oGroup:getGroup("FINT005")
    if len(aGroup) >= 2
        aGroupItems := aGroup[2]
    endIf

    self:lHasGroupItem7 := len(aGroupItems) >= 7

return

//-------------------------------------------------------------------
/*/{Protheus.doc} getSelectComplementPay
    @author guilherme.sordi@totvs.com.br
    @since 13/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getSelectComplementPay() as character class IssueBorderauxSmartViewBusinessObject
return ""

//-------------------------------------------------------------------
/*/{Protheus.doc} getSelectComplementRec
    @author guilherme.sordi@totvs.com.br
    @since 13/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getSelectComplementRec() as character class IssueBorderauxSmartViewBusinessObject
return ""


//-------------------------------------------------------------------
/*/{Protheus.doc} getLocalizationFieldsPay
    @author guilhermed.santos@totvs.com.br
    @since 13/12/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method getLocalizationFieldsPay() as character class IssueBorderauxSmartViewBusinessObject
return ""

//-------------------------------------------------------------------
/*/{Protheus.doc} getCalculatedTaxes
    @author guilhermed.santos@totvs.com.br
    @since 13/12/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method getCalculatedTaxes() as numeric class IssueBorderauxSmartViewBusinessObject
return 0
