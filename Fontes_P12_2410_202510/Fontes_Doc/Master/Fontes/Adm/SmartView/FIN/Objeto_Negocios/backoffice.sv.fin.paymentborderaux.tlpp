#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.paymentborderaux.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SA2,SEA,SE2", customTables="SA2,SEA,SE2", name="Emissão de Borderôs de Pagamentos", country="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} PaymentBorderauxSmartViewBusinessObject
Classe para criação do Objeto de Negócio
@author Fábio Henrique Andrade
@since 29/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
class PaymentBorderauxSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object
    protected method mySetSchema()
    protected method handleData()

	protected data oStCheck as object
    protected data nMoeda as numeric
    protected data dConversaoMoeda as date
    protected data cBorderoInicio as character
    protected data cBorderoFim as character
	protected data lListarOutrasMoedas as logical
	protected data cTipoDataConversao as character
	protected data cTipoComposicaoDeSaldo as character
	protected data cTipoBancoDoFornecedor as character
	protected data cCodBancoAtual as character
	protected data cNomeBancoAtual as character
	protected data cDVContaAtual as character

	protected data aAreaSE2 as array
	protected data aAreas as array

	protected data cCurrentGroupName as character
	protected data cCurrentCorporateCompany as character
	protected data jCheckData as json

	protected method initializeBusinessObject()
    protected method loadStatement()
    protected method loadParameters()
    protected method getValorTitulo() as numeric
	protected method getDadosBancoFornecedor() as character
	protected method getBankName() as character
	protected method getPayee() as character
	protected method loadCheckData()
	protected method hasCheck() as logical 
	protected method getCheckNumber() as character
	protected method atualizaDadosBanco()
	protected method preWhileMyAliasToData()
	protected method postWhileMyAliasToData()
	protected method loadDefaultParameters()

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Fábio Henrique Andrade
@since 29/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method new() as object class PaymentBorderauxSmartViewBusinessObject    
    _Super:new()

	self:initializeBusinessObject()
	self:setCompleteData(.T.)
    self:setMainTable("SEA")
    self:setBranchFilter(.T., .T.)

    self:cCurrentGroupName := AllTrim(FWEmpName(cEmpAnt))
	self:cCurrentCorporateCompany	:= AllTrim(FWFilRazSocial(cEmpAnt))

	self:aAreaSE2 := {}
	self:aAreas := {}
	self:jCheckData := JsonObject():new()

return self

//-------------------------------------------------------------------
/*{Protheus.doc} initilizeBusinessObject
	@author guilherme.sordi
	@since 29/06/2023
	@version 12.1.2210
*/
//------------------------------------------------------------------- 
method initializeBusinessObject() class PaymentBorderauxSmartViewBusinessObject
    self:setDisplayName(STR0009) // "Emissão de Borderôs de Pagamentos"
    self:setDescription(STR0010) // "Emissão relatorio de Borderôs de Pagamentos para envio ao banco"
	self:setPergunte("FINR710") 
return

//-------------------------------------------------------------------
/*{Protheus.doc} mySetSchema
Retorna a estrutura dos campos e conjunto de parametros
@return object: self:oSchema
@author Fábio Henrique Andrade
@since 29/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method mySetSchema() class PaymentBorderauxSmartViewBusinessObject

    Local aFieldsSEA := {} as array
	local aFieldsSA2 := {} as array
	local aFieldsSE2 := {} as array

	aFieldsSEA := { "EA_FILIAL", "EA_FILORIG", "EA_NUMBOR", "EA_CART"  , "EA_PREFIXO", "EA_NUM", "EA_PARCELA",;
                    "EA_TIPO", "EA_FORNECE", "EA_LOJA", "EA_MODELO", "EA_PORTADO", "EA_AGEDEP", "EA_NUMCON",; 
                    "EA_DATABOR" }

	aFieldsSA2 := { "A2_NOME", "A2_CGC", "A2_BANCO", "A2_AGENCIA", "A2_DVAGE", "A2_NUMCON", "A2_DVCTA" }

	aFieldsSE2 := { "E2_VENCREA"} 
	
    self:oSchema:aliasToSchema("SEA", aFieldsSEA )
	self:oSchema:aliasToSchema("SA2", aFieldsSA2)
	self:oSchema:aliasToSchema("SE2", aFieldsSE2)
	
	self:finAddProperty("NOMEBCO", STR0001, "string","A6_NREDUZ") 		// "Nome Reduzido"
	self:finAddProperty("VALORPAGAR", STR0002, "number") 		// "Valor a Pagar"
	self:finAddProperty("BENEFICIARIO", STR0003, "string","EF_BENEF") 		// "Beneficiário"
	self:finAddProperty("CHEQUE", STR0004, "string","EF_NUM") 		// "Cheque"
	self:finAddProperty("GRUPO"     , STR0005, "string") 		// "Grupo Empresa"
	self:finAddProperty("RZSOCIAL"  , STR0006, "string") 		// "Razão Empresa"
	self:finAddProperty("DTDEBITO"  , STR0007, "date"  ) 		// "Data Debito"
	self:finAddProperty("DVCONSEA"  , STR0017, "string") 		// "Dig Con SEA"
	self:finAddProperty("MODELO"  , allTrim(FwX3Titulo('EA_MODELO')), "string") 		// "Modelo"
	self:finAddProperty("TITULOSRESPECTIVOS"  , STR0024, "string") 		// "DOS TÍTULOS RESPECTIVOS, DEBITANDO EM NOSSA C/CORRENTE NUM "
	self:finAddProperty("NODIA"  , STR0025, "string") 		// "NO DIA "
	self:finAddProperty("PELOVALOR"  , STR0026, "string") 		// "PELO VALOR ACIMA TOTALIZADO."
	
	self:SetPropDisplayName("A2_BANCO"  , STR0011) 		// "Banco Forn"
	self:SetPropDisplayName("A2_AGENCIA", STR0012) 		// "Agencia Forn"
	self:SetPropDisplayName("A2_DVAGE"  , STR0013) 		// "Dig Ag Forn"
	self:SetPropDisplayName("A2_NUMCON" , STR0014) 		// "Conta Forn"
	self:SetPropDisplayName("A2_DVCTA"  , STR0015) 		// "Dig Conta Forn"

	aEval(aFieldsSA2, { | cField | self:setCanFilter(cField, .F.)})

    FwFreeArray(aFieldsSEA)
    FwFreeArray(aFieldsSA2)
	FwFreeArray(aFieldsSE2)

return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
Retorna a query a ser executada na função getData 

@author Fábio Henrique Andrade Silva
@since 29/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadStatement() class PaymentBorderauxSmartViewBusinessObject
    local cQuery  := "" as character
	local nParameterOrder := 1 as numeric
	
	self:addFieldsToStruct("EA_DATABOR")
            
	cQuery := " SELECT " + self:getAllFieldsToSQL() 
	cQuery += " , A6_BCOOFI, A6_DVCTA " 
	cQuery += " , SE2.R_E_C_N_O_ AS RECNOSE2 " 
	cQuery += " FROM " + RetSqlName("SEA") + " SEA " 

	cQuery += " INNER JOIN " + RetSqlName("SE2") + " SE2 " 
    cQuery += " ON SE2.E2_FILORIG = SEA.EA_FILORIG " 
    cQuery += " AND SE2.E2_PREFIXO = SEA.EA_PREFIXO "
    cQuery += " AND SE2.E2_NUM     = SEA.EA_NUM "
    cQuery += " AND SE2.E2_PARCELA = SEA.EA_PARCELA "
    cQuery += " AND SE2.E2_TIPO    = SEA.EA_TIPO "
	cQuery += " AND SE2.E2_FORNECE = SEA.EA_FORNECE "
	cQuery += " AND SE2.E2_LOJA    = SEA.EA_LOJA "
    cQuery += " AND SE2.E2_NUMBOR  = SEA.EA_NUMBOR "
    cQuery += " AND SE2.D_E_L_E_T_ = ? "
	
	cQuery += " INNER JOIN " + RetSqlName("SA2") + " SA2 " 
	cQuery += " ON " + FWJoinFilial("SEA","SA2")
	cQuery += " AND SA2.A2_COD    = SEA.EA_FORNECE "
	cQuery += " AND SA2.A2_LOJA   = SEA.EA_LOJA "
	cQuery += " AND SA2.D_E_L_E_T_= ? "
	
	cQuery += " INNER JOIN " + RetSqlName("SA6") + " SA6 " 
	cQuery += " ON " + FWJoinFilial("SEA","SA6")
	cQuery += " AND SA6.A6_COD = SEA.EA_PORTADO "
	cQuery += " AND SA6.A6_AGENCIA = SEA.EA_AGEDEP "
	cQuery += " AND SA6.A6_NUMCON = SEA.EA_NUMCON "
	cQuery += " AND SA6.D_E_L_E_T_= ? "
	
	cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
	
	cQuery += " AND SEA.EA_NUMBOR >= ? "
	cQuery += " AND SEA.EA_NUMBOR <= ? "
	
	cQuery += " AND SEA.EA_CART = ? "
	cQuery += " AND SEA.D_E_L_E_T_= ? "

	If !::lListarOutrasMoedas    
		cQuery += " AND SE2.E2_MOEDA = ? "
	EndIf

	cQuery += " ? "

	cQuery += " ORDER BY ? "

    cQuery := ChangeQuery(cQuery)

    ::oStatement := FWExecStatement():New(cQuery)
    

    ::oStatement:SetString(nParameterOrder++, " ")

    ::oStatement:setString(nParameterOrder++, " ")

    ::oStatement:setString(nParameterOrder++, " ")
	
	self:setStatementBranchFilter(@nParameterOrder)
    ::oStatement:SetString(nParameterOrder++, ::cBorderoInicio)
    ::oStatement:SetString(nParameterOrder++, ::cBorderoFim)

    ::oStatement:setString(nParameterOrder++, "P")
    ::oStatement:setString(nParameterOrder++, " ")

	If !::lListarOutrasMoedas
        ::oStatement:setNumeric(nParameterOrder++, self:nMoeda)
    EndIf  

    ::oStatement:SetUnsafe(nParameterOrder++, self:getFilterToSQL())
    ::oStatement:SetUnsafe(nParameterOrder++, SqlOrder(SEA->(IndexKey( 1 ))) )	//EA_FILIAL+EA_NUMBOR+EA_PREFIXO+EA_NUM+EA_PARCELA+EA_TIPO+EA_FORNECE+EA_LOJA)
       
return 

//-------------------------------------------------------------------
/*{Protheus.doc} preWhileMyAliasToData
	@author guilherme.sordi
	@since 12/01/2024
	@version 12.1.2210
*/
//------------------------------------------------------------------- 
method preWhileMyAliasToData() class PaymentBorderauxSmartViewBusinessObject
	self:aAreaSE2 := SE2->( GetArea() )
    self:aAreas := getArea( )
return

//-------------------------------------------------------------------
/*{Protheus.doc} postWhileMyAliasToData
	@author guilherme.sordi
	@since 12/01/2024
	@version 12.1.2210
*/
//------------------------------------------------------------------- 
method postWhileMyAliasToData() class PaymentBorderauxSmartViewBusinessObject	
    restArea(self:aAreaSE2)
    restArea(self:aAreas)
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
Atribui ao objeto oData o conteúdo do Alias e campos calculados.
@author Fábio Henrique Andrade
@since 19/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData() class PaymentBorderauxSmartViewBusinessObject

	self:atualizaDadosBanco()
	self:loadCheckData()

	self:jData := {;
		"DVCONSEA": 			(self:cAlias)->A6_DVCTA,;
		"A2_BANCO": 			self:getDadosBancoFornecedor("A2_BANCO"),;
		"A2_AGENCIA": 			self:getDadosBancoFornecedor("A2_AGENCIA"),;
		"A2_DVAGE": 			self:getDadosBancoFornecedor("A2_DVAGE"),;
		"A2_NUMCON": 			self:getDadosBancoFornecedor("A2_NUMCON"),;
		"A2_DVCTA": 			self:getDadosBancoFornecedor("A2_DVCTA"),;
		"NOMEBCO": 				self:getBankName(),;
		"A2_CGC": 				self:transformCGC((::cAlias)->A2_CGC,"A2_CGC"),;
		"VALORPAGAR": 			self:getValorTitulo(),;
		"BENEFICIARIO": 		self:getPayee(),;
		"CHEQUE": 				self:getCheckNumber(),;
		"GRUPO": 				self:cCurrentGroupName,;
		"RZSOCIAL": 			self:cCurrentCorporateCompany,;
		"DTDEBITO": 			self:varToTimeStamp(MV_PAR03),;
		"MODELO":				tabela("58", (self:cAlias)->EA_MODELO),;
		"TITULOSRESPECTIVOS":	STR0024,;
		"NODIA":				STR0025,;
		"PELOVALOR":			STR0026;
	}
 
return

//-------------------------------------------------------------------
/*{Protheus.doc} getBankName
	@description Retorna o nome oficial do banco. Não está na query para evitar LEFT JOIN.
	@author guilherme.sordi@totvs.com.br
	@since 28/11/2024
	@version 12.1.2410
*/
//-------------------------------------------------------------------
method getBankName() as character class PaymentBorderauxSmartViewBusinessObject
	local cBankName := "" as character
	if !empty((self:cAlias)->A6_BCOOFI)
		cBankName := tabela("0N", (self:cAlias)->A6_BCOOFI)
	endIf
return cBankName

//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters

Se não receber o objeto de filtro limpa as propriedas de filtro da classe.
Se receber o objeto de filtro, carrega os parâmetros informados pelo usuário para as variáveis públicas MV_PAR
e atualiza as propriedades de filtro da classe.

As propriedades de filtro da classe ajudam a compreesão do código (boas práticas de código limpo).

@param oFilter, objeto, contém o filtro do TReports
@author Fábio Henrique Andrade
@since 29/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadParameters() class PaymentBorderauxSmartViewBusinessObject   

	Local aTipoDatas 	:= {"BORDERO","DATABASE"} as array
	Local aTipoSaldos 	:= {"SALDO_ATUAL","VALOR_ORIGINAL"} as array 
	Local aBancoFornece := {"CADASTRO_FORNECEDOR","TITULO"} as array 

    if self:oCurrentFilter <> NIL

		if !Empty(MV_PAR04)
			::nMoeda := MV_PAR04
		endIf

		::lListarOutrasMoedas := MV_PAR05 == 1

        ::cBorderoInicio   := MV_PAR01
        ::cBorderoFim      := MV_PAR02

		::cTipoDataConversao := aTipoDatas[1]
		if !empty(MV_PAR06)
			::cTipoDataConversao 	 := aTipoDatas[MV_PAR06]
		endIf
		
		::cTipoComposicaoDeSaldo := aTipoSaldos[1]
		if !empty(MV_PAR07)
			::cTipoComposicaoDeSaldo 	 := aTipoSaldos[MV_PAR07]
		endIf

		::cTipoBancoDoFornecedor := aBancoFornece[1]
		if !empty(MV_PAR08)
			::cTipoBancoDoFornecedor 	 := aBancoFornece[MV_PAR08]
		endIf
    endIf

	FwFreeArray(aTipoDatas)
	FwFreeArray(aTipoSaldos)
	FwFreeArray(aBancoFornece)

return

//----------------------------------------------------------
/*/{Protheus.doc}getDadosBancoFornecedor
Localiza dados bancarios do fornecedor

@param ::cAlias, char, alias da Tabela Temporaria SEA
@return lCheque, logical, Verifica se encontrou cheque para o titulo do bordero 

@author Francisco Oliveira
@since 20/04/2023
@version 12
@Type Function
/*/
//-----------------------------------------------------------
				
method getDadosBancoFornecedor(cCampo as character) as character class PaymentBorderauxSmartViewBusinessObject

	Local cRet		:= ""  as character
	Local lBcoTit 	:= .F. as logical

	DbSelectArea("SE2")
	SE2->(DBGoTo((::cAlias)->RECNOSE2))

	//Verifica se deve priorizar os dados bancarios vinculados ao titulo a pagar
	If  ::cTipoBancoDoFornecedor == "TITULO" .And. !Empty(SE2->E2_FORBCO)
		lBcoTit := .T.
	Else
		lBcoTit := .F.
	Endif

	If cCampo $ "A2_BANCO"
		If lBcoTit
			cRet	:= SE2->E2_FORBCO
		Else
			cRet	:= (::cAlias)->A2_BANCO
		Endif
	Elseif cCampo $ "A2_AGENCIA"
		If lBcoTit
			cRet	:= SE2->E2_FORAGE
		Else
			cRet	:= (::cAlias)->A2_AGENCIA
		Endif
	Elseif cCampo $ "A2_DVAGE"
		If lBcoTit
			cRet	:= SE2->E2_FAGEDV
		Else
			cRet	:= (::cAlias)->A2_DVAGE
		Endif
	Elseif cCampo $ "A2_NUMCON"
		If lBcoTit
			cRet	:= SE2->E2_FORCTA
		Else
			cRet	:= (::cAlias)->A2_NUMCON
		Endif
	Elseif cCampo $ "A2_DVCTA"
		If lBcoTit
			cRet	:= SE2->E2_FCTADV
		Else
			cRet	:= (::cAlias)->A2_DVCTA
		Endif
	Endif

Return alltrim(cRet)

//----------------------------------------------------------
/*/{Protheus.doc}getValorTitulo
Faz os calculos dos valores a pagar dos titulos.		

@param ::cAlias, char, alias da Tabela Temporaria SEA
@return nVlrPagar, Numeric, retorna o valor do calculo 

@author Francisco Oliveira
@since 24/04/2023
@version 12
@Type Function
/*/
//-----------------------------------------------------------

method getValorTitulo() as numeric class PaymentBorderauxSmartViewBusinessObject
	Local nAbat  		:= 0 as numeric
	Local nVlrPagar		:= 0 as numeric
	Local nVA 			:= 0 as numeric
	Local aArea 		:= GetArea()        as array
	Local aAreaSA2 		:= SA2->(GetArea()) as array

	Private nJuros 		:= 0 as numeric
	Private dBaixa 		:= CriaVar("E2_BAIXA") as date

	SE2->(DBGoTo((::cAlias)->RECNOSE2))

	nAbat := SomaAbat(SE2->E2_PREFIXO,SE2->E2_NUM,SE2->E2_PARCELA,"P",SE2->E2_MOEDA,dDataBase,SE2->E2_FORNECE,SE2->E2_LOJA, SE2->E2_FILIAL)

	//Efetua calculo dos juros do titulo posicionado
	fa080Juros(1)

	// Valoes Acessórios
	nVA	:= FValAcess( SE2->E2_PREFIXO, SE2->E2_NUM, SE2->E2_PARCELA, SE2->E2_TIPO, SE2->E2_FORNECE, SE2->E2_LOJA, SE2->E2_NATUREZ, !Empty(dBaixa), "" , "P", , , SE2->E2_MOEDA, SE2->E2_MOEDA , SE2->E2_TXMOEDA )
	
	If ::cTipoDataConversao == "BORDERO"
		::dConversaoMoeda := (::cAlias)->EA_DATABOR
	Else 
		::dConversaoMoeda := dDatabase
	EndIf

	If ::cTipoComposicaoDeSaldo == "SALDO_ATUAL"
		nVlrPagar := SE2->E2_SALDO - SE2->E2_SDDECRE + SE2->E2_SDACRES
	Else
		nVlrPagar := SE2->E2_VALOR - SE2->E2_DECRESC + SE2->E2_ACRESC
	EndIf
	nVlrPagar := nVlrPagar - nAbat + nJuros + nVA
	nVlrPagar := xMoeda( nVlrPagar, SE2->E2_MOEDA, ::nMoeda, ::dConversaoMoeda, self:nDecimals, SE2->E2_TXMOEDA)

	RestArea(aAreaSA2)
	RestArea(aArea)

Return nVlrPagar

//----------------------------------------------------------
/*/{Protheus.doc}getPayee
	@description Retorna o campo Beneficiario conforme o modelo do borderô
	@author Francisco Oliveira / Guilherme de Sordi
	@since 26/11/2024
	@version 12.1.2410
	@type method
/*/
//-----------------------------------------------------------
method getPayee() as character class PaymentBorderauxSmartViewBusinessObject
	local cPayee as character

	if (self:cAlias)->EA_MODELO $ "CH/02" .and. self:hasCheck()
		cPayee := self:jCheckData['EF_BENEF']
	else
		cPayee := (self:cAlias)->A2_NOME
	endIf
Return alltrim(cPayee)

//----------------------------------------------------------
/*/{Protheus.doc}getCheckNumber
	@description Retorna o número do cheque, se existir
	@author guilherme.sordi@totvs.com.br
	@since 26/11/2024
	@version 12.1.210
	@type method
/*/
//-----------------------------------------------------------	
method getCheckNumber() as character class PaymentBorderauxSmartViewBusinessObject
	local cCheckNumber := "" as character
	if self:hasCheck()
		cCheckNumber := self:jCheckData['EF_NUM']
	endIf
return cCheckNumber

//----------------------------------------------------------
/*/{Protheus.doc}hasCheck
	@description Retorna se existe cheque
	@author Francisco Oliveira / Guilherme de Sordi
	@since 26/11/2024
	@version 12.1.210
	@type method
/*/
//-----------------------------------------------------------				
method hasCheck() as logical class PaymentBorderauxSmartViewBusinessObject
return !empty(self:jCheckData:getNames())

//----------------------------------------------------------
/*/{Protheus.doc}loadCheckData
	@description Obtém dados de cheque relacionado a borderô. É feito em uma consulta
	à parte para evitar LEFT JOIN de muitos registros entre as tabelas SEA e SEF da
	query principal do objeto de negócio
	@author guilherme.sordi@totvs.com.br
	@since 26/11/2024
	@version 12.1.210
	@type method
/*/
//-----------------------------------------------------------	
method loadCheckData() class PaymentBorderauxSmartViewBusinessObject
	local cQuery as character
	local cAlias as character
	local aData := {}
	local nParamOrder := 1 as numeric
	local aFields := {'EF_NUM', 'EF_BENEF'} as array

	freeObj(self:jCheckData)
	self:jCheckData := JsonObject():new()

	if self:oStCheck == NIL
		cQuery := " SELECT ? "
		cQuery += " FROM " + retSQLName("SEF") + " SEF "
		cQuery += " JOIN " + retSQLName("SEA") + " SEA "
		cQuery += " ON EF_FILORIG = EA_FILORIG"
		cQuery += " AND EF_CART = ? "
		cQuery += " AND EF_PREFIXO = EA_PREFIXO "
		cQuery += " AND EF_TITULO = EA_NUM "
		cQuery += " AND EF_PARCELA = EA_PARCELA "
		cQuery += " AND EF_TIPO = EA_TIPO "
		cQuery += " AND EF_FORNECE = EA_FORNECE "
		cQuery += " AND EF_LOJA = EA_LOJA "
		cQuery += " AND SEA.R_E_C_N_O_ = ? "
		cQuery += " WHERE SEF.D_E_L_E_T_ = ? "
		self:oStCheck := FWExecStatement():new(changeQuery(cQuery))
	endIf

	self:oStCheck:setUnsafe(nParamOrder++, ArrTokStr(aFields,","))
	self:oStCheck:setString(nParamOrder++, " ")
	self:oStCheck:setNumeric(nParamOrder++, (self:cAlias)->RECNOSE2)
	self:oStCheck:setString(nParamOrder++, " ")

	cAlias := self:oStCheck:openAlias()
	aData := self:aliasToJsonArray(cAlias, aFields)
	if !empty(aData)
		self:jCheckData := aData[1]
	endIf
	(cAlias)->(dbCloseArea())	

return


//-------------------------------------------------------------------
/*{Protheus.doc} getNomeBanco

Retorna o nome do banco, consultando a tabela 0N da SX5 
de acordo com o campo A6_BCOOFI.

@param Nil
@return cNomeBanco, character, retorna o nome do banco no cadastro da SX5, tabela 0N
@author Fábio Henrique Andrade
@since 07/07/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method atualizaDadosBanco() class PaymentBorderauxSmartViewBusinessObject

	if ::cCodBancoAtual <> (::cAlias)->EA_PORTADO
		::cCodBancoAtual  := Posicione( "SA6", 1, FwxFilial("SA6", (::cAlias)->EA_FILORIG) + (::cAlias)->(EA_PORTADO+EA_AGEDEP+EA_NUMCON), "A6_BCOOFI" )
		::cNomeBancoAtual := alltrim(Posicione( "SX5", 1, FwxFilial("SX5") + "0N" + ::cCodBancoAtual, "X5_DESCRI" ))
		::cDVContaAtual   := alltrim(Posicione( "SA6", 1, FwxFilial("SA6", (::cAlias)->EA_FILORIG) + (::cAlias)->(EA_PORTADO+EA_AGEDEP+EA_NUMCON), "A6_DVCTA" ))
	endIf

return 

//-------------------------------------------------------------------
/*{Protheus.doc} loadDefaultParameters
	@author guilherme.sordi
	@since 12/01/2024
	@version 12.1.2210
*/
//------------------------------------------------------------------- 
method loadDefaultParameters() class PaymentBorderauxSmartViewBusinessObject
    self:nMoeda                 := 1
    self:dConversaoMoeda        := dDatabase
    self:cBorderoInicio         := ""
    self:cBorderoFim            := ""
	self:cTipoDataConversao     := ""
	self:cTipoComposicaoDeSaldo := ""
	self:cTipoBancoDoFornecedor := ""
	self:cCodBancoAtual         := ""
	self:cNomeBancoAtual        := ""
	self:cDVContaAtual          := ""
return
