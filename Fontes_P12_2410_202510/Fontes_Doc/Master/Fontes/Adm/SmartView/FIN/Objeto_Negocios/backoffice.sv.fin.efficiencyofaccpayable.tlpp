#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.efficiencyofaccpayable.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE2,FK2,FKA,FK5,SA2", name="Eficiência do Contas a Pagar", country="ALL",;
	customTables="SE2,FK2")


//-------------------------------------------------------------------
/*/{Protheus.doc} EfficiencyOfAccountsPayableSmartViewBusinessObject

@author Fábio Henrique Andrade
@since 22/11/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class EfficiencyOfAccountsPayableSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object

    protected data oStatementFK6         as object
    protected data cPrefixFrom           as character
    protected data cPrefixTo             as character
    protected data cClassFrom            as character
    protected data cClassTo              as character
    protected data nCurrency             as numeric
    protected data lListOtherCurrencies  as logical
    protected data nDaysToConsider       as numeric
    protected data dIssueDateFrom        as date
    protected data dIssueDateTo          as date

    protected data cReasonsToWriteOff as character
    protected data nBankCurrency as numeric
    protected data nIssueCurrency as numeric
    protected data dConversionDate as date
    protected data nCurrencyRate as numeric

    protected data nSumTotalPay as numeric
    protected data nSumValuesWithoutPayment as numeric
    protected data nSumBalanceOnReferenceDate as numeric
    protected data dActualDate as date

    protected data dAvailabilityDate as date
    protected data nPayWithoutDelayDiscount as numeric
    protected data nPayWithoutDelayAddition as numeric
    protected data nPayWithDelayDiscount as numeric
    protected data nPayWithDelayAddition as numeric
    protected data nTotalPayWithoutDelay as numeric
    protected data nTotalPayWithDelay as numeric
    protected data nOpenValueOnTheDay as numeric
    protected data nDebitedOnTheDay as numeric
    protected data nWriteOffOnTheDay as numeric
    protected data nWriteOffWithoutDebit as numeric

    protected method initializeBusinessObject()
    protected method mySetSchema()
    protected method loadStatement()
    protected method loadStatementFK6Movements()
    protected method getReasonsToWriteOff() as character
    protected method myAliasToData()
    protected method processAndPrepareData()
    protected method handleData()
    protected method setDefaultParameters()
    protected method loadParameters()
    protected method convertMovementCurrency() as numeric
    protected method loadAvailabilityDate()
    protected method loadFK6Values()
    protected method loadTotalValues()
    protected method resetValues()
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Fábio Henrique Andrade
@since 22/11/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class EfficiencyOfAccountsPayableSmartViewBusinessObject
    _Super:new()

    self:nPayWithoutDelayDiscount := 0
    self:nPayWithoutDelayAddition := 0
    self:nPayWithDelayDiscount := 0
    self:nPayWithDelayAddition := 0
    self:nTotalPayWithoutDelay := 0
    self:nTotalPayWithDelay := 0
    self:nDebitedOnTheDay  := 0
    self:nWriteOffOnTheDay := 0
    self:nWriteOffWithoutDebit := 0
    self:nOpenValueOnTheDay := 0

    self:nIssueCurrency  := 1
    self:dConversionDate := CtoD("//")
    self:nCurrencyRate   := 0

    self:nSumTotalPay := 0
    self:nSumValuesWithoutPayment := 0
    self:nSumBalanceOnReferenceDate := 0
    self:dActualDate := dDatabase

    self:nBankCurrency := 1
    self:cReasonsToWriteOff := self:getReasonsToWriteOff()

    self:setCompleteData(.T.)
    self:setAllTrim(.T.)

    self:setMainTable("SE2")
    self:setBranchFilter(.T., .T.)

    self:initializeBusinessObject()

return self

//-------------------------------------------------------------------
    /*{Protheus.doc} mySetSchema
    @author Fábio Henrique Andrade
    @since 23/11/2023
    @version 12.1.2310
    */
//-------------------------------------------------------------------
method initializeBusinessObject() class EfficiencyOfAccountsPayableSmartViewBusinessObject
    self:setDisplayName(STR0002) //"Eficiência do Contas a Pagar"
    self:setDescription(STR0002) //"Eficiência do Contas a Pagar"
    self:setPergunte("FINT027")
return


//-------------------------------------------------------------------
    /*{Protheus.doc} mySetSchema
    Determina a estrutura dos campos
    @author Fábio Henrique Andrade
    @since 22/11/2023
    @version 12.1.2310
    */
//-------------------------------------------------------------------
method mySetSchema() class EfficiencyOfAccountsPayableSmartViewBusinessObject
    local aFieldsSE2 := {} as array
    local aFieldsFK2 := {} as array
    local aFieldsFKA := {} as array
    local aFieldsFK5 := {} as array
    local aFieldsSA2 := {} as array

    aFieldsSE2 := { "E2_FILIAL","E2_PREFIXO","E2_NUM","E2_PARCELA","E2_TIPO",;
                    "E2_NATUREZ","E2_FORNECE","E2_LOJA","E2_VENCREA","E2_EMISSAO",;
                    "E2_VALOR","E2_MOEDA","E2_TXMOEDA","E2_FILORIG" }
    aFieldsFK2 := { "FK2_FILIAL","FK2_IDFK2","FK2_DATA","FK2_VALOR","FK2_MOEDA",;
                    "FK2_VENCTO","FK2_RECPAG","FK2_TPDOC","FK2_VLMOE2","FK2_MOTBX",;
                    "FK2_FILORI","FK2_SEQ","FK2_IDDOC","FK2_DOC","FK2_TXMOED" }
    aFieldsSA2 := { "A2_NOME" }
    aFieldsFKA := { "FKA_IDPROC","FKA_IDORIG" }
    aFieldsFK5 := { "FK5_DTDISP" }

    self:oSchema:aliasToSchema( "SE2", aFieldsSE2 )
    self:oSchema:aliasToSchema( "FK2", aFieldsFK2 )
    self:oSchema:aliasToSchema( "SA2", aFieldsSA2 )
    self:oSchema:aliasToSchema( "FKA", aFieldsFKA )
    self:oSchema:aliasToSchema( "FK5", aFieldsFK5 )

    self:finAddProperty("DADOS_FORNECEDOR"        , STR0003, "string", "A2_NOME") //"Dados do Fornecedor"
    self:finAddProperty("DADOS_TITULO"            , STR0004, "string", "E2_NUM") //"Dados do Título"
    self:finAddProperty("VALOR_TITULO"            , STR0005, "number") //"Vlr. do Título"
    self:finAddProperty("PAGOS_SATRASO_DESCONTO"  , STR0006, "number") //"Vlrs. Pagos s/ Atraso Desconto"
    self:finAddProperty("PAGOS_SATRASO_ACRESCIMO" , STR0007, "number") //"Vlr. Moeda Relatório"   
    self:finAddProperty("PAGOS_SATRASO_TOTAL"     , STR0008, "number") //"Vlrs. Pagos s/ Atraso Total Pago"
    self:finAddProperty("PAGOS_CATRASO_DESCONTO"  , STR0009, "number") //"Vlrs. Pagos c/ Atraso Desconto"
    self:finAddProperty("PAGOS_CATRASO_ACRESCIMO" , STR0010, "number") //"Vlrs. Pagos c/ Atraso Acréscimo"
    self:finAddProperty("PAGOS_CATRASO_TOTAL"     , STR0011, "number") //"Vlrs. Pagos c/ Atraso Total Pago"
    self:finAddProperty("NAO_PAGO_NO_DIA"         , STR0012, "number") //"Vlrs. não Pagos no dia do Vencto"
    self:finAddProperty("EFICIENCIA_DATA"         , STR0013, "number") //"% Eficiência"
    self:finAddProperty("SALDO_DATA_REFERENCIA"   , STR0014, "number") //"Saldo em aberto Dt. Referência"
    self:finAddProperty("EFICIENCIA_ATUAL"        , STR0015, "number") //"% Atual"
    self:finAddProperty("DEBITADO_NO_DIA"         , STR0016, "number") //"Vlr. Debitado Dt. Refer."
    self:finAddProperty("BAIXADO_NO_DIA"          , STR0017, "number") //"Vlr. Baixado Dt. Refer."
    self:finAddProperty("BAIXADO_SEM_DEBITO"      , STR0018, "number") //"Vlr. Baixado s/ Débito"

return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
Retorna a query a ser executada na função getData

@author Fábio Henrique Andrade Silva
@since 22/11/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method loadStatement() class EfficiencyOfAccountsPayableSmartViewBusinessObject
    local cQuery  := "" as character
    local nParamOrder := 1 as numeric
    Local cDocTypes 	  := "VL|BA|V2|PA" as character
    Local cIssueTypes     := "NF |DP |CC" as character

    cQuery := " SELECT ?, SE2.R_E_C_N_O_ RECNO "
    cQuery += " FROM " + RetSqlName("SE2") + " SE2 "

    cQuery += " INNER JOIN " + RetSqlName("FK7") + " FK7 "
    cQuery += " ON FK7.FK7_ALIAS = ? "
    cQuery += " AND FK7.FK7_FILTIT = SE2.E2_FILIAL "
    cQuery += " AND FK7.FK7_PREFIX = SE2.E2_PREFIXO "
    cQuery += " AND FK7.FK7_NUM = SE2.E2_NUM "
    cQuery += " AND FK7.FK7_PARCEL = SE2.E2_PARCELA  "
    cQuery += " AND FK7.FK7_TIPO = SE2.E2_TIPO "
    cQuery += " AND FK7.FK7_CLIFOR = SE2.E2_FORNECE "
    cQuery += " AND FK7.FK7_LOJA = SE2.E2_LOJA "
    cQuery += " AND FK7.D_E_L_E_T_ = ? "

    cQuery += " INNER JOIN " + RetSQLName("SA2") + " SA2 ON " + FWJoinFilial("SA2","SE2")
    cQuery += "     AND SA2.A2_COD = SE2.E2_FORNECE "
    cQuery += "     AND SA2.A2_LOJA = SE2.E2_LOJA "
    cQuery += "     AND SA2.D_E_L_E_T_ = ? "
    cQuery += " LEFT JOIN " + RetSqlName("FK2") + " FK2 ON "
    cQuery += "     FK2.FK2_IDDOC = FK7.FK7_IDDOC "
    cQuery += "     AND FK2.FK2_NATURE BETWEEN ? AND ? "
    cQuery += "     AND FK2.FK2_DATA >= ? "
    cQuery += "     AND FK2.FK2_TPDOC IN (?) "
    cQuery += "     AND FK2.FK2_TPDOC <> ?  "   			
    cQuery += "     AND FK2.D_E_L_E_T_ = ? "
    cQuery += "     AND NOT EXISTS ("
    cQuery += "         SELECT A.FK2_IDDOC "
    cQuery += "         FROM " + RetSqlName("FK2") + " A "
    cQuery += "         WHERE "
    cQuery += "         A.FK2_FILIAL = FK2.FK2_FILIAL AND "
    cQuery += "         A.FK2_IDDOC = FK2.FK2_IDDOC AND "
    cQuery += "         A.FK2_SEQ = FK2.FK2_SEQ AND "
    cQuery += "         A.FK2_TPDOC = ? AND "
    cQuery += "         A.FK2_RECPAG = ? AND "
    cQuery += "         A.D_E_L_E_T_ = ?) "

    cQuery += " LEFT JOIN " + RetSqlName("FKA") + " FKA "
    cQuery += " ON FK2_IDFK2 = FKA_IDORIG "
    cQuery += " AND FKA_TABORI = ? "

    cQuery += " LEFT JOIN " + RetSqlName("FKA") + " FKAFK5 ON (FKA.FKA_IDPROC = FKAFK5.FKA_IDPROC) "
    cQuery += "     AND FKAFK5.FKA_TABORI = ? "

    cQuery += " LEFT JOIN " + RetSqlName("FK5") + " FK5 "
	cQuery += "     ON FKAFK5.FKA_IDORIG = FK5.FK5_IDMOV "
	cQuery += "     AND FK5.D_E_L_E_T_ = ? "
    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += " AND SE2.E2_PREFIXO BETWEEN ? AND ? "
    cQuery += " AND SE2.E2_NATUREZ BETWEEN ? AND ? "
    cQuery += " AND SE2.E2_TIPO IN (?) " 
    cQuery += " AND SE2.E2_VENCREA BETWEEN ? AND ? "
    cQuery += " AND SE2.E2_STATUS <> ? "

    If !self:lListOtherCurrencies
        cQuery += " AND SE2.E2_MOEDA = ? "
    EndIf

    cQuery += " AND SE2.D_E_L_E_T_ = ? "

    cQuery += " ORDER BY " + SQLOrder("E2_FILIAL+DTOS(E2_VENCREA)+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA")

    cQuery := ChangeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)

    self:oStatement:SetUnsafe(nParamOrder++, self:getAllFieldsToSQL() )
    self:oStatement:SetString(nParamOrder++, 'SE2' )
    self:oStatement:SetString(nParamOrder++, ' ' )

    self:oStatement:SetString(nParamOrder++, ' ' )

    self:oStatement:SetString(nParamOrder++, self:cClassFrom)
    self:oStatement:SetString(nParamOrder++, self:cClassTo)
    self:oStatement:SetDate(nParamOrder++, self:dIssueDateFrom)
    self:oStatement:SetIn(nParamOrder++, StrToKArr(cDocTypes,"|") )
    self:oStatement:SetString(nParamOrder++, 'ES')
    self:oStatement:SetString(nParamOrder++, ' ')
    self:oStatement:SetString(nParamOrder++, 'ES')
    self:oStatement:SetString(nParamOrder++, 'R')
    self:oStatement:SetString(nParamOrder++, ' ')

    self:oStatement:SetString(nParamOrder++, 'FK2')
    self:oStatement:SetString(nParamOrder++, 'FK5')
    self:oStatement:SetString(nParamOrder++, ' ')
    self:setStatementBranchFilter(@nParamOrder)

    self:oStatement:SetString(nParamOrder++, self:cPrefixFrom)
    self:oStatement:SetString(nParamOrder++, self:cPrefixTo)
    self:oStatement:SetString(nParamOrder++, self:cClassFrom)
    self:oStatement:SetString(nParamOrder++, self:cClassTo)
    self:oStatement:SetIn(nParamOrder++, StrToKArr(cIssueTypes,"|") )
    self:oStatement:SetDate(nParamOrder++, self:dIssueDateFrom)
    self:oStatement:SetDate(nParamOrder++, self:dIssueDateTo)
    self:oStatement:SetString(nParamOrder++, 'D')

    if !self:lListOtherCurrencies
        self:oStatement:SetNumeric(nParamOrder++, self:nCurrency)
    endIf
    
    self:oStatement:SetString(nParamOrder++, ' ')

return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatementFK6Movements
Retorna a query a ser executada na função getData

@author Fábio Henrique Andrade Silva
@since 29/11/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method loadStatementFK6Movements() class EfficiencyOfAccountsPayableSmartViewBusinessObject
    local cQuery  := "" as character
    local nParamOrder := 1 as numeric

    if self:oStatementFK6 == Nil
        cQuery := " SELECT " 
        cQuery += "  FK6.FK6_TPDOC, "
        cQuery += "  FK6.FK6_VALMOV "
        cQuery += " FROM " + RetSQLName( "FK6" ) + " FK6 "
        cQuery += " WHERE "
        cQuery += " FK6.FK6_FILIAL = ? "
        cQuery += " AND FK6.FK6_IDORIG = ? "
        cQuery += " AND FK6.FK6_TABORI = ? "
        cQuery += " AND FK6.D_E_L_E_T_ = ? "

        cQuery := ChangeQuery(cQuery)

        self:oStatementFK6 := FWExecStatement():New(cQuery)
    EndIf
    
    self:oStatementFK6:SetString(nParamOrder++, fwXFilial("FK6", (self:cAlias)->E2_FILORIG))
    self:oStatementFK6:SetString(nParamOrder++, (self:cAlias)->FK2_IDFK2)
    self:oStatementFK6:SetString(nParamOrder++, "FK2")
    self:oStatementFK6:SetString(nParamOrder++, ' ')

return

//-------------------------------------------------------------------
/*/{Protheus.doc} myAliasToData
    Método myAliasToData padrão para todos os objetos de negócio. Responsável 
    por preenchar o jData e, consequentemente, o objeto oData com os dados
    que serão retornados pelo objeto de negócio no end-point getData().
    Será implementado no objeto de negócios conforme a necessidade.
    @author guilherme.sordi@totvs.com.br
    @since 24/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method myAliasToData() class EfficiencyOfAccountsPayableSmartViewBusinessObject
    local aArea := {} as array
    local aAreaSE2 := {} as array

    self:setCompleteData(.T.)

    (self:cAlias)->(DBGoTop())    

    if (self:cAlias)->(Eof()) 
        return
    endIf

    aArea := getArea()
    aAreaSE2 := SE2->(getArea())

    self:preWhileMyAliasToData()
    While !(self:cAlias)->(Eof())       
        self:jData := JSONObject():new() 
        self:processAndPrepareData()
        (self:cAlias)->(DbSkip())

        if (self:dActualDate <> (self:cAlias)->E2_VENCREA) .or. (self:cAlias)->(Eof()) 
            self:jData["EFICIENCIA_DATA"]   := (1-(self:nSumValuesWithoutPayment/self:nSumTotalPay))*100
            self:jData["EFICIENCIA_ATUAL"]  := (1-(self:nSumBalanceOnReferenceDate/self:nSumTotalPay))*100
            self:nSumValuesWithoutPayment   := 0
            self:nSumBalanceOnReferenceDate := 0
            self:nSumTotalPay := 0
        endIf

        self:oData:appendData(self:jData)
    Enddo 

    restArea(aAreaSE2)
    restArea(aArea)
    fwFreeArray(aAreaSE2)
    fWFreeArray(aArea)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} processAndPrepareData
    Faz o processamento dos dados do JData e prepara para append no objeto oData.
    Chama os métodos responsáveis pelo complemento dos dados no MI,
    no objeto extendido do financeiro, o ofuscamento de dados protegidos/sendiveis
    conforme LGPD e qualquer outro tratamento que deve ser feito nos dados,
    antes de entregar para a classe do Framework montar o JSON que será entregue
    ao Smart View.
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method processAndPrepareData() class EfficiencyOfAccountsPayableSmartViewBusinessObject
    self:handleData() //Manipula jData antes do append, criado pelo Financeiro para extensão do objeto de negócio
    self:processData() //Manipula jData antes do append, criado pelo Framework para uso do MI
    self:processCustomFields() //Manipula jData antes do append, incluindo o conteúdo dos campos personalizados

    self:addBranchNameToData()

    if self:lEnableCompleteData
        self:completeData() //Completa o jData com campos do schema que ainda não foram preenchidos no jData
    endIf

    self:jData := self:oLGPD:handleDataWithLGPD(self:jData)
    
    self:preAppendData() //Manipula jData imediatamente antes do append
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
@author Fábio Henrique Andrade Silva
@since 22/11/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method handleData() class EfficiencyOfAccountsPayableSmartViewBusinessObject

    local nTotAbat  := 0 

    self:loadAvailabilityDate()
    self:loadFK6Values()
    self:loadTotalValues()

    self:nIssueCurrency  := (self:cAlias)->E2_MOEDA
    self:dConversionDate := StoD((self:cAlias)->E2_VENCREA)
    self:nCurrencyRate   := (self:cAlias)->E2_TXMOEDA
    nTotAbat := self:convertMovementCurrency(SomaAbat((self:cAlias)->E2_PREFIXO,(self:cAlias)->E2_NUM,(self:cAlias)->E2_PARCELA,"P",1))

    self:jData["DADOS_FORNECEDOR"]          := (self:cAlias)->E2_FORNECE + "/" + (self:cAlias)->E2_LOJA + "-" + alltrim((self:cAlias)->A2_NOME)
    self:jData["DADOS_TITULO"]              := (self:cAlias)->E2_PREFIXO + "-" + (self:cAlias)->E2_NUM + "-" + (self:cAlias)->E2_PARCELA + "-" + (self:cAlias)->E2_TIPO 
    self:jData["VALOR_TITULO"]              := self:convertMovementCurrency((self:cAlias)->E2_VALOR)-nTotAbat
    self:jData["PAGOS_SATRASO_DESCONTO"]    := self:convertMovementCurrency(self:nPayWithoutDelayDiscount)
    self:jData["PAGOS_SATRASO_ACRESCIMO"]   := self:convertMovementCurrency(self:nPayWithoutDelayAddition)
    self:jData["PAGOS_SATRASO_TOTAL"]       := self:nTotalPayWithoutDelay
    self:jData["PAGOS_CATRASO_DESCONTO"]    := self:convertMovementCurrency(self:nPayWithDelayDiscount)
    self:jData["PAGOS_CATRASO_ACRESCIMO"]   := self:convertMovementCurrency(self:nPayWithDelayAddition)
    self:jData["PAGOS_CATRASO_TOTAL"]       := self:nTotalPayWithDelay
    self:jData["NAO_PAGO_NO_DIA"]           := self:jData["VALOR_TITULO"] - self:jData["PAGOS_SATRASO_TOTAL"] - self:jData["PAGOS_SATRASO_ACRESCIMO"] + self:jData["PAGOS_SATRASO_DESCONTO"]
    self:jData["EFICIENCIA_DATA"]           := 0
    self:jData["SALDO_DATA_REFERENCIA"]     := self:convertMovementCurrency(self:nOpenValueOnTheDay) - nTotAbat
    self:jData["EFICIENCIA_ATUAL"]          := 0
    self:jData["DEBITADO_NO_DIA"]           := self:nDebitedOnTheDay
    self:jData["BAIXADO_NO_DIA"]            := self:nWriteOffOnTheDay
    self:jData["BAIXADO_SEM_DEBITO"]        := self:nWriteOffWithoutDebit

    self:nSumTotalPay += self:jData["VALOR_TITULO"]
    self:nSumValuesWithoutPayment += self:jData["NAO_PAGO_NO_DIA"]
    self:nSumBalanceOnReferenceDate += self:jData["SALDO_DATA_REFERENCIA"] 
    self:dActualDate := (self:cAlias)->E2_VENCREA

    self:resetValues()

return

/*/{Protheus.doc} loadParameters
    @author Fábio Henrique Andrade
    @since 22/11/2023
    @version 12.1.2310
*/
method loadParameters() class EfficiencyOfAccountsPayableSmartViewBusinessObject   
    if self:oCurrentFilter  == NIL
        self:setDefaultParameters()
    else
    
        self:cPrefixFrom            := MV_PAR01
        self:cPrefixTo              := MV_PAR02
        self:cClassFrom             := MV_PAR03
        self:cClassTo               := MV_PAR04
        self:nCurrency              := MV_PAR05
        self:lListOtherCurrencies   := !(MV_PAR06 == 1)
        self:dIssueDateTo           := MV_PAR07
        self:nDaysToConsider        := MV_PAR08
        self:dIssueDateFrom         := self:dIssueDateTo - self:nDaysToConsider

    endIf
return

/*/{Protheus.doc} setDefaultParameters
    @author Fábio Henrique Andrade
    @since 22/11/2023
    @version 12.1.2310
*/
method setDefaultParameters() class EfficiencyOfAccountsPayableSmartViewBusinessObject
    self:cPrefixFrom            := "   " 
    self:cPrefixTo              := "ZZZ"
    self:cClassFrom             := "          "
    self:cClassTo               := "ZZZZZZZZZZ"
    self:nCurrency              := 1
    self:lListOtherCurrencies   := .T.
    self:nDaysToConsider        := 33
    self:dIssueDateFrom         := dDatabase - 33
    self:dIssueDateTo           := dDatabase
return

//----------------------------------------------------------------
/*/{Protheus.doc}convertMovementCurrency
    @author Fábio Henrique Andrade
    @since 23/11/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method convertMovementCurrency(nValue as numeric) as numeric class EfficiencyOfAccountsPayableSmartViewBusinessObject
    local nConvertedValue := 0 as numeric

    nConvertedValue := xMoeda(nValue, self:nIssueCurrency, self:nCurrency, self:dConversionDate, self:nDecimals, self:nCurrencyRate)

return nConvertedValue

//----------------------------------------------------------------
/*/{Protheus.doc}loadAvailabilityDate
    @author Fábio Henrique Andrade
    @since 24/11/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method loadAvailabilityDate() class EfficiencyOfAccountsPayableSmartViewBusinessObject
	self:dAvailabilityDate := (self:cAlias)->FK2_DATA

	if !Empty((self:cAlias)->FKA_IDORIG) .AND. !Empty((self:cAlias)->FK5_DTDISP)
		self:dAvailabilityDate := (self:cAlias)->FK5_DTDISP
	endIf
return

//----------------------------------------------------------------
/*/{Protheus.doc}loadFK6Values
    @author Fábio Henrique Andrade
    @since 24/11/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method loadFK6Values() class EfficiencyOfAccountsPayableSmartViewBusinessObject
	local cAliasFK6 := "" as character

	self:loadStatementFK6Movements()

	cAliasFK6 := self:oStatementFK6:openAlias(/*cAlias*/, self:cLifeTime, self:cTimeout)

	(cAliasFK6)->(DBGoTop())

	While !(cAliasFK6)->(Eof())
		If (cAliasFK6)->FK6_TPDOC $ "DC!D2"				//DESCONTOS
			If (self:cAlias)->E2_VENCREA >= self:dAvailabilityDate
				self:nPayWithoutDelayDiscount += (cAliasFK6)->FK6_VALMOV
			Else
				self:nPayWithDelayDiscount += (cAliasFK6)->FK6_VALMOV
			Endif
		Elseif (cAliasFK6)->FK6_TPDOC $ "JR|J2|MT|M2"	//JUROS E MULTA
			If (self:cAlias)->E2_VENCREA >= self:dAvailabilityDate
				self:nPayWithoutDelayAddition += (cAliasFK6)->FK6_VALMOV
			Else
				self:nPayWithDelayAddition += (cAliasFK6)->FK6_VALMOV
			Endif
		ElseIf (cAliasFK6)->FK6_TPDOC $ "VA"		//VALORES ACESSÓRIOS
			If (self:cAlias)->E2_VENCREA >= self:dAvailabilityDate
				If (cAliasFK6)->FK6_VALMOV < 0
					self:nPayWithoutDelayDiscount += (cAliasFK6)->FK6_VALMOV
				Else
					self:nPayWithoutDelayAddition += (cAliasFK6)->FK6_VALMOV
				Endif
			Else
				If (cAliasFK6)->FK6_VALMOV < 0
					self:nPayWithDelayAddition += (cAliasFK6)->FK6_VALMOV
				Else
					self:nPayWithoutDelayAddition += (cAliasFK6)->FK6_VALMOV
				Endif
			EndIf
		Endif
		(cAliasFK6)->(dbSkip())
	EndDo

	(cAliasFK6)->( DBCloseArea() )

return

//----------------------------------------------------------------
/*/{Protheus.doc}loadTotalValues
    @author Fábio Henrique Andrade
    @since 30/11/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method loadTotalValues() class EfficiencyOfAccountsPayableSmartViewBusinessObject
	local nTotalValue := 0 as numeric

    if !Empty((self:cAlias)->FK2_IDFK2)
        if (self:cAlias)->FK2_MOTBX $ self:cReasonsToWriteOff

            self:nIssueCurrency  := (self:cAlias)->E2_MOEDA
            self:dConversionDate := StoD((self:cAlias)->FK2_DATA)
            self:nCurrencyRate   := (self:cAlias)->E2_TXMOEDA
            
            nTotalValue := self:convertMovementCurrency((self:cAlias)->FK2_VALOR)
            
            if StoD(self:dAvailabilityDate) > self:dIssueDateFrom
                self:nDebitedOnTheDay  := nTotalValue
                self:nWriteOffOnTheDay := nTotalValue
            else
                self:nWriteOffWithoutDebit := nTotalValue
            endIf

        endIf

        if (self:cAlias)->E2_VENCREA >= self:dAvailabilityDate
            self:nTotalPayWithoutDelay := nTotalValue
        else
            self:nTotalPayWithDelay := nTotalValue
        endIf
	endIf

    SE2->(DbGoTo((self:cAlias)->RECNO))
    self:nOpenValueOnTheDay := Round(SaldoTit((self:cAlias)->E2_PREFIXO,(self:cAlias)->E2_NUM,(self:cAlias)->E2_PARCELA,(self:cAlias)->E2_TIPO,(self:cAlias)->E2_NATUREZ,"P",(self:cAlias)->E2_FORNECE,1,self:dIssueDateTo,self:dIssueDateTo,(self:cAlias)->E2_LOJA,,(self:cAlias)->E2_TXMOEDA), 2)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} getReasonsToWriteOff
Monta a expressão do IN da query para os motivos de baixa que 
movimentem banco

@author Mauricio Pequim Jr
@since  07/06/2018
@version 12.1.17
/*/
//-------------------------------------------------------------------
method getReasonsToWriteOff() as character class EfficiencyOfAccountsPayableSmartViewBusinessObject

	local aMotBx:= ReadMotBx() as array
	local nX := 0 as numeric
	local cMotBx := "" as character

	for nX := 1 to Len(aMotBx)
		if Substr(aMotBx[nX],19,1) == "S"
			cMotBx += "|" + Substr(aMotBx[nX],1,3)
		endIf
	next

	cMotBx := Substr(cMotBx,2,Len(cMotBx))
return cMotBx

//----------------------------------------------------------------
/*/{Protheus.doc}resetValues
    @author Fábio Henrique Andrade
    @since 30/11/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method resetValues() class EfficiencyOfAccountsPayableSmartViewBusinessObject
    self:nPayWithoutDelayDiscount := 0
    self:nPayWithoutDelayAddition := 0
    self:nPayWithDelayDiscount    := 0
    self:nPayWithDelayAddition    := 0
    self:nTotalPayWithoutDelay    := 0
    self:nTotalPayWithDelay       := 0
    self:nDebitedOnTheDay         := 0
    self:nWriteOffOnTheDay        := 0
    self:nWriteOffWithoutDebit    := 0
return 
