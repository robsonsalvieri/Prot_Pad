#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.issueborderaux.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SEA,SE1,SE2", customTables="SEA,SE1,SE2", name="Títulos enviados ao banco", country="ALL")
//-------------------------------------------------------------------
/*/{Protheus.doc} DeliveryOfBankCommunicationSmartViewBusinessObject
Classe para criação do Objeto de Negócio para a Listagem dos títulos
registrados via API. (Borderôs de pagamento ou recebimento)

@author guilherme.sordi@totvs.com.br
@since 13/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
class DeliveryOfBankCommunicationSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.IssueBorderauxSmartViewBusinessObject
    public method new() as object

    protected method initializeBusinessObject()
    protected method handleSchema()
    protected method handleData()
    protected method getSelectComplementPay() as character
    protected method getSelectComplementRec() as character
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
    @author guilherme.sordi@totvs.com.br
    @since 13/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method new() class DeliveryOfBankCommunicationSmartViewBusinessObject
    _Super:new()
return self


//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 13/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class DeliveryOfBankCommunicationSmartViewBusinessObject
    self:setDisplayName(STR0026) //"Títulos enviados ao banco"
    self:setDescription(STR0026) //"Títulos enviados ao banco"
    self:setPergunte("FINT005")
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleSchema
    @author guilherme.sordi@totvs.com.br
    @since 14/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleSchema() class DeliveryOfBankCommunicationSmartViewBusinessObject
    self:oSchema:aliasToSchema("SE1", {"E1_VALJUR", "E1_PORCJUR", "E1_PIS", "E1_COFINS", "E1_CSLL"})
    self:finAddProperty("PCC", STR0027, "number") //"PIS + COFINS + CSLL"
return
//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    @author guilherme.sordi@totvs.com.br
    @since 14/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleData() class DeliveryOfBankCommunicationSmartViewBusinessObject
    local nFee := 0 as numeric

    _Super:handleData()

    nFee := (self:cAlias)->E1_VALJUR
    if empty(nFee)
        nFee := (self:cAlias)->E1_PORCJUR
    endIf
    
    self:jData["E1_VALJUR"] := nFee
    self:jData["PCC"] := (self:cAlias)->E1_PIS + (self:cAlias)->E1_COFINS + (self:cAlias)->E1_CSLL
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getSelectComplementPay
    @author guilherme.sordi@totvs.com.br
    @since 13/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getSelectComplementPay() as character class DeliveryOfBankCommunicationSmartViewBusinessObject
return  ", 0 E1_VALJUR, 0 E1_PORCJUR, E2_PIS E1_PIS, E2_COFINS E1_COFINS, E2_CSLL E1_CSLL"

//-------------------------------------------------------------------
/*/{Protheus.doc} getSelectComplementRec
    @author guilherme.sordi@totvs.com.br
    @since 13/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getSelectComplementRec() as character class DeliveryOfBankCommunicationSmartViewBusinessObject
return ", E1_VALJUR, E1_PORCJUR, E1_PIS, E1_COFINS, E1_CSLL "
