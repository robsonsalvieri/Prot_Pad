#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.listofinvoices.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE2", customTables="SE2", name="Relação de faturas", country="ALL")

/*/{Protheus.doc} ListOfInvoicesFinSmartViewBusinessObject
    @author Fábio Henrique Andrade
    @since 15/09/2023
    @version 12.1.2210
*/
class ListOfInvoicesFinSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
                            
    public method new() as object
    public method mySetSchema() as object

    protected data cSupplierFrom as character
    protected data cSupplierTo as character
    protected data dIssueDateFrom as date
    protected data dIssueDateTo as date
    protected data cClassFrom as character
    protected data cClassTo as character
    protected data lProcessedOnly as logical
    protected data lOpenOnly as logical
    protected data nCurrencyInvoice as numeric
    protected data nCurrency as numeric
    protected data cOriginFather as character
    protected data cOriginSon as character

    protected data cCurrentSupplierCode as character 
    protected data cCurrentSupplierBranch as character 
    protected data cCurrentInvoicePrefix as character 
    protected data cCurrentInvoiceNumber as character 
    protected data cCurrentInvoiceData as character

    protected method loadStatement()
    protected method loadStatementOriginBills()
    protected method appendOriginBills() as object
    protected method myProcessData()
    protected method loadParameters()
    protected method loadDefaultParameters()
    protected method initializeBusinessObject()
    protected method isDistinctProcess() as logical
    protected method setLastAppend()
    protected data oLastAppend as json
    protected data oStatementOriginBills as object

endclass

/*/{Protheus.doc} new
    @author Fábio Henrique Andrade
    @since 15/09/2023
    @version 12.1.2210
*/
method new() class ListOfInvoicesFinSmartViewBusinessObject
    _Super:new()
    self:setCompleteData(.T.)
    self:setAllTrim(.T.)

    self:setAppendInMyAliastoData(.F.)
    self:initializeBusinessObject()
    self:setPageSize(1000000000)
    self:setMainTable("SE2")
    self:setBranchFilter(.T., .T.)

    self:nCurrency          := 1
    self:nCurrencyInvoice   := 1
    self:cOriginFather      := STR0006 //"Títulos que compõe a fatura"
    self:cOriginSon         := STR0007 //"Fatura"
return self


//-------------------------------------------------------------------
/*/{Protheus.doc} initializeBusinessObject
    Método utilizado para inicializar dados do objeto de negócio, como nome, descrição e pergunta.

    @author Matheus Monteiro da Silva
    @since 17/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------  
method initializeBusinessObject() class ListOfInvoicesFinSmartViewBusinessObject
    self:setDisplayName(STR0002)  //Relação de Faturas
    self:setDescription(STR0002)  //Relação de Faturas 
    self:setPergunte("FINT015")
return


/*/{Protheus.doc} mySetSchema
    @author Fábio Henrique Andrade
    @since 15/09/2023
    @version 12.1.2210
*/
method mySetSchema() as object class ListOfInvoicesFinSmartViewBusinessObject
    local aFieldsSE2   := {} as array
    local aFieldsFK2   := {} as array 

    aFieldsSE2 := { "E2_FILIAL","E2_NATUREZ","E2_PREFIXO","E2_NUM","E2_PARCELA",;
                    "E2_FORNECE","E2_LOJA","E2_NOMFOR","E2_VLCRUZ",;
                    "E2_TIPO","E2_EMIS1","E2_VENCREA","E2_MOEDA","E2_VALOR","E2_SALDO" }

    aFieldsFK2 := {"FK2_VALOR"}
    
    self:oSchema:aliasToSchema("SE2", aFieldsSE2 )
    self:oSchema:aliasToSchema("FK2", aFieldsFK2 )

    self:finAddProperty("CLASSIFICACAO" , STR0003, "string") //"Classificação"
    self:finAddProperty("DADOS_FORNECE" , STR0004, "string", "E2_NOMFOR") //"Dados do Fornecedor"
    self:finAddProperty("DADOS_FATURA"  , STR0005, "string", "E2_NUM")  //"Dados da Fatura"
    self:finAddProperty("VALOR_FATURADO", STR0008, "number")  //"Valor que compõe a fatura"
   
    FwFreeArray(aFieldsSE2)
return self:oSchema


/*/{Protheus.doc} getQuery
    @author Fábio Henrique Andrade
    @since 15/09/2023
    @version 12.1.2210
*/
method loadStatement() class ListOfInvoicesFinSmartViewBusinessObject
    local cQuery  := "" as character
    local nParam := 1 as numeric

    cQuery := "SELECT ? " 
    cQuery += "FROM " + RetSQLName("SE2") + " SE2"

    cQuery += " LEFT JOIN " + RetSQLName("FK7") + " FK7 ON "
    cQuery += "     FK7.FK7_FILTIT = SE2.E2_FILIAL "
    cQuery += "     AND FK7.FK7_PREFIX = SE2.E2_PREFIXO "
    cQuery += "     AND FK7.FK7_NUM = SE2.E2_NUM "
    cQuery += "     AND FK7.FK7_PARCEL = SE2.E2_PARCELA "
    cQuery += "     AND FK7.FK7_TIPO = SE2.E2_TIPO "
    cQuery += "     AND FK7.FK7_CLIFOR = SE2.E2_FORNECE "
    cQuery += "     AND FK7.FK7_LOJA = SE2.E2_LOJA "
    cQuery += "     AND FK7.D_E_L_E_T_ = ? "
    cQuery += " LEFT JOIN " + RetSQLName("FK2") + " FK2 ON "
    cQuery += "     FK2.FK2_IDDOC = FK7.FK7_IDDOC"
    cQuery += "     AND FK2.D_E_L_E_T_ = ? "

    cQuery += "WHERE 
	cQuery += self:getSQLBranchFilter()

	cQuery += "     AND E2_FORNECE BETWEEN ? AND ? "
	cQuery += "     AND E2_EMISSAO BETWEEN ? AND ? "
	cQuery += "     AND E2_NATUREZ BETWEEN ? AND ? "
	cQuery += "     AND E2_FATURA =  ? "
    cQuery += "     AND SE2.D_E_L_E_T_ = ? "

    if self:lProcessedOnly
        cQuery += " AND E2_SALDO = ? "
    endIf

    if self:lOpenOnly
        cQuery += " AND E2_SALDO <> ? "
    endIf
   
    cQuery += " ? " 
  
    cQuery += self:cWhereComplement

    cQuery += " ORDER BY ? " 

    cQuery := ChangeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)

    self:oStatement:SetUnsafe(nParam++, self:getAllFieldsToSQL())
    self:oStatement:SetString(nParam++, " ")
    self:oStatement:SetString(nParam++, " ")
    self:setStatementBranchFilter(@nParam)
    self:oStatement:SetString(nParam++, self:cSupplierFrom) 
    self:oStatement:SetString(nParam++, self:cSupplierTo) 
    self:oStatement:SetDate(nParam++, self:dIssueDateFrom) 
    self:oStatement:SetDate(nParam++, self:dIssueDateTo) 
    self:oStatement:SetString(nParam++, self:cClassFrom) 
    self:oStatement:SetString(nParam++, self:cClassTo)
    self:oStatement:SetString(nParam++, "NOTFAT")
    self:oStatement:SetString(nParam++, " ")

    if self:lProcessedOnly
        self:oStatement:SetNumeric(nParam++, 0)
    endIf

    if self:lOpenOnly
        self:oStatement:SetNumeric(nParam++, 0)
    endIf

    self:oStatement:SetUnsafe(nParam++, self:getFilterToSQL())
    self:oStatement:SetUnsafe(nParam++, SqlOrder(SE2->(IndexKey( 1 ))))
return 

/*/{Protheus.doc} loadStatementOriginBills
    @author Fábio Henrique Andrade
    @since 19/09/2023
    @version 12.1.2210
*/
method loadStatementOriginBills()  class ListOfInvoicesFinSmartViewBusinessObject
    local cQuery  := "" as character
    local nParam := 1 as numeric

    If self:oStatementOriginBills == Nil
        cQuery := "SELECT ? 
        cQuery += "FROM " + RetSQLName("SE2") + " SE2 "
        cQuery += " INNER JOIN " + RetSQLName("FK7") + " FK7 ON "
        cQuery += "     FK7.FK7_FILTIT = SE2.E2_FILIAL "
        cQuery += "     AND FK7.FK7_PREFIX = SE2.E2_PREFIXO "
        cQuery += "     AND FK7.FK7_NUM = SE2.E2_NUM "
        cQuery += "     AND FK7.FK7_PARCEL = SE2.E2_PARCELA "
        cQuery += "     AND FK7.FK7_TIPO = SE2.E2_TIPO "
        cQuery += "     AND FK7.FK7_CLIFOR = SE2.E2_FORNECE "
        cQuery += "     AND FK7.FK7_LOJA = SE2.E2_LOJA "
        cQuery += "     AND FK7.FK7_ALIAS = ? "
        cQuery += "     AND FK7.D_E_L_E_T_ = ? "
        cQuery += " INNER JOIN " + RetSQLName("FK2") + " FK2 ON "
        cQuery += "     FK2.FK2_IDDOC = FK7.FK7_IDDOC"
        cQuery += "     AND FK2.FK2_TPDOC = ? "
        cQuery += "     AND FK2.FK2_MOTBX = ? "
        cQuery += "     AND FK2.D_E_L_E_T_ = ? "
        cQuery += "     AND NOT EXISTS( "
        cQuery += "  SELECT FK2EST.FK2_IDDOC FROM  " + RetSQLName("FK2") + " FK2EST "
        cQuery += "  WHERE FK2EST.FK2_IDDOC = FK2.FK2_IDDOC "
        cQuery += "     AND FK2EST.FK2_SEQ = FK2.FK2_SEQ "
        cQuery += "     AND FK2EST.FK2_TPDOC = ? "
        cQuery += "     AND FK2EST.D_E_L_E_T_ = ? ) "
        cQuery += "WHERE "
	    cQuery += "     SE2.E2_FORNECE = ? "
	    cQuery += "     AND SE2.E2_LOJA = ? "
        cQuery += "     AND SE2.E2_FATPREF = ? "
	    cQuery += "     AND SE2.E2_FATURA = ? "
        cQuery += "     AND SE2.D_E_L_E_T_ = ? "

        cQuery += "ORDER BY ? "

        cQuery := ChangeQuery(cQuery)

        self:oStatementOriginBills := FWExecStatement():New(cQuery)
    EndIf
    
    self:oStatementOriginBills:SetUnsafe(nParam++, self:getAllFieldsToSQL())
    self:oStatementOriginBills:SetString(nParam++, "SE2")
    self:oStatementOriginBills:SetString(nParam++, " ")
    self:oStatementOriginBills:SetString(nParam++, "BA")
    self:oStatementOriginBills:SetString(nParam++, "FAT")
    self:oStatementOriginBills:SetString(nParam++, " ")
    self:oStatementOriginBills:SetString(nParam++, "ES")
    self:oStatementOriginBills:SetString(nParam++, " ")
    self:oStatementOriginBills:SetString(nParam++, self:cCurrentSupplierCode) 
    self:oStatementOriginBills:SetString(nParam++, self:cCurrentSupplierBranch) 
    self:oStatementOriginBills:SetString(nParam++, self:cCurrentInvoicePrefix)
    self:oStatementOriginBills:SetString(nParam++, self:cCurrentInvoiceNumber) 
    self:oStatementOriginBills:SetString(nParam++, " ") 
    self:oStatementOriginBills:SetUnsafe(nParam++, SqlOrder(SE2->(IndexKey( 1 ))))
return 

/*/{Protheus.doc} appendOriginBills
    @author Fábio Henrique Andrade
    @since 15/09/2023
    @version 12.1.2210
*/
method appendOriginBills() as object class ListOfInvoicesFinSmartViewBusinessObject
    local cAliasChild := "" as character
    local xDuoDate := nil 
    local xAccountingDate := nil
    local cSupplierData := "" as character

    self:loadStatementOriginBills()
    cAliasChild:=  self:oStatementOriginBills:openAlias()

    (cAliasChild)->(DBGoTop())
 
    While !(cAliasChild)->(Eof())
        xDuoDate        := self:varToTimeStamp((cAliasChild)->E2_VENCREA)
        xAccountingDate := self:varToTimeStamp((cAliasChild)->E2_EMIS1)
        cSupplierData    := AllTrim((cAliasChild)->E2_FORNECE) + "/" + AllTrim((cAliasChild)->E2_LOJA) + " - " + AllTrim((cAliasChild)->E2_NOMFOR)

        self:jData := {; 
            "CLASSIFICACAO": self:cOriginFather,;
            "E2_FILIAL": (cAliasChild)->E2_FILIAL,;
            "DADOS_FORNECE": cSupplierData,;
            "E2_PREFIXO": (cAliasChild)->E2_PREFIXO,;
            "E2_NUM": (cAliasChild)->E2_NUM,;
            "E2_PARCELA": (cAliasChild)->E2_PARCELA,;
            "E2_TIPO": (cAliasChild)->E2_TIPO,;
            "E2_NATUREZ": MascNat( AllTrim((cAliasChild)->E2_NATUREZ)),;
            "E2_EMIS1": xAccountingDate,;
            "E2_VENCREA": xDuoDate,;
            "E2_VALOR": (cAliasChild)->E2_VALOR,;
            "E2_MOEDA":(cAliasChild)->E2_MOEDA,;
            "E2_SALDO": (cAliasChild)->E2_SALDO,;
            "DADOS_FATURA": self:cCurrentInvoiceData,;
            "VALOR_FATURADO": (cAliasChild)->FK2_VALOR;
            }

        self:processAndAppend()
        (cAliasChild)->(DbSkip())
    Enddo

    (cAliasChild)->( DBCloseArea() )
return self:oData

/*/{Protheus.doc} myAliasToData
    @author Fábio Henrique Andrade
    @since 15/09/2023
    @version 12.1.2210
*/
method myProcessData() class ListOfInvoicesFinSmartViewBusinessObject
    local xDuoDate := nil 
    local xAccountingDate := nil
    local cSupplierData := "" as character

    xDuoDate        := self:varToTimeStamp((self:cAlias)->E2_VENCREA)
    xAccountingDate := self:varToTimeStamp((self:cAlias)->E2_EMIS1)
    cSupplierData    := AllTrim((self:cAlias)->E2_FORNECE) + "/" + AllTrim((self:cAlias)->E2_LOJA) + " - " + AllTrim((self:cAlias)->E2_NOMFOR)

    self:cCurrentSupplierCode   := AllTrim((self:cAlias)->E2_FORNECE)
    self:cCurrentSupplierBranch := AllTrim((self:cAlias)->E2_LOJA)
    self:cCurrentInvoicePrefix := AllTrim((self:cAlias)->E2_PREFIXO)
    self:cCurrentInvoiceNumber := AllTrim((self:cAlias)->E2_NUM)
    self:cCurrentInvoiceData   := AllTrim((self:cAlias)->E2_PREFIXO) + "-" + AllTrim((self:cAlias)->E2_NUM) + "-" + AllTrim((self:cAlias)->E2_TIPO)

    If self:isDistinctProcess()
        self:appendOriginBills()
    endIf

    self:jData := {; 
        "CLASSIFICACAO": self:cOriginSon,;
        "E2_FILIAL": (self:cAlias)->E2_FILIAL,;
        "DADOS_FORNECE": cSupplierData,;
        "E2_PREFIXO": (self:cAlias)->E2_PREFIXO,;
        "E2_NUM": (self:cAlias)->E2_NUM,;
        "E2_PARCELA": (self:cAlias)->E2_PARCELA,;
        "E2_TIPO": (self:cAlias)->E2_TIPO,;
        "E2_NATUREZ": MascNat( AllTrim((self:cAlias)->E2_NATUREZ)),;
        "E2_EMIS1": xAccountingDate,;
        "E2_VENCREA": xDuoDate,;
        "E2_VALOR": (self:cAlias)->E2_VALOR,;
        "E2_MOEDA": (self:cAlias)->E2_MOEDA,;
        "E2_SALDO": (self:cAlias)->E2_SALDO,;
        "DADOS_FATURA": self:cCurrentInvoiceData,;
        "VALOR_FATURADO": 0;
        }

    self:setLastAppend()
    self:processAndAppend()
return

/*/{Protheus.doc} loadParameters
    @author Fábio Henrique Andrade
    @since 15/09/2023
    @version 12.1.2210
*/
method loadParameters() class ListOfInvoicesFinSmartViewBusinessObject   
    If self:oCurrentFilter != Nil
        self:cSupplierFrom   := MV_PAR01
        self:cSupplierTo     := MV_PAR02
        self:dIssueDateFrom := MV_PAR03
        self:dIssueDateTo   := MV_PAR04
        self:cClassFrom     := MV_PAR05
        self:cClassTo       := MV_PAR06
        self:lProcessedOnly := MV_PAR07 == 1
        self:lOpenOnly      := MV_PAR07 == 2
    endIf
return

/*/{Protheus.doc} loadDefaultParameters
    @author Fábio Henrique Andrade
    @since 15/09/2023
    @version 12.1.2210
*/
method loadDefaultParameters() class ListOfInvoicesFinSmartViewBusinessObject
    self:cSupplierFrom       := ""
    self:cSupplierTo         := ""
    self:dIssueDateFrom     := dDatabase
    self:dIssueDateTo       := dDatabase
    self:cClassFrom         := ""
    self:cClassTo           := ""
    self:lProcessedOnly     := .F.
    self:lOpenOnly          := .F.
return


/*/{Protheus.doc} isDistinctProcess
    @author matheus.monteiro@totvs.com.br
    @since 22/01/2024
    @version 12.1.2210
*/
method isDistinctProcess() as logical class ListOfInvoicesFinSmartViewBusinessObject
    Local lReturn := .F. as logical 

    If self:oLastAppend == NIL
        return .T.
    Endif 

    lReturn := (self:cAlias)->E2_FORNECE <> self:oLastAppend["E2_FORNECE"] .or. (self:cAlias)->E2_LOJA <> self:oLastAppend["E2_LOJA"] .or.;
    (self:cAlias)->E2_PREFIXO <> self:oLastAppend["E2_PREFIXO"] .or. (self:cAlias)->E2_NUM <> self:oLastAppend["E2_NUM"]

return lReturn

/*/{Protheus.doc} setLastAppend
    @author guilherme.sordi@totvs.com.br
    @since 22/01/2024
    @version 12.1.2210
*/
method setLastAppend() class ListOfInvoicesFinSmartViewBusinessObject
    self:oLastAppend := JSONObject():new()
    self:oLastAppend["E2_FORNECE"] := (self:cAlias)->E2_FORNECE
    self:oLastAppend["E2_LOJA"] := (self:cAlias)->E2_LOJA
    self:oLastAppend["E2_PREFIXO"] := (self:cAlias)->E2_PREFIXO
    self:oLastAppend["E2_NUM"] := (self:cAlias)->E2_NUM
return
