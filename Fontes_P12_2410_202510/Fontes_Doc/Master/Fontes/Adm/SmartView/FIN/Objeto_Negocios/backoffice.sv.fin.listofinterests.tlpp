#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.listofposts.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE1,SE2,FK1,FK2,FK5,F7G,SED", name="Juros recebidos e pagos", country="ALL")
//-------------------------------------------------------------------
/*/{Protheus.doc} ListOfInterestsSmartViewBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 23/08/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
class ListOfInterestsSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.ListOfPostsSmartViewBusinessObject
    public method new() as object 
    protected method handleData() as json
    protected method handleSchema()
    protected method handleQuery() as character
    protected method loadParameters() 
    protected method initializeBusinessObject()
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
    Construtor
    @author guilherme.sordi@totvs.com.br
    @since 23/08/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method new() class ListOfInterestsSmartViewBusinessObject
    _Super:new()

    self:lDistinguirJuros := .T.

return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject

@author Fábio Andrade
@since 08/01/2024
@version 1.0
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class ListOfInterestsSmartViewBusinessObject
     self:setDisplayName(STR0016) //"Juros recebidos e pagos"
    self:setDescription(STR0016) //"Juros recebidos e pagos"
    
    self:setPergunte("FINT010")
return


//-------------------------------------------------------------------
/*{Protheus.doc} handleSchema
    Complementa a estrutura dos campos e conjunto de parametros
    @author guilherme.sordi@totvs.com.br
    @since 23/08/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleSchema() class ListOfInterestsSmartViewBusinessObject    
    self:finAddProperty("JUROS", STR0017, "number")      //"Juros"
    self:finAddProperty("MULTA", STR0018, "number")      //"Multa"
    self:finAddProperty("JUROSMULTA", STR0019, "number") //"Juros + multa"
    self:finAddProperty("ATRASO", STR0020, "number")     //"Atraso"
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    Complementa os dados retornados pelo objeto de negócio
    @author guilherme.sordi@totvs.com.br
    @since 23/08/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData() class ListOfInterestsSmartViewBusinessObject
    local nAtraso := 0 as numeric
    local dVencRea := dDatabase as date
    local dVencto := dDatabase as date
    local dBaixa := dDatabase as date
    
    _Super:handleData()

    self:jData['JUROS'] := self:converteMoedaMovimento( (self:cAlias)->JUROS )
    self:jData['MULTA'] := self:converteMoedaMovimento( (self:cAlias)->MULTA )
    self:jData['JUROSMULTA'] := self:converteMoedaMovimento( (self:cAlias)->JUROS + (self:cAlias)->MULTA )
    
    if self:lPagar
        dVencRea := (self:cAlias)->E2_VENCREA
        dVencto := (self:cAlias)->E2_VENCTO
        dBaixa := (self:cAlias)->FK2_DATA
    else
        dVencRea := (self:cAlias)->E1_VENCREA
        dVencto := (self:cAlias)->E1_VENCTO
        dBaixa := (self:cAlias)->FK1_DATA
    endIf

    if dDatabase > dVencRea
        nAtraso := dateDiffDay(dVencto, dBaixa)
    endIf

    self:jData['ATRASO'] := nAtraso

return self:jData

//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters
    Definição de parâmetros do objeto de negócio
    @author guilherme.sordi@totvs.com.br
    @since 24/08/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method loadParameters() class ListOfInterestsSmartViewBusinessObject    
    if self:oCurrentFilter != NIL
       self:lPagar := MV_PAR01 == 1
       self:dBaixaDe := MV_PAR02
       self:dBaixaAte := MV_PAR03
       self:dDigitacaoDe := MV_PAR04
       self:dDigitacaoAte := MV_PAR05
       self:nMoeda := MV_PAR06
       self:lListarOutrasMoedas := MV_PAR07 == 2
    endIf
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleQuery
    Permite manipular a query, transformando ela em view e executando filtros depois do primeiro resultado.
    @author guilherme.sordi@totvs.com.br
    @since 28/08/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleQuery(cQuery as character) as character class ListOfInterestsSmartViewBusinessObject
    cQuery := " SELECT * FROM ("+ cQuery +") VW_BAIXAS "
    cQuery += " WHERE (JUROS > 0) OR (MULTA > 0) "
return cQuery
