#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.billsrecbysalesrep.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE1,SED,SA1,SA3,SC5,SC6", name="Previsão de comissões", country="ALL", customTables="SE1,SED,SA1,SA3,SC5,SC6")
//-------------------------------------------------------------------
/*/{Protheus.doc} ForecastOfComissionSmartViewBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
class ForecastOfComissionSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.BillsReceivableBySalesRepSmartViewBusinessObject
    public method new() as object 

    protected method initializeBusinessObject()
    protected method mySetSchema()
    protected method handleData()
    protected method loadDefaultParameters()
    protected method loadParameters()
    protected method getMainQueryWhere() as character
    protected method handleMainQueryWhere()
    protected method getRenegotiatedBillsQueryWhere() as character
    protected method handleRenegotiatedBillsQueryParameters()
    protected method getComissionValues() as json
    protected method getComissionValuesFromLegacy() as json
    protected method getCommissionValueIssue() as numeric
    protected method getLegacyCalculate() as json
    protected method loadParametersOfComission() as json

    protected method postCloseQuery()
    protected method appendSalesOrder()
    protected method loadSalesOrder()
    protected method getSalespersonViewForSalesOrder() as character
    protected method handleDataSalesOrder()
    protected method processAndAppendSalesOrder()
    protected method getFieldsToSQLForSalesOrder() as character
    protected method getBaseValueToComissionForSalesOrder() as numeric
    protected method getOriginalRecnoSD1() as numeric
    protected method callMaFisIni()
    protected method loadOrderTaxes()
    protected method subtractTaxes() as numeric
    protected method getInstallments()

    protected data cSalespersonCodeFrom as character
    protected data cSalespersonCodeTo as character
    protected data dIssueFrom as date
    protected data dIssueTo as date
    protected data dDuoFrom as date
    protected data dDuoTo as date
    protected data lDuoDateByOriginalBill as logical
    protected data lConvertCurrency as logical
    protected data lListSalesOrder as logical
    protected data lAddsInterest as logical
    protected data lSubstractDiscount as logical
    protected data nIRRFAliquot as numeric
    protected data nIRRFMinimumValueForWithholding as numeric
    
    protected data jCurrentInstallment as json
    protected data jParametersOnComission as json
    protected data jOrderTaxes as json

    protected data oSalesOrder as object
    protected data oExecOrderItems as object
    protected data oExecSD1 as object
    protected data oExecSE3 as object
    protected data oHmLegacyCalculate as object
endClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method new() as object class ForecastOfComissionSmartViewBusinessObject
    _Super:new()
    
    self:oHmLegacyCalculate := HMNew()
    self:nIRRFMinimumValueForWithholding := SuperGetMv( "MV_VLRETIR", , 0 )
    self:lForecastOfComission := .T.
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} initializeBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class ForecastOfComissionSmartViewBusinessObject
    self:setDisplayName(STR0014) //"Previsão de comissões"
    self:setDescription(STR0014) //"Previsão de comissões"
    self:setPergunte("FINT030")
return

//-------------------------------------------------------------------
/*/{Protheus.doc} initializeBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method mySetSchema() class ForecastOfComissionSmartViewBusinessObject
    _Super:mySetSchema()

    self:oSchema:aliasToSchema("SA3", { "A3_COMIS", "A3_ALEMISS", "A3_ALBAIXA", "A3_ISS", "A3_ICM" })
    self:oSchema:aliasToSchema("SC5", { "C5_FILIAL", "C5_NUM", "C5_EMISSAO", "C5_MOEDA", "C5_TXMOEDA", "C5_CONDPAG", "C5_TIPO", "C5_CLIENTE", "C5_LOJAENT", "C5_TIPOCLI", "C5_COMIS" })
    self:oSchema:aliasToSchema("SC6", { "C6_FILIAL", "C6_NUM", "C6_ITEM", "C6_QTDVEN", "C6_PRCVEN", "C6_VALOR", "C6_QTDENT", "C6_TES", "C6_PRODUTO", "C6_COMIS" })

    self:finAddProperty("VALUE_ISSUE", STR0015, "number") //Comissão na emissão
    self:finAddProperty("VALUE_WRITEOFF", STR0016, "number") //Comissão na baixa
    self:finAddProperty("BASE_VALUE_WRITEOFF", STR0017, "number") //Valor base para baixa
    self:finAddProperty("COMISSION_VALUE", STR0019, "number") //Valor total de comissão
    self:finAddProperty("ISSUE_DATE", STR0020, "date") //Emissão Pedido/Título
    self:finAddProperty("DUO_DATE", STR0022, "date") //Vencimento
    self:finAddProperty("SALES_ORDER_DATA", STR0021, "string") //Dados do pedido
    self:finAddProperty("IRRF_VALUE", STR0023, "number") //Valor IRRF
    self:finAddProperty("NET_COMISSION_AMOUNT", STR0024, "number") //Comissão total (líquido)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} handleData
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleData() class ForecastOfComissionSmartViewBusinessObject
    local lCreditBill := .F. as logical
    local jComissionValues as json
    _Super:handleData()

    lCreditBill := ((self:cAlias)->E1_TIPO $ MVRECANT + "|" + MV_CRNEG)  

    self:jData["ISSUE_DATE"] := self:varToTimeStamp((self:cAlias)->E1_EMISSAO)
    self:jData["DUO_DATE"] := self:varToTimeStamp((self:cAlias)->E1_VENCTO)
    self:jData["A3_COMIS"] := (self:cAlias)->COMIS
    
    self:jData["SALES_ORDER_DATA"] := NIL

    if lCreditBill
        return
    endIf

    jComissionValues := self:getComissionValuesFromLegacy()

    self:jData["VALUE_ISSUE"] := self:convertCurrencyValue( jComissionValues["nComissionValueIssue"] )
    self:jData["BASE_VALUE_WRITEOFF"] := self:convertCurrencyValue(  jComissionValues["nBaseValueWriteOff"] )
    self:jData["VALUE_WRITEOFF"] := self:convertCurrencyValue( jComissionValues["nComissionValueWriteOff"] )
    self:jData["COMISSION_VALUE"] := self:convertCurrencyValue( jComissionValues["nTotalComissionValue"] )   
    self:jData["IRRF_VALUE"] := self:convertCurrencyValue( jComissionValues["nIRRF"] )
    self:jData["NET_COMISSION_AMOUNT"] := self:convertCurrencyValue( jComissionValues["nNetComissionAmount"] )
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadParameters
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class ForecastOfComissionSmartViewBusinessObject
    _Super:loadDefaultParameters()

    self:cSalespersonCodeFrom := ""
    self:cSalespersonCodeTo := ""
    self:dIssueFrom := cToD("  /  /    ")
    self:dIssueTo := cToD("  /  /    ")
    self:dDuoFrom := cToD("  /  /    ")
    self:dDuoTo := cToD("  /  /    ")
    self:lDuoDateByOriginalBill := .T.
    self:lConvertCurrency := .F.
    self:lListSalesOrder := .F.
    self:lAddsInterest := .F.
    self:lSubstractDiscount := .F.
    self:nIRRFAliquot := 0
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadParameters
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class ForecastOfComissionSmartViewBusinessObject
    self:cSalespersonCodeFrom := MV_PAR01
    self:cSalespersonCodeTo := MV_PAR02
    self:dIssueFrom := MV_PAR03
    self:dIssueTo := MV_PAR04
    self:dDuoFrom := MV_PAR05
    self:dDuoTo := MV_PAR06
    self:lDuoDateByOriginalBill := MV_PAR07 == 1
    self:nCurrency := if(empty(MV_PAR08), 1, MV_PAR08)
    self:lConvertCurrency := MV_PAR09 == 2
    self:lListSalesOrder := MV_PAR10 == 1
    self:lAddsInterest := MV_PAR11 == 1
    self:lSubstractDiscount := MV_PAR12 == 1
    self:nIRRFAliquot := MV_PAR13
return

//-------------------------------------------------------------------
/*{Protheus.doc} getMainQueryWhere
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getMainQueryWhere() as character class ForecastOfComissionSmartViewBusinessObject
    local cQuery := "" as character

    cQuery += " AND VW.VENDEDOR BETWEEN ? AND ? "
    cQuery += " AND SE1.E1_EMISSAO BETWEEN ? AND ? "
    
    if self:lDuoDateByOriginalBill
        cQuery += " AND SE1.E1_VENCTO BETWEEN ? AND ? "
    endIf

    if !self:lConvertCurrency
        cQuery += " AND SE1.E1_MOEDA = ? "
    endIf

return cQuery

//-------------------------------------------------------------------
/*{Protheus.doc} handleMainQueryWhere
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleMainQueryWhere() class ForecastOfComissionSmartViewBusinessObject
    self:oStatement:setString(self:nCurrentParameterOrder++, self:cSalespersonCodeFrom)
    self:oStatement:setString(self:nCurrentParameterOrder++, self:cSalespersonCodeTo)
    self:oStatement:setDate(self:nCurrentParameterOrder++, self:dIssueFrom)
    self:oStatement:setDate(self:nCurrentParameterOrder++, self:dIssueTo)
    
    if self:lDuoDateByOriginalBill
        self:oStatement:setDate(self:nCurrentParameterOrder++, self:dDuoFrom)
        self:oStatement:setDate(self:nCurrentParameterOrder++, self:dDuoTo)
    endIf

    if !self:lConvertCurrency
        self:oStatement:setNumeric(self:nCurrentParameterOrder++, self:nCurrency)
    endIf
return

//-------------------------------------------------------------------
/*{Protheus.doc} getRenegotiatedBillsQueryWhere
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getRenegotiatedBillsQueryWhere() as character class ForecastOfComissionSmartViewBusinessObject
    local cQuery := "" as character

    if !self:lDuoDateByOriginalBill
        cQuery += " AND SE1.E1_VENCTO BETWEEN ? AND ? "
    endIf

return cQuery

//-------------------------------------------------------------------
/*{Protheus.doc} handleRenegotiatedBillsQueryParameters
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleRenegotiatedBillsQueryParameters() class ForecastOfComissionSmartViewBusinessObject
    self:nCurrentParameterOrder := 1

    _Super:handleRenegotiatedBillsQueryParameters()

    if !self:lDuoDateByOriginalBill
        self:oStRenegotiatedBills:setDate(self:nCurrentParameterOrder++, self:dDuoFrom)
        self:oStRenegotiatedBills:setDate(self:nCurrentParameterOrder++, self:dDuoTo)
    endIf
return

//-------------------------------------------------------------------
/*{Protheus.doc} postCloseQuery
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method postCloseQuery() class ForecastOfComissionSmartViewBusinessObject
    _Super:postCloseQuery()

    if self:lListSalesOrder
        self:appendSalesOrder()
    endIf
return

//-------------------------------------------------------------------
/*{Protheus.doc} appendSalesOrder
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method appendSalesOrder() class ForecastOfComissionSmartViewBusinessObject
    local aInstallments := {} as array
    local nX := 1 as numeric

    self:loadParametersOfComission()
    self:setMainTable("SC5")
    self:loadMainTableSelectedBranches()
    self:loadBranchNames()
    self:loadSalesOrder()
    self:cAlias := self:oSalesOrder:openAlias()

    (self:cAlias)->(DBGoTop())    

    if (self:cAlias)->(Eof()) 
        (self:cAlias)->(dbCloseArea())
        return
    endIf

    While !(self:cAlias)->(Eof())       
        aInstallments := self:getInstallments()

        for nX := 1 to len(aInstallments)

            self:jCurrentInstallment := {"dDuoDate": aInstallments[nX][1], "nFactor": aInstallments[nX][2], "nInstallment":nX}
            
            if self:jCurrentInstallment["dDuoDate"] >= self:dDuoFrom .and. self:jCurrentInstallment["dDuoDate"] <= self:dDuoTo            
                self:jData := JSONObject():new() 
                self:processAndAppendSalesOrder()
            endIf

            freeObj( self:jCurrentInstallment )

        next nX

        (self:cAlias)->(DbSkip())
    Enddo

    (self:cAlias)->(DBCloseArea())
return


//-------------------------------------------------------------------
/*{Protheus.doc} loadSalesOrder
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadSalesOrder() class ForecastOfComissionSmartViewBusinessObject
    local cQuery := "" as character
    local nCurrentParameterOrder := 1 as numeric

    self:addFieldsToStruct("C5_EMISSAO")    

    cQuery += " SELECT ? "
    cQuery += " , VENDEDORES.SA3RECNO, VENDEDORES.SC5RECNO, VENDEDORES.SC6RECNO, VENDEDORES.COMIS " 
    cQuery += " FROM " + retSQLName("SC5") + " SC5 "
    
    cQuery += " JOIN VENDEDORES  "
    cQuery += " ON VENDEDORES.SC5RECNO = SC5.R_E_C_N_O_ "
    
    cQuery += " JOIN " + retSQLName("SA3") + " SA3  "
    cQuery += " ON VENDEDORES.SA3RECNO = SA3.R_E_C_N_O_ "
    cQuery += " AND SA3.A3_COD BETWEEN ? AND ? "
    
    cQuery += " JOIN " + retSQLName("SC6") + " SC6 "
    cQuery += " ON VENDEDORES.SC6RECNO = SC6.R_E_C_N_O_ "
    cQuery += " AND SC6.C6_QTDVEN > SC6.C6_QTDENT "
    cQuery += " AND SC6.C6_BLOQUEI = ? "
    cQuery += " AND LEFT(SC6.C6_BLQ,?) NOT IN ( ? ) "

    cQuery += " JOIN " + retSQLName("SA1") + " SA1 "
    cQuery += " ON ? "
    cQuery += " AND C5_CLIENTE = A1_COD "
    cQuery += " AND C5_LOJACLI = A1_LOJA "
    cQuery += " AND SA1.D_E_L_E_T_ = ? "

    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += " AND SC5.C5_EMISSAO BETWEEN ? AND ? "

    if !self:lConvertCurrency
        cQuery += " AND SC5.C5_MOEDA = ? "
    endIf

    cQuery += " AND SC5.D_E_L_E_T_ = ? "

    cQuery += " ORDER BY " + SqlOrder(SC5->(IndexKey( 1 ))) + ", " + SqlOrder(SC6->(IndexKey( 1 )))
 
    cQuery := changeQuery(cQuery)

    cQuery := "WITH VENDEDORES AS (" + self:getSalespersonViewForSalesOrder() + ") " + cQuery

    self:oSalesOrder := FWExecStatement():New(cQuery)

    self:oSalesOrder:setUnsafe(nCurrentParameterOrder++,self:getFieldsToSQLForSalesOrder())
    self:oSalesOrder:setString(nCurrentParameterOrder++, self:cSalespersonCodeFrom)
    self:oSalesOrder:setString(nCurrentParameterOrder++, self:cSalespersonCodeTo)
    self:oSalesOrder:setString(nCurrentParameterOrder++, " ")
    self:oSalesOrder:setNumeric(nCurrentParameterOrder++, 1 )
    self:oSalesOrder:SetIn(nCurrentParameterOrder++, {'R','S'})
    self:oSalesOrder:setUnsafe(nCurrentParameterOrder++, FwJoinFilial("SC5", "SA1"))
    self:oSalesOrder:setString(nCurrentParameterOrder++, " ")

    if self:lEmptyMultiBranches
        self:oSalesOrder:SetString(nCurrentParameterOrder++, fwXFilial(self:cMainTable))
    else
        self:oSalesOrder:SetIn(nCurrentParameterOrder++, self:aMainTableSelectedBranches)
    endIf
    self:oSalesOrder:setDate(nCurrentParameterOrder++, self:dIssueFrom)
    self:oSalesOrder:setDate(nCurrentParameterOrder++, self:dIssueTo)
    if !self:lConvertCurrency
        self:oSalesOrder:setNumeric(nCurrentParameterOrder++, self:nCurrency)  
    endIf
    self:oSalesOrder:setString(nCurrentParameterOrder++, " ")


    self:oSalesOrder:setFields(self:aStruct)
return

//-------------------------------------------------------------------
/*{Protheus.doc} getSalespersonViewForSalesOrder
    Cria uma view em SQL que transforma o relacionamento entre 
    as tabelas SC5, SC6 e SA3 em 1 para n. Isto é, nossa query principal
    vai retorna uma linha para cada combinação pedido/item/vendedor.
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getSalespersonViewForSalesOrder() as character class ForecastOfComissionSmartViewBusinessObject
    local cQuery := "" as character
    local nX     := 0  as numeric
    local cVendC5  := "" as character
    local cComisC5  := "" as character
    local cComisC6  := "" as character

    for nX := 1 to 5
        cVendC5 := "C5_VEND" + cValToChar(nX)
        cComisC5 := "C5_COMIS" + cValToChar(nX)
        cComisC6 := "C6_COMIS" + cValToChar(nX)

        cQuery += if( nX > 1, " UNION ", "")

        cQuery += " SELECT SC5.R_E_C_N_O_ SC5RECNO, SA3.R_E_C_N_O_ SA3RECNO, SC6.R_E_C_N_O_ SC6RECNO, "+ cComisC5 +" C5_COMIS, "+ cComisC6 +" C6_COMIS, "
        cQuery += " CASE WHEN "+ cComisC6 +" = 0 THEN "+ cComisC5 +" ELSE "+ cComisC6 +" END AS COMIS  "
        cQuery += " FROM "+ retSQLName("SC5") +" SC5 "

        cQuery += " JOIN "+ retSQLName("SA3") +" SA3 "
        cQuery += " ON " + FwJoinFilial("SC5", "SA3")
        cQuery += " AND "+ cVendC5 + " = A3_COD "
        cQuery += " AND SA3.D_E_L_E_T_ = ' ' "

        cQuery += " JOIN "+ retSQLName("SC6") +" SC6 "
        cQuery += " ON " + FwJoinFilial("SC5", "SC6")
        cQuery += " AND C5_NUM = C6_NUM "
        cQuery += " AND SC6.D_E_L_E_T_ = ' ' "

        cQuery += " WHERE SC5.D_E_L_E_T_ = ' ' "
    next nX

    cQuery := changeQuery(cQuery)

return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} getFieldsToSQLForSalesOrder
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getFieldsToSQLForSalesOrder() as character class ForecastOfComissionSmartViewBusinessObject
    local aTables := {"SA3", "SC6", "SC5", "SA1", "SF4"}
return _Super:getFieldsToSQL(aTables)

//-------------------------------------------------------------------
/*/{Protheus.doc} handleDataSalesOrder
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleDataSalesOrder() class ForecastOfComissionSmartViewBusinessObject
    local nBaseValue := 0 as numeric
    local jComissionValues as json

    local jFormatedText := self:getFormatedText() as json

    nBaseValue := self:getBaseValueToComissionForSalesOrder()

    jComissionValues := self:getComissionValues(nBaseValue)

    self:jData["VALUE_ISSUE"] := self:convertCurrencyValue( jComissionValues["nComissionValueIssue"] )
    self:jData["BASE_VALUE_WRITEOFF"] := self:convertCurrencyValue(  jComissionValues["nBaseValueWriteOff"] )
    self:jData["VALUE_WRITEOFF"] := self:convertCurrencyValue( jComissionValues["nComissionValueWriteOff"] )
    self:jData["COMISSION_VALUE"] := self:convertCurrencyValue( jComissionValues["nTotalComissionValue"] )
    self:jData["IRRF_VALUE"] := self:convertCurrencyValue( jComissionValues["nIRRF"] )
    self:jData["NET_COMISSION_AMOUNT"] := self:convertCurrencyValue( jComissionValues["nNetComissionAmount"] )
    
    self:jData["ISSUE_DATE"] := self:varToTimeStamp((self:cAlias)->C5_EMISSAO)
    self:jData["DUO_DATE"] := self:varToTimeStamp(self:jCurrentInstallment["dDuoDate"])
    self:jData["A3_COMIS"] := (self:cAlias)->COMIS
    
    self:jData["SALESPERSON_DATA"] := jFormatedText["cSalespersonData"]
    self:jData["CUSTOMER_DATA"] := jFormatedText["cCustomerData"]
    self:jData["BILL_DATA"] := jFormatedText["cBillData"]
    self:jData["SALES_ORDER_DATA"] := jFormatedText["cSalesOrderData"]

    self:jData["CLASSIFICATION"] := STR0013 //Pedido de venda
    self:addBranchNameToData()
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getBaseValueToComissionForSalesOrder
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getBaseValueToComissionForSalesOrder() as numeric class ForecastOfComissionSmartViewBusinessObject
    local nBaseValue := 0 as numeric
    
    nBaseValue := (self:cAlias)->C6_PRCVEN * ((self:cAlias)->C6_QTDVEN - (self:cAlias)->C6_QTDENT)

    self:loadOrderTaxes()    
    nBaseValue := self:subtractTaxes(nBaseValue)

    nBaseValue := condicao(nBaseValue, (self:cAlias)->C5_CONDPAG, /*nVIPI*/, (self:cAlias)->C5_EMISSAO, /*nVSol*/)[self:jCurrentInstallment["nInstallment"]][2]

return nBaseValue

//-------------------------------------------------------------------
/*/{Protheus.doc} processAndAppendSalesOrder
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method processAndAppendSalesOrder() class ForecastOfComissionSmartViewBusinessObject
    self:handleDataSalesOrder()
    self:completeData()
    self:jData := self:oLGPD:handleDataWithLGPD(self:jData)    
    self:oData:appendData(self:jData)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getComissionValues
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getComissionValues(nBaseValue as numeric, nIssueBaseValue as numeric) as json class ForecastOfComissionSmartViewBusinessObject
    local nComissionPercent := 0 as numeric
    local nComissionDecimal := 0 as numeric
    local nComissionIssueDecimal := 0 as numeric
    local nComissionWriteOffDecimal := 0 as numeric

    
    local nBaseValueIssue := 0 as numeric
    local nBaseValueWriteOff := 0 as numeric
    local nComissionValueIssue := 0 as numeric
    local nComissionValueWriteOff := 0 as numeric
    local nIRRFDecimal := 0 as numeric

    local jComissionValues := JSONObject():new() as json
    
    default nBaseValue := 0
    default nIssueBaseValue := nBaseValue

    nIRRFDecimal := self:nIRRFAliquot/100

    nComissionPercent := (self:cAlias)->COMIS
    nComissionDecimal := nComissionPercent / 100
    nComissionIssueDecimal := (self:cAlias)->A3_ALEMISS / 100
    nComissionWriteOffDecimal := (self:cAlias)->A3_ALBAIXA / 100

    nBaseValueIssue := nIssueBaseValue * nComissionIssueDecimal
    nBaseValueWriteOff := nBaseValue * nComissionWriteOffDecimal
    nComissionValueIssue := nBaseValueIssue * nComissionDecimal
    nComissionValueWriteOff := nBaseValueWriteOff * nComissionDecimal

    jComissionValues["nBaseValueIssue"] := nBaseValueIssue
    jComissionValues["nBaseValueWriteOff"] := nBaseValueWriteOff
    jComissionValues["nComissionValueIssue"] := nComissionValueIssue
    jComissionValues["nComissionValueWriteOff"] := nComissionValueWriteOff
    jComissionValues["nTotalComissionValue"] := nComissionValueIssue + nComissionValueWriteOff 
    jComissionValues["nIRRF"] := jComissionValues["nTotalComissionValue"] * nIRRFDecimal
    if jComissionValues["nIRRF"] < self:nIRRFMinimumValueForWithholding
        jComissionValues["nIRRF"] := 0
    endIf
    jComissionValues["nNetComissionAmount"] := jComissionValues["nTotalComissionValue"] - jComissionValues["nIRRF"]
 
return jComissionValues

//-------------------------------------------------------------------
/*/{Protheus.doc} getComissionValues
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getComissionValuesFromLegacy() as json class ForecastOfComissionSmartViewBusinessObject    
    local nBaseValueIssue := 0 as numeric
    local nBaseValueWriteOff := 0 as numeric
    local nComissionValueIssue := 0 as numeric
    local nComissionValueWriteOff := 0 as numeric
    local nTotalComissionValue := 0 as numeric
    local nIRRFDecimal := 0 as numeric

    local jComissionValues := JSONObject():new() as json
    local jLegacyCalculate := self:getLegacyCalculate((self:cAlias)->SE1RECNO, (self:cAlias)->A3_COD)

    nIRRFDecimal := self:nIRRFAliquot/100

    if jLegacyCalculate != NIL
        nBaseValueIssue := jLegacyCalculate["nBaseValueIssue"]
        nBaseValueWriteOff := jLegacyCalculate["nBaseValueWriteOff"]
        nComissionValueIssue := jLegacyCalculate["nComissionValueIssue"]
        nComissionValueWriteOff := jLegacyCalculate["nComissionValueWriteOff"]
        nTotalComissionValue := jLegacyCalculate["nComissionValueIssue"] + jLegacyCalculate["nComissionValueWriteOff"] 
    endIf
    
    jComissionValues["nBaseValueIssue"] := nBaseValueIssue
    jComissionValues["nBaseValueWriteOff"] := nBaseValueWriteOff
    jComissionValues["nComissionValueIssue"] := nComissionValueIssue
    jComissionValues["nComissionValueWriteOff"] := nComissionValueWriteOff
    jComissionValues["nTotalComissionValue"] := nTotalComissionValue
    jComissionValues["nIRRF"] := jComissionValues["nTotalComissionValue"] * nIRRFDecimal
    if jComissionValues["nIRRF"] < self:nIRRFMinimumValueForWithholding
        jComissionValues["nIRRF"] := 0
    endIf
    jComissionValues["nNetComissionAmount"] := jComissionValues["nTotalComissionValue"] - jComissionValues["nIRRF"]
 
return jComissionValues

//-------------------------------------------------------------------
/*/{Protheus.doc} getLegacyCalculate
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310

    Constituicao de aBases{} retornada por FA440COMIS()
    Coluna 01    Vendedor
    Coluna 02    Valor do Titulo
    Coluna 03    Base Comissao Emissao
    Coluna 04    Base Comissao Baixa
    Coluna 05    Comissao Emissao
    Coluna 06    Comissao Baixa
    Coluna 07    % Total da comissao
*/
//-------------------------------------------------------------------
method getLegacyCalculate(nRecnoSE1 as numeric, cSalespersonCode as character) as json class ForecastOfComissionSmartViewBusinessObject
    local aLegacy := {} as array
    local jLegacy as json
    local jReturn as json
    local lWriteOnDatabase := .F. as logical
    local lRecalculate := .T. as logical
    local cLegacySalesPersonCode := "" as character
    local nX := 1 as numeric
    local lConverttoCurrency1 := .F. as Logical

    lGotFromHashmap := HMGet(self:oHmLegacyCalculate, nRecnoSE1, jLegacy )    
    if !lGotFromHashmap
        aLegacy := Fa440Comis(nRecnoSE1, lWriteOnDatabase, lRecalculate, /*nRegDevol*/, /*lCalcParc*/, /*nRecnoOrig*/, /*lVendaLoja*/, /*nRegSF2*/)
        if (self:cAlias)->E1_MOEDA > 1
            lConverttoCurrency1 := .T.
        endIf
        jLegacy := JSONObject():new()    
        for nX := 1 to len(aLegacy)
            cLegacySalesPersonCode := aLegacy[nX][1]
            if aLegacy[nX][3] > 0 .and. aLegacy[nX][5] == 0
                aLegacy[nX][5] := self:getCommissionValueIssue(cLegacySalesPersonCode)
            endIf
            if lConverttoCurrency1
                jLegacy[cLegacySalesPersonCode] := {;
                    "nBillValue":               self:convertCurrencyValue(aLegacy[nX][2],,lConverttoCurrency1), ;
                    "nBaseValueIssue":          self:convertCurrencyValue(aLegacy[nX][3],,lConverttoCurrency1), ;
                    "nBaseValueWriteOff":       self:convertCurrencyValue(aLegacy[nX][4],,lConverttoCurrency1),;
                    "nComissionValueIssue":     self:convertCurrencyValue(aLegacy[nX][5],,lConverttoCurrency1), ;
                    "nComissionValueWriteOff":  self:convertCurrencyValue(aLegacy[nX][6],,lConverttoCurrency1), ;
                    "nTotalPercent":            self:convertCurrencyValue(aLegacy[nX][7],,lConverttoCurrency1);
                }
            else
                jLegacy[cLegacySalesPersonCode] := {;
                    "nBillValue":               aLegacy[nX][2], ;
                    "nBaseValueIssue":          aLegacy[nX][3], ;
                    "nBaseValueWriteOff":       aLegacy[nX][4],;
                    "nComissionValueIssue":     aLegacy[nX][5], ;
                    "nComissionValueWriteOff":  aLegacy[nX][6], ;
                    "nTotalPercent":            aLegacy[nX][7];
                }
            endIf

        next nX
        HMSet(self:oHmLegacyCalculate, nRecnoSE1, jLegacy)
    endIf

    if jLegacy != NIL .and. jLegacy:hasProperty(cSalespersonCode)
        jReturn := jLegacy[cSalespersonCode]
    endIf 
return jReturn

//-------------------------------------------------------------------
/*/{Protheus.doc} callMaFisIni
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method callMaFisIni() class ForecastOfComissionSmartViewBusinessObject
    local cType := "" as character
    local cProgram := "MATA461" as character

    MaFisEnd()

    if (self:cAlias)->C5_TIPO $ "DB"
        cType := "F" //Supplier
    else
        cType := "C" //Customer
    endIf

    MaFisIni((self:cAlias)->C5_CLIENTE, (self:cAlias)->C5_LOJAENT, cType, (self:cAlias)->C5_TIPO, (self:cAlias)->C5_TIPOCLI, /*RelImp*/, /*cTpComp*/, /*lInsere*/, /*cAliasP*/, cProgram)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadOrderTaxes
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadOrderTaxes() class ForecastOfComissionSmartViewBusinessObject
    local cQuery := "" as character
    local nOriginalRecnoSD1 := 0 as numeric
    local nOriginalSD1Doc := "" as character
    local nOriginalSD1Serie := "" as character
    local cAlias := "" as character
    local nParameterOrder := 1 as numeric
    local jItemTaxes := JSONObject():new() as json
    local cRecnoSC6 := "" as character
    local nItem := 1 as numeric
    local nFrete, nDespesa, nSeguro, nFreteAutonomo, nEmbalagem as numeric

    nFrete := nDespesa := nSeguro := nFreteAutonomo := nEmbalagem := 0    
    
    self:jOrderTaxes := JSONObject():new()

    self:callMaFisIni()

    if self:oExecOrderItems == NIL
        cQuery := " SELECT SC6.R_E_C_N_O_ SC6RECNO, SC6.C6_PRODUTO, SC6.C6_TES, SC6.C6_QTDVEN, SC6.C6_PRCVEN, SC6.C6_VALOR, "
        cQuery += " SC6.C6_NFORI, SC6.C6_SERIORI, SC6.C6_ITEMORI, "
        cQuery += " SC5.C5_CLIENTE, SC5.C5_LOJACLI, "        
        cQuery += " SF4.F4_ISS "
        
        cQuery += " FROM ? SC6 "
        
        cQuery += " JOIN ? SC5 "
        cQuery += " ON ? "
        cQuery += " AND SC6.C6_NUM = SC5.C5_NUM "
        cQuery += " AND SC5.D_E_L_E_T_ = ? "
        
        cQuery += " LEFT JOIN ? SF4 "
        cQuery += " ON ? "
        cQuery += " AND SC6.C6_TES = SF4.F4_CODIGO "
        cQuery += " AND SF4.D_E_L_E_T_ = ? "        
        
        cQuery += " WHERE C6_FILIAL = ? "
        cQuery += " AND SC6.C6_NUM = ? "
        cQuery += " AND SC6.D_E_L_E_T_ = ? "

        cQuery := changeQuery(cQuery)

        self:oExecOrderItems := FWExecStatement():new(cQuery)
    endIf

    self:oExecOrderItems:setUnsafe(nParameterOrder++, retSQLName("SC6"))
    self:oExecOrderItems:setUnsafe(nParameterOrder++, retSQLName("SC5"))
    self:oExecOrderItems:setUnsafe(nParameterOrder++, FwJoinFilial("SC6", "SC5"))
    self:oExecOrderItems:setString(nParameterOrder++, " ")
    self:oExecOrderItems:setUnsafe(nParameterOrder++, retSQLName("SF4"))
    self:oExecOrderItems:setUnsafe(nParameterOrder++, FwJoinFilial("SC6", "SF4"))
    self:oExecOrderItems:setString(nParameterOrder++, " ")
    self:oExecOrderItems:setString(nParameterOrder++, (self:cAlias)->C6_FILIAL)
    self:oExecOrderItems:setString(nParameterOrder++, (self:cAlias)->C6_NUM)
    self:oExecOrderItems:setString(nParameterOrder++, " ")

    cAlias := self:oExecOrderItems:openAlias()

    while !(cAlias)->(eof())

        nOriginalRecnoSD1 := self:getOriginalRecnoSD1(cAlias)

        MaFisAdd((cAlias)->C6_PRODUTO, (cAlias)->C6_TES, (cAlias)->C6_QTDVEN, (cAlias)->C6_PRCVEN, a410Arred((cAlias)->C6_QTDVEN*(cAlias)->C6_PRCVEN,"D2_DESCON")-(cAlias)->C6_VALOR,;
            nOriginalSD1Doc, nOriginalSD1Serie, nOriginalRecnoSD1, ;
            nFrete, nDespesa, nSeguro, nFreteAutonomo, (cAlias)->C6_VALOR , nEmbalagem;
        )
            
        (cAlias)->(dbSkip())

    endDo

    (cAlias)->(dbGoTop())
    nItem := 1

    //To-do: Precisa mesmo montar todo o pedido com MaFisAdd para só depois chamar os MaFisRet?
    while !(cAlias)->(eof())

        jItemTaxes["nValueIRRF"] := MaFisRet(nItem, "IT_VALIRR") 
        jItemTaxes["nValueINSS"] := MaFisRet(nItem, "IT_VALINS")
        jItemTaxes["nValuePIS"] := MaFisRet(nItem, "IT_VALPIS")
        jItemTaxes["nValueCOFINS"] := MaFisRet(nItem, "IT_VALCOF")
        jItemTaxes["nValueCSLL"] := MaFisRet(nItem, "IT_VALCSL")
        jItemTaxes["nValueISS"] := 0
        jItemTaxes["nValueICMS"] := 0
    
        If (cAlias)->F4_ISS == "S"
            jItemTaxes["nValueISS"] := MaFisRet(nItem, "IT_VALISS")
        Else
            jItemTaxes["nValueICMS"] := MaFisRet(nItem, "IT_VALICM")
        EndIf
        
        cRecnoSC6 := cValToChar((cAlias)->SC6RECNO)
        self:jOrderTaxes[cRecnoSC6] := jItemTaxes

        nItem++
        (cAlias)->(dbSkip())

    endDo

    (cAlias)->(dbCloseArea())
return

//-------------------------------------------------------------------
/*/{Protheus.doc} aInstallments
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getInstallments() class ForecastOfComissionSmartViewBusinessObject
    local aInstallments := {} as array
    local nValue := 1

    aInstallments := condicao(nValue, (self:cAlias)->C5_CONDPAG, /*nVIPI*/, (self:cAlias)->C5_EMISSAO, /*nVSol*/)

return aInstallments

//-------------------------------------------------------------------
/*/{Protheus.doc} loadParametersOfComission
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParametersOfComission() class ForecastOfComissionSmartViewBusinessObject
    local lHelp := .T. as logical
    local cDefault := "S" as character

    self:jParametersOnComission := JSONObject():new()

    self:jParametersOnComission["lIRRF"] := SuperGetMv("MV_COMISIR", lHelp, cDefault) != "N"
    self:jParametersOnComission["lINSS"] := SuperGetMv("MV_COMIINS", lHelp, cDefault) != "N"
    self:jParametersOnComission["lPIS"] := SuperGetMv("MV_COMIPIS", lHelp, cDefault) != "N"
    self:jParametersOnComission["lCOFINS"] := SuperGetMv("MV_COMICOF", lHelp, cDefault) != "N"
    self:jParametersOnComission["lCSLL"] := SuperGetMv("MV_COMICSL", lHelp, cDefault) != "N"
return

//-------------------------------------------------------------------
/*/{Protheus.doc} subtractTaxes
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method subtractTaxes(nBaseValue as numeric) as numeric class ForecastOfComissionSmartViewBusinessObject
    local cItemRecno := cValToChar((self:cAlias)->SC6RECNO)
    
    default nBaseValue := 0

    if !self:jParametersOnComission["lIRRF"]
        nBaseValue -= self:jOrderTaxes[cItemRecno]["nValueIRRF"]
    endIf
    if !self:jParametersOnComission["lINSS"]
        nBaseValue -= self:jOrderTaxes[cItemRecno]["nValueINSS"]
    endIf
    if !self:jParametersOnComission["lPIS"]
        nBaseValue -= self:jOrderTaxes[cItemRecno]["nValuePIS"]
    endIf
    if !self:jParametersOnComission["lCOFINS"]
        nBaseValue -= self:jOrderTaxes[cItemRecno]["nValueCOFINS"]
    endIf
    if !self:jParametersOnComission["lCSLL"]
        nBaseValue -= self:jOrderTaxes[cItemRecno]["nValueCSLL"]
    endIf
    If ( (self:cAlias)->A3_ICM == "N" )
        nBaseValue -= self:jOrderTaxes[cItemRecno]["nValueICMS"]
    EndIf
    If ( (self:cAlias)->A3_ISS == "N" )
        nBaseValue -= self:jOrderTaxes[cItemRecno]["nValueISS"]
    EndIf  

return nBaseValue

//-------------------------------------------------------------------
/*/{Protheus.doc} getOriginalRecnoSD1
    @author guilherme.sordi@totvs.com.br
    @since 07/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getOriginalRecnoSD1(cAlias as character) as numeric class ForecastOfComissionSmartViewBusinessObject
    local cQuery := "" as character
    local nParameter := 1 as numeric
    local nRecno := 0

    if Empty((cAlias)->C6_NFORI) .or. Empty((cAlias)->C6_ITEMORI)
        return nRecno
    endIf

    if self:oExecSD1 == NIL

        cQuery := "SELECT R_E_C_N_O_ RECNO "
        cQuery += "FROM ? SD1 "
        cQuery += "WHERE D1_FILIAL IN ( ? ) "
        cQuery += "AND D1_DOC = ? "
        cQuery += "AND D1_SERIE = ? "
        cQuery += "AND D1_FORNECE = ? "
        cQuery += "AND D1_LOJA = ? "
        cQuery += "AND D1_COD = ? "
        cQuery += "AND D1_ITEM = ? "
        cQuery += "AND D_E_L_E_T_ = ? "

        cQuery := changeQuery(cQuery)

        self:oExecSD1 := FWExecStatement():new(cQuery)
    endIf

    self:oExecSD1:setUnsafe(nParameter++, retSQLName("SD1"))
    self:oExecSD1:setIn(nParameter++, self:getAliasBranchFilter('SD1'))
    self:oExecSD1:setString(nParameter++, (cAlias)->C6_NFORI)
    self:oExecSD1:setString(nParameter++, (cAlias)->C6_SERIORI)
    self:oExecSD1:setString(nParameter++, (cAlias)->C5_CLIENTE)
    self:oExecSD1:setString(nParameter++, (cAlias)->C5_LOJACLI)
    self:oExecSD1:setString(nParameter++, (cAlias)->C6_PRODUTO)
    self:oExecSD1:setString(nParameter++, (cAlias)->C6_ITEMORI)
    self:oExecSD1:setString(nParameter++, " ")

    nRecno := self:oExecSD1:ExecScalar("RECNO")

return nRecno

//-------------------------------------------------------------------
/*/{Protheus.doc} getCommissionValueIssue
    @author guilhermed.santos
    @since 28/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getCommissionValueIssue(cSalesPersonCode as character) as numeric class ForecastOfComissionSmartViewBusinessObject
    local cQuery := "" as character
    local nParameter := 1 as numeric
    local nCommissionValue := 0

    if self:oExecSE3 == NIL

        cQuery := "SELECT SE3.E3_COMIS COMMISSION "
        cQuery += "FROM ? SE3 "
        cQuery += "WHERE SE3.E3_FILIAL IN ( ? ) "
        cQuery += " AND SE3.E3_NUM = ?
        cQuery += " AND SE3.E3_PARCELA = ?
        cQuery += " AND SE3.E3_TIPO = ?
        cQuery += " AND SE3.E3_CODCLI = ?
        cQuery += " AND SE3.E3_LOJA = ?
        cQuery += " AND SE3.E3_VEND = ? "
        cQuery += " AND E3_BAIEMI = ? "
        cQuery += " AND SE3.D_E_L_E_T_ = ? "

        cQuery := changeQuery(cQuery)

        self:oExecSE3 := FWExecStatement():new(cQuery)
    endIf

    self:oExecSE3:setUnsafe(nParameter++, retSQLName("SE3"))
    self:oExecSE3:setIn(nParameter++, self:getAliasBranchFilter('SE3'))
    self:oExecSE3:setString(nParameter++, (self:cAlias)->E1_NUM)
    self:oExecSE3:setString(nParameter++, (self:cAlias)->E1_PARCELA)
    self:oExecSE3:setString(nParameter++, (self:cAlias)->E1_TIPO)
    self:oExecSE3:setString(nParameter++, (self:cAlias)->E1_CLIENTE)
    self:oExecSE3:setString(nParameter++, (self:cAlias)->E1_LOJA)
    self:oExecSE3:setString(nParameter++, cSalesPersonCode)
    self:oExecSE3:setString(nParameter++, "E")
    self:oExecSE3:setString(nParameter++, " ")

    nCommissionValue := self:oExecSE3:ExecScalar("COMMISSION")

return nCommissionValue
