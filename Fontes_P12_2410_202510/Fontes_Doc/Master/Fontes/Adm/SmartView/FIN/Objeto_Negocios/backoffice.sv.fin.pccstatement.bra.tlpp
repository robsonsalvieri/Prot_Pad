#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include 'backoffice.sv.fin.billspayablewithholdingtaxes.bra.ch'

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider



@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE2,SA2",; 
name="Demonstrativo PCC", country="BRA", customTables="SE2,SA2")

//-------------------------------------------------------------------
    /*/{Protheus.doc} PCCStatementSmartViewBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 01/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
class PCCStatementSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.BillsPayableWithholdingTaxesSmartViewBusinessObject
    public method new() as object
    protected method loadParameters()
    protected method getWhere() as character

    protected method initializeBusinessObject()
    protected method handleStatement()
    protected method handleSchema()
    protected method handleData()
endClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
    @author guilherme.sordi@totvs.com.br
    @since 22/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method new() class PCCStatementSmartViewBusinessObject
    _Super:new()
return self

//-------------------------------------------------------------------
    /*/{Protheus.doc} initializeBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 01/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class PCCStatementSmartViewBusinessObject
    self:setDisplayName(STR0021) //"Demonstrativo PCC"
    self:setDescription(STR0021) //"Demonstrativo PCC"
    self:setPergunte("FINT021")
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters
    @author guilherme.sordi@totvs.com.br
    @since 22/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class PCCStatementSmartViewBusinessObject
    if self:oCurrentFilter != NIL
        self:cSupplierFrom := MV_PAR01
        self:cSupplierTo := MV_PAR02
        self:cSupplierUnitFrom := MV_PAR03
        self:cSupplierUnitTo := MV_PAR04
        self:dPCCFrom := MV_PAR05
        self:dPCCTo := MV_PAR06
    endIf
return

//-------------------------------------------------------------------
/*{Protheus.doc} getWhere
    @author guilherme.sordi@totvs.com.br
    @since 22/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getWhere() as character class PCCStatementSmartViewBusinessObject
    local cWhere := "" as character
    
    if self:lPCCBaixa
        cWhere += " AND VW_FK4.SE2_RECNO IS NOT NULL "
        cWhere += " AND (SELECT COUNT(*) "
        cWhere += " FROM " + RetSQLName("FK3") + " FK3 "
        cWhere += " LEFT JOIN VALID_FK2 FK2COUNT "
        cWhere += " 	ON FK3.FK3_TABORI = ? "
        cWhere += " 	AND FK3.FK3_IDORIG = FK2COUNT.FK2_IDFK2 "
        cWhere += " LEFT JOIN " + RetSQLName("FK7") + " FK7COUNT "
        cWhere += " 	ON FK3.FK3_TABORI = ? "
        cWhere += " 	AND FK3.FK3_IDORIG = FK7COUNT.FK7_IDDOC "
        cWhere += " 	AND FK7COUNT.D_E_L_E_T_ = ?  "
        cWhere += " WHERE "
        if self:lEmptyMultiBranches
            cWhere += " FK3.FK3_FILIAL = ? "
        else
            cWhere += " FK3.FK3_FILIAL IN (?) "
        endIf
        cWhere += " AND FK3.FK3_STATUS = ? "
        cWhere += " AND FK3.D_E_L_E_T_ = ? "
        cWhere += " AND FK3.FK3_IMPOS IN (?) "
        cWhere += " AND (FK7COUNT.FK7_IDDOC = VW_FK4.FK7_IDDOC OR FK2COUNT.FK2_IDDOC = VW_FK4.FK7_IDDOC) "
        
        cWhere += " AND FK3.FK3_DATA BETWEEN ? AND ? ) > ? "
    else
        cWhere += " AND ( SE2.E2_PIS > ? OR SE2.E2_COFINS > ? OR SE2.E2_CSLL > ? )"
        cWhere += " AND ( SE2.E2_EMISSAO BETWEEN ? AND ? ) "
    endIf
return cWhere

//-------------------------------------------------------------------
/*{Protheus.doc} handleStatement
    @author guilherme.sordi@totvs.com.br
    @since 22/05/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleStatement() class PCCStatementSmartViewBusinessObject    
    if self:lPCCBaixa
        self:oStatement:setString(self:nParamOrder++, "FK2")
        self:oStatement:setString(self:nParamOrder++, "FK7")
        self:oStatement:setString(self:nParamOrder++, " ")
        if self:lEmptyMultiBranches
            self:oStatement:SetString(self:nParamOrder++, self:oFilterBranches["FK3"][1])
        else
            self:oStatement:SetIn(self:nParamOrder++, self:oFilterBranches["FK3"][1])
        endIf
        self:oStatement:setNumeric(self:nParamOrder++, 1)
        self:oStatement:setString(self:nParamOrder++, " ")
        self:oStatement:setIn(self:nParamOrder++, {'PIS','COF','CSL'})
        self:oStatement:setDate(self:nParamOrder++, self:dPCCFrom)
        self:oStatement:setDate(self:nParamOrder++, self:dPCCTo)
        self:oStatement:SetNumeric(self:nParamOrder++, 0)
    else
        self:oStatement:SetNumeric(self:nParamOrder++, 0)
        self:oStatement:SetNumeric(self:nParamOrder++, 0)
        self:oStatement:SetNumeric(self:nParamOrder++, 0)
        self:oStatement:setDate(self:nParamOrder++, self:dPCCFrom)
        self:oStatement:setDate(self:nParamOrder++, self:dPCCTo)
    endIf
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleSchema
    @author guilherme.sordi@totvs.com.br
    @since 06/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleSchema() class PCCStatementSmartViewBusinessObject
    self:aliasToSchema("SE2", "E2_BASEPIS")
    self:finAddProperty("RET_PCC", STR0022, "number") //Total PCC retido
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    @author guilherme.sordi@totvs.com.br
    @since 06/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleData() class PCCStatementSmartViewBusinessObject
    self:jData["RET_PCC"] := (self:cAlias)->RET_PIS + (self:cAlias)->RET_COF + (self:cAlias)->RET_CSL
    self:jData["E2_BASEPIS"] := xMoeda( (self:cAlias)->E2_BASEPIS, (self:cAlias)->E2_MOEDA, 1, (self:cAlias)->E2_EMISSAO, self:nDecimals, (self:cAlias)->E2_TXMOEDA)
return
