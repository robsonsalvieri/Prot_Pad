#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.listofposts.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE1,SE2,FK1,FK2,FK5,F7G,SED", name="Relação de Baixas por Lote", country="ALL")
//-------------------------------------------------------------------
/*/{Protheus.doc} PostRelationByLotSmartViewBusinessObject
    @author fabioh.andrade@totvs.com.br
    @since 17/10/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
class PostRelationByLotSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.ListOfPostsSmartViewBusinessObject
    public method new() as object 

    protected data cLotFrom as character
    protected data cLotTo as character
    protected data cPortfolio as character

    protected method handleData()
    protected method handleSchema()
    protected method completeStatementPayable()
    protected method completeStatementReceivable()
    protected method loadParameters() 
    protected method loadWheres()
    protected method initializeBusinessObject()

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
    Construtor
    @author fabioh.andrade@totvs.com.br
    @since 17/10/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method new() class PostRelationByLotSmartViewBusinessObject
    _Super:new()

    self:lDistinguirJuros := .T.
    self:lFiltrarDatas    := .F.

    self:cSelectRec := ", FK1_LOTE LOTE "
    self:cSelectPay := ", FK2_LOTE LOTE "

    
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject

@author Fábio Andrade
@since 08/01/2024
@version 1.0
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class PostRelationByLotSmartViewBusinessObject
    self:setDisplayName(STR0021) //"Relação de Baixas por Lote"
    self:setDescription(STR0021) //"Relação de Baixas por Lote"
    self:setPergunte("FINT018")
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleSchema
    Complementa a estrutura dos campos e conjunto de parametros
    @author fabioh.andrade@totvs.com.br
    @since 17/10/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleSchema() class PostRelationByLotSmartViewBusinessObject    
    self:finAddProperty("LOTE",        STR0022, "string") //"Nro. Lote"
    self:finAddProperty("CARTEIRA",    STR0023, "string") //"Carteira"
    self:finAddProperty("JUROS",       STR0017, "number") //"Juros"
    self:finAddProperty("MULTA",       STR0018, "number") //"Multa"
    self:finAddProperty("JUROSMULTA",  STR0019, "number") //"Juros + multa"
    self:finAddProperty("DESCONTO",    STR0024, "number") //"Descontos"
    self:finAddProperty("ATRASO",      STR0020, "number") //"Atraso"
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    Complementa os dados retornados pelo objeto de negócio
    @author fabioh.andrade@totvs.com.br
    @since 17/10/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData() class PostRelationByLotSmartViewBusinessObject
    local nDelay := 0 as numeric
    local dActualDueDate := dDatabase as date
    local dDueDate := dDatabase as date
    local dWriteOff := dDatabase as date

    _Super:handleData()

    self:jData['LOTE']  := (self:cAlias)->LOTE
    self:jData['JUROS'] := self:converteMoedaMovimento( (self:cAlias)->JUROS )
    self:jData['DESCONTO'] := self:converteMoedaMovimento( (self:cAlias)->DESCONTO )
    self:jData['MULTA'] := self:converteMoedaMovimento( (self:cAlias)->MULTA )
    self:jData['JUROSMULTA'] := self:converteMoedaMovimento( (self:cAlias)->JUROS + (self:cAlias)->MULTA )
    self:jData['CARTEIRA'] := self:cPortfolio
    
    if self:lPagar
        dActualDueDate := (self:cAlias)->E2_VENCREA
        dDueDate := (self:cAlias)->E2_VENCTO
        dWriteOff := (self:cAlias)->FK2_DATA
    else
        dActualDueDate := (self:cAlias)->E1_VENCREA
        dDueDate := (self:cAlias)->E1_VENCTO
        dWriteOff := (self:cAlias)->FK1_DATA
    endIf

    if dDatabase > dActualDueDate
        nDelay := dateDiffDay(dDueDate, dWriteOff)
    endIf

    self:jData['ATRASO'] := nDelay

return 

//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters
    Definição de parâmetros do objeto de negócio
    @author fabioh.andrade@totvs.com.br
    @since 17/10/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method loadParameters() class PostRelationByLotSmartViewBusinessObject    
    local aPortfolios := {STR0025,STR0026} //"Pagar" # "Receber"
    if self:oCurrentFilter != NIL
       self:lPagar :=  MV_PAR01 == 1
       self:cLotFrom := MV_PAR02
       self:cLotTo := MV_PAR03
       self:nMoeda := MV_PAR04
       self:lListarOutrasMoedas := MV_PAR05 == 1
       self:cPortfolio := aPortfolios[MV_PAR01]
    endIf
    self:loadWheres()
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadWheres
    Carrega as strings complementares do where das queries de baixas a pagar e receber com a regra do filtro de lotes
    @author fabioh.andrade@totvs.com.br
    @since 18/10/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method loadWheres() class PostRelationByLotSmartViewBusinessObject
    if self:lPagar
        self:cWherePay := " AND FK2_LOTE BETWEEN ? AND ? "
    else
        self:cWhereRec := " AND FK1_LOTE BETWEEN ? AND ? "
    endIf
return


//-------------------------------------------------------------------
/*{Protheus.doc} completeStatementPayable()
    Complementa os parametros dos fontes filhos.
    @author Matheus Monteiro da Silva
    @since 27/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method completeStatementPayable() class PostRelationByLotSmartViewBusinessObject
    self:oStatement:SetString(self:nOrdem++, self:cLotFrom)
    self:oStatement:SetString(self:nOrdem++, self:cLotTo)
return

//-------------------------------------------------------------------
/*{Protheus.doc} completeStatementReceivable()
    Complementa os parametros dos fontes filhos.
    @author Matheus Monteiro da Silva
    @since 27/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method completeStatementReceivable() class PostRelationByLotSmartViewBusinessObject
    self:oStatement:SetString(self:nOrdem++, self:cLotFrom)
    self:oStatement:SetString(self:nOrdem++, self:cLotTo)
return


