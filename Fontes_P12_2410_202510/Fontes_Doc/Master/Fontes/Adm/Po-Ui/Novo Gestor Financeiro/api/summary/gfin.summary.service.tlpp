#include 'tlpp-core.th'
#include "gfin.summary.service.ch"

NameSpace gfin.api.summary
Using NameSpace gfin.util

Class GfinSummaryService
  Data lOk      As Logical
  Public Method new()
  Public Method getData()
  Public Method getDataTables()
EndClass

Method new() Class GfinSummaryService
  ::lOk := .T.
Return Self

//-------------------------------------------------------------------
/*/{Protheus.doc} getData
retorna os Dados da Tabela passada como parâmetro

@Input
    tableNick As Character
    queryRequestParams As Json, parâmetros recebidos

@Return response As Json, retorno da lista de dados

@author Luiz Nai
@since Maio | 2021
/*/
//-------------------------------------------------------------------
Method getData(queryRequestParams As Json, headers As Json) Class GfinSummaryService
  Local response       As Json

  response    := JsonObject():New()

  If(Empty(headers['table']))
    ::lOk := .F.
  EndIf

  If ::lOk
    response["result"] := .T.
    response["response"] := prepareQuery(headers)
  Else
    response := answerErrorFormat(403, STR0001, STR0002)
  EndIf

Return response

/*/{Protheus.doc} getDataTables
  Realiza a contagem dos registros em uma determinada tabela utilizando os recursos disponiveis 
  na classe de listagem gfin.api.tables.GfinTablesService
  @author Vitor Duca
  @since 09/10/2023
  @version 1.0
  @param cTable, Character, Prefixo da tabela que sera considerada
  @param jHeaders, Json, Header Request da API
  @return jResponse, Json, Resposta que sera enviada para a API
/*/
Method getDataTables(cTable As Character, jHeaders As Json) Class GfinSummaryService
  Local oServiceTables := gfin.api.tables.GfinTablesService():new() As Object
  Local jHeaderRequest := JsonObject():new() As Json
  Local jParamsRequest := JsonObject():new() As Json
  Local jResponse      := JsonObject():new() As Json
  Local jResult        := JsonObject():new() As Json
  
  If jHeaders:hasProperty("branches")
    jHeaderRequest["branches"] := jHeaders["branches"]
  Endif

  jParamsRequest["page"] := '1'
  jParamsRequest["pagesize"] := '1'
  jParamsRequest["fields"] := jHeaders['fields']

  If jHeaders:hasProperty("tablebills")
    jParamsRequest["tableBills"] := jHeaders["tablebills"]
  Endif

  If jHeaders:hasProperty("filter")
    jParamsRequest["filter"] := jHeaders["filter"]
  Endif

  jResult := oServiceTables:getData(cTable, jParamsRequest, jHeaderRequest)

  jResponse["result"] := .T.
  jResponse["response"] := JsonObject():new()
  jResponse["response"]["total_rows"] := 0

  If jResult["result"]
    jResponse["response"]["total_rows"] := jResult['response']['remainingRecords'] + len(jResult['response']['items'])
  Endif
  
  jResponse["response"]["field_rows"] := jHeaders['fields']

Return jResponse
