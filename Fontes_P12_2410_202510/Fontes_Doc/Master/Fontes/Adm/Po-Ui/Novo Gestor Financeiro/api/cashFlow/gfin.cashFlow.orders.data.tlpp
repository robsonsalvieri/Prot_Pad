#INCLUDE 'tlpp-core.th'

NameSpace gfin.api.cashFlow
Using NameSpace gfin.util

Static __cCampoChq    As Character

//---------------------------------------------------------------------
/*/{Protheus.doc }getOrdersFlowQuery
retorna os saldos de pedidos no periodo definido

@param queryFilter  , character, filtro da query 
@param cJoin        , character, join da query
@param cCustomSearch, character, filtro personalizado
@param lSales,        logical,   indica consulta ao pedido de compras (.F.)/ ou vendas (.T.)

@author Alison Kaique
@since mar|2021
/*/
//---------------------------------------------------------------------
Function getOrdersFlowQuery(queryFilter as Character, cJoin As Character, cCustomSearch As Character, lSales As Logical)
  Local aliasResult  as Character
  Local retValues    as Array
  Local queryGroup   as Character
  Local queryAdvance as Character
  Local cIsNull      as Character
  Local nAdv         as Numeric

  cIsNull     := totvs.protheus.backoffice.fin.functions.FunctionToDb("ISNULL")
  aliasResult := ''

  If Empty(__cCampoChq) 
    __cCampoChq := Padr("", TamSX3("E5_NUMCHEQ")[1])
  EndIf
  // agrupa os valores por data
  queryGroup := "SELECT F76_DATA, SUM(F76_VALOR) VALUE, COUNT(F76_DATA) QUANTITY"
  queryGroup += " FROM " + RetSqlName('F76') + " F76"

  If !(Empty(cJoin))
    queryGroup += " " + cJoin
  EndIf
  queryGroup += " WHERE F76.D_E_L_E_T_ = ' ' " + queryFilter

  If !(Empty(cCustomSearch))
    queryGroup += " AND " + cCustomSearch
  EndIf

  queryGroup += " GROUP BY F76_DATA "
  queryGroup += " ORDER BY F76_DATA"

  aliasResult := MpSysOpenQuery(ChangeQuery(queryGroup))

  queryAdvance := "SELECT " + cIsNull + "(SUM(FIE_VALOR), 0) ADVANCES FROM ("
  queryAdvance += " SELECT F76_PEDIDO, "
  queryAdvance += " CASE WHEN FIE_VALOR > F76_VALOR THEN F76_VALOR ELSE FIE_VALOR END AS FIE_VALOR "
  queryAdvance += " FROM " + RetSqlName("FIE") + " FIE "
  queryAdvance += " LEFT JOIN " + RetSqlName("F76") + " F76A "
  queryAdvance += " ON " + totvs.protheus.backoffice.ngf.util.BranchRelation( {"FIE", "FIE_FILIAL", "FIE"}, {'F76', "F76_FILIAL", "F76A"} )
  queryAdvance += " AND FIE.FIE_PEDIDO = F76A.F76_PEDIDO "
  queryAdvance += " AND FIE.D_E_L_E_T_ =' ' "
  If !lSales
    queryAdvance += " LEFT JOIN " + RetSqlName("SE2") + " SE2 "
    queryAdvance += " ON " + totvs.protheus.backoffice.ngf.util.BranchRelation( {"FIE", "FIE_FILIAL", "FIE"}, {'SE2', "E2_FILIAL", "SE2"} )
    queryAdvance += " AND E2_PREFIXO = FIE_PREFIX AND E2_NUM = FIE_NUM "
    queryAdvance += " AND E2_PARCELA = FIE_PARCEL AND E2_TIPO = FIE_TIPO "
    queryAdvance += " AND E2_FORNECE = FIE_FORNEC"
    queryAdvance += " AND E2_LOJA = FIE_LOJA"
    queryAdvance += " AND SE2.D_E_L_E_T_ =' '"
    
    queryAdvance += " LEFT JOIN "+ RetSqlName("FK7") + " FK7 "
    queryAdvance += " ON " + totvs.protheus.backoffice.ngf.util.BranchRelation( {"SE2", "E2_FILIAL", "SE2"}, {'FK7', "FK7_FILTIT", "FK7"} )
    queryAdvance += " AND SE2.E2_PREFIXO = FK7.FK7_PREFIX "
    queryAdvance += " AND SE2.E2_NUM = FK7.FK7_NUM "
    queryAdvance += " AND SE2.E2_PARCELA = FK7.FK7_PARCEL "
    queryAdvance += " AND SE2.E2_TIPO = FK7.FK7_TIPO "
    queryAdvance += " AND SE2.E2_FORNECE = FK7.FK7_CLIFOR "
    queryAdvance += " AND SE2.E2_LOJA = FK7.FK7_LOJA "
    queryAdvance += " AND FK7.D_E_L_E_T_ = ' ' "
  
    queryAdvance += " LEFT JOIN " + RetSqlName("FK5") + " FK5"
    queryAdvance += " ON " + totvs.protheus.backoffice.ngf.util.BranchRelation( {"FK7", "FK7_FILIAL", "FK7"}, {'FK5', "FK5_FILIAL", "FK5"} )
    
    queryAdvance += " AND FK7_IDDOC = FK5_IDDOC "
    queryAdvance += " AND ((FK5_TPDOC = 'PA' ) OR (FK5_TPDOC = 'BA' AND FK5_NUMCH <> '" + __cCampoChq + "' ) "
    queryAdvance += " OR (FK5_TPDOC = 'CH' AND FK5_NUMCH <> '" + __cCampoChq + "')) "
    queryAdvance += " AND FK5_RECPAG = 'P' "
    queryAdvance += " AND FK5.D_E_L_E_T_ =' '"
  EndIf
	queryAdvance += " WHERE F76A.D_E_L_E_T_ = ' ' "
  queryAdvance += queryFilter
  queryAdvance += " AND F76_DATA = ?"
  If lSales
    queryAdvance += " AND F76_TIPO = '1' and FIE_CART = 'R'"
  Else
    queryAdvance += " AND F76_TIPO = '2' and FIE_CART = 'P'"
  EndIf
  queryAdvance += " AND F76A.F76_SEQ = "
  queryAdvance += " ( SELECT MIN(F76PARC.F76_SEQ) FROM " + RetSqlName("F76") + " F76PARC " 
  queryAdvance += " WHERE " + totvs.protheus.backoffice.ngf.util.BranchRelation( {"FIE", "FIE_FILIAL", "FIE"}, {'F76', "F76_FILIAL", "F76PARC"} )"
  queryAdvance += " AND F76PARC.F76_PEDIDO = FIE_PEDIDO and F76PARC.D_E_L_E_T_ = ' ' ) "
  queryAdvance  += ") A "

  retValues := {}

  While (aliasResult)->(!EOF())
    nAdv := MPSysExecScalar(ChangeQuery(queryAdvance), "ADVANCES", {(aliasResult)->F76_DATA})
    AAdd(retValues, {;
      {"date", stringDateToIso((aliasResult)->F76_DATA)},;
      {"value",(aliasResult)->VALUE},;
      {"quantity",(aliasResult)->QUANTITY},;
      {"advances", nAdv};
    })
    (aliasResult)->(dbSkip())
  EndDo
  (aliasResult)->(dbCloseArea())

Return arrayToJson(retValues)
