

#include 'tlpp-core.th'

NAMESPACE totvs.protheus.backoffice.fin.attachment

Static __instance As Object

/*/{Protheus.doc} Attachments
	Classe para obter anexos do banco de conhecimento.

	@author Vinicius do Prado
	@since Out | 2023
	@version 1.0
/*/
Class Attachments
	Private Data cDirDocs 	As Character  // Pasta do Banco de Conhecimentos.
	Private Data cKey     	As character  // Chave do registro que contém os anexos.
	Private Data cTable    	As character  // Tabela do registro que contém os anexos.
	Private Data cFileName  As character  // Nome customizado para o arquivo zip.
	Private Data cSeparator As character  // separador conforme o sistema operacional
	Private Data cFilEnt    As character  // filial da entidade
	Private Data cQryAC9    As character  // query AC9

	Public Method new() CONSTRUCTOR
	Static Method getInstance()
	Public Method destroy()
	Public Method reset()

	// Metodos de utilizacao da Classe
	Public Method setKey(cKey)
	Public Method setBranch(cBranch)
	Public Method setTable(cTable)
	Public Method setFileName(cFileName)
	Public Method getAttachmentList() As Array
	Public Method getAttachmentZip()  As Character

	// Metodos de utilizacao da API
	Public Method setBody(cBody)
	Public Method getResponseList() As Json
	Public Method getResponseZip()  As Json

    Private Method getAttachmentData() As Array
EndClass

/*/{Protheus.doc} new
	Construtor da classe.

	@author Vinicius do Prado
	@since Out | 2023
	@version 1.0
/*/
Method new() Class Attachments
	::cFilEnt    := ""
	::cKey       := ""
	::cTable     := ""
	::cQryAC9    := ""
	::cSeparator := "/"
	If !IsSrvUnix()
		::cSeparator := "\"
	Endif
	::cDirDocs := Lower(Alltrim(MsDocPath())) + ::cSeparator
Return Self

/*/{Protheus.doc} getInstance
	Retorna intancia unica da classe.

	@author Vinicius do Prado
	@since Out | 2023
	@version 1.0
/*/
Method getInstance() Class Attachments
	If __instance == Nil
		__instance := Attachments():new()
	EndIf
Return __instance

/*/{Protheus.doc} destroy
	Destroy a classe.

	@author Vinicius do Prado
	@since Out | 2023
	@version 1.0
/*/
Method destroy() class Attachments
	If ValType(__instance) <> "U"
		FreeObj(__instance)
	EndIf
Return

/*/{Protheus.doc} reset
	reseta as variaveis da classe.

	@author Vinicius do Prado
	@since Out | 2023
	@version 1.0
/*/
Method reset() class Attachments
	::cKey      := ""
	::cTable    := ""
	::cFileName := "anexos"
Return

/*/{Protheus.doc} setKey
	Realiza a atribuição para a propriedade Key da classe.

	@author Vinicius do Prado
	@since Out | 2023
	@version 1.0
/*/
Method setKey(cKey As Character) Class Attachments
	::cKey := cKey
Return

/*/{Protheus.doc} setBranch
	Atualiza a filial da entidade

	@author Vinicius do Prado
	@since Out | 2023
	@version 1.0
/*/
Method setBranch(cBranch As Character) Class Attachments
	::cFilEnt := cBranch
Return

/*/{Protheus.doc} setTable
	Realiza a atribuição para a propriedade Table da classe.

	@author Vinicius do Prado
	@since Out | 2023
	@version 1.0
/*/
Method setTable(cTable As Character) Class Attachments
	::cTable := cTable
Return

/*/{Protheus.doc} setFileName
	Realiza a atribuição para a propriedade cFileName da classe.

	@author Vinicius do Prado
	@since Out | 2023
	@version 1.0
/*/
Method setFileName(cFileName As Character) Class Attachments
	::cFileName := cFileName
Return

/*/{Protheus.doc} getAttachmentList
	Retorna um Array contendo uma lista de anexos em base64.

	@author Vinicius do Prado
	@since Out | 2023
	@version 1.0
	@return Array, retorna um array contendo uma lista de anexo em base64.
/*/
Method getAttachmentList() As Array Class Attachments
	Local oAttach     := NIL as Object
	Local cFile       := ""  as Character
	Local aAttachResp := {}  as Array
	Local aAttachList := {}  as Array
	Local nCount      := 1   as Numeric

	aAttachList := ::getAttachmentData()

	For nCount := 1 to Len(aAttachList)
		oAttach := FwFileReader():New(::cDirDocs + aAttachList[nCount]['file'])

		If (oAttach:Open())
			cFile := Encode64(oAttach:FullRead())
			Aadd(aAttachResp, {'filename': aAttachList[nCount]['file'], 'base64Data': cFile})
		Endif

		oAttach:Close()
	Next nCount

Return aAttachResp

/*/{Protheus.doc} getAttachmentZip
    Retorna um Character contendo os anexos em um zip formato base64.

	@author Vinicius do Prado
	@since Out | 2023
	@version 1.0
    @return Character, retorna um character contendo uma lista de anexo zipada em formato base64.
/*/
Method getAttachmentZip() As Character Class Attachments
	Local oAttach     := Nil As Object
	Local cNameZip    := ""  As Character
	Local cFile       := ""  As Character
	Local aAttachResp := {}  As Array
	Local aAttachList := {}  As Array
	Local nCount      := 1   As Numeric
	Local nZipArc     := -1  As Numeric

	aAttachList := ::getAttachmentData()

	For nCount := 1 to Len(aAttachList)
		oAttach := FwFileReader():New(::cDirDocs + aAttachList[nCount]['file'])

		If (oAttach:Open())
			Aadd(aAttachResp, aAttachList[nCount]['path'] + aAttachList[nCount]['file'])
		Endif

		oAttach:Close()
	Next nCount

	If Len(aAttachResp) > 0
		cNameZip := FWUUIDV4() + ".zip"
		nZipArc  := FZip(cNameZip, aAttachResp, ::cDirDocs)
	EndIf

	If nZipArc == 0
		oAttach := FwFileReader():New('system' + ::cSeparator + cNameZip)

		If (oAttach:Open())
			cFile := Encode64(oAttach:FullRead())
		Endif

		oAttach:Close()
		FErase('system' + ::cSeparator + cNameZip)
	Endif

Return cFile

/*/{Protheus.doc} setBody
	Configura as variaveis vindas por API.

	@author Vinicius do Prado
	@since Out | 2023
	@version 1.0
	@param Character, corpo da requisisão contendo as propriedades.
/*/
Method setBody(cBodyRequest As Character) Class Attachments
	Local jBody := JsonObject():new() As Json
	::reset()

	If Empty(jBody:fromJson(cBodyRequest))
		If jBody:hasProperty("key")
			::setKey(jBody['key'])
		EndIf

		If jBody:hasProperty("branch")
			::setBranch(jBody['branch'])
		EndIf

		If jBody:hasProperty("table")
			::setTable(jBody['table'])
		EndIf

		If jBody:hasProperty("filename")
			::setFileName(jBody['filename'])
		EndIf
	EndIf
Return

/*/{Protheus.doc} getResponseList
    Formata o retorno da API para o metodo getAttachmentList:
	{
		items: {
			filename: charcater,
			base64Data: charater
		}
	}

	@author Vinicius do Prado
	@since Out | 2023
	@version 1.0
    @return Json, retorna um json contendo uma lista de anexos.
/*/
Method getResponseList() As Json Class Attachments
	Local jResponse := JsonObject():new() As Json

	jResponse['items'] := ::getAttachmentList()

Return jResponse

/*/{Protheus.doc} getResponseZip
    Configura o retorno da API para o metodo getAttachmentZip:
	{
		items: {
			filename: charcater,
			base64Data: charater (zip)
		}
	}

	@author Vinicius do Prado
	@since Out | 2023
	@version 1.0
    @return Json, retorna um json contendo uma lista de anexo zipada.
/*/
Method getResponseZip() As Json Class Attachments
	Local jResponse := JsonObject():new() As Json
	Local cFile := "" As Character

	jResponse['items'] := {}
	cFile := ::getAttachmentZip()

	If !Empty(cFile)
		AAdd(jResponse['items'],{ "filename": self:cFileName + ".zip", "base64Data": cFile })
	EndIf

Return jResponse

/*/{Protheus.doc} getAttachmentData
    Retorna uma lista com os anexos do banco de conhecimento de um registro.

	@author Vinicius do Prado
	@since Out | 2023
	@version 1.0
    @return Array, lista contendo o caminho e nome dos anexos.
/*/
Method getAttachmentData() As Array Class Attachments
	Local cQuery 	   := "" As Character
	Local cAliasQuery  := "" As Character
	Local cDirDocs 	   := ::cDirDocs As Character
	Local aAttachments := {} As Array
	Local aBindParam   := {} As Array

	If Empty(::cQryAC9)
		cQuery := "SELECT ACB.ACB_OBJETO ANEXO "
		cQuery += "FROM " + RetSqlName("AC9") + " AC9 "
		cQuery += "INNER JOIN " + RetSqlName("ACB") + " ACB ON ACB.ACB_FILIAL = AC9.AC9_FILIAL "
		cQuery +=   "AND ACB.ACB_CODOBJ = AC9.AC9_CODOBJ "
		cQuery +=   "AND ACB.D_E_L_E_T_ = ' ' "
		cQuery += "WHERE AC9.AC9_FILENT = ? "
		cQuery += 	"AND AC9.AC9_ENTIDA = ? "
		cQuery += 	"AND AC9.AC9_CODENT = ? "
		cQuery += 	"AND AC9.D_E_L_E_T_ = ' ' "
		::cQryAC9 := changeQuery(cQuery)
	EndIf

	aAdd(aBindParam, ::cFilEnt)
	aAdd(aBindParam, ::cTable)
	aAdd(aBindParam, ::cKey)

	cAliasQuery := MPSysOpenQuery(::cQryAC9,,,,aBindParam)

	While (cAliasQuery)->(!Eof())
		Aadd(aAttachments, {'file': AllTrim((cAliasQuery)->ANEXO), 'path': cDirDocs})
		(cAliasQuery)->(DbSkip())
	EndDo

	(cAliasQuery)->(DbCloseArea())
Return aAttachments
