#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
Namespace portal.cliente.preload
Using Namespace gfin.util

//-------------------------------------------------------------------
/*/{Protheus.doc} getVersion
EndPoint para obter as versões dos fontes

@body
	{
		"portal.cliente.bills": Numeric - Versão do bills,
		"portal.cliente.customer": Numeric - Versão do customer,
		"portal.cliente.invoices": Numeric - Versão do invoices,
		"portal.cliente.home": Numeric - Versão do home,
	}

@return Nil

@author luiz.nai
@since 29/03/2022
/*/
//-------------------------------------------------------------------
@Get('/api/pc/v1/preload')
Function getPreload()

	Local jPreload       as Json
	Local jHeaders		 as Json
	Local oCustomService as Object
	Local lCustom := FindClass('totvs.backoffice.fin.pc.custom.PCCustomConfig') as Logical

	jHeaders := oRest:getHeaderRequest()

	jPreload := JsonObject():New()

	jPreload[ "result" ]	:= .T.
	jPreload[ "response" ] := JsonObject():New()  
	jPreload[ "response" ]["versions"] :=  JsonObject():New()
	jPreload[ "response" ]["versions"]['portal.cliente.bills'] := portal.cliente.bills.getVersion()
	jPreload[ "response" ]["versions"]['portal.cliente.customer'] := portal.cliente.customer.getVersion()
	jPreload[ "response" ]["versions"]['portal.cliente.invoices'] := portal.cliente.invoices.getVersion()
	jPreload[ "response" ]["versions"]['portal.cliente.home'] := portal.cliente.home.getVersion()
	jPreload[ "response" ]["params"] :=  JsonObject():New()
	jPreload[ "response" ]["params"]["MV_FPCMSG1"] := AllTrim(GetMV("MV_FPCMSG1"))
	jPreload[ "response" ]["params"]["MV_FPCMSG2"] := AllTrim(GetMV("MV_FPCMSG2"))
	jPreload[ "response" ]["params"]["MV_FPCTURL"] := AllTrim(GetMV("MV_FPCTURL"))
	jPreload[ "response" ]["configs"] :=  JsonObject():New()
	jPreload[ "response" ]["configs"]["NGFEMBOL"] := ExistBlock("NGFEMBOL")
	jPreload[ "response" ]["configs"]["NGFBXBOL"] := ExistBlock("NGFBXBOL")

	if lCustom	
		oCustomService := totvs.backoffice.fin.pc.custom.PCCustomConfig():new()
		
		if jHeaders:hasProperty('x-pc-user') .and. !Empty(jHeaders['x-pc-user'])
			oCustomService:setUser(jHeaders['x-pc-user'])
		endIf
		
		jPreload["response"]["custom"] := oCustomService:getUserCustom()
	endif

	AnswerRest(jPreload)
Return
