#include "totvs.ch"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.apps.grr.messages
using namespace totvs.protheus.backoffice.apps.grr.general

//-------------------------------------------------------------------
/*/{Protheus.doc} BillPaidCreatedMessageHandler
    @author claudio.yoshio@totvs.com.br
    @since 02/06/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
class BillPaidCreatedMessageHandler from totvs.protheus.backoffice.apps.grr.messages.HandleMessage
    public method new() as object
    protected method processMessage(jData as json) as logical
endClass

//------------------------------------------------------------------
/*/{Protheus.doc} new
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 02/06/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method new() as object class BillPaidCreatedMessageHandler
    local jMandatoryProperties := JsonObject():new() as json

    _Super:new()

    jMandatoryProperties["organizationIntegrationId"] := {"type": "C", "notEmpty": .T.}
    jMandatoryProperties["billIntegrationId"] := {"type": "C", "notEmpty": .F.}
    jMandatoryProperties["source"] := {"type": "C", "notEmpty": .F.}

    self:setMandatoryProperties(jMandatoryProperties)
return self

//------------------------------------------------------------------
/*/{Protheus.doc} processMessage
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 02/06/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method processMessage(jData as json) as logical class BillPaidCreatedMessageHandler
    local lSuccess := .F. as logical
    local cIdDoc as character
    local cPendingReconciliationStatusCode := "07" as character

    if jData['source'] == 'GRRI110'
        cIdDoc := jData['billIntegrationId']
    endIf

    if !empty(cIdDoc)
        HRJ->(dbSetOrder(2)) //HRJ_IDDOC
        if HRJ->(dbSeek(cIdDoc)) .and. recLock("HRJ", .F.)
            HRJ->HRJ_STAT := cPendingReconciliationStatuscode
            //to-do: Verificar se é melhor gravar o objeto inteiro ou apenas informações que realmente vamos usar
            HRJ->HRJ_DET := Util():getJsonUpdate(HRJ->HRJ_DET, {"BillPaidCreated": jData})
            HRJ->(msUnlock())

            lSuccess := .T.
        endIf
    endIf
return lSuccess
