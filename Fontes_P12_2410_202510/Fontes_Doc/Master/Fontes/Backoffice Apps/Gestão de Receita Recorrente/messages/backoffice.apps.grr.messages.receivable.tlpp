#include "totvs.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.apps.grr.messages
using namespace totvs.protheus.backoffice.apps.grr.general
using namespace totvs.protheus.backoffice.apps.grr.models
using namespace totvs.protheus.backoffice.apps.grr.integration.financial


//-------------------------------------------------------------------
/*/{Protheus.doc} ReceivableMessages
    @description Classe responsável pelas mensagens na integração entre o módulo Financeiro do Protheus
    e a plataforma Gestão de Receita Recorrente.
    @author guilherme.sordi@totvs.com.br
    @since 10/02/2025
    @version 12.1.2410
    @see https://tdn.totvs.com/pages/viewpage.action?pageId=910680319
*/
//-------------------------------------------------------------------
class ReceivableMessages
    private data oHrjStatement as object
    private data oMessenger as object
    private data oIntegrationModel as object
    private data aItems as array
    private data aIdDocs as array
    private data cMessageName as character
    private data cIdDoc as character

    public method new() as object
    // public method handleAllEvents()
    public method clear()
    public method setIdDoc(cIdDoc as character)
    public method sendCreateMessages()
    public method sendUpdateMessages()
    public method sendDeleteMessages()

    private method sendAllMessagesByStatus(aStatus as array, cMessageName as character)
    private method getHrjStatement() as object
    private method getJson(cIdDoc as character) as json
    private method send()
    private method setMessageName(cMessageName as character)
    private method addItem(cIdDoc, jItem)
    private method clearItems()
    private method setError(aIdDocs as array, cErrorMessage as character, cErrorCategory as character)
    private method setStatus(cStatus as character)
endClass

//------------------------------------------------------------------
/*/{Protheus.doc} new
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
    @parameters oSendMessage, object, Injeção de dependência de um objeto da classe SendMessage. Será usado no caso de teste
    para mocar o resultado do envio para o smart link.
/*/
//-------------------------------------------------------------------
method new(oSendMessage as object) as object class ReceivableMessages
    self:aItems := {}
    self:aIdDocs := {}
    self:cIdDoc := ""

    if oSendMessage != NIL
        self:oMessenger := oSendMessage
    endIf
return self

//------------------------------------------------------------------
/*/{Protheus.doc} clear
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method clear() class ReceivableMessages
    self:cIdDoc := ""
    self:clearItems()
    freeObj(self:oHrjStatement)
return

//------------------------------------------------------------------
/*/{Protheus.doc} setIdDoc
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method setIdDoc(cIdDoc as character) class ReceivableMessages
    self:cIdDoc := cIdDoc
return

//------------------------------------------------------------------
/*/{Protheus.doc} getHrjStatement
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getHrjStatement(aStatus as array, aConfirmed as array) as object class ReceivableMessages
    local cQuery as character
    local nParamOrder := 1 as numeric

    default aConfirmed := {"1","2"}   

    //to-do Criar índice para essa consulta. Ela vai se usada com frequência
    if self:oHrjStatement == NIL
        cQuery := " SELECT HRJ_IDDOC "
        cQuery += " FROM " + retSqlName("HRJ") + " HRJ "
        cQuery += " WHERE HRJ_FILIAL = ? "
        cQuery += " AND HRJ_STAT = ? "
        cQuery += " AND HRJ_CONFIR = ? "
        cQuery += " AND HRJ.D_E_L_E_T_ = ? "
        if !empty(self:cIdDoc)
            cQuery += " AND HRJ_IDDOC = ? "
        endIf

        self:oHrjStatement := FWExecStatement():new(changeQuery(cQuery)) 
    endIf
    self:oHrjStatement:setString(nParamOrder++, fwXFilial("HRJ"))
    self:oHrjStatement:setIn(nParamOrder++, aStatus)
    self:oHrjStatement:setIn(nParamOrder++, aConfirmed)
    self:oHrjStatement:setString(nParamOrder++, "")
    if !empty(self:cIdDoc)
        self:oHrjStatement:setString(nParamOrder++, self:cIdDoc)
    endIf
return self:oHrjStatement

//------------------------------------------------------------------
/*/{Protheus.doc} sendCreateMessages
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method sendCreateMessages() class ReceivableMessages
    self:sendAllMessagesByStatus({"01"}, "02", "ReceivableCreate", {"2"})
return

//------------------------------------------------------------------
/*/{Protheus.doc} sendUpdateMessages
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method sendUpdateMessages() class ReceivableMessages
    self:sendAllMessagesByStatus({"01"}, "02", "ReceivableUpdate", {"1"})
return

//------------------------------------------------------------------
/*/{Protheus.doc} sendDeleteMessages
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method sendDeleteMessages() class ReceivableMessages
    self:sendAllMessagesByStatus({"03"}, "04", "ReceivableCancel", {"1"})
return

//------------------------------------------------------------------
/*/{Protheus.doc} sendAllMessagesByStatus
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method sendAllMessagesByStatus(aStatusFrom as array, cStatusTo as character, cMessageName as character, aConfirmed as array) class ReceivableMessages
    local cAlias := self:getHrjStatement(aStatusFrom, aConfirmed):openAlias() as character
    local nMaxItems := 200 as numeric
    local cIdDoc as character
    
    self:setMessageName(cMessageName)

    while !(cAlias)->(eof())

        try
            cIdDoc := (cAlias)->HRJ_IDDOC
            self:addItem( cIdDoc, self:getJson(cIdDoc) )
        catch oError
            self:setError({cIdDoc}, oError:description, "errorOnGetJson")
        endTry

        if len(self:aItems) >= nMaxItems
            self:send()
            self:setStatus(cStatusTo)
            self:clearItems()
        endIf

        (cAlias)->(dbSkip())
    endDo

    (cAlias)->(dbCloseArea())

    self:send()
    self:setStatus(cStatusTo)
    self:clearItems()
return

//------------------------------------------------------------------
/*/{Protheus.doc} getJson
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getJson(cIdDoc as character) as json class ReceivableMessages
    local jDoc as json

    if self:oIntegrationModel == NIL
        self:oIntegrationModel := ReceivableIntegrationModel():new()
    endIf

    self:oIntegrationModel:setIdDoc(cIdDoc)

    if self:cMessageName == "ReceivableCancel"
        jDoc := self:oIntegrationModel:getJsonCancel()
    else
        jDoc := self:oIntegrationModel:getJson()
    endif

    self:oIntegrationModel:clear()

return jDoc

//------------------------------------------------------------------
/*/{Protheus.doc} send
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method send() class ReceivableMessages
    if self:oMessenger == NIL
        self:oMessenger := SendMessage():new()
    endIf

    if len(self:aItems) > 0 .and. !empty(self:cMessageName)
        try
            self:oMessenger:setMessageName(self:cMessageName)
            self:oMessenger:sendBatch(self:aItems)
        catch oError
            self:setError(self:aIdDocs, oError:description, self:cMessageName)
        endtry
    endIf
return

//------------------------------------------------------------------
/*/{Protheus.doc} setMessageName
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method setMessageName(cMessageName as character) class ReceivableMessages
    self:cMessageName := cMessageName
return

//------------------------------------------------------------------
/*/{Protheus.doc} addItem
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method addItem(cIdDoc, jItem) class ReceivableMessages
    aAdd(self:aIdDocs, cIdDoc)
    aAdd(self:aItems, jItem)
return

//------------------------------------------------------------------
/*/{Protheus.doc} clearItems
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method clearItems() class ReceivableMessages
    fwFreeArray(self:aIdDocs)
    self:aIdDocs := {}
    fwFreeArray(self:aItems)
    self:aItems := {}
return

//------------------------------------------------------------------
/*/{Protheus.doc} setStatus
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method setStatus(cStatus as character) class ReceivableMessages
    if len(self:aIdDocs) > 0
        FinancialIntegration():setStatus(cStatus, self:aIdDocs)
    endIf
return

//------------------------------------------------------------------
/*/{Protheus.doc} setError
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 07/03/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method setError(aIdDocs as array, cErrorMessage as character, cErrorCategory as character, jDetails as character) class ReceivableMessages
    local nX as numeric

    for nX := 1 to len(aIdDocs)
        FinancialIntegration():setError(aIdDocs[nX], cErrorMessage, cErrorCategory, jDetails)
    next
return
