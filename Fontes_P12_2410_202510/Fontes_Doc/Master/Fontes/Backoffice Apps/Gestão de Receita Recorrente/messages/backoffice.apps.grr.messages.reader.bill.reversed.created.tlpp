#include "totvs.ch"
#include "tlpp-core.th"

//-------------------------------------------------------------------
/*/{Protheus.doc} BillReversedCreatedMessageReader
    @author claudio.yoshio@totvs.com.br
    @since 05/08/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
class BillReversedCreatedMessageReader from totvs.protheus.backoffice.apps.grr.messages.ReceivableMessageReader
    public method new() as object

    protected method processMessage(jData as json) as logical
endClass

//------------------------------------------------------------------

/*/{Protheus.doc} new
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 05/08/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method new() as object class BillReversedCreatedMessageReader
    local jMandatoryProperties := JsonObject():new() as json
    
    _Super:new()

    jMandatoryProperties["organizationIntegrationId"] := {"type": "C", "notEmpty": .T.}
    jMandatoryProperties["billIntegrationId"] := {"type": "C", "notEmpty": .F.}
    jMandatoryProperties["source"] := {"type": "C", "notEmpty": .F.}

    self:setMandatoryProperties(jMandatoryProperties)
return self

//------------------------------------------------------------------
/*/{Protheus.doc} processMessage
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 05/08/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method processMessage(jData as json) as logical class BillReversedCreatedMessageReader
    local lSuccess := .F. as logical
    local cIdDoc as character
    local cBillReversedCreatedStatusCode := "10" as character

    if jData['source'] == 'GRRI110'
        cIdDoc := jData['billIntegrationId']
    endIf

    HRJ->(dbSetOrder(2)) //HRJ_IDDOC
    if !Empty(cIdDoc) .and. HRJ->(dbSeek(cIdDoc)) .and. recLock("HRJ", .F.)
        HRJ->HRJ_STAT := cBillReversedCreatedStatusCode
        HRJ->(msUnlock())

        lSuccess := .T.
    endIf
return lSuccess
