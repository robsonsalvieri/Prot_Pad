#include "totvs.ch"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.apps.grr.messages
using namespace totvs.protheus.backoffice.apps.grr.general

//-------------------------------------------------------------------
/*/{Protheus.doc} BillAwaitingPaymentCreatedMessageHandler
    @author guilherme.sordi@totvs.com.br
    @since 04/04/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
class BillAwaitingPaymentCreatedMessageHandler from totvs.protheus.backoffice.apps.grr.messages.HandleMessage
    public method new() as object
    protected method processMessage(jData as json) as logical
endClass

//------------------------------------------------------------------
/*/{Protheus.doc} new
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 04/04/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method new() as object class BillAwaitingPaymentCreatedMessageHandler
    local jMandatoryProperties := JsonObject():new() as json

    _Super:new()

    jMandatoryProperties["organizationIntegrationId"] := {"type": "C", "notEmpty": .T.}
    jMandatoryProperties["billIntegrationId"] := {"type": "C", "notEmpty": .F.}
    jMandatoryProperties["source"] := {"type": "C", "notEmpty": .F.}
    jMandatoryProperties["cycle"] := {"type": "N", "notEmpty": .F.}
    jMandatoryProperties["dueDate"] := {"type": "C", "notEmpty": .F.}
    jMandatoryProperties["billId"] := {"type": "C", "notEmpty": .F.}

    self:setMandatoryProperties(jMandatoryProperties)
return self

//------------------------------------------------------------------
/*/{Protheus.doc} processMessage
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 04/04/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method processMessage(jData as json) as logical class BillAwaitingPaymentCreatedMessageHandler
    local lSuccess := .F. as logical
    local cIdDoc as character
    local cAwaitingPaymentStatusCode := "11" as character

    if jData['source'] == 'GRRI110'
        cIdDoc := jData['billIntegrationId']
    endIf

    HRJ->(dbSetOrder(2)) //HRJ_IDDOC
    if !empty(cIdDoc) .and. HRJ->(dbSeek(cIdDoc)) .and. recLock("HRJ", .F.)
        HRJ->HRJ_STAT := cAwaitingPaymentStatuscode
        HRJ->(msUnlock())

        lSuccess := .T.
    endIf
return lSuccess
