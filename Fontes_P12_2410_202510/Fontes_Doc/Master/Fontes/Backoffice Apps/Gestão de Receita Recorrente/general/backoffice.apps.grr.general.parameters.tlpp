#include "totvs.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.apps.grr.general

//-------------------------------------------------------------------
/*/{Protheus.doc} AppParameters
    @description Classe para GRR acessar parâmetros de configuração do Protheus (tabela SYS_APP_PARAM)
    @author guilherme.sordi@totvs.com.br
    @since 13/06/2025
    @version 12.1.2410
*/
//-------------------------------------------------------------------
class AppParameters
    static method getParameter(cParamName as character) as character
    static method setParameter(cParamName as character, cValue as character)
    static method getBranchParameter(cParamName as character) as character
    static method setBranchParameter(cParamName as character, cValue as character)

    static method getPayableProviderParameter() as json
endClass

//------------------------------------------------------------------
/*/{Protheus.doc} getParameter
    @description Retorna o valor do parâmetro de configuração. Os parâmetros da SYS_APP_PARAM não
    são necessariamente strings, mas no GRR estamos usando somente strings.
    A idéia é que dados mais complexos sejam gravados como JSON.
    @type method 
    @author guilherme.sordi@totvs.com.br
	@since 13/06/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getParameter(cParamName as character) as character class AppParameters
    local cValue as character
    local jParam as object

    cParamName := lower(allTrim(cParamName))

    jParam := FwTfConfig()
    if jParam[cParamName] != NIL
        cValue := jParam[cParamName]
    endIf
    
    FreeObj( jParam )
return cValue

//------------------------------------------------------------------
/*/{Protheus.doc} setParameter
    @description Grava um parâmetro tipo string na SYS_APP_PARAM.
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 13/06/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method setParameter(cParamName as character, cValue as character) class AppParameters    
    local jParam := JsonObject():new()
    
    cParamName := lower(allTrim(cParamName))

    jParam[cParamName] := cValue
    FwTFSetConfig(jParam)

    FreeObj( jParam )
return

//------------------------------------------------------------------
/*/{Protheus.doc} getBranchParameter
    @description Grava o valor de um parâmetro consolado de forma exclusiva por filial.
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 13/06/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getBranchParameter(cParamName as character) as character class AppParameters
    cParamName += "_" + cEmpAnt + "|" + cFilAnt
return AppParameters():getParameter(cParamName)

//------------------------------------------------------------------
/*/{Protheus.doc} setBranchParameter
    @description Obtém o valor de um parâmetro consolado de forma exclusiva por filial.
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 13/06/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method setBranchParameter(cParamName as character, cValue as character) class AppParameters
    cParamName += "_" + cEmpAnt + "|" + cFilAnt
    AppParameters():setParameter(cParamName, cValue)
return

//------------------------------------------------------------------
/*/{Protheus.doc} getPayableProviderParameter
    @description Retorna o código/loja do fornecedor configurados 
    no parâmetro MV_GRRFPAY
    @type method
    @author claudio.yoshio@totvs.com.br
	@since 28/07/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getPayableProviderParameter() as json class AppParameters
    local cPayableProvider := SuperGetMV( "MV_GRRFPAY", .T., "" ) as character
    local jPayableProvider := JsonObject():new() as json
    local nTamProviderCode := TamSx3("A2_COD")[1] as numeric
    local nTamProviderBranch := TamSx3("A2_LOJA")[1] as numeric
    
    jPayableProvider['payableProviderCode'] := padR(Util():pipeSplit(cPayableProvider, 1), nTamProviderCode)
    jPayableProvider['payableProviderBranch'] := padR(Util():pipeSplit(cPayableProvider, 2), nTamProviderBranch)
return jPayableProvider
