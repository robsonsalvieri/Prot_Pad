#include "totvs.ch"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.apps.grr.integration.financial
using namespace totvs.protheus.backoffice.apps.grr.general
using namespace totvs.protheus.backoffice.apps.grr.models
using namespace totvs.protheus.backoffice.apps.grr.messages

class Fina060
    private data cIdDoc as character
    private data oBills as object
    private data oExecBill as object

    public method new() as object
    public method setIdDoc(cIdDoc as character)
    public method changeCollectionStatus(cNewCollectionStatus as character)
    public method getCollectionStatus() as json

    private method getExecBill() as object
    private method getBillData() as json
endClass

//------------------------------------------------------------------
/*/{Protheus.doc} new
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 19/05/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method new() as object class Fina060
    self:cIdDoc := ""
return self

//------------------------------------------------------------------
/*/{Protheus.doc} setIdDoc
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 19/05/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method setIdDoc(cIdDoc as character) class Fina060
    self:cIdDoc := cIdDoc
return

//------------------------------------------------------------------
/*/{Protheus.doc} getBillData
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 19/05/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getBillData() as json class Fina060
    local cTableAlias := "SE1" as character
    local jBill := JsonObject():new() as json

    if self:oBills == NIL
        self:oBills := Bills():new()
    endif
    self:oBills:setIdDoc(self:cIdDoc)
    jBill := self:oBills:getBillData(cTableAlias)
return jBill
    
//------------------------------------------------------------------
/*/{Protheus.doc} changeCollectionStatus
    @description Transfere o título para determinada carteira.
    Atualmente nossa única necessidade é remover da carteira do GRR, 
    ou seja, transferir para a carteira 0 (zero).
    Se precisamos transferir para outras carteiras no futuro, 
    precisamos tratar os dados bancários. Avaliar necessidade de receber por parâmetros
    ou ler de algum outro lugar pré-definido.
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 23/04/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method changeCollectionStatus(cNewCollectionStatus as character) class Fina060
    local jBillData := JsonObject():new() as json
    local aBill := {}
    local cBankCode as character
    local cBankBranch as character
    local cBankAccount as character
    local cBillNumberInBank as character
    local nOperation := 2 //Transferência
 
    local aExecAutoErrors := {} as array
    local cErrorMessage := "" as character
    local nX as numeric

    default cNewCollectionStatus := "0"
 
    //Variáveis para ExecAuto
    Private lMsErroAuto     := .F.
    Private lMsHelpAuto     := .T.
    Private lAutoErrNoFile  := .T.
  
    //Para retornar o título para carteira 0 é necessário informar o banco "em branco"
    If cNewCollectionStatus == "0"
        cBankCode := ""
        cBankBranch := ""
        cBankAccount := ""
        cBillNumberInBank := ""
    EndIf
 
    //Chave do título
    jBillData := self:getBillData() 
    AAdd(aBill, {"E1_PREFIXO",   jBillData["prefix"],        Nil})
    AAdd(aBill, {"E1_NUM",       jBillData["number"],        Nil})
    AAdd(aBill, {"E1_PARCELA",   jBillData["installment"],   Nil})
    AAdd(aBill, {"E1_TIPO",      jBillData["type"],          Nil})
 
    //Informações bancárias 
    AAdd(aBill, {"AUTDATAMOV",   dDatabase,      Nil})
    AAdd(aBill, {"AUTBANCO",     PadR(cBankCode,            TamSX3("A6_COD")[1]),       Nil})
    AAdd(aBill, {"AUTAGENCIA",   PadR(cBankBranch,          TamSX3("A6_AGENCIA")[1]),   Nil})
    AAdd(aBill, {"AUTCONTA",     PadR(cBankAccount,         TamSX3("A6_NUMCON")[1]),    Nil})
    AAdd(aBill, {"AUTSITUACA",   PadR(cNewCollectionStatus, TamSX3("E1_SITUACA")[1]),   Nil})
    AAdd(aBill, {"AUTNUMBCO",    PadR(cBillNumberInBank,    TamSX3("E1_NUMBCO")[1]),    Nil})
    AAdd(aBill, {"AUTGRVFI2",    .T.,            Nil})
 
    //Carteira descontada deve ser encaminhado o valor de crédito, desconto e IOF já calculados
    // If jBillData $ "2|7"
    //     AAdd(aBill, {"AUTDESCONT",   090,    Nil})
    //     AAdd(aBill, {"AUTCREDIT",    750,    Nil})
    //     AAdd(aBill, {"AUTIOF",       010,    Nil})
    // EndIf
 
    MsExecAuto({|nOperation, aBill| FINA060(nOperation, aBill)}, nOperation, aBill)
 
    If lMsErroAuto
        aExecAutoErrors := GetAutoGRLog()
         
        For nX := 1 To Len(aExecAutoErrors)
            cErrorMessage += aExecAutoErrors[nX]
        Next nX
    EndIf

    jBillData:fromJson("{}")
    freeObj(jBillData)
    aSize(aBill, 0)
    aBill := NIL
    aSize(aExecAutoErrors, 0)
    aExecAutoErrors := NIL

    if !empty(cErrorMessage)
        throw FlowControl():newException(cErrorMessage)
    endIf
return

//------------------------------------------------------------------
/*/{Protheus.doc} getExecBill
    @description Retorna um objeto FWExecStatement com a query
    que obtém dados da FRV a partir de um IDDOC.
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 14/05/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getExecBill() as object class Fina060
    local cQuery as character
    local cNoDeleted := " " as character
    local nParamOrder := 1 as numeric

    if self:oExecBill == NIL
        cQuery := " SELECT FRV_CODIGO, FRV_DESCRI, FRV_GRRCOD "
        cQuery += " FROM "+ retSqlName("FK7") +" FK7 "

        cQuery += " JOIN "+ retSqlName("SE1") +" SE1  "
        cQuery += " ON FK7.FK7_ALIAS = 'SE1' "
        cQuery += " AND FK7.FK7_FILTIT = SE1.E1_FILIAL "
        cQuery += " AND FK7.FK7_PREFIX = SE1.E1_PREFIXO "
        cQuery += " AND FK7.FK7_NUM = SE1.E1_NUM "
        cQuery += " AND FK7.FK7_PARCEL = SE1.E1_PARCELA "
        cQuery += " AND FK7.FK7_TIPO = SE1.E1_TIPO "
        cQuery += " AND SE1.E1_FILIAL = ? "        
        cQuery += " AND SE1.D_E_L_E_T_ = ? "

        cQuery += " JOIN "+ retSqlName("FRV") +" FRV "
        cQuery += " ON " + fwJoinFilial("SE1", "FRV")
        cQuery += " AND FRV.FRV_CODIGO = SE1.E1_SITUACA "
        cQuery += " AND FRV.D_E_L_E_T_ = ? "

        cQuery += " WHERE FK7_IDDOC = ? "
        cQuery += " AND FK7.D_E_L_E_T_ = ? "

        cQuery := changeQuery(cQuery)

        self:oExecBill := FWExecStatement():new(cQuery)
    endIf
    self:oExecBill:setString(nParamOrder++, FWXFilial("SE1"))
    self:oExecBill:setString(nParamOrder++, cNoDeleted)
    self:oExecBill:setString(nParamOrder++, cNoDeleted)
    self:oExecBill:setString(nParamOrder++, self:cIdDoc)
    self:oExecBill:setString(nParamOrder++, cNoDeleted)
return self:oExecBill

//------------------------------------------------------------------
/*/{Protheus.doc} getCollectionStatus
    @description Retorna um objeto JSON com os dados da situação de cobrança
    do título informado via setIdDoc().
    @type method
    @author guilherme.sordi@totvs.com.br
	@since 14/05/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
method getCollectionStatus() as json class Fina060
    local cAlias as character
    local jData as json

    jData := {"code": "", "description": "", "walletId": ""}

    cAlias := self:getExecBill():openAlias()
    if !(cAlias)->(eof())
        jData['code'] := (cAlias)->FRV_CODIGO
        jData['description'] := (cAlias)->FRV_DESCRI
        jData['walletId'] := (cAlias)->FRV_GRRCOD
    endIf
    (cAlias)->(dbCloseArea())
return jData
