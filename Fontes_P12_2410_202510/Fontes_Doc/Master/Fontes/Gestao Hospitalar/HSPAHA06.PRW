#include "PROTHEUS.CH"
#include "HSPAHA06.CH"
#include "COLORS.CH"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณHSPAHA06  บAutor  ณAntonio Marco       บ Data ณ  16/11/05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณCadastro de impressoras                                     บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณGestao Hospitalar                                           บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ  
ฑฑบAltera็๕esณ L.Gustavo Caloi 28/03/06 -> Padroniza็ใo da HS_MsgInf()    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Function HSPAHA06() 

Private nOpc      := 0                                                          
Private cCadastro := STR0001 // "Cadastro de Impressoras"
Private aRotina   := MenuDef()

dbSelectArea("GFJ")
dbSetOrder(1)
mBrowse(06, 01, 22, 75, "GFJ")

Return(Nil)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณHSAHA061  ณ Autor ณAntonio Marco          ณ Data ณ 16/11/05 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณFuncao de Inclusao/Alteracao/Visualizacao                   ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณSintaxe   ณ                                                            ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Function HSAHA061(cAliasA06, nRegA06, nOpcA06)
 Local nOpcDlg := 0
 Local aSize   := {}, aObjects := {}, aInfo := {}, aPObjs := {}

 Private oEnGFJ
 Private aTela := {}, aGets := {}
 
 aSize 			:= MsAdvSize(.T.)
 aObjects := {}	
 AAdd( aObjects, { 100, 100, .T., .T. } )	
 
 aInfo  := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 0, 0 }
 aPObjs := MsObjSize( aInfo, aObjects, .T. )

 RegToMemory("GFJ", IIf(aRotina[nOpcA06, 4] == 3, .T., .F.))      
 
 DEFINE MSDIALOG oDlg TITLE OemToAnsi(cCadastro) From aSize[7], 000 To aSize[6], aSize[5] Of oMainWnd Pixel
  oEnGFJ := MsMGet():New("GFJ", nRegA06, nOpcA06,,,,, aPObjs[1],, 2)                          
  oEnGFJ:oBox:align := CONTROL_ALIGN_ALLCLIENT
 ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg, {|| nOpcDlg := 1, IIf(Obrigatorio(aGets, aTela), oDlg:End(), nOpcDlg := 0)}, ;
                                                  {|| nOpcDlg := 0, oDlg:End()})

 If nOpcDlg != 0  // nao cancelou?
  FS_GravaGFJ(nOpcA06)
	EndIf
Return(Nil)

// Atualizacao do registro
Static Function FS_GravaGFJ(nOpcA06)
	Local lAchou                                             
	Local cCODIMP
	Local cIDIMPP

	// inclusao da impressora padrao
 If aRotina[nOpcA06, 4] == 3 .And. M->GFJ_IDIMPP == "1"
 	dbSetOrder(2)
  If DbSeek(xFilial("GFJ") + "1")
		 HS_MsgInf(STR0013, STR0011, STR0016) //"Ja existe a Impressora Padrao!"###"ATENCAO"###"Funcao de Inclusao/Alteracao/Visualizacao"

			// inclui a impressora como nao-padrao
		 M->GFJ_IDIMPP := "0"
  EndIf
 EndIf 

	// alteracao da impressora...
 If aRotina[nOpcA06, 4] == 4
		cCODIMP := GFJ->GFJ_CODIMP
		
		// ...de nao-padrao p/ padrao
	 If GFJ->GFJ_IDIMPP == "0" .And. M->GFJ_IDIMPP == "1"
	  dbSetOrder(2)
 	 lAchou := DbSeek(xFilial("GFJ") + "1")

	 	If lAchou .And. GFJ->GFJ_CODIMP != M->GFJ_CODIMP
 	 	HS_MsgInf(STR0013, STR0011, STR0016) //"Ja existe a Impressora Padrao!"###"ATENCAO"###"Funcao de Inclusao/Alteracao/Visualizacao"
		  Return(Nil)
 		EndIf

		 dbSetOrder(1)
			DbSeek(xFilial("GFJ") + cCODIMP)
		EndIf

		// ...de padrao p/ nao-padrao
	 If GFJ->GFJ_IDIMPP == "1" .And. M->GFJ_IDIMPP == "0"
	 	HS_MsgInf(STR0015, STR0011, STR0016)  //"Deve ser definida uma nova Impressora Padrao!"###"ATENCAO"###"Funcao de Inclusao/Alteracao/Visualizacao"
		EndIf
 EndIf

 Begin Transaction
  If aRotina[nOpcA06, 4] != 5 // nao eh exclusao?
   RecLock("GFJ", aRotina[nOpcA06, 4] == 3) // inclcui e trava o registro (apenas na inclusao)
    GFJ->GFJ_FILIAL := xFilial("GFJ")
    GFJ->GFJ_CODIMP := M->GFJ_CODIMP
    GFJ->GFJ_DESIMP := M->GFJ_DESIMP
    GFJ->GFJ_QTLFOL := M->GFJ_QTLFOL
    GFJ->GFJ_IDIMPP := M->GFJ_IDIMPP
  	 GFJ->GFJ_LOGARQ := HS_LOGARQ()
   MsUnlock()
  Else
			cIDIMPP = GFJ->GFJ_IDIMPP

   dbSelectArea("GFJ")  
   dbSetOrder(1)
	  RecLock("GFJ", .F., .T.)
    dbDelete()
 	 MsUnlock()
  	WriteSx2("GFJ") // Atualiza a quantidade de registros deletados no SX2

 		// excluida impressora padrao?
	  If cIDIMPP == "1"
	  	HS_MsgInf(STR0015, STR0011, STR0016) //"Deve ser definida uma nova Impressora Padrao!"###"ATENCAO"###"Funcao de Inclusao/Alteracao/Visualizacao"
 		EndIf
  EndIf
 End Transaction 

Return(Nil)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณHS_ValidImpบAutor ณAntonio Marco       บ Data ณ  18/11/05   บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValida a existencia de uma impressora no cadastro se o      บฑฑ
ฑฑบ          ณcodigo da mesma for informado                               บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณGestao Hospitalar                                           บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function HS_ValidaImp()
Local lRet   := .T.
Local cCampo := ReadVar()

If AllTrim(&(cCampo)) != ""
 dbSelectArea("GFJ")
 dbSetOrder(1)
 If !DbSeek(xFilial("GFJ") + AllTrim(&(cCampo)))
 	lRet := .F.
 EndIf
	DbCloseArea()
EndIf

// ATENCAO! A variavel cCODIMP deve ser definida no programa que
// que imprime o relatorio, como Private
cCODIMP := AllTrim(&(cCampo))

Return(lRet)


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณ MenuDef  ณ Autor ณ Tiago Bandeira        ณ Data ณ 11/07/07 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ Defini็ใo do aRotina (Menu funcional)                      ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณSintaxe   ณ MenuDef()                                                  ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function MenuDef()
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Define Array contendo as Rotinas a executar do programa      ณ
//ณ ----------- Elementos contidos por dimensao ------------     ณ
//ณ 1. Nome a aparecer no cabecalho                              ณ
//ณ 2. Nome da Rotina associada                                  ณ
//ณ 3. Usado pela rotina                                         ณ
//ณ 4. Tipo de Transao a ser efetuada                          ณ
//ณ    1 - Pesquisa e Posiciona em um Banco de Dados             ณ
//ณ    2 - Simplesmente Mostra os Campos                         ณ
//ณ    3 - Gera arquivo TXT para exportacao                      ณ
//ณ    4 - Recebe arquivo TXT                                    ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Local aRotina :=	{{OemToAnsi(STR0002), "axPesqui", 0, 1, 0, nil},;	// Pesquisar 
	                 {OemToAnsi(STR0003), "HSAHA061", 0, 2, 0, nil},;	// Visualizar
               		 {OemToAnsi(STR0004), "HSAHA061", 0, 3, 0, nil},;	// Incluir
		             {OemToAnsi(STR0005), "HSAHA061", 0, 4, 2, nil},;	// Alterar
		             {OemToAnsi(STR0006), "HSAHA061", 0, 5, 1, nil}}	// Excluir 
Return(aRotina)