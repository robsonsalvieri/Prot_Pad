#INCLUDE "PROTHEUS.CH"
#INCLUDE "TBICONN.CH"
#INCLUDE "TMKA530.CH"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณSLAManagerบAutor  ณVendas Clientes     บ Data ณ  07/01/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRealiza operacoes de gerenciamento de multiplos SLAs que    บฑฑ
ฑฑบ          ณatendam a um criterio de busca                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MP10                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Class SLAManager	
Method new() Constructor
Method create()
Method load(oParam)
Method getHistory(register)
Method updSLARegister(register,cNextResponsible)
Method notify(oSLARegister,cNextResponsible)
Method RetFunName(cCodeBlock)

EndClass


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Metodo   ณNew          บAutor  ณ Vendas Clientes บ Data ณ  19/01/08   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMetodo construtor da classe SLAManager                      บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MP10                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method new() Class SLAManager

Return Self


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Metodo   ณcreate       บAutor  ณ Vendas Clientes บ Data ณ  19/01/08   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMetodo que cria e retorna um objeto do tipo SLARegister     บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MP10                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method create() Class SLAManager
Local oSLARegister	:= SLARegister():New()

Return oSLARegister


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Metodo   ณload         บAutor  ณ Vendas Clientes บ Data ณ  19/01/08   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณCarregar uma lista de registros de SLA que atendam a um     บฑฑ
ฑฑบ          ณdeterminado criterio de busca                               บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MP10                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method load(oParam) Class SLAManager
Local aSLARegister	:= {}					//Array de objetos SLARegister
Local cAlias 		:= "SK5"				//Alias temporario da query
Local lNotifyAll	:= SuperGetMV("MV_SLASEND",,.F.)	//Considera registros de SLA ja notificados
Local cWhere 		:= "% %"							//Condicao a ser adicionada na query
Local lDelayed 		:= .T.					//Indica se o registro de SLA esta atrasado

If !lNotifyAll 
	//Nao considerar os usuarios ja notificados 
	cWhere := "% AND SK5.K5_STATUS <> '1' %"
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Efetua a busca conforme os parametros ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
cAlias 	:= GetNextAlias()
BeginSQL alias cAlias
	
	SELECT 	K5_CODIGO,	K5_DATA,	K5_DTEXPIR,		K5_HREXPIR,		K5_STATUS, 
			K5_ITEM
	FROM	%table:SK5% SK5
	WHERE	SK5.K5_FILIAL = %xFilial:SK5% AND 
			SK5.%NOTDEL% 	AND
			SK5.K5_DATA >= %EXP:DtoS(oParam:createdDateBegin)% AND
			SK5.K5_DATA <= %EXP:DtoS(oParam:createdDateEnd)% AND
			SK5.K5_DTEXPIR >= %EXP:DtoS(oParam:dateToExpireBegin)% AND
			SK5.K5_DTEXPIR <= %EXP:DtoS(oParam:dateToExpireEnd)% AND
			SK5.K5_STATUS <> '2'
			%exp:cWhere% 
			ORDER BY SK5.K5_ITEM
EndSql

While (cAlias)->(!Eof())

	lDelayed := .T.

	If (cAlias)->K5_DTEXPIR == DTOS(oParam:dateToExpireBegin)
		//Verifica criterio por hora inicial 
		If (cAlias)->K5_HREXPIR < oParam:hourToExpireBegin
			lDelayed := .F.
		EndIf
		
	ElseIf (cAlias)->K5_DTEXPIR == DTOS(oParam:dateToExpireEnd)
		//Verifica criterio por hora final 
		If (cAlias)->K5_HREXPIR > oParam:hourToExpireEnd
			lDelayed := .F.	
		EndIf
		
	EndIf

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Armazena no array os registros de SLA encontrados conforme os parametros enviados ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If lDelayed  
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Atualiza status do registro de SLA atual ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		DbSelectArea("SK5")
		DbSetOrder(1) 
		If DbSeek(xFilial("SK5") + (cAlias)->K5_CODIGO + (cAlias)->K5_ITEM)
			RecLock("SK5",.F.)
			REPLACE SK5->K5_DTATUA	WITH If(!IsInCallStack("GenRecurrence"), Date(), dDatabase )
			REPLACE SK5->K5_HRATUA	WITH Time()
			REPLACE SK5->K5_STATUS 	WITH "1"	//Notificado
			MsUnLock()
		EndIf	
		AAdd(aSLARegister, {(cAlias)->K5_CODIGO, (cAlias)->K5_ITEM})
	EndIf

	(cAlias)->( DbSkip() )
End 


DbSelectArea(cAlias)
DbCloseArea()

Return aSLARegister


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Metodo   ณgetHistory   บAutor  ณ Vendas Clientes บ Data ณ  19/01/08   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณCarregar o historico de um SLA, com informacoes das         บฑฑ
ฑฑบ          ณocorrencias envolvidas com o SLA                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MP10                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method getHistory(register) Class SLAManager
Local aSLARegSteps	:= {}             		//Array de objetos SLARegisterSteps
Local oSLARegSteps	:= NIL					//Objeto do tipo SLARegisterSteps
Local cAlias 		:= "SK5"				//Alias temporario da query
Local aStru			:= SK5->(dbStruct())	//Estrutura de campos do arquivo SK5
Local nStru			:= 0

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Efetua a busca conforme os parametros ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
cAlias 	:= GetNextAlias()
If TK510NewFields()
	BeginSQL alias cAlias
		
		SELECT 	K5_CODIGO,		K5_ITEM,		K5_CHAVE, 		K5_STATUS,	
				K5_DATA,		K5_HORA,		K5_DTEXPIR,		K5_HREXPIR,
				K5_DTENCER,		K5_SEVE,		K5_CODSLA,		K5_CODRESP
	
		FROM	%table:SK5% SK5
		WHERE	SK5.K5_FILIAL = %xFilial:SK5% AND 
				SK5.%NOTDEL% 	AND
				SK5.K5_CODIGO = %EXP:register:registerID%

	EndSql
Else
	BeginSQL alias cAlias
	
		SELECT 	K5_CODIGO,		K5_ITEM,		K5_CHAVE, 		K5_STATUS,	
				K5_DATA,		K5_HORA,		K5_DTEXPIR,		K5_HREXPIR,
				K5_DTENCER,		K5_SEVERID,		K5_CODSLA,		K5_CODRESP
	
		FROM	%table:SK5% SK5
		WHERE	SK5.K5_FILIAL = %xFilial:SK5% AND 
				SK5.%NOTDEL% 	AND
				SK5.K5_CODIGO = %EXP:register:registerID%

	EndSql	
EndIf

For nStru := 1 To Len(aStru)
	If aStru[nStru,2] <> "C" .AND. aStru[nStru,2] <> "M"
		TcSetField(cAlias,aStru[nStru,1],aStru[nStru,2],aStru[nStru,3],aStru[nStru,4])
	EndIf
Next nStru
	
While (cAlias)->(!Eof())
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Cria um objeto do tipo SLARegisterSteps e carrega dados ณ
	//ณ de historico dos registros de SLA                       ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	oSLARegSteps	:= SLARegisterSteps():New()
	oSLARegSteps:updatedDate	:= (cAlias)->K5_DTATUA
	oSLARegSteps:userNotified	:= (cAlias)->K5_CODRESP	
	
	If (cAlias)->K5_DTEXPIR < If(!IsInCallStack("GenRecurrence"), Date(), dDatabase )
		oSLARegSteps:delayed := .T.
	ElseIf (cAlias)->K5_DTEXPIR == If(!IsInCallStack("GenRecurrence"), Date(), dDatabase )
		If (cAlias)->K5_HREXPIR <= Time()
			oSLARegSteps:delayed := .T.	
		Else
			oSLARegSteps:delayed := .F.	
		EndIf
	Else
		oSLARegSteps:delayed := .F.
	EndIf
	
	AAdd(aSLARegSteps, oSLARegSteps)
	(cAlias)->( DbSkip() )	
End

DbSelectArea(cAlias)
DbCloseArea()

Return aSLARegSteps


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Metodo   ณupdSLARegisteบAutor  ณ Vendas Clientes บ Data ณ  25/01/08   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณAtualizar registro de SLA, incluindo um novo item para o    บฑฑ
ฑฑบ          ณmesmo apos a notificacao do responsavel                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MP10                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method updSLARegister(oSLARegister,cNextResponsible) Class SLAManager
Local lRet		:= .T.								//Retorno da funcao
Local cNewItem	:= Soma1(oSLARegister:item) 		//Cod. do novo item a ser gerado
Local dDataExpir:= oSLARegister:dateToExpire		//Data p/ expirar o reg. de SLA
Local cHoraExpir:= oSLARegister:hourToExpire		//Hora p/ expirar o reg. de SLA

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Atualiza status do registro de SLA atual ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DbSelectArea("SK5")
DbSetOrder(1) 
If DbSeek(xFilial("SK5") + oSLARegister:registerID + oSLARegister:item)
	RecLock("SK5",.F.)
	REPLACE SK5->K5_DTATUA	WITH If(!IsInCallStack("GenRecurrence"), Date(), dDatabase )
	REPLACE SK5->K5_HRATUA	WITH Time()
	REPLACE SK5->K5_STATUS 	WITH "1"	//Notificado
	MsUnLock()
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Localiza a severidade do SLA ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DbSelectArea("SK9")
DbSetOrder(1) 
If DbSeek(xFilial("SK9") + Alltrim(Str(oSLARegister:severityCode)) + oSLARegister:SlaFound)
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Chama a funcao que calcula data e hora p/ a proxima notificacao do SLA		  |
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	nSomaHs := SK9->K9_HRPSEXP		//Nr. de horas p/ notificar apos expirar SLA
	SomaDiaHor(@dDataExpir, @cHoraExpir, nSomaHs)

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Verifica se deve considerar somente os dias uteis 		   ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DbSelectArea("SK6")
	DbSetOrder(1) 
	If DbSeek(xFilial("SK6") + oSLARegister:SlaFound) .AND. SK6->K6_DSUTEIS == '1'
		dDataExpir := DataValida(dDataExpir) 
	EndIf	 
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Inclui novo item p/ o registro de SLA e atualiza data/hora para notificar SLA ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู	
	oSLARegister:item 			:= cNewItem
	oSLARegister:dateToExpire	:= dDataExpir
	oSLARegister:hourToExpire	:= cHoraExpir
	oSLARegister:SlaResponsible	:= cNextResponsible
	oSLARegister:StatusRegister	:= "0"
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Salva o novo registro ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	oSLARegister:save()
	
Else
	ConOut("Severidade " + Str(oSLARegister:severityCode) + " nao encontrada p/ o SLA " + oSLARegister:SlaFound)
	lRet := .F.
EndIf

Return lRet


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Metodo   ณnotify	   บAutor  ณ Vendas Clientes บ Data ณ  25/01/08   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณEnvia notificacao de SLA atrasado ao responsavel            บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MP10                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method notify(oSLARegister,cNextResponsible) Class SLAManager
Local oSLAResponsible 	:= SLAResponsible():New()				//Obj. SLAResponsible
Local oWFTemplate		:= Nil									//Obj. WFTemplate
Local oWFInfo 			:= Nil									//Obj. WFInfo
Local lRet 				:= .F.                              	//Retorno da funcao
Local cCodUser			:= ""									//Cod. interno do usuario
Local cCodTemplate		:= ""									//Cod. do modelo de WF
Local aWFs				:= {}
Local nCount			:= 0

DbSelectArea("SK6")
DbSetOrder(1)
If dbSeek(xFilial("SK6")+oSLARegister:SlaFound)
	cCodTemplate := SK6->K6_MODWF
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Usar o template de WF p/ enviar notificacaoณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	oWFTemplate := WFTemplate():New()
	lRet := oWFTemplate:load( cCodTemplate )
EndIf    

If lRet
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณPosiciona no item do registro de SLAณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DbSelectArea("SK5")
	DbSetOrder(1) //K5_FILIAL+K5_CODIGO+K5_ITEM
	DbSeek(xFilial("SK5")+oSLARegister:registerID+oSLARegister:item)

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Carrega os dados do responsavel ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	oSLAResponsible:load( cNextResponsible )
	oWFTemplate:cbTo := "'" + oSLAResponsible:email + "'"
	cCodUser := oSLAResponsible:codProtheusUser
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Prepara o objeto p/ envio do Workflow baseado no Template WF ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	aWFs := oWFTemplate:buildWF(cCodUser, .T.)
	
	If ValType(aWFs) != "A"
		aWFs := { aWFs }
	EndIf
	
	For nCount := 1 To Len( aWFs )
		oWFInfo := aWFs[nCount]
		oWFInfo:Header:addField(STR0001, oSLARegister:registerID) //"Registro de SLA:"
		oWFInfo:Header:addField(STR0002, oSLARegister:item) //"Item:"
		//oWFInfo:Header:addField('Severidade:', oSLARegister:severityCode)
		oWFInfo:Header:addField(STR0003, oSLARegister:dateToExpire) //"Data que expirou:"
		oWFInfo:Header:addField(STR0004, oSLARegister:hourToExpire)	 //"Hora que expirou:"
		oWFInfo:Header:addField(STR0005, oSLARegister:SlaResponsible + ' - ' + Posicione("SK8", 1, xFilial("SK8")+oSLARegister:SlaResponsible, 'K8_NOME') ) //"Responsแvel:"
	Next
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Executa os processos de envio de Workflow    ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู	
	If Len(aWFs) > 0
		oWFTemplate:fillFields(aWFs[1], cCodUser, aWFs)                
	EndIf
	
	For nCount := 1 To Len( aWFs )
		aWFs[nCount]:start()
		aWFs[nCount]:save()
	Next
	
	ConOut("Responsavel notificado")
Else
	ConOut("Nao foi possivel carregar o Modelo de Workflow: " + cCodTemplate) 
EndIf

Return lRet


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหอออออออัออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณTK530JOBSLA   บAutor  ณVendas Clientes บ Data ณ  21/01/08   บฑฑ
ฑฑฬออออออออออุออออออออออออออสอออออออฯออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFuncao responsavel por disparar o JOB de controle de SLAs   บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MP10                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/                   
Main Function TK530JOBSLA()

StartJob("TkJobSLA",GetEnvServer(),.F.)

Return NIL


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหอออออออัออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณTK530JOBSLA   บAutor  ณVendas Clientes บ Data ณ  21/01/08   บฑฑ
ฑฑฬออออออออออุออออออออออออออสอออออออฯออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFuncao responsavel por disparar o JOB de controle de SLAs   บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MP10                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/   
Main Function TkJobSLA()
Local oManager								//Obj. SLAManager
Local oParam								//Obj. SLALoadParam
Local oSLARegister							//Obj. SLARegister
Local aSLARegister		:= {}				//Array com os registros de SLA
Local cNextResponsible	:= ""				//Proximo responsavel pelo SLA
Local nI 				:= 0				//Usado em lacos For...Next
Local cJobName			:=	getWebJob()		//Nome do Job (Working Thread) em execucao
Local cCodEmp			:=	""				//Codigo da empresa p/ preparar ambiente
Local cCodFil			:=	""				//Codigo da filial p/ preparar ambiente
Local cFuncName			:= ""				//Nome da funcao a executar
Local lTK530SLA 		:= ExistBlock("TK530SLA")		//Indica se existe o P.E. TK530SLA

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Iniciando o ambiente para execucao ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
While Right(cJobName,1) == "_"
	cJobName := Substr(cJobName, 1, rat("_", cJobName)-1)
Enddo
cCodEmp := GetPvProfString(cJobName,"EMPRESA","01",GetAdv97())
cCodFil := GetPvProfString(cJobName,"FILIAL","01",GetAdv97())

ConOut(Repl("-", 80))
ConOut("TkJobSLA(): "+"Starting environment - " + cCodEmp + "/" + cCodFil)
ConOut(Repl("-",80))
RpcSetType ( 3 )

PREPARE ENVIRONMENT EMPRESA cCodEmp FILIAL cCodFil MODULO "TMK"

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Preenche parametros de busca ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
oParam	:= SLALoadParam():New()
oParam:createdDateBegin		:= CTOD('01/01/1990')
oParam:createdDateEnd		:= If(!IsInCallStack("GenRecurrence"), Date(), dDatabase )
oParam:dateToExpireBegin	:= If(!IsInCallStack("GenRecurrence"), Date(), dDatabase ) - 365 
oParam:hourToExpireBegin	:= "00:00"
oParam:dateToExpireEnd		:= If(!IsInCallStack("GenRecurrence"), Date(), dDatabase )
oParam:hourToExpireEnd		:= Time()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Carregar registros de SLA atrasados e ativos ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
oManager:= SLAManager():New()
aSLARegister := oManager:load( oParam )	

For nI := 1 To Len(aSLARegister)
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Cria um objeto do tipo SLARegister e carrega seus dados ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	oSLARegister := oManager:create()
	oSLARegister:load( aSLARegister[nI][1], , aSLARegister[nI][2])

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Encontra o proximo respons. pelo SLA ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	cNextResponsible := oSLARegister:getNextResponsible()

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Enviar notificacao de SLA atrasado ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
    If !Empty(cNextResponsible)
	    oManager:notify(oSLARegister,cNextResponsible)

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Atualiza o registro de SLA atual e cria um novo item para ณ
		//ณ acompanhamento do registro de SLA                         ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If oManager:updSLARegister(oSLARegister,cNextResponsible)
			ConOut("Registro de SLA " + oSLARegister:registerID +" atualizado")
		Else 
			ConOut("Nao foi possivel atualizar o registro de SLA " + oSLARegister:registerID)
		EndIf
	    
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Ponto de Entrada p/ permitir integracoes c/ a aplicacao que usa  SLA ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If lTK530SLA 
			ExecBlock("TK530SLA", .F., .F., {oSLARegister, cNextResponsible} )
		EndIf
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Executa o codeblock de acao p/ SLA atrasado ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		cFuncName := oManager:RetFunName(oSLARegister:cbWhenDelayed) 
		If !Empty( cFuncName ) 
			If FindFunction(cFuncName)
				Eval(&("{|oSLARegister|" + cFuncName + "(oSLARegister)}"), oSLARegister)
			Else
				ConOut("Aviso, funcao nao encontrada no RPO: " + cFuncName)
			EndIf
		EndIf

	Else
		ConOut("Nao foi possivel notificar. O proximo responsavel nao foi encontrado, verificar cad. de responsaveis")
	EndIf
    
Next nI


//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Finalizando o ambiente 				                                   ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
ConOut(Repl("-",80))
ConOut("TkJobSLA(): "+"Environment reseted")
ConOut(Repl("-",80))
RESET ENVIRONMENT	

Return .T.



/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออหอออออออัอออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Metodo   ณRetFunName   บAutor  ณ Vendas Clientes บ Data ณ  08/02/08   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯอออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRetorna o nome da funcao sem os parenteses                  บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MP10                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method RetFunName(cCodeBlock) Class SLAManager
Local cFunName := AllTrim(cCodeBlock)
Local cPosBracket := At("(", cFunName)

If cPosBracket > 0 
	cFunName := SubStr(cFunName, 1, cPosBracket - 1)
EndIf

Return cFunName


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหอออออออัออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ_TMKA530      บAutor  ณVendas Clientes บ Data ณ  07/01/08   บฑฑ
ฑฑฬออออออออออุออออออออออออออสอออออออฯออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFuncao dummy apenas para o programa aparecer no inspetor de บฑฑ
ฑฑบ          ณobjetos                                                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MP10                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/                   
FUNCTION _TMKA530()
RETURN NIL
