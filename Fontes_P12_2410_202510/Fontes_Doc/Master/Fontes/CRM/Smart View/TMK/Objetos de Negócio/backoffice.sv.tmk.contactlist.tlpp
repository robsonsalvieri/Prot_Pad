#Include "Protheus.ch"
#Include "FWlibVersion.ch"
#Include "Totvs.ch"
#Include "Msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-rest.th"
#Include "tlpp-core.th"
#Include "backoffice.sv.tmk.contactlist.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.tmk.contactlist.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="SU4,SU5,SU6", name="Lista de Contatos", country="ALL", customTables="ALL")

/*/{Protheus.doc} contactlistSmartViewBusinessObject
	Classe para criação do Objeto de Negócio para Lista de Contatos.
	@author Squad CRM/Faturamento
	@since 30/04/2025
	@version 1.0
*/
Class contactlistSmartViewBusinessObject from integratedProvider

    public method new()				as object
    public method getData()			as object
    public method getSchema()		as object
	public method TMKSV014CreateSQL()

    protected data aFields		as array
    protected data aStruct		as array
    protected data jItems		as json
    protected data cAlias		as character
    protected data cSelectLoc	as character
    protected data cWhereLoc	as character
    protected data aFieldsValue	as array

Endclass

/*{Protheus.doc} new
	Método de instância da classe
	@return object: self
	@author Squad CRM/Faturamento
	@since 30/04/2025
	@version 1.0
*/
Method new() class contactlistSmartViewBusinessObject

	_Super:new()
    self:appendArea(STR0001) 		//"Telemarketing"
    self:setDisplayName(STR0002)	//"Lista de Contatos"
    self:setDescription(STR0003)	//"Listagem de Contatos"
    self:setIsLookUp(.T.)  

	self:aFieldsValue := {}

Return self

/*{Protheus.doc} getData
	Retorna os dados do objeto de negócio
	@param nPage, numérico, indica a página atual do relatório
	@param oFilter, objeto, contém o filtro do SmartView
	@return object: self:oData
	@author Squad CRM/Faturamento
	@since 30/04/2025
	@version 1.0
*/
Method getData(nPage as numeric, oFilter as object) as object class contactlistSmartViewBusinessObject

	Local nX            := 0   as numeric
	Local nScan         := 0   as numeric
    Local lObfuscated   := .F. as logical
    Local aPDFields     := {}  as array
	Local aU4TipoTel	:= {}  as array
	Local aU4TipoEnd	:= {}  as array
	Local aU4Label		:= {}  as array
	Local aU4Forma		:= {}  as array
	Local aU4Tipo		:= {}  as array
	Local aU6Status	    := {}  as array
	Local xValue		:= Nil as variant

	MV_FIL := ""

	//Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(),"TMK020") 

	//Array para processamento
	self:aFieldsValue := {	{"U4_TIPO",		{"U4_TIPO",		"",	"Iif(!Empty((self:cAlias)->U4_TIPO), aU4Tipo[Val((self:cAlias)->U4_TIPO)], '')"}},;
							{"U4_FORMA",	{"U4_FORMA",	"",	"Iif(!Empty((self:cAlias)->U4_FORMA), aU4Forma[Val((self:cAlias)->U4_FORMA)], '')"}},;
							{"U4_TIPOTEL",	{"U4_TIPOTEL",	"",	"Iif(!Empty((self:cAlias)->U4_TIPOTEL), aU4TipoTel[Val((self:cAlias)->U4_TIPOTEL)], '')"}},;
							{"U4_TIPOEND",	{"U4_TIPOEND",	"",	"Iif(!Empty((self:cAlias)->U4_TIPOEND), aU4TipoEnd[Val((self:cAlias)->U4_TIPOEND)], '')"}},;
							{"U4_LABEL",	{"U4_LABEL",	"",	"Iif(!Empty((self:cAlias)->U4_LABEL), aU4Label[Val((self:cAlias)->U4_LABEL)], '')"}},;
							{"U6_ENTIDA",	{"U6_ENTIDA",	"",	"SVTkEntidade((self:cAlias)->U6_ENTIDA, (self:cAlias)->U6_CODENT,1,,(self:cAlias)->U4_FILIAL)"}},;
							{"U6_STATUS",	{"U6_STATUS",	"",	"Iif(!Empty((self:cAlias)->U6_STATUS), aU6Status[Val((self:cAlias)->U6_STATUS)], '')"}} }

	//Carrega conteúdo dos campos tipo combobox
	aU4TipoTel	:= TkSx3Box("U4_TIPOTEL")
	aU4TipoEnd	:= TkSx3Box("U4_TIPOEND")
	aU4Label	:= TkSx3Box("U4_LABEL")
	aU4Forma	:= TkSx3Box("U4_FORMA")
	aU4Tipo		:= TkSx3Box("U4_TIPO")
	aU6Status	:= TkSx3Box("U6_STATUS")

    //Verifica se precisa fazer o tratamento para LGPD   
    aPDFields	:= FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated	:= Len(aPDFields) > 0

	//Método que monta a Query
	self:TMKSV014CreateSQL()

	//Posiciona no registro inicial
    (self:cAlias)->(dbGoTop())
	While !(self:cAlias)->(Eof())
		self:jItems := JsonObject():new()
        
		For nX := 1 To Len(self:aStruct)
			//Verifica se existe tratamento para valores do JSON
			If (nScan := aScan(self:aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } ) ) > 0
				xValue  := FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2])
			Else
				xValue  := "(self:cAlias)->"+self:aStruct[nX][5]
			Endif

			//Verificação para tratamento de LGPD e campos tipo Data antes informar no JSON
			If lObfuscated .and. (nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0 
				self:jItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
            Elseif UPPER(self:aStruct[nX][3]) == "DATE"
				self:jItems[self:aStruct[nX][1]] := Iif(ValType((self:cAlias)->&(self:aStruct[nX][5])) == "C", totvs.framework.treports.date.stringToTimeStamp( &(xValue) ), totvs.framework.treports.date.dateToTimeStamp( &(xValue) ))
			Else
				self:jItems[self:aStruct[nX][1]] := &(xValue)
			Endif
        Next nX

		self:processData(1) //Tratamento para commit localizado
        self:oData:appendData(self:jItems)

	(self:cAlias)->(dbSkip())

	Enddo
	
	(self:cAlias)->(DBCloseArea())

	//Limpeza das variáveis tipo array
	FwFreeArray(self:aFieldsValue)
	aSize(aPDFields,0)
	aSize(aU4TipoTel,0)
	aSize(aU4TipoEnd,0)
	aSize(aU4Label,0)
	aSize(aU4Forma,0)
	aSize(aU4Tipo,0)
	aSize(aU6Status,0)

Return self:oData

/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
	@return object: self:oSchema
	@author Squad CRM/Faturamento
	@since 30/04/2025
	@version 1.0
*/
Method getSchema() as object class contactlistSmartViewBusinessObject

	Local nX := 0 	as numeric
	Local aParameters := {} as array

	self:aFields := {'U4_FILIAL','U4_DATA','U4_OPERAD','U4_LISTA','U4_DESC','U4_TIPO','U4_FORMA','U4_TELE','U4_CONFIG','U4_TIPOTEL',;
					'U4_MALADIR','U4_TIPOEND','U4_LABEL','U4_ETIQUET','U4_CODCAMP','U4_SCRIPT','U4_EVENTO',;
					'U6_CONTATO','U5_CONTAT','U6_ENTIDA','U5_FONE','U6_DATA','U6_HRINI','U6_HRFIM','U6_STATUS','U6_CODENT'}

    self:aStruct := FatTrGetStruct(self:aFields)

	//Cria a estrutura dos campos Padroes
	For nX := 1 To Len(self:aStruct)
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX

    // Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam("TMK020")

	// Adiciona novo parametro do tipo number
	self:oSchema:addParameter("MV_FIL",  STR0004 + " ?" ,"string", .T.,,,,,, FTSVHelp(,,,.T.)) //Filial   
	self:oSchema:addParameter("MV_PAR01",STR0006 + " ?", "string", .F.,,,,,,STR0005) //"Operadores"	

	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX  

	If __lLookUp       
		self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SU7", 2)
		self:setCustomURL("MV_PAR04", "api/framework/v1/genericLookupService/smartview/SU4", 2)
		self:setCustomURL("MV_FIL"	, "api/framework/v1/genericLookupService/smartview/SM0", 2)
	Endif

	FwFreeArray(aParameters)

Return self:oSchema

/*{Protheus.doc} TMKSV014CreateSQL
	Retorna todas as querys montadas
	@author Squad CRM/Faturamento
	@since 30/04/2025
	@version 2.0
	@return Nil
*/
Method TMKSV014CreateSQL() Class contactlistSmartViewBusinessObject 
    
	Local cFieldsQry := "" as character
    Local cQuery     := "" as character
    Local cNameTmp   := "" as character
    Local nParam     := 1  as numeric
    Local aFiliais	 := {} as array
    Local oQuery     := Nil as object
	Local oTempTableBranch := Nil as object

	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)
    
	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SU4", "U4_FILIAL")

    cNameTmp := oTempTableBranch:GetTableNameForQuery()

	//Trata campos do aFields para utilizar na query
	cFieldsQry := ArrTokStr(self:aFields, ",")

	//Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson(@self:aFieldsValue, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .F., .T.)

	cQuery := " SELECT ? "
	cQuery +=  " ? " //Adição de condições para objeto localizado no select
	cQuery += " FROM " + RetSqlName("SU4") + " SU4 "
	cQuery += " INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = SU4.U4_FILIAL "
	cQuery += " INNER JOIN " + RetSqlName("SU6") + " SU6 ON ? "
	cQuery += " AND SU6.U6_LISTA = SU4.U4_LISTA "
	cQuery += " AND SU6.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSqlName("SU5") + " SU5 ON ? "
	cQuery += " AND SU5.U5_CODCONT = SU6.U6_CONTATO " 
	cQuery += " AND SU5.D_E_L_E_T_ = ? "
	cQuery += " WHERE SU4.U4_FILIAL = TMPFIL.TMP_FILIAL "
	cQuery += " AND SU4.U4_DATA BETWEEN ? AND ? "
	
	If Empty(MV_PAR01)
		cQuery += " AND U4_OPERAD  <> ? "
	Else 
		cQuery += " AND U4_OPERAD  = ? "
	Endif 
	
	If !Empty(MV_PAR04) 
		cQuery += "AND SU4.U4_LISTA = ? "
	Endif

	cQuery +=  " ? " 
	cQuery += " AND SU4.D_E_L_E_T_ = ? "
	cQuery += " ORDER BY SU4.U4_FILIAL,SU4.U4_DATA,SU4.U4_STATUS "

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))
	oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetUnsafe(nParam++ ,self:cSelectLoc) //Adição de condições para objeto localizado no where
	oQuery:SetUnsafe(nParam++ , cNameTmp)
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU6", "SU4"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU5", "SU6"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetDate(nParam++, MV_PAR02)
	oQuery:SetDate(nParam++, MV_PAR03)	
	
	If Empty(MV_PAR01)
		oQuery:SetString(nParam++ , Space(TamSx3("U4_OPERAD")[1]))
	Else 
		oQuery:SetString(nParam++ ,MV_PAR01)
	Endif		
	
	If !Empty(MV_PAR04)
		oQuery:SetString(nParam++, MV_PAR04)
	Endif

	oQuery:SetUnsafe(nParam++ , self:cWhereLoc ) //Where que foi populado no objeto localizado
	oQuery:SetString(nParam++ , ' ')

	self:cAlias := oQuery:OpenAlias()    

    oTempTableBranch:Delete()
    FreeObj(oTempTableBranch)
    
    oQuery:Destroy()
    FreeObj(oQuery)
	oQuery:= Nil
    
	aSize(aFiliais,0) 

Return
