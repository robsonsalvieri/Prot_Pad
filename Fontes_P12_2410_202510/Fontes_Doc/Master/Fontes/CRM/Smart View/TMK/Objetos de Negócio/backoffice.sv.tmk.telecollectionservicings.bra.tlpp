#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWLIBVERSION.CH"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"

Static __aStructBra := {}   as array

namespace totvs.protheus.backoffice.sv.tmk.telecollectionservicings.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGATMK", tables="ACF,ACG", name="Atendimentos do Telecobrança", country="BRA")

//-----------------------------------------------------------------------------------
/*/{Protheus.doc} telecollectionservicingsSmartViewBusinessObjectBRA
Classe para criação do Objeto de Negócio para Atendimentos do Telecobrança Brasil.

@author FAT/CRM
@since 31/08/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------
class telecollectionservicingsSmartViewBusinessObjectBRA from totvs.protheus.backoffice.sv.tmk.telecollectionservicings.treportsintegratedprovider.telecollectionservicingsSmartViewBusinessObject

    public method new()         as object
    public method preData()     as object
    public method processData() as object
    public method preSchema()   as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author FAT/CRM
@since 31/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
method new() class telecollectionservicingsSmartViewBusinessObjectBRA

    _Super:new()

return self

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Método chamado antes do getData do objeto principal

@return object: self:oData

@author FAT/CRM
@since 31/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
method preData() as object class telecollectionservicingsSmartViewBusinessObjectBRA

    self:cSelectLoc := "ACG.ACG_IRRF,ACG.ACG_ISS,ACG.ACG_CSLL,ACG.ACG_COFINS,ACG.ACG_PIS,"

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Deverá ser chamado no momento do processamento da query

@return object: self:oData

@author FAT/CRM
@since 31/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
method processData(nOperLoc as numeric) as object class telecollectionservicingsSmartViewBusinessObjectBRA

    Local nX    := 0    as numeric

    Do Case
        //-----------------------------------------------------------
        //Operação 1 será utilizado somente para commit dos
        //campos antes do appendData
        //Case (nOperLoc == 1)
        //-----------------------------------------------------------
        Case nOperLoc == 1
            for nX := 1 To Len(__aStructBra)
                self:jItems[__aStructBra[nX][1]] := (self:cAliasTmpLoc)->&(__aStructBra[nX][5])
            Next nX

    EndCase

return

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Método chamada antes do getSchema do objeto principal

@return object: self:oSchema

@author FAT/CRM
@since 31/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
method preSchema() as object class telecollectionservicingsSmartViewBusinessObjectBRA

    Local nX            := 0    as numeric

    __aStructBra := FatTrGetStruct({"ACG_IRRF","ACG_ISS","ACG_CSLL","ACG_COFINS","ACG_PIS"})

    for nX := 1 To Len(__aStructBra)
        self:oSchema:addProperty(__aStructBra[nX][1], __aStructBra[nX][2], __aStructBra[nX][3], __aStructBra[nX][4], __aStructBra[nX][5])
    Next nX

return self:oSchema
