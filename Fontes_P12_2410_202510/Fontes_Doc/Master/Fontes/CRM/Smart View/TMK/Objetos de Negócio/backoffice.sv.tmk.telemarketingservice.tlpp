#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.tmk.telemarketingservice.ch"

Static __lLookUp := FwLibVersion() >= "20231121"
using namespace totvs.framework.treports.integratedprovider
namespace totvs.protheus.backoffice.sv.tmk.telemarketingservice.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="SUC,SUD,SU7,SUL,SUH,SUO,SU5,SU9,SB1", name="Atendimento Telemarketing", country="ALL", customTables="SU7,SUC,SUD")

//-------------------------------------------------------------------
/*/{Protheus.doc} telemarketingserviceSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relação de Atendimentos 
de Telemarketing"

@author FAT/CRM
@since 20/06/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class telemarketingserviceSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object
    public method TMKSV010CreateSQL()

    protected data aStruct      as array
    protected data jItems       as json
	protected data cAlias       as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character
    protected data aFields      as array
    protected data aFieldsValue as array

EndClass 

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author FAT/CRM
@since 21/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class telemarketingserviceSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 		//"Telemarketing"
    self:setDisplayName(STR0002)	//"Atendimento Telemarketing"
    self:setDescription(STR0003)	//"Relação de Atendimentos de Telemarketing"
    self:setIsLookUp(.T.)  
    self:setPergunte("TMK034")		//Indica o pergunte que será utilizado no relatório

	self:aStruct   := {}
	self:aFields := {}
	self:aFieldsValue := {}

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author FAT/CRM
@since 21/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData(nPage as numeric, oFilter as object) as object Class telemarketingserviceSmartViewBusinessObject 

    Local nX            := 0   as numeric
    Local nScan         := 0   as numeric
    Local lObfuscated   := .F. as logical
    Local aPDFields     := {}  as array
    Local aUC_Operaca   := {}  as array
    Local aUC_Status    := {}  as array
    Local aUD_Status    := {}  as array
    Local xValue        := ""  as character

    MV_FIL	 := ""

    //Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(),"TMK034") 
  
    /*
        Função criada p/ retornar somente campos que devem ser ofuscados,
        o inverso do que a função utilizada anteriormente nos retorna.
    */
    aPDFields := FatGetfieldsLGPD(self:aFields, self:getCustomFields()) 
    lObfuscated := Len(aPDFields ) > 0  

    self:aFieldsValue := {;  
        {"CONTATO",     {"U5_CONTAT"    , "", "Alltrim((self:cAlias)->UC_CODCONT) + ' - ' + Alltrim((self:cALias)->U5_CONTAT)"}},;
        {"LIGACAO",	    {"UC_OPERACA"   , "", "IIf((self:cAlias)->UC_OPERACA =='1',aUC_Operaca[1],aUC_Operaca[2])"}},;
        {"ENTIDADE",	{"UC_ENTIDAD"   , "", "FwSX2Util():GetX2Name((self:cAlias)->UC_ENTIDAD)"}},;						 
        {"STATUS",	    {"UC_STATUS"    , "", "IIf((self:cAlias)->UC_STATUS == '1',aUC_Status[1],IIf((self:cAlias)->UC_STATUS == '2',aUC_Status[2],aUC_Status[3]) )"}},;
        {"DESCRICAO",   {"UH_DESC"      , "", "SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE,1,,(self:cAlias)->UC_FILIAL)"}},;
        {"OPERADOR",	{"U7_NOME"      , "", "Alltrim((self:cAlias)->UC_OPERADO) + ' - ' + Alltrim((self:cAlias)->U7_NOME)"}},;
        {"MIDIA",	    {"UC_MIDIA"     , "", "Alltrim((self:cAlias)->UC_MIDIA) + ' - ' + Alltrim((self:cAlias)->UH_DESC)"}},;
        {"ENDERECO",	{"U5_END"       , "", "SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE,2,,(self:cAlias)->UC_FILIAL)"}},;
        {"COMUNICAO",	{"UC_TIPO"      , "", "Alltrim((self:cAlias)->UC_TIPO)    + ' - ' + Alltrim((self:cAlias)->UL_DESC)"}},;
        {"BAIRRO",	    {"U5_BAIRRO"    , "", "SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE,10,,(self:cAlias)->UC_FILIAL)"}},;
        {"MUNICIPIO",	{"U5_MUN"       , "", "SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE,3,,(self:cAlias)->UC_FILIAL)"}},;
        {"ESTADO",	    {"U5_EST"       , "", "SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE,4,,(self:cAlias)->UC_FILIAL)"}},;
        {"OBSERVACAO",	{"UC_CODOBS"    , "", "MSMM((self:cAlias)->UC_CODOBS)"}},;
        {"ASSUNTO",	    {"UD_ASSUNTO"   , "", "FWGetSX5('T1', (self:cAlias)->UD_ASSUNTO)[1][4]"}},;
        {"OPERADOR2",   {"UD_OPERADO"   , "", "UsrFullName((self:cAlias)->UD_OPERADO)"}},;
        {"STATUSACAO",	{"UD_STATUS"    , "", "IIf((self:cAlias)->UD_STATUS == '1',aUD_STATUS[1],aUD_STATUS[2])"}};
        }
    
    //Método que monta a Query
	self:TMKSV010CreateSQL()

    aUC_Operaca	:= TkSx3Box("UC_OPERACA") 
    aUC_Status	:= TkSx3Box("UC_STATUS") 
	aUD_Status	:= TkSx3Box("UD_STATUS")

	DbSelectArea(self:cAlias)

	//Posiciona no registro inicial
	(self:cAlias)->(dbGoTop()) 

	//Início da impressão do fluxo do relatório
	While !(self:cAlias)->(Eof())

        self:jItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)

            If (nScan := aScan(self:aFieldsValue,  {|x| x[2][1] == self:aStruct[nX][5] } )) > 0
                xValue  := FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2])
            Else
                xValue := "(self:cAlias)->"+self:aStruct[nX][5]
            Endif

            If lObfuscated .and. ( nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0
                self:jItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
       
            Elseif AllTrim(Upper(self:aStruct[nX][3])) == "DATE"
                
                self:jItems[self:aStruct[nX][1]] := IIF( ValType(&(xValue)) == "C" ,  totvs.framework.treports.date.stringToTimeStamp( &(xValue) ) , totvs.framework.treports.date.dateToTimeStamp( &(xValue) ))
            
            ElseIf self:aStruct[nX][5] == "DATA" .Or. self:aStruct[nX][5] == "RETORNO"
                xValue :=  (self:cAlias)->&((FieldName(nX)))
                IIf(!Empty(xValue), self:jItems[self:aStruct[nX][1]]  := DtoC(Stod((xValue))) + ' - ' + DiaExtenso( StoD(xValue)), "")
            Else
                self:jItems[self:aStruct[nX][1]] := &(xValue)      
            Endif
        Next nX
	
        self:processData(1)
        self:oData:appendData(self:jItems)

        (self:cAlias)->(DbSkip())

	Enddo

    (self:cAlias)->(DbCloseArea())

    FwFreeArray(aPDFields)
    FwFreeArray(aUC_Operaca)
	FwFreeArray(aUD_Status)
	FwFreeArray(aUC_Status)
    FwFreeArray(self:aFields)
    FwFreeArray(self:aFieldsValue)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 21/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object class telemarketingserviceSmartViewBusinessObject 
    
    Local aParameters	:= {} as array
    Local aCposFake	:= {} as array
    Local nX := 0 as numeric
    
    self:aFields     := {"UC_FILIAL","UC_CODIGO","U5_CONTAT","DATA","U5_FCOM1","UC_OPERACA","UC_ENTIDAD","UC_STATUS",;
                         "UH_DESC","U7_NOME","RETORNO","UC_HRPEND","UC_MIDIA","U5_END","UC_TIPO","U5_BAIRRO","UO_DESC",;
                         "UC_CODCAMP","U5_MUN","U5_EST","UC_CODOBS","UD_ASSUNTO","UD_PRODUTO","B1_DESC","UD_OCORREN",;
                         "U9_DESC","UD_OPERADO","UD_DATA","UD_STATUS","UD_DTEXEC","UD_OBS";
                        }

    aCposFake := {;
                    {"DATA"         , STR0010, "string", STR0010, "DATA"},;  //"Data"
                    {"RETORNO"      , STR0017, "string", STR0017, "RETORNO"}; //"Retorno"
            }
                    
    self:aStruct := FatTrGetStruct(self:aFields, aCposFake)

	// Cria a estrutura dos campos Padroes                                   
	If !Empty(self:aStruct)
		For nX := 1 To Len(self:aStruct)						
			self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
		Next nX			
	Endif
	
    // Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam("TMK034")

    // Adiciona novo parametro do tipo number
	self:oSchema:addParameter("MV_FIL"   , STR0038 + " ?" , "string", .T.,,,,,, FTSVHelp(,,,.T.)) // Filial    

    For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX  

    If __lLookUp       
        self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/T5",  2)        
		self:setCustomURL("MV_PAR02", "api/framework/treports/integratedprovider/v1/options/TMK034/MV_PAR02", 1)
		self:setCustomURL("MV_PAR03", "api/framework/treports/integratedprovider/v1/options/TMK034/MV_PAR03", 1)
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)
	Endif

     //Limpa a variavel Statica da memória
    __lLookUp := Nil

    FwFreeArray(aParameters)
	FwFreeArray(aCposFake)

Return self:oSchema

/*{Protheus.doc} TMKSV010CreateSQL
	Retorna todas as querys montadas
	@author TMK
	@since 21/03/2025
	@version 1.0
	@return Nil
*/

Method TMKSV010CreateSQL() Class telemarketingserviceSmartViewBusinessObject 
    Local cFieldsQry    := ""  as character
    Local cQuery        := ""  as character
    Local cNameTmp      := ""  as character
    Local nParam        := 1   as numeric
    Local aFiliais 		:= {}  as array
    Local oQuery 		:= Nil as object
	Local oTempTableBranch := Nil as object

	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)
    
	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SUC", "UC_FILIAL")

    cNameTmp := oTempTableBranch:GetTableNameForQuery()
 
    //Monta campos do Select
    cFieldsQry := "SUC.UC_FILIAL,SUC.UC_CODIGO,SUC.UC_CODCONT, SUC.UC_DATA, SUC.UC_CODOBS,SUC.UC_OPERADO, SUC.UC_OPERACA,SUC.UC_STATUS,"
	cFieldsQry += "SUC.UC_HRPEND,SUC.UC_MIDIA,SUC.UC_PENDENT,SUC.UC_TIPO, SUC.UC_CODCAMP,SUC.UC_ENTIDAD, SUC.UC_CHAVE,SU7.U7_NOME,"
    cFieldsQry += "SUD.UD_OPERADO, SUD.UD_DATA, SUD.UD_STATUS, SUD.UD_DTEXEC, SUD.UD_OBS,SUD.UD_PRODUTO,SUD.UD_ASSUNTO, SUD.UD_OCORREN,"
    cFieldsQry += "SU5.U5_CONTAT,SU5.U5_FCOM1,SU5.U5_END,SU5.U5_BAIRRO,SUO.UO_DESC,SU5.U5_MUN,SU5.U5_EST,SB1.B1_DESC,SUH.UH_DESC,SUL.UL_DESC,SU9.U9_DESC"

	//Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson({@self:aFieldsValue}, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .F., .T.)

    cQuery := "SELECT ? "
	cQuery +=  " ? "
    cQuery += " FROM " + RetSqlName("SUC") + " SUC  "
	cQuery += " INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = SUC.UC_FILIAL "
	cQuery += " INNER JOIN "+ RetSqlName("SUD") +" SUD ON SUD.UD_FILIAL = SUC.UC_FILIAL "
	cQuery += " AND SUD.UD_CODIGO = SUC.UC_CODIGO AND SUD.D_E_L_E_T_ = ?  "
	cQuery += " LEFT JOIN " + RetSqlName("SB1") +" SB1 ON ? "
	cQuery += " AND SB1.B1_COD = SUD.UD_PRODUTO AND SB1.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSqlName("SU5") +" SU5 ON ? "
	cQuery += " AND SU5.U5_CODCONT = SUC.UC_CODCONT AND SU5.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSqlName("SU7") +" SU7 ON ? "
	cQuery += " AND SU7.U7_COD = SUC.UC_OPERADO AND SU7.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SUH") +" SUH ON ? "
	cQuery += " AND SUH.UH_MIDIA = SUC.UC_MIDIA AND SUH.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SUL") +" SUL ON ? "
	cQuery += " AND SUL.UL_TPCOMUN = SUC.UC_TIPO AND SUL.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SU9") +" SU9 ON ? "
	cQuery += " AND SU9.U9_CODIGO = SUD.UD_OCORREN " 
	cQuery += " AND SU9.U9_ASSUNTO  = SUD.UD_ASSUNTO " 
	cQuery += " AND SU9.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SUO") +" SUO ON ? "
	cQuery += " AND SUO.UO_CODCAMP = SUC.UC_CODCAMP AND SUO.D_E_L_E_T_ = ? "
	cQuery += " WHERE SUC.UC_FILIAL  = TMPFIL.TMP_FILIAL "
    cQuery += " AND SUC.UC_DATA BETWEEN ? AND ? "
    cQuery += " AND SUC.UC_CODCANC = ? "
    If !Empty(MV_PAR01) 
        cQuery += " AND SUC.UC_ENTIDAD = ? "
    Endif

    If MV_PAR02 <> 3
        cQuery += "  AND ( SUC.UC_OPERACA = ? OR SUC.UC_OPERACA = ? )"
    Endif

    If MV_PAR03 <> 4 
        cQuery += " AND SUC.UC_STATUS = ? "
    EndIf
    
    cQuery += " AND SUC.UC_CHAORIG = ? "  
    cQuery += " AND SUC.D_E_L_E_T_ = ? "
    cQuery += " ? " // Adição de condições para objeto localizado no where
	cQuery += "ORDER BY UC_FILIAL, UC_CODIGO "

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))
	oQuery:SetUnsafe(nParam++ , cFieldsQry )
	oQuery:SetUnsafe(nParam++ , self:cSelectLoc)
	oQuery:SetUnsafe(nParam++ , cNameTmp)
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SB1", "SUD"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU5", "SUC"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU7", "SUC"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SUH", "SUC"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SUL", "SUC"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU9", "SUD"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SUO", "SUC"))
	oQuery:SetString(nParam++ , ' ')
    oQuery:SetString(nParam++, DTOS(MV_PAR04))
    oQuery:SetString(nParam++, DTOS(MV_PAR05))
    oQuery:SetString(nParam++ , ' ')

    If !Empty(Mv_Par01)
		oQuery:SetString(nParam++ , Mv_Par01 )
	Endif

    If MV_PAR02 <> 3
        oQuery:SetString(nParam++ , Str(MV_PAR02,1) )
        oQuery:SetString(nParam++, Space(FWSX3Util():GetFieldStruct("UC_OPERACA")[3]))
    Endif

    If MV_PAR03 <> 4 
        oQuery:SetString(nParam++, Str(MV_PAR03,1))
    EndIf

    oQuery:SetString(nParam++, Space(FWSX3Util():GetFieldStruct("UC_CHAORIG")[3]))
    oQuery:SetString(nParam++ , ' ')
    oQuery:SetUnsafe(nParam++ ,self:cWhereLoc)

    self:cALias := oQuery:OpenAlias()

	oTempTableBranch:Delete()
	FreeObj(oQuery)
	FreeObj(oTempTableBranch)
    FwFreeArray(aFiliais) 
	
Return
