#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWLIBVERSION.CH"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.tmk.collectionoccurrences.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.tmk.collectionoccurrences.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="ACF,SU7,SA1", name="Ocorrências de cobrança", country="ALL", customTables="ALL" )

//-------------------------------------------------------------------
/*/{Protheus.doc} collectionoccurrencesSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Clientes por Segmento de Negocios.

@author Squad CRM/Faturamento
@since 13/05/2025
@version 1.0
*/
//-------------------------------------------------------------------  
class collectionoccurrencesSmartViewBusinessObject from integratedProvider

    public method new()			as object
    public method getData()		as object
    public method getSchema()	as object
	public method TMKSV017CreateSQL()
	
    protected data aFields		as array
    protected data aStruct		as array
    protected data ojItems		as json
    protected data cSelectLoc	as character
    protected data cWhereLoc	as character
	protected data cAlias 		as character
	protected data cGroupByLoc	as character

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Squad CRM/Faturamento
@since 13/05/2025
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class collectionoccurrencesSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 		//"Telemarketing"
    self:setDisplayName(STR0002)	//"Ocorrências de cobrança"
    self:setDescription(STR0003)	//"Relação de Ocorrências de cobrança"
    self:setIsLookUp(.T.)  			//Indica o uso do Lookup

return self
 
//-------------------------------------------------------------------
/*/{Protheus.doc} collectionoccurrencesSmartViewBusinessObject
Classe para criação do Objeto de Negócio

@return object: self

@author Squad CRM/Faturamento
@since 13/05/2025
@version 1.0
*/
//-------------------------------------------------------------------  
method getData(nPage as numeric, oFilter as object) as object class collectionoccurrencesSmartViewBusinessObject
		
    Local aPDFields     := {}   as array																												           															
	Local nX			:= 0 	as numeric			
	Local nScan			:= 0 	as numeric															
	Local lObfuscated   := .F.  as logical
	Local xValue		:= "" 	as variant

	MV_FIL 		:= "" //Filial
	MV_DOOPER	:= "" //Do Operador
	MV_ATEOPER	:= "" //Ate Operador
    
	FatSetValueMVPAR(oFilter:GetParameters(),"TMK042") //Metodo para retorno do json dos parametros

	//Verifica se precisa fazer o tratamento para LGPD   
    aPDFields	:= FatGetfieldsLGPD(self:aFields, {})
    lObfuscated	:= Len(aPDFields) > 0

	//Método que monta a Query
	self:TMKSV017CreateSQL()
	
	//Posiciona no registro inicial
    (self:cAlias)->(dbGoTop())

	While (self:cAlias)->(!Eof())
		
		self:ojItems := JsonObject():new()
						
		For nX := 1 To Len(self:aStruct)

			xValue := (self:cAlias)->(FieldGet(FieldPos(self:aStruct[nX][5])))
					
			If lObfuscated .And. ( nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) ) > 0 				
				self:ojItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
			Else
				Do Case
					Case UPPER(self:aStruct[nX][3]) == "DATE"
						self:ojItems[self:aStruct[nX][1]] := IIf( ValType(xValue) == "C", ;
															 totvs.framework.treports.date.stringToTimeStamp( xValue), ;
															 totvs.framework.treports.date.dateToTimeStamp( xValue) )
					Case UPPER(self:aStruct[nX][3]) == "STRING"
						self:ojItems[self:aStruct[nX][1]] := Alltrim(xValue)
					Otherwise
						self:ojItems[self:aStruct[nX][1]] := (xValue)
				EndCase
			EndIf

		Next nX
			
		self:processData(1) //Tratamento para commit localizado
		self:oData:appendData(self:ojItems)

		(self:cAlias)->(DbSkip())

	EndDo
    
	//Fechamento de tabelas e limpeza do objeto/variável
    (self:cAlias)->(DBCloseArea())

	aSize(aPDFields,0)
	
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author Squad CRM/Faturamento
@since 13/05/2025
@version 1.0
*/
//-------------------------------------------------------------------   
method getSchema() as object class collectionoccurrencesSmartViewBusinessObject
	
	Local nX 			:= 0 as numeric
	Local aParameters	:= {} as array

	//Campos Padroes
    self:aFields := {'ACF_OPERAD','U7_NOME','ACF_DATA','ACF_INICIO','ACF_MOTIVO','A1_CGC',;
					'A1_TEL','A1_NOME','ACF_CLIENT','ACF_LOJA','ACF_CODIGO','ACF_FILIAL','U9_DESC','ACG_VALOR'}
		
	//Busca a estrutura dos campos
	self:aStruct := FatTrGetStruct(self:aFields)

	//Adiciona campos padroes
	For nX := 1 To Len(self:aStruct)						
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX	

	// Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('TMK042', {'MV_PAR01','MV_PAR02','MV_PAR07'})

	// Adiciona novo parametro do tipo string
	self:oSchema:addParameter("MV_FIL"		, STR0006 + " ?" , "string", .T.,,,,,, FTSVHelp(,,,.T.)) // Filial 
	self:oSchema:addParameter("MV_DOOPER"	, STR0007 + " ?" , "string", .F.,,,,,, FTSVHelp("TMK009", "01")) // Do operador  
	self:oSchema:addParameter("MV_ATEOPER"	, STR0008 + " ?" , "string", .F.,,,,,, FTSVHelp("TMK009", "02")) // Ate operador  
	
	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX
	
	// Adiciona Lookup nos parametros que possuem consulta padrao
	If __lLookUp
		self:setCustomURL("MV_DOOPER"	,"api/framework/v1/genericLookupService/smartview/SU7"	, 2) //Do operador ?                 
		self:setCustomURL("MV_ATEOPER"	,"api/framework/v1/genericLookupService/smartview/SU7"	, 2) //Ate o operador ?              
		self:setCustomURL("MV_PAR06"	,"api/framework/v1/genericLookupService/smartview/SU9"	, 2) //Ocorrencia ? 
		self:setCustomURL("MV_PAR05"	,"api/framework/treports/integratedprovider/v1/options/TMK042/MV_PAR05", 1)
		self:setCustomURL("MV_FIL"		,"api/framework/v1/genericLookupService/smartview/SM0"	, 2) //Filial ?                  
	EndIf

	FwFreeArray(aParameters)

return self:oSchema

//------------------------------------------------------------------
/*{Protheus.doc} TMKSV017CreateSQL
    Método responsável por montar a query SQL.

@return character: Retorna a query SQL montada.
 
@author Squad CRM/Faturamento
@since 07/05/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method TMKSV017CreateSQL() class collectionoccurrencesSmartViewBusinessObject
	
	Local cFieldsQry	:= "" as character
	Local cQuery		:= "" as character
	Local cCpoPersonSQL := "" as character
	Local cNameTmp 		:= "" as character
	Local nParam        := 1  as numeric
	Local aFiliais 		:= {} as array
	Local oQuery 	    := Nil as object
	Local oTempTableBranch := Nil as object

	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "ACF", "ACF_FILIAL")
	cNameTmp := oTempTableBranch:GetTableNameForQuery()

	//Trata campos do aFields para utilizar na query
	cFieldsQry := "ACF.ACF_FILIAL,ACF_OPERAD,U7_NOME,ACF_DATA,ACF_INICIO,ACF_MOTIVO,A1_CGC,A1_TEL,A1_NOME,ACF_CLIENT,ACF_LOJA,"
	cFieldsQry += "ACF_CODIGO,SUM(ACG_VALOR) ACG_VALOR,ACG_CODIGO,U9_DESC"
	cFieldsQry += self:cSelectLoc
	
	//Verifica se tem campos customizados
	cCpoPersonSQL := self:getSQLFields(.T.,Nil,.T.,.F.)
	
	//Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson({}, @self:aStruct , @cFieldsQry , self:getCustomFields(), .T. , .F. , .T.)

    cQuery  := " SELECT ? "  			
	cQuery += " FROM " + RetSqlName("ACF") + " ACF "
	cQuery += " INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = ACF.ACF_FILIAL "
	cQuery += " INNER JOIN " + RetSqlName("SA1") + " SA1 "
	cQuery += " ON ?"
	cQuery += " AND SA1.A1_COD 		= ACF_CLIENT"
	cQuery += " AND SA1.A1_LOJA 	= ACF_LOJA"
	cQuery += " AND SA1.D_E_L_E_T_ 	= ?"
	cQuery += " INNER JOIN " + RetSqlName("SU7") + " SU7 "
	cQuery += " ON ?"
	cQuery += " AND SU7.U7_COD 		= ACF_OPERAD"
	cQuery += " AND SU7.D_E_L_E_T_ 	= ?"
	cQuery += " LEFT JOIN "  + RetSqlName("SU9") + " SU9 "
	cQuery += "	ON ? "
	cQuery += "	AND SU9.U9_CODIGO  = ACF_MOTIVO "
	cQuery += "	AND SU9.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN "  + RetSqlName("ACG") + " ACG "
	cQuery += "	ON ? "
	cQuery += "	AND ACG_CODIGO		= ACF_CODIGO "
	cQuery += "	AND ACG.D_E_L_E_T_	= ? "

	cQuery += " WHERE ACF_FILIAL	= TMPFIL.TMP_FILIAL " 

	If MV_PAR05 <> 3
		cQuery	+= " AND ACF_OPERA = ? " 
	Endif

	If !Empty( MV_PAR06 )
		cQuery += " AND ACF_MOTIVO = ? " 
	Endif

	cQuery += " AND ACF_OPERAD 	BETWEEN ? AND ? "             
	cQuery += " AND ACF_DATA 	BETWEEN ? AND ? "                     
	cQuery += " AND ACF.D_E_L_E_T_ = ? "
	cQuery += " ? "
	cQuery += " GROUP BY ACF_FILIAL, ACF_OPERAD, ACF_CODIGO, ACG_CODIGO, U7_NOME, ACF_DATA, ACF_INICIO,"
    cQuery += " ACF_MOTIVO, A1_CGC, A1_TEL, A1_NOME, ACF_CLIENT, ACF_LOJA, ACG_TITULO, U9_DESC "
	If !Empty(cCpoPersonSQL)
		cQuery += ", ? "
	EndIf 
	cQuery += " ? "
	cQuery += " ORDER BY ACF_FILIAL, ACF_OPERAD, ACF_DATA "
		
	oQuery := FwExecStatement():New(ChangeQuery(cQuery))

	oQuery:SetUnsafe(nParam++ , cFieldsQry) 
	oQuery:SetUnsafe(nParam++ , cNameTmp)
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SA1", "ACF"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU7", "ACF"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU9", "ACF"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("ACG", "ACF"))
	oQuery:SetString(nParam++ , ' ')

	If MV_PAR05 <> 3
		oQuery:SetString(nParam++, Alltrim(Str(MV_PAR05)))                    
	EndIf

	If !Empty( MV_PAR06 )
    	oQuery:SetString(nParam++, MV_PAR06) 
	EndIf

    oQuery:SetString(nParam++, MV_DOOPER) 		 
    oQuery:SetString(nParam++, MV_ATEOPER)		
    oQuery:SetDate(nParam++, MV_PAR03)			
    oQuery:SetDate(nParam++, MV_PAR04)			
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, self:cWhereLoc)

	If !Empty(cCpoPersonSQL)
		oQuery:SetUnsafe(nParam++, cCpoPersonSQL) 
	EndIf

	oQuery:SetUnsafe(nParam++, self:cGroupByLoc) 

	self:cAlias := oQuery:OpenAlias()
	
	(oQuery:Destroy())
	FreeObj(oQuery)
	oTempTableBranch:Delete()
	FWFreeObj(oTempTableBranch)

	FwFreeArray(aFiliais) 
	
Return
