#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.tmk.salesranking.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.tmk.salesranking.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="SUA,SA3,SU7", name="Ranking de Vendas", country="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} salesrankingSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Ranking de Vendas.

@author Squad CRM/Faturamento
@since 30/04/2025
@version 1.0
*/
//-------------------------------------------------------------------
class salesrankingSmartViewBusinessObject from integratedProvider

    public method new()			as object
    public method getData()		as object
    public method getSchema()	as object
	public method TMKSV015CreateSQL()

    protected data aFields		as array
    protected data aStruct		as array
    protected data jItems		as json
    protected data cSelectLoc	as character
    protected data cWhereLoc	as character
	protected data cAlias 		as character
	
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Squad CRM/Faturamento
@since 30/04/2025
@version 1.0
*/
//-------------------------------------------------------------------
method new() class salesrankingSmartViewBusinessObject

	_Super:new()
    self:appendArea(STR0001) 		//"Telemarketing"
    self:setDisplayName(STR0002)	//"Ranking de Vendas"
    self:setDescription(STR0003)	//"Relação de Ranking de Vendas"
    self:setIsLookUp(.T.)  

	self:aStruct := {}
    self:aFields := {}

return self
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView

@return object: self:oData

@author Squad CRM/Faturamento
@since 30/04/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class salesrankingSmartViewBusinessObject

	Local cNome			:= ""	as character
	Local nX            := 0    as numeric
	Local nY            := 0    as numeric
	Local nTotal		:= 0    as numeric
	Local nScan         := 0    as numeric
	Local nScanOper		:= 0    as numeric
    Local lObfuscated   := .F.  as logical
    Local aPDFields     := {}   as array
	Local aValueJson	:= {}  	as array
	
	MV_TTLOP := 1 
	MV_FIL   := ""

    FatSetValueMVPAR(oFilter:GetParameters(),"TMK004") 

    aPDFields	:= FatGetfieldsLGPD(self:aFields, {})
    lObfuscated	:= Len(aPDFields) > 0

	//Montar a Query
	self:TMKSV015CreateSQL()
	
	(self:cAlias)->(DbGoTop())

	While !(self:cAlias)->(Eof())
		If (nScan := aScan(aValueJson, {|x| x[1]==(self:cAlias)->UA_VEND .And. x[6] == (self:cAlias)->UA_FILIAL})) > 0
			
			aValueJson[nScan][3] += (self:cAlias)->UA_VLRLIQ
			If MV_TTLOP == 1
				If (nScanOper := aScan(aValueJson[nScan][5], {|x| x[1] == (self:cAlias)->UA_OPERADO })) > 0
					aValueJson[nScan][5][nScanOper][3] += (self:cAlias)->UA_VLRLIQ
				Else
					cNome := IIf( lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize((self:cAlias)->U7_NOME), (self:cAlias)->U7_NOME )
					aAdd(aValueJson[nScan][5], {(self:cAlias)->UA_OPERADO, cNome, (self:cAlias)->UA_VLRLIQ, 0})
				EndIf
			EndIf
		Else
			If lObfuscated
				cNome := FwProtectedDataUtil():ValueAsteriskToAnonymize((self:cAlias)->A3_NOME)
			Else
				cNome := (self:cAlias)->A3_NOME
			EndIf
			aAdd(aValueJson,{(self:cAlias)->UA_VEND, cNome, (self:cAlias)->UA_VLRLIQ, 0, {}, (self:cAlias)->UA_FILIAL})
			nScan := Len(aValueJson)

			If MV_TTLOP == 1
				cNome := IIf( lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize((self:cAlias)->U7_NOME), (self:cAlias)->U7_NOME )
				aAdd(aValueJson[nScan][5], {(self:cAlias)->UA_OPERADO, cNome, (self:cAlias)->UA_VLRLIQ, 0})
			EndIf
		EndIf

		nTotal += (self:cAlias)->UA_VLRLIQ

		(self:cAlias)->(dbSkip())
	EndDo
	
	//Fechamento de tabelas
	(self:cAlias)->(DBCloseArea())

	For nX := 1 To Len(aValueJson)
		//Vendedor
		self:jItems := JsonObject():new()

		self:jItems['TBTIPO']	:= STR0005	
		self:jItems['UAVEND']	:= aValueJson[nX][1]
		self:jItems['A3NOME']	:= aValueJson[nX][2]
		self:jItems['TBVFATVD']	:= aValueJson[nX][3]
		self:jItems['TBRANKVD']	:= Round(((aValueJson[nX][3] / nTotal) * 100), 2)
		self:jItems['UAFILIAL'] := aValueJson[nX][6]

		self:processData(1) 
		self:oData:appendData(self:jItems)

		//Operador
		For nY := 1 To Len(aValueJson[nX][5])
			self:jItems := JsonObject():new()

			self:jItems['TBTIPO']	:= STR0006	//"Operador"
			self:jItems['UAVEND']	:= aValueJson[nX][1]
			self:jItems['A3NOME']	:= aValueJson[nX][2]
			self:jItems['TBVFATVD']	:= aValueJson[nX][3]
			self:jItems['TBRANKVD']	:= Round(100, 2)
			self:jItems['UAOPERADO']:= aValueJson[nX][5][nY][1]
			self:jItems['U7NOME']	:= aValueJson[nX][5][nY][2]
			self:jItems['TBVFATOP']	:= aValueJson[nX][5][nY][3]
			self:jItems['TBRANKOP']	:= Round(((aValueJson[nX][5][nY][3] / aValueJson[nX][3]) * 100), 2)
			self:jItems['UAFILIAL'] := aValueJson[nX][6]

			self:processData(1) 
			self:oData:appendData(self:jItems)
		Next

	Next

	//Limpeza das variáveis tipo array
	aSize(aPDFields, 0)
	FwFreeArray(aValueJson)

return self:oData
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author Squad CRM/Faturamento
@since 30/04/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class salesrankingSmartViewBusinessObject

	Local nX 			:= 0  as numeric
	Local aCposFake		:= {} as array
	Local aParameters	:= {} as array

	aCposFake := {	{'TB_TIPO'	, STR0004, 'string'},; //"Tipo"
					{'TB_VFATVD', STR0007, 'number'},; //"Valor Faturado(Vendedor)"
					{'TB_RANKVD', STR0008, 'number'},; //"% Ranking(Vendedor)"
					{'TB_VFATOP', STR0009, 'number'},; //"Valor Faturado(Operador)"
					{'TB_RANKOP', STR0010, 'number'} } //"% Ranking(Operador)"

	self:aFields := {"UA_FILIAL","TB_TIPO","UA_VEND","A3_NOME","TB_VFATVD","TB_RANKVD","UA_OPERADO","U7_NOME","TB_VFATOP","TB_RANKOP"}

    self:aStruct := FatTrGetStruct(self:aFields, aCposFake)

	For nX := 1 To Len(self:aStruct)
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX

	aParameters := FtSVSetParam('TMK004', {'MV_PAR05'} )

	self:oSchema:addParameter("MV_FIL",	STR0012 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.)) // Filial

	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter( aParameters[nx][1] , aParameters[nx][2] , aParameters[nx][3] , aParameters[nx][4],,,,,, aParameters[nX][5] )
	Next nX

    self:oSchema:addParameter("MV_TTLOP" , STR0011 + " ?", "number", .F.,,,,,, FTSVHelp("TMK004", "05")) //Total por Operador

    If __lLookUp
		self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SA3", 2)
		self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SA3", 2)
		self:setCustomURL("MV_TTLOP", "api/framework/treports/integratedprovider/v1/options/TMK004/MV_PAR05", 1)
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2) // Filial
	EndIf

    FwFreeArray(aParameters)
	FwFreeArray(aCposFake)

return self:oSchema

//------------------------------------------------------------------
/*{Protheus.doc} TMKSV015CreateSQL
    Método responsável por montar a query SQL.

@return character: Retorna a query SQL montada.
 
@author Squad CRM/Faturamento
@since 30/04/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method TMKSV015CreateSQL() class salesrankingSmartViewBusinessObject

	Local aFiliais		:= {}	as array
	Local cNameTmp	 	:= "" 	as character
	Local cQuery        := ""   as character
	Local cFieldsQry	:= ""	as character
	Local nParam        := 1    as numeric
	Local oQuery 	    	:= Nil  as object
	Local oTempTableBranch 	:= Nil 	as object

	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SUA", "UA_FILIAL")
	cNameTmp := oTempTableBranch:GetTableNameForQuery()

	//Trata campos do aFields para utilizar na query
	cFieldsQry := 'UA_FILIAL, UA_VEND, A3_NOME, UA_OPERADO, U7_NOME, UA_CANC, UA_EMISSAO, UA_OPER, UA_VLRLIQ'
	cFieldsQry += self:cSelectLoc

    cQuery := "SELECT ? "
	cQuery += "FROM " 		+ RetSqlName("SUA") + " SUA "
	cQuery += "INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = SUA.UA_FILIAL "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SA3") + " SA3 "
	cQuery += "	ON ? "
	cQuery += "	AND SA3.A3_COD      = SUA.UA_VEND "
	cQuery += "	AND SA3.D_E_L_E_T_  = ? "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SU7") + " SU7 "
	cQuery += "	ON ? "
	cQuery += "	AND SU7.U7_COD      = SUA.UA_OPERADO "
	cQuery += "	AND SU7.D_E_L_E_T_  = ? "
	cQuery += "WHERE SUA.UA_FILIAL 	= TMPFIL.TMP_FILIAL "
	cQuery += "	AND SUA.UA_VEND    BETWEEN ? AND ? "
	cQuery += "	AND SUA.UA_EMISSAO BETWEEN ? AND ? "
	cQuery += "	AND SUA.UA_OPER 	= ? "
	cQuery += "	AND SUA.UA_CANC 	= ? "
	cQuery += "	AND SUA.D_E_L_E_T_ 	= ? "
	cQuery += " ? "
	cQuery += "ORDER BY SUA.UA_FILIAL, SUA.UA_VEND, SUA.UA_OPERADO "

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))
	
	oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetUnsafe(nParam++, cNameTmp)
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SA3", "SUA"))
	oQuery:SetString(nParam++, ' ')	
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SU7", "SUA"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetString(nParam++, MV_PAR01)
	oQuery:SetString(nParam++, MV_PAR02)
	oQuery:SetDate(nParam++, MV_PAR03)
	oQuery:SetDate(nParam++, MV_PAR04)
	oQuery:SetString(nParam++, '1')
	oQuery:SetString(nParam++, ' ')
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, self:cWhereLoc)

	self:cAlias := oQuery:OpenAlias()
    
	oTempTableBranch:Delete()

	FreeObj(oQuery)
	FreeObj(oTempTableBranch)
	aSize(aFiliais, 0)

Return

