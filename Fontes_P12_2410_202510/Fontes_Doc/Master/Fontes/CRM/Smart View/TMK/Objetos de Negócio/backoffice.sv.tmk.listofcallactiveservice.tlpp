#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.tmk.listofcallactiveservice.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.tmk.listofcallactiveservice.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="SUA,SUB,SB1,SE4,SA4,SU7,SA3,SUH,SUO,SU9,SA1,SUS", name="Ligações realizadas no atendimento de Televendas", country="ALL", customTables="SUA,SUB,SB1")

//-------------------------------------------------------------------
/*/{Protheus.doc} listofcallactiveserviceSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Ligações realizadas no atendimento de Televendas
para embalagens

@author FAT/CRM
@since 01/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class listofcallactiveserviceSmartViewBusinessObject From integratedProvider

    public method new()			as object
    public method getData()		as object
    public method getSchema()	as object
	public method SVOfuscated()	as character
	public method TmkSv004CreateSQL()

    protected data aFields		as array
    protected data aStruct		as array
	protected data aFieldsValue as array
    protected data ojItems		as json
    protected data cAlias		as character
    protected data cSelectLoc	as character
    protected data cWhereLoc	as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author FAT/CRM
@since 01/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class listofcallactiveserviceSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 		//"Telemarketing"
    self:setDisplayName(STR0002)	//"Atendimento Ativo"
    self:setDescription(STR0003)	//"Ligações realizadas no atendimento de Televendas"
    self:setIsLookUp(.T.)

	self:cAlias 		:= ""
	self:aStruct   		:= {}
	self:aFieldsValue   := {}

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio.

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView

@return object: self:oData

@author FAT/CRM
@since 01/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class listofcallactiveserviceSmartViewBusinessObject

	Local cDescEnt		:= ""  	as character
	Local cEndCliSus	:= ""   as character
	Local cBaiCliSus	:= ""   as character
	Local cMunCliSus	:= ""   as character
	Local cEstCliSus	:= ""   as character
	Local cDDDCliSus	:= ""   as character
	Local cTelCliSus	:= ""   as character
	Local xValue		:= ""	as character
	Local nX            := 0    as numeric
	Local nScan         := 0    as numeric
    Local lObfuscated   := .F.  as logical
    Local aPDFields     := {}   as array
	Local aUA_Oper		:= {}  	as array
	Local aUA_Tmk		:= {}  	as array
	Local aUA_TpFrete	:= {}  	as array

	MV_TPENT := 1
	MV_FIL   := ""

	//Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "TMK016")

	//Array para processamento
	self:aFieldsValue := {	{'TB_TABENT',	{'TB_TABENT' 	,"", 'cDescEnt'}},;
					 		{'TB_EMPRESA',	{'TB_EMPRESA'  	,"", 'AllTrim((self:cAlias)->UA_CLIENTE) + " / " + AllTrim((self:cAlias)->UA_LOJA) + " - " + cNomCliSus'}},;
					 		{'TB_CONDPAG',	{'TB_CONDPAG'  	,"", 'AllTrim((self:cAlias)->UA_CONDPG)  + " - " + AllTrim((self:cAlias)->E4_DESCRI)'}},;
					 		{'UA_OPER',		{'UA_OPER'      ,"", 'aUA_Oper[Val(AllTrim((self:cAlias)->UA_OPER))]' }},;
					 		{'TB_ENDEREC',	{'TB_ENDEREC'	,"", 'cEndCliSus'}},;
					 		{'UA_TPFRETE',	{'UA_TPFRETE'	,"", 'IIF((nScan := aScan(aUA_TpFrete, AllTrim((self:cAlias)->UA_TPFRETE))) > 0 , aUA_TpFrete[nScan], "")'}},;
					 		{'UA_TMK',		{'UA_TMK'       ,"", 'IIF(!Empty(AllTrim((self:cAlias)->UA_TMK)),aUA_Tmk[Val(AllTrim((self:cAlias)->UA_TMK))],"")' }},;
					 		{'TB_BAIRRO',	{'TB_BAIRRO'    ,"", 'cBaiCliSus'}},;
							{'TB_TRANSP',	{'TB_TRANSP'    ,"", 'AllTrim((self:cAlias)->UA_TRANSP) + " - " + FTPDObfuscate(AllTrim((self:cAlias)->A4_NREDUZ), "A4_NREDUZ",, .T.)'}},; 
							{'TB_OPERAD',	{'TB_OPERAD'    ,"", 'AllTrim((self:cAlias)->UA_OPERADO) + " - " + FTPDObfuscate(AllTrim((self:cAlias)->U7_NREDUZ), "U7_NREDUZ",, .T.)'}},;
					 		{'TB_CIDADE',	{'TB_CIDADE'    ,"", 'cMunCliSus + " - " + cEstCliSus'}},;
					 		{'TB_VEND',		{'TB_VEND'      ,"", 'AllTrim((self:cAlias)->UA_VEND) + " - " + FTPDObfuscate(AllTrim((self:cAlias)->A3_NREDUZ), "A3_NREDUZ",, .T.)'}},;
							{'TB_TEL', 		{'TB_TEL'		,"", 'cDDDCliSus + " - " + cTelCliSus'}},;
					 		{'TB_MIDIA',	{'TB_MIDIA'	  	,"", 'AllTrim((self:cAlias)->UA_MIDIA) + " - " + AllTrim((self:cAlias)->UH_DESC)'}},;
					 		{'TB_CONTATO',	{'TB_CONTATO'  	,"", 'AllTrim((self:cAlias)->UA_CODCONT) + " - " + cNomeCont'}},;
					 		{'TB_DESCAM',	{'TB_DESCAM'    ,"", 'AllTrim((self:cAlias)->UA_CODCAMP) + " - " + AllTrim((self:cAlias)->UO_DESC)'}},;
					 		{'TB_FONE1',	{'TB_FONE1'     ,"", 'cDDDCont + " - " + cTeleCont'}},;
					 		{'TB_OCORREN',	{'TB_OCORREN'  	,"", 'AllTrim((self:cAlias)->UA_CODLIG) + " - " + AllTrim((self:cAlias)->U9_DESC)'}},;
					 		{'TB_EMAIL',	{'TB_EMAIL'     ,"", 'cEmaiCont'}},;
					 		{'TB_RETORNO',	{'TB_RETORNO'	,"", 'DtoC(sTOd(AllTrim((self:cAlias)->UA_PROXLIG))) + " - " +  AllTrim((self:cAlias)->UA_HRPEND) + " - " + (cDia:= Tk_DiaSemana(AllTrim((self:cAlias)->UA_PROXLIG),Nil,Nil))'}};
					 	}

    //Verifica se precisa fazer o tratamento para LGPD   
    aPDFields 	:= FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated := Len(aPDFields) > 0

	aUA_Oper	:= TkSx3Box("UA_OPER")
	aUA_Tmk		:= TkSx3Box("UA_TMK")
	aUA_TpFrete	:= TkSx3Box("UA_TPFRETE")

	// Chama o método que monta a query
    self:TmkSV004CreateSQL()

	DbSelectArea(self:cAlias)
    (self:cAlias)->(dbGoTop())

	While !(self:cAlias)->(Eof())

		If (self:cAlias)->UA_PROSPEC == 'T'
			cNomCliSus := self:SVOfuscated("US_NOME")
			cEndCliSus := self:SVOfuscated("US_END")
			cBaiCliSus := self:SVOfuscated("US_BAIRRO")
			cMunCliSus := self:SVOfuscated("US_MUN")
			cEstCliSus := self:SVOfuscated("US_EST")
			cDDDCliSus := self:SVOfuscated("US_DDD")
			cTelCliSus := self:SVOfuscated("US_TEL")
			cDescEnt   := STR0022
		Else
			cNomCliSus := self:SVOfuscated("A1_NOME")
			cEndCliSus := self:SVOfuscated("A1_END")
			cBaiCliSus := self:SVOfuscated("A1_BAIRRO")
			cMunCliSus := self:SVOfuscated("A1_MUN")
			cEstCliSus := self:SVOfuscated("A1_EST")
			cDDDCliSus := self:SVOfuscated("A1_DDD")
			cTelCliSus := self:SVOfuscated("A1_TEL")
			cDescEnt   := STR0023
		EndIf

		cNomeCont 	:= self:SVOfuscated("U5_CONTAT")
		cDDDCont	:= self:SVOfuscated("U5_DDD")
		cTeleCont	:= self:SVOfuscated("U5_FCOM1")
		cEmaiCont	:= self:SVOfuscated("U5_EMAIL")

		self:ojItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)

			If (nScan := aScan(self:aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } ) ) > 0
				xValue  := FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2])
			Else
				xValue  := "(self:cAlias)->"+self:aStruct[nX][5]
			EndIf

			//Verificação para tratamento de LGPD e campos tipo Data antes informar no JSON
			If lObfuscated .and. (nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0 
				self:ojItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
			ElseIf UPPER(self:aStruct[nX][3]) == "DATE"
				self:ojItems[self:aStruct[nX][1]] := Iif(ValType(&(xValue)) == "C", totvs.framework.treports.date.stringToTimeStamp( &(xValue) ), totvs.framework.treports.date.dateToTimeStamp( &(xValue) ))
			Else
				self:ojItems[self:aStruct[nX][1]] := &(xValue)
			EndIf

		Next nX

		self:processData(1)
        self:oData:appendData(self:ojItems)

		(self:cAlias)->(DbSkip())

    EndDo

    (self:cAlias)->(DBCloseArea())

	FwFreeArray(aUA_Tmk)
	FwFreeArray(aUA_Oper)
	FwFreeArray(aPDFields)
	FwFreeArray(aUA_TpFrete)
	FwFreeArray(self:aFieldsValue)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object Class listofcallactiveserviceSmartViewBusinessObject

	Local nX 			:= 0  as numeric
	Local aStruCpoFake	:= {} as array
	Local aParameters	:= {} as array

    self:aFields := {'UA_FILIAL','UA_NUM','TB_TABENT','UA_TABELA','UA_EMISSAO','TB_EMPRESA','TB_CONDPAG',;
					 'UA_OPER','TB_ENDEREC','UA_TPFRETE','UA_TMK','TB_BAIRRO','TB_TRANSP',;
					 'TB_OPERAD','TB_CIDADE','UA_ENDENT','TB_VEND','TB_TEL','UA_BAIRROE',;
					 'TB_MIDIA','TB_CONTATO','UA_MUNE','TB_DESCAM','TB_FONE1','UA_ESTE',;
					 'TB_OCORREN','TB_EMAIL','TB_RETORNO','UA_CODOBS','UB_ITEM','UB_PRODUTO',;
					 'B1_DESC','UB_SITPROD','UB_QUANT','UB_VRUNIT','UB_VLRITEM','UB_DESC','UB_VALDESC',;
					 'UB_ACRE','UB_VALACRE','UB_TES','UB_CF','UB_PRCTAB','UB_BASEICM','UB_LOCAL','UB_UM',;
					 'UB_DTENTRE','UB_LOTE','UB_SUBLOTE','UB_DTVALID'}

	//Array com estrutura dos campos Fake para montar o addproperty
    aStruCpoFake := {   {"TB_TABENT" ,	STR0004,    "string"},; // Entidade
                        {"TB_EMPRESA",	STR0005,    "string"},; // Razao Social
                        {"TB_CONDPAG",	STR0006,    "string"},; // Cond. Pagamento
                        {"TB_ENDEREC",	STR0007,    "string"},; // Endereço
                        {"TB_BAIRRO" ,	STR0008,    "string"},; // Bairro
						{"TB_TRANSP" ,	STR0009,    "string"},; // Transportadora
						{"TB_OPERAD" ,	STR0010,    "string"},; // Operador
						{"TB_CIDADE" ,	STR0011,    "string"},; // Cidade
                        {"TB_VEND"   ,	STR0012,    "string"},; // Vendedor
                        {"TB_TEL"	 ,	STR0013,    "string"},; // Telefone
						{"TB_MIDIA"	 ,	STR0014,    "string"},; // Midia
						{"TB_CONTATO",	STR0015,    "string"},; // Contato
                        {"TB_DESCAM" ,	STR0016,    "string"},; // Campanha
                        {"TB_FONE1"  ,	STR0017,    "string"},; // Fone Com. 1
                        {"TB_OCORREN",	STR0018,    "string"},; // Ocorrência
                        {"TB_EMAIL"  ,	STR0019,    "string"},; // E-Mail
                        {"TB_RETORNO",	STR0020,    "string"} } // Descrição

    self:aStruct := FatTrGetStruct(self:aFields, aStruCpoFake)

	// Cria a estrutura dos campos Padroes
	For nX := 1 To Len(self:aStruct)
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX

    // Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('TMK016', {'MV_PAR08'})

	self:oSchema:addParameter("MV_FIL",	STR0025 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.))	// Filial

	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

	// Adiciona novo parametro do tipo number
    self:oSchema:addParameter("MV_TPENT" , STR0024 + " ?", "number", .F.,,,,,, FTSVHelp("TMK016","08")) //Tipo da Entidade

    If __lLookUp
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2) // Filial
		self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SU7", 2) // Do Operador
        self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SU7", 2) // Ate o Operador
        self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/SUH", 2) // Da Midia
        self:setCustomURL("MV_PAR06", "api/framework/v1/genericLookupService/smartview/SUH", 2) // Ate a Midia
        self:setCustomURL("MV_PAR07", "api/framework/v1/genericQuery?tables=SUO&fields=uo_codcamp,uo_desc&DeletedFilter=true&Format=smartview&KeyProperty=uo_codcamp", 2) // Campanha
		self:setCustomURL("MV_TPENT", "api/framework/treports/integratedprovider/v1/options/TMK016/MV_PAR08", 1) // Tipo da entidade
	EndIf

    FwFreeArray(aParameters)
	FwFreeArray(aStruCpoFake)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} TmkSv004CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 07/02/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method TmkSv004CreateSQL() Class listofcallactiveserviceSmartViewBusinessObject

    Local nParam	 	    := 1   as numeric
    Local cQuery  	 	    := ""  as character
    Local cFieldsQry        := ""  as character
    Local cNameTmp          := ""  as character
    Local oQuery 	 	    := Nil as object
	Local oTempTableBranch	:= Nil as object
	Local aFiliais	 		:= {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SUA", "UA_FILIAL")
	cNameTmp := oTempTableBranch:GetTableNameForQuery()

	cFieldsQry := "SUA.UA_FILIAL, SUA.UA_NUM, SUA.UA_TABELA, SUA.UA_EMISSAO, SUA.UA_CODOBS, SUA.UA_MIDIA, SUA.UA_MUNE,"
	cFieldsQry += "SUA.UA_OPER, SUA.UA_TPFRETE, SUA.UA_TMK, SUA.UA_ENDENT, SUA.UA_BAIRROE, SUA.UA_ESTE,"
	cFieldsQry += "SUA.UA_PROSPEC, SUA.UA_CLIENTE, SUA.UA_LOJA, SUA.UA_CONDPG, SUA.UA_OPERADO, SUA.UA_VEND,"
	cFieldsQry += "SUA.UA_CODCAMP, SUA.UA_CODLIG, SUA.UA_PROXLIG, SUA.UA_HRPEND, SUA.UA_TRANSP,SUA.UA_CODCONT,"
	cFieldsQry += "SUB.UB_SITPROD, SUB.UB_QUANT, SUB.UB_VRUNIT, SUB.UB_VLRITEM, SUB.UB_DESC, SUB.UB_VALDESC, SUB.UB_ITEM,"
	cFieldsQry += "SUB.UB_ACRE, SUB.UB_VALACRE, SUB.UB_TES, SUB.UB_CF, SUB.UB_PRCTAB, SUB.UB_BASEICM, SUB.UB_LOCAL,"
	cFieldsQry += "SUB.UB_DTENTRE, SUB.UB_LOTE, SUB.UB_SUBLOTE, SUB.UB_DTVALID, SUB.UB_PRODUTO,SUB.UB_UM,"
	cFieldsQry += "SE4.E4_DESCRI, SA4.A4_NREDUZ, SU7.U7_NREDUZ, SA3.A3_NREDUZ, SUH.UH_DESC, SUO.UO_DESC, SU9.U9_DESC, SB1.B1_DESC,"
	cFieldsQry += "SA1.A1_NOME, SA1.A1_END, SA1.A1_BAIRRO, SA1.A1_MUN, SA1.A1_EST, SA1.A1_DDD, SA1.A1_TEL,"
	cFieldsQry += "SUS.US_NOME, SUS.US_END, SUS.US_BAIRRO, SUS.US_MUN, SUS.US_EST, SUS.US_DDD, SUS.US_TEL,"
	cFieldsQry += "SU5.U5_CONTAT, SU5.U5_DDD, SU5.U5_FCOM1, SU5.U5_EMAIL"
	cFieldsQry += self:cSelectLoc

	//Adiciona campos customizados
	FatInjCposPerson(@self:aFieldsValue, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .F., .T.)

	cQuery := "SELECT ? " 
	cQuery += "FROM " 		+ RetSqlName("SUA") + " SUA "
	cQuery += "INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = SUA.UA_FILIAL "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SUB") + " SUB "
	cQuery += "	ON  SUB.UB_FILIAL   = SUA.UA_FILIAL "
	cQuery += "	AND SUB.UB_NUM 		= SUA.UA_NUM "
	cQuery += "	AND SUB.D_E_L_E_T_ 	= ? "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SE4") + " SE4 "
	cQuery += " ON  ? "
	cQuery += " AND SE4.E4_CODIGO	= SUA.UA_CONDPG "
	cQuery += " AND SE4.D_E_L_E_T_ 	= ? "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SA4") + " SA4 "
	cQuery += " ON  ? "
	cQuery += " AND SA4.A4_COD		= SUA.UA_TRANSP "
	cQuery += " AND SA4.D_E_L_E_T_ 	= ? "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SU7") + " SU7"
	cQuery += " ON  ? "
	cQuery += " AND SU7.U7_COD		= SUA.UA_OPERADO "
	cQuery += " AND SU7.D_E_L_E_T_ 	= ? "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SA3") + " SA3 "
	cQuery += " ON  ? "
	cQuery += " AND SA3.A3_COD		= SUA.UA_VEND "
	cQuery += " AND SA3.D_E_L_E_T_ 	= ? "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SUH") + " SUH "
	cQuery += " ON  ? "
	cQuery += " AND SUH.UH_MIDIA	= SUA.UA_MIDIA "
	cQuery += " AND SUH.D_E_L_E_T_ 	= ? "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SUO") + " SUO "
	cQuery += " ON  ? "
	cQuery += " AND SUO.UO_CODCAMP	= SUA.UA_CODCAMP "
	cQuery += " AND SUO.D_E_L_E_T_ 	= ? "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SU9") + " SU9 "
	cQuery += " ON  ? "
	cQuery += " AND SU9.U9_CODIGO	= SUA.UA_CODLIG "
	cQuery += " AND SU9.D_E_L_E_T_ 	= ? "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SA1") + " SA1 "
	cQuery += "ON  ? "
	cQuery += "AND SA1.A1_COD 		= SUA.UA_CLIENTE "
	cQuery += "AND SA1.A1_LOJA		= SUA.UA_LOJA "
	cQuery += "AND SA1.D_E_L_E_T_ 	= ? "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SUS") + " SUS "
	cQuery += "ON  ? "
	cQuery += "AND SUS.US_COD 		= SUA.UA_CLIENTE "
	cQuery += "AND SUS.US_LOJA		= SUA.UA_LOJA "
	cQuery += "AND SUS.D_E_L_E_T_ 	= ? "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SU5") + " SU5 "
	cQuery += "ON  ? "
	cQuery += "AND SU5.U5_CODCONT	= SUA.UA_CODCONT "
	cQuery += "AND SU5.D_E_L_E_T_ 	= ? "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SB1") + " SB1 "
	cQuery += "	ON  ? "
	cQuery += "	AND SB1.B1_COD 		= SUB.UB_PRODUTO "
	cQuery += "	AND SB1.D_E_L_E_T_ 	= ? "
	cQuery += "WHERE SUA.UA_FILIAL 	= TMPFIL.TMP_FILIAL "
	cQuery += "	AND SUA.UA_EMISSAO BETWEEN ? AND ? "
	cQuery += " AND SUA.UA_OPERADO BETWEEN ? AND ? "
	cQuery += " AND SUA.UA_MIDIA   BETWEEN ? AND ? "
	cQuery += " AND SUA.UA_CANC 	= ? "
	cQuery += " AND SUA.UA_TMK 		= ? "
	cQuery += " AND (SUA.UA_OPER = ? OR SUA.UA_OPER = ?) "

	If !Empty(MV_PAR07)
		cQuery += " AND SUA.UA_CODCAMP = ? "
	EndIf

	If MV_TPENT == 1 .Or. MV_TPENT == 2
		cQuery += "	AND SUA.UA_PROSPEC = ? "
	EndIf

	cQuery += " ? "
	cQuery += "	AND SUA.D_E_L_E_T_ = ? "
	cQuery += "ORDER BY SUA.UA_FILIAL, SUA.UA_NUM "

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))
	oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetUnsafe(nParam++, cNameTmp)
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SE4", "SUA"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SA4", "SUA"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SU7", "SUA"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SA3", "SUA"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SUH", "SUA"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SUO", "SUA"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SU9", "SUA"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SA1", "SUA"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SUS", "SUA"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SU5", "SUA"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SB1", "SUB"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetString(nParam++, DtoS(MV_PAR03))
	oQuery:SetString(nParam++, DtoS(MV_PAR04))
	oQuery:SetString(nParam++, MV_PAR01)
	oQuery:SetString(nParam++, MV_PAR02)
	oQuery:SetString(nParam++, MV_PAR05)
	oQuery:SetString(nParam++, MV_PAR06)
	oQuery:SetString(nParam++, ' ')
	oQuery:SetString(nParam++, '2')
	oQuery:SetString(nParam++, '1')
	oQuery:SetString(nParam++, '2')

	If !Empty(MV_PAR07)
		oQuery:SetString(nParam++, MV_PAR07)
	EndIf

	If MV_TPENT == 1
		oQuery:SetString(nParam++, 'F')
	ElseIf MV_TPENT == 2
		oQuery:SetString(nParam++, 'T')
	EndIf

	oQuery:SetUnsafe(nParam++, self:cWhereLoc)
	oQuery:SetString(nParam++, ' ')

	self:cAlias := oQuery:OpenAlias()

	oTempTableBranch:Delete()

	FreeObj(oQuery)
	FreeObj(oTempTableBranch)

	FwFreeArray(aFiliais)

Return

//-------------------------------------------------------------------
/*{Protheus.doc} SVOfuscated
Retorna o campo ofuscado ou não

@return Nil

@author FAT/CRM
@since 20/02/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method SVOfuscated(cField) Class listofcallactiveserviceSmartViewBusinessObject

	Local cValue := "" as character

	cValue := FTPDObfuscate(AllTrim((self:cAlias)->&(cField)), cField,, .T.)

Return cValue
