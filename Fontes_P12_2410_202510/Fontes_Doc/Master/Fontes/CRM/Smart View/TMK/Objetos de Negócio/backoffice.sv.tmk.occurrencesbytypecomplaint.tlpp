#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.tmk.occurrencesbytypecomplaint.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

using namespace totvs.framework.treports.integratedprovider
namespace totvs.protheus.backoffice.sv.tmk.occurrencesbytypecomplaint.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="SUC,SUD,SB1,SU5,SU7,SUH,SUL,SUN,SU9,SUQ", name="Relação de Ocorrências por tipo de reclamação no telemarketing ativo", country="ALL", customTables="ALL")

/*
	{Protheus.doc} occurrencesbytypecomplaintSmartViewBusinessObject
	Classe para criação do Objeto de Negócio para Relação de Contatos e Entidades Associadas
	para embalagens
	@author FAT/CRM
	@since 01/09/2023
	@version 1.0
*/
Class occurrencesbytypecomplaintSmartViewBusinessObject from IntegratedProvider

    public method new()			as object
    public method getData()		as object
    public method getSchema()	as object
	public method TMKSV006CreateSQL()

    protected data aFields		as array
    protected data aStruct		as array
	protected data aFieldsValue as array 
    protected data cAlias		as character
    protected data cSelectLoc	as character
    protected data cWhereLoc	as character
    protected data ojItems		as json
 
EndClass

/*{Protheus.doc} new
	Método de instância da classe
	@return object: self
	@author FAT/CRM
	@since 01/09/2023
	@version 1.0
*/
   
Method new() class occurrencesbytypecomplaintSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 	 //"Telemarketing"
    self:setDisplayName(STR0002) //"Ocorrências por tipo"
    self:setDescription(STR0003) //"Relação de Ocorrências por tipo de reclamação no telemarketing ativo"
    self:setIsLookUp(.T.)  
	self:aFieldsValue := {}

Return self
 
/*{Protheus.doc} getData
	Retorna os dados do objeto de negócio.

	@param nPage, numérico, indica a página atual do relatório
	@param oFilter, objeto, contém o filtro do SmartView

	@return object: self:oData

	@author FAT/CRM
	@since 01/09/2023
	@version 1.0
*/

Method getData(nPage as numeric, oFilter as object) as object class occurrencesbytypecomplaintSmartViewBusinessObject

	Local cCep			:= ""	as  character
	Local xValue        := ""	as  character
	Local nX            := 0    as  numeric
	Local nScan         := 0    as  numeric
	Local nScanUser     := 0    as  numeric
    Local lObfuscated   := .F.  as  logical
    Local aPDFields     := {}   as  array	
	Local aUser			:= {}  	as  array
	Local aUC_Status	:= {}  	as  array 
	Local aUD_Status	:= {}  	as  array 

	MV_FIL	 := ""
	MV_STSLG := 1 
	MV_TPATN := 1 	

    FatSetValueMVPAR(oFilter:GetParameters(),"TMK018") //Metodo para retorno do json dos parametros

	//Tratamento LGPD
    aPDFields := FatGetfieldsLGPD(self:aFields, {}) //Função criada p/ retornar somente campos que devem ser ofuscados,
    lObfuscated := len( aPDFields ) > 0             //o inverso do que a função utilizada anteriormente nos retorna.

	//Array para processamento
    self:aFieldsValue := {{"ENTIDADE",	{"ENTIDADE",   	"", 'FWGetSX5("T5",(self:cAlias)->UC_ENTIDAD)[1][4]'}},;
                         {"RAZAO"	,	{"RAZAO"   ,   	"", 'SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE,1,,(self:cAlias)->UC_FILIAL)'}},;
                         {"FONEENT",	{"FONEENT" ,  	"", 'FTPDObfuscate(SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE,6,,(self:cAlias)->UC_FILIAL),"U5_FCOM1","TMKR018")'}},;
                         {"CIDADEENT",	{"CIDADEENT",  	"", 'SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE,3,,(self:cAlias)->UC_FILIAL)'}},;
                         {"ESTADOPENT",	{"ESTADOPENT", 	"", "SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE,4,,(self:cAlias)->UC_FILIAL)"}},;						 
                         {"ASSUNTO",	{"ASSUNTO",    	"", 'FWGetSX5("T1",(self:cAlias)->UD_ASSUNTO)[1][4]'}},;
						 {"RESPONSAVEL",{"RESPONSAVEL",	"", 'IIF(nScanUser > 0 , FTPDObfuscate(aUser[nScanUser][4],"U7_NREDUZ","TMKR018") ,"")'}},;
						 {"UD_OBS",	    {"UD_OBS",    	"", 'FATSVGetMemo((self:cAlias)->&(self:aStruct[nX][5]))'}},;
						 {"UD_CODEXEC",	{"UD_CODEXEC",  "", 'FATSVGetMemo((self:cAlias)->&(self:aStruct[nX][5]))'}},;
						 {"UC_CODOBS",	{"UC_CODOBS",   "", 'FATSVGetMemo((self:cAlias)->&(self:aStruct[nX][5]))'}},;
						 {"UC_STATUS",	{"UC_STATUS",   "", "Iif(!Empty((self:cAlias)->UC_STATUS),aUC_Status[Val((self:cAlias)->UC_STATUS)],'')"}},;
						 {"UD_STATUS",	{"UD_STATUS",   "", "Iif(!Empty((self:cAlias)->UD_STATUS),aUD_STATUS[Val((self:cAlias)->UD_STATUS)],'')"}}}


	//Método que monta a Query
	self:TMKSV006CreateSQL()

	aUC_Status	:= TkSx3Box("UC_STATUS") 
	aUD_Status	:= TkSx3Box("UD_STATUS")
	aUser  		:= FWSFAllUsers()

	//Posiciona no registro inicial
    (self:cAlias)->(dbGoTop())

	While !(self:cAlias)->(Eof())

		//Seleciona o intervalo do CEP Exeto Concorrente que nao tem CEP 
		If Mv_Par11 <> "AC3" .And. !Empty(Mv_Par09)
			cCep := SVTkEntidade((self:cAlias)->UC_ENTIDAD,(self:cAlias)->UC_CHAVE,5,,(self:cAlias)->UC_FILIAL,.F.)

			If Alltrim(cCep) < Alltrim(Mv_Par08) .OR. Alltrim(cCep) > Alltrim(Mv_Par09)
				(self:cAlias)->(DbSkip())
				Loop
			Endif	
		Endif

		self:ojItems := JsonObject():new()

		nScanUser  := aScan( aUser, {|aUsr| Upper(AllTrim(aUsr[2])) == Alltrim( (self:cAlias)->UD_OPERADO ) })

        For nX := 1 To Len(self:aStruct)

			If (nScan := aScan(self:aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } )) > 0
				xValue  := FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2])
			Else
				xValue := "(self:cAlias)->"+self:aStruct[nX][5]
			Endif

			If lObfuscated .and. ( nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0
                self:ojItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
            Else
                If Alltrim(self:aStruct[nX][3]) == "date"
                    self:ojItems[self:aStruct[nX][1]] := IIF( ValType(&(xValue)) == "C" ,  totvs.framework.treports.date.stringToTimeStamp( &(xValue) ) , totvs.framework.treports.date.dateToTimeStamp( &(xValue) ))
                Else
                    self:ojItems[self:aStruct[nX][1]] := &(xValue)
                Endif
            Endif
            
        Next nX
	
        self:processData(1) //Tratamento para commit localizado
        self:oData:appendData(self:ojItems)
            
		(self:cAlias)->(dbSkip())

	Enddo

    //Fechamento de tabelas e limpeza do objeto/variável
    (self:cAlias)->(DBCloseArea())

	FwFreeArray(aUser)
	FwFreeArray(aPDFields)
	FwFreeArray(aUC_Status)
	FwFreeArray(aUD_Status)
	FwFreeArray(self:aFieldsValue)

Return self:oData

/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
	@return object: self:oSchema
	@author FAT/CRM
	@since 15/08/2023
	@version 1.0
*/
   
Method getSchema() as object class occurrencesbytypecomplaintSmartViewBusinessObject
	
	Local nX 			:= 0  as numeric
	Local aCposFake 	:= {} as array
	Local aParameters	:= {} as array

    self:aFields := {"UC_FILIAL","UC_DATA","UC_INICIO","UC_FIM","UC_CODIGO","UC_CODCONT","U5_CONTAT","UC_MIDIA",'UC_TIPO',;
					"UC_ENTIDAD","UC_CHAVE","U7_NREDUZ","UC_OPERACA","UH_DESC","UC_STATUS","UC_CODOBS","UL_DESC",;
					"UN_DESC","UD_ITEM","UD_PRODUTO","B1_DESC","UD_OCORREN","U9_DESC","UQ_DESC","UD_DATA","UD_STATUS",;
					"UD_DTEXEC","UD_OBS","UD_CODEXEC","U5_FCOM1","U5_EMAIL","ENTIDADE","RAZAO","FONEENT","CIDADEENT",;
					"ESTADOPENT","ASSUNTO","RESPONSAVEL"}
	
	aCposFake := {{"ENTIDADE"	, STR0006, "string", STR0006, "ENTIDADE"},;
				  {"RAZAO"		, STR0007, "string", STR0007, "RAZAO"},;
				  {"FONEENT"	, STR0008, "string", STR0008, "FONEENT"},;
				  {"CIDADEENT"	, STR0009, "string", STR0009, "CIDADEENT" },;
				  {"ESTADOPENT"	, STR0010, "string", STR0010, "ESTADOPENT"},;
				  {"ASSUNTO"	, STR0011, "string", STR0011, "ASSUNTO"},;
				  {"RESPONSAVEL", STR0012, "string", STR0012, "RESPONSAVEL"}}

    self:aStruct := FatTrGetStruct(self:aFields, aCposFake)

	// Cria a estrutura dos campos Padroes                                   
	If !Empty(self:aStruct)
		For nX := 1 To Len(self:aStruct)						
			self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
		Next nX			
	Endif

    // Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('TMK018', {'MV_PAR07','MV_PAR20'} )

	// Adiciona novo parametro do tipo number
	self:oSchema:addParameter("MV_FIL"   , STR0015 + " ?" , "string", .T.,,,,,, FTSVHelp(,,,.T.)) // Filial                             

	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

	self:oSchema:addParameter("MV_STSLG" , STR0013 + " ?" , "number", .F.,,,,,, FTSVHelp("TMK018","07")) //Status da Ligação
	self:oSchema:addParameter("MV_TPATN" , STR0014 + " ?" , "number", .F.,,,,,, FTSVHelp("TMK018","20")) //Tipo de Atendimento

    If __lLookUp
		self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/SU9", 2)
		self:setCustomURL("MV_PAR04", "api/framework/v1/genericLookupService/smartview/SU7", 2)
		self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/SB1", 2)
		self:setCustomURL("MV_PAR06", "api/framework/v1/genericLookupService/smartview/02",  2)
		self:setCustomURL("MV_PAR10", "api/framework/v1/genericLookupService/smartview/SU5", 2)
		self:setCustomURL("MV_PAR11", "api/framework/v1/genericLookupService/smartview/T5",  2)
		self:setCustomURL("MV_PAR12", "api/framework/v1/genericLookupService/smartview/T3",  2)
		self:setCustomURL("MV_PAR13", "api/framework/v1/genericLookupService/smartview/T3",  2)
		self:setCustomURL("MV_PAR14", "api/framework/v1/genericLookupService/smartview/T3",  2)
		self:setCustomURL("MV_PAR15", "api/framework/v1/genericLookupService/smartview/T3",  2)
		self:setCustomURL("MV_PAR16", "api/framework/v1/genericLookupService/smartview/T3",  2)
		self:setCustomURL("MV_PAR17", "api/framework/v1/genericLookupService/smartview/T3",  2)
		self:setCustomURL("MV_PAR18", "api/framework/v1/genericLookupService/smartview/T3",  2)
		self:setCustomURL("MV_PAR19", "api/framework/v1/genericLookupService/smartview/T3",  2)
		self:setCustomURL("MV_STSLG", "api/framework/treports/integratedprovider/v1/options/TMK018/MV_PAR07", 1)
		self:setCustomURL("MV_TPATN", "api/framework/treports/integratedprovider/v1/options/TMK018/MV_PAR20", 1)
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)
	Endif

    FwFreeArray(aParameters)
	FwFreeArray(aCposFake)

Return self:oSchema

/*{Protheus.doc} TMKSV006CreateSQL
	Retorna todas as querys montadas
	@author TMK
	@since 11/03/2025
	@version 1.0
	@return Nil
*/

Method TMKSV006CreateSQL() Class occurrencesbytypecomplaintSmartViewBusinessObject

	Local cFieldsQry	:= ""  as character
    Local cQuery 		:= ""  as character
	Local cNameTmp 		:= ""  as character
	Local cTMKCTSG	    := ""  as character
	Local cEntidSeg	    := ""  as character
	Local cAliasSeg	    := ""  as character
	Local cNumCamp	    := ""  as character
	Local nTMKSEGN      := 0   as numeric
	Local nParam        := 1   as numeric
	Local nX        	:= 0   as numeric
	Local nMVSeg        := 0   as numeric
    Local aFiliais 		:= {}  as array
	Local aMVsSeg       := {}  as array
	Local aQrySeg       := {}  as array
	Local oQuery 		:= Nil as object
	Local oTempTableBranch := Nil as object

	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SUC", "UC_FILIAL")

	cNameTmp := oTempTableBranch:GetTableNameForQuery()

	//Adiciona apenas os campos das tabelas SUC e SUD
	For nX := 1 To Len(self:aFields)		
		cFieldsQry += IIF( SubStr(self:aFields[nx],1,3) $ 'UC_|UD_', self:aFields[nx] + ", " ,"")
	Next nX	
	
	//Campos que não devem estar no aFields e serão utilziados.
	cFieldsQry += 'UC_OPERADO,UC_CODENCE,UD_ASSUNTO,UD_SOLUCAO,UD_OPERADO,U5_CONTAT,U5_FCOM1,U5_EMAIL,U7_NREDUZ,UH_DESC,UN_DESC,UL_DESC,B1_DESC,U9_DESC,UQ_DESC '

	//Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson(@self:aFieldsValue, @self:aStruct , @cFieldsQry , self:getCustomFields() , .T., .F. , .T. , 'B1_|U5_|U7_|UH_|UL_|UN_|U9_|UQ_|')

	//Limpa o cache dos parametro primeiro
    SuperGetMv()
	cTMKCTSG := SuperGetMV("MV_TMKCTSG")
    nTMKSEGN := SuperGetMV("MV_TMKSEGN")

	 // Validação criada para substituir a função TKSegmento (o uso dessa funçao foi retirado por não utilizar o conceito de Bind na query)
    If !Empty(MV_PAR11) .And. (AllTrim(Upper(MV_PAR11)) == "SA1" .Or. AllTrim(Upper(MV_PAR11)) == "SUS")

		cEntidSeg  := SubStr(AllTrim(Upper(MV_PAR11)), 2, 3)
		cAliasSeg  := AllTrim(Upper(MV_PAR11))

		// Passa pelos parametros de segmentos (MV_PAR11 ao MV_PAR19)
		For nX := 12 To 19
			If !Empty(&('MV_PAR' + AllTrim(Str(nX))))
				aAdd(aMVsSeg, &('MV_PAR' + AllTrim(Str(nX))))
			Endif
		Next nX

		If cAliasSeg == "SA1"
			cNumCamp := "1"
		Endif

		If cTMKCTSG == "N" .Or. cTMKCTSG == "S"

			For nX := 12 To 19

				// Validação pois os campos de Segmento começam com sequencias diferentes na SA1 e SUS
				IIF(cAliasSeg == "SUS" .And. nX == 13,cNumCamp := "2", cNumCamp)

				If !Empty(&('MV_PAR' + AllTrim(Str(nX))))
					aAdd(aQrySeg, cAliasSeg + "." + cEntidSeg + "_SATIV" + cNumCamp)
					nMVSeg++
				Endif

				cNumCamp := Soma1(cNumCamp)

			Next nX

		ElseIf cTMKCTSG == "C"

			For nX := 1 To nTMKSEGN

				// Validação pois os campos de Segmento começam com sequencias diferentes na SA1 e SUS
				If cAliasSeg == "SUS" .And. nX == 2
					cNumCamp := "2"
				Endif

				aAdd(aQrySeg, cAliasSeg + "." + cEntidSeg + "_SATIV" + cNumCamp)
				cNumCamp := Soma1(cNumCamp)
				nMVSeg++

			Next nX

		Endif
	Endif

    cQuery := "SELECT ? "
	cQuery +=  " ? "
    cQuery += " FROM " + RetSqlName("SUC") + " SUC  "
	cQuery += " INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = SUC.UC_FILIAL "
	cQuery += " INNER JOIN "+ RetSqlName("SUD") +" SUD ON SUD.UD_FILIAL = SUC.UC_FILIAL 
	cQuery += " AND SUD.UD_CODIGO = SUC.UC_CODIGO AND SUD.D_E_L_E_T_ = ?  "
	cQuery += " LEFT JOIN " + RetSqlName("SB1") +" SB1 ON ? 
	cQuery += " AND SB1.B1_COD = SUD.UD_PRODUTO AND SB1.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSqlName("SU5") +" SU5 ON ? 
	cQuery += " AND SU5.U5_CODCONT = SUC.UC_CODCONT AND SU5.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSqlName("SU7") +" SU7 ON ? 
	cQuery += " AND SU7.U7_COD = SUC.UC_OPERADO AND SU7.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SUH") +" SUH ON ? 
	cQuery += " AND SUH.UH_MIDIA = SUC.UC_MIDIA AND SUH.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SUN") +" SUN ON ? 
	cQuery += " AND SUN.UN_ENCERR = SUC.UC_CODENCE AND SUN.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SUL") +" SUL ON ? 
	cQuery += " AND SUL.UL_TPCOMUN = SUC.UC_TIPO AND SUL.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SU9") +" SU9 ON ? 
	cQuery += " AND SU9.U9_CODIGO = SUD.UD_OCORREN " 
	cQuery += " AND SU9.U9_ASSUNTO  = SUD.UD_ASSUNTO " 
	cQuery += " AND SU9.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SUQ") +" SUQ ON ? 
	cQuery += " AND SUQ.UQ_SOLUCAO = SUD.UD_SOLUCAO AND SUQ.D_E_L_E_T_ = ? "

	If nMVSeg > 0
        cQuery += " INNER JOIN " + RetSqlName(cAliasSeg) + " ? "
        cQuery += "  ON ? "
        cQuery += " AND ?.?_COD  = SUBSTRING(SUC.UC_CHAVE, 1, 6) "
        cQuery += " AND ?.?_LOJA = SUBSTRING(SUC.UC_CHAVE, 7, 2) "

        // Verifica o valor do parametro MV_TMKCTSG
        If cTMKCTSG == "N"

            cQuery += " AND ( "

            For nX := 1 To nMVSeg
                If nX > 1
                    cQuery += " OR "
                    cQuery += " ? = ? "
                Else
                    cQuery += " ? = ? "
                Endif
            Next nX

            cQuery += " ) "

        ElseIf cTMKCTSG == "S"

            For nX := 1 To nMVSeg
                cQuery += " AND ? = ? "
            Next nX

        ElseIf cTMKCTSG == "C"

            cQuery += " AND ( "

            For nX := 1 To nMVSeg
                If nX > 1
                    cQuery += " OR "
                    cQuery += " ? IN (?) "
                Else
                    cQuery += " ? IN (?) "
                Endif
            Next nX

            cQuery += " ) "

        Endif

        cQuery += " AND ?.D_E_L_E_T_ = ? "

    Endif
	
	cQuery += "WHERE SUC.UC_FILIAL  = TMPFIL.TMP_FILIAL "
	cQuery += "AND SUC.UC_DATA BETWEEN ? AND ? "
	cQuery += "AND SUC.UC_CODCANC = ? "
	cQuery += "AND SUC.UC_CHAORIG = ? "
	cQuery += "AND SUC.D_E_L_E_T_ = ? "

	If !Empty(Mv_Par03)
		cQuery += "AND SUD.UD_OCORREN = ? "
	Endif

	If !Empty(Mv_Par04)
		cQuery += "AND SUC.UC_OPERADO = ? "
	Endif

	If !Empty(Mv_Par05)
		cQuery += "AND SUD.UD_PRODUTO = ? "
	Endif

	If !Empty(Mv_Par10)
		cQuery += "AND SUC.UC_CODCONT = ? "
	Endif
		
	If !Empty(Mv_Par11)
		cQuery += "AND SUC.UC_ENTIDAD = ? "
	Endif

	If (MV_TPATN >  0) .And. (MV_TPATN <> 3)
		cQuery += "AND (SUC.UC_OPERACA = ? OR SUC.UC_OPERACA = ? ) " // Ambos
	Endif

	If (MV_STSLG <> 1) .And. (MV_STSLG <> 0)
		cQuery += "AND (SUC.UC_STATUS  = ? OR SUC.UC_STATUS  = ? ) " // Todas as ligações
	Endif

	If !Empty(Mv_Par06) 
		cQuery +="AND SB1.B1_TIPO = ? "
	Endif	

	cQuery += " ? " // Adição de condições para objeto localizado no where

	cQuery += " ORDER BY UC_FILIAL, UC_CODIGO"

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))
	oQuery:SetUnsafe(nParam++ , cFieldsQry )
	oQuery:SetUnsafe(nParam++ , self:cSelectLoc)
	oQuery:SetUnsafe(nParam++ , cNameTmp)
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SB1", "SUD"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU5", "SUC"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU7", "SUC"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SUH", "SUC"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SUN", "SUC"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SUL", "SUC"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU9", "SUD"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SUQ", "SUD"))
	oQuery:SetString(nParam++ , ' ')

	If nMVSeg > 0

        oQuery:SetUnsafe(nParam++, cAliasSeg)
        oQuery:SetUnsafe(nParam++, FwJoinFilial(AllTrim(Upper(MV_PAR11)), "SUC"))
        oQuery:SetUnsafe(nParam++, cAliasSeg)
        oQuery:SetUnsafe(nParam++, cEntidSeg)
        oQuery:SetUnsafe(nParam++, cAliasSeg)
        oQuery:SetUnsafe(nParam++, cEntidSeg)

        If cTMKCTSG == "N" .Or. cTMKCTSG == "S"
            For nX := 1 To nMVSeg
                oQuery:SetUnsafe(nParam++, aQrySeg[nX])
                oQuery:SetUnsafe(nParam++, aMVsSeg[nX])
            Next nX
        ElseIf cTMKCTSG == "C"
            For nX := 1 To nMVSeg
                oQuery:SetUnsafe(nParam++, aQrySeg[nX])
                oQuery:SetIn(nParam++, aMVsSeg)
            Next nX
        Endif

        oQuery:SetUnsafe(nParam++, cAliasSeg)
        oQuery:SetString(nParam++, ' ')

    Endif
	oQuery:SetString(nParam++ , DtoS(Mv_Par01) )
	oQuery:SetString(nParam++ , DtoS(Mv_Par02) )
	oQuery:SetString(nParam++ , Space( FWSX3Util():GetFieldStruct( "UC_CODCANC" )[3] ) )
	oQuery:SetString(nParam++ , Space( FWSX3Util():GetFieldStruct( "UC_CHAORIG" )[3] ) )
	oQuery:SetString(nParam++ , ' ')

	If !Empty(Mv_Par03)
		oQuery:SetString(nParam++ , Mv_Par03 )
	Endif

	If !Empty(Mv_Par04) 
		oQuery:SetString(nParam++ , Mv_Par04 )
	Endif

	If !Empty(Mv_Par05)
		 oQuery:SetString(nParam++ , Mv_Par05 )
	Endif

	If !Empty(Mv_Par10)
		oQuery:SetString(nParam++ , Mv_Par10 )
	Endif

	If !Empty(Mv_Par11)
		oQuery:SetString(nParam++ , Mv_Par11 )
	Endif

	If (MV_TPATN  > 0) .And. (MV_TPATN <> 3) 
		oQuery:SetString(nParam++ , IIF(MV_TPATN==1,"2","1") )
		oQuery:SetString(nParam++ , Space( FWSX3Util():GetFieldStruct( "UC_OPERACA" )[3] ) )
	Endif

	If (MV_STSLG <> 1) .And. (MV_STSLG <> 0)
		oQuery:SetString(nParam++ , Str(MV_STSLG-1,1) )
		oQuery:SetString(nParam++ , ' ' )
	Endif	

	If !Empty(Mv_Par06)
		oQuery:SetString(nParam++ , AllTrim(Mv_Par06) )
	Endif

	oQuery:SetUnsafe(nParam++ ,self:cWhereLoc)
	
    self:cAlias := oQuery:OpenAlias()  
	
	oTempTableBranch:Delete()
	FreeObj(oQuery)
	FreeObj(oTempTableBranch)
	FwFreeArray(aFiliais) 
	FwFreeArray(aMVsSeg)
	FwFreeArray(aQrySeg)

Return 
