#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.tmk.billsreceivableperclient.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.tmk.billsreceivableperclient.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="ACF,SU5,SA1,ACG", name="Situação dos títulos por cliente", country="ALL", customTables="ACF,ACG,SA1")

//-------------------------------------------------------------------
/*/{Protheus.doc} billsreceivableperclientSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Situacao dos títulos por cliente

@author FAT/CRM
@since 18/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class billsreceivableperclientSmartViewBusinessObject From integratedProvider

    public method new()				as object
    public method getData()			as object
    public method getSchema()		as object
	public method TmkSv003CreateSQL()

	protected method getTotType()	as numeric

    protected data aFields			as array
    protected data aStruct			as array
	protected data aFieldsValue 	as array
    protected data ojItems			as json
    protected data cAlias			as character
    protected data cSelectLoc		as character
    protected data cWhereLoc		as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author FAT/CRM
@since 18/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class billsreceivableperclientSmartViewBusinessObject

	_Super:new()
    self:appendArea(STR0001) 		//Telemarketing
    self:setDisplayName(STR0002)	//Situação dos títulos por cliente
    self:setDescription(STR0003)	//Situação dos títulos em cobrança
    self:setIsLookUp(.T.)

	self:cAlias 		:= ""
	self:aFieldsValue   := {}

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author FAT/CRM
@since 18/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class billsreceivableperclientSmartViewBusinessObject

	Local cStatus		:= ""  as character
	Local nX            := 0   as numeric
	Local nScan         := 0   as numeric
    Local lObfuscated   := .F. as logical
    Local aPDFields     := {}  as array
	Local xValue		:= Nil

	MV_FIL := ""

	//Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "TMK037")

	//Array para processamento
	self:aFieldsValue := {	{'ACG_STATUS',{'ACG_STATUS',"", "cStatus"}},;
							{'TB_VALOR1', {'TB_VALOR1' ,"", "self:getTotType( self:cAlias, '1' )"}},;
							{'TB_VALOR2', {'TB_VALOR2' ,"", "self:getTotType( self:cAlias, '2' )"}},;
							{'TB_VALOR3', {'TB_VALOR3' ,"", "self:getTotType( self:cAlias, '3' )"}};
						}

	//Verifica se precisa fazer o tratamento para LGPD
    aPDFields 	:= FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated := Len(aPDFields) > 0

	// Chama o método que monta a query
    self:TmkSV003CreateSQL()

	dbSelectArea(self:cAlias)
    (self:cAlias)->(dbGoTop())

	While !(self:cAlias)->(Eof())

		cStatus := ""

		If !Empty((self:cAlias)->ACG_STATUS)
			cStatus := RetSX3Box(GetSX3Cache("ACG_STATUS", "X3_CBOX"),,,1)[Val((self:cAlias)->ACG_STATUS)][1]
		EndIf

		self:ojItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)

			If (nScan := aScan(self:aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } )) > 0
				xValue  := FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2])
			Else
				xValue := "(self:cAlias)->"+self:aStruct[nX][5]
			EndIf

			If lObfuscated .and. ( nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0 
				self:ojItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
            Else
				self:ojItems[self:aStruct[nX][1]] := IIf( UPPER(self:aStruct[nX][3]) == "DATE",;
				IIf(ValType((self:cAlias)->&(self:aStruct[nX][5])) == "C", totvs.framework.treports.date.stringToTimeStamp( &(xValue) ), totvs.framework.treports.date.dateToTimeStamp( &(xValue) ) ),;
				&(xValue) )
			EndIf

        Next nX

		self:processData(1) //Tratamento para commit localizado
        self:oData:appendData(self:ojItems)

		(self:cAlias)->(dbSkip())

	EndDo

	//Fechamento de tabelas e limpeza do objeto/variável
    (self:cAlias)->(DBCloseArea())

	FwFreeArray(aPDFields)
	FwFreeArray(self:aFieldsValue)
	
Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 18/09/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object Class billsreceivableperclientSmartViewBusinessObject

	Local nX 			:= 0  as numeric
	Local aStruCpoFake	:= {} as array
	Local aParameters   := {} as array

    self:aFields := {'ACF_FILIAL','ACF_CLIENT','ACF_LOJA','A1_NOME','ACF_CODCON','U5_CONTAT',;
					 'ACG_CODIGO','ACF_DATA','ACF_PENDEN','ACF_HRPEND','ACG_PREFIX','ACG_TITULO',;
					 'ACG_PARCEL','ACG_TIPO','ACG_NATURE','ACG_DTVENC','ACG_DTREAL','ACG_VALOR',;
					 'ACG_ACRESC','ACG_DECRES','ACG_NUMBCO','ACG_VALJUR','ACG_PORJUR','ACG_STATUS',;
					 'TB_VALOR1' ,'TB_VALOR2','TB_VALOR3'}

	// Array com estrutura dos campos Fake para montar o addproperty
    aStruCpoFake := { {"TB_VALOR1", STR0004, "number"},; //"TOTAL PAGO"
                      {"TB_VALOR2",	STR0005, "number"},; //"TOTAL NEGOCIADO"
                      {"TB_VALOR3",	STR0006, "number"} } //"TOTAL CARTÓRIO"

    self:aStruct := FatTrGetStruct(self:aFields, aStruCpoFake)

	// Cria a estrutura dos campos Padroes
	For nX := 1 To Len(self:aStruct)
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX

    //Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('TMK037')

    self:oSchema:addParameter("MV_FIL",	STR0007 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.))	// Filial

	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

	If __lLookUp
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2) 				 // Filial 
        self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/VSC", 2) 				 // Do Cliente
        self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/VSC", 2) 				 // Ate o Cliente
        self:setCustomURL("MV_PAR07", "api/framework/v1/genericLookupService/smartview/SU9", 2) 				 // Ocorrência
		self:setCustomURL("MV_PAR05", "api/framework/treports/integratedprovider/v1/options/TMK037/MV_PAR05", 1) // Status do título
		self:setCustomURL("MV_PAR06", "api/framework/treports/integratedprovider/v1/options/TMK037/MV_PAR06", 1) // Tipo da ligação
	EndIf

    FwFreeArray(aParameters)
	FwFreeArray(aStruCpoFake)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SvTmkRetVal
Retorna o valor para a celula de acordo com o status

@param cAlias, caracter, aliás temporário do relatório
@param cStatus, caracter, código do status.

@return numeric: nRet

@author FAT/CRM
@since 18/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getTotType( cAlias as character , cStatus as character ) as numeric Class billsreceivableperclientSmartViewBusinessObject
	Local nRet := 0	as numeric	// Retorno numerico da funcao

	If (cAlias)->ACG_STATUS == cStatus
		nRet := (cAlias)->ACG_VALOR
	EndIf

Return nRet

//-------------------------------------------------------------------
/*{Protheus.doc} TmkSv003CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 07/02/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method TmkSv003CreateSQL() Class billsreceivableperclientSmartViewBusinessObject

    Local nParam	 	    := 1   as numeric
    Local cQuery  	 	    := ""  as character
    Local cFieldsQry        := ""  as character
    Local cNameTmp          := ""  as character
    Local oQuery 	 	    := Nil as object
	Local oTempTableBranch	:= Nil as object
	Local aFiliais	 		:= {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "ACF", "ACF_FILIAL")
	cNameTmp := oTempTableBranch:GetTableNameForQuery()

	cFieldsQry := "ACF.ACF_FILIAL, ACF.ACF_CLIENT, ACF.ACF_LOJA, ACF.ACF_CODCON, ACF.ACF_CODIGO, ACF.ACF_DATA, ACF.ACF_PENDEN, ACF.ACF_HRPEND,"
	cFieldsQry += "ACG.ACG_CODIGO, ACG.ACG_PREFIX, ACG.ACG_TITULO, ACG.ACG_DTVENC, ACG.ACG_DTREAL, ACG.ACG_VALOR, ACG.ACG_STATUS,"
	cFieldsQry += "ACG.ACG_PARCEL, ACG.ACG_TIPO, ACG.ACG_NATURE, ACG.ACG_ACRESC, ACG.ACG_DECRES, ACG.ACG_NUMBCO, ACG.ACG_VALJUR,"
	cFieldsQry += "ACG.ACG_PORJUR, SA1.A1_NOME, SU5.U5_CONTAT"
	cFieldsQry += self:cSelectLoc

	//Adiciona campos customizados
	FatInjCposPerson(@self:aFieldsValue, @self:aStruct, @cFieldsQry, self:getCustomFields())

	cQuery := "SELECT ? "
	cQuery += " FROM " 		+ RetSqlName("ACF") + " ACF "
	cQuery += "INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = ACF.ACF_FILIAL "
	cQuery += "LEFT JOIN " 	+ RetSqlName("ACG") + " ACG "
	cQuery += "	ON  ACG.ACG_FILIAL 	= ACF.ACF_FILIAL "
	cQuery += "	AND ACG.ACG_CODIGO 	= ACF.ACF_CODIGO "
	cQuery += "	AND ACG.D_E_L_E_T_ 	= ? "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SA1") + " SA1 "
	cQuery += " ON  ? "
	cQuery += " AND SA1.A1_COD 		= ACF.ACF_CLIENT "
	cQuery += " AND SA1.A1_LOJA 	= ACF.ACF_LOJA "
	cQuery += " AND SA1.D_E_L_E_T_ 	= ? "
	cQuery += "LEFT JOIN " 	+ RetSqlName("SU5") + " SU5 "
	cQuery += " ON  ? "
	cQuery += " AND SU5.U5_CODCONT 	= ACF.ACF_CODCON "
	cQuery += " AND SU5.D_E_L_E_T_ 	= ? "
	cQuery += "WHERE ACF.ACF_FILIAL = TMPFIL.TMP_FILIAL "
	cQuery += " AND ACF.ACF_CLIENT BETWEEN ? AND ? "
	cQuery += " AND ACF.ACF_LOJA   BETWEEN ? AND ? "

	If !Empty(MV_PAR07)
		cQuery += " AND ACF.ACF_MOTIVO = ? "
	EndIf

	If (MV_PAR06 > 0 .And. MV_PAR06 <> 3)
		cQuery += " AND ACF.ACF_OPERA = ? "
	EndIf

	If (MV_PAR05 > 0 .And. MV_PAR05 <> 4)
		cQuery += " AND ACG.ACG_STATUS = ? "
	EndIf

	cQuery += " ? " 	// Where que foi populado no objeto localizado
	cQuery += " AND ACF.D_E_L_E_T_ = ? "
	cQuery += "ORDER BY ACF.ACF_FILIAL, ACF.ACF_CLIENT "

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))
	oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetUnsafe(nParam++, cNameTmp)
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SA1", "ACF"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SU5", "ACF"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetString(nParam++, MV_PAR01)
	oQuery:SetString(nParam++, MV_PAR03)
	oQuery:SetString(nParam++, MV_PAR02)
	oQuery:SetString(nParam++, MV_PAR04)

	If !Empty(MV_PAR07)
		oQuery:SetString(nParam++, MV_PAR07)
	EndIf

	If (MV_PAR06 > 0 .And. MV_PAR06 <> 3)
		oQuery:SetString(nParam++, Str(MV_PAR06, 1))
	EndIf

	If (MV_PAR05 > 0 .And. MV_PAR05 <> 4)
		oQuery:SetString(nParam++, Str(MV_PAR05, 1))
	EndIf

	oQuery:SetUnsafe(nParam++, self:cWhereLoc)
	oQuery:SetString(nParam++, ' ')

	self:cAlias := oQuery:OpenAlias()

	oTempTableBranch:Delete()

	FreeObj(oQuery)
	FreeObj(oTempTableBranch)

	FwFreeArray(aFiliais)

Return
