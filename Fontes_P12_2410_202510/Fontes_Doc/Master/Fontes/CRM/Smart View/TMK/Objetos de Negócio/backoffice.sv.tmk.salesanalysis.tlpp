#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWLIBVERSION.CH"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.tmk.salesanalysis.ch"

Static __LookUp	:= FwLibVersion() >= "20231121" 

namespace totvs.protheus.backoffice.sv.tmk.salesanalysis.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="SUA,SUB,SB1,SUO,SUH,SU7", name="Análise de Vendas por período e Representante de Vendas", country="ALL", customTables="SUA,SUB")

//-------------------------------------------------------------------
/*/{Protheus.doc} salesanalysisSmartViewBusinessObject
Classe para criação do Objeto de Negócio para listagem de processos de venda

@author FAT/CRM
@since 19/06/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class salesanalysisSmartViewBusinessObject from IntegratedProvider
    
    public method new() 		as object
    public method getData() 	as object
    public method getSchema()	as object

	public method TMKSV008CreateSQL()

    protected data aFields as array
    protected data aStruct as array

    protected data jItems as json

    protected data cAlias  		as character
    protected data cSelectLoc 	as character
    protected data cWhereLoc 	as character

endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 19/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class salesanalysisSmartViewBusinessObject
	
	_Super:new()
    self:appendArea(STR0001) 	 //"Telemarketing"
    self:setDisplayName(STR0002) //"Análise de Vendas por período e Representante de Vendas"
    self:setDescription(STR0003) //"Relação de Análise de Vendas por período e Representante de Vendas"
    self:setIsLookUp(.T.)

	self:cSelectLoc := ""
	self:cWhereLoc  := ""
	self:cAlias		:= ""

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author FAT/CRM
@since 19/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class salesanalysisSmartViewBusinessObject
	
	Local aRelacao		:= {} as array
	Local aSUBVend		:= {} as array
	Local aDesvio		:= {} as array
	Local aPDFields		:= {} as array
	Local aCposLGPD     := {"A3_NOME"} as array
	Local aCustomFields := self:getCustomFields() as array
	Local cCodChv		:= "" as character
	Local xValue		:= "" as character
	Local cOperacao  	:= "" as character
	Local nX		 	:= 0 as numeric
	Local ny		 	:= 0 as numeric
	Local nPos			:= 0 as numeric
	Local nVlTot1		:= 0 as numeric
	Local nVlTot2		:= 0 as numeric
	Local nVlTot3		:= 0 as numeric
	Local nVlrFat		:= 0 as numeric
	Local nVlrOrc		:= 0 as numeric
	Local nVlrAte		:= 0 as numeric
	Local nTotGer		:= 0 as numeric
	Local nPosDesvio	:= 0 as numeric
	Local nScan			:= 0 as numeric
	Local nPosQtd		:= 1 as numeric
	Local nPosItem		:= 1 as numeric
	Local lObfuscated   := .F. as logical
	
	MV_FIL := ""

	FatSetValueMVPAR(oFilter:GetParameters(),"TMK005") 

	//Montar a Query
	self:TMKSV008CreateSQL()

	DbSelectArea(self:cAlias)
	(self:cAlias)->(DBGoTop())

	//------------------------------------------------------------------- 
	// Tratamento LGPD                                                  
	//------------------------------------------------------------------- 
	aPDFields := FatGetfieldsLGPD(aCposLGPD, aCustomFields)
    lObfuscated := len( aPDFields ) > 0 

	While (self:cAlias)->(!Eof())
	
		//Traducao do codigo de Operacao
		If (self:cAlias)->UA_OPER == "1" 
			cOperacao := STR0004 //"1 - Faturamento" 
			nVlTot1 += (self:cAlias)->UB_VLRITEM
		ElseIf (self:cAlias)->UA_OPER == "2"
			cOperacao := STR0005 //"2 - Orcamento"
			nVlTot2 += (self:cAlias)->UB_VLRITEM
		Else
			cOperacao := STR0006 //STR0006
			nVlTot3 += (self:cAlias)->UB_VLRITEM
		EndIf

		aAdd(aRelacao,{	;
						(self:cAlias)->UB_NUM,;				//Numero do Atendimento
						cOperacao,;							//Codigo de Operacao
						Alltrim((self:cAlias)->UA_CODCAMP) 	+ " - " + Alltrim((self:cAlias)->UO_DESC),;			//Codigo da Campanha
						Alltrim((self:cAlias)->UA_MIDIA  )	+ " - " + Alltrim((self:cAlias)->UH_DESC),;			//Codigo da Midia
						totvs.framework.treports.date.stringToTimeStamp((self:cAlias)->UA_EMISSAO),;	//Emissao
						(self:cAlias)->UA_INICIO,;			//Tempo Inicio
						(self:cAlias)->UA_FIM,;				//Tempo Fim
						(self:cAlias)->UB_VLRITEM,;			//Valor de Item
						(self:cAlias)->UA_VEND,;			//Vendedor
						Alltrim((self:cAlias)->UB_PRODUTO) 	+ " - " + Alltrim((self:cAlias)->B1_DESC),; //Produto
						(self:cAlias)->UB_QUANT,;			//Quantidade do Produto
						(self:cAlias)->UA_FILIAL,;			//Filial
						(self:cAlias)->A3_NOME;
						})

		For nx := 1 to Len( aCustomFields )
						
			xValue  := "(self:cAlias)->"+aCustomFields[nX][1]

			//Verificação para tratamento de LGPD e campos tipo Data antes informar no JSON
			aadd(aRelacao[len(aRelacao)], IIF(lObfuscated .and. (nScan := aScan(aPDFields,  {|x| x[1] == aCustomFields[nX][1] } ) )  > 0,;
												aPDFields[nScan][2],;
												IIF(UPPER(aCustomFields[nX][3]) == "DATE",;
													Iif(ValType(&(xValue)) == "C", totvs.framework.treports.date.stringToTimeStamp( &(xValue) ), totvs.framework.treports.date.dateToTimeStamp( &(xValue) )),;
													&(xValue))))
		Next Nx

		(self:cAlias)->(DbSkip())
		
	EndDo
	
	If !Empty(aRelacao)
		
		//-----------------------------------------------------------------------------
		// Totaliza a quantidade e valores dos itens da SUB do vendedor por atendimento
		//----------------------------------------------------------------------------- 
		For nX := 1 To Len(aRelacao)

			//----------------------------------------------------------------------------
			// Totaliza a quantidade de Atendimentos da SUA por vendedor
			//---------------------------------------------------------------------------- 
			If !cCodChv == aRelacao[nX][1]+aRelacao[nX][9]
				nTotGer ++
			EndIf
			cCodChv := aRelacao[nX][1]+aRelacao[nX][9]
			
			If !Empty(aRelacao[nX][9]) .And. SubStr(aRelacao[nX][2],1,1) $ "1|2"

				nPos := Ascan(aSUBVend, {|x| x[1]+x[2] == aRelacao[nX][1]+aRelacao[nX][9]})
				If nPos > 0

					nPosQtd  := IIF(aRelacao[nX][2] == STR0004, 3, IIF(aRelacao[nX][2] == STR0005, 5, 7))
					nPosItem := IIF(aRelacao[nX][2] == STR0004, 4, IIF(aRelacao[nX][2] == STR0005, 6, 8))

					aSUBVend[nPos][nPosQtd] ++ //Soma a Quantidade de itens (Faturamento)
					aSUBVend[nPos][nPosItem] += aRelacao[nX][8]	//Soma o Valor do item (Faturamento)

				Else

					nVlrFat := IIF(aRelacao[nX][2] == STR0004, aRelacao[nX][8], 0)
					nVlrOrc := IIF(aRelacao[nX][2] == STR0005, aRelacao[nX][8], 0)
					nVlrAte := IIF(aRelacao[nX][2] == STR0006, aRelacao[nX][8], 0)
						
					aAdd(aSUBVend,{	aRelacao[nX][1],;	//Numero do Atendimento
									aRelacao[nX][9],;	//Codigo do Vendedor
									1,;					//Quantidade total de itens (Faturamento)
									nVlrFat,;			//Valor total dos itens (Faturamento)
									1,;					//Quantidade total de itens (Orcamento)
									nVlrOrc,;			//Valor total dos itens (Orcamento)
									1,;					//Quantidade total de itens (Atendimento)
									nVlrAte	,;			//Valor total dos itens (Atendimento)
									aRelacao[nX][2]})	//Codigo da Operacao				
				EndIf				

			EndIf

		Next nX

		For nX := 1 To Len(aRelacao)

			self:jItems := JsonObject():new()

			self:jItems["NUMATEND"]	:= aRelacao[nX][1]
			self:jItems["OPERACAO"]	:= aRelacao[nX][2]
			self:jItems["CAMPANHA"] 	:= Iif(Alltrim(aRelacao[nX][3]) != "-", aRelacao[nX][3],"")
			self:jItems["MIDIA"] 		:= Iif(Alltrim(aRelacao[nX][4]) != "-", aRelacao[nX][4],"")
			self:jItems["EMISSAO"]		:= aRelacao[nX][5]
			self:jItems["TEMPOINI"]	:= aRelacao[nX][6]
			self:jItems["TEMPOFIM"]	:= aRelacao[nX][7]
			self:jItems["VLRITEM"]		:= aRelacao[nX][8]
			If lObfuscated
					self:jItems["VENDEDOR"] := Iif(!Empty(aRelacao[nX][9]), aRelacao[nX][9] + " - " + FwProtectedDataUtil():ValueAsteriskToAnonymize(aRelacao[nX][13]),"")
				Else
					self:jItems["VENDEDOR"] := Iif(!Empty(aRelacao[nX][9]), aRelacao[nX][9] + " - " + aRelacao[nX][13],"")
			EndIf
			self:jItems["PRODUTO"]		:= Iif(Alltrim(aRelacao[nX][10]) != "-", aRelacao[nX][10],"")
			self:jItems["QTDPROD"]		:= aRelacao[nX][11]
			self:jItems["FILIAL"]		:= aRelacao[nX][12]

			If !Empty(aRelacao[nX][9]) .And. SubStr(aRelacao[nX][2],1,1) $ "1|2"

				nPos := Ascan(aSUBVend, {|x| x[1]+x[2] == aRelacao[nX][1]+SubStr(aRelacao[nX][9],1,6)})

				If nPos > 0

					If aRelacao[nX][2] == STR0004
						self:jItems["VLRMITEM"] 	:= (aRelacao[nX][8]/aSUBVend[nPos][3])
						self:jItems["VLRMDESVIO"]	:= ((aSUBVend[nPos][4]/aSUBVend[nPos][3]) - ((&("nVlTot"+SubStr(aRelacao[nX][2],1,1))/nTotGer)))/aSUBVend[nPos][3]

						//-----------------------------------------------------------------------------
						// Verifica se possui saldo de orcamento para calculo de Desvio
						//----------------------------------------------------------------------------- 
						If nVlTot2 > 0

							nPosDesvio := Ascan(aSUBVend, {|x| x[9]+x[2] == STR0005+SubStr(aRelacao[nX][9],1,6)})
							If nPosDesvio == 0

								nPosDesvio := Ascan(aDesvio, {|x| x[1]+x[2] == STR0005+aRelacao[nX][9]})
								If nPosDesvio == 0

									aDesvio := {}
									aAdd(aDesvio,{  STR0005,;		 //Codigo da Operacao
													aRelacao[nX][9],; 		 //Vendedor
													(0-(nVlTot2/nTotGer)) }) //Valor Medio de Desvio (Orcamento)
								EndIf
							EndIf
						EndIf

					ElseIf aRelacao[nX][2] == STR0005

						self:jItems["VLRMITEM"]   := (aSUBVend[nPos][6]/aSUBVend[nPos][5])/aSUBVend[nPos][5]
						self:jItems["VLRMDESVIO"] := ((aSUBVend[nPos][6]/aSUBVend[nPos][5]) - ((&("nVlTot"+SubStr(aRelacao[nX][2],1,1))/nTotGer)))/aSUBVend[nPos][5]

					EndIf

				EndIf

			EndIf
			
			If Len(aRelacao[nX]) > 12
				for ny := 1 To Len(aCustomFields)
					self:jItems[aCustomFields[ny][1]] := aRelacao[nX][12+ny]
				next ny	
			EndIF

			self:processdata(1)
			self:oData:appendData(self:jItems)

			//-----------------------------------------------------------------------------
			// Lanca o valor médio de desvio de orcamento para itens faturados
			//----------------------------------------------------------------------------- 
			If nPosDesvio == 0 .And. nVlTot2 > 0 .And. !Empty(aDesvio)

				self:jItems := JsonObject():new()

				self:jItems["OPERACAO"]	  := aDesvio[1][1] //Codigo da Operacao
				self:jItems["VENDEDOR"]	  := aDesvio[1][2] //Vendedor
				self:jItems["VLRMDESVIO"] := aDesvio[1][3] //Valor Medio de Desvio (Orcamento)
				
				self:processdata(1) 
				self:oData:appendData(self:jItems)

			EndIf

		Next nX

	EndIf

	//Fechamento de tabelas e limpeza do objeto/variável
    (self:cAlias)->(DBCloseArea())
	
	FwFreeArray(aRelacao)
	FwFreeArray(aSUBVend)
	FwFreeArray(aDesvio)
	FwFreeArray(aPDFields)
	FwFreeArray(aCposLGPD)
	FwFreeArray(aCustomFields)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 19/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getSchema() as object class salesanalysisSmartViewBusinessObject

	Local nX          	:= 0  as numeric
	Local aStruCpoFake  := {} as array
    Local aParameters 	:= {} as array
	
	self:aFields := {;
					 "FILIAL", "NUMATEND", "OPERACAO", "CAMPANHA", "MIDIA", "EMISSAO",;
					 "TEMPOINI", "TEMPOFIM", "VLRITEM", "VENDEDOR", "PRODUTO", "QTDPROD",;
					 "VLRMITEM","VLRMDESVIO";
					}

	aStruCpoFake := {;
					{"FILIAL"		,STR0020	,"string"},;
					{"NUMATEND"		,STR0007	,"string"},;
					{"OPERACAO"		,STR0008	,"string"},;
					{"CAMPANHA"		,STR0009	,"string"},;
					{"MIDIA" 		,STR0010	,"string"},;
					{"EMISSAO" 		,STR0011	,"date"	 },;
					{"TEMPOINI"		,STR0012	,"string"},;
					{"TEMPOFIM"		,STR0013	,"string"},;
					{"VLRITEM" 		,STR0014	,"number"},;
					{"VENDEDOR"		,STR0015	,"string"},;
					{"PRODUTO" 		,STR0016	,"string"},;
					{"QTDPROD" 		,STR0017	,"number"},;
					{"VLRMITEM"		,STR0018	,"number"},;
					{"VLRMDESVIO"	,STR0019	,"number"};
					}

	self:aStruct := FatTrGetStruct(self:aFields, aStruCpoFake)

	// Cria a estrutura dos campos Padroes                                   
	For nX := 1 To Len(self:aStruct)						
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX			

	//Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('TMK005')

	self:oSchema:addParameter("MV_FIL", STR0020 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.))	// Filial ?

	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5] )
	Next nX

	If __LookUp	
		self:setCustomURL("MV_FIL"		,"api/framework/v1/genericLookupService/smartview/SM0"	,2) // Filial
		self:setCustomURL("MV_PAR01"	,"api/framework/v1/genericLookupService/smartview/SA3"	,2) // Do Vendedor
        self:setCustomURL("MV_PAR02"	,"api/framework/v1/genericLookupService/smartview/SA3"	,2) // Ate o Vendedor
	EndIF

	FwFreeArray(aParameters)
	FwFreeArray(aStruCpoFake)

return self:oSchema

//------------------------------------------------------------------
/*{Protheus.doc} TMKSV008CreateSQL
    Método responsável por montar a query SQL.

@return character: Retorna a query SQL montada.
 
@author FAT/CRM
@since 13/02/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method TMKSV008CreateSQL() class salesanalysisSmartViewBusinessObject

	Local aFiliais		:= {} as array
	Local cNameTmp	 	:= "" as character
	Local cFieldsQry	:= "" as character
	Local cQuery		:= "" as character
	Local cEmisDe		:= "" as character
	Local cEmisAte		:= "" as character
	Local nParam		:= 1 as numeric
	Local oQuery			:= Nil as object
	Local oTempTableBranch 	:= Nil as object

	aFiliais := FatGetMVFiliais(MV_FIL)

	cEmisDe  := DToS(mv_par03) // A Partir de ?   
	cEmisAte := DToS(mv_par04) // Até a Data ?

	// Função que irá criar a tabela temporaria
	oTempTableBranch 	:= FatSVTableTempBranch(aFiliais, "SUA", "UA_FILIAL")
	cNameTmp 			:= oTempTableBranch:GetTableNameForQuery()

	cFieldsQry := 'UA_FILIAL, UA_NUM, UA_EMISSAO, UA_VEND, UA_CODCAMP, UA_MIDIA, UA_INICIO, UA_FIM, UA_OPER, '
	cFieldsQry += 'UB_NUM, UB_VLRITEM, UB_PRODUTO, UB_QUANT, UO_DESC, UH_DESC, B1_DESC, A3_NOME'
	cFieldsQry += self:cSelectLoc

	//Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson({}, @self:aStruct, @cFieldsQry,  self:getCustomFields(), .T., .T., .T.)

	cQuery := " SELECT ? "
	cQuery += " FROM "		+RetSqlName("SUA") + " SUA "
	cQuery += " INNER JOIN ? TMPFIL " 
	cQuery += " ON TMPFIL.TMP_FILIAL = SUA.UA_FILIAL  
	cQuery += " RIGHT JOIN "+RetSqlName("SUB") + " SUB "
	cQuery += " ON  SUB.UB_FILIAL 	= SUA.UA_FILIAL "
	cQuery += " AND SUB.UB_NUM 		= SUA.UA_NUM "
	cQuery += " LEFT JOIN "+RetSqlName("SUO") + " SUO "
	cQuery += " ON ? "
	cQuery += " AND SUO.UO_CODCAMP	= SUA.UA_CODCAMP "
	cQuery += " AND SUO.D_E_L_E_T_	= ?  "
	cQuery += " LEFT JOIN "+RetSqlName("SUH") + " SUH "
	cQuery += " ON ? "
	cQuery += " AND SUH.UH_MIDIA	= SUA.UA_MIDIA "
	cQuery += " AND SUH.D_E_L_E_T_	= ? "
	cQuery += " LEFT JOIN "+RetSqlName("SB1") + " SB1 "
	cQuery += " ON	? "
	cQuery += " AND SB1.B1_COD		= UB_PRODUTO "
	cQuery += " AND SB1.D_E_L_E_T_	= ? "
	cQuery += " LEFT JOIN "+RetSqlName("SA3") + " SA3 "
	cQuery += " ON ? "
	cQuery += " AND SA3.A3_COD		= SUA.UA_VEND "
	cQuery += " AND SB1.D_E_L_E_T_	= ? "
	cQuery += " WHERE SUA.UA_FILIAL = TMPFIL.TMP_FILIAL "
	cQuery += " AND SUA.UA_EMISSAO 	BETWEEN ? AND ? "
	cQuery += " AND SUA.UA_VEND 	BETWEEN ? AND ? "
	cQuery += " AND SUA.UA_CANC 	= ? "
	cQuery += " ? "
	cQuery += " AND SUA.D_E_L_E_T_ 	= ? "
	cQuery += " ORDER BY UA_FILIAL, UA_NUM, UA_EMISSAO "

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))

	oQuery:SetUnsafe(nParam++ , cFieldsQry)
	oQuery:SetUnsafe(nParam++ , cNameTmp )
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SUO", "SUA"))
    oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SUH", "SUA"))
    oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SB1", "SUB"))
    oQuery:SetString(nParam++ , ' ')
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SA3", "SUA"))
    oQuery:SetString(nParam++ , ' ')
	oQuery:SetString(nParam++ , cEmisDe )
	oQuery:SetString(nParam++ , cEmisAte )
	oQuery:SetString(nParam++ , MV_PAR01 )
	oQuery:SetString(nParam++ , MV_PAR02 )
	oQuery:SetString(nParam++ , ' ' )
	oQuery:SetUnsafe(nParam++, self:cWhereLoc)
	oQuery:SetString(nParam++ , ' ' )

	self:cAlias := oQuery:OpenAlias()

	oTempTableBranch:Delete()

	FreeObj(oTempTableBranch)
	FreeObj(oQuery)

	FwFreeArray(aFiliais)

Return
