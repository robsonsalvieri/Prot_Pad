#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.tmk.accomplishedservicings.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.tmk.accomplishedservicings.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="SUA,SUB,SUC,SUS,SU5,SA1,SB1", name="Atendimentos Realizados", country= "ALL" , customTables="SUA,SUB,SUC,SU5,SB1")

//-------------------------------------------------------------------
/*/{Protheus.doc} accomplishedservicingsSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Atendimentos Realizados

@author FAT/CRM
@since 20/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class accomplishedservicingsSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object
    public method TmkSv002CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data aFieldsValue as array
    protected data jItems       as json
	protected data cAlias       as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author FAT/CRM
@since 21/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class accomplishedservicingsSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 		//"Telemarketing"
    self:setDisplayName(STR0002)	//"Atendimentos Realizados"
    self:setDescription(STR0003)	//"Relação de Atendimentos Realizados"
    self:setIsLookUp(.T.)

    self:cAlias         := ""
    self:aStruct        := {}
    self:aFieldsValue   := {}

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author FAT/CRM
@since 21/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class accomplishedservicingsSmartViewBusinessObject

    Local cTipoOpe      := ""  as character
    Local cTipoMkt      := ""  as character
    Local cDescCliPros  := ""  as character
    Local cNomeCliPros  := ""  as character
    Local xValue        := ""  as character
    Local nX            := 0   as numeric
    Local lObfuscated   := .F. as logical
    Local aPDFields     := {}  as array

    MV_TPENT	:= 1
    MV_CODCAMP	:= ""
    MV_FIL      := ""

    //Método para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(),"TMK039")

    //Verifica se precisa fazer o tratamento para LGPD   
    aPDFields	:= FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated	:= Len(aPDFields) > 0

    //Array para processamento
	self:aFieldsValue := {	{'UA_CODOBS',	{'UA_CODOBS',	"",	"MSMM((self:cAlias)->UA_CODOBS,43)"}},;
	                        {'UA_OPER',	    {'UA_OPER',	    "",	"cTipoOpe"}},;
	                        {'UA_TMK',	    {'UA_TMK',	    "",	"cTipoMkt"}},;
						    {'UA_CLIENTE',	{'UA_CLIENTE',	"",	'cDescCliPros + " - " + (self:cAlias)->UA_CLIENTE + " - " + cNomeCliPros'}} }

	// Chama o método que monta a query
    self:TmkSV002CreateSQL()

	DbSelectArea(self:cAlias)
	(self:cAlias)->(dbGoTop()) 

	While !(self:cAlias)->(Eof())

        cTipoOpe      := ""
        cTipoMkt      := ""
        cNomeCliPros  := ""

        If (self:cAlias)->UA_PROSPEC == 'T'
            cDescCliPros := STR0004
            cNomeCliPros := IIf(lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(AllTrim((self:cAlias)->US_NOME)), AllTrim((self:cAlias)->US_NOME))
        Else
            cDescCliPros := STR0005
            cNomeCliPros := IIf(lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(AllTrim((self:cAlias)->A1_NOME)), AllTrim((self:cAlias)->A1_NOME))
        EndIf

		If !Empty((self:cAlias)->UA_OPER)
			cTipoOpe := RetSX3Box(GetSX3Cache("UA_OPER", "X3_CBOX"),,,1)[Val((self:cAlias)->UA_OPER)][1]
		EndIf

        If !Empty((self:cAlias)->UA_TMK)
			cTipoMkt := RetSX3Box(GetSX3Cache("UA_TMK", "X3_CBOX"),,,1)[Val((self:cAlias)->UA_TMK)][1]
		EndIf

        self:jItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)

            If (nScan := aScan(self:aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } )) > 0
				xValue  := FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2])
			Else
				xValue := "(self:cAlias)->"+self:aStruct[nX][5]
			EndIf

			If lObfuscated .and. ( nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5] } ) ) > 0
                self:jItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
            Else
                If Alltrim(self:aStruct[nX][3]) == "date"
                    self:jItems[self:aStruct[nX][1]] := IIF( ValType(&(xValue)) == "C" ,  totvs.framework.treports.date.stringToTimeStamp( &(xValue) ) , totvs.framework.treports.date.dateToTimeStamp( &(xValue) ))
                Else
                    self:jItems[self:aStruct[nX][1]] := &(xValue)
                EndIf
            EndIf

        Next nX

        self:processData(1)
        self:oData:appendData(self:jItems)

        (self:cAlias)->(DbSkip())

	EndDo

    (self:cAlias)->(dbCloseArea())

    FwFreeArray(aPDFields)
    FwFreeArray(self:aFieldsValue)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 21/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object Class accomplishedservicingsSmartViewBusinessObject

    Local nX            := 0 as numeric
    Local aParameters   := {} as array

    self:aFields := {'UA_FILIAL','UA_NUM','UA_EMISSAO','UA_CLIENTE','UA_CODCONT','U5_EMAIL','U5_FCOM1','UA_OPERADO',;
                     'UA_VEND','UA_MIDIA','UA_CODCAMP','UA_CODLIG','UA_TABELA','UA_OPER','UA_TMK','UA_TPFRETE',;
                     'UC_PENDENT','UA_CODOBS','UB_ITEM','UB_PRODUTO','B1_DESC','UB_SITPROD','UB_QUANT',;
                     'UB_VRUNIT','UB_VLRITEM','UB_DESC','UB_VALDESC','UB_ACRE','UB_VALACRE','UB_TES','UB_CF','UB_PRCTAB',;
                     'UB_BASEICM','UB_LOCAL','UB_UM','UB_DTENTRE','UB_LOTE','UB_SUBLOTE','UB_DTVALID'}

    self:aStruct := FatTrGetStruct(self:aFields)

    For nX := 1 To Len(self:aStruct)
	    self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX

    //Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('TMK039',,,{'07','08'} )

    self:oSchema:addParameter("MV_FIL",	STR0008 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.))	// Filial

	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

    self:oSchema:addParameter("MV_CODCAMP", STR0007 + " ?", "string", .F.,,,,,, FTSVHelp('TMK039', '07')) // Campanha
    self:oSchema:addParameter("MV_TPENT",   STR0006 + " ?", "number", .F.,,,,,, FTSVHelp('TMK039', '08')) // Tipo da Entidade

    If __lLookUp
        self:setCustomURL("MV_FIL",     "api/framework/v1/genericLookupService/smartview/SM0", 2) // Filial 
        self:setCustomURL("MV_PAR01",   "api/framework/v1/genericLookupService/smartview/SU7", 2) // Do Operador
        self:setCustomURL("MV_PAR02",   "api/framework/v1/genericLookupService/smartview/SU7", 2) // Ate o Operador
        self:setCustomURL("MV_PAR05",   "api/framework/v1/genericLookupService/smartview/SUH", 2) // Da Midia
        self:setCustomURL("MV_PAR06",   "api/framework/v1/genericLookupService/smartview/SUH", 2) // Ate a Midia
        self:setCustomURL("MV_CODCAMP", "api/framework/v1/genericQuery?tables=SUO&fields=uo_codcamp,uo_desc&DeletedFilter=true&Format=smartview&KeyProperty=uo_codcamp", 2) //Campanha
		self:setCustomURL("MV_TPENT",   "api/framework/treports/integratedprovider/v1/options/TMK039/MV_PAR08", 1) // Tipo da Entidade
	EndIf

    FwFreeArray(aParameters)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} TmkSv002CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 06/02/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method TmkSv002CreateSQL() Class accomplishedservicingsSmartViewBusinessObject

    Local nParam	 	    := 1   as numeric
    Local cQuery  	 	    := ""  as character
    Local cFieldsQry        := ""  as character
    Local cNameTmp          := ""  as character
    Local oQuery 	 	    := Nil as object
	Local oTempTableBranch	:= Nil as object
	Local aFiliais	 		:= {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SUA", "UA_FILIAL")
	cNameTmp := oTempTableBranch:GetTableNameForQuery()

    cFieldsQry := "SUA.UA_FILIAL, SUA.UA_EMISSAO, SUA.UA_CODCONT, SUA.UA_NUM, SUA.UA_OPERADO, SUA.UA_VEND, SUA.UA_CLIENTE, SUA.UA_PROSPEC,"
    cFieldsQry += "SUA. UA_MIDIA, SUA.UA_CODCAMP, SUA.UA_CODLIG, SUA.UA_TABELA, SUA.UA_OPER, SUA.UA_TMK, SUA.UA_TPFRETE, SUA.UA_CODOBS,"
    cFieldsQry += "SUB.UB_ITEM, SUB.UB_PRODUTO, SUB.UB_SITPROD, SUB.UB_QUANT, SUB.UB_BASEICM, SUB.UB_LOCAL, SUB.UB_UM, SUB.UB_DTENTRE,"
    cFieldsQry += "SUB.UB_VRUNIT, SUB.UB_VLRITEM, SUB.UB_DESC, SUB.UB_VALDESC, SUB.UB_ACRE, SUB.UB_VALACRE, SUB.UB_TES,UB_CF, SUB.UB_PRCTAB,"
    cFieldsQry += "SUB.UB_LOTE, SUB.UB_SUBLOTE, SUB.UB_DTVALID, SU5.U5_EMAIL, SU5.U5_FCOM1, SUC.UC_PENDENT, SB1.B1_DESC, SA1.A1_NOME, SUS.US_NOME"
    cFieldsQry += self:cSelectLoc

    //Adiciona campos customizados
	FatInjCposPerson(@self:aFieldsValue, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .T., .T.)

    cQuery := "SELECT ? "
    cQuery += " FROM " + RetSqlName("SUA") + " SUA "
    cQuery += "INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = SUA.UA_FILIAL "
    cQuery += "LEFT JOIN " + RetSqlName("SUB") + " SUB "
    cQuery += " ON SUB.UB_FILIAL    = SUA.UA_FILIAL "
    cQuery += " AND SUB.UB_NUM      = SUA.UA_NUM "
    cQuery += " AND SUB.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN " + RetSqlName("SB1") + " SB1 "
    cQuery += " ON  ? "
    cQuery += " AND SB1.B1_COD      = SUB.UB_PRODUTO "
    cQuery += " AND SB1.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN " + RetSqlName("SA1") + " SA1 "
    cQuery += " ON  ? "
    cQuery += " AND SA1.A1_COD      = SUA.UA_CLIENTE "
    cQuery += " AND SA1.A1_LOJA     = SUA.UA_LOJA "
    cQuery += " AND SA1.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN " + RetSqlName("SUS") + " SUS "
    cQuery += " ON  ? "
    cQuery += " AND SUS.US_COD      = SUA.UA_CLIENTE "
    cQuery += " AND SUS.US_LOJA     = SUA.UA_LOJA "
    cQuery += " AND SUS.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN " + RetSqlName("SU5") + " SU5 "
    cQuery += " ON  ? "
    cQuery += " AND SU5.U5_CODCONT  = SUA.UA_CODCONT "
    cQuery += " AND SU5.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN " + RetSqlName("SUC") + " SUC "
    cQuery += " ON  ? "
    cQuery += " AND SUC.UC_CODIGO   = SUA.UA_NUM "
    cQuery += " AND SUC.D_E_L_E_T_  = ? "
    cQuery += "WHERE SUA.UA_FILIAL  = TMPFIL.TMP_FILIAL "
    cQuery += " AND SUA.UA_OPERADO BETWEEN ? AND ? "
    cQuery += " AND SUA.UA_EMISSAO BETWEEN ? AND ? "
    cQuery += " AND SUA.UA_MIDIA   BETWEEN ? AND ? "
    cQuery += " AND SUA.UA_CANC     = ? "
    cQuery += " AND SUA.UA_OPER     = ? "

    If !Empty(MV_CODCAMP)
        cQuery += " AND SUA.UA_CODCAMP = ? "
    EndIf

    If MV_TPENT <> 3
        cQuery += " AND SUA.UA_PROSPEC = ? "
    EndIf

    cQuery += " ? "     // Where que foi populado no objeto localizado
    cQuery += " AND SUA.D_E_L_E_T_ = ? "
    cQuery += "ORDER BY SUA.UA_FILIAL, SUA.UA_NUM "

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))
    oQuery:SetUnsafe(nParam++, cFieldsQry)
    oQuery:SetUnsafe(nParam++, cNameTmp)
    oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SB1", "SUB"))
    oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SA1", "SUA"))
    oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SUS", "SUA"))
    oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SU5", "SUA"))
    oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SUC", "SUA"))
    oQuery:SetString(nParam++, ' ')
    oQuery:SetString(nParam++, MV_PAR01)
    oQuery:SetString(nParam++, MV_PAR02)
    oQuery:SetString(nParam++, DtoS(MV_PAR03))
    oQuery:SetString(nParam++, DtoS(MV_PAR04))
    oQuery:SetString(nParam++, MV_PAR05)
    oQuery:SetString(nParam++, MV_PAR06)
    oQuery:SetString(nParam++, ' ')
    oQuery:SetString(nParam++, '3')

    If !Empty(MV_CODCAMP)
        oQuery:SetString(nParam++, MV_CODCAMP)
    EndIf

    If MV_TPENT <> 3
        If MV_TPENT == 1
            oQuery:SetString(nParam++, 'F')
        Else
            oQuery:SetString(nParam++, 'T')
        EndIf
    EndIf

    oQuery:SetUnsafe(nParam++, self:cWhereLoc)
    oQuery:SetString(nParam++, ' ')

	self:cAlias := oQuery:OpenAlias()

	oTempTableBranch:Delete()

	FreeObj(oQuery)
	FreeObj(oTempTableBranch)

	FwFreeArray(aFiliais)

Return
