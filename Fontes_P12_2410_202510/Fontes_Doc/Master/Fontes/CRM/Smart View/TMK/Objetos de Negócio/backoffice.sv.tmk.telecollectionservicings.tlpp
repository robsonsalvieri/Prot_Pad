#INCLUDE "protheus.ch"
#INCLUDE "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.tmk.telecollectionservicings.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.tmk.telecollectionservicings.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATMK", tables="ACF,ACG", name="Atendimentos do Telecobrança", country="ALL", customTables="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} telecollectionservicingsSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Atendimentos do Telecobrança.

@author Squad CRM/Faturamento.
@since 12/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class telecollectionservicingsSmartViewBusinessObject From integratedprovider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object
    public method TMKSV009CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data jItems       as json
	protected data cAliasTmpLoc as character
	protected data cAlias       as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Squad CRM/Faturamento.
@since 12/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class telecollectionservicingsSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 		//"Telemarketing"
    self:setDisplayName(STR0002)	//"Atendimentos do Telecobrança"
    self:setDescription(STR0003)    //"Relação de Atendimentos do Telecobrança"
    self:setIsLookUp(.T.)           //Utilizacao do Lookup

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author Squad CRM/Faturamento.
@since 12/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class telecollectionservicingsSmartViewBusinessObject 

    Local aPDFields   := {} as array
    Local cMotivo     := "" as character
    Local cRespons    := "" as character
    Local lObfuscated := .F.as logical
    Local nX          := 0  as numeric
    Local nScan       := 0  as numeric

    MV_FIL	 := ""

    //Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(),"TMK038") 

    //Método que monta a Query
	self:TMKSV009CreateSQL()

    /*
        Função criada p/ retornar somente campos que devem ser ofuscados,
        o inverso do que a função utilizada anteriormente nos retorna.
    */
    aPDFields := FatGetfieldsLGPD(self:aFields, self:getCustomFields()) 
    lObfuscated := Len(aPDFields ) > 0  

	DbSelectArea(self:cAlias)
	(self:cAlias)->(dbGoTop())

    While (self:cAlias)->(!Eof())

        cRespons := ""

        //Tratamento para retornar o motivo
        cMotivo := IIf( Empty((self:cAlias)->ACF_MOTIVO),;
                    STR0004,; //"Não informado no atendimento"
                    IIf( !Empty( (self:cAlias)->U9_CODIGO),;
                        (self:cAlias)->U9_DESC,;
                        STR0005)) //"Não localizado no cadastro"

        cRespons := IIf( !Empty((self:cAlias)->ACG_OPERAD) .And. !lObfuscated,;
                        (self:cAlias)->ACG_OPERAD + " - " + UsrRetName((self:cAlias)->ACG_OPERAD),;
                        IIf( !Empty((self:cAlias)->ACG_OPERAD) .And. lObfuscated,;
                            (self:cAlias)->ACG_OPERAD + " - " + FwProtectedDataUtil():ValueAsteriskToAnonymize(UsrRetName((self:cAlias)->ACG_OPERAD)), cRespons) )

        self:jItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)

            If self:aStruct[nX][5] == "TB_RESPONS"
                self:jItems[self:aStruct[nX][1]] := cRespons
            ElseIf self:aStruct[nX][5] == "ACF_OBS"
                self:jItems[self:aStruct[nX][1]] := FATSVGetMemo((self:cAlias)->ACF_CODOBS) //"Função que retorna conteúdo de campos memo
            ElseIf self:aStruct[nX][5] == "U9_DESC"
                self:jItems[self:aStruct[nX][1]] := cMotivo

            //Verificação para tratamento de LGPD e campos tipo Data antes informar no JSON
			ElseIf lObfuscated .and. (nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0 
				self:jItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
			ElseIf UPPER(self:aStruct[nX][3]) == "DATE"
				self:jItems[self:aStruct[nX][1]] := IIf( ValType((self:cAlias)->&(self:aStruct[nX][5])) == "C", ;
                                                        totvs.framework.treports.date.stringToTimeStamp( (self:cAlias)->&(self:aStruct[nX][5]) ), ;
                                                        totvs.framework.treports.date.dateToTimeStamp( (self:cAlias)->&(self:aStruct[nX][5]) ))
            Else
				self:jItems[self:aStruct[nX][1]] := (self:cAlias)->&(self:aStruct[nX][5])
			EndIf

        Next nX

        self:processData(1)
        self:oData:appendData(self:jItems)

        (self:cAlias)->(dbSkip())

    Enddo

    //Fechamento de tabelas
    (self:cAlias)->(dbCloseArea())

    //limpeza do objeto/variável
    FwFreeArray(aPDFields)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author Squad CRM/Faturamento.
@since 12/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class telecollectionservicingsSmartViewBusinessObject 

    Local aParameters := {} as array
    Local aCposFake   := {} as array
    Local nX          := 0  as numeric

    self:aFields := {"ACF_FILIAL","ACF_DATA","ACF_CODIGO","ACF_OPERA","ACF_PENDEN","ACF_HRPEND","ACF_CLIENT","ACF_LOJA",;
                     "A1_NOME","ACF_CODCON","U5_CONTAT","ACF_OPERAD","U7_NOME","ACF_MOTIVO","U9_DESC",;
                     "ACF_OBS","ACG_PREFIX","ACG_TITULO","ACG_PARCEL","ACG_TIPO","ACG_NATURE","ACG_DTVENC",;
                     "ACG_DTREAL","ACG_VALOR","ACG_ACRESC","ACG_DECRES","ACG_NUMBCO","ACG_HIST","ACG_PORJUR",;
                     "ACG_VALJUR","ACG_STATUS","TB_RESPONS"}

    aCposFake := { {"TBRESPONS", STR0009, "string", STR0009, "TB_RESPONS"} }

    self:aStruct := FatTrGetStruct(self:aFields, aCposFake)

    // Cria a estrutura dos campos Padroes
    For nX := 1 To Len(self:aStruct)
        If self:aStruct[nX][5] == "ACG_NUMBCO"
            self:addProperty(self:aStruct[nX][1], STR0006, self:aStruct[nX][3], STR0006, self:aStruct[nX][5])  //"Banco"
        ElseIf self:aStruct[nX][5] == "ACG_HIST"
            self:addProperty(self:aStruct[nX][1], STR0007, self:aStruct[nX][3], STR0007, self:aStruct[nX][5])  //"Histórico"
        ElseIf self:aStruct[nX][5] == "ACG_VALJUR"
            self:addProperty(self:aStruct[nX][1], STR0008, self:aStruct[nX][3], STR0008, self:aStruct[nX][5])  //"Valor Juros"
        ElseIf self:aStruct[nX][5] == "TB_RESPONS"
            self:addProperty(self:aStruct[nX][1], STR0009, self:aStruct[nX][3], STR0009, self:aStruct[nX][5])  //"Responsavel"
        Else
            self:addProperty(self:aStruct[nX][1],self:aStruct[nX][2],self:aStruct[nX][3],self:aStruct[nX][4],self:aStruct[nX][5])
        EndIf
    Next nX
                                
	aParameters := FtSVSetParam('TMK038',,,,{"01","02","03","05","04","06","07","08","09","10","11","12"})

    // Adiciona novo parametro do tipo string
	self:oSchema:addParameter("MV_FIL", STR0010 + " ?" , "string", .T.,,,,,, FTSVHelp(,,,.T.)) // Filial   

    For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX  

    If __lLookUp	//Validação para versão da Lib
		self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SU9", 2)
		self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SU9", 2)
		self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/SA1", 2)
		self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/SA1", 2)
		self:setCustomURL("MV_PAR07", "api/framework/v1/genericLookupService/smartview/SU7", 2)
		self:setCustomURL("MV_PAR08", "api/framework/v1/genericLookupService/smartview/SU7", 2)
        self:setCustomURL("MV_PAR12", "api/framework/v1/genericLookupService/smartview/SU9", 2)
        self:setCustomURL("MV_PAR11", "api/framework/treports/integratedprovider/v1/options/TMK038/MV_PAR11", 1)
        self:setCustomURL("MV_FIL"  , "api/framework/v1/genericLookupService/smartview/SM0", 2)
	Endif

    FwFreeArray(aParameters)
	FwFreeArray(aCposFake)

Return self:oSchema

/*{Protheus.doc} TMKSV009CreateSQL
	Retorna todas as querys montadas
	@author Squad CRM/Faturamento.
	@since 03/04/2025
	@version 1.0
	@return Nil
*/

Method TMKSV009CreateSQL() Class telecollectionservicingsSmartViewBusinessObject 
    Local cFieldsQry    := ""  as character
    Local cQuery        := ""  as character
    Local cNameTmp      := ""  as character
    Local nParam        := 1   as numeric
    Local aFiliais 		:= {}  as array
    Local oQuery 		:= Nil as object
	Local oTempTableBranch := Nil as object

	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)
    
	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "ACF", "ACF_FILIAL")

    cNameTmp := oTempTableBranch:GetTableNameForQuery()
 
    //Monta campos do Select
    
    cFieldsQry := self:cSelectLoc
    cFieldsQry += " ACF.ACF_FILIAL,ACF.ACF_DATA,ACF.ACF_OPERAD,ACF.ACF_CODIGO,ACF.ACF_OPERA,ACF.ACF_PENDEN,ACF.ACF_HRPEND, "
    cFieldsQry += " ACF.ACF_CLIENT,ACF.ACF_LOJA,ACF.ACF_CODCON,ACF.ACF_MOTIVO,ACF.ACF_CODOBS, "
    cFieldsQry += " SA1.A1_NOME,SU5.U5_CONTAT,SU7.U7_NOME, "
    cFieldsQry += " ACG.ACG_PREFIX,ACG.ACG_TITULO,ACG.ACG_PARCEL, "
    cFieldsQry += " ACG.ACG_TIPO,ACG.ACG_NATURE,ACG.ACG_DTVENC,ACG.ACG_DTREAL,ACG.ACG_VALOR,ACG.ACG_ACRESC, "
    cFieldsQry += " ACG.ACG_DECRES,ACG.ACG_NUMBCO,ACG.ACG_HIST,ACG.ACG_PORJUR,ACG.ACG_VALJUR, "
    cFieldsQry += " ACG.ACG_STATUS,ACG.ACG_OPERAD,SU9.U9_CODIGO,SU9.U9_DESC "
 
    //Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson({}, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .F., .T.)

    cQuery += " SELECT ? "
    cQuery += " FROM " + RetSqlName("ACF") + " ACF "
    cQuery += " INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = ACF.ACF_FILIAL "
    cQuery += " INNER JOIN " + RetSqlName("ACG") + " ACG ON ACG.ACG_FILIAL = ACF.ACF_FILIAL " 
    cQuery += " AND ACG.ACG_CODIGO = ACF.ACF_CODIGO "
    cQuery += " AND ACG.D_E_L_E_T_ = ?  "
    cQuery += " LEFT JOIN " + RetSqlName("SA1") + " SA1 ON ? "
    cQuery += " AND SA1.A1_COD = ACF.ACF_CLIENT "
    cQuery += " AND SA1.A1_LOJA = ACF.ACF_LOJA "
    cQuery += " AND SA1.D_E_L_E_T_ = ? "
    cQuery += " LEFT JOIN " + RetSqlName("SU5") + " SU5 ON ? " 
    cQuery += " AND SU5.U5_CODCONT = ACF.ACF_CODCON "
    cQuery += " AND SU5.D_E_L_E_T_ = ? " 
    cQuery += " LEFT JOIN " + RetSqlName("SU7") + " SU7 ON ? "
    cQuery += " AND SU7.U7_COD = ACF.ACF_OPERAD "
    cQuery += " AND SU7.D_E_L_E_T_ = ? "
    cQuery += " LEFT JOIN " + RetSqlName("SU9") + " SU9 ON ? "
    cQuery += " AND SU9.U9_CODIGO = ACF.ACF_MOTIVO "
    cQuery += " AND SU9.D_E_L_E_T_ = ? "
    cQuery += " WHERE ACF.ACF_FILIAL = TMP_FILIAL "
    cQuery += " AND ACF.D_E_L_E_T_ =  ? "
    cQuery += " AND ACF.ACF_CODIGO BETWEEN ? AND  ? "
    cQuery += " AND ACF.ACF_CLIENT BETWEEN ? AND ? "
    cQuery += " AND ACF.ACF_LOJA BETWEEN ? AND ? "
    cQuery += " AND ACF.ACF_OPERAD BETWEEN ? AND ? "
    cQuery += " AND ACF.ACF_DATA BETWEEN ? AND ? "
    
    If MV_PAR11 <> 3
        cQuery += " AND ACF.ACF_OPERA   = ? " //Se nao for AMBOS, pego apenas ATIVO ou RECEPTIVO
    Endif
    If !Empty(MV_PAR12)
        cQuery += " AND ACF.ACF_MOTIVO  = ? " //Se definiu uma ocorrencia somente ela eh filtrada
    Endif

    //Where que foi populado no objeto localizado
    cQuery += " ? "

    cQuery += " ORDER BY ACF_FILIAL,ACF_CODIGO "

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))
	oQuery:SetUnsafe(nParam++ , cFieldsQry)
    oQuery:SetUnsafe(nParam++ , cNameTmp)
    oQuery:SetString(nParam++ , ' ')
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("SA1", "ACF"))
	oQuery:SetString(nParam++ , ' ')
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU5", "ACF"))
	oQuery:SetString(nParam++ , ' ')
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU7", "ACF"))
	oQuery:SetString(nParam++ , ' ')
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU9", "ACF"))
	oQuery:SetString(nParam++ , ' ')
    oQuery:SetString(nParam++ , ' ')
    oQuery:SetString(nParam++ , MV_PAR01) //Do Atendimento
    oQuery:SetString(nParam++ , MV_PAR02) //Até o Atendimento
    oQuery:SetString(nParam++ , MV_PAR03) //Do Cliente
    oQuery:SetString(nParam++ , MV_PAR05) //Da Loja
    oQuery:SetString(nParam++ , MV_PAR04) //Até o Cliente
    oQuery:SetString(nParam++ , MV_PAR06) //Até a Loja
    oQuery:SetString(nParam++ , MV_PAR07) //Do Operador
    oQuery:SetString(nParam++ , MV_PAR08) //Até o Operador
    oQuery:setDate(nParam++   , MV_PAR09) //Da Data
    oQuery:setDate(nParam++   , MV_PAR10) //Até a Data

    //Se nao for AMBOS, pego apenas ATIVO ou RECEPTIVO
    If MV_PAR11 <> 3
        oQuery:SetString(nParam++ , Str( MV_PAR11,1 )) //Tipo da ligação
    Endif

    //Se definiu uma ocorrencia somente ela eh filtrada
    If !Empty(MV_PAR12)
        oQuery:SetString(nParam++ , MV_PAR12) //Ocorrência
    Endif

    oQuery:SetUnsafe(nParam++ ,self:cWhereLoc)

    self:cAlias := oQuery:OpenAlias()
    self:cAliasTmpLoc := (self:cAlias)

    oTempTableBranch:Delete()
    FwFreeObj(oTempTableBranch)
    
    oQuery:Destroy()
    FreeObj(oQuery)
	oQuery:= Nil

    FwFreeArray(aFiliais) 

Return
