#include "protheus.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.crm.listofsalesprocesses.ch"
#include "fwlibversion.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.crm.listofsalesprocesses.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACRM", tables="AC1,AC2", name="Processos de Venda", country="ALL", customTables="ALL")
 
//-------------------------------------------------------------------
/*/{Protheus.doc} listofsalesprocessesSmartViewBusinessObject
Classe para criação do Objeto de Negócio para listagem de processos de venda

@author FAT/CRM
@since 10/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class listofsalesprocessesSmartViewBusinessObject from IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object
    public method CrmSV002CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data aFieldsValue as array
    protected data cLocSelect   as character
    protected data cLocWhere    as character
    protected data cAlias       as character
    protected data ojItems      as json

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 10/07/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() class listofsalesprocessesSmartViewBusinessObject

    _Super:new()
    self:appendArea(STR0001)        //CRM
    self:setDisplayName(STR0002)    //Processos de Venda
    self:setDescription(STR0003)    //Relação de Processos de Venda
    self:setIsLookUp(.T.)           //Define que terá consulta de LookUp

    self:aFields        := {}
    self:aStruct        := {}
    self:aFieldsValue   := {}
    self:cLocSelect     := ""
    self:cLocWhere      := ""
    self:cAlias         := ""

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 10/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object class listofsalesprocessesSmartViewBusinessObject

    Local aPDFields     := {}   as array
    Local cObserv1      := ""   as character
    Local cObserv2      := ""   as character
    Local xValue        := ""   as variant
    Local lObfuscated   := .F.  as logical
    Local nScan         := 0    as numeric
    Local nX            := 0    as numeric

    MV_FIL := ""

    FatSetValueMVPAR(oFilter:GetParameters(), "FTR020")

    self:aFieldsValue:= {{"AC1_CODMEM", {"AC1_CODMEM", "", 'cObserv1'}},;
                        {"AC2_CODMEM", {"AC2_CODMEM", "", 'cObserv2'}}}           

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated := Len( aPDFields ) > 0

    self:CrmSV002CreateSQL()

    DbSelectArea(self:cAlias)
    (self:cAlias)->(DbGoTop())

    While !(self:cAlias)->(Eof())

        cObserv1  := MSMM((self:cAlias)->AC1_CODMEM)    //"Observações"
        cObserv2  := MSMM((self:cAlias)->AC2_CODMEM)    //"Observações"

        self:ojItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)

            xValue := IIf( (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nX][5]})) > 0,;
                FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2]),;
                "(self:cAlias)->"+self:aStruct[nX][5] )

            self:ojItems[self:aStruct[nX][1]] := IIf( (lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5]})) > 0), aPDFields[nScan][2],;
                IIf(UPPER(self:aStruct[nX][3]) == "DATE",;
                    IIf(ValType(&(xValue)) == "C", totvs.framework.treports.date.stringToTimeStamp(&(xValue)), totvs.framework.treports.date.dateToTimeStamp(&(xValue))),;
                    &(xValue)) )

        Next

        self:processData(1)
        self:oData:appendData(self:ojItems)

        (self:cAlias)->(DbSkip())

    EndDo

    (self:cAlias)->(DbCloseArea())

    aSize(aPDFields, 0)
    aSize(self:aFieldsValue, 0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 10/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object Class listofsalesprocessesSmartViewBusinessObject

    Local nX            := 0  as numeric
    Local aParameters   := {} as array

    self:aFields := {"AC1_FILIAL","AC1_PROVEN","AC1_DESCRI","AC1_CODMEM","AC2_STAGE","AC2_DESCRI","AC2_CODMEM","AC2_RELEVA"}

    self:aStruct := FatTrGetStruct(self:aFields)

	For nX := 1 To Len(self:aStruct)
        self:oSchema:addProperty(self:aStruct[nX][01], self:aStruct[nX][02], self:aStruct[nX][03], self:aStruct[nX][04], self:aStruct[nX][05])
    Next

    // Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('FTR020')

    self:oSchema:addParameter("MV_FIL", STR0009 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.))  // Filial

    // Adiciona os parâmetros do pergunte no Schema
	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

    If __lLookUp
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2) // Filial
        self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/AC1", 2) // Processo/Venda de
        self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/AC1", 2) // Processo/Venda ate
    EndIf

    aSize(aParameters, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} CrmSV002CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 09/06/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method CrmSV002CreateSQL() class listofsalesprocessesSmartViewBusinessObject

	Local cFieldsQry        := ""  as character
    Local cQuery            := ""  as character
    Local cNameTmp          := ""  as character
    Local nParam            := 1   as numeric
    Local aFiliais	        := {}  as array
    Local oQuery            := Nil as object
	Local oTempTableBranch  := Nil as object

	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "AC1", "AC1_FILIAL")

    cNameTmp := oTempTableBranch:GetTableNameForQuery()

    cFieldsQry := "AC1.AC1_FILIAL, AC1.AC1_PROVEN, AC1.AC1_DESCRI, AC1.AC1_CODMEM, AC2.AC2_STAGE, AC2.AC2_DESCRI, AC2.AC2_CODMEM, AC2.AC2_RELEVA "
    cFieldsQry += self:cLocSelect

    //Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson(@self:aFieldsValue, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .T., .T.)

    cQuery := "SELECT ? "
    cQuery += " FROM "     + RetSqlName("AC1") + " AC1 "
    cQuery += "INNER JOIN ? TMPFIL"
	cQuery += " ON TMPFIL.TMP_FILIAL = AC1.AC1_FILIAL "	
    cQuery += "LEFT JOIN " + RetSqlName("AC2") + " AC2 "
    cQuery += " ON  ? "
    cQuery += " AND AC2.AC2_PROVEN  = AC1.AC1_PROVEN "
    cQuery += " AND AC2.D_E_L_E_T_  = ? "
    cQuery += "WHERE AC1.AC1_FILIAL  = TMPFIL.TMP_FILIAL "
    cQuery += " AND AC1.AC1_PROVEN >= ? "
    cQuery += " AND AC1.AC1_PROVEN <= ? "
    cQuery += " ? "
    cQuery += " AND AC1.D_E_L_E_T_  = ? "
    cQuery += "ORDER BY AC1.AC1_FILIAL, AC1.AC1_PROVEN, AC2.AC2_STAGE "

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))
	oQuery:SetUnsafe(nParam++ , cFieldsQry)
    oQuery:SetUnsafe(nParam++ , cNameTmp)
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("AC2", "AC1"))
    oQuery:SetString(nParam++ , " ")
    oQuery:SetString(nParam++ , MV_PAR01)
    oQuery:SetString(nParam++ , MV_PAR02)
    oQuery:SetUnsafe(nParam++ , self:cLocWhere)
    oQuery:SetString(nParam++ , " ")

    self:cAlias := oQuery:OpenAlias()

    oQuery:Destroy()
	FreeObj(oQuery)

	oTempTableBranch:Delete()
	FreeObj(oTempTableBranch)

	aSize(aFiliais, 0)

Return
