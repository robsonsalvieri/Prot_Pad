#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.crm.customeropportunities.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.crm.customeropportunities.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACRM", tables="AD1,AD2,AD3,AD4,AD9,AC1,AC2,AC3,AC4,AIJ,SA3,SB1,SU5,SUS", name="Oportunidades", country="ALL", customTables="AD1")

//-------------------------------------------------------------------
/*/{Protheus.doc} customeropportunitiesSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relação de oportunidades.

@author FAT/CRM
@since 20/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class customeropportunitiesSmartViewBusinessObject From IntegratedProvider
    
    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object
    public method CrmSV001CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data aFieldsValue as array
	protected data cAlias       as character
    protected data cLocSelect   as character
    protected data cLocWhere    as character
    protected data ojItems      as json

EndClass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 20/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method new() Class customeropportunitiesSmartViewBusinessObject 
	
	_Super:new()
    self:appendArea(STR0001) 		//CRM
    self:setDisplayName(STR0002)	//Oportunidades
    self:setDescription(STR0003)	//Relação de Oportunidades
    self:setIsLookUp(.T.)           //Define que terá consulta de LookUp

    self:aFields        := {}
    self:aStruct        := {}
    self:aFieldsValue   := {}
    self:cLocSelect     := ""
    self:cLocWhere      := ""
    self:cAlias         := ""

Return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author FAT/CRM
@since 20/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class customeropportunitiesSmartViewBusinessObject

    Local cStatusAD1    := ""               as character
    Local cVerba        := ""               as character
    Local cTempPerm     := ""               as character
    Local cStatusAIJ    := ""               as character
    Local cVendedor     := ""               as character
    Local xValue        := ""               as variant
    Local lObfuscated   := .F.              as logical
    Local aPDFields     := {}               as array
    Local nX        	:= 0                as numeric
    Local nScan         := 0				as numeric

    MV_FIL := ""
    NORDER := 0

    FatSetValueMVPAR(oFilter:GetParameters() , "FTR010")

    self:aFieldsValue := {  {"AD1_STATUS", 	{"AD1_STATUS",	"", 'cStatusAD1'}},;
                            {"AD1_VERBA", 	{"AD1_VERBA",	"", 'cVerba'    }},;
                            {"TMPPERMAN", 	{"TMPPERMAN",	"", 'cTempPerm' }},;
                            {"NOMEVEND2", 	{"NOMEVEND2",	"", 'cVendedor' }},;
                            {"AIJ_STATUS", 	{"AIJ_STATUS",	"", 'cStatusAIJ'}}}

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated := Len( aPDFields ) > 0

	self:CrmSV001CreateSQL()

    DbSelectArea(self:cAlias)
	(self:cAlias)->(dbGoTop())

    SA3->(DbSetOrder(1))

	//Início da impressão do fluxo do relatório
	While !(self:cAlias)->(Eof())

        cStatusAD1 := SVAD1Status((self:cAlias)->AD1_STATUS)
        cVerba     := IIf(((self:cAlias))->AD1_MOEDA <= MOEDFIN(), xMoeda( ((self:cAlias))->AD1_VERBA, ((self:cAlias))->AD1_MOEDA, MV_PAR22, dDataBase ),xMoeda( ((self:cAlias))->AD1_VERBA, 1, MV_PAR22, dDataBase))
        cTempPerm  := TKCalcPer(SToD((self:cAlias)->AIJ_DTINIC),(self:cAlias)->AIJ_HRINIC,IIf(!Empty(SToD((self:cAlias)->AIJ_DTENCE)),SToD((self:cAlias)->AIJ_DTENCE),dDataBase),IIf(!Empty((self:cAlias)->AIJ_HRENCE),(self:cAlias)->AIJ_HRENCE,SubStr(Time(),1,5)))
        cStatusAIJ := SV10AIJSt((self:cAlias)->AIJ_PROVEN, (self:cAlias)->AIJ_STAGE, (self:cAlias)->AIJ_DTINIC, (self:cAlias)->AIJ_HRINIC, (self:cAlias)->AIJ_STATUS, (self:cAlias)->AC2_FILIAL)      
        cVendedor  := IIf(SA3->(MsSeek((self:cAlias)->A3_FILIAL +(self:cAlias)->AD2_VEND)), SA3->A3_NOME, "")

        self:ojItems := JsonObject():new()
        For nX := 1 To Len(self:aStruct)

            xValue  :=  IIf( (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nX][5]})) > 0,;
                            FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2]),;
                            "(self:cAlias)->" + self:aStruct[nX][5];
                        )

            self:ojItems[self:aStruct[nX][1]] := IIf( lObfuscated .And. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5]})) > 0,;
                                                        aPDFields[nScan][2],;
                                                        IIf( self:aStruct[nX][3] == "date",;
                                                            IIf(ValType(&(xValue)) == "C", totvs.framework.treports.date.stringToTimeStamp(&(xValue)), totvs.framework.treports.date.dateToTimeStamp(&(xValue))),;
                                                            &(xValue);
                                                        );
                                                    )

        Next nX

        self:processdata(1)
        self:oData:appendData(self:ojItems)

        (self:cAlias)->(DbSkip()) 

	EndDo

    (self:cAlias)->(dbCloseArea())

    aSize(aPDFields, 0)
    aSize(self:aFieldsValue, 0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 20/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object Class customeropportunitiesSmartViewBusinessObject

	Local nX 			:= 0  as numeric
	Local aCposFake		:= {} as array
    Local aParameters	:= {} as array

    self:aFields := {   "AD1_FILIAL",;  //"Filial"
                        "AD1_NROPOR",;  //"Código Oportunidade"
                        "AD1_REVISA",;  //"Revisão"
                        "AD1_DESCRI",;  //"Descrição"
                        "AD1_VEND",;    //"Vendedor"
                        "A3_NOME",;     //"Nome Vendedor"
                        "AD1_PROSPE",;  //"Código do Prospect"
                        "AD1_LOJPRO",;  //"Loja Prospect"
                        "US_NOME",;     //"Razão Social"
                        "AD1_PROVEN",;  //"Processo de Venda"
                        "AC1_DESCRI",;  //"Descrição do Processo"
                        "AD1_STAGE",;   //"Estágio"
                        "AC2_DESCRI",;  //"Descrição do Estágio"
                        "AD1_VERBA",;   //"Verba"
                        "AD1_MOEDA",;   //"Moeda"
                        "AD1_CODPRO",;  //"Produto"
                        "AD1_STATUS",;  //"Status"
                        "AD3_CODCON",;  //"Concorrente"
                        "AC3_NOME",;    //"Razão Social Concorrente"
                        "AD4_PARTNE",;  //"Parceiro"
                        "AC4_NOME",;    //"Nome Parceiro"
                        "AD2_VEND",;    //"Vendedor"
                        "NOMEVEND2",;   //"Vendedor"
                        "AD9_CODCON",;  //"Contato"
                        "U5_CONTAT",;   //"Nome Contato"
                        "AIJ_PROVEN",;  //"Processo de Venda"
                        "AIJ_STAGE",;   //"Estágio de Venda"
                        "AIJ_DTINIC",;  //"Data Início"
                        "AIJ_HRINIC",;  //"Hora Início"
                        "AIJ_DTLIMI",;  //"Data Limite"
                        "AIJ_HRLIMI",;  //"Hora Fim"
                        "AIJ_DTENCE",;  //""Data Encerramento
                        "AIJ_HRENCE",;  //"Hora Encerramento"
                        "AIJ_STATUS",;  //"Status Processo de Venda"
                        "TMPPERMAN" }   //"Tempo de Permanência"

    aCposFake := { {"TMPPERMAN", STR0034, "string", STR0034, "TMPPERMAN" },; //"Tempo de Permanência"
                   {"NOMEVEND2", STR0008, "string", STR0008, "NOMEVEND2" } } //"Nome do Vendedor"

    // Carrega os dados dos campos no array do aStruct
    self:aStruct := FatTrGetStruct(self:aFields, aCposFake)

	// Cria a estrutura dos campos Padroes
	For nX := 1 To Len(self:aStruct)
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX

    // Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('FTR010', {'MV_PAR21'} )

    self:oSchema:addParameter("MV_FIL", STR0044 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.))  // Filial
    self:oSchema:addParameter("NORDER", STR0036 + " ?", "number", .F.,,,,,, FTSVHelp(,,.T.))   // Ordem

    // Adiciona os parâmetros do pergunte no Schema
	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

    If __lLookUp
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)                         // Filial
        self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SUS", 2)                         // Prospect de
        self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/SUS", 2)                         // Prospect ate
        self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/SA3", 2)                         // Representante de
        self:setCustomURL("MV_PAR06", "api/framework/v1/genericLookupService/smartview/SA3", 2)                         // Representante ate
        self:setCustomURL("MV_PAR09", "api/framework/v1/genericLookupService/smartview/AC2", 2)                         // Processo/Venda de
        self:setCustomURL("MV_PAR11", "api/framework/v1/genericLookupService/smartview/AC2", 2)                         // Processo/Venda ate
        self:setCustomURL("MV_PAR13", "api/framework/v1/genericLookupService/smartview/SB1", 2)                         // Produto de
        self:setCustomURL("MV_PAR14", "api/framework/v1/genericLookupService/smartview/SB1", 2)                         // Produto ate
        self:setCustomURL("MV_PAR15", "api/framework/v1/genericLookupService/smartview/02",  2)                         // Tipo de
        self:setCustomURL("MV_PAR16", "api/framework/v1/genericLookupService/smartview/02",  2)                         // Tipo ate
        self:setCustomURL("MV_PAR17", "api/framework/v1/genericLookupService/smartview/SBM", 2)                         // Grupo de
        self:setCustomURL("MV_PAR18", "api/framework/v1/genericLookupService/smartview/SBM", 2)                         // Grupo ate
        self:setCustomURL("MV_PAR19", "api/framework/treports/integratedprovider/v1/options/FTR010/MV_PAR19", 1)        // Considerar Oportunidades
        self:setCustomURL("MV_PAR20", "api/framework/treports/integratedprovider/v1/options/FTR010/MV_PAR20", 1)        // Desconsiderar Oportunidades
        self:setCustomURL("NORDER",   "api/faturamento/smartview/v1/options/crm/customeropportunities/SVCRM001/1", 1)   // Ordem
    EndIf

	aSize(aCposFake, 0)
    aSize(aParameters, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVCRM001
Retorna as opçoes disponível para o parâmetro de ordem.

@return array: Array com as opçoes.

@author FAT/CRM
@since 12/12/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Function SVCRM001(cOpcao as character) as array

	Local cDescCombo	:= "" as character
	Local nX			:= 0  as numeric
	Local aCposIndex	:= {} as array
	Local aRet			:= {} as array

	aCposIndex := {	"AD1_NROPOR",;
					"AD1_PROSPE+AD1_LOJPRO",;
					"AD1_CODPRO",;
					"AD1_VEND",;
					"AD1_PROVEN+AD1_STAGE" }

	If cOpcao == "1"
		For nX := 1 To Len(aCposIndex)
			cDescCombo := FatGetStrCpos(StrTokArr(aCposIndex[nX],"+"))
			aAdd(aRet, cDescCombo)
		Next
	EndIf

	aSize(aCposIndex, 0)

Return aRet

//------------------------------------------------------------------------------
/*/{Protheus.doc} SV10AIJSt

Adiciona o status da evolucao da venda no relatorio.

@sample 	SV10AIJSt(cProVen, cStage, cDtIni, cHrIni, cStatus, cFilAC2)

@param		ExpC1	Processo de Venda
@param		ExpC2	Estagio
@param		ExpC3	Data Inicial
@param		ExpC4	Hora Final
@param		ExpC5	Status encerrado.
@param		ExpC6	Filial AC2.

@return	ExpC Status da Evolucao.

@author	    FAT/CRM
@since		20/06/2023
@version	1.0
/*/
//------------------------------------------------------------------------------
Static Function SV10AIJSt(cProVen as character, cStage as character, cDtIni as character, cHrIni as character, cStatus as character, cFilAC2 as character) as character

    Local aArea 		:= GetArea() as array		// Guarda a area atual.
    Local nDtHrLimit	:= 0 		 as numeric		// Data / Hora limite para avancar o estagio.
    Local nDtHrBase		:= 0   	     as numeric		// Database do sistema (Data/Hora).
    Local dDtNotif		:= cTod("//")as date 		// Data que comecara a notificacao.
    Local cHrNotif		:= ""		 as character	// Hora que comecara a notificacao. 
    Local nDtHrNotif	:= 0		 as numeric		// Data / Hora de notificacao.
    Local dDtLimit		:= cTod("//")as date		// Data limite.
    Local cHrLimit		:= ""  		 as character	// Hora limite.
    Local nHrsInt		:= 0		 as numeric		// Hora em inteiro.
    Local cRetorno		:= ""        as character   // Status da evolucao.
    Local cCalcStatus   := STR0037   as character

    If AC2->(MsSeek(cFilAC2+cProVen+cStage))

        // Calcula a data e hora limite para avancar o estagio.
        Ft300LtEst(cDtIni, cHrIni, @dDtLimit,@cHrLimit)

        IIf( ( AC2->AC2_DNOTIF <> 0 .OR. ( !Empty(AC2->AC2_HNOTIF) .AND. AC2->AC2_HNOTIF <> "00:00" ) ),;
            (   dDtNotif := dDtLimit - AC2->AC2_DNOTIF,;
                cHrNotif :=	cHrLimit,;
                nHrsInt  := HoraToInt(AC2->AC2_HNOTIF),;
                SubtDiaHor(@dDtNotif,@cHrNotif,nHrsInt),;
                nDtHrNotif	:= Val(DTOS(dDtNotif)+StrTran(cHrNotif,":",""));
            ),;
        )

        nDtHrLimit	:= Val(DTOS(dDtLimit)+StrTran(cHrLimit,":",""))
        nDtHrBase	:= Val(DTOS(dDataBase)+StrTran(SubStr(Time(),1,5),":",""))

        // Legenda do estagio atual
        cCalcStatus := IIf( nDtHrLimit <> 0,;
                        IIf( nDtHrNotif <> 0 .AND. nDtHrBase >=  nDtHrNotif  .AND. nDtHrNotif <= nDtHrLimit  .AND. nDtHrLimit > nDtHrBase, STR0038,; // "Em alerta"
                            IIf(nDtHrBase > nDtHrLimit, STR0039, cRetorno) ),;      // "Em atraso"
                            STR0037 )                                               // "Em dia" 

    EndIf

    cRetorno :=  IIf(Empty(cStatus), cCalcStatus, X3Combo("AIJ_STATUS", cStatus))

    RestArea(aArea)
    aSize(aArea, 0)

Return(cRetorno)

//------------------------------------------------------------------------------
/*/{Protheus.doc} SVAD1Status

Retorna a descrição do campo AD1_STATUS de acordo com código 

@sample 	SVAD1Status(cAD1Status)

@param		cAD1Status - Recebe o conteúdo do campo AD1_STATUS

@return	    Descrição do Status

@author	    FAT/CRM
@since		20/06/2023
@version	1.0
/*/
//------------------------------------------------------------------------------
Static Function SVAD1Status(cAD1Status as character) as character

    Local cDescri := "" as character

    cDescri :=  IIf(cAD1Status == "1", cAD1Status + STR0040,;
                    IIf(cAD1Status == "2", cAD1Status + STR0041,;
                        IIf(cAD1Status == "3", cAD1Status + STR0042 ,;
                            IIf(cAD1Status == "9", cAD1Status + STR0043, );
                        );
                    );
                )

Return(cDescri)

//-------------------------------------------------------------------
/*{Protheus.doc} CrmSV001CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 09/06/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method CrmSV001CreateSQL() class customeropportunitiesSmartViewBusinessObject

	Local cFieldsQry        := ""  as character
    Local cQuery            := ""  as character
    Local cNameTmp          := ""  as character
    Local nParam            := 1   as numeric
    Local aFiliais	        := {}  as array
    Local aOrder            := {}  as array
    Local oQuery            := Nil as object
	Local oTempTableBranch  := Nil as object

	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "AD1", "AD1_FILIAL")

    cNameTmp := oTempTableBranch:GetTableNameForQuery()

    //Tratamento para permitir apenas o número de moedas utilizadas
    MV_PAR22 := IIf(MV_PAR22 <= 0 .Or. MV_PAR22 > ContaMoeda(), 1, MV_PAR22)

	//Ordenação do relatório
    NORDER := IIf( NORDER == 0, 1, NORDER )
    aOrder := { "AD1.AD1_FILIAL, AD1.AD1_NROPOR", ;                 // 1 - Oportunidade
                "AD1.AD1_FILIAL, AD1.AD1_PROSPE, AD1.AD1_LOJPRO", ; // 2 - Prospects
                "AD1.AD1_FILIAL, AD1.AD1_CODPRO", ;                 // 3 - Produtos
                "AD1.AD1_FILIAL, AD1.AD1_VEND", ;                   // 4 - Vendedores
                "AD1.AD1_FILIAL, AD1.AD1_PROVEN, AD1.AD1_STAGE" }   // 5 - Processo de vend

	cFieldsQry := "AD1.AD1_FILIAL, AD1.AD1_NROPOR, AD1.AD1_REVISA, AD1.AD1_DESCRI, AD1.AD1_VEND,   AD1.AD1_PROSPE, AD1.AD1_LOJPRO, AD1.AD1_PROVEN, AD1.AD1_STAGE, AD1.AD1_VERBA, AD1.AD1_MOEDA, AD1.AD1_CODPRO, AD1.AD1_STATUS, AD1.AD1_LOJPRO, "
	cFieldsQry += "AD2.AD2_VEND,   AD3.AD3_CODCON, AD4.AD4_PARTNE, AD9.AD9_CODCON, AC1.AC1_DESCRI, AC2.AC2_DESCRI, AC3.AC3_NOME,   AC4.AC4_NOME,   SA3.A3_NOME,   SU5.U5_CONTAT, SUS.US_NOME, "
    cFieldsQry += "AIJ.AIJ_PROVEN, AIJ.AIJ_STAGE,  AIJ.AIJ_DTINIC, AIJ.AIJ_HRINIC, AIJ.AIJ_DTLIMI, AIJ.AIJ_HRLIMI, AIJ.AIJ_DTENCE, AIJ.AIJ_HRENCE, AIJ.AIJ_STATUS, AC2.AC2_FILIAL, SA3.A3_FILIAL "
    cFieldsQry += self:cLocSelect

	//Adiciona campos customizados no array da estrutura
	FatInjCposPerson(@self:aFieldsValue, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .F., .T.)

    cQuery := "SELECT ? "
    cQuery += " FROM "    + RetSqlName("AD1") + " AD1 "
    cQuery += "INNER JOIN ? TMPFIL"
	cQuery += " ON TMPFIL.TMP_FILIAL = AD1.AD1_FILIAL "	
    cQuery += "LEFT JOIN " + RetSqlName("AIJ") + " AIJ "
    cQuery += " ON  ? "
    cQuery += " AND AIJ.AIJ_NROPOR  = AD1.AD1_NROPOR "
    cQuery += " AND AIJ.AIJ_REVISA  = AD1.AD1_REVISA "
    cQuery += " AND AIJ.D_E_L_E_T_  = ? "
	cQuery += "LEFT JOIN " + RetSqlName("AD2") + " AD2 "
    cQuery += " ON  ? "
    cQuery += " AND AD2.AD2_NROPOR  = AD1.AD1_NROPOR "
    cQuery += " AND AD2.AD2_REVISA  = AD1.AD1_REVISA "
    cQuery += " AND AD2.D_E_L_E_T_  = ? "
	cQuery += "LEFT JOIN " + RetSqlName("AD3") + " AD3 "
    cQuery += " ON  ? "
    cQuery += " AND AD3.AD3_NROPOR  = AD1.AD1_NROPOR "
    cQuery += " AND AD3.AD3_REVISA  = AD1.AD1_REVISA "
    cQuery += " AND AD3.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN " + RetSqlName("AC3") + " AC3 "
    cQuery += " ON  ? "
    cQuery += " AND AC3.AC3_CODCON  = AD3.AD3_CODCON "
    cQuery += " AND AC3.D_E_L_E_T_  = ? "
	cQuery += "LEFT JOIN " + RetSqlName("AD4") + " AD4 "
    cQuery += " ON  ? "
    cQuery += " AND AD4.AD4_NROPOR  = AD1.AD1_NROPOR "
    cQuery += " AND AD4.AD4_REVISA  = AD1.AD1_REVISA "
    cQuery += " AND AD4.D_E_L_E_T_  = ? "
	cQuery += "LEFT JOIN " + RetSqlName("AD9") + " AD9 "
    cQuery += " ON  ? "
    cQuery += " AND AD9.AD9_NROPOR  = AD1.AD1_NROPOR "
    cQuery += " AND AD9.AD9_REVISA  = AD1.AD1_REVISA "
    cQuery += " AND AD9.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN " + RetSqlName("SU5") + " SU5 "
    cQuery += " ON  ? "
    cQuery += " AND SU5.U5_CODCONT  = AD9.AD9_CODCON "
    cQuery += " AND SU5.D_E_L_E_T_  = ? "
	cQuery += "LEFT JOIN " + RetSqlName("AC1") + " AC1 "
    cQuery += " ON  ? "
    cQuery += " AND AC1.AC1_PROVEN  = AD1.AD1_PROVEN "
    cQuery += " AND AC1.D_E_L_E_T_  = ? "
	cQuery += "LEFT JOIN " + RetSqlName("AC2") + " AC2 "
    cQuery += " ON  ? "
    cQuery += " AND AC2.AC2_PROVEN  = AD1.AD1_PROVEN "
    cQuery += " AND AC2.AC2_STAGE   = AD1.AD1_STAGE "
    cQuery += " AND AC2.D_E_L_E_T_  = ? "
	cQuery += "LEFT JOIN " + RetSqlName("AC4") + " AC4 "
    cQuery += " ON  ? "
    cQuery += " AND AC4.AC4_NOME    = AD4.AD4_PARTNE"
    cQuery += " AND AC4.D_E_L_E_T_  = ? "
	cQuery += "LEFT JOIN " + RetSqlName("SA3") + " SA3 "
    cQuery += " ON  ? "
    cQuery += " AND SA3.A3_COD      = AD1.AD1_VEND "
    cQuery += " AND SA3.D_E_L_E_T_  = ? "
	cQuery += "LEFT JOIN " + RetSqlName("SUS") + " SUS "
    cQuery += " ON  ? "
    cQuery += " AND SUS.US_COD      = AD1.AD1_PROSPE"
    cQuery += " AND SUS.US_LOJA     = AD1.AD1_LOJPRO"
    cQuery += " AND SUS.D_E_L_E_T_  = ? "
    cQuery += "WHERE AD1.AD1_FILIAL = TMPFIL.TMP_FILIAL "
    cQuery += " AND AD1.AD1_PROSPE >= ? AND AD1.AD1_PROSPE <= ? "
    cQuery += " AND AD1.AD1_LOJPRO >= ? AND AD1.AD1_LOJPRO <= ? "
    cQuery += " AND AD1.AD1_VEND   >= ? AND AD1.AD1_VEND   <= ? "
    cQuery += " AND AD1.AD1_DTINI  >= ? AND AD1.AD1_DTFIM  <= ? "
    cQuery += " AND AD1.AD1_PROVEN >= ? AND AD1.AD1_PROVEN <= ? "
    cQuery += " AND AD1.AD1_STAGE  >= ? AND AD1.AD1_STAGE  <= ? "
    cQuery += " AND AD1.AD1_CODPRO >= ? AND AD1.AD1_CODPRO <= ? "
    cQuery += " AND ( AD1.AD1_CODPRO = ? "
    cQuery += "OR EXISTS ( 
    cQuery += "     SELECT SB1.B1_COD "
    cQuery += "      FROM " + RetSqlName("SB1") + " SB1 "
    cQuery += "     WHERE ? "
    cQuery += "      AND SB1.B1_TIPO   >= ? AND SB1.B1_TIPO  <= ?"
    cQuery += "      AND SB1.B1_COD     = AD1.AD1_CODPRO "
    cQuery += "      AND SB1.B1_GRUPO  >= ? AND SB1.B1_GRUPO <= ?"
    cQuery += "      AND SB1.D_E_L_E_T_ = ? ) ) "
    
    If MV_PAR19 > 1
        cQuery += " AND AD1_STATUS = ? "
    EndIf

    If MV_PAR20 > 1
        cQuery += " AND AD1_STATUS <> ? "
    EndIf

    cQuery += " ? "
    cQuery += " AND AD1.D_E_L_E_T_  = ? "
    cQuery += "ORDER BY ? "

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))
    oQuery:SetUnsafe(nParam++ , cFieldsQry)
    oQuery:SetUnsafe(nParam++ , cNameTmp)
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("AIJ", "AD1"))
    oQuery:SetString(nParam++ , " ")
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("AD2", "AD1"))
    oQuery:SetString(nParam++ , " ")
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("AD3", "AD1"))
    oQuery:SetString(nParam++ , " ")
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("AC3", "AD3"))
    oQuery:SetString(nParam++ , " ")
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("AD4", "AD1"))
    oQuery:SetString(nParam++ , " ")
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("AD9", "AD1"))
    oQuery:SetString(nParam++ , " ")
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("SU5", "AD9"))
    oQuery:SetString(nParam++ , " ")
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("AC1", "AD1"))
    oQuery:SetString(nParam++ , " ")
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("AC2", "AD1"))
    oQuery:SetString(nParam++ , " ")
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("AC4", "AD4"))
    oQuery:SetString(nParam++ , " ")
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("SA3", "AD1"))
    oQuery:SetString(nParam++ , " ")
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("SUS", "AD1"))
    oQuery:SetString(nParam++ , " ")
    oQuery:SetString(nParam++ , MV_PAR01)
    oQuery:SetString(nParam++ , MV_PAR03)
    oQuery:SetString(nParam++ , MV_PAR02)
    oQuery:SetString(nParam++ , MV_PAR04)
    oQuery:SetString(nParam++ , MV_PAR05)
    oQuery:SetString(nParam++ , MV_PAR06)
    oQuery:SetString(nParam++ , DToS(MV_PAR07))
    oQuery:SetString(nParam++ , DToS(MV_PAR08))
    oQuery:SetString(nParam++ , MV_PAR09)
    oQuery:SetString(nParam++ , MV_PAR11)
    oQuery:SetString(nParam++ , MV_PAR10)
    oQuery:SetString(nParam++ , MV_PAR12)
    oQuery:SetString(nParam++ , MV_PAR13)
    oQuery:SetString(nParam++ , MV_PAR14)
    oQuery:SetString(nParam++ , Space(Len(AD1->AD1_CODPRO)))
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("SB1", "AD1"))
    oQuery:SetString(nParam++ , MV_PAR15)
    oQuery:SetString(nParam++ , MV_PAR16)
    oQuery:SetString(nParam++ , MV_PAR17)
    oQuery:SetString(nParam++ , MV_PAR18)
    oQuery:SetString(nParam++ , " ")

    If MV_PAR19 > 1 .And. MV_PAR19 <> 5
        oQuery:SetString(nParam++ , Str(MV_PAR19 - 1, 1))
    ElseIf MV_PAR19 > 1
        oQuery:SetString(nParam++ , "9")
    EndIf

    If MV_PAR20 > 1 .And. MV_PAR20 <> 5
        oQuery:SetString(nParam++ , Str(MV_PAR20 - 1, 1))
    ElseIf MV_PAR20 > 1
        oQuery:SetString(nParam++ , "9")
    EndIf

    oQuery:SetUnsafe(nParam++ , self:cLocWhere)
    oQuery:SetString(nParam++ , " ")
    oQuery:SetUnsafe(nParam++ , aOrder[NORDER])

    self:cAlias := oQuery:OpenAlias()

    oQuery:Destroy()
	FreeObj(oQuery)

	oTempTableBranch:Delete()
	FreeObj(oTempTableBranch)

	aSize(aFiliais, 0)
	aSize(aOrder, 0)

Return
