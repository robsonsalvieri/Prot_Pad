#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.crm.opportunityxsaleproc.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.crm.opportunityxsaleproc.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACRM", tables="AD1,AC2", name="Oportunidades X Processo de Vendas", country="ALL", customTables="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} opportunityxsaleprocSmartViewBusinessObject
Classe para criação do Objeto de Negócio para listagem de processos de venda

@author FAT/CRM
@since 02/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
class opportunityxsaleprocSmartViewBusinessObject from IntegratedProvider

    public method new() 		as object
    public method getData() 	as object
    public method getSchema() 	as object
	public method CrmSV004CreateSQL()

    protected data aFields 		as array
    protected data aStruct 		as array
	protected data aFieldsValue as array
	protected data cLocSelect 	as character
    protected data cLocWhere 	as character
    protected data cAlias 		as character
	protected data ojItems 		as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 02/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class opportunityxsaleprocSmartViewBusinessObject

	_Super:new()
    self:appendArea(STR0001) 	 	//CRM
    self:setDisplayName(STR0002) 	//Oportunidades X Processo de Vendas
    self:setDescription(STR0003) 	//Relação de Oportunidades X Processo de Vendas
    self:setIsLookUp(.T.)			//Define que terá consulta de LookUp

    self:aFields        := {}
    self:aStruct        := {}
    self:aFieldsValue   := {}
    self:cLocSelect     := ""
    self:cLocWhere      := ""
    self:cAlias         := ""

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 02/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class opportunityxsaleprocSmartViewBusinessObject

	Local cProcesso		:= ""  as character
	Local cVendedor		:= ""  as character
	Local cEntidade		:= ""  as character
	Local cCodCli		:= ""  as character
	Local cLojaCli		:= ""  as character
	Local cDescCli		:= ""  as character
	Local xValue		:= ""  as variant
	Local nX            := 0   as numeric
	Local nScan			:= 0   as numeric
	Local lObfuscated	:= .F. as logical
	Local aPDFields		:= {}  as array

	MV_FIL  := ""
	MV_VEND := ""

	//Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "FATR060")

	self:aFieldsValue:= {	{"AC2_PROVEN", 	{"AC2_PROVEN",	"", 'cProcesso'}},;
						 	{"AD1_VEND", 	{"AD1_VEND",	"", 'cVendedor'}},;
						 	{"ENTIDADE", 	{"ENTIDADE",	"", 'cEntidade'}},;
						 	{"AD1_CODCLI", 	{"AD1_CODCLI",	"", 'cCodCli'}},;
						 	{"AD1_LOJCLI", 	{"AD1_LOJCLI",	"", 'cLojaCli'}},;
						 	{"AD1_NOMCLI", 	{"AD1_NOMCLI",	"", 'cDescCli'}}}

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated := Len(aPDFields) > 0

	self:CrmSV004CreateSQL()

    DbSelectArea(self:cAlias)
    (self:cAlias)->(DbGoTop())

	While !(self:cAlias)->(Eof())

		cProcesso := (self:cAlias)->AC1_PROVEN + " - " + (self:cAlias)->AC1_DESCRI
		cVendedor := (self:cAlias)->A3_COD     + ' - ' + IIf(lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize((self:cAlias)->A3_NOME), (self:cAlias)->A3_NOME)
		cEntidade := IIf(!Empty((self:cAlias)->AD1_CODCLI),STR0004,STR0005)
		cCodCli   := IIf(!Empty((self:cAlias)->AD1_CODCLI),((self:cAlias)->AD1_CODCLI),((self:cAlias)->AD1_PROSPE))
		cLojaCli  := IIf(!Empty((self:cAlias)->AD1_CODCLI),((self:cAlias)->AD1_LOJCLI),((self:cAlias)->AD1_LOJPRO))	
		cDescCli  := IIf(!Empty((self:cAlias)->AD1_CODCLI),(self:cAlias)->A1_NOME,(self:cAlias)->US_NOME)

		self:ojItems := JsonObject():new()
		For nX := 1 To Len(self:aStruct)

			If (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nX][5]})) > 0
				xValue := FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2])
			Else
				xValue := "(self:cAlias)->" + self:aStruct[nX][5]
			EndIf

			If lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5]})) > 0
				self:ojItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
			ElseIf UPPER(self:aStruct[nX][3]) == "DATE"
				self:ojItems[self:aStruct[nX][1]] := IIf(ValType(&(xValue)) == "C", totvs.framework.treports.date.stringToTimeStamp(&(xValue)), totvs.framework.treports.date.dateToTimeStamp(&(xValue)))
			Else
				self:ojItems[self:aStruct[nX][1]] := &(xValue)
			EndIf

		Next nX

		self:ProcessData(1)
		self:oData:appendData(self:ojItems)

		(self:cAlias)->(dbSkip())

	EndDo

	(self:cAlias)->(dbCloseArea())

	aSize(aPDFields, 0)
	aSize(self:aFieldsValue, 0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 02/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object class opportunityxsaleprocSmartViewBusinessObject

    Local nX        	:= 0  as numeric
	Local aStruCpoFake  := {} as array
	Local aParameters   := {} as array

	self:aFields := {"AD1_FILIAL","AC2_PROVEN","AC2_STAGE","AC2_DESCRI","AD1_NROPOR","AD1_REVISA","AD1_DESCRI","AD1_DATA","AD1_VEND",;
					 "ENTIDADE","AD1_CODCLI","AD1_LOJCLI","AD1_NOMCLI","AD1_CODPRO","B1_DESC","AD1_VERBA"}
					 
	//Array com estrutura dos campos Fake para montar o addproperty
    aStruCpoFake := {{"ENTIDADE", STR0014, "string"}}

	self:aStruct := FatTrGetStruct(self:aFields, aStruCpoFake)

	For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][2], self:aStruct[nX][5])
    Next nX

	// Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('FATR060', {'MV_PAR04'})

    self:oSchema:addParameter("MV_FIL",  STR0021 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.))  			// Filial
    self:oSchema:addParameter("MV_VEND", STR0022 + " ?", "string", .T.,,,,,, FTSVHelp('MTR530P9R1','01'))  	// Vendedor

    // Adiciona os parâmetros do pergunte no Schema
	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

	If __lLookUp
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2) 					// Filial
        self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/AC1", 2) 					// Processo de Venda
        self:setCustomURL("MV_VEND",  "api/framework/v1/genericLookupService/smartview/SA3", 2) 					// Vendedor
		self:setCustomURL("MV_PAR05", "api/framework/treports/integratedprovider/v1/options/FATR060/MV_PAR05", 1) 	// Considerar Oportunidades
		self:setCustomURL("MV_PAR06", "api/framework/treports/integratedprovider/v1/options/FATR060/MV_PAR06", 1) 	// Desconsiderar Oportunidades
    EndIf

	aSize(aStruCpoFake, 0)
	aSize(aParameters, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} CrmSV004CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 09/06/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method CrmSV004CreateSQL() class opportunityxsaleprocSmartViewBusinessObject

	Local cFieldsQry        := ""  as character
    Local cQuery            := ""  as character
    Local cNameTmp          := ""  as character
	Local cConsQry			:= ""  as character
	Local cDescQry			:= ""  as character
    Local nParam            := 1   as numeric
    Local aFiliais	        := {}  as array
    Local oQuery            := Nil as object
	Local oTempTableBranch  := Nil as object

	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "AD1", "AD1_FILIAL")

    cNameTmp := oTempTableBranch:GetTableNameForQuery()
	
	cConsQry := IIf( MV_PAR05 <> 5, Str(MV_PAR05 - 1, 1), '9' )
	cDescQry := IIf( MV_PAR06 <> 5, Str(MV_PAR06 - 1, 1), '9' )

	cFieldsQry := " AD1.AD1_FILIAL,AC2.AC2_PROVEN,AC2.AC2_STAGE,AC2.AC2_DESCRI,AD1.AD1_NROPOR,AD1.AD1_REVISA,AD1.AD1_DESCRI,AD1.AD1_DATA, "
	cFieldsQry += " AD1.AD1_VEND,AD1.AD1_CODCLI,AD1.AD1_LOJCLI,AD1.AD1_CODPRO,AD1.AD1_VERBA,AD1.AD1_LOJPRO,AD1.AD1_PROSPE,AD1.AD1_STAGE, "
	cFieldsQry += " AD1.AD1_PROVEN,AC1.AC1_PROVEN,AC1.AC1_DESCRI,SA1.A1_NOME,SA3.A3_COD,SA3.A3_NOME,SUS.US_NOME,SB1.B1_DESC "
	cFieldsQry += self:cLocSelect

	//Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson(@self:aFieldsValue, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .F., .T.)

    cQuery := "SELECT ? "
	cQuery += " FROM "    + RetSqlName("AD1") + " AD1 "
	cQuery += "INNER JOIN ? TMPFIL "
	cQuery += "	ON TMPFIL.TMP_FILIAL = AD1.AD1_FILIAL "	
	cQuery += "LEFT JOIN " + RetSqlName("AC1") + " AC1 "
	cQuery += " ON  ? "
	cQuery += " AND AC1.AC1_PROVEN  = AD1.AD1_PROVEN "
	cQuery += " AND AC1.D_E_L_E_T_  = ? "
	cQuery += "LEFT JOIN " + RetSqlName("AC2") + " AC2 "
	cQuery += " ON  ? "
	cQuery += " AND AC2.AC2_PROVEN  = AD1.AD1_PROVEN "
	cQuery += " AND AC2.AC2_STAGE   = AD1.AD1_STAGE "
	cQuery += " AND AC2.D_E_L_E_T_  = ? "
	cQuery += "LEFT JOIN " + RetSqlName("SA1") + " SA1 "
	cQuery += " ON  ? "
	cQuery += " AND SA1.A1_COD      = AD1.AD1_CODCLI "
	cQuery += " AND SA1.A1_LOJA     = AD1.AD1_LOJCLI "
	cQuery += " AND SA1.D_E_L_E_T_  = ? "
	cQuery += "LEFT JOIN " + RetSqlName("SA3") + " SA3 "
	cQuery += " ON  ? "
	cQuery += " AND SA3.A3_COD      = AD1.AD1_VEND "
	cQuery += " AND SA3.D_E_L_E_T_  = ? "
	cQuery += "LEFT JOIN " + RetSqlName("SB1") + " SB1 "
	cQuery += " ON  ? "
	cQuery += " AND SB1.B1_COD     	= AD1.AD1_CODPRO "
	cQuery += " AND SB1.D_E_L_E_T_ 	= ? "
	cQuery += "LEFT JOIN " + RetSqlName("SUS") + " SUS "
	cQuery += " ON  ? "
	cQuery += " AND SUS.US_COD      = AD1.AD1_PROSPE "
	cQuery += " AND SUS.US_LOJA     = AD1.AD1_LOJPRO "
	cQuery += " AND SUS.D_E_L_E_T_  = ? "
	cQuery += "WHERE AD1.AD1_FILIAL = TMPFIL.TMP_FILIAL "

	If !Empty(MV_PAR01)
        cQuery += " AND AD1.AD1_DATA >= ? "		// Data De
    EndIf

    If !Empty(MV_PAR02)
        cQuery += " AND AD1.AD1_DATA <= ? "		// Data Ate
    EndIf

    If !Empty(MV_PAR03)
        cQuery += " AND AD1.AD1_PROVEN = ? "	// Processo de venda
    EndIf

    If !Empty(MV_VEND)
        cQuery += " AND AD1.AD1_VEND IN (?) "	// Vendedor
    EndIf

    If MV_PAR05 != 1
        cQuery += " AND AD1.AD1_STATUS = ? "	// Considera Oportunidades
    EndIf

    If MV_PAR06 != 1
        cQuery += " AND AD1.AD1_STATUS <> ? "	// Desconsidera Oportunidades
    EndIf

	cQuery += " ? "								// Where que foi populado no objeto localizado
	cQuery += " AND AD1.D_E_L_E_T_ = ? "
	cQuery += "ORDER BY AD1.AD1_FILIAL, AC2.AC2_STAGE "

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))
	oQuery:SetUnsafe(nParam++, cFieldsQry )
	oQuery:SetUnsafe(nParam++, cNameTmp)
    oQuery:SetUnsafe(nParam++, FwJoinFilial("AC1", "AD1") )
	oQuery:SetString(nParam++, " " )
	oQuery:SetUnsafe(nParam++, FwJoinFilial("AC2", "AD1") )
	oQuery:SetString(nParam++, " " )
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SA1", "AD1") )
	oQuery:SetString(nParam++, " " )
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SA3", "AD1") )
	oQuery:SetString(nParam++, " " )
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SB1", "AD1") )
	oQuery:SetString(nParam++, " " )
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SUS", "AD1") )
	oQuery:SetString(nParam++, " " )

    If !Empty(MV_PAR01)
        oQuery:SetString(nParam++, DToS(MV_PAR01))
    EndIf

	If !Empty(MV_PAR02)
        oQuery:SetString(nParam++, DToS(MV_PAR02))
    EndIf

    If !Empty(MV_PAR03)
        oQuery:SetString(nParam++, MV_PAR03)
    EndIf

	If !Empty(MV_VEND)
        oQuery:SetIn(nParam++, StrTokArr2(MV_VEND, ';', .F. ))
    EndIf

	If MV_PAR05 != 1
        oQuery:SetString(nParam++, cConsQry)
    EndIf

	If MV_PAR06 != 1
        oQuery:SetString(nParam++, cDescQry)
    EndIf

	oQuery:SetUnsafe(nParam++, self:cLocWhere )
	oQuery:SetString(nParam++, " " )

    self:cAlias := oQuery:OpenAlias()

    oQuery:Destroy()
	FreeObj(oQuery)

	oTempTableBranch:Delete()
	FreeObj(oTempTableBranch)

	aSize(aFiliais, 0)

Return
