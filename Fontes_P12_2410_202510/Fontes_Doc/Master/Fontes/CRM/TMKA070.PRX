#INCLUDE "TMKA070.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "CRMDEF.CH"
#INCLUDE "FWLIBVERSION.CH"

Static __aPrepared	:={}
Static __oRegAGA	:= Nil
Static __oRegAGB	:= Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ TmkA070  ³ Autor ³ Vendas CRM            ³ Data ³ 28/08/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Cadastro de Contatos - Com entrada Automatica              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ aExp1 - Array com os dados ; nExp2 - Opcao do aRotinas     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±± 
±±³Uso       ³ CALL CENTER , FIELD SERVICE                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß  
/*/
Function TmkA070(	aRotAuto, nOpc, xAutoAGA, xAutoAGB, xPrefSU5, cFilDef, aAddFil, cVDefault )

Private aAC			:= { STR0001	,STR0002 }	// "Abandona","Confirma"
Private aRotina		:= MenuDef()				// Define as rotinas utilizadas
Private cCadastro	:= STR0009					// "Atualiza‡„o de Contatos"
Private aAutoCab								// Cabeçalho do contato SU5
Private aAutoAGA								// Itens da rotina automática para a tabela AGA
Private aAutoAGB								// Itens da rotina automática para a tabela AGB
Private lPrefSU5	:=	.F.

Static lIntegDef  := Nil

Default cFilDef   := ""
Default aAddFil   := {}
Default cVDefault := ""

If nModulo == 73
	aRotina := FwLoadMenuDef("CRMA470")
Else	
	aRotina := MenuDef()
EndIf

lIntegDef := FWHasEAI("TMKA070",.T.,,.T.)

//Tratamento para execução através do MILE, pois por default os parâmetros são passados como array.
//Se for criado algum parâmetro que será utilizado pela EXECAUTO, deverá ser incluído um tratamento neste bloco
If FwIsInCallStack("FWMILEProA")
	nOpc      := IIf(ValType(nOpc) == "A" .And. Len(nOpc) > 0, nOpc[1], Nil)
	xPrefSU5  := IIf(ValType(xPrefSU5) == "A" .And. Len(xPrefSU5) > 0, xPrefSU5[1], Nil)
	cFilDef   := IIf(ValType(cFilDef) == "A" .And. Len(cFilDef) > 0, cFilDef[1], "")
	cVDefault := IIf(ValType(cVDefault) == "A" .And. Len(cVDefault) > 0, cVDefault[1], "")
EndIf

If (nOpc == NIL)
	nOpc := 3
Else
	nOpc := nOpc
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao de variaveis para rotina de inclusao automatica    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private lTk070Auto := ( aRotAuto <> NIL )

If lTk070Auto
	If aRotAuto <> Nil
		aAutoCab := aRotAuto
	EndIf
	If xAutoAGA <> Nil
		aAutoAGA := xAutoAGA
	EndIf
	If xAutoAGB <> Nil
		aAutoAGB := xAutoAGB
	EndIf
	If xPrefSU5 <> Nil .Or. ( xAutoAGA == Nil .And. xAutoAGB == Nil )
		lPrefSU5 := xPrefSU5
	EndIf
	MsRotAuto(nOpc,aRotAuto,"SU5")
Else
	//-------------------------------
	// Browse de Contatos
	//-------------------------------
	BrowseDef( /*oMBrowse*/, cFilDef, aAddFil, cVDefault )	
Endif

Return(.T.) 

//------------------------------------------------------------------------------
/*/{Protheus.doc} BrowseDef

Browse de Contatos

@sample	BrowseDef( oMBrowse, cFilDef, aAddFil, cVDefault )

@param		oMBrowse	, Objeto	, Browse criado pelo Widget da Area de Trabalho.
			cFilDef	, Caracter	, Filtro padrao.
			aAddFil	, Array		, Filtros relacionados.
			cVDefault	, Caracter , Visao padrao.
			
@return	oMBrowse	, Objeto	, Retorna o objeto FWMBrowse.

@author	Anderson Silva
@since		05/12/2015
@version	P12
/*/
//------------------------------------------------------------------------------
Static Function BrowseDef( oMBrowse, cFilDef, aAddFil, cVDefault )

Local nX			:= 0
Local oTableAtt	:= Nil
Local lWidget 	:= .F.
Local cMainProc := IIF(nModulo==73,"CRMA470","TMKA070")

Default oMBrowse	:= Nil
Default cFilDef	:= ""
Default aAddFil	:= {}
Default cVDefault	:= ""

If Empty( oMBrowse )
	oMBrowse := FWMBrowse():New()
	oMBrowse:SetAlias("SU5")
Else
	lWidget	:= .T.
EndIf	

oMBrowse:SetCanSaveArea(.T.) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Filtro Default do Browse                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty( cFilDef )
	oMBrowse:SetFilterDefault( cFilDef )
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Filtros adicionais do Browse                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nX := 1 To Len( aAddFil )
	oMBrowse:DeleteFilter( aAddFil[nX][ADDFIL_ID] )
	oMBrowse:AddFilter( aAddFil[nX][ADDFIL_TITULO], ;
					      aAddFil[nX][ADDFIL_EXPR], ;
			             aAddFil[nX][ADDFIL_NOCHECK], ;
					      aAddFil[nX][ADDFIL_SELECTED], ;
					      aAddFil[nX][ADDFIL_ALIAS], ;
					      aAddFil[nX][ADDFIL_FILASK], ;
					      aAddFil[nX][ADDFIL_FILPARSER], ;
					      aAddFil[nX][ADDFIL_ID] )		 
	oMBrowse:ExecuteFilter()	
Next nX

oMBrowse:SetDescription(STR0009)  // "Atualizacao de Contatos"

If !lWidget 
	oTableAtt := TableAttDef()
	oMBrowse:SetAttach( .T. ) 				  //Habilita as visões do Browse	
	oMBrowse:SetViewsDefault(oTableAtt:aViews)
	oMBrowse:SetChartsDefault(oTableAtt:aCharts)
	
	// selecionando a visão inicial 
	If !Empty( cVDefault ) 
		nScan := aScan( oTableAtt:aViews, { |x| x:cID == cVDefault } )
		If nScan > 0 
			oMBrowse:SetIDViewDefault( oTableAtt:aViews[nScan]:cID )
		EndIf
	EndIf
		
	oMBrowse:SetIDChartDefault( "UltCont" )
	oMBrowse:SetMainProc(cMainProc)
	oMBrowse:SetTotalDefault('U5_CODCONT','COUNT',STR0061) //'Total de Registros'
	oMBrowse:Activate()

EndIf

Return Nil

//------------------------------------------------------------------------------
/*/	{Protheus.doc} TableAttDef

Cria as visões e gráficos padrão.

@sample	TableAttDef()

@param		Nenhum

@return	ExpA - Array de Objetos com as Visoes e Gráficos.

@author	Aline Kokumai
@since		02/05/2014
@version	12
/*/
//------------------------------------------------------------------------------
Static Function TableAttDef()

Local oTableAtt	:= FWTableAtt():New()
Local oMyAtivos	:= Nil // Meus Contatos Ativos
Local oAtivos	:= Nil // Contatos Ativos
Local oInativos	:= Nil // Contatos Inativos
Local oUltCont	:= Nil // Colunas: Contatos Por Data do Último Contato

//----------
// Visões
//---------- 
	
// Meus Contatos Ativos
oMyAtivos := FWDSView():New()
oMyAtivos:SetName(STR0057) //"Meus Contatos Ativos"
oMyAtivos:SetID("MyAtivos") 
oMyAtivos:SetOrder(1) // U5_FILIAL+U5_CODCONT+U5_IDEXC
oMyAtivos:SetCollumns({"U5_CODCONT", "U5_CONTAT", "U5_END", "U5_BAIRRO", "U5_MUN",;
							"U5_EST", "U5_DDD", "U5_FONE", "U5_EMAIL", "U5_STATUS"})
oMyAtivos:SetPublic( .T. )
oMyAtivos:AddFilterRelation( 'AO4', 'AO4_CHVREG', 'U5_FILIAL+U5_CODCONT' )  
oMyAtivos:AddFilter(STR0057, "U5_MSBLQL == '2'") //"Meus Contatos Ativos"
oMyAtivos:AddFilter(STR0057, "AO4_CODUSR == '"+RetCodUsr()+"' .AND. AO4_CTRLTT == .T.","AO4") //"Meus Contatos Ativos"
oTableAtt:AddView(oMyAtivos)
	
// Contatos Ativos
oAtivos := FWDSView():New()
oAtivos:SetName(STR0058) //"Contatos Ativos"
oAtivos:SetID("Ativos") 
oAtivos:SetOrder(1) // U5_FILIAL+U5_CODCONT+U5_IDEXC
oAtivos:SetCollumns({"U5_CODCONT", "U5_CONTAT", "U5_END", "U5_BAIRRO", "U5_MUN",;
							"U5_EST", "U5_DDD", "U5_FONE", "U5_EMAIL", "U5_STATUS"})
oAtivos:SetPublic( .T. )
oAtivos:AddFilter(STR0058, "U5_MSBLQL == '2'") //"Contatos Ativos"
oTableAtt:AddView(oAtivos)
	
// Contatos Inativos
oInativos := FWDSView():New()
oInativos:SetName(STR0059) //"Contatos Inativos"
oInativos:SetID("Inativos") 
oInativos:SetOrder(1) // U5_FILIAL+U5_CODCONT+U5_IDEXC
oInativos:SetCollumns({"U5_CODCONT", "U5_CONTAT", "U5_END", "U5_BAIRRO", "U5_MUN",;
							"U5_EST", "U5_DDD", "U5_FONE", "U5_EMAIL", "U5_STATUS"})
oInativos:SetPublic( .T. )
oInativos:AddFilter(STR0059, "U5_MSBLQL == '1'") //"Contatos Inativos"
oTableAtt:AddView(oInativos)
	
//------------
// Gráficos
//------------
		
// Colunas: Contatos Por Data do Último Contato
oUltCont := FWDSChart():New()
oUltCont:SetName(STR0060) //"Contatos Por Data do Último Contato"
oUltCont:setTitle(STR0060) //"Contatos Por Data do Último Contato"
oUltCont:SetID("UltCont") 
oUltCont:SetType("BARCOMPCHART")
oUltCont:SetSeries({ {"SU5", "U5_CODCONT", "COUNT"} })
oUltCont:SetCategory( { {"SU5", "U5_ULTCONT"} } )
oUltCont:SetPublic( .T. )
oUltCont:SetLegend( CONTROL_ALIGN_BOTTOM ) //Inferior
oUltCont:SetTitleAlign( CONTROL_ALIGN_CENTER ) 			
oTableAtt:AddChart(oUltCont)

oTableAtt:SetAlias("SU5")

Return (oTableAtt)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    | MenuDef	³ Autor ³ Venda CRM             ³ Data ³08/12/06  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Funcao de definição do aRotina                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ aRotina   retorna a array com lista de aRotina             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGATMK                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()

Local aRotAdic	:= {}						// Retorno do P.E. TK070ROT
Local lTk070Rot 	:= ExistBlock("TK070ROT")	// Ponto de entrada - Adiciona rotinas ao aRotina 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transação a ser efetuada                          ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³    2 - Simplesmente Mostra os Campos                         ³
//³    3 - Inclui registros no Bancos de Dados                   ³
//³    4 - Altera o registro corrente                            ³
//³    5 - Remove o registro corrente do Banco de Dados          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aRotina		:= {}						// Opções do programa
Local aPermissoes	:= {}						// Opções de acesso aos itens do menu
Local aEntRelac  := {}

aRotina :=	{	{ STR0004,"AxPesqui"	, 0 , 1 , 	0	, .F.	}}		//"Pesquisar"

If IsInCallStack("FATA320") 
	aPermissoes := FT060Permi(__cUserId, "ACA_ACCONT")
Else
	aPermissoes := {.T.,.T.,.T.,.T.}
EndIf            
If aPermissoes[4]
	aAdd(aRotina, { STR0005,"A70Visual"	, 0 , 2 , 	0	, .T.	})		//"Visualizar"
EndIf
If aPermissoes[1]
	aAdd(aRotina, { STR0006,"A70Inclui"	, 0 , 3 , 	0	, .T.	})		//"Incluir"
EndIf
If aPermissoes[2]
	aAdd(aRotina, { STR0007,"A70Altera"	, 0 , 4 , 82	, .T. 	})		//"Alterar"
EndIf
If aPermissoes[3]	
	aAdd(aRotina, { STR0008,"A70Exclui"	, 0 , 5 , 3 	, .T.	})		//"Excluir"
EndIf

Aadd(aEntRelac,{STR0042,"CRMA150", 0 , 4,0,.F.})//"SubContatos"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada - Adiciona rotinas ao aRotina               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lTk070Rot
	aRotAdic := ExecBlock("TK070ROT",.F.,.F.)
	If ValType(aRotAdic) == "A"
		AEval(aRotAdic,{|x| AAdd(aRotina,x)})
	Endif
Endif

Asort(aEntRelac,,,{ | x,y | y[1] > x[1] } )
Aadd(aRotina, {STR0052,aEntRelac	, 0 , 8 , 3 	, .T.	})//"Relacionadas"

Return(aRotina)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A070Inclui³ Autor ³ Vendas CRM            ³ Data ³ 01/03/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Inclusao de Contatos                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER , FIELD SERVICE                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A70INCLUI(cAlias,nReg,nOpc)

Local nOpcA 	:= 0									 // Retorno da gravacao OK ou CANCELA
Local lTMKINCU5	:= ExistBlock("TMKINCU5") 			 	// P.E  Antes da inclusao do contato.
Local lTk070ABOT:= ExistBlock("TK070ABOT")			 	// P.E  Para inclusão de botões ao aButtons (Enchoicebar).
Local lTK070INC := ExistBlock("TK070INC")			 	// P.E apos a gravacao de dados
Local aButadic	:= {}									 // Array de apoio para o ponto de entrada TK070ABOT.
Local aButtons  := {}

Default cAlias	:= "SU5"
Default nReg	:= Recno()
Default nOpc	:= 3

If IsInCallStack("TK271CallCenter")
	SetKey(K_CTRL_T, Nil)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Desvia a rotina para a nova versão do cadastro de manutenção.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If A70Normatizado()
	INCLUI := .T.
	ALTERA := .F.				 
	nOpcA := A70Manut( cAlias, nReg, nOpc )
	Return( nOpcA )
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se utilizado pelo Modulo Juridico, cria o botao "Especialidade" ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nModulo == 76 .Or. nModulo == 77 //JURI e PFS
	If nOpc == 4 //Alterar  
		If ( FindFunction("JA132AEsp") ) .And. ( AliasInDic("NWA") )
	   		AAdd( aButtons, {"EDITABLE", { || JA132AEsp( cAlias, nReg, nOpc ) }, STR0039, STR0039 } ) //"Especialidade"
	 	EndIf
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada - Adiciona rotinas ao aButtons (Enchoicebar)³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lTk070ABOT
	aButadic := ExecBlock("TK070ABOT",.F.,.F.)
	If ValType(aButadic) == "A"
		AEval(aButadic,{|x| AAdd(aButtons,x)})
	Endif
Endif

If ( Type("lTk070Auto") == "U" )
	lTk070Auto := .F.
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia para processamento dos Gets          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOpcA:=0

If !( lTk070Auto )
	nOpcA := AxInclui(cAlias, nReg, nOpc,/*aAcho*/,/*cFunc*/,/*aCpos*/,"Tk070TudOk()"/*cTudoOk*/,/*lF3*/,/*cTransact*/,aButtons)

	If nOpcA == 1	// OK
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Executa o P.E.     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lTMKINCU5
			ExecBlock("TMKINCU5",.F.,.F.)
		Endif
	Endif
Else
	nOpcA := AxIncluiAuto(cAlias)
Endif  

If nOpcA == 1	// OK
	If lTK070INC
		ExecBlock("TK070INC",.F.,.F.)
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Integra Protheus x LEGAL DESK - SIGAPFS             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//Grava na fila de sincronização se o parâmetro MV_JFSINC = '1' - SIGAPFS
	If FindFunction("J170GRAVA")
		J170GRAVA("SU5", xFilial("SU5") + SU5->U5_CODCONT, "3") //U5_FILIAL+U5_CODCONT
	EndIf
	
EndIf

DbSelectarea(cAlias)
Return(nOpcA)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A070Altera³ Autor ³ Vendas CRM            ³ Data ³ 01/03/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Alteração de Contatos	                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER , FIELD SERVICE                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A70ALTERA(cAlias,nReg,nOpc)

Local nOpca := 0
Local lTMKALTA5	:= ExistBlock("TMKALTA5") 			 	// P.E  Antes da alteracao do contato.
Local lTMKALTU5	:= ExistBlock("TMKALTU5") 			 	// P.E  Depois da alteracao do contato.
Local lTk070ABOT:= ExistBlock("TK070ABOT")			 	// P.E  Para inclusão de botões ao aButtons (Enchoicebar).
Local aButadic	:= {}									 // Array de apoio para o ponto de entrada TK070ABOT.
Local lRet 		:= .T.
Local aButtons  := {}

Default cAlias	:= "SU5"
Default nReg	:= Recno()
Default nOpc	:= 4

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Desvia a rotina para a nova versão do cadastro de manutenção.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If A70Normatizado()			 
	INCLUI := .F.
	ALTERA := .T.	 
	nOpcA := A70Manut( cAlias, nReg, nOpc )
	Return( nOpcA )
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se utilizado pelo Modulo Juridico, cria o botao "Especialidade" ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nModulo == 76 .Or. nModulo == 77 //JURI e PFS
	If nOpc == 4 //Alterar  
		If ( FindFunction("JA132AEsp") ) .And. ( AliasInDic("NWA") )
			AAdd( aButtons, {"EDITABLE", { || JA132AEsp( cAlias, nReg, nOpc ) }, STR0039, STR0039 } ) //"Especialidade"  
		EndIf
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada - Adiciona rotinas ao aButtons (Enchoicebar)³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lTk070ABOT
	aButadic := ExecBlock("TK070ABOT",.F.,.F.)
	If ValType(aButadic) == "A"
		AEval(aButadic,{|x| AAdd(aButtons,x)})
	Endif
Endif

If lTMKALTA5
	lRet := U_TMKALTA5(nReg)
	
	// Se o retorno for .F. nao prossegue com o cancelamento
	If (ValType(lRet) <> "L")
		Return(.F.)
	ElseIf !lRet
		Return(.F.)
	Endif
Endif

aButtons := CRMXAddAct("SU5",aButtons) //Adcionar Rotinas no 'Ações relacionadas' do Formulário

If ( Type("lTk070Auto") == "U" .OR. !lTk070Auto )
	nOpcA:=AxAltera( cAlias, nReg, nOpc,/*aAcho*/,/*aCpos*/,/*nColMens*/,/*cMensagem*/,/*cTudoOk*/,/*cTransact*/,/*cFunc*/,aButtons)
Else
	nOpcA := AxIncluiAuto(cAlias,NIL,NIL,nOpc,SU5->(RecNo()))
Endif

If nOpcA == 1	// OK
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Executa o P.E.     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lTMKALTU5
		ExecBlock("TMKALTU5",.F.,.F.)
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Integra Protheus x LEGAL DESK - SIGAPFS             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//Grava na fila de sincronização se o parâmetro MV_JFSINC = '1' - SIGAPFS
	If FindFunction("J170GRAVA")
		J170GRAVA("SU5", xFilial("SU5") + SU5->U5_CODCONT, "4") //U5_FILIAL+U5_CODCONT
	EndIf

Endif

DbSelectarea(cAlias)
Return(nOpcA)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A080Visual³ Autor ³ Vendas CRM            ³ Data ³ 01/03/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Visualiza‡ao de Contatos                      			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER , FIELD SERVICE                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A70Visual(cAlias,nReg,nOpc)

Local lTk070ABOT:= ExistBlock("TK070ABOT")			 	// P.E  Para inclusão de botões ao aButtons (Enchoicebar).
Local aButadic	:= {}									 // Array de apoio para o ponto de entrada TK070ABOT.
Local aButtons  := {}
Local nOpcA		:= 0
Local lBkpINCLUI

Default cAlias	:= "SU5"
Default nReg	:= Recno()
Default nOpc	:= 2

If IsInCallStack("Conpad1")
	lBkpINCLUI	:= INCLUI
	INCLUI		:= .F.
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Desvia a rotina para a nova versão do cadastro de manutenção.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If A70Normatizado()			 
	nOpcA := A70Manut( cAlias, nReg, nOpc )
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de entrada - Adiciona rotinas ao aButtons (Enchoicebar)³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lTk070ABOT
		aButadic := ExecBlock("TK070ABOT",.F.,.F.)
		If ValType(aButadic) == "A"
			AEval(aButadic,{|x| AAdd(aButtons,x)})
		Endif
	Endif
	aButtons := CRMXAddAct("SU5",aButtons) //Adcionar Rotinas no 'Ações relacionadas' do Formulário
	nOpcA := AxVisual( cAlias, nReg, nOpc,/*aAcho*/,/*nColMens*/,/*cMensagem*/,/*cFunc*/,aButtons)
	DbSelectarea(cAlias)
EndIf

If IsInCallStack("Conpad1")
	INCLUI := lBkpINCLUI
EndIf
Return(nOpcA)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A70Exclui ³ Autor ³ Vendas CRM            ³ Data ³01/03/00  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Exclusao do Contato do Cliente                 			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER , FIELD SERVICE                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A70Exclui(cAlias,nReg,nOpc)

Local nOpcA										// Retorno da exclusao - OK ou CANCELA
Local oDlg										// TELA 
Local lRet 		:= .F.							// Retorno da funcao - TRUE exclusao OK, FALSE exclusao NEGADA
Local lTMKDELU5	:= ExistBlock("TMKDELU5")   	// P.E  Antes da exclusao do contato.
Local cTexto    := ""							// Texto para o HELP da validacao antes da exclusao
Local nBusca    := 0 
Local nRecnoSM0 := SM0->(RecNo())
Local cFilAtu	:= SM0->M0_CODFIL
lOCAL aSM0CodFil:= {}
Local lPai			:= .F.

Default cAlias	:= "SU5"
Default nReg	:= Recno()
Default nOpc	:= 5

//Verifica se o registro é contato primário
lPai := PermExcPai(SU5->(U5_FILIAL),SU5->(U5_CODCONT))
If lPai
	Return .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Desvia a rotina para a nova versão do cadastro de manutenção.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If A70Normatizado()			 
	nOpcA := A70Manut( cAlias, nReg, nOpc )
	If nOpcA <> 0
		lRet := .T.
	Else
		If ( Type("lTk070Auto") == "L" .And. lTk070Auto .And. Type("lMsErroAuto") == "L"  )
			lMsErroAuto := .T.
		EndIf
	EndIf
	Return(lRet)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se o P.E. existe e se o Retorno for FALSE sai da exclusao³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lTMKDELU5
	If !U_TMKDELU5(cAlias,nReg)
		Return(lRet)
	Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se existe algum atendimento para o contato na tabela de TELEMARKETING³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectarea("SUC")
DbSetorder(6)
If DbSeek(xFilial("SUC")+SU5->U5_CODCONT)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se nao for uma entrada automatica exibe o HELP  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( Type("lTk070Auto") == "U" .OR. !lTk070Auto )
		HELP(" ",1,"GRAVADO")
	Else
		Return(lRet)
	Endif		
Else

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Valida a exclusao na tabela de Contatos x Entidades        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectarea("AC8")	
	DbSetOrder(1)
	If DbSeek(xFilial( "AC8" ) + SU5->U5_CODCONT)
		SX2->( DbSetOrder(1) )
		SX2->( DbSeek( AC8->AC8_ENTIDA ) )
			
		cTexto := ""
		If !Empty(AC8->AC8_FILENT)
			cTexto := STR0022 + AC8->AC8_FILENT + STR0023 + AllTrim( AC8->AC8_CODENT ) 
		Endif
			
		//"Atencao !"Este contato nao pode ser excluido pois esta associado a entidade '"____", Filial "____", chave "____"Ok"		
		Aviso( STR0020, STR0021 + AllTrim( X2NOME() ) + "'" + cTexto + ".", { STR0024 }, 2 ) 
	Else
		lRet := .T.
	Endif
Endif	

If lRet
	// Preenche um array com as filiais
	DbSelectArea("SM0")
	DbGoTop()
	Do While ! Eof()
		If SM0->M0_CODIGO == cEmpAnt
			Aadd(aSM0CodFil, cFilAtu)
		Endif
		dbSkip()
	End	
	DbGoto(nRecnoSM0)
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se existe Atendimento para o contato ADE            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SX2")
	dbSetOrder(1)
	If dbSeek("ADE") .AND. GetRpoRelease("R1.3")
		DbSelectArea("ADE")
		DbSetOrder(14)
		aFiliais := If(! Empty(xFilial()) .AND. Empty(xFilial("SU5")), aClone(aSM0CodFil), {xFilial()})
		For nBusca := 1 To Len(aFiliais)
	       	If DbSeek(aFiliais[nBusca]+SU5->U5_CODCONT)
	      		Help(" ",1,"TMKNDELADE")
			   	lRet := .F. 
				Exit
			EndIf
		Next			
	EndIf
Endif
		
DbSelectarea("SU5")
If !lRet
	Return(lRet)
Endif
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aTELA[0][0],aGETS[0]
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia para processamento dos Gets          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOpcA:=0
	
SoftLock(cAlias)
	
If ( Type("lTk070Auto") == "U" .OR. !lTk070Auto )
	
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM 9,0 TO 28,80 OF oMainWnd
		
		nOpcA := EnChoice( cAlias, nReg, nOpc, aAC,"AC",STR0012) //"Quanto … exclus„o?"
	
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()})
		
Else          
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se for entrada automatica - exclui o registro³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nOpcA := 2
Endif

DbSelectarea(cAlias)
	
IF nOpcA == 2
		
	BEGIN TRANSACTION
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ apos passar por todas as verificacoes , deleta o registro    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectarea(cAlias)
		RecLock(cAlias,.F.,.T.)
		DbDelete()
		MsUnlock()
			
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Integra Protheus x LEGAL DESK - SIGAPFS             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		//Grava na fila de sincronização se o parâmetro MV_JFSINC = '1' - SIGAPFS
		If FindFunction("J170GRAVA")
			J170GRAVA("SU5", xFilial("SU5") + SU5->U5_CODCONT, "5") //U5_FILIAL+U5_CODCONT
		EndIf

	END TRANSACTION
Else
	MsUnLock()
Endif
	
DbSelectarea(cAlias)
Return(.T.)
	
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A070Contat³ Autor ³ Vendas CRM            ³ Data ³ 01/03/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Valida a inclusao do cargo do contato                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER , FIELD SERVICE                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A070Contato()

Local lRet := .T.

DbSelectArea("SUM")
DbSetOrder(1)
If !Empty(M->U5_FUNCAO)
	If DbSeek(xFilial("SUM")+M->U5_FUNCAO)
		M->U5_DFUNCAO := SUM->UM_DESC
		lRet := .T.
	Else
		M->U5_DFUNCAO := " "
		lRet := .F.
	Endif
Else
	M->U5_DFUNCAO := " "
	lRet := .T.
Endif

lRefresh := .T.

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A070DesT1 ³ Autor ³ Vendas CRM            ³ Data ³ 01/03/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Carrega a descricao do Cargo do SUM para a Enchoice         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER , FIELD SERVICE                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A070DesT1()

Return(Retfield("SUM",1,xFilial("SUM")+SU5->U5_FUNCAO,"SUM->UM_DESC"))

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A070OPE   ³ Autor ³ Vendas CRM            ³ Data ³01/03/00  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Carrega o nome do operador padrao escolhido no complemento  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER , FIELD SERVICE                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A070Ope()
Local lRet := .F.

DbSelectArea("SU7")
DbSetOrder(1)
// Se o campo de codigo estiver preenchido procura no SU7 o operador
If !Empty(M->U5_OPERADO)
	If DbSeek(xFilial("SU7")+M->U5_OPERADO)
		M->U5_NOPERAD:= SU7->U7_NOME
		lRet := .T.
	Else
		M->U5_NOPERAD:= " "
		lRet := .F.
	Endif
Else
	// Se o codigo est  vazio nao existe operador padrao para esse complemento
	M->U5_NOPERAD := ""
	lRet := .T.
Endif

Return(lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A070SEM   ³ Autor ³ Vendas CRM            ³ Data ³01/03/00  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Carrega os dias da semana selecionados na tela - LIGACAO	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A070Sem()

Local cDias		:= M->U5_DIALIG		// Dias da semana em numeros: 1=Segunda,2=Terca,....
Local cNome		:= "" 				// Nome do dia da Semana:Segunda, Terca,....
Local nCont		:= 0				// Contador
Local nPos 		:= 0				// Posicao respectiva no ARRAY
Local cAux      := ""               // Variavel auxiliar para recompor os dias da semana

Local aDias:={	STR0013,;	// 1 - Domingo 
				STR0014,;	// 2 - Segunda 
				STR0015,;	// 3 - Terca 
				STR0016,;	// 4 - Quarta 
				STR0017,;	// 5 - Quinta 
				STR0018,;	// 6 - Sexta 
				STR0019}	// 7 - Sabado 	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica quais os dias da semana e devolve o nome dos dias respectivamente³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(cDias)
	For nCont := 1 TO Len(aDias)
		If !Empty(Substr(cDias,nCont,1))
			nPos := Val(Substr(cDias,nCont,1))
			If nPos > 0
				cNome := cNome + aDias[nPos] + ","
				cAux  += Substr(cDias,nCont,1)
			Endif
		Endif
	Next nCont
	M->U5_NDIALIG := SUBSTR(cNome,1,Len(cNome)-1)
	M->U5_DIALIG  := cAux + Space(TamSX3("U5_DIALIG")[1] - Len(cAux))
Else 
	M->U5_NDIALIG := Space(TamSX3("U5_NDIALIG")[1])
	M->U5_DIALIG  := Space(TamSX3("U5_DIALIG")[1])
Endif

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A070SEMV  ³ Autor ³ Vendas CRM            ³ Data ³01/03/00  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Carrega os dias da semana selecionados na tela - VISITA 	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³FIELD SERVICE                                				  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A070SemV()

Local cDias		:= M->U5_DIAVIS
Local cNome		:= "" 				// Nome do dia da Semana:Segunda, Terca,....
Local nCont		:= 0				// Contador
Local nPos 		:= 0				// Posicao respectiva no ARRAY
Local cAux  	:= ""				// Variavel auxiliar para recompor os dias da semana

Local aDias:={	STR0013,;	// 1 - Domingo 
				STR0014,;	// 2 - Segunda 
				STR0015,;	// 3 - Terca 
				STR0016,;	// 4 - Quarta 
				STR0017,;	// 5 - Quinta 
				STR0018,;	// 6 - Sexta 
				STR0019}	// 7 - Sabado 	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica quais os dias da semana e devolve o nome dos dias respectivamente³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(cDias)
	For nCont := 1 TO Len(aDias)
		If !Empty(Substr(cDias,nCont,1))
			nPos := Val(Substr(cDias,nCont,1))
			If nPos > 0
				cNome := cNome + aDias[nPos] + ","
				cAux  += Substr(cDias,nCont,1)	
			Endif
		Endif
	Next nCont
	M->U5_NDIAVIS := Substr(cNome,1,Len(cNome)-1)
	M->U5_DIAVIS  := cAux + Space(TamSX3("U5_DIAVIS")[1] - Len(cAux))
Else 
	M->U5_NDIAVIS := Space(Len(M->U5_DIAVIS))
	M->U5_DIAVIS  := Space(TamSX3("U5_DIAVIS")[1])
Endif

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ TK070Cod ³ Autor ³ Vendas CRM            ³ Data ³ 28/08/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Verifica se o codigo do contato foi digitado manualmente ou³±±
±±³          ³ foi obtido a partir da funcao GetSXENum().                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ cCodigo - Codigo do contato informado                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Tk070Cod(cCodigo)

Local aArea		:= GetArea()					// Salva o posicionamento atual
Local aAreaSX3	:= SX3->(GetArea())			// Salva o posicionamento do SX3
Local lRet		:= .T.							// Retorno da validacao
Local nX		:= 0							// Auxiliar de loop
Local lTkSelEnt	:= IsInCallStack("TkSelEnt")	// Identifica se a rotina foi acionada pela tela TkSelEnt
Local lTmka070	:= IsInCallStack("TMKA070")		// Identifica se a rotina foi acionada pelo cadastro de contatos
Local aAreaSU5	:= SU5->(GetArea())			// Salva o posicionamento do SU5

Static _cCodOri	:= ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Nao valida o codigo quando for acionado via MsExecAuto³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If IsInCallStack("MSEXECAUTO")
	Return .T.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Le o ultimo numero de contato obtido a partir do GetSXENum(), ³
//³para efetuar o rollback caso o usuario tenha alterado o numero³
//³sugerido, ou utilizado um contato pre-existente               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lTmka070			//Cadastro de contatos
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Recupera valor da variavel aTela (private da AxInclui)³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Empty(_cCodOri) .AND. Type("aTela") == "A"
		SX3->(DbSetOrder(2))
		SX3->(DbSeek("U5_CODCONT"))
		If (nX := aScan(aTela,{|x| AllTrim(Upper(x[1])) == AllTrim(Upper(SX3->X3_TITULO)) }) ) > 0
			_cCodOri := aTela[nX][2]
		EndIf	
	EndIf

ElseIf lTkSelEnt	//Selecao de entidades e contatos (MV_TMKF3)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Recupera valores do acols, comparando com o que esta na³
	//³memoria                                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (nX := aScan(aHeader,{|x|AllTrim(x[2]) == "U5_CODCONT"})) > 0
		_cCodOri	:= aCols[N][nX]
	EndIf
	
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se houve alteracao e cancela o numero anterior³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lTkSelEnt .OR. lTmka070
	If (_cCodOri == cCodigo) .AND. !__lSX8
		M->U5_CODCONT	:= GetSXENum('SU5','U5_CODCONT')
	ElseIf (_cCodOri <> cCodigo) .AND. __lSX8
		RollBackSX8()
	EndIf
EndIf  



//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Validação para não permitir a inclusão de códigos repetidos³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lTkSelEnt
	DbSelectArea("SU5")
	DbSetOrder(1)
	If DbSeek(xFilial("SU5") + cCodigo)
		Help( " ", 1, "EXISTCONT" )
		lRet := .F.
	Endif
EndIf

RestArea(aAreaSU5)
RestArea(aAreaSX3)
RestArea(aArea)

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ TK070CPF ³ Autor ³ Vendas CRM            ³ Data ³ 28/08/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Verifica se o CPF digitado eh valido.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 - CPF a ser validado                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. se for valido ou .F. se nao for valido                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function TK070CPF(cCPF)

Local aArea		:= GetArea()
Local lRet		:= .T.   

Default cCPF	:= M->U5_CPF

If !Empty(cCPF) 
	lRet := (	CGC(cCPF) .AND.;
				Existchav("SU5",cCPF,8,"U5_CPF") .AND.;
				FreeForUse("SU5",cCPF) )
EndIf

RestArea(aArea)

Return lRet	

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³Tk070Cep  ³ Autor ³ Vendas CRM            ³ Data ³28.06.2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao dos dados fornecidos pelos correios             ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Tk070Cep()
//Dummy Function mantida devido a compatibilidade de dicionários
//Deve ser removida do fonte quando a versão 12.1.27 estiver obsoleta.
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³Tk070TudOk ³ Autor ³ Vendas Clientes      ³ Data ³ 03/03/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Consistˆncia geral ao clicar o botao de Ok.                 ³±±
±±³          ³- Campos obrigatorios e CODIDO DE USUARIO ASSOCIADO         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³CALL CENTER                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Tk070TudOk(nOpc)

Local aArea     := GetArea()
Local lRet      := .T.
Local lContinua := ExistBlock("TKVINCU5")			 // P.E. Para validar a inclusao de um contato

If INCLUI
	DbSelectArea("SU5")
	DbSetOrder(1)
	If DbSeek(xFilial("SU5") + M->U5_CODCONT)
		Help( " ", 1, "EXISTCONT")
		lRet := .F.
	Endif
Endif

If lRet .And. lContinua
	lContinua := ExecBlock("TKVINCU5",.F.,.F.)
	lRet      := IIf(ValType(lContinua) <> "L",lRet,lContinua)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Retorna o Registro, o Order e o Alias anterior.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aArea)

Return(lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70Normatizado ³ Autor ³ Vendas CRM      ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Retorna se o SU5 está atualizado e normatizado.            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lNormatizado := A70Normatizado()                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A70Normatizado()   
Return ChkFile("AGA") .And. ChkFile("AGB")

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70Manut ³ Autor ³ Vendas CRM            ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Exibe a tela de inclusão/alteração/exclusão de contato.    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lResultado := A70Manut( cAlias, nReg, nOpcx )              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ cAlias: Alias da tabela SU5                                ³±±
±±³          ³ nReg: Recno do registro posicionado                        ³±±
±±³          ³ nOpcx: 2 - Visualização                                    ³±±
±±³          ³        3 - Inclusão                                        ³±±
±±³          ³        4 - Alteração                                       ³±±
±±³          ³        5 - Exclusão                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lResultado: Resultado do formulário sendo:                 ³±±
±±³          ³             1 - Usuário confirmou o formulário             ³±±
±±³          ³             2 - Usuário cancelou o formulário              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A70Manut(cAlias,nReg,nOpcx)
Local aObjects  := {}															// Temporário para posicionamento dos componentes
Local aPosObj   := {}															// Temporário para posicionamento dos componentes
Local aInfo		:= {}															// Temporário para posicionamento dos componentes
Local aSizeAut  := MsAdvSize()													// Tamanho disponível de exibição da janela
Local nGd1		:= 0															// Temporário para posicionamento dos componentes
Local nGd2		:= 0															// Temporário para posicionamento dos componentes
Local nGd3		:= 0															// Temporário para posicionamento dos componentes
Local nGd4		:= 0															// Temporário para posicionamento dos componentes
Local nOpcGd	:= 0
Local oDlg 		:= NIL															// Objeto da janela
Local oEnch		:= NIL															// Objeto da enchoice
Local nOpcA		:= 0															// Resultado numérico da execução do formulário
Local cSeek		:= ""															// Procura inicial da fillgetdados
Local bWhile	:= Nil															// Bloco com a condição de iteração da fillgetdados
Local lRet		:= .F.															// Variável utilizada no retorno da função
Local cTexto	:= "" 															// Variável utilizada para exibição de mensagem
Local nLenSX8	:= IIf( __lSX8, GetSX8Len(), 0)								// Tamanho dos códigos reservados.
Local aCpoNaoAGA:= { "AGA_CODIGO", "AGA_ENTIDA", "AGA_CODENT" }					// Campos que não serão incluídos no aCols/aHeader do AGB
Local aCpoNaoAGB:= { "AGB_CODIGO", "AGB_ENTIDA", "AGB_CODENT" }					// Campos que não serão incluídos no aCols/aHeader do AGB
Local lTK070INC := ExistBlock("TK070INC")									// P.E apos a gravacao de dados
Local lTMKINCU5	:= ExistBlock("TMKINCU5") 			 						// P.E  Antes da inclusao do contato.
Local lTMKALTA5	:= ExistBlock("TMKALTA5")									// P.E  Antes da alteracao do contato.
Local lTMKALTU5	:= ExistBlock("TMKALTU5")									// P.E  Depois da alteracao do contato.
Local lTk070ABOT:= ExistBlock("TK070ABOT")									// P.E  Para inclusão de botões ao aButtons (Enchoicebar).
Local aButAdic	:= {}															// Array de apoio para o ponto de entrada TK070ABOT.
Local aButtons  := {}
Local lTMKDELU5	:= ExistBlock("TMKDELU5")   								// P.E  Antes da exclusao do contato.
Local lContMsEx	:= .F.															// Se a rotina automática está tendo sucesso em sua execução.
Local nBusca 	:= 0   
Local nRecnoSM0 := SM0->(RecNo())
Local cFilAtu	:= SM0->M0_CODFIL
lOCAL aSM0CodFil:= {}
Local cCadastro	:= Iif( Type("cCadastro") == "U",STR0010,cCadastro)
Local lIsCtrA271:= IsInCallStack('TKCONTATOS') //Usado para garantir que não gera erro no ACOLS durante execução do TK271 quando utiliza Ctrl+A no Televendas
Local aColsBKP	:= {}
Local aHeaderBkp:= {}
Local lFatc020	:= AllTrim( FunName() ) == 'FATC020' //Se a Inclusao do Contato Estiver sendo feita via FATC020 - Funil de Vendas Faturamento
													 //Ajusta o Conteudo do aRotina com as Informa??es do FATA300 e Retorna ao Menu Original.

Private aGets
Private aTela
Private aHeadAGA:= {}
Private aColsAGA:= {}     	
Private aHeadAGB:= {}
Private aColsAGB:= {}     
Private oGetAGA	:= Nil
Private oGetAGB	:= Nil

If !lIsCtrA271
	Private aHeader
	Private aCols
	Private	N	:= 0

Else
	aColsBKP 	:= aClone(aCols)
	aHeaderBkp	:= aClone(aHeader)
EndIf

INCLUI := IIf( Type( 'INCLUI' ) == 'L', INCLUI, .F. )
ALTERA := IIf( Type( 'ALTERA' ) == 'L', ALTERA, .F. )
nOpcGd := IIf( !INCLUI.And.!ALTERA,0,GD_INSERT+GD_UPDATE+GD_DELETE)	// Opção passada para a getdados

If lFatc020
	aRotina	:= SetMenuDef( 'FATA300' )
EndIf
	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Quando é executado por rotina automática, é necessário    ³
//³disponibilizar o campo AG?_CODIGO para que o usuário      ³
//³possa informar-ló e assim a rotina possa alterar e excluir³
//³corretamente.                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (Type("lTk070Auto") == "U" .OR. !lTk070Auto) .OR. nOpcx == 3
	aCpoNaoAGA:= { "AGA_CODIGO", "AGA_ENTIDA", "AGA_CODENT" }
	aCpoNaoAGB:= { "AGB_CODIGO", "AGB_ENTIDA", "AGB_CODENT" }
Else
	aCpoNaoAGA:= { "AGA_ENTIDA", "AGA_CODENT" }
	aCpoNaoAGB:= { "AGB_ENTIDA", "AGB_CODENT" }
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se o P.E. existe e se o Retorno for FALSE sai da exclusao³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOpcx == 5 .And. lTMKDELU5
	If !U_TMKDELU5(cAlias,nReg)
		Return(0)
	Endif
Endif  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se o P.E. existe e se o Retorno for FALSE sai da alteração³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOpcx == 4 .And. lTMKALTA5
	lRet := U_TMKALTA5(nReg)
	
	If (ValType(lRet) <> "L")
		Return(.F.)
	ElseIf !lRet
		Return(.F.)
	Endif
Endif 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Faz as verificações para exclusão.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOpcx == 5
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se existe algum atendimento para o contato na tabela de TELEMARKETING³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectarea("SUC")
	DbSetorder(6)
	If DbSeek(xFilial("SUC")+SU5->U5_CODCONT)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se nao for uma entrada automatica exibe o HELP  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( Type("lTk070Auto") == "U" .OR. !lTk070Auto )
			HELP(" ",1,"GRAVADO")
			Return(nOpcA)
		Else
			Return(nOpcA)
		Endif		
	Else
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Valida a exclusao na tabela de Contatos x Entidades        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectarea("AC8")	
		DbSetOrder(1)
		If DbSeek(xFilial( "AC8" ) + SU5->U5_CODCONT)
			SX2->( DbSetOrder(1) )
			SX2->( DbSeek( AC8->AC8_ENTIDA ) )
				
			cTexto := ""
			If !Empty(AC8->AC8_FILENT)
				cTexto := STR0022 + AC8->AC8_FILENT + STR0023 + AllTrim( AC8->AC8_CODENT ) 
			Endif
				
			//"Atencao !"Este contato nao pode ser excluido pois esta associado a entidade '"____", Filial "____", chave "____"Ok"				
			If Type("lTk070Auto") == "U" .Or. !lTk070Auto
				Aviso( STR0020, STR0021 + AllTrim( X2NOME() ) + "'" + cTexto + ".", { STR0024 }, 2 ) 
			EndIf
			Return(nOpcA) 
		Else
			lRet := .T.
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Exclusao de Anotações    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		                                                                        
			If nModulo == 73 
				CRMA090Del( "SU5", SU5->( Recno() ) )
			EndIf	
		Endif

    Endif   
	
	If lRet

		// Preenche um array com as filiais                                                '
		DbSelectArea("SM0")
		DbGoTop()
		Do While ! Eof()
			If SM0->M0_CODIGO == cEmpAnt
				Aadd(aSM0CodFil, cFilAtu)
			Endif
			dbSkip()
		End	
		DbGoto(nRecnoSM0)
			
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se existe Atendimento para o contato ADE            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SX2")
		dbSetOrder(1)
		If dbSeek("ADE") .AND. GetRpoRelease("R1.3")

			DbSelectArea("ADE")
			DbSetOrder(14)
			aFiliais := If(! Empty(xFilial()) .AND. Empty(xFilial("SU5")), aClone(aSM0CodFil), {xFilial()})
			For nBusca := 1 To Len(aFiliais)
		       	If DbSeek(aFiliais[nBusca]+SU5->U5_CODCONT)
		      		Help(" ",1,"TMKNDELADE")
		   			Return(nOpcA)
				EndIf
			Next			
		EndIf
	Endif	
EndIf   
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Prepara tabela³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea(cAlias)
DbSetOrder(1)   

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se utilizado pelo Modulo Juridico, cria o botao "Especialidade" ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nModulo == 76 .Or. nModulo == 77 //JURI e PFS
	If nOpcx == 4 //Alterar    
		If ( FindFunction("JA132AEsp") ) .And. ( AliasInDic("NWA") )
			AAdd( aButtons, {"EDITABLE", { || JA132AEsp( cAlias, nReg, nOpcx ) }, STR0039, STR0039 } ) //"Especialidade"
		EndIf
	EndIf
EndIf

If nOpcx == 2 .Or. nOpcx == 4
	aButtons := CRMXAddAct("SU5",aButtons) //Adcionar Rotinas no 'Ações relacionadas' do Formulário                                          
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada - Adiciona rotinas ao aButtons (Enchoicebar)³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lTk070ABOT
	aButAdic := ExecBlock("TK070ABOT",.F.,.F.)
	If ValType(aButAdic) == "A"
		AEval(aButAdic,{|x| AAdd(aButtons,x)})
	Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa a Variaveis da Enchoice.                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Type("lTk070Auto") == "U" .OR. !lTk070Auto
	RegToMemory(cAlias,INCLUI,.T.)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se o endereço/telefone existente no SU5, existe ³
//³também em suas respectivas tabelas (AGA, AGB), se não    ³
//³existir cria.                                            ³
//³Essa rotina também verifica se o relacionamento existente³
//³na SU5 com AGA/AGB precisa ser atualizada.               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
A70DefAGA(M->U5_CODCONT)
A70DefAGB(M->U5_CODCONT, "1", M->U5_FCOM1	, M->U5_CODPAIS, M->U5_DDD)
A70DefAGB(M->U5_CODCONT, "2", M->U5_FONE	, M->U5_CODPAIS, M->U5_DDD)
A70DefAGB(M->U5_CODCONT, "3", M->U5_FAX	, M->U5_CODPAIS, M->U5_DDD)
A70DefAGB(M->U5_CODCONT, "5", M->U5_CELULAR, M->U5_CODPAIS, M->U5_DDD)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta vetores da getdados do AGA³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cSeek 	:= 	xFilial("AGA")+cAlias+PadR(M->U5_CODCONT, TamSX3("AGA_CODENT")[1])
bWhile	:=	{||AGA->AGA_FILIAL+AGA->AGA_ENTIDA+AGA->AGA_CODENT}

SaveInter()
FillGetDados(nOpcx,"AGA",1,cSeek,bWhile,,aCpoNaoAGA,,,,,,@aHeadAGA,@aColsAGA)
RestInter()                                

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta vetores da getdados do AGB³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cSeek 	:= 	xFilial("AGB")+cAlias+PadR(M->U5_CODCONT, TamSX3("AGB_CODENT")[1])
bWhile	:=	{||AGB->AGB_FILIAL+AGB->AGB_ENTIDA+AGB->AGB_CODENT}

SaveInter()
FillGetDados(nOpcx,"AGB",1,cSeek,bWhile,,aCpoNaoAGB,,,,,,@aHeadAGB,@aColsAGB)
RestInter()    
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula posicao dos objetos³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AAdd( aObjects, { 100, 60, .T., .T. } )
AAdd( aObjects, { 100, 40, .T., .T. } )
aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 } 
aPosObj := MsObjSize( aInfo, aObjects, .T. ) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define as posicoes da Getdados a partir do folder    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nGd1 := aPosObj[2,1]
nGd2 := aPosObj[2,2]
nGd3 := aPosObj[2,3]-15
nGd4 := aPosObj[2,4]-4

If Type("lTk070Auto") == "U" .Or. !lTk070Auto
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Define objeto da tela³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DEFINE MSDIALOG oDlg TITLE cCadastro From aSizeAut[7],00 To aSizeAut[6],aSizeAut[5] OF oMainWnd PIXEL STYLE nOr(WS_VISIBLE,WS_POPUP)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta Enchoice³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oEnch := MsMGet():New("SU5",nReg,nOpcx,,,,,aPosObj[1],,3)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Cria as GetDados³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aTFolder := {A70AliasTit("AGA"), A70AliasTit("AGB")}
	oTFolder := TFolder():New( nGd1,nGd2,aTFolder,,oDlg,,,,.T.,,nGd4,nGd3/3+10 )
	oGetAGA := MsNewGetDados():New(0,0,0,0,nOpcGd,"A70LOKAGA()","A70TOKAGA()",,,,,,,,oTFolder:aDialogs[1],aHeadAGA,aColsAGA)
	oGetAGA:oBrowse:Align := 5
	oGetAGB := MsNewGetDados():New(0,0,0,0,nOpcGd,"A70LOKAGB()","A70TOKAGB()",,,,,,,,oTFolder:aDialogs[2],aHeadAGB,aColsAGB)
	oGetAGB:oBrowse:Align := 5
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Exibe a tela³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| Iif(A070TOK(oEnch:aGets, oEnch:aTela),(nOpca := 1,oDlg:End()),.F.)},{|| nOpca := 0,ODlg:End()},,aButtons)
	
	aColsAGA := aClone(oGetAGA:aCols)
	aHeadAGA := aClone(oGetAGA:aHeader)
	aColsAGB := aClone(oGetAGB:aCols)
	aHeadAGB := aClone(oGetAGB:aHeader)
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Executa a enchoice.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (lContMsEx := EnchAuto(cAlias,aAutoCab))
		If lContMsEx .And. aAutoAGA <> Nil 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Executa a getdados do AGA, fazendo backup das variáveis³
			//³alteradas.                                             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			N := 0
			aCols	:= aClone(aColsAGA)
			aHeader	:= aClone(aHeadAGA)
			lContMsEx := MsGetDAuto(aAutoAGA,"A70LOKAGA()",{|| A70TOKAGA() },aAutoCab,aRotina[nOpcX][4])
			aColsAGA	:= aClone(aCols)
			aHeadAGA	:= aClone(aHeader)
		EndIf
		If lContMsEx .And. aAutoAGB <> Nil			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Executa a getdados do AGB, fazendo backup das variáveis³
			//³alteradas.                                             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
			N := 0
			aCols	:= aClone(aColsAGB)
			aHeader	:= aClone(aHeadAGB)
			lContMsEx := MsGetDAuto(aAutoAGB,"A70LOKAGB()",{|| A70TOKAGB() },aAutoCab,aRotina[nOpcX][4])
			aColsAGB	:= aClone(aCols)
			aHeadAGB	:= aClone(aHeader)
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se ocorrer tudo bem, valida o formulário e configura a continuação³
		//³para a gravação.                                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lContMsEx .And. A070TOK(aGets, aTela)
			nOpcA := 1
		EndIf		
	EndIf
EndIf

If lFatc020
	aRotina	:= SetMenuDef( 'FATA010' )
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Efetua a gravação.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( nOpcA == 1 )
	Begin Transaction
		A70Grava(nOpcX)
		If (nOpcx == 3 .Or. nOpcx == 4)
			While ( GetSX8Len() > nLenSX8 )
				ConfirmSx8()
			EndDo		
		EndIf
	End Transaction

	If nOpcx == 3 .And. lTMKINCU5
		ExecBlock("TMKINCU5",.F.,.F.)
	Endif   

	If nOpcx == 4 .And. lTMKALTU5
		ExecBlock("TMKALTU5",.F.,.F.)
	Endif
Else
	If (nOpcx == 3 .Or. nOpcx == 4)
		While ( GetSX8Len() > nLenSX8 )
			RollBackSX8()
		EndDo	
	EndIf
EndIf	
 
If  lIsCtrA271
	aCols 	:= aClone(aColsBKP)
	aHeader	:= aClone(aHeaderBkp)
	
EndIf

If nOpcA == 1 .And. nOpcx == 3 .And. lTK070INC
	ExecBlock("TK070INC",.F.,.F.)
EndIf 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Integra Protheus x LEGAL DESK - SIGAPFS             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//Grava na fila de sincronização se o parâmetro MV_JFSINC = '1' - SIGAPFS
If nOpcA == 1 .And. FindFunction("J170GRAVA")
	J170GRAVA("SU5", xFilial("SU5") + SU5->U5_CODCONT, Alltrim(Str(nOpcx))) //U5_FILIAL+U5_CODCONT
EndIf

//Integração Mensagem Unica 
If lIntegDef .And. nOpca == 1 .And. nOpcx <> 2
	FwIntegDef("TMKA070")
Endif

Return(nOpcA)  
          
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70DefAGA ³ Autor ³ Vendas CRM           ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Define um endereço padrão caso não exista um. Ou caso há   ³±±
±±³          ³ preferencia dos valores do SU5 em relação ao AGA, desenvol ³±±
±±³          ³ vido para manter compatibilidade com a SU5 não normatizada.³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lExecutado := A70DefAGA( cCodCont )                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ cCodCont := Código do contato analisado                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lExecutado: Verdadeiro se for incluído um item no AGA, ou  ³±±
±±³          ³ se for alterado o SU5 (Por questão de compatibilidade) e   ³±±
±±³          ³ Falso se nada foi feito.                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/                           
Function A70DefAGA(cCodCont)      
	Local aAreaSU5 		:= SU5->(GetArea())									// Salva a área do SU5
	Local aAreaAGA 		:= AGA->(GetArea())									// 
	Local cCodAGA		:= ""
	Local lExecutado	:= .F.    
	Local nPosCodigo	:= aScan(aHeadAGA,{|x|AllTrim(x[2])=="AGA_CODIGO"})
	Local nPosEnd		:= aScan(aHeadAGA,{|x|AllTrim(x[2])=="AGA_END"})
	Local nPosBairro	:= aScan(aHeadAGA,{|x|AllTrim(x[2])=="AGA_BAIRRO"})	
	Local nPosMunDes	:= aScan(aHeadAGA,{|x|AllTrim(x[2])=="AGA_MUNDES"})		
	Local nPosEst		:= aScan(aHeadAGA,{|x|AllTrim(x[2])=="AGA_EST"})
	Local nPosCEP		:= aScan(aHeadAGA,{|x|AllTrim(x[2])=="AGA_CEP"})	
	Local nPosPais		:= aScan(aHeadAGA,{|x|AllTrim(x[2])=="AGA_PAIS"})
	Local nCount		:= 0   
	
	If ( Type("lPrefSU5") == "U" )		
		lPrefSU5 := .F.
	EndIf	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se algum campo relativo ao endereço tiver conteúdo, e o campo³
	//³de relação com AGA não está preenchido, cria um item no      ³
	//³AGA com o conteúdo do SU5.                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If	(Type("M->U5_END") <> "U"	 	.And. !Empty(M->U5_END))		.Or.;
		(Type("M->U5_BAIRRO") <> "U"	.And. !Empty(M->U5_BAIRRO))	.Or.;
		(Type("M->U5_MUN") <> "U"	 	.And. !Empty(M->U5_MUN))		.Or.;
		(Type("M->U5_EST") <> "U"	 	.And. !Empty(M->U5_EST))		.Or.;		
		(Type("M->U5_CEP") <> "U"	 	.And. !Empty(M->U5_CEP))		.Or.;		
		(Type("M->U5_PAIS") <> "U"	 	.And. !Empty(M->U5_PAIS))

		DbSelectArea("SU5")
		DbSetOrder(1)

		If DbSeek( xFilial("SU5") + cCodCont )
			cCodAGA := SU5->U5_CODAGA
	
			DbSelectArea("AGA")
			DbSetOrder(1)
		
			If Empty(cCodAGA) .Or. !DBSeek( xFilial("AGA") + "SU5" + PadR(cCodCont, TamSX3("AGB_CODENT")[1]))
				RecLock("AGA", .T.)
				Replace AGA->AGA_FILIAL	With xFilial("AGA")
				Replace AGA->AGA_CODIGO	With (cCodAGA:=GetSx8Num("AGA","AGA_CODIGO"))
				Replace AGA->AGA_ENTIDA	With "SU5"					
				Replace AGA->AGA_CODENT	With cCodCont				
				Replace AGA->AGA_TIPO 	With "2"
				Replace AGA->AGA_PADRAO	With "1"
				Replace AGA->AGA_END	With M->U5_END
				Replace AGA->AGA_BAIRRO	With M->U5_BAIRRO
				Replace AGA->AGA_MUNDES	With M->U5_MUN
				Replace AGA->AGA_EST	With M->U5_EST
				Replace AGA->AGA_CEP	With M->U5_CEP
				Replace AGA->AGA_PAIS	With M->U5_PAIS
				AGA->(MsUnLock())
				
				RecLock("SU5", .F.)
				Replace SU5->U5_CODAGA With cCodAGA
				M->U5_CODAGA := cCodAGA
				SU5->(MsUnLock())
				lExecutado := .T. 

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se a variável lPrefSU5 for .T., significa que está em modo    ³
			//³de compatibilidade. Por isso o conteúdo de AGA será atualizado³
			//³com os dados do cabeçalho.                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			ElseIf lPrefSU5 .And. (Type("lTk070Auto") <> "U" .Or. lTk070Auto) .And. !Empty(cCodAGA) 
				AGA->(DbSelectArea("AGA"))
				AGA->(DbSetOrder(2))
				If AGA->(DbSeek( xFilial("AGA") + cCodAGA ))
					RecLock("AGA",.F.)
					Replace AGA->AGA_END	With M->U5_END
					Replace AGA->AGA_BAIRRO	With M->U5_BAIRRO
					Replace AGA->AGA_MUNDES	With M->U5_MUN
					Replace AGA->AGA_EST	With M->U5_EST
					Replace AGA->AGA_CEP	With M->U5_CEP
					Replace AGA->AGA_PAIS	With M->U5_PAIS					
					AGA->(MsUnLock())
				EndIf
				
				For nCount := 1 To Len(aColsAGA)
					If aColsAGA[nCount][nPosCodigo] == cCodAGA
						aColsAGA[nCount][nPosEnd]		:= M->U5_END
						aColsAGA[nCount][nPosBairro]	:= M->U5_BAIRRO
						aColsAGA[nCount][nPosMunDes]	:= M->U5_MUN
						aColsAGA[nCount][nPosEst]		:= M->U5_EST						
						aColsAGA[nCount][nPosCEP]		:= M->U5_CEP						
						aColsAGA[nCount][nPosPais]		:= M->U5_PAIS						
					EndIf
				Next
				lExecutado := .T.
			EndIf
		EndIf
	EndIf
     	
	SU5->(RestArea(aAreaSU5))
	AGA->(RestArea(aAreaAGA))
Return lExecutado

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70DefAGB ³ Autor ³ Vendas CRM           ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Define um telefone padrão caso não exista um. Ou caso há   ³±±
±±³          ³ preferencia dos valores do SU5 em relação ao AGB, desenvol ³±±
±±³          ³ vido para manter compatibilidade com a SU5 não normatizada.³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lExecutado := A70DefAGB( cCodCont )                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ cCodCont := Código do contato analisado                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lExecutado: Verdadeiro se for incluído um item no AGB, ou  ³±±
±±³          ³ se for alterado o SU5 (Por questão de compatibilidade) e   ³±±
±±³          ³ Falso se nada foi feito.                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A70DefAGB(cCodCont, cTipo, cTel, cDDI, cDDD, aCols, aHeader)
	Local aAreaSU5 		:= SU5->(GetArea())
	Local aAreaAGB 		:= AGB->(GetArea())
	Local cCodAGB		:= ""
	Local lAdicionado	:= .F.
	Local nCount		:= 0
	Local nPosCodigo	:= aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_CODIGO"})
	Local nPosTel		:= aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_TELEFO"})	
	Local nPosDDI		:= aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_DDI"})	
	Local nPosDDD		:= aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_DDD"})	
	
	If ( Type("lPrefSU5") == "U" )		
		lPrefSU5 := .F.
	EndIf		
	
	DbSelectArea("SU5")
	DbSetOrder(1)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se algum campo relativo ao telefone tiver conteúdo, e o campo³
	//³de relação com AGB não está preenchido, cria um item no      ³
	//³AGB com o conteúdo do SU5.                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(cTel)
		If DbSeek( xFilial("SU5") + cCodCont )
			cCodAGB := &("SU5->" + A70AGBCampo(cTipo))
		
			DbSelectArea("AGB")
			DbSetOrder(1)
			
			If Empty(cCodAGB) .Or. !DBSeek( xFilial("AGB") + "SU5" + PadR(cCodCont, TamSX3("AGB_CODENT")[1]) + cTipo )
				RecLock("AGB", .T.)
				Replace AGB->AGB_FILIAL	With xFilial("AGB")
				Replace AGB->AGB_CODIGO	With (cCodAGB:=GetSx8Num("AGB","AGB_CODIGO"))
				Replace AGB->AGB_ENTIDA	With "SU5"					
				Replace AGB->AGB_CODENT	With cCodCont
				Replace AGB->AGB_TIPO	With cTipo
				Replace AGB->AGB_PADRAO	With "1"
				Replace AGB->AGB_TELEFO	With cTel
				Replace AGB->AGB_DDI	With cDDI
				Replace AGB->AGB_DDD	With cDDD
				AGB->(MsUnLock())
				
				RecLock("SU5", .F.)
				Replace &("SU5->" + A70AGBCampo(cTipo)) With cCodAGB
				&("M->" + A70AGBCampo(cTipo)) := cCodAGB
				SU5->(MsUnLock())  
				lAdicionado := .T.
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se a variável lPrefSU5 for .T., significa que está em modo    ³
			//³de compatibilidade. Por isso o conteúdo de AGB será atualizado³
			//³com os dados do cabeçalho.                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ				
			ElseIf lPrefSU5 .And. (Type("lTk070Auto") <> "U" .Or. lTk070Auto) .And. !Empty(cCodAGB) 
				AGB->(DbSelectArea("AGB"))
				AGB->(DbSetOrder(2))
				If AGB->(DbSeek( xFilial("AGB") + cCodAGB ))
					RecLock("AGB",.F.)
					Replace AGB->AGB_TELEFO With cTel
					Replace AGB->AGB_DDI	With cDDI
					Replace AGB->AGB_DDD	With cDDD
					AGB->(MsUnLock())
				EndIf
				
				For nCount := 1 To Len(aColsAGB)
					If aColsAGB[nCount][nPosCodigo] == cCodAGB
						aColsAGB[nCount][nPosTel] := cTel
						aColsAGB[nCount][nPosDDI] := cDDI
						aColsAGB[nCount][nPosDDD] := cDDD
					EndIf
				Next
				lAdicionado := .T.
			EndIf
		EndIf
	EndIf
	
	SU5->(RestArea(aAreaSU5))
	AGB->(RestArea(aAreaAGB))
Return lAdicionado

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70AGBCampo ³ Autor ³ Vendas CRM         ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Retorna o nome do campo de código na SU5 do tipo informado.³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ cRet := A70AGBCampo( cTipo )                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ cTipo: Tipo do telefone, sendo:                            ³±±
±±³          ³        1 - Telefone comercial                              ³±±
±±³          ³        2 - Telefone residencial                            ³±±
±±³          ³        3 - Fax                                             ³±±
±±³          ³        5 - Celular                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ cRet: Nome do campo.                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A70AGBCampo( cTipo )
	Local cRet	:= ""	// Retorno da função
	
	If cTipo == "1"
		cRet := "U5_AGBCOM"
	ElseIf cTipo == "2"
		cRet := "U5_AGBRES"	
	ElseIf cTipo == "3"
		cRet := "U5_AGBFAX"
	ElseIf cTipo == "5"
		cRet := "U5_AGBCEL"
	EndIf
Return cRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70AGBCampo ³ Autor ³ Vendas CRM         ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Retorna o nome do campo na SU5 do tipo informado.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ cRet := A70AGBCampo( cTipo )                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ cTipo: Tipo do telefone, sendo:                            ³±±
±±³          ³        1 - Telefone comercial                              ³±±
±±³          ³        2 - Telefone residencial                            ³±±
±±³          ³        3 - Fax                                             ³±±
±±³          ³        5 - Celular                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ cRet: Nome do campo.                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A70SU5Campo(cTipo)
	Local cRet	:= ""
	
	If cTipo == "1"
		cRet := "U5_FCOM1"
	ElseIf cTipo == "2"
		cRet := "U5_FONE"	
	ElseIf cTipo == "3"
		cRet := "U5_FAX"
	ElseIf cTipo == "5"
		cRet := "U5_CELULAR"
	EndIf
Return cRet
  
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70UnicoEnd ³ Autor ³ Vendas CRM         ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Verifica se na aCols há um único endereço padrão.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ .T. := A70UnicoEnd()                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A70UnicoEnd()
	Local nPosPadrao 	:= 0	// Posição do campo AGA_PADRAO
	Local nItemPadrao	:= 0	// Linha com o item padrão
	Local nLin			:= 0	// Contador temporário
		
	If oGetAGA <> Nil
		aCols	:= aClone(oGetAGA:aCols)
		aHeader	:= aClone(oGetAGA:aHeader)
		N		:= oGetAGA:nAt
	EndIf
	
	nPosPadrao := aScan(aHeader,{|x|AllTrim(x[2])=="AGA_PADRAO"})

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Força a existencia de somente um campo definido como padrão.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nPosPadrao > 0 .And. N > 0 
		If aCols[N][nPosPadrao] == "1"
			nItemPadrao := N
		EndIf
		
		For nLin := 1 To Len(aCols)
			If !aTail(aCols[nLin])
				If nItemPadrao > 0 .And. nLin <> nItemPadrao
					aCols[nLin][nPosPadrao] := "2"
				EndIf
			EndIf
		Next
	EndIf	
Return .T.
  
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70VeriEnd ³ Autor ³ Vendas CRM          ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Consiste a aCols e a quantidade de itens padrões.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lRet := A70VeriEnd()                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lRet: Se a aCols está consistênte ou não.                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A70VeriEnd()
	Local nPosPadrao 		:= 0	// Posição do campo AGA_PADRAO
	Local nLin				:= 0	// Contador temporário
	Local nQuant			:= 0	// Quantidade de itens encontrados
	Local lRet				:= .T.	// Retorno da função
	Local nPos				:= 0	// Linha do item padrão
	Local nPosVerificador	:= 0	// Posição do campo verificador
	Local lFaltaObrigatorio	:= .F.	// Indicador de campo obrigatório não preenchido
	Local cTexto			:= ""	// Texto da mensagem
	
	If oGetAGA <> Nil
		aCols	:= aClone(oGetAGA:aCols)
		aHeader	:= aClone(oGetAGA:aHeader)
		N		:= oGetAGA:nAt
	EndIf
	
	nPosPadrao 		:= aScan(aHeader,{|x|AllTrim(x[2])=="AGA_PADRAO"})
	nPosVerificador	:= aScan(aHeader,{|x|AllTrim(x[2])=="AGA_END"})		
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Procura por itens que estejam marcados como padrão.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nLin := 1 To Len(aCols)
		If !aTail(aCols[nLin]) .And. aCols[nLin][nPosPadrao] == "1"
			nQuant++
			nPos := nLin
		EndIf
	Next

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se existir mais que somente um item, deve-se exibir uma mensagem informativa.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nQuant > 1
		If Type("lTk070Auto") == "U" .Or. !lTk070Auto
			Alert(STR0030 + A70AliasTit("AGA") + ".") // "Existe mais de um item escolhido como padrão na aba "
		EndIf
		lRet := .F.
	ElseIf nQuant == 0
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Conta a quantidade de itens cadastrados.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nQuant := 0
		For nLin := 1 To Len(aCols)
			If !aTail(aCols[nLin]) .And. !Empty(aCols[nLin][nPosVerificador])
				nQuant++
				nPos := nLin
			EndIf
		Next
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se existir somente um item, configura-o como padrão.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nQuant == 1 .And. !X3Obrigat("AGA_PADRAO")
			aCols[nPos][nPosPadrao] := "1"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se não existir, verifica se é necessário que tenha ao menos³
		//³um endereço cadastrado para suprir as necessidade de       ³
		//³obrigatoriedade do SU5.                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		ElseIf 	X3Obrigat("U5_END")		.Or.;
				X3Obrigat("U5_BAIRRO")	.Or.;
				X3Obrigat("U5_MUN")		.Or.;
				X3Obrigat("U5_EST")		.Or.;
				X3Obrigat("U5_CEP")		.Or.;
				X3Obrigat("U5_PAIS")
				If Type("lTk070Auto") == "U" .Or. !lTk070Auto
					Alert(STR0031)	// "Deve haver ao menos um endereço cadastrado."
				EndIf
				lRet := .F.		
		EndIf
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se existir ao menos um item válido, verifica se os campos³
	//³obrigatórios foram preenchidos.                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nQuant == 1
		If X3Obrigat("U5_END") .And. Empty(aCols[nPos][aScan(aHeader,{|x|AllTrim(x[2])=="AGA_END"})])
			lFaltaObrigatorio := .T.
			cTexto += " - " + Capital(Posicione('SX3', 2, "AGA_END", 'X3Titulo()')) + Chr(13) + Chr(10)				
		EndIf
		If X3Obrigat("U5_BAIRRO") .And. Empty(aCols[nPos][aScan(aHeader,{|x|AllTrim(x[2])=="AGA_BAIRRO"})])
			lFaltaObrigatorio := .T.
			cTexto += " - " + Capital(Posicione('SX3', 2, "AGA_BAIRRO", 'X3Titulo()')) + Chr(13) + Chr(10)				
		EndIf			
		If X3Obrigat("U5_MUN") .And. Empty(aCols[nPos][aScan(aHeader,{|x|AllTrim(x[2])=="AGA_MUNDES"})])
			lFaltaObrigatorio := .T.
			cTexto += " - " + Capital(Posicione('SX3', 2, "AGA_MUNDES", 'X3Titulo()')) + Chr(13) + Chr(10)				
		EndIf
		If X3Obrigat("U5_EST") .And. Empty(aCols[nPos][aScan(aHeader,{|x|AllTrim(x[2])=="AGA_EST"})])
			lFaltaObrigatorio := .T.
			cTexto += " - " + Capital(Posicione('SX3', 2, "AGA_EST", 'X3Titulo()')) + Chr(13) + Chr(10)				
		EndIf
		If X3Obrigat("U5_CEP") .And. Empty(aCols[nPos][aScan(aHeader,{|x|AllTrim(x[2])=="AGA_CEP"})])
			lFaltaObrigatorio := .T.
			cTexto += " - " + Capital(Posicione('SX3', 2, "AGA_CEP", 'X3Titulo()')) + Chr(13) + Chr(10)				
		EndIf
		If X3Obrigat("U5_PAIS") .And. Empty(aCols[nPos][aScan(aHeader,{|x|AllTrim(x[2])=="AGA_PAIS"})])
			lFaltaObrigatorio := .T.
			cTexto += " - " + Capital(Posicione('SX3', 2, "AGA_PAIS", 'X3Titulo()')) + Chr(13) + Chr(10)				
		EndIf	
	EndIf	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se faltar o preenchimento de item obrigatório exibe a mensagem.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lFaltaObrigatorio
		If Type("lTk070Auto") == "U" .Or. !lTk070Auto
			Alert(STR0032 + Chr(13) + Chr(10) + cTexto)	// "Os seguintes itens são de preenchimento obrigatório:"
		EndIf
		lRet := .F.
	EndIf
Return lRet   

  
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70UnicoTel ³ Autor ³ Vendas CRM         ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Verifica se na aCols há um único telefone padrão de um tipo³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ .T. := A70UnicoTel()                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A70UnicoTel()
	Local nPosPadrao 		:= 0	// Posição do campo "AGB_PADRAO"
	Local nPosTipo	 		:= 0	// Posição do campo "AGB_TIPO"
	Local nItemPadrao		:= 0	// Linha com o item padrão
	Local nLin				:= 0	// Contador temporário
	Local cTipoItem			:= ""	// Tipo do telefone
	
	If oGetAGB <> Nil
		aCols	:= aClone(oGetAGB:aCols)
		aHeader	:= aClone(oGetAGB:aHeader)
		N		:= oGetAGB:nAt
	EndIf
	
	nPosPadrao	:= aScan(aHeader,{|x|AllTrim(x[2])=="AGB_PADRAO"})
	nPosTipo	:= aScan(aHeader,{|x|AllTrim(x[2])=="AGB_TIPO"})		
	 	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Força a existencia de somente um campo de um determinado tipo definido como padrão.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	If nPosPadrao > 0 .And. N > 0
		If aCols[N][nPosPadrao] == "1"
			nItemPadrao := N
			cTipoItem	:= aCols[N][nPosTipo]
		EndIf
		
		For nLin := 1 To Len(aCols)
			If !aTail(aCols[nLin])
				If nItemPadrao > 0 .And. !Empty(cTipoItem)  .And. nLin <> nItemPadrao .And. aCols[nLin][nPosTipo] == cTipoItem
					aCols[nLin][nPosPadrao] := "2"
				EndIf
			EndIf
		Next
	EndIf
Return .T.
  
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70VeriTel ³ Autor ³ Vendas CRM          ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Consiste a aCols e a quantidade de itens padrões.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lRet := A70VeriTel()                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lRet: Se a aCols está consistênte ou não.                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A70VeriTel()
	Local nPosPadrao 		:= 0		// Posição do campo "AGB_PADRAO"
	Local nPosTipo	 		:= 0		// Posição do campo "AGB_TIPO"
	Local nLin				:= 0		// Contador temporário
	Local nQuant			:= 0		// Quantidade de itens encontrados
	Local lRet				:= .T.		// Retorno da função
	Local aTipos			:= {}		// Lista de tipos encontrados
	Local nPos				:= 0		// Contador temporário
	Local nCount			:= 0		// Contador temporário
	Local cTexto			:= ""		// Texto da mensagem a ser exibida
	Local lFaltaObrigatorio	:= .F.		// Indicador da campo obrigatório não preenchido
	Local l070UDefAuto		:= (Type("lTk070Auto") == "U")
	Local nPosPadAGB		:= 0
	Local nPosTipoAGB		:= 0
	
	If oGetAGB <> Nil
		aCols	:= aClone(oGetAGB:aCols)
		aHeader	:= aClone(oGetAGB:aHeader)
		N		:= oGetAGB:nAt
	EndIf
	
	nPosPadrao	:= aScan(aHeader,{|x|AllTrim(x[2])=="AGB_PADRAO"})
	nPosTipo	:= aScan(aHeader,{|x|AllTrim(x[2])=="AGB_TIPO"})			
	If !Empty(aHeadAGB)
		nPosPadAGB	:= aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_PADRAO"})
		nPosTipoAGB	:= aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_TIPO"})
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Cria a array com os tipos e suas quantidades de itens padrões.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nCount := 1 To Len(TkSX3Box("AGB_TIPO"))
		aAdd(aTipos, {SubStr(TkSX3Box("AGB_TIPO")[nCount],0,At("=",TkSX3Box("AGB_TIPO")[nCount])-1), 0})		
	Next
	
	//Acumula a quantidade de itens padrões por tipo já gravado no banco de dados.
	If !l070UDefAuto .And. lTk070Auto
		If FwIsInCallStack("a70Altera") .And. !FwIsInCallStack("A070TOK")
			If nPosPadAGB > 0 .And. nPosTipoAGB .And. !Empty(aColsAGB)
				For nLin := 1 To Len(aColsAGB)
					If !aTail(aColsAGB[nLin])
						If aColsAGB[nLin][nPosPadAGB] == "1"
							nPos := aScan(aTipos, {|x| x[1] == aColsAGB[nLin][nPosTipoAGB]})
							If nPos > 0 
								aTipos[nPos][2]++
							EndIf
						EndIf
					EndIf
				Next
			EndIf
		EndIf
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Acumula a quantidade de itens padrões por tipo.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nLin := 1 To Len(aCols)
		If !aTail(aCols[nLin])
			If aCols[nLin][nPosPadrao] == "1"
				nPos := aScan(aTipos, {|x| x[1] == aCols[nLin][nPosTipo]})
				If nPos > 0 
					aTipos[nPos][2]++
				EndIf
			EndIf
		EndIf
	Next

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Para cada tipo existente.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nCount := 1 To Len(aTipos)	
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se existir mais de um item padrão de um tipo.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aTipos[nCount][2] > 1
			If l070UDefAuto .Or. !lTk070Auto
				Help("",1,"A070TELPAD",, STR0033 + Lower(SubStr(TkSX3Box("AGB_TIPO")[nCount],At("=",TkSX3Box("AGB_TIPO")[nCount])+1)) + STR0034, 1, 0,,,,,,{STR0067 + Lower(SubStr(TkSX3Box("AGB_TIPO")[nCount],At("=",TkSX3Box("AGB_TIPO")[nCount])+1)) + STR0068}) // "Existe mais de um item escolhido como padrão do tipo " " na aba de telefones. Solução: Defina apenas um telefone do tipo " " como padrão."
				lRet := .F.
			EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se não existir algum item selecionado como padrão para o tipo.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		ElseIf aTipos[nCount][2] == 0
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica quantos itens existem cadastrados para o tipo.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nQuant := 0
			For nLin := 1 To Len(aCols)  
				If !aTail(aCols[nLin]) .And. aCols[nLin][nPosTipo] == aTipos[nCount][1]
					nQuant++
					nPos := nLin
				EndIf
			Next

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se existir somente um item para o tipo, configura-o como padrão.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If nQuant == 1 .And. !X3Obrigat("AGB_PADRAO")
				aCols[nPos][nPosPadrao] := "1"
				
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se não existir item cadastrado para esse tipo, cria a mensagem³
			//³que será exibida posteriormente.                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Else				
				If X3Obrigat( A70SU5Campo(aTipos[nCount][1]) )
					lFaltaObrigatorio := .T.
					cTexto += STR0035 + Lower(SubStr(TkSX3Box("AGB_TIPO")[nCount],At("=",TkSX3Box("AGB_TIPO")[nCount])+1)) + Chr(13) + Chr(10)	// " - Telefone padrão do tipo "
				EndIf
			EndIf
		EndIf
	Next
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se faltar o preenchimento de item obrigatório exibe a mensagem.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lFaltaObrigatorio
		If Type("lTk070Auto") == "U" .Or. !lTk070Auto
			Alert(STR0036 + Chr(13) + Chr(10) + cTexto)	// "Os seguintes itens são de preenchimento obrigatório:"
		EndIf
		lRet := .F.
	EndIf
 Return lRet
    
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70LOKAGA ³ Autor ³ Vendas CRM           ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Função de validação da linha selecionada na aCols          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lRet := A70LOKAGA()                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lRet: Se a linha é válida ou não.                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A70LOKAGA()

Local lRet := .T.

lRet := If(!A70UnicoEnd(),.F.,lRet)

Return lRet
            
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70TOKAGA ³ Autor ³ Vendas CRM           ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Função de validação da getdados.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lRet := A70TOKAGA()                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lRet: Se a getdados é válida ou não.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A70TOKAGA()

Local lRet 	   	:= .T.
Local aAreaSX3 	:= SX3->(GetArea())
Local nX 	   	:= 0
Local nY 	   	:= 0
Local cField	:= ""

lRet := If(!A70VeriEnd(),.F.,lRet)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Faz a validação dos campos obrigatórios. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. (INCLUI .Or. ALTERA)
	SX3->( DBSetOrder(2) )
	For nX := 1 To Len(aCols)
		If !(aCols[nX][Len(aCols[nX])])
			For nY := 1 To Len(aHeader)
				cField := AllTrim(aHeader[nY][2])
				If cField != "AGA_CODIGO" 
					If SX3->( DbSeek(cField) ) .And. X3Obrigat(SX3->X3_CAMPO)
						If Empty(aCols[nX][nY])
							lRet := .F.
							Help(1," ","OBRIGAT",,X3Titulo()+"-> "+STR0040+A70AliasTit("AGA"),3,0)	//"Pasta: "
							Exit
						EndIf
					EndIf
				EndIf
			Next nY      
		EndIf		 
    Next nX
EndIf

RestArea(aAreaSX3)

Return lRet
        
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70LOKAGB ³ Autor ³ Vendas CRM           ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Função de validação da linha selecionada na aCols          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lRet := A70LOKAGB()                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lRet: Se a linha é válida ou não.                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A70LOKAGB()

Local lRet := .T.

lRet := If(!A70UnicoTel(),.F.,lRet)

Return lRet
         
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70TOKAGB ³ Autor ³ Vendas CRM           ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Função de validação da getdados.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lRet := A70TOKAGB()                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lRet: Se a getdados é válida ou não.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A70TOKAGB()

Local lRet 	   := .T.
Local aAreaSX3 := SX3->(GetArea())
Local nX 	   := 0
Local nY 	   := 0

lRet := If(!A70VeriTel(),.F.,lRet)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Faz a validação dos campos obrigatórios. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. (INCLUI .Or. ALTERA)
	SX3->( DBSetOrder( 2 ) )
 	For nX := 1 To Len(aCols)
		If !(aCols[nX][Len(aCols[nX])])
			For nY := 1 To Len(aHeader)
				cField := AllTrim(aHeader[nY][2])
				If cField != "AGB_CODIGO"
					If SX3->( DBSeek(cField) ) .And. X3Obrigat(SX3->X3_CAMPO)
						If Empty(aCols[nX][nY])
							lRet := .F.
							Help(1," ","OBRIGAT",,X3Titulo()+"-> "+STR0040+A70AliasTit("AGB"),3,0)	//"Pasta: "
							Exit
						EndIf
					EndIf
				EndIf
			Next nY      
		EndIf		
    Next nX
EndIf
 
RestArea(aAreaSX3)  

Return lRet  


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A070TOK ³ Autor ³ Vendas CRM             ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Função de validação do formulário.                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lRet := A70TOKAGA( aGets, aTela )                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ aGets: Gets da enchoice                                    ³±±
±±³          ³ aTela: Informações dos gets da enchoice                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lRet: Se o formulário é válido ou não.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A070TOK(aGets, aTela)

Local aArea					:= GetArea()
Local aAreaSU5				:= SU5->(GetArea())
Local lRet					:= .T.
Local lTkSelEnt				:= IsInCallStack("TkSelEnt")	// Identifica se a rotina foi acionada pela tela TkSelEnt
Local nCount				:= 0
Local nPos					:= 0
Local aCampos				:= {}
Local l070TudoOK := ExistBlock('MA070TOK')

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se for inclusão, não permite que haja duplicidade de campo chave.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If INCLUI
	DbSelectArea("SU5")
	DbSetOrder(1)
	If DbSeek(xFilial("SU5") + M->U5_CODCONT)
		Help( " ", 1, "EXISTCONT" )
		lRet := .F.
	Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retira a obrigatoriedade dos campos que terão sua obrigatoriedade³
//³verificada por código próprio.                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aCampos :=	{	"U5_FONE"	,;
				"U5_CELULAR",;
				"U5_FCOM1"	,;
				"U5_FAX"	,;
				"U5_CODPAIS",;				
				"U5_DDD"	,;				
				"U5_END"	,;
				"U5_BAIRRO"	,;				
				"U5_MUN"	,;
				"U5_EST"	,;
				"U5_CEP"	,;				
				"U5_PAIS"	}				
For nCount := 1 To Len(aGets)
	nPos := aScan( aCampos, {|x| x $aGets[nCount]} )
	If nPos > 0             
		aGets[nCount] := SubStr(aGets[nCount],0,24)+"F"+SubStr(aGets[nCount],26)
	EndIf
Next

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Faz-se as validações da enchoice.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lRet := If( lRet .And. !Obrigatorio(aGets,aTela), .F., lRet )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Fazem-se as validações das getdados.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( Type("lTk070Auto") <> "U" .And. lTk070Auto )
	aHeader := aClone(aHeadAGA)
	aCols	:= aClone(aColsAGA)
Else
	lRet := If( lRet .And. !A70LOKAGA(), .F., lRet )
EndIf

If !lTkSelEnt
	lRet := If( lRet .And. !A70TOKAGA(), .F., lRet )
EndIf

If ( Type("lTk070Auto") <> "U" .And. lTk070Auto )
	aHeadAGA	:= aClone(aHeader)
	aColsAGA	:= aClone(aCols)
EndIf

If ( Type("lTk070Auto") <> "U" .And. lTk070Auto )
	aHeader := aClone(aHeadAGB)
	aCols	:= aClone(aColsAGB)
Else
	lRet := If( lRet .And. !A70LOKAGB(), .F., lRet )	
EndIf

If !lTkSelEnt
	lRet := If( lRet .And. !A70TOKAGB(), .F., lRet )
EndIf

If ( Type("lTk070Auto") <> "U" .And. lTk070Auto )
	aHeadAGB	:= aClone(aHeader)
	aColsAGB	:= aClone(aCols)
EndIf   
                             
If l070TudoOK
	lRet:= ExecBlock('MA070TOK',.F.,.F.,{lRet})
EndIf

If INCLUI .And. ExistBlock("TKVINCU5")
	lRet:=	ExecBlock("TKVINCU5",.F.,.F.)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Retorna o Registro, o Order e o Alias anterior.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aArea)
RestArea(aAreaSU5)

Return(lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70AliasTit ³ Autor ³ Vendas CRM         ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Retorna o título amigável da tabela                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ cTitulo := A70AliasTit( cAlias )                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ cAlias: Alias da tabela, sendo:                            ³±±
±±³          ³         "AGA" - Endereços                                  ³±±
±±³          ³         "AGB" - Telefones                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ cTitulo: Título.                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A70AliasTit( cAlias )
	Local cTitulo	:= ""
	If cAlias == "AGA"
		cTitulo := STR0037	// "Endereços"
	ElseIf cAlias == "AGB"
		cTitulo := STR0038	// "Telefones"
	EndIf
Return cTitulo

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70Grava ³ Autor ³ Vendas CRM            ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Efetua a gravação do cabeçalho e itens do contato.         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ NIl := A70Grava( nOpc )                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ nOpc: Modo de gravação, sendo:                             ³±±
±±³          ³       3 - Inclusão;                                        ³±±
±±³          ³       4 - Alteração;                                       ³±±
±±³          ³       5 - Exclusão;                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A70Grava(nOpc)

Local aAreaSX3		:= SX3->(GetArea())
Local aAreaAO4		:= AO4->(GetArea())
Local aAGAPadrao	:= {}
Local nAGAPadrao	:= 0
Local aAGBPadrao	:= {}
Local nPos			:= 0
Local lAGAAdic		:= .F.
Local lAGB1Adic		:= .F.
Local lAGB2Adic		:= .F.
Local lAGB3Adic		:= .F.
Local lAGB5Adic		:= .F.
Local cChave		:= "" 
Local aAutoAO4		:= {}	
Local cCodUser		:= ""	
               
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se for inclusão ou alteração.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (nOpc==3) .OR. (nOpc==4)
                      
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualizacao do Cabecalho³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("SU5")
	DbSetOrder(1)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³RecLock considerando inclusao ou alteracao³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RecLock("SU5",!DbSeek(xFilial("SU5")+M->U5_CODCONT))
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Descarga dos campos na tabela³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SX3->(DbSelectArea("SX3"))
	SX3->(DBSetOrder(1))
	SX3->(DbSeek("SU5"))
	While !SX3->(Eof()) .AND. SX3->X3_ARQUIVO == "SU5"
		If ( AllTrim(SX3->X3_CAMPO) =="U5_FILIAL" )
			SU5->U5_FILIAL := xFilial("SU5")
		ElseIf SX3->X3_CONTEXT <> "V"
			SU5->&(SX3->X3_CAMPO) := M->&(SX3->X3_CAMPO)
		EndIf  
		SX3->(DbSkip())
	End  
	
	SU5->(MsUnLock())
	
	If nOpc == 3 
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Adiciona ou Remove o privilegios deste registro.  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("AO3")
		cCodUser:= RetCodUsr()
		AO3->(DbSetOrder(1))	// AO3_FILIAL+AO3_CODUSR
		If AO3->(DbSeek(xFilial("AO3")+cCodUser))
			cChave 	:= PadR(xFilial("SU5")+M->U5_CODCONT,TAMSX3( "AO4_CHVREG")[1])		
			aAutoAO4	:= CRMA200PAut(nOpc,"SU5",cChave,cCodUser,/*aPermissoes*/,/*aNvlEstrut*/,/*cCodUsrCom*/,/*dDataVld*/)    
			CRMA200Auto(aAutoAO4[1],aAutoAO4[2],nOpc)
		EndIf	
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se o endereço/telefone existente no SU5, existe³
	//³em suas respectivas tabelas, se não existir, os cria.   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lAGAAdic 	:= A70DefAGA(M->U5_CODCONT)
	lAGB1Adic	:= A70DefAGB(M->U5_CODCONT, "1", M->U5_FCOM1	, M->U5_CODPAIS, M->U5_DDD)
	lAGB2Adic	:= A70DefAGB(M->U5_CODCONT, "2", M->U5_FONE	, M->U5_CODPAIS, M->U5_DDD)
	lAGB3Adic	:= A70DefAGB(M->U5_CODCONT, "3", M->U5_FAX		, M->U5_CODPAIS, M->U5_DDD)
	lAGB5Adic	:= A70DefAGB(M->U5_CODCONT, "5", M->U5_CELULAR, M->U5_CODPAIS, M->U5_DDD)	

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Gravacao dos itens³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	A070GravaItem("AGA", aColsAGA, aHeadAGA, @aAGAPadrao, "AGA_END"		, {|| A70AGADesamarra() })
	A070GravaItem("AGB", aColsAGB, aHeadAGB, @aAGBPadrao, "AGB_TELEFO"	, {|| A70AGBDesamarra() })  

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Grava o relacionamento entre U5 e AGA.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Len(aAGAPadrao) > 0	
		nAGAPadrao := aAGAPadrao[1][3]
		A70GrvEndDados(	aAGAPadrao[1][1]																									,;
						PadR(aColsAGA[nAGAPadrao][aScan(aHeadAGA,{|x|AllTrim(x[2])=="AGA_END"})],TamSX3("U5_END")[1])		,;
						PadR(aColsAGA[nAGAPadrao][aScan(aHeadAGA,{|x|AllTrim(x[2])=="AGA_BAIRRO"})],TamSX3("U5_BAIRRO")[1])	,;
						PadR(aColsAGA[nAGAPadrao][aScan(aHeadAGA,{|x|AllTrim(x[2])=="AGA_MUNDES"})],TamSX3("U5_MUN")[1])		,;
						PadR(aColsAGA[nAGAPadrao][aScan(aHeadAGA,{|x|AllTrim(x[2])=="AGA_EST"})],TamSX3("U5_EST")[1])		,;
						PadR(aColsAGA[nAGAPadrao][aScan(aHeadAGA,{|x|AllTrim(x[2])=="AGA_CEP"})],TamSX3("U5_CEP")[1])		,;
						PadR(aColsAGA[nAGAPadrao][aScan(aHeadAGA,{|x|AllTrim(x[2])=="AGA_PAIS"})],TamSX3("U5_PAIS")[1])		)
	EndIf
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Grava o relacionamento entre U5 e AGB.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Len(aAGBPadrao) > 0					
		nPos := aScan(aAGBPadrao, {|x| x[2] == "1"})
		If nPos > 0
			A70GrvTelDados( "1"																																,;
							aAGBPadrao[nPos][1]	                                                                                                          	,;
							PadR( aColsAGB[aAGBPadrao[nPos][3]][aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_TELEFO"})] ,TamSX3("U5_FCOM1")[1])	)
							
			A70GrvDDDados(	PadR( aColsAGB[aAGBPadrao[nPos][3]][aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_DDI"})] ,TamSX3("U5_CODPAIS")[1])	,;
							PadR( aColsAGB[aAGBPadrao[nPos][3]][aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_DDD"})] ,TamSX3("U5_DDD")[1])		)
							
		EndIf
		nPos := aScan(aAGBPadrao, {|x| x[2] == "2"})
		If nPos > 0
			A70GrvTelDados( "2"																																,;
							aAGBPadrao[nPos][1]																												,;
							PadR( aColsAGB[aAGBPadrao[nPos][3]][aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_TELEFO"})] ,TamSX3("U5_FONE")[1])	)
													
			A70GrvDDDados(	PadR( aColsAGB[aAGBPadrao[nPos][3]][aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_DDI"})] ,TamSX3("U5_CODPAIS")[1])	,;
							PadR( aColsAGB[aAGBPadrao[nPos][3]][aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_DDD"})] ,TamSX3("U5_DDD")[1])		)							
		EndIf				
		nPos := aScan(aAGBPadrao, {|x| x[2] == "3"})
		If nPos > 0
			A70GrvTelDados( "3"																																,;
							aAGBPadrao[nPos][1]																												,;
							PadR( aColsAGB[aAGBPadrao[nPos][3]][aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_TELEFO"})] ,TamSX3("U5_FAX")[1])		)
							
			A70GrvDDDados(	PadR( aColsAGB[aAGBPadrao[nPos][3]][aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_DDI"})] ,TamSX3("U5_CODPAIS")[1])	,;
							PadR( aColsAGB[aAGBPadrao[nPos][3]][aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_DDD"})] ,TamSX3("U5_DDD")[1])		)							
		EndIf								
		nPos := aScan(aAGBPadrao, {|x| x[2] == "5"})
		If nPos > 0
			A70GrvTelDados( "5"																																,;
							aAGBPadrao[nPos][1]																												,;
							PadR( aColsAGB[aAGBPadrao[nPos][3]][aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_TELEFO"})] ,TamSX3("U5_CELULAR")[1])	)																					
							
			A70GrvDDDados(	PadR( aColsAGB[aAGBPadrao[nPos][3]][aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_DDI"})] ,TamSX3("U5_CODPAIS")[1])	,;
							PadR( aColsAGB[aAGBPadrao[nPos][3]][aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_DDD"})] ,TamSX3("U5_DDD")[1])		)							
		EndIf								
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Retira relacionamento quando não há endereço/telefone padrão selecionado.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	A70LimNaoPadrao(	lAGAAdic	,lAGB1Adic	,lAGB2Adic	,lAGB3Adic	,;
						lAGB5Adic	)			
ElseIf (nOpc==5)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Exclusão dos itens.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	A70DelItem("AGA")
	A70DelItem("AGB")
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Adiciona ou Remove o privilegios deste registro.  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cChave		:= PadR(xFilial("SU5")+SU5->U5_CODCONT,TAMSX3( "AO4_CHVREG")[1])		
	aAutoAO4	:= CRMA200PAut(nOpc,"SU5",cChave,/*cCodUsr*/,/*aPermissoes*/,/*aNvlEstrut*/,/*cCodUsrCom*/,/*dDataVld*/)    
	CRMA200Auto(aAutoAO4[1],aAutoAO4[2],nOpc)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Exclusão do cabeçalho³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("SU5")
	RecLock("SU5",.F.)
	DbDelete() 
	MsUnLock() 

EndIf
	
RestArea(aAreaAO4)
RestArea(aAreaSX3)

Return Nil     

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70LimNaoPadrao ³ Autor ³ Vendas CRM     ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Limpa os dados da SU5, quando um item no AGA/AGB           ³±±
±±³          ³ relacionado é apagao.                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Nil := A70LimNaoPadrao( lAGAAdic, lAGB1Adic, lAGB2Adic,    ³±±
±±³          ³                         lAGB3Adic, lAGB5Adic )             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ lAGAAdic: Se houve um gravação proposital no SU5;          ³±±
±±³          ³ lAGB1Adic: Se houve uma gravação do tipo de telefone 1     ³±±
±±³          ³ lAGB1Adic: Se houve uma gravação do tipo de telefone 2     ³±±
±±³          ³ lAGB1Adic: Se houve uma gravação do tipo de telefone 3     ³±±
±±³          ³ lAGB1Adic: Se houve uma gravação do tipo de telefone 5     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A70LimNaoPadrao(lAGAAdic, lAGB1Adic, lAGB2Adic, lAGB3Adic, lAGB5Adic)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se não encontrar nenhum endereço padrão, apaga qualquer³
	//³relacionamento.                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If aScan( aColsAGA, {|x| x[aScan(aHeadAGA,{|x|AllTrim(x[2])=="AGA_PADRAO"})] == "1" } ) == 0 .And. !lAGAAdic
		A70EndDadosLimpa()
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se não encontrar nenhum telefone padrão para um determinado tipo³
	//³apaga qualquer relacionamento.                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If aScan( aColsAGB, {|x| x[aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_TIPO"})] == "1" .And. x[aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_PADRAO"})] == "1" } ) == 0 .And. !lAGB1Adic
		A70TelDadosLimpa( "1" )
	EndIf
	If aScan( aColsAGB, {|x| x[aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_TIPO"})] == "2" .And. x[aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_PADRAO"})] == "1"} ) == 0 .And. !lAGB2Adic
		A70TelDadosLimpa( "2" )
	EndIf	
	If aScan( aColsAGB, {|x| x[aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_TIPO"})] == "3" .And. x[aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_PADRAO"})] == "1"} ) == 0 .And. !lAGB3Adic
		A70TelDadosLimpa( "3" )
	EndIf 
	If aScan( aColsAGB, {|x| x[aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_TIPO"})] == "5" .And. x[aScan(aHeadAGB,{|x|AllTrim(x[2])=="AGB_PADRAO"})] == "1"} ) == 0 .And. !lAGB5Adic
		A70TelDadosLimpa( "5" )
	EndIf	
Return 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70AGADesamarra ³ Autor ³ Vendas CRM     ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Retira o relacionamento entre SU5 e AGA.                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Nil := A70AGADesamarra()                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A70AGADesamarra()	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se o item que está sendo deletado é o padrão da SU5,³
	//³se for, exclui os dados da SU5.                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If AGA->AGA_CODIGO == SU5->U5_CODAGA
		A70EndDadosLimpa()	
	EndIf		
Return    

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70AGBDesamarra ³ Autor ³ Vendas CRM     ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Retira o relacionamento entre SU5 e AGA.                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Nil := A70AGBDesamarra()                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/ 
Static Function A70AGBDesamarra()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se o item que está sendo deletado é o padrão da SU5,³
	//³se for, exclui os dados da SU5.                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If AGB->AGB_CODIGO == SU5->U5_AGBRES
		A70TelDadosLimpa( "2" )
		A70DDDadosLimpa()
	ElseIf AGB->AGB_CODIGO == SU5->U5_AGBCEL
		A70TelDadosLimpa( "5" )
		A70DDDadosLimpa()
	ElseIf AGB->AGB_CODIGO == SU5->U5_AGBFAX
		A70TelDadosLimpa( "3" )
		A70DDDadosLimpa()
	ElseIf AGB->AGB_CODIGO == SU5->U5_AGBCOM
		A70TelDadosLimpa( "1" )
		A70DDDadosLimpa()
	EndIf	
Return 
 
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70EndDadosLimpa ³ Autor ³ Vendas CRM    ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Limpa os campos do SU5 referentes ao endereço.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Nil := A70EndDadosLimpa()                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A70EndDadosLimpa()
	A70GrvEndDados()
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70GrvEndDados ³ Autor ³ Vendas CRM      ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Grava informações no SU5 relativas ao endereço.            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Nil := A70GrvEndDados( cCodAGA ,cEnd	, cBairro , cMun      ³±±
±±³          ³                        cEst    ,cCEP	, cPais )             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ cCodAGA := Código do AGA dos dados passados por parâmetro. ³±±
±±³          ³ cEnd := Endereço a ser gravado.                            ³±±
±±³          ³ cBairro := Bairro a ser gravado.                           ³±±
±±³          ³ cMun := Municípo a ser gravado.                            ³±±
±±³          ³ cEst := Estado a ser gravado.                              ³±±
±±³          ³ cCEP := CEP a ser gravado.                                 ³±±
±±³          ³ cPais := País a ser gravado.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A70GrvEndDados(	cCodAGA	,cEnd	, cBairro	, cMun	,;
								cEst	,cCEP	, cPais		)
	Default cCodAGA	:= ""
	Default	cEnd	:= ""
	Default cBairro	:= ""
	Default cMun	:= ""
	Default cEst	:= ""
	Default cCEP	:= ""
	Default cPais	:= ""								

	RecLock("SU5", .F.)
	Replace SU5->U5_CODAGA	With cCodAGA
	Replace SU5->U5_END		With cEnd
	Replace SU5->U5_BAIRRO	With cBairro
	Replace SU5->U5_MUN		With cMun
	Replace SU5->U5_EST		With cEst
	Replace SU5->U5_CEP		With cCEP
	Replace SU5->U5_PAIS	With cPais
	SU5->(MsUnLock())
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70TelDadosLimpa ³ Autor ³ Vendas CRM    ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Limpa os campos do SU5 referentes ao telefone de um tipo.  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Nil := A70TelDadosLimpa( cTipo )                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ cTipo: Tipo do telefone.      	                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/ 
Static Function A70TelDadosLimpa( cTipo )	
	A70GrvTelDados( cTipo )
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70GrvEndDados ³ Autor ³ Vendas CRM      ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Grava informações no SU5 relativas ao telefone.            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Nil := A70GrvEndDados( cTipo, cCodAGB, cTel )              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ cTipo := Tipo do telefone.                                 ³±±
±±³          ³ cCodAGB := Código do AGB dos dados passados por parâmetro. ³±±
±±³          ³ cTel := Telefone a ser gravado.                            ³±±
±±³          ³ cDDI := DDI a ser gravado.                                 ³±±
±±³          ³ cDDD := DDD a ser gravado.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/ 
Static Function A70GrvTelDados(	cTipo, cCodAGB, cTel )
	Default cCodAGB	:= ""
	Default cTel	:= ""
	
	RecLock("SU5", .F.)
	Replace &("SU5->" + A70AGBCampo( cTipo ))	With cCodAGB	
	Replace &("SU5->" + A70SU5Campo( cTipo ))	With cTel
	SU5->(MsUnLock())
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70DDDadosLimpa ³ Autor ³ Vendas CRM     ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Limpa os campos do SU5 referentes ao DDI, DDD.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Nil := A70DDDadosLimpa()                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³                               	                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A70DDDadosLimpa()
	A70GrvDDDados()
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70GrvDDDados ³ Autor ³ Vendas CRM       ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Grava informações no SU5 relativas ao DDI, DDD.            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Nil := A70GrvEndDados( cDDI, cDDD )                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ cDDI := DDI a ser gravado.                                 ³±±
±±³          ³ cDDD := DDD a ser gravado.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/ 
Static Function A70GrvDDDados( cDDI, cDDD )
	Default cDDI	:= ""
	Default cDDD	:= ""
	
	RecLock("SU5", .F.)
	Replace SU5->U5_CODPAIS						With cDDI
	Replace SU5->U5_DDD							With cDDD
	SU5->(MsUnLock())
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A070GravaItem ³ Autor ³ Vendas CRM       ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Grava informações relativas a tabela informada.            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ .T. := A070GravaItem(cAlias, aTmpCols, aTmpHeader,         ³±±
±±³          ³                      aCodigos, cCampoVerificador,          ³±±
±±³          ³                      bRetPadrao )                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ cAlias := Alias da tabela.                                 ³±±
±±³          ³ aTmpCols := aCols da tabela a ser gravada.                 ³±±
±±³          ³ aTmpHeader := aHeader da tabela a ser gravada.             ³±±
±±³          ³ aCodigos := Array com os códigos padrões selecionados.     ³±±
±±³          ³ cCampoVerificador := Qual o campo utilizado para verificar ³±±
±±³          ³                      a validade da linha.                  ³±±
±±³          ³ bRetPadrao := Bloco executado para apagar o relacionamento.³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/ 
Static Function A070GravaItem(cAlias, aTmpCols, aTmpHeader, aCodigos, cCampoVerificador, bRetPadrao)
	Local nLin				:= 0
	Local nCpo				:= 0
	Local nPosRecNo			:= 0
	Local nPosCodigo		:= 0
	Local nPosVerificador	:= 0
	Local nLenPrepStat      := 0 
	Local nPosPrepared      := 0
	Local nPosTipo			:= 0
	Local cTempCodigo		:= ""
	Local lNew				:= .F.
	Local cFilAlias		    := FwxFilial(cAlias)
	Local cQuery            := ""
	Local aInsert 	        := {} 
	Local cMD5 		        := ""
	Local nX                := 0
	Local cAliasPSQry       := GetNextAlias()
	Local lVerLib			:= Tmk070Stat()
	Local aRegPad			:= {}
	Local l070UDefAuto		:= (Type("lTk070Auto") == "U")
	Local l070Altera		:= FwIsInCallStack("a70Altera") 

	aCols := aClone(aTmpCols)
	aHeader := aClone(aTmpHeader)
	
	nPosRecNo		:= aScan(aHeader,{|x|AllTrim(x[2])==cAlias+"_REC_WT"})
	nPosCodigo		:= aScan(aHeader,{|x|AllTrim(x[2])==cAlias+"_CODIGO"})	
	nPosVerificador	:= aScan(aHeader,{|x|AllTrim(x[2])==cCampoVerificador})	
	nPosTipo		:= aScan(aHeader,{|x|AllTrim(x[2])==cAlias+"_TIPO"})	
	

	For nLin := 1 to Len(aCols)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Só grava se a linha não estiver deletada.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !aTail(aCols[nLin])
			If !Empty(aCols[nLin][nPosVerificador])
				If nPosCodigo == 1
					If !Empty(aCols[nLin][nPosCodigo])
						(cAlias)->(DbSetOrder(2))
						If (cAlias)->(DbSeek( cFilAlias + aCols[nLin][nPosCodigo] ))
							lNew := .T.
							While (cAlias)->&(cAlias+"_CODIGO") == aCols[nLin][nPosCodigo]
								If (cAlias)->&(cAlias+"_ENTIDA") + AllTrim((cAlias)->&(cAlias+"_CODENT")) == "SU5" + AllTrim(M->U5_CODCONT)
									RecLock(cAlias, .F.)
									lNew := .F.
									Exit
								EndIf
								(cAlias)->(DbSkip())
							EndDo
							If lNew
								RecLock(cAlias, .T.)
							EndIf
						Else
							RecLock(cAlias, .T.)
							lNew := .T.
						EndIf
					Else					
						RecLock(cAlias, .T.)
						lNew := .T.
					EndIf
				Else									
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Verifica se é um registro novo, ou se ele já existe.³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					lNew := aCols[nLin][nPosRecNo] == 0
					If lNew
						RecLock(cAlias,.T.)
					Else			
						(cAlias)->(DbGoto(aCols[nLin][nPosRecNo]))
						RecLock(cAlias,.F.)
					EndIf
				EndIf
					
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Grava os campos existentes no aHeader.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				For nCpo := 1 To Len(aHeader)
					If (aHeader[nCpo][10] != "V" ) .And. aHeader[nCpo][2] != cAlias + "_CODIGO"
						(cAlias)->&(aHeader[nCpo][2]) := aCols[nLin][nCpo]					
					EndIf
				Next nCpo

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Grava campos específicos da tabela.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lNew
					(cAlias)->&(cAlias+"_FILIAL")	:= cFilAlias
					(cAlias)->&(cAlias+"_ENTIDA")	:= "SU5"
					(cAlias)->&(cAlias+"_CODENT")	:= M->U5_CODCONT
					
					cTempCodigo := GetSx8Num(cAlias,cAlias + "_CODIGO")

					If !l070UDefAuto .And. lTk070Auto
						If l070Altera
							If (cAlias)->&(cAlias+"_PADRAO") == '1'
								aAdd(aRegPad, {cFilAlias, M->U5_CODCONT, aCols[nLin][nPosTipo], cTempCodigo })
							EndIf
						EndIf
					EndIf

					While Empty( (cAlias)->&(cAlias+"_CODIGO") )
						// Query para buscar chave duplicada
						cQuery := "SELECT " + CRLF
						cQuery += "	AGB.AGB_CODIGO " + CRLF
						cQuery += "FROM " + CRLF
						cQuery += "	? AGB " + CRLF
						cQuery += "WHERE " + CRLF
						cQuery += "	AGB.AGB_FILIAL = ? " + CRLF
						cQuery += "		AND " + CRLF
						cQuery += "	AGB.AGB_CODIGO = ? " + CRLF
						cQuery += "		AND " + CRLF
						cQuery += "	AGB.AGB_ENTIDA = ? " + CRLF
						cQuery += "		AND " + CRLF
						cQuery += "	AGB.AGB_CODENT = ? " + CRLF
						cQuery += "		AND " + CRLF
						cQuery += "	AGB.D_E_L_E_T_ = ' ' " + CRLF

						Aadd(aInsert, RetSqlName( "AGB" ))
						Aadd(aInsert, cFilAlias)
						Aadd(aInsert, cTempCodigo)
						Aadd(aInsert, "SU5")
						Aadd(aInsert, M->U5_CODCONT)

						nLenPrepStat := Len(aInsert)
						cMD5         := MD5(cQuery) 
						If (nPosPrepared := Ascan(__aPrepared,{|x| x[2] == cMD5})) == 0
							cQuery := ChangeQuery(cQuery) 
							Aadd(__aPrepared,{IIf(lVerLib,FwExecStatement():New(cQuery),FWPreparedStatement():New(cQuery)),cMD5})
							nPosPrepared := Len(__aPrepared)
						Endif 

						__aPrepared[nPosPrepared][1]:SetUnsafe(1, aInsert[1])
						For nX := 2 to nLenPrepStat
							__aPrepared[nPosPrepared][1]:SetString(nX, aInsert[nX])
						Next 

						If lVerLib
							__aPrepared[nPosPrepared][1]:OpenAlias(cAliasPSQry)
							DbSelectArea(cAliasPSQry)
						Else
							cQuery := __aPrepared[nPosPrepared][1]:getFixQuery()
							dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasPSQry,.T.,.T.)
						EndIf

						aInsert := aSize(aInsert,0)

						If (cAliasPSQry)->(!EoF())
							cTempCodigo := GetSx8Num(cAlias,cAlias + "_CODIGO")
						Else
							(cAlias)->&(cAlias+"_CODIGO") := cTempCodigo
						EndIf

						(cAliasPSQry)->(DbCloseArea())
					EndDo
	
				EndIf
	
				(cAlias)->(MsUnlock())
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Acumula lista de itens padrões.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If (cAlias)->&(cAlias+"_PADRAO") == "1"
					aAdd(aCodigos, { (cAlias)->&(cAlias+"_CODIGO"), (cAlias)->&(cAlias+"_TIPO"), nLin } )
				EndIf
			EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se a linha estiver deletada.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Else
			If nPosCodigo == 1
				If !Empty(aCols[nLin][nPosCodigo])
					(cAlias)->(DbSetOrder(2))
					(cAlias)->(DbSeek( cFilAlias + aCols[nLin][nPosCodigo] ))					
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Executa a função responsável por retirar a amarração entre³
					//³o SU5 e a tabela em questão.                              ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					Eval(bRetPadrao)
					
					RecLock(cAlias,.F.,.T.)
					(cAlias)->(DbDelete())
					(cAlias)->(MsUnlock())					
				EndIf
			Else					
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Se a linha existir na base de dados.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If aCols[nLin][nPosRecNo] > 0
					(cAlias)->(DbGoto(aCols[nLin][nPosRecNo]))
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Executa a função responsável por retirar a amarração entre³
					//³o SU5 e a tabela em questão.                              ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					Eval(bRetPadrao)
					
					RecLock(cAlias,.F.,.T.)
					(cAlias)->(DbDelete())
					(cAlias)->(MsUnlock())
				EndIf
			EndIf	
		EndIf
	Next nLin

	If !l070UDefAuto .And. lTk070Auto
		If !Empty(aRegPad)
			A70RegPad(cAlias, aRegPad)
		EndIf
	EndIf
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70DelItem ³ Autor ³ Vendas CRM          ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Apaga os itens relativos a tabela informada.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Nil := A70DelItem( cAlias )                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ cAlias := Alias da tabela.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A70DelItem(cAlias)
Local nLin			:= 0
Local aRecDele	:= {}
Local cFilAlias	:= xFilial(cAlias)
	
DbSelectArea(cAlias)
DbSetOrder(1)
DbSeek(cFilAlias+"SU5"+SU5->U5_CODCONT)
	
While	!(cAlias)->(Eof())												.And.;
		(cAlias)->&(cAlias+"_FILIAL")			==	cFilAlias			.And.;
		(cAlias)->&(cAlias+"_ENTIDA")			==	"SU5"				.And.;
		AllTrim((cAlias)->&(cAlias+"_CODENT"))	==	AllTrim(SU5->U5_CODCONT)
		
	AAdd(aRecDele,(cAlias)->(Recno()))
		
	(cAlias)->(DbSkip())
		
EndDo
	
For nLin := 1 to Len(aRecDele)
	(cAlias)->(DbGoTo(aRecDele[nLin]))
	RecLock(cAlias,.F.)
	DbDelete()
	MsUnLock()
Next nLin
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70PaisGat ³ Autor ³ Vendas CRM          ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Validação do campo AGA_PAIS.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lRet := A70PaisGat()                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Se é o valor é válido ou não.                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A70PaisGat()

Local lRet := !EMPTY(M->AGA_PAIS)

If Type("lTk070Auto") == "L" .AND. lTk070Auto  
	lRet := .F.
EndIf 

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70MunRel ³ Autor ³ Vendas CRM           ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Validação do campo AGA_MUN                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ cValor := A70MunRel()                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Se o valor é válido ou não.                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A70MunRel()

Local lRet := .T.
Local lGatilho := IsInCallStack("RunTrigger")
Local lCC2Pais := cPaisLoc $ "ANG|BRA|CHI|COL|COS|DOM|EQU|EUA|HAI|MEX|PER|PTG|VEN"


If !lGatilho .And. lCC2Pais
	If oGetAGB <> Nil
		aCols	:= oGetAGA:aCols
		aHeader	:= oGetAGA:aHeader
		N		:= oGetAGA:nAt
	EndIf
	lRet := Vazio() .Or. ExistCpo("CC2",GdFieldGet("AGA_EST",N,,aHeader,aCols)+GdFieldGet("AGA_MUN",N,.T.,aHeader,aCols))
ElseIf lGatilho .And. !lCC2Pais
	lRet := .F.
EndIf
 
Return lRet 
 
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ A70CC2Fil ³ Autor ³ Vendas CRM           ³ Data ³ 06/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Filtro da consulta padrão do país.                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lRet := A70CC2Fil()                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Se é para filtrar ou não.                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CALL CENTER                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A70CC2Mun()
	
	Local cFiltro := "CC2->CC2_EST == '" + A70CC2Fil() + "' .And. CC2->CC2_FILIAL == '" + xFilial("CC2") + "'"

Return ConPad1(,,,"CC2",,,.F.,,,,,, cFiltro)


Function A70CC2Fil()

Local xRet
Local aColsCC2Fil	:= {}
Local aHeadCC2Fil	:= {}
Local lCC2Mun		:= FWIsInCallStack("A70CC2Mun")

If oGetAGA <> Nil
	If ( oGetAGA:lEditLine )
		// Se for utilizada a edição por linha no grid, comparo o valor de memória digitado
		If lCC2Mun
			xRet	:= M->AGA_EST
		Else
			xRet	:= ( CC2->CC2_EST == M->AGA_EST )
		EndIf
	Else
		// Se não for edição em linha no grid de dados
		aColsCC2Fil	:= oGetAGA:aCols
		aHeadCC2Fil	:= oGetAGA:aHeader
		N			:= oGetAGA:nAt
		// Análise para retorno dos dados que serão apresentados
		If lCC2Mun
			xRet := GdFieldGet("AGA_EST",N,,aHeadCC2Fil,aColsCC2Fil)
		Else
			xRet := CC2->CC2_EST == GdFieldGet("AGA_EST",N,,aHeadCC2Fil,aColsCC2Fil)
		EndIf
	EndIf
EndIf
Return xRet

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetChild

Retorna os filhos relacionados ao registro.	

@sample		GetChild(cFilialCont,cCont)

@param			ExpC1 - Filial do contato
				ExpC2 - Código do contato

@return		ExpA1 - Array contendo os filho do registro

@author		Cristiane Nishizaka
@since			05/02/2014
@version		12.0
/*/
//------------------------------------------------------------------------------

Static Function GetChild(cFilialCont,cCont)

Local aArea	:= GetArea()  
Local aAreaSU5:= SU5->(GetArea())
Local aFilhos	:= {}
Local cQuery	:= ""
Local cAlias	:= GetNextAlias()

cQuery := "SELECT U5_CONPRI, U5_CODCONT FROM " + RetSqlName("SU5") + " SU5 "
cQuery += "WHERE "
cQuery += "SU5.U5_FILIAL = '" + cFilialCont + "' AND "
cQuery += "SU5.U5_CONPRI = '" + cCont + "' AND "
cQuery += "SU5.D_E_L_E_T_ = ' ' "
cQuery += "ORDER BY SU5.U5_CONPRI, SU5.U5_CODCONT "

cQuery	:= ChangeQuery(cQuery)

If Select(cAlias) > 0
	(cAlias)->(DbCloseArea())
EndIf

DbUseArea(.T.,"TOPCONN",TCGenQry(,,cQuery),cAlias,.F.,.T.)

While !(cAlias)->(EOF())
    Aadd(aFilhos, (cAlias)->U5_CODCONT)
	(cAlias)->(DbSkip())			
End

(cAlias)->(DbCloseArea())

RestArea(aAreaSU5)
RestArea(aArea)

Return aFilhos


//------------------------------------------------------------------------------
/*/{Protheus.doc} PermExcPai

Valida Exclusão, veridificando se o contato está contindo na Estrutura de SubContatos.	

@sample		PermExcPai(cFilialCont,cCont)

@param			ExpC1 - Filial do contato
				ExpC2 - Código do contato

@return		ExpA1 - 	.T. permite exclusão
							.F. não permite exclusão

@author		Cristiane Nishizaka
@since			05/02/2014
@version		12.0
/*/
//------------------------------------------------------------------------------

Static Function PermExcPai(cFilialCont,cCont)

Local nX
Local cFilhos		:= ""
Local lRet			:= .F.
Local aFilhos		:= {}

aFilhos	:= GetChild(cFilialCont,cCont)

If !Empty(aFilhos)
	cFilhos := STR0046 // " pois ele é contato primário do(s) registro(s) "
	For nX := 1 To Len(aFilhos)
		cFilhos +=	aFilhos[nX]
		If (nX < Len(aFilhos)-1)
				cFilhos +=	", "
		ElseIf (nX <> Len(aFilhos))
				cFilhos +=	" e "
		EndIf		
	Next
	Help("",1, STR0043, , STR0044+cCont+cFilhos+STR0045, 1, ) // "Atenção" ## "Não é possível excluir o contato " ## ". Exclua o(s) vínculo(s) e tente novamente. "
	lRet	:= .T.
EndIf 

Return lRet


//------------------------------------------------------------------------------
/*/{Protheus.doc} FtValConPri

Valid do campo A1_CLIPRI, valida a inclusão de filhos na Estrutura de SubContatos.

@sample		FtValConPri(cFilialCont,cCont,cContPri)

@param			ExpC1 - Filial do contato
				ExpC2 - Código do contato				
				ExpC3 - Código do contato primário

@return		ExpA1 - 	.T. permite inclusão
							.F. não permite inclusão

@author		Cristiane Nishizaka
@since			05/02/2014
@version		12.0
/*/
//------------------------------------------------------------------------------

Function FtValConPri(cFilialCont,cCont,cContPri)

Local aArea			:= GetArea()
Local aAreaSU5		:= SU5->(GetArea())
Local lRet			:= .T.
Local aFilhos		:= {}
Local aFilhosAux	:= {}
Local nX			:= 0
Local nY			:= 0
Local nZ			:= 0
Local lAllChild		:= .F.

If !Empty(cContPri)
	//Se o contato selecionado for o próprio contato do cadastro
	If cContPri == cCont
		lRet := .F.	
	Else
		If Empty(aFilhos)
			//Adiciona ao array todos os filhos do contato
			aFilhos := GetChild(cFilialCont,cCont)
		EndIf
		
		While !lAllChild
			nY ++  	
			//Guarda no array auxiliar todos os filhos de cada um dos filhos dos contatos
			If nY <= Len(aFilhos)
				cCont 		:= aFilhos[nY]
				aFilhosAux	:= GetChild(cFilialCont,cCont)
				If !Empty(aFilhosAux)
					For nZ := 1 To Len(aFilhosAux)
						//Adiciona ao array todos os filhos dos filhos do contato 
						Aadd(aFilhos, aFilhosAux[nZ])
					Next nZ
					aFilhosAux := {}
				EndIf
			Else 
				lAllChild	:= .T.
			EndIf
		End
		
		//Se o ContPri selecionado for um filho ou filho de seus filhos
		For nX := 1 To Len(aFilhos)
			If  aFilhos[nX]== cContPri
				lRet := .F.			
			EndIf	
		Next		
	EndIf
EndIf

If !lRet
	Help("",1, STR0043, , STR0047, 1, ) //"Atenção" ## "Contato primário inválido, não é possível criar vínculo. Selecione outro contato."
EndIf

RestArea(aArea)
RestArea(aAreaSU5)

Return  lRet



//------------------------------------------------------------------------------
/*/{Protheus.doc} IntegDef

Mensagem Única	

@sample		IntegDef( xEnt, nTypeTrans, cTypeMessage, cVersion, cTransaction, lJSon ) 

@param		xEnt 
@param		nTypeTrans 
@param		cTypeMessage
@param		cVersion
@param		cTransaction
@param		lJSon

@return		aRet 

@author		Totvs Cascavel
@since		29/08/2018
@version	12
/*/
//------------------------------------------------------------------------------
Static Function IntegDef( xEnt, nTypeTrans, cTypeMessage, cVersion, cTransaction, lJSon ) 

Local aRet 		:= {}
Default lJSon 	:= .F.

If lJSon .And. FindFunction("TMKI070O")
	aRet := TMKI070O( xEnt, nTypeTrans, cTypeMessage)
Endif

Return aRet

/*/{Protheus.doc} SetMenuDef
	Funcao Responsavel por Carregar o MenuDef para ser utilizado em conjunto com a funcao MsMGet()
	@type  Static Function
	@author Paulo V. Beraldo
	@since Out/2020
	@version 1.00
	@param cProgram	, Caracter	, Parametro que Informa o nome do Programa que Retornara o MenuDef
	@return aRot	, Vetor		, Vetor contendo as op??es do MenuDef Solicitado
/*/

Static Function SetMenuDef( cProgram )
Local aRot	:= {}
aRot := &("StaticCall(" + cProgram + ", MenuDef )" )

Return aRot

/*/{Protheus.doc} Tmk070Stat
Função utilizada para validar a data da LIB para utilização da classe FWExecStatement
@type       Function
@author     CRM/Faturamento
@since      Jan/2023
@version    12.1.33
@return     __Tk070VerLib, lógico, se pode ser utilizado a classe FWExecStatement
/*/
Static Function Tmk070Stat()

Static __Tk070VerLib := Nil

If __Tk070VerLib == Nil
	__Tk070VerLib := FWLibVersion() >= "20211116"
EndIf

Return __Tk070VerLib

//-------------------------------------------------------------------------------
/*/{Protheus.doc} A70RegPad()
Atualiza os campos Padrão das tabelas AGA e AGB para 2 quando um novo registro com padrão 1 
é cadastrado na alteração do cadastro do contato.

@param		cAliasPar = Alias passado por parâmetro - tabela AGA ou AGB
@param		aRegPad = Array contendo informações sobre 

@type       Static Function
@author     CRM/Faturamento
@since      Novembro/2024
@version    12.1.2410
@return 	.T., sempre retorna verdadeiro pois as tratativas ocorrem dentro da função
/*/
//-------------------------------------------------------------------------------
Static Function A70RegPad(cAliasPar As Character, aRegPad As Array) As Logical

	Local aArea			As Array
	Local aAreaAlias	As Array
	Local cAliasUpd		As Character
	Local cAlias		As Character
	Local cFilAlias		As Character
	Local cCodCont		As Character
	Local cTipo 		As Character
	Local cCodigo		As Character
	Local nX			As Numeric

	Default cAliasPar := ""
	Default aRegPad := {}

	aArea	  	:= GetArea()
	aAreaAlias  := (cAliasPar)->(GetArea())
	aAreaAlias	:= {}
	cAliasUpd	:= ""
	cAlias		:= ""
	cFilAlias	:= ""
	cCodCont	:= ""
	cTipo 		:= ""
	cCodigo		:= ""
	nX			:= 0

	For nX := 1 To Len(aRegPad)
		If Empty(cAlias)
			cAlias		:= cAliasPar //Alias passado por parâmetro - tabela AGA ou AGB
			cFilAlias	:= aRegPad[nX][1] //FwxFilial da tabela AGA ou AGB
			cCodCont	:= aRegPad[nX][2] //Código do contato 
			cTipo 		:= aRegPad[nX][3] //Tipo do cadastro do endereço ou telefone
			cCodigo		:= aRegPad[nX][4] //Campo Codigo da tabela AGA ou AGB
		Else
			If !(aRegPad[nX][3] $ cTipo)
				cTipo	+= "," + aRegPad[nX][3]  //Tipo do cadastro do telefone
			EndIf

			cCodigo	+= "," + aRegPad[nX][4]  //Campo Codigo da tabela AGB
		Endif
	Next nX

	If cAlias == "AGA"
		If __oRegAGA == Nil
			cQueryAGA := "SELECT R_E_C_N_O_ RECNO, AGA_CODIGO, AGA_PADRAO "
			cQueryAGA += "FROM " + RetSqlName("AGA") + " AGA "
			cQueryAGA += "WHERE AGA.AGA_FILIAL = ? "
			cQueryAGA += "AND AGA.AGA_ENTIDA = 'SU5' "
			cQueryAGA += "AND AGA.AGA_CODENT = ? "
			cQueryAGA += "AND AGA.D_E_L_E_T_ = ' '"

			cQueryAGA	:= ChangeQuery(cQueryAGA)
			__oRegAGA := FwExecStatement():New(cQueryAGA)
		EndIf

		__oRegAGA:SetString(1, cFilAlias)
		__oRegAGA:SetString(2, cCodCont)

		cAliasUpd := __oRegAGA:OpenAlias()

	ElseIf cAlias == "AGB"
		If __oRegAGB == Nil
			cQueryAGB := "SELECT R_E_C_N_O_ RECNO, AGB_CODIGO, AGB_PADRAO "
			cQueryAGB += "FROM " + RetSqlName("AGB") + " AGB "
			cQueryAGB += "WHERE AGB.AGB_FILIAL = ? "
			cQueryAGB += "AND AGB.AGB_ENTIDA = 'SU5' "
			cQueryAGB += "AND AGB.AGB_CODENT = ? "
			cQueryAGB += "AND AGB.AGB_TIPO IN ? "
			cQueryAGB += "AND AGB.D_E_L_E_T_ = ' '"

			cQueryAGB	:= ChangeQuery(cQueryAGB)
			__oRegAGB := FwExecStatement():New(cQueryAGB)
		EndIf

		__oRegAGB:SetString(1, cFilAlias)
		__oRegAGB:SetString(2, cCodCont)
		__oRegAGB:SetUnsafe(3, FormatIn(cTipo,","))

		cAliasUpd := __oRegAGB:OpenAlias()
	EndIf

	While !(cAliasUpd)->(Eof()) 
		
		If !((cAliasUpd)->&(cAlias+"_CODIGO") $ cCodigo ) .And. (cAliasUpd)->&(cAlias+"_PADRAO") == "1"
			(cAlias)->(dbGoto((cAliasUpd)->RECNO))
			RecLock(cAlias, .F.)
				(cAlias)->&(cAlias+"_PADRAO") := "2"
			(cAlias)->(MsUnlock())
		EndIf

		(cAliasUPD)->(DbSkip())
	EndDo

	(cAliasUpd)->(DBCloseArea())

	RestArea(aAreaAlias)
	RestArea(aArea)

Return .T.
