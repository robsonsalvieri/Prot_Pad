#INCLUDE "TMKA040.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "TMKDEF.CH"
#Include 'FWMVCDEF.CH'

Static cLastGroup	:= ""			//Ultimo grupo incluido
Static aFiliais := {}

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuncao    ณ TmkA040  ณ Autor ณ Vendas e CRM    		ณ Data ณ04/02/12  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao ณ Programa de atualizacao do Grupo de Atendimento            ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ CALL CENTER                                                ณฑฑ
ฑฑณFernando  ณ11/12/06|912   ณBops-115389 Alterado a array aRotina        ณฑฑ
ฑฑณ          ณ        |      ณpara criacao do menu funcional              ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/

Function TMKA040()
Local aIndexSU0 := {}
Local oBrowse

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Define Variaveis                                             ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Private aAC := { STR0001,STR0002 } //"Abandona","Confirma"

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Define Array contendo as Rotinas a executar do programa      ณ
//ณ ----------- Elementos contidos por dimensao ------------     ณ
//ณ 1. Nome a aparecer no cabecalho                              ณ
//ณ 2. Nome da Rotina associada                                  ณ
//ณ 3. Usado pela rotina                                         ณ
//ณ 4. Tipo de Transacao a ser efetuada                          ณ
//ณ    1 - Pesquisa e Posiciona em um Banco de Dados             ณ
//ณ    2 - Simplesmente Mostra os Campos                         ณ
//ณ    3 - Inclui registros no Bancos de Dados                   ณ
//ณ    4 - Altera o registro corrente                            ณ
//ณ    5 - Remove o registro corrente do Banco de Dados          ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Private aRotina := MenuDef()  

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Define o cabecalho da tela de atualizacoes                   ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Private cCadastro := STR0009 //"Atualizacao de Grupo de Atendimento"


//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณValida o modo de acesso das tabelas do TMKณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !TmkVerModo()
	Return .F.
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Cria o Browse ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
oBrowse := FWMBrowse():New()
oBrowse:SetAlias('SU0')
oBrowse:SetDescription(STR0009) // Atualizacao de Grupo de Atendimento.		
oBrowse:Activate()


EndFilBrw("SU0",aIndexSU0)	

Return .T.

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuncao    |MenuDef   ณ Autor ณ Vendas e CRM          ณ Data ณ08/12/06  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao ณ Funcao de definicao do aRotina                             ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณRetorno   ณ aRotina   retorna a array com lista de aRotina             ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso       ณ SIGATMK                                                    ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Static Function MenuDef() 
Local aRotina:=	{}

ADD OPTION aRotina TITLE STR0004 ACTION 'VIEWDEF.TMKA040' 	OPERATION 1	ACCESS 0 //"Pesquisar"
ADD OPTION aRotina TITLE STR0005 ACTION 'VIEWDEF.TMKA040'	OPERATION 2	ACCESS 0 //"Visualizar"
ADD OPTION aRotina TITLE STR0006 ACTION 'VIEWDEF.TMKA040'	OPERATION 3	ACCESS 0 //"Incluir"
ADD OPTION aRotina TITLE STR0007 ACTION 'VIEWDEF.TMKA040'	OPERATION 4	ACCESS 0 //"Alterar"
ADD OPTION aRotina TITLE STR0008 ACTION 'VIEWDEF.TMKA040'	OPERATION 5	ACCESS 0 //"Excluir"
ADD OPTION aRotina TITLE STR0052 ACTION "Tk040ChvUn"		OPERATION 7	ACCESS 0 //"Chave Unica"

Return (aRotina)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณModelDef  บ Autor ณ Vendas CRM         บ Data ณ 12/01/12    บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณDefine o modelo de dados (MVC)                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTmkA160                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function ModelDef()
Local oModel
Local oStruSU0 := FWFormStruct(1,'SU0',/*bAvalCampo*/,/*lViewUsado*/)
Local bCommit	 := { |oMdl| Tk040CMM( oMdl ) }
Local bPosValidacao	:= {|oMdl|Tk040Pos(oMdl)}

oStruSU0:SetProperty( 'U0_ACDGRP' , MODEL_FIELD_WHEN, {|| .T. } )

oModel := MPFormModel():New('TMKA040',/*bPreValidacao*/,bPosValidacao,bCommit,/*bCancel*/)
oModel:AddFields('SU0MASTER',/*cOwner*/,oStruSU0,/*bPreValidacao*/,/*bPosValidacao*/,/*bCarga*/)
oModel:SetDescription(STR0009)

Return(oModel)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณViewDef   บ Autor ณ Vendas CRM         บ Data ณ  11/01/11   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณDefine a interface para cadastro em MVC.                    บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTmkA160                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function ViewDef()

Local oView
Local oModel   := FWLoadModel('TMKA040')
Local oStruSU0 := FWFormStruct(2,'SU0')
Local lMACD	   := SuperGetMV("MV_TMKMACD",,.F.)
Local aButtons := {}

oStruSU0:SetProperty( 'U0_ACDGRP' , MVC_VIEW_CANCHANGE, .F.  )

oView := FWFormView():New()
oView:SetModel(oModel)
oView:AddField('VIEW_SU0',oStruSU0,'SU0MASTER')
oView:CreateHorizontalBox('TELA',100)
oView:SetOwnerView('VIEW_SU0','TELA')
oView:SetViewAction( 'BUTTONCANCEL',{ |oView| Tk040Canc( oView ) } )

If 	xFilial("SU0")<> ""	         
	Tk040FilFil(@aFiliais, .F.)
	oView:AddUserButton( STR0035, '', {|oView| (Tk040FilFil(@aFiliais, .T.,oModel) ,Tk040SelFil(@aFiliais,oModel)) },NIL,NIL, {MODEL_OPERATION_UPDATE, MODEL_OPERATION_INSERT}) ///"Filiais compartilhadas"###"Filiais"
EndIf

If lMACD
	oView:AddUserButton( STR0037,'GPEIMG32', {|oView| Tk040ACD(,oModel,oView) },NIL,NIL, {MODEL_OPERATION_UPDATE, MODEL_OPERATION_INSERT}) //"Adiciona Grupos DACs ao Grupo de Atendimento"###"DACs"
EndIf	

Return(oView)
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัอออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040Pos   บAutor  ณVendas CRM         บ Data ณ  01/04/09   บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯอออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณNo momento da gravacao, faz a validacao.                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTMKA040                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Tk040Pos(oMdl)
Local nOpc 	:= oMdl:GetOperation()
lRet:= .T.
cLastGroup	:= ""

If nOpc == MODEL_OPERATION_INSERT
	lRet := Tk040Valid()
ElseIf nOpc == MODEL_OPERATION_UPDATE
	lRet := Tk040AltMV()
ElseIf nOpc == MODEL_OPERATION_DELETE
	lRet:= Tk040DelMVC()
EndIf

Return lRet


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัอออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040CMM   บAutor  ณVendas CRM         บ Data ณ  01/04/09   บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯอออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณNo momento da gravacao, faz a validacao.                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTMKA040                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Tk040CMM(oMdl)

Local nOpc 	:= oMdl:GetOperation()
Local cEsta		:=""			// Codigo da Estacao ATUAL

FWFormCommit(oMdl)

If nOpc == MODEL_OPERATION_INSERT .Or. nOpc == MODEL_OPERATION_UPDATE
	Tk040GrvFil(aFiliais)
EndIf
If nOpc == MODEL_OPERATION_INSERT .AND. ( Alltrim(cEsta) <> Alltrim(SU0->U0_ESTACAO) )
	Tmk273AbreTEF()
EndIf

Return .T.

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัอออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040Canc  บAutor  ณVendas CRM        บ Data ณ  01/04/09  บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯอออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณNo momento da gravacao, faz a validacao.                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTMKA040                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Tk040Canc(oView)

Local oModel := FWModelActive()
Local nOpc := oModel:GetOperation()
Local nI		:= 0
Local aFiliais := {}       
Local aButtons := {}
Local aRegDel  := {}


If nOpc == MODEL_OPERATION_INSERT.AND. !Empty(cLastGroup)
	DbSelectArea("AGJ")
	DbSetOrder(2)//AGJ_FILIAL+AGJ_SU0COD+AGJ_GICOD
	DbSeek(xFilial("AGJ") + cLastGroup)
	While !AGJ->(Eof()) .AND. AGJ->AGJ_FILIAL == xFilial("AGJ") .AND. AGJ->AGJ_SU0COD == cLastGroup
		aAdd(aRegDel,AGJ->(Recno()))
		AGJ->(DbSkip())
	End
	For nI := 1 to Len(aRegDel)
		AGJ->(DbGoTo(aRegDel[nI]))
		RecLock("AGJ",.F.)
		DbDelete()
		MsUnLock()
	Next nI 				
EndIf

Return .T.

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัอออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040AltMV บAutor  ณVendas CRM         บ Data ณ  01/04/09   บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯอออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณNo momento da gravacao, faz a validacao.                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTMKA040                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Tk040AltMV()
Local lRet := .T. // Retorno da funcao

lRet := Tk040Valid()
If lRet
	Tk040VldAlt()
EndIf

Return lRet
/*                                                                      

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuncao    ณTk040Incluiณ Autor ณ Marcelo Kotaki     	ณ Data ณ 01/03/00 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao ณ Programa de Inclusao de Grupo de Atendimento               ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUs o      ณ CALL CENTER                                                ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk040Inclui(cAlias,nReg,nOpc)

Local nOpcA		:= 0			// Opcao de OK , CANCELA do usuario
Local nI		:= 0
Local cEsta		:=""			// Codigo da Estacao ATUAL
Local aFiliais 	:= {}       
Local aButtons 	:= {}
Local aRegDel  	:= {}
Local lMACD	   	:= SuperGetMV("MV_TMKMACD",,.F.)

If 	xFilial("SU0")<> ""	         
	Tk040FilFil(@aFiliais, .F.)  
	aAdd(aButtons, {"selectAll" , {|| IIf(Tk040SelFil(@aFiliais),M->U0_COMPART:="1",M->U0_COMPART:="2")}	,STR0034,STR0035, {|| .T. }}) //"Filiais compartilhadas"###"Filiais"
EndIf

If lMACD
	AAdd(aButtons,{ "GPEIMG32", {|| Tk040ACD(nOpc)} ,STR0036, STR0037 } ) //"Adiciona Grupos DACs ao Grupo de Atendimento"###"DACs"
EndIf

cLastGroup	:= ""
cEsta 		:= SU0->U0_ESTACAO        
nOpcA 		:= AxInclui(cAlias, nReg, nOpc, , , ,"Tk040Valid()" , , , aButtons)
 

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณVerifica a utilizacao do TEF e abre se necessario.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If (nOpcA == 1) .AND. ( Alltrim(cEsta) <> Alltrim(SU0->U0_ESTACAO) )
	Tmk273AbreTEF()
Endif                   

If nOpcA == 1
	Tk040GrvFil(aFiliais)
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณSe o usuario cancelou, apaga eventuais grupos DAC associadosณ
//ณao grupo nao incluido                                       ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If nOpcA <> 1 .AND. !Empty(cLastGroup)
	DbSelectArea("AGJ")
	DbSetOrder(2)//AGJ_FILIAL+AGJ_SU0COD+AGJ_GICOD
	DbSeek(xFilial("AGJ") + cLastGroup)
	While !AGJ->(Eof()) .AND. AGJ->AGJ_FILIAL == xFilial("AGJ") .AND. AGJ->AGJ_SU0COD == cLastGroup
		aAdd(aRegDel,AGJ->(Recno()))
		AGJ->(DbSkip())
	End
	For nI := 1 to Len(aRegDel)
		AGJ->(DbGoTo(aRegDel[nI]))
		RecLock("AGJ",.F.)
		DbDelete()
		MsUnLock()
	Next nI 				
EndIf

DbSelectArea(cAlias)
Return(.T.)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuncao    ณTk040Alteraณ Autor ณ Marcelo Kotaki     	ณ Data ณ 01/03/00 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao ณ Programa de Alteracao de Grupo de Atendimento              ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ CALL CENTER                                                ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk040Altera(cAlias,nReg,nOpc)

Local nOpcA 	:= 0		// Opcao de OK ou CANCELA do usuario
Local cEsta 	:= ""		// Codigo atual da Estacao (TEF)
Local aParam	:= {}		// Define um bloco de validacoes executadas pela funcao AXALTERA
Local aFiliais := {}                     
Local aButtons := {}
Local lFilRet	:= .F.
Local lMACD	   := SuperGetMV("MV_TMKMACD",,.F.)

If lMACD 
	AAdd(aButtons,{ "GPEIMG32", {|| Tk040ACD(nOpc)},STR0036, STR0037 } )//"Adiciona Grupos DACs ao Grupo de Atendimento"###"DACs"
EndIf

If 	xFilial("SU0")<> ""	                     
	Tk040FilFil(@aFiliais)  
	aAdd(aButtons, {"selectAll" , {|| IIf(Tk040SelFil(@aFiliais),M->U0_COMPART:="1",M->U0_COMPART:="2")}	,STR0034,STR0035, {|| .T. }})
EndIf

Aadd(aParam, {|| .T. } )
Aadd(aParam, {|| Tk040VldAlt() } )
Aadd(aParam, {|| .T. } )
Aadd(aParam, {|| .T. } )

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Envia para processamento dos Gets          ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
cEsta := SU0->U0_ESTACAO

nOpcA := AxAltera( cAlias, nReg, nOpc, Nil, Nil, Nil, Nil, "Tk040Valid()", Nil, Nil, aButtons, aParam)

If (nOpcA == 1) .AND. ( Alltrim(cEsta) <> Alltrim(SU0->U0_ESTACAO) )
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณVerifica a utilizacao do TEF e abre se necessario ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	Tmk273AbreTEF()
Endif

If nOpcA == 1
	Tk040GrvFil(aFiliais)
Endif

DbSelectArea(cAlias)

Return(.T.)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuncao    ณTk040DelMVCณ Autor ณMarcelo Kotaki   		ณ Data ณ01/03/00  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao ณ Programa de exclusao de Grupo de Atendimento               ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณCALL CENTER                                                 ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function Tk040DelMVC(cAlias,nReg,nOpc)
Local lRet 			:= .T.
Local nI			:= 0
Local aFiliais		:= {}
Local aRegDel		:= {}
Local cQuery		:= ""
Local cAliasQry		:= GetNextAlias()
Local lDel	:= .F.

If lRet
	//Query para filtrar SU7 para deletar grupo somente quando nใo tem amarra็ใo com operador
	cQuery := "SELECT COUNT(*) AS TOTREC FROM " + RetSqlName("SU7")
	cQuery += " WHERE U7_FILIAL = '" + xFilial("SU7") + "' AND U7_POSTO = '" + SU0->U0_CODIGO + "' AND D_E_L_E_T_ = ''"
	dbUseArea( .T., "TOPCONN", TcGenQry(,,cQuery ), cAliasQry, .F., .T. )
	lDel := (cAliasQry)->TOTREC == 0
	(cAliasQry)->(DbCloseArea())
	
	dbSelectArea("ADE")
	dbSetOrder(3)//ADE_FILIAL+ADE_GRUPO+ADE_OPERAD
	If dbSeek(xFilial("ADE")+SU0->U0_CODIGO) 
		Aviso( STR0069, STR0070,; //Este grupo possui atendimentos vinculados porem nใo serแ possํvel sua exclusใo
		{ STR0030 }, 2 ) //Ok
	Else			
		BEGIN TRANSACTION
			//Apaga grupos compartilhados
			Tk040GrvFil(aFiliais,.T.)

			//Apaga assuntos relacionados ao grupo
			DbSelectArea("SKK")
			DbSetOrder(1)
			DbSeek(xFilial("SKK") + SU0->U0_CODIGO)
			While !SKK->(Eof()) .AND. SKK->KK_FILIAL == xFilial("SKK") .AND. SKK->KK_CODSU0 == SU0->U0_CODIGO
				aAdd(aRegDel,SKK->(Recno()))
				SKK->(DbSkip())
			End
			For nI := 1 to Len(aRegDel)
				SKK->(DbGoTo(aRegDel[nI]))
				RecLock("SKK",.F.)
				DbDelete()
				MsUnLock()
			Next nI
                      
			aRegDel := {}
			//Apaga grupos DAC relacionados ao grupo
			DbSelectArea("AGJ")
			DbSetOrder(2)//AGJ_FILIAL+AGJ_SU0COD+AGJ_GICOD
			DbSeek(xFilial("AGJ") + SU0->U0_CODIGO)
			While !AGJ->(Eof()) .AND. AGJ->AGJ_FILIAL == xFilial("AGJ") .AND. AGJ->AGJ_SU0COD == SU0->U0_CODIGO
				aAdd(aRegDel,AGJ->(Recno()))
				AGJ->(DbSkip())
			End
			For nI := 1 to Len(aRegDel)
				AGJ->(DbGoTo(aRegDel[nI]))
				RecLock("AGJ",.F.)
				DbDelete()
				MsUnLock()
			Next nI
			
			//Apaga o grupo
			If lDel 
				DbSelectArea("SU0")
				RecLock("SU0",.F.,.T.)
				DbDelete()
				Msunlock()
			EndIf	
		END TRANSACTION
	EndIf
EndIf

DbSelectArea(cAlias)
Return lRet 

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuncao    ณTk040Del  ณ Autor ณMarcelo Kotaki   		ณ Data ณ01/03/00  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao ณ Programa de exclusao de Grupo de Atendimento               ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณCALL CENTER                                                 ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function Tk040Del(cAlias,nReg,nOpc)

Local nOpcA		:= 0 
Local aFiliais	:= {}
Local aRegDel	:= {}
Local nI		:= 0
Local oDlg        
// Abaixo Posicionamento
Local aSize	  := MsAdvSize(,.F.)
Local aObjects	  := {}
Local aInfo       := {}
Local aPosObj	  := {}
Local lConinua  := .T.
Local cQuery	:= ""
Local cAliasQry	:= GetNextAlias()
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Monta a entrada de dados do arquivo                          ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If 	xFilial("SU0")<> ""
	Tk040FilFil(@aFiliais)  
EndIf

While .T.
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Envia para processamento dos Gets          ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	nOpcA:=0
	
	SoftLock(cAlias) 
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณRecalcula dimensoes de telaณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	aSize 		:= MsAdvSize()
	aObjects 	:= {} 
	
	AAdd( aObjects, { 100, 100, .T., .T. } )
	
	aInfo 	:= { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 } 
	aPosObj := MsObjSize( aInfo, aObjects ) 
	
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(cCADASTRO) From aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL

		nOpcA:=EnChoice( cAlias, nReg, nOpc, aAC,"AC",STR0011,NIL,aPosObj[1]) //"Quanto a exclusao?"

	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()})

	DbSelectArea(cAlias)
	
	If nOpcA == 2
		
		BEGIN TRANSACTION
			
			DbSelectArea("AG9")
			DbSetOrder(2)			
			If DbSeek(xFilial("AG9") + SU0->U0_CODIGO)
				lConinua := .F.
				Aviso( STR0069, STR0071,	{ STR0030 }, 2 ) //Este grupo possui operadores vinculados, nใo serแ possํvel sua exclusใo.
			Endif

			If lConinua
				cQuery := "SELECT COUNT(*) AS TOTREC FROM " + RetSqlName("SU7")
				cQuery += " WHERE U7_FILIAL = '" + xFilial("SU7") + "' AND U7_POSTO = '" + SU0->U0_CODIGO + "' AND D_E_L_E_T_ = ''"
				dbUseArea( .T., "TOPCONN", TcGenQry(,,cQuery ), cAliasQry, .F., .T. )
				lConinua := (cAliasQry)->TOTREC == 0
				(cAliasQry)->(DbCloseArea())
				If	!lConinua			
					Aviso( STR0069, STR0071,	{ STR0030 }, 2 ) //Este grupo possui operadores vinculados, nใo serแ possํvel sua exclusใo.
				EndIf
			Endif
					
			If lConinua
				//Apaga grupos compartilhados
				Tk040GrvFil(aFiliais,.T.)
	
				//Apaga assuntos relacionados ao grupo
				DbSelectArea("SKK")
				DbSetOrder(1)
				DbSeek(xFilial("SKK") + SU0->U0_CODIGO)
				While !SKK->(Eof()) .AND. SKK->KK_FILIAL == xFilial("SKK") .AND. SKK->KK_CODSU0 == SU0->U0_CODIGO
					aAdd(aRegDel,SKK->(Recno()))
					SKK->(DbSkip())
				End
				For nI := 1 to Len(aRegDel)
					SKK->(DbGoTo(aRegDel[nI]))
					RecLock("SKK",.F.)
					DbDelete()
					MsUnLock()
				Next nI
				               
				aRegDel := {}
				//Apaga grupos DAC relacionados ao grupo
				DbSelectArea("AGJ")
				DbSetOrder(2)//AGJ_FILIAL+AGJ_SU0COD+AGJ_GICOD
				DbSeek(xFilial("AGJ") + SU0->U0_CODIGO)
				While !AGJ->(Eof()) .AND. AGJ->AGJ_FILIAL == xFilial("AGJ") .AND. AGJ->AGJ_SU0COD == SU0->U0_CODIGO
					aAdd(aRegDel,AGJ->(Recno()))
					AGJ->(DbSkip())
				End
				For nI := 1 to Len(aRegDel)
					AGJ->(DbGoTo(aRegDel[nI]))
					RecLock("AGJ",.F.)
					DbDelete()
					MsUnLock()
				Next nI
				
				//Apaga o grupo
				DbSelectArea("SU0")
				RecLock("SU0",.F.,.T.)
				DbDelete()
				Msunlock()
			EndIf
		END TRANSACTION
	Else
		MsUnLock()
	Endif
	
	Exit
End

DbSelectArea(cAlias)
Return(.T.)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัอออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040FilFilบAutor  ณVendas CRM         บ Data ณ  01/04/09   บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯอออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณPreenche as filiais que podem transferir chamados.          บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTMKA040                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Tk040FilFil(aFiliais, lCarregaDados, oModel)

Local aArea		:= GetArea()
Local aAreaSU0	:= SU0->(GetArea())
Local aAreaSM0	:= SM0->(GetArea())
Local nI 		:= 0
Local cGrpAtu	:= ""
Local cFilterSU0

Default lCarregaDados := .T.
Default oModel := Nil

aFiliais := {}

DbSelectArea("SM0")
DbSetOrder(1) //M0_CODIGO+M0_CODFIL
DbSeek(cEmpAnt)

While SM0->(!Eof()) .AND. SM0->M0_CODIGO == cEmpAnt
	If SM0->M0_CODFIL <> cFilAnt
		aAdd(aFiliais, {.F., SubStr(SM0->M0_CODFIL,1,TAMSX3('U0_FILIAL')[1]), SM0->M0_FILIAL, "" })
	EndIf
	SM0->(DbSkip())
End 
            
If lCarregaDados
	
	cGrpAtu := SU0->U0_CODIGO
	cGrpAtu := oModel:GetValue("SU0MASTER",'U0_CODIGO')
    DbSelectArea("SU0")
    DbSetOrder(4) // U0_FILIAL+U0_FILORI+U0_GRPORI
	cFilterSU0 := SU0->(DbFilter())
	SU0->(DbClearFilter())
	For nI := 1 To Len(aFiliais)
	   	If SU0->(DbSeek(aFiliais[nI, 2] + cFilAnt + cGrpAtu ))
	   		aFiliais[nI, 1] := .T.
	   		aFiliais[nI, 4] := SU0->U0_CODIGO
	   	EndIf
	Next nI   	         
	Set Filter To &cFilterSU0
EndIf	

RestArea(aAreaSM0)
RestArea(aAreaSU0)
RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัอออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040GrvFilบAutor  ณVendas CRM         บ Data ณ  01/04/09   บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯอออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณGrava os Grupos de atendimento nas filiais compartilhadas.  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTMKA040                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/             
Static Function Tk040GrvFil(aFiliais,lDeleta)

Local aArea		:= GetArea()
Local aAreaSU0 	:= SU0->(GetArea())
Local aValues  	:= {}
Local lRet 		:= .F.
Local nI 			:= 0
Local nI2 			:= 0
Local cCodSU0 	:= SU0->U0_CODIGO
Local cFilterSU0	:= ""
Local cCodGrupo	:= ""
Local lHasBranch	:= .F.
Local cFilAntBkp	:= cFilAnt
Local cFilSKKOri	:= xFilial("SKK")
Local cFilSU0Tmp	:= ""
Local lIncReg		:= .F.
Local cMsg			:= ""
Local aAssXEquip	:= {} 
Local aAssuntos	:= {}
Local aRegDel		:= {}  

Default lDeleta	:= .F.
                 
If Len(aFiliais) > 0 

	cFilterSU0 := SU0->(DbFilter())
	SU0->(DbClearFilter())
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณArmazena dados do grupoณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DbSelectArea("SX3")
	DbSetOrder(1)
	DbSeek("SU0")
	While SX3->(!Eof()) .AND. SX3->X3_ARQUIVO == "SU0"
		If Upper(SX3->X3_CONTEXT) <> "V" .AND. Upper(SX3->X3_CAMPO) <> "U0_FILIAL"
			aAdd(aValues, {SX3->X3_CAMPO, &("SU0->" + SX3->X3_CAMPO)})	
	    EndIf
		SX3->(DbSkip())
	End
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณArmazena dados da relacao grupo x assuntoณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DbSelectArea("SKK")
	DbSetOrder(1)
	DbSeek(cFilSKKOri + cCodSU0)
	
	While !SKK->(Eof()) .AND. SKK->KK_FILIAL == cFilSKKOri .AND. SKK->KK_CODSU0 == cCodSU0
		AAdd(aAssXEquip,SKK->KK_CODSKQ)
		SKK->(DbSkip())
	End
	
	DbSelectArea("SU0")

	For nI := 1 To Len(aFiliais)
		
		aAssuntos	:= aClone(aAssXEquip)
		aRegDel		:= {}
		cFilAnt		:= aFiliais[nI, 2]
		cFilSU0Tmp	:= xFilial("SU0")  
		
	    If aFiliais[nI, 1] .AND. !lDeleta  
	    
	    	lHasBranch	:= .T.
	    	
    		SU0->(DbSetOrder(4)) // U0_FILIAL+U0_FILORI+U0_GRPORI
	    	
	    	lIncReg 	:= !SU0->(DbSeek( cFilSU0Tmp + cFilAntBkp + cCodSU0 ))
	    	
	    	If !lIncReg
	    		cCodGrupo := SU0->U0_CODIGO
	    	Else
				SU0->(dbSetOrder(1))
				While .T.
					cCodGrupo := GETSXENUM("SU0","U0_CODIGO")
					//Verifico se jแ existe um grupo com o mesmo c๓digo
		    		If SU0->(dbSeek(cFilSU0Tmp+cCodGrupo))
		    			ConfirmSX8()
		    		Else
		    			Exit
		    		EndIf   		
	    		EndDo
	    	EndIf

			RecLock("SU0", lIncReg)

			For nI2 := 1 To Len(aValues)
				Eval(&("{|e1|SU0->" + aValues[nI2,1] + ":= e1}"), aValues[nI2,2])
			Next nI2

			SU0->U0_FILIAL := cFilSU0Tmp
			SU0->U0_FILORI := cFilAntBkp
			SU0->U0_GRPORI := cCodSU0
			SU0->U0_CODIGO := cCodGrupo

			MsUnlock()

			If lIncReg .AND. __lSX8
				ConfirmSX8()
			Endif
			
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณCompatibiliza os assuntos por grupoณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			DbSelectArea("SKK")
			DbSetOrder(1)
			DbSeek(xFilial("SKK") + cCodGrupo)
			
			While !SKK->(Eof()) .AND. SKK->KK_FILIAL == xFilial("SKK") .AND. SKK->KK_CODSU0 == cCodGrupo
				If (nI2 := aScan(aAssuntos,SKK->KK_CODSKQ)) == 0
					//Assunto antigo
					aAdd(aRegDel,SKK->(Recno()))
				Else                
					//Assunto ja existente no SKK do grupo compartilhado
					aDel(aAssuntos,nI2)
					aSize(aAssuntos, Len(aAssuntos)-1 )
				EndIf
				SKK->(DbSkip())
			End
			
			//Apaga assuntos antigos
			For nI2 := 1 to Len(aRegDel)
				SKK->(DbGoTo(aRegDel[nI2]))
				RecLock("SKK",.F.)
				DbDelete()
				MsUnLock()
			Next nI2
			
			//Adiciona novos registros no SKK
			SX5->(DbSetOrder(1)) //X5_FILIAL+X5_TABELA+X5_CHAVE
			
			For nI2 := 1 to Len(aAssuntos)
				If SX5->(DbSeek(xFilial("SX5") + "T1" + aAssuntos[nI2] ))
					RecLock("SKK",.T.)
					SKK->KK_FILIAL	:= xFilial("SKK")
					SKK->KK_CODSU0	:= cCodGrupo
					SKK->KK_CODSKQ	:= aAssuntos[nI2]
					MsUnLock()
				EndIf
			Next nI2

			If lIncReg
				cMsg += STR0038 + cCodGrupo + " (" + STR0018 + aFiliais[nI, 2]+ " - " + AllTrim(GetAdvFVal("SM0","M0_FILIAL",cEmpAnt+aFiliais[nI, 2],1,""))+ ")" + CRLF //"C๓digo: "###"Filial "
			EndIf
			
		Else
			                                         
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณApaga grupo antigo e amarracao entre grupo e assuntosณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู 
			SU0->(DbSetOrder(4)) // U0_FILIAL+U0_FILORI+U0_GRPORI
			If SU0->(DbSeek( cFilSU0Tmp+cFilAntBkp+cCodSU0 )) .AND. Tk040DelOk(SU0->U0_CODIGO)

				//Apaga assuntos relacionados ao grupo
				DbSelectArea("SKK")
				DbSetOrder(1)
				DbSeek(xFilial("SKK") + SU0->U0_CODIGO)
				While !SKK->(Eof()) .AND. SKK->KK_FILIAL == xFilial("SKK") .AND. SKK->KK_CODSU0 == SU0->U0_CODIGO
					aAdd(aRegDel,SKK->(Recno()))
					SKK->(DbSkip())
				End
				For nI2 := 1 to Len(aRegDel)
					SKK->(DbGoTo(aRegDel[nI2]))
					RecLock("SKK",.F.)
					DbDelete()
					MsUnLock()
				Next nI2

				//Apaga grupo
				RecLock("SU0", .F.) 
				DbDelete()
				MsUnlock()
				     
			EndIf
		EndIf	
	Next nI 

	cFilAnt := cFilAntBkp 
	
	DbSelectArea("SU0")	
	Set Filter To &cFilterSU0
	
	RestArea(aAreaSU0)
	
	If lHasBranch
		cCompart := "1"
	Else
		cCompart := "2"	
	EndIf
	
	DbSelectArea("SU0")
	DbSetOrder(1)
	If DbSeek( xFilial("SU0")+cCodSU0 )
		BEGIN TRANSACTION
			RecLock("SU0", .F.) 
			REPLACE SU0->U0_COMPART WITH cCompart
			MsUnlock()            
		END TRANSACTION
	EndIf	
	lRet := .T.

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณNotifica o usuario quanto aos novos gruposณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If !Empty(cMsg)
		Aviso(STR0010,cMsg,{"Ok"},3,STR0023) //"Grupo de atendimento"###"Grupos criados em outras filiais"
	EndIf

EndIf

RestArea(aArea)
	
Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040ValidบAutor  ณVendas CRM          บ Data ณ  03/07/10   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFuncao de validacao da gravacao dos dados da tela de grupos บฑฑ
ฑฑบ          ณde atendimento                                              บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk040Valid()

Local lRet	:= .T.

If (INCLUI .Or. ALTERA) .AND. M->U0_TPALOC == "3" .And. Empty(M->U0_OPALOC)
	MsgAlert(STR0040) //"Ao informar o campo TP. ALOCACAO com a op็ใo OPERADOR FIXO, o campo OPER. FIXO deve obrigatoriamente ser informado."
	lRet := .F.
EndIf

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัออออออออออออออออออออหออออออัออออออออออออปฑฑ
ฑฑบPrograma  ณTk040GetNumบAutor  ณ Vendas CRM         บ Data ณ  06/08/10  บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯออออออออออออออออออออสออออออฯออออออออออออนฑฑ
ฑฑบDesc.     ณ Retorna o proximo codigo de atendimento disponivel.        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ     														            บฑฑ
ฑฑบ          ณ   														               บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCALL CENTER                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk040GetNum()

Local aArea		:= GetArea()
Local cCodGrupo := ""

dbSelectArea("SU0")
dbSetOrder(1)

While .T.
	cCodGrupo := GETSXENUM("SU0","U0_CODIGO")
	If SU0->(dbSeek(xFilial("SU0")+cCodGrupo))
		ConfirmSX8()
	Else
		Exit
	EndIf   		
EndDo

RestArea(aArea)

Return (cCodGrupo)

//------------------------------------------------------------------------------
/*/{Protheus.doc} TMK040CSKA
	Efetua consulta padrใo conforme a localiza็ใo do ambiente

@sample 	TMK040CSKA() 

@return	[lRet],LOGICO,Se usuแrio selecionar um registro (.T.), senใo (.F.)

@author	Servi็os
@since		29/04/2015       
@version	P12   
/*/         
//------------------------------------------------------------------------------
Function TMK040CSKA()
Local lRet     := .F.

Do Case
	Case cPaisLoc $ "BRA"
		lRet := Conpad1( NIL,NIL,NIL,"SKABRA") 
	Case cPaisLoc $ "ARG|BOL|CHI|COL|COS|DOM|MEX|PAN|PAR|PER|POR|SAL|URU|VEN|RUS"
		lRet := Conpad1( NIL,NIL,NIL,"SKASPA")
	Case cPaisLoc $ "EUA"
		lRet := Conpad1( NIL,NIL,NIL,"SKAENG")
	Case cPaisLoc $ "ANG|EQU|HAI|PTG"	
		lRet := Conpad1( NIL,NIL,NIL,"SKAPTG")
EndCase
	
Return lRet

//------------------------------------------------------------------------------
/*/{Protheus.doc} TMK040SUFX
	Retorna um sufixo ('';'ENG';'PTG';'SPA') conforme a localiza็ใo do ambiente

@sample 	TMK040SUFX() 

@return	[cRet],CARACTERE,Sufixo relativo เ localiza็ใo base do ambiente.

@author	Servi็os
@since		29/04/2015       
@version	P12   
/*/         
//------------------------------------------------------------------------------
Function TMK040SUFX()
Local cRet := ''	//cPaisLoc = BRA

Do Case
	Case cPaisLoc $ "ARG|BOL|CHI|COL|COS|DOM|MEX|PAN|PAR|PER|POR|SAL|URU|VEN|RUS"
		cRet := "SPA"
	Case cPaisLoc $ "EUA"
		cRet := "ENG"
	Case cPaisLoc $ "ANG|EQU|HAI|PTG"	
		cRet := "PTG"
EndCase

Return cRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040VldAltบAutor ณArmando M. Tessaroliบ Data ณ  28/01/04   บฑฑ
ฑฑฬออออออออออุอออออออออออสออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValida a alteracao da regra de selecao no grupo de atendimenนฑฑ
ฑฑบ          ณto.                                                         บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP8                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk040VldAlt()

Local aArea := GetArea()		// Registra a base atual
Local lRet  := .T.				// Retorno logico da funcao
Local aArea	:= GetArea()
Local aAreaSX3	:= SX3->(GetArea())
Local cCampoAux := ''

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณAvalia se a Regra de Selecao pode ser alterada no Grupo de Atendimento.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !Empty(SU0->U0_REGSEL) .AND. M->U0_REGSEL <> SU0->U0_REGSEL
	DbSelectArea("SU7")
	DbSetOrder(1)
	MsSeek(xFilial("SU7"))
	While !Eof() .AND. SU7->U7_FILIAL == xFilial("SU7")
		
		If SU7->U7_POSTO <> SU0->U0_CODIGO
			DbSelectArea("SU7")
			DbSkip()
			Loop
		Endif
	    
		DbSelectArea("SK1")
		DbSetOrder(3)		// K1_FILIAL+K1_OPERAD
		If MsSeek(xFilial("SK1") + SU7->U7_COD)
			lRet := .F.
		Endif
		
		If lRet
			DbSelectArea("SU4")
			DbSetOrder(5)		// U4_FILIAL+U4_OPERAD+U4_STATUS
			MsSeek(xFilial("SU4") + SU7->U7_COD)
			While	!Eof()								.AND.;
					SU4->U4_FILIAL == xFilial("SU4")	.AND.;
					SU4->U4_OPERAD == SU7->U7_COD
				
				If SU4->U4_STATUS <> "2" .AND. Val(SU4->U4_TIPO) == COBRANCA		// Nao encerrada e de Cobranca
					lRet := .F.
					Exit
				Endif
				
				DbSelectArea("SU4")
				DbSkip()
			End
		Endif
		
		If !lRet
			Exit
		Endif
		
		DbSelectArea("SU7")
		DbSkip()
	End
	
Endif

If !lRet
	Help( " ", 1, "TMKTLCRGA" )
Endif

RestArea(aArea)

Return(lRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัอออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040SelFilบAutor  ณVendas CRM         บ Data ณ  01/04/09   บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯอออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณSeleciona as filiais que podem transferir chamados.         บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTMKA040                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Tk040SelFil(aFiliais,oModel)
Local lRet := .F.
Local oDlg
Local aButtons := {}
Local nFilial  := 0
Local oFiliais := Nil
Local lTodos   := .F.
Local lInverte := .F.
Local nOpcao   := 0    
Local oOk		:= LoaDbitmap(GetResources(),"LBOK")		// X Verde
Local oNo		:= LoaDbitmap(GetResources(),"LBNO")		// X Vermelho   
Local nI		:= 0 
Local aFilClone := aClone(aFiliais)
Local oView	:= Nil
Default oModel := Nil

oView	:= FwViewActive()

If Len(aFiliais) == 0
	Aadd(aFiliais, {.F., "", "", ""})
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณCria tela para a escolha das filiais ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DEFINE MSDIALOG oDlg TITLE STR0017 FROM 0,0 To 360,500 PIXEL //"Sele็ใo das filiais para transfer๊ncia de chamados"
	
	@ 30,05	Listbox oFiliais Fields;
			HEADER	"",;			// 01 - [x] ou [ ]
					STR0018,;		// 02 - Cod. Filial
					STR0019,;		// 03 - Nome da Filial
					STR0022,;		// 04 - Codigo do grupo
			On DbLCLICK (aFiliais[oFiliais:nAt,1]:= !aFiliais[oFiliais:nAt,1], oFiliais:Refresh());
			On Change (nFilial:= oFiliais:nAt) Size 242,115 Of oDlg Pixel NoScroll

	oFiliais:SetArray(aFiliais)
	oFiliais:bLine:={||{IIF(aFiliais[oFiliais:nAt,1],oOk,oNo),;	// 01 - [x] ou [ ]
					 	aFiliais[oFiliais:nAt,2],;					// 02 - Cod. Filial
					 	aFiliais[oFiliais:nAt,3],;					// 03 - Nome da Filial
					 	aFiliais[oFiliais:nAt,4]}}					// 04 - Codigo do grupo

	oFiliais:Refresh()

	@ 150,05 CheckBox oTodos Var lTodos Size 130,9 Pixel Of oDlg Prompt STR0020 On Change Tk040Tools(1, oFiliais, lTodos) //"Marca e Desmarca Todos"
	@ 150,85 CheckBox oInverte Var lInverte Size 130,9 Pixel Of oDlg Prompt STR0021 On Change Tk040Tools(2, oFiliais, lInverte) //"Inverte e Retorna Sele็ใo"

ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar(oDlg, {|| (nOpcao:= 1,oDlg:End())}, {|| oDlg:End() },,aButtons )

If nOpcao == 1
	For nI := 1 To Len(aFiliais)
		If aFiliais[nI, 1]
			lRet := .T.
			Exit
		EndIf
	Next nI   
Else
	aFiliais := aClone(aFilClone)
EndIf

If lRet 
	oModel:LoadValue("SU0MASTER",'U0_COMPART', Iif( lRet,"1","2"))
	oView:lModify := .T.
EndIf
Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณTk040Tempoณ Autor ณ LUIS MARCELO KOTAKI   ณ Data ณ01/03/00  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณFuncao para validacao do campo de TMA - EXECUTADO pelo SX3  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ CALL CENTER                                                ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function TK040Tempo()

Local lRet := .F.
Local nValor := &(ReadVar())

If nValor >= "00:00" .AND. nValor <= "59:59"
	lRet := .T.
Endif

Return(lRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออออหออออออัอออออออออออออออออออหออออออัอออออออออออปฑฑ
ฑฑบFuncao    ณTK040DescForma บAutor ณMarcelo Kotaki     บ Data ณ 08/03/02  บฑฑ
ฑฑฬออออออออออุอออออออออออออออสออออออฯอออออออออออออออออออสออออออฯอออออออออออนฑฑ
ฑฑบDescricao ณExibe a descricao do pertence de cada campo 				   บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCALL CENTER                          	              		   บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk040DescForma(cForma)
Local cDesc:= ""

Do Case
	Case (cForma == "1") //Item
		cDesc:= STR0012
		
	Case (cForma == "2") //Total
		cDesc:= STR0013
		
	Case (cForma == "3") //Ambos
		cDesc:= STR0014
		
	Case (cForma == "4") //Nao
		cDesc:= STR0015
EndCase

Return(cDesc)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040ToolsบAutor  ณ Vendas Clientes    บ Data ณ  13/03/10   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFuncao para selecao das Filiais apresentados no browser     บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MP10                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Tk040Tools(nTipo, oFiliais, lCheck)

Local nI	:= 0		// Variavel de Controle

If nTipo == 1
	If lCheck
		For nI := 1 To Len(oFiliais:aArray)
			oFiliais:aArray[nI][1] := .T.
			oFiliais:Refresh()
		Next nI
	Else
		For nI := 1 To Len(oFiliais:aArray)
			oFiliais:aArray[nI][1] := .F.
			oFiliais:Refresh()
		Next nI
	Endif
Else
	For nI := 1 To Len(oFiliais:aArray)
		If oFiliais:aArray[nI][1]
			oFiliais:aArray[nI][1] := .F.
		Else
			oFiliais:aArray[nI][1] := .T.
		Endif
		oFiliais:Refresh()
	Next nI
Endif

Return(.T.)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040DelOkบAutor  ณVendas CRM          บ Data ณ  15/04/10   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValida se o grupo de atendimento pode ser apagado           บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTMKA040                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Tk040DelOk(cGrupo)

Local aArea		:= GetArea()
Local aAreaSU0	:= SU0->(GetArea())
Local aAreaADE	:= ADE->(GetArea())
Local aAreaSU7	:= SU7->(GetArea())
Local lRet		:= .T.
Local cQuery	:= ""
Local cFilter	:= ""
Local cFiltro	:= ""
Local cAliasQry	:= GetNextAlias()

#IFDEF TOP

	If TcSrvType() <> "AS/400" 
		
		//Procura por operadores vinculados
        If lRet
			cQuery := "SELECT COUNT(*) AS TOTREC FROM " + RetSqlName("SU7")
			cQuery += " WHERE U7_FILIAL = '" + xFilial("SU7") + "' AND U7_POSTO = '" + cGrupo + "' AND D_E_L_E_T_ = ''"
	
			dbUseArea( .T., "TOPCONN", TcGenQry(,,cQuery ), cAliasQry, .F., .T. ) 
			
			lRet := (cAliasQry)->TOTREC == 0
	
			(cAliasQry)->(DbCloseArea())
		EndIf
		
		//Procura por chamados vinculados
		If lRet
			cQuery := "SELECT COUNT(*) AS TOTREC FROM " + RetSqlName("ADE")
			cQuery += " WHERE ADE_FILIAL = '" + xFilial("ADE") + "' AND ADE_GRUPO = '" + cGrupo + "' AND D_E_L_E_T_ = ''"
	
			dbUseArea( .T., "TOPCONN", TcGenQry(,,cQuery ), cAliasQry, .F., .T. ) 
			
			lRet := (cAliasQry)->TOTREC == 0
	
			(cAliasQry)->(DbCloseArea())
		EndIf
		
	Else
#ENDIF

	//Procura por operadores vinculados
	DbSelectArea("SU7")
	cFilter := DbFilter()
	
	If !Empty(cFilter)
		DbClearFilter()
	Endif
	
	Set Filter To &("U7_FILIAL =='" + xFilial("SU7") + "' .AND. U7_POSTO == '" + cGrupo + "'")
	DbGoTop() 
	lRet := Eof()
	DbClearFilter()
	
	If !Empty(cFilter)
		Set Filter To &cFilter
	EndIf
	     
	//Procura por chamados vinculados
	DbSelectArea("ADE")
	cFilter := DbFilter()
	
	If !Empty(cFilter)
		DbClearFilter()
	Endif
	
	Set Filter To &("ADE_FILIAL =='" + xFilial("ADE") + "' .AND. ADE_GRUPO == '" + cGrupo + "'")
	DbGoTop()
	lRet := Eof()
	DbClearFilter()
	
	If !Empty(cFilter)
		Set Filter To &cFilter
	EndIf

#IFDEF TOP
	EndIf 	
#ENDIF	


RestArea(aAreaSU7)
RestArea(aAreaADE)
RestArea(aAreaSU0)
RestArea(aArea)

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัออออออออออออออออออออหออออออัออออออออออออปฑฑ
ฑฑบPrograma  ณTk040ACD   บ Autor ณ Vendas CRM         บ Data ณ  13/07/10  บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯออออออออออออออออออออสออออออฯออออออออออออนฑฑ
ฑฑบDesc.     ณFuncao para a sele็ใo dos grupos DAC de cada grupo de       บฑฑ
ฑฑบ          ณatendimento                                                 บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCALL CENTER                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Tk040ACD( nOpc, oModel, oView )   

Local aArea			:= GetArea()						// Salva a แrea atual
Local cSeek			:= ""								// Seek utilizado na montagem da aCols
Local cWhile		:= ""								// While utilizado na montagem da aCols
Local aColsBkp		:= {}								// Backup da aCols
Local aHeaderBkp	:= {}								// Backup do aHeader
Local nBkp			:= 0								// Backup do N
Local nPosCODAGI	:= 0								// Posi็ใo no aCols do campo AGJ_GICOD
Local nPosGrupo		:= 0								// Posi็ใo no aCols do campo AGJ_DSCDAC
Local nPosNUMDAC	:= 0								// Posi็ใo no aCols do campo AGJ_NUMDAC
Local lGravar		:= .F.								// Se as altera็๕es serใo gravadas ou nใo
Local nCount		:= 0								// Contador temporแrio
Local nCount2		:= 0								// Contador temporแrio
Local oDlg			:= Nil								// Diแlogo utilizado para a exibi็ใo
Local oGroup		:= Nil								// Caixa de texto com o grupo pesquisado
Local cGroup		:= Space(TamSX3("AGI_COD")[1])		// C๓digo do grupo pesquisado
Local oGrpName		:= Nil								// Caixa de texto com o nome do grupo pesquisado
Local cGrpName		:= Space(TamSx3("AGI_DACNAM")[1]) 	// Nome do grupo pesquisado
Local oGetDados		:= Nil								// Objeto MsNewGetDados com os grupos que o operador pode trabalhar
Local aSize 		:= {}								// Array com os tamanhos dos objeto
Local aObjects 		:= {}								// Array com os objetos
Local aInfo 		:= {}								// Array com as informa็๕es de posicionamento
Local aPosObj 		:= {}								// Array com as posi็๕es dos objetos
Local nPosCod		:= 0



Default nOpc 		:= 0
Default oModel	:= Nil
Default oView	:= Nil

Private aCols
Private aHeader
Private N
Private INCLUI
Private ALTERA


nOpc := oModel:GetOperation()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณFaz os backups necessแrios.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
SaveInter()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณMonta o aCols e aHeader.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DbSelectArea("AGJ")
DbSetOrder(2) //AGJ_FILIAL+AGJ_SU0COD

cSeek	:= xFilial("AGJ") + M->U0_CODIGO
cWhile	:= "AGJ->AGJ_FILIAL+AGJ->AGJ_SU0COD"
	
aHeader := {}
aCols	:= {} 

If nOpc == 3
	INCLUI	:= .F.
	ALTERA	:= .T.
EndIf 

FillGetDados(	4 /*nOpcX*/, "AGJ"/*cAlias*/, 2/*nIndex*/, cSeek/*cSeek*/,; 
				{||&(cWhile)}/*{||&cWhile}*/, /*{|| bCond,bAct1,bAct2}*/, {"AGJ_FILIAL","AGJ_SU0COD","AGJ_DSCGRP"}/*aNoFields*/,; 
				/*aYesFields*/, /*lOnlyYes*/, /*cQuery*/, /*bMontAcols*/, .F./*lEmpty*/,; 
				/*aHeaderAux*/, /*aColsAux*/, /*bAfterCols*/, /*bBeforeCols*/,;
				/*bAfterHeader*/, /*cAliasQry*/)

nposCod := aScan(aHeader, {|x| AllTrim(x[2]) == "AGJ_GICOD"})
				
If nOpc == 3
	INCLUI := .T.
	ALTERA := If(nOpc==4, .T., .F.)
ElseIf nOpc == 4 .AND. Empty(aCols[1][nPosCod])
	INCLUI := .T.
	ALTERA := .F.
EndIf		
				
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณRemove os ultimos dois campos referentes ao Walk-Thru.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If Len(aCols) > 0 .And. Len(aHeader) > 0
	aDel(aHeader, Len(aHeader))
	aSize(aHeader, Len(aHeader)-1)
	aDel(aHeader, Len(aHeader))
	aSize(aHeader, Len(aHeader)-1)	
	For nCount := 1 To Len(aCols)
		aDel(aCols[nCount], Len(aCols[nCount])-1)
		aSize(aCols[nCount], Len(aCols[nCount])-1)
		aDel(aCols[nCount], Len(aCols[nCount])-1)
		aSize(aCols[nCount], Len(aCols[nCount])-1)
	Next
EndIf
	
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณPega as posicoes no aCols de campos utilizados.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
nPosCODAGI		:= aScan(aHeader, {|x| AllTrim(x[2]) == "AGJ_GICOD"})
nPosGRUPO		:= aScan(aHeader, {|x| AllTrim(x[2]) == "AGJ_DSCDAC"})
nPosNUMDAC		:= aScan(aHeader, {|x| AllTrim(x[2]) == "AGJ_NUMDAC"})

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณLimpa o primeiro campo em branco.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If Len(aCols) > 0 .And. Empty(aCols[1][nPosCODAGI])
	aDel(aCols, 1)
	aSize(aCols, Len(aCols)-1)
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณOrdena a aCols pela descri็ใo do grupo.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
aSort(aCols,,,{|x,y| AllTrim(x[2]) < AllTrim(y[2])})	

DEFINE MSDIALOG oDlg FROM 50,003 TO 400,255 TITLE STR0032 PIXEL	// "Selecionar os grupos DAC"

	aSize := MsAdvSize(.F.,.F.,010)
	AAdd( aObjects, { 001, 005, .T., .F. } )   
	AAdd( aObjects, { 001, 002, .T., .F. } ) 	
	AAdd( aObjects, { 001, 005, .T., .F. } )   
	AAdd( aObjects, { 001, 002, .T., .F. } ) 		
	AAdd( aObjects, { 001, 005, .T., .F. } )   
	AAdd( aObjects, { 000, 000, .T., .F. } )   
	aAdd( aObjects, { 120, 100, .F., .F. } )
	AAdd( aObjects, { 001, 002, .T., .F. } )   
	AAdd( aObjects, { 001, 005, .T., .F. } )   	
	aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 } 
	aPosObj := MsObjSize( aInfo, aObjects )

	@ aPosObj[1,1],aPosObj[1,2]  	SAY STR0033 PIXEL SIZE 55,9 OF oDlg	// "Grupos DAC"
	@ aPosObj[1,1],aPosObj[1,2]+45 MSGET oGroup Var cGroup SIZE 55,9 PICTURE PesqPict("AGI", "AGI_COD") OF oDlg Pixel F3 "AGI" VALID Tk040GetGpName(cGroup, @cGrpName, oDlg)

	@ aPosObj[3,1],aPosObj[3,2]  	SAY STR0029 PIXEL SIZE 55,9 OF oDlg	// "Nome"
	@ aPosObj[3,1],aPosObj[3,2]+45 MSGET oGrpName Var cGrpName SIZE 55,9 PICTURE PesqPict("AGI", "AGI_DACNAM") OF oDlg Pixel When .F.
	
	@ aPosObj[5,1],aPosObj[5,2]	BUTTON STR0006 SIZE 40,12 OF oDlg PIXEL ACTION (Tk040IGroup(@cGroup, @cGrpName, oGetDados))	// "Incluir"
	@ aPosObj[5,1],aPosObj[5,2]+45	BUTTON STR0008	SIZE 40,12 OF oDlg PIXEL ACTION (Tk040EGroup(oGetDados:nAT, oGetDados))	// "Excluir"

	oGetDados := MsNewGetDados():New(aPosObj[7,1]+15,aPosObj[7,2],aPosObj[7,3],aPosObj[7,4],0,,,,,,100,,,,oDlg,aHeader,aCols)			

	@ aPosObj[9,1],aPosObj[9,2]+35	BUTTON STR0030	SIZE 40,12 OF oDlg PIXEL ACTION (lGravar:= .T.,oDlg:End())	// "OK"
	@ aPosObj[9,1],aPosObj[9,2]+80	BUTTON STR0031	SIZE 40,12 OF oDlg PIXEL ACTION (lGravar:= .F.,oDlg:End())	// "Cancela"

ACTIVATE MSDIALOG oDlg CENTERED		

If lGravar
	cLastGroup := M->U0_CODIGO
	
	If ValType(oGetDados:aCols[oGetDados:nAt][nPosNUMDAC]) == "N"
		oModel:SetValue("SU0MASTER","U0_ACDGRP",AllTrim(Str(oGetDados:aCols[oGetDados:nAt][nPosNUMDAC])))	//Coloca como padrใo um grupo DAC
	Else
		oModel:SetValue("SU0MASTER","U0_ACDGRP",Space(TAMSX3("U0_ACDGRP")[1]))
	EndIf
	
	oView:EvalChanges()
	
	For nCount := 1 To Len(oGetDados:aCols)
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณSe o item em analise e valido.ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If !Empty(oGetDados:aCols[nCount][nPosGRUPO])
			DbSelectArea("AGJ")
			DbSetOrder(1) //AGJ_FILIAL+AGJ_GICOD+AGJ_SU0COD
			If DbSeek( xFilial("AGJ") + PadR(oGetDados:aCols[nCount][nPosCODAGI],TamSX3("AGJ_GICOD")[1]) + PadR(M->U0_CODIGO,TamSX3("AGJ_SU0COD")[1]) )
				If oGetDados:aCols[nCount][Len(oGetDados:aCols[nCount])]
					RecLock("AGJ", .F.)
					DbDelete()
					MsUnLock()
				EndIf
			Else
				If !oGetDados:aCols[nCount][Len(oGetDados:aCols[nCount])]
					RecLock("AGJ", .T.)
					Replace AGJ->AGJ_FILIAL With xFilial("AGJ")
					Replace AGJ->AGJ_SU0COD With M->U0_CODIGO
					For nCount2 := 1 To Len(oGetDados:aHeader)
						If oGetDados:aHeader[nCount2][10] <> "V"
							AGJ->&(aHeader[nCount2][2]) := oGetDados:aCols[nCount][nCount2]
						EndIf
					Next 
					MsUnLock()
				EndIf
			EndIf				
		EndIf
	Next
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณRestaura o backup.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู 
RestInter()
RestArea( aArea )	

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออหอออออออัอออออออออออออออออหออออออัออออออออออออปฑฑ
ฑฑบPrograma  ณTk040GetGpNameบ Autor ณ Vendas CRM      บ Data ณ  08/05/09  บฑฑ
ฑฑฬออออออออออุออออออออออออออสอออออออฯอออออออออออออออออสออออออฯออออออออออออนฑฑ
ฑฑบDesc.     ณFuncao para validar o grupo dac digitado.                   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1: C๓digo do grupo a ser incluํdo                       บฑฑ
ฑฑบ          ณExpC2: Nome do grupo a ser incluํdo                         บฑฑ
ฑฑบ          ณExpO3: Objeto da Dialog                                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCALL CENTER                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk040GetGpName(cGroup, cGrpName, oDlg)

Local lRet 		:= .F.	 				// Retorno da fun็ใo
Local aAreaAGI 	:= AGI->(GetArea())	// Salva a แrea atual

DbSelectArea("AGI")
DbSetOrder(1)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณSe o grupo DAC ้ valido.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !Empty(cGroup)
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณVerifica a sua exist๊ncia no cadastro de grupos DAC.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If ExistCpo("AGI", cGroup, 1) .AND. DbSeek(xFilial("AGI")+cGroup)
		cGrpName := AGI->AGI_DACNAM
		lRet := .T.
	EndIf
Else
	lRet := .T.
EndIf

RestArea(aAreaAGI)  

Return lRet  
    
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัออออออออออออออออออออหออออออัออออออออออออปฑฑ
ฑฑบPrograma  ณTk040IGroupบAutor  ณ Vendas CRM         บ Data ณ  08/05/09  บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯออออออออออออออออออออสออออออฯออออออออออออนฑฑ
ฑฑบDesc.     ณFuncao para incluir o grupo na lista de grupos.             บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1: C๓digo do grupo a ser incluํdo                       บฑฑ
ฑฑบ          ณExpC2: Nome do grupo a ser incluํdo                         บฑฑ
ฑฑบ          ณExpO3: Objeto da MsNewGetDados                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCALL CENTER                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Tk040IGroup(cCodGrupo, cNomeGrupo, oGetDados)    

Local nPosCODAGI	:= aScan(oGetDados:aHeader, {|x| AllTrim(x[2]) == "AGJ_GICOD"})	// Posi็ใo do campo AGJ_GICOD
Local nPosGRUPO		:= aScan(oGetDados:aHeader, {|x| AllTrim(x[2]) == "AGJ_DSCDAC"})	// Posi็ใo do campo AGJ_DSCDAC
Local nPosDAC		:= aScan(oGetDados:aHeader, {|x| AllTrim(x[2]) == "AGJ_NUMDAC"})	// Posi็ใo do campo AGJ_NUMDAC
Local nPos			:= 0																// Posi็ใo gen้rica

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณSe o usuแrio preencheu o c๓digo do grupo.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !Empty(cCodGrupo)
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณVerifica se o c๓digo preenchido jแ existe na lista.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If (nPos:=aScan(oGetDados:aCols, {|x|x[nPosCODAGI]==cCodGrupo})) <= 0
		If	!Empty(oGetDados:aCols[Len(oGetDados:aCols)][nPosCODAGI]) .AND.;
			!Empty(oGetDados:aCols[Len(oGetDados:aCols)][nPosGRUPO]) 
				 
				aAdd(oGetDados:aCols, Array(Len(oGetDados:aHeader)+1) )
		Endif
		oGetDados:aCols[Len(oGetDados:aCols)][nPosCODAGI]	:= cCodGrupo
		oGetDados:aCols[Len(oGetDados:aCols)][nPosGRUPO]	:= POSICIONE("AGI",1,XFILIAL("AGI")+cCodGrupo,"AGI_DACNAM")
		oGetDados:aCols[Len(oGetDados:aCols)][nPosDAC]		:= POSICIONE("AGI",1,XFILIAL("AGI")+cCodGrupo,"AGI_NUM")
		oGetDados:aCols[Len(oGetDados:aCols)][Len(oGetDados:aHeader)+1]	:= .F.
		aSort(oGetDados:aCols,,,{|x,y| AllTrim(x[2]) < AllTrim(y[2])})
		oGetDados:Refresh()
	Else
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณSe jแ existir e estiver deletado, recupera o item.ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If oGetDados:aCols[nPos][Len(oGetDados:aHeader)+1]
			oGetDados:aCols[nPos][Len(oGetDados:aHeader)+1]	:= .F.
		EndIf
					
	EndIf

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณLimpa os textbox.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	cCodGrupo 	:= Space(TamSX3("AGJ_GICOD")[1])       
	cNomeGrupo 	:= Space(TamSx3("AGJ_DSCGRP")[1]) 
EndIf
	
Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออหอออออออัออออออออออออออออออออหออออออัออออออออออออปฑฑ
ฑฑบPrograma  ณTk040EGroupบAutor  ณ Vendas CRM         บ Data ณ  08/05/09  บฑฑ
ฑฑฬออออออออออุอออออออออออสอออออออฯออออออออออออออออออออสออออออฯออออออออออออนฑฑ
ฑฑบDesc.     ณFuncao para excluir o grupo na lista de grupos.             บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpN1: Posi็ใo do cursor na MsNewGetDadis                   บฑฑ
ฑฑบ          ณExpO2: Objeto da MsNewGetDados                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCALL CENTER                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Tk040EGroup(nAt, oGetDados)
                      
Local nPosCODAGI	:= aScan(oGetDados:aHeader, {|x| AllTrim(x[2]) == "AGJ_GICOD"})	// Posi็ใo do campo AGJ_GICOD

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณVerifica se a posi็ใo do cursor nใo estoura a aCols.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If Len(oGetDados:aCols) >= nAt
	If TmkOK(StrTran(STR0039,"###",AllTrim(oGetDados:aCols[nAt][nPosCODAGI]))) //"Confirma a exclusใo do grupo DAC ### ?"
		oGetDados:aCols[nAt][Len(oGetDados:aHeader)+1]	:= .T.
		oGetDados:Refresh()	
	EndIf
EndIf   

Return


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040LsCpoบAutor  ณVendas CRM          บ Data ณ  02/09/10   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณListagem dos campos de uma tabela para selecao via F3 - Con-บฑฑ
ฑฑบ          ณsulta padrao                                                บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTMKA040                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk040LsCpo(aTabelas)

Local lRet	  := .T.
Local nUsado  := 0
Local nOpcA   := 0
Local nCpo	  := 0
Local nSX3Campo := 0
Local cVar	  := ReadVar()
Local nTam	  := Len(&cVar)
Local oDlgCons
Local oGetCons   
Local aSx3Fields := {}
Local nAt,nX

DbSelectArea("SX3")
DbSetOrder(1) 

For nX := 1 to Len(aTabelas)
	DbSeek(aTabelas[nX])
	While !Eof() .And. (SX3->X3_ARQUIVO == aTabelas[nX])
		If X3Uso(SX3->X3_USADO) .AND. SX3->X3_CONTEXT <> "V"
			nCpo++
			aAdd(aSx3Fields, {AllTrim(SX3->X3_CAMPO), Nil})
			aSX3Fields[nCpo][2] := X3Titulo()
		EndIf
		SX3->(DbSkip())
	End
Next nX

aSort(aSX3Fields,,,{|x,y|x[1]<y[1]})
 
Define MsDialog oDlgCons Title STR0041 From 000, 000 To 450, 300 PIXEL //"Consulta de campos"

	Define Font oFont Name 'Courier New' Size 0, -12		
	oGetCons := TCBrowse():New( 03, 005, 145, 200,, {STR0042,STR0043},,; //"Nome do Campo"###"Descri็ใo"
	                            oDlgCons,,,,,{||},,oFont,,,,,.T./*lUpdate*/,,.T.,,.T./*lDesign*/,,, )  	

	oGetCons:SetArray(aSx3Fields)
	oGetCons:bLine := {||{	aSx3Fields[oGetCons:nAt,1],aSx3Fields[oGetCons:nAt,2]}} 	                            
	oGetCons:blDblClick := {||nOpcA := 1, nAt := oGetCons:nAt, oDlgCons:End()}

	@208,060 BUTTON STR0044	SIZE 40,12 OF oDlgCons PIXEL ACTION (nOpcA := 1, nAt := oGetCons:nAt, oDlgCons:End())	//"Confirmar"
	@208,110 BUTTON STR0045	SIZE 40,12 OF oDlgCons PIXEL ACTION (nOpcA := 0, oDlgCons:End())	//"Cancelar"

Activate MsDialog oDlgCons Centered

If nOpcA == 1 
	If IsInCallStack("TMKA540")
		&cVar := aSx3Fields[nAt,1]
		&cVar += Space(nTam-Len(&cVar))
	Else
		If At(aSx3Fields[nAt,1],&cVar) == 0
			&cVar := AllTrim(&cVar) + Iif(Empty(&cVar),"","+") + aSx3Fields[nAt,1]
			&cVar += Space(nTam-Len(&cVar))
		Else   
			lRet := .F.
			MsgInfo(STR0046) //"Este campo jแ foi adicionado"
		EndIf
	EndIf
EndIf

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040VldChบAutor  ณVendas CRM          บ Data ณ  02/09/10   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValidacao do campo U0_CHAVUNI                               บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTMKA040                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk040VldCh()

Local lRet 		:= .T.
Local aArea		:= Getarea()
Local aAreaSX3	:= SX3->(GetArea())
Local cChave	:= AllTrim(&(ReadVar()))
Local aCampos	:= StrToKArr(cChave,"+") 
Local nX		:= 0

If Empty(cChave)
	Return .T.
EndIf

SX3->(DbSetOrder(2))

For nX := 1 to Len(aCampos)
	If Empty(aCampos[nX])
		MsgInfo(STR0047,STR0048) //"Remova o caractere '+' em excesso no final da chave" ###"Chave ๚nica"
		lRet := .F.
		Exit
	ElseIf !SX3->(DbSeek(aCampos[nX]))
		MsgInfo(STR0049 +aCampos[nX],STR0048) //"Nใo existe o campo "###"Chave ๚nica"
		lRet := .F.
		Exit
	ElseIf !(SX3->X3_ARQUIVO $ "ADE/ADF")
		MsgInfo(STR0050 +aCampos[nX]+ STR0051,STR0048) //"O campo "###" nใo pertence เs tabelas ADE ou ADF"###"Chave ๚nica"
		lRet := .F.
		Exit
	EndIf
Next nX

RestArea(aAreaSX3)
RestArea(aArea)

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040ChvUnบAutor  ณVendas CRM          บ Data ณ  11/01/11   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณConfigura a chave unica do TeleAtendimento para diversos    บฑฑ
ฑฑบ          ณgrupos no Service Desk                                      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTMKA040                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk040ChvUn()

Local aArea		:= GetArea()								//Posicionamento atual
Local aAreaSU0	:= SU0->(GetArea())						//Posicionamento da tabela SU0
Local aAreaTMP	:= {}										//Posicionamento da tabela SU0
Local aGrupos 	:= {}										//Grupos selecionados
Local aFiliais	:= {}										//Filiais com grupos compartilhados

Local cPesq 	:= Space(TAMSX3("U0_NOME")[1])				//Pesquisa do grupo de atendimento
Local cFilter	:= SU0->(DbFilter())						//Filtro da SU0
Local cFilSU0	:= xFilial("SU0")							//Filial da tabela SU0
Local cGrpAtu	:= ""										//Codigo do grupo atual

Local lGrava	:= .F.										//Indica se prossegue com a gravacao
Local lSU0Exc	:= SX2MODO("SU0") == "E"					//Indica se o SU0 esta exclusivo

Local oLBox													//Objeto da listbox
Local oPesq		:= Nil										//Objeto da caixa de pesquisa
Local oOk 	   	:= LoadBitmap( GetResources(), "LBOK")		//Desenho do Checked
Local oNo 	   	:= LoadBitmap( GetResources(), "LBNO")		//Desenho do Not Checked
Local oDlg   												//Objeto da tela

Local nX		:= 0										//Auxiliar de loop
Local nFil		:= 0										//Auxiliar de loop

Private cChave 	:= ""										//Chave unica

cChave := Space(TAMSX3("U0_CHAVUNI")[1])

DbSelectArea("SU0")
DbClearFilter()
DbSetOrder(2)
DbSeek(cFilSU0) 

//Carrega dados das filiais
Tk040FilFil(@aFiliais,.F.) 

//Seleciona os grupos (desprezando os compartilhamentos)
While !Eof() .AND. SU0->U0_FILIAL == cFilSU0 
	If (Empty(SU0->U0_FILORI) .AND. Empty(SU0->U0_GRPORI))
		AAdd(aGrupos,{ .F., SU0->U0_NOME, SU0->U0_CODIGO }) 
	EndIf
	DbSkip()
End

If Len(aGrupos) > 0
	
	DEFINE MSDIALOG oDlg TITLE STR0053 FROM 000, 000 TO 465, 485 PIXEL //"Chave ๚nica por grupos"
		
		//Chave unica	
		@ 010, 006 SAY STR0054 SIZE 025, 007 OF oDlg  PIXEL	//"Chave"
		@ 009, 034 MSGET cChave SIZE 188, 010 OF oDlg  F3 "SU0SX3" PIXEL VALID Tk040VldCh()
	
		@ 029, 004 TO 210, 241 PROMPT STR0055 OF oDlg  PIXEL //"Grupos que utilizarใo a chave acima:"
		
		@ 063, 010 LISTBOX oLBox Fields Header "",STR0056,STR0057 SIZE 225, 129 OF oDlg PIXEL; //"Grupo"###	"C๓digo"
			ON DblClick(oLbox:aArray[oLbox:nAt,1] := !oLbox:aArray[oLbox:nAt,1])
	
		oLBox:SetArray(aGrupos)
		oLBox:bLine := {||{	Iif(aGrupos[oLBox:nAt,1],oOk,oNo),;
								aGrupos[oLBox:nAT,2],;
								aGrupos[oLBox:nAT,3]}} 

		//Texto para pesquisa
		@ 044, 011 SAY STR0004 SIZE 028, 008 OF oDlg PIXEL //"Pesquisar"
		@ 043, 038 MSGET oPesq VAR cPesq SIZE 121, 010 OF oDlg PIXEL
		
		//Acoes da pesquisa
		@ 041, 162 BUTTON STR0058	SIZE 035, 013 OF oDlg PIXEL Action(Tk040Busca(@oLBox,cPesq,@oPesq,.T.))	//"Buscar"
		@ 041, 199 BUTTON STR0059	SIZE 035, 013 OF oDlg PIXEL Action(Tk040Busca(@oLBox,cPesq,@oPesq,.F.))	//"Pr๓ximo"
		
		//Controle de selecao
		@ 195, 011 BUTTON STR0060	SIZE 042, 010 OF oDlg PIXEL Action(AEval(oLBox:aArray,{|x|x[1] := .T.  }), oLBox:Refresh())	//"Marca todos"
		@ 195, 056 BUTTON STR0061 	SIZE 042, 010 OF oDlg PIXEL Action(AEval(oLBox:aArray,{|x|x[1] := .F.  }), oLBox:Refresh())	//"Desmarca todos"
		@ 195, 101 BUTTON STR0062 	SIZE 042, 010 OF oDlg PIXEL Action(AEval(oLBox:aArray,{|x|x[1] := !x[1]}), oLBox:Refresh())	//"Inverter sel."
		
		//Confirmacao da tela
		@ 215, 159 BUTTON STR0044	SIZE 039, 013 OF oDlg PIXEL	Action(lGrava:=.T.,oDlg:End())	//"Confirmar"
		@ 215, 201 BUTTON STR0045	SIZE 039, 013 OF oDlg PIXEL Action(oDlg:End())				//"Cancelar"
	
	ACTIVATE MSDIALOG oDlg CENTERED

Else

	MsgInfo(STR0063) //"Nใo hแ nenhum grupo de atendimento disponํvel nesta filial"

EndIf

If lGrava
	If Empty(cChave)
		lGrava := MsgNoYes(STR0065 + CRLF + STR0064) //"Atencใo: A chave ๚nica configurada estแ vazia, dispensando a valida็ใo de duplicidades."###"Confirma a altera็ใo da chave ๚nica para todos os grupos selecionados?")
	Else
		lGrava := MsgYesNo(STR0064) //"Confirma a altera็ใo da chave ๚nica para todos os grupos selecionados?"
	EndIf
EndIf 

DbSelectArea("SU0")

If lGrava

	BEGIN TRANSACTION
	
	For nX := 1 to Len(aGrupos)    
	
		DbSetOrder(1) //U0_FILIAL+U0_CODIGO  
		
		If aGrupos[nX][1] .AND. SU0->(DbSeek(cFilSU0 + aGrupos[nX][3]))
			RecLock("SU0",.F.)
			SU0->U0_CHAVUNI := cChave
			MsUnLock()
		EndIf
		  
		If lSU0Exc

			cGrpAtu 	:= SU0->U0_CODIGO
			aAreaTmp	:= GetArea()
			
			DbSetOrder(4) //U0_FILIAL+U0_FILORI+U0_GRPORI     
			
			For nFil := 1 To Len(aFiliais)
			   	If SU0->(DbSeek(aFiliais[nFil, 2] + cFilAnt + cGrpAtu ))
					RecLock("SU0",.F.)
					SU0->U0_CHAVUNI := cChave
					MsUnLock()
			   	EndIf
			Next nFil
			
			RestArea(aAreaTmp)
			 
		EndIf
		
	Next nX
	
	END TRANSACTION
	
EndIf

If !Empty(cFilter)
	dbSelectArea( "SU0" ) 
	SET FILTER TO &cFilter
EndIf

RestArea(aAreaSU0)
RestArea(aArea)

Return Nil
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040BuscaบAutor  ณVendas CRM          บ Data ณ  23/11/10   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณPesquisa um valor no array do listbox, posicionando o mesmo บฑฑ
ฑฑบ          ณem caso de sucesso. O objeto do MsGet sera colorido na cor  บฑฑ
ฑฑบ          ณvermelha quando a ocorrencia nao for encontrada.            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpO1 - Listbox das habilidades                             บฑฑ
ฑฑบ          ณExpC2 - Texto pesquisado                                    บฑฑ
ฑฑบ          ณExpO3 - Get da pesquisa                                     บฑฑ
ฑฑบ          ณExpL4 - Indica se deve pesquisar do inicio(.T.) ou nao(.F.) บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTMKA090                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk040Busca(oLbx,cBusca,oGtBusca,lInicio)

Local nStart	:= 1
Local lAchou	:= .F.
Local nCols		:= 0 
Local nCount,nCount2

Default oGtBusca:= Nil
Default lInicio	:= .T.

If !lInicio
	nStart := oLbx:nAt+1
EndIf

nCols := Len(oLbx:aArray[1])

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณProcura em todas as linhas e colunas pelo conteudo solicitado.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
For nCount := nStart To Len(oLbx:aArray)		
	For nCount2 := 2 To nCols
		If Upper(AllTrim(cBusca)) $ Upper(AllTrim(oLbx:aArray[nCount][nCount2]))					
			oLbx:nAt := nCount
			oLbx:Refresh()
			lAchou := .T.
			Exit
		EndIf
	Next

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณSe encontrou um resultado abandona.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If lAchou
		Exit
	EndIf		
Next

If oGtBusca <> Nil
	If lAchou
		oGtBusca:SetColor(CLR_BLACK,CLR_WHITE)
	Else
		oGtBusca:SetColor(CLR_WHITE,CLR_HRED)
	Endif
EndIf

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTk040GrupoบAutor  ณJorge Tavares       บ Data ณ  01/04/11   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValidacao do campo U0_GRPSUP                                บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTMKA040                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Tk040Grupo()

Local aArea 	:= GetArea()
Local cCodigo 	:= M->U0_CODIGO
Local cCodSup	:= M->U0_GRPSUP
Local lValid	:= .F.
Local cLoop		:= ' '

DbSelectArea("SU0")
DbSetOrder(1)
/* 	Indices 
	U0_FILIAL+U0_CODIGO - (1)
	U0_FILIAL+U0_NOME   - (2)
	U0_FILIAL+U0_GRPSUP - (3)
*/
    
While MsSeek( xFilial("SU0") + cCodSup ) .And. !Eof()

	If cCodSup == SU0->U0_CODIGO .And. cCodigo == SU0->U0_GRPSUP
		Help(" ",1,"TMKA04001") //-- Nใo ้ possํvel utilizar este c๓digo.
		Exit					//--  Ele esta preenchido com um grupo su-
								//-- perior que resulta em recursividade.		 
								
	ElseIf cCodSup == cCodigo
		Help(" ",1,"TMKA04003") //-- Esse grupo nใo pode ser superior dele mesmo.
		Exit
	
	ElseIf cCodSup == SU0->U0_CODIGO .And. Empty(SU0->U0_GRPSUP)
		lValid := .T.    
		Exit
			
	ElseIf cCodSup == SU0->U0_CODIGO .And. !Empty(SU0->U0_GRPSUP)
		cCodSup := SU0->U0_GRPSUP  // Continuo varrendo usando o Indice 1		
		
		If !Empty(cLoop) .And. cLoop == SU0->U0_CODIGO		
			Help(" ",1,"TMKA04002") //Grupo usado esta em Loop, corrigir a base de dados
			Exit
		Endif

		If Empty(cLoop)
			cLoop := SU0->U0_CODIGO
		Endif          
		
	Endif     
	
EndDo

RestArea(aArea)
Return lValid
