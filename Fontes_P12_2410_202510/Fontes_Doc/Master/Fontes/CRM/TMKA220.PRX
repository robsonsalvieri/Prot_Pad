#Include 'Protheus.ch'
#INCLUDE 'TMKA220.CH'
#Include 'FWMVCDEF.CH'

/*/


Ŀ
Funcao     Tmka220   Autor  Vendas Clientes        Data 27/12/11  
Ĵ
Descricao  Programa de atualizacao do Cadastro de Encerramento com    
           entrada automatica.    								            
Ĵ
Sintaxe    Void tmka220(void)                                         
Ĵ
 Uso       SIGATMK                                                    
ٱ


/*/

Function TMKA220(xRotAuto,nOpc)
Local oBrowse 
Default xRotAuto := Nil
Private aAC	:= { OemToAnsi(STR0001),OemToAnsi(STR0002) } //"Abandona","Confirma"

//Ŀ
// Define Array contendo as Rotinas a executar do programa      
// ----------- Elementos contidos por dimensao ------------     
// 1. Nome a aparecer no cabecalho                              
// 2. Nome da Rotina associada                                  
// 3. Usado pela rotina                                         
// 4. Tipo de Transacao a ser efetuada                          
//    1 - Pesquisa e Posiciona em um Banco de Dados             
//    2 - Simplesmente Mostra os Campos                         
//    3 - Inclui registros no Bancos de Dados                   
//    4 - Altera o registro corrente                            
//    5 - Remove o registro corrente do Banco de Dados          
//
Private aRotina := MenuDef() 

//Ŀ
// Definicao de variaveis para rotina de inclusao automatica    
//
Private lTk220Auto := ( xRotAuto <> NIL )

//Ŀ
// Verifica se e inclusao na rotina automatica 
//
	If xRotAuto == NIL
		//Ŀ
		// Cria o Browse 
		//
		oBrowse := FWMBrowse():New()
		oBrowse:SetAlias('SUN')
		oBrowse:SetDescription(STR0009) //Atualizao dos Tipos de Encerramento
		oBrowse:DisableDetails()
		oBrowse:SetAttach( .T. ) 				  //Habilita as vises do Browse
		oBrowse:SetTotalDefault('UN_ENCERR','COUNT',STR0012) //'Total de Registros'
		oBrowse:Activate()
	Else
		aRotina := MenuDef()
		FWMVCRotAuto(ModelDef(),"SUN",nOpc,{{"SUNMASTER",xRotAuto}})
	Endif

Return NIL

/*


ͻ
Programa  ModelDef   Autor  Vendas CRM          Data  27/12/11    
͹
Desc.     Define o modelo de dados (MVC)                              
͹
Uso       TmkA220                                                     
ͼ


*/
 
Static Function ModelDef()
Local oModel
Local bPosValidacao	:= {|oMdl|A220ExcMVC(oMdl)}		//Excluso da regra de seleo
Local oStruSUN		:= FWFormStruct(1,'SUN',/*bAvalCampo*/,/*lViewUsado*/)

oModel := MPFormModel():New('TMKA220',/*bPreValidacao*/,bPosValidacao,/*bCommit*/,/*bCancel*/)
oModel:AddFields('SUNMASTER',/*cOwner*/,oStruSUN,/*bPreValidacao*/,/*bPosValidacao*/,/*bCarga*/)
oModel:SetDescription(STR0009)	//Atualizao dos Tipos de Encerramento

Return(oModel)


/*


ͻ
Programa  ViewDef    Autor  Vendas CRM          Data   27/12/11   
͹
Desc.     Define a interface para cadastro em MVC.                    
                                                                      
͹
Uso       TmkA220                                                     
ͼ


*/

Static Function ViewDef()

Local oView
Local oModel   := FWLoadModel('TMKA220')
Local oStruSUN := FWFormStruct(2,'SUN')

oView := FWFormView():New()
oView :SetModel(oModel)
oView :AddField('VIEW_SUN', oStruSUN,'SUNMASTER')
oView :CreateHorizontalBox('TELA',100)
oView :SetOwnerView('VIEW_SUN','TELA')

Return(oView)

/*/


Ŀ
Funcao    MenuDef    Autor  Vendas CRM		       Data   27/12/11 
Ĵ
Descricao Funcao de definicao do aRotina                               
Ĵ
Sintaxe   aRotina   retorna a array com lista de aRotina               
Ĵ
Uso       TmkA220                                                      
Ĵ


/*/
Static Function MenuDef()

Local aRotina   := {}

ADD OPTION aRotina TITLE STR0004  ACTION 'PesqBrw' 			OPERATION 1	ACCESS 0 //"Pesquisar"
ADD OPTION aRotina TITLE STR0005  ACTION 'VIEWDEF.TMKA220'	OPERATION 2	ACCESS 0 //"Visualizar"
ADD OPTION aRotina TITLE STR0006  ACTION 'VIEWDEF.TMKA220'	OPERATION 3	ACCESS 0 //"Incluir"
ADD OPTION aRotina TITLE STR0007  ACTION 'VIEWDEF.TMKA220'	OPERATION 4	ACCESS 0 //"Alterar"
ADD OPTION aRotina TITLE STR0008  ACTION 'VIEWDEF.TMKA220'	OPERATION 5	ACCESS 0 //"Excluir"

If IsInCallStack("TMKA220")   
	aRotina:= CRMXINCROT("SUN",aRotina)
EndIf

Return (aRotina)

/*/


Ŀ
Funcao    A220Exc    Autor  Vendas Clientes        Data 03/03/00  
Ĵ
Descricao  Programa de Exclusao                                       
Ĵ
Sintaxe    Void A220Exc(ExpC1,ExpN1)                                  
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       SIGATMK                                                    
ٱ


/*/
Function A220ExcMVC(oMdl)

Local nOpc 	:= oMdl:GetOperation()
Local cAlias 	:= "SUC"
Local nReg 	:= Recno()
Local aArea	:= GetArea()
Local lRet	:= .T.

if nOpc == MODEL_OPERATION_DELETE 
	//Ŀ
	// Busca no arquivo de ligacoes se houve registro desse Tipo de Encerramento 
	//
	DbSelectArea("SUC")
	DbSetOrder(1)
	DbSeek(xFilial("SUC"))
	While !Eof() .AND. SUC->UC_FILIAL == xFilial("SUC")
		If SUC->UC_CODENCE == SUN->UN_ENCERR
			HELP(" ",1,"TMKNDELREL")
			Return(.F.)
		Endif
		DbSkip()
	End
EndIf

Return (.T.)


/*/


Ŀ
Funcao    A220Alt    Autor  Vendas Clientes        Data  03/03/00 
Ĵ
Descricao  Programa de Alteracao                                      
Ĵ
Sintaxe    Void A220Alt(ExpC1,ExpN1)                                  
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       SIGATMK                                                    
ٱ


/*/
Function A220Alt(cAlias,nReg,nOpc)
Local nOpca := 0

BEGIN TRANSACTION
	
If !lTk220Auto
	nOpca := AxAltera( cAlias, nReg, nOpc)
EndIf
	
END TRANSACTION
DbSelectArea(cAlias)
Return(.T.)


/*/


Ŀ
Funcao    MYTESTSUN  Autor  Vendas Clientes        Data  03/03/00 
Ĵ
Descricao  Teste Rotina automatica                                    
Ĵ
Sintaxe    Void A220Alt(ExpC1,ExpN1)                                  
Ĵ
 Uso       SIGATMK                                                    
ٱ


/*/

User Function MYTESTSUN() 
Local aDadosSUN := {} 

RpcSetType( 4 ) 
RpcSetEnv( 'T1', 'D MG 01 '  ) 
 
// Teste Inclusao 
aDadosSUN := {}
aAdd( aDadosSUN, { "UN_ENCERR"     , "000022"           , NIL } ) 
aAdd( aDadosSUN, { "UN_DESC"    , "ERICA HARUMI MASUDA"            , NIL } ) 
aAdd( aDadosSUN, { "UN_VALIDO"    , "1"                , NIL } ) 
aAdd( aDadosSUN, { "UN_TIPOATE"  , "3"                , NIL } ) 


lMsErroAuto := .F.
MSExecAuto( { | x, y | TMKA220( x, y ) } , aDadosSUN, 4 ) 

If lMsErroAuto
	If __lSX8
		RollBackSX8() 
	EndIf
	
	MostraErro() 
Else
	If __lSX8
		ConFirmSX8() 
	EndIf
	
	MsgInfo('Encerramento inserido com sucesso.')
EndIf
Return (.T.)
