#include 'protheus.ch'
#include 'tlpp-core.th'
#include 'tlpp-rest.th'


#DEFINE _INSERT_COLS_NR 3

#DEFINE GROUP_NO_ALL	10
#DEFINE GROUP_NO   		0
#DEFINE GROUP_TEAM 		1
#DEFINE GROUP_RES  		2
#DEFINE GROUP_TYPE_TEAM 3
#DEFINE GROUP_TYPE_RES  4

/*/{Protheus.doc} md
Get master data from PMS
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
@Get("/api/pms/v1/ru44w120/md/:md")
Function ru44w120_md()

	Local jResponse as Json
	Local jPath     as Json
	Local jQuery    := jsonObject():New() As Json
	Local cSkip
	Local cPageSize
	Local lReqTotalCount
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath  := oRest:getPathParamsRequest()
	jQuery := oRest:GetQueryRequest()

	If ( jQuery <> Nil )
		cSkip := jQuery[ 'skip' ]
		cPageSize := jQuery[ 'take' ]
		lReqTotalCount := jQuery[ 'requireTotalCount' ]
	EndIf

	jPath := oRest:getPathParamsRequest()

	If (jPath <> Nil)
		jResponse := GetMD(jPath[ 'md' ])
	EndIf

	If jResponse <> Nil
		oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
		oRest:setResponse(jResponse)
	EndIf
Return


/*/{Protheus.doc} md
Get master data from PMS Lookups (no filter , no paging, use only for master datas)
@type function
@author Bruno Sobieski
@since 18/07/2023
/*/
@Get("/api/pms/v2/ru44w120/md/:md/{:key}")
Function ru44w120_mdv2()

	Local jResponse as Json
	Local jPath     as Json
	//Local jQuery    := jsonObject():New() As Json
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath  := oRest:getPathParamsRequest()
//	jQuery := oRest:GetQueryRequest()

//	If ( jQuery <> Nil )
		//cFilter 	:= jQuery[ 'filter' ]
//	EndIf

	jPath := oRest:getPathParamsRequest()

	If (jPath <> Nil)
		jResponse := GetMD_V2(jPath[ 'md' ],jPath[ 'key' ])
	EndIf

	If jResponse <> Nil
		oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
		oRest:setResponse(jResponse)
	EndIf
Return

/*/{Protheus.doc} full
Get all allocation by team
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/full/:sessionId/:cDateFrom/:cDateTo")
Function ru44w120_full()

	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath := oRest:getPathParamsRequest()

	If (jPath <> Nil )
		jResponse := GetAlloc(GROUP_TEAM, jPath[ 'sessionId' ], jPath[ 'cDateFrom' ], jPath[ 'cDateTo' ],,,)
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)

Return


/*/{Protheus.doc} ru44w120_poc
Get Percentage of compression for a task
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/poc/:prj/:tsk/:date")
Function ru44w120_poc()

	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath := oRest:getPathParamsRequest()

	If (jPath <> Nil )
		jResponse:=	GetPoc(jPath[ 'prj' ], jPath[ 'tsk' ], jPath[ 'date' ])
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)

Return


/*/{Protheus.doc} ru44w120_fullres
Get all allocation grouped by resource
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/fullres/:sessionId/:cDateFrom/:cDateTo")
Function ru44w120_fullres()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath := oRest:getPathParamsRequest()

	If (jPath <> Nil )
		jResponse := GetAlloc(GROUP_RES, jPath[ 'sessionId' ], jPath[ 'cDateFrom' ], jPath[ 'cDateTo' ],,,)
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)
Return


/*/{Protheus.doc} ru44w120_team
Get allocation for a team, grouped by resource
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/team/:sessionId/:cDateFrom/:cDateTo/:team")
Function ru44w120_team()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath := oRest:getPathParamsRequest()

	If (jPath <> Nil )
		jResponse := GetAlloc(GROUP_RES, jPath[ 'sessionId' ], jPath[ 'cDateFrom' ], jPath[ 'cDateTo' ], jPath[ 'team' ],,)
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)
Return


/*/{Protheus.doc} ru44w120_resource
Get allocation for a resource
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/resource/:sessionId/:cDateFrom/:cDateTo/:resource")
Function ru44w120_resource()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath := oRest:getPathParamsRequest()

	If (jPath <> Nil )
		jResponse := GetAlloc(GROUP_NO, jPath[ 'sessionId' ], jPath[ 'cDateFrom' ], jPath[ 'cDateTo' ],,jPath[ 'resource' ],)
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)
Return

/*/{Protheus.doc} ru44w120_tp_full
Get allocation cube by type grouped by team
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/tp_full/:sessionId/:cDateFrom/:cDateTo")
Function ru44w120_tp_full()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath := oRest:getPathParamsRequest()

	If (jPath <> Nil )
		jResponse := GetAlloc(GROUP_TYPE_TEAM, jPath[ 'sessionId' ], jPath[ 'cDateFrom' ], jPath[ 'cDateTo' ],,,)
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)
Return

/*/{Protheus.doc} ru44w120_tp_team
Get allocation cube by type grouped by resource for a team
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/tp_team/:sessionId/:cDateFrom/:cDateTo/:type/:team")
Function ru44w120_tp_team()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath := oRest:getPathParamsRequest()

	If (jPath <> Nil )
		jResponse := GetAlloc(GROUP_TYPE_RES, jPath[ 'sessionId' ], jPath[ 'cDateFrom' ], jPath[ 'cDateTo' ],jPath[ 'team' ],,jPath[ 'type' ])
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)
Return


/*/{Protheus.doc} ru44w120_tp_res
Get allocation cube by type grouped by resource for a team
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/tp_res/:sessionId/:cDateFrom/:cDateTo/:type/:team")
Function ru44w120_tp_res()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath := oRest:getPathParamsRequest()

	If (jPath <> Nil )
		jResponse := GetAlloc(GROUP_NO, jPath[ 'sessionId' ], jPath[ 'cDateFrom' ], jPath[ 'cDateTo' ],,jPath[ 'team' ],jPath[ 'type' ])
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)
Return

/*/{Protheus.doc} ru44w120_tp_3full
Get allocation full
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/tp_3full/:sessionId/:cDateFrom/:cDateTo")
Function ru44w120_tp_3full()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jQuery    := jsonObject():New() As Json
	Local cTeam
	Local cResource
	Local cType
	Local cProject
	Local cCostColl
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath  := oRest:getPathParamsRequest()
	jQuery := oRest:GetQueryRequest()

	If ( jQuery <> Nil )
		cTeam := jQuery[ 'team' ]
		cResource := jQuery[ 'resource' ]
		cType := jQuery[ 'type' ]
		cProject := jQuery[ 'project' ]
		cCostColl := jQuery[ 'COSTCOLLECTOR' ]
	EndIf

	If (jPath <> Nil )
		jResponse := GetAlloc(GROUP_NO_ALL, jPath[ 'sessionId' ], jPath[ 'cDateFrom' ], jPath[ 'cDateTo' ], cTeam, cResource, cType/*, cProject, cCostColl*/)
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)
Return

/*/{Protheus.doc} ru44w120_ts_full
Get timesheet by type of task
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/ts_full/:sessionId/:cDateFrom/:cDateTo")
Function ru44w120_ts_full()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath := oRest:getPathParamsRequest()

	If (jPath <> Nil )
		jResponse := GetAFU(1, jPath[ 'sessionId' ], jPath[ 'cDateFrom' ], jPath[ 'cDateTo' ],,,)
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)
Return


/*/{Protheus.doc} ru44w120_ts_2full
Get timesheet by date
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/ts_2full/:sessionId/:cDateFrom/:cDateTo")
Function ru44w120_ts_2full()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath := oRest:getPathParamsRequest()

	If (jPath <> Nil )
		jResponse := GetAFU(2, jPath[ 'sessionId' ], jPath[ 'cDateFrom' ], jPath[ 'cDateTo' ],,,)
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)
Return


/*/{Protheus.doc} ru44w120_ts_3full
Get timesheet by type of task, resource, project or team
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/ts_3full/:cDateFrom/:cDateTo")
Function ru44w120_ts_3full()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jQuery    := jsonObject():New() As Json
	Local cTeam
	Local cResource
	Local cType
	Local cProject
	Local cCostColl
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath  := oRest:getPathParamsRequest()
	jQuery := oRest:GetQueryRequest()

	If ( jQuery <> Nil )
		cTeam := jQuery[ 'team' ]
		cResource := jQuery[ 'resource' ]
		cType := jQuery[ 'type' ]
		cProject := jQuery[ 'project' ]
		cCostColl := jQuery[ 'COSTCOLLECTOR' ]
	EndIf

	If (jPath <> Nil )
		jResponse := GetAFU(3,, jPath[ 'cDateFrom' ], jPath[ 'cDateTo' ], cTeam, cResource, cType, cProject, cCostColl)
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)
Return

/*/{Protheus.doc} ru44w120_det_tasks
Get detaileds tasks, delayed, closed or  closed with delay from a date
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/det_tasks/:cTipo")
Function ru44w120_det_tasks()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jQuery    := jsonObject():New() As Json
	Local cTeam
	Local cResource
	Local cType
	Local cProject
	Local cCostColl
	Local cDaysRef
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath  := oRest:getPathParamsRequest()
	jQuery := oRest:GetQueryRequest()

	If ( jQuery <> Nil )
		cTeam := jQuery[ 'team' ]
		cResource := jQuery[ 'resource' ]
		cType := jQuery[ 'type' ]
		cProject := jQuery[ 'project' ]
		cCostColl := jQuery[ 'COSTCOLLECTOR' ]
		cDaysRef := jQuery[ 'DaysRef' ]
	EndIf

	If (jPath <> Nil )
		jResponse := GetTsk(cResource, cTeam, cProject, cType, jPath[ 'cTipo' ], cDaysRef, cCostColl)
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)
Return

/*/{Protheus.doc} ru44w120_tskgnt
Get detailed open tasks, with POC, in a starting date period to use in GANTT
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/tskgnt/:cDateFrom/:cDateTo")
Function ru44w120_tskgnt()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jQuery    := jsonObject():New() As Json
	Local cTeam
	Local cResource
	Local cType
	Local cProject
	Local cCostColl
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath  := oRest:getPathParamsRequest()
	jQuery := oRest:GetQueryRequest()

	If ( jQuery <> Nil )
		cTeam := jQuery[ 'team' ]
		cResource := jQuery[ 'resource' ]
		cType := jQuery[ 'type' ]
		cProject := jQuery[ 'project' ]
		cCostColl := jQuery[ 'COSTCOLLECTOR' ]
	EndIf

	If (jPath <> Nil )
		jResponse := GetTskGnt(cResource, cTeam, cProject, cType, cCostColl, jPath[ 'cDateFrom' ], jPath[ 'cDateTo' ])
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)
Return

/*/{Protheus.doc} ru44w120_ts_total
Get timesheet total by resource
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/ts_total/:cDateFrom/:cDateTo")
Function ru44w120_ts_total()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath := oRest:getPathParamsRequest()

	If (jPath <> Nil )
		jResponse := GetTsTotal(jPath[ 'cDateFrom' ], jPath[ 'cDateTo' ])
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)
Return


/*/{Protheus.doc} ru44w120_alloc_an
Get allocation analysis
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
/*/
	@Get("/api/pms/v1/ru44w120/alloc_an/:cDateFrom/:cDateTo/:nType")
Function ru44w120_alloc_an()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jQuery    := jsonObject():New() As Json
	Local cTeam
	Local cResource
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath := oRest:getPathParamsRequest()
	jQuery := oRest:GetQueryRequest()

	If ( jQuery <> Nil )
		cTeam := jQuery[ 'team' ]
		cResource := jQuery[ 'resource' ]
	EndIf

	If (jPath <> Nil )
		jResponse := RecAnalisys(Stod(jPath[ 'cDateFrom' ]), Stod(jPath[ 'cDateTo' ]), jPath[ 'nType' ], cTeam, cResource)
	EndIf

	oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
	oRest:setResponse(jResponse)
Return


/*/{Protheus.doc} SX5
Get data from SX5
@type function
@author Fernando Nicolau
@since 23/08/2023
/*/
	@Get("/api/pms/v1/ru44w120/sx5/:table")
Function ru44w120_sx5()

	Local jResponse as Json
	Local jPath     as Json
	Local jQuery    := jsonObject():New() As Json
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf

	jPath  := oRest:getPathParamsRequest()
	jQuery := oRest:GetQueryRequest()

	jPath := oRest:getPathParamsRequest()

	If (jPath <> Nil)
		jResponse := GetSX5(jPath[ 'table' ])
	EndIf

	If jResponse <> Nil
		oRest:SetKeyHeaderResponse('Content-Type', 'application/json')
		oRest:setResponse(jResponse)
	EndIf
Return

/*/{Protheus.doc} GetMD
Get master data from PMS
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
@param cMD, character, Master data
@Return Json, Json Object
/*/
Static Function GetMD_V2(cMD,cKey) as Json

	Local aAuxData  := {} As Array
	Local jJsonAux As Json
	Local jJsonRet As Json
	Local cFieldFilial as character 
	Local cIdField 	 := ""
	Local cLabelField:= ""
	cMD := Upper(cMD)

	Do Case
	Case cMD == "AEA"
		cIdField 	:= "AEA_COD"
		cLabelField := "AEA_DESCRI"
	EndCase

	If !Empty(cIdField)
		//cQuery := " SELECT "+cIdField+
		cFieldFilial := IIf(substr(cMD,1,1)=="S", Substr(cMD,2,2), cMD) + "_FILIAL"
		DbSelectArea(cMD)
		DbSetOrder(1)
		DbSeek(xFilial(cMD) + Iif(!Empty(cKey), cKey,''))
		While !EOF() .And. xFilial(cMD) == (cMD)->(FieldGet(FieldPos(cFieldFilial))) .And.;
			IIf(Empty(cKey),.T.,(cMD)->(FieldGet(FieldPos(cFieldFilial))) == cKey)
			jJsonAux :=  {cIdField: AllTrim((cMD)->(FieldGet(FieldPos(cIdField)))), cLabelField: AllTrim((cMD)->(FieldGet(FieldPos(cLabelField))))}
			aAdd(aAuxData, jJsonAux)
			AEA->(DbSkip())
		End
		jsonRet['data'] := aAuxData
		jsonRet['sucess'] := .T.
	Else 
		jsonRet['data'] := {}
		jsonRet['sucess'] := .F.
		jsonRet['message'] := 'Master data not allowed to be queried'
	Endif
	
Return jJsonRet
/*/{Protheus.doc} GetMD
Get master data from PMS
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
@param cMD, character, Master data
@Return Json, Json Object
/*/
Static Function GetMD(cMD) as Json

	Local nX        := 0  As Numeric
	Local aCombo    := {} As Array
	Local aRetCab   := {} As Array
	Local aRetDados := {} As Array
	Local aReturn   := {} As Array
	Local aAuxData  := {} As Array
	Local jJsonAux As Json
	Local jJsonRet As Json

	cMD := Upper(cMD)

	aRetCab   := {}
	aRetDados := {}
	aReturn   := {}

	If cMD == "AE8" .Or. cMD == "ALL"

		aAuxData := {}

		jJsonAux := {"key": "AE8"}
		aAdd(aRetCab, jJsonAux)

		DbSelectArea('AE8')
		AE8->(DbSetOrder(1))
		AE8->(DbSeek(xFilial("AE8")))
		While xFilial("AE8") == AE8->AE8_FILIAL
			jJsonAux :=  {"id": AllTrim(AE8->AE8_RECURS), "label": AllTrim(AE8->AE8_DESCRI)}
			aAdd(aAuxData, jJsonAux)
			AE8->(DbSkip())
		End

		aAdd(aRetDados, aAuxData)
	EndIf

	If cMD == "AE5" .Or. cMD == "ALL"

		aAuxData := {}

		jJsonAux := {"key": "AE5"}
		aAdd(aRetCab, jJsonAux)

		DbSelectArea('AE5')
		AE5->(DbSetOrder(1))
		AE5->(DbSeek(xFilial("AE5")))
		While xFilial("AE5") == AE5->AE5_FILIAL
			jJsonAux :=  {"id": AllTrim(AE5->AE5_GRPCOM), "label": AllTrim(AE5->AE5_DESCRI)}
			aAdd(aAuxData, jJsonAux)
			AE5->(DbSkip())
		End

		aAdd(aRetDados, aAuxData)
	EndIf

	If cMD == "AF8" .Or. cMD == "ALL"
		aAuxData := {}
		jJsonAux := {"key": "AF8"}
		aAdd(aRetCab, jJsonAux)
		DbSelectArea('AF8')
		AF8->(DbSetOrder(1))
		AF8->(DbSeek(xFilial("AF8")))
		While xFilial("AF8") == AF8->AF8_FILIAL
			jJsonAux :=  {"id": AllTrim(AF8->AF8_PROJET), "label": AllTrim(AF8->AF8_DESCRI)}
			aAdd(aAuxData, jJsonAux)
			AF8->(DbSkip())
		End

		aAdd(aRetDados, aAuxData)
	EndIf

	If cMD == "AED" .Or. cMD == "ALL"
		aAuxData := {}
		jJsonAux := {"key": "AED"}
		aAdd(aRetCab, jJsonAux)
		DbSelectArea('AED')
		AED->(DbSetOrder(1))
		AED->(DbSeek(xFilial("AED")))
		While xFilial("AED") == AED->AED_FILIAL
			jJsonAux :=  {"id": AllTrim(AED->AED_EQUIP), "label": AllTrim(AED->AED_DESCRI)}
			aAdd(aAuxData, jJsonAux)
			AED->(DbSkip())
		End

		aAdd(aRetDados, aAuxData)
	EndIf

	If cMD == "SX5FE" .Or. cMD == "ALL"
		aAuxData := {}
		jJsonAux := {"key": "SX5FE"}
		aAdd(aRetCab, jJsonAux)
		DbSelectArea('SX5')
		SX5->(DbSetOrder(1))
		SX5->(DbSeek(xFilial("SX5") + 'FE'))
		While xFilial("SX5") == SX5->X5_FILIAL .And. SX5->X5_TABELA == 'FE'
			jJsonAux :=  {"id": AllTrim(SX5->X5_CHAVE), "label": AllTrim(X5Descri())}
			aAdd(aAuxData, jJsonAux)
			SX5->(DbSkip())
		End

		aAdd(aRetDados, aAuxData)
	EndIf

	If cMD == "AJ8" .Or. cMD == "ALL"
		aAuxData := {}
		jJsonAux := {"key": "AJ8"}
		aAdd(aRetCab, jJsonAux)
		AJ8->(DbSelectArea('AJ8'))
		AJ8->(DbSetOrder(1))
		AJ8->(DbSeek(xFilial("AJ8")))
		While xFilial("AJ8") == AJ8->AJ8_FILIAL
			If AllTrim(AJ8->AJ8_CTASUP) == ''
				jJsonAux :=  {"id": AllTrim(AJ8->AJ8_CODPLA), "label": AllTrim(AJ8->AJ8_DESCCG)}
				aAdd(aAuxData, jJsonAux)
			EndIf
			AJ8->(DbSkip())
		End

		aAdd(aRetDados, aAuxData)
	EndIf

	If Left(cMD, 8) == 'COMBOSX3'
		aAuxData := {}
		jJsonAux := {"key": cMD}
		aAdd(aRetCab, jJsonAux)
		DbSelectArea('SX3')
		SX3->(DbSetOrder(2))
		aCombo	:=	{}
		If SX3->(DbSeek(Substr(cMD, 9, 10))) .And. !Empty(SX3->X3_CBOX)
			aCombo	:=	RetSX3Box(x3cbox(),,, SX3->X3_TAMANHO)
			For nX:=1 To Len(aCombo)
				jJsonAux :=  {"id": AllTrim(aCombo[nx,2]), "label": AllTrim(aCombo[nX,3])}
				aAdd(aAuxData, jJsonAux)
			Next
		EndIf

		aAdd(aRetDados, aAuxData)
	EndIf

	aAdd(aReturn, aRetCab)
	For nX := 1 To Len(aRetDados)
		aAdd(aReturn, aRetDados[nX])
	Next

	jJsonRet := jsonObject():New()
	jJsonRet:Set(aReturn)

Return jJsonRet

/*/{Protheus.doc} GetAlloc
Get Allocation data
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
@param nTipoRet, numeric, param_description
@param cSessionID, character, param_description
@param cDateFrom, character, param_description
@param cDateTo, character, param_description
@param cTeam, character, param_description
@param cRes, character, param_description
@param cTypeTsk, character, param_description
@Return Json, Json Object
/*/
Static Function GetAlloc(nTipoRet, cSessionID, cDateFrom, cDateTo, cTeam, cRes, cTypeTsk)

	Local cAlias    := GetNextAlias()
	Local cAliasTmp := GetNextAlias()
	Local cResFault	:=""
	Local cSql      := ""
	Local nX        := 0
	Local aRes      := {}
	Local cRet      := ""
	Local oFWTMP	//FWTemporaryTable():New(cAliasTMP,aFields )
	Local aFields	:=	{{"TOT_AL_RES", "N", 6,  2},;
		{"TOT_AV_RES" , "N", 6,  2},;
		{"AL_PER_RES" , "N", 6,  2},;
		{"TOT_AL_TSK" , "N", 6,  2},;
		{"TOT_DE_TSK" , "N", 14, 2},;
		{"DATEINI"	  , "C", 8,  0},;
		{"DATETO"	  , "C", 8,  0},;
		{"DATECACHE"  , "C", 14, 0},;
		{"SESSIONID"  , "C", 32, 0},;
		{"TOT_AL_TEA" , "N", 6,  2},;
		{"TOT_AV_TEA" , "N", 6,  2},;
		{"AL_PER_TEA" , "N", 6,  2}}
	Local cFields       := ""
	Local cFieldsAdic	:= ""
	Local nTimeDelay	:= 0
	Local nDoneHours    := 0
	Local nHoursBefore  := 0
	Local nPercCompl    := 0
	Local jJsonRet

	Local cInsert := ""
	Local cSelect := ""

	Local lAE8CalStd := AE8->(ColumnPos("AE8_CALSTD")) > 0
	Local lAE8Start  := AE8->(ColumnPos("AE8_START")) > 0
	Local lAE8Finish := AE8->(ColumnPos("AE8_FINISH")) > 0

	For nx:=1 To Len(aFields)
		cFieldsAdic += ","+aFIELDS[nX,1]
	Next
//nTipoRet
//0=NO GROUP
//1=Group by team
//2=Group By resource
//3=Group by  TEAM + TYPE


//Adjust periods to date
	cDateFrom += "01"
	cDateTo	:= DtoS(LastDay(Stod(cDateTo + "01")))

	cFields	:= " AF9_HORAI, AF9_HORAF, AF9_FINISH, AF9_START, AF9_DESCRI, AF9_TAREFA, AF9_CALEND, AF9_DTATUF, "
	cFields	+= " AF8_PROJET, AF8_DESCRI, AF8_REVISA, "
	cFields	+= " AE8_RECURS, AE8_DESCRI, AE8_UMAX, "

	If lAE8CalStd .And. lAE8Start .And. lAE8Finish
		cFields	+= " AE8_CALSTD, AE8_START, AE8_FINISH, "
	EndIf

	cFields	+= " AFA_QUANT, "
	cFields	+= " AED_EQUIP, AED_DESCRI,"
	cFields	+= " AE5_GRPCOM,AE5_DESCRI "

	SX3->(DBSETORDER(2))
	aFLDtMP	:=	Strtokarr2( cFields, ",", .F.)
	For nX := 1 To len(aFLDtMP)
		If !Empty(aFLDtMP[nX]) .and. SX3->(Dbseek(AllTrim(aFLDtMP[nX])))
			aAdd(aFIELDS, {SX3->X3_CAMPO, SX3->X3_TIPO, SX3->X3_TAMANHO, SX3->X3_DECIMAL})
		ElseIf !Empty(aFLDtMP[nX]) .and. !SX3->(Dbseek(AllTrim(aFLDtMP[nX])))
			aAdd(aFIELDS, {aFLDtMP[nX], "C", 8, 0})
		Else
			cc := ""
		EndIf
	Next nX
	oFWTMP	:=	FWTemporaryTable():New(cAliasTMP, aFIELDS )
	oFWTMP:Create()
	cSqlTemp	:=	oFWTMP:GetRealName()

	While !LockByName('NPWEBREST001' + cDateFrom + cDateTO, .T., .T.)

		Sleep(500)
	Enddo

	TCrefresh("PMSNP001")
	If !TCCanOpen("PMSNP001")
		//Monta estrutura da tabela de historico das execucoes
		TCInternal(30, 'AUTORECNO')
		dbCreate("PMSNP001", aFields, "TOPCONN")
		TCInternal(30, 'OFF')
		TcSqlExec("CREATE INDEX PMSNP001_IDX1 ON PMSNP001 (SESSIONID,DATEINI,DATETO)")
		cError	:=	TCSQLError()
		If !Empty(cError)
			cResFault += CRLF + cError
		EndIf
		TcSqlExec("CREATE INDEX PMSNP001_IDX2 ON PMSNP001 (DATECACHE)")
		cError	:=	TCSQLError()
		If !Empty(cError)
			cResFault += CRLF + cError
		EndIf

		Sleep(1000)
		nConta	:=	0
	Else
		//Delete old cache from table
		TcSqlExec("DELETE FROM  PMSNP001 WHERE DATECACHE < '"+FWTimeStamp(1) /* DtoS(DATE()) */+"'")
		cQuery := "SELECT COUNT(*) conta from PMSNP001 WHERE DATEINI = '" + cDateFrom + "' AND DATETO = '" + cDateTo + "' AND SESSIONID = '" + cSessionID + "' "
		cQuery := ChangeQuery(cQuery)
		dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAliasTmp+"1", .F., .T.)
		nConta  := CONTA
		DbCloseArea()
	EndIf

	If nCONTA ==0
		cSql := " ( ( AF9_START >= '" + cDateFrom + "' "
		cSql += "      AND AFA_START <= '" + cDateTo + "' ) "
		cSql += "     OR ( AF9_FINISH >= '" + cDateFrom + "' "
		cSql += "         AND AF9_FINISH <= '" + cDateTo + "' ) "
		cSql += "     OR ( AF9_START < '" + cDateFrom + "' "
		cSql += "         AND AF9_FINISH > '" + cDateTo + "' ) "
		cSql += "    		 )"
		//Ignore delayed tasks
		/*
		cSql += "     OR  ( AF9_START < '"+cDateFrom+"'  "
		cSql += "    		AND AF9_DTATUF = '') )"
*/
		cInsert	:=	"INSERT INTO " + cSqlTemp + " (DATECACHE, DATEINI, DATETO, SESSIONID, " + cFields + ") "
		//cInsert	+=	" ( "
		cSelect	:=	" SELECT '" + FWTimeStamp(1) /* DtoS(DATE()) */+ "' AS DATECACHE, '" + cDateFrom + "' AS DATEINI, '" + cDateTO + "' AS DATETO, '" + cSessionID + "' AS SESSIONID, " + cFields
		cSelect	+=	" FROM " + RetSqlName('AFA') + " AFA, " + RetSqlName('AE5') + " AE5, " + RetSqlName('AF8') + " AF8, " + RetSqlName('AF9') + " AF9, " + RetSqlName('AE8') + " AE8 LEFT OUTER JOIN " + RetSqlName('AED') + " AED ON "
		cSelect	+=	"	AE8_EQUIP = AED_EQUIP		"
		cSelect	+=	"	WHERE 	AFA_FILIAL = '" + xFilial('AFA') + "' AND AFA.d_e_l_e_t_ = ' ' AND "
		cSelect	+=	"				AF8_FILIAL = '" + xFilial('AF8') + "' AND AF8.d_e_l_e_t_ = ' ' AND "
		cSelect	+=	"				AF9_FILIAL = '" + xFilial('AF9') + "' AND AF9.d_e_l_e_t_ = ' ' AND "
		cSelect	+=	"				AE8_FILIAL = '" + xFilial('AE8') + "' AND AE8.d_e_l_e_t_ = ' ' AND "
		cSelect	+=	"				AE5_FILIAL = '" + xFilial('AE5') + "' AND AE5.d_e_l_e_t_ = ' ' AND "
		cSelect	+=	"				COALESCE(AED_FILIAL, '" + xFilial('AED') + "') = '" + xFilial('AED') + "' AND "
		cSelect	+=	"				COALESCE(AED.D_E_L_E_T_ , ' ') = ' ' AND "
		cSelect	+=	"				AFA_PROJET = AF8_PROJET AND "
		cSelect	+=	"				AFA_PROJET = AF9_PROJET AND "
		cSelect	+=	"				AFA_TAREFA = AF9_TAREFA AND "
		cSelect	+=	"				AFA_REVISA = AF8_REVISA AND "
		cSelect	+=	"				AF9_REVISA = AF8_REVISA AND "
		cSelect	+=	"				AF9_GRPCOM = AE5_GRPCOM AND "
		cSelect	+=	"				AFA_RECURS = AE8_RECURS AND "

		If lAE8CalStd .And. lAE8Start .And. lAE8Finish
			cSelect	+=	"				AE8_START  <= '" + cDateTo + "' AND "
			cSelect	+=	"				(AE8_FINISH = '' OR AE8_FINISH >= '" + cDateFrom + "') AND "
		EndIf

		cSelect	+=	cSql
		cSelect	+=	"	ORDER BY AED_EQUIP, AFA_RECURS, AE5_GRPCOM, AF9_START, AF9_HORAI, AF8_PROJET, AF8_REVISA, AF9_TAREFA  "

		cSelect := ChangeQuery(cSelect)

		cInsert := cInsert + cSelect

		tcSqlExec(cInsert)
		cError	:=	TCSQLError()
		If !Empty(cError)
			cResFault += CRLF + cError
		EndIf

		cInsert	:=	"INSERT INTO " + cSqlTemp + " (DATECACHE, DATEINI, DATETO, SESSIONID, AE8_RECURS, AE8_DESCRI, AE8_UMAX, AED_EQUIP, AED_DESCRI
		If lAE8CalStd
			cInsert	+= ", AE8_CALSTD "
		EndIf
		cInsert	+=  ") "
		// cInsert	+=	"("
		cSelect	:=	" SELECT '" + FWTimeStamp(1) /* DtoS(DATE()) */+ "', '" + cDateFrom + "', '" + cDateTO + "', '" + cSessionID + "', AE8_RECURS, AE8_DESCRI, AE8_UMAX, AED_EQUIP, AED_DESCRI "

		If lAE8CalStd
			cSelect	+=	", AE8_CALSTD "
		EndIf

		cSelect	+=	" FROM " + RetSqlName('AE8') + " AE8 LEFT OUTER JOIN " + RetSqlName('AED') + " AED ON "
		cSelect	+=	"	AE8_EQUIP = AED_EQUIP "

		cSelect	+=	"	WHERE AE8_FILIAL = '" + xFilial('AE8') + "' AND AE8.d_e_l_e_t_ = ' ' AND "
		cSelect	+=	"		AE8_RECURS NOT IN (SELECT DISTINCT AE8_RECURS FROM " + cSqlTemp + ") AND "
		cSelect	+=	"				COALESCE(AED_FILIAL, '" + xFilial('AED') + "') = '" + xFilial('AED') + "' AND "
		cSelect	+=	"				COALESCE(AED.D_E_L_E_T_ , ' ') = ' ' AND "

		If lAE8CalStd .And. lAE8Start .And. lAE8Finish
			cSelect	+=	"				AE8_START  <= '" + cDateTo + "' AND "
			cSelect	+=	"				(AE8_FINISH = '' OR AE8_FINISH >= '" + cDateFrom + "') AND "
		EndIf

		cSelect	+=	"				AE8_USER <> ' ' "
		// cInsert	+=  ")"

		cSelect := ChangeQuery(cSelect)

		cInsert := cInsert + cSelect

		tcSqlExec(cInsert)
		cError	:=	TCSQLError()
		If !Empty(cError)
			cResFault	+= CRLF+cError
		EndIf

		dbSelectArea(cAliasTMP)
		DbGotop()
		//Update task time in period selected
		dDateFrom   := Stod(cDateFrom)
		dDateTo     := Stod(cDateTo)
		aTimeCalc	:=	{}
		While !EoF()
			nHoursBefore	:=	0
			nTimeDelay		:=	0
			If AFA_QUANT > 0
				If AF9_START < dDateFrom .and. Empty(AF9_DTATUF)
					If (nPosT:=Ascan(aTimeCalc,{|x| x[1]==DtoS(AF9_START)+AF9_HORAI+DtoS(Min(dDateFrom-1,AF9_FINISH))+AF9_HORAF+AF9_CALEND+AF8_PROJET}))>0
						nHoursBefore:=	aTimeCalc[nPost,2]
					Else
						nHoursBefore	:=	PmsHrsItv2(AF9_START,AF9_HORAI,Min(dDateFrom-1,AF9_FINISH),AF9_HORAF,AF9_CALEND,AF8_PROJET)
						aAdd(aTimeCalc,{DtoS(AF9_START)+AF9_HORAI+DtoS(Min(dDateFrom-1,AF9_FINISH))+AF9_HORAF+AF9_CALEND+AF8_PROJET,nHoursBefore})
					EndIf
				EndIf

				If (nPosT:=Ascan(aTimeCalc,{|x| x[1]==DtoS(Max(dDateFrom,AF9_START))+AF9_HORAI+DtoS(Min(dDateTo,AF9_FINISH))+AF9_HORAF+AF9_CALEND+AF8_PROJET}))>0
					nTimePer:=	aTimeCalc[nPost,2]
				Else
					nTimePer	:=	PmsHrsItv2(Max(dDateFrom,AF9_START),AF9_HORAI,Min(dDateTo,AF9_FINISH),AF9_HORAF,AF9_CALEND,AF8_PROJET)
					aAdd(aTimeCalc,{DtoS(Max(dDateFrom,AF9_START))+AF9_HORAI+DtoS(Min(dDateTo,AF9_FINISH))+AF9_HORAF+AF9_CALEND+AF8_PROJET,nTImePer})
				EndIf


				If (nPosT:=Ascan(aTimeCalc,{|x| x[1]==DtoS(AF9_START)+AF9_HORAI+DtoS(AF9_FINISH)+AF9_HORAF+AF9_CALEND+AF8_PROJET}))>0
					nTimeTot:=	aTimeCalc[nPost,2]
				Else
					nTimeTot	:=	PmsHrsItv2(AF9_START,AF9_HORAI,AF9_FINISH,AF9_HORAF,AF9_CALEND,AF8_PROJET)
					aAdd(aTimeCalc,{DtoS(AF9_START)+AF9_HORAI+DtoS(AF9_FINISH)+AF9_HORAF+AF9_CALEND+AF8_PROJET,nTimeTot})
				EndIf

				If nTimeTot == 0
					nTimeTOT	:=	nTimePer
				EndIf
				nTimeAloc	:=	AFA_QUANT
				nTimeTsk		:=	nTimeAloc* (nTimePer/nTimeTot)
				If nHoursBefore > 0
					nPercCompl	:=	PmsPOCAF9(AF8_PROJET,AF8_REVISA,AF9_TAREFA)/100
					nDoneHours	:=	nPercCompl * nTimeTot
					//nDelayPerc	:= nTimeDelay/nTimeTot
					nTimeDelay	:=	nHoursBefore - nDoneHours

					nTimeDelay	:=	(AFA_QUANT/ nTimeTOT) * nTimeDelay
					If nTimeDelay < 0
						nTimeDelay :=	0
					EndIf
				EndIf
				//aAdd(aTasks,{AF9_PROJET,AF9_TAREFA,nTimeTsk})
				RecLock(cAliasTMP,.F.)
				TOT_AL_TSK	+=	nTimeTsk
				TOT_DE_TSK	+=	nTimeDelay
				MsUnLock()
			EndIf
			If Ascan(aRes,{|x| x[1]==AE8_RECURS}) == 0
				aAdd(aRes,{AE8_RECURS,AE8_CALSTD,AED_EQUIP,AE8_START,Iif(empty(AE8_FINISH),ddateto,AE8_FINISH)})
			EndIf
			DbSelectArea(cAliasTMP)
			DbSkip()
		Enddo
		//UPDATE RESOURCE ALLOCATION
		//cUpdate	:= 'UPDATE '+cSqlTemp+ " A SET TOT_AL_RES= (SELECT SUM(TOT_AL_TSK) FROM "+cSqlTemp+" B WHERE A.AE8_RECURS=B.AE8_RECURS ) "
		//tcSqlExec(cUpdate)
		//UPDATE TEAM ALLOCATION
		//	cUpdate	:= 'UPDATE '+cSqlTemp+ " A SET TOT_AL_TEA= (SELECT SUM(TOT_AL_TSK) FROM "+cSqlTemp+" B WHERE A.AED_EQUIP=B.AED_EQUIP ) "
		//	tcSqlExec(cUpdate)

		//Update resorces availability in period selected
		For nX:=1 To Len(aRes)

			nTimeRes	:=	PmsHrsItv2(MAX(dDateFrom,ARES[nX,4]),"00:00",Min(dDateTo,aRes[nX,5]),"24:00",IIf(aRes[nX,2]<>"",aRes[nX,2],'001'),,aRes[nX,1])
			tcSqlExec('UPDATE '+cSqlTemp+ " SET TOT_AV_RES="+STR(nTimeRes)+" WHERE AE8_RECURS='"+aRes[nX,1]+"'")
			cError	:=	TCSQLError()
			If !Empty(cError)
				cResFault	+= CRLF+cError
			EndIf
			tcSqlExec('UPDATE '+cSqlTemp+ " SET TOT_AV_TEA=TOT_AV_TEA+"+STR(nTimeRes)+" WHERE AED_EQUIP='"+aRes[nX,3]+"'")
			cError	:=	TCSQLError()
			If !Empty(cError)
				cResFault	+= CRLF+cError
			EndIf
		Next nX

		//UPDATE RESOURCE AVAILABILITY
		cUpdate	:= 'UPDATE '+cSqlTemp+ " A SET AL_PER_RES=( CASE WHEN TOT_AV_RES=0 THEN 0 ELSE TOT_AL_RES/TOT_AV_RES END  )*100 "
		tcSqlExec(cUpdate)
		cError	:=	TCSQLError()
		If !Empty(cError)
			cResFault	+= CRLF+cError
		EndIf
		//UPDATE TEAM AVAILABILITY
		cUpdate	:= 'UPDATE '+cSqlTemp+ " A SET AL_PER_TEA=( CASE WHEN TOT_AV_TEA=0 THEN 0 ELSE TOT_AL_TEA/TOT_AV_TEA END  )*100 "
		tcSqlExec(cUpdate)
		cError	:=	TCSQLError()
		If !Empty(cError)
			cResFault	+= CRLF+cError
		EndIf

		//UPDATE EMPTY ALOCATION
		cUpdate	:= 'UPDATE '+cSqlTemp+ " A SET AF9_DESCRI='NO ALLOCATION' WHERE AF9_TAREFA = '' "
		tcSqlExec(cUpdate)
		cError	:=	TCSQLError()
		If !Empty(cError)
			cResFault	+= CRLF+cError
		EndIf

		//tcsqlExec('DROP TABLE u_pmsnp')
		tcsqlExec('INSERT INTO PMSNP001 ('+cFields+cFieldsAdic+') (SELECT '+cFields+cFieldsAdic+' FROM '+cSqlTemp+')')
		cError	:=	TCSQLError()
		If !Empty(cError)
			cResFault	+= CRLF+cError
		EndIf
		DbCloseArea()
		oFWTMP:Delete()
	EndIf
//Release Lock
	UnLockByName('NPWEBREST001'+cDateFrom+cDateTO)

	cSQL := ""
	If cTeam <> Nil
		cSQL += "AND AED_EQUIP = '"+cTeam+"' "
	EndIf
//Dont list managers
	cSQL += "AND AED_EQUIP <> 'M001' "

	If cRes<> Nil
		cSql += "AND AE8_RECURS = '"+cRES+"' "
	EndIf

	If cTypeTsk<> Nil
		cSql += "AND AE5_GRPCOM = '"+cTypeTsk+"' "
	EndIf

	cWhere	:=	"% WHERE DATEINI='"+cDateFrom+"' AND DATETO = '"+cDateTo+"' AND SESSIONID='"+cSessionID+"' "+cSql+" %"

	If nTipoRet == GROUP_RES
		BeginSql alias cAlias
		SELECT AE8_RECURS,AE8_DESCRI, AED_EQUIP, AED_DESCRI, Sum(TOT_DE_TSK) TOT_DE_TSK, Sum(TOT_AL_TSK) TOT_AL_TSK,  MAX(TOT_AV_RES) TOT_AV_RES  from 	PMSNP001
		%Exp:cWhere%
		GROUP by AE8_RECURS,AE8_DESCRI, AED_EQUIP, AED_DESCRI
		ORDER BY AED_EQUIP,AE8_RECURS
		EndSql
		While !EoF()
			cRet  += ',{"resource":"'+AllTrim(AE8_RECURS)+'","resourceDescription":"'+AllTrim(AE8_DESCRI)+'",'+;
				'"team":"'+AllTrim(AED_EQUIP)+'","teamDescription":"'+AllTrim(AED_DESCRI)+'",'+;
				'"delay":'+AllTrim(sTR(TOT_DE_TSK))+',"allocation":'+AllTrim(sTR(TOT_AL_TSK))+',"availability":'+AllTrim(sTR(TOT_AV_RES))+',"percent":'+If(TOT_AV_RES<> 0,AllTrim(sTR(TOT_AL_TSK/TOT_AV_RES)),'0')+'}'
			DbSkip()
		Enddo
	ElseIf nTipoRet == GROUP_TYPE_RES
		BeginSql alias cAlias
		SELECT AE8_RECURS,AE8_DESCRI,AE5_GRPCOM,AE5_DESCRI, Sum(TOT_DE_TSK) TOT_DE_TSK, Sum(TOT_AL_TSK) TOT_AL_TSK,  MAX(TOT_AV_RES) TOT_AV_RES  from 	PMSNP001
		%Exp:cWhere%
		GROUP by AE8_RECURS,AE8_DESCRI,AE5_GRPCOM,AE5_DESCRI
		ORDER BY AE8_RECURS
		EndSql
		While !EoF()
			cRet  += ',{"type":"'+AllTrim(AE5_GRPCOM)+'","typeDescription":"'+AllTrim(AE5_DESCRI)+'","resource":"'+AllTrim(AE8_RECURS)+'","description":"'+AllTrim(AE8_DESCRI)+'","delay":'+AllTrim(sTR(TOT_DE_TSK))+',"allocation":'+AllTrim(sTR(TOT_AL_TSK))+',"availability":'+AllTrim(sTR(TOT_AV_RES))+',"percent":'+If(TOT_AV_RES<> 0,AllTrim(sTR(TOT_AL_TSK/TOT_AV_RES)),'0')+'}'
			DbSkip()
		Enddo
	ElseIf nTipoRet == GROUP_TEAM
		BeginSql alias cAlias
		SELECT AED_EQUIP,AED_DESCRI, Sum(TOT_AL_TSK) TOT_AL_TSK, Sum(TOT_DE_TSK) TOT_DE_TSK,  MAX(TOT_AV_TEA) TOT_AV_TEA  from 	PMSNP001
		%Exp:cWhere%
		GROUP by AED_EQUIP,AED_DESCRI
		ORDER BY AED_EQUIP
		EndSql
		While !EoF()
			cRet  += ',{"team":"'+AllTrim(AED_EQUIP)+'","teamDescription":"'+AllTrim(AED_DESCRI)+'","delay":'+AllTrim(sTR(TOT_DE_TSK))+',"allocation":'+AllTrim(sTR(TOT_AL_TSK))+',"availability":'+AllTrim(sTR(TOT_AV_TEA))+',"percent":'+If(TOT_AV_TEA<> 0,AllTrim(sTR(TOT_AL_TSK/TOT_AV_TEA)),'0')+'}'
			DbSkip()
		Enddo
	ElseIf nTipoRet == GROUP_TYPE_TEAM
		BeginSql alias cAlias
		SELECT AED_EQUIP,AED_DESCRI,AE5_GRPCOM,AE5_DESCRI, Sum(TOT_DE_TSK) TOT_DE_TSK, Sum(TOT_AL_TSK) TOT_AL_TSK,  MAX(TOT_AV_TEA) TOT_AV_TEA  from 	PMSNP001
		%Exp:cWhere%
		GROUP by AED_EQUIP,AED_DESCRI,AE5_GRPCOM,AE5_DESCRI
		ORDER BY AED_EQUIP
		EndSql
		aType	:=	{}
		cRet3	:=	""
		While !EoF()
			cRet3  += ',{"type":"'+AllTrim(AE5_GRPCOM)+'","typeDescription":"'+AllTrim(AE5_DESCRI)+'","team":"'+AllTrim(AED_EQUIP)+'","teamDescription":"'+AllTrim(AED_DESCRI)+'","delay":'+AllTrim(sTR(TOT_DE_TSK))+',"allocation":'+AllTrim(sTR(TOT_AL_TSK))+',"availability":'+AllTrim(sTR(TOT_AV_TEA))+',"percent":'+If(TOT_AV_TEA<> 0,AllTrim(sTR(TOT_AL_TSK/TOT_AV_TEA)),'0')+'}'
			If Ascan(aType,{|x| x[1]==AE5_GRPCOM}) == 0
				aAdd(aType,{AE5_GRPCOM,AE5_DESCRI})
			EndIf
			DbSkip()
		Enddo
		cRet:=	',[{"DATE_DATE":"MMMMM"}]'
		cRet+=	',['
		cRetTypes	:=	""
		aType	:=	aSort(aType,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
		For nX:=1 To Len(aType)
			cRetTypes+= ',{"type":"'+aType[nX,1]+'","typeDescription":"'+IIf(empty(aType[nX,2]),'Not classified',aType[nX,2])+'"}'
		Next
		cRet+=	Substr(cRetTypes,2) + "]"
		cRet+=	",["+Substr(cRet3,2) + "]"
	ElseIf nTipoRet == GROUP_NO
		BeginSql alias cAlias
		SELECT AF9_START, AF9_FINISH, AE5_GRPCOM, AFA_QUANT, AE5_DESCRI, AE8_RECURS, AE8_DESCRI,AED_EQUIP,AED_DESCRI, AF8_PROJET,af8_revisa,AF8_DESCRI, AF9_TAREFA, AF9_DESCRI, TOT_AL_TSK, TOT_DE_TSK  from 	PMSNP001
		%Exp:cWhere%
		ORDER BY AE5_GRPCOM,AE5_DESCRI,AF9_START
		EndSql
		While !EoF()
			cRet  += ',{"type":"'+AllTrim(AE5_GRPCOM)+'",'+;
				'"typeDescription":"'+AllTrim(AE5_DESCRI)+'",'+;
				'"allocation":'+AllTrim(sTR(TOT_AL_TSK))+','+;
				'"resource":"'+AllTrim(AE8_RECURS)+'",'+;
				'"resourceDescription":"'+AllTrim(AE8_DESCRI)+'",'+;
				'"team":"'+AllTrim(AED_equip)+'",'+;
				'"teamDescription":"'+AllTrim(AED_DESCRI)+'",'+;
				'"project":"'+AllTrim(AF8_PROJET)+'",'+;
				'"projectDescription":"'+AllTrim(AF8_DESCRI)+'",'+;
				'"task":"'+AllTrim(AF9_TAREFA)+'",'+;
				'"taskDescription":"'+AllTrim(AF9_DESCRI)+'",'+;
				'"start":"'+AllTrim(AF9_START)+'",'+;
				'"finish":"'+AllTrim(AF9_FINISH)+'",'+;
				'"hPlanned":'+AllTrim(str(AFA_QUANT))+','+;
				'"hUsed":'+AllTrim(RetUsed(AF8_PROJET,AF9_TAREFA,AE8_RECURS))+','+;
				'"hDelayed":'+AllTrim(STR(TOT_DE_TSK))+','+;
				'"completed":'+AllTrim(STR(PmsPOCAF9(AF8_PROJET,AF8_REVISA,AF9_TAREFA),7,2))+','+;
				'"project":"'+AllTrim(AF8_PROJET)+'",'+;
				'"projectDescription":"'+AllTrim(AF8_DESCRI)+'"}'

			DbSkip()
		Enddo
	ElseIf nTipoRet == GROUP_NO_ALL
		BeginSql alias cAlias
		SELECT AF9_START, AF9_FINISH, AE5_GRPCOM, AFA_QUANT, AE5_DESCRI, AE8_RECURS, AF8_PROJET,af8_revisa,AF8_DESCRI, AF9_TAREFA, AF9_DESCRI, TOT_AL_TSK, TOT_DE_TSK  from 	PMSNP001
		%Exp:cWhere%
		ORDER BY AE5_GRPCOM,AE5_DESCRI,AF9_START
		EndSql
		While !EoF()
			cRet  += ',{"type":"'+AllTrim(AE5_GRPCOM)+'",'+;
				'"typeDescription":"'+AllTrim(AE5_DESCRI)+'",'+;
				'"allocation":'+AllTrim(sTR(TOT_AL_TSK))+','+;
				'"task":"'+AllTrim(AF9_TAREFA)+'",'+;
				'"taskDescription":"'+AllTrim(AF9_DESCRI)+'",'+;
				'"start":"'+AllTrim(AF9_START)+'",'+;
				'"finish":"'+AllTrim(AF9_FINISH)+'",'+;
				'"hPlanned":'+AllTrim(str(AFA_QUANT))+','+;
				'"hUsed":'+AllTrim(RetUsed(AF8_PROJET,AF9_TAREFA,AE8_RECURS))+','+;
				'"hDelayed":'+AllTrim(STR(TOT_DE_TSK))+','+;
				'"completed":'+AllTrim(STR(PmsPOCAF9(AF8_PROJET,AF8_REVISA,AF9_TAREFA),7,2))+','+;
				'"project":"'+AllTrim(AF8_PROJET)+'",'+;
				'"projectDescription":"'+AllTrim(AF8_DESCRI)+'"}'

			DbSkip()
		Enddo

	EndIf
	DbCloseArea()
	cError	:=	TCSQLError()
	If !Empty(cError)
		cResFault	+= CRLF+cError
	EndIf
	If !Empty(cResFault)
		SetRestFault(100,cResFault)
	EndIf
	cRet := "["+Substr(cRet,2)+"]"

	jJsonRet := JsonObject():New()

	jJsonRet:FromJson(cRet)

Return jJsonRet


/*/{Protheus.doc} GetPoc
Get Percentage of compression for a task
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
@param cPrj, character, param_description
@param cTsk, character, param_description
@param cDate, character, param_description
@Return Json, Json Object
/*/
Static Function GetPoc(cPrj, cTsk, cDate)

	Local jJsonRet := JsonObject():New()
	Local nSPI 	:= Nil
	Local nCPI 	:= Nil
	Local nCRTE 	:= Nil
	Local nCOTE 	:= Nil
	Local nCOTP 	:= Nil
	Local aCRTE 	:= {}
	Local aCOTP 	:= {}	
	Local aCOTE 	:= {}	
	Local dDate 	:= dDatabase
	DbSelectArea('AF9')
	DbSelectArea('AFF')
	DbSelectArea('AF8')
	cPrj := Padr(cPrj,Len(AF8->AF8_PROJET))
	cTsk := Padr(cTsk,Len(AF9->AF9_TAREFA))
	DbSetOrder(1)
	MSSeek(xFilial()+cPrj)
	DbSelectArea('AF9')
	DbSetOrder(1)
	MSSeek(xFilial()+AF8->AF8_PROJET+AF8->AF8_REVISA+cTsk)	
	If !Empty(cDate)
		dDate := Stod(cDate)
	Endif
	//Completed is being returned for retrocompatibility
	jJsonRet['COMPLETED'] := PmsPOCAF9(cPrj, Nil, cTsk, (dDate))
	jJsonRet['poc'] := jJsonRet['COMPLETED']/100
	dDate := dDate-1
	nCOTE	:=	PmsRetCOTE(aCOTE,1,AF9->AF9_TAREFA,.T.)[1]
	aCRTE	:=	PmsIniCRTE(AF9->AF9_PROJET,AF9->AF9_REVISA,dDate,AF9->AF9_tarefa,AF9->AF9_tarefa)
	nCRTE	:=	PmsRetCRTE(aCRTE,1,AF9->AF9_TAREFA,.T.)[1]
	If nCRTE<>0
		nCPI	:=	nCOTE/nCRTE
	EndIf
	aCOTP	:=	PmsIniCOTP(AF9->AF9_PROJET,AF9->AF9_REVISA,dDate,AF9->AF9_tarefa,AF9->AF9_tarefa,/*lAcumulado*/.T.,@aCOTP)
	nCOTP	:=	PmsRetCOTP(aCOTP,1,AF9->AF9_TAREFA,.T.)[1]
	If nCOTP<>0
		nSPI	:=	nCOTE/nCOTP
	EndIf

	jJsonRet["bcWs"]	:=	nCOTP
	jJsonRet["bcWp"]	:=	nCOTE
	jJsonRet["spi"]		:=	nSPI
	jJsonRet["acWp"]	:=	nCRTE
	jJsonRet["cpi"]		:=	nCPI


Return jJsonRet


/*/{Protheus.doc} GetAFU
Get timesheet
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
@param nTipoRet, numeric, param_description
@param cSessionID, character, param_description
@param cDateFrom, character, param_description
@param cDateTo, character, param_description
@param cTeam, character, param_description
@param cRes, character, param_description
@param cTypeTsk, character, param_description
@param cProject, character, param_description
@param cCC, character, param_description
@Return Json, Json Object
/*/
Static Function GetAFU(nTipoRet, cSessionID, cDateFrom, cDateTo, cTeam, cRes, cTypeTsk, cProject, cCC)
	Local cAlias    := GetNextAlias() As Character
	Local cSql      := "" As Character
	Local nX        := 0  As Numeric
	Local aRes      := {} As Array
	Local cRet      := "" As Character
	Local cFields   := "" As Character
	Local cGroupbY  := "" As Character
	Local cOrder    := "" As Character
	Local aType     := {} As Array
	Local aTeam     := {} As Array
	Local cRetTypes := "" As Character
	Local cRetTeam	:= "" As Character
	Local cRetTS	:= "" As Character
	Local cRetCC	:= "" As Character
	Local jJsonRet As Json

//Adjust periods to date
	If len(cDateFrom) == 6
		cDateFrom+= "01"
	EndIf
	If len(cDateTo) == 6
		cDateTo	:= DtoS(LastDay(Stod(cDateTo+"01")))
	EndIf

	If nTipoRet == 1
		cFields	:=	"% AED_EQUIP, AED_DESCRI, Sum(AFU_HQUANT) AFU_QUANT, AE5_GROUP, Substring(AFU_DATA,1,6)  period %"
		cGroupBy	:=	"% AED_EQUIP, AED_DESCRI, AE5_GROUP, Substring(AFU_DATA,1,6)  %" //, %
		cOrder	:=	"% Substring(AFU_DATA,1,6), AED_EQUIP , AE5_GROUP %"
	ElseIf nTipoRet == 2
		cFields	:=	"% AE8_RECURS,AE8_DESCRI, AED_EQUIP, AED_DESCRI, Sum(AFU_HQUANT) AFU_QUANT, AE5_GROUP, AF8_PROJET, AF8_DESCRI, Substring(AFU_DATA,1,6)  period %"
		cGroupBy	:=	"% AE8_RECURS,AE8_DESCRI, AED_EQUIP, AED_DESCRI, AE5_GROUP, AF8_PROJET, AF8_DESCRI, Substring(AFU_DATA,1,6)  %" //, %
		cOrder	:=	"% Substring(AFU_DATA,1,6), AED_EQUIP , AE5_GROUP,AE8_RECURS, AE8_DESCRI, AF8_PROJET, AF8_DESCRI %"
	ElseIf nTipoRet == 3
		cFields	:=	"% AE8_RECURS,AE8_DESCRI, AED_EQUIP, AED_DESCRI,  AE5_GROUP, AF8_PROJET, AF8_TPPRJ, AF8_DESCRI, AFU_DATA, AF9_TAREFA, AF9_DESCRI, Substring(AFU_DATA,1,6) period,Sum(AFU_HQUANT) AFU_QUANT  , mIN(AFU_HORAI) AFU_HORAI, MAX(AFU_HORAF) AFU_HORAF %"
		cGroupBy	:=	"% AE8_RECURS,AE8_DESCRI, AED_EQUIP, AED_DESCRI, AE5_GROUP, AF8_PROJET,AF8_TPPRJ, AF8_DESCRI, AFU_DATA,AF9_TAREFA, AF9_DESCRI, Substring(AFU_DATA,1,6)  %" //, %
		cOrder	:=	"% AFU_DATA, AED_EQUIP , AE5_GROUP,AE8_RECURS, AF8_TPPRJ, AF8_PROJET, AF8_DESCRI %"
	Else
		cFields	:=	"% AED_EQUIP, AED_DESCRI, Sum(AFU_HQUANT) AFU_QUANT, AE5_GRPCOM, AE5_DESCRI, Substring(AFU_DATA,1,6) PERIOD "
		cGroupBy	:=	" AED_EQUIP, AED_DESCRI, AE5_GRPCOM, AE5_DESCRI, Substring(AFU_DATA,1,6)"
		cOrder	:=	" Substring(AFU_DATA,1,6), AE5_GRPCOM"
	EndIf

	cSQL := ""

	cSql	:= " AND AFU_DATA BETWEEN '"+cDateFrom+"' AND '"+cDatetO+"' "
	If cTeam <> Nil
		cSQL += "AND AED_EQUIP = '"+cTeam+"' "
	EndIf

	If cRes<> Nil
		cSql += "AND AE8_RECURS = '"+cRES+"' "
	EndIf

	If cTypeTsk<> Nil

		cSql += " AND AE5_GRPCOM = '"+cTypeTsk+"' "
	EndIf

	If cProject <> Nil
		cSQL += "AND AF8_PROJET = '"+cProject+"' "
	EndIf

	If cCC <> Nil
		cSQL += "AND AF8_TPPRJ = '"+cCC+"' "
	EndIf
/*
cSQL += "AND AFU_TAREFA NOT LIKE '01%' "
cSQL += "AND AFU_TAREFA NOT LIKE '02%' "
cSQL += "AND AFU_TAREFA NOT LIKE '04%' " hardcode release 8 filter
*/

	cSql:=	"% "+cSql+ " %"
	BeginSql alias cAlias
		%noparser%
		SELECT %exp:cFields% 
		FROM %table:AFU% AFU,%table:AE5% AE5, %table:AF8% AF8, %table:AF9% AF9,%table:AE8% AE8 LEFT OUTER JOIN %table:AED% AED ON 
				AE8_EQUIP = AED_EQUIP				
		
		WHERE 	AFU_FILIAL = %xfilial:AFU% AND AFU.%notDel% AND
				AF8_FILIAL = %xfilial:AF8% AND AF8.%notDel% AND
				AF9_FILIAL = %xfilial:AF9% AND AF9.%notDel% AND
				AE8_FILIAL = %xfilial:AE8% AND AE8.%notDel% AND
				AE5_FILIAL = %xfilial:AE5% AND AE5.%notDel% AND
				COALESCE(AED_FILIAL,%xfilial:AED%) = %xfilial:AED% AND
				COALESCE(AED.D_E_L_E_T_ , ' ') = ' ' AND
				AFU_PROJET = AF8_PROJET AND
				AFU_PROJET = AF9_PROJET AND
				AFU_TAREFA = AF9_TAREFA AND
				AFU_REVISA = AF8_REVISA AND
				AF9_REVISA = AF8_REVISA AND
				AF9_GRPCOM = AE5_GRPCOM  AND
				AFU_CTRRVS = '1' AND
				AFU_RECURS = AE8_RECURS AND 
				AE8_USER <> ' ' 

				%exp:cSql%				
		
		GROUP BY %exp:cGroupBy%
		ORDER BY %exp:cOrder%
		
	EndSql
	aType	:=	{}
	aTeam	:=	{}
	aPrj	:=	{}
	aRes	:=	{}
	aCC	:= {}
	WHILE !EOF()

		If nTipoRet == 1
			cRetTs  += ',{"type":"'+AllTrim(AE5_GROUP)+'","team":"'+AllTrim(AED_EQUIP)+'","period":"'+AllTrim(PERIOD)+'","hours":'+AllTrim(sTR(AFU_QUANT))+'}'
		Elseif nTipoRet == 2
			cRetTs  += ',{"type":"'+AllTrim(AE5_GROUP)+'","team":"'+AllTrim(AED_EQUIP)+'","project":"'+AllTrim(AF8_PROJET)+'","resource":"'+AllTrim(AE8_RECURS)+'","period":"'+AllTrim(PERIOD)+'","hours":'+AllTrim(sTR(AFU_QUANT))+'}'
			If Ascan(aRes,{|x| x[1]==AE8_RECURS}) == 0
				aAdd(aRes,{AE8_RECURS,AllTrim(AE8_DESCRI)})
			EndIf
			If Ascan(aPrj,{|x| x[1]==AF8_PROJET}) == 0
				aAdd(aPrj,{AF8_PROJET,AllTrim(AF8_DESCRI)})
			EndIf
		ElseIf nTipoRet == 3
			cRetTs  += ',{"type":"'+AllTrim(AE5_GROUP)+'","team":"'+AllTrim(AED_EQUIP)+'","project":"'+AllTrim(AF8_PROJET)+'","costcollector":"'+AllTrim(AF8_TPPRJ)+;
				'","resource":"'+AllTrim(AE8_RECURS)+;
				'","task":"'+AllTrim(AF9_TAREFA)+;
				'","taskDescription":"'+AllTrim(AF9_DESCRI)+;
				'","date":"'+AllTrim(AFU_DATA)+;
				'","start":"'+AllTrim(AFU_HORAI)+;
				'","finish":"'+AllTrim(AFU_HORAF)+;
				'","period":"'+AllTrim(PERIOD)+;
				'","hours":'+AllTrim(sTR(AFU_QUANT))+'}'
			If Ascan(aRes,{|x| x[1]==AE8_RECURS}) == 0
				aAdd(aRes,{AE8_RECURS,AllTrim(AE8_DESCRI)})
			EndIf
			If Ascan(aPrj,{|x| x[1]==AF8_PROJET}) == 0
				aAdd(aPrj,{AF8_PROJET,AllTrim(AF8_DESCRI)})
			EndIf
			If Ascan(aCC,{|x| x[1]==AF8_TPPRJ}) == 0
				aAdd(aCC,{AF8_TPPRJ,AllTrim(tabela('FE',AF8_TPPRJ	))})
			EndIf
		EndIf
		If Ascan(aTeam,{|x| x[1]==AED_EQUIP}) == 0
			aAdd(aTeam,{AED_EQUIP,AllTrim(AED_DESCRI)})
		EndIf
		If Ascan(aType,{|x| x[1]==AE5_GROUP}) == 0
			AE5->(DbSetorder(1))
			AE5->(MsSeek(xFilial()+(cAlias)->AE5_GROUP))
			aAdd(aType,{AE5_GROUP,AllTrim(AE5->AE5_DESCRI),AE5->AE5_COLOR})
		EndIf
		DbSkip()
	Enddo
	cRetTypes:=	""
	cRetTeam	:=	""
	cRetRes	:=	""
	cRetpRJ	:=	""
	cRetCC	:=	""
	aTeam	:=	aSort(aTeam,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
	For nX:=1 To Len(aTeam)
		cRetTeam+= ',{"team":"'+AllTrim(aTeam[nX,1])+'","teamDescription":"'+IIf(empty(aTeam[nX,2]),'Team not informed',AllTrim(aTeam[nX,2]))+'"}'
	Next
	aPrj	:=	aSort(aPrj,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
	For nX:=1 To Len(aPrj)
		cRetPrj+= ',{"project":"'+AllTrim(aPrj[nX,1])+'","projectDescription":"'+AllTrim(aPrj[nX,2])+'"}'
	Next
	aRes	:=	aSort(aRes,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
	For nX:=1 To Len(aRes)
		cRetRes	+= ',{"resource":"'+AllTrim(aRes[nX,1])+'","resourceDescription":"'+AllTrim(aRes[nX,2])+'"}'
	Next
	aType	:=	aSort(aType,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
	For nX:=1 To Len(aType)
		cRetTypes+= ',{"type":"'+AllTrim(aType[nX,1])+'","typeColor":"'+IIf(empty(aType[nX,2]),'cd6155',AllTrim(aType[nX,3]))+'","typeDescription":"'+IIf(empty(aType[nX,2]),'Type not informed',AllTrim(aType[nX,2]))+'"}'
	Next
	aCC	:=	aSort(aCC,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
	For nX:=1 To Len(aCC)
		cRetCC	+= ',{"costcollector":"'+AllTrim(aCC[nX,1])+'","costcollectorDescription":"'+AllTrim(aCC[nX,2])+'"}'
	Next

	cRet:='['
	cRet+=	Substr(cRetTs,2)
	cRet+=	'],['
	cRet+=	Substr(cRetTypes,2)
	cRet+=	'],['
	cRet+=	Substr(cRetRes,2)
	cRet+=	'],['
	cRet+=	Substr(cRetPrj,2)
	cRet+=	'],['
	cRet+=	Substr(cRetTeam,2)
	cRet+=	'],['
	cRet+=	Substr(cRetCC,2)
	cRet+=	']'

	cRet := "[" + cRet + "]"

	jJsonRet := JsonObject():New()

	jJsonRet:FromJson(cRet)

Return jJsonRet


/*/{Protheus.doc} GetTsk
Get detailed tasks
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
@param cRes, character, param_description
@param cEqp, character, param_description
@param cPrj, character, param_description
@param cType, character, param_description
@param cTskSit, character, param_description
@param cDaysRef, character, param_description
@param cCC, character, param_description
@Return Json, Json Object
/*/
Static Function GetTsk(cRes, cEqp, cPrj, cType, cTskSit, cDaysRef, cCC)
	Local cQuery   := ""
	Local cAliasQry:= ""
	Local cRetTypes:=	""
	Local cRetTeam	:=	""
	Local cRetRes	:=	""
	Local nDaysRef
	Local nX
	Local jJsonRet as Json
	Default cEqp      := ""
	Default cRes      := ""
	Default cTypr     := ""
	Default cSqlPrj   := ""
	Default cCC   		:= ""
	Default cDaysRef	:='30'
	Default cTskSit 	:= 1

	nDaysRef	:=	Val(cDaysRef)

	cAliasQry := GetNextAlias()

	cQuery   := "SELECT AF9.*,AFC_DESCRI,AE5_GROUP,AE8_RECURS,AED_EQUIP,AED_DESCRI,AE8_DESCRI,Af8_PROJET,AF8_TPPRJ,af8_descri,COALESCE(afa_quant,0) AFA_QUANT FROM "+RetSqlName('AFC')+" AFC, "+RetSqlName('AE5')+" AE5, "+RetSqlName('AF8')+" AF8, "
	cQuery   += RetSqlName('AF9')+" AF9 "
	cQuery   += "  LEFT OUTER JOIN "+RetSqlName('AFA')+" AFA ON "
	cQuery   += "  			AFA_FILIAL = '"+xFilial('AFA')+"' AND AFA.d_e_l_e_t_=' ' AND "
	cQuery	+=	"				AFA_REVISA = AF9_REVISA AND "
	cQuery	+=	"				AFA_PROJET = AF9_PROJET AND "
	cQuery	+=	"				AFA_TAREFA = AF9_TAREFA "
	cQuery   += "  LEFT OUTER JOIN "+RetSqlName('AE8')+" AE8 ON "
	cQuery	+=	"				AE8_FILIAL = '"+xFilial('AE8')+"' AND AE8.d_e_l_e_t_=' ' AND "
	cQuery	+=	"			   COALESCE(AFA_RECURS,'UNDEFINED      ') = AE8_RECURS "
	cQuery   += "  LEFT OUTER JOIN "+RetSqlName('AED')+" AED ON
	cQuery	+=	"				AED_FILIAL = '"+xFilial('AED')+"' AND AED.D_E_L_E_T_  = ' ' AND "
	cQuery   += "				AE8_EQUIP = AED_EQUIP	"
	cQuery	+=	"	WHERE
	cQuery	+=	"				AF8_FILIAL = '"+xFilial('AF8')+"' AND AF8.d_e_l_e_t_=' ' AND "
//cQuery	+=	"				AF8_DESCRI not like 'NOT PROJECT%' AND "
	cQuery	+=	"				AF9_FILIAL = '"+xFilial('AF9')+"' AND AF9.d_e_l_e_t_=' ' AND "
	cQuery	+=	"				AF9_REVISA = AF8_REVISA AND "
	cQuery	+=	"				AF9_PROJET = AF8_PROJET AND "
	cQuery	+=	"				AFC_FILIAL = '"+xFilial('AFC')+"' AND AFC.d_e_l_e_t_=' ' AND "
	cQuery	+=	"				AFC_REVISA = AF8_REVISA AND "
	cQuery	+=	"				AFC_EDT	  = AF9_EDTPAI AND "
	cQuery	+=	"				AFC_PROJET = AF8_PROJET AND "
	cQuery	+=	"				AE5_FILIAL = '"+xFilial('AE5')+"' AND AE5.d_e_l_e_t_=' ' AND "
	cQuery	+=	"				AF9_GRPCOM = AE5_GRPCOM AND "
//CLOSED WITH DELAY
	If cTskSit == '1'
		cQuery	+=	"				AF9_DTATUF  >=  '"+DtoS(dDatabase-nDaysRef) + "' and "
		cQuery	+=	"				AF9_DTATUF > AF9_FINISH  AND "
		cQuery	+=	"				AF9_DTATUF <> '' "
//Opened and delayed   
	ElseIf cTskSit == '2'
		cQuery	+=	"				AF8_ENCPRJ <> '1' AND "
		cQuery	+=	"				AF9_DTATUF = '' AND "
		cQuery	+=	"				AF9_FINISH < '"+DtoS(dDatabase)+"' "
//All Closed
	ElseIf cTskSit == '3'
		cQuery	+=	"				AF9_DTATUF >=  '"+DtoS(dDatabase-nDaysRef) + "' and "
		cQuery	+=	"				AF9_DTATUF <> ''  "
	EndIf
	If !Empty(cRes)
		cQuery	+=	" AND AE8_RECURS='"+cRes+"' "
	EndIf
	If !Empty(cType)
		DbSelectArea('AE5')
		DbSetOrder(3)
		DbSeek(xFilial()+cType)
		cRetTypes:=	"'"+cType +"'"
		While !Eof() .and. AE5_FILIAL+AE5_GROUP==xFilial()+cType
			cRetTypes+=	",'"+ae5_grpcom +"'"
			DbSkip()
		Enddo
		cQuery	+=	" AND AE5_GRPCOM in ("+cRetTypes +") "
	EndIf
	If !Empty(cEqp)
		cQuery	+=	" AND AED_EQUIP='"+cEqp+"' "
	EndIf
	If !Empty(cPrj)
		cQuery	+=	" AND AF8_PROJET='"+cPrj+"' "
	EndIf
	If !Empty(cCC)
		cQuery	+=	" AND AF8_TPPRJ='"+cCC +"' "
	EndIf

	cQuery	+=	"				ORDER BY AE8_RECURS, AF9_START,AF9_TAREFA"

	cQuery := ChangeQuery(cQuey)

	dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAliasQry, .F., .T.)

	aTeam		:=	{}
	aRecurs	:=	{}
	aType		:=	{}
	aPrj		:=	{}
	aCC		:=	{}
	cRetTs:=	""
	While !Eof()

		cQueryAFD	:=	'SELECT COUNT(*) AS CONTA FROM '
		cQueryAFD	+=	 RETSQLNAME('AFD')+' AFD,'
		cQueryAFD	+=	 RETSQLNAME('AF9')+' AF9 WHERE '
		cQueryAFD	+=	" AFD_FILIAL ='"+xFilial('AFD') + "' AND "
		cQueryAFD	+=	" AFD_PROJET ='"+AF8_PROJET+ "' AND "
		cQueryAFD	+=	" AFD_REVISA ='"+AF9_REVISA+ "' AND "
		cQueryAFD	+=	" AFD_TAREFA ='"+AF9_TAREFA+ "' AND "
		//cQueryAFD	+=	" AFD_TIPO ='1' AND "
		cQueryAFD	+=	" AF9_FILIAL ='"+xFilial('AF9') + "' AND "
		cQueryAFD	+=	" AF9_TAREFA = AFD_PREDEC AND "
		cQueryAFD	+=	" AF9_PROJET ='"+AF8_PROJET+ "' AND "
		cQueryAFD	+=	" AF9_REVISA ='"+AF9_REVISA+ "' AND "
		cQueryAFD	+=	" AF9_DTATUF ='' AND "
		cQueryAFD	+=	" afd.d_e_l_e_t_ =' ' AND "
		cQueryAFD	+=	" af9.d_e_l_e_t_ =' '  "

		cQueryAFD := ChangeQuery(cQueryAFD)

		cAlias	:=	Alias()
		dbUseArea(.T., "TOPCONN", TCGenQry(,,cQueryAFD), cAliasQry+"AFD", .F., .T.)
		nConta:=	CONTA
		DbCloseArea()
		dbselectArea(cAlias)
		cRetTs  += ',{"type":"'+AllTrim(AE5_GROUP)+'","team":"'+AllTrim(AED_EQUIP)+'","project":"'+AllTrim(AF8_PROJET)+'","costcollector":"'+AllTrim(AF8_TPPRJ)+;
			'","resource":"'+AllTrim(AE8_RECURS)+;
			'","task":"'+AllTrim(AF9_TAREFA)+;
			'","taskDescription":"'+AllTrim(AF9_DESCRI)+;
			'","wbsDescription":"'+AllTrim(AFC_DESCRI)+;
			'","start":"'+AllTrim(AF9_START)+;
			'","finish":"'+AllTrim(AF9_FINISH)+;
			'","startReal":"'+AllTrim(AF9_DTATUI)+;
			'","finishReal":"'+AllTrim(AF9_DTATUF)+;
			'","predOpen":'+Str(nConta)+;
			',"hours":'+AllTrim(sTR(AFA_QUANT))+'}'

		If Ascan(aPrj,{|x| x[1]==AF8_PROJET}) == 0
			aAdd(aPrj,{AF8_PROJET,AllTrim(AF8_DESCRI)})
		EndIf
		If Ascan(aTeam,{|x| x[1]==AED_EQUIP}) == 0
			aAdd(aTeam,{AED_EQUIP,AllTrim(AED_DESCRI)})
		EndIf
		If Ascan(aRecurs,{|x| x[1]==AllTrim(AE8_recurs)}) == 0
			aAdd(aRecurs,{AllTrim(AE8_recurs),AllTrim(AE8_descri)})
		EndIf
		If Ascan(aType,{|x| x[1]==AE5_GROUP}) == 0
			AE5->(DbSetorder(1))
			AE5->(MsSeek(xFilial()+(cAliasQry)->AE5_GROUP))
			aAdd(aType,{AE5_GROUP,AllTrim(AE5->AE5_DESCRI),AE5->AE5_COLOR})
		EndIf
		If Ascan(aCC,{|x| x[1]==AF8_TPPRJ}) == 0
			aAdd(aCC,{AF8_TPPRJ,AllTrim(TABELA('FE',AF8_TPPRJ))})
		EndIf

		DbSkip()
	Enddo
//Add original type to list
	If !Empty(cType)
		AE5->(DbSetorder(1))
		If AE5->(MsSeek(xFilial()+cType))
			aAdd(aType,{AE5_GROUP,AllTrim(AE5->AE5_DESCRI),AE5->AE5_COLOR})
		EndIf
	EndIf
	cRetTypes:=	""
	cRetTeam	:=	""
	cRetRes	:=	""
	cRetPrj	:=	""
	cRetCC	:= ""
	aPrj	:=	aSort(aPrj,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
	For nX:=1 To Len(aPrj)
		cRetPrj+= ',{"project":"'+AllTrim(aPrj[nX,1])+'","projectDescription":"'+AllTrim(aPrj[nX,2])+'"}'
	Next

	aCC	:=	aSort(aCC,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
	For nX:=1 To Len(aCC)
		cRetCC+= ',{"costCollector":"'+AllTrim(aCC[nX,1])+'","costCollectorDescription":"'+AllTrim(aCC[nX,2])+'"}'
	Next

	aTeam	:=	aSort(aTeam,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
	For nX:=1 To Len(aTeam)
		cRetTeam+= ',{"team":"'+AllTrim(aTeam[nX,1])+'","teamDescription":"'+IIf(empty(aTeam[nX,2]),'Team not informed',AllTrim(aTeam[nX,2]))+'"}'
	Next
	aType	:=	aSort(aType,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
	For nX:=1 To Len(aType)
		cRetTypes+= ',{"type":"'+AllTrim(aType[nX,1])+'","typeColor":"'+IIf(empty(aType[nX,2]),'cd6155',AllTrim(aType[nX,3]))+'","typeDescription":"'+IIf(empty(aType[nX,2]),'Type not informed',AllTrim(aType[nX,2]))+'"}'
	Next

	aRecurs	:=	aSort(aRecurs,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
	For nX:=1 To Len(aRecurs)
		cRetRes+= ',{"resource":"'+AllTrim(aRecurs[nX,1])+'","resourceDescription":"'+AllTrim(aRecurs[nX,2])+'"}'
	Next
	cRet:='['
	cRet+=	Substr(cRetTs,2)
	cRet+=	'],['
	cRet+=	Substr(cRetTypes,2)
	cRet+=	'],['
	cRet+=	Substr(cRetRes,2)
	cRet+=	'],['
	cRet+=	Substr(cRetPrj,2)
	cRet+=	'],['
	cRet+=	Substr(cRetTeam,2)
	cRet+=	'],['
	cRet+=	Substr(cRetCC,2)
	cRet+=	']'

	DbCloseArea()

	cRet := "[" + cRet + "]"

	jJsonRet := JsonObject():New()

	jJsonRet:FromJson(cRet)

Return jJsonRet


/*/{Protheus.doc} GetTskGnt
Get detailed open tasks
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
@param cRes, character, param_description
@param cEqp, character, param_description
@param cPrj, character, param_description
@param cType, character, param_description
@param cCC, character, param_description
@param cStartFrom, character, param_description
@param cStartTo, character, param_description
@Return Json, Json Object
/*/
Static Function GetTskGnt(cRes, cEqp, cPrj, cType, cCC, cStartFrom, cStartTo)
	Local cQuery   := ""
	Local cAliasQry:= ""
	Local cRetTypes:=	""
	Local cRetTeam	:=	""
	Local cRetRes	:=	""
	Local nX
	Local cRetAlloc	:=	""
	Local dDate:=""
	Local jJsonRet As Json
	Default cEqp      := ""
	Default cRes      := ""
	Default cTypr     := ""
	Default cSqlPrj   := ""
	Default cCC   		:= ""

	Default cTskSit 	:= 1

	cAliasQry:= GetNextAlias()

	cQuery   := "SELECT AF9.*,AE5_CTCPI,AE5_CTSPI,AFC_DESCRI,AE5_GROUP,AE8_RECURS,AED_EQUIP,AED_DESCRI,AE8_DESCRI,Af8_PROJET,AF8_TPPRJ,af8_descri,afa_quant FROM "+RetSqlName('AFC')+" AFC, "+RetSqlName('AE5')+" AE5, "+RetSqlName('AF8')+" AF8, "
	cQuery   += RetSqlName('AF9')+" AF9 "
	cQuery   += "  INNER JOIN "+RetSqlName('AFA')+" AFA ON "
	cQuery   += "  			AFA_FILIAL = '"+xFilial('AFA')+"' AND AFA.d_e_l_e_t_=' ' AND "
	cQuery	+=	"				AFA_REVISA = AF9_REVISA AND "
	cQuery	+=	"				AFA_PROJET = AF9_PROJET AND "
	cQuery	+=	"				AFA_TAREFA = AF9_TAREFA "
	cQuery   += "  INNER JOIN "+RetSqlName('AE8')+" AE8 ON "
	cQuery	+=	"				AE8_FILIAL = '"+xFilial('AE8')+"' AND AE8.d_e_l_e_t_=' ' AND "
	cQuery	+=	"			   COALESCE(AFA_RECURS,'UNDEFINED      ') = AE8_RECURS "
	cQuery   += "  INNER JOIN "+RetSqlName('AED')+" AED ON
	cQuery	+=	"				AED_FILIAL = '"+xFilial('AED')+"' AND AED.D_E_L_E_T_  = ' ' AND "
	cQuery   += "				AE8_EQUIP = AED_EQUIP	"
	cQuery	+=	"	WHERE
	cQuery	+=	"				AF8_FILIAL = '"+xFilial('AF8')+"' AND AF8.d_e_l_e_t_=' ' AND "
	cQuery	+=	"				AF8_DESCRI not like 'NOT PROJECT%' AND "
	cQuery	+=	"				AF9_FILIAL = '"+xFilial('AF9')+"' AND AF9.d_e_l_e_t_=' ' AND "
	cQuery	+=	"				AF9_REVISA = AF8_REVISA AND "
	cQuery	+=	"				AF9_PROJET = AF8_PROJET AND "
	cQuery	+=	"				AFC_FILIAL = '"+xFilial('AFC')+"' AND AFC.d_e_l_e_t_=' ' AND "
	cQuery	+=	"				AFC_REVISA = AF8_REVISA AND "
	cQuery	+=	"				AFC_EDT	  = AF9_EDTPAI AND "
	cQuery	+=	"				AFC_PROJET = AF8_PROJET AND "
	cQuery	+=	"				AE5_FILIAL = '"+xFilial('AE5')+"' AND AE5.d_e_l_e_t_=' ' AND "
	cQuery	+=	"				AF9_GRPCOM = AE5_GRPCOM AND "
	cQuery	+=	"				AF9_START  BETWEEN '"+cStartFrom + "' and '"+cStartTo + "' AND "
	cQuery	+=	"				AF9_DTATUF = ''  "

	If !Empty(cRes)
		cQuery	+=	" AND AE8_RECURS='"+cRes+"' "
	EndIf
	If !Empty(cType)
		DbSelectArea('AE5')
		DbSetOrder(3)
		DbSeek(xFilial()+cType)
		cRetTypes:=	"'"+cType +"'"
		While !Eof() .and. AE5_FILIAL+AE5_GROUP==xFilial()+cType
			cRetTypes+=	",'"+ae5_grpcom +"'"
			DbSkip()
		Enddo
		cQuery	+=	" AND AE5_GRPCOM in ("+cRetTypes +") "
	EndIf
	If !Empty(cEqp)
		cQuery	+=	" AND AED_EQUIP='"+cEqp+"' "
	EndIf
	If !Empty(cPrj)
		cQuery	+=	" AND AF8_PROJET='"+cPrj+"' "
	EndIf
	If !Empty(cCC)
		cQuery	+=	" AND AF8_TPPRJ='"+cCC +"' "
	Else
		cQuery	+=	" AND Substring(AF8_TPPRJ,1,1)<>'Z' "
	EndIf

	cQuery	+=	"				ORDER BY AE8_RECURS, AF9_START,AF8_PROJET, AF9_TAREFA"

	cQuery := ChangeQuery(cQuery)

	dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAliasQry, .F., .T.)

	aTeam		:=	{}
	aRecurs	:=	{}
	aType		:=	{}
	aPrj		:=	{}
	aCC		:=	{}
	aCOTP		:=	{}
	aCOTE		:=	{}
	aCRTE		:=	{}

	cRetAlloc:=	""
	While !Eof()
		AF8->(dbsetorder(1))
		AF8->(DbSeek(xFilial()+(cAliasQry)->AF8_PROJET))
		AF9->(dbsetorder(1))
		AF9->(DbSeek(xFilial()+(cAliasQry)->(AF8_PROJET+AF9_REVISA+AF9_TAREFA)))
		nCOTP	:=	0  //BCWS
		nCOTE	:=	0  //BCWP
		nCRTE	:=	0 	//ACWP
		nSPI:=	-1 //not controlled
		nCPI:=	-1//not controlled

		dDate := date()
		nPoc	:=	PmsPocAF9(AF8_PROJET, AF9_REVISA, AF9_tarefa, dDate)

		dDate := dDate-1
		aCOTP	:=	PmsIniCOTP(AF8_PROJET,AF9_REVISA,dDate,AF9_tarefa,AF9_tarefa,/*lAcumulado*/.T.,@aCOTP)

		nCOTP	:=	PmsRetCOTP(aCOTP,1,AF9_TAREFA,.T.)[1]
		nCOTE	:=	PmsRetCOTE(aCOTE,1,AF9_TAREFA,.T.)[1]

		aCRTE	:=	PmsIniCRTE(AF8_PROJET,AF9_REVISA,dDate,AF9_tarefa,AF9_tarefa)
		nCRTE	:=	PmsRetCRTE(aCRTE,1,AF9_TAREFA,.T.)[1]
		If (AE5_CTSPI <>"2")
			If nCOTP<>0
				nSPI	:=	nCOTE/nCOTP
			EndIf
		EndIf
		If (AE5_CTCPI <>"2")
			If nCRTE<>0
				nCPI	:=	nCOTE/nCRTE
			EndIf
		EndIf
		cUsed:=	RetUsed(AF8_PROJET,AF9_TAREFA,AE8_RECURS)
		cRetAlloc  += ',{"type":"'+AllTrim(AE5_GROUP)+'","team":"'+AllTrim(AED_EQUIP)+'","project":"'+AllTrim(AF8_PROJET)+'","costcollector":"'+AllTrim(AF8_TPPRJ)+;
			'","resource":"'+AllTrim(AE8_RECURS)+;
			'","task":"'+AllTrim(AF9_TAREFA)+;
			'","taskDescription":"'+AllTrim(AF9_DESCRI)+;
			'","wbsDescription":"'+AllTrim(AFC_DESCRI)+;
			'","start":"'+AllTrim(AF9_START)+;
			'","finish":"'+AllTrim(AF9_FINISH)+;
			'","bcWs":'+Str(nCOTP)+;
			',"bcWp":'+Str(nCOTE)+;
			',"acWp":'+Str(nCRTE)+;
			',"spi":'+Str(nSPI)+;
			',"cpi":'+Str(nCPI)+;
			',"poc":'+Str(nPOC)+;
			',"hUsed":'+cUsed+;
			',"hours":'+AllTrim(sTR(AFA_QUANT))+'}'

		If Ascan(aPrj,{|x| x[1]==AF8_PROJET}) == 0
			aAdd(aPrj,{AF8_PROJET,AllTrim(AF8_DESCRI)})
		EndIf
		If Ascan(aTeam,{|x| x[1]==AED_EQUIP}) == 0
			aAdd(aTeam,{AED_EQUIP,AllTrim(AED_DESCRI)})
		EndIf
		If Ascan(aRecurs,{|x| x[1]==AllTrim(AE8_recurs)}) == 0
			aAdd(aRecurs,{AllTrim(AE8_recurs),AllTrim(AE8_descri)})
		EndIf
		If Ascan(aType,{|x| x[1]==AE5_GROUP}) == 0
			AE5->(DbSetorder(1))
			AE5->(MsSeek(xFilial()+(cAliasQry)->AE5_GROUP))
			aAdd(aType,{AE5_GROUP,AllTrim(AE5->AE5_DESCRI),AE5->AE5_COLOR})
		EndIf
		If Ascan(aCC,{|x| x[1]==AF8_TPPRJ}) == 0
			aAdd(aCC,{AF8_TPPRJ,AllTrim(TABELA('FE',AF8_TPPRJ))})
		EndIf

		DbSkip()
	Enddo
//Add original type to list
	If !Empty(cType)
		AE5->(DbSetorder(1))
		If AE5->(MsSeek(xFilial()+cType))
			aAdd(aType,{AE5_GROUP,AllTrim(AE5->AE5_DESCRI),AE5->AE5_COLOR})
		EndIf
	EndIf
	cRetTypes:=	""
	cRetTeam	:=	""
	cRetRes	:=	""
	cRetPrj	:=	""
	cRetCC	:= ""
	aPrj	:=	aSort(aPrj,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
	For nX:=1 To Len(aPrj)
		cRetPrj+= ',{"project":"'+AllTrim(aPrj[nX,1])+'","projectDescription":"'+AllTrim(aPrj[nX,2])+'"}'
	Next

	aCC	:=	aSort(aCC,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
	For nX:=1 To Len(aCC)
		cRetCC+= ',{"costcollector":"'+AllTrim(aCC[nX,1])+'","costcollectorDescription":"'+AllTrim(aCC[nX,2])+'"}'
	Next

	aTeam	:=	aSort(aTeam,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
	For nX:=1 To Len(aTeam)
		cRetTeam+= ',{"team":"'+AllTrim(aTeam[nX,1])+'","teamDescription":"'+IIf(empty(aTeam[nX,2]),'Team not informed',AllTrim(aTeam[nX,2]))+'"}'
	Next
	aType	:=	aSort(aType,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
	For nX:=1 To Len(aType)
		cRetTypes+= ',{"type":"'+AllTrim(aType[nX,1])+'","typeColor":"'+IIf(empty(aType[nX,2]),'cd6155',AllTrim(aType[nX,3]))+'","typeDescription":"'+IIf(empty(aType[nX,2]),'Type not informed',AllTrim(aType[nX,2]))+'"}'
	Next

	aRecurs	:=	aSort(aRecurs,/*nInicio*/,/*nCont*/,{|x,y| x[1]<y[1]})
	For nX:=1 To Len(aRecurs)
		cRetRes+= ',{"resource":"'+AllTrim(aRecurs[nX,1])+'","resourceDescription":"'+AllTrim(aRecurs[nX,2])+'"}'
	Next
	cRet:='['
	cRet+=	Substr(cRetAlloc,2)
	cRet+=	'],['
	cRet+=	Substr(cRetTypes,2)
	cRet+=	'],['
	cRet+=	Substr(cRetRes,2)
	cRet+=	'],['
	cRet+=	Substr(cRetPrj,2)
	cRet+=	'],['
	cRet+=	Substr(cRetTeam,2)
	cRet+=	'],['
	cRet+=	Substr(cRetCC,2)
	cRet+=	']'

	DbCloseArea()

	cRet := "[" + cRet + "]"

	jJsonRet := JsonObject():New()

	jJsonRet:FromJson(cRet)

Return jJsonRet

/*/{Protheus.doc} GetTsTotal
Get timesheet total by resource
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
@param cDateIni, character, param_description
@param cDateTo, character, param_description
@Return Json, Json Object
/*/
Static function GetTsTotal(cDateIni, cDateTo)
	Local cAlias		:=	GetNextAlias()
	Local cWhere1		:=	""
	Local aAFUZPA		:=	{}
	Local NX
	Local nPosAFU
	Local cRet :=	""
	Local jJsonRet As Json
	DEFAULT cDateIni	:=	DtoS(dDatabase - 32)
	DEFAULT cDateTo	:=	DtoS(dDatabase -  2)

	cWhere1	:=	"% AFU_DATA BETWEEN '"+cDateIni+"' AND  '"+cDateTo+"' %"

	BeginSql alias cAlias
		SELECT AE8_RECURS,AE8_DESCRI, AED_EQUIP, AED_DESCRI,SUM(AFU_HQUANT) AFU_HQUANT   
		FROM   
			%table:AFU% AFU ,
			%table:AF8% AF8 ,
			%table:AE8% AE8 , 
			%table:AED% AED  
		WHERE %Exp:cWhere1%
			and AE8_USER  <> ''
			AND AED_EQUIP =AE8_EQUIP
			AND AFU_RECURS=AE8_RECURS
			AND AFU_PROJET=AF8_PROJET
			AND AFU_REVISA=AF8_REVISA
			AND AFU_FILIAL = %xfilial:AFU% 
			AND AF8_FILIAL = %xfilial:AF8% 
			AND AE8_FILIAL = %xfilial:AE8% 
			AND AED_FILIAL = %xfilial:AED% 
			AND AFU.%notDel%
			AND AF8.%notDel%
			AND AE8.%notDel%
			AND AED.%notDel%
		GROUP by AE8_RECURS,AE8_DESCRI, AED_EQUIP, AED_DESCRI
		ORDER BY AE8_RECURS
	EndSql
	While !Eof()
		aAdd(aAFUZPA,{AE8_RECURS,AE8_DESCRI,AED_EQUIP,AED_DESCRI,AFU_HQUANT,0})
		DbSkip()
	Enddo
	DbCloseArea()

	cWhere1	:= "% ZPA_DATE BETWEEN '"+cDateIni+"' AND  '"+cDateTo+"' %"
	BeginSql alias cAlias
		SELECT AE8_RECURS,AE8_DESCRI, AED_EQUIP, AED_DESCRI, SUM(ZPA_HOURS) ZPA_HOURS  
		FROM   
			%table:AE8% AE8 , 
			%table:ZPA% ZPA , 
			%table:AED% AED  
		WHERE %Exp:cWhere1%
			and AE8_USER <> ''
			and ZPA_RECURS = AE8_RECURS
			AND AED_EQUIP  = AE8_EQUIP
			AND AE8_FILIAL = %xfilial:AE8% 
			AND AED_FILIAL = %xfilial:AED% 
			AND ZPA_FILIAL = %xfilial:zpa% 
			AND AE8.%notDel%
			AND AED.%notDel%
			AND ZPA.%notDel%
		GROUP by AE8_RECURS,AE8_DESCRI, AED_EQUIP, AED_DESCRI
		ORDER BY AE8_RECURS
	EndSql
	While !Eof()
		If (nPosAFU:= ascan(aAFUZPA,{|x| x[1]==AE8_RECURS .and. AED_EQUIP==x[3]})) ==0
			aAdd(aAFUZPA,{AE8_RECURS,AE8_DESCRI,AED_EQUIP,AED_DESCRI,0,ZPA_HOURS})
		Else
			aAFUZPA[nPosAFU,6]	:=	ZPA_HOURS
		EndIf
		DbSkip()
	Enddo
	DbCloseArea()
	cRet	:=	''
	For nX:= 1 TO LEN(aAFUZPA)
		cRet	+=	',["resource":"'+AllTrim(aAFUZPA[nX,1])+'","resourceDescription",:"'+AllTrim(aAFUZPA[nX,2])+'","team":"'+AllTrim(aAFUZPA[nX,3])+'","teamDescription":"'+AllTrim(aAFUZPA[nX,4])+'","fact":'+AllTrim(STR(aAFUZPA[nX,5]))+',"availability":'+AllTrim(STR(aAFUZPA[nX,6]))+']'
	Next
	cRet:= '['+Substr(cRet,2)+']'

	jJsonRet := JsonObject():New()

	jJsonRet:FromJson(cRet)

Return jJsonRet


/*/{Protheus.doc} RecAnalisys
Get allocation analysis
@type function
@author Bruno Sobieski
@author Fernando Nicolau
@since 18/07/2023
@param dIniQry, date
@param dFimQry, date
@param cFilter, character
@param cTeam, character, param_description
@param cResource, character, param_description
@Return Json, Json Object
/*/
Static Function RecAnalisys(dIniQry, dFimQry, cFilter, cTeam, cResource)
	Local aRecAF9	:= {}
	Local nX := 0
	Local nY := 0
	Local aAreaAE8 := AE8->(GetArea())
	Local aAloc		:=	{}
	Local aRecAE8	:=	{}
	Local aGantt := {}
	Local ATSK:={}
	Local aResource :={}
	Local nfilter
	Local jRet := JsonObject():new()
	default cfilter:='1'
	nfilter := val(cfilter)
//default cteam  :='A0001'
//default cResource:='AFEDOROVA'


/*
@param nFilter, 	numerico,  Tipo de filtro selecionado(1-Todas as tarefas,2-Tarefas finalizadas,3-Tarefas a Executar e 4-Tarefas com restrio)
*/
	aGantt := {}
	If !Empty(cResource)
		AE8->(dbSetOrder(1))
		If AE8->(dbSeek(xFilial()+cResource))
			aAdd(aRecAE8,AE8->(RECNO()))
		EndIf
	Else
		AE8->(dbSetOrder(4))
		AE8->(dbSeek(xFilial()+cTeam))
		While AE8->(AE8_FILIAL = xFilial() .And. !Eof() .And. (Empty(cTeam) .Or. AllTrim(AE8->AE8_EQUIP) == AllTrim(cTeam)))	//loop para carregar todos os
			aAdd(aRecAE8,AE8->(RECNO()))
			AE8->(DbSkip())
		Enddo
	EndIf

	For nY := 1 To Len(aRecAE8)
		AE8->(MsGoTo(aRecAE8[nY]))

		aAdd(aResource,JsonObject():new())
		nPos := Len(aResource)
		aResource[nPos]['AE8_RECURS' ] := AllTrim(AE8->AE8_RECURS)
		aResource[nPos]['AE8_DESCRI' ] := AllTrim(AE8->AE8_DESCRI)
		aResource[nPos]['AE8_UMAX' ] 	 := cValtoChar(AE8->AE8_UMAX)
		aResource[nPos]['AE8_SUPALO' ] := AE8->AE8_SUPALO
		//aResource[nPos]['ALOC' 	]		 :={}
		//aesource[nPos]['TASKS' ]		 :={}
		aAlocJson	:= {}
		aAlocTasks	:= {}
		aRecAF9	:= {}
		aAloc	:= PmsRetAloc(AE8->AE8_RECURS,dIniQry,"00:00",dFimQry,"24:00",nFilter,,,,aRecAF9) //'Verificando alocacao do recurso '
		If !Empty(aAloc)
			aAdd(aGantt,{{AE8->AE8_RECURS,AE8->AE8_DESCRI,AE8->AE8_UMAX,AE8->AE8_SUPALO},{},{}})
			For nx := 1 to Len(aAloc)-1
				If aAloc[nx][3] > 0
					dIni	:= aAloc[nx][1]
					cHIni	:= aAloc[nx][2]
					dFim	:= aAloc[nx+1][1]
					cHFim	:= aAloc[nx+1][2]
					aAdd(aGantt[Len(aGantt)][2],{dIni,cHIni,dFim,cHFim,aAloc[nx][3]})
					aAdd(aAlocJson,JsonObject():new())
					nPosAloc := Len(aAlocJson)
					aAlocJson[nPosAloc]['START_DAY' 	] := DtoS(dIni)
					aAlocJson[nPosAloc]['START_HOUR' ] := cValtoChar(cHIni)
					aAlocJson[nPosAloc]['FINISH_DAY' ] := DtoS(dFim)
					aAlocJson[nPosAloc]['FINISH_HOUR'] := cValtoChar(cHFim)
					aAlocJson[nPosAloc]['ALLOCATION']  := cValtoChar(aAloc[nx][3])
				EndIf
			Next
			aResource[nPos]['ALOC' 	]:= aAlocJson
			For nx := 1 to Len(aRecAF9)

				dbSelectArea("AF9")
				dbGoto(aRecAF9[nx])
				AFA->(DBSETORDER(5))
				AFA->(DBSEEK(xfilial()+AF9->AF9_PROJET+AF9->AF9_REVISA+AF9->AF9_TAREFA+AE8->AE8_RECURS))
/*			aAdd(aGantt[Len(aGantt)][3],;
				{AF9->AF9_PROJET+"/"+AF9->AF9_TAREFA,;
				AF9->AF9_START,;
				AF9->AF9_HORAI,;
				AF9->AF9_FINISH,;
				AF9->AF9_HORAF,;
				"["+AllTrim(AF9->AF9_PROJET)+AF9->AF9_TAREFA+"]:"+AllTrim(AF9->AF9_DESCRI)+" POC :"+),;
				AFA->AFA_ALOC}) 
			*/
				aAdd(aTsk,JsonObject():new())
				nPosTsk := Len(aTsk)
				aTsk[nPosTsk]['AF9_PROJET'] := AllTrim(AF9->AF9_PROJET)
				aTsk[nPosTsk]['AF9_TAREFA'] := AllTrim(AF9->AF9_TAREFA)
				aTsk[nPosTsk]['AF9_START'] := DtoS(AF9->AF9_START)
				aTsk[nPosTsk]['AF9_HORAI'] := AllTrim(AF9->AF9_HORAI)
				aTsk[nPosTsk]['AF9_FINISH'] := DtoS(AF9->AF9_FINISH)
				aTsk[nPosTsk]['AF9_HORAF'] := AllTrim(AF9->AF9_HORAF)
				aTsk[nPosTsk]['POC'] 		:= AllTrim(TransForm(PmsPOCAF9(AF9_PROJET,AF9_REVISA,AF9_TAREFA,MSDate(),AF9->AF9_QUANT),"@E 999.99%"))
				aTsk[nPosTsk]['AFA_ALOC']	:=	AFA->AFA_ALOC
			Next nx
			aResource[nPos]['TASKS' 	]:= aTsk
		EndIf
	Next
	AE8->(dbSetOrder(1))
	RestArea(aAreaAE8)

	jRet :=  JsonObject():new()
	jRet['RESOURCES'] := aResource
Return jRet:toJson()

/*/{Protheus.doc} GetSX5
Get data from SX5
@type function
@author Fernando Nicolau
@since 23/08/2023
@param cTab, character, param_description
@Return Json, Json Object
/*/
Static Function GetSX5(cTab) as Json

	Local nX        := 0  As Numeric
	Local aRetCab   := {} As Array
	Local aRetDados := {} As Array
	Local aReturn   := {} As Array
	Local aAuxData  := {} As Array
	Local jJsonAux As Json
	Local jJsonRet As Json

	cTab := Upper(cTab)

	aRetCab   := {}
	aRetDados := {}
	aReturn   := {}

	aAuxData := {}
	jJsonAux := {"key": "SX5" + cTab}
	aAdd(aRetCab, jJsonAux)
	DbSelectArea('SX5')
	SX5->(DbSetOrder(1))
	SX5->(DbSeek(xFilial("SX5") + cTab))
	While xFilial("SX5") == SX5->X5_FILIAL .And. SX5->X5_TABELA == cTab
		jJsonAux :=  {"id": AllTrim(SX5->X5_CHAVE), "label": AllTrim(X5Descri())}
		aAdd(aAuxData, jJsonAux)
		SX5->(DbSkip())
	End

	aAdd(aRetDados, aAuxData)

	aAdd(aReturn, aRetCab)
	For nX := 1 To Len(aRetDados)
		aAdd(aReturn, aRetDados[nX])
	Next

	jJsonRet := jsonObject():New()
	jJsonRet:Set(aReturn)

Return jJsonRet

//@GET prj WSRECEIVE startIndex, count,valueadded,version,wbs,taskcode,onlytasks WSSERVICE pmswebrp
	@Get("/api/pms/v1/ru44w120/fullprjstructure/:prjcode")
Function ru44w120_GetPrjFull()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jQuery := jsonObject():New() As Json
	Local  jHeaders := JsonObject():New() as Json

	jQuery	:= oRest:GetQueryRequest()
	jPath 	:= oRest:getPathParamsRequest()


//jHeaders:fromJson(' {"Content-Type":"application/json charset=utf-8"}')
	jHeaders:fromJson(' {"Content-Type":"application/json"}')
	oRest:setHeaderResponse(jHeaders)

	If (jPath <> Nil )
		jResponse:=	GetProject(jPath['prjcode'])
//	jResponse := GetAlloc(GROUP_TEAM, jPath[ 'sessionId' ], jPath[ 'cDateFrom' ], jPath[ 'cDateTo' ],,,)
	EndIf

	oRest:setResponse(jResponse)

Return

Static Function GetProject(ProjectCode)  as Json
	Local aArea		:= GetArea() as Array
	Local cRet				:=	"" as character
	Local lFilterClients := (SuperGetMV("MV_PMSPCLI", .F., 1) == 1 ) as logical
	Local lAccess			:=	.T. as logical
	Local jData := JsonObject():New() as Json


	aListTsk:= {}
	ProjectCode := Padr(ProjectCode,TamSX3('AF8_PROJET')[1])
	AF8->(DbSetOrder(1))
	If AF8->(DbSeek(xFilial()+ProjectCode))
		If lFilterClients  .And. AF8->AF8_CLIENT<>''
			lAccess := .F.
			DbSelectArea('AI3')
			DbSetOrder(4)
			If DbSeek(xFilial()+__cUserID)
				DbSelectArea('AI4')
				DbSetOrder(1)
				If (DbSeek(xFilial()+AI3->AI3_CODUSU+AF8->AF8_CLIENT+AF8->AF8_LOJA))
					lAccess := .T.
				Endif
			Endif
		Endif
		dbSelectArea("AFC")
		AFC->(dbSetOrder(1))
		AFC->(MsSeek(xFilial("AFC") + AF8->AF8_PROJET +  AF8->AF8_REVISA+PADR(AF8->AF8_PROJET ,TamSX3('AFC_EDT')[1]) ))
		If lAccess
			jData := PmsGetWBS(AF8->AF8_PROJET, AF8->AF8_REVISA)
		Else
//		SetRestFault(800,"Clients project control active and user has no access to this client?s project.")
//		cRet:=	""
		Endif
	Else
		//SetRestFault(800,"Project not found ")
		cRet:=	""
	Endif

	RestArea(aArea)
Return jData

Static Function PmsGetWBS(cProject, cVersion) as Json
	Local aArea := GetArea()
	Local aAreaAF9 := AF9->(GetArea())
	Local aAreaAFC := AFC->(GetArea())
	Local jWBS := JsonObject():New()
	Local jTask := JsonObject():New()
	Local cWbs := AFC->AFC_EDT as character
	If PmsChkUser(AFC->AFC_PROJET, , AFC->AFC_EDT, AFC->AFC_EDTPAI, ;
			1, "ESTRUT", AFC->AFC_REVISA,/* cUser*/)
		jWbs['AFC_PROJET'] 	:= Alltrim(AFC->AFC_PROJET)
		jWbs['AFC_EDT'] 	:= Alltrim(AFC->AFC_EDT)
		jWbs['AFC_EDTPAI'] 	:= Alltrim(AFC->AFC_EDTPAI)
		jWbs['AFC_START'] 	:= Dtos(AFC->AFC_START)
		jWbs['AFC_FINISH'] 	:= Dtos(AFC->AFC_FINISH)

		jWbs['AFC_REVISA'] 	:= Alltrim(AFC->AFC_REVISA)
		jWbs['AFC_DESCRI'] 	:= Alltrim(AFC->AFC_DESCRI)
		jWbs['AFC_HORAI'] 	:= Alltrim(AFC->AFC_HORAI)
		jWbs['AFC_HORAF'] 	:= Alltrim(AFC->AFC_HORAF)
		jWbs['AFC_ORDEM'] 	:= Alltrim(AFC->AFC_ORDEM)
		jWbs['hasAccess'] 	:= .T.

		jWbs['subWBSs'] 	:= {}
		jWbs['subTasks'] 	:= {}
	Else
		jWbs['AFC_PROJET'] 	:= Alltrim(AFC->AFC_PROJET)
		jWbs['AFC_EDT'] 	:= Alltrim(AFC->AFC_EDT)
		jWbs['AFC_EDTPAI'] 	:= Alltrim(AFC->AFC_EDTPAI)
		jWbs['AFC_ORDEM'] 	:= Alltrim(AFC->AFC_ORDEM)
		jWbs['hasAccess'] 	:= .F.
		jWbs['subWBSs'] 	:= {}
		jWbs['subTasks'] 	:= {}
	Endif
	dbSelectArea("AFC")
	AFC->(dbSetOrder(2))
	AFC->(MsSeek(xFilial("AFC") + cProject + cVersion + cWbs))
	While !AFC->(Eof()) .And. AFC->AFC_FILIAL == xFilial("AFC") .And. ;
			AFC->AFC_PROJET == cProject .And. ;
			AFC->AFC_REVISA == cVersion .And. ;
			AFC->AFC_EDTPAI == cWbs

		AAdd(jWbs['subWBSs'],PmsGetWBS(AFC->AFC_PROJET, AFC->AFC_REVISA))
		AFC->(DbSkip())
	Enddo
	dbSelectArea("AF9")
	AF9->(dbSetOrder(2))
	AF9->(MsSeek(xFilial("AF9") + cProject + cVersion + cWbs ))
// procura por tarefas filhas
	While !AF9->(Eof()) .And. AF9->AF9_FILIAL == xFilial("AF9") .And. ;
			AF9->AF9_PROJET == cProject .And. ;
			AF9->AF9_REVISA == cVersion .And. ;
			AF9->AF9_EDTPAI == cWbs

		// verificar as tarefas que o usurio tm direito
		jTask := JsonObject():New()
		If PmsChkUser(AF9->AF9_PROJET, AF9->AF9_TAREFA, , AF9->AF9_EDTPAI, ;
				1 , "ESTRUT", AF9->AF9_REVISA, /*cUser*/)
			jTask['AF9_PROJET'] := Alltrim(AF9->AF9_PROJET)
			jTask['AF9_TAREFA'] := Alltrim(AF9->AF9_TAREFA)
			jTask['AF9_EDTPAI'] := Alltrim(AF9->AF9_EDTPAI)
			jTask['AF9_START'] 	:= Dtos(AF9->AF9_START)
			jTask['AF9_FINISH'] := Dtos(AF9->AF9_FINISH)
			jTask['AF9_REVISA'] := Alltrim(AF9->AF9_REVISA)
			jTask['AF9_DESCRI'] := Alltrim(AF9->AF9_DESCRI)
			jTask['AF9_HORAI'] 	:= Alltrim(AF9->AF9_HORAI)
			jTask['AF9_HORAF'] 	:= Alltrim(AF9->AF9_HORAF)
			jTask['AF9_ORDEM'] := Alltrim(AF9->AF9_ORDEM)
			jTask['hasAccess'] := .T.
			AAdd(jWbs['subTasks'],jTask)
			FreeObj(jTask)
		Else
			jTask['AF9_PROJET'] := Alltrim(AF9->AF9_PROJET)
			jTask['AF9_TAREFA'] := Alltrim(AF9->AF9_TAREFA)
			jTask['AF9_EDTPAI'] := Alltrim(AF9->AF9_EDTPAI)
			jTask['AF9_ORDEM'] := Alltrim(AF9->AF9_ORDEM)
			jTask['hasAccess'] := .F.
			AAdd(jWbs['subTasks'],jTask)

		EndIf
		FreeObj(jTask)

		AF9->(dbSkip())
	End

	RestArea(aAreaAFC)
	RestArea(aAreaAF9)
	RestArea(aArea)

Return jWbs

/*/{Protheus.doc} ru44w120_getrestcfg
Get Browse information for PMS portal
@type function
@author Bruno Sobieski
@since 23/10/2023
@param cAlias, character, Alias to be read
@Return Json, Json Object
/*/
	@Get("/api/pms/v1/ru44w120/getrestcfg/browse/:alias")
Function ru44w120_getrestcfg()
	Local jResponse := jsonObject():New() As Json
	Local jPath     := jsonObject():New() As Json
	Local jQuery := jsonObject():New() As Json
	Local cAlias := ''
	Local jHeader:= oRest:getHeaderRequest()
	If (jHeader <> Nil .And. jHeader['Accept-Language'] <> Nil .And.  lower(jHeader['Accept-Language']) $ 'pt,es,en,ru,pt-br,pt-pt')
		if upper(jHeader['Accept-Language']) <> upper(fwRetIdiom())
			FwSetIdiom(jHeader['Accept-Language'])
		Endif
	EndIf
	jQuery	:= oRest:GetQueryRequest()
	jPath 	:= oRest:getPathParamsRequest()

	If (jPath <> Nil )
		cAlias:=	jPath['alias']
	EndIf

//AVOID ERROR IN GETAREA() WHEN NOT AREA IS SELECTED
	DbSelectArea('SM0')


	If (cAlias <> '')
		jResponse := GetSX3Data(cAlias)
	Else
		//SetFault
	EndIf

	If jResponse <> Nil
		oRest:setResponse(jResponse)
	EndIf

Return

Static Function GetSX3Data(cAlias)
	Local cError:=""
	Local aArea := getArea()
	Local aModels := getModels()
	Local nPos:=0
	Local cModel:=""
	Local jResponse := jsonObject():New() As Json
	Local jField := jsonObject():New() As Json
	Local aFields:= Array(0) as Array
	Local cTitle as character
	Local aFieldsVirt := {} as Array
	Local aFieldsToAdd := {} as Array
	Local nX as numeric
	Local nPosField as numeric
	Local nRecSX3 as numeric
	Local nMaxSize := 0 as numeric
	Local jTemp as Json
	DbSelectArea('SX2')
	DbSetOrder(1)
	If !Empty(cAlias).and.Len(cAlias)==3 .And. DBseek(cAlias)
		If (nPos := Ascan(aModels,{|x| x[1]==cAlias})) > 0
			cModel   := aModels[nPos][2]
		Else
			cModel   := X2_SYSOBJ
		Endif
		cTitle := X2Nome()
		aFieldsVirt := getVirtualFields(cAlias)
		DbSelectArea('SX3')
		DBSetOrder(1)
		dbseek(cAlias)
		While !EOF() .And. X3_ARQUIVO==cAlias
			If X3_BROWSE=="S" .And. X3_CONTEXT <> "V"
				jField["field"]:=Alltrim(X3_CAMPO)
				jField["title"]:=Alltrim(X3TITULO())
				jField["description"]:=Alltrim(X3Descric())
				jField["type"]:=X3_TIPO
				jField["size"]:=ALLTRIM(STR(X3_TAMANHO))
				If !Empty(SX3->X3_F3) .And. RUSRESTFW1_LUpsDefJson(X3_F3)['success']
					jField["lookup"] := Alltrim(X3_F3)
				Else
					jField["lookup"] := ""
				Endif
				jField["allowFiltering"]:= .T.
				nMaxSize := X3_TAMANHO
				jField["screenSize"]:=nMaxSize
				if !Empty(x3cbox())
					jField["options"]:= X3Cbox2DxLookup(Alltrim(X3CBOX()),SX3->X3_TAMANHO,@nMaxSize)
					jField["allowFiltering"]:= .F.
					jField["lookup"]:= ""
					jField["screenSize"] := nMaxSize
				ElseIf !Empty(SX3->X3_F3) 
					jTemp := SXB2DxLookup(Alltrim(SX3->X3_F3),@nMaxSize)
					If jTemp <> Nil
						jField["options"]:= jTemp
						jField["allowFiltering"]:= .F.
						jField["lookup"]:= ""
						jField["screenSize"] := nMaxSize
					Endif
				Endif
				aadd(aFIELDS,jField)
				FreeObj(jField)
				jField := jsonObject():New()
				If (nPosField := Ascan(aFieldsVirt,{|x| x[1] == Alltrim(x3_campo) })) > 0
					nRecSX3 := SX3->(Recno())
					DbSelectArea('SX3')
					DBSetOrder(2)
					aFieldsToAdd := aFieldsVirt[nPosField][2]
					For nX:= 1 To Len(aFieldsToAdd)
						If SX3->(Dbseek(aFieldsToAdd[nX][1]))
							jField["field"]:=Alltrim(X3_CAMPO)
							jField["title"]:=aFieldsToAdd[nX][2]
							jField["description"]:=aFieldsToAdd[nX][3]
							jField["type"]:=X3_TIPO
							jField["size"]:=ALLTRIM(STR(X3_TAMANHO))
							If !Empty(SX3->X3_F3) .And. RUSRESTFW1_LUpsDefJson(X3_F3)['success']
								jField["lookup"] := Alltrim(X3_F3)
							Else
								jField["lookup"] := ""
							Endif
							jField["allowFiltering"]:= .T.
							nMaxSize := X3_TAMANHO
							jField["screenSize"]:=nMaxSize
							if !Empty(x3cbox())
								jField["options"]:= X3Cbox2DxLookup(Alltrim(X3CBOX()),SX3->X3_TAMANHO,@nMaxSize)
								jField["allowFiltering"]:= .F.
								jField["lookup"]:= ""
								jField["screenSize"]:=nMaxSize
							ElseIf !Empty(SX3->X3_F3) 
								jTemp := SXB2DxLookup(Alltrim(SX3->X3_F3),@nMaxSize)
								If jTemp <> Nil
									jField["options"]:= jTemp
									jField["allowFiltering"]:= .F.
									jField["lookup"]:= ""
									jField["screenSize"]:=nMaxSize
								Endif
							Endif
							aadd(aFIELDS,jField)
							FreeObj(jField)
							jField := jsonObject():New()
						Endif	
					Next
					DbSelectArea('SX3')
					DBSetOrder(1)
					DbGoto(nRecSX3)
				Endif
			Endif
			DbSkip()
		Enddo

		If Len(aFields) == 0
			cError:= "No fields found for Alias "+cAlias
		Else
			jResponse['data'] 	:= jsonObject():New()
			jResponse['data']['fields'] := aFields
			jResponse['title'] 	:= Alltrim(cTitle)
			jResponse['model'] 	:= Alltrim(cModel)
			jResponse["status"]	:= 200
			jResponse["statusText"]:= "success"
			jResponse["ok"]:= .T.

		Endif
	Else
		cError:= "Alias "+cAlias+" not found"
	Endif
	If !Empty(cError)
		jResponse["status"] := 601
		jResponse["statusText"] :="failed"
		jResponse["ok"] := .F.
		jResponse["message"] :=cError
		jResponse["error"] :=jsonObject():New()
		jResponse["error"]["errorCode"]:=601
		jResponse["error"]["errorMessage"]:=cError
		oRest:setStatusCode( 601 )
		oRest:setFault( cError )
		//	SetRestFault(601,cError)
	Endif
	RestArea(aArea)
Return jResponse

Static Function getModels()
	Local aRet := {{'AF8','RU44W200'},{'AFU','RU44W300'},{'AFF','RU44W310'},{'AJK','RU44W700'}}
	//Local aRet := {{'AF8','AF8FWMODEL'},{'AFU','AFUFWMODEL'},{'AFF','AFFFWMODEL'},{'AJK','AJKFWMODEL'}}
Return aRet

/*/{Protheus.doc} getVirtualFields
Virtual fields to be listed on portal Browse, they are fixed, because the virtual fields need to be managed on the model
This is needed for the FILTERS on the oData object to work Properly
@type function
@version  1.0
@author bsobieski
@since 26/10/2023
@param cTable, character, Table to be listed
@return array, Fields to be listed
/*/
Static Function getVirtualFields(cTable)
Local aFields := {}
Local aFieldsTMP := {}
Local nX
//Any field added here, must be managed on ru44x100 function GetFromQryAlias
//Any field added here, must be DEFINED on MODEL
Do Case 
Case cTable == "AFF"
	aadd(aFields,{"AFF_PROJET", {{ "AF8_DESCRI" , "Project" 	,"Project description"}}}) 
	aadd(aFields,{"AFF_TAREFA", {{ "AF9_DESCRI" , "Task" 		,"Task Description"}}}) 
	aadd(aFields,{"AFF_QUANT", 	{{ "AFF_PERC"	, "Completion" 	,"Completion percentage"}}}) 
Case cTable $ "AFU"
	aadd(aFields,{"AFU_PROJET", {{"AF8_DESCRI", "Project" 	,"Project description"}}}) 
	aadd(aFields,{"AFU_TAREFA", {{"AF9_DESCRI", "Task" 		,"Task description"}}}) 
	aadd(aFields,{"AFU_RECURS", {{"AE8_DESCRI", "Resource" 	,"Resource name"},{"AE8_EQUIP",  "Team Code" ,"Team code"},{"AED_DESCRI",  "Team name" ,"Team name"}}}) 
Case cTable $ "AJK"
	aadd(aFields,{"AJK_PROJET", {{"AF8_DESCRI", "Project" 	,"Project description"}}}) 
	aadd(aFields,{"AJK_TAREFA", {{"AF9_DESCRI", "Task" 		,"Task description"}}}) 
	aadd(aFields,{"AJK_RECURS", {{"AE8_DESCRI", "Resource" 	,"Resource name"},{"AE8_EQUIP",  "Team Code" ,"Team code"},{"AED_DESCRI",  "Team name" ,"Team name"}}}) 
End Case

If ExistBlock('RU44W120')
	aFieldsTMP := ExecBlock('RU44W120',.F.,.F.,{'GETVIRTUALFIELDS',aFields,cTable})
	If aFieldsTMP <> Nil .And. Valtype(aFIELDSTMP)=="A" .And. Len(aFIELDSTMP) > 0
		For nX:=1 To Len(aFieldsTMP)
			AAdd(aFields,aFieldsTMP[nX])
		Next
	Endif
Endif

Return aFields

Static Function X3Cbox2DxLookup(cBox,nTam,nMaxSize)
Local aCombo	:=	RetSX3Box(cBox,,, nTam)
Local nX
Local jJsonAux as Json
Local jJsonRet := JsonObject(): New()
jJsonRet['dataSource'] := {} 
jJsonRet['valueExpr'] := 'id'
jJsonRet['displayExpr'] := 'label'
jJsonAux :=  {"id": "", "label": "Empty"}
If Len(jJsonAux['label']) > nMaxSize
	nMaxSize := Len(jJsonAux['label'])
Endif
aAdd(jJsonRet['dataSource'], jJsonAux)
FreeObj(jJsonAux)
For nX:=1 To Len(aCombo)
	If  !Empty(AllTrim(aCombo[nX,2])) .And. !Empty(AllTrim(aCombo[nX,3]))
		jJsonAux :=  {"id": AllTrim(aCombo[nx,2]), "label": AllTrim(aCombo[nX,3])}
		If Len(jJsonAux['label']) > nMaxSize
			nMaxSize := Len(jJsonAux['label'])
		Endif
		aAdd(jJsonRet['dataSource'], jJsonAux)
		FreeObj(jJsonAux)
	Endif
Next
Return jJsonRet
/*
Static Function SX5DxLookup(cTable, cMaxSize)
Local aArea := GetArea() as Array
Local jJsonAux as Json
Local jJsonRet := JsonObject(): New()
Local nMaxSize := Val(cMaxSize)
jJsonRet['dataSource'] := {} 
jJsonRet['valueExpr'] := 'id'
jJsonRet['displayExpr'] := 'label'
DbSelectArea("SX5")
DbSetOrder(1)
DbSeek(xFilial()+ cTable )
jJsonAux :=  {"id": "", "label": "Empty"}
If Len(jJsonAux['label']) > nMaxSize
	nMaxSize := Len(jJsonAux['label'])
Endif
aAdd(jJsonRet['dataSource'], jJsonAux)
FreeObj(jJsonAux)
While !Eof() .and. X5_TABELA == cTable
	jJsonAux :=  {"id": Alltrim(X5_CHAVE), "label": AllTrim(X5Descri())}
	aAdd(jJsonRet['dataSource'], jJsonAux)
	If Len(jJsonAux['label']) > nMaxSize
		nMaxSize := Len(jJsonAux['label'])
	Endif
	FreeObj(jJsonAux)
	DbSkip()
Enddo

cMaxSize := Alltrim(Str(nMaxSize))
RestArea(aArea)
Return jJsonRet
*/

/*/{Protheus.doc} SXB2DxLookup
Converts SXB lookup into fixed lookup. use carefully only for SXB that are supposed to have few data
@type function
@version  1.0
@author bsobieski
@since 04/04/2024
@param cSXB, character, SXB lookup code
@param cMaxSize, character, Maximum sixe of the texts retrieved
@return json, Devextreme lookup data
/*/
Static Function SXB2DxLookup(cSXB, nMaxSize)
Local aArea := GetArea() as Array
Local jJsonAux as Json
Local jJsonRet as Json //:= JsonObject(): New()
Local bIdField 	 
Local bLabelField
Local cAlias := cSXB

Do Case
Case cSXB == "AE7"
	bIdField 	:= {|| AE7_CODIGO}
	bLabelField := {|| AE7_DESCRI}
	bSeek		:= {|| xFilial() }
	bWhile		:= {|| xFilial() == AE7_FILIAL }
// Case cSXB == "AED"
// 	bIdField 	:= {|| AED_EQUIP}
// 	bLabelField := {|| AED_DESCRI}
// 	bSeek		:= {|| xFilial() }
// 	bWhile		:= {|| xFilial() == AED_FILIAL }
Case cSXB == "NNR"
	bIdField 	:= {|| NNR_CODIGO}
	bLabelField := {|| NNR_DESCRI}
	bSeek		:= {|| xFilial() }
	bWhile		:= {|| xFilial() == NNR_FILIAL }
Case cSXB == "AEA"
	bIdField 	:= {|| AEA_COD}
	bLabelField := {|| AEA_DESCRI}
	bSeek		:= {|| xFilial() }
	bWhile		:= {|| xFilial() == AEA_FILIAL }
Case cSXB == "SH7"
	bIdField 	:= {|| H7_CODIGO}
	bLabelField := {|| H7_DESCRI}
	bSeek		:= {|| xFilial() }
	bWhile		:= {|| xFilial() == H7_FILIAL }
Case Len(cSXB) == 2
	bIdField 	:= {|| X5_CHAVE}
	bLabelField := {|| X5DESCRI()}
	bSeek		:= {|| xFilial() + cSXB }
	bWhile		:= {|| xFilial() == X5_FILIAL .And. X5_TABELA==cSXB }
	cAlias 		:= "SX5"
EndCase
If bIdField <> Nil
	jJsonRet := JsonObject(): New()
	jJsonRet['dataSource'] := {} 
	jJsonRet['valueExpr'] := 'id'
	jJsonRet['displayExpr'] := 'label'
	DbSelectArea(cAlias)
	DbSetOrder(1)
	DbSeek(Eval(bSeek))
	jJsonAux :=  {"id": "", "label": "Empty"}
	If Len(jJsonAux['label']) > nMaxSize
		nMaxSize := Len(jJsonAux['label'])
	Endif
	aAdd(jJsonRet['dataSource'], jJsonAux)
	FreeObj(jJsonAux)
	While !EOF() .And. Eval(bWhile)
		jJsonAux :=  {"id": AllTrim(Eval(bIdField)), "label": AllTrim(Eval(bLabelField))}
		aAdd(jJsonRet['dataSource'], jJsonAux)
		If Len(jJsonAux['label']) > nMaxSize
			nMaxSize := Len(jJsonAux['label'])
		Endif
		FreeObj(jJsonAux)
		DbSkip()
	Enddo
Endif
RestArea(aArea)
Return jJsonRet

                   
//Merge Russia R14 
                   
