#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOTVS.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "PONCALEN.CH"
#INCLUDE "FWADAPTEREAI.CH"
#INCLUDE "GPEA1240.CH"

#DEFINE STR0999 "Enter the name of the employee (maximum - 96 characters, including spaces)"


Static cCpo			:= ""
Static cUltSeq		:= ""
Static lTSREP		:= SuperGetMv( "MV_TSREP" , NIL , .F. )
Static lExec		:= .T.
Static lFechado		:= .F.
Static cEFDAviso
Static lIntegDef 	:= FindFunction("GETROTINTEG") .And. FindFunction("FWHASEAI")
Static oGridTSA		:= Nil
Static oGridOld		:= Nil
// Integration with TAF
Static lRHTAF		:=  .F.
Static dDcgIni		:= SuperGetMV( "MV_DTCGINI", Nil, CTOD(" / / ") )
Static lGestPubl 	:= if(ExistFunc("fUsaGFP"),fUsaGFP() ,.f.)	// Checks if it uses the Public Sheet Management module - SIGAGFP


/*
{Protheus.doc} GPEA240RUS
    Absence Register File (Russia)

    @author A. Fedorova
    @since 11/06/2019
    @version 1.0
    @project DMA3 - Russia
*/
Function GPEA240RUS(nOpcAuto,xAutoCab,xAutoItens,nOpc)

Local aIndexSRA		:= {}           // Variable for Filter
Local aRegs		 	:= {}
Local aArea			:= {}
Local cFiltraSRA	:= ""           // Variable for filter
Local cMsgAlert		:= ""           // Variable for message to user
Local cFilAux       := ""
Local cMatAux		:= ""

Local bFiltraBrw 	:= {|| Nil}     // Variable for Filter

Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .T. , .F. )
Local cCliMdtPs	 := ""
Local nSizeSA1   := 0
Local nSizeLo1   := 0
Local nSizeTD    := 0
Local nPos		 := 0
Local lGp240Auto := .F.
Private aAutoCab	:= {}
Private aAutoItens	:= {}
Private aPerAtual	:= {}
Private cProcesso	:= ""
Private nColPro	 	:= 2
Private oModlBkp	:= Nil
Private aSR8Respal  := {}
Private lGP240VSUB 	:= ExistBlock("GP240VSUB")
Private cFilFun		:= xFilial("SRA")


cCadastro  := OemToAnsi(STR0009)  // "Leave File"
lGp240Auto := ( xAutoCab != Nil )

cEFDAviso  := "0"           // If it does not find this parameter, it will only issue alerts


If lRHTAF .AND. !IsBlind()
	fAlertJob()
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Check if the file is empty                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !ChkVazio("SRA")
	Return
EndIf

If (Type( "lResM2" ) <> "U" .And. lResM2) // Treatment for calling the routine through the termination screen (Model 2)
	If FWChkFuncAccess( "GPEA240RUS", 4, .T.)
		FWExecView(STR0009,'GPEA240RUS', 4, , { || .T. } ) // "Leave File"
	ElseIf FWChkFuncAccess( "GPEA240RUS", 2, .T.)
		Aviso( STR0021, STR0061, {STR0062}) // "Warning" ## "Permission only to display the records." ## "OK"
		FWExecView(STR0009,'GPEA240RUS', 2, , { || .T. } ) // "Leave File"
	Else
		Aviso( STR0021, STR0060, {STR0062}) // "Warning" ## "The user has no permission to access this routine." ## "OK"
	EndIf
EndIf

If IsInCallStack("TCFA040")
	_SetOwnerPrvt( "nOpcNewGd", GD_UPDATE )
Else
	_SetOwnerPrvt( "nOpcNewGd", If( nOpc == 2 .or. nOpc == 5 , 0 , GD_INSERT + GD_UPDATE + GD_DELETE))
EndIf

If (Type( "lResM2" ) == "U" .Or. !lResM2)

	If nOpcAuto <> Nil
		nOpcao := nOpcAuto
	EndIf

	If lGp240Auto .or. nOpcAuto <> Nil
		If lGp240Auto
			aArea		:= GetArea()
			aAutoCab	:= aClone(xAutoCab)
			aAutoItens	:= aClone(xAutoItens)
			nOpcao 	:= If(nOpc<>Nil,nOpc,nOpcao)
			nOpcao		:= If(nOpcao == 3, 4, nOpcao)
			aRotina 	:= MenuDef()


			If (nPos := aScan(aAutoCab,{|x| UPPER(Alltrim(x[1]))=='RA_FILIAL'})) > 0
				cFilAux := aAutoCab[nPos,2]
			Else
				cFilAux := xFilial("SRA")
			EndIf
			If (nPos := aScan(aAutoCab,{|x| UPPER(Alltrim(x[1]))=='RA_MAT'})) > 0
				cMatAux := aAutoCab[nPos,2]
			EndIf

			DbSelectArea('SRA')
			DbSetOrder(1)

			If !dbSeek(cFilAux+cMatAux)
				AutoGrLog(STR0084 + " " + Alias() + " " + Dtoc(MsDate()) + ' ' + Time() ) // "Table"
				AutoGrLog("RA_FILIAL: " + cFilAux)
				AutoGrLog("RA_MAT: " + cMatAux)
				AutoGrLog(STR0085) // "Employee not found with data above"
				AutoGrLog(Replicate("-",80))
			Else
				oModelAuto := ModelDef()
				If GP240Ini(oModelAuto,nOpcao)
					/*Quando se desejar alterar determinada linha é necessário passar uma posição a mais
					no item no seguinte formato : aAdd(aItem,{"LINPOS","R8_FILIAL+R8_MAT+R8_DATAINI",xFilial("SR8"),SRA->RA_MAT,dStart})*/
					FWMVCRotAuto(oModelAuto,"SRA",nOpcao,{{"GPEA240_SRA",aAutoCab},{"GPEA240_SR8",aAutoItens}})
				EndIf
				/*
					O propósito das linhas a seguir é limpar o objeto da memória para que não dê
					"Reference counter overflow ( over 32600 ) on CodeBlock assignment", erro que acontecia
					quando o ExecAuto era chamado mais de 3000x consecutivas.
					Mais informações: http://tdn.totvs.com/pages/viewpage.action?pageId=146179637
				*/
				oModelAuto:Deactivate()
				oModelAuto:Destroy()
				oModelAuto:= nil
				aSize(aAutoCab,0)
				aAutoCab:= nil
				aSize(aAutoItens,0)
				aAutoItens:= nil
			EndIf
			RestArea(aArea)
		Else
			FWExecView(STR0009,'GPEA240RUS', nOpcao, , { || .T. } ) // "Leave File"
		EndIf
	Else 
        oBrowse := BrowseDef()
        oBrowse:Activate()
    EndIf

EndIf

Return 

/*
{Protheus.doc} BrowseDef
    Browse definition

    @author A. Fedorova
    @since 11/06/2019
    @version 1.0
    @project DMA3 - Russia
*/
Static Function BrowseDef()
    Local oBrowse As Object
    Local cCliMdtPs	 := ""
    Local cFiltraRh	 := ""
    Local lSigaMdtPS := If( SuperGetMv("MV_MDTPS",.F.,"N") == "S", .T. , .F. )
    Local nSizeSA1   := 0
    Local nSizeLo1   := 0
    Local nSizeTD    := 0

    oBrowse := FWMBrowse():New()
    oBrowse:SetAlias('SRA')
    oBrowse:SetDescription(STR0009) // "Leave File"
    GpLegend(@oBrowse,.T.) // Create caption
    oBrowse:SetMenuDef( 'GPEA240RUS' )
    oBrowse:SetChgAll(.F.)

    If lSigaMdtps

        dbSelectArea("SA1")
        DbSetOrder(1)

        nSizeSA1   := If((TAMSX3("A1_COD")[1]) < 1,6,(TAMSX3("A1_COD")[1]))
        nSizeLo1   := If((TAMSX3("A1_LOJA")[1]) < 1,2,(TAMSX3("A1_LOJA")[1]))
        nSizeTD    := nSizeSA1+nSizeLo1

        cCliMdtPs := SA1->A1_COD+SA1->A1_LOJA
        cFiltraRh := ChkRh("GPEA240RUS","SRA","1")
        cFiltraRh += IF(!Empty(cFiltraRh),' .and. ','')
        cFiltraRh += 'SubStr(SRA->RA_CC,1,' +AllTrim(Str(nSizeTD))+ ') == "' + cCliMdtps + '"'

        oBrowse:SetFilterDefault(cFiltraRh)

    Else

        //------------------------------------------
        // Search for the filter to be used in the Browse
        //------------------------------------------
        cFiltraRh 	:= CHKRH("GPEA240RUS","SRA","1")
        If ValType(cFiltraRh) == "L"
            cFiltraRh := If(cFiltraRh,".T.",".F.")
        EndIf

        // Browse standard filter according to SRW table (User Restrictions Control)
        oBrowse:SetFilterDefault(cFiltraRh)

    EndIf
Return oBrowse

/*
{Protheus.doc} MenuDef
    Menu definition

    @author A. Fedorova
    @since 11/06/2019
    @version 1.0
    @project DMA3 - Russia
*/
Static Function MenuDef()
Local aRotina 		:=  {}

ADD OPTION aRotina TITLE STR0004 ACTION 'PesqBrw'          	OPERATION 1 ACCESS 0 // "Search"
ADD OPTION aRotina TITLE STR0005 ACTION 'VIEWDEF.GPEA240RUS'	OPERATION 2 ACCESS 0 // "View"
ADD OPTION aRotina TITLE STR0082 ACTION 'VIEWDEF.GPEA240RUS' 	OPERATION 3 ACCESS 0 // "Retype"
ADD OPTION aRotina TITLE STR0008 ACTION 'VIEWDEF.GPEA240RUS' 	OPERATION 5 ACCESS 0 // "Delete"


If lGestPubl .AND. cModulo $ 'GFP*VDF'
	ADD OPTION aRotina TITLE STR0228 ACTION 'GPER580' 	OPERATION 7 ACCESS 0 // "Leave Report"
EndIf

Return aRotina

/*
{Protheus.doc} ModelDef
    Model definition

    @author A. Fedorova
    @since 11/06/2019
    @version 1.0
    @project DMA3 - Russia
*/
Static Function ModelDef()

    Local oModel     As Object
    Local oStructSRA As Object
    Local oStructSR8 As Object

    oModel := FWLoadModel("GPEA240")

    oStructSRA := FWFormStruct(1, "SRA", { |cCampo| AllTrim(cCampo) + "|" $ "RA_FILIAL|RA_MAT|RA_NOMECMP|RA_ADMISSA|" } )

    oStructSR8 := oModel:GetModel("GPEA240_SR8")
    oStructSR8:GetStruct():AddField(FwX3Titulo("RCM_CTK209"), X3Descric("RCM_CTK209"), "R8_CTK209", GetSx3Cache("RCM_CTK209" , "X3_TIPO"), TamSx3("RCM_CTK209")[1], TamSx3("RCM_CTK209")[2],,,,, ;
    {|a,b,c| FWInitCpo(a,b,c),xRet:=(IIF(INCLUI, "", FDESC("RCM", SR8->R8_TIPOAFA, "RCM_CTK209", , xFilial("RCM"), 1))),FWCloseCpo(a,b,c,.T.),FwSetVarMem(a,b,xRet),xRet },/*.F.*/,,.T. )
    oStructSR8:GetStruct():AddTrigger( "R8_TIPOAFA" , "R8_CTK209", {|| .T. } , {|| FDESC("RCM", M->R8_TIPOAFA, "RCM_CTK209", , xFilial("RCM"), 1) } )
    oStructSR8:GetStruct():AddField(FwX3Titulo("RCM_LIMPDL"), X3Descric("RCM_LIMPDL"), "R8_LIMPDL", GetSx3Cache("RCM_LIMPDL" , "X3_TIPO"), TamSx3("RCM_LIMPDL")[1], TamSx3("RCM_LIMPDL")[2],,,,, ;
    {|a,b,c| FWInitCpo(a,b,c),xRet:=(IIF(INCLUI, "", FDESC("RCM", SR8->R8_TIPOAFA, "RCM_LIMPDL", , xFilial("RCM"), 1))),FWCloseCpo(a,b,c,.T.),FwSetVarMem(a,b,xRet),xRet },/*.F.*/,,.T. )
    oStructSR8:GetStruct():AddTrigger( "R8_TIPOAFA" , "R8_LIMPDL", {|| .T. } , {|| FDESC("RCM", M->R8_TIPOAFA, "RCM_LIMPDL", , xFilial("RCM"), 1) } )

    oModel:GetModel("GPEA240_SRA"):SetStruct(oStructSRA)

Return oModel

/*
{Protheus.doc} ViewDef
    View definition

    @author A. Fedorova
    @since 11/06/2019
    @version 1.0
    @project DMA3 - Russia
*/
Static Function ViewDef()
    
    Local oView      As Object
    Local oStructSRA As Object
    Local oStructSR8 As Object
    Local aTemp      As Array
    Local nI         As Numeric

    oView := FWLoadView("GPEA240")

    oStructSRA := oView:GetViewStruct("GPEA240_SRA")
    oStructSRA:RemoveField("RA_NOME")
    oStructSRA:AddField("RA_NOMECMP", X3Ordem("RA_NOMECMP"), X3Titulo("RA_NOMECMP"), X3Descric("RA_NOMECMP"), Nil, "C")

    oStructSR8 := oView:GetViewStruct("GPEA240_SR8")
    aTemp := X3CboxToArray("RCM_CTK209")[1] 
    For nI := 1 To Len(aTemp)
        aTemp[nI] := SubStr(aTemp[nI], 1, 1) + "=" + SubStr(aTemp[nI], 5)
    Next nI
    Aadd(aTemp, " ")
    oStructSR8:AddField("R8_CTK209", "68", FwX3Titulo("RCM_CTK209"), X3Descric("RCM_CTK209"), Nil, GetSx3Cache("RCM_CTK209" , "X3_TIPO"),,,,.F.,,,aTemp,,, .T., X3Picture("RCM_CTK209"))
    oStructSR8:AddField("R8_LIMPDL", "69", FwX3Titulo("RCM_LIMPDL"), X3Descric("RCM_LIMPDL"), Nil, GetSx3Cache("RCM_LIMPDL" , "X3_TIPO"),,,,.F.,,,,,, .T., X3Picture("RCM_LIMPDL"))
    oStructSR8:RemoveField("R8_ABROAD")

Return oView


Static Function GP240Ini(oModel,nOperation)
Local cRotAux		:= ""
Local lRet 			:= .T.

// Assigns the employee's branch in cFilAnt
cFilAnt := SRA->RA_FILIAL
lRHTAF		:= ((SuperGetMv("MV_RHTAF",, .F.) == .T.) .AND. Val(SuperGetMv("MV_FASESOC",/*lHelp*/,' ')) >= 1 )

// BACKING UP THE MODEL BEFORE CHANGING IT

If !(IsInCallStack("FWMILEIMPORT")) .And. nOperation == 3
	oModel:SetOperation( 4 )
EndIf

If nOperation == MODEL_OPERATION_DELETE
	nRet := Gp240Sr8Null()

	If nRet == 1
    	Help(,,'HELP',, STR0025,1,0 ) //"There is no registration of days entitled for the employee."
	   	lRet := .F.
	ElseIf nRet == 2
    	Help(,,'HELP',, STR0096,1,0 ) //"Deletion not allowed due to calculated transactions and paid days."
	   	lRet := .F.
		
	EndIf
Else
	cProcesso := SRA->RA_PROCES
	aPerAtual := {}
	cRotAux := If (SRA->RA_CATFUNC $ "P*A", fGetCalcRot("9"),fGetRotOrdinar())
	fGetPerAtual( @aPerAtual, xFilial("RCH", SRA->RA_FILIAL), SRA->RA_PROCES, cRotAux )
EndIf

cCpo 		:= ""
lExec 		:= .T.

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Gp240LoadºAutor  ³Leandro Drumond   º Data ³  26/04/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Routine used to load alternative grids        			  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPE                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Gp240Load(oModel)


If lTSREP
	 Gp240TSA(oModel)
endIf

If lRHTAF
	Gp240TAF(oModel)
EndIf

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Gp240Sr8NullºAutor  ³Leandro Drumond   º Data ³  26/04/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Checks if there is a record in SR8 for current employee	  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPE                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Gp240TSA(oModel)

oGridTSA 	:= oModel:GetModel("GPEA240_SR8")

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Gp240LoadTAFºAutor  ³Eduardo Vicente   º Data ³  02/05/17   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Alternative oGrid load for TAF    		    			  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPE                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Gp240TAF(oModel)
oModlBkp	:= oModel:GetModel("GPEA240_SR8")

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Gp240Sr8NullºAutor  ³Leandro Drumond   º Data ³  26/04/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Checks if there is a record in SR8 for current employee	  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPE                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Gp240Sr8Null()
Local aArea := GetArea()
Local nRet	:= 1

DbSelectArea("SR8")

If DbSeek(xFilial("SRA")+SRA->RA_MAT)
	nRet := 0
	While SR8->(!Eof() .and. R8_FILIAL == xFilial("SRA") .and. R8_MAT == SRA->RA_MAT)
		If R8_STATUS == "C" .and. R8_DPAGOS > 0
			nRet := 2
			Exit
		EndIf
		DbSkip()
	EndDo
EndIf

RestArea(aArea)

Return nRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp240TudOk³ Autor ³ Leandro Drumond       ³ Data ³ 18.04/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function gp240TudOk(oModel)
Local aQtdAfa		:= {}
Local aDados		:= {}
Local cPeriodo		:= ""
Local cTipoAfa		:= ""
Local cMensaje     	:= STR0163+CHR(13)+CHR(10) // "A new IPP is added for the incapacity"
Local cSucur      	:= xFilial("RIV")

Local lRetorna    	:= .T.
Local lNuevas      	:= .F.
Local lIngMDTGPE:= SuperGetMv("MV_MDTGPE",.F.,"N") == "S" .And. FindFunction( "MDTGPEAfas" )

Local nLinha		:= 0
Local nPos		    := 0
Local nLenGrid		:= 0
Local nLinAtual		:= 0

Local oGrid			:= oModel:GetModel("GPEA240_SR8")
Local cVerba		:= ""
Local lGP240OK		:= ExistBlock("GP240OK")
Local lTemSRD	  := fTemSRD()// customer that only uses electronic point should not do the check, as a way of testing we decided to see if it has a record in the SRD

If !lTemSRD
	return .T.
EndIf

nLinAtual := oGrid:GetLine()
nLenGrid  := oGrid:Length()

SRV->(DbSetOrder(1))

// Validate records by checking for overlap for types of
// distances that do not allow overlapping.
aSR8Respal := Array(0)
For nLinha := 1 to nLenGrid
	oGrid:GoLine(nLinha)
	If !( oGrid:IsDeleted() )
		lRetorna := iif(oGrid:IsUpdated() .OR. oGrid:IsInserted(),gp240LinOk(oGrid), .T. )
		If lRetorna .and. lGP240OK
			lRetorna := ExecBlock("GP240OK", .F., .F. )
		EndIf
		If lRetorna
			cPeriodo := oGrid:GetValue("R8_PER")
			cTipoAfa := oGrid:GetValue("R8_TIPOAFA")
			cVerba	  := oGrid:GetValue("R8_PD")

			If ( nPos := aScan(aQtdAfa,{|x|x[1] + x[2] + x[4] == cPeriodo + cTipoAfa + cVerba}) ) == 0
				aAdd(aQtdAfa,{cPeriodo, cTipoAfa , 1, cVerba})
			Else
				aQtdAfa[nPos,3] += 1
			EndIf
			If ( nPos := aScan(aQtdAfa,{|x|x[3] > 9} ) ) > 0
				if(SRV->(DbSeek(xFilial("SRV") + aQtdAfa[nPos,4])))
					if(SRV->RV_LCTODIA != "S")
						lRetorna := .F.
						Help( ,, 'HELP',, OemToAnsi(STR0079)+CRLF+OemToAnsi(STR0080)+aQtdAfa[nPos,2]+CRLF+OemToAnsi(STR0081)+aQtdAfa[nPos,1], 1, 0) // "Limit of entries of the same type for the same period. Register a new type of absence or change payroll item to accept more entries."##"Absence Type:"##"Period:"
					endIf
				endIf
			EndIf
		EndIf
	EndIf

	

	If !lRetorna
		Exit
	Else
		If Empty(oGrid:GetValue("R8_DATAFIM")) .and. Empty(oGrid:GetValue("R8_DURACAO"))
			oGrid:SetValue("R8_DURACAO",999)
		EndIf

		//----------------------------------------------------------------
		// When the line evaluation is OK
		// And there is integration with the Medicine module
		// Checks the CAT code with TNC - Accidents. To maintain CID code integrity.
		//-----------------------------------------------------------------
		If lIngMDTGPE
			MDTGPEAfas( oModel )
		EndIF
	EndIf


Next nLinha
cMensaje += STR0164 // "do not forget to indicate IPP percentage"

oGrid:GoLine(nLinAtual)

If lNuevas
	lRetorna := lRetorna .and. (IIF(IsBlind(),.T.,MsgYesNo(cMensaje, STR0001))) // "Confirm"
EndIf
If Findfunction("IntAfast") .And. lRHTAF .And. lRetorna .AND. (fVlAltEsoc(oGrid) .Or. oModel:nOperation == 5)
	lRetorna:= IntAfast(oModel,@aDados)
	If !lRetorna
		Help( , , 'HELP', , If(Len(aDados) > 0, aDados[1], "") , 1, 0 )
	EndIf
EndIf

If lGestPubl .AND. cModulo $ 'GFP*VDF' .AND. lRetorna
	cFilSub := oGrid:GetValue("R8_FILSUB")
	cMatSub := oGrid:GetValue("R8_MATSUB")
	dIni	:= oGrid:GetValue("R8_DATAINI")
	dFim	:= oGrid:GetValue("R8_DATAFIM")
	SR8->(DbGoto(oGrid:GetDataId()))
	RI8->(DbSeek( SRA->RA_FILIAL + SRA->RA_MAT + dtos(SR8->R8_DATAINI)))
	lRetorna := fVerRI8(SRA->RA_FILIAL,SRA->RA_MAT,dIni,dFim,.T.  ,.T.   ,.T.   ,.F.   ,.T.,.F.,iif(oGrid:IsInserted(),Nil, RI8->(Recno())),iif(oGrid:IsInserted(),Nil,oGrid:GetDataId()))
	If !lRetorna
		cMsg := OemToAnsi(STR0107) + cValToChar(nLinha-1) +'!'// "Check SUBSTITUTED of row schedule"
		Help( ,, 'Help',, cMsg, 1, 0 )
	EndIf
	// VERIFIES IF THERE ARE DEVIATIONS OR SUBSTITUTIONS FOR THE SUBSTITUTE IN THE PERIOD IN QUESTION
	If !Empty(cMatSub) .AND. lRetorna  // is Public Management?
		lRetorna := fVerRI8(cFilSub,cMatSub,dIni,dFim,.T.  ,.T.   ,.T.   ,.F.   ,.T.,.F.,iif(oGrid:IsInserted(),Nil, RI8->(Recno())),iif(oGrid:IsInserted(),Nil,oGrid:GetDataId()))
		If !lRetorna
			cMsg := OemToAnsi(STR0108)+ cValToChar(nLinha-1) +'!' // "Check SUBSTITUTE of row schedule"
			Help( ,, 'Help',, cMsg, 1, 0 )
		EndIf
	EndIf
EndIf

Return( lRetorna )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gp240DelOk³ Autor ³ Leandro Drumond       ³ Data ³ 24.04.13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validate line deletion                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Gp240DelOk(oGrid, nLine, cAction, cField)
Local lRet 			:= .T.
Local oModel 		:= oGrid:GetModel()
Local nOperation 	:= oModel:GetOperation()

If cAction == 'DELETE' .And. nOperation == MODEL_OPERATION_UPDATE
	If MPUserHasAccess("GPEA240RUS",4,Nil,Nil,Nil)
		If oGrid:GetValue("R8_STATUS") == "C" .and. oGrid:GetValue("R8_DPAGOS") > 0
			Help(,,'HELP',, STR0078,1,0 ) // "Entries calculated and with days paid cannot be deleted."
	    	lRet := .F.
		
		EndIf
	Else
		Help(,,'HELP',, STR0237,1,0 ) // "User without deletion permission."
		lRet := .F.
	EndIf
EndIf

Return lRet


///////////////////////////////////////
//////////////////////////////////////
Static Function fTemSRD()
Local lTemSRD := .F.
dbSelectArea("SRD")
SRD->(dbGotop())
lTemSRD := !SRD->(Eof())
Return lTemSRD

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp240LinOk³ Autor ³ Leandro Drumond       ³ Data ³ 18.04.13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Criticize typed line                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function gp240LinOk(oGrid)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao de variaveis locais         					     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cVerEnv		:= "2.4"
Local cVerGPE		:= ""
Local cHelp			:= ""
Local cLicEsp		:= ""
Local cAnoAfa		:= ""
Local cTipoAfa		:= ""
Local lRet			:= .T.
Local nTotDias		:= 0
Local nX			:= 0
Local nLenGrid		:= oGrid:Length()
Local nLinAtual		:= oGrid:GetLine()
Local oModel		:= FWModelActivate()
// Support variables eSocial validations
Local lShowEFD		:= .T.
Local cDtSeq		:= ""
// Variable to check if it has already been integrated into the TAF
Local lIntegrado	:= .F.

Local cSindical		:= "24"
Local cCessao  		:= "14"
Local cTexto1		:= "01*02"
Local cTexto2		:= " 01 ou 02"
Local cComplHlp 	:= If(cEFDAviso == "0",OemToAnsi(STR0187),"") 		// "But they are not impediment to finish registering this record, according to configuration parameter MV_EFDAVIS."
Local cTpEFTi		:= ""
Local cTpTi			:= GetSx3Cache("R8_TIPOAT", "X3_TITULO")
Local cTiCnCe		:= GetSx3Cache("R8_CNPJCES", "X3_TITULO")
Local cTiTpCe		:= GetSx3Cache("R8_TPCES", "X3_TITULO")
Local cTiCnSi		:= GetSx3Cache("R8_CNPJSIN", "X3_TITULO")
Local cTiTpSi		:= GetSx3Cache("R8_TPSIND", "X3_TITULO")
Local cTpAusencia	:= ""



If !oGrid:IsDeleted()

	If FindFunction("fVersEsoc")
		fVersEsoc( "S2230", .F., /*aRetGPE*/, /*aRetTAF*/, @cVerEnv , @cVerGPE )
	Endif

	If Empty(cVerGPE)
		cVerGPE := cVerEnv
	EndIf

	Do Case
		Case ( cVerGPE == "2.4" ) ; ( cTexto1 := " 01*03 " ) ; ( cTexto2 := " 01 ou 03 " )
		Case ( cVerGPE >= "2.4.02" ) ; ( cTexto1 := " 01 " ) ; ( cTexto2 := " 01 " )
	End Case

    Begin Sequence
		if (oGrid:GetValue("R8_ORIGALT")=="3" .And. Empty(oGrid:GetValue("R8_NRPRCJU")) )
			cHelp := OemToAnsi( STR0222 ) // "Enter Lawsuit number - Must be a valid lawsuit in Lawsuit table S-1070."
			Help( ,, 'HELP',, cHelp, 1, 0)
			lRet := .F.
			Break
		endIf
		If Empty(oGrid:GetValue("R8_DATAINI")) .OR. ;
			( !Empty(oGrid:GetValue("R8_DATAFIM")) .And. oGrid:GetValue("R8_DATAINI") > oGrid:GetValue("R8_DATAFIM") )
			Help(" ",1,"A240DTINI")
			lRet := .F.
			Break
		EndIf

		// Type D leave allowed only for Interns
	    
		// Validation for reason for leave TPEFD
		/*
			CID - R8_CID
			Number of days of leave - R8_DURACAO
			Doctor's name - R8_NMMED
			Class Body Type - R8_IDEOC
			Class Body Number - R8_CRMMED
			UF of the Class Body - R8_UFCRM
		*/
	    
		If !Empty(SRA->RA_DEMISSA) .and.;
			(oGrid:GetValue("R8_DATAINI") >= SRA->RA_DEMISSA .or.;
			 !Empty(oGrid:GetValue("R8_DATAFIM")) .and. oGrid:GetValue("R8_DATAFIM")>= SRA->RA_DEMISSA)
			Aviso(STR0099, STR0192, {STR0062})//"Warning"##"Unable to register leave with date later than employee dismissal"##"OK"
			lRet := .F.
			Break
		EndIf

		

		If !(lRet := gp240SobrepVerif(oGrid))
			Break
		EndIf

		// The type of leave field must be mandatory.
		If Empty(oGrid:GetValue("R8_TIPOAFA"))
			cHelp := OemToAnsi( STR0018 ) // "Mandatory type of leave"
			Help( ,, 'HELP',, cHelp, 1, 0)
	  		lRet := .F.
	  		Break
		EndIf

		// Payment Period and Number
		If !(lRet := gp240ValidNumPag(oGrid:GetValue("R8_PER"), oGrid:GetValue("R8_NUMPAGO")))
			Break
		EndIf

		If Empty(oGrid:GetValue("R8_STATUS"))
			// Validate if the employee is entitled to the number of days informed.
			If !(lRet := gp240DiasDirValid())
				Break
			EndIf
		EndIf


		If Empty(oGrid:GetValue("R8_DATAFIM")) .and. Len(aPerAtual) >0 .and. oGrid:GetValue("R8_DATAINI") < aPerAtual[1,6]
			
			oGrid:SetValue("R8_DPAGOS", nDiaspagos)
			oGrid:SetValue("R8_SDPAGAR", oGrid:GetValue("R8_DIASEMP")-nDiaspagos)
			
		EndIf

		If SR8->(ColumnPos( "R8_PROADIC")) > 0

			cTpAusencia := gp240RetCont(;
												"RCM", 												; 	// cAlias
												1, 														; 	// nIndex
												"RCM_FILIAL = '" + xFilial("RCM") + "' AND RCM_TIPO = '" + oGrid:GetValue("R8_TIPOAFA") + "'",	; 	// cKey
												"RCM_TIPOAF",;
												,.T.									) 	// return column
			If oGrid:GetValue("R8_PROADIC") == '1' .And. (cTpAusencia == "4" .Or. RCM->RCM_CODSEF $ "Q1*Q2*Q3*Q4*Q5*Q6")  // Calculate Proportional Additional and Absence of the Control Days of Entitlement type
				Help( ,, OemToAnsi(STR0021),, OemToAnsi(STR0220), 1, 0 ) //"Warning"##"Proportional calculation of additionals is not allowed for this type of absence."
				lRet := .F.
				Break
			EndIf
		EndIf

	End Sequence
EndIf

// For chile, validate the change in R8_DATAINI
	

If lRet .and. ExistBlock("GP240VAL")
	lRet := ExecBlock("GP240VAL", .F., .F., {oGrid})
	lRet := If(ValType(lRet) == "L", lRet , .T. )
EndIf

If lRet .AND. lGestPubl	.AND. cModulo $ 'GFP*VDF'
	If RCM->RCM_MDAFAS > oGrid:GetValue("R8_DURACAO")
		lRet := .F.
		cHelp := STR0232 // "Number of days of absence is lower than the minimum defined in the type of absence."
	ElseIf (fDesc("SQ3",SRA->RA_CARGO,"Q3_SUBSTIT",NIL,SRA->RA_FILIAL,1) == "1")
		If !Empty(oGrid:GetValue("R8_MATSUB")) .AND. RCM->RCM_DSUBST > oGrid:GetValue("R8_DURACAO")
			lRet := .F.
			cHelp := STR0231 // "Number of days of absence is lower than the minimum to request a substitute."
		ElseIf Empty(oGrid:GetValue("R8_MATSUB")) .AND. Iif(lGP240VSUB, !ExecBlock("GP240VSUB",.F.,.F.), .T.)
			lRet := .F.
			cHelp := STR0229 // "Position of Employee requires a substitute."
		EndIf
	ElseIf !Empty(oGrid:GetValue("R8_MATSUB"))
		lRet := .F.
		cHelp := STR0230 // "Position of Employee does not require a substitute."
	EndIf
	If !lRet
		Help(NIL, NIL, STR0021, NIL, cHelp, 1, 0, NIL, NIL, NIL, NIL, NIL, {""}) // "Warning"
	EndIf
EndIf
If lRet
	lExec := .T.
	lFechado := .F.
EndIf

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp240SobrepVerif³ Autor ³ Leandro Drumond ³ Data ³ 18.04.13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generic                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function gp240SobrepVerif(oGrid)

Local cHelp		  	:= ""
Local cPermSobrep 	:= ""
Local cTipoAfa		:= ""
Local dDtIni        := CtoD("")
Local dDtFim        := CtoD("")
Local lValid		:= .T.
Local nLinha	  	:= 1
Local nAcols	  	:= oGrid:Length()
Local nLinhaAtual	:= oGrid:GetLine()

// There cannot be different launches for the same date or
// same clearance interval when the clearance type is not
// allow overlap.
dDtIni	:= oGrid:GetValue("R8_DATAINI")
dDtFim	:= oGrid:GetValue("R8_DATAFIM")
cTipoAfa:= oGrid:GetValue("R8_TIPOAFA")

For nLinha := 1 TO nAcols
	oGrid:GoLine(nLinha)
    If (nLinha <> nLinhaAtual) .and. !oGrid:IsDeleted()
		If (!Empty(oGrid:GetValue("R8_DATAFIM")) .and. (dDtIni >= oGrid:GetValue("R8_DATAINI")) .and. (dDtIni <= oGrid:GetValue("R8_DATAFIM"))) ;
	 	   .or. ;
	 	   (!Empty(oGrid:GetValue("R8_DATAFIM")) .and. (dDtFim >= oGrid:GetValue("R8_DATAINI")) .and. (dDtFim <= oGrid:GetValue("R8_DATAFIM"))) ;
	 	   .or. ;
	 	   (!Empty(oGrid:GetValue("R8_DATAFIM")) .and. (dDtIni <= oGrid:GetValue("R8_DATAINI")) .and. (dDtFim >= oGrid:GetValue("R8_DATAFIM"))) ;
	 	   .or. ;
	 	   (Empty(oGrid:GetValue("R8_DATAFIM")) .and. (dDtIni <= oGrid:GetValue("R8_DATAINI")) .and. (dDtFim >= oGrid:GetValue("R8_DATAINI")))

				// Check if the offset type allows for overlap
				cPermSobrep := gp240RetCont("RCM",	1, "RCM_FILIAL = '" + xFilial("RCM") + "' AND RCM_TIPO = '" + oGrid:GetValue("R8_TIPOAFA") + "'", "RCM_SOBREP" ,,.T.)
				If cPermSobrep == "2"
 					lValid := .F.
					cHelp := OemToAnsi( STR0017 ) // "Different entry for the same date or leave interval is not allowed."
					Help( ,, 'HELP',, cHelp, 1, 0)
					Exit
   				EndIf
		EndIf

		If (Empty(dDtFim) .and. (dDtIni <= oGrid:GetValue("R8_DATAINI"))) ;
			.or.;
	   	   (Empty(oGrid:GetValue("R8_DATAFIM")) .and. (oGrid:GetValue("R8_DATAINI") <= dDtIni))
				lValid = .F.
				cHelp := OemToAnsi( STR0019 ) // "Leaves later than a leave with no return date are not allowed."
				Help( ,, 'HELP',, cHelp, 1, 0)
				Exit
		EndIf

	EndIf

Next nLinha

oGrid:GoLine(nLinhaAtual)

Return( lValid )



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp240DiasDirValid³ Autor ³ Leandro Drumond       ³ Data ³19.04.2013³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Checks if the number of days of absence informed is allowed,       ³±±
±±³          ³when the absence type is a "period schedule" type.                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ gp240DiasDirValid()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gpea240                                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß */
Static Function gp240DiasDirValid()

Local cTpAusencia := ""
Local cHelp		  := ""
Local cPdAfa	  := ""
Local cTipoAfa	  := ""
Local lRet		  := .T.
Local lStatus     := .F.
Local nDiasDireito:= 0
Local nTotDiasPg  := 0
Local nCnt		  := 0
Local nDurTotal	  := 0
Local nLinha	  := 0
Local nLenGrid	  := 0
Local nLinAtual	  := 0
Local oModel	  := FWModelActivate()
Local oGrid 	  := oModel:GetModel("GPEA240_SR8")
Local lTemSRD	  := fTemSRD() // customer that only uses electronic point should not do the check, as a way of testing we decided to see if it has a record in the SRD

If !lTemSRD
	return lRet
EndIf

nLenGrid	  := oGrid:Length()
nLinAtual	  := oGrid:GetLine()
cPdAfa 		  := oGrid:GetValue("R8_PD")
cTipoAfa	  := oGrid:GetValue("R8_TIPOAFA")

cTpAusencia := gp240RetCont(;
				"RCM", 											; 	// cAlias
				1, 												; 	// nIndex
				"RCM_FILIAL = '" + xFilial("RCM") + "' AND RCM_TIPO = '" + oGrid:GetValue("R8_TIPOAFA") + "'",	; 	// cKey
				"RCM_TIPOAF",,.T.									) 	// return column

If cTpAusencia == "4"
	If Empty(cPdAfa)
	     // ?annot be blank
		cHelp := OemToAnsi( STR0023 ) // "Budget must be informed when the type of absence is period schedule."
		Help( ,, 'HELP',, cHelp, 1, 0)
		lRet := .F.
	Else
		If Empty(oGrid:GetValue("R8_DATAFIM"))
			// ?an't be white
			cHelp := OemToAnsi( STR0024 ) // "Final absence date must be informed when the type of absence is period schedule."
			Help( ,, 'HELP',, cHelp, 1, 0)
			lRet := .F.
		
		EndIf
	EndIf
EndIf

Return ( lRet )



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fOpenRadio ºAutor ³Leandro Drumond     º Data ³  22/04/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Check the filter that must be done on the screen.           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPE                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fOpenRadio(nOpcRadio, cTitJan, cTitBox, cTitRad1, cTitRad2)
Local nOpcAux
Local oRadio
Local oDlg
Local oGroup
Local oFont
Local bSet15
Local bSet24

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaration of arrays to scale screen    		             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aGDCoord		:= {}

nOpcAux   := nOpcRadio
nOpcRadio := 0

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Assemble the Dimensions of Objects     					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aAdvSize		:= MsAdvSize()
aAdvSize[5]		:= (aAdvSize[5]/100) * 30	// horizontal
aAdvSize[6]		:= (aAdvSize[6]/100) * 20	// Vertical
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }
aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )
aGdCoord		:= { (aObjSize[1,1]+3), (aObjSize[1,2]+5), (((aObjSize[1,3])/100)*14.9), (((aObjSize[1,4])/100)*29.6) }	// 1.3 Vertical /1.4 Horizontal

DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
DEFINE MSDIALOG oDlg FROM  aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] TITLE OemToAnsi(cTitJan) PIXEL

	@ aGdCoord[1],aGdCoord[2] GROUP oGroup TO aGdCoord[3],aGdCoord[4] LABEL OemToAnsi(cTitBox) OF oDlg PIXEL
	oGroup:oFont:=oFont

	@ aGdCoord[1]+10,aGdCoord[2]+5 RADIO oRadio VAR nOpcAux ITEMS 	OemToAnsi(cTitRad1), OemToAnsi(cTitRad2);
	          SIZE 115,010 OF oDlg PIXEL

	bSet15 := {|| nOpcRadio := nOpcAux, oDlg:End()}
	bSet24 := {|| nOpcRadio := 0,       oDlg:End()}

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg, bSet15, bSet24, Nil, Nil) CENTERED

Return( nOpcRadio )



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Gp240CommitºAutor ³Leandro Drumond     º Data ³  17/04/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Performs validations before recording.                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPE                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Gp240Commit(oModel)

Local aArea			:= GetArea()
Local aSR8ID		:= {}
Local aGridInsert   := {}
Local aGridDelet    := {}
Local aGridTab      := {}
Local cNumId		:= {}
Local nOperation	:= oModel:GetOperation()
Local nLinha	 	:= 0
Local nLenGrid		:= 0
Local nLinAtual 	:= 0
Local nX            := 0
Local oGrid 	 	:= oModel:GetModel("GPEA240_SR8")
Local oGridSRA		:= oModel:GetModel("GPEA240_SRA")
Local dDataIni
Local dDataFim
Local dMaxInsert := Stod(" / / ")
Local dMaxTab    := Stod(" / / ")
Local dMaxDelete := Stod(" / / ")
Local dDtvTest   := Stod(" / / ")
Local dAux       := StoD(" / / ")
Local cMatOr
Local cFilOr
Local cPosto
Local cSucur := xFilial("RIV")
Local lRet	:= .T.
Local lOrgcfg 	:= SuperGetMv("MV_ORGCFG", NIL, "0" ) == "1" .And. SuperGetMv("MV_IORGGPE", NIL, "0" ) == "1"
Local aRep		:= {}
Local cRet		 := ""
Local mChave	 := ""
Local mChavePesq := ""
Local aASRASUB 	 := {}
Local cNomeSub 	 := ""
Local aParTela 	 := {}
Local cRelease   := GetRpoRelease()

Private oObjREP		:= Nil

//-- Initializes the integration via TSA WebServices
If lTSREP
	oObjREP := PTSREPOBJ():New()
	SX5->(dbSetOrder(1))
EndIf

nLenGrid	:= oGrid:Length()
nLinAtual 	:= oGrid:GetLine()

If lGestPubl .AND. cModulo $ 'GFP*VDF'
	mChave		:= DTOS(oGrid:GetValue("R8_DATAINI"))+oGrid:GetValue("R8_TIPOAFA")
	mChavePesq 	:= if(!Empty(SR8->R8_DATAINI),dtos(SR8->R8_DATAINI)+oGrid:GetValue("R8_TIPOAFA"),mChave)
	aASRASUB := SRA->(GetArea())
	cNomeSub := Posicione("SRA",1,oGrid:GetValue("R8_FILSUB")+oGrid:GetValue("R8_MATSUB"),"RA_NOME")
	RestArea(aASRASUB)
	aParTela := {	'GPEA240RUS',;
					SRA->RA_MAT,;
		            SRA->RA_CATFUNC,;
		            mChave,;
		            SRA->RA_FILIAL,;
		            SRA->RA_CIC,;
		            oGrid:GetValue("R8_DATAINI"),;
		            '6'  ,;
		            'SR8',;
		            "",;
					"",;
					oGrid:GetValue("R8_DATAINI"),;
					oGrid:GetValue("R8_DATAFIM"),;
					oGrid:GetValue("R8_DURACAO"),;
					0,;
					0,;
					oGrid:GetValue("R8_FILSUB"),;
					oGrid:GetValue("R8_MATSUB"),;
					cNomeSub ,;
					Posicione("RCM",1, FwxFilial("RCM") + oGrid:GetValue("R8_TIPOAFA") ,"RCM_DESCRI") ,;
					0,;
					"",;
					"",;
					"",;
					RCM->RCM_PUBLIC;
					}
EndIf

//--If not Exclusion
If nOperation # MODEL_OPERATION_DELETE
	Begin Transaction
    	//--Process Triggers
    	EvalTrigger()
        //--Recording
        SRA->(dbSetOrder(1))
		SRA->( dbSeek (oGridSRA:GetValue("RA_FILIAL") + oGridSRA:GetValue("RA_MAT") ) )
        For nLinha := 1 to nLenGrid
        	oGrid:GoLine(nLinha)

			// Identification number recording.
			cNumId := "SR8" + oGridSRA:GetValue("RA_MAT") + oGrid:GetValue("R8_PD") + DtoS(oGrid:GetValue("R8_DATAINI"))

			oGrid:LoadValue("R8_NUMID", cNumId)

			// Array used to update the calendar at the point
			If oGrid:IsDeleted()
				aAdd( aSR8ID , { nLinha , oGridSRA:GetValue("RA_FILIAL")+cNumId , .T.} )

				If lTSREP
					If RCM->( dbSeek( xFilial("RCM") + oGrid:GetValue("R8_TIPOAFA") ) )
						If RCM->RCM_TIPOAF <> "4"   // Vacation

								/*/
								ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								³ Position in the Employee Register                        ³
								ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
								SRA->(dbSetOrder(1))
								SRA->( dbSeek (oGridSRA:GetValue("RA_FILIAL") + oGridSRA:GetValue("RA_MAT") ) )

								/*/
								ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								³ Run the TSA WebServices - Leave Situation          ³
								ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
								oObjREP:WSAbsenceReason( 6 , , oGrid:GetValue("R8_DATAINI"))

						EndIf
		            EndIf
				EndIf

			Else
				aAdd( aSR8ID , { nLinha , oGridSRA:GetValue("RA_FILIAL")+cNumId , .F.} )

			EndIf

			If oGrid:IsInserted()
				dDataIni	:= oGrid:GetValue("R8_DATAINI")
				dDataFim	:= oGrid:GetValue("R8_DATAFIM")
				cTipoAfa	:= oGrid:GetValue("R8_TIPOAFA")
				cFilOr		:= oGridSRA:GetValue("RA_FILIAL")
				cMatOr		:= oGridSRA:GetValue("RA_MAT")
				cPosto		:= SRA->RA_POSTO

				If lOrgcfg
					DbSelectArea("RCM")
					DbSetOrder(RetOrder("RCM", "RCM_FILIAL+RCM_TIPO"))
					If dbSeek(xFilial("RCM")+cTipoAfa)
						If RCM->RCM_SUBPOS == '2' .AND. !Empty(cPosto) .AND.;
							IIF(IsBlind(),.F.,MsgYesNo(STR0123)) // "Do you want to register a substitute to fill the position of this employee?"
							If (SubstPosto(dDataIni, dDataFim, cMatOr, cFilOr, cPosto))
								Help(,,'HELP',, STR0132,1,0 )// "Substitute successfully registered!"
							EndIf
						EndIf
					EndIf
				EndIf

				If cRelease >= "12.1.023"
					// Transfer of the value of the field R8_DIASEMP to the field TNC_QTAFAS
					DbSelectArea("TNC")
					DbSetOrder(13)
					
				EndIf

			EndIf

        Next nLinha

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Completing the Employee's Stability Expiration Date. ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		RCM->(dbSetOrder(1)) //Fil + Tipo
  		// Scan the Grid to check what state each row is in
  		For nX:=1 to oGrid:Length()
			oGrid:GoLine(nX)
			If oGrid:IsInserted()// Line Inserted.
				AADD(aGridInsert,nX)
			ElseIf oGRid:IsDeleted()// Line Deleted.
				AADD(aGridDelet,nX)
			Else// Row was already in the table.
				AADD(aGridTab,nX)
			EndIf
		Next nX

		// Checks which is the greater of the stability due date of the inserted rows.
		For nX:=1 to Len(aGridInsert)
			dAux := StoD(" / / ")
			oGrid:GoLine(aGridInsert[nX])
			RCM->(dbSeek( xFilial("RCM") + oGrid:GetValue("R8_TIPOAFA")))
			If (RCM->RCM_DIAEST # 0) .And. !Empty(oGrid:GetValue("R8_DATAFIM"))
				dAux := oGrid:GetValue("R8_DATAFIM") + RCM->RCM_DIAEST
			EndIf
			dMaxInsert := If(dAux >= dMaxInsert , dAux , dMaxInsert)
		Next nX

		// Checks which is the greater of the stability due date of the deleted rows.
		For nX:=1 to Len(aGridDelet)
			dAux := Stod(" / / ")
			oGrid:GoLine(aGridDelet[nX])
			RCM->(dbSeek( xFilial("RCM") + oGrid:GetValue("R8_TIPOAFA")))
			If (RCM->RCM_DIAEST # 0) .And. !Empty(oGrid:GetValue("R8_DATAFIM"))
				dAux := oGrid:GetValue("R8_DATAFIM") + RCM->RCM_DIAEST
			EndIf
			dMaxDelete := If(dAux >= dMaxDelete , dAux , dMaxDelete)
		Next nX

		// Checks which is the greater of the stability expiration date of the table rows.
		For nX:=1 to Len(aGridTab)
			dAux := Stod(" / / ")
			oGrid:GoLine(aGridTab[nX])
			RCM->(dbSeek( xFilial("RCM") + oGrid:GetValue("R8_TIPOAFA")))
			If (RCM->RCM_DIAEST # 0) .And. !Empty(oGrid:GetValue("R8_DATAFIM"))
				dAux := oGrid:GetValue("R8_DATAFIM") + RCM->RCM_DIAEST
			EndIf
			dMaxTab := If(dAux >= dMaxTab , dAux , dMaxTab)
		Next nX

		// Recording of RA_DTVTEST.
		SRA->(dbSetOrder(1))
		If SRA->( dbSeek (oGridSRA:GetValue("RA_FILIAL") + oGridSRA:GetValue("RA_MAT") ) )
			RecLock("SRA", .F.)

			dDtvTest := If(dMaxInsert > dMaxTab , dMaxInsert , dMaxTab)

			If Empty(dDtvTest) // User is deleting the only absence he had.
				If !Empty(dMaxDelete) .And. SRA->RA_DTVTEST == dMaxDelete
					SRA->RA_DTVTEST := Stod(" / / ")
				EndIf
			Else
				If Empty(dMaxDelete) // It's not deleting anything
					SRA->RA_DTVTEST := If( SRA->RA_DTVTEST < dDtvTest , dDtvTest , SRA->RA_DTVTEST )
				ElseIf SRA->RA_DTVTEST == dMaxDelete // Deleted an absence, but there were others registered.
					SRA->RA_DTVTEST := dDtvTest
				EndIf
			EndIf

			SRA->(MsUnlock())
		EndIf
		oGrid:GoLine(nLinAtual)

		If ExistBlock("GP240GRV")
			ExecBlock("GP240GRV",.F.,.F.)
		EndIf

		

		If lGestPubl .AND. cModulo $ 'GFP*VDF'
			gp240GrvRi8(oModel)
			If !oGrid:IsDeleted()
				If (QueryRI6(mChavePesq,SRA->RA_FILIAL,SRA->RA_MAT,"SR8") == "NP")
					ExcluiRI6()
				EndIf
				lRet:= VDFA060(aParTela)
				// Publish if you have a Substitute
				If lRet .AND. !Empty(oGrid:GetValue("R8_FILSUB")) .AND. !Empty(oGrid:GetValue("R8_MATSUB")) // Check if you have a Substitute
					SETFUNNAME("VDFA070")
					aParTela[1]  := "VDFA070"
					aParTela[4]  := mChave
					If (QueryRI6(mChavePesq,SRA->RA_FILIAL,SRA->RA_MAT,"SR8") == 'NP')
						ExcluiRI6()
					EndIf
					lRet := VDFA060(aParTela)
				EndIf
				TRBRI6->( dbCloseArea())
			Else
				lRet := DelAfasGFP(aParTela, mChavePesq )
			EndIf
			If !lRet
				oModel:SetErrorMessage("",,oModel:GetId(),"","GPEA240RUS",STR0233) // "An error or cancellation occurred during the generation of the Act\Ordinance."
			EndIf
		EndIf
		If lIntegDef
			EmployeeSit(oModel,.T.)
		Endif
		If lRet
			FWFormCommit( oModel )

			If lTSREP
				/* Code repositioned to after Commit to update record only after recording.*/
				For nx := 1 to Len (aSR8ID)
					// update the placement on the grid
					oGrid:GoLine(nx)
					oGridTSA:GoLine(nx)
					/* TSA Integration */
					If !oGrid:IsDeleted(nx) // check if the line is not deleted
						If RCM->( dbSeek( xFilial("RCM") + oGrid:GetValue("R8_TIPOAFA") ) )
							If RCM->RCM_TIPOAF <> "4"     // Vacation

								/* Analyze need to delete previous record in TSA application */
								If oGrid:IsUpdated() .and. oGrid:Length() >= nLinha
									oGridTSA:GoLine(nLinha)

									If oGrid:GetValue("R8_DATAINI") <> oGridTSA:GetValue("R8_DATAINI") .Or. ;
									   oGrid:GetValue("R8_DATAFIM") <> oGridTSA:GetValue("R8_DATAFIM") .Or. ;
									   oGrid:GetValue("R8_TIPOAFA") <> oGridTSA:GetValue("R8_TIPOAFA")

										/* Run the TSA WebServices - Leave Situation */
										oObjREP:WSAbsenceReason( 6, , oGridTSA:GetValue("R8_DATAINI") )
									EndIf
								EndIf

								/* Checks whether the period of leave is present or future for submission to the TSA */
								// Checks if the Start date is greater/equal to today's date
								// Checks if there is no end date of removal
								// Checks if the end date is greater/equal to today's date
								// Date() was used instead of dDataBase to get the date from the computer
								If oGrid:GetValue("R8_DATAINI") >= Date() .Or. Empty(oGrid:GetValue("R8_DATAFIM")) .Or. (!Empty(oGrid:GetValue("R8_DATAFIM")) .And. oGrid:GetValue("R8_DATAFIM") >= Date())

									/* Run the TSA WebServices - Leave Situation */
									If oObjREP:WSAbsenceReason( 5 , , oGrid:GetValue("R8_DATAINI"), oGrid:GetValue("R8_DATAFIM") )
										aadd(aRep,aSR8ID[nx][2])
									EndIf
								EndIf
							EndIf
						EndIf
					EndIf
				Next nx
			EndIf
			If lIntegDef
				EmployeeSit(oModel,.F.)
			Endif

			If Type("lResM2") <> "U" .And. lResM2 // Treatment for calling the routine through the termination screen (Model 2)
				lM2Modif:= .T.
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Assemble Array with data for sending e-mail              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			fMontaMail("018")
		EndIf
	End Transaction

	If lTSREP .And. Len(aRep) > 0
		dbSelectArea("SR8") // posicona in the registry to be updated
		SR8->(dbSetOrder(5))
		For nX := 1 to Len(aRep)
			/* Writes the Log of the WebServices TSA export control */
			If SR8->(dbSeek(aRep[nx]))
				oObjRep:WSUpdRHExp( "SR8" )
			EndIf
		Next nX
	EndIf

	// Entry Point executed after recording the record
	If ExistBlock("GP240MAN")
		Execblock("GP240MAN",.F.,.F.,{nOperation})
	EndIf

//-- if it is Exclusion
ElseIf nOperation == MODEL_OPERATION_DELETE

	If lTSREP
		For nLinha := 1 to nLenGrid
        	oGrid:GoLine(nLinha)
			If RCM->( dbSeek( xFilial("RCM") + oGrid:GetValue("R8_TIPOAFA") ) )
				If RCM->RCM_TIPOAF <> "4"     //Vacation
					/*/
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³ Run the TSA WebServices - Leave Situation                    ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					oObjREP:WSAbsenceReason( 6 , , oGrid:GetValue("R8_DATAINI"))

				EndIf
			EndIf
		Next nLinha
		oGrid:GoLine(nLinAtual)
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Completion of the employee's Stability Expiration Date. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	// Checks which is the highest stability expiration date of the SR8 table.
	For nX:=1 to oGrid:Length()
		dAux := StoD(" / / ")
		oGrid:GoLine(nX)
		RCM->(dbSeek( xFilial("RCM") + oGrid:GetValue("R8_TIPOAFA")))
		If (RCM->RCM_DIAEST # 0) .And. !Empty(oGrid:GetValue("R8_DATAFIM"))
			dAux := oGrid:GetValue("R8_DATAFIM") + RCM->RCM_DIAEST
		EndIf
		dMaxDelete := If(dAux >= dMaxDelete , dAux , dMaxDelete)
	Next nX

	// Updates the employee stability expiration date.
	RecLock("SRA",.F.)
		SRA->RA_DTVTEST := If(SRA->RA_DTVTEST > dMaxDelete , SRA->RA_DTVTEST , Stod(" / / ") )
	SRA->(MsUnLock())

	oGrid:GoLine(nLinAtual)

	// Entry Point executed before record deletion
	If ExistBlock("GP240EXC")
		Execblock("GP240EXC",.F.,.F.,{nOperation})
	EndIf

	If lRet .AND. lGestPubl .AND. cModulo $ 'GFP*VDF'
		gp240GrvRi8(oModel)
		lRet := DelAfasGFP(aParTela, mChavePesq )
		If !lRet
			oModel:SetErrorMessage("",,oModel:GetId(),"","GPEA240RUS",STR0233) // "An error or cancellation occurred during the generation of the Act\Ordinance."
		EndIf
	EndIf
	if lRet
		FWFormCommit(oModel)
	EndIf
	If lIntegDef
		EmployeeSit(oModel,.F.)
	Endif

EndIf

RUGPE24004_ReCalcRFDFALVAT()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Updates the Employee Registration "SRA" - By Naldo           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ


// Updates departures from the electronic point calendar
If FindFunction("fRecCalAfa")
	fRecCalAfa(oGrid,aSR8ID,nOperation)
EndIf

RestArea(aArea)

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SubstPosto ºAutor ³Mohanad             º Data ³ 20/03/14    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³SUBSTITUICAO DE POSTO - AFASTAMENTO/FERIAS                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPE,SIGAORG                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function SubstPosto(dDataIni, dDataFim, cMatOr, cFilOr, cPosto)
Local aAreaSRA	:= SRA->(GetArea())
Local oGet1
Local oGet2
Local oGet3
Local c3Lbx, oGroup1, oGroup2, oGroup, oFont
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjCoords		:= {}
Local aObjSize		:= {}
Local dDtIni			:= dDataIni
Local lGravou			:= .F.
Local lNovo			:= .T.
Local oNo				:= LoadBitmap( GetResources(), "LBNO" )
Local oOk				:= LoadBitmap( GetResources(), "LBOK" )
Private oDlg
Private cMatr			:= CriaVar("RA_MAT")
Private cNome			:= SPACE(30)
Private cFilFun		:= xFilial("SRA")
Private o3Lbx
Private a3Lbx			:= {}
Private lCheck		:= .F.
Private nOpca			:= 0

// ASSEMBLE THE DIMENSIONS OF THE OBJECTS
aAdvSize		:= MsAdvSize(,.T.,380)
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
aAdd( aObjCoords , { 000 , 025 , .T. , .F. } )
aAdd( aObjCoords , { 000 , 085 , .T. , .F. } )
aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )

bSet15	:= {|| GPEA240VLD(6), If(nOpca == 1, oDlg:End(), NIL ) }
bSet24	:= {|| oDlg:End()}


DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD

DEFINE MSDIALOG oDlg FROM aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] TITLE STR0124 PIXEL  // "Position Replacement"
oDlg:lEscClose := .F. // It does not allow to exit when pressing the ESC key.

@ aObjSize[1][1],aObjSize[1][2] GROUP oGroup1 TO aObjSize[1][3],aObjSize[1][4] LABEL STR0125 OF oDlg PIXEL // "Start Date: "
oGroup1:oFont:= oFont
@ aObjSize[1][1]+11,aObjSize[1][2]+10    SAY STR0125 PIXEL  // "Start Date: "
@ aObjSize[1][1]+8, aObjSize[1][4]*0.15  MSGET oGet1 VAR dDtIni SIZE 40,10 OF oGroup1 PIXEL VALID NAOVAZIO(dDtIni)


@ aObjSize[2][1],aObjSize[2][2] GROUP oGroup2 TO aObjSize[2][3],aObjSize[2][4] LABEL STR0140 OF oDlg PIXEL	// "Registered replacements"
	oGroup2:oFont:= oFont

DbSelectArea("RCX")
DbSetOrder(RetOrder("RCX","RCX_FILIAL+RCX_POSTO+RCX_FILOCU+RCX_CODOCU"))
DbSeek(xFilial("RCX")+cPosto)
While RCX->(!Eof() .AND. RCX_FILIAL == xFilial("RCX") .AND. RCX_POSTO == cPosto)
	aAdd(a3Lbx, {.F., RCX->RCX_FILFUN, RCX->RCX_MATFUN, fDesc("SRA",RCX->RCX_MATFUN,"RA_NOME",, RCX->RCX_FILFUN)})
	DbSkip()
EndDo
RCX->(DbCloseArea())

@ aObjSize[2,1]+6,aObjSize[2,2]+6 LISTBOX o3Lbx VAR c3Lbx FIELDS;
	 	HEADER 	"",;
	 			OemtoAnsi(STR0145),;	// "Branch"
				OemtoAnsi(STR0146),;	// "Registration"
				OemtoAnsi(STR0147);		// "Employee"
		COLSIZES	GetTextWidth(0,"W"),;
					GetTextWidth(0,"BBBBBBBBBBBB"),;
					GetTextWidth(0,"BBBBBB"),;
					GetTextWidth(0,"BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB") SIZE aObjSize[2,4]-16,aObjSize[2,3]-42 OF oGroup2 PIXEL;
	ON DBLCLICK (GPEA240VLD(4)) // DOUBLE-CLICK
	o3Lbx:SetArray(a3Lbx)
	o3Lbx:bLine := {|| LineLstBox(o3Lbx , .T. , {oOk, oNo})} // BUILD LISTBOX LINE

@ aObjSize[3][1],aObjSize[3][2] GROUP oGroup TO aObjSize[3][3],aObjSize[3][4] LABEL STR0141 OF oDlg PIXEL	// "New replacement"
	oGroup:oFont:= oFont

//GRUPO 2
@ aObjSize[3][1]+18,aObjSize[3][2]+5	    	SAY STR0126 PIXEL // "Branch:"
@ aObjSize[3][1]+15,aObjSize[3][4]*0.08    	MSGET oGet2 VAR cFilFun SIZE 40,10 OF oGroup PIXEL PICTURE "@!" F3 "SM0" VALID GPEA240VLD(1) When (GPEA240VLD(5)) HASBUTTON
@ aObjSize[3][1]+18,aObjSize[3][4]*0.25   	SAY STR0127 PIXEL // "Empl. Reg.:"
@ aObjSize[3][1]+15,aObjSize[3][4]*0.34   	MSGET oGet3 VAR cMatr SIZE 40,10 OF oGroup PIXEL PICTURE "@!" F3 "SRAFIL" VALID GPEA240VLD(2, cMatOr, cFilOr, cPosto, dDtIni) When (GPEA240VLD(5)) HASBUTTON
@ aObjSize[3][1]+18,aObjSize[3][4]*0.52   	SAY STR0128 PIXEL // "Empl. Name.:"
@ aObjSize[3][1]+18,aObjSize[3][4]*0.63   	SAY cNome SIZE 150,10 OF oGroup PIXEL


ACTIVATE MSDIALOG oDlg ON INIT enchoicebar(oDlg,bSet15, bSet24) CENTERED

If nOpca == 1

	DbSelectArea("RCX")
	DbSetOrder(RetOrder("RCX","RCX_FILIAL+RCX_FILFUN+RCX_MATFUN+RCX_POSTO"))
	If DbSeek(xFilial("RCX")+cFilFun+cMatr+cPosto)
		RecLock("RCX",.F.)
		RCX->RCX_DTINI		:= dDtIni
		lNovo := .F.
	Else
		RecLock("RCX",.T.)
		RCX->RCX_FILIAL		:= xFilial("RCX")
		RCX->RCX_TIPOCU		:= "1" // "1-Employee"
		RCX->RCX_POSTO		:= cPosto
		RCX->RCX_DTINI		:= dDtIni
		RCX->RCX_FILFUN		:= cFilFun
		RCX->RCX_MATFUN		:= cMatr
		RCX->RCX_SUBST		:= "1" // "1-YES"
		RCX->RCX_AFAST		:= "1" // "1-YES"
		lNovo := .T.
	EndIf

	lGravou := .T.

	MsUnLock()
	RCX->(DbCloseArea())

	If lGravou .And. lNovo
		DbSelectArea("RBU")
		RBU->(RecLock("RBU", .T.))
		RBU->RBU_FILIAL	:= xFilial("RBU")
		RBU->RBU_CODMOV	:= &(GetSx3Cache("RBU_CODMOV", "X3_RELACAO")) // SEQUENTIAL NUMBER
		RBU->RBU_DTAMOV	:= dDataBase
		RBU->RBU_OPERAC	:= "3" // 3-OCCUPATION
		RBU->RBU_POSTO	:= cPosto
		RBU->RBU_DTINI	:= dDtIni
		RBU->RBU_FILFUN	:= cFilFun
		RBU->RBU_MATFUN	:= cMatr
		RBU->RBU_TIPOCU	:= "1"  //1-EMPLOYEE
		RBU->RBU_SUBST	:= "1" // "1-YES"
		RBU->(MsUnLock())
		RBU->(DbCloseArea())
	EndIf

EndIf

RestArea(aAreaSRA)
Return lGravou



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³INTEGDEF  ºAutor  ³                     º Data ³ 23/01/2012 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Function for interacting with EAI                           º±±
±±º          ³sending and receiving                                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function IntegDef( cXml, nType, cTypeMsg, cVersaoMsg )

Local aRet := {}

aRet:= GPEY240( cXml, nType, cTypeMsg, cVersaoMsg )

Return aRet

/*ÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Funcao    ³EmployeeSit	  ³Autor³ Alberto M.             ³ Data ³06/01/2015³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Descricao ³Routine to perform the integration of status messages          ³
³          ³of the official.                                               ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³EmployeeMsg                                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³Integration via single message.                                ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³<void>                                                         ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³lExclusao - .T. for delete message.                            ³
³          ³nPosDtIni - Position in the aCols of the start date R8_DATAINI.³
³          ³nPosDtFim - Position in the aCols of the end date R8_DATAFIM.  ³
³          ³nPosChave - Position in aCols of type R8_TIPO.                 ³
³          ³nPosDurac - Position in aCols of duration R8_DURACAO           ³
³          ³nX        - Acols line being verified.                         ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ*/

Static Function EmployeeSit(oModel,lOld)


// Variables to store the old values.
Local dGuarDatIni
Local dGuarDatFim
Local nGuarDurac
Local cGuarTipo
Local nOperation	:= oModel:GetOperation()
Local nLinha	 	:= 0
Local nLenGrid		:= 0
Local nX            := 0
Local oGrid 	 	:= oModel:GetModel("GPEA240_SR8")

Private lErroInt 	:= .F.
Private lExcAfastPIMS := .F.

DEFAULT lOld := .F.
nLenGrid	:= oGrid:Length()

If lOld
	oGridOld := {}

	For nLinha := 1 to nLenGrid
		oGrid:GoLine(nLinha)
		If oGrid:IsUpdated()
			SR8->(dbGoto(oGrid:GetDataId()))
			aadd(oGridOld,{oGrid:GetDataId(),SR8->R8_TIPOAFA,SR8->R8_DATAINI,SR8->R8_DATAFIM,SR8->R8_DURACAO})
		EndIf
	Next nLinha

Else

	// Exclusion.
	If nOperation == MODEL_OPERATION_DELETE
		For nLinha := 1 to nLenGrid
			oGrid:GoLine(nLinha)
			SetRotInteg("GPEM040")
			FwIntegDef("GPEM040")
		Next nLinha
	// Inclusion or change.
	Else
		For nLinha := 1 to nLenGrid
			lErroInt 	:= .F.
			oGrid:GoLine(nLinha)
			lExcAfastPIMS := .F.
			If oGrid:IsDeleted() .And. !oGrid:IsInserted()
				lExcAfastPIMS := .T.
				SetRotInteg("GPEM040")
				FwIntegDef("GPEM040")
			ElseIf oGrid:IsInserted()
				SetRotInteg("GPEM040")
				FwIntegDef("GPEM040")
			ElseIf oGrid:IsUpdated()
				nX := Ascan(oGridOld,{|x| x[1]==oGrid:GetDataId()})
				If nX > 0 .AND. ;
				  (oGrid:GetValue("R8_DATAINI") <> oGridOld[nX][3] .OR. ;
				   oGrid:GetValue("R8_DATAFIM") <> oGridOld[nX][4] .OR. ;
				   oGrid:GetValue("R8_TIPOAFA") <> oGridOld[nX][2] .OR. ;
				   oGrid:GetValue("R8_DURACAO") <> oGridOld[nX][5])

					If oGrid:GetValue("R8_DATAINI") <> oGridOld[nX][3]
						// If there is a change in the start date, it will first delete
						// the PIMS record and add a new one afterwards with the new date.
						// Saves the new SR8 values (after the change).

						dGuarDatIni := oGrid:GetValue("R8_DATAINI")
						dGuarDatFim := oGrid:GetValue("R8_DATAFIM")
						nGuarDurac  := oGrid:GetValue("R8_DURACAO")
						cGuarTipo   := oGrid:GetValue("R8_TIPOAFA")
						// Updates the variable to trigger the deletion in the message.
						lExcAfastPIMS := .T.

						If Nx > 0
							oGrid:SetValue("R8_DATAINI",oGridOld[nX][3])
							oGrid:SetValue("R8_DATAFIM",oGridOld[nX][4])
							oGrid:SetValue("R8_DURACAO",oGridOld[nX][5])
							oGrid:SetValue("R8_TIPOAFA",oGridOld[nX][2])

							// Perform the integration
							SetRotInteg("GPEM040")
							FwIntegDef("GPEM040")


							// Returns the exclude variable to its normal state.
							lExcAfastPIMS := .F.

							oGrid:SetValue("R8_DATAINI",dGuarDatIni)
							oGrid:SetValue("R8_DATAFIM",dGuarDatFim)
							oGrid:SetValue("R8_DURACAO",nGuarDurac)
							oGrid:SetValue("R8_TIPOAFA",cGuarTipo)
						EndIf

					EndIf

					SetRotInteg("GPEM040")
					FwIntegDef("GPEM040")

					If lErroInt
						nX := Ascan(oGridOld,{|x| x[1]==oGrid:GetDataId()})
						If Nx > 0
							oGrid:SetValue("R8_DATAINI",oGridOld[nX][3])
							oGrid:SetValue("R8_DATAFIM",oGridOld[nX][4])
							oGrid:SetValue("R8_DURACAO",oGridOld[nX][5])
							oGrid:SetValue("R8_TIPOAFA",oGridOld[nX][2])

							// Perform the integration
							SetRotInteg("GPEM040")
							FwIntegDef("GPEM040")
						EndIf
					EndIf
				 EndIf
			EndIf

		Next nLinha
	EndIf
EndIf

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao  gp240DDerechoºAutor  ºJ. Peñaloza         º Data º  04/09/2014 º±±
±±ÌÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.    º Validates that the duration of absence no sea mayor a sus   º±±
±±º         º right days, in case the type of absence is of type          º±±
±±º         º schedule of periods                                         º±±
±±ÌÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso      º X3_VALID SR8->R8_DURACAO                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function gp240DDerecho()
Local lRet := .T.
Local nDias    := 0
Local aSR8Cols := {}
Local cConcepto:= ''
Local cTipoAfa := ''
Local dDtIni := CTOD(' / / ')
If Type("aCols") == "U"
	aSR8Cols 	  	:= oGet:aCols
Else
	aSR8Cols 	  	:= aClone(aCols)
Endif
cConcepto := aSR8Cols[n,GdFieldPos("R8_PD", aHeader)]
cTipoAfa  := aSR8Cols[n,GdFieldPos("R8_TIPOAFA", aHeader)]

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao   ºCalcDiasDerºAutor  ºJ. Peñaloza             º Data º  04/09/2014 º±±
±±ÌÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.    º Calculates the right days of the employee for the country chile º±±
±±ÌÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso      º gp240DDerecho()                                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function CalcDiasDer(cConcepto)

Local nDiasDer := 0
Local nDiasAnt := 0
Local nDiasPag := 0
Local nSaldo   := 0
Local nDias    := 0
Local aSR8Cols := {}
Local aArea    := GetArea()
Local cQuery := ''
Local cTempF   := CriaTrab(Nil, .F.)
Local nPosPagar := 0
Local nPosConc  := 0
Local nX := 0
If Type("aCols") == "U"
	aSR8Cols 	  	:= oGet:aCols
Else
	aSR8Cols 	  	:= aClone(aCols)
Endif
nPosPagar := GdFieldPos("R8_SDPAGAR", aHeader)
nPosConc  := GdFieldPos("R8_TIPOAFA", aHeader)

RestArea(aArea)
Return nDias



//-------------------------------------------------------------------
/*/{Protheus.doc} fVlAltEsoc
Function responsible for verifying if the change involves eSocial fields
@author  Rafael Reis
@since   28/06/2018
@version 1
/*/
//-------------------------------------------------------------------
Static Function fVlAltEsoc(oGrid)
Local lReturn := .F.

If (oGrid:isFieldUpdated('R8_DATAINI')	.OR. oGrid:isFieldUpdated('R8_DATAFIM')	.OR. oGrid:isFieldUpdated('R8_TPEFD')	.OR. oGrid:isFieldUpdated('R8_TIPOAT')	.OR. ;
	oGrid:isFieldUpdated('R8_NMMED')	.OR. oGrid:isFieldUpdated('R8_CRMMED')	.OR. oGrid:isFieldUpdated('R8_UFCRM')	.OR. oGrid:isFieldUpdated('R8_IDEOC')	.OR. ;
	oGrid:isFieldUpdated('R8_ORIGALT')	.OR. oGrid:isFieldUpdated('R8_NRPRCJU')	.OR. (SR8->(ColumnPos( "R8_TPPROC")) > 0 .And. oGrid:isFieldUpdated('R8_TPPROC'))	.OR. oGrid:isFieldUpdated('R8_CONTAFA')	.OR. ;
	oGrid:isFieldUpdated('R8_OBSAFAS')	.OR. oGrid:isFieldUpdated('R8_CID')		.OR. oGrid:isFieldUpdated('R8_DURACAO')	.OR. oGrid:isFieldUpdated('R8_CNPJCES')	.OR. ;
	oGrid:isFieldUpdated('R8_TPCES')	.OR. oGrid:isFieldUpdated('R8_CNPJSIN')	.OR. oGrid:isFieldUpdated('R8_TPSIND')	)
	lReturn := .T.
Elseif(oGrid:IsDeleted())
	lReturn := .T.
Endif

Return lReturn




//-------------------------------------------------------------------
/*/{Protheus.doc} fAlertJob
Function to display alert and link to TDN with guidance on
envio de afastamentos
@author  Rafael Reis
@since   26/10/2018
@version 1
/*/
//-------------------------------------------------------------------
Static Function fAlertJob()
Local oButton1
Local oCheckBo1
Local lCheckBo1 := .F.
Local oGroup1
Local oPanel1
Local oSay1
Local oSay2
Local cSession	:= "AlertaAfastamento"
Local lChkMsg := fwGetProfString(cSession,"MSG_JOBAFAS_" + cUserName,'',.T.) == ""
Static oDlg

If lChkMsg

	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0009) FROM 000, 000  TO 200, 500 COLORS 0, 16777215 PIXEL // "Leave File "

		@ 000, 000 MSPANEL oPanel1 SIZE 300, 150 OF oDlg COLORS 0, 16777215 RAISED
		@ 005, 012 GROUP oGroup1 TO 055, 237 PROMPT OemToAnsi(STR0021) OF oPanel1 COLOR 0, 16777215 PIXEL // "Warning"
		@ 022, 017 SAY oSay1 PROMPT OemToAnsi(STR0241) SIZE 215, 025 OF oPanel1 COLORS 0, 16777215 PIXEL // "The end date of leave of absence is integrated to TAF if it is earlier than the base date of the system, otherwise, it is integrated through JOB according to guidelines in the link below."
		@ 063, 012 SAY oSay2 PROMPT "http://tdn.totvs.com/x/dQdFFw" SIZE 095, 007 OF oPanel1 COLORS 0, 16777215 PIXEL
		@ 080, 012 CHECKBOX oCheckBo1 VAR lCheckBo1 PROMPT OEMToAnsi(STR0242) SIZE 067, 008 OF oPanel1 COLORS 0, 16777215 PIXEL // "Do not display it again"
		@ 070, 200 BUTTON oButton1 PROMPT "OK" SIZE 037, 012 OF oPanel1 PIXEL

		oSay2:bLClicked := {|| ShellExecute("open","http://tdn.totvs.com/x/dQdFFw","","",1) }

		oButton1:bLClicked := {|| oDlg:End() }

	ACTIVATE MSDIALOG oDlg CENTERED

	If lCheckBo1
		fwWriteProfString(cSession,"MSG_JOBAFAS_" + cUserName, 'CHECKED', .T.)
	Endif

Endif

Return

/*/{Protheus.doc} gp240Child()
    
    This function show window for select childrens.

    @type Function
    @params 
    @return .F.
    @author dchizhov
    @since 10.10.2023
    @version 12.1.33
    @example gp240Child()
*/
Function gp240Child()

    Local bSet15 As Block
    Local bSet24 As Block
    Local oDlg   As Object
    Local oBtn1  As Object
    Local oBtn2  As Object
    Local oBtn3  As Object
    Local oBtn4  As Object
    Local oBYChild := LoadBitmap( GetResources(), "BR_VERDE" )
    Local oBOChild := LoadBitmap( GetResources(), "BR_CINZA" )
    Local oBOther  := LoadBitmap( GetResources(), "BR_VERMELHO" )

    Local oModel   := FWModelActivate()
    Local oView    := FWViewActivate()
    Local oGrid    := oModel:GetModel("GPEA240_SR8")

    Local aAdvSize      := {} As Array
    Local aInfoAdvSize  := {} As Array
    Local aObjCoords    := {} As Array
    Local aObjSize      := {} As Array

    Local aAdv1Size     := {} As Array
    Local aInfo1AdvSize := {} As Array
    Local aObj1Coords   := {} As Array
    Local aObj1Size     := {} As Array

    Local aAdv2Size     := {} As Array
    Local aInfo2AdvSize := {} As Array
    Local aObj2Coords   := {} As Array
    Local aObj2Size     := {} As Array

    Local aAdv3Size     := {} As Array
    Local aInfo3AdvSize := {} As Array
    Local aObj3Coords   := {} As Array
    Local aObj3Size     := {} As Array

    Local aAdv4Size     := {} As Array
    Local aInfo4AdvSize := {} As Array
    Local aObj4Coords   := {} As Array
    Local aObj4Size     := {} As Array

    Local cChildrens  As Character
    Local lRes := .T. As Logical

    Begin Sequence

        Private oFont1 As Object
        Private oFont2 As Object
        Private oLbx1  As Object
        Private oLbx2  As Object
        Private aLbx1  As Array
        Private aLbx2  As Array
        Private cLbx1  As Character
        Private cLbx2  As Character
        Private nPosVerba := Ascan( aHeader, { |x| Trim(x[2]) == "R8_CHILDS" } ) As Numeric
        Private nLbxAtiva As Numeric

        // Fill ListBox aLbx1 and aLbx2
        gp240MontaChild(@aLbx1, @aLbx2)

        DEFINE FONT oFont1  NAME "Arial"        SIZE 7, 16 BOLD
        DEFINE FONT oFont2  NAME "Courier New"  SIZE 7, 16 BOLD

        aAdvSize        := MsAdvSize(,.T., 380)
        aInfoAdvSize    := { aAdvSize[1], aAdvSize[2], aAdvSize[3], aAdvSize[4], 5, 5 }
        aAdd( aObjCoords, { 000, 020, .T., .F. } )                 //Line 1
        aAdd( aObjCoords, { 000, 000, .T., .T. } )                 //Line 2
        aAdd( aObjCoords, { 000, 035, .T., .F. } )                 //Line 3
        aObjSize        := MsObjSize( aInfoAdvSize, aObjCoords )

        aAdv1Size        := aClone(aObjSize[1])
        aInfo1AdvSize    := { aAdv1Size[2], aAdv1Size[1], aAdv1Size[4], aAdv1Size[3], 0, 0 }
        aAdd( aObj1Coords, { 000, 000, .T., .T. } )                // 1-title dictionary
        aAdd( aObj1Coords, { 020, 000, .F., .T. } )                // 2-button
        aAdd( aObj1Coords, { 000, 000, .T., .T. } )                // 3-title selected
        aObj1Size        := MsObjSize( aInfo1AdvSize, aObj1Coords,,.T. )

        aAdv2Size        := aClone(aObjSize[2])
        aInfo2AdvSize    := { aAdv2Size[2], aAdv2Size[1], aAdv2Size[4], aAdv2Size[3], 0, 0 }
        aAdd( aObj2Coords, { 000, 000, .T., .T., .T.} )            // 1-list dictionary
        aAdd( aObj2Coords, { 020, 000, .F., .T.} )                 // 2-button
        aAdd( aObj2Coords, { 000, 000, .T., .T., .T.} )            // 3-list selected
        aObj2Size        := MsObjSize( aInfo2AdvSize, aObj2Coords,,.T. )

        aAdv3Size        := aClone(aObjSize[3])
        aInfo3AdvSize    := { aAdv3Size[2], aAdv3Size[1], aAdv3Size[4], aAdv3Size[3], 5, 5}
        aAdd( aObj3Coords, { 000, 000, .T., .T. } )                // legenda 1
        aAdd( aObj3Coords, { 000, 000, .T., .T. } )                // legenda 2
        aAdd( aObj3Coords, { 000, 000, .T., .T. } )                // legenda 3
        aObj3Size        := MsObjSize( aInfo3AdvSize , aObj3Coords )

        aAdv4Size        := aClone(aObj2Size[2])
        aInfo4AdvSize    := { aAdv4Size[2], aAdv4Size[1], aAdv4Size[4], aAdv4Size[3], 4, 4 }
        aAdd( aObj4Coords, { 000, 000, .T., .T. } )                // space
        aAdd( aObj4Coords, { 000, 000, .T., .T. } )                // button 1
        aAdd( aObj4Coords, { 000, 000, .T., .T. } )                // button 2
        aAdd( aObj4Coords, { 000, 000, .T., .T. } )                // space
        aAdd( aObj4Coords, { 000, 000, .T., .T. } )                // button 3
        aAdd( aObj4Coords, { 000, 000, .T., .T. } )                // button 4
        aAdd( aObj4Coords, { 000, 000, .T., .T. } )                // space
        aObj4Size        := MsObjSize( aInfo4AdvSize, aObj4Coords )

        DEFINE MSDIALOG oDlg FROM aAdvSize[7], 0 TO aAdvSize[6], aAdvSize[5] Title OemToAnsi(STR0296) PIXEL // "Select children for whose care absence is registered"

        @ aObj1Size[1, 1]    , aObj1Size[1, 2] GROUP oGroup TO aObj1Size[1, 3], aObj1Size[1, 4] OF oDlg PIXEL
        @ aObj1Size[1, 1] + 5, aObj1Size[1, 2] + 5 SAY OemToAnsi(STR0297) SIZE 120, 07 PIXEL FONT oFont1 COLOR CLR_BLUE // "Children available for choice"

        @ aObj1Size[3, 1]    , aObj1Size[3, 2] GROUP oGroup TO aObj1Size[3,3], aObj1Size[3, 4] OF oDlg PIXEL
        @ aObj1Size[3, 1] + 5, aObj1Size[3, 2] + 5 SAY OemToAnsi(STR0298) SIZE 120, 07 PIXEL FONT oFont1 COLOR CLR_BLUE // "Children Selected for Absence"


        @ aObjSize[3,1]    , aObjSize[3,2] GROUP oGroup TO aObjSize[3,3], aObjSize[3,4]  OF oDlg PIXEL
        @ aObj3Size[1,1]   , aObj3Size[1,2] BITMAP NAME "BR_VERDE" SIZE 8,8 of oDlg PIXEL
        @ aObj3Size[2,1]   , aObj3Size[2,2] BITMAP NAME "BR_CINZA" SIZE 8,8 of oDlg PIXEL
        @ aObj3Size[3,1]   , aObj3Size[3,2] BITMAP NAME "BR_VERMELHO" SIZE 8,8 of oDlg PIXEL

        @ aObj3Size[1,1]   , aObj3Size[1,2] + 10 SAY OemToAnsi(STR0304) SIZE 280,07 PIXEL FONT oFont2 COLOR CLR_BLUE // "Children/stepchildren under 1.5 years old"
        @ aObj3Size[2,1]   , aObj3Size[2,2] + 10 SAY OemToAnsi(STR0305) SIZE 280,07 PIXEL FONT oFont2 COLOR CLR_BLUE // "Children/stepchildren over 1.5 years old"
        @ aObj3Size[3,1]   , aObj3Size[3,2] + 10 SAY OemToAnsi(STR0306) SIZE 280,07 PIXEL FONT oFont2 COLOR CLR_BLUE // "Relatives who are not children"

        @ aObj2Size[1, 1]    , aObj2Size[1, 2] LISTBOX oLbx1 VAR cLbx1 FIELDS HEADER Space(2),;
        OemtoAnsi(STR0301),; // "Record number"
        OemtoAnsi(STR0302),; // "Full name"
        OemtoAnsi(STR0303);  // "Date of Birth"
        COLSIZES GetTextWidth(0,"B"),;
        GetTextWidth(0, "BB"),;
        GetTextWidth(0,"BBBBBBBBBBBBBBBBBBBB"),;
        GetTextWidth(0,"BBBBBBBB");
        SIZE aObj2Size[1, 3], aObj2Size[1, 4] OF oDlg PIXEL
        oLbx1:SetArray(aLbx1)

        @ aObj2Size[3, 1]    , aObj2Size[3, 2] LISTBOX oLbx2 VAR cLbx2 FIELDS HEADER Space(2),;
        OemtoAnsi(STR0301),; // "Record number"
        OemtoAnsi(STR0302),; // "Full name"
        OemtoAnsi(STR0303);  // "Date of Birth"
        COLSIZES GetTextWidth(0,"B"),;
        GetTextWidth(0, "BB"),;
        GetTextWidth(0,"BBBBBBBBBBBBBBBBBBBB"),;
        GetTextWidth(0,"BBBBBBBB");
        SIZE aObj2Size[3, 3], aObj2Size[3, 4] OF oDlg PIXEL
        oLbx2:SetArray(aLbx2)

        oLbx1:bLine := { ||  {Iif(aLbx1[oLbx1:nAt, 1] == "1", oBYChild, Iif(aLbx1[oLbx1:nAt, 1] == "2", oBOChild, oBOther)), aLbx1[oLbx1:nAt, 2], aLbx1[oLbx1:nAt, 3], aLbx1[oLbx1:nAt, 4] }}
        oLbx2:bLine := { ||  {Iif(aLbx2[oLbx2:nAt, 1] == "1", oBYChild, Iif(aLbx2[oLbx2:nAt, 1] == "2", oBOChild, oBOther)), aLbx2[oLbx2:nAt, 2], aLbx2[oLbx2:nAt, 3], aLbx2[oLbx2:nAt, 4] }}

        oBtn1 := TBitmap():New(aObj4Size[2, 1], aObj4Size[2, 2], 25, 25,,"NEXT", .T., oDlg,;
        {||gp240MoveChild(1, .T., oLbx1:nAt)},,.F.,.F.,,,.F.,,.T.,,.F.)
        oBtn1:CTOOLTIP := STR0299 // "Move Child"

        oBtn2 := TBitmap():New(aObj4Size[3, 1], aObj4Size[3, 2], 25, 25,,"PGNEXT", .T., oDlg,;
        {||gp240MoveChild(1, .F., oLbx1:nAt)},,.F.,.F.,,,.F.,,.T.,,.F.)
        oBtn2:CTOOLTIP := STR0300 // "Move all children"

        oBtn3 := TBitmap():New(aObj4Size[5, 1], aObj4Size[5, 2], 25, 25,,"PREV", .T., oDlg,;
        {||gp240MoveChild(2, .T., oLbx2:nAt)},,.F.,.F.,,,.F.,,.T.,,.F.)
        oBtn3:CTOOLTIP := STR0299 // "Move Child"

        oBtn4 := TBitmap():New(aObj4Size[6, 1], aObj4Size[6, 2], 25, 25,,"PGPREV", .T., oDlg,;
        {||gp240MoveChild(2, .F., oLbx2:nAt)},,.F.,.F.,,,.F.,,.T.,,.F.)
        oBtn4:CTOOLTIP := STR0300 // "Move all children"

        nOpca := 0

        bSet15 := {|| nOpca := 1, oDlg:End()}
        bSet24 := {|| oDlg:End()}

        ACTIVATE MSDIALOG oDlg ON INIT (EnchoiceBar(oDlg, bSet15, bSet24))

        If nOpca == 1
            cChildrens := ""
            AEval(aLbx2, {|X| cChildrens += Iif(Empty(AllTrim(X[2])), "", X[2] + ",")})
            cChildrens := Iif(Len(cChildrens) > 0, SubStr(cChildrens, 1, Len(cChildrens) - 1), cChildrens)
            cChildrens := PadR(cChildrens, TamSx3("R8_CHILDS")[1], " ")
            If AllTrim(oGrid:GetValue("R8_CHILDS")) == AllTrim(cChildrens)
                lRes := .F.
                Break
            Else
                oGrid:LoadValue("R8_CHILDS", cChildrens)
                oView:Refresh("GPEA240_SR8")
            EndIf
        EndIf
        lRes := .F.
    End Sequence

Return lRes

/*/{Protheus.doc} gp240MontaChild(aLbx1, aLbx2)
    
    This function get lists for window for select children.

    @type Function
    @params aLbx1, Array, Array for select item.
    @params aLbx2, Array, Array for selected item.
    @return Nil
    @author dchizhov
    @since 10.10.2023
    @version 12.1.33
    @example gp240MontaChild(@aLbx1, @aLbx2)
*/
Static Function gp240MontaChild(aLbx1, aLbx2)

    Local aArea    As Array
    Local aSRBArea As Array
    Local cSelect  As Character
    Local nLenName As Numeric
    Local cType    As Character
    Local dDateVal As Date

    aLbx1 := {}
    aLbx2 := {}
    dDateVal := Date()

    cSelect := aCols[n, nPosVerba]
    nLenName := TamSx3("RB_NOME")[1]

    aArea := GetArea()
    aSRBArea := SRB->(GetArea())
    DbSelectArea("SRB")
    SRB->(DbSetOrder(RetOrdem("SRB", "RB_FILIAL+RB_MAT")))
    SRB->(DbGoTop())

    If SRB->(DbSeek(SRA->RA_FILIAL + SRA->RA_MAT))
        While !SRB->(EOF()) .And. SRB->RB_FILIAL == SRA->RA_FILIAL .And. SRB->RB_MAT == SRA->RA_MAT
            cType := Iif(SRB->RB_GRAUPAR $ "FE", Iif(dDateVal <= RU07XFUN41_AddMonthsForCareChild(SRB->RB_DTNASC), "1", "2"), "3")
            If SRB->RB_COD $ cSelect
                Aadd(aLbx2, {cType, SRB->RB_COD, SRB->RB_NOME, SRB->RB_DTNASC})
            Else
                Aadd(aLbx1, {cType, SRB->RB_COD, SRB->RB_NOME, SRB->RB_DTNASC})
            EndIf
            SRB->(DbSkip())
        EndDo
    EndIf

    If Len(aLbx1) == 0
        Aadd(aLbx1, {"1", Space(2), Space(nLenName), CToD("//")})
    EndIf
    If Len(aLbx2) == 0
        Aadd(aLbx2, {"1", Space(2), Space(nLenName), CToD("//")})
    EndIf

    SRB->(RestArea(aSRBArea))
    RestArea(aArea)

Return Nil

/*/{Protheus.doc} gp240MoveChild(nLbx, lSoUmaVerba, nPosLbx)
    
    This function get move children by list to list.

    @type Function
    @params nLbx, Numeric,        Number of listbox, which call function.
    @params lSoUmaVerba, Logical, Move one element or all elements.
    @params nPosLbx, Numeric,     Number of item for move.
    @return lSuccess, Logical, Is success.
    @author dchizhov
    @since 10.10.2023
    @version 12.1.33
    @example {||gp240MoveChild(1, .T., oLbx1:nAt)}
*/
Static Function gp240MoveChild(nLbx, lSoUmaVerba, nPosLbx)

    Local nCnt1     As Numeric
    Local nQtdDel   As Numeric
    Local nPosCod   As Numeric
    Local aLbxOrig  As Array
    Local aLbxDest  As Array
    Local cCodVerba As Character
    Local lSuccess  As Logical
    Local nLenName  As Numeric

    aLbxOrig  := Iif(nLbx == 1, aLbx1, aLbx2)
    aLbxDest  := Iif(nLbx == 1, aLbx2, aLbx1)
    cCodVerba := aLbxOrig[nPosLbx, 2]
    lSuccess := .T.
    nLenName := TamSx3("RB_NOME")[1]

    Begin Sequence
        // if Orig is empty
        If Len(aLbxOrig) == 1 .And. Empty(aLbxOrig[1, 2])
            Break
        EndIf

        // If target array is empty
        If Len(aLbxDest) == 1 .And. Empty(aLbxDest[1, 2])
            Adel(aLbxDest,1)
            ASize(aLbxDest, Len(aLbxDest) - 1)
        EndIf

        // add elements in target array and delete from orig
        If lSoUmaVerba
            Aadd(aLbxDest, {aLbxOrig[nPosLbx, 1], aLbxOrig[nPosLbx, 2], aLbxOrig[nPosLbx, 3], aLbxOrig[nPosLbx, 4]})
            Adel(aLbxOrig, nPosLbx)
            ASize(aLbxOrig, Len(aLbxOrig) - 1)
        Else
            nQtdDel := 0
            For nCnt1 := 1 To Len(aLbxOrig)
                Aadd(aLbxDest, {aLbxOrig[nCnt1, 1], aLbxOrig[nCnt1, 2], aLbxOrig[nCnt1, 3], aLbxOrig[nCnt1, 4]})
                aLbxOrig[nCnt1, 2] := Space(2)
                nQtdDel ++
            Next nCnt1
            aSort(aLbxOrig,,,{|x, y| x[2] > y[2] })
            ASize(aLbxOrig, Len(aLbxOrig) - nQtdDel)
        EndIf

        // Add element in empty orig list
        If Len(aLbxOrig) == 0
            Aadd(aLbxOrig, {"1", Space(2), Space(nLenName), CToD("//")})
        EndIf

        aSort(aLbxOrig,,,{|x, y| x[2] < y[2] })
        aSort(aLbxDest,,,{|x, y| x[2] < y[2] })

        // Positione on movable element
        If lSoUmaVerba
            nPosCod := Ascan(aLbxDest, { |x| x[2] == cCodVerba })
            If nPosCod > 0
                If(nLbx == 1, oLbx2:nAt := nPosCod, oLbx1:nAt := nPosCod)
                If(nLbx == 1, oLbx2:nRowPos := 6, oLbx1:nRowPos := 6)
            EndIf
        EndIf

        // Rewrite original array
        aLbx1 := If(nLbx == 1, aLbxOrig, aLbxDest)
        aLbx2 := If(nLbx == 1, aLbxDest, aLbxOrig)

        oLbx1:Refresh(.T.)
        oLbx2:Refresh(.T.)

    End Sequence

Return lSuccess

/*/{Protheus.doc} RUGPE24001_SetYearBalance()
    Calculation of the number of sick days for one type of absence per year.
    This function also changes the value of the paid days field (R8_DPAGAR) when the annual limit is exceeded.
	There is also a check for the number of days of absence at a time (RCM_DIASEM or R8_DIASEMP).

    Jira task: RULOC-5165

    @type Function
	@param 
    @return Numeric, Number of sick days per year.
    @author vselyakov
    @since 2023/10/13
    @version 12.1.33
    @example RULOC_3379()
*/
Function RUGPE24001_SetYearBalance()
    Local nBalanceYear := 0 As Numeric
    Local oModel := FWModelActivate() As Object
    Local oGrid := oModel:GetModel("GPEA240_SR8") As Object
    Local aArea := GetArea() As Array
    Local aRCMArea := RCM->(GetArea()) As Array
    Local nGridLength := oGrid:nLine As Numeric // 1 is subtracted because the last line is what is being added.
    Local nI := 0 As Numeric
    Local cTypeAbsence := "" As Character
    Local cYearAbsence := "" As Character
    Local nCurrentLine := 0 As Numeric // Calculation R8_BALYEAR
	Local nPaidDays := 0 As Numeric // Paid Days (R8_DPAGAR).

    // Calculation Days by year.
    If !Empty(oGrid:GetValue("R8_TIPOAFA"))
		// Accounting for the number of days per one absence (RCM_DIASEM or R8_DIASEMP).
		nPaidDays := Min(oGrid:GetValue("R8_DIASEMP"), oGrid:GetValue("R8_DPAGAR"))
		oGrid:LoadValue("R8_DPAGAR", Min(nPaidDays, oGrid:GetValue("R8_DURACAO"))) // Set limit for paid days.
		nPaidDays := 0

		// Check limits per one year.
        cTypeAbsence := oGrid:GetValue("R8_TIPOAFA")
        cYearAbsence := SubStr(oGrid:GetValue("R8_PER"), 1, 4)
        nCurrentLine := oGrid:nLine
        DbSelectArea("RCM")

        RCM->(DbSetOrder(1)) // RCM_FILIAL+RCM_TIPO.
        If RCM->(DbSeek(FwxFilial("RCM") + cTypeAbsence))
            If !Empty(RCM->RCM_LIMPDL)
                For nI := 1 To nGridLength
                    oGrid:GoLine(nI)

					/* If this is the last row in the grid, then the Days Paid field (R8_DPAGAR) 
					 * is checked to see if it exceeds the annual limit.
					*/
					If nI == nGridLength
						nPaidDays := Max(RCM->RCM_LIMPDL - nBalanceYear, 0)
						nPaidDays := Min(nPaidDays, oGrid:GetValue("R8_DPAGAR"))
						oGrid:LoadValue("R8_DPAGAR", nPaidDays) // Set limit for paid days.
					EndIf
                    
                    If oGrid:GetValue("R8_TIPOAFA") == cTypeAbsence .And. SubStr(oGrid:GetValue("R8_PER"), 1, 4) == cYearAbsence
                        nBalanceYear += oGrid:GetValue("R8_DPAGAR")
                    EndIf
                Next nI
            EndIf
        EndIf

        RCM->(RestArea(aRCMArea))
        RestArea(aArea)
        oGrid:GoLine(nCurrentLine)
    EndIf

Return nBalanceYear


/*/{Protheus.doc} RUGPE24002_GetDayForDpagar(cTypeAbs, dBegin, dEnd, nDuracao, cRaFil, cRaMat, lSeeWarn)

    Calculation of paid days depending on the type of absence

    @type Function
    @param cTypeAbs, Character, Code type of absence
    @param dBegin,   Date,      Date of begin absence
    @param dEnd,     Date,      Date of end absence
    @param nDuracao, Numeric,   Current duracao
    @param oGrid,    Object,    Grid for SR8
    @param cRaFil,   Character, Filial empl
    @param cRaMat,   Character, Mat Empl
    @param lSeeWarn, Logical,   See warning on screen
    @return Numeric, Number of sick days per year. (default = 99999)
    @author dchizhov
    @since 2023.11.20
    @version 12.1.33
    @example oGrid:SetValue("R8_DPAGAR", If( Empty(nEmpDias), 0, Min(nEmpDias,RUGPE24002_GetDayForDpagar(cAfast, dDtIni, dDtFim, nDuracao, oGrid)) ) )
*/
Function RUGPE24002_GetDayForDpagar(cTypeAbs, dBegin, dEnd, nDuracao, oGrid, cRaFil, cRaMat, lSeeWarn)

    Local nCalcDays := 99999  As Numeric
    Local cTypeOfDayAbs := "" As Character
    Local lUltSemana          As Logical
    Local nPosSem             As Numeric
    Local nLimit              As Numeric
    Local aRCMArea            As Array

    Private aPeriodo As Array

    Default cRaFil := SRA->RA_FILIAL
    Default cRaMat := SRA->RA_MAT
    Default lSeeWarn := .T.

    aRCMArea := RCM->(GetArea())

    cTypeOfDayAbs := Posicione("RCM", 1, FwxFilial("RCM") + cTypeAbs, "RCM_TIPODI")
    nLimit := oGrid:GetValue("R8_AGELIMI")
    nLimit := Iif(nLimit > 0, Min(nLimit, RCM->RCM_LIMPDL), RCM->RCM_LIMPDL)

    If Empty(dBegin) .Or. Empty(dEnd)

        If ValType(nDuracao) == "N"
            nCalcDays := nDuracao 
        EndIf
    
    Else
    
        If ValType(nDuracao) <> "N"
            nDuracao := dEnd - dBegin + 1
        EndIf

        If SubStr(DToS(dBegin), 1, 6) == SubStr(DToS(dEnd), 1, 6)

            fCarPeriodo(SubStr(DToS(dBegin), 1, 6), "FOL", @aPeriodo, @lUltSemana, @nPosSem)

            If cTypeOfDayAbs == "1"
                nCalcDays := RU07XFUN02_GetPeriodWorkingDays(dBegin, dEnd)
            ElseIf ValType(nDuracao) == "N"
                nCalcDays := nDuracao
            EndIf
        Else
            If cTypeOfDayAbs == "1" .And. lSeeWarn .And. !IsBlind()
                MsgInfo(STR0309) // "Since absence affects several periods, the calculation of working days for the paid days field will not be carried out"
            EndIf
            nCalcDays := nDuracao
        EndIf
    EndIf
    nCalcDays := Min(nCalcDays, nLimit)
    RCM->(RestArea(aRCMArea))

Return nCalcDays

/*/{Protheus.doc} RUGPE24003_GetMinLimForDpagar()

    Calcminimal limit for fill R8_DPAGAR. 

    @type Function
    @params 
    @return nLimit, Numeric, Minimal limit for DPAGAR.
    @author dchizhov
    @since 22.11.2023
    @version 12.1.33
    @example RUGPE24003()
*/
Function RUGPE24003_GetMinLimForDpagar()
    
    Local nLimit   As Numeric
    Local nDuracao As Numeric
    Local nDiasemp As Numeric
    Local cTypeAbs As Character
    Local oModel := FWModelActivate()
    Local oGrid := oModel:GetModel("GPEA240_SR8")

    nDuracao := oGrid:GetValue("R8_DURACAO")
    cTypeAbs := oGrid:GetValue("R8_TIPOAFA")

    nDuracao := RUGPE24002_GetDayForDpagar(cTypeAbs, oGrid:GetValue("R8_DATAINI"), oGrid:GetValue("R8_DATAFIM"), nDuracao, oGrid, , , .F.)

    nDiasemp := oGrid:GetValue("R8_DIASEMP")

    nLimit := Min(nDuracao, Iif(M->R8_AGELIMI > 0 .And. M->R8_AGELIMI < nDiasemp, M->R8_AGELIMI, nDiasemp))

Return nLimit

/*/{Protheus.doc} RUGPE24004_ReCalcRFDFALVAT()

    This function calculate filling field RF_DFALVAT. Count days that exclude from vacation experience/

    @type Function
    @return .T.
    @author dchizhov
    @since 28.03.2024
    @version 12.1.23.10
    @example RUGPE24004_ReCalcRFDFALVAT()
*/
Function RUGPE24004_ReCalcRFDFALVAT()

    Local aArea    As Array
    Local aSRFArea As Array
    Local aAbsType As Array
    Local aTemp    As Array
    Local aAbsShr  As Array
    Local cKey     As Character
    Local cPerIter As Character
    Local cPerMax  As Character
    Local dDateBeg As Date
    Local dDateFim As Date
    Local nX       As Numeric
    Local nDCount  As Numeric
    Local nTemp    As Numeric
    Local nItem    As Numeric

    aArea := GetArea()
    aSRFArea := SRF->(GetArea())
    cKey := xFilial("SRF") + SRA->RA_MAT

    DbSelectArea("SRF")
    SRF->(DbSetOrder(RetOrdem("SRF", "RF_FILIAL+RF_MAT")))

    If SRF->(DbSeek(cKey))
        While !SRF->(EOF()) .And. SRF->RF_FILIAL + SRF->RF_MAT == cKey
            If SRF->RF_STATUS <> "1"
                Loop
            EndIf

            cPerIter := AnoMes(SRF->RF_DATABAS)
            cPerMax := AnoMes(SRF->RF_DATAFIM)
            nDCount := 0
            While cPerIter <= cPerMax
                If Val(SubStr(cPerIter, 5, 2)) > 12
                    cPerIter := Soma1(SubStr(cPerIter, 1, 4)) + "01"
                    Loop
                EndIf
                dDateBeg := Max(SToD(cPerIter + "01"), SRA->RA_ADMISSA)
                dDateFim := Min(SRF->RF_DATAFIM, LastDate(dDateBeg))
                dDateFim := Min(dDateFim, Iif(!Empty(SRA->RA_DEMISSA) .And. SRA->RA_DEMISSA > SRA->RA_ADMISSA, SRA->RA_DEMISSA, dDateFim))
                aTemp := RU07XFUN42_GetS219Table(dDateBeg)
                For nX := 1 To Len(aTemp)
                    nItem := aScan(aAbsType, {|X| X[1] == aTemp[nX, 1] .And. X[2] == aTemp[nX, 2] .And. X[3] == aTemp[nX, 3]})
                    If nItem > 0
                        aTemp[nX, 4] := aAbsType[nItem, 4]
                    EndIf 
                Next nX
                aAbsType := aTemp
                aAbsShr := RU07XFUN43_GetAbsShrinkKNOExper(dDateBeg, dDateFim, , , aAbsType)
                For nX := 1 To Len(aAbsShr)

                    nItem := AScan(aAbsType, {|X| X[1] <= cPerIter .And. X[2] >= cPerIter .And. X[3] == aAbsShr[nX, 3]})
                    If nItem > 0
                        nTemp := Min(dDateFim, aAbsShr[nX, 2]) - Max(dDateBeg, aAbsShr[nX, 1]) + 1
                        If nTemp > aAbsType[nItem, 4]
                            nTemp -= aAbsType[nItem, 4]
                            aAbsType[nItem, 4] := 0
                        Else
                            aAbsType[nItem, 4] -= nTemp
                            nTemp := 0
                        EndIf
                        nDCount += nTemp
                    EndIf

                Next nX
                cPerIter := Soma1(cPerIter)
            EndDo

            If SRF->RF_DFALVAT <> nDCount
                RecLock("SRF", .F.)
                SRF->RF_DFALVAT := nDCount 
                SRF->(MsUnlock())
            EndIf
            SRF->(DbSkip())
        EndDo
    EndIf

    RestArea(aArea)
    SRF->(RestArea(aSRFArea))

Return .T.
