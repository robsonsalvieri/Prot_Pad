#INCLUDE "TOTVS.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "GPEA040.CH"
#INCLUDE "FWADAPTEREAI.CH"
#INCLUDE "INKEY.CH"

//-------------------------------------------------------------------
/*/{Protheus.doc} GPEA040RUS 
Establish Positions Register File (Russia)

@author E. Moskovkina
@since 05/12/2017
@version 1.0
@project DMA3 - Russia
/*/
//-------------------------------------------------------------------
Function GPEA040RUS()
	Local oBrowse as object

	oBrowse := BrowseDef()
	oBrowse:Activate()
	
Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} BrowseDef
Browse definition

@author E. Moskovkina
@since 05/12/2017
@version 1.0
@project DMA3 - Russia
/*/
//-------------------------------------------------------------------
Static Function BrowseDef()
	Local oBrowse as object

	oBrowse := FWLoadBrw("GPEA040")
	oBrowse:SetDescription(OemToAnsi(STR0009))
Return oBrowse 

//-------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Menu definition

@author E. Moskovkina
@since 05/12/2017
@version 1.0
@project DMA3 - Russia
/*/
//-------------------------------------------------------------------
Static Function MenuDef()
	Local aRotina :=  FWLoadMenuDef("GPEA040")
Return aRotina

/*/
{Protheus.doc} ModelDef()
    Defines GPEA040RUS Model

    @type Function
    @params
    @author dtereshenko
    @since 2020/06/18
    @version 12.1.23
    @return oModel    Object    GPEA040RUS Model
/*/
Static Function ModelDef()
    Local oModel As Object
    Local oStruSRV := FWFormStruct(1, "SRV")
    Local bPosValid	:= { |oModel| ModelPosValidation(oModel) }

    If MV_MODFOL == "1"
        oStruSRV:SetProperty("RV_DESMSEG", MODEL_FIELD_INIT, { |oMdl| DESMSEG(oMdl) })
        oStruSRV:AddTrigger("RV_CODMSEG", "RV_DESMSEG", {|| .T. }, { |oMdl| DESMSEGRU(oMdl) })
    EndIf

    oStruSRV:SetProperty("RV_EMPCONS", MODEL_FIELD_WHEN, { |oModel| FWhenEmpCons(oModel) })

    oModel := MPFormModel():New("GPEA040", /*bPreValid*/, bPosValid, /*bCommit*/, /*bCancel*/)

    oModel:AddFields("SRVMASTER", /*cOwner*/, oStruSRV, /*bPreValidacao*/, /*bPosValidacao*/, /*bCarga*/)
    oModel:SetDescription(OemToAnsi(STR0009))
    oModel:GetModel("SRVMASTER"):SetDescription(OemToAnsi(STR0009))

Return oModel


//-------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Definição do interface
@author E. Moskovkina
@since 05/12/2017
@version 1.0
@project DMA3 - Russia
/*/
//-------------------------------------------------------------------
Static Function ViewDef()
    Local oView    As Object
    Local oStruSRV As Object

    oView := FWLoadView("GPEA040")
    oStruSRV := oView:GetViewStruct("VIEW_SRV")

    oStruSRV:RemoveField("RV_REFPLR")
    oStruSRV:RemoveField("RV_ACUMAUX")
    oStruSRV:RemoveField("RV_CODDSR")
    oStruSRV:RemoveField("RV_SALFAMI")
    oStruSRV:RemoveField("RV_HRSATIV")
    oStruSRV:RemoveField("RV_DSRPROF")
    oStruSRV:RemoveField("RV_REFTAB")
    oStruSRV:RemoveField("RV_PIS")
    oStruSRV:RemoveField("RV_RRA")
    oStruSRV:RemoveField("RV_CONVCOL")
    oStruSRV:RemoveField("RV_MEDREAJ")
    oStruSRV:RemoveField("RV_COMPPRO")
    oStruSRV:RemoveField("RV_CODPRTR")
    oStruSRV:RemoveField("RV_CODCRI")
    oStruSRV:RemoveField("RV_HOMOLOG")
    oStruSRV:RemoveField("RV_CODDIFE")
    oStruSRV:RemoveField("RV_DESMEMO")
    oStruSRV:RemoveField("RV_FERSEG")
    oStruSRV:RemoveField("RV_CODMEMO")
    oStruSRV:RemoveField("RV_CODABO")
    oStruSRV:RemoveField("RV_CODMPA")
    oStruSRV:RemoveField("RV_REFABON")
    oStruSRV:RemoveField("RV_ORIGEM")
    oStruSRV:RemoveField("RV_CODREMU")
    oStruSRV:RemoveField("RV_INSSJUB")
    oStruSRV:RemoveField("RV_BSEREMT")
    oStruSRV:RemoveField("RV_TCMARG")
    oStruSRV:RemoveField("RV_AGLTRCT")
    oStruSRV:RemoveField("RV_DIRF")
    oStruSRV:RemoveField("RV_RAIS")
    oStruSRV:RemoveField("RV_GRPVERB")

Return oView
// Russia_R5

/*{Protheus.doc} Gpea040UserValidation(oModel)
    The function checks that the user has changed the data in the fields RV_NATUREZ, RV_INCIRF of the wage type.
    If the wage type has already participated in the calculations (available in the SRD table),
    then a warning is issued. 
    Further depends on the user's decision. 

    @type  Function
    @author vselyakov
    @since 2021/06/22
    @version 12.1.23
    @param oModel, Object, Object of model.
    @return lUserAnswer, Logical, User answer for continue saving.
    @example
    Gpea040UserValidation(oModel)
*/
Function Gpea040UserValidation(oModel)
    Local lUserAnswer := .T. As Logical
    
    // When user change fields RV_NATUREZ or RV_INCIRF of Type of Payment what exist on SRD by edit mode 
    // then show to user messagebox "There are calculation results with the previous value (code). Continue?"
    If (oModel:GetOperation() != MODEL_OPERATION_DELETE .And. oModel:GetOperation() != MODEL_OPERATION_INSERT .And. IsSRVCodeExistSRD(SRV->RV_COD))
        If (SRV->RV_NATUREZ != M->RV_NATUREZ .Or. SRV->RV_INCIRF != M->RV_INCIRF)
            lUserAnswer := MsgYesNo(STR0404, STR0017) // "There are calculation results with the previous value (code). Continue?"
        EndIf
    EndIf
Return lUserAnswer

/*{Protheus.doc} ModelPosValidation(oModel)
    Function PosValidation of model.
    If User confirm continue of saving then will be call 
    standart function of validation Gpea040TudOk(oModel).

    @type  Function
    @author vselyakov
    @since 2021/06/22
    @version 12.1.23
    @param oModel, Object, Object of model.
    @return lValidationResult, Logical, Result of validation.
    @example {|oMdl| ModelPosValidation(oMdl)}
*/
Static Function ModelPosValidation(oModel)
    Local lValidationResult := .F. As Logical

    If Gpea040UserValidation(oModel)
        lValidationResult := Gpea040TudOk(oModel)
    Else
        Help( ,, STR0024,, STR0406, 1, 0 )
    EndIf

Return lValidationResult

/*{Protheus.doc} IsSRVCodeExistSRD(cRvCode)
    The function checks if there are records in SRD for the specified type of payment.

    @type  Function
    @author vselyakov
    @since 2021/06/22
    @version 12.1.23
    @param cRvCode, Character, Value of editing RV_COD.
    @return lExist, Logical, There is record into SRD on RV_COD exists.
    @example
    IsSRVCodeExistSRD(SRV->RV_COD)
*/
Static Function IsSRVCodeExistSRD(cRvCode)
    Local lExist := .F.      As Logical
    Local oStatement         As Object
    Local aArea := GetArea() As Array
    Local cTab               As Character

    oStatement := FWPreparedStatement():New(" SELECT COUNT(*) AS LINES FROM " + RetSQLName("SRD") + " WHERE RD_FILIAL = ? AND RD_PD = ? AND D_E_L_E_T_=' ' ")
    oStatement:SetString(1, xFilial("SRD"))
    oStatement:SetString(2, cRvCode)

    cTab := MPSysOpenQuery(oStatement:GetFixQuery(), GetNextAlias())

    lExist := ((cTab)->LINES > 0)

    oStatement:Destroy()
    FwFreeObj(oStatement)
    RestArea(aArea)

Return lExist

/*/{Protheus.doc} DESMSEGRU
    This function return description for paying type selected in "for next period" - field

    @type function

    @param oModel, Object, Model

    @version 12.1.33
    @author DChizhov
    @since 2022/02/15

    @return cRet, Character, Description of payment type code

    @example DESMSEGRU(oMdl)
/*/
Function DESMSEGRU(oModel)

    Local aArea := GetArea() As Array
    Local cRet  := ''        As Character
    
    cRet := Fdesc('SRV', M->RV_CODMSEG, 'RV_DESC')
    cRet := PadR(cRet, GetSx3Cache('RV_DESMSEG', 'X3_TAMANHO'))

    RestArea(aArea)

Return cRet
