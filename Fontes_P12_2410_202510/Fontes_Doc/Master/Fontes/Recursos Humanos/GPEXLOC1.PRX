#INCLUDE "PROTHEUS.CH"  
#INCLUDE "GPEXLOC1.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³			ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.			  			³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data	³ FNC			 ³  Motivo da Alteracao 					³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Flavio Corre³25/02/15³PCREQ-2898      ³Inclusao fonte							³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³CalcLiq   ³ Autor ³ Equipe RH             ³ Data ³28/03/95  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Calculo o Liquido do Adiantamento                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ CalcLiq(nValAdto,nIrCalc)                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function CalcLiq(nValAdto,nIrCalc)

Local nLiqAdi	:= 0
Val_liq := 0

// Soma Para compor o Liquido do adiantamento
Aeval( aPd ,{ |x| If(x[1] <> aCodfol[006,1],SomaInc(x,13,@nLiqAdi,13,"S",,,,.F.,aCodFol ),"") } )

nLiqAdi	:= nLiqAdi + VAL_ADTO

Val_liq := ( nLiqAdi - nIrCalc )

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Sal_famiARG ³ Autor ³ Bruno Sobieski      ³ Data ³ 18.01.03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Carrega Variaveis com Tabela Sal.Familia                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cAnoMes  = Ano e mes da tabela                             ³±±
±±³          ³ aTabela  = Tabela onde sera devolvidos os valores          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Sal_FamiARG(aTabelaSF,cAnoMes)
Local aTabela	:=	{}                  


If FPHIST82(xFilial("SRX") , "10" , cAnoMes+"11" ) .Or. FPHIST82(xFilial("SRX") , "10" , "      11" )
	aTabela	:=	{'1',Array(6,2),0}
	aTabela[2,1,1] 	:= Val( SubStr( SRX->RX_TXT ,  1 , 10 ) )
	aTabela[2,1,2] 	:= Val( SubStr( SRX->RX_TXT , 11 , 08 ) )
	aTabela[2,2,1] 	:= Val( SubStr( SRX->RX_TXT , 19 , 10 ) )
	aTabela[2,2,2]		:= Val( SubStr( SRX->RX_TXT , 29 , 08 ) )
	aTabela[2,3,1] 	:= Val( SubStr( SRX->RX_TXT , 37 , 10 ) )
	aTabela[2,3,2]		:= Val( SubStr( SRX->RX_TXT , 47 , 08 ) )
	DbSelectArea('SRX')
	If (MsSeek( cFilial + "10" + cAnoMes + "12" ) .Or.MsSeek( cFilial + '10' + "      " + "12" ) )
		aTabela[2,4,1] 	:= Val( SubStr( SRX->RX_TXT ,  1 , 10 ) )
		aTabela[2,4,2] 	:= Val( SubStr( SRX->RX_TXT , 11 , 08 ) )
		aTabela[2,5,1] 	:= Val( SubStr( SRX->RX_TXT , 19 , 10 ) )
		aTabela[2,5,2]		:= Val( SubStr( SRX->RX_TXT , 29 , 08 ) )
		aTabela[2,6,1] 	:= Val( SubStr( SRX->RX_TXT , 37 , 10 ) )
		aTabela[2,6,2]		:= Val( SubStr( SRX->RX_TXT , 47 , 08 ) )
		aTabela[3]			:= Val( SubStr( SRX->RX_TXT , 55 , 03 ) )
   EndIf                                
	AAdd(aTabelaSF,aClone(aTabela))
EndIf   
If FPHIST82(xFilial("SRX") , "10" , cAnoMes+"21" ) .Or. FPHIST82(xFilial("SRX") , "10" , "      21" )
	aTabela	:=	{'2',Array(6,2),0}
	aTabela[2,1,1] 	:= Val( SubStr( SRX->RX_TXT ,  1 , 10 ) )
	aTabela[2,1,2] 	:= Val( SubStr( SRX->RX_TXT , 11 , 08 ) )
	aTabela[2,2,1] 	:= Val( SubStr( SRX->RX_TXT , 19 , 10 ) )
	aTabela[2,2,2]		:= Val( SubStr( SRX->RX_TXT , 29 , 08 ) )
	aTabela[2,3,1] 	:= Val( SubStr( SRX->RX_TXT , 37 , 10 ) )
	aTabela[2,3,2]		:= Val( SubStr( SRX->RX_TXT , 47 , 08 ) )
	DbSelectArea('SRX')
	If (MsSeek( cFilial + "10" + cAnoMes + "22" ) .Or.MsSeek( cFilial + '10' + "      " + "22" ) )
		aTabela[2,4,1] 	:= Val( SubStr( SRX->RX_TXT ,  1 , 10 ) )
		aTabela[2,4,2] 	:= Val( SubStr( SRX->RX_TXT , 11 , 08 ) )
		aTabela[2,5,1] 	:= Val( SubStr( SRX->RX_TXT , 19 , 10 ) )
		aTabela[2,5,2]		:= Val( SubStr( SRX->RX_TXT , 29 , 08 ) )
		aTabela[2,6,1] 	:= Val( SubStr( SRX->RX_TXT , 37 , 10 ) )
		aTabela[2,6,2]		:= Val( SubStr( SRX->RX_TXT , 47 , 08 ) )
		aTabela[3]			:= Val( SubStr( SRX->RX_TXT , 55 , 03 ) )
   EndIf                                
	AAdd(aTabelaSF,aClone(aTabela))
EndIf   
If FPHIST82(xFilial("SRX") , "10" , cAnoMes+"31" ) .Or. FPHIST82(xFilial("SRX") , "10" , "      31" )
	aTabela	:=	{'3',Array(6,2),0}
	aTabela[2,1,1] 	:= Val( SubStr( SRX->RX_TXT ,  1 , 10 ) )
	aTabela[2,1,2] 	:= Val( SubStr( SRX->RX_TXT , 11 , 08 ) )
	aTabela[2,2,1] 	:= Val( SubStr( SRX->RX_TXT , 19 , 10 ) )
	aTabela[2,2,2]		:= Val( SubStr( SRX->RX_TXT , 29 , 08 ) )
	aTabela[2,3,1] 	:= Val( SubStr( SRX->RX_TXT , 37 , 10 ) )
	aTabela[2,3,2]		:= Val( SubStr( SRX->RX_TXT , 47 , 08 ) )
	DbSelectArea('SRX')
	If (MsSeek( cFilial + "10" + cAnoMes + "32" ) .Or.MsSeek( cFilial + '10' + "      " + "32" ) )
		aTabela[2,4,1] 	:= Val( SubStr( SRX->RX_TXT ,  1 , 10 ) )
		aTabela[2,4,2] 	:= Val( SubStr( SRX->RX_TXT , 11 , 08 ) )
		aTabela[2,5,1] 	:= Val( SubStr( SRX->RX_TXT , 19 , 10 ) )
		aTabela[2,5,2]		:= Val( SubStr( SRX->RX_TXT , 29 , 08 ) )
		aTabela[2,6,1] 	:= Val( SubStr( SRX->RX_TXT , 37 , 10 ) )
		aTabela[2,6,2]		:= Val( SubStr( SRX->RX_TXT , 47 , 08 ) )
		aTabela[3]			:= Val( SubStr( SRX->RX_TXT , 55 , 03 ) )
   EndIf                                
	AAdd(aTabelaSF,aClone(aTabela))
EndIf   
If FPHIST82(xFilial("SRX") , "10" , cAnoMes+"A1" ) .Or. FPHIST82(xFilial("SRX") , "10" , "      A1" )
	aTabela	:=	{'A',Array(6,2),0}
	aTabela[2,1,1] 	:= Val( SubStr( SRX->RX_TXT ,  1 , 10 ) )
	aTabela[2,1,2] 	:= Val( SubStr( SRX->RX_TXT , 11 , 08 ) )
	aTabela[2,2,1] 	:= Val( SubStr( SRX->RX_TXT , 19 , 10 ) )
	aTabela[2,2,2]		:= Val( SubStr( SRX->RX_TXT , 29 , 08 ) )
	aTabela[2,3,1] 	:= Val( SubStr( SRX->RX_TXT , 37 , 10 ) )
	aTabela[2,3,2]		:= Val( SubStr( SRX->RX_TXT , 47 , 08 ) )
	DbSelectArea('SRX')
	If (MsSeek( cFilial + "10" + cAnoMes + "A2" ) .Or.MsSeek( cFilial + '10' + "      " + "A2" ) )
		aTabela[2,4,1] 	:= Val( SubStr( SRX->RX_TXT ,  1 , 10 ) )
		aTabela[2,4,2] 	:= Val( SubStr( SRX->RX_TXT , 11 , 08 ) )
		aTabela[2,5,1] 	:= Val( SubStr( SRX->RX_TXT , 19 , 10 ) )
		aTabela[2,5,2]		:= Val( SubStr( SRX->RX_TXT , 29 , 08 ) )
		aTabela[2,6,1] 	:= Val( SubStr( SRX->RX_TXT , 37 , 10 ) )
		aTabela[2,6,2]		:= Val( SubStr( SRX->RX_TXT , 47 , 08 ) )
		aTabela[3]			:= Val( SubStr( SRX->RX_TXT , 55 , 03 ) )
   EndIf                                
	AAdd(aTabelaSF,aClone(aTabela))
EndIf   


If Len(aTabelaSF) < 4
	If GetRemoteType() == -1
		MsgLogGrid( Ap5GetHelp("GPEXSFAM"), .F. ) // Retornar string da funcao Help
	Else
	  	Help(" ",1,"GPEXSFAM")
	EndIf
	Return ( .F. )                                                  
EndIf
Return ( .T. )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ FValoriza³ Autor ³ Mauro                 ³ Data ³ 23.03.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valorizacao dos codigos da Matriz aPd                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aPd      =  Matriz com as Verbas a Serem Valorizadas       ³±±
±±³          ³ aCodfol  =  Matriz Com as Verbas de Referencia             ³±±
±±³          ³ SalMes   =  Salario Mensal                                 ³±±
±±³          ³ SalDia   =  Salario Mensal                                 ³±±
±±³          ³ SalHora  =  Salario Mensal                                 ³±±
±±³          ³ SalMin   =  Salario Minimo                                 ³±±
±±³          ³ nDiasrop =  Dias Proporcionais                             ³±±
±±³          ³ lMontaPd =  Se Deve Buscar Movto. Src para Array aPd       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Necessario³ Estar Carregado os Parametros Adic. Tempo Serv.            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Util      ³ Se For Criado no Programa chamador as Variaveis            ³±±
±±³          ³ nInsal,nPeric e nAdtServ Retorna o Valor da Ins.Per.Ad.    ³±±
±±³          ³ cCodAdt  =  Opcional-Codiga do Calculo Ad.Tempo Serv.      ³±±
±±³          ³ cCodIns  =  Opcional-Codigo do Calc. da Insalub.           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function FValoriza(aPd,aCodfol,SalMes,Saldia,SalHora,SalMin,nDiasProp,lIncorp,lMontaPd,lPropTarefa)

Local cVerbAdic := aCodFol[01,1]+"*"+aCodFol[02,1]+"*"+aCodFol[03,1]+"*"+aCodFol[04,1]+"*"+aCodFol[05,1]+"*"+;
                   aCodFol[36,1]+"*"+aCodFol[37,1]+"*"+aCodFol[38,1]+"*"+aCodFol[39,1]+"*"+aCodFol[984,1]+"*"+aCodFol[988,1]
Local lPropAdic := .T.
Local lFatorHE	:= .F.
Local N			:= 0       
Local SavSalHora:= 0
Local SavSaldia	:= 0
Local nVlHrBase := 0
Local nTotHrEfe	:= 0 
Local nHrsEfTrab:= 0 
Local aHrExtras	:= {} 
Local nPos
Local nPosAlias := 0
Local nSalHoraC	:= 0    
Local lComissBRA:= .F.  
Local lTemComiss:= .F.  
Local cCodSRV	:= ''	

Local lUsaMin	:= GETNEWPAR("MV_USASMIN",.T.) 	

//--Situacao do Funcionario na data de referencia
cSitFolh	:= If( type("cSitFolh")=="U",SRA->RA_SITFOLH,cSitFolh)

//--Parametro utilizado na Rescisao para nao incorporar
//--e nao calcular Adicionais na 2o. chamada
lIncorpSal := If(lIncorp = Nil , .T. , lIncorp) // Utilizada no Ponto de Entrada INCORPSL

//--Parametro para Buscar Movto. SRC para o aPd
lMontaPd := If (lMontaPd = Nil , .F. , lMontaPd)

//--Para que indica se deve proporcionalizar tarefas
lPropTarefa := If( lPropTarefa == Nil, .F., lPropTarefa)

//--Variavel para indicar se rescisao foi calculada pelo sistema para 
//-- não valorizar as verbas 
lGerRes := If (Type("lGerRes") == "U" .Or. lGerRes == Nil  , .F. , lGerRes)

// Carrega a Matriz Caso Nao Exista Ainda
If ((aPd = Nil .Or. Len(aPd) = 0 ) .And. SalMes # Nil) .Or. lMontaPd
	// aPd := {}  // LIMPA A MATRIZ DO SRC
	dbSelectArea( "SRC" )
	dbSeek( SRA->RA_FILIAL + SRA->RA_MAT )
	While SRC->( !Eof() .And. SRA->RA_FILIAL + SRA->RA_MAT == RC_FILIAL + RC_MAT )
		cDel := " "
		If SRC->RC_TIPO2 $ "CKR"
			cDel := "D"
		EndIf
		SRC->( FMatriz(RC_PD,RC_VALOR,RC_HORAS,RC_SEMANA,RC_CC,RC_TIPO1,RC_TIPO2,RC_PARCELA,cDel,If(cDel#'D',SRC->RC_DATA,),,RC_SEQ ) )
		dbSelectArea( "SRC" )
		dbSkip()
	Enddo
EndIf

If SalMes # Nil
	// DEIXA A MATRIZ NA ORDEM DE VERBA
	aPd := aSort(aPd,,,{ |x,y| x[1]+x[11] < y[1]+y[11] })
EndIf

//-- Para o Brasil verifica a existencia de lancamento de verba de comissao
If (lComissBRA := ( ( cPaisLoc == "BRA" )  .and.  (SRA->RA_CATFUNC == "C" )) )
	If ( (	nPos := Ascan(aPd,{ |X| X[1] = aCodfol[165,1] .And. X[3] = cSemana .And. X[9] # "D"}) ) > 0)
		lTemComiss := .T.
	EndIf	
EndIf

// Ponto de Entrada para ordenar o array aPd
// Para uso em incorporacoes salarias fora da regra padrao do sistema
If ExistBlock( "GPEINCSL" )
	ExecBlock("GPEINCSL",.F.,.F.)
EndIf

For N = 1 To Len(aPd)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ N„o deve valorizar verbas de outras semanas ou Verbas Deletadas³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If AllTrim( aPd[n,3] ) # AllTrim( cSemana ) .Or. aPd[n,9] == "D"
		Loop
	EndIf

	//--Nao Valorizar Verbas de Rescisao
	If cTipoRot == "1" .And. aPd[n,7] $ "R/I" .And. lGerRes
		Loop
	EndIf
	
	//--Nao Valorizar Verbas de Periculosidade e Insalubridade no adiantamento 
	//--se o mnemonico P_SOMAINC estiver desativado
	If cTipoRot == "2" .And. !P_ADCINTEG .And. ( aPd[n,1] $ aCodFol[36,1] + "*" + aCodFol[37,1] + "*" + aCodFol[38,1] + "*" + aCodFol[39,1] )
		Loop
	EndIf

	//--Quando for Rescisao, nao valoriza as verbas informadas vindas do SRC pois ja foram valorizadas
	//--no roteiro da Folha
	If cTipoRot == "4" .And. IsInCallStack("GPEM040");
	   .And. ( ( nPosAlias := GdFieldPos( "RR_ALI_WT") ) > 0 );
	   .And. ( aPd[n, 7] != "I" .Or. aScan( aCols, { |x| x[1] == aPd[n, 1] .And. x[nPosAlias] == "SRC" } ) > 0 )
			Loop
	EndIf

	//--Nao Valorizar Verbas de Periculosidade e Insalubridade no adiantamento 
	//--se o mnemonico P_SOMAINC estiver desativado
	If cTipoRot == "2" .And. !P_ADCINTEG .And. ( aPd[n,1] $ aCodFol[36,1] + "*" + aCodFol[37,1] + "*" + aCodFol[38,1] + "*" + aCodFol[39,1] )
		Loop
	EndIf

	// Posiciona na Matriz de Incidencia
	nPosv := ASCAN(aPdv, { |Y| Y[1]=aPd[n,1] })
	
	// Fator de Hora Extra para o Chile - utiliza um fator sobre o salario
	If cPaisLoc=="CHI" .And. aPdv[nPosv,17]=="S"
		SavSalHora	:= SalHora
		SavSaldia	:= Saldia 
		lFatorHE	:= fCalHeFator()
	EndIf

	// Diferente de Insalubridade e Periculosidade
	If (SalMes # Nil .Or. (SalMes = Nil .And. aPdv[nPosv,18] # "S" )) .And.;
		Empty(aPdv[nPosv,31]) //Tarefa
		// Quando for Periculosidade ou Insalubridade informada e nao tem no cadastro, calcular
		// caso contrario ja foi calculada pelo incorpora.    
		If !aPd[n,1] $ cVerbAdic .Or. ;
			 ( aPd[n,1] $ cVerbAdic .And. aPd[n,5] = 0 .And. aPd[n,7] $ "I*F*G" )		
			If aPd[n,6] = "H"
				aPd[n,5] := Round((Salhora * aPd[n,4] ) * (aPdv[nPosv,2] / 100 ),MsDecimais(1))
				aPd[n,9] := " "
			ElseIf aPd[n,6] = "F" .And. aPd[n,5] = 0.00
				aPd[n,5] := Round((Salhora * aPd[n,4] ) * (aPdv[nPosv,2] / 100 ),MsDecimais(1))
				aPd[n,9] := " "
			ElseIf aPd[n,6] = "D"
				aPd[n,5] := Round((Saldia * aPd[n,4]) * (aPdv[nPosv,2] / 100),MsDecimais(1))
				aPd[n,9] := " "
			EndIf
		EndIf
	EndIf
	
    //Restaura o valor original apos o calculo Fator de Hora Extra
    If lFatorHE
	    SalHora	:= SavSalHora
	    Saldia	:= SavSaldia
	    lFatorHE := .F.
	EndIf	

	// Quando Incorporar o Salario
   If SalMes # NIl .And. PosSrv(aPd[n,1],Sra->Ra_Filial,"Rv_Incorp") = "S"

	   	If ExistBlock("INCORPSL")
			If !ExecBlock("INCORPSL",.F.,.F.)
				Loop
			EndIf
		EndIf

		If lIncorpSal 
		    
		    nValor	:= aPd[n,5]
		    
		    If lDissidio
			    //-- Se for insalubirdade/periculosidade, assume o valor integral
			    //-- na composicao do salario do mes
			    If Type("nIntInsal") <> 'U' .and. ( aPd[n,1] $ aCodFol[37,1]+"*"+aCodFol[38,1]+"*"+aCodFol[39,1] )
			    	nValor	:= if( nIntInsal > 0, nIntInsal, aPd[n,5] )

					// Se o funcionario esteve de ferias o mes todo, o valor da insalubridade ja esta incorporado na verba das ferias
					If ( cSitFolh = "F" .and. DiasTrab == 0 .and. cTipoRot == "1")
						nValor := 0.00
					EndIf

	            ElseIf Type("nIntPercul") <> 'U' .and. ( aPd[n,1] $ aCodFol[36,1])
	            	nValor	:= if( nIntPercul > 0, nIntPercul, aPd[n,5] )
	            EndIf
			EndIf
			
			SalMes  += nValor
			SalHora += (nValor / SRA->RA_HRSMES)    //--(SalMes /0 SRA->RA_hrsmes)
			SalDia  += (nValor / 30)                //--SalMes / 30
			
			//Caso seja a rotina de rescisao, atualiza o cabecalho do salario com os valores com a incorporacao			
			If cTipoRot == "4" .And. IsInCallStack( "GPEM040" )
				M->RG_SALMES  := SalMes
				M->RG_SALDIA  := SalDia
				M->RG_SALHORA := SalHora
			EndIf
		EndIf
        
		//-- Quando o funcionario tem dias proporcionais,as verbas ja estao proporcionalizadas, então nao faz a proporcionalizacao
		lPropAdic := .T.
		If nDiasProp # 0 .AND.	( aPd[n,1] $ ( aCodfol[36,1]+'.'+aCodfol[37,1]+'.'+aCodfol[38,1]+'.'+ aCodfol[39,1]) .and. ldissidio )
				lPropAdic := .F.
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//| Nao fazer a proporcao se as verbas forem informadas ou geradas.						   |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aPd[n,7] $ "I*G"  
			lPropAdic := .F.
		EndIf
		//-- Proporcionaliza aos Dias Trabalhados
		If nDiasProp # Nil .And. nDiasProp >= 0 .And. ! aPd[n,1]$ aCodFol[165,1]+'*'+aCodFol[166,1]+'*'+aCodFol[347,1] .And.;
		   (Empty(aPdv[nPosv,31]) .Or. (!Empty(aPdv[nPosv,31]) .And. lPropTarefa))  ;
		   .And. lPropAdic
			//-- Proporcionalizar as Horas ou Dias e gravar para gravar Qtd.
			If aPd[n,6] $ "D*H" .And. aPd[n,4] > 0
				aPd[n,4] := ( (aPd[n,4] / 30 ) * nDiasProp )
			Else
				aPd[n,4] := nDiasProp
			EndIf
			aPd[n,5] := Round(( (aPd[n,5] / 30 ) * nDiasProp ),MsDecimais(1))
		EndIf   
		
	
	//--Quando nao incorpora faz proporcional as verbas de Insal.Peric. e Ad.Tempo Serv.
	Elseif (aPd[n,1] $ cVerbAdic .And. ! aPd[n,7] $ "I*G" ) // Nao fazer a proporcao se as verbas forem informadas ou geradas.

		//-- Verifica se os Adicionais nao forem sobre soma de verbas proporcionalizar
		//-- Caso contratio as verbas ja estao proporcionalizadas
		lPropAdic := .T.
		If nDiasProp # 0
			If (aPd[n,1] = aCodfol[37,1] .And. Sra->Ra_Insmin = 999.99) .Or. ;
					(aPd[n,1] = aCodfol[38,1] .And. Sra->Ra_Insmed = 999.99) .Or. ;
					(aPd[n,1] = aCodfol[39,1] .And. SRA->RA_Insmax = 999.99) .Or. ;
					( aPd[n,1] $ ( aCodfol[37,1]+'.'+aCodfol[38,1]+'.'+ aCodfol[39,1]) .and. ldissidio)
				lPropAdic := .F.
			Elseif aPd[n,1] = aCodFol[36,1] .And. SRA->RA_PERICUL = 999.99
				lPropAdic := .F.
			EndIf
		EndIf

		//-- Proporcionalizar as verbas de Adicionais		
		If nDiasProp # Nil .And. lPropAdic .And. SRA->RA_TIPOPGT # "S"
			If nDiasMat > 0
				/*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				  | Se ouver licenca maternidade, utiliza os dias de licenca na proporcionalizacao						 |
				  | Funcionaria de licenca maternidade tem direito a todos os adicionais que recebia antes do afastamento| 
				  | Art. 393 da CLT                                                                                      |
				  ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				aPd[n,5] := Round( ( (aPd[n,5] / 30 ) * Min(nDiasMat+nDiasProp, 30) ) , MsDecimais(1) )
			Else
				aPd[n,5] := Round( ( (aPd[n,5] / 30 ) * Min(nDiasProp, 30) ) , MsDecimais(1) )
			EndIf			
			If aPd[n,5] <= 0.00
				aPd[n,9] := "D"
			EndIf
		EndIf
	
	EndIf

	//-- Codigo correspondente
	If PosSrv(aPd[n,1],SRA->RA_FILIAL,"RV_CODCORR") # "   " .And. aPd[n,9] # "D"
		nPosApd	:= 0
		nPosApd	:= Ascan(aPd , { |x| X[1] == SRV->RV_CODCORR .And. X[3] == aPd[n,3] .And. X[2] == aPd[n,2] .And. X[11] == aPd[n,11] .And. X[9] # "D" })
        If nPosApd > 0
			If !(aPd[nPosApd][7] $ "I/E/G") // Tratamento feito quando estiver uma verba correspondente informada.
				aPd[nPosApd][5] += aPd[n,5]
				aPd[nPosApd][4] += aPd[n,4]  				
			EndIf
            
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ *** Novo Tratamento atraves da correspondencia entre verbas de horas extras *** 			   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			//--Trata Valorizacao de horas extras para comissionista no Brasil 
			If lTemComiss .And. ( aPdv[nPosv,17]=="S" )
				Aadd(aHrExtras,nPosApd)
			EndIf	

        Else				
			
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ *** Novo Tratamento atraves da correspondencia entre verbas de horas extras *** 			   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			//-- Tratamento de horas extras para comissionista para o Brasil
			If 	( cPaisLoc == "BRA" )
			    //-- Verba de horas extras
				If	( aPdv[nPosv,17]=="S" )  
					If lTemComiss 
						//-- Os codigos correspondentes de horas extras somente serão tratados na ocorrencia de comissao
						FMatriz( SRV->RV_CODCORR,aPd[n,5],aPd[n,4],aPd[n,3],aPd[n,2],aPd[n,6], , , ,aPd[n,10], ,aPd[n,11] ) 			
						nPosApd	:= Ascan(aPd , { |x| X[1] == SRV->RV_CODCORR .And. X[3] == aPd[n,3] .And. X[2] == aPd[n,2] .And. X[11] == aPd[n,11] .And. X[9] # "D" })
		
						//--Trata Valorizacao de horas extras para comissionista no Brasil  
						//-- *** Novo Tratamento atraves da correspondencia entre verbas de horas extras ***
						If (nPosApd > 0)
							Aadd(aHrExtras,nPosApd)
						EndIf
					EndIf		
				Else
					FMatriz( SRV->RV_CODCORR,aPd[n,5],aPd[n,4],aPd[n,3],aPd[n,2],aPd[n,6], , , ,aPd[n,10], ,aPd[n,11] ) 				
				EndIf
			Else
				FMatriz( SRV->RV_CODCORR,aPd[n,5],aPd[n,4],aPd[n,3],aPd[n,2],aPd[n,6], , , ,aPd[n,10], ,aPd[n,11] ) 
			EndIf

		EndIf
	Else
		
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ *** Novo Tratamento atraves da correspondencia entre verbas de horas extras *** 			   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	    //-- Apura horas efetivamente trabalhadas para comissionista no Brasil
		//-- Aqui sao acumuladas as qtde de horas Extras Originais (para nao corrermos o risco de incluir as correspondentes) 
		If 	lTemComiss .And.  ( aPdv[nPosv,17]=="S" ) 
			//-- Comissionista puro
		    If Empty(SRA->RA_SALARIO)
		    	//-- Baseado na verba correspondente, obtem a verba principal a qual esta associada.
		    	fHECodCorr(SRA->RA_FILIAL, aPd[n,1], @cCodSRV)
		    	//--Verifica se o Novo tratamento das horas extras estah ativo para comissionista
		    	//-- ou seja existe uma verba extra principal com codigo correspondente
		    	If !Empty(cCodSRV)
			    	//-- Se a verba principal nao foi informada, ou seja, o usuario digitou (ou veio direto do ponto) a verba de hora extra especifica
			     	//-- para comissao
			    	If 	Empty( nPosApd	:= Ascan(aPd , { |x| X[1] == cCodSRV .And. X[3] == aPd[n,3] .And. X[2] == aPd[n,2] .And. X[11] == aPd[n,11] .And. X[9] # "D" }))  
						//-- Adiciona a verba especifica de hora extra para comissao para ser valorizada
						//--Trata Valorizacao de horas extras para comissionista no Brasil 
						Aadd(aHrExtras,n)
			    	EndIf
			    EndIf	
		    EndIf
		EndIf
	EndIf

	//-- Deletar a Verbas de  Peric., Insal. e Adicionais do aPD quando Roteiro FOL e Funcionario Demitido
    If aPd[n,7] == "C" .and. aPD[n,1] $ cVerbAdic .and. cSITFOLH == "D"
	    aPd[n,9] := "D"
    EndIf

Next
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ *** Novo Tratamento atraves da correspondencia entre verbas de horas extras *** 			   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/	
//-- Valoriza as horas extras correspondentes para comissionista
If lTemComiss .and. ( !Empty(aHrExtras) ) 
   fHrTrabDesc(@nHrsEfTrab,cSemana)
   nTotHrEfe	:= nHrsEfTrab
   //-- Calculo das horas efetivamente trabalhadas
   For nPos:= 1 To Len(aHrExtras)
		nTotHrEfe+=aPd[aHrExtras[nPos]][4]
   Next	
   
   //-- Se Existir verba de comissao
   If (	nPos := Ascan(aPd,{ |X| X[1] = aCodfol[165,1] .And. X[3] = cSemana .And. X[9] # "D"}) ) > 0
 	  nSalHoraC:= aPd[nPos,5] / nTotHrEfe
	   //-- Valorizacao das horas extras sobre comissao
	   For nPos:= 1 To Len(aHrExtras)                                 
		   	// Posiciona na Matriz de Incidencia
			nPosv := ASCAN(aPdv, { |Y| Y[1]=aPd[aHrExtras[nPos],1] })
			aPd[aHrExtras[nPos],5] := Round((nSalHoraC * aPd[aHrExtras[nPos],4] ) * (aPdv[nPosv,2] / 100 ),MsDecimais(1))
	   Next
   EndIf 
EndIf
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ AssiMed  ³ Autor ³ Equipe RH             ³ Data ³28/03/95  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Calculo da Assistencia Medica                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AssiMed(aCodfol)                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ´±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function AssiMed(aCodfol,lCalcDem)

Local q			:= 0     
Local lCalcNovo := If( SuperGetMv("MV_ASSIMED",,"2") == "2", .T. , .F. )	
Local nPercMax 	:= 0

Static 	cCodAssM

//--Situacao do Funcionario na data de referencia
cSitFolh	:= If( type("cSitFolh")=="U",SRA->RA_SITFOLH,cSitFolh)

DEFAULT lCalcDem := .F.
                      
If lCalcNovo

	fAssMed2(aCodfol)

Else
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Para funcion rios que recebem semanalmente, deve ser calculado  ³
	//³ somente na £ltima semana do mˆs                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SRA->RA_TIPOPGT = "S" .And. ! lUltSemana
		Return
	EndIf
	
	If ( cSitFolh # "D" .or. ( cSitFolh == "D" .and. lCalcDem ) ) .And. SRA->RA_ASMEDIC # Space(2) .And. CalcAsmed .And. aCodfol[49,1] # Space(3)
		If SubStr(SRA->RA_ASMEDIC,1,1) # "E"
			BaseAmed := (titular + (Val(SRA->RA_DPASSME) * Depasme)) * Percasm / 100    			
			FMatriz(aCodfol[49,1],BaseAmed,Val(SRA->RA_DPASSME)+1)
			FMatriz(aCodfol[213,1],((titular + (Val(SRA->RA_DPASSME) * Depasme))-BaseAmed),Val(SRA->RA_DPASSME)+1)
		Elseif SubStr(SRA->RA_ASMEDIC,1,1) = "E"
			cSalmes := Salmes
			For q=1 to 6
				cLimsal := Str(Q,1)
				If cSalmes < nLim&cLimSal.
					BaseAmed := ((nPercD&cLimSal. * nVlrPlano) / 100) * Val(SRA->RA_DPASSME) + (nPercF&cLimSal. * nVlrPlano) / 100
					FMatriz(aCodfol[49,1],BaseAmed,Val(SRA->RA_DPASSME)+1)
					FMatriz(aCodfol[213,1],((nVlrPlano*(Val(SRA->RA_DPASSME)+1))-BaseAmed),Val(SRA->RA_DPASSME)+1)
					Exit
				EndIf
			Next
		EndIf
	EndIf
	            
EndIf

Return( NIL )

/*/ 
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Val_Refe ³ Autor ³ Stiefano              ³ Data ³ 22/08/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Carrega Tabela de Vale Refeicao                            ´±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Val_Refe(aRefeicao,cFil, cTnoTrab, dDtRefer )
Local cAlias := Alias()
Local nValRefe := nNumVale := nPerDesc := nTetoRefe := 0 
Local cCodigo    := ""
Local cDesc      := ""
Local lUseParamVR	:= .T.  
Local aSR5Area	:= SR5->(GetArea()	)  
Local nFilQtos 	:= 0
Local nEmpDE 	:= 0
Local nEmpQtos	:= 0

DEFAULT dDtRefer	:= dDataBase

cTnoTrab	:= SRA->RA_TNOTRAB

aRefeicao := {}
dbSelectArea("SRX")

If (cFil # Nil .And. Empty(cFilial)) .Or. cFil == Nil
	cFil := cFilial
EndIf   

               
If FPHIST82( cFil , "26" )
	dbSelectArea("SR5")
	dbSetOrder(1)
		
	If 	SR5->( dbSeek( xFilial("SR5") + "X2601") )
		While SR5->( !Eof() .and. R5_FILIAL + R5_ARQUIVO + R5_REG ==  (xFilial("SR5")+ "X2601"))
			If AllTrim(R5_CAMPO) == "X26_FILIAL"
				nFilQtos 	:= SR5->R5_QUANTOS    
			ElseIf AllTrim(R5_CAMPO) == "X26_EMPRE1"	
				nEmpDE 		:= SR5->R5_DE   
				nEmpQtos 	:= SR5->R5_QUANTOS    
			EndIf  
			SR5->( dbSkip() )
		EndDo
	EndIf
    RestArea(aSR5Area)
    
    dbSelectArea("SRX")
    
	While ! Eof() .And. SRX->RX_TIP = "26" 
		lUseParamVR	:= .T. 
		cCodigo    := Subs(SRX->RX_COD, nFilQtos+1,2)
		cDesc      := SubStr(SRX->RX_TXT,1,16)
		nValRefe   := Val(SubStr(SRX->RX_TXT,21,12))
		nNumVale   := Val(SubStr(SRX->RX_TXT,33,03))
		nPerDesc   := Val(SubStr(SRX->RX_TXT,36,07))
		nTetoRefe  := Val(SubStr(SRX->RX_TXT,43,12))
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ A qtde de VR informada no CAd.Periodo somente sera considerada  |
		//³ se nao houver qde de VR informada no param. 26                  |		
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nNumVale = 0
			nNumVale   	:= fDiasVR(dDtRefer,,@cTnoTrab)			//-- Utiliza Cadastro de Periodo 
			lUseParamVR	:= .F. 
		EndIf
		Aadd(aRefeicao,{ cCodigo ,nValRefe,nNumVale,nPerDesc,nTetoRefe , cDesc, lUseParamVR, Subs(SRX->RX_COD,nEmpDE, nEmpQtos ) })
		dbSkip( ) 
	EndDo                            
EndIf 

dbSelectArea(cAlias) 
Return ( .T. ) 



/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fDiasVR  ³ Autor ³ R.H. Natie            ³ Data ³ 11/03/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Carrega Dias de Vale Refeicao do Cadastro de Periodo       ´±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fDiasVR( dDtRefer , lShowHelp , cTnoTrab )
                              
Local aAreaSRX		:= SRX->(GetArea())
Local aAreaSRA		:= SRA->(GetArea())
Local nValeRefe		:= 0
Local cCompetencia	:= "" 
Local cMsgHelp		:= "" 
Local cTurnoRCF		:= ""

cCompetencia		:= MesAno(dDtRefer)
cTurnoRCF			:= cTnoTrab 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Retorna dias Vale Refeicao                                   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("RCF")
If !DbSeek(xFilial("RCF") + cCompetencia  + cTurnoRCF ) 
	cTurnoRCF	:= "   "    
	If !dbSeek(xFilial("RCF") + cCompetencia + "   " ) 
		DEFAULT lShowHelp := IF( FindFunction( "AddLogExecRot" ) , .F. , .T. ) 
		IF ( lShowHelp ) 
			If GetRemoteType() == -1
				MsgLogGrid( Ap5GetHelp("GPCALEND"), .F. ) // Retornar string da funcao Help
			Else
				HELP( " ",1,"GPCALEND",  )						//--Nao existe periodo cadastrado 
			EndIf
		ElseIf FindFunction( "AddLogExecRot" ) 
			cMsgHelp := "Function: " 
			cMsgHelp += ProcName() 
			cMsgHelp += "/Line: " 
			cMsgHelp += AllTrim( Str( ProcLine() ) ) 
			cMsgHelp += " - "
			cMsgHelp += "Help:GPCALEND"
			cMsgHelp += " -> "
			cMsgHelp += STR0005		//"Nao existe periodo cadastrado"
			AddLogExecRot( cMsgHelp ) 
		EndIf
		nValeRefe	:= 0
	Else 
		nValeRefe   :=	RCF->(RCF_DREFEI)  				//-- Dias Vale Refeicao
	EndIf	
Else 
	nValeRefe   :=	RCF->(RCF_DREFEI)  					//-- Dias Vale Refeicao	
EndIf	
	
RestArea(aAreaSRX, aAreaSRA)

Return (nValeRefe)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³CalRefeica³ Autor ³ Equipe RH             ³ Data ³28/03/95  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Calculo de Vale Refeicao                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ CalRefeicao(aCodfol,aRefeicao)                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function CalRefeicao(aCodFol,aRefeicao)

Local nPos		:= 0
Local nValDesc	:= 0
Local nPosRef	:= 0
Local nQtdDesc	:= 0
Local nQtdVale	:= 0
Local lAdmMes	:=.F.

Local cOrigem
Local cTipo

lDissidio 	:= If( type("lDissidio")=="U",.F.,lDissidio)
//--Situacao do Funcionario na data de referencia
If !lDissidio
	cSitFolh := SRA->RA_SITFOLH
EndIf

//--Competencia de calculo
cFolMes		:= cPeriodo

//--Verifica se funcionario e admitido no mes
lAdmMes		:= If( MesAno(SRA->RA_ADMISSA) == cFolMes, .T., .F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Para funcion rios que recebem semanalmente,deve ser calculado³
//³ somente na £ltima semana do mˆs                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If SRA->RA_TIPOPGT == "S" .And. !lUltSemana 
	Return( Nil )
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se Existe Codigo de Vale Refeicao e Desconta        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aCodfol[050,1] # Space(3) .And. SRA->RA_VALEREF # Space(02)
	If ( cSitFolh == "F" .And. SuperGetMv("MV_CALREFE") == "S" ) .Or.;			//-- Cal.p/func.Ferias 
		( cSitFolh == "A" .And. SuperGetMv("MV_CALREAF") == "S" ) .Or.;      	//-- Cal.p/func.Afastamento  
		( cSitFolh == " " )
		nPosRef := Ascan(aPd, { |x| 	 						 ;
			x[01] = aCodFol[050,1] ; //Compara o Codigo da Verba
		.and. x[09] # "D"			 ; //Verifica se Esta Deletada
		.and. !( x[06] $ "D" )       ; //Tipo da Verba - D(ias),H(oras),V(alor)
		.and. !( x[07] $ "I*G")      ; //Origem do Calculo - I(nformado), G(erado), C(alculado), K(Ferias)...
		}								 ;
		)
		If nPosRef == 0 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica Qual Vale Refeicao e do Funcionario                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( nPos := Ascan( aRefeicao , {|x| x[8]+ x[1] == ( xFilial("SRA",SRA->RA_FILIAL) + SRA->RA_VALEREF ) }) ) <= 0 
				nPos := Ascan( aRefeicao , {|x| x[8]+ x[1] ==  ( Space(FWGETTAMFILIAL) + SRA->RA_VALEREF) })
			EndIf 	
			If nPos > 0 
		  		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Utiliza todas as informacoes do Parametro 26 - VR            ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If aRefeicao[nPos,7]
					nQtdVale := aRefeicao[nPos,3]
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Calc. dias proporc.Dt Admissao ou Calc.Prop.p/ Afastados     ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If ( cSitFolh == "F" .And. SuperGetMv("MV_CALREFE") == "S" .And.;	//-- Prop.Ferias 
						SuperGetMv("MV_CALREPR") == "S" ) .Or.;
						( (cSitFolh == "A"  .or.  (cSitFolh == " " .and. lAdmMes) ) .And. SuperGetMv("MV_CALREAF") == "S" .And.;	//-- Prop.Afastamento
						SuperGetMv("MV_CALREPR") == "S" ) .Or.;
						( cSitFolh == " " .and. SuperGetMv("MV_CALREAD") == "S" .and. lAdmMes )		//-- Prop.Admissao 
						If DiasTrab == 0
							nQtdVale := 0
						EndIf 

						If nQtdVale > 0  
							nQtdVale:=(DiasTrab*nQtdVale)/30
							If nQtdVale > INT(nQtdVale)
								nQtdVale := INT(nQtdVale)+1
							EndIf
						EndIf
					EndIf
		  		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Utiliza Qtde de VR  do Cadastro de Periodos                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				Else
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Calc. dias proporc.Dt Admissao ou Calc.Prop.p/ Afastados     ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If ( cSitFolh == "F"  .And. SuperGetMv("MV_CALREFE") == "S" .And.;		//-- Prop.Ferias 
						SuperGetMv("MV_CALREPR") == "S" ) .Or.;
					   ( ( cSitFolh == "A" .or.  (cSitFolh == " " .and. lAdmMes) ) .And. SuperGetMv("MV_CALREAF") == "S" .And.;		//-- Prop.Afastamento
						SuperGetMv("MV_CALREPR") == "S" ) .OR. ;
					   ( cSitFolh == " " .And. SuperGetMv("MV_CALREAD") == "S" .and. lAdmMes )			//-- Prop.Admissao 
						
						FTrabCalen(dDataBase,,,,,,,,,@nQtdVale,,,,.T.,If( cSitFolh == " " .and. SuperGetMv("MV_CALREAD") == "S",.T.,.F.) ) 
					Else
						FTrabCalen(dDataBase,,,,,,,,,@nQtdVale,,,,.F.,If( cSitFolh == " " .and. SuperGetMv("MV_CALREAD") == "S",.T.,.F.) ) 
					EndIf
				
				EndIf 
				
				nPosRef := Ascan(aPd, { |x|   x[01] = aCodFol[050,1] ; //Compara o Codigo da Verba
				.and. x[09] # "D"			 ; //Verifica se Esta Deletada
				}							 ;
				)
				
				IF !Empty( nPosRef )
					cTipo			:= aPd[nPosRef,6]	//Salva  o Tipo   da Verba
					cOrigem			:= aPd[nPosRef,7]	//Salva  a Origem da Verba
				EndIf
				
				
				If nPosRef == 0 .or. ;
					(nPosRef > 0 .And. cOrigem == "G" .and. aPd[nPosRef,5] == 0) .or. (nPosRef > 0 .And. cOrigem # "G" ) //--Quando codigo existir no apd, origem for G (gerado), e valor > 0, utilizar valor do apd 
					//--Quando For lancado quantidade utiliza a Lancada.
						If nPosRef > 0 .And. aPd[nPosRef,6] == "D"
							nValDesc		:= ((aRefeicao[nPos,2] * aPd[nPosRef,4]) * aRefeicao[nPos,4]) / 100
							nQtdDesc		:= aPd[nPosRef,4]
							aPd[nPosRef,9]	:= "D"							//--Deleta a Verba  em aPd para Regravar
						Else 
							nValDesc := ((aRefeicao[nPos,2] * nQtdVale) * aRefeicao[nPos,4]) / 100
							nQtdDesc := nQtdVale
 						EndIf
    	
					If aRefeicao[nPos,5] > 0 .And. nValDesc > aRefeicao[nPos,5]
						FMatriz(aCodFol[050,1],aRefeicao[nPos,5],nQtdDesc,,,cTipo,cOrigem,,,,.T.)		//-- Gera pelo Teto de Desconto
					Else
						FMatriz(aCodFol[050,1],nValDesc,nQtdDesc,,,cTipo,cOrigem,,,,.T.)				//-- Gera o Calculado
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf
EndIf	

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³CHK_CODFOL³ Autor ³ Jose Ricardo			³ Data ³ 29.03.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ consiste o codigo de identificacao do codigo				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ chk_codfol(Codigo)										  ³±±
±±³			 ³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ codigo = codigos da folha								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ SRV														  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Chk_CodFol()
	Local aSaveArea	:= GetArea()
	Local nRecSrv := 0
	Local nOrdSrv := 0
	Local lRet := .T.
	
	dbSelectArea( "SRV" )
	nRecSrv := RecNo()
	nOrdSrv := IndexOrd()
	dbSetOrder(2)

	If dbSeek(cFilial + M->RV_CODFOL) .AND. M->RV_COD != SRV->RV_COD .AND. M->RV_CODFOL != "   "
		Help(" ", 1, "XREPCODFOL")
		lRet:= .F.
	EndIf
	
	dbSelectArea( "SRV" )	
	DbGoto(nRecSrv)
	dbSetOrder(nOrdSrv)

	RestArea(aSaveArea)
Return lRet

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³GpePonCalendInfo³Autor³Marinaldo de Jesus ³ Data ³07/02/2002³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Retorna Array com Informacoes do Ponto de Acordo com o Calen³
³          ³dario de Marcacoes e com a Regra de Apontamento				³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Comentario nos Parametros Formais >					³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³SIGAPON                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³--> aInfoCalend (Array contendo Informacoes do Calendario)	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function GpePonCalendInfo(	cFil		,; //[,	<@>cFil			] -> Opicional, Filial do Funcionario Para Montagem do Calendario
							cMat		,; //[,	<@>cMat			] -> Opicional, Matricula do Funcionario Para Montagem do Calendario
							cTno		,; //[,	<@>cTno			] -> Opicional, Turno do Funcionario Para Montagem do Calendario
							cSeq		,; //[,	<@>cSeq			] -> Opicional, Sequencia do Funcionario Para Montagem do Calendario
							cCc			,; //[,	<@>cCc			] -> Opicional, Centro de Custo do Funcionario Para Montagem do Calendario
							dDataIni	,; //[	<@>dDataIni		] -> Opicional, Data Inicial Para Montagem do Calendario
							dDataFim	,; //[,	<@>dDataFim		] -> Opicional, Data Final Para Montagem do Calendario
							aTabCalend	,; //[,	<@>aTabCalend	] -> Opicional, Array que contera o Calendario de Marcacoes do Funcionario
							aTabPadrao	,; //[,	<@>aTabPadrao	] -> Opicional, Array com Informacoes das Tabalas de Horario Padrao (SPJ)
							aTurnos		 ; //[,	<@>aTurnos		] -> Opicional, Array com Informacoes das Trocas de Turno do Funcionario
						  )
                       
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Inclui "PONCALEND.CH" para retorno dos Elementos do Calendario³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
#INCLUDE "PONCALEN.CH"

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Define o Numero de Elementos de aInfoCalend				   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
#DEFINE ELEMENTOS_AINFOCALEND	12
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Define a Estrutura do Array aInfoCalend                       ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
#DEFINE DIAS_TRABALHADOS		01
#DEFINE HORAS_TRABALHADAS		02
#DEFINE DIAS_DE_DSR				03
#DEFINE HORAS_DE_DSR			04
#DEFINE DIAS_COMPENSADOS		05
#DEFINE HORAS_COMPENSADAS		06
#DEFINE DIAS_NAO_TRABALHADOS	07
#DEFINE HORAS_NAO_TRABALHADAS	08
#DEFINE DIAS_AFAST_FERIAS		09
#DEFINE HORAS_AFAST_FERIAS		10
#DEFINE DIAS_AFAST_NAO_FER		11
#DEFINE HORAS_AFAST_NAO_FER		12

Local aInfoCalend	:= Array( ELEMENTOS_AINFOCALEND )
Local bAscanOrdem	:= { |x| x[ CALEND_POS_ORDEM ] == StrZero( nStartDate , 02 ) .AND. x[ CALEND_POS_TIPO_MARC ] == "1E" }
Local cTipoDia		:= ""
Local cTipAfas		:= ""
Local cPagInt		:= ""
Local dStartDate	:= Ctod("//")
Local lFeriado		:= .F.
Local lAfastado		:= .F.
Local lTrbFeriado	:= .F.
Local nStartDate	:= 0.00
Local nFinishDate	:= 0.00
Local nHorasTrab	:= 0.00
Local nHorasInte	:= 0.00
Local nOneDSR		:= HrsSem2OneDsr( SRA->RA_HRSEMAN )
Local nMarc			:= 0.00
Local nPosCalend	:= 0.00

Static aSiglaMarc
Static nSizeSiglaMarc

Default cFil			:= SRA->RA_FILIAL
Default cMat			:= SRA->RA_MAT
Default cTno			:= SRA->RA_TNOTRAB
Default cSeq			:= SRA->RA_SEQTURN
Default cCc				:= SRA->RA_CC
Default dDataIni 		:= Ctod("01/"+StrZero(Month(dDataBase),02)+"/"+StrZero(Year(dDataBase),04), "DD/MM/YY")
Default dDataFim 		:= Ctod(StrZero(f_UltDia(dDataIni),02)+"/"+StrZero(Month(dDataBase),02)+"/"+StrZero(Year(dDataBase),04), "DD/MM/YY")
Default aTabPadrao		:= {}
Default	nSizeSiglaMarc	:= TabMarc( "SPJ" , @aSiglaMarc )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Inicializa Todos os Elementos de aInfoCalend com Zeros        ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aFill( aInfoCalend , 0.00  )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Define o Periodo Para Carga das Informacoes em aInfoCalend    ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
dStartDate	:= dDataIni
nFinishDate	:= ( ( dDataFim - dDataIni ) + 1 )

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Inicializa as Variaveis para o Calendario                     ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aTabCalend	:= {}
aTurnos		:= {}

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Cria o Calendario de Marcacoes de Acordo com o Periodo Passado³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF CriaCalend(dDataIni,dDataFim,cTno,cSeq,@aTabPadrao,@aTabCalend,cFil,cMat,cCc,@aTurnos)
	For nStartDate := 1.00 To nFinishDate
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³A Pesquisa da Data em aTabCalend devera ser Feita Baseada   na³
		³Ordem                  									   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IF ( nPosCalend := aScan( aTabCalend , bAscanOrdem ) ) == 0.00
			dStartDate++
		Else
			dStartDate := aTabCalend[ nPosCalend , CALEND_POS_DATA ]
		EndIf
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Retorna Conteudo de aTabCalend de Acordo com as Chaves Indices³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		cTipoDia	:= GetInfoPosTab( CALEND_POS_TIPO_DIA		, "1E" , dStartDate , aTabCalend )
		lFeriado	:= GetInfoPosTab( CALEND_POS_FERIADO		, "1E" , dStartDate , aTabCalend )
		lAfastado	:= GetInfoPosTab( CALEND_POS_AFAST			, "1E" , dStartDate , aTabCalend )
		cTipAfas	:= GetInfoPosTab( CALEND_POS_TIP_AFAST		, "1E" , dStartDate , aTabCalend )
		lTrbFeriado	:= GetInfoPosTab( CALEND_POS_TRAB_FERIADO	, "1E" , dStartDate , aTabCalend )
		cPagInt		:= GetInfoPosTab( CALEND_POS_PAGINT			, "1E" , dStartDate , aTabCalend )
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Apura o Numero de Horas Trabalhadas na Tabela.                ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		nHorasTrab := nHorasInte := 0.00
		For nMarc := 1 To nSizeSiglaMarc 
			nHorasTrab := __TimeSum( nHorasTrab , GetInfoPosTab(CALEND_POS_HRS_TRABA,aSiglaMarc[nMarc],dStartDate,aTabCalend) )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Se a Regra Define o Pagamento do Intervalo, considera as Horas³
			³de Intervalo como Horas Trabalhadas						   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF ( ( "I" + SubStr( aSiglaMarc[ nMarc ] , 1 , 1 ) ) $ cPagInt )
				nHorasInte := __TimeSum( nHorasInte , GetInfoPosTab(CALEND_POS_HRS_TRABA,aSiglaMarc[nMarc],dStartDate,aTabCalend) )
			EndIf	
		Next nMarc
		IF !( lAfastado )
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Carrega aInfoCalend com as Informacoes de Dias e Horas    Para³
			³Funcionarios sem Afastamento								   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF ( ( ( cTipoDia == "S" ) .AND. ( !( lFeriado ) ) .OR. ( ( lFeriado ) .AND. ( lTrbFeriado ) ) ) )
				aInfoCalend[ DIAS_TRABALHADOS	]++
				aInfoCalend[ HORAS_TRABALHADAS	]:=	__TimeSum( aInfoCalend[ HORAS_TRABALHADAS ] , __TimeSum( nHorasTrab , nHorasInte ) )
			ElseIf ( cTipoDia == "D" .OR. lFeriado )
				aInfoCalend[ DIAS_DE_DSR ]++
				aInfoCalend[ HORAS_DE_DSR ] := __TimeSum( aInfoCalend[ HORAS_DE_DSR ] , nOneDSR )
			ElseIf ( cTipoDia == "C" )
				aInfoCalend[ DIAS_COMPENSADOS	]++
				aInfoCalend[ HORAS_COMPENSADAS	]:=	__TimeSum( aInfoCalend[ HORAS_COMPENSADAS ] , __TimeSum( nHorasTrab , nHorasInte ) )
			ElseIf ( cTipoDia == "N" )
				aInfoCalend[ DIAS_NAO_TRABALHADOS ]++
				aInfoCalend[ HORAS_NAO_TRABALHADAS ]:= __TimeSum( aInfoCalend[ HORAS_NAO_TRABALHADAS ] , __TimeSum( nHorasTrab , nHorasInte ) )
			EndIf
		Else
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³Carrega aInfoCalend com as Informacoes de Dias e Horas    Para³
			³Funcionarios com Afastamento								   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF ( cTipAfas == "F" )
				aInfoCalend[ DIAS_AFAST_FERIAS ]++
				aInfoCalend[ HORAS_AFAST_FERIAS ]:= __TimeSum( aInfoCalend[ HORAS_AFAST_FERIAS ] , __TimeSum( nHorasTrab , nHorasInte ) )
			Else
				aInfoCalend[ DIAS_AFAST_NAO_FER ]++
				aInfoCalend[ HORAS_AFAST_NAO_FER ]:= __TimeSum( aInfoCalend[ HORAS_AFAST_NAO_FER ] , __TimeSum( nHorasTrab , nHorasInte ) )
			EndIf
		EndIf	
	Next nStartDate

EndIf

Return( aClone( aInfoCalend ) )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³CHKNIF    ºAutor  ³Marcelo Silveira    º Data ³  28/11/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Validador do numero NIF - Angola                           º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA280                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function ChkNIF(cNIF)

Local dgVerif
Local nPos		:= 0
Local nX   		:= 0
Local nNumDig	:= 0
Local nNumValid	:= 0
Local cNIFAux	:= ""
Local lRet		:= .T.

DEFAULT cNIF	:= Space(13)

If !Empty( cNIF )

	cNIF 	:= AllTrim( cNIF )
	nNumDig	:= Len( cNIF)

	//Digitos identificadores valido para o NIF em Angola: 1,2,3,5 ou 7
	If !( Substr(cNIF,1,1) $ "1|2|3|5|7" )
		MsgAlert( OemToAnsi(STR0001) + ' "' + Substr(cNIF,1,1) + '"  ' + OemToAnsi(STR0002) ) //"O Dígito identificador "#"informado no codigo NIF não é válido"
		Return(.F.)
	EndIf

	//Conta somente os digitos do codigo digitado, pois pode ser utilizada letras como parte do codigo.
	For nX := 1 To nNumDig
		If IsDigit( Substr(cNIF,nX,1) )
			cNIFAux += Substr(cNIF,nX,1)
		EndIf
	Next nX

	//Numero total de digitos	
	nNumDig := Len( cNIFAux )
	
	//A soma do produto de cada um dos digitos numericos multiplicados pelo seu posicionamento em ordem inversa
	//Ex. codigo '1234567890' = digitos: 1*10, 2*9, 3*8...
	For nX := 1 To nNumDig
	    
		nNumValid += Val( Substr(cNIFAux,nX,1) ) * (nNumDig - nPos)

		If nX == nNumDig 
			dgVerif := Val( Substr(cNIFAux,nX,1) )
		EndIf

		nPos ++
			
	Next nX

	//Com o numero obtido na soma eh tirado o modulo dele dividido por 11
	nNumValid := nNumValid % 11

	//Se o resto for igual a zero ou 10 o digito verificador deve ser zero
	If nNumValid == 0 .Or. nNumValid == 10
		nNumValid := 0
	EndIf
	
	//Eh permitido o cadastrar codigo considerado invalido devido a possibilidade de cadastro de estrangeiros com numero do passaporte
	If !dgVerif == nNumValid
		lRet := MsgYesNo( OemToAnsi(STR0003), OemToAnsi(STR0004) ) //"O código NIF informado não é válido. Este código será aceito?"#"Atenção"
	EndIf		
	
EndIf
	
Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³CHKCURP   ºAutor  ³Silvia Taguti	     º Data ³  20/12/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Valida CURP - Mexico                                       º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function ChkCurp(cCurp)

Local cCur01 := cCur02 := cCur03 := cCur04 := cCur05 := ""
Local cCur06 := cCur07 := cCur08 := cCur09 := ""
Local xCur01 := xCur02 := xCur03 := xCur04 := xCur05 := ""
Local xCur06 := xCur07 := xCur08 := xCur09 := ""

Local cVogais:= "AEIOU"
Local nX := 0      
Local aPalAlt:= {}
Local nPos := 0
Local cCalcDig:= ""
Local nResult := 0
Local n := 0
Local	cPriSobr := ""
Local	cSecSobr := ""
Local	cPriNome := ""
Local	cSecNome := ""
Local	dDtNasc  := Ctod("//")
Local	cSexo    := ""
Local lCompl   := .F.
Local cCurpT   := ""             
Local cNomVal  := ""
Local cSobreNome
Private cCaractCor := ""

AADD(aPalAlt,{	"BUEI", "BUEY", "CACA", "CACO", "CAGA", "CAGO", "CAKA", "CAKO", "COGE", "COJA",;
				"COJE", "COJI", "COJO", "CULO", "FETO", "GUEY", "JOTO", "KACA", "KACO", "KAGA",;
				"KAGO", "KOGE", "KOJO", "KAKA", "KULO", "MAME", "MAMO", "MEAR", "MEAS", "MEON",;
				"MION", "MOCO", "MULA", "PEDA", "PEDO", "PENE", "PUTA", "PUTO", "QULO", "RATA",;
				"RUIN"} )

cCur01 := Substr(cCurp,1,2)    	//Primeiro Sobr.-Primeira letra e Primeira vogal interna
cCur02 := Substr(cCurp,3,1)     //Segundo Sobr.-Primeira letra 
cCur03 := Substr(cCurp,4,1)     //Primeiro Nome - Primeira letra
cCur04 := Substr(cCurp,5,6)     //data de nascimento aammdd
cCur05 := Substr(cCurp,11,1)    //Sexo (HM)
cCur06 := Substr(cCurp,12,2)    //Lugar de nascimento
cCur07 := Substr(cCurp,14,1)    //Primeira consoante interna do 1 Sobrenome
cCur08 := Substr(cCurp,15,1)		//Primeira consoante interna do 2 Sobrenome
cCur09 := Substr(cCurp,16,1)    //Primeira consoante interna do 1 Nome
cCur10 := Substr(cCurp,17,1)    //Homoclave
cCur11 := Substr(cCurp,18,1)    //Digito verificador

cPriSobr := M->RA_PRISOBR
cSecSobr := M->RA_SECSOBR
cPriNome := M->RA_PRINOME
cSecNome := M->RA_SECNOME
dDtNasc  := M->RA_NASC
cSexo    := M->RA_SEXO  

cPriSobr := If(!Empty(cPriSobr),CurpTroca(cPriSobr),cPriSobr)
cSecSobr := If(!Empty(cSecSobr),CurpTroca(cSecSobr),cSecSobr)
cPriNome := If(!Empty(cPriNome),CurpTroca(cPriNome),cPriNome)
cSecNome := If(!Empty(cSecNome),CurpTroca(cSecNome),cSecNome)

If Len(Alltrim(cCurp))== 10
	lCompl := .T.
EndIf	

If !Empty(cPriSobr) 
	xCur01 := Left(Alltrim(cPriSobr),1)
	For nX := 2 To Len(Alltrim(cPriSobr))
		If Substr(cPriSobr,nX,1) $ cVogais
			xCur01 += Substr(cPriSobr,nX,1)
			Exit
		EndIf	
	Next
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Quando o Sobrenome materno eh iniciado por: da, das, la, las etc, essa palabra eh descartada  |
//|e utilizado o Sobrenome posterior																		   	 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
n := 1        
y := 0
If !Empty(cSecSobr)
	cSobreNome := cSecSobr
	For nx := 1 to Len(Alltrim(cSecSobr))
		y := y+1 
		cSobreNome := Substr(cSecSobr,n,y)
		If cSobreNome = "DA " .OR. cSobreNome =  "DAS " .OR. cSobreNome = "DE " .OR.;
			cSobreNome = "DEL " .OR. cSobreNome = "DER " .OR. cSobreNome = "DI " .OR.;
			cSobreNome = "DIE " .OR. cSobreNome = "DD " .OR. cSobreNome = "EL " .OR.;
			cSobreNome = "LA " .OR. cSobreNome = "LOS " .OR. cSobreNome = "LAS " .OR.;
	      cSobreNome = "LE " .OR. cSobreNome = "LES " .OR. cSobreNome = "MAC " .OR.;
   	   cSobreNome = "MC " .OR. cSobreNome = "VAN " .OR. cSobreNome = "VON " .OR.;
      	cSobreNome = "Y " .OR. cSobreNome = "MA " 
      	n := nx+1
      	y := 0
      EndIf
   Next nx
   xCur02 := Left(Alltrim(cSoBreNome),1)
Else 
	xCur02 := "X"
EndIf

If !Empty(cPriNome)
	If Alltrim(cPriNome)== "MARIA" .OR. Alltrim(cPriNome)== "JOSE" .OR. Alltrim(cPriNome)== "JOSÉ" .OR.;
		Alltrim(cPriNome)== "JOSÈ"  .OR. Alltrim(cPriNome)== "MARÍA" .OR.Alltrim(cPriNome)== "MARÌA" 
		xCur03 := Left(Alltrim(cSecNome),1)		
		If Empty(cSecNome)
			xCur03 := Left(Alltrim(cPriNome),1)		
		EndIf
	Else
		xCur03 := Left(Alltrim(cPriNome),1)		
	EndIf
EndIf	

nPos := ASCAN(aPalAlt,{ |X| X[1] = Upper(xCur01+xCur02+xCur03) })

If nPos > 0
	xCur01 := "X"+Substr(xCur01,2,1)
EndIf

If !Empty(dDtNasc)
	xCur04 := Substr(Dtos(dDtNasc),3,6)
EndIf	

If !Empty(cSexo )
	xCur05 := If(cSexo == "M", "H", "M")
EndIf	
If lCompl
	cCur05 := xCur05
EndIf	

xCur06 := EstCurp(M->RA_NATURAL)
If lCompl
	cCur06 := xCur06
EndIf	

If !Empty(cPriSobr)
	For nX := 2 To Len(Alltrim(cPriSobr))
		If !(Substr(cPriSobr,nX,1) $ cVogais)
			xCur07 := Substr(cPriSobr,nX,1)
			Exit
		EndIf	
	Next
	xCur07:= If(Empty(xCur07), "X",xCur07)     
Else
	xCur07 := "X"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Quando o Sobrenome materno eh iniciado por: da, das, la, las etc, essa palabra eh descartada  |
//|e utilizado o Sobrenome posterior																		   	 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
n := 1
y := 0
If !Empty(cSecSobr)
	cSobreNome := cSecSobr
	For nx := 1 to Len(Alltrim(cSecSobr))
		y := y+1 
		cSobreNome := Substr(cSecSobr,n,y)
		If cSobreNome = "DA " .OR. cSobreNome =  "DAS " .OR. cSobreNome = "DE " .OR.;
			cSobreNome = "DEL " .OR. cSobreNome = "DER " .OR. cSobreNome = "DI " .OR.;
			cSobreNome = "DIE " .OR. cSobreNome = "DD " .OR. cSobreNome = "EL " .OR.;
			cSobreNome = "LA " .OR. cSobreNome = "LOS " .OR. cSobreNome = "LAS " .OR.;
	      cSobreNome = "LE " .OR. cSobreNome = "LES " .OR. cSobreNome = "MAC " .OR.;
   	   cSobreNome = "MC " .OR. cSobreNome = "VAN " .OR. cSobreNome = "VON " .OR.;
      	cSobreNome = "Y " .OR. cSobreNome = "MA " 
      	n := nx+1
      	y := 0
      EndIf
   Next nx

	For nX := 2 To Len(Alltrim(cSobreNome))
		If !(Substr(cSobreNome,nX,1) $ cVogais)
			xCur08 := Substr(cSobreNome,nX,1)
			Exit
		EndIf	
	Next
	xCur08:= If(Empty(xCur08), "X",xCur08)     
Else
	xCur08 := "X"
EndIf

If !Empty(cPriNome)
	If Alltrim(cPriNome)== "MARIA" .OR. Alltrim(cPriNome)== "JOSE" .OR. Alltrim(cPriNome)== "JOSÉ" .OR.;
		Alltrim(cPriNome)== "JOSÈ"  .OR. Alltrim(cPriNome)== "MARÍA" .OR.Alltrim(cPriNome)== "MARÌA" 
      If Empty(cSecNome)
      	cNomVal := cPriNome
      Else
         cNomVal := cSecNome
      EndIf   
  	Else
  		cNomVal := cPriNome    
  	EndIf	
	For nX := 2 To Len(Alltrim(cNomVal))
		If !(Substr(cNomVal,nX,1) $ cVogais)
			xCur09 := Substr(cNomVal,nX,1)
			Exit
		EndIf	
	Next  
	xCur09:= If(Empty(xCur09), "X",xCur09)   
Else	
	xCur09 := "X"
EndIf

If lCompl
	cCur07 := xCur07
	cCur08 := xCur08
	cCur09 := xCur09
EndIf	

xCur10 := "0"
If lCompl
	cCur10 := xCur10
EndIf	

cCalcDig := Upper(xCur01+xCur02+xCur03+xCur04+xCur05+xCur06+xCur07+xCur08+xCur09+xCur10)

n := 18
For nX := 1 to Len(Trim(cCalcDig))
	If ASC(Substr(cCalcDig,nX,1)) <= 57
		nValor := Val(Substr(cCalcDig,nX,1))
	ElseIf ASC(Substr(cCalcDig,nX,1)) <=78
		nValor :=ASC(Substr(cCalcDig,nX,1)) -65+10
	Else	
		nValor := ASC(Substr(cCalcDig,nX,1))-64+10
	EndIf	
	nResult += (nValor * n)
	n -= 1
Next		

If Mod(nResult,10)== 0
	xCur11 := "0"
Else	
	xCur11 := Str(10-Mod(nResult,10),1)
Endi	


If lCompl
	cCur11 := xCur11
EndIf	

If (cCur01+cCur02+cCur03+cCur04+cCur05+cCur06+cCur07+cCur08+cCur09+cCur10+cCur11 <> xCur01+xCur02+xCur03+;
	xCur04+xCur05+xCur06+xCur07+xCur08+xCur09+xCur10+xCur11 ) .OR. Empty(cCur05) .OR. Empty(cCur06) .OR. Empty(cCur07);
	.OR. Empty(cCur08) .OR. Empty(cCur09)
	If !(MsgYesNo(OemToAnsi(STR0005), OemToAnsi(STR0004)))
		Return (.F.)
	EndIf	
EndIf
  
cCurpT := (cCur01+cCur02+cCur03+cCur04+cCur05+cCur06+cCur07+cCur08+cCur09+cCur10+cCur11)                                            

If lCompl
	M->RA_CURP := cCurpT
EndIf
	
Return (.T.)     

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³CHKRFC    ºAutor  ³Silvia Taguti       º Data ³  27/12/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Valida RFC - Mexico                                        º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function ChkRfc(cRfc, cPrimNome, cSeguNome, cPrimSobr, cSeguSobr, dDataNasc, cSexo, lPortal)

Local cRfc01 	:= cRfc02 := cRfc03 := cRfc04 := cRfc05 := ""
Local xRfc01 	:= xRfc02 := xRfc03 := xRfc04 := xRfc05 := ""
Local cPriSobr	:= cSecSobr := cPriNome := cSecNome := cNome := ""
Local cBkpPSobr	:= cBkpSSobr := cBkpPNome := ""
Local cAux1		:= cAux2	:= cAux3	:=""

Local cVogais	:= "AEIOU"
Local aPalAlt	:= {}
Local nX 		:= 0
Local nPos 		:= 0
Local aNome		:=Array(100)
Local cAscNome	:= ""
Local nAcuHom	:= 0
Local nAux1		:= 0
Local nAux2		:= 0
Local nTotAsc	:= 0
Local cValreg	:= ""
Local cAcuHom	:= ""
Local nValHom1	:= 0
Local nValHom2	:= 0
Local nValHom3	:= 0              
Local nValAscI	:= 0
Local lCompl	:= .F.
Local cSobreNome      
Local nRqPriNome := 0
Local nRqSecNome := 0
Local nRqPriSobr := 0
Local nRqSecSobr := 0
Local nRqNasc	:=  0
Local lSRA		:= IsInCallStack("GPEA010") .or. IsInCallStack("GPEA265")
Local lCurric	:= IsInCallStack("RSPA010")

Private cCaractCor := ""

Default cRfc  		:= Space(13)
Default cPrimNome  	:= ""
Default cSeguNome  	:= ""
Default cPrimSobr  	:= ""
Default cSeguSobr  	:= ""
Default dDataNasc  	:= Date()
Default cSexo  		:= ""
Default lPortal     := .F.

aPalAlt	:=	{"BUEI","BUEY","CACA","CACO","CAGA","CAGO","CAKA","CAKO","COGE","COJA","COJE","COJI","COJO","CULO","FETO","GUEY","JOTO","KACA","KACO","KAGA",;
		"KAGO","KOGE","KOJO","KAKA","KULO","MAME","MAMO","MEAR","MEAS","MEON","MION","MOCO","MULA","PEDA","PEDO","PENE","PUTA","PUTO","QULO","RATA",;
		"RUIN"}

cRfc01 := Substr(cRfc,1,2)		//Primeira Letra + Prim. vogal do Sobr. Paterno
cRfc02 := Substr(cRfc,3,1)		//Primeira Letra do Sobr. Materno
cRfc03 := Substr(cRfc,4,1)		//Primeira Letra do Nome	
cRfc04 := Substr(cRfc,5,6)		//Data de Nascimento aammdd
cRfc05 := Substr(cRfc,11,3)		//Homoclave

lGp010Auto  := IIf(Type("lGp010Auto") =="U", .F., lGp010Auto)

//# Verifica se o Nr.de RFC serah validado ou nao
If !lSRA .And. !lPortal
	If IsInCallStack("RSPA010")	
		MsgAlert(OemToAnsi(STR0006), OemToAnsi(STR0004))	//#"Não foi possível validar o número de RFC !" ## "Atençäo"
		Return(.T.)
	Else
		If SRQ->(Type("RQ_PRINOME")) == "U" .OR.;
			SRQ->(Type("RQ_PRISOBR")) == "U" .OR.;
			SRQ->(Type("RQ_SECSOBR")) == "U" .OR.;     
			SRQ->(Type("RQ_NASC")) == "U"		
			MsgAlert(OemToAnsi(STR0006), OemToAnsi(STR0004))	//#"Não foi possível validar o número de RFC !" ## "Atençäo"
			Return(.T.)
		EndIf
	EndIf	
EndIf
If lSRA .OR. "GPEA010MNT" $ AllTRim(Upper(procname(3)))
	cPriNome := M->RA_PRINOME
	cSecNome := M->RA_SECNOME
	cPriSobr := M->RA_PRISOBR
	cSecSobr := M->RA_SECSOBR
	dDtNasc  := M->RA_NASC
	cSexo    := M->RA_SEXO
ElseIf lCurric .And. cPaisLoc == "MEX"
	cPriNome := M->QG_PRINOME
	cSecNome := M->QG_SECNOME
	cPriSobr := M->QG_PRISOBR
	cSecSobr := M->QG_SECSOBR
	dDtNasc  := M->QG_DTNASC 
	cSexo    := M->QG_SEXO
ElseIf lPortal .And. cPaisLoc == "MEX"
	cPriNome := Upper(cPrimNome)
	cSecNome := Upper(cSeguNome)
	cPriSobr := Upper(cPrimSobr)
	cSecSobr := Upper(cSeguSobr)
	dDtNasc  := dDataNasc
	cSexo    := cSexo
ElseIf IsInCallStack("PONA330")
	cPriNome := M->PW_POSNOM1
	cSecNome := M->PW_POSNOM2
	cPriSobr := M->PW_POSNOM3
	cSecSobr := M->PW_POSNOM4
	dDtNasc  := M->PW_NASC
	cSexo    := M->PW_SEXO
Else
	//Resgata posicao dos campos
	nRqPriNome 	:= GdFieldPos("RQ_PRINOME",aHeader)
	nRqSecNome 	:= GdFieldPos("RQ_SECNOME",aHeader)
	nRqPriSobr 	:= GdFieldPos("RQ_PRISOBR",aHeader)
	nRqSecSobr 	:= GdFieldPos("RQ_SECSOBR",aHeader)
	nRqNasc		:=  GdFieldPos("RQ_NASC",aHeader)

	//# Atualiza variaveis com SRQ, conforme existirem	            
	If nRqPriNome > 0
		cPriNome := aCols[n,nRqPriNome]
	Else 
		cPriNome := ""
	EndIf 
	If nRqSecNome > 0 
		cSecNome := aCols[n,nRqSecNome]
	Else
		cSecNome := ""	
	EndIf             
	If nRqPriSobr > 0
		cPriSobr := aCols[n,nRqPriSobr]
	Else
		cPriSobr := ""	
	EndIf             
	If nRqSecSobr > 0
		cSecSobr := aCols[n,nRqSecSobr]	
	Else 
		cSecSobr := ""
	EndIf        
	If nRqNasc > 0 
		dDtNasc  := aCols[n,nRqNasc]
	Else
		dDtNasc := cToD("  /  /  ")
	EndIf
	cBkpPNome:= cPriNome
	cBkpPSobr:= cPriSobr
	cBkpSSobr:= cSecSobr
EndIf

cPriNome := If(!Empty(cPriNome),NomeComp(CurpTroca(cPriNome)),cPriNome)
cSecNome := If(!Empty(cSecNome),NomeComp(CurpTroca(cSecNome)),cSecNome)
cPriSobr := If(!Empty(cPriSobr),NomeComp(CurpTroca(cPriSobr)),cPriSobr)
cSecSobr := If(!Empty(cSecSobr),NomeComp(CurpTroca(cSecSobr)),cSecSobr)

If Len(Alltrim(cRfc))== 10
	lCompl := .T.
EndIf	

If !Empty(cPriSobr)
	xRfc01 := Left(Alltrim(cPriSobr),1) 
	If !Empty(cSecSobr)
		For nX := 2 To Len(Alltrim(cPriSobr))
		If Substr(cPriSobr,nX,1) $ cVogais
				xRfc01 += Substr(cPriSobr,nX,1)
				Exit
			Endif	
		Next
	Else
		xRfc01 += Substr(cPriSobr,2,1)
	EndIf
	cPriSobr := LimpText(Alltrim(cPriSobr))
EndIf

If !Empty(cSecSobr)
	If !(Empty(cPriSobr))
		cSobreNome := ExtraiNome(cSecSobr)
		xRfc02 := Left(Alltrim(cSobreNome),1)
	Else
		cSobreNome := ExtraiNome(cSecSobr)
		xRfc02 := Left(Alltrim(cSobreNome),2)
	EndIf
Endif

cSecSobr:= LimpText(Alltrim(cSecSobr))

If !Empty(cPriNome)
	If Alltrim(cPriNome)== "MARIA" .OR. Alltrim(cPriNome)== "JOSE"
		If Empty(cSecSobr) .OR. Empty(cPriSobr) 
			xRfc03 := Left(Alltrim(ExtraiNome(cSecNome)),2)	
		Else	
			xRfc03 := Left(Alltrim(ExtraiNome(cSecNome)),1)
		EndIf	
	ElseIf Empty(cSecSobr) .OR. Empty(cPriSobr) 
		xRfc03 := Left(Alltrim(ExtraiNome(cPriNome)),2)					
	Else
		xRfc03 := Left(Alltrim(cPriNome),1)		
	EndIf	
	cPriNome := LimpText(Alltrim(cPriNome))  
EndIf	

nPos := ASCAN(aPalAlt,{ |x| x = Upper(xRfc01+xRfc02+xRfc03) })

If nPos > 0
	xRfc03 := "X"+Substr(xRfc03,2,1)
EndIf

If !Empty(dDtNasc)
	xRfc04 := Substr(Dtos(dDtNasc),3,6)    
EndIf	

cValreg := cRfc01+cRfc02+cRfc03+cRfc04

If lSRA .OR. "GPEA010MNT" $ AllTRim(Upper(Procname(3)))
	cNomeAtu := LimpText(Alltrim(M->RA_PRISOBR))+" "+LimpText(Alltrim(M->RA_SECSOBR))+" "+;
				LimpText(Alltrim(M->RA_PRINOME))+" "+LimpText(Alltrim(M->RA_SECNOME))

ElseIf lCurric .And. cPaisLoc == "MEX"
	cNomeAtu := LimpText(Alltrim(M->QG_PRISOBR))+" "+LimpText(Alltrim(M->QG_SECSOBR))+" "+;
				LimpText(Alltrim(M->QG_PRINOME))+" "+LimpText(Alltrim(M->QG_SECNOME))
ElseIf lPortal .And. cPaisLoc == "MEX"
	cNomeAtu := LimpText(Alltrim(cPriSobr))+" "+LimpText(Alltrim(cSecSobr))+" "+;
				LimpText(Alltrim(cPriNome))+" "+LimpText(Alltrim(cSecNome))	
Else
	//# Atualiza variaveis com SRQ
	cNomeAtu := LimpText(Alltrim(cBkpPSobr))+" "+LimpText(Alltrim(cBkpSSobr))+" "+;
				LimpText(Alltrim(cBkpPNome))
EndIf

cNome := ""
For nx:=1 to Len(cNomeAtu)
	If Substr(cNomeAtu,nx,1) == "#"
		cNome += "Ñ"
	Else
		cNome += Substr(cNomeAtu,nx,1)
	EndIf
Next nx		

j:= 1
aNome[1]:= " "

For nX := 1 To Len(cNome)
	j := j + 1
 	cAscNome := StrZero(ValAsc(Substr(cNome, nX, 1)), 2)
  	aNome[j] := Substr(cAscNome, 1, 1)
   j += 1
   aNome[j] := Substr(cAscNome, 2, 1)
Next 

nTotAsc = 0

For nX:= 1 To j -1 
	cAux1 := aNome[nX]
	cAux2 := aNome[nX + 1]
	cAux3 := cAux1 + cAux2
	nAux1 := Val(cAux3)
	nAux2 := Val(cAux2)
	nTotAsc += (nAux1 * nAux2)
Next 

cAcuHom := Substr(StrZero(nTotAsc,4), 2, 3)
nAcuHom := Val(cAcuHom)

nValHom1 := Int(nAcuHom / 34)
nValHom2 := Mod(nAcuHom,34)

nAcuHom := 0
If nValHom1 >= 9 .AND. nValHom1 <= 22 
	nAcuHom := nValHom1 + 56
ElseIf nValHom1 > 22                             	
	nAcuHom := nValHom1 + 57
ElseIf nValHom1 >= 0 .AND. nValHom1 <= 8 
	nAcuHom := nValHom1 + 1
EndIf

If nAcuHom >= 0 .AND. nAcuHom <= 9 
	cValReg := cValReg + Alltrim(Str(nAcuHom))
Else
	cValReg := cValReg + Chr(nAcuHom)
End If

nAcuHom := 0
If nValHom2 >= 9 .AND. nValHom2 <= 22 
	nAcuHom := nValHom2 + 56
ElseIf nValHom2 > 22 
	nAcuHom := nValHom2 + 57
ElseIf nValHom2 >= 0 .AND. nValHom2 <= 8 
	nAcuHom := nValHom2 + 1
EndIf

If nAcuHom >= 0 .AND. nAcuHom <= 9 
	cValReg := cValReg + Alltrim(Str(nAcuHom))
Else
	cValReg := cValReg + Chr(nAcuHom)
End If

j := 13
nAcuHom := 0
For nX := 1 To 12
	nValAscI := ValAscI(Substr(Trim(cValReg), nX, 1))
	nAcuHom := nAcuHom + (nValAscI * j)
	j := j - 1
Next nx

nValHom3 := Mod(nAcuHom, 11)
If nValHom3 = 0 
	cValReg = cValReg + "0"
Else
	nValHom3 := 11 - nValHom3
	If nValHom3 == 10 
		cValReg := cValReg + "A"
	Else
		If nValHom3 == 0 
			cValReg := cValReg + "0"
		Else
			cValReg := cValReg + Alltrim(Str(nValHom3))
		End If
	End If
End If

xRfc05:= Alltrim(Substr(cValReg,11,3))
If lCompl
	cRfc05 := xRfc05
EndIf

If !lGp010Auto .And. (cRfc01+cRfc02+cRfc03+cRfc04+cRfc05 <> xRfc01+xRfc02+xRfc03+xRfc04+xRfc05) .OR. Len(cRfc01+cRfc02+cRfc03+cRfc04+cRfc05) < 13
	If !(MsgYesNo(OemToAnsi(STR0007), OemToAnsi(STR0004)))
		Return(.F.)
	Else
		//# Atualiza variaveis com SRQ
		If Type("oGet") != "U" .And. !lSRA .And. !lCurric .And. AllTRim(Upper(procname(3))) <> "GPEA010MNT" .AND. AllTRim(Upper(procname(3))) <> "PN330ROT" .AND. AllTRim(Upper(procname(3))) <> "ENCHOTUDOK"
			aCols[oGet:oBrowse:nAt,GdFieldPos("RQ_CIC",aHeader)] := cRfc01+cRfc02+cRfc03+cRfc04+cRfc05
		EndIf
	EndIf
EndIf 

If lCompl
	If lSRA .OR. "GPEA010MNT" $ AllTRim(Upper(procname(3))) 
		M->RA_CIC := cRfc01+cRfc02+cRfc03+cRfc04+cRfc05
	EndIf
EndIf

Return(.T.)


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³LimpText  ³ Autor ³Silvia Taguti          ³ Data ³ 30/12/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Retira caracteres estranhos de um palavra.                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³LimpText()											      ³±±
±±³Parametros³ExpC1 - Texto para limpeza.							      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function LimpText(cTexto)
Local cTmpCar	:= ""
Local cCarac	:= "-.;/\*,:$%&"
Local nItem		:= 1

For nItem := 1 to Len(cCarac)
	cTmpCar	:= Substr(cCarac,nItem,1)
	cTexto	:= StrTran(cTexto,cTmpCar)
Next nItem	

Return cTexto

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³CHKIMSS   ºAutor  ³Silvia Taguti       º Data ³  27/12/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida IMSS - Mexico                                        º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function ChkImss(cImss, lExibeHelp)

Local ni:=0, nSoma:=0, nDV:=0
Local nAux := 0
Local cTexto := ""

Default lExibeHelp := .T.

//# Obrigatorio digitar 10 ou 11 digitos
If Len(Alltrim(cImss)) < 10
	If lExibeHelp
		Help(" ",1, "RGINVALID")	//# Codigo IMSS Invalido. Verifique o codigo IMSS.
	EndIf
	Return(.F.)            
EndIf

cTexto := Substr(cImss,1,10)

For ni:=1 To Len(cTexto)
	If mod(ni,2)==0
  		nAux := Val(Substr(cTexto,ni,1)) * 2
  		If nAux >= 10
	   	nAux := 1+ (nAux-10)
  		EndIf 
  		nSoma += nAux
 	Else
  		nSoma += Val(Substr(cTexto,ni,1))
 	EndIf
Next

nDV := Mod(Int(nSoma/10) * 10 + 10 - nSoma,10)

If Len(Alltrim(cImss)) == 11
	If Right(Alltrim(cImss),1) <> Str(nDv,1)
		If lExibeHelp
			Help(" ",1, "DIGVERINVA")	// Digito Verificador Invalido!  Informe um digito verificador válido.
		EndIf
		Return(.F.)            
	EndIf
Else
	M->RA_RG :=	cTexto+Str(nDv,1)
EndIf	

Return(.T.) 


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³EstCurp   ºAutor  ³Microsiga           º Data ³  04/01/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna o Estado para Curp                                 º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP              	                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function EstCurp(cEstado,cTipo)

Local cEstCurp := " "
Local cEntFed	:= "  "
Default cTipo  := "1"

If Alltrim(cEstado) == "AGS"   
	cEstCurp :="AS"  
	cEntFed	:="01"
ElseIf Alltrim(cEstado) == "BC"
	cEstCurp :="BC"
	cEntFed	:="02"
ElseIf Alltrim(cEstado) == "BCS"
	cEstCurp :="BS"
	cEntFed	:="03"
ElseIf Alltrim(cEstado) == "CAMP"
	cEstCurp :="CC"
	cEntFed	:="04"
ElseIf Alltrim(cEstado) == "CHIH"
	cEstCurp :="CH"
	cEntFed	:="08"
ElseIf Alltrim(cEstado) == "CHIS"
	cEstCurp :="CS"
	cEntFed	:="07"
ElseIf Alltrim(cEstado) == "COAH"
	cEstCurp :="CL"
	cEntFed	:="05"
ElseIf Alltrim(cEstado) == "COL"
	cEstCurp :="CM"
	cEntFed	:="06"
ElseIf Alltrim(cEstado) == "DF"
	cEstCurp :="DF"
	cEntFed	:="09"
ElseIf Alltrim(cEstado) == "DGO"
	cEstCurp :="DG"
	cEntFed	:="10"
ElseIf Alltrim(cEstado) == "EX"
	cEstCurp :="NE"
	cEntFed	:="00"
ElseIf Alltrim(cEstado) == "GRO"
	cEstCurp :="GR"
	cEntFed	:="12"
ElseIf Alltrim(cEstado) == "GTO"
	cEstCurp :="GT"
	cEntFed	:="11"
ElseIf Alltrim(cEstado) == "HGO"
	cEstCurp :="HG"
	cEntFed	:="13"
ElseIf Alltrim(cEstado) == "JAL"
	cEstCurp :="JC"
	cEntFed	:="14"
ElseIf Alltrim(cEstado) == "MEX"
	cEstCurp :="MC"
	cEntFed	:="15"
ElseIf Alltrim(cEstado) == "MICH"
	cEstCurp :="MN"
	cEntFed	:="16"
ElseIf Alltrim(cEstado) == "MOR"
	cEstCurp :="MS"
	cEntFed	:="17"
ElseIf Alltrim(cEstado) == "NAY"
	cEstCurp :="NT"
	cEntFed	:="18"
ElseIf Alltrim(cEstado) == "NL"
	cEstCurp :="NL"
	cEntFed	:="19"
ElseIf Alltrim(cEstado) == "OAX"
	cEstCurp :="OC"
	cEntFed	:="20"
ElseIf Alltrim(cEstado) == "PUE"
	cEstCurp :="PL"
	cEntFed	:="21"
ElseIf Alltrim(cEstado) == "QROO"
	cEstCurp :="QR"
	cEntFed	:="23"
ElseIf Alltrim(cEstado) == "QRO"
	cEstCurp :="QT"
	cEntFed	:="22"
ElseIf Alltrim(cEstado) == "SIN"
	cEstCurp :="SL"
	cEntFed	:="25"
ElseIf Alltrim(cEstado) == "SLP"
	cEstCurp :="SP"
	cEntFed	:="24"
ElseIf Alltrim(cEstado) == "SON"
	cEstCurp :="SR"
	cEntFed	:="26"
ElseIf Alltrim(cEstado) == "TAB"
	cEstCurp :="TC"
	cEntFed	:="27"
ElseIf Alltrim(cEstado) == "TAMP"
	cEstCurp :="TS"
	cEntFed	:="28"
ElseIf Alltrim(cEstado) == "TLAX"
	cEstCurp :="TL"
	cEntFed	:="29"
ElseIf Alltrim(cEstado) == "VER"
	cEstCurp :="VZ"
	cEntFed	:="30"
ElseIf Alltrim(cEstado) == "YUC"
	cEstCurp :="YN"
	cEntFed	:="31"
ElseIf Alltrim(cEstado) == "ZAC"
	cEstCurp :="ZS"
	cEntFed	:="32"
EndIf

If cTipo == "1"
	Return cEstCurp      
Else 
	Return cEntFed      	
EndIf	

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³CURPTROCA ºAutor  ³Microsiga           º Data ³  05/22/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function CurpTroca(cCaract)

Local nx := 0    

cCaractCor := ""

For nx := 1 to Len(cCaract)
	If Substr(cCaract,nx,1) $ "Ñ|ñ"
		cCaractCor += "X"
	ElseIf Substr(cCaract,nx,1) $ "Á|á" 
		cCaractCor += "A"
	ElseIf Substr(cCaract,nx,1) $ "É|é"
		cCaractCor += "E"
	ElseIf Substr(cCaract,nx,1) $ "Í|í" 
		cCaractCor += "I"
	ElseIf Substr(cCaract,nx,1) $ "Ó|ó" 
		cCaractCor += "O"
	ElseIf Substr(cCaract,nx,1) $ "Ú|ú" 
		cCaractCor += "U"
	ElseIf Substr(cCaract,nx,1) $ "À|à" 
		cCaractCor += "A"
	ElseIf Substr(cCaract,nx,1) $ "È|è" 
		cCaractCor += "E"
	ElseIf Substr(cCaract,nx,1) $ "Ì|ì" 
		cCaractCor += "I"
	ElseIf Substr(cCaract,nx,1) $ "Ò|ò" 
		cCaractCor +=  "O"
	ElseIf Substr(cCaract,nx,1) $ "Ù|ù" 
		cCaractCor += "U"
	ElseIf Substr(cCaract,nx,1) $ "#"
		cCaractCor += "X"
	Else
		cCaractCor += Substr(cCaract,nx,1)
	EndIf	
Next
	
Return(cCaractCor)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ fAfasAts ³ Autor ³ Equipe RH             ³ Data ³ 14/11/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Busca os afastamentos de acordo com o parametro P_TPAFATS	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fAfasAts 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function fAfasAts(dDataIni,dDataFim,dDataAtu)
Local aArea    := GetArea()
Local lQuery   := .F.
Local nDAfas   := 0
Local sDataIni := DtoS(dDataIni)
Local sDataFim := DtoS(dDataFim)
Local cTipoAf  := ""
Local cTipoAfas:= ALLTRIM(P_TPAFATS)
Local ns

cIndCond:= "R8_FILIAL + R8_MAT + DTOS(R8_DATAINI)"
cFor:='(R8_FILIAL+R8_MAT = "'+SRA->RA_FILIAL+SRA->RA_MAT+'")'
cFor+=' .AND. ( DTOS(R8_DATAFIM) >= "'+SDataIni+'" .OR. EMPTY(R8_DATAFIM))' 
cFor+=' .AND. DTOS(R8_DATAINI) <= "'+SDataFim+'"'

For nS:=1 to Len(cTipoAfas) Step 3
		cTipoAf += "'"+Subs(cTipoAfas,nS,3)+"'"
		If ( nS+3) <= Len(cTipoAfas)
			cTipoAf += "," 
		EndIf
Next nS        

lQuery	:= .T. 
//-- Modifica variaveis para a Query
cSQL := "SELECT * "    
cSQL += " FROM " +    RetSqlName("SR8") + " SR8, " + RetSqlName("SRA") + " SRA "
cSQL += " WHERE "  
cSQL += " SR8.R8_FILIAL  = '"+SRA->RA_FILIAL+"' AND"
cSQL += " SR8.R8_MAT     = '"+SRA->RA_MAT   +"' AND"
cSQL += " ( SR8.R8_DATAFIM >= '"+sDataIni+"' OR SR8.R8_DATAFIM = '        ' ) AND"
cSQL += " SR8.R8_DATAINI <= '"+sDataFim+"' AND"
cSQL += " SR8.R8_TIPOAFA IN ("   + Upper(cTipoAf) + ") AND " 
cSQL += " SR8.R8_FILIAL = SRA.RA_FILIAL AND SR8.R8_MAT  =  SRA.RA_MAT AND"
cSQL += " SR8.D_E_L_E_T_ <> '*' AND  SRA.D_E_L_E_T_ <> '*' "

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define order by de acordo com a ordem...                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

cSQL += " ORDER BY R8_FILIAL, R8_MAT,R8_DATAINI "
cSQL     	:= ChangeQuery(cSQL)
aStruSR8 	:= SR8->(dbStruct())
SR8->( dbCloseArea() ) //Fecha o SR8 para uso da Query
dbUseArea(.T., "TOPCONN",TcGenQry(,,cSQL), "SR8",.F.,.T.)
For nS := 1 To Len(aStruSR8)
	If ( aStruSR8[nS][2] <> "C" )
		TcSetField("SR8",aStruSR8[nS][1],aStruSR8[nS][2],aStruSR8[nS][3],aStruSR8[nS][4])
	EndIf
Next nS

DbSelectArea("SR8")
DbGotop()
While !Eof()
   IF SR8->R8_TIPOAFA $ cTipoAf  // Verifica se satisfaz o tipo de afastamento
      
      /*Caso o inicio do afastamento seja menor que o inicio do Periodo, utiliza o Inicio do Periodo,
      caso contrario utiliza a data do afastamento*/
      dAuxIni := IF ( SR8->R8_DATAINI < dDataIni,dDataIni,SR8->R8_DATAINI) 
      
      /*Caso o fim do afastamento seja vazio ou for maior que o fim do Periodo, utiliza o Fim do Periodo,
      caso contrario utiliza a data do fim do afastamento*/
      dAuxFim := IF ( EMPTY(SR8->R8_DATAFIM) .OR. SR8->R8_DATAFIM > dDataFim,dDataFim,SR8->R8_DATAFIM)
      
      nDAfas += dAuxFim - dAuxIni +1 // soma os dias de afastamentos
      
      /*Alimenta o Mnemonico LAFASATS para indicar se Continua Afastado no Periodo, 
      se continua, nao abre novo periodo*/
      LAFASATS := IF ( EMPTY(SR8->R8_DATAFIM),.T.,.F.)

   EndIf
   DbSkip()
EndDo

dbSelectArea("SR8")
dbSelectArea("SR8")
dbCloseArea()
dbSelectArea( "SR8" )
Set Filter to
dbSetOrder( RetOrdem( "SR8", "R8_FILIAL+R8_MAT+DTOS(R8_DATAINI)+R8_TIPOAFA+STR(R8_DIASEMP)" ) )
RestArea( aArea )

Return(nDAfas)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ChkCuil   ³Autor  ³Silvia Taguti       ³ Data ³  06/11/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Validacao do Cuil - Argentina                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ChkCuil(cCUIL)

Local lRetorno := .T.
Local cSituaFun := " "

aArea := GetArea()

lGp010Auto  := If (Type("lGp010Auto") =="U",.F.,lGp010Auto)

dbSelectArea("SRA")
dbSetOrder(5)      // RA_FILIAL + RA_CIC
lFound  := .F.			
If dbSeek(xFilial("SRA")+cCUIL)
   While SRA->( !Eof() ) .AND. xFilial("SRA")+cCUIL == SRA->RA_FILIAL+SRA->RA_CIC .AND. !lFound							
	  	_cCod  := SRA->RA_CIC				     
		cSituaFun:= SRA->RA_SITFOLH
      If _cCod == SRA->RA_CIC
         SRA->(DbSkip())
	      Loop
      EndIf			      
	End
   lFound   := .T.
	If lFound .AND. cSituaFun <> "D"
		If !lGp010Auto
			If !(MsgYesNo(OemToAnsi(STR0008), OemToAnsi(STR0004)))
				Return (.F.)
			EndIf	        
		EndIf
	EndIf
EndIf   

RestArea(aArea)

Return(lRetorno)     

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEXFUN   ºAutor  ³Erika Kanamori      º Data ³  09/08/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Busca e posiciona no registro de ferias do funcionario.     º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fBuscaFerias(cMat, dDataIni )

Local lRet:= .F.  
        
dbSelectArea("SRH")
dbSetOrder(RetOrder( "SRH", "RH_FILIAL+RH_MAT+DTOS(RH_DATAINI)" ))
If dbSeek(xFilial("SRH") + cMat + DtoS(dDataIni) )
	lRet:= .T. 
EndIf

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEXFUN   ºAutor  ³Erika Kanamori      º Data ³  09/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Busca verbas do SRR do periodo e matricula selecionados.    º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fBuscaSRR( cMat, cRoteiro, aPeriodo )
Local aVerbas:= {}
      
dbSelectArea("SRR")
dbSetOrder(RetOrder( "SRR", "RR_FILIAL+RR_MAT+RR_PERIODO+RR_ROTEIR+RR_SEMANA+RR_PD" ))
If dbSeek(xFilial("SRR") + cMat + aPeriodo[1,1] + cRoteiro + aPeriodo[1,2] )
	While ("SRR")->(!Eof()) .AND. ("SRR")->(RR_FILIAL+RR_MAT+RR_PERIODO+RR_ROTEIR+RR_SEMANA) == xFilial("SRR") + cMat + aPeriodo[1,1] + cRoteiro + aPeriodo[1,2]
    	aAdd(aVerbas, { SRR->RR_PD, SRR->RR_HORAS, RR_VALOR, RR_ROTEIR} )
    	("SRR")->(dbSkip())
    End
EndIf

Return(aVerbas)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fValidRuc ºAutor  ³ Ademar fernandes   º Data ³  04/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Validar o numero do Ruc da empresa para o Peru.            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fValidRuc()
 
Local cFator := "5432765432"
Local cRuc   := &(Readvar())  
Local nTotal := 0  
Local nCnt   := 0
Local nTot1  := 0
Local nTot2  := 0
Local nTot3  := 0
Local i
Local nDig   := Val(Substr(cRuc,11,1))

	For i=1 to Len(cFator)
		nCnt   := Val(Substr(cFator,i,1)) * Val(Substr(cRuc,i,1))
		nTotal += nCnt
	Next i
	
	nTot1 := NoRound(nTotal / 11 ,0 )
	
	nTot2 := nTotal - ( nTot1 * 11 )
	
	nTot3 := 11 - nTot2  
	
	IF nTot3 >= 10
	   nTot3 -=10
	EndIf
	
	IF nTot3 <> nDig
		Help(" ",1, "DIGVERINVA")	// Digito Verificador Invalido!  Informe um digito verificador válido.
		Return(.F.)
	EndIf

Return( .T. )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEXFUN   ºAutor  ³Erika Kanamori      º Data ³  01/11/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna dias trabalhados e valores acumulados do ano.       º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³calculo de Utilidades da localizacao Peru.                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cAnoPLR - Ano a ser acumulado.                              º±±
±±º          ³cCondPLR - Condicao das verbas a serem acumuladas no ano.   º±±
±±º          ³cVrbDTrab - Verbas de remuneracao (serao consideradas no    º±±
±±º          ³        calculo de dias trabalhados.                        º±±
±±º          ³nAcumlAno - Retorno dos valores acumulados no ano (por      º±±
±±º          ³        referencia).                                        º±±
±±º          ³nDTrabAno - Retorno dos dias trabalhados no ano (por        º±±
±±º          ³        referencia).                                        º±±
±±º          ³cMat -  Opcional. Traz os acumulados do ano somente desse   º±±
±±º          ³        funcionario.                                        º±±
±±º          ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function fRetTotFunc( cAnoPLR, cCondPLR, cVrbDTrab,nAcumlAno, nDTrabAno, cMat )   
                         
Local nAcumFunc := 0
Local nDiasFunc:= 0 
Local nDias    := 0 
Local cVerbas  := ""    
Local cVrbSemPg:= ""
Local cVrbComPg:= "" 
Local aAreaSRA:= ("SRA")->(GetArea())

          
//busca verbas de afastamento
dbSelectArea("RCM")
dbSetOrder(RetOrder("RCM", "RCM_FILIAL+RCM_TIPO"))
While ("RCM")->(!Eof()) .AND. ("RCM")->(RCM_FILIAL) == xFilial("RCM")
	If ("RCM")->RCM_TIPO == "1"
    	cVrbSemPg:= ("RCM")->RCM_PD + "/"           
	EndIf
	If ("RCM")->RCM_TIPO == "2"
    	cVrbComPg:= ("RCM")->RCM_PD + "/"           
	EndIf      
	("RCM")->(dbSkip())
End

           
dbSelectArea("SRA")
dbSetOrder(RetOrder("SRA", "RA_FILIAL+RA_MAT"))     
If (cMat <> Nil)
	dbSeek(xFilial("SRA") + cMat)
EndIf    

While ("SRA")->(!Eof()) .AND. ("SRA")->(RA_FILIAL) == xFilial("SRA") .AND. ( cMat == Nil .OR. ("SRA")->RA_MAT == cMat )
	If Year(("SRA")->RA_ADMISSA) <= Val(cAnoPLR) .AND. (Empty(("SRA")->RA_DEMISSA) .OR. Year(("SRA")->RA_DEMISSA) >= Val(cAnoPLR) ) .AND.;
	  ("SRA")->RA_PLR <> "2" .And. Empty(("SRA")->RA_CATCONS)                                                                             
	    //busca acumulados
		nAcumFunc:= 0 
		
		fBuscaAcm(  , cCondPLR, CtoD("01/01/"+ cAnoPLR), CtoD("31/12/"+ cAnoPLR), "V", @nAcumFunc, , , .F. )
					   
		//busca dias trabalhados            
		If (lMensal:= !(("SRA")->RA_CATFUNC $ "C*T"))
			cVerbas:= cVrbDTrab + "/"
			cVerbas+= cVrbComPg
		Else                   
			cVerbas:= cVrbSemPg
		EndIf
		
		
		nDiasFunc:= 0
		fBuscaAcm(	cVerbas, , CtoD("01/01/"+ cAnoPLR), CtoD("31/12/"+ cAnoPLR), "H", , @nDiasFunc, , .F. )               
		                               
		//Para comissionados e tarefeiros, os dias trabalhados sao calculados descontando-se os dias de afastamento sem pagamento.
		If !lMensal  
			nDias:= 360
			If Year(SRA->RA_ADMISSA) == Val(cAnoPLR)
				If !Empty(SRA->RA_DEMISSA)                   
		    		nDias:= DateDiffDay( ("SRA")->RA_ADMISSA, ("SRA")->RA_DEMISSA )
		  		Else
					nDias:= DateDiffDay( ("SRA")->RA_ADMISSA, CtoD("31/12/"+ cAnoPLR) )		  		
		  		EndIf
			EndIf                                                               
			nDiasFunc:= nDias - Abs(nDiasFunc)
		EndIf    
		If ("SRA")->RA_CATFUNC == "H"
			nDiasFunc:= nDiasFunc / 8
		EndIf
		
		nDiasFunc:= Min(nDiasFunc, 360)
		
		
		//totaliza
		nAcumlAno+= nAcumFunc
		nDTrabAno+= nDiasFunc
				        
	EndIf
	("SRA")->(dbSkip())
End      

RestArea( aAreaSRA )

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEXFUN   ºAutor  ³Erika Kanamori      º Data ³  18/02/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Grava o conteudo passado na tabela auxiliar desejada.       º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³calculo de Utilidades da localizacao Peru.                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³cTabela  - Tabela onde sera gravada a informacao.           º±±
±±º          ³cCampo   - Nome do campo a ser gravado (RCB_CAMPOS)         º±±
±±º          ³cConteudo- Conteudo a ser gravado.                          º±±
±±º          ³nRCCSeq  - Numero da linha a ser gravada (RCC_SEQUEN).      º±±
±±º          ³dData    - Data para referencia de mes/ano na tabela RCC    º±±
±±º          ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fGravaTabAux( cTabela, cCampo, cConteudo, nRCCSEq, dData )
       
Local nPosIni := 0
Local nTamanho:= 0
Local nEspaco := 0                                               
Local cMesAno:= Iif(dData <> Nil, MesAno(dData), Space(6))
Local cRCCConteu:= ""
Local lRet:= .F.    
Local aAreaRCB:= ("RCB")->(GetArea())
Local aAreaRCC:= ("RCC")->(GetArea())                           
   
dbSelectArea("RCB")
dbSetOrder(RetOrder("RCB", "RCB_FILIAL+RCB_CODIGO"))
dbSeek(xFilial("RCB")+ cTabela)
While ("RCB")->(!Eof()) .AND. ("RCB")->(RCB_FILIAL+RCB_CODIGO) == xFilial("RCB")+cTabela .AND. nTamanho == 0
	If AllTrim(("RCB")->RCB_CAMPOS) == cCampo
		nTamanho:= ("RCB")->RCB_TAMAN
	Else
		nPosIni+= ("RCB")->RCB_TAMAN 	
	EndIf 
	
   ("RCB")->(dbSkip())
End                   
RestArea( aAreaRCB )

dbSelectArea("RCC")
dbSetOrder(RetOrder("RCC", "RCC_FILIAL+RCC_CODIGO+RCC_FIL+RCC_CHAVE+RCC_SEQUEN"))
If dbSeek(xFilial("RCC")+cTabela+xFilial("RCC")+cMesAno+nRCCSeq)        
	nEspaco:= nTamanho - len(cConteudo)
	cRCCConteu:= Substr(("RCC")->RCC_CONTEU, 1, nPosIni)+ Space(nEspaco)+cConteudo
	IF Reclock( "RCC" , .F. )
		RCC->RCC_CONTEU:= cRCCConteu
		RCC->( MsUnLock() )
		lRet:= .T.
	EndIf
EndIf
RestArea( aAreaRCC )

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ChkDocEQU ºAutor  ³Alex Fagundes       º Data ³  31/05/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida ChkDocEQU - Equador                                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function ChkDocEQU(cDocEQU, lExibeHelp)

	Local ni:=0, nSoma:=0, nDV:=0
	Local nAux := 0
	Local cTexto := ""    
	Local cCoefic := "212121212"
	Local cDoisDigitos := ""
	Local cDivisao := ""
	
	DEFAULT lExibeHelp := .T.
	
	//# Obrigatorio digitar 9 ou 10 digitos
	If Len(Alltrim(SubStr(cDocEQU,1,9))) < 9
		If lExibeHelp
			Help(" ",1,"RGINVALID")	//# Codigo Cédula de identidade Invalido. Verifique o codigo.
		EndIf
		Return(.F.)            
	EndIf
	
	cTexto := Substr(cDocEQU,1,9)
	
	For ni:=1 To Len(cTexto)
		nAux := val(SubStr(cTexto,ni,1)) * val(SubStr(cCoefic,ni,1))
		If nAux>10
			cDoisDigitos := AllTrim(Str(nAux))
			nAux := Val(SubStr(cDoisDigitos,1,1)) + Val(SubSTR(cDoisDigitos,2,1))
		EndIf
		nSoma += nAux
	Next
	              
	cDivisao := Alltrim(Str(Mod(nSoma/10,10)))
	nDV := Val(Substr( cDivisao , At( ".", cDivisao ) + 1 , Len(cDivisao) ))
	nDV := 10 - nDV

	If Len(Alltrim(cDocEQU)) == 11
		If Right(Alltrim(cDocEQU),1) <> Str(nDv,1)
			If lExibeHelp
				Help(" ",1,"DIGVERINVA")	// Digito Verificador Invalido!  Informe um digito verificador válido.
			EndIf
			Return(.F.)            
		EndIf
	Else
//		M->RA_RG :=	cTexto+"-"+Str(nDv,1)   // Ajustado para gravar corretamente respeitando a mascara do campo.
		M->RA_RG :=	cTexto+Str(nDv,1)
	EndIf	
	
Return(.T.)



/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ fValRotArg		³Autor³Igor Franzoi		  ³ Data ³17/02/2011³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Retorna o ultimo roteiro para recalculo de Ganancias		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEM020														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³lRet														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function fValRotArg( aLog, cRotCalc )

Local lRet := .T.


//Variavel para uso no log
Local cMsgLog	:= ""

//Variaveis de query
Local cAliasQry := ""
Local cWhere	:= ""
Local cOrder	:= ""            
Local lInconsist:= .F.

Local nLstSeq	:= 1

	
cAliasQry := GetNextAlias()

cWhere += "%"
cWhere += " RH8.RH8_FILIAL = 		'" + SRA->RA_FILIAL + "' "
cWhere += " AND RH8.RH8_MAT = 		'" + SRA->RA_MAT    + "' "
cWhere += " AND RH8.RH8_PROCES = 	'" + SRA->RA_PROCES + "' "
cWhere += " AND RH8.RH8_PERIOD = 	'" + RCH->RCH_PER   + "' "
cWhere += "%"

//cOrder := "% RH8_FILIAL, RH8_MAT, RH8_PROCES, RH8_PERIOD, RH8_ROTEIR, RH8_NUMPAG, RH8_SEQCAL, RH8_DATA, RH8_HORA %"
cOrder := "%R_E_C_N_O_ %"    

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Seleciona os dados do cabecalho e item do calendario		  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/	
BeginSql alias cAliasQry
	column RH8_DATA as Date
	SELECT RH8.RH8_FILIAL, RH8.RH8_MAT, RH8.RH8_PROCES, RH8.RH8_PERIOD, RH8.RH8_ROTEIR, RH8.RH8_NUMPAG, RH8.RH8_SEQCAL, RH8.RH8_DATA, RH8.RH8_HORA
	FROM 		%table:RH8% RH8
	WHERE 		%exp:cWhere% AND
				RH8.%NotDel% 
	ORDER BY    %exp:cOrder%				
EndSql
	
While ( (cAliasQry)->(!Eof()) )

	If !( nLstSeq == Val((cAliasQry)->(RH8_SEQCAL)) )
		lInconsist := .T.
	EndIf

	If ( cRotCalc == (cAliasQry)->(RH8_ROTEIR) ) .and. !lInconsist
		lInconsist := .T.
	ElseIf lInconsist		
		cMsgLog += (cAliasQry)->(RH8_ROTEIR) + " / "
		lRet := .F.
	EndIf
	
	nLstSeq++
	
	(cAliasQry)->(dbSkip())
EndDo

If !lRet
	cMsgLog := CRLF + OemToAnsi(STR0004) + " - " + OemToAnsi(STR0009) + cMsgLog
	cMsgLog += CRLF + OemToAnsi(STR0010) + " "+ SRA->RA_FILIAL + " / " + SRA->RA_MAT
	aAdd( aLog, cMsgLog )
EndIf

(cAliasQry)->(dbCloseArea())


Return lRet

/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ fLastRotArg	³Autor³Igor Franzoi		  ³ Data ³17/02/2011³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Retorna o ultimo roteiro para recalculo de Ganancias		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEM020														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³lRet														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function fLastRotArg()

Local cRotRet	:= ""

Local cRotCal	:= fGetCalcRot(cTipoRot)

//Variaveis de query
Local cAliasQry := ""
Local cWhere	:= ""
Local cOrder	:= ""                       
Local cPer		:= If( Type( "cPeriodo" ) == "U",RCH->RCH_PER, cPeriodo )

	
cAliasQry := GetNextAlias()

cWhere += "%"
cWhere += " RH8.RH8_FILIAL = 		'" + SRA->RA_FILIAL + "' "
cWhere += " AND RH8.RH8_MAT = 		'" + SRA->RA_MAT    + "' "
cWhere += " AND RH8.RH8_PROCES = 	'" + SRA->RA_PROCES + "' "
cWhere += " AND RH8.RH8_PERIOD = 	'" + cPer   + "' "
cWhere += "%"

cOrder := "% RH8_FILIAL, RH8_MAT, RH8_PROCES, RH8_PERIOD, RH8_NUMPAG, RH8_SEQCAL, RH8_DATA, RH8_HORA %"

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Seleciona os dados do cabecalho e item do calendario		  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/	
BeginSql alias cAliasQry
	column RH8_DATA as Date
	SELECT RH8_FILIAL, RH8_MAT, RH8_PROCES, RH8_PERIOD, RH8_ROTEIR, RH8_NUMPAG, RH8_DATA, RH8_HORA
	FROM 		%table:RH8% RH8
	WHERE 		%exp:cWhere% AND
				RH8.%NotDel% 
	ORDER BY    %exp:cOrder%				
EndSql

While ( (cAliasQry)->(!Eof()) )		
	If !( (cAliasQry)->(RH8_ROTEIR) == cRotCal )
		cRotRet := (cAliasQry)->(RH8_ROTEIR)
	EndIf			
	(cAliasQry)->(dbSkip())
EndDo

(cAliasQry)->(dbCloseArea())

Return cRotRet

/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ fRecRotArg		³Autor³Igor Franzoi		  ³ Data ³17/02/2011³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Grava ultimo calculo										³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEM020														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³lRet														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function fRecRotArg( cRotGrava )

Local lRet := .T.

Local cAlias := "RH8"
Local cKeyRH8:= ""
Local cUltRot:= ""
Local nUltSeq:= 0
Local nSeqCal:= 1
           
dbSelectArea(cAlias)
(cAlias)->(dbSetOrder(2))

cKeyRH8	:= SRA->RA_FILIAL+SRA->RA_MAT+SRA->RA_PROCES+RCH->RCH_PER

(cAlias)->(dbSeek(cKeyRH8))

While ( (cAlias)->(!Eof() .and. ;
		(cAlias)->(RH8_FILIAL)+(cAlias)->(RH8_MAT)+(cAlias)->(RH8_PROCES)+(cAlias)->(RH8_PERIOD) == cKeyRH8 ))
    

	cUltRot := (cAlias)->(RH8_ROTEIR)
	nUltSeq	:= val((cAlias)->(RH8_SEQCAL))
	
	(cAlias)->(dbSkip())
EndDo

(cAlias)->(dbSetOrder(1))    

If ( cUltRot == cRotGrava )	
	cKeyRH8	:= SRA->RA_FILIAL+SRA->RA_MAT+SRA->RA_PROCES+RCH->RCH_PER+cRotGrava+RCH->RCH_NUMPAG+dtos(RCH->RCH_DTINI)
	If (cAlias)->(dbSeek(cKeyRH8))
		RecLock( cAlias, .F. )
	EndIf
	nSeqCal := val((cAlias)->(RH8_SEQCAL))
Else
	RecLock( cAlias, .T.)
	nSeqCal := nUltSeq+1	
EndIf

(cAlias)->(RH8_FILIAL) := SRA->RA_FILIAL
(cAlias)->(RH8_MAT)    := SRA->RA_MAT
(cAlias)->(RH8_PROCES) := SRA->RA_PROCES
(cAlias)->(RH8_PERIOD) := RCH->RCH_PER
(cAlias)->(RH8_ROTEIR) := cRotGrava
(cAlias)->(RH8_NUMPAG) := RCH->RCH_NUMPAG
(cAlias)->(RH8_DATA)   := RCH->RCH_DTINI
(cAlias)->(RH8_HORA)   := Val(Time())
(cAlias)->(RH8_SEQCAL) := AllTrim(Str(nSeqCal))

(cAlias)->(MsUnlock())

Return lRet

/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ fDelRH8Arg		³Autor³Igor Franzoi		  ³ Data ³18/02/2011³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Deleta as informações do RH8								³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEM120														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³lRet														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function fDelRH8Arg( cPeriodo, cRoteiro, cNumPag )

Local cAliasRH8	:= "QRH8"
Local cDelet
Local cQuery
Local cCpoRecno
Local cQueryDelet
Local cWhere
Local cFilRecno

Local lRet 		:= .T.

Local nMinRec	:= 0
Local nMaxRec	:= 0

cWhere := "%"
cWhere += " RH8.RH8_PERIOD = '" + cPeriodo + "'"
cWhere += " AND RH8.RH8_NUMPAG = '" + cNumPag + "'"
cWhere += " AND RH8.RH8_ROTEIR = '" + cRoteiro + "' "
cWhere += "%"

BeginSql alias cAliasRH8
	SELECT MIN(RH8.R_E_C_N_O_) MINREC, MAX(RH8.R_E_C_N_O_) MAXREC
	FROM	%table:RH8% RH8
	WHERE	%exp:cWhere% AND
			RH8.%NotDel%
EndSql

nMinRec := (cAliasRH8)->( MINREC )
nMaxRec := (cAliasRH8)->( MAXREC )

(cAliasRH8)->( DbCloseArea() )

cDelet 		:= "% RH8.D_E_L_E_T_ = ' ' %"
cCpoRecno 	:= "R_E_C_N_O_"

cQuery := " DELETE FROM " + InitSqlName("RH8") + " "
cQuery += " WHERE "
                                                   
While ( nMinRec <= nMaxRec )

	cFilRecno := "("
	cFilRecno += InitSqlName("RH8")+"." + cCpoRecno + " >= " + AllTrim( Str( nMinRec , 18 , 0 ) )
	cFilRecno += " AND "
	cFilRecno += InitSqlName("RH8")+"." + cCpoRecno + " <= " + AllTrim( Str( ( nMinRec += 1024 ) , 18 , 0 ) )
	cFilRecno += ")"
	cQueryDelet := ( cQuery +  cFilRecno )
	
	TcSqlExec( cQueryDelet )

End While

TcRefresh( InitSqlName("RH8") )

Return( lRet )

/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ fDelRH8Mat		³Autor³Igor Franzoi		  ³ Data ³18/02/2011³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Deleta as informações do RH8 - Por Funcionario				³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEM120														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³lRet														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function fDelRH8MAT( cRoteiro,cNumPag, cFilDel, cMatDel  )

Local lRet		:= .T.

Local cQuery	:= ""
Local cWhere	:= ""

DEFAULT cFilDel := ""
DEFAULT cMatDel	:= ""

If Empty(cFilDel)
	cFilDel := SRA->RA_FILIAL
EndIf

If Empty(cMatDel)
	cMatDel := SRA->RA_MAT
EndIf

cWhere += " RH8_FILIAL	= '" +cFilDel+ "' "
cWhere += " AND RH8_MAT = '" +cMatDel+ "' "
cWhere += " AND RH8_PERIOD = '" + cPeriodo + "'"
cWhere += " AND RH8_NUMPAG = '" + cNumPag + "'"
cWhere += " AND RH8_ROTEIR = '" + cRoteiro + "' "

cQuery := " DELETE FROM " + InitSqlName("RH8")
cQuery += " WHERE " + cWhere

TcSqlExec( cQuery )

TcRefresh( InitSqlName("RH8") )

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ExtraiNome  ³ Autor ³ Renan Borges		³ Data ³ 05.09.14 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna o nome sem as preposições.					      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/

Static Function ExtraiNome(cComplNome) 
Local nx	:= 0
Local y		:= 0
Local n		:= 1
Local cNome	:= ""

Default cComplNome := ""

	For nx 	:= 1 to Len(Alltrim(cComplNome))
		y 	:= y + 1
		cNome := Substr(cComplNome,n,y)
		If 	cNome = "DA "	.OR. cNome = "DAS " .OR. cNome = "DE "  .OR.;
			cNome = "DEL " 	.OR. cNome = "DER " .OR. cNome = "DI "  .OR.;
			cNome = "DIE " 	.OR. cNome = "DD "  .OR. cNome = "EL "  .OR.;
			cNome = "LA "	.OR. cNome = "LOS " .OR. cNome = "LAS " .OR.;
			cNome = "LE "	.OR. cNome = "LES " .OR. cNome = "MAC " .OR.;
			cNome = "MC "	.OR. cNome = "VAN " .OR. cNome = "VON " .OR.;
			cNome = "Y "	.OR. cNome = "MA "
			n := nx + 1
			y := 0
		Endif
	Next nx
	
Return(cNome)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³NomeComp  ³ Autor ³ Renan Borges		³ Data ³ 10.09.14 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Trata nomes compostos que comecem com "CH" ou "LL".        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/

Static Function NomeComp(cNome) 

If ( Left(Alltrim(cNome),2) ) $ ( "CH|LL" )
	cNome := Substr(cNome,1,1) + Substr(cNome,3)
EndIf
	
Return cNome

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³fRetMat	³ Autor ³ Alceu Pereira 		³ Data ³ 21/08/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Selecionar Matriculas na tabela RDZ, tendo como chave os 	  ³±±
±±³          |valores filial+matricula concatenados,juntos formam o codigo|±±  
±±³			 |da entidade pesquisando no campo RDZ_CODENT, depois de 	  |±±
±±³			 |localizar o registro o campo RZ_CODRDO servira de chave     |±±
±±³          |Localizando as matriculas do funcionario e retornando 	  |±±
±±³			 |um array com as matriculas.								  |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fRetMat(cCodEnt)											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Esta funcao recebe o codigo da entidade que e formado		  ³±±
±±³          |pela concatenacao da filial+matricula.					  ³±±	
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Localizado Argentina										  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fRetMat(cCodEnt)

Local aAmat := {}  //Array que contem as matriculas 
Local aArea	:= GetArea()
Local nCODRD0 := 0
Local cAlias := "" 


		cAlias := "Q1"
		 
		cQuery := " SELECT "
		cQuery += " RDZ_CODRD0 AS CODRD0"
		cQuery += " FROM "+	RetSqlName("RDZ") + " RDZ "
		cQuery += " WHERE " 
		cQuery += "	RDZ_FILIAL = '" + xFilial("RDZ") + "' AND "
		cQuery += " RDZ_CODENT = '" + cCodEnt + "' AND "   
		cQuery += " D_E_L_E_T_ <> '*' "		
		
		cQuery := ChangeQuery(cQuery)
			
		dbUseArea(.T., "TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)  
		nCODRD0 := CODRD0 
		(cAlias)->(DbCloseArea())    	

		cAlias := "Q2"
			
		cQuery := " SELECT "      
		cQuery += " RDZ_CODENT " 
		cQuery += " FROM " + RetSqlName("RDZ") "
		cQuery += " WHERE RDZ_FILIAL = '" + xFilial("RDZ") + "' AND "
		cQuery += " RDZ_CODRD0 = '" + nCODRD0 + "' AND "
		cQuery += " D_E_L_E_T_ <> '*' "		
		
		cQuery := ChangeQuery(cQuery)
		dbUseArea(.T., "TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)  			
		
		While (cAlias)->(!Eof())
			filial := "'" + SUBSTR((cAlias)->RDZ_CODENT,1,FWGETTAMFILIAL) + "'"
			matricula := "'" + SUBSTR((cAlias)->RDZ_CODENT,FWGETTAMFILIAL + 1, 6) + "'" 
			AADD(aAmat,{ALLTRIM(filial),ALLTRIM(matricula)})
			(cAlias)->(DbSkip())
		Enddo  
		(cAlias)->(DbCloseArea())						
		

If Empty(aAmat)
	aAdd(aAmat,{"'" + SRA->RA_FILIAL + "'", "'" + SRA->RA_MAT + "'"})
EndIf

RestArea(aArea)

Return(aAmat)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³fCalAntig	³ Autor ³ Alceu Pereira 		³ Data ³ 21/08/08   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Tem como objetivo calcular a antiguidade dos funcionarios	    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fCalAntig(aMat)	            								³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Esta funcao recebe os parametros:                             ³±±
±±³          |01-(aMat) Array contendo filiais e matriculas que serao utili-³±±
±±³          |   zadas como chave para localizar as datas de contratacao    ³±±
±±³			 |02-(nMeses) Numero de meses (fracao) que sera considerado como³±±
±±³			 |   1 Ano para divisao final                                   ³±± 
±±³ 		 |03-(dDtDemiss) Data de Demissao do funcionario                ³±± 
±±³ 		 |04-(lRetMes) Se estiver com .T. indica que o retorno deve ser ³±± 
±±³ 		 |   um array com a quantidade de meses e anos apurados         ³±± 
±±³ 		 |05-(lTruncMes) .T. = trunca as decimais da variavel nTotTempo ³±± 
±±³ 		 |                     antes de retornar a quantidade de Meses  ³±± 
±±³ 		 |               .F. = se tiver decimais na variavel nTotTempo, ³±± 
±±³ 		 |                     acrescenta 1 antes de retornar           ³±± 
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Localizado Argentina									    	³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function fCalAntig(aMat,nMeses,dDtDemiss,lRetMes,lTruncMes)

Local nTempo    	:= 0
Local nTotTempo 	:= 0 
Local nTotalAnos	:= 0
Local nContMat		:= 1  
Local aArea			:= GetArea()
Local cAlias		:= ""  
Local aRet			:= {}

Default dDtDemiss	:= dDataBase 
Default nMeses      := 12
Default lRetMes		:= .F.
Default lTruncMes	:= .F.

cAlias := "Q1"  

For nContMat = 1 to Len(aMat)
	
	cQuery := " SELECT "
	cQuery += " RA_ADMISSA , "
	cQuery += " RA_DEMISSA "
	If cPaisLoc =="ARG"
		cQuery += ", RA_MESTRAB "
	EndIf	
	cQuery += " FROM "+	RetSqlName("SRA") + " SRA "
	cQuery += " WHERE "
	cQuery += "	RA_FILIAL = " + ALLTRIM(aMat[nContMat,1]) + " AND "
	cQuery += " RA_MAT = " + ALLTRIM(aMat[nContMat,2]) + " AND "
	cQuery += " D_E_L_E_T_ <> '*' "
	
	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)
	
	//#nTempo := DateDiffMonth( STOD(RA_ADMISSA), if(!Empty(RA_DEMISSA),STOD(RA_DEMISSA), dDtDemiss))	//# Retorna Meses
	nTempo := DateDiffDay( STOD(RA_ADMISSA), Iif(!Empty(RA_DEMISSA),STOD(RA_DEMISSA),dDtDemiss) )		//# Retorna Dias
	                                   
	If cPaisLoc == "ARG"   
		nTempo += (Val(RA_MESTRAB)*30)
	EndIf
	nTotTempo := (nTotTempo + nTempo)
	
	(cAlias)->(DbCloseArea())
	
Next nContMat
             
If cPaisLoc == "ARG"   
	
	nTotalAnos := (nTotTempo / 365)
Else
	nTotTempo  := (nTotTempo / 30)
	nTotalAnos := (nTotTempo / 12)
EndIf	

//# Acrescenta 1 nos Meses
If (nTotTempo - INT(nTotTempo)) > 0
	If !lTruncMes
		nTotTempo += 1
	EndIf
EndIf

//# Acrescenta 1 nos Anos
If ( ((nTotTempo/12) - INT(nTotTempo/12)) * 12 ) >= nMeses
	nTotalAnos += 1
EndIf

//# Trunca os Meses e/ou Anos
nTotTempo  := INT(nTotTempo)
nTotalAnos := INT(nTotalAnos)

If lRetMes
	aAdd(aRet,nTotTempo)	//# Meses
	aAdd(aRet,nTotalAnos)	//# Anos
	Return(aRet)		
EndIf
                                                  
RestArea(aArea)
		
Return(nTotalAnos)	//# Anos


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³fRetMatAdm³ Autor ³ Silvia Taguti 		³ Data ³ 21/08/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Selecionar Matriculas na tabela SRA, tendo como chave os 	  ³±±
±±³			 |um array com as matriculas.								  |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ fRetMatAdm(cCodEnt)										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Esta funcao recebe o codigo da entidade que e formado		  ³±±
±±³          |pela concatenacao da filial+matricula.					  ³±±	
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Localizado Argentina										  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fRetMatAdm(cCodCic,cFilEnt,cMatEnt,lMesAdm)

Local aAmat := {}  //Array que contem as matriculas 
Local cOldAlias := Alias()
Local aArea		:= SRA->( GetArea() )                         

DEFAULT lMesAdm := .F.

dbSelectArea("SRA")
SRA->(DbSetOrder( RetOrder( "SRA", "RA_CIC+RA_FILIAL+RA_MAT" ) ) )                       
SRA->(dbSeek( cCodCic + cFilEnt ))
				
While SRA->( !Eof() .and. (( RA_CIC + RA_FILIAL ) == ( cCodCic + cFilEnt )))
    If (SRA->( RA_CIC + RA_FILIAL ) == ( cCodCic + cFilEnt))
		aAdd(aAmat,{"'" + SRA->RA_FILIAL + "'","'" + SRA->RA_MAT + "'"})
		SRA->(dbSkip())
	EndIf
EndDo             

If lMesAdm
	aAdd(aAmat,{"'" + xFilial("SRA") + "'","'" + M->RA_MAT + "'"})
EndIf	
             
RestArea( aArea )
DbSelectArea( cOldAlias )	

Return(aAmat)
