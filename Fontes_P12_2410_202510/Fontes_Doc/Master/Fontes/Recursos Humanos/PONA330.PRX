#INCLUDE "PROTHEUS.CH"
#INCLUDE "SIGAWIN.CH"
#INCLUDE "PONA330.CH"
#INCLUDE "JPEG.CH"

Static l330Valid	:=	ExistBlock("PN330VALID")
/*/


Ŀ
Funo     PONA330   Autor  Mauricio MR            Data  22/11/03 
Ĵ
Descrio  Cadastramento de Visitantes                                
Ĵ
Sintaxe    PONA330()                                                  
Ĵ
 Uso       Generico                                                   
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
Cecilia C.  20/05/14TPQAN3Incluido o fonte da 11 para a 12 e efetu- 
                          ada a limpeza.                            
Luis Artuso 30/03/15TRTLPIAjuste para exibicao da foto na GetDados  
                          no momento da captura.                    
Renan Borges|02/04/15|TRDIZ9|Ajuste na resoluo de captura da foto.   
ٱ

*/

Function PONA330( cRotina, bBlock, nOper, aAuto, nOpc )

//Ŀ
// Define Variaveis para Chamada EXTERNA de Consulta Padrao SXB 
//
Local aSavARot := If(Type("aRotina")!="U",aRotina,{})
Local cSavcCad := If(Type("cCadastro")!="U",cCadastro,"")
Local nSavN    := If(Type("N")!="U",N,Nil)
Local nScan    := 0                

//Ŀ
// Define Variaveis                                             
//
LOCAL cFiltra					//	Variavel para filtro
LOCAL aIndex		:= {}	 	//	Variavel para filtro

Private lVisitante	:= .F.
Private lPona330Aut := ( aAuto <> NIL )
Private aRotAuto    := aClone(aAuto)
PRIVATE aAcho		 := {}

Begin Sequence

   SetRepName("IMGVIST")    
   
//   PackRepository()  

	/*
	Ŀ
	So Executa se o Modo de Acesso dos Arquivos do Ponto estiverem OK		 
	*/
	IF !( ValidArqPon() )
		Break
	EndIF
	
	DEFAULT nOper  		:= 0 
	
	//Ŀ
	// Define Array contendo as Rotinas a executar do programa      
	// ----------- Elementos contidos por dimensao ------------     
	// 1. Nome a aparecer no cabecalho                              
	// 2. Nome da Rotina associada                                  
	// 3. Usado pela rotina                                         
	// 4. Tipo de Transao a ser efetuada                          
	//    1 - Pesquisa e Posiciona em um Banco de Dados             
	//    2 - Simplesmente Mostra os Campos                         
	//    3 - Inclui registros no Bancos de Dados                   
	//    4 - Altera o registro corrente                            
	//    5 - Remove o registro corrente do Banco de Dados          
	//   
	PRIVATE aAlter
	
	PRIVATE aRotina :=  MenuDef() // ajuste para versao 9.12 - chamada da funcao MenuDef() que contem aRotina
	
	If ExistBlock( "P330ROTI")
	   	aRotina := ExecBlock( "P330ROTI" ,.F.,.F.,{aRotina}, .F. )
	EndIf

	//Ŀ
	// Define o cabecalho da tela de atualizacoes                   
	//
	Private cCadastro := OemToAnsi( STR0009 )  //"Cadastro de Visitantes"
	Private bFiltraBrw 	:= {|| Nil}				//Variavel para Filtro	
	
	//Ŀ
	// Inicializa o filtro utilizando a funcao FilBrowse                      
	//
	cFiltra 	:= CHKRH("PONA330","SPW","1")
	bFiltraBrw 	:= {|| FilBrowse("SPW",@aIndex,@cFiltra) }
	Eval(bFiltraBrw)
	
	If ValType( cRotina ) == "C"
		//Ŀ
		// Faz tratamento para chamada por outra rotina             
		//
		If !Empty( nScan := AScan( aRotina, { |x| Upper( x[2] ) == Upper( cRotina ) .And. x[4] == nOper } ) ) 
			cRoda := cRotina + "( 'SPW', SPW->( Recno() ), " + Str(nScan,2) + " )" 
			xRet  := Eval( { || &( cRoda ) } ) 
		EndIf 
	Else  
	
		//Ŀ
		// Endereca a funcao de BROWSE                                  
		//
		dbSelectArea( "SPW" )
		dbGoTop()
		
		If lPona330Aut
			MsRotAuto(nOpc,aRotAuto,"SPW")
		Else
			mBrowse( 6, 1,22,75,"SPW",,,,,,fVisitCor() ) 
		EndIf

		
	
		//Ŀ
		// Deleta o filtro utilizando a funcao FilBrowse                     	   
		//
		EndFilBrw("SPW",aIndex)
	
	Endif
	
	//Ŀ
	//Restaura os dados de entrada para Chamada por SXB             
	//
	aRotina   := aSavARot
	cCadastro := cSavcCad                    
	
	If ValType( nSavN ) == "N"
		N := nSavN 
	EndIf 	

End Sequence


//Ŀ
// Libera Locks						                         
//
If lVisitante
   FreeLocks( 'SPW', Nil, .T. ) 
Endif

RetPadEpoch()
FechaReposit()
SetRepName("SIGAADV")

Return

/*


Ŀ
Funo     Pn330Rot  Autor  Mauricio MR            Data  26.11.03 
Ĵ
Descrio  Rotina para Manutencao de Visitantes			              
Ĵ
Sintaxe    Pn330Rot(ExpC1,ExpN1,ExpN2)                                
Ĵ
Parametros ExpC1 = Alias do arquivo 								  
 		  ExpN1 = Numero do registro								  
 	 	  ExpN2 = Numero da opcao Selecionada						  
Ĵ
Uso		  Pn330Rot  												  
ٱ

                                                                             */
Function Pn330Rot(cAlias,nReg,nOpc)

Local aBotoes     	:= {}              
Local aAcessos		:= {}
Local aAdvSize		:= {} 
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aSPWHeader	:= {}
Local aSPWCols		:= {}
Local aSPWFields	:= {}
Local aSPWAltera	:= {}
Local aSPWNaoAlt	:= {}
Local aSPWVirtEn	:= {}
Local aSPWNotFields	:= {}
Local aSPWRecnos	:= {}
Local aSPWVisuEn	:= {}
Local aSPWMemoEn	:= {}

Local aSvKeys		:= GetKeys()
Local aSYPRecnos	:= {}

Local bSet15		:= { || NIL }
Local bSet24		:= { || NIL }

Local cCadastro 	:= OemToAnsi( STR0009 )  //"Cadastro de Visitantes"
Local cSPWKeySeek	:= ""
Local cFilSPW		:= ""
Local cVisSPW		:= ""
Local lLocks		:= .F.
Local nOpcAlt		:= 0.00
Local nSPWUsado		:= 0.00
Local nLoop			:= 0.00
Local nLoops		:= 0.00
Local oDlg			:= NIL
Local oEnchoice		:= NIL	                                  
Local bGrava
Local cNomeFull		:= ""

Private aFotos		:= {}		//Variavel para Fotos Capturadas
Private lGravouFoto	:= .F.     //Variavel Se Gravou a Foto
Private bGetFoto	:= {|| lGravouFoto:= Pn330GetFoto(Alltrim(cEmpAnt+xFilial("SPW")+M->PW_VISITA), @aFotos, @oEnchoice),SetKey( VK_F4,bGetFoto) }

Private nOpx		:= nOpc
Private nRegx		:= nReg
Private nSaveSX8   	:= GetSX8Len() 
                          
Begin Sequence

	/*
	Ŀ
	 Verifica a existencia de variavel de controle de rotina auto-
	 matica se a chamada da funcao Pn330Rot() nao for realizada   
	 via a funcao PONA330() (Este programa).                      
	*/
    IF !IsMemVar("lPona330Aut") 
       	SetMemVar( "lPona330Aut" , .F.	, .T. )
    Endif                                                                  
	
	/*
	Ŀ
	 Quando For Inclusao Posiciona o SPW No Final do Arquivo	   
	*/
	IF ( nOpc == 3  ) //Inclusao
		/*
		Ŀ
		 Garante que na Inclusao o Ponteiro do SPW estara em Eof()    
		*/
		PutFileInEof( "SPW" , @nReg )  
        
	Endif	

	/*
	Ŀ
	Bloqueia Chaves Logicas de Marcacoes do Funcionario           
	Deixamos de Realizar o bloqueio pois o bloqueio sera feito    
	pela numeracao automatica									   
	*/
	//If !(lVisitante:= Pona330Locks( nOpX , 'SPW', {} )) 
	//    Break
    //Endif

	
	/*
	Ŀ
	 Monta os Dados para a Enchoice							   
	*/
	
	aSPWNotFields	:= { "PW_FILIAL","PW_NOMFULL","PW_DCARGO"}
   

	aSPWCols		:= SPW->(GdMontaCols(@aSPWHeader,@nSPWUsado,@aSPWVirtEn,@aSPWVisuEn,NIL,aSPWNotFields,@aSPWRecnos, NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,.F.))
	cFilSPW			:= SPW->PW_FILIAL
	cVisSPW			:= SPW->PW_VISITA

	/*
	Ŀ
	 Cria as Variaveis de Memoria e Carrega os Dados Conforme o ar
	 quivo														   
	*/
	If ! lPona330Aut
		For nLoop := 1 To nSPWUsado
			aAdd( aSPWFields , aSPWHeader[ nLoop , 02 ] )
			Private &( "M->"+aSPWHeader[ nLoop , 02 ] ) := aSPWCols[ 01 , nLoop ]
		Next nLoop
 	EndIf
    
	/*
	Ŀ
	Define os Campos Editaveis na Enchoice.                       
	                   										   
	*/
	IF ( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
			
		/*
		Ŀ
		 Define os Campos Editaveis								   
		*/
		nLoops := Len( aSPWVisuEn )
		
		For nLoop := 1 To nLoops
			aAdd( aSPWNaoAlt , aSPWVisuEn[ nLoop ] )
		Next nLoop
		
		IF ( nOpc == 4 ) 
			aAdd( aSPWNaoAlt , "PW_VISITA" )
		EndIF
		
		nLoops := Len( aSPWFields )
		For nLoop := 1 To nLoops
			IF ( aScan( aSPWNaoAlt , { |cNaoA| cNaoA == aSPWFields[ nLoop ] } ) == 0.00 )
				aAdd( aSPWAltera , aSPWFields[ nLoop ] )
			EndIF
		Next nLoop
	EndIF
	

	/*
	Ŀ
	 Monta as Dimensoes dos Objetos         					   
	*/
	aAdvSize		:= MsAdvSize()
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }

	aAdd( aObjCoords , { 000 , 300 , .T. , .T. } ) 

	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )

	/*
	Ŀ
	 Define o Bloco para a Tecla <CTRL-O> 						   
	*/

     bSet15		:= { || IF(; 
								( ( nOpc == 3 ) .or. ( nOpc == 4 )) .and. ; 
								(RstEnchoVlds(),.T.) .and. ;
								Iif ( l330Valid , ExecBlock("PN330VALID" , .F. , .F.) , .T. ) .AND. ;
								Obrigatorio( oEnchoice:aGets , oEnchoice:aTela ) 	.and.;	//Verifica os Campos Obrigatoris na Enchoice
							    PN330EncTOk(@cNomeFull,Nil, Nil, oEnchoice)					.and.; 
								EnchoTudOk( oEnchoice )				 				.and.;	//Valida Todos os Campos da Enchoice
							    Pn330Homonimo(cNomeFull)										; 
							    ,;
								(;
									nOpcAlt := 1.00 ,;
									oDlg:End();
							 	),;
							 	IF(; 
							 		( ( nOpc == 3 ) .or. ( nOpc == 4 ) ) ,;				//Inclusao ou Visualizacao
							 			(;
							 				nOpcAlt := 0.00 ,;
							 				.F.;
							 			 ),;	
									(;
										nOpcAlt := IF( nOpc == 2 , 0 , 1.00 ) ,;		//Visualizacao ou Exclusao
										oDlg:End();
							 		);
							 	  );
						   );
					 }
	/*
	Ŀ
	 Define o Bloco para a Teclas <CTRL-X>     	   			   
	*/
	bSet24		:= { || ( nOpcAlt := 0.00 , oDlg:End() ) }

	/*
	Ŀ
	 Define o Bloco de Definicao de Botoes 	   	   			   
	*/
	If   ( ( nOpc == 3 ) .or. ( nOpc == 4 )  )	
	
			aAdd(; 
					aBotoes	,;
									{;
										"MAQFOTO"			 ,;	
			   							bGetFoto 			 ,;
			       	   					OemToAnsi( STR0015 ) ,;	//"Obtem Foto <F4>..."
			       	   					OemToAnsi( STR0115 ) ;	//"Foto"
			           				};
			    )	    

		 SetKEY(VK_F4,bGetFoto)
	Endif	   
	If ! lPona330Aut
		/*
		Ŀ
		 Monta o Dialogo Principal para a Manutencao             	   
		*/
		DEFINE MSDIALOG oDlg TITLE OemToAnsi( STR0009 ) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL
			/*
			Ŀ
			 Monta o Objeto Enchoice para o SPW                      	   
			*/
			oEnchoice	:= MsmGet():New(	"SPW"		,;
											nReg		,;
											nOpc		,;
											NIL			,;
											NIL			,;
											NIL			,;
											aSPWFields	,;
											aObjSize[1] ,;
											aSPWAltera	,;
											NIL			,;
											NIL			,;
											"PN330EncTOk",;
											oDlg		,;
											NIL			,;
											.F.			 ;
										)
			oEnchoice:oBox:Align := CONTROL_ALIGN_ALLCLIENT
	
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 ,, aBotoes ) CENTERED	
	//	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 ) CENTERED	
	Else            
		If  PN330EncTOk() 
		   nOpcAlt := 1
		Else
		   nOpcAlt := 0
   		AutoGRLog("Inconsistencia em PN330EncTOk()")
   		lMsErroAuto:= .t.
	   EndIf
	EndIf
	/*
	Ŀ
	 Restaura as Teclas de Atalho                				   
	*/
	RestKeys( aSvKeys, .T.  )
	          
	/*
	Ŀ
	Quando Confirmada a Opcao e Nao for Visualizacao Grava ou   Ex
 	clui as Informacoes do SPW      							   
	*/
	IF( nOpcAlt == 1 )
		/*
		Ŀ
		 Apenas se nao For Visualizacao              				   
		*/
 		IF ( nOpc != 2 ) .OR. ( nOpc != 5 )
 		

			/*
			Ŀ
			 Gravando/Incluido ou Excluindo Informacoes                   
			*/
			
			bGrava := { ||;
					             Pn330Grava(		nOpc		,;	//Opcao de Acordo com aRotina
												 	nReg		,;	//Numero do Registro do Arquivo Pai ( SPW )
												 	aSPWHeader	,;	//Campos do Arquivo Pai ( SPW )
												 	aSPWVirtEn	,;	//Campos Virtuais do Arquivo Pai ( SPW )												 
												 	lGravouFoto ;   //Se Capturou a Foto
										   );
						}
			If ! lPona330Aut
				MsAguarde(bGrava)
			Else 
			   Eval(bGrava)
			EndIf	
						

		EndIF
	ElseIF ( nOpc == 3 )
		/*
		Ŀ
		 RollBack da Numeracao Automatica            				   
		*/
		While (GetSx8Len() > nSaveSx8)
			RollBackSx8()
		EndDo              
		
	EndIF

End Sequence


//-- Apaga BMP Foto do Diretorio
nLoops	:=	Len(aFotos)

For nLoop:=1 to nLoops

	IF File(aFotos[nLoop, 1])
		// Apaga o arquivo da mquina CLIENTE
		FERASE( aFotos[nLoop, 1])
	Endif

	IF File(aFotos[nLoop, 2])
		// Apaga o arquivo da mquina SERVIDOR
		FERASE( aFotos[nLoop, 2])
	Endif	

Next nX

/*
Ŀ
Libera os Locks             								   
*/
PonFreeLocks(Nil,Nil,.T.)
/*
Ŀ
Libera as Chaves Exclusivas 								   
*/
FreeUsedCode()

Return( nOpcAlt )


/*
Ŀ
Funo    Pn330Grava   AutorMauricio MR            Data 27/11/2003
Ĵ
Descrio                                                             
Ĵ
Sintaxe   <Vide Parametros Formais>									
Ĵ
Parametros<Vide Parametros Formais>									
Ĵ
Uso       Pona330()	                                                
*/

Static Function Pn330Grava(		nOpc		,;	//Opcao de Acordo com aRotina
							 	nReg		,;	//Numero do Registro do Arquivo Pai ( SPW )
							 	aSPWHeader	,;	//Campos do Arquivo Pai ( SPW )
							 	aSPWVirtEn	,;	//Campos Virtuais do Arquivo Pai ( SPW )
						 		lGravouFoto  ;  //Se Capturou a Foto
							  )


Local lLock			:= .F.
Local lRet			:= .T.
Local nCpoMemo		:= 0.00
Local nChoice		:= 0.00
Local nChoices		:= 0.00

DEFAULT nOpc		:= 0.00
DEFAULT nReg		:= 0.00
DEFAULT aSPWHeader	:= {}
DEFAULT aSPWVirtEn	:= {}

nChoices			:= Len(	aSPWHeader	)

/*
Ŀ
 Se for Inclusao/Alteracao ( nOpc == 3 .or. nOpc == 4 )	   
*/
IF ( nOpc == 3 .or. nOpc == 4 )
	Begin Transaction
		IF !Empty( nReg )
			SPW->( MsGoto( nReg ) )
			lLock := RecLock( "SPW" , .F. , .F. )
		Else
			lLock := RecLock( "SPW" , .T. , .F. )
		EndIF 
        
		IF !( lLock )
			Break
		EndIF
        SPW->PW_FILIAL := xFilial('SPW')              	
	  
		//-- Inclui a Foto no Repositorio 	
        If lGravouFoto
           //-- Atualiza identifcador do BMP  
           M->PW_BITMAP:=Alltrim(cEmpAnt+xFilial('SPW')+M->PW_VISITA)
		Endif
		
		For nChoice := 1 To nChoices
			IF ( aScan( aSPWVirtEn , { |cCpo| ( cCpo == aSPWHeader[ nChoice , 02 ] ) } ) == 0.00 )
				
				SPW->( &( aSPWHeader[ nChoice , 02 ] ) ) :=&( "M->"+aSPWHeader[ nChoice , 02 ] ) 
            Endif
		Next nChoice

		
		//-- Monta Campo Nome Completo
	    SPW->PW_NOMFULL:=  SPW->(ALLTRIM(PW_NOME)+' '+ALLTRIM(PW_POSNOM1)+' '+ALLTRIM(PW_POSNOM2)+' '+ALLTRIM(PW_POSNOM3)+' '+ALLTRIM(PW_POSNOM4))
		
		SPW->( MsUnLock() )
		/*
		Ŀ
		 Confirmando a Numeracao Automatica          				   
		*/
		IF ( nOpc == 3 .and. ( __lSX8 ) )
		  	ConfirmSX8()
		EndIF
	End Transaction
EndIF

Return( NIL )


/*/

Ŀ
Funo     Pn330Dele Autor  Mauricio MR            Data  09/02/04 
Ĵ
Descrio  Programa de exclusao de Visitantes                         
Ĵ
Sintaxe    Pn330dele(ExpC1,ExpN1,ExpN2)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao selecionada                        
Ĵ
 Uso       Pn330Dele                                                  
ٱ

*/
Function Pn330dele(cAlias,nReg,nOpc,aAcho)

Local nOpcA,j,lRet:=.T.,cNivant
Local aAC       := { STR0003,STR0001 }  //"Abandona"###"Confirma"
Local aAux		:= {}
Local lAchou    := .F.
Local aArquivos := {}
Local nElem     := 0
Local cArquivo  := SPACE(06)
Local cNome     := SPACE(30)
Local nX
Local cMsgErr 

Local aAdvSize				:= {}
Local aInfoAdvSize			:= {}
Local aObjSize				:= {}
Local aObjCoords			:= {}

Local aPosEnch		:={}  
Default aAcho	:={}

//Ŀ
// Monta a entrada de dados do arquivo                          
//
Private aTELA[0][0],aGETS[0]

Aeval(aAcho,{|x| IF(x $'PW_NOMFULL.PW_DCARGO',Nil,AADD(aAux, x) )}) 
aAcho:=aClone(aAux)


nOpcA:=0
dbSelectArea( cAlias )
dbSetOrder( 1 )

//Ŀ
// Verifica se existe movimentacao nos arquivos                 
//
aArquivos:={{"SPY",1}}                                              

If ! lPona330Aut
	/*/
	Ŀ
	 Monta as Dimensoes dos Objetos         					   
	/*/
	aAdvSize		:= MsAdvSize()
	aAdvSize[5]:=(aAdvSize[5]/100)*100
	aAdvSize[6]:= (aAdvSize[6]/100)*100
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }
	aAdd( aObjCoords , { 015 , 020 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )
	aObjSize[1,2] := 5
	
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL
		
		aPosEnch := {aObjSize[1,1] + 14, aObjSize[1,2], aObjSize[1,4]*0.46, aObjSize[1,4]*0.995}  // ocupa todo o  espao da janela
		
		If len(aAcho) == 0   
			nOpcA:=EnChoice( cAlias, nReg, nOpc, aAC,"AC",STR0010,,aPosEnch )  //"Quanto  excluso?"
		Else
			nOpcA:=EnChoice( cAlias, nReg, nOpc, aAC,"AC",STR0010,aAcho,aPosEnch )  //"Quanto  excluso?"
		EndIf	
		
		nOpca := 1
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()}) CENTERED
	
	dbSelectArea( cAlias )
	dbSetOrder( 1 )
	
Else
	nOpcA := 2
EndIf

nElem := Len( aArquivos )
For nX := 1 to nElem
	cArquivo := ALLTRIM( RetSqlName(aArquivos[nX,1]) )
	cNome    := ALLTRIM( FWX2Nome(aArquivos[nX,1]) )
	dbSelectArea( aArquivos[nX,1] )
	dbSetOrder( aArquivos[nX,2] )
	If dbSeek( SPW->PW_FILIAL + SPW->PW_VISITA )
		lAchou := .T.
		Exit
	Endif
Next 

// Se nao Achou pode Deletar
Begin Transaction
	
	If nOpcA == 2
		If !lAchou
			dbSelectArea( cAlias )
			IF RecLock(cAlias,.F.,.T.)
				IF !( cAlias )->( FkDelete( @cMsgErr ) )
					( cAlias )->( RollBackDelTran( cMsgErr ) )
				EndIF
				( cAlias )->( MsUnlock() )
				WRITESX2(cAlias,1)
			EndIF
			dbSkip()
		Else
			HELP(" ",1,"EXISTVIS",,cArquivo+" : "+cNome,5,2)
		Endif
	EndIF
End Transaction

Return( NIL )


/*

Ŀ
 Funo    PnLegend  Autor  Maurico MR	         Data 22/11/2003
Ĵ
 Descrio Cria uma janela contendo a legenda da mBrowse              
Ĵ
 Uso       PnLegend                                                   
ٱ

*/
Function Pnlegend()

IF Type( "cCadastro" ) == "U"
	Private cCadastro := STR0011							//"Legenda"
EndIF

BrwLegenda(	OemToAnsi(cCadastro)						,;	//Titulo do Cadastro
			OemToAnsi( STR0012 )						,;	//"Legenda"
			{;
				{"ENABLE"		,OemToAnsi(STR0013)	}	,;	//"Situao Normal"
				{"BR_VERMELHO"	,OemToAnsi(STR0014)	}	;	//"Com Restricao"
			};
		 )

Return( .T. )


/*/


Ŀ
Funcao     fVisitCor Autor  Mauricio MR            Data 02.12.2003
Ĵ
Descrio  Funcao p/ definir cores p/ as situacoes dos visitantes.    
Ĵ
Retorno   aCores                                                      
Ĵ
Parametros															  
ٱ

/*/
Function fVisitCor()

Local aCores	:=	{	                                    	 	 ;
					{ "SPW->PW_SITVIST=='1'" , 'BR_VERDE'		}	,;
             		{ "SPW->PW_SITVIST=='2'" , 'BR_VERMELHO'	}	;
				    }

Return(aCores)

/*/


Ŀ
Funcao    Pn330RUltVist   Autor  Mauricio MR            Data 02.12.2003
Ĵ
Descrio  Funcao p/ Gerar o Conteudo Padrao da Ultima Visita.		       
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/
Function Pn330RUltVist(cVisita)
	
	Local dtVisita	:= Ctod('')             
	Local cFilSPY	:= xFilial('SPY')
	Local cChave	:= ''
	Local nRecSPY	:= 0.00 
	
	DEFAULT cVisita:= If(TYPE('M->PW_VISITA') <> 'U', M->PW_VISITA, SPW->PW_VISITA)
	cChave := cFilSPY + cVisita
	
	If SPY->(dbSeek(cChave))
		While SPY->(!EOF() .AND. (cFilSPY + PY_VISITA) == (cChave) .AND. PY_DTVISIT <= dDataBase )
			nRecSPY := SPY->(Recno())
			SPY->(dbSkip())
		Enddo 
	
		If !EMPTY(nRecSPY)
			SPY->(DbGoto(nRecSPY))
			dtVisita := SPY->PY_DTVISIT
		Endif
	Endif
	
Return(dtVisita)


/*/


Ŀ
Funcao    PN330EncTOk	  Autor  Mauricio MR            Data 02.12.2003
Ĵ
Descrio  Funcao p/ Validacao de Enchoice							       
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/
Function PN330EncTOk(cNomeFull)
Local lRet:= .T. 
Local aCpoUsado	:= {}
Local nX	:= 0
Local nTam	:= 0
Local cNomeTemp	:= ""

AADD(aCpoUsado , 'PW_POSNOM2')            
AADD(aCpoUsado , 'PW_POSNOM3')            
AADD(aCpoUsado , 'PW_POSNOM4')

nTam	:=	LEN(aCpoUsado)

Begin Sequence
    If cPaisLoc == 'BRA'
		If Pn330ExistCpo("M->PW_CPF")     .AND. Pn330ExistCpo("M->PW_PIS")     .AND. Pn330ExistCpo("M->PW_RG")      .AND.;
		   Pn330ExistCpo("M->PW_NUMCP")   .AND. Pn330ExistCpo("M->PW_HABILIT") .AND. Pn330ExistCpo("M->PW_RESERVI") .AND.;
		   Pn330ExistCpo("M->PW_TITULOE") .AND. Pn330ExistCpo("M->PW_OUTRDOC")
		   Help(" ",1,"PN330NOID") //"Informe Algum Documento de Identificacao" 
		   lRet:= .F.
		   break
		Endif 
		If !Pn330VRESERVI(Pn330ValidCpo("M->PW_RESERVI")) .OR. !Pn330VOUTRDOC(Pn330ValidCpo("M->PW_OUTRDOC")) .OR. !Pn330VHABILIT(Pn330ValidCpo("M->PW_HABILIT")) .OR.;
		   !Pn330VTITULOE(Pn330ValidCpo("M->PW_TITULOE")) .OR. !Pn330VNUMCP(Pn330ValidCpo("M->PW_NUMCP"))     .OR. !Pn330VPIS(Pn330ValidCpo("M->PW_PIS"))         .OR.;
		       !Pn330VCPF(Pn330ValidCpo("M->PW_CPF"))  
		       lRet:= .F.
		   break
		Endif 
	Endif
End

//-- Monta Campo Nome Completo
If lRet 

	cNomeFull	:= ALLTRIM(M->PW_NOME)+' '+ALLTRIM(M->PW_POSNOM1)+' '	  
	For nX := 1 TO nTam
		If ( X3USO( GetSx3Cache( aCpoUsado[nX], "X3_USADO" )) )
			cNomeTemp	:= "M->" + ALLTRIM(aCpoUsado[nX])
			cNomeFull	+= ALLTRIM(&cNomeTemp) + ' '
		EndIf
	Next
	
	M->PW_NOMFULL:= cNomeFull
Endif

Return(lRet)

/*/


Ŀ
Funcao    Pn330VCPF		  Autor  Mauricio MR            Data 02.12.2003
Ĵ
Descrio  Funcao p/ Validar CPF.									       
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/
Function Pn330VCPF(cCPF)
Local aArea		:= GetArea()
Local aSPWArea	:= SPW->(GetArea())
Local cFilSPW	:= xFilial('SPW')
Local lRet		:= .T.             
If ( cPaisLoc == "BRA" )

	If VALTYPE("cCPF") == 'N'
	   cCPF:=Alltrim(STR(cCPF))
	Endif
	
	If ! Pn330VCampos(cCPF,4,cFilSPW+cCPF,&('{||!ChkCPF("'+cCPF+'") }'))
	   lRet:= .F.
	Endif

ElseIf ( cPaisLoc == "MEX" )
	ChkRfc(cCPF)
EndIf

RestArea(aSPWArea) 
RestArea(aArea)         

Return(lRet)   

/*/


Ŀ
Funcao    Pn330VPIS		  Autor  Mauricio MR            Data 02.12.2003
Ĵ
Descrio  Funcao p/ Validar PIS.									       
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/

Function Pn330VPIS(cPIS)
Local aArea		:= GetArea()
Local aSPWArea	:= SPW->(GetArea())
Local cFilSPW	:= xFilial('SPW')
Local lRet		:= .T.

If VALTYPE("cPIS") == 'N'
   cPis:=Alltrim(STR(cPis))
Endif

If ! Pn330VCampos(cPIS,5,cFilSPW+cPIS,&('{||!ChkPIS("'+cPIS+'") }'))
   lRet:= .F.
Endif

RestArea(aSPWArea) 
RestArea(aArea)         
Return(lRet)            

/*/


Ŀ
Funcao    Pn330VRG		  Autor  Mauricio MR            Data 02.12.2003
Ĵ
Descrio  Funcao p/ Validar RG.									       
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/
Function Pn330VRG(cRG)

Local aArea		:= GetArea()
Local aSPWArea	:= SPW->(GetArea())
Local cFilSPW	:= xFilial('SPW')
Local lRet		:= .T.

If ! Pn330VCampos(cRG,3,cFilSPW+cRG)
   lRet:= .F.
Endif

RestArea(aSPWArea) 
RestArea(aArea)         
Return(lRet)    


/*/


Ŀ
Funcao    Pn330VNUMCP	  Autor  Mauricio MR            Data 02.12.2003
Ĵ
Descrio  Funcao p/ Validar Cart.Prof.								       
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/
Function Pn330VNUMCP(cNUMCP)
Local aArea		:= GetArea()
Local aSPWArea	:= SPW->(GetArea())
Local cFilSPW	:= xFilial('SPW')
Local lRet		:= .T.

If ! Pn330VCampos(cNUMCP,10,cFilSPW+cNUMCP)
   lRet:= .F.
Endif

RestArea(aSPWArea) 
RestArea(aArea)         
Return(lRet) 
         
/*/


Ŀ
Funcao    Pn330VHABILIT	  Autor  Mauricio MR            Data 02.12.2003
Ĵ
Descrio  Funcao p/ Validar Habilitacao.							       
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/
Function Pn330VHABILIT(cHABILIT)

Local aArea		:= GetArea()
Local aSPWArea	:= SPW->(GetArea())
Local cFilSPW	:= xFilial('SPW')
Local lRet		:= .T.


If ! Pn330VCampos(cHABILIT,06,cFilSPW+cHABILIT)
   lRet:= .F.
Endif

RestArea(aSPWArea) 
RestArea(aArea)         
Return(lRet)           


/*/


Ŀ
Funcao    Pn330VRESERVI	  Autor  Mauricio MR            Data 02.12.2003
Ĵ
Descrio  Funcao p/ Validar Reservista.							       
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/
Function Pn330VRESERVI(cRESERVI)

Local aArea		:= GetArea()
Local aSPWArea	:= SPW->(GetArea())
Local cFilSPW	:= xFilial('SPW')
Local lRet		:= .T.

If ! Pn330VCampos(cRESERVI,07,cFilSPW+cRESERVI)
   lRet:= .F.
Endif

RestArea(aSPWArea) 
RestArea(aArea)

Return(lRet)            
/*/


Ŀ
Funcao    Pn330VTITULOE	  Autor  Mauricio MR            Data 02.12.2003
Ĵ
Descrio  Funcao p/ Validar Titulo Eleitoral.						       
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/
Function Pn330VTITULOE(cTITULOE)
Local aArea		:= GetArea()
Local aSPWArea	:= SPW->(GetArea())
Local cFilSPW	:= xFilial('SPW')
Local lRet		:= .T.

If ! Pn330VCampos(cTITULOE,08,cFilSPW+cTITULOE)
   lRet:= .F.
Endif

RestArea(aSPWArea) 
RestArea(aArea)         
Return(lRet)    

/*/


Ŀ
Funcao    Pn330VOUTRDOC	  Autor  Mauricio MR            Data 02.12.2003
Ĵ
Descrio  Funcao p/ Validar Documento Alternativo.					       
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/      
Function Pn330VOUTRDOC(cOUTRDOC)
Local aArea		:= GetArea()
Local aSPWArea	:= SPW->(GetArea())
Local cFilSPW	:= xFilial('SPW')
Local lRet		:= .T.

If ! Pn330VCampos(cOUTRDOC,09,cFilSPW+cOUTRDOC)
   lRet:= .F.
Endif

RestArea(aSPWArea) 
RestArea(aArea)         
Return(lRet)     


/*/


Ŀ
Funcao    Pn330VCampos	  Autor  Mauricio MR            Data 02.12.2003
Ĵ
Descrio  Funcao p/ Validar Campos.								       
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/      
Function Pn330VCampos(cCampo,nOrdem,cKey,bBlocoSai)
Local aArea		:= GetArea()
Local aSPWArea	:= SPW->(GetArea())
Local cFilSPW	:= xFilial('SPW')
Local cChave	:= ''
Local lRet		:= .T.
Local nRecno	:= SPW->(Recno())
    
DEFAULT bBlocoSai:={||.F.}

If !Empty(cCampo)
   SPW->(DbSetOrder(nOrdem))
   
   If ! SPW->(Dbseek(cKey))
      If (Eval(bBlocoSai))
         lRet:=.F.
      Endif    
   Else 
      
      If (SPW->(Recno()) <> nRecno)  
      	 lRet:= .F.  
      	 cHelp:= OemToAnsi( SPW->PW_VISITA + ALLTRIM(SPW->PW_NOMFULL))
       	 Help( ' ' , 1 , "PN330IDDUP" , , OemToAnsi( cHelp ) ,5  , 0 )//"Documento de Identificacao ja registrado para o Visitante: " 
      Endif 	
   Endif
  
Endif

RestArea(aSPWArea) 
RestArea(aArea)         

Return(lRet) 
  

/*/


Ŀ
Funcao    Pn330Homonimo	  Autor  Mauricio MR            Data 02.12.2003
Ĵ
Descrio  Funcao p/ Validar Homonimos.								       
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/   
Static Function Pn330Homonimo(cNomeFull)
Local aArea		:= GetArea()
Local aSPWArea	:= SPW->(GetArea())
Local cFilSPW	:= xFilial('SPW')
Local cChave	:= ''
Local lRet		:= .T.
Local nRecno	:= If(INCLUI,0,SPW->(Recno()))

//-- Verfica Homonimos
SPW->(DBSETORDER(RetOrdem( 'SPW', 'PW_FILIAL+PW_NOMFULL' )))
cChave	:= cFilSPW + cNomeFull//cFilSPW + ALLTRIM(M->PW_NOME) +' '+ ALLTRIM(M->PW_POSNOM1) +' '+ ALLTRIM(M->PW_POSNOM2) + ' ' + ALLTRIM(M->PW_POSNOM3) + ' ' + ALLTRIM(M->PW_POSNOM4)
If SPW->(Dbseek(cChave))
   If ( SPW->(Recno()) <> nRecno)   
      //Outro Visitante Com Mesmo Nome. Confirma Informacoes?"
	  lRet := MsgNoYes( OemToAnsi( STR0016 ) , cCadastro ) 
   Endif
Endif

RestArea(aSPWArea) 
RestArea(aArea)
Return(lRet)


/*/


Ŀ
Funcao    Pn330GetFoto	  Autor  Mauricio MR            Data 06.04.2004
Ĵ
Descrio  Funcao p/ Captura de Foto								       
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/   
function Pn330GetFoto(cIdFile, aFotos, oEnchoice)
                                    
Local lRet		:= .F.

Local cFileJpg	:= '' 
Local cPathSrv	:= ''
Local cDestFile := ''

Local nHDImgDll	:= 0  
Local nRet		:= 0 
Local nx		:= 0  

Private  cPathFile	:= ''                              

cPathSrv	:= Upper(GetTempPath()) //Upper(GetPvProfString( GetEnvServer() , "SourcePath" , "" , GetADV97() ) )
cFileJpg	:=	Alltrim(cIdFile)+'.BMP'
cPathFile	:= UPPER(cPathSrv+cFileJpg)

Begin Sequence
  	
	nHandle := ExecInDLLOpen("imageload2.DLL")  
	If nHandle == -1
	    Alert(STR0122)
	    Return
	EndIf     
	
	// Obtem lista de cameras
	lstDisp := ""
	nRet:=ExeDLLRun2( nHandle, 1, @lstDisp) 
	
	// Define dimeno de captura
	cTam:= "0360|0270"
  	nRet:=ExeDLLRun2( nHandle, 3, @cTam) // Largura|Altura (com 4 caracteres)
	
	// Altera Titulo da janela de captura
	cTitle := OemToAnsi(STR0119)
	nRet:=ExeDLLRun2( nHandle, 4, @cTitle) 
	
  	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0120) FROM 000, 000  TO 450, 450 COLORS 0, 16777215 PIXEL
		cComboBo1 := 1
		lstDisp := alltrim(lstDisp)+alltrim(lstDisp)
		aItems := StrTokArr(lstDisp, "|")
	    @ 015, 004 MSCOMBOBOX oComboBo1 VAR cComboBo1 ITEMS aItems SIZE 131, 010 OF oDlg COLORS 0, 16777215 PIXEL
	    @ 005, 004 SAY oSay1 PROMPT OemToAnsi(STR0121) SIZE 067, 007 OF oDlg COLORS 0, 16777215 PIXEL
	    
	    DEFINE SBUTTON oSButton1 FROM 027, 018 TYPE 14 OF oDlg ENABLE ACTION {|| Captura(oComboBo1:nAt) }
  	    DEFINE SBUTTON oSButton2 FROM 027, 058 TYPE 02 OF oDlg ENABLE ACTION {|| lRet:=.F.,oDlg:End() }
  	    DEFINE SBUTTON oSButton3 FROM 027, 098 TYPE 13 OF oDlg ENABLE ACTION {|| lRet:= fDlgIncFoto(cIdFile),oDlg:End()}
        @ 057, 023 BITMAP oBitmap1 SIZE 350, 250 OF oDlg NOBORDER PIXEL
	ACTIVATE MSDIALOG oDlg CENTERED
	  
    If !lRet
    	Break
    Endif
              
	For nx:= 1 to len(oEnchoice:aEntryCtrls)
		If  oEnchoice:aEntryCtrls[nx]:ClassName() == 'FWIMAGEFIELD'
			oEnchoice:aEntryCtrls[nx]:oImagem:ReLoad(cIdFile)
		Endif
	Next
	
	oEnchoice:Refresh()
    //-- Armazena Fotos para Posterior Eliminacao
    aadd(aFotos,{cPathFile, cDestFile } )
    
    //-- Verifica se Gravou em um dos Locais
    lRet:= ( File(cPathFile) .OR. File(cDestFile) )
    
    If !lRet
	 	cFileJpg	:=	Alltrim(cEmpAnt+xFilial("SPW")+M->PW_VISITA)+'.BMP'
   		cPathFile	:= UPPER(cPathSrv+cFileJpg)
    	lRet:= File(cPathFile)
    Endif	
End 

ExecInDLLClose(nHandle)

Return lRet
            
/*/


Ŀ
Funcao    Captura		  Autor  Gustavo M.             Data 03.04.2014
Ĵ
Descrio  Funcao p/ Captura de Foto								       
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/   
Static Function Captura(nAt)
Local cAt	:= ""

Default nAt = 0

	cAt := cValToChar(nAt-1)

	// Define dispositivo
	nRet := ExeDLLRun2( nHandle, 2, @cAt)
    
   	FERASE(cPathFile)
	// Abre tela de captura e define arquivo de imagem de saida
	tmpImgPadrao := cPathFile
	nRet := ExeDLLRun2( nHandle, 5, @tmpImgPadrao )
	
	// A aplicaao continuara ao fechar a janela de captura
	// agora exibimos a imagem capturada
   
	// Necessario para "troca de imagem"
	oBitmap1:Load(,"ok.png")
  	ProcessMessages()
	
	// Exibe imagem capturada
	oBitmap1:Load(,cPathFile)
	ProcessMessages()

Return

/*


Ŀ
Funo    fDlgIncFoto   Autor  Mauricio MR            Data  06/04/04 
Ĵ
Descrio  Coloca a Foto no Repositorio        						  	 
Ĵ
Sintaxe   fDlgIncFoto(cIdFile)										     
Ĵ
Parametros cIdFile - Identificador da Foto (M->PW_VISITA) 			     
Ĵ
Retorno    lFile - Logico de Retorno de Sucesso da Inclusao da Foto      
Ĵ
 Uso       Pona330                                                       
ٱ

*/
Function fDlgIncFoto(cIdFile)

Local cDirAtu	:= ""

Local lFile		:= .T.
Local oDlg8
Local oBmp

/*
Ŀ
 Inclui a Foto              								   
*/

DEFINE MSDIALOG oDlg8   FROM -1000,400 TO 1600,800  PIXEL 

@ 000, 000 REPOSITORY oBmp SIZE 60, 73 OF oDlg8    

lFile	:= fPutFoto(@oBmp, cIdFile )

ACTIVATE MSDIALOG oDlg8 ON INIT (oBmp:lStretch := .T.,oDlg8:End()) 


Return lFile
/*


Ŀ
Funo    fPutFoto   Autor  Mauricio MR            Data  06/08/04 
Ĵ
Descrio  Insere  a Foto no Repositorio          					  
Ĵ
Sintaxe   fPutFoto(oBmp,cIdFile)									  
Ĵ
Parametros cIdFile 	- Caminho do \RootPath							  
Ĵ
Retorno    lFile - Logico representado que Incluiu a Foto             
Ĵ
 Uso       Pona330                                                    
ٱ

*/
Static Function fPutFoto(oBmp,cIdFile)
Local lFile 	:= .T. 
Local cFileJpg	:= '' 

cFileJpg		:=	Alltrim(cIdFile)+'.BMP'
cPathPict   	:= Upper(GetTempPath()+cFileJpg) 

IF !Empty( cPathPict)
	IF ( lFile	:= File( cPathPict) )
   		oBmp:DeleteBmp (Alltrim(cIdFile))
		oBmp:InsertBmp(cPathPict,,.T.)
		oBmp:LoadBmp("ok.png")
		oBmp:Refresh()
	    oBmp:LoadBmp(cPathPict)
		oBmp:Refresh()
	EndIF
EndIF

M->PW_BITMAP:=Alltrim(cEmpAnt+xFilial('SPW')+M->PW_VISITA)
	
Return (.T.)

/*/


Ŀ
Funcao    Pn330VVis   t   Autor  Mauricio MR            Data 14.02.2004
Ĵ
Descrio  Funcao p/ Validar Codigo do Visitante	   					   
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/
Function Pn330VVis(cVisita)
Local lRet:= .T.

DEFAULT cVisita:= If(TYPE('M->PW_VISITA') <> 'U', M->PW_VISITA,SPW->PW_VISITA)
//-- Retirado controle de cadastro unico de visitante. Controle sera feito pela 
//-- geracao automatica.
//lRet:= ( EXISTCHAV("SPW",M->PW_VISITA) .And. Val(cVisita) > 0 .And. FreeForUse("SPW",cVisita))

lRet:= EXISTCHAV("SPW",M->PW_VISITA) 

Return (lRet)

/*/


Ŀ
Funcao    Pn330RVis   t   Autor  Mauricio MR            Data 14.02.2004
Ĵ
Descrio  Funcao p/ Inicializar Codigo do Visitante	 				   
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/
Function Pn330RVis()
Local xRet:= ''

xRet:=IIF(INCLUI,GetSx8Num("SPW","PW_VISITA"),"")

Return (xRet)


/*/


Ŀ
Funcao    Pn330WVis       Autor  Mauricio MR            Data 14.02.2004
Ĵ
Descrio  Funcao p/ Liberar Foco do Codigo do Visitante	 			   
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/
Function Pn330WVis()
Local lRet:= .F.

lRet:=If(INCLUI,.F.,.T.)

Return (lRet)



/*/


Ŀ
Funcao    Pn330VNasc      Autor  Mauricio MR            Data 14.02.2004
Ĵ
Descrio  Funcao p/ Validar Data de Nacimento							   
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/
Function Pn330VNASC()
Local lRet:= .T.

lRet:= Vazio() .or. ChkDtNa(dDataBase,M->PW_NASC)

Return (lRet)

/*/


Ŀ
Funcao    Pn330ExistCpo   Autor  Leandro Dr             Data 18.09.2008
Ĵ
Descrio  Funcao p/ Validar se campos existem e estao vazios              
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/
Static Function Pn330ExistCpo(cCampo)
Local lRet := .T.

If !( Type("&cCampo") == "U" )
	lRet := Empty(&cCampo)
EndIf

Return(lRet)

/*/


Ŀ
Funcao    Pn330ValidCpo   Autor  Leandro Dr             Data 18.09.2008
Ĵ
Descrio  Funcao p/ Validar conteudo dos campos						   
Ĵ
Retorno    			                                                       
Ĵ
Parametros						   		  								   
ٱ

/*/
Static Function Pn330ValidCpo(cCampo)

If !( Type("&cCampo") == "U" )
	cCampo := &cCampo
Else
	cCampo := ""
EndIf

Return(cCampo)
/*
Ŀ
Funo    Pona330Locks    AutorMauricio MR          Data 14/04/2004
Ĵ
Descrio Bloqueia Visitantes									         
Ĵ
Sintaxe   <Vide parametros formais>									 
Ĵ
Parametros<Vide parametros formais>									 
Ĵ
Retorno   NIL		                                               	     
Ĵ
Observao                                                      	     
Ĵ
Uso       PONA330                                                      
*/
/*
Static Function Pona330Locks( nOpc , cAlias, aRecnos )

Local lLocks	:= .T.
Local aRecAux	:= {}


AEval(aRecnos, {|x| If( !Empty(x),aADD(aRecAux,x), Nil) } )
/*
Ŀ
Se nao For Visualizacao 				 					   
*/
/*
IF ( nOpc <> 2 ) 

	Begin Sequence

		IF !( lLocks := WhileNoLock( cAlias , aRecAux , {xFilial(cAlias)+SPW->PW_VISITA} , 1 , 1 , .T. , NIL ) )
			Break
		EndIF

	End Sequence
	
EndIF

Return( lLocks )

*/

/*
Ŀ
Funo     MenuDef		Autor  Luiz Gustavo      Data 30/11/2006
Ĵ
Descrio Isola opcoes de menu para que as opcoes da rotina possam    
          ser lidas pelas bibliotecas Framework da Versao 9.12 .      
Ĵ
Sintaxe   < Vide Parametros Formais >									
Ĵ
 Uso      PONA330	                                                    
Ĵ
 Retorno  aRotina														
Ĵ
Parametros< Vide Parametros Formais >									
*/

Static Function MenuDef()

 Local aRotina :=     { 		{ STR0004,"PesqBrw"		, 0 , 1, ,.F.}	,; 	//"Pesquisar"   
								{ STR0005,"Pn330Rot"	, 0 , 2}	,; 	//"Visualizar"
								{ STR0006,"Pn330Rot" 	, 0 , 3}	,; 	//"Incluir"
								{ STR0007,"Pn330Rot"	, 0 , 4}	,; 	//"Alterar"
								{ STR0008,"Pn330Dele"	, 0 , 5}	,; 	//"Excluir" 
								{ STR0012,"PnLegend"	, 0 , 5, 7 ,.F.} }	//"Legenda"
								
	Return aRotina
