#INCLUDE "PROTHEUS.CH"
#INCLUDE "TBICONN.CH"
/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programador  ³ Data     ³ FNC            ³  Motivo da Alteracao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ³±±
±±³Tatiane V. M.³25/08/2009³00000020388/2009³Compatibilizacao dos fontes para aumento do³±±
±±³             ³          ³                ³campo filial e gestão corporativa.         ³±±
±±³Tiago Malta  ³04/12/2009³00000020388/2009³Criado a Função fGetSM0 e Alterado o uso   ³±±
±±³             ³          ³                ³ da tabela SM0 para a função fGetSM0.      ³±±
±±³Mauricio T.  ³09/03/2010³00000004630/2010³Criacao da funcao RHTamFilial para retornar³±±
±±³             ³          ³                ³com o Tamanho Maximo da Filial (12)        ³±±
±±³Mauricio T.  ³24/03/2010³00000006562/2010³A Funcao GetFilRecnos de tratar a empresa  ³±±
±±³             ³          ³                ³como Grupo de Empresas como estava anterior³±±
±±³             ³          ³                ³mente, e por isso foi retornado a funcao.  ³±±
±±³Mariana M.   ³22/06/2015³ 000000147582015³Ajuste da função RHTamFilial para retornar ³±±
±±³             ³          ³                ³o tamanho real da filial das tabelas ao    ³±±
±±³             ³          ³                ³executar o GPECONV						    ³±±
±±³Vitor Andrade³29/07/2016³TVKRTS          ³Incluida funcoes AllFilUsr e AllStrFil para³±±
±±³             ³          ³                ³retornar Filiais de acesso de um determina-³±±
±±³             ³          ³                ³usuario (enviado como parametro).          ³±±
±±³Raquel Hager ³13/10/2016³TWCSGE          ³Realizado ajuste na função fValidFil devido³±±
±±³             ³          ³                ³a limitação de tecnologia em não aceitar   ³±±
±±³             ³          ³                ³mais de 2000mb de caracteres de acesso,    ³±±
±±³             ³          ³                ³restringindo assim para a filial logada p/ ³±±
±±³             ³          ³                ³evitar geração de error log.               ³±±
±±³Gabriel A.   ³21/12/2016³MRH-3119        ³Ajuste para que a função fValidFil() possa ³±±
±±³             ³          ³                ³também retornar as filiais que o usuário   ³±±
±±³             ³          ³                ³não tem acesso.                            ³±±
±±³Cícero Alves ³03/05/2017³DRHPAG-641      ³Incluído o ponto de entrada GPEXINSCR na   ³±±
±±³             ³          ³                ³Função fInfo								³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³InRhLibFilExec³Autor ³Marinaldo de Jesus   ³ Data ³14/05/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Executar Funcoes Dentro de RHLIBFIL                          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³InRhLibFilExec( cExecIn , aFormParam )						 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³uRet                                                 	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³Generico 													 ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function InRhLibFilExec( cExecIn , aFormParam )

Local uRet

DEFAULT cExecIn		:= ""
DEFAULT aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= __ExecMacro( cExecIn )
EndIF

Return( uRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³fFilFunc  ³ Autor ³Equipe Advanced RH     ³ Data ³13/03/1998³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Retorna a filial a ser utilizada para Pesquisa/Grava‡„o no  ³
³          ³arquivo desejado, sempre com base na Filial do Funcion rio. ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³ fFilFun(cVar)                                              ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parƒmetros³ cVar = Alias do arquivo cuja filial se deseja Pesq./Gravar ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³ Gen‚rico                                                   ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function fFilFunc( cAlias )

DEFAULT cAlias	:= Alias()

Return( xFilial( Upper( AllTrim( cAlias ) ) , IF( Select( "SRA" ) > 0 , SRA->RA_FILIAL , cFilAnt ) ) )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³GetFilRecnos  	³Autor³Marinaldo de Jesus ³ Data ³30/05/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Carregar Array com o Codigo da Filial e os Recnos 			³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³GetFilRecnos( cEmp , cFilDe , cFilAte )						³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³aFilials                 									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generico   											    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function GetFilRecnos( cEmp , cFilDe , cFilAte )

Local aArea		:= GetArea()
Local aAreaSM0	:= SM0->( GetArea() )
Local aFiliais	:= {}
Local lEqualFil	:= .F.

DEFAULT cEmp	:= cEmpAnt
DEFAULT cFilDe	:= cFilAnt
DEFAULT cFilAte	:= cFilDe

lEqualFil := ( cFilDe == cFilAte )

SM0->( dbSetOrder( 01 ) )
IF SM0->( dbSeek( cEmp + IF( lEqualFil , cFilDe , "" ) , .F. ) )
	While ( SM0->( !Eof() .and. M0_CODIGO == cEmp .and.  IF( lEqualFil , AllTrim(FWGETCODFILIAL) == AllTrim(cFilDe) , .T. ) ) )
	 	IF !( lEqualFil )
	 		IF ( SM0->( FWGETCODFILIAL < cFilDe .or. FWGETCODFILIAL > cFilAte ) )
	 			SM0->( dbSkip() )
	 			Loop
	 		EndIF
	 	EndIF
	 	SM0->( aAdd( aFiliais , { FWGETCODFILIAL , Recno() } ) )
		SM0->( dbSkip() )
	End While
EndIF

RestArea( aAreaSM0 )
RestArea( aArea    )

Return( aClone( aFiliais ) )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³RhFilial	³ Autor ³ Mauro			    	³ Data ³ 16.10.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Pocisiona no arquivo através da chave verificando sempre   ³±±
±±³			 ³ o compartilhamento do arquivo a ser pesquisado e retornando³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function RhFilial(cAliasPe,cFilPe)

If cAliasPe # NIL .And. cFilPe # NIL
	dbSelectArea( cAliasPe )
	If ( cFilPe # Nil .And. cFilial == Space(FWGETTAMFILIAL) ) .Or. cFilPe == Nil
		cFilPe := cFilial
	Endif
Else
	cFilPe	:= Space(FWGETTAMFILIAL)
Endif
Return( cFilPe)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³fGetSM0	³ Autor ³ Tiago Malta       	³ Data ³ 04.12.09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna Array com as informações do Sigamat                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Generico 												  ³±±
±±ÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ³±±
±±³Parametros³<Vide Parametros Formais>								      ³±±
±±ÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ³±±
±±³Retorno   ³ aSM0                   							          ³±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function fGetSM0(lFilterGrup)

Local aSM0      := FWLoadSM0()
Local aRetSm0   := {}
Local i         := 0

Default lFilterGrup := .F.

   	For i := 1 To Len(aSM0)
    	IF	lFilterGrup .AND. aSM0[i][SM0_GRPEMP] == cEmpAnt
			IF Empty(aSM0[i][SM0_EMPRESA])
				aSM0[i][SM0_EMPRESA] :=	aSM0[i][SM0_GRPEMP]
			Endif
			aAdd( aRetSm0 , aSM0[i] )
	    Elseif !(lFilterGrup)
			IF Empty(aSM0[i][SM0_EMPRESA])
				aSM0[i][SM0_EMPRESA] :=	aSM0[i][SM0_GRPEMP]
			Endif
			aAdd( aRetSm0 , aSM0[i] )
	    Endif
	Next i

Return(aRetSm0)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³RHTamFilialºAutor  ³Mauricio T.         º Data ³  03/03/10  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna a Filial do tamanho do layout da filial a ser trata º±±
±±º          ³da.                                                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function RHTamFilial(cFilCnv, cEmp)
Local cNewFil
Local nTamFil := 12

If !Empty(cEmp)
	nTamFil := FwSizeFilial(cEmp)
EndIf

DEFAULT cFilCnv := ""

cNewFil := cFilCnv + Space(12)

cNewFil := Substr(cNewFil, 1, nTamFil )

Return( cNewFil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fValidFil ºAutor  ³Microsiga - RH      º Data ³  19/07/00   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retornar String com as Filiais de Acesso do Usuario        º±±
±±ºRetorno   ³ "01/05/07/09/"                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP5                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function fValidFil(cAlias,lTpRet)

Local aArea := Getarea()
Local aEmpCor
Local I
Local nPosFilAcess := SM0_USEROK	// Verifica se o usuario possui acesso a filial

Default lTpRet := .F.

Static cFilAcess
Static cFilAcess2
Static cEmpAcess
Static lNewCarga := .T.

If !Empty(cAlias)
	lNewCarga := .T.
EndIf

If (cFilAcess == NIL .And. !lTpRet) .Or. (cFilAcess2 == Nil .And. lTpRet) .Or. cEmpAcess # cEmpAnt .Or. lNewCarga

	If !Empty(cAlias)
		DbSelectArea( cAlias )
		lNewCarga := .T.
	Else
		lNewCarga := .F.
	EndIf

	aEmpCor := FWLoadSM0()
	If lTpRet
		cFilAcess2 := Space(FWGETTAMFILIAL)+"/"
	Else
		cFilAcess := Space(FWGETTAMFILIAL)+"/"
	EndIf
	cEmpAcess := cEmpAnt

	If lTpRet
		If Len(aEmpCor) > 0
			aEval( aEmpcor , { |x| (  iif( Substr(x[1],1,2) == cEmpant .and. !( x[nPosFilAcess] ), cFilAcess2 += If( ( Empty(cAlias) .or. Empty( xFilial(cAlias) ) ), x[2], xFilial(cAlias,x[2]) ) + "/",) ) } )
		EndIf
	Else
		If Len(aEmpCor) > 0
			aEval( aEmpcor , { |x| (  iif( Substr(x[1],1,2) == cEmpant .and. x[nPosFilAcess], cFilAcess += If( ( Empty(cAlias) .or. Empty( xFilial(cAlias) ) ), x[2], xFilial(cAlias,x[2]) ) + "/",) ) } )
		EndIf
	EndIf

	If !Empty(cAlias)
		RestArea(aArea)
	EndIf

EndIf

Return( IIf(lTpRet,cFilAcess2,cFilAcess) )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ fInfo 	³ Autor ³Equipe Advanced RH	    ³ Data ³23/03/1995³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Carrega Matriz com dados da Filial Determinada			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ FilCorrente - Filial a ser carregada						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function fInfo( aInfo1 , cFilCorrente , cEmpCorrente )
	Local aArea				:= GetArea()
	Local aAreaSM0			:= SM0->( GetArea() )
	Local cTipEnd			:= SuperGetMv( "MV_PAREND" )
	Local lRetorno			:= .T.

	Default cTipEnd			:= "C"
	Default cFilCorrente	:= cFilAnt
	Default cEmpCorrente	:= cEmpAnt

	// 01 - Filial
	// 02 - Reduz Filial
	// 03 - Nome Completo
	// 04 - Endereco Cobranca
	// 05 - Cidade Cobranca
	// 06 - Estado Cobranca
	// 07 - Cep Cobranca
	// 08 - Cgc Cobranca
	// 09 - Insc Cobranca
	// 10 - Telefone
	// 11 - Fax 					Char	14
	// 12 - Producao Rural			Char	01  - "S" ou "N""
	// 13 - Bairro Cobranca 		Char	20
	// 14 - Compl. End. Cobranca	Char	12
	// 15 - Tipo de Inscricao		Num	01  -  1-CEI, 2-CGC, 3-CPF, 4-INCRA
	// 16 - CNAE					Char	05
	// 17 - FPAS					Char	04
	// 18 - Acid. de Trabalho		Char	08
	// 19 - C¢digo Municipio		Char	07
	// 20 - Natureza Juridica		Char	04
	// 21 - Data Base Pgto. RAIS	Char	02
	// 22 - N§ de Proprietarios		Char	02
	// 23 - Se Modificou Endere‡o 	Char	01  - "S" ou "N"
	// 24 - Se Modificou INSC/CGC 	Char	01  - "S" ou "N"
	// 25 - Causa da Mudan‡a		Char	01
	// 26 - INC./CGC Anterior		Char	14
	// 27 - CEI						Char	14
	// 28 - Tipo Insc.				Char	01
	// 29 - Razao Social Completa	Char	254

	aInfo := Array(29)

	IF ( lRetorno := SM0->( dbSeek( cEmpCorrente + cFilCorrente ) ) )
		aInfo[ 01 ] := SM0->M0_FILIAL
		aInfo[ 02 ] := SM0->M0_NOME
		aInfo[ 03 ] := SM0->M0_NOMECOM

		IF ( cTipEnd == "C" )
			aInfo[ 04 ] := SM0->M0_ENDCOB
			aInfo[ 05 ] := SM0->M0_CIDCOB
			aInfo[ 06 ] := SM0->M0_ESTCOB
			aInfo[ 07 ] := SM0->M0_CEPCOB
		Else
			aInfo[ 04 ] := SM0->M0_ENDENT
			aInfo[ 05 ] := SM0->M0_CIDENT
			aInfo[ 06 ] := SM0->M0_ESTENT
			aInfo[ 07 ] := SM0->M0_CEPENT
		EndIf

		aInfo[ 08 ] := SM0->M0_CGC
		aInfo[ 09 ] := SM0->M0_INSC
		aInfo[ 10 ] := SM0->M0_TEL
		aInfo[ 11 ] := SM0->M0_FAX
		aInfo[ 12 ] := SM0->M0_PRODRUR

		IF ( cTipEnd == "C" )
			aInfo[ 13 ] := SM0->M0_BAIRCOB
			aInfo[ 14 ] := SM0->M0_COMPCOB
		Else
			aInfo[ 13 ] := SM0->M0_BAIRENT
			aInfo[ 14 ] := SM0->M0_COMPENT
		EndIf

		aInfo[ 15 ] := If( Type("SM0->M0_CEI") # "U" .And. !Empty( SM0->M0_CEI ), 1, SM0->M0_TPINSC )
		aInfo[ 16 ] := SM0->M0_CNAE
		aInfo[ 17 ] := SM0->M0_FPAS
		aInfo[ 18 ] := SM0->M0_ACTRAB
		aInfo[ 19 ] := SM0->M0_CODMUN
		aInfo[ 20 ] := SM0->M0_NATJUR
		aInfo[ 21 ] := SM0->M0_DTBASE
		aInfo[ 22 ] := SM0->M0_NUMPROP
		aInfo[ 23 ] := SM0->M0_MODEND
		aInfo[ 24 ] := SM0->M0_MODINSC
		aInfo[ 25 ] := SM0->M0_CAUSA
		aInfo[ 26 ] := SM0->M0_INSCANT
		aInfo[ 27 ] := If( Type("SM0->M0_CEI") # "U" .And. !Empty( SM0->M0_CEI ), SM0->M0_CEI, "" )
		aInfo[ 28 ] := SM0->M0_TPINSC
		aInfo[ 29 ] := If( Type("SM0->M0_FULNAME") # "U" .And. !Empty( SM0->M0_FULNAME ), SM0->M0_FULNAME, "" )

		// Ponto de Entrada para alteracao do Tipo (aInfo[ 15 ]) e No Inscricao ( aInfo[ 08 ])
		If ExistBlock("GPEXINSCR", , .T.)
			EXECBLOCK("GPEXINSCR", .F., .F.)
		Endif
	Else
		aFill( aInfo , "" )
	EndIf

	aInfo1	:= aClone(aInfo)

	RestArea( aAreaSM0 )
	RestArea( aArea )
Return( lRetorno )


/*/{Protheus.doc} AllFilUsr
Retorna as filiais que um determinado usuário possui acesso.
@type function
@author Victor Andrade
@since 25/07/2016
@version 1.0
@param cUsrCod, character, Código do usuário a ter as informações retornadas.
@return xRet, Retorna True caso tenha acesso a todas as filiais, caso contrário, retorna um array com as filiais.
@example
(examples)
@see (links_or_references)
/*/Function AllFilUsr(cUsrCod)

Local cFiliais  := ""
Local aArea		:= GetArea()
Local aGrpFil	:= {}
Local aGrpUsr	:= {}
Local aAcessos	:= {}
Local aAcesFil	:= {}
Local nGrp		:= 0
Local nX		:= 0
Local nRecSPF

Default	cUsrCod	:= RetCodUsr()

// --> Recupera o recno do arquivo de usuário
nRecSPF:= FWSFLoadUser(cUsrCod)[1]

If FWSFUser(nRecSPF,"PROTHEUSDATA","USR_ALLEMP" )== "1" .Or. PswId() == "000000"
	Return(.T.)
Else
	aGrpFil := Separa( FWSFUser(nRecSPF,"PROTHEUSFILIAL","USR_FILIAL",.T.),";")

	For nX := 1 To Len(aGrpFil)
		If !Empty(aGrpFil[nX])
			Aadd(aAcessos, aGrpFil[nX] )
		EndIf
	Next nX

	aGrpUsr := UsrRetGrp(, cUsrCod)
	For nGrp := 1 To Len(aGrpUsr)
		aAcesFil := FWGrpEmp(aGrpUsr[nGrp])
		If !Empty(aAcesFil) .And. aAcesFil[1] == "@@@@"
			Return(.T.)
		Else
			For nX := 1 To Len(aAcesFil)
				Aadd(aAcessos, SubStr(aAcesFil[nX], Len(cEmpAnt)+1) )
			Next nX
		EndIf
	Next nGrp

EndIf

RestArea(aArea)

Return(aAcessos)

/*/{Protheus.doc} AllStrFil
Função para retornar as filiais do usuário em uma string, caso usuário tenha acesso full é retornado True
@type function
@author Victor Andrade
@since 26/07/2016
@version 1.0
@param cUsrCod, character, (Descrição do parâmetro)
@return xRet, Retorna ".T." caso tenha acesso a todas as filiais, caso contrário retorna a string com as filiais separadas por |
@example
(examples)
@see (links_or_references)
/*/Function AllStrFil(cUsrCod)

Local xFilUsr  := AllFilUsr(cUsrCod)
Local nX 	   := 0
Local cFiliais := ""
Local lCont	   := .F.

If ValType(xFilUsr) == "L"
	cFiliais := ".T."
Else
	For nX := 1 To Len(xFilUsr)
		cFiliais += xFilUsr[nX] + "|"
	Next nX
EndIf

Return(cFiliais)

/*/{Protheus.doc} fTrGrpGPE
Abre o grupo de empresa informada para que seja possível a utilização das tabelas
@type Function
@author martins.marcio
@since 08/08/2025
@return lRet
/*/
Function fTrGrpGPE(cEmpOpen, cFilOpen, cTables, cModulo)

	Local cRelease := GetRpoRelease()
	Local lRet	   := .T.

	Default cEmpOpen	:= ""
	Default cFilOpen	:= ""
	Default cTables		:= "SRA"
	Default cModulo		:= "GPE"

	If !Empty(cEmpOpen) .And. !Empty(cFilOpen)
		If cEmpOpen == cEmpAnt
			cFilAnt := cFilOpen
		ElseIf cEmpOpen <> cEmpAnt
			If cRelease <= "12.1.2410"
				RPCSetType(3)
				RESET ENVIRONMENT
				PREPARE ENVIRONMENT EMPRESA cEmpOpen FILIAL cFilOpen TABLES cTables MODULO cModulo
			Else
				lRet := .F.
			EndIf
		EndIf
	EndIf
Return lRet
