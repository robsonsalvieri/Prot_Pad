#INCLUDE "TOTVS.CH"
#INCLUDE "RESTFUL.CH"
#INCLUDE "tlpp-core.th"

/*/{Protheus.doc} PONXTAE
Classe para integração de documentos com o TAE. Herda a classe FWTotvsSign do Framework
@type Class
@author Cícero Alves
@since 11/06/2025
@See https://tdn.totvs.com/pages/releaseview.action?pageId=653840979 
/*/
Class PONXTAE From TOTVS.FRAMEWORK.TOTVSSIGN.ENGINE
	
	public method New()
	public method GetResponseCode()
	public method GetTypeErro403()
	public method SendMail()
	public method NotifFunc()
	
EndClass

/*/{Protheus.doc} PONXTAE:New
Método contrutor da classe
@type method
@author Cí­cero Alves
@since 11/06/2025
@return object, instância da classe 
/*/
Method New() class PONXTAE
	
	_Super:New()
	
Return Self

/*/{Protheus.doc} PONXTAE:GetResponseCode
Retrona o código HTTP recebido após realizar uma requisição
@type method
@author Cí­cero Alves
@since 11/06/2025
@return character, Código HTTP de retorno
/*/
Method GetResponseCode() class PONXTAE
Return ::oCLient:oResponseH:GetStatusCode()

/*/{Protheus.doc} PONXTAE:GetTypeErro403
Retorna qual o tipo do erro do Código 403 de acordo com as informações do ResposeHeader
@type method
@author Cícero Alves
@since 11/06/2025
@return Character, "1" = Erro de bloqueio do firewall; "2" = Usuário sem permissão
/*/
Method GetTypeErro403() class PONXTAE
	
	Local cRet := "" as Character
	
	If ::GetResponseCode() == "403"
		cRet := If(::oCLient:GetHeader("Server") == "CloudFront", "1", "2")
	EndIf
	
Return cRet

/*/{Protheus.doc} PONXTAE:SendMail
Envia email para os assinates pendentes do documento
@type method
@author Cí­cero Alves
@since 11/06/2025
@param cIdDoc, character, Id do documento no TAE
@return Logical, de acordo com o retorno da requisição
/*/
Method SendMail(cIdDoc) class PONXTAE
	
	Local aHeader 	:= {} 	as Array
	Local cBody		:= "" 	as Character
	Local cPath		:= "" 	as Character
	Local lret 		:= .F. 	as Logical
	
	Default cIdDoc := ""
	
	If !Empty(cIdDoc)
		
		cPath := "signintegration/v2/Publicacoes/reenvia-email-pendentes"
		
		cBody := '{"listaDocumentos": [' + cIdDoc + ']}'
		
		Aadd(aHeader, "Accept: application/json")
		Aadd(aHeader, "Content-Type: application/json")
		Aadd(aHeader, "Authorization: Bearer " + ::cAuthToken)
		
		::oClient:SetPath(cPath)
		::oClient:SetPostParams(cBody)
		
		lRet := ::oClient:Post(aHeader)
	EndIf
	
Return lRet

/*/{Protheus.doc} PONXTAE:NotifFunc
Envia email para os assinates pendentes do documento
@type method
@author Marco Nakazawa
@since 17/07/2025
@param cIdDoc, character, Id do documento no TAE
@param lMRHTae, logical, Conteudo do parâmetro MV_MRHTAE
@param cTpNotTae, numeric, tipo do envio da notificação E-mail, WhatsApp ou SMS
@return Array, 1º posição logical - Sucesso ou falha (.T. ou .F.), 2º posição character, mensagem de erro caso houver
/*/
Method NotifFunc(cIdDoc, lMRHTae, nTpNotTae) class PONXTAE
	Local aHeader 	:= {} 		as Array
	Local aResp 	:= {.T.,""} as Array
	Local cBody		:= "" 		as Character
	Local cPath		:= "" 		as Character
	Local cTpNotTae	:= "" 		as Character
	Local cMsg		:= "" 		as Character
	Local lRet 		:= .F. 		as Logical
	Local oErro					as object
	
	Default cIdDoc		:= ""
	Default lMRHTae		:= .F.
	Default nTpNotTae	:= 1
	
	If !Empty(cIdDoc)
		aAdd(aHeader, "content-type: application/json")
		
		cTpNotTae := cValToChar(nTpNotTae)
		
		cPath := "documents/v1/publicacoes"

		cBody := '{'
		cBody +=	'"idDocumento": ' + cIdDoc + ','
		cBody +=	'"destinatarios": ['
		cBody +=		'{'
		cBody +=			'"email": "' + AllTrim(SRA->RA_EMAIL) + '",'
		cBody +=			'"acao": "0",'
		cBody +=			'"tipoIdentificacao": "1",'
		cBody +=			'"tipoAutenticacao": "' + If(lMRHTae, "2", "1") + '",'
		cBody +=			'"nomeCompleto": "' + AllTrim(SRA->RA_NOME) + '",'
		cBody +=			'"identificacao": "' + Alltrim(SRA->RA_CIC) + '",'
		
		If lMRHTae
			If nTpNotTae > 1
				cBody +=		'"tipoEnvioDocumento": ' + If(nTpNotTae == 3, "1", "2") + ','
				cBody +=		'"tipoEnvioCodigo": ' + cTpNotTae + ','
				cBody +=		'"telefone": "' + AllTrim(SRA->RA_DDDCELU) + Alltrim(SRA->RA_NUMCELU) + '"'
			Else
				cBody +=		'"tipoEnvioDocumento": 1,'
				cBody +=		'"tipoEnvioCodigo": 1'
			EndIf
		EndIf

		cBody +=		'}'
		cBody +=	']'
		cBody += '}'

		Aadd(aHeader, "Accept: application/json")
		Aadd(aHeader, "Content-Type: application/json")
		Aadd(aHeader, "Authorization: Bearer " + ::cAuthToken)
		
		::oClient:SetPath(cPath)
		::oClient:SetPostParams(cBody)
		
		lRet := ::oClient:Post(aHeader)

		// Retorna apenas se deu erro de integração com o TAE
		If !lRet
			oErro := JsonObject():New()
			oErro:FromJSON(::oClient:cResult)
			oErro:ToJSON(::oClient:cResult)

			If ValType(oErro['errors']) == "A"
				cMsg := DecodeUTF8(oErro['errors'][1])
			EndIf
		EndIf

		aResp := {}
		aAdd(aResp, lRet)
		aAdd(aResp, cMsg)
	EndIf	
Return aResp
