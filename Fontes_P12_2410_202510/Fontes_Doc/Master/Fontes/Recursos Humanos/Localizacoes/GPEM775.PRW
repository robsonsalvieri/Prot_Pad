#INCLUDE "PROTHEUS.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "GPEM775.CH"
/*/


Ŀ                       
Funo     GPEM775   Autor  Rogerio Vaz Melonio    Data  22/07/08 
Ĵ
Descrio  Geracao de arquivo texto do Quadro de Pessoal              
Ĵ
 Uso       Generico                                                   
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
Marcos Kato 22/10/08      Insercao do Cod. Assoc. Pratonal 1,2 e 3 e
                          o Ano de Constituicao da Empresa no Arq.  
Marcos Kato 19/11/08      Substituicao RGP_HRSEMAN para RA_HRSEMAN  
                          porque nao foi encontrado o campo no atusx
Marcos Kato 28/11/08      Tratamento para o campo M0_NIRE.		  
Tiago Malta 07/01/1000280/ Alterado o conteudo da variavel cFilDe e 
|            |        |  2010| cFilAte para space(FWGETTAMFILIAL).	  
 Alex       22/12/1029463/ Atender Nova Interface Totvs 11.5 utili- 
                      2010 zando a funcao MsAdvSize.                
-------------------------- versao 11.5------------------------------
Emerson     30/06/11013705Atender a Nova Interface Totvs 11.5 utili-
Campos               /2011zando a funcao MsAdvSize( p/ flexibilidade
                          da tela de acordo com a resolucao).       
ٱ

/*/
Function GPEM775()
Local nOpca 	:= 0
Local bBtnCalcule							//bloco do boto OK
Local bPergunte								//bloco para o pergunte
Local cSays			:= OemToAnsi(STR0002)	//"Este programa gera o arquivo texto do Quadro de Pessoal"
Local oDlg
Local oBtnCalcule
Local oBtnEnd
Local oBtnPergunte
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aButtons		:= {}
Local bSet15		:= { || NIL }
Local bSet24		:= { || NIL }

Private cFilDe := cFilAte := Space(FWGETTAMFILIAL)
Private cTxtParam := ""
Private oAnoMes, oTxtParam, aParamQP
Private cAnoMes := Space(6)
Private cTipo:="",cMorada:="",cLocalidade:="",cCodPostal:="",cDescPostal:="",cCodEstab:="",cCodIRCT:=""
Private cPerg := Padr("GPEM775",10)
Pergunte(cPerg,.F.)
DEFINE FONT oFont1  NAME "Arial" SIZE 0,-13 BOLD
cTxtParam := OemToAnsi(Gpem775QbrPar(cPerg))
cBlkGet :=  "{ | u | If( PCount() == 0, cTxtParam,cTxtParam:= u ) }"
cBlKVld :=  "{|| .T. }"
cBlKWhen := "{|| .F. }"

/*
Ŀ
 Monta as Dimensoes dos Objetos         					   
*/
aAdvSize		:= MsAdvSize( , .T., 370)
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }					 
aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
aObjSize	:= MsObjSize( aInfoAdvSize , aObjCoords )

DEFINE MSDIALOG oDlg TITLE OemToAnsi( STR0001 ) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL // "Gerao do Quadro de Pessoal"
	/*/
	Ŀ
	 Descricao da Janela                                                      
	 */
	@ aObjSize[1,1] , aObjSize[1,2] TO aObjSize[1,3],aObjSize[1,4] OF oDlg PIXEL  //@ 11,12 TO 100,245 OF oDlg PIXEL
	@ aObjSize[1,1]+15 , aObjSize[1,2]+5 SAY cSays Of oDlg Pixel  FONT oFont1 // "Este programa gera o arquivo texto do Quadro de Pessoal"
	@ aObjSize[1,1]+25 , aObjSize[1,2]+5 SAY STR0003 Of oDlg Pixel FONT oFont1 // "Parametros:"
	oTxtParam := TMultiGet():New(aObjSize[1,1]+40,aObjSize[1,2]+5,&cBlKGet,oDlg,(aObjSize[1,4]-aObjSize[1,2])-10,(aObjSize[1,3]-aObjSize[1,1])-50,,.F.,,,,.T.,,.F.,&(cBlkWhen),.F.,.F.,.F.,&(cBlkVld),,.F.,.F.,.T.)

	/*/
	Ŀ
	 Funcoes em GpFiltro  - Blocos de Execucao dos filtros                    
	 GpFiltro - Chama a janela principal do cadastro de filtros               
	 GpFltAlsGet - Retorna a expressao para elaborar o filtro                 
	*/
	AADD(aButtons, {"LogOcorr" ,{ || GPM775Log() },STR0005,STR0005} )
	AADD(aButtons, {"Parametros" ,{ || Pergunte("GPEM775",.T.), cTxtParam := OemToAnsi(Gpem775QbrPar(cPerg)), oTxtParam:Refresh() },STR0003,STR0003} )
	
	bSet15		:= { || nOpca := 1, IF( GPM775Ok()( ),oDlg:End(), nOpca:=0 ) }
	bSet24		:= { || oDlg:End() }

ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 , NIL , aButtons )

//Ŀ
// Variaveis utilizadas para parametros                         
//
//Ŀ
// mv_par01 - Filial de                                         
// mv_par02 - Filial Ate                                        
// mv_par03 - Ano/Mes Referencia                                
// mv_par04 - Nome do Arquivo                                   
// mv_par05 - Local do Arquivo                                  
// mv_par06 - Capital Social                                    
// mv_par07 - % Privado Nacional                                
// mv_par08 - % Estrangeiro                                     
// mv_par09 - % Publico                                         
// mv_par10 - Volume de Negocios                                
// mv_par11 - Nome de Contato                                   
// mv_par12 - Fone 1                                            
// mv_par13 - Fone 2                                            
// mv_par14 - Fax                                               
// mv_par15 - Email                                             
//

IF nOpca == 1
	ProcGpe({|lEnd| f775GravaQP()})  // Chamada do Processamento
Endif

Return

/*


ͻ
Funcao    GPM775GetFile Autor  Rogerio Melonio    Data   07/08/08  
͹
Desc.     Visualizacao do Relatorio de Log de Processamento.           
͹
Uso        AP                                                          
ͼ


*/
Function GPM775GetFile()
Local cArq
mvRet := Alltrim(ReadVar())
cArq := Space(80)
cTipo := STR0015+".txt"+")   | *."+"txt"+"  " // "Arquivos de Quadro de Pessoal (*.txt)   | *.txt  "
cGetFile := cGetFile("",OemToAnsi(STR0016),0,,.F.,GETF_LOCALFLOPPY + GETF_LOCALHARD + GETF_RETDIRECTORY) //"Selecione o Diretorio"
If Empty(cGetFile)
	Return(.F.)
Endif
&mvRet := cGetFile
Return(.T.)

/*


ͻ
Funcao    GPM775Log     Autor  Rogerio Melonio    Data   07/08/08  
͹
Desc.     Visualizacao do Relatorio de Log de Processamento.           
͹
Uso        AP                                                          
ͼ


*/
Static Function GPM775Log()
cPathFile := ""
cExtRel := ".##R"
cPathFile := ( __RelDir + "GPEM775" + cExtRel )
If File(cPathFile)
	OurSpool( "GPEM775" )
	Ms_Flush()
Else
	MsgStop(STR0013) // "No existe Log de Processamento para exibir"
Endif
Return

/*


ͻ
Funcao    f775LogTipo    Autor  Rogerio Vaz Melonio  Data 04/09/08
͹
Desc.      Rotina p/ verificar se campos dos registros  estao corretos
͹
Uso        Cria Log de Inconsistencia de registros conforme o tipo    
ͼ


*/
Function f775LogTipo(cTipo,cMorada,cLocalidade,cCodPostal,cDescPostal,cCodEstab,cCodIRCT)
If cTipo == '00'
	/*/
	Registro de Contato					
	Nome(s) do Contato							A 			100		S 		Padr(mv_par11,100)
	Correio Eletrnico							A 			60		S 		Padr(mv_par15,60)
	/*/
	cTxtOco := ""
	cTxtOco += Iif(Empty(mv_par11),STR0019,"") 	// "Nome "
	cTxtOco += Iif(Empty(mv_par12),STR0020,"") 	// "Fone 1 "
	cTxtOco += Iif(Empty(mv_par15),STR0021,"") 	// "Email "
	If !Empty(cTxtOco)
		If aTotRegs[1] == 0
			cLog := STR0035 // "Inconsistncias nas informaes do Contato"
			Aadd(aTitle,cLog)  
			Aadd(aLog,{})
			aTotRegs[1] := len(aLog)
		EndIf	
		Aadd(aLog[aTotRegs[1]], cTxtOco ) 
	Endif
ElseIf cTipo == '10'
	/*/
	Registro da Empresa					
	N de Identificao Fiscal 					N 			9		S 		SM0->M0_CGC 
	Ano de Referncia da Informao 			N 			4		S 		Substr(mv_par03,1,4)
	Nome da Empresa 							A 			60		S 		SM0->M0_NOMECOM
	Morada 										A 			40		S 		SM0->M0_ENDCOB e caso esteja vazio, SM0->M0_ENDENT
	Localidade 									A 			20		S 		SM0->M0_BAIRCOB, se estiver vazio, SM0->M0_BAIRENT
	Cdigo Postal 								N 			7		S 		SM0->M0_CEPCOB, se estiver vazio, SM0->M0_CEPENT
	Cdigo Postal (descritivo) 					A 			20		S 		fDesc("SCG",SM0->M0_CEPCOB,"CG_DESCEP") ou fDesc("SCG", SM0-> M0_CEPENT, "CG_DESCEP")
	Telefone 									N 			9		S 		SM0->M0_TEL
	Cdigo da Atividade Principal da Empresa 	N 			5		S 		SM0->M0_CNAE 
	Cdigo de Natureza Jurdica 				N 			2		S 		SM0->M0_NATJUR 
	Volume de negcios, ano anterior 			N 			12		S 		StrZero(mv_par10*100,12)
	/*/
	cTxtOco := ""
	cTxtOco += Iif(Empty(SM0->M0_CGC),STR0022,"") 		// "NIF "
	cTxtOco += Iif(Empty(mv_par03),STR0023,"") 			// "Ano/Mes de referencia "
	cTxtOco += Iif(Empty(SM0->M0_NOMECOM),STR0019,"") 	// "Nome "
	cTxtOco += Iif(Empty(cMorada),STR0024,"") 			// "Endereco "
	cTxtOco += Iif(Empty(cLocalidade),STR0025,"") 		// "Bairro "
	cTxtOco += Iif(Empty(cCodPostal),STR0026,"") 		// "Codigo Postal "
	cTxtOco += Iif(Empty(cDescPostal),STR0027,"") 		// "Descricao do Codigo Postal "
	cTxtOco += Iif(Empty(SM0->M0_TEL),STR0028,"") 		// "Telefone "
	cTxtOco += Iif(Empty(SM0->M0_CNAE),STR0029,"") 		// "Cod. Atividade "
	cTxtOco += Iif(Empty(SM0->M0_NATJUR),STR0030,"") 	// "Cod. Natureza Juridica "
	cTxtOco += Iif(Empty(mv_par10),STR0031,"") 			// "Volume de Negocios do ano anterior "
	If !Empty(cTxtOco)
		If aTotRegs[2] == 0
			cLog := STR0036 // "Inconsistencias no Cadastro da Empresa"
			Aadd(aTitle,cLog)  
			Aadd(aLog,{})
			aTotRegs[2] := len(aLog)
		EndIf	
		Aadd(aLog[aTotRegs[2]], cTxtOco ) 
	Endif
ElseIf cTipo == '20'
	/*/
	Registro do estabelecimento
	N do Estabelecimento 						N 			4		S 		RCO_NESTAB
	Nome do Estabelecimento 					A 			60		S 		RCO_NOME
	Morada 										A 			40		S 		RCO_END
	Localidade 									A 			20		S 		RCO_LOCAL
	Cdigo Postal 								N 			7		S 		RCO_CEP
	Cdigo Postal (descritivo) 					A 			20		S 		fDesc("SCG",RCO->RCO_CEP,"CG_DESCEP"))
	Telefone 									N 			9		S 		RCO_FONE
	Cdigo de Distrito / Municpio / Freguesia 	N 			6		S 		RCO_BAIRRO
	Cd. Atividade Principal do Estabelecimento	N 			5		S 		RCO_ATIVID
	/*/
	cTxtOco := ""
	cTxtOco += Iif(Empty(RCO->RCO_NESTAB),STR0032,"") 	// "Numero do Estabelecimento "
	cTxtOco += Iif(Empty(RCO->RCO_NOME),STR0019,"") 	// "Nome "
	cTxtOco += Iif(Empty(RCO->RCO_END),STR0024,"") 		// "Endereco "
	cTxtOco += Iif(Empty(RCO->RCO_LOCAL),STR0025,"") 	// "Bairro "
	cTxtOco += Iif(Empty(RCO->RCO_CEP),STR0026,"") 		// "Codigo Postal "
	cTxtOco += Iif(Empty(cDescPostal),STR0027,"") 		// "Descricao do Codigo Postal "
	cTxtOco += Iif(Empty(RCO->RCO_FONE),STR0028,"") 	// "Telefone "
	cTxtOco += Iif(Empty(RCO->RCO_BAIRRO),STR0033,"") 	// "Cdigo de Estado / Municpio / Bairro "
	cTxtOco += Iif(Empty(RCO->RCO_ATIVID),STR0029,"") 	// "Cod. Atividade "
	If !Empty(cTxtOco)
		If aTotRegs[3] == 0
			cLog := STR0037 // "Inconsistencias no Cadastro do Estabelecimento"
			Aadd(aTitle,cLog)  
			Aadd(aLog,{})
			aTotRegs[3] := len(aLog)
		EndIf	
		Aadd(aLog[aTotRegs[3]], Alltrim(RCO->RCO_NESTAB)+"-"+AllTrim(RCO->RCO_NOME)+":"+cTxtOco ) 
	Endif
ElseIf cTipo == '30'
	/*/
	Registro do estabelecimento/IRCT
	N do Estabelecimento 						N 			4		S 		RCO->RCO_NESTAB
	Cdigo do IRCT 								N 			5		S 		SRA->RA_CODIRCT
	/*/
	cTxtOco := ""
	cTxtOco += Iif(Empty(cCodEstab),STR0032,"") 	// "Numero do Estabelecimento "
	cTxtOco += Iif(Empty(cCodIRCT),STR0034,"") 		// "Cdigo do IRCT "
	If !Empty(cTxtOco) 
		If aTotRegs[4] == 0
			cLog := STR0038 // "Inconsistencias no Cadastro do Estabelecimento/IRCT"
			Aadd(aTitle,cLog)  
			Aadd(aLog,{})
			aTotRegs[4] := len(aLog)
		Endif
		Aadd(aLog[aTotRegs[4]], cCodEstab+"-"+cCodIRCT+":"+cTxtOco ) 
	EndIf	
ElseIf cTipo == '40'
	/*/
	cTipo == '40'
	Registro do Funcionario
	Descrio dos Campos			Contedo Obrigatrio
	1. Tipo de Registro 			S 
	2 - N. do Estabelecimento 		S 
	3. Cdigo do IRCT 				S 
	5. Nome do Trabalhador 			S 
	6.1 Data de Nascimento 			S 
	6.2 Data de Admisso 			S 
	7.1 Salario Base 				S 
	7.3 Valor de Horas extras		
	9.1 Horas Normais 				Obrigatrio se 7.1 > 0
	9.2 Horas Extras				Obrigatrio se 7.3 > 0
	10. Perodo Normal de Trabalho 	S 
	12. Cod. Categoria Profissional S 
	13. Cod. de Profisso 			S 
	14. Cod. de Habilitao 		S 
	15. Cod. de Nacionalidade 		S 
	17. Situao na Profisso 		S 
	18. Tipo de Contrato 			S 
	19. Regime de Durao do Trabalho 	S 
	20. Sexo 						S 
	/*/
	cTxtOco := ""
	cTxtOco += Iif(Empty(RGP->RGP_CODEST),STR0032,"") 	// "Numero do Estabelecimento "
	cTxtOco += Iif(Empty(RGP->RGP_IRCT),STR0033,"") 	// "Cdigo do IRCT "
	cTxtOco += Iif(Empty(SRA->RA_NOME),STR0019,"") 		// "Nome "
	cTxtOco += Iif(Empty(SRA->RA_NASC),STR0043,"") 		// "Data de Nascimento "
	cTxtOco += Iif(Empty(SRA->RA_ADMISSA),STR0044,"") 	// "Data de Admisso "
	cTxtOco += Iif(RGP->RGP_REMUBA > 0 .And. RGP->RGP_HRNORM = 0,STR0045,"") 	// "Horas Normais "
	cTxtOco += Iif(RGP->RGP_TRBSUP > 0 .And. RGP->RGP_HRSUPL = 0,STR0046,"") 	// "Horas Extras " 
	
	//If RGP->RGP_HRSEMAN - Int(RGP->RGP_HRSEMAN) == 0 // se eh hora completa, multiplica por 10 
        //nPerNormal 	:= RGP->RGP_HRSEMAN 
        //ElseIf RGP->RGP_HRSEMAN - Int(RGP->RGP_HRSEMAN) <= 0.50
        //   nPerNormal := (Int(RGP->RGP_HRSEMAN) + 0.50) * 10
        //Else
    	//   nPerNormal := (Int(RGP->RGP_HRSEMAN)+1) * 10
        //EndIf			
	If SRA->RA_HRSEMAN - Int(SRA->RA_HRSEMAN) == 0 // se eh hora completa, multiplica por 10 
        nPerNormal 	:= SRA->RA_HRSEMAN 
    ElseIf SRA->RA_HRSEMAN - Int(SRA->RA_HRSEMAN) <= 0.50
        nPerNormal := (Int(SRA->RA_HRSEMAN) + 0.50) * 10
    Else
        nPerNormal := (Int(SRA->RA_HRSEMAN)+1) * 10
    EndIf			
	cTxtOco += Iif(nPerNormal=0,STR0047,"")     	// "Perodo Normal de Trabalho Semanal (PNT) "
	cTxtOco += Iif(Empty(RGP->RGP_CATEGO),STR0048,"") 	// "Cdigo de Categoria Profissional "
	cTxtOco += Iif(Empty(RGP->RGP_CODHAB),STR0049,"") 	// "Cdigo de Grau de Instruao "
	cTxtOco += Iif(Empty(RGP->RGP_CODPRO),STR0050,"") 	// "Cdigo de Profisso "
	cTxtOco += Iif(Empty(RGP->RGP_CODNAC),STR0051,"") 	// "Cdigo de Nacionalidade "
	cTxtOco += Iif(Empty(RGP->RGP_SITPRO),STR0052,"") 	// "Situao na Profisso "
	cTxtOco += Iif(Empty(RGP->RGP_TIPOCO),STR0053,"") 	// "Tipo de Contrato "
	cTxtOco += Iif(Empty(RGP->RGP_REGIME),STR0054,"") 	// "Regime de Durao do Trabalho "
	cTxtOco += Iif(Empty(SRA->RA_SEXO ),STR0055,"") 	// "Sexo "

	If !Empty(cTxtOco) 
		If aTotRegs[5] == 0
			cLog := STR0039 // "Inconsistencias no Cadastro de Funcionarios"
			Aadd(aTitle,cLog)  
			Aadd(aLog,{})
			aTotRegs[5] := len(aLog)
		Endif
		Aadd(aLog[aTotRegs[5]], SRA->RA_MAT+"-"+AllTrim(SRA->RA_NOME)+":"+cTxtOco ) 
	Else
		// se nao ha inconsistencias no cadastro do funcionario
		If aTotRegs[13] == 0
			cLog := STR0014 // "Funcionrios sem inconsistencias."
			Aadd(aTitle,cLog)  
			Aadd(aLog,{})
			aTotRegs[13] := len(aLog)
		EndIf	
		Aadd(aLog[aTotRegs[13]], SRA->RA_FILIAL+"-"+SRA->RA_MAT+ space(02)+SRA->RA_NOME ) 
	EndIf	
Endif
Return

/*


ͻ
Funcao    GPM775Ok  Autor  Microsiga            Data   22/08/08   
͹
Desc.                                                                 
͹
Uso        AP                                                         
ͼ


*/
Static Function GPM775Ok()
Return (MsgYesNo(OemToAnsi(STR0012),OemToAnsi(STR0007))) //"Confirma configurao dos parmetros?"###"Ateno"

/*/
Ŀ
Funo    Gpem775QbrParAutor Rogerio Vaz Melonio    Data22/08/2008
Ĵ
Descrio Retorna o texto dos parametros separado em linhas      		
Ĵ
Sintaxe   <Vide Parametros Formais>									
Ĵ
Parametros<Vide Parametros Formais>									
/*/
Function Gpem775QbrPar(cPerg)
nLinha := 1
nStrPar := 1
cTxtPar := ""
cTxtSX1 := ""
dbSelectArea("SX1")
dbSetOrder(1)
dbSeek(cPerg)
aParamQP := {}
While !Eof() .And. SX1->X1_GRUPO == cPerg
	cTxtSX1 += AllTrim(SX1->X1_PERGUNT)+":'"
	cVarX1 := AllTrim(SX1->X1_VAR01)
	If SX1->X1_TIPO == "C"
		cContX1 := AllTrim(&cVarX1)
	ElseiF SX1->X1_TIPO == "N"
		cContX1 := AllTrim(Transform(&cVarX1,SX1->X1_PICTURE))
	ElseiF SX1->X1_TIPO == "D"
		cContX1 := Dtoc(&cVarX1)
	Endif
	Aadd(aParamQP,{SX1->X1_PERGUNT+" : ",cContX1})
	cTxtSX1 +=  cContX1+"' ; " 
	dbSkip()
EndDo
While nStrPar <= Len(cTxtSX1)
	If nLinha > 6
		Exit
	Endif
	cTxtPar += Substr(cTxtSX1,nStrPar,80)+Chr(13)
	nStrPar+=80  
	nLinha++
EndDo
While nLinha <= 6
	cTxtPar += Chr(13)
	nLinha++
EndDo
Return(cTxtPar)

/*


ͻ
Funcao    f775GravaQP    Autor  Rogerio Vaz Melonio  Data 02/09/08
͹
Desc.      Rotina de gravacao do arquivo texto do quadro de pessoal   
͹
Uso                                                                   
ͼ


*/
FUNCTION f775GravaQP()
Local nY
Private cNomArq, nHdl
Private contafun, nTotal
Private cEOL    := CHR(13)+CHR(10)
Private aEstab := {}
Private aIRCT  := {}
Private cFilProc
Private aInfo 	:= {}
Private aLog := aLogTot := {}
Private aTitle	:= {}
Private aTotRegs
//Ŀ
// Variaveis utilizadas para parametros                         
//
//Ŀ
// mv_par01 - Filial de                                         
// mv_par02 - Filial Ate                                        
// mv_par03 - Ano/Mes Referencia                                
//
// Monta array com as filiais que serao processadas pela rotina                   
aFilProc := {}
dbSelectArea("SM0")
cEmpFilOld := CNUMEMP
cEmpFilAtu := CEMPANT + mv_par01
dbSeek(cEmpFilAtu,.T.)
While !Eof() .And. SM0->M0_CODIGO+SM0->M0_CODFIL <= CEMPANT+mv_par02
	aAdd(aFilProc,{SM0->M0_CODFIL,SM0->M0_FILIAL+"/"+SM0->M0_NOME+"/"+M0_NOMECOM,SM0->M0_CGC})
	dbSkip()
EndDo
dbSelectArea("RGP")
dbSetOrder(1) // RGP_FILIAL + RGP_ANOMES + RGP_CODEST + RGP_IRCT + RGP_MATRIC
aTotRegs := array(13)
aFill(aTotRegs,0)
aLog := {}
For nY := 1 To Len(aFilProc)
	lTemArq := .F.
	dbSelectArea("RGP")
	dbSetOrder(1) // RGP_FILIAL + RGP_ANOMES + RGP_CODEST + RGP_IRCT + RGP_MATRIC
	cFilAtu := aFilProc[nY][1] // Filial + empresa a processar
	If aTotRegs[11] == 0
		cLog := STR0010  // "Filial processada: "
		Aadd(aTitle,cLog)  
		Aadd(aLog,{})
		aTotRegs[11] := len(aLog)
		Aadd(aLog[aTotRegs[11]], cFilAtu+"-"+aFilProc[nY][2]) 
	EndIf	
	If dbSeek(cFilAtu+mv_par03) // Filial + Ano + Mes
		cFilProc := RGP->RGP_FILIAL
		nTotal := 0
		f775ContaFunc(cFilAtu) // chama rotina para totalizar funcionarios por estabelecimento / IRCT da filial que esta sendo processada
		cLin1 := f775GrvCab('00',cFilAtu) // Chama rotina de geracao do cabecalho - informacoes de contato
		cLin2 := f775GrvCab('10',cFilAtu) // Chama rotina de geracao do cabecalho - informacoes da empresa
		cLin3 := f775GrvCab('20',cFilAtu) // Chama rotina de geracao do cabecalho - informacoes do estabelecimento
		cLin4 := f775GrvCab('30',cFilAtu) // Chama rotina de geracao do cabecalho - informacoes do estabelecimento/IRCT
		cExt := Iif(!".TXT"$Upper(mv_par04),".txt","")
		// nome do arquivo no formato NIF_AAAA.TXT
		// se foi escolhida mais de uma filial, o nome sera criado automaticamente
		// nome formado por caminho + nome + extensao
		cNomArq := AllTrim(mv_par05)+Iif(mv_par01==mv_par02,+AllTrim(mv_par04),AllTrim(aFilProc[nY][3])+"_"+Substr(mv_par03,1,4))+cExt
		nHdl    := fCreate(cNomArq)
		If nHdl == -1
			MsgStop(STR0017+cNomArq+STR0018,STR0007) // ###"O arquivo de nome "###" nao pode ser gravado! Verifique os parametros."###"Atencao!"
			Return
		Endif
		fWrite(nHdl,cLin1,Len(cLin1))
		fWrite(nHdl,cLin2,Len(cLin2))
		fWrite(nHdl,cLin3,Len(cLin3))
		fWrite(nHdl,cLin4,Len(cLin4))
		lTemArq := .T.
		dbSelectArea("RGP")
		ProcRegua( nTotal )
		contafun := StrZero(0,10)
		cEstabIrct := RGP->RGP_CODEST+RGP->RGP_IRCT
		While !Eof() .And. RGP->RGP_FILIAL+RGP->RGP_ANOMES <= cFilAtu+mv_par03
			IncProc(RGP->RGP_MATRIC + "-" + RGP->RGP_NOME)
			If RGP->RGP_CODEST+RGP->RGP_IRCT <> cEstabIrct
				contafun := StrZero(0,10)
				cEstabIrct := RGP->RGP_CODEST+RGP->RGP_IRCT
			Endif
			cLin := f775GrvDet() // monta a linha com dados do funcionario
			If !Empty(cLin)
				fWrite(nHdl,cLin,Len(cLin))
				lTemArq := .T.
			Endif
			dbSelectArea("RGP")
			dbSkip()
		EndDo
		// Fecha o arquivo gerado somente se houver registros validos
		If lTemArq
			fClose(nHdl)
		Endif
	Else
		// Log informando que nao foram encontrados registros para processar
		If aTotRegs[12] == 0
			cLog := STR0056 // "No foram encontrados registros para processar nesta filial"
			Aadd(aTitle,cLog)  
			Aadd(aLog,{})
			aTotRegs[12] := len(aLog)
			Aadd(aLog[aTotRegs[12]], aFilProc[nY][2] ) 
		EndIf	
	Endif
	If aTotRegs[1] == 0 .and. aTotRegs[2] == 0 .and. aTotRegs[3] == 0 .and. aTotRegs[4]==0 .and. ;
	   aTotRegs[5] == 0
		If aTotRegs[6] == 0
			cLog := STR0008 // "Arquivo texto do Quadro de Pessoal Gerado com sucesso!"
			Aadd(aTitle,cLog)  
			Aadd(aLog,{})
			aTotRegs[6] := len(aLog)
		EndIf	
		Aadd(aLog[aTotRegs[6]], STR0009 ) // "Nao foi encontrada nenhuma inconsistencia durante a geracao."
	EndIf   
Next

If aTotRegs[7] == 0
	cLog := STR0057+Chr(13) // "Informaes para gerar o arquivo texto do Quadro de Pessoal."
	Aadd(aTitle,cLog)  
	Aadd(aLog,{})
	aTotRegs[7] := len(aLog)
	Aadd(aLog[aTotRegs[7]], STR0003) //"Parmetros"
	For nY := 1 To Len(aParamQP)
		Aadd(aLog[aTotRegs[7]], aParamQP[nY][1]+aParamQP[nY][2] )
	Next
EndIf	

fMakeLog(aLog,aTitle,,,"GPEM775",STR0011,"G","L",,.F.) // "Log de ocorrncias da gerao do arquivo texto do Quadro de Pessoal"
Return Nil

/*


Ŀ
Funo    f775GrvCab Autor  Rogerio Vaz Melonio    Data 03/09/2008
Ĵ
Descrio  Cabecalho dos arquivos a serem gerados                     
ٱ


*/
Function f775GrvCab(cTipo,cFilAtu)
Local nX
// Monta os cabecalhos dos arquivos conforme o tipo de registro
//Ŀ
// Variaveis utilizadas para parametros                         
//
//Ŀ
// mv_par01 - Filial de                                         
// mv_par02 - Filial Ate                                        
// mv_par03 - Ano/Mes Referencia                                
// mv_par04 - Nome do Arquivo                                   
// mv_par05 - Local do Arquivo                                  
// mv_par06 - Capital Social                                    
// mv_par07 - % Privado Nacional                                
// mv_par08 - % Estrangeiro                                     
// mv_par09 - % Publico                                         
// mv_par10 - Volume de Negocios                                
// mv_par11 - Nome de Contato                                   
// mv_par12 - Fone 1                                            
// mv_par13 - Fone 2                                            
// mv_par14 - Fax                                               
// mv_par15 - Email                                             
//
If cTipo == '00'
	/*/
	cTipo == '00'
	Estrutura do Registro de Contato					
	Descricao						Conteudo	Tipo	Tamanho		Obg 	Varavel / Campo
	Tipo de Registro 				00			A 			2		S 		"00"
	Nome(s) do Contato							A 			100		S 		Padr(mv_par11,100)
	Contato					
	Telefone 1 									N 			9		S 		mv_par12
	Telefone 2 									N 			9				mv_par13
	Fax 										N 			9				mv_par14
	Correio Eletrnico							A 			60		S 		Padr(mv_par15,60)
	Aplicativo de Gerao da Informao 					
	Nome da Empresa Produtora 					A 			60				space(60)
	Nome do Aplicativo							A 			40				space(40)
	Verso do Aplicativo						A 			20				space(20)
	Tamanho do registro										309		
	/*/
	cCab := cTipo 				// Tipo de Registro
	cCab += Padr(mv_par11,100) 	// Nome(s) do Contato
	cCab += mv_par12 			// Fone 1
	cCab += mv_par13 			// Fone 2
	cCab += mv_par14 			// Fax
	cCab += Padr(mv_par15,60) 	// Email
	cCab += Space(60)           // Brancos
	cCab += Space(40)			// Brancos
	cCab += Space(20)			// Brancos
	cCab += cEOL 				// caracter de fim de linha 
	f775LogTipo(cTipo,Nil,Nil,Nil,Nil,Nil,Nil)
ElseIf cTipo == '10'
	/*/
	cTipo == '10'
	Estrutura do Registro da Empresa					
	Descricao						Conteudo	Tipo	Tamanho		Obg 	Varavel / Campo
	Tipo de Registro 				10			N 			2		S 		"10"
	N de Identificao Fiscal 					N 			9		S 		SM0->M0_CGC 
	Ano de Referncia da Informao 			N 			4		S 		Substr(mv_par03,1,4)
	Nome da Empresa 							A 			60		S 		SM0->M0_NOMECOM
	Localizao e Contatos da Sede da Empresa 					
	Morada 										A 			40		S 		SM0->M0_ENDCOB e caso esteja vazio, SM0->M0_ENDENT
	Localidade 									A 			20		S 		SM0->M0_BAIRCOB, se estiver vazio, SM0->M0_BAIRENT
	Cdigo Postal 								N 			7		S 		SM0->M0_CEPCOB, se estiver vazio, SM0->M0_CEPENT
	Cdigo Postal (descritivo) 					A 			20		S 		fDesc("SCG",SM0->M0_CEPCOB,"CG_DESCEP") ou fDesc("SCG", SM0-> M0_CEPENT, "CG_DESCEP")
	Telefone 									N 			9		S 		SM0->M0_TEL
	Fax 										N 			9				SM0->M0_FAX
	Correio Eletrnico 							A 			60				Padr(mv_par15,60)
	Cdigo de Distrito / Municpio / Freguesia 	N 			6		S 	
	Associaes Patronais 			 		
	Cdigo de Associao Patronal 				N 			3		
	Cdigo de Associao Patronal 				N			3		
	Cdigo de Associao Patronal 				N			3		
	Cdigo da Atividade Principal da Empresa 	N 			5		S 		SM0->M0_CNAE 
	Cdigo de Natureza Jurdica 				N 			2		S 		SM0->M0_NATJUR 
	Ano de Constituio da Empresa 				N 			4		S 	
	Nmero de Pessoas ao Servio da Empresa 	N 			5		S 		RecCount("RGP")
	Capital Social 				 	
	Montante 									N 			12				StrZero(mv_par06*100,12)	
	% Privado Nacional 				   			N 			4				StrZero(mv_par07*100,4)
	% Estrangeiro 					   			N 			4				StrZero(mv_par08*100,4)
	% Pblico 									N 			4				StrZero(mv_par09*100,4)
	Volume de negcios, ano anterior 			N 			12		S 		StrZero(mv_par10*100,12)
	/*/
	dbSelectArea("SM0")
	dbSeek(CEMPANT+cFilAtu) // posiciona SIGAMAT na Empresa/Filial que esta sendo processada
	cMorada := Iif(!Empty(SM0->M0_ENDCOB),SM0->M0_ENDCOB,SM0->M0_ENDENT)
	cLocalidade := Iif(!Empty(SM0->M0_BAIRCOB),SM0->M0_BAIRCOB,SM0->M0_BAIRENT)
	cCodPostal := Iif(!Empty(SM0->M0_CEPCOB),SM0->M0_CEPCOB,SM0->M0_CEPENT)
	cDescPostal := fDesc("SCG",cCodPostal,"CG_DESCEP")
	cCab := cTipo 							// Tipo de Registro
	cCab += Padr(SM0->M0_CGC,9)			 	// NIF
	cCab += Substr(mv_par03,1,4)			// Ano/Mes de referencia
	cCab += Padr(SM0->M0_NOMECOM,60) 		// Nome da Empresa
	cCab += Padr(cMorada,40) 				// Morada
	cCab += Padr(cLocalidade,20) 			// Localidade
	cCab += Padr(cCodPostal,7)				// Codigo Postal
	cCab += Padr(cDescPostal,20) 			// Descricao Codigo Postal
	cCab += Padr(SM0->M0_TEL,9) 			// Telefone
	cCab += Padr(SM0->M0_FAX,9) 			// Fax
	cCab += Padr(mv_par15,60) 				// Email
	cCab += Padr(SM0->M0_CODMUN,6)			// Cdigo de Distrito / Municpio / Freguesia
	cCab += Padr(SM0->M0_ASSPAT1,3) 		// Associacao Patronal 1
	cCab += Padr(SM0->M0_ASSPAT2,3)			// Associacao Patronal 2
	cCab += Padr(SM0->M0_ASSPAT3,3)	 		// Associacao Patronal 3
	cCab += Padr(SM0->M0_CNAE,5) 	        // Cod. Atividade da Empresa
	cCab += Padr(SM0->M0_NATJUR,2) 			// Cod. Natureza Juridica
	cCab += IIF(EMPTY(SM0->M0_NIRE),"0000",STRZERO(SM0->M0_NIRE,4)) // Ano de Constituicao da empresa
	cCab += StrZero(nTotal,5) 				// Numero de empregados da empresa 
	cCab += StrZero(mv_par06*100,12)	 	// Capital Social
	cCab += StrZero(mv_par07*10,4)		 	// % privado nacional
	cCab += StrZero(mv_par08*10,4) 		// % estrangeiro
	cCab += StrZero(mv_par09*10,4) 		// % publico
	cCab += StrZero(mv_par10*100,12) 		// Volume de Negocios do ano anterior
	cCab += cEOL 				// caracter de fim de linha
	f775LogTipo(cTipo,cMorada,cLocalidade,cCodPostal,cDescPostal,Nil,Nil)
ElseIf cTipo == '20'
	/*/
	Estrutura do Registro do estabelecimento
	Descricao						Conteudo	Tipo	Tamanho		Obg 	Varavel / Campo
	Tipo de Registro 					20		N 			2		S 		"20"
	N do Estabelecimento 						N 			4		S 		RCO_NESTAB
	Nome do Estabelecimento 					A 			60		S 		RCO_NOME
	Localizao e Contatos do Estabelecimento 					
	Morada 										A 			40		S 		RCO_END
	Localidade 									A 			20		S 		RCO_LOCAL
	Cdigo Postal 								N 			7		S 		RCO_CEP
	Cdigo Postal (descritivo) 					A 			20		S 		fDesc("SCG",RCO->RCO_CEP,"CG_DESCEP"))
	Telefone 									N 			9		S 		RCO_FONE
	Fax 										N 			9				RCO_FAX
	Correio Eletrnico 							A 			60				RCO_EMAIL
	Cdigo de Distrito / Municpio / Freguesia 	N 			6		S 		RCO_BAIRRO
	Nmero de Contribuinte da Segurana Social 	N 			11				RCO_NISS
	Cd. Atividade Principal do Estabelecimento	N 			5		S 		RCO_ATIVID
	N de pessoas ao servio no Estabelecimento N			5		S 		calcular
	/*/
	dbSelectArea("RCO")
	dbSetOrder(1) // RCO_FILIAL+RCO_CODIGO
	cCab := ""
	For nX := 1 To Len(aEstab)
		If dbSeek(xFilial("RCO")+aEstab[nX][1])
			cCab += cTipo 								// Tipo de Registro
			cCab +=  StrZero(Val(RCO->RCO_NESTAB),4) 	// 2. Nro do Estabelecimento 
			cCab +=  Padr(RCO->RCO_NOME,60) 	// 3. Nome do Estabelecimento 
			cCab +=  Padr(RCO->RCO_END,40) 		// 4.1. Morada 
			cCab +=  Padr(RCO->RCO_LOCAL,20) 	// 4.2. Localidade 
			cCab +=  RCO->RCO_CEP 				// 4.3. Cdigo Postal 
			cDescPostal := fDesc("SCG",RCO->RCO_CEP,"CG_DESCEP")
			cCab +=  Padr(cDescPostal,20) 		// 4.4. Cdigo Postal (descritivo) 
			cCab +=  Padr(RCO->RCO_FONE,9) 		// 4.5. Telefone 
			cCab +=  Padr(RCO->RCO_FAX,9) 		// 4.6. Fax 
			cCab +=  Padr(RCO->RCO_EMAIL,60) 	// 4.7. Correio Eletrnico 
			cCab +=  RCO->RCO_BAIRRO 			// 4.8. Cdigo de Distrito / Municpio / Freguesia 
			cCab +=  StrZero(Val(RCO->RCO_NISS),11) // 5. Nmero de Contribuinte da Segurana Social 
			cCab +=  StrZero(Val(RCO->RCO_ATIVID),5) // 6. Cdigo da Atividade Principal do Estabelecimento
			cCab +=  StrZero(aEstab[nX][2],5) 		// 7. N de pessoas ao servio no Estabelecimento 
			cCab += cEOL 			// caracter de fim de linha
			f775LogTipo(cTipo,Nil,Nil,Nil,cDescPostal,Nil,Nil)
		Else
			If aTotRegs[8] == 0
				cLog := STR0040 // "Estabelecimento no Quadro de Pessoal no encontrado no Cadastro de Estabelecimentos"
				Aadd(aTitle,cLog)  
				Aadd(aLog,{})
				aTotRegs[8] := len(aLog)
			EndIf	
			Aadd(aLog[aTotRegs[8]], aEstab[nX][1]) 
		Endif
	Next
ElseIf cTipo == '30'
	/*/
	Estrutura do Registro do estabelecimento/IRCT
	Descricao						Conteudo	Tipo	Tamanho		Obg 	Varavel / Campo
	Tipo de Registro 					30		N			2		S 		"30"
	N do Estabelecimento 						N 			4		S 		RCO->RCO_NESTAB
	Instrumento de Regulamentao Coletiva de Trabalho (IRCT) 					
	Cdigo do IRCT 								N 			5		S 		SRA->RA_CODIRCT
	N de Boletim Trabalho e Emprego (BTE)		N 			2				RGD->RGD_NUMBOL
	Data de publicao no BTE (AAAAMMDD)					8				dtos(RGD->RGD_DTPUBL)
	Data incio eficcia tab salarial (AAAAMM) 	N 			6				Substr(dtos(RGD->RGD_DTEFIC),1,6)
	Controle de N de Trabalhadores abrangidos 	N 			5		S 		calcular
	/*/
	dbSelectArea("RGD")
	dbSetOrder(1) // RGD_FILIAL+RGD_CODIGO
	dbSelectArea("RCO")
	dbSetOrder(1) // RCO_FILIAL+RCO_CODIGO
	cCab := ""
	For nX := 1 To Len(aIRCT)
		dbSelectArea("RCO")
		If dbSeek(xFilial("RCO")+aIRCT[nX][1]) // busco no RCO o estabelecimento 
			cCodEstab := aIRCT[nX][1]
			cCodIRCT  := aIRCT[nX][2]
			dbSelectArea("RGD")
			If dbSeek(cFilAtu+aIRCT[nX][2]) // busco no RGD o IRCT
				cCab += cTipo 							// Tipo de Registro
				cCab += StrZero(Val(RCO->RCO_NESTAB),4)	// 2. N do Estabelecimento 
				cCab += aIRCT[nX][2] 					// 3.1. - Cdigo do IRCT 
				cCab += StrZero(Val(RGD->RGD_NUMBOL),2)	// 3.2. - N de Boletim Trabalho e Emprego (BTE)
				cCab += dtos(RGD->RGD_DTPUBL) 			// 3.3 - Data de publicao no BTE (AAAAMMDD)
				cCab += Substr(dtos(RGD->RGD_DTEFIC),1,6) // 3.4. - Data de incio de eficcia da ltima tabela salarial (AAAAMM) 
				cCab += StrZero(aIRCT[nX][3],5)	// 4. Controle de N de Trabalhadores abrangidos 
				cCab += cEOL 					// caracter de fim de linha
			Else
				If aTotRegs[9] == 0
					cLog := STR0041 // "IRCT no Quadro de Pessoal no encontrado no Cadastro de IRCT"
					Aadd(aTitle,cLog)  
					Aadd(aLog,{})
					aTotRegs[9] := len(aLog)
				EndIf	
				Aadd(aLog[aTotRegs[9]], aIRCT[nX][2]) 
			Endif
		Else
			If aTotRegs[8] == 0
				cLog := STR0040 // "Estabelecimento no Quadro de Pessoal no encontrado no Cadastro de Estabelecimentos"
				Aadd(aTitle,cLog)  
				Aadd(aLog,{})
				aTotRegs[8] := len(aLog)
			EndIf	
			Aadd(aLog[aTotRegs[8]], aIRCT[nX][1]) 
		Endif
		f775LogTipo(cTipo,Nil,Nil,Nil,Nil,cCodEstab,cCodIRCT)
	Next
Endif
Return(cCab)

/*


Ŀ
Funo    f775GrvDet Autor  Rogerio Vaz Melonio    Data 03/09/2008
Ĵ
Descrio  Detalhe dos funcionarios para gravar no arquivo TXT        
ٱ


*/
Function f775GrvDet() // monta a linha com dados do funcionario
dbSelectArea("SRA")
cDet := ""
If dbSeek(RGP->RGP_FILIAL+RGP->RGP_MATRIC)
	contafun := soma1(contafun)
	cSexo := Iif(SRA->RA_SEXO=="M","1","2")
	cUltPromo := Iif(!Empty(RGP->RGP_ULTPRO),Substr(dtos(RGP->RGP_ULTPRO),1,6),Repl("0",6))
	cDet := "40"				// Tipo de Registro
	cDet +=  RGP->RGP_CODEST	// 2 - N. do Estabelecimento 
	cDet +=  RGP->RGP_IRCT 		// 3. Cdigo do IRCT 
	cDet +=  Substr(contafun,Len(contafun)-1,2)	// 4 N. de Linha 
	cDet +=  Padr(SRA->RA_NOME,40) 				// 5. Nome do Trabalhador 
	cDet +=  Substr(dtos(SRA->RA_NASC),1,6) 	// Nascimento 
	cDet +=  Substr(dtos(SRA->RA_ADMISSA),1,6) 	// Admisso na empresa 
	cDet +=  cUltPromo 							// Ultima Promoo
	cDet +=  StrZero(RGP->RGP_REMUBA*100,8) 	// Base 
	cDet +=  StrZero(RGP->RGP_PREMIO*100,8) 	// Prmios e Subsdios Regulares 
	cDet +=  StrZero(RGP->RGP_TRBSUP*100,8) 	// Trabalho Suplementar (efetuado em Outubro) 
	cDet +=  StrZero(RGP->RGP_IRREGU*100,8) 	// Prestaes Irregulares (pagas em Outubro) 
	cDet +=  StrZero(RGP->RGP_HRNORM,3) 	// Horas Normais 
	cDet +=  StrZero(RGP->RGP_HRSUPL,3) 	// Horas Suplementares (efetuadas em Outubro)
	cDet +=  StrZero(RGP->RGP_PERNOR,3) 	// Perodo Normal de Trabalho Semanal (PNT) 
	cDet +=  RGP->RGP_CONTRO 	// Controle de Remunerao Base 
	cDet +=  RGP->RGP_CATEGO 	// Cdigo de Categoria Profissional 
	cDet +=  RGP->RGP_CODPRO 	// Cdigo de Profisso 
	cDet +=  RGP->RGP_CODHAB 	// Cdigo de Habilitao 
	cDet +=  RGP->RGP_CODNAC 	// Cdigo de Nacionalidade 
	cDet +=  "0" 			 	// Situao face  Lei n 20/98 
	cDet +=  RGP->RGP_SITPRO 	// Situao na Profisso 
	cDet +=  RGP->RGP_TIPOCO 	// Tipo de Contrato 
	cDet +=  RGP->RGP_REGIME 	// Regime de Durao do Trabalho 
	cDet +=  cSexo 			 	// Sexo 
	cDet +=  SRA->RA_NISSPS  	// N de Beneficirio da Segurana Social 
	cDet += cEOL 				// caracter de fim de linha
	f775LogTipo('40',Nil,Nil,Nil,Nil,Nil,Nil)
Else
	cDet := ""
	If aTotRegs[10] == 0
		cLog := STR0042 // "Funcionario no quadro de pessoal nao encontrado no Cadastro de Funcionarios"
		Aadd(aTitle,cLog)  
		Aadd(aLog,{})
		aTotRegs[10] := len(aLog)
	EndIf	
	Aadd(aLog[aTotRegs[10]], RGP->RGP_MATRIC+"-"+AllTrim(RGP->RGP_MATRIC)) 
Endif
dbSelectArea("RGP")
Return(cDet)

/*


Ŀ
Funo    f775ContaFunc Autor  Rogerio Vaz Melonio    Data 03/09/2008
Ĵ
Descrio  Rotina para totalizar funcionarios por estabeleciento / IRCT  
ٱ


*/
Function f775ContaFunc(cFilAtu)
Local nX
// Select para totalizar funcionario por estabelecimento/IRCT da filia que esta sendo processada
cQry1 := "SELECT COUNT(RGP_MATRIC) FUNC_IRCT,RGP_CODEST,RGP_IRCT  FROM " + RetSQLName("RGP")+" RGP "
cQry1 += " WHERE RGP.RGP_FILIAL='"+cFilAtu+"'"
cQry1 += " AND RGP.RGP_ANOMES = '"+mv_par03+"' AND RGP.D_E_L_E_T_ <> '*' "
cQry1 += " GROUP BY RGP_FILIAL,RGP_CODEST,RGP_IRCT "
cQry1 += " ORDER BY RGP_CODEST,RGP_IRCT "
cQry1 := ChangeQuery(cQry1)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQry1),"TMP1", .F., .T.)
dbSelectArea("TMP1")
dbGoTop()
While !Eof()
	aAdd(aIRCT,{TMP1->RGP_CODEST,TMP1->RGP_IRCT,TMP1->FUNC_IRCT})
	dbSkip()
Enddo
dbCloseArea()
aSort(aIRCT,,,{|x,y| x[1]+x[2] < y[1]+y[2] }) // coloca em ordem por Estabelecimento + IRCT
For nX := 1 To Len(aIRCT)
	nPosEst	 := aScan(aEstab,{|x| x[1] == aIRCT[nX][1]})
	If nPosEst == 0
		aAdd(aEstab,{aIRCT[nX][1],0})
		nPosEst := Len(aEstab)
	Endif
	aEstab[nPosEst][2] += aIRCT[nX][3] // acumula a quantidade de funcionarios do IRCT ao Estabelecimento
	nTotal += aIRCT[nX][3] // acumula a quantidade de funcionarios do Estabelecimento para a empresa
Next
Return