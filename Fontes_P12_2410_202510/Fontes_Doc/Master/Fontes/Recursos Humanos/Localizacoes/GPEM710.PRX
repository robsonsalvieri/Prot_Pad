#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPEM710.CH"

/*/


Ŀ
Programa  GPEM710   Autor  Microsiga       		        Data   11/28/06
Ĵ
Desc.      GERA ARQUIVO DA DECLARACAO ANUAL - MEXICO 		                 
                                                            		         
Ĵ
Uso        AP                                                         	     
Ĵ
             ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data      BOPS/FNC    Motivo da Alteracao                     
Ĵ
Valdeci J.  07/01/2008-----------Alteracao da tabela S015 (Def. Tabelas) p/
                      -----------a tabela Fisica RGC - Localidade de Pago. 
Silvia T    08/02/2008140541	  Acumular usando RD_DATARQ,retirar rotina  
                            	  de Traj.Laboral                           
Erika K     02/06/2008-----------Alteracao do nome do pergunte para integra
                            	  cao de dicionarios Mexico e R1.2.         
Valdeci Lira23/01/2009001218/2009Ajuste para nova vigencia de 26/12/2008   
Igor Franzoi22/09/2009021020/2009Busca da Descricao do Estado conforme Loc.
			   22/09/2009021020/2009Pagamento - RGC_ESTADO (Descricao no SX5) 
Marcelo     13/01/2010000596/2010Inclusao da funcao AjustaSX1 para alterar 
                            	  o dicionario de dados SX1.                
Francisco Jr05/02/2010002364/2010Compatibilizacao de Filial e ajusta sx pa-
                            	  ra Gestao Corporativa.                    
R.Berti     12/12/2012     TFWBL2Tratam.p/ demitidos anteriorm. ao ano base
                            	  onde meses inicial/final=mes pagto.do PTU.
Laura Medina21/05/2013     THGXPISe hizo un cambio para que el programa    
                            	  actualice el campo RCV_STATUS='S-Sistema'.
Laura Medina18/06/2013     THIDJRAdecuacion del tamao de la sucursal en el
                            	  grupo de preguntas.                       
Raul Ortiz  19/06/2014TPVLZC     Se agrega llamado a la funcion fBuscaTraj.
L Samaniego 24/06/2014TPXLBP     Se validad si aFunc tiene datos, si esta  
                                 vacio no se graban datos en RCV.          
Ĵ
Jonathan Glz|04/05/2015 PCREQ-4256Se elimina la funcion AjustaSX1 con motivo
            |                     de adecuacion a nuevas estructuras en SXs 
            |                     de Version 12                             
Dora Vega   |27/04/2017 MMI-4482  Merge de replica del issue MMI-265. Nueva 
            |                     funcion fAniosTrb para el calculo correcto
            |                     de anios trabajados en archivo DIM (MEX)  
ٱ


/*/
Function GPEM710()
	Local nOpca 		:= 0
	Local aSays		:= {}
	Local aButtons	:= {} //<== arrays locais de preferencia

	Private cCadastro := OemToAnsi(STR0001) //"Generacion archivo de la Declaracion Anual"

		If  RCV->(FieldPos("RCV_STATUS"))=0
			cHelp := OemToAnsi( STR0015 )//"El campo RCV_STATUS no existe, ejecute la rutina RHUPDMOD. "
			Help( ' ' , 1 , "GP710NOCPO" , , cHelp , 1 , 0 )
			Return Nil
		Endif

		Pergunte("GPEM710",.F.)

		AADD(aSays,OemToAnsi(STR0002) )  //"Este programa genera el archivo de la Declaracion Anual"

		AADD(aButtons, { 5,.T.,{|| Pergunte("GPEM710",.T. ) } } )
		AADD(aButtons, { 1,.T.,{|o| nOpca := 1,IF(gpm710OK(),FechaBatch(),nOpca:=0) }} )
		AADD(aButtons, { 2,.T.,{|o| FechaBatch() }} )

		FormBatch( cCadastro, aSays, aButtons )

		//Ŀ
		// Variaveis utilizadas para parametros                         
		//
		//Ŀ
		// mv_par01 - Roteiros                                          
		// mv_par02 - Roteiros (continuacao)                            
		// mv_par03 - Filial de                                         
		// mv_par04 - Filial Ate                                        
		// mv_par05 - Matricula de                                      
		// mv_par06 - Matricula Ate                                     
		// mv_par07 - Centralizar na Filial                             
		// mv_par08 - Ano Base                                          
		//
		IF nOpca == 1
			ProcGpe({|lEnd| GPM710Processa()})  // Chamada do Processamento
		Endif

Return Nil

/*/


ͻ
Funcao    GPM710ProcessaAutor  Silvia Taguti      Data   21/11/06  
͹
Desc.                                                                  
                                                                       
͹
Uso        AP                                                          
ͼ


/*/
Static Function GPM710Processa()
	//Ŀ
	// Define Variaveis LOCAIS DO PROGRAMA                          
	//
	Local cFilAnterior
	Local aInfo 			:= { }
	Local aCodFol 		:= {}   // Matriz com Codigo da folha
	Local cMesPag
	Local cVerifAdt 		:= " "
	Local nX
	Local nA
	Local nI
	Local nPercCpmf		:= 0
	Local cVerba
	Local nPos 			:= 0
	Local cQuery
	Local cChave
	Local nMax 			:= 0
	Local nMin 			:= 0
	Local nPont 			:= 0
	Local cAliasSRD 		:= "SRD"
	Local cMat
	Local cFil
	Local cPutFilAnt		:= cFilAnt
	Local lLimpaTudo		:= .F.
	Local aLog				:= {}
	Local aTitle			:= {}
	Local aTotRegs		:= array(1)
	Local lTemCpoDFE		:= ""
	Local cVbPensao		:= ""
	//Ŀ
	// Define Variaveis PRIVADAS BASICAS                            
	//
	Private aCRA 		:= { STR0003,STR0004,STR0005 } //"Confirma"###"Redigita"###"Abandona"

	//Ŀ
	// Define Variaveis PRIVADAS DO PROGRAMA                        
	//
	Private cInfOut 		:= ""
	Private cDescri 		:= ""
	Private aFunc 		:= {}
	Private cFilDe		:= mv_par03
	Private cFilAte		:= mv_par04
	Private cMatDe		:= mv_par05
	Private cMatAte		:= mv_par06
	Private cCentra		:= mv_par07
	Private cAno			:= strzero(MV_PAR08,4)
	Private nTamCpf		:= TamSX3("RA_CIC")[1]
	Private aRoteiros		:= {}
	Private nVlAnual  	:= 0
	Private cAnoRef		:= cAno
	//Ŀ
	//Eliminar na proxima versao.
	//
	Private cArqCompl	:= "GPCMPL"+cEmpAnt

		mv_par01 += mv_par02

		If Len(mv_par01) > 0
			SelecRoteiros(Nil,1)
		EndIf

		aFill(aTotRegs,0)

		dbSelectArea( "SRA" )
		dbSetOrder(1)
		dbSeek( cFilDe + cMatDe , .T. )
		While SRA->(!eof())
			cChave	:= SRA->RA_FILIAL + SRA->RA_MAT + SRA->RA_CIC
			dbSelectArea("RCW")
			If dbSeek( cChave )
				If empty(RCW->RCW_ANO )
					lLimpaTudo := .T.
				EndIf
				Exit
			Endif
			SRA->( dbskip() )
		EndDo

		//Ŀ
		// Limpar o arquivo da DIRF                                     
		//
		If lLimpaTudo
			dbSelectArea( "RCV" )
			If RecCount() > 0
				Aviso(OemToAnsi(STR0009),OemToAnsi(STR0010),{OemToAnsi(STR0011)}) //"Atencao"#"As tabelas RCV e RCW serao limpas para que a partir de agora o historico possa ser armazenado."#"OK"

				#IFDEF TOP
					If ( TcSrvType()!="AS/400" )
						//
						//Verifica qual eh o maior e o menor
						//
						cQuery := "SELECT MIN(R_E_C_N_O_) MINRECNO,"
						cQuery +=	 "MAX(R_E_C_N_O_) MAXRECNO "
						cQuery += "FROM "+RetSqlName("RCV")+" RCV "

						cQuery := ChangeQuery(cQuery)

						dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"RCW710DELE")

						nMax := RCW710DELE->MAXRECNO
						nMin := RCW710DELE->MINRECNO
						dbCloseArea()
						dbSelectArea("RCV")
						//Ŀ
						//Monta a string de execucao no banco
						//
						cQuery := "DELETE FROM "+RetSqlName("RCV")+" "
						cQuery += "WHERE "
						//Ŀ
						//Executa a string de execucao no banco para os proximos 1024 registro
						//
						For nPont := nMin To nMax STEP 1024
							cChave := "R_E_C_N_O_>="+Str(nPont,10,0)+" AND R_E_C_N_O_<="+Str(nPont+1023,10,0)+""
							TcSqlExec(cQuery+cChave)
						Next nPont
						//Ŀ
						//A tabela eh fechada para restaurar o buffer da aplicacao
						//
						dbSelectArea("RCV")
						dbCloseArea()
						ChkFile("RCV",.F.)
						//
						//Verifica qual eh o maior e o menor
						//
						cQuery := "SELECT MIN(R_E_C_N_O_) MINRECNO,"
						cQuery +=	 "MAX(R_E_C_N_O_) MAXRECNO "
						cQuery += "FROM "+RetSqlName("RCW")+" RCW "

						cQuery := ChangeQuery(cQuery)

						dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"RCW710DELE")

						nMax := RCW710DELE->MAXRECNO
						nMin := RCW710DELE->MINRECNO
						dbCloseArea()
						dbSelectArea("RCW")
						//Ŀ
						//Monta a string de execucao no banco
						//
						cQuery := "DELETE FROM "+RetSqlName("RCW")+" "
						cQuery += "WHERE "
						//Ŀ
						//Executa a string de execucao no banco para os proximos 1024 registro
						//
						For nPont := nMin To nMax STEP 1024
							cChave := "R_E_C_N_O_>="+Str(nPont,10,0)+" AND R_E_C_N_O_<="+Str(nPont+1023,10,0)+""
							TcSqlExec(cQuery+cChave)
						Next nPont
						//Ŀ
						//A tabela eh fechada para restaurar o buffer da aplicacao
						//
						dbSelectArea("RCW")
						dbCloseArea()
						ChkFile("RCW",.F.)
					Else //- Opcao para AS400
				#ELSE
						If Ma280Flock("RCV")           // Abre Arquivo RCV Exclusivo
							OpenIndX("RCV",.F.)
							Zap                        // Limpa Arquivo
							dbCloseArea()
							ChkFile("RCV",.F.)
						    If Ma280Flock("RCW")       // Abre Arquivo RCW Exclusivo
								OpenIndX("RCW",.F.)
							    Zap                    // Limpa Arquivo
							    dbCloseArea()
							    ChkFile("RCW",.F.)
							Else
								cHelp := OemToAnsi( STR0014 )//"Nao foi possivel abrir os arquivos RCV e RCW no modo exclusivo
								Help( ' ' , 1 , "GP710NOPEN" , , cHelp , 1 , 0 )
								Return Nil
						    EndIF
						Else
							cHelp := OemToAnsi( STR0014 )//"Nao foi possivel abrir os arquivos RCV e RCW no modo exclusivo
							Help( ' ' , 1 , "GP710NOPEN" , , cHelp , 1 , 0 )
							Return Nil
						Endif
				#ENDIF

				#IFDEF TOP
					EndIf
				#ENDIF

				If File(cArqCompl+getDbExtension())
					fErase(cArqCompl+getDbExtension())
				EndIf
				If File(cArqCompl+retIndExt())
					fErase(cArqCompl+retIndExt())
				EndIf
			Endif
		Else

		//Ŀ
		//Limpar RCW se forem encontrados no RCW informacoes dos funcionarios solicitados.		 
		//Apenas serao limpos os dados dos funcionario selecionados para geracao.               
		//
		#IFDEF TOP
			If ( TcSrvType()!="AS/400" )

				cQuery := "DELETE FROM "+RetSqlName("RCW")+" "
				cQuery += "WHERE "
			    cQuery += " RCW_FILIAL 		>=	'"	+ cFilDe 	+ "'"
			    cQuery += " AND RCW_FILIAL 	<= 	'"	+ cFilAte 	+ "'"
			 	cQuery += " AND RCW_MAT 		>= 	'" 	+ cMatDe 	+ "'"
			 	cQuery += " AND RCW_MAT 		<= 	'" 	+ cMatAte 	+ "'"
			    cQuery += " AND RCW_ANO 		=  	'" 	+ cAno 		+ "'"
				cQuery += " AND EXISTS("
				cQuery += " SELECT R_E_C_N_O_ FROM " + RetSqlName("SRA") +""
				cQuery += " WHERE "
				cQuery += " RA_FILIAL = RCW_FILIAL "
				cQuery += " AND RA_MAT = RCW_MAT "
				cQuery += " AND D_E_L_E_T_ = ' ')"
				TcSqlExec(cQuery)
				dbSelectArea("RCW")
				dbCloseArea()
				ChkFile("RCW",.F.)
			Else //- Opcao para AS400
		#ELSE
			dbSelectArea("SRA")
			SRA->( dbSeek(cFilDe+cMatDe,.T.))
			While SRA->( !EOF())

				If SRA->RA_FILIAL < cFilDe .or. SRA->RA_FILIAL > cFilAte
					SRA->( dbSkip() )
					loop
				Endif
				If SRA->RA_MAT < cMatDe .or. SRA->RA_MAT > cMatAte
					SRA->( dbskip() )
					loop
				EndIf

				cChave	:= SRA->RA_FILIAL+ SRA->RA_MAT+ SRA->RA_CIC

				dbSelectArea("RCW")
				RCW->(dbSeek( cChave ) )
				While RCW->( !eof()) .and. RCW->RCW_FILIAL+RCW->RCW_MAT+RCW->RCW_RFC == cChave
					RecLock("RCW",.F.,.T.)
					dbDelete()
					MsUnlock()
					RCW->(dbSkip())
				EndDo
				SRA->( dbSkip() )
			EndDo
		#ENDIF
			#IFDEF TOP
			EndIf
			#ENDIF

		EndIf

		If cCentra # Space(FwGetTamFilial)  // Filiais devem ser centralizadas na filial
			If !fInfo(@aInfo,cCentra)
				Return
			Endif
			cCgc := aInfo[8]
		Endif

		//Ŀ
		//Montagem das Datas para a Query
		//
		cDtPesqI := cAno + "01"
		cDtPesqF := cAno + "12"

		dbSelectArea( "SRQ" )
		lTemCpoDFE := ( FieldPos( "RQ_VERBDFE" ) # 0 )

		//Ŀ
		// Posiciona Ponteiro "DE" Informado                            
		//
		dbSelectArea( "SRA" )
		dbSetOrder(1)
		dbSeek( cFilDe + cMatDe , .T. )

		GPProcRegua(SRA->(RecCount()))

		cFilAnterior  := Replicate("!", FwGetTamFilial)

		While SRA->(!Eof()) .And. SRA->RA_FILIAL + SRA->RA_MAT <= cFilAte + cMatAte

			//Ŀ
			// Movimenta cursor para movimentacao da barra                  
			//
			GPIncProc(SRA->RA_FILIAL+" - "+SRA->RA_MAT+" - "+SRA->RA_NOME)

			If SRA->RA_FILIAL < cFilDe .or. SRA->RA_FILIAL > cFilAte
				SRA->( dbSkip() )
				loop
			Endif
			If SRA->RA_MAT < cMatDe .or. SRA->RA_MAT > cMatAte
				SRA->( dbskip() )
				loop
			EndIf

			If SRA->RA_FILIAL # cFilAnterior
				cFilAnterior := SRA->RA_FILIAL
				//Ŀ
				// Carrega Variaveis CODIGOS DA FOLHA                           
				//
				If !FP_CODFOL(@aCodFol,cFilAnterior)
					Exit
				Endif
				If cCentra == Space(FwGetTamFilial)
					If ! fInfo(@aInfo,cFilAnterior)
						Exit
					Endif
					cCgc := aInfo[8]
				Endif
			Endif

			nDel 			:= 0
			cInfOut 		:= ""
			aFunc   		:= {}

			cInfOut     := ""
			lGravouAlgum:= .F.
			nVlAnual 	:= 0

			//Ŀ
			// Buscar Valores no Acumulado "SRD"                            
			//
			cMat := SRA->RA_MAT
			cFil := SRA->RA_FILIAL
			dbSelectArea("SRD")
			#IFDEF TOP
				If TcSrvType()<>"AS/400"
					lQuery 		:= .T.
					cAliasSRD 	:= "m710MontSRD"
					aStru  		:= SRD->(dbStruct())
					cQuery 		:= "SELECT * "
					cQuery 		+= " FROM "+	RetSqlName("SRD")
					cQuery 		+= " WHERE RD_FILIAL  ='" + cFil + "'"
					cQuery 		+= " AND RD_MAT     = '" + cMat+ "'"
					cQuery 		+= " AND RD_DATARQ BETWEEN '"+cDtPesqI+"' AND '"+cDtPesqF+"'"
					cQuery 		+= " AND D_E_L_E_T_ = ' ' "
					cQuery 		+= "ORDER BY "+SqlOrder(SRD->(IndexKey()))

					cQuery := ChangeQuery(cQuery)
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSRD,.T.,.T.)

					For nX := 1 To len(aStru)
						If aStru[nX][2] <> "C" .And. FieldPos(aStru[nX][1])<>0
							TcSetField(cAliasSRD,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
						EndIf
					Next nX
					dbSelectArea(cAliasSRD)
				Else
			#ENDIF
				(cAliasSRD)->(MsSeek(SRA->RA_FILIAL + SRA->RA_MAT + cDtPesqI,.T.))
			#IFDEF TOP
				EndIf
			#ENDIF


			While (cAliasSRD)->(!Eof()) .And. SRA->RA_FILIAL+SRA->RA_MAT == (cAliasSRD)->RD_FILIAL+(cAliasSRD)->RD_MAT
				//Ŀ
				//Verifica se o roteiro pertence aos Roteiros selecionados|
				//
				If (Ascan( aRoteiros, { |x| x[1] == (cAliasSRD)->RD_ROTEIR  } ) == 0)
					(cAliasSRD)->( dbSkip() )
					Loop
				Endif

				//Ŀ
				//Despreza os lanctos de transferencias de outras empresas
				//
		     	If (cAliasSRD)->RD_EMPRESA # cEmpAnt .And. !Empty((cAliasSRD)->RD_EMPRESA)
					(cAliasSRD)->( dbSkip() )
					Loop
				Endif

				If (cAliasSRD)->RD_PD == fGetCodFol("0473")
					nVlAnual := (cAliasSRD)->RD_VALOR
				Endif

				//Ŀ
				//Posiciona a Verba no SRV
				//
				PosSrv((cAliasSRD)->RD_PD,(cAliasSRD)->RD_FILIAL)
				cTipDirf := SRV->RV_DECLANU
				cDescri  := SRV->RV_DESC

				If Substr((cAliasSRD)->RD_DATARQ,1,4) == cAno
					//Ŀ
					//Mes de Pagamento para gravacao RCW
					//
					cMesPag := SUBSTR((cAliasSRD)->RD_DATARQ,5,2)

					If !cTipDirf$ "    "
						nVal := (cAliasSRD)->RD_VALOR

		       			If !(cTipDirf $ "2G  |2H  |2L  |2X  |3H  |4J  |3N  |6U1 |6V1 |6Y1 |5WE |5WG |5XE |5XG |5A1E|5A1G|5L1E|5L1G|6T1 ")
		       				If SRV->RV_TIPOCOD == "2" //Desconto
								nVal *= -1
							Endif
		            	Endif
						//Ŀ
						// Grava no Arquivo de Movimento RCW                    
						//
						GravaFun(cMesPag,cTipDirf,nVal)
						lGravouAlgum := .T.
					Endif
				Endif
				(cAliasSRD)->( dbSkip())
			Enddo
			#IFDEF TOP
				dbSelectArea(cAliasSRD)
				dbCloseArea()
			#ENDIF

		//	If lGravouAlgum
				cFilAnt	:= SRA->RA_FILIAL
				//Ŀ
				//Gravar Cabealho da Declaracao Anual 
				//
				If Len(aFunc) > 0
					FGravaRCV(aInfo)
				EndIf
				//Ŀ
				//Gravar Beneficiarios e informacoes complementares
				//
				cInfOut 	:= ""
				cFilAnt := cPutFilAnt
		//	Endif
			dbSelectArea("SRA")
			dbSkip()
		Enddo

		fMakeLog(aLog,aTitle,,,"SE"+cAno,OemToAnsi(STR0013),"M","P",,.F.) //"Log de ocorrencias da Declaracao

Return Nil

/*/


ͻ
Funcao    GravaFun  Autor  Microsiga            Data   01/19/05   
͹
Desc.                                                                 
                                                                      
͹
Uso        AP                                                         
ͼ


/*/
Static Function GravaFun(cMesPag,cTipDirf,nVal)
	Local nPos	:= 0

		nPos := Ascan(aFunc,{ |x| x[1] = cMespag .And. x[2] = cTipDirf})
		If nPos > 0
			aFunc[nPos,3] += nVal
		Else
			Aadd(aFunc,{cMesPag,cTipDirf,nVal})
		Endif
Return

/*/


ͻ
Funcao    FGravaRCV Autor  Microsiga            Data   01/19/05   
͹
Desc.                                                                 
                                                                      
͹
Uso        AP                                                         
ͼ


/*/
Static Function FGravaRCV(aInfo)
	Local cChave		:= ""
	//Local cMesIni	:= ""
	Local cChaveAno	:= ""
	//Local cMesFin 	:= If( Year(SRA->RA_DEMISSA) == mv_par08 ,StrZero(Month(SRA->RA_DEMISSA),2),"12" )
	Local cSindic 	:= If(!Empty(SRA->RA_SINDICA),"X", " ")
	Local cPatCalcAn	:= If(nVlAnual > 0 ,"X", " ")
	Local cNomRep 	:= " "
	Local cRFCRep 	:= " "
	Local cCURPRep	:= " "
	Local nf			:= 0
	Local nNumdias	:= 0
	Local nAnosTrab	:= 0
	Local cChaveAsi	:= " "
	Local cEstado 	:= " "

	Private cMesIni	:= ""
	Private cMesFin 	:= ""

		/*If !Empty(SRA->RA_FECREI)
			cMesIni := If( Year(SRA->RA_FECREI) >= mv_par08 ,StrZero(Month(SRA->RA_FECREI),2),"01" )
		Else
			cMesIni := If( Year(SRA->RA_ADMISSA) >= mv_par08 ,StrZero(Month(SRA->RA_ADMISSA),2),"01" )
		EndIf*/

		//"	Para aquellos empleados dados de baja antes del 01/enero del anio en que se genera la declaracin y que
		//  contengan ingresos corresponden a PTU(Participacin de los trabajadores en las utilidades) pagado en el anio
		//  donde se genera la declaracin; Se deber anotar  el mes en el cual se efectu el pago del PTU "
		//Ŀ
		//Demitidos anteriormente ao ano base: Os meses inicial e final ficam iguais ao mes do PTU pago  
		//
		/*If !Empty(SRA->RA_DEMISSA) .And. Year(SRA->RA_DEMISSA) < mv_par08 .And. Len(aFunc) > 0
			cMesIni := aFunc[1,1]
			cMesFin := cMesIni
		EndIf*/

		dbSelectArea("RCJ")
		dbSetorder(1)
		If dbSeek(xFilial("RCJ")+SRA->RA_PROCES)
			cChaveAsi := RCJ->RCJ_CLASIM
		Endif

		cNomRep 	:= If(FTABELA("S021",1,4) <> Nil,FTABELA("S021",1,4)," ")
		cRFCRep 	:= If(FTABELA("S021",1,5) <> Nil,FTABELA("S021",1,5)," ")
		cCURPRep	:= If(FTABELA("S021",1,6) <> Nil,FTABELA("S021",1,6)," ")

		//nAux01:=fPosTab("S015",SRA->RA_KEYLOC,"=",4)
		//cEstado:= RDescLocPago(SRA->RA_KEYLOC)
		cEstado	:= fEstPag( SRA->RA_KEYLOC )

		//Ŀ
		// Grava no Arquivo de Cabecalho                 
		//
		cChave	:=  SRA->RA_FILIAL+SRA->RA_MAT+SRA->RA_CIC+cAno+cMesIni+cMesFin
		dbSelectArea("RCV")
		
		If !Empty(SRA->RA_DEMISSA) .And. Year(SRA->RA_DEMISSA) < mv_par08 .And. Len(aFunc) > 0
			// Neste caso, demitido em ano ant., precisa substituir o registro incorreto, caso encontre algum no ano atual
			cChaveAno :=  SRA->RA_FILIAL+SRA->RA_MAT+SRA->RA_CIC+cAno
			If dbSeek( cChaveAno )
				cChave	:=  cChaveAno
			EndIf
		EndIf

		If dbSeek( cChave )
			RecLock("RCV",.F.)
		Else
			RecLock("RCV",.T.)
		Endif

		fBuscaTraj({})
		
		If !Empty(SRA->RA_DEMISSA) .And. Year(SRA->RA_DEMISSA) < mv_par08 .And. Len(aFunc) > 0 .or. (!Empty(SRA->RA_FECREI) .and. Year(SRA->RA_FECREI) >= mv_par08 .and. Len(aFunc) > 0)
			cMesIni := aFunc[1,1]
			cMesFin := cMesIni
		ElseIf !Empty(SRA->RA_DEMISSA) .And. Year(SRA->RA_DEMISSA) == mv_par08 .And. Len(aFunc) > 0
			cMesIni := aFunc[1,1]
			cMesFin := aFunc[Len(aFunc),1]
		EndIf

		nNumDias := 0
		nAnosTrab:= 0

		cDiaFin := StrZero(F_ULTDIA(Ctod("01/"+cMesFin+"/"+cAno)),2)
		nNumdias := (Ctod(cDiaFin+"/"+cMesFin+"/"+cAno) - Ctod("01/"+cMesIni+"/"+cAno))+1
		nAnosTrab := NoRound(nNumDias/365)

		RCV->RCV_FILIAL 	:= SRA->RA_FILIAL
		RCV->RCV_MAT    	:= SRA->RA_MAT
		RCV->RCV_RFCFONT	:= aInfo[8]
		RCV->RCV_RFC		:= SRA->RA_CIC
		RCV->RCV_CURP    	:= SRA->RA_CURP
		RCV->RCV_PRINOM  	:= SRA->RA_PRINOME
		RCV->RCV_SEGNOM  	:= SRA->RA_SECNOME
		RCV->RCV_PRISOB  	:= SRA->RA_PRISOBR
		RCV->RCV_SEGSOB  	:= SRA->RA_SECSOBR
		RCV->RCV_NOMFONT 	:= aInfo[3]
		RCV->RCV_MESINI  	:= cMesIni
		RCV->RCV_MESFIN  	:= cMesFin
		RCV->RCV_ANO     	:= cAno
		RCV->RCV_CVEZON  	:= SRA->RA_CVEZON
		RCV->RCV_EMCALA  	:= cPatCalcAn
		RCV->RCV_SINDIC  	:= cSindic
		RCV->RCV_KEYLOC  	:= Alltrim(cEstado)
		RCV->RCV_NOMREP  	:= cNomRep
		RCV->RCV_RFCREP  	:= cRFCRep
		RCV->RCV_CURPRE  	:= cCURPRep
		RCV->RCV_NUMDIA  	:= StrZero(nNumDias,3)
		RCV->RCV_NUMANO  	:= StrZero(nAnosTrab,2)
		RCV->RCV_CLASIM	:= cChaveAsi
		RCV->RCV_STATUS	:= "S"
		MsUnlock()
		If Len(aFunc) > 0
			For nf	:= 1 To Len(aFunc)
				dbSelectArea("RCW")
				If dbSeek( SRA->RA_FILIAL + SRA->RA_MAT + SRA->RA_CIC + aFunc[nf,1] + aFunc[nf,2]  )
					RecLock("RCW",.F.)
				Else
					RecLock("RCW",.T.)
				Endif
				RCW->RCW_FILIAL  	:= SRA->RA_FILIAL
				RCW->RCW_MAT     	:= SRA->RA_MAT
				RCW->RCW_RFC  	:= SRA->RA_CIC
				RCW->RCW_MES     	:= aFunc[nf,1]
				RCW->RCW_TIPORE 	:= aFunc[nf,2]     //Mexico tamanho de 4
				RCW->RCW_VALOR   	:= aFunc[nf,3]
				RCW->RCW_ANO		:= cAno
				RCW->RCW_MESINI	:= cMesIni
				RCW->RCW_MESFIN 	:= cMesFin
				MsUnLock()
			Next nf
		Endif
Return Nil

/*


ͻ
Funcao    GPM710Ok  Autor  Silvia Taguti        Data   30/01/06   
͹
Desc.                                                                 
                                                                      
͹
Uso        AP                                                         
ͼ


*/
Static Function GPM710Ok()
Return (MsgYesNo(OemToAnsi(STR0006),OemToAnsi(STR0007))) //"Confirma configurao dos parmetros?"###"Ateno"

/*
Ŀ
Funo     fEstPag		Autor  Igor Franzoi      Data 22/09/2009
Ĵ
Descrio Localiza a descricao do Estado da Localidade Pago			
Ĵ
Sintaxe   fEstPag														
Ĵ
 Uso      GPEM710														
Ĵ
 Retorno  															
Ĵ
Parametros< Vide Parametros Formais >									
*/
Static Function fEstPag( cLocPag )
	Local aArea	:= GetArea()
	Local cEstado	:= ""

		dbSelectArea("RGC")
		RGC->(dbSetOrder(RetOrder("RGC","RGC_FILIAL+RGC_KEYLOC")))
		RGC->(dbSeek(xFilial("RGC")+cLocPag))

		cEstado := AllTrim(RGC->RGC_ESTADO)

		RestArea(aArea)

Return ( cEstado )
