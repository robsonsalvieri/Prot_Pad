#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPEM045CHI.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³GPEM045CHI³ Autor ³  Alex Hernandez                 ³ Data ³ 17/08/15 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Genera Archivo Declaraciones t - Chile                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS/FNC       ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Alex Hdez   ³04/01/16³PCREQ-7944      ³Se paso a la v12 a partir del fuente de   ³±±
±±³            ³        ³                ³v11 con la fecha 14/12/15                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Function GPEM045CHI()
	Local cString     := "RCV"  
	Private nPos      := 0				
	Private cPerg     := "GPEM045CHI"
	Private cTitulo   := STR0001 // "Genera Archivo Declaraciones" 		
	
	If Pergunte(cPerg, .T.)
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³mv_par01 - Rango Filial                           ³
		//³mv_par02 - Rango Empleado                         ³
		//³mv_par03 - RUT declarante                         ³
		//³mv_par04 - Año Base                               ³
		//³mv_par05 - Ruta y Nombre del archivo              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
		Processa({|lEnd| GPEM045Imp(@lEnd)},oemtoansi(STR0011),cTitulo, .T.)
	Endif		
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEM045IMPºAutor  ³ Alex Hernandez     º Data ³  28/12/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Processamento das Constancias                               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Generico                                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Parametros³ lEnd    = Terminal Reporte                                 º±±
±±³          ³ WnRel   = Nombre de la pregunta                            º±±
±±³          ³ cString = Nombre de la Tabla                               º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Static Function GPEM045IMP(lEnd) //lEnd,wnRel,cString
	Local cQuery     := ""
	Local cTRBCON    := ""
	Local cCadena    := ""
	Local cArqRCV    := CriaTrab( Nil, .F. )
	Local nSumMesC03 := 0
	Local nSumMesC04 := 0
	Local nSumMesC05 := 0
	Local nSumMesC08 := 0 
	Local nSumMesC33 := 0 
	Local nSumMesC09 := 0
	Local cRutNoTmp  := ""
	Local aMeses := {0,0,0,0,0,0,0,0,0,0,0,0}
	Local nA := 0	
	Local nB := 0
	Local nC := 0
	Local nD := 0
	Local nE := 0
	Local nF := 0
	Local nG := 0
	Local nH := 0	
	
 	Private cOrden   := " RCV_FOLIO, RCV_RFC, RCW_MES, RCW_TIPORE"
	Private dEmision	
	Private cMatTmp  := ""
	Private nMesC03  := 0
	Private nMesC04  := 0
	Private nMesC05  := 0
	Private nMesC08  := 0
	Private nMesC33  := 0
	Private nMesC09  := 0
	Private nFolio   := 0

	Private cFils    := ""
	Private cMats    := ""
	Private cRUT     := ALLTRIM(mv_par03)
	Private nAnoRef  := mv_par04
	Private cRutaNom := ALLTRIM(mv_par05)
	
	MakeSqlExpr(cPerg)
	cFils := ALLTRIM(mv_par01)
	cMats := ALLTRIM(mv_par02)

	//Total de Registros
	cQuery  := Gpr47SQL(cFils, cMats, cRUT, nAnoRef, "TOTAL")
	cTRBCON := ChangeQuery(cQuery)
	dbUseArea(.T., 'TOPCONN', TcGenQry( ,, cTRBCON ) ,cArqRCV, .T., .T.)
	ProcRegua((cArqRCV)->(Total))
	(cArqRCV)->(DbCloseArea())
	//Consulta
	cQuery  := Gpr47SQL(cFils, cMats, cRUT, nAnoRef, cOrden)
	cTRBCON := ChangeQuery(cQuery)
	dbUseArea(.T., 'TOPCONN', TcGenQry( ,, cTRBCON ) ,cArqRCV, .T., .T.)

	While (cArqRCV)->(!Eof()) 
		cFILtmp  := (cArqRCV)->RCV_FILIAL 
		cMATtmp  := (cArqRCV)->RCV_MAT  
		cRFCtmp  := (cArqRCV)->RCV_RFC 
		nMesTmp  := VAL((cArqRCV)->(RCW_MES))  	
		nFolio   := (cArqRCV)->(RCV_FOLIO)
	
		nSumMesC03 := 0
	 	nSumMesC04 := 0
		nSumMesC05 := 0
		nSumMesC08 := 0 
		nSumMesC33 := 0 
		nSumMesC09 := 0
		
		aMeses := {0,0,0,0,0,0,0,0,0,0,0,0}
		
		cRutNoF	:= ALLTRIM((cArqRCV)->(RCV_RFC))
	
		While !(cArqRCV)->(Eof()) .And. (cArqRCV)->RCV_FILIAL + (cArqRCV)->RCV_MAT + (cArqRCV)->RCV_RFC  ==	cFILtmp + 	cMATtmp + cRFCtmp 
			
			DO CASE 
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "A"
						nA 	:= (cArqRCV)->(RCW_VALOR)
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "B" 
						nB 	:= (cArqRCV)->(RCW_VALOR)
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "C" 
						nC 	:= (cArqRCV)->(RCW_VALOR)
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "D"
						nD 	:= (cArqRCV)->(RCW_VALOR)
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "E" 
						nE 	:= (cArqRCV)->(RCW_VALOR)
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "F"
						nF 	:= (cArqRCV)->(RCW_VALOR)
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "G"
						nG 	:= (cArqRCV)->(RCW_VALOR)
					CASE ALLTRIM((cArqRCV)->RCW_TIPORE)	== "H" 
						nH 	:= (cArqRCV)->(RCW_VALOR)
			ENDCASE	
			
			Incproc(oemtoansi(STR0010)) //"Procesando Archivo..."
			If lEnd
	      		MsgInfo(cCancel,STR0002)//"Fin"
	      		Exit
	      	Endif
			
			(cArqRCV)->(dbSkip()) 
			If nMesTmp	!= 	VAL((cArqRCV)->(RCW_MES))  .OR.		(cArqRCV)->(Eof())		.OR. cRFCTmp != (cArqRCV)->RCV_RFC	
				nAUX01  := FPOSTAB("S015", nAnoRef, "=", 4, nMesTmp, "=", 5)
				nFacAct := IF (nAUX01 > 0, FTABELA("S015", nAUX01, 6), 0)
				
				IF nFacAct > 0 .AND. (nA != 0 .OR. nB != 0)
					nMesC03 := ROUND(( nFacAct * (nA - nB) ),0)  
				ELSE
					nMesC03 := 0
				ENDIF
									
				IF nFacAct > 0 .AND. (nC + nD) != 0
					nMesC04 := ROUND(( nFacAct * (nC + nD )),0) 
				ELSE
					nMesC04 := 0
				ENDIF
				IF nFacAct > 0 .AND. nE != 0
					nMesC05 := ROUND(( nFacAct * nE ), 0) 
				ELSE 
				 	nMesC05 := 0
				ENDIF  
				
				IF nFacAct > 0 .AND. nG != 0
					nMesC08 := ROUND(( nFacAct * nG ),0) 
				ELSE 
				 	nMesC08 := 0
				ENDIF
				
				IF nFacAct > 0 .AND. nF != 0
					nMesC33 := ROUND(( nFacAct * nF ),0) 
				ELSE 
				 	nMesC33 := 0
				ENDIF 
				
				IF nFacAct > 0 .AND. nH != 0
					nMesC09 := ROUND(( nFacAct * nH ),0) 
				ELSE 
				 	nMesC09 := 0
				ENDIF   
				
				nSumMesC03 += nMesC03
			 	nSumMesC04 += nMesC04
				nSumMesC05 += nMesC05
				nSumMesC08 += nMesC08 
				nSumMesC33 += nMesC33 
				nSumMesC09 += nMesC09
				
			 	IF ( ROUND(nMesC03,0) != 0 .OR. ROUND(nMesC04,0) != 0 .OR. ROUND(nMesC05,0) != 0 .OR.  ROUND(nMesC33,0) != 0 .OR. ROUND(nMesC08,0) != 0 .OR.  ROUND(nMesC09,0) != 0)
					aMeses[nMesTmp] := 1				
				EndIF
				
				nMesTmp := val((cArqRCV)->(RCW_MES))
				cRutNoTmp := cRutNoF
				
				nMesC03 := 0
				nMesC04 := 0
				nMesC05 := 0
				nMesC08 := 0 
				nMesC33 := 0 
				nMesC09 := 0 
				
				nA := 0	
				nB := 0
				nC := 0
				nD := 0
				nE := 0
				nF := 0
				nG := 0
				nH := 0		
				
			EndIf		
			
				
		Enddo

		cCadena += SumXMes(cRutNoF, nSumMesC03, nSumMesC04, nSumMesC05, nSumMesC08, nSumMesC33, nSumMesC09, aMeses, nFolio)

	Enddo
	GuardaC (cRutaNom, cCadena) 
	(cArqRCV)->(DbCloseArea())

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³GuardaC   ³ Autor ³ Alex Hernandez        ³ Data ³10/09/2015³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Concatena una cadena con los valores                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GuardaC(cRutaNom, cCadena)                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±³          ³cRutaNom = Ruta y nombre del archivo a guardar              ³±± 
±±³          ³cCadena  = Cadenade a guardar en el archivo                 ³±± 
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³GPEM045CHI                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function GuardaC (cRutaNom, cCadena)
	Private nHdl 
	
	cRutaNom += IIF(RIGHT(AllTrim(UPPER(cRutaNom)), 4) != ".CSV" , ".CSV", "")
 	nHdl     := fCreate(cRutaNom)
	If nHdl == -1
	    MsgAlert(STR0004 + cRutaNom + STR0005, STR0006) //"El nombre del archivo "## " no puede ser creado! Compruebe los parámetros." ### "Atención!"
	    Return
	Endif
   
    If fWrite(nHdl,cCadena,Len(cCadena)) != Len(cCadena)
       MsgAlert(STR0007,STR0006) //"Ocurrió un error en la grabación del archivo." ##"¡Atención!"
       RETURN  
    ELSE
    	fClose(nHdl)	
       MSGINFO(STR0008 + cRutaNom + STR0009 ) //"El archivo " ## " se guardo de manera correcta! " 
    Endif
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³SumXMes   ³ Autor ³ Alex Hernandez        ³ Data ³10/09/2015³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Concatena una cadena con los valores                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ SumXMes(cRutNoF ,nSumMesC03, nSumMesC04, nSumMesC05,       ³±±
±±³          ³ nSumMesC08, nSumMesC33, nSumMesC09, aMeses, nFolio)        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±³          ³cRutNoF = RUT de la persona a quien se le pago la renta.(C2)³±± 
±±³          ³nSumMesC03 = Suma Renta Total Neta Pagada(C3)               ³±± 
±±³          ³nSumMesC04 = Suma Impuesto Unico Retenido(C4)               ³±±
±±³          ³nSumMesC05 = Suma Mayor Retencion Solicitado(ART.88 LIR)(C5)³±± 	
±±³          ³nSumMesC08 = Suma Renta Total No Gravada(C8)                ³±±
±±³          ³nSumMesC33 = Suma Renta Total Exenta (C33)                  ³±±
±±³          ³nSumMesC09 = Suma REBAJA POR ZONAS EXTREMAS(Fra. DL 889)(C9)³±±
±±³          ³aMeses  = Array de meses                                    ³±±
±±³          ³nFolio  = Folio del Documento                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³GPEM045CHI                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/				  
Static Function SumXMes(cRutNoF ,nSumMesC03, nSumMesC04, nSumMesC05, nSumMesC08, nSumMesC33, nSumMesC09, aMeses, nFolio)
		Local nCnt    := 0 
		Local cCadena := ""
		
		IF  ( ROUND(nSumMesC03,0) != 0 .OR. ROUND(nSumMesC04,0) != 0 .OR. ROUND(nSumMesC05,0) != 0 .OR. ROUND(nSumMesC08,0) != 0 .OR. ROUND(nSumMesC33,0) != 0 .OR. ROUND(nSumMesC09,0) != 0)
			cCadena := IIF(LEN(ALLTRIM(cRutNoF)) > 9,SUBSTR(cRutNoF,1,9),ALLTRIM(cRutNoF)) + ";" 
			cCadena += ALLTRIM(STR(ROUND(nSumMesC03,0))) + ";" + ALLTRIM(STR(ROUND(nSumMesC04,0))) + ";"   
			cCadena += ALLTRIM(STR(ROUND(nSumMesC05,0))) + ";" +  ALLTRIM(STR(ROUND(nSumMesC08,0)))+ ";" + ALLTRIM(STR(ROUND(nSumMesC33,0))) + ";"  + ALLTRIM(STR(ROUND(nSumMesC09,0)))
			//Se coloca la 'X' al mes que corresponde
			For nCnt := 1 To 12
				IF aMeses[nCnt] == 1
					cCadena += ";X"
				ELSE
					cCadena += ";"
				END
			Next
			cCadena += ";" + ALLTRIM(STR(nFolio)) + CHR(13)+CHR(10)
		ENDIF
Return cCadena