#INCLUDE "INKEY.CH"
#INCLUDE "protheus.ch"
#INCLUDE "report.ch"
#INCLUDE "gper021.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPER021  ³ Autor    ³ Alceu Pereira      ³ Data ³ 21/08/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relacao de Liquidos                           			    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPER021(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico - Angola                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³             ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS     ³  Motivo da Alteracao                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tiago Malta ³13/01/10³00421/2010³Alterado o conteudo da variavel Filant³±±
±±³            ³        ³          ³ para Replicate("!",FWGETTAMFILIAL).  ³±±
±±³Claudinei S.³30/11/11³30146/2011³Inclusao da Funcao AJUSTASX1,para     ³±±
±±³            ³        ³          ³ajustar matrícula.                    ³±±
±±³Claudinei S.³03/01/12³00164/2012³alteração para geração de valores em  ³±±
±±³            ³		   ³	 TEGYE5³dolar ou kwanza sem olhar o RA_MDAPAG.³±±
±±³Emerson Camp³10/01/12³00400/2012³Remoção da pergunta Moeda de pagto, p/³±±
±±³            ³		   ³	 TEHKKJ³o Equador pois o mesmo so utiliza a   ³±±
±±³            ³		   ³	       ³moeda Dolar, e transformado os textos ³±±
±±³            ³		   ³	       ³chumbados em arquivo de CH            ³±±
±±³Renan Borges³02/04/14³    TPECOS³ Retirada a restrição para gerar dados³±±
±±³            ³		   ³	       ³somente para funcionários que recebem ³±±
±±³            ³		   ³	       ³por transferencia bancária. 		    ³±±
±±³            ³		   ³	       ³                                      ³±±
±±³Jonathan glz³05/05/15³PCREQ-4256³Se elimina la funcion AJUSTASX1, la   ³±±
±±³            ³		   ³	       ³cual realiza la modificacion aldiccio-³±±
±±³            ³		   ³	       ³nario de datos(SX1) por motivo de     ³±±
±±³            ³		   ³	       ³adecuacion de nueva estructura de SXs ³±±
±±³            ³		   ³	       ³para version 12 y se cambia el grupo  ³±±
±±³            ³		   ³	       ³de preguntas por GP021RA              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function GPER021()

Local oReport

Private nPgQuant	:= 0
Private nPgValorK	:= 0

Private	cFil
Private	cCusto
Private	cBanco
Private	cMat
Private	cNome
Private	cSituacao
Private	dDataDe
Private	dDataAte
Private	cProcessos
Private	cCategoria
Private	cAgencia
Private	nTipoMoeda
Private	cConta

SetMnemonicos(NIL,NIL,.T.)

oReport := ReportDef()
oReport:PrintDialog()

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ReportDef ³ Autor ³ Alceu Pereira         ³ Data ³ 22.08.09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Definicao do relatorio                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß */

Static Function ReportDef()

Local oReport
Local oSection
Local oTransp

Local cDesc	:=	STR0001 //"Relação de Liquidos."
Local aOrd	  	:=	{ STR0002 } //"Filial+Bco/Ag.+Mat"

Pergunte("GP021RA",.F.)

	DEFINE REPORT oReport NAME "GPER021" TITLE OemToAnsi(STR0003) PARAMETER "GP021RA" ACTION {|oReport| PrintReport(oReport)} DESCRIPTION cDesc TOTAL IN COLUMN PAGE TOTAL IN COLUMN //"RELAÇÃO DE LÍQUIDOS"

		DEFINE SECTION oSection OF oReport TABLES "SRA" ORDERS aOrd TOTAL IN COLUMN TITLE STR0003//"RELAÇÃO DE LÍQUIDOS"
		oSection:SetHeaderBreak(.T.)

		   	DEFINE CELL NAME "RA_FILIAL"	OF oSection ALIAS "SRA" SIZE 10
		   	DEFINE CELL NAME "RA_MAT" 		OF oSection ALIAS "SRA"	SIZE 20
			DEFINE CELL NAME "RA_NOME" 		OF oSection ALIAS "SRA" SIZE 50
			DEFINE CELL NAME "RA_NIB" 		OF oSection ALIAS "SRA" SIZE 40

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³PrintReport³ Autor ³ R.H. - Alceu Pereira    ³ Data ³ 27.08.09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relacao de Liquidos                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Static Function PrintReport(oReport)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao de Variaveis Locais                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//-- Objeto
Local oSection 	:= oReport:Section(1)
Local oBreakPg

//-- Array
Local aCodFol  	:= {}
Local aVerba		:= {}
//-- String
Local cTitulo		:= ""
Local cCabec		:= ""
Local cAliasX		:= ""
Local cSitQuery	:= ""
Local cCatQuery	:= ""
Local cOrdem		:= ""
Local cArqNtx		:= ""
Local cFiltro		:= ""
Local cParBanco 	:= ""
Local cParConta 	:= ""
Local cRProc		:= ""
Local cAuxPrc		:= ""
Local cTitFil		:= ""
Local cTitCc		:= ""
Local cTitLP		:= ""

//-- Numerico
Local nOrdem		:= oSection:GetOrder()
Local nReg			:= 0
Local nValor    	:= 0
Local nValBenef	:= 0
Local nTamCod		:= 0
Local nTPgQuant	:= 0
Local nTPgValorK	:= 0
Local nVerba     	:= 0

//-- Variaveis de Acesso do Usuario                               ³
Private cAcessaSR1	:= &( " { || " + ChkRH( "GPER021" , "SR1" , "2" ) + " } " )
Private cAcessaSRA	:= &( " { || " + ChkRH( "GPER021" , "SRA" , "2" ) + " } " )
Private cAcessaSRC	:= &( " { || " + ChkRH( "GPER021" , "SRC" , "2" ) + " } " )
Private cAcessaSRD	:= &( " { || " + ChkRH( "GPER021" , "SRD" , "2" ) + " } " )
Private cAcessaSRR	:= &( " { || " + ChkRH( "GPER021" , "SRR" , "2" ) + " } " )
Private cAcessaSRG	:= &( " { || " + ChkRH( "GPER021" , "SRG" , "2" ) + " } " )
Private cAcessaSRH	:= &( " { || " + ChkRH( "GPER021" , "SRH" , "2" ) + " } " )

//-- Array
Private aInfo		:= {}
Private aRoteiros	:= {}

cFil    		:= mv_par01
cCusto	   		:= mv_par03
cBanco 	   	:= mv_par04
cMat	   		:= mv_par05
cNome	   		:= mv_par06
cSituacao  	:= mv_par07
dDataDe		:= mv_par08
dDataAte		:= mv_par09
cProcessos 	:= mv_par10
cCategoria    := mv_par11
cAgencia		:= SUBSTR(cBanco,4,5)

If cPaisLoc <> "ANG"
	//kwanza e moeda especifica de Angola, Equador e Haiti so trabalham com Dolar, nao necessitam dessa pergunta "mv_par12"
	nTipoMoeda	:= 2
	cConta		:= mv_par12
Else
	nTipoMoeda	:= mv_par12
	cConta		:= mv_par13
EndIf


	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Carregando array aRoteiros com os roteiros selecionados      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Len(mv_par02) > 0
		SelecRoteiros()
	EndIf

	lRescisao   := .F.

	cTitulo := STR0003//"RELACAO DE LIQUIDOS"

	IF nTipoMoeda = 1
		DEFINE CELL NAME "VALORK" 	OF oSection ALIAS "   " ALIGN LEFT TITLE OemToAnsi(STR0004) PICTURE "@E  99,999,999,999.99" SIZE 17 //"KWANZA"
		DEFINE FUNCTION NAME "PAGVALORK" FROM oSection:Cell("VALORK")		FUNCTION SUM   PRINT WHEN {|| oSection:GetFunction("PAGVALORK"):PageValue() > 0 } TITLE OemToAnsi(STR0005)  PICTURE "@E  99,999,999,999.99" NO END SECTION NO END REPORT END PAGE  //"Total em KWANZA"
    	DEFINE FUNCTION FROM oSection:Cell("VALORK") FUNCTION ONPRINT BREAK oBreakPg FORMULA {|| oSection:GetFunction("PAGVALORK"):PageValue() } PICTURE "@E  99,999,999,999.99" NO END SECTION NO END REPORT
	Else
		DEFINE CELL NAME "VALORD" 	OF oSection ALIAS "   " ALIGN LEFT TITLE OemToAnsi(STR0006)  PICTURE "@E  99,999,999,999.99" SIZE 17 //"DOLAR"
		DEFINE FUNCTION NAME "PAGVALORD" FROM oSection:Cell("VALORD")		FUNCTION SUM   PRINT WHEN {|| oSection:GetFunction("PAGVALORD"):PageValue() > 0 } TITLE OemToAnsi(STR0007)   PICTURE "@E  99,999,999,999.99" NO END SECTION NO END REPORT END PAGE //"Total em DOLAR"
		DEFINE FUNCTION FROM oSection:Cell("VALORD") FUNCTION ONPRINT BREAK oBreakPg FORMULA {|| oSection:GetFunction("PAGVALORD"):PageValue() } PICTURE "@E  99,999,999,999.99" NO END SECTION NO END REPORT
    Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define se devera ser impresso Funcionarios ou Beneficiarios  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	dbSelectArea( "SRQ" )

	cCabec := STR0008+space(1)+DTOC(dDataAte)+space(1)+STR0009+space(1)+cConta+space(1)+STR0010
	oReport:OnPageBreak({|| oReport:SkipLine(),oReport:PrintText(cCabec),oReport:SkipLine()})
	//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
	#IFDEF TOP
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Faz filtro no arquivo...                                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cFiltro	:= "%"

		//-- Adiciona no filtro o parametro tipo Range
		//-- Filial
		If !Empty(mv_par01)
			cFiltro +=" SRA."+RANGESX1("RA_FILIAL"  ,MV_PAR01)+" AND "
		EndIf

		//-- Centro de Custo
		If !Empty(mv_par03)
			cFiltro +=" SRA."+RANGESX1("RA_CC"  ,MV_PAR03)+" AND "
		EndIf

		//-- Matricula
		If !Empty(mv_par05)
			cFiltro +=" SRA."+RANGESX1("RA_MAT"  ,MV_PAR05)+" AND "
		EndIf
		//-- Nome
		If !Empty(mv_par06)
			cFiltro +=" SRA."+RANGESX1("RA_NOME"  ,MV_PAR06)+" AND "
		EndIf

	    cFiltro+="%"

		cAliasX := "SRA"
		//-- Modifica variaveis para a Query
		For nReg:=1 to Len(cSituacao)
			cSitQuery += "'"+Subs(cSituacao,nReg,1)+"'"
			If ( nReg+1 ) <= Len(cSituacao)
				cSitQuery += ","
			Endif
		Next nReg
		cSitQuery := "%" + cSitQuery + "%"

		cCatQuery := ""
		For nReg:=1 to Len(cCategoria)
			cCatQuery += "'"+Subs(cCategoria,nReg,1)+"'"
			If ( nReg+1 ) <= Len(cCategoria)
				cCatQuery += ","
			Endif
		Next nReg
		cCatQuery := "%" + cCatQuery + "%"

		If nOrdem == 1
			cOrdem += "%RA_FILIAL, RA_BCDEPSA,RA_MAT%"
		Endif

		//NAO RETIRAR ESTA LINHA
		//Este relatório abre a query abaixo com o nome de "SRA" e
		//como a tabela "SRA" eh utilizada em vários outros programas,
		//foi preciso fechar primeiro a area para depois poder utiliza-la.
		SRA->( dbCloseArea() )

		BeginSql alias cAliasX
			SELECT *
			FROM %table:SRA% SRA
			WHERE SRA.RA_SITFOLH IN (%exp:Upper(cSitQuery)%) AND
				   SRA.RA_CATFUNC IN (%exp:Upper(cCatQuery)%) AND
				   %exp:cFiltro%
				   SRA.%notDel%
			ORDER BY %exp:cOrdem%
		EndSql

	#ELSE
		cAliasX := "SRA"
		//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
		MakeAdvplExpr("GPE020R")

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta a string de Processos para Impressao                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		cFiltro	:= "%%"

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta a string de Processos para Impressao                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		If AllTrim(cProcessos) <> "*"
			cRProc := ""
			nTamCod := GetSx3Cache( "RCJ_CODIGO" , "X3_TAMANHO" )
			For nReg := 1 to Len(cProcessos) Step 5
				If Len(Subs(cProcessos,nReg,5)) < nTamCod
					cAuxPrc := Subs(cProcessos,nReg,5) + Space(nTamCod - Len(Subs(cProcessos,nReg,5)))
				Else
					cAuxPrc := Subs(cProcessos,nReg,5)
				EndIf

				cRProc += cAuxPrc

				If ( nReg+5 ) <= Len(cProcessos)
					cRProc += "','"
				EndIf

			Next X
			cFiltro := "%SRA.RA_PROCES IN ('"+ cRProc + "') AND%"
		EndIf

		//-- Adiciona no filtro o parametro tipo Range
		//-- Filial
		If !Empty(mv_par01)
			cFiltro += mv_par01 + " .AND. "
		EndIf

		//-- Centro de Custo
		If !Empty(mv_par03)
			cFiltro += mv_par03 + " .AND. "
		EndIf
		//-- Matricula
		If !Empty(mv_par05)
			cFiltro += mv_par05 + " .AND. "
		EndIf
		//-- Nome
		If !Empty(mv_par06)
			cFiltro += mv_par06 + " .AND. "
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica a ordem selecionada                                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nOrdem == 1
			cOrdem:= "RA_FILIAL + RA_BCDEPSA + RA_MAT"
		Endif

		cArqNtx  := CriaTrab(NIL,.F.)
		IndRegua((cAliasX),cArqNtx,cOrdem,,,STR0011)//"Selecionando Regitros..."
		(cAliasX)->( DbGoTop() )

	  	cFiltro += 'RA_SITFOLH $ "' + cSituacao + '" .AND. '
		cFiltro += 'RA_CATFUNC $ "' + cCategoria + '" .AND. '
		cFiltro += 'RA_TPPGTO = "2" '

	  	oSection:SetFilter(cFiltro)
	#ENDIF

	FilAnt := Replicate("!",FWGETTAMFILIAL)

	//-- Define o total da regua da tela de processamento do relatorio
	oReport:SetMeter((cAliasX)->( RecCount() ))

	//-- Incializa impressão
	oSection:Init(.F.)

	While (cAliasX)->( !EOF() )

		//-- Incrementa a régua da tela de processamento do relatório
  		oReport:IncMeter()

		//-- Verifica se o usuário cancelou a impressão do relatorio
		If oReport:Cancel()
			Exit
		EndIf

		nValor		:= 0

		If (cAliasX)->RA_FILIAL # FilAnt
			If !Fp_CodFol(@aCodFol,(cAliasX)->RA_FILIAL) .Or. !fInfo(@aInfo,(cAliasX)->RA_FILIAL)
				Exit
			Endif
			FilAnt := (cAliasX)->RA_FILIAL
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Consiste controle de acessos e filiais validas               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		If !((cAliasX)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
			// Incluir atualizacao TESTATOTAL
			(cAliasX)->( dbSkip() )
			Loop
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verif. se Demitido esta dentro da Data de Pagamento do Parametro³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		If (SRA->RA_SITFOLH =="D")
			dbSelectArea("SRG")
			If dbSeek( (cAliasX)->(RA_FILIAL + RA_MAT )  )
				While !( SRG->( Eof() ) )  .And. (cAliasX)->(RA_FILIAL + RA_MAT ) ==  SRG->(RG_FILIAL + RG_MAT )
					If SRG->RG_DATAHOM >= dDataDe .And. SRG->RG_DATAHOM <= dDataAte
						lRescisao := .T.
					EndIf
					SRG->( dbSkip())
				Enddo
			Else
				dbSelectArea("SRA")
				(cAliasX)->( dbSkip() )
				Loop
			EndIf
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Busca os valores de Liquido                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nTipoMoeda = 1
			Gp020BuscaLiq(@nValor)
		Else
			Gp020BuscaLiq(@nValor,,FGETCODFOL("1101"))
		Endif

		nValBenef := 0

	    //-- Atualiza campo valor
		If nTipoMoeda == 1
	    	oSection:Cell("VALORK"):SetValue(nValor)
		Else
	    	oSection:Cell("VALORD"):SetValue(nValor)
		Endif

		oSection:PrintLine()

		(cAliasX)->( dbSkip() )
	EndDo

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Finaliza impressao inicializada pelo metodo Init             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	oSection:Finish()

	#IFNDEF TOP
		dbSelectArea("SRA")
		Set Filter to
		RetIndex("SRA")
		dbSetOrder(1)
		fErase( cArqNtx + OrdBagExt() )
	#Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Termino do relatorio                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	dbSelectArea("SRA")
	dbCloseArea()
	ChkFile("SRA")

Return NIL
