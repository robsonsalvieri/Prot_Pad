#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPER039.CH"

/*


ͻ
Programa  GPER039   Autor  Silvia Taguti        Data   23/04/03   
͹
Desc.     Hace la impresion de Recibo de Pago                         
                                                                      
͹
Uso        Generico                                                   
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
Luiz Gustavo|29/01/07116748Retiradas funcoes de ajuste de dicionario.|  
Renata E.   |02/07/08147528Ajuste no tamanho do fonte (R039Imp)	  |  
ٱ

Ŀ
Programador   Data      FNC              Motivo da Alteracao                      
ĳ
Rogerio R.   29/07/200900000018278/2009Compatibilizacao dos fontes para aumento do
                                       campo filial e gesto corporativa.         
Alex         27/10/200900000026128/2009Adaptao Gesto Corporativa               
                                       Respeitar o Grupo de campos de Filiais.    
ٱ

*/
FUNCTION GPER039()

Local cString := "SRA"  // alias do arquivo principal (Base)

//Ŀ
// Define Variaveis Private(Basicas)                            
//
Private NomeProg := "GPER039"
Private cPerg    :="GPR039"
Private cSem_De  := "  /  /    "
Private cSem_Ate := "  /  /    "

//Ŀ
// Define Variaveis Private(Programa)                           
//
Private aProve := {}
Private aDesco := {}
Private aInfo  := {}
Private aCodFol:= {}
Private li     := 0
Private LiTot	:= 0
Private Titulo := STR0006		//"EMISSO DE RECIBOS DE PAGAMENTOS"

//Ŀ
// Verifica as perguntas selecionadas                           
//
pergunte("GPR039",.T.)

wnrel:="GPER039"                                                   

//Ŀ
// Variaveis utilizadas para parametros                         
// mv_par01        //  Data de Referencia para a impressao      
// mv_par02        //  Emitir Recibos(Folha/13Salario)          
// mv_par03        //  Numero da Semana                         
// mv_par04        //  Filial De                                
// mv_par05        //  Filial Ate                               
// mv_par06        //  Centro de Custo De                       
// mv_par07        //  Centro de Custo Ate                      
// mv_par08        //  Matricula De                             
// mv_par09        //  Matricula Ate                            
// mv_par10        //  Nome De                                  
// mv_par11        //  Nome Ate                                 
// mv_par12        //  Chapa De                                 
// mv_par13        //  Chapa Ate                                
// mv_par14        //  Situacoes a Imprimir                     
// mv_par15        //  Categorias a Imprimir                    
// mv_par16        //  Ordem                                    
// mv_par17        //  Tipo de relatorio                        
//

dDataRef   := mv_par01
Esc        := mv_par02
Semana     := mv_par03
cFilDe     := mv_par04
cFilAte    := mv_par05
cCcDe      := mv_par06
cCcAte     := mv_par07
cMatDe     := mv_par08
cMatAte    := mv_par09
cNomDe     := mv_par10
cNomAte    := mv_par11
ChapaDe    := mv_par12
ChapaAte   := mv_par13
cSituacao  := mv_par14
cCategoria := mv_par15
nOrdem     := mv_par16
nTipRel    := mv_par17

cMesAnoRef := StrZero(Month(dDataRef),2) + StrZero(Year(dDataRef),4)

RptStatus({|lEnd| R039Imp(@lEnd,wnRel,cString,cMesAnoRef)},Titulo)  // Chamada do Relatorio

Return

/*


Ŀ
Funo     R039IMP   Autor  R.H. - Silvia Taguti   Data  23.04.03 
Ĵ
Descrio  Processamento Para emissao do Recibo                       
Ĵ
Sintaxe    R039IMP(lEnd,Wnrel,cString)                                
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
Ĵ

*/
Static Function R039Imp(lEnd,WnRel,cString,cMesAnoRef)

//Ŀ
// Define Variaveis Locais (Basicas)                            
//
Local aOrdBag     := {}
Local cMesArqRef  
Local cArqMov     := ""
Local cAcessaSR1  := &("{ || " + ChkRH("GPER039","SR1","2") + "}")
Local cAcessaSRA  := &("{ || " + ChkRH("GPER039","SRA","2") + "}")
Local cAcessaSRC  := &("{ || " + ChkRH("GPER039","SRC","2") + "}")
Local cAcessaSRI  := &("{ || " + ChkRH("GPER039","SRI","2") + "}")
Local nHoras      := 0 
Local nX				:=	0
Local cSitFunc
Local dDtPesqAf

// Crea los objetos con las configuraciones de fuentes
Private oFont08  := TFont():New( "Courier New",,08,,.f.,,,,,.f. )
Private oFont08b := TFont():New( "Courier New",,08,,.t.,,,,,.f. )
Private oFont09  := TFont():New( "Courier New",,09,,.f.,,,,,.f. )
Private oFont10  := TFont():New( "Courier New",,10,,.f.,,,,,.f. )
Private oFont10b := TFont():New( "Courier New",,10,,.t.,,,,,.f. )

Private cAliasMov := ""
Private cDescMoeda 	:= SubStr(GetMV("MV_SIMB1"),1,3)
Private oPrn := TMSPrinter():New()
Private cPict1 := PesqPict("SRC","RC_VALOR",15)
Private cPict2 := PesqPict("SRC","RC_HORAS",10)
Private nPag := 0                            
Private cRG  := "" 
oPrn:StartPage()

If Esc == 2  
	cMesArqRef := "13" + Right(cMesAnoRef,4)
Else
	cMesArqRef := cMesAnoRef
Endif	              

dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("RA_RG")
cRG := Alltrim(Substr(X3TITULO(),1,12))
dbSelectArea( "SRA")
If nOrdem == 1
	dbSetOrder(1)
ElseIf nOrdem == 2
	dbSetOrder(2)
ElseIf nOrdem == 3
	dbSetOrder(3)
Endif

dbGoTop()


//Ŀ
// Selecionando o Primeiro Registro e montando Filtro.          
//
If nOrdem == 1
	dbSeek(cFilDe + cMatDe,.T.)
	cInicio := "SRA->RA_FILIAL + SRA->RA_MAT"
	cFim    := cFilAte + cMatAte
ElseIf nOrdem == 2
	dbSeek(cFilDe + cCcDe + cMatDe,.T.)
	cInicio  := "SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_MAT"
	cFim     := cFilAte + cCcAte + cMatAte
ElseIf nOrdem == 3
	dbSeek(cFilDe + cNomDe + cMatDe,.T.)
	cInicio := "SRA->RA_FILIAL + SRA->RA_NOME + SRA->RA_MAT"
	cFim    := cFilAte + cNomAte + cMatAte
Endif

dbSelectArea("SRA")
//Ŀ
// Carrega Regua Processamento                                  
//
SetRegua(RecCount())   // Total de elementos da regua

TOTVENC:= TOTDESC:= 0

Desc_Fil := ""
cFilialAnt := Space(FWGETTAMFILIAL)
cFuncaoAnt := "    "
cCcAnt     := Space(9)
Vez        := 0


dbSelectArea("SRA")
While !SRA->(EOF()) .And. &cInicio <= cFim
	
	//Ŀ
	// Movimenta Regua Processamento                                
	//
	IncRegua()  // Anda a regua
	
	If lEnd
		@Prow()+1,0 PSAY cCancel
		Exit
	Endif
	
	//Ŀ
	// Consiste Parametrizacao do Intervalo de Impressao            
	//
	If (SRA->RA_CHAPA < ChapaDe) .Or. (SRA->Ra_CHAPa > ChapaAte) .Or. ;
		(SRA->RA_NOME < cNomDe)    .Or. (SRA->Ra_NOME > cNomAte)    .Or. ;
		(SRA->RA_MAT < cMatDe)     .Or. (SRA->Ra_MAT > cMatAte)     .Or. ;
		(SRA->RA_CC < cCcDe)       .Or. (SRA->Ra_CC > cCcAte)
		SRA->(dbSkip(1))
		Loop
	EndIf
	
	aProve:={}         // Zera Lancamentos
	aDesco:={}         // Zera Lancamentos
	
	Ordem_rel := 1     // Ordem dos Recibos
	
	//Ŀ
	// Verifica Data Demissao         
	//
	cSitFunc := SRA->RA_SITFOLH
	dDtPesqAf:= CTOD("01/" + Left(cMesAnoRef,2) + "/" + Right(cMesAnoRef,4),"DDMMYY")
	If cSitFunc == "D" .And. (!Empty(SRA->RA_DEMISSA) .And. MesAno(SRA->RA_DEMISSA) > MesAno(dDtPesqAf))
		cSitFunc := " "
	Endif
	
	//Ŀ
	// Consiste situacao e categoria dos funcionarios			     |
	//
	If !( cSitFunc $ cSituacao ) .OR.  ! ( SRA->RA_CATFUNC $ cCategoria )
		dbSkip()
		Loop
	Endif
	If cSitFunc $ "D" .And. Mesano(SRA->RA_DEMISSA) # Mesano(dDataRef)
		dbSkip()
		Loop
	Endif
	
	//Ŀ
	// Consiste controle de acessos e filiais validas				 |
	//
	If !(SRA->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
		dbSkip()
		Loop
	EndIf
	
	If SRA->RA_CODFUNC # cFuncaoAnt           // Descricao da Funcao
		DescFun(Sra->Ra_Codfunc,Sra->Ra_Filial)
		cFuncaoAnt:= Sra->Ra_CodFunc
	Endif
	
	If SRA->RA_CC # cCcAnt                   // Centro de Custo
		DescCC(Sra->Ra_Cc,Sra->Ra_Filial)
		cCcAnt:=SRA->RA_CC
	Endif
	
	If SRA->RA_Filial # cFilialAnt
		If ! Fp_CodFol(@aCodFol,Sra->Ra_Filial) .Or. ! fInfo(@aInfo,Sra->Ra_Filial)
			Exit
		Endif
		Desc_Fil := aInfo[3]  //Filial
		dbSelectArea("SRA")
		cFilialAnt := SRA->RA_FILIAL
	Endif
	
	Totvenc := Totdesc := 0
	
	If Esc == 1
		dbSelectArea("SRC")
		dbSetOrder(1)
		If dbSeek(SRA->RA_FILIAL + SRA->RA_MAT)
			While !Eof() .And. SRC->RC_FILIAL+SRC->RC_MAT == SRA->RA_FILIAL+SRA->RA_MAT
				If SRC->RC_SEMANA # Semana
					dbSkip()
					Loop
				Endif
				If !Eval(cAcessaSRC)
					dbSkip()
					Loop
				EndIf
				
				If PosSrv( Src->Rc_Pd , SRA->RA_FILIAL , "RV_TIPOCOD" ) == "1"
					fSomaPd("P",SRC->RC_PD,SRC->RC_HORAS,SRC->RC_VALOR)
					TOTVENC += Src->Rc_Valor
				Elseif PosSrv( Src->Rc_Pd , SRA->RA_FILIAL , "RV_TIPOCOD" ) == "2"
					fSomaPd("D",SRC->RC_PD,SRC->RC_HORAS,SRC->RC_VALOR)
					TOTDESC += Src->Rc_Valor
				Endif
				dbSelectArea("SRC")
				dbSkip()
			Enddo
		Endif
	Elseif Esc == 2
		dbSelectArea("SRI")
		dbSetOrder(2)
		If dbSeek(SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_MAT)
			While !Eof() .And. SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_MAT == SRI->RI_FILIAL + SRI->RI_CC + SRI->RI_MAT
				If !Eval(cAcessaSRI)
					dbSkip()
					Loop
				EndIf
				If PosSrv( SRI->RI_PD , SRA->RA_FILIAL , "RV_TIPOCOD" ) == "1"
					fSomaPd("P",SRI->RI_PD,SRI->RI_HORAS,SRI->RI_VALOR)
					TOTVENC = TOTVENC + SRI->RI_VALOR
				Elseif PosSrv( SRI->RI_PD , SRA->RA_FILIAL , "RV_TIPOCOD" ) == "2"
					fSomaPd("D",SRI->RI_PD,SRI->RI_HORAS,SRI->RI_VALOR)
					TOTDESC = TOTDESC + SRI->RI_VALOR
				Endif
				dbSkip()
			Enddo
		Endif
	Endif
	
	dbSelectArea("SRA")
	
	If TOTVENC = 0 .And. TOTDESC = 0
		dbSkip()
		Loop
	Endif
	
	If Vez == 0  .And.  Esc == 1 //--> Verifica se for FOLHA.
		PerSemana() // Carrega Datas referentes a Semana.
	EndIf
	nPag += 1
	If nTipRel == 1 
		fImpreRec()   // Impressao do Recibo de Pagamento
	ElseIf nTipRel == 2
		fImpreZeb()
	Endif		
	TotDesc := TotVenc := 0
	DbSelectArea('SRA')
	dbSkip()
ENDDO

//Ŀ
// Seleciona arq. defaut do Siga caso Imp. Mov. Anteriores      
//
If !Empty( cAliasMov )
	fFimArqMov( cAliasMov , aOrdBag , cArqMov )
EndIf

//Ŀ
// Termino do relatorio                                         
//
dbSelectArea("SRC")
dbSetOrder(1)          // Retorno a ordem 1
dbSelectArea("SRI")
dbSetOrder(1)          // Retorno a ordem 1
dbSelectArea("SRA")
SET FILTER TO
RetIndex("SRA")

// Cerra la pagina
oPrn:EndPage()
// Mostra la pentalla de Setup
oPrn:Setup()
// Mostra la pentalla de preview
oPrn:Preview()

MS_FLUSH()

Return
/*


Ŀ
Funo    fImpreZeb  Autor  Silvia Taguti          Data  23.04.03 
Ĵ
Descrio  IMRESSAO DO RECIBO ZEBRADO                                 
Ĵ
Sintaxe    fImpreZeb()                                                
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
Ĵ

*/
Static Function fImpreZeb() 

If Li > 3000 
	// Cerra la pagina
	oPrn:EndPage()
	oPrn:StartPage()  
	Li := 0
Endif
fCabecZeb()
fLancaZeb()

Return Nil
/*


Ŀ
Funo    fCabecZeb  Autor  Silvia Taguti          Data  24.03.03 
Ĵ
Descrio  IMRESSAO Cabecalho Zebrado                                 
Ĵ
Sintaxe    fCabecZeb()                                                
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
Ĵ

*/
Static Function fCabecZeb()// Cabecalho do Recibo Zebrado

oPrn:Box(Li,100,Li+960,2400)
oPrn:Say(Li+=40,140,OemToAnsi(STR0010),oFont10,50) // Empresa           
oPrn:Say(Li,350,Desc_Fil,oFont10,50) // Descricao Empresa  
oPrn:Say(Li,1500,cRG,oFont10,50) // Cedula          
oPrn:Say(Li,1800,SRA->RA_RG,oFont10,50) // Numero da Cedula   
oPrn:Say(Li+=50,140,OemToAnsi(STR0003),oFont10,50) // Funcionario       
oPrn:Say(Li,350,SRA->RA_NOME,oFont10,50) // Nome Funcionario   
oPrn:Say(Li,1500,OemToAnsi(STR0001),oFont10,50) // Matricula          
oPrn:Say(Li,1800,SRA->RA_MAT,oFont10,50) // Numero Matricula   
oPrn:Say(Li+=50,140,OemToAnsi(STR0002),oFont10,50) // Centro de custo    
cDet := DescCc(SRA->RA_CC,SRA->RA_FILIAL) + ' '
oPrn:Say(Li,350,cDet,oFont10,50) // Centro de custo    
If !Empty(Semana) .And. Semana # '99' .And.  Upper(SRA->RA_TIPOPGT) == 'S'
	oPrn:Say(Li,1500,OemToAnsi(STR0014)+Semana+'('+cSem_De+OemToAnsi(STR0015)+cSem_Ate + ')',oFont10,50) // Matricula          
Endif
oPrn:Line(Li+=50,100,Li,2400)
oPrn:Say(Li+=30,350,OemToAnsi(STR0016),oFont10,50) // Ingresos   
oPrn:Say(Li,1550,OemToAnsi(STR0017),oFont10,50) // Descontos 
oPrn:Line(Li+=45,100,Li,2400)     //Linha
oPrn:Line(Li,1200,Li+545,1200)    //Coluna

Return Nil

/*


Ŀ
Funo    fLancaZeb  Autor  Silvia Taguti          Data  24.04.03 
Ĵ
Descrio  Impressao das Verbas (Lancamentos) Zebrado                 
Ĵ
Sintaxe    fLancaZeb()                                                
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
Ĵ

*/
Static Function fLancaZeb()// Impressao dos Lancamentos

Local nTermina  := 0
Local nCont     := 0
Local nValidos  := 0

nTermina := Max(LEN(aProve),LEN(aDesco))
Li += 40
LiTot := Li
For nCont := 1 To nTermina
	IF nCont <= LEN(aProve)
		oPrn:Say(Li,140,aProve[nCont,1],oFont10,50)//Concepto
		oPrn:Say(Li,650,Transform(aProve[nCont,2],cPict2),oFont10,50)//Horas
		oPrn:Say(Li,800,Transform(aProve[nCont,3],cPict1),oFont10,50)//Valor
	ENDIF

	IF nCont <= LEN(aDesco)
		oPrn:Say(Li,1250,aDesco[nCont,1],oFont10,50)//Concepto
		oPrn:Say(Li,1750,Transform(aDesco[nCont,2],cPict2),oFont10,50)//Horas
		oPrn:Say(Li,1900,Transform(aDesco[nCont,3],cPict1),oFont10,50)//Valor
	ENDIF
	Li+=40
	//---- Soma 1 nos nValidos e Linha
	nValidos ++
	If nValidos = 8
		oPrn:Say(Li,140,OemToAnsi(STR0018),oFont09,50)//continua
		oPrn:Line(Li+=40,100,Li,2400)
		oPrn:Line(Li+=70,100,Li,2400)
		oPrn:Line(Li+=75,100,Li,2400)
		
		LI +=200
		If li >= 3000
			// Cerra la pagina
			oPrn:EndPage()
			oPrn:StartPage()
			Li:= 0
		Endif
		Li+=100
		fCabecZeb()
		Li += 40
		LiTot := Li
		nValidos := 0
   Endif
Next         

oPrn:Line(LiTot+=360,100,LiTot,2400)
oPrn:Say(LiTot+=30,140,OemToAnsi(STR0019)+Transform(TotVenc,cPict1),oFont10,50)//Total Vencimento
oPrn:Say(LiTot,1300,OemToAnsi(STR0020)+Transform(TotDesc,cPict1),oFont10,50)//Total Desconto  
oPrn:Line(LiTot+=40,100,LiTot,2400)
oPrn:Say(LiTot+=30,1300,OemToAnsi(STR0021)+" "+cDescMoeda+ "  "+Transform(TotVenc-TotDesc,cPict1),oFont10,50)//Neto
oPrn:Line(LiTot+=40,100,LiTot,2400)
oPrn:Line(LiTot+=80,1300,LiTot,2000)
oPrn:Say(LiTot+=20,120,StrZero(nPag,4),oFont10,50) //pagina
oPrn:Say(LiTot,1600,OemToAnsi(STR0022),oFont10,50) //Firma

Li := Litot
Li += 200
//Quebrar pagina
If LI > 3000
	// Cerra la pagina
	oPrn:EndPage()
	oPrn:StartPage()
	Li:= 0
EndIf
Return Nil

/*


Ŀ
Funo    fImpreRec  Autor  Silvia Taguti          Data  23.04.03 
Ĵ
Descrio  IMRESSAO DO RECIBO                                         
Ĵ
Sintaxe    fImpreRec()                                                
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
Ĵ

*/
Static Function fImpreRec() 

If Li > 3000 
	// Cerra la pagina
	oPrn:EndPage()
	oPrn:StartPage()  
	Li := 0
Endif
fCabecRec()
fLancaRec()

Return Nil
/*


Ŀ
Funo    fCabecRec  Autor  Silvia Taguti          Data  24.03.03 
Ĵ
Descrio  IMRESSAO Cabecalho                                         
Ĵ
Sintaxe    fCabecREc()                                                
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
Ĵ

*/
Static Function fCabecRec()// Cabecalho do Recibo            

oPrn:Say(Li+=40,330,Desc_Fil,oFont10,50) // Descricao Empresa  
oPrn:Say(Li,1800,SRA->RA_RG,oFont10,50) // Numero da Cedula
oPrn:Say(Li+=50,330,SRA->RA_NOME,oFont10,50) // Nome Funcionario   
oPrn:Say(Li,1800,SRA->RA_MAT,oFont10,50) // Numero Matricula   
cDet := DescCc(SRA->RA_CC,SRA->RA_FILIAL) + ' '
oPrn:Say(Li+=50,330,cDet,oFont10,50) // Centro de custo    
If !Empty(Semana) .And. Semana # '99' .And.  Upper(SRA->RA_TIPOPGT) == 'S'
	oPrn:Say(Li,1700,Semana+' (' + cSem_De + OemToAnsi(STR0015) +cSem_Ate + ')',oFont10,50) // Matricula          
Endif
Li+=125
Return Nil

/*


Ŀ
Funo    fLancaRec  Autor  Silvia Taguti          Data  24.04.03 
Ĵ
Descrio  Impressao das Verbas (Lancamentos)                         
Ĵ
Sintaxe    fLancaRec()                                                
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
Ĵ

*/
Static Function fLancaRec()// Impressao dos Lancamentos

Local nTermina  := 0
Local nCont     := 0
Local nValidos  := 0

nTermina := Max(LEN(aProve),LEN(aDesco))
Li += 40
LiTot := Li
For nCont := 1 To nTermina
	IF nCont <= LEN(aProve)
		oPrn:Say(Li,140,aProve[nCont,1],oFont10,50)//Concepto
		oPrn:Say(Li,650,Transform(aProve[nCont,2],cPict2),oFont10,50)//Horas
		oPrn:Say(Li,800,Transform(aProve[nCont,3],cPict1),oFont10,50)//Valor
	ENDIF

	IF nCont <= LEN(aDesco)
		oPrn:Say(Li,1300,aDesco[nCont,1],oFont10,50)//Concepto
		oPrn:Say(Li,1800,Transform(aDesco[nCont,2],cPict2),oFont10,50)//Horas
		oPrn:Say(Li,1950,Transform(aDesco[nCont,3],cPict1),oFont10,50)//Valor
	ENDIF
	Li+=40
	//---- Soma 1 nos nValidos e Linha
	nValidos ++
	If nValidos = 8
		oPrn:Say(Li,140,OemToAnsi(STR0018),oFont09,50)//continua
		
		LI +=385
		If li >= 3000
			// Cerra la pagina
			oPrn:EndPage()
			oPrn:StartPage()
			Li:= 0
		Endif
		Li+=100
		fCabecRec()
		Li += 40
		LiTot := Li
		nValidos := 0
   Endif
Next         

oPrn:Say(LiTot+=390,500,Transform(TotVenc,cPict1),oFont10,50)//Total Vencimento
oPrn:Say(LiTot,1700,Transform(TotDesc,cPict1),oFont10,50)//Total Desconto  
oPrn:Say(LiTot+=70,1700,Transform(TotVenc-TotDesc,cPict1),oFont10,50)//Neto
oPrn:Say(LiTot+=120,120,StrZero(nPag,4),oFont10,50) //pagina
LiTot+=20
Li := Litot
Li += 200
//Quebrar pagina
If LI > 3000
	// Cerra la pagina
	oPrn:EndPage()
	oPrn:StartPage()
	Li:= 0
EndIf
Return Nil

/*


Ŀ
Funo    fSomaPd    Autor  R.H. - Mauro           Data  24.09.95 
Ĵ
Descrio  Somar as Verbas no Array                                   
Ĵ
Sintaxe    fSomaPd(Tipo,Verba,Horas,Valor)                            
Ĵ
Parametros                                                            
Ĵ

*/
Static Function fSomaPd(cTipo,cPd,nHoras,nValor)

Local Desc_paga

Desc_paga := DescPd(cPd,Sra->Ra_Filial)  // mostra como pagto

//--Array para o Recibo Pre-Impresso
If cTipo = 'P'
   cArray := "aProve"
Elseif cTipo = 'D'
   cArray := "aDesco"
Endif

nPos := Ascan(&cArray,{ |X| X[1] = cPd })
If nPos == 0
    Aadd(&cArray,{cPd+" "+Desc_Paga,nHoras,nValor })
Else
    &cArray[nPos,2] += nHoras
    &cArray[nPos,3] += nValor
Endif
Return

*-------------------------------------------------------
Static Function Transforma(dData) //Transforma as datas no formato DD/MM/AAAA
*-------------------------------------------------------
Return(StrZero(Day(dData),2) +"/"+ StrZero(Month(dData),2) +"/"+ Right(Str(Year(dData)),4))


***************************
Static Function PerSemana() // Pesquisa datas referentes a semana.
***************************

If !Empty(Semana) 
	cChaveSem := StrZero(Year(dDataRef),4)+StrZero(Month(dDataRef),2)+SRA->RA_TNOTRAB
	If !Srx->(dbSeek(If(cFilial==Space(FWGETTAMFILIAL),Space(FWGETTAMFILIAL),SRA->RA_FILIAL) + "01" + cChaveSem + Semana , .T. )) .And. ;
		!Srx->(dbSeek(If(cFilial==Space(FWGETTAMFILIAL),Space(FWGETTAMFILIAL),SRA->RA_FILIAL) + "01" + Subs(cChaveSem,3,9) + Semana , .T. )) .And. ;
		!Srx->(dbSeek(If(cFilial==Space(FWGETTAMFILIAL),Space(FWGETTAMFILIAL),SRA->RA_FILIAL) + "01" + Left(cChaveSem,6)+"999"+ Semana , .T. )) .And. ;
		!Srx->(dbSeek(If(cFilial==Space(FWGETTAMFILIAL),Space(FWGETTAMFILIAL),SRA->RA_FILIAL) + "01" + Subs(cChaveSem,3,4)+"999"+ Semana , .T. )) .And. ;
		HELP( " ",1,"SEMNAOCAD" )
		Return Nil
	Endif
	
	If Len(AllTrim(SRX->RX_COD)) == 9
		cSem_De  := Transforma(CtoD(Left(SRX->RX_TXT,8),"DDMMYY"))
		cSem_Ate := Transforma(CtoD(Subs(SRX->RX_TXT,10,8),"DDMMYY"))
	Else
	   cSem_De  := Transforma(If("/" $ SRX->RX_TXT , CtoD(SubStr( SRX->RX_TXT, 1,10),"DDMMYY") , StoD(SubStr( SRX->RX_TXT, 1,8 ))))
	   cSem_Ate := Transforma(If("/" $ SRX->RX_TXT , CtoD(SubStr( SRX->RX_TXT, 12,10),"DDMMYY"), StoD(SubStr( SRX->RX_TXT,12,8 ))))
	EndIf
EndIf	

Return Nil