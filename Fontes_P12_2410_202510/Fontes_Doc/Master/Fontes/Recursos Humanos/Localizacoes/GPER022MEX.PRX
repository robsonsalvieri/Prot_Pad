#INCLUDE "PROTHEUS.CH"
#INCLUDE "fileio.ch"
#INCLUDE "GPER022MEX.ch"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPER022MEX ³ Autor ³ Jesus Peñaloza           ³ Data ³ 01.04.14   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Informe Comparativo Bimestral                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador   ³ Data   ³ BOPS/FNC  ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³LuisEnríquez  ³28-12-16³SERINN001-798³-Se modificó creación de tablas temp.   ³±±
±±³              ³        ³             ³ para utilizar clase FWTemporaryTable.  ³±±
±±³LuisEnríquez  ³20-01-17³SERINN001-866³-Se realiza merge de 12.1.15 contra main³±±
±±³              ³        ³             ³ para modificar creación de tablas tem-.³±±
±±³              ³        ³             ³ porales CTREE.                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPER022MEX()
	Local   oReport
	Private cPerg   := "GPER022MEX"
	Private cBim    := ''
	Private cAnio   := ''
	Private cLisPat := ''
	Private cLisReg := ''
	Private cArcMov := ''
	Private cArcPer := ''
	Private nInform := 0
	Private aLisReg := {}
	Private cFilSRA := xFilial("SRA")
	Private cFilRCO := xFilial("RCO")
	Private cFilRHC := xfilial("RHC")
	Private cFilRHD := xfilial("RHD")
	Private cRegPat := ''
	Private lTodos  := .F.
	Private lPrinted := .T.
	Private cTitulo := OemToAnsi(STR0034)//COMPARATIVO DE CUOTAS OBRERO-PATRONALES, APORTACIONES Y AMORTIZACIONES INFONAVIT -BIMESTRAL
	Private oTmpTable := Nil
	Private aOrdem := {}

	oReport := ReportDef()
	oReport:PrintDialog()
	If !lPrinted
		GPER022MEX()
	EndIf
	TMB->(dbCloseArea())
	TPB->(dbCloseArea())
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³TodoOK    ³ Autor ³ Microsiga             ³ Data ³06/04/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica los valores de los parametros                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ TodoOK(cExp1)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cExp1.- Nombre del grupo de preguntas                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER022                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function TodoOK(cPerg)
	Local nCont:=0
	Local lRet := .F.
	Local nHdm := 0
	Local nHdp := 0
	Local nTamReg:= TamSX3("RCO_CODIGO")[1]

	Pergunte(cPerg,.F.)
	cBim    := MV_PAR01
	cAnio   := AllTrim(MV_PAR02)
	cLisReg := MV_PAR03
	cArcMov := MV_PAR04
	cArcPer := MV_PAR05
	nInform := MV_PAR06
	nHdm := fOpen(cArcMov)
	nHdp := fOpen(cArcPer)
	If Len(Alltrim(cBim)) == 0 .or. Val(cBim)>6 .OR. Val(cBim)<1 .or. Valtype(Val(cBim)) != "N"
		Aviso(STR0001, STR0002, {STR0003})//Ingresa un Bimestre
	ElseIf Len(cAnio) != 4 .or. Valtype(Val(cAnio)) != "N"
		Aviso(STR0001, STR0004, {STR0003})//"Ingresa el año a procesar"
	ElseIf Len(AllTrim(cLisReg)) == 0
		Aviso(STR0001, STR0005, {STR0003})//"Selecciona al menos un registro patronal"
	ElseIf nHdm == -1
		Aviso(STR0001, STR0006, {STR0003})//"El archivo de movimientos no fue encontrado"
	ElseIf nHdp == -1
		Aviso(STR0001, STR0007, {STR0003})//"El archivo de datos personales no fue encontrado"
	Else
		lRet := .T.
	EndIf
	fClose(nHdm)
	fClose(nHdp)
	If nInform == 1
		lTodos := .T.
	EndIf
	For nCont := 1 To Len( cLisReg ) Step nTamReg       
		IF EMPTY(SubStr( cLisReg , nCont , nTamReg ))
			EXIT
		ENDIF   
		cLisPat+="'"+SubStr( cLisReg , nCont , nTamReg )+"',"
		aAdd(aLisReg, SubStr( cLisReg , nCont , nTamReg ))
	Next       

	cLisPat:=substr(cLisPat,1,len(cLisPat)-1)
Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³CreaTablas³ Autor ³ Jesus Peñaloza        ³ Data ³06/04/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Creacion de tablas temporales                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ CreaTablas()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER022                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function CreaTablas()
	Local aTMBMov := {}
	Local aTMBPer := {}
	
	If oTmpTable <> Nil
		oTmpTable:Delete()
		oTmpTable := Nil 
	EndIf

	aAdd(aTMBMov, {"TM_RG", TamSx3("RA_RG")[3], TamSx3("RA_RG")[1], TamSx3("RA_RG")[2]})
	aAdd(aTMBMov, {"TM_MAT", TamSx3("RA_MAT")[3], TamSx3("RA_MAT")[1], TamSx3("RA_MAT")[2]})
	aAdd(aTMBMov, {"TM_NREPAT", TamSx3("RHC_CODRPA")[3], TamSx3("RHC_CODRPA")[1], TamSx3("RHC_CODRPA")[2]})
	aAdd(aTMBMov, {"TM_TPMOV", TamSx3("RHC_TPMOV")[3], TamSx3("RHC_TPMOV")[1], TamSx3("RHC_TPMOV")[2]})
	aAdd(aTMBMov, {"TM_DTMOV", TamSx3("RHC_DTMOV")[3], TamSx3("RHC_DTMOV")[1], TamSx3("RHC_DTMOV")[2]})
	aAdd(aTMBMov, {"TM_NDTRAB", TamSx3("RHC_NDTRAB")[3], TamSx3("RHC_NDTRAB")[1], TamSx3("RHC_NDTRAB")[2]})
	aAdd(aTMBMov, {"TM_SALDII", TamSx3("RHC_SALDII")[3], TamSx3("RHC_SALDII")[1], TamSx3("RHC_SALDII")[2]})
	aAdd(aTMBMov, {"TM_RTPAT", TamSx3("RHC_RTPAT")[3], TamSx3("RHC_RTPAT")[1], TamSx3("RHC_RTPAT")[2]})
	aAdd(aTMBMov, {"TM_CYVPAT", TamSx3("RHC_CYVPAT")[3], TamSx3("RHC_CYVPAT")[1], TamSx3("RHC_CYVPAT")[2]})
	aAdd(aTMBMov, {"TM_CYVTRA", TamSx3("RHC_CYVTRA")[3], TamSx3("RHC_CYVTRA")[1], TamSx3("RHC_CYVTRA")[2]})
	aAdd(aTMBMov, {"TM_INFONA", TamSx3("RHC_INFONA")[3], TamSx3("RHC_INFONA")[1], TamSx3("RHC_INFONA")[2]})
	aAdd(aTMBMov, {"TM_AMORCF", TamSx3("RHC_AMORCF")[3], TamSx3("RHC_AMORCF")[1], TamSx3("RHC_AMORCF")[2]})
	aAdd(aTMBMov, {"TM_LEIDO", "C", 1, 0})


	oTmpTable := FWTemporaryTable():New("TMB")
	oTmpTable:SetFields( aTMBMov )
	aOrdem	:=	{"TM_MAT","TM_DTMOV","TM_TPMOV"}  
	oTmpTable:AddIndex("IN1", aOrdem)
	oTmpTable:Create()

	AADD(aTMBPer, {"TP_RG"   ,TamSx3("RA_RG")[3],TamSx3("RA_RG")[1] ,TamSx3("RA_RG")[2]})
	AADD(aTMBPer, {"TP_MAT"   ,TamSx3("RA_MAT")[3],TamSx3("RA_MAT")[1],TamSx3("RA_MAT")[2]})
	AADD(aTMBPer, {"TP_NOME",TamSx3("RA_NOME")[3],TamSx3("RA_PRINOME")[1]+TamSx3("RA_SECNOME")[1]+TamSx3("RA_PRISOBR")[1]+TamSx3("RA_SECSOBR")[1],TamSx3("RA_NOME")[2]})
	AADD(aTMBPer, {"TP_CURP"  ,TamSx3("RA_CURP")[3],TamSx3("RA_CURP")[1],TamSx3("RA_CURP")[2]})

	oTmpTable := FWTemporaryTable():New("TPB")
	oTmpTable:SetFields( aTMBPer )
	aOrdem	:=	{"TP_MAT"}  
	oTmpTable:AddIndex("IN1", aOrdem)
	oTmpTable:Create()

	Processa({|| LeePersona(cArcPer)},STR0008, STR0009, .T.)//Leyendo Archivo de Datos Personales...
	Processa({|| LeeMovimie(cArcMov)},STR0010, STR0009, .T.)//Leyendo Archivo de Movimientos...
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³LeeMovimie ³ Autor ³ Jesus Peñaloza       ³ Data ³06/04/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Lee archivo de movimientos, y los guarda en la tabla temp. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ LeeMovimie(cExp1)                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cExp1.- Nombre del archivo a leer                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CreaTablas()                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function LeeMovimie(cArc)
	Local cEOL    := CHR(13)+CHR(10)
	Local nMov    := fOpen(cArc)
	Local cLinea  := ''
	Local nLinea  := 100+Len(cEOL)
	Local nLineas := 0
	Local nX      := 0
	Local nTamFile:= fSeek(nMov,0,FS_END)

	Local cNss    := ''
	Local cmat    := ''
	Local cReg    := ''
	Local cTip    := ''
	Local cFec    := ''
	Local nDias   := 0
	Local nSaldii := 0
	Local nRtpat  := 0
	Local nCyvpat := 0
	Local nCyvtra := 0
	Local nInfona := 0
	Local nAmorcf := 0
	Local nOrder := 0
	Local aArea := GetArea()
	Local cAliasSRA:=CriaTrab(Nil,.F.)
	fSeek(nMov,0,FS_SET)
	nLineas := fRead(nMov, @cLinea, nLinea)

	dbSelectArea("SRA")
	IndRegua("SRA",cAliasSRA,"RA_FILIAL+RA_RG",,"RA_FILIAL == '"+cFilSRA+"'","Seleccionando Registros...")
	nOrder := RetIndex("SRA")
	dbSelectArea("SRA")
	#IFNDEF TOP
		dbSetIndex(cAliasSRA+OrdBagExt())
	#ENDIF
	dbSelectArea("SRA")
	dbSetOrder(nOrder+1)
	ProcRegua(nTamFile/100) 
	While nLineas >= nLinea
		IncProc()
		cNss := substr(cLinea, 22, 11)
		If SRA->(dbSeek(cFilSRA+cNss))
			cMat := SRA->RA_MAT
		EndIf

		cReg := substr(cLinea, 1, 10)
		cTip := substr(cLinea, 34, 1)
		cFec := substr(cLinea, 35, 10)
		nDias:= Val(substr(cLinea, 45, 2))
		nSaldii := Val(substr(cLinea, 47, 4)+"."+substr(cLinea,51,2))
		nRtpat  := Val(substr(cLinea, 53, 6)+"."+substr(cLinea,59,2))
		nCyvpat := Val(substr(cLinea, 61, 6)+"."+substr(cLinea,67,2))
		nCyvtra := Val(substr(cLinea, 69, 6)+"."+substr(cLinea,75,2))
		nInfona := Round(Val(substr(cLinea, 84, 9)),2)
		nAmorcf := Round(Val(substr(cLinea, 93, 8)),2)

		dbSelectArea("RCO")
		RCO->(dbSetOrder(1))
		For nX := 1 to Len(aLisReg)
			If RCO->(dbSeek(cFilRCO+aLisReg[nX]))
				cRegPat := RCO->RCO_NREPAT
			EndIf
			If cReg == substr(cRegPat,1,10) .and. (Val(substr(cFec, 4, 2)) == Val(cBim)*2 .or. Val(substr(cFec, 4, 2)) == Val(cBim)*2-1);
			.and. (cAnio == substr(cFec, 7, 4))
				RecLock("TMB", .T.)
				TMB->TM_RG    := cNss
				TMB->TM_MAT    := cMat
				TMB->TM_TPMOV  := cTip
				TMB->TM_DTMOV  := CTOD(cFec)
				TMB->TM_NDTRAB := nDias
				TMB->TM_SALDII := nSaldii
				TMB->TM_RTPAT  := nRtpat
				TMB->TM_CYVPAT := nCyvpat
				TMB->TM_CYVTRA := nCyvtra
				TMB->TM_INFONA := nInfona
				TMB->TM_AMORCF := nAmorcf
				MsUnlock()
			EndIf
		Next nX
		nLineas := fRead(nMov, @cLinea, nLinea)
	EndDo
	RetIndex("SRA")
	Ferase(cAliasSRA+OrdBagext())
	fClose(nMov)
	RestArea( aArea )
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³LeePersona ³ Autor ³ Jesus Peñaloza       ³ Data ³09/04/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Lee archivo de datos personales y los guarda en la tabla   ³±±
±±³          ³ temporal                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ LeePersona(cExp1)                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cExp1.- Nombre del archivo a leer                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CreaTablas()                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function LeePersona(cArc)
	Local cEOL    := CHR(13)+CHR(10)
	Local nMov    := fOpen(cArc)
	Local cLinea  := ''
	Local nLinea  := 104+Len(cEOL)
	Local nLineas := 0
	Local nX      := 0
	Local nTamFile:= fSeek(nMov,0,FS_END)

	Local cReg  := ''
	Local cNss  := ''
	Local cMat  := ''
	Local cNom  := ''
	Local cCurp := ''
	Local cAliasSRA:=CriaTrab(Nil,.F.)
	Local nOrder := 0
	Local aArea := GetArea()
	fSeek(nMov,0,FS_SET)
	nLineas := fRead(nMov, @cLinea, nLinea)

	dbSelectArea("SRA")
	IndRegua("SRA",cAliasSRA,"RA_FILIAL+RA_RG",,"RA_FILIAL == '"+cFilSRA+"'","Seleccionando Registros...")
	nOrder := RetIndex("SRA")
	dbSelectArea("SRA")
	#IFNDEF TOP
		dbSetIndex(cAliasSRA+OrdBagExt())
	#ENDIF
	dbSelectArea("SRA")
	dbSetOrder(nOrder+1)
	ProcRegua(nTamFile/100)
	While nLineas >= nLinea
		IncProc()
		cNss := substr(cLinea, 24, 11)
		If SRA->(dbSeek(cFilSRA+cNss))
			cMat := SRA->RA_MAT
			cReg := SRA->RA_CODRPAT
		EndIf

		cNss  := substr(cLinea, 24, 11)
		cNom  := substr(cLinea, 35, 49)
		cCurp := substr(cLinea, 87, 18)
		dbSelectArea("RCO")
		RCO->(dbSetOrder(1))
		For nX := 1 to Len(aLisReg)
			If cReg == aLisReg[nX]
				RecLock("TPB", .T.)
				TPB->TP_RG    := cNss
				TPB->TP_MAT    := cMat
				TPB->TP_NOME := cNom
				TPB->TP_CURP   := cCurp
				MsUnlock()
			EndIf
		Next nX
		nLineas := fRead(nMov, @cLinea, nLinea)
	EndDo
	RetIndex("SRA")
	Ferase(cAliasSRA+OrdBagext()) 
	fClose(nMov)
	RestArea( aArea )
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ ReportDef  ³ Autor ³ Jesus Peñaloza        ³ Data ³09/04/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Definicion de la estructura del reporte                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ReportDef()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER022MEX()                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()

	Local aArea      := GetArea()

	Local 	oReport
	Local	oSection1
	Local 	oSection2
	Local 	oSection3
	Local 	oSection4
	Local 	oSection5
	Local 	oSection6
	Local  oSection7
	Local  oSection8
	Local  oSection9

	Private cNomeProg := "GPER022"

	Private aOrd    := {OemToAnsi("Nombre"),OemToAnsi("Matricula")}	//### "Nombre"###"Matricula"

	cTitulo := Trim(cTitulo)

	oReport:=TReport():New(cNomeProg,OemToAnsi(cTitulo),  cPerg ,{|oReport| PrintReport(oReport)})	
	oReport:SetPortrait()     // Define a orientacao de pagina do relatorio como retrato.
	oReport:nColSpace	:=1
	oReport:nFontBody	:= 6 // Define o tamanho da fonte.
	oReport:nLineHeight	:= 40 // Define a altura da linha.
	oReport:cFontBody	:="COURIER NEW"

	oReport:Setlandscape(.T.)//Pag Horizontal
	oReport:SetTotalInLine(.F.)

	oSection1:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"
	oSection2:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"
	oSection3:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

	oSection1:SetHeaderPage(.T.)	//Exibe Cabecalho da Secao
	oSection1:SetLineStyle(.t.)   //Pone titulo del campo y aun lado el y valor
	oSection1:SetCharSeparator(" ")    
	oSection1:SetHeaderBreak(.T.)

	TRCell():New(oSection1,"RCO_NREPAT",    ,STR0027 ,PesqPict("RHD","RCO_NREPAT")		,TamSx3("RCO_NREPAT")[1])//Registro Patronal
	TRCell():New(oSection1,"SM0_CGC",       ,STR0028 ,                                	,TamSx3("RA_CIC")[1]+1)//"RFC:"
	TRCell():New(oSection1,"RCO_NOME",	    ,STR0029 ,PesqPict("RHD","RCO_NOME")     	,TamSx3("RCO_NOME")[1])//"Nombre o Razón Social "
	oSection1:Cell("RCO_NOME"):SetCellBreak(.T.)
	TRCell():New(oSection1,"MESANO",        ,STR0030 ,                                 ,10                      )//"Bimestre Y Ano de Proceso"

	oSection4:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

	oSection4:SetTotalInLine(.F.)  
	oSection4:SetHeaderSection(.T.)	
	oSection4:SetHeaderPage(.T.)	
	oSection4:SetLineStyle(.F.)
	oSection4:nColSpace:=10    

	TRCell():New(oSection4,"ORIGEN",     ,STR0011 ,           ,7)//Proviene de
	TRCell():New(oSection4,"RA_RG",      ,STR0012 ,PesqPict("SRA","RA_RG"),TamSx3("RA_RG")[1]+3)//"Numero de Seguridad Social"
	TRCell():New(oSection4,"NOMECOM",    ,STR0013 ,PesqPict("SRA","RA_NOME")          ,TamSx3("RA_NOME")[1]+20)//"Nombre"
	TRCell():New(oSection4,"EMPLEADO",   ,STR0014 ,PesqPict("SRA","RA_MAT")         ,TamSx3("RA_MAT")[1])//"Empleado"
	TRCell():New(oSection4,"RA_CURP",    ,STR0015 ,PesqPict("SRA","RA_CURP")        ,TamSx3("RA_CURP")[1]+2)//"C.U.R.P."

	oSection5:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/) //"EncPrograma"

	oSection5:SetTotalInLine(.f.)  
	oSection5:SetHeaderSection(.T.)	//Exibe Cabecalho da Secao
	oSection5:SetHeaderPage(.T.)	//Exibe Cabecalho da Secao
	oSection5:SetLineStyle(.F.)   //Pone titulo del campo y aun lado el y valor 
	oSection5:SetTotalText(" ") 

	TRCell():New(oSection5,"ORIGEN",       ,STR0011 ,           ,TamSx3("RA_CIC")[1])//Proviene de
	TRCell():New(oSection5,"RHC_TPMOV",    ,STR0016 , PesqPict("RHC","RHC_TPMOV")      ,4)//Clave
	TRCell():New(oSection5,"RHC_DTMOV",    ,STR0017 , PesqPict("RHC","RHC_DTMOV")      ,10)//Fecha
	TRCell():New(oSection5,"RHC_NDTRAB",   ,STR0018 , PesqPict("RHC","RHC_NDTRAB")     ,4 )//"Dias"
	TRCell():New(oSection5,"ERRDIAS",      ,"", ,1 )
	TRCell():New(oSection5,"RHC_SALDII",   ,STR0019 , PesqPict("RHC","RHC_SALDII")     ,TamSx3("RHC_SALDII")[1])//SDI
	TRCell():New(oSection5,"ERRSALDI",      ,"", ,1 )
	TRCell():New(oSection5,"RETIRO",       ,STR0020 , PesqPict("RHC","RHC_RETPAT")      ,TamSx3("RHC_RETPAT")[1])//Retiro
	TRCell():New(oSection5,"ERRRETIRO",      ,"", ,1 )
	TRCell():New(oSection5,"CYVPAT",       ,STR0021 , PesqPict("RHC","RHC_CYVPAT")     ,TamSx3("RHC_CYVPAT")[1])//C. y V. Pat
	TRCell():New(oSection5,"ERRPAT",      ,"", ,1 )
	TRCell():New(oSection5,"CYVOBR",       ,STR0022 , PesqPict("RHC","RHC_CYVPAT")     ,TamSx3("RHC_CYVPAT")[1])//C. Y V. Obr.
	TRCell():New(oSection5,"ERROBR",      ,"", ,1 )
	TRCell():New(oSection5,"SUMA",         ,STR0023 , PesqPict("RHC","RHC_CYVPAT")     ,TamSx3("RHC_CYVPAT")[1])//Suma.
	TRCell():New(oSection5,"APORTPAR",     ,STR0024 , "9,999,999.99"     ,14)//"Aport. Par."
	TRCell():New(oSection5,"ERRINFONA",      ,"", ,1 )
	TRCell():New(oSection5,"AMORT",        ,STR0025 , "9,999,999.99"     ,14)//Amortizacion
	TRCell():New(oSection5,"ERRAMORT",      ,"", ,1 )
	TRCell():New(oSection5,"SUMAI",        ,STR0023 , PesqPict("RHC","RHC_CYVPAT")    ,TamSx3("RHC_CYVPAT")[1])//Suma
	TRCell():New(oSection5,"TOTAL",        ,STR0026 , PesqPict("RHC","RHC_CYVPAT")    ,TamSx3("RHC_CYVPAT")[1])//Total

	oSection6:= TRSection():New(oReport,oemtoansi("EncPrograma"),,aOrd,/*Campos do SX3*/,/*Campos do SIX*/)//"EncPrograma"

	oSection6:SetTotalInLine(.f.)  
	oSection6:SetHeaderSection(.f.)	//Exibe Cabecalho da Secao
	oSection6:SetHeaderPage(.f.)	//Exibe Cabecalho da Secao
	oSection6:SetLineStyle(.F.)   //Pone titulo del campo y aun lado el y valor
	TRCell():New(oSection6,"ORIGEN",       ,STR0011 ,      	,5)//Proviene
	TRCell():New(oSection6,"RHC_TPMOV",    ,STR0016 ,PesqPict("RHC","RHC_TPMOV")      	,5)//Clave
	TRCell():New(oSection6,"RHC_DTMOV",    ,STR0017 ,PesqPict("RHC","RHC_DTMOV")      	,10)//Fecha
	TRCell():New(oSection6,"RHC_NDTRAB",   ,STR0018 , /*PesqPict("RHC","RHC_NDTRAB")*/"99,999"   ,6)//"Dias"
	TRCell():New(oSection6,"RHC_SALDII",   ,STR0019 , PesqPict("RHC","RHC_SALDII")     	,TamSx3("RHC_SALDII")[1])//SDI
	TRCell():New(oSection6,"RETIRO",       ,STR0020 ,PesqPict("RHC","RHC_RETPAT")     	,TamSx3("RHC_RETPAT")[1])//Retiro
	TRCell():New(oSection6,"CYVPAT",       ,STR0021 ,PesqPict("RHC","RHC_CYVPAT") ,TamSx3("RHC_CYVPAT")[1])//C. y V. Pat
	TRCell():New(oSection6,"CYVOBR",       ,STR0022 ,PesqPict("RHC","RHC_CYVTRA"),TamSx3("RHC_CYVTRA")[1])//C. Y V. Obr.
	TRCell():New(oSection6,"SUMA",         ,STR0023 ,PesqPict("RHC","RHC_CYVPAT")        ,TamSx3("RHC_CYVPAT")[1])//Suma.
	TRCell():New(oSection6,"APORTPAR",     ,STR0024 ,PesqPict("RHC","RHC_CYVPAT") ,TamSx3("RHC_CYVPAT")[1])//"Aport. Par."
	TRCell():New(oSection6,"AMORT",        ,STR0025 ,/*"9,999,999.99"*/ PesqPict("RHC","RHC_AMORCF"),12)//Amortizacion
	TRCell():New(oSection6,"SUMAI",        ,STR0023 ,PesqPict("RHC","RHC_CYVPAT")     	,TamSx3("RHC_CYVPAT")[1])//Suma
	TRCell():New(oSection6,"TOTAL",        ,STR0026 ,PesqPict("RHC","RHC_CYVPAT")    ,TamSx3("RHC_CYVPAT")[1])//Total

	oSection7 := TRSection():New(oReport,oemtoansi("EncPrograma"), ,aOrd, ,)//EncPrograma
	oSection7:SetTotalInLine(.f.)  
	oSection7:SetHeaderSection(.F.)	//Exibe Cabecalho da Secao
	oSection7:SetHeaderPage(.F.)	//Exibe Cabecalho da Secao
	oSection7:SetLineStyle(.F.)   //Pone titulo del campo y aun lado el y valor 
	oSection7:SetTotalText(" ")

	TRCell():New(oSection7,"REGISTROS", ,STR0035 , ,100)//Registros

	oSection8 := TRSection():New(oReport,oemtoansi("EncPrograma"), ,aOrd, ,)//EncPrograma
	oSection8:SetTotalInLine(.f.)  
	oSection8:SetHeaderSection(.F.)	//Exibe Cabecalho da Secao
	oSection8:SetHeaderPage(.F.)	//Exibe Cabecalho da Secao
	oSection8:SetLineStyle(.F.)   //Pone titulo del campo y aun lado el y valor 
	oSection8:SetTotalText(" ")

	TRCell():New(oSection8,"ORIGEN",     ,STR0011 ,           ,7)//Proviene de
	TRCell():New(oSection8,"RA_RG",      ,STR0012 ,PesqPict("SRA","RA_RG"),TamSx3("RA_RG")[1]+3)//"Numero de Seguridad Social"
	TRCell():New(oSection8,"NOMECOM",    ,STR0013 ,PesqPict("SRA","RA_NOME")          ,TamSx3("RA_NOME")[1]+20)//"Nombre"
	TRCell():New(oSection8,"EMPLEADO",   ,STR0014 ,PesqPict("SRA","RA_MAT")         ,TamSx3("RA_MAT")[1])//"Empleado"
	TRCell():New(oSection8,"RA_CURP",    ,STR0015 ,PesqPict("SRA","RA_CURP")        ,TamSx3("RA_CURP")[1]+2)//"C.U.R.P."

	oSection9 := TRSection():New(oReport,oemtoansi("EncPrograma"), ,aOrd, ,)
	oSection9:SetTotalInLine(.f.)  
	oSection9:SetHeaderSection(.F.)	//Exibe Cabecalho da Secao
	oSection9:SetHeaderPage(.F.)	//Exibe Cabecalho da Secao
	oSection9:SetLineStyle(.F.)   //Pone titulo del campo y aun lado el y valor 
	oSection9:SetTotalText(" ")

	TRCell():New(oSection9,"ORIGEN",       ,STR0011 ,           ,TamSx3("RA_CIC")[1])//Proviene de
	TRCell():New(oSection9,"RHC_TPMOV",    ,STR0016 , /*PesqPict("RHC","RHC_TPMOV")*/      ,4)//Clave
	TRCell():New(oSection9,"RHC_DTMOV",    ,STR0017 , /*PesqPict("RHC","RHC_DTMOV")*/      ,10)//Fecha
	TRCell():New(oSection9,"RHC_NDTRAB",   ,STR0018 , /*PesqPict("RHC","RHC_NDTRAB")*/     ,4 )//"Dias"
	TRCell():New(oSection9,"RHC_SALDII",   ,STR0019, ,12)//SDI
	TRCell():New(oSection9,"RETIRO",       ,STR0020 , /*PesqPict("RHC","RHC_RETPAT")*/      ,TamSx3("RHC_RETPAT")[1])//Retiro
	TRCell():New(oSection9,"CYVPAT",       ,STR0021 , /*PesqPict("RHC","RHC_CYVPAT")*/     ,TamSx3("RHC_CYVPAT")[1])//C. y V. Pat
	TRCell():New(oSection9,"CYVOBR",       ,STR0022 , /*PesqPict("RHC","RHC_CYVPAT")*/     ,TamSx3("RHC_CYVPAT")[1])//C. Y V. Obr.
	TRCell():New(oSection9,"SUMA",         ,STR0023 , /*PesqPict("RHC","RHC_CYVPAT")*/     ,TamSx3("RHC_CYVPAT")[1])//Suma.
	TRCell():New(oSection9,"APORTPAR",     ,STR0024 , /*"9,999,999.99"*/     ,14)//"Aport. Par.
	TRCell():New(oSection9,"AMORT",        ,STR0025 , /*"9,999,999.99"*/     ,14)//Amortizacion
	TRCell():New(oSection9,"SUMAI",        ,STR0023 , /*PesqPict("RHC","RHC_CYVPAT")*/    ,TamSx3("RHC_CYVPAT")[1])//Suma
	TRCell():New(oSection9,"TOTAL",        ,STR0026 , /*PesqPict("RHC","RHC_CYVPAT")*/    ,TamSx3("RHC_CYVPAT")[1])//Total

	OSECTION1:NLINESBEFORE:=0
	OSECTION2:NLINESBEFORE:=0
	OSECTION3:NLINESBEFORE:=0
	OSECTION4:NLINESBEFORE:=0
	OSECTION5:NLINESBEFORE:=0
	OSECTION6:NLINESBEFORE:=0
	OSECTION7:NLINESBEFORE:=0
	OSECTION8:NLINESBEFORE:=0
	OSECTION9:NLINESBEFORE:=0

Return(oReport)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao     ³ PrintReport³ Autor ³ Jesus Peñaloza     ³ Data ³ 09.04.14 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descripción³ Función para impresion del Reporte                        ³±± 
±±³           ³                                                           ³±± 
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros ³ oExp1.- Objeto de tipo TReport                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxis   ³ PrintReport(oExp1)                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PrintReport(oReport)

	Local oSection1  := oReport:Section(1)
	Local oSection4  := oReport:Section(4)
	Local oSection5  := oReport:Section(5)
	Local oSection6  := oReport:Section(6)
	Local oSection7  := oReport:Section(7)
	Local oSection8  := oReport:Section(8)
	Local oSection9  := oReport:Section(9)

	Local cTitle	 := ""     
	Local cOrdem     := "" 
	Local cAliasRHC  := "QRYRHC" 
	Local cCposQuery := ""
	Local cCposFrom  := ""
	Local cWhere     := ""
	Local cMesIni    := ""
	Local cMesFin    := ""
	Local cAnoMesIni := ""
	Local cAnoMesFin := ""
	Local cRegPAT    := ""

	Local nOrdem	  := oSection1:GetOrder()
	Local nDiaFin    :=0
	Local nAmort     :=0
	Local nSaldi     :=0
	Local nDiasT     :=0
	Local nRetiro    :=0
	Local nCyvtra    :=0
	Local nCyvpat    :=0
	Local nAmorti    :=0
	Local nSuma      :=0
	Local nSumaI     :=0
	Local nApoPat    :=0
	Local nIRhc      :=1

	Local dDataIni            
	Local dDataFin 

	Local aRHC       :={}
	Local aRHBT      :={}

	Local nTdtra := 0
	Local nTsald := 0
	Local nTrtpa := 0
	Local nTcyvp := 0
	Local nTcyvt := 0
	Local nTinfo := 0
	Local nTamor := 0
	Local lSwitch := .T.
	Local lNoLeido := .F.
	Local cTipomov := ''

	Pergunte(oReport:GetParam(),.F.)
	If TodoOk(cPerg)
		CreaTablas()

		If cBim == "1"
			cMesIni := "01"
			cMesFin := "02"
		ElseIf cBim == "2"
			cMesIni := "03"
			cMesFin := "04"
		ElseIf cBim == "3"
			cMesIni := "05"
			cMesFin := "06"
		ElseIf cBim == "4"
			cMesIni := "07"
			cMesFin := "08"
		ElseIf cBim == "5"
			cMesIni := "09"
			cMesFin := "10"
		ElseIf cBim == "6"
			cMesIni := "11"
			cMesFin := "12"
		Else
			Return
		Endif

		cAnoMesIni := cAnio+cMesIni
		cAnoMesFin := cAnio+cMesFin
		dDataIni  := Ctod("01/"+cMesIni+"/"+ cAnio)           
		dDataFin  := Ctod("01/"+cMesFin+"/"+ cAnio)           
		nDiaFin	  := F_ULTDIA(dDataFin)
		dDataFin  :=Ctod(SUBSTR(strZERO(nDiaFin,2),1,2)+"/"+cMesFin+"/"+cAnio)
		cTitle 	:= If(AllTrim(oReport:Title())==AllTrim(cTitle), OemToansi(cTitulo),oReport:Title())
		If nInform == 1
			cTitulo+=STR0038
		Else
			cTitulo+=STR0039
		EndIf
		oReport:SetTitle(cTitulo) 

		#IFDEF TOP
			//Transforma los parametros de tipo Rango en expresion SQL para ser utilizada en query 
			cCposQuery := "% RCO_NREPAT,RA_NOME,RCO_ATIVID,RCO_CVEZON,RCO_NOME,RA_RG,RA_PRISOBR,RA_SECSOBR,RA_PRINOME,RA_SECNOME,RA_CIC,RA_CURP, RHC.*,RHD_VALINF,RHD_FATRSC %"
	
			cCposFrom   := "%" + RetSqlName("RHD") + " RHD, " 
			cCposFrom   +=  RetSqlName("RCO")+ " RCO, "
			cCposFrom   +=  RetSqlName("SRA")+ " SRA, "
			cCposFrom   +=  RetSqlName("RHC")+ " RHC  %"
	
			cWhere :="%    RCO_CODIGO=RHD_CODRPA"
			cWhere +="     AND RA_MAT=RHD_MAT"
			cWhere +="     AND RA_FILIAL=RHD_FILIAL"
			cWhere +="     AND RHD_FILIAL=RHC_FILIAL"
			cWhere +="     AND RHC_MAT=RHD_MAT"
			cWhere +="     AND RHC_ANOMES=RHD_ANOMES"
			cWhere +="     AND RHD_CODRPA IN ("+CLISPAT+")"//Registro Patronal 
			cWhere +="     AND RHC_DTMOV BETWEEN '"+Dtos(dDataIni)+"' AND '"+Dtos(dDataFin)+"' "//AnioMes
			cWhere +="     AND RHC_FILIAL  ='"+cFilRHC+"'"
			cWhere +="     AND RCO_FILIAL= '"  + xFilial("RCO" , RHD->RHD_FILIAL) +"' "
			cWhere +="     AND RA_FILIAL  ='"+cFilSRA+"'"
			cWhere +="     AND RHD_FILIAL  ='"+cFilRHD+"'"
	
			If ( TcSrvType()=="AS/400" )
				cWhere+="     AND RHD.@DELETED@  = ' ' " 
				cWhere+="     AND RCO.@DELETED@  = ' ' " 
				cWhere+="     AND SRA.@DELETED@  = ' ' " 
				cWhere+="     AND RHC.@DELETED@  = ' ' %" 
			ELSE 
				cWhere+="     AND RHD.D_E_L_E_T_  = ' ' " 
				cWhere+="     AND RCO.D_E_L_E_T_  = ' ' " 		
				cWhere+="     AND SRA.D_E_L_E_T_  = ' ' " 						
				cWhere+="     AND RHC.D_E_L_E_T_  = ' ' %" 				
			ENDIF                   
	
			If nOrdem == 2
				cOrdem := "% RCO_NREPAT,RHC_MAT,RHC_DTMOV,RHC_SEQMVT%"
			ElseIf nOrdem == 1
				cOrdem := "%RCO_NREPAT,RA_PRISOBR,RA_SECSOBR,RA_PRINOME,RA_SECNOME,RHC_DTMOV,RHC_SEQMVT%"
			Endif
	
			BeginSql alias cAliasRHC 
			SELECT  %exp:cCposQuery%
			FROM  %exp:cCposFrom% 
			WHERE %exp:cWhere%     
			ORDER BY %exp:cOrdem%
			EndSql
			//Los campo tipo fecha los deje como tipo fecha, pues por query son recuperados como string
			TCSetField(cAliasRHC,"RHC_DTMOV","D")
			TCSetField(cAliasRHC,"RHC_DTMINF","D")   

		#ELSE
			MSGERROR(STR0036)
		#ENDIF	

		Begin Sequence   

			dbSelectArea( cAliasRHC )
			(cAliasRHC)->(DbGoTop())  
			If (cAliasRHC)->(!Eof())
				While (cAliasRHC )->(!Eof())
					aAdd(aRHC,{(cAliasRHC)->RA_NOME,;	 //1
					(cAliasRHC)->RCO_ATIVID,; //2
					(cAliasRHC)->RCO_CVEZON,; //3
					(cAliasRHC)->RCO_NOME,;//4
					(cAliasRHC)->RA_RG,;//5
					(cAliasRHC)->RA_PRISOBR,;//6
					(cAliasRHC)->RA_SECSOBR  ,; //7
					(cAliasRHC)->RA_PRINOME, ;//8
					(cAliasRHC)->RA_SECNOME, ; //9
					(cAliasRHC)->RA_CIC, ; //10
					(cAliasRHC)->RA_CURP, ; //11
					(cAliasRHC)->RHC_MAT, ; //12
					(cAliasRHC)->RHC_RTPAT, ; //13
					(cAliasRHC)->RHC_CYVPAT, ; //14
					(cAliasRHC)->RHC_CYVTRA, ; //15
					(cAliasRHC)->RHC_INFONA, ; //16
					(cAliasRHC)->RHC_AMORCF, ; //17
					(cAliasRHC)->RHC_VALINF,; //18
					(cAliasRHC)->RHC_TPMINF,;//19
					(cAliasRHC)->rhc_TIPINF,; //20
					(cAliasRHC)->RHC_NUMINF,; //21
					(cAliasRHC)->RHC_TPMOV,; //22
					(cAliasRHC)->RHC_DTMOV,;//23  			(cAliasRHC)->RHC_NDTRAB-(cAliasRHC)->RHC_NDINC-(cAliasRHC)->RHC_NDFAL,;//24
					(cAliasRHC)->RHC_NDTRAB-(cAliasRHC)->RHC_NDFAL,;//24
					(cAliasRHC)->RHC_SALDII,; //25
					(cAliasRHC)->RHC_NDINC,;//26
					(cAliasRHC)->RHC_NDFAL,;//27
					(cAliasRHC)->RCO_NREPAT,;//28
					(cAliasRHC)->RHD_VALINF,;//29
					(cAliasRHC)->RHD_FATRSC,;//30
					(cAliasRHC)->RHC_DTMINF,; //31
					(cAliasRHC)->RHC_CFPAT,; //32
					(cAliasRHC)->RHC_EXEPAT,; //33
					(cAliasRHC)->RHC_EXETRA,; //34
					(cAliasRHC)->RHC_PDTRA,; //35
					(cAliasRHC)->RHC_PDPAT,; //36
					(cAliasRHC)->RHC_GMPPAT,; //37
					(cAliasRHC)->RHC_GMPTRA,; //38
					(cAliasRHC)->RHC_IVPAT,; //39
					(cAliasRHC)->RHC_IVTRA,; //40
					(cAliasRHC)->RHC_GPSPAT,; //41
					(cAliasRHC)->RHC_GPSPAT,; //42//GPSTRA
					(cAliasRHC)->RHC_RETPAT	})//43
					If (cAliasRHC)->RHC_TPMOV=="00"
						aRHBT:=leeRHB((cAliasRHC)->RHC_MAT,(cAliasRHC)->RHC_FILIAL,dDataFin )
						IF!EMPTY(aRHBT)
							aRHC[len(aRHC)][31]:= aRHBT[1][3]
							aRHC[len(aRHC)][19]:= aRHBT[1][4]
						EndIf
					Endif
					(cAliasRHC)->(dbskip()) 
				EndDo
				oReport:SetMeter(len(aRHC)) 
			EndIf

			If  nIRhc <= len(aRHC)	
				oSection1:Init()  
				oSection4:Init()
				oSection5:Init()
				oSection5:hIDE()
				oSection5:PrintLine()
				oSection5:sHOW()

				While nIRhc <= len(aRHC)
					cRegPAT:=aRHC[nIRhc,28]
					oSection1:Cell("RCO_NREPAT"):SetValue(aRHC[nIRhc,28])//Registro Patronal
					oSection1:Cell("SM0_CGC"):SetValue(SM0->M0_CGC+" ")//RFC.
					oSection1:Cell("RCO_NOME"):SetValue(aRHC[nIRhc,4])//"Nombre o Razón Social: "
					oSection1:Cell("MESANO"):SetValue(cBim+"/"+cAnio)//Bimestre/Anio
					oSection1:PrintLine()
					oReport:Fatline()		

					nSaldi:= 0
					nDiasT:=0
					nRetiro:=0
					nCyvtra:=0
					nCyvpat:=0
					nAmorti:=0
					nSuma:=0
					nSumaI:=0
					nApoPat:=0
					nAmort:=0

					nTdtra := 0
					nTsald := 0
					nTrtpa := 0
					nTcyvp := 0
					nTcyvt := 0
					nTinfo := 0
					nTamor := 0

					While nIRhc <= len(aRHC).And. cRegPAT==aRHC[nIRhc,28]

						dbSelectArea("TPB")
						TPB->(dbSetOrder(1))
						If lTodos == .F. //Si el reporte es de Diferencias
							If TMB->(dbSeek(ARHC[nIRhc,12]+DTOS(ARHC[nIRhc,23])+cTipomov))
								RecLock("TMB", .F.)
								TMB->TM_LEIDO := '*'
								MsUnlock()
								If TMB->TM_NDTRAB == ARHC[nIRhc,24] .AND. TMB->TM_SALDII == ARHC[nIRhc,25] .AND. TMB->TM_RTPAT == ARHC[nIRhc,13];
								.AND. TMB->TM_CYVPAT == ARHC[nIRhc,14] .AND. TMB->TM_CYVTRA == ARHC[nIRhc,15] .AND. TMB->TM_INFONA == ARHC[nIRhc,16];
								.AND. TMB->TM_AMORCF == ARHC[nIRhc,17]
									nIRhc++ //Aumentamos el contador para buscar el siguiente movimiento 
									loop //y evaluamos la expresion
								EndIf
							EndIf
						EndIf
						If TPB->(dbSeek(aRHC[nIRhc,12]))
							oSection4:Cell("ORIGEN"):SetValue("IMSS")
							If aRHC[nIRhc,5] != TPB->TP_RG
								oSection4:Cell("RA_RG"):SetValue(TPB->TP_RG +" *")
							Else
								oSection4:Cell("RA_RG"):SetValue(TPB->TP_RG)
							EndIf
							If alltrim(aRHC[nIRhc,6])+" "+alltrim(aRHC[nIRhc,7])+" "+alltrim(aRHC[nIRhc,8])+" "+alltrim(aRHC[nIRhc,9]) != alltrim(TPB->TP_NOME)
								oSection4:Cell("NOMECOM"):SetValue(TPB->TP_NOME+" *")
							Else
								oSection4:Cell("NOMECOM"):SetValue(TPB->TP_NOME)
							EndIf
							oSection4:Cell("EMPLEADO"):SetValue(TPB->TP_MAT)
							If aRHC[nIRhc,11] != TPB->TP_CURP
								oSection4:Cell("RA_CURP"):SetValue(TPB->TP_CURP+" *")
							Else
								oSection4:Cell("RA_CURP"):SetValue(TPB->TP_CURP)
							EndIf
							oSection4:PrintLine()
						EndIf
						oSection4:Cell("ORIGEN"):SetValue("Protheus")
						oSection4:Cell("RA_RG"):SetValue(aRHC[nIRhc,5])
						oSection4:Cell("NOMECOM"):SetValue(rtrim(aRHC[nIRhc,6])+" "+rtrim(aRHC[nIRhc,7])+" "+rtrim(aRHC[nIRhc,8])+" "+rtrim(aRHC[nIRhc,9]))
						oSection4:Cell("EMPLEADO"):SetValue(aRHC[nIRhc,12])
						oSection4:Cell("RA_CURP"):SetValue(aRHC[nIRhc,11])
						oSection4:PrintLine()
						cMat:=aRHC[nIRhc,12]

						While nIRhc <= len(aRHC).And. cMat==ARHC[nIRhc,12]  .And. cRegPAT==aRHC[nIRhc,28]
							IF ARHC[nIRhc,22] == '00'
								cTipomov := '9'
							ElseIf ARHC[nIRhc,22] == '01'
								cTipomov := '8'
							ElseIf ARHC[nIRhc,22] == '02'
								cTipomov := '2'
							ElseIf ARHC[nIRhc,22] == '05'
								cTipomov := '7'
							ElseIf ARHC[nIRhc,22] == '06'
								cTipomov := '8'
							EndIf
							oSection5:Cell("ORIGEN"):Show()  
							oSection5:Cell("RHC_TPMOV"):Show()
							oSection5:Cell("RHC_DTMOV"):Show()
							oSection5:Cell("RHC_NDTRAB"):Show()
							oSection5:Cell("RHC_SALDII"):Show()
							oSection5:Cell("RETIRO"):Show()
							oSection5:Cell("CYVPAT"):Show()
							oSection5:Cell("CYVOBR"):Show()
							oSection5:Cell("SUMA"):Show()
							oSection5:Cell("APORTPAR"):Show()
							oSection5:Cell("AMORT"):Show()
							oSection5:Cell("SUMAI"):Show()
							oSection5:Cell("TOTAL"):Show()

							//Inicializa Celdas
							oSection5:Cell("ORIGEN"):SetValue(Space(1))
							oSection5:Cell("RHC_TPMOV"):SetValue(Space(1))
							oSection5:Cell("RHC_DTMOV"):SetValue(Space(1))
							oSection5:Cell("RHC_NDTRAB"):SetValue(0)
							oSection5:Cell("RHC_SALDII"):SetVALue(0)
							oSection5:Cell("RETIRO"):SetValue(Space(1))
							oSection5:Cell("CYVPAT"):SetValue(Space(1))
							oSection5:Cell("CYVOBR"):SetValue(Space(1))
							oSection5:Cell("SUMA"):SetValue(0)
							oSection5:Cell("APORTPAR"):SetValue(0)
							oSection5:Cell("AMORT"):SetValue(0)
							oSection5:Cell("SUMAI"):SetValue(0)
							oSection5:Cell("TOTAL"):SetValue(0)
							dbSelectArea("TMB")
							TMB->(dbSetOrder(1))
							If lSwitch//Procesa Registros IMSS
								If TMB->(dbSeek(cMat+DTOS(ARHC[nIRhc,23])+cTipomov))
									RecLock("TMB", .F.)
									TMB->TM_LEIDO := '*'
									MsUnlock()
									oSection5:Cell("ORIGEN"):SetValue(STR0032)
									oSection5:Cell("RHC_TPMOV"):SetValue(TMB->TM_TPMOV)
									oSection5:Cell("RHC_DTMOV"):SetValue(TMB->TM_DTMOV)
									oSection5:Cell("ORIGEN"):SetValue(STR0032)
									oSection5:Cell("RHC_TPMOV"):SetValue(TMB->TM_TPMOV)
									oSection5:Cell("RHC_DTMOV"):SetValue(TMB->TM_DTMOV)
									oSection5:Cell("RHC_NDTRAB"):SetValue(TMB->TM_NDTRAB)
									If TMB->TM_NDTRAB != ARHC[nIRhc, 24]
										oSection5:Cell("ERRDIAS"):SetValue("*")
									EndIf
									oSection5:Cell("RHC_SALDII"):SetValue(TMB->TM_SALDII)
									If TMB->TM_SALDII != ARHC[nIRhc, 25]
										oSection5:Cell("ERRSALDI"):SetValue("*")
									EndIf
									oSection5:Cell("RETIRO"):SetValue(TMB->TM_RTPAT)
									If TMB->TM_RTPAT != ARHC[nIRhc, 13]
										oSection5:Cell("ERRRETIRO"):SetValue("*")
									EndIf
									oSection5:Cell("CYVPAT"):SetValue(TMB->TM_CYVPAT)
									If TMB->TM_CYVPAT != ARHC[nIRhc, 14]
										oSection5:Cell("ERRPAT"):SetValue("*")
									EndIf
									oSection5:Cell("CYVOBR"):SetValue(TMB->TM_CYVTRA)
									If TMB->TM_CYVTRA != ARHC[nIRhc, 15]
										oSection5:Cell("ERROBR"):SetValue("*")
									EndIf
									oSection5:Cell("APORTPAR"):SetValue(TMB->TM_INFONA)
									If TMB->TM_INFONA != ARHC[nIRhc, 16]
										oSection5:Cell("ERRINFONA"):SetValue("*")
									EndIf
									oSection5:Cell("AMORT"):SetValue(TMB->TM_AMORCF)
									If TMB->TM_AMORCF != ARHC[nIRhc, 17]
										oSection5:Cell("ERRAMORT"):SetValue("*")
									EndIf
									oSection5:Cell("SUMA"):SetValue(TMB->TM_RTPAT+TMB->TM_CYVPAT+TMB->TM_CYVTRA)
									oSection5:Cell("SUMAI"):SetValue(TMB->TM_INFONA+TMB->TM_AMORCF)
									oSection5:Cell("TOTAL"):SetValue(TMB->TM_RTPAT+TMB->TM_CYVPAT+TMB->TM_CYVTRA+TMB->TM_INFONA+TMB->TM_AMORCF)
									If TMB->TM_TPMOV == "2"
										oSection5:Cell("RHC_NDTRAB"):Hide()
										oSection5:Cell("RHC_SALDII"):Hide()
										oSection5:Cell("RETIRO"):Hide()
										oSection5:Cell("CYVPAT"):Hide()
										oSection5:Cell("CYVOBR"):Hide()
										oSection5:Cell("SUMA"):Hide()
										oSection5:Cell("APORTPAR"):Hide()
										oSection5:Cell("AMORT"):Hide()
										oSection5:Cell("SUMAI"):Hide()
										oSection5:Cell("TOTAL"):Hide()
									EndIf
									nTdtra += TMB->TM_NDTRAB
									nTsald += TMB->TM_SALDII
									nTrtpa += TMB->TM_RTPAT
									nTcyvp += TMB->TM_CYVPAT
									nTcyvt += TMB->TM_CYVTRA
									nTinfo += TMB->TM_INFONA
									nTamor += TMB->TM_AMORCF
									oSection5:PrintLine()
									oSection5:Cell("ERRDIAS"):SetValue("")
									oSection5:Cell("ERRSALDI"):SetValue("")
									oSection5:Cell("ERRRETIRO"):SetValue("")
									oSection5:Cell("ERRPAT"):SetValue("")
									oSection5:Cell("ERROBR"):SetValue("")
									oSection5:Cell("ERRINFONA"):SetValue("")
									oSection5:Cell("ERRAMORT"):SetValue("")
								EndIf//dbSeek
							Else//Procesa Registros Protheus
								If nIRhc < len (aRHC)
									If cMesIni== SUBSTR(DTOS(ARHC[nIRhc,23]),5,2) .AND. (ARHC[nIRhc+1,22]=="00" .and. cMesFin== SUBSTR(DTOS(ARHC[nIRhc+1,23]),5,2))
										nIRhc++
										ARHC[nIRhc,24]+=ARHC[nIRhc-1,24]// RHC_NDTRAB
										ARHC[nIRhc,26]+=ARHC[nIRhc-1,26]// RHC_NDINC
										ARHC[nIRhc,27]+=ARHC[nIRhc-1,27]// RHC_NDFAL
										ARHC[nIRhc,13]+=ARHC[nIRhc-1,13]// RHC_RTPAT
										ARHC[nIRhc,14]+=ARHC[nIRhc-1,14]// RHC_CYVPAT
										ARHC[nIRhc,15]+=ARHC[nIRhc-1,15]// RHC_CYVTRA
										ARHC[nIRhc,16]:=ROUND(	ARHC[nIRhc,16]+ARHC[nIRhc-1,16],2)// RHC_INFONA
										ARHC[nIRhc,17]:=ROUND(ARHC[nIRhc,17]+ARHC[nIRhc-1,17],2)// RHC_AMORCF
									EndIf
								EndIf
								oSection5:Cell("ORIGEN"):SetValue(STR0031)
								oSection5:Cell("RHC_DTMOV"):SetValue(ARHC[nIRhc,23])
								oSection5:Cell("RHC_NDTRAB"):SetValue(ARHC[nIRhc,24])
								oSection5:Cell("RHC_SALDII"):SetValue(ARHC[nIRhc,25])
								oSection5:Cell("RETIRO"):SetValue(ARHC[nIRhc,13])
								oSection5:Cell("CYVPAT"):SetValue(ARHC[nIRhc,14])
								oSection5:Cell("CYVOBR"):SetValue(ARHC[nIRhc,15])
								oSection5:Cell("APORTPAR"):SetValue(ARHC[nIRhc,16])//aPORT pAT.
								oSection5:Cell("AMORT"):SetValue(ARHC[nIRhc,17])//RHC_AMORCFs
								oSection5:Cell("SUMA"):SetValue(ARHC[nIRhc,13]+ARHC[nIRhc,14]+ARHC[nIRhc,15])//RHC_RTPAT+RHC_CYVPAT+RHC_CYVTRA
								oSection5:Cell("SUMAI"):SetValue(ARHC[nIRhc,16]+ARHC[nIRhc,17])
								oSection5:Cell("TOTAL"):SetValue(ARHC[nIRhc,13]+ARHC[nIRhc,14]+ARHC[nIRhc,15]+ARHC[nIRhc,16]+ARHC[nIRhc,17])
								oSection5:Cell("RHC_TPMOV"):SetValue(cTipomov)
								If cTipomov == '2'
									oSection5:Cell("RHC_NDTRAB"):Hide()
									oSection5:Cell("RHC_SALDII"):Hide()
									oSection5:Cell("RETIRO"):Hide()
									oSection5:Cell("CYVPAT"):Hide()
									oSection5:Cell("CYVOBR"):Hide()
									oSection5:Cell("SUMA"):Hide()
									oSection5:Cell("APORTPAR"):Hide()
									oSection5:Cell("AMORT"):Hide()
									oSection5:Cell("SUMAI"):Hide()
									oSection5:Cell("TOTAL"):Hide()
								EndIf
								nSaldi+=ARHC[nIRhc,25]
								nAmort+=ARHC[nIRhc,17]
								nDiasT+=ARHC[nIRhc,24]
								nRetiro+=ARHC[nIRhc,13]
								nCyvtra+=ARHC[nIRhc,14]
								nCyvpat+=ARHC[nIRhc,15]
								nAmorti+=nAmort
								nSuma+=ARHC[nIRhc,13]+ARHC[nIRhc,14]+ARHC[nIRhc,15]
								nSumaI+=ARHC[nIRhc,16]+ARHC[nIRhc,17]
								nApoPat+=ARHC[nIRhc,16]
								oSection5:PrintLine()
								nIRhc++
								oReport:IncMeter()//regla de avance
							EndIf
							lSwitch := !lSwitch
						EndDo //Matricula
					EndDo //Registro Pat.
					oSection6:Init()
					oSection6:Cell("ORIGEN"):SetValue(STR0032)
					oSection6:Cell("RHC_NDTRAB"):SetValue(nTdtra)
					oSection6:Cell("RHC_SALDII"):SetValue(nTsald)
					oSection6:Cell("RETIRO"):SetValue(nTrtpa)
					oSection6:Cell("CYVPAT"):SetValue(nTcyvp)
					oSection6:Cell("CYVOBR"):SetValue(nTcyvt)
					oSection6:Cell("SUMA"):SetValue(nTrtpa+nTcyvp+nTcyvt)
					oSection6:Cell("APORTPAR"):SetValue(nTinfo)
					oSection6:Cell("AMORT"):SetValue(nTamor)
					oSection6:Cell("SUMAI"):SetValue(nTinfo+nTamor)
					oSection6:Cell("TOTAL"):SetValue(nTrtpa+nTcyvp+nTcyvt+nTinfo+nTamor)
					oSection6:Printline()
					oReport:IncMeter()
					oSection6:Cell("ORIGEN"):SetValue(STR0031)
					oSection6:Cell("RHC_NDTRAB"):SetValue(nDiasT)
					oSection6:Cell("RHC_SALDII"):SetValue(nSaldi)
					oSection6:Cell("RETIRO"):SetValue(nRetiro)
					oSection6:Cell("CYVPAT"):SetValue(nCyvpat)
					oSection6:Cell("CYVOBR"):SetValue(nCYvtra)
					oSection6:Cell("SUMA"):SetValue(nSuma)
					oSection6:Cell("APORTPAR"):SetValue(nApoPat)
					oSection6:Cell("AMORT"):SetValue(nAmorti)
					oSection6:Cell("SUMAI"):SetValue(nSumai)
					oSection6:Cell("TOTAL"):SetValue(nSuma+nSumai)
					oSection6:Printline()
					oReport:IncMeter()
					oSection6:Finish()
					dbSelectArea("TMB")
					TMB->(dbSetOrder(1))
					TMB->(dbGoTop())
					dbSelectArea("TPB")
					TPB->(dbSetOrder(1))
					While !TMB->(EOF())
						If TMB->TM_LEIDO != "*"
							If TPB->(dbSeek(TMB->TM_MAT))
								lNoLeido := .T.
							EndIf
						EndIf
						TMB->(dbSkip())
					EndDo
					oSection7:Init()
					oSection7:Cell("REGISTROS"):SetValue("")
					oSection7:PrintLine()
					If lNoLeido
						oSection7:Cell("REGISTROS"):SetValue(STR0033)
						oSection7:PrintLine()
					EndIf
					oSection7:Finish()
					If lNoLeido
						oSection8:Init()
						oSection9:Init()
						dbSelectArea("TMB")
						TMB->(dbSetOrder(1))
						TMB->(dbGoTop())
						dbSelectArea("TPB")
						TPB->(dbSetOrder(1))
						While !TMB->(EOF())
							If TMB->TM_LEIDO != "*"
								If TPB->(dbSeek(TMB->TM_MAT))
									oSection8:Cell("ORIGEN"):SetValue(STR0032)
									oSection8:Cell("RA_RG"):SetValue(TPB->TP_RG)
									oSection8:Cell("NOMECOM"):SetValue(TPB->TP_NOME)
									oSection8:Cell("EMPLEADO"):SetValue(TPB->TP_MAT)
									oSection8:Cell("RA_CURP"):SetValue(TPB->TP_CURP)
									oSection8:PrintLine()
									oReport:IncMeter()
								EndIf
								oSection9:Cell("ORIGEN"):SetValue(STR0032)
								oSection9:Cell("RHC_DTMOV"):SetValue(TMB->TM_DTMOV)
								oSection9:Cell("RHC_NDTRAB"):SetValue(TMB->TM_NDTRAB)
								oSection9:Cell("RHC_SALDII"):SetValue(TMB->TM_SALDII)
								oSection9:Cell("RETIRO"):SetValue(TMB->TM_RTPAT)
								oSection9:Cell("CYVPAT"):SetValue(TMB->TM_CYVPAT)
								oSection9:Cell("CYVOBR"):SetValue(TMB->TM_CYVTRA)
								oSection9:Cell("APORTPAR"):SetValue(TMB->TM_INFONA)//aPORT pAT.
								oSection9:Cell("AMORT"):SetValue(TMB->TM_AMORCF)//RHC_AMORCFs
								oSection9:Cell("SUMA"):SetValue(TMB->TM_RTPAT+TMB->TM_CYVPAT+TMB->TM_CYVTRA)
								oSection9:Cell("SUMAI"):SetValue(TMB->TM_INFONA+TMB->TM_AMORCF)
								oSection9:Cell("TOTAL"):SetValue(TMB->TM_RTPAT+TMB->TM_CYVPAT+TMB->TM_CYVTRA+TMB->TM_INFONA+TMB->TM_AMORCF)
								oSection9:Cell("RHC_TPMOV"):SetValue(TMB->TM_TPMOV)
								oSection9:Printline()
								oReport:IncMeter()
							EndIf
							TMB->(dbSkip())
						EndDo
						oSection8:Finish()
						oSection9:Finish()
					EndIf
					oReport:EndPage() //-- Salta Pagina
				EndDo
				oSection5:Finish()
				oSection1:Finish()
			EndIf
		End Sequence
		(cAliasRHC)->(dbCloseArea())

	Else
		oReport:CancelPrint()
		lPrinted := .F.
		return
	EndIf
Return ( lPrinted )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao     ³ leeRHB     | Autor ³ Jesus Peñaloza     ³ Data ³ 10.04.14 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descripción³ Funcion que regresa el historico de modificaciones        ³±±
±±³           ³ Infonavit en el periodo indicado en los parametros        ³±± 
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros ³ cExp1.- Matricula del empleado                            ³±±
±±³           ³ cExp2.- Filial de tabla RHB                               ³±±
±±³           ³ dExp3.- Fecha, a partir de la cual se establecera el      ³±±
±±³           ³         periodo de las modificaciones infonavit           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxis   ³ leeRHB(cExp1,cExp2,dExp3)                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function leeRHB(cMat,cFilRHB,dFecha )

	Local cAliasRHB:=CriaTrab(Nil,.F.)
	Local cQueryRHB :=""
	Local aRHB:={}

	cQueryRHB:= "select RHB_FILIAL,RHB_MAT,MAX(RHB_DTMINF) RHB_DTMINF ,RHB_TPMINF "
	cQueryRHB+= " from "+RetSqlName("RHB") 
	cQueryRHB+=" WHERE RHB_DTMINF<='"+dtos(dFecha)+"'"
	cQueryRHB+=" AND RHB_FILIAL='"+cFilRHB+"'"
	cQueryRHB+=" AND RHB_MAT='"+cMat+"'"
	cQueryRHB+= " AND D_E_L_E_T_  = ' ' "
	cQueryRHB+= " group by  RHB_FILIAL,RHB_MAT,RHB_DTMINF,RHB_TPMINF "
	cQueryRHB := ChangeQuery(cQueryRHB)
	DbUseArea( .T., 'TOPCONN', TCGENQRY(,, cQueryRHB), cAliasRHB, .T., .T. )
	TCSetField(cAliasRHB,"RHB_DTMINF","D")  

	WHILE  !(cAliasRHB)->(EOF())
		AADD(aRHB,{(cAliasRHB)->RHB_FILIAL,(cAliasRHB)->RHB_MAT,(cAliasRHB)->RHB_DTMINF,(cAliasRHB)->RHB_TPMINF})
		(cAliasRHB)->(dbskip()) 
	enddo
	(cAliasRHB)->(dbCloseArea())  
Return (aRHB)
