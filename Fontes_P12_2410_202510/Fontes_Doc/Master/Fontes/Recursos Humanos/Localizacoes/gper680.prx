#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPER680.CH"
#INCLUDE "REPORT.CH"

/*/
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥ GPER680  ≥ Autor ≥ Gisele Varandas               ≥ Data ≥ 17/12/2007 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Relatorio de Saldos e Salarios                                       ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe   ≥ GPER680()                                                            ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ Especifico                                                           ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥         ATUALIZACOES SOFRIDAS DESDE A CONSTRUÄAO INICIAL.                       ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Programador ≥ Data     ≥ Nr. FNC      ≥  Motivo da Alteracao                     ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Kelly       ≥28/06/2008≥148920        ≥Ajuste no grupo de perguntas.             ≥±±
±±≥Leandro Dr. ≥07/08/2008≥149954        ≥Ajuste na desc. do metodo TMSPrinter      ≥±±
±±≥Luciana     ≥24/08/2009≥020801/2009   ≥Ajuste na ordem  de impressao do relatorio≥±±
±±≥            ≥          ≥              ≥que estava invertida.                     ≥±±
±±≥Luciana     ≥03/09/2009≥022057/2009   ≥Ajuste para exibir corretamente o total de≥±±
±±≥            ≥          ≥              ≥paginas geradas no relatorio.             ≥±±
±±≥Luciana     ≥18/01/2010≥030345/2010   ≥Inclusao do campo RG no lugar do CPF se o ≥±±
±±≥            ≥          ≥              ≥Pais for Bolivia.                         ≥±±
±±≥Luciana     ≥26/01/2010≥001620/2010   ≥Inclusao dos parenteses na variavel       ≥±±
±±≥            ≥          ≥              ≥cPeriodo da query utilizada para contagem ≥±±
±±≥            ≥          ≥              ≥dos funcionarios a serem exibidos no      ≥±±
±±≥            ≥          ≥              ≥relatorio.                                ≥±±
±±≥L.Trombini  ≥05/08/2011≥000016819/2011≥Ajuste no lay out do relatorio devido mu- ≥±±
±±≥            ≥          ≥              ≥danÁa na LegislaÁ„o do Pais - Bolivia     ≥±±
±±≥Leandro Dr. ≥28/01/2012≥000002306/2012≥Ajuste para imprimir corretamente os itens≥±±
±±≥            ≥          ≥        TEKVGJ≥K e J.                                    ≥±±
±±≥Claudinei S.≥08/02/2012≥000003178/2012≥Ajuste funcoes Fimprime e Frodape para nao≥±±
±±≥            ≥          ≥        TEMN66≥estourar quando o valor for de milhoes.   ≥±±
±±≥Claudinei S.≥03/12/2012≥000022502/2012≥Ajustes nos campos,Dias uteis pagos no mes≥±±
±±≥            ≥          ≥        TFRXQI≥e Horas/Dias pagadas, letras das colunas  ≥±±
±±≥            ≥          ≥              ≥Aporte Sindical e Total Dctos.            ≥±±
±±≥M. Silveira ≥07/12/2012≥000028612/2012≥Disponibilizado em formato personalizavel ≥±±
±±≥            ≥          ≥        TGBVQW≥(TReport) p/ permitir exportacao p/ Excel.≥±±
±±|M. Silveira |05/02/2013|        TGJWB9|Ajustes para retirar os controles SetPrint|±±
±±|            |          |              |nao imprimir folha vazia indevidamente.   |±±
±±|Claudinei S.|22/03/2013|000006815/2013|Ajuste para que seja impresso o relatorio |±±
±±|            |          |        TGXLHV|qdo num. pagto estiver informado na RHC   |±±
±±|M. Silveira |26/11/2013|        THOVNO|Diversos ajustes realizados p/ atender aos|±±
±±|            |          |              |requisitos levantados pelo Hub-Mex:       |±±
±±|            |          |              |*permitir a selecao de multiplos processos|±±
±±|            |          |              | e roteiros.                              |±±
±±|            |          |              |*alinhada a posicao de titulos das colunas|±±
±±|            |          |              | e a linha dos dados no formato TReport.  |±±
±±|            |          |              |*retirados campos Fundo Solid. e Sindicais|±±
±±≥            ≥          ≥              ≥                                          ≥±±
±±≥Jonathan Glz≥06/05/2015≥    PCREQ-4256≥Se elimina la funcion AjustaSX1, la cual  ≥±±
±±≥            ≥          ≥              ≥realiza la modificacion aldiccionario de  ≥±±
±±≥            ≥          ≥              ≥datos(SX1) por motivo de adecuacion  nueva≥±±
±±≥            ≥          ≥              ≥estructura de SXs para version 12         ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
/*/
Function GPER680()

Private aInfo   	:= {}
Private aRoteiros	:= {}
Private cPerg   	:= "GPR680"

	If FindFunction("TRepInUse") .And. TRepInUse()
 		//Relatorio em formato TReport
 		GPER680R4()
 	Else
 		//Relatorio em formato TMSPrinter
 		GPER680R3()
	EndIF

Return

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥Funáao    ≥ GPER682R4  ≥ Autor ≥ Marcelo Silveira      ≥ Data ≥07/12/2012≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descriáao ≥ Relatorio de Saldos e Salarios                               ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ GPER680                                                      ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Function GPER680R4()

	Private oReport

	Private nOrdem		:= 1
	Private nPerEvent	:= 0
	Private nTotFunc	:= 0
	Private nMaxFunc	:= 32

	Private cPerg		:= "GPR680"
	Private cCodFilial	:= "##"
	Private cAliasQry	:= ""
	Private cProcesso	:= ""
	Private cPeriodo	:= ""
	Private cRoteiro	:= ""
	Private cCompet		:= ""

	Private oCNew08		:= TFont():New("Courier New",Nil,08,,.F.,,,,.F.,.F.)
	Private oCNew08N 	:= TFont():New("Courier New",Nil,08,,.T.,,,,.F.,.F.)
	Private oCNew10		:= TFont():New("Courier New",Nil,10,,.F.,,,,.F.,.F.)
	Private oCNew10N 	:= TFont():New("Courier New",Nil,10,,.T.,,,,.F.,.F.)

	Private aInfo		:= Array(26)
	Private aOrd		:= {	OemToAnsi("Matricula"),;   		//1 - "Matricula"
						 		OemToAnsi("Centro de Custos");	//2 - "Centro de Custos"
							}

	//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
	//≥ Executa relatorio T-REPORT                                   ≥
	//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
	Pergunte(cPerg,.F.)

	oReport := ReportDef()
	oReport:SetLandscape()
	oReport:PrintDialog()

Return


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥Funáao    ≥ ReportDef  ≥ Autor ≥ Marcelo Silveira      ≥ Data ≥05/11/2012≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descriáao ≥ Relatorio de Saldos e Salarios                               ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ GPER680                                                      ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function ReportDef()

	Local oReport
	Local oSectCab	//Section Cabecalho
	Local oSectFun	//Section Funcionarios
	Local oSectLan 	//Section Lancamentos

	Local cTitulo	:=	STR0003	//"PLANILLA DE SUELDOS Y SALARIOS"
	Local cnomeprog	:= "GPER680"
	Local cDesc1	:= "PLANILLA DE SUELDOS Y SALARIOS"
	Local cDesc2	:= STR0004	//"Ser† impresso de acordo com os parametros solicitados pelo"
	Local cDesc3	:= STR0005	//"usu†rio."
	Local bBlkEmp	:= {|| If( !Empty((cAliasFun)->RA_FILIAL) .And. cCodFilial != (cAliasFun)->RA_FILIAL, Eval( {|| fInfo(@aInfo,(cAliasFun)->RA_FILIAL), OemToAnsi(STR0008) +": "+ aInfo[3]} ), OemToAnsi(STR0008) +": "+aInfo[3] )}

	DEFINE REPORT oReport NAME "GPER680" TITLE cTitulo PARAMETER cPerg ACTION {|oReport| R680ImpR4(oReport) } DESCRIPTION cDesc1

	oReport:nFontBody := 6
	oReport:SetDynamic()
	oReport:SetTotalInLine(.F.)
	oReport:PageTotalBefore(.T.)
	oReport:SetEdit(.F.)			//Nao permite ao usuario personalizar o relatorio
	oReport:lHeaderVisible := .F.	//Nao imprime o cabecalho padrao


    //SECTION 1 - CABECALHO
	DEFINE SECTION oSectCab OF oReport TITLE OemToAnsi("Cabecalho") TABLE "SM0","SRA" ORDERS aOrd PAGE HEADER  			//Section Fixa
		DEFINE CELL NAME "EMPRESA"    	OF oSectCab TITLE ""	SIZE 60 BLOCK {|| Eval( bBlkEmp ) } 	//Nome Empresa:
		DEFINE CELL NAME "PAGINA"		OF oSectCab TITLE ""	SIZE 30	 								//Numero de paginas
		DEFINE CELL NAME "NUM_EMP"		OF oSectCab TITLE "" 	SIZE 60									//Numero do empregador

		oSectCab:Cell("EMPRESA"):nAlign := 1
		oSectCab:Cell("PAGINA"):nAlign := 3
		oSectCab:Cell("NUM_EMP"):nAlign := 1

		oSectCab:SetLineStyle()
		oSectCab:SetCols(2)


    //SECTION 2 - FUNCIONARIO
	DEFINE SECTION oSectFun OF oReport TITLE OemToAnsi("Funcionarios") TABLE "SM0","SRA" ORDERS aOrd

		DEFINE CELL NAME "NUM"			OF oSectFun TITLE "" 	SIZE 06
		DEFINE CELL NAME "CI"			OF oSectFun TITLE "" 	SIZE 10
		DEFINE CELL NAME "APELLIDOS"	OF oSectFun TITLE ""	SIZE 40
		DEFINE CELL NAME "NACIONAL"		OF oSectFun TITLE ""	SIZE 40
		DEFINE CELL NAME "NASCIMENTO"	OF oSectFun TITLE ""	SIZE 25
		DEFINE CELL NAME "SEXO"			OF oSectFun TITLE ""	SIZE 20
		DEFINE CELL NAME "OCUPACION"	OF oSectFun TITLE ""  	SIZE 30
		DEFINE CELL NAME "INGRESSO"		OF oSectFun TITLE ""	SIZE 30

		oSectFun:Cell("NUM"			):nAlign := 3
		oSectFun:Cell("CI"			):nAlign := 3
		oSectFun:Cell("APELLIDOS"	):nAlign := 3
		oSectFun:Cell("NACIONAL"	):nAlign := 3
		oSectFun:Cell("NASCIMENTO"	):nAlign := 3
		oSectFun:Cell("SEXO"		):nAlign := 2
		oSectFun:Cell("OCUPACION"	):nAlign := 2
		oSectFun:Cell("INGRESSO"	):nAlign := 3


    //SECTION 3 - LANCAMENTOS
	DEFINE SECTION oSectLan OF oReport TITLE OemToAnsi("Lancamentos") TABLE "SM0","SRA" ORDERS aOrd

		DEFINE CELL NAME "DIASHPM"		OF oSectLan TITLE "" 	PICTURE '@E 999'				SIZE 08	//DIAS HAB. PAGADOS MES
		DEFINE CELL NAME "HORASDP"		OF oSectLan TITLE "" 	PICTURE '@E 999.99'			SIZE 10	//HORAS PAGADOS MES
		DEFINE CELL NAME "HABERB"		OF oSectLan TITLE "" 	PICTURE '@E 99,999,999.99'	SIZE 13 //HABER BASICO (A)
		DEFINE CELL NAME "BONOANT"		OF oSectLan TITLE "" 	PICTURE '@E 999,999.99'		SIZE 13 //BONO DE ANTIGUEDAD (B)
		DEFINE CELL NAME "HENUMERO"		OF oSectLan TITLE "" 	PICTURE '@E 999'				SIZE 07	//HORA EXTRA - NUMERO
		DEFINE CELL NAME "HEMONTO"		OF oSectLan TITLE "" 	PICTURE '@E 999,999.99'		SIZE 13 //HORA EXTRA - MONTO PAGADO (C)
		DEFINE CELL NAME "REMBONO"		OF oSectLan TITLE "" 	PICTURE '@E 999,999.99'		SIZE 13 //REMUNERACAO - BONO DE PRODUCION (D)
		DEFINE CELL NAME "REMOTROS"		OF oSectLan TITLE "" 	PICTURE '@E 999,999.99'		SIZE 13 //REMUNERACAO - OUTROS BONOS (F)
		DEFINE CELL NAME "SDSRNUM"		OF oSectLan TITLE "" 	PICTURE '@E 999'				SIZE 09	//SUELDO DSR - NUMERO DE DOMINGOS
		DEFINE CELL NAME "SDSRSUE"		OF oSectLan TITLE "" 	PICTURE '@E 999,999.99'		SIZE 13 //SUELDO DSR - SUELDO DOMINICAL (E)
		DEFINE CELL NAME "TOTGANADO"	OF oSectLan TITLE "" 	PICTURE '@E 99,999,999.99'	SIZE 15 //TOTAL GANADO (G)
		DEFINE CELL NAME "DAFP"			OF oSectLan TITLE "" 	PICTURE '@E 999,999.99'		SIZE 13 //DESCONTO - AFP 12 (H)
		DEFINE CELL NAME "DRCIVA"		OF oSectLan TITLE "" 	PICTURE '@E 999,999.99'		SIZE 13 //DESCONTO - RC-IVA(I)
		DEFINE CELL NAME "DODESC"		OF oSectLan TITLE "" 	PICTURE '@E 999,999.99'		SIZE 13 //DESCONTO - OUTROS DESCONTOS (J)
		DEFINE CELL NAME "TOTDCTOS"		OF oSectLan TITLE "" 	PICTURE '@E 99,999,999.99'	SIZE 15 //TOTAL DESCONTOS (L)
		DEFINE CELL NAME "LIQUIDOP"		OF oSectLan TITLE "" 	PICTURE '@E 99,999,999.99'	SIZE 14 //LIQUIDO PAGABLE (M)
		DEFINE CELL NAME "FIRMA"			OF oSectLan TITLE "" 									SIZE 14 //FIRMA

		oSectLan:Cell("DIASHPM"	):nAlign := 3
		oSectLan:Cell("HABERB"	):nAlign := 3
		oSectLan:Cell("BONOANT"	):nAlign := 3
		oSectLan:Cell("HENUMERO"):nAlign := 3
		oSectLan:Cell("HEMONTO"	):nAlign := 3
		oSectLan:Cell("REMBONO"	):nAlign := 3
		oSectLan:Cell("REMOTROS"):nAlign := 3
		oSectLan:Cell("SDSRNUM"):nAlign := 3
		oSectLan:Cell("SDSRSUE"):nAlign := 3
		oSectLan:Cell("TOTGANADO"):nAlign := 3
		oSectLan:Cell("DAFP"):nAlign := 3
		oSectLan:Cell("DRCIVA"):nAlign := 3
		oSectLan:Cell("DODESC"):nAlign := 3
		oSectLan:Cell("TOTDCTOS"):nAlign := 3
		oSectLan:Cell("LIQUIDOP"):nAlign := 3
		oSectLan:Cell("FIRMA"):nAlign := 3

	//TOTALIZADORES
	DEFINE FUNCTION NAME "THABERB"  	FROM oSectLan:Cell("HABERB") 	FUNCTION SUM TITLE "" PICTURE "@E 999,999,999.99"	NO END SECTION	//HABER BASICO (A)
	DEFINE FUNCTION NAME "TBONOANT" 	FROM oSectLan:Cell("BONOANT") 	FUNCTION SUM TITLE "" PICTURE "@E 999,999,999.99"	NO END SECTION	//BONO DE ANTIGUEDAD (B)
	DEFINE FUNCTION NAME "THENUMERO" 	FROM oSectLan:Cell("HENUMERO") 	FUNCTION SUM TITLE "" PICTURE "@E 9999"				NO END SECTION	//HORA EXTRA - NUMERO
	DEFINE FUNCTION NAME "THEMONTO" 	FROM oSectLan:Cell("HEMONTO") 	FUNCTION SUM TITLE "" PICTURE "@E 999,999,999.99"	NO END SECTION	//HORA EXTRA - MONTO PAGADO (C)
	DEFINE FUNCTION NAME "TREMBONO" 	FROM oSectLan:Cell("REMBONO") 	FUNCTION SUM TITLE "" PICTURE "@E 999,999,999.99"	NO END SECTION	//REMUNERACAO - BONO DE PRODUCION (D)
	DEFINE FUNCTION NAME "TREMOTROS" 	FROM oSectLan:Cell("REMOTROS") 	FUNCTION SUM TITLE "" PICTURE "@E 999,999,999.99"	NO END SECTION	//REMUNERACAO - OUTROS BONOS (F)
	DEFINE FUNCTION NAME "TSDSRNUM" 	FROM oSectLan:Cell("SDSRNUM") 	FUNCTION SUM TITLE "" PICTURE "@E 9999"				NO END SECTION	//SUELDO DSR - NUMERO DE DOMINGOS
	DEFINE FUNCTION NAME "TSDSRSUE" 	FROM oSectLan:Cell("SDSRSUE") 	FUNCTION SUM TITLE "" PICTURE "@E 999,999,999.99"	NO END SECTION	//SUELDO DSR - SUELDO DOMINICAL (E)
	DEFINE FUNCTION NAME "TTOTGANADO" 	FROM oSectLan:Cell("TOTGANADO")	FUNCTION SUM TITLE "" PICTURE "@E 999,999,999.99"	NO END SECTION	//TOTAL GANADO (G)
	DEFINE FUNCTION NAME "TDAFP" 		FROM oSectLan:Cell("DAFP") 		FUNCTION SUM TITLE "" PICTURE "@E 999,999,999.99"	NO END SECTION 	//DESCONTO - AFP 12 (H)
	DEFINE FUNCTION NAME "TDRCIVA" 		FROM oSectLan:Cell("DRCIVA") 	FUNCTION SUM TITLE "" PICTURE "@E 999,999,999.99"	NO END SECTION	//DESCONTO - RC-IVA(I)
	DEFINE FUNCTION NAME "TDODESC"		FROM oSectLan:Cell("DODESC") 	FUNCTION SUM TITLE "" PICTURE "@E 999,999,999.99"	NO END SECTION	//DESCONTO - OUTROS DESCONTOS (J)
	DEFINE FUNCTION NAME "TOTDCTOS" 	FROM oSectLan:Cell("TOTDCTOS") 	FUNCTION SUM TITLE "" PICTURE "@E 999,999,999.99"	NO END SECTION	//TOTAL DESCONTOS (L)
	DEFINE FUNCTION NAME "TLIQUIDOP" 	FROM oSectLan:Cell("LIQUIDOP") 	FUNCTION SUM TITLE "" PICTURE "@E 999,999,999.99"	NO END SECTION	//LIQUIDO PAGABLE (M)

Return(oReport)


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥Funáao    ≥ R680ImpR4  ≥ Autor ≥ Marcelo Silveira      ≥ Data ≥05/11/2012≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descriáao ≥ Relatorio de Saldos e Salarios                               ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ GPER680                                                      ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function R680ImpR4(oReport)

Local nX		:= 0
Local nReg		:= 0
Local nTamProc	:= 0
Local nVHDiaPg 	:= 0
Local nDiaPgMes	:= 0
Local cMatAnt	:= ""
Local cOrdem 	:= ""
Local cSitQuery	:= ""
Local cCatQuery	:= ""
Local cAuxPrc	:= ""
Local cProcs	:= ""
Local cFilPd	:= ""
Local cFilPer	:= ""
Local cFiltro	:= ""
Local cPersona	:= ""
Local cEmpMTrab	:= ""
Local cCondicao := ""
Local cDescMes	:= ""

Local cTexto0 	:= "|   N∫  |   CI    |         APPELIDOS Y NOMBRE         |        NACIONALIDAD       |  FECHA DE NACIMIENTO | SEXO |      OCCUPACION QUE DESEMPE—A         |    FECHA DE INGRESSO       |" //5
Local cTexto1 	:= "| DIAS  | HORAS/|   HABER   | BONO DE  |   HORAS EXTRAS  |    REMUNERACION     |    SUELDO DSR     |   TOTAL    |        DESCUENTOS              |   TOTAL    |  LIQUIDO   |  FIRMA   |" //4
Local cTexto2 	:= "|  HAB. |  DIAS |  BASICO   |ANTIGUEDAD|NUMERO|  MONTO   | BONO DE  | OTROS    | NUMERO | SUELDO   | GANADO (G) |   AFP    | RC-IVA   |  Otros   |   DCTOS    |  Pagable   |          |" //3
Local cTexto3 	:= "|PAGADOS|PAGADAS|    (A)    |    (B)   |      |  PAGADO  |PRODUCION | BONOS    |   DE   |DOMINICAL |  A+B+C+D+  |  12.71%  |   13%    |  Dctos.  |    (K)     |    (L)     |          |" //2
Local cTexto4 	:= "|  MES  |       |           |          |      |   (C)    |   (D)    |  (F)     |DOMINGOS|   (E)    |     E+F    |   (H)    |   (I)    |   (J)    |   H+I+J    |    G-K     |          |"
Local cFirma	:= "   ..........."

Local i			:= 0
Local iX		:= 0
Local nQtdFun	:= 0
Local nTotPage	:= 0
Local nPageAtu	:= 1
Local aVerba 	:= Array(14,2)

Private	nPerEvent
Private oSectCab	:= oReport:Section(1)
Private oSectFun	:= oReport:Section(2)
Private oSectLan	:= oReport:Section(3)

	/*/
	⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
	≥ mv_par01        //  Processo						           ≥
	≥ mv_par02        //  Roteiro							       ≥
	≥ mv_par03        //  Competencia                              ≥
	≥ mv_par04        //  Filial  De                               ≥
	≥ mv_par05        //  Filial  Ate                              ≥
	≥ mv_par06        //  Centro de Custo De                       ≥
	≥ mv_par07        //  Centro de Custo Ate                      ≥
	≥ mv_par08        //  Matricula De                             ≥
	≥ mv_par09        //  Matricula Ate                            ≥
	≥ mv_par10        //  Nome De                                  ≥
	≥ mv_par11        //  Nome Ate                                 ≥
	≥ mv_par12        //  Situacao                                 ≥
	≥ mv_par13        //  Categoria                                ≥
	≥ mv_par14        //  Personal - Eventual/Permanente           ≥
	≥ mv_par15        //  Responsavel da empresa pela Assinatura   ≥
	≥ mv_par16        //  Numero de documento do responsavel       ≥
	¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ/*/
	cProcesso 	:= mv_par01
	cCompet		:= mv_par03
	cFilDe   	:= mv_par04
	cFilAte  	:= mv_par05
	cCcDe    	:= mv_par06
	cCcAte   	:= mv_par07
	cMatDe   	:= mv_par08
	cMatAte  	:= mv_par09
	cNomDe   	:= mv_par10
	cNomAte  	:= mv_par11
	cSituacao  	:= mv_par12
	cCategoria 	:= mv_par13
	nPerEvent	:= mv_par14
	cNomEmpr	:= mv_par15
	cNumDocId	:= mv_par16
	nOrdem		:= oReport:GetOrder()

	cPersona 	:= If( nPerEvent == 1, STR0010, STR0075) 	//1-PERMANENTE / 2-EVENTUAL
	cEmpMTrab	:= OemToAnsi(STR0009) +": "+ If( fTabela("S007",1,5 ) <> Nil, fTabela("S007",1,5 ), " " )
	cNomEmpr	:= If( (cNomEmpr := fTabela("S007",1,10 )) <> Nil, cNomEmpr, mv_par15 )
	cNumDocId	:= If( (cNumDocId := fTabela("S007",1,11 )) <> Nil, cNumDocId, mv_par16 )

	If !Empty(cCompet)
		cPeriodo := SubStr(cCompet,3,4) + SubStr(cCompet,1,2)
	EndIf

	cSitQuery	:= ""
	If !Empty(cSituacao)
		For nReg:=1 to Len(cSituacao)
			cSitQuery += "'"+Subs(cSituacao,nReg,1)+"'"
			If ( nReg+1 ) <= Len(cSituacao)
				cSitQuery += ","
			Endif
		Next nReg
		cSitQuery := "%" + cSitQuery + "%"
	EndIf

	cCatQuery	:= ""
	If !Empty(cSituacao)
		For nReg:=1 to Len(cCategoria)
			cCatQuery += "'"+Subs(cCategoria,nReg,1)+"'"
			If ( nReg+1 ) <= Len(cCategoria)
				cCatQuery += ","
			Endif
		Next nReg
		cCatQuery := "%" + cCatQuery + "%"
	EndIf

	//Trata os roteiros para a query
	SelecRoteiros()
	For nX := 1 to Len(aRoteiros)
		cRoteiro += "'" + aRoteiros[nX, 1] + "'"
		If( nX+1 ) <=  Len(aRoteiros)
			cRoteiro += ","
		EndIf
	Next
	cRoteiro := "%" + cRoteiro + "%"

	//Trata os processos para a query
	nTamProc := GetSx3Cache( "RCJ_CODIGO", "X3_TAMANHO" )
	For nX := 1 to Len(Alltrim(cProcesso)) Step 5
		If Len(Subs(cProcesso,nX,5)) < nTamProc
			cAuxPrc := Subs(cProcesso,nX,5) + Space(nTamProc - Len(Subs(cProcesso,nX,5)))
		Else
			cAuxPrc := Subs(cProcesso,nX,5)
		EndIf
		cProcs += "'" + cAuxPrc + "',"
	Next nX
	cProcs := "%" + Substr( cProcs, 1, Len(cProcs)-1) + "%"

	cAliasFun := GetNextAlias()
	cAliasQry := GetNextAlias()

	lExeQry	  := .T.

	If nOrdem == 1
		cOrdFun   := "% RA_FILIAL, RA_MAT %"
		cOrdLan	  := "% RA_FILIAL, RA_MAT, PD, SEQ %"
		cCondicao := "(cAliasQry)->FILIAL == (cAliasFun)->RA_FILIAL .And. (cAliasQry)->RA_MAT == (cAliasFun)->RA_MAT"
	Else
		cOrdFun   := "% RA_FILIAL, RA_CC, RA_MAT %"
		cOrdLan   := "% RA_FILIAL, RA_CC, RA_MAT, PD, SEQ %"
		cCondicao := "(cAliasQry)->FILIAL == (cAliasFun)->RA_FILIAL .And. (cAliasQry)->RA_CC == (cAliasFun)->RA_CC .And. (cAliasQry)->RA_MAT == (cAliasFun)->RA_MAT"
	EndIf

	cFilPd	:= If( Alltrim(FWModeAccess("SRV",3)) == "C", "%'" + xFilial("SRV") + "'%", "%SRA.RA_FILIAL%" )
	cFilPer	:= If( Alltrim(FWModeAccess("RCF",3)) == "C", "%'" + xFilial("RCF") + "'%", "%SRA.RA_FILIAL%" )

	BEGIN REPORT QUERY oSectFun

		BeginSql alias cAliasFun

			SELECT	SRA.RA_FILIAL,			SRA.RA_MAT,			SRA.RA_CC,			SRA.RA_NOME,
					SRA.RA_DEPTO,			SRA.RA_HRSMES,		SRA.RA_SALARIO,		SRA.RA_SITFOLH,
					SRA.RA_DTREC,			SRA.RA_CATFUNC,		SRA.RA_CIC,			SRA.RA_NACIONA,
					SRA.RA_NASC,			SRA.RA_SEXO,		SRA.RA_ADMISSA,		SRA.RA_CODFUNC,
					SRA.RA_TNOTRAB,			SRA.RA_PROCES,		SRA.RA_HRSMES,		SRA.RA_CARGO,
					SRC.RC_FILIAL FILIAL,	SRC.RC_CC CCUSTO,
					RCF.RCF_HRSDIA,			RCF.RCF_DIATRA

			FROM %table:SRA% SRA

			INNER JOIN  %table:SRC% SRC
			ON 	    SRA.RA_FILIAL = SRC.RC_FILIAL	AND
					SRA.RA_MAT    = SRC.RC_MAT
			INNER JOIN %table:RCF% RCF
			ON		RCF.RCF_FILIAL	=	%exp:cFilPer%	AND
					RCF.RCF_PROCES	=	SRA.RA_PROCES	AND
					RCF.RCF_PER 	=	%exp:Upper(cPeriodo)%

			WHERE	SRA.RA_FILIAL BETWEEN %Exp:cFilDe% AND %Exp:cFilAte% AND
					SRA.RA_MAT    BETWEEN %Exp:cMatDe% AND %Exp:cMatAte% AND
					SRA.RA_CC     BETWEEN %Exp:cCcDe%  AND %Exp:cCcAte%  AND
					SRA.RA_NOME   BETWEEN %Exp:cNomDe% AND %Exp:cNomAte% AND
					SRA.RA_SITFOLH IN (%exp:Upper(cSitQuery)%) AND
			   		SRA.RA_CATFUNC IN (%exp:Upper(cCatQuery)%) AND
			   		SRC.RC_PROCES  IN (%exp:Upper(cProcs)%)  	AND
			   		SRC.RC_PERIODO =   %exp:Upper(cPeriodo)%   AND
			   		SRC.RC_ROTEIR  IN (%exp:Upper(cRoteiro)%)  AND
					SRA.%notDel% AND SRC.%notDel% AND RCF.%notDel%
			UNION

			(SELECT	SRA.RA_FILIAL,			SRA.RA_MAT,			SRA.RA_CC,			SRA.RA_NOME,
					SRA.RA_DEPTO,			SRA.RA_HRSMES,		SRA.RA_SALARIO,		SRA.RA_SITFOLH,
					SRA.RA_DTREC,			SRA.RA_CATFUNC,		SRA.RA_CIC,			SRA.RA_NACIONA,
					SRA.RA_NASC,			SRA.RA_SEXO,		SRA.RA_ADMISSA,		SRA.RA_CODFUNC,
					SRA.RA_TNOTRAB,			SRA.RA_PROCES,		SRA.RA_HRSMES,		SRA.RA_CARGO,
					SRD.RD_FILIAL FILIAL,	SRD.RD_CC CCUSTO,
					RCF.RCF_HRSDIA,			RCF.RCF_DIATRA

			FROM %table:SRA% SRA

			INNER JOIN  %table:SRD% SRD
			ON 	    SRA.RA_FILIAL = SRD.RD_FILIAL	AND
					SRA.RA_MAT    = SRD.RD_MAT
			INNER JOIN %table:RCF% RCF
			ON		RCF.RCF_FILIAL	=	%exp:cFilPer%	AND
					RCF.RCF_PROCES	=	SRA.RA_PROCES	AND
					RCF.RCF_PER 	=	%exp:Upper(cPeriodo)%

			WHERE	SRA.RA_FILIAL BETWEEN %Exp:cFilDe% AND %Exp:cFilAte% AND
					SRA.RA_MAT    BETWEEN %Exp:cMatDe% AND %Exp:cMatAte% AND
					SRA.RA_CC     BETWEEN %Exp:cCcDe%  AND %Exp:cCcAte%  AND
					SRA.RA_NOME   BETWEEN %Exp:cNomDe% AND %Exp:cNomAte% AND
					SRA.RA_SITFOLH IN (%exp:Upper(cSitQuery)%) AND
			   		SRA.RA_CATFUNC IN (%exp:Upper(cCatQuery)%) AND
			   		SRD.RD_PROCES  IN (%exp:Upper(cProcs)%)  	AND
			   		SRD.RD_PERIODO =   %exp:Upper(cPeriodo)%   AND
			   		SRD.RD_ROTEIR  IN (%exp:Upper(cRoteiro)%)  AND
					SRA.%notDel% AND SRD.%notDel% AND RCF.%notDel%)
			ORDER BY %exp:cOrdFun%

		EndSql

	END REPORT QUERY oSectFun
	
	
	
	BEGIN REPORT QUERY oSectLan

		BeginSql alias cAliasQry

			SELECT	SRA.RA_FILIAL,			SRA.RA_MAT,				SRA.RA_CC,				SRA.RA_NOME,
					SRA.RA_DEPTO,			SRA.RA_HRSMES,			SRA.RA_SALARIO,			SRA.RA_SITFOLH,
					SRA.RA_DTREC,			SRA.RA_CATFUNC,			SRA.RA_CIC,				SRA.RA_NACIONA,
					SRA.RA_NASC,			SRA.RA_SEXO,			SRA.RA_ADMISSA,			SRA.RA_CODFUNC,
					SRA.RA_CARGO,
					SRC.RC_FILIAL FILIAL,	SRC.RC_MAT MAT,			SRC.RC_CC CCUSTO,		SRC.RC_PD PD,
					SRC.RC_TIPO1 TIPO1,		SRC.RC_QTDSEM QTDSEM,	SRC.RC_HORINFO HORINFO,	SRC.RC_HORAS HORAS,
					SRC.RC_VALINFO VALINFO,	SRC.RC_VALOR VALOR,		SRC.RC_VNAOAPL VNAOAPL,	SRC.RC_DATA DATPGT,
					SRC.RC_DTREF DTREF,		SRC.RC_SEMANA SEMANA,	SRC.RC_SEQ SEQ,			SRC.RC_PROCES PROCES,
					SRC.RC_PERIODO PERIODO,	SRC.RC_NUMID NUMID,		SRC.RC_ROTEIR ROTEIR,
					SRV.RV_COD,				SRV.RV_INFSAL INFSAL,	SRV.RV_CODFOL CODFOL
			FROM %table:SRA% SRA
			INNER JOIN	%table:SRC% SRC
			ON 	    SRA.RA_FILIAL = SRC.RC_FILIAL	AND
					SRA.RA_MAT    = SRC.RC_MAT
			INNER JOIN 	%table:SRV% SRV
			ON		SRV.RV_FILIAL = %exp:cFilPd%	AND
					SRV.RV_COD    = SRC.RC_PD

			WHERE	SRA.RA_FILIAL BETWEEN %Exp:cFilDe% AND %Exp:cFilAte% AND
					SRA.RA_MAT    BETWEEN %Exp:cMatDe% AND %Exp:cMatAte% AND
					SRA.RA_CC     BETWEEN %Exp:cCcDe%  AND %Exp:cCcAte%  AND
					SRA.RA_NOME   BETWEEN %Exp:cNomDe% AND %Exp:cNomAte% AND
					SRA.RA_SITFOLH IN (%exp:Upper(cSitQuery)%) AND
			   		SRA.RA_CATFUNC IN (%exp:Upper(cCatQuery)%) AND
			   		SRC.RC_PROCES  IN (%exp:Upper(cProcs)%)  	AND
			   		SRC.RC_PERIODO =   %exp:Upper(cPeriodo)%   AND
			   		SRC.RC_ROTEIR  IN (%exp:Upper(cRoteiro)%)  AND
			   		SRV.RV_INFSAL <> (%exp:""%) AND
					SRA.%notDel% AND SRC.%notDel% AND SRV.%notDel%
			UNION

			(SELECT SRA.RA_FILIAL,			SRA.RA_MAT,				SRA.RA_CC,				SRA.RA_NOME,
					SRA.RA_DEPTO,			SRA.RA_HRSMES,			SRA.RA_SALARIO,			SRA.RA_SITFOLH,
					SRA.RA_DTREC,			SRA.RA_CATFUNC,			SRA.RA_CIC,				SRA.RA_NACIONA,
					SRA.RA_NASC,			SRA.RA_SEXO,			SRA.RA_ADMISSA,			SRA.RA_CODFUNC,
					SRA.RA_CARGO,
					SRD.RD_FILIAL FILIAL,	SRD.RD_MAT MAT,			SRD.RD_CC CCUSTO,		SRD.RD_PD PD,
					SRD.RD_TIPO1 TIPO1,		SRD.RD_QTDSEM QTDSEM,	SRD.RD_HORINFO HORINFO,	SRD.RD_HORAS HORAS,
					SRD.RD_VALINFO VALINFO,	SRD.RD_VALOR VALOR,		SRD.RD_VNAOAPL VNAOAPL,	SRD.RD_DATPGT DATPGT,
					SRD.RD_DTREF DTREF,		SRD.RD_SEMANA SEMANA,	SRD.RD_SEQ SEQ,			SRD.RD_PROCES PROCES,
					SRD.RD_PERIODO PERIODO,	SRD.RD_NUMID NUMID,		SRD.RD_ROTEIR ROTEIR,
					SRV.RV_COD,				SRV.RV_INFSAL INFSAL,	SRV.RV_CODFOL CODFOL
			FROM %table:SRA% SRA

			INNER JOIN %table:SRD% SRD
			ON 	    SRA.RA_FILIAL = SRD.RD_FILIAL	AND
					SRA.RA_MAT    = SRD.RD_MAT
			INNER JOIN %table:SRV% SRV
			ON		SRV.RV_FILIAL 	= %exp:cFilPd%	AND
					SRV.RV_COD    	= SRD.RD_PD

			WHERE	SRA.RA_FILIAL BETWEEN %Exp:cFilDe% AND %Exp:cFilAte% AND
					SRA.RA_MAT    BETWEEN %Exp:cMatDe% AND %Exp:cMatAte% AND
					SRA.RA_CC     BETWEEN %Exp:cCcDe%  AND %Exp:cCcAte%  AND
					SRA.RA_NOME   BETWEEN %Exp:cNomDe% AND %Exp:cNomAte% AND
					SRA.RA_SITFOLH	IN	(%exp:Upper(cSitQuery)%)	AND
			   		SRA.RA_CATFUNC	IN	(%exp:Upper(cCatQuery)%)	AND
			   		SRD.RD_PROCES	IN	(%exp:Upper(cProcs)%)		AND
			   		SRD.RD_PERIODO	=	%exp:Upper(cPeriodo)%		AND
			   		SRD.RD_ROTEIR	IN	(%exp:Upper(cRoteiro)%)		AND
			   		SRV.RV_INFSAL	<>	(%exp:""%) AND
					SRA.%notDel% AND SRD.%notDel% AND SRV.%notDel%)
			ORDER BY %exp:cOrdLan%

	EndSql

	END REPORT QUERY oSectLan

	//Quantidade total de funcionarios
	Do While (cAliasFun)->( !Eof())
		nTotFunc++
  		(cAliasFun)->(DbSkip())
	EndDo
	(cAliasFun)->( dbGoTop())

	If nTotFunc > 0
		If nTotFunc > 27
			nTotPage := If( Int( nTotFunc/(nMaxFunc ) ) >= 1, Int(nTotFunc/(nMaxFunc-1)), 1 )
			If Mod( nTotFunc, (nMaxFunc-1) ) > 0
				nTotPage ++
			EndIf
		Else
			nTotPage := 1
		EndIf
	EndIf

	cDescMes := OemToAnsi(STR0012) +" "+ UPPER(FDESC_MES(Val(SubStr(cCompet,1,2)))) +" "+ STR0013 +" "+ SubStr(cCompet,3,4)
	cTotPage := OemToAnsi(STR0073) +" "+ cValToChar(nPageAtu) +" "+ STR0013 +" "+ cValToChar(nTotPage)

	

	// IMPRESSAO DO CABECALHO
	If (cAliasFun)->(!Eof())
		oReport:OnPageBreak({||	oSectCab:Init(),;
								oSectCab:Cell("NUM_EMP"):SetValue( cEmpMTrab ),;
								oSectCab:Cell("PAGINA"):SetValue( cTotPage ),;
								oSectCab:PrintLine(),;
								oSectCab:Finish(),;
								oReport:Say((oReport:nRow)+030, (oReport:nCol+1200), STR0003, oCNew10),;
								oReport:Say((oReport:nRow)+060, (oReport:nCol+1270), cPersona, oCNew10N),;
								oReport:Say((oReport:nRow)+090, (oReport:nCol+1320), STR0011, oCNew08N),;
								oReport:Say((oReport:nRow)+120, (oReport:nCol+1900), cDescMes, oCNew08N),;
								oReport:Say((oReport:nRow)+180, (oReport:nCol), cTexto0, oCNew08N),;
								oReport:Say((oReport:nRow)+210, (oReport:nCol), cTexto1, oCNew08),;
								oReport:Say((oReport:nRow)+240, (oReport:nCol), cTexto2, oCNew08),;
								oReport:Say((oReport:nRow)+270, (oReport:nCol), cTexto3, oCNew08),;
								oReport:Say((oReport:nRow)+300, (oReport:nCol), cTexto4, oCNew08),;
								oReport:Line( (oReport:nRow)+330, (oReport:nCol)+10, (oReport:nRow)+330, (oReport:nCol)+3180 ),;
								oReport:SetRow(oReport:nRow+360) })
	EndIf
	
	//IMPRESSAO DOS LANCAMENTOS
	//Recurso para retirar o espaco da primeira linha porque foram iniciadas duas secoes ao mesmo tempo
	oSectFun:SetHeaderSection(.F.)
	oSectFun:Init()

	If (cAliasFun)->(!Eof())
		oSectFun:PrintLine()
	EndIf

	oSectLan:SetHeaderSection(.F.)
	oSectLan:Init()
	
	If (cAliasFun)->(!Eof())
		oSectLan:PrintLine()
	EndIf
	
	oReport:SetRow(oReport:nRow-90)

	If nTotFunc <= 0
		Return()
	EndIf

	Do While (cAliasFun)->(!Eof())

		nDiaPgMes := 0
		nVHDiaPg  := 0

		For i := 1 To 14
			For iX := 1 to 2
				aVerba[i,iX] := 0
			Next iX
		Next i

		IF (cAliasFun)->RA_MAT <> cMatAnt .And. &cCondicao
			oSectFun:Cell("NUM"			):SetValue( nQtdFun += 1 )
			oSectFun:Cell("CI"			):SetValue( (cAliasFun)->RA_CIC )
			oSectFun:Cell("APELLIDOS"	):SetValue( (cAliasFun)->RA_NOME )
			oSectFun:Cell("NACIONAL"	):SetValue( fDesc("SX5","34"+(cAliasFun)->RA_NACIONA,"X5_DESCRI") )
			oSectFun:Cell("NASCIMENTO"	):SetValue( (cAliasFun)->RA_NASC )
			oSectFun:Cell("SEXO"		):SetValue( (cAliasFun)->RA_SEXO )
			oSectFun:Cell("OCUPACION"	):SetValue( fDesc("SQ3",(cAliasFun)->RA_CARGO,"Q3_DESCSUM") )
			oSectFun:Cell("INGRESSO"	):SetValue( (cAliasFun)->RA_ADMISSA )
			oSectFun:PrintLine()
			nDiaPgMes	:= (cAliasFun)->RCF_DIATRA
		EndIf

		Do While (cAliasQry)->(!Eof()) .And. &cCondicao

			If (cAliasQry)->CODFOL $ "0031|0048"
				nVHDiaPg += (cAliasQry)->HORAS
			EndIf

			Do Case
				Case (cAliasQry)->INFSAL == "A"
					aVerba[1,1] += (cAliasQry)->VALOR
				Case (cAliasQry)->INFSAL == "B"
			   		aVerba[2,1] += (cAliasQry)->VALOR
				Case (cAliasQry)->INFSAL == "C"
			   		aVerba[3,1] += (cAliasQry)->VALOR
			   		aVerba[3,2] += (cAliasQry)->HORAS
				Case (cAliasQry)->INFSAL == "D"
				   	aVerba[4,1] += (cAliasQry)->VALOR
				Case (cAliasQry)->INFSAL == "F"
			   		aVerba[5,1] += (cAliasQry)->VALOR
				Case (cAliasQry)->INFSAL == "E"
				   	aVerba[6,1] += (cAliasQry)->VALOR
			   		aVerba[6,2] += (cAliasQry)->HORAS
				Case (cAliasQry)->INFSAL == "H"
			   		aVerba[7,1] += (cAliasQry)->VALOR
				Case (cAliasQry)->INFSAL == "I"
			   		aVerba[8,1] += (cAliasQry)->VALOR
				Case (cAliasQry)->INFSAL == "J"
			   		aVerba[9,1] += (cAliasQry)->VALOR
			   	Case (cAliasQry)->INFSAL == "K" 			//Nao mais usado (retirado em 2013/11)
			   		aVerba[13,1] += (cAliasQry)->VALOR
				Case (cAliasQry)->INFSAL == "L"
			   		aVerba[14,1] += (cAliasQry)->VALOR
				Case (cAliasQry)->INFSAL == "N"     		//Nao mais usado (retirado em 2013/11)
			   		aVerba[14,1] += (cAliasQry)->VALOR
			EndCase

			(cAliasQry)->(DbSkip())
		EndDo

		If aVerba[1,1] > 0

			aVerba[10,1] := aVerba[1,1] + aVerba[2,1] + aVerba[3,1] + aVerba[4,1] + aVerba[5,1] + aVerba[6,1]	//TOTAL GANADO (G)
			aVerba[11,1] := aVerba[7,1] + aVerba[8,1] + aVerba[9,1] 				 								//TOTAL DESCONTOS (L)
			aVerba[12,1] := aVerba[10,1] - aVerba[11,1] 															//LIQUIDO PAGABLE (N)

			oSectLan:Cell("DIASHPM"):SetValue(nDiaPgMes)		//DIAS HAB. PAGADOS MES
			oSectLan:Cell("HORASDP"):SetValue(nVHDiaPg)			//HORAS PAGADOS MES
			oSectLan:Cell("HABERB"):SetValue(aVerba[1,1])		//HABER BASICO (A)
			oSectLan:Cell("BONOANT"):SetValue(aVerba[2,1])		//BONO DE ANTIGUEDAD (B)
			oSectLan:Cell("HENUMERO"):SetValue(aVerba[3,2])	//HORA EXTRA - NUMERO
			oSectLan:Cell("HEMONTO"):SetValue(aVerba[3,1])		//HORA EXTRA - MONTO PAGADO (C)
			oSectLan:Cell("REMBONO"):SetValue(aVerba[4,1])		//REMUNERACAO - BONO DE PRODUCION (D)
			oSectLan:Cell("REMOTROS"):SetValue(aVerba[5,1])	//REMUNERACAO - OUTROS BONOS (F)
			oSectLan:Cell("SDSRNUM"):SetValue(aVerba[6,2])		//SUELDO DSR - NUMERO DE DOMINGOS
			oSectLan:Cell("SDSRSUE"):SetValue(aVerba[6,1])		//SUELDO DSR - SUELDO DOMINICAL (E)
			oSectLan:Cell("TOTGANADO"):SetValue(aVerba[10,1])	//TOTAL GANADO (G)
			oSectLan:Cell("DAFP"):SetValue(aVerba[7,1])		//DESCONTO - AFP 12 (H)
			oSectLan:Cell("DRCIVA"):SetValue(aVerba[8,1])		//DESCONTO - RC-IVA(I)
			oSectLan:Cell("DODESC"):SetValue(aVerba[9,1])		//DESCONTO - OUTROS DESCONTOS (J)
			oSectLan:Cell("TOTDCTOS"):SetValue(aVerba[11,1])	//TOTAL DESCONTOS (L)
			oSectLan:Cell("LIQUIDOP"):SetValue(aVerba[12,1])	//LIQUIDO PAGABLE (M)
			oSectLan:Cell("FIRMA"):SetValue( cFirma )			//FIRMA

			oSectLan:PrintLine()

		EndIf

		cCodFilial	:= (cAliasFun)->RA_FILIAL
		cMatAnt 	:= (cAliasFun)->RA_MAT

		//Salta pagina - limite de impressao dos lancamentos por funcionario
	    If oReport:nRow >= 2350
		    nPageAtu++
		    cTotPage := cValToChar(nPageAtu) + " " + STR0013 + " " + cValToChar(nTotPage)
			oReport:EndPage()
	    Endif

		(cAliasFun)->(DbSkip())

	EndDo

	oSectFun:Finish()
	oSectLan:Finish()

	//Salta pagina - limite para nao cortar a impressao do rodape (330)
    If oReport:nRow >= 2110
		nPageAtu++
	    cTotPage := cValToChar(nPageAtu) + " " + STR0013 + " " + cValToChar(nTotPage) //"CORRESPONDIENTE AL MES DE"
		oReport:EndPage()
	EndIf

	//IMPRESSAO DO RODAPE
	oReport:Say(oReport:nRow, oReport:nCol, "", oCNew08 ) //Recurso - linha vazia para iniciar a posicao da Linha no relatorio

	//Nome do Empregador
	oReport:Say((oReport:nRow)+300, (oReport:nCol)+170, cNomEmpr, oCNew08 )
	oReport:Line( (oReport:nRow)+327, (oReport:nCol)+130, (oReport:nRow)+327, (oReport:nCol)+750 )
	oReport:Say((oReport:nRow)+330, (oReport:nCol)+145, STR0071, oCNew08N )

	//Documento de Identidade
	oReport:Say((oReport:nRow)+300, (oReport:nCol)+1120, cNumDocId, oCNew08 )
	oReport:Line( (oReport:nRow)+327, (oReport:nCol)+1000, (oReport:nRow)+327, (oReport:nCol)+1500 )
	oReport:Say((oReport:nRow)+330, (oReport:nCol)+1100, STR0070, oCNew08N )

	//Assinatura (firma)
	oReport:Say((oReport:nRow)+330, (oReport:nCol)+1950, STR0068, oCNew08N )
	oReport:Line( (oReport:nRow)+327, (oReport:nCol)+1750, (oReport:nRow)+327, (oReport:nCol)+2250 )

	//Data
	oReport:Say((oReport:nRow)+300, (oReport:nCol)+2450, STR0018 + " " + DtoC(dDataBase), oCNew08N )

Return()


//RELATORIO FORMATO TMSPRINTER
/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥ GPER680R3  ≥ Autor ≥ Gisele Varandas       ≥ Data ≥17/12/2007≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descriáao ≥ Relatorio de Saldos e Salarios                               ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ GPER680                                                      ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Function GPER680R3
//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ Define Variaveis Locais (Basicas)                            ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
Local cString  :="SRA"        								// ALias do arquivo principal (Base)
Local aOrd     := {STR0001,STR0002} 						//" Matricula "###" Centro de Custo "
Local nTotregs,nMult,nPosAnt,nPosAtu,nPosCnt,cSav20,cSav7 	//Regua
Local cDesc1   := STR0003									//"PLANILHA DE SALDOS E SALARIOS"
Local cDesc2   := STR0004			   						//"Ser† impresso de acordo com os parametros solicitados pelo"
Local cDesc3   := STR0005			   						//"usu†rio."
Local cMesAnoRef	:= ""
Local aPerAberto	:= {}
Local aPerFechado	:= {}
Local aPerTodos		:= {}
Local cNumPg		:= Space(2)
Local cSavAlias,nSavRec,nSavOrdem
Local cQuerySRA := ""
Local nCount := 0

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ Define Variaveis Private(Basicas)                            ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
Private nomeprog:="GPER680"
Private aReturn := {STR0006, 1,STR0007, 1, 2, 1, "",1 }	//"Zebrado"###"AdministraáÑo"
Private anLinha := {}
Private nLastKey := 0
Private cPerg   :="GPR680"

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ Variaveis Utilizadas na funcao IMPR                          ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
Private Titulo  := STR0003		//"PLANILLA DE SUELDOS Y SALARIOS"
Private AT_PRG  := "GPER680"
Private CONTFL  := 1  			//CONTA PAGINA
Private nLi     := 0
Private nTamanho:="G"
Private nOrdem
Private aTotSoma:= array(15)

Private cProcesso	:= "" // Armazena o processo selecionado na Pergunte GPR040 (mv_par01).
Private cRoteiro	:= "" // Armazena o Roteiro selecionado na Pergunte GPR040 (mv_par02).
Private cDescRel	:= "" // Armazena a descricao do Roteiro selecionado (funcao Gpr040Roteiro).
Private cPeriodo	:= ""
Private cCond		:= ""
Private cRot		:= ""
Private lRotEmpty	:= .T. // Variavel utilizada na visuzalizaÁ„o de roteiros em branco na consulta padrao
Private dDtPerIni	:= Ctod("  /  /  ")
Private dDtPerFim	:= Ctod("  /  /  ")
Private cMes		:= ""
Private cAno		:= ""

//Objetos p/ Impresssao Grafica
Private oFont07,oFont08, oFont10, oFont15, oFont10n, oFont21, oFont12,oFont16
Private oPrint

oFont05	:= TFont():New("Courier New",05,05,,.F.,,,,.T.,.F.)
oFont05n	:= TFont():New("Courier New",05,05,,.T.,,,,.T.,.F.)		//negrito
oFont06	:= TFont():New("Courier New",06,06,,.F.,,,,.T.,.F.)
oFont07	:= TFont():New("Courier New",07,07,,.F.,,,,.T.,.F.)
oFont07n	:= TFont():New("Courier New",07,07,,.T.,,,,.T.,.F.)		//negrito
oFont08	:= TFont():New("Courier New",08,08,,.T.,,,,.T.,.F.)
oFont08n	:= TFont():New("Courier New",08,08,,.T.,,,,.T.,.F.)		//negrito
oFont10	:= TFont():New("Courier New",10,10,,.F.,,,,.T.,.F.)
oFont10n	:= TFont():New("Courier New",10,10,,.T.,,,,.T.,.F.)     //negrito
oFont12	:= TFont():New("Courier New",12,12,,.F.,,,,.T.,.F.)		//Normal s/negrito
oFont15	:= TFont():New("Courier New",15,15,,.T.,,,,.T.,.F.)
oFont21	:= TFont():New("Courier New",21,21,,.T.,,,,.T.,.T.)
oFont16	:= TFont():New("Arial",16,16,,.T.,,,,.T.,.F.)


/*/
⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
≥ Verifica as perguntas selecionadas                           ≥
¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
≥ Variaveis utilizadas para parametros                         ≥
≥ mv_par01        //  Processo						           ≥
≥ mv_par02        //  Roteiro							       ≥
≥ mv_par03        //  Competencia                              ≥
≥ mv_par04        //  Filial  De                               ≥
≥ mv_par05        //  Filial  Ate                              ≥
≥ mv_par06        //  Centro de Custo De                       ≥
≥ mv_par07        //  Centro de Custo Ate                      ≥
≥ mv_par08        //  Matricula De                             ≥
≥ mv_par09        //  Matricula Ate                            ≥
≥ mv_par10        //  Nome De                                  ≥
≥ mv_par11        //  Nome Ate                                 ≥
≥ mv_par12        //  Situacao                                 ≥
≥ mv_par13        //  Categoria                                ≥
≥ mv_par13        //  Ordem                                    ≥
¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
≥ Carregando variaveis mv_par?? para Variaveis do Sistema.     ≥
¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ/*/
If Pergunte(cPerg, .T., Titulo)

	cProcesso 	:= mv_par01 // Processo selecionado na Pergunte.
	cRoteiro	:= mv_par02 // Roteiro selecionado na Pergunte.
	cCompet	:= mv_par03 // Competencia.
	cFilDe   	:= mv_par04
	cFilAte  	:= mv_par05
	cCcDe    	:= mv_par06
	cCcAte   	:= mv_par07
	cMatDe   	:= mv_par08
	cMatAte  	:= mv_par09
	cNomDe   	:= mv_par10
	cNomAte  	:= mv_par11
	cSit     	:= mv_par12
	cCat     	:= mv_par13
	nPerEvent	:= mv_par14
	cNomEmpr	:= mv_par15
	cNumDocId	:= mv_par16
	nOrdem   	:= mv_par17

	If !Empty(cCompet)
		cMes  		:= substr(mv_par03,1,2)
		cAno  		:= substr(mv_par03,3,4)
		cPeriodo	:= cAno + cMes
	EndIf

	/*
	⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
	≥ Carregar os periodos abertos (aPerAberto) e/ou os periodos   ≥
	≥ fechados (aPerFechado), de acordo com uma determinada compe- ≥
	≥ tencia.                 									   ≥
	¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
	fRetPerComp( cMes, cAno, Nil, cProcesso, Nil, @aPerAberto, @aPerFechado, @aPerTodos)

	/*
	⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
	≥ Retorna o mes e o ano do periodo selecionado na pergunte 			≥
	≥ para montagem e Data Inicio e Fim utilizadas para verificar	 	≥
	≥ Situacao do funcionario.                  						≥
	¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
	If (nPos:=aScan(aPerAberto, {|x| x[3] == cMes .And. x[4] == cAno})) > 0
		dDtPerIni   := aPerAberto[nPos,5]
		dDtPerFim   := aPerAberto[nPos,6]
	Elseif (nPos:=aScan(aPerFechado, {|x| x[3] == cMes .And. x[4] == cAno})) > 0
		dDtPerIni   := aPerFechado[nPos,5]
		dDtPerFim   := aPerFechado[nPos,6]
	Endif

	lFirst	:= .T.
	oPrint 	:= TMSPrinter():New(STR0003)
	oPrint	:SetLandscape()
	oPrint  :SetPaperSize(9)

	MsgRun(STR0079,"",{|| CursorWait(), fGP680Imp(aPerAberto,aPerFechado), CursorArrow()}) //"Gerando relatorio, aguarde..."

	oPrint:Preview()  		// Visualiza impressao grafica antes de imprimir
Else
	Return()
Endif

Return

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Funcao    ≥fGP680Imp ∫Autor  ≥Equipe RH Microsiga ∫ Data ≥  19/12/07   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥                                                            ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ GPER680                                                    ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function fGP680Imp(aPerAberto,aPerFechado)

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ Variaveis de Acesso do Usuario                               ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
Local cAcessaSRA	:= &( " { || " + ChkRH( "GPER680" , "SRA" , "2" ) + " } " )
Local aVerbasAux	:= {}
Local aVerbasFunc	:= {}
Local aRegSRE		:= {}
Local cChave 		:= ""
Local cMatricula	:= ""
Local cAcessaSRC  	:= &("{ || " + ChkRH("GPER680","SRC","2") + "}")
Local cTipAfas    	:= " "
Local cAliasAnt		:= " "

Local aSomaVerba[14,2]

Local lAs400		:= ( TcSrvType() == "AS/400" )		//-- Retiramos a execucao da query para servidores AS/400.

Local nX
Local nY
Local nReg
Local nTotRegPrc	:= 0
Local nRegMov		:= 0
Local nCont			:= 0
Local nContFunc		:= 0
Local nTot			:= 0
Local nTransf 		:= 0

#IFDEF TOP
	Local aSRAFilter	:= {}
	Local aFields		:= {}

	Local cCateg
	Local cSRAQuery
	Local cSvSRAQuery
#ENDIF

Private cAliasMov   := "SRA"
Private cDescDet    := ""
Private nCodSeq		:= 0    	//N. sequencial
Private nContPage	:= 0    	//Conta N. de Pagina
Private nLin1		:= 700    	//Variavel para impressao de Dados da Lin1
Private nLin2		:= 770    	//Variavel para impressao de Dados da Lin2
Private nLin3		:= 820		//Variavel para impressao de linha de separacao entre registros
Private nMaxFunc	:= 12     	//N. maximo de funcionario por pagina
Private nHoras		:= 0        //Horas (RC_HORAS)
Private nFaltas     := 0
Private nVFalt      := 0
Private nVHDiaPg	:= 0		//Valor Pago Hora/Dia
Private nDiaPgMes	:= 0		//N. de Dias Uteis Pagos do Mes

Private aTotSoma[14,2]    		//Array para armazernar totalizadores
Private 	cMainTmp	:= ""

aEval(aTotSoma,{|x|aFill(x,0)})

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥MUDAR ANO PARA 4 DIGITOS≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
SET CENTURY ON

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ Posiciona no Primeiro Registro Selecionado                   ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
dbSelectArea("SRA")
nSavRec   := RecNo()
nSavOrdem := IndexOrd()


// ver as ordens do relatorio e aplicar o dbsetorder
Begin Sequence

	dbSelectArea( "SRA" )
	dbGoTop()
	If nOrdem == 1  // por Matricula
		cChave := cFilDe + cMatDe
		dbSetOrder( 1 )
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_MAT"
		cFim     := cFilAte + cMatAte
	ElseIf nOrdem == 2
		cChave := cFilDe + cCcDe + cMatDe
		dbSetOrder( 2 )
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_MAT"
		cFim     := cFilAte + cCcAte + cMatAte
	EndIF



	#IFDEF TOP

		//QUERY PARA RETORNAR O TOTAL DE FUCNIONARIOS
		cMainTmp	:= GetNextAlias()
		nTotRegPrc	:= 0
		cQuerySRA := "SELECT COUNT(RA_MAT)  as nTot "
		cQuerySRA += "FROM " + RetSqlName("SRA") + " SRA "
		If !Empty( aPerAberto )
			cQuerySRA += "WHERE EXISTS( SELECT RC_MAT FROM " + RetSqlName("SRC") + " SRC "
			cQuerySRA += "               WHERE SRC.RC_FILIAL = SRA.RA_FILIAL AND "
			cQuerySRA += "               SRC.RC_MAT = SRA.RA_MAT AND SRC.D_E_L_E_T_ = ' ' )  "

		Elseif !Empty( aPerAberto ) .And. !Empty( aPerFechado )
			cQuerySRA += " UNION "
			cQuerySRA := "SELECT COUNT(RA_MAT)  as nTot "
			cQuerySRA += "FROM " + RetSqlName("SRA") + " SRA "
			cQuerySRA += "WHERE EXISTS( SELECT RD_MAT FROM " + RetSqlName("SRD") + " SRD "
			cQuerySRA += "               WHERE SRD.RD_FILIAL = SRA.RA_FILIAL AND "
			cQuerySRA += "               SRD.RD_MAT = SRA.RA_MAT AND "
			cQuerySRA += "               SRD.RD_PERIODO = '" + cPeriodo + "' AND "
			cQuerySRA += "SRD.D_E_L_E_T_ = ' '  ) "
		Else
			cQuerySRA += "WHERE EXISTS( SELECT RD_MAT FROM " + RetSqlName("SRD") + " SRD "
			cQuerySRA += "               WHERE SRD.RD_FILIAL = SRA.RA_FILIAL AND "
			cQuerySRA += "               SRD.RD_MAT = SRA.RA_MAT AND "
			cQuerySRA += "               SRD.RD_PERIODO = '" + cPeriodo + "' AND "
			cQuerySRA += "SRD.D_E_L_E_T_ = ' '  ) "
		Endif
		cQuerySRA += " AND SRA.D_E_L_E_T_ = ' ' "
		cQuerySRA += "UNION "
		cQuerySRA += "SELECT COUNT(R8_MAT) as nTot "
		cQuerySRA += "FROM " +  RetSqlName("SR8") + " SR8, " + RetSqlName("SRA") + " SRA "
		cQuerySRA += "WHERE SR8.R8_FILIAL = SRA.RA_FILIAL AND "
		cQuerySRA += "SR8.R8_MAT = SRA.RA_MAT AND "
		cQuerySRA += "SRA.RA_FILIAL BETWEEN '" + cFilDe + "' AND '" + cFilAte	+ "' AND "
		cQuerySRA += "SRA.RA_MAT    BETWEEN '" + cMatDe + "' AND '" + cMatAte	+ "' AND "
		cQuerySRA += "SRA.RA_CC     BETWEEN '" + cCcDe  + "' AND '" + cCcAte	+ "' AND "
		cQuerySRA += "(R8_DATAINI <= '" + DtoS(dDtPerIni) + "' AND (R8_DATAFIM >= '" + DtoS(dDtPerFim) + "' OR R8_DATAFIM = '" + Space(Len(DtoS(SR8->R8_DATAINI))) + "') ) AND "
		cQuerySRA += "SR8.D_E_L_E_T_ = ' '


		cQuerySRA := ChangeQuery(cQuerySRA)
		dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuerySRA), cMainTmp, .F., .T.)

		dbSelectArea( cMainTmp)

			While (cMainTmp)->( !Eof())
				nCount:= ( cMainTmp )-> nTot
				nTotRegPrc+= nCount
		  		dbSkip()
			Enddo

	#ELSE
		nTotRegPrc := SRA->( Reccount() )
	#ENDIF

	While (cAliasMov)->( !Eof() .And. &cInicio <= cFim )

		cCcto := SRA->RA_CC
		aRegSRE	:= {}

		/*
		⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
		≥ Consiste Parametrizacao do Intervalo de Impressao            ≥
		¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
		If ( SRA->RA_NOME < cNomDe )  .Or. ( SRA->RA_NOME > cNomAte ) .Or. ;
			( SRA->RA_MAT < cMatDe )   .Or. ( SRA->RA_MAT > cMatAte )
			SRA->(dbSkip(1))
			Loop
		EndIf

		If (nOrdem # 2 .And. ((SRA->RA_CC < cCcDe) .Or. (SRA->RA_CC > cCcAte))) .Or.;
		   (nOrdem == 2 .And. (((cAliasMov)->RA_CC < cCcDe) .Or. ((cAliasMov)->RA_CC > cCcAte)))
			(cAliasMov)->( DbSkip() )
			Loop
		EndIf

		/*
		⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
		≥ Retorna a situaÁ„o do funcioanrio no periodo  a ser listado ≥
		¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
		fSitFunc(SRA->RA_FILIAL,SRA->RA_MAT,dDtPerIni,dDtPerFim,@cTipAfas)

		/*
		⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
		≥ Verifica Situacao e Categoria do Funcionario conforme pergunte   ≥
		¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
 		If	!(cTipAfas $ cSit) .Or. !(SRA->RA_CATFUNC $ cCat)
 			dbSelectArea("SRA")
      		SRA->( dbSkip() )
 			Loop
		Endif

		/*
		⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
		≥ Consiste controle de acessos e filiais validas			   |
		¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
		If !(SRA->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
			dbSelectArea("SRA")
      		SRA->( dbSkip() )
 			Loop
		EndIf

		/*
		⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
		≥ Retorna Todas as Transferencias do Funcionario.              ≥
		¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
		If fTransf(@aRegSRE,,,,,,,.T.)

			aSort( aRegSRE ,,, { |x,y| x[7] > y[7] } )

			For nTransf := 1 to Len(aRegSRE)
				If cPeriodo < AnoMes(aRegSRE[nTransf][7])
					cCcto := aRegSRE[nTransf][3]
				EndIf
			Next nTransf

		EndIf

		//Seleciona os roteiros para pesquisa das verbas
		SelecRoteiros()

    	aVerbasFunc := Array(0)
		For nX := 1 to Len(aRoteiros)
			//Retorna as verbas do funcionario, de acordo com os periodos selecionados
			aVerbasAux	:= RetornaVerbasFunc(	SRA->RA_FILIAL	,; 	// Filial do funcionario corrente
												SRA->RA_MAT	  	,; 	// Matricula do funcionario corrente
												@cCcto			,; 	// Centro de Custos
												aRoteiros[nX,1]	,; 	// Roteiro selecionado na pergunte
												NIL			  	,; 	// Array com as verbas que dever„o ser listadas. Se NIL retorna todas as verbas.
												aPerAberto	  	,; 	// Array com os Periodos e Numero de pagamento abertos
												aPerFechado	 	 ) 	// Array com os Periodos e Numero de pagamento fechados
			If (nVbs := Len(aVerbasAux)) > 0
				For nY := 1 To nVbs
					aAdd( aVerbasFunc, aVerbasAux[nY] )
				Next nY
			EndIf
		Next nX

		If Empty(aVerbasFunc)
			dbSelectArea("SRA")
			SRA->( dbSkip() )
			Loop
		EndIf

		aEval(aSomaVerba,{|x|aFill(x,0)})

		For nReg := 1 to Len(aVerbasFunc)

			If !Eval(cAcessaSRC)
				dbSkip()
				Loop
			EndIf

			fSomaInfSal(@aSomaVerba,aVerbasFunc,nReg)

		Next nReg
		cRCHFil := xFilial("RCF")
		cAliasRCF := "RCF"
		dbSelectArea(cAliasRCF)
		dbSetOrder(1) //RetOrder("RCF", "RCF_FILIAL+RCF_ANO+RCF_MES"))	// Ordem 1
		DbSeek( XFILIAL("RCF")+cAno+cMes, .F. )
		nQtHoras 	:= 0
		nDiaPgMes	:= 0
		nTeste 		:= .T.
		While RCF->( !Eof() ) .and. RCF->( RCF_FILIAL + RCF_ANO + RCF_MES ) == cRCHFil + cAno + cMes
		    If (SRA->RA_TNOTRAB = RCF->RCF_TNOTRA .AND. RCF->RCF_ROTEIR = "   " ).OR. (RCF->RCF_TNOTRA = "@@@" .AND. RCF->RCF_ROTEIR = "   ")
   			   nQtHoras 	:= RCF->RCF_HRSDIA
   			   nDiaPgMes	:= RCF->RCF_DIATRA
   			   nTeste 		:= .F.
	        EndIf
	        dBSkip()
   		EndDo

	    //Total Ganado (G)
		@aSomaVerba[10,1]:= (@aSomaVerba[1,1] + @aSomaVerba[2,1] + @aSomaVerba[3,1] + @aSomaVerba[4,1] + @aSomaVerba[5,1] + @aSomaVerba[6,1])
		//Total Dctos. (K)
		@aSomaVerba[11,1]:= (@aSomaVerba[7,1] + @aSomaVerba[8,1] + @aSomaVerba[9,1])
		//LÌquidos Pagable (L)
		@aSomaVerba[12,1]:= (@aSomaVerba[10,1] - @aSomaVerba[11,1])


	    //Incrementa totalizadores
	   	For nCont:=1 to Len(aSomaVerba)
			aTotSoma[nCont,1] += aSomaVerba[nCont,1]
			aTotSoma[nCont,2] += aSomaVerba[nCont,2]
		Next

		//Busca informacao para peencher o campo Dias H·b. Pagados Mes
		If ( cTipAfas == "D" ) .and. (!Empty(MesAno(SRA->RA_DEMISSA)) .and. Val(MesAno(SRA->RA_DEMISSA)) == Val(cAno + cMes))
			nDiaPgMes := DAY(SRA->RA_DEMISSA)
		EndIf

	    //Calcula Valor Horas/DÌas Pagadas
	    nVHDiaPg	:= Round(SRA->RA_HRSMES / 30,2)

	    If nFaltas > 0
	      If (SRA->RA_CATFUNC) $ "H*T*G*J*D"
	         nFaltas := Round(nFaltas * nQtHoras,2)
	      EndIf
	      nVFalt := Round(nVHDiaPg * nFaltas ,2)
	      //Abate a falta nos totalizadores
          aSomaVerba[1,1] := aSomaVerba[1,1] - nVFalt
          aSomaVerba[10,1]:= aSomaVerba[10,1] - nVFalt
          aSomaVerba[12,1]:= aSomaVerba[12,1] - nVFalt
          aTotSoma[1,1]   := aTotSoma[1,1] - nVFalt
          aTotSoma[10,1]  := aTotSoma[10,1] - nVFalt
          aTotSoma[12,1]  := aTotSoma[12,1]- nVFalt
	    EndIf
	    //Contador para controlar o num. de sequencia e a quebra de pagina
	    nCodSeq++

		//Verifica a mudanca ou nao de pagina
		If nCodSeq == 1 .OR. MOD( nCodSeq,nMaxFunc ) == 0
			If MOD( nCodSeq,nMaxFunc ) == 0
				oPrint:EndPage()
			EndIf
	   		oPrint:StartPage()
			fCabecalho( nTotRegPrc )
		EndIf

		fImprime(aSomaVerba)
		nFaltas := nVFalt := 0
		dbSelectArea("SRA")
		dbSkip()

	Enddo

    //VerificaÁ„o para impressao do RodapÈ. Se n„o der para imprimir os totalizadores e rodapÈ na mesma p·gina;
    //inicia-se uma nova p·gina para impress„o dessas informaÁıes

	If ( nTotRegPrc > 0 )
	 	If nLin3 < 1890
	 		If nCodSeq == 0
				fCabecalho(nTotRegPrc)
			EndIf
	 		fRodape()
	 	Else
			oPrint:EndPage()
		   	oPrint:StartPage()
			fCabecalho(nTotRegPrc)
		 	fRodape()
	 	EndIf
	EndIf

End Sequence

/*
⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
≥ Fecha o SRA                                                  ≥
¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
	If ( Select( "SRA" ) > 0 )
		SRA->( dbCloseArea() )
	EndIf
	DbSelectArea("SRA")


Return Nil

/*/
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo	 ≥fCabecalho   ≥ Autor ≥ Equipe de RH       ≥ Data ≥03/01/08  ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ 						                                      ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe   ≥fCabecalho()									     		  ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Parametros≥                                                            ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso	     ≥ Gper680  												  ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function fCabecalho(nTotRegPrc)

Local nCol	  	 := 172
Local cDescMes	 := ' '
Local nMes 	  	 := Val(cMes)
Local nTotPage   := 0
Local cDescEmpr  := ' '
Local cNEmpMTrab := ' '
Local nTotPageAux:= 0

oPrint:StartPage()

nTotPageAux:= (nTotRegPrc/nMaxFunc) - INT(nTotRegPrc/nMaxFunc)

If (nTotPageAux*100) > 84
	nTotPage:= INT(nTotRegPrc/nMaxFunc)+1					//se tiverem mais de 10 func. na ultima pag. sera criado uma pag. para imprimir o rodape (totalizador + as assinaturas e data do relatorio)
EndIf

If (nTotRegPrc/nMaxFunc) > INT(nTotRegPrc/nMaxFunc) 	    //Cabem 12 func. por pagina, se o resultado da divis„o n„o der inteiro ser· necess·rio uma nova pag. para imprimir o restante dos funcion·rios
	nTotPage:= INT(nTotRegPrc/nMaxFunc)+1
EndIf

cDescMes:= fDesc_Mes(nMes, 9)

nLin1:= 700
nLin2:= 770
nLin3:= 820
nContPage++

fInfo(@aInfo,SRA->RA_FILIAL)                			//Carrega array com informacoes da Filial

cNEmpMTrab:= If(FTABELA("S007",1,5, ) <> Nil,FTABELA("S007",1,5, )," ")

cDescEmpr:= PadR(aInfo[3],35)

/*
⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
≥ Montagem do cabecalho Principal do Relatorio                 ≥
¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
oPrint:say (0180,2600,STR0073+' '+Str(nContPage,3)+' '+STR0013+' '+Str(nTotPage,3),oFont08) //PAGINA
oPrint:say (0180,0050,STR0008,oFont08) 			//NOMBRE O RAZ”N SOCIAL
oPrint:say (0180,0450,cDescEmpr,oFont08)    	//DESCR NOMBRE O RAZON SOCIAL
oPrint:say (0220,0050,STR0009,oFont08) 			//N∫ EMPLEADOR MINISTERIO DE TRABAJO
oPrint:say (0220,0700,cNEmpMTrab,oFont08)   	//DESCR N∫ EMPLEADOR MINISTERIO DE TRABAJO

oPrint:say (0280,1350,STR0003,oFont10) 			//PLANILLA DE SUELDOS Y SALARIOS

If nPerEvent == 1
	oPrint:say (0330,1410,STR0010,oFont10n) 	//PERSONAL PERMANENTE
ElseIf nPerEvent == 2
	oPrint:say (0330,1420,STR0075,oFont10n) 	//PERSONAL EVENTUAL
EndIf

oPrint:say (0380,1490,STR0011,oFont08)     		//(En Bolivianos)

oPrint:say (0405,2310,STR0012,oFont08) 			//CORRESPONDIENTE AL MES DE
oPrint:say (0405,2770,Upper(cDescMes),oFont08)  //M S
oPrint:say (0405,3000,STR0013,oFont08)      	//DE
oPrint:say (0405,3080,cAno,oFont08)       		//ANO

/*
⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
≥ Montagem das Linas e Colunas dos Titulos da Planilha         ≥
¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/

oPrint:Box(0460,0030,0520,3350)
oPrint:Box(0530,0030,0680,3350)

//Linhas
oPrint:line(0575,nCol*4,0575,nCol*10)  //Linhas para os TÌtulos "Horas Extras, Bonos, Dominicales"
oPrint:line(0575,(nCol*11)+20,0575,(nCol*16)-50) //Linhas para os TÌtulo "Descuentos" //14


//Colunas 1
oPrint:line(0460,nCol,0520,nCol)
oPrint:line(0460,nCol*2,0520,nCol*2)
oPrint:line(0460,nCol*6,0520,nCol*6)
oPrint:line(0460,nCol*9,0520,nCol*9)
oPrint:line(0460,nCol*11,0520,nCol*11)
oPrint:line(0460,(nCol*12)+70,0520,(nCol*12)+70)
oPrint:line(0460,nCol*13,0520,nCol*13)
oPrint:line(0460,(nCol*17)-70,0520,(nCol*17)-70)

//Colunas 2
oPrint:line(0530,nCol,0680,nCol)
oPrint:line(0530,nCol*2,0680,nCol*2)
oPrint:line(0530,nCol*3,0680,nCol*3)
oPrint:line(0530,nCol*4,0680,nCol*4)
oPrint:line(0580,nCol*5,0680,nCol*5)
oPrint:line(0530,nCol*6,0680,nCol*6)
oPrint:line(0580,nCol*7,0680,nCol*7)
oPrint:line(0530,nCol*8,0680,nCol*8)
oPrint:line(0580,nCol*9,0680,nCol*9)
oPrint:line(0530,nCol*10,0680,nCol*10)
oPrint:line(0530,(nCol*11)+20,0680,(nCol*11)+20)
oPrint:line(0580,(nCol*12)+10,0680,(nCol*12)+10)
oPrint:line(0580,(nCol*13)+10,0680,(nCol*13)+10)
//oPrint:line(0580,(nCol*14),0680,nCol*14)
//oPrint:line(0580,(nCol*15)-30,0680,(nCol*15)-30)
oPrint:line(0530,(nCol*16)-50,0680,(nCol*16)-50)
oPrint:line(0530,(nCol*17)-50,0680,(nCol*17)-50)
oPrint:line(0530,(nCol*18)-50,0680,(nCol*18)-50)


//Titulos
oPrint:say (0473,0082,STR0014,oFont08)          				//N∫
oPrint:say (0473,0240,STR0015,oFont08)          				//CI
oPrint:say (0473,0490,STR0016,oFont08)   	    				//Apellidos y Nombre
oPrint:say (0473,1190,"CATEGORIA",oFont08)	    				//Categoria do funcionario
oPrint:say (0473,1635,STR0017,oFont08)    	    				//Nacionalidad
oPrint:say (0473,1900,STR0018+' '+STR0013+' '+STR0019,oFont08)	//Fecha de Nacim
oPrint:say (0473,2150,STR0020,oFont08)   						//Sexo
oPrint:say (0473,2370,STR0021,oFont08)   						//OcupaciÛn que DesempeÒa
oPrint:say (0473,2890,STR0018+' '+STR0013+' '+STR0022,oFont08)	//Fecha de Ingreso
oPrint:say (0542,0030,STR0023,oFont08)   						//Dias H¡B.
oPrint:say (0582,0050,STR0024,oFont08)  						//Pagados
oPrint:say (0622,0075,STR0025,oFont08)   						//Mes
oPrint:say (0542,0215,STR0026,oFont08)   						//Horas/
oPrint:say (0582,0225,STR0027,oFont08)  						//Dia
oPrint:say (0622,0205,STR0028,oFont08)  						//Pagadas
oPrint:say (0542,0385,STR0029,oFont08)   	   					//Haber
oPrint:say (0582,0375,STR0030,oFont08)  						//B·sico
oPrint:say (0622,0405,STR0031,oFont08)  						//(A)
oPrint:say (0542,0550,STR0032,oFont08)   						//Bono de
oPrint:say (0582,0515,STR0033,oFont08)  	   					//AntigÎedad
oPrint:say (0622,0580,STR0034,oFont08)  						//(B)
oPrint:say (0540,0760,STR0035,oFont08)  						//Hora Extra
oPrint:say (0582,0730,STR0036,oFont08)  	   					//Numero
oPrint:say (0582,0910,STR0037,oFont08)  						//Monto
oPrint:say (0612,0900,STR0038,oFont08)  	   					//Pagado
oPrint:say (0642,0920,STR0039,oFont08)  						//(C)
oPrint:say (0540,1160,STR0078,oFont08)  						//INGRESOS
oPrint:say (0582,1065,STR0032,oFont08)  						//Bono de
oPrint:say (0612,1034,STR0041,oFont08)  						//Produccion
oPrint:say (0642,1100,STR0042,oFont08)  						//(D)
oPrint:say (0582,1250,STR0043,oFont08)  						//Otros
oPrint:say (0612,1250,STR0040,oFont08)  	   					//Bonos
oPrint:say (0642,1265,STR0044,oFont08)          				//(F)
oPrint:say (0540,1420,STR0045,oFont08)  	   					//Salario Dominical
oPrint:say (0582,1393,STR0036+' '+STR0013,oFont08)				//Numero de
oPrint:say (0612,1400,STR0046,oFont08)  						//Domingos
oPrint:say (0582,1580,STR0047,oFont08)  	   					//Salario
oPrint:say (0612,1563,STR0048,oFont08)  						//Dominical
oPrint:say (0642,1610,STR0049,oFont08)          				//(E)
oPrint:say (0540,1785,STR0050,oFont08)   	   					//Total
oPrint:say (0575,1750,STR0051,oFont08)  	   					//Ganado (G)
oPrint:say (0615,1755,STR0052,oFont08)  						//A+B+C+D+
OPrint:say (0640,1795,STR0074,oFont08)							//E+F
oPrint:say (0540,2240,STR0053,oFont08)  						//Descuentos
oPrint:say (0582,1965,STR0054,oFont08)  						//AFP
oPrint:say (0612,1940,"12.71%",oFont08)  						//12.21%
oPrint:say (0642,1960,STR0056,oFont08)          				//(H)
oPrint:say (0582,2110,STR0057,oFont08)  						//RC-IVA
oPrint:say (0612,2140,STR0058,oFont08)  						//13%
oPrint:say (0642,2140,STR0059,oFont08)          				//(I)
oPrint:say (0582,2285,"",oFont08)  	   	   						//Fondo
oPrint:say (0612,2285,"",oFont08)              					//Solid.
oPrint:say (0642,2305,""	 ,oFont08)          				//(K)
oPrint:say (0582,2440,STR0043,oFont08)   						//Otros
oPrint:say (0612,2440,STR0060,oFont08)          				//Dctos.
oPrint:say (0642,2460,"(J)"     ,oFont08)  						//(J)
oPrint:say (0582,2580,"",oFont08) 	  							//Aportes
oPrint:say (0612,2590,"",oFont08)          						//Sind.
oPrint:say (0642,2600,"",oFont08)          					    //(N)
oPrint:say (0542,2730,STR0050,oFont08)   						//Total
oPrint:say (0572,2730,STR0060,oFont08)  	   					//Dctos
oPrint:say (0607,2740,"(K)"  ,oFont08)  						//(L)
oPrint:say (0642,2730,"H+I+J",oFont08) 							//H+I+J
oPrint:say (0542,2900,"LIQUIDO",oFont08)          			   	//Liquido
oPrint:say (0572,2900,"PAGABLE",oFont08)  						//Pagable
oPrint:say (0607,2925,"(L)",oFont08)  							//(M)
oPrint:say (0642,2925,"G-K",oFont08)          					//G-L
oPrint:say (0542,3150,STR0068,oFont08)          				//Firma


//antes da inclusao da coluna Fondo Solidario
/*
oPrint:say (0582,2295,STR0043,oFont08)  	   	   				//Otros
oPrint:say (0612,2295,STR0060,oFont08)              			//Dctos.
oPrint:say (0642,2315,STR0061,oFont08)          				//(J)
oPrint:say (0542,2450,STR0050,oFont08)   						//Total
oPrint:say (0572,2450,STR0060,oFont08)          				//Dctos.
oPrint:say (0607,2470,STR0062,oFont08)  						//(K)
oPrint:say (0642,2455,STR0063,oFont08)  	   					//H+I+J
oPrint:say (0542,2620,STR0064,oFont08)   						//LÌquido
oPrint:say (0572,2625,STR0065,oFont08)  	   					//Pagable
oPrint:say (0607,2650,STR0066,oFont08)  						//(L)
oPrint:say (0642,2650,STR0067,oFont08)  						//G-K
oPrint:say (0542,2910,STR0068,oFont08)          				//Firma
*/

Return Nil

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥fImprime   ≥ Autor ≥ R.H. - Gisele Varandas≥ Data ≥03.01.08 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Imprime Dados do Funcion·rio                               ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe   ≥ fImprime(aSomaVerbas)					                  ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Parametros≥                                                            ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function fImprime(aSomaVerbas)

Local cCodSeq	:= Str(nCodSeq,6)
Local cNaciona	:= fDesc("SX5","34"+SRA->RA_NACIONA,"X5_DESCRI")
Local nCol		:= 172
Local cCargo	:= fDesc("SQ3",SRA->RA_CARGO,"Q3_DESCSUM")
Local cCateg	:= fDesc("SX5","28"+SRA->RA_CATFUNC,"X5_DESCRI")

//Impressao de Dados Linha 1
oPrint:say (nLin1,0010,cCodSeq,oFont08)   	   				// N∫
If cPaisLoc == "BOL"
	oPrint:say (nLin1,0190,SRA->RA_RG,oFont07n)    			// CI
Else
	oPrint:say (nLin1,0190,SRA->RA_CIC,oFont07n)    			// CI
Endif
oPrint:say (nLin1,0370,SRA->RA_NOME,oFont08)   			// Apellidos y Nombre
oPrint:say (nLin1,1190,cCateg,oFont08)   				// Categoria
oPrint:say (nLin1,1650,cNaciona,oFont08) 				// Nacionalidad
oPrint:say (nLin1,1930,DtoC(SRA->RA_NASC),oFont08)  	// Fecha de Nascimiento
oPrint:say (nLin1,2170,SRA->RA_SEXO,oFont08)   			// Sexo
oPrint:say (nLin1,2380,cCargo,oFont08)   	   			// Ocupacion que desempena
oPrint:say (nLin1,2920,DtoC(SRA->RA_ADMISSA),oFont08)  // Fecha de ingresso

//Impressao de Dados Linha 2
oPrint:say (nLin2,0055,Transform((nDiaPgMes - nFaltas),'@E 999.9'),oFont05)		 		//DÌas H·b. Pagados Mes
oPrint:say (nLin2,nCol+20,Transform(nVHDiaPg,'@E 999,999.99'),oFont05)	     	     		//Horas/DÌas Pagadas
oPrint:say (nLin2,(nCol*2)+20,Transform(aSomaVerbas[1,1],'@E 99,999,999.99'),oFont05)		//Haber Basico (A)
oPrint:say (nLin2,(nCol*3)+15,Transform(aSomaVerbas[2,1],'@E 99,999,999.99'),oFont05)		//Bono de Antiguidad (B)
oPrint:say (nLin2,(nCol*4)+50,Str(aSomaVerbas[3,2],3),oFont05)         			     	//N. de Horas Extra
oPrint:say (nLin2,(nCol*5)+10,Transform(aSomaVerbas[3,1],'@E 99,999,999.99'),oFont05)		//Monto Pagado H. E. (C)
oPrint:say (nLin2,(nCol*6)+10,Transform(aSomaVerbas[4,1],'@E 99,999,999.99'),oFont05)		//Bono de Produc. (D)
oPrint:say (nLin2,(nCol*7)+15,Transform(aSomaVerbas[5,1],'@E 99,999,999.99'),oFont05)		//Otros Bonos (F)
oPrint:say (nLin2,(nCol*8)+45,Str(aSomaVerbas[6,2],3),oFont05)         				 	//N. de Domingos
oPrint:say (nLin2,(nCol*9)+20,Transform(aSomaVerbas[6,1],'@E 99,999,999.99'),oFont05)   	//Salario Dominical (E)
oPrint:say (nLin2,(nCol*10)+30,Transform(aSomaVerbas[10,1],'@E 99,999,999.99'),oFont05) 	//Ganado (G) (A+B+C+E+F)
oPrint:say (nLin2,(nCol*11)+25,Transform(aSomaVerbas[7,1],'@E 99,999,999.99'),oFont05) 	//AFP (H)
oPrint:say (nLin2,(nCol*12)+15,Transform(aSomaVerbas[8,1],'@E 99,999,999.99'),oFont05) 	//RC-IVA (I)
//oPrint:say (nLin2,(nCol*13)+15,Transform(aSomaVerbas[9,1],'@E 99,999,999.99'),oFont05) 	//Fondo Solidario = SIP (K) -----------------------------------especifico
oPrint:say (nLin2,(nCol*14)-05,Transform(aSomaVerbas[9,1],'@E 99,999,999.99'),oFont05)  	//Otros Dctos (J)
//oPrint:say (nLin2,(nCol*15)-25,Transform(aSomaVerbas[14,1],'@E 99,999,999.99'),oFont05) 	//Aporte Sindicales (L)          -----------------------------------especifico
oPrint:say (nLin2,(nCol*16)-35,Transform(aSomaVerbas[11,1],'@E 99,999,999.99'),oFont05) 	//Total Dctos (K) (H+I+J)
oPrint:say (nLin2,(nCol*17)-35,Transform(aSomaVerbas[12,1],'@E 99,999,999.99'),oFont05) 	//Liquido Pagable (L) (G-K)
oPrint:say (nLin2,(nCol*18)-50," ...........................",oFont05)  	 		 	 	//Firma

//Impressao de Dados Linha 3
oPrint:Line(nLin3,0030,nLin3,3350)

nLin1:= nLin1+130
nLin2:= nLin2+130
nLin3:= nLin3+130

Return Nil

/*/
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥fRodape      ≥ Autor ≥ Equipe de RH       ≥ Data ≥03/01/08  ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥Imprime totalizadores e rodapÈ do relatorio                 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe   ≥fRodape()										     		  ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Parametros≥                                                            ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso	     ≥ Gper680 												      ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function fRodape()

Local cDataSis	:= DtoC(dDataBase)
Local nCol		:= 172

cEmpMTrab	:= OemToAnsi(STR0009) +": "+ If( fTabela("S007",1,5 ) <> Nil, fTabela("S007",1,5 ), " " )
cNomEmpr	:= If( (cNomEmpr := fTabela("S007",1,10 )) <> Nil, cNomEmpr, mv_par15 )
cNumDocId	:= If( (cNumDocId := fTabela("S007",1,11 )) <> Nil, cNumDocId, mv_par16 )

/*
⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
≥ Impressao del TOTALES				                           ≥
¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
oPrint:say (nLin1,nCol-50,STR0069,oFont08n)
oPrint:say (nLin1,(nCol*2)+10,Transform(aTotSoma[1,1],'@E 999,999,999.99'),oFont05n)     //Total Haber Basico (A)
oPrint:say (nLin1,(nCol*3)-00,Transform(aTotSoma[2,1],'@E 999,999,999.99'),oFont05n)     //Total Bono de Antiguidad (B)
oPrint:say (nLin1,(nCol*4)+50,Str(aTotSoma[3,2],3),oFont05n)         			         //Total N. de Horas Extra
oPrint:say (nLin1,(nCol*5)-00,Transform(aTotSoma[3,1],'@E 999,999,999.99'),oFont05n)     //Total Monto Pagado H. E. (C)
oPrint:say (nLin1,(nCol*6)-00,Transform(aTotSoma[4,1],'@E 999,999,999.99'),oFont05n)     //Total Bono de Produc. (D)
oPrint:say (nLin1,(nCol*7)+05,Transform(aTotSoma[5,1],'@E 999,999,999.99'),oFont05n)     //Total Otros Bonos (E)
oPrint:say (nLin1,(nCol*8)+45,Str(aTotSoma[6,2],3),oFont05n)         			   	     //Total N. de Domingos
oPrint:say (nLin1,(nCol*9)+10,Transform(aTotSoma[6,1],'@E 999,999,999.99'),oFont05n)     //Total Salario Dominical (E)
oPrint:say (nLin1,(nCol*10)+20,Transform(aTotSoma[10,1],'@E 999,999,999.99'),oFont05n)   //Total Ganado (G) (A+B+C+E+F)
oPrint:say (nLin1,(nCol*11)+20,Transform(aTotSoma[7,1],'@E 999,999,999.99'),oFont05n)    //Total AFP (H)
oPrint:say (nLin1,(nCol*12)+05,Transform(aTotSoma[8,1],'@E 999,999,999.99'),oFont05n)    //Total RC-IVA (I)
//oPrint:say (nLin1,(nCol*13)+05,Transform(aTotSoma[9,1],'@E 999,999,999.99'),oFont05n)   //Total Fondo Solidario (K) ----------- especÌfico (retirado em 2013/11)
oPrint:say (nLin1,(nCol*14)-10,Transform(aTotSoma[9,1],'@E 999,999,999.99'),oFont05n)    //Total Otros Dctos (J)
//oPrint:say (nLin1,(nCol*15)-35,Transform(aTotSoma[14,1],'@E 999,999,999.99'),oFont05n)   //Total Sindicato (L)       ----------- especÌfico (retirado em 2013/11)
oPrint:say (nLin1,(nCol*16)-45,Transform(aTotSoma[11,1],'@E 999,999,999.99'),oFont05n)   //Total Total Dctos (K) (H+I+J)
oPrint:say (nLin1,(nCol*17)-40,Transform(aTotSoma[12,1],'@E 999,999,999.99'),oFont05n)   //Total Liquido Pagable (L) (G-K)

/*
⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
≥ Impressao do Rodape do Relatorio                             ≥
¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/

oPrint:line(nLin1+230,0200,nLin1+230,1000)
oPrint:line(nLin1+230,1210,nLin1+230,1900)
oPrint:line(nLin1+230,2100,nLin1+230,2550)

oPrint:say (nLin1+190,0270,cNomEmpr,oFont10)		//Nombre Del Empleador o Representante Legal
oPrint:say (nLin1+240,0245,STR0071,oFont08)
oPrint:say (nLin1+190,1400,cNumDocId,oFont10)       //N∫ de Documento de Identidad
oPrint:say (nLin1+240,1320,STR0070,oFont08)
oPrint:say (nLin1+240,2275,STR0068,oFont08)         //Firma

oPrint:say (nLin1+200,2800,STR0018,oFont08)  		//Fecha:
oPrint:say (nLin1+200,2900,cDataSis,oFont08)  		//Fecha el Sistema

Return Nil

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥fSomaInfSal≥ Autor ≥ R.H. - Gisele Varandas≥ Data ≥02.01.08 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Somar as Verbas no Array                                   ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe   ≥ fSomaInfSal(aSomaVerbas,aVerbasFunc,nReg)                  ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Parametros≥                                                            ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function fSomaInfSal(	aSomaVerbas	,;	// @Array com as opcoes a serem gravadas a totalizacao
								aVerbasFunc	,;	// Array com as informacoes a serem processadas
								nReg		 ;	// Posicao do array acima para busca das informacoes.
					  		)

DEFAULT aVerbasFunc := {}

//Buscar da aVerbasFunc o valor das Horas (RC_HORAS) da verba com ID 0031
If  PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_CODFOL") == "0031"
	nHoras := aVerbasFunc[nReg, 6]
EndIf
//Buscar da aVerbasFunc o valor das Horas (RC_HORAS) da verba com ID 0032 - Horistas
If  PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_CODFOL") == "0032"
	nHoras := aVerbasFunc[nReg, 6]
EndIf


//Buscar da aVerbasFunc a qtde de faltas/ domingos da verba com ID 0054
If  PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_CODFOL") == "0054"  .or. PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_CODFOL") == "1237"
	nfaltas += aVerbasFunc[nReg, 6]
EndIf

/*
⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
≥ Geracao do array com as totalizacoes                         ≥
¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ*/
Do Case
	Case PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_INFSAL") == "A"
		aSomaVerbas[1,1] += aVerbasFunc[nReg,7]
	Case PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_INFSAL") == "B"
   		aSomaVerbas[2,1] += aVerbasFunc[nReg,7]
	Case PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_INFSAL") == "C"
   		aSomaVerbas[3,1] += aVerbasFunc[nReg,7]
   		aSomaVerbas[3,2] += aVerbasFunc[nReg,6]
	Case PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_INFSAL") == "D"
	   	aSomaVerbas[4,1] += aVerbasFunc[nReg,7]
	Case PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_INFSAL") == "F"
   		aSomaVerbas[5,1] += aVerbasFunc[nReg,7]
	Case PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_INFSAL") == "E"
	   	aSomaVerbas[6,1] += aVerbasFunc[nReg,7]
   		aSomaVerbas[6,2] += aVerbasFunc[nReg,6]
	Case PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_INFSAL") == "H"
   		aSomaVerbas[7,1] += aVerbasFunc[nReg,7]
	Case PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_INFSAL") == "I"
   		aSomaVerbas[8,1] += aVerbasFunc[nReg,7]
	Case PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_INFSAL") == "J"
   		aSomaVerbas[9,1] += aVerbasFunc[nReg,7]
   	Case PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_INFSAL") == "K"     // incluida essa opcao no cadastro de verbas para o FONDO SOLIDARIO
   		aSomaVerbas[13,1] += aVerbasFunc[nReg,7]
	Case PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_INFSAL") == "L"     // incluida essa opcao no cadastro de verbas para o SINDICATO (Nao devera ser mais utilizada)
   		aSomaVerbas[14,1] += aVerbasFunc[nReg,7]
   	Case PosSrv(aVerbasFunc[nReg, 3],SRA->RA_FILIAL,"RV_INFSAL") == "N"     // incluida essa opcao no cadastro de verbas para o SINDICATO
   		aSomaVerbas[14,1] += aVerbasFunc[nReg,7]
EndCase

Return Nil