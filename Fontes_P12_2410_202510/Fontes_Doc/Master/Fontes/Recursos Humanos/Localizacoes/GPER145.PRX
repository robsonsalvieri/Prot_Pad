#INCLUDE "PROTHEUS.CH"
#Include "GPER145.CH"

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    Ё GPER145  Ё Autor Ё Ricardo Berti         Ё Data Ё 20/05/13 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Impressao do Quinquenio  - Bolivia	                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё GPER145()                                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Especifico                                                 Ё╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.             Ё╠╠
╠╠цддддддддддддбддддддддбддддддддддддддддбдддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁAnalista    Ё Data   Ё Chamado		 Ё  Motivo da Alteracao           Ё╠╠
╠╠цддддддддддддеддддддддеддддддддддддддддедддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё			   Ё		Ё     			 Ё                                Ё╠╠
╠╠юддддддддддддаддддддддаддддддддддддддддадддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function GPER145()

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define Variaveis Locais  		                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local cString	:= "SRA"           			// Alias do arquivo principal (Base)
Local aOrd		:= {STR0001,STR0010,STR0002}//" Matricula "###" Nome "###" Centro de Costo "
Local cDesc1	:= STR0003					//"ImpressЦo do Pagamento de IndenizaГЦo por tempo de ServiГo - QuinquЙnio"
Local cDesc2	:= STR0004					//"SerА impresso de acordo com os parametros solicitados pelo"
Local cDesc3	:= STR0005					//"usuАrio."  
Local wnReL

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define Variaveis Private		                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Private aReturn	  := {STR0006, 1,STR0007, 1, 2, 1, "",1 }	//"Zebrado"###"Administra┤└o"
Private cPerg	  := "GPER145"
Private nomeprog  := "GPER145"
Private nLastKey  := 0
Private nPagTipo  := 0
Private cNCheque  := ""
Private cNomeBanco:= ""
Private oPrint		//Objeto p/ Impresssao Grafica 
   
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis UtiLizadas na funcao IMPR                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Private Titulo  := STR0008		//"EMISS▌O DOCUMENTO DE QUINQUENIO"
Private AT_PRG  := nomeprog
Private wCabec0 := 3
Private wCabec1 := ""
Private wCabec2 := ""
Private wCabec3 := ""
Private Contfl  := 1
Private nTamanho:="M"  

/*зддддддддддддддддддддддддддддддддддддддддддддддддддд©  
  Ё Checa atualiz.obrigatoria e informa ao usuario	  Ё
  юддддддддддддддддддддддддддддддддддддддддддддддддддды*/
If !CpoUsado("RC_MESMED") .Or. !CpoUsado("RD_MESMED") .Or. !CpoUsado( "RC_DSOLQUI" ) .Or. !CpoUsado( "RD_DSOLQUI" )
	//Retorna as informacoes sobre a atualizacao que precisa ser executada pelo RHUPDMOD
	cMsg := fRhUpdMsg( 217 )
	MsgAlert( cMsg )
	Return NIL
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica as perguntas selecionadas                           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Pergunte(cPerg,.F.)
   
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Envia controle para a funcao SETPRINT                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
wnrel:="GPER145"            //Nome Default do relatorio em Disco
wnrel:=SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,nTamanho)
If nLastKey = 27
	Return
Endif
	
SetDefault(aReturn,cString)

If nLastKey = 27
	Return
Endif                             
      
oPrint 	:= TMSPrinter():New( STR0009 )		//-- Objeto para impressao grafica    // "Recibo de Quinquenio"
oPrint:SetPortrait()               
   
RptStatus({|lEnd| GP145Imp(@lEnd,wnRel,cString)},Titulo)

oPrint:Preview()  		// Visualiza impressao grafica antes de imprimir

Return


/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё GP145imp Ё Autor Ё Microsiga             Ё Data Ё 20.05.13 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Recibo de Quinquenio                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё GP145IMP(lEnd,wnRel,cString)                           	  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Static Function GP145IMP(lEnd,WnRel,cString)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define Variaveis Locais                                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local cVerCodFol
Local cFilMat
Local nImprVias
Local nx

Local cAliasQry := GetNextAlias()
Local cFilPd	:= If( Alltrim(FWModeAccess("SRV",3)) == "C", "%'" + xFilial("SRV") + "'%", "%SRA.RA_FILIAL%" )
Local cFiliAnt	:= "__"
Local cAuxPrc	:= ""
Local cSitQuery	:= ""
Local cCatQuery	:= ""
Local cProcQuery:= ""
Local cOrdem

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis utilizadas para parametros - GPER145               Ё
//Ё mv_par01  01    //  Selecao de Processos				 	 Ё
//Ё mv_par02  02    //  Mes/Ano Competencia ? 					 Ё
//Ё mv_par03  03    //  FiLial De                                Ё
//Ё mv_par04  04    //  FiLial Ate                               Ё
//Ё mv_par05  05    //  Centro De Custo De                       Ё
//Ё mv_par06  06    //  Centro De Custo Ate                      Ё
//Ё mv_par07  07    //  Matricula De                             Ё
//Ё mv_par08  08    //  Matricula Ate                            Ё
//Ё mv_par09  09    //  Nome De 		                         Ё
//Ё mv_par10  10    //  Nome Ate        		                 Ё
//Ё mv_par11  11    //  Situacao 		                         Ё
//Ё mv_par12  12    //  Categoria		                         Ё
//Ё mv_par13  15    //  N╖ de Vias                               Ё
//Ё mv_par14  16    //  Forma de Pagto.                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local cProcessos:= mv_par01
Local cPeriodo	:= SubStr(mv_par02,3,4) + SubStr(mv_par02,1,2) // AAAAMM
Local cFilDe	:= mv_par03
Local cFilAte	:= mv_par04
Local cCCDe		:= mv_par05
Local cCCAte	:= mv_par06
Local cMatDe	:= mv_par07
Local cMatAte	:= mv_par08
Local cNomDe	:= mv_par09
Local cNomAte	:= mv_par10
Local cSituacao	:= mv_par11
Local cCategoria:= mv_par12
Local nNumVias	:= Max(mv_par13,1)
Local nOrdem	:= aReturn[8]
Local nTamCod	:= GetSx3Cache( "RCJ_CODIGO" , "X3_TAMANHO" )

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis de Acesso do Usuario                               Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Local cAcessaSRA	:= &( " { || " + ChkRH( "GPER145" , "SRA" , "2" ) + " } " )
Local cAcessaSRC	:= &( " { || " + ChkRH( "GPER145" , "SRC" , "2" ) + " } " )
Local nHoras
Local cRoteir  		:= "QUI"  // fGetRotTipo("B") - QUINQUENIO

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define Variaveis Privadas                                    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Private cCausa  	:= 	"1"
Private nSalario  	:= 0
Private aBol		:= {}         //Matriz com campos especificos para o Finiquito da Bolivia
Private aCodFol 	:= {}         // Matriz com Codigo da folha
Private aInfo   	:= {}         // Matriz com dados da empresa
Private dDtPagoQui	:= CtoD('')

nPagTipo := mv_par14  // 1-Dinh  2-Cheque

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define Ordem Do Relatorio                                    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If nOrdem == 1
	cOrdem := "%RA_FILIAL,RA_MAT%"
ElseIf nOrdem == 2
	cOrdem := "%RA_FILIAL,RA_NOME,RA_MAT%"
ElseIf nOrdem == 3
	cOrdem := "%RA_FILIAL,RA_CC,RA_MAT%"    
EndIf

If nPagTipo == 2
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Variaveis utilizadas para parametros - GPER143               Ё
	//Ё mv_par01  01    //  No.do cheque                             Ё
	//Ё mv_par02  02    //  Nome do Banco                            Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	Pergunte("GPER143", .T.)
	cNCheque    := mv_par01
	cNomeBanco  := mv_par02
EndIf

// Processos
For nX := 1 to Len(cProcessos) step 5
	If !Empty(Subs(cProcessos,nX,5))
		If Len(Subs(cProcessos,nX,5)) < nTamCod
			cAuxPrc := Subs(cProcessos,nX,5) + Space(nTamCod - Len(Subs(cProcessos,nX,5)))
		Else
			cAuxPrc := Subs(cProcessos,nX,5)
		EndIf
		cProcQuery += "'" + cAuxPrc + "',"
	EndIf
Next nX
cProcQuery := "%" + Substr( cProcQuery, 1, Len(cProcQuery)-1) + "%"

cSitQuery	:= ""
cSituacao := Trim(cSituacao)
For nX:=1 to Len(cSituacao)
	cSitQuery += "'"+Subs(cSituacao,nX,1)+"'"
	If ( nX+1 ) <= Len(cSituacao)
		cSitQuery += "," 
	Endif
Next
cSitQuery := "%" + cSitQuery + "%"

cCatQuery	:= ""
cCategoria := Trim(cCategoria)
For nX:=1 to Len(cCategoria)
	cCatQuery += "'"+Subs(cCategoria,nX,1)+"'"
	If ( nX+1 ) <= Len(cCategoria)
		cCatQuery += "," 
	Endif
Next
cCatQuery := "%" + cCatQuery + "%"
/*
здддддддддддддддддддддддддддддддддддддддддд©
Ё Processa dados para a impressao 		   Ё
юдддддддддддддддддддддддддддддддддддддддддды*/

BeginSql alias cAliasQry
		COLUMN DATAPG as Date

		SELECT 	SRA.RA_FILIAL,			SRA.RA_MAT,				SRA.RA_CC,				SRA.R_E_C_N_O_ REGSRA,
				SRC.RC_FILIAL FILIAL,	SRC.RC_MAT MAT,			SRC.RC_CC CCUSTO,		SRC.RC_PD PD,
				SRC.RC_VALOR VALOR,		SRC.RC_SEQ SEQ,			SRC.RC_PROCES PROCES, 	SRC.RC_MESMED MESMED,
				SRC.RC_PERIODO PERIODO,	SRC.RC_NUMID NUMID,		SRC.RC_ROTEIR ROTEIR,	
				SRC.RC_TIPO1 TIPO1,		SRC.RC_HORAS HORAS,	    SRC.RC_DATA DATAPG,
				SRV.RV_COD,				SRV.RV_CODFOL,			SRV.RV_DESC
		FROM %table:SRA% SRA

		INNER JOIN %table:SRC% SRC 
		ON 	    SRA.RA_FILIAL = SRC.RC_FILIAL	AND
				SRA.RA_MAT    = SRC.RC_MAT		AND
				SRA.RA_CC     = SRC.RC_CC						

		INNER JOIN %table:SRV% SRV
		ON		SRV.RV_FILIAL = %exp:cFilPd%	AND
				SRV.RV_COD    = SRC.RC_PD

		WHERE	SRA.RA_FILIAL BETWEEN %exp:cFilDe% AND %exp:cFilAte% AND
				SRA.RA_MAT    BETWEEN %exp:cMatDe% AND %exp:cMatAte% AND
				SRA.RA_CC     BETWEEN %exp:cCcDe%  AND %exp:cCcAte%  AND
				SRA.RA_NOME   BETWEEN %exp:cNomDe% AND %exp:cNomAte% AND
				SRA.RA_SITFOLH	IN	(%exp:Upper(cSitQuery)%)	AND
		   		SRA.RA_CATFUNC	IN	(%exp:Upper(cCatQuery)%)	AND
		   		SRC.RC_PROCES  IN (%exp:Upper(cProcQuery)%)  AND
		   		SRC.RC_ROTEIR = %exp:cRoteir% AND
		   		SRC.RC_PERIODO = %exp:Upper(cPeriodo)%   AND  
				SRA.%notDel% AND SRC.%notDel% AND SRV.%notDel%
		UNION

		(SELECT SRA.RA_FILIAL,			SRA.RA_MAT,				SRA.RA_CC,				SRA.R_E_C_N_O_ REGSRA,
				SRD.RD_FILIAL FILIAL,	SRD.RD_MAT MAT,			SRD.RD_CC CCUSTO,		SRD.RD_PD PD,
				SRD.RD_VALOR VALOR,		SRD.RD_SEQ SEQ,			SRD.RD_PROCES PROCES, 	SRD.RD_MESMED MESMED,
				SRD.RD_PERIODO PERIODO,	SRD.RD_NUMID NUMID,		SRD.RD_ROTEIR ROTEIR,	
				SRD.RD_TIPO1 TIPO1,		SRD.RD_HORAS HORAS,	    SRD.RD_DATPGT DATAPG,
				SRV.RV_COD,				SRV.RV_CODFOL,			SRV.RV_DESC
		FROM %table:SRA% SRA

		INNER JOIN %table:SRD% SRD
		ON 	    SRA.RA_FILIAL = SRD.RD_FILIAL	AND
				SRA.RA_MAT    = SRD.RD_MAT		AND
				SRA.RA_CC     = SRD.RD_CC

		INNER JOIN %table:SRV% SRV
		ON		SRV.RV_FILIAL 	= %exp:cFilPd%	AND
				SRV.RV_COD    	= SRD.RD_PD

		WHERE	SRA.RA_FILIAL BETWEEN %exp:cFilDe% AND %exp:cFilAte% AND
				SRA.RA_MAT    BETWEEN %exp:cMatDe% AND %exp:cMatAte% AND
				SRA.RA_CC     BETWEEN %exp:cCcDe%  AND %exp:cCcAte%  AND
				SRA.RA_NOME   BETWEEN %exp:cNomDe% AND %exp:cNomAte% AND
				SRA.RA_SITFOLH	IN	(%exp:Upper(cSitQuery)%)	AND
		   		SRA.RA_CATFUNC	IN	(%exp:Upper(cCatQuery)%)	AND
		   		SRD.RD_PROCES  IN (%exp:Upper(cProcQuery)%)  AND
		   		SRD.RD_ROTEIR = %exp:cRoteir% AND
		   		SRD.RD_PERIODO = %exp:Upper(cPeriodo)%   AND  
				SRA.%notDel% AND SRD.%notDel% AND SRV.%notDel%)
		ORDER BY %exp:cOrdem%

EndSql

Count TO nX               
SetRegua(nX) // No. de registros a procesar
dbGoTop()
SetPrc(0,0)

Do While (cAliasQry)->(!Eof())

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Movimenta Regua de Processamento                             Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	IncRegua()
      
	If lEnd
		@Prow()+1,0 PSAY cCancel
		Exit
    Endif

	cFilMat := ( (cAliasQry)->RA_FILIAL + (cAliasQry)->RA_MAT )

	/*
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁConsiste Filiais e Acessos                                             Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF !( (cAliasQry)->RA_FILIAL $ fValidFil() ) .or. !Eval( cAcessaSRA ) .Or. !Eval( cAcessaSRC )
      	(cAliasQry)->( dbSkip() )
       	Loop
	EndIf

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Carrega Variaveis Codigos Da Folha                           Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	If ( cFiliAnt # (cAliasQry)->RA_FILIAL )
		cFiliAnt := (cAliasQry)->RA_FILIAL
		If !fP_CodFol(@aCodFol,cFiliAnt)
			Return
		EndIf
	EndIf

	cVerCodFol:=  aCodFol[754,1]+"*"+ ;	// Promedio 1 Mes Indemnizacion
			      aCodFol[755,1]+"*"+ ;		// Promedio 2 Mes Indemnizacion
			      aCodFol[756,1]+"*"+ ;		// Promedio 3 Mes Indemnizacion
			      aCodFol[760,1]+"*"+ ;		// Promedio 4 Mes Indemnizacion 
			      aCodFol[757,1]+"*"+ ;		// Indemnizacion Ano
			      aCodFol[758,1]+"*"+ ;		// Indemnizacion Mes
			      aCodFol[759,1]+"*"+ ;		// Indemnizacion Dia
			      aCodFol[761,1]+"*"+ ;		// Promedio Indemnizacion
			      aCodFol[768,1]+"*"+ ;		// Deahucio
			      aCodFol[084,1]+"*"+ ; 	// Otros
	              aCodFol[1274,1]+"*"+ ;	// Valor Quinquenio
	              aCodFol[1275,1]	    	// Sal. Quinquenio		                         

	dDtPagoQui := (cAliasQry)->DATAPG  // Fecha Retiro Quinquenio

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Posiciona func.p/uso na fBuscaSal() - Obtem salario na data do Quinquenio  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SRA")
	SRA->(dbSetOrder(1))
	SRA->(dbSeek((cAliasQry)->RA_FILIAL+(cAliasQry)->RA_MAT))
	nSalario := fBuscaSal(dDtPagoQui,,,.F.)
	If nSalario == 0
		nSalario := SRA->RA_SALARIO
	EndIf
	//fBuscaFunc(dDtPagoQui,@cCodFunc, @cDescFunc ) 		//Funcao do Funcion. na Dt Referencia
		
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Carrega Matriz Com Dados da Empresa                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	fInfo(@aInfo,SRA->RA_FILIAL)
		
	While (cAliasQry)->( !Eof() .And. cFilMat == RA_FILIAL + RA_MAT )

		If ( (cAliasQry)->VALOR == 0.00 )
			(cAliasQry)->( dbSkip() )
			Loop
		EndIf
		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Carrega array de campos especificos para impressao  Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		If (cAliasQry)->PD $ cVerCodFol 
			nHoras	:= If ((cAliasQry)->TIPO1=="H",fConvHoras((cAliasQry)->HORAS,"1"),(cAliasQry)->HORAS)
			Aadd(aBol,{(cAliasQry)->RV_CODFOL,(cAliasQry)->RV_DESC,Str(nHoras,6,2),(cAliasQry)->VALOR,(cAliasQry)->MESMED})
		EndIf	

		(cAliasQry)->( dbSkip() )
	EndDo	                              

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Imprime o Numero de Vias Especificado                        Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	For nImprVias := 1 to nNumVias
		
		ExecBlock('IMPQUIBOL',.F.,.F.)	

	Next
		
EndDo
   
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Termino do relatorio                                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SRA")
Set Filter to 
dbSetOrder(1)

SeTPgEject(.t.)

Set Device To Screen
   
MS_FLUSH()
Return
