#INCLUDE "TOTVS.CH" 
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "Protheus.ch"
#INCLUDE "GPEM560.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPEM560  ³ Autor ³ J. Ricardo                         ³ Data ³ 20/06/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Manutencao do Arquivo da DIRF                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPEM560()                                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ Requisito         ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Bruno Nunes ³20/01/14³ M12RH01_001943_01 ³ Unificacao da DIRF da versao 11 com      ³±±
±±³            ³        ³                   ³ a fase 4                                 ³±±
±±³Christiane V³29/04/14³        TPJZJ8     ³ Replicas P11 para P12                    ³±±
±±³Sidney O.   ³26/05/14³        315506     ³ Replicas P11 para P12                    ³±±
±±³Mariana M.  ³28/08/14³        TQHTF5     ³ Ajuste na tela de Alt. Inform. Complemen.³±±  
±±³M. Silveira ³28/11/14³        TQUWKL     ³Ajuste p/ que a alteracao de dados gerados³±±
±±³            ³        ³                   ³pelo FIN conservem a indicacao da Origem. ³±±
±±³Wag Mobile  ³14/01/15³ TRKAQ7            ³Ajuste em m560AltInf() para permitir que  ³±±
±±³            ³        ³                   ³seja alterada a descricao da verba nas    ³±±
±±³            ³        ³                   ³informacoes complementares                ³±±
±±³M. Silveira ³31/07/15³        TRZOLH     ³Ajuste p/ que a data de pagto exibida no  ³±±
±±³            ³        ³                   ³mes 12 considere a maior data de pagamento³±±
±±³Claudinei S.³08/11/16³        TWLEW6     ³Ajuste em m560AltInf() e mPoeDesc() para  ³±±
±±³            ³        ³                   ³permitir a gravação da descrição das      ³±±
±±³            ³        ³                   ³informacoes complementares com até 150    ³±±
±±³            ³        ³                   ³caracteres.                               ³±±
±±³Claudinei S.³30/12/16³           MRH-3728³Ajuste na mascara da verba em m560AltInf()³±±
±³Jônatas A.   ³05/01/17³     MRH-4752      ³Compatibilização fontes DIRF P118->P12114 ³±±
±³             ³        ³                   ³Incluida nova tela de Pensao para DIRF    ³±±
±±³            ³        ³                   ³2016 em diante e retirada a informação de ³±±
±±³            ³        ³                   ³pensao da tela principal da DIRF, RRA e   ³±±
±±³            ³        ³                   ³informações complementares                ³±±
±±³Renan Borges³19/01/17³     MRH-1206      ³Ajuste para ser possível lançar em um re- ³±±
±±³            ³        ³                   ³gistro de origem do financeiro, informaçõ-³±±
±±³            ³        ³                   ³es referentes ao financeiro e não ao ges- ³±±
±±³            ³        ³                   ³tão de folha, pois a geração da dirf da fo³±±
±±³            ³        ³                   ³lha sempre recalcula os valores da folha. ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPEM560

	Local cFiltraSRL	:= ''				//Variavel para filtro
	Local aIndexSRL		:= {}				//Variavel Para Filtro
	Local cFilSR4		:= ""

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Variaveis                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private bFiltraBrw 	:= {|| Nil}		//Variavel para Filtro
	Private nVez 		:= 1
	Private aColsComp 	:= {}
	Private aHeaderComp	:= {}
	Private aColsRec	:= {}
	Private cAno		:= ''
	Private lLoadMsg  	:= .T.
	Private aSM0 		:= FWLoadSM0()

	M->RL_TIPOFJ := " "

	cCadastro := OeMtoAnsi(STR0001) //"Manuten‡„o da D.I.R.F."

	Private aRotina := MenuDef() 	// ajuste para versao 9.12 - chamada da funcao MenuDef() que contem aRotina

	//Comunicado sobre a não disponibilização da Rais e Dirf para releases inferiores à 12.1.25
	If GetRpoRelease() < "12.1.023" .AND. FindFunction("DirfRaisMsg") 
		DirfRaisMsg()
	EndIf	

	dbSelectArea("RCS")  
	If RCS->( ColumnPos("RCS_NOMED"))== 0 
		Aviso(STR0079,STR0089,{"OK"})		//"Atencao"#"Tabelas desatualizadas. Execute o atualizador."
		Return
	EndIf

	While Empty(cAno) 
		If Pergunte("GPM560", .T.) 
			If Empty(MV_PAR01) 
				Help(,, "Help", NIL, "Ano calendário não informado", 1, 0,,,,,, {"Informe um ano para a busca das informações."})
			Else 
				cAno := Strzero(MV_PAR01, 4)
			EndIf
		Else
			Return()
		EndIf
	EndDo

	cCadastro	:= OeMtoAnsi(STR0001)  +STR0080 +cAno //"Manuten‡„o da D.I.R.F."#" - Ano Calendario "

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Endereca a funcao de BROWSE                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SRL")
	SRL->(dbSetOrder(1))
	SRL->(dbGoTop())

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Carregando Filtro de BROWSE                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
	cFiltraSRL	:= ChkRh("GPEM560", "SRL", "1")

	If AllTrim(Upper(TCGetDB())) $ "DB2|ORACLE|POSTGRES|INFORMIX"
		cFilSR4 := " RL_FILIAL||RL_MAT||RL_CPFCGC IN (SELECT R4_FILIAL||R4_MAT||R4_CPFCGC"
		cFilSR4 += " FROM " + RetSqlName("SR4")
		cFilSR4 += " WHERE R4_FILIAL = RL_FILIAL AND R4_MAT = RL_MAT AND R4_CPFCGC = RL_CPFCGC AND R4_ANO = '" + cAno + "')"
	Else
		cFilSR4 := " RL_FILIAL + RL_MAT + RL_CPFCGC IN (SELECT R4_FILIAL + R4_MAT + R4_CPFCGC"
		cFilSR4 += " FROM " + RetSqlName("SR4")
		cFilSR4 += " WHERE R4_FILIAL = RL_FILIAL AND R4_MAT = RL_MAT AND R4_CPFCGC = RL_CPFCGC AND R4_ANO = '" + cAno + "')"
	EndIf

	bFiltraBrw	:= {|| FilBrowse("SRL", @aIndexSRL, @cFiltraSRL)}
	Eval( bFiltraBrw ) 
	mBrowse(6, 1, 22, 75, "SRL",,,,,,,,,,,,,, Iif(Empty(cFilSR4),, cFilSR4))

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Deleta o filtro utilizando a funcao FilBrowse                     	   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	EndFilBrw("SRL", aIndexSrl)

	SRL->(DbCloseArea())
	RCS->(DbCloseArea())

Return()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp560Man  ³ Autor ³ J. Ricardo            ³ Data ³ 20.06.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Visualizacao,Inclusao,Alteracao do Arquivo da  ³±±
±±³          ³ DIRF                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ gp560Vis(ExpC1,ExpN1,ExpN2)                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ gpeM560                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gp560MAN(cAlias,nReg,nOpc)

Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aButton		:= {}
Local bSet15		:= { || NIL }
Local bSet24		:= { || NIL }
Local nOpca			:= 0.00
Local oDlg
Local oGet			
Local aArea			:= GetArea()
Local nDel			:= 0.00
Local cX3Campo
Local cMCampo
Local aColsAnt		:= {}
Local nOpGD			:= 0

Private nUsado		:= 0
Private aCols 		:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Matriz de Cabecalho para Enchoice³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aM560Obrig 	:= {}
Private aM560Nao   	:= {"RL_FILIAL"}
Private aM560Dif   	:= {"RL_MAT","RL_CODRET","RL_CPFCGC","RL_TIPOFJ", "RL_NIFEX", "RL_PAIS"}
Private lInfoFin	:= .F.

cAlias := "SRL" 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do Arquivo                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea( "SX3" )
DbSetOrder( 1 )
DbSeek( "SRL" )
If nOpc == 3
	aAdd(aM560Nao,"RL_ORIGEM")
	M->RL_ORIGEM := "1"
	While SX3->(!Eof()) .and. (SX3->X3_ARQUIVO == "SRL")
		cX3Campo := "SRL->"+(SX3->X3_CAMPO)
		cMCampo  := "M->"+(SX3->X3_CAMPO)
		If Ascan(aM560Nao,ALLTRIM(Upper(SX3->X3_CAMPO))) = 0
			If Type(cX3Campo) = "D"
				&(cMCampo) := CtoD("  /  /  ")
			ElseIf Type(cX3Campo) = "C"
				&(cMCampo) := SPACE(LEN(&(cX3Campo)))
			ElseIf Type(cX3Campo) = "N"
				&(cMCampo) := 0
			ElseIf Type(cX3Campo) = "L"
				&(cMCampo) := .F.
			Endif
			AADD(aM560Obrig,SX3->X3_CAMPO)
		Endif
		SX3->( dbSkip())
	Enddo
ElseIf nOpc == 4
	aAdd(aM560Dif,"RL_ORIGEM")
	While SX3->(!Eof()) .and. (SX3->X3_ARQUIVO == "SRL")
		If Ascan(aM560Nao,ALLTRIM(Upper(SX3->X3_CAMPO))) == 0
			cX3Campo := "SRL->"+(SX3->X3_CAMPO)
			cMCampo  := "M->"+(SX3->X3_CAMPO)
			If SX3->X3_CONTEXT <> "V"
				&cMCampo := &(cX3Campo)
				If &cMCampo = Nil
					If Type(cX3Campo) = "D"
						&(cMCampo) := CtoD("  /  /  ")
					ElseIf Type(cX3Campo) = "C"
						&(cMCampo) := SPACE(LEN(SRL->&(SX3->X3_CAMPO)))
					ElseIf Type(cX3Campo) = "N"
						&(cMCampo) := 0
					ElseIf Type(cX3Campo) = "L"
						&(cMCampo) := .F.
					Endif
				Endif
			EndIf
			If Ascan(aM560Dif,ALLTRIM(Upper(SX3->X3_CAMPO))) = 0
				AADD(aM560Obrig,x3_campo)
			Endif
		Endif
		SX3->( dbSkip())
	Enddo
EndIf

Private aTELA[0][0],aGETS[0],aHeader[0],Continua:=.F.

nUsado := If( cAno >= '2007', If( cAno >= '2016', If( cAno >= '2023', 43, 40 ), 48 ), 31 )

aAlter := {	"M550RBT", "M550CPR", "M550PRI", "M550PEJ", "M550DEP", "M550IMP", "M550SAF", "M550PPA", "M550DAJ", "M550PRP",;
			"M550LUC", "M550VTS", "M550ONT", "M55013S", "M550D13", "M55013I", "M550ORE", "M550QRE", "M550IFC", "M550COM",;
            "M550EXI", "M550DEX", "M550REX", "M550JUD", "M55013J", "M55013E", "M55013D", "M55013R", "M55013T", "M550EXP",;
            "M550EXD", "M550EPA", "M550EPP", "M550131", "M550132", "M550133", "M550134", "M55013A", "M55013B", "M55013C",;
            "M55091" , "M550APE", "M550S"  , "M550S1" , "M550H1" , "M550F1" , "M550DTP", "M550PLRE","M550PLPE","M550PLIR",;
            "M550IMU", "M550ISE","M550SAFE1", "M550IRSMP", "M550IRSMP13", "M550IRSMSU"}

nUsado := 0
GeraManu( cAlias, @aHeader, @aCols, @nUsado, nOpc )

aColsAnt := Aclone(aCols)

if nOpc == 2			//visualizar
	nOpGD := 0
elseif nOpc == 3		//incluir
	nOpGD := GD_UPDATE
elseif nOpc == 4		//alterar
	If SRL->RL_ORIGEM == '2'
		lInfoFin := MsgNoYes( OemToAnsi(STR0103) )
	EndIf
	nOpGD := GD_UPDATE
elseif nOpc == 5		//alt.inf.compl
	nOpGD := GD_UPDATE
elseif nOpc == 6		//excluir
	nOpGD := GD_DELETE
endIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta as Dimensoes dos Objetos         					     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aAdvSize		:= MsAdvSize()
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 3 , 3 }					 
aAdd( aObjCoords , { 000 , 040 , .T. , .T. } )
aAdd( aObjCoords , { 000 , 060 , .T. , .T. } )
aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords ,.T.)
cCadastro		:= OeMtoAnsi(STR0001) + STR0080 + cAno //"Manuten‡„o da D.I.R.F."#" - Ano Calendario "

DEFINE MSDIALOG oDlg TITLE cCadastro From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL 
	
	nOpca := EnChoice( cAlias , nReg , nOpc , NIL , NIL , NIL , NIL , aObjSize[1] ,If(nOpc==2.or. nopc==6,NIL,aM560Obrig),3 )

	dbSelectArea("SR4")
  	oGet	:=MsNewGetDados():New(aObjSize[2,1],aObjSize[2,2],aObjSize[2,3]-10,aObjSize[2,4],nOpGD,"gp560LinOk","gp560LinOk","",aAlter, 1, 12 ,NIL,NIL,"gp560DelOk",oDlg,aHeader,aCols)
	bSet15	:= {||nOpca:=1,If(If(nOpc==3,Obrigatorio(aGets,aTela),.T.).and.oGet:TudoOk(),oDlg:End(),nOpca:=0)}
	bSet24	:= {||oDlg:End()}
	aButton := {}
	aAdd(aButton,{"NOTE",{|| gpm560Benef(nOpc)},STR0002})//Informacoes Complementares
	aAdd(aButton,{"NOTE",{|| gpm560RRA(nOpc)},"RRA"})//RRA
	
	If (nOpc == 4 .Or. nOpc == 2 ) .And. cAno >= "2016" .And. SRQ->(dbSeek(SRL->RL_FILIAL+SRL->RL_MAT))
		aAdd(aButton,{"NOTE",{|| GPm561(nOpc)},STR0104}) //Pensao 
	EndIf
	
	If (nOpc == 4 .Or. nOpc == 2 ) .And. cAno >= "2016" .And. SMU->(dbSeek(SRL->RL_FILIAL+SRL->RL_MAT))
		aAdd(aButton,{"NOTE",{|| GPm562(nOpc)},STR0110}) //Previdência Complementar 
	EndIf
	
	If nOpc == 3 .AND. lLoadMsg
		lLoadMsg := .F.
		MsgInfo(STR0114) /* ## Certifique-se de selecionar como fonte pagadora a filial centralizadora que será responsável pelas informações geradas no Arquivo da D.I.R.F." ## */
	Endif

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 , NIL , aButton ) CENTERED
	
If nOpcA == 1 .and. nOpc # 2
	Begin Transaction
		If nOpc == 6 //Exclusao
			nDel := 0
			FDel560(@nDel)
	    Else
			gp560Grava( "SR4", aColsAnt, nUsado, oGet:aCols )
			EvalTrigger()
		EndIf	
	End Transaction
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integridade da janela                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aColsComp 	:= {}
aHeaderComp	:= {}
nOpca 		:= 0
dbSelectArea(cAlias)
RestArea( aArea )
Return(nOpca)          

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp560Grava³ Autor ³ J. Ricardo            ³ Data ³ 20.02.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava no arquivo da DIRF                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ gp560Grava                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gp560Grava( cAlias, aColsAnt, nUsado, aColsNew )
Local aLetra	:= {}
Local nX 		:= 0
Local nY 		:= 0
Local aAreaSRL 	:= SRL->( GetArea() )
Local dDtPagto	:= ctod( "  /  /  " )

//Necessario manter os espacos nas letas com apenas 1 caractere devido ao tamanho do campo na tabela SR4
If cAno < "2007"
	aLetra := {	"A ", "B ", "M ", "C ", "T ", "D ", "E ", "F ", "G ", "H ",;
				"P ", "U ", "I ", "J ", "K ", "L ", "O ", "R ", "V ", "W ",;
				"X ", "Y ", "Z ", "1 ", "2 ", "3 ", "4 ", "5 " }
ElseIf cAno < "2016"
	aLetra := {	"A ", "B ", "M ", "C ", "T ", "D ", "J ", "B1", "T1", "C1",;
				"M1", "L ", "I ", "O ", "Q ", "R ", "0 ", "E ", "G ", "P ",;
				"U" , "H ", "H1", "F ", "F1", "V ", "Z ", "1 ", "5 ", "S ",;
				"S1", "W ", "6 ", "7 ", "8 ", "9 ", "Y ", "2 ", "61", "71",;
				"81", "91", "4 ", "O1", "C3", "Q1" }
ElseIf cAno == '2016'
	aLetra := {	"A ", "B ", "T ", "D ", "J ", "B1", "T1",;
				"L ", "I ", "O ", "Q ", "R ", "0 ", "E ", "G ", "P ",;
				"U ", "H ", "H1", "F ", "F1", "V ", "Z ", "1 ", "5 ",;
				"S ", "S1", "W ", "6 ", "7 ", "Y ", "2 ", "61", "71",;
				"4 ", "O1", "Q1" }
ElseIf cAno >= '2017' .And. cAno < '2023'
	aLetra := {	"A ", "B ", "T ", "D ", "J ", "B1", "T1",;
				"L ", "I ", "O ", "Q ", "R ", "0 ", "E ", "E1","G ", "P ",;
				"U ", "H ", "H1", "F ", "F1", "V ", "Z ", "1 ", "5 ",;
				"S ", "S1", "W ", "6 ", "7 ", "Y ", "2 ", "61", "71",;
				"4 ", "O1", "Q1", "I2", "I3"}
ElseIf cAno >= '2023' 
	aLetra := {	"A ", "B ", "T ", "D ", "J ", "B1", "T1",;
				"L ", "I ", "O ", "Q ", "R ", "0 ", "E ", "E1","G ", "P ",;
				"U ", "H ", "H1", "F ", "F1", "V ", "Z ", "1 ", "5 ",;
				"S ", "S1", "W ", "6 ", "7 ", "Y ", "2 ", "61", "71",;
				"4 ", "O1", "Q1", "I2", "I3", "B4", "B5", "B6" }
EndIf	        	 

Begin Transaction
	dbSelectArea("SRL")
	dbSetOrder(1)			// RL_FILIAL+RL_MAT+RL_TIPOFJ+RL_CPFCGC+RL_CODRET+RL_PAIS+RL_NIFEX
	If dbSeek(xFilial("SRL")+M->(RL_MAT+RL_TIPOFJ+RL_CPFCGC+RL_CODRET+RL_PAIS+RL_NIFEX))
		RecLock("SRL",.F.,.T.)
	Else
		RecLock("SRL",.T.)
	Endif
	SRL->RL_FILIAL  := xFilial("SRL")
	SRL->RL_MAT     := M->RL_MAT
	SRL->RL_TIPOFJ  := M->RL_TIPOFJ
	SRL->RL_CGCFONT := M->RL_CGCFONT
	SRL->RL_NOMFONT := M->RL_NOMFONT
	SRL->RL_CODRET  := M->RL_CODRET
	SRL->RL_CPFCGC  := M->RL_CPFCGC
	SRL->RL_BENEFIC := M->RL_BENEFIC
	SRL->RL_ENDBENE := M->RL_ENDBENE
	SRL->RL_UFBENEF := M->RL_UFBENEF
   If Type("SRL->RL_COMPLE1") # 'U'
		SRL->RL_COMPLE1 := M->RL_COMPLE1
	Endif
	
   If Type("SRL->RL_CIDABEN") # 'U'
      SRL->RL_CIDABEN := M->RL_CIDABEN
   Endif

   If Type("SRL->RL_NUMDOC") # 'U'
      SRL->RL_NUMDOC := M->RL_NUMDOC
   Endif

   If Type("SRL->RL_CC") # 'U'
      SRL->RL_CC := M->RL_CC
   Endif

	If Type("SRL->RL_ORIGEM") # "U" 
		SRL->RL_ORIGEM := If( SRL->RL_ORIGEM <> "2", "1", "2" )
	Endif
	
	If Type("SRL->RL_DEPTO") # 'U'
      SRL->RL_DEPTO := M->RL_DEPTO
   Endif

	// Gravacao dos campos referentes ao Residente Exterior sendo testado o primeiro
	// campo e caso exista gravara tambem todos os demais relacionados ao assunto
	If Type("SRL->RL_NEMPR") # 'U'
		SRL->RL_CGCEX := M->RL_CGCEX
		SRL->RL_NEMPR := M->RL_NEMPR
		SRL->RL_TPCON := M->RL_TPCON
		SRL->RL_DTINI := M->RL_DTINI
		SRL->RL_DTFIM := M->RL_DTFIM
		SRL->RL_PAIS  := M->RL_PAIS
		SRL->RL_NIFEX := M->RL_NIFEX
		SRL->RL_LOGEX := M->RL_LOGEX
		SRL->RL_NUMEX := M->RL_NUMEX
		SRL->RL_COMPL := M->RL_COMPL
		SRL->RL_BAIEX := M->RL_BAIEX
		SRL->RL_POSEX := M->RL_POSEX
		SRL->RL_CIDEX := M->RL_CIDEX
		SRL->RL_ESTEX := M->RL_ESTEX
		SRL->RL_TELEX := M->RL_TELEX
		SRL->RL_BREEX := M->RL_BREEX
		SRL->RL_TPREX := M->RL_TPREX
		SRL->RL_TRBEX := M->RL_TRBEX
		SRL->RL_BEDINI:= M->RL_BEDINI
		SRL->RL_PAEXNI:= M->RL_PAEXNI
	Endif
	MsUnlock()
	For nX := 1 TO 12

		// A Data de Pagamento eh fixo na coluna 2
		dDtPagto := aColsNew[nX,2]		
        For nY := 3 to nUsado
        	If aColsNew[nX,nY] # aColsAnt[nX,nY] .AND. aColsNew[nX,nY] >= 0.00  
  				cTipDirf := aLetra[ nY - 2 ]
				dbSelectArea("SR4")
				If dbSeek(SRL->(RL_FILIAL+RL_MAT+RL_CPFCGC+RL_CODRET) +cAno+ StrZero(nX,2) + cTipDirf + SRL->(RL_PAIS+RL_NIFEX))
					RecLock("SR4",.F.,.T.)
				Else
					RecLock("SR4",.T.)
				Endif
				SR4->R4_FILIAL  := SRL->RL_FILIAL
				SR4->R4_MAT     := SRL->RL_MAT
				SR4->R4_CPFCGC  := SRL->RL_CPFCGC
				SR4->R4_CODRET  := SRL->RL_CODRET
				SR4->R4_TIPOREN := cTipDirf
				SR4->R4_VALOR   := aColsNew[nX,nY]
				SR4->R4_MES     := STRZERO(nX,2)
				SR4->R4_ANO		:= cANO
				If Type("SR4->R4_ORIGEM") # "U" 
					SR4->R4_ORIGEM := If( SR4->R4_ORIGEM == "2" .OR. lInfoFin, "2", "1" )				
				EndIf			
				If Type("SR4->R4_PAIS") # 'U'
					SR4->R4_PAIS  := SRL->RL_PAIS
				EndIf
				If Type("SR4->R4_NIFEX") # 'U'
					SR4->R4_NIFEX := SRL->RL_NIFEX
				EndIf

				If Type("SR4->R4_DTPGT") # 'U'
					SR4->R4_DTPGT := dDtPagto
				EndIf

				MsUnlock()
			ElseIf dDtPagto # aColsAnt[nX,2]
				cTipDirf := aLetra[ nY - 2 ]
				dbSelectArea("SR4")
				If dbSeek(SRL->(RL_FILIAL+RL_MAT+RL_CPFCGC+RL_CODRET) +cAno+ StrZero(nX,2) + cTipDirf + SRL->(RL_PAIS+RL_NIFEX))
					RecLock("SR4",.F.,.T.)
					If SR4->(ColumnPos("R4_DTPGT")) > 0
						SR4->R4_DTPGT := dDtPagto
					EndIf
				EndIf
				MsUnlock()
			Endif
			
		Next nY 
	Next nX
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Grava informacoes complementares³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	m560GravInf(SRL->RL_MAT,SRL->RL_TIPOFJ,SRL->RL_CPFCGC,SRL->RL_CODRET)	

End Transaction
RestArea( aAreaSRL )
Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp560LinOk³ Autor ³ J. Ricardo            ³ Data ³ 20.02.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Critica linha digitada                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gp560LinOk(o)
Local nx
Local lRet 	:= .T.

// Colunas Fixas: Primeira = Mes e Segunda = Data de Papagamento.

For nx := 2 To Len( aHeader )
	If nx > 2  .and. !Empty(aCols[n,nX]) .and. aCols[n,nx] < 0.00 .And. lRet
		Help(" ",1,"M560SVALOR")
		lRet := .F.
		Exit
	ElseIf nx == 2 .and. Valtype( aCols[n,nx] ) == "D"
		If ! Empty( aCols[n,nx] ) .and. Month( aCols[n,nx] ) # n .And. lRet
			Help( " ", 1, "M560DTPGT" )		// "Data pagamento Inválida. O Mês da Data de Pagamento difere do Mês de referência."
			lRet := .F.
			Exit
		EndIf
	Endif
Next nx

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp560DelOk³ Autor ³ J. Ricardo            ³ Data ³ 20.02.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Critica linha digitada                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gp560DelOk(o)

Return(.f.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp560TudOk³ Autor ³ J. Ricardo            ³ Data ³ 20.02/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gp560TudOk(o)
Local lRetorna  := .T.
Continua        := .F.

Return(lRetorna)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³F560DEL   ³ Autor ³ J. Ricardo            ³ Data ³ 20.06.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Exclusao                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ F560DEL                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function FDel560(nDel)
Local lExcluiSRL := .T.
Begin Transaction
	RCS->(DbSetOrder(1)) //incluida chamada do indice para evitar ocorrencia de error.log
	RCS->(dbSeek(             xFilial("SRL")+SRL->(RL_MAT+RL_TIPOFJ+RL_CPFCGC+RL_CODRET)+cANO))
	While RCS->(!Eof()) .And. xFilial("SRL")+SRL->(RL_MAT+RL_TIPOFJ+RL_CPFCGC+RL_CODRET)+cANO == ;
							  RCS->(RCS_FILIAL+RCS_MAT+RCS_TIPOFJ+RCS_CPFBEN+RCS_CODRET+RCS_ANO)
		RecLock("RCS",.F.,.T.)
		dbDelete()
      	RCS->( dbSkip() )
	EndDo
	
	If cANO >= "2016"
		dbselectArea("SM8")
		SM8->(dbSetOrder(2))
		SM8->(dbSeek( xFilial("SRL")+SRL->RL_MAT+SRL->RL_CODRET+cANO))
		While SM8->(!Eof()) .And. xFilial("SRL")+SRL->RL_MAT+SRL->RL_CODRET+cANO== ;
				SM8->M8_FILIAL+SM8->M8_MAT+SM8->M8_CODRET+SM8->M8_ANO
			RecLock("SM8",.F.,.T.)
			SM8->(dbDelete())
	      	SM8->( dbSkip() )
		EndDo
		
		dbselectArea("SM9")
		SM9->(dbSetOrder(2))
		SM9->(dbSeek( xFilial("SRL")+SRL->RL_MAT+SRL->RL_CODRET+cANO))
		While SM9->(!Eof()) .And. xFilial("SRL")+SRL->RL_MAT+SRL->RL_CODRET+cANO== ;
				SM9->M9_FILIAL+SM9->M9_MAT+SM9->M9_CODRET+SM9->M9_ANO
			RecLock("SM9",.F.,.T.)
			SM9->(dbDelete())
	      	SM9->( dbSkip() )
		EndDo
		
	EndIf

	dbSelectArea("SR4")
	SR4->(DbSetOrder(1))
	dbSeek(             SRL->(RL_FILIAL+RL_MAT+RL_CPFCGC+RL_CODRET+cAno ))
	While ! Eof() .And. SRL->(RL_FILIAL+RL_MAT+RL_CPFCGC+RL_CODRET)+cAno == ;
						SR4->(R4_FILIAL+R4_MAT+R4_CPFCGC+R4_CODRET+R4_ANO) 
		If SR4->R4_PAIS == SRL->RL_PAIS .AND. SR4->R4_NIFEX == SRL->RL_NIFEX
			If !(SR4->( FieldPos("R4_IDCMPL"))== 0 )
				If RFI->(DbSeek(SR4->R4_FILIAL+SR4->R4_MAT+SR4->R4_IDCMPL+SR4->R4_CODRET))
					RecLock("RFI",.F.,.T.)
					RFI->(DbDelete())
					RFI->(MsUnlock())
				EndIf
			EndIf
		RecLock("SR4",.F.,.T.)
		dbDelete()
		nDel ++
		EndIf
      dbSkip()
	EndDo

	If SR4->(dbSeek(SRL->(RL_FILIAL+RL_MAT+RL_CPFCGC+RL_CODRET)) )
		While ! Eof() .And.	SRL->(RL_FILIAL+RL_MAT+RL_CPFCGC+RL_CODRET)+cAno == ;
							SR4->(R4_FILIAL+R4_MAT+R4_CPFCGC+R4_CODRET+R4_ANO) 
			If SR4->R4_PAIS == SRL->RL_PAIS .AND. SR4->R4_NIFEX == SRL->RL_NIFEX
				lExcluiSRL := .f.
			EndIf
			dbSkip()
		EndDo
	EndIf
	If lExcluiSRL
		dbSelectArea("SRL")
		RecLock("SRL",.F.,.T.)
		dbDelete()
	EndIf
	aColsComp 	:= {}
	aHeaderComp := {}
End Transaction
Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³GeraManu  ³ Autor ³                       ³ Data ³ 20.06.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Gera o aHeader e aCols                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GeraManu                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function GeraManu( cAlias, aHeader, aCols, nUsado, nOpc, lTelaS )

Local aLetra := {}

Local aMes   := {STR0021,STR0022,STR0023,STR0024,STR0025,STR0026,;	//"Janeiro	R$"###"Fevereiro  R$"###"Marco      R$"###"Abril      R$"###"Maio       R$"###"Junho      R$"
                 STR0027,STR0028,STR0029,STR0030,STR0031,STR0032}	//"Julho	R$"###"Agosto     R$"###"Setembro   R$"###"Outubro    R$"###"Novembro   R$"###"Dezembro   R$"

Local aDirfv[12,32]
Local nX
Local nY
Default lTelaS := .T.


If cAno < "2007"
	aLetra := {	"A", "B", "M", "C", "T", "D", "E", "F", "G", "H",;
				"P", "U", "I", "J", "K", "L", "O", "Q", "R", "V",;
				"W", "X", "Y", "Z", "1", "2", "3", "4", "5", "O1",;
				"C3","Q1"}
				
ElseIf cAno < "2016"
	aLetra := {	"A" , "B" , "M", "C" , "T" , "D", "J", "B1", "T1", "C1",;
				"M1", "L" , "I", "O" , "Q" , "R", "0", "E" , "G" , "P" ,;
			 	"U" , "H" , "H1", "F", "F1", "V", "Z", "1" , "5" , "S" ,;
				"S1", "W" , "6" , "7", "8" , "9", "Y", "2" , "61", "71",;
				"81", "91", "4" , "O1","C3", "Q1"}
				
	aDirfv := Array( 12, 48 )
	
ElseIf cAno == '2016'
	aLetra := {	"A" , "B" ,  "T", "D", "J", "B1", "T1",;
				"L" , "I", "O" , "Q", "R", "0", "E" , "G" , "P" ,;
				"U" , "H" , "H1", "F", "F1", "V", "Z", "1", "5" , "S" ,;
				"S1", "W" , "6" , "7",  "Y", "2", "61", "71",;
				"4" , "O1", "Q1"}

	aDirfv := Array( 12, 39 )
	
ElseIf cAno >= '2017' .And. cAno < '2023'
	aLetra := {	"A" , "B" ,  "T", "D", "J", "B1", "T1",;
				"L" , "I", "O" , "Q", "R", "0", "E" , "E1","G" , "P" ,;
				"U" , "H" , "H1", "F", "F1", "V", "Z", "1", "5" , "S" ,;
				"S1", "W" , "6" , "7",  "Y", "2", "61", "71",;
				"4" , "O1", "Q1", "I2", "I3"}

	aDirfv := Array( 12, 42 )
ElseIf cAno >= '2023'
	aLetra := {	"A" , "B" ,  "T", "D", "J", "B1", "T1",;
				"L" , "I", "O" , "Q", "R", "0", "E" , "E1","G" , "P" ,;
				"U" , "H" , "H1", "F", "F1", "V", "Z", "1", "5" , "S" ,;
				"S1", "W" , "6" , "7",  "Y", "2", "61", "71",;
				"4" , "O1", "Q1", "I2", "I3", "B4", "B5", "B6"}

	aDirfv := Array( 12, 45 )
EndIf

aHeader := {}
AADD(aHeader,{STR0033	, "M550MES"  ,"@!"                      ,  13    , 0  ,  "! EMPTY()"   ,"û", "C" , " " }) //"Mes            "--
AADD(aHeader,{STR0090	, "M550DTP"  ,"@D"                      ,  08    , 0  ,  ""				,"û", "D" , " " }) //"Data Pagamento "--

AADD(aHeader,{STR0034 , "M550RBT"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO()"  ,"û", "N" , " " }) //"Rend.Tributavel""A"
AADD(aHeader,{STR0035 , "M550CPR"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Contrib.Previd.""B"

If cAno < "2016"
	AADD(aHeader,{STR0036 , "M550PRI"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Cont.Prev.Priv.""M"

	AADD(aHeader,{STR0037 , "M550PEJ"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Pensao Judicial""C"
EndIf

AADD(aHeader,{STR0051 , "M550DEP"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Val.Dependentes""T"
AADD(aHeader,{STR0038 , "M550IMP"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Imposto Retido ""D"
AADD(aHeader,{STR0046 , "M55013S"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //" 13o  Salario  ""J"
If cAno < "2007"
	AADD(aHeader,{STR0047 , "M550D13"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"k- Deducoes 13o  " - "K"
Else
	AADD(aHeader,{STR0071 , "M550131"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"13o-Prev.Oficial""B1"
	AADD(aHeader,{STR0072 , "M550132"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"13o-Dependentes""T1"
	If cAno < "2016"
		AADD(aHeader,{STR0073 , "M550133"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"13o-Pensao Alim.""C1"
		AADD(aHeader,{STR0074 , "M550134"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"13o-Prev.Privada""M1"
	EndIf
EndIf	
AADD(aHeader,{STR0048 , "M55013I"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Imp.Retido 13o ""L"

AADD(aHeader,{STR0045 , "M550ONT"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Outros Nao Trib""I"
AADD(aHeader,{STR0049 , "M550ORE"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Outr.Rend.Trib.""O"
AADD(aHeader,{STR0099 , "M550QRE"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Outr.Rend.Imp.Ret.""Q"
AADD(aHeader,{STR0050 , "M550IFC"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Infor. Complem.""R"

If cAno >= "2007"
	AADD(aHeader,{STR0091 , "M550APE"	,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Abono Pecuniario  ""0"
Endif

AADD(aHeader,{STR0039 , "M550SAF"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Ind. Rescisao  ""E"
AADD(aHeader,{STR0113 , "M550SAFE1"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //""Ind. Rescisao 13"  "E1"
AADD(aHeader,{STR0041 , "M550DAJ"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Diaria/Aj.Custo""G"
AADD(aHeader,{STR0043 , "M550LUC"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Lucro Div. 1996""P"
AADD(aHeader,{STR0044 , "M550VTS"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Val.Pag.Tit.Soc""U"

AADD(aHeader,{STR0042 , "M550PRP"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Prov.Pens/Apos.""H"
If cAno >= "2007"
	AADD(aHeader,{STR0092 , "M550H1"	,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Prov. de Aposentadoria ou Ref. por Molestia Grave 13o""H1"
Endif
AADD(aHeader,{STR0040 , "M550PPA"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Parte Prov.Apos""F"
If cAno >= "2007"
	AADD(aHeader,{STR0093 , "M550F1"	,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Parte Prov. de Apos. / Pensao (65 Anos ou Mais) 13o""F1"
Endif

AADD(aHeader,{STR0053 , "M550COM"	,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Imp.Comp.Dec.Judicial""V"
AADD(aHeader,{STR0057 , "M550JUD"	,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Dep.Judicial Imp.Retido""Z"
AADD(aHeader,{STR0058 , "M55013J"	,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"13o Imp.Comp.Dec.Judicial""1"
AADD(aHeader,{STR0062 , "M55013T"	,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"13o Dep.Judicial Imp.Retido" "5"
If cAno >= "2007"
	AADD(aHeader,{STR0094 , "M550S"		,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Imp.Compensado virtude decisao judic.Anos Anteriores""S"
	AADD(aHeader,{STR0095 , "M550S1"	,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Imp.Compensado virtude decis. judic.Anos Anteriores 13o""S1"
Endif
AADD(aHeader,{STR0054 , "M550EXI"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Rend.Pagos Exig.Suspensa""W"
If cAno < "2007"
	AADD(aHeader,{STR0055 , "M550DEX"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"X-Deducao Exig. Suspensa" - "X"
Else 
	AADD(aHeader,{STR0067 , "M550EXP"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Ded.Exig.Suspensa Previdencia""6"
	AADD(aHeader,{STR0068 , "M550EXD"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Ded.Exig.Suspensa Dependente""7"
	If cAno < "2016"
		AADD(aHeader,{STR0069 , "M550EPA"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Ded.Exig.Suspensa Pensao""8"
		AADD(aHeader,{STR0070 , "M550EPP"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Ded.Exig.Suspensa Prev.Privada""9"
	EndIf
EndIf

AADD(aHeader,{STR0056 , "M550REX"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Y-Imposto Retido Exig.Susp.""Y"
AADD(aHeader,{STR0059 , "M55013E"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"13o Rend.Pagos Exig.Susp.""2"
If cAno < "2007"
	AADD(aHeader,{STR0060 , "M55013D"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"13o Deducao Exig. Suspensa" - "3"
Else
	AADD(aHeader,{STR0075 , "M55013A"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"13o-Ded.Exig.Susp.Prev.Oficial""61"
	AADD(aHeader,{STR0076 , "M55013B"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"13o-Ded.Exig.Susp. Dependente""71"
	If cAno < "2016"
		AADD(aHeader,{STR0077 , "M55013C"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"13o-Ded. Exig.Susp. Pensao""81"
		AADD(aHeader,{STR0078 , "M55091"   ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"13o-Ded.Exig.Susp. Prev.Privada""91"
	EndIf
EndIf	
AADD(aHeader,{STR0061 , "M55013R"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"13o Imposto Retido Exig.Susp.""4"
If cAno >= "2013"
	AADD(aHeader,{STR0100 , "M550PLRE"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Outr. Rend. (PLR)""O1"
	If cAno < "2016"
		AADD(aHeader,{STR0101 , "M550PLPE"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Pensao Judicial (PLR)""C3"
	EndIf
	AADD(aHeader,{STR0102 , "M550PLIR"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Imposto Ret. (PLR)""Q1"
EndIf
If cAno >= "2017"
	AADD(aHeader,{STR0111 , "M550IMU"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Rendim. Entid. Imunes""I2"
	AADD(aHeader,{STR0112 , "M550ISE"  ,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Rendim. Entid. Isentas""I3"
EndIf

If cAno >= "2023"
	AADD(aHeader,{STR0121 , "M550IRSMP"  	,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Ded. Simplificada""B4"
	AADD(aHeader,{STR0122 , "M550IRSMP13"  ,"@E 999,999,999,999.99"    ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Ded. Simplificada 13o""B5"
	AADD(aHeader,{STR0123 , "M550IRSMSU"  	,"@E 999,999,999,999.99"   ,  15    , 2  ,  "POSITIVO() " ,"û", "N" , " " }) //"Trib.Leg.Susp""B6"
EndIf

If cAno < '2007'
	nUsado := 32
ElseIf cAno < '2016'
	nUsado := 48
ElseIf cAno == '2016'
	nUsado := 39
ElseIf cAno >= '2017' .And. cAno < '2023'
	nUsado := 42
ElseIf cAno >= '2023'
	nUsado := 45
EndIf

For nX := 1 to 12
    For nY := 1 TO nUsado
		If nY == 1
			aDirfv[nX,nY] := aMes[nX]
		ElseIf nY == 2
			aDirfv[nX,nY] := ctod( "  /  /  " )
		Else
			aDirfv[nX,nY] := 0.00
		Endif
	Next nY
Next nX

If nOpc # 3
	dbSelectArea("SR4")
	dbSeek(            SRL->(RL_FILIAL+RL_MAT+RL_CPFCGC+RL_CODRET)+cAno )
	While !Eof() .And. SRL->(RL_FILIAL+RL_MAT+RL_CPFCGC+RL_CODRET)+cAno ==;
				       SR4->(R4_FILIAL+R4_MAT+R4_CPFCGC+R4_CODRET+R4_ANO)

		If SR4->R4_PAIS == SRL->RL_PAIS .AND. SR4->R4_NIFEX == SRL->RL_NIFEX
			nMes	:= Val(SR4->R4_MES)
			nElem	:= Ascan( aLetra, Alltrim(SR4->R4_TIPOREN) )  

			// Preenche a Data de Pagamento sempre na coluna 2
			If nMes = 12
				//Considera a maior data em dezembro para nao sair a data de pagamento do 13o. Salario 
				adirfv[nMes,2] := If( adirfv[nMes,2] < SR4->R4_DTPGT, SR4->R4_DTPGT, adirfv[nMes,2] )
			Else
				adirfv[nMes,2] := SR4->R4_DTPGT
			EndIf

			If nElem > 0
				adirfv[nMes,nElem+2] := SR4->R4_VALOR
			EndIf
		EndIf
		dbSkip(1)
	Enddo
Endif

aCols   := Aclone(aDirfv)

For nX := 1 to 12
	Aadd(aCols[nX],.F.)
Next nX
Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³gpm560BenefºAutor  ³Microsiga          º Data ³  01/22/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Atualiza dados referentes aos beneficiarios e informacoes  º±±
±±º          ³complementares                                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP5                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gpm560Benef(nOpcx)
Local aArea	:= GetArea()

FWExecView(STR0002,'GPEM560A', 4,, { || .T. } )	 //'Informações Complementares'
RestArea(aArea)
Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³gpm560RRA	 ºAutor  ³Leandro Drumond    º Data ³  16/08/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Atualiza dados referentes ao RRA.                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM560                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gpm560RRA(nOpcx)
Local aArea		:= GetArea()

Private n
Private lUpdRRA := !( nOpcX == 2 )

dbSelectArea("SRA")
dbSetOrder(1)
dbSeek(xFilial('SRA')+SRL->RL_MAT)

FWExecView(STR0098,'GPEM910', 4,, { || .T. } )	 //'Rendimentos Recebidos Acumuladamente'

RestArea(aArea)
Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³gpm561	 ºAutor  ³Leandro Drumond    º Data ³  16/08/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Atualiza dados referentes a PENSAO                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM560                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function gpm561(nOpcx)
Local aArea		:= GetArea()

Private n

	DbSelectArea("SRA")
	DbSeek(xFilial('SRA')+SRL->RL_MAT)
	
	FWExecView(STR0104,'GPEM561', If(nOpcx==2,MODEL_OPERATION_VIEW,MODEL_OPERATION_UPDATE),, { || .T. } )	 //Pensão

RestArea(aArea)
return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³gpm562	 ºAutor  ³Leandro Drumond    º Data ³  16/08/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Atualiza dados referentes a Previdencia                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM560                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function gpm562(nOpcx)
Local aArea		:= GetArea()

Private n

	DbSelectArea("SRA")
	DbSeek(xFilial('SRA')+SRL->RL_MAT)
	
	FWExecView(STR0110,'GPEM562', If(nOpcx==2,MODEL_OPERATION_VIEW,MODEL_OPERATION_UPDATE),, { || .T. } )	 //Previdencia

RestArea(aArea)
return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEM560   ºAutor  ³Microsiga           º Data ³  02/14/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function a560ValidPD()
Local cPd	 := &( ReadVar() )
Local oModel := FwModelActive()
Local oMGrid := oModel:GetModel('DETAILRCS')
Local lRet   := .T.

    Begin Sequence
	If !Empty( cPd )
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Carrega a Descricao da Verba                                  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        */
		If SRV->( dbSeek(xFilial("SRV")+cPd))
            oMGrid:SetValue('RCS_TIPORE', AllTrim(SRV->RV_DIRF) )
            oMGrid:SetValue('RCS_DESCRI', AllTrim(SRV->RV_DESC) )
		Else
			lRet := .F.
		EndIf
	EndIf
    End Sequence

Return( lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³m560GravInf ºAutor  ³Andreia dos Santos º Data ³ 01/24/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Faz a gravacao no arquivo de informacoes complementares     º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEM560                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/                     
Function m560GravInf(cMatr,cTipoFJ,cCPFBEN,cCodRet)
Local aArea		:= GetArea()
Local nX 		:= 1
Local nT 		:= 1		                   
Local nPosVerba := GdFieldPos("RCS_VERBA",aHeaderComp)
Local nPosTpRe  := GdFieldPos("RCS_TIPORE",aHeaderComp)
Local cVerba	:= ""
Local cTipoRe	:= ""

For nX := 1 to len(aColsComp)       
	If !empty(aColsComp[nx][01])
		
		cVerba := aColsComp[nX][nPosVerba]
		
		//--Verifica se Nao esta Deletado no aCols
		If nx <= Len(aColsRec)               		// Altera Linha existente
			RCS->(dbGoto(aColsRec[nx]))
			
			RecLock("RCS",.F.)

			cTipoRe := aColsComp[nx][nPosTpRe] //RCS->RCS_TIPORE

			//--Verifica se esta deletado
			If aColsComp[nx][Len(aColsComp[nx])] 
				RCS->(dbDelete())
				MsUnlock()		   
				Loop    
			EndIf
		Else                                  		// Nova Linha da GetDados
			If aColsComp[nx][Len(aColsComp[nx])] 
		       loop
		    EndIf   
		
			cTipoRe := aColsComp[nx][nPosTpRe]
		
			RecLock("RCS",.T.)
	   
			RCS->RCS_FILIAL		:= xFilial("SRL")
			RCS->RCS_MAT 		:= cMATR
			RCS->RCS_TIPOFJ		:= cTipoFJ
			RCS->RCS_CPFBEN		:= cCpfBen
			RCS->RCS_CODRET		:= cCodRet
			RCS->RCS_ANO		:= cAno 
		EndIf
		
		For nt := 1 To Len(aHeaderComp)
			cCampo		:= Trim(aHeaderComp[nt][2])
			xConteudo 	:= aColsComp[nx][nt]
			Replace &cCampo With xConteudo
		Next nt                     

		RCS->RCS_TIPORE		:= cTipoRe
		If Type("RCS->RCS_ORIGEM") # "U" 
			RCS->RCS_ORIGEM := "1"
		Endif

		MsUnlock()
	EndIf	
Next      
aColsComp 	:= {}
aHeaderComp := {}
RestArea( aArea )
Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³m560AltInfºAutor  ³Microsiga           º Data ³  01/30/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Altera dados das informacoes complementares atraves da     º±±
±±º          ³ verba informada                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP5                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function m560AltInf()
Local cVerba:= SPACE(03)
Local cCIC 	:= SPACE(14)
Local cNome := SPACE(20)
Local cDescri := SPACE(150)
Local cDescSRV := SPACE(20)
Local lRet
Local nOpca
Local nX 
Local aArea := GetArea()
Local cAltera   
Local cSubst             

Local oDlg
Local oVerba
Local oCIC
Local oNOme
Local oAltera
Local oSubs
Local oNewDsc
Local cAno

Local cAlias	:= "RCS"
Local cCpoDel	:= ""
Local cRecno	:= 0
Local cDel      := ""
Local cWhere 	:= ""
Local cUpdate	:= ""
Local nTot		:= 0
Local nCont 	:= 0
Local nMinRec	:= 0
Local nMaxRec	:= 0
Local bSet15
Local bSet24
Local cAliasRCS	:= GetNextAlias()
Local aSays		   	:= {}
Local aButtons	   	:= {}
Local nOpcA		  	:= 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaração de arrays para dimensionar tela		             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}

Private aItem := {"Não","Sim"}

If !Pergunte("GPM560",.T.)
	Return
EndIf

cAno := strZero( MV_PAR01,4)


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta as Dimensoes dos Objetos         					     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aAdvSize		:= MsAdvSize()
aAdvSize[5]		:=	556 //horizontal
aAdvSize[3]		:=	280 // LARGURA
aAdvSize[4]		:=	97  // ALTURA	
aAdvSize[6]		:=  255 //Vertical
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 4 , 4 }					 
aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
aObjSize	:= MsObjSize( aInfoAdvSize , aObjCoords )

DEFINE MSDIALOG oDlg FROM  aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] TITLE OemToAnsi(STR0004) PIXEL  //"Alteracao das Informacoes Complementares"
	nOpca := 2
	@ aObjSize[1][1], aObjSize[1][2] TO aObjSize[1][3]+28, aObjSize[1][4] LABEL "" OF oDlg PIXEL 
	
	@ aObjSize[1][1]+10	, aObjSize[1][2]+5 SAY STR0007  SIZE 053,07 OF oDlg PIXEL   //VERBA
	@ aObjSize[1][1]+10	, aObjSize[1][2]+60 MSGET oVerba VAR cVerba SIZE 016,10 OF oDlg PIXEL PICTURE "@!"  F3 "SRV"  VALID (lRet := ( mPoeDesc(cVerba,@cNome,@cDescSRV) ),oNome:Refresh(),lRet) HASBUTTON
	@ aObjSize[1][1]+10	, aObjSize[1][2]+100 MSGET oNome  VAR cDescSRV  SIZE 100,10 OF oDlg PIXEL PICTURE "@!" WHEN .F.
	 
	@ aObjSize[1][1]+25, aObjSize[1][2]+5 SAY STR0008  SIZE 055,07 OF oDlg PIXEL  //"Altera CPF/CNPJ?" 
	@ aObjSize[1][1]+25, aObjSize[1][2]+60 COMBOBOX oAltera VAR cAltera ITEMS aItem  SIZE 040,10 OF oDlg PIXEL  
	
	@ aObjSize[1][1]+40, aObjSize[1][2]+5 SAY STR0005 SIZE 055,07 OF oDlg PIXEL  //"CPF/CNPJ"
	@ aObjSize[1][1]+40, aObjSize[1][2]+60 MSGET oCIC   VAR cCIC   SIZE 070,10 OF oDlg PIXEL VALID CGC( cCIC ) WHEN (cAltera=="Sim" ) 

	@ aObjSize[1][1]+55, aObjSize[1][2]+5 SAY STR0116  SIZE 055,15 OF oDlg PIXEL  //"Substituir conteúdo completo?"
	@ aObjSize[1][1]+55, aObjSize[1][2]+60 COMBOBOX oSubs VAR cSubst ITEMS aItem  SIZE 040,10 OF oDlg PIXEL 
	
	@ aObjSize[1][1]+70, aObjSize[1][2]+5 SAY STR0117 SIZE 055,07 OF oDlg PIXEL  //"Novo conteúdo:"
	@ aObjSize[1][1]+70	, aObjSize[1][2]+60 MSGET oNewDsc  VAR cDescri  SIZE 150,10 OF oDlg PIXEL PICTURE "@!"  WHEN (cSubst=="Sim") 
	
	bSet15 := {|| nOpcA :=1 ,oDlg:End()}
	bSet24 := {|| nOpcA :=2 ,oDlg:End()}
	
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg, bSet15, bSet24, Nil, Nil) CENTERED

If nOpca == 1

	If Empty(cVerba)	  
		Aviso(STR0063,STR0066,{"OK"})//Inconsistencia##"E necessario informar a Verba"
	ElseIf  cSubst=="Sim" .And. Empty(cDescri)	
			Aviso(STR0063,STR0118,{"OK"})//Inconsistência##"É necessário informar o novo conteúdo caso tenha selecionado para substituir o conteúdo completo."
	Else
		cFilSRV := SRV->RV_FILIAL
		
		// Substitui Conteúdo Completo
		If  (cSubst == "Sim" .And. !Empty(cDescri)) 
			If (!Empty( cFilSRV) .and. MsgYesNo( OemToAnsi( STR0085 +cFilSRV + STR0086 + STR0087 ) )) .or. Empty( cFilSRV )
				
				cCpoDel	:= "D_E_L_E_T_"
				cRecno	:= "R_E_C_N_O_" 
		
				cDel	:= "" + cCpoDel + " = ' '"
		
				cWhere  :=  " WHERE " + cDel 
				MsMinMaxRec( cAlias , @nMinRec , @nMaxRec,,cWhere  )
		
				If nMaxrec == nMinRec .AND. nMaxRec # 0
					nTot	:= ( nMaxrec - nMinRec ) + 1
				Else
					nTot	:= ( nMaxrec - nMinRec )
				Endif
		
				If nTot # 0
					// controle de transacao para processar de 1024 em 1024 registros
					nTot  := Ceiling(nTot / 1024) //Arredonda para cima em caso de numero quebrado
					If nTot < 1
						nTot := 1
					Else
						If (Int(nTot) - nTot) > 0 
							nTot ++
						EndIf
					EndIf
					
					nCont := nMinRec + 1024
							
					For nx = 1 to nTot
					
						cUpdate		:= "UPDATE  "+RetSqlName(cAlias)
						cUpdate 	+= " SET RCS_NOME  ='" + cNome  + "'" + CRLF
						If !Empty(cCic )
							cUpdate += ", RCS_CPFCGC ='" + cCic  + "'" + CRLF
						EndIf			

						///Altera CPF/CNPJ
						If (cAltera == "Sim" .And.  !Empty(cCic)) 
							cUpdate 	+= ", RCS_DESCRI ='" + AllTrim( cDescri ) + " / CNPJ: " + Transform(Left(cCIC,14),'@R ##.###.###/####-##') + "'" + CRLF
						ElseIf cAltera == "Não"
							cUpdate 	+= ", RCS_DESCRI ='" + AllTrim( cDescri ) + "'" + CRLF
						EndIf

						cUpdate 	+= " WHERE RCS_FILIAL LIKE '%"+Alltrim(cFilSRV)+"%'" + CRLF
						cUpdate 	+= " AND RCS_ANO     = '" + cAno + "'" + CRLF
						cUpdate 	+= " AND RCS_VERBA <> ' ' " + CRLF
						cUpdate 	+= " AND RCS_VERBA = '" + cVerba + "'" + CRLF
						cUpdate 	+= " AND " + cRecno + ">=" + allTrim(str(nMinRec)) + " AND " + cRecno + "<=" + allTrim(str(nCont)) +CRLF
						cUpdate 	+= " AND  " + cDel +  CRLF    
						TcSqlExec ( cUpdate )
		
						nMinRec := nCont
						nCont  := nMinRec + 1024
					Next nx
				Endif 
			
			EndIf
		// Não Substitui Conteúdo Completo
		ElseIf cSubst == "Não" .And. cAltera == "Sim" .And. !Empty(cCic)   

				cFilSRV	:= "%'%"+Alltrim(SRV->RV_FILIAL)+"%'%"

				DbSelectArea("RCS")
				nOrder := RetOrder("RCS", "RCS_FILIAL+RCS_MAT+RCS_TIPOFJ+RCS_CPFBEN+RCS_CODRET+RCS_ANO+RCS_VERBA+RCS_TIPORE+RCS_CPFCGC+RCS_OUTROS", .T.)
				If nOrder > 0
					RCS->(DbSetOrder(nOrder)) // RCS_FILIAL+RCS_MAT+RCS_TIPOFJ+RCS_CPFBEN+RCS_CODRET+RCS_ANO+RCS_VERBA+RCS_TIPORE+RCS_CPFCGC+RCS_OUTROS

					BeginSQL ALIAS cAliasRCS
						SELECT RCS_FILIAL, RCS_MAT, RCS_TIPOFJ, RCS_CPFBEN , RCS_CODRET , RCS_ANO , RCS_VERBA , RCS_TIPORE, RCS_CPFCGC, RCS_OUTROS, RCS_DESCRI
						FROM %Table:RCS% RCS
						WHERE 	RCS_FILIAL	LIKE %exp:cFilSRV% AND
								RCS_ANO 	= %exp:cAno% AND
								RCS_VERBA 	= %exp:cVerba% AND
								RCS.%NotDel%
					EndSQL

					While (cAliasRCS)->(!Eof())				
						nPos	:= AT("CNPJ: ",(cAliasRCS)->RCS_DESCRI) 
						If nPos > 0
							If RCS->(DbSeek( (cAliasRCS)->RCS_FILIAL + (cAliasRCS)->RCS_MAT + (cAliasRCS)->RCS_TIPOFJ + (cAliasRCS)->RCS_CPFBEN + (cAliasRCS)->RCS_CODRET + (cAliasRCS)->RCS_ANO + (cAliasRCS)->RCS_VERBA + (cAliasRCS)->RCS_TIPORE + (cAliasRCS)->RCS_CPFCGC + (cAliasRCS)->RCS_OUTROS ))
								RecLock("RCS",.F.)
								cSubString 		:= AllTrim((cAliasRCS)->RCS_DESCRI)
								cSubString		:= SubStr(cSubString,1,nPos+5) // Soma 5 pois a primeira posição é do C em "CNPJ: "
								cSubString		+=  Transform(Left(cCIC,14),'@R ##.###.###/####-##') 
								RCS->RCS_DESCRI	:= cSubString
								RCS->RCS_CPFCGC	:= cCic
								RCS->(MsUnLock())
							EndIf
						EndIf		
					(cAliasRCS)->(DbSkip())
					EndDo
					(caliasRCS)->(DbCloseArea())
				Else
					aAdd(aSays,OemToAnsi(STR0119)) //"Para realizar essa operação é necessário executar o último UPDDISTR disponível."
					aAdd(aSays,OemToAnsi(STR0120)) //"Clique em Visualizar para consultar a documentacao no TDN. "
					aAdd(aSays,"") 
					aAdd(aButtons, { 15 ,.T.,{||  ShellExecute("open","https://tdn.totvs.com/pages/releaseview.action?pageId=669612954","","",1) } } )
					aAdd(aButtons, { 2 ,.T.,{|o| FechaBatch() }} )
					FormBatch( STR0063, aSays, aButtons )		
				EndIf	
		EndIf
	Endif
Endif
	
RestArea( aArea )
Return()
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³mPoeDesc  ºAutor  ³Microsiga           º Data ³  01/28/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function mPoeDesc(cVerba,cDesc,cDesc150)
cDesc := ""
if SRV->(dbSeek(xFilial("SRV",SRL->RL_FILIAL)+cVerba))
	cDesc := SRV->RV_DESC
	cDesc150 := PADR( cDesc, 150, " " )
	cDesc := PADR( cDesc, TamSX3("RCS_NOME")[1], " " )
EndIf	
Return( .t. )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEM560   ºAutor  ³Microsiga           º Data ³  01/24/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function a560DetInf()
Local nPosOut 	:= GdFieldPos("RCS_OUTROS")
Local cDetalhe	:= &( ReadVar() )
Local cTexto	:= ""
Local lRet		:= .T.

If !empty(cDetalhe) .and. !empty(aCols[n,nPosOut])                                                     
	cTexto 	:= STR0081 //"O campo 'Detalhe' e o campo 'Espec.Outros' nao podem ser preenchidos simultaneamente. "
	cTexto 	+= STR0082 //"Detalhe=> Quando deseja-se imprimir informacoes no quadro 6-Informacoes Complementares.  "
	cTexto 	+= STR0083 //"Espec.Outros=> Quando deseja-se imprimir informacoes na linha 07 do quadro 04-Rendimentos Isentos e nao tributaveis. "
	cTexto	+= STR0084 //"Preencha uma linha para cada informacao."
	Aviso(STR0079 ,cTexto,{"Ok"}) //"Atencao"
	lRet := .F.
EndIf

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEM560   ºAutor  ³Microsiga           º Data ³  01/24/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function a560OutInf()
Local nPosDet 	:= GdFieldPos("RCS_DESCRI")
Local cOutros	:= &( ReadVar() )
Local lRet		:= .T.

If !empty(cOutros) .and. !empty(aCols[n,nPosDet])                                                     
	cTexto 	:= STR0081 //"O campo 'Detalhe' e o campo 'Espec.Outros' nao podem ser preenchidos simultaneamente. "
	cTexto 	+= STR0082 //"Detalhe=> Quando deseja-se imprimir informacoes no quadro 6-Informacoes Complementares.  "
	cTexto 	+= STR0083 //"Espec.Outros=> Quando deseja-se imprimir informacoes na linha 07 do quadro 04-Rendimentos Isentos e nao tributaveis. "
	cTexto	+= STR0084 //"Preencha uma linha para cada informacao."
	Aviso(STR0079,cTexto,{"Ok"}) //"Atencao"
	lRet := .F.
EndIf
Return( lRet )
 
/*                                	
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ MenuDef		³Autor³  Luiz Gustavo     ³ Data ³15/12/2006³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Isola opcoes de menu para que as opcoes da rotina possam    ³
³          ³ser lidas pelas bibliotecas Framework da Versao 9.12 .      ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEM560                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³aRotina														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/   
Static Function MenuDef()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³    2 - Simplesmente Mostra os Campos                         ³
//³    3 - Inclui registros no Bancos de Dados                   ³
//³    4 - Altera o registro corrente                            ³
//³    5 - Remove o registro corrente do Banco de Dados          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aRotina :=	{	{ STR0009 ,"AxPesqui"  , 0 , 1,,.F.},; //"Pesquisar"
						{ STR0010 ,"gp560Man"  , 0 , 2},; //"Visualizar"
						{ STR0011 ,"gp560Man"  , 0 , 3},; //"Incluir" 
						{ STR0012 ,"gp560Man"  , 0 , 6},; //"Alterar"
						{ STR0003 ,"m560AltInf", 0 , 5},; //"Alt. Inform. Compl."
						{ STR0013 ,"gp560Man"  , 0 , 5} } //"Excluir"

Return(aRotina)


/*/{Protheus.doc} fValidFtPag
Faz a validação do CNPJ da fonte pagadora e preenche o campo referente ao nome da fonte pagadora selecionada
@type function
@author Rafael Reis
@since 22/03/2018
@return lógico
/*/
Function fValidFtPag()

	Local nPos := aScan(aSM0, { |x| x[18] == M->RL_CGCFONTE })

	If (naovazio() .AND. CGC(M->RL_CGCFONTE))
		If(nPos > 0)
			M->RL_NOMFONT := aSM0[nPos][17]
			Return .T.
		Else
			MsgInfo(STR0115) // ##CNPJ não cadastrado.##
			Return .F.
		Endif
	Endif

Return .F.



