#INCLUDE "PONA290.CH"
#INCLUDE "PROTHEUS.CH"

/*/{Protheus.doc} PONA290
Cadastro de Periodos de Apontamentos
@type Function
@author Marinaldo de Jesus
@since 29/07/2002
@history Cecilia C., 20/05/2014, Incluido o fonte da 11 para a 12 e efetuada a limpeza.
/*/
Function PONA290()
	
	Local aArea		:= GetArea()
	Local cFiltra	:= ""
	Local cAlias	:= "SPO"
	Local oMBrowse	:= FWMBrowse():New()
	
	//So Executa se o Modo de Acesso dos Arquivos do Ponto Estiverem OK
	If ( ValidArqPon() .And. CheckModSPO())
		
		Private cCadastro := OemToAnsi( STR0006 ) // "Cadastro de Periodos de Apontamento"
		
		// Inicializa o filtro
		cFiltra := ChkRh( FunName(), cAlias, "1" )
		
		oMBrowse:SetAlias(cAlias)
		oMBrowse:SetDescription(OemToAnsi(cCadastro))
		oMBrowse:SetmenuDef("PONA290")
		oMBrowse:SetFilterDefault( cFiltra )
		
		oMBrowse:AddLegend( "Empty(SPO->PO_RESPONS) .Or. SPO->PO_RESPONS=='U'", "GREEN", STR0017 ) // "Registro do UsuАrio"
		oMBrowse:AddLegend( "SPO->PO_RESPONS == 'S'", "RED", STR0018 ) // "Registro do Sistema"
		
		oMBrowse:Activate()
		
	EndIf
	
	RestArea( aArea )
	
Return( NIL )

/*
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPna290DtVld Ё Autor ЁMarinaldo de Jesus    Ё Data Ё29/07/2002Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁValidar as Datas no Cadastro de Periodos de Apontamento		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁPna290DtVld()												 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                     		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlRet                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁCadastro de Periodos de Apontamento                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pna290DtVld()

Local aArea			:= GetArea()
Local aAreaSPO		:= SPO->( GetArea() )
Local cVar			:= ReadVar()
Local dDataVld		:= Ctod("//")
Local lDataIni		:= .F.
Local lDataFim		:= .F.
Local lRet			:= .T.
Local lPeriodo		:= .F.

Begin Sequence

	IF !( lRet := NaoVazio() )
		Break
	EndIF
	
	IF ( ValType( cVar ) == "C" )
		cVar 		:= Upper( Alltrim( cVar ) )
		lDataIni	:= ( "PO_DATAINI" $ cVar )
		lDataFim	:= ( "PO_DATAFIM" $ cVar )
	EndIF
		
	IF ( ( lDataIni ) .or. ( lDataFim ) )

		dDataVld := &( cVar ) 
		IF ( lRet := !Empty( dDataVld ) )
			IF ( lDataIni )
				IF !Empty( M->PO_DATAFIM )
			 		lPeriodo := .T.
			 		IF !( lRet := ( dDataVld <= M->PO_DATAFIM ) )
						Help(" ",1,"PO_DATAINI")
						Break
			 		EndIF
			 	EndIF
			ElseIF ( lDataFim )
				lPeriodo := .T.
				IF !( lRet := ( dDataVld >= M->PO_DATAINI ) )
					Help(" ",1,"PO_DATAFIM")
					Break
				EndIF
			EndIF
		 	IF !( lRet := FreeForUse( "SPO" , xFilial( "SPO" ) + Dtos( dDataVld ) ) )
		 		Break
		 	EndIF
			IF ( lPeriodo )
				IF !( lRet := ValidPerSPO() )
					Break
				EndIF
			EndIF
		EndIF	

		IF ( lRet := ExistChav( "SPO" , Dtos( dDataVld ) , RetOrdem( "SPO" , "PO_FILIAL+ DTOS(PO_DATAINI)" ) ) )
			IF !( lRet := ExistChav( "SPO" , Dtos( dDataVld ) , RetOrdem( "SPO" , "PO_FILIAL+ DTOS(PO_DATAFIM)" ) ) )
				Break
			EndIF	
		Else
			Break
		EndIF

	EndIF

End Sequence
	
( RestArea( aAreaSPO ) , RestArea( aArea ) )

Return( lRet )

/*
зддддддддддбдддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPona290Inc	 Ё Autor ЁMarinaldo de Jesus   Ё Data Ё29/07/2002Ё
цддддддддддедддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁInclusao no Cadastro de Periodos de Apontamento       		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁPona290Inc( cAlias , nReg , nOpc )						 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                     		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁAxInclui(cAlias,nReg,nOpc,,,,"ValidPerSPO()") -> nOpcA		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁCadastro de Periodos de Apontamento                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pona290Inc( cAlias, nReg, nOpc )
Return( AxInclui( cAlias, nReg, 3, NIL, NIL, NIL, "ValidPerSPO()" ) )

/*
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPona290Alt	Ё Autor ЁMarinaldo de Jesus    Ё Data Ё29/07/2002Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁAlteracao no Cadastro de Periodos de Apontamento       		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁPona290Alt( cAlias , nReg , nOpc )						 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                     		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁAxAltera(cAlias,nReg,nOpc,,,,,"ValidPerSPO()") -> nOpcA	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁCadastro de Periodos de Apontamento                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pona290Alt( cAlias , nReg , nOpc )

Local aCpos		:= {}
Local aLastDet	:= ( cAlias )->( GdMontaCols( @aCpos , NIL , NIL , NIL , NIL , NIL , NIL , cAlias ) )
Local aNewDet	:= {}
Local nPosResp	:= 0
Local uRet

If SPO->PO_RESPONS == "S"
	Help(,, STR0018, , STR0021, 1, 0,,,,,, {STR0022}) //  "NЦo И possМvel alterar registros criados pelo sistema." - "O registro serА aberto apenas para visualizaГЦo."
	AxVisual(cAlias, nReg, 2 ) // Abre o registro como visualizar
Else
	uRet := AxAltera( cAlias, nReg, 4, NIL, NIL, NIL, NIL, "ValidPerSPO()")
	
	IF !( ( cAlias )->( Recno() ) == nReg )
		( cAlias )->( dbGoto( nReg ) )
	EndIF
	aNewDet := ( cAlias )->( GdMontaCols( aCpos , NIL , NIL , NIL , NIL , NIL , NIL , cAlias ) )
	
	IF !fCompArray( aNewDet , aLastDet )
		IF ( ( nPosResp := GdFieldPos( "PO_RESPONS" , aCpos ) ) > 0 )
			IF !( ( cAlias )->( Recno() ) == nReg )
				( cAlias )->( dbGoto( nReg ) )
			EndIF
			IF RecLock( cAlias , .F. )
				( cAlias )->( FieldPut( FieldPos( "PO_RESPONS" ) , "U" ) )
				( cAlias )->( MsUnLock() )
			EndIF
		EndIF
	EndIF
EndIf

Return( uRet )

/*
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPna290GerPa	Ё Autor ЁMarinaldo de Jesus    Ё Data Ё06/09/2002Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁGeracao de Periodos de Apontamentos no SPO             		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL															 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁCadastro de Periodos de Apontamento                          Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pna290GeraPa()
         
Local cPerg 		:= "PNA290"
Local lRet			:= .T.
Local uSaveMvPar	:= IF( Type("mv_par01") != "U" , mv_par01 , NIL )

Private lAbortPrint	:= .F.

IF ( lRet := Pergunte( cPerg , .T. ) )
	Processa( { || Pna290PutPer( ( Mv_Par01 + 1 ) ) } , STR0008 , NIL , .T. ) //Gerar Periodos de Apontamento
EndIF

IF !Empty( uSaveMvPar )
	mv_par01 := uSaveMvPar
EndIF

Return( MbrChgLoop( .F. ) )

/*
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPna290PutPerЁ Autor ЁMarinaldo de Jesus    Ё Data Ё06/09/2002Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁGeracao de Periodos de Apontamentos no SPO             		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL															 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPna290GeraPa()												 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function Pna290PutPer( nNumPer )

Local aPeriodos		:= {}
Local cFilSPO		:= xFilial( "SPO" )
Local dPerIni		:= Ctod("//")
Local dPerFim		:= Ctod("//")
Local dPerIAtu		:= Ctod("//")
Local dPerFAtu		:= Ctod("//")
Local nPeriodo		:= 0
Local nPeriodos		:= 0

MsAguarde( { || aPeriodos := GetPerAponta( nNumPer , cFilSPO , NIL , .F. ) } , STR0010 )	//"Aguarde... Montando Periodos de Apontamento"

IF ( ( nPeriodos := Len( aPeriodos ) ) > 0 )
	ProcRegua( nPeriodos )
	GetPonMesDat( @dPerIAtu , @dPerFAtu )
	For nPeriodo := 1 To nPeriodos
		IF ( ( nPeriodo == 1 ) .or. ( nPeriodo % 100 == 0 ) )
			IncProc( STR0009 ) //Gerando Periodos de Apontamento
		Else
			IncProc()
		EndIF
		IF ( lAbortPrint )
			Exit
		EndIF
		dPerIni	:= aPeriodos[ nPeriodo , 01 ]
		dPerFim	:= aPeriodos[ nPeriodo , 02 ]
		IF ( ( dPerIni >= dPerIAtu ) .and. ( dPerFim <= dPerFAtu ) )
			Loop
		EndIF
		PutPerSPO( cFilSPO , dPerIni , dPerFim , "0" , "U" )
	Next nPeriodo
EndIF

Return( NIL ) 

/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё MenuDef		ЁAutorЁ  Luiz Gustavo     Ё Data Ё30/11/2006Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁIsola opcoes de menu para que as opcoes da rotina possam    Ё
Ё          Ёser lidas pelas bibliotecas Framework da Versao 9.12 .      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA290                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function MenuDef()
	
	Local aRotina := {}
	
	aAdd(aRotina, {STR0001, "PesqBrw"			, 0, 1, , .T.})	// "Pesquisar"
	aAdd(aRotina, {STR0002, "AxVisual"			, 0, 2, , })	// "Visualizar"
	aAdd(aRotina, {STR0011, "PonModPer(.T.)"	, 0, 3, , })	// "Alt.Per.Atu"
	aAdd(aRotina, {STR0016, "CallPON470"   		, 0, 4, , })	// "Bloq. de Periodos"
	aAdd(aRotina, {STR0003, "Pona290Inc"		, 0, 3, , })	// "Incluir"
	aAdd(aRotina, {STR0004, "Pona290Alt"		, 0, 4, , })	// "Alterar"
	aAdd(aRotina, {STR0005, "Pona290Del"		, 0, 5, , })	// "Excluir"
	aAdd(aRotina, {STR0007, "Pna290GeraPa"		, 0, 3, , })	// "Gerar Per.Ant."
	
Return aRotina

/*/{Protheus.doc} CallPON470
Executa a view da rotina PONA470
@type  Function
@author CМcero Alves
@since 24/03/2022
/*/
Function CallPON470( cAlias, nReg, nOpc)
	
	FWExecView(STR0016, "PONA470", 4,, { || .T. })
	
Return Nil

/*/{Protheus.doc} Pona290Del
Valida e realiza a exclusЦo de um registro
@type Function
@author CМcero Alves
@since 30/05/2022
/*/
Function Pona290Del(cAlias, nRecno, nOpc, cTransact, aCpos, aButtons, aParam, aAuto, lMaximized)
	
	If SPO->PO_RESPONS == "S" // "Registro do Sistema"
		Help(,, STR0018, , STR0019, 1, 0,,,,,, {STR0020}) // "NЦo И possМvel excluir registros criados pelo sistema." - "Apenas registros cadastrados pelos usuАrios podem ser excluМdos."
	Else
		AxDeleta(cAlias, nRecno, 5, cTransact, aCpos, aButtons, aParam, aAuto, lMaximized)
	EndIf
	
Return
