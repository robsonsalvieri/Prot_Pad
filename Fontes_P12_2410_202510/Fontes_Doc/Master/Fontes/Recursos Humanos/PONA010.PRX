#INCLUDE "PROTHEUS.CH"
#INCLUDE "PONA010.CH"
#INCLUDE "PONCALEN.CH"

Static lPnaCel := ExistBlock( "PNA010CEL" )
/*/
зддддддддддбддддддддддбдддддбдддддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPONA010   ЁAutorЁMarinaldo de Jesus       Ё Data Ё27/08/2003Ё
цддддддддддеддддддддддадддддадддддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Marca┤■es                                       Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                                    Ё
цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё            ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL           Ё
цддддддддддддбддддддддддбдддддддддддбддддддддддддддддддддддддддддддддддд╢
ЁProgramador ЁData      ЁBOPS       ЁMotivo da Alteracao                Ё
цддддддддддддеддддддддддедддддддддддеддддддддддддддддддддддддддддддддддд╢
ЁCecilia Car.Ё29/05/14  ЁTPQAN3     ЁIncluido o fonte da 11 para a 12 e Ё
Ё            Ё14/06/11  Ё           Ёefetuada a limpeza.                Ё
юддддддддддддаддддддддддадддддддддддаддддддддддддддддддддддддддддддддддды/*/

Function PONA010( cAlias , nReg , nOpc , aNewAdvSize , lMinSize )

Local aSvKeys		  := GetKeys()
Local aArea			:= GetArea()
Local aAreaSRA		:= SRA->( GetArea() )
Local aAreaSP8		:= SP8->( GetArea() )
Local aAreaSPC		:= SPC->( GetArea() )
Local aIndexSRA		:= {}
Local cFiltraSRA	:= ""
Local cFunName		:= Upper( AllTrim( FunName() ) )

DEFAULT nOpc		:= 4
DEFAULT aNewAdvSize	:= MsAdvSize()
DEFAULT lMinSize	:= .F.

Private lIsPona010	:= ( cFunName == "PONA010" )

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSo Executa se o Modo de Acesso dos Arquivos do Ponto estiverem OK e o CaЁ
	Ёdastro de Funcionario Nao Estiver Vazio								 Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( ValidArqPon() .and. ChkVazio("SRA") )
		Break
	EndIF

	IF ( lIsPona010 )

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define Array contendo as Rotinas a executar do programa      Ё
		Ё ----------- Elementos contidos por dimensao ------------     Ё
		Ё 1. Nome a aparecer no cabecalho                              Ё
		Ё 2. Nome da Rotina associada                                  Ё
		Ё 3. Usado pela rotina                                         Ё
		Ё 4. Tipo de Transa┤└o a ser efetuada                          Ё
		Ё    1 - Pesquisa e Posiciona em um Banco de Dados             Ё
		Ё    2 - Simplesmente Mostra os Campos                         Ё
		Ё    3 - Inclui registros no Bancos de Dados                   Ё
		Ё    4 - Altera o registro corrente                            Ё
		Ё    5 - Remove o registro corrente do Banco de Dados          Ё
		Ё    6 - Legenda                                               Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		
		Private aRotina := MenuDef()
		
		Private bFiltraBrw	:= { || NIL }
		Private cCadastro   := OemToAnsi( STR0006 )	//"Cadastro de Marca┤■es"
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Inicializa o filtro utilizando a funcao FilBrowse                      Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		cFiltraRh	:= ChkRh( "PONA010" , "SRA" , "1" )
		bFiltraBrw 	:= { || FilBrowse( "SRA" , @aIndexSRA , @cFiltraRH ) }
		Eval( bFiltraBrw )
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Chama a Funcao de Montagem do Browse                                   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		mBrowse( 6 , 1 , 22 , 75 , "SRA"  , NIL , NIL , NIL , NIL , NIL , fCriaCor() )
    	/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Deleta o filtro utilizando a funcao FilBrowse                     	 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		EndFilBrw( "SRA" , aIndexSra )
	Else
		Pona010Mnt( cAlias , nReg , nOpc )
	EndIF

End Sequence
		
/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura os Dados de Entrada 											 Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestKeys( aSvKeys )
RestArea( aAreaSPC )
RestArea( aAreaSP8 )
RestArea( aAreaSRA )
RestArea( aArea )

Return( NIL )
/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPona010MntЁ Autor ЁMarinaldo de Jesus     Ё Data Ё18/06/2002Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁPona010Mnt( cAlias , nReg , nOpc )							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁcAlias = Alias do arquivo                                   Ё
Ё          ЁnReg   = Numero do registro                                 Ё
Ё          ЁnOpc   = Numero da opcao selecionada                        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPona010()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Pona010Mnt( cAlias , nReg , nOpc )

Local aArea				:= GetArea()
Local aSvKeys			:= GetKeys()
Local aButtons			:= {}
Local aInfoAdvSize		:= {}
Local aObjSize			:= {}
Local aObjCoords		:= {}
Local aSRAHeader		:= {}
Local aSRACols			:= {}
Local aSRAFields		:= {}
Local aSRAAltera		:= {}
Local aSRANotFields		:= {}
Local aSRARecnos		:= {}
Local aSP8GdAltera  	:= {}
Local aSP8GdNaoAlt		:= {}
Local aSP8Recnos		:= {}
Local aSP8NotFields		:= {}
Local aSP8VirtGd		:= {}
Local aSP8VisuGd		:= {}
Local aSP8MemoGd		:= {}
Local aSP8Header		:= {}
Local aSP8Cols			:= {}
Local aSP8Query			:= {}
Local aSP8Dele			:= {}
Local bSP8GdDelOk		:= { || Pona010DelOk( cAlias , nReg , nOpc , @aSP8Dele , @aGdSp8ColR ) }
Local bSP8KeySkip		:= { || .F. }
Local bSet15			:= { || NIL }
Local bSet24			:= { || NIL }
Local bDialogInit		:= { || NIL }
Local bOrdMarc			:= { || NIL }
Local bOrdMarcs			:= { || NIL }
Local bCalend			:= { || NIL }
LocAL bClrOrd			:= { || NIL }
Local bSrtOrd			:= { || NIL }
Local bFiltro			:= { || NIL }
Local bGdSeek			:= { || NIL }
Local cFilSRA			:= ""
Local cCodSRA			:= ""
Local cSRAKeySeek		:= ""
Local cPerg				:= "PNA010"
Local dPerIni			:= Ctod("//")
Local dPerFim			:= Ctod("//")
Local lSelePer			:= .F.
Local lSp8Lock			:= .F.
Local lSpcLock			:= .F.
Local nOpcAlt			:= 0
Local nSRAUsado			:= 0
Local nSP8Usado			:= 0
Local nLoop				:= 0
Local nLoops			:= 0
Local nOpcNewGd			:= IF( ( ( nOpc == 2 ) .or. ( nOpc == 5 ) ) , 0 , GD_UPDATE + GD_DELETE	)
Local oDlg				:= NIL
Local oEnchoice			:= NIL	
Local oGetDados			:= NIL

Private	aGdSp8ColR		:= {}
Private aGdSp8HedR		:= {}
Private aTabPadrao		:= {}
Private aTabCalend		:= {}
Private cLinOKUltKey	:= "__LinOKUltKey__"
Private cBldCldUltKey	:= "__cBldCldUltKey__"
Private lP8PGIniCa		:= .F.

DEFAULT lPnaCel	:= ExistBlock( "PNA010CEL" )
Begin Sequence

 
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Checa o periodo de Apontamento em Relacao ao Pergunte		   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( Pona010PergChk( cPerg , @aTabPadrao , @aTabCalend ) )
		Break
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Verifica o Periodo Para a Carga das Informacoes			   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	//"Deseja selecionar per║odo?"###
	IF ( lSelePer := MsgNoYes( OemToAnsi( STR0050 ) , cCadastro ) )
		IF !Pergunte( cPerg , .T. )
			Break
		EndIF
		dPerIni := MV_PAR01
		dPerFim := MV_PAR02
	EndIF	

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta os Dados para a Enchoice							   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdd( aSRANotFields , "RA_MAT"		)
	aAdd( aSRANotFields , "RA_NOME"		)
	aAdd( aSRANotFields , "RA_SITFOLH"	)
	aAdd( aSRANotFields , "RA_ADMISSA"	)
	aAdd( aSRANotFields , "RA_CATFUNC"	)
	aAdd( aSRANotFields , "RA_CODFUNC"	)
	aAdd( aSRANotFields , "RA_DESCFUN"	)
	aAdd( aSRANotFields , "RA_CC"	   	)
	aAdd( aSRANotFields , "RA_DESCCC"	)
	
	aAdd( aSRANotFields , "RA_REGRA"	)
	aAdd( aSRANotFields , "RA_CRACHA"	)
	aAdd( aSRANotFields , "RA_TNOTRAB"	)
	aAdd( aSRANotFields , "RA_SEQTURN"	)
	aAdd( aSRANotFields , "RA_DESCTUR"	)
	aSRACols		:= SRA->(;
								GDMontaCols(;
												@aSRAHeader,;	//01 -> Array com os Campos do Cabecalho da GetDados
												@nSRAUsado,;	//02 -> Numero de Campos em Uso
												NIL,;			//03 -> [@]Array com os Campos Virtuais
												NIL,;			//04 -> [@]Array com os Campos Visuais	
												NIL,;			//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
												aSRANotFields,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
												@aSRARecnos,;	//07 -> [@]Array unidimensional contendo os Recnos
												NIL,;			//08 -> Alias do Arquivo Pai
												NIL,;			//09 -> Chave para o Posicionamento no Alias Filho
												NIL,;			//10 -> Bloco para condicao de Loop While
												NIL,;			//11 -> Bloco para Skip no Loop While
												NIL,;			//12 -> Se Havera o Elemento de Delecao no aCols 
												NIL,;			//13 -> Se cria variaveis Publicas
												NIL,;			//14 -> Se Sera considerado o Inicializador Padrao
												NIL,;			//15 -> Lado para o inicializador padrao
												NIL,;			//16 -> Opcional, Carregar Todos os Campos
												NIL,;			//17 -> Opcional, Nao Carregar os Campos Virtuais
												NIL,;			//18 -> Opcional, Utilizacao de Query para Selecao de Dados
												NIL,;			//19 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
												NIL,;			//20 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
												NIL,;			//21 -> Carregar Coluna Fantasma e/ou BitMap ( Logico ou Array )
												.T.,;			//22 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
												NIL,;			//23 -> Verifica se Deve Checar se o campo eh usado
												NIL,;			//24 -> Verifica se Deve Checar o nivel do usuario
												NIL,;			//25 -> Verifica se Deve Carregar o Elemento Vazio no aCols
												NIL,;			//26 -> [@]Array que contera as chaves conforme recnos
												NIL,;			//27 -> [@]Se devera efetuar o Lock dos Registros
												NIL,;			//28 -> [@]Se devera obter a Exclusividade nas chaves dos registros
												NIL,;			//29 -> Numero maximo de Locks a ser efetuado
												NIL,;			//30 -> Utiliza Numeracao na GhostCol
												.F.;			//31 -> Carrega os Campos de Usuario
											);
							 )
	cFilSRA			:= SRA->RA_FILIAL
	cCodSRA			:= SRA->RA_MAT
	cSRAKeySeek		:= ( cFilSRA + 	cCodSRA )
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁBloqueia Chaves Logicas de Marcacoes do Funcionario           Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( lSp8Lock := Pona010Locks( nOpc , "SP8" ) )
		Break
	EndIF
	IF !( lSpcLock := Pona010Locks( nOpc , "SPC" ) )
       Break
    EndIF
    
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Cria as Variaveis de Memoria e Carrega os Dados Conforme o arЁ
	Ё quivo														   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	For nLoop := 1 To nSRAUsado
		aAdd( aSRAFields , aSRAHeader[ nLoop , 02 ] )
		SetMemVar( aSRAHeader[ nLoop , 02 ] , aSRACols[ 01 , nLoop ] , .T. )
	Next nLoop
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁAdiciona o Elemento "NOUSER" para que os Campos de Usuario naoЁ
	Ёsejam Carregados na MsMGet()								   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdd( aSRAFields , "NOUSER" )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define os Campos que nao serao carregados					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdd( aSP8NotFields , "P8_FILIAL"	)
	aAdd( aSP8NotFields , "P8_MAT"		)

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta os Dados para a GetDados							   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aSP8Query		:= Array( 09 )
	aSP8Query[01]	:= "P8_FILIAL='"+cFilSRA+"'"
	aSP8Query[02]	:= " AND "
	aSP8Query[03]	:= "P8_MAT='"+cCodSRA+"'"
	IF ( lSelePer )
		aSP8Query[04]	:= " AND "
		aSP8Query[05]	:= "P8_DATA>='"+Dtos( dPerIni )+"'"
		aSP8Query[06]	:= " AND "
		aSP8Query[07]	:= "P8_DATA<='"+Dtos( dPerFim )+"'"
	Else
		aSP8Query[04]	:= ""
		aSP8Query[05]	:= ""
		aSP8Query[06]	:= ""
		aSP8Query[07]	:= ""
	EndIF
	aSP8Query[08]	:= " AND "
	aSP8Query[09]	:= "D_E_L_E_T_=' ' "
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Quando For Inclusao Posiciona o SP8 No Final do Arquivo	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( nOpc == 3  ) //Inclusao
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Garante que na Inclusao o Ponteiro do SP8 estara em Eof()    Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		PutFileInEof( "SP8" )
	EndIf
	
	IF ( lSelePer )
		bSP8KeySkip := { || P8_DATA < dPerIni .or. P8_DATA > dPerFim }
	EndIF	
	SP8->( dbSetOrder( RetOrdem( "SP8" , "P8_FILIAL+P8_MAT+DTOS(P8_DATA)+STR(P8_HORA,5,2)" ) ) )
   	
	aSP8Cols:= SP8->(GDMontaCols(	@aSP8Header,;
									@nSP8Usado,;
									@aSP8VirtGd,;
									@aSP8VisuGd,;
									NIL,;
									aSP8NotFields,;
									@aSP8Recnos,;
									"SP8",;
									cSRAKeySeek,;
									NIL,;        //10
									bSP8KeySkip,;
									NIL,;
									NIL,;
									NIL,;
									NIL,;
									NIL,;
									NIL,;
									aSP8Query,;
									.F.,;
									.F.,;       //20
									.T.,;
									NIL,;
									NIL,;
									NIL,;
									NIL,;
									NIL,;
									NIL,;
									NIL,;
									NIL,;
									NIL,;		//30
									NIL,;
									nOpc;
								  );
					  )
					  
	aGdSp8HedR	:= aClone( aSP8Header )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё  Ponto-de-Entrada inclusЦo de novos campos				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( lPnaCel )
		IF ( ValType( uRet := ExecBlock("PNA010CEL",.F.,.F.,{ aSP8Cols } ) ) == "A" )
			aSP8Cols := aClone(uRet)
			uRet	 := NIL
		EndIF
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSe ja houver informacoes no SP8 nao Permite a Inclusao, apenasЁ
	Ёalteracao ou Exclusao										   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( ( nOpc == 3 ) .and. ( Len( aSP8Recnos ) > 0 ) )
		//"J═ Existem Marca┤■es cadastrados para este funcion═rio. Selecione a op┤└o de Altera┤└o"
		MsgInfo(  OemToAnsi( STR0007 ) , cCadastro )
		Break
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define os Campos Nao Alteraveis            				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	For nLoop := 1 To Len( aSP8MemoGd )
		aAdd( aSP8GdNaoAlt  , aSP8MemoGd[ nLoop , 02 ] )
	Next nLoop

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Campos que so poderao ser Alterados via Manutencao de   MarcaЁ
	Ё coes Normais												   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdd( aSP8GdNaoAlt , "P8_DATA"  )
	aAdd( aSP8GdNaoAlt , "P8_HORA"  )
	aAdd( aSP8GdNaoAlt , "P8_CC"    )
	aAdd( aSP8GdNaoAlt , "P8_FLAG"  )
	aAdd( aSP8GdNaoAlt , "P8_TURNO" )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Carrega os Campos Editaveis para a GetDados				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	For nLoop := 1	To nSP8Usado
		SetMemVar( aSP8Header[ nLoop , 02 ] , GetValType( aSP8Header[ nLoop , 08 ] , aSP8Header[ nLoop , 04 ] ) , .T. )
		IF (;
				(;
					( aScan( aSP8VirtGd		, aSP8Header[ nLoop , 02 ] ) == 0 );
					.and.;
		   			( aScan( aSP8VisuGd		, aSP8Header[ nLoop , 02 ] ) == 0 );
		   			.and.;
		   			( aScan( aSP8NotFields	, aSP8Header[ nLoop , 02 ] ) == 0 );
		   			.and.;
		   			( aScan( aSP8GdNaoAlt	, aSP8Header[ nLoop , 02 ] ) == 0 );
		   		);
		   		.or.;
		   		( aScan( aSP8MemoGd	, { |x| aSP8Header[ nLoop , 02 ] == x[1] } ) > 0 );
		  	)
			aAdd( aSP8GdAltera , aSP8Header[ nLoop , 02 ] )
		EndIF
	Next nLoop

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta as Dimensoes dos Objetos         					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdvSize		:= MsAdvSize()
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 0 }
	aAdd( aObjCoords , { 000 , 060 , .T. , .F. } )
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Bloco para o Botao e Tecla de Atalho de Insercao  deЁ
	ЁOrdem na Marcacao										      Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bOrdMarc	:= { ||	MsAguarde(;
									{ ||;
											Pona010MarcOrd(	oGetDados	,;
															nOpc		,;
															@aTabPadrao	,;
															@aTabCalend	 ;
														   );
								    },;
								    cCadastro	,;
								    OemToAnsi( STR0030 );	//"Ordenando Marca┤└o..."
								   ),;
						SetKey( VK_F4 , bOrdMarc	);
					}

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Bloco para o Botao e Tecla de Atalho de Insercao  deЁ
	ЁOrdens nas Marcacoes										  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bOrdMarcs	:= { || MsAguarde(;
									{ ||;
											Pona010OrdMarc(	oGetDados	,;
															nOpc		,;
															@aTabPadrao	 ;
														   );
									},;
									cCadastro	,;
									OemToAnsi( STR0014 );	//"Ordenando Marca┤■es..."
								 ),;
						SetKey( VK_F5 , bOrdMarcs );
					}

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Bloco para o Botao e Tecla de Atalho para a MontagemЁ
	Ёe Visualizacao do Calendario			 					  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bCalend		:= { ||	MsAguarde(;
									{ ||;
											Pona010CalMarc(	oGetDados	,;
															@aTabPadrao ,;
															@aTabCalend	 ;
										   				  );
									},;
									cCadastro,;
									OemToAnsi( STR0015 );	//"Calend═rio de Marca┤■es..."
								  ),;
						SetKey( VK_F6 , bCalend );
					 }

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Bloco para o Botao e Tecla de Atalho para Limpar   oЁ
	Ёconteudo do campo P8_ORDEM									  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bClrOrd		:= { ||	MsAguarde(;
									{ ||;
											Pona010ClrOrdM(	oGetDados , nOpc );
									},;
									cCadastro,;
									OemToAnsi( STR0019 );	//"Limpando as Ordens..."
								  ),;
						SetKey( VK_F7 , bClrOrd );
					}

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Bloco para o Botao e Tecla de Atalho que indexam  osЁ
	Ёlancamentos                								  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSrtOrd		:= { ||	MsAguarde(;
									{ ||;
											Pona010StrOrdM( oGetDados , nOpc );
									},;
									cCadastro,;
									OemToAnsi( STR0032 );	//"Indexando Lan┤amentos..."
								  ),;
						SetKey( VK_F8 , bSrtOrd );
					}
	
	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Bloco para o Botao e Tecla de Atalho que indexam  osЁ
	Ёlancamentos                								  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bFiltro		:= { ||	Pona010FltMarc(	oGetDados		,;
										nOpc			,;
										nOpcNewGd		,;
										aTabPadrao		,;
										@aTabCalend		,;
										dPerIni			,;
										dPerFim			,;
										aSP8GdAltera	,;
										bSP8GdDelOk		 ;
									   ),;
						SetKey( VK_F9 , bFiltro );
					}

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Botao para a Ordenacao da Marcacao Corrente		  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
		aAdd(; 
				aButtons	,;
								{;
									"BMPTRG"				,;	
		   							bOrdMarc				,;
		       	   					OemToAnsi( STR0029  )	,;	//"Ordenar Marca┤└o <F4>"
		       	   					OemToAnsi( STR0029  )	 ;	//"Ordenar"    
		           				};
		     )					 	
	EndIF

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Botao para a Ordenacao de Todas as Marcacoes		  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
		aAdd(; 
				aButtons	,;
								{;
									"AUTOM"					,;	
		   							bOrdMarcs				,;
		       	   					OemToAnsi( STR0013  )	,;	//"Ordenar Marca┤■es <F5>"
		       	   					OemToAnsi( STR0013  )	 ;	//"Ordenar" 
		           				};
		     )					 	
	EndIF
	
	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Botao para a Consulta ao Calendario de Marcacoes	  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdd(; 
			aButtons	,;
							{;
								"S4SB014N"			,;
   		       					bCalend				,;
   	    	   					OemToAnsi( STR0015 ),;//"Calend═rio de Marca┤■es <F6>"
   	    	   					OemToAnsi( STR0015 ) ;//"Calend."  	    	   					
   	        				};
   	     )

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Botao para Limpar as Ordens                     	  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
		aAdd(; 
				aButtons	,;
								{;
									"note"				,;
	   		       					bClrOrd					,;
	   	    	   					OemToAnsi( STR0018  ) 	,;//"Limpar as Ordens <F7>"
	   	    	   					OemToAnsi( STR0018  ) 	; //"Limpar"	  118 	    	   					
	   	        				};
	   	     )
	EndIF

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Botao para Indexar os Lancamentos               	  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
		aAdd(; 
				aButtons	,;
								{;
									"SDUORDER"				,;
	   		       					bSrtOrd					,;
	   	    	   					OemToAnsi( STR0031  ) 	,;//"Indexar Lan┤amentos <F8>"
	   	    	   					OemToAnsi( STR0031  ) 	;//"Indexar" 131
	   	        				};
	   	     )
	EndIF

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Botao para Filtrar os Lancamentos               	  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdd(; 
			aButtons	,;
							{;
								"FILTRO"			,;
   		       					bFiltro				,;
   	    	   					OemToAnsi( STR0042 ),;//"Filtrar Lan┤amentos <F9>"
   	    	   					OemToAnsi( STR0042 ) ;//"Filtrar" 142
   	        				};
   	     )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Botao de Pesquisa na GetDados					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bGdSeek	:= { ||	GdSeek( oGetDados ),;
					SetKey( VK_F10 , bGdSeek );
				   }
	aAdd(;
			aButtons	,;
							{;
								"pesquisa" 							,;
	   							bGdSeek								,;
	       	   					OemToAnsi( STR0001 + "...<10>"  )	,;	//"Pesquisar"
	       	   					OemToAnsi( STR0001+ " <10>")		 ;	//"Pesquisar"
	           				};
	     )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Bloco para a Teclas <CTRL-O>   ( Button OK da EnchoiЁ
	Ё ceBar )													   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet15		:= { || IF(; 
								( ( nOpc == 3 ) .or. ( nOpc == 4 ) );					//Inclusao ou Alteracao
								.and.;				
								oGetDados:TudoOk(),;									//Valida as Informacoes da GetDados
								(;
									nOpcAlt := 1,;
									RestKeys( aSvKeys ),;
									oDlg:End();
							 	),;
							 	IF(; 
							 		( ( nOpc == 3 ) .or. ( nOpc == 4 ) ) ,;				//Inclusao ou Visualizacao
							 			(;
							 				nOpcAlt := 0 ,;
							 				.F.;
							 			 ),;	
									(;
										nOpcAlt := IF( nOpc == 2 , 0 , 1 ) ,;		//Visualizacao ou Exclusao
										RestKeys( aSvKeys ),;
										oDlg:End();
							 		);
							 	  );
						   );
					 }
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o  Bloco  para a Teclas <CTRL-X> ( Button Cancel da EnЁ
	Ё choiceBar )												   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet24		:= { || ( nOpcAlt := 0 , RestKeys( aSvKeys ) , oDlg:End() ) }

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Bloco para o Init do Dialog						   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bDialogInit := { ||;
						IF( ( nOpc == 3 ) .or. ( nOpc == 4 ) , SetKey( VK_F4 , bOrdMarc	)	, NIL ),;
						IF( ( nOpc == 3 ) .or. ( nOpc == 4 ) , SetKey( VK_F5 , bOrdMarcs )	, NIL ),;
						SetKey( VK_F6 , bCalend  ),;
						IF( ( nOpc == 3 ) .or. ( nOpc == 4 ) , SetKey( VK_F7 , bClrOrd  )	, NIL ),;
						IF( ( nOpc == 3 ) .or. ( nOpc == 4 ) , SetKey( VK_F8 , bSrtOrd )	, NIL ),;
						SetKey( VK_F9  , bFiltro ),;
						SetKey( VK_F10 , bGdSeek ),;
						EnchoiceBar( oDlg , bSet15 , bSet24 , NIL , aButtons );
					}
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta o Dialogo Principal para a Manutencao das Marcacoes    Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	DEFINE MSDIALOG oDlg TITLE OemToAnsi( STR0006 ) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta o Objeto Enchoice para o SRA                      	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		oEnchoice	:= MsmGet():New(	"SRA"		,;
										nReg		,;
										2			,;
										NIL			,;
										NIL			,;
										NIL			,;
										aSRAFields	,;
										aObjSize[1]	,;
										aSRAAltera	,;
										NIL			,;
										NIL			,;
										NIL			,;
										oDlg		,;
										NIL			,;
										.F.			 ;
									)
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta o Objeto GetDados para o SP8						   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		oGetDados	:= MsNewGetDados():New(	aObjSize[2,1]								,;
											aObjSize[2,2]								,;
											aObjSize[2,3]								,;
											aObjSize[2,4]								,;
											nOpcNewGd									,;
											"Pona010LinOk"								,;
											"Pona010TudOk"								,;
											""											,;
											aSP8GdAltera								,;
											0											,;
											Len(aSP8Cols)								,;
											NIL											,;
											NIL											,;
											bSP8GdDelOk									,;
											oDlg										,;
											aSP8Header									,;
											aSP8Cols									 ;
										 )
	ACTIVATE MSDIALOG oDlg CENTERED ON INIT Eval( bDialogInit )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁRestaura as Teclas de Atalho								   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	RestKeys( aSvKeys )
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁQuando Confirmada a Opcao e Nao for Visualizacao Grava ou   ExЁ
	Ёclui as Informacoes do SRA e SP8							   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF( nOpcAlt == 1 )
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Apenas se nao For Visualizacao              				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
 		IF ( nOpc != 2 )
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Gravando/Incluido ou Excluindo Informacoes do SRY/SP8        Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			MsAguarde(;
							{ ||;
									Pona010Grava(;
													nOpc			,;
													nReg			,;
													aSRAHeader		,;
													aSP8Header		,;
													oGetDados:aCols	,;
													aSP8VirtGd		,;
													aSP8Recnos		,;
													aSP8MemoGd		,;
													aTabPadrao		,;
													aTabCalend		 ;
												 );
							 },;
							 cCadastro;
						)
		EndIF
	EndIF

End Sequence         

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Libera Locks						                           Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
IF ( lSp8Lock )
	FreeLocks( "SP8" , NIL , .T. )
EndIF
IF ( lSpcLock )
	FreeLocks( "SPC", NIL , .T. )
EndIF

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura os Dados de Entrada								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestArea( aArea )

Return( nOpcAlt )

/*/
зддддддддддбддддддддддддбдддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPona010LinOkЁAutorЁMarinaldo de Jesus     Ё Data Ё18/06/2002Ё
цддддддддддеддддддддддддадддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁPona010LinOk( oBrowse )									    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ 															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁPona010()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Pona010LinOk( oBrowse )

Local aCposKey			:= {}
Local cOrdem			:= ""
Local cPeriodo			:= ""
Local cKeyAtu			:= ""
Local dPerIni			:= Ctod("//")
Local dPerFim			:= Ctod("//")
Local dPerIAux			:= Ctod("//")
Local dPerFAux			:= Ctod("//")
Local dDataRef			:= Ctod("//")
Local lLinOk			:= .T.
Local lNotMcIgual		:= ( SuperGetMv( "MV_MCIGUAL" , NIL , "N" , cFilAnt ) == "N" )

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Coloca o Cursor do Mouse em estado de Espera  	  		  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Evitar que os Inicializadores padroes sejam carregados indeviЁ
	Ё damente													   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	PutFileInEof( "SP8" )
	
	Begin Sequence

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Se a Linha da GetDados Nao Estiver Deletada				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF !( GdDeleted() )
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Verifica Itens Duplicados na GetDados	apenas se o conteudo doЁ
			Ё parametro MV_MCIGUAL for igual a "N"						   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			IF ( lNotMcIgual )
				aCposKey := { "P8_DATA" , "P8_HORA" }
				IF !( lLinOk := GdCheckKey( aCposKey , 4 ) )
					Break
				EndIF
			EndIF	

			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Verifica Se o Campos Estao Devidamente Preenchidos		   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			aCposKey := { "P8_DATA" , "P8_PAPONTA" }
			IF !( lLinOk := GdNoEmpty( aCposKey ) )
		    	Break
			EndIF

			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Carrega o Periodo Informado                       		   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			cPeriodo	:= GdFieldGet("P8_PAPONTA")
			dPerIni	    := Stod( SubStr( cPeriodo , 1 , 8 ) )
			dPerFim	    := Stod( SubStr( cPeriodo , 9 , 8 ) )
			dDataRef	:= dPerFim
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Valida Periodo e Carrega Calendario de Marcacoes  		   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			cKeyAtu := ( cEmpAnt + SRA->( RA_FILIAL + RA_MAT ) + ( Dtos( dPerIni ) + Dtos( dPerFim ) ) )
			IF ( !( cLinOKUltKey == cKeyAtu ) .or. Empty( aTabCalend ) )
				cLinOKUltKey := cKeyAtu
				/*/
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Verifica se o periodo informado eh valido         		   Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				IF !( lLinOk := PerAponta(	@dPerIAux 		,;	//Data Inicial passada como referencia
											@dPerFAux 		,;	//Data Final   passada como referencia
											dDataRef		,;	//Data Base
											.T.				,;	//Mostrar o Help
											xFilial("SRA")	,;	//Filial para GetMv
											.F.				 ;	//Se eh para gerar um novo periodo
		 								  );
					)		 			 					
					cLinOKUltKey := "__LinOKUltKey__"
					Break
				EndIF
				IF !( lLinOk := ( ( dPerIni == dPerIAux ) .and. ( dPerFim == dPerFAux ) ) )
					//"O per║odo informado n└o ┌ v═lido"###"Aviso de Inconsist┬ncia"
            		MsgInfo( OemToAnsi( STR0012 ) , OemToAnsi( STR0010 ) )
					cLinOKUltKey := "__LinOKUltKey__"
					Break
				EndIF
				/*/
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Monta Calendadrio Conforme Periodo Para Validacao da Ordem   Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				IF !( lLinOk := Pona010BldCld( @aTabPadrao , @aTabCalend , dPerIni , dPerFim ) )
            		cLinOKUltKey := "__LinOKUltKey__"
            		Break
				EndIF
			EndIF	

			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Verifica se a Ordem Informaca eh Valida           		   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			IF !Empty( cOrdem := GdFieldGet("P8_ORDEM") )
				IF !( lLinOk := ( aScan( aTabCalend , { |x| x[ CALEND_POS_ORDEM ] == cOrdem .and. x[ CALEND_POS_TIPO_MARC ] == "1E" } ) > 0 ) )
					//"A Ordem informada n└o ┌ v═lida para o per║odo"###"Aviso de Inconsist┬ncia"
            		MsgInfo( OemToAnsi( STR0011 ) , OemToAnsi( STR0010 ) )
					Break
				EndIF
			EndIF	
		
		EndIF
		
	End Sequence
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSe Houver Alguma Inconsistencia na GetDados, Seta-lhe o Foco  Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( lLinOk )
		oBrowse:SetFocus()
	EndIF

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura o Estado do Cursor								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()
	
Return( lLinOk )

/*/
зддддддддддбддддддддддддбдддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPona010TudOkЁAutorЁMarinaldo de Jesus     Ё Data Ё18/06/2002Ё
цддддддддддеддддддддддддадддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁPona010TudOk( oBrowse )									    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ 															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁPona010()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Pona010TudOk( oBrowse )

Local lTudoOk := .T.

Return( lTudoOk  )

/*/
зддддддддддбддддддддддддбдддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPona010DelOkЁAutorЁMarinaldo de Jesus     Ё Data Ё18/07/2003Ё
цддддддддддеддддддддддддадддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁValidar a Delecao na GetDados                               Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁPona010TudOk( oBrowse )									    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ 															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁPona010()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Pona010DelOk( cAlias , nRecno , nOpc , aSP8Dele , aGdSp8ColR )
         
Local bAscan		:= { || NIL }
Local bAeval		:= { || NIL }
Local cOrdem		:= ""
Local cPaponta		:= ""
Local dData			:= Ctod("//")
Local lDelOk 		:= .T.
Local lStatusDel	:= .F.
Local nHora			:= 0
Local nPosData		:= 0
Local nPosHora		:= 0
Local nPosOrdem		:= 0
Local nPosAponta	:= 0
Local nPosPaponta	:= 0
Local nPosDeleted	:= 0
Local nExistDele	:= 0
Local nExistRel		:= 0
Local nLoop			:= 0
Local nLoops		:= 0

Static lFirstDelOk
Static lLstDelOk

DEFAULT lFirstDelOk	:= .T.
DEFAULT lLstDelOk	:= .T.

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Coloca o Cursor do Mouse em estado de Espera  	  		  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()
	Begin Sequence
	
		//Apenas se for a primeira vez
		IF !( lFirstDelOk )
			lFirstDelOk	:= .T.
			lDelOk 		:= lLstDelOk
			lLstDelOk	:= .T.
			Break
		EndIF
	
		lStatusDel	:= !( GdDeleted() ) //Inverte o Estado
	
		bAscan		:= { |x| x[nPosData] == dData .and. x[ nPosHora ] == nHora }
		bAeval		:= { |x| IF(;
									( x[nPosOrdem] == cOrdem );
									.and.;
									( x[nPosPaponta] == cPaponta ) ,;
									(;
										dData	:= x[nPosData],;
										nHora	:= x[nPosHora],;
										IF(;
												( aScan( aGdSp8ColR , bAscan ) == 0 ) ,;
												aAdd( aGdSp8ColR , aClone( x ) ) ,;
												NIL;
										  ),;
										x[ nPosAponta ] := "N";
									),;
									NIL;	
								);	
					 	}
		cOrdem		:= GdFieldGet( "P8_ORDEM"	)
		cPaponta	:= GdFieldGet( "P8_PAPONTA"	)
		dData		:= GdFieldGet( "P8_DATA"	)
		nHora		:= GdFieldGet( "P8_HORA"	)
		nPosData	:= GdFieldPos( "P8_DATA"	)
		nPosHora	:= GdFieldPos( "P8_HORA"	)
		nPosOrdem	:= GdFieldPos( "P8_ORDEM"	)
		nPosAponta	:= GdFieldPos( "P8_APONTA"	)
		nPosPaponta	:= GdFieldPos( "P8_PAPONTA"	)
		nPosDeleted	:= GdFieldPos( "GDDELETED"	)
	
		IF ( lStatusDel )	//Deletar
			IF ( ( nExistDele := aScan( aSP8Dele , bAscan ) ) == 0 )
				aAdd( aSP8Dele , aClone( aCols[ n ] ) )
				aEval( aCols , bAeval )
			EndIF
		Else				//Restaurar
			IF ( ( nExistDele := aScan( aSP8Dele , bAscan ) ) > 0 )
				nLoops := Len( aGdSp8ColR )
				For nLoop := 1 To nLoops
					dData	:= aGdSp8ColR[ nLoop , nPosData		]
					nHora	:= aGdSp8ColR[ nLoop , nPosHora		]
					IF ( ( nExistRel := aScan( aCols , bAscan ) ) > 0 )
						IF (;
								( aCols[ nExistRel , nPosPaponta ] == aGdSp8ColR[ nLoop , nPosPaponta ] );
								.and.;
								( aCols[ nExistRel , nPosOrdem	 ] == aGdSp8ColR[ nLoop , nPosOrdem	  ] );
							)								
							IF !( aCols[ nExistRel , nPosDeleted ] )
								aCols[ nExistRel , nPosAponta ] := aGdSp8ColR[ nLoop , nPosAponta ]
							ElseIF (;
										( aCols[ nExistRel , nPosData ] == aSP8Dele[ nExistDele , nPosData ] );
										.and.;
										( aCols[ nExistRel , nPosHora ] == aSP8Dele[ nExistDele , nPosHora ] );
									)
								aCols[ nExistRel , nPosAponta ] := aGdSp8ColR[ nLoop , nPosAponta ]
							EndIF
						EndIF
					EndIF
				Next nLoop
				aDel( aSP8Dele , nExistDele )
				aSize( aSP8Dele , ( Len( aSP8Dele ) - 1 ) )
			EndIF
		EndIF
	
		//Ja Passou pela funcao
		lFirstDelOk := .F.
	
	End Sequence
/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura o Cursor do Mouse								  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()
		
Return( lDelOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPona010Grava ЁAutorЁMarinaldo de Jesus    Ё Data Ё27/08/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁGrava as Informacoes no SP8                                 Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁPona010()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Pona010Grava(	nOpc		,;	//Opcao de Acordo com aRotina
							 	nReg		,;	//Numero do Registro do Arquivo Pai ( SRA )
							 	aSRAHeader	,;	//Campos do Arquivo Pai ( SRA )
							 	aSP8Header	,;	//Campos do Arquivo Filho ( SP8 )
							 	aSP8Cols	,;	//Itens do Arquivo Filho ( SP8 )
							 	aSP8VirtGd	,;	//Campos Virtuais do Arquivo Filho ( SP8 )
							 	aSP8Recnos	,;	//Recnos do Arquivo Filho ( SP8 )
							 	aSP8MemoGd	,;	//Campos Memo na GetDados ( SP8 )
							 	aTabPadrao 	,;	//Array com as Informacoes para Montagem do Calendario de Marcacoes
							 	aTabCalend	 ;	//Array contendo as Informacoes do Calendario de Marcacoes
							  )

Local aTamHora		:= TamSX3( "P8_HORA" )
Local aPeriodos		:= {}
Local aSP8PutMsMm	:= {}
Local cFil			:= SRA->RA_FILIAL
Local cMat			:= SRA->RA_MAT
Local cFilMat		:= ( cFil + cMat )
Local cPeriodo		:= ""
Local cOrdem		:= ""
Local cChave		:= ""
Local cMsgErr		:= ""
Local dPerIni		:= Ctod("//")
Local dPerFim		:= Ctod("//")
Local lSPCDelInfo	:= .F.
Local lExistOrdDel	:= .F.
Local lAddNew		:= .F.
Local lNotMcIgual	:= ( SuperGetMv( "MV_MCIGUAL" , NIL , "N" , cFilAnt ) == "N" )
Local lLock			:= .F.
Local nHeader		:= 0
Local nHeaders		:= 0
Local nCol			:= 0
Local nCols			:= 0
Local nRecno		:= 0
Local nRecnos		:= 0
Local nCpoMemo		:= 0
Local nLoop			:= 0
Local nLoops		:= 0
Local nOrdem		:= 0
Local nOrdens		:= 0
Local nPosPer		:= 0
Local nPosCalend	:= 0
Local nDeleted		:= GdFieldPos( "GDDELETED"	, aSP8Header )
Local nPosPaponta	:= GdFieldPos( "P8_PAPONTA" , aSP8Header )
Local nPosOrdem		:= GdFieldPos( "P8_ORDEM"	, aSP8Header )
Local nPosAponta	:= GdFieldPos( "P8_APONTA"	, aSP8Header )
Local nPosData		:= GdFieldPos( "P8_DATA"	, aSP8Header )
Local nPosHora		:= GdFieldPos( "P8_HORA"	, aSP8Header )

DEFAULT nOpc		:= 0
DEFAULT nReg		:= 0
DEFAULT aSRAHeader	:= {}
DEFAULT aSP8Header	:= {}
DEFAULT aSP8Cols	:= {}
DEFAULT aSP8VirtGd	:= {}
DEFAULT aSP8Recnos	:= {}

nHeaders			:= Len(	aSP8Header	)
nCols				:= Len(	aSP8Cols	)
nRecnos				:= Len( aSP8Recnos	)

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Apenas se Existir o Participante							   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
IF !Empty( nReg )
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Se for Exclusao ( nOpc == 5 )								   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( nOpc == 5 )
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Carrega os Periodos para a Exclusao dos Apontamentos		   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		nLoops := Len( aSP8Cols )
		For nLoop := 1 To nLoops
			IF Empty( cPeriodo := aSP8Cols[ nLoop , nPosPaponta ] )
				Loop
			EndIF
			IF ( aScan( aPeriodos , { |x| ( x[1] == cPeriodo ) } ) == 0 )
				dPerIni	    := Stod( SubStr( cPeriodo , 1 , 8 ) )
				dPerFim	    := Stod( SubStr( cPeriodo , 9 , 8 ) )
				aAdd( aPeriodos , { cPeriodo , dPerIni , dPerFim } )
			EndIF
		Next nLoop
		//""Excluir, al┬m dos apontamentos calculados pelo sistema, os apontamentos informados pelo usu═rio?"
		IF ( SPC->( dbSeek( cFilMat , .F. ) ) )
			lSPCDelInfo := MsgNoYes( OemToAnsi( STR0037 ) , cCadastro ) 
		EndIF
		/*/
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Coloca o Cursor do Mouse em estado de Espera  	  		  Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		CursorWait()
			Begin Transaction
				/*/
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Exclui os Apontamentos									   Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				nLoops := Len( aPeriodos )
				For nLoop := 1 To nLoops
					MsProcTxt( OemToAnsi( STR0040 ) )	//"Excluindo Apontamentos..."
					cPeriodo	:= aPeriodos[ nLoop , 01 ]
					dPerIni		:= aPeriodos[ nLoop , 02 ]
					dPerFim		:= aPeriodos[ nLoop , 03 ]
					SP8DelSPC( dPerIni , dPerFim , cFil , cMat , lSPCDelInfo )
				Next nLoop
				MsProcTxt( OemToAnsi( STR0039 ) )	//"Excluindo Marca┤■es..."
				/*/
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Deleta os Memos											   Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				MsMmDel( "SP8" , aSP8Recnos , aSP8MemoGd )
				/*/
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Deleta os Dados do Arquivo Filho							   Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				PonDelRecnos( "SP8" , aSP8Recnos )
			End Transaction
		/*/
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Restaura o Cursor do Mouse								  Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		CursorArrow()
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Se for Inclusao/Alteracao ( nOpc == 3 .or. nOpc == 4 )	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	ElseIF ( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
		/*/
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Coloca o Cursor do Mouse em estado de Espera  	  		  Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		CursorWait()
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁCarrega os Periodos para a Exclusao dos Apontamentos corresponЁ
			Ёdente a Marcacoes que serao Deletadas ou que foram    flegadasЁ
			Ёcomo nao Apontadas											   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			nLoops := Len( aSP8Cols )
			For nLoop := 1 To nLoops
				IF Empty( cPeriodo := aSP8Cols[ nLoop , nPosPaponta ] )
					Loop
				EndIF
				IF ( aSP8Cols[ nLoop , nDeleted ] .or. !( aSP8Cols[ nLoop , nPosAponta ] == "S" ) )
					lExistOrdDel	:= .T.
					cOrdem			:= aSP8Cols[ nLoop , nPosOrdem ]
				EndIF
				IF ( ( nPosPer := aScan( aPeriodos , { |x| ( x[1] == cPeriodo ) } ) ) == 0 )
					dPerIni	    := Stod( SubStr( cPeriodo , 1 , 8 ) )
					dPerFim	    := Stod( SubStr( cPeriodo , 9 , 8 ) )
					aAdd( aPeriodos , { cPeriodo , dPerIni , dPerFim , { cOrdem } } )
				Else
					IF ( aScan( aPeriodos[ nPosPer , 04 ] , { |x| ( x == cOrdem ) } ) == 0 )
						aAdd( aPeriodos[ nPosPer , 04 ] , cOrdem )
					EndIF
				EndIF
			Next nLoop
			IF ( ( lExistOrdDel ) .and. SPC->( dbSeek( cFilMat , .F. ) ) )
				//""Excluir, al┬m dos apontamentos calculados pelo sistema, os apontamentos informados pelo usu═rio?"
				lSPCDelInfo := MsgNoYes( OemToAnsi( STR0037 ) , cCadastro ) 
			EndIF
			Begin Transaction
				/*/
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Garante o Posicionamento no Arquivo Pai                	   Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				SRA->( MsGoto( nReg ) )
				/*/
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Grava os Dados do Arquivo Filho                        	   Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				IF ( nRecnos == nCols )
					For nRecno := 1 To nRecnos
						SP8->( dbGoto( aSP8Recnos[ nRecno ] ) )
						IF !( lLock := RecLock( "SP8" , .F. , .F. ) )
							Loop
						EndIF
						IF !( aSP8Cols[ nRecno , nDeleted ] )
							MsProcTxt( OemToAnsi( STR0038 ) )	//"Gravando Marca┤■es..."
							For nHeader := 1 To nHeaders
								IF ( aScan( aSP8VirtGd , { |cCpo| ( cCpo == aSP8Header[ nHeader , 02 ] ) } ) == 0 )
									SP8->( &( aSP8Header[ nHeader , 02 ] ) ) := aSP8Cols[ nRecno , nHeader ]
								ElseIF ( ( nCpoMemo := aScan( aSP8MemoGd , { |cCpo| ( cCpo[1] == aSP8Header[ nHeader , 02 ] ) } ) ) > 0 )
									/*/
									здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
									Ё Armazena as Informacoes que serao gravadas no SYP      	   Ё
									юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
									SP8->( aAdd( aSP8PutMsMm , { Recno() , &("SP8->"+aSP8MemoGd[nCpoMemo,02]) , aSP8Cols[ nRecno , nHeader ] , aSP8MemoGd[nCpoMemo,02] } ) )
								EndIF
							Next nHeader
						Else
							MsProcTxt( OemToAnsi( STR0039 ) ) //"Excluindo Marca┤■es..."
							/*/
							здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							Ё Deleta os Memos do Arquivo Filho                       	   Ё
							юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
							MsMmDel( "SP8" , { aSP8Recnos[ nRecno ] } , aSP8MemoGd )
							/*/
							здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							Ё Deleta o Registro do Arquivo Filho                    	   Ё
							юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
							IF !SP8->( FkDelete( @cMsgErr ) )
								RollBackDelTran( cMsgErr )
							EndIF
						EndIF
						SP8->( MsUnlock() )
					Next nRecno
				ElseIF ( nRecnos < nCols )
					For nRecno := 1 To nRecnos
						SP8->( dbGoto( aSP8Recnos[ nRecno ] ) )
						IF !( lLock := RecLock( "SP8" , .F. , .F. ) )
							Loop
						EndIF
						IF !( aSP8Cols[ nRecno , nDeleted ] )
							MsProcTxt( OemToAnsi( STR0038 ) )	//"Gravando Marca┤■es..."
							SP8->P8_FILIAL	:= cFil
							SP8->P8_MAT		:= cMat
							For nHeader := 1 To nHeaders
								IF ( aScan( aSP8VirtGd , { |cCpo| ( cCpo == aSP8Header[ nHeader , 02 ] ) } ) == 0 )
									SP8->( &( aSP8Header[ nHeader , 02 ] ) ) := aSP8Cols[ nRecno , nHeader ]
								ElseIF ( ( nCpoMemo := aScan( aSP8MemoGd , { |cCpo| ( cCpo[1] == aSP8Header[ nHeader , 02 ] ) } ) ) > 0 )
									/*/
									здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
									Ё Armazena as Informacoes que serao gravadas no SYP      	   Ё
									юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
									SP8->( aAdd( aSP8PutMsMm , { Recno() , &("SP8->"+aSP8MemoGd[nCpoMemo,02]) , aSP8Cols[ nRecno , nHeader ] , aSP8MemoGd[nCpoMemo,02] } ) )
								EndIF
							Next nHeader
						Else
							MsProcTxt( OemToAnsi( STR0039 ) ) //"Excluindo Marca┤■es..."
							/*/
							здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							Ё Deleta os Memos do Arquivo Filho                       	   Ё
							юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
							MsMmDel( "SP8" , { aSP8Recnos[ nRecno ] } , aSP8MemoGd )
							/*/
							здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							Ё Deleta o Registro do Arquivo Filho                    	   Ё
							юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
							IF !SP8->( FkDelete( @cMsgErr ) )
								RollBackDelTran( cMsgErr )
							EndIF
						EndIF
						SP8->( MsUnlock() )
					Next nRecno	
					/*/
					здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					Ё Seta a Ordem do SP8                                   	   Ё
					юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
					SP8->( dbSetOrder( RetOrdem( "SP8" , "P8_FILIAL+P8_MAT+DTOS(P8_DATA)+STR(P8_HORA,5,2)" ) ) )
					/*/
					здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					Ё Grava os Novos Registros                              	   Ё
					юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
					For nCol := nRecno To nCols
						MsProcTxt( OemToAnsi( STR0038 ) )	//"Gravando Marca┤■es..."
						IF !( aSP8Cols[ nCol , nDeleted ] )
							/*/
							здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							Ё Verifica se ja Existe o Registro e efetua o Lock correspondenЁ
							Ё te														   Ё
							юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
							cChave	:= ( cFilMat + Dtos( aSP8Cols[ nCol , nPosData ] ) + Str( aSP8Cols[ nCol , nPosHora ] , aTamHora[1] , aTamHora[2] ) )
							IF !( lAddNew := !( SP8->( dbSeek( cChave , .F. ) ) ) )
								IF !( lNotMcIgual )
									lAddNew := .T. 
								EndIF
							EndIF
							IF !( lLock := RecLock( "SP8" , lAddNew , .F. ) )
								Loop
							EndIF
							SP8->P8_FILIAL	:= cFil
							SP8->P8_MAT		:= cMat
							For nHeader := 1 To nHeaders
								IF ( aScan( aSP8VirtGd , { |cCpo| ( cCpo == aSP8Header[ nHeader , 02 ] ) } ) == 0 )
									SP8->( &( aSP8Header[ nHeader , 02 ] ) ) := aSP8Cols[ nCol , nHeader ]
								ElseIF ( ( nCpoMemo := aScan( aSP8MemoGd , { |cCpo| ( cCpo[1] == aSP8Header[ nHeader , 02 ] ) } ) ) > 0 )
									/*/
									здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
									Ё Armazena as Informacoes que serao gravadas no SYP      	   Ё
									юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
									SP8->( aAdd( aSP8PutMsMm , { Recno() , &("SP8->"+aSP8MemoGd[nCpoMemo,02]) , aSP8Cols[ nCol , nHeader ] , aSP8MemoGd[nCpoMemo,02] } ) )
								EndIF
							Next nHeader
							SP8->( MsUnlock() )
						EndIF
					Next nCol
				EndIF
				/*/
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Grava as Informacoes no SYP                           	   Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				nRecnos := Len( aSP8PutMsMm )
				For nRecno := 1 To nRecnos
					SP8->( dbGoto( aSP8PutMsMm[ nRecno , 01 ] ) )
					MsMm( aSP8PutMsMm[ nRecno , 02 ] , NIL , NIL , aSP8PutMsMm[ nRecno , 03 ] , 1 , NIL , NIL , "SP8" , aSP8PutMsMm[ nRecno , 04 ] )
				Next nRecno
				/*/
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Deleta as Informacoes do SPC para o Reapontamento     	   Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				IF SPC->( dbSeek( cFilMat , .F. ) )
					nLoops := Len( aPeriodos )
					For nLoop := 1 To nLoops
						MsProcTxt( OemToAnsi( STR0040 ) )	//"Excluindo Apontamentos..."
						cPeriodo	:= aPeriodos[ nLoop , 01 ]
						dPerIni		:= aPeriodos[ nLoop , 02 ]
						dPerFim		:= aPeriodos[ nLoop , 03 ]
						IF !( Pona010BldCld( @aTabPadrao , @aTabCalend , dPerIni , dPerFim , .F. ) )
							Loop
						EndIF
						nOrdens := Len( aPeriodos[ nLoop , 04 ] )
						For nOrdem := 1 To nOrdens
							cOrdem := aPeriodos[ nLoop , 04 , nOrdem ]
							IF (;
									(;
										nPosCalend := aScan( aTabCalend , {;
																				|x| ( x[ CALEND_POS_ORDEM ] == cOrdem ); 
																					.and.;
																					( x[ CALEND_POS_TIPO_MARC ] == "1E" );
																	  	  };
													        );
									) > 0;
								)
								dPerIni := aTabCalend[ nPosCalend , CALEND_POS_DATA ]
								dPerFim := dPerIni
								SP8DelSPC( dPerIni , dPerFim , cFil , cMat , lSPCDelInfo )
							EndIF	
						Next nOrdem	
					Next nLoop
				EndIF
			End Transaction
		/*/
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Restaura o Cursor do Mouse								  Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		CursorArrow()
	EndIF
EndIF
	
Return( NIL )

/*/
зддддддддддбддддддддддддддбдддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPona010MarcOrdЁAutorЁMarinaldo de Jesus   Ё Data Ё27/08/2003Ё
цддддддддддеддддддддддддддадддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁOrdenar a Marcacao Corrente                                 Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁPona010()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Pona010MarcOrd( oGetDados , nOpc , aTabPadrao , aTabCalend )

Local aSvKeys			:= GetKeys()
Local aMarcacoes		:= {}
Local cPeriodo			:= ""
Local dPerIni			:= Ctod("//")
Local dPerFim			:= Ctod("//")
Local lReordena			:= .F.
Local nPosData			:= GdFieldPos( "P8_DATA" 	, oGetDados:aHeader )
Local nPosHora			:= GdFieldPos( "P8_HORA" 	, oGetDados:aHeader )
Local nPosOrdem			:= GdFieldPos( "P8_ORDEM" 	, oGetDados:aHeader )
Local nPosFlag			:= GdFieldPos( "P8_FLAG" 	, oGetDados:aHeader )
Local nPosTno			:= GdFieldPos( "P8_TURNO" 	, oGetDados:aHeader )
Local nPosFunc			:= GdFieldPos( "P8_FUNCAO" 	, oGetDados:aHeader )
Local nPosGiro			:= GdFieldPos( "P8_GIRO" 	, oGetDados:aHeader )
Local nPosCc			:= GdFieldPos( "P8_CC" 		, oGetDados:aHeader )
Local nPosApont			:= GdFieldPos( "P8_APONTA" 	, oGetDados:aHeader )
Local nPosRelog			:= GdFieldPos( "P8_RELOGIO"	, oGetDados:aHeader )
Local nPosTpMarc		:= GdFieldPos( "P8_TPMARCA"	, oGetDados:aHeader )
Local nPosPaponta		:= GdFieldPos( "P8_PAPONTA" , oGetDados:aHeader )
Local nPosDeleted		:= GdFieldPos( "GDDELETED"	, oGetDados:aHeader )
Local nLenMarc			:= 0
Local nAt				:= oGetDados:oBrowse:nAt

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Apenas na Inclusao no Alteraca         					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
		Break
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Nao Ordena Marcacao Deletada           					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( oGetDados:aCols[ nAt , nPosDeleted ]  )
		Break
	EndIF

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Coloca o Cursor do Mouse em estado de Espera  	  		  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	CursorWait()

		cPeriodo	:= oGetDados:aCols[ nAt , nPosPaponta ] 
		dPerIni	    := Stod( SubStr( cPeriodo , 1 , 8 ) )
		dPerFim	    := Stod( SubStr( cPeriodo , 9 , 8 ) )
		aAdd( aMarcacoes , Array( ELEMENTOS_AMARC ) )
		nLenMarc := Len( aMarcacoes )
		aMarcacoes[ nLenMarc , AMARC_DATA   	] := oGetDados:aCols[ nAt , nPosData	]					//01 - Data
		aMarcacoes[ nLenMarc , AMARC_HORA   	] := oGetDados:aCols[ nAt , nPosHora	]					//02 - Hora
		aMarcacoes[ nLenMarc , AMARC_ORDEM  	] := oGetDados:aCols[ nAt , nPosOrdem	]					//03 - Ordem
		aMarcacoes[ nLenMarc , AMARC_FLAG   	] := oGetDados:aCols[ nAt , nPosFlag	]					//04 - Flag
		aMarcacoes[ nLenMarc , AMARC_RECNO 		] := 0   													//05 - Recno
		aMarcacoes[ nLenMarc , AMARC_TURNO		] := oGetDados:aCols[ nAt , nPosTno		]					//06 - Turno
		aMarcacoes[ nLenMarc , AMARC_FUNCAO		] := oGetDados:aCols[ nAt , nPosFunc	]					//07 - Funcao 
		aMarcacoes[ nLenMarc , AMARC_GIRO		] := oGetDados:aCols[ nAt , nPosGiro	]					//08 - Giro
		aMarcacoes[ nLenMarc , AMARC_CC			] := oGetDados:aCols[ nAt , nPosCc		]					//09 - Centro de Custo
		aMarcacoes[ nLenMarc , AMARC_APONTA		] := oGetDados:aCols[ nAt , nPosApont	]					//10 - Flag de Apontamento
		aMarcacoes[ nLenMarc , AMARC_RELOGIO	] := oGetDados:aCols[ nAt , nPosRelog	]					//11 - Relogio
		aMarcacoes[ nLenMarc , AMARC_TIPOMARC	] := oGetDados:aCols[ nAt , nPosTpMarc	]					//12 - Tipo da Marcacao
		lReordena := (;
						( aMarcacoes[ nLenMarc , AMARC_FLAG ] $ "M,I" );
						.and.;
						!Empty( aMarcacoes[ nLenMarc , AMARC_ORDEM ] );
		  			  )
		aMarcacoes[ nLenMarc , AMARC_L_ORIGEM 	] := lReordena												//13 - Logico Origem 
		aMarcacoes[ nLenMarc , AMARC_DTHR2STR 	] := DataHora2Str(;                                           
																	oGetDados:aCols[ nAt , nPosData	] ,;
																	oGetDados:aCols[ nAt , nPosHora	]  ;
																  )                                 		//14 - String de Data/Hora para aSort
		aMarcacoes[ nLenMarc , AMARC_PERAPONTA	] := oGetDados:aCols[ nAt , nPosPaponta	]					//15 - Periodo de Apontamento
		IF !( Pona010BldCld( @aTabPadrao , @aTabCalend , dPerIni , dPerFim ) )
			Break
		EndIF
		PutOrdMarc( @aMarcacoes , aTabCalend , .T. )
        oGetDados:aCols[ nAt , nPosOrdem	] := aMarcacoes[ nLenMarc , AMARC_ORDEM ]
        oGetDados:aCols[ nAt , nPosApont	] := "N"
        oGetDados:aCols[ nAt , nPosTno		] := aMarcacoes[ nLenMarc , AMARC_TURNO ]
        oGetDados:aCols[ nAt , nPosPaponta	] := aMarcacoes[ nLenMarc , AMARC_PERAPONTA ]
		oGetDados:oBrowse:Refresh()
		
	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Restaura o Cursor do Mouse								  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	CursorArrow()

End Sequence

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura as Teclas de Atalho                     	  		  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestKeys( aSvKeys , .T. )

Return( NIL )

/*/
зддддддддддбддддддддддддддбдддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPona010OrdMarcЁAutorЁMarinaldo de Jesus   Ё Data Ё27/08/2003Ё
цддддддддддеддддддддддддддадддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁOrdenar as Marcacoes                                        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁPona010()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Pona010OrdMarc( oGetDados , nOpc , aTabPadrao )

Local aSvKeys		:= GetKeys()
Local aMarcacoes	:= {}
Local aTabCalend	:= {}
Local aMarcAll		:= {}
Local aPeriodos		:= {}
Local aCols			:= aClone( oGetDados:aCols )
Local aHeader		:= aClone( oGetDados:aHeader )
Local bSort			:= { || NIL } 
Local bAsc			:= { || NIL }
Local cPeriodo		:= ""
Local dPerIni		:= Ctod("//")
Local dPerFim		:= Ctod("//")
Local lReordena		:= .F.
Local nPosData		:= GdFieldPos( "P8_DATA" 	, aHeader )
Local nPosHora		:= GdFieldPos( "P8_HORA" 	, aHeader )
Local nPosOrdem		:= GdFieldPos( "P8_ORDEM" 	, aHeader )
Local nPosFlag		:= GdFieldPos( "P8_FLAG" 	, aHeader )
Local nPosTno		:= GdFieldPos( "P8_TURNO" 	, aHeader )
Local nPosFunc		:= GdFieldPos( "P8_FUNCAO" 	, aHeader )
Local nPosGiro		:= GdFieldPos( "P8_GIRO" 	, aHeader )
Local nPosCc		:= GdFieldPos( "P8_CC" 		, aHeader )
Local nPosApont		:= GdFieldPos( "P8_APONTA" 	, aHeader )
Local nPosRelog		:= GdFieldPos( "P8_RELOGIO"	, aHeader )
Local nPosTpMarc	:= GdFieldPos( "P8_TPMARCA"	, aHeader )
Local nPosPaponta	:= GdFieldPos( "P8_PAPONTA" , aHeader )
Local nPosDeleted	:= GdFieldPos( "GDDELETED"	, aHeader )
Local nLoop			:= 0
Local nLoops		:= 0
Local nPeriodo		:= 0
Local nPeriodos		:= 0
Local nMarc			:= 0
Local nLenMarc		:= 0
Local nPosMarc		:= 0
Local nPosPer		:= 0

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Apenas na Inclusao no Alteraca         					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
		Break
	EndIF

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Coloca o Cursor do Mouse em estado de Espera  	  		  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	CursorWait()
	
		bSort := { |x,y| (;
								DataHora2Str( x[nPosData] , x[nPosHora] ) + ;
								x[nPosPaponta];
						  ) < ;
						  (;
						  		DataHora2Str( y[nPosData] , y[nPosHora] ) + ;
						  		y[nPosPaponta];
						   );
				  }
		aSort( aCols , NIL , NIL , bSort )
		
		nLoops := Len( aCols )
		For nLoop := 1 To nLoops
			MsProcTxt( OemToAnsi( STR0014  ) )	//"Ordenando Marca┤■es..."
			IF Empty( cPeriodo := aCols[ nLoop , nPosPaponta ] )
				Loop
			EndIF
			IF ( aScan( aPeriodos , { |x| ( x[1] == cPeriodo ) } ) == 0 )
				dPerIni	    := Stod( SubStr( cPeriodo , 1 , 8 ) )
				dPerFim	    := Stod( SubStr( cPeriodo , 9 , 8 ) )
				aAdd( aPeriodos , { cPeriodo , dPerIni , dPerFim } )
			EndIF
		Next nLoop
		
		nPeriodos := Len( aPeriodos )
		For nPeriodo := 1 To nPeriodos
			MsProcTxt( OemToAnsi( STR0014  ) )	//"Ordenando Marca┤■es..."
			cPeriodo := aPeriodos[ nPeriodo , 01 ]
			IF ( ( nPosPer := aScan( aCols , { |x| x[ nPosPaponta ] == cPeriodo } ) ) > 0 )
				dPerIni		:= aPeriodos[ nPeriodo , 02 ]
				dPerFim		:= aPeriodos[ nPeriodo , 03 ]
				aMarcacoes	:= {}
				nLenMarc	:= 0
				For nLoop := nPosPer To nLoops
					/*/
					здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					Ё Nao Ordena Marcacao Deletada           					   Ё
					юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
					IF ( aCols[ nLoop , nPosDeleted ] )
						Loop
					EndIF
					/*/
					здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					Ё Nao Ordena Marcacao Fora do Periodo    					   Ё
					юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
					IF !( aCols[ nLoop , nPosPaponta ] == cPeriodo )
						Exit
					EndIF
					aAdd( aMarcacoes , Array( ELEMENTOS_AMARC ) )
					nLenMarc := Len( aMarcacoes )
					aMarcacoes[ nLenMarc , AMARC_DATA   	] := aCols[ nLoop , nPosData	]					//01 - Data
					aMarcacoes[ nLenMarc , AMARC_HORA   	] := aCols[ nLoop , nPosHora	]					//02 - Hora
					aMarcacoes[ nLenMarc , AMARC_ORDEM  	] := aCols[ nLoop , nPosOrdem	]					//03 - Ordem
					aMarcacoes[ nLenMarc , AMARC_FLAG   	] := aCols[ nLoop , nPosFlag	]					//04 - Flag
					aMarcacoes[ nLenMarc , AMARC_RECNO 		] := 0   											//05 - Recno
					aMarcacoes[ nLenMarc , AMARC_TURNO		] := aCols[ nLoop , nPosTno		]					//06 - Turno
					aMarcacoes[ nLenMarc , AMARC_FUNCAO		] := aCols[ nLoop , nPosFunc	]					//07 - Funcao 
					aMarcacoes[ nLenMarc , AMARC_GIRO		] := aCols[ nLoop , nPosGiro	]					//08 - Giro
					aMarcacoes[ nLenMarc , AMARC_CC			] := aCols[ nLoop , nPosCc		]					//09 - Centro de Custo
					aMarcacoes[ nLenMarc , AMARC_APONTA		] := aCols[ nLoop , nPosApont	]					//10 - Flag de Apontamento
					aMarcacoes[ nLenMarc , AMARC_RELOGIO	] := aCols[ nLoop , nPosRelog	]					//11 - Relogio
					aMarcacoes[ nLenMarc , AMARC_TIPOMARC	] := aCols[ nLoop , nPosTpMarc	]					//12 - Tipo da Marcacao
					lReordena := (;
									( aMarcacoes[ nLenMarc , AMARC_FLAG ] $ "M,I" );
									.and.;
									!Empty( aMarcacoes[ nLenMarc , AMARC_ORDEM ] );
					  			  )
					aMarcacoes[ nLenMarc , AMARC_L_ORIGEM 	] := lReordena										//13 - Logico Origem 
					aMarcacoes[ nLenMarc , AMARC_DTHR2STR 	] := DataHora2Str(;                                           
																				aCols[ nLoop , nPosData	] ,;
																				aCols[ nLoop , nPosHora	]  ;
																			  )                                 //14 - String de Data/Hora para aSort
					aMarcacoes[ nLenMarc , AMARC_PERAPONTA	] := aCols[ nLoop , nPosPaponta	]					//15 - Periodo de Apontamento
				Next nLoop
				IF ( nLenMarc > 0    )
					IF !( Pona010BldCld( @aTabPadrao , @aTabCalend , dPerIni , dPerFim , .F. ) )
						nLenMarc := 0   
						Loop
					EndIF
					PutOrdMarc( @aMarcacoes , aTabCalend , .T. )
					For nMarc := 1 To nLenMarc
						aAdd( aMarcAll , aClone( aMarcacoes[ nMarc ] ) )
					Next nMarc 
				EndIF
			EndIF
		
		Next nPeriodo
		
		aMarcacoes	:= {}
		aTabCalend	:= {}
		nLenMarc	:= Len( aMarcAll )
		aCols		:= oGetDados:aCols
		bAsc		:= { |x| ( x[ nPosData ] == aMarcAll[ nMarc , AMARC_DATA ] );
							 .and.;
					 		 ( x[ nPosHora ] == aMarcAll[ nMarc , AMARC_HORA ] );
						}
		For nMarc := 1 To nLenMarc
			MsProcTxt( OemToAnsi( STR0014  ) )	//"Ordenando Marca┤■es..."
			IF ( ( nPosMarc := aScan( aCols , bAsc )  ) > 0    )
		        aCols[ nPosMarc , nPosOrdem 	] := aMarcAll[ nMarc , AMARC_ORDEM ]
		        aCols[ nPosMarc , nPosTno		] := aMarcAll[ nMarc , AMARC_TURNO ]
		        aCols[ nPosMarc , nPosApont		] := "N"
		        aCols[ nPosMarc , nPosPaponta	] := aMarcAll[ nMarc , AMARC_PERAPONTA ]
			EndIF
		Next nMarc
		
		oGetDados:oBrowse:Refresh()
		
	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Restaura o Cursor do Mouse								  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	CursorArrow()

End Sequence

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura as Teclas de Atalho                     	  		  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestKeys( aSvKeys , .T. )
	
Return( NIL )

/*/
зддддддддддбддддддддддддддбдддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPona010CalMarcЁAutorЁMarinaldo de Jesus   Ё Data Ё27/08/2003Ё
цддддддддддеддддддддддддддадддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁMostrar Calendario de Marcacoes conforme Periodo            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁPona010()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Pona010CalMarc( oGetDados , aTabPadrao , aLstTabCalend )

Local aSvKeys		:= GetKeys()
Local bCriaCalend	:= { || NIL }
Local bShowCalend	:= { || NIL }
Local cPeriodo		:= ""
Local lCriaCalend	:= .F.

Private aTabCalend	:= aLstTabCalend
Private aCols		:= aClone( oGetDados:aCols	 )
Private aHeader		:= aClone( oGetDados:aHeader )
Private dPerIni		:= Ctod("//")
Private dPerFim		:= Ctod("//")
Private lP8PGIniCa	:= .T.
Private n			:= oGetDados:oBrowse:nAt

cPeriodo	:= GdFieldGet( "P8_PAPONTA" )
dPerIni		:= Stod( SubStr( cPeriodo , 1 , 8 ) )
dPerFim		:= Stod( SubStr( cPeriodo , 9 , 8 ) )

Begin Sequence

	IF Empty( cPeriodo )
		//"O per║odo informado n└o ┌ v═lido"###"Aviso de Inconsist┬ncia"
		MsgInfo( OemToAnsi( STR0012 ) , OemToAnsi( STR0010 ) )
		Break
	EndIF

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁCria o Calendario do Periodo        						  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bCriaCalend	:= { || lCriaCalend := Pona010BldCld( @aTabPadrao , @aTabCalend , dPerIni , dPerFim ) }
	MsAguarde( bCriaCalend , cCadastro , OemToAnsi( STR0016 ) ) //"Criando o Calend═rio..."
	IF ( lCriaCalend  )
		/*/
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁMostra o Calendario conforme periodo						  Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		bShowCalend	:= { || F_ALT_H(NIL,aCols,GdFieldPos("GDDELETED"),aHeader,.F.,NIL,NIL,.T.) }
		MsAguarde( bShowCalend , cCadastro , OemToAnsi( STR0017 ) ) //"Montando o Calend═rio..."
	EndIF

End Sequence

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura as Teclas de Atalho                     	  		  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestKeys( aSvKeys , .T. )

Return( NIL )

/*/
зддддддддддбддддддддддддддбдддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPona010ClrOrdMЁAutorЁMarinaldo de Jesus   Ё Data Ё27/08/2003Ё
цддддддддддеддддддддддддддадддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁLimpar as Ordens das Marcacoes                              Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁPona010()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Pona010ClrOrdM( oGetDados , nOpc )

Local aSvKeys			:= GetKeys()
Local aArea				:= GetArea()

Local aAdvSize			:= {}
Local aInfoAdvSize		:= {}
Local aObjCoords		:= {}
Local aObjSize			:= {}

Local aInfo1AdvSize		:= {}
Local aObj1Coords		:= {}
Local aObj1Size			:= {}

Local aInfo2AdvSize		:= {}
Local aObj2Coords		:= {}
Local aObj2Size			:= {}

Local aInfo3AdvSize		:= {}
Local aObj3Coords		:= {}
Local aObj3Size			:= {}

Local aInfo4AdvSize		:= {}
Local aObj4Coords		:= {}
Local aObj4Size			:= {}

Local bSet15			:= { || NIL }
Local bSet24			:= { || NIL }
Local cSpaceOrdem		:= Space( TamSx3("P8_ORDEM")[1] )
Local cPeriodo			:= ""
Local dPerIni			:= Ctod("//")
Local dPerFim			:= Ctod("//")
Local lbSet15			:= .F.
Local nOpcRad1			:= 2
Local nOpcRad2			:= 2
Local nLoop				:= 0
Local nLoops			:= 0
Local nPosOrdem			:= GdFieldPos( "P8_ORDEM" 	, oGetDados:aHeader )
Local nPosAponta		:= GdFieldPos( "P8_APONTA" 	, oGetDados:aHeader )
Local nPosPaponta		:= GdFieldPos( "P8_PAPONTA"	, oGetDados:aHeader )
Local nPosFlag			:= GdFieldPos( "P8_FLAG"	, oGetDados:aHeader )
Local nPosData			:= GdFieldPos( "P8_DATA"	, oGetDados:aHeader )
Local nPosDeleted		:= GdFieldPos( "GDDELETED"	, oGetDados:aHeader )
Local oDlg				:= NIL
Local oFont				:= NIL
Local oFontBig			:= NIL
Local oGroup			:= NIL
Local oPerIni			:= NIL
Local oPerFim			:= NIL
Local oRadio1			:= NIL
Local oRadio2			:= NIL

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Apenas na Inclusao no Alteraca         					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
		Break
	EndIF

    /*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Bloco para a Teclas <CTRL-O>   ( Button OK da EnchoiЁ
	Ё ceBar )													   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet15 := { ||	IF(;
							Eval( oPerIni:bValid );
							.and.;
							Eval( oPerFim:bValid ),;
							(;
								lbSet15 := .T. ,;
								RestKeys( aSvKeys , .T. ),;
								oDlg:End();
							),;
							.F.;
					   );
				}

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o  Bloco  para a Teclas <CTRL-X> ( Button Cancel da EnЁ
	Ё choiceBar )												   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet24 := { || RestKeys( aSvKeys , .T. ) , oDlg:End() }

/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta as Dimensoes dos Objetos         					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdvSize	:= MsAdvSize( ,.T.,370)
	
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
	aAdd( aObjCoords , { 000 , 080, .T. , .F. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )   
	
	//Divisao Em 3 linhas horizontais proporcionais
	aObj1Coords := {}
	aAdv1Size    := aClone(aObjSize[1])
	
	aInfo1AdvSize    := { aAdv1Size[2] , aAdv1Size[1] , aAdv1Size[4] , aAdv1Size[3] , 5 , 5 }
	aAdd( aObj1Coords , { 000 , 020 , .T. , .F. } )	//1-Inicial - Final
	aAdd( aObj1Coords , { 000 , 020 , .T. , .F. } )	//2-Limpar
	aAdd( aObj1Coords , { 000 , 020 , .T. , .F. } )	//3-Considerar
	aObj1Size := MsObjSize( aInfo1AdvSize , aObj1Coords)
 

	//Divisao Em 6 Colunas da Linha 1-Inicial - Final
	aObj2Coords := {}
	aAdv2Size    := aClone(aObj1Size[1])
	
	aInfo2AdvSize    := { aAdv2Size[2] , aAdv2Size[1] , aAdv2Size[4] , aAdv2Size[3] , 5 , 5 }
	aAdd( aObj2Coords , { 055 , 000 , .F. , .T. } )	
	aAdd( aObj2Coords , { 020 , 000 , .F. , .T. } )	
	aAdd( aObj2Coords , { 060 , 000 , .F. , .T. } )	
	aAdd( aObj2Coords , { 020 , 000 , .F. , .T. } )	
	aAdd( aObj2Coords , { 030 , 000 , .F. , .T. } )	

	aObj2Size := MsObjSize( aInfo2AdvSize , aObj2Coords,,.T.)


	//Divisao Em 2 Colunas da Linha 2-Limpar
	aObj3Coords := {}
	aAdv3Size    := aClone(aObj1Size[2])
	
	aInfo3AdvSize    := { aAdv3Size[2] , aAdv3Size[1] , aAdv3Size[4] , aAdv3Size[3] , 5, 0 }
	aAdd( aObj3Coords , { 055 , 000 , .F. , .T. } )	
	aAdd( aObj3Coords , { 140 , 000 , .F. , .T. } )	
	aAdd( aObj3Coords , { 000 , 000 , .T. , .T. } )	

	aObj3Size := MsObjSize( aInfo3AdvSize , aObj3Coords,,.T.)

	//Divisao Em 2 Colunas da Linha 3-Considerar
	aObj4Coords := {}
	aAdv4Size    := aClone(aObj1Size[3])
	
	aInfo4AdvSize    := { aAdv4Size[2] , aAdv4Size[1] , aAdv4Size[4] , aAdv4Size[3] , 5 , 0 }
	aAdd( aObj4Coords , { 055 , 000 , .F. , .T. } )	
	aAdd( aObj4Coords , { 140 , 000 , .F. , .T. } )	
	aAdd( aObj4Coords , { 000 , 000 , .T. , .T. } )	

	aObj4Size := MsObjSize( aInfo4AdvSize , aObj4Coords,,.T.)


	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁMonta Dialogo para a selecao do Periodo 					  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(cCadastro) From aAdvSize[7],0 TO aAdvSize[6]/1.65,aAdvSize[5] OF oMainWnd PIXEL			
	
		@ aObjSize[1,1] , aObjSize[1,2] GROUP oGroup TO aObjSize[1,3],aObjSize[1,4] LABEL OemToAnsi(STR0020) OF oDlg PIXEL		//"Per║odo:"
		oGroup:oFont:= oFont  
		
		@ aObj2Size[2,1] , aObj2Size[2,2]	SAY OemToAnsi(STR0021)		SIZE 50,10 OF oDlg PIXEL FONT oFont	//"Inicial:"
		@ aObj2Size[3,1] , aObj2Size[3,2]	MSGET oPerIni VAR dPerIni	SIZE 50,10 OF oDlg PIXEL FONT oFont VALID !Empty( dPerIni ) HASBUTTON
	
		@ aObj2Size[4,1] , aObj2Size[4,2]	SAY OemToAnsi(STR0022)		SIZE 50,10 OF oDlg PIXEL FONT oFont	//"Final:"		
		@ aObj2Size[5,1] , aObj2Size[5,2]	MSGET oPerFim VAR dPerFim	SIZE 50,10 OF oDlg PIXEL FONT oFont VALID ( !Empty( dPerFim ) .and. ( dPerIni <= dPerFim ) ) HASBUTTON
	                                                                        
		@ aObj3Size[2,1] , aObj3Size[2,2]	SAY OemToAnsi(STR0025)		SIZE 150,10 OF oDlg PIXEL FONT oFont	//"Limpar Ordens de Itens Informados/Modificados?"

		@ aObj3Size[3,1] , aObj3Size[3,2]	RADIO oRadio1 VAR nOpcRad1	ITEMS 	OemToAnsi(STR0023)	,;			//"Sim"  
																				OemToAnsi(STR0024)	 ;			//"N└o"
																		SIZE 050,010 OF oDlg PIXEL
	
		@ aObj4Size[2,1] , aObj4Size[2,2]	SAY OemToAnsi(STR0026)		SIZE 150,10 OF oDlg PIXEL FONT oFont	//"Considerar o Periodo de Apontamento ou a Data?"
		@ aObj4Size[3,1] , aObj4Size[3,2]	RADIO oRadio2 VAR nOpcRad2	ITEMS 	OemToAnsi(STR0027)	,;			//"Periodo"
																				OemToAnsi(STR0028)	 ;			//"Data"
																		SIZE 050,010 OF oDlg PIXEL
																							

	ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 )
	
	IF ( lbSet15 )
		IF ( nOpcRad2 == 1 )
			cPeriodo := ( Dtos( dPerIni ) + Dtos( dPerFim ) )
		EndIF
		nLoops := Len( oGetDados:aCols )
		For nLoop := 1 To nLoops
			MsProcTxt( OemToAnsi( STR0019  ) )	//"Limpando as Ordens..."
			IF ( ( nOpcRad2 == 1 ) .and. !( oGetDados:aCols[ nLoop , nPosPaponta ] == cPeriodo ) )
				Loop
			ElseIF ( ( oGetDados:aCols[ nLoop , nPosData ] < dPerIni ) .or. ( oGetDados:aCols[ nLoop , nPosData ] > dPerFim ) )
				Loop			
			End IF
			IF ( ( nOpcRad1 == 2 ) .and. ( oGetDados:aCols[ nLoop , nPosFlag ] $ "M/I" ) )
				Loop
			EndIF
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Nao Ordena Marcacao Deletada           					   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			IF ( oGetDados:aCols[ nLoop , nPosDeleted ]  )
				Loop
			EndIF
			oGetDados:aCols[ nLoop , nPosOrdem 	] := cSpaceOrdem
			oGetDados:aCols[ nLoop , nPosAponta	] := "N"
		Next nLoop
		oGetDados:oBrowse:Refresh()
	EndIF

End Sequence

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura as Teclas de Atalho                     	  		  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestKeys( aSvKeys , .T. )
	
Return( NIL )

/*/
зддддддддддбддддддддддддддбдддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPona010StrOrdMЁAutorЁMarinaldo de Jesus   Ё Data Ё27/08/2003Ё
цддддддддддеддддддддддддддадддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁIndexar os Lancamentos                                      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁPona010()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Pona010StrOrdM( oGetDados , nOpc )

Local aSvKeys		:= GetKeys()
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjCoords	:= {}
Local aObjSize		:= {}
Local bSort			:= { || NIL }
Local lbSet15		:= .F.
Local nPosData		:= GdFieldPos( "P8_DATA" 	, oGetDados:aHeader )
Local nPosHora		:= GdFieldPos( "P8_HORA"	, oGetDados:aHeader )
Local nPosPaponta	:= GdFieldPos( "P8_PAPONTA" , oGetDados:aHeader )
Local nOpcRad		:= 1
Local oFont			:= NIL
Local oDlg			:= NIL
Local oGroup		:= NIL
Local oRadio		:= NIL	

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Apenas na Inclusao no Alteraca         					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
		Break
	EndIF
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta as Dimensoes dos Objetos         					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdvSize		:= MsAdvSize( , .T.,380 )
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Redimensiona                           					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
	aAdd( aObjCoords , { 000 , 040 , .T. , .F. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )

	//Inclusao de uma linha dentro do aObjSize
	aObj1Coords := {}
	aAdv1Size    := aClone(aObjSize[1])
	
	aInfo1AdvSize    := { aAdv1Size[2] , aAdv1Size[1] , aAdv1Size[4] , aAdv1Size[3] , 5 , 5 }
	aAdd( aObj1Coords , { 000 , 012 , .T. , .F. } )	
	aObj1Size := MsObjSize( aInfo1AdvSize , aObj1Coords)

	//Divisao Em 3 Colunas para posicionar a Radio Centralizado
	aObj2Coords := {}
	aAdv2Size    := aClone(aObj1Size[1])
	
	aInfo2AdvSize    := { aAdv2Size[2] , aAdv2Size[1] , aAdv2Size[4] , aAdv2Size[3] , 5, 10 }
	aAdd( aObj2Coords , { 055 , 000 , .F. , .T. } )	
	aAdd( aObj2Coords , { 140 , 000 , .F. , .T. } )	
	aAdd( aObj2Coords , { 000 , 000 , .T. , .T. } )	
	aObj2Size := MsObjSize( aInfo2AdvSize , aObj2Coords,,.T.)

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Bloco para a Teclas <CTRL-O>   ( Button OK da EnchoiЁ
	Ё ceBar )													   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet15 := { ||	(;
						lbSet15 := .T. ,;
						RestKeys( aSvKeys , .T. ),;
						oDlg:End();
					  );
				}

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o  Bloco  para a Teclas <CTRL-X> ( Button Cancel da EnЁ
	Ё choiceBar )												   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet24 := { || RestKeys( aSvKeys , .T. ) , oDlg:End() }
	
	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁMonta Dialogo para a selecao do Periodo 					  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(cCadastro) From aAdvSize[7],0 TO aAdvSize[6]/2.6,aAdvSize[5] OF oMainWnd PIXEL			
	
		@ aObjSize[1,1] , aObjSize[1,2] GROUP oGroup TO aObjSize[1,3],aObjSize[1,4] LABEL OemToAnsi(STR0033 ) OF oDlg PIXEL		//"Indexa┤└o:"
		oGroup:oFont:= oFont

		@ aObj2Size[2,1] , aObj2Size[2,2]	SAY OemToAnsi(STR0034)		SIZE 150,10 OF oDlg PIXEL FONT oFont	//"Indexar os Lan┤amentos de Marca┤■es em ordem:"
		@ aObj2Size[3,1] , aObj2Size[3,2]	RADIO oRadio VAR nOpcRad	ITEMS 	OemToAnsi(STR0035)	,;			//"Ascendente"
																		 		OemToAnsi(STR0036)	 ;			//"Descendente"
																		SIZE 115,010 OF oDlg PIXEL

	ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 )
	
	IF ( lbSet15 )
		IF ( nOpcRad == 1 )
			bSort := { |x,y| (;
								DataHora2Str( x[nPosData] , x[nPosHora] ) + ;
								x[nPosPaponta];
							  ) < ;
					  		(;
					  			DataHora2Str( y[nPosData] , y[nPosHora] ) + ;
					  			y[nPosPaponta];
					   		);
			  		}
		Else
			bSort := { |x,y| (;
								DataHora2Str( x[nPosData] , x[nPosHora] ) + ;
								x[nPosPaponta];
							  ) > ;
					  		(;
					  			DataHora2Str( y[nPosData] , y[nPosHora] ) + ;
					  			y[nPosPaponta];
					   		);
			  		}
		EndIF
		aSort( oGetDados:aCols , NIL , NIL , bSort )
		oGetDados:oBrowse:Refresh()
	EndIF		

End Sequence

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura as Teclas de Atalho                     	  		  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestKeys( aSvKeys , .T. )

Return( NIL )

/*/
зддддддддддбддддддддддддддбдддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPona010FltMarcЁAutorЁMarinaldo de Jesus   Ё Data Ё29/08/2003Ё
цддддддддддеддддддддддддддадддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFiltrar os Lancamentos                                      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁPona010()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Pona010FltMarc(	oGetDados		,;	//Objeto GetDados Original
								nOpc			,;	//Opcao conforme a Rotina
								nOpcNewGd		,;	//Opcao do Objeto GetDados de acordo com nOpc do aRotina
								aTabPadrao		,;	//Array com as Informacoes para Montagem do Calendario de Marcacoes
								aTabCalend		,;	//Array contendo as Informacoes do Calendario de Marcacoes
								dIniPer			,;  //Periodo Inicial Previamente Selecionado
								dFimPer			,;	//Periodo Final Previamente Selecionado
								aSP8GdAltera	,;	//Campos Alteraveis na GetDados
								bSP8GdDelOk		 ;	//Bloco para Validacao da Delecao
							  )	

Local aSvKeys		:= GetKeys()
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjCoords	:= {}
Local aObjSize		:= {}

Local aAdv1Size		:= {}
Local aInfo1AdvSize	:= {}
Local aObj1Coords	:= {}
Local aObj1Size		:= {} 

Local aAdv2Size		:= {}
Local aInfo2AdvSize	:= {}
Local aObj2Coords	:= {}
Local aObj2Size		:= {}

Local aCheckBox		:= {}
Local aChecks		:= {}
Local bSort			:= { || NIL }
Local dPerIni		:= dIniPer
LOcal dPerFim		:= dFimPer
Local lbSet15		:= .F.
Local nCheck		:= 0
Local nChecks		:= 0
Local oFont			:= NIL
Local oDlg			:= NIL
Local oGroup		:= NIL

Begin Sequence


	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define os Elementos do CheckBox							   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdd( aCheckBox , Array( 03 ) )
	nChecks := Len( aCheckBox )
	aCheckBox[ nChecks , 01 ] := NIL
	aCheckBox[ nChecks , 02 ] := .F.
	aCheckBox[ nChecks , 03 ] := OemToAnsi( STR0044 )	//"Marca┤■es n└o ordenadas"

	aAdd( aCheckBox , Array( 03 ) )
	nChecks := Len( aCheckBox )
	aCheckBox[ nChecks , 01 ] := NIL
	aCheckBox[ nChecks , 02 ] := .F.
	aCheckBox[ nChecks , 03 ] := OemToAnsi( STR0045 )	//"Marca┤■es ordenadas ║mpares"

	aAdd( aCheckBox , Array( 03 ) )
	nChecks := Len( aCheckBox )
	aCheckBox[ nChecks , 01 ] := NIL
	aCheckBox[ nChecks , 02 ] := .F.
	aCheckBox[ nChecks , 03 ] := OemToAnsi( STR0046 )	//"Duplicadas"

	aAdd( aCheckBox , Array( 03 ) )
	nChecks := Len( aCheckBox )
	aCheckBox[ nChecks , 01 ] := NIL
	aCheckBox[ nChecks , 02 ] := .F.
	aCheckBox[ nChecks , 03 ] := OemToAnsi( STR0047 )	//"Considerar o per║odo:"

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Bloco para a Teclas <CTRL-O>   ( Button OK da EnchoiЁ
	Ё ceBar )													   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet15 := { ||	(;
						lbSet15 := .T. ,;
						RestKeys( aSvKeys , .T. ),;
						oDlg:End();
					 );
				}

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o  Bloco  para a Teclas <CTRL-X> ( Button Cancel da EnЁ
	Ё choiceBar )												   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet24 := { || RestKeys( aSvKeys , .T. ) , oDlg:End() }
	

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta as Dimensoes dos Objetos         					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdvSize		:= MsAdvSize( , .T., 380 )
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
	aAdd( aObjCoords , { 000 , 090 , .T. , .F. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )
	
	//Divisao Em 5 linhas horizontais 
	aObj1Coords := {}
	aAdv1Size    := aClone(aObjSize[1])
	
	aInfo1AdvSize    := { aAdv1Size[2] , aAdv1Size[1] , aAdv1Size[4] , aAdv1Size[3] , 20 , 5 }
	aAdd( aObj1Coords , { 000 , 010 , .T. , .F. } )	
	aAdd( aObj1Coords , { 000 , 005 , .T. , .F. } )	
	aAdd( aObj1Coords , { 000 , 005 , .T. , .F. } )	
	aAdd( aObj1Coords , { 000 , 005 , .T. , .F. } )	
	aAdd( aObj1Coords , { 000 , 005 , .T. , .F. } )	
	aAdd( aObj1Coords , { 000 , 020 , .T. , .F. } )	//Dta Inicial - Final
	aObj1Size := MsObjSize( aInfo1AdvSize , aObj1Coords)
	

	//Divisao Em 6 Colunas da Linha 5 -Inicial - Final
	aObj2Coords := {}
	aAdv2Size    := aClone(aObj1Size[6])
	
	aInfo2AdvSize    := { aAdv2Size[2] , aAdv2Size[1] , aAdv2Size[4] , aAdv2Size[3] , 5 , 5 }
	aAdd( aObj2Coords , { 055 , 000 , .F. , .T. } )	
	aAdd( aObj2Coords , { 020 , 000 , .F. , .T. } )	
	aAdd( aObj2Coords , { 060 , 000 , .F. , .T. } )	
	aAdd( aObj2Coords , { 020 , 000 , .F. , .T. } )	
	aAdd( aObj2Coords , { 030 , 000 , .F. , .T. } )	
	aObj2Size := MsObjSize( aInfo2AdvSize , aObj2Coords,,.T.)

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁMonta Dialogo para a selecao do Periodo 					  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(cCadastro) From aAdvSize[7],0 TO aAdvSize[6]/1.6,aAdvSize[5] OF oMainWnd PIXEL			
	
		@ aObjSize[1,1] , aObjSize[1,2] GROUP oGroup TO aObjSize[1,3],aObjSize[1,4] LABEL OemToAnsi(STR0043) OF oDlg PIXEL		//"Filtro Lan┤amentos de Marca┤■es:"
		oGroup:oFont:= oFont

		@ aObj1Size[2,1] , aObj1Size[2,2]	CHECKBOX aCheckBox[ 01 , 01 ] VAR aCheckBox[ 01 , 02 ] PROMPT aCheckBox[ 01 , 03 ] SIZE 200,10 OF oDlg PIXEL FONT oFont
		@ aObj1Size[3,1] , aObj1Size[3,2]	CHECKBOX aCheckBox[ 02 , 01 ] VAR aCheckBox[ 02 , 02 ] PROMPT aCheckBox[ 02 , 03 ] SIZE 200,10 OF oDlg PIXEL FONT oFont
		@ aObj1Size[4,1] , aObj1Size[5,2]	CHECKBOX aCheckBox[ 03 , 01 ] VAR aCheckBox[ 03 , 02 ] PROMPT aCheckBox[ 03 , 03 ] SIZE 200,10 OF oDlg PIXEL FONT oFont
		@ aObj1Size[5,1] , aObj1Size[5,2]	CHECKBOX aCheckBox[ 04 , 01 ] VAR aCheckBox[ 04 , 02 ] PROMPT aCheckBox[ 04 , 03 ] SIZE 200,10 OF oDlg PIXEL FONT oFont
		aCheckBox[ 04 , 01 ]:cToolTip := OemToAnsi( STR0048 )	//"Ap╒s selecionar pressione a tecla <TAB> para habilitar a digita┤└o do per║odo"

		@ aObj2Size[2,1] , aObj2Size[2,2]	SAY OemToAnsi(STR0021)		SIZE 50,10 OF oDlg PIXEL FONT oFont	//"Inicial:"
		@ aObj2Size[3,1] , aObj2Size[3,2]	MSGET oPerIni VAR dPerIni	SIZE 50,10 OF oDlg PIXEL FONT oFont WHEN ( aCheckBox[ 04 , 02 ] ) VALID !Empty( dPerIni ) HASBUTTON
		@ aObj2Size[4,1] , aObj2Size[4,2]	SAY OemToAnsi(STR0022)		SIZE 50,10 OF oDlg PIXEL FONT oFont	//"Final:"
		@ aObj2Size[5,1] , aObj2Size[5,2]	MSGET oPerFim VAR dPerFim	SIZE 50,10 OF oDlg PIXEL FONT oFont WHEN ( aCheckBox[ 04 , 02 ] ) VALID ( !Empty( dPerFim ) .and. ( dPerIni <= dPerFim ) ) HASBUTTON
				
	ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 )

	IF ( lbSet15 )
		For nCheck := 1 To nChecks
			aAdd( aChecks , aCheckBox[ nCheck , 02 ] )
		Next nCheck
		Pona010McFilt(	oGetDados		,;	//Objeto GetDados Original
						nOpc			,;	//Opcao conforme a Rotina
						nOpcNewGd		,; 	//Opcao do Objeto GetDados de acordo com nOpc do aRotina
						@aTabPadrao		,;	//Array com as Informacoes para Montagem do Calendario de Marcacoes
						@aTabCalend		,;	//Array contendo as Informacoes do Calendario de Marcacoes
						aSP8GdAltera	,;	//Campos Alteraveis na GetDados
						bSP8GdDelOk		,;	//Bloco para Validacao da Delecao
						aChecks			,;	//Array com as Opcoes de Filtro
						dPerIni			,;	//Data Inicial para Filtro	( Depende do elemento 4 de aChecks )
						dPerFim			 ;	//Data Final para Filtro	( Depende do elemento 4 de aChecks )
					)
	EndIF		

End Sequence

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura as Teclas de Atalho                     	  		  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestKeys( aSvKeys , .T. )

Return( NIL )

/*/
зддддддддддбддддддддддддддбдддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPona010McFilt ЁAutorЁMarinaldo de Jesus   Ё Data Ё29/08/2003Ё
цддддддддддеддддддддддддддадддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁDisponibilizar as Marcacoes Filtradas para alteracao        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁPona010FltMarc() em Pona010()	                            Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Pona010McFilt(	oGetDados		,;	//Objeto GetDados Original
								nOpc			,;	//Opcao conforme a Rotina
								nOpcNewGd		,; 	//Opcao do Objeto GetDados de acordo com nOpc do aRotina
								aTabPadrao		,;	//Array com as Informacoes para Montagem do Calendario de Marcacoes
								aTabCalend		,;	//Array contendo as Informacoes do Calendario de Marcacoes
								aSP8GdAltera	,;	//Campos Alteraveis na GetDados
								bSP8GdDelOk		,;	//Bloco para Validacao da Delecao
								aChecks			,;	//Array com as Opcoes de Filtro
								dPerIni			,;	//Data Inicial para Filtro	( Depende do elemento 4 de aChecks )
								dPerFim			 ;	//Data Final para Filtro	( Depende do elemento 4 de aChecks )
							)

Local aSvKeys		:= GetKeys()
Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjCoords	:= {}
Local aObjSize		:= {}
Local aCols			:= aClone( oGetDados:aCols   )
Local aHeader		:= aClone( oGetDados:aHeader )
Local aNewCols		:= {}
Local aDuplic		:= {}
Local aJaProc		:= {}
Local aImpares		:= {}
Local aButtons		:= {}
Local bOrdMarc		:= { || NIL }
Local bOrdMarcs		:= { || NIL }
Local bCalend		:= { || NIL }
LocAL bClrOrd		:= { || NIL }
Local bSet15		:= { || NIL }
Local bSet24		:= { || NIL }
Local bDialogInit	:= { || NIL }
Local bImpares		:= { || NIL }
Local bSort			:= { || NIL }
Local cOrdem		:= ""
Local dData			:= Ctod("//")
Local nHora			:= 0
Local lbSet15		:= .F.
Local lNaoOrdena	:= aChecks[ 1 ]
Local lImpares		:= aChecks[ 2 ]
Local lDuplicadas	:= aChecks[ 3 ]
Local lConsiPer		:= aChecks[ 4 ]
Local nLoop			:= 0
Local nLoop1		:= 0
Local nLoops		:= 0
Local nDuplic		:= 0
Local nImpar		:= 0
Local nImpares		:= 0
Local nPosData		:= GdFieldPos( "P8_DATA"	, aHeader )
Local nPosHora		:= GdFieldPos( "P8_HORA"	, aHeader )
Local nPosOrdem		:= GdFieldPos( "P8_ORDEM"	, aHeader )
Local nPosDelete	:= GdFieldPos( "GDDELETED"	, aHeader )
Local oFont			:= NIL
Local oDlg			:= NIL
Local oGroup		:= NIL
Local oGdMcFilt		:= NIL

Begin Sequence

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSe ja processou desconsidera       						  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bImpares := { |x,y| IF(;
							( nLoop1 <> nLoop );
							.and.;
							cOrdem == x[ nPosOrdem ];
							.and.;
							!( x[nPosDelete ] ),;
							(;
								++nImpar,;
								aAdd( aImpares , y );
							),;	
							NIL;
						 );
				}		 	

	nLoops := Len( aCols )
	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Coloca o Cursor do Mouse em estado de Espera  	  		  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	CursorWait()
		For nLoop := 1 To nLoops
			/*/
			зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁNao Considera Marcacoes Deletadas  						  Ё
			юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			IF ( aCols[ nLoop , nPosDelete ] )
				Loop
			EndIF
			/*/
			зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁSe ja processou desconsidera       						  Ё
			юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			IF ( aScan( aJaProc , nLoop ) > 0 )
				Loop
			EndIF
			/*/
			зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁVerifica as Marcacoes Nao Ordenadas						  Ё
			юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			IF ( ( lNaoOrdena ) .and. Empty( aCols[ nLoop , nPosOrdem ] ) )
				/*/
				зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				ЁDesconsidera as Marcacoes fora do Periodo Selecionado		  Ё
				юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				IF ( lConsiPer )
					IF (;
							( aCols[ nLoop , nPosData ] < dPerIni );
							.or.;
							( aCols[ nLoop , nPosData ] > dPerFim );
						)
						Loop
					EndIF
				EndIF
				aAdd( aNewCols , aClone( aCols[ nLoop ] ) )
				aAdd( aJaProc , nLoop )
			Else
				/*/
				зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				ЁDesconsidera as Marcacoes fora do Periodo Selecionado		  Ё
				юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				IF ( lConsiPer )
					IF (;
							( aCols[ nLoop , nPosData ] < dPerIni );
							.or.;
							( aCols[ nLoop , nPosData ] > dPerFim );
						)
						Loop
					EndIF
				EndIF
				cOrdem		:= aCols[ nLoop , nPosOrdem ]
				dData		:= aCols[ nLoop , nPosData	]
				nHora		:= aCols[ nLoop , nPosHora	]
				/*/
				зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				ЁVerifica as Marcacoes Duplicadas   						  Ё
				юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				IF ( lDuplicadas )
					aDuplic		:= {}
					For nLoop1 := ( nLoop + 1 ) To nLoops
						/*/
						зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						ЁNao Considera Marcacoes Deletadas  						  Ё
						юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
						IF ( aCols[ nLoop , nPosDelete ] )
							Loop
						EndIF
						IF ( aScan( aJaProc , nLoop1 ) == 0 )
							IF (;
									( aCols[ nLoop1 , nPosData ] == dData );
									.and.;
									( aCols[ nLoop1 , nPosHora ] == nHora );
								)
								aAdd( aDuplic , nLoop1 )
							EndIF		
						EndIF
					Next nLoop1
					IF ( ( nDuplic := Len( aDuplic ) ) > 0 )
						IF ( aScan( aJaProc , nLoop ) == 0 )
							aAdd( aNewCols , aClone( aCols[ nLoop ] ) )
							aAdd( aJaProc , nLoop )
						EndIF
						For nLoop1 := 1 To nDuplic
							IF ( aScan( aJaProc , aDuplic[ nLoop1 ] ) == 0 )
								aAdd( aNewCols , aClone( aCols[ aDuplic[ nLoop1 ] ] ) )
								aAdd( aJaProc , aDuplic[ nLoop1 ] )
							EndIF
						Next nLoop1
					EndIF	
				EndIF
				/*/
				зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				ЁVerifica as Marcacoes Impares      						  Ё
				юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				IF ( ( lImpares ) .and. !Empty( aCols[ nLoop , nPosOrdem ] ) )
					aImpares	:= {}
					nImpar	    := 0
					aEval( aCols , bImpares )
					IF ( ( nImpar % 2 ) > 0 )
						nImpares := Len( aImpares )
						For nLoop1 := 1 To nImpares
							IF ( aScan( aJaProc , aImpares[ nLoop1 ] ) == 0 )
								aAdd( aNewCols , aClone( aCols[ aImpares[ nLoop1 ] ] ) )
								aAdd( aJaProc , aImpares[ nLoop1 ] )
							EndIF	
						Next nLoop1
					EndIF
				EndIF	
			EndIF
		Next nLoop
	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Restaura o Cursor do Mouse								  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	CursorArrow()
		
	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁCarrega aCols com as Novas Informacoes						  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF Empty( aCols := aClone( aNewCols ) )
		//"N└o existem informa┤■es a serem disponibilizadas"
		MsgInfo(  OemToAnsi( STR0049 ) , cCadastro )
		Break
	EndIF

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Bloco para o Botao e Tecla de Atalho de Insercao  deЁ
	ЁOrdem na Marcacao										      Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bOrdMarc	:= { ||	MsAguarde(;
									{ ||;
											Pona010MarcOrd(	oGdMcFilt	,;
															nOpc		,;
															@aTabPadrao	,;
															@aTabCalend	 ;
														   );
								    },;
								    cCadastro	,;
								    OemToAnsi( STR0030 );	//"Ordenando Marca┤└o..."
								   ),;
						SetKey( VK_F4 , bOrdMarc	);
					}

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Bloco para o Botao e Tecla de Atalho de Insercao  deЁ
	ЁOrdens nas Marcacoes										  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bOrdMarcs	:= { || MsAguarde(;
									{ ||;
											Pona010OrdMarc(	oGdMcFilt	,;
															nOpc		,;
															@aTabPadrao	 ;
														   );
									},;
									cCadastro	,;
									OemToAnsi( STR0014 );	//"Ordenando Marca┤■es..."
								 ),;
						SetKey( VK_F5 , bOrdMarcs );
					}

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Bloco para o Botao e Tecla de Atalho para a MontagemЁ
	Ёe Visualizacao do Calendario			 					  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bCalend		:= { ||	MsAguarde(;
									{ ||;
											Pona010CalMarc(	oGdMcFilt	,;
															@aTabPadrao ,;
															@aTabCalend	 ;
										   				  );
									},;
									cCadastro,;
									OemToAnsi( STR0015 );	//"Calend═rio de Marca┤■es..."
								  ),;
						SetKey( VK_F6 , bCalend );
					 }

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Bloco para o Botao e Tecla de Atalho para Limpar   oЁ
	Ёconteudo do campo P8_ORDEM									  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bClrOrd		:= { ||	MsAguarde(;
									{ ||;
											Pona010ClrOrdM(	oGdMcFilt , nOpc );
									},;
									cCadastro,;
									OemToAnsi( STR0019 );	//"Limpando as Ordens..."
								  ),;
						SetKey( VK_F7 , bClrOrd );
					}

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Botao para a Ordenacao da Marcacao Corrente		  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
		aAdd(; 
				aButtons	,;
								{;
									"BMPTRG"				,;	
		   							bOrdMarc				,;
		       	   					OemToAnsi( STR0029  )	,;	//"Ordenar Marca┤└o <F4>..."
		       	   					OemToAnsi( STR0113  )	 ;	//"Ordenar"
		           				};
		     )					 	
	EndIF

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Botao para a Ordenacao de Todas as Marcacoes		  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
		aAdd(; 
				aButtons	,;
								{;
									"AUTOM"					,;	
		   							bOrdMarcs				,;
		       	   					OemToAnsi( STR0013  )	,;	//"Ordenar Marca┤■es <F5>..."
		       	   					OemToAnsi( STR0113  )	 ;	//"Ordenar"
		           				};
		     )					 	
	EndIF
	
	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Botao para a Consulta ao Calendario de Marcacoes	  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdd(; 
			aButtons	,;
							{;
								"S4SB014N"			,;
   		       					bCalend				,;
   	    	   					OemToAnsi( STR0015 ),;//"Calend═rio de Marca┤■es <F6>..."
   	    	   					OemToAnsi( STR0115 ) ;//"Calend."
   	        				};
   	     )

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁDefine o Botao para Limpar as Ordens                     	  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( ( nOpc == 3 ) .or. ( nOpc == 4 ) )
		aAdd(; 
				aButtons	,;
								{;
									"note"				,;
	   		       					bClrOrd					,;
	   	    	   					OemToAnsi( STR0018  ) 	,;//"Limpar as Ordens <F7>..."
	   	    	   					OemToAnsi( STR0118  ) 	;//"Limpar"
	   	        				};
	   	     )
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta as Dimensoes dos Objetos         					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdvSize		:= MsAdvSize( )
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 0 }
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Bloco para a Teclas <CTRL-O>   ( Button OK da EnchoiЁ
	Ё ceBar )													   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet15		:= { || IF(; 
								( ( nOpc == 3 ) .or. ( nOpc == 4 ) );			//Inclusao ou Alteracao
								.and.;				
								oGdMcFilt:TudoOk(),;							//Valida as Informacoes da GetDados
								(;
									lbSet15 := .T. ,;
									RestKeys( aSvKeys ),;
									oDlg:End();
							 	),;
							 	IF(; 
							 		( ( nOpc == 3 ) .or. ( nOpc == 4 ) ) ,;		//Inclusao ou Visualizacao
							 			(;
							 				lbSet15 := .F. ,;
							 				.F.;
							 			 ),;	
									(;
										lbSet15 := .F.,;						//Visualizacao ou Exclusao
										RestKeys( aSvKeys ),;
										oDlg:End();
							 		);
							 	  );
						   );
					 }

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o  Bloco  para a Teclas <CTRL-X> ( Button Cancel da EnЁ
	Ё choiceBar )												   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet24 := { || RestKeys( aSvKeys , .T. ) , oDlg:End() }

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Bloco para o Init do Dialog						   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bDialogInit := { ||;
						IF( ( nOpc == 3 ) .or. ( nOpc == 4 ) , SetKey( VK_F4 , bOrdMarc	)	, NIL ),;
						IF( ( nOpc == 3 ) .or. ( nOpc == 4 ) , SetKey( VK_F5 , bOrdMarcs )	, NIL ),;
						SetKey( VK_F6 , bCalend  ),;
						IF( ( nOpc == 3 ) .or. ( nOpc == 4 ) , SetKey( VK_F7 , bClrOrd  )	, NIL ),;
						EnchoiceBar( oDlg , bSet15 , bSet24 , NIL , aButtons );
					}

	/*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁMonta Dialogo para a selecao do Periodo 					  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(cCadastro) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL			

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta o Objeto GetDados para o SP8						   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		oGdMcFilt	:= MsNewGetDados():New(	aObjSize[1,1]								,;
											aObjSize[1,2]								,;
											aObjSize[1,3]								,;
											aObjSize[1,4]								,;
											nOpcNewGd									,;
											"Pona010LinOk"								,;
											"Pona010TudOk"								,;
											""											,;
											aSP8GdAltera								,;
											0											,;
											Len(aCols)									,;
											NIL											,;
											NIL											,;
											bSP8GdDelOk									,;
											oDlg										,;
											aHeader										,;
											aCols									 	 ;
										 )
        
	ACTIVATE MSDIALOG oDlg CENTERED ON INIT Eval( bDialogInit )

	IF ( lbSet15 )
		/*/
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Coloca o Cursor do Mouse em estado de Espera  	  		  Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		CursorWait()
			IF ( ( nLoops := Len( aJaProc ) ) > 0 )
				/*/
				зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Substitui as Informacoes Antigas                 	  		  Ё
				юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				For nLoop := 1 To nLoops
					oGetDados:aCols[ aJaProc[ nLoop ] ] := aClone( oGdMcFilt:aCols[ nLoop ] )
				Next nLoop
				/*/
				зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Adiciona as Novas Informacoes                    	  		  Ё
				юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				nLoops := Len( oGdMcFilt:aCols )
				For nLoop := nLoop To nLoops
					aAdd( oGetDados:aCols , aClone( oGdMcFilt:aCols[ nLoop ] ) )
				Next nLoop
				oGetDados:oBrowse:Refresh()
				bSort := { |x,y| (;
									DataHora2Str( x[nPosData] , x[nPosHora] )	;
									< ;
						  			DataHora2Str( y[nPosData] , y[nPosHora] )	;
						  		  );	
				  		}
				aSort( oGetDados:aCols , NIL , NIL , bSort )
			EndIF	
		/*/
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Restaura o Cursor do Mouse								  Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		CursorArrow()
	EndIF		

End Sequence

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura as Teclas de Atalho                     	  		  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestKeys( aSvKeys , .T. )

Return( NIL )

/*/
зддддддддддбддддддддддддддбдддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPona010BldCld ЁAutorЁMarinaldo de Jesus   Ё Data Ё28/08/2003Ё
цддддддддддеддддддддддддддадддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁCriar o Calendario de Marcacoes conforme Periodo            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁPona010()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Pona010BldCld(	aTabPadrao		,;	//Array contendo informacoes da Tabela Padrao
								aTabCalend		,;	//Array contendo informacoes do Calendario de Marcacoes
								dPerIni			,;	//Periodo Inicial Para a Montagem do Calendario
								dPerFim			,;	//Periodo Final Para a Montagem do Calendario
								lShowMsgInfo	 ;	//Se deve Mostrar mensagem de Inconsitencia
							 )

Local cKeyAtu	:= ( cEmpAnt + SRA->( RA_FILIAL + RA_MAT ) + ( Dtos( dPerIni ) + Dtos( dPerFim ) ) )
Local lCalendOk	:= .T.

DEFAULT aTabPadrao		:= {}
DEFAULT aTabCalend		:= {}
DEFAULT lShowMsgInfo	:= .T.

IF ( !( cBldCldUltKey == cKeyAtu ) .or. Empty( aTabCalend ) )
	aTabCalend		:= {}
	cBldCldUltKey	:= cKeyAtu
	IF !( lCalendOk := SRA->( CriaCalend(	dPerIni			,;	//Data Inicial do Calendario
											dPerFim			,;	//Data Final do Calendario
											RA_TNOTRAB		,;	//Turno de Trabalho
											RA_SEQTURN		,;	//Sequencia de Turno
											@aTabPadrao		,;	//Tabela de Horario Padrao
											@aTabCalend		,;	//Calendario de Marcacoes
											RA_FILIAL		,;	//Filial do Funcionario
											RA_MAT			,;	//Matricula do Funcionario
											RA_CC			,;	//Centro de Custo do Funcionario
											{}				 ;	//Trocas de Turno
							  			  );
							);
		)
		IF ( lShowMsgInfo )
			//"N└o foi poss║vel criar o calend═rio de Marca┤■es para o per║odo informado"###"Aviso de Inconsist┬ncia"
			MsgInfo( OemToAnsi( STR0009 ) , OemToAnsi( STR0010 ) )
		EndIF
	EndIF
EndIF

Return( lCalendOk )

/*/
зддддддддддбддддддддддддддбдддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPona010PergChkЁAutorЁMarinaldo de Jesus   Ё Data Ё28/08/2003Ё
цддддддддддеддддддддддддддадддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁAjusta Pergunte com o periodo atual                         Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁPona010()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Pona010PergChk( cPerg , aTabPadrao , aTabCalend )

Local aArea			:= GetArea()
Local aAreaSX1		:= SX1->( GetArea() )
Local dPerIni		:= Ctod("//")
Local dPerFim		:= Ctod("//")
Local dPerCIni		:= Ctod("//")
Local dPerCFim		:= Ctod("//")
Local lPerAponOk    := .F.

CursorWait()

	IF ( lPerAponOk := PerAponta( @dPerIni , @dPerFim , NIL , .T. , xFilial("SRA") ) )
		IF Pona010BldCld( @aTabPadrao , @aTabCalend , dPerIni , dPerFim )
			dPerCIni	:= aTabCalend[ 01					, CALEND_POS_LIM_MARCACAO , 01 ]
			dPerCFim	:= aTabCalend[ Len( aTabCalend )	, CALEND_POS_LIM_MARCACAO , 01 ]
			IF ( dPerCIni < dPerIni )
				dPerIni := dPerCIni
			EndIF
			IF ( dPerCFim > dPerFim )
				dPerFim := dPerCFim
			EndIF
			Pergunte( cPerg , .F. )
			IF ( MV_PAR01 <> dPerIni )
				SetMVValue(cPerg, "MV_PAR01", dPerIni )
			EndIF	
			IF ( MV_PAR02 <> dPerFim )
				SetMVValue(cPerg, "MV_PAR02", dPerFim )
			EndIF	
		EndIF
	EndIF
	Pergunte(cPerg, .F.)
	RestArea( aAreaSX1 )
	RestArea( aArea )

CursorArrow()

Return( lPerAponOk )

/*/
зддддддддддбдддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁAltPona010	 ЁAutor ЁMarinaldo de Jesus    Ё Data Ё03/09/2003Ё
цддддддддддедддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁExecuta a Opcao de Alteracao do PONA010              		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>   								 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>   								 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                                                    	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerica                                                     Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function AltPona010( cAlias , nReg , nOpc )
Return( PONA010( cAlias , nReg , 4 , NIL , NIL ) )

/*/
зддддддддддбддддддддддддддддбдддддбддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPona010Locks    ЁAutorЁMauricio MR         Ё Data Ё14/04/2004Ё
цддддддддддеддддддддддддддддадддддаддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁBloqueia Lancamentos de Marcacoes /Apont/Abonos		         Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide parametros formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL		                                               	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁPONA010                                                      Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Pona010Locks( nOpc , cAlias , aRecnos )

Local lLocks	:= .T.

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSe nao For Visualizacao				 					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( nOpc == 2 )
		Break
	EndIF

	IF !( lLocks := WhileNoLock( cAlias , aRecnos , { xFilial( cAlias ) + SRA->RA_MAT } , 1 , 1 , .T. , NIL ) )
		Break
	EndIF

End Sequence

Return( lLocks )            

/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё MenuDef		ЁAutorЁ  Luiz Almeida     Ё Data Ё14/11/2006Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁIsola opcoes de menu para que as opcoes da rotina possam    Ё
Ё          Ёser lidas pelas blibliotecas Framework da Versao 9.12 .     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA010                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Static Function MenuDef()

 Local aRotina := {;	   
 							{ STR0001 , "PesqBrw"	 , 0 , 01, ,.F.} ,; //"Pesquisar"
							{ STR0002 , "Pona010Mnt" , 0 , 02} ,; //"Visualizar"
							{ STR0008 , "GpLegend"   , 0 , 05, ,.F.}; //"Legenda"
							}                                                     
							
	Return aRotina
