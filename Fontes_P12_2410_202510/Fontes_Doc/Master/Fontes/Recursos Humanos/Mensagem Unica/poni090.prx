#INCLUDE "TBICONN.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWADAPTEREAI.CH"
#INCLUDE "PONCALEN.CH"
#INCLUDE "PONI090.CH"

#DEFINE PON_JORNADA		"1"
#DEFINE PON_INTERVALO	"2"
#DEFINE PON_LANCHE		"3"
#DEFINE SEP 	" |"

STATIC aResult := { .T.,"" }

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥Integdef  ∫Autor  ≥Microsiga           ∫ Data ≥  09/11/12   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥ Funcao de integracao com o adapter EAI para recebimento e  ∫±±
±±∫          ≥ envio de informaÁıes do cadastro de ExceÁıes				  ∫±±
±±∫          ≥ (SP2) utilizando o conceito de mensagem unica. 		      ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ PONA090                                                    ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

Function PONI090( cXML, nTypeTrans, cTypeMessage, cVersaoMsg )

Local cVersaoTrat 	:= "1.000|"
Local lRet			:= .T.
 			
Local cCodFunEx		:= ""           //Codigo externo de Funcionario
Local cTurnExt		:= ""           //Codigo externo do Turno de Trabalho
Local cCustoExt		:= ""           //Codigo externo do Centro de Custo
Local cReason	  	:= ""           //Codigo interno do Motivo da exceÁ„o
Local cStartDt	  	:= ""           //Codigo interno da Data inicial da exceÁ„o
Local cEndDt	  	:= ""           //Codigo interno da Data final da exceÁ„o
Local cCodFun	  	:= ""           //Codigo interno da Matricula do funcion·rio
Local cTurno	  	:= ""           //Codigo interno do CÛdigo do turno de trabalho
Local cCusto	  	:= ""           //Codigo interno do CÛdigo do centro de custo
Local cDiaTrab	  	:= ""           //Codigo interno do tipo de dia da exceÁ„o Dia trabalhado
Local cTipoDia		:= ""           //Codigo interno do tipo de dia Dia trabalhado
Local cTipoHora	  	:= ""           //Codigo interno do Tipo de hor·rio Jornada
Local nComHora	  	:= ""           //Codigo interno da Hora de Entrada
Local nFimHora	  	:= ""           //Codigo interno da Hora de Saida
Local cXMLRet	  	:= ""
Local cEvento		:= "upsert"
Local cXmlErro		:= ""
Local cXmlWarn		:= ""
Local cValExt		:= ""
Local cValInt		:= ""
Local cLogErro  	:= ""

Local aErroAuto		:= {}
Local aExceAu		:= {}
Local aArea			:= GetArea()
Local aHoras		:= {}

Local nCount		:= 0
Local nOpcx			:= 0
Local nI			:= 0

Local oXmlMvInt		:= Nil
Local oXmlBusin		:= Nil
Local oXmlArr 		:= Nil

Local aRetor		:= {}
Local xAux 			:= Nil
Local lExisteReg 	:= .T.

DEFAULT cVersaoMsg := "1.000"

Private lMsErroAuto    := .F.
Private lMsHelpAuto    := .T.
Private lAutoErrNoFile := .T.

If nTypeTrans == TRANS_RECEIVE
	
	If cTypeMessage == EAI_MESSAGE_BUSINESS
		
		If cVersaoMsg $ cVersaoTrat
			oXmlMvInt	:= XmlParser( cXML, '_', @cXmlErro, @cXmlWarn)
			
			If oXmlMvInt <> Nil .And. Empty(cXmlErro) .And. Empty(cXmlWarn)
						
				cMarca := oXmlMvInt:_TotvsMessage:_MessageInformation:_Product:_Name:Text
				
				oXmlBusin := oXmlMvInt:_TotvsMessage:_BusinessMessage
				
				If XmlChildEx(oXmlBusin, '_BUSINESSEVENT') <> Nil .And. XmlChildEx(oXmlBusin:_BusinessEvent, '_EVENT' ) <> Nil   
				
					cEvento := Upper( oXmlBusin:_BusinessEvent:_Event:Text )
					
					If XmlChildEx(oXmlBusin:_BusinessEvent, '_IDENTIFICATION') <> Nil .And.;
						XmlChildEx(oXmlBusin:_BusinessEvent:_Identification, '_KEY') <> Nil
						
						cValExt := oXmlBusin:_BusinessEvent:_Identification:_Key:Text
					
					ElseIf XmlChildEx(oXmlBusin:_BusinessContent, '_INTERNALID') <> Nil
					
						cValExt := oXmlBusin:_BusinessContent:_InternalId:Text
					Else
						lRet := .F.
						cXmlRet := STR0001 // "Chave do registro n„o enviada"
					EndIf				
					
					If FindFunction("CFGA070INT")
						
						DbSelectArea( "SP2" )
						SP2->( DbSetOrder( 3 ) ) // P2_FILIAL+P2_MAT+P2_CC+P2_TURNO+DTOS(P2_DATA)+P2_TIPODIA
						
						DbSelectArea('SRA')
						SRA->( DbSetOrder( 1 ) ) // RA_FILIAL+RA_MAT
						
						DbSelectArea('SR6')
						SR6->( DbSetOrder( 1 ) )  // R6_FILIAL+R6_TURNO

						DbSelectArea('CTT')
						CTT->( DbSetOrder( 1 ) )  // CTT_FILIAL+CTT_CUSTO
						
		     			cValInt := PONI090Rcv( CFGA070INT( cMarca, "SP2", "P2_MAT", cValExt ), { "P2_FILIAL", "P2_MAT", "P2_CC", "P2_TURNO", "P2_DATA" } )
		     							
						If cEvento == "UPSERT" .And. Empty( cValInt )
							nOpcx := 3
							
							lExisteReg := .F.
							
							Aadd( aExceAu, { "P2_FILIAL", xFilial("SP2"), Nil } )
						
						ElseIf cEvento == "UPSERT" .And. !Empty( cValInt ) .And. ;
							SP2->( DbSeek( cValInt ) )

							nOpcx	:= 4
							Aadd( aExceAu, { "P2_FILIAL", SP2->P2_FILIAL, Nil } )
								
						ElseIf cEvento == "DELETE" .And. !Empty( cValInt ) .And. ;
							SP2->( DbSeek( cValInt ) )
							
							nOpcx := 5
							Aadd( aExceAu, { "P2_FILIAL", SP2->P2_FILIAL, Nil } )
						Else
						
							lRet := .F.
							cXmlRet := STR0002 // "CombinaÁ„o de chave e operaÁ„o inv·lida"
							
						EndIf
						
						//---------------------------------------------------
						//  Caso seja a alteraÁ„o de uma exceÁ„o realiza o 
						// posicionamento em cada tabela relacionada 
						// Centro de Custo, Turno, Funcion·rio
						If lExisteReg
							
							cStartDt 	:= SP2->P2_DATA
							cEndDt  	:= SP2->P2_DATAATE
							cCodFun 	:= SP2->P2_MAT
							cTurno  	:= SP2->P2_TURNO
							cCusto  	:= SP2->P2_CC

							Aadd( aExceAu, { "P2_DATA"   , cStartDt, Nil })
							Aadd( aExceAu, { "P2_DATAATE", cEndDt  , Nil })
							
							If !Empty( cCodFun ) .And. SRA->( DbSeek( xFilial("SRA")+cCodFun ) )
								Aadd( aExceAu, { "P2_MAT"    , cCodFun , Nil })
							Else
								Aadd( aExceAu, { "P2_MAT"    , Space( TamSX3("P2_MAT")[1]), Nil })
							EndIf
							
							If !Empty( SP2->P2_CC ) .And. CTT->( DbSeek( xFilial("CTT")+SP2->P2_CC ) )
								Aadd( aExceAu, { "P2_CC"     , cCusto  , Nil })
							Else
								Aadd( aExceAu, { "P2_CC"     , Space( TamSX3("P2_CC")[1]), Nil })
							EndIf
							
							If !Empty( SP2->P2_TURNO ) .And. SR6->( DbSeek( xFilial("SR6")+ SP2->P2_TURNO ) )
								Aadd( aExceAu, { "P2_TURNO"  , cTurno  , Nil })
							Else
								Aadd( aExceAu, { "P2_TURNO"  , Space( TamSX3("P2_TURNO")[1]), Nil })
							EndIf
							
						Else
						
							If lRet
								If XmlChildEx(oXmlBusin:_BusinessContent, '_STARTDATE') <> NIL
									cStartDt := STOD( StrTran( SubStr(oXmlBusin:_BusinessContent:_StartDate:Text, 1, 10 ), "-", "") )
									Aadd( aExceAu, { "P2_DATA", cStartDt, Nil })
								Else
									lRet := .F.
									cXMLRet += STR0003 + 'StartDate' + SEP // 'N„o existe a Tag '
								EndIf
							EndIf
							
							If lRet
								If XmlChildEx(oXmlBusin:_BusinessContent, '_ENDDATE') <> NIL
									cEndDt := STOD( StrTran( SubStr( oXmlBusin:_BusinessContent:_EndDate:Text, 1, 10 ), "-", "" ) )
									Aadd( aExceAu, { "P2_DATAATE", cEndDt, Nil })
								ElseIf !Empty( cStartDt )
									cEndDt := cStartDt
									Aadd( aExceAu, { "P2_DATAATE", cEndDt, Nil })
								Else
									cXMLRet += STR0003 + 'EndDate' + SEP // 'N„o existe a Tag '
								EndIf
							EndIf 
		
							If lRet
								If XmlChildEx(oXmlBusin:_BusinessContent, '_EMPLOYEECODE') <> NIL
								
									cCodFunEx	:= oXmlBusin:_BusinessContent:_EmployeeCode:Text
									xAux     := IntFunInt( cCodFunEx, cMarca, "2.003")
									
									If xAux[1]

										cCodFun := xAux[2][2] + xAux[2][3]									
										
										If !Empty(cCodFun) .AND. SRA->( DbSeek( cCodFun ) )
											cCodFun := SRA->RA_MAT 
											Aadd( aExceAu, { "P2_MAT", cCodFun, Nil })
										EndIf
									EndIf
								Else	
									Aadd( aExceAu, { "P2_MAT", Space( TamSX3("P2_MAT")[1]), Nil })
								EndIf
							EndIf
		
							If lRet
								If XmlChildEx(oXmlBusin:_BusinessContent, '_SHIFTCODEWORKINTERNALID') <> NIL
									
									cTurnExt	:= oXmlBusin:_BusinessContent:_ShiftCodeWorkInternalId:Text
									cTurno		:= CFGA070INT( cMarca, 'SR6', 'R6_TURNO', cTurnExt )
									cTurno 		:= GPEI080Rcv( cTurno, { "R6_FILIAL", "R6_TURNO" } )
									
									If !Empty(cTurno) .AND. SR6->(DbSeek( cTurno ) )
										cTurno := SR6->R6_TURNO                  
										Aadd( aExceAu, { "P2_TURNO", cTurno, Nil })
									EndIf
								Else	
									Aadd( aExceAu, { "P2_TURNO", Space( TamSX3("P2_TURNO")[1]), Nil })
								EndIf
							EndIf
							
							If lRet
								// verifica se foi enviado centro de custo na exceÁ„o
								If XmlChildEx(oXmlBusin:_BusinessContent, '_COSTCENTERINTERNALID') <> NIL
									
									cCustoExt	:= oXmlBusin:_BusinessContent:_CostCenterInternalId:Text
									xAux		:= IntCusInt( cCustoExt, cMarca, '2.000')
	 
									If xAux[1]
										cCusto := xAux[2,2]+xAux[2,3]
										
										If !Empty(cCusto) .AND. CTT->(DbSeek( cCusto ) )
											cCusto := CTT->CTT_CUSTO      
											Aadd( aExceAu, { "P2_CC", cCusto, Nil } )
										EndIf
									EndIf
								Else
									// caso n„o tenha enviado CC na mensagem usa o do funcion·rio
									cCusto := SRA->RA_CC
 									Aadd( aExceAu, { "P2_CC", cCusto, Nil } )
								EndIf
							EndIf

						EndIf // final da identificaÁ„o de alteraÁ„o de registro

						If lRet .And. XmlChildEx(oXmlBusin:_BusinessContent, '_REASONFOREXCEPTION') <> NIL
							cReason := oXmlBusin:_BusinessContent:_ReasonForException:Text 
							Aadd( aExceAu, { "P2_MOTIVO", cReason, Nil } )
						EndIf
						
						If lRet
							Aadd( aExceAu, { "P2_CODHEXT", "1", Nil })
							Aadd( aExceAu, { "P2_CODHNOT", "1", Nil })
							Aadd( aExceAu, { "P2_HERDHOR", "N", Nil })
							Aadd( aExceAu, { "P2_MINHNOT",     SuperGetMv( "MV_MINHNOT" , NIL , .F. )  , Nil })
							Aadd( aExceAu, { "P2_HORMENO",Val( SuperGetMv( "MV_HORMENO" , NIL , .F. ) ), Nil })
							Aadd( aExceAu, { "P2_HORMAIS",Val( SuperGetMv( "MV_HORMAIS" , NIL , .F. ) ), Nil })
						EndIf

	  					If lRet .And. XmlChildEx( oXmlBusin:_BusinessContent, "_LISTOFEXCEPTION" ) <> Nil .And. ;
	  						XmlChildEx( oXmlBusin:_BusinessContent:_ListOfException, "_EXCEPTION" ) <> Nil
	  						
							If ValType( oXmlBusin:_BusinessContent:_ListOfException:_Exception ) <> "A"
								XmlNode2Arr(oXmlBusin:_BusinessContent:_ListOfException:_Exception, "_Exception")
							EndIf

							oXmlArr := oXmlBusin:_BusinessContent:_ListOfException:_Exception
							
							For nCount := 1 To Len( oXmlArr )
							
								If lRet
									If XmlChildEx( oXmlArr[nCount], '_WORKEDDAY') <> NIL
										
										cDiaTrab := oXmlArr[nCount]:_WorkedDay:Text
										
										If cDiaTrab == "1"
											Aadd( aExceAu, { "P2_TRABA", "S", Nil })
										ElseIf cDiaTrab == "2"
											Aadd( aExceAu, { "P2_TRABA", "C", Nil })
										ElseIf cDiaTrab == "3"
											Aadd( aExceAu, { "P2_TRABA", "D", Nil })
										ElseIf cDiaTrab == "4"
											Aadd( aExceAu, { "P2_TRABA", "N", Nil })
										EndIf
										
									Else
										lRet := .F.
										cXMLRet += STR0003 + 'WorkedDay' + SEP // 'N„o existe a Tag '
									EndIf
								EndIf
				                   
			 		            If lRet
				 		            If !Empty(cCodFun) .AND. cStartDt == cEndDt
				 		            	
				 		            	cTipoDia := Space(TamSX3("P2_TIPODIA")[1])
				 		            	Aadd( aExceAu, { "P2_TIPODIA", cTipoDia, Nil })
				 		            	
									ElseIf XmlChildEx( oXmlArr[nCount], '_DAYTYPE') <> NIL
									
										cTipoDia	:= oXmlArr[nCount]:_DayType:Text 
		
										If cTipoDia == "1"
											Aadd( aExceAu, { "P2_TIPODIA", "S", Nil })
		
										ElseIf cTipoDia == "2"
											Aadd( aExceAu, { "P2_TIPODIA", "C", Nil })
		
										ElseIf cTipoDia == "3"
											Aadd( aExceAu, { "P2_TIPODIA", "D", Nil })
		
										ElseIf cTipoDia == "4"
											Aadd( aExceAu, { "P2_TIPODIA", "N", Nil })
										EndIf
									
									EndIf
								EndIf
									
								If lRet .And. XmlChildEx( oXmlArr[nCount], "_LISTOFEXCEPTIONHOUR") <> Nil .And. ;
									XmlChildEx( oXmlArr[nCount]:_ListOfExceptionHour, "_EXCEPTIONHOUR") <> Nil

									If ValType( oXmlArr[nCount]:_ListOfExceptionHour:_ExceptionHour ) <> "A"
										XmlNode2Arr( oXmlArr[nCount]:_ListOfExceptionHour:_ExceptionHour, "_ExceptionHour")
									EndIf
									
									aSize( aHoras, 0)
									
									For nI:= 1 To Len( oXmlArr[nCount]:_ListOfExceptionHour:_ExceptionHour )
										
										If lRet
											If XmlChildEx( oXmlArr[nCount]:_ListOfExceptionHour:_ExceptionHour[nI], "_HOURTYPE") <> Nil
												cTipoHora := oXmlArr[nCount]:_ListOfExceptionHour:_ExceptionHour[nI]:_HourType:Text									
											Else
												lRet := .F.
												cXMLRet += STR0003 + 'HourType' + SEP // 'N„o existe a Tag '
											EndIf 
										EndIf
										
										If lRet
											If XmlChildEx( oXmlArr[nCount]:_ListOfExceptionHour:_ExceptionHour[nI], "_STARTHOUR") <> Nil
												nComHora := Val( oXmlArr[nCount]:_ListOfExceptionHour:_ExceptionHour[nI]:_StartHour:Text )
											Else
												lRet:= .F.
												cXMLRet += STR0003 + 'StartHour' + SEP // 'N„o existe a Tag '
											EndIf         
										EndIf
										
										If lRet
											If XmlChildEx( oXmlArr[nCount]:_ListOfExceptionHour:_ExceptionHour[nI], "_ENDHOUR") <> Nil
												nFimHora := Val( oXmlArr[nCount]:_ListOfExceptionHour:_ExceptionHour[nI]:_EndHour:Text )
											Else
												lRet:= .F.
												cXMLRet += STR0003 + 'EndHour' + SEP // 'N„o existe a Tag '
												Exit
											EndIf	
										EndIf
	
										If !Empty(cTipoHora) .And. ; // tipo da hora deve estar preenchido
											( ( !Empty(nComHora) .AND. !Empty(nFimHora) ) .Or. ; // entrada e saÌda diferentes de zero
											  ( Empty(nComHora)  .AND. !Empty(nFimHora) ) .Or. ; // entrada 00h e saÌda diferentes de zero
											  ( !Empty(nComHora) .AND. Empty(nFimHora) ) ) // entrada diferentes de zero e saÌda 00h
											
											Aadd ( aHoras, { cTipoHora,nComHora,nFimHora } )
										Else
											lRet := .F.
											cXMLRet += STR0004 + SEP // 'Houve erro na montagem das Horas de Entrada e Saida'
											Exit
										EndIf
								
									Next nI

								EndIf
								
								If !lRet
									Exit
								EndIf
								
							Next nCount
							
							If lRet
								If Len(aHoras) > 0
									cXmlErro := ""
									PONInter( aHoras, aExceAu, @cXmlErro )
									aHoras := {}
									
									If !Empty( cXmlErro )
										lRet := .F.
										cXmlRet := cXmlErro
									EndIf
								Else
			    					lRet := .F.
			    					cXMLRet += STR0005 + SEP // "O n˙mero de itens identificados n„o corresponde com o n˙mero de itens lidos com sucesso "
								EndIf
							EndIf
						EndIf
	
					Else
						lRet   := .F.
						cXMLRet += STR0006 // "Atualize EAI"
					EndIf
				EndIf
				
				If lRet 
					
					BeginTran()
					
					MsExecAuto({|x,y| PONA090(x,y)}, aExceAu, nOpcx)
				
					If lMsErroAuto
						
						DisarmTransaction()
						aErroAuto:= GetAutoGRLog()
						
						For nCount := 1 To Len(aErroAuto)
							cLogErro += StrTran( StrTran( StrTran( StrTran( StrTran( aErroAuto[nCount], CHR(10), "" ), CHR(13), "" ), "/", "" ), "<", "" ), ">", "" ) + SEP
						Next nCount
						
						lRet	 := .F.
						cXMLRet := EncodeUTF8( cLogErro )
		
					Else
						cValInt := PONI090Snd( { cEmpAnt, xFilial("SP2"), cCodFun, cCusto, cTurno, DTOS( cStartDt ) } )
						
						If nOpcx <> 5

							CFGA070Mnt( cMarca, "SP2", "P2_MAT", cValExt, cValInt )
							
							//----------------------------------------------------------------
							//  Dados ok para gravaÁ„o
							//----------------------------------------------------------------
							cXMLRet += '<ListOfInternalId>'
								cXMLRet += '<InternalId>'
									cXMLRet += '<Name>'+ "EMPLOYEEEXCEPTION" +'</Name>'
									cXMLRet += '<Origin>'+ cValExt +'</Origin>'
									cXMLRet += '<Destination>'+ cValInt +'</Destination>'
								cXMLRet += '</InternalId>'
							cXMLRet += '</ListOfInternalId>'
							
						Else
							
							CFGA070Mnt( , "SP2", "P2_MAT", , cValInt, .T. )
							
						EndIf
						
						EndTran()
						
					EndIf
				EndIf
					
			Else
				lRet := .F.
				cXMLRet := STR0007 + cXmlErro + '|' + cXmlWarn // 'Xml mal formatado '
			EndIf
		Else
		
			lRet := .F.
			cXmlRet := STR0008 // "Vers„o da mensagem n„o tratada pelo Adapter"
		
		EndIf
		
	ElseIf cTypeMessage == EAI_MESSAGE_RESPONSE

		//-----------------------------------------------------------
		//  Trata o retorno da Response no formato do InternalIdType
		TrataResp( @lRet, @cXmlRet, cXml )
		
	ElseIf cTypeMessage == EAI_MESSAGE_WHOIS
		
		cXMLRet := cVersaoTrat
		
	EndIf
	
ElseIf nTypeTrans == TRANS_SEND

	DbSelectArea("SP2")
	SP2->(DbSetOrder(3))//P2_FILIAL+P2_MAT+P2_CC+P2_TURNO+DTOS(P2_DATA)+P2_TIPODIA
	
	cValInt := PONI090Snd( { cEmpAnt, xFilial("SP2"), SP2->P2_MAT, SP2->P2_CC, SP2->P2_TURNO, DTOS( SP2->P2_DATA ) } )
	
	If !Inclui .AND. !Altera
		cEvento	:=	"Delete"
		
		CFGA070Mnt( , "SP2", "P2_MAT", , cValInt )
		
	EndIf
	
	//Monta XML de envio de mensagem unica
	cXMLRet := '<BusinessEvent>'
		cXMLRet += '<Entity>EmployeeException</Entity>'
		cXMLRet += '<Event>' + cEvento + '</Event>'
		cXMLRet += '<Identification>'
			cXMLRet += '<key name="Code">' + cValInt + '</key>'
		cXMLRet += '</Identification>'
	cXMLRet += '</BusinessEvent>'
	cXMLRet += '<BusinessContent>'
		cXMLRet += '<CompanyId>'+ cEmpAnt +'</CompanyId>'
		//cXMLRet += '<BranchId>'+ xFilial("SP2") +'</BranchId>'
		cXMLRet += '<BranchId>'+ cFilAnt +'</BranchId>'
		cXMLRet += '<ReasonForException>'+ SP2->P2_MOTIVO +'</ReasonForException>'
		cXMLRet += '<StartDate>'+ Transform( DtoS(SP2->P2_DATA), "@R 9999-99-99" ) +'</StartDate>'
		cXMLRet += '<EndDate>'+ Transform( DtoS(SP2->P2_DATAATE), "@R 9999-99-99" ) +'</EndDate>'
		
		cXMLRet += '<InternalId>'+ cValInt +'</InternalId>'  // cÛdigo para gravaÁ„o do de-para pela RM
		
		If !Empty( SP2->P2_MAT )
			cXMLRet += '<EmployeeCode>'+ IntFunExt( , , SP2->P2_MAT, "2.003" )[2] +'</EmployeeCode>'  // CÛdigo do funcion·rio conforme a mensagem Employee
		EndIf
		
		cTurno := IIF( !Empty( SP2->P2_TURNO ), SP2->P2_TURNO, SRA->RA_TNOTRAB )
		cTurno := GPEI080Snd( { cEmpAnt, xFilial("SR6"), cTurno } )
		cXMLRet += '<ShiftCodeWorkInternalId>'+ cTurno +'</ShiftCodeWorkInternalId>'
		cXMLRet += '<ShiftCodeWork>'+ cTurno +'</ShiftCodeWork>'
	
		cCusto := IIF( !Empty( SP2->P2_CC ), SP2->P2_CC, SRA->RA_CC )
		cXMLRet += '<CostCenterInternalId>'+ IntCusExt(, xFilial("CTT"), cCusto, '2.000')[2] +'</CostCenterInternalId>'
				
		cXMLRet += '<ListOfException>'       
			cXMLRet += '<Exception>'
			
			cTipoDia := SP2->P2_TRABA
			
			If SP2->P2_TRABA == "S"
				cTipoDia := "1"
			ElseIf SP2->P2_TRABA == "N"
				cTipoDia := "4"
			ElseIf SP2->P2_TRABA == "C"
				cTipoDia := "2"
			ElseIf SP2->P2_TRABA == "D"
				cTipoDia := "3"
			Else
				cTipoDia := ""
			EndIf
			
			cXMLRet += '<WorkedDay>'+ cTipoDia +'</WorkedDay>' 	
			
			cTipoDia := SP2->P2_TIPODIA
			
			If SP2->P2_TRABA == "S"
				cTipoDia := "1"
			ElseIf SP2->P2_TRABA == "N"
				cTipoDia := "4"
			ElseIf SP2->P2_TRABA == "C"
				cTipoDia := "2"
			ElseIf SP2->P2_TRABA == "D"
				cTipoDia := "3"
			Else
				cTipoDia := ""
			EndIf 
			
			cXMLRet += '<DayType>'+ cTipoDia +'</DayType>' 
			
			cXMLRet += '<ListOfExceptionHour>'
			
				PONExter(aRetor)
		    	For nI:= 1 To Len(aRetor)
		    		If nI == 1
						cXMLRet += '<ExceptionHour>'
							cXMLRet += '<HourType>' + aRetor[nI][1] +'</HourType>' 
							cXMLRet += '<StartHour>'+ STR( aRetor[nI][2], 5, 2 ) +'</StartHour>' 
							cXMLRet += '<EndHour>'  + STR( aRetor[nI][3], 5, 2) +'</EndHour>' 
						cXMLRet += '</ExceptionHour>'
					EndIf
					If nI == 2
						cXMLRet += '<ExceptionHour>'
							cXMLRet += '<HourType>' + aRetor[nI][1] +'</HourType>' 
							cXMLRet += '<StartHour>'+ STR( aRetor[nI][2], 5, 2) +'</StartHour>' 
							cXMLRet += '<EndHour>'  + STR( aRetor[nI][3], 5, 2) +'</EndHour>' 
						cXMLRet += '</ExceptionHour>'
					EndIf
					If nI == 3
						cXMLRet += '<ExceptionHour>'
							cXMLRet += '<HourType>' + aRetor[nI][1] +'</HourType>' 
							cXMLRet += '<StartHour>'+ STR( aRetor[nI][2], 5, 2) +'</StartHour>' 
							cXMLRet += '<EndHour>'  + STR( aRetor[nI][3], 5, 2) +'</EndHour>' 
						cXMLRet += '</ExceptionHour>'
					EndIf
					If nI == 4
						cXMLRet += '<ExceptionHour>'
							cXMLRet += '<HourType>' + aRetor[nI][1] +'</HourType>' 
							cXMLRet += '<StartHour>'+ STR( aRetor[nI][2], 5, 2) +'</StartHour>' 
							cXMLRet += '<EndHour>'  + STR( aRetor[nI][3], 5, 2) +'</EndHour>' 
						cXMLRet += '</ExceptionHour>'     
					EndIf
				Next nI
	 	
				cXMLRet += '</ListOfExceptionHour>'
			cXMLRet += '</Exception>'
		cXMLRet += '</ListOfException>'
	cXMLRet += '</BusinessContent>'
	 
	
EndIf

RestArea(aArea)

cXMLRet := Encodeutf8(cXMLRet)

Return { lRet, cXMLRet }   

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥PONInter  ∫Autor  ≥Microsiga           ∫ Data ≥  08/03/13   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥FunÁ„o Auxilar para construÁ„o e retirada dos dados do XML  ∫±±
±±∫          ≥ 					  										  ∫±±
±±∫          ≥Passa aCampos e cErroMsg por referencia. lRet .F. quando    ∫±±
±±∫          ≥ houver erro e em cErroMsg a msg de erro.                   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ PONI090                                                    ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

Static Function PONInter(aHrs,aExceAu,cErroMsg)

Local aEntradas		:= {}
Local aSaidas		:= {}
Local aItem			:= {}
Local aBatidas		:= {}
Local aCampos		:= {}

Local nJornada		:= 0
Local nLanche		:= 0
Local nIntervalo	:= 0
Local nI			:= 1
Local nMaxBatidas 	:= 0

Local cHrEntr		:= ""			// Hora de Entrada

Local lRet			:= .T.

For nI := 1 To Len(aHrs)
	
	If aHrs[nI][1] == PON_JORNADA
		nJornada++
		cHrEntr := aHrs[nI][2]				//Horario de Entrada
		AAdd(aBatidas,{aHrs[nI][2],"N"})
		AAdd(aBatidas,{aHrs[nI][3],"N"})

	ElseIf aHrs[nI][1] == PON_LANCHE
		nLanche++				
		AAdd(aBatidas,{aHrs[nI][2],"N"})
		AAdd(aBatidas,{aHrs[nI][3],"N"})

	ElseIf aHrs[nI][1] == PON_INTERVALO
		nIntervalo++
		AAdd(aBatidas,{aHrs[nI][2],"S"})
		AAdd(aBatidas,{aHrs[nI][3],"N"})

	EndIf

Next nI

//ValidaÁıes
If nJornada != 1
	cErroMsg += SEP + STR0009  // "Deve ser informado sempre apenas um registro do tipo Jornada."	
EndIf

If nLanche+nIntervalo > 3
	cErroMsg += SEP + STR0010 // "O Microsiga Protheus aceita apenas 4 entradas e saÌdas."
EndIf

lRet := Empty(cErroMsg)

If lRet 	 			

	//Orderna Entradas e SaÌdas
	aSort(aBatidas,,,{|x,y| (x[1] < y[1])})			

	//ComeÁa com o Horario de Entrada
	nPos := AScan(aBatidas,{|x| x[1]  == cHrEntr})

	nMaxBatidas := Len( aBatidas )
	
	// percorre a lista
	nI := 1 
	While nI <= ( nMaxBatidas / 2 )
		
		AAdd(aCampos,{ /*1-Entrada*/, /*2-SaÌda*/, /*3-Intervalo*/ } )
	
		aCampos[nI, 1] := aBatidas[nPos][1] //1-Entrada 
		
		If ++nPos > nMaxBatidas
			nPos := 1
		EndIf
		
		aCampos[nI, 2] := aBatidas[nPos][1] //SaÌda
		aCampos[nI, 3] := aBatidas[nPos][2] //Intervalo
		
		If ++nPos > nMaxBatidas
			nPos := 1
		EndIf
		
		nI++
	End 

EndIf

For nI := 1 To Len(aCampos)

	If nI == 1
		Aadd( aExceAu, { "P2_INTERV1",aCampos[nI][3], Nil })
		Aadd( aExceAu, { "P2_ENTRA1" ,aCampos[nI][1], Nil })
		Aadd( aExceAu, { "P2_SAIDA1" ,aCampos[nI][2], Nil })
	EndIf
	
	If nI == 2
		Aadd( aExceAu, { "P2_INTERV2",aCampos[nI][3], Nil })
		Aadd( aExceAu, { "P2_ENTRA2" ,aCampos[nI][1], Nil })
		Aadd( aExceAu, { "P2_SAIDA2" ,aCampos[nI][2], Nil })
	EndIf
	
	If nI == 3
		Aadd( aExceAu, { "P2_INTERV3",aCampos[nI][3], Nil })
		Aadd( aExceAu, { "P2_ENTRA3" ,aCampos[nI][1], Nil })
		Aadd( aExceAu, { "P2_SAIDA3" ,aCampos[nI][2], Nil })
	EndIf

	If nI == 4
		Aadd( aExceAu, { "P2_ENTRA4" ,aCampos[nI][1], Nil })
		Aadd( aExceAu, { "P2_SAIDA4" ,aCampos[nI][2], Nil })
	EndIf

Next nI

Return

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥PONExter  ∫Autor  ≥Microsiga           ∫ Data ≥  08/03/13   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥FunÁ„o Auxilar para construÁ„o e retirada dos dados do XML  ∫±±
±±∫          ≥ 					  										  ∫±±
±±∫          ≥Passa aCampos e cErroMsg por referencia. lRet .F. quando    ∫±±
±±∫          ≥ houver erro e em cErroMsg a msg de erro.                   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ PONI090                                                    ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

Static Function PONExter(aRet)

Local aHrs := {}

Local nI 	      := 0
Local nContInt    := 0

Local nEntra 	  := 0
Local nSaida 	  := 0
Local nUltimoInt  := 0

Local cInter 	  := ""
Local cTipo 	  := ""
Local aFuncCalend := {}
Local aFuncPadrao := {}

//-------------------------------------------------------
//  Quando hor·rio n„o for preenchido È preciso verificar
// o conte˙do da tabela de hor·rio padr„o
If SP2->P2_HERDHOR <> "S" 
	For nI := 1 To 4
	
		nEntra := SP2->&("P2_ENTRA"+cValToChar(nI))
		nSaida := SP2->&("P2_SAIDA"+cValToChar(nI))
	
		If nI < 4
			cInter := SP2->&("P2_INTERV"+cValToChar(nI))
		Else
			cInter := "N"
		EndIf
	
		AAdd(aHrs,{nEntra,"N"})
		AAdd(aHrs,{nSaida,cInter})
		
		//----------------------------------------------------------
		//  Caso o valor do hor·rio de entrada esteja "prÛximo"
		// de meia noite aceita o valor 0.00
		If nSaida <> 0 .Or. ( nEntra >= 21 )
			nUltimoInt := Len( aHrs )
		EndIf
	
	Next nI
Else
	DbSelectArea( "SR6" )
	SR6->( DbSetOrder( 1 ) )
	SR6->( DbSeek( xFilial("SR6")+SRA->RA_TNOTRAB ) )
	
	DbSelectArea("SPJ")
	SPJ->( DbSetOrder( 1 ) )
	SPJ->( DbSeek( xFilial("SPJ")+SRA->( RA_TNOTRAB + RA_SEQTURN ) ) )

	CriaCalend( SP2->P2_DATA, ;
				SP2->P2_DATA, ;
				SRA->RA_TNOTRAB, ;
				SRA->RA_SEQTURN, ;
				@aFuncPadrao   , ;
				@aFuncCalend   , ;
				SRA->RA_FILIAL , ;
				SRA->RA_MAT )
	
	If Len( aFuncCalend ) > 0
		SPJ->( DbSeek( xFilial("SPJ")+aFuncCalend[1, CALEND_POS_TURNO]+ ; // filial + turno
						aFuncCalend[1, CALEND_POS_SEQ_TURNO]+ ; // sequÍncia do turno
						cValToChar( DOW( aFuncCalend[1, CALEND_POS_DATA] ) ) ) ) // dia da semana
		
		For nI := 1 To Len( aFuncCalend )
			
			If "E" $ aFuncCalend[nI, CALEND_POS_TIPO_MARC]
				cTipo := "N"
			Else
				
				nContInt++
				cTipo := SPJ->&("PJ_INTERV"+cValToChar(nContInt))
				
			EndIf
			
			aAdd( aHrs, { aFuncCalend[nI, CALEND_POS_HORA] , cTipo } )
		
		Next nI
		
		nUltimoInt := Len( aHrs )
	EndIf
	
EndIf

//----------------------------------------------------------
//  Verifica se houve a configuraÁ„o com intervalos
If nUltimoInt > 0
	//Adiciona a Jornada
	AAdd(aRet,{"1",aHrs[1][1],aHrs[nUltimoInt][1] } )
	
	For nI := 2 To Len(aHrs)-1 Step 2	
		
		If aHrs[nI][2] == "S"
			cTipo := "2"
		Else
			cTipo := "3"
		EndIf
		
		If aHrs[nI][1] <> 0 .And. aHrs[nI+1][1] <> 0
			AAdd( aRet, { cTipo, aHrs[nI][1], aHrs[nI+1][1] } )
		EndIf
		
	Next nI
ElseIf Len( aRet ) > 0
	AAdd(aRet,{"1",aHrs[1][1],aHrs[2][1] } )
EndIf

Return aRet

//-------------------------------------------------------------------
/*/{Protheus.doc} PONI090Snd
Rotina para integraÁ„o por EAI 

@since 26/06/2013
@version P11
@param	aTempDados - valor para realizar o tratamento, inserindo pipes 
	na string de retorno
@return	cTempKey   - chave interna do de/para dividida em pipes

/*/
//-------------------------------------------------------------------
Function PONI090Snd( aTempDados )

Local nZ       := 1
Local cTempKey := ""

Default aTempDados := {}

For nZ := 1 To Len( aTempDados )

	cTempKey += RTrim( aTempDados[nZ] ) + "|"

Next nZ

cTempKey := SubStr( cTempKey, 1, Len( cTempKey ) -1 )
	
Return cTempKey

//-------------------------------------------------------------------
/*/{Protheus.doc} PONI090Rcv
Rotina para realizar a separaÁ„o da chave da tabela do conte˙do gravado 
no de/para, removendo os pipes e identificando o tamanho dos campos 

@since 26/06/2013
@version P11
@param	cTmpKeyInt - valor para realizar o tratamento, array para inserir pipes e string
	para remover pipes
@param aCposChave - array contendo os campos para ajuste de tamanho
@return	cTempKey   - chave interna do de/para dividida em pipes

/*/
//-------------------------------------------------------------------
Function PONI090Rcv( cTmpKeyInt, aCposChave )

Local cTempKey  := ""
Local xAuxKey   := {}
Local nZ        := 0

Default cTmpKeyInt := ""
Default aCposChave := { "P2_FILIAL", "P2_MAT", "P2_CC", "P2_TURNO", "P2_DATA" }

xAuxKey := StrToKarr( StrTran( cTmpKeyInt, "|", "| " ), "|" )

If ( Len( xAuxKey ) - 1) == Len( aCposChave )
	
	For nZ := 2 To Len( xAuxKey )
		
		xAuxKey[nZ] := SubStr( xAuxKey[nZ], 2)
		cTempKey += PadR( xAuxKey[nZ], TamSX3( aCposChave[nZ-1] )[1] )
	
	Next nZ
EndIf

xAuxKey := aSize( xAuxKey, 0 )

Return cTempKey 

//-------------------------------------------------------------------
/*/{Protheus.doc} TrataResp
	Trata a resposta da aplicaÁ„o destino para gravaÁ„o do De/Para 

@since 26/06/2013
@version P11
@param 	lProcOk 	Status do processamento (referÍncia)
@param 	cMsgRet 	Mensagem de retorno do processamento (referÍncia)
@param 	cXml    	Mensagem da response para tratamento

/*/
//-------------------------------------------------------------------
Static Function TrataResp( lProcOk, cMsgRet, cXml )

Local oObjXml 	:= Nil
Local cXmlErro 	:= ""
Local cXmlWarn 	:= ""
Local nZ 		:= 0
Local cMarca 	:= ""

oObjXml := XmlParser( cXml, "_", @cXmlErro, @cXmlWarn )

If oObjXml <> Nil .And. Empty( cXmlErro ) .And. Empty( cXmlWarn )

	cMarca := oObjXml:_TotvsMessage:_MessageInformation:_Product:_Name:Text

	If XmlChildEx( oObjXml:_TotvsMessage:_ResponseMessage:_ProcessingInformation, "_STATUS" ) <> Nil .And. ;
		Upper( oObjXml:_TotvsMessage:_ResponseMessage:_ProcessingInformation:_Status:Text ) == "OK"
	
		oObjXml := XmlChildEx( oObjXml:_TotvsMessage:_ResponseMessage:_ReturnContent, "_LISTOFINTERNALID" )  
		
		If oObjXml <> Nil
			
			If XmlChildEx( oObjXml, "_INTERNALID" ) <> Nil
				
				If Valtype( oObjXml:_InternalId ) <> "A"
					XmlNode2Arr( oObjXml:_InternalId, "_InternalId" )
				EndIf
				
				For nZ := 1 To Len( oObjXml:_InternalId )
				
					If XmlChildEx( oObjXml:_InternalId[nZ], "_NAME" ) <> Nil .And. ;
						Upper( oObjXml:_InternalId[nZ]:_Name:Text ) == "EMPLOYEEEXCEPTION" .And. ;
						XmlChildEx( oObjXml:_InternalId[nZ], "_ORIGIN" ) <> Nil .And. ;
						XmlChildEx( oObjXml:_InternalId[nZ], "_DESTINATION" ) <> Nil
					
						CFGA070Mnt( cMarca, 'SP2', 'P2_MAT', ;
							oObjXml:_InternalId[nZ]:_Destination:Text, ;
							oObjXml:_InternalId[nZ]:_Origin:Text )
						
						Exit
					EndIf
				Next nZ
			
			Else
			
				lProcOk := .F.
				cMsgRet := STR0011 // "N„o enviado conte˙do de retorno para cadastro de de-para"
			
			EndIf
			
		Else
			
			lProcOk := .F.
			cMsgRet := STR0012 // "N„o È possÌvel gravar de-para por erro na leitura do retorno"
		
		EndIf
		
	Else
	
		lProcOk 	:= .F.
		cMsgRet 	:= STR0013 + "|" // "Erro no processamento pela outra aplicaÁ„o"
		
		If XmlChildEx( oObjXml:_TotvsMessage:_ResponseMessage:_ProcessingInformation, "_LISTOFMESSAGES" ) <> Nil
			oObjXml := oObjXml:_TotvsMessage:_ResponseMessage:_ProcessingInformation:_ListOfMessages
			
			If XmlChildEx( oObjXml, "_MESSAGE" ) <> Nil 
				
				If Valtype( oObjXml:_Message ) <> "A"
					XmlNode2Arr( oObjXml:_Message, "_Message" )
				EndIf
				
				For nZ := 1 To Len( oObjXml:_Message )
				
					cMsgRet += Alltrim( oObjXml:_Message[nZ]:Text ) + "|"
				
				Next nZ
			Else
				
				lProcOk := .F.
				cMsgRet += STR0014 // "Erro no processamento, mas sem detalhes do erro pela outra aplicaÁ„o"
				
			EndIf
			
		EndIf
		
	EndIf

Else
	lProcOk 	:= .F.
	cMsgRet 	:= STR0015 + "|" + cXmlErro + "|" + cXmlWarn // "Falha na leitura da resposta, de-para n„o ser· gravado"
EndIf

If !lProcOk
	aResult[1] := .F.
	aResult[2] := cMsgRet
Else
	aResult[1] := .T.
	aResult[2] := ""
EndIf

oObjXml := Nil
DelClassIntF()

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} PI090GetStatus
	Retorna a vari·vel com o status do processamento 

@since  	13/08/2013
@version 	P11
@return 	aValor, ARRAY '{ Status, Mensagem }'
/*/
//-------------------------------------------------------------------
Function PI090GetStatus()

Return aResult