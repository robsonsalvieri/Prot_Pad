#INCLUDE "GPEA650.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "PONCALEN.CH"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuncao        ณ Gpea650 ณ Autor    ณ Pedro Eloy          ณ Data ณ 16/01/06   ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao     ณ Cadastro Contrib. Patronal                                   ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso           ณ Generico                                                     ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ		      	   ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL     		ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณProgramador   ณData    ณ FNC      ณ Motivo da alteracao 				        ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณRaquel Hager  ณ12/04/13ณM12RH01   ณRQ003 - Unificacao da Folha de Pagamento. ณฑฑ
ฑฑณRaquel Hager  ณ19/06/13ณM12RH01   ณInclusao da funcao gp650TudOk e validacao ณฑฑ
ฑฑณ				 ณ        |RQ003     ณde linha deletada na funcao gp650LinOk.   ณฑฑ
ฑฑณRaquel Hager  ณ25/06/13ณM12RH01   ณCorrecao no gp650TudOk para utilizacao da ณฑฑ
ฑฑณ				 ณ        |RQ003     ณfuncao GdValidGet.					    ณฑฑ
ฑฑณHenrique Vita ณ19/12/14ณTRGW99    ณR้plica do chamado TRECZG referente ao    ณฑฑ
ฑฑณ				 ณ        |          ณCadastro de Contribui็ใo Patronal 	    ณฑฑ
ฑฑณAllyson M     ณ16/06/16ณTVIT55    ณAjuste p/ validar permissao de acesso		ณฑฑ
ฑฑณJ๔natas A.    ณ12/04/17ณDRHPAG-241ณSubstitui็ใo das fun็๕es de cria็ใo de    ณฑฑ
ฑฑณ              ณ        ณ	         ณtabelas temporแrias pela classe           ณฑฑ
ฑฑณ              ณ        ณ	         ณFWTemporaryTable().                       ณฑฑ
ฑฑณRafael Reis   ณ29/08/17ณDRHPAG5326ณTratamento para cria็ใo de tabela         ณฑฑ
ฑฑณ              ณ        ณ	         ณtemporแria quando dicionแrio nใo existe   ณฑฑ
ฑฑณ              ณ        ณ	         ณno banco.                                 ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿ */
Function Gpea650()
Local aFixe 	:= {}
Local nTamEmp   := 0
Local aIndex    := { { "TR_CODEMP+TR_FILIAL" } }
Local aSeek     := { { STR0002,{{"" ,"C",nTamEmp,0,STR0002}} } }	// 'Tipo' ## 'Descri็ใo'
Local aCampos	:= {}
Local cAliasTMP	:= GetNextAlias()
Local cFilAcess	:= ""
Local cFilQry	:= ""
Local cCodFil	:= ""
Local nI		:= 0
Local nSizeFil		:= FWSizeFilial()
Local nRegSm0	:= 0

Private oTmpTbl

DbSelectArea("SM0")
//Verifica se o Arquivo Esta Vazio
If SELECT ("SM0") == 0
	Return
EndIf

// Definicao dos campo da mBrowse
nTamEmp := Iif(Len(FWCODEMP("SM0")) = 0, Len(cEmpant), Len(FWCODEMP("SM0")))
aFixe := {	{STR0002	,{|| TRB->TR_CODEMP}, "C" , "@!" , 0 , nTamEmp , 0 , , , , , , , },;     	// "Empresa"
			{STR0003	,{|| TRB->TR_FILIAL}, "C" , "@!" , 0 , nSizeFil , 0 , , , , , , , , },;	// "Filial"
			{STR0004	,{|| TRB->TR_NOME}  , "C" , "@!" , 0 , Len(M0_FILIAL) , 0 , , , , , , , , } } 	            // "Descricao"

// Criado um arquivo temporario para utilizar como espelho para a mBrowse
aCampos := {{ "TR_CODEMP" 	,"C",nTamEmp, 0 	} ,;	    // Codigo da Empresa
       		{ "TR_FILIAL"  	,"C",nSizeFil,0 	} ,;	// Codigo da Filial
       		{ "TR_NOME"		,"C",Len(M0_FILIAL) ,0 }}	   				// Descricao da Filial


	oTmpTbl := FWTemporaryTable():New("TRB")
	oTmpTbl:SetFields(aCampos)
	oTmpTbl:AddIndex("EMPFIL", {"TR_CODEMP", "TR_FILIAL"})
	oTmpTbl:Create()

	If MPDicInDB()

		// Monta variavel com filiais que o usuแrio tem acesso
		cFilAcess := fValidFil()
		For nI := 1 To Len(cFilAcess) Step (nSizeFil + 1)
			If !Empty(cCodFil := Substr(cFilAcess, nI, nSizeFil))

				cFilQry += "'" + cCodFil + "'"

				If (nI + nSizeFil + 1) < Len(cFilAcess)
					cFilQry += ","
				EndIf
			ElseIf nI == (Len(cFilAcess)-(nSizeFil + 1)) .And. !Empty(cFilQry)
				cFilQry := Substr(cFilQry, 1, Len(cFilQry) - 1)
			EndIf
		Next

		// Modifica variaveis para a Query
		cFilQry := "%" + cFilQry + "%"

		BeginSql alias cAliasTMP
			SELECT SM0.M0_CODIGO, SM0.M0_CODFIL, SM0.M0_FILIAL
			FROM %table:SM0% SM0
			WHERE      SM0.M0_CODIGO = %exp:cEmpant%
				   AND SM0.M0_CODFIL IN ( %exp:Upper( cFilQry )% )
				   AND SM0.%notDel%
			ORDER BY SM0.M0_CODIGO, SM0.M0_CODFIL, SM0.M0_FILIAL
		EndSql
		While !(cAliasTMP)->(Eof())
				RecLock("TRB",.T.)
		        	TRB->TR_CODEMP	:=	(cAliasTMP)->M0_CODIGO
		        	TRB->TR_FILIAL	:=	(cAliasTMP)->M0_CODFIL
		        	TRB->TR_NOME	:=	(cAliasTMP)->M0_FILIAL
				TRB->(MsUnLock())
	  		(cAliasTMP)->(dbSkip())
		EndDo
		(cAliasTMP)->(dbCloseArea())

	Else

		// Gravar no temporario somente os registros do SigaMat.Emp
		// com a mesma empresa, setada ao escolher.
		nRegSm0 := SM0->(RECNO())
		dbSelectArea("SM0")
		dbGotop()
		While !Eof()
			If SM0->M0_CODIGO == cEmpant .And. FWGETCODFILIAL $ fValidFil()
				RecLock("TRB",.T.)
		        	TRB->TR_CODEMP	:=	SM0->M0_CODIGO
		        	TRB->TR_FILIAL	:=	FWGETCODFILIAL
		        	TRB->TR_NOME	:=	SM0->M0_FILIAL
				MsUnLock()
			EndIf
			dbSelectArea("SM0")
	  		SM0->(dbSkip())
		EndDo
		dbGoTo(nRegSm0)

	Endif

Private aRotina := MenuDef() // Ajuste para versao 9.12 - chamada da funcao MenuDef() que contem aRotina														}

dbSelectArea("TRB")

oBrowse := FWMBrowse():New()
oBrowse:SetAlias('TRB')
oBrowse:SetFields(aFixe)
oBrowse:SetDescription(OemToAnsi(STR0001))
oBrowse:SetQueryIndex(aIndex)
oBrowse:SetSeek(.T.,aSeek)
oBrowse:DisableDetails()
oBrowse:Activate()

// Fechando o arquivo temporario, criado como espelho
dbSelectArea("TRB")
dbCloseArea()
If oTmpTbl <> Nil
	oTmpTbl:Delete()
	oTmpTbl := Nil
Endif

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออัอออออออัออออออออออออออออออออัออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณ gp650Atu ณAutor  ณ Equipe RH          ณData  ณ             บฑฑ
ฑฑฬออออออออออุออออออออออฯอออออออฯออออออออออออออออออออฯออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Vis.,Inc.,Alt. e Del. "Cadastro de Contribuicao Patronal". บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบSintaxe   ณ gp650Atu(ExpC1,ExpN1,ExpN2)								  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ ExpC1 = Alias do arquivo									  บฑฑ
ฑฑบ          ณ ExpN1 = Numero do registro   							  บฑฑ
ฑฑบ          ณ ExpN2 = Numero da opcao selecionada						  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ GPEA650                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function gp650Atu(RCTcAlias,nReg,nOpcx)
Local aAdvSize		:= {}				// Variavel para controle de coordenadas da janela.
Local aInfoAdvSize	:= {}				// Variavel para controle de coordenadas da janela.
Local aObjCoords	:= {}				// Variavel para controle de coordenadas da janela.
Local aObjSize		:= {}				// Variavel para controle de coordenadas da janela.
Local oDlg								// Variavel do tipo objetos
Local oFont								// Variavel do tipo objetos
Local oGroup							// Variavel do tipo objetos
Local cKey			:= ''				// variavel com os valores das chaves. (variavel para getdados da tabela RCT)
Local aRCTNotFields	:= {"RCT_FILIAL"}  // vetor com os campos que nao seria visualizados. (variavel para getdados da tabela RCT)
Local aRCTVirtGd	:= {}				// vetor com os campos virtuais. (variavel para getdados da tabela RCT)
Local aRCTVisualGd	:= {} 				// vetor com os campos visuais. (variavel para getdados da tabela RCT)
Local aRCTCols		:= {}				// vetor com as colunas da GetDados RCT. (variavel para getdados da tabela RCT)
Local bSet15		:= { || NIL }		// Bloco com as validacoes do botao OK. (variavel da janela)
Local bSet24		:= { || NIL }		// Bloco com as validacoes do botao Cancelar. (variavel da janela)
Local nPosTipoAfa 	:= 0
Local nPosDesc		:= 0
Local nLinha		:= 0
Local cQuery		:= ""
Local bSeekWhile	:= {|| RCT->RCT_FILIAL  }
Local cSVFil 		:= cFilAnt

Private cFil	 	:= TRB->TR_FILIAL
Private aRCTColsRec	:= {}				// Vetor que contem os Recnos da tabela. (variavel para getdados da tabela RCT)
Private aHeader		:= {}				// Vetor com o cabecalho da GetDados RCT. (variavel para getdados da tabela RCT)
Private nRCTUsado	:= 0.00 			// Variavel que retorna a quantidade de campos da tabela. (variavel para getdados da tabela RCT)
Private oGet							// Variavel do tipo objetos

		RCTcAlias	:= "RCT"
		cFilAnt		:= TRB->TR_FILIAL

		// Nao habilita Insert/Altera/Deletar se a opcao do aRotina for igual de 2(Visualizacao) e 5(Exclusao)
		_SetOwnerPrvt( "nOpcNewGd", IF( nOpcX == 2 .or. nOpcX == 5 , 0 , GD_INSERT + GD_UPDATE + GD_DELETE))

		// Monta as Dimensoes dos Objetos
		aAdvSize		:= MsAdvSize()
		aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 5 , 5 }
		aAdd( aObjCoords , { 000 , 020 , .T. , .F. } )
		aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
		aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )

		nOpca := 0

		cKey	 := ( cFil )
		aRCTCols := RCT->( GdMontaCols(		@aHeader		,;  //01 -> Array com os Campos do Cabecalho da GetDados
											@nRCTUsado		,;	//02 -> Numero de Campos em Uso
											@aRCTVirtGd		,;	//03 -> [@]Array com os Campos Virtuais
											@aRCTVisualGd	,;	//04 -> [@]Array com os Campos Visuais
											"RCT"			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
											aRCTNotFields	,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
											@aRCTColsRec	,;	//07 -> [@]Array unidimensional contendo os Recnos
											"SM0"			,;	//08 -> Alias do Arquivo Pai
											cKey			,;	//09 -> Chave para o Posicionamento no Alias Filho
											bSeekWhile		,;	//10 -> Bloco para condicao de Loop While
											Nil				,;	//11 -> Bloco para Skip no Loop While
											Nil				,;	//12 -> Se Havera o Elemento de Delecao no aCols
											Nil				,;	//13 -> Se cria variaveis Publicas
											Nil				,;	//14 -> Se Sera considerado o Inicializador Padrao
											Nil				,;	//15 -> Lado para o inicializador padrao
											Nil				,;	//16 -> Opcional, Carregar Todos os Campos
											Nil 			,;	//17 -> Opcional, Nao Carregar os Campos Virtuais
											cQuery			,;	//18 -> Opcional, Utilizacao de Query para Selecao de Dados
											.F.				,;	//19 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
											.F.				,;	//20 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
											.F.				,;	//21 -> Carregar Coluna Fantasma e/ou BitMap ( Logico ou Array )
											Nil				,;	//22 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
											Nil				,;	//23 -> Verifica se Deve Checar se o campo eh usado
											Nil				,;	//24 -> Verifica se Deve Checar o nivel do usuario
											Nil				,;	//25 -> Verifica se Deve Carregar o Elemento Vazio no aCols
											Nil				,;	//26 -> [@]Array que contera as chaves conforme recnos
											Nil				,;	//27 -> [@]Se devera efetuar o Lock dos Registros
											Nil				,;	//28 -> [@]Se devera obter a Exclusividade nas chaves dos registros
											Nil				,;	//29 -> Numero maximo de Locks a ser efetuado
											.F.			 	;	//30 -> Utiliza Numeracao na GhostCol
		 						  ))

		// Validacao da existencia de registro no Incluir
		If ( ( nOpcX == 3 ) .And. ( Len( aRCTCols ) > 0 ) .And. !Empty(aRCTCols[1][1]) ) // Quando for Inclusao
			Alert(STR0010)	// "Ja possui registro cadastrado."
			cFilAnt := cSVFil
			Return
		ElseIf ( nOpcX # 3 ) .And. Empty(aRCTCols[1][1]) //Quando Nao for Inclusao
			Alert(STR0011)	// "Nao possui registro cadastrado."
			cFilAnt := cSVFil
			Return
		EndIf

		DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
		DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0012) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL	// "Contribuicao Sindical"

		// Atribuindo o nome ao campo Visual (descricao do sindicato)
		nPosTipoAfa := GdFieldPos( "RCT_SIND"	, aHeader )
		nPosDesc	:= GdFieldPos( "RCT_DESC"	, aHeader )
		For nLinha := 1 to Len(aRCTCols)
			aRCTCols[nLinha, nPosDesc] := fDesc("RCE", aRCTCols[nLinha, nPosTipoAfa], "RCE_DESCRI")
		Next nLinha

		@ aObjSize[1,1] , aObjSize[1,2] GROUP oGroup TO aObjSize[1,3] ,aObjSize[1,4]*0.10	LABEL OemToAnsi(STR0002) OF oDlg PIXEL		// "Empresa"
		oGroup:oFont:= oFont
		@ aObjSize[1,1] , aObjSize[1,4]*0.11 GROUP oGroup TO aObjSize[1,3] ,aObjSize[1,4]*0.8	LABEL OemToAnsi(STR0013) OF oDlg PIXEL	//"Nome da Empresa"
		oGroup:oFont:= oFont
		@ aObjSize[1,1] , aObjSize[1,4]*0.81 GROUP oGroup TO aObjSize[1,3] ,aObjSize[1,4] LABEL OemToAnsi(STR0003) OF oDlg PIXEL		// "Filial"
		oGroup:oFont:= oFont
		@ aObjSize[1,1]+10 , aObjSize[1,2]+10      SAY TRB->TR_CODEMP			SIZE 050,10 OF oDlg PIXEL FONT oFont
		@ aObjSize[1,1]+10 , aObjSize[1,4]*0.10+10 SAY TRB->TR_NOME 			SIZE 146,10 OF oDlg PIXEL FONT oFont
		@ aObjSize[1,1]+10 , aObjSize[1,4]*0.80+10 SAY TRB->TR_FILIAL 			SIZE 050,10 OF oDlg PIXEL FONT oFont

		oGet := MsNewGetDados():New(aObjSize[2,1],;	// nTop
									 aObjSize[2,2],;   	// nLelft
									 aObjSize[2,3],;	// nBottom
		                             aObjSize[2,4],;	// nRright
									 nOpcNewGd,;		// Controle do que podera ser realizado na GetDado - nstyle
									 "gp650LinOk",;		// Funcao para validar a edicao da linha - ulinhaOK
									 "gp650TudOk",;		// Funcao para validar todas os registros da GetDados - uTudoOK
	  								 NIL,;				// cIniCPOS
									 NIL,;				// aAlter
									 0,; 				// nfreeze
									 99999,;  			// nMax
									 NIL,;		 		// cFieldOK
									 NIL,;				// usuperdel
									 Nil,;				// udelOK
									 @oDlg,;        	// Objeto de dialogo - oWnd
									 @aHeader,;			// Vetor com Colunas - AparHeader
									 @aRCTCols;			// Vetor com Header - AparCols
									)

			bSet15	:= {|| nOpcA:= (IF(nOpcx=2.Or.nOpcx=3.Or.nOpcx=4,1,2)),IF(oGet:TudoOk(),oDlg:End(),nOpca:=0)} 	// Botao OK
			bSet24	:= {|| oDlg:End()} 																			    	// Botao Cancela
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 )

	    // Visualizacao, Inclusao e Alteracao
	    If nOpcA == 1
		        Begin Transaction
		        	// Gravacao
		        	gp650Grava(RCTcAlias, nOpcx)
		        	// Processa Gatilhos
		            EvalTrigger()
		        End Transaction
		ElseIf nOpcA == 2
		    	// Se for Exclusao
		        Begin Transaction
		            gp650Dele()
		        End Transaction
		EndIf

	dbSelectArea("TRB")
	cFilAnt := cSVFil

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออัอออออออัออออออออออออออออออัออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณ gp650Grava ณ Autor ณ Equipe RH        ณData  ณ             บฑฑ
ฑฑฬออออออออออุออออออออออออฯอออออออฯออออออออออออออออออฯออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Grava no arquivo.										  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ GPEA650                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function gp650Grava(RCTcAlias, nOpc)
Local aArea		:= GetArea()
Local aRCTCols  := (oGet:aCols)
Local nAcols	:= Len( aRCTCols    )
Local nAcolsRec := Len( aRCTColsRec )
Local nMaxArray	:= Len( aHeader  )
Local nCol 		:= 0
Local nX		:= 0

		dbSelectArea(RCTcAlias)
		For nX := 1 to nAcols
		    If nX <= nAcolsRec
		        dbGoto(aRCTColsRec[nX])
		        RecLock(RCTcAlias,.F.)
		        // Verifica se esta deletado
		        If aRCTCols[nX,nRCTUsado+1]  # .F.
		            (RCTcAlias)->( dbDelete() )
		        Else
		        	For nCol:= 1 to nMaxArray
				        cCampo    	:= Trim(aHeader[nCol][2])
			    	    xConteudo 	:= aRCTCols[nX,nCol]
			        	&cCampo 	:= xConteudo
	                Next
		        EndIf
	       	    ( RCTcAlias )->( MsUnlock() )
		        Loop
		    EndIf

	    	// Verifica se Nao esta Deletado no aCols
	    	// e faz a gravacao dos campos digitados na GetDados
	    	// rotina utilizada na inclusใo de registro.
	       	If aRCTCols[nX,nRCTUsado+1]  = .F.
	          		RecLock(RCTcAlias,.T.)
	   				RCT->RCT_FILIAL	:=	TRB->TR_FILIAL
		        	For nCol:= 1 to nMaxArray
				        cCampo    	:= Trim(aHeader[nCol][2])
			    	    xConteudo 	:= aRCTCols[nX,nCol]
			        	&cCampo 	:= xConteudo
	                Next
		    Else
		        Loop
		    EndIf
		    ( RCTcAlias )->( MsUnlock() )
		Next nX

		RestArea(aArea)

Return( Nil )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออัอออออออัอออออออออออออออออออัออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณ gp650Dele ณAutor  ณ Equipe RH         ณ Data ณ             บฑฑ
ฑฑฬออออออออออุอออออออออออฯอออออออฯอออออออออออออออออออฯออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Deleta os Registros. 									  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ GPEA650                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Static Function gp650Dele()
Local aArea	:= GetArea()

	// Tem a finalidade de apagar os registro de todas as filiais
	// setada, ja que esta sendo utilizado o SM0 e a mesma empresa.
    dbSelectArea("RCT")
    dbSetOrder(1)
    If dbSeek(TRB->TR_FILIAL,.f.)
		While !Eof() .And. TRB->TR_FILIAL == RCT->RCT_FILIAL
	    	RecLock("RCT",.F.)
			    dbDelete( )
	    	MsUnlock()
	     	RCT->(dbSkip())
	    EndDo
	EndIf

	RestArea(aArea)

Return


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออัอออออออัออออออออออออออออออัออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณ gp650LinOk ณAutor  ณ Equipe RH        ณData  ณ 16/01/06    บฑฑ
ฑฑฬออออออออออุออออออออออออฯอออออออฯออออออออออออออออออฯออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Critica linha digitada									  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ GPEA650                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function gp650LinOk()
Local aArea		:= GetArea()
Local lRet		:= .T.
Local nPos		:= 0
Local aRCTCols	:= (oGet:aCols)
Local nAcols	:= Len( aRCTCols )
Local cSindPos	:= GdFieldPos( "RCT_SIND"	, aHeader )
Local cTipoPos	:= GdFieldPos( "RCT_TPCONT"	, aHeader )
Local nPosMes	:= GdFieldPos( "RCT_MES"	, aHeader )
Local nPosAno	:= GdFieldPos( "RCT_ANO"	, aHeader )
Local cSind		:= aRCTCols[oGet:nAt][cSindPos]
Local cTipo		:= aRCTCols[oGet:nAt][cTipoPos]
Local cMesNat	:= aRCTCols[oGet:nAt][nPosMes]
Local cAnoNat	:= aRCTCols[oGet:nAt][nPosAno]
Local nLinha	:= oGet:nAt

		// Caso o registro no aCols esteja deletado e/ou vazio
	   	If !aRCTCols[nLinha,nRCTUsado+1]
			If lRet .And. Empty(aRCTCols[nLinha,nPosAno]) .Or. Empty(aRCTCols[nLinha,nPosMes])
	       		Alert(STR0014)		// "Registro em branco"
	       		lRet := .F.
			EndIf
	    EndIf

		// Verifica o periodo do mes valido
		If lRet .And. !aRCTCols[nLinha,nRCTUsado+1] .And. nPosMes > 0
		    If ( (Val(Alltrim(aRCTCols[nLinha,nPosMes]))< Val("1") .Or. Val(Alltrim(aRCTCols[nLinha,nPosMes]) )> Val("12") ) )
		     	Alert(STR0015)		// "Mes no periodo incorreto."
		        lRet := .F.
		    EndIf
		EndIf

		// Verifica a duplicidade na aCols
	  	If lRet .And. Len(aRCTCols) > 1
			nPos := aScan(aRCTCols,{|x| x[1]==cSind .And. x[3]==cTipo .And. x[4]==cMesNat .And. x[5]==cAnoNat .And. !x[nRCTUsado+1] })
		    If nPos > 0 .And. nPos # nLinha
		     	Alert(STR0016) 	// "Analise este registro, duplicidade"
		       	lRet := .F.
		    EndIf
		EndIf

	RestArea(aArea)

Return( lRet )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออัอออออออัออออออออออออออออออัออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณ gp650TudOk ณAutor  ณ Equipe RH        ณData  ณ 19/06/13    บฑฑ
ฑฑฬออออออออออุออออออออออออฯอออออออฯออออออออออออออออออฯออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Critica todas as linhas.									  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ GPEA650                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function gp650TudOk()
Local aArea			:= GetArea()
Local aRCTCols		:= (oGet:aCols)
Local aCposKey      := {}
Local aCposObrig	:= {}
Local lRet 			:= .T.

	// Campos de preenchimento obrigatorio
	aCposObrig	:= {"RCT_SIND", "RCT_TPCONT", "RCT_MES", "RCT_ANO"}
	// Campos que compoem a chave
	aCposKey	:= {"RCT_SIND", "RCT_TPCONT", "RCT_MES", "RCT_ANO"}

	If !(GdValidGet(aCposKey, 4, aCposObrig,.T.,aHeader,aRCTCols))
 		lRet := .F.
	EndIf

	RestArea(aArea)

Return( lRet )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออัอออออออัออออออออออออออออออออัออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณ Gp650Top ณAutor  ณ Equipe RH          ณData  ณ 01/18/06    บฑฑ
ฑฑฬออออออออออุออออออออออฯอออออออฯออออออออออออออออออออฯออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Rotina utilizada pela a mBrowse, quando o foco e utilizado บฑฑ
ฑฑบ          ณ a tabela SM0, validado quando retorna para mBrowse.        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ GPEA650                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function Gp650Top()
Return "    "

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออัอออออออัอออออออออออออออออออัออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณ Gp650Bot  ณ Autor ณ Equipe RH         ณData  ณ 01/18/06    บฑฑ
ฑฑฬออออออออออุอออออออออออฯอออออออฯอออออออออออออออออออฯออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Rotina utilizada pela a mBrowse, quando o foco e utilizado บฑฑ
ฑฑบ          ณ a tabela SM0, validado quando retorna para mBrowse.        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ GPEA650                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function Gp650Bot()

Return "ZZZZ"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออัอออออออัออออออออออออออออออออัออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณ Gp650Pes ณAutor  ณ Pedro Eloy         ณData  ณ 01/18/06    บฑฑ
ฑฑฬออออออออออุออออออออออฯอออออออฯออออออออออออออออออออฯออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Realiza pesquisa. 				  					      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ GPEA650                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function Gp650Pes()
Local cCampo,oDlg,nOpcaPes:=0
Local cOrd,oCbx,nOpt1
Local aOrd			:= {}
Local aAdvSize		:= {}				// Variavel para controle de coordenadas da janela.
Local aInfoAdvSize	:= {}				// Variavel para controle de coordenadas da janela.
Local aObjCoords	:= {}				// Variavel para controle de coordenadas da janela.
Local aObjSize		:= {}				// Variavel para controle de coordenadas da janela.
Local aAdv1Size		:= {}
Local aInfo1AdvSize	:= {}
Local aObj1Size		:= {}
Local aObj1Coords 	:= {}
Local bSet15		:= { || Nil }		// Bloco com as validacoes do botao OK. (variavel da janela)
Local bSet24		:= { || Nil }		// Bloco com as validacoes do botao Cancelar. (variavel da janela)

	Aadd(aOrd,"Empresa + Codigo Filial")

	aAdvSize	:= MsAdvSize( ,.T.,370)
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 10 , 10 }
	aAdd( aObjCoords , { 000 , 000 , .T. , .T.}  )
	aObjSize	:= MsObjSize( aInfoAdvSize , aObjCoords )
	aAdv1Size    := aClone(aObjSize[1])

	aInfo1AdvSize    := { aAdv1Size[2] , aAdv1Size[1] , aAdv1Size[4] , aAdv1Size[3] , 5 , 5 }
	aAdd( aObj1Coords , { 000 , 020 , .T. , .F. } )
	aAdd( aObj1Coords , { 000 , 020 , .T. , .F. } )
	aObj1Size := MsObjSize( aInfo1AdvSize , aObj1Coords )

	// Processa Choices dos Campos do Banco de Dados
	cOrd   := "1"
	cCampo := cEmpAnt + Space(FwSizeFilial())

	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0017) From aAdvSize[7],0 TO aAdvSize[6]*0.55,aAdvSize[5]*0.70 OF oMainWnd PIXEL //"Pesquisa"
		@ aObj1Size[1,1] , aObj1Size[1,2] COMBOBOX oCBX VAR cOrd ITEMS aOrd  SIZE 165,44  ON CHANGE (nOpt1:=oCbx:nAt)  OF oDlg FONT oDlg:oFont PIXEL//0.6,1.3
		@ aObj1Size[2,1] , aObj1Size[2,2] MSGET cCampo SIZE 165,10 PIXEL//2.1,1.3

		bSet15	:= {|| nOpcaPes := 1,oDlg:End()} 	// Botao OK
		bSet24	:= {|| oDlg:End()} 					// Botao Cancela

	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 ) CENTERED

	If nOpcaPes == 0
		Return
	Else
		dbSelectArea("TRB")
		If !dbSeek(AllTrim(cCampo),.F.)
			Alert(STR0018)  //"Filial nao encontrada"
			dbGotop()
		EndIf
	EndIf

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออัอออออออัออออออออออออออออออออัออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณ MenuDef  ณ Autor ณ Luiz Gustavo       ณData  ณ 12/01/07    บฑฑ
ฑฑฬออออออออออุออออออออออฯอออออออฯออออออออออออออออออออฯออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Isola opcoes de menu para que as opcoes da rotina possam   บฑฑ
ฑฑบ          ณ ser lidas pelas bibliotecas Framework da Versao 9.12 .     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ GPEA650                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Static Function MenuDef()
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Define array contendo as Rotinas a executar do programa      ณ
	//ณ ----------- Elementos contidos por dimensao ------------     ณ
	//ณ 1. Nome a aparecer no cabecalho                              ณ
	//ณ 2. Nome da Rotina associada                                  ณ
	//ณ 3. Usado pela rotina                                         ณ
	//ณ 4. Tipo de Transao a ser efetuada                          ณ
	//ณ    1 - Pesquisa e Posiciona em um Banco de Dados             ณ
	//ณ    2 - Simplesmente Mostra os Campos                         ณ
	//ณ    3 - Inclui registros no Bancos de Dados                   ณ
	//ณ    4 - Altera o registro corrente                            ณ
	//ณ    5 - Remove o registro corrente do Banco de Dados          ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	Local aRotina	:=	{ 	{ OemToAnsi(STR0017)  , "Gp650Pes"  , 0 , 1, ,.F.},;	// "Pesquisa"
							{ OemToAnsi(STR0006)  , "gp650Atu"	, 0 , 2},;			// "Visualizar"
							{ OemToAnsi(STR0007)  , "gp650Atu"	, 0 , 6},;			// "Incluir"
							{ OemToAnsi(STR0008)  , "gp650Atu"	, 0 , 4},; 			// "Alterar"
							{ OemToAnsi(STR0009)  , "gp650Atu"	, 0 , 5}}  			// "Excluir"


Return aRotina
