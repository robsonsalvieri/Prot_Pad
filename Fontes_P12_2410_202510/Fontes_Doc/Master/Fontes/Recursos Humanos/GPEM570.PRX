#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPEM570.CH"
#INCLUDE "report.ch"

#DEFINE cPERG 'GPM570'

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPEM570  ³ Autor ³Pedro Eloy                          ³ Data ³ 10/08/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio da DIRF                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPEM570()                                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ Requisito         ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Bruno Nunes ³20/01/14³ M12RH01_001943_01 ³ Unificacao da DIRF da versao 11 com      ³±±
±±³            ³        ³                   ³ a fase 4                                 ³±±
±±³Christiane V³29/04/14³TPJZJ8			    ³ Réplicas P11 para P12                    ³±±
±±³Luis Artuso ³19/02/15³TRRDAI      		³Ajuste p/ exibicao do ano calendario, in- ³±±
±±³            ³        ³             		³formado no grupo de perguntas (MV_PAR11). ³±±
±±³Mariana M.  ³22/01/16³            		³Ajuste na impressão das informções comple-³±±
±±³            ³        ³             		³mentares referente ao plano de saude      ³±±
±±³Claudinei S.³08/11/16³TWLEW6             ³Ajuste em M570Imp(),controle de acesso    ³±±
±±³            ³        ³                   ³não estava funcionando para a tabela SRL  ³±±
±±³Marcos Cout.³24/01/17³MRH-5467           ³Realizar a compatibilização do fonte para ³±±
±±³            ³        ³                   ³a versão da DIRF 2017                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPEM570()
Local oReport		:= Nil
Local aOfusca		:= If(FindFunction('ChkOfusca'), ChkOfusca(), { .T., .F. }) //[1]Acesso; [2]Ofusca; [3]Mensagem
Local aFldRel		:= {"RL_CPFCGC","RL_BENEFIC","RQ_NOME","RCS_CPFCGC"}
Local lBlqAcesso	:= aOfusca[2] .And. !Empty( FwProtectedDataUtil():UsrNoAccessFieldsInList( aFldRel ) )
Private cAlias		:= "SRL"

	If lBlqAcesso	//Tratamento de acesso a dados pessoais
		Help(" ",1,aOfusca[3,1],,aOfusca[3,2],1,0)	//"Dados Protegidos- Acesso Restrito: Este usuário não possui permissão de acesso aos dados dessa rotina. Saiba mais em {link documentação centralizadora}"
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Variaveis utilizadas para parametros                          ³
		//³ mv_par01        //  Filial  De                                ³
		//³ mv_par02        //  Filial  Ate                               ³
		//³ mv_par03        //  C.G.C. De                                 ³
		//³ mv_par04        //  C.G.C. Ate                                ³
		//³ mv_par05        //  Matricula De                              ³
		//³ mv_par06        //  Matricula Ate                             ³
		//³ mv_par07        //  Apenas com I.R.                           ³
		//³ mv_par08        //  Nome De                                   ³
		//³ mv_par09        //  Nome Ate                                  ³
		//³ mv_par10        //  Apenas Valores negativos                  ³
		//³ mv_par11        //  Ano de Competencia                        ³
		//³ mv_par12        //  Rendimentos acima de?                     ³
		//³ mv_par13        //  Imprime Complementares?                   ³
		//³ mv_par15        //  Imprime Ded. Separadamente?               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oReport := ReportDef()
		oReport:PrintDialog()
	EndIf

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEM570   ºAutor  ³Equipe - RH         º Data ³  10/08/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()
Local oReport
Local oSection1,oSection2
Local cDesc		:= STR0008  //"RELA€O DA D.I.R.F. ANUAL "
Local aOrd      := {STR0004,STR0005,STR0028} //"Matricula"###"C.G.C"###"Nome"
Local oBreakCC

//Comunicado sobre a não disponibilização da Rais e Dirf para releases inferiores à 12.1.25
If GetRpoRelease() < "12.1.023" .AND. FindFunction("DirfRaisMsg")
	DirfRaisMsg()
EndIf

Pergunte(cPERG, .F.)

DEFINE REPORT oReport NAME "GPEM570" TITLE OemToAnsi(STR0008) PARAMETER cPERG ACTION {|oReport| M570Imp(oReport)}   DESCRIPTION OemtoAnsi(STR0044) TOTAL IN COLUMN

	DEFINE SECTION o1SRL OF oReport TITLE OemToAnsi(STR0045) ORDERS aOrd TABLE "SRL"
		o1SRL:SetLineStyle()	// Impressao da descricao e conteudo do campo na mesma linha

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ oSection1 = usado para montar o cabeçalho do relatorio           ³
		//³ "Codigo de Retencao :"###"CPF : "###"Nome :"                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DEFINE CELL NAME "RL_CODRET" 	OF o1SRL TITLE STR0024
		DEFINE CELL NAME "RL_CPFCGC" 	OF o1SRL TITLE STR0025 SIZE 20
		DEFINE CELL NAME "RL_BENEFIC" 	OF o1SRL TITLE STR0026
		DEFINE CELL NAME "RL_MAT" 		OF o1SRL TITLE STR0083
		DEFINE CELL NAME "RL_FILIAL"	OF o1SRL TITLE STR0084

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ oSection2 = usado para fazer a segunda quebra no relatorio, tabela SR4          ³
	//³ " Mes       Moeda    Rend. Tributavel             Deducoes       Imposto Retido"³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DEFINE SECTION o2SRL OF oReport TITLE OemToAnsi(STR0046) ORDERS aOrd TABLE "SRL" TOTAL IN COLUMN

		DEFINE CELL NAME "MES" 		OF o2SRL 	TITLE STR0032       SIZE 10
		DEFINE CELL NAME "MOEDA" 	OF o2SRL 	TITLE STR0033		SIZE 10	BLOCK {|| STR0030 }
		DEFINE CELL NAME "RENDA" 	OF o2SRL 	TITLE STR0034		SIZE 12 ALIGN RIGHT PICTURE "@E 999,999,999.99"
		DEFINE CELL NAME "DEDUCAO" 	OF o2SRL 	TITLE STR0035		SIZE 12	ALIGN RIGHT PICTURE "@E 999,999,999.99"
		DEFINE CELL NAME "DEDINSS" 	OF o2SRL 	TITLE STR0085		SIZE 12	ALIGN RIGHT PICTURE "@E 999,999,999.99"
		DEFINE CELL NAME "DEDDEP" 	OF o2SRL 	TITLE STR0086		SIZE 12	ALIGN RIGHT PICTURE "@E 999,999,999.99"
		DEFINE CELL NAME "RETIDO" 	OF o2SRL 	TITLE STR0036		SIZE 12	ALIGN RIGHT PICTURE "@E 999,999,999.99"
		DEFINE CELL NAME "RATEIO" 	OF o2SRL 	TITLE STR0057       SIZE 150 ALIGN LEFT

		o2SRL:SetHeaderBreak(.F.)
		o2SRL:SetHeaderSection(.T.)

	DEFINE SECTION o3SRL OF oReport TITLE "TESTE" ORDERS aOrd TABLE "SRL" TOTAL IN COLUMN
		DEFINE CELL NAME "IDCMPL"	OF o3SRL TITLE STR0053		 SIZE 10  											//"Complemento RRA"
		DEFINE CELL NAME "DESC"		OF o3SRL TITLE STR0052		 SIZE 30  											//"Descrição"
		DEFINE CELL NAME "RENDRRA"	OF o3SRL TITLE STR0034		 SIZE 11 ALIGN RIGHT   		PICTURE "@E 999,999,999.99" //"Rend. Tributavel"
		DEFINE CELL NAME "PREVRRA"	OF o3SRL TITLE STR0051		 SIZE 11 ALIGN RIGHT   		PICTURE "@E 999,999,999.99" //"Contrib. Previdencia"
		DEFINE CELL NAME "JUSRRA"	OF o3SRL TITLE STR0054	 	 SIZE 11 ALIGN RIGHT   		PICTURE "@E 999,999,999.99" //"Desp. Judiciais"
		DEFINE CELL NAME "PENSRRA"	OF o3SRL TITLE STR0055	   	 SIZE 11 ALIGN RIGHT   		PICTURE "@E 999,999,999.99" //"Pensão"
		DEFINE CELL NAME "IMPRRA"	OF o3SRL TITLE STR0036		 SIZE 11 ALIGN RIGHT   		PICTURE "@E 999,999,999.99" //"Imposto Retido"
		DEFINE CELL NAME "RENDISEN"	OF o3SRL TITLE STR0056		 SIZE 11 ALIGN RIGHT   		PICTURE "@E 999,999,999.99" //"Rendimentos Isentos"

		o3SRL:SetHeaderBreak(.T.)
		o3SRL:SetHeaderSection(.T.)

Return(oReport)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEM570   ºAutor  ³Microsiga           º Data ³  11/08/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function M570Imp(oReport)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Declaracao de variaveis                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local oSection1 	:= oReport:Section(1) //Objeto
Local oSection2 	:= oReport:Section(2) //Objeto
Local oSection3 	:= oReport:Section(3) //Objeto
Local oBreakCc		:= Nil				  //Objeto
Local aDirfv[13,5]
Local aArea 		:= GetArea()
Local lValorIR		:= .F.
Local lFirst		:= .T.
Local M				:= 1
Local n				:= 1
Local nFunc 		:= 0
Local cInicio		:= ''
Local cFim			:= ''
Local cIndCond		:= ''
Local cFor			:= ''
Local cArqNtx		:= ''
Local oMsnTitle		:= Nil
Local oBreakFil		:= Nil
Local nValRend 		:= 0
Local nRenda    	:= 0
Local nDeducao  	:= 0
Local nRetido   	:= 0
Local nDedINSS		:= 0
Local nDedDep		:= 0
Local nI        	:= 0
Local nPos			:= 0
Local lRateio   	:= IIF(mv_par13 == 1,.T.,.F.)
Local lImpLin		:= .F.
Local cRateio   	:= ""
Local cRateio1  	:= ""
Local aRateio   	:= {}
Local aRatRRA		:= {}
Local aValRRA		:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis de Acesso do Usuario                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cAcessaSRL	:= &( " { || " + ChkRH( "GPEM570" , "SRL" , "2" ) + " } " )
Local cAcessaSR4	:= &( " { || " + ChkRH( "GPEM570" , "SR4" , "2" ) + " } " )
Local nOrdem		:= oSection1:GetOrder()
Local cOrdem		:= ""
Local aMes   := { STR0010,STR0011,STR0012,STR0013,STR0014,STR0015,; 	//"Janeiro  "###"Fevereiro"###"Marco    "###"Abril    "###"Maio     "###"Junho    "
				  STR0016,STR0017,STR0018,STR0019,STR0020,STR0021,; 	//"Julho    "###"Agosto   "###"Setembro "###"Outubro  "###"Novembro "###"Dezembro "
				  STR0022}                               				//"13o.Sal. "

Local lExibeMatFil  := .F.
Local lDedDep		:= .F.
Local nSize			:= 0

Private cAnox	:= Strzero(MV_PAR11,4)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se o ano for >= 2016 | Verifica se existe tabela SM8 e SM9
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If MV_PAR11 >= 2016
	dbSelectArea( "SM8" )
	If SM8->( fieldpos("M8_ANO"))== 0
		Aviso(STR0081,STR0082,{"OK"}) //"Atencao"#"Tabelas desatualizadas. Execute o atualizador"
		Return()
	EndIf

	dbSelectArea( "SM9" )
	If SM9->( fieldpos("M9_ANO"))== 0
		Aviso(STR0081,STR0082,{"OK"}) //"Atencao"#"Tabelas desatualizadas. Execute o atualizador"
		Return()
	EndIf
Endif

oReport:cTitle	:= AllTrim(oReport:cTitle) + " - " + cAnoX

dbSelectArea( "SRL" )
If nOrdem == 1
	oBreakCc 	:= TRBreak():New(oSection1,{|| (cAlias)->(RL_FILIAL + RL_MAT) },,.F.,,.f.) 				// quebra por Matricula
ElseIf nOrdem == 2
	oBreakCc 	:= TRBreak():New(oSection1,{|| (cAlias)->(RL_FILIAL + RL_CPFCGC) },,.F.,,.f.)			// quebra por CPF/CNPJ
ElseIf nOrdem == 3
	oBreakCc 	:= TRBreak():New(oSection1,{|| (cAlias)->(RL_FILIAL + RL_BENEFIC)},,.F.,,.f.) 			// quebra por Beneficiario
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Faz filtro no arquivo...                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                          ³
//³ mv_par01        //  Filial  De                                ³
//³ mv_par02        //  Filial  Ate                               ³
//³ mv_par03        //  C.G.C. De                                 ³
//³ mv_par04        //  C.G.C. Ate                                ³
//³ mv_par05        //  Matricula De                              ³
//³ mv_par06        //  Matricula Ate                             ³
//³ mv_par07        //  Apenas com I.R.                           ³
//³ mv_par08        //  Nome De                                   ³
//³ mv_par09        //  Nome Ate                                  ³
//³ mv_par10        //  Apenas Valores negativos                  ³
//³ mv_par11        //  Ano de Competencia                        ³
//³ mv_par12        //  Rendimentos acima de?                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cFilDe 		:= MV_PAR01
cFilAte		:= MV_PAR02
cCGCDe 		:= MV_PAR03
cCGCAte		:= MV_PAR04
cMatDe		:= MV_PAR05
cMatAte		:= MV_PAR06
nApenasIr	:= MV_PAR07
cNomeDe		:= MV_PAR08
cNomeAte	:= MV_PAR09
nApValNeg	:= MV_PAR10
nAnoComp	:= MV_PAR11
nValAcima	:= MV_PAR12

if Valtype(MV_PAR14) == 'N'
	lExibeMatFil := Iif(MV_PAR14 == 1, .T., .F.)
endif

if Valtype(MV_PAR15) == 'N'
	lDedDep := Iif(MV_PAR15 == 1, .T., .F.)
endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MakeSqlExpr("GPM570")
cAlias	:= GetNextAlias()

oSection1:BeginQuery()

If nOrdem == 1
	cOrdem += "%RL_FILIAL , RL_MAT%"
ElseIf nOrdem == 2
	cOrdem += "%RL_FILIAL , RL_CGCFONT , RL_CODRET , RL_TIPOFJ , RL_CPFCGC%"
ElseIf nOrdem == 3
	cOrdem += "%RL_FILIAL , RL_BENEFIC%"
Endif

BeginSql alias cAlias

SELECT SRL.*
FROM %table:SRL% SRL
WHERE	SRL.RL_FILIAL 	>= %exp:cFilDe% AND SRL.RL_FILIAL	<= %exp:cFilAte% AND
		SRL.RL_CGCFONT 	>= %exp:cCGCDe% AND SRL.RL_CGCFONT	<= %exp:cCGCAte% AND
		SRL.RL_MAT		>= %exp:cMatDe% AND SRL.RL_MAT		<= %exp:cMatAte% AND
		SRL.RL_BENEFIC	>= %exp:cNomeDe% AND SRL.RL_BENEFIC	<= %exp:cNomeAte% AND
	  	SRL.%notDel%
		ORDER BY %exp:cOrdem%
EndSql
oSection1:EndQuery()

cFilAnterior := Replicate("!", FWGETTAMFILIAL)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o total da regua da tela de processamento do relatorio ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
(cAlias)->(dbSelectArea(cAlias))
oReport:SetMeter((cAlias)->(RecCount()))
(cAlias)->(dbGotop())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Iniciando a oSection 1 com o Init()³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection1:Init()

While ( cAlias )->( !Eof() )
	lImpLin	:= .F.
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Movimenta Regua Processamento                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Incrementa a régua da tela de processamento do relatório     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
  	oReport:IncMeter()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o usuário cancelou a impressão do relatorio      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If oReport:Cancel()
		Exit
	EndIf

	If (cAlias)->RL_FILIAL # cFilAnterior
		cFilAnterior := (cAlias)->RL_FILIAL
	Endif

	dbSelectArea(cAlias)
	cChave := (cAlias)->(RL_FILIAL + RL_MAT + RL_CODRET)
	While !Eof() .And. (cAlias)->(RL_FILIAL + RL_MAT + RL_CODRET) = cChave
		//Posiciona no registro da SRL para o Controle de acesso usuário(GPEA270), ("cAlias" não é igual a "SRL")
		SRL->(dbSeek( (cAlias)->RL_FILIAL+ (cAlias)->RL_MAT + (cAlias)->RL_TIPOFJ + (cAlias)->RL_CPFCGC + (cAlias)->RL_CODRET ) )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Busca Informacoes de Rateio                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lRateio
			aRateio := {}
			aRatRRA	:= {}
			aRatRRA := GM570Rat( (cAlias)->RL_FILIAL, cAnox, (cAlias)->RL_CPFCGC, "1889", (cAlias)->RL_MAT )
	       aRateio := GM570Rat( (cAlias)->RL_FILIAL, cAnox, (cAlias)->RL_CPFCGC, (cAlias)->RL_CODRET, (cAlias)->RL_MAT )
	  	EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Consiste controle de acessos e filiais validas               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !((cAlias)->RL_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRL)
			dbSelectArea(cAlias)
			(cAlias)->( DbSkip() )
			Loop
		EndIf

		For M := 1 to 13
			For N:= 1 TO 5
				aDirfv[M,N] := 000000000000.00
			Next N
		Next M

		lValorIr 	:= .F.
		lNegativo   := .F.
		nTemVl		:= 0
		nValRend	:= 0
		aValRRA 	:= {}

		dbSelectArea("SR4")
		dbSeek( (cAlias)->(RL_FILIAL+RL_MAT+RL_CPFCGC+RL_CODRET) ) 		//+ cAnox )
		While !Eof() .And. (cAlias)->(RL_FILIAL+RL_MAT+RL_CPFCGC+RL_CODRET+RL_PAIS+RL_NIFEX) == ;
							    SR4->(R4_FILIAL+R4_MAT+R4_CPFCGC+R4_CODRET+R4_PAIS+R4_NIFEX)
			If SR4->R4_ANO <> cAnox
				SR4->( dbSkip() )
				Loop
			Endif

	        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Consiste controle de acessos                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !(SR4->R4_FILIAL $ fValidFil()) .Or. !Eval( cAcessaSR4 )
				dbSelectArea("SR4")
				SR4->( dbSkip() )
				Loop
			EndIf

			nElem := 0

		    If Len(Alltrim(SR4->R4_TIPOREN)) == 1
	        	If Alltrim(SR4->R4_TIPOREN) $ 'AxBxCxDxJxKxLxTxM'
					nMes   := Val(SR4->R4_MES)
					If Alltrim(SR4->R4_TIPOREN) $ 'AxJ'
						nElem  := 1
						nMes   := If (Alltrim(SR4->R4_TIPOREN) = "A",Val(SR4->R4_MES),13)
	                Elseif Alltrim(SR4->R4_TIPOREN) $ 'BxCxKxTxM'
						If lDedDep
							If Alltrim(SR4->R4_TIPOREN) $ 'BxCxKxM' // Contribuição Previdenciária , Pensao Judicial, Deducoes 13o.Salario , Contribuicao a Previdencia Privada
								nElem := 4
							ElseIf Alltrim(SR4->R4_TIPOREN) $ 'T' // Valor Dependentes
								nElem := 5
							EndIf
						Else
							nElem  := 2
						EndIf
						nMes   := If (Alltrim(SR4->R4_tiporen) # "K",Val(SR4->R4_MES),13)
					Elseif Alltrim(SR4->R4_TIPOREN) $ 'DxL'
						nElem  := 3
						nMes   := If (Alltrim(SR4->R4_TIPOREN) # "L",Val(SR4->R4_MES),13)

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Seta Variavel que tem IR                                     ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If Alltrim(SR4->R4_TIPOREN) == 'D'
							lValorIR := .T.
						Endif
					Endif

					nMesUf := Val(SR4->R4_MES)
					If nElem > 0
						aDirfv[nMes,nElem] +=  SR4->R4_VALOR
					EndIf
					nTemVl	+=  SR4->R4_VALOR

					If nElem == 1
						nValRend += SR4->R4_VALOR
					EndIf

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica se Tem Valores Negativos                            ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If SR4->R4_VALOR < 0.00
						lNegativo := .T.
					Endif
				EndIf
			ElseIf Len(Alltrim(SR4->R4_TIPOREN)) == 2
				nMes := 13
				If Alltrim(SR4->R4_TIPOREN) $ 'B1xC1xK1xT1xM1'
					If lDedDep
						If Alltrim(SR4->R4_TIPOREN) $ 'B1xC1xK1xM1' // 13o - Previdencia Oficial, 13o - Pensao judicial,13o - Contribuicao a previdencia privada
							nElem := 4
						ElseIf Alltrim(SR4->R4_TIPOREN) $ 'T1' // 13o - Dependentes
							nElem := 5
						EndIf
					Else
						nElem  := 2
					EndIf
			    EndIf
				nMesUf := 13
				If Alltrim(SR4->R4_TIPOREN) $ "O1*Q1*C3"
					nMes   := Val(SR4->R4_MES)
					nMesUf := Val(SR4->R4_MES)
					If Alltrim(SR4->R4_TIPOREN) == "O1"
						nElem  := 1
					ElseIf Alltrim(SR4->R4_TIPOREN) == "C3"
						nElem  := 2
					Else
						nElem  := 3
					EndIf
				EndIf
				If nElem > 0
					aDirfv[nMes,nElem] +=  SR4->R4_VALOR
				EndIf
				nTemVl	+=  SR4->R4_VALOR
				If nElem == 1
					nValRend += SR4->R4_VALOR
				EndIf

				If ( nPos := aScan( aValRRA, { |x| x[1] == SR4->R4_IDCMPL } ) ) == 0
					aAdd(aValRRA,{SR4->R4_IDCMPL , fDesc("RF1", SR4->R4_IDCMPL     , "RF1_DESC"), 0 , 0 , 0 , 0 , 0 , 0 })
					nPos := Len(aValRRA)
				EndIf
				If AllTrim(SR4->R4_TIPOREN) == 'A1'
					aValRRA[nPos,03] += SR4->R4_VALOR
				ElseIf AllTrim(SR4->R4_TIPOREN) == 'B2'
					aValRRA[nPos,04] += SR4->R4_VALOR
				ElseIf AllTrim(SR4->R4_TIPOREN) == 'B3'
					aValRRA[nPos,05] += SR4->R4_VALOR
				ElseIf AllTrim(SR4->R4_TIPOREN) == 'C2'
					aValRRA[nPos,06] += SR4->R4_VALOR
				ElseIf AllTrim(SR4->R4_TIPOREN) == 'D2'
					aValRRA[nPos,07] += SR4->R4_VALOR
				ElseIf AllTrim(SR4->R4_TIPOREN) == 'I1'
					aValRRA[nPos,08] += SR4->R4_VALOR
				EndIf

			Endif

			dbSkip()
		Enddo

		If nValRend < nValAcima .and. nApValNeg # 1
			(cAlias)->( DbSkip() )
			Loop
		EndIf

		If nTemVl == 0   // se houver valor, dezpreza o funcionario
			dbSelectArea(cAlias)
			(cAlias)->( DbSkip() )
			loop
		Endif

		If nApValNeg # 1 .or. (nApValNeg==1 .And. lNegativo )
			If nApenasIr # 1  .Or. (nApenasIr == 1  .And. lValorIr)

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Impressao do Cabecalho do Funcionario                        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If (cAlias)->RL_TIPOFJ == "1"
	 				oSection1:Cell("RL_CPFCGC"):SetTitle(STR0025)	// "CPF : " - Muda o nome do titulo da celula
				ElseIf  (cAlias)->RL_TipoFJ == "2"
					oSection1:Cell("RL_CPFCGC"):SetTitle(STR0027)	//  "CGC : " - Muda o nome do titulo da celula
				Endif

				oSection1:Cell("RL_CODRET"):SetBlock({|| (cAlias)->RL_CODRET 	})
				oSection1:Cell("RL_CPFCGC"):SetBlock({|| (cAlias)->RL_CPFCGC 	})
				oSection1:Cell("RL_BENEFIC"):SetBlock({||alltrim((cAlias)->RL_BENEFIC) + SPACE(5)})

				if lExibeMatFil // SIM
					nSize := len(alltrim((cAlias)->RL_BENEFIC) + SPACE(5))
					oSection1:Cell("RL_BENEFIC"):SetSize(nSize)
					oSection1:Cell("RL_MAT"):Enable()
					oSection1:Cell("RL_FILIAL"):Enable()
				ELSE
					oSection1:Cell("RL_MAT"):Disable()
					oSection1:Cell("RL_FILIAL"):Disable()
				ENDIF

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Faz a impressao da oSection1 ou seja do cabeçalho.           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oSection1:PrintLine()
				lImpLin	:= .T.

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Impressao dos Valores                                        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oSection2:Init()
				nRenda   := 0
				nDeducao := 0
				nRetido  := 0
				nDedINSS := 0
				nDedDep  := 0
				For M := 1 to 13
					oSection2:Cell("MES"):SetBlock({|| 		aMes[M] 	})	// Contem os Meses
					oSection2:Cell("RENDA"):SetBlock({||	aDirfv[M,1]	})	// Valor dos Rent.Tributavel

					If lDedDep
						If oReport:nDevice == 4 .And. oReport:nExcelPrintType == 3
							oSection2:Cell("DEDUCAO"):Enable()	
						Else
							oSection2:Cell("DEDUCAO"):Disable()
						EndIf
						oSection2:Cell("DEDINSS"):Enable()
						oSection2:Cell("DEDDEP"):Enable()
						oSection2:Cell("DEDINSS"):SetBlock({|| aDirfv[M,4]	})	// Valores Deduçoes - INSS
						oSection2:Cell("DEDDEP"):SetBlock({|| aDirfv[M,5]	})	// Valores Deduçoes - Dependentes				// Valores Deduçoes
					Else
						oSection2:Cell("DEDUCAO"):SetBlock({|| 	aDirfv[M,2]	})	// Valores Deduçoes
						oSection2:Cell("DEDINSS"):Disable() 					// Valores Deduçoes - INSS
						oSection2:Cell("DEDDEP"):Disable() 						// Valores Deduçoes - Dependentes
					EndIf

					oSection2:Cell("RETIDO"):SetBlock({|| 	aDirfv[M,3]	})	// Valores Imposto Retido
					oSection2:Cell("RATEIO"):Disable()

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Incrementa Totalizadores                                     ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					nRenda   += aDirfv[M,1]
					nDeducao += aDirfv[M,2]
					nRetido  += aDirfv[M,3]

					If lDedDep
						nDedINSS += aDirfv[M,4]
						nDedDep  += aDirfv[M,5]
					EndIf

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Impressao da oSection2 ou seja do corpo do relatorio.        ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					oSection2:PrintLine()
				Next M

				M	:= 1

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Campos totalizadores                                         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oSection2:Cell("MES"):SetBlock({|| STR0043 })
				oSection2:Cell("RENDA"):SetBlock({|| nRenda })

				If lDedDep
					oSection2:Cell("DEDINSS"):SetBlock({|| 	nDedINSS })
					oSection2:Cell("DEDDEP"):SetBlock({|| 	nDedDep })
				Else
					oSection2:Cell("DEDUCAO"):SetBlock({|| 	nDeducao })
				EndIf

				oSection2:Cell("RETIDO"):SetBlock({|| nRetido })

				oSection2:PrintLine()
				oSection2:Finish()
				oReport:SkipLine()

				If Len(aValRRA) > 0
					oSection3:Init()
					For nPos := 1 to Len(aValRRA)
						oSection3:Cell("IDCMPL"):SetBlock({|| aValRRA[nPos,01]})
						oSection3:Cell("DESC"):SetBlock({|| aValRRA[nPos,02]})
						oSection3:Cell("RENDRRA"):SetBlock({|| aValRRA[nPos,03]})
						oSection3:Cell("PREVRRA"):SetBlock({|| aValRRA[nPos,04]})
						oSection3:Cell("JUSRRA"):SetBlock({|| aValRRA[nPos,05]})
						oSection3:Cell("PENSRRA"):SetBlock({|| aValRRA[nPos,06]})
						oSection3:Cell("IMPRRA"):SetBlock({|| aValRRA[nPos,07]})
						oSection3:Cell("RENDISEN"):SetBlock({|| aValRRA[nPos,08]})

						oSection3:PrintLine()
					Next nPos
					oSection3:Finish()
					oReport:SkipLine()
				EndIf
			Endif
    	Endif
		dbSelectArea(cAlias)
		dbSkip()
	EndDo

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Imprimir detalhes do rateio Assistencia Medica e Odontologica ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRateio .and. lImpLin

		oReport:SkipLine()
		oSection2:Init()
		oSection2:Cell("MES"):Disable()
		oSection2:Cell("MOEDA"):Disable()
		oSection2:Cell("RENDA"):Disable()
		oSection2:Cell("DEDUCAO"):Disable()
		oSection2:Cell("DEDINSS"):Disable()
		oSection2:Cell("DEDDEP"):Disable()
		oSection2:Cell("RETIDO"):Disable()
		oSection2:Cell("RATEIO"):Enable()

		For nI := 1 To Len(aRateio)
			If aRateio[nI][7] $ cChave
				cRateio  := ""
				cRateio  := PADR( SubStr( aRateio[nI][1],1,60 ) ,60 ) + Space(1) + "Valor: " + AllTrim( aRateio[nI][2] ) //62
				oSection2:Cell("RATEIO"):SetBlock({|| cRateio })
				oSection2:PrintLine()

				//Se .T. sigfinica que veio de dentro da SM8 ou SM9
				If(aRateio[ni,5]) == .T.
					cRateio1 := "CPF/CNPJ: " + aRateio[ni,6]
				Else
					cRateio1 := "CPF/CNPJ: " + aRateio[ni,4]
				EndIf

				If(!Empty(cRateio1))
					oSection2:Cell("RATEIO"):SetBlock({|| cRateio1 })
					oSection2:PrintLine()
				EndIf
				//Linha em branco para separar registro
				oSection2:Cell("RATEIO"):SetBlock({|| "" })
				oSection2:PrintLine()
			EndIf
		Next nI

		For nI := 1 to Len(aRatRRA)
			cRateio  := ""
			cRateio1 := ""
			cRateio  := PADR( SubStr( aRatRRA[nI][1],1,62 ) ,62 ) + Space(1) + "Valor: " + AllTrim( aRatRRA[nI][2] )
			oSection2:Cell("RATEIO"):SetBlock({|| cRateio })
			oSection2:PrintLine()

			//Se .T. sigfinica que veio de dentro da SM8 ou SM9
			If(aRatRRA[ni,5]) == .T.
				cRateio1 := "CPF/CNPJ: " +aRatRRA[ni,6]
			Else
				cRateio1 := "CPF/CNPJ: " +aRatRRA[ni,4]
			EndIf

			If !Empty(cRateio1)
				oSection2:Cell("RATEIO"):SetBlock({|| cRateio1 })
				oSection2:PrintLine()
			EndIf
			//Linha em branco para separar registro
			oSection2:Cell("RATEIO"):SetBlock({|| "" })
			oSection2:PrintLine()
		Next nI

		oSection2:Finish()

		oSection2:Cell("MES"):Enable()
		oSection2:Cell("MOEDA"):Enable()
		oSection2:Cell("RENDA"):Enable()
		oSection2:Cell("DEDUCAO"):Enable()
		oSection2:Cell("RETIDO"):Enable()
	EndIf

	If lImpLin
		oReport:SkipLine()
		oReport:Thinline()
	EndIf
Enddo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza impressao inicializada pelo metodo Init             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection2:Finish()
oSection1:Finish()

Return(NIL)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GM570Rat  ºAutor  ³Alessandro Santos   º Data ³  18/01/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Busca as informacoes de Rateio Assistencia Medica e Odonto  º±±
±±º          ³logica do funcionario.                                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Generico                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GM570Rat ( cFil, cAno, cCPFCGC, cCODRET, cMat )
Local aDados := {}
Local nCont	 := 0
Local nPos	 := 0
Local aAreaRCS := RCS->(GetArea())

dbSelectArea( "RCS" )
dbSetOrder( 3 ) // RCS_FILIAL+RCS_ANO+RCS_CPFBEN+RCS_CODRET
dbSeek( cFil + cAno + cCPFCGC + cCodRet )
While RCS->( !Eof() ) .AND. RCS->RCS_ANO == cAno .AND. RCS->RCS_CPFBEN == cCPFCGC .AND.;
	RCS->RCS_CODRET == cCODRET

	If ( nPos := aScan( aDados, { |aDados| aDados[3] == RCS->RCS_OUTROS .And. aDados[4] == AllTrim(RCS->RCS_CPFCGC) .And. aDados[6] == RCS->RCS_FILIAL + RCS->RCS_MAT} ) ) == 0
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Adiciona Informacoes do rateio                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aAdd(aDados,{IIF( AllTrim( RCS->RCS_DESCRI ) == "ASSISTENCIA MEDICA","ASSISTENCIA MEDICA TITULAR",RCS->RCS_DESCRI ),;
						 RCS->RCS_VALOR,;
						 RCS->RCS_OUTROS,;
						 AllTrim(RCS->RCS_CPFCGC),;
						 .F. ,;
						 ""/*cpfcnpj*/,;
						 RCS->RCS_FILIAL + RCS->RCS_MAT})
	Else
		aDados[nPos, 2] += RCS->RCS_VALOR
	EndIf

    RCS->( dbSkip() )
EndDo

If !Empty(cMat) .AND. cAno >= "2016"
	InfCompPensao(cFil, cAno, cMat, cCodRet,  @aDados)
	InfCompPrv   (cFil, cAno, cMat, cCodRet,  @aDados)
EndIf

For nCont := 1 To Len(aDados)
	aDados[nCont, 2] := Transform( aDados[nCont, 2], "@E 999,999,999.99" )
	aDados[nCont, 4] := Transform( aDados[nCont, 4], If( Len(aDados[nCont, 4]) == 11, "@R 999.999.999-99", "@!R NN.NNN.NNN/NNNN-99" ) )
Next nCont

RestArea(aAreaRCS)
Return(aDados)

/*/{Protheus.doc} InfCompPensao
Obtém valores da pensao para quadro 7 de informações complementares
@author Oswaldo Leite
@since 02/01/2017
@version P12
/*/
Static Function InfCompPensao(cFil, cAno, cMat, cRlCodRet, aDados)
	Local cGetAlias  := GetNextAlias()
	Local cDesc      := ''
	Local cCpfCnpj	:= ''
	Local nPos 		:= 0
	Local aLetra     := {{"C" , OemToAnsi(STR0073)},; //Pensao Judicial
							{"C1", OemToAnsi(STR0074)},; //13º – Pensão Judicial
							{"C2", OemToAnsi(STR0075)},; //Pensão Judicial - Art. 12-A, Lei Nº 7.713 de 1988.
							{"C3", OemToAnsi(STR0076)},; //Pensão Judicial (PLR).
							{"8" , OemToAnsi(STR0077)},; //Pensão Alimentícia – Tributação sob exigibilidade suspensa.
							{"81", OemToAnsi(STR0078)}}  //13º – Pensão Alimentícia – Tributação sob exigibilidade suspensa.

	BeginSql Alias cGetAlias
		SELECT	M8_FILIAL, M8_MAT, M8_ANO, M8_CODRET, M8_CPFBEN, M8_CODBEN, M8_TIPOREN, SUM( M8_VALOR ) AS TOTAL
		FROM	%Table:SM8%
		WHERE	M8_FILIAL	= %Exp:( cFil )%
		  AND	M8_ANO		= %Exp:( cAno )%
		  AND	M8_MAT		= %Exp:( cMat )%
		  AND	( M8_CODRET	= %Exp:( cRlCodRet )%  OR  M8_CODRET = '1889')
		  AND	%NotDel%
		GROUP BY M8_FILIAL, M8_MAT, M8_ANO, M8_CODRET, M8_CPFBEN, M8_CODBEN, M8_TIPOREN
		ORDER BY M8_FILIAL, M8_MAT, M8_ANO, M8_CODRET, M8_CPFBEN, M8_CODBEN, M8_TIPOREN
	EndSql

	While ( cGetAlias )->( !Eof() .And. M8_FILIAL + M8_MAT + M8_ANO == cFil + cMat + cAno )

		cCodBen	:= ( cGetAlias )->M8_CODBEN
		cTipRen	:= ( cGetAlias )->M8_TIPOREN
		cCpfBen	:= ( cGetAlias )->M8_CPFBEN
		nTotSM8	:= ( cGetAlias )->TOTAL

		Dbselectarea('SRQ')
		SRQ->(DbSetOrder(1))

		SRQ->(DBSeek(cFil+cMat+(cGetAlias)->(M8_CODBEN) ))

	   If SRQ->( dbSeek( cFil + cMat + cCodBen ) )
			cDesc := PAD( Upper( AllTrim( SRQ->RQ_NOME ) ), 60 )+ Space( 1 )

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Define a prioridade de impressao das informacoes complementares³
			//³ dependendo do tipo de retencao da verba                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If( ( nPos := aScan( aLetra, { |x| x[ 1 ] == AllTrim( cTipRen ) } ) ) > 0 )
				cDesc += PADR( SUBSTR( UPPER( ALLTRIM( aLetra[ nPos, 2 ] ) ), 1, 38 ), 38 ) + Space( 1 )

				//Formata o CPF/CNPJ
				cCpfCnpj := ''
				If !Empty( cCpfBen )
					cCpfCnpj += Transform( cCpfBen, If( Len( AllTrim( cCpfBen ) ) == 11, "@R 999.999.999-99", "@!R NN.NNN.NNN/NNNN-99" ) )
				EndIf
			EndIf

			//Modelo de Impressao
			//------------------------------------------------------------------------------
			//FABIANA FERREIRA FE XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX Valor: 7.172,092.19
			//CPF/CNPJ: 185.795.258-88
			//FABIANA FERREIRA FE XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX Valor: 7.172,092.19
			//CPF/CNPJ: 185.795.258-88
			//------------------------------------------------------------------------------
			//123456789012345678901234567890123456789012345678901234567890123456789012345678

			//Realiza alimentacao de aDados
			aAdd( aDados, { cDesc, nTotSM8, 0, '', .T., cCpfCnpj, cFil + cMat } )

		EndIf
		(cGetAlias)->(DBSkip())
	EndDo

	(cGetAlias)->(DbCLoseArea())

Return

/*/{Protheus.doc} InfCompPrv
Obtem valores da previdencia para quadro 7 de informaçoes complementares
@author Oswaldo Leite
@since 02/01/2017
@version P12
/*/
Static Function InfCompPrv(cFil, cAno, cMat,cRlCodRet, aDados)

	Local cGetAlias	:= GetNextAlias()
	Local cDesc		:= ''
	Local cCpfCnpj	:= ''
	Local nPos		:= 0
	Local aLetra	:= {{"M", OemToAnsi(STR0071)},; //Contribuição à Previdência Privada.
							{"M1", OemToAnsi(STR0072)},; //13o-Contribuição à Previdência Privada.
							{"M2", OemToAnsi(STR0058)},; //FAPI
							{"M3", OemToAnsi(STR0059)},; //Fundo Prev.Serv.Público
							{"M4", OemToAnsi(STR0060)},; //Contr. Ente público patroc.
							{"M5", OemToAnsi(STR0061)},; //13o-FAPI
							{"M6", OemToAnsi(STR0062)},; //13o-Fundo Prev.Serv.Público
							{"M7", OemToAnsi(STR0063)},; //13o-Contr.Ente público patroc.
							{"9" , OemToAnsi(STR0079)},; //Previdência privada – Tributação sob exigibilidade suspensa.
							{"91", OemToAnsi(STR0080)},; //13º – Previdência Privada – Tributação sob exigibilidade suspensa.
							{"9A", OemToAnsi(STR0065)},; //Exigib. Susp.-FAPI
							{"9B", OemToAnsi(STR0066)},; //Exigib. Susp.-Fundo Prev.Serv.Público
							{"9C", OemToAnsi(STR0067)},; //Exigib. Susp.-Contr. Ente público patroc.
							{"9D", OemToAnsi(STR0068)},; //13o-Exigib. Susp.-FAPI
							{"9E", OemToAnsi(STR0069)},; //13o-Exigib. Susp.-Fundo Prev.Serv.Público
							{"9F", OemToAnsi(STR0070)}}  //13o-Exigib. Susp.-Contr. Ente públ. patroc.

	BeginSQL Alias cGetAlias
		SELECT M9_FILIAL, M9_MAT, M9_ANO, M9_CODRET, M9_CODFOR, M9_CNPJ, M9_NOMEEMP, M9_TIPOREN, SUM( M9_VALOR ) TOTAL
		FROM %Table:SM9%
		WHERE M9_FILIAL = %Exp:( cFil )%
			AND M9_ANO = %Exp:( cAno )%
			AND M9_MAT = %Exp:( cMat )%
			AND ( M9_CODRET = %Exp:( cRlCodRet )%  OR M9_CODRET = '1889')
			AND %NotDel%
		GROUP BY M9_FILIAL, M9_MAT, M9_ANO, M9_CODRET, M9_CODFOR, M9_CNPJ, M9_NOMEEMP, M9_TIPOREN
		ORDER BY M9_FILIAL, M9_MAT, M9_ANO, M9_CODRET, M9_CODFOR, M9_CNPJ, M9_NOMEEMP, M9_TIPOREN
	EndSQL

	While ( cGetAlias )->( ! Eof() .And. M9_FILIAL + M9_MAT + M9_ANO == cFil + cMat + cAno )

		cNomeEmp	:= ( cGetAlias )->M9_NOMEEMP
		cTipRen 	:= ( cGetAlias )->M9_TIPOREN
		cCNPJ	 	:= ( cGetAlias )->M9_CNPJ
		nTotSM9	:= ( cGetAlias )->TOTAL

		cDesc := PAD( Upper( AllTrim( cNomeEmp ) ), 30 ) + Space( 1 )

		If( ( nPos := aScan( aLetra, { |x| x[ 1 ] == AllTrim( cTipRen ) } ) ) > 0 )
			cDesc += PADR( SUBSTR( UPPER( ALLTRIM( aLetra[ nPos, 2 ] ) ), 1, 38 ), 38 ) + Space( 1 )
			//Formata o CPF/CNPJ
			cCpfCnpj := ''
			If ! Empty( cCNPJ  )
				cCpfCnpj := Transform( cCNPJ, If( Len( AllTrim( cCNPJ ) ) == 11, "@R 999.999.999-99", "@!R NN.NNN.NNN/NNNN-99" ))
			EndIf
		EndIf
		//Modelo de Impressao
		//------------------------------------------------------------------------------
		//FABIANA FERREIRA FE XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX Valor: 7.172,092.19
		//CPF/CNPJ: 185.795.258-88
		//FABIANA FERREIRA FE XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX Valor: 7.172,092.19
		//CPF/CNPJ: 185.795.258-88
		//------------------------------------------------------------------------------
		//123456789012345678901234567890123456789012345678901234567890123456789012345678

		//Valores do Array aDados
		//1 = cDesc = Nome + Descrição | 2 = Valor Pago
		//3 = 0  | 4 = ''
		//5 = Se registro vindo da SM8 ou SM9 | 6 = CPF ou CNPJ
		Aadd(aDados, { cDesc, nTotSM9, 0, '', .T., cCpfCnpj, cFil + cMat })

		(cGetAlias)->(DBSkip())
	EndDo

	(cGetAlias)->(DbCLoseArea())

Return
