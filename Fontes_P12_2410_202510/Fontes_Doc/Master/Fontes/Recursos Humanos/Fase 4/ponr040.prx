#INCLUDE "PROTHEUS.CH"
#include 'PONR040.CH'
#include 'PONCALEN.CH' 
#INCLUDE "report.ch"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ PONR040  ³ Autor ³ R.H. - J. Ricardo     ³ Data ³ 10.04.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Divergencias nas marcacoes                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ PONR040(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Mauricio MR ³17/10/01³010812³Correcao Impressao Nome Func Completo e   ³±± 
±±³            ³        ³      ³de Todas as Ocorrencias Qdo quebra de Pag.³±±
±±³Mauricio MR ³01/11/01³      ³Calcula Qtde de Marcacoes no Dia de Acordo³±± 
±±³            ³        ³      ³com o calendario do funcionario para veri-³±± 
±±³            ³        ³      ³ficar numero de Marcacoes Efetuadas.      ³±± 
±±³Mauricio MR ³05/11/01³      ³Quebra de Turno Corrigida.                ³±±   
±±³Marinaldo   ³19/11/01³Melhor³A funcao GetMarcacoes passara a utilizar a³±±
±±³            ³--------³------³Funcao fDiasFolga() para Verificar as  Fol³±±
±±³            ³--------³------³gas Automaticas.						  ³±±
±±³Mauricio MR ³19/11/01³      ³Correcao Calculo de Marcacoes p/ H.Noturno³±±
±±³            ³        ³      ³Limita a verificacao de Divergencias ao   ³±±  
±±³            ³        ³      ³periodo solicitado.                       ³±±  
±±³Mauricio MR ³02/01/02³Acerto³Verificacao da existencia de Ordem Valida ³±± 
±±³            ³        ³      ³para calculo do no. de Marcacoes segundo  ³±± 
±±³            ³        ³      ³a TabCalend.                              ³±±  
±±³ Priscila R.³04/06/02³------³Ajuste no relatorio para que seja impresso³±± 
±±³            ³--------³------³corretamente o C.C no tamanho 20.         ³±± 
±±³Mauricio MR ³07/06/02³Acerto³Chamada a funcao correta DescCC().        ³±±  
±±³Mauricio MR ³04/12/02³------³Retirada a restricao de existencia de cra-³±± 
±±³            ³--------³------³cha para a emissao do relatorio.	      ³±±   
±±³=======================================================================³±± 
±±³                         *** Versao 9.12 ***                           ³±± 
±±³=======================================================================³±± 
±±³Leandro Dr  ³24/03/08³Melhor³Inclusão de chave processo/periodo/roteiro³±± 
±±³            |        |      |NumPagto. Impressao por data ou periodo   ³±± 
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programador  ³ Data     ³ FNC            ³  Motivo da Alteracao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ³±±
±±³Aldo Marini  ³12/04/2011³00000006207/2011³Conversao para TReport                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function PONR040()
Local	oReport   
Local	aArea 		:= GetArea()
Private nColPro		:= 1
Private cPerg		:= "PN040RA"
Private cProcesso 	:= Nil
Private cPeriodo  	:= Nil
Private cRoteiro  	:= Nil
Private cNumPagto 	:= Nil

If __cRDD == "TOPCONN" .And. ;	// Verifica se esta utilizando ambiente TOP
	GetRpoRelease("R5").And. ;	// Verifica se esta no Release 11.5
	TRepInUse()					// Verifica se Imprime em formato personalizavel

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Verifica se deve imprimir por resultados ou apontamentos     ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If fOpcRadio(@nColPro, STR0001,;      //"Relatorio de Divergencias"
						   STR0035,;      //"Selecione a opcao de impressao:"
				   		   STR0036,;      //"Por Periodo"
				   		   STR0037 ) == 0 //"Por Datas"
		Return
	EndIf
		
	cPerg := IF(nColPro == 1,"PN040RA","PN040RB")	

	pergunte(cPerg,.F.) 

    oReport := ReportDef()
    oReport:PrintDialog()
Else
 	PONR040R3()
EndIF    

RestArea( aArea )

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ ReportDef  ³ Autor ³ Totvs IP RH           ³ Data ³12/04/2011³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Divergencias nas marcacoes                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ PONR040                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONR040 - Generico                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()
Local oReport 
Local oSection1 
Local oSection2 

Private aOrd    := {STR0004 , STR0005 , STR0006 , STR0007 , STR0030 , STR0034 } // 'Matricula'###'Centro de Custo'###'Nome'###'Turno'###'C.Custo+Nome'###'Departamento'
Private cTitulo		:= OemToAnsi(STR0001)			//'Divergencias nas Marcacoes'

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao dos componentes de impressao                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE REPORT oReport NAME "PONR040" TITLE cTitulo PARAMETER cPerg ACTION {|oReport| PN040Imp(oReport)} DESCRIPTION OemtoAnsi(STR0043) TOTAL IN COLUMN // "Este programa emite Relacao dos Funcionarios com Divergencias de Marcacoes"

	oSection1 := TRSection():New( oReport, OemToAnsi(STR0044), {"SRA","CTT","SR6"}, aOrd,,,,,.F.,.F.,.F.) // "Funcionarios"

		TRCell():New(oSection1,"RA_FILIAL"	,"SRA")
		TRCell():New(oSection1,"RA_CHAPA"	,"SRA",,,5)
		TRCell():New(oSection1,"RA_MAT"		,"SRA",,,6)
		TRCell():New(oSection1,"RA_NOME"	,"SRA",,,30,,,,.T.)
		TRCell():New(oSection1,"RA_CC"		,"SRA",OemToAnsi(STR0048),,9)	// "C.Custo"
		TRCell():New(oSection1,"CTT_DESC01"	,"CTT",,,20,,,,.T.)
		TRCell():New(oSection1,"RA_TNOTRAB"	,"SRA",OemToAnsi(STR0049),,3)	// "Tno"
		TRCell():New(oSection1,"R6_DESC"	,"SR6",,,15,,,,.T.)
		TRCell():New(oSection1,"RA_DEPTO"	,"SRA",OemToAnsi(STR0054),,9)	// Depto
		TRCell():New(oSection1,"QB_DESCRIC"	,"SQB",,,15,,,,.T.)

	oSection2 := TRSection():New( oSection1, OemToAnsi(STR0046),,,,,,,.F.,.F.,.F. )	 //"Marcações"

		TRCell():New( oSection2, "DATA"			, "" ,OemToAnsi(STR0047),,10)		// "Data"
		TRCell():New( oSection2, "MARCACOES"	, "" ,OemToAnsi(STR0046),,70)		// "Marcações"
		TRCell():New( oSection2, "OCORRENCIAS"	, "" ,OemToAnsi(STR0045),,40)		// "Ocorrencias"
		TRCell():New( oSection2, "PROCESSO"		, "" ,OemToAnsi(STR0050),,5)	// "Processo"
		TRCell():New( oSection2, "PERIODO"		, "" ,OemToAnsi(STR0051),,6)	// "Periodo"	
		TRCell():New( oSection2, "ROTEIRO"		, "" ,OemToAnsi(STR0052),,3)	// "Rot"
		TRCell():New( oSection2, "NUMPAGTO"		, "" ,OemToAnsi(STR0053),,2)	// "NP"

Return(oReport)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ PN040Imp   ³ Autor ³ Totvs IP RH           ³ Data ³12/04/2011³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Impressao do relatorio em TReport                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ PN040Imp(oReport)                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oReport                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONR040 - Generico - Release 4                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PN040Imp(oReport)

Local oSection  := oReport:Section(1)
Local oSection1 := oReport:Section(1):Section(1)
Local cSitQuery	:= ""    
Local cCatQuery	:= ""  
Local nReg		:= 0
Local nOrdem	:= 1

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis de Acesso do Usuario                               ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Local cAcessaSRA	:= &( " { || " + ChkRH( "PONR040" , "SRA" , "2" ) + " } " )

//-- Variaveis Locais
* Caracter
Local cKey       := ''
Local cDet       := ''
Local cOrdem     := ''
Local cTraba     := '' 

* Data
Local dDataOk    := CtoD('  /  /  ')
Local dData      := CtoD('  /  /  ')

* Array
Local aTabPadrao := {}
Local aTabCalend := {}
Local aNewMarc	 := {}
Local aMarcacoes := {}
Local aMotivo    := {}
Local aTurnos    := {}
Local aHENA      := {}
Local aIntervalo := {}
Local aSiglaMarc := {}

* L¢gico
Local lIntMaior	 := .F.
Local lIntMenor  := .F.

* N£m‚rico
Local nX         := 0
Local nY         := 0
Local nZ         := 0
Local nQuant     := 0   
Local nHrReal	 := 0
Local nIntProg   := 0
Local nIntReal   := 0
Local nPos1E     := 0
Local nW         := 0
Local nLenMarc	 := 0
Local nLenCalend := 0
Local nPosOrdem	 := 0  
Local nTolInt	 := 0

Local oBreakCc
Local oBreakFil
Local oBreakFun

Private cAliasQry 	:= GetNextAlias()

Private cFilAnte   := ''
Private cTnoAnt    := ''

Private cSit     := ""
Private cCat     := ""
Private lCC      := .F.
Private lNMarc   := .F.
Private lHExtr   := .F.
Private lInter   := .F.
Private lSitfu   := .F.
Private dDataIni := CTOD("  /  /  ")
Private dDataFim := CTOD("  /  /  ")
Private dPerIni  := CTOD('  /  /  ')
Private dPerFim  := CTOD('  /  /  ')
Private lPeriodo  := .F.
Private oPeriodo  := RHPERIODO():New()

TabMarc('SPJ',@aSiglaMarc)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carregando variaveis mv_par?? para Variaveis do Sistema.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( nColPro == 1 ) 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ PN040RA - Perguntes por Periodo                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ MV_PAR01        //  Processo                                 ³
	//³ MV_PAR02        //  Periodo                                  ³
	//³ MV_PAR03        //  Roteiro                                  ³
	//³ MV_PAR04        //  Numero de Pagamento                      ³
	//³ MV_PAR05        //  Departamento                             ³
	//³ MV_PAR06        //  Filial                                   ³
	//³ MV_PAR07        //  Centro de Custo                          ³
	//³ MV_PAR08        //  Turno                                    ³
	//³ MV_PAR09        //  Matricula                                ³
	//³ MV_PAR10        //  Nome                                     ³
	//³ MV_PAR11        //  Situacao                                 ³
	//³ MV_PAR12        //  Categoria                                ³
	//³ MV_PAR13        //  Imprime C.C em outra Pagina              ³
	//³ MV_PAR14        //  Imprime Mensagem No. Marca‡”es           ³
	//³ MV_PAR15        //  Imprime Mensagem H. Extra                ³
	//³ MV_PAR16        //  Imprime Intervalo                        ³
	//³ MV_PAR17        //  Imprime Situa‡„o do Funcion rio          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	lPeriodo := .T.
	cProcesso := mv_par01			//Processo
	cPeriodo  := mv_par02			//Periodo
	cRoteiro  := mv_par03			//Roteiro
	cNumPagto := mv_par04			//Numero de Pagamento
	cDeptoDe  := mv_par05			//Depto De
	cFilDe    := mv_par06
	CcDe      := mv_par07
	TurDe     := mv_par08
	MatDe     := mv_par09
	NomDe     := mv_par10
	cSit      := mv_par11
	cCat      := mv_par12
	lCC       := If(mv_par13 == 1,.T.,.F.)
	lNMarc    := If(mv_par14 == 1,.T.,.F.)
	lHExtr    := If(mv_par15 == 1,.T.,.F.)
	lInter    := If(mv_par16 == 1,.T.,.F.)
	lSitfu    := If(mv_par17 == 1,.T.,.F.)
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ PN040RB - Perguntes por Data                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                         ³
	//³ MV_PAR01        //  Processo                                 ³
	//³ MV_PAR02        //  Data Inicial                             ³
	//³ MV_PAR03        //  Data Final                               ³
	//³ MV_PAR04        //  Departamento                             ³
	//³ MV_PAR05        //  Filial                                   ³
	//³ MV_PAR06        //  Centro de Custo                          ³
	//³ MV_PAR07        //  Turno                                    ³
	//³ MV_PAR08        //  Matricula                                ³
	//³ MV_PAR09        //  Nome                                     ³
	//³ MV_PAR10        //  Situacao                                 ³
	//³ MV_PAR11        //  Categoria                                ³
	//³ MV_PAR12        //  Imprime C.C em outra Pagina              ³
	//³ MV_PAR13        //  Imprime Mensagem No. Marca‡”es           ³
	//³ MV_PAR14        //  Imprime Mensagem H. Extra                ³
	//³ MV_PAR15        //  Imprime Intervalo                        ³
	//³ MV_PAR16        //  Imprime Situa‡„o do Funcion rio          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	lPeriodo := .F.
	cProcesso := mv_par01			
	dPerIni   := mv_par02
	dPerFim   := mv_par03
	cDeptoDe  := mv_par04			
	cFilDe    := mv_par05
	CcDe      := mv_par06
	TurDe     := mv_par07
	MatDe     := mv_par08
	NomDe     := mv_par09
	cSit      := mv_par10
	cCat      := mv_par11
	lCC       := If(mv_par12 == 1,.T.,.F.)
	lNMarc    := If(mv_par13 == 1,.T.,.F.)
	lHExtr    := If(mv_par14 == 1,.T.,.F.)
	lInter    := If(mv_par15 == 1,.T.,.F.)
	lSitfu    := If(mv_par16 == 1,.T.,.F.)
EndIf

SP4->(dbSetOrder(1))
SRA->(dbSetOrder(1))

If lPeriodo
	oPeriodo:cProcesso := cProcesso
	oPeriodo:cPeriodo  := cPeriodo
	oPeriodo:cRoteiro  := cRoteiro
	oPeriodo:cNumPagto := cNumPagto

	oPeriodo:GetPer()
	
	If !(oPeriodo:lFound )
		MsgInfo( OemToAnsi( oPeriodo:cMsgNotFoundPer ) )    //"Período de Apontamento não encontrado."
		Return
	EndIf
	
	dPerIni := oPeriodo:dDataIni
	dPerFim := oPeriodo:dDataFim
Else
	oPeriodo:dDataIni := dPerIni
	oPeriodo:dDataFim := dPerFim	
EndIf	

//-- Modifica variaveis para a Query
For nReg:=1 to Len(cSit)
	cSitQuery += "'"+Subs(cSit,nReg,1)+"'"
	If ( nReg+1 ) <= Len(cSit)
		cSitQuery += "," 
	Endif
Next nReg        
cSitQuery := "%" + cSitQuery + "%"

cCatQuery := ""
For nReg:=1 to Len(cCat)
	cCatQuery += "'"+Subs(cCat,nReg,1)+"'"
	If ( nReg+1 ) <= Len(cCat)
		cCatQuery += "," 
	Endif
Next nReg        
cCatQuery := "%" + cCatQuery + "%"

//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
MakeSqlExpr(cPerg)
	
BEGIN REPORT QUERY oSection

cFilSRACTT	:= "%" + FWJoinFilial("SRA", "CTT") + "%"
cFilSRASR6	:= "%" + FWJoinFilial("SRA", "SR6") + "%"	
cFilSRASQB	:= "%" + FWJoinFilial("SRA", "SQB") + "%"	
nOrdem		:= oSection:GetOrder()

If nOrdem == 1
	cOrdem := "%SRA.RA_FILIAL,SRA.RA_MAT%"
ElseIf nOrdem == 2
	cOrdem := "%SRA.RA_FILIAL,SRA.RA_CC,SRA.RA_MAT%"
ElseIf nOrdem == 3
	cOrdem := "%SRA.RA_FILIAL,SRA.RA_NOME,SRA.RA_MAT%"
ElseIf nOrdem == 4
	cOrdem := "%SRA.RA_FILIAL,SRA.RA_TNOTRAB%"
ElseIf nOrdem == 5
	cOrdem := "%SRA.RA_FILIAL,SRA.RA_CC,SRA.RA_NOME%"
ElseIf nOrdem == 6
	cOrdem := "%SRA.RA_FILIAL,SRA.RA_DEPTO,SRA.RA_MAT%"
Endif

cQryProcesso := "%(SRA.RA_DEMISSA = '          ' OR (SRA.RA_DEMISSA >= '"+DTOS(dPerIni) + "' AND  SRA.RA_DEMISSA <> '          '))"
If !Empty(cProcesso)
	cQryProcesso += " AND SRA.RA_PROCES = '" + cProcesso + "' "	
Endif
cQryProcesso += "%"
 
BeginSql alias cAliasQry
	SELECT SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_TNOTRAB, SRA.RA_DEMISSA,SRA.RA_ADMISSA,SRA.RA_SITFOLH,SRA.RA_CHAPA,
		SRA.RA_SEQTURN,SRA.RA_DEPTO, SRA.RA_PROCES,
		CTT.CTT_FILIAL, CTT.CTT_CUSTO, CTT.CTT_DESC01,
		SR6.R6_TURNO, SR6.R6_DESC,
		SQB.QB_DEPTO, SQB.QB_DESCRIC
	FROM %table:SRA% SRA
	INNER JOIN %table:CTT% CTT ON RA_CC = CTT_CUSTO AND CTT.%NotDel% AND %exp:cFilSRACTT%
	INNER JOIN %table:SR6% SR6 ON RA_TNOTRAB = R6_TURNO AND SR6.%NotDel% AND %exp:cFilSRASR6%
	INNER JOIN %table:SQB% SQB ON RA_DEPTO = QB_DEPTO AND SQB.%NotDel% AND %exp:cFilSRASQB%
	WHERE SRA.RA_SITFOLH	IN	(%exp:Upper(cSitQuery)%) 	AND
		  SRA.RA_CATFUNC	IN	(%exp:Upper(cCatQuery)%)	AND
	      %exp:cQryProcesso% AND 
 	      SRA.%notDel%   
	ORDER BY %exp:cOrdem%
EndSql

If ( nColPro == 1 ) 
	END REPORT QUERY oSection PARAM mv_par05,mv_par06,mv_par07,mv_par08,mv_par09,mv_par10
Else
	END REPORT QUERY oSection PARAM mv_par04,mv_par05,mv_par06,mv_par07,mv_par08,mv_par09
Endif

DEFINE BREAK oBreakFil OF oSection WHEN {|| (cAliasQry)->RA_FILIAL }  TITLE OemToansi(STR0001) 
oBreakFil:SetPageBreak(.T.)
oBreakFil:SetHeaderBreak(.T.) 

DEFINE BREAK oBreakFun OF oSection WHEN {|| (cAliasQry)->RA_MAT }  TITLE OemToansi(STR0001) 
oBreakFun:SetHeaderBreak(.T.) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Quebra de pagina C.Custo                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (nOrdem = 2 .or. nOrdem = 5) 
	DEFINE BREAK oBreakCc OF oSection WHEN {|| (cAliasQry)->RA_CC } TITLE OemToansi(STR0001) 
	If lCC 
		oBreakCc:SetPageBreak(.T.) 
	Endif 
ElseIf (nOrdem = 4) 
	DEFINE BREAK oBreakCc OF oSection WHEN {|| (cAliasQry)->RA_TNOTRAB } TITLE OemToansi(STR0001) 
	If lCC 
		oBreakCc:SetPageBreak(.T.) 
	Endif 
Endif 
oSection:SetHeaderSection(.T.)
oSection:SetHeaderBreak(.T.) 

oSection:lHeaderPage:=.F. 
oSection1:lHeaderPage:=.F. 

oReport:OnPageBreak( {|| oReport:lOnPageBreak:= .F.,;
							If(oSection1:lPrinting,;
							(oSection:PrintLine(),	oReport:SkipLine());
							,);
					 },.F. )

//-- Carrega C¢digos de Hora Extra N„o Autorizada
cKey  := ''
aHENA := {}
If SP4->(dbSeek(fFilFunc('SP4') + Space(Len((cAliasQry)->RA_TNOTRAB)), .F.)) .Or. ;
	SP4->(dbSeek(fFilFunc('SP4') + (cAliasQry)->RA_TNOTRAB, .F.)) .Or. ;
	SP4->(dbSeek(Space(Len((cAliasQry)->RA_FILIAL)) + (cAliasQry)->RA_TNOTRAB, .F.)) .Or. ;
	SP4->(dbSeek(Space(Len((cAliasQry)->RA_FILIAL)) + Space(Len((cAliasQry)->RA_TNOTRAB)), .F.))
	cKey := SP4->P4_FILIAL + SP4->P4_TURNO
	While !SP4->(Eof()) .And. SP4->P4_FILIAL + SP4->P4_TURNO == cKey
		If Len(aHena) == 0 .Or. aScan(aHENA, SP4->P4_CODNAUT) == 0
			aAdd(aHENA, SP4->P4_CODNAUT)
		EndIf	
		SP4->(dbSkip())
	EndDo								
EndIf

cFilAnte	:= (cAliasQry)->RA_FILIAL
cTnoAnt		:= (cAliasQry)->RA_TNOTRAB
nTolInt 	:= __Min2Hrs( ( SuperGetMv("MV_DIVTINT",NIL,0, (cAliasQry)->RA_FILIAL) ) )   //-- Minutos para Tolerancia de Intervalo

//-- Define o total da regua da tela de processamento do relatorio
oReport:SetMeter( 100 )  
                                                 
//-- Incializa impressao   
oSection:Init(.F.)                              

While !(cAliasQry)->( EOF() ) 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Movimenta Regua de Processamento                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:IncMeter( 1 )   

	//-- Verifica se o usuário cancelou a impressão do relatorio
	If oReport:Cancel()
		Exit
	EndIf               

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Posiciona o registro da query na tabela de Funcionarios                ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If !SRA->(dbSeek( (cAliasQry)->RA_FILIAL + (cAliasQry)->RA_MAT ))
		(cAliasQry)->(DbSkip())
	   	Loop
	Endif

	//-- Processa quebra de Filial.
	If (cAliasQry)->RA_FILIAL != cFilAnte 
	    //--Somente "Zera" as variaveis se jah foi impresso algo para nao pula 
	    //--de pagina na primeira vez

		cFilAnte := (cAliasQry)->RA_FILIAL
   		cTnoAnt := (cAliasQry)->RA_TNOTRAB
		nTolInt := __Min2Hrs( ( SuperGetMv("MV_DIVTINT",NIL,0, (cAliasQry)->RA_FILIAL) ) )   //-- Minutos para Tolerancia de Intervalo
		
		//-- Carrega C¢digos de Hora Extra N„o Autorizada (Filial)
		cKey  := ''
		If SP4->(dbSeek(fFilFunc('SP4') + Space(Len((cAliasQry)->RA_TNOTRAB)), .F.)) .Or. ;
			SP4->(dbSeek(fFilFunc('SP4') + (cAliasQry)->RA_TNOTRAB, .F.)) .Or. ;
			SP4->(dbSeek(Space(Len((cAliasQry)->RA_FILIAL)) + (cAliasQry)->RA_TNOTRAB, .F.)) .Or. ;
			SP4->(dbSeek(Space(Len((cAliasQry)->RA_FILIAL)) + Space(Len((cAliasQry)->RA_TNOTRAB)), .F.))
			cKey := SP4->P4_FILIAL + SP4->P4_TURNO
			While !SP4->(Eof()) .And. SP4->P4_FILIAL + SP4->P4_TURNO == cKey
				If Len(aHena) == 0 .Or. aScan(aHENA, SP4->P4_CODNAUT) == 0
					aAdd(aHENA, SP4->P4_CODNAUT)
				EndIf	
				SP4->(dbSkip())
			EndDo								
		EndIf
	Endif

	If cFilAnte + cTnoAnt <> (cAliasQry)->RA_FILIAL +(cAliasQry)->RA_TNOTRAB
		cFilAnte	:= (cAliasQry)->RA_FILIAL
		cTnoAnt		:= (cAliasQry)->RA_TNOTRAB

		//-- Carrega C¢digos de Hora Extra N„o Autorizada (Turno)
		cKey  := ''
		If SP4->(dbSeek(fFilFunc('SP4') + Space(Len((cAliasQry)->RA_TNOTRAB)), .F.)) .Or. ;
			SP4->(dbSeek(fFilFunc('SP4') + (cAliasQry)->RA_TNOTRAB, .F.)) .Or. ;
			SP4->(dbSeek(Space(Len((cAliasQry)->RA_FILIAL)) + (cAliasQry)->RA_TNOTRAB, .F.)) .Or. ;
			SP4->(dbSeek(Space(Len((cAliasQry)->RA_FILIAL)) + Space(Len((cAliasQry)->RA_TNOTRAB)), .F.))
			cKey := SP4->P4_FILIAL + SP4->P4_TURNO
			While !SP4->(Eof()) .And. SP4->P4_FILIAL + SP4->P4_TURNO == cKey
				If Len(aHena) == 0 .Or. aScan(aHENA, SP4->P4_CODNAUT) == 0
					aAdd(aHENA, SP4->P4_CODNAUT)
				EndIf	
				SP4->(dbSkip())
			EndDo								
		EndIf
	Endif

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Consiste Filiais e Acessos                                             ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF !( (cAliasQry)->RA_FILIAL $ fValidFil() ) .or. !Eval( cAcessaSRA )
		(cAliasQry)->(DbSkip())
	   	Loop
	EndIF

	//-- Reinicializa Variaveis do Funcionario
	aTabCalend	:= {} 
	aTurnos		:= {}
	aMarcacoes	:= {}

	//-- Carrega as Marcacoes do Periodo
	IF !GetMarcacoes( @aMarcacoes		,;	//Marcacoes dos Funcionarios
					  @aTabCalend		,;	//Calendario de Marcacoes
					  @aTabPadrao		,;	//Tabela Padrao
					  @aTurnos			,;	//Turnos de Trabalho
					  dPerIni 			,;	//Periodo Inicial
					  dPerFim			,;	//Periodo Final
					  (cAliasQry)->RA_FILIAL	,;	//Filial
					  (cAliasQry)->RA_MAT		,;	//Matricula
					  (cAliasQry)->RA_TNOTRAB	,;	//Turno
					  (cAliasQry)->RA_SEQTURN	,;	//Sequencia de Turno
					  (cAliasQry)->RA_CC		,;	//Centro de Custo
					  "SP8"				,;	//Alias para Carga das Marcacoes
					  .F.    			,;	//Se carrega Recno em aMarcacoes
					  .T.      			,;	//Se considera Apenas Ordenadas
				      .T. 				,;	//Se Verifica as Folgas Automaticas
				  	  .F.    			,;	//Se Grava Evento de Folga Automatica Periodo Anterior
  				  	  NIL				,;
					  NIL				,;
					  NIL				,;
					  .F.				,;
					  NIL				,;
					  .T.				,;  //Se considera data de apontamento
					  .T.				;	//Se considera data inicial				  	  
					)
		(cAliasQry)->(dbSkip())
		Loop
    EndIF

	aNewMarc := {}
	nLenMarc := Len( aMarcacoes )
	For nX := 1 To nLenMarc
		IF ( cOrdem := aMarcacoes[ nX , 03 ] ) == "ZZ"
			Loop
		EndIF	
		dDataApo := aMarcacoes[ nX , AMARC_DATAAPO ]
		aAdd( aNewMarc , {} )
		For nY := nX To nLenMarc
			IF aMarcacoes[ nY , AMARC_DATAAPO ] == dDataApo .and. aMarcacoes[ nY , 03 ] != "ZZ"
				aAdd( aNewMarc[Len(aNewMarc)] , aClone( aMarcacoes[ nY ] ) )
				aMarcacoes[ nY , 03 ] := "ZZ"
			Else
				Exit
			EndIF
		Next nY
	Next nX

	aMarcacoes := aClone( aNewMarc )
	
	//-- Monta o Array aImp com as ocorrˆncias do per¡odo
	aImp := {}
	nLenMarc := Len(aMarcacoes)
	nLenCalend:=Len(aTabCalend)
	
	For nX := 1 to nLenMarc
		dData  := aMarcacoes[nX, 1, 1]
		dDataApo := aMarcacoes[nX,1,AMARC_DATAAPO]
	    
	    //-- Limita as Marcacoes ao Periodo Solicitado
	    dDataOk := If( ( nPos1E := aScan(aTabCalend, { |x| x[CALEND_POS_DATA_APO] == dDataApo .And. x[4] == '1E' }) ) > 0, aTabCalend[nPos1E,1], dData)
	    If Abs(dData - dDataOk) > 2  .OR. ;
	       (Abs(dData - dDataOk) <= 2 .And. dDataOk < dPerIni) .Or. ;
		   (Abs(dData - dDataOk) <= 2 .And. dDataOk > dPerFim)
	       Loop
		EndIf	
		
	  	//--Se Existe Calendario para a Ordem Obtem Numero de Marcacoes Possiveis
	    If nPos1E>0  
		   	        
			//-- Calcula a Quantidade de Marcacoes possiveis de acordo com o 
			//--Calendario do Funcionario
	        nTabMarc:=0
	        For nPosOrdem:=nPos1E to nLenCalend
	           If aTabCalend[nPosOrdem,CALEND_POS_DATA_APO] <> dDataApo
	              Exit
	           Endif
	           nTabMarc++
	        Next nPosOrdem
	    Endif
	    
		//-- Procura por Excecoes na Data
		cTraba := ''
		nQuant := 0
		aIntervalo := {}
		If nPos1E > 0
			cTraba := aTabCalend[nPos1E, 6]
		  	//-- Corre Todas as Marcacoes realizadas de acordo com a DataApo
			For nW := nPos1E to nLenCalend
				If aTabCalend[nW,CALEND_POS_DATA_APO] <> dDataApo
              		Exit
           		Endif
				nQuant ++                                                
				//-- Se segundo a Tabela Padrao Existir Intervalo
				//-- Armazena-o para posterior verificacao
				If aTabCalend[nW,9] > 0
				   aAdd(aIntervalo, { aTabCalend[nW,9],aTabCalend[nW,4] })
				Endif
			Next nW
		EndIf

		//-- Reinicializa Variaveis
		dDtAfas  := CtoD('  /  /  ')
		dDtRet   := CtoD('  /  /  ')
		cTipAfas := ''
		aMotivo  := {}

		If nPos1E == 0
			aAdd(aMotivo, STR0015 ) // '** Nao existe Calendario           '
		EndIf	

		//-- MSG n£mero de Marca‡”es
		If lNMarc						
			//-- Marca‡”es Impares
			If Len(aMarcacoes[nX])%2 > 0
				aAdd(aMotivo, STR0016 ) // '** Numero de Marcacoes Impar       '
			EndIf

			//-- Menos ou Mais marca‡”es que o programado
			If Len(aMarcacoes[nX]) < nQuant
				aAdd(aMotivo, STR0017 ) // '** Menos Marcacoes que o Programado'
			ElseIf Len(aMarcacoes[nX]) > nQuant .And. nQuant > 0
				aAdd(aMotivo, STR0018 ) // '** Mais Marcacoes que o Programado '
			EndIf	
		EndIf

		//-- MSG Hora Extra n„o autorizada
		If lHExtr .And. Len(aHENA) > 0
			For nY := 1 to Len(aHENA)
				If SPC->(dbSeek(fFilFunc('SPC')+(cAliasQry)->RA_MAT+aHENA[nY]+DtoS(dData), .F.))
					aAdd(aMotivo, STR0019 ) // '** Hora Extra Nao Autorizada       '
					Exit
				EndIf
			Next nY
		EndIf

		//-- MSG Intervalo
		If lInter
			//-- Intervalo Menor ou Maior que o Programado
			nW := 0
			For nW := 1 To Len(aIntervalo)
				nIntReal := 0
				nIntProg := 0 
				nHrReal	 := 0
				//-- Obtem o numero de horas/minutos de intervalo

				//-- Localiza o numero da posicao da marcacao pela sigla.
				nPosInt := Ascan(aSiglaMarc, { |x| x == aIntervalo[nW,2] })
	
				lIntMaior:=lIntMenor:= .F.		 				
 				If nPosInt+1 <= Len(aMarcacoes[nX])

					If  Empty(nTolInt)
						nIntReal := fDHtoNS(aMarcacoes[nX, nPosInt+1, 1], aMarcacoes[nX, nPosInt+1, 2]) - fDHtoNS(aMarcacoes[nX, nPosInt, 1], aMarcacoes[nX, nPosInt, 2])
						nIntProg := fDhToNs(,aIntervalo[nW,1])
						lIntMenor:= (nIntReal) < (nIntProg)  
						lIntMaior:=	(nIntReal) > (nIntProg)
                    Else 
	               	   
                    	CalcHours(  aMarcacoes[nX, nPosInt+1, 1]		,;	//01 -> Data 1
									aMarcacoes[nX, nPosInt+1, 2]		,;	//02 -> Hora 1
									aMarcacoes[nX, nPosInt, 1]			,;	//03 -> Data 2
									aMarcacoes[nX, nPosInt, 2]			,;	//04 -> Hora 2
									@nHrReal							,;	//05 -> <@>Horas Normais Apontadas
									NIL									,;	//06 -> <@>Horas Noturnas Apontadas
									.F.									;	//07 -> Apontar Horas Noturnas
								 )  
						If nHrReal > SomaHoras(aIntervalo[nW,1],nTolInt)
							lIntMaior:=.T.
						ElseIf aIntervalo[nW,1] > SomaHoras(nHrReal,nTolInt)
	 						lIntMenor:=.T.
						Endif							 
					Endif
						
				Endif

				If lIntMenor
				   	aAdd(aMotivo, '** '+Trim(Str(nW,2))+'o.'+STR0020) // ' Intervalo Menor que o Programado'
				ElseIf   lIntMaior
					aAdd(aMotivo, '** '+Trim(Str(nW,2))+'o.'+STR0021) // ' Intervalo Maior que o Programado'
				EndIf
			Next nW
		EndIf

		//-- MSG Situa‡„o do Funcion rio
		If lSitFu
			//-- Marca‡”es Anteriores a Admiss„o ou Posteriores a Demiss„o
			If !Empty(fDtaAdmis()) .And. dData < fDtaAdmis()
				aAdd(aMotivo, STR0022 ) // '** Marcacoes antes da Admisssao    '
			ElseIf !Empty((cAliasQry)->RA_DEMISSA) .And. dData > (cAliasQry)->RA_DEMISSA
				aAdd(aMotivo, STR0023 ) // '** Marcacoes apos Demissao         '
			EndIf

			//-- Marca‡”es durante Ferias ou Afastamentos
			If fAfasta( (cAliasQry)->RA_FILIAL,(cAliasQry)->RA_Mat,dData,@dDtafas,@dDtRet,@cTipAfas) .And. aTabCalend[nPos1E,10] # 'E'
				If cTipAfas == 'F'
					aAdd(aMotivo, STR0024 ) // '** Marcacoes durante as Ferias     '
				Else
					aAdd(aMotivo, STR0025 ) // '** Marcacoes durante Afastamento   '
				EndIf
			EndIf		
		EndIf

		cProc := aMarcacoes[nX , 01 , AMARC_PROCESSO ]
		cPer  := aMarcacoes[nX , 01 , AMARC_PERIODO  ]
		cRot  := aMarcacoes[nX , 01 , AMARC_ROTEIRO  ]
		cNPag := aMarcacoes[nX , 01 , AMARC_NUM_PAGTO]

		If Len(aMotivo) > 0
			cDet := ""
			For nZ := 1 to Len(aMarcacoes[nX])
				cDet += StrZero(aMarcacoes[nX, nZ, 2], 5, 2) + Space(1)
			Next nZ
			cDet := Left(cDet,80)
			aAdd(aImp, { Subs(Dtos(dData),7,2)+'/' + Subs(Dtos(dData),5,2), cDet, "",cProc, cPer, cRot, cNPag } )
			For nZ := 1 to Len(aMotivo)
				If nZ == 1
					aImp[Len(aImp),3] += aMotivo[nZ]
				Else	
					aAdd(aImp, { Space(1), Space(1), aMotivo[nZ], Space(1), Space(1), Space(1), Space(1) })
				EndIf
			Next nZ

		EndIf
	Next nX

	// Verifica se não carregou as marcações e salta para o proximo funcionario
	If Len(aImp) == 0
		(cAliasQry)->(DbSkip())
		Loop
	Endif

	oSection:PrintLine()
	oReport:SkipLine()
	oSection1:Init(.F.) 
	oSection1:PrintHeader() 
	
	For nX := 1 to Len( aImp )

		//-- Altera valores da Section
		oSection1:Cell( "DATA"):SetValue(aImp[nX,1])
		oSection1:Cell( "MARCACOES"):SetValue(aImp[nX,2])
		oSection1:Cell( "OCORRENCIAS"):SetValue(aImp[nX,3])
		oSection1:Cell( "PROCESSO"):SetValue(aImp[nX,4])
		oSection1:Cell( "PERIODO"):SetValue(aImp[nX,5])
		oSection1:Cell( "ROTEIRO"):SetValue(aImp[nX,6])
		oSection1:Cell( "NUMPAGTO"):SetValue(aImp[nX,7])

		//-- Imprime os conteudos da Section
	   	oSection1:PrintLine()
	Next

	oSection1:Finish()
	oReport:ThinLine()
	oReport:SkipLine()		

	(cAliasQry)->(DbSkip())

EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Termino do Relatorio                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza impressao inicializada pelo metodo Init             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSection:Finish()

If Select(cAliasQry) > 0
	(cAliasQry)->(dbCloseArea())
Endif

Return



//======>>>>>>>>>> DAQUI PARA BAIXO, TRATA-SE DO RELEASE 3
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ PONR040  ³ Autor ³ R.H. - J. Ricardo     ³ Data ³ 10.04.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Divergencias nas marcacoes                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ PONR040(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Mauricio MR ³17/10/01³010812³Correcao Impressao Nome Func Completo e   ³±± 
±±³            ³        ³      ³de Todas as Ocorrencias Qdo quebra de Pag.³±±
±±³Mauricio MR ³01/11/01³      ³Calcula Qtde de Marcacoes no Dia de Acordo³±± 
±±³            ³        ³      ³com o calendario do funcionario para veri-³±± 
±±³            ³        ³      ³ficar numero de Marcacoes Efetuadas.      ³±± 
±±³Mauricio MR ³05/11/01³      ³Quebra de Turno Corrigida.                ³±±   
±±³Marinaldo   ³19/11/01³Melhor³A funcao GetMarcacoes passara a utilizar a³±±
±±³            ³--------³------³Funcao fDiasFolga() para Verificar as  Fol³±±
±±³            ³--------³------³gas Automaticas.						  ³±±
±±³Mauricio MR ³19/11/01³      ³Correcao Calculo de Marcacoes p/ H.Noturno³±±
±±³            ³        ³      ³Limita a verificacao de Divergencias ao   ³±±  
±±³            ³        ³      ³periodo solicitado.                       ³±±  
±±³Mauricio MR ³02/01/02³Acerto³Verificacao da existencia de Ordem Valida ³±± 
±±³            ³        ³      ³para calculo do no. de Marcacoes segundo  ³±± 
±±³            ³        ³      ³a TabCalend.                              ³±±  
±±³ Priscila R.³04/06/02³------³Ajuste no relatorio para que seja impresso³±± 
±±³            ³--------³------³corretamente o C.C no tamanho 20.         ³±± 
±±³Mauricio MR ³07/06/02³Acerto³Chamada a funcao correta DescCC().        ³±±  
±±³Mauricio MR ³04/12/02³------³Retirada a restricao de existencia de cra-³±± 
±±³            ³--------³------³cha para a emissao do relatorio.	      ³±±   
±±³=======================================================================³±± 
±±³                         *** Versao 9.12 ***                           ³±± 
±±³=======================================================================³±± 
±±³Leandro Dr  ³24/03/08³Melhor³Inclusão de chave processo/periodo/roteiro³±± 
±±³            |        |      |NumPagto. Impressao por data ou periodo   ³±± 
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function PONR040R3()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Locais (Basicas)                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cDesc1  := STR0001  // 'Divergencias nas Marcacoes'
Local cDesc2  := STR0002  // 'Ser  impresso de acordo com os parametros solicitados pelo'
Local cDesc3  := STR0003  // 'usuario.'
Local cString := 'SRA' //-- Alias do arquivo principal (Base)
Local aOrd    := {STR0004 , STR0005 , STR0006 , STR0007 , STR0030 , STR0034 } // 'Matricula'###'Centro de Custo'###'Nome'###'Turno'###'C.Custo+Nome'###'Departamento'
Local wnRel

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis PRIVATE(Basicas)                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aReturn    := { STR0008 , 1, STR0009 , 2, 2, 1, '',1 } // 'Zebrado'###'Administra‡„o'
Private nomeprog   := 'PONR040'
Private aLinha     := {}
Private nLastKey   := 0
Private cPerg      := ''
Private aTabPadrao := {}
Private nPagAtu    := 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis Utilizadas na funcao IMPR                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private Titulo   := OemToAnsi(STR0001 ) // 'Divergencias nas Marcacoes'
Private cCabec   := ''
Private AT_PRG   := 'PONR040'
Private wCabec0  := 1
Private wCabec1  := STR0011  // 'Data   Marcacoes                                                                       Ocorrencia'
Private CONTFL   := 1
Private LI       := 0 
Private nTamanho := 'M'
Private lRoda	 := .F.  

Private cProcesso := Nil
Private cPeriodo  := Nil
Private cRoteiro  := Nil
Private cNumPagto := Nil
Private cDeptoDe
Private cDeptoAte
Private dPerIni   := CtoD("//")
Private dPerFim   := CtoD("//")
Private lPeriodo  := .F.
Private nColPro
Private oPeriodo  := RHPERIODO():New()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis Private(Programa)                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private nOrdem

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Verifica se deve imprimir por periodo ou datas			   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If fOpcRadio(@nColPro, STR0001,;      //"Relatorio de Divergencias"
					   STR0035,;      //"Selecione a opcao de impressao:"
			   		   STR0036,;      //"Por Periodo"
			   		   STR0037 ) == 0 //"Por Datas"
	Return
EndIf
	
cPerg := IF(nColPro == 1,"PNR040A","PNR040B")	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(cPerg,.F.)

If ( nColPro == 1 ) 
	cProcesso := mv_par01			//Processo
	cPeriodo  := mv_par02			//Periodo
	cRoteiro  := mv_par03			//Roteiro
	cNumPagto := mv_par04			//Numero de Pagamento
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel := 'PONR040' //-- Nome Default do relatorio em Disco
wnrel := SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,nTamanho)

nOrdem   := aReturn[8]

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carregando variaveis mv_par?? para Variaveis do Sistema.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( nColPro == 1 ) 
	lPeriodo := .T.
	cProcesso := mv_par01			//Processo
	cPeriodo  := mv_par02			//Periodo
	cRoteiro  := mv_par03			//Roteiro
	cNumPagto := mv_par04			//Numero de Pagamento
	cDeptoDe  := mv_par05			//Depto De
	cDeptoAte := mv_par06			//Depto Ate	
	cFilDe    := mv_par07
	cFilAte   := mv_par08
	CcDe      := mv_par09
	CcAte     := mv_par10
	TurDe     := mv_par11
	TurAte    := mv_par12
	MatDe     := mv_par13
	MatAte    := mv_par14
	NomDe     := mv_par15
	NomAte    := mv_par16
	cSit      := mv_par17
	cCat      := mv_par18
	lCC       := If(mv_par19 == 1,.T.,.F.)
	lNMarc    := If(mv_par20 == 1,.T.,.F.)
	lHExtr    := If(mv_par21 == 1,.T.,.F.)
	lInter    := If(mv_par22 == 1,.T.,.F.)
	lSitfu    := If(mv_par23 == 1,.T.,.F.)
Else
	lPeriodo := .F.
	cProcesso := mv_par01			//Processo
	dPerIni   := mv_par02
	dPerFim   := mv_par03
	cDeptoDe  := mv_par04			//Depto De
	cDeptoAte := mv_par05			//Depto Ate
	cFilDe    := mv_par06
	cFilAte   := mv_par07
	CcDe      := mv_par08
	CcAte     := mv_par09
	TurDe     := mv_par10
	TurAte    := mv_par11
	MatDe     := mv_par12
	MatAte    := mv_par13
	NomDe     := mv_par14
	NomAte    := mv_par15
	cSit      := mv_par16
	cCat      := mv_par17
	lCC       := If(mv_par18 == 1,.T.,.F.)
	lNMarc    := If(mv_par19 == 1,.T.,.F.)
	lHExtr    := If(mv_par20 == 1,.T.,.F.)
	lInter    := If(mv_par21 == 1,.T.,.F.)
	lSitfu    := If(mv_par22 == 1,.T.,.F.)
EndIf

If	nLastKey == 27
	Return( NIL )
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
	Return( NIL )
Endif

If lPeriodo
	oPeriodo:cProcesso := cProcesso
	oPeriodo:cPeriodo  := cPeriodo
	oPeriodo:cRoteiro  := cRoteiro
	oPeriodo:cNumPagto := cNumPagto

	oPeriodo:GetPer()
	
	If !(oPeriodo:lFound )
		MsgInfo( OemToAnsi( oPeriodo:cMsgNotFoundPer ) )    //"Período de Apontamento não encontrado."
		Return
	EndIf
	
	dPerIni := oPeriodo:dDataIni
	dPerFim := oPeriodo:dDataFim
Else
	oPeriodo:dDataIni := dPerIni
	oPeriodo:dDataFim := dPerFim	
EndIf	

cCabec := STR0001  // 'Divergencias nas Marcacoes'

Titulo := OemToAnsi(cCabec)
RptStatus({|lEnd| PO040Imp(@lEnd,wnRel,cString)},Titulo)

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ PO040Imp ³ Autor ³ R.H. - J. Ricardo     ³ Data ³ 10.04.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Divergencia nas Marcac”es                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ PO040Imp(lEnd,wnRel,cString)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd        - A‡Æo do Codelock                             ³±±
±±³          ³ wnRel       - T¡tulo do relat¢rio                          ³±±
±±³          ³ cString     - Mensagem                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PO040Imp(lEnd,WnRel,cString)

//-- Variaveis Locais
* Caracter
Local cKey       := ''
Local cDet       := ''
Local cFil       := ''  
Local cTno       := ''
Local cSeqAnt    := '  '
Local cOrdem     := ''
Local cChave     := ''
Local cProc		 := ''
Local cPer       := ''
Local cRot		 := ''
Local cNPag		 := ''
Local dDataApo   := CtoD("//")
Local cTraba     := '' 
Local cAcessaSRA := &("{ || " + ChkRH("PONR040","SRA","2") + "}")
* Data
Local dDataOk    := CtoD('  /  /  ')
Local dData      := CtoD('  /  /  ')
Local dData1     := CtoD('  /  /  ')
Local dData2     := CtoD('  /  /  ')
Local dData3     := CtoD('  /  /  ')
Local dData4     := CtoD('  /  /  ')
* Array
Local aTabCalend := {}
Local aNewMarc	 := {}
Local aMarcacoes := {}
Local aMotivo    := {}
Local aTurnos    := {}
Local aHENA      := {}
Local aIntervalo := {}
Local aSiglaMarc := {}

* L¢gico
Local lCabec     := .F.
Local lCabecCC   := .F. 
Local lIntMaior	 := .F.
Local lIntMenor  := .F.
* N£m‚rico
Local nX         := 0
Local nY         := 0
Local nZ         := 0
Local nQuant     := 0   
Local nHrReal	 := 0
Local nIntProg   := 0
Local nIntReal   := 0
Local nPos1E     := 0
Local nW         := 0
Local nLenMarc	 := 0
Local nLenCalend := 0
Local nPosOrdem	 := 0  
Local nTolInt	 := 0

Private cFilAnte   := ''
Private cTnoAnt    := ''
Private cCcAnt     := ''

TabMarc('SPJ',@aSiglaMarc)

dbSelectArea('SRA')
dbGoTop()
dbSetOrder(nOrdem)
If nOrdem == 1
	dbSeek(cFilDe + MatDe,.T.)
	cInicio := 'SRA->RA_FILIAL + SRA->RA_MAT'
	cFim    := cFilAte + MatAte
ElseIf nOrdem == 2
	dbSeek(cFilDe + CcDe + MatDe,.T.)
	cInicio := 'SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_MAT'
	cFim    := cFilAte + CcAte + MatAte
ElseIf nOrdem == 3
	dbSeek(cFilDe + NomDe + MatDe,.T.)
	cInicio := 'SRA->RA_FILIAL + SRA->RA_NOME + SRA->RA_MAT'
	cFim    := cFilAte + NomAte + MatAte
ElseIf nOrdem == 4
	dbSeek(cFilDe + TurDe,.T.)
	cInicio := 'SRA->RA_FILIAL + SRA->RA_TNOTRAB'
	cFim    := cFilAte + TurAte
ElseIf nOrdem == 5
	dbSetOrder(8)
	dbSeek(cFilDe + CcDe + NomDe,.T.)
	cInicio  := 'SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_NOME'
	cFim     := cFilAte + CcAte + NomAte
ElseIf nOrdem == 6
	dbSetOrder(22)
	dbSeek(cFilDe + cDeptoDe + MatDe,.T.)
	cInicio  := 'SRA->RA_FILIAL + SRA->RA_DEPTO + SRA->RA_MAT'
	cFim     := cFilAte + cDeptoAte + MatAte	
Endif

//-- Carrega C¢digos de Hora Extra N„o Autorizada
cKey  := ''
aHENA := {}
If SP4->(dbSeek(fFilFunc('SP4') + Space(Len(SRA->RA_TNOTRAB)), .F.)) .Or. ;
	SP4->(dbSeek(fFilFunc('SP4') + SRA->RA_TNOTRAB, .F.)) .Or. ;
	SP4->(dbSeek(Space(Len(SRA->RA_FILIAL)) + SRA->RA_TNOTRAB, .F.)) .Or. ;
	SP4->(dbSeek(Space(Len(SRA->RA_FILIAL)) + Space(Len(SRA->RA_TNOTRAB)), .F.))
	cKey := SP4->P4_FILIAL + SP4->P4_TURNO
	While !SP4->(Eof()) .And. SP4->P4_FILIAL + SP4->P4_TURNO == cKey
		If Len(aHena) == 0 .Or. aScan(aHENA, SP4->P4_CODNAUT) == 0
			aAdd(aHENA, SP4->P4_CODNAUT)
		EndIf	
		SP4->(dbSkip())
	EndDo								
EndIf

SetRegua(SRA->(RecCount()))

While SRA->(!Eof()) .And. &cInicio <= cFim
	IncRegua()

	//-- Cancela, caso pressionado ALT+A
	If lEnd
		Impr(cCancela,'C')
		Exit
	EndIF

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Consiste controle de acessos e filiais validas               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SRA->( !(RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA) )
		fCabTotal()
		Loop
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Consiste Parametrizacao do Intervalo de Impressao            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IF SRA->(;
				(  RA_DEMISSA   < dPerIni .and. !Empty(RA_DEMISSA)		 ) .or. ;
				(  RA_TNOTRAB	< Turde ) .or. ( 	RA_TNOTRAB	> TurAte ) .or. ;
				(  RA_NOME 		< NomDe ) .or. ( 	RA_NOME 	> NomAte ) .or. ;
				(  RA_MAT 		< MatDe ) .or. ( 	RA_MAT 		> MatAte ) .or. ;
				(  RA_CC 		< CCDe  ) .or. ( 	RA_CC 		> CCAte	 ) .or. ;
				(  RA_DEPTO		< cDeptoDe) .or. ( 	RA_DEPTO	> cDeptoAte	 );
			)
		fCabTotal()
		Loop
	EndIF	

	If !Empty(cProcesso)
		If SRA->RA_PROCES <> cProcesso
			fCabTotal()
			Loop
		EndIf
	EndIf

	//-- Processa a Quebra de Filial
	If SRA->RA_FILIAL # cFilAnte
		 //--Somente "Zera" as veriaveis se jah houve impressao anterior
	    //-- Pois existe tratamento posterior em que eh verificado se algumas dessas
	    //-- variaveis estao em branco para nao pular pagina se ainda nao foi nada impresso
	    If !Empty(cTnoAnt)
	   		cTnoAnt := 'úúú'
			cSeqAnt := 'úú'
			cCcAnt  := '!!!!!!!!!'
		Endif
		//-- Carrega C¢digos de Hora Extra N„o Autorizada (Filial)
		cKey  := ''
		If SP4->(dbSeek(fFilFunc('SP4') + Space(Len(SRA->RA_TNOTRAB)), .F.)) .Or. ;
			SP4->(dbSeek(fFilFunc('SP4') + SRA->RA_TNOTRAB, .F.)) .Or. ;
			SP4->(dbSeek(Space(Len(SRA->RA_FILIAL)) + SRA->RA_TNOTRAB, .F.)) .Or. ;
			SP4->(dbSeek(Space(Len(SRA->RA_FILIAL)) + Space(Len(SRA->RA_TNOTRAB)), .F.))
			cKey := SP4->P4_FILIAL + SP4->P4_TURNO
			While !SP4->(Eof()) .And. SP4->P4_FILIAL + SP4->P4_TURNO == cKey
				If Len(aHena) == 0 .Or. aScan(aHENA, SP4->P4_CODNAUT) == 0
					aAdd(aHENA, SP4->P4_CODNAUT)
				EndIf	
				SP4->(dbSkip())
			EndDo								
		EndIf
		nTolInt := __Min2Hrs( ( SuperGetMv("MV_DIVTINT",NIL,0, cFilAnte) ) )   //-- Minutos para Tolerancia de Intervalo
	Endif

	//-- Processa a Quebra de Turno
	If cTnoAnt+cSeqAnt # SRA->RA_TNOTRAB+SRA->RA_SEQTURN
		If !Empty(cTnoAnt)
	   		cTnoAnt := 'úúú'
	   	Endif	
		//-- Carrega C¢digos de Hora Extra N„o Autorizada (Turno)
		cKey  := ''
		If SP4->(dbSeek(fFilFunc('SP4') + Space(Len(SRA->RA_TNOTRAB)), .F.)) .Or. ;
			SP4->(dbSeek(fFilFunc('SP4') + SRA->RA_TNOTRAB, .F.)) .Or. ;
			SP4->(dbSeek(Space(Len(SRA->RA_FILIAL)) + SRA->RA_TNOTRAB, .F.)) .Or. ;
			SP4->(dbSeek(Space(Len(SRA->RA_FILIAL)) + Space(Len(SRA->RA_TNOTRAB)), .F.))
			cKey := SP4->P4_FILIAL + SP4->P4_TURNO
			While !SP4->(Eof()) .And. SP4->P4_FILIAL + SP4->P4_TURNO == cKey
				If Len(aHena) == 0 .Or. aScan(aHENA, SP4->P4_CODNAUT) == 0
					aAdd(aHENA, SP4->P4_CODNAUT)
				EndIf	
				SP4->(dbSkip())
			EndDo								
		EndIf

		cSeqAnt    := SRA->RA_SEQTURN
	Endif

	aMarcacoes := {}
	aTabCalend := {}
	aTurnos    := {}		

	//-- Carrega as Marcacoes do Periodo
	IF !GetMarcacoes( @aMarcacoes		,;	//Marcacoes dos Funcionarios
					  @aTabCalend		,;	//Calendario de Marcacoes
					  @aTabPadrao		,;	//Tabela Padrao
					  @aTurnos			,;	//Turnos de Trabalho
					  dPerIni 			,;	//Periodo Inicial
					  dPerFim			,;	//Periodo Final
					  SRA->RA_FILIAL	,;	//Filial
					  SRA->RA_MAT		,;	//Matricula
					  SRA->RA_TNOTRAB	,;	//Turno
					  SRA->RA_SEQTURN	,;	//Sequencia de Turno
					  SRA->RA_CC		,;	//Centro de Custo
					  "SP8"				,;	//Alias para Carga das Marcacoes
					  .F.    			,;	//Se carrega Recno em aMarcacoes
					  .T.      			,;	//Se considera Apenas Ordenadas
				      .T. 				,;	//Se Verifica as Folgas Automaticas
				  	  .F.    			,;	//Se Grava Evento de Folga Automatica Periodo Anterior
  				  	  NIL				,;
					  NIL				,;
					  NIL				,;
					  .F.				,;
					  NIL				,;
					  .T.				,;  //Se considera data de apontamento
					  .T.				;	//Se considera data inicial				  	  
					)
   		fCabTotal()
		Loop
    EndIF
	
	aNewMarc := {}
	nLenMarc := Len( aMarcacoes )
	For nX := 1 To nLenMarc
		IF ( cOrdem := aMarcacoes[ nX , 03 ] ) == "ZZ"
			Loop
		EndIF
		dDataApo := aMarcacoes[ nX , AMARC_DATAAPO ]
		aAdd( aNewMarc , {} )
		For nY := nX To nLenMarc
			IF aMarcacoes[ nY , AMARC_DATAAPO ] == dDataApo .and. aMarcacoes[ nY , 03 ] != "ZZ"
				aAdd( aNewMarc[Len(aNewMarc)] , aClone( aMarcacoes[ nY ] ) )
				aMarcacoes[ nY , 03 ] := "ZZ"
			Else
				Exit
			EndIF
		Next nY
	Next nX

	aMarcacoes := aClone( aNewMarc )
	
	//-- Monta o Array aImp com as ocorrˆncias do per¡odo
	aImp := {}
	nLenMarc := Len(aMarcacoes)
	nLenCalend:=Len(aTabCalend)
	
	For nX := 1 to nLenMarc
		dData  := aMarcacoes[nX, 1, 1]
		dDataApo := aMarcacoes[nX,1,AMARC_DATAAPO]
	    
	    //-- Limita as Marcacoes ao Periodo Solicitado
	    dDataOk := If( ( nPos1E := aScan(aTabCalend, { |x| x[CALEND_POS_DATA_APO] == dDataApo .And. x[4] == '1E' }) ) > 0, aTabCalend[nPos1E,1], dData)
	    If Abs(dData - dDataOk) > 2  .OR. ;
	       (Abs(dData - dDataOk) <= 2 .And. dDataOk < dPerIni) .Or. ;
		   (Abs(dData - dDataOk) <= 2 .And. dDataOk > dPerFim)
	       Loop
		EndIf	
	  
	  	//--Se Existe Calendario para a Ordem Obtem Numero de Marcacoes Possiveis
	    If nPos1E>0  
		   	        
			//-- Calcula a Quantidade de Marcacoes possiveis de acordo com o 
			//--Calendario do Funcionario
	        nTabMarc:=0
	        For nPosOrdem:=nPos1E to nLenCalend
	           If aTabCalend[nPosOrdem,CALEND_POS_DATA_APO] <> dDataApo
	              Exit
	           Endif
	           nTabMarc++
	        Next nPosOrdem
	    Endif
	    
		//-- Procura por Excecoes na Data
		cTraba := ''
		nQuant := 0
		aIntervalo := {}
		If nPos1E > 0
			cTraba := aTabCalend[nPos1E, 6]
		  	//-- Corre Todas as Marcacoes realizadas de acordo com a DataApo
			For nW := nPos1E to nLenCalend
				If aTabCalend[nW,CALEND_POS_DATA_APO] <> dDataApo
              		Exit
           		Endif
				nQuant ++                                                
				//-- Se segundo a Tabela Padrao Existir Intervalo
				//-- Armazena-o para posterior verificacao
				If aTabCalend[nW,9] > 0
				   aAdd(aIntervalo, { aTabCalend[nW,9],aTabCalend[nW,4] })
				Endif
			Next nW
		EndIf

		//-- Reinicializa Variaveis
		dDtAfas  := CtoD('  /  /  ')
		dDtRet   := CtoD('  /  /  ')
		cTipAfas := ''
		aMotivo  := {}

		If nPos1E == 0
			aAdd(aMotivo, STR0015 ) // '** Nao existe Calendario           '
		EndIf	

		//-- MSG n£mero de Marca‡”es
		If lNMarc						
			//-- Marca‡”es Impares
			If Len(aMarcacoes[nX])%2 > 0
				aAdd(aMotivo, STR0016 ) // '** Numero de Marcacoes Impar       '
			EndIf

			//-- Menos ou Mais marca‡”es que o programado
			If Len(aMarcacoes[nX]) < nQuant
				aAdd(aMotivo, STR0017 ) // '** Menos Marcacoes que o Programado'
			ElseIf Len(aMarcacoes[nX]) > nQuant .And. nQuant > 0
				aAdd(aMotivo, STR0018 ) // '** Mais Marcacoes que o Programado '
			EndIf	
		EndIf

		//-- MSG Hora Extra n„o autorizada
		If lHExtr .And. Len(aHENA) > 0
			For nY := 1 to Len(aHENA)
				If SPC->(dbSeek(fFilFunc('SPC')+SRA->RA_MAT+aHENA[nY]+DtoS(dData), .F.))
					aAdd(aMotivo, STR0019 ) // '** Hora Extra Nao Autorizada       '
					Exit
				EndIf
			Next nY
		EndIf

		//-- MSG Intervalo
		If lInter
			//-- Intervalo Menor ou Maior que o Programado
			nW := 0
			For nW := 1 To Len(aIntervalo)
				nIntReal := 0
				nIntProg := 0 
				nHrReal	 := 0
				//-- Obtem o numero de horas/minutos de intervalo

				//-- Localiza o numero da posicao da marcacao pela sigla.
				nPosInt := Ascan(aSiglaMarc, { |x| x == aIntervalo[nW,2] })
	
				lIntMaior:=lIntMenor:= .F.		 				
 				If nPosInt+1 <= Len(aMarcacoes[nX])

					If  Empty(nTolInt)
						nIntReal := fDHtoNS(aMarcacoes[nX, nPosInt+1, 1], aMarcacoes[nX, nPosInt+1, 2]) - fDHtoNS(aMarcacoes[nX, nPosInt, 1], aMarcacoes[nX, nPosInt, 2])
						nIntProg := fDhToNs(,aIntervalo[nW,1])
						lIntMenor:= (nIntReal) < (nIntProg)  
						lIntMaior:=	(nIntReal) > (nIntProg)
                    Else 
	               	   
                    	CalcHours(  aMarcacoes[nX, nPosInt+1, 1]		,;	//01 -> Data 1
									aMarcacoes[nX, nPosInt+1, 2]		,;	//02 -> Hora 1
									aMarcacoes[nX, nPosInt, 1]			,;	//03 -> Data 2
									aMarcacoes[nX, nPosInt, 2]			,;	//04 -> Hora 2
									@nHrReal							,;	//05 -> <@>Horas Normais Apontadas
									NIL									,;	//06 -> <@>Horas Noturnas Apontadas
									.F.									;	//07 -> Apontar Horas Noturnas
								 )  
						If nHrReal > SomaHoras(aIntervalo[nW,1],nTolInt)
							lIntMaior:=.T.
						ElseIf aIntervalo[nW,1] > SomaHoras(nHrReal,nTolInt)
	 						lIntMenor:=.T.
						Endif							 
					Endif
						
				Endif

				If lIntMenor
				   	aAdd(aMotivo, '** '+Trim(Str(nW,2))+'o.'+STR0020) // ' Intervalo Menor que o Programado'
				ElseIf   lIntMaior
					aAdd(aMotivo, '** '+Trim(Str(nW,2))+'o.'+STR0021) // ' Intervalo Maior que o Programado'
				EndIf
			Next nW
		EndIf

		//-- MSG Situa‡„o do Funcion rio
		If lSitFu
			//-- Marca‡”es Anteriores a Admiss„o ou Posteriores a Demiss„o
			If !Empty(fDtaAdmis()) .And. dData < fDtaAdmis()
				aAdd(aMotivo, STR0022 ) // '** Marcacoes antes da Admisssao    '
			ElseIf !Empty(SRA->RA_DEMISSA) .And. dData > SRA->RA_DEMISSA
				aAdd(aMotivo, STR0023 ) // '** Marcacoes apos Demissao         '
			EndIf

			//-- Marca‡”es durante Ferias ou Afastamentos
			If fAfasta( SRA->RA_Filial,SRA->RA_Mat,dData,@dDtafas,@dDtRet,@cTipAfas) .And. aTabCalend[nPos1E,10] # 'E'
				If cTipAfas == 'F'
					aAdd(aMotivo, STR0024 ) // '** Marcacoes durante as Ferias     '
				Else
					aAdd(aMotivo, STR0025 ) // '** Marcacoes durante Afastamento   '
				EndIf
			EndIf		
		EndIf

		cProc := aMarcacoes[nX , 01 , AMARC_PROCESSO ]
		cPer  := aMarcacoes[nX , 01 , AMARC_PERIODO  ]
		cRot  := aMarcacoes[nX , 01 , AMARC_ROTEIRO  ]
		cNPag := aMarcacoes[nX , 01 , AMARC_NUM_PAGTO]
		
		If !( cChave == cProc + cPer + cRot + cNPag )
			cDet := STR0039 + cProc + ' - ' + STR0040 + cPer + ' - ' + STR0041 + cRot + ' - ' + STR0042 + cNPag
			aAdd(aImp, cDet)
			cChave := cProc + cPer + cRot + cNPag
		EndIf
		
		If Len(aMotivo) > 0
			cDet := Subs(Dtos(dData),7,2)+'/' + Subs(Dtos(dData),5,2) + Space(2)
			For nZ := 1 to Len(aMarcacoes[nX])
				cDet += StrZero(aMarcacoes[nX, nZ, 2], 5, 2) + Space(1)
			Next nZ
			cDet := If(Len(cDet)>=87,Left(cDet,87),cDet+Space(87-Len(cDet)))
			aAdd(aImp, cDet)
			For nZ := 1 to Len(aMotivo)
				If nZ == 1
					aImp[Len(aImp)] += aMotivo[nZ]
				Else	
					aAdd(aImp, Space(87) + aMotivo[nZ])
				EndIf
			Next nZ
		EndIf
	Next nX

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Imprime Funcionarios                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	fImpFun(@aImp)
	fCabTotal()
EndDo
	
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Imprime o Rodape do Relatorio                                ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
IF lRoda
	IF Li < 58
		Li := 58
	EndIF	
	Impr("","F")
EndIF

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ T‚rmino do Relat¢rio                                         ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Set Device To Screen
If aReturn[5] == 1
	Set Printer To
	Commit
	OurSpool(wnrel)
Endif
MS_FLUSH()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fCabTotal   ³ Autor ³ Equipe Advanced RH  ³ Data ³ 26/03/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³  					                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONR040                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fCabTotal()

dbSelectArea( "SRA" )
dbSkip()

nPagAtu := ContFl

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fImpFun     ³ Autor ³ Equipe Advanced RH  ³ Data ³ 26/03/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime um Funcionario e Executa Quebras                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONR040                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fImpFun(aFun)

If	Len(aFun) == 0 
	Return( NIL )
Endif

If cCcAnt # SRA->RA_CC .And. SRA->( !Eof() ) .And. (nOrdem == 2 .Or. nOrdem == 5)	// Quebra p/ C.Custo
	fImprime({},2)
Elseif cTnoAnt # SRA->RA_TNOTRAB .And. SRA->( !Eof() ) .And. nOrdem == 4			// Quebra p/ Turno
	fImprime({},4)
ElseIf cFilAnte # SRA->RA_FILIAL .And. SRA->( !Eof() )								// Quebra p/ Filial
	fImprime({},3)
Endif

cFilAnte:= SRA->RA_FILIAL
cCcAnt  := SRA->RA_CC
cTnoAnt := SRA->RA_TNOTRAB

fImprime(aFun,1)
aFun := {}

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fImprime    ³ Autor ³ Equipe Advanced RH  ³ Data ³ 26/03/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³  										                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONR040                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fImprime(aFun,nTipo)
// nTipo: 1- Funcionario
//        2- Centro de Custo
//        3- Filial
//        4- Turno

Local Det    := ""
Local nPre   := 0

If nTipo == 2	// Salta pagina a cada Centro de Custo
	If lCC .And. !Empty(cCcAnt)
		fPNR040Linha(2,,"P")
	Else
		fPNR040Linha(2)
	Endif
ElseIf nTipo == 3	// Salta pagina a cada Filial
	If nOrdem == 2 .Or. nOrdem == 4 .Or. nOrdem == 5
		fPNR040Linha(nOrdem,,"P")
	Else
		fPNR040Linha(3,,"P")
	Endif
ElseIf nTipo == 4	// Salta pagina a cada Filial
	fPNR040Linha(4)	// Imprime cabecalho do Turno
Endif

If nTipo == 1
	Det := STR0031 + SRA->RA_CHAPA+" "+STR0032+SRA->RA_MAT+" "+STR0033+SRA->RA_NOME+" "+STR0038+SRA->RA_DEPTO // "CHAPA: "###"MATR: "###"NOME: "###"DEPTO: "
	fPNR040Linha(1,Det)
	For nPre := 1 to Len(aFun)
		If Li >= 58
			If nOrdem == 2	.Or. nOrdem == 4 .Or. nOrdem == 5 // - Centro de Custo ou Turno de Trab
				fPNR040Linha(nOrdem,,"P")
			Else
				fPNR040Linha(3,,"P")
			Endif
			Det := STR0031+SRA->RA_CHAPA+" "+STR0032+SRA->RA_MAT+" "+STR0033+SRA->RA_NOME+" "+STR0038+SRA->RA_DEPTO
			fPNR040Linha(1,Det)
		
		Endif
		fPNR040Linha(1,aFun[nPre])
	Next nPre
	IF Li < 58
		fPNR040Linha(1,Replicate("-",132))
	EndIF	
Endif

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fPNR040Linha³ Autor ³ Equipe Advanced RH  ³ Data ³ 26/03/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Imprime cabecalhos						                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ PONR040                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fPNR040Linha(nTipo,Det,cPara)

Local cDet:=""

nTipo := If(nTipo==NIL,0,nTipo)
cPara := If(cPara==Nil,"C",cPara)

If nTipo == 1
	Impr(Det,"C")
	lRoda := .T.
ElseIf nTipo > 0
	If nTipo == 2 .Or. nTipo == 5 .Or. nPagAtu <> ContFl
		cDet:= STR0027+SRA->RA_FILIAL+SPACE(1)+STR0029+SUBS(SRA->RA_CC+SPACE(20),1,20)+' - '+DescCc(SRA->RA_CC,SRA->RA_FILIAL,30)		//"Filial: "###"    C.C: "
	Elseif nTipo == 3 .Or. nPagAtu <> ContFl
		cDet:= STR0027+SRA->RA_FILIAL		//"Filial: "
	Elseif nTipo == 4 .Or. nPagAtu <> ContFl
		cDet:= STR0027+SRA->RA_FILIAL+SPACE(1)+STR0028+SRA->RA_TNOTRAB+' - '+FDescTno(SRA->RA_FILIAL,SRA->RA_TNOTRAB)		//"Filial: "###" Turno: "
	Endif
	Impr(cDet,cPara)
	Impr(Replicate("-",132),"C")
	lRoda := .T.
Endif
nPagAtu := ContFl

Return( NIL )
/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr040Proc	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR01 para variavel private		³
|		   |cProcesso    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr040Proc(cTip)

Local lRet := .T.

cProcesso := &( ReadVar() )

If ( cTip == 'A' )
	lRet := PonX1Valid(cProcesso)
Else
	If !Empty(cProcesso)
		lRet := PonX1Valid(cProcesso)
	EndIf
EndIf

Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr040Per	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR02 para variavel private		³
|		   |cPeriodo    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr040Per()

Local cChave := cProcesso
Local lRet := .T.

If !Empty(cRoteiro)
	cChave += cRoteiro
Else
	cChave += "PON"
EndIf

cPeriodo := &( ReadVar() )

cChave += cPeriodo

lRet := PonX1Valid(cChave)

Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr040Rot	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR03 para variavel private		³
|		   |cRoteiro    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr040Rot()

Local lRet := .T.

cRoteiro := &( ReadVar() )

lRet := PonX1Valid(cProcesso+cRoteiro)

Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³Pnr040NPag	 	³Autor³  Leandro Drumond  ³ Data ³18/04/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Transferir o parametro MV_PAR04 para variavel private		³
|		   |cNumPag	    											    |
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL                      									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso	   ³Generica      										    	³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function Pnr040NPag()

Local lRet := .T.

cNumPagto := &( ReadVar() )

lRet := PonX1Valid(cProcesso+cRoteiro+cPeriodo+cNumPagto)

Return( lRet )