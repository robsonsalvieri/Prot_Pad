#INCLUDE "PONM030.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "PONCALEN.CH"
    
Static lPnm030CposBlock

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    Ё PONM030  Ё Autor Ё Equipe Advanced RH    Ё Data Ё10/04/1996Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Gera┤└o das Marca┤■es                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso  	 Ё SIGAPON										              Ё╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.             Ё╠╠
╠╠цддддддддддддбддддддддбддддддбдддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                     Ё╠╠
╠╠цддддддддддддеддддддддеддддддедддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁMauricio MR Ё21/09/04Ё073995ЁUtilizacao da funcao PutMarcAuto para a   Ё╠╠
╠╠Ё& Marinaldo Ё        Ё      Ёgeracao de marcacoes automaticas.	      Ё╠╠
╠╠ЁLuiz GustavoЁ05/02/07Ё      ЁRetiradas funcoes de ajuste de dicionario.Ё╠╠
╠╠ЁIgor FranzoiЁ10/05/07Ё      ЁVerificar campos a mais para Query p/ SRA Ё╠╠
╠╠ЁLeandro DR  |25/02/08ЁMelhorЁInclusao de pergunte "PROCESSO"  e ajuste Ё╠╠
╠╠Ё			   Ё		Ё      Ёpara seu tratamento. 					  Ё╠╠
╠╠цддддддддддддбддддддддбддддддбдддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё FNC  Ё  Motivo da Alteracao                     Ё╠╠
╠╠цддддддддддддеддддддддеддддддедддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁAlceu P.    Ё28/07/11Ё00000016920/2011ЁAjuste na Query da tabela SRA.  Ё╠╠
╠╠ЁLeandro Dr. Ё28/11/11ЁTEBZD7ЁAjuste no pergunte SX1.                   Ё╠╠
╠╠юддддддддддддаддддддддаддддддадддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function PONM030()

Local aArea			:= GetArea()
Local aSays			:= {}
Local aButtons		:= {}
Local cPerg			:= "PONM030"
Local cSvFilAnt		:= cFilAnt
Local lBarG1ShowTm 	:= .F.
Local lBarG2ShowTm 	:= .F.
Local nOpcA			:= 0

Private cCadastro	:= OemToAnsi( STR0001 ) // "Gera┤└o das Marca┤■es"
Private lAbortPrint	:= .F.

DEFAULT lPnm030CposBlock := ExistBlock("PNM030CPOS")

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё So Executa se os Modos de Acesso dos Arquivos Relacionados esЁ
Ё tiverm OK 												   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
IF ValidArqPon() 

	aAdd(aSays,OemToAnsi( STR0003 ) )// "Este programa tem como objetivo gerar automaticamente as  marca┤■es"
	aAdd(aSays,OemToAnsi( STR0004 ) )// "do funcion═rio de acordo com o calend═rio de horas e suas exce┤■es."
	
	aAdd( aButtons , { 5 , .T. , { ||  Pergunte( cPerg , .T. ) } } )
	aAdd( aButtons , { 1 , .T. , { |o| nOpcA := 1 , IF( GpConfOk() , FechaBatch() , nOpcA := 0 ) } } )
	aAdd( aButtons , { 2 , .T. , { |o| FechaBatch() }} )
	
	FormBatch( cCadastro , aSays , aButtons )

	IF ( nOpcA == 1 )
		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Verifica se deve Mostrar Calculo de Tempo nas BarGauge			 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		lBarG1ShowTm := ( SuperGetMv("MV_PNSWTG1",NIL,"N") == "S" )
		lBarG2ShowTm := ( SuperGetMv("MV_PNSWTG2",NIL,"S") == "S" )
		/*                               
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Executa o Processo de Geracao de Marcacoes      					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		Proc2BarGauge( { || Pnm030Processa( cPerg ) } , STR0001 , NIL , STR0024 , .T. , lBarG1ShowTm , lBarG2ShowTm )	// "Gera┤└o das Marca┤■es"###"Gerando Marca┤■es..."
	EndIF

EndIF
	                           	
cFilAnt := cSvFilAnt
RestArea( aArea )

Return( NIL )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддддддбдддддддбддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    Ё Pnm030Processa  Ё Equipe Advanced RH     Ё Data Ё30/04/1996Ё╠╠
╠╠цддддддддддедддддддддддддддддадддддддаддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Processa a Geracao das Marcacoes                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso  	 Ё SIGAPON							             			  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Static Function Pnm030Processa( cPerg )

Local aAreaSRA			:= SRA->( GetArea() )
Local aAreaSP8			:= SP8->( GetArea() )
Local aTabCalend		:= {}
Local aNReg				:= {}
Local aTpMarc			:= {}
Local aNewMarc			:= {}
Local aMarcacoes		:= {} 
Local aMarcNoGER		:= {}	
Local aMarcAux			:= {}
Local aTabMarc			:= {}
Local aTabPadrao		:= {} 
Local aLogs				:= {}
Local aLogDet			:= {}
Local aLogTitle			:= {}
Local aRecsSR6			:= {}
Local aRecsBarG			:= {}


Local bSraScope			:= { || .T. }
Local bAcessaSRA 		:= &("{ || " + ChkRH("PONM030","SRA","2") + "}")

Local cFilDe			:= ""
Local cFilAte			:= ""
Local cCCDe				:= ""
Local cCCAte			:= ""
Local cCustoGrv			:= ""
Local cMatDe			:= ""
Local cMatAte			:= "" 
Local cMarc				:= ""
Local cTurDe			:= ""
Local cTurAte			:= ""
Local cRegDe    		:= ""
Local cRegAte   		:= ""
Local cCategoria		:= "" 
Local cFilOld    		:= "__cFilOld__"
Local cFil       		:= "" 
Local cFlag				:= ""
Local cTno       		:= ""
Local cMat       		:= ""
Local cSeq       		:= ""
Local cSitFolh			:= "" 
Local cTurno     		:= ""
Local cTpMarcs   		:= ""
Local cCc        		:= ""
Local cFilTnoDe	 		:= ""
Local cFilTnoAte 		:= ""
Local cTimeIni	 		:= Time()
Local cLastFil			:= "__cLastFil__"
Local cFilTnoSRA 		:= "__cFilTnoSRA__"
Local cFilTnoOld 		:= "__cFilTnoOld__"
Local cFilTnoSeqOld		:= "__cFilTnoSeqOld__"
Local cMsgBarG1			:= ""
Local cSvFilAnt			:= cFilAnt
Local cAutoSai			:= ""

Local dPerIni			:= Ctod("//")
Local dPerFim			:= Ctod("//")
Local dDataIni			:= Ctod("//")
Local dDataFim			:= Ctod("//")
Local dDemissa			:= Ctod("//")
Local dAdmissa			:= Ctod("//")
Local dPerIGeA			:= Ctod("//")
Local dPerFGeA			:= Ctod("//")

Local lSobrepoe			:= .T.
Local lAleato			:= .F.
Local lSR6Comp			:= Empty( xFilial( "SR6" ) )
Local lIncProcG1		:= .T.
Local lChkRegra			:= .F.
Local lSemMarc			:= .F.
Local lCompleta			:= .F.

Local nMinuto     		:= 0
Local nLastRec			:= 0
Local nRecsSR6			:= 0
Local nIncPercG1		:= 0
Local nIncPercG2		:= 0
Local nCount1Time		:= 0        
Local nLoop				:= 0
Local nCount			:= 0    
Local nLoop2			:= 0
Local nCount2			:= 0 
Local nX				:= 0

#IFDEF TOP

	Local aStruSRA	 	:= {}
	Local aTempSRA	 	:= SRA->( dbStruct() )
	Local aCposSRA	 	:= {}

	Local cQuery	 	:= ""
	Local cQueryCond	:= ""	

	Local lQueryOpened	:= .F.

	Local nContField 	:= 0

	Local uRet			:= NIL

#ENDIF

Private oPeriodo		:= RHPERIODO():New()
Private cProcesso		

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Carregando as Perguntas 								       Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Pergunte( cPerg , .F. )

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Atribuindo as Perguntas as variaveis do Programa		       Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
cFilDe		:= mv_par01				//Filial De
cFilAte		:= mv_par02				//Filial Ate
cCCDe		:= mv_par03				//Centro de Custo De
cCCAte		:= mv_par04				//Centro de Custo Ate
cMatDe		:= mv_par05				//Matricula De
cMatAte		:= mv_par06				//Matricula Ate
dDataIni	:= mv_par07				//Data Inicial
dDataFim	:= mv_par08				//Data Final
cTpMarcs	:= mv_par09				//Tipos de Marcacoes a Gerar
lSobrepoe	:= ( mv_par10 == 1 ) 	//Se Sobrepoe as Marcacoes Existentes	
cRegDe		:= mv_par11				//Regra De
cRegAte		:= mv_par12				//Regra Ate
lAleato		:= ( mv_par13 == 1 )	//Se Gera Marcacoes Aleatroias	
nMinuto		:= Abs( mv_par14 )		//Minutos para a Aleatoriedade das Marcacoes
cTurDe		:= mv_par15				//Turno De
cTurAte		:= mv_par16				//Turno Ate
lCompleta	:= ( mv_par17 == 1 )	//Se Completa Marcacoes
lSemMarc	:= ( mv_par18 == 1 )	//Se Gera marcacoes para Funcionario Sem Marcacoes
cAutoSai	:= mv_par19 			//Se Gera marcacoes de Intervalo para Dias Nao Trabalhados
lChkRegra	:= ( mv_par20 == 1 )	//Se Segue a Regra de Apontamento na Geracao das Marcacoes 
cCategoria	:= mv_par21				//Categorias para serem geradas as marcacoes
cProcesso 	:= mv_par22				//Processo que devera ser considerado na geracao das marcacoes

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Seta as Variaveis de Memoria que Serao Utilizadas na  GeracaoЁ
Ё Automatica de Marcacoes									   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
IF !( lChkRegra )
	SetMemVar( "PA_MARCAUT" , cTpMarcs						, .T. )
	SetMemVar( "PA_ALEATOR" , IF( lAleato	, "S" , "N" )	, .T. )
	SetMemVar( "PA_MINALEA" , nMinuto						, .T. )
	SetMemVar( "PA_AUTOMSM" , IF( lSemMarc  , "S" , "N" )	, .T. )
	SetMemVar( "PA_COMPMAR" , IF( lCompleta , "S" , "N" )	, .T. )
	SetMemVar( "R6_AUTOSAI" , cAutoSai						, .T. )
EndIF

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Seleciona o Ordem e Retorna o Numero de Registros      	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
SRA->( dbSetOrder( RetOrdem( "SRA","RA_FILIAL+RA_TNOTRAB" ) ) )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Cria o Bloco dos Funcionarios que atendam ao Scopo	   	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
bSraScope	:= { ||;
						(;                                 
							( RA_FILIAL		>= cFilDe ) .and. ( RA_FILIAL	<= cFilAte ) .and. ;
							( RA_REGRA		>= cRegDe ) .and. ( RA_REGRA	<= cRegAte ) .and. ;
							( RA_MAT		>= cMatDe ) .and. ( RA_MAT		<= cMatAte ) .and. ;
							( RA_CC			>= cCCDe  ) .and. ( RA_CC		<= cCCAte  ) .and. ;
							( RA_TNOTRAB	>= cTurDe ) .and. ( RA_TNOTRAB	<= cTurAte ) .and. ;
							( RA_PROCES		=  cProcesso) .and. ( RA_CATFUNC $ cCategoria) ;
						);
				}

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Monta Query para o SRA Quando TOP                      	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
#IFDEF TOP
	
	/*
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Seta apenas os Campos do SRA que serao Utilizados           Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	nContField	:= Len(aTempSRA)
	aAdd( aCposSRA , "RA_FILIAL"	)
	aAdd( aCposSRA , "RA_MAT" 		)	
	aAdd( aCposSRA , "RA_NOME"		)
	aAdd( aCposSRA , "RA_CC"		)
	aAdd( aCposSRA , "RA_TNOTRAB"	)
	aAdd( aCposSRA , "RA_SEQTURN"	)
	aAdd( aCposSRA , "RA_REGRA"  	)
	aAdd( aCposSRA , "RA_ADMISSA"  	)
	If cPaisLoc == "MEX"
		aAdd( aCposSRA , "RA_FECREI"  	)
	EndIf
	aAdd( aCposSRA , "RA_DEMISSA"  	)
	aAdd( aCposSRA , "RA_SITFOLH"  	)
	aAdd( aCposSRA , "RA_CATFUNC"  	)
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁVerifica e Seta os campos a mais incluidos no Mex             Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/				
	fAdCpoSra(aCposSra)
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Ponto de Entrada para Campos do Usuario                      Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF ( lPnm030CposBlock )
		IF ( ValType( uRet := ExecBlock("PNM030CPOS",.F.,.F.,aCposSRA) ) == "A" )
			IF ( Len( uRet ) >= Len( aCposSRA ) )
				aCposSRA := aClone(uRet)
				uRet	 := NIL
			EndIF
		EndIF
	EndIF
	For nX := 1 To nContField
		IF ( aScan( aCposSRA , { |x| x == AllTrim( aTempSRA[ nX , 1 ] ) } ) > 0 )
			aAdd( aStruSRA , aClone( aTempSRA[ nX ] ) )
		EndIF
	Next nX

	aCposSRA	:= aTempSRA := NIL
	nContField	:= Len( aStruSRA )

	cQuery := "SELECT "                 
	For nX := 1 To nContField
		cQuery += aStruSRA[ nX , 1 ] + ", "
	Next nX
	cQuery 		:= SubStr( cQuery , 1 , Len( cQuery ) - 2 )

	cQueryCond	+= " FROM "
	cQueryCond	+= InitSqlName("SRA") + " SRA "
	cQueryCond	+= " WHERE "
	cQueryCond	+= " ( "
	cQueryCond	+= 		"SRA.RA_DEMISSA='"+Space(Len(Dtos(dDataIni)))+"'"
	cQueryCond	+= 		" OR "
	cQueryCond	+= 		"SRA.RA_DEMISSA>='"+Dtos(dDataIni)+"'"
	cQueryCond	+= " ) "
//	cQueryCond	+= " AND "
//	cQueryCond	+= "SRA.RA_ADMISSA<='"+Dtos(dDataFim)+"'"
	cQueryCond	+= " AND "
	cQueryCond	+= "SRA.RA_FILIAL>='"+cFilDe+"'"
	cQueryCond	+= " AND "
	cQueryCond	+= "SRA.RA_FILIAL<='"+cFilAte+"'"
	cQueryCond	+= " AND "
	cQueryCond	+= "SRA.RA_CC>='"+cCCDe+"'"
	cQueryCond	+= " AND "
	cQueryCond	+= "SRA.RA_CC<='"+cCCAte+"'"     
	cQueryCond	+= " AND "
	cQueryCond	+= "SRA.RA_MAT>='"+cMatDe+"'"
	cQueryCond	+= " AND "
	cQueryCond	+= "SRA.RA_MAT<='"+cMatAte+"'"
	cQueryCond	+= " AND "
	cQueryCond	+= "SRA.RA_REGRA>='"+cRegDe+"'"
	cQueryCond	+= " AND "
	cQueryCond	+= "SRA.RA_REGRA<='"+cRegAte+"'"
	cQueryCond	+= " AND "
	cQueryCond	+= "SRA.RA_TNOTRAB>='"+cTurDe+"'"
	cQueryCond	+= " AND "
	cQueryCond	+= "SRA.RA_TNOTRAB<='"+cTurAte+"'"
	cQueryCond	+= " AND "
	cQueryCond	+= "SRA.RA_PROCES='"+cProcesso+"'"
	cQueryCond	+= " AND "	
	cQueryCond	+= "SRA.D_E_L_E_T_=' ' "

	cQuery	    += cQueryCond
	cQuery		+= "ORDER BY "+SqlOrder( SRA->( IndexKey() ) )
	cQuery		:= ChangeQuery(cQuery)

	SRA->( dbCloseArea() ) //Fecha o SRA para uso da Query
	IF ( lQueryOpened := MsOpenDbf(.T.,"TOPCONN",TcGenQry(,,cQuery),"SRA",.T.,.T.) )
		For nX := 1 To nContField
			IF ( aStruSRA[nX,2] <> "C" )
				TcSetField("SRA",aStruSRA[nX,1],aStruSRA[nX,2],aStruSRA[nX,3],aStruSRA[nX,4])
			EndIF
		Next nX
		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Verifica o Total de Registros a Serem Processados            Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		cQuery := "SELECT COUNT(*) NLASTREC "
		cQuery += cQueryCond
		cQuery := ChangeQuery(cQuery)
       	IF ( MsOpenDbf(.T.,"TOPCONN",TcGenQry(,,cQuery),"__QRYCOUNT",.T.,.T.) )
			nLastRec := __QRYCOUNT->NLASTREC
			__QRYCOUNT->( dbCloseArea() )
		Else
			MsAguarde( { || SRA->( dbEval( { || ++nLastRec } ) ) } , STR0029 + STR0030 )	//'Aguarde...'###'Selecionaldo Funcionarios'
			SRA->( dbGotop() )
		EndIF
	Else
		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Restaura Arquivo Padrao e Ordem                             Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		ChkFile( "SRA" )
		SRA->( dbSetOrder( RetOrdem( "SRA","RA_FILIAL+RA_TNOTRAB" ) ) )
		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Verifica o Total de Registros a Serem Processados            Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		aRecsBarG := {}
		CREATE SCOPE aRecsBarG FOR Eval( bSraScope )
		nLastRec := SRA->( ScopeCount( aRecsBarG ) )
		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Procura e posiciona no primeiro funcion═rio.                Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		SRA->( dbSeek( cFilDe + cTurDe , .T. ) )
	EndIF
	nContField := 0
#ELSE
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Verifica o Total de Registros a Serem Processados            Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	aRecsBarG := {}
	CREATE SCOPE aRecsBarG FOR Eval( bSraScope )
	nLastRec := SRA->( ScopeCount( aRecsBarG ) )
	/*
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Procura e posiciona no primeiro funcion═rio.                Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	SRA->( dbSeek( cFilDe + cTurDe , .T. ) )
#ENDIF

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Inicializa a Mensagem para a IncProcG2() ( Funcionarios )	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
IncProcG2( OemToAnsi( STR0024 ) , .F. )	//"Gerando Marca┤■es..."

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Inicializa a Regua para a IncProcG2() ( Funcionarios )	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
BarGauge2Set( nLastRec )

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Inicializa Filial/Turno De/Ate							   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
cFilTnoDe	:= ( cFilDe  + cTurDe )
cFilTnoAte	:= ( cFilAte + cTurAte )

Begin Sequence

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Processa a Geracao de Marcacoes                              Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	While SRA->(;
					!Eof();
					.and.;
					(;
							( ( cFilTnoSRA := ( ( cFil := RA_FILIAL ) + ( cTno := RA_TNOTRAB ) ) ) >= cFilTnoDe );
							.and. ;
	                        ( cFilTnoSRA <= cFilTnoAte );
	                 );
	            )     

		If Dtos(fDtaAdmis()) > Dtos(dDataFim)
			SRA->(dbSkip())
	       	Loop
		EndIf
		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Consiste filtro do intervalo De / Ate.                       Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IF SRA->( !Eval( bSraScope ) )
			SRA->( dbSkip() )
			Loop
		EndIF

        /*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Aborta o processamento 	 								   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IF ( lAbortPrint )
			aAdd( aLogDet , STR0013 ) //"Geracao das marcacoes calcelada pelo usuario"
			Break
		EndIF

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Redefine variaveis.										   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		cMat		:= SRA->RA_MAT
		cSeq		:= SRA->RA_SEQTURN
		cTurno		:= SRA->RA_TNOTRAB
		cCc			:= SRA->RA_CC
		cSitFolh	:= SRA->RA_SITFOLH
		dDemissa	:= SRA->RA_DEMISSA
		dAdmissa	:= fDtaAdmis()

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Atualiza a Mensagem para a IncProcG1() ( Turnos )			   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IF !( cFilTnoSeqOld == ( cFilTnoSRA + cSeq ) )
			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Atualiza o Filial/Turno/Sequencias Anteriores				   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			cFilTnoSeqOld := ( cFilTnoSRA + cSeq )
			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Atualiza a Mensagem para a BarGauge do Turno 				   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			cMsgBarG1 := ( STR0021 + " " + cFil + " - " + STR0022 + " " + cTno + " - " + Left(AllTrim( fDesc( "SR6" , cTno , "R6_DESC" , NIL , cFil , 01 ) ),50) + " " + STR0023 + " " + cSeq )
			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Verifica se Houve Troca de Filial para Verificacai dos TurnosЁ
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			IF !( cLastFil == SRA->RA_FILIAL )
				/*
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Atualiza o Filial Anterior								   Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
				cLastFil := SRA->RA_FILIAL
				/*
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Obtem o % de Incremento da 2a. BarGauge					   Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
				nIncPercG1 := SuperGetMv( "MV_PONINC1" , NIL , 5 , cLastFil )
				/*
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Obtem o % de Incremento da 2a. BarGauge					   Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
				nIncPercG2 := SuperGetMv( "MV_PONINCP" , NIL , 5 , cLastFil )
				/*
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Realimenta a Barra de Gauge para os Turnos de Trabalho       Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
				IF ( !lSR6Comp .or. ( nRecsSR6 == 0 ) )
					CREATE SCOPE aRecsSR6 FOR ( R6_FILIAL == cLastFil .or. Empty( R6_FILIAL ) )
					nRecsSR6 := SR6->( ScopeCount( aRecsSR6 ) )
				EndIF
				/*
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Define o Contador para o Processo 1                          Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
				--nCount1Time
				/*
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Define o Numero de Elementos da BarGauge                     Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
				BarGauge1Set( nRecsSR6 )
				/*
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Inicializa Mensagem na 1a BarGauge                           Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
				IncProcG1( cMsgBarG1 , .F. )
   				/*
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				Ё Reinicializa a Filial/Turno Anterior                         Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
				cFilTnoOld := "__cFilTnoOld__"
            EndIF
			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁVerifica se Deve Incrementar a Gauge ou Apenas Atualizar a MenЁ
			Ёsagem														   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			IF ( lIncProcG1 := !( cFilTnoOld == cFilTnoSRA ) )
				cFilTnoOld := cFilTnoSRA
			EndIF
			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁIncrementa a Barra de Gauge referente ao Turno				   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			//"Filial:"###"Turno:"###"Sequencia:"
			IncPrcG1Time( cMsgBarG1 , nRecsSR6 , cTimeIni , .F. , nCount1Time , nIncPercG1 , lIncProcG1 )
		EndIF

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Movimenta a R┌gua de Processamento                           Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IncPrcG2Time( OemToAnsi( STR0020 ) , nLastRec , cTimeIni , .T. , 2 , nIncPercG2 ) //"Marca┤■es Geradas:"

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Consiste Situacao na Folha.                                  Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IF (;
				( cSitFolh == "E" );
				.or.;
				(;
					( cSitFolh == "D" );
					.and.;
					( dDemissa < dDataIni );
				);
			)
			SRA->( dbSkip() )
			Loop
		EndIF
        
		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Consiste controle de acessos e filiais validas               Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IF (;
				!( cFil $ fValidFil() );
				.or.;
				SRA->( !Eval( bAcessaSRA ) );
			)
			SRA->( dbSkip() )
			Loop
		EndIF

		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Quebra de Filial											  Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IF !( cFilOld == cFil )

			/*
			зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Carrega a Nova Filial                                       Ё
			юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			cFilOld 	:= cFil
			cFilAnt		:= IF( !Empty( cFilOld ) , cFilOld , cFilAnt )
			/*
			зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Reinicializa a Tabela de Horario Padrao                     Ё
			юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			IF xRetModo( "SRA" , "SPJ" , .F. )
				aTabPadrao := {}
			EndIF

		EndIF

		oPeriodo:cProcesso 	:= SRA->RA_PROCES
		
	    If !(fGetPeriodo( oPeriodo , @aLogDet ))
	    	SRA->( dbSkip() )
	    	Loop
	    EndIf
	    
		dPerIni    		:= oPeriodo:dDataIni
		dPerFim    		:= oPeriodo:dDataFim

		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁConsiste o Periodo de Apontamento                             Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IF (;
				( dDataFim < dDataIni );
				.or.;
				( dDataIni > dPerFim );
				.or.;
				( dDataFim < dPerIni );
			)	
			aAdd( aLogDet , STR0002 )	//"Periodo para geracao das marcacoes invalido."
			aAdd( aLogDet , STR0008 + " " + cFil + " " + STR0009 ) //"As marcacoes para a filial"###"nao foram geradas"
			Break
		EndIF
		
		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Cria Calendario com o periodo completo com Trocas de Turno  Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		aTabCalend := {}
		IF !CriaCalend( dPerIni , dPerFim , cTurno , cSeq , @aTabPadrao , @aTabCalend , cFil , cMat , cCc , {} , NIL , NIL , .F. )
			aAdd( aLogDet , STR0014 + " " + STR0021 + " " + cFil + " " + STR0022 + " " + cTurno )	//"Tabela de Horario Padrao nao cadastrada para o Turno corrente"###"Filial:"###"Turno:"
			aAdd( aLogDet , STR0008 + " " + cFil + " " + STR0022 + " " + cTurno + " " + STR0009 )	//"As marcacoes para a filial"###"nao foram geradas"###"Turno:"
			SRA->( dbSkip() )
			Loop
		EndIF  
		
		/*/
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define o Periodo para a Geracao das Marcacoes Automaticas   Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		dPerIGeA		:= Max( dPerIni , dDataIni )
		dPerFGeA		:= Min( dPerFim , dDataFim )
		IF (;
				( dAdmissa > dPerIni );
				.and.;
				( dAdmissa <= dPerFim );
			)	
			dPerIGeA	:= dAdmissa
		EndIF
		IF (;
				( dDemissa < dPerFim );
				.and.;
				!Empty( dDemissa );
			)	
			dPerFGeA	:= dDemissa
		EndIF
		dPerIGeA	:= Max( dPerIGeA , dPerIni  )
		dPerFGeA	:= Min( dPerFGeA , dPerFim  )
		IF ( dPerFGeA < dPerIGeA )
			SRA->( dbSkip() )
			Loop
		EndIF	

        /*/
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Cria array com as marca┤■es do Periodo para o funcion═rio.  Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF !GetMarcacoes(	@aMarcacoes 		,;	//01 -> Marcacoes dos Funcionarios
							aTabCalend			,;	//02 -> Calendario de Marcacoes
							NIL					,;	//03 -> Tabela Padrao
							NIL					,;	//04 -> Turnos de Trabalho
							dPerIGeA			,;	//05 -> Periodo Inicial
							dPerFGeA			,;	//06 -> Periodo Final
							NIL					,;	//07 -> Filial
					    	cMat				,;	//08 -> Matricula
							NIL					,;	//09 -> Turno
							NIL					,;	//10 -> Sequencia de Turno
							NIL					,;	//11 -> Centro de Custo
							NIL					,;	//12 -> Alias para Carga das Marcacoes
							NIL					,;	//13 -> Se carrega Recno em aMarcacoes
							NIL					,;	//14 -> Se considera Apenas Ordenadas
							NIL					,;  //15 -> Verifica as Folgas Automaticas
							NIL					,;  //16 -> Se Grava Evento de Folga Mes Anterior
							NIL          		,;	//17 -> Se Carrega as Marcacoes Automaticas
							NIL              	,;	//18 -> Registros de Marcacoes Automaticas que deverao ser Deletados
							NIL         		,;	//19 -> Bloco para avaliar as Marcacoes Automaticas que deverao ser Desprezadas
							.F.					 ;	//20 -> Se Considera o Periodo de Apontamento das Marcacoes
					 	)
			SRA->( dbSkip() )
			Loop
		EndIF

		/*/
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Gera marca┤oes Autom═ticas.                                 Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/ 

		aMarcAux:={}
		IF PutMarcAuto( aTabCalend , @aMarcacoes , dPerIGeA , dPerFGeA , cFil , lChkRegra , lSobrepoe, @aMarcAux )
			/*
			зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Grava as Marcacoes no SP8                                   Ё
			юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			PutMarcacoes( aMarcacoes , cFil , cMat , "SP8" , .F. , .T. )
		EndIF
                                    
	    //-- Inclui ocorrencias de marcacoes nЦo geradas
		If !Empty(aMarcAux)
			AADD(aMarcNoGer,{cMat + "-" + SRA->RA_NOME,aMarcAux})        
		Endif

		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Seleciona pr╒ximo funcion═rio.                              Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		SRA->( dbSkip() )

	End While

End Sequence

#IFDEF TOP
	/*
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Fecha a Query Montada e Restaura o SRA padrao               Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF ( lQueryOpened )
		SRA->( dbCloseArea() )
		ChkFile( "SRA" )
	EndIF
#ENDIF

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Gera o Log de Inconsistencias                                Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
IF !Empty( aLogDet ) .OR. !Empty( aMarcNoGer)
	aAdd( aLogTitle , STR0025 ) //"Log de Ocorrencias na Geracao de Marcacoes"
    IF !Empty( aLogDet ) 
	    aAdd(aLogs, aClone(aLogDet) )
	Endif    
    
	IF !Empty( aMarcNoGer )    
	   
  		aAdd( aLogs, aClone(aMarcNoGer) )	
   
    	/*/
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Redefine o Array aMarcNoGer 								  Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aMarcNoGer := {}
		IF ( ( nCount := Len( aLogs[Len(aLogs)] ) ) > 0 )
    		aAdd( aMarcNoGer , STR0031 ) //"MarcaГУes NЦo Geradas"
	  	    aAdd( aMarcNoGer , __PrtThinLine() ) 
			aAdd( aMarcNoGer , PADR(STR0032,10) + SPACE(1) + PADR(STR0033,90) + SPACE(1) +  STR0034 ) // ' Data     MarcaГУes                                        ObservaГЦo' 
		  	For nLoop := 1 To nCount     
		  	    nCount2		:=Len(aLogs[Len(aLogs), nLoop, 2 ] )
		  	    cMat		:=aLogs[Len(aLogs), nLoop, 1 ]
   		  	    aAdd( aMarcNoGer , __PrtThinLine() ) 
		  	    aAdd( aMarcNoGer , cMat ) 
   		  	    aAdd( aMarcNoGer , __PrtThinLine() ) 
		  		For nLoop2:= 1 To nCount2
				   	//-- Corre todas as marcacoes do dia 		
				   	dData	:= PADR(Dtoc(aLogs[Len(aLogs), nLoop, 2, nLoop2, 1 ]),10) //Data
				   	cMsg 	:= STR0035  // "MarcaГУpes em quantidade Мmpar"	
			   		cMarc	:= ""
					For nX := 1 TO Len( aLogs[ Len(aLogs), nLoop, 2, nLoop2, 2] )     //Array das Marcacoes
						If Len(cMarc) > 81 
						   IF "IMPAR"$ UPPER(aLogs[Len(aLogs), nLoop, 2, nLoop2,3 ])  //Tipo de Ocorrencia ('Impar') 
					  		  aAdd( aMarcNoGer , dData  + SPACE(1) +  PADR(cMarc, 90) )
				  			  dData 	:= Space(10)
							  cMarc	:= ""
						   ENDIF
						Endif		                              
		    		
		    			cFlag:=  If( aLogs[ Len(aLogs), nLoop,  2, nLoop2, 2, nX, 4 ] <> "A", Space(3), "[A]" )
		    			cMarc+= StrTran(StrZero(aLogs[ Len(aLogs), nLoop,  2, nLoop2, 2, nX, 2 ],5,2),'.',':') + cFlag + Space(1)
		    			
					Next nX    
					IF "IMPAR"$ UPPER(aLogs[Len(aLogs), nLoop,  2, nLoop2, 3 ]) 
						aAdd( aMarcNoGer , dData + SPACE(1) +  PADR(cMarc, 90) + SPACE(1) + cMsg ) // "MarcaГУpes em quantidade Мmpar"	
					ENDIF	
				Next nLoop2
			Next nLoop
		EndIF	
		aLogs[ Len(aLogs) ] := aClone( aMarcNoGer )
		
    Endif
    
	fMakeLog( aLogs , aLogTitle , cPerg,,cPerg )
EndIF

cFilAnt	:= cSvFilAnt

RestArea( aAreaSP8 )
RestArea( aAreaSRA )

Return( NIL )


/*/   
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁfGetPeriodo   Ё Autor Ё Leandro Drumond   Ё Data Ё21/02/2008Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁObtem o periodo de apontamento							    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁExemplo   ЁoPeriodo:=RHPERIODO:New()       //Criacao do Obj            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁfGetPeriodo( oPeriodo )										Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                                    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function fGetPeriodo( oPeriodo , aLogDet)

Local aArea 		:= GetArea()
Local lRet 			:= .T.

Begin Sequence      

	oPeriodo:PerSel()

    /*/
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁMostra Advertencia para Periodo Nao Encontrado				  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	If !oPeriodo:lFound
 		lRet		:= .F. 
		aAdd( aLogDet , STR0036 + " " + SRA->RA_MAT + " " + STR0009 ) //"As marcacoes do funcionario "###" nao foram geradas"
 		Break
	Endif	
	
End Sequence

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRecupera Valores do Periodo antes da Troca de Periodo 		  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/	
If !lRet
	oPeriodo:RollBack()
Endif

RestArea( aArea )

Return( lRet )