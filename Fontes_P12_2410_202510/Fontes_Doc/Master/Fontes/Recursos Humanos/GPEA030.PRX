#INCLUDE "TOTVS.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "GPEA030.CH"
#INCLUDE "FWADAPTEREAI.CH"

//Recuperar versão de envio
Static lIntTAF    	:= ((SuperGetMv("MV_RHTAF",, .F.) == .T.) .AND. Val(SuperGetMv("MV_FASESOC",/*lHelp*/,' ')) >= 0 )
Static lMiddleware	:= If( cPaisLoc == 'BRA' .AND. Findfunction("fVerMW"), fVerMW(), .F. )
Static lMarca		:= .F.
Static lMsbql
Static lUsaRUJ
Static lNewRUJ
Static lRUJCCT
Static lUsaRI4
Static lUsaABO

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    	³ GPEA030    ³ Autor ³ Mauricio Takakura     	      ³ Data ³ 31/10/10 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o 	³ Cadastro de Funcoes em MVC                                   			³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   	³ GPEA030()                                                    			³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      	³ Generico ( DOS e Windows )                                   			³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               			³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador  ³ Data     ³ FNC			³  Motivo da Alteracao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Leandro Dr.  ³15/10/2012³         M12RH01³Requisito RHU210_03_03. Unificacao da Folha³±±
±±³Renan Borges ³05/08/2015³          TRZVKM³Ajuste para  verificar compartilhamento en-³±±
±±³             ³          ³                ³tre tabelas SQS, SRJ e SQ3.                ³±±
±±³Marcos Cout. ³04/07/2017³    DRHESOCP-543³Realizar a compatibilização do evento S1030³±±
±±³             ³          ³                ³(Funcao) Extemporâneo                      ³±±
±±³Claudinei S. ³19/09/2017³DRHESOCP-904    ³Liberação dos campos exclusivos AUDESP     ³±±
±±³             ³          ³                ³somente se MV_AUDESP estiver ativo.        ³±±
±±³Eduardo Vicen³22/11/2017³DRHESOCP-2077	³Inclusão de função de consulta padrão		³±±
±±³E.Moskovkina ³11/12/2017³DMA3-35         ³RUS-Localization: Changes on Model/View in ³±±
±±³             ³          ³                ³accordance with localized MVC.  	        ³±±
±±|Eduardo Vice ³06/12/2017³DRHESOCP-2197   ³Inclusão de novas TAGS para orgão publico  ³±±
±±³Marcos Cout. ³17/01/2018³DRHESOCP-2483   ³Realizada compatibilização do programa para³±±
±±³             ³          ³                ³geração dos extemporâneos do evento S-1030 ³±±
±±³             ³          ³                ³e S-1040 do eSocial                        ³±±
±±³Eduardo vic  ³02/03/2018|DRHESOCP-3167	³inclusão de validação de tab compartilhada ³±±
±±³             ³          ³                ³e S-1040 do eSocial                        ³±±
±±³Marcos Cout. ³08/03/2018³DRHESOCP-3455   ³Realizar os ajustes necessários para que a ³±±
±±³             ³          ³                ³geração do evento S-1030 seja efetivo      ³±±
±±³Wesley Alves ³10/07/2019³DRHGCH-96235    ³Limpar o conteudo do campo memo quando o   ³±±
±±³             ³          ³                ³ponteiro associado (RDY) for apagado       ³±±

±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPEA030()

	Local oMBrowse
	Local cFiltraRh
	Local cSQ3		:= ""
	Local cSRJ		:= ""

	cSRJ := FWModeAccess( "SRJ", 1) + FWModeAccess( "SRJ", 2) + FWModeAccess( "SRJ", 3)
	cSQ3 := FWModeAccess( "SQ3", 1) + FWModeAccess( "SQ3", 2) + FWModeAccess( "SQ3", 3)

	If cSQ3 > cSRJ
		//"O compartilhamento da tabela de Cargos (SQ3) deverá ser igual ou possuir menor quantidade de compartilhamento Exclusivo (Filial/Unidade/Empresa) que a tabela de Funções (SRJ). Ex: Se a tabela de Funções (SRJ) estiver com C E E, a SQ3 não poderá ser E E E."
		//"Altere o modo de acesso atraves do Configurador. Arquivos SRJ e SQ3."
		MsgInfo( oEmToAnsi( STR0015 ) + CRLF + CRLF + oEmToAnsi( STR0016 ) )
		Return (.F.)
	EndIf

	oMBrowse := FWMBrowse():New()

	oMBrowse:SetAlias("SRJ")
	If  cPaisloc != "PAR"
		oMBrowse:SetDescription(OemToAnsi(STR0009)) //'Cadastro de Fun‡”es'
	Else
		oMBrowse:SetDescription(OemToAnsi(STR0052)) //'Cadastro de Fun‡”es'
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa o filtro utilizando a funcao FilBrowse                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cFiltraRh := CHKRH("GPEA030","SRJ","1")
	oMBrowse:SetFilterDefault( cFiltraRh )

	oMBrowse:Activate()

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} BrowseDef
Browse definition

@author E. Moskovkina
@since 11/12/2017
@version 1.0
@project MA3 - Russia
/*/
//-------------------------------------------------------------------
Static Function BrowseDef()
	Local oBrwSRJ
	Local cFiltraRh

	oBrwSRJ := FWmBrowse():New()
	oBrwSRJ:SetAlias( 'SRJ' )
	//oBrwSRJ:SetDescription(STR0001)	//"Cargo"

	//Inicializa o filtro utilizando a funcao FilBrowse
	cFiltraRh	:= CHKRH(FunName(),"SRJ","1")
	//Filtro padrao do Browse conforme tabela SRJ (Cargo)
	oBrwSRJ:SetFilterDefault(cFiltraRh)

Return oBrwSRJ

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ MenuDef		³Autor³  Mauricio Takakura³ Data ³31/10/2010³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Criacao do Menu do Browse.                                  ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEA030                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³aRotina														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function MenuDef()

	Local aRotina := {}

	ADD OPTION aRotina TITLE STR0004  ACTION "PESQBRW"         		OPERATION 1 ACCESS 0 DISABLE MENU 	//'Pesquisar'
	ADD OPTION aRotina TITLE STR0005  ACTION "VIEWDEF.GPEA030" 		OPERATION 2 ACCESS 0 				//'Visualizar'
	ADD OPTION aRotina TITLE STR0006  ACTION "VIEWDEF.GPEA030" 		OPERATION 3 ACCESS 0				//'Incluir'
	ADD OPTION aRotina TITLE STR0007  ACTION "VIEWDEF.GPEA030" 		OPERATION 4 ACCESS 0				//'Alterar'
	ADD OPTION aRotina TITLE STR0008  ACTION "VIEWDEF.GPEA030" 		OPERATION 5 ACCESS 0				//'Excluir'
	ADD OPTION aRotina TITLE STR0013  ACTION 'VIEWDEF.GPEA030'		OPERATION 9 ACCESS 0 				//"Copiar"
	If cPaisLoc == "PAR"
		ADD OPTION aRotina TITLE STR0050  ACTION "GPEM051()"		OPERATION 6 ACCESS 0 				//"Cambio Sal Min"
		ADD OPTION aRotina TITLE STR0051  ACTION "ConsHist()"		OPERATION 7 ACCESS 0 				//"Consultar histórico"
	Endif

Return aRotina

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ ModelDef 		³Autor³  Mauricio Takakura³ Data ³31/10/2010³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Regras de Modelagem da gravacao.                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEA030                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³aRotina														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function ModelDef()

	Local aUniqueLine
	Local oModel
	Local oStructSRJ
	Local oStructRUJ
	Local oStructRI4

	// Bloco de codigo doa Fields
	Local bTOkVld		:= { || GPEA030TOK( oModel )}

	DEFAULT lUsaRUJ		:= cPaisLoc == "BRA" .and. AliasInDic("RUJ")
	DEFAULT lNewRUJ		:= lUsaRUJ .and. RUJ->(ColumnPos( "RUJ_EVEATR" )) > 0
	DEFAULT lRUJCCT 	:= lUsaRUJ .and. RUJ->(ColumnPos( "RUJ_CODCCT" )) > 0
	DEFAULT lUsaRI4     := cPaisLoc == "BRA" .and. AliasInDic("RI4") .and. RI4->(ColumnPos( "RI4_SALARI" )) > 0
	DEFAULT lUsaABO     := lUsaRUJ .and. RUJ->(ColumnPos( "RUJ_PDABO" )) > 0

	// Criacao do Objeto de Modelagem de dados da tabela SRJ //
	oModel     := MPFormModel():New("GPEA030", NIL, bTOkVld, {|oModel| GPEA030Commit(oModel)})
	oModel:SetDescription( OemToAnsi(STR0009) ) //'Cadastro de Fun‡”es'

	// Estrutura de campos do Model //
	oStructSRJ := FWFormStruct(1, "SRJ")
	If cPaisLoc <> "RUS"
		oStructSRJ:AddField("REQUISITOS","REQUISITOS","RJ_DESCREQ","C",  6,0,{|a,b,c,d| FWInitCpo (a,b,c),lRet:=.T.,FWCloseCpo(a,b,c,lRet),lRet } ,Nil,,.F.,Nil,.F.,.F.,.F.,"")
		FWMemoVirtual( oStructSRJ,{ {'RJ_DESCREQ','RJ_MEMOREQ', "RDY"} } )
	EndIf
	oStructSRJ:RemoveField("RJ_FILIAL")

	oModel:AddFields("GPEA030_SRJ", NIL, oStructSRJ)
	oModel:SetPrimaryKey({"RJ_FUNCAO"})

	If lUsaRUJ
		oStructRUJ := FWFormStruct(1, "RUJ")

		oStructRUJ:SetProperty( "RUJ_TPCALC", MODEL_FIELD_VALID, {|oGrid| Gp030TpVld(oGrid)})
		oStructRUJ:SetProperty( "RUJ_PDBASE", MODEL_FIELD_VALID, {|oGrid| Gp030BsVld(oGrid)})

		If lNewRUJ
			oStructRUJ:SetProperty( "RUJ_EVEFAL", MODEL_FIELD_VALID, {|oGrid| Gp030GetEve(oGrid,.T.)})
			oStructRUJ:SetProperty( "RUJ_EVEATR", MODEL_FIELD_VALID, {|oGrid| Gp030GetEve(oGrid,.F.)})
		EndIf

		oStructRUJ:SetProperty( "RUJ_BSCALC", MODEL_FIELD_WHEN, { |oGrid| oGrid:GetValue("RUJ_TPCALC") $ "1*3" } )
		oStructRUJ:SetProperty( "RUJ_PERC", MODEL_FIELD_WHEN, { |oGrid| oGrid:GetValue("RUJ_TPCALC") $ "1*3" } )
		oStructRUJ:SetProperty( "RUJ_VLFIXO", MODEL_FIELD_WHEN, { |oGrid| oGrid:GetValue("RUJ_TPCALC") $ "2*3" } )

		oModel:AddGrid("GPEA030_RUJ", "GPEA030_SRJ", oStructRUJ,/*bLinePre*/, {|oGrid| Gp030VldRUJ(oGrid)}, /*bPre*/,{|oGrid| Gp030TudOk(oGrid)},/*bLoad*/)

		oModel:GetModel( "GPEA030_RUJ" ):SetOptional(.T.)

		aUniqueLine := If(lRUJCCT, {"RUJ_TPADIC", "RUJ_TPCALC", "RUJ_BSCALC", "RUJ_CODCCT"}, {"RUJ_TPADIC", "RUJ_TPCALC", "RUJ_BSCALC"} )

		oModel:GetModel( "GPEA030_RUJ" ):SetUniqueLine(aUniqueLine)

		oModel:SetRelation( "GPEA030_RUJ", { { 'RUJ_FILIAL', 'xFilial( "SRJ" )' }, { 'RUJ_FUNCAO', 'RJ_FUNCAO' }}, RUJ->( IndexKey( 1 ) ) )
	EndIf

	If lUsaRI4
		oStructRI4 := FWFormStruct(1, "RI4")

		oStructRI4:AddField( 	STR0079			   , ;           	// [01] Titulo do campo "Descrição"
								STR0008			   , ;	            // [02] ToolTip do campo
								"RI4_DESC"	       , ;              // [03] Id do Field
								"C"           	   , ;              // [04] Tipo do campo
								30		           , ;              // [05] Tamanho do campo
								0         		   , ;              // [06] Decimal do campo
												   , ;  			// [07] Code-block de validação do campo
												   , ;      		// [08] Code-block de validação When do campo
												   , ;              // [09] Lista de valores permitido do campo
								.F.                , ;              // [10] Indica se o campo tem preenchimento obrigatório
								{|| FDesc("SWY", RI4->RI4_CODCCT, "WY_DESC", NIL, RI4->RI4_FILCCT)}		   , ;              // [11] Code-block de inicializacao do campo
								NIL                , ;              // [12] Indica se trata-se de um campo chave
								Nil                , ;              // [13] Indica se o campo não pode receber valor em uma operação de update.
								.T.)          		           		// [14] Indica se o campo é virtual

		oModel:AddGrid("GPEA030_RI4", "GPEA030_SRJ", oStructRI4,/*bLinePre*/,/*bLinePos*/, /*bPre*/,/*tudook*/,/*bLoad*/)

		oModel:GetModel( "GPEA030_RI4" ):SetOptional(.T.)
		oModel:GetModel( "GPEA030_RI4" ):SetNoInsertLine(.T.)

		oModel:SetRelation( "GPEA030_RI4", { { 'RI4_FILSRJ', 'xFilial( "SRJ" )' }, { 'RI4_CODSRJ', 'RJ_FUNCAO' }}, RI4->( IndexKey( 2 ) ) )
	EndIf

	oModel:GetModel( "GPEA030_SRJ" ):SetDescription( OemToAnsi(STR0009) ) //'Cadastro de Fun‡”es'

	oModel:SetVldActivate( { || fVldModel() } )

Return( oModel )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ ViewDef  		³Autor³  Mauricio Takakura³ Data ³31/10/2010³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Regras de Interface com o Usuario                           ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEA030                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³aRotina														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function ViewDef()
	Local oView
	Local oModel
	Local oStructSRJ
	Local oStructRUJ
	Local oStructRI4

	DEFAULT lUsaRUJ	:= cPaisLoc == "BRA" .and. AliasInDic("RUJ")
	DEFAULT lUsaRI4 := cPaisLoc == "BRA" .and. AliasInDic("RI4") .and. RI4->(ColumnPos( "RI4_SALARI" )) > 0

	// Criacao da Interface //
	oView := FWFormView():New()

	// Vincular o View ao Model //
	oModel := FWLoadModel("GPEA030")
	oStructSRJ := FWFormStruct(2, "SRJ")

	oStructSRJ:RemoveField("RJ_FILIAL")
	oStructSRJ:RemoveField("RJ_DESCREQ")

	oStructSRJ:SetProperty( 'RJ_FUNCAO' , MVC_VIEW_FOLDER_NUMBER,'C')
	oStructSRJ:SetProperty( 'RJ_DESC' , MVC_VIEW_FOLDER_NUMBER,'C')

	If ColumnPos("RJ_CUMADIC") > 0 .And. aScan(oStructSRJ:aFields, {|x|x[1]=="RJ_CUMADIC"} ) > 0
		oStructSRJ:SetProperty( 'RJ_CUMADIC' , MVC_VIEW_FOLDER_NUMBER,'C')
	EndIf

	// retira os códigos da Audesp
	If !SuperGetMv('MV_AUDESP',, .F.)
		oStructSRJ:RemoveField( "RJ_ADTPFUN" )
		oStructSRJ:RemoveField( "RJ_ADTPJU" )
		oStructSRJ:RemoveField( "RJ_ADTPESC" )
		oStructSRJ:RemoveField( "RJ_ADATIV" )
		oStructSRJ:RemoveField( "RJ_ADTPROV" )
		oStructSRJ:RemoveField( "RJ_ADHORAS" )
	Endif

	oView:SetModel(oModel)
	oView:AddField("GPEA030_SRJ", oStructSRJ)

	oView:CreateHorizontalBox("FIELDS", 100)

	If lUsaRUJ
		oStructRUJ := FWFormStruct(2, "RUJ")

		oStructRUJ:RemoveField("RUJ_FUNCAO")

		oView:AddGrid("GPEA030_RUJ", oStructRUJ, "GPEA030_RUJ" )

		oView:CreateFolder('PASTAS','FIELDS')

		oView:AddSheet( 'PASTAS', 'ABA01', STR0075) // Principal
		oView:AddSheet( 'PASTAS', 'ABA02', STR0076) // Adicionais por Função

		oView:CreateHorizontalBox( 'ABAS1'  , 100,,, 'PASTAS', 'ABA01' )
		oView:CreateHorizontalBox( 'ABAS2' ,  100,,, 'PASTAS', 'ABA02' )

		oView:SetOwnerView("GPEA030_SRJ", "ABAS1")
		oView:SetOwnerView("GPEA030_RUJ", "ABAS2")

		If lUsaRI4
			oStructRI4 := FWFormStruct(2, "RI4")
			oStructRI4:RemoveField("RI4_CODSRJ")
			oStructRI4:RemoveField("RI4_FILSRJ")

			If Empty(xFilial("SWY"))
				oStructRI4:RemoveField("RI4_FILCCT")
			EndIf

			oStructRI4:AddField( 	"RI4_DESC"             	, ;              // [01] Campo
									"06"  					, ;              // [02] Ordem
									STR0079      			, ;              // [03] Titulo
									STR0079              	, ;              // [04] Descricao
									NIL              		, ;              // [05] Help
									"C"                		, ;              // [06] Tipo do campo   COMBO, Get ou CHECK
															, ;              // [07] Picture
															, ;              // [08] PictVar
															, ;              // [09] F3
									.F.  		            , ;              // [10] Editavel
															, ;              // [11] Folder
									""                		, ;              // [12] Group
															, ;              // [13] Lista Combo
															, ;              // [14] Tam Max Combo
															, ;              // [15] Inic. Browse
									.T.)

			oView:AddGrid("GPEA030_RI4", oStructRI4, "GPEA030_RI4" )

			oView:AddSheet( 'PASTAS', 'ABA03', STR0081) // Salário x CCT

			oView:CreateHorizontalBox( 'ABAS3'  , 100,,, 'PASTAS', 'ABA03' )

			oView:SetOwnerView("GPEA030_RI4", "ABAS3")

			oView:addUserButton( STR0082 + " (F7)"	,"MAGIC_BMP", {|oModel| MsAguarde( { || LoadCCT(oModel) } , OemtoAnsi(STR0083)) }	,STR0082,VK_F7	, {MODEL_OPERATION_INSERT,MODEL_OPERATION_UPDATE} ) //"Associar CCT's"

		EndIf
	Else
		oView:SetOwnerView("GPEA030_SRJ", "FIELDS")
	EndIf

Return oView

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Funcao     ³ GPEA030TOK     ³Autor³  Marcos Coutinho ³ Data ³15/01/2018³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descricao ³ Realiza a validação do TudOk                               ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >                                 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEA030                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³aRotina                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >                                 ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function GPEA030TOK(oMdlSRJ)

Local lRet       := .T.
Local nOperation := oMdlSRJ:nOperation
Local cChave     := ""
Local lContinua	 := .T.
Local oMdl		 := oMdlSRJ:GetModel("GPEA030_SRJ")
Local cAliasRJ8  := "RJ8"
Local cStatRJ7	:= .F.
Local lCopiaTab  := ChkFile("RJ7") .And. ChkFile("RJ8")
Local nHoraInicio := 0

DEFAULT	lMsbql		:= SRJ->( ColumnPos( "RJ_MSBLQL" ) ) > 0

	nHoraInicio := Seconds()

	IF lInttaf .AND. EMPTY(oMdl:GetValue("RJ_CODCBO"))
		lContinua := .F.
		IF IsBlind() .Or. !(MsgYesNo(STR0035, STR0033))
			lRet := .F.
			Help(,,,OemToAnsi(STR0033),OemtoAnsi(STR0036),1,0)
		ENDIF
	ENDIF

	If lContinua .And. lMsbql .And. nOperation == MODEL_OPERATION_UPDATE .And. oMdl:GetValue("RJ_MSBLQL") == "1" .And. oMdl:GetValue("RJ_MSBLQL") != SRJ->RJ_MSBLQL .And. fVldAtiv( oMdl:GetValue("RJ_FUNCAO") )
		lContinua 	:= .F.
		lRet 		:= .F.
		Help( " ", 1, OemToAnsi(STR0033), Nil, OemToAnsi(STR0058), 1, 0 )//"Atenção"##"Não é possivel efetuar o bloqueio da função pois há funcionários ativos vinculados"
	EndIf

	If lRet .And. (lInttaf .Or. lMiddleware)
		If Len(FwFldGet('RJ_MEMOREQ')) > 999
			Help(,,,OemToAnsi(STR0033),OemToAnsi(STR0059),1,0)//##"O campo Requisitos ultrapassou o número de caracteres permitido para tag dscAtivDes no eSocial (999)."
			lRet := .F.
		EndIf
	EndIf

	//------------------------------------------------------------------
	//| Verificação se é inclusão e alteração do cadastro de verbas
	//| Inclusão por RECLOCK na tabela RJ* para tratamento posterior JOB
	//------------------------------------------------------------------
	If !IsBlind() .And. lCopiaTab .And. lRet
		//Gera a chave para validação da filial na tabela RJ7
		//Chave composta por: Filial da RJ7 + Cód. da Tabela + Filial Logada(cadastro)
		cChave:= xFilial("RJ7")+"4"+cFilAnt
		cStatRJ7 := fVldRJ7(1,cChave)

		If cStatRJ7 //Inclusao
			Begin Transaction
				//Alimentando a tabela
				 Reclock( cAliasRJ8, .T.  )
				( cAliasRJ8 )->RJ8_FILIAL  := xFilial( "RJ8" )
				( cAliasRJ8 )->RJ8_FILPAR  := ""
				( cAliasRJ8 )->RJ8_TABELA  := "4"
				( cAliasRJ8 )->RJ8_CONTEU  := xFilial("SRJ") + oMdl:GetValue('RJ_FUNCAO')
				( cAliasRJ8 )->RJ8_DATA    := Date()
				( cAliasRJ8 )->RJ8_HORA    := SecsToTime(nHoraInicio)
				( cAliasRJ8 )->RJ8_OPERAC  := iif(nOperation == 3, "1",iif(nOperation== 4,"2","3"))
				( cAliasRJ8 )->RJ8_USUARI  := UsrRetName(RetCodUsr())
				( cAliasRJ8 )->RJ8_STATUS  := "0"
				( cAliasRJ8 )->RJ8_ESOCIA  := "1"
				( cAliasRJ8 )->RJ8_MSGLOG  := iif(nOperation==3,STR0047,iif(nOperation==4,STR0048,STR0049)) + oMdl:GetValue('RJ_FUNCAO') //"Inclusao de função cod: " ## "Alteração de Função cod: " ## "Exclusão de Função cod: "
				( cAliasRJ8 )->( MsUnlock() )

				( cAliasRJ8 )->( dbCloseArea() )
			End Transaction
	    Endif
	Endif

Return lRet

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ GPEA030Commit  ³Autor³  Mauricio Takakura³ Data ³31/10/2010³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Gravacao das informacoes com as validacoes pertinentes      ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEA030                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³aRotina														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function GPEA030Commit(oModel)
	Local aAreaGPE
	Local cKeyChkDel
	Local lJobRJB		:= .F.
	Local lIntGPE  		:= If(GetMv("MV_QGINT",.F.,"N") == "S",.T.,.F.)
	Local lChkDelOK     := .T.
	Local nOperation
	Local lTSREP		:= SuperGetMv( "MV_TSREP" , NIL , .F. )
	Local lMdtGPE		:= SuperGetMV( 'MV_MDTGPE', .F., 'N' ) == 'S'
	Local cReqAnt		:= If( lMdtGPE, NGMemo( M->RJ_DESCREQ, .F., 'RDY' ), "" )
	Local lCargSQ3		:= SuperGetMv("MV_CARGSQ3",,.F.) //Define origem das infos de cargo nos eventos eSocial pela tabela SQ3 e não pela SRJ (Padrão .F. -> SRJ).

	Private oObjREP		:= Nil

	nOperation := oModel:GetOperation()

	//-- Inicializa a integracao via WebServices TSA
	If lTSREP
		oObjREP := PTSREPOBJ():New()
	EndIF

	If nOperation == MODEL_OPERATION_INSERT .or. nOperation == MODEL_OPERATION_UPDATE

		If Empty(FwFldGet( 'RJ_MEMOREQ', 1, oModel ))
			FwFldPut( 'RJ_DESCREQ', '',, oModel )
		EndIf

		If nOperation == MODEL_OPERATION_UPDATE .And. !lCargSQ3 .And. (lInttaf .Or. lMiddleware)
			lJobRJB := FwFldGet("RJ_DESC") <> SRJ->RJ_DESC .Or. FwFldGet("RJ_CODCBO") <> SRJ->RJ_CODCBO
		EndIf

		FWFormCommit(oModel)

		If lJobRJB
			fGravaRJB()
		EndIf

		If lIntGPE
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Realiza a gravacao do responsavel no arquivo utilizado  |
			//|pelos modulos do Quality Celerina, caso haja integracao.|
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			DbSelectArea( "SRJ" )
			DbSetOrder( 1 )
			DbSeek( xFilial( 'SRJ' ) + FwFldGet( 'RJ_FUNCAO', 1, oModel ), .F. )

			QC_QUALITY()
		EndIf

	   	If lTSREP
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Executa o WebServices TSA - Funcoes                          ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			If oObjREP:WSPositionLevel( If(nOperation == MODEL_OPERATION_INSERT,1,2) )

				/*/
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Grava o Log do controle de exportacao WebServices TSA        ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				oObjRep:WSUpdRHExp( "SRJ" )

            Endif
		Endif

		//Integração com o SIGAMDT
		If lMdtGPE
			If nOperation == MODEL_OPERATION_INSERT .And. FindFunction("MDTW030")
				MDTW030( "SRJ" , M->RJ_FUNCAO ) //Executa o W.F. de Aviso do SESMT
			EndIf

			If nOperation == MODEL_OPERATION_UPDATE .And. FindFunction( 'NGIntegra' )
				NGIntegra( { cReqAnt } )
			EndIf
		EndIf

	// Tratamento de exclusão da integração com o Quality //
	ElseIf nOperation == MODEL_OPERATION_DELETE
		If lIntGPE
			aAreaGPE := GetArea()

			dbSelectArea("SRJ")
			cKeyChkDel := SRJ->( FieldGet( FieldPos( "RJ_FUNCAO" ) ) )

			DbSeek("QAC")
			cModoQAC:= FWModeAccess("QAC", 3)
			DbSetOrder(1)

			cPriFil := QXPOSFIL(cEmpAnt)

			If TAMSX3("QAC_FUNCAO")[1] == 8		// Limitar o codigo gerado ao tamanho do campo
				If cModoQAC == "C"
					cCargo:= cEmpAnt+cPriFil+Right(AllTrim(cKeyChkDel), 4)
				Else
					cCargo:= cEmpAnt+cFilAnt+Right(AllTrim(cKeyChkDel), 4)
				Endif
			Else
				If cModoQAC == "C"
					cCargo:= cEmpAnt+cPriFil+cKeyChkDel
				Else
					cCargo:= cEmpAnt+cFilAnt+cKeyChkDel
				Endif
			Endif

			If QAC->(DbSeek(cFilial+cCargo))
				QDA160Dele(@lChkDelOK)
			Endif

			RestARea(aAreaGPE)

		EndIf

		FWFormCommit( oModel )


		If lTSREP
			/*/
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Executa o WebServices TSA - Funcoes                          ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			oObjREP:WSPositionLevel( 3 )

		Endif

	EndIf

Return(.T.)


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Função ³GPEA030VLDSAL³ Autor ³ MOHANAD ODEH ³Data³05/12/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Valida Salário de acordo com mínimo do Cód. MTSS³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³GPEA030VLDSAL()                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Vide Paramentros Formais                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Generico 									  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function GPEA030VLDSAL(nSalario)
Local lRet := .T.

	If !POSITIVO(nSalario)
			lRet := .F.
	EndIf

	If cPaisLoc == "COS" .AND. lRet == .T.
		DbSelectArea("RCC")
		DbSetOrder(1)
		dbSeek(xFilial('SRJ') + "S006")
		While !EOF() .AND. xFilial('SRJ') + "S006" == RCC->RCC_FILIAL+RCC->RCC_CODIGO

			If M->RJ_CODMTSS == Substr(RCC->RCC_CONTEU, 1, 4)
				If nSalario < NoRound(Val(Substr(RCC->RCC_CONTEU, 50, 12)),2)
					Help(" ",1,".A030VLD.") // Problema: "Valor Inválido" ### Solução: "O Salário deve ser maior ou igual ao Salário Mínimo do Código MTSS"
					//Help( " ", 1, "Help",, OemToAnsi(STR0014), 1, 0 ) // "O Salário deve ser maior ou igual ao Salário Mínimo do Código MTSS"
				    lRet := .F.
				    Exit
				EndIf
			EndIf
			dbSkip()
		Enddo
		RCC->(DbCloseArea())
	EndIf

Return lRet
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³IntegDef  ºAutor  ³ Totvs  			   º Data ³  15/02/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Mensagem Única											    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Mensagem Única                                            	º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function IntegDef(cXML, nTypeTrans, cTypeMessage, cVersion)

Local aRet

// Chama a rotina que trata a parte principal
aRet := GPEI030( cXML, nTypeTrans, cTypeMessage, cVersion )
//
Return aRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fTpContSRJ³ Autor ³ Eduardo Vicente       ³ Data ³ 22/11/17 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Opcoes de Contagem 											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPEA030                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fTpContSRJ()

Local cTitulo:= OemtoAnsi(STR0031)//"Contagem de Tempo Especial" //
Local MvParDef	:="1234"
Local aSaveArea := GetArea()
Local lOpt 		:= .F.
Local MvPar
Local mvRet
Static aCat:={"1-"+OemtoAnsi(STR0027),"2-"+OemtoAnsi(STR0028),"3-"+OemtoAnsi(STR0029),"4-"+OemtoAnsi(STR0030)}

MvPar:=&(Alltrim(ReadVar()))		 // Carrega Nome da Variavel do Get em Questao
MvRet:=Alltrim(ReadVar())			 // Iguala Nome da Variavel ao Nome variavel de Retorno

VAR_IXB := MvPar

// Chama funcao f_Opcoes
lOpt := f_Opcoes(@MvPar,cTitulo,aCat,MvParDef,12,49,.F.)
If lOpt
	VAR_IXB :=  StrTran(MvPar,"*","")
EndIf

RestArea(aSaveArea)
Return( .T. )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fAltVer³ Autor ³ Guilherme Bertelli    ³ Data ³ 16/08/18    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Função para verificar se houve alterações em campos 	       ±±
±± utilizados pelo E-Social                                                ±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPEA030                                                    ³±±
±±³ Parâmetros:                                                           ³±±
±±³        aArrCPO - Array dos campos                                     ³±±
±±³        cTab    - Tabela que será realizada a pesquisa                 ³±±
±±³        oMdl    - Model que contém o valor dos campos                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function fAltVer(aArrCPO,cTab, oMdl)
Local lRet	:= .F.
Local nX		:= 0

For nX:= 1 to Len(aArrCPO)
	If &(cTab+"->"+aArrCPO[nX]) != oMdl:GetValue(aArrCPO[nX]) .And. !lRet
		lRet	:= .T.
	EndIf
Next nX
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} fVldAtiv()
Função que efetua a verificação se há funcionários ativos vinculados à função
@author  Allyson Luiz Mesashi
@since   28/04/2022
/*/
//-------------------------------------------------------------------
Static Function fVldAtiv( cCodFunc )

Local cAliasQRY	:= "QRYSRA"
Local cQuery	:= ""
Local cTabSRA	:= RetSqlName("SRA")
Local cTabSRJ	:= RetSqlName("SRJ")
Local lTemAtiv	:= .F.

cQuery	:= "SELECT COUNT(*) AS CONT "
cQuery 	+= "FROM " + cTabSRJ + " SRJ "
cQuery 	+= "INNER JOIN " + cTabSRA + " SRA "
cQuery 	+= "ON " + FWJoinFilial( "SRJ", "SRA" ) + " AND SRA.RA_CODFUNC = SRJ.RJ_FUNCAO AND SRA.RA_SITFOLH != 'D' AND SRA.D_E_L_E_T_ = ' ' "
cQuery 	+= "WHERE SRJ.RJ_FILIAL = '" + xFilial("SRJ") + "' "
cQuery 	+= "AND SRJ.RJ_FUNCAO = '" + cCodFunc + "' "
cQuery 	+= "AND SRJ.D_E_L_E_T_ = ' ' "

dbUseArea(.T., "TOPCONN", TcGenQry( Nil, Nil, cQuery), cAliasQRY, .T., .T.)

lTemAtiv := (cAliasQRY)->CONT > 0

(cAliasQRY)->( dbCloseArea() )

Return lTemAtiv

/*/{Protheus.doc} fGravaRJB()
Função para gravação da tabela RJB
@author	isabel.noguti
@since	29/11/2022
/*/
Static Function fGravaRJB()

	If ChkFile("RJB") .and. ChkFile("RJC") // se existem as tabelas RJB e RJC, continua com o processamento

		RJB->(DbSetOrder(2)) //RJB_FILIAL+RJB_CODIGO+RJB_FCHAVE+RJB_CHAVE
		RecLock("RJB", .T.)
			RJB->RJB_FILIAL 	:= Fwxfilial('RJB')
			RJB->RJB_CODIGO 	:= GetSx8Num("RJB", "RJB_CODIGO")
			RJB->RJB_EVENT  	:= "S2206"
			RJB->RJB_TIPO		:= "4" //Atualização dados funcao
			RJB->RJB_STATUS		:= "0" //Pendente
			RJB->RJB_FCHAVE 	:= SRJ->RJ_FILIAL
			RJB->RJB_CHAVE		:= SRJ->RJ_FUNCAO
			RJB->RJB_DTINC		:= dDataBase
		MsUnLock()
		ConfirmSx8()
	Endif

Return(.T.)

/*/{Protheus.doc} Gp030Verbas()
Fução para seleção de verbas para compor a base do adicional
@author	Leandro Drumond
@since	23/10/2023
/*/
Function Gp030Verbas()
Local lRet 		:= .F.

lRet := FwFldGet("RUJ_TPCALC") $ "1*3" .and. FwFldGet("RUJ_BSCALC") == '2'

Return lRet

/*/{Protheus.doc} Gp030VldDtFim()
Validação da data final de cálculo do adicional
@author	Leandro Drumond
@since	23/10/2023
/*/
Function Gp030VldDtFim() //Validar data final
Local lRet := .T.

If !Empty(M->RUJ_DTFIM) .and. M->RUJ_DTFIM < FwFldGet("RUJ_DTINI")
	lRet := .F.
				//"Atenção" ### "Data final inválida" ### "Data final deve ser maior que a data inicial"
	Help(NIL, NIL, STR0033, NIL, STR0073, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0074})
EndIf

Return lRet

/*/{Protheus.doc} Gp030TpVld()
Valida tipo de cálculo
@author	Leandro Drumond
@since	25/10/2023
/*/
Function Gp030TpVld(oGrid)

If M->RUJ_TPCALC == "2"
	oGrid:LoadValue("RUJ_BSCALC", "")
	oGrid:LoadValue("RUJ_PERC", 0)
ElseIf M->RUJ_TPCALC == "1"
	oGrid:LoadValue("RUJ_VLFIXO", 0)
EndIf

Return .T.

/*/{Protheus.doc} Gp030BsVld()
Seleção de verbas para base de cálculo
@author	Leandro Drumond
@since	25/10/2023
/*/
Function Gp030BsVld(oGrid)

Local aItens 	:= {}
Local aRet 		:= {}
Local cRet 		:= ""
Local MvParDef	:= ""
Local cFilSRV 	:= xFilial("SRV")

dbSelectArea("SRV")
dbSeek(cFilSRV)

While !Eof() .And. SRV->RV_FILIAL == cFilSRV

	If SRV->RV_TIPOCOD $ "1*2"
		aAdd(aItens, SRV->RV_COD + "-" + SRV->RV_DESC)
		MvParDef += SRV->RV_COD
	EndIf

	dbSkip()
EndDo

If f_Opcoes(@aRet, STR0072, aItens, MvParDef, , , .F., 3) //"Selecione as verbas para compor a base do adicional"
	aEval(aRet,{|x| cRet += x +"*"})
	oGrid:LoadValue("RUJ_PDBASE",cRet)
EndIf

Return .T.

/*/{Protheus.doc} Gp030VldRUJ()
Valida a linha digitada
@author	Leandro Drumond
@since	25/10/2023
/*/
Function Gp030VldRUJ(oGrid)

Local lRet 		:= .T.

If oGrid:GetValue("RUJ_TPCALC") $ "2*3" //Valor Fixo
	If Empty(oGrid:GetValue("RUJ_VLFIXO"))
					//"Atenção" ### "Valor fixo não informado" ### "Informe o valor fixo que será utilizado no cálculo."
		Help(NIL, NIL, STR0033, NIL, STR0070, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0071})
		lRet := .F.
	EndIf
EndIf

If lRet .and. oGrid:GetValue("RUJ_TPCALC") $ "1*3" //Percentual
	If Empty(oGrid:GetValue("RUJ_BSCALC"))
					//"Atenção" ### "Base de cálculo não informada" ### "Informe a base de cálculo que deverá ser utilizada."
		Help(NIL, NIL, STR0033, NIL, STR0068, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0069})
		lRet := .F.
	ElseIf Empty(oGrid:GetValue("RUJ_PERC"))
					//"Atenção" ### "Percentual não informado" ### "Informe o percentual que deverá ser utilizado."
		Help(NIL, NIL, STR0033, NIL, STR0066, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0067})
		lRet := .F.
	EndIf
EndIf

If lRet .and. oGrid:GetValue("RUJ_BSCALC") == "2"
	If Empty(oGrid:GetValue("RUJ_PDBASE"))
					//"Atenção" ### "Verbas não informadas" ### "Informe as verbas que devem ser utilizadas como base de cálculo."
		Help(NIL, NIL, STR0033, NIL, STR0064, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0065})
		lRet := .F.
	EndIf
EndIf

If lRet .and. Empty(oGrid:GetValue("RUJ_PDFOL")) .and. Empty(oGrid:GetValue("RUJ_PDADI")) .and. Empty(oGrid:GetValue("RUJ_PDFER")) .and.;
		      Empty(oGrid:GetValue("RUJ_PD131")) .and. Empty(oGrid:GetValue("RUJ_PD132")) .and. Empty(oGrid:GetValue("RUJ_PDRESC"))
				//"Atenção" ### "Nenhuma verba para pagamento foi informada" ### "Informe ao menos uma verba para pagamento."
	Help(NIL, NIL, STR0033, NIL, STR0062, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0063})
	lRet := .F.
EndIf

If lRet .and. lUsaABO .and. !Empty(oGrid:GetValue("RUJ_PDFER")) .and. !Empty(oGrid:GetValue("RUJ_PDABO"))
	If oGrid:GetValue("RUJ_PDFER") == oGrid:GetValue("RUJ_PDABO")
		//"Atenção" ### "Verbas de férias e abono não devem ser iguais." ### "Informe verbas diferentes ou deixa a verba de abono vazia para calcular todos os dias na verba de férias."
		Help(NIL, NIL, STR0033, NIL, STR0089, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0090})
		lRet := .F.
	EndIf
EndIf

Return lRet

/*/{Protheus.doc} Gp030TudOk()
Valida grid da RUJ
@author	Leandro Drumond
@since	25/10/2023
/*/
Function Gp030TudOk(oGrid)

Local lRet 		:= .T.
Local nX 		:= 1
Local nTamGrid  := oGrid:Length()
Local cPdFOL    := ""
Local cPdADI    := ""
Local cPdFER 	:= ""
Local cPd131 	:= ""
Local cPd132 	:= ""
Local cPdRES 	:= ""
Local cPdAux	:= ""
Local cCCTAux   := ""

DEFAULT lUsaRUJ	:= cPaisLoc == "BRA" .and. AliasInDic("RUJ")
DEFAULT lRUJCCT := lUsaRUJ .and. RUJ->(ColumnPos( "RUJ_CODCCT" )) > 0

Begin Sequence

	For nX := 1 to nTamGrid
		oGrid:GoLine(nX)

		If !oGrid:IsDeleted()
			cCCTAux := If(lRUJCCT,oGrid:GetValue("RUJ_CODCCT"),"")
			cPdAux  :=  oGrid:GetValue("RUJ_PDFOL")
			If !Empty(cPdAux)
				If cCCTAux + cPdAux $ cPdFOL
					lRet := .F.
					Break
				Else
					cPdFOL += cCCTAux + cPdAux + "*"
				EndIf
			EndIf
			cPdAux := oGrid:GetValue("RUJ_PDADI")
			If !Empty(cPdAux)
				If cCCTAux + cPdAux $ cPdADI
					lRet := .F.
					Break
				Else
					cPdADI += cCCTAux + cPdAux + "*"
				EndIf
			EndIf
			cPdAux := oGrid:GetValue("RUJ_PDFER")
			If !Empty(cPdAux)
				If cCCTAux + cPdAux $ cPdFER
					lRet := .F.
					Break
				Else
					cPdFER += cCCTAux + cPdAux + "*"
				EndIf
			EndIf
			cPdAux := oGrid:GetValue("RUJ_PD131")
			If !Empty(cPdAux)
				If cCCTAux + cPdAux $ cPd131
					lRet := .F.
					Break
				Else
					cPd131 += cCCTAux + cPdAux + "*"
				EndIf
			EndIf
			cPdAux := oGrid:GetValue("RUJ_PD132")
			If !Empty(cPdAux)
				If cCCTAux + cPdAux $ cPd132
					lRet := .F.
					Break
				Else
					cPd132 += cCCTAux + cPdAux + "*"
				EndIf
			EndIf
			cPdAux := oGrid:GetValue("RUJ_PDRESC")
			If !Empty(cPdAux)
				If cCCTAux + cPdAux $ cPdRES
					lRet := .F.
					Break
				Else
					cPdRES += cCCTAux + cPdAux + "*"
				EndIf
			EndIf
		EndIf
	Next nX

End Sequence

If !lRet
	//"Atenção" ### "Verba duplicada" ### "Não informar a mesma verba em mais de uma linha para o mesmo roteiro de cálculo."
	Help(NIL, NIL, STR0033, NIL, STR0060, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0061})
EndIf

Return lRet

/*/{Protheus.doc} Gp030GetEve
Busca eventos do ponto eletrônico
@author Leandro Drumond
@since 05/02/2024
@version P12
@return Nil
/*/
Function Gp030GetEve(oGrid,lFaltas)

Local aItens 	:= {}
Local aRet 		:= {}
Local cRet 		:= ""
Local MvParDef	:= ""
Local cFilSP9 	:= xFilial("SP9")

dbSelectArea("SP9")
dbSeek(cFilSP9)

While !Eof() .And. SP9->P9_FILIAL == cFilSP9

	If SRV->RV_TIPOCOD $ "1*2"
		aAdd(aItens, SP9->P9_CODIGO + "-" + SP9->P9_DESC)
		MvParDef += SP9->P9_CODIGO
	EndIf

	dbSkip()
EndDo

If f_Opcoes(@aRet, If(lFaltas,STR0077,STR0078), aItens, MvParDef, , , .F., 3) //"Selecione os eventos de falta"##"Selecione os eventos de atraso"
	aEval(aRet,{|x| cRet += x +"*"})
	oGrid:LoadValue(If(lFaltas,"RUJ_EVEFAL","RUJ_EVEATR"),cRet)
EndIf

Return .T.

/*/{Protheus.doc} LoadCCT
Seleciona as CCT's que serão asssociadas a função
@author Leandro Drumond
@since 04/04/2024
@Type     Function
/*/
Static Function LoadCCT(oModel)
Local aArea 		:= GetArea()
Local aStru  		:= {}
Local aLstIndices	:= {}
Local aColumns		:= {}
Local aPesq			:= {}
Local oGrid			:= oModel:GetModel("GPEA030_RI4")
Local cIdGrid		:= ""
Local nOpcX			:= 0
Local lInclui 		:= .T.
Local oSize
Local oTela
Local oDlgGrid
Local oPanel
Local oMark
Local oFont
Local oGroup
Local nTamCCT		:= TamSx3("RI4_CODCCT")[1]

Private cAliasTRB

Static cAliasRI4
Static oArqRI4

If oArqRI4 == Nil //Monta temporária com filiais disponíveis
	Aadd(aStru, {"OK"		, "C", 2						, 0})
	Aadd(aStru, {"FILIAL"	, "C", FwGetTamFilial			, 0})
	Aadd(aStru, {"CODIGO"  	, "C", nTamCCT					, 0})
	Aadd(aStru, {"NOME"  	, "C", 20						, 0})
	AAdd(aLstIndices, {"FILIAL","CODIGO"})
	AAdd(aLstIndices, {"NOME"})

	cAliasRI4 := cAliasTRB := GetNextAlias()

	oArqRI4 := RhCriaTrab(cAliasTRB, aStru, aLstIndices)

	DbSelectArea("SWY")
	DbSetOrder(1)

	SWY->(DbGoTop())

	While SWY->(!Eof())
		RecLock(cAliasTRB, .T.)
		(cAliasTRB)->FILIAL	:= SWY->WY_FILIAL
		(cAliasTRB)->CODIGO	:= SWY->WY_CODIGO
		(cAliasTRB)->NOME	:= SWY->WY_DESC
		(cAliasTRB)->(MsUnlock())
		SWY->(DbSkip())
	EndDo
Else
	cAliasTRB := cAliasRI4
EndIf

AAdd(aColumns,FWBrwColumn():New())
aColumns[Len(aColumns)]:SetData( &("{||(cAliasTRB)->FILIAL}") )
aColumns[Len(aColumns)]:SetTitle(STR0084) //"Filial"
aColumns[Len(aColumns)]:SetSize(FwGetTamFilial)
aColumns[Len(aColumns)]:SetDecimal(0)
aColumns[Len(aColumns)]:SetPicture("@!")

AAdd(aColumns,FWBrwColumn():New())
aColumns[Len(aColumns)]:SetData( &("{||(cAliasTRB)->CODIGO}") )
aColumns[Len(aColumns)]:SetTitle(STR0085) //"Código"
aColumns[Len(aColumns)]:SetSize(nTamCCT)
aColumns[Len(aColumns)]:SetDecimal(0)
aColumns[Len(aColumns)]:SetPicture("@!")

AAdd(aColumns,FWBrwColumn():New())
aColumns[Len(aColumns)]:SetData( &("{||(cAliasTRB)->NOME}") )
aColumns[Len(aColumns)]:SetTitle(STR0079) //"Descrição"
aColumns[Len(aColumns)]:SetSize(20)
aColumns[Len(aColumns)]:SetDecimal(0)
aColumns[Len(aColumns)]:SetPicture("@!")

Aadd( aPesq , {	STR0012, { { "CODIGO"   , "C", nTamCCT, 0, "CODIGO", "@!"}}, 1})
Aadd( aPesq , {	STR0013, { { "NOME   "  , "C", 20 , 0, "NOME"  , "@!"}}, 1})

oSize := FwDefSize():New(.F.)

oSize:AddObject( "CABECALHO",(oSize:aWindSize[3]*1.1),(oSize:aWindSize[3]*0.4) , .F., .F. ) // Não dimensionavel
oSize:aMargins 	:= { 0, 0, 0, 0 } 		// Espaco ao lado dos objetos 0, entre eles 3
oSize:lProp 		:= .F. 				// Proporcional
oSize:Process() 	   					// Dispara os calculos

DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD

DEFINE MSDIALOG oDlgGrid TITLE OemToAnsi( STR0086 ) From 0,0 TO 600,1230 OF oMainWnd PIXEL //"Selecione as CCT's que serão associadas"

// Cria o conteiner onde serão colocados os paineis
oTela		:= FWFormContainer():New( oDlgGrid )
cIdGrid  	:= oTela:CreateHorizontalBox( 80 )

oTela:Activate( oDlgGrid, .F. )

//Cria os paineis onde serao colocados os browses
oPanel	:= oTela:GeTPanel( cIdGrid )

@ oSize:GetDimension("CABECALHO","LININI")+1 , oSize:GetDimension("CABECALHO","COLINI")+4	GROUP oGroup TO oSize:GetDimension("CABECALHO","LINEND") * 0.090 ,oSize:GetDimension("CABECALHO","COLEND") * 0.431  OF oDlgGrid PIXEL
oGroup:oFont:=oFont
@ oSize:GetDimension("CABECALHO","LININI")+9 , oSize:GetDimension("CABECALHO","COLINI")+6 SAY "" Of oDlgGrid Pixel

oMark := FWMarkBrowse():New()

oMark:SetOwner(oPanel)
oMark:SetAlias(cAliasTRB)
oMark:SetTemporary(.T.)
oMark:SetColumns(aColumns)
oMark:SetFieldMark('OK')
oMark:SetFilterDefault( "" )
oMark:SetIgnoreARotina(.T.)
oMark:SetMenuDef('')
oMark:SetSeek( .T., aPesq )

oMark:bAllMark := { || SetMarkAll(oMark:Mark(), lMarca := !lMarca, cAliasTRB ), oMark:Refresh(.T.)  }

oMark:Activate()

SetMarkAll(oMark:Mark(),.F.,cAliasTRB) //Desmarca todos os registros

oMark:Refresh(.T.)

ACTIVATE MSDIALOG oDlgGrid CENTERED ON INIT EnchoiceBar(oDlgGrid, {||nOpcX := 1, oDlgGrid:End() } ,{|| oDlgGrid:End() }, NIL, {})

If nOpcX == 1

	oGrid:GoLine(1)

	lInclui := oGrid:Length() > 1 .Or. !Empty(oGrid:GetValue("RI4_CODCCT"))

	//Adiciona filiais selecionadas
	(cAliasTRB)->(dbGoTop())

	While (cAliasTRB)->(!EOF())
		If !Empty((cAliasTRB)->OK)

			If !oGrid:SeekLine({{"RI4_FILCCT",(cAliasTRB)->FILIAL},{"RI4_CODCCT",(cAliasTRB)->CODIGO}})
				If lInclui
					oGrid:AddLine(.T.) //Se não encontrar, adiciona uma linha
				EndIf

				oGrid:LoadValue("RI4_FILIAL"	,xFilial("RI4",(cAliasTRB)->FILIAL))
				oGrid:LoadValue("RI4_FILCCT"	,(cAliasTRB)->FILIAL)
				oGrid:LoadValue("RI4_CODCCT"	,(cAliasTRB)->CODIGO)
				oGrid:LoadValue("RI4_DESC"    	,(cAliasTRB)->NOME)
				oGrid:LoadValue("RI4_FILSRJ"	,SRJ->RJ_FILIAL)
				oGrid:LoadValue("RI4_CODSRJ"	,SRJ->RJ_FUNCAO)

				lInclui := .T.
			EndIf
		EndIf
		(cAliasTRB)->(dbSkip())
	EndDo

	oGrid:GoLine(1)
EndIf

RestArea(aArea)

Return Nil

/*/{Protheus.doc} SetMarkAll
Marca/Desmarca os registros
@author Leandro Drumond
@since 04/04/2024
@Type     Function
/*/
Static Function SetMarkAll(cMarca,lMarcar,cAliasTRB)

Local cAliasMark := cAliasTRB
Local aAreaMark  := (cAliasMark)->( GetArea() )

DEFAULT cMarca := ""

dbSelectArea(cAliasMark)
(cAliasMark)->( dbGoTop() )

While !(cAliasMark)->( Eof() )
	RecLock( (cAliasMark), .F. )
	(cAliasMark)->OK := IIf( lMarcar , cMarca, '  ' )
	MsUnLock()
	(cAliasMark)->( dbSkip() )
EndDo

RestArea(aAreaMark)

Return .T.
/*/{Protheus.doc} fVldModel
Valida do model
@author Leandro Drumond
@since 21/06/2024
@Type     Function
/*/
Static Function fVldModel()
Local lRet := .T.

DEFAULT lUsaRUJ		:= cPaisLoc == "BRA" .and. AliasInDic("RUJ")

If lUsaRUJ .and. xFilial("SRJ") <> xFilial("RUJ")
	lRet := .F.
	//"Atenção" ### "O compartilhamento das tabelas SRJ e RUJ estão diferentes." ### "Altere o compartilhamento da tabela RUJ para ficar igual ao da SRJ."
	Help(NIL, NIL, STR0033, NIL, STR0087, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0088})
EndIf

Return lRet

