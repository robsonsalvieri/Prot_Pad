#INCLUDE "PROTHEUS.CH"
#INCLUDE "RHLIBSX2.CH"

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Static Utilizadas na InitSqlName()  						   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static __aSqlName	:= {}
Static __nSqlName	:= 0

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Static Utilizadas na Funcao GetSx2Unico()					   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static aGetSx2Unico

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Static Utilizadas na Funcao GetArrUniqe()					   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static aGetArrUniqe

/*/
зддддддддддбддддддддддддддбддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁInRhLibSx2ExecЁAutor ЁMarinaldo de Jesus   Ё Data Ё14/05/2003Ё
цддддддддддеддддддддддддддаддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁExecutar Funcoes Dentro de RHLIBSX2                          Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁInRhLibSx2Exec( cExecIn , aFormParam )						 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁuRet                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function InRhLibSx2Exec( cExecIn , aFormParam )

Local uRet

DEFAULT cExecIn		:= ""
DEFAULT aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= __ExecMacro( cExecIn )
EndIF

Return( uRet )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRetTableName	ЁAutorЁMarinaldo de Jesus Ё Data Ё02/05/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetornar Nome da Tabela Baseado no SX2                      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico													Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RetTableName(cAlias, cRdd, cCodEmp)
	Local aArea		:= GetArea()
	Local lReopen	:= .F.
	Local cReturn

	IF (Type( "__cRdd" ) == "C")
		DEFAULT cRdd := __cRdd
	Else
		DEFAULT cRdd := RddName()
	EndIF

	IF ValType(cCodEmp) == "C"
		lReopen:= (cCodEmp != cEmpAnt)
	EndIf

	IF (cRdd == "TOPCONN")
		cReturn:= InitSqlName( cAlias )
	Else
		cReturn:= RetFullName( cAlias )
	EndIF

	RestArea(aArea)
Return cReturn

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁInitSqlName		ЁAutorЁMarinaldo de Jesus Ё Data Ё31/01/2003Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁInicializa os Nomes dos Arquivos para a Montagem de Query   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico													Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
#IFDEF TOP

	Function InitSqlName( cAlias )

	Local cRetSqlName
	Local nPosSqlName

	cAlias := IF( Empty( cAlias ) , Alias() , cAlias )
	cAlias := Upper( AllTrim( cAlias ) )

    IF (;
    		( __nSqlName > 0 );
    		.and.;
    		( __nSqlName <= Len( __aSqlName ) );
    		.and.;
    		( __aSqlName[ __nSqlName , 01 ] == cAlias );
    		.and.;
    		( __aSqlName[ __nSqlName , 03 ] == cEmpAnt );
    	 )
		nPosSqlName := __nSqlName
	Else
		IF ( ( __nSqlName := ( nPosSqlName := aScan( __aSqlName , { |x| ( x[ 01 ] == cAlias ) .and. ( x[ 03 ] == cEmpAnt ) } ) ) ) == 0 )
			aAdd( __aSqlName , { cAlias , RetSqlName( cAlias ) , cEmpAnt } )
			__nSqlName := ( nPosSqlName := Len( __aSqlName ) )
		EndIF
	EndIF
	cRetSqlName := __aSqlName[ nPosSqlName , 02 ]

	Return( cRetSqlName )

#ENDIF

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁResetSqlName	ЁAutorЁMarinaldo de Jesus Ё Data Ё31/01/2003Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁReinicializa as Staticas utilizadas em InitSqlName()        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico													Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function ResetSqlName()

__aSqlName	:= {}
__nSqlName	:= 0

Return( NIL )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁSx2ChkTable		ЁAutorЁMarinaldo de Jesus Ё Data Ё02/08/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁVerifica a Existencia "Fisica" de uma Tabela do SX2         Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                  	                                Ё
цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё             ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.         Ё
цддддддддддддбддддддддбддддддбдддддддддддддддддддддддддддддддддддддддддд╢
ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                     Ё
цддддддддддддеддддддддеддддддедддддддддддддддддддддддддддддддддддддддддд╢
Ё Mauricio MRЁ05/10/05Ё      Ё	Verificar se a Tabela Esta vazia.		Ё
юддддддддддддаддддддддаддддддадддддддддддддддддддддддддддддддддддддддддды/*/
Function Sx2ChkTable( cAlias , cRdd, lVazio )

Local lSx2ChkTable := .F.

Local cTable
Local cSvAlias

DEFAULT lVazio:= .T.

Begin Sequence

	IF ( Type( "__cRdd" ) == "C" )
		DEFAULT cRdd := __cRdd
	Else
		DEFAULT cRdd := RddName()
	EndIF

	cTable := RetTableName( cAlias , cRdd )
	IF ( cRdd <> "TOPCONN" )
		cTable += GetDbExtension()
	EndIF

	IF !( MsFile( cTable , NIL , cRdd ) )
		Break
	EndIF

	cSvAlias := Alias()
	IF !CheckExecForm( { || dbSelectArea( cAlias ) } , .F. )
		Break
	EndIF

	If ( lSx2ChkTable := ( Select( cAlias ) > 0 ) )
	    (cAlias)->(DbGotop())
	    lVazio := (cAlias)->( Eof() )
	Endif

End Sequence

IF (;
		!Empty( cSvAlias );
		.and.;
		( Select( cSvAlias ) > 0 );
	)
	dbSelectArea( cSvAlias )
EndIF

Return( lSx2ChkTable )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁGetSx2Unico	    ЁAutorЁMarinaldo de Jesus Ё Data Ё06/08/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna a UniqueKey definida no SX2                         Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais> 									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais> 									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso	   ЁGenerico      												Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function GetSx2Unico( cAlias )

Local cSx2Unico
Local nPosAlias

Begin Sequence

	DEFAULT cAlias 			:= Alias()
	DEFAULT aGetSx2Unico	:= {}

	cAlias		:= Upper( AllTrim( cAlias ) )
	nPosAlias	:= aScan( aGetSx2Unico , { |x| ( x[1] == cEmpAnt ) .and. ( x[2] == cAlias ) } )
	IF ( nPosAlias == 0 )
		cSx2Unico := FWX2Unico( cAlias)
		aAdd( aGetSx2Unico , { cEmpAnt , cAlias , cSx2Unico } )
	Else
		cSx2Unico := aGetSx2Unico[ nPosAlias , 3 ]
	EndIF

End Sequence

Return( cSx2Unico )

/*/
зддддддддддбддддддддддддддддддбдддддбддддддддддддддддддбдддддбдддддддддд©
ЁFun┤┘o    ЁSx2UnicoCacheClearЁAutorЁMarinaldo de JesusЁData Ё11/03/2003Ё
цддддддддддеддддддддддддддддддадддддаддддддддддддддддддадддддадддддддддд╢
ЁDescri┤┘o ЁReinicializa as Staticas utilizadas em GetSx2Unico()      	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico													Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Sx2UnicoCacheClear()
aGetSx2Unico := {}
Return( NIL )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁX2Unique2Index	ЁAutorЁMarinaldo de Jesus Ё Data Ё12/09/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna Expressao de Indice conforme X2_UNIQUE              Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                  	                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function X2Unique2Index( cAlias , aFields )

Local cIndexUnique := ""

Local aFieldTam
Local cField
Local cFieldType
Local nField
Local nFields

DEFAULT cAlias := Alias()

cAlias	:= Upper( AllTrim( cAlias ) )
IF ( aFields == NIL )
	aFields := GetArrUniqe( cAlias )
EndIF

nFields		:= Len( aFields )
For nField := 1 To nFields
	cField := aFields[ nField ]
	cFieldType	:= ( cAlias )->( Type( cField ) )
	IF ( cFieldType == "D" )
		cField	:= "Dtos(" + cField + ")"
	ElseIF ( cFieldType == "N" )
		aFieldTam := TamSx3( cField )
		IF !Empty( aFieldTam )
			cField	:= "Str(" + cField + "," + AllTrim( Str( aFieldTam[1] ) ) + "," + AllTrim( Str( aFieldTam[2] ) ) + ")"
		Else
			cField	:= "Str(" + cField + ",10,2)"
		EndIF
	ElseIF ( cFieldType <> "C" )
		Loop
	EndIF
	cIndexUnique += cField
	IF ( nField < nFields )
		cIndexUnique += "+"
	EndIF
Next nField
IF ( SubStr( cIndexUnique , -1 ) == "+" )
	cIndexUnique := SubStr( cIndexUnique , 1 , ( Len( cIndexUnique ) - 1 ) )
EndIF

Return( cIndexUnique )

/*/
зддддддддддбдддддддддддбдддддбддддддддддддддддддддддддддбддддбдддддддддд©
ЁFun┤┘o    ЁGetArrUniqeЁAutorЁMarinaldo de Jesus        ЁDataЁ11/08/2004Ё
цддддддддддедддддддддддадддддаддддддддддддддддддддддддддаддддадддддддддд╢
ЁDescri┤┘o ЁRetorna Array com os campos que compoe a chave unica		Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico              			                  	   		Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function GetArrUniqe( cAlias , aFormalHeader )

Local aChrDelStr
Local aFieldsUnique
Local nAliasPos

Begin Sequence

	DEFAULT cAlias		    := Alias()
	DEFAULT aGetArrUniqe	:= {}

	cAlias		:= Upper( AllTrim( cAlias ) )
	nAliasPos	:= aScan( aGetArrUniqe , { |x| ( ( x[1] == cEmpAnt ) .and. ( x[2] == cAlias ) ) } )
	IF ( nAliasPos > 0 )
		Break
	EndIF

	aChrDelStr		:= { "DTOS" , "STR" , "STRZERO" , "(" , ")" , "," , " " }
	aFieldsUnique	:= StrToArray( GetSx2Unico( cAlias ) , "+" , { | cString | StrDelChr( @cString , aChrDelStr ) , !Empty( cString ) } )
	aFieldsUnique	:= FieldsChgCpo( cAlias , aClone( aFieldsUnique ) , aFormalHeader )

	aAdd( aGetArrUniqe , { cEmpAnt , cAlias , aFieldsUnique } )

End Sequence

DEFAULT aFieldsUnique := aGetArrUniqe[ nAliasPos , 3 ]

Return( aFieldsUnique )

/*/
зддддддддддбддддддддддддддддддбдддддбдддддддддддддддддддбддддбдддддддддд©
ЁFun┤┘o    ЁArrUniqeCacheClearЁAutorЁMarinaldo de Jesus ЁDataЁ11/03/2003Ё
цддддддддддеддддддддддддддддддадддддадддддддддддддддддддаддддадддддддддд╢
ЁDescri┤┘o ЁReinicializa as Staticas utilizadas em GetArrUniqe()      	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico													Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function ArrUniqeCacheClear()
aGetArrUniqe := {}
Return( NIL )

/*
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁSx2ChkModo 	Ё Autor ЁMarinaldo de Jesus    Ё Data Ё06/04/2001Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁVerifica o Modo de Acesso de Dois Arquivos Relacionados		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁSx2ChkModo( cAlias1 , cAlias2 )								 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁcAlias1 -> Alias do Arquivo Principal            		 	 Ё
Ё          ЁcAlias2 -> Alias do Arquivo Secundario           		 	 Ё
Ё          ЁlHelp   -> Nao Mostrar o Help                    		 	 Ё
Ё          ЁcMod1   -> Modo de Acesso do 1o Alias            		 	 Ё
Ё          ЁcMod2   -> Modo de Acesso do 2o Alias            		 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlRet  									                     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGeral                                                        Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Sx2ChkModo( cAlias1 , cAlias2 , lHelp , cMod1 , cMod2 )

Local cAlias	:= Alias()
Local cHelp 	:= ""
Local lRet		:= .T.
Local nLstSel1	:= 0
Local nLstSel2	:= 0
Local nAtuSel1	:= 0
Local nAtuSel2	:= 0

Begin Sequence

	cAlias1 := Upper( AllTrim( IF( cAlias1 == NIL .or. ValType( cAlias1 ) != "C" , Alias() , cAlias1 ) ) )
	cAlias2 := Upper( AllTrim( IF( cAlias2 == NIL .or. ValType( cAlias2 ) != "C" , Alias() , cAlias2 ) ) )
	lHelp	:= IF( lHelp   == NIL .or. ValType( lHelp   ) != "L" , .T.     , lHelp   )

	nLstSel1 := Select( cAlias1 )
	CheckExecForm( { || dbSelectArea( AllTrim( cAlias1 ) ) , ( ( nAtuSel1 := Select( cAlias1 ) ) > 0 ) } , .F. , "NOEXISTALS" )

	nLstSel2 := Select( cAlias2 )
	CheckExecForm( { || dbSelectArea( AllTrim( cAlias2 ) ) , ( ( nAtuSel2 := Select( cAlias2 ) ) > 0 ) } , .F. , "NOEXISTALS" )

	IF ( ( nAtuSel1 == 0 ) .or. ( nAtuSel2 == 0 ) )
		Break
	EndIF
 //-- Verifica o modo de abertura do Alias 1
	If  AT( cAlias1+"E" , cArqTab ) > 0
		cMod1	:= "E"
	Else
		cMod1	:= Space(1)
	Endif

    //-- Verifica o modo de abertura do Alias 2
	If  AT( cAlias2+"E" , cArqTab ) > 0
		cMod2	:= "E"
	Else
		cMod2	:= Space(1)
	Endif

	IF !( lRet := ( cMod1 == cMod2 ) )
		IF ( lHelp )
			cHelp += STR0001 + cAlias1 + STR0002 + cMod1 + " - "	//"Alias: "###"Modo de Acesso: "
			cHelp += STR0001 + cAlias2 + STR0002 + cMod2			//"Alias: "###"Modo de Acesso: "
			CursorArrow()
			Help( "" , 1 , "MODOACESSO" , , cHelp , 5 , 0 )
		EndIF
	EndIF

End Sequence

IF (;
		( nLstSel1 == 0 );
		.and.;
		( nAtuSel1 > 0 );
	)
	( cAlias1 )->( dbCloseArea() )
EndIF

IF (;
		( nLstSel2 == 0 );
		.and.;
		( nAtuSel2 > 0 );
	)
	( cAlias2 )->( dbCloseArea() )
EndIF

IF ( Select( cAlias ) > 0 )
	dbSelectArea( cAlias )
EndIF

Return( lRet )


/*
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁxRetModo    Ё Autor ЁMarinaldo de Jesus    Ё Data Ё06/04/2001Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁVerifica o Modo de Acesso de Dois Arquivos Relacionados		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁxRetModo( cAlias1 , cAlias2 )								 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁcAlias1 -> Alias do Arquivo Principal            		 	 Ё
Ё          ЁcAlias2 -> Alias do Arquivo Secundario           		 	 Ё
Ё          ЁlHelp   -> Nao Mostrar o Help                    		 	 Ё
Ё          ЁcMod1   -> Modo de Acesso do 1o Alias            		 	 Ё
Ё          ЁcMod2   -> Modo de Acesso do 2o Alias            		 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlRet  									                     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGeral                                                        Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function xRetModo( cAlias1 , cAlias2 , lHelp , cMod1 , cMod2 )
Return( Sx2ChkModo( @cAlias1 , @cAlias2 , @lHelp , @cMod1 , @cMod2 ) )


/*
зддддддддддбдддддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁGpeRelationFileЁ Autor ЁMarinaldo de Jesus   Ё Data Ё14/05/2001Ё
цддддддддддедддддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁValida o Relacionamentos dos Arquivos do Getao de Pessoal      Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁGpeRelationFile( lShowHelp )								   Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>                          			   Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlRet -> Se todos os Arquivos Estao com o Relacionamento CorretoЁ
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	       Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerica                                                       Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function GpeRelationFile( lShowHelp )

Local lRetModo		:= .T.

Static lGpeChkRel
Static cLastEmpChk

Default lShowHelp	:= .T.

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Consiste o Modo de Acesso dos Arquivos                       Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Begin Sequence
	Default lGpeChkRel	:= .T.
	Default cLastEmpChk	:= cEmpAnt
	IF ( cLastEmpChk <> cEmpAnt )
		lGpeChkRel := .T.
	EndIf
	IF !( lGpeChkRel )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SR0",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SR2",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SR3",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SR4",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SR7",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SR8",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SR9",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SRB",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SRC",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SRD",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SRF",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SRG",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SRH",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SRK",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SRL",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SRO",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SRP",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SRQ",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SRS",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SRR",lShowHelp) )
		Break
	EndIf
	IF !( lRetModo := xRetModo("SRA", "SRT",lShowHelp) )
		Break
	EndIf
End Sequence

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Se estiver Tudo Ok nao verifica Novamente					   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
lGpeChkRel := !( lRetModo )

Return( lRetModo )

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддбдддддбдддддддддддддддддддддддддддддбддддбдддддддддд©╠╠
╠╠ЁFun┤┘o	 ЁChkVazioЁAutorЁMarcos A. Stiefano Versao  I ЁDataЁ27/02/1996Ё╠╠
╠╠Ё      	 Ё        ЁAutorЁMarinaldo de Jesus Versao II ЁDataЁ07/06/2004Ё╠╠
╠╠цддддддддддеддддддддадддддадддддддддддддддддддддддддддддаддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁVerifica se o Arquivo Esta Vazio 				 		  	  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso		 ЁGenerico 												  	  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function ChkVazio( cAlias , lShowHelp )

Local aArea			:= GetArea()
Local aAreaAlias	:= ( cAlias )->( GetArea() )
Local lChkVazio		:= .T.

Local cModo
Local cNomeArq
Local cArquivo

Begin Sequence

	If !ChkFile(cAlias)
		Break
	EndIf

	cArquivo := AllTrim( RetSqlName(cAlias) )
	cNomeArq := AllTrim( FWX2Nome(cAlias))
	cModo	 := AllTrim( FWModeAccess(cAlias, 3) )

	//VerSenha(114) -> Verifica se no configurador foi habilitado a visualizacao de
	//outras filiais
	If ( cModo == "E" .AND. !VerSenha(114) )
		lChkVazio := ( cAlias )->( dbSeek( xFilial() , .F. ) )
		Break
	EndIf

	( cAlias )->( dbGotop() )
	lChkVazio := ( cAlias )->( !Bof() .and. !Eof() )

End Sequence

If !( lChkVazio )
	DEFAULT lShowHelp := .T.
	If ( lShowHelp )
		Help( " " , 1 , "ARQUIVAZIO" , NIL , cArquivo + " : " + cNomeArq , 4 , 2 )
	EndIf
EndIf

RestArea( aAreaAlias )
RestArea( aArea )

Return( lChkVazio )
