#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPEA1120.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "FWMVCDEF.CH"

Static lGp120Auto	:= .F.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o       ³ GPEA120  ³ Autor ³ J.Ricardo             ³ Data ³ 20.02.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o    ³ Manutencao Acumulados Anuais                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso         ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador  ³ Data   ³FNC             ³ Motivo da Alteracao              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Leandro Dr.|05/10/12|M12RH01	   |Projeto Unificacao da Folha.             ³±±
±±³			  |	       |           |Requisito.: RHU210_01_09                   ³±±
±±³Esther V.  |22/04/14|M_RH003    |Replica V11 para V12. Tratamento do para-³±±
±±³           |        |           |metro MV_ITMCLVL para aceitar '3', nao   ³±±
±±³           |        |           |obrigando o preenchimento dos campos.    ³±±
±±³Christiane |30/04/15|TSGB98     |Correções para GPEA120M                  ³±±
±±³Emerson Cam|13/07/15|           |Remoção do AjustaSX3                     ³±±
±±³Renan B.   |04/02/16|           |Ajuste para não aparecer a opção de "Sal-³±±
±±³           |        |           |var e incluir novo" pois ela só é válida ³±±
±±³           |        |           |para cadastros simples, que não envolva a³±±
±±³           |        |           |hierarquia de pai-filho.                 ³±±
±±³Gabriel A. |29/06/16|           |Ajustes para importação via MILE.        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPEA120(nOpcAuto,xAutoCab,xAutoItens,nOpc)
Local aArea
Local aAutoCab		:= {}
Local aAutoItens	:= {}
Local aIndexSRA		:= {}		//Variavel Para Filtro
Local cFiltraSRA			//Variavel para filtro
Local cFilAux		:= ""
Local cMatAux		:= ""
Local nOpcao
Local nPos			:= 0
Local oBrwSRA

Private aCRA 		:= { STR0001 , STR0002 , STR0003 }  //"Confirma"###"Redigita"###"Abandona"
Private bFiltraBrw 	:= {|| Nil}		//Variavel para Filtro
Private lItemClVl 	:= SuperGetMv( "MV_ITMCLVL", .F., "2" ) $ "1*3"	// Verificar contabilizacao por item contabil e classe de valor

cCadastro 		:= OemToAnsi(STR0009)  //"Acumulados Anuais"
lGp120Auto 		:= ( xAutoCab<>Nil )

If lGp120Auto .or. nOpcAuto <> Nil
	If lGp120Auto
		aArea		:= GetArea()
		aAutoCab	:= xAutoCab
		aAutoItens	:= xAutoItens
		nOpcao 		:= If(nOpc<>Nil,nOpc,nOpcao)
		nOpcao		:= If(nOpcao == 3, 4, nOpcao)
		aRotina 	:= MenuDef()

		DbSelectArea("SRA")

		If (nPos := aScan(aAutoCab,{|x| UPPER(Alltrim(x[1]))=='RA_FILIAL'})) > 0
			cFilAux := aAutoCab[nPos,2]
		Else
			cFilAux := xFilial("SRA")
		EndIf
		If (nPos := aScan(aAutoCab,{|x| UPPER(Alltrim(x[1]))=='RA_MAT'})) > 0
			cMatAux := aAutoCab[nPos,2]
		EndIf

		DbSelectArea('SRA')
		DbSetOrder(1)

		If !dbSeek(cFilAux+cMatAux)
			AutoGrLog(STR0021 + " " + Alias() + " " + Dtoc(MsDate()) + ' ' + Time() ) // "Tabela "
			AutoGrLog("RA_FILIAL: " + cFilAux)
			AutoGrLog("RA_MAT: " + cMatAux)
			AutoGrLog(STR0022) // "Funcionário não encontrado com dados acima"
			AutoGrLog(Replicate("-",80))
		Else
			oModelAuto := ModelDef()
			FWMVCRotAuto(ModelDef(),"SRA",nOpcao,{{"GPEA120_SRA",aAutoCab},{"GPEA120_SRD",aAutoItens}})
		EndIf
		RestArea(aArea)
	Else
		//Executa rotina COM Interface
		FWExecView(STR0009,'GPEA120', nOpcAuto, , { || .T. } ) //"Acumulados Anuais"
	EndIf
Else

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o Arquivo Esta Vazio                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !ChkVazio("SRA")
		Return
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa o filtro utilizando a funcao FilBrowse                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cFiltraRh := CHKRH("GPEA120","SRA","1")

	If ValType(cFiltraRh) == "L"
		cFiltraRh := if(xRetFilRh,".T.",".F.")
	EndIf

	oBrwSRA := FwMBrowse():New()

	oBrwSRA:SetAlias( 'SRA' )
	oBrwSRA:SetDescription(OemToAnsi(STR0009)) 	  //"Controle de Periodos por Empregado"
	//oBrwSRA:SetmenuDef( 'GPEA120' )

	GpLegend(@oBrwSRA,.T.) //Cria legenda

	//Filtro padrao do Browse conforme tabela SRW (Controle de Restricoes de Usuarios)
	oBrwSRA:SetFilterDefault(cFiltraRh)

	oBrwSRA:Activate()

EndIf

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MenuDef   ºAutor  ³Leandro Drumond     º Data ³  12/03/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Menu Funcional                                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPE                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function MenuDef()
Local aRotina :=  {}

ADD OPTION aRotina TITLE STR0004 ACTION 'PesqBrw'          	OPERATION 1 ACCESS 0 //"Pesquisar"
ADD OPTION aRotina TITLE STR0005 ACTION 'VIEWDEF.GPEA120'	OPERATION 2 ACCESS 0 //"Visualizar"
If IsInCallStack("FWMILEDIALOG")
	ADD OPTION aRotina TITLE "" ACTION 'VIEWDEF.GPEA120' 	OPERATION 3 ACCESS 0
EndIf
//So permite manutencao via rotina automatica
If lGp120Auto
	ADD OPTION aRotina TITLE STR0020 ACTION 'VIEWDEF.GPEA120' 	OPERATION 4 ACCESS 0 //"Manutencao"
	ADD OPTION aRotina TITLE STR0008 ACTION 'VIEWDEF.GPEA120' 	OPERATION 5 ACCESS 0 //"Excluir"
EndIf
ADD OPTION aRotina TITLE STR0016 ACTION 'GPER510' 			OPERATION 6 ACCESS 0 //"Imprimir"

Return aRotina
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ModelDef  ºAutor  ³Leandro Drumond     º Data ³  12/03/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Definicao do modelo de dados do cadastro de acumulados      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPE                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ModelDef()
Local oStructSRA
Local oStructSRD
Local oModel
Local aRemovCmps := {}
Local aAux	:={}

oModel:= MpFormModel():New("GPEA120", /*Pre-Validacao*/, /*Pos-Validacao*/, {|oModel|Gp120Grava(oModel)}/*Commit*/, /*{||lVldActivate := .T.}*//*Cancel*/)

oStructSRA := FWFormStruct(1,"SRA",{|cCampo| AllTrim(cCampo)+"|" $ "RA_FILIAL|RA_MAT|RA_NOME|RA_ADMISSA|RA_PROCES|RA_POSTO|"})
oModel:AddFields("GPEA120_SRA", /*cOwner*/, oStructSRA , /*Pre-Validacao*/,/*Pos-Validacao*/,/*Carga*/)
oModel:GetModel("GPEA120_SRA"):SetOnlyView( .T. )
oModel:GetModel("GPEA120_SRA"):SetOnlyQuery( .T. )

If !IsInCallStack("FWMILEIMPORT")
	oStructSRA:SetProperty( 'RA_MAT'    ,MODEL_FIELD_INIT,FwBuildFeature( STRUCT_FEATURE_INIPAD, 'SRA->RA_MAT'     ) )
	oStructSRA:SetProperty( 'RA_NOME'   ,MODEL_FIELD_INIT,FwBuildFeature( STRUCT_FEATURE_INIPAD, 'SRA->RA_NOME'    ) )
	oStructSRA:SetProperty( 'RA_ADMISSA',MODEL_FIELD_INIT,FwBuildFeature( STRUCT_FEATURE_INIPAD, 'SRA->RA_ADMISSA' ) )
	oStructSRA:SetProperty( 'RA_PROCES'	,MODEL_FIELD_INIT,FwBuildFeature( STRUCT_FEATURE_INIPAD, 'SRA->RA_PROCES' 	) )
	oStructSRA:SetProperty( 'RA_POSTO'	,MODEL_FIELD_INIT,FwBuildFeature( STRUCT_FEATURE_INIPAD, 'SRA->RA_POSTO' 	) )
EndIf

oStructSRD := FWFormStruct(1,"SRD")
oModel:AddGrid("GPEA120_SRD", "GPEA120_SRA"/*cOwner*/, oStructSRD , /*bLinePre*/, { |oGrid| gp120LinOk(oGrid) } /* bLinePost*/, /*bPre*/, /*bPost*/,{|oGrid| CargaSRD(oGrid) }/*bLoad*/)

If !IsInCallStack("FWMILEIMPORT")
	aAux := FwStruTrigger('RD_PD','RD_TIPO1','SRV->RV_TIPO',.T.,'SRV',1,'xFilial("SRV",SRA->RA_FILIAL) + M->RD_PD','.T.')
	oStructSRD:AddTrigger( aAux[1] , aAux[2] , aAux[3] , aAux[4] )
EndIf

oModel:GetModel('GPEA120_SRD'):SetOptional(.T.)

If !IsInCallStack("FWMILEIMPORT")
	oStructSRD:SetProperty( 'RD_SEMANA'	,MODEL_FIELD_OBRIGAT,.F. )
	oStructSRD:SetProperty( 'RD_PROCES'	,MODEL_FIELD_INIT,FwBuildFeature( STRUCT_FEATURE_INIPAD, 'SRA->RA_PROCES' 	) )
	oStructSRD:SetProperty( 'RD_TIPO2'	,MODEL_FIELD_INIT,FwBuildFeature( STRUCT_FEATURE_INIPAD,  "'I'"			 	) )
EndIf
//Remove campo do modelo de dados
oStructSRD:RemoveField( "RD_MAT" )
oStructSRD:RemoveField( "RD_POSTO" )
oStructSRD:RemoveField( "RD_VSTATUS" )
oStructSRD:RemoveField( "RD_NUMID" )

oModel:SetRelation("GPEA120_SRD",{{"RD_FILIAL",'xFilial("SRD")'},{"RD_MAT","RA_MAT"}},SRD->(IndexKey()))

//Funcao a ser chamada antes da ativacao do Modelo de Dados.
//Pode ser utilizada para inibir a inicializacao do model.
//Se o retorno for negativo, o Model nao sera exibido e a rotina sera abortada
If !lGp120Auto
	oModel:SetVldActivate( { |oModel| GP120VldIni(oModel,oModel:GetOperation()) } )
EndIf

Return(oModel)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ViewDef   ºAutor  ³Leandro Drumond     º Data ³  12/03/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Definicao da tela do cadastro de acumulados anuais		  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPE                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ViewDef()
Local oModel
Local oStructSRA
Local oStructSRD
Local oView
Local aRemovCmps := {}

oModel:= FwLoadModel("GPEA120")

oView := FWFormView():New()

oView:SetModel(oModel)

oStructSRA := FWFormStruct(2,"SRA",{|cCampo| AllTrim(cCampo)+"|" $ "RA_MAT|RA_NOME|RA_ADMISSA|RA_PROCES|RA_POSTO"})
oStructSRA:SetNoFolder()

oView:AddField( "GPEA120_SRA" , oStructSRA )

oStructSRD := FWFormStruct(2,"SRD")
oStructSRD:SetProperty("RD_DATARQ" ,MVC_VIEW_PVAR,FwBuildFeature(STRUCT_FEATURE_PICTVAR,"'@R 99/9999'"))
oView:AddGrid(  "GPEA120_SRD" , oStructSRD )

oStructSRD:RemoveField( "RD_MAT" )
oStructSRD:RemoveField( "RD_POSTO" )
oStructSRD:RemoveField( "RD_VSTATUS" )
oStructSRD:RemoveField( "RD_NUMID" )
oStructSRD:RemoveField( "RD_MES" )
oStructSRD:RemoveField( "RD_IDCMPL" )

oView:SetViewProperty("GPEA120_SRA","OnlyView") //Somente visualizacao. Nao permite edicao dos campos do cabecalho (SRA)

oView:createHorizontalBox("FORMFIELD",15)
oView:createHorizontalBox("GRID"     ,85)

oView:SetOwnerView( "GPEA120_SRA","FORMFIELD")
oView:SetOwnerView( "GPEA120_SRD","GRID")
oView:SetCloseOnOk({|| .T.})

Return(oView)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³CargaSRD   ºAutor  ³Leandro Drumond    º Data ³  12/03/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Efetua a carga dos dados da SRD filtrados conforme SX1	  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPE                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function CargaSRD(oGrid)
Local aRet 			:= {}
Local aRet2			:= {}
Local nX			:= 0
Local nPosPD		:= 0
Local nPosDesc		:= 0
Local nPosDat		:= 0

aRet2 := FormLoadGrid(oGrid)

If lGp120Auto .Or. IsInCallStack("FWMILEIMPORT")
	Return aRet2
EndIf

nPosPD 	 := oGrid:GetIdField("RD_PD")
nPosDesc := oGrid:GetIdField("RD_DESCPD")
nPosDat  := oGrid:GetIdField("RD_DATARQ")

For nX:= 1 TO Len(aRet2)
	SRD->(MsGoto(aRet2[nX,1]))
	aRet2[nX,2,nPosDesc] := fDesc('SRV',aRet2[nX,2,nPosPD],'RV_DESC')
	aRet2[nX,2,nPosDat] := SubStr(SRD->RD_DATARQ,5,2) + SubStr(SRD->RD_DATARQ,1,4)
	Do Case
		Case mv_par01 == 1 .And. ( SRD->RD_DATARQ >= SubStr(mv_par02,3,4) + SubStr(mv_par02,1,2) .and. SRD->RD_DATARQ <= SubStr(mv_par03,3,4) + SubStr(mv_par03,1,2))
			aAdd(aRet,aClone(aRet2[nX]))
		Case mv_par01 == 2 .And. ( DtoS(SRD->RD_DATPGT) >= DtoS(mv_par04) .and. DtoS(SRD->RD_DATPGT) <= DtoS(mv_par05) )
			aAdd(aRet,aClone(aRet2[nX]) )
		Case mv_par01 == 3 .And. ( SRD->RD_PERIODO >= mv_par06 .and. SRD->RD_PERIODO <= mv_par07 )
			aAdd(aRet,aClone(aRet2[nX])  )
	EndCase
Next

Return aRet
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GP120VldIniºAutor  ³Leandro Drumond    º Data ³  12/03/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Pre-validacao do model					    			  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPE                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GP120VldIni(oModel,nOperacao)
Local cFim			:= ""
Local cIni			:= ""
Local cAnoDe 	 	:= ""
Local cMesDe 	 	:= ""
Local cAnoAte 	 	:= ""
Local cMesAte 	 	:= ""
Local cAnoIni 	 	:= ""
Local cMesIni 	 	:= ""
Local cKey			:= ""
Local cMat			:= SRA->RA_MAT
Local cFilSRD 		:= xFilial("SRD")
Local nIndex		:= 0
Local nParam		:= 0
Local lRet			:= .T.

If IsInCallStack("FWMILEIMPORT")
	Return lRet
EndIf

lRet := Pergunte("GPA121",.T.)

If lRet
	For nParam := 1 to 7
		If !lRet
			Exit
		EndIf
		lRet := gp120ValidParam(mv_par01, STR(nParam,1,0), &("mv_par0" + STR(nParam,1,0)))
	Next nParam
EndIf


Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Gp120Grava ºAutor  ³Leandro Drumond    º Data ³  14/03/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Auxilia na gravacao do modelo					  			  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPE                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Gp120Grava( oModel )
Local aSaveLines := FWSaveRows( oModel )
Local cDatArq	 := ""
Local nOperacao  := oModel:GetOperation()
Local oGrid 	 := oModel:GetModel("GPEA120_SRD")
Local nX		 := 0
Local nTamGrid	 := oGrid:Length()

If !IsInCallStack("FWMILEIMPORT")
	If nOperacao == 4
		For nX := 1 to nTamGrid
			oGrid:GoLine(nX)
			If oGrid:IsUpdated() .or. oGrid:IsInserted()
				cDatArq := oGrid:GetValue("RD_DATARQ")
				oGrid:SetValue("RD_DATARQ",SubStr(cDatArq,3,4) + SubStr(cDatArq,1,2)) //Ajusta para gravar corretamente no banco # AAAAMM
				oGrid:SetValue("RD_MES",SubStr(cDatArq,1,2)) //Ajusta para gravar o mês
				oGrid:SetValue("RD_TIPO2","I") //Ajusta para gravar o Tipo2
			Endif
		Next nX
	EndIf
EndIf

FWRestRows( aSaveLines, oModel )

FWFormCommit( oModel )

Return .T.
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³                                                                       ³±±
±±³                                                                       ³±±
±±³                   ROTINAS DE CRITICA DE CAMPOS                        ³±±
±±³                                                                       ³±±
±±³                                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp120LinOk³ Autor ³ J. Ricardo            ³ Data ³ 20.02.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Critica linha digitada                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gp120LinOk(oGrid)

Local lRet		:= .T.
Local cTipo1	:= ""
Local cInicio	:= ""
Local cFim		:= ""
Local cAnoMes	:= ""
Local cMeses	:= "01_02_03_04_05_06_07_08_09_10_11_12_13"
Local nHoras	:= 0
Local nValor	:= 0
Local nLimDe	:= 0
Local nLimAte	:= 0

If !(FunName() $ "GPEA120*GPEA120M*CFGA600")
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Carrega Perguntas do Periodo a ser Validado                  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If !lGp120Auto .And. !IsInCallStack("FWMILEIMPORT")
		Pergunte("GPA121",.F.)

		//Competencia
		If mv_par01 == 1
			cInicio := ( Subst( mv_par02, -4 ) + Subst( mv_par02 , 1, 2 )  )
			cFim	:= ( Subst( mv_par03, -4 ) + Subst( mv_par03 , 1, 2 )  )

		//Data de pagamento
		ElseIf mv_par01 == 2
			cInicio	:= mv_par04
		   	cFim	:= mv_par05

		//Periodo
		ElseIf mv_par01 == 3
			cInicio	:= mv_par06
		   	cFim	:= mv_par07
		EndIf
	EndIf

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Monta a Sequencia da Verba "Chave Inicial Codigo da Verba"    ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If !( lLinOk := fMontaSeq(oGrid) )
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Para Linhas Deletadas o Retorno devera ser "True"             ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If ( oGrid:IsDeleted() )
			lLinOk := .T.
		EndIf
		Return( lLinOk )
	EndIf

	If !oGrid:IsDeleted()

		cTipo1 := oGrid:GetValue("RD_TIPO1")
		nHoras := oGrid:GetValue("RD_HORAS")
		nValor := oGrid:GetValue("RD_VALOR")

		//Competencia - a data do arquivo deve estar entre as compentencias DE e ATE
		//				informadas na entrada da tela
		If !lGp120Auto
			If mv_par01 == 1
		    	cAnoMes := ( Subst( oGrid:GetValue("RD_DATARQ"), -4 ) + Subst( oGrid:GetValue("RD_DATARQ") , 1, 2 )  )
		        IF cAnoMes < cInicio .or. cAnoMes > cFim
					Help(" ",1,"GPA120DTAR")
		           	lRet := .F.
		   		EndIf
				If !( Subst( cAnoMes , -2 ) $ cMeses )
					Help(" ",1,"GPA120MESI")
		           	lRet := .F.
				EndIf
			EndIf

			//Data de Pagamento - a data de pagamento deve estar entre as datas DE e ATE
			//					  informadas na entrada da tela
			If mv_par01 == 2
		    	IF oGrid:GetValue("RD_DATPGT") < cInicio .or. oGrid:GetValue("RD_DATPGT") > cFim
					Help(" ",1,"GPA120DTPG")
		           	lRet := .F.
		        EndIf
			EndIf
		EndIf

		IF cTipo1 $ "DH" .and. nHoras = 0 .and. lRet
			Help(" ",1,"A040SHORAS")
			lRet := .F.
		EndIf

		IF nHoras == 0 .and. nValor == 0 .and. lRet
			Help(" ",1,"A040SVAHOR")
			lRet := .F.
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Verifica Se a data de referencia foi alterada. So podera ser ³
		³ alterada se a verba tiver opcao de lancamento diario. Senao  ³
		³ a data deve ser sempre o ultimo dia do periodo informado.    ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If !Empty(oGrid:GetValue("RD_PD")) .and. !Empty(oGrid:GetValue("RD_DTREF")) .and. lRet
			If ( oGrid:GetValue("RD_DTREF") <> gp120RetDtRef(oGrid:GetValue("RD_PERIODO"), .F., oGrid:GetValue("RD_SEMANA"), oGrid:GetValue("RD_ROTEIR")) )

				If ( PosSRV(oGrid:GetValue("RD_PD") , SRA->RA_FILIAL , "RV_LCTODIA" ) == "N" )
					Help(" ",1,"GPA120REF")
					lRet := .F.
				EndIf
			EndIf
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Verifica Se a Quantidade de Horas ou Valor Estao  conforme os³
		³ limites do cadastro de verbas.							   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		// Valor
		If lRet .and. (cTipo1 $ "V")
			nLimDe  := PosSRV(oGrid:GetValue("RD_PD") , SRA->RA_FILIAL , "RV_VLIMDE" )
			nLimAte := PosSRV(oGrid:GetValue("RD_PD") , SRA->RA_FILIAL , "RV_VLIMATE" )
			If (nLimDe > 0 .and. nLimAte > 0) .and. !(oGrid:GetValue("RD_VALOR") >= nLimDe .and. oGrid:GetValue("RD_VALOR") <= nLimAte)
				Help(" ",1,"GPA120LIM")
				lRet := .F.
			EndIf

		// Horas
		ElseIf lRet .AND. (cTipo1 $ "DH")
			nLimDe  := PosSRV(oGrid:GetValue("RD_PD") , SRA->RA_FILIAL , "RV_RLIMDE" )
			nLimAte := PosSRV(oGrid:GetValue("RD_PD") , SRA->RA_FILIAL , "RV_RLIMATE" )

			If (nLimDe > 0 .and. nLimAte > 0) .and. !(oGrid:GetValue("RD_HORAS") >= nLimDe .and. oGrid:GetValue("RD_HORAS") <= nLimAte)
				Help(" ",1,"GPA120LIM")
				lRet := .F.
			EndIf
	  	EndIf

		If (lRet)
			//Periodo e Numero de Pagamento
			lRet := gp120NrPagtoValid(oGrid:GetValue("RD_SEMANA"))
		EndIf
	EndIf
Else
	lRet := fMontaSeq(oGrid)
EndIf

Return( lRet )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³ fPosSRD  ³ Autor ³ Marinaldo de Jesus 	  ³ Data ³11/08/2000³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Posicionar Cursor em aCols de Acordo com o Tipo da Verba   ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³ fPosSRR( cCampo , lPos )                   				³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³ cCampo - Campo em que o Cursor esta Posicionado            ³
³          ³ lPos   - .T. para posicionar na Coluna Correspondente      ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Generico 												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function fPosSRD( cCampo , lPos )

Local nPosPD	:= 0
Local nPosTip	:= 0
Local nPosHrs	:= 0
Local nPosVal	:= 0
Local nPulos	:= 0
Local cRetVar	:= ""
Local cTipo		:= ""

lPos := IF(lPos == NIL .or. ValType( lPos ) != "L" ,.F.,lPos)

If !lPos ; 	Return( .T. ) ; EndIf

If !( FunName() == "GPEA120" .or. FunName() == "GPEA120M" ) .and. Type( "aCols" ) != "U" .and. Type( "aHeader" ) != "U"

	cRetVar	:= &( ReadVar() )

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Verifica o Tipo da Verba (Horas,Dias,Valor ou Fixo)           ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	IF "RD_PD" $ Upper( AllTrim( cCampo ) )
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Se a Verba nao Estiver Cadastrada.                            ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If  ( cTipo := AllTrim(PosSRV(  cRetVar , SRA->RA_FILIAL , "RV_TIPO" )) ) == "@"
			Return(.T.)
		EndIf
	Else
		cTipo := cRetVar
	EndIf

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Verifica a Posicao das Colunas em aHeader.                    ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	nPosPD	:= GdFieldPos( "RD_PD"  )
	nPosTip := GdFieldPos( "RD_TIPO1" )
	nPosHrs := GdFieldPos( "RD_HORINFO" )
	nPosVal := GdFieldPos( "RD_VALINFO" )

	If "RD_PD" $ Upper( AllTrim( cCampo ) )

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Altera Tipo da Verba em aCols para Substituir na  Redigitacao ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If aCols[n,nPosTip] != cTipo
			aCols[n,nPosTip] := cTipo
	    EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Quantidade de Vezes a Saltar                                  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If Empty(cTipo)				//Se o Tipo da Verba nao Estiver Definido
			nPulos :=  ( nPosTip - nPosPD ) - 1
		ElseIf ( cTipo $ "H_D" )	//Se o Tipo da Verba For Horas ou Dias
			nPulos := ( nPosHrs - nPosPD ) - 1
		ElseIf ( cTipo $ "F_V" )	//Se o Tipo da Verba For Fixo ou Valor
			nPulos :=  ( nPosVal - nPosPD ) - 1
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Garante o Posicionamento na Coluna Chamadora da Funcao.       ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		While oGet:oBrowse:nColPos <> nPosPD
	    	  If oGet:oBrowse:nColPos > nPosPD
			  		oGet:oBrowse:GoLeft()
			  ElseIf oGet:oBrowse:nColPos < nPosPD
					oGet:oBrowse:GoRight()
			  EndIf
		EndDo

	ElseIf "RD_TIPO1" $ Upper( AllTrim( cCampo ) )
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Quantidade de Vezes a Saltar                                  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If Empty(cTipo)				//Se o Tipo da Verba nao Estiver Definido
			Return( .T. )
		ElseIf ( cTipo $ "H_D" )	//Se o Tipo da Verba For Horas ou Dias
				nPulos := ( nPosHrs - nPosTip ) - 1
		ElseIf ( cTipo $ "F_V" )	//Se o Tipo da Verba For Fixo ou Valor
				nPulos :=  ( nPosVal - nPosTip ) - 1
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Garante o Posicionamento na Coluna Chamadora da Funcao.       ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		While oGet:oBrowse:nColPos <> nPosTip
	    	  If oGet:oBrowse:nColPos > nPosTip
				  	oGet:oBrowse:GoLeft()
			  ElseIf oGet:oBrowse:nColPos < nPosTip
					oGet:oBrowse:GoRight()
			  EndIf
		EndDo

	EndIf

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Posiciona Ponteiro na Coluna Correspondente                   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	While nPulos <> 0
		If nPulos > 0
			oGet:oBrowse:GoRight()
	  	    nPulos--
	    ElseIf nPulos < 0
    		oGet:oBrowse:GoLeft()
			nPulos++
		EndIf
	EndDo

EndIf

Return( .T. )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³ fIncSrd  ³ Autor ³ Marinaldo de Jesus 	  ³ Data ³14/08/2000³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Carregar Incidencias das Verbas no SRD                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³ fIncSrd()                                  				³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Generico 												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function fIncSrd()

Local oModel
Local oGrid
Local nPosInss	:= 0
Local nPosFgts	:= 0
Local nPosIr	:= 0
Local cRetVar	:= ""


If FunName() == "GPEA120" .or. FunName() == "GPEA120M" .Or. !IsInCallStack("FWMILEIMPORT")
	oModel 	:= FWModelActive()
	oGrid 	:= oModel:GetModel( 'GPEA120_SRD' )

	PosSRV( oGrid:GetValue("RD_PD") , SRA->RA_FILIAL )

	If oGrid:GetIdField("RD_INSS") > 0
		oGrid:LoadValue("RD_INSS",SRV->RV_INSS)
	EndIf
	If oGrid:GetIdField("RD_FGTS") > 0
		oGrid:LoadValue("RD_FGTS",SRV->RV_FGTS)
	EndIf
	If oGrid:GetIdField("RD_IR") > 0
		oGrid:LoadValue("RD_IR"	,SRV->RV_IR)
	EndIf
	If oGrid:GetIdField("RD_DESCPD") > 0
		oGrid:LoadValue("RD_DESCPD"	,SRV->RV_DESC)
	EndIf
	If oGrid:GetIdField("RD_TIPO1") > 0
		oGrid:LoadValue("RD_TIPO1"	,SRV->RV_TIPO)
	EndIf

ElseIf Type( "aCols" ) != "U" .and. Type( "aHeader" ) != "U"

	cRetVar	:= &( ReadVar() )

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Posiciona na Verba e Retorna para Retorno das Incidencias     ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	PosSrv(  cRetVar , SRA->RA_FILIAL )

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Verifica a Posicao das Colunas em aHeader.                    ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	nPosInss	:= GdFieldPos( "RD_INSS" )
	nPosFgts	:= GdFieldPos( "RD_FGTS" )
	nPosIr		:= GdFieldPos( "RD_IR" )

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Carrega Incidencias da Verba                                  ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If nPosInss > 0
		aCols[ n , nPosInss ] := SRV->RV_INSS	//Incidencia do Inss
	EndIf
	If nPosFgts > 0
		aCols[ n , nPosFgts ] := SRV->RV_FGTS	//Incidencia do Fgts
	EndIf
	If nPosIr > 0
		aCols[ n , nPosIr   ] := SRV->RV_IR	//Incidencia do Ir
	EndIf

EndIf

Return( .T. )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFunction  ³gp120ValidParamºAutor  ³Tatiane Vieira Matiasº Data ³  23/08/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida os parametros informados no SX1 - GPA121            		º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA120                                                    		º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function gp120ValidParam(cTpConsulta, cCampo, cConteudoCpo)

Local lRet 		:= .T.

If cCampo == "TPCONSULTA" .OR. cCampo == "1"
	If Empty(cTpConsulta)
		lRet := .F.
	EndIf

//Competencia
ElseIf cCampo == "COMPETENCIADE"  .OR. cCampo == "2" .OR. ;
       cCampo == "COMPETENCIAATE" .OR. cCampo == "3"
	If cTpConsulta == 1
		//Empty
		If Empty(cConteudoCpo)
			lRet := .F.
		Else
			//Validar Tamanho (MMAAAA)
			If Len(AllTrim(cConteudoCpo)) <> 6
       	    	lRet := .F.
   			EndIf

			//Validar Mes/Ano
			IF CtoD("01/" + Subst( cConteudoCpo, 1, 2 ) + "/" + Subst(cConteudoCpo, -4)) == CTOD("//")
            	lRet := .F.
			EndIf

			//Validar Mes/Ano Ate > Mes/Ano De
			If cCampo == "COMPETENCIAATE" .OR. cCampo == "3"
				If(Subst(cConteudoCpo, -4) + Subst(cConteudoCpo, 1, 2)) < (Subst(mv_par02, -4) + Subst(mv_par02, 1, 2))
	    	      	lRet := .F.
				EndIf
			EndIf
		EndIf
	EndIf

//Data de Pagamento
ElseIf cCampo == "DTPAGTODE" .OR. cCampo == "4" .OR. ;
	   cCampo == "DTPAGTOATE" .OR. cCampo == "5"
	If cTpConsulta == 2
		//Empty
		If Empty(cConteudoCpo)
			lRet := .F.
		Else
			//Validar Data Ate > Data De
			If cCampo == "DTPAGTOATE" .OR. cCampo == "5"
				If cConteudoCpo < mv_par04
	    	      	lRet := .F.
				EndIf
			EndIf
		EndIf
	EndIf

//Periodo
ElseIf cCampo == "PERIODODE" .OR. cCampo == "6" .OR. ;
	   cCampo == "PERIODOATE" .OR. cCampo == "7"
	If cTpConsulta == 3
	     //Empty
		If Empty(cConteudoCpo)
			lRet := .F.
		Else
			//Validar se o periodo informado esta na consulta padrao
			If cCampo == "PERIODODE" .OR. cCampo == "6"
				lRet := gp120PerValid(cConteudoCpo, NIL, NIL)
			Else
				//Validar se o periodo informado esta na consulta padrao e
				//Validar Periodo Ate > Periodo De, atraves do campo mes e
				// ano de cada periodo.
				lRet := gp120PerValid(mv_par06, cConteudoCpo, NIL)
			EndIf
		EndIf
	EndIf
EndIf

Return ( lRet )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³gp120RetDtRef ³ Autor ³ Tatiane Matias        ³ Data ³20/08/2004³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³retorna data de referencia do periodo informado             	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ³
³Sintaxe   ³gp120RetDtRef(cPeriodo, lDataIni)								³			³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³lDataIni - indica se retorna o primeiro dia do periodo (.T.).   ³
³          ³        Se esse parametro nao for passado (NIL ou .F.) essa  	³
³          ³  		funcao ira retornar o ultimo dia do periodo informado.  ³
³          ³cPeriodo - periodo informado.                                   ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³Data de referencia. Primeiro dia do periodo, se lDataIni = .T.  ³                                                          								   ³
³          ³ ou ultimo dia do periodo.                                   	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³Gpea120LinOk                                                    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function gp120RetDtRef(cPeriodo, plDataIni, cSemana, cRoteiro )

Local 	aArea		:= GetArea()
Local 	lGpea120    := FunName() $ "GPEA120*GPEA120M*CFGA600"
Local 	dDataRef   	:= CtoD("")
Local 	aCols		:= If(!lGpea120 , (oGet:aCols) , )
Local 	lRotBlank
Local 	oModel

If lGpea120
	oModel 	:= FWModelActive()
EndIf

DEFAULT plDataIni  	:= .F.
DEFAULT cPeriodo	:= If(!lGpea120 ,aCols[n, GdFieldPos("RD_PERIODO")] , oModel:GetModel('GPEA120_SRD'):GetValue("RD_PERIODO") )
DEFAULT cSemana		:= If(!lGpea120 ,aCols[n, GdFieldPos("RD_SEMANA")]  , oModel:GetModel('GPEA120_SRD'):GetValue("RD_SEMANA")  )
DEFAULT cRoteiro	:= If(!lGpea120 ,aCols[n, GdFieldPos("RD_ROTEIR")]  , oModel:GetModel('GPEA120_SRD'):GetValue("RD_ROTEIR")  )

fPosPeriodo( xFilial("RCH"), SRA->RA_PROCES, cPeriodo, cSemana, cRoteiro,, @lRotBlank, 4 )

If lRotBlank
	cRoteiro := Space( GetSx3Cache( "RCH_ROTEIR", "X3_TAMANHO" ) )
EndIf
If plDataIni
	dDataRef := gp240RetCont("RCH", 1, xFilial("RCH") + SRA->RA_PROCES + cPeriodo + cSemana + cRoteiro, "RCH_DTINI",  "!Empty(RCH->RCH_DTFECH)")
Else
	dDataRef := gp240RetCont("RCH", 1, xFilial("RCH") + SRA->RA_PROCES + cPeriodo + cSemana + cRoteiro, "RCH_DTFIM",  "!Empty(RCH->RCH_DTFECH)")
EndIf

RestArea( aArea )

Return (Iif(Empty(dDataRef), CTOD("//"), dDataRef))

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³gp120PerValid ³ Autor ³ Tatiane Matias        ³ Data ³20/08/2004³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Valida o periodo informado             	                        ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ³
³Sintaxe   ³gp120PerValid(cPerDe, cPerAte, cPerGet)							³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³cPerDe  - Periodo DE informado na tela de parametros (SX1)      ³
³          ³cPerAte - Periodo ATE informado na tela de parametros (SX1)  	³
³          ³cPerGet - Periodo informado na GetDados                         ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³gp120ValidParam                                                 ³
³          ³X3_VALID do campo RD_PERIODO                                    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function gp120PerValid(cPerDe, cPerAte, cPerGet)

Local lRet 		:= .T.
Local nCont		:= 0
Local cAnoDe	:= ""
Local cAnoAte	:= ""
Local cAnoGet	:= ""
Local cMesDe	:= ""
Local cMesAte	:= ""
Local cMesGet	:= ""
Local cPer 		:= If( !Empty(cPerGet), cPerGet, If( Empty(cPerAte), cPerDe, cPerAte) )

If (IsInCallStack("FWMILEIMPORT"))
	DbSelectArea("SRA")
	SRA->(DbSetOrder(1))
	SRA->(DbSeek(M->RA_FILIAL + M->RA_MAT))
Endif

//Validar EXISTCPO
dbSelectArea("RCH")
dbSetOrder(2)
DbSeek( xFilial("RCH") + SRA->RA_PROCES + cPer , .T. )

While RCH->( !Eof() .AND. RCH_FILIAL+RCH_PROCES+RCH_PER == xFilial("RCH") + SRA->RA_PROCES + cPer )
	If !Empty(RCH_DTFECH)
		nCont++
		Exit
	EndIf
	RCH->(DbSkip())
EndDo
If nCont == 0
	Help( "", 1, "REGNOIS" )
	lRet := .F.
EndIf

//Validar Periodo Ate > Periodo De
If !lGp120Auto .AND. !Empty(cPerDe) .AND. !Empty(cPerAte) .AND. lRet .And. !IsInCallStack("FWMILEIMPORT")
	// retorna ano e mes do periodo DE informado.
	cAnoDe 	 := gp240RetCont("RCH", 1, xFilial("RCH") + SRA->RA_PROCES, "RCH_ANO", "!Empty(RCH->RCH_DTFECH) .AND. (RCH->RCH_PROCES == '" + SRA->RA_PROCES + "') .AND. (RCH->RCH_PER == '" + cPerDe + "')")
	cMesDe 	 := gp240RetCont("RCH", 1, xFilial("RCH") + SRA->RA_PROCES, "RCH_MES", "!Empty(RCH->RCH_DTFECH) .AND. (RCH->RCH_PROCES == '" + SRA->RA_PROCES + "') .AND. (RCH->RCH_PER == '" + cPerDe + "')")

	// retorna ano e mes do periodo ATE informado.
	cAnoAte 	 := gp240RetCont("RCH", 1, xFilial("RCH") + SRA->RA_PROCES, "RCH_ANO", "!Empty(RCH->RCH_DTFECH) .AND. (RCH->RCH_PROCES == '" + SRA->RA_PROCES + "') .AND. (RCH->RCH_PER == '" + cPerAte + "')")
	cMesAte 	 := gp240RetCont("RCH", 1, xFilial("RCH") + SRA->RA_PROCES, "RCH_MES", "!Empty(RCH->RCH_DTFECH) .AND. (RCH->RCH_PROCES == '" + SRA->RA_PROCES + "') .AND. (RCH->RCH_PER == '" + cPerAte + "')")

	If !Empty(cPerGet) .AND. mv_par01 == 3
		// retorna ano e mes do periodo ATE informado.
		cAnoGet 	 := gp240RetCont("RCH", 1, xFilial("RCH") + SRA->RA_PROCES, "RCH_ANO", "!Empty(RCH->RCH_DTFECH) .AND. (RCH->RCH_PROCES == '" + SRA->RA_PROCES + "') .AND. (RCH->RCH_PER == '" + cPerGet + "')")
		cMesGet 	 := gp240RetCont("RCH", 1, xFilial("RCH") + SRA->RA_PROCES, "RCH_MES", "!Empty(RCH->RCH_DTFECH) .AND. (RCH->RCH_PROCES == '" + SRA->RA_PROCES + "') .AND. (RCH->RCH_PER == '" + cPerGet + "')")

		If (cAnoGet + cMesGet) < (cAnoDe + cMesDe) .OR. (cAnoGet + cMesGet) > (cAnoAte + cMesAte)
			Help(" ",1,"GPA120PER2")
			lRet := .F.
		EndIf

	ElseIf (cAnoAte + cMesAte) < (cAnoDe + cMesDe)
		Help(" ",1,"GPA120PER1")
		lRet := .F.
	EndIf
EndIf

cPeriodo := cPerGet

Return ( lRet )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³gp120NrPagtoValid ³ Autor ³ Tatiane Matias        ³ Data ³30/08/2004³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Valida o numero de pagamento informado 	                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ³
³Sintaxe   ³gp120NrPagtoValid()					    		                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³X3_VALID do campo RD_SEMANA                                         ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function gp120NrPagtoValid(cNumPagto)

Local aSRDCols		:= {}
Local cChkNrPagto	:= GETMV("MV_CATNPGT")
Local cPeriodo		:= ""
Local lRet			:= .T.
Local nPosPeriodo 	:= 0
Local nCont			:= 0
Local nIndex		:= 0
Local oModel
Local oGrid

If FunName() $ "GPEA120*GPEA120M*CFGA600"
	oModel 	:= FWModelActive()
	oGrid 	:= oModel:GetModel( 'GPEA120_SRD' )
	cPeriodo:= oGrid:GetValue("RD_PERIODO")
Else
	aSRDCols		:= oGet:aCols
	nPosPeriodo 	:= GdFieldPos( "RD_PERIODO", aHeader )
	cPeriodo		:= aSRDCols[n, nPosPeriodo]
EndIf

If (IsInCallStack("FWMILEIMPORT"))
	DbSelectArea("SRA")
	SRA->(DbSetOrder(1))
	SRA->(DbSeek(M->RA_FILIAL + M->RA_MAT))
Endif

If !(SRA->RA_CATFUNC $ cChkNrPagto)
	lRet := ExistCpo("RCH", SRA->RA_PROCES + cPeriodo + cNumPagto, 3)

	If lRet
		dbSelectArea("RCH")
		nIndex	:= IndexOrd()
		dbSetOrder(1)
		DbSeek( xFilial("RCH") + SRA->RA_PROCES + cPeriodo + cNumPagto, .T. )

		While RCH->( !Eof() .AND. RCH_FILIAL+RCH_PROCES+RCH_PER+RCH_NUMPAG == xFilial("RCH") + SRA->RA_PROCES + cPeriodo + cNumPagto)
			If !Empty(RCH_DTFECH)
				nCont++
				Exit
			EndIf
			RCH->(DbSkip())
		EndDo
		If nCont == 0
			EXISTCPO("RCH", SRA->RA_PROCES + cPeriodo + cNumPagto + "11111111" ,3)
			lRet := .F.
		EndIf
		dbSetOrder(nIndex)
	EndIf

EndIf

Return ( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³GPEA120ValidRot ³Autor³Tatiane Matias      ³ Data ³01/10/2004³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Verifica se o roteiro foi informado no cadastro de periodo.  ³
³          ³Caso tenha sido informado, nao podera ter lancamentos para   ³
³          ³roteiros diferentes do cadastrado no periodo.                ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³Boolean                                             	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³X3_VALID do campo RD_ROTEIR                                  ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function gp120RotValid(cRoteiro)

Local oModel 	:= FWModelActive()
Local oGrid 	:= oModel:GetModel( 'GPEA120_SRD' )
Local nOrdem 	:= 1
Local cRotPer	:= ""
Local lRet		:= .T.

If MV_MODFOL == "1"
	nOrdem 	:= RetOrdem("RCF","RCF_FILIAL+RCF_PROCES+RCF_PER+RCF_SEMANA+RCF_ROTEIR+RCF_TNOTRA")
	cRotPer	:= gp240RetCont("RCF", nOrdem, xFilial("RCF") + SRA->RA_PROCES + oGrid:GetValue("RD_PERIODO"), "RCF_ROTEIR")
Else
	nOrdem 	:= RetOrdem("RCH","RCH_FILIAL+RCH_PROCES+RCH_PER+RCH_NUMPAG+RCH_ROTEIR")
	cRotPer	:= gp240RetCont("RCH", nOrdem, xFilial("RCH") + SRA->RA_PROCES + oGrid:GetValue("RD_PERIODO") + oGrid:GetValue("RD_SEMANA"), "RCH_ROTEIR")
EndIf

If !Empty(cRotPer) .and. (cRoteiro <> cRotPer)
 	Help(" ",1,"NOLANCROT")
	lRet := .F.
EndIf

Return ( lRet )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³gp120RetRot   ³ Autor ³ Tatiane Matias        ³ Data ³14/09/2005³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³retorna o roteiro do periodo e numero de pagamento informado  	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ³
³Sintaxe   ³gp120RetRot()													³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³Roteiro                                                      	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³Gpea120LinOk                                                    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function gp120RetRot()

Local oModel 	:= FWModelActive()
Local oGrid 	:= oModel:GetModel( 'GPEA120_SRD' )
Local cPeriodo	:= oGrid:GetValue("RD_PERIODO")
Local cSemana	:= oGrid:GetValue("RD_SEMANA")
Local cRot		:= ""

cRot := gp240RetCont("RCH", 1, xFilial("RCH") + SRA->RA_PROCES + cPeriodo + cSemana, "RCH_ROTEIR",  "!Empty(RCH->RCH_DTFECH)")

Return (cRot)

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³fMontaSeq ³ Autor ³ Marinaldo de Jesus    ³ Data ³04/08/2000³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Montar Sequencia Automatica das Verbas                      ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fMontaSeq()                                                 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³Logico por se Tratar de Validacao                           ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³ Generico                                                   ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fMontaSeq(oGrid)
Local aSaveLine 	:= FWSaveRows()
Local cNumSeq		:= ""
Local cStrCols 		:= ""
Local cStrCopy		:= ""
Local lRet			:= .T.
Local nX			:= 0
Local nSeq			:= 0
Local nTamSeq		:= TamSX3("RD_SEQ")[1]
Local nLenCols		:= oGrid:Length()

If IsInCallStack("FWMILEIMPORT")
	Return lRet
EndIf

cNumSeq		:= PosSrv( oGrid:GetValue("RD_PD") , SRA->RA_FILIAL, "RV_QTDLANC" )

If !lItemClVl
	cStrCols 	:= oGrid:GetValue("RD_PD") + oGrid:GetValue("RD_CC") + oGrid:GetValue("RD_SEMANA") + oGrid:GetValue("RD_PERIODO")
else
	cStrCols 	:= oGrid:GetValue("RD_PD") + oGrid:GetValue("RD_CC") + oGrid:GetValue("RD_ITEM") + oGrid:GetValue("RD_CLVL") + oGrid:GetValue("RD_SEMANA") + oGrid:GetValue("RD_PERIODO")
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Renumera Sequencia de Lancamentos                             ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
For nX := 1 To nLenCols
	oGrid:GoLine(nX)
    If !lItemClVl
		cStrCopy := oGrid:GetValue("RD_PD") + oGrid:GetValue("RD_CC") + oGrid:GetValue("RD_SEMANA") + oGrid:GetValue("RD_PERIODO")
    Else
		cStrCopy := oGrid:GetValue("RD_PD") + oGrid:GetValue("RD_CC") + oGrid:GetValue("RD_ITEM") + oGrid:GetValue("RD_CLVL") + oGrid:GetValue("RD_SEMANA") + oGrid:GetValue("RD_PERIODO")
    EndIf
	If !( oGrid:IsDeleted() )
		If ( cStrCopy == cStrCols )
			nSeq++
			If ( lRet := ( nSeq <= Val( cNumSeq ) )  .or. ( nSeq == 1 .and. Val(cNumSeq) == 0 ) )
				oGrid:SetValue("RD_SEQ",StrZero( nSeq , nTamSeq ))
			Else
				Help(" ",1,"A090MAXSEQ")
				Return( lRet )
			EndIf
		EndIf
	Else
		oGrid:SetValue("RD_SEQ",Space( nTamSeq ))
	EndIf
Next nX

FWRestRows( aSaveLine )

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³gp120FiltroºAutor  ³Christiane Vieira   º Data ³ 24/02/2010 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Criação da consulta padrao RCH10                           º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA120                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function gp120Filtro()

Local cFiltro	:= " ! EMPTY(RCH->RCH_DTFECH)"

cFiltro := "@#" + cFiltro + "@#"

Return ( cFiltro )
