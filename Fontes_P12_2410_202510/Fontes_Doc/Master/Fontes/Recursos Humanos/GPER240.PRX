#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPER240.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ GPER240  ³ Autor ³ J.Ricardo                   ³ Data ³ 29/03/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Guia GRPS                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³CHAMADO/REQ³  Motivo da Alteracao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Mohanad     ³28/02/14³ M12RH01   ³Unificacao da Folha V12                    ³±±
±±³            ³        ³  194404   ³                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/ 
Function Gper240()
Local cDesc1 	:= STR0001	// "GUIA DE I.N.S.S. (G.R.P.S.)"
Local cDesc2 	:= STR0002	// "SERA IMPRESSO DE ACORDO COM OS PARAMETROS SOLICITADOS PELO USUARIO."
Local cDesc3 	:= STR0003	// "OBS. DEVER  SER IMPRESSA UMA GUIA PARA CADA TIPO DE CONTRATO."
Local cString	:= "SRZ"	// ALIAS DO ARQUIVO PRINCIPAL (BASE)
Local aOrd   	:= {}    	// ORDEM
Local aOrdBag 	:= {}
Local cArqMov 	:= cAliasMov := ""
Local lGPSDiss	:= .F.
Local lGPSDisMov	:= .F.
Local aTpc		:= {}
Local nx		:= 1

Private aReturn := {STR0004,1,STR0005, 1, 2, 1, "",1 }		//"ZEBRADO"###"ADMINISTRACAO"
Private nomeprog:= "GPER240"
Private aLinha  := {}
Private nLastKey:= 0
Private cPerg   := "GPR240"
Private m_pag   := 1
Private nTamanho:= "M"
Private nColunas:= 132
Private aCRA   		:= {STR0006,STR0007,STR0008}		//"CONFIRMA"###"REPETE"###"ABANDONA"
Private Titulo 		:= STR0009						//"EMISSO GUIA DE RECOLHIMENTO DA PREVIDENCIA SOCIAL"
Private aPerAberto 	:= {}
Private aPerFechado	:= {} 
Private cTipoCont:= 0
Private nQtdGps  := 0
//OBJETOS P/ IMPRESSSAO GRAFICA
Private oFont07,oFont08, oFont10, oFont15, oFont10n, oFont21, oFont12,oFont16
Private oPrint
Private dDataAte
Private cRoteiro := ''
Private cCcInic		:= ""
Private cCcFina		:= ""
Private lDicInter		:= FindFunction("fChkInterm") .And. fChkInterm()

oFont07	:= TFont():New("Courier New",07,07,,.F.,,,,.T.,.F.)
oFont08	:= TFont():New("Courier New",08,08,,.T.,,,,.T.,.F.)		//NEGRITO 
oFont10	:= TFont():New("Courier New",10,10,,.F.,,,,.T.,.F.)
oFont10n:= TFont():New("Courier New",10,10,,.T.,,,,.T.,.F.)
oFont12	:= TFont():New("Courier New",12,12,,.F.,,,,.T.,.F.)		//NORMAL S/NEGRITO
oFont15	:= TFont():New("Courier New",15,15,,.T.,,,,.T.,.F.)
oFont21 := TFont():New("Courier New",21,21,,.T.,,,,.T.,.T.)
oFont16	:= TFont():New("Arial",16,16,,.T.,,,,.T.,.F.)

//VERIFICA AS PERGUNTAS SELECIONADAS
Pergunte("GPR240",.F.)
//  VARIAVEIS UTILIZADAS PARA PARAMETROS                         
//  MV_PAR01        //  FILIAL  DE                               
//  MV_PAR02        //  FILIAL  ATE                              
//  MV_PAR03        //  CENTRO DE CUSTO DE                       
//  MV_PAR04        //  CENTRO DE CUSTO ATE                      
//  MV_PAR05        //  1=C.CUSTO , 2=GRUPO C.CUSTO ,3=FILIAL    
//  MV_PAR06        //  CENTRALIZADO ( S/N )                     
//  MV_PAR07        //  MES E ANO DA COMPETENCIA                 
//  MV_PAR08        //  IMPRIME POR -CC, NIVEL CC E FILIAL       
//  MV_PAR09        //  FOLHA/FERIAS,13O.SALARIO/DISSIDIO/DISSIDIO(PARC)
//  MV_PAR10        //  TP DE CONTRATO                           
//  MV_PAR11        //  OPCAO GUIA GRPS / GPS. / GPS GRAFICA     
//  MV_PAR12        //  CODIGO DE PAGAMENTO.                     
//  MV_PAR13        //  TIPO DE RECIBO (PRE/ZEBRADO)             
//  MV_PAR14        //  ATM / MULTA / JUROS                      
//  MV_PAR15        //  TEXTO 1                                  
//  MV_PAR16        //  TEXTO 2                                  
//  MV_PAR17        //  TEXTO 3                                  
//  MV_PAR18        //  TEXTO 4                                  

//ENVIA CONTROLE PARA A FUNCAO SETPRINT
wnrel:="GPER240"            //NOME DEFAULT DO RELATORIO EM DISCO
SetEnableVias(.T.)     

wnrel:=SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.F., ,.T.,nTamanho )

If	nLastKey == 27
	Return
Endif                

SetDefault(aReturn,cString)

If	nLastKey == 27
	Return
Endif

//CARREGANDO VARIAVEIS MV_PARXX PARA VARIAVEIS DO SISTEMA.
cFilIni       := mv_par01
cFilFim       := mv_par02
cCcIni        := mv_par03
cCcFim        := mv_par04
nPorCc        := mv_par05 
cCentra       := mv_par06
cMesAnoGuia   := mv_par07
dDtRefe		:= ctod( "01/" + SubStr(mv_par07,1,2)+"/"+SubStr(mv_par07,3,4), "DDMMYY" ) 
cCompetencia  := SubStr(mv_par07,1,1)+Space(01)+SubStr(mv_par07,2,1)+Space(04)+SubStr(mv_par07,5,1)+Space(01)+SubStr(mv_par07,6,1)
cNivCCusto    := mv_par08
cTipo         := mv_par09
cTpc          := MV_PAR10
nOpcao        := mv_par11
cCodPag       := PadR(mv_par12,4)
nTipRel       := mv_par13
nValJuros     := mv_par14              
cTexto1		:= mv_par15
cTexto2		:= mv_par16 
cTexto3		:= mv_par17
cTexto4		:= mv_par18
cAnoMes		:= SubStr(mv_par07,3,4)+	SubStr(mv_par07,1,2)
cCcInic		  := cCcIni
cCcFina		  := cCcFim

if empty(cTpc)
	 Help( "" , 1 , OemToAnsi(STR0013) , NIL , OemToAnsi( STR0018 ) , 1 , 0 ) //"Tipo de Contrato não preenchido, informe pelo menos um."
	 return 
endif 
If MV_PAR11 == 1 .AND. MV_PAR13 == 3
	MsgAlert(STR0012, STR0011)  //"A OPÇÃO GPS-GRAFICA SÓ ESTÁ DISPONIVEL QUANDO O TIPO DE GUIA FOR GPS.  VERIFIQUE OS PARÂMETROS TIPO DE GUIA E TIPO DE FORMULÁRIO." ### "AJUSTAR."
	Return
Endif
If lDicInter
	cTpc := Alltrim(Replace(cTpc,"*",""))
EndIf
cTipCont      := cTpc // Armazenar o tipo de contrato selecionado.

//VERIFICA SE EXISTE O ARQUIVO DE FECHAMENTO DO MES INFORMADO
If cTipo  == 2
	cMesAnoGuia := "13" + Substr(cMesAnoGuia,3,4)
Endif

//CARREGAR OS PERIODOS ABERTOS (APERABERTO) E/OU OS PERIODOS FECHADOS (APERFECHADO), DE ACORDO COM UMA DETERMINADA COMPETENCIA
fRetPerComp( SubStr( mv_par07, 1, 2 ), SubStr( mv_par07, 3, 4 ), Nil, Nil, Nil, @aPerAberto, @aPerFechado,,,,,.T.)

//VARIAVEIS UTILIZADAS PARA GERACAO DO SRZ VIA PROCEDURE
lFolPgto	:= ( cTipo == 1 )
lFol13Sl	:= ( cTipo == 2 )
lGPSDiss	:= ( cTipo == 3 )
lGPSDisMov	:= ( cTipo == 4 ) 	//DISSIDIO PARCELADO

//QUANDO FOR DISSIDIO PARCELADO SERA FEITO O MESMO TRATAMENTO PARA A FOLHA, MAS CONSIDERANDO SOMENTE AS VERBAS DE DISSIDIO
//AJUSTE NECESSARIO PORQUE A RHH GERA APENAS O VALOR INTEGRAL
cTipo	:= If(lGPSDisMov, 1, cTipo)

//VERIFICA EXISTENCIA DA NOVA TABELA DE DISSIDIO ACUMULADO (RHH) SE A GPS FOR DE DISSIDIO. SE NAO EXISTIR A TABELA SERA APRESENTADA A MENSAGEM	
//INFORMANDO A NECESSIDADE DA EXECUCAO DO UPDATE 150 PARA A CRIACAO E IMPEDE A EXECUCAO DA GPS DE DISSIDIO ATE QUE O UPDATE SEJA EXECUTADO
If lGPSDiss .and. ! Sx2ChkTable( "RHH" )
	Aviso( STR0013, STR0014, { STR0015 } ) //"Atencao" ## "Execute a opção do compatibilizador referente à criação da nova tabela de Dissídio Acumulado. Para maiores informações verifique respectivo Boletim Técnico." ## "OK"
	Return()
Endif

//VERIFICA SE DEVE GERAR O ARQUIVO SRZ COM OS VALORES DE INSS.
//SOMENTE GERAR SRZ SE NAO FOR GPS DE DISSIDIO.
If !lGPSDiss
	If lFol13Sl
		cRoteiro := fGetCalcRot('6')
	EndIf
	If lDicInter
		cTpc := Alltrim(Replace(cTpc,"*",""))
		For nx := 1 to Len(alltrim(cTpc))
			aAdd(aTpc,val(substr(alltrim(cTpc),nx,1)))
		next nx
		
		If !fGeraFolSRZ(cTipo,aTpc,cMesAnoGuia, aPerAberto, aPerFechado)
			Return()
		EndIf
		cTpc := cTipCont
	Else
		If cTpc = "3"	// SE SELECIONAR TIPO DE CONTRATO 3-AMBOS, GERAR RESUMO DA FOLHA COM OS DOIS TIPOS DE CONTRATO.
		   If !fGeraFolSRZ(cTipo,{1,2},cMesAnoGuia, aPerAberto, aPerFechado)
				Return()
			EndIf
		Else
			If !fGeraFolSRZ(cTipo,{Val(cTipCont)},cMesAnoGuia, aPerAberto, aPerFechado)
				Return()
			EndIf
		EndIf 
	EndIf
EndIf

//VERIFICA SE EXISTE O ARQUIVO DE FECHAMENTO DO MES INFORMADO
If cTipo == 2
	cMesAnoGuia := "13" + Substr(cMesAnoGuia,3,4) 
Endif

// PARA GPS DE DISSIDIO FOI CRIADA UMA NOVA FUNCAO TAMBEM ARMAZENADA NO GPEM240 E
// FOI ACRESCENTADO O MES/ANO COMPETENCIA NA PASSAGEM DE PARAMETROS PARA ESTA GPS
IF ! lGPSDiss
	RptStatus({|lEnd| GPM240Proc(2,@lEnd,wnRel,,cFilIni,cFilFim,cCcIni,cCcFim,cTipCont,lGPSDisMov)},Titulo) //"EMISSAO GUIA DE RECOLHIMENTO DA PREVIDENCIA SOCIAL"
Else
	RptStatus({|lEnd| GPM240PrcD(2,@lEnd,wnRel,,cFilIni,cFilFim,cCcIni,cCcFim,cTipCont,cAnoMes)},Titulo) //"EMISSO GUIA DE RECOLHIMENTO DA PREVIDENCIA SOCIAL"
EndIf

//SELECIONA ARQ. DEFAUT DO SIGA CASO IMP. MOV. ANTERIORES
If !Empty(cAliasMov)
	fFimArqMov( cAliasMov , aOrdBag , cArqMov )
EndIf
Return()