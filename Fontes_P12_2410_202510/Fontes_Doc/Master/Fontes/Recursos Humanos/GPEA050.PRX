#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "GPEA1050.CH"
#INCLUDE "PONCALEN.CH"

#DEFINE Confirma 1
#DEFINE Redigita 2
#DEFINE Abandona 3
Static lInitDesc
Static lRefTrab		:= FindFunction("fRefTrab") .And. fRefTrab("F")
Static nRhTAEEx		:= SuperGetMv( "MV_RHTAEEX",,3 )
Static lTemREQ     	:= nRhTAEEx <> 3 .and. ChkFile("REQ") .and. FindFunction("fDelDocTAE") .and. FindFunction("FwTotvsSign") .and. cPaisLoc $ "BRA|RUS"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPEA050  ³ Autor  ³ J. Ricardo           		  ³ Data ³ 10/08/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastro de Periodos por Empregado                           		³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPEA050()                                                   			³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                     		³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.               		³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³PROGRAMADOR ³ DATA   ³CHAMADO/REQ     ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Leandro Dr. ³01/02/13³RHU210_01_15    ³Unificacao da Folha. Conversao para MVC.  ³±±
±±³            ³        ³M12RH01         ³                                          ³±±
±±³Raquel Hager|19/03/14|M12RH01         |Ajustes devido acesso pela rotina gpea011 ³±±
±±³            |        |RHU210_03_14    |Gestao de Funcionario.                    ³±±
±±³Mohanad     |03/04/14|TPCF37          |Exibe mensagem alertando sobre as         ³±±
±±³            |        |                |consequencias no calculo de Ferias        ³±±
±±³Christiane V³18/07/14³TPXZ82          ³Retirada do botão "Salvar e Incluir Novo",³±±
±±³            ³        ³                ³gerava error log e não precisa existir em ³±±
±±³            ³        ³                ³rotinas que são "pai e filho".            ³±±
±±³Claudinei S.³18/03/15³TRDIG0          ³Incluida a Gpea050Tok() para validar se os³±±
±±³            ³        ³	             ³dias informados de ferias programadas sao ³±±
±±³            ³        ³ 	             ³inferiores a 10 dias.                     ³±±
±±³Mariana M.  ³18/09/15³TTIUQ4          ³Declarada a variável lGp050Auto na função ³±±
±±³            ³        ³ 	             ³Gp050PVal para que não gere o error.log   ³±±
±±³Mariana M.  ³25/09/15³TTIWK2          ³Ajuste de CH                              ³±±
±±³Jonathan Glz³01/10/15³PCREQ-2095      ³Modificacion en la funcion RfDataInit() se³±±
±±³            ³        ³	             ³pone manera correcta la instruccion GoLine³±±
±±³            ³        ³	             ³en la linea 819.							³±±
±±³GSantacruz  ³09/11/15³PCREQ-5930      ³Merge 12.1.8                              ³±±
±±³Mariana M.  ³08/12/15³TTRQEN          ³Ajuste no MenuDef para que a chamada do   ³±±
±±³            ³        ³ 	             ³comando imprimir chame a rotina GPER400, e³±±
±±³            ³        ³ 	             ³não a GPEA600 (que não existe)			³±±
±±³Mariana M.  ³21/12/15³TTUQD2          ³Ajustada a função Gp050DIAF para que pos- ³±±
±±³            ³        ³                ³sibilite informar mais de 30 dias quando  ³±±
±±³            ³        ³                ³houver informação no parametro 46 e quando³±±
±±³            ³        ³                ³o funcionario possuir ferias programadas	³±±
±±³Allyson M   ³07/01/16³TUBROB          ³Ajuste na validacao do cadastro das datas ³±±
±±³            ³        ³ 	             ³de programacao p/ nao considerar o abono  ³±±
±±³            ³        ³ 	             ³actual, en lugar de la fecha del sistema. ³±±
±±³Gabriel A.  ³16/06/16³TVHDD5          ³Ajuste para permitir inclusão via MILE.   ³±±
±±³Raquel Hager³21/06/16³TVFCNJ          ³Ajustes para importação via MILE com      ³±±
±±³            ³        ³ 	             ³adapter em MVC.							³±±
±±³Claudinei S.³05/07/16³TUQEO7    		 ³Ajuste em gp050linok para emitir mensagens³±±
±±³			   ³        ³              	 ³quando os dias de abono forem maiores que ³±±
±±³			   ³        ³              	 ³1/3 das férias, data de inicio das férias ³±±
±±³			   ³        ³              	 ³for um feriado, sábado ou domingo.        ³±±
±±³Raquel H.   ³14/09/16³TVYKSA    	   	 ³Ajuste em gp050linok para emitir msg ao   ³±±
±±³			   ³        ³              	 ³usuário sobre inconsistencia no cadastro  ³±±
±±³			   ³        ³              	 ³de Periodo quanto a roteiro de Férias     ³±±
±±³			   ³        ³              	 ³ativo e uso do array aTabFer quanto este  ³±±
±±³			   ³        ³              	 ³possuir conteudo.                         ³±±
±±³Raquel H.  ³03/11/16³TWCXMD    	   	 ³Inclusão de método SetChAll e acerto no   ³±±
±±³			  ³        ³              	 ³SetRelation de acordo com a Filial do func.³±±
±±³Jonathan Glz³10/01/16³PCREQ-7944      ³Localizacion GPE de Chile p/v12.          ³±±
±±³            ³        ³ 	             ³-Se realiza merge con version 12.1.7, para³±±
±±³            ³        ³ 	             ³estabilizar rutina con cambios de 12.1.9  ³±±
±±³            ³        ³ 	             ³-Se modifica la funcion RfDataInit() para ³±±
±±³            ³        ³ 	             ³Permitir que el campo RF_DATABAS ponga la ³±±
±±³            ³        ³ 	             ³fecha de admisión (RA_ADMISSA) con el año ³±±
±±³Renan Borges³17/03/17³MRH-7062        ³Ajuste na aprovação de solicitação de fé- ³±±
±±³            ³        ³                ³rias, para não gerar error.log.           ³±±
±±³Claudinei S.|28/04/17|MRH-482         |Ajuste na rotina considerar os dias de    ³±±
±±³            |        |                |de direito em casos de regime parcial.    ³±±
±±³Cícero Alves|21/06/17|DRHPAG-2800	 |Correção de erro ao acessar a rotina no   ³±±
±±³            |        |                |México									³±±
±±³Veronica Flo|02/05/19|DMINA-6587 	 |Se eliminan mensaje al confirmar cambios  ³±±
±±³            |        |                |en la función Gp050PVal , asi mismo se    ³±±
±±³            |        |                |elimina  mensaje "Registro modificado con ³±±
±±³            |        |                |éxito" agregando la propiedad de          ³±±
±±³            |        |                |oView:showUpdateMsg(.F.) en la función    ³±±
±±³            |        |                |ViewDef y en la función Gp050ClsOk se     ³±±
±±³            |        |                |quita el lRet=.F. ya que esta indica que  ³±±
±±³            |        |                |no salga del grid al confirmar. (COL)		³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function GPEA050(nOpcAuto, xAutoCab, xAutoItens,  nOpc)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿        
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cFil			:= ""
Local cMat			:= ""
Local nT			:= 0
Local nOpcao 		:= nOpc
Local oBrwSRA
Local oModelAuto
Local nPos	:= 0
Local aAutoFun	:= {}
Private aAutoCab	:= {}
Private aAutoItens  := {}
Private lIncSRA		:= .T.

Private lGp050Auto := If( type("lGp050Auto") == "U", (xAutoCab <> NIl) .or. IsBlind() , lGp050Auto) 

If lGp050Auto .Or. nOpcAuto <> Nil
	dbSelectArea("SRA")
	aAutoCab	:= xAutoCab
	aAutoItens 	:= xAutoItens
	
	If (nT := aScan(aAutoCab,{|x| UPPER(Alltrim(x[1]))=='RF_FILIAL'})) > 0
		cFil := aAutoCab[nT,2]
	Else
		cFil := xFilial("SRA")
	EndIf
	
 	If (nT := aScan(aAutoCab,{|x| UPPER(Alltrim(x[1]))=='RF_MAT'})) > 0
		cMat :=	aAutoCab[nT,2]
	Else
		cMat :=	SRA->RA_MAT	
 	EndIf	
	
	dbSelectArea("SRA")
	dbSetOrder(1)
	If !dbSeek(cFil+cMat)
		AutoGRLog("Tabela "+Alias()+" "+Dtoc(MsDate())+' '+Time() )
		AutoGrLog("RF_FILIAL: "+cFil)
		AutoGrLog("RF_MAT: "+cMat)
		AutoGRLog("Pesquisa nao encontrada com dados acima")
		AutoGRLog(Replicate("-",80))
		Return .F.
	EndIf
	
	nOpcao 		:= If(nOpc<>Nil,nOpc,nOpcao)
	aRotina 	:= MenuDef()	
	
	If lGp050Auto
		
		if(Len(aAutoCab) > 0 )					
			nPos := AScan (aAutoCab[1], {|x|x[1]=='RF_FILIAL'})		
			if(nPos > 0)
				cFil := aAutoCab[1,nPos,2]
				aAdd(aAutoFun,{'RA_FILIAL',cFil,Nil})
			endIf
			nPos := AScan (aAutoCab[1], {|x|x[1]=='RF_MAT'})		
			if(nPos > 0)
				cMat := aAutoCab[1,nPos,2]
				aAdd(aAutoFun,{'RA_MAT',cMat,Nil})
			endIf
		Else		
			aAdd(aAutoFun,{'RA_FILIAL'	,SRA->RA_FILIAL	,Nil})
			aAdd(aAutoFun,{'RA_MAT'		,SRA->RA_MAT		,Nil})
		endIf
		
		oModelAuto  := ModelDef()
		//Executa rotina automatica SEM Interface
		FWMVCRotAuto(oModelAuto,"SRA",nOpcao,{{'GPEA050_SRA',aAutoFun},{"GPEA050_SRF",aAutoCab},{"GPEA050_RHI",aAutoItens}})
	Else
		//Executa rotina COM Interface
		FWExecView(STR0009,'GPEA050', nOpcAuto, , { || .T. } ) //"Controle de Periodos por Empregado"
	EndIf
	
Else
	
	oBrwSRA := FwMBrowse():New()
	oBrwSRA:SetAlias( 'SRA' )
	oBrwSRA:SetDescription(OemToAnsi(STR0009)) 	  //"Controle de Periodos por Empregado"
	cFiltraRh := CHKRH("GPEA050","SRA","1")

	oBrwSRA:AddLegend( "RA_SITFOLH==' '"	, "GREEN"	, STR0027 ) //"Situação Normal"
	oBrwSRA:AddLegend( "RA_RESCRAI$'30/31'"	, "PINK"	, STR0028 ) //"Transferido"
	oBrwSRA:AddLegend( "RA_SITFOLH=='D'"	, "RED"		, STR0029 ) //"Demitido"
	oBrwSRA:AddLegend( "RA_SITFOLH=='A'"	, "YELLOW"	, STR0030 ) //"Afastado"
	oBrwSRA:AddLegend( "RA_SITFOLH=='F'"	, "BLUE"	, STR0031 ) //"Férias"
	
	oBrwSRA:SetMenuDef( 'GPEA050' )
	oBrwSRA:SetChgAll(.F.)
	oBrwSRA:SetFilterDefault(cFiltraRh)   
	    
	oBrwSRA:Activate()
EndIf
                               
Return( NIL )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MenuDef    ³ Autor ³ Leandro Drumond       ³ Data ³ 01/02/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Menu Funcional                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MenuDef()                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()

	Local aRotina   := {}

	ADD OPTION aRotina TITLE STR0004	ACTION 'PesqBrw'			OPERATION 1 ACCESS 0 	//"Pesquisar"
	ADD OPTION aRotina TITLE STR0005	ACTION 'VIEWDEF.GPEA050'	OPERATION 2 ACCESS 0	//"Visualizar"
	ADD OPTION aRotina TITLE STR0032	ACTION 'VIEWDEF.GPEA050'	OPERATION 3 ACCESS 0	//"Atualizar"
	ADD OPTION aRotina TITLE STR0060	ACTION 'GPEA053'			OPERATION 4 ACCESS 0	//"Em Lote"
	ADD OPTION aRotina TITLE STR0011	ACTION 'GPER400'			OPERATION 8 ACCESS 0	//"Imprimir"
	
Return aRotina

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ModelDef   ³ Autor ³ Leandro Drumond       ³ Data ³ 01/02/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Modelo de dados e Regras de Preenchimento               		³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ModelDef()                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ModelDef()
Local oStructSRA
Local oStructSRF
Local oStructRHI
Local oModel
Local bPreRHI := { |oModelGrid, nLine, cAction, cField| gp050DelRHI(oModelGrid, nLine, cAction, cField) }
Local bPreSRF := { |oModelGrid, nLine, cAction, cField| gp050ChkDel(oModelGrid, nLine, cAction, cField) }
Local bPost	:= {|oMdl| Gp050PVal(oMdl)}

oModel:= MpFormModel():New("GPEA050", /*Pre-Validacao*/, /*Pos-Validacao*/, { |oModel| Gp050Commit( oModel ) }/*Commit*/, /*Cancel*/)

oStructSRA := FWFormStruct(1,"SRA",{|cCampo| AllTrim(cCampo)+"|" $ "RA_FILIAL|RA_MAT|RA_NOME|RA_ADMISSA|"})
oModel:AddFields("GPEA050_SRA", /*cOwner*/, oStructSRA , /*Pre-Validacao*/,/*Pos-Validacao*/,/*Carga*/)
oModel:GetModel("GPEA050_SRA"):SetOnlyView( .T. )
oModel:GetModel("GPEA050_SRA"):SetOnlyQuery( .T. )

oStructSRA:SetProperty( 'RA_MAT' 	, MODEL_FIELD_WHEN , {||.F.})
oStructSRA:SetProperty( 'RA_NOME' 	, MODEL_FIELD_WHEN , {||.F.})
oStructSRA:SetProperty( 'RA_ADMISSA', MODEL_FIELD_WHEN , {||.F.})


oStructSRF := FWFormStruct(1,"SRF")
oStructSRF :SetProperty('RF_DFERANT'		,MODEL_FIELD_VALID, {|| fValidSRF('RF_DFERANT')	} )

oModel:AddGrid("GPEA050_SRF", "GPEA050_SRA"/*cOwner*/, oStructSRF , bPreSRF , { |oGrid| gp050LinOk(oGrid) } /* bLinePost*/, { || !(lInitDesc := .F.) }, bPost,/*bLoad*/)
oModel:GetModel("GPEA050_SRF"):SetOptional(.T.)
oModel:GetModel("GPEA050_SRF"):SetUniqueLine( { "RF_DATABAS" , "RF_DATAFIM" , "RF_PD" } )

oStructSRF:SetProperty( 'RF_DESCPD' , MODEL_FIELD_INIT,{|oGrid|If(lInitDesc,FDESC("SRV",If(oGrid:Length()>0,oGrid:GetValue("RF_PD"),SRF->RF_PD),"RV_DESC"),"")})

oModel:SetRelation("GPEA050_SRF",{{"RF_FILIAL",'xFilial("SRF",SRA->RA_FILIAL)'},{"RF_MAT","RA_MAT"}},SRF->(IndexKey()))

If HabilitaAba()
	oStructRHI := FWFormStruct(1,"RHI")
	oModel:AddGrid("GPEA050_RHI", "GPEA050_SRA"/*cOwner*/, oStructRHI , bPreRHI/*bLinePre*/, /* bLinePost*/, /*bPre*/, /*bPost*/,/*bLoad*/)
	oModel:GetModel('GPEA050_RHI'):SetOptional(.T.)
	
	oModel:SetRelation("GPEA050_RHI",{{"RHI_FILIAL",'xFilial("RHI")'},{"RHI_MAT","RA_MAT"}},RHI->(IndexKey()))
EndIf

oModel:SetVldActivate( { |oModel| Gp050IniMod( oModel,oModel:GetOperation() ) } )  

Return(oModel)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ViewDef    ³ Autor ³ Leandro Drumond       ³ Data ³ 01/02/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Visualizador de dados da rotina.               				³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ViewDef()                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ViewDef()

Local oModel
Local oView
Local oStructSRA
Local oStructSRF
Local oStructRHI

oModel     := FwLoadModel("GPEA050")

oView := FWFormView():New()

oView:SetModel(oModel)

oStructSRA := FWFormStruct(2,"SRA",{|cCampo| AllTrim(cCampo)+"|" $ "RA_MAT|RA_NOME|RA_ADMISSA|"})
oStructSRA:SetNoFolder()

oView:AddField( "GPEA050_SRA" , oStructSRA )

oStructSRF := FWFormStruct(2,"SRF")
oView:AddGrid(  "GPEA050_SRF" , oStructSRF )

oStructSRF:RemoveField( "RF_FILIAL" )
oStructSRF:RemoveField( "RF_MAT" )
oStructSRF:RemoveField( "RF_NOME" )
oStructSRF:RemoveField( "RF_ADMISSA" )

oView:SetViewProperty("GPEA050_SRA","OnlyView") //Somente visualizacao. Nao permite edicao dos campos do cabecalho (SRA)

oView:CreateHorizontalBox("FORMFIELD",10)
oView:CreateHorizontalBox("GRID"     ,90)

If HabilitaAba() //Cria abas somente se variavel for TRUE, do contrario monta a tela sem utilizacao de abas
	oStructRHI := FWFormStruct(2,"RHI")
	oView:AddGrid(  "GPEA050_RHI" , oStructRHI )
	oStructRHI:RemoveField( "RHI_FILIAL" )
	oStructRHI:RemoveField( "RHI_MAT" )
	oStructRHI:RemoveField( "RHI_NOME" )
	oStructRHI:RemoveField( "RHI_NUMPAG" )
	oStructRHI:RemoveField( "RHI_PROCES" )
	oStructRHI:RemoveField( "RHI_ROTEIR" )
	oStructRHI:RemoveField( "RHI_PERIOD" )
	oStructRHI:RemoveField( "RHI_DTPAGO" )
	oStructRHI:RemoveField( "RHI_DTRECI" )
	oStructRHI:RemoveField( "RHI_SALMES" )
	oStructRHI:RemoveField( "RHI_MARK" )
	oStructRHI:RemoveField( "RHI_TPCALC" )
	oStructRHI:RemoveField( "RHI_ADMISS" )
	
	oView:CreateFolder( 'PASTAS','GRID')
	oView:AddSheet( 'PASTAS', 'ABA01', STR0024 ) //"Contr. Dias Direito"
	oView:AddSheet( 'PASTAS', 'ABA02', STR0025 ) //"Programação de Férias"
	oView:CreateHorizontalBox( 'BOXABA01', 100,,, "PASTAS", "ABA01" )
	oView:CreateHorizontalBox( 'BOXABA02', 100,,, "PASTAS", "ABA02" )
	oView:SetOwnerView( "GPEA050_SRA","FORMFIELD")
	oView:SetOwnerView( "GPEA050_SRF", "BOXABA01" )
	oView:SetOwnerView( "GPEA050_RHI", "BOXABA02" )
Else
	oView:SetOwnerView( "GPEA050_SRA","FORMFIELD")
	oView:SetOwnerView( "GPEA050_SRF","GRID")
EndIf

If cPaisLoc == "COL"
	oView:showUpdateMsg(.F.) //Define si se muestra mensaje de Actualización Si-(.T.) No-(.F.)
EndIf

oView:SetCloseOnOk( { |oView| Gp050ClsOk( oView ) } )
Return(oView)     

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Gp050ClsOkºAutor  ³Leandro Drumond     º Data ³  03/10/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Tratamento para o metodo SetCloseOnOk (fechar tela Ok)      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPE                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Gp050ClsOk( oView )
Local nOperation := oView:oModel:GetOperation()
Local oGrid   	 := oView:oModel:GetModel("GPEA050_SRF")
Local nLinGrdPos := oGrid:GetLine() //Linha posicionada atualmente no Grid
Local lRet     	 :=  .F.
Local aArea    	 := GetArea()
Local nX  		 := 0

If nOperation == MODEL_OPERATION_UPDATE
	lRet := .T.
	For nX:=1 to oGrid:GetQtdLine()
		oGrid:GoLine( nX )
		if !oGrid:IsDeleted()	
			Exit
		EndIf
	Next nX
	oGrid:GoLine( nLinGrdPos )
EndIf

RestArea( aArea )

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gp050IniMod³ Autor ³ Leandro Drumond      ³ Data ³ 05.02.13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Pre-inicializacao                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Gp050IniMod(oModel,nOperacao)

lInitDesc := .T.
lGp050Auto := IIf( type("lGp050Auto") == "U", IsBlind() , lGp050Auto) 

If !lGp050Auto
	If !(IsInCallStack("CFG600lmdl")) .And. nOperacao == 3
		oModel:SetOperation( 4 )
		SX3->( dbSetOrder( 2 ) ) //X3_CAMPO
		If SX3->( dbSeek("RF_DFERANT")) 
			Aviso( STR0068 , STR0069 + X3Titulo() + STR0070 + CRLF + STR0071 , {STR0072} ) 
		EndIf
	EndIf
Endif

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp050LinOk³ Autor ³ Leandro Drumond       ³ Data ³ 05.02.13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Critica linha digitada                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function gp050LinOk(oGrid)      
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Declaracao de variaveis locais         					     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local nDias			:= 0
	Local nPro1			:= 0  
	Local nPro2			:= 0
	Local nPro3			:= 0
	Local nFer1			:= 0
	Local nFer2			:= 0
	Local nFer3			:= 0
	Local nAbo1			:= 0
	Local nAbo2			:= 0
	Local nAbo3			:= 0
	Local lRet			:= .T.									
	Local lFeriado		:= .F.
	Local lDiaDom		:= .F.
	Local lDiaSab		:= .F.
	Local lAntFer		:= .F.
	Local lDSR			:= .F.
	Local aTabFer		:= {}
	Local aTabFer2		:= {}
	Local nTempoParc	:= 0
	Local dDSR			:= CtoD("//")
	Local dFer			:= CtoD("//") //Data do Feriado, utilizado na Reforma Trabalhista

	lGp050Auto := IIf( type("lGp050Auto") == "U", IsBlind() , lGp050Auto)

	If !fTab_Fer(@aTabFer,,@aTabFer2)
		Help(" ", 1, "Help",, OemToAnsi(STR0057), 1, 0) //"A configuração dos períodos esta incorreta ou não existe período válido para o roteiro de férias. Verifique o cadastro de períodos."	
		Return .F.
	EndIf

	SetMnemonicos(xFilial("RCA"),NIL,.T.,"P_REGPARCI") 

	P_REGPARCI	:= If( Type("P_REGPARCI") == "U", .F. , P_REGPARCI)

	Begin Sequence

		//Verifica se houve mudança na programação de férias e exclui eventuais avisos de férias que tenham sido enviados para o TAE
		If lTemREQ
			If !fVldExcTAE(oGrid)
				lRet := .F.
				Break
			EndIf
		EndIf

		If !oGrid:IsDeleted()

			If oGrid:GetValue("RF_DATABAS") > oGrid:GetValue("RF_DATAFIM")
				If lGp050Auto
					AutoGrLog( OemToAnsi( STR0018 ) )// A Data Final do Periodo deve ser maior que a Data de Inicio do Periodo
				Else
					Help(,,'HELP',, STR0018,1,0 ) 
				EndIf
				lRet := .F.
				Break
			EndIf
		
			If oGrid:GetValue("RF_DFERVAT") > oGrid:GetValue("RF_DIASDIR")
				If lGp050Auto				
					AutoGrLog( OemToAnsi( STR0020 ) )//Os Dias Vencidos devem ser menor que os Dias de Direito
				Else
					Help(,,'HELP',, STR0020,1,0 )
				EndIf
				lRet := .F.
				Break
			EndIf
			
			If cPaisLoc == "BRA"
				nPro1	:= ( oGrid:GetValue("RF_DFEPRO1") + oGrid:GetValue("RF_DABPRO1") ) //Total de dias da Primeira Programacao de ferias
				nPro2	:= ( oGrid:GetValue("RF_DFEPRO2") + oGrid:GetValue("RF_DABPRO2") ) //Total de dias da Segunda Programacao de ferias
				nPro3	:= ( oGrid:GetValue("RF_DFEPRO3") + oGrid:GetValue("RF_DABPRO3") ) //Total de dias da Terceira Programacao de ferias
				nFer1	:= ( oGrid:GetValue("RF_DFEPRO1") ) //Total de dias da Primeira Programacao de ferias
				nFer2	:= ( oGrid:GetValue("RF_DFEPRO2") ) //Total de dias da Segunda Programacao de ferias
				nFer3	:= ( oGrid:GetValue("RF_DFEPRO3") ) //Total de dias da Terceira Programacao de ferias
				nAbo1	:= ( oGrid:GetValue("RF_DABPRO1") ) //Total de dias de Abono da Primeira Programacao
				nAbo2	:= ( oGrid:GetValue("RF_DABPRO2") ) //Total de dias de Abono da Segunda Programacao
				nAbo3	:= ( oGrid:GetValue("RF_DABPRO3") ) //Total de dias de Abono da Terceira Programacao
			EndIf		
			
			If cPaisLoc <> "BRA" //Campo RF_DIASANT (nPosDAnt) nao e utilizado no BRASIL
				nDias:= oGrid:GetValue("RF_DIASANT")
			//Se não utilizar rotinas automaticas apresenta as mensagens
			ElseIf (!lGp050Auto) .And. !(IsBlind())
				If !lRefTrab .And. ( (nPro1 > 0 .AND. nPro1 < 10) .OR. (nPro2 > 0 .AND. nPro2 < 10) .OR. (nPro3 > 0 .AND. nPro3 < 10) )							
					lRet:=(MsgYesNo(OemToAnsi(STR0045)+OemToAnsi(STR0056) + CRLF + OemToAnsi(STR0058),OemToAnsi(STR0016))) //Conforme Artigo 134 paragrafo 1o da CLT, 
					//Os dias programados de ferias + abono nao podem ser inferiores a 10 dias, deseja continuar?.
					//Salvo pelo Artigo 130 A da CLT, que trata da modalidade de regime de tempo parcial. 	
					If !lRet
						Help(,,'HELP',, STR0046,1,0 ) //Os dias programados de ferias + abono nao podem ser inferiores a 10 dias
						Break
					Endif				
				Endif

				If lRet .And. ((nFer1 == 0 .And. nAbo1 > 0) .Or. (nFer2 == 0 .And. nAbo2 > 0) .Or. (nFer3 == 0 .And. nAbo3 > 0))
					lRet 	:= .F.
					Help(,,'HELP',, STR0093 ,1,0 ) // "O campo Dias de Férias (Programação) não pode estar vazio caso o campo Dias de Abono (Programação) esteja preenchido."
					Break
				EndIf	
				
				If lRet .And. cPaisLoc == "BRA" .And. (( nAbo1 + nAbo2 + nAbo3) > 0 )
					nTempoParc := SRA->RA_HRSEMAN
					If ( !lRefTrab .And. SRA->RA_HOPARC == "1" .And. nTempoParc <= 25 .And. nTempoParc  > 0 .And. Len(aTabFer2) > 0	.And. P_REGPARCI )
						nPosTbFer := Ascan(aTabFer2, { |X|  nTempoParc <= X[6] .And. nTempoParc > X[5] })
						//Dias de abono preenchdos e o funcionário se enquadra no regime de tempo parcial. Conforme o Artigo 143, inciso 3º da CLT,
						//o abono pecuniário não se aplica aos empregados sob o regime de tempo parcial, deseja continuar mesmo assim?
						lRet:=(MsgYesNo(OemToAnsi(STR0053)+ CRLF + OemToAnsi(STR0054),OemToAnsi(STR0014)))
					Endif
				Endif

				If Len(aTabFer) > 0
					If lRet .And. (( nAbo1 + nAbo2 + nAbo3) > (aTabFer[3]/3))
						lRet:=(MsgYesNo(OemToAnsi(STR0047)+OemToAnsi(STR0056),OemToAnsi(STR0016))) //Os dias de abono estão maiores que 1/3 dos dias de férias vencidas, deseja continuar?.
						If !lRet
							Help(,,'HELP',, STR0047,1,0 ) //Os dias de abono estão maiores que 1/3 dos dias de férias vencidas
							Break
						Endif	
					Endif
				EndIf
				//Verifica se o dia de inicio da primeira programação de férias sera em um feriado, sabado ou domingo.
				lFeriado := !Empty(oGrid:GetValue("RF_DATAINI")) .And. !Empty(dFer := fVldDSR(SRA->RA_FILIAL, SRA->RA_MAT, oGrid:GetValue("RF_DATAINI"), 0, "F")) //Feriado
				lDiaDom := If( Alltrim(DiaSemana( oGrid:GetValue("RF_DATAINI") )) == "Domingo", .T., .F.)	//Domingo
				lDiaSab := If( Alltrim(DiaSemana( oGrid:GetValue("RF_DATAINI") )) == "Sabado"	, .T., .F.)	//Sabado
				
				If lRefTrab .And. !Empty(oGrid:GetValue("RF_DATAINI"))
					lAntFer := !Empty(dFer := fVldDSR(SRA->RA_FILIAL, SRA->RA_MAT, oGrid:GetValue("RF_DATAINI"), 2, "F")) //verifica se antecede feriado em menos de 2 dias
					dDSR	 := fVldDSR(SRA->RA_FILIAL, SRA->RA_MAT, oGrid:GetValue("RF_DATAINI"), 2, "D")
					lDSR	 := (lRefTrab .AND. !Empty(dDSR))//verifica se antecede DSR em menos de 2 dias (dia seguinte feriado)
				EndIf
				
				If lRet .And. !lRefTrab .And. lFeriado
					lRet:=(MsgYesNo(OemToAnsi(STR0048) + OemToAnsi(STR0049) + OemToAnsi(STR0052) + OemToAnsi(STR0053) + OemToAnsi(STR0056),OemToAnsi(STR0016))) 
					//As ferias do funcionario, referentes à primeira programação de férias, iniciam em um feriado, deseja continuar?.
					If !lRet
						Help(,,'HELP',, STR0048 + STR0049 + STR0052 + STR0053,1,0 ) //Os dias de abono estão maiores que 1/3 dos dias de férias vencidas
						Break
					Endif	
				Endif
				
				If lRet .And. lDiaSab .And. !lDSR .And. !lFeriado
					lRet:=(MsgYesNo(OemToAnsi(STR0048) + OemToAnsi(STR0049) + OemToAnsi(STR0052) + OemToAnsi(STR0054) + OemToAnsi(STR0056),OemToAnsi(STR0016))) 
					//As ferias do funcionario, referentes à primeira programação de férias, iniciam em um sabado, deseja continuar?.
					If !lRet
						Help(,,'HELP',, STR0048 + STR0049 + STR0052 + STR0054,1,0 ) //As ferias do funcionario, referentes à primeira programação de férias, iniciam em um sabado
						Break
					Endif				
				Endif
				
				If lRet .And. lDiaDom .And. !lDSR .And. !lFeriado
					lRet:=(MsgYesNo(OemToAnsi(STR0048) + OemToAnsi(STR0049) + OemToAnsi(STR0052) + OemToAnsi(STR0055) + OemToAnsi(STR0056),OemToAnsi(STR0016))) 
					//As ferias do funcionario, referentes à primeira programação de férias, iniciam em um domingo, deseja continuar?.
					If !lRet
						Help(,,'HELP',, STR0048 + STR0049 + STR0052 + STR0055,1,0 ) //As ferias do funcionario, referentes à primeira programação de férias, iniciam em um domingo
						Break
					Endif	
				Endif
				
				If lRet .And. lAntFer
					lRet:=(MsgYesNo(oemToAnsi(STR0066) + CRLF + oemToAnsi(STR0063) + "(" + dToC(oGrid:GetValue("RF_DATAINI")) + "). " + STR0064 + "(" + dToC(dFer) +")." + CRLF + oemToAnsi(STR0067),OemToAnsi(STR0014))) 
					//"Conforme Artigo 134 parágrafo 3º da CLT, é vedado o início das férias no período de dois dias que antecede feriado ou DSR."##"Data inicial do período de gozo: " (//). Feriado: (//)##"Deseja continuar?##"Atenção"
					If !lRet
						Help(,,'HELP',, oemToAnsi(STR0066) + CRLF + oemToAnsi(STR0063) + "(" + dToC(oGrid:GetValue("RF_DATAINI")) + "). " + STR0064 + "(" + dToC(dFer) +").",1,0 ) //"Conforme Artigo 134 parágrafo 3º da CLT, é vedado o início das férias no período de dois dias que antecede feriado ou DSR."##"Data inicial do período de gozo: " (//). Feriado: (//)##"Deseja continuar?##"Atenção"
						Break
					Endif					
				Endif
				
				If lRet .And. lDSR
					lRet:=(MsgYesNo(oemToAnsi(STR0066) + CRLF + oemToAnsi(STR0063) + "(" + dToC(oGrid:GetValue("RF_DATAINI")) + "). " + STR0065 + "(" + dToC(dDSR) +")." + CRLF + oemToAnsi(STR0067),OemToAnsi(STR0014))) 
					//"Conforme Artigo 134 parágrafo 3º da CLT, é vedado o início das férias no período de dois dias que antecede feriado ou DSR."##"Data inicial do período de gozo: " (//). Feriado: (//)##"Deseja continuar?##"Atenção"
					If !lRet
						Help(,,'HELP',, oemToAnsi(STR0066) + CRLF + oemToAnsi(STR0063) + "(" + dToC(oGrid:GetValue("RF_DATAINI")) + "). " + STR0065 + "(" + dToC(dDSR) +").",1,0 ) //"Conforme Artigo 134 parágrafo 3º da CLT, é vedado o início das férias no período de dois dias que antecede feriado ou DSR."##"Data inicial do período de gozo: " (//). Feriado: (//)##"Deseja continuar?##"Atenção"
						Break
					Endif
				Endif


				//Verifica se o dia de inicio da segunda programação de férias sera em um feriado, sabado ou domingo.
				lAntFer := lFeriado := lDiaDom := lDiaSab := .F.
				dDSR := CtoD("//")
				dFer := CtoD("//")
				
				lFeriado := !Empty(oGrid:GetValue("RF_DATINI2")) .And. !Empty(dFer := fVldDSR(SRA->RA_FILIAL, SRA->RA_MAT, oGrid:GetValue("RF_DATINI2"), 0, "F")) //Feriado
				lDiaDom := If( Alltrim(DiaSemana( oGrid:GetValue("RF_DATINI2") )) == "Domingo", .T., .F.)	//Domingo
				lDiaSab := If( Alltrim(DiaSemana( oGrid:GetValue("RF_DATINI2") )) == "Sabado"	, .T., .F.)	//Sabado
				
				If lRefTrab .And. !Empty(oGrid:GetValue("RF_DATINI2"))
					lAntFer := !Empty(dFer := fVldDSR(SRA->RA_FILIAL, SRA->RA_MAT, oGrid:GetValue("RF_DATINI2"), 2, "F")) //verifica se antecede feriado em menos de 2 dias
					dDSR	 := fVldDSR(SRA->RA_FILIAL, SRA->RA_MAT, oGrid:GetValue("RF_DATINI2"), 2, "D")
					lDSR	 := (lRefTrab .AND. !Empty(dDSR))//verifica se antecede DSR em menos de 2 dias (dia seguinte feriado)
				EndIf
				
				If lRet .And. !lRefTrab .And. lFeriado
					lRet:=(MsgYesNo(OemToAnsi(STR0048) + OemToAnsi(STR0050) + OemToAnsi(STR0052) + OemToAnsi(STR0053) + OemToAnsi(STR0056),OemToAnsi(STR0016))) 
					//As ferias do funcionario, referentes à segunda programação de férias, iniciam em um feriado, deseja continuar?.
					If !lRet
						Help(,,'HELP',, STR0048 + STR0050 + STR0052 + STR0053,1,0 ) //As ferias do funcionario, referentes à segunda programação de férias, iniciam em um feriado
						Break
					Endif	
				Endif
				
				If lRet .And. lDiaSab .And. !lDSR .And. !lFeriado
					lRet:=(MsgYesNo(OemToAnsi(STR0048) + OemToAnsi(STR0050) + OemToAnsi(STR0052) + OemToAnsi(STR0054) + OemToAnsi(STR0056),OemToAnsi(STR0016))) 
					//As ferias do funcionario, referentes à segunda programação de férias, iniciam em um sabado, deseja continuar?.
					If !lRet
						Help(,,'HELP',, STR0048 + STR0050 + STR0052 + STR0054,1,0 ) //As ferias do funcionario, referentes à segunda programação de férias, iniciam em um sabado, deseja continuar?
						Break
					Endif	
				Endif
				
				If lRet .And. lDiaDom .And. !lDSR .And. !lFeriado
					lRet:=(MsgYesNo(OemToAnsi(STR0048) + OemToAnsi(STR0050) + OemToAnsi(STR0052) + OemToAnsi(STR0055) + OemToAnsi(STR0056),OemToAnsi(STR0016))) 
					//As ferias do funcionario, referentes à segunda programação de férias, iniciam em um domingo, deseja continuar?.
					If !lRet
						Help(,,'HELP',, STR0048 + STR0050 + STR0052 + STR0055,1,0 ) //As ferias do funcionario, referentes à segunda programação de férias, iniciam em um domingo
						Break
					Endif	
				Endif
				
				If lRet .And. lAntFer
					lRet:=(MsgYesNo(oemToAnsi(STR0066) + CRLF + oemToAnsi(STR0063) + "(" + dToC(oGrid:GetValue("RF_DATINI2")) + "). " + STR0064 + "(" + dToC(dFer) +")." + CRLF + oemToAnsi(STR0067),OemToAnsi(STR0014))) 
					//"Conforme Artigo 134 parágrafo 3º da CLT, é vedado o início das férias no período de dois dias que antecede feriado ou DSR."##"Data inicial do período de gozo: " (//). Feriado: (//)##"Deseja continuar?##"Atenção"
					If !lRet
						Help(,,'HELP',, oemToAnsi(STR0066) + CRLF + oemToAnsi(STR0063) + "(" + dToC(oGrid:GetValue("RF_DATINI2")) + "). " + STR0064 + "(" + dToC(dFer) +").",1,0 ) //"Conforme Artigo 134 parágrafo 3º da CLT, é vedado o início das férias no período de dois dias que antecede feriado ou DSR."##"Data inicial do período de gozo: " (//). Feriado: (//)##"Deseja continuar?##"Atenção"
						Break
					Endif					
				Endif
				
				If lRet .And. lDSR
					lRet:=(MsgYesNo(oemToAnsi(STR0066) + CRLF + oemToAnsi(STR0063) + "(" + dToC(oGrid:GetValue("RF_DATINI2")) + "). " + STR0065 + "(" + dToC(dDSR) +")." + CRLF + oemToAnsi(STR0067),OemToAnsi(STR0014))) 
					//"Conforme Artigo 134 parágrafo 3º da CLT, é vedado o início das férias no período de dois dias que antecede feriado ou DSR."##"Data inicial do período de gozo: " (//). Feriado: (//)##"Deseja continuar?##"Atenção"
					If !lRet
						Help(,,'HELP',, oemToAnsi(STR0066) + CRLF + oemToAnsi(STR0063) + "(" + dToC(oGrid:GetValue("RF_DATINI2")) + "). " + STR0065 + "(" + dToC(dDSR) +").",1,0 ) //"Conforme Artigo 134 parágrafo 3º da CLT, é vedado o início das férias no período de dois dias que antecede feriado ou DSR."##"Data inicial do período de gozo: " (//). Feriado: (//)##"Deseja continuar?##"Atenção"
						Break
					Endif
				Endif

				//Verifica se o dia de inicio da terceira programação de férias sera em um feriado, sabado ou domingo.
				lAntFer := lFeriado := lDiaDom := lDiaSab := .F.
				dDSR := CtoD("//")
				dFer := CtoD("//")
				
				lFeriado :=  !Empty(oGrid:GetValue("RF_DATINI3")) .And. !Empty(dFer := fVldDSR(SRA->RA_FILIAL, SRA->RA_MAT, oGrid:GetValue("RF_DATINI3"), 0, "F")) //Feriado
				lDiaDom := If( Alltrim(DiaSemana( oGrid:GetValue("RF_DATINI3") )) == "Domingo", .T., .F.)	//Domingo
				lDiaSab := If( Alltrim(DiaSemana( oGrid:GetValue("RF_DATINI3") )) == "Sabado"	, .T., .F.)	//Sabado
				
				If lRefTrab .And. !Empty(oGrid:GetValue("RF_DATINI3"))
					lAntFer := !Empty(dFer := fVldDSR(SRA->RA_FILIAL, SRA->RA_MAT, oGrid:GetValue("RF_DATINI3"), 2, "F")) //verifica se antecede feriado em menos de 2 dias
					dDSR	 := fVldDSR(SRA->RA_FILIAL, SRA->RA_MAT, oGrid:GetValue("RF_DATINI3"), 2, "D")
					lDSR	 := (lRefTrab .AND. !Empty(dDSR))//verifica se antecede DSR em menos de 2 dias (dia seguinte feriado)
				EndIf
							
				If lRet .And. !lRefTrab .And. lFeriado
					lRet:=(MsgYesNo(OemToAnsi(STR0048) + OemToAnsi(STR0051) + OemToAnsi(STR0052) + OemToAnsi(STR0053) + OemToAnsi(STR0056),OemToAnsi(STR0016))) 
					//As ferias do funcionario, referentes à terceira programação de férias, iniciam em um feriado, deseja continuar?.
					If !lRet
						Help(,,'HELP',, STR0048 + STR0051 + STR0052 + STR0053,1,0 ) //As ferias do funcionario, referentes à terceira programação de férias, iniciam em um feriado
						Break
					Endif	
				Endif
				
				If lRet .And. lDiaSab .And. !lDSR .And. !lFeriado
					lRet:=(MsgYesNo(OemToAnsi(STR0048) + OemToAnsi(STR0051) + OemToAnsi(STR0052) + OemToAnsi(STR0054) + OemToAnsi(STR0056),OemToAnsi(STR0016))) 
					//As ferias do funcionario, referentes à terceira programação de férias, iniciam em um sabado, deseja continuar?.
					If !lRet
						Help(,,'HELP',, STR0048 + STR0051 + STR0052 + STR0054,1,0 ) //As ferias do funcionario, referentes à terceira programação de férias, iniciam em um sabado
						Break
					Endif	
				Endif
				
				If lRet .And. lDiaDom .And. !lDSR .And. !lFeriado
					lRet:=(MsgYesNo(OemToAnsi(STR0048) + OemToAnsi(STR0051) + OemToAnsi(STR0052) + OemToAnsi(STR0055) + OemToAnsi(STR0056),OemToAnsi(STR0016))) 
					//As ferias do funcionario, referentes à terceira programação de férias, iniciam em um domingo, deseja continuar?.
					If !lRet
						Help(,,'HELP',, STR0048 + STR0051 + STR0052 + STR0055,1,0 ) ////As ferias do funcionario, referentes à terceira programação de férias, iniciam em um domingo
						Break
					Endif	
				Endif

				If lRet .And. lAntFer
					lRet:=(MsgYesNo(oemToAnsi(STR0066) + CRLF + oemToAnsi(STR0063) + "(" + dToC(oGrid:GetValue("RF_DATINI3")) + "). " + STR0064 + "(" + dToC(dFer) +")." + CRLF + oemToAnsi(STR0067),OemToAnsi(STR0014))) 
					//"Conforme Artigo 134 parágrafo 3º da CLT, é vedado o início das férias no período de dois dias que antecede feriado ou DSR."##"Data inicial do período de gozo: " (//). Feriado: (//)##"Deseja continuar?##"Atenção"
					If !lRet
						Help(,,'HELP',, oemToAnsi(STR0066) + CRLF + oemToAnsi(STR0063) + "(" + dToC(oGrid:GetValue("RF_DATINI3")) + "). " + STR0064 + "(" + dToC(dFer) +").",1,0 ) //"Conforme Artigo 134 parágrafo 3º da CLT, é vedado o início das férias no período de dois dias que antecede feriado ou DSR."##"Data inicial do período de gozo: " (//). Feriado: (//)##"Deseja continuar?##"Atenção"
						Break
					Endif					
				Endif
				
				If lRet .And. lDSR
					lRet:=(MsgYesNo(oemToAnsi(STR0066) + CRLF + oemToAnsi(STR0063) + "(" + dToC(oGrid:GetValue("RF_DATINI3")) + "). " + STR0065 + "(" + dToC(dDSR) +")." + CRLF + oemToAnsi(STR0067),OemToAnsi(STR0014))) 
					//"Conforme Artigo 134 parágrafo 3º da CLT, é vedado o início das férias no período de dois dias que antecede feriado ou DSR."##"Data inicial do período de gozo: " (//). Feriado: (//)##"Deseja continuar?##"Atenção"
					If !lRet
						Help(,,'HELP',, oemToAnsi(STR0066) + CRLF + oemToAnsi(STR0063) + "(" + dToC(oGrid:GetValue("RF_DATINI3")) + "). " + STR0065 + "(" + dToC(dDSR) +").",1,0 ) //"Conforme Artigo 134 parágrafo 3º da CLT, é vedado o início das férias no período de dois dias que antecede feriado ou DSR."##"Data inicial do período de gozo: " (//). Feriado: (//)##"Deseja continuar?##"Atenção"
						Break
					Endif
				Endif

				If lRet .And. Empty(oGrid:GetValue("RF_DFERVAT") ) .And. Empty(oGrid:GetValue("RF_DFERAAT")) .And. (oGrid:GetValue("RF_STATUS") $ " |1" )
					lRet:=(MsgYesNo(OemToAnsi(STR0075) + OemToAnsi(STR0056),OemToAnsi(STR0016))) 
						//"A quantidade de dias de férias vencidas ou a quantidade de dias de ferias proporcionais devem ser preenchidas"##"Deseja continuar?##"Atenção"
					If !lRet 
						Help(,,'HELP',, STR0075 ,1,0 ) ////"A quantidade de dias de férias vencidas ou a quantidade de dias de ferias proporcionais devem ser preenchidas"
						Break
					Endif 
				Endif	
			Else
				If !Gp050DIAF()
					lRet := .F.
					Break
				EndIf
			EndIf	 

			nDias+= oGrid:GetValue("RF_DFERAAT")
			If cPaisLoc == "BRA"
				If oGrid:GetValue("RF_DFERVAT") > 0 .and. nDias > 0
					If lGp050Auto
						AutoGrLog( OemToAnsi( STR0022 ) )  	//Quando informado os Dias Vencidos nao deve ser informado nem os Dias de Antecipacao nem os Dias Proporcionais			
					Else
						Help(,,'HELP',, STR0022,1,0 ) 				
					EndIf  
					lRet := .F.
					Break  			  					
				EndIf
			EndIf
		
			If (cPaisLoc == "BRA" .And.  nDias > oGrid:GetValue("RF_DIASDIR") );
				.OR. ( cPaisLoc <> "BRA" .And.  ( oGrid:GetValue("RF_DFERVAT") + oGrid:GetValue("RF_DFERAAT") + oGrid:GetValue("RF_DIASANT") - oGrid:GetValue("RF_DFERANT") > oGrid:GetValue("RF_DIASDIR"))) 
				If lGp050Auto
					If cPaisLoc == "BRA"
						AutoGrLog( OemToAnsi( STR0019 ) )//Os Dias de Antecipacao para o Periodo e os Dias Proporcionais devem ser menor que os Dias de Direito
					Else
						AutoGrLog( OemToAnsi( STR0081 ) ) //La suma de días vencidos, días proporcionales, días anticipados menos los días pagados deben ser menor a los días derecho.
					EndIf	
				Else
					If cPaisLoc == "BRA"				
						Help(,,'HELP',, STR0019,1,0 ) //Os Dias de Antecipacao para o Periodo e os Dias Proporcionais devem ser menor que os Dias de Direito
					Else
						Help(,,'HELP',, STR0081,1,0 ) //La suma de días vencidos, días proporcionales, días anticipados menos los días pagados deben ser menor a los días derecho.
					EndIf	
				EndIf
				lRet := .F.
				Break
			EndIf
			
			If Empty(oGrid:GetValue("RF_STATUS"))
				If lGp050Auto
					AutoGrLog( OemToAnsi( STR0036 ) )//O campo situação deve ser peenchido.
				Else				
					Help(,,'HELP',, STR0036,1,0 )
				EndIf
				lRet := .F.
				Break
			EndIf		
		
			//Permite alteracao dos dias antecipados apos o pagamento, porem a quantidade nao podera ser inferior a que esta gravada
			If cPaisLoc == "MEX"
				If oGrid:GetValue("RF_DFERANT") > 0 .And. oGrid:GetValue("RF_DIASANT") < oGrid:GetValue("RF_DFERANT")
					If lGp050Auto
						AutoGrLog( OemToAnsi(STR0026) )
					Else
						Help(,,'HELP',, STR0026,1,0 ) //"Depois de calculados (Pagos) os dias de Antecipacao nao podem ser alterados para uma quantidade inferior."
					EndIf
					lRet := .F.
					Break
				EndIf
			EndIf 
			
			If cPaisLoc == "ARG"
				If (oGrid:GetValue("RF_DIASANT") < oGrid:GetValue("RF_DFERANT")) .and. (oGrid:GetValue("RF_DFERVAT") == 0)
					If lGp050Auto
						AutoGrLog( OemToAnsi(STR0026) )
					Else
						Help(,,'HELP',, STR0026,1,0 ) //"Depois de calculados (Pagos) os dias de Antecipacao nao podem ser alterados para uma quantidade inferior."
					EndIf
					lRet := .F.
					Break
				EndIf
			EndIf
			
			If lRet .and. lRefTrab //Tratamento referente ao período de férias na reforma trabalhista
				If (( nFer1 < 14 .And. nFer2 < 14 .And. nFer3 < 14) ; //Se não há agendamento de férias com 14 dias ou mais
						.And. (oGrid:GetValue("RF_DIASDIR") - (nPro1 + nPro2 + nAbo3)) < 14)  //e o saldo de dias que resta para ultima programação(considerando o abono) for inferior a 14 dias.
					lRet := .F.
					Help(,,'HELP',, STR0061,1,0, NIL, NIL, NIL, NIL, NIL, {STR0096} ) //"Conforme Artigo 134 parágrafo 1º da CLT, ao menos um dos periodos de férias deve ser igual ou superior a 14 dias corridos. Caso os dois primeiros períodos não atendam a essa exigência, o último deverá ter 14 dias ou mais. Certifique-se de ter dias suficientes para programar o último período."
					Break
				ElseIf ( !Empty(nFer1) .AND. nFer1 < 5 ) .or. (!Empty(nFer2) .and. nFer2 < 5 ) .or. (!Empty(nFer3) .and. nFer3 < 5 )
					lRet := .F.
					Help(,,'HELP',, STR0062,1,0 ) //"Conforme Artigo 134 parágrafo 1º da CLT, os períodos de férias não devem ser inferiores a 5 dias corridos."
					Break
				ElseIf (oGrid:GetValue("RF_DIASDIR") - (nPro1 + nPro2 + nPro3)) > 0 .And. (oGrid:GetValue("RF_DIASDIR") - (nPro1 + nPro2 + nPro3)) < 5
					Help(,, STR0076, , STR0077, 1, 0,,,,,, {STR0078} ) //"O saldo de férias que restará é inferior a 5 dias. Conforme Artigo 134 parágrafo 1º da CLT, as férias não devem ser concedidas com menos de 5 dias de gozo."
					lRet := .F.
					Break				
				EndIf 
			EndIf
		EndIf  

	End Sequence

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp050VerEdit	  ³ Autor ³ Leandro Drumond       ³ Data ³04.02.2013³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verificar se o registro poderá ser editado. So permitira efetuar  ³±±
±±³          ³manutencao nos periodos que nao foram utilizados em calculo,      ³±±
±±³          ³ou seja, quando o campo Dias Pagos for maior que zero.            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ gp050VerEdit()                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ When de todos os campos da tabela SRF SX3                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß */
Function gp050VerEdit()
Local oModel 		:= FWModelActive()
Local lEdit 		:= .T.
Local lGestPubl 	:= if(ExistFunc("fUsaGFP"),fUsaGFP(),.f.) //Verifica se utiliza o modulo de Gestao de Folha Publica - SIGAGFP	

If lGestPubl .AND. M->RA_REGIME == '2'	
	Return(.T.)
EndIf

If oModel:GetValue("GPEA050_SRF","RF_DFERANT") > 0
	lEdit := .F.
EndIf        
	
If cPaisLoc == "ARG"
	lEdit := gp050DAnt(oModel)
EndIf
	
Return (lEdit)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp050DAnt		  ³ Autor ³ Leandro Drumond       ³ Data ³04.02.2013³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verificar se o campo Antecipos poderá ser Editado na Argentina.   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ gp050DAnt(oModel)                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ gp050VerEdit                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß */
Static Function gp050DAnt(oModel)

Local lEdit := .F.

If	oModel:GetValue("GPEA050_SRF","RF_STATUS") == "1" .and. oModel:GetValue("GPEA050_SRF","RF_DFERVAT") == 0 
	lEdit := .T.
EndIf
	
Return (lEdit)
/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³gp050ChkDel   ³Autor³Tatiane Matias       ³ Data ³27/04/2004³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Verifica se o registro pode ser excluido.                   ³
³          ³O registro so podera ser excluido se o Dias Pagos for zero. ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³No bDelOk da oGet     .				                        ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function gp050ChkDel(oModelGrid, nLine, cAction, cField)
Local lRet := .T.

If cAction == 'DELETE'
	lRet := !oModelGrid:GetValue("RF_DFERANT") > 0
	If !lRet
		Help( ,,,OemToAnsi(STR0016), OemToAnsi(STR0033), 1, 0 ) //"Atenção"##"Períodos com dias de férias pagos não pode ser excluídos." 
	ElseIf lTemREQ
		If !fVldExcTAE(oModelGrid, .T.)
			lRet := .F.
		EndIf
	EndIf
EndIf
             
Return ( lRet ) 

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³gp050DelRHI   ³Autor³Mohanad Odeh         ³ Data ³09/09/2011³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Verifica se o registro pode ser excluido.                   ³
³          ³O registro so podera ser excluido se o Status for 1 ou 2    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³Na pre-validacao de linha.				                    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function gp050DelRHI(oModelGrid, nLine, cAction, cField)
Local lRet := .T.

If cAction == 'DELETE'
	lRet := oModelGrid:GetValue("RHI_STATUS") $ "1"
EndIf

Return (lRet)  

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³gp050AltRHI   ³Autor³Mohanad Odeh         ³ Data ³16/09/2011³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Verifica se o registro pode ser excluido.                   ³
³          ³O registro so podera ser alterado se o Status for 1         ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³When da SX3          				                        ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function gp050AltRHI()
Local oModel 	:= FWModelActive()
Local lRet 		:= .T.
               
If HabilitaAba()
	If !oModel:GetModel( 'GPEA050_RHI' ):IsInserted() //Nao eh uma linha nova
		lRet :=	oModel:GetValue("GPEA050_RHI","RHI_STATUS") $ "1"
	EndIf
EndIf

Return (lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³gp050DtValid	  ³ Autor ³ Leandro Drumond       ³ Data ³05.02.2013³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica se a data inicio da 1a, 2a e 3a ferias é maior que a data³±±
±±³          ³de inicio do periodo.                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ gp050DtValid(dData)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Valid de campo                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß */
Function gp050DtValid(dData)
Local oModel 	:= FWModelActive()
Local oGrid		:= oModel:GetModel("GPEA050_SRF")
Local lRet		:= .T.
Local nCont		:= 0
Local nLine		:= 0

lGp050Auto := IIf( type("lGp050Auto") == "U", IsBlind() , lGp050Auto)

If !IsInCallStack("GPEA053")
	nLine		:= oGrid:nLine
EndIf

If lRet .and. !Empty(oModel:GetValue("GPEA050_SRF","RF_DATINI2")) 
	If Empty(oModel:GetValue("GPEA050_SRF","RF_DATAINI")) .or. oModel:GetValue("GPEA050_SRF","RF_DATINI2") < oModel:GetValue("GPEA050_SRF","RF_DATAINI") + oModel:GetValue("GPEA050_SRF","RF_DFEPRO1")
		If lGp050Auto
			AutoGrLog( OemToAnsi(STR0040) )  //"A Data inicial da 2ª programação de férias deve ser maior que a Data final da 1ª programação."
		Else
			Help(,,STR0016,, OemToAnsi(STR0040) ,1,0)
		EndIf
      	lRet := .F.
    EndIf
EndIf  

If lRet .and. !Empty(oModel:GetValue("GPEA050_SRF","RF_DATINI3")) 
	If Empty(oModel:GetValue("GPEA050_SRF","RF_DATINI2")) .or. oModel:GetValue("GPEA050_SRF","RF_DATINI3") < oModel:GetValue("GPEA050_SRF","RF_DATINI2") + oModel:GetValue("GPEA050_SRF","RF_DFEPRO2")
		If lGp050Auto
			AutoGrLog( OemToAnsi(STR0041) )  //"A Data inicial da 3ª programação de férias deve ser maior que a Data final da 2ª programação."
		Else
			Help(,,STR0016,, OemToAnsi(STR0041) ,1,0)
		EndIf
		lRet := .F.
    EndIf
EndIf

If lRet
	//Se já estiver preenchido os dias de férias programados, efetua validação para não permitir programar mais dias do que o funcionário possui
	If oModel:GetValue("GPEA050_SRF","RF_DFEPRO1") + oModel:GetValue("GPEA050_SRF","RF_DFEPRO2") + oModel:GetValue("GPEA050_SRF","RF_DFEPRO3") > 0
		lRet := Gp050DIAF()
	EndIf
EndIf

If lRet .And. cPaisLoc == "BRA" .And. !IsInCallStack("GPEA053") .And. oGrid:nLine > 1
	For nCont := oGrid:nLine - 1 To 1 Step - 1
		oGrid:GoLine( nCont )
		If !oGrid:IsDeleted() .And. oGrid:GetValue("RF_STATUS") == "1" .And. Empty(oGrid:GetValue("RF_DATAINI")) .And. Empty(oGrid:GetValue("RF_DATINI2")) .And. Empty(oGrid:GetValue("RF_DATINI3"))
			If !lGp050Auto
				Aviso( STR0016, OemToAnsi(STR0074), {"Ok"} ) //"Verifique se há programação de férias cadastrada para o período aquisitivo anterior para evitar problema no cálculo de férias"
			EndIf
			Exit
		EndIf
	Next nCont
	oGrid:GoLine( nLine )
EndIf

Return ( lRet ) 

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³gp050DescPD   ³Autor³Reginaldo            ³ Data ³28/11/2005³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Preenche o Campo Descricao da Verba                         ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function GP050DescPd(cPd)
Local oModel	:= FWModelActive()
Local cDescPd
Local lGestPubl 	:= If(ExistFunc("fUsaGFP"),fUsaGFP(),.f.) //Verifica se utiliza o modulo de Gestao de Folha Publica - SIGAGFP	
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Carrega a Descricao da Verba                                  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

cDescPd := fDesc( "SRV" , cPd , "RV_DESC" )
If lGestPubl .AND. M->RA_REGIME == '2'
	Return(.T.)
EndIf
oModel:LoadValue("GPEA050_SRF","RF_DESCPD",cDescPd)

Return ( .T. )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³GP050ATDIAS	  ³ Autor ³ Leandro Drumond       ³ Data ³05.02.2013³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Atualiza os campo Data Fim Periodo e Dias de Direito dependendo do³±±
±±³          ³tipo de verba selecionada para ATS.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GP050ATDIAS(dDataIni, dDataFim, cPd)                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Valid de campo                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß */
Function GP050ATDIAS(dDataIni, dDataFim, cPd)

Local oModel	:= FWModelActive()
Local lATS   	:= .F.
Local sDataIni 
Local nDiasDir  := 0
Local lGestPubl := if(ExistFunc("fUsaGFP"),fUsaGFP(),.f.) //Verifica se utiliza o modulo de Gestao de Folha Publica - SIGAGFP	



OMODEL := FWModelActive()
If lGestPubl .And. M->RA_REGIME == '2'
	Return(GP051ATDIAS()) //Desvia para a GP051ATDIAS
ElseIf !lGestPubl .And. 'RF_DATAFIM' $ ReadVar()
	Return .T.
EndIf
DEFAULT cPd       := oModel:GetValue("GPEA050_SRF","RF_PD")
DEFAULT dDataFim  := CtoD('')

If !Empty(dDataIni)

	sDataIni := DtoS(dDataIni)

	If cPd == FGETCODFOL("0001")
	   dDataFim := StoD(StrZero(val(Substr(sDataIni,1,4))+1,4)+Substr(sDataIni,5,4))
	   lAts := .T.
	ElseIf cPd == FGETCODFOL("0002")
	   dDataFim := StoD(StrZero(val(Substr(sDataIni,1,4))+2,4)+Substr(sDataIni,5,4))
	   lAts := .T.   
	ElseIf cPd == FGETCODFOL("0003")
	   dDataFim := StoD(StrZero(val(Substr(sDataIni,1,4))+3,4)+Substr(sDataIni,5,4))
	   lAts := .T.
	ElseIf cPd == FGETCODFOL("0004")
	   dDataFim := StoD(StrZero(val(Substr(sDataIni,1,4))+4,4)+Substr(sDataIni,5,4))
	   lAts := .T.
	ElseIf cPd == FGETCODFOL("0005")
	   dDataFim := StoD(StrZero(val(Substr(sDataIni,1,4))+5,4)+Substr(sDataIni,5,4))
	   lAts := .T.
	EndIf

	If lAts 
		nDiasDir :=  dDataFim - dDataIni 
		dDataFim --
        //Se for inicializador padrao, a linha corrente (n) nao esta atualizada
        //, por isso nao se deve alterar ou se estiver montando a tela, o acols ainda 
        //nao estara completamente pronto
        If !IsInCallStack("InitPad") .And. oModel:GetModel("GPEA050_SRF"):Length() > 0
        	oModel:LoadValue("GPEA050_SRF","RF_DATAFIM",dDataFim)
			If cPaisLoc != "MEX" 
				oModel:LoadValue("GPEA050_SRF","RF_DIASDIR",nDiasDir)
			EndIf
		EndIf
	EndIf
EndIf

Return ( .T. ) 

/*/{Protheus.doc}RfDataInit
Se estiver no primeiro registro da getDados, ira inicializar data base 
para o lancamento com referencia na data de admissao e ano atual, 
se não, irá inicializar a partir do último ano lancado
@author Microsiga
@since 12/12/2008
/*/
Function RfDataInit()

	Local aArea			:= GetArea()
	Local oModel		:= FWModelActive()
	Local oModelSRF		:= oModel:GetModel("GPEA050_SRF")
	Local cMat			:= ""
	Local dRet			:= cToD("  /  /  ")
	Local lFirst		:= .F.
	Local lPosicioned	:= .F.
	Local nI			:= 0
	Local nTamLoop		:= 0
	Local nPosField		:= 0
	Local nPosDeleted	:= 0

	// Carrega a variavel cMat
	cMat := oModel:GetValue("GPEA050_SRA","RA_MAT")

	// Primeiro registro?
	If oModelSRF:Length() <= 1
		lFirst := .T.
	EndIf 

	If (!Empty(cMat))
		// Se nao estiver posicionado, ira posicionar
		If ( SRA->RA_MAT != cMat ) .and. ( lFirst )
			SRA->(dbSelectArea("SRA"))
			SRA->(dbSetOrder( RetOrder( "RA_FILIAL+RA_MAT" )))
			If SRA->( dbSeek( xFilial("SRA") + cMat ) )
			    lPosicioned := .T. 
			Else
				lPosicioned := .F.
			EndIf
		Else
			lPosicioned := .T.
		EndIf

		// Encontra o maior ano
		If ( lFirst )
			// Para o primeiro registro, o ano inicial do periodo deve ser o ano de admissao
			If ( ! Empty(oModel:GetValue("GPEA050_SRA","RA_ADMISSA")))
				If !Empty(oModel:GetValue("GPEA050_SRA","RA_ADMISSA")) .and. lPosicioned
					dRet := oModel:GetValue("GPEA050_SRA","RA_ADMISSA")
				EndIf
			EndIf
		Else
			If ( Type("oGet:aHeader") != "U")
				nPosField 	:= gdFieldPos("RF_DATAFIM", aSRFHeader)
				nPosDeleted := gdFieldPos( "GDDELETED", aSRFHeader )
			EndIf  

			//Procura pelo maior lancamento
			nI := 1
			nTamLoop := oModelSRF:Length()
			While nI <= nTamLoop
				oModelSRF:GoLine(nI)

				If !Empty( oModelSRF:GetValue("RF_DATAFIM") ) .and.  !( oModelSRF:IsDeleted())
					If ( oModelSRF:GetValue("RF_DATAFIM")  > dRet )
						dRet := oModelSRF:GetValue("RF_DATAFIM")
					EndIf
				EndIf
				nI ++
			EndDo
		EndIf

		If Empty(dRet)
			If cPaisLoc = "CHI"
				If ( valType(oModel:GetValue("GPEA050_SRA","RA_ADMISSA")) != "U")
					If !Empty(oModel:GetValue("GPEA050_SRA","RA_ADMISSA")) .and. lPosicioned
						dRet := StoD(Substr(DTOS(dDataBase),1,4)+Substr(DTOS(oModel:GetValue("GPEA050_SRA","RA_ADMISSA")),5,4))
					EndIf
				EndIf
			Else
				dRet := dDataBase
			Endif
		EndIf

		If !(lFirst)
			// Se nao for o primeiro registro, adiciona um dia na data fim para comecar
			// um novo ciclo. No caso do primeiro registro o novo ciclo comeca com a data
			// de admissao mesmo
			dRet++
		EndIf
	EndIf

	RestArea( aArea )

Return dRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEA050   ºAutor  ³Microsiga           º Data ³  12/15/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Verifica se a verba indicada e uma verba de ausencia relaci-º±±
±±º          ³onada na tabela RCM                                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function PdIsAbsence( cVerba, lShowHelp )
Local aArea:= GetArea()
Local lRet := .T.

DEFAULT lShowHelp := .F.

If MV_MODFOL == "2"
	lRet := .F.
	If !Empty(cVerba)
		dbSelectArea("RCM")
		RCM->( dbSetOrder( RetOrder("RCM", "RCM_FILIAL + RCM_PD" ) ) )
		lRet := RCM->( dbSeek(xFilial("RCM") + cVerba ) )  
		If RCM->RCM_TIPOAF != "4"
			lRet := .F.
		EndIf
	EndIf
EndIf

If !lRet .and. lShowHelp
	Help("", 1, "RF_PD")
EndIf
	
RestArea(aArea)

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEA050   ºAutor  ³Leandro Drumond     º Data ³  05/02/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Filtra as verbas de ausencia para a consulta padrao SRVSRF. º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function PdFilAbsence()
Local aArea   := {}
Local cFiltro := ""

If MV_MODFOL == "2"
	aArea   := GetArea()
	dbSelectArea("RCM")
	cFiltro := "@#SRV->RV_COD $ '"
	RCM->( dbSetOrder( RetOrder("RCM", "RCM_FILIAL + RCM_PD" ) ) )
	RCM->( dbSeek(xFilial("RCM") ) )
	While RCM->(!Eof() .and. RCM_FILIAL == xFilial("RCM"))
		If RCM->RCM_TIPOAF == "4"
			cFiltro += RCM->RCM_PD + "|"
		EndIf
		DbSkip()	
	EndDo
	cFiltro += "'@#"
	RestArea(aArea)
Else
	cFiltro := "@#.T.@#"
EndIf

Return cFiltro

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GPEA050   ºAutor  ³Microsiga           º Data ³  12/16/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function RfDataFimInit()
Local oModel
Local cPd 		:= ""  
Local dRet	
Local aArea 	:= {}

If MV_MODFOL == "2" 
	aArea  := GetArea()
	oModel := FWModelActive()
	
	//Se for um inicializador padrao que estiver executando
	//carrega a verba com o que tiver na ultima linha de aCols
	If IsInCallStack("INITPAD")
		cPd := oModel:GetValue("GPEA050_SRF","RF_PD") 
	EndIf
    
	If Empty(cPd)
		cPd := Space(GetSx3Cache( "RV_COD", "X3_TAMANHO"))
	EndIf
	
	//Informa a data de inicio, e solicita a dataFim do periodo
	//mediante verba informada              
	Gp050AtDias(RfDataInit() , @dRet, cPd)

	If Empty(dRet)	
		dRet := CtoD('')	
	EndIf  
	
	RestArea(aArea)                    
Else 
	dRet := dDataBase
EndIf
		
Return dRet
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³DistDFer050 ³ Autor ³ Mohanad Odeh          ³ Data ³ 14/09/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica se o programa que chama é GPEA050                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ DistDFer050()                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/         
Function DistDFer050()

If FunName() == "GPEA050"
	Return .T.
Else 
	Return DistDFerias()
EndIf 

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WHEN_RHIDTFIM³ Autor ³ Mohanad Odeh          ³ Data ³16/09/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validação para chamar a função fDataferM2 com os parametros    ±±
±±³          ³de fontes diferente do GPEA050                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³WHEN_RHIDTFIM()                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function WHEN_RHIDTFIM()

If cPaisLoc == "DOM"
	If FunName() <> "GPEA050"
		Return fValidDur() .AND. fDataferM2( M->RHI_DTINI, M->RHI_DFERIA, "M->RHI_DTFIM", "M->RHI_DTAVIS")
	EndIf
EndIf

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³When_RhDtIni ³ Autor ³ Leandro Drumond       ³ Data ³06/02/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³When do campo RHI_DTINI                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³WHEN_RHIDTFIM()                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function When_RhDtIni()
Local oModel := FWModelActive() 
Local lRet 	 := .F.
If FunName() <> "GPEA050"
	If oModel:GetOperation() == MODEL_OPERATION_INSERT
		lRet := .T.
	EndIf
Else
	If Empty(oModel:GetValue("GPEA050_RHI","RHI_DTINI"))
		lRet := .T.
	EndIf
EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³TriggerDFer ³ Autor ³ Mohanad Odeh          ³ Data ³ 16/09/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Gatilho para calcular chamar a função fDataferM2 com os       ³±±
±±³          ³parametros do fonte GPEA050                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³TriggerDFer()                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function TriggerDFer()
Local oModel

If FunName() == "GPEA050"
	oModel := FWModelActive()
	fDataferM2( oModel:GetValue("GPEA050_RHI","RHI_DTINI"),oModel:GetValue("GPEA050_RHI","RHI_DFERIA"), "", "M->RHI_DTAVIS" )
	Return(oModel:GetValue("GPEA050_RHI","RHI_DFERIA"))
EndIf

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³VldDTIni050 ³ Autor ³ Mohanad Odeh          ³ Data ³ 14/09/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida RHI_DTINI permitindo alteração somente se for inclusão ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³VldDTIni050()                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function VldDTIni050()
Local oModel	:= FWModelActive()
Local lRet		:= .T.
                    
If HabilitaAba()
	If !oModel:GetModel('GPEA050_RHI'):IsInserted() // VERIFICA SE RECNO É DIFERENTE DE ZERO
		lRet := .F.
	EndIf
EndIf

Return lRet	

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³CPerProg050 ³ Autor ³ Mohanad Odeh          ³ Data ³ 06/09/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Preenche itens de calculo (RHJ) de acordo com RHI_DTINI.      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³CPerProg050()                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function CPerProg050()

If FunName() == "GPEA050"
	Return .T.
Else 
	Return CriaPerAqu()
EndIf

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Gp050PVal ³ Autor ³ Mohanad Odeh            ³ Data ³ 03/04/14 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Gp050PVal()                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function Gp050PVal(oModel)
Local lRet := .T.

lGp050Auto := IIf( type("lGp050Auto") == "U", IsBlind() , lGp050Auto) 

If(!lGp050Auto)	.And. cPaisLoc <> "COL"
	If !MsgYesNo(STR0037 + chr(13) + chr(10) + STR0038,STR0016) // "Toda e qualquer alteracao feita neste cadastro interferira diretamente no calculo de ferias." ### "Deseja confirmar?" ### Atencao
		Help(" ",1,".GPEA050ALT.") // P: As alterações realizadas não foram consideradas para gravação. S: Para finalizar, selecione o botão 'Fechar' 
		lRet := .F.
	EndIf 
EndIf

Return lRet


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gp050Commit ³ Autor ³ Leandro Drumond       ³ Data ³ 05/02/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Preenche itens de calculo (RHJ) de acordo com RHI_DTINI.      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Gp050Commit()                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Gp050Commit(oModel)
Local aPerFerias	:= {}
Local aArea			:= {}
Local cTipoAfa		:= ""
Local cVrbDescVac	:= ""
Local oModelRHI 	:= oModel:GetModel( 'GPEA050_RHI' )
Local oModelSRF
Local nAux			:= 0
Local nX			:= 0
Local nDFerVen		:= 0 //Dias de ferias vencidas
Local nDFerAVe		:= 0 //Dias de ferias a vencer
Local nDFerPag		:= 0
Local nDADesc 		:= 0
Local nDaPagar 		:= 0
Local nRecAux       := 0
Local lDelTAE       := .F.

lGp050Auto := IIf( type("lGp050Auto") == "U", IsBlind() , lGp050Auto)

If HabilitaAba() //Caso utilize a aba RHI

	/*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Busca verba de gozo de ferias.     					  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	cVrbDescVac:= fGetCodFol("0786")
	
	If oModel:GetOperation() == MODEL_OPERATION_INSERT .or. oModel:GetOperation() == MODEL_OPERATION_UPDATE
	
		// DELETA REGISTROS
		For nX := 1 To oModelRHI:Length()
			oModelRHI:GoLine( nX )
			//Linha foi apagada e nao eh nova
			If oModelRHI:IsDeleted() .and. !oModelRHI:IsInserted()
			  	DbSelectArea("RHI")
				DbSetOrder(1)
				If DbSeek(xFilial("SRA") + SRA->RA_MAT + DtoS(oModelRHI:GetValue("RHI_DTINI")))
					If RHI->( RecLock( "RHI" , .F., .F. ))
						RHI->(dbDelete())
						RHI->(MsUnLock())
					EndIf					
				EndIf
				DbCloseArea()
	
				DbSelectArea("RHJ")
				DbSetOrder(1)
				DbSeek( xFilial("RHI") + SRA->RA_MAT)
				RHJ->( DbGoTop() )
	
				While RHJ->( !Eof() )
					If RHJ->RHJ_DTINI == oModelRHI:GetValue("RHI_DTINI")
						If RHJ->( RecLock( "RHJ" , .F., .F. ))
							RHJ->( dbDelete() )
							RHJ->( MsUnLock() )
						EndIf
					EndIF
					RHJ->( DbSkip() )
				EndDo
				DbCloseArea()
	
				//SR8 - AUSENCIAS
				DbSelectArea("SR8")
				SR8->(DbSetOrder( RetOrdem( "SR8", "R8_FILIAL+R8_NUMID") ))
				If SR8->(dbSeek(xFilial("SR8")+"SR8"+SRA->RA_MAT+cVrbDescVac+DtoS(oModelRHI:GetValue("RHI_DTINI"))))
					RecLock("SR8",.F.)
					SR8->( dbDelete() )
					SR8->( MsUnlock() )
				EndIf
				SR8->(dbCloseArea())
			EndIf
		Next
	
		For nX := 1 To oModelRHI:Length()
			If !oModelRHI:IsDeleted()  .and. oModelRHI:IsInserted() //Linha nao foi deletada e eh nova
	
				CargaFerias(@aPerFerias, oModelRHI:GetValue("RHI_DTINI"))
				nAux := 1	
				If !(Len(aPerFerias) < 1)
					DbSelectArea("RHJ")
	
					//Dias a descontar que ainda nao foram compensados
					dbGoTop()
					dbSeek(xFilial("RHJ")+SRA->RA_MAT+ DtoS(aPerFerias[nAux, 1]))
					While RHJ->(RHJ_FILIAL+RHJ_MAT+DtoS(RHJ_DTBASE)) == xFilial("RHJ")+SRA->RA_MAT+DtoS(aPerFerias[nAux, 1])
						If RHJ->RHJ_STATUS == "1" .And. RHJ->RHJ_DTINI <> oModelRHI:GetValue("RHI_DTINI")
							nDADesc+= RHJ->RHJ_DIASPG
							//Calculando dias a descontar dentro do periodo posicionado
							nDaPagar:= aPerFerias[nAux, 4] + aPerFerias[nAux, 5] - aPerFerias[nAux, 7] - nDADesc
						EndIf
						RHJ->(dbSkip())
					EndDo         
			
					/*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Inclui linhas novas na grid do RHJ 	  					  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					For nAux:= 1 to len(aPerFerias)
						If RHJ->(RecLock("RHJ", .T.))
							RHJ->RHJ_FILIAL := SRA->RA_FILIAL
							RHJ->RHJ_MAT  	:= SRA->RA_MAT
							RHJ->RHJ_DTINI 	:= oModelRHI:GetValue("RHI_DTINI")
			
							RHJ->RHJ_DTBASE := aPerFerias[nAux, 1]
							RHJ->RHJ_DTFIM 	:= aPerFerias[nAux, 2]
			
							RHJ->RHJ_DIASDI := aPerFerias[nAux, 3] //dias de direito
							RHJ->RHJ_DFERAA := aPerFerias[nAux, 4]  //dias proporcionais
							RHJ->RHJ_DFERVA := aPerFerias[nAux, 5]  //dias vencidos
							RHJ->RHJ_DIASAN := aPerFerias[nAux, 6]  //dias antecipados
							RHJ->RHJ_DFERAN := aPerFerias[nAux, 7]  //dias pagos
			
							RHJ->RHJ_DIASPG := nDaPagar  //dias a descontar
			
							RHJ->( MsUnLock() )
						EndIf		
				    Next
			
				    /*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Carrega dias de ferias vencidas e proporcionais e 	   ³
					//³ desconta dias pagos.								   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				    Aeval(aPerFerias,{|x| (nDFerVen += x[5], nDFerAVe += x[4], nDFerPag += x[7])})
					If nDFerVen > nDFerPag
						nDFerVen:= nDFerVen - nDFerPag
					Else
						nDFerVen:= 0
						nDFerPag:= nDFerPag - nDFerVen
						nDFerAVe:= Max(nDFerAVe - nDFerPag, 0)
					Endif
	
				EndIf
	
			    oModelRHI:LoadValue("RHI_DFERVE",nDFerVen)
			    oModelRHI:LoadValue("RHI_DFERPR",nDFerAVe)
	
				/*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Busca o tipo de ausencia da verba de gozo de ferias	  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				cTipoAfa:= gp240RetCont("RCM", RetOrdem( "RCM", "RCM_FILIAL+RCM_PD" ), xFilial("RCM") + cVrbDescVac, "RCM_TIPO")
	
			EndIf
				
			If oModelRHI:IsInserted()
	
				/*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Busca o proximo numero sequencial.                    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				dbSelectArea("SR8")
				SR8->(DbSetOrder( RetOrdem( "SR8", "R8_FILIAL+R8_MAT+R8_SEQ" ) ))
				If SR8->(dbSeek(xFilial("SR8")+SRA->RA_MAT))
					While SR8->(R8_FILIAL+R8_MAT) == xFilial("SR8")+SRA->RA_MAT
						cSequencia:= SR8->R8_SEQ
						SR8->(dbSkip())
					EndDo
					cSequencia:= StrZero(Val(cSequencia) + 1, 3)
				Else
					cSequencia:= "001"
				EndIf
	
				/*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Cria o registro referente as ferias lancadas          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				RecLock("SR8", .T.)
					SR8->R8_FILIAL	:= xFilial("SR8")
					SR8->R8_MAT		:= SRA->RA_MAT
					SR8->R8_SEQ		:= cSequencia
					SR8->R8_DATA	:= Date()
					SR8->R8_TIPOAFA	:= cTipoAfa
					SR8->R8_PD		:= cVrbDescVac
					SR8->R8_DATAINI	:= oModelRHI:GetValue("RHI_DTINI")
					SR8->R8_DURACAO	:= oModelRHI:GetValue("RHI_DFERIA")
					SR8->R8_DATAFIM	:= oModelRHI:GetValue("RHI_DTFIM")
					SR8->R8_DIASEMP := 999
					SR8->R8_DPAGAR	:= oModelRHI:GetValue("RHI_DFERIA")
					SR8->R8_CONTINU	:= "2"
					SR8->R8_DNAPLIC	:= 0
					SR8->R8_DPAGOS	:= 0
					SR8->R8_PER		:= oModelRHI:GetValue("RHI_PERIOD")
					SR8->R8_NUMPAGO	:= oModelRHI:GetValue("RHI_NUMPAG")
					SR8->R8_NUMID	:= "SR8" + SRA->RA_MAT + cVrbDescVac + Dtos(oModelRHI:GetValue("RHI_DTINI"))
					SR8->R8_SDPAGAR	:= oModelRHI:GetValue("RHI_DFERIA")
					SR8->R8_PROCES	:= oModelRHI:GetValue("RHI_PROCES")
				MsUnlock()
				SR8->(dbCloseArea())
			Else
				dbSelectArea("SR8")
				SR8->(DbSetOrder(RetOrdem("SR8", "R8_FILIAL+R8_NUMID"))) 
				If SR8->(dbSeek(xFilial("SR8")+"SR8"+SRA->RA_MAT+cVrbDescVac+DtoS(oModelRHI:GetValue("RHI_DTINI"))))
					RecLock("SR8",.F.)
						SR8->R8_DATA	:= Date()
						SR8->R8_TIPOAFA	:= cTipoAfa
						SR8->R8_DURACAO	:= oModelRHI:GetValue("RHI_DFERIA")
						SR8->R8_DATAFIM	:= oModelRHI:GetValue("RHI_DTFIM")
						SR8->R8_DPAGAR	:= oModelRHI:GetValue("RHI_DFERIA")
						SR8->R8_PER		:= oModelRHI:GetValue("RHI_PERIOD")
						SR8->R8_NUMPAGO	:= oModelRHI:GetValue("RHI_NUMPAG")
						SR8->R8_SDPAGAR	:= oModelRHI:GetValue("RHI_DFERIA")
						SR8->R8_PROCES	:= oModelRHI:GetValue("RHI_PROCES")
					SR8->(MsUnlock())
					SR8->(dbCloseArea())
				EndIf
		    EndIf
	    Next
	EndIf
EndIf

//Verifica se houve mudança na programação de férias e exclui eventuais avisos de férias que tenham sido enviados para o TAE
If lTemREQ

	oModelSRF 	:= oModel:GetModel( 'GPEA050_SRF' )
	
	For nX := 1 To oModelSRF:Length()
		oModelSRF:GoLine( nX )

		If oModel:GetOperation() == MODEL_OPERATION_DELETE //Se for exclusão
			If !Empty(oModelSRF:GetValue("RF_DATAINI"))
				If fDelDocTAE(SRA->RA_FILIAL, SRA->RA_MAT, , "2", oModelSRF:GetValue("RF_DATAINI"))
					lDelTAE := .T.
				EndIf
			EndIf
			If !Empty(oModelSRF:GetValue("RF_DATINI2"))
				If fDelDocTAE(SRA->RA_FILIAL, SRA->RA_MAT, , "2", oModelSRF:GetValue("RF_DATINI2"))
					lDelTAE := .T.
				EndIf
			EndIf
			If !Empty(oModelSRF:GetValue("RF_DATINI3"))
				If fDelDocTAE(SRA->RA_FILIAL, SRA->RA_MAT, , "2", oModelSRF:GetValue("RF_DATINI3"))
					lDelTAE := .T.
				EndIf
			EndIf
		Else
			//Linha foi apagada e nao eh nova
			If oModelSRF:IsDeleted() .and. !oModelSRF:IsInserted()
				If !Empty(oModelSRF:GetValue("RF_DATAINI"))
					If fDelDocTAE(SRA->RA_FILIAL, SRA->RA_MAT, , "2", oModelSRF:GetValue("RF_DATAINI"))
						lDelTAE := .T.
					EndIf
				EndIf
				If !Empty(oModelSRF:GetValue("RF_DATINI2"))
					If fDelDocTAE(SRA->RA_FILIAL, SRA->RA_MAT, , "2", oModelSRF:GetValue("RF_DATINI2"))
						lDelTAE := .T.
					EndIf
				EndIf
				If !Empty(oModelSRF:GetValue("RF_DATINI3"))
					If fDelDocTAE(SRA->RA_FILIAL, SRA->RA_MAT, , "2", oModelSRF:GetValue("RF_DATINI3"))
						lDelTAE := .T.
					EndIf
				EndIf
			EndIf
			If oModelSRF:IsUpdated()
				//Se a data de programação de férias foi alterada, exclui o aviso de férias do TAE, caso exista.
				If !Empty(oModelSRF:GetValue("RF_DATAINI")) .or. !Empty(oModelSRF:GetValue("RF_DATINI2")) .or. !Empty(oModelSRF:GetValue("RF_DATINI3")) .or. ;
					oModelSRF:IsFieldUpdated("RF_DATAINI") .or. oModelSRF:IsFieldUpdated("RF_DATINI2") .or. oModelSRF:IsFieldUpdated("RF_DATINI3")
				
					aArea := GetArea()
					
					nRecAux := oModelSRF:GetDataId()

					SRF->(DbGoTo(nRecAux))

					If !Empty(SRF->RF_DATAINI) .and. SRF->RF_DATAINI <> oModelSRF:GetValue("RF_DATAINI")
						If fDelDocTAE(SRA->RA_FILIAL, SRA->RA_MAT, , "2", SRF->RF_DATAINI)
							lDelTAE := .T.
						EndIf
					EndIf
					If !Empty(SRF->RF_DATINI2) .and. SRF->RF_DATINI2 <> oModelSRF:GetValue("RF_DATINI2")
						If fDelDocTAE(SRA->RA_FILIAL, SRA->RA_MAT, , "2", SRF->RF_DATINI2)
							lDelTAE := .T.
						EndIf
					EndIf
					If !Empty(SRF->RF_DATINI3) .and. SRF->RF_DATINI3 <> oModelSRF:GetValue("RF_DATINI3")
						If fDelDocTAE(SRA->RA_FILIAL, SRA->RA_MAT, , "2", SRF->RF_DATINI3)
							lDelTAE := .T.
						EndIf
					EndIf

					RestArea(aArea)
				EndIf
			EndIf
		EndIf
	Next nX

	If lDelTAE .and. !lGp050Auto
		Aviso( STR0016, STR0089, {"Ok"} ) //"Atenção" ### "Conforme parametrização os Avisos de Férias integrados ao TAE com programação alterada/excluída foram excluídos do TAE."
	EndIf
EndIf

//Grava dados do modelo
FWFormCommit( oModel )

If oModel:GetOperation() == MODEL_OPERATION_INSERT
	oModel:DeActivate()
	oModel:SetOperation(MODEL_OPERATION_UPDATE)
	oModel:Activate()
EndIf

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ValidRhiDtIni³ Autor ³ Christiane Vieira     ³ Data ³ 26/01/12³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validação do campo RHI_DTINI quando o mesmo estiver no fonte  ³±±
±±³          ³GPEA050                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ValidRhiDtIni()                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function ValidRhiDtIni()
Local oModel
Local lRet := .F.
Local dDataFim := CtoD("//")

If !FunName() == "GPEA050"
	lRet := fDataferM2( M->RHI_DTINI, M->RHI_DFERIA, "M->RHI_DTFIM", "M->RHI_DTAVIS") .And. CriaPerAqu() .And. DistDFerias()
Else 
	oModel	:= FWModelActive()

	lRet := fDataferM2( oModel:GetValue("GPEA050_RHI","RHI_DTINI"), oModel:GetValue("GPEA050_RHI","RHI_DFERIA"), "M->RHI_DTFIM", "M->RHI_DTAVIS") 
	
	If !Empty(oModel:GetValue("GPEA050_RHI","RHI_DFERIA"))
		GpeCalend(,,,,, oModel:GetValue("GPEA050_RHI","RHI_DTINI"), @dDataFim, oModel:GetValue("GPEA050_RHI","RHI_DFERIA"), "F", ReadVar(), .F.)
		oModel:LoadValue("GPEA050_RHI","RHI_DTFIM",dDataFim)	
	EndIf
EndIf

Return (lRet)
/*/                                                           

ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ValidRhiDFeria³ Autor ³ Christiane Vieira   ³ Data ³ 27/01/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validação do campo RHI_DFERIA quando o mesmo estiver no fonte  ±±
±±³          ³GPEA050                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ValidRhiDFeria()                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function ValidRhiDFeria()
Local oModel
Local lRet := .F.     
Local dDataFim := CtoD("//")

If !FunName() == "GPEA050"
	lRet := fValidDur() .And. fDataferM2( M->RHI_DTINI,M->RHI_DFERIA, "M->RHI_DTFIM", "M->RHI_DTAVIS") .And. DistDFerias()
Else
    oModel	:= FWModelActive()
	                                                                                                            
	lRet := fDataferM2( oModel:GetValue("GPEA050_RHI","RHI_DTINI"), oModel:GetValue("GPEA050_RHI","RHI_DFERIA"), "M->RHI_DTFIM", "M->RHI_DTAVIS") 
	
	GpeCalend(,,,,, oModel:GetValue("GPEA050_RHI","RHI_DTINI"), @dDataFim, oModel:GetValue("GPEA050_RHI","RHI_DFERIA"), "F", oModel:GetValue("GPEA050_RHI","RHI_DFERIA"), .F.)
	oModel:LoadValue("GPEA050_RHI","RHI_DTFIM",dDataFim)
EndIf
	
Return (lRet)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³GP50DtIni   ³ Autor ³ Equipe RH			  ³ Data ³ 17/05/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validação do RF_DATABAS, para nao aceitar data menor que a    ³±±
±±³          ³admissão do funcionario                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³GP50DtIni()		                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/

Function GP50DtIni()

Local dRFData		:= Ctod("//")
Local lRFDataOk 	:= .T.
Local lGestPubl 	:= if(ExistFunc("fUsaGFP"),fUsaGFP(),.f.) //Verifica se utiliza o modulo de Gestao de Folha Publica - SIGAGFP	

If lGestPubl .AND. M->RA_REGIME == '2'
	Return(.T.) //Nao utiliza estava funcao, pois o GFP valida tudo pela GP051ATDIAS, chamada e desviada pela GP050ATDIAS
EndIf
Begin Sequence

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³A Data nao Pode esta em Branco                               ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	IF !( lRFDataOk := NaoVazio() )  
		Break
	EndIF

	/*/
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³Carrega o Conteudo da Memoria                                ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
	dRFData := &( ReadVar() )
	If dRFData < SRA->RA_ADMISSA
		Help(" ",1,".GPEA25001.")
		lRFDataOk := .F.
		break
	EndIF  

End Sequence

Return( lRFDataOk )

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Gp050DIAF  ³ Autor ³ Antonio Carmo        ³ Data ³ 30.09.13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validacao dos dias de férias, não pode passar de 30 dias.   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³GPEA050                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Gp050DIAF()

Local lRet 		:= .T.
Local oModel 	:= FWModelActive()
Local nIdade 	:= DateDiffYear( oModel:GetValue("GPEA050_SRF","RF_DATAINI"),SRA->RA_NASC) 
Local lIdade 	:= !lRefTrab .And. (nIdade < 18 .Or. nIdade >= 50)
Local aTabFer	:= Array(4)
Local nQtdDia3Pr:= 0 // Total de dias das 3 programações
Local aPerAtual	:= {}
Local dIniPerRCH:= CtoD("//") // Primeiro dia do periodo atual 
Local dDtInteg	:=  CtoD("//") // Data da Integração das Férias
Local SRHArea   := SRH->(GetArea())

lGp050Auto := IIf( type("lGp050Auto") == "U", IsBlind() , lGp050Auto)

If IsInCallStack("GPEA053")
	If !fTab_Fer(@aTabFer)
		aTabFer[3] := 30
	EndIf
EndIf

dbSelectArea("SRH")
dbSetOrder(2)

If lIdade .and. oModel:GetValue("GPEA050_SRF","RF_DFEPRO1") + oModel:GetValue("GPEA050_SRF","RF_DABPRO1") < 30
	If lGp050Auto
		AutoGrLog( STR0059 )  // Gera o erro da execauto para captura na origem da chamada
	Else
		MsgAlert( STR0059 , OemToAnsi( STR0016 ) )
	EndIf	
EndIf

If fGetPerAtual( @aPerAtual, xFilial("RCH"),SRA->RA_PROCES ,fGetCalcRot("3"))
	dIniPerRCH := aPerAtual[1][6]
	dDtInteg	:= aPerAtual[1][9]
EndIf

If !(oModel:GetValue("GPEA050_SRF","RF_DATAINI") < dIniPerRCH) .OR. Empty(oModel:GetValue("GPEA050_SRF","RF_DATAINI"))
	If Empty(dDtInteg) .Or. !SRH->(DbSeek(SRA->RA_FILIAL+SRA->RA_MAT+DTOS(oModel:GetValue("GPEA050_SRF","RF_DATAINI"))))
		nQtdDia3Pr += oModel:GetValue("GPEA050_SRF","RF_DFEPRO1") +  oModel:GetValue("GPEA050_SRF","RF_DABPRO1")
	EndIf
EndIf

If !(oModel:GetValue("GPEA050_SRF","RF_DATINI2") < dIniPerRCH) .OR. Empty(oModel:GetValue("GPEA050_SRF","RF_DATINI2"))
	If Empty(dDtInteg) .Or.  !SRH->(DbSeek(SRA->RA_FILIAL+SRA->RA_MAT+DTOS(oModel:GetValue("GPEA050_SRF","RF_DATINI2"))))
		nQtdDia3Pr += oModel:GetValue("GPEA050_SRF","RF_DFEPRO2") +  oModel:GetValue("GPEA050_SRF","RF_DABPRO2")
	Endif
EndIf

If !(oModel:GetValue("GPEA050_SRF","RF_DATINI3") < dIniPerRCH) .OR. Empty(oModel:GetValue("GPEA050_SRF","RF_DATINI3"))
	If Empty(dDtInteg) .Or. !SRH->(DbSeek(SRA->RA_FILIAL+SRA->RA_MAT+DTOS(oModel:GetValue("GPEA050_SRF","RF_DATINI3"))))
		nQtdDia3Pr += oModel:GetValue("GPEA050_SRF","RF_DFEPRO3") +  oModel:GetValue("GPEA050_SRF","RF_DABPRO3")
	Endif
EndIf

If !IsInCallStack("GPEA053")
	nQtdDia3Pr += oModel:GetValue("GPEA050_SRF","RF_DFERANT")
EndIf

If nQtdDia3Pr <=  If( !IsInCallStack("GPEA053"), oModel:GetValue("GPEA050_SRF","RF_DIASDIR"), aTabFer[3])
	lRet := .T.
Else
	If lGp050Auto .And. IsBlind()
		AutoGrLog( OemToAnsi(STR0044) )  //"A quantidade de dias de férias e dias de abono das três programações ultrapassam 30 dias."
	Else
		Help(,,STR0016,, OemToAnsi(STR0044) ,1,0 )
	EndIf
	lRet := .F.
EndIf

RestArea (SRHArea)
Return( lRet )

/*/{Protheus.doc} IntegDef
	Rotina responsável pela Mensagem Única da Rotina GPEA050
@author PHILIPE.POMPEU
@since 14/04/2015
@version P12
@param cXML, character, o XML em forma de string
@param nTypeTrans, numérico, Tipo de Transmissão
@param cTypeMessage, character, Tipo da Mensagem
@param cVerMsg, character, Versão da Mensagem
@return aRet, Vetor
/*/
Static Function IntegDef( cXML, nTypeTrans, cTypeMessage,cVerMsg)
	Local aRet := {}
	Default cVerMsg := "1.000"
	aRet:= GPEI050( cXML, nTypeTrans, cTypeMessage, cVerMsg)
Return aRet

/*/{Protheus.doc} HabilitaAba
Verifica se Habilita a Aba, função que substitui a variável Private lHabAba,
Variavel para habilitar a aba de Programação de férias
@author philipe.pompeu
@since 17/04/2015
@version P12
@return lResult, Verdadeiro caso deva ser habilitada.
/*/
Static Function HabilitaAba()
	Local lResult := (cPaisLoc == "DOM") 
Return lResult

/*-----------------------------------------------*/
/*/{Protheus.doc} fVldDSR
Valida se range de data passada possui DSR e retorna a data do DSR.
@author esther.viveiro
@since 03/11/2017
@version P12
@return dRet, data do dia de DSR se encontrado, caso contrário retorna data em branco.
/*/
Function fVldDSR(	cFil	,;	//[<@>cFil		] 	-> Opcional, Filial do Funcionario Para Montagem do Calendario
					cMat	,;	//[<@>cMat		] 	-> Opcional, Matricula do Funcionario Para Montagem do Calendario
					dDataIni,;	//[<@>dDataIni] 	-> Opcional, Data a ser verificada
					nIntDias,;	//[<@>nIntDias] 	-> Opcional, Intervalo de Dias  a ser verificado
					cTipo	,;	//[<@>cTipo] 		-> Opcional, Define o tipo do dia para busca (D - DSR / F - Feriado)
					lMeuRH	)	//[<@>Solicitação] 	-> Opcional, Se é do MeuRH

	Local dStartDate	:= Ctod("//")
	Local dDataFim	    := Ctod("//")
	Local dRet          := CtoD("//")
	Local dDataAux		:= CtoD("//")
	Local nStartDate	:= 0.00
	Local nFinishDate	:= 0.00
	Local aTabCalend	:= {}
	Local aTurnos		:= {}
	Local aTabPadrao	:= {}  
	Local aArea		    := GetArea()
	Local cTipoDia		:= ""
	Local cTno			:= ""
	Local cSeq			:= ""
	Local cCc			:= ""
	Local cAuxAlias     := "QRCG"
	Local cRCG          := ""
	Local cQryWhere     := ""
	Local cAliasQry		:= GetNextAlias() 
	Local lFerTab		:= .F.
	Local lPerPonto		:= .F.
	Local lTemAlocacao  := .F.

	Static lUsaGS

	DEFAULT cFil		:= SRA->RA_FILIAL
	DEFAULT cMat		:= SRA->RA_MAT
	DEFAULT dDataIni 	:= Ctod("01/"+StrZero(Month(dDataBase),02)+"/"+StrZero(Year(dDataBase),04),"DD/MM/YY")
	DEFAULT nIntDias	:= 0
	DEFAULT cTipo		:= "D"
	DEFAULT	lMeuRH		:= .F.
	DEFAULT lUsaGS      := GetMvRH("MV_TECXRH", NIL, .F. )

	Private oPeriodo	:= NIL

	If !lMeuRH .and. lUsaGS .and. cTipo == "F" .and. ExistFunc("TecFerAloc")//Se existir integração com GS, verifica se existe alocação e se é feriado na localidade de trabalho
		dDataAux := dDataIni
		dDataFim := dDataIni + nIntDias
		While dDataAux <= dDataFim
			If TecFerAloc(cFil, cMat, dDataAux, @lTemAlocacao)
				Return( dDataAux ) //Se é feriado, retorna o dia do feriado
			EndIf
			dDataAux += 1
		EndDo 
		If lTemAlocacao //Se tem alocação, mas não é feriado, retorna data vazia
			Return(dRet)
		EndIf  
	EndIf

	If !lMeuRH
		lPerPonto := !Empty(GetMv( "MV_PAPONTA" , NIL , "" ))
	Else
		lPerPonto := !Empty(SuperGetMv( "MV_PAPONTA" , NIL , "", cFil ))
	EndIf

	//reposiciona SRA.
	If SRA->(RA_FILIAL+RA_MAT) != cFil + cMat
		If !(SRA->(DbSeek(cFil + cMat)))
				RestArea(aArea)
				Return( dRet )
		EndIf
	EndIf

	cTno	:= SRA->RA_TNOTRAB
	cSeq	:= SRA->RA_SEQTURN
	cCc		:= SRA->RA_CC

	/*Define o Periodo Para Carga das Informacoes em aInfoCalend*/
	dDataFim    := dDataIni + nIntDias
	dStartDate	:= dDataIni
	nFinishDate	:= ( ( dDataFim - dDataIni ) + 1 )

	//Objeto de Periodos - Utilizado no calendario
	If !Empty(cSeq)
		oPeriodo	:= RHPERIODO():New()

		/*Instancia o objeto caso nao exista para uso da funcao CriaCalend*/
		If Type("oPeriodo") == "U"
			SetMemVar("oPeriodo" , RHPERIODO():New() , .T. , .T. )
			oPeriodo:cProcesso := SRA->RA_PROCES
			oPeriodo:GetPer()
		EndIf
	EndIf
	
	dDataAux := Ctod("01/"+StrZero(Month(dDataIni),02)+"/"+StrZero(Year(dDataIni),04),"DD/MM/YY")
	
	//Obtem a última troca de turno
	BeginSql alias cAliasQry
		column PF_DATA as Date
		SELECT 	MAX(PF_DATA) PF_DATA
		FROM 	%table:SPF% SPF
		WHERE 	SPF.PF_FILIAL = %exp:cFil%
		AND SPF.PF_MAT = %exp:cMat%
		AND SPF.PF_DATA <= %exp:DtoS(dDataAux)%
		AND SPF.%NotDel%
	EndSql
	
	If (cAliasQry)->(!Eof())
		dDataAux :=(cAliasQry)->PF_DATA 		
	EndIf
	
	(cAliasQry)->(DbCloseArea())
	
	/*Cria o Calendario de Marcacoes de Acordo com o Periodo Passado*/
	If lPerPonto .And. SRA->( CriaCalend(	dDataAux		,; //01 -> Data Inicial do Periodo
							dDataFim		,; //02 -> Data Final do Periodo
							cTno			,; //03 -> Turno Para a Montagem do Calendario
							cSeq			,; //04 -> Sequencia Inicial para a Montagem Calendario
							@aTabPadrao		,; //05 -> Array Tabela de Horario Padrao
							@aTabCalend		,; //06 -> Array com o Calendario de Marcacoes
							cFil			,; //07 -> Filial para a Montagem da Tabela de Horario
							cMat			,; //08 -> Matricula para a Montagem da Tabela de Horario
							cCc				,; //09 -> Centro de Custo para a Montagem da Tabela
							@aTurnos		,; //10 -> Array com as Trocas de Turno
							NIL				,; //11 -> Array com Todas as Excecoes do Periodo
							NIL	 			,; //12 -> Se executa Query para a Montagem da Tabela Padrao
							.F.				,; //13 -> Se executa a funcao se sincronismo do calendario
							.F.				,; //14 -> Se Forca a Criacao de Novo Calendario
							NIL  			,; //15 -> Array com marcacoes para tratamento de Turnos Opcionais
							.F.  			,; //16 -> .T. Determina a Criacao/Carga do Calendario Fisico
							NIL				,; //17 -> .T. Caso exista calendario fisico 
							NIL				,; //18 -> Data inicial do calendario fisico
							NIL				,; //19 -> Data final do calendario fisico 
							.F.				,; //20 -> .T. determina que o calendario sera gravado no caso de nao existir
							.F.		 	 	; //21 -> .T. determina que a rotina chamadora eh a Geracao de Calendarios (PONM400)
						);
			)

		For nStartDate := 1.00 To nFinishDate
			/*Retorna Conteudo de aTabCalend de Acordo com as Chaves Indices*/
			cTipoDia	:= GetInfoPosTab( CALEND_POS_TIPO_DIA 		, "1E" , dStartDate , aTabCalend )
			lFerTab		:= GetInfoPosTab( CALEND_POS_FERIADO 		, "1E" , dStartDate , aTabCalend )
			If cTipoDia == cTipo .Or. lFerTab
				dRet := dStartDate
				Exit
			EndIf
			dStartDate++
		Next nStartDate
	Else
		// caso não tenha criado o calendário pelo Ponto, verificar a RCG.
		cRCG	 := "%"+RetFullName("RCG",cEmpAnt)+"%"
		cTipo	 := If( cTipo == "D", "3", "4" )
		dDataAux := dDataIni
		
		cQryWhere := "RCG_FILIAL = '" + xFilial("RCG",cFil) + "' "
		cQryWhere += " AND RCG_PROCES = '" + SRA->RA_PROCES + "' "
		cQryWhere += " AND (RCG_TNOTRA = '@@@' OR RCG_TNOTRA = '" + cTno + "' )"
		cQryWhere += " AND RCG_DIAMES >= '" + DtoS(dDataIni) + "' "
		cQryWhere += " AND RCG_DIAMES <= '" + DtoS(dDataFim) + "' "
		cQryWhere := "% "+ cQryWhere + " %"

		BeginSQL Alias cAuxAlias
			SELECT RCG_FILIAL, RCG_DIAMES, RCG_TNOTRA, RCG_SEMANA, RCG_ANO, RCG_MES, RCG_TIPDIA
			FROM %Exp:cRCG% RCG
			WHERE RCG.%notDel% AND %exp:cQryWhere%
			ORDER BY RCG_DIAMES ASC, RCG_TNOTRA DESC, RCG_SEMANA ASC
		EndSQL

		While !(cAuxAlias)->(Eof())
			If (cAuxAlias)->RCG_TIPDIA == cTipo; //DSR ou Feriado
				.AND. ((SRA->RA_TIPOPGT == 'M' .AND. (cAuxAlias)->RCG_SEMANA == '01') .OR. (SRA->RA_TIPOPGT == 'S' .AND. !Empty((cAuxAlias)->RCG_SEMANA)))
				dRet := StoD((cAuxAlias)->RCG_DIAMES)
				Exit
			EndIf
			(cAuxAlias)->(DbSkip())
		EndDo
		(cAuxAlias)->( dbCloseArea() )	
	EndIF
	RestArea(aArea)
Return( dRet )

Static Function fValidSRF(cCampo)
Local lRet	:= .T.

lGp050Auto := IIf( type("lGp050Auto") == "U", IsBlind() , lGp050Auto)
  
If !lGp050Auto 
	If !MsgYesNo(STR0073 + chr(13) + chr(10) + STR0038,STR0016) //"Verifique se o valor está correto antes de finalizar a alteração, pois o mesmo ficará bloqueado imediatamente após o campo ser atualizado e o valor for superior a 0. ### "Deseja confirmar?" ### Atencao
		lRet := .F.
	EndIf
EndIf

Return lRet

/*
{Protheus.doc} fVldExcTAE
Valida se existe documento integrado no TAE e se pode ser efetuada a exclusão/alteração da programação de férias
@author Leandro Drumond
@since 02/12/2021
@version P12
*/
Static Function fVldExcTAE(oGrid, lDelete)
Local aArea
Local lRet 		:= .T.
Local nRet		:= 0
Local nRecAux	:= 0

DEFAULT lDelete := .F.

If ( oGrid:IsDeleted() .and. !oGrid:IsInserted() ) .or. lDelete

	//Se o campo não esta vazio, verifica se existe aviso integrado com o TAE e assinado
	If !Empty(oGrid:GetValue("RF_DATAINI"))
		nRet := fChkDocTAE(SRA->RA_FILIAL, SRA->RA_MAT, , "2", oGrid:GetValue("RF_DATAINI"))
	EndIf

	If nRet < 2 .and. !Empty(oGrid:GetValue("RF_DATINI2"))
		nRet := fChkDocTAE(SRA->RA_FILIAL, SRA->RA_MAT, , "2", oGrid:GetValue("RF_DATINI2"))
	EndIf

	If nRet < 2 .and. !Empty(oGrid:GetValue("RF_DATINI3"))
		nRet := fChkDocTAE(SRA->RA_FILIAL, SRA->RA_MAT, , "2", oGrid:GetValue("RF_DATINI3"))
	EndIf
EndIf

If nRet == 0 .and. ( ( oGrid:IsUpdated() .and. !oGrid:IsInserted() ) .or. lDelete )
	//Se a data de programação de férias foi alterada, exclui o aviso de férias do TAE, caso exista.
	If !Empty(oGrid:GetValue("RF_DATAINI")) .or. !Empty(oGrid:GetValue("RF_DATINI2")) .or. !Empty(oGrid:GetValue("RF_DATINI3")) .or. ;
		oGrid:IsFieldUpdated("RF_DATAINI") .or. oGrid:IsFieldUpdated("RF_DATINI2") .or. oGrid:IsFieldUpdated("RF_DATINI3")
	
		aArea := GetArea()
		
		nRecAux := oGrid:GetDataId()

		If nRecAux > 0
			SRF->(DbGoTo(nRecAux))

			If !Empty(SRF->RF_DATAINI) .and. SRF->RF_DATAINI <> oGrid:GetValue("RF_DATAINI")
				nRet := fChkDocTAE(SRA->RA_FILIAL, SRA->RA_MAT, , "2", SRF->RF_DATAINI)
			EndIf
			If nRet < 2 .and. !Empty(SRF->RF_DATINI2) .and. SRF->RF_DATINI2 <> oGrid:GetValue("RF_DATINI2")
				nRet := fChkDocTAE(SRA->RA_FILIAL, SRA->RA_MAT, , "2", SRF->RF_DATINI2)
			EndIf
			If nRet < 2 .and. !Empty(SRF->RF_DATINI3) .and. SRF->RF_DATINI3 <> oGrid:GetValue("RF_DATINI3")
				nRet := fChkDocTAE(SRA->RA_FILIAL, SRA->RA_MAT, , "2", SRF->RF_DATINI3)
			EndIf
		EndIf

		RestArea(aArea)
	EndIf
EndIf

If nRet == 2
	lRet := .F.
ElseIf nRet == 3
	If nRHTAEEX == 1
		If lGp050Auto
			AutoGrLog( OemToAnsi( STR0090 ) )// "O aviso de férias integrado ao TAE já foi assinado e conforme parametrização do MV_RHTAEEX a programação de férias não poderá ser alterada ou excluída."
		Else
			Help(,,STR0016,, STR0090,1,0 ) //"Atenção" ### "O aviso de férias integrado ao TAE já foi assinado e conforme parametrização do MV_RHTAEEX a programação de férias não poderá ser alterada ou excluída."
		EndIf
		lRet := .F.
	ElseIf nRHTAEEX == 2 
		If lGp050Auto
			AutoGrLog( OemToAnsi( STR0091 ) )// "O aviso de férias integrado ao TAE já foi assinado e não poderá ser excluído."
			lRet := .F.
		ElseIf !MsgYesNo(STR0092,STR0091) //"O aviso de férias integrado ao TAE já foi assinado e não poderá ser excluído."   ### "Prosseguir com a alteração/exclusão da programação de férias? "
			Help(,,STR0016,, STR0091,1,0 ) //"Atenção" ### "O aviso de férias integrado ao TAE já foi assinado e não poderá ser excluído."
			lRet := .F.
		EndIf
	EndIf 
EndIf

Return lRet
