#INCLUDE "PROTHEUS.CH" 

/*/
Ŀ
Funo    InRhLibSxbExecAutor Marinaldo de Jesus    Data 14/05/2003
Ĵ
Descrio Executar Funcoes Dentro de RHLIBSXB                          
Ĵ
Sintaxe   InRhLibSxbExec( cExecIn , aFormParam )						 
Ĵ
Parametros<Vide Parametros Formais>									 
Ĵ
Retorno   uRet                                                 	     
Ĵ
Observao                                                      	     
Ĵ
Uso       Generico 													 
/*/
Function InRhLibSxbExec( cExecIn , aFormParam )
         
Local uRet

DEFAULT cExecIn		:= ""
DEFAULT aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= __ExecMacro( cExecIn )
EndIF

Return( uRet )

/*/
Ŀ
Funo    Rd2SxbFiltAutorMarinaldo de Jesus         Data10/08/2004
Ĵ
Descrio Filtro de Consulta Padrao									
Ĵ
Sintaxe   <Vide Parametros Formais>									
Ĵ
Parametros<Vide Parametros Formais>									
Ĵ
Uso       Consulta Padrao (SXB)				                  	   	
/*/
Function Rd2SxbFilt()
         
Local cReadVar	:= Upper( AllTrim( ReadVar() ) )
Local cRet		:= "@#.T.@#"

Local cCodCom

//RD7 = Refinamento de Avaliacoes
IF ( "RD7_ITECOM" $ cReadVar ) 
	IF ( IsInGetDados( { "RD7_CODCOM" , "RD7_ITECOM" } ) )
		cCodCom	:= GdFieldGet("RD7_CODCOM")
	ElseIF ( IsMemVar( "RD7_CODCOM" ) )
		cCodCom := GetMemVar( "RD7_CODCOM" )
	EndIF
//RD8 = Questoes Modelo de Avaliacao
ElseIF ( "RD8_ITECOM" $ cReadVar )
	IF ( IsInGetDados( { "RD8_CODCOM" , "RD8_ITECOM" } ) )
		cCodCom	:= GdFieldGet("RD8_CODCOM")
	ElseIF ( IsMemVar( "RD8_CODCOM" ) )
		cCodCom := GetMemVar( "RD8_CODCOM" )
	EndIF
//RDB = Respostas de Avaliacoes
ElseIF ( "RDB_ITECOM" $ cReadVar )
	IF ( IsInGetDados( { "RDB_CODCOM" , "RDB_ITECOM" } ) )
		cCodCom	:= GdFieldGet("RDB_CODCOM")
	ElseIF ( IsMemVar( "RDB_CODCOM" ) )
		cCodCom := GetMemVar( "RDB_CODCOM" )
	EndIF
//RDD = Resultado de Avaliacoes
ElseIF ( "RDD_ITECOM" $ cReadVar )
	IF ( IsInGetDados( { "RDD_CODCOM" , "RDD_ITECOM" } ) )
		cCodCom	:= GdFieldGet("RDD_CODCOM")
	ElseIF ( IsMemVar( "RDD_CODCOM" ) )
		cCodCom := GetMemVar( "RDD_CODCOM" )
	EndIF
//RDO = Resultado de Avaliacoes
ElseIF ( "RDO_ITECOM" $ cReadVar )
	IF ( IsInGetDados( { "RDO_CODCOM" , "RDO_ITECOM" } ) )
		cCodCom	:= GdFieldGet("RDO_CODCOM")
	ElseIF ( IsMemVar( "RDO_CODCOM" ) )
		cCodCom := GetMemVar( "RDO_CODCOM" )
	EndIF
EndIF

IF !Empty( cCodCom )
	cRet := "@#RD2->RD2_CODIGO=='"+cCodCom+"'@#"
EndIF

Return( cRet )

/*/
Ŀ
Funo    RdhSxbFilterAutorMarinaldo de Jesus    	Data27/05/2004
Ĵ
Descrio Filtro de Consulta Padrao									
Ĵ
Sintaxe   <Vide Parametros Formais>									
Ĵ
Parametros<Vide Parametros Formais>									
Ĵ
Uso       Consulta Padrao (SXB)				                  	   	
/*/
Function RdhSxbFilter()
         
Local cReadVar	:= Upper( AllTrim( ReadVar() ) )
Local cCodTip	:= ""
Local cRet		:= "@#.T.@#"

//RDA
IF ( "RDA_CODNET" $ cReadVar ) 
	IF ( IsInGetDados( { "RDA_CODTIP" } ) )
		cCodTip	:= GdFieldGet( "RDA_CODTIP" )
	ElseIF ( IsMemVar( "RDA_CODTIP" ) )
		cCodTip := GetMemVar( "RDA_CODTIP" )
	EndIF
//RDB
ElseIF ( "RDB_CODNET" $ cReadVar ) 
	IF ( IsInGetDados( { "RDB_CODTIP" } ) )
		cCodTip	:= GdFieldGet( "RDB_CODTIP" )
	ElseIF ( IsMemVar( "RDB_CODTIP" ) )
		cCodTip := GetMemVar( "RDB_CODTIP" )
	EndIF
//RDC
ElseIF ( "RDC_CODNET" $ cReadVar ) 
	IF ( IsInGetDados( { "RDC_CODTIP" } ) )
		cCodTip	:= GdFieldGet( "RDC_CODTIP" )
	ElseIF ( IsMemVar( "RDC_CODTIP" ) )
		cCodTip := GetMemVar( "RDC_CODTIP" )
	EndIF
//RDD
ElseIF ( "RDD_CODNET" $ cReadVar ) 
	IF ( IsInGetDados( { "RDD_CODTIP" } ) )
		cCodTip	:= GdFieldGet( "RDD_CODTIP" )
	ElseIF ( IsMemVar( "RDD_CODTIP" ) )
		cCodTip := GetMemVar( "RDD_CODTIP" )
	EndIF
//RDF
ElseIF ( "RDF_CODNET" $ cReadVar ) 
	IF ( IsInGetDados( { "RDF_CODTIP" } ) )
		cCodTip	:= GdFieldGet( "RDF_CODTIP" )
	ElseIF ( IsMemVar( "RDF_CODTIP" ) )
		cCodTip := GetMemVar( "RDF_CODTIP" )
	EndIF
EndIF

IF !Empty( cCodTip )
	cRet := "@#RDH->RDH_CODTIP=='"+cCodTip+"'@#"
EndIF

Return( cRet )

/*/
Ŀ
Funo    SQOSxbFiltAutorMarinaldo de Jesus         Data10/08/2004
Ĵ
Descrio Filtro de Consulta Padrao									
Ĵ
Sintaxe   <Vide Parametros Formais>									
Ĵ
Parametros<Vide Parametros Formais>									
Ĵ
Uso       Consulta Padrao (SXB)				                  	   	
/*/
Function SQOSxbFilt()
         
Local cReadVar	:= Upper( AllTrim( ReadVar() ) )
Local cRet		:= "@#.T.@#"

Local cExpFiltro
Local cFieldFiltro

//RD8 = Questoes do Modelo de Avaliacao
IF ( "RD8_CODQUE" $ cReadVar ) 
	cFieldFiltro := "SQO->QO_TIPO"
	cExpFiltro	 := "APD"
//RDB = Itens de Respostas da Avaliao
ElseIF ( "RDB_CODQUE" $ cReadVar ) 
	cFieldFiltro := "SQO->QO_TIPO"
	cExpFiltro	 := "APD"
EndIF

IF !Empty( cExpFiltro ) .and. !Empty( cFieldFiltro )
	cRet := "@#"+cFieldFiltro+"=='"+cExpFiltro+"'@#"
EndIF

Return( cRet )

/*/
Ŀ
Funo    SqpSxbFilterAutorMarinaldo de Jesus    	Data27/05/2004
Ĵ
Descrio Filtro de Consulta Padrao									
Ĵ
Sintaxe   <Vide Parametros Formais>									
Ĵ
Parametros<Vide Parametros Formais>									
Ĵ
Uso       Consulta Padrao (SXB)				                  	   	
/*/
Function SqpSxbFilter()
         
Local cReadVar	:= Upper( AllTrim( ReadVar() ) )
Local cCodQue	:= ""
Local cRet		:= "@#.T.@#"

//RDB - Itens de Respostas da Avaliacao
IF ( "RDB_CODALT" $ cReadVar ) 
	IF ( IsInGetDados( { "RDB_CODQUE" } ) )
		cCodQue	:= GdFieldGet( "RDB_CODQUE" )
	ElseIF ( IsMemVar( "RDB_CODQUE" ) )
		cCodQue := GetMemVar( "RDB_CODQUE" )
	EndIF
EndIF

IF !Empty( cCodQue )
	cRet := "@#SQP->QP_QUESTAO=='"+cCodQue+"'@#"
EndIF

Return( cRet )

/*/
Ŀ
Funo    SxbFiltRH   AutorMauricio MR			   	Data23/02/2005
Ĵ
Descrio Filtro de Consulta Padrao									
Ĵ
Sintaxe   <Vide Parametros Formais>									
Ĵ
Parametros<Vide Parametros Formais>									
Ĵ
Uso       Consulta Padrao (SXB)				                  	   	
/*/
Function SxbFiltRH(cAlias,cRetorno)                     
Local bFilter
Local uRet				:= .T. 

Static aFiltro			:= {}

DEFAULT cAlias			:=	Alias()
DEFAULT cRetorno		:=	"2"  //Assume filtro de Relatorio

If ((nPos:= Ascan(aFiltro,{|aRot| aRot[1] == cAlias .AND. aRot[2] == cRetorno })) == 0 ) 
    bFilter	:= &("{ || " + ChkRH(' ',cAlias,cRetorno) + " }" )
    AADD(aFiltro,{cAlias, cRetorno, bFilter })
Else
    bFilter:= aFiltro[nPos,3]
Endif

uRet	:=Eval(bFilter)
	
Return(uRet)

/*


Ŀ
Programa  ConsBlqTarAutor  Jonatas A. T. Alves  Data   15/03/11   
Ĵ
Desc.      Funcao para Consulta Padrao da tabela de motivos de 		  
           bloqueio de tarefas.                                       
ĳ
 Uso		  Generico 												  
ٱ
*/
Function ConsBlqTar()
Local cCons 	:= ""
Local cCpoRet	:= "CODIGO"
Local cConteud 	:= ""
Local nCPn 		:= 0 //VARIAVEL UTILIZADA QUANDO E ABERTA MAIS DE UMA GETDADOS AO MESMO TEMPO
Local xRet
Local aArea		:= GetArea()

// n - VARIAVEL DE POSICIONAMENTO DO OBJETO GETDADOS
// O TRECHO ABAIXO CONTROLA PARA QUE NO HAJA CONFLITO ENTRE 2 GETDADOS, CASO SEJA 
// DISPARADA UMA CONSULTA F3 ENTRE 2 TABELAS. EX.: S008 FAZ CONSULTA EM S016
If Type( 'n' ) == "N"
	nCpn := n
EndIf

xRet := Gp310SXB( "S026", cCpoRet )

If ValType( xRet ) <> "L" .Or. ( ValType( xRet ) == "L" .And. !xRet )
	VAR_IXB := &__READVAR
Endif

If nCpn > 0
	n := nCpn
EndIf

RestArea( aArea )
Return( .T. )    

/*


ͻ
Programa  GpFilRCJ Autor  Leandro Drumond      Data   27/08/14   
͹
Desc.     Funcao para filtro dos processos na consulta padrao.        
͹
Uso        Generico                                                  
ͼ


*/
Function GpFilRCJ()
Local cFiltro 	:= ""
Local cFilAux	:= ""
Local aProcAux	:= {}
Local aArea		:= GetArea()
Local nX		:= 0

If Empty(xFilial("RCJ"))
	cFiltro := ".T."
Else
	DbSelectArea("RCJ")
	DbSetOrder(1)
	DbGoTop()
	While RCJ->(!Eof())
		If ( RCJ->( aScan( aProcAux , { |x| ( X[2] == RCJ_CODIGO ) } ) )  == 0 )
			aAdd(aProcAux,{RCJ->RCJ_FILIAL,RCJ->RCJ_CODIGO})
		EndIf
		RCJ->(DbSkip())		
	EndDo
	For nX := 1 to Len(aProcAux)
		If cFilAux <> aProcAux[nX,1]
			cFilAux := aProcAux[nX,1]
			If Empty(cFiltro)
				cFiltro += "(RCJ->RCJ_FILIAL == '" + aProcAux[nX,1] + "' .AND. RCJ->RCJ_CODIGO $ ('"
			Else
				cFiltro += "')) .OR. (RCJ->RCJ_FILIAL == '" + aProcAux[nX,1] + "' .AND. RCJ->RCJ_CODIGO $ ('"
			EndIf
		EndIf
		cFiltro += aProcAux[nX,2] + "*"		
	Next nX
	If !Empty(cFiltro)
		cFiltro += "'))
	EndIf	 
	RestArea(aArea)
EndIf
	
cFiltro := "@#" + cFiltro + "@#"

Return cFiltro


/*


ͻ
Programa  gpSRYRelFil Autor  Leandro Drumond      Data   27/08/14   
͹
Desc.     Funcao para filtro dos roteiros na consulta padrao.        
͹
Uso        Generico                                                  
ͼ


*/
Function gpSRYRelFil()

Local aArea		:= GetArea()
Local cFiltro	:= ""
Local cFilRot	:= ""
Local cAliasQry	:= ""
Local lGPEA090	:= IsInCallStack("GPEA090")
Local lGPEA580	:= IsInCallStack("GPEA580")
Local lOpeSoc	:= SuperGetMv("MV_OPESOC", NIL, .F.)   

If Type("cProcesso") == "U"
	cProcesso := ""
EndIf

If lGPEA090 .Or. lGPEA580
	If lGPEA090
		cFiltro += "!(SRY->RY_TIPO $ '3*4*8*C*D*E*I*G*J')"
	Else
		cFiltro += "!(SRY->RY_TIPO $ '8*C*D*E*G*I*J')" //VTR, PLA, VRF, VAL, INC, BEN, MUV
	EndIf
	If cPaisLoc == "BRA" .And. !lOpeSoc
		cFiltro += " .AND. SRY->RY_TIPO != 'O'"
	EndIf
Else
	If !Empty(cProcesso)
		cAliasQry	:= GetNextAlias()
	
		BeginSql alias cAliasQry
			SELECT DISTINCT RCH_ROTEIR
			FROM %table:RCH% RCH
			WHERE RCH_PROCES = %exp:cProcesso% AND  
		    RCH.%notDel%   
		EndSql
	
		While (cAliasQry)->(!Eof())
			cFilRot += (cAliasQry)->RCH_ROTEIR + "*"
			(cAliasQry)->(DbSkip())
		EndDo
		 
		(cAliasQry)->(DbCloseArea())
	EndIf	
	
	cFiltro += "(SRY->RY_CALCULO $ '" + cFilRot + "')"
	cFiltro += ".AND. !(SRY->RY_TIPO $ 'G*J')"
	If cPaisLoc == "BRA" .And. !lOpeSoc
		cFiltro += " .AND. SRY->RY_TIPO != 'O'"
	EndIf
EndIf

cFiltro := "@#" + cFiltro + "@#"

RestArea(aArea)

Return cFiltro

/*/
Ŀ
Funo    fTopFunSRY  AutorLeandro Drumond          Data27/08/2014
Ĵ
Descrio Filtra filiais da consulta padrao							
Ĵ
Sintaxe   <Vide Parametros Formais>									
Ĵ
Uso       Generico							                  	   	
/*/
Function fTopFunSRY(nTipo)
 
Local cTopFun := ""

If nTipo == 1
	If Type("cFilDe") <> "U" .and. !Empty(cFilDe) .and. Len(AllTrim(cFilDe)) == Len(AllTrim(xFilial("SRY")))
		cTopFun := SubStr(cFilDe + Space(FWGETTAMFILIAL),FWGETTAMFILIAL)
	Else
		cTopFun := Space(FWGETTAMFILIAL)
	EndIf
Else
	If Type("cFilAte") <> "U" .and. !Empty(cFilAte) .and. Len(AllTrim(cFilAte)) == Len(AllTrim(xFilial("SRY")))
		cTopFun := SubStr(cFilAte + Replicate("Z",FWGETTAMFILIAL),FWGETTAMFILIAL)
	Else
		cTopFun := Replicate("Z",FWGETTAMFILIAL)
	EndIf
EndIf

cFilRCJ := ""

Return cTopFun

/*


ͻ
FunctionFilRCHSXB      Autor  Leandro Drumond   Data  28/01/2014  
͹
Desc.   Filtro para consulta padro RCHGEN.                           
                                                                      
͹
Uso     Consulta Padro RCHGEN		                                  
ͼ

*/
Function FilRCHSXB()
Local cFiltro := ""

cRotSXB 	:= If(Type("cRotSXB") == "U", "" , cRotSXB)
cPerSelSXB  := If(Type("cPerSelSXB") == "U", "",cPerSelSXB)
cDtFechSXB	:= If(Type("cDtFechSXB") == "U", "", cDtFechSXB) //"1" - Periodo aberto, "2" - Periodo Fechado

If !Empty(cRotSXB)
	cFiltro := "( RCH->RCH_ROTEIR $ '" + cRotSXB + "' )"
EndIf

If !Empty(cPerSelSXB)
	cFiltro += If(Empty(cFiltro),""," .AND. ")
	cFiltro += "( RCH_PERSEL == '" + cPerSelSXB + "' )"
EndIf

If !Empty(cDtFechSXB)
	cFiltro += If(Empty(cFiltro),""," .AND. ")
	If cDtFechSXB == "1"
		cFiltro += "( Empty(RCH_DTFECH) )"
	Else
		cFiltro += "( !Empty(RCH_DTFECH) )"
	EndIf
EndIf

cFiltro := "@#" + cFiltro + "@#"

Return cFiltro

/*/
Ŀ
Funo    SXBFiltFil      AutorLeandro Drumond      Data 23/02/2016
Ĵ
Descrio Funo para filtrar a filial no SXB de acordo com usurio	 
Ĵ
Uso       Filtro de filial para utilizao na SXB                      
/*/
Function SXBFiltFil(cCampo)

Local cFiltro		:= " "
Local cAliasc		:= Posicione("SX3", 2, cCampo, "X3_ARQUIVO")

aSM0 := FWLoadSM0()

If !Empty(&cCampo)
	If Versenha(114) .And. Versenha(115) .And. len(aSM0) < 200
		cFiltro	:= cCampo + " $ '" + fValidFil(cAliasc) + "' "
	Else
		cFiltro	:= cCampo + " $ '" + cFilAnt + "' "
	EndIf
EndIf

cFiltro := "@#" + cFiltro + "@#"

Return(cFiltro)


/*/{Protheus.doc} GPEFilQry
Funo para retornar uma string com as filiais que o usurio possui acesso, preparada para ser usada em Query
@type function
@author Victor Andrade
@since 28/04/2016
@version 1.0
@param cCampo, character, Campo Filial a ser pesquisado 
@return $cFiltro, $String com as filial que usurio possui acesso.
@example
(examples)
@see (links_or_references)/*/
Function GPEFilQry(cCampo)

Local cFiltro		:= " "
Local cAliasc		:= Posicione("SX3", 2, cCampo, "X3_ARQUIVO")
Local aFils  		:= {}
Local cFils	    	:= "" 
Local cFilQry		:= ""
Local cTable		:= ""
Local i				:= 0
aSM0 := FWLoadSM0()

If !Empty(cCampo)
	If Versenha(114) .And. Versenha(115) .And. len(aSM0) < 200
		cFils := fValidFil(cAliasc)
		aFils := Separa(cFils, "/")
			
			For i:= 1 To Len( aFils )
			
				If i <> Len( aFils )
					cFilQry += "'" + aFils[i] + "', "
				Else
					cFilQry += "'" + aFils[i] + "' "
				EndIf
			Next i

		cFiltro	:= " AND " + cCampo + " IN (" + cFilQry + ") "
	Else
		cTable := FWTabPref(cCampo)
		cFiltro	:= " AND " + cCampo + " = '" + xFilial(cTable,cFilAnt) + "' "
	EndIf
EndIf

Return(cFiltro)


/*/
Ŀ
Funo    SxbFiltRH   AutorMauricio MR			   	Data23/02/2005
Ĵ
Descrio Filtro de Consulta Padrao									
Ĵ
Sintaxe   <Vide Parametros Formais>									
Ĵ
Parametros<Vide Parametros Formais>									
Ĵ
Uso       Consulta Padrao (SXB)				                  	   	
/*/
Function SxbM0()                     
Local cRet	:= SM0->M0_CODFIL

If IsInCallStack("APTR010") .Or. IsInCallStack("APTR020") .Or. IsInCallStack("APTR030") .Or. IsInCallStack("APTR040")
	cRet := xFilial("RE0",SM0->M0_CODFIL)
EndIf
Return(cRet)

/*/
Ŀ
Funo    GPLOGSXB   AutorFlavio Correa 		   	Data03/03/2017
Ĵ
Descrio  Consulta Padrao Processo para o LOG						
Ĵ
Sintaxe   <Vide Parametros Formais>									
Ĵ
Parametros<Vide Parametros Formais>									
Ĵ
Uso       Consulta Padrao (SXB)				                  	   	
/*/
Function GPLOGSXB()
Local lRet 			:= .T.
Local cTitulo  		:= ""
Local MvPar    		:= &(ReadVar())
Local MvParDef 		:= "" 
Local MvRet			:= ""
Local lRet     		:= .T. 
Local l1Elem   		:= .F.
Local nGrupo			  
Local aArea			:= GetArea()
	
Private aResul	 := {}
	
aadd(aResul,"FOL")
aadd(aResul,"ADI")
aadd(aResul,"FER")
aadd(aResul,"RES")
aadd(aResul,"131")
aadd(aResul,"132")
aadd(aResul,"VTR")
aadd(aResul,"AUT")
aadd(aResul,"PLA")
aadd(aResul,"VRF")
aadd(aResul,"VAL")
aadd(aResul,"PLR")
aadd(aResul,"INC")
aadd(aResul,"BEN")
aadd(aResul,"MUV")
aadd(aResul,"VEX")
aadd(aResul,"Fechamento")
aadd(aResul,"Contabilizao") 
aadd(aResul,"Proviso Frias")
aadd(aResul,"Proviso 13 ")
aadd(aResul,"Mdias")	
	
VAR_IXB 	:= MvPar
MvParDef	:=	"12345689CDEFGIJKWXYZM"

f_Opcoes(@MvPar,cTitulo,aResul,MvParDef,12,49,l1Elem)		// Chama funcao f_Opcoes
For nGrupo := 1 To Len(MvPar)
	If (SubStr(MvPar, nGrupo, 1) # "*")
		MvRet += SubStr(mvpar, nGrupo, 1)
	Else
		MvRet += Space(1)
	Endif
Next nGrupo

VAR_IXB := AllTrim(MvRet)
RestArea(aArea)

Return lRet

/*/{Protheus.doc} GpFilRCH
Filtro para a consulta padro RCHSF
@author cicero.pereira
@since 03/07/2017
@version 12.1.07
@return cFiltro, expresso de filtro que ser executada
/*/
Function GpFilRCH()

	Local cFiltro 	:= ".T."
	Local cFilAux	:= ""
	Local aProcPer	:= {}
	Local cAliasQry	:= ""
	Local cFltPer	:= ""
	Local cProcQry	:= "% '' = '' %"
	Local aArea		:= GetArea()
	Local nX		:= 0

	If Type( "cProcesso" ) == "U"
		cProcesso := ""
	EndIf
	If Type( "cRot" ) == "U"
		cRot := ""
	EndIf

	If !Empty(cProcesso)	
		cFiltro += " .AND. (RCH_PROCES == '" + cProcesso + "')"
	EndIf
	
	If !Empty(cRot)
		cFiltro += " .AND. (RCH_ROTEIR == '" + cRot + "')"
	EndIf

	cFiltro := "@#" + cFiltro + "@#"

Return cFiltro

