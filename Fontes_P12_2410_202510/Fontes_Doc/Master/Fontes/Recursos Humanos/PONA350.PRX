#INCLUDE "PONA350.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "PONCALEN.CH"
#INCLUDE "TBICONN.CH"

Static lPEaCols	:=	ExistBlock("PN350ACOLS")
Static lPEGrv 	:=	ExistBlock("PN350GRV")

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддддбдддддддбддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    Ё PONA350       Ё Autor Ё  Mauricio MR     Ё Data Ё12/04/2004Ё╠╠
╠╠цддддддддддедддддддддддддддадддддддаддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Manutencao de Pre-Abonos                                   Ё╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Pona350                                                    Ё╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё            ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.          Ё╠╠
╠╠цддддддддддбдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgram.  Ё Data   Ё BOPS Ё  Motivo da Alteracao                       Ё╠╠
╠╠цддддддддддеддддддддеддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁCecilia C.Ё20/05/14ЁTPQAN3ЁIncluido o fonte da 11 para a 12 e efetuada Ё╠╠
╠╠Ё          Ё        Ё      Ёa limpeza.                                  Ё╠╠
╠╠ЁArtuso    Ё11/11/14Ё039565ЁAjuste na validacao de inclusao. Ao incluir Ё╠╠
╠╠Ё          Ё        Ё  2014Ёpre-abono com arquivo vazio, era exibida menЁ╠╠
╠╠Ё          Ё        Ё      Ёsagem indicando a existencia de pre-abono.  Ё╠╠
╠╠Ё          Ё        Ё      ЁEfetuado ajuste para corrigir errorlog na   Ё╠╠
╠╠Ё          Ё        Ё      Ёconsistencia das datas: De/Ate. Caso a data Ё╠╠
╠╠Ё          Ё        Ё      Ёinicial fosse superior a data final, ao exi-Ё╠╠
╠╠Ё          Ё        Ё      Ёbir a mensagem de inconsistencia, era geradoЁ╠╠
╠╠Ё          Ё        Ё      Ёerrorlog.                                   Ё╠╠
╠╠ЁArtuso    Ё26/10/15Ё032947ЁCriacao do P.E. PN350ACOLS.				  Ё╠╠
╠╠Ё          Ё        Ё  2015Ё                                            Ё╠╠
╠╠ЁRenan B.  Ё13/05/16ЁTVEQZXЁCriacao do P.E. PN350GRV para alterar dados Ё╠╠
╠╠Ё          Ё        Ё      Ёno aCols antes da gravaГЦo e da TudoOk.     Ё╠╠
╠╠юддддддддддаддддддддаддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function PONA350(xAutoCab, xAutoItens, nOpcAuto, cMsgErro, lPn350Api)
	
	Local bBlock
	Local aArea			:= GetArea()
	Local aAreaRF0		:= {}
	Local aIndex		 := {}
	Local cFiltra		:= ""
	Local dPerIniPar	:= Ctod("//")
	Local dPerFimPar	:= Ctod("//")
	Local nPos			:= 0
	Local nPosFil		:= 0
	Local nPosMat		:= 0
	Local nX			:= 0
	Local nRegSRA		:= 0
	Local lContinua 	:= .T.
	
	Private	aAutoCab	:= xAutoCab
	Private	aAutoItens	:= xAutoItens
	Private l350Auto	:= (xAutoCab <> Nil)
	
	Private bFiltraBrw	:= { || NIL }
	
	Private dPerIni     := Ctod("//")
	Private dPerFim     := Ctod("//")

	Default cMsgErro	:= ""
	Default lPn350Api	:= .F.

	// SС executa se o Modo de Acesso do SPB e SRA foram iguais e se este Зltimo nao estiver vazio e Se o Periodo Estiver em Aberto
	If ValidArqPon() .and. ChkVazio("SRA")
		
		// Reinicializa VariАveis Staticas
		PonDestroyStatic()
		
		aAreaRF0 := RF0->( GetArea() )
		
		// Declara VariАveis que SerЦo Utilizadas
		Private cCadastro   := OemToAnsi( STR0001 ) // "ManutenГЦo de PrИ-Abonos"
		
		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define Array contendo as Rotinas a executar do programa      Ё
		Ё ----------- Elementos contidos por dimensao ------------     Ё
		Ё 1. Nome a aparecer no cabecalho                              Ё
		Ё 2. Nome da Rotina associada                                  Ё
		Ё 3. Usado pela rotina                                         Ё
		Ё 4. Tipo de Transa┤└o a ser efetuada                          Ё
		Ё    1 - Pesquisa e Posiciona em um Banco de Dados             Ё
		Ё    2 - Simplesmente Mostra os Campos                         Ё
		Ё    3 - Inclui registros no Bancos de Dados                   Ё
		Ё    4 - Altera o registro corrente                            Ё
		Ё    5 - Remove o registro corrente do Banco de Dados          Ё
		Ё    6 - Legenda                                               Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		Private aRotina := MenuDef() // ajuste para versao 9.12 - chamada da funcao MenuDef() que contem aRotina
		
		// Inicializa o filtro utilizando a funГЦo FilBrowse
		cFiltra		:= ChkRh( FunName(), "SRA" , "1" )
		bFiltraBrw 	:= { || FilBrowse( "SRA" , @aIndex , @cFiltra ) }
		Eval( bFiltraBrw )
		
		// EndereГa a funГЦo de BROWSE
		If !( l350Auto )
			
			If nOpcAuto<>Nil
				nPos := Ascan(aRotina, {|x| x[4]== nOpcAuto})
				If ( nPos # 0 )
					bBlock := &( "{ |x,y,z,k| " + aRotina[ nPos,2 ] + "(x,y,z,k) }" )
					Eval( bBlock, Alias(), SRA->(Recno()), nPos)
				EndIf
			Else
				// Chamada da mBrowse
				mBrowse( 6, 1, 22, 75, "SRA",,,,,, fCriaCor())
			EndIf
		Else
			nPos := Ascan(aRotina, {|x| x[4] == nOpcAuto})
			If ( nPos # 0 )
				
				// Posiciona no funcinoАrio
				nPosFil := Ascan(aAutoCab, {|x| Alltrim(x[1]) $ "RA_FILIAL|RF0_FILIAL" })
				nPosMat := Ascan(aAutoCab, {|x| Alltrim(x[1]) $ "RA_MAT|RF0_MAT"})
				
				If nPosFil > 0 .And. nPosMat > 0 .And. SRA->(dbSeek(aAutoCab[nPosFil][2] + aAutoCab[nPosMat][2]))
					bBlock := &( "{ |x, y, z, k| " + aRotina[ nPos, 2 ] + "(x, y, z, k) }" )
					Eval( bBlock, Alias(), SRA->(Recno()), nPos)
				Else
					// FuncionАrio nЦo encontrado
					Help(,, "Help",, STR0144, 1, 0,,,,,.T., {STR0145})  // "Revise as informaГУes de filial e matrМcula informadas"
				EndIf
				
			EndIf
		Endif
		
		// Deleta o filtro utilizando a funГЦo FilBrowse
		EndFilBrw( "SRA" , aIndex )
		
		RestArea( aAreaRF0 )
		
	EndIf
	
	RestArea( aArea )
	
Return( NIL )

/*
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPn350Atu  Ё Autor ЁMauricio MR            Ё Data Ё12/04/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁPn350Atu(ExpC1,ExpN1,ExpN2)                                 Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё
Ё          Ё ExpN1 = Numero do registro                                 Ё
Ё          Ё ExpN2 = Numero da opcao selecionada                        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      Ё Pona350                                                    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pn350Atu(cAlias,nReg,nOpcX)

	Local aQueryCond		:= {}
	Local aAreaSRA			:= SRA->( GetArea() )
	Local aRecno			:= {}
	Local aNotFields		:= {"RF0_FILIAL", "RF0_MAT"}
	Local aNotAltera		:= {"RF0_ABONA", "RF0_NATEST", "RF0_USUAR", "RF0_FLAG"}
	Local aAltera			:= {}
	Local aAdvSize			:= {}
	Local aInfoAdvSize		:= {}
	Local aObjSize			:= {}
	Local aObjCoords		:= {}
	Local aGdCoords			:= {}
	Local aGpGdCoords		:= {}
	Local aKeys				:= GetKeys()
	Local aGrava			:= {}
	Local aFldRot 			:= {'RA_NOME', 'RA_ADMISSA'}
	Local aOfusca			:= If(FindFunction('ChkOfusca'), ChkOfusca(), {.T. , .F.}) //[1] Acesso; [2]Ofusca
	Local aFldOfusca 		:= {}

	Local bSet15			:= { ||aCols	:= aClone(oGet:aCols) , nopcA:=1, ;
												IF( (nOpcx == 5) , ;
													( ;
													If( fPon350AllDel()	,;
														(oDlg:End(),.T.),;
														(nOpcA:=0, .F.)  ;
														);
													),;
													If( IIF (lPEaCols , fVldPe() , .T.) .And. IIF (lPEGrv, fVldPEGrv() , .T.) .AND. oGet:TudoOk() ,;
														oDlg:End()    		,;
														(nOpcA:=0, .F.) 	;
														);
												);
								}
	Local bSet24			:= { ||oDlg:End() }
	Local bSvSet15			:= { || NIL }
	Local bSvSet24			:= { || NIL }
	Local bChange			:= { || NIL }
	Local bNIL				:= { || NIL }
	Local cFil				:= SRA->RA_FILIAL
	Local cMat				:= SRA->RA_MAT
	Local cNome				:= SRA->RA_NOME
	Local cPerg				:= "PNA350"
	Local cPrefix			:= ( PrefixoCpo( "RF0" )  + "_" )
	Local dPerIni			:= Ctod('')
	Local dPerFim			:= Ctod('')
	Local nField			:= 0.00
	Local nX				:= 0.00
	Local nLenX				:= 0.00
	Local nCnt		  		:= 0.00
	Local nOpcA				:= 0.00
	Local nPosFlag			:= 0.00
	Local lOfuscaNom		:= .F.
	Local lOfuscaAdm		:= .F.
	Local oFont
	Local oLinhas
	Local oGroup

	Private oDlg
	Private oGet

	Private aArrayClone		:= {}
	Private aElem			:= {}
	Private aVirtual		:= {}
	Private aVisual			:= {}
	Private aHeader			:= Array( 0 )
	Private aCols			:= {}
	Private cFiliAnt		:= ""
	Private Continua		:= .F.
	Private nUsado			:= 0.00
	Private nSvnAt			:= 1.00
	Private nRec350			:= 0

	Begin Sequence


		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Monta o cabecalho e Detalhe                                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea( "RF0" )
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Seta o indice (FILIAL + MATRICULA)                           Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		RF0->( dbSetOrder( 1 ))
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Posiciona no Funcionario                                     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		RF0->( dbSeek( cFil + cMat , .F. ) )
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Monta Query para a Selecao das Informacoes                   Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If IsInCallStack("MDTA685") .Or. ;
			IsInCallStack("MDTA110") .Or. ;
			IsInCallStack("MDTA410") //Se forem chamadas pelo MDT
			aQueryCond := Array( 07 )
		Else
			aQueryCond := Array( 05 )
		EndIf
		aQueryCond[01] := cPrefix + "FILIAL='"+cFil+"'"
		aQueryCond[02] := " AND "
		aQueryCond[03] := cPrefix + "MAT='"+cMat+"'"
		aQueryCond[04] := " AND "
		aQueryCond[05] := "D_E_L_E_T_=' ' "
		If IsInCallStack("MDTA685") .Or. ;
			IsInCallStack("MDTA110") .Or. ;
			IsInCallStack("MDTA410") //Se forem chamadas pelo MDT
			aQueryCond[06] := " AND "
			aQueryCond[07] := cPrefix + "NATEST ='"+TNY->TNY_NATEST+"'"
		EndIf

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Monta o cabecalho e Detalhe                                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aCols		:= GdMontaCols(@aHeader,@nUsado,@aVirtual,@aVisual,'RF0',aNotFields,@aRecno,'RF0',(cFil+cMat),NIL,NIL,NIL,NIL,NIL,NIL,NIL,NIL,aQueryCond,.F.,.F.,.T.)
		nRec350		:= Len( aRecno )
		
		// Verifica se existe algum dado no arquivo
		If !l350Auto
			If ( ( nCnt := nRec350 ) > 0 ) .and. nOpcx = 3  .And. ;//--Quando Inclusao e existir Registro
				!IsInCallStack("MDTA685") .And. !IsInCallStack("MDTA110") .And. ;
				!IsInCallStack("MDTA410")//NЦo for chamado pelo MDT
				Help(" ",1,"Pn230Exist")
				Break
			Elseif nCnt = 0 .And. nOpcx # 3  //--Quando Nao for Inclusao e nao existir Registro
				Help(" ",1,"Pn230NoLan")
				Break
			EndIF
		EndIf
		
		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Carrega os Campos Editaveis conforme aHeader				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		For nField := 1	To nUsado
			IF (;
					( aScan( aVirtual 		, aHeader[ nField , 02 ] ) == 0 ) .and. ;
					( aScan( aVisual  		, aHeader[ nField , 02 ] ) == 0 ) .and. ;
					( aScan( aNotFields		, aHeader[ nField , 02 ] ) == 0 ) .and. ;
					( aScan( aNotAltera		, aHeader[ nField , 02 ] ) == 0 )       ;
				)
				aAdd( aAltera , aHeader[ nField , 02 ] )
			EndIF
		Next nField

		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Inicializa nOpcA											  Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		nOpcA := 0.00

		If !( l350Auto )
			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Monta as Dimensoes dos Objetos         					   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			aAdvSize		:= MsAdvSize()
			aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }
			aAdd( aObjCoords , { 015 , 020 , .T. , .F. } )
			aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
			aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )
			aGpGdCoords		:= { aObjSize[2,1],aObjSize[2,2],aObjSize[2,3],aObjSize[2,4]}
			aGdCoords		:= { aGpGdCoords[1]+20,aGpGdCoords[2],aGpGdCoords[3]-20,aGpGdCoords[4]}

			//Tratamento dados sensМveis
			If aOfusca[2]
				aFldOfusca 	:= FwProtectedDataUtil():UsrNoAccessFieldsInList( aFldRot) // CAMPOS SEM ACESSO
				IF aScan( aFldOfusca , { |x| x:CFIELD == "RA_NOME" } ) > 0 
					lOfuscaNom	:= FwProtectedDataUtil():IsFieldInList( "RA_NOME" )
				ENDIF
				IF aScan( aFldOfusca , { |x| x:CFIELD == "RA_ADMISSA" } ) > 0 
					lOfuscaAdm := FwProtectedDataUtil():IsFieldInList( "RA_ADMISSA" )
				ENDIF
			EndIf

			/*
			зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Mostra Tela 										          Ё
			юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			SetaPilha()
				DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
				DEFINE MSDIALOG oDlg TITLE cCadastro From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL

				@ aObjSize[1,1]+15 , aObjSize[1,2] GROUP oGroup TO ( aObjSize[1,3] + 10 ),( ( aObjSize[1,4]/100*10 - 2 ) ) LABEL OemToAnsi(STR0010) OF oDlg PIXEL				// "Matricula:"
				oGroup:oFont:= oFont
				@ aObjSize[1,1]+15 , ( ( aObjSize[1,4]/100*10 ) ) GROUP oGroup TO ( aObjSize[1,3] + 10 ),( aObjSize[1,4]/100*80 - 2 ) LABEL OemToAnsi(STR0011) OF oDlg PIXEL	// "Nome:"
				oGroup:oFont:= oFont
				@ aObjSize[1,1]+15 , ( aObjSize[1,4]/100*80 ) GROUP oGroup TO ( aObjSize[1,3] +10 ),aObjSize[1,4]  LABEL OemToAnsi(STR0012) OF oDlg PIXEL						// "Admiss└o:"
				oGroup:oFont:= oFont

				@ aObjSize[1,1]+20, ( aObjSize[1,2] + 5 )				SAY StrZero(Val(SRA->RA_MAT),Len(SRA->RA_MAT))	SIZE 050,10 OF oDlg PIXEL FONT oFont
				@ aObjSize[1,1]+20, ( ( aObjSize[1,4]/100*10 ) + 5 )	SAY If(lOfuscaNom,Replicate('*',15),OemToAnsi(SRA->RA_NOME))	SIZE 146,10 OF oDlg PIXEL FONT oFont
				@ aObjSize[1,1]+20, ( ( aObjSize[1,4]/100*80 ) + 5 )	SAY If(lOfuscaAdm,Replicate('*',10),Dtoc(SRA->RA_ADMISSA))		SIZE 050,10 OF oDlg PIXEL FONT oFont

				@ aGpGdCoords[1]+13,aGpGdCoords[2]		SAY OemToAnsi(STR0013)	SIZE 120,10 OF oDlg PIXEL FONT oFont															// "Periodos de Pre-Abonos"

				@ aGpGdCoords[1]+13,(aGpGdCoords[4]-25.5) SAY oLinhas VAR StrZero(IF(Type("n")!="U",n,0),4)+"/"+StrZero(Len(aCols),4) SIZE 146,10 OF oDlg PIXEL FONT oFont


				aArrayClone := aClone(aCols)

				If '6.09'$(cVersao)
					oGet := MSGetDados():New(aGdCoords[1],aGdCoords[2],aGdCoords[3],aGdCoords[4],nOpcx														 ,"Pn350LinOK","Pn350TudOK","",If(nOpcx=2.Or.nOpcx=5,Nil,.T.),If(!Empty(aAltera),aAltera,Nil), , NIL, NIL, NIL		  , NIL			, NIL,	"fPon350Del()")
					oGet:oBrowse:bChange := { || oLinhas:Refresh() }
				Else
					oGet := MsNewGetDados():New(aGdCoords[1],aGdCoords[2],aGdCoords[3],aGdCoords[4],IF(nOpcX==2.Or.nOpcX==5,0,GD_INSERT+GD_UPDATE+GD_DELETE	),"Pn350LinOK","Pn350TudOK","",If(!Empty(aAltera),aAltera,Nil) ,0 , 99999, NIL, NIL, "fPon350Del()"	, oDlg			, aClone(aHeader)	,aClone(aCols))
				Endif

				ACTIVATE MSDIALOG oDlg ON INIT (EnchoiceBar( oDlg , bSet15 , bSet24 , Nil , Nil ), oDlg:Refresh()) CENTERED
				SetaPilha()
		Else
			nOpca := 1
			
			If !MsGetDAuto(aAutoItens,"Pn350LinOK",{|| Pn350TudOK()}, aAutoCab, nOpcx)
				nOpcA := 0
			EndIf
		Endif
		
		/*
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁSe nao For Exclusao                                           Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		IF nOpcX != 5
			IF nOpcA == 1 .and. ( nOpcX == 3 .or. nOpcX == 4 )

				Begin Transaction

					/*
					здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					ЁGrava as Marcacoes                                            Ё
					юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
					If ( lPEaCols )
						aGrava	:= aClone(aCols)
					Else
						If ( !l350Auto )
							aGrava	:= aClone(oGet:aCols)
						Else
							aGrava	:= aClone(aCols)
						EndIf
					EndIf
					Pn350Grava( aGrava,aRecno,aHeader,nOpcX,{"DTPREI","DTPREF","CODABO"})
					/*
					здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					ЁProcessa Gatilhos                                             Ё
					юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
					EvalTrigger()
				End Transaction
			EndIF
			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁSe for Exclusao                                               Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		ElseIF nOpcA == 1 .and. nOpcX == 5
			Pn350Dele(aRecno)
			fMrhDelRh3(aRecno)
		EndIF

		MBrChgLoop(.F.)

	End Sequence

	/*
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Restaura o Estado das Teclas de Atalho conforme Entrada 	  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	RestKeys( aKeys )

	/*
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Restaura os Dados de Entrada								  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	RestArea( aAreaSRA )

	If nOpcA == 1 .and. nOpcX == 3
		MbrChgLoop( .F. )
	EndIf

Return( NIL )


/*
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPn350LinOk	Ё Autor ЁMauricioMR            Ё Data Ё12/04/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁValida a Linha Digitada na GetDados                          Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁPn350LinOk                                     			 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁoGet-> Objeto da Getdados									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlRet                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA350                                                      Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pn350LinOk(oGet)

Local cHelp			:= ""
Local cPrefix		:= ( PrefixoCpo( "RF0"  )  + "_" )
Local dDtPreI		:= Ctod('')
Local dDtPreF		:= Ctod('')
Local dPerIni		:= Ctod('')
Local dPerFim		:= Ctod('')
Local lRet			:= .T.
Local nPosDtPreI	:= GdFieldPos(cPrefix+"DTPREI")
Local nPosDtPreF	:= GdFieldPos(cPrefix+"DTPREF")
Local nPosHorIni	:= GdFieldPos(cPrefix+"HORINI")
Local nPosHorFim	:= GdFieldPos(cPrefix+"HORFIM")
Local nPosHorTab	:= GdFieldPos(cPrefix+"HORTAB")
Local nPosFlag		:= GdFieldPos(cPrefix+"FLAG")
Local nPosAbona		:= GdFieldPos(cPrefix+"ABONA")
Local nPosDif		:= 0
Local nHorIni		:= 0.00
Local nHorFim		:= 0.00
Local nLenaCols		:= 0

//-- Obtem o Periodo de Apontamento em Aberto
GetPonMesDat( @dPerIni , @dPerFim )

nLenaCols:=Len(aCols)

IF !GdDeleted() .and. aCols[ n , nPosAbona ] <> "S" //Se abono jА foi utilizado nЦo precisa validar

	Begin Sequence
		/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁConsiste Data+Horarios 										   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			dDtPreI	:= aCols[ n , nPosDtPreI ]
			nHorIni	:= aCols[ n , nPosHorIni ]
			dDtPreF	:= aCols[ n , nPosDtPreF ]
			nHorFim	:= aCols[ n , nPosHorFim ]
	        cConfTab:= aCols[ n , nPosHorTab ]

			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁNao sao Permitidas Horas Em Branco Se Nao Serao Herdadas da Tabela		   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			IF 	(!Empty(nHorIni) .OR. !Empty(nHorFim)) .And. ( cConfTab == 'S')
			    cHelp := STR0058	// "Nao Informe Horas se o Horario sera Conforme Tabela de Horario Padrao."
			  	Help( ' ' , 1 , 'PN230INCON' , , OemToAnsi( cHelp ) , 5 , 0 )
				lRet:=.F.
				Break
			Endif

			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁNao sao Permitidas Datas Em Branco							   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			IF 	Empty(dDtPreI) .OR. Empty(dDtPreF)
			    cHelp := STR0057	// "Nao sao Permitidas Datas Em Branco"
			  	Help( ' ' , 1 , 'PN230INCON' , , OemToAnsi( cHelp ) , 5 , 0 )
				lRet:=.F.
 				Break
			Endif

         	/*
			зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁNao Permite que novos Periodos sejam Anteriores ao Periodo em Aberto  	      Ё
			юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
            If  (dDtPreI < dPerIni .And. n > nRec350) .Or. (Type("lPn350Api") == "L" .And. lPn350Api .And. dDtPreI < dPerIni)
             	cHelp := STR0054 + DTOC(dPerIni)	// "Nao sao Permitidos Periodos Anteriores ao Dia : "
			  	Help( ' ' , 1 , 'PN230INCON' , , OemToAnsi( cHelp ) , 5 , 0 )
				lRet:=.F.
				Break
            Endif

			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁVerifica a Existencia de Horas Zeradas sem Assumir H.Padrao   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			If  (nHorIni  == nHorFim) .And. Empty(nHorIni) .And. ( cConfTab <> 'S')

			    If MsgYesNo(OemToAnsi(STR0059),OemToAnsi(STR0040)) // "As Horas Nao Foram Informadas. Assumir Horario Conforme Tabela de Horario Padrao?" ## "Atencao"
			       aCols[ n , nPosHorTab ] := 'S'
			    Else
			    	lRet:=.F.
					Break
			    Endif
			Endif

			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁVerifica a Existencia de Horas Identicas <> 0				   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			If  (nHorIni  == nHorFim) .And. (dDtPreI ==	dDtPreF) .And. !Empty(nHorIni)

			    cHelp := STR0051	// "Horas em Duplicidade para a mesma Data"
			  	Help( ' ' , 1 , 'PN230INCON' , , OemToAnsi( cHelp ) , 5 , 0 )
				lRet:=.F.
				Break
			Endif

			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁVerifica se a hora fim eh menor que a hora inicio quando a    Ё
			Ёdata inicial e final do pre abono for igual.				   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			If  (dDtPreI ==	dDtPreF) .And. (nHorFim < nHorIni)
			    cHelp := STR0060	// "Hora Final Menor que a Hora Inicial para o Periodo."
			  	Help( ' ' , 1 , 'PN230INCON' , , OemToAnsi( cHelp ) , 5 , 0 )
				lRet:=.F.
				Break
			Endif

			/*
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁVerifica se Codigo do Abono eh Valido						   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
			If !Pn350VCod(n)
				lRet:=.F.
				Break
			Endif

			If !lRet
			   Break
			Endif

	End Sequence

EndIF

If (!Empty(cHelp) .And. Type("cMsgErro") == "C", cMsgErro := cHelp, Nil)

/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSe Lancamento Consistente, Verifica se Houve Manutencao em    Ё
	ЁLancamentos Anteriores										   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
If lRet
   If n <= Len( aArrayClone )
   	  If !fCompArray( aCols[ n ] , aArrayClone[ n ], @nPosDif ) .AND. nPosDif <> nPosFlag
   	     aCols[n, nPosFlag] := 'M'
   	  Else
         aCols[n, nPosFlag] := aArrayClone[n, nPosFlag]
   	  Endif
   Endif
Endif

Return( lRet )

/*
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPn350TudOk	Ё Autor ЁMauricioMR            Ё Data Ё12/04/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁValida Todos os Itens da GetDados                            Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁPn350TudOk()                                   			 	 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁVOID                                                	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlRet                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA350                                                      Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function Pn350TudOk(oGet)

Local cHelp			:= ""
Local cHorIni		:= ''
Local cHorFim		:= ''
Local cLimIni		:= ""
Local cLimFim		:= ""
Local cPrefix		:= ( PrefixoCpo( "RF0"  )  + "_" )
Local lRet			:= .T.
Local nPosDtPreI	:= GdFieldPos(cPrefix+"DTPREI")
Local nPosDtPreF	:= GdFieldPos(cPrefix+"DTPREF")
Local nPosHorIni	:= GdFieldPos(cPrefix+"HORINI")
Local nPosHorFim	:= GdFieldPos(cPrefix+"HORFIM")
Local nPosHorTab	:= GdFieldPos(cPrefix+"HORTAB")
Local nX			:= 0
Local nY			:= 0
Local nLenaCols		:= Len(aCols)
Local lTemMsgErr	:= Type("cMsgErro") == "C"

//-- Corre Cada um dos Horarios do Dia
For nX:=1 to nLenaCols
	//-- Se estiver deletado despreza-o
	If GdDeleted(nX)
		If l350Auto
			If !fPon350Del(nX)
				lRet := .F. 
				EXIT
			EndIf
		Else
			Loop
		EndIf
	Endif
	
	//-- Converte Data Hora  para Criar Limite
	cLimIni := DataHora2Str( aCols[ nX , nPosDtPreI ] , aCols[ nX , nPosHorIni] )

	/*
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSe for Assumido o Horario Padrao, Forca a Hora Final como 23.59 para testar  Ё
	Ёsobreposicao de horarios.													  Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	nHorFim := If( ( aCols[ nX , nPosHorTab ]== 'S' )  , 23.59, aCols[ nX , nPosHorFim ] )

	cLimFim := DataHora2Str( aCols[ nX , nPosDtPreF ] , nHorFim )
	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁVerifica a Consistencia entre horarios de um Mesmo Periodo				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF 	(cLimFim < cLimIni)
   	 	cHelp := STR0060	// "Hora Final Menor que a Hora Inicial para o Periodo."
  		Help( ' ' , 1 , 'PN230INCON' , , OemToAnsi( cHelp ) , 5 , 0 )
		lRet:=.F.

		If (!Empty(cHelp) .And. lTemMsgErr , cMsgErro := cHelp, Nil)

		Return lRet
	Endif
	
    //-- Corre os Demais Horarios do Dia
	For nY:=1 to nLenaCols
        //-- Se for o mesmo horario que deu origem ao limite ou se estiver deletado despreza-o
		If nX==nY .or. GdDeleted(nY)
			Loop
		Endif
        //-- Converte a Data+Hora do Horario Lido
		cHorIni := DataHora2Str( aCols[ nY , nPosDtPreI ] , aCols[ nY , nPosHorIni] )
		
		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁSe for Assumido o Horario Padrao, Forca a Hora Final como 23.59 para testar  Ё
		Ёsobreposicao de horarios.													  Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		nHorFim := If( ( aCols[ nY , nPosHorTab ]== 'S' )  , 23.59, aCols[ nY , nPosHorFim ] )
		
		cHorFim := DataHora2Str( aCols[ nY , nPosDtPreF ] , nHorFim )
		
        //-- Verifica se o Horario Lido esta contido Total ou Parcial no Limite criado
		If (cHorIni < cLimFim .AND. cHorIni >= cLimIni) .Or. (cHorFim < cLimFim .AND. cHorFim > cLimIni)
			lRet:=.F.
        	cHelp:= STR0052 +STRZERO(nY,4)  // "Existe Sobreposicao de Horarios para o Periodo"
			Help( ' ' , 1 , 'PN230INCON' , , OemToAnsi( cHelp ) , 5 , 0 )
			Exit
        Endif
	Next nY
	
	If !lRet
		Exit
	Endif
	
Next nX

Return( lRet )

/*
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPn350Grava  Ё Autor ЁMauricio MR           Ё Data Ё12/04/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁGrava Informacoes no RF0                                     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁPn350Grava(aCols,aRecno,aHeader,nOpcX)				   		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁaCols   -> Array com as Informacoes a serem Gravadas 	     Ё
Ё          ЁaRecno  -> Registros Existentes no RF0               		 Ё
Ё          ЁaHeader -> Array com os Campos                      		 Ё
Ё          ЁnOpcX   -> Tipo de Operacao: 3 = Inclusao , 4 = Alteracao	 Ё
Ё          ЁaCampos -> Campos p/ verificacao de existencia de lancamento Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                                                  	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA350                                                      Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function Pn350Grava(aCols,aRecno,aHeader,nOpcX, aCampos)

Local lCpoRF0		:= RF0->(ColumnPos("RF0_SOLRH3")) > 0
Local aArea			:= GetArea()
Local cAlias		:= "RF0"
Local cPrefix		:= ( PrefixoCpo( cAlias ) + "_" )
Local cFil			:= SRA->RA_FILIAL
Local cMat			:= SRA->RA_MAT
Local cChave		:= ""
Local naPosCampo	:= Array(Len(aCampos))
Local nArray		:= 0.00
Local nForArray		:= Len( aCols )
Local nRecno		:= 0.00
Local nHeader		:= 0.00
Local nForX         := 0.00
Local nForHeader	:= Len( aHeader )
Local nPosDtPreI	:= GdFieldPos(cPrefix+"DTPREI")
Local nPosDtPreF	:= GdFieldPos(cPrefix+"DTPREF")
Local nPosHrIni		:= GdFieldPos(cPrefix+"HORINI")
Local nPosHrFim		:= GdFieldPos(cPrefix+"HORFIM")
Local lAppend		:= .F.
Local lGrava		:= .T.

//-- Verifica a Existencia de Lancamento Valido para Gravacao
//-- Pois como sempre teremos um lancamento vazio para cada manutencao, na confirmacao da gravacao
//-- sem manutencao, esses lancamentos deverao ser descartados.
If Len(aCols)==1
    //-- Inicializa todas as posicioes dos campos de consistencia
    aFill(naPosCampo,0.00)
    //-- Define o posicionamento de cada um dos campos de consistencia de integridade
    For nForX:=1 To Len(aCampos)
    	naPosCampo[nForX]	:= GdFieldPos(cPrefix+aCampos[nForX])
    	//-- Se houver inconsistencia de localizacao de campos no aheader nao grava registros
    	If Empty(naPosCampo[nForx] )
    	   lGrava:= .F.
    	   Exit
    	Endif
    Next nForX

    If lGrava
	    //-- Verifica a Integridade de todos os campos
	    //-- Altera flag de gravacao para .F. de modo que iremos detectar a ocorrencia de pelo
	    //-- menos uma hora nao nula. Se isso ocorrer, entao temos um horario valido e alteramos
	    //-- o flag de gravacao para permitir a gravacao do item.
	    lGrava:=.F.
	    For nForX:=1 To Len(aCampos)
		    //-- Se os Campos para integridade do lancamento estiverem vazios, nao grava lancamento
	    	If !Empty( aCols[  1, naPosCampo[nForx] ] )
	    	   lGrava:=.T.
	    	   Exit
	    	Endif
	    Next nForX
    Endif

Endif

If lGrava
	(cAlias)->( dbSetOrder( RetOrdem(cAlias) ) )

	/*
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁGrava as Marcacoes                                            Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	IF nOpcX == 3  // Inclusao
		For nArray := 1 To nForArray
			IF !GdDeleted(nArray,,aCols) //Nao Grava Registros Deletados
			    cChave := cFil + cMat + Dtos(aCols[ nArray , nPosDtPreI ] ) +  Str( aCols[ nArray , nPosHrIni ] , 5 , 2 )
				lAppend := ( (cAlias)->( MsSeek( cChave , .F. ) ) )
				RecLock(cAlias,!lAppend)
				(cAlias)->(&(cPrefix+"FILIAL"))	:= cFil
				(cAlias)->(&(cPrefix+"MAT"))		:= cMat
				For nHeader := 1 To nForHeader
					IF (cAlias)->( FieldPos ( aHeader[ nHeader , 2 ] ) > 0 )
						(cAlias)->( FieldPut( FieldPos ( aHeader[ nHeader , 2 ] ) , aCols[ nArray , nHeader ] ) )
					EndIF
				Next nHeader
				( cAlias )->( MsUnLock() )
			EndIF
		Next nArray
	ElseIF nOpcX == 4 //Alteracao
	    //-- Nao houve inclusao de novos Elementos
		IF nRec350 == nForArray
			//-- Corre todos os Elementos
			For nArray := 1 To nForArray
				//-- Se Existir Registro correspondente ao Elemento (Neste Caso sempre vai existir)
				//-- Posiciona
				IF !( lAppend := Empty( aRecno[ nArray ] ) )
					(cAlias)->( dbGoto( aRecno[ nArray ] ) )
				EndIF
				//-- Para Elementos Nao deletados
				IF !GdDeleted(nArray,,aCols)
				    //-- Cria/Bloqueia Registro
					RecLock(cAlias,lAppend)
					//-- Salva Campos que Nao fizeram parte do aHeader
					(cAlias)->(&(cPrefix+"FILIAL"))	:= cFil
					(cAlias)->(&(cPrefix+"MAT"))		:= cMat
					//-- Corre todos os campos do aHeader
					For nHeader := 1 To nForHeader
					    //-- Se o campo foi encontrado (Nao virtual, por exemplo)
					    //-- Grava seu novo conteudo
						IF (cAlias)->( FieldPos ( aHeader[ nHeader , 2 ] ) > 0 )
							(cAlias)->( FieldPut( FieldPos ( aHeader[ nHeader , 2 ] ) , aCols[ nArray , nHeader ] ) )
						EndIF
					Next nHeader
					//-- Desbloqueia registro
					( cAlias )->( MsUnLock() )
				//-- Se Elemento Deletado e exisitir registro associado, deleta-o
				ElseIF !lAppend
					RecLock(cAlias,lAppend)
					(cAlias)->( dbDelete() )
					( cAlias )->( MsUnLock() )
					If lCpoRF0 .And. !Empty( RF0->RF0_SOLRH3 )
						fDelReqById( RF0->RF0_FILIAL, RF0->RF0_MAT, RF0->RF0_SOLRH3 )
					EndIf
				EndIF
			Next nArray
		//-- Se ocorreu inclusao de novos elementos
		ElseIF nRec350 < nForArray
		    //-- Corre todos os registros pre-existentes
			For nRecno := 1 To nRec350
				//-- Se existir registro associado posiciona
				IF !( lAppend := Empty( aRecno[ nRecno ] ) )
					(cAlias)->( dbGoto( aRecno[ nRecno ] ) )
				EndIF
				//-- Se elememento Nao deletado
				IF !GdDeleted( nRecno,,aCols )
				    //-- Bloqueia/Cria novo registro
					RecLock(cAlias,lAppend)
					//-- Atualiza campos que nao fizeram parte do aHeader
					(cAlias)->(&(cPrefix+"FILIAL"))	:= cFil
					(cAlias)->(&(cPrefix+"MAT"))		:= cMat
					//-- Corre todas as colunas do aHeader
					For nHeader := 1 To nForHeader
	                    //-- Se existir o campo (nao eh virtual, por exemplo)
						IF (cAlias)->( FieldPos ( aHeader[ nHeader , 2 ] ) > 0 )
						    //-- Grava novo conteudo
							(cAlias)->( FieldPut( FieldPos ( aHeader[ nHeader , 2 ] ) , aCols[ nRecno , nHeader ] ) )
						EndIF
					Next nHeader
					( cAlias )->( MsUnLock() )
				//-- Para Elementos Deletados
				ElseIF !lAppend
				    //-- Bloqueia Registro e Deleta-o
					RecLock(cAlias,lAppend)
					(cAlias)->( dbDelete() )
					( cAlias )->( MsUnLock() )
					If lCpoRF0 .And. !Empty( RF0->RF0_SOLRH3 )
						fDelReqById( RF0->RF0_FILIAL, RF0->RF0_MAT, RF0->RF0_SOLRH3 )
					EndIf
				EndIF
			Next nRecno

			//-- Corre os novos Elementos
	        nRecno:=If(nRecno == 0.00,1, nRecno)

	        For nArray := nRecno To nForArray
	            //-- Para Elementos Nao Deletados
				IF !GdDeleted(nArray,,aCols) //Nao Grava Registros Deletados
				 	cChave := cFil + cMat + Dtos(aCols[ nArray , nPosDtPreI ] ) + Str( aCols[ nArray , nPosHrIni ] , 5 , 2 )
					//-- Verifica a Existencia do Elemento
					lAppend := ( (cAlias)->( MsSeek( cChave , .F. ) ) )
					//-- Cria/Bloqueia elementos
					RecLock(cAlias,!lAppend)
					//-- Atualiza campos que nao fizeram parte do aHeader
					(cAlias)->(&(cPrefix+"FILIAL"))	:= cFil
					(cAlias)->(&(cPrefix+"MAT"))		:= cMat
				   	//-- Corre todas as colunas do aHeader
					For nHeader := 1 To nForHeader
					 //-- Se existir o campo (nao eh virtual, por exemplo)
						IF (cAlias)->( FieldPos ( aHeader[ nHeader , 2 ] ) > 0 )
							(cAlias)->( FieldPut( FieldPos ( aHeader[ nHeader , 2 ] ) , aCols[ nArray , nHeader ] ) )
						EndIF
					Next nHeader
					( cAlias )->( MsUnLock() )
				EndIF
			Next nArray
		//-- Se ocorreu Delecao de Elementos
		Else
			//-- Corre todos os registros pre-existentes
			For nRecno := 1 To nRec350
				//-- Se existir registro associado posiciona
				IF !( lAppend := Empty( aRecno[ nRecno] ) )
					(cAlias)->( dbGoto( aRecno[ nRecno] ) )
				EndIF
				//-- Utiliza Area Lida Antes da Manutencao para Gravar os Lancamentos Atuais
				If nRecno <= nForArray
					//-- Se elememento Nao deletado
					IF !GdDeleted(nRecno,,aCols)
					    //-- Bloqueia/Cria novo registro
						RecLock(cAlias,lAppend)
						//-- Atualiza campos que nao fizeram parte do aHeader
						(cAlias)->(&(cPrefix+"FILIAL"))	:= cFil
						(cAlias)->(&(cPrefix+"MAT"))		:= cMat
						//-- Corre todas as colunas do aHeader
						For nHeader := 1 To nForHeader
		                    //-- Se existir o campo (nao eh virtual, por exemplo)
							IF (cAlias)->( FieldPos ( aHeader[ nHeader , 2 ] ) > 0 )
							    //-- Grava novo conteudo
								(cAlias)->( FieldPut( FieldPos ( aHeader[ nHeader , 2 ] ) , aCols[ nRecno , nHeader ] ) )
							EndIF
						Next nHeader
					//-- Para Elementos Deletados
					ElseIF !lAppend
					    //-- Bloqueia Registro e Deleta-o
						RecLock(cAlias,lAppend)
						(cAlias)->( dbDelete() )
					EndIF
					( cAlias )->( MsUnLock() )
				Else
				    //-- Deleta Elementos anteriores a Manutencao
				    RecLock(cAlias,lAppend)
					(cAlias)->( dbDelete() )
					(cAlias )->( MsUnLock() )
				Endif
			Next nRecno
		EndIF
	EndIF
Endif

/*
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura os Dados de Entrada                                  Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
RestArea( aArea )

Return( NIL )

/*
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPn350Dele   Ё Autor ЁMauricio MR           Ё Data Ё12/04/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁDeleta as Marcacoes do RFO                                   Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁPn350Dele( aRecno )                                   		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁaRecno -> Array com os Registros a Serem Deletados		     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                                                  	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA350                                                      Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function Pn350Dele( aRecno )

Local cAlias 	:= "RF0"
Local lGoto		:= .F.
Local nX	 	:= 0.00

Begin Transaction
	For nX := 1 To nRec350
		IF ( lGoto := !Empty(   aRecno[ nX ] ) )
			(cAlias)->( dbGoto( aRecno[ nX ] ) )
			RecLock(cAlias,.F.,.T.)
			(cAlias)->( dbDelete() )
			( cAlias )->( MsUnLock() )
		EndIF
	Next nX
End Transaction

Return( NIL )

/*/{Protheus.doc} fMrhDelRH3
Realiza a exclusЦo de uma requisicao e das tabelas relacionadas (RH3, RH4, RKG)
@type static function
@version 1.0
@author Henrique Ferreira
@since 20/09/2023
@return Nil
/*/

Static Function fMrhDelRH3( aRecno )

Local lCpoRF0	:= RF0->(ColumnPos("RF0_SOLRH3")) > 0
Local cAlias 	:= "RF0"
Local lGoto		:= .F.
Local nX	 	:= 0.00

If lCpoRF0
	For nX := 1 To nRec350
		IF ( lGoto := !Empty(   aRecno[ nX ] ) )
			(cAlias)->( dbGoto( aRecno[ nX ] ) )
			If !Empty( RF0->RF0_SOLRH3 )
				fDelReqById(RF0->RF0_FILIAL, RF0->RF0_MAT,RF0->RF0_SOLRH3, "")
			EndIf
		EndIF
	Next nX
EndIf

Return( NIL )


/*
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁPn350ChoiBarЁ Autor ЁMauricioMR            Ё Data Ё12/04/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁMonta Button Bar                                             Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁPn350ChoiBar												 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                   		     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁNIL                                                          Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA300                                                      Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Static Function Pn350ChoiBar( oDlg , bOk , bCancel , oGet , nOpcX , aNotFields , cProg , lSetKey )

Local aKeys			:= GetKeys()
Local bSvSet15		:= { || NIL }
Local bSvSet24		:= { || NIL }


Local bConfirma		:= { || NIL }
Local bCancela		:= { || NIL }
Local lOk			:= .F.
Local lEnd			:= .F.

Local oBar
Local oBtOk
Local oBtCan
Local oBtRec
Local oBtCop
Local oBtCol
Local oBtCal
Local oBtAge
Local oBtSpo
Local oBtHlp

DEFAULT lSetKey := .F.

DEFINE BUTTONBAR oBar SIZE 25,25 3D TOP OF oDlg


bConfirma	:= { || ( lOk := Eval(bOk),ButtonOff(bSvSet15,bSvSet24,lOk,aKeys,lSetKey) ) }
bCancela	:= { || ( lEnd := Eval(bCancel),ButtonOff(bSvSet15,bSvSet24,lEnd,aKeys,lSetKey) ) }

DEFINE BUTTON oBtRec RESOURCE "S4WB005N"  OF oBar GROUP ACTION NaoDisp()	TOOLTIP OemtoAnsi( STR0031 )
oBtRec:cTitle:= OemToAnsi(STR0031)	// "Recortar"
DEFINE BUTTON oBtCop RESOURCE "S4WB006N"  OF oBar GROUP ACTION NaoDisp()	TOOLTIP OemtoAnsi( STR0032 )
oBtCop:cTitle:= OemToAnsi(STR0032)	// "Copiar"
DEFINE BUTTON oBtCol RESOURCE "S4WB007N"  OF oBar GROUP ACTION NaoDisp()	TOOLTIP OemtoAnsi( STR0033 )
oBtCol:cTitle:= OemToAnsi(STR0033)	// "Colar"
DEFINE BUTTON oBtCal RESOURCE "S4WB008N"  OF oBar GROUP ACTION Calculadora() 	TOOLTIP OemtoAnsi( STR0034 )
oBtCal:cTitle:= OemToAnsi(STR0134)	// "Calc"
DEFINE BUTTON oBtAge RESOURCE "S4WB009N"  OF oBar GROUP ACTION Agenda()    	TOOLTIP OemtoAnsi( STR0035 )
oBtAge:cTitle:= OemToAnsi(STR0135)	// "Agenda"
DEFINE BUTTON oBtSpo RESOURCE "S4WB010N"  OF oBar GROUP ACTION OurSpool()  	TOOLTIP OemtoAnsi( STR0036 )
oBtSpo:cTitle:= OemToAnsi(STR0136)	// "Gerencia"
DEFINE BUTTON oBtHlp RESOURCE "S4WB016N"  OF oBar GROUP ACTION HelProg()   	TOOLTIP OemtoAnsi( STR0037 )
oBtHlp:cTitle:= OemToAnsi(STR0137)	// "Help"
DEFINE BUTTON oBtOk  RESOURCE "OK" 		  OF oBar GROUP ACTION Eval( bConfirma )	TOOLTIP OemtoAnsi( STR0038 )
oBtOk:cTitle:= OemToAnsi(STR0138)	// "Ok"
bSvSet15 := SetKey(15,oBtOk:bAction)
DEFINE BUTTON oBtCan RESOURCE "CANCEL"	  OF oBar GROUP	ACTION Eval( bCancela )		TOOLTIP OemtoAnsi( STR0039 )
oBtCan:cTitle	:= OemToAnsi(STR0139)	// "Cancelar"
bSvSet24 		:= SetKey(24,oBtCan:bAction)

oDlg:bSet15 	:= oBtOk:bAction
oDlg:bSet24 	:= oBtCan:bAction
oBar:bRClicked 	:= {|| AllwaysTrue() }

oBtRec:Disable()
oBtCop:Disable()
obtCol:Disable()

IF ( ( nOpcX == 2 ) .or. ( nOpcX == 5 ) )
	oBtCal:Disable()
	oBtAge:Disable()
	oBtSpo:Disable()
	oBtHlp:Disable()

EndIF

Return( NIL )

/*
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁButtonOff	Ё Autor ЁMauricio MR           Ё Data Ё12/04/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁHabilita/Desabilita Botoes Confirma e Sair da Rotina         Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁButtonOff(bSet15,bSet24,lOk,aKeys,lSetKey)					 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁbSet15 - Bloco de confirmacao da rotina                	     Ё
Ё          ЁbSet24 - Bloco de abandono da  rotina              		     Ё
Ё          ЁlOK    - Habilita Bloco de codigo correspondente   		     Ё
Ё          ЁaKeys  - Array de teclas ativas antes da  entrada da rotina  Ё
Ё          ЁlSetkey- Restaura as teclas originalmente programadas  	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   Ё.T.                                                  	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA350                                                      Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Static Function ButtonOff(bSet15,bSet24,lOk,aKeys,lSetKey)

DEFAULT bSet15	:= { || NIL }
DEFAULT bSet24	:= { || NIL }
DEFAULT lOk		:= .T.
DEFAULT lSetKey	:= .F.

IF ( ValType( lOk ) != "L" )
	lOk := .T.
EndIF

IF lOk
	IF ( ValType( bSet15 ) == "B" )
		SetKey( 15 , bSet15 )
	EndIF
	IF ( ValType( bSet24 ) == "B" )
		SetKey( 24 , bSet24 )
	EndIF
	RestKeys( aKeys , lSetKey )
EndIF

Return( .T. )





/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁPn350VHor     Ё Autor Ё Mauricio MR           Ё Data Ё 12/04/04 Ё╠╠
╠╠цддддддддддеддддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Valida o "Hora Inicial" e "Hora Final"                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё PonA350                                                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function Pn350VHor()

Local lRet  	:= .T.
Local cVar  	:= ''
Local cPrefix	:= ( PrefixoCpo( "RF0"  ) + "_" )
Local nPosHrIn	:=GdFieldPos(cPrefix+"HORINI")
Local nPosHrFm	:=GdFieldPos(cPrefix+"HORFIM")

//Atualiza Campo Hora Inicial
If (cVar := ReadVar()) == 'M->RF0_HORINI'
    aCols[n, nPosHrIn]:= M->RF0_HORINI
Endif

//Atualiza Campo Hora Final
If (cVar := ReadVar()) == 'M->RF0_HORFIM'
    aCols[n, nPosHrFm]:= M->RF0_HORFIM
Endif

If aCols[n, nPosHrIn] > 24 .Or. aCols[n, nPosHrFm] > 24
	//-- Consiste horas > 24
	Help('',1,'PN210VHMIN')
	lRet := .F.
ElseIf aCols[n, nPosHrIn] - Int(aCols[n, nPosHrIn]) > 0.59 .Or. aCols[n, nPosHrFm] - Int(aCols[n, nPosHrFm]) > 0.59
	//-- Consiste minutos maior que .59
	Help('',1,'PN210VHMIN')
	lRet := .F.
Endif

Return lRet



/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁPn350VDtI Ё Autor Ё Mauricio MR           Ё Data Ё 12/04/04 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁCritica a data da linha digitada                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function Pn350VDtI()
Local lRet 		:= .T.

Local cPrefix	:= ( PrefixoCpo( "RF0"  ) + "_" )
Local cVar		:= ''
Local dData		:= Ctod('')
Local nPosData	:= GdFieldPos(cPrefix+"DTPREI")

//-- Verifica qual a variavel ativa
cVar:=ReadVar()

dData	:= aCols[ n , nPosData   ]


If EMPTY(dData)
   lRet:= .F.
Endif

Return lRet

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁPn350VCOD Ё Autor Ё Mauricio MR           Ё Data Ё 20/04/04 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁCritica codigo de Pre-abono                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function Pn350VCod(nElem)
Local lRet 		:= .T.
Local cHelp		:= ""
Local cPrefix	:= ( PrefixoCpo( "RF0"  ) + "_" )

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verfica se Abono e Valido                                    Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
cCodAbo := If (nElem <> Nil, aCols[nElem, GdFieldPos(cPrefix+"CODABO") ], M->(&(cPrefix+"CODABO")) )

If !Empty(cCodAbo)
	If !DescAbono(cCodAbo,"L",cPrefix+"DESCABO") .OR. (SP6->P6_PREABO <> 'S')
	   lRet:= .F.
	Endif
Else
	cHelp := STR0056 	// "Codigo do Abono Informado Invalido."
  	Help( ' ' , 1 , 'PN230INCON' , , OemToAnsi( cHelp ) , 5 , 0 )
    lRet:= .F.
Endif

Return lRet

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁPn350VDtF Ё Autor Ё Mauricio MR           Ё Data Ё 12/04/04 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁCritica a data da linha digitada                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function Pn350VDtF()
Local lRet 		:= .T.

Local cPrefix	:= ( PrefixoCpo( "RF0"  ) + "_" )
Local cVar		:= ''
Local dData		:= Ctod('')
Local nPosData	:= GdFieldPos(cPrefix+"DTPREF")

//-- Verifica qual a variavel ativa
cVar:=ReadVar()

If cVar ==  "M->RF0_DTREF"
	dData	:= M->RF0_DTREF
Else
	dData	:= aCols[ n , nPosData   ]
Endif

If EMPTY(dData)
  	lRet:= .F.
Endif

Return lRet



/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁPn350RDtI Ё Autor Ё Mauricio MR           Ё Data Ё 12/04/04 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁInicializador da Data                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function Pn350RDtI()
Return(dDataBase)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁPn350RDtF Ё Autor Ё Mauricio MR           Ё Data Ё 12/04/04 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁInicializador da Data                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function Pn350RDtF()
Return(dDataBase)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁPn350RUserЁ Autor Ё Mauricio MR           Ё Data Ё 20/04/04 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁInicializador do Usuario                                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function Pn350RUser()
Return(UsrRetName(RetCodUsr()))


/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁPn350When     Ё Autor Ё Mauricio MR           Ё Data Ё 22/04/04 Ё╠╠
╠╠цддддддддддеддддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁHabilita ou Nao Entrada de Dados		                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                       Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function Pn350When()
Local lRet			:= .T.
Local nPosAbona		:= GdFieldPos("RF0_ABONA")

/*
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁNao Aconselha que Periodos ja Abonados sejam modificados/eliminados	      Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
If  aCols[ n , nPosAbona ]	== 'S'
	MsgAlert(OemToAnsi(STR0055)) // "Periodos Abonados Nao Devem ser Modificados ou Eliminados."
	lRet:= .F.
EndIf

Return lRet

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддддддбддддддбдддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁfPon350Del()    ЁAutor ЁMauricio MR        Ё Data Ё22/04/02  Ё╠╠
╠╠цддддддддддеддддддддддддддддаддддддадддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁLiberacao de Delecao de Lancamentos                          Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   ЁfPon350Del()												   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ										 					   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNIL                                                  	       Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObserva┤└oЁ                                                      	   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      ЁPn350			                                               Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*
*/
Function fPon350Del(nLine)
	
	Local lRet			:= .T.
	Local nPosAbona		:= GdFieldPos("RF0_ABONA")
	Local nPosDtIni		:= GdFieldPos("RF0_DTPREI")
	Local nPosDtFim		:= GdFieldPos("RF0_DTPREF")
	Local nPosAbo		:= GdFieldPos("RF0_CODABO")
	Local dPerIni		:= cToD("")
	Local dPerFim		:= cToD("")
	Local lTemMsgErr	:= Type("cMsgErro") == "C"
	Default nLine := 0
	
	nLine := If( nLine > 0, nLine, n)
	
	// NЦo Aconselha que Periodos ja Abonados sejam modificados/eliminados
	If aCols[ nLine, nPosAbona ] == 'S'
		
		//-- Obtem o Periodo de Apontamento em Aberto
		GetPonMesDat( @dPerIni, @dPerFim )
		If aCols[ nLine, nPosDtFim ] < dPerIni // PerМodos anteriores
			Help( ,, "Help",, STR0141, 1, 0,,,,,, {STR0055}) // "Abono jА foi utilizado" - "Periodos Abonados Nao Devem ser Modificados ou Eliminados."
			lRet:= .F.
			If (lTemMsgErr, cMsgErro := STR0141 + " - " + STR0055, Nil)
		ElseIf aCols[ nLine, nPosDtIni ] < dPerIni .And. aCols[ nLine, nPosDtFim ] >= dPerIni
			DbSelectArea("SPK")
			DbSetOrder(1)
			SPK->(dbSeek(SRA->RA_FILIAL + SRA->RA_MAT + DToS(dPerIni), .T.))
			
			While SPK->(!EoF() .And. PK_FILIAL + PK_MAT == SRA->RA_FILIAL + SRA->RA_MAT) .And. SPK->PK_DATA <= aCols[ nLine, nPosDtFim ]
				If SPK->(PK_FILIAL + PK_MAT + PK_CODABO) == SRA->RA_FILIAL + SRA->RA_MAT + aCols[nLine, nPosAbo] 
					Help( ,, "Help",, STR0141, 1, 0,,,,,, {STR0142}) // "Para excluir este registro И necessАrio excluir os apontamentos do funcionАrio."
					lRet := .F.
					If (lTemMsgErr, cMsgErro := STR0141 + " - " + STR0142, Nil)
					Exit
				EndIf
				SPK->(dbSkip())
			EndDo
			
			// "O prИ-abono jА foi utilizado em prerМodos anteriores, deseja alterar a data final para o Зltimo dia do perМodo anterior?"
			If lRet .And. (l350Auto .Or. MsgYesNo(STR0143, STR0141) )
				oGet:aCols[nLine, nPosDtFim] := dPerIni - 1
				oGet:Refresh()
			EndIf
			
			lRet := .F.
			
		ElseIf aCols[ nLine, nPosDtIni ] >= dPerIni .And. aCols[ nLine, nPosDtIni ] <= dPerFim
			DbSelectArea("SPK")
			DbSetOrder(1)
			SPK->(dbSeek(SRA->RA_FILIAL + SRA->RA_MAT + DToS(aCols[nLine, nPosDtIni]), .T. ))
			While SPK->(!EoF() .And. PK_FILIAL + PK_MAT == SRA->RA_FILIAL + SRA->RA_MAT) .And. SPK->PK_DATA <= aCols[ nLine, nPosDtFim ]
				If SPK->(PK_FILIAL + PK_MAT + PK_CODABO) == SRA->RA_FILIAL + SRA->RA_MAT + aCols[nLine, nPosAbo]
					Help( ,, "Help",, STR0141, 1, 0,,,,,, {STR0142}) // "Para excluir este registro И necessАrio excluir os apontamentos do funcionАrio."
					lRet := .F.
					If (lTemMsgErr, cMsgErro := STR0141 + " - " + STR0142, Nil)
					Exit
				EndIf
				SPK->(dbSkip())
			EndDo
			
		EndIf
	Endif
	
Return lRet


/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддддддбддддддбдддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁfPon350AllDel   ЁAutor ЁMauricio MR        Ё Data Ё22/04/02  Ё╠╠
╠╠цддддддддддеддддддддддддддддаддддддадддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁLiberacao de Delecao de Lancamentos                          Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   ЁfPon350AllDel												   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ										 					   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNIL                                                  	       Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObserva┤└oЁ                                                      	   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      ЁPn350			                                               Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*
*/
Function fPon350AllDel()
Local lRet			:= .T.
Local cMsg			:= ''
Local nPosAbona		:= GdFieldPos("RF0_ABONA")
Local nX	 		:= 0
Local nFornX 		:= Len( aCols )

For nX := 1 To nFornX
	//-- Se Existir um Lancamento Utilizado
	If aCols[ nX , nPosAbona ]	== 'S'
	   lRet:= .F.
	   Exit
	Endif
Next nX

/*
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁNao Aconselha que Periodos ja Abonados sejam Eliminados            	      Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
If  !lRet
    cMsg:= 	OemToAnsi(	STR0061	) 	//"Existem Periodos Abonados que Nao Devem ser Eliminados."
    MsgAlert(cMsg) // cMsg ## "Atencao"
Endif

Return lRet

/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё MenuDef		ЁAutorЁ  Luiz Gustavo     Ё Data Ё30/11/2006Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁIsola opcoes de menu para que as opcoes da rotina possam    Ё
Ё          Ёser lidas pelas bibliotecas Framework da Versao 9.12 .      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA340                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/


Static Function MenuDef()

	Local aRotina :=   {	{ STR0002,"PesqBrw"	 , 0 , 1, ,.F.},; 		//"Pesquisar"
							{ STR0003,"Pn350Atu" , 0 , 2},; 			//"Visualizar"
							{ STR0004,"Pn350Atu" , 0 , 3,,,.T.},;		//"Incluir"
							{ STR0005,"Pn350Atu" , 0 , 4},; 			//"Alterar"
							{ STR0006,"Pn350Atu" , 0 , 5},; 			//"Excluir"
							{ STR0007,"gpLegend" , 0 , 6 , , .F. } ;	//"Legenda"
						}

 Return aRotina


/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё fVldPe		ЁAutorЁ  Luis Artuso	      Ё Data Ё13/10/2015Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁExecucao do P.E. PN350ACOLS                                 Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁPONA350                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
*/


Static Function fVldPe()

	aCols := ExecBlock("PN350ACOLS" , .F. , .F. , {aCols})

Return .T.

/*/{Protheus.doc}
@author: Matheus Bizutti
@since:  26/01/16
@return: lRetPE - Retorno para a gravaГЦo ou nЦo dos dados.
/*/
Static Function fVldPEGrv()

Local lRetPE := .T.

	lRetPE := ExecBlock("PN350GRV" , .F. , .F. , {aCols})

Return ( lRetPE )
