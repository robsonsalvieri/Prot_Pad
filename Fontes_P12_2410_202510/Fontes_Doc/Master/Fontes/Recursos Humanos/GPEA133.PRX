#Include "PROTHEUS.CH"
#Include "GPEA131.CH"
#INCLUDE "FWMVCDEF.CH"

Static cVALBox	    := ""
Static cVRFBox	    := ""
Static cVTRBox	    := ""
Static cFilVTRBox	:= ""
Static cFilVABox	:= ""
Static cFilVRBox	:= ""
Static lTemRecalc
Static lTemRU1
Static lX6Recalc

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Gpea133  ³ Autor ³ Equipe RH Inovacao           ³ Data        ³ 15/01/14  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Cadastramento de Beneficios:                                              ³±±
±±³          ³ -Vale Transporte, Vale Refeicao, Vale Alimentacao                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programador ³   Data   ³ FNC/Chamado    ³  Motivo da Alteracao                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Raquel Hager³17/01/2014³M12RH01  RQ2008 ³ Unificacao Folha de Pagamento.              ³±±
±±³Jaqueline L ³17/01/2018³DRHPAG-7577     ³ Retirada a obrigatoriedade do preenchimento ³±±
±±³Jaqueline L ³17/01/2018³DRHPAG-7577     ³ dos dias da semana quando o parametro       ³±±
±±³Jaqueline L ³17/01/2018³DRHPAG-7577     ³ MV_USACPER estiver como .F.                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Gpea133()

Local oMBrowse
Local cFiltraRh
Local lChKDupRecalc := GetRPORelease() < "12.1.2310" .And. Substr(FWX2Unico("RU1"), 68, 10) == "RU1_RECALC"  

If lChKDupRecalc
	fChkDupRecalc()
EndIf

SetMnemonicos( xFilial("RCA"), Nil, .T., "P_RECALCVT" )

oMBrowse := FWMBrowse():New()
oMBrowse:SetMenuDef("GPEA133")
oMBrowse:SetAlias("SRA")
oMBrowse:SetDescription(OemToAnsi(STR0046)) //"Atualização Vales"

GpLegMVC(@oMBrowse)	// Adicionar a Legenda no Browse

oMBrowse:ForceQuitButton()	//"Incluir botao de sair"

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Inicializa o filtro utilizando a funcao FilBrowse                      ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
cFiltraRh := CHKRH("GPEA133","SRA","1")
oMBrowse:SetFilterDefault( cFiltraRh )
oMBrowse:DisableDetails()
oMBrowse:Activate()

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Menudef     ³ Autor ³ Equipe RH Inovacao ³ Data ³ 10/01/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Isola opcoes de menu para que as opcoes da rotina possam   ³±±
±±³          ³ ser lidas pelas bibliotecas Framework da Versao 9.12.      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEA133                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function MenuDef()
Local aRet

Local aRotina := { 	{ OemToAnsi(STR0001) 	,"PesqBrw"	, 0 , 1, 0 ,.T.},;    			//"Pesquisar"
					{ OemToAnsi(STR0002)	,"VIEWDEF.GPEA133"	, 0 , 2, 0 , NIL},;  	//"Visualizar"
					{ OemToAnsi(STR0028) 	,"VIEWDEF.GPEA133"	, 0 , 4, 0, NIL },;  	//"Manutenção"
					{ OemToAnsi(STR0004) 	,"VIEWDEF.GPEA133"	, 0 , 4, 0, NIL},;  	//"Alterar"
					{ OemToAnsi(STR0006)	,"GpLegend"	, 0 , 5 , 0 ,.F.}}				//"Legenda"


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada para adicionar opcoes de menu.			      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If IsInCallStack("GPEA131")
	If ExistBlock( "GPA131MN")
		IF ( ValType( aRet := ExecBlock( "GPA131MN" ,.F.,.F.,{aClone(aRotina)}, .F. ) ) == "A" )
	   		aRotina := aClone(aRet)
		Endif
	EndIf
	If funname() == "GPEA131"
		SetFunName("GPEA133")
	EndIf
EndIf
Return( aRotina )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ ModelDef 		³Autor³  Flavio Correa	  ³ Data ³13/07/2016³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Regras de Modelagem da gravacao.                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEA003                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³aRotina														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function ModelDef()

    Local aGpa133FSM7   := { "M7_DIASPRO" }
    Local bCommit	    := { | oModel | fCommit(oModel) }
	Local lUsaCPer	    := SuperGetMv("MV_USACPER",, .F.)
    Local oModel
	Local oStructSRA
	Local oRU10
	Local oRU11
	Local oRU12
	Local oSR00
	Local oSR01
	Local oSR02
	Local oSM70
	Local oSM71
	Local oSM72
	Local oSP7

    DEFAULT lTemRU1     := AliasInDic("RU1")

	oModel := MPFormModel():New( "GPEA133", { |oMdl| IsBlind() .Or. fsetOSM7(oMdl) }, Nil, bCommit, Nil )
	oModel:SetDescription( OemToAnsi(STR0029) ) //"Cadastro de beneficios"

	If lUsaCPer
		aadd(aGpa133FSM7,"M7_VALDOM")
		aadd(aGpa133FSM7,"M7_VALSEG")
		aadd(aGpa133FSM7,"M7_VALTER")
		aadd(aGpa133FSM7,"M7_VALQUA")
		aadd(aGpa133FSM7,"M7_VALQUI")
		aadd(aGpa133FSM7,"M7_VALSEX")
		aadd(aGpa133FSM7,"M7_VALSAB")
		aadd(aGpa133FSM7,"M7_VALFIX")
	EndIf

	// Cabecalho de dados - SRA (Funcionario)//
	oStructSRA := FWFormStruct(1, "SRA",  { |cCampo| Gpa133SRAStru(cCampo) })
	oModel:AddFields("GPEA133_MSRA", NIL, oStructSRA )
	oModel:GetModel( "GPEA133_MSRA" ):SetDescription( OemToAnsi(STR0029) )
	oModel:GetModel('GPEA133_MSRA'):SetOnlyQuery(.T.)
	oModel:GetModel('GPEA133_MSRA'):SetOnlyView(.T.)

	//SP7 - Faltas
	oSP7 := FWFormStruct(1, "SP7")
	oModel:AddGrid("GPEA133_MSP7", "GPEA133_MSRA", oSP7)
	oModel:GetModel("GPEA133_MSP7" ):SetDescription( OemToAnsi(STR0001) ) //"Histórico de Vales"
	oModel:GetModel("GPEA133_MSP7" ):SetOptional( .T. )
	oModel:GetModel('GPEA133_MSP7'):SetOnlyView(.T.)
	oModel:GetModel('GPEA133_MSP7'):SetOnlyQuery(.T.)
	oModel:SetRelation( "GPEA133_MSP7", { { "P7_FILIAL", 'SRA->RA_FILIAL' }, { "P7_MAT", 'SRA->RA_MAT' }}, SP7->( IndexKey( 1 ) ) )

	// ************* CADASTRO *****************

	//SM7 - VALE TRANSPORTE
	oSM70 := FWFormStruct(1, "SM7")
	A133Str(@oSM70,"SM7")
	A133Remove(@oSM70,aGpa133FSM7)
	oSM70:SetProperty("M7_TPVALE"	, MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'0'"))
	oModel:AddGrid("GPEA133_MSM70", "GPEA133_MSRA", oSM70,, { |oGrid| SM7LinOk(oGrid)}, /*bPre*/, /*bPost*/, { |oGrid| CargaAux( oGrid ) } )
	oModel:GetModel( "GPEA133_MSM70" ):SetUniqueLine( { 'M7_TPVALE', 'M7_CODIGO'})
	oModel:GetModel( "GPEA133_MSM70" ):SetDescription( OemToAnsi(STR0017) )
	oModel:GetModel( "GPEA133_MSM70" ):SetOptional( .T. )
	oModel:SetRelation( "GPEA133_MSM70", { { "M7_FILIAL", 'SRA->RA_FILIAL' }, { "M7_MAT", 'SRA->RA_MAT' }, { "M7_TPVALE", "'0'" } }, SM7->( IndexKey( 3 ) ) )

	//SM7 - VALE REFEICAO
	oSM71 := FWFormStruct(1, "SM7")
	A133Str(@oSM71,"SM7")
	A133Remove(@oSM71,aGpa133FSM7)
	oSM71:SetProperty("M7_TPVALE"	, MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'1'"))
	oSM71:SetProperty("*"	, MODEL_FIELD_WHEN, {|oMdl| (oMdl:GetValue('M7_TPCALC') <> '2') } )
	oModel:AddGrid("GPEA133_MSM71", "GPEA133_MSRA", oSM71,, { |oGrid| SM7LinOk(oGrid)}, /*bPre*/, /*bPost*/, { |oGrid| CargaAux( oGrid ) } )
	oModel:GetModel( "GPEA133_MSM71" ):SetUniqueLine( {  'M7_TPVALE', 'M7_CODIGO'} )
	oModel:GetModel( "GPEA133_MSM71" ):SetDescription( OemToAnsi(STR0018) )
	oModel:GetModel( "GPEA133_MSM71" ):SetOptional( .T. )
	oModel:SetRelation( "GPEA133_MSM71", { { "M7_FILIAL", 'SRA->RA_FILIAL' }, { "M7_MAT", 'SRA->RA_MAT' }, { "M7_TPVALE", "'1'" } }, SM7->( IndexKey( 3 ) ) )

	//SM7 - VALE ALIMENTACAO
	oSM72 := FWFormStruct(1, "SM7")
	A133Str(@oSM72,"SM7")
	A133Remove(@oSM72,aGpa133FSM7)
	oSM72:SetProperty("M7_TPVALE",	MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'2'"))
	oSM72:SetProperty("*"	, MODEL_FIELD_WHEN, {|oMdl| (oMdl:GetValue('M7_TPCALC') <> '2') } )
	oModel:AddGrid("GPEA133_MSM72", "GPEA133_MSRA", oSM72,, { |oGrid| SM7LinOk(oGrid)}, /*bPre*/, /*bPost*/, { |oGrid| CargaAux( oGrid ) } )
	oModel:GetModel( "GPEA133_MSM72" ):SetUniqueLine( { 'M7_TPVALE', 'M7_CODIGO'} )
	oModel:GetModel( "GPEA133_MSM72" ):SetDescription( OemToAnsi(STR0019) )
	oModel:GetModel( "GPEA133_MSM72" ):SetOptional( .T. )
	oModel:SetRelation( "GPEA133_MSM72", { { "M7_FILIAL", 'SRA->RA_FILIAL' }, { "M7_MAT", 'SRA->RA_MAT' }, { "M7_TPVALE", "'2'" } }, SM7->( IndexKey( 3 ) ) )

	// ************** CALCULO *****************

	//SR0 - VALE TRANSPORTE
	oSR00 := FWFormStruct(1, "SR0")
	A133Str(@oSR00,"SR0")
	oSR00:SetProperty("R0_CODIGO" 	, MODEL_FIELD_VALID,FwBuildFeature(STRUCT_FEATURE_VALID,"NaoVazio()"))
	oSR00:SetProperty("R0_TPVALE"	, MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'0'"))
	oSR00:SetProperty("*"	, MODEL_FIELD_WHEN, {|oMdl| .F. } )
	oSR00:SetProperty("R0_PEDIDO"	, MODEL_FIELD_WHEN, {|oMdl| .T. } )
	oModel:AddGrid("GPEA133_MSR00", "GPEA133_MSRA", oSR00, { |oGrid, nLine, cAction, cField| A133DelOk(oGrid, nLine, cAction, cField) }, { |oGrid| BenLin133Ok(oGrid) }, /*bPre*/, /*bPost*/, { |oGrid| CargaAux( oGrid ) } )
	oModel:GetModel( "GPEA133_MSR00" ):SetUniqueLine( { 'R0_FILIAL', 'R0_MAT', 'R0_TPVALE', 'R0_CODIGO','R0_NROPED'} )
	oModel:GetModel( "GPEA133_MSR00" ):SetDescription( OemToAnsi(STR0029) )
	oModel:GetModel( "GPEA133_MSR00" ):SetNoDeleteLine( .T. )
	oModel:GetModel( "GPEA133_MSR00" ):SetOptional( .T. )
	oModel:SetRelation( "GPEA133_MSR00", { { "R0_FILIAL", 'SRA->RA_FILIAL' }, { "R0_MAT", 'SRA->RA_MAT' }, { "R0_TPVALE", "'0'" } }, SR0->( IndexKey( 3 ) ) )

	//SR0 - VALE REFEICAO
	oSR01 := FWFormStruct(1, "SR0")
	A133Str(@oSR01,"SR0")
	oSR01:SetProperty("R0_CODIGO" 	, MODEL_FIELD_VALID,FwBuildFeature(STRUCT_FEATURE_VALID,"NaoVazio()"))
	oSR01:SetProperty("R0_TPVALE"	, MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'1'"))
	oSR01:SetProperty("*"	, MODEL_FIELD_WHEN, {|oMdl| .F. } )
	oSR01:SetProperty("R0_PEDIDO"	, MODEL_FIELD_WHEN, {|oMdl| .T. } )
	oModel:AddGrid("GPEA133_MSR01", "GPEA133_MSRA", oSR01,{ |oGrid, nLine, cAction, cField| A133DelOk(oGrid, nLine, cAction, cField) }, { |oGrid| BenLin133Ok(oGrid) }, /*bPre*/, /*bPost*/, { |oGrid| CargaAux( oGrid ) } )
	oModel:GetModel( "GPEA133_MSR01" ):SetUniqueLine( { 'R0_FILIAL', 'R0_MAT', 'R0_TPVALE', 'R0_CODIGO','R0_NROPED'} )
	oModel:GetModel( "GPEA133_MSR01" ):SetDescription( OemToAnsi(STR0029) )
	oModel:GetModel( "GPEA133_MSR01" ):SetNoDeleteLine( .T. )
	oModel:GetModel( "GPEA133_MSR01" ):SetOptional( .T. )
	oModel:SetRelation( "GPEA133_MSR01", { { "R0_FILIAL", 'SRA->RA_FILIAL' }, { "R0_MAT", 'SRA->RA_MAT' }, { "R0_TPVALE", "'1'" } }, SR0->( IndexKey( 3 ) ) )

	//SR0 - VALE ALIMENTACAO
	oSR02 := FWFormStruct(1, "SR0")
	A133Str(@oSR02,"SR0")
	oSR02:SetProperty("R0_CODIGO" ,MODEL_FIELD_VALID,FwBuildFeature(STRUCT_FEATURE_VALID,"NaoVazio()"))
	oSR02:SetProperty("R0_TPVALE",	MODEL_FIELD_INIT, FwBuildFeature(STRUCT_FEATURE_INIPAD, "'2'"))
	oSR02:SetProperty("*"	, MODEL_FIELD_WHEN, {|oMdl| .F. } )
	oSR02:SetProperty("R0_PEDIDO"	, MODEL_FIELD_WHEN, {|oMdl| .T. } )
	oModel:AddGrid("GPEA133_MSR02", "GPEA133_MSRA", oSR02,{ |oGrid, nLine, cAction, cField| A133DelOk(oGrid, nLine, cAction, cField) }, { |oGrid| BenLin133Ok(oGrid) }, /*bPre*/, /*bPost*/, { |oGrid| CargaAux( oGrid ) } )
	oModel:GetModel( "GPEA133_MSR02" ):SetUniqueLine( { 'R0_FILIAL', 'R0_MAT', 'R0_TPVALE', 'R0_CODIGO','R0_NROPED'} )
	oModel:GetModel( "GPEA133_MSR02" ):SetDescription( OemToAnsi(STR0029) )
	oModel:GetModel( "GPEA133_MSR02" ):SetNoDeleteLine( .T. )
	oModel:GetModel( "GPEA133_MSR02" ):SetOptional( .T. )
	oModel:SetRelation( "GPEA133_MSR02", { { "R0_FILIAL", 'SRA->RA_FILIAL' }, { "R0_MAT", 'SRA->RA_MAT' }, { "R0_TPVALE", "'2'" } }, SR0->( IndexKey( 3 ) ) )

    If lTemRU1
        //RU1 - VALE TRANSPORTE
        oRU10 := FWFormStruct( 1, "RU1" )
        oModel:AddGrid( "GPEA133_MRU10", "GPEA133_MSRA", oRU10, Nil, Nil, Nil, Nil, { |oGrid| CargaAux( oGrid ) } )
        oModel:GetModel( "GPEA133_MRU10" ):SetDescription( OemToAnsi(STR0029) )
        oModel:GetModel( "GPEA133_MRU10" ):SetNoDeleteLine( .T. )
        oModel:GetModel( "GPEA133_MRU10" ):SetNoInsertLine( .T. )
        oModel:GetModel( "GPEA133_MRU10" ):SetOptional( .T. )
        oModel:SetRelation( "GPEA133_MRU10", { { "RU1_FILIAL", "SRA->RA_FILIAL" }, { "RU1_MAT", "SRA->RA_MAT" }, { "RU1_TPVALE", "'0'" } }, RU1->( IndexKey( 1 ) ) )

        //RU1 - VALE REFEICAO
        oRU11 := FWFormStruct( 1, "RU1" )
        oModel:AddGrid( "GPEA133_MRU11", "GPEA133_MSRA", oRU11, Nil, Nil, Nil, Nil, { |oGrid| CargaAux( oGrid ) } )
        oModel:GetModel( "GPEA133_MRU11" ):SetDescription( OemToAnsi(STR0029) )
        oModel:GetModel( "GPEA133_MRU11" ):SetNoDeleteLine( .T. )
        oModel:GetModel( "GPEA133_MRU11" ):SetNoInsertLine( .T. )
        oModel:GetModel( "GPEA133_MRU11" ):SetOptional( .T. )
        oModel:SetRelation( "GPEA133_MRU11", { { "RU1_FILIAL", "SRA->RA_FILIAL" }, { "RU1_MAT", "SRA->RA_MAT" }, { "RU1_TPVALE", "'1'" } } , RU1->( IndexKey( 1 ) ) )

        //RU1 - VALE ALIMENTACAO
        oRU12 := FWFormStruct( 1, "RU1" )
        oModel:AddGrid( "GPEA133_MRU12", "GPEA133_MSRA", oRU12, Nil, Nil, Nil, Nil, { |oGrid| CargaAux( oGrid ) } )
        oModel:GetModel( "GPEA133_MRU12" ):SetDescription( OemToAnsi(STR0029) )
        oModel:GetModel( "GPEA133_MRU12" ):SetNoDeleteLine( .T. )
        oModel:GetModel( "GPEA133_MRU12" ):SetNoInsertLine( .T. )
        oModel:GetModel( "GPEA133_MRU12" ):SetOptional( .T. )
        oModel:SetRelation( "GPEA133_MRU12", { { "RU1_FILIAL", "SRA->RA_FILIAL" }, { "RU1_MAT", "SRA->RA_MAT" }, { "RU1_TPVALE", "'2'" } } , RU1->( IndexKey( 1 ) ) )
    EndIf

	oModel:SetVldActivate( { |oModel| fMsgCancel(oModel) } )

Return( oModel )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ ViewDef  		³Autor³  Flavio Correa	 ³ Data ³13/07/2016³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Regras de Interface com o Usuario                           ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³< Vide Parametros Formais >									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³GPEA003                                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Retorno  ³aRotina														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³< Vide Parametros Formais >									³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function ViewDef()
Local oView
Local oModel
Local oStructSRA
Local oRU10
Local oRU11
Local oRU12
Local oSR00
Local oSR01
Local oSR02
Local oSM70
Local oSM71
Local oSM72
Local aGpa133FRU1 	:= { "RU1_FILIAL", "RU1_MAT", "RU1_TPVALE", "RU1_TPBEN", "RU1_CC", "RU1_PERIOD", "RU1_NROPGT", "RU1_ROTEIR", "RU1_PEDIDO", "RU1_CODCCT" }
Local aGpa133FSR0 	:= { "R0_FILIAL", "R0_MAT", "R0_MEIO", "R0_DESC","R0_TPVALE" }
Local aGpa133FSM7 	:= { "M7_FILIAL", "M7_MAT", "M7_TPVALE","M7_DIASPRO" }
Local aTmp			:= {}
Local aButtons		:= {{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,STR0060},{.T.,STR0061},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil}}//"Salvar"##"Fechar"
Local lUsaCPer		:= SuperGetMv("MV_USACPER",, .F.)
Local oSP7

DEFAULT lTemRU1	    := AliasInDic("RU1")
DEFAULT lTemRecalc  := If(lTemRU1,RU1->( ColumnPos("RU1_RECALC") ) > 0,.F.)
DEFAULT lX6Recalc   := fIntRHGS() .And. SuperGetMv("MV_BENEXGS", Nil, .F.)
DEFAULT P_RECALCVT  := .F.

If GetMv("MV_GSPUBL", Nil, "1") == "2"
	aGpa133FSR0 := { "R0_FILIAL", "R0_MAT", "R0_CODIGO", "R0_TPVALE", "R0_VLRVALE", "R0_VLRFUNC", "R0_VLREMP", "R0_FALTAS", "R0_FERIAS", "R0_AFAST" }
EndIf

If lUsaCPer
	aadd(aGpa133FSM7,"M7_VALDOM")
	aadd(aGpa133FSM7,"M7_VALSEG")
	aadd(aGpa133FSM7,"M7_VALTER")
	aadd(aGpa133FSM7,"M7_VALQUA")
	aadd(aGpa133FSM7,"M7_VALQUI")
	aadd(aGpa133FSM7,"M7_VALSEX")
	aadd(aGpa133FSM7,"M7_VALSAB")
	aadd(aGpa133FSM7,"M7_VALFIX")

	aadd(aGpa133FSR0,"R0_QVALDOM")
	aadd(aGpa133FSR0,"R0_QVALSEG")
	aadd(aGpa133FSR0,"R0_QVALTER")
	aadd(aGpa133FSR0,"R0_QVALQUA")
	aadd(aGpa133FSR0,"R0_QVALQUI")
	aadd(aGpa133FSR0,"R0_QVALSEX")
	aadd(aGpa133FSR0,"R0_QVALSAB")
	aadd(aGpa133FSR0,"R0_QVALFIX")
EndIf

// Vincular o View ao Model //
oModel := FWLoadModel("GPEA133")

oView := FWFormView():New()
oView:SetModel(oModel)

// Criacao do Cabecalho - SRA (Funcionario)
oStructSRA := FWFormStruct(2, "SRA", { |cCampo| Gpa133SRAStru(cCampo) })
oStructSRA:SetNoFolder()
oView:AddField("GPEA133_VSRA", oStructSRA, "GPEA133_MSRA" )

oSM70 	:= FWFormStruct(2, "SM7")
A133Remove(@oSM70,aGpa133FSM7)
oSM70:SetProperty("M7_CODIGO" , MVC_VIEW_COMBOBOX, CodBenef(0,.T.) )
oView:AddGrid("GPEA133_VSM70", oSM70, "GPEA133_MSM70" )

oSM71 	:= FWFormStruct(2, "SM7")
A133Remove(@oSM71,aGpa133FSM7)
oSM71:SetProperty("M7_CODIGO" , MVC_VIEW_COMBOBOX, CodBenef(1,.T.) )
oView:AddGrid("GPEA133_VSM71", oSM71, "GPEA133_MSM71" )

oSM72 	:= FWFormStruct(2, "SM7")
A133Remove(@oSM72,aGpa133FSM7)
oSM72:SetProperty("M7_CODIGO" , MVC_VIEW_COMBOBOX, CodBenef(2,.T.) )
oView:AddGrid("GPEA133_VSM72", oSM72, "GPEA133_MSM72" )

oSP7 	:= FWFormStruct(2, "SP7")
oView:AddGrid("GPEA133_VSP7", oSP7, "GPEA133_MSP7" )

oSR00 	:= FWFormStruct(2, "SR0")
aTmp := aClone(aGpa133FSR0)
aadd(aTmp,"R0_TPCALC")
A133Remove(@oSR00,aTmp)
oSR00:SetProperty("R0_CODIGO" , MVC_VIEW_COMBOBOX, CodBenef(0,.T.) )
oSR00:SetProperty("R0_NROPED" , MVC_VIEW_ORDEM, "0" )
oSR00:SetProperty("R0_PEDIDO" , MVC_VIEW_ORDEM, "0" )
oView:AddGrid("GPEA133_VSR00", oSR00, "GPEA133_MSR00" )

oSR01 	:= FWFormStruct(2, "SR0")
aTmp := aClone(aGpa133FSR0)
aAdd(aTmp, "RG2_DIADIF")
aAdd(aTmp, "RG2_VALDIF")
aAdd(aTmp, "RG2_CUNIDF")
aAdd(aTmp, "RG2_CFUNDF")
aAdd(aTmp, "RG2_CEMPDF")
aAdd(aTmp, "RG2_VTDUTE")
aAdd(aTmp, "RG2_VTDNUT")
aAdd(aTmp, "RG2_DUTILM")
A133Remove(@oSR01,aTmp)
oSR01:SetProperty("R0_CODIGO" , MVC_VIEW_COMBOBOX, CodBenef(1,.T.) )
oSR01:SetProperty("R0_NROPED" , MVC_VIEW_ORDEM, "0" )
oSR01:SetProperty("R0_PEDIDO" , MVC_VIEW_ORDEM, "0" )
oView:AddGrid("GPEA133_VSR01", oSR01, "GPEA133_MSR01" )

oSR02 	:= FWFormStruct(2, "SR0")
aTmp := aClone(aGpa133FSR0)
aAdd(aTmp, "RG2_DIADIF")
aAdd(aTmp, "RG2_VALDIF")
aAdd(aTmp, "RG2_CUNIDF")
aAdd(aTmp, "RG2_CFUNDF")
aAdd(aTmp, "RG2_CEMPDF")
aAdd(aTmp, "RG2_VTDUTE")
aAdd(aTmp, "RG2_VTDNUT")
aAdd(aTmp, "RG2_DUTILM")
A133Remove(@oSR02,aTmp)
oSR02:SetProperty("R0_CODIGO" , MVC_VIEW_COMBOBOX, CodBenef(2,.T.) )
oSR02:SetProperty("R0_NROPED" , MVC_VIEW_ORDEM, "0" )
oSR02:SetProperty("R0_PEDIDO" , MVC_VIEW_ORDEM, "0" )
oView:AddGrid("GPEA133_VSR02", oSR02, "GPEA133_MSR02" )

If lTemRU1
    oRU10 	:= FWFormStruct( 2, "RU1" )
    A133Remove( @oRU10, aGpa133FRU1 )
    If lTemRecalc .And. !P_RECALCVT
        oRU10:RemoveField("RU1_RECALC")
    EndIf
    oRU10:SetProperty( "RU1_CODIGO", MVC_VIEW_COMBOBOX, CodBenef( 0, .T. ) )
    oRU10:SetProperty( "RU1_NROPED", MVC_VIEW_ORDEM, "0" )
    oView:AddGrid( "GPEA133_VRU10", oRU10, "GPEA133_MRU10" )

    If lTemRecalc .And. !lX6Recalc
        AAdd( aGpa133FRU1, "RU1_RECALC" )
    EndIf

    oRU11 	:= FWFormStruct( 2, "RU1" )
    A133Remove( @oRU11, aGpa133FRU1 )
    oRU11:SetProperty( "RU1_CODIGO", MVC_VIEW_COMBOBOX, CodBenef( 1, .T. ) )
    oRU11:SetProperty( "RU1_NROPED", MVC_VIEW_ORDEM, "0" )
    oView:AddGrid( "GPEA133_VRU11", oRU11, "GPEA133_MRU11" )

    oRU12 	:= FWFormStruct( 2, "RU1" )
    A133Remove( @oRU12, aGpa133FRU1 )
    oRU12:SetProperty( "RU1_CODIGO", MVC_VIEW_COMBOBOX, CodBenef( 2, .T. ) )
    oRU12:SetProperty( "RU1_NROPED", MVC_VIEW_ORDEM, "0" )
    oView:AddGrid( "GPEA133_VRU12", oRU12, "GPEA133_MRU12" )
EndIf

// Desenho da Tela //
oView:CreateHorizontalBox( "SRA_HEAD", 10 )
oView:CreateHorizontalBox( "SM7", IIf( !lTemRU1, 45, 30 ) )
oView:CreateHorizontalBox( "SR0", IIf( !lTemRU1, 45, 30 ) )
If lTemRU1
    oView:CreateHorizontalBox( "RU1", 30 )
EndIf

oView:CreateFolder( 'FOLDER_SM7', 'SM7')
oView:AddSheet( "FOLDER_SM7", "VT1", STR0017, { || A133Folder( oView, "FOLDER_SM7", "FOLDER_SR0", Iif( lTemRU1, "FOLDER_RU1", "" ) ) } )//"Vale Transporte"
oView:CreateHorizontalBox( 'VT1', 100, , , 'FOLDER_SM7', 'VT1')
oView:AddSheet( "FOLDER_SM7", "VR1", STR0018, {|| A133Folder(oView, "FOLDER_SM7", "FOLDER_SR0", Iif( lTemRU1, "FOLDER_RU1", "" ) ) } )//"Vale Refeição"
oView:CreateHorizontalBox( 'VR1', 100, , , 'FOLDER_SM7', 'VR1')
oView:AddSheet( "FOLDER_SM7", "VA1", STR0019, {|| A133Folder(oView, "FOLDER_SM7", "FOLDER_SR0", Iif( lTemRU1, "FOLDER_RU1", "" ) ) } )//"Vale Alimentação"
oView:CreateHorizontalBox( 'VA1', 100, , , 'FOLDER_SM7', 'VA1')
oView:AddSheet('FOLDER_SM7','VSP7',STR0045)//"Faltas
oView:CreateHorizontalBox( 'VSP7', 100, , , 'FOLDER_SM7', 'VSP7')

oView:CreateFolder( 'FOLDER_SR0', 'SR0')
oView:AddSheet( "FOLDER_SR0", "VT", STR0017, { || A133Folder( oView, "FOLDER_SR0", "FOLDER_SM7", Iif( lTemRU1, "FOLDER_RU1", "" ) ) } )//"Vale Transporte"
oView:CreateHorizontalBox( 'VT', 100, , , 'FOLDER_SR0', 'VT')
oView:AddSheet( "FOLDER_SR0", "VR", STR0018, { || A133Folder( oView, "FOLDER_SR0", "FOLDER_SM7", Iif( lTemRU1, "FOLDER_RU1", "" ) ) } )//"Vale Refeição"
oView:CreateHorizontalBox( 'VR', 100, , , 'FOLDER_SR0', 'VR')
oView:AddSheet( "FOLDER_SR0", "VA", STR0019, { || A133Folder( oView, "FOLDER_SR0", "FOLDER_SM7", Iif( lTemRU1, "FOLDER_RU1", "" ) ) } )//"Vale Alimentação"
oView:CreateHorizontalBox( 'VA', 100, , , 'FOLDER_SR0', 'VA')
If lTemRU1
    oView:CreateFolder( "FOLDER_RU1", "RU1" )
    oView:AddSheet( "FOLDER_RU1", "VT2", STR0017, { || A133Folder( oView, "FOLDER_RU1", "FOLDER_SR0" ) } )//"Vale Transporte"
    oView:CreateHorizontalBox( "VT2", 100, Nil, Nil, "FOLDER_RU1", "VT2" )
    oView:AddSheet( "FOLDER_RU1", "VR2", STR0018, { || A133Folder( oView, "FOLDER_RU1", "FOLDER_SR0" ) } )//"Vale Refeição"
    oView:CreateHorizontalBox( "VR2", 100, Nil, Nil, "FOLDER_RU1", "VR2" )
    oView:AddSheet( "FOLDER_RU1", "VA2", STR0019, { || A133Folder( oView, "FOLDER_RU1", "FOLDER_SR0" ) } )//"Vale Alimentação"
    oView:CreateHorizontalBox( "VA2", 100, Nil, Nil, "FOLDER_RU1", "VA2" )
EndIf

oView:SetOwnerView( "GPEA133_VSRA", "SRA_HEAD" )
oView:SetOwnerView('GPEA133_VSM70','VT1')
oView:SetOwnerView('GPEA133_VSM71','VR1')
oView:SetOwnerView('GPEA133_VSM72','VA1')
oView:SetOwnerView('GPEA133_VSP7','VSP7')
oView:SetOwnerView('GPEA133_VSR00','VT')
oView:SetOwnerView('GPEA133_VSR01','VR')
oView:SetOwnerView('GPEA133_VSR02','VA')
If lTemRU1
    oView:SetOwnerView( "GPEA133_VRU10", "VT2" )
    oView:SetOwnerView( "GPEA133_VRU11", "VR2" )
    oView:SetOwnerView( "GPEA133_VRU12", "VA2" )
EndIf

oView:addUserButton(OemToAnsi(STR0030) ,"MAGIC_BMP", {|| FWExecView(STR0030, 'GPEA132',MODEL_OPERATION_UPDATE, , { || .T. }, , ,aButtons)},   OemToAnsi(STR0030),, {MODEL_OPERATION_INSERT,MODEL_OPERATION_UPDATE,MODEL_OPERATION_VIEW } ) //"Histórico"
If lTemRecalc .And. (lX6Recalc .Or. P_RECALCVT)
    oView:addUserButton(OemToAnsi(STR0056), "MAGIC_BMP", { || FWExecView(STR0056, "GPEA133A", MODEL_OPERATION_UPDATE, Nil, { || .T. }, Nil, Nil, aButtons) }, OemToAnsi(STR0056), Nil, { MODEL_OPERATION_INSERT, MODEL_OPERATION_UPDATE, MODEL_OPERATION_VIEW } )//"Ver recálculo"
EndIf

oView:SetCloseOnOk({ || .T. })//retira bota salvar e criar novo
Return oView

Static Function A133Folder(oView, cFolder1, cFolder2, cFolder3 )

Local aFolder       := oView:GetFolderActive(cFolder1,2)
Local aFolder1      := oView:GetFolderActive(cFolder2,2)
Local aFolder2      := {}

Default cFolder3    := ""

If !Empty(cFolder3)
    aFolder2      := oView:GetFolderActive(cFolder3, 2)
    If Len(aFolder) > 0 .And. Len(aFolder1) > 0 .And. Len(aFolder2) > 0 .And. aFolder[1] <> aFolder1[1] .And. aFolder[1] <> aFolder2[1]
        oView:SelectFolder(cFolder3, aFolder[1], 2)
    EndIf
EndIf

If Len(aFolder) > 0 .And. Len(aFolder1) > 0 .And. aFolder[1] <> aFolder1[1]
	oView:SelectFolder(cFolder2,aFolder[1],2)
EndIf

Return


/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ A133Remove      	³Autor³  Flavio Correa	³ Data ³13/10/2011³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Remove do View campos iguais dos grids'''''            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
*/
Static Function A133Remove(oObj,aCampos)
Local nI 	:= 1
Local nTam 	:= Len(aCampos)
For nI := 1 To nTam
	oObj:RemoveField( aCampos[nI] )
Next nI

Return

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ A133Str      	³Autor³  Flavio Correa	³ Data ³13/10/2011³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³SAltera strutura comum do model para os 3 grid's            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
*/
Static Function A133Str(oObj,cTabela)
Local lUsaCPer	:= SuperGetMv("MV_USACPER",, .F.) // .T. Cadastro de Período, .F. Ponto Eletrônico

If cTabela == "SR0"
	oObj:SetProperty("R0_SALBASE"	, MODEL_FIELD_WHEN, {|| .F. } )
	oObj:SetProperty("R0_TPVALE"	, MODEL_FIELD_WHEN, {|| .T. } )
	oObj:SetProperty('R0_MAT'		, MODEL_FIELD_OBRIGAT, .F. )
	oObj:SetProperty('R0_DIASPRO'	, MODEL_FIELD_OBRIGAT, .F. )
	oObj:SetProperty('R0_QDIADIF'	, MODEL_FIELD_OBRIGAT, .F. )
	If !lUsaCPer
		oObj:SetProperty('R0_QDIAINF'	, MODEL_FIELD_OBRIGAT, .F. )
	EndIf
	oObj:SetProperty('R0_VALDIF'	, MODEL_FIELD_OBRIGAT, .F. )
	oObj:SetProperty('R0_QDIACAL'	, MODEL_FIELD_OBRIGAT, .F. )
	oObj:SetProperty('R0_VALCAL'	, MODEL_FIELD_OBRIGAT, .F. )
	oObj:SetProperty('R0_SALBASE'	, MODEL_FIELD_OBRIGAT, .F. )
	oObj:SetProperty('R0_CC'		, MODEL_FIELD_OBRIGAT, .F. )
Else
	oObj:SetProperty('M7_MAT'		  , MODEL_FIELD_OBRIGAT, .F. )
	If !lUsaCPer
		oObj:SetProperty('M7_QDIAINF'	, MODEL_FIELD_OBRIGAT, .F. )
	EndIf
EndIf
Return

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³ Gpa133SRAStru	³Autor³  Flavio Correa	³ Data ³13/10/2011³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Selecionar os campos para a estrutura do SRA                ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
*/
Static Function Gpa133SRAStru( cCampo )
	Local lRet := .F.
	cCampo := AllTrim( cCampo )
	If cCampo $ 'RA_MAT*RA_NOME*RA_ADMISSA'
		lRet := .T.
	EndIf
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ BenLin133Ok  º Autor ³ Marcos A. Stiefano º Data ³19/07/95 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Critica linha digitada.      						      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA133							                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function BenLin133Ok(oGrid)
Local lRet  := .T.
Local lUsaCPer	:= SuperGetMv("MV_USACPER",, .F.)

If !oGrid:IsDeleted()
	If lUsaCPer .And. Empty(oGrid:GetValue("R0_QDIAINF")) .AND.  Empty(oGrid:GetValue("M7_QDNUTIL"))
		Help(" ",1,"GP130SDIA")
		lRet:= .F.
	EndIf

	If Empty(oGrid:GetValue("R0_CODIGO"))
		Help(" ",1,"GP130SMEI")
		lRet:= .F.
	EndIf
EndIf

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ SM7LinOk     º Autor ³ Marcos A. Stiefano º Data ³19/07/95 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Critica linha digitada.      						      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPEA133							                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function SM7LinOk(oGrid)
Local lRet  	:= .T.
Local lUsaCPer	:= SuperGetMv("MV_USACPER",, .F.)
Local cMsg	:= STR0031//"Integração com ponto ligado, obrigatorio preencher a quantidade de vales de todos os dias da semana"
Local aAreaRFO	:= RFO->( GetArea() )
Local aAreaSRN	:= SRN->( GetArea() )
Local cFilSRN	:= xFilial('SRN', SRA->RA_FILIAL)
Local cFilRFO	:= xFilial('RFO', SRA->RA_FILIAL)
Local lBlqLSRN  := SRN->(FieldPos("RN_MSBLQL")) > 0 
Local lBlqLRFO  := RFO->(FieldPos("RFO_MSBLQL")) > 0 
Local lBlqDSRN  := SRN->(FieldPos("RN_MSBLQD")) > 0 
Local lBlqDRFO	:= RFO->(FieldPos("RFO_MSBLQD")) > 0 
Local lMsBlq    := .F.

If !oGrid:IsDeleted()
	If !lUsaCPer
		If Empty(oGrid:GetValue("M7_VALSEG")) .And. ;
			 Empty(oGrid:GetValue("M7_VALTER")) .And. ;
			 Empty(oGrid:GetValue("M7_VALQUA")) .And. ;
			 Empty(oGrid:GetValue("M7_VALQUI")) .And. ;
			 Empty(oGrid:GetValue("M7_VALSEX")) .And. ;
			 Empty(oGrid:GetValue("M7_VALSAB")) .And. ;
			 Empty(oGrid:GetValue("M7_VALDOM")) .And. ;
			 Empty(oGrid:GetValue("M7_QDIAINF"))
				cMsg += CRLF + " Tipo : " + oGrid:cDescription + " / Linha : " + AllTrim(Str(oGrid:nLine))
				Help( ,, "QTDVALES",, OemToAnsi(cMsg), 1,,,,,,, {OemToAnsi(STR0047)} )
				lRet:= .F.
		EndIf
	Else
		If Empty(oGrid:GetValue("M7_QDIAINF")) .AND. Empty(oGrid:GetValue("M7_QDNUTIL"))
			Help(" ",1,"GP130SDIA")
			lRet:= .F.
		EndIf
	EndIf
		
		If lRet
			// Vale Transporte
			If oGrid:GetValue("M7_TPVALE") == "0"
				DbSelectArea("SRN")
				DbSetOrder(1) //-RN_FILIAL+RN_COD
				If SRN->(dbSeek(cFilSRN + oGrid:GetValue("M7_CODIGO")))				
					If lBlqLSRN .And. SRN->RN_MSBLQL == "1" 
						lMsBlq := .T.
					ElseIf lBlqDSRN .And. !Empty(SRN->RN_MSBLQD) .And. SRN->RN_MSBLQD < dDataBase
						lMsBlq := .T.
					EndIf
				EndIf
			EndIf
			// Vale Alimentação/Vale Refeição
			DbSelectArea("RFO")
			DbSetOrder(1) //-RFO_FILIAL+RFO_TPVALE+RFO_CODIGO
			If RFO->(dbSeek(cFilRFO+ oGrid:GetValue("M7_TPVALE")+oGrid:GetValue("M7_CODIGO")))
				If lBlqLRFO .And. RFO->RFO_MSBLQL == "1"
					lMsBlq := .T.
				ElseIf lBlqDRFO .And. !Empty(RFO->RFO_MSBLQD) .And. RFO->RFO_MSBLQD < dDataBase
					lMsBlq := .T.
				EndIf
			EndIf
			If   lMsBlq
				Help(" ",1,"REGBLOQ")
				lRet := .F.
			EndIf

	RestArea(aAreaRFO)
	RestArea(aAreaSRN)
		EndIf
			
EndIf

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ CodBenef ³ Autor ³ Equipe RH Inovacao ³ Data ³ 16/01/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Monta Combo com os codigos de beneficios                   ³±±
±±³          ³ periodo indicado.                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GPEA133                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function CodBenef(nTpVale, lArray)

	Local aArea		:= GetArea()
	Local aAreaSR0	:= SR0->( GetArea() )
	Local aAreaRFO	:= RFO->( GetArea() )
	Local cOpcBox   := ""
	Local cRet
	Local cChave	:= xFilial('RFO',SRA->RA_FILIAL) + StrZero(nTpVale, 1)
	Local nTamCod	:= TamSX3("M7_CODIGO")[1]
	Local cFilSRN	:= xFilial('SRN', SRA->RA_FILIAL)
	Local cFilRFO	:= xFilial('RFO', SRA->RA_FILIAL)
	Local lBlqLSRN  := SRN->(FieldPos("RN_MSBLQL")) > 0 
	Local lBlqLRFO  := RFO->(FieldPos("RFO_MSBLQL")) > 0 
	Local lBlqDSRN  := SRN->(FieldPos("RN_MSBLQD")) > 0 
	Local lBlqDRFO	:= RFO->(FieldPos("RFO_MSBLQD")) > 0 
	Local lMsBlq    := .F.

	DEFAULT nTpVale := 0
	DEFAULT lArray  := .F.

	If nTpVale == 0 .And. !Empty(cVTRBox) .And. cFilSRN == cFilVTRBox
		cRet := If( lArray, StrToArray(cVTRBox,";"), cVTRBox )
		Return cRet
	ElseIf nTpVale == 1 .And. !Empty(cVRFBox) .And. cFilVRBox == cFilRFO
		cRet := If( lArray, StrToArray(cVRFBox,";"), cVRFBox )
		Return cRet
	ElseIf nTpVale == 2 .And. !Empty(cVALBox) .And. cFilVABox == cFilRFO
		cRet := If( lArray, StrToArray(cVALBox,";"), cVALBox )
		Return cRet
	EndIf

	dbSelectArea('SR0')

	If nTpVale = 0	
		DbSelectArea("SRN")
		DbSetOrder(1) //-RN_FILIAL+RN_COD
		SRN->(DbGoTop())
		SRN->(dbSeek(cFilSRN))
		While SRN->(!Eof() .And. RN_FILIAL == cFilSRN)
				If lBlqLSRN .And. SRN->RN_MSBLQL == "1" 
					lMsBlq := .T.
				ElseIf lBlqDSRN .And. !Empty(SRN->RN_MSBLQD) .And. SRN->RN_MSBLQD < dDataBase
					lMsBlq := .T.
				EndIf
			If !lMsBlq
				cOpcBox += SRN->( RN_COD + "=" + RN_DESC + ";" )
			EndIf
			lMsBlq := .F.
			SRN->(dbSkip())
		EndDo
		cFilVTRBox := cFilSRN

	Else //-nTpVale $ "1*2"

		DbSelectArea("RFO")
		DbSetOrder(1) //-RFO_FILIAL+RFO_TPVALE+RFO_DESCR+RFO_TPBEN+RFO_CODIGO
		RFO->(DbGoTop())
		RFO->(dbSeek(cChave))
		While RFO->( !Eof() .And. RFO_FILIAL + RFO_TPVALE == cChave)
				If lBlqLRFO .And. RFO->RFO_MSBLQL == "1"
					lMsBlq := .T.
				ElseIf lBlqDRFO .And. !Empty(RFO->RFO_MSBLQD) .And. RFO->RFO_MSBLQD < dDataBase
					lMsBlq := .T.
				EndIf
			If   !lMsBlq
				cOpcBox += RFO->( PadR(RFO_CODIGO, nTamCod ) + "=" + RFO_DESCR + ";" )
			EndIf
			lMsBlq := .F.
			RFO->(dbSkip())			
		EndDo
		If nTpVale == 1
			cFilVRBox := cFilRFO
		EndIf
	EndIf

	RestArea(aAreaSR0)
	RestArea(aAreaRFO)
	RestArea(aArea)

	If nTpVale == 0
		cVTRBox := cOpcBox
	ElseIf nTpVale == 1
		cVRFBox := cOpcBox
	ElseIf nTpVale == 2
		cVALBox := cOpcBox
	EndIf

	If lArray
		cRet := StrToArray(cOpcBox,";")
	Else
		cRet := cOpcBox
	EndIf

Return( cRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ BuscaTBen   ³ Autor ³Flavio Correa       ³ Data ³ 01/08/16 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Tipo beneficios											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPEA133                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function BuscaTBen(nTpVale,cCodigo,cFil)
Local cTpBen := ""

If nTpVale == "0"
	DbSelectArea("SRN")
   	SRN->(DbSetOrder(1)) //-RN_FILIAL+RN_COD
    If SRN->(dbSeek(xFilial('SRN',cFil)+cCodigo))
    	cTpBen := SRN->RN_TPBEN
   	EndIf
Else //-nTpVale $ "1*2"
   	DbSelectArea("RFO")
   	RFO->(DbSetOrder(1)) //-RFO_FILIAL, RFO_TPVALE, RFO_CODIGO
   	If RFO->(dbSeek(xFilial('RFO',cFil)+nTpVale+cCodigo))
		cTpBen := RFO->RFO_TPBEN
	EndIf
EndIf

Return cTpBen

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A133DelOk ³ Autor ³ Flavio Correa         ³ Data ³ 07.04.15 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida exclusao da linha                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function A133DelOk(oGrid, nLine, cAction, cField)
Local lRet 			:= .T.
Local oModel 		:= oGrid:GetModel()
Local nOperation 	:= oModel:GetOperation()

If cAction == 'DELETE' .and. nOperation == MODEL_OPERATION_UPDATE

	If oGrid:GetValue("R0_PEDIDO") == "2"
		Help( , ,'Help', ,STR0044,1,0) //"Item já foi gerado pedido e não pode ser excluída!"
		lRet := .F.
		oGrid:UnDeleteLine()
	EndIf

EndIf

Return lRet


/*/{Protheus.doc}fCriaTemp()
- Cria tabela de backup da SR0
@author: Leandro Drumond
@since:  28/09/2017
/*/
Static Function fCriaTemp(cAliasLock)
Local aFields		:= {}
Local lRet			:= .T.
Local nI			:= 1

cAliasLock := "BKPSR0_" + cEmpAnt
aFields := SR0->(dbStruct())
If TCCanOpen(cAliasLock)
	TCDelFile(cAliasLock)
EndIf

MsCreate(cAliasLock,aFields,"TOPCONN")
MsOpenDbf( .T. , __cRdd , cAliasLock , cAliasLock , .T. )
While !SR0->(eof())
	RecLock(cAliasLock,.T.)
	For nI := 1 To Len(aFields)
		&((cAliasLock)->(aFields[nI][1])) := &("SR0->"+aFields[nI][1])
	Next nI
	(cAliasLock)->(msUnlock())
	SR0->(dbSkip())
EndDo
(cAliasLock)->(dbCloseArea())
Return lRet

Static Function fSetoSM7(oModel)
Local oView := FWViewActive()
Local nPos  := 0
Local nX	:= 0
Local nY	:= 2

 	For nX := 0 To 2
		nPos := Ascan(oModel:getModel("GPEA133_MSM7"+cValtoChar(nX), "M7_CODIGO"):OFORMMODELSTRUCT:aFields,{|x| x[3]== "M7_CODIGO"})
		If(nPos > 0)
			oModel:getModel("GPEA133_MSM7"+cValtoChar(nX), "M7_CODIGO"):OFORMMODELSTRUCT:aFields[nPos][9] := CodBenef(nX,.T.)
			oView:aviews[nY++][3]:OSTRUCTVIEW:afields[1][13] := CodBenef(nX,.T.)
		EndIf
		nPos := 0
	Next nX
	oView:SetModel(oModel)
Return (.T.)

/*/{Protheus.doc} CargaAux
Carrega registros de cadastro/cálculo de benefícios
@author Allyson Luiz Mesashi
@since 09/03/2022
/*/
Static Function CargaAux( oGrid )

Local aRet := {}

oGrid:aDataModel := {}
aRet := FormLoadGrid(oGrid)

Return aClone(aRet)

/*/{Protheus.doc} fCommit
Commit da rotina GPEA133
@author Allyson Luiz Mesashi
@since 28/07/2023
/*/
Static Function fCommit(oModel)

Local cSR0NroPed    := ""
Local cSR0Pedido    := ""
Local lRet          := .T.
Local nCont         := 0
Local nCont2        := 0
Local oGridRU1VA    := Nil
Local oGridRU1VR    := Nil
Local oGridRU1VT    := Nil
Local oGridSR0VA    := Nil
Local oGridSR0VR    := Nil
Local oGridSR0VT    := Nil

DEFAULT lTemRU1     := AliasInDic("RU1")

If lTemRU1
    oGridRU1VA    := oModel:GetModel('GPEA133_MRU12')
    oGridRU1VR    := oModel:GetModel('GPEA133_MRU11')
    oGridRU1VT    := oModel:GetModel('GPEA133_MRU10')
    oGridSR0VA    := oModel:GetModel('GPEA133_MSR02')
    oGridSR0VR    := oModel:GetModel('GPEA133_MSR01')
    oGridSR0VT    := oModel:GetModel('GPEA133_MSR00')

    For nCont := 1 To oGridSR0VA:Length()
        oGridSR0VA:GoLine(nCont)
        If !oGridSR0VA:IsInserted() .And. !oGridSR0VA:IsDeleted() .And. oGridSR0VA:IsUpdated() .And. oGridSR0VA:IsFieldUpdated("R0_PEDIDO")
            cSR0NroPed    := oGridSR0VA:GetValue("R0_NROPED")
            cSR0Pedido    := oGridSR0VA:GetValue("R0_PEDIDO")
            For nCont2 := 1 To oGridRU1VA:Length()
                oGridRU1VA:GoLine(nCont2)
                If oGridRU1VA:GetValue("RU1_NROPED") == cSR0NroPed
                    oGridRU1VA:LoadValue( "RU1_PEDIDO", cSR0Pedido )
                EndIf
            Next nCont2
        EndIf
    Next nCont

    For nCont := 1 To oGridSR0VR:Length()
        oGridSR0VR:GoLine(nCont)
        If !oGridSR0VR:IsInserted() .And. !oGridSR0VR:IsDeleted() .And. oGridSR0VR:IsUpdated() .And. oGridSR0VR:IsFieldUpdated("R0_PEDIDO")
            cSR0NroPed    := oGridSR0VR:GetValue("R0_NROPED")
            cSR0Pedido    := oGridSR0VR:GetValue("R0_PEDIDO")
            For nCont2 := 1 To oGridRU1VR:Length()
                oGridRU1VR:GoLine(nCont2)
                If oGridRU1VR:GetValue("RU1_NROPED") == cSR0NroPed
                    oGridRU1VR:LoadValue( "RU1_PEDIDO", cSR0Pedido )
                EndIf
            Next nCont2
        EndIf
    Next nCont

    For nCont := 1 To oGridSR0VT:Length()
        oGridSR0VT:GoLine(nCont)
        If !oGridSR0VT:IsInserted() .And. !oGridSR0VT:IsDeleted() .And. oGridSR0VT:IsUpdated() .And. oGridSR0VT:IsFieldUpdated("R0_PEDIDO")
            cSR0NroPed    := oGridSR0VT:GetValue("R0_NROPED")
            cSR0Pedido    := oGridSR0VT:GetValue("R0_PEDIDO")
            For nCont2 := 1 To oGridRU1VT:Length()
                oGridRU1VT:GoLine(nCont2)
                If oGridRU1VT:GetValue("RU1_NROPED") == cSR0NroPed
                    oGridRU1VT:LoadValue( "RU1_PEDIDO", cSR0Pedido )
                EndIf
            Next nCont2
        EndIf
    Next nCont

EndIf

lRet := FWFormCommit( oModel )

Return lRet

/*/{Protheus.doc} fChkDupRecalc
Função para verificar se existe duplicidade com a chave única antiga nas versões anteriores a 2310 para aviso de possível erro na migração
@author Bruno Costa
@since 06/05/2024
@version 1.0
/*/
Function fChkDupRecalc()
Local oGroup1
Local oPanel1
Local oSay1
Local oButton1
Local oCheckBo1
Local oDlg
Local cAliasRU1 := ""
Local cAliasRU3 := ""
Local cSession	:= "AlertaRU1RU3"
Local cKey		:= "MSG_RU1RU3_"
Local lCheckBo1 := .F.
Local lDupRU1   := .F.
Local lDupRU3   := .F.
Local lChkMsg 	:= fwGetProfString(cSession, cKey + cUserName, '', .T.) == ""

If lChkMsg

	cAliasRU1 := GetNextAlias()
	cAliasRU3 := GetNextAlias()
	
	BeginSql alias cAliasRU1
		SELECT COUNT(*) AS CONTADOR 
		FROM %table:RU1% RU1 
		WHERE RU1.%NotDel% 
		GROUP BY RU1_FILIAL,RU1_MAT,RU1_TPVALE,RU1_CODIGO,RU1_NROPED,RU1_DATA
		HAVING COUNT(*) > 1
	EndSql

	If ( cAliasRU1 )->( !Eof() )
		If ( cAliasRU1 )->CONTADOR > 0
			lDupRU1 := .T.
		EndIf
	EndIf

	(cAliasRU1)->( DbCloseArea() ) 

	BeginSql alias cAliasRU3
		SELECT COUNT(*) AS CONTADOR
		FROM %table:RU3% RU3 
		WHERE RU3.%NotDel% 
		GROUP BY RU3_FILIAL,RU3_MAT,RU3_TPVALE,RU3_CODIGO,RU3_NROPED,RU3_DATA
		HAVING COUNT(*) > 1
	EndSql

	If ( cAliasRU3 )->( !Eof() )
		If ( cAliasRU3 )->CONTADOR > 0
			lDupRU3 := .T.
		EndIf
	EndIf

	(cAliasRU3)->( DbCloseArea() ) 

	If lDupRU1 .Or. lDupRU3
		
		DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0063) FROM 000, 000  TO 200, 500 COLORS 0, 16777215 PIXEL // "Aviso Importante"

			@ 000, 000 MSPANEL oPanel1 SIZE 300, 150 OF oDlg COLORS 0, 16777215 RAISED
			@ 005, 012 GROUP oGroup1 TO 065, 237 PROMPT OF oPanel1 COLOR 0, 16777215 PIXEL
			@ 010, 017 SAY oSay1 PROMPT OemToAnsi(STR0064 + " " + Iif(lDupRU1 .And. lDupRU3, STR0065 + "e " + STR0066, Iif(lDupRU1, STR0065, STR0066 ) ) + " " + STR0067)  SIZE 215, 035 OF oPanel1 COLORS 0, 16777215 PIXEL //#Identificado que a chave única da(s) tabela(s)#Detalhamento (RU1)#Histórico Detalhe Benefícios (RU3)##está atualizada, e neste caso o processo de Migração de Releases ou conferência de Duplicidade (CheckDupl) causará erro, pois existem registros duplicados com a chave única antiga. 
			@ 045, 017 SAY oSay1 PROMPT OemToAnsi(STR0068)  SIZE 215, 035 OF oPanel1 COLORS 0, 16777215 PIXEL //#Aviso importante que deverá ser direcionado ao setor responsável pelas atualizações de pacotes e dicionários, para maiores informações clique em "Abrir Link" para consultar a documentação.
			@ 080, 012 CHECKBOX oCheckBo1 VAR lCheckBo1 PROMPT OEMToAnsi(STR0069) SIZE 067, 008 OF oPanel1 COLORS 0, 16777215 PIXEL //#Não exibir novamente
			@ 070, 160 BUTTON oButton1 PROMPT (STR0070) SIZE 037, 012 OF oPanel1 PIXEL //#Abrir Link
			@ 070, 200 BUTTON oButton2 PROMPT "OK" SIZE 037, 012 OF oPanel1 PIXEL

			oButton1:bLClicked := {|| ShellExecute("open","https://tdn.totvs.com/pages/releaseview.action?pageId=841169932","","",1) }
			oButton2:bLClicked := {|| oDlg:End() }

		ACTIVATE MSDIALOG oDlg CENTERED

		If lCheckBo1
			fwWriteProfString(cSession, cKey + cUserName, 'CHECKED', .T.)
		EndIf
	EndIf
EndIf	

Return

/*/{Protheus.doc} fMsgCancel
Função para exibir documentação referente a Cancelamento de Cálculo
@since 12/06/2025
@version 1.0
/*/
Function fMsgCancel(oModel)
Local oGroup1
Local oPanel1
Local oSay1
Local oButton1
Local oCheckBo1
Local oDlg
Local cSession		:= "AlertaSX9SM7"
Local cKey			:= "MSG_SX9SM7_"
Local lCheckBo1 	:= .F.
Local lChkMsg 		:= fwGetProfString(cSession, cKey + cUserName, '', .T.) == ""
Local nOperation 	:= oModel:GetOperation()

If !IsBlind() .And. nOperation == MODEL_OPERATION_UPDATE .And. lChkMsg
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0063) FROM 000, 000  TO 200, 500 COLORS 0, 16777215 PIXEL // "Aviso Importante"

		@ 000, 000 MSPANEL oPanel1 SIZE 300, 150 OF oDlg COLORS 0, 16777215 RAISED
		@ 005, 012 GROUP oGroup1 TO 065, 237 PROMPT OF oPanel1 COLOR 0, 16777215 PIXEL
		// "Ao Deletar um registro a rotina verifica se existem dados vinculados àquele Benefício (tabela SM7) garantindo a integridade entre as tabelas." 
		@ 010, 017 SAY oSay1 PROMPT OemToAnsi(STR0071) SIZE 215, 035 OF oPanel1 COLORS 0, 16777215 PIXEL 
		// O sistema exibe uma mensagem de Violação de Integridade e em qual tabela a referência foi encontrada.
		@ 025, 017 SAY oSay1 PROMPT OemToAnsi(STR0072)  SIZE 215, 035 OF oPanel1 COLORS 0, 16777215 PIXEL 
		// "Nessa ocasião, saiba como prossegur através da documentação disponível em 'Abrir link'."
		@ 045, 017 SAY oSay1 PROMPT OemToAnsi(STR0073)  SIZE 215, 035 OF oPanel1 COLORS 0, 16777215 PIXEL 
		
		@ 080, 012 CHECKBOX oCheckBo1 VAR lCheckBo1 PROMPT OEMToAnsi(STR0069) SIZE 067, 008 OF oPanel1 COLORS 0, 16777215 PIXEL //#Não exibir novamente
		@ 070, 160 BUTTON oButton1 PROMPT (STR0070) SIZE 037, 012 OF oPanel1 PIXEL //#Abrir Link
		@ 070, 200 BUTTON oButton2 PROMPT "OK" SIZE 037, 012 OF oPanel1 PIXEL

		oButton1:bLClicked := {|| ShellExecute("open","https://tdn.totvs.com/pages/releaseview.action?pageId=911836535","","",1) }
		oButton2:bLClicked := {|| oDlg:End() }

	ACTIVATE MSDIALOG oDlg CENTERED

	If lCheckBo1
		fwWriteProfString(cSession, cKey + cUserName, 'CHECKED', .T.)
	EndIf
EndIf

Return .T.
