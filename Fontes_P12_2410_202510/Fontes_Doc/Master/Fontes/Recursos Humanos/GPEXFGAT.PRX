#INCLUDE "PROTHEUS.CH" 
#INCLUDE "GPEX1FUN.CH"

Static lCodCBO

/*/


Ŀ
			ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.			  			
Ĵ
Programador  Data	 FNC			   Motivo da Alteracao 					
Ĵ
Flavio Corre25/02/15PCREQ-2898      Inclusao fonte							
Henrique V. 30/06/15TSHQW7          Rplica do chamado TSASXB. Corrigido		
                                    tratamento da Filial na funcao DescFun	
                                    p/ que retorne a funcao correta 			
Marcos Perei03/09/15PCREQ-5342      Produtizacao proj. Gesto Pblica na 12.  
Renan Borges03/04/17        MRH-9141Ajuste para mostrar Centro de Custo corre-
                                    tamente do perodo de impresso do espelho
                                    do ponto.                                   
ٱ

*/
/*
Usar essa documentao quando inclui o fonte em alguma pasta de inovao, por exemplo
12.1.6, a cada merge com o fonte da sustentao atualizar as informaes abaixo para 
que no merge final fique facil a atualizao do fonte
ͳ
Data Fonte Sustentao ChangeSet 
ĳ  
    30/06/2015          312588   
ͱ
*/

/*/


Ŀ
Funo	 DescPd 	 Autor  J. Ricardo 		     Data  23.02.95 
Ĵ
Descrio  Retorna a Descricao do Codigo de Provento / Desconto		  
Ĵ
 Uso		  Generico 												  
ٱ

/*/
Function DescPd(cCodigo, cFil, nBytes)
	Local aSaveArea	:= GetArea()

	Default nBytes := 20
	nBytes := Min(nBytes, 20)

	dbSelectArea("SRV")
	dbSetOrder(1)

	If cFil != Nil .and. !(cFilial == Space(FwSizeFilial()))
		cFil := xFilial('SRV',cFil) //ajusta conforme compartilhamento da tabela SRV
	Else
		cFil := cFilial
	EndIf
	
	If dbSeek(cFil + cCodigo)
		cRet := Left(RV_DESC + Space(nBytes), nBytes)
	Else
		cRet := Left(STR0010 + Space(nBytes), nBytes)  //"** Nao Cadastrado **"
	EndIf

	RestArea(aSaveArea)
Return cRet

/*/


Ŀ
Funo	 ADescPD	 Autor  J. Ricardo 		     Data  22.03.95 
Ĵ
Descrio  Verifica a existencia do codigo de prov/desc e Retorna Desc
Ĵ
 Uso		  Generico 												  
ٱ

/*/
Function ADescPd(cCampo)
	Local cVar
	Local aArea := GetArea()
	Local lRet  := .F.
	Local nLoop
	Local oModel
	Local oView
	
	cVar := &(ReadVar())
	dbSelectArea("SRV")
	nOrdemV := IndexOrd()
	dbSetOrder(1)
	
	If dbSeek( xFilial("SRV") + cVar)
		lRet := .T.
		If FunName() == "GPEA120"
			oModel 	:= FWModelActive()
			oModel:GetModel( 'GPEA120_SRD' ):SetValue("RD_DESCPD",SRV->RV_DESC)
			oView := FWViewActive()
			oView:Refresh()			
		Else
			If !(Type("aHeader") != "A" .Or. Type("aCols") != "A")
				For nLoop := 1 To Len(aHeader)
					If Trim(aHeader[nLoop][2]) == cCampo   // campo virtual da descricao do
						aCols[n][nLoop] := RV_DESC			  // provento/desconto/base
					EndIf
				Next nLoop
			EndIf
		EndIf
	EndIf
	
	dbSetOrder(nOrdemV)
	RestArea(aArea)
Return ( lRet )

/*/


Ŀ
Funo	 DescCc 	 Autor  Ze Maria			     Data  23.02.95 
Ĵ
Descrio  Retorna a Descricao do Centro de Custo					  
Ĵ
 Uso		  Generico 												  
ٱ

/*/
Function DescCc(cCodigo,cFil,nBytes)
	Local aArea := GetArea()

	Default nBytes := 25
	nBytes := Min(nBytes, 25)

	dbSelectArea("CTT")

	If dbSeek( xFilial( "CTT", cFil ) + cCodigo )
		cRet := Left(CTT_DESC01 + Space(nBytes), nBytes)
	Else
		cRet := Left(STR0011 + Space(nBytes), nBytes)  //"** Nao Cadastrado **"
	EndIf

	RestArea(aArea)
Return( cRet )

/*/


Ŀ
Funo	 DescFun	 Autor  Ze Maria			     Data  23.02.95 
Ĵ
Descrio  Retorna a Descricao da Funcao 							  
Ĵ
 Uso		  Generico 												  
ٱ

/*/
Function DescFun(cCodigo, cFil, nBytes)
	Local aArea := GetArea()

	Default nBytes := TamSX3("RJ_DESC ")[1]

	dbSelectArea("SRJ")

	If dbSeek ( xFilial("SRJ", cFil) + cCodigo )
		cRet := Left(RJ_DESC + Space(nBytes), nBytes)
	Else
		cRet := Left(STR0011 + Space(nBytes), nBytes)  //"** Nao Cadastrado **"
	EndIf

	RestArea(aArea)
Return ( cRet )

/*/


Ŀ
Funo	 DescBco	 Autor  Ze Maria			     Data  23.02.95 
Ĵ
Descrio  Retorna a Descricao de Banco ou de Agncia 				  
Ĵ
 Uso		  Generico 												  
ٱ

/*/
Function DescBco(cCodigo, cFil, nBytes, lNomeAg, lVldSA6)
	Local aArea		:= GetArea()
	Local cChave	:= ""
	Local cRet		:= ""

	DEFAULT nBytes	:= 40
	DEFAULT lNomeAg	:= .F.
	DEFAULT lVldSA6 := .T.
	
	nBytes := Min(nBytes, 40)
	
	dbSelectArea("SA6" )
	
	If (cFil != Nil .AND. cFilial == Space(FWGETTAMFILIAL)) .OR. cFil == Nil .OR. (cFil != Nil .AND. Len(Alltrim(cFilial)) < FWGETTAMFILIAL)
		cFil := cFilial
	EndIf
	
	cChave := cFil + cCodigo

	If dbSeek(cChave)
		If !lNomeAg  .And. Len( AllTrim( cCodigo ) ) > 3
			cChave := cFil + Substr( cCodigo, 1, 3 )
		EndIf

		While SA6->( !Eof() .and. iif(lNomeAg , AllTrim(A6_FILIAL+A6_COD+A6_AGENCIA), AllTrim(A6_FILIAL+A6_COD) ) == AllTrim(cChave) )
 			If SA6->A6_BLOCKED <> "1"
				If lNomeAg
					cRet := Left(A6_NOMEAGE + Space(nBytes), nBytes)
				Else
					cRet := Left(A6_NOME + Space(nBytes), nBytes)
				EndIf
				Exit
			EndIf
			SA6->(DbSkip())			
		EndDo
	EndIf
		
 	If Empty(cRet)
	 	If lVldSA6
			cRet := Left(STR0011 + Space(nBytes), nBytes)  //"** Nao Cadastrado **"
		Else
			cRet := Space(nBytes)
		EndIf			
	EndIf
	
	RestArea(aArea)

    Return cRet

/*/


Ŀ
Funo	 DescVT 	 Autor  Marcos A. Stiefano	 Data  11.07.95 
Ĵ
Descrio  Retorna a Descricao do Codigo 							  
Ĵ
 Uso		  Generico 												  
ٱ

/*/
Function DescVT(cCodigo, cFil, nBytes)
	Local aSaveArea	:= GetArea()

	Default nBytes := 15
	nBytes := Min(nBytes, 15)

	dbSelectArea("SRN" )
	
	If (cFil != Nil .AND. cFilial == Space(FWGETTAMFILIAL)) .OR. cFil == Nil .OR. (cFil != Nil .AND. Len(Alltrim(cFilial)) < FWGETTAMFILIAL)
		cFil := cFilial
	EndIf
	
	If dbSeek(cFil + cCodigo )
		cRet := Left(RN_DESC+Space(nBytes),nBytes)
	Else
		cRet := Left(STR0011+Space(nBytes),nBytes)  //"** Nao Cadastrado **"
	EndIf
	
	RestArea(aSaveArea)
Return cRet

/*/


Ŀ
Funo	 ADescVT	 Autor  Marcos A. Stiefano	 Data  11.07.95 
Ĵ
Descrio  Verifica a existencia do cod. Vale Transporte, Retorna Desc
Ĵ
 Uso		  Generico 												  
ٱ

/*/
Function ADescVT(cCampo)

Local cVar , lRet := .F.
Local aArea := GetArea()
Local nLoop

cVar := &(ReadVar())
dbSelectArea("SRN")
nOrdemV := IndexOrd()
dbSetOrder(1)

If dbSeek(cFilial + cVar )
	lRet := .T.
	For nLoop := 1 to Len(aHeader)
		If Trim(aHeader[nLoop][2]) == cCampo   // campo virtual da descricao do
			aCols[n][nLoop] := RN_DESC			  // Meio Vale Transporte
		EndIf
	Next nLoop
EndIf

dbSetOrder(nOrdemV)
RestArea(aArea)

Return ( lRet )

/*/


Ŀ
Funo	 ADescTA	 Autor  Marcos A. Stiefano	 Data  11.07.95 
Ĵ
Descrio  Verifica a existencia do cod. Tarefas , Retorna Desc		  
Ĵ
 Uso		  Generico 								 				  
ٱ

/*/
Function ADescTa(cCampo)
	LocaL cVar
	Local lRet := .F.
	Local aArea := GetArea()
	Local nLoop
	
	cVar := &(ReadVar())
	dbSelectArea("SRX")
	
	If dbSeek(xFilial("SRX") + "59" + "0" + cVar )
		lRet := .T.
		For nLoop := 1 To Len(aHeader)
			If Trim(aHeader[nLoop][2]) == "RO_DESTAR"
				aCols[n][nLoop] := SubStr( SRX->RX_TXT,01,30 )
			ElseIf Trim(aHeader[nLoop][2]) == "RO_VALOR"
				aCols[n][nLoop] := Val( SubStr( SRX->RX_TXT,31,12 ) )
			ElseIf Trim(aHeader[nLoop][2]) == "RO_VERBA"
				aCols[n][nLoop] := SubStr( SRX->RX_TXT,43,03 )
			EndIf
		Next nLoop
	EndIf
	
	RestArea(aArea)
Return ( lRet )

/*/


Ŀ
Funo	 DescTA 	 Autor  Marcos A. Stiefano	 Data  11.07.95 
Ĵ
Descrio  Retorna a Descricao do Codigo 							  
Ĵ
 Uso		  Generico 												  
ٱ

/*/
Function DescTA(cCodigo,cFil,nBytes)
	Local aArea  := GetArea()
	Local nOrdem := IndexOrd()
	Local nX	 := 0 
	Local cTab 	 := "S070"
	
	Default nBytes := 15
	nBytes := Min(nBytes, 15)

	nx := fPosTab(cTab, cCodigo, "=", 4, cFil,"=",1 )
	If nx == 0
		nx := fPosTab(cTab, cCodigo, "=", 4 )
	EndIf
	If nX > 0
		cRet := SubStr(fTabela(cTab,nX,5),1,nBytes)  //Sindicato
	Else
		cRet := Left("** Nao Cadastrado **" + Space(nBytes), nBytes)
	EndIf	
	
	RestArea(aArea)
	dbSetOrder( nOrdem )
Return( cRet )

/*


Ŀ
Funo     fCodCBO() Autor  Emerson Rosa de Souza  Data 06/01/2003
Ĵ
Descrio Retorna o codigo do CBO de acordo com a funcao e data       
Ĵ
 Uso      Generico                                                    
ٱ

*/
Function fCodCBO(cCodFil,cCodFunc,dDatRef,lNewCbo)
Local aArea := GetArea()
Local cRet,nAnoRef

dbSelectArea( "SRJ" )
dbSetOrder(1)

Default lNewCbo := .F.
Default lCodCBO := SRJ->( ColumnPos( "RJ_CODCBO" ) ) > 0

//Ŀ
// O novo campo devera ser utilizado a partir do exercicio 2003 
//
cCodFil  := If(cCodFil == Nil , xFilial("SRJ"), xFilial("SRJ",cCodFil))
cCodFunc := If(cCodFunc == Nil, "   ", cCodFunc)
nAnoRef  := If(dDatRef  == Nil, 2003, If(ValType(dDatRef) == "C", Val(dDatRef), Year(dDatRef)))
cRet     := If(nAnoRef > 2002 .AND. lCodCBO, Space(6), Space(5))

If MsSeek( cCodFil + cCodFunc )   
                                      
	If (lNewCbo .AND. lCodCBO)
		cRet := SRJ->RJ_CODCBO  // Novo campo criado para uso em 2003
	Else	
		If (nAnoRef > 2002 .AND. lCodCBO)
			cRet := SRJ->RJ_CODCBO  // Novo campo criado para uso em 2003
		Else
			cRet := SRJ->RJ_CBO     // Campo antigo preservado para uso em 2002
		EndIf	
	EndIf
EndIf

RestArea( aArea )

Return StrZero(Val(cRet), Len(cRet))

/*


ͻ
Programa  fDescRCC  Autor   Rogerio Vaz Melonio Data   25/04/08   
͹
Desc.      Retorna descricao de campo conteudo da tabela RCC          
                                                                      
͹
Parametros cCodigo = codigo da tabela                				  
			  cConteudo = chave a ser localizada para a tabela		  	  
			  nPos1 = posicao inicial do chave		       				  
			  nPos2 = tamanho da chave 								  
			  nPos3 = posicao inicial do retorno						  
			  nPos4 = tamanho do retorno 								  
͹
Uso        AP8                                                        
ͼ


*/
Function fDescRCC(cCodigo,cConteudo,nPos1,nPos2,nPos3,nPos4,lValidFil)
Local cRet := ""
Local cFilialOri := cFilAnt
Local cRccFilOri := ""

DEFAULT lValidFil := .F.

_aArea := GetArea()

If nPos1 = Nil
	nPos1 := 0
EndIf
If nPos2 = Nil
	nPos2 := 0
EndIf

If cCodigo = "S043"
	cFilialOri := If(FunName()=="EDAPP",SRG->RG_FILIAL,SRA->RA_FILIAL)
EndIf

If cCodigo <> Nil .AND. cConteudo <> Nil
	dbSelectArea( "RCC" )
	dbSetOrder(1)
	cRccFilOri := xFilial("RCC",cFilialOri)
	dbSeek(cRccFilOri+ cCodigo)
	While !Eof() .AND. RCC->RCC_FILIAL+RCC_CODIGO == cRccFilOri+cCodigo
		If RCC->RCC_FILIAL+RCC_CODIGO == cRccFilOri+cCodigo .AND. Alltrim(Substr(RCC->RCC_CONTEU,nPos1,nPos2)) == Alltrim(cConteudo)
			If !lValidFil .or. RCC->RCC_FIL == cRccFilOri .or. RCC->RCC_FIL == SRA->RA_FILIAL 
				cRet := Substr(RCC->RCC_CONTEU,nPos3,nPos4)
				Exit
			EndIf
		EndIf
		dBSkip()	
	EndDo
	//No encontrou registro com a filial, busca sem filial
	If lValidFil .and. Empty(cRet)
		dbSeek(xFilial("RCC")+ cCodigo)
		While !Eof() .AND. RCC->RCC_FILIAL+RCC_CODIGO == xFilial("RCC")+cCodigo
			If RCC->RCC_FILIAL+RCC_CODIGO == xFilial("RCC")+cCodigo .AND. Alltrim(Substr(RCC->RCC_CONTEU,nPos1,nPos2)) == Alltrim(cConteudo)
				If Empty(RCC->RCC_FIL) 
					cRet := Substr(RCC->RCC_CONTEU,nPos3,nPos4)
			Exit
		EndIf
			EndIf
		dBSkip()	
	EndDo
EndIf
EndIf

RestArea(_aArea)

Return(cRet)           

/*


ͻ
Programa  GPEXFUN   Autor  Erika Kanamori       Data   08/01/09   
͹
Desc.     Retorna o campo CTP_TAXA para conversao de valores.         
                                                                      
͹
Uso        AP                                                         
ͼ


*/
Function RetTaxaConver( cMoeda, dDataConv )
Local nTaxaConver:= 0

If len(cMoeda) < 2
	cMoeda:= StrZero(  Val(cMoeda), 2 )
EndIf
	
dbSelectArea("CTP")
                 
nTaxaConver:= POSICIONE( "CTP", 1, xFilial("CTP")+ StrZero(Year(dDataConv),4) + StrZero(Month(dDataConv), 2) + StrZero(Day(dDataConv), 2) + cMoeda, "CTP_TAXA" )

Return (nTaxaConver)          

/*/


Ŀ
Funo	 DescCateg  Autor  J. Ricardo 			 Data  11.04.96 
Ĵ
Descrio  Retorna a Descricao da Categoria do Funcionario.			  
Ĵ
 Uso		  Generico 												  
ٱ

/*/
Function DescCateg(cCat,nTam)
Local cRet := " "                          

cCat	:=	If(cCat==Nil,SRA->RA_CATFUNC,cCat)
nTam 	:= 	If(nTam==Nil,12,nTam)

cRet:=fDesc("SX5","28"+cCat,"X5DESCRI()",nTam,SRA->RA_FILIAL)

Return Left(cRet+Space(nTam),nTam)

/*


ͻ
Programa  fBuscaFuncAutor  R.H. - Natie         Data   16/03/04   
͹
Desc.      Carrega Funcoes de acordo com  a data de referencia        
                                                                      
                                                                      
͹
Uso        AP                                                         
ͼ

*/
Function fBuscaFunc( dDataDe, cCodFunc, cDescFunc, cCatFunc, lCpo )

Local aSR7 := SR7->( GetArea() )
Default cCatFunc 	:= ""
Default lCpo 		:= .T.

SR7->(dbsetorder(2)) //Utiliar o indice 2 para tratamento correto da sequencia quando ha registros na mesma data

If SR7->( dbSeek( SRA->(RA_FILIAL + RA_MAT) )  )
	While SR7->(!EOF()) .and. MesAno(SR7->R7_DATA)	<= MesAno(dDataDe) .AND.;
								SR7->R7_FILIAL 	== SRA->RA_FILIAL .AND. ;
								SR7->R7_MAT   	== SRA->RA_MAT
		cCodFunc 	:=	SR7->R7_FUNCAO
		cCatFunc	:=  SR7->R7_CATFUNC
		If !empty(SR7->R7_DESCFUN)
			cDescFunc 	:= 	SR7->R7_DESCFUN									 //-- 20 Bytes 
		Else
			cDescFunc 	:= 	fDesc( "SRJ", SR7->R7_FUNCAO, "RJ_DESC" )
		EndIf
		If lCpo
			cCodFunc += space(1)
		EndIf
		SR7->(dbSkip())
	EndDo
Else 
	cCodFunc 	:=	SRA->RA_CODFUNC
	cDescFunc	:=  LEFT(fDesc("SRJ",SRA->RA_CODFUNC,"RJ_DESC" ,,SRA->RA_FILIAL),30)
	cCatFunc	:= 	SRA->RA_CATFUNC
	
	If lCpo
		cCodFunc += space(1)
	EndIf
		
EndIf    
RestArea(aSR7)	
Return 

/*/


Ŀ
Funcao    fGetCargo	  Autor  Emerson Grassi Rocha  Data  27/05/04 
Ĵ
Descrio Retorna Codigo do Cargo.	                			  	  
Ĵ
Retorno   ExpA1:            										  
			 ExpA2: 													  
Ĵ
ParametrosExpC1: Campo do SX3                                         
ٱ

/*/
Function fGetCargo(cMat,cFilMat)

Local aSaveArea	:= GetArea()
Local aSaveSRA	:= SRA->( GetArea() )
Local cCargo	:= Space(05)

DEFAULT cFilMat := xFilial("SRA")

dbSelectArea("SRA")
If SRA->(IndexOrd()) != 0
	dbSetOrder(1)
	dbSeek(cFilMat+cMat)	
EndIf
If !Eof()
	If !Empty(SRA->RA_CARGO)
		cCargo := SRA->RA_CARGO
	Else           
		cCargo := FDesc("SRJ", SRA->RA_CODFUNC, "RJ_CARGO")
	EndIf
EndIf

RestArea(aSaveSRA)
RestArea(aSaveARea)

Return( cCargo )

/*


ͻ
Programa  GPEXFUN1  Autor  Microsiga            Data   06/30/05   
͹
Desc.      Usado na pesquisa do campo periodo, para preencher os      
            pagamento e roteiro.                                      
͹
Uso        AP                                                         
ͼ


*/
Function PagRot(cPagto,cRot)
Return(&("MV_PAR"+Alltrim(cPagto)):=RCH->RCH_NUMPAG,&("MV_PAR"+Alltrim(cRot)):=RCH->RCH_ROTEIR)

/*


ͻ
Programa  GPEXFUN1  Autor  Microsiga            Data   09/15/05   
͹
Desc.                                                                 
                                                                      
͹
Uso        AP                                                        
ͼ


*/
Function fBuscaCC(dDataRef, cCodCC, cDescCC, uData, lNoRept , lOrigem  ) 
Local aTransf	:= {}
Local nX		:= 0 

fTransfCc( aTransf , uData , lNoRept , lOrigem )   

cCodCC 	:=	SRA->RA_CC
cDescCC	:=  LEFT(fDesc("CTT",SRA->RA_CC,"CTT_DESC01",,SRA->RA_FILIAL),25)

For nX := 1 to Len(aTransf)
	If AnoMes(aTransf[nX,07]) <= AnoMes(dDataRef)
		cCodCC 	:= aTransf[nX,06]
		cDescCC	:= LEFT(fDesc("CTT",cCodCC,"CTT_DESC01",,SRA->RA_FILIAL),25)
	EndIf
Next
Return

/*


ͻ
Programa  GPEXFUN1  Autor  Microsiga            Data   09/15/05   
͹
Desc.                                                                 
                                                                      
͹
Uso        AP                                                        
ͼ


*/
Function fBuscaDepto(dDataRef, cCodDepto, cDescDepto, uData, lNoRept , lOrigem  ) 
Local aTransf	:= {}
Local nX		:= 0 

fTransfDep( aTransf , uData , lNoRept , lOrigem )

cCodDepto 	:=	SRA->RA_DEPTO
cDescDepto	:=  LEFT(fDesc("SQB",SRA->RA_DEPTO,"QB_DESCRIC",,SRA->RA_FILIAL),25)	 

For nX := 1 to Len(aTransf)
	cCodDepto 	:=	If( aTransf[nX,07] <= dDataRef,aTransf[nX,17],cCodDepto)
	cDescDepto	:=	LEFT(fDesc("SQB",cCodDepto,"QB_DESCRIC",,SRA->RA_FILIAL),25)
Next	

Return
/*


ͻ
Funo     fBuscaItem Autor  Christiane Vieira     Data 09/04/2010
͹
Desc.                                                                 
                                                                      
͹
Uso        AP                                                        
ͼ


*/
Function fBuscaItem(dDataRef, cCodItem, cDescItem, uData, lNoRept , lOrigem  ) 
Local aTransf	:= {}
Local nX		:= 0 

fTransfItem( aTransf , uData , lNoRept , lOrigem )   

cCodItem 	:=	SRA->RA_ITEM
cDescItem	:=  LEFT(fDesc("CTD",SRA->RA_ITEM,"CTD_DESC01",,SRA->RA_FILIAL),25)	 

For nX := 1 to Len(aTransf)
	cCodItem 	:=	If( aTransf[nX,07] <= dDataRef,aTransf[nX,21],cCodItem)
	cDescItem	:=	LEFT(fDesc("CTD",cCodItem,"CTD_DESC01",,SRA->RA_FILIAL),25)
Next	

Return


/*


Funo     fBuscaCLVL Autor  Christiane Vieira     Data 09/04/2010
ͻ
͹
Desc.                                                                 
                                                                      
͹
Uso        AP                                                        
ͼ


*/
Function fBuscaCLVL(dDataRef, cCodCLVL, cDescCLVL, uData, lNoRept , lOrigem  ) 
Local aTransf	:= {}
Local nX		:= 0 

fTransfCLVL( aTransf , uData , lNoRept , lOrigem )   

cCodCLVL 	:=	SRA->RA_CLVL
cDescCLVL	:=  LEFT(fDesc("CTH",SRA->RA_CLVL,"CTH_DESC01",,SRA->RA_FILIAL),25)	 

For nX := 1 to Len(aTransf)
	cCodCLVL 	:=	If( aTransf[nX,07] <= dDataRef,aTransf[nX,22],cCodCLVL)
	cDescCLVL	:=	LEFT(fDesc("CTH",cCodCLVL,"CTH_DESC01",,SRA->RA_FILIAL),25)
Next	

Return

/*/


Ŀ
Funo     fBuscaProc Autor  Mauricio Takakura     Data 15/05/2007
Ĵ
Descrio  Retornar o Processo em determinada data na transferencia   
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
ٱ

*/
Function fBuscaProc(dDataRef, cCodProc, cDescProc, uData, lNoRept , lOrigem  ) 
Local aTransf	:= {}
Local nX		:= 0 

fTransfProc( aTransf , uData , lNoRept , lOrigem )

cCodProc	:=	SRA->RA_PROCES
cDescProc	:=  LEFT(fDesc("RCJ",SRA->RA_PROCES,"RCJ_DESCRI",,SRA->RA_FILIAL),25)

For nX := 1 to Len(aTransf)
	cCodProc	:=	If( aTransf[nX,07] <= dDataRef, aTransf[nX,15], cCodProc)
	cDescProc	:=	LEFT(fDesc("RCJ",cCodProc,"RCJ_DESCRI",,SRA->RA_FILIAL),25)
Next	

Return
