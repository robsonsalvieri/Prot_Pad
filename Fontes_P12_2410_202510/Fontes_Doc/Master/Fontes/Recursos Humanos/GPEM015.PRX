#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPEM015.CH"

Static lGPM19RGB

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPEM015  ³ Autor ³ Equipe de RH.              ³ Data   ³03/04/2013³±±
±±³ÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Integracao Folha - Vale Refeicao, Alimentacao e Transporte        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GPEM015(void)                                                     ³±±
±±³ÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Mensal                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³   Data ³   FNC     ³  Motivo da Alteracao                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Cecilia     ³03/04/13³RHU210     ³ Unificacao da Folha. Beneficios.           ³±±
±±³Raquel Hager³17/01/13³M12RH01    ³ Unificacao da Folha. Beneficios Fase 2.    ³±±
±±³Cecilia     ³17/06/13³TPUSJB     ³ Replica do chamado TIKEYZ da versao 11-gera³±±
±±³            ³        ³           ³ cao da verba ID 0052-Provento Vale Transpor³±±
±±³            ³        ³           ³ te.                                        ³±±
±±³Gustavo M.  ³29/07/14³TQD857		 ³Ajuste para não gerar verba em branco caso  ³±±
±±³            ³        ³           ³Id 052 nao esta amarrado a uma verba.       ³±±
±±³Esther V.   ³31/03/15³TS9103     ³Ajuste para nao realizar integracao caso    ³±±
±±³            ³        ³           ³não haja verba de VR cadastradas.           ³±±
±±³Mariana M   ³12/06/15³TSLCVF     ³Ajuste para que gere mensagem no sistema  	 ³±±
±±³            ³        ³           ³quando o ID 0210 não esteja cadastrada p/ 	 ³±±
±±³            ³        ³           ³o calculo de VT 				 			 ³±±
±±³Renan Borges³17/05/16³TVDGZ6     ³Ajuste para proporcionalizar insalubridade  ³±±
±±³            ³        ³           ³corretamente quando a base de calculo for   ³±±
±±³            ³        ³           ³por salário mínimo.                         ³±±
±±³João Balbino³02/06/16³TVHVTP     ³Ajuste para considerar a filail indicada no ³±±
±±³            ³        ³           ³periodo selecionado                         ³±±
±±³Raquel Hager³27/06/16³TVKHMY     ³Ajuste no filtro da tabela SRA de acordo com³±±
±±³            ³        ³           ³o compartilhamento do período válido para   ³±±
±±³            ³        ³           ³integração.                                 ³±±
±±³Renan Borges³05/01/17³MRH-4073   ³Ajuste para quando o funcionário tem Vale   ³±±
±±³            ³        ³           ³Refeição sem desconto, o sistema ao integrar³±±
±±³            ³        ³           ³ o Benefício com a folha não seja gerada na ³±±
±±³            ³        ³           ³RGB a verba de desconto VR (Id 0050) com va-³±±
±±³            ³        ³           ³lor 0,00.                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPEM015()

GPEM019()

Return

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Funcao    ³ fIntegraVT     ³Autor³  Christiane Vieira³ Data ³01/02/2013³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descricao ³Rotina de Processamento.                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³GPEM015                                                     ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function fIntegraVT(cPdEmp, cPdFun, aCodFol, cPro015, cPer015, cPag015, dDataRef)

Local aPerAtual := {}
Local cPerFol	:= cPer015
Local nLctoFunc	:= 0
Local nLctoEmp	:= 0
Local nLctoDif	:= 0
Local nLctoTot  := 0
Local nQtde		:= 0
Local nQtdeAux	:= 0
Local nQtdeTot	:= 0
Local lNovoCalcB:= NovoCalcBEN()
Local cSeq		:= " "

If P_INTPERAT
	fGetPerAtual( @aPerAtual, xFilial("RCH", (cAliasSRA)->RA_FILIAL), (cAliasSRA)->RA_PROCES, Iif((cAliasSRA)->RA_CATFUNC $ "A*P", fGetCalcRot("9"), fGetRotOrdinar()) )
	If !Empty(aPerAtual)
		cPerFol := aPerAtual[1, 1]
	EndIf
EndIf

DbSelectArea("SR0")
DbSetOrder(RetOrder("SR0", "R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO"))
DbSeek((cAliasSRA)->(RA_FILIAL+RA_MAT)+"0")

While SR0->(!Eof() .and. R0_FILIAL+R0_MAT+R0_TPVALE == (cAliasSRA)->(RA_FILIAL+RA_MAT) + "0")

	If lNovoCalcB .and. ( SR0->R0_PERIOD <> cPer015 .OR. SR0->R0_NROPGT < cPag015 )
		SR0->(DbSkip())
		Loop
	EndIf

	If P_LSEPBEN
		nLctoFunc := 0
		nLctoEmp  := 0
		nLctoDif  := 0
		nLctoTot  := 0
		nQtde  	  := 0
	EndIf

	nLctoFunc += SR0->R0_VLRFUNC + SR0->R0_CFUNDF
	nLctoEmp  += SR0->R0_VLREMP + SR0->R0_CEMPDF
	nLctoDif  += SR0->R0_VALDIF
	nLctoTot  := nLctoFunc + nLctoEmp
	nQtdeTot  += SR0->R0_QDIACAL
	
	nQtdeAux := SR0->R0_QDIACAL

	If nQtdeAux > nQtde
		nQtde := nQtdeAux
	EndIf

	If P_LSEPBEN
		cSeq := Soma1(cSeq)
		fRGBVT(cPdEmp, cPdFun, aCodFol, cPro015, cPerFol, cPag015, dDataRef, nLctoFunc, nLctoEmp, nLctoDif, nLctoTot, nQtde, .T., cSeq)
	EndIf

	SR0->(DbSkip())
EndDo

If !P_LSEPBEN
	fRGBVT(cPdEmp, cPdFun, aCodFol, cPro015, cPerFol, cPag015, dDataRef, nLctoFunc, nLctoEmp, nLctoDif, nLctoTot, nQtdeTot)
EndIf

Return( .T. )

/*/{Protheus.doc} fRGBVT
Grava os lançamentos de VT na tabela RGB
@author allyson.mesashi
@since 29/07/2020
@version 1.0
/*/
Static Function fRGBVT( cPdEmp, cPdFun, aCodFol, cPro015, cPer015, cPag015, dDataRef, nLctoFunc, nLctoEmp, nLctoDif, nLctoTot, nQtde, lSepara, cSeqAux )

Local nValLcto		:= 0
Local nX			:= 0
Local cDescPd		:= ""
Local cPdFind		:= ""
Local cSeq			:= " "
Local lGrava		:= .T.

DEFAULT lSepara		:= .F.
DEFAULT lGPM19RGB 	:= ExistBlock("GPM19RGB")

If lSepara
	cSeq		:= cSeqAux
EndIf

DbSelectArea("RGB")
DbSetOrder( RetOrder( "RGB", "RGB_FILIAL+RGB_PROCES+RGB_MAT+RGB_PERIOD+RGB_SEMANA+RGB_ROTEIR+RGB_PD+RGB_CC+RGB_SEQ+DTOS(RGB_DTREF)" ) )

Begin Transaction

	For nX := 0 to 2
		nValLcto := If (nX == 0, nLctoFunc, If (nX == 1, nLctoEmp, nLctoTot ) )
		cPdFind  := If (nX == 0, cPdFun, If (nX == 1, cPdEmp, aCodFol[52,1] ) )
		cDescPd  := fDesc( "SRV" , cPdFind  , "RV_DESC" )

		If !Empty(cPdFind) .And. nValLcto > 0
			lGrava := .T.
			If DbSeek( (cAliasSRA)->(RA_FILIAL) + cPro015 + (cAliasSRA)->RA_MAT + cPer015 + cPag015 + cRotFol +  cPdFind + (cAliasSRA)->RA_CC , .F. )
				If RGB->RGB_DTREF == dDataRef
					If nSobrepor == 1 .And. (!lSepara .Or. RGB->RGB_SEQ == cSeq)
						RGB->( RecLock( "RGB" , .F. ) )
						cSeq := RGB->RGB_SEQ
					ElseIf !lSepara .Or. RGB->RGB_SEQ == cSeq
						aAdd( aLog[Len(aLog)], (cAliasSRA)->RA_FILIAL + "  " + (cAliasSRA)->RA_MAT + "-" + If(lOfusca, Replicate('*',15), (cAliasSRA)->RA_NOME ) + "   " + cPdFind + "   " + cDescPd )
						lGrava := .F.
					ElseIf lSepara .And. RGB->RGB_SEQ != cSeq
						If RetValSrv(cPdFind, (cAliasSRA)->RA_FILIAL,"RV_QTDLANC") >= cSeq
							RGB->( RecLock( "RGB" , .T. ) )
						Else
							aAdd( aLog[Len(aLog)], (cAliasSRA)->RA_FILIAL + "  " + (cAliasSRA)->RA_MAT + "-" + If(lOfusca, Replicate('*',15), (cAliasSRA)->RA_NOME ) + "   " + cPdFind + "   " + cDescPd )
							lGrava := .F.
						EndIf
					EndIf
				Else
					cSeq := " "
					While RGB->(!Eof() .and. RGB_FILIAL+RGB_PROCES+RGB_MAT+RGB_PERIOD+RGB_SEMANA+RGB_ROTEIR+RGB_PD == (cAliasSRA)->(RA_FILIAL) + cPro015 + (cAliasSRA)->RA_MAT + cPer015 + cPag015 + cRotFol +  cPdFind )
						cSeq := Soma1(cSeq)
						RGB->(DbSkip())
					EndDo
					If RetValSrv(cPdFind, (cAliasSRA)->RA_FILIAL,"RV_QTDLANC") >= cSeq
						RGB->( RecLock( "RGB" , .T. ) )
					Else
						aAdd( aLog[Len(aLog)], (cAliasSRA)->RA_FILIAL + "  " + (cAliasSRA)->RA_MAT + "-" + If(lOfusca, Replicate('*',15), (cAliasSRA)->RA_NOME ) + "   " + cPdFind + "   " + cDescPd )
						lGrava := .F.
					EndIf
				EndIf
			Else
				RGB->( RecLock( "RGB" , .T. ) )
			EndIf

			If lGrava
				RGB->RGB_FILIAL	:= 	(cAliasSRA)->RA_FILIAL
				RGB->RGB_MAT 	:= 	(cAliasSRA)->RA_MAT
				RGB->RGB_CC	  	:= 	(cAliasSRA)->RA_CC
				RGB->RGB_PD	  	:= 	cPdFind
				RGB->RGB_SEMANA	:=	cPag015
				RGB->RGB_DTREF 	:=  dDataRef
				RGB->RGB_TIPO1 	:= 	"V"
				RGB->RGB_TIPO2 	:= 	"G"
				RGB->RGB_HORAS 	:=  nQtde
				RGB->RGB_VALOR 	:= 	nValLcto
				RGB->RGB_PARCEL	:= 	0
				RGB->RGB_SEQ	:= 	cSeq
				If lTemItem
					RGB->RGB_ITEM := (cAliasSRA)->RA_ITEM
				EndIf
				If lTemClVl
					RGB->RGB_CLVL := (cAliasSRA)->RA_CLVL
				EndIf
				RGB->RGB_PROCES	:= cPro015
				RGB->RGB_PERIOD	:= cPer015
				RGB->RGB_ROTORI := cRotBen
				RGB->RGB_ROTEIR	:= cRotFol

				If lGPM19RGB
					ExecBlock("GPM19RGB", .F. , .F., { cRotBen, (cAliasSRA)->RA_FILIAL, (cAliasSRA)->RA_MAT, cPer015, cPag015 } )
				EndIf

				MsUnLock()
			EndIf
		EndIf
	Next nX

End Transaction

return

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Funcao    ³ fIntegraVRA    ³Autor³  Leandro Drumond  ³ Data ³11/08/2013³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descricao ³ Rotina de Processamento.                                   ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³ GPEM015                                                    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function fIntegraVRA(cPdEmp, cPdFun, cPro015, cPer015, cPag015, dDataRef, cTipCal, cPdTot, nSobrePor, nAglutina, cSemNumId)

Local aPerAtual := {}
Local cPerFol	:= cPer015
Local lNovoCalcB:= NovoCalcBEN()
Local nCustFunc := 0
Local nCustEmp  := 0
Local nHoras    := 0
Local nHorasAux := 0
Local nHorasTot := 0
Local nValTot	:= 0
Local cSeq		:= " "

Default cPdTot 	:= ""

If FunName() = "GPEM019"
	dDataDe := dDataRef //Quando vem pela rotina de integrações, dDataRef é a data inicial do período
Endif

DEFAULT nAglutina := 0
DEFAULT nSobrePor := 0

If P_INTPERAT
	fGetPerAtual( @aPerAtual, xFilial("RCH", (cAliasSRA)->RA_FILIAL), (cAliasSRA)->RA_PROCES, Iif((cAliasSRA)->RA_CATFUNC $ "A*P", fGetCalcRot("9"), fGetRotOrdinar()) )
	If !Empty(aPerAtual)
		cPerFol := aPerAtual[1, 1]
	EndIf
EndIf

DbSelectArea("SR0")
DbSetOrder(RetOrder("SR0", "R0_FILIAL+R0_MAT+R0_TPVALE+R0_CODIGO"))
DbSeek((cAliasSRA)->(RA_FILIAL+RA_MAT)+cTipCal)

While SR0->(!Eof() .and. R0_FILIAL+R0_MAT+R0_TPVALE == (cAliasSRA)->(RA_FILIAL+RA_MAT) + cTipCal)

	If lNovoCalcB .and. ( SR0->R0_PERIOD <> cPer015 .OR. SR0->R0_NROPGT < cPag015 )
		SR0->(DbSkip())
		Loop
	EndIf

	If P_LSEPBEN
		nCustFunc := 0
		nCustEmp  := 0
		nHoras    := 0
		nValTot	  := 0
	EndIf

	nCustFunc += SR0->R0_VLRFUNC
	nCustEmp  += SR0->R0_VLREMP
	nValTot	  := nCustFunc + nCustEmp
	nHorasTot += SR0->R0_QDIACAL

	nHorasAux := SR0->R0_QDIACAL
	
	If nHorasAux > nHoras
		nHoras := nHorasAux
	EndIf

	If P_LSEPBEN
		cSeq := Soma1(cSeq)
		fRGBVRA(cPdEmp, cPdFun, aCodFol, cPro015, cPerFol, cPag015, dDataRef, nCustFunc, nCustEmp, nHoras, .T., cSeq, cPdTot, nValTot, nSobrePor, nAglutina, cSemNumId)
	EndIf

	SR0->(DbSkip())
EndDo

If !P_LSEPBEN
	fRGBVRA(cPdEmp, cPdFun, aCodFol, cPro015, cPerFol, cPag015, dDataRef, nCustFunc, nCustEmp, nHorasTot, .T., cSeq, cPdTot, nValTot, nSobrePor, nAglutina, cSemNumId)
	cSeq := Soma1(cSeq)
EndIf

Return( .T. )

/*/{Protheus.doc} fRGBVRA
Grava os lançamentos de VR/VA na tabela RGB
@author allyson.mesashi
@since 29/07/2020
@version 1.0
/*/
Static Function fRGBVRA(cPdEmp, cPdFun, aCodFol, cPro015, cPer015, cPag015, dDataRef, nCustFunc, nCustEmp, nHoras, lSepara, cSeqSR0, cPdTot, nValTot, nSobrePor, nAglutina, cSemNumId)

Local cPDFind		:= ""
Local cSeqAux		:= ""
Local lGrava		:= .T.
Local lAtualiza		:= .F.
Local nIndice 		:= RetOrder( "RGB", "RGB_FILIAL+RGB_PROCES+RGB_MAT+RGB_PERIOD+RGB_SEMANA+RGB_ROTEIR+RGB_PD+RGB_CC+RGB_SEQ+DTOS(RGB_DTREF)" )
Local nValCalc		:= 0
Local nX			:= 0
Local nRecRCH		:= 0

DEFAULT lSepara		:= .F.
DEFAULT lGPM19RGB 	:= ExistBlock("GPM19RGB")

If lSepara
	cSeqAux		:= cSeqSR0
EndIf

Begin Transaction

	For nX := 1 to 3
		If !(nCustFunc > 0 .or. nCustEmp > 0) .Or. (nX == 3 .And. Empty(cPdTot))
			Loop
		EndIf

		If nX == 1
			cPDFind  := cPdFun
			nValCalc := nCustFunc	 	// Custo Funcionario
		ElseIf nX == 2
			cPDFind  := cPdEmp
			nValCalc := nCustEmp 		// Custo Empresa
		Else
			cPDFind  := cPdTot
			nValCalc := nValTot 		// Valor Total
		EndIf

		lGrava := .T.

		If nValCalc <= 0
			Loop
		EndIf

		DbSelectArea("RGB")
		DbSetOrder( nIndice )
		If DbSeek( (cAliasSRA)->(RA_FILIAL+RA_PROCES+RA_MAT) + cPer015 + cPag015 + cRotFol + cPdFind + (cAliasSRA)->RA_CC , .F. )
			If RGB->RGB_DTREF == dDataRef
				If nSobrepor == 1 .And. (!lSepara .Or. RGB->RGB_SEQ == cSeqAux)
					RGB->( RecLock( "RGB" , .F. ) )
				ElseIf lSepara .And. RGB->RGB_SEQ != cSeqAux
					If RetValSrv(cPdFind, (cAliasSRA)->RA_FILIAL,"RV_QTDLANC") >= cSeqAux
						RGB->( RecLock( "RGB" , .T. ) )
					Else
						aAdd( aLog[Len(aLog)], (cAliasSRA)->RA_FILIAL + "  " + (cAliasSRA)->RA_MAT + "-" + If(lOfusca, Replicate('*',15), (cAliasSRA)->RA_NOME ) + "   " + cPdFind + "   " + Str( nValCalc, 12, 2) )
						lGrava := .F.
					EndIf
				Else
					If nAglutina == 1
						If (RGB->RGB_VALOR + nValCalc) > 0
							RGB->( RecLock( "RGB" , .F. ) )
							lAtualiza := .T.
						Else
							lGrava := .F.
						EndIf
					Else
						RCH->( dbsetOrder( Retorder( "RCH" , "RCH_FILIAL+RCH_PROCES+RCH_ROTEIR+RCH_PER+RCH_NUMPAG" ) ) )
						RCH->( dbSeek( xFilial("RCH") + cPro015 + fGetRotOrdinar() + cPer015 + cPag015, .F. ) )
						If dDataDe <> RCH->RCH_DTINI .Or. !Empty(cSemNumId) //Vale refeição calculado em mais de um periodo, criar sequencia (Gestão de Serviços)
							While RGB->(!Eof() .and. RGB_FILIAL+RGB_PROCES+RGB_MAT+RGB_PERIOD+RGB_SEMANA+RGB_ROTEIR+RGB_PD+RGB_CC == (cAliasSRA)->(RA_FILIAL+RA_PROCES+RA_MAT) + cPer015 + cPag015 + cRotFol + cPdFind + (cAliasSRA)->RA_CC)
								cSeqAux := Soma1(RGB->RGB_SEQ)
								RGB->(DbSkip())
							EndDo

							RGB->( RecLock( "RGB" , .T. ) )
						Else
							aAdd( aLog[Len(aLog)], (cAliasSRA)->RA_FILIAL + "  " + (cAliasSRA)->RA_MAT + "-" + If(lOfusca, Replicate('*',15), (cAliasSRA)->RA_NOME ) + "   " + cPDFind + "   " + Str( nValCalc, 12, 2) )
							lGrava := .F.
						EndIf
						RCH->(dbGoTo(nRecRCH))
					EndIf
				EndIf
			Else
				cSeqAux := " "
				While RGB->(!Eof() .and. RGB_FILIAL+RGB_PROCES+RGB_MAT+RGB_PERIOD+RGB_SEMANA+RGB_ROTEIR+RGB_PD == (cAliasSRA)->(RA_FILIAL) + cPro015 + (cAliasSRA)->RA_MAT + cPer015 + cPag015 + cRotFol +  cPdFind )
					cSeqAux := Soma1(cSeqAux)
					RGB->(DbSkip())
				EndDo
				If RetValSrv(cPdFind, (cAliasSRA)->RA_FILIAL,"RV_QTDLANC") >= cSeqAux
					RGB->( RecLock( "RGB" , .T. ) )
				Else
					aAdd( aLog[Len(aLog)], (cAliasSRA)->RA_FILIAL + "  " + (cAliasSRA)->RA_MAT + "-" + If(lOfusca, Replicate('*',15), (cAliasSRA)->RA_NOME ) + "   " + cPDFind + "   " + Str( nValCalc, 12, 2) )
					lGrava := .F.
				EndIf
			EndIf
		Else
			RGB->( RecLock( "RGB" , .T. ) )
		EndIf

		If lGrava
			RGB->RGB_FILIAL	:= 	(cAliasSRA)->RA_FILIAL
			RGB->RGB_MAT 	:= 	(cAliasSRA)->RA_MAT
			RGB->RGB_CC		:= 	(cAliasSRA)->RA_CC
			RGB->RGB_PD		:= 	cPDFind
			RGB->RGB_HORAS	:= 	If(lAtualiza,RGB->RGB_HORAS + nHoras, nHoras)
			RGB->RGB_SEMANA :=	cPag015  // So para semanalista
			RGB->RGB_DTREF	:=  dDataRef
			RGB->RGB_TIPO1	:= 	"V"
			RGB->RGB_TIPO2	:= 	"G"
			RGB->RGB_VALOR	:= 	If(lAtualiza,RGB->RGB_VALOR + nValCalc, nValCalc)
			RGB->RGB_PARCEL	:= 	0
			RGB->RGB_SEQ	:= 	cSeqAux
			If !Empty(cSemNumId)
				RGB->RGB_NUMID := cRotBen+cPro015+cPer015+cSemNumId
			EndIf
			If lTemItem
				RGB->RGB_ITEM := (cAliasSRA)->RA_ITEM
			EndIf
			If lTemClVl
				RGB->RGB_CLVL := (cAliasSRA)->RA_CLVL
			EndIf
			RGB->RGB_PROCES := cPro015
			RGB->RGB_PERIOD := cPer015
			RGB->RGB_ROTORI := cRotBen
			RGB->RGB_ROTEIR := cRotFol

			If lGPM19RGB
				ExecBlock("GPM19RGB", .F. , .F., { cRotBen, (cAliasSRA)->RA_FILIAL, (cAliasSRA)->RA_MAT, cPer015, cPag015 } )
			EndIf

			MsUnLock()
		EndIf

	Next nX

End Transaction

Return

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Funcao    ³ GpM015Per      ³Autor³  Leandro Drumond  ³ Data ³11/08/2013³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descricao ³ Rotina de Processamento.                                   ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³ nTipo - 1=Periodo e 2=Numero Pagamento  					³
³    	   ³                   cPergPro - processo                      ³
³   	   ³                   cPergPer - Periodo                       ³
³    	   ³                   cPergPag - Numero de Pagamento           ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³ GPEM015                                                    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function Gpm015Per( nTipo, cPergProc, cPergPer, cPergPag)	// 1 - Campo RCH_PER e 2 - RCH_NUMPAG
Local cOldAlias := Alias()
Local cFilRCH	:= xFilial("RCH")
Local cMsg
Local lRet 		:= .T.
Local cPergRot  := ""
Local cRotAuxAut	:= fGetCalcRot("9")
Local cRotAuxFol	:= fGetCalcRot("1")

	cPeriodo := cPergPer

	Begin Sequence

		If ( nTipo == 1 )
			If !( lRet := NaoVazio() )
				Break
			EndIf
		EndIf

		DbSelectArea("RCH")
		If ( nTipo == 1 )
			RCH->( dbsetOrder( Retorder( "RCH" , "RCH_FILIAL+RCH_PROCES+RCH_PER+DTOS(RCH_DTFECH)" ) ) )
			RCH->( dbSeek( cFilRCH + cPergProc + cPergPer + SPACE(8) , .F. ) )
		Else
			RCH->( dbsetOrder( Retorder( "RCH" , "RCH_FILIAL+RCH_PROCES+RCH_PER+RCH_NUMPAG+RCH_ROTEIR" ) ) )
			cPergRot := cRotAuxFol
			If !(RCH->( dbSeek( cFilRCH + cPergProc + cPergPer + cPergPag + cPergRot, .F. ) ))
				cPergRot := cRotAuxAut
				RCH->( dbSeek( cFilRCH + cPergProc + cPergPer + cPergPag + cPergRot, .F. ) )
			EndIf
			While RCH->(!Eof() .and. ((RCH_FILIAL+RCH_PROCES+RCH_PER+RCH_NUMPAG+RCH_ROTEIR)	== (cFilRCH+cPergProc+cPergPer+cPergPag+cPergRot)))
				If Empty(RCH->RCH_DtFech)
					Exit
				EndIf
				RCH->( dbSkip() )
			EndDo
			If RCH->(!Empty(RCH_DTFECH) .and. ((RCH_FILIAL+RCH_PROCES+RCH_PER+RCH_NUMPAG+RCH_ROTEIR)==(cFilRCH+cPergProc+cPergPer+cPergPag+cPergRot)))
				RCH->(DbGoBottom())
				RCH->(dbSkip())
			EndIf
		EndIf
		If RCH->( Eof() )
			lRet := .F.
			If nTipo == 1
				cMsg := STR0016	// "Periodo nao Cadastrado!"
				MsgInfo( cMsg, STR0011)
			Else
				cMsg := STR0017	// "No. Pagamento nao Cadastrado para este Periodo!"
				MsgInfo( cMsg, STR0011)
			EndIf
		EndIf

	End Sequence

	DbSelectArea(cOldAlias)

Return( lRet )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Funcao    ³ GPEM015A       ³Autor³  Leandro Drumond  ³ Data ³27/02/2014³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descricao ³ Efetua integracao dos outros beneficios.                   ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³ GPEM015                                                    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function GPEM015A()

GPEM019()

Return
