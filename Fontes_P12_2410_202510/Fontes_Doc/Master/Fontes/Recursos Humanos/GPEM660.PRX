#INCLUDE 'TOTVS.CH'
#INCLUDE "GPEM660.CH"
#INCLUDE "FWADAPTEREAI.CH"

#DEFINE FILTERSIZE 31000


/*


Ŀ
Funo     GPEM660   Autor  Emerson Rosa de Souza  Data   04/08/01  
Ĵ
Descrio  Cadastro de movimentacao dos Titulos                         
Ĵ
Sintaxe    GPEM660()                                                    
Ĵ
 Uso       Generico                                                     
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.               
Ĵ
Programador  Data      BOPS   Motivo da Alteracao                     
Ĵ
Cecilia H Y  15/05/13 M12RH01unificacao da Folha de Pagamento.        
Wag Mobile  31/10/2014TQQPAGAjuste na exclusao dos titulos p/ validar 
                      o campo correto de filial.                       
Renan Borges03/03/2015TRSYPDAjuste para excluir os titulos gerados pe-
                            lo gesto de pessoal tanto no prprio mdu
                            lo como no mdulo do financeiro tambm.   
Gabriel A.  10/12/2015TTRWNDAjuste para no permitir alterao de     
                            ttulos j integrados com o financeiro    
Esther V.   13/06/2016TVFY37Incluida validacao de acesso do usuario e 
                            alterado metodo para chamada do Browse p/ 
                            FwmBrowse a fim de permanecer com opcao de
                            edicao de filtros apos insercao do filtro 
                            default.									
Renan Borges06/01/16MRH-1219Ajuste para excluir titulos com mesmo nmero
			   				  mas filiais diferentes corretamente.       
Isabel N.08/08/17DRHPAG-4306Ajuste p/filtrar filiais que o usuario no
                            tem acesso caso string seja maior que 2kb.
Gabriel A.  14/08/2017DRHPAGAjuste na rotina fProcExc() a fim de      
                      -4948 excluir resgistros da SQF aps deletar    
                            dados da RC1.                             
ٱ

*/
Function GPEM660()

LOCAL cFiltraRh		:= ""			//Variavel para filtro
Local cValidFil		:= ""			//Variavel para filtro de acesso a filiais

LOCAL nleg
LOCAL lGPLeg 		:= .F.
Local aLegenda  	:= {}

//Private aIndexRC1	:= {}			//Variavel Para Filtro
//Private bFiltraBrw	:= {|| Nil}		//Variavel para Filtro

Private oBrwRC1
Private aRotina		:= MenuDef()	// ajuste para versao 9.12 - chamada da funcao MenuDef() que contem aRotina

//Define o cabecalho da tela de atualizacoes
Private cCadastro  := OemToAnsi(STR0007) //"Movimentacao de Titulos"

//Inicializa o filtro utilizando a funcao FilBrowse
cFiltraRh 	:= CHKRH(FunName(),"RC1","1")
cValidFil := "@RC1_FILTIT in " + FormatIN( fValidFil(), '/' )

cValidFil := IIf(Len(cValidFil) > FILTERSIZE, "@RC1_FILTIT not in ('" + Space( Len(fValidFil(, .T.)) ) + "')", cValidFil)

oBrwRC1 := FWmBrowse():New()
oBrwRC1:SetAlias( 'RC1' )
oBrwRC1:SetDescription(cCadastro)

AADD( ALEGENDA, { "RC1->RC1_INTEGR==' '" , "GRAY"   , '' 				})
AADD( ALEGENDA, { "RC1->RC1_INTEGR=='0'" , "GREEN"	,OemToAnsi(STR0008) })
AADD( ALEGENDA, { "RC1->RC1_INTEGR=='1'" , "RED"	,OemToAnsi(STR0009) })
AADD( ALEGENDA, { "RC1->RC1_INTEGR=='2'" , "YELLOW" ,OemToAnsi(STR0010) })

If ExistBlock("GP660LEG")
	IF ValType( uRet := ExecBlock('GP660LEG',.F.,.F.,aLegenda) ) == "A"
		aLegenda 	:= aClone(uRet)
		lGPLeg := .T.
	Endif
Endif

If lGPLeg
	for nleg := 1 to len(aLegenda)
		oBrwRC1:AddLegend( aLegenda[nleg,1]	, aLegenda[nleg,2]	, aLegenda[nleg,3] )
	next nleg
Else
	oBrwRC1:AddLegend( "RC1->RC1_INTEGR==' '" , "GRAY")
	oBrwRC1:AddLegend( "RC1->RC1_INTEGR=='0'" , "GREEN"	,OemToAnsi(STR0008) )
	oBrwRC1:AddLegend( "RC1->RC1_INTEGR=='1'" , "RED"	,OemToAnsi(STR0009) )
	oBrwRC1:AddLegend( "RC1->RC1_INTEGR=='2'" , "YELLOW",OemToAnsi(STR0010) )
Endif

oBrwRC1:SetFilterDefault(cFiltraRh)
oBrwRC1:AddFilter(OemToAnsi(STR0027), cValidFil,.T.,.T.) //Filtro Padro de Acesso

oBrwRC1:Activate()

Return Nil

/*/


Ŀ
Funcao     fDefCor   Autor  Emerson Rosa de Souza  Data 23.08.2001
Ĵ
Descrio  Funcao p/ definir cores p/ tipos de titulos                
Ĵ
Retorno   aCores                                                      
Ĵ
Parametros								                                
ٱ

/*/
Function fDefCor()
Local aCores :=	{ { "RC1->RC1_INTEGR==' '" , 'BR_CINZA'   },;
				  { "RC1->RC1_INTEGR=='0'" , 'BR_VERDE'   },;
            	  { "RC1->RC1_INTEGR=='1'" , 'BR_VERMELHO'},;
				  { "RC1->RC1_INTEGR=='2'" , 'BR_AMARELO' }}

Return(aCores)

/*/

Ŀ
 Funo   fMostLeg   Autor  Emerson Rosa de Souza  Data  23.08.01 
Ĵ
Descrio  Cria uma janela contendo a legenda da mBrowse              
Ĵ
 Uso       fMostLeg                                                   
ٱ

/*/
Function fMostLeg()

Local acor := {}

aadd(acor, {"BR_VERDE",    OemToAnsi(STR0008)})   //"Titulos Nao Liberados"
aadd(acor, {"BR_VERMELHO", OemToAnsi(STR0009)})   //"Titulos Liberados"
aadd(acor, {"BR_AMARELO",  OemToAnsi(STR0010)})  //"Titulos Inconsistentes")

If ExistBlock("GP660COR")
   	IF ValType( uRet := ExecBlock('GP660COR',.F.,.F.,acor) ) == "A"
		acor 	:= aClone(uRet)
	Endif
Endif

BrwLegenda(cCadastro,"Legenda", acor)

Return .T.

/*

Ŀ
Funo     fFilRC1   Autor  Emerson Rosa de Souza  Data  05.08.01 
Ĵ
Descrio  Permite filtrar o RC1 e apresenta-lo na MarkBrowse         
Ĵ
Parametros 								                                     
Ĵ
 Uso      Generico                                                    
ٱ

*/
Function fFilRC1()
Static cFiltroRet := ""

cFiltroRet := BuildExpr("RC1",,cFiltroRet)

EndFilBrw("RC1",aIndexRC1)
aIndexRC1  := {}
bFiltraBrw := {|| FilBrowse("RC1",@aIndexRC1,@cFiltroRet) }
Eval(bFiltraBrw)
dbGoTop()

Return Nil

/*

Ŀ
Funo     fExcRC1   Autor  Emerson Rosa de Souza  Data  05.08.01 
Ĵ
Descrio  Permite excluir titulos do arquivo RC1                     
Ĵ
Parametros 								                                     
Ĵ
 Uso      Generico                                                    
ٱ

*/
Function fExcRC1()
Local aSays			:= {}
Local aButtons		:= {}

Private lAbortPrint := .F.

nOpca := 0
Pergunte("GPM660",.F.)

AADD(aSays,OemToAnsi(STR0011) )  //"Exclusao de titulos nao liberados, liberados e inconsistentes."

AADD(aButtons, { 5,.T.,{|| Pergunte("GPM660",.T. ) } } )
AADD(aButtons, { 1,.T.,{|o| nOpca := 1,IF(GPM660OK(),FechaBatch(),nOpca:=0) }} )
AADD(aButtons, { 2,.T.,{|o| FechaBatch() }} )

//Ŀ
// Inicializa a gravacao dos lancamentos do SIGAPCO        
//
PcoIniLan("000092")
FormBatch( STR0012, aSays, aButtons ) //"Exclusao de Titulos"

//Ŀ
// Variaveis utilizadas para parametros                         
//
If nOpca == 1
	ProcGpe({|lEnd| fProcExc()},,,.T.)  // Chamada do Processamento
Endif
//Ŀ
// Finaliza a gravacao dos lancamentos do SIGAPCO          
//
PcoFinLan("000092")

Return Nil

/*

Ŀ
Funo     fExcRC1   Autor  Emerson Rosa de Souza  Data  05.08.01 
Ĵ
Descrio  Permite excluir titulos do arquivo RC1                     
Ĵ
Parametros 								                                     
Ĵ
 Uso      Generico                                                    
ٱ

*/
Function fProcExc()
Local cInicio
Local cFim
Local cPrefixo := "GPE"
Local cDelete  := ""
Local cPrefix	  := ""
Local lRetBx	  := .T.
Local lExclui	  := .T.
Local nX		  := 0
Local cArqSE2
local lRotinaEai:=  .F.
Local lMsgHelp	:= .F.
Local aRet		:= {}
Local lRet		:= .T.
Local lFilCent  := RC1->( ColumnPos("RC1_FILCEN") ) > 0
Local cChvAuxE2	:= "" // Campos complementares para posicionar na SE2: E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA
Local oSX1	:= FWSX1Util():New()
Local lNewPerg	:= .F.

Private pRetEAI := .F.

cDel := "D_E_L_E_T_ = '*'"

oSX1:AddGroup("GPM660")
oSX1:SearchGroup()

//Ŀ
// Carregando as Perguntas                                      
//
cFilDe     :=  mv_par01 //Filial De
cFilAte    :=  mv_par02	//Filial Ate
cCodTitDe  :=  mv_par03	//Cod. do Titulo De
cCodTitAte :=  mv_par04 //Cod. do Titulo Ate
cNumTitDe  :=  mv_par05	//Nro do Titulo De
cNumTitAte :=  mv_par06	//Nro do Titulo Ate
dDtEmisDe  :=  mv_par07	//Data Emissao De
dDtEmisAte :=  mv_par08	//Data Emissao Ate
dDtVencDe  :=  mv_par09	//Data Vencimento De
dDtVencAte :=  mv_par10	//Data Vencimento Ate
cTipTit    :=  Str(mv_par11,1) //Tipo de Titulo    1-Nao Liberado, 2-Liberados, 3-Inconsistentes, 4-Todos

lNewPerg := Len(oSX1:GetGroup("GPM660")[2]) >= 23
If lNewPerg
	cTipoDe     :=  PADR( AllTrim(mv_par12), GetSx3Cache("RC1_TIPO", "X3_TAMANHO") ) //Tipo Titulo De
	cTipoAte    :=  PADR( AllTrim(mv_par13), GetSx3Cache("RC1_TIPO", "X3_TAMANHO") ) //Tipo Titulo Ate ?
	cNaturDe	:=  PADR( AllTrim(mv_par14), GetSx3Cache("RC1_NATURE", "X3_TAMANHO") ) //Natureza De ?
	cNaturAte	:=  PADR( AllTrim(mv_par15), GetSx3Cache("RC1_NATURE", "X3_TAMANHO") ) //Natureza Ate ?
	cForneDe	:=  PADR( AllTrim(mv_par16), GetSx3Cache("RC1_FORNEC", "X3_TAMANHO") ) //Fornecedor De ?
	cLojaDe		:=  PADR( AllTrim(mv_par17), GetSx3Cache("RC1_LOJA", "X3_TAMANHO") ) //Loja De ?
	cForneAte	:=  PADR( AllTrim(mv_par18), GetSx3Cache("RC1_FORNEC", "X3_TAMANHO") ) //Fornecedor Ate ?
	cLojaAte	:=  PADR( AllTrim(mv_par19), GetSx3Cache("RC1_LOJA", "X3_TAMANHO") ) //Loja Ate ?
	cPrefixDe	:=  PADR( AllTrim(mv_par20), GetSx3Cache("RC1_PREFIX", "X3_TAMANHO") ) //Prefixo De ?
	cPrefixAte	:=  PADR( AllTrim(mv_par21), GetSx3Cache("RC1_PREFIX", "X3_TAMANHO") ) //Prefixo Ate ?
	cParcDe		:=  PADR( AllTrim(mv_par22), GetSx3Cache("RC1_PARC", "X3_TAMANHO") ) //Parcela De ?
	cParcAte	:=  PADR( AllTrim(mv_par23), GetSx3Cache("RC1_PARC", "X3_TAMANHO") ) //Parcela Ate ?
EndIf

dbSelectArea( "RC1" )

//Ŀ
// Carrega Regua Processamento	                                  
//
GpProcRegua(RC1->(RecCount()))

dbSetOrder(1)
dbSeek(cInicio :=(xFilial("RC1",cFilDe)+cFilDe+cCodTitDe), .T. )
cInicio := "RC1->RC1_FILIAL+RC1->RC1_FILTIT+RC1->RC1_CODTIT"
cFim := xFilial("RC1",cFilAte)+cFilAte+cCodTitAte
lRotinaEai := FWHasEAI("GPEM660",.T.) //VERIFICA SE A INTEGRACAO DE TITULOS COM SISTEMAS EXTERNOS ESTA HABILITADO

While !Eof() .And. ( &cInicio <= cFim )

	lRet := .T.

	IF lAbortPrint
		Break
	EndIF

	//Ŀ
	// Consiste parametrizacao					                        
	//
	If (RC1->RC1_CODTIT < cCodTitDe) .Or. (RC1->RC1_CODTIT > cCodTitAte) .Or.;
	   (RC1->RC1_NUMTIT < cNumTitDe) .Or. (RC1->RC1_NUMTIT > cNumTitAte) .Or.;
 	   (RC1->RC1_VENCTO < dDtVencDe) .Or. (RC1->RC1_VENCTO > dDtVencAte) .Or.;
   	   (RC1->RC1_EMISSA < dDtEmisDe) .Or. (RC1->RC1_EMISSA > dDtEmisAte) .Or.;
  	   (RC1->RC1_INTEGR == "0" .And. cTipTit $ "2*3") .Or.;
   	   (RC1->RC1_INTEGR == "1" .And. cTipTit $ "1*3") .Or.;
   	   (RC1->RC1_INTEGR == "2" .And. cTipTit $ "1*2") .Or.;
	   (lNewPerg .And. (;
	   		( (RC1->RC1_TIPO < cTipoDe)		.Or. (RC1->RC1_TIPO > cTipoAte)) .Or.;
			( (RC1->RC1_NATURE < cNaturDe)	.Or. (RC1->RC1_NATURE > cNaturAte)) .Or.;
			( (RC1->RC1_FORNEC + RC1->RC1_LOJA < cForneDe + cLojaDe) .Or. (RC1->RC1_FORNEC + RC1->RC1_LOJA > cForneAte + cLojaAte)) .Or.;
			( (RC1->RC1_PREFIX < cPrefixDe)	.Or. (RC1->RC1_PREFIX > cPrefixAte)) .Or.;
			( (RC1->RC1_PARC < cParcDe)		.Or. (RC1->RC1_PARC > cParcAte)) ;
	   )) 
   	   dbSelectArea( "RC1" )
   	   RC1->(dbSkip())
   	   Loop
	EndIf

	//Consiste acesso do usuario a filiais
	If ! ( RC1->RC1_FILTIT $ fValidFil() )
		RC1->(dbSkip())
		Loop
	EndIf

	//Ŀ
	// Movimenta barra de processamento				                  
	//
	GpIncProc(STR0013 + RC1->RC1_FILTIT + " - "  + STR0014 +;         //"Titulo: "
					    RC1->RC1_NUMTIT +  " - "  + RC1->RC1_DESCRI )  //"Filial: "

	cPrefix:= RC1->RC1_PREFIX

	If TCCanOpen(RetSqlname("RJ1"))
		fDelDetRes(RC1->RC1_FILTIT,	RC1->RC1_CODTIT, RC1->RC1_PREFIX, RC1->RC1_NUMTIT)
	EndIf

   //Ŀ
	// Ponto de Entrada na Exclusao do Titulo.                
	//
	IF ExistBlock("GPM060EX")
		Begin Transaction
			If ExecBlock("GPM060EX",.F.,.F.)
				RecLock("RC1",.F.,.F.)
				dbDelete()
				MsUnLock()
				//integracao com modulo SIGAPCO
				PcoDetLan("000092","01","GPEM660", .T.)
			EndIf
		End Transaction
	Else
		//-- Titulos 0-Nao liberados ou 2-Inconsistente nao constam no financeiro apenas no nGPE
		//-- Logo apenas eliminanos da Folha
		If  RC1->RC1_INTEGR $"0.2"
			Begin Transaction
				RecLock("RC1",.F.,.F.)
				dbDelete()
				MsUnLock()

				//integracao com modulo SIGAPCO
				PcoDetLan("000092","01","GPEM660", .T.)
			End Transaction
		Else
            //QUANDO HA INTEGRACAO DE TITULOS COM SISTEMAS EXTERNO, NAO EH GERADO O TITULO NO SIGAFIN
			If !lRotinaEai

				//Verifica se os titulos ja foram integrados e exclui do SIGAFIN
				dbSelectArea("SE2")

				//Criado indice temporario para exclusao apenas pelo prefixo (GPE) + numero do titulo.
				//Na exclusao, nao podemos veriricar a filial, pois ela pode ser diferente entre a RC1 e SE2 (financeiro)
				cArqSE2 := criatrab("",.F.)
				IndRegua("SE2",cArqSE2,"E2_PREFIXO+E2_NUM+E2_FILORIG+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA",,,"Selecionando Registros") //"Selecionando Registros..."
				nIndex 	:= RetIndex("SE2")
				SE2->( dbSetOrder(nIndex+1))
				cChvAuxE2 := PADR( AllTrim(RC1->RC1_PARC), Len(SE2->E2_PARCELA) ) + RC1->RC1_TIPO + RC1->RC1_FORNEC + If(Empty( RC1->RC1_LOJA) , "00", RC1->RC1_LOJA  )

		  		If DbSeek(cPrefix+RC1->RC1_NUMTIT+IIf(lFilCent,RC1->RC1_FILCEN,RC1->RC1_FILTIT) + cChvAuxE2, .F.)
					While !Eof() .And. SE2->E2_FILORIG = IIf(lFilCent,RC1->RC1_FILCEN,RC1->RC1_FILTIT) .AND. SE2->E2_NUM == RC1->RC1_NUMTIT .And. cPrefix == SE2->E2_PREFIXO;
						.And. SE2->(E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA) == cChvAuxE2
						lRetBx	:= .T.
						//Titulo Principal ou Titulo Filho ja baixado no SIGAFIN nao pode ser excluido
						If	( !Empty(E2_BAIXA) .And. ( E2_VALOR # E2_SALDO ) ) .AND. AllTrim(E2_ORIGEM) == 'GPEM670'
							MsgAlert( OemToAnsi( STR0021 +  RC1->RC1_NUMTIT + STR0022), OemToAnsi(STR0017) ) //"O ttulo "##" nao pode ser excluido pois j foi baixado no Mdulo Financeiro" ##"Atencao"
							lRetBx:= .F.
							Exit
						Endif
						dbSkip()
					Enddo

					If lRetBx

						Begin Transaction

							//Exclui o titulo do SE2
							dbSelectArea("SE2")
					 		dbSeek(cPrefix+RC1->RC1_NUMTIT+IIf(lFilCent,RC1->RC1_FILCEN,RC1->RC1_FILTIT) + cChvAuxE2, .F.)
							lExclui	:=	.T.
							While  SE2-> ( !Eof() ) .AND. SE2->E2_FILORIG = IIf(lFilCent,RC1->RC1_FILCEN,RC1->RC1_FILTIT) .And. SE2->E2_NUM = RC1->RC1_NUMTIT .And. cPrefix == SE2->E2_PREFIXO;
								.And. SE2->(E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA) == cChvAuxE2
								//Ŀ
								// Verifica se o titulo nao esta em bordero (Rotina Prov: FINA050)   
								//

								If lExclui .AND. !Empty(SE2->E2_NUMBOR)
									lExclui	:= .F.
								Else
									// Caso seja o titulo principal, verifica se existe titulo de impostos
									// gerado, e confirma se estes estao ou nao em um outro bordero.
									aTitImp := ImpCtaPg()
									For nX := 1 To Len(aTitImp)
										If !Empty(aTitImp[nX][8]) .and. (aTitImp[nX][7] == aTitImp[nX][6])
											lExclui	:= .F.
										Endif
									Next
								EndIf

								If ( !lExclui )
									lMsgHelp	:= .T.
								Else
									RecLock("SE2",.F.,.F.)
									dbDelete()
									MsUnLock()
									SE2->( FKCOMMIT() )
								EndIf
								SE2-> ( dbSkip() )
							Enddo

					 		//Exclui o titulo do GPE
					 		If ( lExclui )
								RecLock("RC1",.F.,.F.)
								dbDelete()
								MsUnLock()
								//integracao com modulo SIGAPCO
								PcoDetLan("000092","01","GPEM660", .T.)
							EndIf

						End Transaction

						If lMsgHelp
							Help("",1,"FA050BORD")
							lMsgHelp := .F.
						EndIf

					Endif
				Endif
			else

				Begin Transaction
	 	  			aRet := FwIntegDef("GPEM660")
					If aRet <> nil .And. !Empty(aRet)
						lRet := aRet[1]
						If !lRet
							Help( ,, OemToAnsi(STR0017),, aRet[2], 1, 0 ) // Ateno
						EndIf
					EndIf
					If lRet
						RecLock("RC1",.F.,.F.)
						If pRetEAI = .T.
							dbDelete()
							MsUnLock()
					 	endif
						//integracao com modulo SIGAPCO
						PcoDetLan("000092","01","GPEM660", .T.)
					EndIf
				End Transaction
			endif
		Endif
	Endif

	//Exclui registros dos valores por Item do titulo presentes na SQF
	If Deleted()//se RC1->(RC1_FILTIT + RC1_NUMTIT) deletado
		SX3->(DbSetOrder(2))
		//ColumnPos - Retirar na 12.1.18
		If SX3->( DbSeek("QF_FILTIT") ) //se campo existe realiza gravacao
			Begin Transaction
				DbSelectArea("SQF")
				If DbSeek(xFilial("SQF",RC1->RC1_FILTIT)+RC1->RC1_FILTIT+RC1->RC1_NUMTIT)
					While SQF->( !Eof() ) .AND. SQF->(QF_FILTIT + QF_NUMTIT) == RC1->(RC1_FILTIT + RC1_NUMTIT)
						RecLock("SQF",.F.,.F.)
							DbDelete()
						SQF->( MsUnLock() )
						SQF->( DbSkip() )
					EndDo
				EndIf
			End Transaction
		EndIf
	EndIf

	dbSelectArea( "RC1" )
	dbSkip()
Enddo

Return Nil

/*/


Ŀ
Funo    GPM670OOk  Autor  Emerson Rosa de Souza  Data  13.08.01 
Ĵ
Descrio  Confirma parametros             				               
Ĵ
Sintaxe    GPM670Ok()    			                                     
Ĵ
Parametros 							                                        
Ĵ
 Uso                                                                  
ٱ

/*/
Static Function GPM660Ok()

Return (MsgYesNo(OemToAnsi(STR0016),OemToAnsi(STR0017))) //"Confirma configurao dos parmetros?"###"Ateno"

/*

Ŀ
Funo     fVerLog   Autor  Emerson Rosa de Souza  Data  05.08.01 
Ĵ
Descrio  Permite a visualizacao do LOG de erros			            
Ĵ
Parametros 								                                     
Ĵ
 Uso      Generico                                                    
ٱ

*/
Function fVerLog()
Local cNomeArq := If(nModulo # 64,"TITULOG","APTILOG")

IF File( __RELDIR + cNomeArq + ".##R" )
	OurSpool( cNomeArq )
Else
	Aviso( OemToAnsi(STR0017), OemToAnsi(STR0023), { "OK" } ) //##"Atencao"##"No h arquivo de Log de Erro para visualizao."
EndIf

Return Nil

/*


Ŀ
Funo    Gpm660Inc  Autor Emerson Grassi Rocha    Data 15/07/05  
Ĵ
Descrio  Inclusao da Manutencao de Titulos.			                  
           						                       		               
Ĵ
Sintaxe    Gpm660Inc(ExpC1, ExpN1, ExpN2)                             
Ĵ
Parametros ExpC1 = Alias do Arquivo.                                  
           ExpC2 = Registro.                                          
           ExpC3 = Opcao Selecionada.                                 
Ĵ
Retorno    Nil                                                        
Ĵ
 Uso       Generico                                                   
ٱ

*/
Function Gpm660Inc(cAlias,nReg,nOpc)
Local aGPM660	:= {}

//Ŀ
// Inicializa a gravacao dos lancamentos do SIGAPCO        
//
PcoIniLan("000092")

// Ponto de entrada, para inclusao de botoes na enchoice.
If ExistBlock("GP660BUI")
	aGPM660 :=  ExecBlock("GP660BUI",.F.,.F.)
Endif

AxInclui(cAlias,nReg,nOpc,,,,"Gpm660Vld(1)",,"Gpm660_PCO()",aGPM660)

//Ŀ
// Finaliza a gravacao dos lancamentos do SIGAPCO          
//
PcoFinLan("000092")

Return Nil

/*


ͻ
Programa  Gpm660_PCOAutor  Microsiga            Data   27/04/07   
͹
Desc.     Funcao utilizada no parametro ctransact da funcao axinclui()
          para integracao com modulo SIGAPCO                          
͹
Uso        AP                                                         
ͼ


*/
Function Gpm660_PCO()

//integracao com modulo SIGAPCO
PcoDetLan("000092","01","GPEM660")

Return

/*


Ŀ
Funo    Gpm660Alt  Autor Emerson Grassi Rocha    Data 15/07/05  
Ĵ
Descrio  Alteracao da Manutencao de Titulos.			               
           								                                     
Ĵ
Sintaxe    Gpm660Alt(ExpC1, ExpN1, ExpN2)                             
Ĵ
Parametros ExpC1 = Alias do Arquivo.                                  
           ExpC2 = Registro.                                          
           ExpC3 = Opcao Selecionada.                                 
Ĵ
Retorno    Nil                                                        
Ĵ
 Uso       Generico                                                   
ٱ

*/
Function Gpm660Alt(cAlias,nReg,nOpc)

	Local aGPM660	:= {}
	Local cParcela	:= IIf(Empty(RC1->RC1_PARC), Space(GetSx3Cache("RC1_PARC", "X3_TAMANHO")), RC1->RC1_PARC)
	Local cChave	:= xFilial("SE2",RC1->RC1_FILTIT) + RC1->RC1_PREFIX + RC1->RC1_NUMTIT + cParcela + RC1->RC1_TIPO + RC1->RC1_FORNEC + RC1->RC1_LOJA

	// Inicializa a gravacao dos lancamentos do SIGAPCO
	PcoIniLan("000092")

	dbSelectArea(cAlias)
	dbGoto(nReg)

	// Ponto de entrada, para inclusao de botoes na enchoice.
	If ExistBlock("GP660BUA")
		aGPM660 :=  ExecBlock("GP660BUA",.F.,.F.)
	Endif

	DbSelectArea("SE2")
	SE2->( DbSetOrder(1)) //E2_FILIAL+E2_PREFIX+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA

	If RC1->RC1_INTEGR == "1"
		If SE2->(DbSeek( cChave, .F.))
			// O ttulo ### j foi integrado com o financeiro. Favor exclu-lo do Contas a Pagar para efetuar a alterao.
			MsgAlert( OemToAnsi( STR0021 +  RC1->RC1_NUMTIT + STR0026), OemToAnsi(STR0017) )
		Else
			AxAltera(cAlias,nReg,nOpc,,,,,"Gpm660Vld(2)","Gpm660_PCO()",,aGPM660)
		EndIf
	Else
		AxAltera(cAlias,nReg,nOpc,,,,,"Gpm660Vld(2)","Gpm660_PCO()",,aGPM660)
	EndIf


	// Finaliza a gravacao dos lancamentos do SIGAPCO
	PcoFinLan("000092")

Return Nil

/*


Ŀ
Funo    Gpm660Vld  Autor  Emerson Grassi Rocha   Data  15/07/05 
Ĵ
Descrio  Validacao na inclusao.                                     
Ĵ
 Uso                                                                  
ٱ

*/
Function Gpm660Vld(nTipo)
Local lRet		:= .T.
DEFAULT nTipo := 1

If nTipo == 1
	If __lSX8
		ConfirmSX8()
	EndIf

	dbSelectArea( "RC1" )
	dbSetOrder(1)

	lRet := ( ExistChav("RC1",M->RC1_FILTIT+M->RC1_CODTIT+M->RC1_PREFIX+M->RC1_NUMTIT ) )
EndIf

If nTipo == 2 
	lRet := fRc1VldTipo()
Endif	 
	
If lRet .And. ExistBlock("GPM60VLD")
	lRet := ExecBlock("GPM60VLD",.F.,.F.,{nTipo})
EndIf

Return(lRet)

/*


Ŀ
Funcao    fRc1VldTipo Autor  Tania Bronzeri        Data 08/03/2006
Ĵ
Descricao  Valida a inclusao do campo RC1_TIPO.                       
Ĵ
 Uso                                                                  
ٱ

*/
Function fRc1VldTipo()
Local cTipo		:=	AllTrim(M->RC1_TIPO)

IF Empty(cTipo) .OR. !ExistCpoRH( "SX5", "05" + cTipo ) 
	Aviso( STR0017, STR0024, { "OK" } )		// "Atencao!"###"Tipo do Titulo Invalido ou Nao Informado. Informe tipo valido."
	Return .F.
EndIF

Return .T.

/*


Ŀ
Funo    Rc1TitIni  Autor  Emerson Grassi Rocha   Data  15/07/05 
Ĵ
Descrio  Inicializador Padrao do campo RC1_NUMTIT.                  
Ĵ
 Uso                                                                  
ٱ

*/
Function Rc1TitIni()
Local lFunc		:= (FunName() != "GPEM660")
Local lProcInc	:= FunName() == "APTA100" .And. ( nOpcao == 3 .Or. nOpcao == 4 )
  //Ŀ
  // RETIRAR TRATAMENTO DO TAMSX3 NA NOVA VERSAO                  
  // RC1_NUMTIT esta diferente do SE2_NUM /Marco/2007             
  // RC1_NUMTIT gerado sequencialmente a partir do RC1 /Maio/2008 
  //
Local nTamNUmTit := TamSX3("RC1_NUMTIT")[1]
Local cRc1NumTit := IIf	( lFunc .And. !lProcInc, space(nTamNUmTit), Right(GetSx8Num("RC1","RC1_NUMTIT",,RetOrdem( "RC1" , "RC1_FILIAL+RC1_NUMTIT" )) ,nTamNUmTit )  )

Return	( cRc1NumTit )

/*
Ŀ
Funo     MenuDef	    Autor  Luiz Gustavo      Data 15/12/2006
Ĵ
Descrio Isola opcoes de menu para que as opcoes da rotina possam    
          ser lidas pelas bibliotecas Framework da Versao 9.12 .      
Ĵ
Sintaxe   < Vide Parametros Formais >			         	
Ĵ
 Uso      GPEM640                                                     
Ĵ
 Retorno  aRotina							
Ĵ
Parametros< Vide Parametros Formais >					
*/

Static Function MenuDef()
Local aRotina := {		{ STR0002, 'PesqBrw'  , 0, 1,,.F.},;	//"Pesquisar"
					 	{ STR0003, 'AxVisual' , 0, 2},;			//"Visualizar"
 					 	{ STR0015, 'fVerLog'  , 0, 2,,.F.},;	//"Log de Erros"
					 	{ STR0018, 'Gpm660Inc', 0, 3},;   		//"Incluir"
					 	{ STR0004, 'Gpm660Alt', 0, 4},;    		//"Alterar"
					 	{ STR0005, 'fExcRC1'  , 0, 1,,.F.},;	//"Excluir"
		             	{ STR0006, 'fMostLeg' , 0, 5,,.F.} }	//"Legenda"

// Ponto de entrada p/ inclusao de novas opcoes em aRotina
If ExistBlock("GP660ROT")
	aRotina := ExecBlock( "GP660ROT" ,.F.,.F.,{aRotina}, .F. )
Endif

Return aRotina

/*


ͻ
Programa  IntegDef  Autor   Edna Dalfovo   Data   20/02/12          
͹
Descricao  Mensagem nica						    
͹
Uso        Mensagem nica                                               
ͼ


*/
Static Function IntegDef(cXML, nTypeTrans, cTypeMessage)

Local aRet

// Chama a rotina que trata a parte principal
aRet :=  GPEN660( cXML, nTypeTrans, cTypeMessage )

Return aRet


/*/{Protheus.doc} fDelDetRes
// Exclui os registros de detalhe do titulo de Resciso na tabela RJ1
@author paulo.inzonha
@since 18/01/2019
@version 1.0
@return lRet, Retorna .T. se executou corretamente
@param cFilTit, characters, Filial do titulo
@param cCodTit, characters, Codigo do Titulo
@param cPrefixo, characters, Prefixo do Titulo
@param cNumTit, characters, Numero do Titulo
@type function
/*/
Static Function fDelDetRes(cFilTit,cCodTit, cPrefixo, cNumTit)
Local lRet := .T.
Local nSzPref	:= TamSX3( "RJ1_PREFIX" )[1]

dbSelectArea("RJ1")
RJ1->(dbSetOrder(1))
RJ1->(dbSeek(cFilTit+cCodTit+PadR(cPrefixo,nSzPref)+cNumTit))
While !RJ1->(Eof()) .And. (	RJ1->RJ1_FILIAL == cFilTit  .AND. ;
 							RJ1->RJ1_CODTIT == cCodTit  .AND. ;
 							RJ1->RJ1_PREFIX == PadR(cPrefixo,nSzPref) .AND. ;
 							RJ1->RJ1_NUMTIT == cNumTit   )

	RecLock("RJ1",.F.)
	RJ1->(dbDelete())
	RJ1->(MsUnLock())


	RJ1->(DbSkip())

Enddo
RJ1->(dbCloseArea())

Return lRet
