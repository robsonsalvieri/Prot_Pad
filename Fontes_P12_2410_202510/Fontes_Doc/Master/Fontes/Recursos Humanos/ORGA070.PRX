#INCLUDE "PROTHEUS.CH"
#INCLUDE "ORGA070.CH"


/*/
зддддддддддбддддддддддбдддддбдддддддддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁORGA070   ЁAutorЁKELLY SOARES                 Ё Data Ё20/09/2006Ё
цддддддддддеддддддддддадддддадддддддддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁGerenciamento de Visoes                                         Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                                        Ё
цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё            ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL               Ё
цддддддддддддбддддддддддбдддддддддддбддддддддддддддддддддддддддддддддддддддд╢
ЁProgramador ЁData      ЁFNC/CHAMADOЁMotivo da Alteracao                    Ё
цддддддддддддеддддддддддедддддддддддеддддддддддддддддддддддддддддддддддддддд╢
ЁCecilia Car.Ё01/08/2014ЁTQEIB9     ЁIncluido o fonte da 11 para a 12 e     Ё
Ё            Ё          Ё           Ёefetuada a limpeza.                    Ё
ЁRenan BorgesЁ30/12/2014ЁTRDL56     ЁAjuste para mostrar numero de postos   Ё
Ё            Ё          Ё           Ёcongelados e cancelados corretamente.  Ё 
ЁFlavio C    Ё27/07/2015ЁPCDEF-50796ЁAtualizaГЦo de contadores              Ё
юддддддддддддаддддддддддадддддддддддаддддддддддддддддддддддддддддддддддддддды/*/  

/*
╠╠иммммммммммяммммммммммямммммммяммммЁ╠╠
╠╠ЁData Fonte SustentaГЦoЁ ChangeSet Ё╠╠
╠╠ЁддддддддддддаддддддддаддддддддддддЁ╠╠  
╠╠Ё    30/12/2014        Ё  276739   Ё╠╠ 
╠╠иммммммммммяммммммммммямммммммяммммм╠╠
*/
Function ORGA070()
	LOCAL aIndexRDK		:= {}
	
	Private bFiltraBrw	:= {|| Nil}
	Private aRotina		:= MenuDef()
							
	Private cCadastro   := OemToAnsi( STR0004 ) //"Vis■es"
	
	// Variaveis para execucao da funcao Gpea410Mnt()
	Private lIsGpea410	:= .T.
	Private lReduzSize	:= .F.                                            
	Private aAdvSize	:= MsAdvSize()
	// Variaveis para execucao da funcao FCADRBTA10()
	Private lNotOrgm10	:= .F.
	
	cFiltraRh := CHKRH("ORGA070","RDK","1")
	bFiltraBrw 	:= {|| FilBrowse("RDK",@aIndexRDK,@cFiltraRH) }
	Eval(bFiltraBrw)
	
	DbSelectArea("RDK")
	DbGoTop()
	
	DbSelectArea("RD4")
	DbGoTop()
	
	mBrowse(6,1,22,75,"RDK")
	
	EndFilBrw("RDK",aIndexRDK)	
Return

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁOrga070MntЁ Autor ЁMarinaldo de Jesus     Ё Data Ё18/06/2002Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Visoes (Manutencao)								Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁOrga070()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Orga070Mnt( cAlias , nReg , nOpc , lDlgPadSiga , nOpcNewGd )
	Local aArea			:= GetArea()
	Local aSvKeys		:= GetKeys()
	Local nOpcAlt		:= 0
	
	Org070Tree( cAlias , nReg , nOpc )
	
	RestArea( aArea )
	RestKeys( aSvKeys , .T. )	
Return nOpcAlt


/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁOrg070Tree   ЁAutorЁMarinaldo de Jesus    Ё Data Ё21/07/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁOrga070()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Org070Tree( cAlias , nReg , nOpc )
	Local aArea			:= GetArea()
	Local aAdvSize		:= {}
	Local aInfoAdvSize	:= {}
	Local aObjSize		:= {}
	Local aObjSizeTree	:= {}
	Local aObjCoords	:= {}
	Local aDbTreeInfo	:= {}
	Local aMenuPopUp	:= {}
	Local aButtons		:= {}
	Local aComplDesc	:= {}
	Local cFilRDK		:= xFilial("RDK")
	Local cKeyFilter	:= ""
	Local cTipo			:= RDK->RDK_HIERAR
	Local nOrderItem	:= RetOrdem( "RD4" , "RD4_FILIAL+RD4_CODIGO+RD4_ITEM+RD4_TREE" )
	Local nOrderTree	:= RetOrdem( "RD4" , "RD4_FILIAL+RD4_CODIGO+RD4_TREE+RD4_ITEM" )
	Local oFont
	Local oGroup
	Local bTreeChange   := { || Orga70Query() } 
	Local bSet15		:= { || oDlg:End() }
	Local bSet24		:= { || oDlg:End() }
	Local bRD4Permit    := {|| .T.}
	Local cHierarchy    := ""
	
	Private aHeader		:= {}
	Private aCols		:= {}
	Private aDados		:= {}
	Private cRegAtual	:= "SubStr(oTree:GetCargo(),3)"
	Private oGetDados                     
	Private oTree
	Private oNumPostos
	Private oNumOcupados
	Private aChaves     := {}
	Private oDlg   

	aAdd( aComplDesc , "RD4_EMPIDE" )
	aAdd( aComplDesc , "RD4_FILIDE" )
	aAdd( aComplDesc , "RD4_CODIDE" )
	
	RDK->( MsGoto( nReg ) )
	cKeyFilter := RDK->RDK_CODIGO 
	cHierarchy := RDK->RDK_HIERAR
	bRD4Permit := Org060Perm(cFilRDK, cKeyFilter, cHierarchy)
	
	Orga70Header(cTipo)
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define aDbTreeInfo                                      	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aDbTreeInfo :=	{;
	 			    	{;
	 			    		cAlias,;				//01 , 01 -> Alias Mestre do Tree
	 			    		nReg,;					//01 , 02 -> Recno para o Posicionamento do Tree
	 			    		cFilRDK,;				//01 , 03 -> Filial para a Montagem do Tree
	 			    		cKeyFilter,;			//01 , 04 -> Chave para a Montagem do Tree
	 			    		"RDK_DESC",;			//01 , 05 -> Descricao do Tree
	 			    		{|| "NORMAS"},;
	 			    		{|| "NORMAS"};
	 			    	},;
	 			    	{;
	 			    		"RD4",;					//02 , 01 -> Alias Filho do Tree
	 			    		"RD4_ITEM",;			//02 , 02 -> Item
	 			    		nOrderItem,;			//02 , 03 -> Ordem do Item
	 			    		"RD4_TREE",;			//02 , 04 -> Tree (Grupo Superior)
	 			    		nOrderTree,;			//02 , 05 -> Ordem do Tree
	 			    		"(AllTrim(RD4_CODIDE) + ' - ' + AllTrim(RD4_DESC)" + IIF(cHierarchy == "1", ")", " + ' (' + AllTrim(FDesc('SQB', FDesc('RCL', RD4_CODIDE, 'RCL_DEPTO', NIL, RD4_FILIDE, 2), 'QB_DESCRIC',NIL,RD4_FILIDE)) + ')')"),;			//02 , 06 -> Descricao do Item
	 			    		NIL,;					//02 , 07 -> Bloco para o DelOk
	 			    		"RD4SetPdFilter()",;	//02 , 08 -> Funcao Para Setar o Filtro
	 			    		NIL,;					//02 , 09 -> Bloco para Validar o Cut
	 			    		NIL,;					//02 , 10 -> Bloco para Get das Informacoes
	 			    		IIf(cHierarchy == "1", {|| 'BR_BRANCO'}, {|| 'BR_AMARELO'}),;//NIL,;					//02 , 11 -> Campo para Get do Resource1
	 			    		IIf(cHierarchy == "1", {|| 'BR_BRANCO'}, {|| 'BR_AMARELO'}),;//NIL,;					//02 , 12 -> Campo para Get do Resource2
	 			    		"RD4_FILIAL",;			//02 , 13 -> Campo de Filial
	 			    		"RD4_CODIGO",;			//02 , 14 -> Campo de Codigo
	 			    		NIL,;					//02 , 15 -> Bloco para Adicao de Novo Item
	 			    		IIf(cHierarchy == "1", "RD4SQB", "RD4RCL"),;					//02 , 16 -> Alias para a Consulta Padrao
	 			    		bRD4Permit,;             //02 , 17 -> Bloco para verificar a inclusao do node na Tree
	 			    		'BR_VERMELHO',;          //02 , 18 -> Resource 1 do cut
	 			    		'BR_VERMELHO';           //02 , 19 -> Resource 2 do cut
	 			    	};
	 			    }
	
	aAdd( aButtons , { "S4WB011N" , {|| If(cTipo=="1",Orgm10Hist(),Orga30Hist()) } , OemToAnsi(STR0005) , OemToAnsi(STR0005) } )
	
	If ( nOpc = 3 ) .And. ( cTipo == "1" )
		aAdd( aButtons , { "SDUNEW"		, {|| Orgm10Inc()  }, OemToAnsi(STR0009), OemToAnsi(STR0009), {|| .T.} } )
		aAdd( aButtons , { "SDUSETDEL"	, {|| Orga70Pend() }, OemToAnsi(STR0010), OemToAnsi(STR0010), {|| .T.} } )
		aAdd( aButtons , { "ALTERA"		, {|| Gp410Postos()}, OemToAnsi(STR0011), OemToAnsi(STR0011), {|| .T.} } )
	Endif

	aAdvSize := MsAdvSize()
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )
	aObjSizeTree	:= { { aObjSize[1,1] , aObjSize[1,2] , aObjSize[1,3] - 80 , ( aObjSize[1,4 ] ) } }
	
	DEFINE FONT oFont NAME "Arial" SIZE 0,-11 BOLD
	DEFINE MSDIALOG oDlg TITLE OemToAnsi( STR0004 ) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF GetWndDefault() PIXEL
		
		@ aObjSize[1,3] - 80 , aObjSize[1,2] GROUP oGroup TO aObjSize[1,3] - 5, aObjSize[1,4] LABEL ""	OF oDlg PIXEL
		oGroup:oFont:= oFont
		@ aObjSize[1,3] - 75 , aObjSize[1,2] + 5 SAY If(cTipo=="1",STR0025, STR0028)	 SIZE 150,10 OF oDlg PIXEL FONT oFont	// NUMERO DE POSTOS
        
		If (cTipo == "2")
			@ aObjSize[1,3]-75 , aObjSize[1,2] + 260 SAY oNumPostos VAR STR0039 + ": 0" SIZE 120,10 OF oDlg PIXEL 
			@ aObjSize[1,3]-75 , aObjSize[1,2] + 380 SAY oNumOcupados VAR STR0040 + ": 0" SIZE 120,10 OF oDlg PIXEL 			
		EndIf
		
		oGetDados := MsNewGetDados():New(;
										   aObjSize[1,3] - 65,;
			                               aObjSize[1,2],;
			    		                   aObjSize[1,3],;
			                               aObjSize[1,4],;
			                               NIL,;
			                               NIL,;
			                               NIL,;
			                               NIL,;
			                               NIL,;
			                               0,;
			                               99999,;
			                               NIL,;
			                               NIL,;
			                               NIL,;
			                               oDlg,;
			                               aHeader,;
			                               aCols,;
			                              )	
	                                                                                                                  
		oTree := ApdBldTree( aDbTreeInfo , STR0004 , NIL , bTreeChange , NIL , .F. , aObjSizeTree , ; //"Visoes"
		                     NIL , oDlg , .T. ,, aMenuPopUp,,,,,,,,,.F.)	

		oGetDados:aCols := {}
		oGetDados:Hide()
		
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 , NIL , @aButtons ) 

	RestArea(aArea)
Return

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁOrgm10Inc    ЁAutorЁKelly Soares          Ё Data Ё24/10/2006Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁChamada da funcao FCADRBTA10() - Inclusao de movto de depto Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁOrga070()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Orgm10Inc()
	Local lTrocouEmp := .F.
	Local aAlias := {}
	Local nRecno := PosIdent("1", , @lTrocouEmp, @aAlias)
	
	If nRecno > 0
		SQB->(DBGoTo(nRecno))
		
		FCADRBTA10()
		Orga70Query()
	Else
		Aviso("",OemToAnsi(STR0015),{"OK"})
	Endif
	If lTrocouEmp
		Org70TrocaEmp(aAlias, cEmpAnt)
	EndIf
Return

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁGp410Postos  ЁAutorЁKelly Soares          Ё Data Ё25/10/2006Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁChamada da funcao GPEA410MNT() - Atualizacao de postos      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁOrga070()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Gp410Postos()
	Local lTrocouEmp := .F.
	Local aAlias := {}
	Local nRecno := PosIdent("2", .T., @lTrocouEmp, @aAlias)
	
	If nRecno > 0
		Gpea410Mnt( "RCL" , nRecno , 2 )
	Else
		Aviso("",OemToAnsi(STR0015),{"OK"})
	Endif
	If lTrocouEmp
		Org70TrocaEmp(aAlias, cEmpAnt)
	EndIf

Return ( NIL )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁOrgm10Hist   ЁAutorЁKelly Soares          Ё Data Ё25/10/2006Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁChamada da funcao GPEM010MNT() - Historico de deptos        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁOrga070()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Orgm10Hist()
	Local lTrocouEmp := .F.
	Local aAlias := {}
	Local nRecno := PosIdent("1", , @lTrocouEmp, @aAlias)
	Local aAreaRBT:= {}
		
	If nRecno > 0

		aAreaRBT := RBT->( GetArea() )

		dbSelectArea("RBT")	
		dbSetOrder(1)		
		RBT->( dbSeek( xFilial("RBT")+SQB->QB_DEPTO,.F.) )	
	
		ORGM10Oper("RBX",nRecno)     
	Else
		Aviso("",OemToAnsi(STR0015),{"OK"})
	Endif
	
	If Len( aAreaRBT ) > 0
		RestArea( aAreaRBT )
	EndIf
	If lTrocouEmp
		Org70TrocaEmp(aAlias, cEmpAnt)
	EndIf

Return
          
/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁOrga30Hist   ЁAutorЁKelly Soares          Ё Data Ё25/10/2006Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁChamada da funcao GPEA030MNT() - Historico de postos        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁOrga070()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Orga30Hist()
	Local lTrocouEmp := .F.
	Local aAlias := {}
	Local nRecno := PosIdent("2", , @lTrocouEmp, @aAlias)
	
	If nRecno > 0
		Orga030Mnt( "RCL" , nRecno , 2 )
	Else
		Aviso("",OemToAnsi(STR0015),{"OK"})
	Endif	
	If lTrocouEmp
		Org70TrocaEmp(aAlias, cEmpAnt)
	EndIf

Return

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁOrga70Pend   ЁAutorЁKelly Soares          Ё Data Ё30/10/2006Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁExibe movimentacoes pendentes do departamento.              Ё  
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁOrga070()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Orga70Pend()
	Local lTrocouEmp := .F.
	Local aAlias := {}
	Local nRecno := PosIdent("1", , @lTrocouEmp, @aAlias)
	Local cRD4Chave:= RTRIM(RD4->RD4_CHAVE)

	If nRecno > 0
		AxAlM010()
		
		While Len(cRD4Chave) > 0
			While (nPos := Ascan(aDados, { |aBusca| RTRIM(aBusca[10]) == cRD4Chave} ) ) > 0
				ADel(aDados, nPos)
				ASize(aDados, Len(aDados)-1)
			EndDo
			
			cRD4Chave:= SubStr(cRD4Chave, 1, Len(cRD4Chave) - 3)
		EndDo
		
      aDados := {}
		Orga70Query()
	Else
		Aviso("", OemToAnsi(STR0015),{"OK"})
	Endif   
	If lTrocouEmp
		Org70TrocaEmp(aAlias, cEmpAnt)
	EndIf
 	
Return

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁPosIdent     ЁAutorЁKelly Soares          Ё Data Ё25/10/2006Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁPosiciona no identificador de acordo com item da tree       Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁOrga070()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function PosIdent(cTipo, lPostos, lMudouEmp, aAlias)
	Local nRet		:= 0
	Local cAlias	:= ""
	Local cIndice
		
	Default lPostos := .F.
	
	cAlias := If(cTipo=="1" .Or. lPostos, "SQB", "RCL")
	cIndice	:= If(cTipo=="1" .Or. lPostos, "QB_FILIAL+QB_DEPTO","RCL_FILIAL+RCL_POSTO")
	
	aAdd(aAlias , "SQB" )
	
	If cTipo == "1"
		aAdd( aAlias , "RBT" )
	Else 
		aAdd( aAlias , "RCL" )
		aAdd( aAlias , "RBU" )
	Endif
	                    
	DbSelectArea("RD4")
	DbSetOrder(1)
	If DbSeek( xFilial("RD4") + RDK->RDK_CODIGO + &cRegAtual )
		If Empty(RD4->RD4_EMPIDE)
			Return nRet
		Endif
		
		If RD4->RD4_EMPIDE <> cEmpAnt
			lMudouEmp := .T.
			Org70TrocaEmp(aAlias, RD4->RD4_EMPIDE)	
		Endif 
	
		(cAlias)->( DbSetOrder( RetOrder(cAlias, cIndice) ) )
		(cAlias)->( DbGoTop() )	
	
		If (cAlias)->( DbSeek(xFilial(cAlias) + RD4->RD4_CODIDE) )
			nRet := (cAlias)->( Recno() )
		Endif	
	Endif
Return nRet

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁOrg70TrocaEmpЁAutorЁKelly Soares          Ё Data Ё25/10/2006Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁAbre o arquivo correspondente a empresa da visao.           Ё  
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁOrga070()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Org70TrocaEmp(aAlias, cEmp)
	Local nX
		
	For nX := 1 to Len(aAlias)
		If Select(aAlias[nX]) > 0
			(aAlias[nX])->(DbCloseArea())
		EndIf	
	Next nX
	
	If Select("SX2TMP") > 0
		SX2TMP->( DbCloseArea() )
	Endif
	OpenSxs(,,.F.,.F.,cEmp,"SX2TMP","SX2")
	
	If Select("SX2TMP") > 0
	
		For nX := 1 to Len(aAlias)
			ChkFile(aAlias[nX],.T.,,,,,,"SX2TMP") 
		Next nX
		
	Endif
Return
 
/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁOrga70Query  ЁAutorЁKelly Soares          Ё Data Ё07/11/2006Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁPrepara a query referente aos dados do item da visao, que   Ё  
Ё          Ёserao exibidos na getdados abaixo da tree.                  Ё  
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁOrg070Tree	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Orga70Query()
	Local aArea			:= GetArea()
	Local nPos			:= 0
	Local nVagos		:= 0
	Local nOcupados		:= 0
	Local nCancelados   := 0
	Local nCongelados	:= 0 
	Local nTotal 		:= 0
	Local nExiste		:= 1
	Local nRecRCLAnt	:= 0
	Local nRecRCXAnt	:= 0
	Local nRecSRAAnt	:= 0
	Local cFilRDK 		:= RDK->RDK_FILIAL
	Local cCodigo  		:= RDK->RDK_CODIGO
	Local cTipo    		:= RDK->RDK_HIERAR
	Local cItem			:= SubStr(oTree:GetCargo(), 3)
	Local cQuery   		:= ""
	Local cAlias1  		:= GetNextAlias()
	Local cAlias2  		:= GetNextAlias()
	Local cAlias3  		:= GetNextAlias()
	Local lExiste		:= .F.	                                
	Local lHabilita     := Org060MnuItEnable(oTree:GetCargo())
	Local aFiles		:= {"RCL", "SRA", "RCX"}		

	oGetDados:aCols := {}	
	oGetDados:Show()
	oGetDados:Refresh()
	
If ValType(oDlg) == "O"
	If cItem = "000000" .or. !lHabilita
		If cTipo == "2"
			oNumPostos:SetText("")
			oNumOcupados:SetText("")	
		Endif
		
		//Se nЦo tem acesso ao item, desabilitar os botУes "Solicitar", "Pendentes" e "Postos"
		If !lHabilita
			//BotЦo Solicitar
			If (nPos := aScan(oDlg:ACONTROLS, {|x| AllTrim(x:cTitle) == OemToAnsi(STR0009) } )) > 0
				oDlg:aControls[nPos]:lActive := .F.
			EndIf
			
			//BotЦo Pendentes
			If (nPos := aScan(oDlg:ACONTROLS, {|x| AllTrim(x:cTitle) == OemToAnsi(STR0010) } )) > 0
				oDlg:aControls[nPos]:lActive := .F.
			EndIf
			
			//BotЦo Postos
			If (nPos := aScan(oDlg:ACONTROLS, {|x| AllTrim(x:cTitle) == OemToAnsi(STR0011) } )) > 0
				oDlg:aControls[nPos]:lActive := .F.
			EndIf
		EndIf
		oGetDados:Hide()
		oGetDados:Refresh()
		Return
	Else
		//BotЦo Solicitar
		If (nPos := aScan(oDlg:ACONTROLS, {|x| AllTrim(x:cTitle) == OemToAnsi(STR0009) } )) > 0
			oDlg:aControls[nPos]:lActive := .T.
		EndIf
		
		//BotЦo Pendentes
		If (nPos := aScan(oDlg:ACONTROLS, {|x| AllTrim(x:cTitle) == OemToAnsi(STR0010) } )) > 0
			oDlg:aControls[nPos]:lActive := .T.
		EndIf
		
		//BotЦo Postos
		If (nPos := aScan(oDlg:ACONTROLS, {|x| AllTrim(x:cTitle) == OemToAnsi(STR0011) } )) > 0
			oDlg:aControls[nPos]:lActive := .T.
		EndIf
	Endif
EndIf  

	CursorWait()
	
	BEGINSQL ALIAS cAlias1
		SELECT 
			RD4.RD4_EMPIDE, 
			RD4.RD4_FILIDE, 
			RD4.RD4_CODIDE, 
			RD4.RD4_DESC, 
			RD4.RD4_CHAVE 
		FROM 
			%table:RD4% RD4 
		WHERE 
			RD4.RD4_FILIAL = %exp:cFilRDK% AND 
			RD4.RD4_CODIGO = %exp:cCodigo% AND 
			RD4.RD4_EMPIDE <> %exp:'  '% AND 
			RD4.RD4_ITEM = %exp:cItem% AND 
			RD4.%notDel%
	ENDSQL
	
	If (cAlias1)->(Bof()) .And. (cAlias1)->(Eof())
		oGetDados:Refresh()
		(cAlias1)->( DbCloseArea() )
		RestArea(aArea)
		CursorArrow()
		Return
	Endif
	
	If ( nPos := Ascan( aDados , { |x| x[1] == OemToAnsi(STR0026) .AND.;
										x[7] == (cAlias1)->RD4_EMPIDE .AND.;
	                                    x[8] == (cAlias1)->RD4_FILIDE .AND.;
	                                    x[9] == (cAlias1)->RD4_CODIDE } ) ) > 0
		aAdd( oGetDados:aCols , Array(8) )
		oGetDados:aCols[nExiste][1] := aDados[nPos][1]
		oGetDados:aCols[nExiste][2] := aDados[nPos][2]
		oGetDados:aCols[nExiste][3] := aDados[nPos][3]
		oGetDados:aCols[nExiste][4] := aDados[nPos][4]
		oGetDados:aCols[nExiste][5] := aDados[nPos][5]
		oGetDados:aCols[nExiste][6] := aDados[nPos][6]
		oGetDados:aCols[nExiste][7] := ""
		oGetDados:aCols[nExiste][8] := .F.
		nExiste++
		lExiste := .T.
	Endif
	
	If ( nPos := Ascan( aDados , { |x| x[1]+x[7]+x[8]+x[9] == OemToAnsi(STR0027) + ;
	                                                (cAlias1)->RD4_EMPIDE + ;
	                                                (cAlias1)->RD4_FILIDE + ;
	                                                (cAlias1)->RD4_CODIDE } ) ) > 0
		aAdd( oGetDados:aCols , Array(8) )
		oGetDados:aCols[nExiste][1] := aDados[nPos][1]
		oGetDados:aCols[nExiste][2] := aDados[nPos][2]
		oGetDados:aCols[nExiste][3] := aDados[nPos][3]
		oGetDados:aCols[nExiste][4] := aDados[nPos][4]
		oGetDados:aCols[nExiste][5] := aDados[nPos][5]
		oGetDados:aCols[nExiste][6] := aDados[nPos][6]
		oGetDados:aCols[nExiste][7] := ""
		oGetDados:aCols[nExiste][8] := .F.
		lExiste := .T.
	Endif
	
	If lExiste
		oGetDados:Refresh()
		(cAlias1)->( DbCloseArea() )
		RestArea(aArea)
		CursorArrow()
		Return
	Endif
	
	/*
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё VISAO ORGANIZACIONAL													Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	If cTipo == "1"		
		cQuery := "SELECT "
		cQuery += "		RCL.RCL_STATUS RCL_STATUS, 
		cQuery += "		SUM(RCL.RCL_NPOSTO) AS RCL_NPOSTO, "
		cQuery += "		SUM(RCL.RCL_OPOSTO) AS RCL_OPOSTO "
		cQuery += "FROM RCL" + (cAlias1)->RD4_EMPIDE + "0 RCL "
		cQuery += "WHERE RCL.RCL_FILIAL = '" + xFilial("RCL", (cAlias1)->RD4_FILIDE) + "' AND "
		cQuery += "      RCL.RCL_DEPTO  = '" + (cAlias1)->RD4_CODIDE + "' AND "
		cQuery += "      RCL.D_E_L_E_T_ = ' ' "
		cQuery += "GROUP BY RCL.RCL_STATUS"
	
		cQuery := ChangeQuery(cQuery)
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias2,.T.,.T.)
		
		While (cAlias2)->( !Eof() )					

			If ( (cAlias2)->RCL_STATUS $ "1/2")
				nVagos += (cAlias2)->RCL_NPOSTO - (cAlias2)->RCL_OPOSTO
				nOcupados += (cAlias2)->RCL_OPOSTO
				nTotal +=  (cAlias2)->RCL_NPOSTO
			ElseIf ( (cAlias2)->RCL_STATUS == "3" )
				nCongelados += (cAlias2)->RCL_NPOSTO
				nTotal +=  (cAlias2)->RCL_NPOSTO
			ElseIf ( (cAlias2)->RCL_STATUS == "4" )
				nCancelados += (cAlias2)->RCL_NPOSTO
				nTotal +=  (cAlias2)->RCL_NPOSTO
			EndIf 
					
		    (cAlias2)->( DbSkip() )
		End
		
		aAdd( oGetDados:aCols , Array(8) )
		oGetDados:aCols[1][1] := OemToAnsi(STR0026)
		oGetDados:aCols[1][2] := nVagos
		oGetDados:aCols[1][3] := nOcupados
		oGetDados:aCols[1][4] := nCancelados
		oGetDados:aCols[1][5] := nCongelados
		oGetDados:aCols[1][6] := nTotal
		oGetDados:aCols[1][7] := ""
		oGetDados:aCols[1][8] := .F.
		
		aAdd(aDados, {	OemToAnsi(STR0026),;
						nVagos,;
						nOcupados,;
						nCancelados,;
						nCongelados,;
						nTotal,;
		                (cAlias1)->RD4_EMPIDE,;
		                (cAlias1)->RD4_FILIDE,;
		                (cAlias1)->RD4_CODIDE,;
		                (cAlias1)->RD4_CHAVE} )
		
		(cAlias2)->( DbCloseArea() )
		
		// QUANTIDADES DO GRUPO
		
/*		cQuery := "SELECT RD4.RD4_EMPIDE, RD4.RD4_FILIDE, RD4.RD4_CODIDE, RD4.RD4_DESC, RD4.RD4_CHAVE "
		cQuery += "FROM " + RetSqlName("RD4") + " RD4 "
		cQuery += "WHERE RD4.RD4_FILIAL = '" + cFilRDK + "' AND "
		cQuery += "      RD4.RD4_CODIGO = '" + cCodigo + "' AND "
		cQuery += "      RD4.RD4_EMPIDE	<> '  ' AND " 
		cQuery += "      RD4.RD4_CHAVE LIKE '" + AllTrim((cAlias1)->RD4_CHAVE) + "%' AND "
		cQuery += "      RD4.D_E_L_E_T_ = ' ' "
		cQuery += "ORDER BY 1, 2, 3"
*/
		cQuery := "SELECT "
		cQuery += "		RCL.RCL_STATUS, "
		cQuery += "		SUM(RCL.RCL_NPOSTO) AS RCL_NPOSTO, "
		cQuery += "		SUM(RCL.RCL_OPOSTO) AS RCL_OPOSTO "
		cQuery += "FROM " + RetSqlName("RCL") + " RCL "
		cQuery += "WHERE RCL.RCL_FILIAL = '" + xFilial("RCL") + "' "
		cQuery += "  AND RCL.RCL_DEPTO IN (	"
		cQuery += "							SELECT RD4_CODIDE "
		cQuery += "							FROM " + RetSqlName("RD4") + " RD4 "
		cQuery += "							WHERE RD4.RD4_FILIAL = '" + xFilial("RD4") + "' "
		cQuery += "							AND RD4.RD4_CODIGO = '" + cCodigo + "' "
		cQuery += "							AND RD4.RD4_CHAVE LIKE '" + AllTrim((cAlias1)->RD4_CHAVE) + "%' "
		cQuery += "							AND RD4.D_E_L_E_T_ = ' ' "
		cQuery += "	 					  ) "
		cQuery += "  AND RCL.D_E_L_E_T_ = ' ' "
		cQuery += "GROUP BY RCL.RCL_STATUS"
	
		cQuery := ChangeQuery(cQuery)
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias3,.T.,.T.)
		
		nVagos		:= 0
		nOcupados	:= 0
		nCancelados := 0
		nCongelados := 0          
		nTotal		:= 0
		
		While (cAlias3)->( !Eof() )					
			If ( (cAlias3)->RCL_STATUS $ "1/2")
				nVagos += (cAlias3)->RCL_NPOSTO - (cAlias3)->RCL_OPOSTO
				nOcupados += (cAlias3)->RCL_OPOSTO
				nTotal +=  (cAlias3)->RCL_NPOSTO
			ElseIf ( (cAlias3)->RCL_STATUS == "3" )
				nCongelados += (cAlias3)->RCL_NPOSTO
				nTotal +=  (cAlias3)->RCL_NPOSTO
			ElseIf ( (cAlias3)->RCL_STATUS == "4" )
				nCancelados += (cAlias3)->RCL_NPOSTO
				nTotal +=  (cAlias3)->RCL_NPOSTO
			EndIf 
					
		    (cAlias3)->( DbSkip() )
		End
		
		aAdd( oGetDados:aCols , Array(8) )
		oGetDados:aCols[2][1] := OemToAnsi(STR0027)
		oGetDados:aCols[2][2] := nVagos
		oGetDados:aCols[2][3] := nOcupados
		oGetDados:aCols[2][4] := nCancelados
		oGetDados:aCols[2][5] := nCongelados
		oGetDados:aCols[2][6] := nTotal
		oGetDados:aCols[2][7] := ""
		oGetDados:aCols[2][8] := .F.
		
		aAdd(aDados, {	OemToAnsi(STR0027),;
						nVagos,;
						nOcupados,;
						nCancelados,;
						nCongelados,;
						nTotal,;
		                (cAlias1)->RD4_EMPIDE,;
		                (cAlias1)->RD4_FILIDE,;
		                (cAlias1)->RD4_CODIDE,;
		                (cAlias1)->RD4_CHAVE} )
		
		(cAlias3)->( DbCloseArea() )
		
	/*
	зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё VISAO DE COMUNICACAO													Ё
	юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
	Else
			
		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Caso a Visao aponte um posto de outra empresa, seta a empresa para 	Ё
		Ё trazer as informacoes corretas do funcionario							Ё		
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		If ( cEmpAnt != (cAlias1)->(RD4_EMPIDE) )
		
			nRecRCLAnt := RCL->(Recno())
			nRecSRAAnt := SRA->(Recno())
			nRecRCXAnt := RCX->(Recno())
		
			Org70TrocaEmp(aFiles, (cAlias1)->(RD4_EMPIDE))
			
			dbSelectArea("RCL")
			dbSelectArea("SRA")
			dbSelectArea("RCX")		
		EndIf
				
		RCL->(DBSetOrder(2))
		
		If RCL->(DBSeek( (cAlias1)->(RD4_FILIDE+RD4_CODIDE) ) )
			oNumPostos:SetText(STR0039 + ": " + AllTrim(Str(RCL->RCL_NPOSTO)))
			oNumOcupados:SetText(STR0040 + ": " + AllTrim(Str(RCL->RCL_OPOSTO)))
		Else
			oNumPostos:SetText("")
			oNumOcupados:SetText("")			
		EndIf
	
		oGetDados:aCols := RCX->(GdMontaCols(	@oGetDados:aHeader,;
												NIL,;
												NIL,;
												NIL,;
												NIL,;			//5
												{"RCX_FILIAL", "RCX_POSTO", "RCX_CODMOV"},;
												NIL,;
												"RCX",;
												xFilial("RCX", (cAlias1)->RD4_FILIDE) + (cAlias1)->RD4_CODIDE,;
												NIL,;			//10
												NIL,;			
												NIL,;			
												NIL,;			
												NIL,;			
												NIL,;			//15
												NIL,;			
												NIL,;			
												NIL,;			
												NIL,;			
												NIL,;			//20
												NIL,;			
												NIL,;			
												NIL,;			
												NIL,;			
												.F.;			//25
												)	)
		
		/*
		зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Caso tenha alterado a Empresa para uso da visao, volta as informacoes Ё
		юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
		If ( cEmpAnt != (cAlias1)->(RD4_EMPIDE) )
		
			Org70TrocaEmp(aFiles, cEmpAnt)
			
			dbSelectArea("SRA")
			dbSelectArea("RCL")
			dbSelectArea("RCX")
				
			RCL->( dbGoto(nRecRCLAnt) )
			SRA->( dbGoto(nRecSRAAnt) )
			RCX->( dbGoTo(nRecRCXAnt) )	
			
		EndIf
	Endif			
	
	(cAlias1)->( DbCloseArea() )
	
	oGetDados:Refresh()
	
	RestArea(aArea)
	CursorArrow()
	
Return NIL
                   
/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁOrga70Header ЁAutorЁKelly Soares          Ё Data Ё08/11/2006Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁMonta header da getdados referente aos totais de postos.    Ё  
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁOrga070Query                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Orga70Header(cTipo)
	Local aArea      := GetArea()
	Local cUsado     := ""
	
	DbSelectArea("SX3")
	DbSetOrder(2)
	DBSeek("RA_NOME")
	cUsado := SX3->X3_USADO
	   
	If cTipo == "1"
		aAdd( aHeader , Array(17) )
		aHeader[1][1] := OemToAnsi(STR0016)
		aHeader[1][2] := OemToAnsi(STR0016)
		aHeader[1][3] := "@!"
		aHeader[1][4] := 30
		aHeader[1][5] := 0
		aHeader[1][6] := ""
		aHeader[1][7] := cUsado
		aHeader[1][8] := "C"
		
		aAdd( aHeader , Array(17) )
		aHeader[2][1] := OemToAnsi(STR0017)
		aHeader[2][2] := OemToAnsi(STR0017)
		aHeader[2][3] := "99999"
		aHeader[2][4] := 5
		aHeader[2][5] := 0
		aHeader[2][6] := ""
		aHeader[2][7] := cUsado
		aHeader[2][8] := "N"
		
		aAdd( aHeader , Array(17) )
		aHeader[3][1] := OemToAnsi(STR0018)
		aHeader[3][2] := OemToAnsi(STR0018)
		aHeader[3][3] := "99999"
		aHeader[3][4] := 5
		aHeader[3][5] := 0
		aHeader[3][6] := ""
		aHeader[3][7] := cUsado
		aHeader[3][8] := "N"
		
		aAdd( aHeader , Array(17) )
		aHeader[4][1] := OemToAnsi(STR0019)
		aHeader[4][2] := OemToAnsi(STR0019)
		aHeader[4][3] := "99999"
		aHeader[4][4] := 5
		aHeader[4][5] := 0
		aHeader[4][6] := ""
		aHeader[4][7] := cUsado
		aHeader[4][8] := "N"
		
		aAdd( aHeader , Array(17) )
		aHeader[5][1] := OemToAnsi(STR0020)
		aHeader[5][2] := OemToAnsi(STR0020)
		aHeader[5][3] := "99999"
		aHeader[5][4] := 5
		aHeader[5][5] := 0
		aHeader[5][6] := ""
		aHeader[5][7] := cUsado
		aHeader[5][8] := "N"
		
		aAdd( aHeader , Array(17) )
		aHeader[6][1] := OemToAnsi(STR0021)
		aHeader[6][2] := OemToAnsi(STR0021)
		aHeader[6][3] := "99999"
		aHeader[6][4] := 5
		aHeader[6][5] := 0
		aHeader[6][6] := ""
		aHeader[6][7] := cUsado
		aHeader[6][8] := "N"
		
		// COLUNA FANTASMA PARA ALINHAMENTO DOS CAMPOS NA GETDADOS
		aAdd( aHeader , Array(17) )
		aHeader[7][1] := ""
		aHeader[7][2] := ""
		aHeader[7][3] := "@!"
		aHeader[7][4] := 1
		aHeader[7][5] := 0
		aHeader[7][6] := ""
		aHeader[7][7] := cUsado
		aHeader[7][8] := "C"
	Else 
		aHeader:= GdMontaHeader(NIL, NIL, NIL, "RCX", {"RCX_FILIAL", "RCX_POSTO", "RCX_CODMOV"} )	
	EndIf             
	
	RestArea(aArea)
Return

Static Function MenuDef()
	Local aRetorno:=	{;
							{ STR0001 , "AxPesqui"	 , 0 , 1 },;	//"Pesquisar"
							{ STR0002 , "Orga070Mnt" , 0 , 2 },;	//"Visualizar"
							{ STR0003 , "Orga070Mnt" , 0 , 4 };		//"Atualizar"
						}
Return aRetorno
