#INCLUDE  "RHLIBDAT.CH"
#INCLUDE  "PROTHEUS.CH"

/*
зддддддддддбддддддддддддддбддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁInRhLibDatExecЁAutor ЁMarinaldo de Jesus   Ё Data Ё14/05/2003Ё
цддддддддддеддддддддддддддаддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁExecutar Funcoes Dentro de RHLIBDAT                          Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
#Sintaxe   ЁInRhLibDatExec( cExecIn , aFormParam )						 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁuRet                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/
Function InRhLibDatExec( cExecIn , aFormParam )

Local uRet

DEFAULT cExecIn		:= ""
DEFAULT aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= __ExecMacro( cExecIn )
EndIF

Return( uRet )

/*/
зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁMesDia()   Ё Autor Ё Marinaldo de Jesus   Ё Data Ё08/08/2001Ё
цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRetorna o Mes/Dia de Uma data no formato "MMDD"             Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁMesDia( dDate )                  						    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁMes e Dia no Formato "MMDD"                                 Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ dDate = Variavel do Tipo Data                              Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function MesDia( dDate )

Local cMesDia := ""

DEFAULT dDate := Ctod("//")

IF ( ValType( dDate ) == "D" )
	cMesDia := SubStr( AllTrim( Dtos( dDate ) ) , -4  )
EndIF

Return( cMesDia )

/*/
зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁAnoMes	   Ё Autor Ё Marinaldo de Jesus   Ё Data Ё21/01/2003Ё
цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRetorna o Ano/Mes de Uma data no formato "AAAAMM"           Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁAnoMes( dDate )                  						    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁAno e Mes no Formato "AAAAMM"                               Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁdDate = Variavel do Tipo Data                               Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function AnoMes( dDate )

Local cAnoMes	:= ""

DEFAULT dDate	:= Ctod("//")

IF ( ValType( dDate ) == "D" )
	cAnoMes := SubStr( Dtos( dDate ) , 1 , 6 )
EndIF

Return( cAnoMes )

/*/
зддддддддддбддддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁConflictDateЁ Autor ЁMarinaldo de Jesus    Ё Data Ё06/07/2004Ё
цддддддддддеддддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁVerifica se Um Periodo Esta Conflutando com Outro      		 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais >									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais >									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlConflictDate                                          	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                                     Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function ConflictDate( dFirstChk , dLastChk , dFirstDate , dLastDate )

Begin Sequence

	IF (;
			lConflictDate := (;
								( dFirstChk <= dFirstDate );
								.and.;
								( dLastChk >= dLastDate );
							);
		)
		Break
	EndIF

	IF (;
			lConflictDate := (;
								( dFirstChk >= dFirstDate );
								.and.;
								( dLastChk <= dLastDate );
							);
		)
		Break
	EndIF

	IF (;
			lConflictDate := (;
			   					( dFirstChk >= dFirstDate );
			   					.and.;
			   					( dFirstChk <= dLastDate );
			   					.and.;
			   					( dLastChk >= dLastDate );
			   				);
		)
		Break
	EndIF

	IF (;
			lConflictDate := (;
								( dFirstChk <= dFirstDate );
								.and.;
								( dLastChk >= dFirstDate );
								.and.;
								( dLastChk <= dLastDate );
							);
			   	)
		Break
	EndIF

End Sequence

Return( lConflictDate )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁDay2Str			ЁAutorЁMarinaldo de Jesus Ё Data Ё02/04/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna String do Dia de uma Data                           Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Day2Str( uData )
Local cType := ValType( uData )
IF ( cType == "D" )
	Return( StrZero( Day( uData ) , 2 ) )
ElseIF ( cType == "N" )
	Return( StrZero( uData , 2 ) )
ElseIF ( cType == "C" )
	Return( StrZero( Val( uData ) , 2 ) )
EndIF

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁMonth2Str		ЁAutorЁMarinaldo de Jesus Ё Data Ё02/04/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna String do Mes de uma Data                           Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Month2Str( uData )
Local cType := ValType( uData )
IF ( cType == "D" )
	Return( StrZero( Month( uData ) , 2 ) )
ElseIF ( cType == "N" )
	Return( StrZero( uData , 2 ) )
ElseIF ( cType == "C" )
	Return( StrZero( Val( uData ) , 2 ) )
EndIF

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁYear2Str		ЁAutorЁMarinaldo de Jesus Ё Data Ё02/04/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna String do Ano de uma Data                           Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Year2Str( uData )
Local cType := ValType( uData )
IF ( cType == "D" )
	Return( StrZero( Year( uData ) , 4 ) )
ElseIF ( cType == "N" )
	Return( StrZero( uData , 4 ) )
ElseIF ( cType == "C" )
	Return( StrZero( Val( uData ) , 4 ) )
EndIF

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁDaySum			ЁAutorЁMarinaldo de Jesus Ё Data Ё23/04/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁSoma Dias em Uma Data                                       Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function DaySum( dDate , nDays )
Return( ( dDate += nDays ) )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁDaySub			ЁAutorЁMarinaldo de Jesus Ё Data Ё23/04/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁSubtrai Dias em Uma Data                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function DaySub( dDate , nDays )
Return( ( dDate -= nDays ) )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁMonthSum		ЁAutorЁMarinaldo de Jesus Ё Data Ё23/04/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁSoma Meses em Uma Data                                      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function MonthSum( dDate , nMonth )

Local nMonthAux	:= Month( dDate )
Local nDayAux	:= Day( dDate )
Local nYearAux	:= Year( dDate )

Local nYearPlus

nMonthAux += nMonth
IF ( nMonthAux > 12 )
	IF ( ( nMonthAux % 12 ) == 0 )
		nYearPlus	:= ( ( nMonthAux / 12 ) - 1 )
		nYearAux	+= nYearPlus
		//Tratamento quando a soma do mЙs informado em dDate com nMonth seja multiplo de 12. Ex: 36, 48.. e dDate seja mЙs 1
		nMonthAux	:= ( nYearPlus * 12 )
		If nMonthAux > 12
			nMonthAux := 12
		Endif
	Else
		nYearPlus	:= ( nMonthAux / 12 )
		nYearPlus	:= Int( nYearPlus )
		nMonthAux	:= ( nMonthAux - ( nYearPlus * 12 ) )
		nYearAux	+= nYearPlus
		While ( nMonthAux > 12 )
			nMonthAux	-= 12
			++nYearAux
		End While
	EndIF
EndIF
dDate := Ctod( Day2Str( nDayAux ) + "/" + Month2Str( nMonthAux ) + "/" + Year2Str( nYearAux ) , "DDMMYYYY" )
IF Empty( dDate )
	dDate	:= Ctod( Day2Str( 1 ) + "/" + Month2Str( nMonthAux ) + "/" + Year2Str( nYearAux ) , "DDMMYYYY" )
	nDayAux	:= Last_Day( dDate )
	dDate	:= Ctod( Day2Str( nDayAux ) + "/" + Month2Str( nMonthAux ) + "/" + Year2Str( nYearAux ) , "DDMMYYYY" )
EndIF

Return( dDate )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁMonthSub		ЁAutorЁMarinaldo de Jesus Ё Data Ё23/04/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁSubtrai Meses em Uma Data                                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function MonthSub( dDate , nMonth )

Local nMonthAux	:= Month( dDate )
Local nDayAux	:= Day( dDate )
Local nYearAux	:= Year( dDate )

While ( nMonth >= 12 )
	nMonth -= 12
	--nYearAux
End While
nMonthAux -= nMonth
IF ( nMonthAux <= 0 )
	nMonthAux := ( 12 + nMonthAux )
	--nYearAux
EndIF

dDate := Ctod( Day2Str( nDayAux ) + "/" + Month2Str( nMonthAux ) + "/" + Year2Str( nYearAux ) , "DDMMYYYY" )
IF Empty( dDate )
	dDate	:= Ctod( Day2Str( 1 ) + "/" + Month2Str( nMonthAux ) + "/" + Year2Str( nYearAux ) , "DDMMYYYY" )
	nDayAux	:= Last_Day( dDate )
	dDate	:= Ctod( Day2Str( nDayAux ) + "/" + Month2Str( nMonthAux ) + "/" + Year2Str( nYearAux ) , "DDMMYYYY" )
EndIF

Return( dDate )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁYearSum			ЁAutorЁMarinaldo de Jesus Ё Data Ё23/04/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁSoma Anos em Uma Data                                       Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function YearSum( dDate , nYear )

Local nMonthAux	:= Month( dDate )
Local nDayAux	:= Day( dDate )
Local nYearAux	:= Year( dDate )

nYearAux += nYear
dDate := Ctod( Day2Str( nDayAux ) + "/" + Month2Str( nMonthAux ) + "/" + Year2Str( nYearAux ) , "DDMMYYYY" )
IF Empty( dDate )
	dDate 	:= Ctod( Day2Str( 1 ) + "/" + Month2Str( nMonthAux ) + "/" + Year2Str( nYearAux ) , "DDMMYYYY" )
	nDayAux	:= Last_Day( dDate )
	dDate 	:= Ctod( Day2Str( nDayAux ) + "/" + Month2Str( nMonthAux ) + "/" + Year2Str( nYearAux ) , "DDMMYYYY" )
EndIF

Return( dDate )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁYearSub			ЁAutorЁMarinaldo de Jesus Ё Data Ё23/04/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁSubtrai Anos em Uma Data                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function YearSub( dDate , nYear )

Local nMonthAux	:= Month( dDate )
Local nDayAux	:= Day( dDate )
Local nYearAux	:= Year( dDate )

nYearAux -= nYear
dDate := Ctod( Day2Str( nDayAux ) + "/" + Month2Str( nMonthAux ) + "/" + Year2Str( nYearAux ) , "DDMMYYYY" )
IF Empty( dDate )
	dDate 	:= Ctod( Day2Str( 1 ) + "/" + Month2Str( nMonthAux ) + "/" + Year2Str( nYearAux ) , "DDMMYYYY" )
	nDayAux	:= Last_Day( dDate )
	dDate 	:= Ctod( Day2Str( nDayAux ) + "/" + Month2Str( nMonthAux ) + "/" + Year2Str( nYearAux ) , "DDMMYYYY" )
EndIF

Return( dDate )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁDateDiffDay		ЁAutorЁMarinaldo de Jesus Ё Data Ё24/04/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁApura Diferenca em Dias entre duas Datas                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function DateDiffDay( dDate1 , dDate2 )

Local uDayRet

IF ( dDate2 > dDate1 )
	uDayRet	:= dDate2
	dDate2 	:= dDate1
	dDate1 	:= uDayRet
EndIF

uDayRet := ( dDate1 - dDate2 )

Return( uDayRet )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁDateDiffMonth	ЁAutorЁMarinaldo de Jesus Ё Data Ё24/04/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁApura Diferenca em Meses entre duas Datas                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function DateDiffMonth( dDate1 , dDate2 )

Local nMonth1
Local nMonth2

Local uMonthRet

IF ( ValType( dDate1 ) == "C" )
	dDate1 := CToD( dDate1 , "DDMMYYYY" )
EndIF
IF ( ValType( dDate2 ) == "C" )
	dDate2 := CToD( dDate2 , "DDMMYYYY" )
EndIF

IF ( dDate2 > dDate1 )
	uMonthRet	:= dDate2
	dDate2 		:= dDate1
	dDate1 		:= uMonthRet
EndIF

nMonth1 	:= ( ( Year( dDate1 ) * 12 ) + Month( dDate1 ) )
nMonth2 	:= ( ( Year( dDate2 ) * 12 ) + Month( dDate2 ) )
uMonthRet	:= ( nMonth1 - nMonth2 )
IF ( Day( dDate1 ) - Day( dDate2 ) > F_ULTDIA(dDate1) )
	++uMonthRet
EndIF

Return( uMonthRet )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁDateDiffYear	ЁAutorЁMarinaldo de Jesus Ё Data Ё24/04/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁApura Diferenca em Anos entre duas Datas                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function DateDiffYear( dDate1 , dDate2 )

Local nMonth1
Local nMonth2

Local uYearRet

IF ( ValType( dDate1 ) == "C" )
	dDate1 := CToD( dDate1 , "DDMMYYYY" )
EndIF
IF ( ValType( dDate2 ) == "C" )
	dDate2 := CToD( dDate2 , "DDMMYYYY" )
EndIF

IF ( dDate2 > dDate1 )
	uYearRet	:= dDate2
	dDate2 		:= dDate1
	dDate1 		:= uYearRet
EndIF

nMonth1 	:= ( ( Year( dDate1 ) * 12 ) + Month( dDate1 ) )
nMonth2 	:= ( ( Year( dDate2 ) * 12 ) + Month( dDate2 ) )
uYearRet	:= ( ( nMonth1 - nMonth2 ) - 1 )
IF ( Day( dDate1 ) >= Day( dDate2 ) )
	++uYearRet
EndIF
uYearRet	/= 12
uYearRet	:= Int( uYearRet )

Return( uYearRet )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁDateDiffYMD		ЁAutorЁMarinaldo de Jesus Ё Data Ё27/09/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna Array contendo a Diferenca de Anos/Meses/Dias  entreЁ
Ё  		   Ёduas Datas													Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function DateDiffYMD( dDate1 , dDate2 )

Local nYears
Local nMonths
Local nDays

nYears		:= DateDiffYear( @dDate1 , @dDate2 )
nMonths		:= DateDiffMonth( dDate1 , dDate2 )
nMonths		-= ( nYears * 12 )
IF ( AnoMes( dDate1 ) <> AnoMes( dDate2 ) )
	If dDate1 > dDate2
		If Day(dDate1) < Day(dDate2)
			nDays := (Day(dDate1)+ 30) - Day(dDate2)
			nMonths -= 1
			If nMonths < 0
				nMonths := 0
			Endif
		Else
         nDays := Day(dDate1) - Day(dDate2)
		Endif
	Else
		If Day(dDate1) > Day(dDate2)
			nDays := (Day(dDate2)+ 30) - Day(dDate1)
			nMonths -= 1
			If nMonths < 0
				nMonths := 0
			Endif
		Else
         nDays := Day(dDate2) - Day(dDate1)
		Endif
	Endif
Else
	nDays	:= DateDiffDay( dDate1 , dDate2 )
EndIF

Return( { nYears , nMonths , nDays } )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁLast_Day		ЁAutorЁMarinaldo de Jesus Ё Data Ё29/09/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna o Ultimo dia do Mes									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Last_Day( dDate )

Local nMonth
Local nYear

IF ( ValType( dDate ) == "C" )
	dDate := CToD( dDate , "DDMMYYYY" )
EndIF

nMonth	:= ( Month( dDate ) + 1 )
nYear	:= Year( dDate )
IF ( nMonth > 12 )
	nMonth := 1
    ++nYear
EndIF

dDate := CToD( "01/" + Month2Str( nMonth ) + "/" + Year2Str( nYear ) , "DDMMYYYY" )
dDate -= 1

Return( Day( dDate ) )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁFirstDate		ЁAutorЁMarinaldo de Jesus Ё Data Ё02/07/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna a Data do Primeiro dia Conforme Data de Referencia  Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function FirstDate( dDate )
Local cYearMonth	:= AnoMes( dDate )
Local cDate			:= ( "01/" + SubStr( cYearMonth , -2 ) + "/" + SubStr( cYearMonth , 1 , 4 ) )
Return( Ctod( cDate , "DDMMYYYY" ) )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁLastDate		ЁAutorЁMarinaldo de Jesus Ё Data Ё02/07/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna a Data do Ultimo dia Conforme Data de Referencia    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function LastDate( dDate )
Local cDate := ( Day2Str( Last_Day( FirstDate( dDate ) ) ) + "/" + Month2Str( dDate ) + "/" + Year2Str( dDate ) )
Return( Ctod( cDate , "DDMMYYYY" ) )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁFirstYDate		ЁAutorЁMarinaldo de Jesus Ё Data Ё02/07/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna a Data do Primeiro dia Conforme Data de Referencia  Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function FirstYDate( dDate )
Local cDate := ( "01/01/" + Year2Str( dDate ) )
Return( Ctod( cDate , "DDMMYYYY" ) )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁLastYDate		ЁAutorЁMarinaldo de Jesus Ё Data Ё02/07/2004Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna a Data do Ultimo dia do Ano Conforme Data de ReferenЁ
Ё          Ёcia															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function LastYDate( dDate )
Local cDate := ( Day2Str( Last_Day( FirstDate( dDate ) ) ) + "/12/" + Year2Str( dDate ) )
Return( Ctod( cDate , "DDMMYYYY" ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁDiaSemana Ё Autor ЁEquipe Advanced RH     Ё Data Ё09/04/1996Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o Ё Retorna a descricao do dia da semana conforme nTam         Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ёuso       Ё Generico                                                   Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function DiaSemana( dData , nTam , nDia )

Local aDia		:= Array( 7 , 2 )
Local cReturn	:= ""
Local nPosDia	:= 0

DEFAULT nTam 	:= 7

aDia[ 01 , 01 ] := STR0001 ; aDia[ 01 , 02 ] :=  1	// "Domingo"
aDia[ 02 , 01 ] := STR0002 ; aDia[ 02 , 02 ] :=  2	// "Segunda"
aDia[ 03 , 01 ] := STR0003 ; aDia[ 03 , 02 ] :=  3	// "Terca"
aDia[ 04 , 01 ] := STR0004 ; aDia[ 04 , 02 ] :=  4	// "Quarta"
aDia[ 05 , 01 ] := STR0005 ; aDia[ 05 , 02 ] :=  5	// "Quinta"
aDia[ 06 , 01 ] := STR0006 ; aDia[ 06 , 02 ] :=  6	// "Sexta"
aDia[ 07 , 01 ] := STR0007 ; aDia[ 07 , 02 ] :=  7	// "Sabado"

IF Empty( dData )
	IF !Empty( nDia )
		IF ( ( nPosDia := aScan( aDia , { |x| x[ 2 ] == nDia } ) ) > 0 )
			cReturn := Left( aDia[ nPosDia , 01 ] + Space( nTam ) , nTam )
		Else
			cReturn := Space( nTam )
		EndIF
	Else
		cReturn := Space( nTam )
	EndIF
Else
	cReturn := Left( aDia[ Dow( dData ) , 01 ] + Space( nTam ) , nTam )
EndIF

Return( cReturn )


/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    Ё SomaMesAno	    ЁAutorЁ Equipe RH(Adilson)Ё Data Ё17/05/2007Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Somar Mes e Ano de Uma Variavel Tipo Caracter   Ё
Ё          Ёou Tipo Data e Retorna no Formato AAAAMM                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function SomaMesAno( uData )

Local nMes, nAno, cRet
Local lData := If( ValType(uData)=="D",.T.,.F. )

nMes := If( lData,Month( uData ),Val(Right( uData,2 )) )
nAno := If( lData,Year( uData ),Val(Left( uData,4 )) )

nMes++
If nMes == 13
	nMes := 1
	nAno++
EndIf

uData := StrZero(nAno,4) + StrZero(nMes,2)
cRet  := uData

Return( cRet )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    Ё SubMesAno		ЁAutorЁ Equipe RH(Adilson)Ё Data Ё17/05/2007Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Subtrair Mes e Ano de Uma Variavel Tipo CaracterЁ
Ё          Ёno Formato AAAAMM											Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function SubMesAno( uData )

Local nMes, nAno, cRet
Local lData := If( ValType(uData)=="D",.T.,.F. )

nMes := If( lData,Month( uData ),Val(Right( uData,2 )) )
nAno := If( lData,Year( uData ),Val(Left( uData,4 )) )

nMes--
If nMes == 0
	nMes := 12
	nAno--
EndIf

uData := StrZero(nAno,4) + StrZero(nMes,2)
cRet  := uData

Return( cRet )

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁDateWorkDay		ЁAutorЁKelly Soares       Ё Data Ё16/07/2008Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna quantidade de dias uteis entre duas datas.          Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁdDate1 = Data Inicial     									Ё
Ё          ЁdDate2 = Data Final       									Ё
Ё          ЁlSaturday = Considera o sabado como dia util				Ё
Ё          ЁlSunday   = Considera o domingo como dia util				Ё
Ё          ЁlHoliday  = Considera feriado como dia util					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function DateWorkDay( dDate1 , dDate2 , lSaturday , lSunday , lHoliday )

Local nRet		:= 0
Local aHoliday	:=	RetFeriados()

Default lSaturday := .F.
Default lSunday   := .F.
Default lHoliday  := .F.

While dDate1 <= dDate2
	If  ( lSunday .Or. ( !lSunday .And. Dow(dDate1) <> 1 ) ) .And. ;
		( lSaturday .Or. ( !lSaturday .And. Dow(dDate1) <> 7 ) ) .And. ;
		( lHoliday .Or. ( !lHoliday .And. Ascan(aHoliday,DTOS(dDate1)) == 0 ) )
		nRet++
	Endif
	dDate1++
Enddo

Return nRet

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁDateNoWorkDay	ЁAutorЁKelly Soares       Ё Data Ё25/07/2008Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna quantidade de dias nao uteis entre duas datas.      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁdDate1 = Data Inicial     									Ё
Ё          ЁdDate2 = Data Final       									Ё
Ё          ЁlSaturday = Considera o sabado como dia nao util			Ё
Ё          ЁlSunday   = Considera o domingo como dia nao util			Ё
Ё          ЁlHoliday  = Considera feriado como dia nao util				Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function DateNoWorkDay( dDate1 , dDate2 , lSaturday , lSunday , lHoliday )

Local nRet		:= 0
Local aHoliday	:=	RetFeriados()

Default lSaturday := .T.
Default lSunday   := .T.
Default lHoliday  := .T.

While dDate1 <= dDate2
	If  ( lSunday .And. Dow(dDate1) = 1 ) .Or. ;
		( lSaturday .And. Dow(dDate1) = 7 ) .Or. ;
		( lHoliday .And. Ascan(aHoliday,DTOS(dDate1)) > 0 )
		nRet++
	Endif
	dDate1++
Enddo

Return nRet

/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁDaysForMonth 	ЁAutorЁKelly Soares       Ё Data Ё07/11/2008Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna quantidade de dias no mes de acordo com especifica- Ё
Ё          Ёcao desejada.                                               Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁdData = Data de referencia                                  Ё
Ё          ЁnDia  = Dia da semana a ser contado (1-domingo)             Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function DaysForMonth( nDia , dData )

Local nRet		:= 0
Local cMesAno	:= ""
Local dInicio
Local dFim

Default dData := dDataBase

cMesAno := Strzero(Month(dData),2) + "/" + Strzero(Year(dData),4)

dInicio := CTOD("01/" + cMesAno)
dFim    := CTOD(StrZero(f_ultdia(dData),2) + "/" + cMesAno)

While dInicio <= dFim
	If Dow(dInicio) = nDia
		nRet++
	Endif
	dInicio++
Enddo

Return nRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё fVldMesAno Ё Autor Ё Mauricio Takakura   Ё Data Ё 08/09/11 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Validar o lancamento de Mes/Ano ou Ano/Mes                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ cMesAno - Mes/Ano ou Ano/Mes                               Ё╠╠
╠╠Ё          Ё nTipo - 1=Valida MM/AAAA                                   Ё╠╠
╠╠Ё          Ё nTipo - 2=Valida AAAA/MM                                   Ё╠╠
╠╠Ё          Ё nTipo - 3=Valida se data/Periodo final e menor que inicial Ё╠╠
╠╠Ё          Ё nTipo - 4=Valida se data/Periodo inicial e maior que final Ё╠╠
╠╠Ё          Ё cCpoDtComp - 3=Campo tipo data para comparar               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё GENERICO                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function fVldMesAno( cMesAno, nTipo, cCpoDtComp )
Local cMes
Local cAno
Local cMes2
Local cAno2
Local cDtMenor := ""
Local dData
Local dDataMen
Local lRet 	:= .F.
Local nTam	:= 0
Local nPosDt := 0

Default nTipo := 1
Default  cCpoDtComp := ""

If !Empty(AllTrim(cCpoDtComp))
	// OBS: PARA UTILIZAR O 3 PARAMENTRO A FUNCAO DEVE SER CHAMADA DE UMA ESTRUTURA
	// QUE TENHA CRIADA AS VARIAVEIS AHEADER E ACOLS COMO PRIVATE
	If (nPosDt := GdFieldPos(cCpoDtComp, aHeader)) > 0
		cDtMenor := aCols[n, nPosDt]
	EndIf
EndIf

If ((Valtype(cMesAno) == "C") .AND. (Valtype(cDtMenor) == "C"))

	If Empty( cMesAno ) .Or. ( Len(cMesAno) <> Len(AllTrim(cMesAno)) )
		Return( .F. )
	EndIf

	nTam := Len( cMesAno )

	If nTipo == 1
		If nTam == 4
			cMes := StrZero(Val(Substr( cMesAno, 1, 2)), 2)
			cAno := StrZero( Val(Substr( cMesAno, 3, 2 )), 2)
		ElseIf nTam == 5
			cMes := StrZero(Val(Substr( cMesAno, 1, 2)), 2)
			cAno := StrZero( Val(Substr( cMesAno, 4, 2 )), 2)
		ElseIf nTam == 6
			cMes := StrZero(Val(Substr( cMesAno, 1, 2)), 2)
			cAno := StrZero( Val(Substr( cMesAno, 3, 4 )), 4)
		ElseIf nTam == 7
			cMes := StrZero(Val(Substr( cMesAno, 1, 2)), 2)
			cAno := StrZero( Val(Substr( cMesAno, 4, 4 )), 4)
		EndIf
	ElseIf nTipo == 2
		If nTam == 4
			cAno := StrZero(Val(Substr( cMesAno, 1, 2)), 2)
			cMes := StrZero( Val(Substr( cMesAno, 3, 2 )), 2)
		ElseIf nTam == 5
			cAno := StrZero(Val(Substr( cMesAno, 1, 2)), 2)
			cMes := StrZero( Val(Substr( cMesAno, 4, 2 )), 2)
		ElseIf nTam == 6
			cAno := StrZero(Val(Substr( cMesAno, 1, 4)), 4)
			cMes := StrZero( Val(Substr( cMesAno, 5, 2 )), 2)
		ElseIf nTam == 7
			cAno := StrZero(Val(Substr( cMesAno, 1, 4)), 4)
			cMes := StrZero( Val(Substr( cMesAno, 6, 2 )), 2)
		EndIf
	ElseIf (nTipo == 3 .OR. nTipo == 4 ) .AND. !Empty(cDtMenor)
		If nTam == 4
			cAno := StrZero(Val(Substr( cMesAno, 1, 2)), 2)
			cMes := StrZero( Val(Substr( cMesAno, 3, 2 )), 2)
			cAno2 := StrZero(Val(Substr( cDtMenor, 1, 2)), 2)
			cMes2 := StrZero( Val(Substr( cDtMenor, 3, 2 )), 2)
		ElseIf nTam == 5
			cAno := StrZero(Val(Substr( cMesAno, 1, 2)), 2)
			cMes := StrZero( Val(Substr( cMesAno, 4, 2 )), 2)
			cAno2 := StrZero(Val(Substr( cDtMenor, 1, 2)), 2)
			cMes2 := StrZero( Val(Substr( cDtMenor, 4, 2 )), 2)
		ElseIf nTam == 6
			cAno := StrZero(Val(Substr( cMesAno, 1, 4)), 4)
			cMes := StrZero( Val(Substr( cMesAno, 5, 2 )), 2)
			cAno2 := StrZero(Val(Substr( cDtMenor, 1, 4)), 4)
			cMes2 := StrZero( Val(Substr( cDtMenor, 5, 2 )), 2)
		ElseIf nTam == 7
			cAno := StrZero(Val(Substr( cMesAno, 1, 4)), 4)
			cMes := StrZero( Val(Substr( cMesAno, 6, 2 )), 2)
			cAno2 := StrZero(Val(Substr( cDtMenor, 1, 4)), 4)
			cMes2 := StrZero( Val(Substr( cDtMenor, 6, 2 )), 2)
		EndIf

		dData := Ctod( "01/" + cMes + "/" + cAno )
		dDataMen := Ctod( "01/" + cMes2 + "/" + cAno2 )

		If (!Empty(dData) .Or. dData >= Ctod( "01/01/1900" )) .AND. (!Empty(dDataMen) .Or. dDataMen >= Ctod( "01/01/1900" ))

			If nTipo == 3
				If dDataMen > dData
					MsgInfo(OemToAnsi(STR0008) + CRLF + OemToAnsi(STR0009) + AllTrim(aHeader[nPosDt,1]) ) // "AtenГЦo!" ### "Essa Data/PerМodo nЦo pode ser menor do que "
					Return .F.
				EndIf
			ElseIf nTipo == 4
				If dDataMen < dData
					MsgInfo(OemToAnsi(STR0008) + CRLF + OemToAnsi(STR0012) + AllTrim(aHeader[nPosDt,1]) ) // "AtenГЦo!" ### "Essa Data/PerМodo nЦo pode ser maior do que "
					Return .F.
				EndIf
			EndIf

		EndIf
	Else
		If nTam == 4
			cAno := StrZero(Val(Substr( cMesAno, 1, 2)), 2)
			cMes := StrZero( Val(Substr( cMesAno, 3, 2 )), 2)
		ElseIf nTam == 5
			cAno := StrZero(Val(Substr( cMesAno, 1, 2)), 2)
			cMes := StrZero( Val(Substr( cMesAno, 4, 2 )), 2)
		ElseIf nTam == 6
			cAno := StrZero(Val(Substr( cMesAno, 1, 4)), 4)
			cMes := StrZero( Val(Substr( cMesAno, 5, 2 )), 2)
		ElseIf nTam == 7
			cAno := StrZero(Val(Substr( cMesAno, 1, 4)), 4)
			cMes := StrZero( Val(Substr( cMesAno, 6, 2 )), 2)
		EndIf
	EndIf

	dData := Ctod( "01/" + cMes + "/" + cAno )
ElseIf ((Valtype(cMesAno) == "D") .AND. (Valtype(cDtMenor) == "D"))
	dData := cMesAno
	dDataMen := cDtMenor

	If (!Empty(dData) .Or. dData >= Ctod( "01/01/1900" )) .AND. (!Empty(dDataMen) .Or. dDataMen >= Ctod( "01/01/1900" ))
		If nTipo == 3
			If dDataMen > dData
				MsgInfo(OemToAnsi(STR0008) + CRLF + OemToAnsi(STR0009) + AllTrim(aHeader[nPosDt,1]) ) // "AtenГЦo!" ### "Essa Data/PerМodo nЦo pode ser menor do que "
				Return .F.
			EndIf
		ElseIf nTipo == 4
			If dDataMen < dData
				MsgInfo(OemToAnsi(STR0008) + CRLF + OemToAnsi(STR0012) + AllTrim(aHeader[nPosDt,1]) ) // "AtenГЦo!" ### "Essa Data/PerМodo nЦo pode ser maior do que "
				Return .F.
			EndIf
		EndIf
	EndIf
Else
	MsgInfo(OemToAnsi(STR0008) + CRLF + OemToAnsi(STR0014)) // "AtenГЦo!" ### "O(s) formato(s) de DATA(s) ou PERмODO(s) informado(s) nЦo И(sЦo) compatМvel(is) com o(s) esperado(s)."
	Return .F.
EndIf

If !Empty(dData) .Or. dData >= Ctod("01/01/1900")
	lRet := .T.
EndIf

Return lRet


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё fVerifData Ё Autor Ё Mohanad Odeh        Ё Data Ё 12/03/13 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Verificar se o periodo esta encerrado                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё RHLIBDAT                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function fVerifData()
Local lRet 	:= .T.
Local cMesAno	:= aCols[n,GDFieldPos('RCC_CHAVE', aHeader)]
Local cAno := SUBSTR(cMesAno, 3,4)
Local cMes := SUBSTR(cMesAno, 1,2)
Local cFilRCH 	:= xFilial("RCH")
Local aAreaAtu := GetArea()
Local aRCHArea := RCH->( GetArea() )

If Len(Alltrim(cMesAno)) == 6
	DbSelectArea("RCH")
	DbSetOrder(7) // FILIAL+ANO+MES
	If dbSeek(cFilRCH+cAno+cMes)
		While RCH->( !EoF() .And. RCH->RCH_FILIAL+RCH->RCH_ANO+RCH->RCH_MES == cFilRCH+cAno+cMes )
			If Empty(RCH->RCH_DTFECH)
				lRet := .T.
			EndIf
			dbSkip()
		End While
	EndIf

	If !lRet
		AVISO("",OemToAnsi(STR0010),{OemToAnsi(STR0011)} ) //""#"A Data que esta sendo alterada ja esta fechada" ### "Ok"
	EndIf
EndIf

RestArea(aRCHArea)
RestArea(aAreaAtu)

Return(lRet)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё RHPerDat   Ё Autor Ё Mohanad Odeh        Ё Data Ё 11/09/13 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Retorna os periodos entre duas datas                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ DTIni - Data Inicial a comparar                            Ё╠╠
╠╠Ё          Ё DTFim - Data Final a comparar                              Ё╠╠
╠╠Ё          Ё nTipo - 1=Retorna PerМodos no formato MM/AAAA              Ё╠╠
╠╠Ё          Ё nTipo - 2=Retorna PerМodos no formato AAAA/MM              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function RHPerDat(DTIni, DTFim, nTipo)
Local nMIni		:= 0	// MES DA DATA INICIAL
Local nAnoIni		:= 0	// ANO DA DATA INICIAL
Local nQTD			:= 0	// QUANTIDADE DE MESES ENTRE AS DATAS
Local nX			:= 0
Local aPeriodos	:= {}

Default nTipo := 1

nMini 	:= Month(DTIni)
nAnoIni := Year(DTIni)
nQTD	:= DateDiffMonth(DTIni, DTFim)

For nx:=0 To nQTD

	If nTipo == 1
		Aadd(aPeriodos, STRZERO(nMini, 2) + STRZERO(nAnoini, 4))
	Else
		Aadd(aPeriodos, STRZERO(nAnoini, 4) + STRZERO(nMini, 2))
	EndIf
	If nMini == 12 //SE MES FOR DEZEMBRO INCREMENTA ANO E ATRIBUI JANEIRO PARA MES
		nAnoini++
		nMini := 1
	Else
		nMini++
	EndIf

Next nx

Return aPeriodos

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o	 ЁChkValDt  Ё Autor Ё Emerson Campos    	Ё Data Ё 22/04/13 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Valida duas datas informadas, necessario informar o tipo   Ё╠╠
╠╠Ё          Ё  de operacao a ser utilizado                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё ChkValDt(dDt1,dDt2,cTpOp,cHelp)							  Ё╠╠
╠╠Ё			 Ё 															  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ dDt1 - Informe a primeira data  							  Ё╠╠
╠╠Ё          Ё dDt2 - Informe a segunda data 							  Ё╠╠
╠╠Ё          Ё cTpOp- [ == ] dDt01 == dDt02 igual 						  Ё╠╠
╠╠Ё          Ё        [ != ] dDt01 != dDt02 diferente					  Ё╠╠
╠╠Ё          Ё        [ >  ] dDt01 > dDt02 Maior						  Ё╠╠
╠╠Ё          Ё        [ <  ] dDt01 < dDt02 Menor						  Ё╠╠
╠╠Ё          Ё        [ >= ] dDt01 >= dDt02 Maior igual					  Ё╠╠
╠╠Ё          Ё        [ <= ] dDt01 <= dDt02 Menor igual					  Ё╠╠
╠╠Ё          Ё cHelp- Informe o help a ser utilizado, caso contrario 	  Ё╠╠
╠╠Ё          Ё        sera utilizado uma mensagem padrao				  Ё╠╠
╠╠Ё          Ё lHelp- .T. Default, se .F. NЦo apresenta o Help			  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso		 Ё Generico 												  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function ChkValDt(dDt1,dDt2,cTpOp,cHelp,lHelp)
Local cAlias 	:= Alias()
Local lRet		:= .T.
DEFAULT cHelp 	:= "DATAINVALI"
DEFAULT lHelp	:= .T.

If ValType(dDt1) == "D" .AND. ValType(dDt2) == "D" .AND. cTpOp $ "==*!=*>*<*>=*<="
	Do Case
		Case cTpOp	== "=="
			If dDt1 == dDt2
				lRet	:= .F.
			Endif
		Case cTpOp	== "!="
			If dDt1 != dDt2
				lRet	:= .F.
			Endif
		Case cTpOp	== ">"
			If dDt1 > dDt2
				lRet	:= .F.
			Endif
		Case cTpOp	== "<"
			If dDt1 < dDt2
				lRet	:= .F.
			Endif
		Case cTpOp	== ">="
			If dDt1 >= dDt2
				lRet	:= .F.
			Endif
		Case cTpOp	== "<="
			If dDt1 <= dDt2
				lRet	:= .F.
			Endif
	EndCase

	If !lRet .AND. lHelp
		Help( , , 'HELP', , cHelp, 1, 0 )
	EndIf
Else
	If lHelp
		//"Os parametros informados na funГЦo 'ChkValDt', nЦo sЦo compativeis com os esperados."
		Help( , , 'HELP', , OemToAnsi(STR0013), 1, 0 )
		lRet	:= .F.
	EndIf
EndIf

dbSelectArea( cAlias )

Return lRet

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o	 ЁGPEchkData|Autor  Ё Natie Sugahara        Ё Data Ё 30/12/03 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Valida datas                              			      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso		 Ё Generico 												  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
FUNCTION GpeChkData(dDtIni,dDtFim)
Local cAlias := Alias()

If dDtFim < dDtIni
	Help(" ",1,"DATAMENOR")
	Return (.F.)
EndIf

dbSelectArea( cAlias )

Return (.T.)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Funcao    ЁfChkDiaMes╨ Autor ЁEmerson Rosa        ╨ Data Ё  08/06/04   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Desc.     ЁValida o Dia e o Mes digitado.                              ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨ParametrosЁ cDiaMes - Dia e Mes que sera validado                      ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё SIGAGPE                                                    ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function fChkDiaMes(cDiaMes)
Local lRet := .T.
Local cDia := Substr(cDiaMes,1,2)
Local cMes := Substr(cDiaMes,3,2)
Local nDiaValido

If Val( cDia ) <= 0 .Or. Val( cDia ) > 31 .Or. Val( cMes ) <= 0 .Or. Val( cMes ) > 12
	lRet := .F.
Else
	nDiaValido := F_ULTDIA(CTOD("01/"+cMes+"/"+StrZero(Year(dDataBase),4),"DDMMYY"))
	If Val( cDia ) > nDiaValido
		lRet := .F.
	EndIf
EndIf

Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o	 ЁChkDtAd	Ё Autor Ё Marcos A. Stiefano    Ё Data Ё 27.02.96 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Verifica Data Admissao / Data Nascimento					  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso		 Ё Generico 												  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function ChkDtAd(dDtNas,dDtAdm)
Local cAlias	:= Alias()
Local cChv2300	:= ""
Local cStat2300	:= "-1"
Local aInfos	:= {}
Local cTpInsc	:= ""
Local cNrInsc	:= "0"
Local lAdmPubl	:= .F.
Local lMatTSV	:= .F.
Local lRet		:= .T.
Default dDtNas	:= SToD("")

If dDtNas >= dDtAdm
	Help(" ",1,"DATADMINVA")
	dbSelectArea( cAlias )
	Return (.F.)
EndIf

If ( cPaisLoc == "BRA")
	If ( ValType(dDtNas) =="D" .AND. ValType(dDtAdm) == "D")
		If ( DateDiffYear(dDtNas , dDtAdm) < 14 )
			MsgAlert(STR0017) // Funcionario possui menos de 14 anos. Verifique o art. 403 da CLT.
		EndIf
	EndIf

	If ReadVar() == "M->RA_ADMISSA" .And. !Inclui .And. dDtAdm != SRA->RA_ADMISSA .And. SRA->RA_CATEFD $ fCatTrabEFD("TSV")
		lMatTSV := SRA->(ColumnPos("RA_DESCEP")) > 0 .And. SRA->RA_DESCEP == "1"
		cChv2300 := AllTrim( SRA->RA_CIC ) + ";" + If(lMatTSV, SRA->RA_CODUNIC, "") + ";" + SRA->RA_CATEFD + ";" + DTOS( SRA->RA_ADMISSA )
		cStat2300 := TafGetStat( "S-2300", cChv2300 )
		If cStat2300 == "-1" .And. ChkFile("RJ9") .And. ChkFile("RJE")
			If Len( aInfos := fXMLInfos() ) >= 4
				cTpInsc	:= aInfos[1]
				cNrInsc	:= aInfos[2]
				lAdmPubl := aInfos[4]
				cChv2300 := If( lMatTSV, SRA->RA_CODUNIC, AllTrim( SRA->RA_CIC ) + AllTrim( SRA->RA_CATEFD ) + DTOS( SRA->RA_ADMISSA ) )
				cChv2300 := cTpInsc + PADR( Iif( !lAdmPubl .And. cTpInsc == "1", SubStr(cNrInsc, 1, 8), cNrInsc), 14) + "S2300" + Padr(cChv2300, fTamRJEKey(), " ")
				GetInfRJE( 2, cChv2300, @cStat2300 )	//RJE_TPINSC+RJE_INSCR+RJE_EVENTO+RJE_KEY+RJE_INI
			EndIf
		EndIf
		If cStat2300 != "-1"
			Help(,,STR0008,,STR0018,1,0)//"Trabalhador jА integrado ao eSocial. NЦo И possМvel alterar a Data de admissЦo pois faz parte da chave do evento."
			lRet := .F.
		EndIf
	EndIf
EndIf

dbSelectArea( cAlias )

Return(lRet)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o	 ЁMesAno 	Ё Autor Ё R.H				    Ё Data Ё 23.02.95 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Retorna AnoMes de um campo data							  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso		 Ё Generico 												  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function MesAno( dData )
Return( AnoMes( dData ) )

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o	 ЁFDATA6 	Ё Autor Ё J. Ricardo 		    Ё Data Ё 11.07.95 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Transforma um campo data em caracter: ddmmaa 			  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso		 Ё Generico 												  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function FData6(dData)
	cData := StrZero(DAY(dData),2) + StrZero(MONTH(dData),2) + Right(StrZero(Year(dData),4),2)
Return cData

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o	 ЁChkDtNa	Ё Autor Ё Marcos A. Stiefano	Ё Data Ё 29.06.95 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Verifica Data Nascimento / Data Admissao					  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso		 Ё Generico 												  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function ChkDtNa(dDtBas, dDtNas, dDtAdmissao)
	Local cAlias := Alias()
	Default dDtAdmissao:= CTOD("  /  /  ")

	If dDtNas >= dDtBas
		Help(" ",1, "DATINVALID")
		Return (.F.)
	EndIf

	If !Empty(dDtAdmissao)
		If dDtNas >= dDtAdmissao
			Help(" ", 1, "DATINVAL01")
			Return (.F.)
		EndIf

		If YearSum(dDtNas , 14) >= dDtAdmissao
			Help(" ", 1, "DATINVAL02")
			Return (.F.)
		EndIf
	EndIf

	dbSelectArea( cAlias )
Return (.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o	 ЁChkDtOp	Ё Autor Ё Marcos A. Stiefano	Ё Data Ё 29.06.95 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Verifica Data Opcao / Data Admissao 		 				  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso		 Ё Generico 												  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function ChkDtOp(dDtAdm,dDtOpc)
Local cAlias := Alias()

/*Quando essa funГЦo for chamada a partir da ImportaГЦo Logix
deve sempre retornar verdadeiro(.T.)*/
if(IsInCallStack("RHIMP01"))
	Return(.T.)
endIf

If dDtOpc < dDtAdm
	Help(" ",1, "OPCINVALID")
	Return (.F.)
EndIf

dbSelectArea( cAlias )

Return (.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o	 ЁChkDtDe	Ё Autor Ё Marcos A. Stiefano	Ё Data Ё 29.06.95 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Verifica Data Demissao / Data Admissao 					  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso		 Ё Generico 												  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function ChkDtDe(dDtAdm,dDtDem)
Local cAlias := Alias()
/*Quando essa funГЦo for chamada a partir da ImportaГЦo Logix
deve sempre retornar verdadeiro(.T.)*/
if(IsInCallStack("RHIMP01"))
	Return(.T.)
endIf

If Inclui
	If (IsInCallStack("GPEA010") .And. !IsBlind()) .And. !Empty(dDtDem)
		Help(" ",1, "NAODEMINCL")
		Return (.F.)
	EndIf
EndIf

If !Empty(dDtDem) .AND. Inclui = .F.
	If dDtDem < dDtAdm
		Help(" ",1, "DATINVADEM")
		Return (.F.)
	EndIf
EndIf
dbSelectArea( cAlias )
Return (.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o	 ЁChkDt1234 Ё Autor Ё Marcos A. Stiefano	Ё Data Ё 29.06.95 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Verifica Datas 											  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso		 Ё Generico 												  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ/*/
Function ChkDt1(dDtAfa,dDtAdm,dDtDem)
Local cAlias := Alias()

// Data Afastamento < Data de Admissao
If !Empty(dDtAfa) .AND. !Empty(dDtAdm)
	If dDtAfa < dDtAdm
		Help(" ",1, "DATAFASADM")
		Return (.F.)
	EndIf
EndIf

// Data Afastamento > Data de Demissao
If !Empty(dDtAfa) .AND. !Empty(dDtDem)
	If dDtAfa > dDtDem
		Help(" ",1, "DATAFASDEM")
		Return (.F.)
	EndIf
EndIf

dbSelectArea( cAlias )

Return (.T.)

Function ChkDt2(dDtAfa,dDtAdm,dDtDem,dDtRet)
Local cAlias := Alias()

// Data Retorno < Data de Afastamento
If !Empty(dDtRet) .AND. !Empty(dDtAfa)
	If dDtRet < dDtAfa
		Help(" ",1, "DATRETAFAS")
		Return (.F.)
	EndIf
EndIf

// Data Retorno < Data de Admissao
If !Empty(dDtRet) .AND. !Empty(dDtAdm)
	If dDtRet < dDtAdm
		Help(" ",1, "DATRETOADM")
		Return (.F.)
	EndIf
EndIf

// Data Retorno > Data de Demissao
If !Empty(dDtRet) .AND. !Empty(dDtDem)
	If dDtRet > dDtDem
		Help(" ",1, "DATRETODEM")
		Return (.F.)
	EndIf
EndIf

dbSelectArea( cAlias )

Return (.T.)

Function ChkDt3(dDtAdm,dDtVex)
Local cAlias := Alias()

// Data Vencimento Experiencia < Data de Admissao
If !Empty(dDtVex) .AND. !Empty(dDtAdm)
	If dDtVex < dDtAdm
		Help(" ",1, "DATVEXPADM")
		Return (.F.)
	EndIf
EndIf

dbSelectArea( cAlias )

Return (.T.)

Function ChkDt4(dDtAdm,dDtVem)
Local cAlias := Alias()

// Data Exame Medico < Data de Admissao
If !Empty(dDtVem) .AND. !Empty(dDtAdm)
	If dDtVem < dDtAdm
		Help(" ",1, "DATVEMEADM")
		Return (.F.)
	EndIf
EndIf

dbSelectArea( cAlias )

Return (.T.)
/*/
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбббдддддддддд©
ЁFun┤┘o    ЁDaysBetwDates   ЁAutorЁEmerson Campos       Ё Data Ё09/02/2015Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддббддддадддддддддд╢
ЁDescri┤┘o ЁRetorna array contendo os dias entre duas datas.              Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддббддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									       Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддббддд╢
ЁParametrosЁdDate1 = Data Inicial     									       Ё
Ё          ЁdDate2 = Data Final		                                    	Ё
Ё          ЁlHoliday  = Considera feriado como dia util				    	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддббддд╢
ЁRetorno   ЁaInterval[1] = Data denro do intervalo                       Ё
Ё          ЁaInterval[2] = Se И feriado .T. ou .F.                       Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддббдддддд╢
ЁUso       ЁGenerico 	                                                 Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function DaysBetwDates( dDate1 , dDate2 , lHoliday )
Local aHoliday	:=	RetFeriados()
Local aInterval	:= {}

Default lHoliday  := .F.

While dDate2 >= dDate1
	aADD(aInterval, {dDate2, Ascan(aHoliday,DTOS(dDate1)) > 0 })
	dDate2--
Enddo

Return aInterval

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё fPeriodVld Ё Autor Ё Mauricio Takakura   Ё Data Ё 08/09/11 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Validar o lancamento de Mes/Ano ou Ano/Mes  no acols       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё GENERICO                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function fPeriodVld( cValor,cTipo )
Local lRet 		:= .T.
Local nPosDt1 	:= GdFieldPos("ANOMESI", aHeader)
Local nPosDt2 	:= GdFieldPos("ANOMESF", aHeader)
Local nPosFil 	:= GdFieldPos("RCC_FIL", aHeader)
Local nPosINSS 	:= GdFieldPos("PERCINSS", aHeader)
Local cFilCols	:= aCols[n, nPosFil]
Local cData1
Local cData2
Local nI		:= 1
Local lCodS001	:= (cCodigo == "S001" .And. cPaisLoc == "BRA")
Local nPosDel	:= Len(aHeader) + 1
Local nINSS     := 0

If cTipo == 1
	cData1 := cValor

	If nPosDt2 > 0
		cData2 := aCols[n, nPosDt2]
	EndIf

	If lCodS001
		nINSS  := aCols[n, nPosINSS]
	EndIf
ElseIf cTipo == 2
	cData2 := cValor

	If nPosDt1 > 0
		cData1 := aCols[n, nPosDt1]
	EndIf

	If lCodS001
		nINSS  := aCols[n, nPosINSS]
	EndIf
ElseIf cTipo == 3 .And. lCodS001
	cData1 := aCols[n, nPosDt1]
	cData2 := aCols[n, nPosDt2]

	nINSS  := cValor
EndIf

For nI := 1 To Len(aCols)
	If aCols[nI, nPosDel]	//.T. linha deletada
		nI ++
	Else
		If nI <> n
			If cFilCols == aCols[nI, nPosFil] .And. cData1 <= aCols[nI, nPosDt2] .And.;
			( ( lCodS001 .And. cData1 >= aCols[nI, nPosDt1] .And. aCols[nI,nPosINSS] == nINSS ) .Or. ( !(lCodS001) .And. cData1 >= aCols[nI, nPosDt1] ) )
				MsgInfo(OemToAnsi(STR0008) + CRLF + OemToAnsi(STR0015 + alltrim(str(nI)))  ) // "AtenГЦo!" ### "Periodo inicial jА cadastrado na linha: "
				Return .F.
			EndIf

			If cFilCols == aCols[nI, nPosFil] .And. cData2 >= aCols[nI, nPosDt1] .And.;
			( ( lCodS001 .And. cData2 <= aCols[nI, nPosDt2] .And. aCols[nI,nPosINSS] == nINSS ) .Or. ( !(lCodS001) .And. cData2 <= aCols[nI, nPosDt2] ) )
				MsgInfo(OemToAnsi(STR0008) + CRLF + OemToAnsi(STR0016 + alltrim(str(nI)))  ) // "AtenГЦo!" ### "Periodo final jА cadastrado na linha : "
				Return .F.
			EndIf
		EndIf
	EndIf
Next nI
Return lRet

/*/{Protheus.doc}fChkSitFol
Valida se o campo RA_SITFOLH estА em branco na inclusЦo
@author Gabriel de Souza Almeida
@since 22/04/2016
@version P12
/*/
Function fChkSitFol()
	Local lRet := .F.

	If Inclui
		If (IsInCallStack("GPEA010") .And. !IsBlind()) .And. !(Empty(M->RA_SITFOLH))
			lRet := .T.
		EndIf
	EndIf
Return lRet

/*/{Protheus.doc}fChkRescRai
Valida se o campo RA_RESCRAI estА em branco na inclusЦo
@author Gabriel de Souza Almeida
@since 22/04/2016
@version P12
/*/
Function fChkRescRai()
	Local lRet := .F.

	If Inclui .And. (IsInCallStack("GPEA010") .And. !IsBlind())
		lRet := .T.
	EndIf
Return lRet
