#INCLUDE "PROTHEUS.CH"

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Verifica se a Execucao eh no AS/400                          ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static lExInAs400 := ExeInAs400()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ RHLIBAFA ³ Autor ³ Equipe Advanced RH              ³ Data ³25/10/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Biblioteca de funcoes para tratamento de afastamentos                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Christiane V³11/04/2014³M_RH003/003252³Criação da função fCargaTpAus e alteração ³±±
±±³			   ³          ³              ³da função fDescAfast.                     ³±±
±±³M. Silveira ³14/07/2017³DRHPONTP-1062 ³Ajuste em fLoadTransf p/ retornar os dados³±±
±±³			   ³          ³              ³de transferencias sem alterar o conteudo. ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³InRhLibAfaExec³Autor ³Marinaldo de Jesus   ³ Data ³14/05/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Executar Funcoes Dentro de RHLIBAFA                          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³InRhLibAfaExec( cExecIn , aFormParam )						 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									 ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³uRet                                                 	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      	     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³Generico 													 ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function InRhLibAfaExec( cExecIn , aFormParam )
         
Local uRet

DEFAULT cExecIn		:= ""
DEFAULT aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= __ExecMacro( cExecIn )
EndIF

Return( uRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³fAfasta   ³ Autor ³ Equipe Advanced RH    ³ Data ³30/01/1998³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³ Verifica se Funcionario est  afastado na data passada.     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parƒmetros³cFil     = Filial do Funcion rio                            ³
³          ³cMat     = Matricula Funcion rio                            ³
³          ³dDtPesq  = Data para Pesquisa                               ³
³          ³dIniAfas = Vari vel Data para retorno Inicio Afastamento    ³
³          ³dFimAfas = Vari vel Data para retorno Fim Afastamento       ³
³          ³cTipAfas = Vari vel String para retorno Tipo Afastamento    ³
³          ³cIdAfas  = Vari vel String para retorno NUMID 				³
³          ³lPesqId  = Se efetua pesquisa de datas pelo NUMID			³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fAfasta(cFil, cMat, dDtPesq, dIniAfas , dFimAfas, cTipAfas) ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³Generico                                                    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function fAfasta( cFil , cMat , dDtPesq , dIniAfas , dFimAfas , cTipAfas , aAfasta, cIdAfas, lPesqId )

Local bAfasta	:= { || NIL }
Local cFilMat	:= ""
Local nSvOrder	:= 0
Local nOrder	:= RetOrdem( "SR8" , "R8_FILIAL+R8_MAT+DTOS(R8_DATAINI)+R8_TIPO" )
Local nAfa		:= 0
Local nAfasta	:= 0
Local lRet		:= .F.
Local lVerArea	:= .F.

Local cCpos		:= ""
Local cWhere	:= ""
Local cOrder	:= ""
Local cAliasQry := "_QRYSR8"
Local cSvAlias	:= Alias()

DEFAULT cFil    := SRA->RA_FILIAL
DEFAULT cMat    := SRA->RA_MAT
DEFAULT dDtPesq := dDataBase
DEFAULT cIdAfas := ""
DEFAULT lPesqId := .F.

Static lUseR8NumId := fVerNumId()

dIniAfas := Ctod("//")
dFimAfas := dIniAfas
cTipAfas := ""

If lPesqId
	
		
	cCpos  := "% SR8.R8_DATAINI, SR8.R8_DATAFIM, SR8.R8_TIPO, SR8.R8_NUMID, SR8.R8_TIPOAFA %"

	cWhere := "% SR8.R8_FILIAL = '"+cFil+"'"
	cWhere += " AND SR8.R8_MAT    = '"+cMat+"'"
	cWhere += " AND SR8.R8_NUMID  = '"+cIdAfas+"'%"
	
	cOrder := "% SR8.R8_NUMID %"
	
	BeginSql alias cAliasQry

		SELECT 		%exp:cCpos%
		FROM 		%table:SR8% SR8
		WHERE 		%exp:cWhere% AND SR8.%NotDel%
		ORDER BY	%exp:cOrder%
			
	EndSql
		
	If !Empty( dIniAfas )
		dIniAfas  := (cAliasQry)->(R8_DATAINI)
		dFimAfas  := (cAliasQry)->(R8_DATAFIM)
		cTipAfas  := fCargaTpAus((cAliasQry)->(R8_TIPOAFA))
	EndIf
	
	(cAliasQry)->( dbCloseArea() )
	dbSelectArea( cSvAlias )
	
Else

	IF !( ValType( aAfasta ) == "A" )
		cFil		:= xFilial( "SR8" , cFil )
		cFilMat 	:= ( cFil + cMat )
		nSvOrder	:= SR8->( IndexOrd() )
		IF !( nOrder == nSvOrder )
			SR8->( dbSetOrder( nOrder ) )
		EndIF
		bAfasta := { || dDtPesq >= R8_DATAINI .and. dDtPesq <= IF( !Empty( R8_DATAFIM ) , R8_DATAFIM , dDtPesq ) }
		IF SR8->( dbSeek( cFilMat , .F. ) )
			While SR8->( !Eof() .and. R8_FILIAL + R8_MAT == cFilMat )
				IF ( lRet := SR8->( Eval( bAfasta ) ) )
					dIniAfas  := SR8->R8_DATAINI
					dFimAfas  := SR8->R8_DATAFIM				
					cTipAfas  := fCargaTpAus(SR8->(R8_TIPOAFA))
					If lUseR8NumId 
						cIdAfas	  := SR8->R8_NUMID
					EndIf
					Exit
				EndIF
				SR8->( dbSkip() )
			End While
		EndIF
		IF !( nOrder == nSvOrder )
			SR8->( dbSetOrder( nSvOrder ) )
		EndIF
	Else
		bAfasta := { || dDtPesq >= aAfasta[ nAfa , 01 ] .and. dDtPesq <= IF( !Empty( aAfasta[ nAfa , 02 ] ) , aAfasta[ nAfa , 02 ] , dDtPesq ) }
		nAfasta	:= Len( aAfasta )
		If ( Select("SR8") == 0 )
			dbSelectArea("SR8")
			lVerArea := .T.
		EndIf
		If lVerArea
			SR8->( dbCloseArea() )
		EndIf	
	    For nAfa := 1 To nAfasta
	    	IF ( lRet := Eval( bAfasta ) )
				dIniAfas  := aAfasta[ nAfa , 01 ]
				dFimAfas  := aAfasta[ nAfa , 02 ]
				cTipAfas  := fCargaTpAus(aAfasta[ nAfa , 03 ])
				If lUseR8NumId 
					cIdAfas   := aAfasta[ nAfa , 04 ]
				EndIf
	    		Exit
	    	EndIF
	    Next nAfa
	EndIF

EndIf
	
Return( lRet )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³fVerNumId		³Autor³Igor Franzoi		  ³ Data ³16/06/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Verifica se o numero R8_NUMID esta como usado				³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³Generico													³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Static Function fVerNumId()

Local aArea := GetArea()
Local aSx3Area := SX3->(GetArea())
Local lRet := .F.

SX3->(dbSetOrder(2))

If SX3->(dbSeek("R8_NUMID"))
	lRet := SX3->(x3uso(X3_USADO))
Else
	lRet := .F.
EndIf

RestArea(aSx3Area)
RestArea(aArea)

Return lRet

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³fAfastaPer		³Autor³Marinaldo de Jesus ³ Data ³14/01/2003³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Carregar em aAfasta Todos os Afastamentos do Periodo		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Uso       ³Generico													³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/
Function fAfastaPer( aAfasta , dPerIni , dPerFim , cFil , cMat )

Local cAlias		:= "SR8"
Local cAliasQuery	:= ""
Local lAfasta		:= .F.
Local cIniData	 	:= ""
Local cFimData		:= ""
Local cQuery	 	:= ""
Local nX			:= 0

Static aSr8Fields
Static cQrySr8Fields
Static nSr8Fields
Static __oSt1
Static __cEmpAux

DEFAULT aSr8Fields := ( cAlias )->( dbStruct() )
DEFAULT nSr8Fields := Len( aSr8Fields )

If ( cQrySr8Fields == NIL )
	cQrySr8Fields := ""
	For nX := 1 To nSr8Fields
		cQrySr8Fields += aSr8Fields[ nX , 01 ] + ", "
	Next nX
EndIf
	
aAfasta := {}
DEFAULT dPerIni	:= Ctod("//")
DEFAULT dPerFim	:= Ctod("//")
DEFAULT cFil	:= SRA->RA_FILIAL
DEFAULT cMat	:= SRA->RA_MAT

cIniData	:= Dtos( dPerIni )
cFimData	:= Dtos( dPerFim )
cAliasQuery := GetNextAlias()

If __oSt1 == Nil .or. ( __cEmpAux == Nil .or. __cEmpAux <> cEmpAnt )
	__cEmpAux   := cEmpAnt

	__oSt1 := FWPreparedStatement():New()

	cQuery := "SELECT "
	cQuery += cQrySr8Fields
	cQuery := SubStr( cQuery , 1 , Len( cQuery ) - 2 )
	cQuery += " FROM "
	cQuery += InitSqlName( cAlias )
	cQuery += " WHERE "
	cQuery += "R8_FILIAL = ? "
	cQuery += " AND "
	cQuery += "R8_MAT = ? "
	cQuery += " AND "
	cQuery += " ( "
	cQuery += 		"R8_DATAFIM = ? "        //Data Fim em branco
	cQuery += 		" OR "
	cQuery += 		" ( "
	cQuery += 				"R8_DATAINI >= ? "           // Data Inicial dentro do Periodo
	cQuery += 				" AND "
	cQuery += 				"R8_DATAINI <= ? "
	cQuery += 		" ) "
	cQuery += 		" OR "
	cQuery += 		" ( "
	cQuery += 				"R8_DATAFIM >= ? "          // Data Final dentro do Periodo
	cQuery += 				" AND "
	cQuery += 				"R8_DATAFIM <= ? "
	cQuery += 		" ) "
	cQuery += 		" OR "
	cQuery += 		" ( "
	cQuery += 				"R8_DATAINI <= ? "          // Periodo do Afastamento Engloba o Periodo de Apontamento
	cQuery += 				" AND "
	cQuery += 				"R8_DATAFIM >= ? "
	cQuery += 		" ) "
	cQuery += " ) "
	cQuery += " AND "
	cQuery += "D_E_L_E_T_=' ' "
	cQuery += "ORDER BY " + SqlOrder( (cAlias)->( IndexKey() ) )

	cQuery := ChangeQuery( cQuery )

	__oSt1:SetQuery(cQuery)

EndIf

__oSt1:SetString(1,cFil)
__oSt1:SetString(2,cMat)
__oSt1:SetString(3,Space(Len(cFimData)))
__oSt1:SetString(4,cIniData)
__oSt1:SetString(5,cFimData)
__oSt1:SetString(6,cIniData)
__oSt1:SetString(7,cFimData)
__oSt1:SetString(8,cIniData)
__oSt1:SetString(9,cFimData)

cQuery := __oSt1:getFixQuery()

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQuery)

For nX := 1 To nSr8Fields
	If !( aSr8Fields[ nX , 02 ] == "C" )
		TcSetField(cAliasQuery,aSr8Fields[nX,01],aSr8Fields[nX,02],aSr8Fields[nX,03],aSr8Fields[nX,04])
	EndIf
Next nX

While ( cAliasQuery )->( !Eof() )
	( cAliasQuery )->( aAdd( aAfasta , { R8_DATAINI , R8_DATAFIM , R8_TIPOAFA , R8_NUMID } ) )

	( cAliasQuery )->( dbSkip() )
End While
	
( cAliasQuery )->( dbCloseArea() )

lAfasta := ( Len( aAfasta ) > 0 )

Return( lAfasta )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fDescAfast³ Autor ³ Equipe Advanced RH    ³ Data ³13/06/1997³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna uma descri‡„o de afastamento ( SX5 - Tab.30 )       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³fDescAfast( cTipAfasta , nSize , cFil )					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function fDescAfast( cTipAfasta , nSize , cFil, lDemitido, cNumID, cFilFun )

Local cRet   		:= ""
Local aArea 		:= GetArea()

DEFAULT cTipAfasta	:= ""  
DEFAULT cNumId		:= ""
DEFAULT nSize		:= 60
DEFAULT lDemitido   := .F.
DEFAULT cFilFun		:= cFilAnt
                            
If !Empty(cNumId)
	dbSelectArea("SR8")
	SR8->( dbSetOrder(RetOrder("SR8","R8_FILIAL+R8_NUMID")) )
	IF SR8->( dbSeek( xFilial("SR8",cFilFun) + cNumId ))   
		If !lDemitido
			cRet :=  fDesc( "RCM", SR8->R8_TIPOAFA, "RCM_DESCRI", nSize, cFil, NIL, .F. )
		Endif
	Endif
Endif

RestArea(aArea)

Return( cRet )     

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³PonSitPer()        ³Autor³Mauricio MR       ³Data³09/09/2005³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Obtem Afatamentos e Transferencias do Funcionario para o pe-³
³          ³riodo informado.											³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³RetSituacao(cFil,cMat,dDataIni,dDataFim,aAfasta,aTransf )	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³cFil		-> Filial do Funcionario						³
³          ³cMat   		-> Matricula do Funcionario						³
³          ³dDataIni	-> Data Inicio do Periodo						³
³          ³dDataFim	-> Data Final do Periodo						³
³          ³aAfasta		-> Array com os afastamentos (Por Referencia)	³
³          ³aTransf		-> Array com as transferencias (Por Referencia)	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL 														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³Generico                                                    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function PonSitPer( cFil , cMat , dDataIni , dDataFim, aAfasta, aTransf, lOrigem)

Local aArea				:= GetArea()
Local aAreaSRA			:= SRA->( GetArea() )
Local cSvFilAnt			:= cFilAnt
Local lTransfEmp		:= .F.

DEFAULT cFil			:= SRA->RA_FILIAL
DEFAULT cMat			:= SRA->RA_MAT
DEFAULT dDataIni		:= Ctod("//")
DEFAULT dDataFim		:= Ctod("//")
DEFAULT aTransf			:= {}
DEFAULT lOrigem			:= .T.
     
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Atualiza cFilAnt Conforme Filial do Funcionario               ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
cFilAnt		:= IF( !Empty( cFil ) , cFil , cFilAnt )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Carrega os Afastamentos Referente ao Periodo se o array dos ³
³ mesmos foi passado (mesmo que vazio).						  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ/*/   
If aAfasta <> NIL
	aAfasta := {}
	fAfastaPer( @aAfasta , dDataIni , dDataFim , cFil , cMat )
Else
	aAfasta := {}
Endif

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Verifica se Funcionario foi Transferido De Empresa ou de Fi³
³ lial	e carrega as transferencias							 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/	
If aTransf <> NIL
	aTransf:= {}
	lTransfEmp := fLoadTransf(@aTransf, dDataIni , dDataFim, lOrigem)
Else
	aTransf	:= {}
Endif 
/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Restaura Dados de Entrada									 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/	
cFilAnt := cSvFilAnt

RestArea( aAreaSRA )
RestArea( aArea )

Return( lTransfEmp  )                                    

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³fLoadTransf()      ³Autor³Equipe RH         ³Data³06/08/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Obtem Afatamentos e Transferencias do Funcionario para o pe-³
³          ³riodo informado.											³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fLoadTransf(aTransf)                                     	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³aTransf		-> Array com as transferencias (Por Referencia)	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³Logico(se possui transferencias, .T.)						³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³SIGAPON                                                     ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fLoadTransf(aTransf, dDtIniPon, dDtFimPon, lOrigem)

Local nX		:= 0
Local cFilMat	:= SRA->(RA_FILIAL + RA_MAT )
Local aAux		:= {}
Local nElem		:= 0
          
//-- Variaveis para obter as transferencias do funcionario
Local lNoRept := .F.			//Nao Armazena Itens Iguais

Default dDtIniPon	:= cToD("//")
Default dDtFimPon	:= cToD("//")
Default lOrigem		:= .T.

//-- Carrega todas as transferencias
fTransfAll(aAux, , lNoRept, lOrigem, dDtIniPon, dDtFimPon)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Filtrar as Transferencias do Funcionario				       ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/	
aTransf:= {}

If !Empty(aAux)
	For nX := 1 to Len(aAux)  
		//-- Se Transf DE e Transf PARA for diferente ( cEmpAnt + cFilAnt + cMat ) desconsidera
		If ( ( cEmpAnt + cFilMat ) <> (aAux[ nX , 04 ] + aAux[ nX , 10 ] + aAux[ nX , 11 ] )) .AND.;
			( ( cEmpAnt + cFilMat ) <> (aAux[ nX , 01 ] + aAux[ nX , 08 ] + aAux[ nX , 09 ] ) ) 
			Loop
		Endif
                 
		//Posicao anterior a Transferencia 
		aAdd(aTransf, aClone(aAux[nX]))
		nElem:= Len(aTransf)
		
		//-- Carrega informacoes para montagem do registro anterior aa transferencia
		//-- Desativado em 07/2017 para avaliacao, para que o array passe a retornar os dados sem alteracao de conteudo
		/*
		If ( ( cEmpAnt + cFilMat ) <> (aAux[ nX , 04 ] + aAux[ nX , 10 ] + aAux[ nX , 11 ] )) 
			aTransf[nElem , 7 ] := aTransf[nElem, 7 ] - 1 	//Data Trf - 1
			aTransf[nElem , 4 ] := aTransf[nElem, 1 ]		//Emp Para  = Emp De
			aTransf[nElem , 5 ] := aTransf[nElem, 2 ]     	//(Fil+Mat) Para  = (Fil+Mat) De
			aTransf[nElem , 6 ] := aTransf[nElem, 3 ]     	//CC  Para  = CC De 
			aTransf[nElem , 11] := aTransf[nElem, 9 ]    	//Mat Para  = Mat De
			aTransf[nElem , 10] := aTransf[nElem, 8 ]     	//Fil Para  = Fil De 
		Else

			aTransf[nElem , 1 ] := aTransf[nElem, 4 ]		//Emp De  = Emp Para
			aTransf[nElem , 2 ] := aTransf[nElem, 5 ]     	//(Fil+Mat) De  = (Fil+Mat) Para
			aTransf[nElem , 3 ] := aTransf[nElem, 6 ]     	//CC  De  = CC Para
			aTransf[nElem , 9 ] := aTransf[nElem, 11]    	//Mat De  = Mat Para
			aTransf[nElem , 8 ] := aTransf[nElem, 10]     	//Fil De  = Fil Para
		Endif
		*/
	Next  
Endif                  
           
Return (!Empty(aTransf))

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡„o    ³PonRetSit()        ³Autor³Mauricio MR       ³Data³09/09/2005³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡„o ³Obtem a Situacao do Funcionario em determinada data em fun- ³
³          ³cao dos afastamentos e das transferencias. 					³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³RetSituacao(cFil,cMat,dDataIni,dDataFim,aAfasta,aTransf )	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³cFil		-> Filial do Funcionario						³
³          ³cMat   		-> Matricula do Funcionario						³
³          ³dDataRef	-> Data para a pesquisa da situacao do func.	³
³          ³aAfasta		-> Array com os afastamentos (Por Referencia)	³
³          ³aTransf		-> Array com as transferencias (Por Referencia)	³
³          ³lTransfEmp	-> Logico que indica se a transferencia encon- 	³
³          ³               trada foi entre empresas.                  	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³NIL 														³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³Generico                                                    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function PonRetSit( cFil , cMat , dDataRef, aAfasta, aTransf, lTransfEmp  )

Local aArea				:= GetArea()
Local aAreaSRA			:= SRA->( GetArea() )
Local aReturn			:= Array( 4 )
Local bAscanTrf			:= { || NIL }
Local bAscanRTrf		:= { || NIL }

Local dIniAfas			:= Ctod("//")                                      
Local dFimAfas			:= Ctod("//")                                      
Local cTipAfas			:= ""
Local cSvFilAnt			:= cFilAnt
Local lTransf			:= .F.
Local nX				:= 0
Local nPosTransf		:= 0
Local nPosRTransf		:= 0

DEFAULT cFil			:= SRA->RA_FILIAL
DEFAULT cMat			:= SRA->RA_MAT
DEFAULT dDataRef		:= Ctod("//")
DEFAULT aAfasta			:= {}
DEFAULT aTransf			:= {}    
DEFAULt lTransfEmp 		:= .F.

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Inicializa os Valores DEFAULT para o aReturn                  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aReturn[1]	:= Space( Len( SRA->RA_SITFOLH ) )
aReturn[2]	:= Space( Len( SRA->RA_AFASFGT ) )
aReturn[3]	:= Space( Len( SRA->RA_RESCRAI ) )
aReturn[4]	:= Ctod("//")

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Atualiza cFilAnt Conforme Filial do Funcionario               ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
cFilAnt		:= IF( !Empty( cFil ) , cFil , cFilAnt )

    dIniAfas:= dFimAfas:= Ctod("//")                                      
    cTipAfas:= ""
    IF (lAfastper:= !Empty(aAfasta) )                                 
		If ( lAfast := fAfasta( cFil , cMat , dDataRef , @dIniAfas , @dFimAfas , @cTipAfas , aAfasta ) )
			 aReturn[1] := IF( AllTrim( cTipAfas) == "F" , "F" , "A" )
			 aReturn[2] := AllTrim( cTipAfas )
		Endif
	Endif		
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Verifica se Funcionario foi Transferido De Empresa ou de Fi³
	³ lial														 ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/	
	IF !( aReturn[1] == "D" )
		IF ( lTransf := !Empty(aTransf) )	
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Monta Bloco para Buca da Transferencia                     ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/	
			bAscanTrf := { |x|;
								( x[01] == cEmpAnt );		//Empresa de Origem igual a cEmpAnt
								.and.;
								( x[08] == cFilAnt );		//Filial de Origem igual a cFilAnt
								.and.;
								( x[07] <= dDataRef );		//Data da Transferencia igual Data de Referencia
						 }
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Procura o Indice da ultima Transferencia                   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/	
			nPosTransf := aScan( aTransf , bAscanTrf )
			nX		   := nPosTransf
			While ( ( nX := aScan( aTransf , bAscanTrf , ++nX ) ) > 0 )
				nPosTransf := nX
			End While
			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ So considera a Transferencia se funcionario nao Retornou   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			IF ( nPosTransf > 0 )
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Monta Bloco para Buca da Transferencia                     ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/	
				bAscanRTrf := { |x|;
									( x[04] == cEmpAnt );					//Empresa de Destino igual a cEmpAnt
									.and.;
									( x[10] == cFilAnt );					//Filial de Destino igual a cFilAnt
									.and.;
									( x[07] >= aTransf[ nPosTransf , 7 ] );	//Retorno de Transferencia
								}
				/*
				ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				³ Verifica Se Teve Transferencia Posterior a Referencia      ³
				ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
				IF ( (nPosRTransf := aScan( aTransf , bAscanRTrf ) ) == 0 )
					/*
					ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					³ So Atualiza se Nao Teve Retorno e Se a Transferencia    for³
					³ Dentro da Referencia										 ³
					ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
					aReturn[1] := "D"
					aReturn[2] := IF( lTransfEmp , "N" , "5" )
					aReturn[3] := "31"
					aReturn[4] := aTransf[ nPosTransf , 07 ] //A Demissao é a Data de Transferencia
				EndIF
			EndIF
		EndIF
	ElseIF Empty( aReturn[1] )
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Considera a Situacao do Proprio Cadastro                   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		aReturn[1] := SRA->RA_SITFOLH
		aReturn[2] := SRA->RA_AFASFGT
		aReturn[3] := SRA->RA_RESCRAI
		aReturn[4] := SRA->RA_DEMISSA
	EndIF

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Se Funcionario foi Transferido a Situacao esta como de Demi³
	³ tido e Devera Ser Mantida. Caso a Data de Demissao   esteja³
	³ preenchida e Funcionario Esteja com Situacao Diferente   De³
	³ Demitido Atualiza a Situacao do Funcionario				 ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/	
	
	IF !( aReturn[1] == "D" )
		IF !Empty( SRA->RA_DEMISSA )
			IF ( Dtos( SRA->RA_DEMISSA ) <= Dtos(dDataRef ) )
				aReturn[1] := "D"
				aReturn[2] := SRA->RA_AFASFGT
				aReturn[3] := SRA->RA_RESCRAI
				aReturn[4] := SRA->RA_DEMISSA
			EndIF
		EndIF
	EndIF	

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Restaura Dados de Entrada									 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/	
cFilAnt := cSvFilAnt
RestArea( aAreaSRA )
RestArea( aArea )

Return( aReturn )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Função    ³fCargaTpAus()      ³Autor³Equipe RH         ³Data³09/04/2014³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descrição ³Obtem Tipo de Ausência conforme a tabela RCM                ³
³          ³                                 							³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fCargaTpAus(cCodAus)                                     	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³cCodAus		-> Código da Ausência (tabela SR8)             	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Retorno   ³Retorna o tipo de ausência            						³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Observa‡„o³                                                      		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³SIGAPON                                                     ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fCargaTpAus(cCodAus)

Local cTipAus := ""
            
dbSelectArea("RCM")
If DbSeek(xFilial("RCM")+cCodAus)
	If RCM->RCM_TIPOAF == "1"
		cTipAus := "A"
	ElseIf RCM->RCM_TIPOAF == "4"
		cTipAus := "F"	
	Endif
Endif
           
Return (cTipAus)
