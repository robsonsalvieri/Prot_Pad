#INCLUDE 'PROTHEUS.CH'
#INCLUDE 'REPORT.CH'
#INCLUDE 'GPER135.CH'

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ GPER135    ³Autor  ³M. Silveira                      ³ Data ³22/11/2016³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relatorio Conciliacao de Ferias                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³               ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador   ³Data      ³Ticket    ³Motivo da Alteracao                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³          ³          ³                                              ³±±
±±³Wesley Alves  ³15/07/2019³DRHGCH-11970³Inclusão da Verba de Periculosidade - Ferias³±±
±±³              ³          ³            ³Mes Seguinte nas verbas do próximo mês      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GPER135()

	Local aArea 	:= GetArea()
	Local oReport   := Nil

	Private cPerg	:= "GPER135"
	Private cUltFil := ""
	Private aCodFol := {}
	Private aInfo   := {}
	Private aOrd    := {	OemToAnsi(STR0016),; //"Filial + Matricula",
						  	OemToAnsi(STR0017),; //"Filial + Centro de Custos"
							OemToAnsi(STR0018) } //"Filial + Departamento"

	oReport := ReportDef()

	If(oReport <> Nil)
		oReport:PrintDialog()
	EndIf

	oReport := Nil
	RestArea(aArea)

Return()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ReportDef   ³ Autor ³M. Silveira            ³ Data ³22/11/2016³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Relatorio Conciliacao de Ferias                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER135                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function ReportDef()
	Local oReport	:= Nil
	Local oSectFun	:= Nil
	Local oSectLct	:= Nil
	Local oSecTot	:= Nil
	Local cRelTit	:= OemToAnsi(STR0001) //"Relatório Conciliação de Férias"
	Local cRelDesc	:= OemToAnsi(STR0002) //"Este programa emite a Impressão do Relatório Conciliação de Férias"

	Pergunte(cPerg,.F.)

	DEFINE REPORT oReport NAME "GPER135" TITLE cRelTit PARAMETER cPerg ACTION {|oReport| R135Imp(oReport)} DESCRIPTION cRelDesc TOTAL IN COLUMN

	DEFINE SECTION oSectFun OF oReport 	TITLE cRelTit TABLES "SRA","SRH" TOTAL IN COLUMN ORDERS aOrd
		DEFINE CELL NAME "RA_FILIAL"  OF oSectFun SIZE 10 TITLE OemToAnsi(STR0003) ALIAS "SRA" 	Block {||(cAliasFun)->RA_FILIAL}						//"FILIAL"
		DEFINE CELL NAME "RA_MAT" 	  OF oSectFun SIZE 10 TITLE OemToAnsi(STR0004) ALIAS "SRA" 	Block {||(cAliasFun)->RA_MAT} 							//"MATRICULA"
		DEFINE CELL NAME "RA_NOME" 	  OF oSectFun SIZE 50 TITLE OemToAnsi(STR0005) ALIAS "SRA" 	Block {||(cAliasFun)->RA_NOME} 							//"NOME FUNCIONARIO"
		DEFINE CELL NAME "INICIO" 	  OF oSectFun SIZE 15 TITLE OemToAnsi(STR0006)      	    Block {||DtoC( Stod((cAliasFun)->RH_DATAINI) )} 		//"INICIO"
		DEFINE CELL NAME "TERMINO" 	  OF oSectFun SIZE 15 TITLE OemToAnsi(STR0007)  	        Block {||DtoC( Stod((cAliasFun)->RH_DATAFIM) )}		//"TERMINO"
		DEFINE CELL NAME "DIAS" 	  OF oSectFun SIZE 08 TITLE OemToAnsi(STR0008)              Block {||Transform((cAliasFun)->RH_DFERIAS,'@E 99.9')} //"DIAS"
		DEFINE CELL NAME "TIPO" 	  OF oSectFun SIZE 08 TITLE OemToAnsi(STR0009)              Block {||(cAliasFun)->RH_TIPCAL}  						//"TIPO"
		DEFINE CELL NAME "VL_MESANT"  OF oSectFun SIZE 20 TITLE OemToAnsi(STR0010) 	PICTURE "@E 999,999.99" 											//"VALOR MES ANT."
		DEFINE CELL NAME "VL_MES"	  OF oSectFun SIZE 20 TITLE OemToAnsi(STR0011)  PICTURE "@E 999,999.99" 											//"VALOR MES"
		DEFINE CELL NAME "VL_MESSEG"  OF oSectFun SIZE 20 TITLE OemToAnsi(STR0012) 	PICTURE "@E 999,999.99" 											//"VALOR MES SEG."
		DEFINE CELL NAME "VL_TOTAL"   OF oSectFun SIZE 20 TITLE OemToAnsi(STR0019) 	PICTURE "@E 999,999.99" 											//"TOTAL"

 	DEFINE SECTION oSectLct OF oReport 	TITLE cRelTit TABLES "SRA","SRC","SRD" ORDERS aOrd

	//Total Geral
	DEFINE FUNCTION NAME "TVL_MESANT" FROM oSectFun:Cell("VL_MESANT") FUNCTION SUM  NO END SECTION PICTURE "@E 999,999,999.99"
	DEFINE FUNCTION NAME "TVL_MES"    FROM oSectFun:Cell("VL_MES")    FUNCTION SUM  NO END SECTION PICTURE "@E 999,999,999.99"
	DEFINE FUNCTION NAME "TVL_MESSEG" FROM oSectFun:Cell("VL_MESSEG") FUNCTION SUM  NO END SECTION PICTURE "@E 999,999,999.99"
	DEFINE FUNCTION NAME "TVL_TOTAL"  FROM oSectFun:Cell("VL_TOTAL")  FUNCTION SUM  NO END SECTION PICTURE "@E 999,999,999.99"

	oReport:SetLandscape()
	oReport:DisableOrientation()
	oSectFun:Cell("VL_MESANT"):nHeaderAlign := 3
	oSectFun:Cell("VL_MES"   ):nHeaderAlign := 3
	oSectFun:Cell("VL_MESSEG"):nHeaderAlign := 3
	oSectFun:Cell("VL_TOTAL" ):nHeaderAlign := 3

Return( oReport )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³R135Imp     ³ Autor ³M. Silveira            ³ Data ³22/11/2016³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Imprime o Relatorio Conciliacao de Ferias                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER135                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function R135Imp(oReport)

    Local cParam     := ""
    Local cQryFun    := ""
    Local cQryLct    := ""
    Local cFilSRD	 := ""
	Local cFilSRC	 := ""
	Local cOrdFun    := ""
	Local cTotFil    := ""
	Local cTotCC     := ""
	Local cTotDpt    := ""
	Local nI         := 0
	Local nProv1     := 0
	Local nProv2     := 0
	Local nDesc      := 0
	Local nLiquido   := 0
	Local nInssMS    := 0
	Local aFiltros   := {}									//Filtros definidos nas perguntas do relatorio
    Local dRef       := MV_PAR02							//Referencia para pesquisa dos dados
    Local dLctIni    := cTod("//")							//Data Inicial se for do movimento ou competencia se for dos acumulados
    Local dLctFim    := cTod("//")							//Data Final se for do movimento ou competencia se for dos acumulados
	Local cVerbas    := fCodVerbas()						//Verbas de Ferias do mes seguinte que serao utilizadas
    Local cMesRef    := MesAno( MV_PAR02 )				 	//Mes de referencia informado pelo usuario
	Local lImpNome   := MV_PAR09 == 1						//Define se o nome sera impresso no relatorio
	Local bImpFil    := { || Eval( {|| fInfo(@aInfo,cUltFil),aInfo[1] } ) }

	Private oSectFun := oReport:Section(1)
	Private oSectLct := oReport:Section(2)
	Private nOrdem   := oSectFun:GetOrder()

	If Empty( cVerbas )
		Alert( OemToAnsi(STR0020) ) //"Não foi possível carregar as verbas para a geração do relatório"
		Return()
	EndIf

	cAliasFun := GetNextAlias()

	MakeSqlExpr(cPerg)

	If nOrdem == 1
		cOrdFun := " RA_FILIAL,RA_MAT "
	ElseIf nOrdem == 2
		cOrdFun := " RA_FILIAL,RA_CC,RA_MAT "
	ElseIf nOrdem == 3
		cOrdFun := " RA_FILIAL,RA_DEPTO,RA_MAT "
	EndIf

	//--------------------------------------------------------//
	//Quebras de pagina                                       //
	//--------------------------------------------------------//
	If nOrdem == 1
		DEFINE BREAK oBreakFil OF oSectFun WHEN {|| (cAliasFun)->RA_FILIAL }
		DEFINE FUNCTION NAME "TVL_MESANT" FROM oSectFun:Cell("VL_MESANT") FUNCTION SUM  BREAK oBreakFil NO END SECTION NO END REPORT PICTURE "@E 999,999,999.99"
		DEFINE FUNCTION NAME "TVL_MES"    FROM oSectFun:Cell("VL_MES")    FUNCTION SUM  BREAK oBreakFil NO END SECTION NO END REPORT PICTURE "@E 999,999,999.99"
		DEFINE FUNCTION NAME "TVL_MESSEG" FROM oSectFun:Cell("VL_MESSEG") FUNCTION SUM  BREAK oBreakFil NO END SECTION NO END REPORT PICTURE "@E 999,999,999.99"
		DEFINE FUNCTION NAME "TVL_TOTAL"  FROM oSectFun:Cell("VL_TOTAL")  FUNCTION SUM  BREAK oBreakFil NO END SECTION NO END REPORT PICTURE "@E 999,999,999.99"
		oBreakFil:OnBreak({ |x,y| cTotFil := OemToAnsi(STR0013) + " " + x + " - " + Eval( bImpFil ), oReport:ThinLine()})	//"Total da Filial: "
		oBreakFil:SetTotalText({|| cTotFil })
		oBreakFil:SetTotalInLine(.F.)
	EndIf

	If nOrdem == 2
		DEFINE BREAK oBreakCC OF oSectFun WHEN {|| (cAliasFun)->RA_CC }
		DEFINE FUNCTION NAME "TVL_MESANT" FROM oSectFun:Cell("VL_MESANT") FUNCTION SUM  BREAK oBreakCC NO END SECTION NO END REPORT PICTURE "@E 999,999,999.99"
		DEFINE FUNCTION NAME "TVL_MES"    FROM oSectFun:Cell("VL_MES")    FUNCTION SUM  BREAK oBreakCC NO END SECTION NO END REPORT PICTURE "@E 999,999,999.99"
		DEFINE FUNCTION NAME "TVL_MESSEG" FROM oSectFun:Cell("VL_MESSEG") FUNCTION SUM  BREAK oBreakCC NO END SECTION NO END REPORT PICTURE "@E 999,999,999.99"
		DEFINE FUNCTION NAME "TVL_TOTAL"  FROM oSectFun:Cell("VL_TOTAL")  FUNCTION SUM  BREAK oBreakCC NO END SECTION NO END REPORT PICTURE "@E 999,999,999.99"
		oBreakCC:OnBreak({ |x,y| cTotCC := OemToAnsi(STR0014) +" "+ x +" - "+ fDesc("CTT",x,"CTT_DESC01"), oReport:ThinLine()})	//"Total do Centro de Custos: "
		oBreakCC:SetTotalText({|| cTotCC })
		oBreakCC:SetTotalInLine(.F.)
	EndIf

	If nOrdem == 3
		DEFINE BREAK oBreakDpt OF oSectFun WHEN {|| (cAliasFun)->RA_DEPTO }
		DEFINE FUNCTION NAME "TVL_MESANT" FROM oSectFun:Cell("VL_MESANT") FUNCTION SUM  BREAK oBreakDpt NO END SECTION NO END REPORT PICTURE "@E 999,999,999.99"
		DEFINE FUNCTION NAME "TVL_MES"    FROM oSectFun:Cell("VL_MES")    FUNCTION SUM  BREAK oBreakDpt NO END SECTION NO END REPORT PICTURE "@E 999,999,999.99"
		DEFINE FUNCTION NAME "TVL_MESSEG" FROM oSectFun:Cell("VL_MESSEG") FUNCTION SUM  BREAK oBreakDpt NO END SECTION NO END REPORT PICTURE "@E 999,999,999.99"
		DEFINE FUNCTION NAME "TVL_TOTAL"  FROM oSectFun:Cell("VL_TOTAL")  FUNCTION SUM  BREAK oBreakDpt NO END SECTION NO END REPORT PICTURE "@E 999,999,999.99"
		oBreakDpt:OnBreak({ |x,y| cTotDpt := OemToAnsi(STR0015) + " " + x +" - "+ fDesc("SQB",x,"QB_DESCRIC"), oReport:ThinLine()})	//"Total do Departamento: "
		oBreakDpt:SetTotalText({|| cTotDpt })
		oBreakDpt:SetTotalInLine(.F.)
	EndIf

	For nI:= 1 to 8
		cParam := StrZero(nI,2)
		If nI == 2 .Or. nI == 9 //Referencia / Imprime Nomes
			Loop
		ElseIf nI == 5 //Situacoes
			aAdd(aFiltros,"(RA_SITFOLH IN (" + fSqlIn(MV_PAR05,1) + "))")
		ElseIf nI == 6 //Categorias
			MV_PAR06 := StrTran(MV_PAR06, '*')
			aAdd(aFiltros,"(RA_CATFUNC IN (" + fSqlIn(MV_PAR06,1) + "))")
		ElseIf ( Len(AllTrim(&('MV_PAR' + cParam ))) > 0)
			aAdd(aFiltros,&('MV_PAR' + cParam ))
		EndIf
	Next nI

	For nI:= 1 to Len(aFiltros)
		If nI == 1
			cWhere := ' WHERE ' + aFiltros[nI]
		Else
			cWhere += ' AND ' + aFiltros[nI]
		EndIf
	Next nI

	//--------------------------------------------------------//
	//Data ou Competencia para pesquisa dos lancamentos       //
	//--------------------------------------------------------//
    dLctIni := MesAno( MonthSub(dRef,1) ) //Competencia anterior a atual
    dLctFim := MesAno( dRef )

	//--------------------------------------------------------//
	// Query dos cabecalho de ferias - SRH                    //
	//--------------------------------------------------------//
	cQryFun += " SELECT RA_FILIAL, RA_MAT, RA_CC, RA_NOME, RA_DEPTO, RH_DFERIAS, RH_DATAINI, RH_DATAFIM,  RH_TIPCAL, RH_DTRECIB"
	cQryFun += " FROM " + RetSqlName("SRA") + " SRA "
	cQryFun += " INNER JOIN "+ RetSqlName("SRH") + " SRH ON( RA_FILIAL = RH_FILIAL AND RA_MAT = RH_MAT AND SRA.D_E_L_E_T_ = '' AND SRH.D_E_L_E_T_ = '') "
	cQryFun += " AND ( ( RH_DATAINI >= '"+ DtoS(FirstDate(dRef)) +"' AND RH_DATAINI <= '"+ DtoS(LastDate(dRef)) + "')"
	cQryFun += " OR    ( RH_DATAFIM >= '"+ DtoS(FirstDate(dRef)) +"' AND RH_DATAFIM <= '"+ DtoS(LastDate(dRef)) + "') )"
	If !Empty(cWhere)
		cQryFun += cWhere
	EndIf
	cQryFun += " ORDER BY " + cOrdFun
	cQryFun	:= ChangeQuery(cQryFun)

	//--------------------------------------------------------//
	// Query dos lancamentos mensais e acumulados - SRC/SRD   //
	//--------------------------------------------------------//
	cFilSRD	:= FWJoinFilial("SRD","SRV")
	cFilSRC	:= FWJoinFilial("SRC","SRV")


	BEGIN REPORT QUERY oSectFun
		DbUseArea(.T., "TOPCONN", TCGenQry(,,cQryFun), cAliasFun, .F., .T.)
	END REPORT QUERY oSectFun

	//--------------------------------------------------------//
	//Inicia a impressao do relatorio                         //
	//--------------------------------------------------------//
	oSectFun:SetHeaderSection(.T.)
	oSectFun:Init()

	If !lImpNome
		oSectFun:Cell("RA_NOME"):Disable()
	EndIf

	Do While (cAliasFun)->(!Eof())

		// Garanto a posição na tabela SRH
		dbSelectArea( "SRH" )
		SRH->(DbSetOrder(2))
		dbSeek( (cAliasFun)->RA_FILIAL + (cAliasFun)->RA_MAT + (cAliasFun)->RH_DATAINI )

		nProv1  := 0
		nProv2  := 0
		nDesc   := 0
		nInssMS := 0
		nLiquido:= 0

		If cUltFil # (cAliasFun)->RA_FILIAL
			cUltFil := (cAliasFun)->RA_FILIAL
		EndIf
		cAliasLct := GetNextAlias()
		cQryLct := " SELECT RD_PD VERBA, RD_VALOR VALOR, RD_DATPGT DDATA, RV_TIPOCOD TIPO"
		cQryLct += " FROM " + RetSqlName("SRD") + " SRD "
		cQryLct += " INNER JOIN "+ RetSqlName("SRV") + " SRV ON( " + cFilSRD + " AND SRV.RV_COD = SRD.RD_PD )"
		cQryLct += " WHERE RD_PD IN ( " + cVerbas + " ) AND ( RD_PERIODO >= '"+ dLctIni +"' AND RD_PERIODO <= '"+ dLctFim +"')"
		cQryLct += " AND RD_FILIAL = '"+ (cAliasFun)->RA_FILIAL +"'"
		cQryLct += " AND RD_MAT = '"+ (cAliasFun)->RA_MAT +"'"
		cQryLct += " AND RD_DATPGT = '"+ (cAliasFun)->RH_DTRECIB +"'"
		cQryLct += " AND SRD.D_E_L_E_T_ = ''"
		cQryLct += " UNION "
		cQryLct += " SELECT RC_PD VERBA, RC_VALOR VALOR, RC_DATA DDATA, RV_TIPOCOD TIPO"
		cQryLct += " FROM "+ RetSqlName("SRC") + " SRC "
		cQryLct += " INNER JOIN "+ RetSqlName("SRV") + " SRV ON( " + cFilSRC + " AND SRV.RV_COD = SRC.RC_PD )"
		cQryLct += " WHERE RC_PD IN ( " + cVerbas + " ) AND ( RC_PERIODO >= '"+ dLctIni +"' AND RC_PERIODO <= '"+ dLctFim +"')"
		cQryLct += " AND RC_FILIAL = '"+ (cAliasFun)->RA_FILIAL +"'"
		cQryLct += " AND RC_MAT = '"+ (cAliasFun)->RA_MAT +"'"
		cQryLct += " AND RC_DATA = '"+ (cAliasFun)->RH_DTRECIB +"'"
		cQryLct += " AND SRC.D_E_L_E_T_ = ''"
		cQryLct	:= ChangeQuery(cQryLct)
		DbUseArea(.T., "TOPCONN", TCGenQry(,,cQryLct), cAliasLct, .F., .T.)

		Do While (cAliasLct)->(!Eof())

			If (cAliasLct)->VERBA $ aCodFol[102,1] //Liquido de Ferias
				nLiquido := (cAliasLct)->VALOR
			EndIf

			If (cAliasLct)->VERBA $ aCodFol[232,1] //INSS Mes Seguinte
				nInssMS := (cAliasLct)->VALOR
			EndIf

			If !(cAliasLct)->VERBA $ aCodFol[102,1] +"/"+ aCodFol[232,1]
				nDesc += (cAliasLct)->VALOR
			EndIf

			(cAliasLct)->(DbSkip())

		EndDo
		(cAliasLct)->(dbCloseArea())
		nProv1 := ( nLiquido + nInssMS ) - nDesc
		nProv2 := ( nLiquido - nProv1  )

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³Define a coluna que os valores serao impressos, pois o valor dos campos³
		³MES, ANTERIOR ou SEGUINTE sera conforme a data de Referencia.          ³
		³Exemplo: Ferias de 20/01/XX a 18/02/XX                                 ³
		³Se a data de Referencia for: 31/01/XX                                  ³
		³O valor de 20/01 a 31/01 sera impresso na coluna "VALOR MES" e o valor ³
		³de 01/02 a 18/02 sera impresso na coluna "VALOR MES SEG."              ³
		³Se a data de Referencia for: 28/02/XX                                  ³
		³O valor de 20/01 a 31/01 sera impresso na coluna "VALOR MES ANT." e o  ³
		³valor de 01/02 a 18/02 sera na coluna "VALOR DO MES"                   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If Empty(nProv1) .And. Empty(nProv2)
			oSectFun:Cell("VL_MESANT"):SetValue( 0.00 )
			oSectFun:Cell("VL_MES"   ):SetValue( 0.00 )
			oSectFun:Cell("VL_MESSEG"):SetValue( 0.00 )
			oSectFun:Cell("VL_TOTAL" ):SetValue( 0.00 )
		Else
			If MesAno( StoD((cAliasFun)->RH_DATAINI) ) < cMesRef
				oSectFun:Cell("VL_MESANT"):SetValue( nProv1 )
				oSectFun:Cell("VL_MES"   ):SetValue( nProv2 )
				oSectFun:Cell("VL_MESSEG"):SetValue( 0.00 )
			Else
				oSectFun:Cell("VL_MESANT"):SetValue( 0.00 )
				oSectFun:Cell("VL_MES"   ):SetValue( nProv1 )
				oSectFun:Cell("VL_MESSEG"):SetValue( nProv2 )
			EndIf
			oSectFun:Cell("VL_TOTAL"):SetValue( nProv1+nProv2 )
		EndIf

		oSectFun:PrintLine()

		(cAliasFun)->(DbSkip())

	EndDo
	oSectFun:Finish()
	(cAliasFun)->(dbCloseArea())

Return()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³fCodVerbas  ³ Autor ³M. Silveira            ³ Data ³22/11/2016³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³Carrega verbas de ferias provento e desconto mes/mes seguinte ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GPER135                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function fCodVerbas()

Local cRet := ""

Fp_CodFol(@aCodFol)

If !Empty( aCodFol )
	//Liquido de Ferias e demais verbas de Ferias do mes seguinte
	cRet := "'" + aCodFol[102,1] + "'," //Liquido Ferias
	cRet += "'" + aCodFol[073,1] + "'," //Pagto Ferias mes sequinte
	cRet += "'" + aCodFol[205,1] + "'," //Abono Mes Seguinte
	cRet += "'" + aCodFol[076,1] + "'," //Medias Fer Val mes seguinte
	cRet += "'" + aCodFol[078,1] + "'," //Adicional 1/3 mes seguinte
	cRet += "'" + aCodFol[206,1] + "'," //1/3 Abono Mes Sequinte
	cRet += "'" + aCodFol[081,1] + "'," //DSR s/ Medias mes sequinte
	cRet += "'" + aCodFol[083,1] + "'," //Medias sobre horas mes seguinte
	cRet += "'" + aCodFol[085,1] + "'," //Outros Adicionais Mes Seg.
	cRet += "'" + aCodFol[160,1] + "'," //Convencao Coletiva Mes Seguinte
	cRet += "'" + aCodFol[344,1] + "'," //Media de Ferias do Comissionado no Mes Seguinte
	cRet += "'" + aCodFol[346,1] + "'," //Media de Ferias do Tarefeiro no Mes Seguinte
	cRet += "'" + aCodFol[637,1] + "'," //Media de Ferias do Professor no Mes Seguinte
	cRet += "'" + aCodFol[1301,1] + "'," //Periculosidade Ferias no Mes Seguinte
	cRet += "'" + aCodFol[232,1] + "',"  //Inss Ferias Mes Seg.
	cRet += "'" + aCodFol[1297,1] + "'," //ATS Ferias no Mes Seguinte
	cRet += "'" + aCodFol[1303,1] + "'," //Periculosidade Ferias no Mes Seguinte
	cRet += "'" + aCodFol[1305,1] + "'," //Insalubridade Ferias no Mes Seguinte
	cRet += "'" + aCodFol[1309,1] + "'," //Adicional Cargo e Confiança Ferias no Mes Seguinte
	cRet += "'" + aCodFol[633,1] +	"'," //Media Horas sobre Abono Mes seguinte
	cRet += "'" + aCodFol[634,1] +	"'," //Media valor sobre Abono Mes seguinte
	cRet += "'" + aCodFol[1331,1] + "'," //DSR s/ Medias Abono Mes Seguinte
	cRet += "'" + aCodFol[1311,1] + "'" //Adicional Transferencia Ferias no Mes Seguinte
EndIf

Return( cRet )
