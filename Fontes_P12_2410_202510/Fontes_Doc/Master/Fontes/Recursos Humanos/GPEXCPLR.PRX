#INCLUDE "GPEXCALC.CH" 
#INCLUDE "PROTHEUS.CH"

Static __lMemCalc

/*/


Ŀ
			ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.			  			
Ĵ
Programador  Data	 FNC			   Motivo da Alteracao 					
Ĵ
Flavio Corre25/02/15PCREQ-2898      Inclusao fonte							
Allyson M   04/08/16TVPCHS          Ajuste p/ verificar IR de PLR j pago  	
Allyson M   15/08/16TVTFOY          Ajuste p/ verificar se h verba lanada 	
Tiago B.    19/07/17MPRIMESP-10402  ajuste em FCalcIRLucro e Calc_IrPLR para  
            	   	    		 correto calculo do IRRF no pgto de PLR.   
ٱ

*/

/*/


Ŀ
Funo     Calc_IrPLR Autor  Gustavo M.		      Data  11.01.13
Ĵ
Descrio  Funcao para Calcular Imp. de Renda sobre PLR               
Ĵ
Sintaxe e                                                             
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
ٱ

*/
Function Calc_IrPLR(nBaseIni,nPenAl,nIrCalc,nBaseRed,nDedDep,nDepeAl,aTabIr,lIrMin,nAliq,aCodFol) 

Local aArea		:= GetArea()
Local cCodIR    := ""
Local cAnoIR    := ""
Local cDtPLR    := ""
Local nPagIR	:= 0

DEFAULT lIrMin	:= If( lIrMin = Nil, .T., lIrMin )
DEFAULT __lMemCalc	:= cPaisLoc == "BRA" .And. fMemCalc() // Memria de Clculo

lAutMEI		:= If( Type("lAutMEI") 		== "U"	, .F.	, lAutMEI)
lResExt		:= If( Type("lResExt") 		== "U"	, .F.	, lResExt)
nPercIRREx	:= If( Type("nPercIRREx") 	== "U"	, 25.00	, nPercIRREx)

//Para MEI nao ira calcular o IR
If lAutMEI
	Return
Endif

If ! lResExt
	nAliq    := 0
	nBaseIni -= nPenAl
	nPenAl   := 0
	nBaseRed := nBaseIni
EndIf

If nBaseIni <= 0
	nDedDep := 0.00
	Return Nil
EndIf

If !lResExt    
	cCodIR   := aCodFol[152,1]
	nPagIR   := 0
	IRPLRANT := 0
	cAnoIR	 := StrZero(Year(DDATA_PGTO),4)
	cDtPLR   := If( (cTipoRot <> "F" .And. !(cTipoRot =='4' .and. cCompl == 'S' .and. Year(dDataDem) <> Year(GetMemVar("RG_DATAHOM"))) ), Alltrim(Str(Ano(dDataDe))) , If(!Empty(aSind) , aSind[1,8] , Substr(CANOMES,1,4) ) )
	//Obtem o valor do IR descontado no mesmo ano de pagamento
	aEval(aPdAcum, {|aPdAcum| If( aPdAcum[1] $ cCodIR .and. SubStr(aPdAcum[4],1,4) == cAnoIR, nPagIR += aPdAcum[3], Nil ) } )
	//Obtem o valor do IR da primeira parcela 
	aEval(aPdAcum, {|aPdAcum| If( aPdAcum[1] $ cCodIR .and. SubStr(aPdAcum[4],1,4) == cDtPLR, IRPLRANT += aPdAcum[3], Nil ) } )
EndIf

// DEDUCAO POR DEPENDENTES
If nDedDep # Nil .and. !lResExt  
	If cModulo = "GPE" 
   		nDedDep := aTabIr[20] * (If(VAL(SRA->RA_DEPIR) > aTabIr[21],aTabIr[21],VAL(SRA->RA_DEPIR)))   
 	Else
 	    nDedDep := aTabIr[20] * (If(VAL(SRA->RA_DEPIR) > aTabIr[21],aTabIr[21],nDedDep))
 	EndIf
Else
	nDedDep := 0.00
EndIf

nBaseRed := nBaseIni - nDedDep 
If nBaseIni > 0 .And. __lMemCalc 
	fAddMemLog("Base do clculo do IR: R$ " + AllTrim(Transform( nBaseIni, "@E 999,999,999,999.99" )),1,2)
	Iif(nDedDep > 0, fAddMemLog("Deduo de dependente: R$ " + AllTrim(Transform( nDedDep, "@E 999,999,999,999.99")) ,1,2), "")
EndIf
If nBaseRed <=  aTabIr[7]  .and. !lResExt
	nBaseRed := 0
	Return Nil
EndIf

// DEDUCAO POR PENSAO ALIMENTICIA
If !lResExt
	If aTabIr[7] - (nBaseRed - nPenAl) < 0
		nDepeAl := nPenAl
	Else
		nDepeAl := nBaseRed - aTabIr[7]
	EndIf
Else
	nDepeAl := 0
EndIf

nBaseRed -= nDepeAl
If nBaseRed <=  aTabIr[7] .and. !lResExt
	nBaseRed := 0
	Return Nil
EndIf

If nBaseRed > 0 .And. nBaseRed<> nBaseIni .And. __lMemCalc 
	fAddMemLog("Base aps dedues: R$ " +  AllTrim(Transform( nBaseRed, "@E 999,999,999,999.99" )),1,2)
EndIf

If !lResExt
	If __lMemCalc 
		fAddMemLog("Clculo do IR PLR sobre a tabela: ",1,2)
	EndIf
	// CALCULO DO IR SOBRE A TABELA
	// SE MENOR OU IGUAL AO VALOR DE ISENCAO VEZES A QUANTIDADE DE MESES, NAO CALCULA NADA
	If nBaseRed <= aTabIr[7]
		nBaseRed := 0              	
		Return Nil
	// SE MENOR QUE FAIXA 2, UTILIZA FAIXA 1 PARA O CALCULO
	ElseIf nBaseRed <= aTabIr[8]
		nIrCalc := (nBaseRed * (aTabIr[9]/100))- aTabIr[10] - nPagIR
		nAliq   := aTabIr[9]
		If __lMemCalc 
			fAddMemLog(AllTrim(Transform(nBaseRed, "@E 999,999,999,999.99" ))+" * "+cvaltochar(aTabIr[9])+"% - "+AllTrim(Transform(aTabIr[10], "@E 999,999,999,999.99" ))+ " (parcela a deduzir do IR) "+IIF(nPagIR > 0," - "+AllTrim(Transform(nPagIR, "@E 999,999,999,999.99" )), "")+" = R$ "+ AllTrim(Transform(nIrCalc, "@E 999,999,999,999.99" )),1,3)
		EndIf
	// SE MENOR QUE FAIXA 3, UTILIZA FAIXA 2 PARA O CALCULO
	ElseIf nBaseRed <= aTabIr[11] 
		nIrCalc := (nBaseRed * (aTabIr[12]/100))- aTabIr[13] - nPagIR
		nAliq   := aTabIr[12]
		If __lMemCalc 
			fAddMemLog(AllTrim(Transform(nBaseRed, "@E 999,999,999,999.99" ))+" * "+cvaltochar(aTabIr[12])+"% - "+AllTrim(Transform(aTabIr[13], "@E 999,999,999,999.99" ))+ " (parcela a deduzir do IR) "+IIF(nPagIR > 0," - "+AllTrim(Transform(nPagIR, "@E 999,999,999,999.99" )), "")+" = R$ "+ AllTrim(Transform(nIrCalc, "@E 999,999,999,999.99" )),1,3)
		EndIf
	// SE MENOR QUE FAIXA 4, UTILIZA FAIXA 3 PARA O CALCULO
	ElseIf nBaseRed <= aTabIr[14]
		nIrCalc := (nBaseRed * (aTabIr[15]/100))- aTabIr[16] - nPagIR
		nAliq   := aTabIr[15]
		If __lMemCalc 
			fAddMemLog(AllTrim(Transform(nBaseRed, "@E 999,999,999,999.99" ))+" * "+cvaltochar(aTabIr[15])+"% - "+AllTrim(Transform(aTabIr[16], "@E 999,999,999,999.99" ))+ " (parcela a deduzir do IR) "+IIF(nPagIR > 0," - "+AllTrim(Transform(nPagIR, "@E 999,999,999,999.99" )), "")+" = R$ "+ AllTrim(Transform(nIrCalc, "@E 999,999,999,999.99" )),1,3)
		EndIf
	// SE MAIOR OU IGUAL FAIXA 4, UTILIZA FAIXA 4 PARA O CALCULO
	Else
		nIrCalc := (nBaseRed * (aTabIr[18]/100))- aTabIr[19] - nPagIR
	    nAliq  := aTabIr[18]
		If __lMemCalc 
			fAddMemLog(AllTrim(Transform(nBaseRed, "@E 999,999,999,999.99" ))+" * "+cvaltochar(aTabIr[18])+"% - "+AllTrim(Transform(aTabIr[19], "@E 999,999,999,999.99" ))+ " (parcela a deduzir do IR) "+IIF(nPagIR > 0," - "+AllTrim(Transform(nPagIR, "@E 999,999,999,999.99" )), "")+" = R$ "+ AllTrim(Transform(nIrCalc, "@E 999,999,999,999.99" )),1,3)
		EndIf
	EndIf
ElseIf lIrResExt  //Residentes no exterior
	nIrCalc	:= nBaseRed * ( nPercIRREx / 100 )
	nAliq  	:= nPercIRREx
EndIf    

nIrCalc = NoRound( nIrCalc , 2)  // DEIXA O VALOR COM 2 CASAS APOS A VIRGULA

// VERIFICA RETENCAO
If nIrCalc < aTabIr[22] .And. lIrMin .and. !lResExt
	nIrCalc  := 0
	nBaseRed := 0
EndIf

RestArea( aArea )    

Return Nil  

/*/


Ŀ
Funo     SemanaMes  Autor  Claudinei Soares       Data  13/09/13
Ĵ
Descrio  Funcao para retornar a quantidade de semanas no Mes.       
Ĵ
Sintaxe e                                                             
Ĵ
Parametros                                                            
Ĵ
 Uso       Calculo do Menor Aprendiz                                  
ٱ

*/

Function SemanaMes(cPeriodo) 
	
Local dData		:= CtoD("//")
Local nSemanas	:= 0 

IF !( "GPEA300" $ FUNNAME() ) 

	dData	:= StoD(cPeriodo+"01") 
	
	Do Case	
		Case F_ULTDIA(dData) == 31
			nSemanas := 4.428571
		Case F_ULTDIA(dData) == 30
			nSemanas := 4.285714
		Case F_ULTDIA(dData) == 29
			nSemanas := 4.142857
		Case F_ULTDIA(dData) == 28 
			nSemanas := 4
	EndCase

ENDIF
	
Return (nSemanas)

/*/


Ŀ
Funo     FPensPLR    autor  Emerson Rosa         Data  15/03/01 
Ĵ
Descrio  Calcula a Pensao Sobre Participacao nos Lucros e Resultados
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
ٱ

*/
Function fPensPLR(cVbPLR, nPercPensao, nValFixo, nNrSlMin, lCalSlLiq, dDataPgto, cVbBaseP, cTpPiso, nVlPiso)

Local lCalSlBru 	:= .F.
Local lSairPens		:= .F.
Local nValPePLR 	:= nBseIrPLR := nIrPLR := nBsePenAl := 0.00
Local nContVez		:= 0
Local nPosP1
Local nVlBase	 	:= 0
Local lUsarIrPLR 	:= .T.
Local aPensAnt		:= {}
Local nValPenAnt	:= 0
Local cDtPLR		:= If( (cTipoRot <> "F" .And. !(cTipoRot =='4' .and. ccompl == 'S' .and. Year(ddatadem) <> Year(GetMemVar("RG_DATAHOM"))) ), Alltrim(Str(Ano(dDataDe))) , If(!Empty(aSind) , aSind[1,8] , Substr(CANOMES,1,4) ) ) //Na Resciso
Local lIRPens	   	:= If( Type("P_LIRPENS") == "U", .F., P_LIRPENS)

Default cTpPiso	 	:= ""
Default nVlPiso	 	:= 0

DEFAULT __lMemCalc 	:= (cPaisLoc == "BRA" .And. fMemCalc())

//Ŀ
// Se nao existir codigo de pensao ou percentual, retorna 		 
//
If Empty(cVbPLR) .OR. (nPercPensao == 0 .And. nValFixo == 0 .And. nNrSlMin == 0)
	If __lMemCalc
		fAddMemLog("Obs.: clculo abortado por no possuir cdigo de penso ou percentuais preenchidos.", 1, 2)
	EndIf
	Return Nil
EndIf

//Ŀ
// Se ja foi lancado o codigo de pensao sobre PLR, retorna      
//
If Ascan(aPd, { |X| X[1] == cVbPLR .AND. X[9] # "D" } ) > 0
	If __lMemCalc
		fAddMemLog("Obs.: clculo abortado devido verba " + cVbPLR + " j existir no clculo.", 1, 2)
	EndIf
	Return Nil
EndIf

//Ŀ
// Verifica se existem os identificadores da PLR e IR da PLR    
//
If aCodfol[151,1] == Space(3) .Or. aCodFol[152,1] == Space(03)
	Return Nil
EndIf

//Ŀ
// Se nao existir codigo de pagamento do PLR, retorna			 
//
nPosP1 := Ascan(aPd, { |x| x[1] == aCodFol[151,1] .And. x[5] > 0 .And. x[9] # "D" })
If nPosP1 == 0
	Return Nil
EndIf

//Ŀ
// Verifica se o IR esta contido nos codigos de base da pensao  
//
lUsarIrPLR := ( Empty(cVbBaseP) .Or. aCodFol[152,1] $ cVbBaseP )

//Ŀ
// Pegar o Valor do Imposto de Renda Calculado					 
//
nIrPLR := 0.00
If lUsarIrPLR
	If __lMemCalc
		fAddMemLog("Obs.: IR ser considerado na base de clculo.", 1, 2)
	EndIf
	nPos2  := Ascan(aPd, { |X| X[1] = aCodFol[152,1] .AND. X[9] # "D" } )
	If nPos2 > 0
		nIrPLR := aPd[nPos2,5]
	EndIf
EndIf

//Ŀ
// Monta a base da pensao de acordo com o cadastro beneficiarios
//
nValPePLR := 0.00
nBsePenAl := 0.00
If nValFixo > 0
	nValPePLR := nValFixo
	If __lMemCalc
		fAddMemLog("Penso sobre valor fixo. Valor da penso: R$ " + AllTrim(Transform( nValPePLR, "@E 999,999,999,999.99" )), 1, 2)
	EndIf
ElseIf nNrSlMin > 0
	nBsePenAl := NoRound(Val_SalMin * nNrSlMin, 2)
	If __lMemCalc
		fAddMemLog("Penso sobre salrio mnimo: ", 1, 2)
		fAddMemLog("Salrio mnimo: R$ " + AllTrim(Transform( Val_SalMin, "@E 999,999,999,999.99" )), 1, 3)
		fAddMemLog("Qtde. salrio(s) mnimo(s): " + cValToChar(nNrSlMin), 1, 3)
		fAddMemLog("Base de clculo: " + cValToChar(nBsePenAl), 1, 3)
	EndIf
Else
	If __lMemCalc
		fAddMemLog("Penso sobre base de clculo:", 1, 2)
		fAddMemLog("Verbas consideradas: ", 1, 3)
	EndIf
	Aeval( aPd ,{ |X| If(X[1]$cVbBaseP.Or.Empty(cVbBaseP),SomaInc(X, 28, @nBsePenAl, Nil, Nil, Nil, Nil, Nil, .F., aCodFol, Nil, Nil, Nil, Nil, Nil, Nil, .T.),"") })
EndIf
Aeval( aPd ,{ |X| If(!(X[1] $ cVbPLR+"/"+aCodFol[1279,1]),SomaInc(X,5,@nBseIrPLR,,,,,,.F.,aCodFol),"") })

// Busca valor de penso descontada em parcela anterior
aPensAnt := fBuscaAcmPer(cVbPLR, "", "V", , , cDtPLR+"01", cDtPLR+"12", ,, , .T. , .F.  )
aEval(aPensAnt, {|x| nValPenAnt += x[3] } )

If lIRPens  // Calcula o IR antes da penso
	If __lMemCalc    
		fAddMemLog("Mnemnico P_LIRPENS: " + cValToChar(lIRPens) + " ("+ retRcaDes("P_LIRPENS")+")", 1, 2)
		fAddMemLog("1 clculo do IR (antes do 1 clculo da penso): ", 1, 2)
	EndIf
	Calc_IrPLR(nBseIrPLR, nValPePLR+nValPenAnt, @nIrPLR,0 ,0.00,, aTabIrPlr,,,@aCodFol) 
EndIf
        
//Ŀ
// Calcular pensao se nao for informado valor fixo 			 
//
If nValFixo == 0
	nValPePLR := NoRound((((nBsePenAl-nIrPLR) * nPercPensao)/100),2)
	If __lMemCalc
		fAddMemLog("Base de clculo da penso: " + AllTrim(Transform( nBsePenAl-nIrPLR, "@E 999,999,999,999.99" )), 1, 2)
		fAddMemLog("1 clculo da penso:" , 1, 2)
		fAddMemLog("IR de PLR: R$ " + AllTrim(Transform( nIrPLR, "@E 999,999,999,999.99" )), 1, 3)
		fAddMemLog("% da penso: " + cValToChar(nPercPensao), 1, 3)
		fAddMemLog("Valor da penso: R$ " + AllTrim(Transform( nValPePLR, "@E 999,999,999,999.99" )), 1, 3)	
	EndIf
EndIf

If cPaisLoc == "BRA" .And. cTpPiso $ "2/3/4"
	If cTpPiso == "2"
		nVlBase	:= nVlPiso
	ElseIf cTpPiso == "3"
		nVlBase := NoRound( (nVlPiso/100) * Val_SalMin, 2 )
	ElseIf cTpPiso == "4"
		nVlBase := NoRound( (nVlPiso/100) * (nBsePenAl-nIrPLR), 2 )
	EndIf
	nValPePLR := fPisoPens( cTpPiso, nValPePLR, nVlBase )
	If __lMemCalc
		fAddMemLog("Obs.: h configurao de piso para penso:", 1, 3)
		fAddMemLog("Tipo do piso: " + cTpPiso, 1, 4)
		fAddMemLog("Valor do piso: R$ " + AllTrim(Transform( nVlBase, "@E 999,999,999,999.99" )), 1, 4)
		fAddMemLog("Valor da penso aps validao do piso: R$ " + AllTrim(Transform( nValPePLR, "@E 999,999,999,999.99" )), 1, 4)
	EndIf
EndIf

If nValPePLR > 0
	FGeraVerba(cVbPLR,nValPePLR,nPercPensao,,,"V",,,,dDataPgto)
Endif

If nValFixo > 0 .Or. nNrSlMin > 0 .Or. (cPaisLoc == "BRA" .And. cTpPiso $ "2/3")
	Return
EndIf

//Ŀ
// Se nao utilizar IR, sair depois de calcular a pensao		 
//
If !lUsarIrPLR
	Return
EndIf

nContVez  := 0
nValPePLR := 0
lCalSlBru := .F.
lSairPens := .F.

While Max((((nBsePenAl-nIrPLR) * nPercPensao)/100),nValPePLR) - Min((((nBsePenAl-nIrPLR) * nPercPensao)/100),nValPePLR) > 0.01 .And. ((lIRPens .And. lCalSlLiq) .OR. !lIRPens)
	nValPePLR := NoRound((((nBsePenAl-nIrPLR) * nPercPensao)/100),2)
	nContVez++
	
	If __lMemCalc .And. nContVez > 1
		fAddMemLog(cValToChar(nContVez) + " clculo da penso:" , 1, 2)
		fAddMemLog("IR de PLR: R$ " + AllTrim(Transform( nIrPLR, "@E 999,999,999,999.99" )), 1, 3)
		fAddMemLog("% da penso: " + cValToChar(nPercPensao), 1, 3)
		fAddMemLog("Valor da penso: R$ " + AllTrim(Transform( nValPePLR, "@E 999,999,999,999.99" )), 1, 3)
	EndIf
	
	If cPaisLoc == "BRA" .And. cTpPiso == "4"
		nVlBase 	:= NoRound( (nVlPiso/100) * (nBsePenAl-nIrPLR), 2 )
		nValPePLR 	:= fPisoPens( cTpPiso, nValPePLR, nVlBase )
		If __lMemCalc .And. nContVez > 1
			fAddMemLog("Obs.: h configurao de piso para penso:", 1, 3)
			fAddMemLog("Tipo do piso: " + cTpPiso, 1, 4)
			fAddMemLog("Valor do piso: R$ " +  AllTrim(Transform( nVlBase, "@E 999,999,999,999.99" )), 1, 4)
			fAddMemLog("Valor da penso aps validao do piso: R$ " + AllTrim(Transform( nValPePLR, "@E 999,999,999,999.99" )), 1, 4)
		EndIf
	EndIf

	//Ŀ
	// Procura codigo de Pensao em aPd								 
	//
	nPos := Ascan(aPd, { |X| X[1] = cVbPLR .AND. X[9] # "D" } )
	If nPos > 0
		If cPaisLoc == "BRA" .And. cTpPiso == "4" .And. nContVez > 1 .And. !(Abs(nValPePLR - aPd[nPos, 5]) > 0.01)
			lSairPens	:= .T.
			If __lMemCalc
				fAddMemLog("Obs.: valor da penso finalizada com o valor de R$ " + AllTrim(Transform( nValPePLR, "@E 999,999,999,999.99" )) + " pois a diferena em relao ao valor anterior de R$ " + AllTrim(Transform( aPd[nPos, 5], "@E 999,999,999,999.99" )) + " no foi maior que R$ 0,01.", 1, 2)
			EndIf
		EndIf
		aPd[nPos,5] := nValPePLR
	EndIf
	
	//Ŀ
	// Se for calculo pelo bruto, cancela apos o primeiro calculo   
	//
	If lCalSlBru .Or. lSairPens
		Exit
	EndIf
	
	nIrPLR    := 0.00

	If Empty(aTabIrPlr)
		CarIrPlr(@aTabIrPlr,MesAno(dDataDe))
	EndIf
	
	If !Empty(aTabIrPlr)
		Calc_IrPLR(nBseIrPLR, nValPePLR+nValPenAnt, @nIrPLR,0 ,0.00,, aTabIrPlr,,,@aCodFol) 
	Else	
		Help("",1,"IRPLR") // "Calculo de IR s/ PLR nao foi realizado devido a falta de informacao na tabela."  
	Endif
	
	//Ŀ
	// Se nao for calculo pelo liquido, indica que e pelo bruto	 
	//
	If !lCalSlLiq
		lCalSlBru := .T.
	EndIf
Enddo

Return .T.

/*/


Ŀ
Funo    fSalBasPLR Autor  Leandro Drumond        Data  03.07.13 
Ĵ
Descrio  Carrega salario base - PLR			                      
Ĵ
Sintaxe    fSalBasPLR()								                  
ٱ

/*/
Function fSalBasPLR()
Local aArea		:= GetArea()
Local aSalario	:= {}
Local cTipoPLR	:= aSind[1,2]
Local cBsPLR	:= aSind[1,3]
Local cSalRef	:= aSind[1,7]
Local dDataAux	:= StoD(cSalRef+"01")
Local nMedAux	:= 0
Local nGTar		:= 0
Local nGComis	:= 0

nSalAnt := 0

If aScan(aPd, { |X| X[1] == aCodfol[151,1] .And. X[9] != "D" } ) == 0
	If cTipoPLR == "1" //Percentual
		If cBsPLR == "1" //Salario Contratual
			If SRA->RA_CATFUNC $ "T*I*J*C" //Tarefeiro, professor e comissionado
				If SRA->RA_CATFUNC == "C"
					nMedAux := Max(DateDiffMonth( dDataDe , dDataAte ),1)
					fPropGComis("2",@nGComis)
				Else
					nMedAux := Max(DateDiffMonth( dTarIni , dTarFim ),1)
					fPropGTar("2",@nGTar)
				EndIf
							
				fCMComiss(aCodFol,@nSalAnt,nMedAux)
				
				If SRA->RA_CATFUNC == "C"
					If nSalAnt < nGComis
						nSalAnt := nGComis
					EndIf
				Else
					If nSalAnt < nGTar
						nSalAnt := nGTar
					EndIf
				EndIf
	        Else		  		
				fBuscaSal(dDataAux,StoD(cSalRef+STRZERO(F_ULTDIA(dDataAux),2)),@aSalario,.F.)
				If !Empty(aSalario)
					nSalAnt := aSalario[Len(aSalario),2]
				Else
					nSalAnt := SRA->RA_SALARIO
				EndIf
			EndIf
			If Empty(nSalAnt)
				If SRA->RA_CATFUNC == "T"
					fPropGTar("2",@nSalAnt)
				ElseIf SRA->RA_CATFUNC == "C"
					fPropGComis("2",@nSalAnt)
				Else
					nSalAnt := fBuscPDSRD(cSalRef,aCodFol[318,1])
				EndIf
			Else
				If SRA->RA_TIPOPGT = "S" .And. SRA->RA_CATFUNC $ "S"
					nSalAnt := nSalAnt / 7 * 30
				ElseIf SRA->RA_CATFUNC $ "H*G"
					nSalAnt := nSalAnt * SRA->RA_HRSMES
				ElseIf SRA->RA_CATFUNC $ "D"
					nSalAnt := nSalAnt * 30
				EndIf		
			EndIf		
		ElseIf cBsPLR == "2" //Remuneracao
			nSalAnt := fBuscPDSRD(cSalRef,aCodFol[318,1])
		ElseIf cBsPLR == "3" //Piso Sindicato
			nSalAnt := aSind[1,9]
		ElseIf cBsPLR == "4" //Salario Minimo
			nSalAnt := Val_SalMin
		EndIf
		nSalAnt := nSalAnt * ( aSind[1,10] / 100 )
	Else //Valor Fixo
		nSalAnt := aSind[1,4]
	EndIf
EndIf

RestArea(aArea)

Return Nil

/*/


Ŀ
Funo    fAvosPLR   Autor  Leandro Drumond        Data  03.07.13 
Ĵ
Descrio Retornar Numero de Avos de um Periodo Verificando se		  
			 Periodo no mes for Superior a 15 Dias Trabalhados			  
Ĵ
Sintaxe    fAvosPLR(@nAvos,dDataLim)					              
ٱ

/*/
Function fAvosPLR(nAvos,dDataLim)
Local dDtIni	:= CtoD("")
Local dDataAux	:= CtoD("")

dDataAux := YearSub( dDataLim , 1 )+1 //Data inferior limite para obter 12 avos - 01/01/aaaa

nAvos := 0

If SRA->RA_ADMISSA <= dDataAux
	dDtIni := dDataAux
ElseIf SRA->RA_ADMISSA < dDataLim
	dDtIni := SRA->RA_ADMISSA
EndIf

If !Empty(SRA->RA_DEMISSA)
	dDataLim := Min(dDataLim,SRA->RA_DEMISSA) //Calcula somente at a data de demissao
	dDataAux := dDataLim
EndIf

If !Empty(dDtIni)
	If dDtIni == dDataAux
		nAvos := 12
	Else
		dDataAux := dDataLim
		While MesAno(dDataAux) > MesAno(dDtIni)  //Soma um avo por cada mes completo trabalhado
			nAvos++
			dDataAux := MonthSub( dDataAux , 1 )
		EndDo
		dDataAux := StoD(MesAno(dDtIni) + STRZERO(F_ULTDIA(dDtIni),2))
		If ( dDataAux - dDtIni ) + 1 >= 15 //Se no mes de admissao tiver trablhado mais que 15 dias, soma um avo.
			nAvos++
		EndIf
		//Se a data limite for a data de demissao, verifica se o funcionario trabalhou mais de 15 dias no mes
		If (dDataLim - FirstDate(dDataLim)) + 1 < 15   
			nAvos--
		Endif	
	
	EndIf
	nAvos := If (nAvos < 0 , 0 , nAvos)
EndIf

Return


/*


ͻ
FunctionfChkPLRAnt	   Autor  Leandro Drumond   Data   13/08/2013 
͹
Desc.   Verifica se houve pagamento de PLR no ano atual 		      
                                                                      
͹
Uso      Roteiro de Calculo                                           
ͼ

*/
Function fChkPLRAnt(nValPlrAnt,aPdAcm,cCodIr,nBsIrAnt,cDtPLR)

Local lRet			:= .F.
Local cAnoIR   		:= ""

DEFAULT cCodIR 	 := aCodFol[151,1] + "/" + P_PDBIRPLR
DEFAULT aPdAcm   := aPdAcum
DEFAULT nBsIrAnt := 0 //Soma apenas se a primeira parcela ter sido paga no mesmo ano
DEFAULT cDtPLR   := ""

If cTipoRot <> '4'
	cAnoIR := StrZero(Year(DDATA_PGTO),4)
Else
	cAnoIR := StrZero(Year(GetMemVar("RG_DATAHOM")),4)
EndIf

nValPlrAnt := 0

aEval(aPdAcm, {|x| If( x[1] $ cCodIR, ( If( Empty(cDtPLR) .or. SubStr(x[4],1,4) == cDtPLR, nValPlrAnt += x[3], Nil), If(SubStr(x[4],1,4) == cAnoIR, nBsIrAnt += x[3], Nil) ), Nil ) } )

If nValPlrAnt > 0
	lRet := .T.
Endif

Return lRet
