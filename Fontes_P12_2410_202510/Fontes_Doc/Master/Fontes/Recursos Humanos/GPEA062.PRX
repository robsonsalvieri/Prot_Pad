#INCLUDE "TOTVS.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "GPEA062.CH"

Static lAltCampos  	:= .F.
                            
/*/


Ŀ
Funo    	 GPEA062     Autor  Equipe Inovao RH          Data  04/02/2015   
Ĵ
Descrio 	 Cadastro de Agrupamento de Benefcios                        			
Ĵ
Sintaxe   	 GPEA062()                                                    			
Ĵ
 Uso      	 Generico ( DOS e Windows )                                   			
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.               			
Ĵ
Programador   Data      FNC			  Motivo da Alteracao                      
Ĵ
Allyson M.   26/09/2016TVVKRA 		    Ajuste em fRetCriter() p/ no considerar a 
						   				filial logada (ir considerar a do filtro) 
Isabel N.    08/09/2017DRHPAG-5695     Alterada configurao mxima dos critrios 
                                       de benefcios, de 5 p/ at 7.              
ٱ

*/
Function GPEA062()

	Local oMBrowse
	Local cFiltraRh

//CHRIS
	oMBrowse := FWMBrowse():New()

	oMBrowse:SetAlias("SJQ")
	oMBrowse:SetDescription(OemToAnsi(STR0001)) //'Cadastro de Agrupamento de Benefcios'

	//Ŀ
	// Inicializa o filtro utilizando a funcao FilBrowse                      
	//
	cFiltraRh := CHKRH("GPEA062","SJQ","1")
	oMBrowse:SetFilterDefault( cFiltraRh )

	oMBrowse:Activate()

Return

/*                                	
Ŀ
Funo     MenuDef		Autor  Mauricio Takakura Data 31/10/2010
Ĵ
Descrio Criacao do Menu do Browse.                                  
Ĵ
Sintaxe   < Vide Parametros Formais >									
Ĵ
 Uso      GPEA062                                                     
Ĵ
 Retorno  aRotina														
Ĵ
Parametros< Vide Parametros Formais >									
*/
Static Function MenuDef()

	Local aRotina := {}

	ADD OPTION aRotina TITLE STR0002  ACTION "PESQBRW"         		OPERATION 1 ACCESS 0 DISABLE MENU 	//'Pesquisar'
	ADD OPTION aRotina TITLE STR0003  ACTION "VIEWDEF.GPEA062" 		OPERATION 2 ACCESS 0 				//'Visualizar'
	ADD OPTION aRotina TITLE STR0004  ACTION "VIEWDEF.GPEA062" 		OPERATION 3 ACCESS 0				//'Incluir'
	ADD OPTION aRotina TITLE STR0005  ACTION "VIEWDEF.GPEA062" 		OPERATION 4 ACCESS 0				//'Alterar'
	ADD OPTION aRotina TITLE STR0006  ACTION "VIEWDEF.GPEA062" 		OPERATION 5 ACCESS 0				//'Excluir']
	ADD OPTION aRotina TITLE STR0027  ACTION "GPER013()" 				OPERATION 2 ACCESS 0 			//'Visualizar'
	
Return aRotina

/*
Ŀ
Funo     ModelDef 		Autor  Mauricio Takakura Data 31/10/2010
Ĵ
Descrio Regras de Modelagem da gravacao.                            
Ĵ
Sintaxe   < Vide Parametros Formais >									
Ĵ
 Uso      GPEA062                                                     
Ĵ
 Retorno  aRotina														
Ĵ
Parametros< Vide Parametros Formais >									
*/
Static Function ModelDef()
	Local oModel
	Local oStructSJQ
	Local oStructSJS
	
	Local bPosValid	:= {|oMdl| Gpea062TudOk(oMdl)}
	
	// Criacao do Objeto de Modelagem de dados da tabela SJQ //
	oModel     := MPFormModel():New("GPEA062", NIL, bPosValid, NIL)

	// Estrutura de campos do Model //
	oStructSJQ := FWFormStruct(1, "SJQ")          
	oStructSJQ:RemoveField("JQ_FILIAL")   
	
	oModel:AddFields("GPEA062_SJQ", NIL, oStructSJQ)
	oModel:SetPrimaryKey({"JQ_CODIGO"})
	
	oStructSJS := FWFormStruct(1,"SJS")
	oStructSJS:SetProperty( '*' 	, MODEL_FIELD_WHEN ,{ |oModel| When_NotEdit(oModel) })
	
	oModel:AddGrid("GPEA062_SJS", "GPEA062_SJQ"/*cOwner*/, oStructSJS , { |oGrid, nLine, cAction, cField| Gp062DelOk(oGrid, nLine, cAction, cField) }/*bLinePre*/, { |oModel| SJS_LinhaOK(oModel) }/* bLinePost*/, /*bPre*/, /*bPost*/,/*bLoad*/)
	oModel:GetModel('GPEA062_SJS'):SetOptional(.T.)
	
	oModel:SetDescription(OemToAnsi(STR0001)) // "Cadastro de Agrupamento de Benefcios"

	oModel:SetRelation( "GPEA062_SJS", { { 'JS_FILIAL', 'xFilial( "SJQ" )' }, { 'JS_CDAGRUP', 'JQ_CODIGO' }} , SJS->( IndexKey( 2 ) ) )
	oModel:GetModel( "GPEA062_SJS" ):SetUniqueLine( { 'JS_TABELA' } )
	
	oModel:SetActivate( { |oModel| fInitModel(oModel,oModel:GetOperation()) } )
	
Return( oModel )

/*                                	
Ŀ
Funo     ViewDef  		Autor  Mauricio Takakura Data 31/10/2010
Ĵ
Descrio Regras de Interface com o Usuario                           
Ĵ
Sintaxe   < Vide Parametros Formais >									
Ĵ
 Uso      GPEA030                                                     
Ĵ
 Retorno  aRotina														
Ĵ
Parametros< Vide Parametros Formais >									
*/
Static Function ViewDef()
	Local oView 
	Local oModel
	Local oStructSJQ
	Local oStructSJS
	
	oModel := FWLoadModel("GPEA062")
	
	// Criacao da Interface //
	oView := FWFormView():New()
	oView:SetModel(oModel)
	
	// Vincular o View ao Model //
	oStructSJQ := FWFormStruct(2, "SJQ")
	oStructSJQ:RemoveField("JQ_FILIAL")

	oView:SetModel(oModel)
	oView:AddField("GPEA062_SJQ", oStructSJQ)
	
	oStructSJS := FWFormStruct(2,"SJS")
	
	oView:AddGrid(  "GPEA062_SJS" , oStructSJS )

	oStructSJS:RemoveField( "JS_FILIAL" )
	oStructSJS:RemoveField( "JS_CDAGRUP" )
	
	oStructSJS:SetProperty( 'JS_SEQ'  , MVC_VIEW_ORDEM, "1" )
	oStructSJS:SetProperty( 'JS_TABELA'  , MVC_VIEW_ORDEM, "2" )
	oStructSJS:SetProperty( 'JS_CAMPO'  , MVC_VIEW_ORDEM, "3" )
		
	oView:createHorizontalBox("FORMFIELD",20)
	oView:createHorizontalBox("GRID"     ,80)

	oView:SetOwnerView( "GPEA062_SJQ","FORMFIELD")
	oView:SetOwnerView( "GPEA062_SJS","GRID")
	
	oView:EnableTitleView( "GPEA062_SJS", OemToAnsi(STR0016) ) //"Hierarquia para Agrupamento do Benefcio"
	
	oView:AddIncrementField("GPEA062_SJS","JS_SEQ")
	
	If oModel:GetOperation() <> 3 //Altera
		oView:GetStruct('GPEA062_SJQ'):SetProperty('JQ_FILREF'  , MODEL_FIELD_NOUPD  , .T.)
		oView:GetStruct('GPEA062_SJQ'):SetProperty('JQ_CODIGO'  , MODEL_FIELD_NOUPD  , .T.)
		oView:GetStruct('GPEA062_SJQ'):SetProperty('JQ_PERINI'  , MODEL_FIELD_NOUPD  , .T.)
	EndIf
oView:SetCloseOnOk({ || .T. }) //Fecha tela apos commit
		
Return oView


/*


Ŀ
Funcao     fCrgTabBen   Autor  Emerson Campos    Data  04/02/2015 
Ĵ
Descricao  Monta Combo com os codigos de outros beneficios            
Ĵ
Uso        GPEA064                                                    
ٱ

*/
Function fCrgTabBen()
Local cOpcBox     	:= ""
Local lTemCCT   	:= AliasInDic("SWY")

If fIntRHGS()
	cOpcBox := OemToAnsi(STR0010) //SRA=Funcionario;ABS=Local de Trabalho;SQB=Departamento;CTT=Centro de Custo;RCL=Posto;SQ3=Cargo;SRJ=Funo;SR6=Turno;RC3=Sindicato
Else
	cOpcBox := OemToAnsi(STR0026) 
EndIf

If lTemCCT 
	cOpcBox += ";" + STR0030 //Conveno Coletiva
EndIf

Return( cOpcBox )


/*


ͻ
Funcao     fVldPer      Autor Natie Sugahara       Data   09/11/07    
͹
Descricao  Valida o Periodo e o Numero de Pagto.						   
͹
Uso        X3_VALID -  JS_DTINI                                          
ͼ

*/
Function fValidPer(nTipo) 
Local oModel 	:= FWModelActivate()
Local aArea	:= GetArea()
Local cPer	:= ""
Local nAno	:= 0
Local nMes	:= 0	
	
	// Forca nomenclatura de codigo de periodo formato AAAA/MM
	If nTipo == 1	
		If Type(oModel:GetValue("GPEA062_SJQ","JQ_PERINI")) != "U"   
			cPer := oModel:GetValue("GPEA062_SJQ","JQ_PERINI")
			nAno := Val(SubStr(cPer, 0, 4))
			nMes := Val(SubStr(cPer, 5, 2))
			
			If (nAno < 1900 .Or. nAno > 3000 ) .Or. (nMes < 1 .Or. nMes > 12 )
				Help(" ", 1, "Help",, OemToAnsi(STR0012), 1, 0) // "Data Invalida! Verifique formato (AAAAMM)."
				Return(.F.)
			EndIf
		
			//Verifica se existe perodo cadastrado na tabela RCH
			RCH->( dbSetOrder(RetOrder("RCH","RCH_FILIAL+RCH_ANO+RCH_MES")))
			If !RCH->( dbSeek(xFilial("RCH")+ cPer))
				Help(" ", 1, "Help",, OemToAnsi(STR0011), 1, 0) // "Perodo no cadastrado."
				Return(.F.)
			Endif
		
		EndIf
	Else
		If Type(oModel:GetValue("GPEA062_SJQ","JQ_PERFIM")) != "U" .And. !Empty(oModel:GetValue("GPEA062_SJQ","JQ_PERFIM"))   
			cPer := oModel:GetValue("GPEA062_SJQ","JQ_PERFIM")
			nAno := Val(SubStr(cPer, 0, 4))
			nMes := Val(SubStr(cPer, 5, 2))
			
			If (nAno < 1900 .Or. nAno > 3000 ) .Or. (nMes < 1 .Or. nMes > 12 )
				Help(" ", 1, "Help",, OemToAnsi(STR0012), 1, 0) // "Data Invalida! Verifique formato (AAAAMM)."
				Return(.F.)
			EndIf
			
			If !Empty(oModel:GetValue("GPEA062_SJQ","JQ_PERINI")) .And. oModel:GetValue("GPEA062_SJQ","JQ_PERFIM") < oModel:GetValue("GPEA062_SJQ","JQ_PERINI")
				Help(" ", 1, "Help",, OemToAnsi(STR0009), 1, 0) // "O perodo final deve ser igual ou posterior ao perodo inicial."
				Return(.F.)
			Endif
			//Verifica se existe perodo cadastrado na tabela RCH
			RCH->( dbSetOrder(RetOrder("RCH","RCH_FILIAL+RCH_ANO+RCH_MES")))
			If !RCH->( dbSeek(xFilial("RCH")+ cPer))
				Help(" ", 1, "Help",, OemToAnsi(STR0011), 1, 0) // "Perodo no cadastrado."
				Return(.F.)
			Endif			
		EndIf	
	Endif
Return( .T. )


/*


ͻ
Funcao     fVldTabela   Autor Christiane Vieira    Data   05/02/2015  
͹
Descricao  Preencher a sequncia quando informada a tabela SRA - Func.	   
͹
Uso        X3_VALID -  JS_TABELA                                         
ͼ

*/
Function fVldTabela() 
Local oModel 	:= FWModelActivate()
Local oGrid   	:= oModel:GetModel("GPEA062_SJS")
Local cTabela 	:= GetMemVar("JS_TABELA")
Local cCampo	:= ""

	If !(Empty(cTabela))
		Do Case
			Case ( cTabela == "SRA" ) ; ( cCampo := "RA_MAT" )	
			Case ( cTabela == "ABS" ) ; ( cCampo := "ABS_LOCAL" )
			Case ( cTabela == "SQB" ) ; ( cCampo := "QB_DEPTO" )
			Case ( cTabela == "CTT" ) ; ( cCampo := "CTT_CUSTO" )
			Case ( cTabela == "SQ3" ) ; ( cCampo := "Q3_CARGO" )
			Case ( cTabela == "RCL" ) ; ( cCampo := "RCL_POSTO" )
			Case ( cTabela == "SRJ" ) ; ( cCampo := "RJ_FUNCAO" )
			Case ( cTabela == "SR6" ) ; ( cCampo := "R6_TURNO" )
			Case ( cTabela == "RCE" ) ; ( cCampo := "RCE_CODIGO" )
			Case ( cTabela == "SM0" ) ; ( cCampo := "M0_CODFIL" )
			Case ( cTabela == "SA1" ) ; ( cCampo := "A1_COD" )
			Case ( cTabela == "TDX" ) ; ( cCampo := "TDX_TURNO" )	 
			Case ( cTabela == "SWY" ) ; ( cCampo := "WY_CODIGO" )
		End Case	
		
		oGrid:SetValue("JS_CAMPO", cCampo)
	Endif	

Return( .T. )

/*


ͻ
Funcao     fVldSeq      Autor Christiane Vieira    Data   05/02/2015  
͹
Descricao  Validar a sequncia quando informada a tabela SRA-Funcionrio   
͹
Uso        X3_VALID -  JS_SEQ                                            
ͼ

*/
Function fVldSeq() 
Local oModel 	:= FWModelActivate()
Local oGrid   	:= oModel:GetModel("GPEA062_SJS")
Local cSeq		:= GetMemVar("M->JS_SEQ")
Local cTabela 	:= ""
	
	cTabela	:= oGrid:GetValue("JS_TABELA")
		
	If Val(cSeq) < 1 .Or. Val(cSeq) > 7
		Help(" ", 1, "Help",, OemToAnsi(STR0015), 1, 0) //A sequncia deve estar entre '01' e '07'.
		Return(.F.)	 
	Endif
	
	If cTabela == "SRA" .And. cSEQ != "01"
		Help(" ", 1, "Help",, OemToAnsi(STR0013), 1, 0) //Para a tabela de Funcionrios(SRA), a sequncia dever ser sempre '01'.
		Return(.F.)
	ElseIf cTabela != "SRA" .And. cSEQ == "01"
		Help(" ", 1, "Help",, OemToAnsi(STR0014), 1, 0) //A sequncia '01' dever ser utilizada apenas para a tabela de Funcionrios(SRA)."
		Return(.F.)
	Endif

Return( .T. )

/*
Ŀ
Fun??o    Gpea062TudOk Autor Christiane Vieira       Data 22/06/2004
Ĵ
Descri??o Validacao dos campos antes da gravao                        
Ĵ
Sintaxe   Gpea062TudOk( )										      	  
Ĵ
Parametros                                                              
Ĵ
 Uso      GPEA062()                                                     
*/
Function Gpea062TudOk(oModelGrid) 
Local cCampos	:= ''
Local lTudoOK 	:= .T.     
Local oModel  	:= oModelGrid:GetModel('GPEA062_SJQ')

Local oModelDet	:= oModelGrid:GetModel("GPEA062_SJS")
Local oGrid   	:= oModelDet:GetModel("GPEA062_SJS")
Local nLinhas	:= oModelDet:GetQtdLine()

Local nLinGrid	:= 0
Local nLinAux	:= 0

	If (oModelGrid:GetOperation() <> 5) 
		// Quando nao for exclusao, ele valida os campos da tela
		If oModelGrid:GetOperation() == 3 //Incluso
			//Verificar se j existe outro agrupamento cadastrado com o mesmo cdigo
			dbSelectArea("SJQ")
			DbSetOrder( RetOrder( "SJQ", "JQ_FILIAL+JQ_CODIGO" ) )
			If dbSeek( xFilial("SJQ") +  oModel:GetValue('JQ_CODIGO')) 
				Help(" ", 1, OemToAnsi(STR0008),, OemToAnsi(STR0020), 1, 0) // "J existe um agrupamento cadastrado com este cdigo."
				lTudoOK := .F.     
				Return            
			Endif
		Endif

		//Verificar se j existe criterio ativo	
		If oModel:GetValue('JQ_STATUS') == "1"
			SJQ->(DbSetOrder( 6 ))//JQ_FILIAL+JQ_STATUS+JQ_FILREF                                                                                                                                   
			If dbSeek( xFilial("SJQ")+ "1" + oModel:GetValue('JQ_FILREF'))
				If SJQ->JQ_CODIGO <> oModel:GetValue('JQ_CODIGO')
					Help(" ", 1, OemToAnsi(STR0008),, OemToAnsi(STR0023), 1, 0) //"J existe configurao de critrio com o Status ATIVO, s  permitido um criterio ativo."                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                   
					lTudoOK := .F.    
					Return
				EndIf             
			Endif			
		EndIf
		
		//Verificar se j existe registro para o mesmo tipo de beneficio em perodo que contempla o perodo informado.
		If lTudoOK
			dbSelectArea("SJQ") 
			SJQ->(DbSetOrder( 1 ))//JQ_FILIAL+JQ_FILREF+JQ_PERINI"
			SJQ->(dbSeek( xFilial("SJQ") + oModel:GetValue('JQ_FILREF')))

			While !EOF() .AND. xFilial('SJQ') + oModel:GetValue('JQ_FILREF')  == SJQ->JQ_FILIAL+SJQ->JQ_FILREF
				IF ((oModel:GetValue('JQ_PERINI') >= SJQ->JQ_PERINI .And. (Empty(SJQ->JQ_PERFIM) .OR. oModel:GetValue('JQ_PERINI') <= SJQ->JQ_PERFIM )) .Or. ;
				   (oModel:GetValue('JQ_PERINI') <= SJQ->JQ_PERINI .And. (Empty(oModel:GetValue('JQ_PERFIM')) .OR. oModel:GetValue('JQ_PERFIM') >= SJQ->JQ_PERINI )))
					If SJQ->JQ_CODIGO <> oModel:GetValue('JQ_CODIGO')
						Help(" ", 1, OemToAnsi(STR0008),, OemToAnsi(STR0022), 1, 0) // "J existe configurao de agrupamento para este benefcio no perodo informado."
						lTudoOK := .F.
						Exit
					EndIf
				Endif
				dbSkip()
			Enddo			
		Endif
		
		//Verificar se foram informadas as tabelas para hierarquia
		If nLinhas == 1
			If Empty(oModelDet:GetValue( 'JS_TABELA', 1 )) .And. Empty(oModelDet:GetValue( 'JS_SEQ', 1 ))
				Help(" ", 1, OemToAnsi(STR0008),, OemToAnsi(STR0019), 1, 0) // " necessrio informar ao menos uma tabela para realizar o agrupamento de beneficios."
				lTudoOK := .F.
				Return
			Endif			
		Endif	
		
		//Validar se existem sequncias duplicadas
		For nLinGrid := 1 to nLinhas
			For nLinAux := nLinGrid + 1 to nLinhas
				IF oModelDet:GetValue('JS_SEQ', nLinGrid) == oModelDet:GetValue('JS_SEQ', nLinAux) .And. !oModelDet:IsDeleted(nLinGrid)
					Help(" ", 1, OemToAnsi(STR0008),, OemToAnsi(STR0017), 1, 0) // "Existem sequncias duplicadas, altere o sequenciamento dos registros."
					lTudoOK := .F.
					Exit				
				Endif 
			Next nLInAux
		Next nLinGrid
		
		//Verificar se o registro do funcionrio foi deletado
		If lTudoOK
			For nLinGrid := 1 to nLinhas
				If oModelDet:IsDeleted(nLinGrid) .And. oModelDet:GetValue('JS_TABELA', nLinGrid) == "SRA" .And. oModelDet:GetValue('JS_SEQ', nLinGrid) == "01" 
					MsgAlert( OemToAnsi(STR0021), OemToAnsi(STR0008) ) // "O registro referente ao cadastro de funcionrios no pode ser excludo. O mesmo ser recuperado."
					oModelDet:GoLine(nLinGrid)
					oModelDet:UnDeleteLine()
					Exit
				Endif
			Next
		Endif		
	EndIf

Return lTudoOK

/*/


Ŀ
Funo    fInitModel    Autor  Christiane Vieira      Data  07/02/15 
Ĵ
Descrio Carrega enchoice apos ativacao do modelo.                      
Ĵ
Sintaxe   fInitModel(oModel,nOperation)                                  
ٱ

/*/
Static Function fInitModel(oModel,nOperation)

Local oModelDet	:= oModel:GetModel("GPEA062_SJS")
Local oGrid   	:= oModel:GetModel("GPEA062_SJS")

If nOperation == MODEL_OPERATION_INSERT

	oGrid:LoadValue("JS_CAMPO", "RA_MAT")
	oGrid:LoadValue("JS_TABELA", "SRA")
	oGrid:LoadValue("JS_SEQ", "01")
Endif

Return Nil

/*/


Ŀ
Funo    SJS_LinhaOK  Autor  Flavio S. Correa       Data  01/04/15 
Ĵ
Descrio Linha OK do grid SJS		               			 	        
Ĵ

/*/
Static Function SJS_LinhaOK(oGrid)
Local lRet 		:= .T.
Local nLinhas	:= oGrid:Length(.T.)
Local oModel	:= FwModelActive()

If nLinhas > 7
	Help(" ", 1, "Help",, OemToAnsi(STR0024), 1, 0) //"Numero mximo de sequncias  07"
	lRet := .F.	 
EndIf

Return lRet

/*/


Ŀ
Funo    fRetCriter  Autor  Flavio S. Correa       Data  01/04/15 
Ĵ
Descrio retornar o cdigo do critrio ativo. Esta funo ser  		
            utilizada pelo mdulo de Gesto de Servios no momento da  	
            gravao do arquivo de relacionamento entidade x benefcios  
            (lanamento de vnculos).	               			 	    
Ĵ

/*/
Function fRetCriter(cFiltro, cEnt)
Local aArea 	:= GetArea()
Local cCod		:= ""
Local cQry 		:= GetNextAlias()
Local cFilSJQ	:= ""
Local cWhere	:= '%1=1%'
Default cFiltro := cFilant
Default cEnt := ""

If VALTYPE(cEnt) == 'C' .AND. !EMPTY(cEnt)
	cWhere := "JS_TABELA = '" + cEnt + "' "
	cWhere := "%"+cWhere+"%"
EndIf

cFilSJQ := xFilial("SJQ", cFiltro)

BEGINSQL ALIAS cQry
	SELECT JQ_CODIGO
	FROM %table:SJQ% SJQ INNER JOIN %Table:SJS% SJS
	ON JQ_CODIGO = JS_CDAGRUP AND JS_FILIAL = JQ_FILIAL
	WHERE SJQ.%notDel% AND %exp:cWhere%
	AND SJS.%notDel%
	AND JQ_FILIAL = %exp:cFilSJQ%
	AND JQ_STATUS = %exp:'1'%
	AND JQ_FILREF IN (%exp:cFiltro%,%exp:''%)
	ORDER BY JQ_FILREF DESC
ENDSQL

If !(cQry)->(Eof())
	cCod := (cQry)->JQ_CODIGO
EndIf
(cQry)->(dbCloseArea())


RestArea(aArea)
Return cCod


/*/


Ŀ
Funo    fIntRHGS  Autor  Flavio S. Correa       Data  01/04/15 
Ĵ
Descrio retornar o contedo do parmetro MV_TECXRH.			 		
Ĵ

/*/
Function fIntRHGS()
Local lRet := SuperGetMV("MV_TECXRH",,.F.)
Return lRet

/*


Ŀ
Funo    Gp062DelOk Autor  Flavio Correa          Data  07.04.15 
Ĵ
Descrio Valida exclusao da linha                                    
Ĵ
 Uso       Generico                                                   
ٱ

*/
Static Function Gp062DelOk(oGrid, nLine, cAction, cField)
Local lRet 			:= .T.
Local oModel 		:= oGrid:GetModel()
Local nOperation 	:= oModel:GetOperation()

If cAction == 'DELETE' .and. nOperation == MODEL_OPERATION_UPDATE

	If nLine == 1
		Help( , ,'Help', ,STR0021,1,0) //
		lRet := .F.
		oGrid:UnDeleteLine()
	EndIf
	
EndIf

Return lRet

/*


Ŀ
Funo    When_NotEdit Autor  Flavio Correa          Data  07.04.15 
Ĵ
Descrio When da linha 1                                  
Ĵ
 Uso       Generico                                                   
ٱ

*/
Static Function When_NotEdit(oGrid)
Local lRet	:= .T.

If oGrid:GetLine() == 1 //.And. oGrid:GetValue("JS_TABELA") == "SRA" .And. oGrid:GetValue('JS_SEQ', nLinGrid) == "01" 
	lRet := .F.
EndIf
 
Return lRet
