#INCLUDE "PROTHEUS.CH"
#INCLUDE "APDA170.CH"
/*/
зддддддддддбддддддддддбдддддбдддддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁAPDA170   ЁAutorЁMarinaldo de Jesus       Ё Data Ё16/07/2003Ё
цддддддддддеддддддддддадддддадддддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Participantes/Pessoas vs Visoes	                Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico                                                    Ё
цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё            ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL           Ё
цддддддддддддбддддддддддбдддддддддддбддддддддддддддддддддддддддддддддддд╢
ЁProgramador ЁData      ЁBOPS       ЁMotivo da Alteracao                Ё
цддддддддддддеддддддддддедддддддддддеддддддддддддддддддддддддддддддддддд╢
ЁCecilia Car.Ё03/07/2014ЁTPZWBQ     ЁIncluido o fonte da 11 para a 12 e Ё
Ё            Ё          Ё           Ёefetuada a limpeza.                Ё
ЁFlavio Corr.Ё25/08/2014ЁTQGT43     ЁLiberaГЦo do Grid VisУes para ediГЦo Ё
ЁEmerson CampЁ23/01/2015ЁTRHOYE     ЁCom o Alias esta repetindo a mesma Ё
Ё            Ё          Ё           ЁdescriГЦo do item da visЦo, fiz    Ё
Ё            Ё          Ё           Ёdiversos testes e nЦo ocorreu uma  Ё
Ё            Ё          Ё           Ёfalha pela falta do Alias          Ё
ЁFlavio C.   Ё17/01/2017ЁMRH-4991   Ё Foi corrigido a rotina para que o Ё
Ё            Ё          Ё           ЁnЦo altere o participante em ediГЦoЁ
Ё            Ё          Ё           Ёquando consulta o Mentor atravИs   Ё
Ё            Ё          Ё           Ёda Consulta PadrЦo(F3)Com o Alias  Ё
Ё            Ё          Ё           Ёesta repetindo a mesma				Ё
юддддддддддддаддддддддддадддддддддддаддддддддддддддддддддддддддддддддддды/*/
Function APDA170( cAlias , nReg , nOpc , lExecAuto )

Local aArea		:= GetArea()
Local aAreaRde	:= RDE->( GetArea() )
Local aAreaRd0	:= RD0->( GetArea() )
Local aAreaRdk	:= RDK->( GetArea() )
Local aAreaRD4	:= RD4->( GetArea() )
Local aAreaRdz	:= RDZ->( GetArea() )
Local aAreaSra	:= SRA->( GetArea() )
Local lExistOpc	:= ( ValType( nOpc ) == "N" )

Local bBlock
Local nPos

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSo Executa se o Modo de Acesso dos Arquivos do Modulo APD estiЁ
	Ёverem OK													   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( ApdRelationFile() )
		Break
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁRedefine o Alias                                              Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	cAlias	:= "RD0"

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define Array contendo as Rotinas a executar do programa      Ё
	Ё ----------- Elementos contidos por dimensao ------------     Ё
	Ё 1. Nome a aparecer no cabecalho                              Ё
	Ё 2. Nome da Rotina associada                                  Ё
	Ё 3. Usado pela rotina                                         Ё
	Ё 4. Tipo de Transa┤└o a ser efetuada                          Ё
	Ё    1 - Pesquisa e Posiciona em um Banco de Dados             Ё
	Ё    2 - Simplesmente Mostra os Campos                         Ё
	Ё    3 - Inclui registros no Bancos de Dados                   Ё
	Ё    4 - Altera o registro corrente                            Ё
	Ё    5 - Remove o registro corrente do Banco de Dados          Ё
	Ё    6 - Copiar                                                Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	Private aRotina := MenuDef() // ajuste para versao 9.12 - chamada da funcao MenuDef() que contem aRotina

	Private cCadastro
	IF ( cModulo == "APD" )
		cCadastro   := OemToAnsi( STR0007 )	//"Participantes vs Visoes"
	Else
		cCadastro   := OemToAnsi( STR0008 )	//"Pessoas vs Visoes"
	EndIF

	IF ( lExistOpc )

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		ЁGarante o Posicinamento do Recno                              Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		DEFAULT nReg	:= ( cAlias )->( Recno() )
		IF !Empty( nReg )
			( cAlias )->( MsGoto( nReg ) )
		EndIF

		DEFAULT lExecAuto := .F.
		IF ( lExecAuto )

			nPos := aScan( aRotina , { |x| x[4] == nOpc } )
			IF ( nPos == 0 )
				Break
			EndIF
			bBlock := &( "{ |a,b,c,d| " + aRotina[ nPos , 2 ] + "(a,b,c,d) }" )
			Eval( bBlock , cAlias , nReg , nPos )

		Else

			Apda170Mnt( cAlias , nReg , nOpc , .T. )

		EndIF

	Else

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Chama a Funcao de Montagem do Browse                                   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		RD0->( dbSetOrder( RetOrdem( cAlias , "RD0_FILIAL+RD0_CODIGO" ) ) )
		mBrowse( 6 , 1 , 22 , 75 , cAlias , NIL , NIL , NIL , "RD0->RD0_MSBLQL=='2'")
	EndIF

End Sequence

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁColoca o Ponteiro do Mouse em Estado de Espera			   	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura os Dados de Entrada 											 Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestArea( aAreaRde )
RestArea( aAreaRd0 )
RestArea( aAreaRdk )
RestArea( aAreaRD4 )
RestArea( aAreaRdz )
RestArea( aAreaSra )
RestArea( aArea    )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura o Cursor do Mouse                				   	   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( NIL )

/*/
зддддддддддбддддддддддддддбддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁInApda170Exec ЁAutor ЁMarinaldo de Jesus   Ё Data Ё24/08/2004Ё
цддддддддддеддддддддддддддаддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁExecutar Funcoes Dentro de APDA170                           Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁInApda170Exec( cExecIn , aFormParam )						 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁuRet                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function InApda170Exec( cExecIn , aFormParam )

Local uRet

DEFAULT cExecIn		:= ""
DEFAULT aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= &( cExecIn )
EndIF

Return( uRet )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda170VisЁ Autor ЁMarinaldo de Jesus     Ё Data Ё27/02/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Participantes/Pessoas ( Visualizar )			Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA170()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda170Vis( cAlias , nReg )
Return( APDA170( cAlias , nReg , 2 ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda170IncЁ Autor ЁMarinaldo de Jesus     Ё Data Ё27/02/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Participantes/Pessoas ( Incluir )				Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA170()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda170Inc( cAlias , nReg )
Return( APDA170( cAlias , nReg , 3 ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda170AltЁ Autor ЁMarinaldo de Jesus     Ё Data Ё27/02/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Participantes/Pessoas ( Alterar )				Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA170()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda170Alt( cAlias , nReg )
Return( APDA170( cAlias , nReg , 4 ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda170DelЁ Autor ЁMarinaldo de Jesus     Ё Data Ё27/02/2004Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Participantes/Pessoas ( Excluir )				Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>          							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA170()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda170Del( cAlias , nReg )
Return( APDA170( cAlias , nReg , 5 ) )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda170MntЁ Autor ЁMarinaldo de Jesus     Ё Data Ё18/06/2002Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCadastro de Participantes/Pessoas (Manutencao)				Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁApda170Mnt( cAlias , nReg , nOpc , lDlgPadSiga )			Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁcAlias 		= Alias do arquivo                              Ё
Ё          ЁnReg   		= Numero do registro                            Ё
Ё          ЁnOpc   		= Numero da opcao selecionada                   Ё
Ё          ЁlDlgPadSiga = Numero da opcao selecionada                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA170()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function Apda170Mnt( cAlias , nReg , nOpc , lDlgPadSiga )

Local aArea				:= GetArea(Alias())
Local aAreaRde			:= RDE->( GetArea() )
Local aSvKeys			:= GetKeys()
Local aAdvSize			:= {}
Local aInfoAdvSize		:= {}
Local aObjSize			:= {}
Local aObjCoords		:= {}
Local aRd0Header		:= {}
Local aRd0Cols			:= {}
Local aRd0Fields		:= {}
Local aRD0Folders		:= {}
Local aRd01Fields		:= {}
Local aRd0Altera		:= {}
Local aRd0VirtEn		:= {}
Local aRd0NotFields		:= {}
Local aRd0Keys			:= {}
Local aRd0VisuEn		:= {}
Local aRdeGdAltera  	:= {}
Local aRdeGdNaoAlt		:= {}
Local aRdeRecnos		:= {}
Local aRdeKeys			:= {}
Local aRdeNotFields		:= {}
Local aRdeVirtGd		:= {}
Local aRdeVisuGd		:= {}
Local aRdeHeader		:= {}
Local aRdeCols			:= {}
Local aSvRdeCols		:= {}
Local aRdeQuery			:= {}
Local aFolders			:= Array( 02 )
Local aPages			:= Array( 02 )
Local aObjFolder		:= Array( 02 )
Local aButtons			:= {}
Local aFreeLocks		:= {}
Local bRdeGdDelOk		:= { |lDelOk| CursorWait() , lDelOk := RdeGdDelOk( oGdRde , nOpc ) , CursorArrow() , lDelOk }
Local bSet15			:= { || NIL }
Local bSet24			:= { || NIL }
Local bDialogInit		:= { || NIL }
Local bRdeGdTudOk		:= { || NIL }
Local bGdRdeSeek		:= { || NIL }
Local bSvblDblClick		:= { || NIL }
Local bGetRd0			:= { || NIL }
Local bGetRde			:= { || NIL }
Local cFilRD0			:= ""
Local cCodRD0			:= ""
Local cRD0KeySeek		:= ""
Local lLocks			:= .F.
Local lExecLock			:= ( ( nOpc <> 2 ) .and. ( nOpc <> 3 ) )
Local lVisoes			:= .F.
Local nOpcAlt			:= 0
Local nRD0Usado			:= 0
Local nRDEUsado			:= 0
Local nLoop				:= 0
Local nOpcNewGd			:= IF( ( ( nOpc == 2 ) .or. ( nOpc == 5 ) ) , 0 , GD_INSERT + GD_UPDATE + GD_DELETE	)
Local nRdeMaxLocks		:= 50
Local oDlg				:= NIL
Local oEndRd0			:= NIL
Local oGdRde			:= NIL
Local oFolders			:= NIL

Private aGets
Private aTela

Private aRdeGdUndo		:= {}
Private nGetSX8Len		:= GetSX8Len()

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁPoe o cursor do Mouse em Estado de Espera					   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Nao Permite a Exclusao                                       Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( nOpc == 5 )
		MsgInfo( STR0020 )	//"OpГao nao disponМvel"
		Break
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁCheca a Opcao Selecionada									   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aRotSetOpc( cAlias , NIL , nOpc , .F. )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta os Dados para a Enchoice							   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aRd0NotFields	:= { "RD0_FILIAL" }
	bGetRd0			:= { |lLock,lExclu|	IF( lExecLock , ( lLock := .F. , lExclu	:= .T. ) , aRd0Keys := NIL ),;
										aRd0Cols := RD0->(;
															GdBuildCols(	@aRd0Header		,;	//01 -> Array com os Campos do Cabecalho da GetDados
																			@nRd0Usado		,;	//02 -> Numero de Campos em Uso
																			@aRd0VirtEn		,;	//03 -> [@]Array com os Campos Virtuais
																			@aRd0VisuEn		,;	//04 -> [@]Array com os Campos Visuais
																			"RD0"			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
																			aRd0NotFields	,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
																			NIL         	,;	//07 -> [@]Array unidimensional contendo os Recnos
																			"RD0"		   	,;	//08 -> Alias do Arquivo Pai
																			NIL				,;	//09 -> Chave para o Posicionamento no Alias Filho
																			NIL				,;	//10 -> Bloco para condicao de Loop While
																			NIL				,;	//11 -> Bloco para Skip no Loop While
																			NIL				,;	//12 -> Se Havera o Elemento de Delecao no aCols
																			NIL				,;	//13 -> Se Sera considerado o Inicializador Padrao
																			NIL				,;	//14 -> Opcional, Carregar Todos os Campos
																			NIL				,;	//15 -> Opcional, Nao Carregar os Campos Virtuais
																			NIL				,;	//16 -> Opcional, Utilizacao de Query para Selecao de Dados
																			NIL				,;	//17 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
																			NIL				,;	//18 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
																			NIL				,;	//19 -> Carregar Coluna Fantasma
																			NIL				,;	//20 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
																			NIL				,;	//21 -> Verifica se Deve Checar se o campo eh usado
																			NIL				,;	//22 -> Verifica se Deve Checar o nivel do usuario
																			NIL				,;	//23 -> Verifica se Deve Carregar o Elemento Vazio no aCols
																			@aRd0Keys		,;	//24 -> [@]Array que contera as chaves conforme recnos
																			@lLock			,;	//25 -> [@]Se devera efetuar o Lock dos Registros
																			@lExclu			 ;	//26 -> [@]Se devera obter a Exclusividade nas chaves dos registros
																	    );
														  ),;
										IF( lExecLock , ( lExclu ) , .T. );
	  					}
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁLock do Registro do RD0									   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( lLocks := WhileNoLock( "RD0_RDE" , NIL , NIL , 1 , 1 , .T. , 1 , 5 , bGetRd0 ) )
		Break
	EndIF
	CursorWait()
	cFilRD0			:= RD0->RD0_FILIAL
	cCodRD0			:= RD0->RD0_CODIGO
	cRD0KeySeek		:= ( xFilial( "RDE" ,cFilRD0 ) + cCodRD0 )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Cria as Variaveis de Memoria e Carrega os Dados Conforme o arЁ
	Ё quivo														   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/

	aRD0Folders := {}
	DbSelectArea("SXA")
	DbSetOrder(1)
	DbSeek( "RD0" )
	While !Eof() .And. SXA->XA_ALIAS == "RD0"
		aAdd( aRD0Folders , XADescric() )
		DbSkip()
	Enddo

	DbSelectArea("SX3")
	DbSetOrder(2)
	For nLoop := 1 To nRD0Usado
		aAdd( aRd0Fields , aRd0Header[ nLoop , 2])
		If SX3->(DbSeek(aRd0Header[ nLoop , 2])) //.AND. X3USO(SX3->X3_USADO)
			Aadd( aRd01Fields, {SX3->X3_TITULO,;
						SX3->X3_CAMPO,;
						SX3->X3_TIPO,;
						SX3->X3_TAMANHO,;
						SX3->X3_DECIMAL,;
						SX3->X3_PICTURE,;
						&("{||" + AllTrim(SX3->X3_VALID)+ "}"),;
						X3Obrigat(SX3->X3_CAMPO),;
                    						SX3->X3_NIVEL,;
						SX3->X3_RELACAO,;
						SX3->X3_F3,;
						&("{||" + AllTrim(SX3->X3_WHEN) + "}"),;
						SX3->X3_VISUAL=="V",;
						.F.,;
						SX3->X3_CBOX,;
						VAL(SX3->X3_FOLDER),;
						.F.,;
						"",;
						SX3->(X3_FOLDER+X3_ORDEM)} )
		Endif

		SetMemVar( aRd0Header[ nLoop , 02 ] , aRd0Cols[ 01 , nLoop ] , .T. )
	Next nLoop
	aSort(aRd01Fields,,,{|x,y| x[19] < y[19] })
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta os Dados para a GetDados							   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aAdd( aRdeNotFields , "RDE_FILIAL"  )
	aAdd( aRdeNotFields , "RDE_CODPAR"	)
	aRdeQuery		:= Array( 05 )
	aRdeQuery[01]	:= "RDE_FILIAL='"+cFilRD0+"'"
	aRdeQuery[02]	:= " AND "
	aRdeQuery[03]	:= "RDE_CODPAR='"+cCodRD0+"'"
	aRdeQuery[04]	:= " AND "
	aRdeQuery[05]	:= "D_E_L_E_T_=' ' "
	RDE->( dbSetOrder( RetOrdem( "RDE" , "RDE_FILIAL+RDE_CODPAR+RDE_CODVIS+RDE_ITEVIS" ) ) )
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Quando For Inclusao Posiciona o RDE No Final do Arquivo	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( nOpc == 3  ) //Inclusao
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Garante que na Inclusao o Ponteiro do RDE estara em Eof()    Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		PutFileInEof( "RDE" )
	EndIF
	bGetRde	:= { |lLock,lExclu| IF( lExecLock , ( lLock := .T. , lExclu := .T. ) , aRdeKeys := NIL ),;
						 		aRdeCols := RDE->(;
													GdBuildCols(	@aRdeHeader		,;	//01 -> Array com os Campos do Cabecalho da GetDados
																	@nRdeUsado		,;	//02 -> Numero de Campos em Uso
																	@aRdeVirtGd		,;	//03 -> [@]Array com os Campos Virtuais
																	@aRdeVisuGd		,;	//04 -> [@]Array com os Campos Visuais
																	"RDE"			,;	//05 -> Opcional, Alias do Arquivo Carga dos Itens do aCols
																	aRdeNotFields	,;	//06 -> Opcional, Campos que nao Deverao constar no aHeader
																	@aRdeRecnos		,;	//07 -> [@]Array unidimensional contendo os Recnos
																	"RD0"		   	,;	//08 -> Alias do Arquivo Pai
																	cRD0KeySeek		,;	//09 -> Chave para o Posicionamento no Alias Filho
																	NIL				,;	//10 -> Bloco para condicao de Loop While
																	NIL				,;	//11 -> Bloco para Skip no Loop While
																	NIL				,;	//12 -> Se Havera o Elemento de Delecao no aCols
																	NIL				,;	//13 -> Se Sera considerado o Inicializador Padrao
																	NIL				,;	//14 -> Opcional, Carregar Todos os Campos
																	NIL				,;	//15 -> Opcional, Nao Carregar os Campos Virtuais
																	aRdeQuery		,;	//16 -> Opcional, Utilizacao de Query para Selecao de Dados
																	.F.				,;	//17 -> Opcional, Se deve Executar bKey  ( Apenas Quando TOP )
																	.F.				,;	//18 -> Opcional, Se deve Executar bSkip ( Apenas Quando TOP )
																	.T.				,;	//19 -> Carregar Coluna Fantasma
																	NIL				,;	//20 -> Inverte a Condicao de aNotFields carregando apenas os campos ai definidos
																	NIL				,;	//21 -> Verifica se Deve Checar se o campo eh usado
																	NIL				,;	//22 -> Verifica se Deve Checar o nivel do usuario
																	NIL				,;	//23 -> Verifica se Deve Carregar o Elemento Vazio no aCols
																	@aRdeKeys		,;	//24 -> [@]Array que contera as chaves conforme recnos
																	@lLock			,;	//25 -> [@]Se devera efetuar o Lock dos Registros
																	@lExclu			,;	//26 -> [@]Se devera obter a Exclusividade nas chaves dos registros
																	nRdeMaxLocks	 ;	//27 -> Numero maximo de Locks a ser efetuado
															    );
													  ),;
								IF( lExecLock , ( lLock .and. lExclu ) , .T. );
	  		    }
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁLock do Registro do RDE									   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF !( lLocks := WhileNoLock( "RDE" , NIL , NIL , 1 , 1 , .T. , nRdeMaxLocks , 5 , bGetRde ) )
		Break
	EndIF
	CursorWait()
	aSvRdeCols	:= {}
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁSe ja houver informacoes no RDE nao Permite a Inclusao, apenasЁ
	Ёalteracao ou Exclusao										   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( ( nOpc <> 3 ) .and. ( Len( aRdeRecnos ) == 0 ) )
		//"Nao existem visoes cadastrados para este participante. Selecione a op┤└o de InclusЦo"
		MsgInfo(  OemToAnsi( STR0022 ) , cCadastro )
		Break
   	ElseIF ( ( nOpc == 3 ) .and. ( Len( aRdeRecnos ) > 0 ) )
		//"Ja Existem visoes cadastrados para este participante. Selecione a op┤└o de Altera┤└o"
		MsgInfo(  OemToAnsi( STR0023 ) , cCadastro )
		Break
	EndIF
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Carrega os Campos Editaveis para a GetDados				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	For nLoop := 1	To nRDEUsado
		SetMemVar( aRdeHeader[ nLoop , 02 ] , GetValType( aRdeHeader[ nLoop , 08 ] , aRdeHeader[ nLoop , 04 ] ) , .T. )
		IF (;
				( aScan( aRdeVirtGd		, aRdeHeader[ nLoop , 02 ] ) == 0 ) .and.	;
		   		( aScan( aRdeVisuGd		, aRdeHeader[ nLoop , 02 ] ) == 0 ) .and.	;
		   		( aScan( aRdeNotFields	, aRdeHeader[ nLoop , 02 ] ) == 0 ) .and.	;
		   		( aScan( aRdeGdNaoAlt	, aRdeHeader[ nLoop , 02 ] ) == 0 )			;
		  	)
			aAdd( aRdeGdAltera , aRdeHeader[ nLoop , 02 ] )
		EndIF
	Next nLoop

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta as Dimensoes dos Objetos         					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	DEFAULT lDlgPadSiga	:= .F.
	aAdvSize		:= MsAdvSize( NIL , lDlgPadSiga )
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }
	aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Bloco para o TudoOk da GetDados do RDE			   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bRdeGdTudOk		:= { || oGdRde:TudoOk() }						//Valida as Informacoes da GetDados RDE

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Bloco para a Tecla <CTRL-O>						   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet15		:= { || IF(;
								(;
									( nOpc == 3 );	//Inclusao
									.or.;
									( nOpc == 4 );	//Alteracao
								);
								.and.;
								Eval( bRdeGdTudOk ),;							//Valida as Informacoes da GetDados
								(;
									nOpcAlt := 1 ,;
									RestKeys( aSvKeys , .T. ),;
									oDlg:End();
							 	),;
							 	IF(;
							 		(;
							 			( nOpc == 3 );	//Inclusao
							 			.or.;
							 			( nOpc == 4 );	//Alteracao
							 		),;
							 			(;
							 				nOpcAlt := 0 ,;
							 				.F.;
							 			 ),;
									(;
										nOpcAlt := IF( nOpc == 2 , 0 , 1 ) ,;	//Visualizacao ou Exclusao
										RestKeys( aSvKeys , .T. ),;
										oDlg:End();
							 		);
							 	  );
						   );
					 }
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Bloco para a Tecla <CTRL-X>     	   				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bSet24		:= { || ( nOpcAlt := 0 , RestKeys( aSvKeys , .T. ) , oDlg:End() ) }

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Botao de Pesquisa na GetDados					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bGdRdeSeek := { ||	GdRdeSeek( oGdRde , lVisoes )	,;
						SetKey( VK_F4 , bGdRdeSeek ) 	 ;
			   }
	aAdd(;
			aButtons	,;
							{;
								"pesquisa",;
	   							bGdRdeSeek,;
	       	   					OemToAnsi( ( STR0001 + " " + STR0011 ) + "...<F4>"  ),;	//"Pesquisar"###"Vis■es"
	       	   					OemToAnsi( STR0001 );									//"Pesquisar"
	           				};
	     )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Bloco para a Inicializacao do Dialog            	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	bDialogInit		:= { ||;
								oGdRde:Hide()											,;
								aObjFolder[01,01,01] := oEndRd0							,;
								aObjFolder[01,01,02] := { || .T. }						,;
								aObjFolder[01,01,03] := NIL								,;
								aObjFolder[02,01,01] := oGdRde							,;
								aObjFolder[02,01,02] := bRdeGdTudOk						,;
								aObjFolder[02,01,03] := NIL								,;
								EnchoiceBar( oDlg , bSet15 , bSet24 , NIL , aButtons )	,;
								SetKey( VK_F4 , bGdRdeSeek )							 ;
					   }


	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Conteudo do aPages								   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aPages[ 01 ]		:= OemToAnsi( IF( cModulo == "APD" , STR0009 /*"Participante"*/ , STR0010 /*"Pessoa"*/ ) )
	aPages[ 02 ]		:= OemToAnsi( STR0011 /*"Vis■es"*/ )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define o Conteudo do aFolders								   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aFolders[ 01 ]		:= OemToAnsi( "&"+IF( cModulo == "APD" , STR0009 /*"Participante"*/ , STR0010 /*"Pessoa"*/ ) )
	aFolders[ 02 ]		:= OemToAnsi( "&"+STR0011 /*"Vis■es"*/ )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Define os Elementos para o Array do Objeto Folder        	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aObjFolder[ 01 ]	:= Array( 01 , 03 )
	aObjFolder[ 02 ]	:= Array( 01 , 03 )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Monta o Dialogo Principal para a Manutencao das Formulas	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	DEFINE MSDIALOG oDlg TITLE OemToAnsi( STR0007 ) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF GetWndDefault() PIXEL

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta as Dimensoes do Objeto Folder   					   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
		aObjSize := MsObjSize( aInfoAdvSize , aObjCoords )

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Carrega o Objeto Folder               					   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		oFolders := TFolder():New(	aObjSize[ 1 , 1 ]		,;
									aObjSize[ 1 , 2 ]		,;
									aFolders				,;
									aPages					,;
									oDlg					,;
									NIL						,;
									NIL						,;
									NIL						,;
									.T.						,;
									.F.						,;
									aObjSize[ 1 , 4 ]		,;
									aObjSize[ 1 , 3 ]		 ;
								 )
		oFolders:bSetOption := { |nNewFolder,lFolderSetOption| IF( lFolderSetOption := FolderSetOption( nNewFolder , oFolders:nOption , aObjFolder ) , ( lVisoes := ( nNewFolder == 2 ) ) , ( lVisoes := ( oFolders:nOption == 2 ) ) ) , lFolderSetOption }

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta as Dimensoes para os Objetos Enchoice e GetDados   	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		aObjCoords := {}
		aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
		aObjSize := MsObjSize( aInfoAdvSize , aObjCoords )
		aObjSize[ 1 , 1 ] += 15 //Alinha em Relacao ao Folder

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta o Objeto Enchoice para o RD0                      	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		oEndRd0	:= MsmGet():New(	cAlias					,;
									nReg					,;
									2   					,;
									NIL						,;
									NIL						,;
									NIL						,;
									aRd0Fields				,;
									aObjSize[1]				,;
									aRd0Altera				,;
									NIL						,;
									NIL						,;
									NIL						,;
									oFolders:aDialogs[ 01 ]	,;
									NIL						,;
									.F.						,;
									NIL						,;
									.F.			 			,;
									,;
									.F.,;
									,;
									aRd01Fields,aRd0Folders;
								)




		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Monta o Objeto GetDados para o RDE						   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		oGdRde	:= MsNewGetDados():New(	aObjSize[1,1]															,;
												aObjSize[1,2]													,;
												aObjSize[1,3]													,;
												aObjSize[1,4]													,;
												nOpcNewGd														,;
												"RdeGdLinOk"													,;
												"RdeGdTudOk"													,;
												""																,;
												aRdeGdAltera													,;
												0																,;
												Val(Replicate("9",GetSx3Cache( "RDE_CODVIS" , "X3_TAMANHO" )))	,;
												NIL																,;
												NIL																,;
												bRdeGdDelOk														,;
												oDlg															,;
												aRdeHeader														,;
												aRdeCols									 					 ;
											 )



		bSvblDblClick := oGdRde:oBrowse:blDblClick
		IF ( nOpc == 4 )	//Alteracao
			oGdRde:oBrowse:blDblClick := { || GdRdeblDblClick( oGdRde , bSvblDblClick ) }
		EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Inicializa o Primeiro Elemento da GetDados como Deletado	   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF Empty( aRdeRecnos )
			GdFieldPut( "GDDELETED" , .T. , 1 , oGdRde:aHeader , oGdRde:aCols )
		EndIF
	ACTIVATE MSDIALOG oDlg ON INIT Eval( bDialogInit ) CENTERED

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Coloca o Ponteiro do Mouse em Estado de Espera			   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	CursorWait()

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁGarante o Posicionamento do RD0							   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	RD0->( MsGoto( nReg ) )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁQuando Confirmada a Opcao e Nao for Visualizacao Grava ou   ExЁ
	Ёclui as Informacoes do RD0 e RDE							   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF( nOpcAlt == 1 )
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Apenas se nao For Visualizacao              				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
 		IF ( nOpc != 2 )
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Gravando/Incluido ou Excluindo Informacoes do SRY/RDE        Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			aRdeCols := oGdRde:aCols //Redireciona o Ponteiro do aRdeCols
			MsAguaRde(;
						{ ||;
								Apda170Grava(	nOpc		,;	//Opcao de Acordo com aRotina
							 					nReg		,;	//Numero do Registro do Arquivo Pai ( RD0 )
							 					aRdeHeader	,;	//Campos do Arquivo Filho ( RDE )
							 					aRdeCols	,;	//Itens Atual do Arquivo Filho ( RDE )
							 					aSvRdeCols	,;	//Itens Anterior do Arquivo Filho ( RDE )
							 					aRdeVirtGd	,;	//Campos Virtuais do Arquivo Filho ( RDE )
							 					aRdeRecnos	 ;	//Recnos do Arquivo Filho ( RDE )
											);
						};
					 )
		EndIF
	Else
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё RollBack da Numeracao Automatica            				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		While ( GetSX8Len() > nGetSX8Len )
			RollBackSX8()
		End While
	EndIF

End Sequence

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Coloca o Ponteiro do Mouse em Estado de Espera			   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁLibera os Locks             								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
aAdd( aFreeLocks , { "RD0_RDE"	, NIL		 , aRd0Keys } )
aAdd( aFreeLocks , { "RDE"		, aRdeRecnos , aRdeKeys } )
ApdFreeLocks( aFreeLocks )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura os Dados de Entrada								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestArea( aAreaRde )
RestArea( aArea )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura as Teclas de Atalho											 Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
RestKeys( aSvKeys , .T. )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
ЁRestaura o Estado do Ponteiro do Mouse						   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( MbrChgLoop( .F. ) )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁGdRdeSeek	 ЁAutorЁMarinaldo de Jesus    Ё Data Ё01/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁEfetuar Pesquisa na GetDados                               	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA170()                                              		Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function GdRdeSeek( oGdRde , lVisoes )

Local aSvKeys		:= GetKeys()
Local cProcName3	:= Upper( Alltrim( ProcName( 3 ) ) )
Local cProcName5	:= Upper( Alltrim( ProcName( 5 ) ) )

Local cMsgInfo
Local cTitle

Begin Sequence

	IF !( "APDA170MNT" $ ( cProcName3 + cProcName5 ) )
		Break
	EndIF

	IF !( lVisoes )
		cMsgInfo := STR0012	//"OpГЦo disponМvel apenas para pesquisa na GetDados ( Folder VisУes )."
		Break
	EndIF

	GdSeek( oGdRde , OemToAnsi( cMsgInfo ) )

End Sequence

IF !Empty( cMsgInfo )
	cTitle	:= ( cCadastro + " - " + ( STR0001 + " " + STR0011 ) )	//"Pesquisar"###"Vis■es"
	MsgInfo( OemToAnsi( cMsgInfo ) , OemToAnsi( cTitle ) )
EndIF

RestKeys( aSvKeys , .T. )

Return( NIL )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁGdRdeUndo  	 ЁAutorЁMarinaldo de Jesus    Ё Data Ё02/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁUnDo na Linha da GetDados                                  	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA170()                                              		Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function GdRdeUndo( oGdRde , lVisoes )

Local aSvKeys		:= GetKeys()
Local cMsgInfo
Local cTitle
Local nPos
Local nAt
Local nLenUndo

Begin Sequence


	IF !( lVisoes )
		cMsgInfo := STR0017	//"OpГЦo disponМvel apenas para a GetDados ( Folder VisУes )."
		Break
	EndIF

	IF Empty( aRdeGdUndo )
		cMsgInfo := STR0018	//"NЦo existem alteraГУes a serem desfeitas para esta linha da GetDados."
		Break
	EndIF

	nAt := oGdRde:oBrowse:nAt
	IF ( ( nPos := aScan( aRdeGdUndo , { |x| x[1] == nAt } ) ) == 0 )
		cMsgInfo := STR0018	//"NЦo existem alteraГУes a serem desfeitas para esta linha da GetDados."
		Break
	EndIF

	IF ( ( nLenUndo := Len( aRdeGdUndo[ nPos , 02 ] ) ) == 0 )
		cMsgInfo := STR0018	//"NЦo existem alteraГУes a serem desfeitas para esta linha da GetDados."
		Break
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Restaura os Dados da Linha da GetDados					   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	oGdRde:aCols[ nAt ] := aClone( aRdeGdUndo[ nPos , 02 , nLenUndo ] )
	aDel(  aRdeGdUndo[ nPos , 02 ] , nLenUndo   )
	aSize( aRdeGdUndo[ nPos , 02 ] , --nLenUndo )

End Sequence

IF ( !Empty( cMsgInfo ) )
	cTitle	:= OemToAnsi( cCadastro + " - " + ( STR0019 + " " + STR0011 ) )	//"Desfazer"###"Vis■es"
	MsgInfo( cMsgInfo , cTitle )
EndIF

RestKeys( aSvKeys , .T. )

Return( NIL )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁGdRdeAddUndo ЁAutorЁMarinaldo de Jesus    Ё Data Ё02/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁCarrega Informacoes para Undo                              	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA170()                                              		Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function GdRdeAddUndo( oGdRde )

Local nAt := oGdRde:oBrowse:nAt

Local nPos

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Adiciona informacoes para o Undo                             Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
IF ( ( nPos := aScan( aRdeGdUndo , { |x| x[1] == nAt } ) ) == 0 )
	aAdd( aRdeGdUndo , { nAt , {} } )
	nPos := Len( aRdeGdUndo )
EndIF
IF (;
		Empty( aRdeGdUndo[ nPos , 02 ] );
		.or.;
		!( ArrayCompare(;
							oGdRde:aCols[ nAt ],;
							aRdeGdUndo[ nPos , 02 , Len( aRdeGdUndo[ nPos , 02 ] ) ];
					  );
		  );
	)
	aAdd( aRdeGdUndo[ nPos , 02 ] , aClone( oGdRde:aCols[ nAt ] ) )
EndIF

Return( NIL )

/*/
зддддддддддбддддддддддддбдддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeGdLinOk	ЁAutorЁMarinaldo de Jesus     Ё Data Ё18/06/2002Ё
цддддддддддеддддддддддддадддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁLinha Ok da GetDados RDE                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁRdeGdLinOk( oBrowse )									    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ 															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA170()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeGdLinOk( oBrowse )

Local aCposKey		:= {}
Local lLinOk		:= .T.

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Altera o Estado do Cursor  								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

	Begin Sequence

		IF !( GdDeleted() )

			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Verifica Se o Campos Estao Devidamente Preenchidos		   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			aCposKey := GdObrigat( aHeader )
			IF !( lLinOk := GdNoEmpty( aCposKey ) )
		    	Break
			EndIF

			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Verifica Itens Duplicados na GetDados						   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			aCposKey := GetArrUniqe( "RDE" )
			IF !( lLinOk := GdCheckKey( aCposKey , 4 ) )
				Break
			EndIF

			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Verifica se Existem Status Duplicados. So pode Existir um StaЁ
			Ё tus ativo para uma mesma Visao.                              Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			IF ( GdFieldGet( "RDE_STATUS" ) == "1" )
				aCposKey := { "RDE_CODVIS" , "RDE_STATUS" }
				IF !( lLinOk := GdCheckKey( aCposKey , 4 ) )
					Break
				EndIF
			EndIF
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Verifica se Existem ResponsАveis Duplicados. So pode Existir um Ё
			Ё ResponsАvel no item de visЦo                             Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			If RDE->(ColumnPos( "RDE_RESP")) > 0
				If ( GdFieldGet( "RDE_RESP" ) == "1" )
					aCposKey := { "RDE_CODVIS" , "RDE_ITEVIS","RDE_STATUS","RDE_RESP" }
					If !( lLinOk := GdCheckKey( aCposKey , 4 ) )
						Break
					Else
						If !( lLinOk := RdeChkResp(GdFieldGet( "RDE_CODVIS" ),GdFieldGet( "RDE_ITEVIS" ),GdFieldGet( "RDE_STATUS" )) )
							Break
						EndIf
					EndIF
				EndIF
			EndIf
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Verifica se Existe a Chave 	   							   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			IF !( lLinOk := ExistCpo( "RD4" , GdFieldGet("RDE_CODVIS") + GdFieldGet("RDE_ITEVIS") ) )
				Break
			EndIF

		EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Reinicializa as Variaveis de Memoria						   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		SetMemVar( "RDE_CODVIS" , Space( GetSx3Cache( "RDE_CODVIS" , "X3_TAMANHO" ) ) )
		SetMemVar( "RDE_ITEVIS" , Space( GetSx3Cache( "RDE_ITEVIS" , "X3_TAMANHO" ) ) )

	End Sequence

	IF !( lLinOk )
		oBrowse:SetFocus()
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁGarante o Posiconamento no Final do Arquivo para Evitar trazerЁ
	Ёa Descricao do Registro em que esta posicionado			   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	PutFileInEof( "RDE" )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura o Estado do Cursor								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( lLinOk )

/*/
зддддддддддбддддддддддддбдддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeGdTudOk	ЁAutorЁMarinaldo de Jesus     Ё Data Ё18/06/2002Ё
цддддддддддеддддддддддддадддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁTudo Ok da GetDados RDE                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁRdeGdTudOk( oBrowse )									    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ 															Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA170()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeGdTudOk( oBrowse )

Local lTudoOk := .T.

Local nLoop

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Altera o Estado do Cursor  								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

	Begin Sequence

	    /*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Percorre Todas as Linhas para verificar se Esta Tudo OK      Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		For nLoop := 1 To Len( aCols )
			n := nLoop
			IF !( lTudoOk := RdeGdLinOk( oBrowse ) )
				oBrowse:Refresh()
				Break
			EndIF
		Next nLoop

	End Sequence

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura o Estado do Cursor								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( lTudoOk  )

/*/
зддддддддддбддддддддддддбдддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeGdDelOk  ЁAutorЁMarinaldo de Jesus     Ё Data Ё18/07/2003Ё
цддддддддддеддддддддддддадддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁValidar a Delecao na GetDados                               Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<vide parametros formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA170()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function RdeGdDelOk( oGdRde , nOpc )

Local lDelOk 		:= .T.
Local lStatusDel	:= .F.

Static lFirstDelOk
Static lLstDelOk

DEFAULT lFirstDelOk	:= .T.
DEFAULT lLstDelOk	:= .T.

Begin Sequence

	//Quando for Visualizacao ou Exclusao Abandona
	IF (;
			( nOpc == 2 ) .or. ;	//Visualizacao
			( nOpc == 5 );			//Exclusao
		)
		Break
	EndIF

	//Apenas se for a primeira vez
	IF !( lFirstDelOk )
		lFirstDelOk	:= .T.
		lDelOk 		:= lLstDelOk
		lLstDelOk	:= .T.
		Break
	EndIF

	lStatusDel	:= !( GdDeleted() ) //Inverte o Estado

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Adiciona informacoes para o Undo                             Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	GdRdeAddUndo( oGdRde )

	IF ( lStatusDel )	//Deletar
		lDelOk := GdRdeblDblClick( oGdRde , NIL , .F. )
	EndIF

	//Ja Passou pela funcao
	lFirstDelOk := .F.

End Sequence

Return( lDelOk )

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁGdRdeblDblClick	 ЁAutorЁMarinaldo de JesusЁ Data Ё02/03/2004Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁEdit dos Campos da GetDados              					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁoGdRde:oBrowse:blDblClick									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function GdRdeblDblClick( oGdRde , blDblClick , lEvalblDblClick )

Local aArea			:= GetArea()
Local aAreaRDE		:= RDE->( GetArea( "RDE" ) )
Local aAreaRD6		:= RD6->( GetArea( "RD6" ) )
Local aAreaRDA		:= RDA->( GetArea( "RDA" ) )
Local aAreaRD9		:= RD9->( GetArea( "RD9" ) )
Local lNoModify		:= .F.
Local nRdeCodVis	:= GdFieldPos( "RDE_CODVIS" , oGdRde:aHeader )
Local nRdeIteVis	:= GdFieldPos( "RDE_ITEVIS" , oGdRde:aHeader )

Local cRdeCodVis
Local cRdeIteVis
Local cFilRDE
Local cFilRD6
Local cFilRDA
Local cFilRD9
Local cKeySeek
Local cMsgInfo
Local nAt
Local nColPos
Local uRet

DEFAULT lEvalblDblClick := .T.

Begin Sequence

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Se Estiver Executando blDblClick							   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( lEvalblDblClick )
		nColPos	:= oGdRde:oBrowse:nColPos
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Adiciona informacoes para o Undo                             Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		GdRdeAddUndo( oGdRde )
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Se nao Forem as Colunas de Codigo de Visao ou Item de Visao  Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF (;
				!(;
					( nColPos == nRdeCodVis );
					.or.;
					( nColPos == nRdeIteVis );
				);
				.or.;
				(;
					( nRdeCodVis == 0 );
					.or.;
					( nRdeIteVis == 0 );
				);
			)
			Break
		EndIF
	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Obtem o Memo                               				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	nAt			:= oGdRde:oBrowse:nAt
	cRdeCodVis	:= oGdRde:aCols[ nAt , nRdeCodVis ]
	cRdeIteVis	:= oGdRde:aCols[ nAt , nRdeIteVis ]

	RDE->( dbSetOrder( RetOrdem( "RDE" , "RDE_FILIAL+RDE_CODPAR+RDE_CODVIS+RDE_ITEVIS" ) ) )
	cFilRDE 	:= xFilial( "RDE" , xFilial( "RD0" ) )
	cKeySeek	:= ( cFilRDE + GetMemVar("RD0_CODIGO") + cRdeCodVis + cRdeIteVis )
	IF ( RDE->( !dbSeek( cKeySeek , .F. ) ) )
		Break
	EndIF

	RD6->( dbSetOrder( RetOrdem( "RD6" , "RD6_FILIAL+RD6_CODVIS" ) ) )
	cFilRD6 	:= xFilial( "RD6" , cFilRDE )
	cKeySeek	:= ( cFilRD6 + cRdeCodVis )
	IF ( RD6->( !dbSeek( cKeySeek , .F. ) ) )
		Break
	EndIF

	RD9->( dbSetOrder( RetOrdem( "RD9" , "RD9_FILIAL+RD9_CODAVA+RD9_CODADO" ) ) )
	cFilRD9		:= xFilial( "RD9" , cFilRD6 )
	cKeySeek	:= ( cFilRD9 + RD6->RD6_CODIGO + GetMemVar("RD0_CODIGO") )
	IF ( lNoModify := RD9->( dbSeek( cKeySeek , .F. ) ) )
		Break
	EndIF

	RDA->( dbSetOrder( RetOrdem( "RDA" , "RDA_FILIAL+RDA_CODAVA+RDA_CODADO" ) ) )
	cFilRDA		:= xFilial( "RDA" , cFilRD6 )
	cKeySeek	:= ( cFilRDA + RD6->RD6_CODIGO + GetMemVar("RD0_CODIGO") )
	IF ( lNoModify := RDA->( dbSeek( cKeySeek , .F. ) ) )
		Break
	EndIF

	RDA->( dbSetOrder( RetOrdem( "RDA" , "RDA_FILIAL+RDA_CODAVA+RDA_CODDOR" ) ) )
	IF ( lNoModify := RDA->( dbSeek( cKeySeek , .F. ) ) )
		Break
	EndIF

End Sequence

IF ( lNoModify )
	IF ( lEvalblDblClick )
		cMsgInfo := STR0013	//"O ConteЗdo deste campo nЦo pode ser Alterado."
	Else
		cMsgInfo := STR0014	//"Este Мtem nЦo pode ser ExcluМdo."
		uRet := .F.
	EndIF
	cMsgInfo += CRLF
	cMsgInfo += STR0015	//"EstА sendo utilizado na AvaliaГЦo:"
	cMsgInfo += CRLF
	cMsgInfo += RD6->RD6_CODIGO + " - " + AllTrim( RD6->RD6_DESC )
	cMsgInfo += CRLF
	IF !Empty( cFilRD6 )
		cMsgInfo += STR0016 + " " + cFilRD6 //"da Filial:"
	EndIF
	MsgInfo( OemToAnsi( cMsgInfo ) , cCadastro )
Else
	IF !( lEvalblDblClick )
		uRet := .T.
	ElseIF ( lEvalblDblClick )
		GdRstDblClick( oGdRde , blDblClick )
	EndIF
EndIF

RestArea( aAreaRD9 )
RestArea( aAreaRDA )
RestArea( aAreaRD6 )
RestArea( aAreaRDE )
RestArea( aArea )

Return( uRet )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁApda170Grava ЁAutorЁMarinaldo de Jesus    Ё Data Ё21/07/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁGrava as Informacoes no RDE                                 Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁAPDA170()	                                                Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function Apda170Grava(	nOpc		,;	//Opcao de Acordo com aRotina
							 	nReg		,;	//Numero do Registro do Arquivo Pai ( RD0 )
							 	aRdeHeader	,;	//Campos do Arquivo Filho ( RDE )
							 	aRdeCols	,;	//Itens Atual do Arquivo Filho ( RDE )
							 	aSvRdeCols	,;	//Itens Anterior do Arquivo Filho ( RDE )
							 	aRdeVirtGd	,;	//Campos Virtuais do Arquivo Filho ( RDE )
							 	aRdeRecnos	 ;	//Recnos do Arquivo Filho ( RDE )
							  )

/*/
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Variaveis de Inicializacao Obrigatoria					  Ё
юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Local aMestre		:= GdPutIStrMestre( 01 )
Local aItens		:= {}
Local cOpcao		:= IF( ( nOpc == 5 ) , "DELETE" , IF( ( ( nOpc == 3 ) .or. ( nOpc == 4 ) ) , "PUT" , NIL ) )

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Altera o Estado do Cursor  								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorWait()

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Carrega os Itens Apenas se Houveram Alteracoes ou na ExclusaoЁ
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	IF ( ( nOpc == 5 ) .or. !( ArrayCompare( aRdeCols , aSvRdeCols ) ) )

		aItens	:= GdPutIStrItens( 01 )

		aItens[ 01 , 01 ] := "RDE"
		aItens[ 01 , 02 ] := {;
								{ "FILIAL" , xFilial( "RDE" , xFilial( "RD0" ) ) },;
								{ "CODPAR" , GetMemVar("RD0_CODIGO") };
							 }
		aItens[ 01 , 03 ] := aClone( aRdeHeader )
		aItens[ 01 , 04 ] := aClone( aRdeCols   )
		aItens[ 01 , 05 ] := aClone( aRdeVirtGd )
		aItens[ 01 , 06 ] := aClone( aRdeRecnos )
		aItens[ 01 , 07 ] := {}

	EndIF

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Seta a Gravacao ou Exclusao Apenas se Houveram Alteracoes  ouЁ
	Ё se foi Selecionada a Exclusao								   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	aMestre[ 01 , 01 ]	:= "RD0"
	aMestre[ 01 , 02 ]	:= nReg
	aMestre[ 01 , 03 ]	:= .F.
	aMestre[ 01 , 04 ]	:= {}
	aMestre[ 01 , 05 ]	:= {}
	aMestre[ 01 , 06 ]	:= {}
	aMestre[ 01 , 07 ]	:= aClone( aItens )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Grava e/ou exclui as Informacoes                        	   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	GdPutInfoData( aMestre , cOpcao )

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Confirmando a Numeracao Automatica          				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	While ( GetSX8Len() > nGetSX8Len )
		ConfirmSX8()
	End While

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Restaura o Estado do Cursor								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
CursorArrow()

Return( NIL )

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeCodParVld	 ЁAutorЁMarinaldo de JesusЁ Data Ё12/03/2004Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁValid do campo RDE_CODPAR									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID do campo RDE_CODPAR								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeCodParVld()

Local lRdeCodParOk

Begin Sequence

	IF ( NaoVazio() )
		Break
	EndIF

	lRdeCodParOk := ExistCpo( "RD0" )

End Sequence

Return( lRdeCodParOk )

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeCodVisVld	 ЁAutorЁMarinaldo de JesusЁ Data Ё04/12/2002Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁValid do campo RDE_CODVIS									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID do campo RDE_CODVIS								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeCodVisVld()

Local cDescVis
Local cDescIte
Local lRdeCodVisOk
Local lIsGetDados

IF ( lRdeCodVisOk := ( NaoVazio() .and. ExistCpo( "RDK" ) ) )
	IF ( lIsGetDados := IsInGetDados( { "RDE_CODVIS" , "RDE_ITEVIS" , "RDE_DESCOD" , "RDE_DESITE" } ) )
		cDescVis	:= RdeDesCodInit( lIsGetDados , GdFieldGet( "RDE_CODVIS" , n , .T. ) )
		cDescIte	:= RdeDesIteInit( lIsGetDados , ( GdFieldGet( "RDE_CODVIS" , n , .T. ) + GdFieldGet( "RDE_ITEVIS" ) ) )
		GdFieldPut( "RDE_DESCOD" , cDescVis )
		GdFieldPut( "RDE_DESITE" , cDescIte )
	ElseIF (;
				( IsMemVar( "RDE_CODVIS" ) ) .and.	;
				( IsMemVar( "RDE_ITEVIS" ) )		;
			)
		cDescVis		:= RdeDesCodInit( lIsGetDados , GetMemVar( "RDE_CODVIS" ) )
		cDescIte		:= RdeDesIteInit( lIsGetDados , ( GetMemVar( "RDE_CODVIS" ) + GetMemVar( "RDE_ITEVIS" ) ) )
		SetMemVar( "RDE_DESCOD" , cDescVis )
		SetMemVar( "RDE_DESITE" , cDescIte )
	EndIF
EndIF

Return( lRdeCodVisOk )

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeCodVisInit	 ЁAutorЁMarinaldo de JesusЁ Data Ё04/12/2002Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁInicializador Padrao do Campo RDE_CODVIS					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID do campo RDE_CODVIS								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeCodVisInit()

Local cRdeCodVisInit := Space( GetSx3Cache( "RDE_CODVIS" , "X3_TAMANHO" ) )

Return( cRdeCodVisInit )

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeDesCodInit	 ЁAutorЁMarinaldo de JesusЁ Data Ё17/12/2002Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁInicializadora Padrao do Campo RDE_DESCOD					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO do campo RDE_DESCOD								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeDesCodInit( lGetDados , cCodVis )

Local nOrder := RetOrdem( "RDK" , "RDK_FILIAL+RDK_CODIGO" , .F. )

DEFAULT lGetDados	:= .F.
DEFAULT cCodVis		:= RDE->RDE_CODVIS

IF ( lGetDados )
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Posiciona no Final do Arquivo para Evitar que na Inclusao  deЁ
	Ё nova Linha na GetDados Seja Carregada a Descricao Baseado  noЁ
	Ё Posicionamento do RDE pois a Funcao RdeDesCodInit() Tambem EhЁ
	Ё Chamada no Inicializador Padrao do Campo RDE_DESCOD          Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	PutFileInEof( "RDE" )
EndIF

If Type("RD0_CODIGO") <> "U" .and. ( RDE->(dbSeek(xFilial("RD0")+RD0_CODIGO)) .OR.  !Empty(M->RDE_CODVIS) )
	cDesc := ( fDesc( "RDK" , cCodVis , "RDK_DESC" , GetSx3Cache( "RDE_DESCOD" , "X3_TAMANHO" ) , xFilial("RDE") , nOrder , .F. ) )
Else
	cDesc:= ""
EndIf

Return cDesc

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeDesCodBrw	 ЁAutorЁMarinaldo de JesusЁ Data Ё21/01/2004Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁInicializadora Browse para Campo RDE_DESCOD					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_INIBRW do campo RDE_DESCOD								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeDesCodBrw()
Return( RdeDesCodInit( .F. , RDE->RDE_CODVIS ) )

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeIteVisVld	 ЁAutorЁMarinaldo de JesusЁ Data Ё04/12/2002Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁValid do campo RDE_IteVIS									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID do campo RDE_IteVIS								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeIteVisVld()

Local cDescVis
Local cDescIte
Local lRdeIteVisOk
Local lIsGetDados

IF ( lRdeIteVisOk := ( NaoVazio() .and. ExistCpo( "RD4" , GetMemVar( "RDE_CODVIS" ) + GetMemVar( "RDE_ITEVIS" ) ) ) )
	IF ( lIsGetDados := IsInGetDados( { "RDE_CODVIS" , "RDE_ITEVIS" , "RDE_DESCOD" , "RDE_DESITE" } ) )
		cDescVis	:= RdeDesCodInit( lIsGetDados , GdFieldGet( "RDE_CODVIS" ) )
		cDescIte	:= RdeDesIteInit( lIsGetDados , ( GdFieldGet( "RDE_CODVIS" ) + GdFieldGet( "RDE_ITEVIS" , n , .T. ) ) )
		GdFieldPut( "RDE_DESCOD" , cDescVis )
		GdFieldPut( "RDE_DESITE" , cDescIte )
	ElseIF (;
				( IsMemVar( "RDE_CODVIS" ) ) .and.	;
				( IsMemVar( "RDE_ITEVIS" ) )		;
			)
		cDescVis		:= RdeDesCodInit( lIsGetDados , GetMemVar( "RDE_CODVIS" ) )
		cDescIte		:= RdeDesIteInit( lIsGetDados , ( GetMemVar( "RDE_CODVIS" ) + GetMemVar( "RDE_ITEVIS" ) ) )
		SetMemVar( "RDE_DESCOD" , cDescVis )
		SetMemVar( "RDE_DESITE" , cDescIte )
	EndIF
EndIF

Return( lRdeIteVisOk )

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeIteVisWhen	 ЁAutorЁMarinaldo de JesusЁ Data Ё04/12/2002Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁWhen do Campo RDE_ITEVIS									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID do campo RDE_ITEVIS								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeIteVisWhen()

Local cReadVar		:= Upper( AllTrim( ReadVar() ) )
Local nRdkIndexOrd	:= RDK->( IndexOrd() )

IF ( SubStr( cReadVar , 4 ) == "RDE_ITEVIS" )
	RDK->( dbSetOrder( RetOrdem( "RDK" , "RDK_FILIAL+RDK_CODIGO" ) ) )
	IF ( IsInGetDados( { "RDE_CODVIS" } ) )
		SetMemVar( "RDE_CODVIS" , GdFieldGet( "RDE_CODVIS" ) )
		RDK->( MsSeek( xFilial("RDK") + GetMemVar( "RDE_CODVIS" ) , .F. ) )
	ElseIF ( IsMemVar( "RDE_CODVIS" ) )
		RDK->( MsSeek( xFilial("RDK") + GetMemVar( "RDE_CODVIS" ) , .F. ) )
	EndIF
	RDK->( dbSetOrder( nRdkIndexOrd ) )
EndIF

Return( .T. )

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeIteVisInit	 ЁAutorЁMarinaldo de JesusЁ Data Ё04/12/2002Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁInicializador Padrao do Campo RDE_IteVIS					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID do campo RDE_IteVIS								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeIteVisInit()

Local cRdeIteVisInit := Space( GetSx3Cache( "RDE_ITEVIS" , "X3_TAMANHO" ) )

Return( cRdeIteVisInit )

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeDesIteInit	 ЁAutorЁMarinaldo de JesusЁ Data Ё17/12/2002Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁInicializadora Padrao do Campo RDE_DESITE					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO do campo RDE_DESITE								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeDesIteInit( lGetDados , cKeyIteVis )

Local nOrder := RetOrdem( "RD4" , "RD4_FILIAL+RD4_CODIGO+RD4_ITEM" , .F. )

DEFAULT lGetDados	:= .F.
/*Com o Alias esta repetindo a mesma descriГЦo do item da visЦo, fiz diversos
testes e nЦo ocorreu uma falha pela falta do Alias*/
DEFAULT cKeyIteVis	:= RDE_CODVIS + RDE_ITEVIS

IF ( lGetDados )
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Posiciona no Final do Arquivo para Evitar que na Inclusao  deЁ
	Ё nova Linha na GetDados Seja Carregada a Descricao Baseado  noЁ
	Ё Posicionamento do RDE pois a Funcao RdeDesCodInit() Tambem EhЁ
	Ё Chamada no Inicializador Padrao do Campo RDE_DESCOD          Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	PutFileInEof( "RDE" )
EndIF

If Type("RD0_CODIGO") <> "U" .AND. ( RDE->(dbSeek(xFilial("RD0")+RD0_CODIGO)) .OR. !Empty(M->RDE_ITEVIS) )
	cDesc := fDesc( "RD4" , cKeyIteVis , "RD4_DESC" , GetSx3Cache( "RDE_DESITE" , "X3_TAMANHO" ) , xFilial("RDE") , nOrder , .F. )
Else
	cDesc := ""
EndIf

Return cDesc

/*/
зддддддддддбдддддддддддддддддбдддддбддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeDesIteBrw	 ЁAutorЁMarinaldo de JesusЁ Data Ё21/01/2004Ё
цддддддддддедддддддддддддддддадддддаддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁInicializadora Browse para Campo RDE_DESITE					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_INIBRW do campo RDE_DESITE								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeDesIteBrw()
Return( RdeDesIteInit( .F. , RDE->( RDE_CODVIS + RDE_ITEVIS ) ) )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeStatusVld ЁAutorЁMarinaldo de Jesus    Ё Data Ё06/10/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do Campo RDE_STATUS        	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_VALID para o campo RDE_STATUS                        	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeStatusVld()

Local lRdeStatusOk := .T.

Begin Sequence

	IF !( lRdeStatusOk := Pertence( OpBxAtvInat( .T. ) ) )
		Break
	EndIF

End Sequence

Return( lRdeStatusOk )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeStatusBox ЁAutorЁMarinaldo de Jesus    Ё Data Ё06/10/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Retornar as Opcoes do Campo RDE_STATUS        	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_CBOX para o campo RDE_STATUS                        	    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeStatusBox()
Return( OpBxAtvInat( .F. ) )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeStatusInitЁAutorЁMarinaldo de Jesus    Ё Data Ё06/10/2003Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Retornar as Opcoes do Campo RDE_STATUS        	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO para o campo RDE_STATUS                        	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeStatusInit()
Return( SubStr( OpBxAtvInat( .T. ) , 1 , 1 ) )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeStatusWhenЁAutorЁMarinaldo de Jesus    Ё Data Ё11/03/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para When no Campo RDE_STATUS 				       	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_WHEN do campo RDE_STATUS		                        	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeStatusWhen()

Local lRdeStatusWhen	:= .F.

Return( lRdeStatusWhen )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeDataVld   ЁAutorЁMarinaldo de Jesus    Ё Data Ё20/01/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Validar o Conteudo do campo RDE_DATA          	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_CBOX para o campo RD4_DATA                          		Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeDataVld()

Local lRdeDataVld := .T.

Begin Sequence

	IF !( lRdeDataVld := NaoVazio() )
		Break
	EndIF

End Sequence

Return( lRdeDataVld )

/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeDataInit  ЁAutorЁMarinaldo de Jesus    Ё Data Ё20/01/2004Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFuncao para Inicializar o conteudo do campo RDE_DATA        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO para o campo RDE_DATA                          	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RdeDataInit()
Return( dDataBase )
/*/
зддддддддддбдддддддддддддбдддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁRdeChkResp  ЁAutorЁEduardo Ferreira    Ё Data Ё11/07/2016Ё
цддддддддддедддддддддддддадддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁFunГЦo de validaГЦo de responsАvel por item visЦo 			Ё
Ё		   Ёcampo RDE_RESP        										Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁcCodVis=VisЦo, cIteVis - Item da VisЦo e Status= PartXIteVisЁ
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁX3_RELACAO para o campo RDE_DATA                          	Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function RdeChkResp(cCodVis,cIteVis,cStatus)
Local lRet 		:= .T.
Local cRd0Cod	:= GetMemVar("RD0_CODIGO")
Local aArea		:= GetArea()
Local aAreaRde	:= RDE->( GetArea() )
Local aOfusca	:= If(FindFunction('ChkOfusca'), ChkOfusca(), { .T., .F. }) //[2]Ofuscamento
Local aFldRel	:= Iif( aOfusca[2], FwProtectedDataUtil():UsrNoAccessFieldsInList( {"RD0_NOME"} ), {} )
Local lOfusca	:= Len(aFldRel) > 0

dbSelectArea("RDE")
RDE->(dbSetOrder(8))//RDE_FILIAL+RDE_CODVIS+RDE_ITEVIS+RDE_STATUS+RDE_CODPAR
If cStatus == "1"
	If RDE->(dbSeek(xFilial("RDE")+cCodVis+cIteVis+"1"))
		While !RDE->(Eof()) .And. (xFilial("RDE")+cCodVis+cIteVis+"1") == (RDE_FILIAL+RDE_CODVIS+RDE_ITEVIS+RDE_STATUS)
			If RDE->RDE_RESP== "1" .And. RDE->RDE_CODPAR != cRd0Cod
				Help(" ",1,"RdeChkResp",, STR0024+chr(13)+chr(10) + RDE->RDE_CODPAR+"-"+Iif(lOfusca, Replicate('*',15),Posicione("RD0",1,xFilial("RD0")+RDE->RDE_CODPAR,"RD0_NOME")),1,0) //"O participante abaixo jА И responsАvel por este item de visЦo."
				lRet:= .F.
				exit
			EndIf
			RDE->(dbSkip())
		EndDo
	EndIf
EndIf
RestArea( aAreaRde )
RestArea( aArea    )
Return lRet
/*
зддддддддддбддддддддддддддддбдддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    Ё MenuDef		ЁAutorЁ  Luiz Gustavo     Ё Data Ё21/12/2006Ё
цддддддддддеддддддддддддддддадддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁIsola opcoes de menu para que as opcoes da rotina possam    Ё
Ё          Ёser lidas pelas bibliotecas Framework da Versao 9.12 .      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё< Vide Parametros Formais >									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁAPDA160                                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  ЁaRotina														Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ< Vide Parametros Formais >									Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды*/

Static Function MenuDef()

 Local aRotina := {;
								{ STR0001 , "AxPesqui"	 , 0 , 01,,.F. } ,; //"Pesquisar"
								{ STR0002 , "Apda170Mnt" , 0 , 02 } ,; //"Visualizar"
								{ STR0003 , "Apda170Mnt" , 0 , 04 } ,; //"Incluir"
								{ STR0004 , "Apda170Mnt" , 0 , 04 } ,; //"Alterar"
								{ STR0005 , "Apda170Mnt" , 0 , 05 }, ; //"Excluir"
  								{ STR0021 , "Apda020Leg" , 0 , 05, ,.F.} ; //"Legenda"
											}
Return aRotina
