#Include "PROTHEUS.CH"
#INCLUDE "GPER580.CH"
#INCLUDE "REPORT.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPER580  ³ Autor ³ Maurico T. Takakura   ³ Data ³ 06.10.05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Afastamentos e Ausencias                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GpeR580(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Tatiane    ³13/06/06³100544|Conversao para relatorio personalizavel   ³±± 
±±³ Mauricio T.³23/11/06³------|Alteracao do nome de variavel errada.  Tra³±± 
±±³            ³        ³      |tamento para AS/400.                      ³±± 
±±³ Mauricio T.³24/11/06³------|Compatibilizacao do tamanho da pergunte do³±± 
±±³            ³        ³      |Registro Patronal com o tamanho da tabela.³±± 
±±³            ³        ³      |Alterado de 2 para 4 o tamanho.           ³±± 
±±³ Mauricio T.³03/04/07³121824|Retirado o campo RV_DECLANU, do select,   ³±± 
±±³            ³        ³      |pois o campo eh somente para o Mexico e o ³±± 
±±³            ³        ³      |relatorio nao utiliza o campo.            ³±± 
±±³ Silvia     ³05/06/07³125944|Ajuste na selecao da pergunta Processo.   ³±± 
±±³ Mauricio T.³15/04/08³------|Inclusao da validacao do campos com ins-  ³±± 
±±³            ³        ³      |trucao IN, pois quando em branco gerava er³±± 
±±³            ³        ³      |rro no Select.                            ³±± 
±±³ Erika K.   ³02/06/08³------|Alteracao de indices da tabela SR8 e      |±± 
±±³            ³        ³      |perguntes para integracao de dicionarios  ³±±
±±³            ³        ³      |Mexico e R 1.2.                           ³±±
±±³Gisele V.   ³28/07/08³148902³Alteracao no relat. pois cpos RA_CODRPAT, ³±±
±±³            ³        ³      ³R8_TIPORSC e R8_NCERINC e tabela RCO ex-  ³±±
±±³            ³        ³      ³clusivos do Mexico.                       ³±±
±±³            ³        ³      ³Alteracao na query do R3 pois nao trazia  ³±±
±±³            ³        ³      ³afastamentos sem data fim.                ³±±
±±³Gisele V.   ³19/09/08³151996³Inclusao de validacao do Filtro para   	  ³±±
±±³            ³        ³      ³Processo, Tipo de Afastamento e Verba qdo ³±±
±±³            ³        ³      ³selecionar em branco (Todos).             ³±±
±±³Marcos Kato ³26/02/09³------³Inclusao no pergunte GPE850R O Motivo de  ³±±
±±³            ³        ³      ³Ausencia e inclusao apenas no Treport da  ³±±
±±³            ³        ³      ³descricao do Motivo de Ausencia.          ³±±
±±³            ³        ³      ³Criado a funcao fAusencia() e MarcaTD()   ³±±
±±³            ³        ³      ³que sera executado pelo valid da nova     ³±±
±±³            ³        ³      ³pergunta.Alteracao especifico Venezuela   ³±±
±±³            ³        ³      ³referente ao FNC 00000004687/2009         ³±±
±±³Adilson S.  ³08/09/09³22559/³Acerto na Query p/ campo RCM_TIPOBS para  ³±±
±±³            ³        ³  2009³processamento somente na Venezuela.       ³±±
±±³            ³        ³      ³Acerto no tamanho da pergunta Categoria.  ³±±
±±³Francisco Jr³31/12/09³30905/³Acerto para uso na Gestao Corporativa,    ³±±
±±³            ³        ³  2009³Retirado ajuste do Dicionario de Dados,   ³±±
±±³            ³        ³      ³Ajustado via Atusx.                       ³±±
±±³Alex        ³23/12/10³029463³Atender Nova Interface Totvs 11.5 utili-  ³±±
±±³            ³        ³ /2010³zando a funcao MsAdvSize.                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GpeR580()

	Local oReport 

	Private aInfo   		:= {}
	Private cFilaInfo   	:= ""

	If FindFunction("TRepInUse") .And. TRepInUse()

		//-- Interface de impressao
		Pergunte("GPE580R",.F.)
	   	oReport := ReportDef()
   		oReport:PrintDialog()
 	Else
 		GpeR580R3()
	EndIF

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ReportDef³ Autor ³ R.H. - Tatiane Matias ³ Data ³ 13.06.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Definicao do relatorio                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ReportDef()

Local oReport 
Local oSection1   
Local oSection2   
Local oSection3

Local	cDesc		:=	STR0007 + ". "+STR0002		// "Ausencias"	# "Ser  impresso de acordo com os parametros solicitados pelo usuario."
Local aOrd     :=	{STR0005,; 	//"Matricula"
			 			 STR0006,; 	//"Nome"
			 			 STR0004,; 	//"Centro de Custo"
			 			 STR0008} 	//"Processo"

If cPaisLoc == "MEX"
	aAdd( aOrd, STR0003 )	// "Registro Patronal"
EndIf

	//-- Inicio definicao do Relatorio
	DEFINE REPORT oReport NAME "GPER580" TITLE OemToAnsi(STR0011) PARAMETER "GPE580R" ACTION {|oReport| PrintReport(oReport)} DESCRIPTION cDesc
                                     
		//-- Section da Empresa e Filial
		DEFINE SECTION oSection1 OF oReport ORDERS aOrd TITLE STR0023 
		oSection1:SetHeaderBreak(.T.)
				DEFINE CELL NAME "EMPRESA"		OF oSection1 TITLE STR0023 ALIAS "   " BLOCK {|| aInfo[3] } SIZE 10 
				DEFINE CELL NAME "FILIAL"		OF oSection1 TITLE STR0027 ALIAS "   " BLOCK {|| cFilaInfo + ' - ' + aInfo[1]} SIZE 20
				                                
		//-- Utilizada essa section para as ordens 1=Matricula e 2=Nome
		//-- Section de Funcionario
		DEFINE SECTION oSection2 OF oReport TABLES "SRA" TITLE STR0020
		oSection2:SetHeaderBreak(.T.)
			DEFINE CELL NAME "FILIAL"		OF oSection2 TITLE STR0027 ALIAS "   " BLOCK {|| cFilaInfo + ' - ' + aInfo[1]} SIZE 20
				
			DEFINE CELL NAME "RA_MAT" 		OF oSection2 ALIAS "SRA"
			DEFINE CELL NAME "RA_NOME" 	OF oSection2 ALIAS "SRA"
	      	
	      
	      //-- Section de Ausencias do Funcionario
			DEFINE SECTION oSection3 OF oSection2 TABLES "SR8", "RCM", "SRV" TITLE STR0001
			oSection3:SetHeaderBreak(.T.)
	
				DEFINE CELL NAME "R8_TIPOAFA" 	OF oSection3 ALIAS "SR8"
				DEFINE CELL NAME "RCM_DESCRI" 	OF oSection3 ALIAS "RCM" SIZE 20
				DEFINE CELL NAME "R8_PD"		OF oSection3 ALIAS "SR8"
				DEFINE CELL NAME "RV_DESC"		OF oSection3 ALIAS "SRV" SIZE 20 
				DEFINE CELL NAME "R8_DATAINI"	OF oSection3 ALIAS "SR8"
				DEFINE CELL NAME "R8_DATAFIM"	OF oSection3 ALIAS "SR8"
				DEFINE CELL NAME "R8_DURACAO"	OF oSection3 ALIAS "SR8"
				DEFINE CELL NAME "DURACAOPER"	OF oSection3 TITLE STR0016  ALIAS "   " BLOCK {|nDias| aAreaSR8 := (cAliasSR8)->( GetArea() ), nDias := fDiasAfasPeriodo( (cAliasMov)->RA_MAT,, (cAliasRCM)->RCM_TIPODI, If( dDataDe  < (cAliasSR8)->R8_DATAINI, (cAliasSR8)->R8_DATAINI, dDataDe ), If( dDataAte > (cAliasSR8)->R8_DATAFIM, (cAliasSR8)->R8_DATAFIM, dDataAte ) ), RestArea( aAreaSR8 ), nDias }
				DEFINE CELL NAME "CONTINUA"		OF oSection3 TITLE STR0017  ALIAS "   " BLOCK {|nPos| nPos 	:= aScan( aBoxCont, {|x| x[2] == (cAliasSR8)->R8_CONTINU } ), If( nPos > 0, aBoxCont[nPos,3], "" )} SIZE 3
				If cPaisLoc == "MEX"
					DEFINE CELL NAME "TPAUSENCIA"		OF oSection3 TITLE STR0018  ALIAS "   " BLOCK {|nPos| nPos 	:= aScan( aBoxTipoRSC, {|x| x[2] == (cAliasSR8)->R8_TIPORSC } ), If( nPos > 0, aBoxTipoRSC[nPos,3], "")} SIZE 10
					DEFINE CELL NAME "R8_NCERINC"		OF oSection3 ALIAS "SR8"
			   	ElseIf cPaisLoc=="VEN"
					DEFINE CELL NAME "MOTAUS" 	OF oSection3 TITLE "Motivo"  ALIAS "   " BLOCK {|nPos| nPos 	:= aScan( aBoxAusRcm, {|x| x[2] == (cAliasRCM)->RCM_TIPOBS } ), If( nPos > 0, aBoxAusRcm[nPos,3], "")} SIZE 20
				EndIf
		//-- Utilizada essa section para as ordens 3=Centro de Custo
		//-- Section de Centro de Custo
		DEFINE SECTION oSection4 OF oReport TABLES "SRA" TITLE STR0004
		oSection4:SetHeaderBreak(.T.)
				
			DEFINE CELL NAME "RA_FILIAL" 	OF oSection4 ALIAS "SRA"
			DEFINE CELL NAME "RA_CC" 		OF oSection4 ALIAS "SRA"
			DEFINE CELL NAME "DESCRICAO" 	OF oSection4 TITLE OemToAnsi(STR0045) ALIAS "SRA"  BLOCK {|| DescCc((cAliasMov)->RA_CC,cFilAnterior) } SIZE 30
	                                          
			//-- Section de Funcionarios do Centro de Custo
			DEFINE SECTION oSection5 OF oSection4 TABLES "SRA" TITLE STR0020
			oSection5:SetHeaderBreak(.T.)
				
				DEFINE CELL NAME "RA_MAT" 	OF oSection5 ALIAS "SRA"
				DEFINE CELL NAME "RA_NOME" 	OF oSection5 ALIAS "SRA"

		      //-- Section de Ausencias do Funcionario
				DEFINE SECTION oSection6 OF oSection5 TABLES "SR8", "RCM", "SRV" TITLE STR0001
				oSection6:SetHeaderBreak(.T.)
		
					DEFINE CELL NAME "R8_TIPOAFA" 	OF oSection6 ALIAS "SR8"
					DEFINE CELL NAME "RCM_DESCRI" 	OF oSection6 ALIAS "RCM" SIZE 20
					DEFINE CELL NAME "R8_PD"			OF oSection6 ALIAS "SR8"
					DEFINE CELL NAME "RV_DESC"			OF oSection6 ALIAS "SRV" SIZE 10
					DEFINE CELL NAME "R8_DATAINI"		OF oSection6 ALIAS "SR8"
					DEFINE CELL NAME "R8_DATAFIM"		OF oSection6 ALIAS "SR8"
					DEFINE CELL NAME "R8_DURACAO"		OF oSection6 ALIAS "SR8"
					DEFINE CELL NAME "DURACAOPER"		OF oSection6 TITLE STR0016  ALIAS "   " BLOCK {|nDias| aAreaSR8 := (cAliasSR8)->( GetArea() ), nDias := fDiasAfasPeriodo( (cAliasMov)->RA_MAT,, (cAliasRCM)->RCM_TIPODI, If( dDataDe  < (cAliasSR8)->R8_DATAINI, (cAliasSR8)->R8_DATAINI, dDataDe ), If( dDataAte > (cAliasSR8)->R8_DATAFIM, (cAliasSR8)->R8_DATAFIM, dDataAte ) ), RestArea( aAreaSR8 ), nDias }
					DEFINE CELL NAME "CONTINUA"		OF oSection6 TITLE STR0017  ALIAS "   " BLOCK {|nPos| nPos 	:= aScan( aBoxCont, {|x| x[2] == (cAliasSR8)->R8_CONTINU } ), If( nPos > 0, aBoxCont[nPos,3], "" )} SIZE 3
					If cPaisLoc == "MEX"
						DEFINE CELL NAME "TPAUSENCIA"		OF oSection6 TITLE STR0018  ALIAS "   " BLOCK {|nPos| nPos 	:= aScan( aBoxTipoRSC, {|x| x[2] == (cAliasSR8)->R8_TIPORSC } ), If( nPos > 0, aBoxTipoRSC[nPos,3], "")} SIZE 10
						DEFINE CELL NAME "R8_NCERINC"		OF oSection6 ALIAS "SR8"
					EndIf
					//-- Total da Section Centro de Custo
					DEFINE FUNCTION FROM oSection5:Cell("RA_MAT") 		FUNCTION COUNT NO END REPORT TITLE STR0022 OF oSection4
					DEFINE FUNCTION FROM oSection6:Cell("R8_TIPOAFA") 	FUNCTION COUNT NO END REPORT TITLE STR0026 OF oSection4
					DEFINE FUNCTION FROM oSection6:Cell("R8_DURACAO") 	FUNCTION SUM 	NO END REPORT TITLE STR0031 OF oSection4
					DEFINE FUNCTION FROM oSection6:Cell("DURACAOPER") 	FUNCTION SUM 	NO END REPORT TITLE STR0024 OF oSection4

		//-- Utilizada essa section para as ordens 4=Processo
      //-- Section de Processo
		DEFINE SECTION oSection7 OF oReport TABLES "SRA", "RCJ" TITLE STR0008
		oSection7:SetHeaderBreak(.T.)
				
			DEFINE CELL NAME "RA_PROCES" 	OF oSection7 ALIAS "SRA"
			DEFINE CELL NAME "RCJ_DESCRI"	OF oSection7 ALIAS "RCJ"
	
	      //-- Section de Funcionarios do Processo
			DEFINE SECTION oSection8 OF oSection7 TABLES "SRA" TITLE STR0020
			oSection8:SetHeaderBreak(.T.)
				
				DEFINE CELL NAME "RA_MAT" 		OF oSection8 ALIAS "SRA"
				DEFINE CELL NAME "RA_NOME" 	OF oSection8 ALIAS "SRA"

		      //-- Section de Ausencias do Funcionario
				DEFINE SECTION oSection9 OF oSection8 TABLES "SR8", "RCM", "SRV" TITLE STR0001
				oSection9:SetHeaderBreak(.T.)
		
					DEFINE CELL NAME "R8_TIPOAFA" 	OF oSection9 ALIAS "SR8"
					DEFINE CELL NAME "RCM_DESCRI" 	OF oSection9 ALIAS "RCM" SIZE 20
					DEFINE CELL NAME "R8_PD"			OF oSection9 ALIAS "SR8"
					DEFINE CELL NAME "RV_DESC"			OF oSection9 ALIAS "SRV" SIZE 20
					DEFINE CELL NAME "R8_DATAINI"		OF oSection9 ALIAS "SR8"
					DEFINE CELL NAME "R8_DATAFIM"		OF oSection9 ALIAS "SR8"
					DEFINE CELL NAME "R8_DURACAO"		OF oSection9 ALIAS "SR8"
					DEFINE CELL NAME "DURACAOPER"		OF oSection9 TITLE STR0016  ALIAS "   " BLOCK {|nDias| aAreaSR8 := (cAliasSR8)->( GetArea() ), nDias := fDiasAfasPeriodo( (cAliasMov)->RA_MAT,, (cAliasRCM)->RCM_TIPODI, If( dDataDe  < (cAliasSR8)->R8_DATAINI, (cAliasSR8)->R8_DATAINI, dDataDe ), If( dDataAte > (cAliasSR8)->R8_DATAFIM, (cAliasSR8)->R8_DATAFIM, dDataAte ) ), RestArea( aAreaSR8 ), nDias }
					DEFINE CELL NAME "CONTINUA"		OF oSection9 TITLE STR0017  ALIAS "   " BLOCK {|nPos| nPos 	:= aScan( aBoxCont, {|x| x[2] == (cAliasSR8)->R8_CONTINU } ), If( nPos > 0, aBoxCont[nPos,3], "" )} SIZE 3
					If cPaisLoc == "MEX"
						DEFINE CELL NAME "TPAUSENCIA"		OF oSection9 TITLE STR0018  ALIAS "   " BLOCK {|nPos| nPos 	:= aScan( aBoxTipoRSC, {|x| x[2] == (cAliasSR8)->R8_TIPORSC } ), If( nPos > 0, aBoxTipoRSC[nPos,3], "")} SIZE 10
						DEFINE CELL NAME "R8_NCERINC"		OF oSection9 ALIAS "SR8"
					EndIf
					//-- Total da Section Processo
					DEFINE FUNCTION FROM oSection8:Cell("RA_MAT") 		FUNCTION COUNT NO END REPORT TITLE STR0022 OF oSection7
					DEFINE FUNCTION FROM oSection9:Cell("R8_TIPOAFA") 	FUNCTION COUNT NO END REPORT TITLE STR0026 OF oSection7
					DEFINE FUNCTION FROM oSection9:Cell("R8_DURACAO") 	FUNCTION SUM 	NO END REPORT TITLE STR0031 OF oSection7
					DEFINE FUNCTION FROM oSection9:Cell("DURACAOPER") 	FUNCTION SUM 	NO END REPORT TITLE STR0024 OF oSection7

		//-- Utilizada essa section para as ordens 5=Registro Patronal e Pais Mexico
		If cPaisLoc == "MEX"

	      //-- Section de Codigo Patronal
			DEFINE SECTION oSection10 OF oReport TABLES "SRA", "RCO" TITLE STR0003
			oSection10:SetHeaderBreak(.T.)
					
				DEFINE CELL NAME "RA_CODRPAT" OF oSection10 ALIAS "SRA"
				DEFINE CELL NAME "RCO_NOME"	OF oSection10 ALIAS "RCO"     
				DEFINE CELL NAME "RCO_NREPAT" OF oSection10 ALIAS "RCO"     
		
		      //-- Section de Funcionarios do Codigo Patronal
				DEFINE SECTION oSection11 OF oSection10 TABLES "SRA" TITLE STR0020
				oSection11:SetHeaderBreak(.T.)
					
					DEFINE CELL NAME "RA_MAT" 		OF oSection11 ALIAS "SRA"
					DEFINE CELL NAME "RA_NOME" 	OF oSection11 ALIAS "SRA"
	
			      //-- Section de Ausencias do Funcionario
					DEFINE SECTION oSection12 OF oSection11 TABLES "SR8", "RCM", "SRV" TITLE STR0001
					oSection12:SetHeaderBreak(.T.)
			
						DEFINE CELL NAME "R8_TIPOAFA" 	OF oSection12 ALIAS "SR8"
						DEFINE CELL NAME "RCM_DESCRI" 	OF oSection12 ALIAS "RCM" SIZE 20
						DEFINE CELL NAME "R8_PD"			OF oSection12 ALIAS "SR8"
						DEFINE CELL NAME "RV_DESC"			OF oSection12 ALIAS "SRV" SIZE 20
						DEFINE CELL NAME "R8_DATAINI"		OF oSection12 ALIAS "SR8"
						DEFINE CELL NAME "R8_DATAFIM"		OF oSection12 ALIAS "SR8"
						DEFINE CELL NAME "R8_DURACAO"		OF oSection12 ALIAS "SR8"
						DEFINE CELL NAME "DURACAOPER"		OF oSection12 TITLE STR0016  ALIAS "   " BLOCK {|nDias| aAreaSR8 := (cAliasSR8)->( GetArea() ), nDias := fDiasAfasPeriodo( (cAliasMov)->RA_MAT,, (cAliasRCM)->RCM_TIPODI, If( dDataDe  < (cAliasSR8)->R8_DATAINI, (cAliasSR8)->R8_DATAINI, dDataDe ), If( dDataAte > (cAliasSR8)->R8_DATAFIM, (cAliasSR8)->R8_DATAFIM, dDataAte ) ), RestArea( aAreaSR8 ), nDias }
						DEFINE CELL NAME "CONTINUA"		OF oSection12 TITLE STR0017  ALIAS "   " BLOCK {|nPos| nPos 	:= aScan( aBoxCont, {|x| x[2] == (cAliasSR8)->R8_CONTINU } ), If( nPos > 0, aBoxCont[nPos,3], "" )} SIZE 3
						DEFINE CELL NAME "TPAUSENCIA"		OF oSection12 TITLE STR0018  ALIAS "   " BLOCK {|nPos| nPos 	:= aScan( aBoxTipoRSC, {|x| x[2] == (cAliasSR8)->R8_TIPORSC } ), If( nPos > 0, aBoxTipoRSC[nPos,3], "")} SIZE 10
						DEFINE CELL NAME "R8_NCERINC"		OF oSection12 ALIAS "SR8"
						
						//-- Total da Section de Codigo Patronal
						DEFINE FUNCTION FROM oSection11:Cell("RA_MAT") 			FUNCTION COUNT NO END REPORT TITLE STR0022 OF oSection10
						DEFINE FUNCTION FROM oSection12:Cell("R8_TIPOAFA") 	FUNCTION COUNT NO END REPORT TITLE STR0026 OF oSection10
						DEFINE FUNCTION FROM oSection12:Cell("R8_DURACAO") 	FUNCTION SUM 	NO END REPORT TITLE STR0031 OF oSection10
						DEFINE FUNCTION FROM oSection12:Cell("DURACAOPER") 	FUNCTION SUM 	NO END REPORT TITLE STR0024 OF oSection10
		EndIf

Return oReport

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³PrintReport³ Autor ³ R.H. - Tatiane Matias   ³ Data ³ 13.06.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³                                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ PrintReport(oReport)                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function PrintReport(oReport)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao de Variaveis Locais                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//-- Numerico
Local nOrdem   	  		:= oReport:Section(1):GetOrder()
Local nTamProc    		:= GetSx3Cache( "RA_PROCES", "X3_TAMANHO" )
Local nTamCodAfas 		:= GetSx3Cache( "RCM_TIPO", "X3_TAMANHO" )
Local nTamCodPD  		:= GetSx3Cache( "RV_COD", "X3_TAMANHO" )
Local nCont		  		:= 0

//-- Objeto
Local oSection1 		:= oReport:Section(1)
Local oSection2 		:= IIf(nOrdem == 1 .OR. nOrdem == 2, oReport:Section(2), IIf(nOrdem == 3, oReport:Section(3), IIf(nOrdem == 4, oReport:Section(4), oReport:Section(5))))
Local oSection3 		:= oSection2:Section(1)
Local oSection4 		:= oSection3:Section(1)
Local cParSM0			:= mv_par02
//-- Logico
Local lAllProc	  		// Se devera considerar todos os Processos
Local lAllAfast  		// Se devera considerar todos os Tipos de Afastamentos
Local lAllPD	  		// Se devera considerar todos os Tipos de Afastamentos

//--String
Local cMaxProces		:= ""
Local cMinProces		:= "ZZZZZZ"
Local cFiltro			:= ""
Local cSitQuery			:= "" 
Local cCatQuery			:= ""
Local cMotQuery			:= ""
Local cMotAus			:= ""
Local cSvProcesso
Local cSvCodAfas
Local cSvCodPD 
Local cCposSel			:= ""
Local cFrom				:= ""
Local cWhere			:= ""
Local SR8Ordem			:= RetOrdem("SR8","R8_FILIAL+R8_MAT+DTOS(R8_DATAINI)+R8_TIPOAFA+STR(R8_DIASEMP)" )

//Monta filtro das filiais
Local cJoinSRV := "% " + fTbJoinSQL("SRV", "SRA") + " %"
Local cJoinRCM := "% " + fTbJoinSQL("RCM", "SRA") + " %"
Local cJoinRCJ := "% " + fTbJoinSQL("RCJ", "SRA") + " %"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao de Variaveis Privadas                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//-- Array
Private aBoxCont	:= {}
Private aBoxTipoRSC	:= {}
Private aBoxAusRCM	:= {}
Private aCodFol 	:= {}

//--String
Private cAliasMov   	:= "SRA"
Private cAliasSR8		:= "SR8"
Private cAliasRCM		:= "RCM"

Private cFilAnterior	:= ""
Private cAcessaSRA  	:= &("{ || " + ChkRH("GPER040","SRA","2") + "}")

   //-- Carrega variaveis da pergunte GP580R         
	cProcesso	:= mv_par01							// Processos
	cSit     	:= mv_par06							// Situacao
	cCat     	:= mv_par07							// Categoria
	cCodAfas 	:= mv_par08							// Lista de Cod. Tipo Ausencias
	cCodPD	 	:= mv_par09							// Lista de Verbas
	If AllTrim(cCodPd) <> "*"
		cCodPD	+= mv_par10							// Cont. Lista de Verbas
	EndIf
	cTipLan  	:= mv_par11							// Lancamentos
	dDataDe  	:= mv_par12							// Data Ausencia De
	dDataAte 	:= mv_par13							// Data Ausencia Ate
	lImpFil  	:= (mv_par14 == 1)					// Imprimir Total Filial
	lImpEmp  	:= (mv_par15 == 1)					// Imprimir Total Empresa
    If cPaisLoc=="VEN"
		cMotAus := mv_par16    
    Endif


	oSection2:SetRelation({||cFilaInfo},"",1)
	//oSection2:SetParentFilter({|cParam| ALLTRIM(SRA->RA_FILIAL)== ALltrim(cParam)},{|| cEmpAnt+alltrim(SRA->RA_FILIAL) })	
	//-- Verifica se imprime total da Filial
	If lImpFil
		
		//-- Total da Filial
		DEFINE BREAK oBreakFil OF oReport WHEN {||  cFilaInfo + ' - ' + aInfo[1] } TITLE STR0036  TOTAL IN LINE
		
		If nOrdem == 1 .OR. nOrdem == 2
			DEFINE COLLECTION OF oSection3 FUNCTION SUM FORMULA {|| oSection3:Cell("R8_TIPOAFA"):GetValue(.T.) + " - " + oSection3:Cell("RCM_DESCRI"):GetValue(.T.)} CONTENT oSection3:Cell("R8_DURACAO") 	BREAK oBreakFil  TITLE OemToAnsi(STR0031) PICTURE "@E 999,999,999.99" 	NO END SECTION NO END REPORT 
			DEFINE COLLECTION OF oSection3 FUNCTION SUM FORMULA {|| oSection3:Cell("R8_TIPOAFA"):GetValue(.T.) + " - " + oSection3:Cell("RCM_DESCRI"):GetValue(.T.)} CONTENT oSection3:Cell("DURACAOPER") 	BREAK oBreakFil  TITLE OemToAnsi(STR0024) PICTURE "@E 999,999,999.99" 	NO END SECTION NO END REPORT

			DEFINE FUNCTION FROM oSection2:Cell("RA_MAT") 		FUNCTION COUNT BREAK oBreakFil NO END SECTION NO END REPORT TITLE STR0022
			DEFINE FUNCTION FROM oSection3:Cell("R8_TIPOAFA") 	FUNCTION COUNT BREAK oBreakFil NO END SECTION NO END REPORT TITLE STR0026
			DEFINE FUNCTION FROM oSection3:Cell("R8_DURACAO") 	FUNCTION SUM 	BREAK oBreakFil NO END SECTION NO END REPORT TITLE STR0031
			DEFINE FUNCTION FROM oSection3:Cell("DURACAOPER") 	FUNCTION SUM 	BREAK oBreakFil NO END SECTION NO END REPORT TITLE STR0024
		Else	
			DEFINE COLLECTION OF oSection4 FUNCTION SUM FORMULA {|| oSection4:Cell("R8_TIPOAFA"):GetValue(.T.) + " - " + oSection4:Cell("RCM_DESCRI"):GetValue(.T.)} CONTENT oSection4:Cell("R8_DURACAO") 	BREAK oBreakFil  TITLE OemToAnsi(STR0031) PICTURE "@E 999,999,999.99" 	NO END SECTION NO END REPORT
			DEFINE COLLECTION OF oSection4 FUNCTION SUM FORMULA {|| oSection4:Cell("R8_TIPOAFA"):GetValue(.T.) + " - " + oSection4:Cell("RCM_DESCRI"):GetValue(.T.)} CONTENT oSection4:Cell("DURACAOPER") 	BREAK oBreakFil  TITLE OemToAnsi(STR0024) PICTURE "@E 999,999,999.99" 	NO END SECTION NO END REPORT

			DEFINE FUNCTION FROM oSection3:Cell("RA_MAT") 		FUNCTION COUNT BREAK oBreakFil NO END SECTION NO END REPORT TITLE STR0022
			DEFINE FUNCTION FROM oSection4:Cell("R8_TIPOAFA") 	FUNCTION COUNT BREAK oBreakFil NO END SECTION NO END REPORT TITLE STR0026
			DEFINE FUNCTION FROM oSection4:Cell("R8_DURACAO") 	FUNCTION SUM 	BREAK oBreakFil NO END SECTION NO END REPORT TITLE STR0031
			DEFINE FUNCTION FROM oSection4:Cell("DURACAOPER") 	FUNCTION SUM 	BREAK oBreakFil NO END SECTION NO END REPORT TITLE STR0024
		EndIf
	EndIf
	
	//-- Verifica se imprime total da Empresa
	If lImpEmp
		//-- Total do Relatorio
		If nOrdem == 1 .OR. nOrdem == 2
			DEFINE COLLECTION OF oSection3 FUNCTION SUM FORMULA {|| oSection3:Cell("R8_TIPOAFA"):GetValue(.T.) + " - " + oSection3:Cell("RCM_DESCRI"):GetValue(.T.)} CONTENT oSection3:Cell("R8_DURACAO") 	TITLE OemToAnsi(STR0031) PICTURE "@E 999,999,999.99" 	NO END SECTION
			DEFINE COLLECTION OF oSection3 FUNCTION SUM FORMULA {|| oSection3:Cell("R8_TIPOAFA"):GetValue(.T.) + " - " + oSection3:Cell("RCM_DESCRI"):GetValue(.T.)} CONTENT oSection3:Cell("DURACAOPER") 	TITLE OemToAnsi(STR0024) PICTURE "@E 999,999,999.99" 	NO END SECTION

			DEFINE FUNCTION FROM oSection2:Cell("RA_MAT") 		FUNCTION COUNT NO END SECTION TITLE STR0022
			DEFINE FUNCTION FROM oSection3:Cell("R8_TIPOAFA") 	FUNCTION COUNT NO END SECTION TITLE STR0026
			DEFINE FUNCTION FROM oSection3:Cell("R8_DURACAO") 	FUNCTION SUM 	NO END SECTION TITLE STR0031
			DEFINE FUNCTION FROM oSection3:Cell("DURACAOPER") 	FUNCTION SUM 	NO END SECTION TITLE STR0024
		Else
			DEFINE COLLECTION OF oSection4 FUNCTION SUM FORMULA {|| oSection4:Cell("R8_TIPOAFA"):GetValue(.T.) + " - " + oSection4:Cell("RCM_DESCRI"):GetValue(.T.)} CONTENT oSection4:Cell("R8_DURACAO") 	TITLE OemToAnsi(STR0031) PICTURE "@E 999,999,999.99" 	NO END SECTION
			DEFINE COLLECTION OF oSection4 FUNCTION SUM FORMULA {|| oSection4:Cell("R8_TIPOAFA"):GetValue(.T.) + " - " + oSection4:Cell("RCM_DESCRI"):GetValue(.T.)} CONTENT oSection4:Cell("DURACAOPER") 	TITLE OemToAnsi(STR0024) PICTURE "@E 999,999,999.99" 	NO END SECTION

			DEFINE FUNCTION FROM oSection3:Cell("RA_MAT") 		FUNCTION COUNT NO END SECTION TITLE STR0022
			DEFINE FUNCTION FROM oSection4:Cell("R8_TIPOAFA") 	FUNCTION COUNT NO END SECTION TITLE STR0026
			DEFINE FUNCTION FROM oSection4:Cell("R8_DURACAO") 	FUNCTION SUM 	NO END SECTION TITLE STR0031
			DEFINE FUNCTION FROM oSection4:Cell("DURACAOPER") 	FUNCTION SUM 	NO END SECTION TITLE STR0024
		EndIf
	EndIf

	lAllAfast 	:= AllTrim( cCodAfas ) 	== "*"
	lAllProc 	:= AllTrim( cProcesso )	== "*"
	lAllPD 		:= AllTrim( cCodPD ) 	== "*"

	Sx3Box2Arr( "R8_CONTIN" , @aBoxCont )
	If cPaisLoc == "MEX"
		Sx3Box2Arr( "R8_TIPORSC", @aBoxTipoRSC )
	ElseIf cPaisLoc == "VEN"	
		Sx3Box2Arr( "RCM_TIPOBS", @aBoxAusRCM )	
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz filtro no arquivo...                                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	#IFDEF TOP

		cAliasMov 	:= GetNextAlias()
		cAliasSR8	:= cAliasMov
		cAliasRCM	:= cAliasMov

		//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
		MakeSqlExpr("GPE580R")

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Modifica variaveis para a Query                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		//-- Situacao do Funcionario
		For nCont:=1 to Len(cSit)
			cSitQuery += "'"+Subs(cSit,nCont,1)+"'"
			If ( nCont+1 ) <= Len(cSit)
				cSitQuery += "," 
			Endif
		Next nCont     
		cSitQuery := "%" + cSitQuery + "%"

		//-- Categoria do Funcionario
		cCatQuery := ""
		For nCont:=1 to Len(cCat)
			cCatQuery += "'"+Subs(cCat,nCont,1)+"'"
			If ( nCont+1 ) <= Len(cCat)
				cCatQuery += "," 
			Endif
		Next nCont
		cCatQuery := "%" + cCatQuery + "%"

		If cPaisLoc=="VEN"
			//-- Motivo da Ausencia
			cMotQuery := ""
			For nCont:=1 to Len(cMotAus)
				cMotQuery += "'"+Subs(cMotAus,nCont,1)+"'"
				If ( nCont+1 ) <= Len(cMotAus)
					cMotQuery += "," 
				Endif
			Next nCont
			cMotQuery := cMotQuery
        Endif
		//-- Monta a string de Processos para Impressao
		If !lAllProc       
			cSvProcesso := cProcesso
			cProcesso 	:= ""
			For nCont:= 1 to Len(Alltrim(cSvProcesso)) Step nTamProc
				cProcesso += "'" + Substr(cSvProcesso, nCont, nTamProc)+Space(nTamProc-Len(Substr(cSvProcesso,nCont,nTamProc)))+ "',"
				If Substr(cSvProcesso, nCont, nTamProc) > cMaxProces 
					cMaxProces := Substr(cSvProcesso, nCont, nTamProc)
				EndIf
				If Substr(cSvProcesso, nCont, nTamProc) < cMinProces
					cMinProces := Substr(cSvProcesso, nCont, nTamProc)
				EndIf
			Next
			cProcesso := Substr( cProcesso, 1, Len(cProcesso)-1)
			If !Empty(AllTrim(cProcesso))
				cFiltro 	+= 'SRA.RA_PROCES IN (' + cProcesso + ') AND '
			EndIf
		EndIf

		//-- Monta a string de Codigos de Afastamentos para Impressao
		If !lAllAfast
			cSvCodAfas := cCodAfas
			cCodAfas   := ""
			For nCont:= 1 to Len(Alltrim(cSvCodAfas)) Step nTamCodAfas
				cCodAfas += "'" + Substr(cSvCodAfas, nCont, nTamCodAfas) + "',"
			Next
			cCodAfas := Substr( cCodAfas, 1, Len(cCodAfas)-1)
			If !Empty(AllTrim(cCodAfas))
				cFiltro 	+= 'SR8.R8_TIPOAFA IN (' + cCodAfas + ') AND '
			EndIf
		EndIf

		//-- Monta a string de Codigos de Verbas para Impressao
		If !lAllPD
			cSvCodPD := cCodPD
			cCodPD := ""
			For nCont:= 1 to Len(Alltrim(cSvCodPD)) Step nTamCodPD
				cCodPD += "'" + Substr(cSvCodPD, nCont, nTamCodPD) + "',"
			Next
			cCodPD := Substr( cCodPD, 1, Len(cCodPD)-1)
			If !Empty(AllTrim(cCodPD))
				cFiltro 	+= 'SR8.R8_PD IN (' + cCodPD + ') AND '
			EndIf
		EndIf

		//-- Seleciona apenas os registros com saldo (cTipLan == 1) ou
		//   sem saldo (cTipLan == 2)
		cFiltro += IIf(cTipLan == 1	, " SR8.R8_SDPAGAR  <> 0 AND ", "")
		cFiltro += IIf(cTipLan == 2	, " SR8.R8_SDPAGAR  = 0  AND ", "")
		cFiltro := "%" + cFiltro + "%"

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta string de ordenacao                                          		  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nOrdem == 1												// Matricula
			cOrdem := "%RA_FILIAL, RA_MAT%"
		ElseIf nOrdem == 2										// Nome
			cOrdem := "%RA_FILIAL, RA_NOME, RA_MAT%"
		ElseIf nOrdem == 3										// Centro de Custo
			cOrdem := "%RA_FILIAL, RA_CC, RA_MAT%"
		ElseIf nOrdem == 4										// Processo
			cOrdem := "%RA_FILIAL, RA_PROCES, RA_MAT%"
		ElseIf nOrdem == 5 .and. cPaisLoc == "MEX"		// Registro Patronal
			cOrdem := "%RA_FILIAL, RA_CODRPAT, RA_MAT%"
		EndIf

		cCposSel := "%"
		cCposSel += "RA_FILIAL, RA_MAT, RA_NOME, RA_TNOTRAB, RA_SEQTURN, RA_CC, RA_PROCES, "
		cCposSel += "R8_FILIAL, R8_MAT, R8_TIPOAFA, R8_PD, R8_DATAINI, R8_DATAFIM, R8_DURACAO, "
		cCposSel += "R8_CONTINU, RV_DESC, RCM_DESCRI, RCM_TIPODI, RCJ_DESCRI"
		If	cPaisLoc == "MEX"
			cCposSel += ", RCO_NOME, RCO_NREPAT , RA_CODRPAT, R8_NCERINC, R8_TIPORSC "
			cFrom    := "%," +  RetSqlName("RCO") + " RCO " + "%"
			cWhere   := "%SRA.RA_CODRPAT = RCO.RCO_CODIGO					AND%"
		ElseIf	cPaisLoc == "VEN"			
			cCposSel += ", RCM_TIPOBS "
			cWhere   := "%RCM.RCM_TIPOBS IN ("+cMotQuery+") 	AND%"
			cFrom	 :="%%"
		Else
			cWhere	 := "%%"
			cFrom	 :="%%"
		EndIf
		cCposSel += "%"
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inicializa Query                                                  		  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		BEGIN REPORT QUERY oSection2

		BeginSql alias cAliasMov
			SELECT
				%exp:cCposSel%
			FROM %table:SRA% SRA, %table:SR8% SR8, %table:SRV% SRV, 
				  %table:RCM% RCM, %table:RCJ% RCJ %exp:cFrom%
			WHERE SRA.RA_FILIAL 	= SR8.R8_FILIAL					AND
					SRA.RA_MAT 		= SR8.R8_MAT					  	AND
					SR8.R8_PD 		= SRV.RV_COD						AND
					SR8.R8_TIPOAFA  = RCM.RCM_TIPO						AND
					SRA.RA_PROCES	= RCJ.RCJ_CODIGO				  	AND
					%exp:cJoinSRV% AND %exp:cJoinRCM% AND %exp:cJoinRCJ% AND
					%exp:cWhere%
					SRA.RA_SITFOLH IN (%exp:Upper(cSitQuery)%) 	AND
				    SRA.RA_CATFUNC IN (%exp:Upper(cCatQuery)%) 	AND
					((SR8.R8_DATAFIM BETWEEN %exp:dDataDe% AND %exp:dDataAte%) OR
					 (SR8.R8_DATAFIM = '        ' AND SR8.R8_DATAINI <= %exp:dDataAte% )) AND
				   %exp:cFiltro%
					SR8.%notDel% AND SRA.%notDel% AND SRV.%notDel% AND RCM.%notDel% AND RCJ.%notDel%
			ORDER BY %exp:cOrdem%
		EndSql
		
		/*
		Prepara relatorio para executar a query gerada pelo Embedded SQL passando como 
		parametro a pergunta ou vetor com perguntas do tipo Range que foram alterados 
		pela funcao MakeSqlExpr para serem adicionados a query
		*/
		END REPORT QUERY oSection2 PARAM mv_par02, mv_par03, mv_par04, mv_par05, if (cPaisLoc == "MEX", mv_par16, "")

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Relacionamento entre as Section (Filhas e Pais)                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nOrdem == 1	 .OR. nOrdem == 2		// Matricula e Nome

			//-- Utiliza a query do Pai (Section2) - Funcionarios
			oSection3:SetParentQuery()

			//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
			//-- Filial e matricula da tabela de funcionarios (SRA) com a tabela de ausencias (SR8)
			oSection3:SetParentFilter({|cParam| (cAliasMov)->R8_FILIAL + (cAliasMov)->R8_MAT == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT })

		Else
			//-- Utiliza a query do Pai (Section2) 
			// 				Ordem 3 (Centro de Custo) 	- Centro de Custo do funcionario
			// 				Ordem 4 (Processo) 			- Processo do funcionario
			// 				Ordem 5 (Codigo Patronal) 	- Codigo Patronal do funcionario
			oSection3:SetParentQuery()
			

			//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
			If nOrdem == 3
				//-- Filtro por Filial e Centro de Custo
				oSection3:SetParentFilter({|cParam| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CC == cParam}			,{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CC })

			ElseIf nOrdem == 4
				//-- Filtro por Filial e Processo
				oSection3:SetParentFilter({|cParam| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_PROCES == cParam}		,{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_PROCES })

			ElseIf nOrdem == 5 .and. cPaisLoc == "MEX"		
				//-- Filtro por Filial e Codigo Patronal
				oSection3:SetParentFilter({|cParam| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CODRPAT == cParam}	,{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CODRPAT })

			EndIf

			//-- Utiliza a query do Pai (Section3) - Funcionario
			oSection4:SetParentQuery()

			//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
			//-- Filial e matricula da tabela de funcionarios (SRA) com a tabela de ausencias (SR8)
			oSection4:SetParentFilter({|cParam| (cAliasMov)->R8_FILIAL + (cAliasMov)->R8_MAT == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT })

		EndIf		
		
	#ELSE             
		cAliasMov   := "SRA"
		cAliasSR8	:= "SR8"  
		cAliasRCM	:= "RCM"
		
		//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
		MakeAdvplExpr("GPE580R")

		DbSelectArea(cAliasMov)

		//-- Adiciona no filtro o parametro tipo Range
		//-- Filial
		If !Empty(mv_par02)
			cFiltro += mv_par02 + " .AND. "
		EndIf                 
		
		//-- Centro de Custo
		If !Empty(mv_par03)
			cFiltro += mv_par03 + " .AND. "
		EndIf                 

		//-- Matricula
		If !Empty(mv_par04)
			cFiltro += mv_par04 + " .AND. "
		EndIf                 

		//-- Nome
		If !Empty(mv_par05)
			cFiltro += mv_par05 + " .AND. "
		EndIf                 

		//-- Registro Patronal
		If !Empty(mv_par16) .And. cPaisLoc=="MEX"
			cFiltro += mv_par16 + " .AND. "
		EndIf                 
		
	  	cFiltro += 'RA_SITFOLH $ "' + cSit + '" .AND.'
		cFiltro += 'RA_CATFUNC $ "' + cCat + '"'      
		  
		
		If !lAllProc
			cFiltro += ' .AND. RA_PROCES $ "' + cProcesso + '"'
		EndIf
                               
		//-- Filtra a tabela de Funcionarios conforme perguntes
	  	oSection2:SetFilter(cFiltro) 

		//-- Monta string de filtro das ausencias
		cFiltro := " ((R8_DATAFIM >= dDataDe .and. R8_DATAINI <= dDataAte ) .Or. "
		cFiltro += "    ( Empty(R8_DATAFIM) .and. R8_DATAINI <= dDataAte )) "
		cFiltro += IIf(!lAllAfast		, " .And. R8_TIPOAFA 	$ cCodAfas ", "")
		cFiltro += IIf(!lAllPD			, " .And. R8_PD 		$ cCodPD "	, "")
		cFiltro += IIf(cTipLan == 1	, " .And. R8_SDPAGAR  <> 0 "		, "")
		cFiltro += IIf(cTipLan == 2	, " .And. R8_SDPAGAR  = 0 "			, "")
		If cPaisLoc=="VEN"
			cFiltro += 'RCM_TIPOBS $ "' + cMotAus + '"'      
		Endif
		If nOrdem == 1	 .OR. nOrdem == 2
			//-- Filtra a tabela de Ausencias conforme perguntes
		  	oSection3:SetFilter(cFiltro)    

			//-- Relaciona a Section Filha (Ausencia) com a Section Pai (Funcionario)
			oSection3:SetRelation({|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT },"SR8", SR8Ordem ,.T.)
			//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
			//-- Filial e matricula da tabela de funcionarios (SRA) com a tabela de ausencias (SR8)
			oSection3:SetParentFilter({|cParam| (cAliasSR8)->R8_FILIAL + (cAliasSR8)->R8_MAT == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT })

			If nOrdem == 1							// Matricula
				oSection2:SetIdxOrder ( 1 )
			ElseIf nOrdem == 2					// Nome
				oSection2:SetIdxOrder ( 3 )
			EndIf

			//-- Posiciona as tabelas de verbas e tipo de ausencia
			TRPosition():New(oSection3,"SRV",1,{|| xFilial("SRV")+(cAliasSR8)->R8_PD})
			TRPosition():New(oSection3,"RCM",1,{|| xFilial("RCM")+(cAliasSR8)->R8_TIPOAFA})
		Else
		
			//-- Filtra a tabela de Ausencias conforme perguntes
		  	oSection4:SetFilter(cFiltro) 

			//-- Relaciona a Section Filha (Ausencia) com a Section Pai (Funcionario)
			oSection4:SetRelation({|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT },"SR8",SR8Ordem,.T.)

			//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
			//-- Filial e matricula da tabela de funcionarios (SRA) com a tabela de ausencias (SR8)
			oSection4:SetParentFilter({|cParam| (cAliasSR8)->R8_FILIAL + (cAliasSR8)->R8_MAT == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT })
		
			If nOrdem == 3							// Centro de Custo
				oSection2:SetIdxOrder ( 2 )

				//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
				//-- Filtro por Filial e Centro de Custo
				oSection3:SetParentFilter({|cParam| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CC == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CC })

			ElseIf nOrdem == 4					// Processo
				oSection2:SetIdxOrder ( RetOrder( "SRA", "RA_FILIAL+RA_PROCES+RA_MAT"))

				//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
				//-- Filtro por Filial e Processo
				oSection3:SetParentFilter({|cParam| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_PROCES == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_PROCES })

				//-- Posiciona a tabela de processo
				TRPosition():New(oSection2,"RCJ",1,{|| xFilial("RCJ") + (cAliasMov)->RA_PROCES})

			ElseIf nOrdem == 5 .and. cPaisLoc == "MEX"		// Registro Patronal
				oSection2:SetIdxOrder ( RetOrder( "SRA", "RA_FILIAL+RA_CODRPAT+RA_MAT") )

				//-- Condicao de relacionamento da secao filha em relacao a sua secao pai
				//-- Filtro por Filial e Codigo Patronal
				oSection3:SetParentFilter({|cParam| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CODRPAT == cParam},{|| (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_CODRPAT })

				//-- Posiciona a tabela de registro patronal
				TRPosition():New(oSection2,"RCO",1,{|| xFilial("RCO") + (cAliasMov)->RA_CODRPAT})
			EndIf
				
			//-- Posiciona as tabelas de verbas e tipo de ausencia
			TRPosition():New(oSection4,"SRV",1,{|| xFilial("SRV")+(cAliasSR8)->R8_PD})
			TRPosition():New(oSection4,"RCM",1,{|| xFilial("RCM")+(cAliasSR8)->R8_TIPOAFA})

		EndIf
	
		(cAliasMov)->( DbGoTop() )

	#Endif	

	//-- Condicao de impressao do Funcionario
	oSection2:SetLineCondition({|| fCondSRA() }) 
  	If nOrdem <> 1 .AND. nOrdem <> 2
  		oSection3:SetLineCondition({|| SR8->( DbSeek( (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT, .F.) ) }) 
  	EndIf
  	
	//-- Carrega informacoes da Filial
	cFilaInfo := (cAliasMov)->RA_FILIAL
	fInfo(@aInfo,(cAliasMov)->RA_FILIAL)
	
	//-- Impressao na quebra de pagina - Impressao da informacao da Empresa e Filial
	oReport:OnPageBreak({|| oSection1:Init(), oSection1:Finish() })

	//-- Define o total da regua da tela de processamento do relatorio
	oReport:SetMeter((cAliasMov)->( RecCount() ))  

	//-- Impressao do Relatorio
	oSection2:Print()

	#IFNDEF TOP 
		dbSelectArea(cAliasMov)
		DbClearFilter()
		dbSetOrder(1)
	#Endif	

Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fCondSRA   ³ Autor ³ R.H. - Tatiane Matias   ³ Data ³ 13.06.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se a linha deve ser impressa                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fCondSRA()                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fCondSRA()

Local lRetorno
	
	lRetorno := .T.           

	//-- Verifica a existencia de Ausencias para o Funcionario
  	If SR8->( DbSeek( (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT, .F.) )

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Verifica Quebra de Filial                                    ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If (cAliasMov)->RA_FILIAL # cFilAnterior
			If !FP_CODFOL(@aCodFol,(cAliasMov)->RA_FILIAL) .Or. !fInfo(@aInfo,(cAliasMov)->RA_FILIAL)
				//-- Registro nao deve ser impressao (Nao acessa Filial ou nao carregado os identificadores de calculo) 
				lRetorno := .F.                                    

				//-- Carrega os arrays com as informacoes da Filial anterior.
				FP_CODFOL(@aCodFol,cFilAnterior)
				fInfo(@aInfo,cFilAnterior)

			Else
				cFilAnterior 	:= (cAliasMov)->RA_FILIAL
				cFilaInfo 		:= (cAliasMov)->RA_FILIAL
			EndIf
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consiste controle de acessos e filiais validas			   |
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If !((cAliasMov)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
			//-- Registro nao deve ser impressao (Nao acessa tabela de Funcionarios ou Filial invalida) 
			lRetorno := .F.
		EndIf        
	Else
		//-- Registro nao deve ser impressao (Funcionario não possui ausencia a ser impressa) 
		lRetorno := .F.
	EndIf
		
Return lRetorno

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GPER580R3³ Autor ³ Maurico T. Takakura   ³ Data ³ 06.10.05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Afastamentos e Ausencias                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GpeR580(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GpeR580R3()

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define Variaveis Locais (Basicas)                            ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Local cString		:= "SRA"        // alias do arquivo principal (Base)
Local cDesc1 		:= STR0001		// "Ausencias"
Local cDesc2 		:= STR0002		// "Ser  impresso de acordo com os parametros solicitados pelo usuario."
Local cDesc3		:= ""
Local nCont			:= 0
Local cSvProcesso
Local cSvCodAfas
Local cSvCodPD

Local aOrd      	:= {STR0005,STR0006,STR0004,STR0008}	//"Matricula"###"Nome"###"Centro de Custo"###"Processo"
If cPaisLoc == "MEX"
	aAdd( aOrd, STR0003 )	// "Registro Patronal"
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define Variaveis Private(Basicas)                            ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Private aReturn		:= { STR0009, 1,STR0010, 2, 2, 1,"",1 }	//"Zebrado"###"Administra‡„o"
Private cPerg   	:= "GPER580"
Private Titulo		:= STR0011		//"IMPRESSÄO DE AUSENCIAS"

Private lAllProc					// Se devera considerar todos os Processos
Private lAllAfast					// Se devera considerar todos os Tipos de Afastamentos
Private lAllPD						// Se devera considerar todos os Tipos de Afastamentos

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis Utilizadas na funcao IMPR                          ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Private aLinha  	:= {}
Private nOrdem
Private nTamanho	:= "G"
Private nLastKey 	:= 0
Private nomeprog	:= "GPER580"

Private Li	        := 0
Private nTamProc    := GetSx3Cache( "RA_PROCES", "X3_TAMANHO" )
Private nTamCodAfas := GetSx3Cache( "RCM_TIPO", "X3_TAMANHO" )
Private nTamCodPD	:= GetSx3Cache( "RV_COD", "X3_TAMANHO" )

Private wCabec1 := ""
Private wCabec2 := ""

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis de Controle 			                           ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Private cCcAnt
Private cProcesAnt
Private cCodRPatAnt
Private cFilAnterior
Private cFim
Private cInicio
Private cMinProces	:= "ZZZZZZ"
Private cMaxProces	:= ""

Private nTotFunQueb		// Totalizador de Funcionario por Quebra de Ordem
Private nTotFunFil		// Totalizador de Funcionario por Filial
Private nTotFunEmp		// Totalizador de Funcionario por Empresa

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Atualizar o Pergunte do Relatorio                            ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/


/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Verifica as perguntas selecionadas                           ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
pergunte("GPER580",.F.)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Envia controle para a funcao SETPRINT                        ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
wnrel:="GPER580"           //Nome Default do relatorio em Disco
wnrel:=SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,nTamanho)

If nLastKey = 27
	Return
EndIf

SetDefault(aReturn,cString)

If nLastKey = 27
	Return
Endif

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Variaveis utilizadas para parametros                         ³
³ mv_par01        //  Processo						           ³
³ mv_par02        //  Filial  De                               ³
³ mv_par03        //  Filial  Ate                              ³
³ mv_par04        //  Centro de Custo De                       ³
³ mv_par05        //  Centro de Custo Ate                      ³
³ mv_par06        //  Matricula De                             ³
³ mv_par07        //  Matricula Ate                            ³
³ mv_par08        //  Nome De                                  ³
³ mv_par09        //  Nome Ate                                 ³
³ mv_par10        //  Situacao                                 ³
³ mv_par11        //  Categoria                                ³
³ mv_par12        //  Lista de Cod. Tipo Ausencias             ³
³ mv_par13        //  Lista de Verbas                          ³
³ mv_par14        //  Cont. Lista de Verbas                    ³
³ mv_par15        //  Lancamentos                              ³
³ mv_par16        //  Data Ausencia De:                        ³
³ mv_par17        //  Data Ausencia Ate:                       ³
³ mv_par18		  //  Imprimir Total Filial					   ³
³ mv_par19        //  Imprimir Total Empresa                   ³
³ mv_par20        //  Registro Patronal De:                    ³
³ mv_par21        //  Registro Patronal Ate:                   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Carregando variaveis mv_par?? para Variaveis do Sistema.     ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
nOrdem   := aReturn[8]

cProcesso:= mv_par01
cFilDe   := mv_par02
cFilAte  := mv_par03
cCcDe    := mv_par04
cCcAte   := mv_par05
cMatDe   := mv_par06
cMatAte  := mv_par07
cNomDe   := mv_par08
cNomAte  := mv_par09
cSit     := mv_par10
cCat     := mv_par11
cCodAfas := mv_par12
cCodPD	 := mv_par13
If AllTrim(cCodPd) <> "*"
	cCodPD	 += mv_par14
EndIf
cTipLan  := mv_par15
dDataDe  := mv_par16
dDataAte := mv_par17
lImpFil  := (mv_par18 == 1)
lImpEmp  := (mv_par19 == 1)
cRgPatDe := mv_par20
cRgPatAte:= mv_par21

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Gerar lista de Itens dos Processos.  										  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
lAllProc 	:= ( AllTrim( cProcesso ) == "*" ) .Or. Empty( cProcesso )
If !lAllProc
	cSvProcesso := cProcesso
	cProcesso 	:= "("
	For nCont:= 1 to Len(Alltrim(cSvProcesso)) Step nTamProc
		cProcesso += "'" + Substr(cSvProcesso, nCont, nTamProc)+Space(nTamProc-Len(Substr(cSvProcesso,nCont,nTamProc)))+ "',"
		If Substr(cSvProcesso, nCont, nTamProc) > cMaxProces 
			cMaxProces := Substr(cSvProcesso, nCont, nTamProc)
		EndIf
		If Substr(cSvProcesso, nCont, nTamProc) < cMinProces
			cMinProces := Substr(cSvProcesso, nCont, nTamProc)
		EndIf
	Next
	cProcesso := Substr( cProcesso, 1, Len(cProcesso)-1) + ")"
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Monta Filtro dos Tipos de Afastamento 										  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
lAllAfast 	:= ( AllTrim( cCodAfas ) == "*" ) .Or. Empty( cCodAfas )
If !lAllAfast
	cSvCodAfas := cCodAfas
	cCodAfas   := "("
	For nCont:= 1 to Len(Alltrim(cSvCodAfas)) Step nTamCodAfas
		cCodAfas += "'" + Substr(cSvCodAfas, nCont, nTamCodAfas) + "',"
	Next
	cCodAfas := Substr( cCodAfas, 1, Len(cCodAfas)-1) + ")"
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³Monta Filtro das Verbas			  										  ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
lAllPD 	:= ( AllTrim( cCodPD ) == "*" ) .Or. Empty( cCodPD )
If !lAllPD
	cSvCodPD := cCodPD
	cCodPD   := "("
	For nCont:= 1 to Len(Alltrim(cSvCodPD)) Step nTamCodPD
		cCodPD += "'" + Substr(cSvCodPD, nCont, nTamCodPD) + "',"
	Next
	cCodPD := Substr( cCodPD, 1, Len(cCodPD)-1) + ")"
EndIf

RptStatus({|lEnd| GR580Imp(@lEnd,wnRel,cString)},Capital(Titulo))

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ GR580Imp ³ Autor ³ Mauricio Takakura     ³ Data ³ 06.10.05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ausencias e Afastamentos                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ GR580Imp(lEnd,wnRel,cString)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lEnd        - A‡ao do Codelock                             ³±±
±±³          ³ wnRel       - T¡tulo do relat¢rio                          ³±±
±±³          ³ cString     - Mensagem                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Emissao dos Afastamentos, Ausencias e Faltas               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GR580Imp(lEnd,WnRel,cString)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Define Variaveis Private			                           ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Private aCodFol  := {}
Private aEmp     := {}  		// Empresa
Private aFil     := {}  		// Filial
Private aCc      := {}  		// Centro de Custo
Private aProc	 := {} 			// Processo 
If cPaisLoc == "MEX"
	Private aCodRPat := {}		// Codigo Registro Patronal
EndIf

Private cCcto		:= ""

Private cAliasMov   := "SRA"

fImpAusen(lEnd)

dbSelectArea("SRA")
DbClearFilter()
dbSetOrder(1)

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Termino do relatorio                                         ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
//--Gerar Rodape no final da Impressao
Li := 58
Impr("","F")
Set Device To Screen
If aReturn[5] = 1
	Set Printer To
	Commit
	ourspool(wnrel)
Endif
MS_FLUSH()

Return(.T.)

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpAusen    ³ Autor ³ Mauricio Takakura  ³ Data ³06/10/2005³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Impressao de Ausencias e Afastamentos                      ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpAusen(lEnd)                                			    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper580  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function fImpAusen(lEnd)

Local aMovSR8

Local cChave
Local cAcessaSRA  	:= &("{ || " + ChkRH("GPER040","SRA","2") + "}")

Local nReg			:= 0
Local nTotRegPrc	:= 0

#IFDEF TOP 
	Local aSRAFilter	:= {}
	Local aSRAFields

	Local cSRAQuery
	Local cSvSRAQuery
#ENDIF

Private aBoxCont
Private aBoxTipoRSC

Sx3Box2Arr( "R8_CONTIN" , @aBoxCont )
If cPaisLoc == "MEX"
	Sx3Box2Arr( "R8_TIPORSC", @aBoxTipoRSC )
EndIf

Begin Sequence

	dbSelectArea( "SRA" )
	dbGoTop()

	If nOrdem == 1			// Matricula
		dbSetOrder( 1 )
		cChave := cFilDe + cMatDe
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_MAT"
		cFim     := cFilAte + cMatAte
	ElseIf nOrdem == 2		// Nome
		dbSetOrder( 3 )
		cChave := cFilDe + cNomDe + cMatDe
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_NOME + SRA->RA_MAT"
		cFim     := cFilAte + cNomAte + cMatAte
	ElseIf nOrdem == 3		// Centro de Custo
		dbSetOrder( 2 )
		cChave := cFilDe + cCcDe + cMatDe
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_MAT"
		cFim     := cFilAte + cCcAte + cMatAte
	ElseIf nOrdem == 4		// Processo
		dbSetOrder( RetOrder( "SRA", "RA_FILIAL+RA_PROCES+RA_MAT"))
		If !lAllProc
			cChave   := cFilDe + cMinProces
			cFim     := cFilAte + cMaxProces
		Else
			cChave := cFilDe
			cFim     := cFilAte
		EndIf
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_PROCES"
	ElseIf nOrdem == 5 .and. cPaisLoc == "MEX"		// Registro Patronal
		dbSetOrder( RetOrder( "SRA", "RA_FILIAL+RA_CODRPAT+RA_MAT") )
		cChave := cFilDe + cRgPatDe + cMatDe
		dbSeek(cChave,.T.)
		cInicio  := "SRA->RA_FILIAL + SRA->RA_CODRPAT + SRA->RA_MAT"
		cFim     := cFilAte + cRgPatAte + cMatAte
	EndIf

	#IFDEF TOP                    
		aSRAFields := SRA->( dbStruct() )
		cAliasMov := IF( Substr(cAliasMov,1,1) = "Q", Substr(cAliasMov, 2), cAliasMov)
		If Empty( aSRAFilter )
			aAdd( aSRAFilter, { "RA_FILIAL"		, ">="	, cFilDe	, cFilDe 	} )
			aAdd( aSRAFilter, { "RA_FILIAL"		, "<="	, cFilAte	, cFilAte 	} )
			aAdd( aSRAFilter, { "RA_CC"			, ">="	, cCcDe		, cCcDe 	} )
			aAdd( aSRAFilter, { "RA_CC"			, "<="	, cCcAte	, cCcAte 	} )
			aAdd( aSRAFilter, { "RA_MAT"		, ">="	, cMatDe	, cMatDe 	} )
			aAdd( aSRAFilter, { "RA_MAT"		, "<="	, cMatAte	, cMatAte 	} )
			If !lAllProc
				aAdd( aSRAFilter, { "RA_PROCES"		, "IN"	, cProcesso	, cProcesso } )
			EndIf
			If cPaisLoc == "MEX"
				aAdd( aSRAFilter, { "RA_CODRPAT", ">="	, cRgPatDe	, cRgPatDe 	} )
				aAdd( aSRAFilter, { "RA_CODRPAT", "<="	, cRgPatAte	, cRgPatAte	} )
			EndIf
		EndIf
		If !CreateQry( 	aSRAFields	 ,;			// Array com os campos para a Query
						@aSRAFilter	 ,;			// Filtro para query
						cAliasMov	 ,;			// Alias da Query
						@cSRAQuery	 ,;			// String com a Query Montada
						@cSvSRAQuery  ;			// Copia da Query
					 )
			( cAliasMov )->( DbSeek( cChave, .T.) )
		Else
			cAliasMov := "Q" + cAliasMov
		EndIf
	#ENDIF
		
	cFilAnterior := Space(FWGETTAMFILIAL)
	cCcAnt  	 := Space(09)
	cProcesAnt	 := Space(05)
	cCodRPatAnt	 := Space(04)

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Variaveis de Totalizacao.                                    ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	nTotFunQueb		:= 0
	nTotFunFil		:= 0
	nTotFunEmp		:= 0

	nTotRegPrc := If( UPPER( cMatAte ) == 'ZZZZZZ', (cAliasMov)->( RECCOUNT() ), If( Val( cMatAte ) - Val( cMatDe ) <= 0, 1, Val( cMatAte ) - Val( cMatDe )) )
	SetRegua(nTotRegPrc)

	While (cAliasMov)->( !EOF() .And. &cInicio <= cFim )
	
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Movimenta Regua de Processamento                             ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		IncRegua()

		If lEnd
			@Prow()+1,0 PSAY cCancel
			Exit
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Verifica Quebra de Filial                                    ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If (cAliasMov)->RA_FILIAL # cFilAnterior
			If !FP_CODFOL(@aCodFol,(cAliasMov)->RA_FILIAL) .Or. !fInfo(@aInfo,(cAliasMov)->RA_FILIAL)
				Exit
			EndIf
			cFilaInfo := (cAliasMov)->RA_FILIAL
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consiste Parametrizacao do Intervalo de Impressao            ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If cPaisLoc == "MEX"
			If ( (cAliasMov)->RA_NOME < cNomDe ) .Or. ( (cAliasMov)->RA_NOME > cNomAte ) .Or. ;
				( (cAliasMov)->RA_MAT < cMatDe ) .Or. ( (cAliasMov)->RA_MAT > cMatAte ) .Or. ;
				( (cAliasMov)->RA_CC < cCcDe) .Or. ( (cAliasMov)->RA_CC > cCcAte) .Or. ;
				( (cAliasMov)->RA_CODRPAT < cRgPatDe) .Or. ( (cAliasMov)->RA_CODRPAT > cRgPatAte )
				fTestaTotal()
				Loop
			EndIf
        Else
        	If ( (cAliasMov)->RA_NOME < cNomDe ) .Or. ( (cAliasMov)->RA_NOME > cNomAte ) .Or. ;
				( (cAliasMov)->RA_MAT < cMatDe ) .Or. ( (cAliasMov)->RA_MAT > cMatAte ) .Or. ;
				( (cAliasMov)->RA_CC < cCcDe) .Or. ( (cAliasMov)->RA_CC > cCcAte)
				fTestaTotal()
				Loop
			EndIf
		EndIf
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Verifica Situacao e Categoria do Funcionario                 ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If !((cAliasMov)->RA_SITFOLH $ cSit) .Or. !((cAliasMov)->RA_CATFUNC $ cCat) .Or. ;
			( !lAllProc .and. !((cAliasMov)->RA_PROCES $ cProcesso) )
			fTestaTotal()
			Loop
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consiste controle de acessos e filiais validas			   |
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If !((cAliasMov)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
			fTestaTotal()
			Loop
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Buscar lancamentos do Funcionario na Tabela SR8 			   |
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/		
		aMovSR8	:= RetornaMovSR8( 	(cAliasMov)->RA_FILIAL	,; 	// Filial do funcionario corrente
									(cAliasMov)->RA_MAT	  	 ; 	// Matricula do funcionario corrente
								)

		If Empty(aMovSR8)
			fTestaTotal()
			Loop
		EndIf

		For nReg := 1 to Len(aMovSR8)
			fSoma(@aCc  , "C", NIL, NIL, aMovSR8, nReg)
			fSoma(@aProc, "C", NIL, NIL, aMovSR8, nReg)
			fSoma(@aFil , "C", NIL, NIL, aMovSR8, nReg)
			fSoma(@aEmp , "C", NIL, NIL, aMovSR8, nReg)
			If cPaisLoc == "MEX"
				fSoma(@aCodRPat , "C", NIL, NIL, aMovSR8, nReg)
			EndIf
		Next nReg

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Imprime Funcionarios                                         ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		fImpFun(@aMovSR8)
		fTestaTotal()
	Enddo

End Sequence

#IFDEF TOP
	( cAliasMov )->( dbCloseArea() )
#ENDIF

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³RetornaMovSR8³ Autor ³ Mauricio Takakura  ³ Data ³07/10/2005³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Retorna o Movimento em SR8 do funcionario no periodo       ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³RetornaMovSR8(cFilial, cMatricula)             			    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper580  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function RetornaMovSR8(	cFil	  	 ,;
					   	cMatricula 	  ;
					   )

Local aArea			:= GetArea()
Local aAreaSR8
Local aMovSR8		:= {}

Local cAliasSR8		:= "SR8"
Local cAliasQSR8	:= "SR8"

Local dDataIniPer
Local dDataFimPer

Local nDias
Local lQuery 	:= .F.

#IFDEF TOP         
	Local aStruSR8  := {}
	Local cQuery	:= ""
	Local nCont
	Local lAs400	:= ( TcSrvType() == "AS/400" )		//-- Retiramos a execucao da query para servidores AS/400.
#ENDIF
	
Begin Sequence

	(cAliasSR8)->(DbSetOrder(1)) // R8_FILIAL+R8_MAT+DTOS(R8_DATAINI)+R8_TIPOAFA+STR(R8_DIASEMP)
	
	cAliasQSR8:= GetNextAlias()

	#IFDEF TOP
		If !lAs400
			lQuery	 := .T.
			aStruSR8 := SR8->( dbStruct() )

			cQuery := "SELECT * "
        	cQuery += "FROM "+RetSqlName("SR8")+ " SR8 "
			cQuery += "WHERE SR8.R8_FILIAL='"+cFil+"' AND "
			cQuery += "SR8.R8_MAT='"+cMatricula+"' AND "
			If !lAllAfast
				cQuery += "SR8.R8_TIPOAFA IN "+cCodAfas+" AND "
			EndIf
			If !lAllPD
				cQuery += "SR8.R8_PD IN "+cCodPD+" AND "
			EndIf
			If cTipLan == 1
				cQuery += "SR8.R8_SDPAGAR <> 0 AND " 
			ElseIf cTipLan == 2
				cQuery += "SR8.R8_SDPAGAR = 0 AND "
			EndIF
			cQuery += "((SR8.R8_DATAFIM >= '"+DtoS(dDataDe)+"'" + " AND SR8.R8_DATAINI <= '"+DtoS(dDataAte)+"')"+" OR "
		    cQuery += "( SR8.R8_DATAFIM = '        ' AND SR8.R8_DATAINI <= '"+DtoS(dDataAte)+"'"+" )) AND "
			cQuery += " SR8.D_E_L_E_T_ = ' ' "
	   		cQuery += "ORDER BY "+SqlOrder(SR8->( IndexKey() ))
			
			cQuery := ChangeQuery(cQuery)
		   
			dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAliasQSR8, .F., .T.)
			
			For nCont := 1 To Len(aStruSR8)
				If ( aStruSR8[nCont][2] <> "C" )
					TcSetField(cAliasQSR8,aStruSR8[nCont][1],aStruSR8[nCont][2],aStruSR8[nCont][3],aStruSR8[nCont][4])
				EndIf
			Next nCont
			 
			cAliasSR8:= cAliasQSR8
			
		Else
			(cAliasSR8)->( DbSeek( (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT, .F.) )
		EndIf
	#ELSE
		(cAliasSR8)->( DbSeek( (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT, .T.) )
	#ENDIF

	While ( cAliasSR8 )->( !Eof() ) .and. ( (cAliasMov)->RA_FILIAL + (cAliasMov)->RA_MAT == (cAliasSR8)->R8_FILIAL + (cAliasSR8)->R8_MAT )

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consistencia do Codigo do Tipo de Ausencias                  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If !lAllAfast .and. !((cAliasSR8)->R8_TIPOAFA $ cCodAfas )
			(cAliasSR8)->( DbSkip() )
			Loop
		EndIf

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consistencia do Codigo da Verba da Ausencia                  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If !lAllPD .and. !((cAliasSR8)->R8_PD $ cCodPD )
			(cAliasSR8)->( DbSkip() )
			Loop
		EndIf
              
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consistencia do Tipo de Lancamento - Com Saldo ou Sem Saldo  ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If cTipLan = 1 .and. !((cAliasSR8)->R8_SDPAGAR <> 0 )
			(cAliasSR8)->( DbSkip() )
			Loop
		ElseIf cTipLan = 2 .and. !((cAliasSR8)->R8_SDPAGAR = 0 )
			(cAliasSR8)->( DbSkip() )
			Loop
		EndIf
		
		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Consistencia do Periodo Informado                            ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If ( (cAliasSR8)->R8_DATAFIM >= dDataDe .and. (cAliasSR8)->R8_DATAINI <= dDataAte ) .Or.;
		   ( Empty((cAliasSR8)->R8_DATAFIM) .and. (cAliasSR8)->R8_DATAINI <= dDataAte )
		   
		   	fPosReg("RCM",1,xFilial("RCM")+(cAliasSR8)->R8_TIPOAFA)
		   	
   			/*
			ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			³ Dias do Periodo                          					   ³
			ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
			dDataIniPer := If( dDataDe  < (cAliasSR8)->R8_DATAINI, (cAliasSR8)->R8_DATAINI, dDataDe )
			dDataFimPer := If( dDataAte > (cAliasSR8)->R8_DATAFIM, (cAliasSR8)->R8_DATAFIM, dDataAte )
			aAreaSR8 := (cAliasSR8)->( GetArea() )
			nDias 		:= fDiasAfasPeriodo( (cAliasMov)->RA_MAT,, RCM->RCM_TIPODI, dDataIniPer, dDataFimPer )
			RestArea( aAreaSR8 )
	   
			aAdd(aMovSR8, {	(cAliasSR8)->R8_TIPOAFA		, RCM->RCM_DESCRI			,;
							(cAliasSR8)->R8_PD			, PosSRV( (cAliasSR8)->R8_PD,(cAliasSR8)->R8_FILIAL, "RV_DESC") ,;
							(cAliasSR8)->R8_DATAINI		, (cAliasSR8)->R8_DATAFIM	,;
							(cAliasSR8)->R8_DURACAO		, (cAliasSR8)->R8_CONTINU	,;
							If ( cPaisLoc == "MEX",(cAliasSR8)->R8_NCERINC, "" )	,;
							If ( cPaisLoc == "MEX", (cAliasSR8)->R8_TIPORSC, "" )	,;
							RCM->RCM_TIPODI				, nDias					     ;
				         })
		EndIf
		(cAliasSR8)->( DbSkip() )
	Enddo

	If lQuery
		( cAliasQSR8 )->( dbCloseArea() )
	EndIf

End Sequence

//Ordenar por Filial, MAtricula e Verba
aSort( aMovSR8,,, { |x,y| x[1] + x[3] + DTOS(x[5]) < y[1] + y[3] + DTOS(y[5]) } )

RestArea( aArea )

Return ( aMovSR8 )

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fSoma        ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fSoma(aMatriz,cArq,cCod,nValor,aDadosFunc,nReg)     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fSoma(	aMatriz		,;	// @Array com as opcoes a serem gravadas a totalizacao
						cArq		,;	// Tipo de Arquivo "C" ou "I"
						cCod		,;	// Codigo a ser processado para totalizacao
						nValor		,;	// Valor a ser totalizado - Opcional: Se nao informado busca do array
						aDadosFunc	,;	// Array com as informacoes a serem processadas
						nReg		 ;	// Posicao do array acima para busca das informacoes.
					  )
Local nRet
Local nVal1 := 0
Local nDias := 0

DEFAULT aDadosFunc := {}

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Caso o Codigo nao seja passado o tratamento e feito de acordo³ 
³ com o cArq (Arquivo usado)  								   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If	cCod == Nil
	cCod := If(Len(aDadosFunc) > 0, aDadosFunc[nReg, 1], "")
EndIf

If nValor == Nil
	nValor := If(Len(aDadosFunc) > 0, aDadosFunc[nReg,7], 0)
EndIf

If cArq == "C"
	nVal1 		:= nValor
	nDias 		:= If(Len(aDadosFunc) > 0, aDadosFunc[nReg,12], 0)
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Geracao do array com as totalizacoes                         ³ 
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
nRet := Ascan( aMatriz,{|X| x[1] == cCod } )
If	nRet == 0
	Aadd (aMatriz,{cCod,aDadosFunc[nReg,2],nVal1,nDias,1})  		// se nao cria elemento
Else
	aMatriz[nRet,3] += nVal1                   	// se ja so adiciona
	aMatriz[nRet,4] += nDias
	aMatriz[nRet,5] ++
EndIf

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fTestaTotal  ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Executa Quebras                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fTestaTotal()                                        		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fTestaTotal()
Local nQ

cFilAnterior 	:= (cAliasMov)->RA_FILIAL
dbSelectArea( cAliasMov )
dbSkip()

If Eof() .Or. &cInicio > cFim
	fImpCc()
	fImpProc()
	If cPaisLoc == "MEX"
		fImpRegPatr()
	EndIf
	fImpFil()
	fImpEmp()
ElseIf cFilAnterior # (cAliasMov)->RA_FILIAL
	fImpCc()
	fImpProc()
	If cPaisLoc == "MEX"
		fImpRegPatr()
	EndIf
	fImpFil()
ElseIf nOrdem == 3 .and. cCcAnt # (cAliasMov)->RA_CC .And. !Eof()
	fImpCc()
ElseIf nOrdem == 4 .and. cProcesAnt # (cAliasMov)->RA_PROCES .And. !Eof()
	fImpProc()
ElseIf cPaisLoc == "MEX" .and. nOrdem == 5 .and. cCodRPatAnt # (cAliasMov)->RA_CODRPAT .And. !Eof()
	fImpRegPatr()
EndIf

dbSelectArea(cAliasMov)

Return(Nil)

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpEmp      ³ Autor ³ Mauricio Takakura  ³ Data ³04/11/2005³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime Empresa		                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpEmp()										     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpEmp()

If Len(aEmp) == 0
	Return Nil
EndIf

If lImpEmp
	fImprime(aEmp,4)
EndIf

aEmp 		:= {}
nTotFunEmp	:= 0

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpFil      ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime Filial			                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpFil()										     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpFil()

If Len(aFil) == 0
	Return Nil
EndIf

If lImpFil
	fImprime(aFil,3)
EndIf

aFil 		:= {}
nTotFunFil	:= 0

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpFun      ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime um Funcionario                                     ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpFun(aMovSR8) 			                           		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpFun(aMovSR8)

If Len(aMovSR8) == 0
	Return Nil
EndIf

fImprime(aMovSR8,1)

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpCc       ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime Centro de Custo                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpCC()				                            		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpCc()

If Len(aCc) == 0
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Zera variaveis do c.c. anterior						       ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aCc := {}

	Return Nil

EndIf
If nOrdem == 3
	fImprime(aCc,2)
EndIf

aCc := {}

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpProc     ³ Autor ³ Mauricio Takakura  ³ Data ³04/11/2005³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime Processos                                          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpProc()       		                            		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpProc()

If Len(aProc) == 0
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Zera variaveis do c.c. anterior						       ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aProc := {}

	Return Nil

EndIf
If nOrdem == 4
	fImprime(aProc,5)
EndIf

aProc := {}

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImpRegPatr  ³ Autor ³ Mauricio Takakura  ³ Data ³07/11/2005³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ Imprime Registro Patronal - Somente para Mexico            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImpRegPatr()      		                            		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImpRegPatr()

If Len(aCodRPat) == 0
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Zera variaveis do c.c. anterior						       ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	aCodRPat := {}

	Return Nil

EndIf
If nOrdem == 5
	fImprime(aCodRPat,6)
EndIf

aCodRPat := {}

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fImprime     ³ Autor ³ Equipe de RH       ³ Data ³          ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³ 						                                    ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fImprime()										     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³ Gper040  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Static Function fImprime( aMovSR8	,;		// Array com os movimentos a imprimir
						  nTipo		 ;		// 1-Funcionario; 2-Centro de Custo; 3-Filial; 4-Empresa; 5-Processo
 						 )
Local cFunc
Local cDesc

Local DET

Local nPos
Local nConta
Local nCount
Local nMaximo
Local nTotal 	:= 0
Local nDiasT 	:= 0
Local nDiasA	:= 0

nMaximo:= Len(aMovSR8)

//"Funcionario                                      Tipo Afastamento          Verba                     Data Inicio   Data Final   Duracao Ausencia   Duracao Perido    Cont.   Tp Incapac.          Nro. Incap." 
//"XXXXXX-XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  XXX-XXXXXXXXXXXXXXXXXXX   XXX-XXXXXXXXXXXXXXXXXXX   XX/XX/XXXX    XX/XX/XXXX   XXX                XXX               XXX     XXXXXXXXXXXXXXXXXXXX XXXXXXXXXX"
WCabec1 := STR0023+aInfo[3]+Space(20)+If(nTipo#4,STR0027+cFilaInfo+" - "+aInfo[1],Space(26))	//"Empresa: "###" 
WCabec2 := STR0020+Space(48-Len(STR0020))+STR0012+Space(27-Len(STR0012))+STR0013+Space(27-Len(STR0013))+STR0014+Space(15-Len(STR0014))+STR0015+Space(13-Len(STR0015))+;
		   STR0021+Space(19-Len(STR0021))+STR0016+Space(3)+STR0017+Space(3)
If cPaisLoc == "MEX"
	WCabec2 += STR0018+Space(23-Len(STR0018))+STR0019
EndIf

If nTipo == 1 .and. cFilAnterior <> (cAliasMov)->RA_FILIAL
	IMPR(DET, "P")
	
	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Reimprimir Sub-Cabecalho.                           		   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If nOrdem == 3
		cCcAnt := "" // Centro de Custo
	ElseIf nOrdem = 4
		cProcesAnt := ""
	ElseIf cPaisLoc == "MEX" .and. nOrdem = 5
		cCodRPatAnt := ""
	EndIf
EndIf

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Imprimir o sub-cabecalho.                           		   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
If nOrdem <> 1 .and. nOrdem <> 2 .and. nTipo == 1 .and. !Empty(aMovSR8)
	DET := ""
	If nOrdem == 3 .and. (cAliasMov)->RA_CC <> cCcAnt // Centro de Custo
		//Filial + "" + Centro de Custo 
		DET:= STR0032 + cFilAnterior + STR0035 + (cAliasMov)->RA_CC + DescCc((cAliasMov)->RA_CC,cFilAnterior)			//"Filial: "###" C.CUSTO: "
	ElseIf nOrdem = 4 .and. (cAliasMov)->RA_PROCES <> cProcesAnt
		DbSelectArea( "RCJ" )
		DbSetOrder( 1 )  // RCJ_FILIAL + RCJ_CODIGO
		DbSeek( xFilial("RCJ") + (cAliasMov)->RA_PROCES, .F. )
		DET := STR0025 + " " + (cAliasMov)->RA_PROCES + " " + RCJ->RCJ_DESCRI	// "Processo: "
	ElseIf cPaisLoc == "MEX" .and. nOrdem = 5 .and. (cAliasMov)->RA_CODRPAT <> cCodRPatAnt
		DbSelectArea( "RCO" )
		DbSetOrder( 1 )  // RCO_FILIAL + RCO_CODIGO + RCO_NREPAT
		DbSeek( xFilial("RCO") + (cAliasMov)->RA_CODRPAT, .F. )
		DET := STR0028 + " " + (cAliasMov)->RA_CODRPAT + " " + RCO->RCO_NOME + Space(10) + STR0033 + RCO->RCO_NREPAT	// "Cod. Reg. Patronal:" + ### + Descricao + "Nro. Registro Patronal"
	EndIf
	If !Empty(DET)
		IMPR(DET, "C")
		DET := ""
		IMPR(DET, "C")
	EndIf
EndIf

If nTipo <> 1
	DET := " "
	IMPR(DET, "C")
EndIf

If nTipo == 1
	nTotFunQueb++							// Totalizador de Funcionario por Quebra de Ordem
	nTotFunFil++							// Totalizador de Funcionario por Filial
	nTotFunEmp++							// Totalizador de Funcionario por Empresa
	cFunc   := (cAliasMov)->RA_MAT + "-" + Substr((cAliasMov)->RA_NOME,1,40)
	DET     := ""
   	DET     += cFunc + Space(48-Len(cFunc))	// Nome do Funcionario
	For nConta := 1 TO nMaximo
		If nConta > 1
			DET := Space(48)
		EndIf
		DET 	+= Substr(aMovSR8[nConta, 1] + "-" + aMovSR8[nConta, 2],1,24) + Space(3)	// Tipo de Afastamento
		DET 	+= Substr(aMovSR8[nConta, 3] + "-" + aMovSR8[nConta, 4],1,24) + Space(3)	// Codigo da Verba
		DET 	+= DTOC(aMovSR8[nConta, 5]) + Space(7)		 								// Data Inicio
		DET 	+= DTOC(aMovSR8[nConta, 6]) + Space(6)										// Data Final
		
		cDesc 	:= Str( aMovSR8[nConta, 7],3,0)												// Duracao da Ausencia
		DET 	+= cDesc + Space(19-Len(cDesc))
		cDesc 	:= Str( aMovSR8[nConta, 12],3,0)											// Duracao do Periodo
		DET 	+= cDesc + Space(17-Len(cDesc))
		
		nPos 	:= aScan( aBoxCont, {|x| x[2] == aMovSR8[nConta, 8] } )						// Continuacao de Ausencia
		cDesc	:= If( nPos > 0, Space(2) + aBoxCont[nPos,3], Space(3) )
		DET 	+= cDesc + Space(8-Len(cDesc))
		
		If cPaisLoc == "MEX"
			nPos 	:= aScan( aBoxTipoRSC, {|x| x[2] == aMovSR8[nConta, 10] } )				// Tipo de Afastamento
			cDesc	:= If( nPos > 0, aBoxTipoRSC[nPos,3], Space(10))
			DET 	+= cDesc + Space(23-Len(cDesc))
			DET 	+= aMovSR8[nConta, 9]													// Nro da Ausencia
		EndIf
		
		Impr(Det, "C")

		cCcAnt 			:= (cAliasMov)->RA_CC
		cProcesAnt 		:= (cAliasMov)->RA_PROCES
		
		If cPaisLoc == "MEX"
	   		cCodRPatAnt	:= (cAliasMov)->RA_CODRPAT
	 	EndIf
	
	Next nConta
EndIf

If nTipo # 1
	If !Empty( aMovSR8 )
		If nTipo == 2 .Or. nTipo == 5 .Or. nTipo == 6
			// "Total de Funcionarios:" #### + "Total de Reg. Ausencias:" + #### + "Total de Dias Periodo:"
			DET := STR0022 + " " + Str(nTotFunQueb, 4, 0) + Space(10) + STR0026 + " "
			For nConta := 1 TO nMaximo
				nTotal += aMovSR8[nConta, 5]		// Nro de Ausencias
				nDiasT += aMovSR8[nConta, 4]		// Nro de Dias Periodo
			Next nConta
	    	DET += Str(nTotal, 4, 0) + Space(10) + STR0024 + " " + Str(nDiasT, 5, 0) // "Total de Dias Periodo:"
	    	nTotFunQueb := 0

			Impr(REPL("=",220), "C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
			Impr(DET,"C")
			Impr(REPL("=",220), "C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
	  	EndIf
	EndIf

	/*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Se Impressao de Filial ou Empresa, ejetar Pagina    		   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	If (nTipo # 2 .and. nTipo # 5 .and. nTipo # 6) .Or. nTipo == 3 .Or. nTipo == 4
		Impr("","P")

		/*
		ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		³ Resumo de Filial e/ou Empresa.                      		   ³
		ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
		If nTipo == 3 .Or. nTipo == 4

			If nTipo == 4
				WCabec1 := STR0023+aInfo[3]	//"Empresa: "###" 
			EndIf
			
			// Tipo Ausencia             Tot. Dur. Ausenc.  Tot. Dur. Periodo         Tipo Ausencia             Tot. Dur. Ausenc.  Tot. Dur. Periodo         Tipo Ausencia             Tot. Dur. Ausenc.  Tot. Dur. Periodo"
			// XXX-XXXXXXXXXXXXXXXXXXX   99999			    99999                      
			WCabec2 := Substr(STR0012,1,26) + Space(27-Len(Substr(STR0012,1,26))) + Substr(STR0029,1,19) + Space(20-Len(Substr(STR0029,1,19)))
			WCabec2 += Substr(STR0030,1,26) + Space(27-Len(Substr(STR0030,1,26)))
			WCabec2 += WCabec2 + WCabec2

			DET := ""
			nCount := 1
			aSort( aMovSR8, NIL, NIL, { |x,y| x[1] < y[1] } )
			For nConta := 1 TO nMaximo
				cDesc := aMovSR8[nConta, 1] + "-" + Substr(aMovSR8[nConta, 2],1,20)
				DET   += cDesc + Space(27-Len(cDesc))
				DET   += Str(aMovSR8[nConta, 3],5,0) + Space(15)
				DET	  += Str(aMovSR8[nConta, 4],5,0) + Space(22)

				If nCount == 3 .Or. nConta == nMaximo
					Impr(DET, "C" )
					DET := ""
					nCount := 1
				Else
				   nCount++
				EndIf

				nTotal += aMovSR8[nConta, 5]		// Nro de Ausencias
				nDiasT += aMovSR8[nConta, 4]		// Nro de Dias Periodo
				nDiasA += aMovSR8[nConta, 3]		// Nro de Dias Ausencias
			Next nConta

			// Total de Funcionarios Processados: #### Numero de Ausencias Processadas: ### Total Dias Ausencias: ###  Total Dias Periodo ###
			DET := STR0022 + " " + If( nTipo=4, Str(nTotFunEmp,5,0), Str(nTotFunFil,5,0)) + Space(20) + STR0026 + " " + Str(nTotal,5,0) + Space(20)
			DET += STR0031 + " " + Str( nDiasA, 5, 0 ) + Space(20) + STR0024 + " " + Str( nDiasT, 5, 0)
			Impr("","C")
			Impr(REPL("=",220), "C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
			Impr(DET,"C")
			Impr(REPL("=",220), "C")   // Salta Pagina apos Quebra Cc/Filial/Empresa
		ElseIf nTipo == 4
			DET:= STR0034+aInfo[3]		//"Empresa: "
			Impr(DET,"C")
		EndIf
	Else
		Impr("","C")
	EndIf

EndIf

Return Nil

/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³fAusencia    ³ Autor ³ Marcos Kato		  ³ Data ³26/02/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Mostra os Itens do Motivo de Ausencia a serem selecionada   ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³fAusencia()  									     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³Especifico Venezuela  												    ³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

Function fAusencia()
Local oDlg
Local oOk       := LoadBitmap( GetResources(), "CHECKED" ) 
Local oNo       := LoadBitmap( GetResources(), "UNCHECKED")
Local oMarcaTD  := LoadBitmap( GetResources(), "UNCHECKED")
Local nOpc		:= 0 ,nCont		:= 0
Local cTitulo	:= STR0037//"Motivo de Ausencia"
Local cRet		:= "",cVar		:= ""
Local lMarcaTD	:=.F.
Local aObjSize		:= {}
Local aObjCoords	:= {}
Local aButtons		:= {}
Local bSet15		:= { || NIL }
Local bSet24		:= { || NIL }

Private oLbx
Private aAusencia 	:= {}

aAdd(aAusencia,{.F.,STR0038})//"1-Outros"
aAdd(aAusencia,{.F.,STR0039})//"2=Morte"
aAdd(aAusencia,{.F.,STR0040})//"3=Incap. absoluta e permanente"
aAdd(aAusencia,{.F.,STR0041})//"4=Incap. absoluta e temporal"
aAdd(aAusencia,{.F.,STR0042})//"5=Incap. parcial e permanente"
aAdd(aAusencia,{.F.,STR0043})//"6=Incap. parcial e temporal"

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Monta as Dimensoes dos Objetos         					   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
aAdvSize		:= MsAdvSize()
aAdvSize[5]	:=	(aAdvSize[5]/100) * 60	//horizontal
aAdvSize[6]	:=  (aAdvSize[6]/100) * 60	//Vertical
aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 0 , 0 }					 
aAdd( aObjCoords , { 000 , 000 , .T. , .T. } )
aObjSize	:= MsObjSize( aInfoAdvSize , aObjCoords )
aGdCoord	:= { (aObjSize[1,1]+3), (aObjSize[1,2]+5), (((aObjSize[1,3])/100)*60), (((aObjSize[1,4])/100)*52) }	//1,3 Vertical /1,4 Horizontal

DEFINE MSDIALOG oDlg TITLE OemToAnsi( STR0001 ) From aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF oMainWnd PIXEL
	@ aGdCoord[1]+5 , aGdCoord[2] LISTBOX oLbx VAR cVar FIELDS HEADER ;   //10,10 
   " ", STR0037;//"Motivo de Ausencia"
   SIZE aGdCoord[4]+12, (aGdCoord[3]/1.7) OF oDlg PIXEL ON dblClick(aAusencia[oLbx:nAt,1] := !aAusencia[oLbx:nAt,1]) //230,095
	oLbx:SetArray( aAusencia )
	oLbx:bLine := {|| {Iif(aAusencia[oLbx:nAt,1],oOk,oNo),;
                      aAusencia[oLbx:nAt,2]}}

	@ (aGdCoord[3]/1.4) , 10 CHECKBOX oMarcaTD VAR lMarcaTD Prompt STR0044 Size 048,008  PIXEL OF oDlg ON CLICK (MarcaTd(lMarcaTd),IIF(lMarcaTd,oOk,oNo))//"Todos"  107,10 
                      
	bSet15		:= { || nOpc:=1,oDlg:End() }
	bSet24		:= { || oDlg:End() }

ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar( oDlg , bSet15 , bSet24 , NIL , aButtons )

If nOpc==1
	MV_PAR16:=""
	For nCont:=1 To Len(aAusencia)
		If aAusencia[nCont][1]
			MV_PAR16+=Substr(aAusencia[nCont][2],1,1)
		Endif
	Next
Endif
Return .T.
/*/
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o	   ³MarcaTD    ³ Autor ³ Marcos Kato		  ³ Data ³26/02/2008³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Marca e Desmarca os Itens do Motivo de Ausencia            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³MarcaTD(lOk)  									     		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso	   ³Especifico Venezuela  										³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/

Static Function MarcaTd(lOk)
Local n:=0
For n:=1 To Len(oLbx:aArray)
	oLbx:aArray[n][1]:=Iif(lOk,.T.,.F.)
Next      
oLbx:Refresh()
return .T.
