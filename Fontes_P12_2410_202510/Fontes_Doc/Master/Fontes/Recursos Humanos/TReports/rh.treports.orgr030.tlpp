#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "rh.treports.orgr030.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAORG", tables="RCL, SQB, SQ3, SRJ, CTT", name="Movimentação de Postos", country="ALL", initialRelease="12.1.2210")
class ORGR030ReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass
 
method new() as object class ORGR030ReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Movimentação de Postos"
return self
 
method getDescription() as character class ORGR030ReportsBusinessObject
return STR0002 //"Objeto contendo informações de movimentações de postos de trabalho."
 
method getAreas() as array class ORGR030ReportsBusinessObject
return {"RH"}
 
method getData(nPage as numeric, oFilter as object) as object class ORGR030ReportsBusinessObject
    local cAlias as character
    local cQuery as character
    local cDescCTT as character
    local cDescSQ3 as character  
    local cDescSRJ as character  
    local cDescSQB as character  
	local cFilExec as character
	local cEmpExec as character
    Local nParamOrder as numeric
    Local oStatement  as object

    //Variáveis com o nome da Filial e Grupo de Empresa
	cFilExec := AllTrim(FWFilialName())
	cEmpExec := AllTrim(FWEmpName(cEmpAnt))

    cQuery := "SELECT "
    cQuery += "RCL.RCL_FILIAL, RCL.RCL_DEPTO, RCL.RCL_POSTO, RCL.RCL_STATUS, RCL.RCL_CARGO, RCL.RCL_CC, RCL.RCL_OPOSTO, RCL.RCL_FUNCAO, RCL.RCL_SALAR, RCL.RCL_BENEF, RCL.RCL_ENCARG, RCL.RCL_NPOSTO, RCL.RCL_FGTS "  
    cQuery += "FROM " + RetSqlName("RCL") + " RCL "
    cQuery += "WHERE RCL.D_E_L_E_T_ = ? " // " "
    cQuery +=   "? "                      // IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), ""))
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Define o valor dos bind parameters
    nParamOrder := 1
    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetUnsafe(nParamOrder++, IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), ""))

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    while !(cAlias)->(Eof())
        cDescCTT    := DescCc( (cAlias)->RCL_CC, (cAlias)->RCL_FILIAL )
        cDescSQ3    := fDesc("SQ3",(cAlias)->RCL_CARGO, "Q3_DESCSUM",,(cAlias)->RCL_FILIAL)
        cDescSRJ    := fDesc("SRJ",(cAlias)->RCL_FUNCAO, "RJ_DESC",,(cAlias)->RCL_FILIAL)
        cDescSQB    := fDesc("SQB",(cAlias)->RCL_DEPTO, "QB_DESCRIC",,(cAlias)->RCL_FILIAL)
        
        self:oData:appendData({"RCL_FILIAL": (cAlias)->RCL_FILIAL,;
                "RCL_POSTO": (cAlias)->RCL_POSTO,;
                "RCL_STATUS": Iif( (cAlias)->RCL_STATUS == "1","Criação", ( Iif( (cAlias)->RCL_STATUS == "2","Atualização", (Iif( (cAlias)->RCL_STATUS == "3","Ocupação", (Iif( (cAlias)->RCL_STATUS == "4","Congelamento", (Iif( (cAlias)->RCL_STATUS == "5","Cancelamento", "" )) )) )) ) ) ) ,; 
                "RCL_DEPTO": (cAlias)->RCL_DEPTO,;
                "QB_DESCRIC": RTrim( cDescSQB ),;
                "RCL_CARGO": (cAlias)->RCL_CARGO,;
                "Q3_DESCSUM": RTrim( cDescSQ3 ),;               
                "RJ_DESC": RTrim( cDescSRJ ),;
                "RCL_CC": (cAlias)->RCL_CC,;
                "RCL_FUNCAO": (cAlias)->RCL_FUNCAO,; 
                "RCL_SALAR": (cAlias)->RCL_SALAR,; 
                "RCL_BENEF": (cAlias)->RCL_BENEF,;    
                "RCL_ENCARG": (cAlias)->RCL_ENCARG,;  
                "RCL_NPOSTO": (cAlias)->RCL_NPOSTO,;     
                "RCL_FGTS": (cAlias)->RCL_FGTS,;  
                "CTT_DESC01": RTrim( cDescCTT ),;               
                "FilialExec": cFilExec,;
			    "EmpresaExec": cEmpExec  })                              
                
        (cAlias)->( dbSkip() )
    end

    (cAlias)->( DBCloseArea() )
return self:oData
 
method getSchema() as object class ORGR030ReportsBusinessObject
    local aFieldsRCL as array
    local aFieldsSQB as array
    local aFieldsSQ3 as array
    local aFieldsSRJ as array
    local aFieldsCTT as array
           
    aFieldsRCL := { "RCL_FILIAL", "RCL_DEPTO", "RCL_POSTO", "RCL_STATUS", "RCL_CARGO", "RCL_CC", "RCL_NPOSTO", "RCL_OPOSTO", "RCL_SALAR", "RCL_BENEF", "RCL_ENCARG", "RCL_FGTS" }  
    aFieldsSQB := { "QB_DESCRIC" }
    aFieldsSQ3 := { "Q3_DESCSUM" }
    aFieldsSRJ := { "RJ_DESC" }
    aFieldsCTT := { "CTT_DESC01" }
    
    self:oSchema:aliasToSchema("RCL", aFieldsRCL)
    self:oSchema:aliasToSchema("SQB", aFieldsSQB)
    self:oSchema:aliasToSchema("SQ3", aFieldsSQ3)
    self:oSchema:aliasToSchema("SRJ", aFieldsSRJ)
    self:oSchema:aliasToSchema("CTT", aFieldsCTT)

    self:oSchema:addProperty("FilialExec", "Filial de Execução", "string", "Filial Exec", "FilialExec")
    self:oSchema:addProperty("EmpresaExec", "Empresa de Execução", "string", "Empresa Exec", "EmpresaExec")
 	
return self:oSchema
