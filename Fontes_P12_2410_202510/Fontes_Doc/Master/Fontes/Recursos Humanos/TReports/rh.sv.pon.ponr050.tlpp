#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#Include "PONCALEN.CH"
#Include "TOTVS.CH"
#include "rh.sv.pon.ponr050.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPON", tables="SRA, SPC, SPH, SP6", name="Eventos Abonados", country="ALL", initialRelease="12.1.2210")
class PONR050TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getAreas() as array
    public method getSchema() as object
    public method getData() as object
endclass

method new() as object class PONR050TReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) // "Eventos Abonados"
return self

method getDescription() as character class PONR050TReportsBusinessObject
return STR0002 // "Objeto contendo os eventos abonados do ponto eletrônico"

method getAreas() as array class PONR050TReportsBusinessObject
return {STR0003} // "RH"

method getSchema() as object class PONR050TReportsBusinessObject
    
    local aFieldsSRA as array
    local aFieldsSPC as array
    
    aFieldsSRA := {"RA_FILIAL", "RA_MAT", "RA_NOME", "RA_ADMISSA", "RA_CATFUNC", "RA_SITFOLH", "RA_TNOTRAB"}
    aFieldsSPC := {"PC_DATA", "PC_PD", "PC_TPMARCA"}
    
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("SPC", aFieldsSPC)
	
    self:oSchema:addProperty("RA_CC"	 , STR0004, "string", STR0005, "RA_CC") 		// "Centro de Custo do Funcionário" - "C.C. Func"
    self:oSchema:addProperty("CCFUNDESC" , STR0006, "string", STR0007, "CCFUNDESC") 	// "Descrição Centro de Custo do Funcionário" - "Desc. C.C. Func"
    self:oSchema:addProperty("PC_CC"	 , STR0008, "string", STR0009, "PC_CC") 		// "Centro de Custo do Lançamento" - "C.C. Lanc"
    self:oSchema:addProperty("CCLANDESC" , STR0010, "string",  STR0011, "CCLANDESC")	// "Descrição do Centro de Custo do Lançamento" - "Desc. C.C. Lanc"
	self:oSchema:addProperty("TURNODESC" , STR0012, "string", STR0013, "TURNODESC") 	// "Descrição do Turno" - "Desc. Turno"
	self:oSchema:addProperty("MOTIVOABO" , STR0014, "string", STR0014, "MOTIVOABO") 	// "Motivo do Abono"
	self:oSchema:addProperty("PREVISTO"  , STR0015, "string", STR0015, "PREVISTO") 		// "Horário Previsto"
	self:oSchema:addProperty("REALIZADO" , STR0016,	"string", STR0016, "REALIZADO") 	// "Horário Realizado"
	self:oSchema:addProperty("PC_QUANTC" , STR0017,	"string", STR0018, "PC_QUANTC") 	// "Horas do Evento" - "Horas"
	self:oSchema:addProperty("PC_QTABONO", STR0019,	"string", STR0019, "PC_QTABONO") 	// "Horas Abonadas"
	self:oSchema:addProperty("QTNABONO"  , STR0020,	"string", STR0020, "QTNABONO") 		// "Horas Não Abonadas"
	self:oSchema:addProperty("CODABONO"  , STR0021,	"string", STR0022, "CODABONO") 		// "Código do Abono" - "Cod. Abono"
	self:oSchema:addProperty("EVENTDESC" , STR0023,	"string", STR0024, "EVENTDESC") 	// "Descição do Evento" - "Desc. Evento"
	self:oSchema:addProperty("GRPEMPRESA", STR0025,	"string", STR0025, "GRPEMPRESA") 	// "Grupo de Empresas"
	self:oSchema:addProperty("EMPRESA"   , STR0026,	"string", STR0026, "EMPRESA") 		// "Empresa"
	self:oSchema:addProperty("TABELA"	 , STR0036, "string", STR0037, "TABELA") 		// "Tabela do lançamento" - "Tabela"
	
	self:oSchema:AddParameter("FILIAL1", STR0027, "string", .F.) 	// "Filial De?"
	self:oSchema:AddParameter("FILIAL2", STR0028, "string", .F.) 	// "Filial Até?"
	self:oSchema:AddParameter("MAT1"   , STR0029, "string", .F.) 	// "Matrícula De?"
	self:oSchema:AddParameter("MAT2"   , STR0030, "string", .F.) 	// "Matrícula Até?"
	self:oSchema:AddParameter("DATA1"  , STR0031, "date", .F.) 		// "Data de?"
	self:oSchema:AddParameter("DATA2"  , STR0032, "date", .F.) 		// "Data Até?"
	
return self:oSchema

method getData(nPage as numeric, oFilter as object) as object class PONR050TReportsBusinessObject
	local cAliasSPC							as character
	local cLGPDSRA							as character
	local cDescCC							as character
	local cSitFol							as character
	local cCateg							as character
	local cLastFil := "***"					as character
	local cLastMat := "***"					as character
	local cHPrevisto						as character
	local cHRealizado						as character
	Local cGrpEmpNom := FWGrpName(cEmpAnt)	as character
	local cEmpNome							as character
	local cQuery							as character
	local cFilDe							as character
	local cFilAte							as character
	local cMatDe							as character
	local cMatAte							as character
    local cDataDe							as character
	local cDataAte							as character
	local dIniAtu	:= sToD("//")			as date
	local dFimAtu	:= sToD("//")			as date
	local dDataSPC	:= sToD("//")			as date
	local jParams							as json
	local aPeriodos							as array
	local aFieldsSRA						as array
	local aLGPDSRA							as array
	local aTabCalend						as array
	local aMarcacoes						as array
	local nI								as numeric
	local nParamOrder := 1					as numeric
	
	jParams := oFilter:getParameters()

	// Validação do filtro "Filial De" e "Filial Até" para utilização na Query
	If (!Empty(jParams['FILIAL1', 1]), cFilDe := jParams['FILIAL1', 1], cFilDe := Space(GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
	If (!Empty(jParams['FILIAL2', 1]), cFilAte := jParams['FILIAL2', 1], cFilAte := Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
	
	// Validação do filtro "Matrícula De" e "Matrícula Até" para utilização na Query
	If (!Empty(jParams['MAT1', 1]), cMatDe := jParams['MAT1', 1], cMatDe := Space(GetSX3Cache("RA_MAT", "X3_TAMANHO")))
	If (!Empty(jParams['MAT2', 1]), cMatAte := jParams['MAT2', 1], cMatAte := Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
	
	// Validação do filtro "Data De" e "Data Até" para utilização na Query
	cDataDe := StrTran( SubStr(ArrTokStr(jParams['DATA1']), 1, 10), "-" )
	cDataAte := StrTran( SubStr(ArrTokStr(jParams['DATA2']), 1, 10), "-" )
	
	aFieldsSRA := {"RA_FILIAL", "RA_MAT", "RA_NOME", "RA_CATFUNC", "RA_SITFOLH", "RA_TNOTRAB", "RA_CC" }
	aLGPDSRA := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsSRA)
	
	For nI := 1 To Len(aFieldsSRA)
		If(aScan(aLGPDSRA, aFieldsSRA[nI]) == 0, cLGPDSRA += aFieldsSRA[nI] + "*", Nil)
	Next

	cQuery :=	"SELECT "
	cQuery +=		"'SPC' AS TABELA, "
	cQuery +=		"RA_FILIAL, "
	cQuery +=		"RA_MAT, "
	cQuery +=		"RA_NOME, "
	cQuery +=		"RA_ADMISSA, "
	cQuery +=		"RA_CATFUNC, "
	cQuery +=		"RA_SITFOLH, "
	cQuery +=		"RA_TNOTRAB, "
	cQuery +=		"RA_CC, "
	cQuery +=		"SRA.R_E_C_N_O_ AS RECNO, "
	cQuery +=		"PC_FILIAL, "
	cQuery +=		"PC_MAT, "
	cQuery +=		"PC_DATA, "
	cQuery +=		"PC_PD, "
	cQuery +=		"PC_TPMARCA, "
	cQuery +=		"PC_QUANTC, "
	cQuery +=		"PC_ABONO, "
	cQuery +=		"PC_QTABONO, "
	cQuery +=		"PC_CC, "
	cQuery +=		"P6_DESC, "
	cQuery +=		"R6_DESC, "
	cQuery +=		"CTT_DESC01, "
	cQuery +=		"P9_DESC "
	cQuery +=	"FROM " + RetSQLName("SPC") + " SPC "
	cQuery +=	"INNER JOIN " + RetSQLName("SP6") + " SP6 ON " + FWJoinFilial("SP6", "SPC") + " AND SP6.P6_CODIGO = SPC.PC_ABONO AND SP6.D_E_L_E_T_ = ? "		// 1
	cQuery +=	"INNER JOIN " + RetSQLName("SP9") + " SP9 ON " + FWJoinFilial("SP9", "SPC") + " AND SP9.P9_CODIGO = SPC.PC_PD AND SP9.D_E_L_E_T_ = ? "		// 2
	cQuery +=	"INNER JOIN " + RetSQLName("SRA") + " SRA ON SRA.RA_FILIAL = SPC.PC_FILIAL AND SRA.RA_MAT = SPC.PC_MAT AND SRA.D_E_L_E_T_ = ? "				// 3
	cQuery +=	"INNER JOIN " + RetSQLName("CTT") + " CTT ON " + FWJoinFilial("CTT", "SRA") + " AND CTT.CTT_CUSTO = SRA.RA_CC AND SRA.D_E_L_E_T_ = ? "		// 4
	cQuery +=	"INNER JOIN " + RetSQLName("SR6") + " SR6 ON " + FWJoinFilial("SR6", "SRA") + " AND SR6.R6_TURNO = SRA.RA_TNOTRAB AND SR6.D_E_L_E_T_ = ? "	// 5
	cQuery +=	"WHERE PC_FILIAL BETWEEN ? AND ? "	// 6 e 7
	cQuery +=		"AND PC_MAT  BETWEEN ? AND ? "		// 8 e 9
	cQuery +=		"AND PC_DATA  BETWEEN ? AND ? "		// 10 e 11
	cQuery +=		"AND SPC.D_E_L_E_T_ = ? "			// 12
	cQuery +=	"UNION ALL "
	cQuery +=	"SELECT "
	cQuery +=		"'SPH' AS TABELA, "
	cQuery +=		"RA_FILIAL, "
	cQuery +=		"RA_MAT, "
	cQuery +=		"RA_NOME, "
	cQuery +=		"RA_ADMISSA, "
	cQuery +=		"RA_CATFUNC, "
	cQuery +=		"RA_SITFOLH, "
	cQuery +=		"RA_TNOTRAB, "
	cQuery +=		"RA_CC, "
	cQuery +=		"SRA.R_E_C_N_O_ AS RECNO, "
	cQuery +=		"PH_FILIAL, "
	cQuery +=		"PH_MAT, "
	cQuery +=		"PH_DATA, "
	cQuery +=		"PH_PD, "
	cQuery +=		"PH_TPMARCA, "
	cQuery +=		"PH_QUANTC, "
	cQuery +=		"PH_ABONO, "
	cQuery +=		"PH_QTABONO, "
	cQuery +=		"PH_CC, "
	cQuery +=		"P6_DESC, "
	cQuery +=		"R6_DESC, "
	cQuery +=		"CTT_DESC01, "
	cQuery +=		"P9_DESC "
	cQuery +=	"FROM " + RetSQLName("SPH") + " SPH "
	cQuery +=	"INNER JOIN " + RetSQLName("SP6") + " SP6 ON " + FWJoinFilial("SP6", "SPH") + " AND SP6.P6_CODIGO = SPH.PH_ABONO AND SP6.D_E_L_E_T_ = ? "		// 13
	cQuery +=	"INNER JOIN " + RetSQLName("SP9") + " SP9 ON " + FWJoinFilial("SP9", "SPH") + " AND SP9.P9_CODIGO = SPH.PH_PD AND SP9.D_E_L_E_T_ = ? "		// 14
	cQuery +=	"INNER JOIN " + RetSQLName("SRA") + " SRA ON SRA.RA_FILIAL = SPH.PH_FILIAL AND SRA.RA_MAT = SPH.PH_MAT AND SRA.D_E_L_E_T_ = ? "				// 15
	cQuery +=	"INNER JOIN " + RetSQLName("CTT") + " CTT ON " + FWJoinFilial("SRA", "CTT") + " AND CTT.CTT_CUSTO = SRA.RA_CC AND SRA.D_E_L_E_T_ = ? "		// 16
	cQuery +=	"INNER JOIN " + RetSQLName("SR6") + " SR6 ON " + FWJoinFilial("SRA", "SR6") + " AND SR6.R6_TURNO = SRA.RA_TNOTRAB AND SR6.D_E_L_E_T_ = ? "	// 17
	cQuery +=	"WHERE PH_FILIAL BETWEEN ? AND ? "	// 18 e 19
	cQuery +=		"AND PH_MAT  BETWEEN ? AND ? "		// 20 e 21
	cQuery +=		"AND PH_DATA  BETWEEN ? AND ? "		// 22 e 23
	cQuery +=		"AND SPH.D_E_L_E_T_ = ? "			// 24
	cQuery +=	"ORDER BY PC_FILIAL, PC_MAT, PC_DATA, PC_PD, PC_CC"

	cQuery := ChangeQuery(cQuery)

	oStatement := FwExecStatement():New(cQuery)

	For nI := 1 To 2
		oStatement:SetString(nParamOrder++, " ")		// 1 ou 13
		oStatement:SetString(nParamOrder++, " ")		// 2 ou 14
		oStatement:SetString(nParamOrder++, " ")		// 3 ou 15
		oStatement:SetString(nParamOrder++, " ")		// 4 ou 16
		oStatement:SetString(nParamOrder++, " ")		// 5 ou 17
		oStatement:SetString(nParamOrder++, cFilDe)		// 6 ou 18
		oStatement:SetString(nParamOrder++, cFilAte)	// 7 ou 19
		oStatement:SetString(nParamOrder++, cMatDe)		// 8 ou 20
		oStatement:SetString(nParamOrder++, cMatAte)	// 9 ou 21
		oStatement:SetString(nParamOrder++, cDataDe)	// 10 ou 22
		oStatement:SetString(nParamOrder++, cDataAte)	// 11 ou 23
		oStatement:SetString(nParamOrder++, " ")		// 12 ou 24
	Next
	
	cAliasSPC := oStatement:OpenAlias()

	While (cAliasSPC)->(!Eof())
		dDataSPC := sToD((cAliasSPC)->PC_DATA)
		
		If !((cAliasSPC)->RA_FILIAL == cLastFil .And. (cAliasSPC)->RA_MAT == cLastMat)
			
			cLastMat := (cAliasSPC)->RA_MAT
			cDescCC := (cAliasSPC)->(DescCC(RA_CC, RA_FILIAL))
			cSitFol	:= (cAliasSPC)->(fDesc("SX5", "31" + RA_SITFOLH, "X5_DESCRI", 15, RA_FILIAL))
			cCateg	:= (cAliasSPC)->(fDesc("SX5", "28" + RA_CATFUNC, "X5_DESCRI", 11, RA_FILIAL))
			cEmpNome := AllTrim(FWCompanyName(cEmpAnt,(cAliasSPC)->RA_FILIAL))
			
			If !(cAliasSPC)->RA_FILIAL == cLastFil
				cLastFil := (cAliasSPC)->RA_FILIAL
				// Obtem as datas do Periodo em Aberto
				GetPonMesDat( @dIniAtu, @dFimAtu, (cAliasSPC)->(RA_FILIAL))
			EndIf
			
			aPeriodos := Monta_Per( SToD(cDataDe), SToD(cDataAte), (cAliasSPC)->RA_FILIAL, (cAliasSPC)->RA_MAT, dIniAtu, dFimAtu )
			
			dbSelectArea("SRA")
			SRA->(dbGoTo((cAliasSPC)->RECNO))
			
			If (nPer := aScan(aPeriodos, {|x| dDataSPC >= x[1] .And. dDataSPC <= x[2]})) > 0
				GetPerMarc(@aTabCalend, @aMarcacoes, aPeriodos[nPer][1], aPeriodos[nPer][2])
			EndIf
		EndIf
		
		// Verifica se troucou o período 
		If nPer > 0 .And. dDataSPC > aPeriodos[nPer][2]
			If (nPer := aScan(aPeriodos, {|x| dDataSPC >= x[1] .And. dDataSPC <= x[2]})) > 0
				GetPerMarc(@aTabCalend, @aMarcacoes, aPeriodos[nPer][1], aPeriodos[nPer][2])
			EndIf
		EndIf
		
		cHPrevisto := GetHPrevisto(dDataSPC, aTabCalend)
		cHRealizado := GetHRealizado(dDataSPC, aMarcacoes)
		
		(cAliasSPC)->(self:oData:appendData({;
			"RA_FILIAL":RA_FILIAL,;
			"RA_MAT":If("RA_MAT" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(RA_MAT), 1, 10), AllTrim(RA_MAT)),;
			"RA_NOME":If("RA_NOME" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(RA_NOME), 1, 10), AllTrim(RA_NOME)),;
			"RA_ADMISSA":FwTimeStamp( 5, sToD(RA_ADMISSA), "00:00:00" ),;
			"RA_CATFUNC":If("RA_CATFUNC" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(cCateg), 1, 10), cCateg),;
			"RA_SITFOLH":If("RA_SITFOLH" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(cSitFol), 1, 10), cSitFol),;
			"RA_TNOTRAB":If("RA_TNOTRAB" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(RA_TNOTRAB), 1, 10), AllTrim(RA_TNOTRAB)),;
			"TURNODESC":If("RA_TNOTRAB" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(R6_DESC), 1, 10), AllTrim(R6_DESC)),;
			"RA_CC":If("RA_CC" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(RA_CC), 1, 10), AllTrim(RA_CC)),;
			"CCFUNDESC":If("RA_CC" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(cDescCC), 1, 10), AllTrim(cDescCC)),;
			"PC_DATA":FwTimeStamp( 5, dDataSPC, "00:00:00" ),;
			"PC_PD":PC_PD,;
			"PC_TPMARCA":PC_TPMARCA,;
			"PC_QUANTC":Transform(StrZero(PC_QUANTC, 5, 2), "99:99"),;
			"CODABONO":AllTrim(PC_ABONO),;
			"MOTIVOABO":P6_DESC,;
			"PC_QTABONO":Transform(StrZero(PC_QTABONO, 5, 2), "99:99"),;
			"PC_CC":PC_CC,;
			"CCLANDESC":CTT_DESC01,;
			"EVENTDESC":AllTrim(P9_DESC),;
			"QTNABONO":Transform(StrZero(SubHoras(PC_QUANTC, PC_QTABONO), 5, 2), "99:99"),;
			"PREVISTO":cHPrevisto,;
			"REALIZADO":cHRealizado,;
			"GRPEMPRESA": AllTrim(cGrpEmpNom),;
			"EMPRESA": cEmpNome,;
			"TABELA": If(TABELA == "SPC", STR0038, STR0039 ); // "Apontamentos" - "Histórico de Apontamentos"
		}))
		
		(cAliasSPC)->(dbSkip())
		
	EndDo
	
	(cAliasSPC)->(dbCloseArea())
	
	FwFreeObj(jParams)
	FwFreeObj(oStatement)
return self:oData

/*/{Protheus.doc} GetPerMarc
Carrega o calendário do ponto e as marcações realizadas no período
@type  Static Function
@author Cícero Alves
@since 09/11/2023
@param aTabCalend, Array, Variável para carga do calendário, deve ser passada por referência
@param aMarcacoes, Array, Variável para carga das marcações, deve ser passada por referência
@param dDataIni, Data, Data de Inicio do período de apontamento
@param dDataFim, Data, Data final do período de apontamento
@return cHRealizado, Character, Marcações realizadas na data de referência
/*/
Static Function GetPerMarc(aTabCalend, aMarcacoes, dDataIni, dDataFim)
	
	Local aTabPadrao	:= {}
	Local aTurnos		:= {}
	
	aTabCalend	:= {}
	aMarcacoes	:= {}
	
	GetMarcacoes(	@aMarcacoes				,;	//Marcacoes dos Funcionarios
					@aTabCalend				,;	//Calendario de Marcacoes
					@aTabPadrao				,;	//Tabela Padrao
					@aTurnos				,;	//Turnos de Trabalho
					dDataIni			    ,;	//Periodo Inicial
					dDataFim                ,;	//Periodo Final
					SRA->RA_FILIAL			,;	//Filial
					SRA->RA_MAT				,;	//Matricula
					SRA->RA_TNOTRAB			,;	//Turno
					SRA->RA_SEQTURN			,;	//Sequencia de Turno
					SRA->RA_CC				,;	//Centro de Custo
					"SP8"					,;	//Alias para Carga das Marcacoes
					.F.    					,;	//Se carrega Recno em aMarcacoes
					.T.      				,;	//Se considera Apenas Ordenadas
					.T. 					,;	//Se Verifica as Folgas Automaticas
					.F.    			 		,;	//Se Grava Evento de Folga Automatica Periodo Anterior
					Nil						,;	//17 -> Se Carrega as Marcacoes Automaticas
					Nil	    				,;	//18 -> Registros de Marcacoes Automaticas que deverao ser Desprezadas
					Nil						,;	//19 -> Bloco para avaliar as Marcacoes Automaticas que deverao ser Desprezadas
					Nil						,;	//20 -> Se Considera o Periodo de Apontamento das Marcacoes
					Nil						,;	//21 -> Se Efetua o Sincronismo dos Horarios na Criacao do Calendario
					Nil						,;  //22 -> Se carrega as marcacoes desconsideradas (Uso com lPort1510)
					.T.						 ;  //23 -> Se carrega as marcacoes das duas tabelas SP8 e SPG
	)
	
Return

/*/{Protheus.doc} GetHPrevisto
Retorna uma string com os horários previstos para funcionário
@type  Static Function
@author Cícero Alves
@since 09/11/2023
@param dDataRef, Data, Data de referência para busca do horário
@param aTabCalend, Array, Calendário do ponto
@return cHPrevisto, Character, Horários previstos para o funcionário na data passada
/*/
Static Function GetHPrevisto(dDataRef, aTabCalend)
	
	Local cHPrevisto	:= ""
	Local nPosCalen		:= 0
	
	Default aTabCalend := {}
	
	If (nPosCalen := aScan(aTabCalend, {|x| x[CALEND_POS_DATA_APO] == dDataRef})) > 0
		
		If aTabCalend[nPosCalen, CALEND_POS_TIPO_DIA] == "C"
			cHPrevisto := STR0033 // "Compensado"
		ElseIF aTabCalend[nPosCalen, CALEND_POS_TIPO_DIA] == "D"
			cHPrevisto := STR0034 // "D.S.R."
		ElseIF aTabCalend[nPosCalen, CALEND_POS_TIPO_DIA] == "N"
			cHPrevisto := STR0035 // "Não trabalhado"
		Else
			While nPosCalen <= Len(aTabCalend) .And. aTabCalend[nPosCalen][CALEND_POS_DATA_APO] == dDataRef
				cHPrevisto += StrTran(StrZero(aTabCalend[nPosCalen][CALEND_POS_HORA], 5, 2), '.', ':') + " "
				nPosCalen++
			EndDo
		EndIf
	EndIf
	
Return cHPrevisto

/*/{Protheus.doc} GetHRealizado
Retorna uma string com as marcações realizadas pelo funcionário
@type  Static Function
@author Cícero Alves
@since 09/11/2023
@param dDataRef, Data, Data de referência para busca das marcações
@param aMarcacoes, Array, Todas as marcações realizadas pelo funcionário no período
@return cHRealizado, Character, Marcações realizadas na data de referência
/*/
Static Function GetHRealizado(dDataRef, aMarcacoes)
	
	Local cHRealizado	:= ""
	Local nPosMarc		:= 0
	
	Default aMarcacoes := {}
	
	If (nPosMarc := aScan(aMarcacoes, {|x| x[AMARC_DATAAPO] == dDataRef})) > 0
		While nPosMarc <= Len(aMarcacoes) .And. aMarcacoes[nPosMarc][AMARC_DATAAPO] == dDataRef
			cHRealizado += StrTran(StrZero(aMarcacoes[nPosMarc][AMARC_HORA], 5, 2), '.', ':') + " "
			nPosMarc++
		EndDo
	EndIf
	
Return cHRealizado
