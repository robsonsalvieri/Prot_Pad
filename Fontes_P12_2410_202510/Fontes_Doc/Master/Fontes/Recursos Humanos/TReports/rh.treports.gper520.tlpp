#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "rh.treports.gper520.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SRC, SRD, SRV", customTables="SRA, SRV", name="Relatório Seguro de Vida", country="ALL", initialRelease="12.1.2210")
class gper520TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass

method new() class gper520TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) // RH
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) // Relatório Seguro de Vida
return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type  Metodo
@author Squad Rotinas
@since  Julho 07,2023
/*/
method getDescription() as character class gper520TReportsBusinessObject
return (STR0003) // Emite um Relatório de Seguro de Vida

method getData(nPage as numeric, oFilter as object) as object class gper520TReportsBusinessObject
    
    Local cQuery as character    
	Local cWhere		:= ""	
	Local cFilExec := AllTrim(FWFilialName())      //Variável com o nome da Filial e Grupo de Empresa
	Local cEmpExec := AllTrim(FWEmpName(cEmpAnt))  //Variável com o nome da Filial e Grupo de Empresa	                         
	Local cAcessaSRA	:= &( " { || " + ChkRH( "GPER520" , "SRA" , "2" ) + " } " ) //-- Variaveis de Acesso do Usuario      

	Local aCodFol		:= {}
	Local aPerAberto    := {}
	Local aPerFechado   := {}
	Local aPerTodos     := {}
	Local aCodFolQuery  := {}
	Local aPDFields     := {}
	Local aAllFields    := {}
	
    Local jParams       := oFilter:getParameters() as JSON
	Local jData         := JSONObject():New()     
    Local jLGPD         := JSONObject():New()     
    Local cProcesso     := jParams["processo"][1]
    Local cRot      	:= jParams["roteiro"][1]
    Local cPeriodo      := jParams["periodo"][1]
    Local cNroPag       := jParams["nropag"][1]
    Local cFil          := jParams["filial"][1]
    Local cCC           := jParams["centroCusto"][1]
    Local cMatricula    := jParams["matricula"][1]
    Local aSituacao     := jParams["situacao"]
    Local aCategoria    := jParams["categoria"]

	Local nPosAb		:= 0 
	Local nPosFc		:= 0
	Local nBasSeg	 	:= 0
	Local nCusBen 		:= 0
	Local nCusEmp   	:= 0

    Local cValidFil     := fValidFil() as Character
    Local nParamOrder   := 1           as Numeric
    Local nSituacao     := 0           as Numeric
    Local nCategoria    := 0           as Numeric
    Local oStatement    := NIL         as Object
	Local nFields       := 1

	Local lObfuscated   := .F.

	Private lAberto		:= .T.
	Private lFechado	:= .T.
	Private cAliasMov 	:= ""
	Private cTabela
    Private cAlias

	// Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

	// Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RC_PD", "RD_PD", "RC_VALOR", "RD_VALOR", "RA_FILIAL", "RA_MAT"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nSituacao := 1 To Len(aSituacao)
        aSituacao[nSituacao] := IIf(aSituacao[nSituacao] == NIL .or. Empty(aSituacao[nSituacao]), " ", aSituacao[nSituacao])
    Next nSituacao

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nCategoria := 1 To Len(aCategoria)
        aCategoria[nCategoria] := IIf(aCategoria[nCategoria] == NIL .or. Empty(aCategoria[nCategoria]), " ", aCategoria[nCategoria])
    Next nCategoria

	If !Empty(cPeriodo)
		cAno	:= SubStr(cPeriodo,1,4)		
		cMes	:= SubStr(cPeriodo,5,2) 
		fRetPerComp( 	cMes		  ,;	// Obrigatorio - Mes para localizar as informacoes
						cAno		  ,;	// Obrigatorio - Ano para localizar as informacoes
						xFilial("RCH"),;	// Opcional - Filial a Pesquisar
						cProcesso	  ,;	// Opcional - Filtro por Processo
						cRot	  ,;	// Opcional - Filtro por Roteiro
						@aPerAberto	  ,;	// Por Referencia - Array com os periodos Abertos
						@aPerFechado, ;		// Por Referencia - Array com os periodos Fechados
						@aPerTodos    ;		// Por Referencia - Array com os periodos Abertos e Fechados em Ordem Crescente
					)
		nPosAb	:= 	aScan(aPerAberto, {|x|  x[1]+x[2]+x[7]+x[8] == cPeriodo + cNroPag + cProcesso + cRot })
		lAberto	:= 	If(nPosAb == 0, .F., .T.)
		nPosFc	:=	aScan(aPerFechado, {|x|  x[1]+x[2]+x[7]+x[8] == cPeriodo + cNroPag + cProcesso + cRot })
		lFechado:= 	If(nPosFc == 0, .F., .T.)

		If  lAberto .Or. lFechado

			If( lAberto, cAliasMov	:= "SRC", cAliasMov := "SRD")

			If lAberto
				cAliasMov 	:= "SRC"
				cTabela 	:= "RC"
				cAcessa 	:= &("{ || " + ChkRH("GPER520","SRC","2") + "}") 
			Else 
				cAliasMov 	:= "SRD"
				cTabela 	:= "RD"
				cAcessa 	:= &("{ || " + ChkRH("GPER520","SRD","2") + "}") 
			EndIf

            If Len(aSituacao) > 0     // 5
                cWhere += " SRA.RA_SITFOLH IN (?) AND "
            EndIf
            
            If Len(aCategoria) > 0    // 6
                cWhere += " SRA.RA_CATFUNC IN (?) AND "
            EndIf

            If !Empty(cFil)           // 7
                cWhere += " SRA.RA_FILIAL = ? AND "
            EndIf
            
            If !Empty(cMatricula)    // 8
                cWhere += " SRA.RA_MAT = ? AND "
            EndIf

            If !Empty(cProcesso)     // 9
                cWhere += " SRA.RA_PROCES = ? AND "
            Endif

            If !Empty(cCC)           // 10
                cWhere += " SRA.RA_CC = ? AND "
            Endif

			If !Empty(cRot)          // 11
				cWhere += cAliasMov + "." + cTabela + "_ROTEIR = ? AND "
			Endif

			If !Empty(cPeriodo)      // 12
				cWhere += cAliasMov + "." + cTabela + "_PERIODO = ? AND "
			Endif

			If !Empty(cNroPag)       // 13
				cWhere += cAliasMov + "." + cTabela + "_SEMANA = ? AND "
			Endif

			cQuery +=     " SELECT SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_CC, " 
			cQuery +=     cAliasMov + "." + cTabela + "_PD PD, " + cAliasMov + "." + cTabela + "_VALOR VALOR "
			cQuery +=     " ? "                                              // 1
			cQuery +=     " FROM "+RetSqlName("SRA")+" SRA "
			cQuery +=     " INNER JOIN "+RetSqlName(cAliasMov)+" "+cAliasMov+" "
			cQuery +=     " 	ON "+cAliasMov+"."+cTabela+"_MAT = SRA.RA_MAT "
			cQuery +=     "    AND "+cAliasMov+"."+cTabela+"_FILIAL = SRA.RA_FILIAL "
			cQuery +=     "    AND "+cAliasMov+".D_E_L_E_T_ = ? "            // 2
			cQuery +=     " INNER JOIN " + RetSqlName("SRV") + " SRV "
			cQuery +=     " 	ON SRV.RV_COD 		= "+cAliasMov+"."+cTabela+"_PD "
			cQuery +=     "    AND SRV.RV_CODFOL 	IN (?) "   // 3
			cQuery +=     "    AND "+FwJoinFilial("SRV",cAliasMov)
			cQuery +=     "    AND SRV.D_E_L_E_T_ = ? "        // 4
			cQuery +=     " WHERE "+cWhere                     // 5 até 13
			cQuery +=     "        SRA.D_E_L_E_T_ = ? "        // 14
    
            cQuery := ChangeQuery(cQuery)

            // Instancia a classe
            oStatement := FwExecStatement():New(cQuery)

            // Captura e define os campos personalizados das tabelas 
			cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRC", "SRD", "SRV"})
    		oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

			oStatement:SetString(nParamOrder++, " ") // 2

			AAdd(aCodFolQuery, '0153')
			AAdd(aCodFolQuery, '0154')
			AAdd(aCodFolQuery, '0155')

			oStatement:SetIN(nParamOrder++, aCodFolQuery) // 3

			oStatement:SetString(nParamOrder++, " ")      // 4

            If Len(aSituacao) > 0                         // 5
                oStatement:SetIN(nParamOrder++, aSituacao)
            EndIf
            
            If Len(aCategoria) > 0                        // 6
                oStatement:SetIN(nParamOrder++, aCategoria)
            EndIf

            If !Empty(cFil)                               // 7
                oStatement:SetString(nParamOrder++, cFil)
            EndIf
            
            If !Empty(cMatricula)                         // 8
                oStatement:SetString(nParamOrder++, cMatricula)
            EndIf

            If !Empty(cProcesso)                          // 9
                oStatement:SetString(nParamOrder++, cProcesso)
            Endif

            If !Empty(cCC)                                // 10
                oStatement:SetString(nParamOrder++, cCC)
            Endif

			If !Empty(cRot)                               // 11
				oStatement:SetString(nParamOrder++, cRot)
			Endif

			If !Empty(cPeriodo)                           // 12
				oStatement:SetString(nParamOrder++, cPeriodo)
			Endif

			If !Empty(cNroPag)                            // 13
				oStatement:SetString(nParamOrder++, cNroPag)
			Endif

			oStatement:SetString(nParamOrder++, " ")      // 14

            // Executa a query e retorna o alias criado
            cAlias := oStatement:OpenAlias()

			cFilialAnt := cFilAnt
			Fp_CodFol( @aCodFol, cFilialAnt, .T. )

			If !(cAlias)->(Eof())
				cCCustoAnt 	:= Trim(IIf(jLGPD["RA_CC"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),      (cAlias)->RA_CC))		
				cMatAnt 	:= (cAlias)->RA_MAT
				cNomeAnt 	:= Trim(IIf(jLGPD["RA_NOME"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),    (cAlias)->RA_NOME))
				cFilialAnt	:= (cAlias)->RA_FILIAL

				While !(cAlias)->(Eof())     
					If !((cAlias)->RA_FILIAL $ cValidFil) .Or. !Eval(cAcessaSRA) .Or. !Eval(cAcessa)
						(cAlias)->( dbSkip() )
						Loop
					EndIf	

					If (cAlias)->(RA_MAT) != cMatAnt .Or. (cAlias)->RA_FILIAL != cFilialAnt
						jData := JSONObject():New()
						jData["FilialExec"]      := cFilExec
						jData["EmpresaExec"]     := cEmpExec
						jData["RA_FILIAL"]       := Trim(IIf(jLGPD["RA_FILIAL"], FwProtectedDataUtil():ValueAsteriskToAnonymize(cFilialAnt),  cFilialAnt))
						jData["RA_CC"]      	 := cCCustoAnt
						jData["RA_MAT"]          := Trim(IIf(jLGPD["RA_MAT"],    FwProtectedDataUtil():ValueAsteriskToAnonymize(cMatAnt),     cMatAnt))
						jData["RA_NOME"]         := cNomeAnt
						jData["BASE"]            := nBasSeg
						jData["EMPRESA"]         := nCusEmp
						jData["BENEF"]           := nCusBen
						jData["CUSTO"]           := nCusEmp+nCusBen

						// Adiciona os campos customizados no JSON
						GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        				// Adiciona as informações da linha
						self:oData:appendData(jData)			

						nBasSeg := 0
						nCusEmp := 0
						nCusBen := 0	
					Endif

					If (cAlias)->RA_MAT != cMatAnt //Mudou o funcionario				
						cMatAnt 	:= (cAlias)->RA_MAT
						cNomeAnt 	:= Trim(IIf(jLGPD["RA_NOME"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),    (cAlias)->RA_NOME))
						cCCustoAnt 	:= Trim(IIf(jLGPD["RA_CC"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),      (cAlias)->RA_CC))		
					Endif
					
					If (cAlias)->RA_FILIAL != cFilialAnt //Mudou a filial
						cFilialAnt := (cAlias)->RA_FILIAL
						Fp_CodFol( @aCodFol, cFilialAnt, .T. )
					Endif

					If (cAlias)->VALOR > 0
						Do Case 
							Case Trim((cAlias)->PD) == aCodFol[155,1]
								nBasSeg += (cAlias)->VALOR
							Case Trim((cAlias)->PD) == aCodFol[153,1]
								nCusBen += (cAlias)->VALOR
							Case Trim((cAlias)->PD) == aCodFol[154,1]
								nCusEmp += (cAlias)->VALOR
						EndCase
					Endif
					
					(cAlias)->(DbSkip())
				EndDo

				jData := JSONObject():New()
				jData["FilialExec"]      := cFilExec
				jData["EmpresaExec"]     := cEmpExec
				jData["RA_FILIAL"]       := Trim(IIf(jLGPD["RA_FILIAL"], FwProtectedDataUtil():ValueAsteriskToAnonymize(cFilialAnt),  cFilialAnt))
				jData["RA_CC"]      	 := cCCustoAnt
				jData["RA_MAT"]          := Trim(IIf(jLGPD["RA_MAT"],    FwProtectedDataUtil():ValueAsteriskToAnonymize(cMatAnt),     cMatAnt))
				jData["RA_NOME"]         := cNomeAnt
				jData["BASE"]            := nBasSeg
				jData["EMPRESA"]         := nCusEmp
				jData["BENEF"]           := nCusBen
				jData["CUSTO"]           := nCusEmp+nCusBen

				// Adiciona os campos customizados no JSON
				GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        		// Adiciona as informações da linha
				self:oData:appendData(jData)				
			Endif

			(cAlias)->(DbCloseArea())
			
		EndIf
	EndIf

     // Limpa os objetos da memória
    FwFreeObj(oStatement)
    FwFreeObj(jParams)
	FwFreeObj(jData)
	FwFreeObj(jLGPD)
	FwFreeArray(aCodFolQuery)
    FwFreeArray(aSituacao)
    FwFreeArray(aCategoria)
    FwFreeArray(aCodFol)
    FwFreeArray(aPerAberto)
    FwFreeArray(aPerFechado)
    FwFreeArray(aPerTodos)
	FwFreeArray(aPDFields)
    FwFreeArray(aAllFields)
return self:oData

method getSchema() as object class gper520TReportsBusinessObject
    local aFieldsSRA as array
    
    aFieldsSRA := { "RA_FILIAL", "RA_NOME", "RA_CC" }
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)

    self:oSchema:addProperty("RA_MAT",       STR0004,        "string",  STR0004,      "RA_MAT") // "Matrícula"
    self:oSchema:addProperty("BASE",         STR0005,        "number",  STR0005,      "BASE") // "Base Segurada"
    self:oSchema:addProperty("EMPRESA",      STR0006,        "number",  STR0006,      "EMPRESA") // "Custo Empresa"
    self:oSchema:addProperty("BENEF",        STR0007,        "number",  STR0007,      "BENEF") // "Custo Beneficiário"
    self:oSchema:addProperty("CUSTO",        STR0008,        "number",  STR0008,      "CUSTO") // "Custo Total"
    self:oSchema:addProperty("FilialExec",   STR0009,        "string",  STR0011,      "FilialExec") // "Filial de Execução"
    self:oSchema:addProperty("EmpresaExec",  STR0010,        "string",  STR0012,      "EmpresaExec") // "Empresa de Execução"

    // Parâmetros
    self:oSchema:addParameter("processo",    STR0013 + " ?",     "string", .F.) // "Processo"
    self:oSchema:addParameter("roteiro",     STR0014 + " ?",     "string", .F.) // "Roteiro"
    self:oSchema:addParameter("periodo",     STR0015 + " ?",     "string", .F.) // "Período"
    self:oSchema:addParameter("nropag",      STR0016 + " ?",     "string", .F.) // "Número Pgto"
    self:oSchema:addParameter("filial",      STR0017 + " ?",     "string", .F.) // "Filial"
    self:oSchema:addParameter("centroCusto", STR0018 + " ?",     "string", .F.) // "Centro de Custo"
    self:oSchema:addParameter("matricula",   STR0004 + " ?",     "string", .F.) // "Matrícula"
    self:oSchema:addParameter("situacao",    STR0019 + " ?",     "string", .T.) // "Situação"
    self:oSchema:addParameter("categoria",   STR0020 + " ?",       "string", .T.) // "Categoria"
    self:oSchema:addParameter("quebraCC",    STR0021 + " ?",     "string", .F.) // "Quebra C.C."

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("processo",    "/api/framework/v1/genericLookupService/smartview/RCJ", 2)
    self:setCustomURL("roteiro",     "/api/framework/v1/genericLookupService/smartview/SRY", 2)
    self:setCustomURL("filial",      "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("centroCusto", "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("matricula",   "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("situacao",    "/api/rh/smartview/v1/options/GPEParams/getSX5/31",     2)
    self:setCustomURL("categoria",   "/api/rh/smartview/v1/options/GPEParams/getSX5/28",     2)
    self:setCustomURL("quebraCC",    "/api/rh/smartview/v1/options/GPEParams/getSNOptions",  1)
return self:oSchema
