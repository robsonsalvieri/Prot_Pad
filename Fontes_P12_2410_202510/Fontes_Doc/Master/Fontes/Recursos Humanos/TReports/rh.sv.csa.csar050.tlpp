#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.csa.csar050.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} CSAR050TReportsBusinessObject:new()
    Classe do objeto de negócio do relatório Aumento Programado (CSAR050).
    @type cLASS
    @version 12.1.2210
    @author arthur.sales
    @since 19/09/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SRJ, CTT, RB7, SQ3, SX5", customTables="SRA, SRJ, CTT, RB7, SQ3", name="Rel. Aumento Programado", country="ALL", initialRelease="12.1.2210")
Class CSAR050TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} CSAR050TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2210
    @author arthur.sales
    @since 19/09/2023
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class CSAR050TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0003) // "Aumento Programado"
Return self

/*/{Protheus.doc} CSAR050TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2210
    @author arthur.sales
    @since 19/09/2023
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class CSAR050TReportsBusinessObject
Return STR0001 // "Relação de Aumentos Salariais Programados para os Funcionários."

/*/{Protheus.doc} CSAR050TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2210
    @author arthur.sales
    @since 19/09/2023
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class CSAR050TReportsBusinessObject
    // Declaração das variáveis locais
    Local cAlias        As Character // Alias do arquivo temporário
    Local cQuery        As Character // Query a ser executada
    Local cFieldSX5     As Character // Nome do campo de descrição da SX5
    Local cFilter       As Character // Filtro do relatório
    Local cWhere        As Character // Cláusula where da query
    Local cFilExec      As Character // Nome da filial de execução
    Local cEmpExec      As Character // Nome da empresa de execução
    Local jParams       As JSON      // Parâmetros do relatório
    Local nParamOrder   As Numeric
    Local nFields       As Numeric
    Local oStatement    As Object
    Local cFilial1      As Character
    Local cFilial2      As Character
    Local cCC1          As Character
    Local cCC2          As Character
    Local cFuncao1      As Character
    Local cFuncao2      As Character
    Local cDtAdmis1     As Character
    Local cDtAdmis2     As Character
    Local cDtAumento1   As Character
    Local cDtAumento2   As Character
    Local cListar       As Character
    Local cCustomFields As Character // Campos customizados no formato SQL
    Local cName         As Character // Nome real do campo
    Local aPDFields     As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR   As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR     As Array     // Campos com nome diferente do real
    Local aAllFields    As Array     // Estrutura de todos os campos do ON
    Local lObfuscated   As Logical   // Flag que indica se tem algum campo para ofuscar
    Local jData         As JSON      // Informações a serem adicionadas no ON
    Local jLGPD         As JSON      // Indica quais campos devem ser ofuscados

    // Inicialização das variáveis
    cAlias        := ""
    cQuery        := ""
    cFieldSX5     := FDescSX5(2)
    cFilter       := IIf(oFilter:hasFilter(), "AND " + oFilter:getSQLExpression(), "")
    cWhere        := ""
    cFilExec      := AllTrim(FwFilialName())
    cEmpExec      := AllTrim(FwEmpName(cEmpAnt))
    jParams       := oFilter:getParameters()
    nParamOrder   := 1
    nFields       := 1
    oStatement    := NIL
    cFilial1      := IIf(jParams:hasProperty("PAR_FILIAL1")       .and. Len(jParams["PAR_FILIAL1"]) > 0       .and. !Empty(jParams["PAR_FILIAL1"][1]),            jParams["PAR_FILIAL1"][1],      "")
    cFilial2      := IIf(jParams:hasProperty("PAR_FILIAL2")       .and. Len(jParams["PAR_FILIAL2"]) > 0       .and. !Empty(jParams["PAR_FILIAL2"][1]),            jParams["PAR_FILIAL2"][1],      "")
    cCC1          := IIf(jParams:hasProperty("PAR_CC1")           .and. Len(jParams["PAR_CC1"]) > 0           .and. !Empty(jParams["PAR_CC1"][1]),                jParams["PAR_CC1"][1],          "")
    cCC2          := IIf(jParams:hasProperty("PAR_CC2")           .and. Len(jParams["PAR_CC2"]) > 0           .and. !Empty(jParams["PAR_CC2"][1]),                jParams["PAR_CC2"][1],          "")
    cFuncao1      := IIf(jParams:hasProperty("PAR_FUNCAO1")       .and. Len(jParams["PAR_FUNCAO1"]) > 0       .and. !Empty(jParams["PAR_FUNCAO1"][1]),            jParams["PAR_FUNCAO1"][1],      "")
    cFuncao2      := IIf(jParams:hasProperty("PAR_FUNCAO2")       .and. Len(jParams["PAR_FUNCAO2"]) > 0       .and. !Empty(jParams["PAR_FUNCAO2"][1]),            jParams["PAR_FUNCAO2"][1],      "")
    cDtAdmis1     := IIf(jParams:hasProperty("PAR_DTADMIS1")      .and. Len(jParams["PAR_DTADMIS1"]) > 0      .and. !Empty(jParams["PAR_DTADMIS1"][1]),           jParams["PAR_DTADMIS1"][1],     FwTimeStamp(6, CToD("")))
    cDtAdmis2     := IIf(jParams:hasProperty("PAR_DTADMIS2")      .and. Len(jParams["PAR_DTADMIS2"]) > 0      .and. !Empty(jParams["PAR_DTADMIS2"][1]),           jParams["PAR_DTADMIS2"][1],     FwTimeStamp(6, CToD("")))
    cDtAumento1   := IIf(jParams:hasProperty("PAR_DTAUMENTO1")    .and. Len(jParams["PAR_DTAUMENTO1"]) > 0    .and. !Empty(jParams["PAR_DTAUMENTO1"][1]),         jParams["PAR_DTAUMENTO1"][1],   FwTimeStamp(6, CToD("")))
    cDtAumento2   := IIf(jParams:hasProperty("PAR_DTAUMENTO2")    .and. Len(jParams["PAR_DTAUMENTO2"]) > 0    .and. !Empty(jParams["PAR_DTAUMENTO2"][1]),         jParams["PAR_DTAUMENTO2"][1],   FwTimeStamp(6, CToD("")))
    cListar       := IIf(jParams:hasProperty("PAR_LISTAR")        .and. Len(jParams["PAR_LISTAR"]) > 0        .and. !Empty(jParams["PAR_LISTAR"][1]),             jParams["PAR_LISTAR"][1],       "")
    cCustomFields := ""
    cName         := ""
    aPDFields     := {}
    aPDFieldsNR   := {}
    aFieldsNR     := {}
    aAllFields    := {}
    lObfuscated   := .F.
    jData         := NIL
    jLGPD         := JSONObject():New()

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RA_SALARIO", "RA_CATFUNC", "RA_CODFUNC", "RJ_DESC", "RB7_SALARI", "RB7_CATEG", "RB7_FUNCAO"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Define a cláusula where de acordo com os parâmetros
    If (!Empty(cFilial1))
        cWhere += IIf(!Empty(cWhere), " AND ", "") + "RA_FILIAL >= ? " // 8
    EndIf

    If (!Empty(cFilial2))
        cWhere += IIf(!Empty(cWhere), " AND ", "") + "RA_FILIAL <= ? " // 9
    EndIf

    If (!Empty(cCC1))
        cWhere += IIf(!Empty(cWhere), " AND ", "") + "RA_CC >= ? " // 10
    EndIf

    If (!Empty(cCC2))
        cWhere += IIf(!Empty(cWhere), " AND ", "") + "RA_CC <= ? " // 11
    EndIf

    If (!Empty(cFuncao1))
        cWhere += IIf(!Empty(cWhere), " AND ", "") + "RA_CODFUNC >= ? " // 12
    EndIf

    If (!Empty(cFuncao2))
        cWhere += IIf(!Empty(cWhere), " AND ", "") + "RA_CODFUNC <= ? " // 13
    EndIf

    If (!Empty(cDtAdmis1))
        cWhere += IIf(!Empty(cWhere), " AND ", "") + "RA_ADMISSA >= ? " // 14
    EndIf

    If (!Empty(cDtAdmis2))
        cWhere += IIf(!Empty(cWhere), " AND ", "") + "RA_ADMISSA <= ? " // 15
    EndIf

    If (!Empty(cDtAumento1))
        cWhere += IIf(!Empty(cWhere), " AND ", "") + "RB7_DATALT >= ? " // 16
    EndIf

    If (!Empty(cDtAumento2))
        cWhere += IIf(!Empty(cWhere), " AND ", "") + "RB7_DATALT <= ? " // 17
    EndIf

    If (!Empty(cListar) .AND. (cListar == "2" .OR. cListar == "3"))
        cWhere += IIf(!Empty(cWhere), " AND ", "") + "RB7_ATUALI = ? " // 18
    EndIf

    // Montagem da query de busca
    cQuery += "SELECT "
    cQuery +=     "RA_FILIAL, "
    cQuery +=     "RA_MAT, "
    cQuery +=     "RA_NOME, "
    cQuery +=     "RA_SALARIO      AS SALARIO_SRA, "
    cQuery +=     "RA_CATFUNC      AS CATEG_SRA, "
    cQuery +=     "RA_CODFUNC      AS COD_FUN_SRA, "
    cQuery +=     "SRJ.RJ_DESC     AS DESC_FUN_SRA, "
    cQuery +=     "RA_CC, "
    cQuery +=     "CTT_DESC01, "
    cQuery +=     "RB7_DATALT, "
    cQuery +=     "RB7_TPALT, "
    cQuery +=     cFieldSX5 + " AS X5_DESCRI, "
    cQuery +=     "RB7_PERCEN, "
    cQuery +=     "RB7_SALARI      AS SALARIO_RB7, "
    cQuery +=     "RB7_CATEG       AS CATEG_RB7, "
    cQuery +=     "RB7_FUNCAO      AS COD_FUN_RB7, "
    cQuery +=     "SRJ_RB7.RJ_DESC AS DESC_FUN_RB7, "
    cQuery +=     "RB7_ATUALI, "
    cQuery +=     "RB7_CARGO "
    cQuery +=     "? " // 1
    cQuery += "FROM "
    cQuery +=     RetSqlName("SRA") + " SRA "
    cQuery +=     "LEFT JOIN "
    cQuery +=         RetSqlName("SRJ") + " SRJ "
    cQuery +=         "ON "
    cQuery +=             FWJoinFilial("SRJ", "SRA") + " "
    cQuery +=             "AND SRJ.RJ_FUNCAO  = SRA.RA_CODFUNC "
    cQuery +=             "AND SRJ.D_E_L_E_T_ = ? " // 2
    cQuery +=     "LEFT JOIN "
    cQuery +=         RetSqlName("CTT") + " CTT "
    cQuery +=         "ON "
    cQuery +=             FWJoinFilial("CTT", "SRA") + " "
    cQuery +=             "AND CTT.CTT_CUSTO  = SRA.RA_CC "
    cQuery +=             "AND CTT.D_E_L_E_T_ = ? " // 3
    cQuery +=     "LEFT JOIN "
    cQuery +=         RetSqlName("RB7") + " RB7 "
    cQuery +=         "ON "
    cQuery +=             FWJoinFilial("RB7", "SRA") + " "
    cQuery +=             "AND RB7.RB7_MAT    = RA_MAT "
    cQuery +=             "AND RB7.D_E_L_E_T_ = ? " // 4
    cQuery +=     "LEFT JOIN "
    cQuery +=         RetSqlName("SX5") + " SX5 "
    cQuery +=         "ON "
    cQuery +=             FWJoinFilial("SX5", "SRA") + " "
    cQuery +=             "AND SX5.X5_TABELA  = ? " // 5
    cQuery +=             "AND SX5.X5_CHAVE   = RB7.RB7_TPALT "
    cQuery +=             "AND SX5.D_E_L_E_T_ = ? " // 6
    cQuery +=     "LEFT JOIN "
    cQuery +=         RetSqlName("SRJ") + " SRJ_RB7 "
    cQuery +=         "ON "
    cQuery +=             StrTran(FWJoinFilial("SRJ", "RB7"), "SRJ.", "SRJ_RB7.") + " "
    cQuery +=             "AND SRJ_RB7.RJ_FUNCAO = RB7.RB7_FUNCAO "
    cQuery +=             "AND SRJ_RB7.D_E_L_E_T_ = ? " // 7
    cQuery += "WHERE "
    cQuery +=     cWhere
    cQuery +=     IIf(!Empty(cWhere), " AND ", "") + "SRA.RA_SITFOLH IN (?) " // 19
    cQuery +=     "AND SRA.D_E_L_E_T_ = ? " // 20
    cQuery +=     "? " // 21
    cQuery := ChangeQuery(cQuery)

	// Instancia a classe
	oStatement := FwExecStatement():New(cQuery)

    // Captura e define os campos personalizados das tabelas SRA, SRJ, CTT, RB7 e SQ3
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRJ", "CTT", "RB7", "SQ3"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    // Definições dos valores dos parâmetros
    oStatement:SetString(nParamOrder++, " ")  // 2
    oStatement:SetString(nParamOrder++, " ")  // 3
    oStatement:SetString(nParamOrder++, " ")  // 4
    oStatement:SetString(nParamOrder++, "41") // 5
    oStatement:SetString(nParamOrder++, " ")  // 6
    oStatement:SetString(nParamOrder++, " ")  // 7

    If (!Empty(cFilial1))
        oStatement:SetString(nParamOrder++, cFilial1) // 8
    EndIf

    If (!Empty(cFilial2))
        oStatement:SetString(nParamOrder++, cFilial2) // 9
    EndIf

    If (!Empty(cCC1))
        oStatement:SetString(nParamOrder++, cCC1) // 10
    EndIf

    If (!Empty(cCC2))
        oStatement:SetString(nParamOrder++, cCC2) // 11
    EndIf

    If (!Empty(cFuncao1))
        oStatement:SetString(nParamOrder++, cFuncao1) // 12
    EndIf

    If (!Empty(cFuncao2))
        oStatement:SetString(nParamOrder++, cFuncao2) // 13
    EndIf

    If (!Empty(cDtAdmis1))
        oStatement:SetString(nParamOrder++, StrTran(SubStr(cDtAdmis1, 1, 10), "-")) // 14
    EndIf

    If (!Empty(cDtAdmis2))
        oStatement:SetString(nParamOrder++, StrTran(SubStr(cDtAdmis2, 1, 10), "-")) // 15
    EndIf

    If (!Empty(cDtAumento1))
        oStatement:SetString(nParamOrder++, StrTran(SubStr(cDtAumento1, 1, 10), "-")) // 16
    EndIf

    If (!Empty(cDtAumento2))
        oStatement:SetString(nParamOrder++, StrTran(SubStr(cDtAumento2, 1, 10), "-")) // 17
    EndIf

    If (!Empty(cListar))
        If (cListar == "2")
            oStatement:SetString(nParamOrder++, "S") // 18
        ElseIf (cListar == "3")
            oStatement:SetString(nParamOrder++, "N") // 18
        EndIf
    EndIf

    oStatement:SetIN(nParamOrder++,     {" ", "A", "F"}) // 19
    oStatement:SetString(nParamOrder++, " ")             // 20
    oStatement:SetUnsafe(nParamOrder++, cFilter)         // 21

	// Executa a query e retorna o alias criado
	cAlias := oStatement:OpenAlias()

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))
        // Cria o JSON com as informações da linha
        jData := JSONObject():New()
        jData["FilialExec"]      := Trim(cFilExec)
        jData["EmpresaExec"]     := Trim(cEmpExec)
        jData["OriginPlatform"]  := "Protheus"
        jData["SALARIO_RB7"]     := IIf(jLGPD["RB7_SALARI"],       0, (cAlias)->SALARIO_RB7)
        jData["SALARIO_SRA"]     := IIf(jLGPD["RA_SALARIO"],       0, (cAlias)->SALARIO_SRA) 
        jData["RB7_PERCEN"]      := IIf(jLGPD["RB7_PERCEN"],       0, (cAlias)->RB7_PERCEN)
        jData["RA_FILIAL"]       := Trim(IIf(jLGPD["RA_FILIAL"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),    (cAlias)->RA_FILIAL))
        jData["RA_MAT"]          := Trim(IIf(jLGPD["RA_MAT"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),       (cAlias)->RA_MAT))
        jData["RA_NOME"]         := Trim(IIf(jLGPD["RA_NOME"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),      (cAlias)->RA_NOME))
        jData["CATEG_SRA"]       := Trim(IIf(jLGPD["RA_CATFUNC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CATEG_SRA),    (cAlias)->CATEG_SRA))
        jData["COD_FUN_SRA"]     := Trim(IIf(jLGPD["RA_CODFUNC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->COD_FUN_SRA),  (cAlias)->COD_FUN_SRA))
        jData["DESC_FUN_SRA"]    := Trim(IIf(jLGPD["RJ_DESC"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->DESC_FUN_SRA), (cAlias)->DESC_FUN_SRA))
        jData["RA_CC"]           := Trim(IIf(jLGPD["RA_CC"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),        (cAlias)->RA_CC))
        jData["CTT_DESC01"]      := Trim(IIf(jLGPD["CTT_DESC01"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01),   (cAlias)->CTT_DESC01))
        jData["RB7_DATALT"]      := Trim(IIf(jLGPD["RB7_DATALT"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB7_DATALT),   IIf(Empty((cAlias)->RB7_DATALT), "", totvs.framework.treports.date.stringToTimeStamp((cAlias)->RB7_DATALT))))
        jData["RB7_TPALT"]       := Trim(IIf(jLGPD["RB7_TPALT"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB7_TPALT),    (cAlias)->RB7_TPALT))
        jData["X5_DESCRI"]       := Trim(IIf(jLGPD["X5_DESCRI"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->X5_DESCRI),    (cAlias)->X5_DESCRI))
        jData["CATEG_RB7"]       := Trim(IIf(jLGPD["RB7_CATEG"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CATEG_RB7),    (cAlias)->CATEG_RB7))
        jData["COD_FUN_RB7"]     := Trim(IIf(jLGPD["RB7_FUNCAO"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->COD_FUN_RB7),  (cAlias)->COD_FUN_RB7))
        jData["RB7_ATUALI"]      := Trim(IIf(jLGPD["RB7_ATUALI"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB7_ATUALI),   (cAlias)->RB7_ATUALI))
        jData["RB7_CARGO"]       := Trim(IIf(jLGPD["RB7_CARGO"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB7_CARGO),    (cAlias)->RB7_CARGO))
        jData["DESC_FUN_RB7"]    := Trim(IIf(jLGPD["RJ_DESC"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->DESC_FUN_RB7), (cAlias)->DESC_FUN_RB7))
        jData["DESC_CARGO"]      := Trim(IIf(jLGPD["RB7_CARGO"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB7_CARGO),    FDesc("SQ3", (cAlias)->RB7_CARGO, "Q3_DESCSUM")))
        jData["DESC_CATEG_SRA"]  := Trim(IIf(jLGPD["RA_CATFUNC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CATEG_SRA),    FDesc("SX5", "28" + (cAlias)->CATEG_SRA, cFieldSX5)))
        jData["DESC_CATEG_RB7"]  := Trim(IIf(jLGPD["RB7_CATEG"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CATEG_RB7),    FDesc("SX5", "28" + (cAlias)->CATEG_RB7, cFieldSX5)))

        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
    FwFreeArray(aAllFields)
    FwFreeArray(aFieldsNR)
    FwFreeArray(aPDFields)
    FwFreeArray(aPDFieldsNR)
Return self:oData

/*/{Protheus.doc} CSAR050TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2210
    @author arthur.sales
    @since 19/09/2023
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class CSAR050TReportsBusinessObject
    // Declaração das variáveis locais
    Local aFieldsSRA As Array // Campos que serão utilizados da tabela SRA
    Local aFieldsRB7 As Array // Campos que serão utilizados da tabela RB7
    Local aFieldsSX5 As Array // Campos que serão utilizados da tabela SX5

    // Inicialização das variáveis
    aFieldsSRA := {"RA_FILIAL", "RA_MAT", "RA_NOME", "RA_CC"}
    aFieldsRB7 := {"RB7_DATALT", "RB7_TPALT", "RB7_PERCEN", "RB7_ATUALI", "RB7_CARGO"}
    aFieldsSX5 := {"X5_DESCRI"}

    // Define os schemas das tabelas
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("RB7", aFieldsRB7)
    self:oSchema:aliasToSchema("SX5", aFieldsSX5)

    // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_FILIAL1",    STR0004,           "string", .F.) // "Filial De"
    self:oSchema:addParameter("PAR_FILIAL2",    STR0005,           "string", .F.) // "Filial Até"
    self:oSchema:addParameter("PAR_CC1",        STR0006,           "string", .F.) // "Centro de Custo De"
    self:oSchema:addParameter("PAR_CC2",        STR0007,           "string", .F.) // "Centro de Custo Até"
    self:oSchema:addParameter("PAR_FUNCAO1",    STR0008,           "string", .F.) // "Função De"
    self:oSchema:addParameter("PAR_FUNCAO2",    STR0009,           "string", .F.) // "Função Até"
    self:oSchema:addParameter("PAR_DTADMIS1",   STR0010,           "date",   .F.) // "Data de Admissão De"
    self:oSchema:addParameter("PAR_DTADMIS2",   STR0011,           "date",   .F.) // "Data de Admissão Até"
    self:oSchema:addParameter("PAR_DTAUMENTO1", STR0012,           "date",   .F.) // "Data de Aumento De"
    self:oSchema:addParameter("PAR_DTAUMENTO2", STR0013,           "date",   .F.) // "Data de Aumento Até"
    self:oSchema:addParameter("PAR_LISTAR",     STR0014,           "string", .F.) // "Listar"

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",     STR0015,             "string", "Filial Exec",             "FilialExec") // "Filial de Execução",     
    self:oSchema:addProperty("EmpresaExec",    STR0016,             "string", "Empresa Exec",            "EmpresaExec") // "Empresa de Execução",    
    self:oSchema:addProperty("DESC_FUN_SRA",   STR0017 + " SRA",    "string", "Descrição Função SRA",    "DESC_FUN_SRA") // "Descrição Função SRA",   
    self:oSchema:addProperty("DESC_FUN_RB7",   STR0017 + " RB7",    "string", "Descrição Função RB7",    "DESC_FUN_RB7") // "Descrição Função RB7",   
    self:oSchema:addProperty("COD_FUN_SRA",    STR0018 + " SRA",    "string", "Código Função SRA",       "COD_FUN_SRA") // "Código Função SRA",      
    self:oSchema:addProperty("CATEG_SRA",      STR0019 + " SRA",    "string", "Categoria Funcional SRA", "CATEG_SRA") // "Categoria Funcional SRA",
    self:oSchema:addProperty("COD_FUN_RB7",    STR0018 + " RB7",    "string", "Código Função RB7",       "COD_FUN_RB7") // "Código Função RB7",      
    self:oSchema:addProperty("CATEG_RB7",      STR0019 + " RB7",    "string", "Categoria Funcional RB7", "CATEG_RB7") // "Categoria Funcional RB7",
    self:oSchema:addProperty("SALARIO_RB7",    STR0020 + " RB7",    "number", "Salário RB7",             "SALARIO_RB7") // "Salário RB7",            
    self:oSchema:addProperty("SALARIO_SRA",    STR0020 + " SRA",    "number", "Salário SRA",             "SALARIO_SRA") // "Salário SRA",            
    self:oSchema:addProperty("DESC_CARGO",     STR0021,             "string", "Descrição Cargo",         "DESC_CARGO") // "Descrição Cargo",        
    self:oSchema:addProperty("DESC_CATEG_SRA", STR0022 + " SRA",    "string", "Descrição Categoria SRA", "DESC_CATEG_SRA") // "Descrição Categoria SRA",
    self:oSchema:addProperty("DESC_CATEG_RB7", STR0022 + " RB7",    "string", "Descrição Categoria RB7", "DESC_CATEG_RB7") // "Descrição Categoria RB7",
    self:oSchema:addProperty("CTT_DESC01",     STR0023,             "string", "Descrição Centro Custo",  "CTT_DESC01") // "Descrição Centro Custo", 

    self:setCustomURL("PAR_FILIAL1",    	"/api/rh/smartview/v1/options/GPEParams/getBranches",                   2)
    self:setCustomURL("PAR_FILIAL2",    	"/api/rh/smartview/v1/options/GPEParams/getBranches",                   2)
    self:setCustomURL("PAR_CC1",            "/api/framework/v1/genericLookupService/smartview/CTT",                 2)
    self:setCustomURL("PAR_CC2",            "/api/framework/v1/genericLookupService/smartview/CTT",                 2)
    self:setCustomURL("PAR_FUNCAO1",        "/api/framework/v1/genericLookupService/smartview/SRJ",                 2)
    self:setCustomURL("PAR_FUNCAO2",        "/api/framework/v1/genericLookupService/smartview/SRJ",                 2)
    self:setCustomURL("PAR_LISTAR",         "/api/rh/smartview/v1/options/CSAR050/PAR_LISTAR",                      1)
Return self:oSchema
