#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "PONCALEN.CH"
#include "rh.treports.ponr200.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPON", tables="SRA, SPJ", name="Quadro de Horários por Funcionário", country="ALL", initialRelease="12.1.2210")
class PONR200TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getAreas() as array
    public method getSchema() as object
    public method getData() as object
endclass

method new() as object class PONR200TReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) // "Quadro de Horários por Funcionário"
return self

method getDescription() as character class PONR200TReportsBusinessObject
return STR0002 // "Objeto contendo Informações dos Horários dos Funcionários"

method getAreas() as array class PONR200TReportsBusinessObject
return {STR0003} // "RH"

method getSchema() as object class PONR200TReportsBusinessObject
    local aFieldsSRA as array
    local aFieldsSPJ as array
    
    aFieldsSRA := {"RA_FILIAL", "RA_MAT", "RA_ADMISSA", "RA_NOME", "RA_CC", "RA_CODFUNC", "RA_CATFUNC", "RA_SITFOLH", "RA_TNOTRAB", "RA_SEQTURN"}
    aFieldsSPJ := {"PJ_TURNO", "PJ_SEMANA", "PJ_CODREF", "PJ_ENTRA1", "PJ_SAIDA1", "PJ_ENTRA2", "PJ_SAIDA2", "PJ_ENTRA3", "PJ_SAIDA3", "PJ_ENTRA4", "PJ_SAIDA4", "PJ_TPEXT", "PJ_TPEXTN" } 
    
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("SPJ", aFieldsSPJ)
	
    self:oSchema:addProperty("DATA"		, STR0004, "date"  , STR0004, "DATA") 		// "Data"
    self:oSchema:addProperty("DIA"		, STR0005, "string", STR0005, "PJ_DIA") 	// "Dia"
    self:oSchema:addProperty("EXCECAO"	, STR0006, "string", STR0006, "EXCECAO")	// "Exceção"
    self:oSchema:addProperty("HRSTRAB"	, STR0007, "number", STR0008, "HRSTRAB") 	// "Horas Trabalhadas" - "Hrs. Trab."
    self:oSchema:addProperty("HRSINT"	, STR0009, "number", STR0010, "HRSINT") 	// "Horas de Intervalo" - STR0009
    self:oSchema:addProperty("PJ_TPDIA"	, STR0011, "string", STR0011, "PJ_TPDIA") 	// "Tipo do Dia"
    self:oSchema:addProperty("CCDESC"	, STR0012, "string", STR0013, "CCDESC") 	// "Descrição do Centro de Custo" - "Desc. C.C."
    self:oSchema:addProperty("TURNODESC", STR0014, "string", STR0015, "TURNODESC") 	// "Descrição do Turno" - "Desc. Turno"
    self:oSchema:addProperty("FUNCDESC"	, STR0016, "string", STR0017, "FUNCDESC") 	// "Descrição da Função" - "Desc. Função"
    self:oSchema:addProperty("REFDESC"	, STR0018, "string", STR0018, "REFDESC") 	// "Refeição"
	
	self:oSchema:AddParameter("FILIAL1", STR0019, "string", .F.)	// "Filial De?"
	self:oSchema:AddParameter("FILIAL2", STR0020, "string", .F.)	// "Filial Até?"
	self:oSchema:AddParameter("MAT1", STR0021, "string", .F.) 		// "Matrícula De?"
	self:oSchema:AddParameter("MAT2", STR0022, "string", .F.) 		// "Matrícula Até?"
	self:oSchema:AddParameter("TURNO1", STR0023, "string", .F.) 	// "Turno De?"
	self:oSchema:AddParameter("TURNO2", STR0024, "string", .F.) 	// "Turno Até?"
	self:oSchema:AddParameter("DATA1", STR0025, "date", .F.) 		// "Data de?"
	self:oSchema:AddParameter("DATA2", STR0026, "date", .F.) 		// "Data Até?"
	
return self:oSchema

method getData(nPage as numeric, oFilter as object) as object class PONR200TReportsBusinessObject
    local cAliasSRA			as character
	local cTipoDia			as character
	local cHENormal 		as character
	local cHENoturna		as character
	local cDesFun			as character
	local cDescCC			as character
	local cCateg			as character
	local cSitFol			as character
	local cDescTur			as character
	local cLastFil			as character
	local cFilDe			as character
	local cFilAte			as character
	local cMatDe			as character
	local cMatAte			as character
	local cTurnoDe			as character
	local cTurnoAte			as character
    local dDataDe			as date
	local dDataAte			as date
	local dPerIni			as date
	local dPerFim			as date
	local dMarcDay			as date
	local dDtAdmis			as date
	local aTabCalend 		as array
	local aTurnos			as array
	local aTabPadrao		as array
	local aMarc				as array
	local aPeriodos			as array
	local nI				as numeric
	local nPer				as numeric
	local nMarc				as numeric
	local nLoops			as numeric
	local nHorasTrab		as numeric
	local nHorasInte		as numeric
	local nParamOrder := 1	as numeric
	local lBuscaPer			as logical
	local oJParams			as json
	local oStatement		as Object
	
	oJParams := oFilter:getParameters()
	
	// Validação do filtro "Filial De" e "Filial Até" para utilização na Query
	If (!Empty(oJParams['FILIAL1', 1]), cFilDe := oJParams['FILIAL1', 1], cFilDe := Space(GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
	If (!Empty(oJParams['FILIAL2', 1]), cFilAte := oJParams['FILIAL2', 1], cFilAte := Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))

	// Validação do filtro "Matrícula De" e "Matrícula Até" para utilização na Query
	If (!Empty(oJParams['MAT1', 1]), cMatDe := oJParams['MAT1', 1], cMatDe := Space(GetSX3Cache("RA_MAT", "X3_TAMANHO")))
	If (!Empty(oJParams['MAT2', 1]), cMatAte := oJParams['MAT2', 1], cMatAte := Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))

	// Validação do filtro "Turno De" e "Turno Até" para utilização na Query
	If (!Empty(oJParams['TURNO1', 1]), cTurnoDe := oJParams['TURNO1', 1], cTurnoDe := Space(GetSX3Cache("RA_TNOTRAB", "X3_TAMANHO")))
	If (!Empty(oJParams['TURNO2', 1]), cTurnoAte := oJParams['TURNO2', 1], cTurnoAte := Replicate("Z", GetSX3Cache("RA_TNOTRAB", "X3_TAMANHO")))

	
	If !Empty(oJParams['DATA1', 1])
		dDataDe := sToD(StrTran(SubStr(ArrTokStr(oJParams['DATA1']), 1, 10), "-" ))
	EndIf
	
	If !Empty(oJParams['DATA2', 1])
		dDataAte := sToD(StrTran(SubStr(ArrTokStr(oJParams['DATA2']), 1, 10), "-" ))
	EndIf
	
	// Se as duas datas estiverem em branco busca pelo período de apontamento
	lBuscaPer := If(Empty(dDataDe) .And. Empty(dDataAte), .T., .F.)
	
	dMarcDay := cToD("//")
  
	cQuery :=	"SELECT "
	cQuery +=		"RA_FILIAL, "
	cQuery +=		"RA_MAT, "
	cQuery +=		"RA_NOME, "
	cQuery +=		"RA_CC, "
	cQuery +=		"RA_ADMISSA, "
	cQuery +=		"RA_TNOTRAB, "
	cQuery +=		"RA_SEQTURN, "
	cQuery +=		"RA_CATFUNC, "
	cQuery +=		"RA_CODFUNC, "
	cQuery +=		"RA_SITFOLH, "
	cQuery +=		"R_E_C_N_O_ AS RECNO "
	cQuery +=	"FROM " + RetSQLName("SRA") + " SRA "
	cQuery +=	"WHERE SRA.RA_FILIAL BETWEEN ? AND ? "		// 1 e 2
	cQuery +=		"AND SRA.RA_MAT BETWEEN ? AND ? "		// 3 e 4
	cQuery +=		"AND SRA.RA_TNOTRAB BETWEEN ? AND ? "	// 5 e 6
	cQuery +=		"AND SRA.D_E_L_E_T_ = ? "				// 7

	cQuery := ChangeQuery(cQuery)

	oStatement := FwExecStatement():New(cQuery)

	oStatement:SetString(nParamOrder++,  cFilDe)	// 1
	oStatement:SetString(nParamOrder++,  cFilAte)	// 2
	oStatement:SetString(nParamOrder++,  cMatDe)	// 3
	oStatement:SetString(nParamOrder++,  cMatAte)	// 4
	oStatement:SetString(nParamOrder++,  cTurnoDe)	// 5
	oStatement:SetString(nParamOrder++,  cTurnoAte)	// 6
	oStatement:SetString(nParamOrder++,  " ")		// 7

	cAliasSRA := oStatement:OpenAlias()
		
	cLastFil := "***"
	
    While !(cAliasSRA)->(Eof())
		dbSelectArea("SRA")
		SRA->(dbGoTo((cAliasSRA)->RECNO))
		dDtAdmis := sToD((cAliasSRA)->RA_ADMISSA)
		
		If (cAliasSRA)->RA_FILIAL != cLastFil
			cLastFil := (cAliasSRA)->RA_FILIAL
			
			// Se as duas datas estiverem em branco busca pelo período de apontamento
			If(lBuscaPer, CheckPonMes( @dDataDe, @dDataAte, .F., .T., .F., (cAliasSRA)->RA_FILIAL), NIL)
			
			// Obtem as datas do Periodo em Aberto
			GetPonMesDat( @dPerIni, @dPerFim, cLastFil )
		EndIf
		
		// Busca as descrições
		cDesFun := (cAliasSRA)->(fDesc("SRJ", RA_CODFUNC, "RJ_DESC", 20, RA_FILIAL))
		cDescCC := (cAliasSRA)->(DescCC(RA_CC, RA_FILIAL))
		cCateg	:= (cAliasSRA)->(fDesc("SX5", "28" + RA_CATFUNC, "X5_DESCRI", 11, RA_FILIAL))
		cSitFol	:= (cAliasSRA)->(fDesc("SX5", "31" + RA_SITFOLH, "X5_DESCRI", 15, RA_FILIAL))
		cDescTur := (cAliasSRA)->(fDesc("SR6", RA_TNOTRAB, "R6_DESC", 50, RA_FILIAL))
		
		aPeriodos := Monta_per(dDataDe, dDataAte, (cAliasSRA)->RA_FILIAL, (cAliasSRA)->RA_MAT, dPerIni, dPerFim )
		
		For nPer := 1 To Len(aPeriodos)
			
			aTabCalend := {}
			aTabPadrao := {}
			aTurnos := {}
			
			If (cAliasSRA)->(!CriaCalend(	aPeriodos[nPer][1] 	,;	//01 -> Data Inicial do Periodo
											aPeriodos[nPer][2]	,;	//02 -> Data Final do Periodo
											RA_TNOTRAB			,;	//03 -> Turno Para a Montagem do Calendario
											RA_SEQTURN			,;	//04 -> Sequencia Inicial para a Montagem Calendario
											@aTabPadrao			,;	//05 -> Array Tabela de Horario Padrao
											@aTabCalend			,;	//06 -> Array com o Calendario de Marcacoes
											RA_FILIAL   	  	,;	//07 -> Filial para a Montagem da Tabela de Horario
											RA_MAT   			,;	//08 -> Matricula para a Montagem da Tabela de Horario
											RA_CC		   		,;	//09 -> Centro de Custo para a Montagem da Tabela
											@aTurnos			,;	//10 -> Array com as Trocas de Turno
											NIL					,;	//11 -> Array com Todas as Excecoes do Periodo
											NIL					,;	//12 -> Se executa Query para a Montagem da Tabela Padrao
											.T.					,;	//13 -> Se executa a funcao se sincronismo do calendario
											.T.			 		;	//14 -> Se Forca a Criacao de Novo Calendario
										))
				LOOP
			EndIf
			
			nLoops := Len(aTabCalend)
			nMarc := 1
			aMarc := Array(8)
			
			For nI := 1 To nLoops
				
				If aTabCalend[nI][CALEND_POS_DATA_APO] < aPeriodos[nPer][3]
					LOOP
				ElseIf aTabCalend[nI][CALEND_POS_DATA_APO] > aPeriodos[nPer][4]
					EXIT
				EndIf
				
				// Acumula horas trabalhadas e de intervalo
				nHorasTrab := SomaHoras(nHorasTrab, aTabCalend[nI][CALEND_POS_HRS_TRABA])
				nHorasInte := SomaHoras(nHorasInte, aTabCalend[nI][CALEND_POS_HRS_INTER])
				
				// Guarda o horário da marcação
				aMarc[nMarc] := aTabCalend[nI][CALEND_POS_HORA]
				nMarc++
				
				dMarcDay := If(Empty(dMarcDay), aTabCalend[nI, CALEND_POS_DATA], dMarcDay)
				
				If (nI < nLoops .And. aTabCalend[nI][CALEND_POS_ORDEM] != aTabCalend[nI + 1][CALEND_POS_ORDEM]) .Or. nI == nLoops
					
					// Tipo de dia 
					If aTabCalend[nI][CALEND_POS_TIPO_DIA] == "S"
						cTipoDia := STR0027 // "Trabalhado"
					ElseIf aTabCalend[nI][CALEND_POS_TIPO_DIA] == "D"
						cTipoDia := STR0028 // "D.S.R."
					ElseIf aTabCalend[nI][CALEND_POS_TIPO_DIA] == "C"
						cTipoDia := STR0029 // "Compensado"
					ElseIf aTabCalend[nI][CALEND_POS_FERIADO]
						cTipoDia := STR0030 // "Feriado"
					Else
						cTipoDia := STR0031 // "Não Trabalhado"
					EndIf
					
					//Busca a descrição dos tipos de Hora Extra
					If aTabCalend[nI][CALEND_POS_FERIADO] .And. aTabCalend[nI][CALEND_POS_TIPO_DIA] != "S"
						fTipoExtra( aTabCalend[nI][CALEND_POS_TP_HE_FER_NR], @cHENormal, .F. )
						fTipoExtra( aTabCalend[nI][CALEND_POS_TP_HE_FER_NT], @cHENoturna, .F. )
					Else
						fTipoExtra( aTabCalend[nI][CALEND_POS_TIPO_HE_NOR], @cHENormal, .F. )
						fTipoExtra( aTabCalend[nI][CALEND_POS_TIPO_HE_NOT], @cHENoturna, .F. )
					EndIf
					
					(cAliasSRA)->(self:oData:appendData({;
							"RA_FILIAL": RA_FILIAL,;
							"RA_MAT": RA_MAT,;
							"RA_NOME": RA_NOME,;
							"RA_ADMISSA": FwTimeStamp( 5, dDtAdmis, "00:00:00" ),;
							"RA_CODFUNC": RA_CODFUNC,;
							"FUNCDESC": cDesFun,;
							"RA_CC": RA_CC,;
							"CCDESC": cDescCC,;
							"RA_CATFUNC": cCateg,;
							"RA_SITFOLH": cSitFol,;
							"RA_TNOTRAB": RA_TNOTRAB, ;
							"TURNODESC": cDescTur,;
							"RA_SEQTURN": RA_SEQTURN,;
							"PJ_ENTRA1": aMarc[1],;
							"PJ_SAIDA1": aMarc[2],;
							"PJ_ENTRA2": aMarc[3],;
							"PJ_SAIDA2": aMarc[4],;
							"PJ_ENTRA3": aMarc[5],;
							"PJ_SAIDA3": aMarc[6],;
							"PJ_ENTRA4": aMarc[7],;
							"PJ_SAIDA4": aMarc[8],;
							"PJ_TURNO": aTabCalend[nI][CALEND_POS_TURNO],;
							"PJ_SEMANA": aTabCalend[nI][CALEND_POS_SEQ_TURNO],;
							"PJ_TPDIA": cTipoDia,;
							"PJ_TPEXT": cHENormal,;
							"PJ_TPEXTN": cHENoturna,;
							"PJ_CODREF": aTabCalend[nI][CALEND_POS_COD_REFEICAO],;
							"REFDESC": AllTrim( fDesc("SP1", aTabCalend[nI][CALEND_POS_COD_REFEICAO], "P1_DESC")),; 
							"DATA": FwTimeStamp( 5, dMarcDay, "00:00:00" ),;
							"DIA": DiaSemana( dMarcDay, 3 ),;
							"EXCECAO": If(aTabCalend[nI][CALEND_POS_EXCECAO] == "E", STR0032, STR0033),;
							"HRSTRAB": nHorasTrab,;
							"HRSINT": nHorasInte;
						}))
					
					// Reinicializa as variáveis na troca de dia
					aMarc := Array(8)
					nMarc := 1
					nHorasTrab := 0
					nHorasInte := 0
					cHENormal := ""
					cHENoturna := ""
					dMarcDay := cToD("//")
					
				EndIf
			Next nI
		Next nPer
		
        (cAliasSRA)->( dbSkip() )
    EndDo
    
    (cAliasSRA)->( DBCloseArea() )
    
return self:oData
