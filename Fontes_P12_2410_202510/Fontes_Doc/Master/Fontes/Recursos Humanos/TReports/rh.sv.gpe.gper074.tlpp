#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper074.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER074TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Alterações Cadastrais de Funcionários.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 25/10/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SR9, SRA, CTT, SRJ", customTables="ALL", name="Relatório Alterações Cadastrais de Funcionários", country="ALL", initialRelease="12.1.2310")
Class GPER074TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} GPER074TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 25/10/2023
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER074TReportsBusinessObject
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Alterações Cadastrais de Funcionários"
Return self

/*/{Protheus.doc} GPER074TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 25/10/2023
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER074TReportsBusinessObject
Return STR0003 // "Relatório Alterações Cadastrais de Funcionários."

/*/{Protheus.doc} GPER074TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 25/10/2023
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER074TReportsBusinessObject
    // Declaração das variáveis locais
    Local cQuery          As Character // Query para busca dos registros
    Local cAlias          As Character // Alias do arquivo temporário principal
    Local cFiltro         As Character // Filtro do objeto de negócio
    Local cFilExec        As Character // Nome da filial de execução
    Local cEmpExec        As Character // Nome da empresa de execução
    Local cDtAlteracaoDe  As Character // Parâmetro Data Alteração De
    Local cDtAlteracaoAte As Character // Parâmetro Data Alteração Até
    Local cFilialDe       As Character // Parâmetro Filial De
    Local cFilialAte      As Character // Parâmetro Filial Até
    Local cMatriculaDe    As Character // Parâmetro Matrícula De
    Local cMatriculaAte   As Character // Parâmetro Matrícula Até
    Local cCCDe           As Character // Parâmetro C.C. De
    Local cCCAte          As Character // Parâmetro C.C. Até
    Local cCampo          As Character // Parâmetro  Campo
    Local cFilSRASRJ      As Character // Expressão SQL do join dos campos de filial das tabelas SRA e SRJ
    Local cFilSRACTT      As Character // Expressão SQL do join dos campos de filial das tabelas SRA e CTT
    Local cLanguage       As Character // Idioma do sistema
    Local cSX3Desc        As Character // Nome do campo de descrição da SX3
    Local cCustomFields   As Character // Campos customizados no formato SQL
    Local cName           As Character // Nome real do campo
    Local nParamOrder     As Numeric   // Contador auxiliar para definição de valor dos bind parameters
    Local nSituacoes      As Numeric   // Contador para percorrer as situações
    Local nCategorias     As Numeric   // Contador para percorrer as categorias
    Local nFields         As Numeric   // Contador de campos
    Local jParams         As JSON      // Parâmetros do objeto de negócio
    Local jData           As JSON      // Informações a serem adicionadas no ON
    Local jLGPD           As JSON      // Indica quais campos devem ser ofuscados
    Local jFields         As JSON      // Contém a descrição e combo box dos campos
    Local aCategorias     As Array     // Vetor de categorias
    Local aSituacoes      As Array     // Vetor de situações
    Local aPDFields       As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR     As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aAllFields      As Array     // Estrutura de todos os campos do ON
    Local lObfuscated     As Logical   // Flag que indica se tem algum campo para ofuscar
    Local oStatement      As Object    // Instância da classe FwExecStatement

    // Inicialização das variáveis locais
    cQuery          := ""
    cAlias          := ""
    cFiltro         := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    cFilExec        := AllTrim(FwFilialName())
    cEmpExec        := AllTrim(FwEmpName(cEmpAnt))
    cDtAlteracaoDe  := ""
    cDtAlteracaoAte := ""
    cFilialDe       := ""
    cFilialAte      := ""
    cMatriculaDe    := ""
    cMatriculaAte   := ""
    cCCDe           := ""
    cCCAte          := ""
    cCampo          := ""
    cFilSRASRJ      := FwJoinFilial("SRA", "SRJ")
    cFilSRACTT      := FwJoinFilial("SRA", "CTT")
    cLanguage       := FWRetIdiom()
    cSX3Desc        := IIf(cLanguage == "pt-br", "X3_DESCRIC", IIf(cLanguage == "es", "X3_DESCSPA", "X3_DESCENG"))
    cCustomFields   := ""
    cName           := ""
    nParamOrder     := 1
    nSituacoes      := 0
    nCategorias     := 0
    nFields         := 1
    jParams         := oFilter:getParameters()
    jData           := NIL
    jLGPD           := JSONObject():New()
    jFields         := JSONObject():New()
    aCategorias     := {}
    aSituacoes      := {}
    aPDFields       := {}
    aPDFieldsNR     := {}
    aAllFields      := {}
    lObfuscated     := .F.
    oStatement      := NIL

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cDtAlteracaoDe  := IIf(jParams:hasProperty("PAR_DTALTERACAODE")  .and. Len(jParams["PAR_DTALTERACAODE"])  > 0 .and.!Empty(jParams["PAR_DTALTERACAODE"][1]),  jParams["PAR_DTALTERACAODE"][1],  FwTimeStamp(6, CToD("")))
        cDtAlteracaoAte := IIf(jParams:hasProperty("PAR_DTALTERACAOATE") .and. Len(jParams["PAR_DTALTERACAOATE"]) > 0 .and.!Empty(jParams["PAR_DTALTERACAOATE"][1]), jParams["PAR_DTALTERACAOATE"][1], FwTimeStamp(6, CToD("")))
        cFilialDe       := IIf(jParams:hasProperty("PAR_FILIALDE")       .and. Len(jParams["PAR_FILIALDE"])       > 0 .and.!Empty(jParams["PAR_FILIALDE"][1]),       jParams["PAR_FILIALDE"][1],       "")
        cFilialAte      := IIf(jParams:hasProperty("PAR_FILIALATE")      .and. Len(jParams["PAR_FILIALATE"])      > 0 .and.!Empty(jParams["PAR_FILIALATE"][1]),      jParams["PAR_FILIALATE"][1],      Replicate("Z", GetSX3Cache("R9_FILIAL", "X3_TAMANHO")))
        cCCDe           := IIf(jParams:hasProperty("PAR_CENTROCUSTODE")  .and. Len(jParams["PAR_CENTROCUSTODE"])  > 0 .and.!Empty(jParams["PAR_CENTROCUSTODE"][1]),  jParams["PAR_CENTROCUSTODE"][1],  "")
        cCCAte          := IIf(jParams:hasProperty("PAR_CENTROCUSTOATE") .and. Len(jParams["PAR_CENTROCUSTOATE"]) > 0 .and.!Empty(jParams["PAR_CENTROCUSTOATE"][1]), jParams["PAR_CENTROCUSTOATE"][1], Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cMatriculaDe    := IIf(jParams:hasProperty("PAR_MATRICULADE")    .and. Len(jParams["PAR_MATRICULADE"])    > 0 .and.!Empty(jParams["PAR_MATRICULADE"][1]),    jParams["PAR_MATRICULADE"][1],    "")
        cMatriculaAte   := IIf(jParams:hasProperty("PAR_MATRICULAATE")   .and. Len(jParams["PAR_MATRICULAATE"])   > 0 .and.!Empty(jParams["PAR_MATRICULAATE"][1]),   jParams["PAR_MATRICULAATE"][1],   Replicate("Z", GetSX3Cache("R9_MAT", "X3_TAMANHO")))
        cCampo          := IIf(jParams:hasProperty("PAR_CAMPO")          .and. Len(jParams["PAR_CAMPO"])          > 0 .and.!Empty(jParams["PAR_CAMPO"][1]),          jParams["PAR_CAMPO"][1],          "")
        aCategorias     := IIf(jParams:hasProperty("PAR_CATEGORIAS"), jParams["PAR_CATEGORIAS"], {})
        aSituacoes      := IIf(jParams:hasProperty("PAR_SITUACOES"),  jParams["PAR_SITUACOES"],  {})
    EndIf

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nSituacoes := 1 To Len(aSituacoes)
        aSituacoes[nSituacoes] := IIf(aSituacoes[nSituacoes] == NIL .or. Empty(aSituacoes[nSituacoes]), " ", aSituacoes[nSituacoes])
    Next nSituacoes

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nCategorias := 1 To Len(aCategorias)
        aCategorias[nCategorias] := IIf(aCategorias[nCategorias] == NIL .or. Empty(aCategorias[nCategorias]), " ", aCategorias[nCategorias])
    Next nCategorias

    // Trata as variáveis de data para ficarem no formato do banco
    cDtAlteracaoDe  := StrTran(SubStr(cDtAlteracaoDe,  1, 10), "-", "")
    cDtAlteracaoAte := StrTran(SubStr(cDtAlteracaoAte, 1, 10), "-", "")

    // Montagem da query de busca
    cQuery += "SELECT "
    cQuery +=     "RA_FILIAL, "
    cQuery +=     "RA_CC, "
    cQuery +=     "CTT_DESC01, "
    cQuery +=     "RA_MAT, "
    cQuery +=     "RA_NOME, "
    cQuery +=     "RA_ADMISSA, "
    cQuery +=     "RA_CODFUNC, "
    cQuery +=     "RJ_DESC, "
    cQuery +=     "R9_DATA, "
    cQuery +=     "R9_CAMPO, "
    cQuery +=     "R9_DESC "
    cQuery +=     "? " // 1
    cQuery += "FROM "
    cQuery +=     RetSQLName("SRA") + " SRA "
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("SR9") + " SR9 "
    cQuery +=         "ON "
    cQuery +=             "R9_FILIAL          = RA_FILIAL "
    cQuery +=             "AND R9_MAT         = RA_MAT "
    cQuery +=             "AND R9_DATA BETWEEN ? AND ? " // 2 e 3
    cQuery +=             "AND (? = ? OR R9_CAMPO = ?) " // 4, 5 e 6
    cQuery +=             "AND SR9.D_E_L_E_T_ = ? "      // 7
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("CTT") + " CTT "
    cQuery +=         "ON "
    cQuery +=             cFilSRACTT + " "
    cQuery +=             "AND CTT_CUSTO      = RA_CC "
    cQuery +=             "AND CTT.D_E_L_E_T_ = ? " // 8
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("SRJ") + " SRJ "
    cQuery +=         "ON "
    cQuery +=             cFilSRASRJ + " "
    cQuery +=             "AND RJ_FUNCAO      = RA_CODFUNC "
    cQuery +=             "AND SRJ.D_E_L_E_T_ = ? " // 9
    cQuery += "WHERE "
    cQuery +=     "RA_FILIAL  BETWEEN ? AND ? "                            // 10 e 11
    cQuery +=     "AND RA_MAT BETWEEN ? AND ? "                            // 12 e 13
    cQuery +=     "AND RA_CC  BETWEEN ? AND ? "                            // 14 e 15
    cQuery +=     IIf(Len(aSituacoes)  > 0, " AND RA_SITFOLH IN (?)", " ") // 16
    cQuery +=     IIf(Len(aCategorias) > 0, " AND RA_CATFUNC IN (?)", " ") // 17
    cQuery +=     "AND SRA.D_E_L_E_T_ = ? "                                // 18
    cQuery +=     "? "                                                     // 19
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Captura e define os campos personalizados das tabelas SR9, SRA, CTT, SRJ
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SR9", "SRA", "CTT", "SRJ"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    // Define o valor dos bind parameters
    oStatement:SetDate(nParamOrder++,   SToD(cDtAlteracaoDe))  // 2
    oStatement:SetDate(nParamOrder++,   SToD(cDtAlteracaoAte)) // 3
    oStatement:SetString(nParamOrder++, cCampo)                // 4
    oStatement:SetString(nParamOrder++, " ")                   // 5
    oStatement:SetString(nParamOrder++, cCampo)                // 6
    oStatement:SetString(nParamOrder++, " ")                   // 7
    oStatement:SetString(nParamOrder++, " ")                   // 8
    oStatement:SetString(nParamOrder++, " ")                   // 9
    oStatement:SetString(nParamOrder++, cFilialDe)             // 10
    oStatement:SetString(nParamOrder++, cFilialAte)            // 11
    oStatement:SetString(nParamOrder++, cMatriculaDe)          // 12
    oStatement:SetString(nParamOrder++, cMatriculaAte)         // 13
    oStatement:SetString(nParamOrder++, cCCDe)                 // 14
    oStatement:SetString(nParamOrder++, cCCAte)                // 15

    If (Len(aSituacoes) > 0)
        oStatement:SetIn(nParamOrder++, aSituacoes)            // 16
    EndIf

    If (Len(aCategorias) > 0)
        oStatement:SetIn(nParamOrder++, aCategorias)           // 17
    EndIf

    oStatement:SetString(nParamOrder++, " ")                   // 18
    oStatement:SetUnsafe(nParamOrder++, cFiltro)               // 19

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))
        // Verifica se o campo já se encontra no JSON
        // Se sim, acessa o atributo do campo e captura a descrição e combo box
        // Senão, utiliza a função GetSX3Cache() para as informações do campo e as grava como atributo no JSON, para não precisar buscar novamente
        If (jFields:hasProperty((cAlias)->R9_CAMPO))
            cDesc := jFields[(cAlias)->R9_CAMPO][1]
            cCBox := jFields[(cAlias)->R9_CAMPO][2]
        Else
            cDesc := Trim(GetSX3Cache((cAlias)->R9_CAMPO, cSX3Desc))
            cCBox := Trim(GetSX3Cache((cAlias)->R9_CAMPO, "X3_CBOX"))
            jFields[(cAlias)->R9_CAMPO] := {cDesc, cCBox}
        EndIf
        
        // Adiciona as informações da linha
        jData := {;
            "RA_FILIAL":   Trim(IIf(jLGPD["RA_FILIAL"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),  (cAlias)->RA_FILIAL)),;
            "RA_CC":       Trim(IIf(jLGPD["RA_CC"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),      (cAlias)->RA_CC)),;
            "CTT_DESC01":  Trim(IIf(jLGPD["CTT_DESC01"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01), (cAlias)->CTT_DESC01)),;
            "RA_MAT":      Trim(IIf(jLGPD["RA_MAT"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),     (cAlias)->RA_MAT)),;
            "RA_NOME":     Trim(IIf(jLGPD["RA_NOME"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),    (cAlias)->RA_NOME)),;
            "RA_ADMISSA":  totvs.framework.treports.date.dateToTimeStamp(SToD((cAlias)->RA_ADMISSA)),;
            "RA_CODFUNC":  Trim(IIf(jLGPD["RA_CODFUNC"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CODFUNC), (cAlias)->RA_CODFUNC)),;
            "RJ_DESC":     Trim(IIf(jLGPD["RJ_DESC"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RJ_DESC),    (cAlias)->RJ_DESC)),;
            "R9_DATA":     totvs.framework.treports.date.dateToTimeStamp(SToD((cAlias)->R9_DATA)),;
            "R9_CAMPO":    Trim(IIf(jLGPD["R9_CAMPO"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->R9_CAMPO),   (cAlias)->R9_CAMPO)),;
            "X3_DESCRIC":  cDesc,;
            "X3_CBOX":     cCBox,;
            "R9_DESC":     Trim(IIf(jLGPD["R9_DESC"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RC0_FILIAL), (cAlias)->R9_DESC)),;
            "FilialExec":  cFilExec,;
            "EmpresaExec": cEmpExec;
        }

        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
    FwFreeObj(jFields)
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)
    FwFreeArray(aAllFields)
    FwFreeArray(aPDFields)
Return self:oData

/*/{Protheus.doc} GPER074TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 25/10/2023
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER074TReportsBusinessObject
    // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_DTALTERACAODE",  STR0004,  "date",   .F.)
    self:oSchema:addParameter("PAR_DTALTERACAOATE", STR0005,  "date",   .F.)
    self:oSchema:addParameter("PAR_FILIALDE",       STR0006,  "string", .F.)
    self:oSchema:addParameter("PAR_FILIALATE",      STR0007,  "string", .F.)
    self:oSchema:addParameter("PAR_CENTROCUSTODE",  STR0008,  "string", .F.)
    self:oSchema:addParameter("PAR_CENTROCUSTOATE", STR0009,  "string", .F.)
    self:oSchema:addParameter("PAR_MATRICULADE",    STR0010,  "string", .F.)
    self:oSchema:addParameter("PAR_MATRICULAATE",   STR0011,  "string", .F.)
    self:oSchema:addParameter("PAR_CATEGORIAS",     STR0012,  "string", .T.)
    self:oSchema:addParameter("PAR_SITUACOES",      STR0013,  "string", .T.)
    self:oSchema:addParameter("PAR_CAMPO",          STR0014,  "string", .F.)
    self:oSchema:addParameter("PAR_QUEBRA",         STR0015,  "string", .F.)
    self:oSchema:addParameter("PAR_DETALHES",       STR0016,  "string", .F.)

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_FILIALDE",       "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("PAR_FILIALATE",      "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("PAR_CENTROCUSTODE",  "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("PAR_CENTROCUSTOATE", "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("PAR_MATRICULADE",    "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("PAR_MATRICULAATE",   "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("PAR_CATEGORIAS",     "/api/rh/smartview/v1/options/GPEParams/getSX5/28",     2)
    self:setCustomURL("PAR_SITUACOES",      "/api/rh/smartview/v1/options/GPEParams/getSX5/31",     2)
    self:setCustomURL("PAR_CAMPO",          "/api/rh/smartview/v1/options/GPER074/getR9Campo",      2)
    self:setCustomURL("PAR_QUEBRA",         "/api/rh/smartview/v1/options/GPER074",                 1)
    self:setCustomURL("PAR_DETALHES",       "/api/rh/smartview/v1/options/GPEParams/getSNOptions",  1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",   STR0017,       "string",   STR0018,    "FilialExec",  NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpresaExec",  STR0019,       "string",   STR0020,    "EmpresaExec", NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("RA_FILIAL",   "RA_FILIAL",    "string",   STR0021,    "RA_FILIAL",   NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RA_CC",       "RA_CC",        "string",   STR0022,    "RA_CC",       NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("CTT_DESC01",  "CTT_DESC01",   "string",   STR0023,    "CTT_DESC01",  NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RA_MAT",      "RA_MAT",       "string",   STR0024,    "RA_MAT",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RA_NOME",     "RA_NOME",      "string",   STR0025,    "RA_NOME",     NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RA_ADMISSA",  "RA_ADMISSA",   "date",     STR0026,    "RA_ADMISSA",  NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RA_CODFUNC",  "RA_CODFUNC",   "string",   STR0027,    "RA_CODFUNC",  NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("RJ_DESC",     "RJ_DESC",      "string",   STR0028,    "RJ_DESC",     NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("R9_DATA",     "R9_DATA",      "date",     STR0029,    "R9_DATA",     NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("R9_CAMPO",    "R9_CAMPO",     "string",   STR0030,    "R9_CAMPO",    NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("X3_DESCRIC",  "X3_DESCRIC",   "string",   STR0031,    "X3_DESCRIC",  NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("X3_CBOX",     "X3_CBOX",      "string",   STR0032,    "X3_CBOX",     NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("R9_DESC",     "R9_DESC",      "string",   STR0033,    "R9_DESC",     NIL, NIL, NIL, .T.)
Return self:oSchema
