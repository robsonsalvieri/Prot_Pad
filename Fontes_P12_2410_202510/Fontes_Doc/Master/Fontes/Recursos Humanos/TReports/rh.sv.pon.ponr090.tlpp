#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "PONCALEN.CH"
#include "rh.sv.pon.ponr090.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPON", tables="SRA, SPB, SPL, SRV", name="Resultados do Ponto", country="ALL", initialRelease="12.1.2210")
class PONR090TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getAreas() as array
    public method getSchema() as object
    public method getData() as object
	
	protected data ojItems as json
	
endclass

method new() as object class PONR090TReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) // "Resultados do Ponto"
return self

method getDescription() as character class PONR090TReportsBusinessObject
return STR0002 // "Objeto contendo os resultados do ponto eletrônico"

method getAreas() as array class PONR090TReportsBusinessObject
return {STR0003} // "RH"

method getSchema() as object class PONR090TReportsBusinessObject
    
    local aFieldsSRA as array
    local aFieldsSPB as array
    
    aFieldsSRA := {"RA_FILIAL", "RA_MAT", "RA_ADMISSA", "RA_NOME", "RA_CATFUNC", "RA_SITFOLH" }
    aFieldsSPB := {"PB_PD", "PB_TIPO1", "PB_HORAS", "PB_VALOR", "PB_TIPO2", "PB_CONVOC", "PB_DATA"} 
    
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("SPB", aFieldsSPB)
	
    self:oSchema:addProperty("RA_CC"	 , STR0004, "string", STR0005, "RA_CC") 		// "Centro de Custo do Funcionário" - "C.C. Func"
    self:oSchema:addProperty("CCFUNDESC" , STR0006, "string", STR0007, "CCFUNDESC") 	// "Descrição Centro de Custo do Funcionário" - "Desc. C.C. Func"
    self:oSchema:addProperty("PB_CC"	 , STR0008, "string", STR0009, "PB_CC") 		// "Centro de Custo do Lançamento" - "C.C. Lanc"
    self:oSchema:addProperty("CCLANDESC" , STR0010, "string", STR0011, "CCLANDESC") 	// "Descrição do Centro de Custo do Lançamento" - "Desc. C.C. Lanc"
    self:oSchema:addProperty("PB_INTEGRA", STR0012, "string", STR0013, "PB_INTEGRA") 	// "Lançamento integrado com a Folha de Pagamento" - "Integrado"
    self:oSchema:addProperty("RV_TIPOCOD", STR0014, "string", STR0015, "RV_TIPOCOD") 	// "Tipo do Lançamento" - "Tipo Lanc."
    self:oSchema:addProperty("RV_DESC"	 , STR0016, "string", STR0017, "RV_DESC") 		// "Descrição da Verba"
    self:oSchema:addProperty("TABELA"	 , STR0032, "string", STR0033, "TABELA") 		// "Tabela do lançamento" - "Tabela"
	
	self:oSchema:AddParameter("FILIAL1", STR0018, "string", .F.) // "Filial De?"
	self:oSchema:AddParameter("FILIAL2", STR0019, "string", .F.) // "Filial Até?"
	self:oSchema:AddParameter("MAT1", STR0020, "string", .F.)  // "Matrícula De?"
	self:oSchema:AddParameter("MAT2", STR0021, "string", .F.) // "Matrícula Até?"
	self:oSchema:AddParameter("CENTROC1", STR0022, "string", .F.) // "Centro de Custo De?"
	self:oSchema:AddParameter("CENTROC2", STR0023, "string", .F.) // "Centro de Custo Até?"
	self:oSchema:AddParameter("DATA1", STR0024, "date", .F.) // "Data de?"
	self:oSchema:AddParameter("DATA2", STR0025, "date", .F.) // "Data Até?"
	
return self:oSchema

method getData(nPage as numeric, oFilter as object) as object class PONR090TReportsBusinessObject
    local cAliasSPB					as character
	Local cFilDe					as character
	Local cFilAte					as character
	Local cMatDe					as character
	Local cMatAte					as character
	Local cCCDe						as character
	Local cCCAte					as character
	local cCateg					as character
	local cSitFol					as character
	local cDescCC					as character
    local cDataDe					as character
	local cDataAte					as character
	local cDigHoras					as character
	local cLastKey := "***"			as character
	local cLGPDSRA					as character
	Local cQuery					as Character
	Local dDataSPB := sToD("//")	as Date
	Local dDtAdmis := sToD("//")	as Date
	local aFieldsSRA				as array
	local aLGPDSRA					as array
	local nI						as numeric
	local nParamOrder := 1			as numeric
	local oJParams					as json
	local oStatement				as Object
	
	oJParams := oFilter:getParameters()	

	// Validação do filtro "Filial De" e "Filial Até" para utilização na Query
	If (!Empty(oJParams['FILIAL1', 1]), cFilDe := oJParams['FILIAL1', 1], cFilDe := Space(GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
	If (!Empty(oJParams['FILIAL2', 1]), cFilAte := oJParams['FILIAL2', 1], cFilAte := Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))

	// Validação do filtro "Matrícula De" e "Matrícula Até" para utilização na Query
	If (!Empty(oJParams['MAT1', 1]), cMatDe := oJParams['MAT1', 1], cMatDe := Space(GetSX3Cache("RA_MAT", "X3_TAMANHO")))
	If (!Empty(oJParams['MAT2', 1]), cMatAte := oJParams['MAT2', 1], cMatAte := Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))

	// Validação do filtro "Centro de Custo De" e "Centro de Custo Até" para utilização na Query
	If (!Empty(oJParams['CENTROC1', 1]), cCCDe := oJParams['CENTROC1', 1], cCCDe := Space(GetSX3Cache("RA_CC", "X3_TAMANHO")))
	If (!Empty(oJParams['CENTROC2', 1]), cCCAte := oJParams['CENTROC2', 1], cCCAte := Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
	
	cDataDe := StrTran( SubStr(ArrTokStr(oJParams['DATA1']), 1, 10), "-" )
	cDataAte := StrTran( SubStr(ArrTokStr(oJParams['DATA2']), 1, 10), "-" )
	
	// Define se as horas estão em sexagesimal ou centesimal
	cDigHoras := SuperGetmv("MV_HORASDE")
	
	aFieldsSRA := {"RA_MAT", "RA_NOME", "RA_CATFUNC", "RA_SITFOLH", "RA_CC" }
	aLGPDSRA := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsSRA)
	For nI := 1 To Len(aFieldsSRA)
		If(aScan(aLGPDSRA, aFieldsSRA[nI]) == 0, cLGPDSRA += aFieldsSRA[nI] + "*", Nil)
	Next
	
	cQuery :=	"SELECT "
	cQuery +=		"'SPB' AS TABELA, "
	cQuery +=		"PB_PD, "
	cQuery +=		"PB_TIPO1, "
	cQuery +=		"PB_HORAS, "
	cQuery +=		"PB_VALOR, "
	cQuery +=		"PB_CC, "
	cQuery +=		"PB_TIPO2, "
	cQuery +=		"PB_INTEGRA, "
	cQuery +=		"PB_CONVOC, "
	cQuery +=		"PB_DATA, "
	cQuery +=		"RA_FILIAL, "
	cQuery +=		"RA_MAT, "
	cQuery +=		"RA_ADMISSA, "
	cQuery +=		"RA_NOME, "
	cQuery +=		"RA_CC, "
	cQuery +=		"RA_CATFUNC, "
	cQuery +=		"RA_SITFOLH, "
	cQuery +=		"RV_DESC, "
	cQuery +=		"RV_TIPOCOD, "
	cQuery +=		"CTT_DESC01 "
	cQuery +=	"FROM " + RetSQLName("SPB") + " SPB "
	cQuery +=	"INNER JOIN " + RetSQLName("SRA") + " SRA ON SRA.RA_FILIAL = SPB.PB_FILIAL AND SRA.RA_MAT = SPB.PB_MAT AND SRA.D_E_L_E_T_ = ? "			// 1
	cQuery +=	"INNER JOIN " + RetSQLName("SRV") + " SRV ON " + FWJoinFilial("SRV", "SPB") + " AND SRV.RV_COD = SPB.PB_PD AND SRV.D_E_L_E_T_ = ? "		// 2
	cQuery +=	"INNER JOIN " + RetSQLName("CTT") + " CTT ON " + FWJoinFilial("CTT", "SPB") + " AND CTT.CTT_CUSTO = SPB.PB_CC AND CTT.D_E_L_E_T_ = ? "	// 3
	cQuery +=	"WHERE SPB.PB_DATA BETWEEN ? AND ? "		// 4 e 5
	cQuery +=		"AND SRA.RA_FILIAL BETWEEN ? AND ? "	// 6 e 7
	cQuery +=		"AND SRA.RA_CC BETWEEN ? AND ? "		// 8 e 9
	cQuery +=		"AND SRA.RA_MAT BETWEEN ? AND ? "		// 10 e 11
	cQuery +=		"AND SPB.D_E_L_E_T_ = ? "				// 12
	cQuery +=	"UNION ALL "
	cQuery +=	"SELECT "
	cQuery +=		"'SPL' AS TABELA, "
	cQuery +=		"PL_PD, "
	cQuery +=		"PL_TIPO1, "
	cQuery +=		"PL_HORAS, "
	cQuery +=		"PL_VALOR, "
	cQuery +=		"PL_CC, "
	cQuery +=		"PL_TIPO2, "
	cQuery +=		"PL_INTEGRA, "
	cQuery +=		"PL_CONVOC, "
	cQuery +=		"PL_DATA, "
	cQuery +=		"RA_FILIAL, "
	cQuery +=		"RA_MAT, "
	cQuery +=		"RA_ADMISSA, "
	cQuery +=		"RA_NOME, "
	cQuery +=		"RA_CC, "
	cQuery +=		"RA_CATFUNC, "
	cQuery +=		"RA_SITFOLH, "
	cQuery +=		"RV_DESC, "
	cQuery +=		"RV_TIPOCOD, "
	cQuery +=		"CTT_DESC01 "
	cQuery +=	"FROM " + RetSQLName("SPL") + " SPL "
	cQuery +=	"INNER JOIN " + RetSQLName("SRA") + " SRA ON SRA.RA_FILIAL = SPL.PL_FILIAL AND SRA.RA_MAT = SPL.PL_MAT AND SRA.D_E_L_E_T_ = ? "			// 13
	cQuery +=	"INNER JOIN " + RetSQLName("SRV") + " SRV ON " + FWJoinFilial("SRV", "SPL") + " AND SRV.RV_COD = SPL.PL_PD AND SRV.D_E_L_E_T_ = ? "		// 14
	cQuery +=	"INNER JOIN " + RetSQLName("CTT") + " CTT ON " + FWJoinFilial("CTT", "SPL") + " AND CTT.CTT_CUSTO = SPL.PL_CC AND CTT.D_E_L_E_T_ = ? "	// 15
	cQuery +=	"WHERE SPL.PL_DATA BETWEEN ? AND ? "		// 16 e 17
	cQuery +=		"AND SRA.RA_FILIAL BETWEEN ? AND ? "	// 18 e 19
	cQuery +=		"AND SRA.RA_CC BETWEEN ? AND ? "		// 20 e 21
	cQuery +=		"AND SRA.RA_MAT BETWEEN ? AND ? "		// 22 e 23
	cQuery +=		"AND SPL.D_E_L_E_T_ = ? "				// 24
	cQuery +=	"ORDER BY RA_FILIAL, RA_MAT, PB_PD, PB_CC, PB_TIPO1, PB_TIPO2, PB_CONVOC, PB_INTEGRA ASC"

	cQuery := ChangeQuery(cQuery)

	oStatement := FwExecStatement():New(cQuery)

	For nI := 1 To 2
		oStatement:SetString(nParamOrder++,  " ")				// 1 ou 13
		oStatement:SetString(nParamOrder++,  " ")				// 2 ou 14
		oStatement:SetString(nParamOrder++,  " ")				// 3 ou 15
		oStatement:SetString(nParamOrder++,  cDataDe)			// 4 ou 16
		oStatement:SetString(nParamOrder++,  cDataAte)			// 5 ou 17
		oStatement:SetString(nParamOrder++,  cFilDe)			// 6 ou 18
		oStatement:SetString(nParamOrder++,  cFilAte)			// 7 ou 19
		oStatement:SetString(nParamOrder++,  cCCDe)				// 8 ou 20
		oStatement:SetString(nParamOrder++,  cCCAte)			// 9 ou 21
		oStatement:SetString(nParamOrder++,  cMatDe)			// 10 ou 22
		oStatement:SetString(nParamOrder++,  cMatAte)			// 11 ou 23
		oStatement:SetString(nParamOrder++,  " ")				// 12 ou 24
	Next

	cAliasSPB := oStatement:OpenAlias()
    
	(cAliasSPB)->(dbGoTop())
	cLastKey := If((cAliasSPB)->(!Eof()), (cAliasSPB)->(PB_PD + PB_CC + PB_TIPO2 + PB_CONVOC + PB_INTEGRA + cValToChar(PB_HORAS) + PB_DATA), "***")
	self:ojItems := JsonObject():new()
	
    While (cAliasSPB)->(!Eof())
		dDtAdmis := sToD((cAliasSPB)->RA_ADMISSA)
		dDataSPB := sToD((cAliasSPB)->PB_DATA)
		
		If Empty(self:ojItems["RA_FILIAL"])
			
			cSitFol	:= (cAliasSPB)->(fDesc("SX5", "31" + RA_SITFOLH, "X5_DESCRI", 15, RA_FILIAL))
			cCateg	:= (cAliasSPB)->(fDesc("SX5", "28" + RA_CATFUNC, "X5_DESCRI", 11, RA_FILIAL))
			cDescCC := (cAliasSPB)->(DescCC(RA_CC, RA_FILIAL))
			
			self:ojItems["RA_FILIAL"]	:= (cAliasSPB)->RA_FILIAL
			self:ojItems["RA_MAT"]		:= If("RA_MAT" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasSPB)->RA_MAT), 1, 10), AllTrim((cAliasSPB)->RA_MAT))
			self:ojItems["RA_NOME"]		:= If("RA_NOME" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasSPB)->RA_NOME), 1, 10), AllTrim((cAliasSPB)->RA_NOME))
			self:ojItems["RA_ADMISSA"]	:= (cAliasSPB)->(FwTimeStamp( 5, dDtAdmis, "00:00:00" ))
			self:ojItems["RA_CC"]		:= If("RA_CC" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasSPB)->RA_CC), 1, 10), AllTrim((cAliasSPB)->RA_CC))
			self:ojItems["CCFUNDESC"]	:= If("RA_CC" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(cDescCC), 1, 10), cDescCC)
			self:ojItems["RA_CATFUNC"]	:= If("RA_CATFUNC" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(cCateg), 1, 10), cCateg)
			self:ojItems["RA_SITFOLH"]	:= If("RA_SITFOLH" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(cSitFol), 1, 10), cSitFol)
			self:ojItems["PB_PD"]		:= (cAliasSPB)->PB_PD
			self:ojItems["PB_TIPO1"]	:= (cAliasSPB)->PB_TIPO1
			self:ojItems["PB_HORAS"]	:= (cAliasSPB)->PB_HORAS
			self:ojItems["PB_VALOR"]	:= (cAliasSPB)->PB_VALOR
			self:ojItems["PB_CC"]		:= (cAliasSPB)->PB_CC
			self:ojItems["CCLANDESC"]	:= (cAliasSPB)->CTT_DESC01
			self:ojItems["PB_TIPO2"]	:= (cAliasSPB)->PB_TIPO2
			self:ojItems["PB_INTEGRA"]	:= If((cAliasSPB)->PB_INTEGRA == "1", STR0030, STR0031) // "Sim" - "Não"
			self:ojItems["PB_CONVOC"]	:= (cAliasSPB)->PB_CONVOC
			self:ojItems["PB_DATA"]		:= (cAliasSPB)->(FwTimeStamp( 5, dDataSPB, "00:00:00" ))
			self:ojItems["RV_TIPOCOD"]	:= (cAliasSPB)->RV_TIPOCOD
			self:ojItems["RV_DESC"]		:= (cAliasSPB)->RV_DESC
			self:ojItems["TABELA"]		:= If((cAliasSPB)->TABELA == "SPB", STR0034, STR0035) // "Resultados do Período" - "Histórico de Resultados"
			
		Else
			self:ojItems["PB_HORAS"] := If(cDigHoras == "S" .And. self:ojItems["PB_TIPO1"] == "H", SomaHoras(self:ojItems["PB_HORAS"], (cAliasSPB)->PB_HORAS), self:ojItems["PB_HORAS"] + (cAliasSPB)->PB_HORAS)
			self:ojItems["PB_VALOR"] += (cAliasSPB)->PB_VALOR
		EndIf
		
		cLastKey := (cAliasSPB)->(PB_PD + PB_CC + PB_TIPO2 + PB_CONVOC + PB_INTEGRA + cValToChar(PB_HORAS) + PB_DATA)
		
        (cAliasSPB)->(dbSkip())
		
		If (cAliasSPB)->(Eof()) .Or. !(cAliasSPB)->(PB_PD + PB_CC + PB_TIPO2 + PB_CONVOC + PB_INTEGRA + cValToChar(PB_HORAS) + PB_DATA) == cLastKey
			
			// Verbas de desconto
			If self:ojItems["RV_TIPOCOD"] $ "2|4"
				self:ojItems["PB_HORAS"] := self:ojItems["PB_HORAS"] * (-1)
				self:ojItems["PB_VALOR"] := self:ojItems["PB_VALOR"] * (-1)
			EndIf
			
			If self:ojItems["RV_TIPOCOD"] == "4"
				self:ojItems["RV_TIPOCOD"] := STR0026 // "Base Desconto"
			ElseIf self:ojItems["RV_TIPOCOD"] == "3"
				self:ojItems["RV_TIPOCOD"] := STR0027 // "Base Provento"
			ElseIf self:ojItems["RV_TIPOCOD"] == "2"
				self:ojItems["RV_TIPOCOD"] := STR0028 // "Desconto"
			Else
				self:ojItems["RV_TIPOCOD"] := STR0029 // "Provento"
			EndIf
			
			
			self:oData:appendData(self:ojItems)
			
			self:ojItems := JsonObject():new()
		EndIf
		
    EndDo
    
	(cAliasSPB)->(DBCloseArea())
    
Return self:oData
