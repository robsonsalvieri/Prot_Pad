#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "rh.treports.gper065.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, RIR, RIQ", customTables="SRA", name="Relatório de Outros Benefícios", country="ALL", initialRelease="12.1.2210")
class gper065TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass

method new() class gper065TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) // RH
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) // Relatório de Outros Benefícios
return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type  Metodo
@author Squad Rotinas
@since  Julho 07,2023
/*/
method getDescription() as character class gper065TReportsBusinessObject
return (STR0003) // Emite um Relatório de Outros Benefícios
 
method getData(nPage as numeric, oFilter as object) as object class gper065TReportsBusinessObject
    
    Local cQuery as character    

	Local cWhere		:= ""

	//Variaveis de Acesso do Usuario
	Local cAcessaSRA	:= &( " { || " + ChkRH( "GPER065" , "SRA" , "2" ) + " } " )
	Local cAcessaRIQ	:= ""

	//Variáveis com o nome da Filial e Grupo de Empresa
	Local cFilExec := AllTrim(FWFilialName())
	Local cEmpExec := AllTrim(FWEmpName(cEmpAnt))

    Local jParams       := oFilter:getParameters() as JSON
    Local cProcesso     := jParams["processo"][1]
    Local cPeriodo      := jParams["periodo"][1]
    Local cNroPag       := jParams["nropag"][1]
    Local aSituacao     := jParams["situacao"]
    Local aCategoria    := jParams["categoria"]
    Local cCCT    		:= jParams["cct"][1]
    Local cFil          := jParams["filial"][1]
    Local cCC           := jParams["centroCusto"][1]
    Local cMatricula    := jParams["matricula"][1]
    Local cDepto    	:= jParams["departamento"][1]
    Local cTipoBen    	:= jParams["tipoBeneficio"][1]
	Local cRoteiro     	:= fGetCalcRot('I')

    Local lTemCCT 	    := fChkCCT()

	Local cTpBen 		:= ""
	Local cDescTpBen 	:= ""
	Local cCodBen 		:= ""
	Local cDesCdBen 	:= ""
	Local nVlBase 		:= 0
	Local nVlBenef 		:= 0
	Local nVlDesc 		:= 0
	Local nVlEmp 		:= 0
	Local cConvT		:= ""

    Local cFields       := ""          as Character
    Local cValidFil     := fValidFil() as Character
    Local nParamOrder   := 1           as Numeric
    Local nSituacao     := 0           as Numeric
    Local nCategoria    := 0           as Numeric

    Local lObfuscated   := .F.
    Local nFields       := 1
    Local aPDFields     := {}
    Local aAllFields    := {}
    Local aPDFieldsNR   := {}
    Local aFieldsNR     := {}
    Local jData         := NIL
    Local jLGPD         := JSONObject():New()
    Local cCustomFields := ""
    Local cName         := ""
    Local cFiltro       := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

    Private cAlias
	Private lPerFech    := .T.
	Private cAliasRel  	:= "RIQ"

	fPerBene(cProcesso, cPeriodo, cNroPag, cRoteiro)

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

	// Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {cAliasRel + "_TPBENE", cAliasRel + "_COD", cAliasRel + "_VALCAL", cAliasRel + "_VALBEN", cAliasRel + "_VLRFUN", cAliasRel + "_VLREMP", cAliasRel + "_CODCCT", "RIS_DESC"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

	If lPerFech 
		cAcessaRIQ := &( " { || " + ChkRH( "GPER065" , "RIR" , "2" ) + " } " )
	Else
		cAcessaRIQ := &( " { || " + ChkRH( "GPER065" , "RIQ" , "2" ) + " } " )
	EndIf

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nSituacao := 1 To Len(aSituacao)
        aSituacao[nSituacao] := IIf(aSituacao[nSituacao] == NIL .or. Empty(aSituacao[nSituacao]), " ", aSituacao[nSituacao])
    Next nSituacao

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nCategoria := 1 To Len(aCategoria)
        aCategoria[nCategoria] := IIf(aCategoria[nCategoria] == NIL .or. Empty(aCategoria[nCategoria]), " ", aCategoria[nCategoria])
    Next nCategoria

	If !Empty(cFil)
		cWhere += " SRA.RA_FILIAL = ? AND " // 3
	EndIf

	If !Empty(cProcesso)
		cWhere += " SRA.RA_PROCES = ? AND " // 4
	Endif

	If !Empty(cCC)
		cWhere += " SRA.RA_CC = ? AND " // 5
	Endif

	If !Empty(cMatricula)
		cWhere += " SRA.RA_MAT = ? AND " // 6
	EndIf

	If !Empty(cDepto)
		cWhere += " SRA.RA_DEPTO = ? AND " // 7
	Endif

	If Len(aSituacao) > 0
		cWhere += " SRA.RA_SITFOLH IN (?) AND " // 8
	EndIf
	
	If Len(aCategoria) > 0
		cWhere += " SRA.RA_CATFUNC IN (?) AND " // 9
	EndIf

	If !Empty(cPeriodo)
		cWhere += cAliasRel+"."+cAliasRel+"_PERIOD = ? AND " // 10
	Endif

	If !Empty(cNroPag)
		cWhere += cAliasRel+"."+cAliasRel+"_NUMPAG = ? AND " // 11
	Endif

	If !Empty(cCCT) .And. lTemCCT
		cWhere += cAliasRel+"."+cAliasRel+"_CODCCT = ? AND " // 12
	Endif

    // Monta os campos que serão buscados na query
    cFields += "SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_CC, SRA.RA_DEPTO, SRA.RA_PROCES, "
    cFields += cAliasRel + "." + cAliasRel + "_TPBENE TPBENE, " + cAliasRel + "." + cAliasRel + "_COD COD, " + cAliasRel + "." + cAliasRel + "_VALCAL VALCAL, "
    cFields += cAliasRel + "." + cAliasRel + "_VALBEN VALBEN, " + cAliasRel + "." + cAliasRel + "_VLRFUN VLRFUN, " + cAliasRel + "." + cAliasRel + "_VLREMP VLREMP "
    cFields += IIf(lTemCCT, ", " + cAliasRel + "." + cAliasRel + "_CODCCT CODCCT ", "")
    cFields += " ? " // 1

	cQuery += " SELECT "
	cQuery +=     cFields
	cQuery += " FROM " + RetSqlName("SRA") + " SRA "
	cQuery += " INNER JOIN " + RetSqlName(cAliasRel) + " " + cAliasRel + " "
	cQuery +=       " ON "
	cQuery +=       cAliasRel + "." + cAliasRel + "_FILIAL = SRA.RA_FILIAL"
	cQuery +=       " AND " + cAliasRel + "." + cAliasRel + "_PROCES = SRA.RA_PROCES"
    cQuery +=       " AND " + cAliasRel + "." + cAliasRel + "_MAT = SRA.RA_MAT"
	cQuery +=       " AND " + cAliasRel + ".D_E_L_E_T_ = ? " // 2
	cQuery += " WHERE " + cWhere
	cQuery +=        " SRA.D_E_L_E_T_ = ? " // 13
	cQuery +=        " ? " // 14
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

	// Captura e define os campos personalizados das tabelas SRA
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    // Define o valor dos bind parameters
	oStatement:SetString(nParamOrder++, " ") // 2

	If !Empty(cFil)
		oStatement:SetString(nParamOrder++, cFil) // 3
	EndIf

	If !Empty(cProcesso)
		oStatement:SetString(nParamOrder++, cProcesso) // 4
	Endif

	If !Empty(cCC)
		oStatement:SetString(nParamOrder++, cCC) // 5
	Endif

	If !Empty(cMatricula)
		oStatement:SetString(nParamOrder++, cMatricula) // 6
	EndIf

	If !Empty(cDepto)
		oStatement:SetString(nParamOrder++, cDepto) // 7
	Endif
    If Len(aSituacao) > 0
		oStatement:SetIN(nParamOrder++, aSituacao) // 8
	EndIf

	If Len(aCategoria) > 0
		oStatement:SetIN(nParamOrder++, aCategoria) // 9
	EndIf

	If !Empty(cPeriodo)
		oStatement:SetString(nParamOrder++, cPeriodo) // 10
	Endif

	If !Empty(cNroPag)
		oStatement:SetString(nParamOrder++, cNroPag) // 11
	Endif

	If !Empty(cCCT) .And. lTemCCT
		oStatement:SetString(nParamOrder++, cCCT) // 12
	Endif

	oStatement:SetString(nParamOrder++, " ") // 13
	oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cFiltro), Replace(Replace(cFiltro, "RIQ", cAliasRel), "RIR", cAliasRel), "")) // 14

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

	While !(cAlias)->(Eof())
		//Consiste Filiais e Acessos
		If !((cAlias)->RA_FILIAL $ cValidFil) .Or. !Eval(cAcessaSRA) .Or. !Eval(cAcessaRIQ)
			(cAlias)->( dbSkip() )
			Loop
		EndIf

		If !Empty(cTipoBen) .and. !((cAlias)->TPBENE $ cTipoBen)
			(cAlias)->( dbSkip() )
			Loop
		EndIf

		cTpBen 		:= (cAlias)->TPBENE
		cDescTpBen	:= fDescRCC("S011",(cAlias)->TPBENE,1,2,3,30)
		cCodBen		:= (cAlias)->COD
		cDesCdBen	:= FDESC('RIS',(cAlias)->TPBENE+(cAlias)->COD,'RIS_DESC')
		nVlBase		:= (cAlias)->VALCAL
		nVlBenef	:= (cAlias)->VALBEN
		nVlDesc		:= (cAlias)->VLRFUN
		nVlEmp		:= (cAlias)->VLREMP
		If lTemCCT
			cConvT		:= (cAlias)->CODCCT
		Endif

		jData := {;
			"RA_FILIAL":   Trim(IIf(jLGPD["RA_FILIAL"],           FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL), (cAlias)->RA_FILIAL)),;
			"RA_CC":       Trim(IIf(jLGPD["RA_CC"],               FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),     (cAlias)->RA_CC)),;
			"RA_MAT":      Trim(IIf(jLGPD["RA_MAT"],              FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),    (cAlias)->RA_MAT)),;
			"RA_NOME":     Trim(IIf(jLGPD["RA_NOME"],             FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),   (cAlias)->RA_NOME)),;
			"RA_PROCES":   Trim(IIf(jLGPD["RA_PROCES"],           FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_PROCES), (cAlias)->RA_PROCES)),;
			"RA_DEPTO":    Trim(IIf(jLGPD["RA_DEPTO"],            FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_DEPTO),  (cAlias)->RA_DEPTO)),;
			"DESCCODBEN":  Trim(IIf(jLGPD["RIS_DESC"],            FwProtectedDataUtil():ValueAsteriskToAnonymize(cDesCdBen),           cDesCdBen)),;
			"TPBENE":      Trim(IIf(jLGPD[cAliasRel + "_TPBENE"], FwProtectedDataUtil():ValueAsteriskToAnonymize(cTpBen),              cTpBen)),;
			"DESCTPBENE":  Trim(IIf(jLGPD[cAliasRel + "_TPBENE"], FwProtectedDataUtil():ValueAsteriskToAnonymize(cDescTpBen),          cDescTpBen)),;
			"CODBEN":      Trim(IIf(jLGPD[cAliasRel + "_COD"],    FwProtectedDataUtil():ValueAsteriskToAnonymize(cCodBen),             cCodBen)),;
			"cCCT":        Trim(IIf(jLGPD[cAliasRel + "_CODCCT"], FwProtectedDataUtil():ValueAsteriskToAnonymize(cConvT),              cConvT)),;
			"VAL_BASE":    IIf(jLGPD[cAliasRel + "_VALCAL"], 0, nVlBase),;
			"VAL_BENEF":   IIf(jLGPD[cAliasRel + "_VALBEN"], 0, nVlBenef),;
			"VAL_FUNC":    IIf(jLGPD[cAliasRel + "_VLRFUN"], 0, nVlDesc),;
			"VAL_EMPRESA": IIf(jLGPD[cAliasRel + "_VLREMP"], 0, nVlEmp),;
			"nTemCCT":     Iif(lTemCCT, 1, 0),;
			"FilialExec":  cFilExec,;
			"EmpresaExec": cEmpExec;
		}

		// Adiciona os campos customizados no JSON
		GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

		// Adiciona as informações da linha
		self:oData:appendData(jData)

		(cAlias)->(DbSkip())
	EndDo

	(cAlias)->(DbCloseArea())

    // Limpa os vetores e objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeArray(aAllFields)
    FwFreeArray(aPDFields)
	FwFreeArray(aPDFieldsNR)
	FwFreeArray(aFieldsNR)
return self:oData
 
method getSchema() as object class gper065TReportsBusinessObject
    Local aFieldsSRA    as Array     // Campos que serão utilizados da tabela SRA
    Local nFieldSize    as Numeric   // Tamanho do campo RCB_CAMPOS
    Local cRCBBranch    as Character // Filial da RCB
    Local cCodeSizeS011 as Character // Tamanho do código da tabela S011
    Local cDescSizeS011 as Character // Tamanho do código da tabela S011

    // Inicialização das variáveis
    aFieldsSRA    := {"RA_FILIAL", "RA_NOME", "RA_CC"}
    nFieldSize    := GetSX3Cache("RCB_CAMPOS", "X3_TAMANHO")
    cRCBBranch    := FwXFilial("RCB")
    cCodeSizeS011 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("BRA_S01101", nFieldSize) + "S011", "RCB_TAMAN"))
    cDescSizeS011 := CValToChar(Posicione("RCB", 3, cRCBBranch + PadR("BRA_S01102", nFieldSize) + "S011", "RCB_TAMAN"))

    self:oSchema:aliasToSchema("SRA", aFieldsSRA)

	self:oSchema:addProperty("RA_MAT",      STR0004, "string", STR0004, "RA_MAT")
	self:oSchema:addProperty("RA_DEPTO",    STR0005, "string", STR0005, "RA_DEPTO")
	self:oSchema:addProperty("RA_PROCES",   STR0006, "string", STR0006, "RA_PROCES")
	self:oSchema:addProperty("TPBENE",      STR0007, "string", STR0007, "TPBENE")
	self:oSchema:addProperty("DESCTPBENE",  STR0008, "string", STR0008, "DESCTPBENE")
	self:oSchema:addProperty("CODBEN",      STR0009, "string", STR0009, "CODBEN")
	self:oSchema:addProperty("DESCCODBEN",  STR0010, "string", STR0011, "DESCCODBEN")
	self:oSchema:addProperty("VAL_BASE",    STR0012, "number", STR0012, "VAL_BASE")
	self:oSchema:addProperty("VAL_BENEF",   STR0013, "number", STR0014, "VAL_BENEF")
	self:oSchema:addProperty("VAL_FUNC",    STR0015, "number", STR0015, "VAL_FUNC")
	self:oSchema:addProperty("VAL_EMPRESA", STR0016, "number", STR0016, "VAL_EMPRESA")

    self:oSchema:addProperty("FilialExec",  STR0017, "string", STR0018, "FilialExec")
    self:oSchema:addProperty("EmpresaExec", STR0019, "string", STR0020, "EmpresaExec")
    self:oSchema:addProperty("cCCT",        STR0021, "string", STR0021, "cCCT")
    self:oSchema:addProperty("nTemCCT",     STR0022, "number", STR0022, "nTemCCT")

    // Parâmetros
    self:oSchema:addParameter("processo",      STR0023 + " ?",      "string", .F.)
    self:oSchema:addParameter("periodo",       STR0024 + " ?",      "string", .F.)
    self:oSchema:addParameter("nropag",        STR0025 + " ?",      "string", .F.)
    self:oSchema:addParameter("situacao",      STR0026 + " ?",      "string", .T.)
    self:oSchema:addParameter("categoria",     STR0027 + " ?",      "string", .T.)
    self:oSchema:addParameter("filial",        STR0028 + " ?",      "string", .F.)
    self:oSchema:addParameter("matricula",     STR0004 + " ?",      "string", .F.)
    self:oSchema:addParameter("centroCusto",   STR0029 + " ?",      "string", .F.)
    self:oSchema:addParameter("cct",           STR0030 + " ?",      "string", .F.)
    self:oSchema:addParameter("departamento",  STR0005 + " ?",      "string", .F.)
    self:oSchema:addParameter("tipoBeneficio", STR0031 + " ?",      "string", .F.)
    self:oSchema:addParameter("totaliza",      STR0032 + " ?",      "string", .F.)

	// Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("processo",      "/api/framework/v1/genericLookupService/smartview/RCJ",                                      2)
    self:setCustomURL("situacao",      "/api/rh/smartview/v1/options/GPEParams/getSX5/31",                                          2)
    self:setCustomURL("categoria",     "/api/rh/smartview/v1/options/GPEParams/getSX5/28",                                          2)
    self:setCustomURL("filial",        "/api/rh/smartview/v1/options/GPEParams/getBranches",                                        2)
    self:setCustomURL("matricula",     "/api/framework/v1/genericLookupService/smartview/SRA",                                      2)
    self:setCustomURL("centroCusto",   "/api/framework/v1/genericLookupService/smartview/CTT",                                      2)
    self:setCustomURL("cct",           "/api/framework/v1/genericLookupService/smartview/SWY",                                      2)
    self:setCustomURL("departamento",  "/api/framework/v1/genericLookupService/smartview/SQB",                                      2)
    self:setCustomURL("periodo",       "/api/framework/v1/genericLookupService/smartview/RCH08",                                    2)
    self:setCustomURL("tipoBeneficio", "/api/rh/smartview/v1/options/GPEParams/getRCC/S011/" + cCodeSizeS011 + "/" + cDescSizeS011, 2)
    self:setCustomURL("totaliza",      "/api/rh/smartview/v1/options/GPER065",                                                      1)
return self:oSchema

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ fPerBene º Autor ³ Leandro Drumond    º Data ³ 28/02/2014  º±±
±±ÃÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Verifica periodo do beneficio.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ GPER009                                                    º±±
±±ÀÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fPerBene(cBeneProc, cBenePer, cBeneNmpg, cBeneRot)
	Local cFilRCH	   := xFilial("RCH")

	Default cBeneProc	:= ""
	Default cBenePer	:= ""
	Default cBeneNmpg	:= ""
	Default cBeneRot	:= ""

	DbSelectArea("RCH")
	RCH->( dbsetOrder( Retorder( "RCH" , "RCH_FILIAL+RCH_PROCES+RCH_PER+RCH_NUMPAG+RCH_ROTEIR" ) ) )
	RCH->( dbSeek( cFilRCH + cBeneProc + cBenePer + cBeneNmPg + cBeneRot, .F. ) )
	If RCH->( !Eof() )
		If !Empty(RCH->RCH_DTFECH)
		    cAliasRel := "RIR"
		    lPerFech  := .T.
		Else //periodo atual
		    cAliasRel   := "RIQ"
		    lPerFech  := .F.
		EndIf
	EndIf 

Return


/*/{Protheus.doc} fChkCCT
//Verifica se campos da CCT existem
@author Leandro Drumond
@since 04/07/2022
/*/
Static Function fChkCCT()
Return RIQ->(ColumnPos("RIQ_CODCCT")) > 0
