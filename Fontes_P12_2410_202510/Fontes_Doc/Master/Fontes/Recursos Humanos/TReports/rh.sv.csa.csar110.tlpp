#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.csa.csar110.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} CSAR110TReportsBusinessObject:new()
	Classe do objeto de negócio do relatório de funcionários (CSAR110).
	@type Class
	@version 12.1.2310
	@author emerson.grassi
	@since 12/09/2025
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACSA", tables="SRA, CTT, SRJ, SQ3, SQB", customTables="SRA, CTT, SRJ, SQ3, SQB", name="Rel. Funcionários - Cargo Detalhado", country="ALL", initialRelease="12.1.2210")
Class CSAR110TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
	// Define os métodos da classe
	Public Method new()            As Object
	Public Method getDescription() As Character
	Public Method getData()        As Object
	Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} CSAR110TReportsBusinessObject:new()
	Instancia a classe do objeto de negócio.
	@type Method
	@version 12.1.2210
	@author emerson.grassi
	@since 31/10/2023
	@return Object, Instância do objeto de negócio
/*/
Method new() As Object Class CSAR110TReportsBusinessObject
	_Super:new()

	//Define a Área
	self:appendArea(STR0002) // "RH"

	// Define o nome do objeto de negócio
	self:setDisplayName(STR0003) // "Relatório de Funcionários - Cargo Detalhado"

Return self

/*/{Protheus.doc} CSAR110TReportsBusinessObject:getDescription()
	Retorna a descrição do objeto de negócio.
	@type Method
	@version 12.1.2310
	@author emerson.grassi
	@since 31/10/2023
	@return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class CSAR110TReportsBusinessObject
Return STR0001 // "Este objeto de negócio emite a Impressão do Relatório de Funcionários - Cargo Detalhado."

/*/{Protheus.doc} CSAR110TReportsBusinessObject:getData()
	Captura os dados do objeto de negócio.
	@type Method
	@version 12.1.2310
	@author emerson.grassi
	@since 31/10/2023
	@return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class CSAR110TReportsBusinessObject
	// Declaração das variáveis locais
    Local aCategorias   	   as Array     // Categorias
	Local aSituacoes           as Array     // Situações
	Local aPDFields            as Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR          as Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aAllFields           as Array     // Estrutura de todos os campos do ON
    Local aFieldsNR            as Array     // Campos com nome diferente do real
	Local cAlias               as Character // Alias do arquivo temporário
	Local cJoinCTT             as Character // Join da Query
	Local cJoinSQ3             as Character // Join da Query
	Local cJoinSQB             as Character // Join da Query
	Local cJoinSRJ             as Character // Join da Query
	Local cQuery               as Character // Query a ser executada
    Local cCustomFields        as Character // Campos customizados no formato SQL
    Local cName                as Character // Nome real do campo
    Local cFiltro              as Character // Filtro do objeto de negócio
	Local jParams              as JSON      // Parâmetros do relatório
    Local jData                as JSON      // Informações a serem adicionadas no ON
    Local jLGPD                as JSON      // Indica quais campos devem ser ofuscados
	Local nParamOrder          as Numeric   // Contador de Parâmetros
    Local nFields              as Numeric   // Contador de campos
    Local nAux                 as Numeric   // Contador auxiliar
    Local oStatement           as Object    // Objeto usada para utilização da classe FwExecStatement
    Local lObfuscated          as Logical   // Flag que indica se tem algum campo para ofuscar
	Local cQ3DescDet		   as Character // Campo memo do SQ3

    Local cFilDe        	   as Character // Parâmetro Filial De
    Local cFilAte       	   as Character // Parâmetro Filial Ate
    Local cMatDe        	   as Character // Parâmetro Matrícula De
    Local cMatAte       	   as Character // Parâmetro Matrícula Até
    Local cNomeDe       	   as Character // Parâmetro Nome De
    Local cNomeAte      	   as Character // Parâmetro Nome Até
    Local cCargoDe      	   as Character // Parâmetro Cargo De
    Local cCargoAte     	   as Character // Parâmetro Cargo Até

	// Inicialização das variáveis
    cFilDe               := ""
    cFilAte              := ""
    cMatDe               := ""
    cMatAte              := ""
    cNomeDe              := ""
    cNomeAte             := ""
    cCargoDe             := ""
    cCargoAte            := ""
    aCategorias   		 := {}
    aSituacoes           := {}
    aPDFields            := {}
    aPDFieldsNR          := {}
    aAllFields           := {}
    aFieldsNR            := {}
	cAlias               := GetNextAlias()
	cJoinCTT             := ""
	cJoinSQ3             := ""
	cJoinSQB             := ""
	cJoinSRJ             := ""
	cQuery               := ""
    cCustomFields        := ""
    cName                := ""
    cFiltro              := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
	jParams              := oFilter:getParameters()
    jData                := NIL
    jLGPD                := JSONObject():New()
    oStatement    		 := NIL
	nParamOrder          := 1
    nFields              := 1
    lObfuscated          := .F.

	// Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RA_NOME", "RA_SALARIO", "RJ_DESFUNC", "Q3_DESC_CARGO", "Q3_DESCDET", "QB_DESC_DEPTO", "CTT_CC"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cFilDe      := IIf(jParams:hasProperty("PAR_FILIALDE") .and. Len(jParams["PAR_FILIALDE"]) > 0 .and.!Empty(jParams["PAR_FILIALDE"][1]), jParams["PAR_FILIALDE"][1]	, "")
        cFilAte     := IIf(jParams:hasProperty("PAR_FILIALATE").and. Len(jParams["PAR_FILIALATE"])> 0 .and.!Empty(jParams["PAR_FILIALATE"][1]),jParams["PAR_FILIALATE"][1]	, Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cMatDe      := IIf(jParams:hasProperty("PAR_MATDE")    .and. Len(jParams["PAR_MATDE"])    > 0 .and.!Empty(jParams["PAR_MATDE"][1]),    jParams["PAR_MATDE"][1]		, "")
        cMatAte     := IIf(jParams:hasProperty("PAR_MATATE")   .and. Len(jParams["PAR_MATATE"])   > 0 .and.!Empty(jParams["PAR_MATATE"][1]),   jParams["PAR_MATATE"][1]		, Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))           
        cNomeDe     := IIf(jParams:hasProperty("PAR_NOMEDE")   .and. Len(jParams["PAR_NOMEDE"])   > 0 .and.!Empty(jParams["PAR_NOMEDE"][1]),   jParams["PAR_NOMEDE"][1]		, "")
        cNomeAte    := IIf(jParams:hasProperty("PAR_NOMEATE")  .and. Len(jParams["PAR_NOMEATE"])  > 0 .and.!Empty(jParams["PAR_NOMEATE"][1]),  jParams["PAR_NOMEATE"][1]	, Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
        cCargoDe    := IIf(jParams:hasProperty("PAR_CARDE")    .and. Len(jParams["PAR_CARDE"])    > 0 .and.!Empty(jParams["PAR_CARDE"][1]),    jParams["PAR_CARDE"][1]		, "")
        cCargoAte   := IIf(jParams:hasProperty("PAR_CARATE")   .and. Len(jParams["PAR_CARATE"])   > 0 .and.!Empty(jParams["PAR_CARATE"][1]),   jParams["PAR_CARATE"][1]		, Replicate("Z", GetSX3Cache("RA_CARGO", "X3_TAMANHO")))
        aSituacoes  := IIf(jParams:hasProperty("PAR_SIT"),                                                                                     jParams["PAR_SIT"]			, {})
        aCategorias := IIf(jParams:hasProperty("PAR_CAT"), 																					   jParams["PAR_CAT"]			, {})
    EndIf               

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aSituacoes)
        aSituacoes[nAux] := IIf(aSituacoes[nAux] == NIL .or. Empty(aSituacoes[nAux]), " ", aSituacoes[nAux])
    Next nAux

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aCategorias)
        aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
    Next nAux         

	cJoinCTT  :=  FWJoinFilial("SRA", "CTT")
	cJoinSRJ  :=  FWJoinFilial("SRA", "SRJ")
	cJoinSQ3  :=  FWJoinFilial("SRA", "SQ3")
	cJoinSQB  :=  FWJoinFilial("SRA", "SQB")

	// Montagem da query de busca
	cQuery := " SELECT RA_FILIAL, RA_MAT, RA_NOME, "
	cQuery += " RA_CC, CTT_DESC01 as CTT_CC, RA_TIPOPGT, "
	cQuery += " RA_CODFUNC, SRJ.RJ_DESC AS RJ_DESFUNC, SRJ.RJ_CODCBO, "
	cQuery += " RA_SITFOLH, RA_CATFUNC, RA_SALARIO, "
	cQuery += " RA_CARGO, Q3_DESCSUM AS Q3_DESC_CARGO, Q3_DESCDET, "
    cQuery += " RA_DEPTO, QB_DESCRIC AS QB_DESC_DEPTO, "
	cQuery += " SRA.R_E_C_N_O_ REGSRA "
	cQuery += " ? " 														// 1
	cQuery += " FROM " + retsqlname("SRA") + " SRA "
	cQuery += " LEFT JOIN " + retsqlname("CTT") + " CTT ON "
	cQuery +=       cJoinCTT
	cQuery +=       " AND CTT.CTT_CUSTO = SRA.RA_CC "
	cQuery +=       " AND CTT.D_E_L_E_T_ = ? " 								// 2
	cQuery += " LEFT JOIN " + retsqlname("SRJ") + " SRJ ON "
	cQuery +=       cJoinSRJ
	cQuery +=       " AND SRJ.RJ_FUNCAO = SRA.RA_CODFUNC "
	cQuery +=       " AND SRJ.D_E_L_E_T_ = ? " 								// 3
	cQuery += " LEFT JOIN " + retsqlname("SQ3") + " SQ3 ON "
	cQuery +=       cJoinSQ3
	cQuery +=       " AND SQ3.Q3_CARGO = SRA.RA_CARGO "
	cQuery +=       " AND (SQ3.Q3_CC = SRA.RA_CC OR Q3_CC = ?) " 			// 4
	cQuery +=       " AND SQ3.D_E_L_E_T_ = ? "                   			// 5
	cQuery += " LEFT JOIN " + retsqlname("SQB") + " SQB ON "
	cQuery +=       cJoinSQB
	cQuery +=       " AND SQB.QB_DEPTO = SRA.RA_DEPTO "
	cQuery +=       " AND SQB.D_E_L_E_T_ = ? " 								// 6
    cQuery += "WHERE "
    cQuery +=     "RA_FILIAL BETWEEN ? AND ? "                              // 7 e 8
    cQuery +=     "AND RA_MAT BETWEEN ? AND ? "                             // 9 e 10
    cQuery +=     "AND RA_NOME BETWEEN ? AND ? "                            // 11 e 12
	cQuery +=     "AND RA_CARGO BETWEEN ? AND ? "                           // 13 e 14
    cQuery +=     IIf(Len(aSituacoes) > 0, " AND RA_SITFOLH IN (?) ", " ")  // 15
    cQuery +=     IIf(Len(aCategorias) > 0, " AND RA_CATFUNC IN (?) ", " ") // 16
    cQuery +=     "AND SRA.D_E_L_E_T_ = ? "                                 // 17
    cQuery +=     "? "                                                      // 18
	cQuery +=     " ORDER BY SRA.RA_FILIAL, SRA.RA_MAT"
	cQuery := ChangeQuery(cQuery)
	oStatement := FwExecStatement():New(cQuery)

    // Captura e define os campos personalizados das tabelas SRA, CTT, SRJ, SQ3, SQB
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "CTT", "SRJ", "SQ3", "SQB"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    // Define o valor dos bind parameters
	oStatement:SetString(nParamOrder++, " ")       		// 2
	oStatement:SetString(nParamOrder++, " ")       		// 3
	oStatement:SetString(nParamOrder++, " ")       		// 4
	oStatement:SetString(nParamOrder++, " ")       		// 5
	oStatement:SetString(nParamOrder++, " ")       		// 6
	oStatement:SetString(nParamOrder++, cFilDe)    		// 7
	oStatement:SetString(nParamOrder++, cFilAte)   		// 8
	oStatement:SetString(nParamOrder++, cMatDe)    		// 9
	oStatement:SetString(nParamOrder++, cMatAte)   		// 10
	oStatement:SetString(nParamOrder++, cNomeDe)  		// 11
	oStatement:SetString(nParamOrder++, cNomeAte) 		// 12
	oStatement:SetString(nParamOrder++, cCargoDe) 		// 13
	oStatement:SetString(nParamOrder++, cCargoAte) 		// 14

	If (Len(aSituacoes) > 0)
		oStatement:SetIn(nParamOrder++, aSituacoes) 	// 15
	EndIf     

	If (Len(aCategorias) > 0)
		oStatement:SetIn(nParamOrder++, aCategorias) 	// 16
	EndIf

	oStatement:SetString(nParamOrder++, " ")     		// 17
	oStatement:SetUnsafe(nParamOrder++, cFiltro) 		// 18

	cAlias := oStatement:OpenAlias()

	dBSelectArea("SRA")
	dbSetOrder(1)

	// Percorre todos os registros
	While (!(cAlias)->(EOF()))

		SRA->( DbGoTo((cAlias)->REGSRA) )
    
		//Campo memo
		cQ3DescDet := ""
		If !Empty((cAlias)->Q3_DESCDET)
			cQ3DescDet := Alltrim(MSMM((cAlias)->Q3_DESCDET,  ,,,3,,,,,"RDY"))
		endIf

		// Adiciona as informações da linha
		jData := {;
            "OriginPlatform" : "Protheus",;
            "RA_FILIAL"      : Trim(IIf(jLGPD["RA_FILIAL"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),       (cAlias)->RA_FILIAL)),;
            "RA_MAT"         : Trim(IIf(jLGPD["RA_MAT"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),          (cAlias)->RA_MAT)),;
            "RA_NOME"        : Trim(IIf(jLGPD["RA_NOME"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),         (cAlias)->RA_NOME)),;
			"RA_TIPOPGT"     : Trim(IIf(jLGPD["RA_TIPOPGT"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_TIPOPGT),     (cAlias)->RA_TIPOPGT)),;
            "RA_SITFOLH"     : Trim(IIf(jLGPD["RA_SITFOLH"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_SITFOLH),      (cAlias)->RA_SITFOLH)),;
            "RA_CODFUNC"     : Trim(IIf(jLGPD["RA_CODFUNC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CODFUNC),      (cAlias)->RA_CODFUNC)),;
            "RJ_DESFUNC"     : Trim(IIf(jLGPD["RJ_DESFUNC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RJ_DESFUNC),      (cAlias)->RJ_DESFUNC)),;
            "RJ_CODCBO"      : Trim(IIf(jLGPD["RJ_CODCBO"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RJ_CODCBO),       (cAlias)->RJ_CODCBO)),;
            "RA_CATFUNC"     : Trim(IIf(jLGPD["RA_CATFUNC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CATFUNC),      (cAlias)->RA_CATFUNC)),;
            "RA_SALARIO"     : Trim(IIf(jLGPD["RA_SALARIO"],  FwProtectedDataUtil():ValueAsteriskToAnonymize(Str((cAlias)->RA_SALARIO)), AllTrim(Str((cAlias)->RA_SALARIO)))),;
            "RA_CARGO"       : Trim(IIf(jLGPD["RA_CARGO"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CARGO),        (cAlias)->RA_CARGO)),;
            "Q3_DESC_CARGO"  : Trim(IIf(jLGPD["Q3_DESCSUM"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->Q3_DESC_CARGO),   (cAlias)->Q3_DESC_CARGO)),;
            "Q3_DESCDET"     : Trim(IIf(jLGPD["cQ3DescDet"],  FwProtectedDataUtil():ValueAsteriskToAnonymize(cQ3DescDet),                 cQ3DescDet)),;
            "RA_DEPTO"       : Trim(IIf(jLGPD["RA_DEPTO"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_DEPTO),        (cAlias)->RA_DEPTO)),;
            "QB_DESC_DEPTO"  : Trim(IIf(jLGPD["QB_DESCRIC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->QB_DESC_DEPTO),   (cAlias)->QB_DESC_DEPTO)),;
            "RA_CC"          : Trim(IIf(jLGPD["RA_CC"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),           (cAlias)->RA_CC)),;
            "CTT_CC"         : Trim(IIf(jLGPD["CTT_DESC01"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_CC),          (cAlias)->CTT_CC));
		}

        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

		// Pula para o próximo registro
		(cAlias)->(DBSkip())
	End

	SRA->(DbCloseArea())

    // Fecha a área da tabela temporária
    If !Empty(cAlias)
        (cAlias)->(DBCloseArea())
    Endif

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
	FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeArray(aCategorias)
	FwFreeArray(aSituacoes)
    FwFreeArray(aAllFields)
    FwFreeArray(aPDFields)
    FwFreeArray(aPDFieldsNR)
    FwFreeArray(aFieldsNR)
Return self:oData

/*/{Protheus.doc} CSAR110TReportsBusinessObject:getSchema()
	Define o schema do objeto de negócio.
	@type Method
	@version 12.1.2310
	@author emerson.grassi
	@since 31/10/2023
	@return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class CSAR110TReportsBusinessObject

	// Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_FILIALDE",    STR0021,    "string", .F.) // "Filial De"
    self:oSchema:addParameter("PAR_FILIALATE",   STR0022,    "string", .F.) // "Filial Até"
    self:oSchema:addParameter("PAR_MATDE",       STR0023,    "string", .F.) // "Matrícula De"
    self:oSchema:addParameter("PAR_MATATE",      STR0024,    "string", .F.) // "Matrícula Até"
    self:oSchema:addParameter("PAR_NOMEDE",      STR0025,    "string", .F.) // "Nome Funcionário De"
    self:oSchema:addParameter("PAR_NOMEATE",     STR0026,    "string", .F.) // "Nome Funcionário Até"
	self:oSchema:addParameter("PAR_CARDE",       STR0029,    "string", .F.) // "Cargo. De"
    self:oSchema:addParameter("PAR_CARATE",      STR0030,    "string", .F.) // "Cargo. Até"
    self:oSchema:addParameter("PAR_SIT",         STR0027,    "string", .T.) // "Situações"
    self:oSchema:addParameter("PAR_CAT",         STR0028,    "string", .T.) // "Categorias"

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_FILIALDE",      "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("PAR_FILIALATE",     "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("PAR_MATDE",         "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("PAR_MATATE",        "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("PAR_CARDE",         "/api/framework/v1/genericLookupService/smartview/SQ3", 2)
    self:setCustomURL("PAR_CARATE",        "/api/framework/v1/genericLookupService/smartview/SQ3", 2)
    self:setCustomURL("PAR_SIT",           "/api/rh/smartview/v1/options/GPEParams/getSX5/31",     2)
    self:setCustomURL("PAR_CAT",           "/api/rh/smartview/v1/options/GPEParams/getSX5/28",     2)

	// Adiciona as propriedades do objeto de negócio 
	self:oSchema:addProperty("RA_FILIAL",       STR0004,             "string",     STR0004,        "RA_FILIAL")     // "Filial"
	self:oSchema:addProperty("RA_MAT",          STR0005,             "string",     STR0005,        "RA_MAT")        // "Matrícula"
	self:oSchema:addProperty("RA_NOME",         STR0012,             "string",     STR0012,        "RA_NOME")       // "Nome"
	self:oSchema:addProperty("RA_CC",           STR0007,             "string",     STR0007,        "RA_CC")         // "Centro Custo"
	self:oSchema:addProperty("CTT_CC",          STR0014,             "string",     STR0014,        "CTT_CC")        // "Desc. Centro Custo"
	self:oSchema:addProperty("RA_SITFOLH",      STR0008,             "string",     STR0008,        "RA_SITFOLH")    // "Sit. Folha"
	self:oSchema:addProperty("RA_CODFUNC",      STR0016,             "string",     STR0016,        "RA_CODFUNC")    // "Cod. Função"
	self:oSchema:addProperty("RJ_DESFUNC",      STR0017,             "string",     STR0017,        "RJ_DESFUNC")    // "Desc. Função"
	self:oSchema:addProperty("RJ_CODCBO",       STR0018,             "string",     STR0018,        "RJ_CODCBO")     // "CBO"
	self:oSchema:addProperty("RA_CATFUNC",      STR0019,             "string",     STR0019,        "RA_CATFUNC")    // "Cat. Func."
	self:oSchema:addProperty("RA_TIPOPGT",      STR0020,             "string",     STR0020,        "RA_TIPOPGT")    // "Tipo Pgto."
	self:oSchema:addProperty("RA_SALARIO",      STR0013,             "string",     STR0013,        "RA_SALARIO")    // "Salário"
	self:oSchema:addProperty("RA_CARGO",        STR0009,             "string",     STR0009,        "RA_CARGO")      // "Cargo"
	self:oSchema:addProperty("Q3_DESC_CARGO",   STR0015,             "string",     STR0015,        "Q3_DESC_CARGO") // "Desc. Cargo"
	self:oSchema:addProperty("RA_DEPTO",        STR0006,             "string",     STR0006,        "RA_DEPTO")      // "Departamento"
	self:oSchema:addProperty("QB_DESC_DEPTO",   STR0010,             "string",     STR0010,        "QB_DESC_DEPTO") // "Desc. Depto."
	self:oSchema:addProperty("Q3_DESCDET",      STR0011,             "string",     STR0011,        "Q3_DESCDET")    // "Desc. Detalhada"
Return self:oSchema
