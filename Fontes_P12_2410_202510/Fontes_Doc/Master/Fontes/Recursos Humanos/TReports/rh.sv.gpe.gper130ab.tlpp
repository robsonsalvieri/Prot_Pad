#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.sv.gpe.gper130ab.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,SRH,SRF",  customTables="SRA,SRH,SRF", name="Solicitação de Abono de Férias", country="ALL", initialRelease="12.1.2210")
class GPER130ABTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getWherePar() as character
    private method geraREQ() as variant

    @Get("/api/rh/smartview/v1/options/GPER130AB/:param")
    Public Method getComboBox() As Variant
endclass
 
method new() as object class GPER130ABTReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Solicitação de Abono de Férias"
return self
 
method getDescription() as character class GPER130ABTReportsBusinessObject
return STR0003//"Objeto contendo informações de solicitação de abono de férias"
 
method getAreas() as array class GPER130ABTReportsBusinessObject
return {STR0002}//"RH"
 
method getData(nPage as numeric, oFilter as object) as object class GPER130ABTReportsBusinessObject
    local aInfo as array
    local cAlias as character
    local cDescCTT as character
    local cQuery as character
    local cUltFil as character
    local cOrdem as character
    local cDescDep as character
    local cLastFil as character
    local cCodEmp as character
    local cImprDem as character
    local cAcessaSRA := &( " { || " + ChkRH( "GPER130" , "SRA" , "2" ) + " } " ) as character
    local dDtSolAb as date 
    local dDtDe as date 
    local dDtAte as date 
    local nDAbnPec as numeric
    local nDiasAb as numeric
    local nProg as numeric
    local jParams as json
    local nPOrder as numeric
    local lTAE as logical
    local cCustomFields as character // Campos customizados no formato SQL
    local jData as JSON // Informações a serem adicionadas no ON
    local aAllFields as array     // Estrutura de todos os campos do ON

    Private lGp1033 as logical
    
    Static oStatement
    Static __cEmpAnt   := cEmpAnt 
    Static __cWhere    := ""

    jParams    := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    cOrdem     := "SRA.RA_FILIAL, SRA.RA_MAT" //Ordenação 1: Matricula
    cLastFil   := "_cLastFil"
    nDAbnPec   := 15
    nPOrder    := 16
    cImprDem   := "2" // não imprime demitidos por padrão
    aAllFields := self:getStructFields()
    dDtDe      := Date()
    dDtAte     := Date()+30

    //Tratamento para verificar se chamad foi do GPER1033 e se esta enviando para o TAE
    If !Empty(jParams['IMPDEM', 1]) .And. Len(jParams['IMPDEM', 1]) > 1
        lGp1033 := .T.
        lTAE := SubStr(jParams['IMPDEM', 1], 2, 1) == "2"
        jParams['IMPDEM', 1] := SubStr(jParams['IMPDEM', 1], 1, 1)
    EndIf

    cWhere := self:getWherePar(jParams)

    If !Empty(jParams['ORDER', 1])
		cOrdem := Iif(jParams['ORDER', 1] == "1", "SRA.RA_FILIAL, SRA.RA_MAT", Iif(jParams['ORDER', 1] == "2", "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_MAT", ;
                  Iif(jParams['ORDER', 1] == "3", "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_NOME", "SRA.RA_FILIAL, SRA.RA_NOME, SRA.RA_MAT" )))
	EndIf

    If !Empty(jParams['DIASAB', 1])
        If jParams['DIASAB', 1] < 15
            nDAbnPec :=  15
        Else
		    nDAbnPec :=  jParams['DIASAB', 1] 
        ENDIF
    EndIf

    If !Empty(jParams['IMPDEM', 1])
        cImprDem := cValToChar(jParams['IMPDEM', 1])
    EndIf

    If !Empty(jParams['DTFDE', 1]) .And. !Empty(jParams['DTFATE', 1])
        dDtDe  := SToD(StrTran(SubStr(jParams['DTFDE', 1], 1, 10), "-" ))
        dDtAte := SToD(StrTran(SubStr(jParams['DTFATE', 1], 1, 10), "-" ))
    EndIf 

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(__cWhere == cWhere)

        cQuery  :=  " SELECT    SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME,SRA.RA_NOMECMP, SRA.RA_NSOCIAL, "
        cQuery  +=  "           SRA.RA_DEPTO, SRA.RA_CATFUNC, SRA.RA_SITFOLH, SRA.RA_CC, "
        cQuery  +=  "           SRF.RF_DATAINI, SRF.RF_DATINI2, SRF.RF_DATINI3, SRF.RF_DABPRO1, SRF.RF_DABPRO2, "
        cQuery  +=  "           SRF.RF_DABPRO3, SRF.RF_ABOPEC, SRF.RF_TEMABPE, SRF.RF_DATABAS, SRF.RF_DATAFIM, "
        cQuery  +=  "           SRF.RF_DFEPRO1, SRF.RF_DFEPRO2, SRF.RF_DFEPRO3,"
        cQuery  +=  "           SRH.RH_ABOPEC, SRH.RH_DABONPE,SRH.RH_DATABAS, SRH.RH_DBASEAT, SRH.RH_DATAFIM, SRH.RH_DATAINI"
        cQuery  +=  "           ? "
        cQuery  +=  " FROM      " + RetSqlName("SRA") + " SRA"
        cQuery  +=  "           LEFT JOIN " + RetSqlName("SRH") + " SRH ON SRH.RH_FILIAL = SRA.RA_FILIAL AND SRH.RH_MAT = SRA.RA_MAT AND SRH.RH_DABONPE > ? AND SRH.RH_DATAINI >= ? AND SRH.RH_DATAFIM <= ? AND SRH.D_E_L_E_T_ = ? "
        cQuery  +=  "           LEFT JOIN " + RetSqlName("SRF") + " SRF ON SRF.RF_FILIAL = SRA.RA_FILIAL AND SRF.RF_MAT = SRA.RA_MAT AND SRF.RF_STATUS = ? AND ((SRF.RF_DATAINI BETWEEN ? AND ? ) OR (SRF.RF_DATINI2 BETWEEN ? AND ?) OR (SRF.RF_DATINI3 BETWEEN ? AND ?)) AND SRF.RF_TEMABPE = ? AND SRF.D_E_L_E_T_ = ? "
        cQuery  +=  " WHERE     SRA.D_E_L_E_T_ = ?  "
        
        If !Empty(cWhere)
            cQuery += cWhere    
        EndIf

        //Os filtros serão setados na interface do novo TReports
        If oFilter:hasFilter()
            cQuery +=   " AND " + oFilter:getSQLExpression()
        EndIf

        cQuery  += " ORDER BY " + cOrdem

        cQuery     := ChangeQuery(cQuery)
        __cEmptAnt := cEmpAnt 
        __cWhere   := cWhere
        oStatement := FwExecStatement():New(cQuery)
    EndIf

     // Captura e define os campos personalizados das tabelas SRA e SRF
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRF", "SRH"})
    oStatement:SetUnsafe(1, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados

    oStatement:SetString(2, '0')
    oStatement:setDate(3, dDtDe)
    oStatement:setDate(4, dDtAte)
    oStatement:SetString(5, ' ') //SRH.D_E_L_E_T_
    oStatement:SetString(6, '1')
    oStatement:setDate(7, dDtDe)
    oStatement:setDate(8, dDtAte)
    oStatement:setDate(9, dDtDe)
    oStatement:setDate(10, dDtAte)
    oStatement:setDate(11, dDtDe)
    oStatement:setDate(12, dDtAte)
    oStatement:SetString(13, 'S')
    oStatement:SetString(14, ' ') //SRF.D_E_L_E_T_
    oStatement:SetString(15, ' ') //SRA.D_E_L_E_T_
   
    If !("RA_FILIAL" $ jParams['FILDE', 1])
        If !Empty(jParams['FILDE', 1])
            oStatement:SetString(nPOrder++, jParams["FILDE", 1])
        EndIf
        If !Empty(jParams['FILATE', 1])
            oStatement:SetString(nPOrder++, jParams["FILATE", 1])
        EndIf
    EndIf 

    If !("RA_MAT" $ jParams['MATDE', 1])
        If !Empty(jParams['MATDE', 1])
            oStatement:SetString(nPOrder++, jParams["MATDE", 1])
        EndIf
        If !Empty(jParams['MATATE', 1])
            oStatement:SetString(nPOrder++, jParams["MATATE", 1])
        EndIf
    EndIf 

    If !("RA_CC" $ jParams['CCDE', 1])
        If !Empty(jParams['CCDE', 1])
            oStatement:SetString(nPOrder++, jParams["CCDE", 1])
        EndIf
        If !Empty(jParams['CCATE', 1])
            oStatement:SetString(nPOrder++, jParams["CCATE", 1])
        EndIf
    EndIf 

    If !("RA_NOME" $ jParams['NOMEDE', 1])
        If !Empty(jParams['NOMEDE', 1])
            oStatement:SetString(nPOrder++, jParams["NOMEDE", 1])
        EndIf
        If !Empty(jParams['NOMEATE', 1])
            oStatement:SetString(nPOrder++, jParams["NOMEATE", 1])
        EndIf
    EndIf 

    cAlias := oStatement:OpenAlias()

    While !(cAlias)->(Eof())

        //Consiste Filiais e Acessos
        If cLastFil != (cAlias)->RA_FILIAL
            cNameEmp := RTrim( FWEmpName(cCodEmp))
            If !((cAlias)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA) 
                (cAlias)->( dbSkip() )
                Loop
            EndIf
        EndIf

      //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ?
        //? Consiste Situacao do Funcionario                             ?
        //? Inclusao do tratamento para Imprime Demitidos S/N no Brasil. ?
        //? Se nao for Brasil considera-se o param como 2 (Nao imprime)	 ?
        //?ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ?
        If ((cAlias)->RA_SITFOLH $ "D" .AND. cImprDem <> "1") .Or. ;// 1 - Imprime Demitido = Sim
            (cPaisLoc == "BRA" .And. SRA->RA_CATFUNC $ "A|P")
            (cAlias)->(dbSkip(1))
            Loop
        Endif

        If cUltFil != (cAlias)->RA_FILIAL
            cUltFil := (cAlias)->RA_FILIAL
            fInfo(@aInfo, (cAlias)->RA_FILIAL)
        EndIf

        cLastFil := (cAlias)->RA_FILIAL 
        cDescCTT := DescCc((cAlias)->RA_CC, (cAlias)->RA_FILIAL ) 
        cDescDep := RTrim( FDesc("SQB", (cAlias)->RA_DEPTO, "SQB->QB_DESCRIC") ) 

        If !Empty((cAlias)->RH_DATABAS) .And. (cAlias)->RF_DATABAS <= (cAlias)->RH_DATABAS // Se tiver cálculo de férias, não considera a programação
            dDtSolAb := DaySub(STOD((cAlias)->RH_DBASEAT), nDAbnPec)

            If lTAE
                self:geraREQ(cAlias, StoD((cAlias)->RH_DATAINI), StoD((cAlias)->RH_DATAFIM))
            EndIf 
          
            jData := {;
                "BranchCode": (cAlias)->RA_FILIAL,;  // Código da Filial
                "EmployeeCode": (cAlias)->RA_MAT,; // Num da matricula
                "StartAcquisition": totvs.framework.treports.date.stringToTimeStamp((cAlias)->RH_DATABAS),; // Data Base Ferias Inicial
                "EndAcquisition": totvs.framework.treports.date.stringToTimeStamp((cAlias)->RH_DBASEAT),; // Data Base Ferias Final
                "CurrentDate": FwTimeStamp( 5, dDtSolAb, "00:00:00" ),; // Data 
                "BranchCity": RTrim( aInfo[05] ),; // Cidade da filial
                "BranchName": RTrim( aInfo[03] ),; // Nome da filial
                "CashBenefitNumberDays": (cAlias)->RH_DABONPE,; // Dias de abono pecuniário
                "CostCenterCode": (cAlias)->RA_CC,; // Codigo Centro de Custo
                "CostCenterDescription": RTrim( cDescCTT ),; // Descrição Centro de Custo
                "EmployeeName": iIf(!Empty((cAlias)->RA_NSOCIAL), RTRIM((cAlias)->RA_NSOCIAL), RTRIM((cAlias)->RA_NOMECMP)),; // Nome completo do funcionário
                "DepartmentCode": (cAlias)->RA_DEPTO ,;
                "DepartmentName": cDescDep;// Departamento
            }
        
            
            // Adiciona os campos customizados no JSON
            GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, {})

            // Adiciona as informações da linha
            self:oData:appendData(jData)

            (cAlias)->( dbSkip() )
            Loop
        EndIf
        If ((cAlias)->RF_DABPRO1 > 0 .or. (cAlias)->RF_DABPRO2 > 0 .or. (cAlias)->RF_DABPRO3 > 0 )//N?o tem cálculo de férias e tem programaç?o
            
            dDtSolAb := DaySub(STOD((cAlias)->RF_DATAFIM), nDAbnPec)

            For nProg := 1 to 3  
                nDiasAb := 0
                If nProg == 1 .and. !EMPTY((cAlias)->RF_DABPRO1)
                    nDiasAb  := (cAlias)->RF_DABPRO1 
                ElseIf nProg == 2 .and. !EMPTY((cAlias)->RF_DABPRO2)
                    nDiasAb  := (cAlias)->RF_DABPRO2
                ElseIf nProg == 3 .and. !EMPTY((cAlias)->RF_DABPRO3)
                    nDiasAb  := (cAlias)->RF_DABPRO3
                EndIf
                     
                If nDiasAb > 0

                    If lTAE
                        self:geraREQ(cAlias, StoD((cAlias)->RF_DATABAS), StoD((cAlias)->RF_DATAFIM))
                    EndIf 

                    jData := {;
                        "BranchCode": (cAlias)->RA_FILIAL,;  // Código da Filial
                        "EmployeeCode": (cAlias)->RA_MAT,; // Num da matricula
                        "StartAcquisition": totvs.framework.treports.date.stringToTimeStamp((cAlias)->RF_DATABAS), ; // Data Base Ferias Inicial
                        "EndAcquisition": totvs.framework.treports.date.stringToTimeStamp((cAlias)->RF_DATAFIM),; // Data Base Ferias Final
                        "CurrentDate": FwTimeStamp( 5, dDtSolAb, "00:00:00" ),; // Data 
                        "BranchCity": RTrim( aInfo[05] ),; // Cidade da filial
                        "BranchName": RTrim( aInfo[03] ),; // Nome da filial
                        "CashBenefitNumberDays": nDiasAb,; // Dias de abono pecuniário
                        "CostCenterCode": (cAlias)->RA_CC,; // Codigo Centro de Custo
                        "CostCenterDescription": RTrim( cDescCTT ),; // Descrição Centro de Custo
                        "EmployeeName": iIf(!Empty((cAlias)->RA_NSOCIAL), RTRIM((cAlias)->RA_NSOCIAL), RTRIM((cAlias)->RA_NOMECMP)),; // Nome completo do funcionário
                        "DepartmentCode": (cAlias)->RA_DEPTO ,;
                        "DepartmentName": cDescDep; // Departamento
                    }

                    // Adiciona os campos customizados no JSON
                    GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, {})

                    // Adiciona as informações da linha
                    self:oData:appendData(jData) 
                EndIf 
               

            Next nProg
            (cAlias)->( dbSkip() )
        EndIf
    enddo

    (cAlias)->( DBCloseArea() )
    
    // Libera os objetos da memória
    jData := NIL
    FwFreeObj(jData)

return self:oData
 
method getSchema() as object class GPER130ABTReportsBusinessObject
       
    self:oSchema:addProperty("BranchCode", "Código filial", "string", "Filial", "RA_FILIAL") // Codigo da Filial 
    self:oSchema:addProperty("EmployeeCode", "Numero da matrícula", "string", "Matricula", "RH_MAT") // Num da matricula
    self:oSchema:addProperty("EmployeeName", "Nome do funcionário", "string", "Nome funcionário", "RA_NOMECMP") // Nome completo do funcionário 
    self:oSchema:addProperty("StartAcquisition", "Data início período", "date", "Dt. início per.", "RF_DATABAS") // Data Base Ferias Inicial
    self:oSchema:addProperty("EndAcquisition", "Data fim período", "date", "Dt. fim per.", "RF_DATAFIM") // Data Base Ferias Final
    self:oSchema:addProperty("BranchCity", "Cidade filial", "string", "Cidade fil.", "M0_CIDCOB",,,, .F.) // Cidade da filial
    self:oSchema:addProperty("BranchName", "Nome filial", "string", "Nome fil.", "M0_NOMECOM",,,, .F.) // Nome da filial
    self:oSchema:addProperty("CurrentDate", "Data da solicitação de abono de férias", "date", "Dt. solicit. abono", "CurrentDate",,,, .F.) // Data solicit
    self:oSchema:addProperty("CostCenterCode", "Codigo centro de custo", "string", "Cádigo c.custo", "RA_CC") // Codigo Centro de Custo
    self:oSchema:addProperty("CostCenterDescription", "Descrição centro de custo", "string", "Desc. c.custo", "CostCenterDescription",,,, .F.)
    self:oSchema:addProperty("CashBenefitNumberDays", "Dias de abono pecuniário", "number", "Dias de abono", "CashBenefitNumberDays") // Numero da carteira de trabalho
    self:oSchema:addProperty("DepartmentCode", "Código do departamento", "string", "Departamento", "RA_DEPTO") // Departamento
    self:oSchema:addProperty("DepartmentName", "Descrição departamento", "string", "Desc. departamento", "RA_DEPTO") // Departamento

    self:addParameter("FILDE","Filial De?","string", .F.) 
    self:addParameter("FILATE","Filial Até?","string", .F.) 
    self:addParameter("MATDE","Matricula De?","string", .F.) 
    self:addParameter("MATATE","Matricula Até?","string", .F.)
    self:addParameter("NOMEDE","Nome De?","string", .F.)
    self:addParameter("NOMEATE","Nome Até?","string", .F.)
    self:addParameter("CCDE","Centro de Custo De?","string", .F.) 
    self:addParameter("CCATE","Centro de Custo Até?","string", .F.)
    self:addParameter("DTFDE","Período Férias De?","date", .F.) 
    self:addParameter("DTFATE","Período Férias Até?","date", .F.)
    self:addParameter("DIASAB","Dias prévios p/ Ab. Pecuniário?","number", .F.)
    self:addParameter("IMPDEM","Imprime Demitidos?","string", .F.)
    self:addParameter("ORDER", "Ordem de Impressão?", "string", .F.)

    If FWLibVersion() >= "20231121"        
        self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("ORDER", "/api/rh/smartview/v1/options/GPER130AB/ORDER", 1) 
        self:setCustomURL("IMPDEM", "/api/rh/smartview/v1/options/GPER130AB/IMPDEM", 1) 
    EndIf    
return self:oSchema

/*/{Protheus.doc} getWherePar
Metodo que retorna a consulta conforme os parâmetros
@type  Metodo
@author Bruno Costa
@return character
@since 22/12/2023
/*/
method getWherePar( jPar as json) as character class GPER130ABTReportsBusinessObject
    
    Local cRet as character

    If !(lGp1033 .And. "RA_FILIAL" $ jPar['FILDE', 1])
        If !Empty(jPar['FILDE', 1])
            cRet += " AND SRA.RA_FILIAL >= ?"
        EndIf
        If !Empty(jPar['FILATE', 1])
            cRet += " AND SRA.RA_FILIAL <= ?"
        EndIf
    Else
        cRet += " AND " + jPar['FILDE', 1]
    EndIf

    If !(lGp1033 .And. "RA_MAT" $ jPar['MATDE', 1])
        If !Empty(jPar['MATDE', 1])
            cRet += " AND SRA.RA_MAT >= ?"
        EndIf
        If !Empty(jPar['MATATE', 1])
            cRet += " AND SRA.RA_MAT <= ?"
        EndIf
    Else 
        cRet += " AND " + jPar['MATDE', 1]
    EndIf 

    If !(lGp1033 .And. "RA_CC" $ jPar['CCDE', 1])
        If !Empty(jPar['CCDE', 1])
            cRet += " AND SRA.RA_CC >= ?"
        EndIf
        If !Empty(jPar['CCATE', 1])
            cRet += " AND SRA.RA_CC <= ?"
        EndIf
    Else 
        cRet += " AND " + jPar['CCDE', 1]
    EndIf

    If !(lGp1033 .And. "RA_NOME" $ jPar['NOMEDE', 1])
        If !Empty(jPar['NOMEDE', 1])
            cRet += " AND SRA.RA_NOME >= ?"
        EndIf
        If !Empty(jPar['NOMEATE', 1])
            cRet += " AND SRA.RA_NOME <= ?"
        EndIf
    Else 
        cRet += " AND " + jPar['NOMEDE', 1]
    EndIf

Return cRet

/*/{Protheus.doc} getComboBox
Metodo que retorna o combobox dos valores de ordem para o parâmetro ORDER
@type  Metodo
@author Maria Luísa Arcanjo
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() as Variant Class GPER130ABTReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    Local cParam    As Character 
    local aValues   as Array   

    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    jItem     := NIL
    nValues   := 0
    cParam    := ""
    aValues   := {}

    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    jResponse["data"] := {}

     // Monta o vetor de valores do parâmetro
    If cParam == "ORDER"
        AAdd(aValues, EncodeUTF8("Matrícula"))
        AAdd(aValues, EncodeUTF8("C.Custo + Matrícula"))
        AAdd(aValues, EncodeUTF8("C.Custo + Nome"))
        AAdd(aValues, EncodeUTF8("Nome"))
    Else
        AAdd(aValues, EncodeUTF8("Sim"))
        AAdd(aValues, EncodeUTF8("Não"))   
    EndIf 

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL

/*/{Protheus.doc} geraREQ
Metodo para criar o registro na REQ para controle do que enviar no GPER1033
@type  Metodo
@author Leandro Drumond
@return character
@since 27/03/2025
/*/
method geraREQ(cAlias as character, dDtIni as date, dDtFim as date) as variant class GPER130ABTReportsBusinessObject
local nDoc := "SVGPER130ABO"+(cAlias)->RA_FILIAL+(cAlias)->RA_MAT  as character

DbSelectArea("REQ")
DbSetOrder(1)
RecLock("REQ", !REQ->(DbSeek((cAlias)->(RA_FILIAL + RA_MAT + nDoc))))
REQ->REQ_FILIAL := (cAlias)->RA_FILIAL 
REQ->REQ_MAT	:= (cAlias)->RA_MAT
REQ->REQ_DTINI	:= dDtIni
REQ->REQ_DTFIM 	:= dDtFim
REQ->REQ_NDOC	:= nDoc
REQ->REQ_TPDOC	:= "2"
REQ->REQ_ID 	:= 0
REQ->REQ_DTINTE	:= Date()
REQ->REQ_STATUS	:= "N"

REQ->(MsUnLock())

Return Nil
