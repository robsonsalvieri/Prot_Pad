#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "rh.treports.gper012.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SLY, SJS, CTT, RHK, RHL, RHM, SG0, SJX, SL0, SLE", customTables="SRA, RHK, RHL, RHM, SJX, SL0, SLE, SLY", name="Relatório de divergências de plano de saúde", country="ALL", initialRelease="12.1.2210")
class GPER012TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass

method new() class GPER012TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) // RH
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) // Relatório de divergências de plano de saúde

    // Indica que no objeto Schema que o LookUp será do tipo Key-Value (combobox)
    self:setIsCBoxLookup(.T., .F.)
return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type  Metodo
@author Squad Rotinas
@since  Julho 05,2023
/*/
method getDescription() as character class GPER012TReportsBusinessObject
return (STR0003) // Emite um relatorio de Beneficios por Funcionario
 
method getData(nPage as numeric, oFilter as object) as object class GPER012TReportsBusinessObject
    
    Local cAlias 		as Character
    Local cQuery 		as Character    
	Local nParamOrder 	as Numeric
	Local oStatement 	as Object
	Local oStatementD 	as Object

    Local aColumns 		:= {}
	Local aAloc 		:= {}
    Local nX
	Local cWhere 		:= ""
	Local cMatOld 		:= ""
	Local lOk
    Local lCorpManage	:= fIsCorpManage( FWGrpCompany() )
	Local cLayoutGC 	:= FWSM0Layout(cEmpAnt)
	Local nStartEmp		:= At("E",cLayoutGC)
	Local nStartUnN		:= At("U",cLayoutGC)
	Local nEmpLength	:= Len(FWSM0Layout(cEmpAnt, 1))
	Local nUnNLength	:= Len(FWSM0Layout(cEmpAnt, 2))
	
	//Variáveis com o nome da Filial e Grupo de Empresa
	Local cFilExec 		:= AllTrim(FWFilialName())
	Local cEmpExec 		:= AllTrim(FWEmpName(cEmpAnt))
	
	Local cAliasMain := GetNextAlias()

    Local jParams := oFilter:getParameters() as JSON

	Local cFil       	:= jParams["filial"][1]
	Local cCC        	:= jParams["centroCusto"][1]
	Local cMat       	:= jParams["matricula"][1]
	Local cSind      	:= jParams["sindicato"][1]
	Local aCategoria 	:= jParams["categoria"]
	Local aSituacao  	:= jParams["situacao"]

	Local lDepend
	Local lAgreg
	Local nPos
	Local nPosNovo
	Local aTmp
    
	Local cCustomFields As Character // Campos customizados no formato SQL
    Local cName         As Character // Nome real do campo
    Local aPDFields     As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR   As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR     As Array     // Campos com nome diferente do real
    Local aAllFields    As Array     // Estrutura de todos os campos do ON
    Local lObfuscated   As Logical   // Flag que indica se tem algum campo para ofuscar
    Local jData         As JSON      // Informações a serem adicionadas no ON
    Local jLGPD         As JSON      // Indica quais campos devem ser ofuscados
    Local nFields       As Numeric
	Local aCustomFields as Array

    cCustomFields := ""
    cName         := ""
    aPDFields     := {}
    aPDFieldsNR   := {}
    aFieldsNR     := {}
    aAllFields    := {}
	aWherePer	  := {}
    lObfuscated   := .F.
    jData         := NIL
    jLGPD         := JSONObject():New()
	aCustomFields := {}

	// Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RHK_CDPSAG", "G0_DESCR", "LY_CODIGO", "G0_DESCR1", "RA_UN"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

	nParamOrder := 1

	For nX:=1 To Len(aSituacao)
        If Empty(aSituacao[nX])
            aSituacao[nX] := ' '
        Endif
    Next nX

	If Len(aSituacao) > 0
		cWhere += " SRA.RA_SITFOLH IN (?) AND " // 17
	EndIf
    
	If Len(aCategoria) > 0
		cWhere += " SRA.RA_CATFUNC IN (?) AND " // 18
	EndIf

	If !Empty(cFil)
		cWhere += " SRA.RA_FILIAL = ? AND " // 19
	EndIf
	
	If !Empty(cCC)
		cWhere += " SRA.RA_CC = ? AND " // 20
	EndIf
	
	If !Empty(cMat)
		cWhere += " SRA.RA_MAT = ? AND " // 21
	EndIf
	
	If !Empty(cSind)
		cWhere += " SRA.RA_SINDICA = ? AND " // 22
	EndIf

    cQuery += " SELECT "
    cQuery += "     SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_CC, SRA.RA_SINDICA "
    cQuery += "     , RHK.RHK_MAT, RHK.RHK_PERINI, RHK.RHK_CDPSAG, RHK.RHK_TPFORN, RHK.RHK_TPCALC, RHK.RHK_PLANO "
	cQuery += "		, RHK.RHK_CODFOR, RHK.RHK_PD, RHK.RHK_PDDAGR, RHK.RHK_PERFIM, RHK.RHK_TPPLAN "
    cQuery += "     , RHL.RHL_MAT, RHL.RHL_PERINI, RHL.RHL_TPFORN, RHL.RHL_TPCALC, RHL.RHL_PLANO, RHL.RHL_CODFOR, RHL.RHL_PERFIM, RHL.RHL_TPPLAN "
    cQuery += "     , RHM.RHM_MAT, RHM.RHM_PERINI, RHM.RHM_TPFORN, RHM.RHM_TPCALC, RHM.RHM_PLANO, RHM.RHM_CODFOR, RHM.RHM_PERFIM, RHM.RHM_TPPLAN "
    cQuery += "     , SJX.JX_PERINI, SJX.JX_TPFORN, SJX.JX_PLANO, SJX.JX_CODFORN, SJX.JX_PD, SJX.JX_PDDEP, SJX.JX_PERFIM, SJX.JX_TPPLANO "
    cQuery += "     , SL0.L0_PERINI, SL0.L0_TPFORN, SL0.L0_PLANO, SL0.L0_CODFORN, SL0.L0_PERFIM, SL0.L0_TPPLANO "
    cQuery += "     , SLE.LE_PERINI, SLE.LE_TPFORN, SLE.LE_PLANO, SLE.LE_CODFORN, SLE.LE_PERFIM, SLE.LE_TPPLANO "
    cQuery += "     , SLY.LY_CHVENT, SLY.LY_CODIGO, SLY.LY_ALIAS, SLY.LY_FILENT, SLY.LY_AGRUP, SLY.LY_DTINI, SLY.LY_DTFIM "
    cQuery += "     , ISNULL(CTT.CTT_DESC01, '') CTT_DESC01 "
    cQuery += "     ? " // 1
    cQuery += " FROM "+RetSqlName("SRA")+" SRA "
    cQuery += " INNER JOIN "+RetSqlName("SLY")+" SLY "
    cQuery += "     ON "+FWJoinFilial( "SLY", "SRA" )+" "
    cQuery += "     AND SLY.LY_TIPO     = ? " // 2
    cQuery += "     AND (? >= SLY.LY_DTINI AND (? <= SLY.LY_DTFIM OR SLY.LY_DTFIM = ?)) " // 3, 4 e 5
    cQuery += "     AND SLY.D_E_L_E_T_  = ? " // 6
    cQuery += " INNER JOIN "+RetSqlName("SJS")+" SJS "
    cQuery += "     ON "+FWJoinFilial( "SJS", "SRA" )+" "
    cQuery += "     AND SJS.JS_CDAGRUP  = SLY.LY_AGRUP "
    cQuery += "     AND SJS.JS_TABELA   = SLY.LY_ALIAS "
    cQuery += "     AND SJS.D_E_L_E_T_	= ? " // 7
    cQuery += " LEFT JOIN "+RetSqlName("CTT")+" CTT "
    cQuery += "     ON "+FWJoinFilial( "CTT", "SRA" )+" "
    cQuery += "     AND CTT.CTT_CUSTO   = SRA.RA_CC "
    cQuery += "     AND CTT.D_E_L_E_T_  = ? " // 8
    cQuery += " LEFT JOIN "+RetSqlName("RHK")+" RHK "
    cQuery += "     ON "+FWJoinFilial( "RHK", "SRA" )+" "
    cQuery += "     AND RHK.RHK_MAT     = SRA.RA_MAT "
    cQuery += "     AND RHK.D_E_L_E_T_  = ? " // 9
    cQuery += " LEFT JOIN "+RetSqlName("RHL")+" RHL "
    cQuery += "     ON "+FWJoinFilial( "RHL", "SRA" )+" "
    cQuery += "     AND RHL.RHL_MAT		= RHK.RHK_MAT "
	cQuery += "		AND RHL.RHL_TPFORN 	= RHK.RHK_TPFORN "
	cQuery += "		AND RHL.RHL_CODFOR 	= RHK.RHK_CODFOR "
	cQuery += "		AND RHL.RHL_TPPLAN 	= RHK.RHK_TPPLAN "
	cQuery += "		AND RHL.RHL_PLANO 	= RHK.RHK_PLANO "
    cQuery += "     AND RHL.D_E_L_E_T_  = ? " // 10
    cQuery += " LEFT JOIN "+RetSqlName("RHM")+" RHM "
    cQuery += "     ON "+FWJoinFilial( "RHM", "SRA" )+" "
    cQuery += "     AND RHM.RHM_MAT		= RHK.RHK_MAT "
	cQuery += "		AND RHM.RHM_TPFORN 	= RHK.RHK_TPFORN "
	cQuery += "		AND RHM.RHM_CODFOR 	= RHK.RHK_CODFOR "
	cQuery += "		AND RHM.RHM_TPPLAN 	= RHK.RHK_TPPLAN "
	cQuery += "		AND RHM.RHM_PLANO 	= RHK.RHK_PLANO "
    cQuery += "     AND RHM.D_E_L_E_T_  = ? " // 11
    cQuery += " LEFT JOIN "+RetSqlName("SG0")+" SG0 "
    cQuery += "     ON "+FWJoinFilial( "SG0", "SRA" )+" "
    cQuery += "     AND SG0.G0_CODIGO   = RHK.RHK_CDPSAG "
    cQuery += "     AND SG0.G0_STATUS   = ? " // 12
    cQuery += "     AND SG0.D_E_L_E_T_  = ? " // 13
    cQuery += " LEFT JOIN "+RetSqlName("SJX")+" SJX "
    cQuery += "     ON "+FWJoinFilial( "SJX", "SRA" )+" "
    cQuery += "     AND SJX.JX_CODIGO   = SLY.LY_CODIGO "
    cQuery += "     AND SJX.JX_CODIGO  	= RHK.RHK_CDPSAG "
    cQuery += "     AND SJX.D_E_L_E_T_  = ? " // 14
    cQuery += " LEFT JOIN "+RetSqlName("SL0")+" SL0 "
    cQuery += "     ON "+FWJoinFilial( "SL0", "SRA" )+" "
    cQuery += "     AND SL0.L0_CODIGO   = SLY.LY_CODIGO "
    cQuery += "     AND SL0.L0_CODIGO  	= RHK.RHK_CDPSAG"
    cQuery += "     AND SL0.D_E_L_E_T_  = ? " // 15
    cQuery += " LEFT JOIN "+RetSqlName("SLE")+" SLE "
    cQuery += "     ON "+FWJoinFilial( "SLE", "SRA" )+" "
    cQuery += "     AND SLE.LE_CODIGO   = SLY.LY_CODIGO"
    cQuery += "     AND SLE.D_E_L_E_T_  = ? " // 16
    cQuery += "     AND RHK.RHK_CDPSAG  = SJX.JX_CODIGO "
    cQuery += " WHERE "
    cQuery += "     "+cWhere+" "
    cQuery += "     SRA.RA_PLSAUDE 		!= ? " // 23
    cQuery += "     AND (RHK.RHK_TPCALC <> ? OR RHK.RHK_TPCALC IS NULL) " // 24
    cQuery += "     AND SRA.D_E_L_E_T_ 	= ? " // 25

	cQuery := ChangeQuery(cQuery)
	// Instancia a classe
	oStatement := FwExecStatement():New(cQuery)

	// Captura e define os campos personalizados das tabelas SLY e SRA
	aCustomFields := self:getCustomFields()
	cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "RHK", "RHL", "RHM", "SJX", "SL0", "SLE", "SLY"})
	oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

	oStatement:SetString(nParamOrder++, "PS") // 2
	oStatement:SetString(nParamOrder++, dtos(ddatabase)) // 3
	oStatement:SetString(nParamOrder++, dtos(ddatabase)) // 4
	oStatement:SetString(nParamOrder++, " ") // 5
	oStatement:SetString(nParamOrder++, " ") // 6
	oStatement:SetString(nParamOrder++, " ") // 7
	oStatement:SetString(nParamOrder++, " ") // 8
	oStatement:SetString(nParamOrder++, " ") // 9
	oStatement:SetString(nParamOrder++, " ") // 10
	oStatement:SetString(nParamOrder++, " ") // 11
	oStatement:SetString(nParamOrder++, "1") // 12
	oStatement:SetString(nParamOrder++, " ") // 13
	oStatement:SetString(nParamOrder++, " ") // 14
	oStatement:SetString(nParamOrder++, " ") // 15
	oStatement:SetString(nParamOrder++, " ") // 16
	
	If Len(aSituacao) > 0
		oStatement:SetIn(nParamOrder++, aSituacao) // 17
	EndIf
    
	If Len(aCategoria) > 0
		oStatement:SetIn(nParamOrder++, aCategoria) // 18
	EndIf

	If !Empty(cFil)
		oStatement:SetString(nParamOrder++, FWxFilial("SRA",cFil)) // 19
	EndIf
	
	If !Empty(cCC)
		oStatement:SetString(nParamOrder++, cCC) // 20
	EndIf
	
	If !Empty(cMat)
		oStatement:SetString(nParamOrder++, cMat) // 21
	EndIf
	
	If !Empty(cSind)
		oStatement:SetString(nParamOrder++, cSind) // 22
	EndIf
	oStatement:SetString(nParamOrder++, "2") // 23
	oStatement:SetString(nParamOrder++, "1") // 24
	oStatement:SetString(nParamOrder++, " ") // 25
			
	// Executa a query e retorna o alias criado
	cAlias := oStatement:OpenAlias()

	aColumnUn := { "RA_UN"      ,"C",1,0,}
	aColumnEm := { "RA_EM"      ,"C",1,0,}
    If lCorpManage
		aColumnUn := { "RA_EM"      ,"C",nEmpLength,0,}
		aColumnEm := { "RA_UN"      ,"C",nUnNLength,0,}
	EndIf

	Aadd( aColumns, aColumnUn)
	Aadd( aColumns, aColumnEm)
	
	Aadd( aColumns, { "RA_FILIAL"  ,"C",TAMSX3("RA_FILIAL")[1],TAMSX3("RA_FILIAL")[2]	,GetSx3Cache( "RA_FILIAL" , "X3_PICTURE" ) })
	Aadd( aColumns, { "RA_MAT"     ,"C",TAMSX3("RA_MAT")[1],TAMSX3("RA_MAT")[2]			,GetSx3Cache( "RA_MAT" , "X3_PICTURE" )})
	Aadd( aColumns, { "RA_NOME"    ,"C",TAMSX3("RA_NOME")[1],TAMSX3("RA_NOME")[2]		,GetSx3Cache( "RA_NOME" , "X3_PICTURE" )})
	Aadd( aColumns, { "RA_CC"      ,"C",TAMSX3("RA_CC")[1],TAMSX3("RA_CC")[2]			,GetSx3Cache( "RA_CC" , "X3_PICTURE" )})
	Aadd( aColumns, { "RA_SINDICA" ,"C",TAMSX3("RA_SINDICA")[1],TAMSX3("RA_SINDICA")[2]	,GetSx3Cache( "RA_SINDICA" , "X3_PICTURE" )})
	Aadd( aColumns, { "RHK_CDPSAG" ,"C",TAMSX3("RHK_CDPSAG")[1],TAMSX3("RHK_CDPSAG")[2]	,GetSx3Cache( "RHK_CDPSAG" , "X3_PICTURE" )})
	Aadd( aColumns, { "G0_DESCR"   ,"C",TAMSX3("G0_DESCR")[1],TAMSX3("G0_DESCR")[2]		,GetSx3Cache( "G0_DESCR" , "X3_PICTURE" )})
	Aadd( aColumns, { "LY_CODIGO"  ,"C",TAMSX3("LY_CODIGO")[1],TAMSX3("LY_CODIGO")[2]	,GetSx3Cache( "LY_CODIGO" , "X3_PICTURE" )})
	Aadd( aColumns, { "LY_ALIAS"   ,"C",TAMSX3("LY_ALIAS")[1],TAMSX3("LY_ALIAS")[2]		,GetSx3Cache( "LY_ALIAS" , "X3_PICTURE" )})
	Aadd( aColumns, { "G0_DESCR1"  ,"C",TAMSX3("G0_DESCR")[1],TAMSX3("G0_DESCR")[2]		,GetSx3Cache( "G0_DESCR" , "X3_PICTURE" )})
	Aadd( aColumns, { "CTT_DESC01" ,"C",TAMSX3("CTT_DESC01")[1],TAMSX3("CTT_DESC01")[2]	,GetSx3Cache( "CTT_DESC01" , "X3_PICTURE" )})
	
	oTmpTable := FWTemporaryTable():New( cAliasMain, aColumns )

	oTmpTable:AddIndex( "IND1", {"RA_FILIAL", "RA_MAT"})
	oTmpTable:Create()
	(cAliasMain)->(DbSetOrder(1))

    While !(cAlias)->(Eof())
	
		cCodCri := fRetCriter((cAlias)->RA_FILIAL)
		cCodAlias := fRetAlias(cCodCri, (cAlias)->RA_FILIAL)
		cFilSra := (cAlias)->RA_FILIAL
	
		If (cAlias)->LY_AGRUP == cCodCri
			lOk := .T.
		Endif

		If lOk .AND. !Empty((cAlias)->LY_ALIAS) .AND. (cAlias)->LY_ALIAS $ cCodAlias	
			lOk := .T.
		EndIf
	
		If lOk
		
			DbSelectArea("SRA")
			SRA->(DbSeek((cAlias)->RA_FILIAL + (cAlias)->RA_MAT))
			
			//Checa Criterio
			Do Case
				Case ((cAlias)->LY_ALIAS == "SQB")
					lOk := Alltrim(SRA->RA_DEPTO) == Alltrim((cAlias)->LY_CHVENT)
				Case ((cAlias)->LY_ALIAS == "RCE")
					lOk := Alltrim(SRA->RA_SINDICA) == Alltrim((cAlias)->LY_CHVENT)
				Case ((cAlias)->LY_ALIAS == "CTT")
					lOk := Alltrim(SRA->RA_CC) == Alltrim((cAlias)->LY_CHVENT)
				Case ((cAlias)->LY_ALIAS == "SQ3")
					lOk := Alltrim(SRA->RA_CARGO) == Alltrim((cAlias)->LY_CHVENT)
				Case ((cAlias)->LY_ALIAS == "RCL")
					lOk := Alltrim(SRA->RA_POSTO) == Alltrim((cAlias)->LY_CHVENT)
				Case ((cAlias)->LY_ALIAS == "SRJ")
					lOk := Alltrim(SRA->RA_CODFUNC) == Alltrim((cAlias)->LY_CHVENT)
				Case ((cAlias)->LY_ALIAS == "SR6")
					lOk := Alltrim(SRA->RA_TNOTRAB) == Alltrim((cAlias)->LY_CHVENT)
				Case ((cAlias)->LY_ALIAS == "SM0")
					lOk := Alltrim(SRA->RA_FILIAL) == Alltrim((cAlias)->LY_CHVENT)
				Case ((cAlias)->LY_ALIAS $ "ABS*SA1*TDX")
					dDtIni	:= sToD((cAlias)->LY_DTINI)
					If !Empty((cAlias)->LY_DTFIM)
						dDtFim := sToD((cAlias)->LY_DTFIM)
					Else
						dDtFim := cToD( StrZero( f_UltDia( dDtIni ), 2) + "/" + SubStr( (cAlias)->LY_DTINI, 5, 2 ) + "/" + SubStr( (cAlias)->LY_DTINI, 1, 4 ) )
					EndIf
					
					aAloc := TXRetAloc((cAlias)->RA_FILIAL, (cAlias)->RA_MAT , dDtIni, dDtFim)
					lOk := .F.
					For nX := 1 to Len(aAloc)
						If ((cAlias)->LY_ALIAS == "ABS" .AND. aAloc[nX][07] == Alltrim((cAlias)->LY_CHVENT )) .OR. ((cAlias)->LY_ALIAS == "SA1" .AND. aAloc[nX][10] + aAloc[nX][11] == Alltrim((cAlias)->LY_CHVENT)) .OR. ((cAlias)->LY_ALIAS == "TDX" .AND. aAloc[nX][15] + aAloc[nX][08] == Alltrim((cAlias)->LY_CHVENT ))
							lOk := .T.
							Exit
						EndIf
					Next nX		
				OtherWise
					lOk := .F.
			EndCase
		EndIf 
	
		If lOk
			//Checa Dependentes
			If cMatOld <> (cAlias)->RA_MAT
				nParamOrder := 1
				
				cAliasDep := GetNextAlias()
				cQueryDep := " SELECT COUNT(1) AS QTD "
				cQueryDep += " FROM "+RetSqlName("SRB")+" SRB "
				cQueryDep += " WHERE SRB.RB_FILIAL 	= ? "
				cQueryDep += "   AND SRB.RB_MAT     = ? " 
				cQueryDep += "   AND SRB.D_E_L_E_T_ = ? "
				
				cQueryDep := ChangeQuery(cQueryDep)
				// Instancia a classe
				oStatementD := FwExecStatement():New(cQueryDep)
				oStatementD:SetString(nParamOrder++, xFilial("SRB", (cAlias)->RA_FILIAL))
				oStatementD:SetString(nParamOrder++, (cAlias)->RA_MAT)
				oStatementD:SetString(nParamOrder++, " ")
 
				cAliasDep := oStatementD:OpenAlias()
				
				lDepend := .F.
				If (cAliasDep)->QTD > 0
					lDepend := .T.
				EndIf
				cMatOld := (cAlias)->RA_MAT
				(cAliasDep)->(DbCloseArea())
			EndIf
		
			//Checa Planos Agregados
			lAgreg := .F.
			If !Empty((cAlias)->RHM_TPPLAN)
				If ((cAlias)->RHM_TPPLAN <> (cAlias)->LE_TPPLANO;
				.OR. (cAlias)->RHM_CODFOR <> (cAlias)->LE_CODFORN;
				.OR. (cAlias)->RHM_PLANO <> (cAlias)->LE_PLANO) 
					lAgreg := .T.
				EndIf
			EndIf
	
			If ((Alltrim((cAlias)->LY_CODIGO) <> Alltrim((cAlias)->RHK_CDPSAG))	.OR. Empty((cAlias)->RHK_CDPSAG));
			.OR. ((cAlias)->RHK_TPPLAN <> (cAlias)->JX_TPPLANO;
				.OR. (cAlias)->RHK_CODFOR <> (cAlias)->JX_CODFORN;
				.OR. (cAlias)->RHK_PD <> (cAlias)->JX_PD;
				.OR. (cAlias)->RHK_PDDAGR <> (cAlias)->JX_PDDEP;
				.OR. (cAlias)->RHK_PLANO <> (cAlias)->JX_PLANO;
				.OR. (cAlias)->RHK_PERFIM <> (cAlias)->JX_PERFIM);
			.OR. (lDepend;
				.AND. ( (cAlias)->RHL_TPPLAN	<> (cAlias)->L0_TPPLANO;
					.OR. (cAlias)->RHL_CODFOR <> (cAlias)->L0_CODFORN; 
					.OR. (cAlias)->RHL_PLANO <> (cAlias)->L0_PLANO));
			.OR. lAgreg				
				lGrava := .T.
				If !((cAliasMain)->(DbSeek((cAlias)->RA_FILIAL+(cAlias)->RA_MAT)))
					RecLock(cAliasMain,.T.)
				Else
					//Definir Prioridade de Gravação
					aTmp := Separa(cCodAlias,"/")
					
					nPosNovo := ascan(aTmp,{|x| Alltrim(x)==Alltrim((cAlias)->LY_ALIAS)})
					nPos := ascan(aTmp,{|x| Alltrim(x)==Alltrim((cAliasMain)->LY_ALIAS)})
					
					If nPosNovo >= nPos
						lGrava := .F.
					Else
						RecLock(cAliasMain,.F.)
					EndIf
				EndIf
				
				If lGrava
					(cAliasMain)->RA_EM := ""
					(cAliasMain)->RA_UN := ""
					If lCorpManage
						(cAliasMain)->RA_EM := Substr((cAlias)->RA_FILIAL, nStartEmp, nEmpLength)
						(cAliasMain)->RA_UN := Substr((cAlias)->RA_FILIAL, nStartUnN, nUnNLength)
					EndIF
					(cAliasMain)->RA_FILIAL := (cAlias)->RA_FILIAL
					(cAliasMain)->RA_MAT 	:= (cAlias)->RA_MAT
					(cAliasMain)->RA_CC 	:= (cAlias)->RA_CC
					(cAliasMain)->RA_NOME 	:= (cAlias)->RA_NOME
					(cAliasMain)->RA_SINDICA := (cAlias)->RA_SINDICA
					(cAliasMain)->RHK_CDPSAG := (cAlias)->RHK_CDPSAG
					(cAliasMain)->G0_DESCR 	:= fDescDefP((cAlias)->RHK_CDPSAG, (cAlias)->RA_FILIAL)
					(cAliasMain)->LY_CODIGO := (cAlias)->LY_CODIGO
					(cAliasMain)->LY_ALIAS 	:= (cAlias)->LY_ALIAS
					(cAliasMain)->G0_DESCR1 := fDescDefP((cAlias)->LY_CODIGO, (cAlias)->RA_FILIAL)
					(cAliasMain)->CTT_DESC01 := (cAlias)->CTT_DESC01

					For nFields := 1 To Len(aCustomFields)
						(cAliasMain)->&(aCustomFields[nFields][1]) := (cAlias)->&(aCustomFields[nFields][1])
					Next nFields
					
					(cAliasMain)->(MsUnlock())
				EndIf
			EndIf
		EndIf
		(cAlias)->(DbSkip())
	EndDo
	(cAlias)->(DbCloseArea())

    DbSelectArea(cAliasMain)
	(cAliasMain)->(DbSetOrder(1))
	(cAliasMain)->(DbGoTop())
	
	While !((cAliasMain)->(Eof()))

		// Cria o JSON com as informações da linha
		jData := JSONObject():New()

		jData["RA_FILIAL"]		:= IIf(jLGPD["RA_FILIAL"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasMain)->RA_FILIAL), 	(cAliasMain)->RA_FILIAL)
		jData["RA_CC"]			:= IIf(jLGPD["RA_CC"],  	FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasMain)->RA_CC),    	(cAliasMain)->RA_CC)
		jData["RA_MAT"]			:= IIf(jLGPD["RA_MAT"],  	FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasMain)->RA_MAT),    	(cAliasMain)->RA_MAT)
		jData["RA_SINDICA"]		:= IIf(jLGPD["RA_SINDICA"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasMain)->RA_SINDICA),   (cAliasMain)->RA_SINDICA)
		jData["RA_NOME"]		:= IIf(jLGPD["RA_NOME"],  	FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasMain)->RA_NOME),    	(cAliasMain)->RA_NOME)
		jData["CTT_DESC01"]		:= IIf(jLGPD["CTT_DESC01"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasMain)->CTT_DESC01),   (cAliasMain)->CTT_DESC01)
		jData["CodAtivo"]		:= IIf(jLGPD["RHK_CDPSAG"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasMain)->RHK_CDPSAG),   (cAliasMain)->RHK_CDPSAG)
		jData["DescAtivo"]		:= IIf(jLGPD["G0_DESCR"],  	FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasMain)->G0_DESCR),    	(cAliasMain)->G0_DESCR)
		jData["CodDevido"]		:= IIf(jLGPD["LY_CODIGO"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasMain)->LY_CODIGO),    (cAliasMain)->LY_CODIGO)
		jData["DescDevido"]		:= IIf(jLGPD["G0_DESCR1"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasMain)->G0_DESCR1),    (cAliasMain)->G0_DESCR1)
		jData["Empresa"]		:= cEmpAnt
		jData["UnidadeNegocio"]	:= IIf(jLGPD["RA_UN"],  	FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasMain)->RA_UN),    	(cAliasMain)->RA_UN)
		jData["FilialExec"]		:= cFilExec
		jData["EmpresaExec"]	:= cEmpExec

		// Adiciona os campos customizados no JSON
		GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAliasMain, aPDFields, lObfuscated)

		// Adiciona as informações da linha
		self:oData:appendData(jData)
		
		(cAliasMain)->(DbSkip())
	EndDo

    (cAliasMain)->(DbCloseArea())

	// Limpa os objetos da memória
	jData := NIL
	FwFreeObj(jData)
	FwFreeObj(jLGPD)
	FwFreeArray(aAllFields)
	FwFreeArray(aFieldsNR)
	FwFreeArray(aPDFields)
	FwFreeArray(aPDFieldsNR)
	
	If(valtype(oTmpTable) == "O")
        oTmpTable:Delete()
        freeObj(oTmpTable)
        oTmpTable := nil
    EndIf

return self:oData
 
method getSchema() as object class GPER012TReportsBusinessObject
    local aFieldsSRA as array
    
    aFieldsSRA := { "RA_FILIAL", "RA_NOME" }
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
	self:oSchema:addProperty("RA_MAT",          STR0004,    "string",     STR0004,    "RA_MAT")
    self:oSchema:addProperty("RA_CC",           STR0005,    "string",     STR0005,    "RA_CC")
    self:oSchema:addProperty("RA_SINDICA",      STR0021,    "string",     STR0021,    "RA_SINDICA")
    self:oSchema:addProperty("CTT_DESC01",      STR0022,    "string",     STR0022,    "CTT_DESC01")
    self:oSchema:addProperty("CodAtivo",        STR0006,    "string",     STR0006,    "CodAtivo")
    self:oSchema:addProperty("DescAtivo",       STR0007,    "string",     STR0007,    "DescAtivo")
    self:oSchema:addProperty("CodDevido",       STR0008,    "string",     STR0008,    "CodDevido")
    self:oSchema:addProperty("DescDevido",      STR0009,    "string",     STR0009,    "DescDevido")
    self:oSchema:addProperty("Empresa",         STR0010,    "string",     STR0010,    "Empresa")
    self:oSchema:addProperty("UnidadeNegocio",  STR0011,    "string",     STR0011,    "UnidadeNegocio")
    self:oSchema:addProperty("FilialExec",      STR0012,    "string",     STR0013,    "FilialExec")
    self:oSchema:addProperty("EmpresaExec",     STR0014,    "string",     STR0015,    "EmpresaExec")

	//PARAMETROS
    self:oSchema:addParameter("filial",      STR0016 + " ?",       "string", .F.)
    self:oSchema:addParameter("centroCusto", STR0017 + " ?",       "string", .F.)
    self:oSchema:addParameter("sindicato",   STR0018 + " ?",       "string", .F.)
    self:oSchema:addParameter("categoria",   STR0019 + " ?",       "string", .T.)
    self:oSchema:addParameter("situacao",    STR0020 + " ?",       "string", .T.)
    self:oSchema:addParameter("matricula",   STR0004 + " ?",       "string", .F.)

    // Define os endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("filial",      "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("centroCusto", "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("sindicato",   "/api/framework/v1/genericLookupService/smartview/RCE", 2)
    self:setCustomURL("categoria",   "/api/rh/smartview/v1/options/GPEParams/getSX5/28",     2)
    self:setCustomURL("situacao",    "/api/rh/smartview/v1/options/GPEParams/getSX5/31",     2)
    self:setCustomURL("matricula",   "/api/framework/v1/genericLookupService/smartview/SRA", 2)
return self:oSchema

/*/{Protheus.doc} fDescDefP
Fornece a descrição da definição de plano
@author Gabriel de Souza Almeida
@since 18/05/2015
@version P12
@param cCodDP, caractere, Codigo da definição de plano
@return cDesc, caractere, Descrição da definição de plano
/*/
Static Function fDescDefP(cCodDP, cFilProc)
	Local cDesc := ""
	Default cFilProc := cFilant
	
	DbSelectArea("SG0")
	SG0->(DbSetOrder(1))
	
	If SG0->(MsSeek(xFilial("SG0", cFilProc)+cCodDP))
		cDesc := SG0->G0_DESCR
	EndIf
Return cDesc

/*/{Protheus.doc} fRetAlias
Retorna os códigos dos Alias
@author Gabriel de Souza Almeida
@since 18/05/2015
@version P12
@param cFiltro, caractere, Codigo do critério
@return cCod, caractere, Codigos dos Alias
/*/
Static Function fRetAlias(cFiltro, cFilProc)
	Local aArea 	:= GetArea()
	Local cCod		:= ""
	Local cAliasQry := GetNextAlias()
	Local cFilSJS	:= ""
	Local cQry 		:= ""
	Local oStatement2 as Object
	Local nParamOrder as Numeric
	Default cFilProc := cFilant

	cFilSJS := xFilial("SJS", cFilProc)
	nParamOrder := 1

	cQry := " SELECT SJS.JS_TABELA FROM "+RetSqlName("SJS")+" SJS "
	cQry += " WHERE SJS.JS_FILIAL 	= ? "
	cQry += "   AND SJS.JS_CDAGRUP 	= ? "
	cQry += "   AND SJS.JS_SEQ 		> ? "
	cQry += "   AND D_E_L_E_T_ 		= ? "
	cQry += " ORDER BY SJS.JS_SEQ "

	cQry := ChangeQuery(cQry)
	// Instancia a classe
	oStatement2 := FwExecStatement():New(cQry)
	oStatement2:SetString(nParamOrder++, cFilSJS)
	oStatement2:SetString(nParamOrder++, cFiltro)
	oStatement2:SetString(nParamOrder++, "01")
	oStatement2:SetString(nParamOrder++, " ")
			
	// Executa a query e retorna o alias criado
	cAliasQry := oStatement2:OpenAlias()

	cCod := ""
	While !(cAliasQry)->(Eof())
		cCod += (cAliasQry)->JS_TABELA + " / "
		
		(cAliasQry)->(dbSkip())
	EndDo
	(cAliasQry)->(dbCloseArea())

	RestArea(aArea)
Return cCod
