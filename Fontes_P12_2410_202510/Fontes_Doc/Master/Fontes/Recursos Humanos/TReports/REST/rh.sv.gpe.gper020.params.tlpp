#Include "TLPP-CORE.TH"
#Include "TLPP-REST.TH"
#Include "TOTVS.CH"

/*/{Protheus.doc} GPER020Params
    Definição dos parâmetros de tipo ComboBox do relatório
    Relação de Líquidos (GPER020) do Smart View.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 17/11/2023
/*/
Class GPER020Params
    // Declaração dos métodos públicos
    Public Method new() As Object

    @Get("/api/rh/smartview/v1/options/GPER020/:param")
    Public Method getComboBox() As Variant

    @Get("/api/rh/smartview/v1/options/GPER020/getBankAgency")
    Public Method getBankAgency() As Variant
EndClass

/*/{Protheus.doc} GPER020Params::new
    Método construtor da classe.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 17/11/2023
    @return Object, Instância da classe
/*/
Method new() Class GPER020Params As Object
Return self

/*/{Protheus.doc} GPER020Params::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 17/11/2023
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() As Variant Class GPER020Params
    // Declaração das variáveis locais
    Local jPath     As JSON      // Path parameters da requisição
    Local jResponse As JSON      // Resposta da requisição
    Local jItem     As JSON      // Item do valor do parâmetro
    Local nValues   As Numeric   // Contador dos valores do parâmetro
    Local cParam    As Character // Parâmetro sendo requisitado os valores
    Local aValues   As Array     // Valores do parâmetro

    // Inicialização das variáveis
    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    cParam    := ""
    aValues   := {}

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Verifica qual parâmetro foi solicitado
    If (cParam == "PAR_IMPRIMIR")
        // Monta o vetor de valores do parâmetro
        AAdd(aValues, FwHTTPEncode("Funcionários"))
        AAdd(aValues, FwHTTPEncode("Beneficiários"))
        AAdd(aValues, FwHTTPEncode("Ambos"))
    ElseIf (cParam $ "PAR_NOMEFUN|PAR_TOTAG|PAR_TOTBANCO")
        AAdd(aValues, FwHTTPEncode("Sim"))
        AAdd(aValues, FwHTTPEncode("Não"))
    ElseIf (cParam == "PAR_QUEBRA")
        AAdd(aValues, FwHTTPEncode("Filial"))
        AAdd(aValues, FwHTTPEncode("Filial + Centro de Custo"))
    ElseIf (cParam == "PAR_TIPO")
        AAdd(aValues, FwHTTPEncode("Analítico"))
        AAdd(aValues, FwHTTPEncode("Sintético"))
    ElseIf (cParam == "PAR_FUNCONTACORR")
        AAdd(aValues, FwHTTPEncode("Com"))
        AAdd(aValues, FwHTTPEncode("Sem"))
        AAdd(aValues, FwHTTPEncode("Ambos"))
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL

/*/{Protheus.doc} GPER020Params::getBankAgency
    Retorna o banco e agência conforme tabela SA6.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 14/11/2023
    @return Variant, Retorno nulo pré-fixado
/*/
Method getBankAgency() As Variant Class GPER020Params
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cQuery       As Character // Query para busca dos registros
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Termo de busca
    Local cTerm        As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação
    Local nTotal       As Numeric   // Total de registros da query
    Local nParamOrder  As Numeric   // Ordem dos bind parameters da query
    Local oStatement   As Object    // Instância da classe FwExecStatement

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cQuery       := ""
    cAlias       := ""
    cSearch      := ""
    cTerm        := ""
    nPage        := 1
    nPageSize    := 100
    nCount       := 1
    nTotal       := 0
    nParamOrder  := 1
    oStatement   := NIL

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cTerm := Upper(FwUTF8Decode(jQueryParams["q"]))
            cSearch := "(UPPER(LEFT(A6_COD, ?)) = ? OR "     // 1 e 2
            cSearch += "UPPER(LEFT(A6_AGENCIA, ?)) = ? OR "  // 3 e 4
            cSearch += "UPPER(LEFT(A6_NOME, ?)) = ?) "       // 5 e 6
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["remainingRecords"] := 0
    jResponse["keyProperty"]      := "BankAgency"
    jResponse["descriptor"]       := {;
        "A6_COD":     FwHTTPEncode("Cód. Banco"),;
        "A6_AGENCIA": FwHTTPEncode("Nro. Agência"),;
        "A6_NOME":    FwHTTPEncode("Nome Banco"),;
        "BankAgency": FwHTTPEncode("Banco/Ag");
    }

    // Busca as categorias na SX5
    cQuery += "SELECT "
    cQuery +=     "A6_COD, "
    cQuery +=     "A6_AGENCIA, "
    cQuery +=     "A6_NOME "
    cQuery += "FROM "
    cQuery +=     RetSQLName("SA6") + " "
    cQuery += "WHERE "
    cQuery +=     cSearch
    cQuery +=     IIf(!Empty(cSearch), " AND", "") + " D_E_L_E_T_ = ? " // 7
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FWExecStatement():New(cQuery)

    // Define o valor dos bind parameters
    If (jQueryParams:hasProperty("q"))
        oStatement:SetNumeric(nParamOrder++, Len(cTerm)) // 1
        oStatement:SetString(nParamOrder++, cTerm)       // 2
        oStatement:SetNumeric(nParamOrder++, Len(cTerm)) // 3
        oStatement:SetString(nParamOrder++, cTerm)       // 4
        oStatement:SetNumeric(nParamOrder++, Len(cTerm)) // 5
        oStatement:SetString(nParamOrder++, cTerm)       // 6
    EndIf

    oStatement:SetString(nParamOrder++, " ") // 7

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Conta a quantidade total de registros da query
    Count To nTotal
    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        jItem := {;
            "A6_COD":     FwHTTPEncode(AllTrim((cAlias)->A6_COD)),;
            "A6_AGENCIA": FwHTTPEncode(AllTrim((cAlias)->A6_AGENCIA)),;
            "A6_NOME":    FwHTTPEncode(AllTrim((cAlias)->A6_NOME)),;
            "BankAgency": FwHTTPEncode(AllTrim((cAlias)->(A6_COD + A6_AGENCIA)));
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    jResponse["remainingRecords"] := IIf(nCount > nPageSize .and. !(cAlias)->(EOF()), nTotal - (nPage * nPageSize), 0)
    jResponse["nextPageUrl"]      := IIf(nCount > nPageSize .and. !(cAlias)->(EOF()), oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], ""), NIL)

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeObj(oStatement)
Return NIL
