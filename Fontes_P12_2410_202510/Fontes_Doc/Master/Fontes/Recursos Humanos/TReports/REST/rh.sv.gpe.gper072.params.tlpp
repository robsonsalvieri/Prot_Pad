#Include "TLPP-CORE.TH"
#Include "TLPP-REST.TH"
#Include "TOTVS.CH"

/*/{Protheus.doc} GPER072Params
    Definição dos parâmetros de tipo ComboBox do relatório
    Transferências de Funcionários (GPER072) do Smart View.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 28/11/2023
/*/
Class GPER072Params
    // Declaração dos métodos públicos
    Public Method new() As Object

    @Get("/api/rh/smartview/v1/options/GPER072/getCompanies")
    Public Method getCompanies() As Variant

    @Get("/api/rh/smartview/v1/options/GPER072/getAllBranches")
    Public Method getAllBranches() As Variant
EndClass

/*/{Protheus.doc} GPER072Params::new
    Método construtor da classe.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 28/11/2023
    @return Object, Instância da classe
/*/
Method new() Class GPER072Params As Object
Return self

/*/{Protheus.doc} GPER072Params::getCompanies
    Retorna as empresas do sistema.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 29/11/2023
    @return Variant, Retorno nulo pré-fixado
/*/
Method getCompanies() As Variant Class GPER072Params
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local aSM0         As Array     // Informações das filiais/empresas do sistema
    Local aAuxSM0      As Array     // Vetor auxiliar para as informações das filiais/empresas do sistema
    Local nSM0         As Numeric   // Contador auxiliar do vetor da SM0
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação
    Local nTotal       As Numeric   // Total de registros da query
    Local nStart       As Numeric   // Índice de começo do laço
    Local cSearch      As Character // Termo de busca

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    aSM0         := FwLoadSM0()
    aAuxSM0      := {}
    nSM0         := 0
    nPage        := 1
    nPageSize    := 100
    nCount       := 1
    nTotal       := 0
    nStart       := 1
    cSearch      := ""

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage     := IIf(jQueryParams:hasProperty("page"),     Val(jQueryParams["page"]),     nPage)
        cSearch   := IIf(jQueryParams:hasProperty("q"),        jQueryParams["q"],             "")
        nPageSize := IIf(jQueryParams:hasProperty("pageSize"), Val(jQueryParams["pageSize"]), nPageSize)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["remainingRecords"] := 0
    jResponse["keyProperty"]      := "M0_CODIGO"
    jResponse["descriptor"]       := {;
        "M0_CODIGO": EncodeUTF8("Empresa"),;
        "M0_NOME":   EncodeUTF8("Nome");
    }

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        nStart := ((nPage - 1) * nPageSize) + 1
    EndIf

    // Filtra os registros de forma que não repita a mesma empresa, e, se o termo de busca estiver preenchido, também filtra conforme ele
    AEval(aSM0, {|x| IIf(!ExistsInMatrix(aAuxSM0, SM0_GRPEMP, x[SM0_GRPEMP]) .and. ;
        (Empty(cSearch) .or. Upper(cSearch) $ Upper(x[SM0_NOME]) .or. Upper(cSearch) $ Upper(x[SM0_GRPEMP])), AAdd(aAuxSM0, x), NIL)})

    // Captura o número total de registros
    nTotal := Len(aAuxSM0)

    // Percorre as filiais adicionando cada item no JSON de resposta
    For nSM0 := nStart To nTotal
        // Verifica se chegou no final da página e ainda há registros
        If (nCount > nPageSize)
            // Define a quantidade de registros restantes e a URL da próxima página
            jResponse["remainingRecords"] := nTotal - (nPage * nPageSize)
            jResponse["nextPageUrl"]      := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "");
                + IIf(jQueryParams:hasProperty("pageSize"), "&pageSize=" + jQueryParams["pageSize"], "")

            // Sai do laço de repetição
            EXIT
        EndIf

        jItem := {;
            "M0_CODIGO": EncodeUTF8(AllTrim(aAuxSM0[nSM0][SM0_GRPEMP])),;
            "M0_NOME":   EncodeUTF8(AllTrim(aAuxSM0[nSM0][SM0_NOME]));
        }
        AAdd(jResponse["data"], jItem)

        // Soma um no contador de registros
        nCount++
    Next nSM0

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aSM0)
    FwFreeArray(aAuxSM0)
Return NIL

/*/{Protheus.doc} GPER072Params::getAllBranches
    Retorna todas as filiais do sistema independente da empresa logada.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 30/11/2023
    @return Variant, Retorno nulo pré-fixado
/*/
Method getAllBranches() As Variant Class GPER072Params
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local aSM0         As Array     // Informações das filiais/empresas do sistema
    Local aAuxSM0      As Array     // Vetor auxiliar para as informações das filiais/empresas do sistema
    Local nSM0         As Numeric   // Contador auxiliar do vetor da SM0
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação
    Local nTotal       As Numeric   // Total de registros da query
    Local nStart       As Numeric   // Índice de começo do laço
    Local cSearch      As Character // Termo de busca

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    aSM0         := FwLoadSM0()
    aAuxSM0      := {}
    nSM0         := 0
    nPage        := 1
    nPageSize    := 100
    nCount       := 1
    nTotal       := 0
    nStart       := 1
    cSearch      := ""

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage     := IIf(jQueryParams:hasProperty("page"),     Val(jQueryParams["page"]),     nPage)
        cSearch   := IIf(jQueryParams:hasProperty("q"),        jQueryParams["q"],             "")
        nPageSize := IIf(jQueryParams:hasProperty("pageSize"), Val(jQueryParams["pageSize"]), nPageSize)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["remainingRecords"] := 0
    jResponse["keyProperty"]      := "M0_CODFIL"
    jResponse["descriptor"]       := {;
        "M0_CODIGO": EncodeUTF8("Empresa"),;
        "M0_CODFIL": EncodeUTF8("Filial"),;
        "M0_NOMRED": EncodeUTF8("Nome");
    }

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        nStart := ((nPage - 1) * nPageSize) + 1
    EndIf

    // Se o termo de busca estiver preenchido, filtra as filiais conforme ele
    If (!Empty(cSearch))
        AEval(aSM0, {|x| IIf(Upper(cSearch) $ Upper(x[SM0_CODFIL]) .or. Upper(cSearch) $ Upper(x[SM0_NOMRED]) .or. ;
            Upper(cSearch) $ Upper(x[SM0_GRPEMP]), AAdd(aAuxSM0, x), NIL)})
    Else
        aAuxSM0 := AClone(aSM0)
    EndIf

    // Captura o número total de registros
    nTotal := Len(aAuxSM0)

    // Percorre as filiais adicionando cada item no JSON de resposta
    For nSM0 := nStart To nTotal
        // Verifica se chegou no final da página e ainda há registros
        If (nCount > nPageSize)
            // Define a quantidade de registros restantes e a URL da próxima página
            jResponse["remainingRecords"] := nTotal - (nPage * nPageSize)
            jResponse["nextPageUrl"]      := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "");
                + IIf(jQueryParams:hasProperty("pageSize"), "&pageSize=" + jQueryParams["pageSize"], "")

            // Sai do laço de repetição
            EXIT
        EndIf

        jItem := {;
            "M0_CODIGO": EncodeUTF8(AllTrim(aAuxSM0[nSM0][SM0_GRPEMP])),;
            "M0_CODFIL": EncodeUTF8(AllTrim(aAuxSM0[nSM0][SM0_CODFIL])),;
            "M0_NOMRED": EncodeUTF8(AllTrim(aAuxSM0[nSM0][SM0_NOMRED]));
        }
        AAdd(jResponse["data"], jItem)

        // Soma um no contador de registros
        nCount++
    Next nSM0

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aSM0)
    FwFreeArray(aAuxSM0)
Return NIL

/*/{Protheus.doc} ExistsInMatrix
    Verifica se um dado valor existe em uma certa posição de um item de uma matriz.
    @type Function
    @version 12.1.2310
    @author arthur.sales
    @since 11/30/2023
    @param aMatrix, Array, Matriz que será verificada
    @param nPos, numeric, Posição do item da matriz
    @param xValue, Variant, Valor que será comparado
    @return Logical, Retorna .T. caso o valor exista e .F. caso contrário
/*/
Static Function ExistsInMatrix(aMatrix As Array, nPos As Numeric, xValue As Variant) As Logical
Return AScan(aMatrix, {|x| x[nPos] == xValue}) > 0
