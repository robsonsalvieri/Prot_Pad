#Include "TLPP-CORE.TH"
#Include "TLPP-REST.TH"
#Include "TOTVS.CH"

/*/{Protheus.doc} GPESmartViewParamsUtils
    Definição dos endpoints para parâmetros de tipo LookUp que são comuns em diferentes objetos
    de negócio do Smart View.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 14/11/2023
/*/
Class GPESmartViewParamsUtils
    // Declaração dos métodos públicos
    Public Method new() As Object

    @Get("/api/rh/smartview/v1/options/GPEParams/getBranches")
    Public Method getBranches() As Variant

    @Get("/api/rh/smartview/v1/options/GPEParams/getSX5/:table")
    Public Method getSX5() As Variant

    @Get("/api/rh/smartview/v1/options/GPEParams/getRCC/:tableCode/:codeSize/:descSize")
    Public Method getRCC() As Variant

    @Get("/api/rh/smartview/v1/options/GPEParams/getSNOptions")
    Public Method getSNOptions() As Variant

    @Get("/api/rh/smartview/v1/options/GPEParams/getS049")
    Public Method getS049() As Variant

    @Get("/api/rh/smartview/v1/options/GPEParams/getCodUnic")
    Public Method getCodUnic() As Variant
EndClass

/*/{Protheus.doc} GPESmartViewParamsUtils::new
    Método construtor da classe.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 14/11/2023
    @return Object, Instância da classe
/*/
Method new() Class GPESmartViewParamsUtils As Object
Return self

/*/{Protheus.doc} GPESmartViewParamsUtils::getBranches
    Retorna as filiais do sistema.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 14/11/2023
    @return Variant, Retorno nulo pré-fixado
/*/
Method getBranches() As Variant Class GPESmartViewParamsUtils
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local aSM0         As Array     // Informações das filiais/empresas do sistema
    Local nSM0         As Numeric   // Contador auxiliar do vetor da SM0
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação
    Local nTotal       As Numeric   // Total de registros da query
    Local nStart       As Numeric   // Índice de começo do laço
    Local cSearch      As Character // Termo de busca

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    aSM0         := FwLoadSM0()
    nSM0         := 0
    nPage        := 1
    nPageSize    := 100
    nCount       := 1
    nTotal       := Len(aSM0)
    nStart       := 1
    cSearch      := ""

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage   := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)
        cSearch := IIf(jQueryParams:hasProperty("q"),    jQueryParams["q"],         "")

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["remainingRecords"] := 0
    jResponse["keyProperty"]      := "M0_CODFIL"
    jResponse["descriptor"]       := {;
        "M0_CODFIL": FwHttpEncode("Código"),;
        "M0_NOMRED": FwHttpEncode("Descrição");
    }

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        nStart := (nPage - 1) * nPageSize
    EndIf

    // Percorre as filiais adicionando cada item no JSON de resposta
    For nSM0 := nStart To Len(aSM0)
        // Pula as filiais de empresa diferente da empresa logada
        If (aSM0[nSM0][SM0_GRPEMP] != cEmpAnt)
            LOOP
        EndIf

        // Se o termo de busca estiver preenchido, filtra as filiais com base nele
        If (!Empty(cSearch) .and. !(Upper(cSearch) $ Upper(aSM0[nSM0][SM0_CODFIL])) .and. !(Upper(cSearch) $ Upper(aSM0[nSM0][SM0_NOMRED])))
            LOOP
        EndIf

        // Verifica se chegou no final da página e ainda há registros
        If (nCount > nPageSize)
            // Define a quantidade de registros restantes e a URL da próxima página
            jResponse["remainingRecords"] := nTotal - (nPage * nPageSize)
            jResponse["nextPageUrl"]      := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")

            // Sai do laço de repetição
            EXIT
        EndIf

        jItem := {;
            "M0_CODFIL": FwHttpEncode(AllTrim(aSM0[nSM0][SM0_CODFIL])),;
            "M0_NOMRED": FwHttpEncode(AllTrim(aSM0[nSM0][SM0_NOMRED]));
        }
        AAdd(jResponse["data"], jItem)

        // Soma um no contador de registros
        nCount++
    Next nSM0

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aSM0)
Return NIL

/*/{Protheus.doc} GPESmartViewParamsUtils::getSX5
    Retorna as chaves e descrições da tabela da SX5 requisitada.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 14/11/2023
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: table (X5_TABELA)
/*/
Method getSX5() As Variant Class GPESmartViewParamsUtils
    // Declaração das variáveis locais
    Local jPath        As JSON      // Path parameters da requisição
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cQuery       As Character // Query para busca dos registros
    Local cAlias       As Character // Alias do arquivo temporário
    Local cTable       As Character // Tabela da SX5
    Local cFieldSX5    As Character // Campo de descrição da SX5 de acordo com a linguagem do ambiente
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação
    Local nTotal       As Numeric   // Total de registros da query
    Local nParamOrder  As Numeric   // Ordem dos bind parameters da query
    Local oStatement   As Object    // Instância da classe FwExecStatement

    // Inicialização das variáveis
    jPath        := oRest:getPathParamsRequest()
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cQuery       := ""
    cAlias       := ""
    cTable       := ""
    cFieldSX5    := FDescSX5(2)
    cSearch      := ""
    nPage        := 1
    nPageSize    := 100
    nCount       := 1
    nTotal       := 0
    nParamOrder  := 1
    oStatement   := NIL

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cTable := jPath["table"]
    EndIf

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := " AND (UPPER(" + cFieldSX5 + ") LIKE UPPER(?) OR " // 2 e 3
            cSearch += "UPPER(X5_CHAVE) LIKE UPPER(?)) "                  // 4
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["remainingRecords"] := 0
    jResponse["keyProperty"]      := "X5_CHAVE"
    jResponse["descriptor"]       := {;
        "X5_CHAVE":  FwHttpEncode("Chave"),;
        "X5_DESCRI": FwHttpEncode("Descrição");
    }

    // Busca as categorias na SX5
    cQuery += "SELECT "
    cQuery +=     "X5_CHAVE, "
    cQuery +=     cFieldSX5 + " AS X5_DESCRI "
    cQuery += "FROM "
    cQuery +=     RetSQLName("SX5") + " "
    cQuery += "WHERE "
    cQuery +=     "X5_TABELA = ? " // 1
    cQuery +=     "AND X5_FILIAL = ? " // 2
    cQuery +=     cSearch
    cQuery +=     "AND D_E_L_E_T_ = ? " // 5
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FWExecStatement():New(cQuery)

    // Define o valor dos bind parameters
    oStatement:SetString(nParamOrder++, cTable) // 1

    oStatement:SetString(nParamOrder++, AllTrim(xFilial("SX5"))) // 2

    If (jQueryParams:hasProperty("q"))
        oStatement:SetString(nParamOrder++, "%" + jQueryParams["q"] + "%") // 3
        oStatement:SetString(nParamOrder++, "%" + jQueryParams["q"] + "%") // 4
    EndIf

    oStatement:SetString(nParamOrder++, " ") // 5

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Conta a quantidade total de registros da query
    Count To nTotal
    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        jItem := {;
            "X5_CHAVE":  FwHttpEncode(IIf(!Empty((cAlias)->X5_CHAVE), AllTrim((cAlias)->X5_CHAVE), (cAlias)->X5_CHAVE)),;
            "X5_DESCRI": FwHttpEncode(AllTrim((cAlias)->X5_DESCRI));
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    jResponse["remainingRecords"] := IIf(nCount > nPageSize .and. !(cAlias)->(EOF()), nTotal - (nPage * nPageSize), 0)
    jResponse["nextPageUrl"]      := IIf(nCount > nPageSize .and. !(cAlias)->(EOF()), oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], ""), NIL)

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeObj(oStatement)
Return NIL

/*/{Protheus.doc} GPESmartViewParamsUtils::getRCC
    Retorna as chaves e descrições da tabela da RCC requisitada.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 14/11/2023
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: tableCode (RCC_CODIGO), codeSize e descSize (tamanho do código e da descrição presente no RCC_COUNTEU)
/*/
Method getRCC() As Variant Class GPESmartViewParamsUtils
    // Declaração das variáveis locais
    Local jPath        As JSON      // Path parameters da requisição
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cQuery       As Character // Query para busca dos registros
    Local cAlias       As Character // Alias do arquivo temporário
    Local cTableCode   As Character // Tabela da SX5
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação
    Local nTotal       As Numeric   // Total de registros da query
    Local nCodeSize    As Numeric   // Tamanho do código
    Local nDescSize    As Numeric   // Tamanho da descrição
    Local nParamOrder  As Numeric   // Ordem dos bind parameters da query
    Local oStatement   As Object    // Instância da classe FwExecStatement

    // Inicialização das variáveis
    jPath        := oRest:getPathParamsRequest()
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cQuery       := ""
    cAlias       := ""
    cTable       := ""
    cSearch      := ""
    nPage        := 1
    nPageSize    := 100
    nCount       := 1
    nTotal       := 0
    nCodeSize    := 0
    nDescSize    := 0
    nParamOrder  := 1
    oStatement   := NIL

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cTableCode := jPath["tableCode"]
        nCodeSize  := Val(jPath["codeSize"])
        nDescSize  := Val(jPath["descSize"])
    EndIf

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := " AND (UPPER(SUBSTRING(RCC_CONTEU, 1, ?)) LIKE UPPER(?) OR " // 2 e 3
            cSearch += "UPPER(SUBSTRING(RCC_CONTEU, ?, ?)) LIKE UPPER(?)) "         // 4, 5 e 6
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["remainingRecords"] := 0
    jResponse["keyProperty"]      := "Code"
    jResponse["descriptor"]       := {;
        "Branch":      FwHttpEncode("Filial"),;
        "Code":        FwHttpEncode("Código"),;
        "Sequence":    FwHttpEncode("Sequência"),;
        "Description": FwHttpEncode("Descrição");
    }

    // Busca os registros na RCC
    cQuery += "SELECT "
    cQuery +=     "RCC_CONTEU, "
    cQuery +=     "RCC_FIL, "
    cQuery +=     "RCC_FILIAL, "
    cQuery +=     "RCC_SEQUEN "
    cQuery += "FROM "
    cQuery +=     RetSQLName("RCC") + " "
    cQuery += "WHERE "
    cQuery +=     "RCC_CODIGO = ? " // 1
    cQuery +=     "AND RCC_FILIAL = ? " // 2
    cQuery +=     cSearch
    cQuery +=     "AND D_E_L_E_T_ = ? " // 7
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FWExecStatement():New(cQuery)

    // Define o valor dos bind parameters
    oStatement:SetString(nParamOrder++, cTableCode) // 1

    oStatement:SetString(nParamOrder++, AllTrim(xFilial("RCC"))) // 2

    If (jQueryParams:hasProperty("q"))
        oStatement:SetUnsafe(nParamOrder++, CValToChar(nCodeSize))         // 3
        oStatement:SetString(nParamOrder++, "%" + jQueryParams["q"] + "%") // 4
        oStatement:SetUnsafe(nParamOrder++, CValToChar(nCodeSize + 1))     // 5
        oStatement:SetUnsafe(nParamOrder++, CValToChar(nDescSize))         // 6
        oStatement:SetString(nParamOrder++, "%" + jQueryParams["q"] + "%") // 7
    EndIf

    oStatement:SetString(nParamOrder++, " ") // 8

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Conta a quantidade total de registros da query
    Count To nTotal
    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        jItem := {;
            "Branch":      FwHttpEncode(AllTrim(IIf(!Empty((cAlias)->RCC_FIL), (cAlias)->RCC_FIL, (cAlias)->RCC_FILIAL))),;
            "Code":        FwHttpEncode(AllTrim(SubStr((cAlias)->RCC_CONTEU, 1, nCodeSize))),;
            "Sequence":    FwHttpEncode(AllTrim((cAlias)->RCC_SEQUEN)),;
            "Description": FwHttpEncode(AllTrim(SubStr((cAlias)->RCC_CONTEU, nCodeSize + 1, nDescSize)));
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    jResponse["remainingRecords"] := IIf(nCount > nPageSize .and. !(cAlias)->(EOF()), nTotal - (nPage * nPageSize), 0)
    jResponse["nextPageUrl"]      := IIf(nCount > nPageSize .and. !(cAlias)->(EOF()), oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], ""), NIL)

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeObj(oStatement)
Return NIL


/*/{Protheus.doc} GPESmartViewParamsUtils::getSNOptions
    Retorna os valores de ComboBox de "Sim" e "Não"
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 28/11/2023
    @return Variant, Retorno nulo pré-fixado
/*/
Method getSNOptions() As Variant Class GPESmartViewParamsUtils
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local nValues      As Numeric   // Contador dos valores do parâmetro
    Local aValues      As Array     // Valores do parâmetro
    Local cOption      As Character // Opção de ordem das opções (1 = Sim e Não | 2 = Não e Sim)

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    nValues      := 0
    aValues      := {}
    cOption      := "1"

    // Verifica se foi enviado o parâmetro "option"
    If (jQueryParams:hasProperty("option"))
        cOption := jQueryParams["option"]
    EndIf

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    If (cOption == "1")
        AAdd(aValues, FwHttpEncode("Sim"))
        AAdd(aValues, FwHttpEncode("Não"))
    ElseIf (cOption == "2")
        AAdd(aValues, FwHttpEncode("Não"))
        AAdd(aValues, FwHttpEncode("Sim"))
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL

/*/{Protheus.doc} GPESmartViewParamsUtils::getS049
    Lookup para a tabela S049.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 24/01/2025
    @return Variant, Retorno nulo pré-fixado
/*/
Method getS049() As Variant Class GPESmartViewParamsUtils
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação
    Local nTotal       As Numeric   // Total de registros da query
    Local nItem        As Numeric   // Contador de registros
    Local nStart       As Numeric   // Índice de começo do laço
    Local aTabS049     As Array     // Registros da tabela S049
    Local cSearch      As Character // Termo de busca

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    nPage        := 1
    nPageSize    := 100
    nCount       := 1
    nTotal       := 0
    nItem        := 0
    nStart       := 1
    aTabS049     := {}
    cSearch      := ""

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage     := IIf(jQueryParams:hasProperty("page"),     Val(jQueryParams["page"]),     nPage)
        nPageSize := IIf(jQueryParams:hasProperty("pageSize"), Val(jQueryParams["pageSize"]), nPageSize)

        // Corrige o número e tamanho da página, em caso de ser menor ou igual a zero
        nPage     := IIf(nPage     <= 0, 1,   nPage)
        nPageSize := IIf(nPageSize <= 0, 100, nPageSize)

        // Captura o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := FtAcento(Upper(jQueryParams["q"]))
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["remainingRecords"] := 0
    jResponse["keyProperty"]      := "Categoria"
    jResponse["descriptor"]       := {;
        "Categoria": FwHttpEncode("Categoria"),;
        "Descricao": FwHttpEncode("Descrição");
    }

    // Carrega a tabela S049
    fCarrTab(@aTabS049, "S049")

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        nStart := (nPage - 1) * nPageSize
    EndIf

    nTotal := Len(aTabS049)
    nItem  := nStart

    // Percorre os itens adicionando-os no JSON de resposta
    While (nItem <= nTotal .and. nCount <= nPageSize)
        // Valida a tabela e o termo de busca
        If (aTabS049[nItem][1] == "S049" .and. (Empty(cSearch) .or. cSearch $ FtAcento(Upper(aTabS049[nItem][5])) .or. cSearch $ FtAcento(Upper(aTabS049[nItem][7]))))
            jItem := {;
                "Categoria": FwHttpEncode(AllTrim(aTabS049[nItem][5])),;
                "Descricao": FwHttpEncode(AllTrim(aTabS049[nItem][7]));
            }
            AAdd(jResponse["data"], jItem)

            // Incrementa o contador de registros "válidos"
            nCount++
        EndIf

        // Incrementa o contador de registros
        nItem++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    jResponse["remainingRecords"] := IIf(nItem > nPageSize .and. nItem < nTotal, nTotal - (nPage * nPageSize), 0)
    jResponse["nextPageUrl"]      := IIf(nItem > nPageSize .and. nItem < nTotal, oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "") + IIf(jQueryParams:hasProperty("pageSize"), "&pageSize=" + CValToChar(nPageSize), ""), NIL)

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
Return NIL

/*/{Protheus.doc} GPESmartViewParamsUtils::getCodUnic
    LookUp para o campo RA_CODUNIC.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 24/01/2025
    @return Variant, Retorno nulo pré-fixado
/*/
Method getCodUnic() As Variant Class GPESmartViewParamsUtils
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cQuery       As Character // Query para busca dos registros
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação
    Local nTotal       As Numeric   // Total de registros da query
    Local nParamOrder  As Numeric   // Ordem dos bind parameters da query
    Local oStatement   As Object    // Instância da classe FwExecStatement

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cQuery       := ""
    cAlias       := ""
    cSearch      := ""
    nPage        := 1
    nPageSize    := 100
    nCount       := 1
    nTotal       := 0
    nParamOrder  := 1
    oStatement   := NIL

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage     := IIf(jQueryParams:hasProperty("page"),     Val(jQueryParams["page"]),     nPage)
        nPageSize := IIf(jQueryParams:hasProperty("pageSize"), Val(jQueryParams["pageSize"]), nPageSize)

        // Corrige o número e tamanho da página, em caso de ser menor ou igual a zero
        nPage     := IIf(nPage     <= 0, 1,   nPage)
        nPageSize := IIf(nPageSize <= 0, 100, nPageSize)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := "(UPPER(RA_CODUNIC) LIKE UPPER(?) OR " // cSearch
            cSearch += "UPPER(RA_NOME) LIKE UPPER(?) OR "     // cSearch
            cSearch += "UPPER(RA_NSOCIAL) LIKE UPPER(?) OR "  // cSearch
            cSearch += "UPPER(RA_FILIAL) LIKE UPPER(?)) "     // cSearch
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["remainingRecords"] := 0
    jResponse["keyProperty"]      := "RA_CODUNIC"
    jResponse["descriptor"]       := {;
        "RA_FILIAL":  FwHttpEncode("Filial"),;
        "RA_CODUNIC": FwHttpEncode("Matrícula eSocial"),;
        "RA_NOME":    FwHttpEncode("Nome");
    }

    // Busca as categorias na SX5
    cQuery += "SELECT "
    cQuery +=     "RA_CODUNIC, "
    cQuery +=     "RA_NOME, "
    cQuery +=     "RA_NSOCIAL, "
    cQuery +=     "RA_FILIAL "
    cQuery += "FROM "
    cQuery +=     RetSQLName("SRA") + " "
    cQuery += "WHERE "
    cQuery +=     cSearch
    cQuery +=     IIf(!Empty(cSearch), "AND ", "") + "D_E_L_E_T_ = ? " // " "
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FWExecStatement():New(cQuery)

    // Define o valor dos bind parameters
    If (jQueryParams:hasProperty("q"))
        oStatement:SetString(nParamOrder++, "%" + jQueryParams["q"] + "%")
        oStatement:SetString(nParamOrder++, "%" + jQueryParams["q"] + "%")
        oStatement:SetString(nParamOrder++, "%" + jQueryParams["q"] + "%")
        oStatement:SetString(nParamOrder++, "%" + jQueryParams["q"] + "%")
    EndIf

    oStatement:SetString(nParamOrder++, " ")

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Conta a quantidade total de registros da query
    Count To nTotal
    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        jItem := {;
            "RA_FILIAL":  FwHttpEncode(RTrim((cAlias)->RA_FILIAL)),;
            "RA_CODUNIC": FwHttpEncode(RTrim((cAlias)->RA_CODUNIC)),;
            "RA_NOME":    FwHttpEncode(RTrim(IIf(!Empty((cAlias)->RA_NSOCIAL), (cAlias)->RA_NSOCIAL, (cAlias)->RA_NOME)));
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    jResponse["remainingRecords"] := IIf(nCount > nPageSize .and. !(cAlias)->(EOF()), nTotal - (nPage * nPageSize), 0)
    jResponse["nextPageUrl"]      := IIf(nCount > nPageSize .and. !(cAlias)->(EOF()), oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "") + IIf(jQueryParams:hasProperty("pageSize"), "&pageSize=" + CValToChar(nPageSize), ""), NIL)

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeObj(oStatement)
Return NIL
