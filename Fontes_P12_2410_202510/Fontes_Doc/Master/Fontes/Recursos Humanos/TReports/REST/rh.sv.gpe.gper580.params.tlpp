#Include "TLPP-CORE.TH"
#Include "TLPP-REST.TH"
#Include "TOTVS.CH"

/*/{Protheus.doc} GPER580Params
    Definição dos parâmetros do objeto de negócio Afastamentos (GPER580) do Smart View.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 05/12/2023
/*/
Class GPER580Params
    // Declaração dos métodos públicos
    Public Method new() As Object

    @Get("/api/rh/smartview/v1/options/GPER580/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} GPER580Params::new
    Método construtor da classe.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 05/12/2023
    @return Object, Instância da classe
/*/
Method new() Class GPER580Params As Object
Return self

/*/{Protheus.doc} GPER580Params::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 05/12/2023
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() As Variant Class GPER580Params
    // Declaração das variáveis locais
    Local jPath     As JSON      // Path parameters da requisição
    Local jResponse As JSON      // Resposta da requisição
    Local jItem     As JSON      // Item do valor do parâmetro
    Local nValues   As Numeric   // Contador dos valores do parâmetro
    Local cParam    As Character // Parâmetro sendo requisitado os valores
    Local aValues   As Array     // Valores do parâmetro

    // Inicialização das variáveis
    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    cParam    := ""
    aValues   := {}

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Verifica qual parâmetro foi solicitado
    If (cParam == "MovementType")
        AAdd(aValues, EncodeUTF8("Com saldo"))
        AAdd(aValues, EncodeUTF8("Sem saldo"))
        AAdd(aValues, EncodeUTF8("Ambos"))
    ElseIf (cParam == "BreakBy")
        AAdd(aValues, EncodeUTF8("Filial"))
        AAdd(aValues, EncodeUTF8("Filial + Centro de Custo"))
        AAdd(aValues, EncodeUTF8("Filial + Processo"))
        AAdd(aValues, EncodeUTF8("Filial + Departamento"))
    ElseIf (cParam == "ReportType")
        AAdd(aValues, EncodeUTF8("Analítico"))
        AAdd(aValues, EncodeUTF8("Sintético"))
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL
