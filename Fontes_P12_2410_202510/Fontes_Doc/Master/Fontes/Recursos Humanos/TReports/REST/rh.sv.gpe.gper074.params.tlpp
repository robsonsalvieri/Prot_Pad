#Include "TLPP-CORE.TH"
#Include "TLPP-REST.TH"
#Include "TOTVS.CH"

/*/{Protheus.doc} GPER074Params
    Definição dos parâmetros do objeto de negócio Alterações Cadastrais de Funcionários (GPER074) do Smart View.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 04/12/2023
/*/
Class GPER074Params
    // Declaração dos métodos públicos
    Public Method new() As Object

    @Get("/api/rh/smartview/v1/options/GPER074")
    Public Method getComboBox() As Variant

    @Get("/api/rh/smartview/v1/options/GPER074/getR9Campo")
    Public Method getR9Campo() As Variant
EndClass

/*/{Protheus.doc} GPER074Params::new
    Método construtor da classe.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 04/12/2023
    @return Object, Instância da classe
/*/
Method new() Class GPER074Params As Object
Return self

/*/{Protheus.doc} GPER074Params::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 04/12/2023
    @return Variant, Retorno nulo pré-fixado
/*/
Method getComboBox() As Variant Class GPER074Params
    // Declaração das variáveis locais
    Local jResponse As JSON    // Resposta da requisição
    Local jItem     As JSON    // Item do valor do parâmetro
    Local nValues   As Numeric // Contador dos valores do parâmetro
    Local aValues   As Array   // Valores do parâmetro

    // Inicialização das variáveis
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Monta o vetor de valores do parâmetro
    AAdd(aValues, FwHttpEncode("Filial + Centro de Custo"))
    AAdd(aValues, FwHttpEncode("Filial"))

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL

/*/{Protheus.doc} GPER074Params::getR9Campo
    Retorna os possíveis valores para o R9_CAMPO.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 04/12/2023
    @return Variant, Retorno nulo pré-fixado
/*/
Method getR9Campo() As Variant Class GPER074Params
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cQuery       As Character // Query para busca dos registros
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Consulta de busca
    Local cLanguage    As Character // Idioma do sistema
    Local cSX3Desc     As Character // Nome do campo de descrição da SX3
    Local cTerm        As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação
    Local nTotal       As Numeric   // Total de registros da query
    Local nParamOrder  As Numeric   // Ordem dos bind parameters da query
    Local oStatement   As Object    // Instância da classe FwExecStatement

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cQuery       := ""
    cAlias       := ""
    cSearch      := ""
    cLanguage    := FWRetIdiom()
    cSX3Desc     := IIf(cLanguage == "pt-br", "X3_DESCRIC", IIf(cLanguage == "es", "X3_DESCSPA", "X3_DESCENG"))
    cTerm        := ""
    nPage        := 1
    nPageSize    := 100
    nCount       := 1
    nTotal       := 0
    nParamOrder  := 1
    oStatement   := NIL

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage     := IIf(jQueryParams:hasProperty("page"),     Val(jQueryParams["page"]),     nPage)
        nPageSize := IIf(jQueryParams:hasProperty("pageSize"), Val(jQueryParams["pageSize"]), nPageSize)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cTerm := FwUTF8Decode(jQueryParams["q"])
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["remainingRecords"] := 0
    jResponse["keyProperty"]      := "R9_CAMPO"
    jResponse["descriptor"]       := {;
        "R9_CAMPO":   FwHttpEncode("Campo"),;
        "X3_DESCRIC": FwHttpEncode("Descrição");
    }

    // Busca as categorias na SX5
    cQuery += "SELECT DISTINCT "
    cQuery +=     "R9_CAMPO "
    cQuery += "FROM "
    cQuery +=     RetSQLName("SR9") + " SR9 "
    cQuery += "WHERE "
    cQuery +=     "D_E_L_E_T_ = ? " // 1
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    oStatement:SetString(nParamOrder++, " ") // 1

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Conta a quantidade total de registros da query
    Count To nTotal
    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        cDesc := Trim(GetSX3Cache((cAlias)->R9_CAMPO, cSX3Desc))

        // Se o termo de busca estiver preenchido, filtra as filiais com base nele
        If (!Empty(cTerm) .and. !(Upper(cTerm) $ Upper(cDesc)) .and. !(Upper(cTerm) $ Upper((cAlias)->R9_CAMPO)))
            (cAlias)->(DBSkip())
            LOOP
        EndIf

        jItem := {;
            "R9_CAMPO":   FwHttpEncode(AllTrim((cAlias)->R9_CAMPO)),;
            "X3_DESCRIC": FwHttpEncode(cDesc);
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    jResponse["remainingRecords"] := IIf(nCount > nPageSize .and. !(cAlias)->(EOF()), nTotal - (nPage * nPageSize), 0)
    jResponse["nextPageUrl"]      := IIf(nCount > nPageSize .and. !(cAlias)->(EOF()), oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "");
        + IIf(jQueryParams:hasProperty("pageSize"), "&pageSize=" + jQueryParams["pageSize"], ""), NIL)

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
Return NIL
