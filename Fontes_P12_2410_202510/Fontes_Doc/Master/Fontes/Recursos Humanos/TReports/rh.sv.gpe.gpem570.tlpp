#Include "TOTVS.FRAMEWORK.TREPORTS.INTEGRATEDPROVIDER.th"
#Include "TLPP-CORE.th"
#Include "TLPP-REST.th"
#Include "rh.sv.gpe.gpem570.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} GPEM570TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Conferência da DIRF.
    @type Class
    @version 12.1.2410
    @author arthur.sales
    @since 19/12/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,SRG,SRR,SRV,SR6,CTT", name="Relatório Conferência da DIRF", country="ALL", initialRelease="12.1.2310")
Class GPEM570TReportsBusinessObject From IntegratedProvider
    // Define os métodos da classe
    Public Method new()       As Object
    Public Method getData()   As Object
    Public Method getSchema() As Object
EndClass

/*/{Protheus.doc} GPEM570TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 19/12/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPEM570TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a área
    self:appendArea(STR0001) // "RH"

    // Define o nome e descrição do objeto de negócio
    self:setDisplayName(STR0002) // "Conferência da DIRF"
    self:setDescription(STR0003) // "Este objeto de negócio traz as informações do Relatório Conferência da DIRF."
Return self

/*/{Protheus.doc} GPEM570TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 19/12/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPEM570TReportsBusinessObject
    // Declaração das variáveis locais
    Local cQuery              As Character // Query para busca dos registros
    Local cFilExec            As Character // Nome da filial de execução
    Local cEmpExec            As Character // Nome da empresa de execução
    Local cAlias              As Character // Alias do arquivo temporário
    Local cFilter             As Character // Filtro do objeto de negócio
    Local cBranchCodeFrom     As Character // Parâmetro BranchCodeFrom
    Local cBranchCodeTo       As Character // Parâmetro BranchCodeTo
    Local cEmployeeCodeFrom   As Character // Parâmetro EmployeeCodeFrom
    Local cEmployeeCodeTo     As Character // Parâmetro EmployeeCodeTo
    Local cIdentificationFrom As Character // Parâmetro IdentificationFrom
    Local cIdentificationTo   As Character // Parâmetro IdentificationTo
    Local cEmployeeNameFrom   As Character // Parâmetro EmployeeNameFrom
    Local cEmployeeNameTo     As Character // Parâmetro EmployeeNameTo
    Local cTaxYear            As Character // Parâmetro TaxYear
    Local cName               As Character // Nome real do campo
    Local cValidFil           As Character // Filiais válidas
    Local cPrevBranch         As Character // Filial anterior
    Local cPrevEmployee       As Character // Funcionário anterior
    Local cKeySR4             As Character // Chave de busca da SR4
    Local cRL_FILIAL          As Character // Conteúdo do campo RL_FILIAL
    Local cRL_MAT             As Character // Conteúdo do campo RL_MAT
    Local cRL_BENEFIC         As Character // Conteúdo do campo RL_BENEFIC
    Local cRL_CPFCGC          As Character // Conteúdo do campo RL_CGCFONT
    Local cRL_CODRET          As Character // Conteúdo do campo RL_CODRET
    Local cKeyAllocation      As Character // Chave de busca para o rateio
    Local lOnlyIR             As Logical   // Parâmetro OnlyIR
    Local lOnlyNegativeValues As Logical   // Parâmetro OnlyNegativeValues
    Local lPrintComplement    As Logical   // Parâmetro PrintComplement
    Local lPrintDedSep        As Logical   // Parâmetro PrintDedSep
    Local lValorIr            As Logical   // Indica se houve valor de IR retido
    Local lNegativo           As Logical   // Indica se o valor é negativo
    Local jParams             As JSON      // Parâmetros do objeto de negócio
    Local jData               As JSON      // Informações de uma linha do ON
    Local jLGPD               As JSON      // Indica quais campos devem ser ofuscados
    Local oStatement          As Object    // Instância da classe FwExecStatement
    Local nIncomeAbove        As Numeric   // Parâmetro IncomeAbove
    Local nParamOrder         As Numeric   // Ordem dos bind parameters da query
    Local nAux1               As Numeric   // Contador auxiliar para percorrer vetores
    Local nAux2               As Numeric   // Contador auxiliar para percorrer vetores internos
    Local nFields             As Numeric   // Contador de campos
    Local nEmployees          As Numeric   // Contador de funcionários
    Local nTemVl              As Numeric   // Indica se tem valor
    Local nValRend            As Numeric   // Valor de renda
    Local nElem               As Numeric   // Indica qual a posição no vetor de valores da DIRF, conforme o tipo do valor
    Local nMes                As Numeric   // Mês referente aos valores
    Local nPos                As Numeric   // Auxiliar para busca em vetores
    Local aPDFieldsNR         As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR           As Array     // Campos com nome diferente do real
    Local aRateio             As Array     // Informações de rateio
    Local aRatRRA             As Array     // Informações de rateio da RRA
    Local aDIRFValues         As Array     // Valores da DIRF
    Local aValRRA             As Array     // Valores da RRA
    Local aMes                As Array     // Meses do ano

    // Declaração das variáveis privadas
    Private cQuerySM8     As Character // Query para busca dos registros da SM8
    Private cQuerySM9     As Character // Query para busca dos registros da SM9
    Private oStatementSM8 As Object    // Instância da classe FwExecStatement para busca dos registros da SM8
    Private oStatementSM9 As Object    // Instância da classe FwExecStatement para busca dos registros da SM9

    // Inicialização das variáveis
    cQuery              := ""
    cFilExec            := AllTrim(FwFilialName())
    cEmpExec            := AllTrim(FwEmpName(cEmpAnt))
    cAlias              := ""
    cFilter             := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    cBranchCodeFrom     := ""
    cBranchCodeTo       := ""
    cIdentificationFrom := ""
    cIdentificationTo   := ""
    cEmployeeCodeFrom   := ""
    cEmployeeCodeTo     := ""
    cEmployeeNameFrom   := ""
    cEmployeeNameTo     := ""
    cTaxYear            := ""
    cValidFil           := fValidFil()
    cPrevBranch         := ""
    cPrevEmployee       := ""
    cPrevApprovalDate   := ""
    cAccessSRL          := &("{|| " + ChkRH("GPEM570", "SRL", "2") + "}")
    cAccessSR4          := &("{|| " + ChkRH("GPEM570", "SR4", "2") + "}")
    cKeySR4             := ""
    cRL_FILIAL          := ""
    cRL_MAT             := ""
    cRL_BENEFIC         := ""
    cRL_CPFCGC          := ""
    cRL_CODRET          := ""
    cKeyAllocation      := ""
    cQuerySM8           := ""
    cQuerySM9           := ""
    lOnlyIR             := .F.
    lOnlyNegativeValues := .F.
    lPrintComplement    := .F.
    lPrintDedSep        := .F.
    lValorIr            := .F.
    lNegativo           := .F.
    jParams             := oFilter:getParameters()
    jData               := JSONObject():New()
    jLGPD               := JSONObject():New()
    oStatement          := NIL
    oStatementSM8       := NIL
    oStatementSM9       := NIL
    nIncomeAbove        := 0
    nParamOrder         := 1
    nAux1               := 0
    nAux2               := 0
    nFields             := 1
    nEmployees          := 0
    nTemVl              := 0
    nValRend            := 0
    nElem               := 0
    nMes                := 0
    nPos                := 0
    aFieldsNR           := {}
    aPDFieldsNR         := {}
    aRateio             := {}
    aRatRRA             := {}
    aDIRFValues         := Array(13, 5)
    aValRRA             := {}
    aMes                := {STR0004, STR0005, STR0006, STR0007, STR0008, STR0009,; //"Janeiro" | "Fevereiro" | "Março" | "Abril" | "Maio" | "Junho"
        STR0010, STR0011, STR0012, STR0013, STR0014, STR0015, STR0016} //"Julho" | "Agosto" | "Setembro" | "Outubro" | "Novembro"  | "Dezembro" | "13º Salário"

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RL_BENEFIC", "RL_CPFCGC"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := RTrim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| RTrim(x) == cName}) <= 0
    Next nFields

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cBranchCodeFrom     := IIf(jParams:hasProperty("BranchCodeFrom")     .and. Len(jParams["BranchCodeFrom"])     > 0 .and.!Empty(jParams["BranchCodeFrom"][1]),     jParams["BranchCodeFrom"][1],            cFilAnt)
        cBranchCodeTo       := IIf(jParams:hasProperty("BranchCodeTo")       .and. Len(jParams["BranchCodeTo"])       > 0 .and.!Empty(jParams["BranchCodeTo"][1]),       jParams["BranchCodeTo"][1],              cFilAnt)
        cIdentificationFrom := IIf(jParams:hasProperty("IdentificationFrom") .and. Len(jParams["IdentificationFrom"]) > 0 .and.!Empty(jParams["IdentificationFrom"][1]), jParams["IdentificationFrom"][1],        "")
        cIdentificationTo   := IIf(jParams:hasProperty("IdentificationTo")   .and. Len(jParams["IdentificationTo"])   > 0 .and.!Empty(jParams["IdentificationTo"][1]),   jParams["IdentificationTo"][1],          Replicate("Z", GetSX3Cache("RL_CGCFONT", "X3_TAMANHO")))
        cEmployeeCodeFrom   := IIf(jParams:hasProperty("EmployeeCodeFrom")   .and. Len(jParams["EmployeeCodeFrom"])   > 0 .and.!Empty(jParams["EmployeeCodeFrom"][1]),   jParams["EmployeeCodeFrom"][1],          "")
        cEmployeeCodeTo     := IIf(jParams:hasProperty("EmployeeCodeTo")     .and. Len(jParams["EmployeeCodeTo"])     > 0 .and.!Empty(jParams["EmployeeCodeTo"][1]),     jParams["EmployeeCodeTo"][1],            Replicate("Z", GetSX3Cache("RL_MAT", "X3_TAMANHO")))
        cEmployeeNameFrom   := IIf(jParams:hasProperty("EmployeeNameFrom")   .and. Len(jParams["EmployeeNameFrom"])   > 0 .and.!Empty(jParams["EmployeeNameFrom"][1]),   jParams["EmployeeNameFrom"][1],          "")
        cEmployeeNameTo     := IIf(jParams:hasProperty("EmployeeNameTo")     .and. Len(jParams["EmployeeNameTo"])     > 0 .and.!Empty(jParams["EmployeeNameTo"][1]),     jParams["EmployeeNameTo"][1],            Replicate("Z", GetSX3Cache("RL_BENEFIC", "X3_TAMANHO")))
        lOnlyIR             := IIf(jParams:hasProperty("OnlyIR")             .and. Len(jParams["OnlyIR"])             > 0 .and.!Empty(jParams["OnlyIR"][1]),             jParams["OnlyIR"][1] == "1",             .F.)
        lOnlyNegativeValues := IIf(jParams:hasProperty("OnlyNegativeValues") .and. Len(jParams["OnlyNegativeValues"]) > 0 .and.!Empty(jParams["OnlyNegativeValues"][1]), jParams["OnlyNegativeValues"][1] == "1", .F.)
        cTaxYear            := IIf(jParams:hasProperty("TaxYear")            .and. Len(jParams["TaxYear"])            > 0 .and.!Empty(jParams["TaxYear"][1]),            jParams["TaxYear"][1],                   Str(Year(Date()), 4))
        nIncomeAbove        := IIf(jParams:hasProperty("IncomeAbove")        .and. Len(jParams["IncomeAbove"])        > 0 .and.!Empty(jParams["IncomeAbove"][1]),        jParams["IncomeAbove"][1],               0)
        lPrintComplement    := IIf(jParams:hasProperty("PrintComplement")    .and. Len(jParams["PrintComplement"])    > 0 .and.!Empty(jParams["PrintComplement"][1]),    jParams["PrintComplement"][1] == "1",    .T.)
        lPrintDedSep        := IIf(jParams:hasProperty("PrintDedSep")        .and. Len(jParams["PrintDedSep"])        > 0 .and.!Empty(jParams["PrintDedSep"][1]),        jParams["PrintDedSep"][1] == "1",        .T.)
    EndIf

    // Monta a query de busca para a SM8
    cQuerySM8 := "SELECT "
    cQuerySM8 +=     "M8_FILIAL, "
    cQuerySM8 +=     "M8_MAT, "
    cQuerySM8 +=     "M8_ANO, "
    cQuerySM8 +=     "M8_CODRET, "
    cQuerySM8 +=     "M8_CPFBEN, "
    cQuerySM8 +=     "M8_CODBEN, "
    cQuerySM8 +=     "M8_TIPOREN, "
    cQuerySM8 +=     "SUM(M8_VALOR) AS TOTAL "
    cQuerySM8 += "FROM "
    cQuerySM8 +=     RetSQLName("SM8") + " "
    cQuerySM8 += "WHERE "
    cQuerySM8 +=     "M8_FILIAL      = ? "                     // cFil
    cQuerySM8 +=     "AND M8_MAT     = ? "                     // cMat
    cQuerySM8 +=     "AND (M8_CODRET = ?  OR  M8_CODRET = ?) " // cRlCodRet | "1889"
    cQuerySM8 +=     "AND M8_ANO     = ? "                     // cAno
    cQuerySM8 +=     "AND D_E_L_E_T_ = ? "                     // " "
    cQuerySM8 += "GROUP BY "
    cQuerySM8 +=     "M8_FILIAL, "
    cQuerySM8 +=     "M8_MAT, "
    cQuerySM8 +=     "M8_ANO, "
    cQuerySM8 +=     "M8_CODRET, "
    cQuerySM8 +=     "M8_CPFBEN, "
    cQuerySM8 +=     "M8_CODBEN, "
    cQuerySM8 +=     "M8_TIPOREN "
    cQuerySM8 += "ORDER BY "
    cQuerySM8 +=     "M8_FILIAL, "
    cQuerySM8 +=     "M8_MAT, "
    cQuerySM8 +=     "M8_ANO, "
    cQuerySM8 +=     "M8_CODRET, "
    cQuerySM8 +=     "M8_CPFBEN, "
    cQuerySM8 +=     "M8_CODBEN, "
    cQuerySM8 +=     "M8_TIPOREN "
    cQuerySM8 := ChangeQuery(cQuerySM8)

    // Instancia a classe
    oStatementSM8 := FwExecStatement():New(cQuerySM8)

    // Monta a query de busca para a SM9
    cQuerySM9 := "SELECT "
    cQuerySM9 +=     "M9_FILIAL, "
    cQuerySM9 +=     "M9_MAT, "
    cQuerySM9 +=     "M9_ANO, "
    cQuerySM9 +=     "M9_CODRET, "
    cQuerySM9 +=     "M9_CODFOR, "
    cQuerySM9 +=     "M9_CNPJ, "
    cQuerySM9 +=     "M9_NOMEEMP, "
    cQuerySM9 +=     "M9_TIPOREN, "
    cQuerySM9 +=     "SUM(M9_VALOR) TOTAL "
    cQuerySM9 += "FROM "
    cQuerySM9 +=     RetSQLName("SM9") + " "
    cQuerySM9 += "WHERE "
    cQuerySM9 +=     "M9_FILIAL = ? "                         // cFil
    cQuerySM9 +=     "AND M9_MAT = ? "                        // cMat
    cQuerySM9 +=     "AND (M9_CODRET = ?  OR M9_CODRET = ?) " // cRlCodRet | "1889"
    cQuerySM9 +=     "AND M9_ANO = ? "                        // cAno
    cQuerySM9 +=     "AND D_E_L_E_T_ = ? "                    // " "
    cQuerySM9 += "GROUP BY "
    cQuerySM9 +=     "M9_FILIAL, "
    cQuerySM9 +=     "M9_MAT, "
    cQuerySM9 +=     "M9_ANO, "
    cQuerySM9 +=     "M9_CODRET, "
    cQuerySM9 +=     "M9_CODFOR, "
    cQuerySM9 +=     "M9_CNPJ, "
    cQuerySM9 +=     "M9_NOMEEMP, "
    cQuerySM9 +=     "M9_TIPOREN "
    cQuerySM9 += "ORDER BY "
    cQuerySM9 +=     "M9_FILIAL, "
    cQuerySM9 +=     "M9_MAT, "
    cQuerySM9 +=     "M9_ANO, "
    cQuerySM9 +=     "M9_CODRET, "
    cQuerySM9 +=     "M9_CODFOR, "
    cQuerySM9 +=     "M9_CNPJ, "
    cQuerySM9 +=     "M9_NOMEEMP, "
    cQuerySM9 +=     "M9_TIPOREN "
    cQuerySM9 := ChangeQuery(cQuerySM9)

    // Instancia a classe
    oStatementSM9 := FwExecStatement():New(cQuerySM9)

    // Montagem da query principal
    cQuery := "SELECT "
    cQuery +=     "RL_MAT, "
    cQuery +=     "RL_CPFCGC, "
    cQuery +=     "RL_CODRET, "
    cQuery +=     "RL_PAIS, "
    cQuery +=     "RL_NIFEX, "
    cQuery +=     "RL_FILIAL, "
    cQuery +=     "RL_CGCFONT, "
    cQuery +=     "RL_BENEFIC, "
    cQuery +=     "R4_TIPOREN, "
    cQuery +=     "R4_MES, "
    cQuery +=     "R4_VALOR, "
    cQuery +=     "R4_IDCMPL, "
    cQuery +=     "R4_FILIAL, "
    cQuery +=     "R4_MAT, "
    cQuery +=     "R4_CPFCGC, "
    cQuery +=     "R4_CODRET, "
    cQuery +=     "R4_PAIS, "
    cQuery +=     "R4_NIFEX "
    cQuery += "FROM "
    cQuery +=     RetSQLName("SRL") + " SRL "
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("SR4") + " SR4 "
    cQuery +=         "ON "
    cQuery +=             FwJoinFilial("SR4", "SRL") + " "
    cQuery +=             "AND R4_MAT         =  RL_MAT "
    cQuery +=             "AND R4_CPFCGC      =  RL_CPFCGC "
    cQuery +=             "AND R4_CODRET      =  RL_CODRET "
    cQuery +=             "AND R4_ANO         = ? " // cTaxYear
    cQuery +=             "AND R4_PAIS        =  RL_PAIS "
    cQuery +=             "AND R4_NIFEX       =  RL_NIFEX "
    cQuery +=             "AND SR4.D_E_L_E_T_ = ? " // " "
    cQuery += "WHERE "
    cQuery +=     "RL_FILIAL      BETWEEN ? AND ? " // cBranchCodeFrom | cBranchCodeTo
    cQuery +=     "AND RL_CGCFONT BETWEEN ? AND ? " // cIdentificationFrom | cIdentificationTo
    cQuery +=     "AND RL_MAT     BETWEEN ? AND ? " // cEmployeeCodeFrom | cEmployeeCodeTo
    cQuery +=     "AND RL_BENEFIC BETWEEN ? AND ? " // cEmployeeNameFrom | cEmployeeNameTo
    cQuery +=     "AND SRL.D_E_L_E_T_ = ? "         // " "
    cQuery +=     "? "                              // cFilter
    cQuery += "ORDER BY "
    cQuery +=     "RL_FILIAL, "
    cQuery +=     "RL_MAT, RL_CPFCGC, RL_CODRET, RL_PAIS, RL_NIFEX "
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Define o valor dos bind parameters
    oStatement:SetString(nParamOrder++, cTaxYear)
    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetString(nParamOrder++, cBranchCodeFrom)
    oStatement:SetString(nParamOrder++, cBranchCodeTo)
    oStatement:SetString(nParamOrder++, cIdentificationFrom)
    oStatement:SetString(nParamOrder++, cIdentificationTo)
    oStatement:SetString(nParamOrder++, cEmployeeCodeFrom)
    oStatement:SetString(nParamOrder++, cEmployeeCodeTo)
    oStatement:SetString(nParamOrder++, cEmployeeNameFrom)
    oStatement:SetString(nParamOrder++, cEmployeeNameTo)
    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetUnsafe(nParamOrder++, cFilter)

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Cria os atributos para filial e empresa de execução, e o vetor de funcionários
    jData["FilialExec"]  := RTrim(cFilExec)
    jData["EmpresaExec"] := RTrim(cEmpExec)
    jData["Employees"]   := {}

    // Percorre os registros da query
    While (cAlias)->(!EOF())
        // Verifica o acesso às tabelas SR4 e SRL e se a filial é válida
        If ((cAlias)->RL_FILIAL == cPrevBranch .or. (Eval(cAccessSR4) .and. Eval(cAccessSRL) .and. (cAlias)->RL_FILIAL $ cValidFil))
            // Busca informações de rateio
            If lPrintComplement
                aRateio := {}
                aRatRRA := {}
                aRatRRA := GM570Rat((cAlias)->RL_FILIAL, cTaxYear, (cAlias)->RL_CPFCGC, "1889", (cAlias)->RL_MAT)
                aRateio := GM570Rat((cAlias)->RL_FILIAL, cTaxYear, (cAlias)->RL_CPFCGC, (cAlias)->RL_CODRET, (cAlias)->RL_MAT)
            EndIf

            // Monta o vetor de valores
            For nAux1 := 1 To 13
                For nAux2 := 1 To 5
                    aDIRFValues[nAux1][nAux2] := 0
                Next nAux2
            Next nAux1

            // Reinicializa o valor das variáveis
            lValorIr  := .F.
            lNegativo := .F.
            nTemVl    := 0
            nValRend  := 0
            aValRRA   := {}

            // Captura as informações do funcionário
            cRL_FILIAL  := RTrim((cAlias)->RL_FILIAL)
            cRL_MAT     := RTrim((cAlias)->RL_MAT)
            cRL_BENEFIC := RTrim(IIf(jLGPD["RL_BENEFIC"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RL_BENEFIC), (cAlias)->RL_BENEFIC))
            cRL_CPFCGC  := RTrim(IIf(jLGPD["RL_CPFCGC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RL_CPFCGC),  (cAlias)->RL_CPFCGC))
            cRL_CODRET  := RTrim((cAlias)->RL_CODRET)

            // Monta a chave de busca para passar por todos elementos da SR4
            cKeySR4        := (cAlias)->(RL_FILIAL + RL_MAT + RL_CPFCGC + RL_CODRET + RL_PAIS + RL_NIFEX)
            cKeyAllocation := (cAlias)->(RL_FILIAL + RL_MAT + RL_CODRET)
            While (!(cAlias)->(EOF()) .and. cKeySR4 == (cAlias)->(R4_FILIAL + R4_MAT + R4_CPFCGC + R4_CODRET + R4_PAIS + R4_NIFEX))
                nElem := 0

                If Len(AllTrim((cAlias)->R4_TIPOREN)) == 1
                    If AllTrim((cAlias)->R4_TIPOREN) $ 'AxBxCxDxJxKxLxTxM'
                        nMes := Val((cAlias)->R4_MES)

                        If AllTrim((cAlias)->R4_TIPOREN) $ 'AxJ'
                            nElem := 1
                            nMes  := IIf(AllTrim((cAlias)->R4_TIPOREN) = "A", Val((cAlias)->R4_MES), 13)
                        ElseIf AllTrim((cAlias)->R4_TIPOREN) $ 'BxCxKxTxM'
                            If lPrintDedSep
                                If AllTrim((cAlias)->R4_TIPOREN) $ 'BxCxKxM' // Contribuição Previdenciária , Pensão Judicial, Deduções 13o.Salario , Contribuição à Previdência Privada
                                    nElem := 4
                                ElseIf AllTrim((cAlias)->R4_TIPOREN) $ 'T' // Valor Dependentes
                                    nElem := 5
                                EndIf
                            Else
                                nElem := 2
                            EndIf

                            nMes := IIf(AllTrim((cAlias)->R4_TIPOREN) != "K", Val((cAlias)->R4_MES), 13)
                        ElseIf AllTrim((cAlias)->R4_TIPOREN) $ 'DxL'
                            nElem := 3
                            nMes  := IIf(AllTrim((cAlias)->R4_TIPOREN) != "L", Val((cAlias)->R4_MES), 13)

                            // Seta variável que tem IR
                            If AllTrim((cAlias)->R4_TIPOREN) == 'D'
                                lValorIR := .T.
                            EndIf
                        EndIf

                        If nElem > 0
                            aDIRFValues[nMes,nElem] += (cAlias)->R4_VALOR
                        EndIf

                        nTemVl += (cAlias)->R4_VALOR

                        If nElem == 1
                            nValRend += (cAlias)->R4_VALOR
                        EndIf

                        // Verifica se tem valores negativos
                        If (cAlias)->R4_VALOR < 0.00
                            lNegativo := .T.
                        EndIf
                    EndIf
                ElseIf Len(AllTrim((cAlias)->R4_TIPOREN)) == 2
                    nMes := 13
                    If AllTrim((cAlias)->R4_TIPOREN) $ 'B1xC1xK1xT1xM1'
                        If lPrintDedSep
                            If AllTrim((cAlias)->R4_TIPOREN) $ 'B1xC1xK1xM1' // 13o - Previdência Oficial, 13o - Pensão judicial,13o - Contribuição a previdência privada
                                nElem := 4
                            ElseIf AllTrim((cAlias)->R4_TIPOREN) $ 'T1' // 13o - Dependentes
                                nElem := 5
                            EndIf
                        Else
                            nElem := 2
                        EndIf
                    EndIf

                    If AllTrim((cAlias)->R4_TIPOREN) $ "O1*Q1*C3"
                        nMes := Val((cAlias)->R4_MES)
                        If AllTrim((cAlias)->R4_TIPOREN) == "O1"
                            nElem := 1
                        ElseIf AllTrim((cAlias)->R4_TIPOREN) == "C3"
                            nElem := 2
                        Else
                            nElem := 3
                        EndIf
                    EndIf

                    If nElem > 0
                        aDIRFValues[nMes,nElem] += (cAlias)->R4_VALOR
                    EndIf

                    nTemVl += (cAlias)->R4_VALOR

                    If nElem == 1
                        nValRend += (cAlias)->R4_VALOR
                    EndIf

                    If (nPos := AScan(aValRRA, {|x| x[1] == (cAlias)->R4_IDCMPL})) == 0
                        AAdd(aValRRA,{(cAlias)->R4_IDCMPL , fDesc("RF1", (cAlias)->R4_IDCMPL, "RF1_DESC"), 0, 0, 0, 0, 0, 0})
                        nPos := Len(aValRRA)
                    EndIf

                    If AllTrim((cAlias)->R4_TIPOREN) == 'A1'
                        aValRRA[nPos][3] += (cAlias)->R4_VALOR
                    ElseIf AllTrim((cAlias)->R4_TIPOREN) == 'B2'
                        aValRRA[nPos][4] += (cAlias)->R4_VALOR
                    ElseIf AllTrim((cAlias)->R4_TIPOREN) == 'B3'
                        aValRRA[nPos][5] += (cAlias)->R4_VALOR
                    ElseIf AllTrim((cAlias)->R4_TIPOREN) == 'C2'
                        aValRRA[nPos][6] += (cAlias)->R4_VALOR
                    ElseIf AllTrim((cAlias)->R4_TIPOREN) == 'D2'
                        aValRRA[nPos][7] += (cAlias)->R4_VALOR
                    ElseIf AllTrim((cAlias)->R4_TIPOREN) == 'I1'
                        aValRRA[nPos][8] += (cAlias)->R4_VALOR
                    EndIf
                EndIf

                // Pula para o próximo registro
                (cAlias)->(DBSkip())
            End

            // Despreza o funcionário caso o valor da renda esteja abaixo do mínimo e esteja definido para não trazer valores negativos, ou não tenha valor
            If ((nValRend < nIncomeAbove .and. !lOnlyNegativeValues) .or. nTemVl == 0)
                Loop
            EndIf

            // Valida os parâmetros "Apenas Valores Negativos" e "Apenas IR Retido"
            If (!lOnlyNegativeValues .or. (lOnlyNegativeValues .and. lNegativo)) .and. (!lOnlyIR .or. (lOnlyIR .and. lValorIr))
                // Adiciona as informações do funcionário
                AAdd(jData["Employees"], {;
                    "BranchCode":    cRL_FILIAL,;
                    "EmployeeCode":  cRL_MAT,;
                    "EmployeeName":  cRL_BENEFIC,;
                    "EmployeeId":    IIf(Len(cRL_CPFCGC) == 14, Transform(cRL_CPFCGC, "@!R NN.NNN.NNN/NNNN-99"), Transform(cRL_CPFCGC, "@R 999.999.999-99")),;
                    "RetentionCode": cRL_CODRET,;
                    "Currency":      STR0017; // "Real"
                })

                // Incrementa o contador de funcionários e inicializa o vetor de valores
                nEmployees++
                jData["Employees"][nEmployees]["Values"] := {}

                For nAux1 := 1 To 13
                    AAdd(jData["Employees"][nEmployees]["Values"], {;
                        "Month":               aMes[nAux1],;
                        "TaxableIncome":       aDIRFValues[nAux1][1],;
                        "INSSDeductions":      aDIRFValues[nAux1][4],;
                        "DependentDeductions": aDIRFValues[nAux1][5],;
                        "Deductions":          aDIRFValues[nAux1][2],;
                        "WithheldTax":         aDIRFValues[nAux1][3];
                    })
                Next nAux1

                // Imprime detalhes do rateio assistência médica e odontológica
                If lPrintComplement
                    // Inicializa o vetor de rateio
                    jData["Employees"][nEmployees]["Apportionments"] := {}

                    For nAux1 := 1 To Len(aRateio)
                        If aRateio[nAux1][7] $ cKeyAllocation
                            AAdd(jData["Employees"][nEmployees]["Apportionments"], {;
                                "Description":    RTrim(aRateio[nAux1][1]),;
                                "Identification": RTrim(IIf(aRateio[nAux1][5], aRateio[nAux1][6], aRateio[nAux1][4])),;
                                "Value":          aRateio[nAux1][2];
                            })
                        EndIf
                    Next nAux1

                    For nAux1 := 1 to Len(aRatRRA)
                        AAdd(jData["Employees"][nEmployees]["Apportionments"], {;
                                "Description":    RTrim(aRatRRA[nAux1][1]),;
                                "Identification": RTrim(IIf(aRatRRA[nAux1][5], aRatRRA[nAux1][6], aRatRRA[nAux1][4])),;
                                "Value":          aRatRRA[nAux1][2];
                            })
                    Next nAux1
                EndIf

                // Imprime o complemento de RRA
                If (Len(aValRRA) > 0)
                    // Inicializa o vetor de complemento RRA
                    jData["Employees"][nEmployees]["RRAComplement"] := {}

                    For nAux1 := 1 To Len(aValRRA)
                        AAdd(jData["Employees"][nEmployees]["RRAComplement"], {;
                            "Description":    RTrim(aValRRA[nAux1][2]),;
                            "TaxableIncome":  aValRRA[nAux1][3],;
                            "PrivateContrib": aValRRA[nAux1][4],;
                            "LegalExpenses":  aValRRA[nAux1][5],;
                            "Pension":        aValRRA[nAux1][6],;
                            "WithheldTax":    aValRRA[nAux1][7],;
                            "ExemptIncome":   aValRRA[nAux1][8];
                        })
                    Next nAux1
                EndIf
            EndIf
        EndIf

        // Atualiza a filial atual
        cPrevBranch := (cAlias)->RL_FILIAL
    End

    // Adiciona o registro ao vetor de dados do objeto de negócio
    self:appendData(jData)

    // Fecha a área do alias temporário
    (cAlias)->(DBCloseArea())

    // Limpa os objetos e vetores da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jParams)
    FwFreeObj(jLGPD)
    FwFreeObj(oStatement)
    FwFreeObj(oStatementSM8)
    FwFreeObj(oStatementSM9)
    FwFreeArray(aFieldsNR)
    FwFreeArray(aPDFieldsNR)
    FwFreeArray(aRateio)
    FwFreeArray(aRatRRA)
    FwFreeArray(aDIRFValues)
    FwFreeArray(aValRRA)
    FwFreeArray(aMes)
Return self:oData

/*/{Protheus.doc} GPEM570TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 19/12/2024
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPEM570TReportsBusinessObject
    // Declaração das variáveis locais
    Local aFields  As Array // Campos das propriedades aninhadas

    // Inicialização das variáveis
    aFields := {}

    // Define os parâmetros do relatório
    self:oSchema:addParameter("BranchCodeFrom",     STR0018, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0032) //  "Filial De"                  | "Código da Filial inicial para filtrar os dados."
    self:oSchema:addParameter("BranchCodeTo",       STR0019, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0033) //  "Filial Até"                 | "Código da Filial final para filtrar os dados."
    self:oSchema:addParameter("IdentificationFrom", STR0020, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0034) //  "CNPJ De"                    | "Código do CNPJ inicial para filtrar os dados."
    self:oSchema:addParameter("IdentificationTo",   STR0021, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0035) //  "CNPJ Até"                   | "Código do CNPJ final para filtrar os dados."
    self:oSchema:addParameter("EmployeeCodeFrom",   STR0022, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0036) //  "Matrícula De"               | "Código da Matrícula inicial para filtrar os dados."
    self:oSchema:addParameter("EmployeeCodeTo",     STR0023, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0037) //  "Matrícula Até"              | "Código da Matrícula final para filtrar os dados."
    self:oSchema:addParameter("EmployeeNameFrom",   STR0024, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0038) //  "Nome De"                    | "Nome inicial para filtrar os dados."
    self:oSchema:addParameter("EmployeeNameTo",     STR0025, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0039) //  "Nome Até"                   | "Nome final para filtrar os dados."
    self:oSchema:addParameter("OnlyIR",             STR0026, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0040) //  "Apenas com I.R."            | "Controla se devem ser impressos somente os funcionários que tiveram impostos de renda retidos na fonte."
    self:oSchema:addParameter("OnlyNegativeValues", STR0027, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0041) //  "Apenas valores negativos"   | "Controla se devem ser impressos somente os funcionários que tiveram valores negativos."
    self:oSchema:addParameter("TaxYear",            STR0028, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0042) //  "Ano de Competência"         | "Ano da declaração."
    self:oSchema:addParameter("IncomeAbove",        STR0029, "number", .F., NIL, NIL, NIL, NIL, NIL, STR0043) //  "Rendimentos acima de"       | "Somente serão considerados os valores acima do informado aqui."
    self:oSchema:addParameter("PrintComplement",    STR0030, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0044) //  "Imprime Complementares"     | "Controla a impressão de informações complementares de assistência médica e odontológica."
    self:oSchema:addParameter("PrintBranch",        STR0088, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0089) //  "Imprime Filial e Matrícula" | "Controla a impressão da filial e matrícula dos funcionários."
    self:oSchema:addParameter("PrintDedSep",        STR0031, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0045) //  "Imprime Ded. Separadamente" | "Controla se a exibição dos valores de Deduções será em 2 colunas: Dependentes e INSS."

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("BranchCodeFrom",     "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("BranchCodeTo",       "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("EmployeeCodeFrom",   "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("EmployeeCodeTo",     "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("OnlyIR",             "/api/rh/smartview/v1/options/GPEParams/getSNOptions",  1)
    self:setCustomURL("OnlyNegativeValues", "/api/rh/smartview/v1/options/GPEParams/getSNOptions",  1)
    self:setCustomURL("PrintComplement",    "/api/rh/smartview/v1/options/GPEParams/getSNOptions",  1)
    self:setCustomURL("PrintBranch",        "/api/rh/smartview/v1/options/GPEParams/getSNOptions",  1)
    self:setCustomURL("PrintDedSep",        "/api/rh/smartview/v1/options/GPEParams/getSNOptions",  1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",  STR0046, "string", STR0046, "FilialExec",  NIL, NIL, NIL, .F.) // "Filial de Execução"
    self:oSchema:addProperty("EmpresaExec", STR0047, "string", STR0047, "EmpresaExec", NIL, NIL, NIL, .F.) // "Empresa de Execução"

    // Adiciona a nested property Employees
    aFields := {}
    AAdd(aFields, {"BranchCode",         STR0048, "string", STR0048})   // "Código da Filial"
    AAdd(aFields, {"EmployeeCode",       STR0049, "string", STR0049})   // "Matrícula do Funcionário"
    AAdd(aFields, {"EmployeeName",       STR0050, "string", STR0050})   // "Nome do Funcionário"
    AAdd(aFields, {"EmployeeId",         STR0051, "string", STR0051})   // "CPF do Funcionário"
    AAdd(aFields, {"RetentionCode",      STR0052, "string", STR0052})   // "Código de Retenção"
    AAdd(aFields, {"Currency",           STR0053, "string", STR0053})   // "Moeda"
    AAdd(aFields, {"Values",             STR0054, "string", STR0054})   // "Valores"
    AAdd(aFields, {"Apportionments",     STR0055, "string", STR0055})   // "Rateios"
    AAdd(aFields, {"RRAComplement" ,     STR0094, "string", STR0094})   // "Complemento RRA"
    self:addNestedProperty("Employees", STR0056, STR0056, NIL, aFields) // "Funcionários"

    // Adiciona a nested property no segundo nível do objeto
    aFields := {}
    AAdd(aFields, {"Month",               STR0057, "string", STR0057}) // "Mês"
    AAdd(aFields, {"TaxableIncome",       STR0058, "number", STR0058}) // "Renda tributável"
    AAdd(aFields, {"INSSDeductions",      STR0059, "number", STR0059}) // "Deduções INSS"
    AAdd(aFields, {"DependentDeductions", STR0060, "number", STR0060}) // "Deduções Dependentes"
    AAdd(aFields, {"Deductions",          STR0061, "number", STR0061}) // "Deduções"
    AAdd(aFields, {"WithheldTax",         STR0062, "number", STR0062}) // "Imposto Retido"
    self:transformInNested("Values", NIL, aFields)

    // Adiciona a nested property no segundo nível do objeto
    aFields := {}
    AAdd(aFields, {"Description",    STR0063, "string", STR0063}) // "Descrição"
    AAdd(aFields, {"Identification", STR0064, "string", STR0064}) // "CPF/CNPJ"
    AAdd(aFields, {"Value",          STR0065, "number", STR0065}) // "Valor"
    self:transformInNested("Apportionments", NIL, aFields)

    // Adiciona a nested property no segundo nível do objeto
    aFields := {}
    AAdd(aFields, {"Description",    STR0063, "string", STR0063}) // "Descrição"
    AAdd(aFields, {"TaxableIncome",  STR0058, "number", STR0058}) // "Renda Tributável"
    AAdd(aFields, {"PrivateContrib", STR0090, "number", STR0090}) // "Contribuição Previdência"
    AAdd(aFields, {"LegalExpenses",  STR0091, "number", STR0091}) // "Despesas Judiciais"
    AAdd(aFields, {"Pension",        STR0092, "number", STR0092}) // "Pensão"
    AAdd(aFields, {"WithheldTax",    STR0062, "number", STR0062}) // "Imposto Retido"
    AAdd(aFields, {"ExemptIncome",   STR0093, "number", STR0093}) // "Rendimentos Isentos"
    self:transformInNested("RRAComplement", NIL, aFields)
Return self:oSchema

/*/{Protheus.doc} ³GM570Rat
    Busca as informações de Rateio Assistência Medica e Odontológica do funcionário.
    @type Function
    @author Alessandro Santos
    @since 18/01/11
    @version P12
/*/
Static Function GM570Rat(cFil, cAno, cCPFCGC, cCODRET, cMat)
    Local aDados := {}
    Local nCont  := 0
    Local aAreaRCS := RCS->(GetArea())

    // Definição de valores padrão
    Default cFil    := ""
    Default cAno    := ""
    Default cCPFCGC := ""
    Default cCODRET := ""
    Default cMat    := ""

    DBSelectArea("RCS")
    DBSetOrder(3) // RCS_FILIAL + RCS_ANO + RCS_CPFBEN + RCS_CODRET
    DBSeek(cFil + cAno + cCPFCGC + cCodRet)
    While RCS->(!EOF()) .and. RCS->RCS_ANO == cAno .and. RCS->RCS_CPFBEN == cCPFCGC .and. RCS->RCS_CODRET == cCODRET

        // Adiciona informações do rateio
        AAdd(aDados,{IIf(AllTrim(RCS->RCS_DESCRI) == "ASSISTENCIA MEDICA", "ASSISTENCIA MEDICA TITULAR", RCS->RCS_DESCRI),;
                        RCS->RCS_VALOR,;
                        RCS->RCS_OUTROS,;
                        AllTrim(RCS->RCS_CPFCGC),;
                        .F. ,;
                        ""/*cpfcnpj*/,;
                        RCS->RCS_FILIAL + RCS->RCS_MAT})

        RCS->(DBSkip())
    End

    If !Empty(cMat) .and. cAno >= "2016"
        InfCompPensao(cFil, cAno, cMat, cCodRet, @aDados)
        InfCompPrv(cFil, cAno, cMat, cCodRet, @aDados)
    EndIf

    For nCont := 1 To Len(aDados)
        aDados[nCont, 2] := aDados[nCont, 2]
        aDados[nCont, 4] := Transform(aDados[nCont, 4], "@!R NN.NNN.NNN/NNNN-99")
    Next nCont

    RestArea(aAreaRCS)
Return(aDados)

/*/{Protheus.doc} InfCompPensao
    Obtém valores da pensao para quadro 7 de informações complementares
    @type Function
    @author Oswaldo Leite
    @since 02/01/2017
    @version P12
/*/
Static Function InfCompPensao(cFil, cAno, cMat, cRlCodRet, aDados)
    Local cGetAlias   := ""
    Local cDesc       := ""
    Local cCpfCnpj    := ""
    Local cCodBen     := ""
    Local cTipRen     := ""
    Local cCpfBen     := ""
    Local nTotSM8     := ""
    Local nPos        := 0
    Local nParamOrder := 0
    Local aLetra      := {{"C", STR0066},; // "Pensão Judicial"
                        {"C1",  STR0067},; // "13º - Pensão Judicial"
                        {"C2",  STR0068},; // "Pensão Judicial - Art. 12-A, Lei Nº 7.713 de 1988."
                        {"C3",  STR0069},; // "Pensão Judicial (PLR)."
                        {"8" ,  STR0070},; // "Pensão Alimentícia - Tributação sob exigibilidade suspensa."
                        {"81",  STR0071}}  // "13º - Pensão Alimentícia - Tributação sob exigibilidade suspensa."

    // Definição de valores padrão
    Default cFil      := ""
    Default cAno      := ""
    Default cMat      := ""
    Default cRlCodRet := ""
    Default aDados    := {}

    // Define o valor dos bind parameters
    nParamOrder := 1
    oStatementSM8:SetString(nParamOrder++, cFil)
    oStatementSM8:SetString(nParamOrder++, cMat)
    oStatementSM8:SetString(nParamOrder++, cRlCodRet)
    oStatementSM8:SetString(nParamOrder++, "1889")
    oStatementSM8:SetString(nParamOrder++, cAno)
    oStatementSM8:SetString(nParamOrder++, " ")

    // Executa a query e retorna o alias criado
    cGetAlias := oStatementSM8:OpenAlias()

    While (cGetAlias)->(!EOF() .and. M8_FILIAL + M8_MAT + M8_ANO == cFil + cMat + cAno)
        cCodBen := (cGetAlias)->M8_CODBEN
        cTipRen := (cGetAlias)->M8_TIPOREN
        cCpfBen := (cGetAlias)->M8_CPFBEN
        nTotSM8 := (cGetAlias)->TOTAL

        DBSelectArea('SRQ')
        SRQ->(DBSetOrder(1))
        If SRQ->(DBSeek(cFil + cMat + cCodBen))
            cDesc := Pad(Upper(AllTrim(SRQ->RQ_NOME)), 60) + Space(1)

            // Define a prioridade de impressao das informacoes complementares dependendo do tipo de retencao da verba
            If ((nPos := AScan(aLetra, {|x| x[1] == AllTrim(cTipRen)})) > 0)
                cDesc += PadR(SubStr(Upper(AllTrim(aLetra[nPos, 2])), 1, 38), 38) + Space(1)

                // Formata o CPF/CNPJ
                cCpfCnpj := ""
                If !Empty(cCpfBen)
                    cCpfCnpj += Transform(cCpfBen, If(Len(AllTrim(cCpfBen)) == 11, "@R 999.999.999-99", "@!R NN.NNN.NNN/NNNN-99"))
                EndIf
            EndIf

            //Realiza alimentacao de aDados
            AAdd(aDados, {cDesc, nTotSM8, 0, '', .T., cCpfCnpj, cFil + cMat})
        EndIf

        (cGetAlias)->(DBSkip())
    End

    (cGetAlias)->(DBCloseArea())
Return NIL

/*/{Protheus.doc} InfCompPrv
    Obtem valores da previdencia para quadro 7 de informaçoes complementares
    @type Function
    @author Oswaldo Leite
    @since 02/01/2017
    @version P12
/*/
Static Function InfCompPrv(cFil, cAno, cMat, cRlCodRet, aDados)
    Local cGetAlias   := ""
    Local cDesc       := ""
    Local cCpfCnpj    := ""
    Local cNomeEmp    := ""
    Local cTipRen     := ""
    Local cCNPJ       := ""
    Local nTotSM9     := ""
    Local nPos        := 0
    Local nParamOrder := 0
    Local aLetra      := {{"M", STR0072},; // "Contribuição à Previdência Privada."
                        {"M1",  STR0073},; // "13º Contribuição à Previdência Privada."
                        {"M2",  STR0074},; // "FAPI"
                        {"M3",  STR0075},; // "Fundo Prev. Serv. Público"
                        {"M4",  STR0076},; // "Contr. Ente público patroc."
                        {"M5",  STR0077},; // "13º FAPI"
                        {"M6",  STR0078},; // "13º Fundo Prev.Serv.Público"
                        {"M7",  STR0079},; // "13º Contr. Ente público patroc."
                        {"9" ,  STR0080},; // "Previdência privada - Tributação sob exigibilidade suspensa."
                        {"91",  STR0081},; // "13º - Previdência Privada - Tributação sob exigibilidade suspensa."
                        {"9A",  STR0082},; // "Exigib. Susp. - FAPI"
                        {"9B",  STR0083},; // "Exigib. Susp. - Fundo Prev.Serv.Público"
                        {"9C",  STR0084},; // "Exigib. Susp. - Contr. Ente público patroc."
                        {"9D",  STR0085},; // "13º Exigib. Susp. - FAPI"
                        {"9E",  STR0086},; // "13º Exigib. Susp. - Fundo Prev. Serv. Público"
                        {"9F",  STR0087}}  // "13º Exigib. Susp. - Contr. Ente públ. patroc."

    // Definição de valores padrão
    Default cFil      := ""
    Default cAno      := ""
    Default cMat      := ""
    Default cRlCodRet := ""
    Default aDados    := {}

    // Define o valor dos bind parameters
    nParamOrder := 1
    oStatementSM9:SetString(nParamOrder++, cFil)
    oStatementSM9:SetString(nParamOrder++, cMat)
    oStatementSM9:SetString(nParamOrder++, cRlCodRet)
    oStatementSM9:SetString(nParamOrder++, "1889")
    oStatementSM9:SetString(nParamOrder++, cAno)
    oStatementSM9:SetString(nParamOrder++, " ")

    // Executa a query e retorna o alias criado
    cGetAlias := oStatementSM9:OpenAlias()

    While (cGetAlias)->(! EOF() .and. M9_FILIAL + M9_MAT + M9_ANO == cFil + cMat + cAno)
        cNomeEmp := (cGetAlias)->M9_NOMEEMP
        cTipRen  := (cGetAlias)->M9_TIPOREN
        cCNPJ    := (cGetAlias)->M9_CNPJ
        nTotSM9  := (cGetAlias)->TOTAL

        cDesc := Pad(Upper(AllTrim(cNomeEmp)), 30) + Space(1)

        If((nPos := AScan(aLetra, {|x| x[1] == AllTrim(cTipRen)})) > 0)
            cDesc += PadR(SubStr(Upper(AllTrim(aLetra[nPos, 2])), 1, 38), 38) + Space(1)

            // Formata o CPF/CNPJ
            cCpfCnpj := ''
            If !Empty(cCNPJ)
                cCpfCnpj := Transform(cCNPJ, If(Len(AllTrim(cCNPJ)) == 11, "@R 999.999.999-99", "@!R NN.NNN.NNN/NNNN-99"))
            EndIf
        EndIf

        // Valores do Array aDados
        // 1 = cDesc = Nome + Descrição | 2 = Valor Pago
        // 3 = 0  | 4 = ''
        // 5 = Se registro vindo da SM8 ou SM9 | 6 = CPF ou CNPJ
        AAdd(aDados, {cDesc, nTotSM9, 0, '', .T., cCpfCnpj, cFil + cMat})

        (cGetAlias)->(DBSkip())
    End

    (cGetAlias)->(DBCloseArea())
Return NIL
