#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "totvs.ch"
#Include "rh.sv.gpe.gper082.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER082TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Conferência de FGTS.
    @type Class
    @version 12.1.2410
    @author arthur.sales
    @since 14/01/2025
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,CTT,SRC,SRD,RHH,RGB,SRG,SRR", name="Relatório Conferência de FGTS", country="ALL", initialRelease="12.1.2310")
Class GPER082TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()       As Object
    Public Method getData()   As Object
    Public Method getSchema() As Object

    @Get("/api/rh/smartview/v1/options/GPER082/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} GPER082TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 14/01/2025
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER082TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome e descrição do objeto de negócio
    self:setDisplayName(STR0002) // "Conferência de FGTS"
    self:setDescription(STR0003) // "Este objeto de negócio traz as informações do Relatório Conferência de FGTS."
Return self

/*/{Protheus.doc} GPER082TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 14/01/2025
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER082TReportsBusinessObject
    // Declaração das variáveis locais
    Local cFilExec            As Character // Nome da filial de execução
    Local cEmpExec            As Character // Nome da empresa de execução
    Local cBranchCodeFrom     As Character // Parâmetro BranchCodeFrom
    Local cBranchCodeTo       As Character // Parâmetro BranchCodeTo
    Local cCostCenterCodeFrom As Character // Parâmetro CostCenterCodeFrom
    Local cCostCenterCodeTo   As Character // Parâmetro CostCenterCodeTo
    Local cReferenceDate      As Character // Parâmetro ReferenceDate
    Local cPrintType          As Character // Parâmetro PrintType
    Local ceSocialCodeFrom    As Character // Parâmetro eSocialCodeFrom
    Local ceSocialCodeTo      As Character // Parâmetro eSocialCodeTo
    Local cEmployeeCodeFrom   As Character // Parâmetro EmployeeCodeFrom
    Local cEmployeeCodeTo     As Character // Parâmetro EmployeeCodeTo
    Local aeSocialCat         As Array     // Parâmetro eSocialCat
    Local dReferenceDate      As Date      // Data de referência
    Local jParams             As JSON      // Parâmetros do objeto de negócio
    Local jData               As JSON      // Informações a serem adicionadas no ON
    Local nCat                As Numeric   // Contador de categorias

    // Declaração das variáveis privadas
    Private jInfo As JSON // Informações dos registros do ON

    // Inicialização das variáveis
    cFilExec            := AllTrim(FwFilialName())
    cEmpExec            := AllTrim(FwEmpName(cEmpAnt))
    cBranchCodeFrom     := ""
    cBranchCodeTo       := ""
    cCostCenterCodeFrom := ""
    cCostCenterCodeTo   := ""
    cReferenceDate      := ""
    cPrintType          := ""
    ceSocialCodeFrom    := ""
    ceSocialCodeTo      := ""
    cEmployeeCodeFrom   := ""
    cEmployeeCodeTo     := ""
    aeSocialCat         := {}
    dReferenceDate      := CToD("")
    jParams             := oFilter:getParameters()
    jData               := NIL
    jInfo               := JSONObject():New()
    nCat                := 0

    If (jParams != NIL)
        cBranchCodeFrom     := IIf(jParams:hasProperty("BranchCodeFrom")     .and. Len(jParams["BranchCodeFrom"])     > 0 .and.!Empty(jParams["BranchCodeFrom"][1]),     jParams["BranchCodeFrom"][1],     "")
        cBranchCodeTo       := IIf(jParams:hasProperty("BranchCodeTo")       .and. Len(jParams["BranchCodeTo"])       > 0 .and.!Empty(jParams["BranchCodeTo"][1]),       jParams["BranchCodeTo"][1],       Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCostCenterCodeFrom := IIf(jParams:hasProperty("CostCenterCodeFrom") .and. Len(jParams["CostCenterCodeFrom"]) > 0 .and.!Empty(jParams["CostCenterCodeFrom"][1]), jParams["CostCenterCodeFrom"][1], "")
        cCostCenterCodeTo   := IIf(jParams:hasProperty("CostCenterCodeTo")   .and. Len(jParams["CostCenterCodeTo"])   > 0 .and.!Empty(jParams["CostCenterCodeTo"][1]),   jParams["CostCenterCodeTo"][1],   Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cReferenceDate      := IIf(jParams:hasProperty("ReferenceDate")      .and. Len(jParams["ReferenceDate"])      > 0 .and.!Empty(jParams["ReferenceDate"][1]),      jParams["ReferenceDate"][1],      FwTimeStamp(6, CToD("")))
        ceSocialCodeFrom    := IIf(jParams:hasProperty("eSocialCodeFrom")    .and. Len(jParams["eSocialCodeFrom"])    > 0 .and.!Empty(jParams["eSocialCodeFrom"][1]),    jParams["eSocialCodeFrom"][1],    "")
        ceSocialCodeTo      := IIf(jParams:hasProperty("eSocialCodeTo")      .and. Len(jParams["eSocialCodeTo"])      > 0 .and.!Empty(jParams["eSocialCodeTo"][1]),      jParams["eSocialCodeTo"][1],      Replicate("Z", GetSX3Cache("RA_CODUNIC", "X3_TAMANHO")))
        cEmployeeCodeFrom   := IIf(jParams:hasProperty("EmployeeCodeFrom")   .and. Len(jParams["EmployeeCodeFrom"])   > 0 .and.!Empty(jParams["EmployeeCodeFrom"][1]),   jParams["EmployeeCodeFrom"][1],   "")
        cEmployeeCodeTo     := IIf(jParams:hasProperty("EmployeeCodeTo")     .and. Len(jParams["EmployeeCodeTo"])     > 0 .and.!Empty(jParams["EmployeeCodeTo"][1]),     jParams["EmployeeCodeTo"][1],     Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cPrintType          := IIf(jParams:hasProperty("PrintType")          .and. Len(jParams["PrintType"])          > 0 .and.!Empty(jParams["PrintType"][1]),          jParams["PrintType"][1],          "1")
        aeSocialCat         := IIf(jParams:hasProperty("eSocialCat"),        jParams["eSocialCat"], {})
    EndIf

    // Trata as variáveis de data para ficarem no formato do banco
    cReferenceDate := StrTran(SubStr(cReferenceDate, 1, 10), "-", "")

    // Converte a data para o tipo data
    dReferenceDate := SToD(cReferenceDate)

    // Trata conteúdo vazio
    For nCat := 1 To Len(aeSocialCat)
        aeSocialCat[nCat] := IIf(Empty(aeSocialCat[nCat]), " ", aeSocialCat[nCat])
    Next nCat

    // Cria o atributo para armazenar as filiais
    jInfo["Branches"] := {}

    // Captura as informações do relatório
    GetInfo(cBranchCodeFrom, cBranchCodeTo, cCostCenterCodeFrom, cCostCenterCodeTo, dReferenceDate, cPrintType, ceSocialCodeFrom, ceSocialCodeTo, aeSocialCat, cEmployeeCodeFrom, cEmployeeCodeTo)

    // Totaliza as informações de bases
    GetTotal(@jInfo)

    // Adiciona as informações da linha
    jData := {;
        "FilialExec":  cFilExec,;
        "EmpresaExec": cEmpExec,;
        "TotalBases":  jInfo["TotalBases"],;
        "Branches":    jInfo["Branches"];
    }

    // Adiciona as informações da linha
    self:oData:appendData(jData)

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jParams)
    FwFreeObj(jInfo)
Return self:oData

/*/{Protheus.doc} GPER082TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 14/01/2025
    @return Object, Schema do objeto de negócio
    @obs https://tdn.totvs.com/pages/releaseview.action?pageId=625448935
/*/
Method getSchema() As Object Class GPER082TReportsBusinessObject
    // Declaração das variáveis locais
    Local aFields As Array

    // Inicialização das variáveis
    aFields := {}

    // Define os parâmetros do relatório
    self:oSchema:addParameter("ReferenceDate",      STR0011, "date",   .F., NIL, NIL, NIL, NIL, NIL, STR0020) // "Data Referência" |       "Data de Referência que será utilizada como período de impressão."
    self:oSchema:addParameter("BranchCodeFrom",     STR0004, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0013) // "Filial De" |             "Código da Filial inicial para filtrar os dados."
    self:oSchema:addParameter("BranchCodeTo",       STR0005, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0014) // "Filial Até" |            "Código da Filial final para filtrar os dados."
    self:oSchema:addParameter("CostCenterCodeFrom", STR0006, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0015) // "C.C. De" |               "Código do Centro de Custo inicial para filtrar os dados."
    self:oSchema:addParameter("CostCenterCodeTo",   STR0007, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0016) // "C.C. Até" |              "Código do Centro de Custo final para filtrar os dados."
    self:oSchema:addParameter("EmployeeCodeFrom",   STR0079, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0079) // "Matrícula De" |          "Código da Matrícula inicial para filtrar os dados."
    self:oSchema:addParameter("EmployeeCodeTo",     STR0080, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0080) // "Matrícula Até"|          "Código da Matrícula final para filtrar os dados."
    self:oSchema:addParameter("eSocialCodeFrom",    STR0008, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0017) // "Matrícula eSocial De" |  "Código da Matrícula eSocial inicial para filtrar os dados."
    self:oSchema:addParameter("eSocialCodeTo",      STR0009, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0018) // "Matrícula eSocial Até" | "Código da Matrícula eSocial final para filtrar os dados."
    self:oSchema:addParameter("eSocialCat",         STR0010, "string", .T., NIL, NIL, NIL, NIL, NIL, STR0019) // "Categoria eSocial" |     "Categoria eSocial do funcionário para filtrar os dados."
    self:oSchema:addParameter("PrintType",          STR0012, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0021) // "Tipo Impressão" |        "Sobre qual Folha deseja gerar as informações."

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("BranchCodeFrom",     "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("BranchCodeTo",       "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("CostCenterCodeFrom", "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("CostCenterCodeTo",   "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("EmployeeCodeFrom",   "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("EmployeeCodeTo",     "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("eSocialCodeFrom",    "/api/rh/smartview/v1/options/GPEParams/getCodUnic",    2)
    self:setCustomURL("eSocialCodeTo",      "/api/rh/smartview/v1/options/GPEParams/getCodUnic",    2)
    self:setCustomURL("eSocialCat",         "/api/rh/smartview/v1/options/GPEParams/getS049",       2)
    self:setCustomURL("PrintType",          "/api/rh/smartview/v1/options/GPER082/PrintType",       1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",  STR0022, "string", STR0022, "FilialExec",  NIL, NIL, NIL, .F.) // "Filial de Execução"
    self:oSchema:addProperty("EmpresaExec", STR0023, "string", STR0023, "EmpresaExec", NIL, NIL, NIL, .F.) // "Empresa de Execução"

    aFields := {}
    AAdd(aFields, {"Branch",      STR0024, "string", STR0024}) // "Filial"
    AAdd(aFields, {"TpValue",     STR0025, "string", STR0025}) // "Tipo de Valor"
    AAdd(aFields, {"TpValueDesc", STR0026, "string", STR0026}) // "Descrição do Tipo de Valor"
    AAdd(aFields, {"BaseValue",   STR0027, "number", STR0027}) // "Valor da Base"
    AAdd(aFields, {"FGTS",        STR0028, "number", STR0028}) // "FGTS"
    self:addNestedProperty("TotalBases", STR0081, STR0081, NIL, aFields) // "Total Geral das Bases"

    aFields := {}
    AAdd(aFields, {"BranchCode",    STR0029, "string", STR0029}) // "Código da Filial"
    AAdd(aFields, {"BranchName",    STR0030, "string", STR0030}) // "Nome da Filial"
    AAdd(aFields, {"BranchCNPJ",    STR0031, "string", STR0031}) // "CNPJ da Filial"
    AAdd(aFields, {"BranchAddress", STR0032, "string", STR0032}) // "Endereço da Filial"
    AAdd(aFields, {"BranchZipCode", STR0033, "string", STR0033}) // "CEP da Filial"
    AAdd(aFields, {"BranchCity",    STR0034, "string", STR0034}) // "Município da Filial"
    AAdd(aFields, {"BranchState",   STR0035, "string", STR0035}) // "Estado da Filial"
    AAdd(aFields, {"FGTSMonthly",   STR0036, "string", STR0036}) // "FGTS Mensal"
    AAdd(aFields, {"FGTSDismissal", STR0037, "string", STR0037}) // "FGTS Rescisório"
    self:addNestedProperty("Branches", STR0082, STR0082, NIL, aFields) // "Filiais"

    aFields := {}
    AAdd(aFields, {"EmployeeName",       STR0038, "string", STR0038}) // "Nome do Funcionário"
    AAdd(aFields, {"EmployeeSocialName", STR0039, "string", STR0039}) // "Nome Social do Funcionário"
    AAdd(aFields, {"EmployeeCode",       STR0040, "string", STR0040}) // "Matrícula"
    AAdd(aFields, {"eSocialCode",        STR0041, "string", STR0041}) // "Matrícula eSocial"
    AAdd(aFields, {"Identification",     STR0042, "string", STR0042}) // "CPF"
    AAdd(aFields, {"Category",           STR0043, "string", STR0043}) // "Categoria"
    AAdd(aFields, {"Dismissal",          STR0044, "string", STR0044}) // "Desligamento"
    AAdd(aFields, {"TaxRate",            STR0045, "number", STR0045}) // "Alíquota"
    AAdd(aFields, {"BasesMonthly",       STR0046, "string", STR0046}) // "Bases"
    self:transformInNested("FGTSMonthly", NIL, aFields)

    aFields := {}
    AAdd(aFields, {"TpValue",     STR0025, "string", STR0025}) // "Tipo de Valor"
    AAdd(aFields, {"TpValueDesc", STR0026, "string", STR0026}) // "Descrição do Tipo de Valor"
    AAdd(aFields, {"BaseValue",   STR0027, "number", STR0027}) // "Valor da Base"
    AAdd(aFields, {"FGTS",        STR0028, "number", STR0028}) // "FGTS"
    self:transformInNested("BasesMonthly", NIL, aFields)

    aFields := {}
    AAdd(aFields, {"EmployeeName",       STR0038, "string", STR0038}) // "Nome do Funcionário"
    AAdd(aFields, {"EmployeeSocialName", STR0039, "string", STR0039}) // "Nome Social do Funcionário"
    AAdd(aFields, {"EmployeeCode",       STR0040, "string", STR0040}) // "Matrícula"
    AAdd(aFields, {"eSocialCode",        STR0041, "string", STR0041}) // "Matrícula eSocial"
    AAdd(aFields, {"Identification",     STR0042, "string", STR0042}) // "CPF"
    AAdd(aFields, {"Category",           STR0043, "string", STR0043}) // "Categoria"
    AAdd(aFields, {"Dismissal",          STR0044, "string", STR0044}) // "Desligamento"
    AAdd(aFields, {"TaxRate",            STR0045, "number", STR0045}) // "Alíquota"
    AAdd(aFields, {"BasesDismissal",     STR0046, "string", STR0046}) // "Bases"
    self:transformInNested("FGTSDismissal", NIL, aFields)

    aFields := {}
    AAdd(aFields, {"TpValue",     STR0025, "string", STR0025}) // "Tipo de Valor"
    AAdd(aFields, {"TpValueDesc", STR0026, "string", STR0026}) // "Descrição do Tipo de Valor"
    AAdd(aFields, {"BaseValue",   STR0027, "number", STR0027}) // "Valor da Base"
    AAdd(aFields, {"FGTS",        STR0028, "number", STR0028}) // "FGTS"
    self:transformInNested("BasesDismissal", NIL, aFields)
Return self:oSchema

/*/{Protheus.doc} GetInfo
    Captura as informações a serem adicionadas no objeto de negócio.
    @type Function
    @version 12.1.2410
    @author arthur.sales
    @since 14/01/2025
    @param cBranchCodeFrom, Character, Parâmetro Filial De
    @param cBranchCodeTo, Character, Parâmetro Filial Até
    @param cCostCenterCodeFrom, Character, Parâmetro Centro de Custo De
    @param cCostCenterCodeTo, Character, Parâmetro Centro de Custo Até
    @param dReferenceDate, Date, Parâmetro Data de Referência
    @param cPrintType, Character, Parâmetro Tipo de Impressão
    @param ceSocialCodeFrom, Character, Parâmetro Matrícula eSocial De
    @param ceSocialCodeTo, Character, Parâmetro Matrícula eSocial Até
    @param aeSocialCat, Array, Parâmetro Categoria eSocial
    @param cEmployeeCodeFrom, Character, Parâmetro Matrícula De
    @param cEmployeeCodeTo, Character, Parâmetro Matrícula Até
    @return Variant, Retorno nulo pré-fixado
/*/
Static Function GetInfo(cBranchCodeFrom As Character, cBranchCodeTo As Character, cCostCenterCodeFrom As Character, cCostCenterCodeTo As Character, dReferenceDate As Date, cPrintType As Character, ceSocialCodeFrom As Character, ceSocialCodeTo As Character, aeSocialCat As Array, cEmployeeCodeFrom As Character, cEmployeeCodeTo As Character) As Variant
    // Declaração das variáveis locais
    Local cCatTSV      As Character // Categorias do eSocial de Trabalhadores Sem Vínculo
    Local cCatTCV      As Character // Categorias do eSocial de Trabalhadores Com Vínculo
    Local cFil         As Character // Filial atual
    Local cQuery       As Character // Query utilizada para busca dos registros
    Local cAlias       As Character // Nome do arquivo temporário
    Local cPeriodo     As Character // Período selecionado pelo usuário
    Local cIncFGTS     As Character // Código de incidência do FGTS
    Local cCatEFD      As Character // Categoria do eSocial do funcionário
    Local cPeriodoFGTS As Character // Período do FGTS (Mensal ou Anteriores)
    Local cCodSaq      As Character // Código do saque da rescisão
    Local cTipoRes     As Character // Tipo de rescisão
    Local cTpValor     As Character // Tipo de valor conforme MOS
    Local cTipoFGTS    As Character // Tipo de FGTS (Mensal ou Anteriores)
    Local cName        As Character // Nome real do campo
    Local cRotRes      As Character // Roteiro rescisão
    Local cRotFol      As Character // Roteiro folha
    Local l1200        As Logical   // Indica se enquadra no evento S-1200
    Local lFuncDemi    As Logical   // Indica se o funcionário está demitido
    Local dVencFGTS    As Date      // Data de vencimento do FGTS
    Local dDemissa     As Date      // Data de desligamento do funcionário
    Local nParamOrder  As Numeric   // Auxiliar contador dos bind parameters
    Local nBranch      As Numeric   // Contador de filiais
    Local nAux         As Numeric   // Contador auxiliar
    Local nFields      As Numeric   // Contador de campos
    Local nFGTSBas     As Numeric   // Valor de base do FGTS
    Local nFGTSDep     As Numeric   // Valor de depósito do FGTS
    Local nPos         As Numeric   // Auxiliar de busca em vetor
    Local nPos2        As Numeric   // Auxiliar 2 de busca em vetor
    Local nTaxFGTS     As Numeric   // Alíquota do FGTS para o funcionário
    Local oStatement   As Object    // Instância da classe FwExecStatement()
    Local aValidFil    As Array     // Filiais válidas e que o usuário possui acesso
    Local aInfo        As Array     // Informações da filial atual
    Local aRotAux      As Array     // Usada para unificar os demais vetores de roteiros
    Local aRotSeek     As Array     // Roteiros de folha de pagamento ou segunda parcela do 13º salário
    Local aRotAuton    As Array     // Roteiro de autônomos
    Local aRot131      As Array     // Roteiro de primeira parcela do 13º salário
    Local aRIncFGTS    As Array     // Regras de incidência do FGTS
    Local aRTipoFGTS   As Array     // Regras do tipo do FGTS
    Local aPDFieldsNR  As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR    As Array     // Campos com nome diferente do real
    Local aTabS037     As Array     // Registros da tabela S037
    Local jCodSaq      As JSON      // Armazena o código de saque conforme tipo de rescisão
    Local jTipoRes     As JSON      // Armazena o tipo de rescisão do eSocial conforme o tipo de rescisão do Protheus
    Local jLGPD        As JSON      // Indica quais campos devem ser ofuscados
    Local jTpValor     As JSON      // Tipos de valores que influem na apuração do FGTS
    Local jAux         As JSON      // JSON auxiliar

    // Declaração das variáveis privada
    Private aIncRes As Array // Variável usada na função fM40TpRes()

    // Inicialização das variáveis
    cCatTSV         := fCatTrabEFD("TSV")
    cCatTCV         := fCatTrabEFD("TCV")
    cFil            := ""
    cQuery          := ""
    cAlias          := ""
    cIncFGTS        := ""
    cCatEFD         := ""
    cPeriodoFGTS    := ""
    cCodSaq         := ""
    cTipoRes        := ""
    cTpValor        := ""
    cTipoFGTS       := ""
    cName           := ""
    cRotRes         := ""
    cRotFol         := ""
    l1200           := .F.
    lFuncDemi       := .F.
    dVencFGTS       := CToD("")
    dDemissa        := CToD("")
    cPeriodo        := SubStr(AnoMes(dReferenceDate), 1, 4) + SubStr(AnoMes(dReferenceDate), 5, 2)
    nParamOrder     := 1
    nBranch         := 0
    nAux            := 0
    nFields         := 1
    nFGTSBas        := 0
    nFGTSDep        := 0
    nPos            := 0
    nPos2           := 0
    nTaxFGTS        := 0
    oStatement      := NIL
    aValidFil       := StrToArray(fValidFil(), "/")
    aInfo           := {}
    aRotAux         := {}
    aRotSeek        := {}
    aRotAuton       := {}
    aRot131         := {}
    aRIncFGTS       := {}
    aRTipoFGTS      := {}
    aPDFieldsNR     := {}
    aFieldsNR       := {}
    aTabS037        := {}
    aIncRes         := {}
    jCodSaq         := JSONObject():New()
    jTipoRes        := JSONObject():New()
    jLGPD           := JSONObject():New()
    jTpValor        := TipoValor()
    jAux            := NIL

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RA_NOME", "RA_NSOCIAL"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Carrega a tabela S037 para capturar a alíquota do FGTS
    fCarrTab(@aTabS037, "S037")

    BEGIN SEQUENCE
        // Monta a query da SRC fora do laço para ganho de performance
        cQuery := "SELECT "
        cQuery +=     "RC_FILIAL, "
        cQuery +=     "RC_CC, "
        cQuery +=     "RC_MAT, "
        cQuery +=     "RC_ROTEIR, "
        cQuery +=     "RC_VALOR, "
        cQuery +=     "RV_INCFGTS, "
        cQuery +=     "RC_PD, "
        cQuery +=     "RC_PROCES, "
        cQuery +=     "RC_PERIODO, "
        cQuery +=     "RC_SEMANA, "
        cQuery +=     "TABELA, "
        cQuery +=     "RA_NOME, "
        cQuery +=     "RA_NSOCIAL, "
        cQuery +=     "RA_CATEFD, "
        cQuery +=     "RA_CODUNIC, "
        cQuery +=     "RA_CIC, "
        cQuery +=     "RA_DEMISSA, "
        cQuery +=     "RA_SITFOLH, "
        cQuery +=     "RA_RESCRAI, "
        cQuery +=     "RG_TIPORES, "
        cQuery +=     "RA_PERFGTS, "
        cQuery +=     "SRA.R_E_C_N_O_ RECNO_SRA, "
        cQuery +=     "RV_TIPOCOD, "
        cQuery +=     "RV_CONTRAP, "
        cQuery +=     "RV_CODFOL "
        cQuery += "FROM "
        cQuery +=     RetSQLName("SRA") + " SRA "
        cQuery +=     "LEFT JOIN "
        cQuery +=         "( "
        cQuery +=             "SELECT "
        cQuery +=                 "RC_FILIAL, "
        cQuery +=                 "RC_CC, "
        cQuery +=                 "RC_MAT, "
        cQuery +=                 "RC_ROTEIR, "
        cQuery +=                 "RC_VALOR, "
        cQuery +=                 "RV_INCFGTS, "
        cQuery +=                 "RC_PD, "
        cQuery +=                 "RC_PROCES, "
        cQuery +=                 "RC_PERIODO, "
        cQuery +=                 "RC_SEMANA, "
        cQuery +=                 "? AS RR_DATA, " // " "
        cQuery +=                 "'SRC' AS TABELA, "
        cQuery +=                 "RV_TIPOCOD, "
        cQuery +=                 "RV_CONTRAP, "
        cQuery +=                 "RV_CODFOL "
        cQuery +=             "FROM "
        cQuery +=                 RetSQLName("SRC") + " SRC "
        cQuery +=                 "INNER JOIN "
        cQuery +=                     RetSQLName("SRV") + " SRV "
        cQuery +=                     "ON "
        cQuery +=                         FwJoinFilial("SRV", "SRC") + " "
        cQuery +=                         "AND RV_COD = RC_PD "
        cQuery +=                         "AND RV_INCFGTS NOT IN (?) " // {" ", "00"}
        cQuery +=                         "AND SRV.D_E_L_E_T_ = ? "    // " "
        cQuery +=             "WHERE "
        cQuery +=                 "RC_FILIAL BETWEEN ? AND ? "       // cBranchCodeFrom | cBranchCodeTo
        cQuery +=                 "AND RC_CC BETWEEN ? AND ? "       // cCostCenterCodeFrom | cCostCenterCodeTo
        cQuery +=                 "AND RC_PERIODO = ? "              // cPeriodo
        cQuery +=                 "AND RC_ROTEIR IN (?) "            // aRotAux
                                    // Ignora verba se existir RHH para ela
        cQuery +=                 "AND NOT EXISTS (SELECT 1 FROM " + RetSQLName("RHH") + " RHH WHERE " + FwJoinFilial("RHH", "SRC") + " AND RHH_MAT = RC_MAT AND RHH_MESANO = RC_PERIODO AND RHH_VERBA = RC_PD AND RHH.D_E_L_E_T_ = ?) " // " "
        cQuery +=                 "AND SRC.D_E_L_E_T_ = ? "          // " "
        cQuery +=             "UNION "
        cQuery +=             "SELECT "
        cQuery +=                 "RD_FILIAL, "
        cQuery +=                 "RD_CC, "
        cQuery +=                 "RD_MAT, "
        cQuery +=                 "RD_ROTEIR, "
        cQuery +=                 "RD_VALOR, "
        cQuery +=                 "RV_INCFGTS, "
        cQuery +=                 "RD_PD, "
        cQuery +=                 "RD_PROCES, "
        cQuery +=                 "RD_PERIODO, "
        cQuery +=                 "RD_SEMANA, "
        cQuery +=                 "? AS RR_DATA, " // " "
        cQuery +=                 "'SRD' AS TABELA, "
        cQuery +=                 "RV_TIPOCOD, "
        cQuery +=                 "RV_CONTRAP, "
        cQuery +=                 "RV_CODFOL "
        cQuery +=             "FROM "
        cQuery +=                 RetSQLName("SRD") + " SRD "
        cQuery +=                 "INNER JOIN "
        cQuery +=                     RetSQLName("SRV") + " SRV "
        cQuery +=                     "ON "
        cQuery +=                         FwJoinFilial("SRV", "SRD") + " "
        cQuery +=                         "AND RV_COD = RD_PD "
        cQuery +=                         "AND RV_INCFGTS NOT IN (?) " // {" ", "00"}
        cQuery +=                         "AND SRV.D_E_L_E_T_ = ? "    // " "
        cQuery +=             "WHERE "
        cQuery +=                 "RD_FILIAL BETWEEN ? AND ? "              // cBranchCodeFrom | cBranchCodeTo
        cQuery +=                 "AND RD_CC BETWEEN ? AND ? "              // cCostCenterCodeFrom | cCostCenterCodeTo
        cQuery +=                 "AND RD_PERIODO = ? "                     // cPeriodo
        cQuery +=                 "AND (RD_EMPRESA = ? OR RD_EMPRESA = ?) " // " " | cEmpAnt
        cQuery +=                 "AND RD_ROTEIR IN (?) "                   // aRotAux
                                    // Ignora verba se existir RHH para ela
        cQuery +=                 "AND NOT EXISTS (SELECT 1 FROM " + RetSQLName("RHH") + " RHH WHERE " + FwJoinFilial("RHH", "SRD") + " AND RHH_MAT = RD_MAT AND RHH_MESANO = RD_PERIODO AND RHH_VERBA = RD_PD AND RHH.D_E_L_E_T_ = ?) " // " "
        cQuery +=                 "AND SRD.D_E_L_E_T_ = ? "                 // " "
        cQuery +=             "UNION "
        cQuery +=             "SELECT "
        cQuery +=                 "RHH_FILIAL, "
        cQuery +=                 "RHH_CC, "
        cQuery +=                 "RHH_MAT, "
        cQuery +=                 "RHH_ROTEIR, "
        cQuery +=                 "RHH_VALOR, "
        cQuery +=                 "RV_INCFGTS, "
        cQuery +=                 "RHH_VERBA, "
        cQuery +=                 "RHH_PROCES, "
        cQuery +=                 "RHH_MESANO, "
        cQuery +=                 "RHH_SEMANA, "
        cQuery +=                 "? AS RR_DATA, " // " "
        cQuery +=                 "'RHH' AS TABELA, "
        cQuery +=                 "RV_TIPOCOD, "
        cQuery +=                 "RV_CONTRAP, "
        cQuery +=                 "RV_CODFOL "
        cQuery +=             "FROM "
        cQuery +=                 RetSQLName("RHH") + " RHH "
        cQuery +=                 "INNER JOIN "
        cQuery +=                     RetSQLName("SRV") + " SRV "
        cQuery +=                     "ON "
        cQuery +=                         FwJoinFilial("SRV", "RHH") + " "
        cQuery +=                         "AND RV_COD = RHH_VB "
        cQuery +=                         "AND RV_INCFGTS NOT IN (?) " // {" ", "00"}
        cQuery +=                         "AND SRV.D_E_L_E_T_ = ? "    // " "
        cQuery +=             "WHERE  "
        cQuery +=                 "RHH_FILIAL BETWEEN ? AND ? " // cBranchCodeFrom | cBranchCodeTo
        cQuery +=                 "AND RHH_MESANO = ? "         // cPeriodo
        cQuery +=                 "AND RHH_CC BETWEEN ? AND ? " // cCostCenterCodeFrom | cCostCenterCodeTo
        cQuery +=                 "AND RHH_INTEGR = ? "         // "S"
        cQuery +=                 "AND RHH_VB != ? "            // "000"
        cQuery +=                 "AND RHH_ROTEIR IN (?) "      // aRotAux
        cQuery +=                 "AND RHH.D_E_L_E_T_ = ? "     // " "
        cQuery +=             "UNION "
        cQuery +=             "SELECT "
        cQuery +=                 "RR_FILIAL, "
        cQuery +=                 "RR_CC, "
        cQuery +=                 "RR_MAT, "
        cQuery +=                 "RR_ROTEIR, "
        cQuery +=                 "RR_VALOR, "
        cQuery +=                 "RV_INCFGTS, "
        cQuery +=                 "RR_PD, "
        cQuery +=                 "RR_PROCES, "
        cQuery +=                 "RR_PERIODO, "
        cQuery +=                 "RR_SEMANA, "
        cQuery +=                 "RR_DATA, "
        cQuery +=                 "'SRR' AS TABELA, "
        cQuery +=                 "RV_TIPOCOD, "
        cQuery +=                 "RV_CONTRAP, "
        cQuery +=                 "RV_CODFOL "
        cQuery +=             "FROM "
        cQuery +=                 RetSQLName("SRR") + " SRR "
        cQuery +=                 "INNER JOIN "
        cQuery +=                     RetSQLName("SRV") + " SRV "
        cQuery +=                     "ON "
        cQuery +=                         FwJoinFilial("SRV", "SRR") + " "
        cQuery +=                         "AND RV_COD = RR_PD "
        cQuery +=                         "AND RV_INCFGTS NOT IN (?) " // {" ", "00"}
        cQuery +=                         "AND SRV.D_E_L_E_T_ = ? "    // " "
        cQuery +=             "WHERE "
        cQuery +=                 "RR_FILIAL BETWEEN ? AND ? " // cBranchCodeFrom | cBranchCodeTo
        cQuery +=                 "AND RR_CC BETWEEN ? AND ? " // cCostCenterCodeFrom | cCostCenterCodeTo
        cQuery +=                 "AND RR_PERIODO = ? "        // cPeriodo
        cQuery +=                 "AND RR_TIPO3 = ? "          // "R"
                                    // Ignora verba se existir RHH para ela
        cQuery +=                 "AND NOT EXISTS (SELECT 1 FROM " + RetSQLName("RHH") + " RHH WHERE " + FwJoinFilial("RHH", "SRR") + " AND RHH_MAT = RR_MAT AND RHH_MESANO = RR_PERIODO AND RHH_VERBA = RR_PD AND RHH.D_E_L_E_T_ = ?) " // " "
        cQuery +=                 "AND SRR.D_E_L_E_T_ = ? "    // " "
        cQuery +=         ") SRC "
        cQuery +=         "ON "
        cQuery +=             FwJoinFilial("SRC", "SRA") + " "
        cQuery +=             "AND RC_MAT = RA_MAT "
        cQuery +=     "LEFT JOIN "
        cQuery +=         RetSQLName("SRG") + " SRG "
        cQuery +=         "ON "
        cQuery +=             FwJoinFilial("SRG", "SRC") + " "
        cQuery +=             "AND RG_MAT = RC_MAT "
        cQuery +=             "AND RG_DTGERAR = RR_DATA "
        cQuery +=             "AND TABELA = ? " // "SRR"
        cQuery += "WHERE "
        cQuery +=     "RA_FILIAL BETWEEN ? AND ? "      // cBranchCodeFrom | cBranchCodeTo
        cQuery +=     "AND RA_CC BETWEEN ? AND ? "      // cCostCenterCodeFrom | cCostCenterCodeTo
        cQuery +=     "AND RA_CODUNIC BETWEEN ? AND ? " // ceSocialCodeFrom | ceSocialCodeTo

        If (!Empty(aeSocialCat))
            cQuery += "AND RA_CATEFD IN (?) " // aeSocialCat
        EndIf

        cQuery +=     "AND RA_MAT BETWEEN ? AND ? " // cEmployeeCodeFrom | cEmployeeCodeTo
        cQuery +=     "AND RC_FILIAL != ? "         // " "
        cQuery +=     "AND SRA.D_E_L_E_T_ = ? "     // " "
        cQuery += "ORDER BY "
        cQuery +=     "RC_FILIAL, "
        cQuery +=     "RC_MAT, "
        cQuery +=     "RV_INCFGTS "
        cQuery := ChangeQuery(cQuery)

        // Instancia a classe
        oStatement := FwExecStatement():New(cQuery)

        If (cPrintType == "1") // Folha de pagamento
            aRotSeek  := fGetRotTipo("1")
            aRotAuton := fGetRotTipo("9")
            aRot131   := fGetRotTipo("5")
        ElseIf (cPrintType == "2") // Anual (Segunda parcela do 13º salário)
            aRotSeek := fGetRotTipo("6")
        EndIf

        // Agrupa os vetores de roteiros
        aRotAux := {}
        AEval(aRotSeek,  {|x| AAdd(aRotAux, x)})
        AEval(aRotAuton, {|x| AAdd(aRotAux, x)})
        AEval(aRot131,   {|x| AAdd(aRotAux, x)})

        // INÍCIO - Definições dos valores dos parâmetros - SRC
            oStatement:SetString(nParamOrder++, " ")
            oStatement:SetIN(nParamOrder++, {" ", "00"})
            oStatement:SetString(nParamOrder++, " ")
            oStatement:SetString(nParamOrder++, cBranchCodeFrom)
            oStatement:SetString(nParamOrder++, cBranchCodeTo)
            oStatement:SetString(nParamOrder++, cCostCenterCodeFrom)
            oStatement:SetString(nParamOrder++, cCostCenterCodeTo)
            oStatement:SetString(nParamOrder++, cPeriodo)
            oStatement:SetIN(nParamOrder++, aRotAux)
            oStatement:SetString(nParamOrder++, " ")
            oStatement:SetString(nParamOrder++, " ")
        // FIM - Definições dos valores dos parâmetros - SRC

        // INÍCIO - Definições dos valores dos parâmetros - SRD
            oStatement:SetString(nParamOrder++, " ")
            oStatement:SetIN(nParamOrder++, {" ", "00"})
            oStatement:SetString(nParamOrder++, " ")
            oStatement:SetString(nParamOrder++, cBranchCodeFrom)
            oStatement:SetString(nParamOrder++, cBranchCodeTo)
            oStatement:SetString(nParamOrder++, cCostCenterCodeFrom)
            oStatement:SetString(nParamOrder++, cCostCenterCodeTo)
            oStatement:SetString(nParamOrder++, cPeriodo)
            oStatement:SetString(nParamOrder++, " ")
            oStatement:SetString(nParamOrder++, cEmpAnt)
            oStatement:SetIN(nParamOrder++, aRotAux)
            oStatement:SetString(nParamOrder++, " ")
            oStatement:SetString(nParamOrder++, " ")
        // FIM - Definições dos valores dos parâmetros - SRD

        // INÍCIO - Definições dos valores dos parâmetros - RHH
            oStatement:SetString(nParamOrder++, " ")
            oStatement:SetIN(nParamOrder++, {" ", "00"})
            oStatement:SetString(nParamOrder++, " ")
            oStatement:SetString(nParamOrder++, cBranchCodeFrom)
            oStatement:SetString(nParamOrder++, cBranchCodeTo)
            oStatement:SetString(nParamOrder++, cPeriodo)
            oStatement:SetString(nParamOrder++, cCostCenterCodeFrom)
            oStatement:SetString(nParamOrder++, cCostCenterCodeTo)
            oStatement:SetString(nParamOrder++, "S")
            oStatement:SetString(nParamOrder++, "000")
            oStatement:SetIN(nParamOrder++, aRotAux)
            oStatement:SetString(nParamOrder++, " ")
        // FIM - Definições dos valores dos parâmetros - RHH

        // INÍCIO - Definições dos valores dos parâmetros - SRR
            oStatement:SetIN(nParamOrder++, {" ", "00"})
            oStatement:SetString(nParamOrder++, " ")
            oStatement:SetString(nParamOrder++, cBranchCodeFrom)
            oStatement:SetString(nParamOrder++, cBranchCodeTo)
            oStatement:SetString(nParamOrder++, cCostCenterCodeFrom)
            oStatement:SetString(nParamOrder++, cCostCenterCodeTo)
            oStatement:SetString(nParamOrder++, cPeriodo)
            oStatement:SetString(nParamOrder++, "R")
            oStatement:SetString(nParamOrder++, " ")
            oStatement:SetString(nParamOrder++, " ")
        // FIM - Definições dos valores dos parâmetros - SRR

        // INÍCIO - Definições dos parâmetros do join da subquery com a SRA
            // oStatement:SetString(nParamOrder++, "RHH")
            // oStatement:SetIN(nParamOrder++, {"SRC", "SRD"})
            // oStatement:SetString(nParamOrder++, " ")
            // oStatement:SetString(nParamOrder++, "D")
            // oStatement:SetIN(nParamOrder++, {"30", "31", " "})
            // oStatement:SetString(nParamOrder++, "SRR")
            // oStatement:SetString(nParamOrder++, " ")
            // oStatement:SetString(nParamOrder++, "D")
            // oStatement:SetIN(nParamOrder++, {"30", "31"})
        // FIM - Definições dos parâmetros do join da subquery com a SRA

        // INÍCIO - Definições dos parâmetros do join da subquery com a SRG
            oStatement:SetString(nParamOrder++, "SRR")
        // FIM - Definições dos parâmetros do join da subquery com a SRG

        // INÍCIO - Definições dos parâmetros para a SRA
            oStatement:SetString(nParamOrder++, cBranchCodeFrom)
            oStatement:SetString(nParamOrder++, cBranchCodeTo)
            oStatement:SetString(nParamOrder++, cCostCenterCodeFrom)
            oStatement:SetString(nParamOrder++, cCostCenterCodeTo)
            oStatement:SetString(nParamOrder++, ceSocialCodeFrom)
            oStatement:SetString(nParamOrder++, ceSocialCodeTo)
            If (!Empty(aeSocialCat))
                oStatement:SetIN(nParamOrder++, aeSocialCat)
            EndIf
            oStatement:SetString(nParamOrder++, cEmployeeCodeFrom)
            oStatement:SetString(nParamOrder++, cEmployeeCodeTo)
            oStatement:SetString(nParamOrder++, " ")
            oStatement:SetString(nParamOrder++, " ")
        // FIM - Definições dos parâmetros para a SRA

        // Executa a query e retorna o alias criado
        cAlias := oStatement:OpenAlias()

        // Definição das regras de incidência de FGTS
        aRIncFGTS := RIncFGTS()

        // Definição das regras de tipo do FGTS
        aRTipoFGTS := RTipoFGTS()

        While (!(cAlias)->(EOF()))
            // Verifica se o usuário não tem acesso à filial
            If (cFil != (cAlias)->RC_FILIAL .and. AScan(aValidFil, {|x| AllTrim(x) == AllTrim((cAlias)->RC_FILIAL)}) <= 0)
                (cAlias)->(DBSkip())
                Loop
            EndIf

            // Converte a data de demissão em tipo data
            dDemissa := SToD((cAlias)->RA_DEMISSA)

            // Verifica se o funcionário está demitido
            lFuncDemi := IIf((cAlias)->RA_SITFOLH == "D" .and. !Empty((cAlias)->RA_DEMISSA) .and. !(cAlias)->RA_RESCRAI $ "30/31" .and. AnoMes(dDemissa) >= cPeriodo, .T., .F.)
            If (lFuncDemi)
                // Existem valores de dissídios gerados nos valores futuros de funcionário demitido, porem rescisão complementar não foi calculada, portanto valores não foram pagos
                If ((cAlias)->TABELA == "RHH" .and. SRK->(DBSeek((cAlias)->RC_FILIAL + (cAlias)->RC_MAT + (cAlias)->RC_PD + (cAlias)->RC_CC)))
                    If (SRK->RK_STATUS != "3")
                        (cAlias)->(DBSkip())
                        Loop
                    EndIf
                ElseIf ((cAlias)->TABELA == "SRR")
                
                    // Existem Valores de Dissídios gerados em Lançamentos por Funcionários
                    // Obs. Importante: Podem existir lançamentos oriundos da Integração, por isso deve-se validar que não existem em Lançamentos Futuros
                    // pois quando o dissídio lançar para Valores Futuros, ao integrar a Rescisão na Folha, os lançamentos irão para Valores Mensais
                    cRotRes := fGetCalcRot("4")
                    cRotFol := fGetCalcRot("1")

                    SRK->(DBSetOrder(1))
                    SRK->(dbGoTop())
                    nOrder := RetOrder("RGB", "RGB_FILIAL+RGB_PROCES+RGB_MAT+RGB_PERIOD+RGB_SEMANA+RGB_ROTEIR+RGB_PD+RGB_CC+RGB_SEQ+DTOS(RGB_DTREF)")
                    RGB->(DBSetOrder(nOrder))
                    RGB->(dbGoTop())
                    If (RGB->(DBSeek((cAlias)->RC_FILIAL + (cAlias)->RC_PROCES + (cAlias)->RC_MAT + (cAlias)->RC_PERIODO + (cAlias)->RC_SEMANA + cRotFol + (cAlias)->RC_PD + (cAlias)->RC_CC)) .and.;
                        !(SRK->(DBSeek((cAlias)->RC_FILIAL + (cAlias)->RC_MAT + (cAlias)->RC_PD + (cAlias)->RC_CC))))
                        If (AllTrim(RGB->RGB_ROTORI) == cRotRes)
                            (cAlias)->(DBSkip())
                            Loop
                        EndIf
                    EndIf

                // Condição necessária para evitar duplicidade em caso da rescisão estiver integrada com a folha, já que o relatório busca os registros da SRR
                ElseIf ((cAlias)->TABELA $ "SRC|SRD")
                    (cAlias)->(DBSkip())
                    Loop
                EndIf
            EndIf

            // Verifica se houve troca de filial
            If (cFil != (cAlias)->RC_FILIAL)
                // Captura as informações da filial
                fInfo(@aInfo, (cAlias)->RC_FILIAL)

                // Monta um JSON com as informações da filial
                jAux := JSONObject():New()
                jAux["BranchCode"]    := RTrim((cAlias)->RC_FILIAL)
                jAux["BranchName"]    := RTrim(aInfo[3])
                jAux["BranchCNPJ"]    := Transform(RTrim(aInfo[8]), IIf(Len(RTrim(aInfo[8])) == 11, "@R 999.999.999-99", "@!R NN.NNN.NNN/NNNN-99"))
                jAux["BranchAddress"] := RTrim(aInfo[4])
                jAux["BranchZipCode"] := Transform(RTrim(aInfo[7]), "@R #####-###")
                jAux["BranchCity"]    := RTrim(aInfo[5])
                jAux["BranchState"]   := RTrim(aInfo[6])
                jAux["FGTSMonthly"]   := {}
                jAux["FGTSDismissal"] := {}

                // Adiciona o registro no vetor de filiais e acrescenta o contador de filiais
                AAdd(jInfo["Branches"], jAux)
                nBranch++

                // Captura a alíquota do FGTS na tabela S037
                If (Len(aTabS037) > 0)
                    // Procura o registro com filial e com o mês de referência
                    If ((nPos := AScan(aTabS037, {|x| x[1] == "S037" .and. x[2] == (cAlias)->RC_FILIAL .and. x[3] == cPeriodo})) > 0)
                        nTaxFGTS := aTabS037[nPos][9]

                    // Procura o registro sem filial mas com o mesmo mês de referência
                    ElseIf ((nPos := AScan(aTabS037, {|x| x[1] == "S037" .and.x[2] == Space(Len((cAlias)->RC_FILIAL)) .and. x[3] == cPeriodo})) > 0)
                        nTaxFGTS := aTabS037[nPos][9]

                    // Procura o registro com filial e sem mês referência
                    ElseIf ((nPos := AScan(aTabS037, {|x| x[1] == "S037" .and. x[2] == (cAlias)->RC_FILIAL})) > 0)
                        nTaxFGTS := aTabS037[nPos][9]

                    // Procura o registro sem filial e sem mês referência
                    ElseIf ((nPos := AScan(aTabS037, {|x| x[1] == "S037" .and. x[2] == Space(Len((cAlias)->RC_FILIAL))})) > 0)
                        nTaxFGTS := aTabS037[nPos][9]
                    EndIf
                EndIf
            EndIf

            // Captura algumas informações do registro atual
            cFil         := (cAlias)->RC_FILIAL
            cIncFGTS     := AllTrim((cAlias)->RV_INCFGTS)
            cCatEFD      := AllTrim((cAlias)->RA_CATEFD)
            cPeriodoFGTS := IIf((cAlias)->TABELA == "RHH", "Anteriores", "Apuração")

            // Ignora o código de incidência 31 (Empréstimo eConsignado)
            If (cIncFGTS == "31")
                (cAlias)->(DBSkip())
                Loop
            EndIf

            // Verifica se o código de saque para o tipo de rescisão não está no JSON
            If (!jCodSaq:hasProperty((cAlias)->RG_TIPORES))
                jCodSaq[(cAlias)->RG_TIPORES] := fDescRCC("S043", (cAlias)->RG_TIPORES, 1, 2, 52, 2)
            EndIf

            // Captura o código de saque
            cCodSaq := jCodSaq[(cAlias)->RG_TIPORES]

            // Posiciona no funcionário para a funcção fM40TpRes()
            SRA->(DBGoTo((cAlias)->RECNO_SRA))

            // Verifica se o tipo de rescisão não está no JSON
            If (lFuncDemi .and. !jTipoRes:hasProperty((cAlias)->RG_TIPORES))
                // Limpa o vetor utilizado na função fM40TpRes()
                aIncRes := {}

                // Realiza um de/para da tipo da rescisão conforme o eSocial
                jTipoRes[(cAlias)->RG_TIPORES] := fM40TpRes((cAlias)->RG_TIPORES, NIL, NIL, dDemissa)
            EndIf

            // Captura o tipo de rescisão conforme eSocial
            cTipoRes := IIf(lFuncDemi, jTipoRes[(cAlias)->RG_TIPORES], "")

            // Calcula a data de vencimento do FGTS
            // O FGTS tem que ser recolhido até o dia 20 do mês subsequente, exceto as demissões que geram recolhimento e que ocorreram até o dia 9 (neste caso, recolhe antes do dia 20)
            dVencFGTS := DataValida(IIf(lFuncDemi .and. Day(dDemissa) <= 9 .and. cCodSaq $ "00|NA", DaySum(FirstDate(dDemissa), 19), DaySum(LastDate(dReferenceDate), 20)))

            // Verifica se o funcionário está demitido e a data de desligamento é diferente do período de referência ou é um funcionário sem vínculo (diferente de diretor não empregado) demitido
            // Neste caso, enquadra-se no evento 1200 e não pode se enquadrar nos eventos 2299 e 2399
            l1200 := AnoMes(dDemissa) != cPeriodo .or. (lFuncDemi .and. cCatEFD != "721" .and. cCatEFD $ cCatTSV)

            // Aplica as regras de tipo de FGTS conforme o MOS
            For nAux := 1 To Len(aRTipoFGTS)
                If (cIncFGTS $ aRTipoFGTS[nAux]["codIncFGTS"] .and. &(aRTipoFGTS[nAux]["regra"]))
                    cTipoFGTS := aRTipoFGTS[nAux]["tipoFGTS"]
                    EXIT
                EndIf
            Next nAux

            // Aplica as regras de incidência de FGTS conforme o MOS
            cTpValor := ""
            For nAux := 1 To Len(aRIncFGTS)
                If (cIncFGTS $ aRIncFGTS[nAux]["codIncFGTS"] .and. &(aRIncFGTS[nAux]["codCateg"]) .and. aRIncFGTS[nAux]["tipoFGTS"] == cTipoFGTS .and. aRIncFGTS[nAux]["periodo"] == cPeriodoFGTS)
                    cTpValor := aRIncFGTS[nAux]["tpValor"]
                    EXIT
                EndIf
            Next nAux

            // FGTS mensal
            If (cTipoFGTS == "Mensal")
                // Busca pelo funcionário, se não encontrar, insere um registro novo
                nPos := AScan(jInfo["Branches"][nBranch]["FGTSMonthly"], {|x| x["EmployeeCode"] == RTrim((cAlias)->RC_MAT)})
                If (nPos <= 0)
                    // Monta um JSON com as informações do funcionário
                    jAux := JSONObject():New()
                    jAux["EmployeeName"]       := RTrim(IIf(jLGPD["RA_NOME"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),    (cAlias)->RA_NOME))
                    jAux["EmployeeSocialName"] := RTrim(IIf(jLGPD["RA_NSOCIAL"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NSOCIAL), (cAlias)->RA_NSOCIAL))
                    jAux["EmployeeCode"]       := RTrim((cAlias)->RC_MAT)
                    jAux["eSocialCode"]        := RTrim((cAlias)->RA_CODUNIC)
                    jAux["Identification"]     := IIf(Len(RTrim((cAlias)->RA_CIC)) == 11, Transform((cAlias)->RA_CIC, "@R 999.999.999-99"), "")
                    jAux["Category"]           := RTrim((cAlias)->RA_CATEFD)
                    jAux["Dismissal"]          := IIf(!lFuncDemi, "", DToC(dDemissa))
                    jAux["TaxRate"]            := IIf(Empty((cAlias)->RA_PERFGTS), nTaxFGTS, (cAlias)->RA_PERFGTS)
                    jAux["BasesMonthly"]       := {}

                    // Adiciona o funcionário ao vetor de FGTS mensal
                    AAdd(jInfo["Branches"][nBranch]["FGTSMonthly"], jAux)

                    // Captura a posição do registro inserido
                    nPos := Len(jInfo["Branches"][nBranch]["FGTSMonthly"])
                EndIf

                // Busca pelo tipo de valor no vetor de bases, se não encontrar, insere um registro novo, se encontrar, atualiza os valores
                nPos2 := AScan(jInfo["Branches"][nBranch]["FGTSMonthly"][nPos]["BasesMonthly"], {|x| x["TpValue"] == cTpValor})
                If (nPos2 > 0)
                    jInfo["Branches"][nBranch]["FGTSMonthly"][nPos]["BasesMonthly"][nPos2]["BaseValue"] += IIf((cAlias)->RV_TIPOCOD $ "2,4", (cAlias)->RC_VALOR * -1, (cAlias)->RC_VALOR)
                    jInfo["Branches"][nBranch]["FGTSMonthly"][nPos]["BasesMonthly"][nPos2]["FGTS"]      := NoRound(jInfo["Branches"][nBranch]["FGTSMonthly"][nPos]["BasesMonthly"][nPos2]["BaseValue"] * jInfo["Branches"][nBranch]["FGTSMonthly"][nPos]["TaxRate"] / 100, 2)
                Else
                    AAdd(jInfo["Branches"][nBranch]["FGTSMonthly"][nPos]["BasesMonthly"], {;
                        "TpValue":     cTpValor,;
                        "TpValueDesc": IIf(jTpValor:hasProperty(cTpValor), jTpValor[cTpValor], ""),;
                        "BaseValue":   IIf((cAlias)->RV_TIPOCOD $ "2,4", (cAlias)->RC_VALOR * -1, (cAlias)->RC_VALOR),;
                        "FGTS":        NoRound(IIf((cAlias)->RV_TIPOCOD $ "2,4", (cAlias)->RC_VALOR * -1, (cAlias)->RC_VALOR) * jInfo["Branches"][nBranch]["FGTSMonthly"][nPos]["TaxRate"] / 100, 2);
                    })
                EndIf

                // Verifica se existe uma verba de contrapartida
                If (!Empty((cAlias)->RV_CONTRAP) .and. (cAlias)->RV_CODFOL $ "0224,0226,0227,0228,0925,0926")
                    DBSelectArea("SRV")
                    DBSetOrder(1) // RV_FILIAL + RV_COD
                    DBSeek(FwXFilial("SRV", (cAlias)->RC_FILIAL) + (cAlias)->RV_CONTRAP)

                    // Verifica se encontrou a verba de contrapartida e valida a incidência de FGTS
                    If (Found() .and. !Empty(RV_INCFGTS) .and. RV_INCFGTS != "00")
                        // Verifica se a incidência de FGTS da verba de contrapartida é diferente da verba "original"
                        // Se for, é necessário aplicar a regra do tipo de valor novamente
                        If (AllTrim(RV_INCFGTS) != cIncFGTS)
                            // Aplica as regras de tipo de valor conforme o MOS
                            cTpValor := ""
                            For nAux := 1 To Len(aRIncFGTS)
                                If (AllTrim(RV_INCFGTS) $ aRIncFGTS[nAux]["codIncFGTS"] .and. &(aRIncFGTS[nAux]["codCateg"]) .and. aRIncFGTS[nAux]["tipoFGTS"] == cTipoFGTS .and. aRIncFGTS[nAux]["periodo"] == cPeriodoFGTS)
                                    cTpValor := aRIncFGTS[nAux]["tpValor"]
                                    EXIT
                                EndIf
                            Next nAux
                        EndIf

                        // Busca pelo tipo de valor no vetor de bases, se não encontrar, insere um registro novo, se encontrar, atualiza os valores
                        nPos2 := AScan(jInfo["Branches"][nBranch]["FGTSMonthly"][nPos]["BasesMonthly"], {|x| x["TpValue"] == cTpValor})
                        If (nPos2 > 0)
                            jInfo["Branches"][nBranch]["FGTSMonthly"][nPos]["BasesMonthly"][nPos2]["BaseValue"] += IIf(RV_TIPOCOD $ "2,4", (cAlias)->RC_VALOR * -1, (cAlias)->RC_VALOR)
                            jInfo["Branches"][nBranch]["FGTSMonthly"][nPos]["BasesMonthly"][nPos2]["FGTS"]      := NoRound(jInfo["Branches"][nBranch]["FGTSMonthly"][nPos]["BasesMonthly"][nPos2]["BaseValue"] * jInfo["Branches"][nBranch]["FGTSMonthly"][nPos]["TaxRate"] / 100, 2)
                        Else
                            AAdd(jInfo["Branches"][nBranch]["FGTSMonthly"][nPos]["BasesMonthly"], {;
                                "TpValue":     cTpValor,;
                                "TpValueDesc": IIf(jTpValor:hasProperty(cTpValor), jTpValor[cTpValor], ""),;
                                "BaseValue":   IIf(RV_TIPOCOD $ "2,4", (cAlias)->RC_VALOR * -1, (cAlias)->RC_VALOR),;
                                "FGTS":        NoRound(IIf(RV_TIPOCOD $ "2,4", (cAlias)->RC_VALOR * -1, (cAlias)->RC_VALOR) * jInfo["Branches"][nBranch]["FGTSMonthly"][nPos]["TaxRate"] / 100, 2);
                            })
                        EndIf
                    EndIf
                EndIf

            // FGTS rescisório
            ElseIf (cTipoFGTS == "Rescisório")
                // Busca pelo funcionário, se não encontrar, insere um registro novo
                nPos := AScan(jInfo["Branches"][nBranch]["FGTSDismissal"], {|x| x["EmployeeCode"] == RTrim((cAlias)->RC_MAT)})
                If (nPos <= 0)
                    // Monta um JSON com as informações do funcionário
                    jAux := JSONObject():New()
                    jAux["EmployeeName"]       := RTrim(IIf(jLGPD["RA_NOME"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),    (cAlias)->RA_NOME))
                    jAux["EmployeeSocialName"] := RTrim(IIf(jLGPD["RA_NSOCIAL"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NSOCIAL), (cAlias)->RA_NSOCIAL))
                    jAux["EmployeeCode"]       := RTrim((cAlias)->RC_MAT)
                    jAux["eSocialCode"]        := RTrim((cAlias)->RA_CODUNIC)
                    jAux["Identification"]     := IIf(Len(RTrim((cAlias)->RA_CIC)) == 11, Transform((cAlias)->RA_CIC, "@R 999.999.999-99"), "")
                    jAux["Category"]           := RTrim((cAlias)->RA_CATEFD)
                    jAux["Dismissal"]          := IIf(!lFuncDemi, "", DToC(dDemissa))
                    jAux["TaxRate"]            := IIf(Empty((cAlias)->RA_PERFGTS), nTaxFGTS, (cAlias)->RA_PERFGTS)
                    jAux["BasesDismissal"]     := {}

                    // Adiciona o funcionário ao vetor de FGTS rescisório
                    AAdd(jInfo["Branches"][nBranch]["FGTSDismissal"], jAux)

                    // Captura a posição do registro inserido
                    nPos := Len(jInfo["Branches"][nBranch]["FGTSDismissal"])
                EndIf

                // Busca pelo tipo de valor no vetor de bases, se não encontrar, insere um registro novo, se encontrar, atualiza os valores
                nPos2 := AScan(jInfo["Branches"][nBranch]["FGTSDismissal"][nPos]["BasesDismissal"], {|x| x["TpValue"] == cTpValor})
                If (nPos2 > 0)
                    jInfo["Branches"][nBranch]["FGTSDismissal"][nPos]["BasesDismissal"][nPos2]["BaseValue"] += IIf((cAlias)->RV_TIPOCOD $ "2,4", (cAlias)->RC_VALOR * -1, (cAlias)->RC_VALOR)
                    jInfo["Branches"][nBranch]["FGTSDismissal"][nPos]["BasesDismissal"][nPos2]["FGTS"]      := NoRound(jInfo["Branches"][nBranch]["FGTSDismissal"][nPos]["BasesDismissal"][nPos2]["BaseValue"] * jInfo["Branches"][nBranch]["FGTSDismissal"][nPos]["TaxRate"] / 100, 2)
                Else
                    AAdd(jInfo["Branches"][nBranch]["FGTSDismissal"][nPos]["BasesDismissal"], {;
                        "TpValue":     cTpValor,;
                        "TpValueDesc": IIf(jTpValor:hasProperty(cTpValor), jTpValor[cTpValor], ""),;
                        "BaseValue":   IIf((cAlias)->RV_TIPOCOD $ "2,4", (cAlias)->RC_VALOR * -1, (cAlias)->RC_VALOR),;
                        "FGTS":        NoRound(IIf((cAlias)->RV_TIPOCOD $ "2,4", (cAlias)->RC_VALOR * -1, (cAlias)->RC_VALOR) * jInfo["Branches"][nBranch]["FGTSDismissal"][nPos]["TaxRate"] / 100, 2);
                    })
                EndIf

                // Verifica se existe uma verba de contrapartida
                If (!Empty((cAlias)->RV_CONTRAP) .and. (cAlias)->RV_CODFOL $ "0224,0226,0227,0228,0925,0926")
                    DBSelectArea("SRV")
                    DBSetOrder(1) // RV_FILIAL + RV_COD
                    DBSeek(FwXFilial("SRV", (cAlias)->RC_FILIAL) + (cAlias)->RV_CONTRAP)

                    // Verifica se encontrou a verba de contrapartida e valida a incidência de FGTS
                    If (Found() .and. !Empty(RV_INCFGTS) .and. RV_INCFGTS != "00")
                        // Verifica se a incidência de FGTS da verba de contrapartida é diferente da verba "original"
                        // Se for, é necessário aplicar a regra do tipo de valor novamente
                        If (AllTrim(RV_INCFGTS) != cIncFGTS)
                            // Aplica as regras de tipo de valor conforme o MOS
                            cTpValor := ""
                            For nAux := 1 To Len(aRIncFGTS)
                                If (AllTrim(RV_INCFGTS) $ aRIncFGTS[nAux]["codIncFGTS"] .and. &(aRIncFGTS[nAux]["codCateg"]) .and. aRIncFGTS[nAux]["tipoFGTS"] == cTipoFGTS .and. aRIncFGTS[nAux]["periodo"] == cPeriodoFGTS)
                                    cTpValor := aRIncFGTS[nAux]["tpValor"]
                                    EXIT
                                EndIf
                            Next nAux
                        EndIf

                        // Busca pelo tipo de valor no vetor de bases, se não encontrar, insere um registro novo, se encontrar, atualiza os valores
                        nPos2 := AScan(jInfo["Branches"][nBranch]["FGTSDismissal"][nPos]["BasesDismissal"], {|x| x["TpValue"] == cTpValor})
                        If (nPos2 > 0)
                            jInfo["Branches"][nBranch]["FGTSDismissal"][nPos]["BasesDismissal"][nPos2]["BaseValue"] += IIf(RV_TIPOCOD $ "2,4", (cAlias)->RC_VALOR * -1, (cAlias)->RC_VALOR)
                            jInfo["Branches"][nBranch]["FGTSDismissal"][nPos]["BasesDismissal"][nPos2]["FGTS"]      := NoRound(jInfo["Branches"][nBranch]["FGTSDismissal"][nPos]["BasesDismissal"][nPos2]["BaseValue"] * jInfo["Branches"][nBranch]["FGTSDismissal"][nPos]["TaxRate"] / 100, 2)
                        Else
                            AAdd(jInfo["Branches"][nBranch]["FGTSDismissal"][nPos]["BasesDismissal"], {;
                                "TpValue":     cTpValor,;
                                "TpValueDesc": IIf(jTpValor:hasProperty(cTpValor), jTpValor[cTpValor], ""),;
                                "BaseValue":   IIf(RV_TIPOCOD $ "2,4", (cAlias)->RC_VALOR * -1, (cAlias)->RC_VALOR),;
                                "FGTS":        NoRound(IIf(RV_TIPOCOD $ "2,4", (cAlias)->RC_VALOR * -1, (cAlias)->RC_VALOR) * jInfo["Branches"][nBranch]["FGTSDismissal"][nPos]["TaxRate"] / 100, 2);
                            })
                        EndIf
                    EndIf
                EndIf
            EndIf

            // Pula para o próximo registro
            (cAlias)->(DBSkip())
        End
    END SEQUENCE

    // Fecha a tabela temporária
    (cAlias)->(DBCloseArea())

    // Libera os objetos e vetores da memória
    FwFreeArray(aValidFil)
    FwFreeArray(aInfo)
    FwFreeArray(aRotAux)
    FwFreeArray(aRotSeek)
    FwFreeArray(aRotAuton)
    FwFreeArray(aRot131)
    FwFreeArray(aRIncFGTS)
    FwFreeArray(aRTipoFGTS)
    FwFreeArray(aPDFieldsNR)
    FwFreeArray(aFieldsNR)
    FwFreeArray(aTabS037)
    FwFreeObj(oStatement)
    FwFreeObj(jCodSaq)
    FwFreeObj(jTipoRes)
    FwFreeObj(jLGPD)
    FwFreeObj(jTpValor)
    jAux := NIL
    FwFreeObj(jAux)
Return NIL

/*/{Protheus.doc} GPER082TReportsBusinessObject::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 14/01/2025
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() As Variant Class GPER082TReportsBusinessObject
    // Declaração das variáveis locais
    Local jPath     As JSON    // Path parameters da requisição
    Local jResponse As JSON    // Resposta da requisição
    Local jItem     As JSON    // Item do valor do parâmetro
    Local nValues   As Numeric // Contador dos valores do parâmetro
    Local aValues   As Array   // Valores do parâmetro

    // Inicialização das variáveis
    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Verifica qual parâmetro foi solicitado
    If (cParam == "PrintType")
        AAdd(aValues, FwHTTPEncode(STR0047)) // "Mensal"
        AAdd(aValues, FwHTTPEncode(STR0048)) // "Anual"
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL

/*/{Protheus.doc} RIncFGTS
    Monta o vetor de regras de incidência de FGTS conforme MOS (Manual de Orientação do eSocial).
    @type Function
    @version 12.1.2410
    @author arthur.sales
    @since 16/01/2025
    @return Array, Regras de incidência de FGTS
/*/
Static Function RIncFGTS() As Array
    // Declaração das variáveis locais
    Local aRIncFGTS As Array // Vetor de regras de incidência de FGTS

    // Incialização das variáveis
    aRIncFGTS := {}

    // FGTS mensal
    AAdd(aRIncFGTS, {;
        "tpValor": "11",;
        "codIncFGTS": "11,21,91,93",;
        "codCateg": "!cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Mensal",;
        "periodo": "Apuração";
    })

    // FGTS 13° salário
    AAdd(aRIncFGTS, {;
        "tpValor": "12",;
        "codIncFGTS": "12,92",;
        "codCateg": "!cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Mensal",;
        "periodo": "Apuração";
    })

    // FGTS (período anterior) mensal
    AAdd(aRIncFGTS, {;
        "tpValor": "13",;
        "codIncFGTS": "11,21,91,93",;
        "codCateg": "!cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Mensal",;
        "periodo": "Anteriores";
    })

    // FGTS (período anterior) 13º salário
    AAdd(aRIncFGTS, {;
        "tpValor": "14",;
        "codIncFGTS": "12,92",;
        "codCateg": "!cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Mensal",;
        "periodo": "Anteriores";
    })

    // FGTS mensal - Aprendiz/Contrato Verde e Amarelo
    AAdd(aRIncFGTS, {;
        "tpValor": "15",;
        "codIncFGTS": "11,21,91,93",;
        "codCateg": "cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Mensal",;
        "periodo": "Apuração";
    })

    // FGTS 13° salário - Aprendiz/Contrato Verde e Amarelo
    AAdd(aRIncFGTS, {;
        "tpValor": "16",;
        "codIncFGTS": "12,92",;
        "codCateg": "cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Mensal",;
        "periodo": "Apuração";
    })

    // FGTS (período anterior) mensal - Aprendiz/Contrato Verde e Amarelo
    AAdd(aRIncFGTS, {;
        "tpValor": "17",;
        "codIncFGTS": "11,21,91,93",;
        "codCateg": "cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Mensal",;
        "periodo": "Anteriores";
    })

    // FGTS (período anterior) 13º salário - Aprendiz/Contrato Verde e Amarelo
    AAdd(aRIncFGTS, {;
        "tpValor": "18",;
        "codIncFGTS": "12,92",;
        "codCateg": "cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Mensal",;
        "periodo": "Anteriores";
    })

    // FGTS mês da rescisão
    AAdd(aRIncFGTS, {;
        "tpValor": "21",;
        "codIncFGTS": "11,91",;
        "codCateg": "!cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Apuração";
    })

    // FGTS 13° salário rescisório
    AAdd(aRIncFGTS, {;
        "tpValor": "22",;
        "codIncFGTS": "12,92",;
        "codCateg": "!cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Apuração";
    })

    // FGTS aviso prévio indenizado
    AAdd(aRIncFGTS, {;
        "tpValor": "23",;
        "codIncFGTS": "21,93",;
        "codCateg": "!cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Apuração";
    })

    // FGTS (período anterior) mês da rescisão
    AAdd(aRIncFGTS, {;
        "tpValor": "24",;
        "codIncFGTS": "11,91",;
        "codCateg": "!cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Anteriores";
    })

    // FGTS (período anterior) 13º salário rescisório
    AAdd(aRIncFGTS, {;
        "tpValor": "25",;
        "codIncFGTS": "12,92",;
        "codCateg": "!cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Anteriores";
    })

    // FGTS (período anterior) aviso prévio indenizado
    AAdd(aRIncFGTS, {;
        "tpValor": "26",;
        "codIncFGTS": "21,93",;
        "codCateg": "!cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Anteriores";
    })

    // FGTS mês da rescisão - Aprendiz/Contrato Verde e Amarelo
    AAdd(aRIncFGTS, {;
        "tpValor": "27",;
        "codIncFGTS": "11,91",;
        "codCateg": "cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Apuração";
    })

    // FGTS 13° salário rescisório - Aprendiz/Contrato Verde e Amarelo
    AAdd(aRIncFGTS, {;
        "tpValor": "28",;
        "codIncFGTS": "12,92",;
        "codCateg": "cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Apuração";
    })

    // FGTS aviso prévio indenizado - Aprendiz/Contrato Verde e Amarelo
    AAdd(aRIncFGTS, {;
        "tpValor": "29",;
        "codIncFGTS": "21,93",;
        "codCateg": "cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Apuração";
    })

    // FGTS (período anterior) mês da rescisão - Aprendiz/Contrato Verde e Amarelo
    AAdd(aRIncFGTS, {;
        "tpValor": "30",;
        "codIncFGTS": "11,91",;
        "codCateg": "cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Anteriores";
    })

    // FGTS (período anterior) 13° salário rescisório - Aprendiz/Contrato Verde e Amarelo
    AAdd(aRIncFGTS, {;
        "tpValor": "31",;
        "codIncFGTS": "12,92",;
        "codCateg": "cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Anteriores";
    })

    // FGTS (período anterior) aviso prévio indenizado - Aprendiz/Contrato Verde e Amarelo
    AAdd(aRIncFGTS, {;
        "tpValor": "32",;
        "codIncFGTS": "21,93",;
        "codCateg": "cCatEFD $ '103,107,108'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Anteriores";
    })

    // FGTS mensal - Indenização compensatória do empregado doméstico
    AAdd(aRIncFGTS, {;
        "tpValor": "41",;
        "codIncFGTS": "11,21,91,93",;
        "codCateg": "cCatEFD == '104'",;
        "tipoFGTS": "Mensal",;
        "periodo": "Apuração";
    })

    // FGTS 13° salário - Indenização compensatória do empregado doméstico
    AAdd(aRIncFGTS, {;
        "tpValor": "42",;
        "codIncFGTS": "12,92",;
        "codCateg": "cCatEFD == '104'",;
        "tipoFGTS": "Mensal",;
        "periodo": "Apuração";
    })

    // FGTS (período anterior) mensal - Indenização compensatória do empregado doméstico
    AAdd(aRIncFGTS, {;
        "tpValor": "43",;
        "codIncFGTS": "11,21,91,93",;
        "codCateg": "cCatEFD == '104'",;
        "tipoFGTS": "Mensal",;
        "periodo": "Anteriores";
    })

    // FGTS (período anterior) 13º salário - Indenização compensatória do empregado doméstico
    AAdd(aRIncFGTS, {;
        "tpValor": "44",;
        "codIncFGTS": "12,92",;
        "codCateg": "cCatEFD == '104'",;
        "tipoFGTS": "Mensal",;
        "periodo": "Anteriores";
    })

    // FGTS mês da rescisão - Indenização compensatória do empregado doméstico
    AAdd(aRIncFGTS, {;
        "tpValor": "45",;
        "codIncFGTS": "11,91",;
        "codCateg": "cCatEFD == '104'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Apuração";
    })

    // FGTS 13° salário rescisório - Indenização compensatória do empregado doméstico
    AAdd(aRIncFGTS, {;
        "tpValor": "46",;
        "codIncFGTS": "12,92",;
        "codCateg": "cCatEFD == '104'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Apuração";
    })

    // FGTS aviso prévio indenizado - Indenização compensatória do empregado doméstico
    AAdd(aRIncFGTS, {;
        "tpValor": "47",;
        "codIncFGTS": "21,93",;
        "codCateg": "cCatEFD == '104'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Apuração";
    })

    // FGTS (período anterior) mês da rescisão - Indenização compensatória do empregado doméstico
    AAdd(aRIncFGTS, {;
        "tpValor": "48",;
        "codIncFGTS": "11,91",;
        "codCateg": "cCatEFD == '104'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Anteriores";
    })

    // FGTS (período anterior) 13º salário rescisório - Indenização compensatória do empregado doméstico
    AAdd(aRIncFGTS, {;
        "tpValor": "49",;
        "codIncFGTS": "12,92",;
        "codCateg": "cCatEFD == '104'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Anteriores";
    })

    // FGTS (período anterior) aviso prévio indenizado - Indenização compensatória do empregado doméstico
    AAdd(aRIncFGTS, {;
        "tpValor": "50",;
        "codIncFGTS": "21,93",;
        "codCateg": "cCatEFD == '104'",;
        "tipoFGTS": "Rescisório",;
        "periodo": "Anteriores";
    })
Return aRIncFGTS

/*/{Protheus.doc} RTipoFGTS
    Monta o vetor de regras de tipo de FGTS conforme MOS (Manual de Orientação do eSocial).
    @type Function
    @version 12.1.2410
    @author arthur.sales
    @since 17/01/2025
    @return Array, Regras de tipo de FGTS
/*/
Static Function RTipoFGTS() As Array
    // Declaração das variáveis locais
    Local aRTipoFGTS As Array // Regras do tipo de FGTS

    // Inicialização das variáveis
    aRTipoFGTS := {}

    // INÍCIO - Incidência de FGTS mensal
        // Evento S-1200
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "11,91",;
            "regra": "l1200",;
            "tipoFGTS": "Mensal";
        })

        // Evento S-2299 de empregado desligado por motivo que não gera guia de FGTS rescisório
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "11,91",;
            "regra": "lFuncDemi .and. cCatEFD $ cCatTCV .and. !cTipoRes $ '02,03,05,06,14,17,23,26,27,33'",;
            "tipoFGTS": "Mensal";
        })

        // Evento S-2299 de empregado desligado por motivo que gera guia de FGTS rescisório desde que a data de desligamento acrescida de 10 dias seja posterior à data de vencimento do FGTS mensal
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "11,91",;
            "regra": "lFuncDemi .and. cCatEFD $ cCatTCV .and. cTipoRes $ '02,03,05,06,14,17,23,26,27,33' .and. dDemissa + 10 > dVencFGTS",;
            "tipoFGTS": "Mensal";
        })

        // Evento S-2399 de diretor não empregado com FGTS por motivo que não gera guia de FGTS rescisório
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "11,91",;
            "regra": "lFuncDemi .and. cCatEFD == '721' .and. !cTipoRes $ '01,02,04,06'",;
            "tipoFGTS": "Mensal";
        })

        // Evento S-2399 de diretor não empregado com FGTS por motivo que gera guia de FGTS rescisório desde que a data de término acrescida de 10 dias seja posterior à data de vencimento do FGTS mensal
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "11,91",;
            "regra": "lFuncDemi .and. cCatEFD == '721' .and. cTipoRes $ '01,02,04,06' .and. dDemissa + 10 > dVencFGTS",;
            "tipoFGTS": "Mensal";
        })

        // Evento S-2299 de empregado desligado por motivo que gera guia de FGTS rescisório desde que a data de desligamento acrescida de 10 dias seja igual ou anterior à data de vencimento do FGTS mensal
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "11,91",;
            "regra": "lFuncDemi .and. cCatEFD $ cCatTCV .and. cTipoRes $ '02,03,05,06,14,17,23,26,27,33' .and. dDemissa + 10 <= dVencFGTS",;
            "tipoFGTS": "Rescisório";
        })

        // Evento S-2399 de diretor não empregado com FGTS por motivo que gera guia de FGTS rescisório desde que a data de término acrescida de 10 dias seja igual ou anterior à data de vencimento do FGTS mensal
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "11,91",;
            "regra": "lFuncDemi .and. cCatEFD == '721' .and. !cTipoRes $ '01,02,04,06' .and. dDemissa + 10 <= dVencFGTS",;
            "tipoFGTS": "Rescisório";
        })
    // FIM - Incidência de FGTS mensal

    // INÍCIO - Incidências de FGTS para 13º salário
        // Evento S-1200
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "12,92",;
            "regra": "l1200",;
            "tipoFGTS": "Mensal";
        })

        // Evento S-2299 de empregado desligado por motivo que não gera guia de FGTS rescisório
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "12,92",;
            "regra": "lFuncDemi .and. cCatEFD $ cCatTCV .and. !cTipoRes $ '02,03,05,06,14,17,23,26,27,33'",;
            "tipoFGTS": "Mensal";
        })

        // Evento S-2399 de diretor não empregado com FGTS por motivo que não gera guia de FGTS rescisório
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "12,92",;
            "regra": "lFuncDemi .and. cCatEFD == '721' .and. !cTipoRes $ '01,02,04,06'",;
            "tipoFGTS": "Mensal";
        })

        // Evento S-2299 de empregado desligado por motivo que gera guia de FGTS rescisório independentemente da data de vencimento do FGTS mensal
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "12,92",;
            "regra": "lFuncDemi .and. cCatEFD $ cCatTCV .and. cTipoRes $ '02,03,05,06,14,17,23,26,27,33'",;
            "tipoFGTS": "Rescisório";
        })

        // Evento S-2399 de diretor não empregado com FGTS por motivo que gera guia de FGTS rescisório independentemente da data de vencimento do FGTS mensal
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "12,92",;
            "regra": "lFuncDemi .and. cCatEFD == '721' .and. cTipoRes $ '01,02,04,06'",;
            "tipoFGTS": "Rescisório";
        })
    // FIM - Incidências de FGTS para 13º salário

    // INÍCIO - Verificação de FGTS indenizado suspenso decorrente de ação judicial
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "93",;
            "regra": ".T.",;
            "tipoFGTS": "Rescisório";
        })
    // FIM - Verificação de FGTS indenizado suspenso decorrente de ação judicial

    // INÍCIO - Verificação de FGTS indenizado
        // Evento S-1200
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "21",;
            "regra": "l1200",;
            "tipoFGTS": "Mensal";
        })

        // Evento S-2299 de empregado desligado por motivo que não gera guia de FGTS rescisório
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "21",;
            "regra": "lFuncDemi .and. cCatEFD $ cCatTCV .and. !cTipoRes $ '02,03,05,06,14,17,23,26,27,33'",;
            "tipoFGTS": "Mensal";
        })

        // Evento S-2399 de diretor não empregado com FGTS por motivo que não gera guia de FGTS rescisório
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "21",;
            "regra": "lFuncDemi .and. cCatEFD == '721' .and. !cTipoRes $ '01,02,04,06'",;
            "tipoFGTS": "Mensal";
        })

        // "Else"
        AAdd(aRTipoFGTS, {;
            "codIncFGTS": "21",;
            "regra": ".T.",;
            "tipoFGTS": "Rescisório";
        })
    // FIM - Verificação de FGTS indenizado
Return aRTipoFGTS

/*/{Protheus.doc} RTipoFGTS
    Monta um JSON com a descrição dos tipos de valores que influem na apuração do FGTS, conforme documentação do eSocial.
    @type Function
    @version 12.1.2410
    @author arthur.sales
    @since 17/01/2025
    @return Array, Regras de tipo de FGTS
/*/
Static Function TipoValor() As JSON
    // Declaração das variáveis locais
    Local jTpValor As JSON

    // Inicialização das variáveis
    jTpValor := JSONObject():New()

    // Monta o JSON com a descrição dos tipos de valores que influem na apuração do FGTS
    jTpValor["11"] := STR0049 // FGTS mensal
    jTpValor["12"] := STR0050 // FGTS 13° salário
    jTpValor["13"] := STR0051 // FGTS (período anterior) mensal
    jTpValor["14"] := STR0052 // FGTS (período anterior) 13º salário
    jTpValor["15"] := STR0053 // FGTS mensal - Aprendiz/Contrato Verde e Amarelo
    jTpValor["16"] := STR0054 // FGTS 13° salário - Aprendiz/Contrato Verde e Amarelo
    jTpValor["17"] := STR0055 // FGTS (período anterior) mensal - Aprendiz/Contrato Verde e Amarelo
    jTpValor["18"] := STR0056 // FGTS (período anterior) 13º salário - Aprendiz/Contrato Verde e Amarelo
    jTpValor["21"] := STR0057 // FGTS mês da rescisão
    jTpValor["22"] := STR0058 // FGTS 13° salário rescisório
    jTpValor["23"] := STR0059 // FGTS aviso prévio indenizado
    jTpValor["24"] := STR0060 // FGTS (período anterior) mês da rescisão
    jTpValor["25"] := STR0061 // FGTS (período anterior) 13º salário rescisório
    jTpValor["26"] := STR0062 // FGTS (período anterior) aviso prévio indenizado
    jTpValor["27"] := STR0063 // FGTS mês da rescisão - Aprendiz/Contrato Verde e Amarelo
    jTpValor["28"] := STR0064 // FGTS 13° salário rescisório - Aprendiz/Contrato Verde e Amarelo
    jTpValor["29"] := STR0065 // FGTS aviso prévio indenizado - Aprendiz/Contrato Verde e Amarelo
    jTpValor["30"] := STR0066 // FGTS (período anterior) mês da rescisão - Aprendiz/Contrato Verde e Amarelo
    jTpValor["31"] := STR0067 // FGTS (período anterior) 13° salário rescisório - Aprendiz/Contrato Verde e Amarelo
    jTpValor["32"] := STR0068 // FGTS (período anterior) aviso prévio indenizado - Aprendiz/Contrato Verde e Amarelo
    jTpValor["41"] := STR0069 // FGTS mensal - Indenização compensatória do empregado doméstico
    jTpValor["42"] := STR0070 // FGTS 13° salário - Indenização compensatória do empregado doméstico
    jTpValor["43"] := STR0071 // FGTS (período anterior) mensal - Indenização compensatória do empregado doméstico
    jTpValor["44"] := STR0072 // FGTS (período anterior) 13º salário - Indenização compensatória do empregado doméstico
    jTpValor["45"] := STR0073 // FGTS mês da rescisão - Indenização compensatória do empregado doméstico
    jTpValor["46"] := STR0074 // FGTS 13° salário rescisório - Indenização compensatória do empregado doméstico
    jTpValor["47"] := STR0075 // FGTS aviso prévio indenizado - Indenização compensatória do empregado doméstico
    jTpValor["48"] := STR0076 // FGTS (período anterior) mês da rescisão - Indenização compensatória do empregado doméstico
    jTpValor["49"] := STR0077 // FGTS (período anterior) 13º salário rescisório - Indenização compensatória do empregado doméstico
    jTpValor["50"] := STR0078 // FGTS (período anterior) aviso prévio indenizado - Indenização compensatória do empregado doméstico
Return jTpValor

/*/{Protheus.doc} RTipoFGTS
    Totaliza as bases de FGTS Mensal e Rescisorio em um só vetor.
    @type Function
    @version 12.1.2410
    @author arthur.sales
    @since 22/01/2025
    @param jInfo, JSON, Informações do objeto de negócio (referência)
    @return Array, Regras de tipo de FGTS
/*/
Static Function GetTotal(jInfo As JSON) As Variant
    // Declaração das variáveis locais
    Local nBranch   As Numeric // Contador de filiais
    Local nEmployee As Numeric // Contador de funcionários
    Local nBase     As Numeric // Contador de bases
    Local nPos      As Numeric // Auxiliar de busca em vetor
    Local jBranch   As JSON    // JSON para a filial
    Local jEmployee As JSON    // JSON para o funcionário
    Local jBase     As JSON    // JSON para a base

    // Inicialização das variáveis
    nBranch   := 0
    nEmployee := 0
    nBase     := 0
    nPos      := 0
    jBranch   := NIL
    jEmployee := NIL
    jBase     := NIL

    // Cria o atributo totalizador de bases
    jInfo["TotalBases"] := {}

    For nBranch := 1 To Len(jInfo["Branches"])
        jBranch := jInfo["Branches"][nBranch]

        // Percorre os funcionário de FGTS Mensal e as bases dos mesmos
        For nEmployee := 1 To Len(jBranch["FGTSMonthly"])
            jEmployee := jBranch["FGTSMonthly"][nEmployee]

            For nBase := 1 To Len(jEmployee["BasesMonthly"])
                jBase := jEmployee["BasesMonthly"][nBase]

                // Busca pelo tipo de valor no vetor totalizador de bases, se não encontrar, insere um registro novo, se encontrar, atualiza os valores
                nPos := AScan(jInfo["TotalBases"], {|x| x["TpValue"] == jBase["TpValue"] .and. x["Branch"] == jInfo["Branches"][nBranch]["BranchCode"]})
                If (nPos > 0)
                    jInfo["TotalBases"][nPos]["BaseValue"] += jBase["BaseValue"]
                    jInfo["TotalBases"][nPos]["FGTS"]      := NoRound(jInfo["TotalBases"][nPos]["BaseValue"] * jEmployee["TaxRate"] / 100, 2)
                Else
                    AAdd(jInfo["TotalBases"], {;
                        "Branch":      jBranch["BranchCode"],;
                        "TpValue":     jBase["TpValue"],;
                        "TpValueDesc": jBase["TpValueDesc"],;
                        "BaseValue":   jBase["BaseValue"],;
                        "FGTS":        NoRound(jBase["BaseValue"] * jEmployee["TaxRate"] / 100, 2);
                    })
                EndIf
            Next nBase
        Next nEmployee

        // Percorre os funcionário de FGTS Rescisório e as bases dos mesmos
        For nEmployee := 1 To Len(jBranch["FGTSDismissal"])
            jEmployee := jBranch["FGTSDismissal"][nEmployee]

            For nBase := 1 To Len(jEmployee["BasesDismissal"])
                jBase := jEmployee["BasesDismissal"][nBase]

                // Busca pelo tipo de valor no vetor totalizador de bases, se não encontrar, insere um registro novo, se encontrar, atualiza os valores
                nPos := AScan(jInfo["TotalBases"], {|x| x["TpValue"] == jBase["TpValue"] .and. x["Branch"] == jInfo["Branches"][nBranch]["BranchCode"]})
                If (nPos > 0)
                    jInfo["TotalBases"][nPos]["BaseValue"] += jBase["BaseValue"]
                    jInfo["TotalBases"][nPos]["FGTS"]      := NoRound(jInfo["TotalBases"][nPos]["BaseValue"] * jEmployee["TaxRate"] / 100, 2)
                Else
                    AAdd(jInfo["TotalBases"], {;
                        "Branch":      jBranch["BranchCode"],;
                        "TpValue":     jBase["TpValue"],;
                        "TpValueDesc": jBase["TpValueDesc"],;
                        "BaseValue":   jBase["BaseValue"],;
                        "FGTS":        NoRound(jBase["BaseValue"] * jEmployee["TaxRate"] / 100, 2);
                    })
                EndIf
            Next nBase
        Next nEmployee
    Next nBranch

    // Libera os objetos da memória
    jBranch   := NIL
    jEmployee := NIL
    jBase     := NIL
    FwFreeObj(jBranch)
    FwFreeObj(jEmployee)
    FwFreeObj(jBase)
Return NIL
