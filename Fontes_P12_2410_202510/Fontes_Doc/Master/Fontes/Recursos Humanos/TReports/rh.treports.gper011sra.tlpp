#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "rh.treports.gper011sra.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SR0, RG2, RHR, RHS, SRA, RIQ, RIR, RHK, RHM, RHL, RFO, SRN, SRB", customTables="SRA", name="Rel. Beneficios por Funcionario", country="ALL", initialRelease="12.1.2210")
class GPER011SRATReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass

method new() class GPER011SRATReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) // RH
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) // Rel. Beneficios por Funcionario
return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type  Metodo
@author Squad Rotinas
@since  Julho 05,2023
/*/
method getDescription() as character class GPER011SRATReportsBusinessObject
return (STR0003) // Emite um relatorio de Beneficios por Funcionario
 
method getData(nPage as numeric, oFilter as object) as object class GPER011SRATReportsBusinessObject
    local cAlias as character
    local cQuery as character    
    local cQueryTot as character    

    Local cWhereVrf 	:= "" as Character
	Local cWhereVtr 	:= "" as Character
	Local cWhereVal 	:= "" as Character
	Local cWhereOB 		:= "" as Character
	Local cWherePS 		:= "" as Character
	Local cWhereAgr		:= "" as Character
	Local cWhereDep		:= "" as Character

    Local jParams       := oFilter:getParameters() as JSON
    Local cProc         := IIf(jParams:hasProperty("PAR_PROCESSO") .and. Len(jParams["PAR_PROCESSO"]) > 0, jParams["PAR_PROCESSO"][1], "") as Character
    Local cNroPag       := IIf(jParams:hasProperty("PAR_NROPAG")   .and. Len(jParams["PAR_NROPAG"])   > 0, jParams["PAR_NROPAG"][1],   "") as Character
    Local cPer          := IIf(jParams:hasProperty("PAR_PERIODO")  .and. Len(jParams["PAR_PERIODO"])  > 0, jParams["PAR_PERIODO"][1],  "") as Character
    Local cFil          := IIf(jParams:hasProperty("PAR_FILIAL")   .and. Len(jParams["PAR_FILIAL"])   > 0, jParams["PAR_FILIAL"][1],   "") as Character
    Local cCC           := IIf(jParams:hasProperty("PAR_CC")       .and. Len(jParams["PAR_CC"])       > 0, jParams["PAR_CC"][1],       "") as Character
    Local cMatricula    := IIf(jParams:hasProperty("PAR_MAT")      .and. Len(jParams["PAR_MAT"])      > 0, jParams["PAR_MAT"][1],      "") as Character
	Local cCCT          := IIf(jParams:hasProperty("PAR_CCT")      .and. Len(jParams["PAR_CCT"])      > 0, jParams["PAR_CCT"][1],      "") as Character
    Local cVR           := IIf(jParams:hasProperty("PAR_VR")       .and. Len(jParams["PAR_VR"])       > 0, jParams["PAR_VR"][1],       "") as Character
	Local cVA           := IIf(jParams:hasProperty("PAR_VA")       .and. Len(jParams["PAR_VA"])       > 0, jParams["PAR_VA"][1],       "") as Character
	Local cVT           := IIf(jParams:hasProperty("PAR_VT")       .and. Len(jParams["PAR_VT"])       > 0, jParams["PAR_VT"][1],       "") as Character
	Local cOB           := IIf(jParams:hasProperty("PAR_OB")       .and. Len(jParams["PAR_OB"])       > 0, jParams["PAR_OB"][1],       "") as Character
	Local cPS           := IIf(jParams:hasProperty("PAR_PS")       .and. Len(jParams["PAR_PS"])       > 0, jParams["PAR_PS"][1],       "") as Character
	Local cDep          := IIf(jParams:hasProperty("PAR_DEP")      .and. Len(jParams["PAR_DEP"])      > 0, jParams["PAR_DEP"][1],      "") as Character
	Local cAgr          := IIf(jParams:hasProperty("PAR_AGR")      .and. Len(jParams["PAR_AGR"])      > 0, jParams["PAR_AGR"][1],      "") as Character
	Local aCategorias   := IIf(jParams:hasProperty("PAR_CATEGORIAS"), jParams["PAR_CATEGORIAS"], {}) as Array
    Local aSituacoes    := IIf(jParams:hasProperty("PAR_SITUACOES"),  jParams["PAR_SITUACOES"],  {}) as Array
    Local cGetDB        := AllTrim(Upper(TcGetDB())) as Character
    Local cConcat       := IIf("SQL"$cGetDB,"+","||") as Character
    Local lUnion        := .F. as Logical
    Local lTemCCT 	    := fChkCCT() as Logical
    Local cR0CCT 		:= "" as Character
    Local cRG2CCT		:= "" as Character
    Local cRHRCCT		:= "" as Character
    Local cRHSCCT		:= "" as Character
    Local cNULLCCT	    := "" as Character
    Local cSR0GRP		:= "" as Character
    Local cRG2GRP		:= "" as Character
    Local cTable		:= "" as Character
    Local cPrefix		:= "" as Character
    Local aRescrai      := {'30','31'}
	Local nTamRISCod	:= GetSX3Cache("RIS_COD", "X3_TAMANHO") as Numeric
    Local nParamOrder   := 1 as Numeric
    Local nAux          := 0 as Numeric
    Local oStatement    := NIL as Object
    Local lRotAbD       := fPerAbert(cProc, cPer, fGetCalcRot("D")) as Logical
    Local lRotAbE       := fPerAbert(cProc, cPer, fGetCalcRot("E")) as Logical
    Local lRotAb8       := fPerAbert(cProc, cPer, fGetCalcRot("8")) as Logical
    Local lRotAbI       := fPerAbert(cProc, cPer, fGetCalcRot("I")) as Logical
    Local lRotAbC       := fPerAbert(cProc, cPer, fGetCalcRot("C")) as Logical
    
	Local cCustomFields As Character // Campos customizados no formato SQL
    Local cName         As Character // Nome real do campo
    Local aPDFields     As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR   As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR     As Array     // Campos com nome diferente do real
    Local aAllFields    As Array     // Estrutura de todos os campos do ON
    Local lObfuscated   As Logical   // Flag que indica se tem algum campo para ofuscar
    Local jData         As JSON      // Informações a serem adicionadas no ON
    Local jLGPD         As JSON      // Indica quais campos devem ser ofuscados
    Local nFields       As Numeric

    // Variáveis com o nome da Filial e Grupo de Empresa
    Local cFilExec := AllTrim(FWFilialName())     as Character
    Local cEmpExec := AllTrim(FWEmpName(cEmpAnt)) as Character

    // Tratamento de valor padrão
    cVR  := IIf(Empty(cVR), "1", cVR)
    cVA  := IIf(Empty(cVA), "1", cVA)
    cVT  := IIf(Empty(cVT), "1", cVT)
    cOB  := IIf(Empty(cOB), "1", cOB)
    cPS  := IIf(Empty(cPS), "1", cPS)
    cDep := IIf(Empty(cDep), "1", cDep)
    cAgr := IIf(Empty(cAgr), "1", cAgr)

    // Tratamento de CCT
    cR0CCT   := IIf(lTemCCT, ",R0_CODCCT as FuncCCT ",  ",'' as FuncCCT ")
    cRG2CCT  := IIf(lTemCCT, ",RG2_CODCCT as FuncCCT ", ",'' as FuncCCT ")
    cRHRCCT  := IIf(lTemCCT, ",RHR_CODCCT as FuncCCT ", ",'' as FuncCCT ")
    cRHSCCT  := IIf(lTemCCT, ",RHS_CODCCT as FuncCCT ", ",'' as FuncCCT ")
    cNULLCCT := ",'' as FuncCCT "
    cSR0GRP  := IIf(lTemCCT, ", R0_CODCCT ",  "")
    cRG2GRP  := IIf(lTemCCT, ", RG2_CODCCT ", "")

    cCustomFields := ""
    cName         := ""
    aPDFields     := {}
    aPDFieldsNR   := {}
    aFieldsNR     := {}
    aAllFields    := {}
	aWherePer	  := {}
    lObfuscated   := .F.
    jData         := NIL
    jLGPD         := JSONObject():New()

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RA_NOME"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

	If !(Empty(cProc))
		cWhereVtr += " SRA.RA_PROCES = ? AND " // 6
		cWhereVrf += " SRA.RA_PROCES = ? AND " // 6
		cWhereVal += " SRA.RA_PROCES = ? AND " // 6
		cWhereOB  += " SRA.RA_PROCES = ? AND " // 4
		cWherePS  += " SRA.RA_PROCES = ? AND " // 6
		cWhereAgr += " SRA.RA_PROCES = ? AND " // 6
		cWhereDep += " SRA.RA_PROCES = ? AND " // 7
	EndIf

    If !(Empty(cPer))
        If cVR == "1"
            cRtBen    := fGetCalcRot("D") // VRF
            cWhereVrf += IIf(!Empty(cRtBen) .and. !lRotAbD, " (RG2.RG2_PERIOD = ? AND RG2.RG2_NROPGT = ? AND RG2.RG2_ROTEIR = ?) AND ", "(SR0.R0_PERIOD = ? AND SR0.R0_NROPGT = ? AND SR0.R0_ROTEIR = ?) AND ") // 7, 8 e 9
        EndIf

        If cVA == "1"
            cRtBen    := fGetCalcRot("E") // VAL
            cWhereVal += IIf(!Empty(cRtBen) .and. !lRotAbE, " (RG2.RG2_PERIOD = ? AND RG2.RG2_NROPGT = ? AND RG2.RG2_ROTEIR = ?) AND ", "(SR0.R0_PERIOD = ? AND SR0.R0_NROPGT = ? AND SR0.R0_ROTEIR = ?) AND ") // 7, 8 e 9
        EndIf

        If cVT == "1"
            cRtBen    := fGetCalcRot("8") // VTR
            cWhereVtr += IIf(!Empty(cRtBen) .and. !lRotAb8, " (RG2.RG2_PERIOD = ? AND RG2.RG2_NROPGT = ? AND RG2.RG2_ROTEIR = ?) AND ", "(SR0.R0_PERIOD = ? AND SR0.R0_NROPGT = ? AND SR0.R0_ROTEIR = ?) AND ") // 7, 8 e 9
        EndIf

        If cOB == "1"
            cWhereOB += IIf(lRotAbI, " (RIQ.RIQ_PERIOD = ?)", " (RIR.RIR_PERIOD = ?)") + " AND " // 5
        EndIf

        If cPS == "1"
            cWherePS += " ((((CAST((SUBSTRING(RHK.RHK_PERINI,3,4) ? SUBSTRING(RHK.RHK_PERINI,1,2)) AS VARCHAR(6)) <= ? AND RHK.RHK_PERFIM = ?)) OR " // 7, 8 e 9
            cWherePS += " ((CAST((SUBSTRING(RHK.RHK_PERINI,3,4) ? SUBSTRING(RHK.RHK_PERINI,1,2)) AS VARCHAR(6)) <= ?) AND " // 10 e 11
            cWherePS += " (CAST((SUBSTRING(RHK.RHK_PERFIM,3,4) ? SUBSTRING(RHK.RHK_PERFIM,1,2)) AS VARCHAR(6)) >= ?))) OR " // 12 e 13
            cWherePS += " (RHK_PERINI IS NULL AND RHK_PERFIM IS NULL)) AND "
        EndIf

        If cAgr == "1"
            cWhereAgr += " ((((CAST((SUBSTRING(RHM.RHM_PERINI,3,4) ? SUBSTRING(RHM.RHM_PERINI,1,2)) AS VARCHAR(6)) <= ? AND RHM.RHM_PERFIM = ?)) OR " // 7, 8 e 9
            cWhereAgr += " ((CAST((SUBSTRING(RHM.RHM_PERINI,3,4) ? SUBSTRING(RHM.RHM_PERINI,1,2)) AS VARCHAR(6)) <= ?) AND " // 10 e 11
            cWhereAgr += " (CAST((SUBSTRING(RHM.RHM_PERFIM,3,4) ? SUBSTRING(RHM.RHM_PERFIM,1,2)) AS VARCHAR(6)) >= ?))) OR " // 12 e 13
            cWhereAgr += " (RHM_PERINI IS NULL AND RHM_PERFIM IS NULL)) AND "
        EndIf

        If cDep == "1"
            cWhereDep += " ((((CAST((SUBSTRING(RHL.RHL_PERINI,3,4) ? SUBSTRING(RHL.RHL_PERINI,1,2)) AS VARCHAR(6)) <= ? AND RHL.RHL_PERFIM = ?)) OR " // 8, 9 e 10
            cWhereDep += " ((CAST((SUBSTRING(RHL.RHL_PERINI,3,4) ? SUBSTRING(RHL.RHL_PERINI,1,2)) AS VARCHAR(6)) <= ?) AND " // 11 e 12
            cWhereDep += " (CAST((SUBSTRING(RHL.RHL_PERFIM,3,4) ? SUBSTRING(RHL.RHL_PERFIM,1,2)) AS VARCHAR(6)) >= ?))) OR " // 13 e 14
            cWhereDep += " (RHL_PERINI IS NULL AND RHL_PERFIM IS NULL)) AND "
        EndIf
    EndIf

	If !(Empty(cFil))
		cWhereVtr += " SRA.RA_FILIAL = ? AND " // 10
		cWhereVrf += " SRA.RA_FILIAL = ? AND " // 10
		cWhereVal += " SRA.RA_FILIAL = ? AND " // 10
		cWhereOB  += " SRA.RA_FILIAL = ? AND " // 6
		cWherePS  += " SRA.RA_FILIAL = ? AND " // 14
		cWhereAgr += " SRA.RA_FILIAL = ? AND " // 14
		cWhereDep += " SRA.RA_FILIAL = ? AND " // 15
	EndIf

	If !(Empty(cCC))
		cWhereVtr += " SRA.RA_CC = ? AND " // 11
		cWhereVrf += " SRA.RA_CC = ? AND " // 11
		cWhereVal += " SRA.RA_CC = ? AND " // 11
		cWhereOB  += " SRA.RA_CC = ? AND " // 7
		cWherePS  += " SRA.RA_CC = ? AND " // 15
		cWhereAgr += " SRA.RA_CC = ? AND " // 15
		cWhereDep += " SRA.RA_CC = ? AND " // 16
	EndIf

	If !(Empty(cMatricula))
		cWhereVtr += " SRA.RA_MAT = ? AND " // 12
		cWhereVrf += " SRA.RA_MAT = ? AND " // 12
		cWhereVal += " SRA.RA_MAT = ? AND " // 12
		cWhereOB  += " SRA.RA_MAT = ? AND " // 8
		cWherePS  += " SRA.RA_MAT = ? AND " // 16
		cWhereAgr += " SRA.RA_MAT = ? AND " // 16
		cWhereDep += " SRA.RA_MAT = ? AND " // 17
	EndIf

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aSituacoes)
        aSituacoes[nAux] := IIf(aSituacoes[nAux] == NIL .or. Empty(aSituacoes[nAux]), " ", aSituacoes[nAux])
    Next nAux

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aCategorias)
        aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
    Next nAux

    If (Len(aSituacoes) > 0)
        cWhereVtr += " (SRA.RA_SITFOLH IN (?) " // 13
        cWhereVrf += " (SRA.RA_SITFOLH IN (?) " // 13
        cWhereVal += " (SRA.RA_SITFOLH IN (?) " // 13
        cWhereOB  += " (SRA.RA_SITFOLH IN (?) " // 9
        cWherePS  += " (SRA.RA_SITFOLH IN (?) " // 17
        cWhereAgr += " (SRA.RA_SITFOLH IN (?) " // 17
        cWhereDep += " (SRA.RA_SITFOLH IN (?) " // 18

        If cPaisLoc == "BRA"	//Transferência entre empresas/filiais deixa RA_SITFOLH="D"
            If (AScan(aSituacoes, {|x| x == "T"}) > 0) .And. !(AScan(aSituacoes, {|x| x == "D"}) > 0)		//só transferidos
                cWhereVtr += " OR (SRA.RA_SITFOLH = ? AND SRA.RA_RESCRAI IN (?))" // 14 e 15
                cWhereVrf += " OR (SRA.RA_SITFOLH = ? AND SRA.RA_RESCRAI IN (?))" // 14 e 15
                cWhereVal += " OR (SRA.RA_SITFOLH = ? AND SRA.RA_RESCRAI IN (?))" // 14 e 15
                cWhereOB  += " OR (SRA.RA_SITFOLH = ? AND SRA.RA_RESCRAI IN (?))" // 10 e 11
                cWherePS  += " OR (SRA.RA_SITFOLH = ? AND SRA.RA_RESCRAI IN (?))" // 18 e 19
                cWhereAgr += " OR (SRA.RA_SITFOLH = ? AND SRA.RA_RESCRAI IN (?))" // 18 e 19
                cWhereDep += " OR (SRA.RA_SITFOLH = ? AND SRA.RA_RESCRAI IN (?))" // 19 e 20
            ElseIf (AScan(aSituacoes, {|x| x == "D"}) > 0) .And. !(AScan(aSituacoes, {|x| x == "T"}) > 0)	//só demitidos
                cWhereVtr += " AND (SRA.RA_SITFOLH = ? AND SRA.RA_RESCRAI NOT IN (?))" // 14 e 15
                cWhereVrf += " AND (SRA.RA_SITFOLH = ? AND SRA.RA_RESCRAI NOT IN (?))" // 14 e 15
                cWhereVal += " AND (SRA.RA_SITFOLH = ? AND SRA.RA_RESCRAI NOT IN (?))" // 14 e 15
                cWhereOB  += " AND (SRA.RA_SITFOLH = ? AND SRA.RA_RESCRAI NOT IN (?))" // 10 e 11
                cWherePS  += " AND (SRA.RA_SITFOLH = ? AND SRA.RA_RESCRAI NOT IN (?))" // 18 e 19
                cWhereAgr += " AND (SRA.RA_SITFOLH = ? AND SRA.RA_RESCRAI NOT IN (?))" // 18 e 19
                cWhereDep += " AND (SRA.RA_SITFOLH = ? AND SRA.RA_RESCRAI NOT IN (?))" // 19 e 20
            EndIf
        EndIf

        cWhereVtr += ") AND "
        cWhereVrf += ") AND "
        cWhereVal += ") AND "
        cWhereOB  += ") AND "
        cWherePS  += ") AND "
        cWhereAgr += ") AND "
        cWhereDep += ") AND "
    EndIf

    If (Len(aCategorias) > 0)
        cWhereVtr += " SRA.RA_CATFUNC IN (?) AND " // 16
        cWhereVrf += " SRA.RA_CATFUNC IN (?) AND " // 16
        cWhereVal += " SRA.RA_CATFUNC IN (?) AND " // 16
        cWhereOB  += " SRA.RA_CATFUNC IN (?) AND " // 12
        cWherePS  += " SRA.RA_CATFUNC IN (?) AND " // 20
        cWhereAgr += " SRA.RA_CATFUNC IN (?) AND " // 20
        cWhereDep += " SRA.RA_CATFUNC IN (?) AND " // 21
    EndIf

    If lTemCCT .and. !Empty(cCCT)
		cWhereVtr += " RCE_CCT = ? AND " // 17
		cWhereVrf += " RCE_CCT = ? AND " // 17
		cWhereVal += " RCE_CCT = ? AND " // 17
		cWherePS  += " RCE_CCT = ? AND " // 21
		cWhereAgr += " RCE_CCT = ? AND " // 21
		cWhereDep += " RCE_CCT = ? AND " // 22
	EndIf

    If cVR == "1"
        lUnion    := .T.
        cTable    := IIf(lRotAbD, "SR0", "RG2")
        cPrefix   := IIf(lRotAbD, "R0_", "RG2_")
        cWhereVrf := Replace(cWhereVrf, "RCE_CCT", cPrefix + "CODCCT")

        // Monta a query para VR
        cQuery += "SELECT "
        cQuery +=     "'VR'                                                  AS Tipo, "
        cQuery +=     "RA_FILIAL                                             AS RA_FILIAL, "
        cQuery +=     "RA_MAT                                                AS RA_MAT, "
        cQuery +=     "RA_NOME                                               AS NomeFunc, "
        cQuery +=     "RA_CC                                                 AS RA_CC, "
        cQuery +=     cPrefix + "CODIGO                                      AS CodBen, "
        cQuery +=     cPrefix + "TPVALE                                      AS TpBen, "
        cQuery +=     "SUM(" + cPrefix + "VALCAL)                            AS ValCal, "
        cQuery +=     "SUM(" + IIf(lRotAbD, "R0_VLRFUNC", "RG2_CUSFUN") + ") AS ValFunc, "
        cQuery +=     "SUM(" + IIf(lRotAbD, "R0_VLREMP",  "RG2_CUSEMP") + ") AS ValEmp, "
        cQuery +=     "''                                                    AS CodBen2, "
        cQuery +=     "''                                                    AS DescBen2, "
        cQuery +=     "RFO_CODIGO                                            AS CodDes, "
        cQuery +=     "RFO_DESCR                                             AS DescBen, "
        cQuery +=     "''                                                    AS OrigemPLA "
        cQuery +=     "? " // 1
        cQuery +=     "? " // 2
        cQuery += "FROM "
        cQuery +=     RetSQLName("SRA") + " SRA "
        cQuery +=     "INNER JOIN "
        cQuery +=         RetSQLName(cTable) + " " + cTable + " "
        cQuery +=         "ON "
        cQuery +=             FwJoinFilial(cTable, "SRA") + " "
        cQuery +=             "AND " + cPrefix + "MAT         = RA_MAT "
        cQuery +=             "AND " + cPrefix + "TPVALE      = ? " // 3
        cQuery +=             "AND " + cTable  + ".D_E_L_E_T_ = ? " // 4
        cQuery +=     "LEFT JOIN "
        cQuery +=         RetSQLName("RFO") + " RFO "
        cQuery +=         "ON "
        cQuery +=             FwJoinFilial("RFO", "SRA") + " "
        cQuery +=             "AND RFO_CODIGO     = " + cPrefix + "CODIGO "
        cQuery +=             "AND RFO_TPVALE     = " + cPrefix + "TPVALE "
        cQuery +=             "AND RFO.D_E_L_E_T_ = ? " // 5
        cQuery += "WHERE "
        cQuery +=     cWhereVrf
        cQuery +=     "SRA.D_E_L_E_T_ = ? " // 18
        cQuery += "GROUP BY "
        cQuery +=     "RA_FILIAL, "
        cQuery +=     "RA_MAT, "
        cQuery +=     "RA_NOME, "
        cQuery +=     "RA_CC, "
        cQuery +=     cPrefix + "CODIGO, "
        cQuery +=     cPrefix + "TPVALE, "
        cQuery +=     "RFO_CODIGO, "
        cQuery +=     "RFO_DESCR "
        cQuery +=     "?" // 19
        cQuery +=     "?" // 20
	EndIf

    If cVA == "1"
        If lUnion
            cQuery += " UNION ALL "
        Else
            lUnion := .T.
        Endif

        cTable    := IIf(lRotAbE, "SR0", "RG2")
        cPrefix   := IIf(lRotAbE, "R0_", "RG2_")
        cWhereVal := Replace(cWhereVal, "RCE_CCT", cPrefix + "CODCCT")

        // Monta a query para VA
        cQuery += "SELECT "
        cQuery +=     "'VA'                                                  AS Tipo, "
        cQuery +=     "RA_FILIAL                                             AS RA_FILIAL, "
        cQuery +=     "RA_MAT                                                AS RA_MAT, "
        cQuery +=     "RA_NOME                                               AS NomeFunc, "
        cQuery +=     "RA_CC                                                 AS RA_CC, "
        cQuery +=     cPrefix + "CODIGO                                      AS CodBen, "
        cQuery +=     cPrefix + "TPVALE                                      AS TpBen, "
        cQuery +=     "SUM(" + cPrefix + "VALCAL)                            AS ValCal, "
        cQuery +=     "SUM(" + IIf(lRotAbE, "R0_VLRFUNC", "RG2_CUSFUN") + ") AS ValFunc, "
        cQuery +=     "SUM(" + IIf(lRotAbE, "R0_VLREMP",  "RG2_CUSEMP") + ") AS ValEmp, "
        cQuery +=     "''                                                    AS CodBen2, "
        cQuery +=     "''                                                    AS DescBen2, "
        cQuery +=     "RFO_CODIGO                                            AS CodDes, "
        cQuery +=     "RFO_DESCR                                             AS DescBen, "
        cQuery +=     "''                                                    AS OrigemPLA "
        cQuery +=     "? " // 1
        cQuery +=     "? " // 2
        cQuery += "FROM "
        cQuery +=     RetSQLName("SRA") + " SRA "
        cQuery +=     "INNER JOIN "
        cQuery +=         RetSQLName(cTable) + " " + cTable + " "
        cQuery +=         "ON " 
        cQuery +=             FwJoinFilial(cTable, "SRA") + " "
        cQuery +=             "AND " + cPrefix + "MAT         = RA_MAT "
        cQuery +=             "AND " + cPrefix + "TPVALE      = ? " // 3
        cQuery +=             "AND " + cTable  + ".D_E_L_E_T_ = ? " // 4
        cQuery +=     "LEFT JOIN "
        cQuery +=         RetSQLName("RFO") + " RFO "
        cQuery +=         "ON "
        cQuery +=             FwJoinFilial("RFO", "SRA") + " "
        cQuery +=             "AND RFO_CODIGO     = " + cPrefix + "CODIGO "
        cQuery +=             "AND RFO_TPVALE     = " + cPrefix + "TPVALE "
        cQuery +=             "AND RFO.D_E_L_E_T_ = ? " // 5
        cQuery += "WHERE "
        cQuery +=     cWhereVal
        cQuery +=     "SRA.D_E_L_E_T_ = ? " // 18
        cQuery += "GROUP BY "
        cQuery +=     "RA_FILIAL, "
        cQuery +=     "RA_MAT,"
        cQuery +=     "RA_NOME,"
        cQuery +=     "RA_CC,  "
        cQuery +=     cPrefix + "CODIGO, "
        cQuery +=     cPrefix + "TPVALE, "
        cQuery +=     "RFO_CODIGO,"
        cQuery +=     "RFO_DESCR "
        cQuery +=     "?" // 19
        cQuery +=     "?" // 20
	EndIf

    If cVT == "1"
        If lUnion
            cQuery += " UNION ALL "
        Else
            lUnion := .T.
        Endif

        cTable    := IIf(lRotAb8, "SR0", "RG2")
        cPrefix   := IIf(lRotAb8, "R0_", "RG2_")
        cWhereVtr := Replace(cWhereVtr, "RCE_CCT", cPrefix + "CODCCT")

        cQuery += "SELECT "
        cQuery +=     "'VT'                                                  AS Tipo, "
        cQuery +=     "RA_FILIAL                                             AS RA_FILIAL, "
        cQuery +=     "RA_MAT                                                AS RA_MAT, "
        cQuery +=     "RA_NOME                                               AS NomeFunc, "
        cQuery +=     "RA_CC                                                 AS RA_CC, "
        cQuery +=     cPrefix + "CODIGO                                      AS CodBen, "
        cQuery +=     cPrefix + "TPVALE                                      AS TpBen, "
        cQuery +=     "SUM(" + cPrefix + "VALCAL)                            AS ValCal, "
        cQuery +=     "SUM(" + IIf(lRotAb8, "R0_VLRFUNC", "RG2_CUSFUN") + ") AS ValFunc, "
        cQuery +=     "SUM(" + IIf(lRotAb8, "R0_VLREMP",  "RG2_CUSEMP") + ") AS ValEmp, "
        cQuery +=     "''                                                    AS CodBen2, "
        cQuery +=     "''                                                    AS DescBen2, "
        cQuery +=     "RN_COD                                                AS CodDes, "
        cQuery +=     "RN_DESC                                               AS DescBen, "
        cQuery +=     "''                                                    AS OrigemPLA "
        cQuery +=      "? " // 1
        cQuery +=      "? " // 2
        cQuery += "FROM "
        cQuery +=     RetSqlName("SRA") + " SRA "
        cQuery +=     "INNER JOIN "
        cQuery +=         RetSQLName(cTable) + " " + cTable + " "
        cQuery +=         "ON " 
        cQuery +=             FwJoinFilial(cTable, "SRA") + " "
        cQuery +=             "AND " + cPrefix + "MAT        = RA_MAT "
        cQuery +=             "AND " + cPrefix + "TPVALE     = ? " // 3
        cQuery +=             "AND " + cTable  + ".D_E_L_E_T_ = ? " // 4
        cQuery +=     "LEFT JOIN "
        cQuery +=         RetSqlName("SRN") + " SRN "
        cQuery +=         "ON " 
        cQuery +=             FwJoinFilial("SRN", cTable) + " "
        cQuery +=             "AND RN_COD         = " + cPrefix + "CODIGO "
        cQuery +=             "AND SRN.D_E_L_E_T_ = ? " // 5
        cQuery += "WHERE "
        cQuery +=     cWhereVtr
        cQuery +=     "SRA.D_E_L_E_T_ = ? " // 18
        cQuery += "GROUP BY "
        cQuery +=     "RA_FILIAL, "
        cQuery +=     "RA_MAT, "
        cQuery +=     "RA_NOME, "
        cQuery +=     "RA_CC, "
        cQuery +=     cPrefix + "CODIGO, "
        cQuery +=     cPrefix + "TPVALE, "
        cQuery +=     "RN_COD, "
        cQuery +=     "RN_DESC "
        cQuery +=     "?" // 19
        cQuery +=     "?" // 20
	EndIf

    //outros beneficios 
	If cOB == "1"
        If lUnion
            cQuery += " UNION ALL "
        Else
            lUnion := .T.
        Endif

        cTable  := IIf(lRotAbI, "RIQ",  "RIR")
        cPrefix := IIf(lRotAbI, "RIQ_", "RIR_")

        // Monta a query para Outros Benefícios
        cQuery += "SELECT DISTINCT "
        cQuery +=     "'Outros'         AS Tipo, "
        cQuery +=     "RA_FILIAL        AS RA_FILIAL, "
        cQuery +=     "RA_MAT           AS RA_MAT, "
        cQuery +=     "RA_NOME          AS NomeFunc, "
        cQuery +=     "RA_CC            AS RA_CC, "
        cQuery +=     cPrefix + "COD    AS CodBen, "
        cQuery +=     cPrefix + "TPBENE AS TpBen, "
        cQuery +=     cPrefix + "VALBEN AS ValCal, "
        cQuery +=     cPrefix + "VLRFUN AS ValFunc, "
        cQuery +=     cPrefix + "VLREMP AS ValEmp, "
        cQuery +=     "''               AS CodBen2, "
        cQuery +=     "''               AS DescBen2, "
        cQuery +=     "''               AS CodDes, "
        cQuery +=     "''               AS DescBen, "
        cQuery +=     "''               AS OrigemPLA "
        cQuery +=     "? " // 1
        cQuery +=     "? " // 2
        cQuery += "FROM "
        cQuery +=     RetSQLName("SRA") + " SRA "
        cQuery +=     "INNER JOIN "
        cQuery +=         RetSQLName(cTable) + " " + cTable + " "
        cQuery +=         "ON "
        cQuery +=             FwJoinFilial(cTable, "SRA") + " "
        cQuery +=             "AND " + cPrefix + "MAT         = RA_MAT "
        cQuery +=             "AND " + cTable  + ".D_E_L_E_T_ = ? " // 3
        cQuery += "WHERE "
        cQuery +=     cWhereOB
        cQuery +=     "SRA.D_E_L_E_T_ = ? " // 13
	EndIf

    // plano de saude titular 
	If cPS == "1"
        If lUnion
            cQuery += " UNION ALL "
        Else
            lUnion := .T.
        Endif

        cTable   := IIf(lRotAbC, "RHR",  "RHS")
        cPrefix  := IIf(lRotAbC, "RHR_", "RHS_")
        cWherePS := Replace(cWherePS, "RCE_CCT", cPrefix + "CODCCT")

        // Monta a query para Outros Benefícios
        cQuery += "SELECT DISTINCT "
        cQuery +=     "'PS'                                     AS Tipo, "
        cQuery +=     "RA_FILIAL                                AS RA_FILIAL, "
        cQuery +=     "RA_MAT                                   AS RA_MAT, "
        cQuery +=     "RA_NOME                                  AS NomeFunc, "
        cQuery +=     "RA_CC                                    AS RA_CC, "
        cQuery +=     "RHK_TPPLAN                               AS CodBen, "
        cQuery +=     "RHK_TPFORN                               AS TpBen, "
        cQuery +=     cPrefix + "VLRFUN + " + cPrefix + "VLREMP AS ValCal,
        cQuery +=     cPrefix + "VLRFUN                         AS ValFunc, "
        cQuery +=     cPrefix + "VLREMP                         AS ValEmp, "
        cQuery +=     "RHK_PLANO                                AS CodBen2, "
        cQuery +=     "''                                       AS DescBen2, "
        cQuery +=     "RHK_CODFOR                               AS CodDes, "
        cQuery +=     "''                                       AS DescBen, "
        cQuery +=     cPrefix + "ORIGEM                         AS OrigemPLA "
        cQuery +=     "? " // 1
        cQuery +=     "? " // 2
        cQuery += "FROM "
        cQuery +=     RetSQLName("SRA") + " SRA "
        cQuery +=     "INNER JOIN "
        cQuery +=         RetSQLName("RHK") + " RHK "
        cQuery +=         "ON "
        cQuery +=             FwJoinFilial("RHK", "SRA") + " "
        cQuery +=             "AND RHK_MAT        = RA_MAT "
        cQuery +=             "AND RHK.D_E_L_E_T_ = ? " // 3
        cQuery +=     "INNER JOIN "
        cQuery +=         RetSQLName(cTable) + " " + cTable + " "
        cQuery +=         "ON "
        cQuery +=             FwJoinFilial(cTable, "RHK") + " "
        cQuery +=             "AND " + cPrefix + "MAT         = RHK_MAT "
        cQuery +=             "AND " + cPrefix + "TPFORN      = RHK_TPFORN "
        cQuery +=             "AND " + cPrefix + "CODFOR      = RHK_CODFOR "
        cQuery +=             "AND " + cPrefix + "TPPLAN      = RHK_TPPLAN "
        cQuery +=             "AND " + cPrefix + "PLANO       = RHK_PLANO "
        cQuery +=             "AND " + cPrefix + "ORIGEM      = ? " // 4
        cQuery +=             "AND " + cTable  + ".D_E_L_E_T_ = ? " // 5
        cQuery += "WHERE "
        cQuery +=     cWherePS
        cQuery +=     "SRA.D_E_L_E_T_ = ? " // 22
	Endif

    //agregados
	If cAgr == "1" .AND. cPS == "1"
        If lUnion
            cQuery += " UNION ALL "
        Endif

        cTable    := IIf(lRotAbC, "RHR",  "RHS")
        cPrefix   := IIf(lRotAbC, "RHR_", "RHS_")
		cWhereAgr := Replace(cWhereAgr, "RCE_CCT", cPrefix + "CODCCT")

        // Monta a query para Outros Benefícios
        cQuery += "SELECT DISTINCT "
        cQuery +=     "'PS'                                     AS Tipo, "
        cQuery +=     "RA_FILIAL                                AS RA_FILIAL, "
        cQuery +=     "RA_MAT                                   AS RA_MAT, "
        cQuery +=     "RA_NOME                                  AS NomeFunc, "
        cQuery +=     "RA_CC                                    AS RA_CC, "
        cQuery +=     "RHM_TPPLAN                               AS CodBen, "
        cQuery +=     "RHM_TPFORN                               AS TpBen, "
        cQuery +=     cPrefix + "VLRFUN + " + cPrefix + "VLREMP AS ValCal, "
        cQuery +=     cPrefix + "VLRFUN                         AS ValFunc, "
        cQuery +=     cPrefix + "VLREMP                         AS ValEmp, "
        cQuery +=     "RHM_PLANO                                AS CodBen2, "
        cQuery +=     "''                                       AS DescBen2, "
        cQuery +=     "RHM_CODFOR                               AS CodDes, "
        cQuery +=     "''                                       AS DescBen, "
        cQuery +=     cPrefix + "ORIGEM                         AS OrigemPLA "
        cQuery +=     "? " // 1
        cQuery +=     "? " // 2
        cQuery += "FROM "
        cQuery +=     RetSqlName("SRA")+" SRA "
        cQuery +=     "INNER JOIN "
        cQuery +=         RetSqlName("RHM")+" RHM "
        cQuery +=         "ON "
        cQuery +=             FwJoinFilial("RHM", "SRA") + " "
        cQuery +=             "AND RHM_MAT       = RA_MAT "
        cQuery +=             "AND RHM.D_E_L_E_T_= ? " // 3
        cQuery +=     "INNER JOIN "
        cQuery +=         RetSQLName(cTable) + " " + cTable + " "
        cQuery +=         "ON "
        cQuery +=             FwJoinFilial(cTable, "RHM") + " "
        cQuery +=             "AND " + cPrefix + "MAT         = RHM_MAT "
        cQuery +=             "AND " + cPrefix + "TPFORN      = RHM_TPFORN "
        cQuery +=             "AND " + cPrefix + "CODFOR      = RHM_CODFOR "
        cQuery +=             "AND " + cPrefix + "TPPLAN      = RHM_TPPLAN "
        cQuery +=             "AND " + cPrefix + "PLANO       = RHM_PLANO "
        cQuery +=             "AND " + cPrefix + "ORIGEM      = ? " // 4
        cQuery +=             "AND " + cTable  + ".D_E_L_E_T_ = ? " // 5
        cQuery += "WHERE "
        cQuery +=     cWhereAgr
        cQuery +=     "SRA.D_E_L_E_T_ = ? " // 22
	Endif

    //Dependentes
	If cDep == "1" .AND. cPS == "1"
        If lUnion
            cQuery += " UNION ALL "
        Endif

        cTable    := IIf(lRotAbC, "RHR",  "RHS")
        cPrefix   := IIf(lRotAbC, "RHR_", "RHS_")
		cWhereDep := Replace(cWhereDep, "RCE_CCT", cPrefix + "CODCCT")

        cQuery += "SELECT DISTINCT "
        cQuery +=     "'PS'                                     AS Tipo, "
        cQuery +=     "RA_FILIAL                                AS RA_FILIAL, "
        cQuery +=     "RA_MAT                                   AS RA_MAT, "
        cQuery +=     "RB_NOME                                  AS NomeFunc, "
        cQuery +=     "RA_CC                                    AS RA_CC, "
        cQuery +=     "RHL_TPPLAN                               AS CodBen, "
        cQuery +=     "RHL_TPFORN                               AS TpBen, "
        cQuery +=     cPrefix + "VLRFUN + " + cPrefix + "VLREMP AS ValCal, "
        cQuery +=     cPrefix + "VLRFUN                         AS ValFunc, "
        cQuery +=     cPrefix + "VLREMP                         AS ValEmp, "
        cQuery +=     "RHL_PLANO                                AS CodBen2, "
        cQuery +=     "''                                       AS DescBen2, "
        cQuery +=     "RHL_CODFOR                               AS CodDes, "
        cQuery +=     "''                                       AS DescBen, "
        cQuery +=     cPrefix + "ORIGEM                         AS OrigemPLA "
        cQuery +=     "? " // 1
        cQuery +=     "? " // 2
        cQuery += "FROM "
        cQuery +=     RetSQLName("SRA") + " SRA "
        cQuery +=     "INNER JOIN "
        cQuery +=         RetSQLName("RHL") + " RHL "
        cQuery +=         "ON "
        cQuery +=             FwJoinFilial("RHL", "SRA") + " "
        cQuery +=             "AND RHL_MAT        = RA_MAT "
        cQuery +=             "AND RHL.D_E_L_E_T_ = ? " // 3
        cQuery +=     "INNER JOIN "
        cQuery +=         RetSQLName(cTable) + " " + cTable + " "
        cQuery +=         "ON "
        cQuery +=             FwJoinFilial(cTable, "RHL") + " "
        cQuery +=             "AND " + cPrefix + "MAT         = RHL_MAT "
        cQuery +=             "AND " + cPrefix + "TPFORN      = RHL_TPFORN "
        cQuery +=             "AND " + cPrefix + "CODFOR      = RHL_CODFOR "
        cQuery +=             "AND " + cPrefix + "TPPLAN      = RHL_TPPLAN "
        cQuery +=             "AND " + cPrefix + "PLANO       = RHL_PLANO "
        cQuery +=             "AND " + cPrefix + "ORIGEM      = ? " // 4
        cQuery +=             "AND " + cTable  + ".D_E_L_E_T_ = ? " // 5
        cQuery +=     "INNER JOIN "
        cQuery +=         RetSQLName("SRB") + " SRB "
        cQuery +=         "ON "
        cQuery +=             FwJoinFilial("SRB", "SRA") + " "
        cQuery +=             "AND RB_MAT         = RA_MAT "
        cQuery +=             "AND RB_COD         = " + cPrefix + "CODIGO "
        cQuery +=             "AND SRB.D_E_L_E_T_ = ? " // 6
        cQuery += "WHERE "
        cQuery +=     cWhereDep
        cQuery +=     "SRA.D_E_L_E_T_ = ? " // 23
	Endif

    If lUnion
        cQueryTot += "SELECT "
        cQueryTot +=     "ROW_NUMBER() OVER(PARTITION BY RA_FILIAL, RA_MAT ORDER BY RA_FILIAL, RA_MAT) AS LinhaMat, "
        cQueryTot +=     "TAB.* "
        cQueryTot += "FROM "
        cQueryTot +=     "("
        cQueryTot +=         cQuery
        cQueryTot +=     ") TAB "
        cQueryTot := ChangeQuery(cQueryTot)

        // Instancia a classe
        oStatement := FwExecStatement():New(cQueryTot)

        // Captura e define os campos personalizados das tabelas SLY e SRA
        cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA"})

        // Define o valor dos bind parameters
        If (cVR == "1")
            
            oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1
            Iif(lRotAbD, oStatement:SetUnsafe(nParamOrder++, cR0CCT), oStatement:SetUnsafe(nParamOrder++, cRG2CCT)) // 2

            oStatement:SetString(nParamOrder++, '1') // 3
            oStatement:SetString(nParamOrder++, " ") // 4
            oStatement:SetString(nParamOrder++, " ") // 5

            If (!Empty(cProc))
                oStatement:SetString(nParamOrder++, cProc) // 6
            EndIf

            If (!Empty(cPer))
                cRtBen := fGetCalcRot("D") // VRF
                If (!Empty(cRtBen))
                    oStatement:SetString(nParamOrder++, cPer)    // 7
                    oStatement:SetString(nParamOrder++, cNroPag) // 8
                    oStatement:SetString(nParamOrder++, cRtBen)  // 9
                EndIf
            EndIf

            If (!Empty(cFil))
                oStatement:SetString(nParamOrder++, cFil) // 10
            EndIf

            If (!Empty(cCC))
                oStatement:SetString(nParamOrder++, cCC) // 11
            EndIf

            If (!Empty(cMatricula))
                oStatement:SetString(nParamOrder++, cMatricula) // 12
            EndIf

            If (Len(aSituacoes) > 0)
                oStatement:SetIN(nParamOrder++, aSituacoes) // 13
                If cPaisLoc == "BRA"	//Transferência entre empresas/filiais deixa RA_SITFOLH="D"
                    If (AScan(aSituacoes, {|x| x == "T"}) > 0) .And. !(AScan(aSituacoes, {|x| x == "D"}) > 0)		//só transferidos
                        oStatement:SetString(nParamOrder++, 'D')    // 14
                        oStatement:SetIN(nParamOrder++, aRescrai)   // 15
                    ElseIf (AScan(aSituacoes, {|x| x == "D"}) > 0) .And. !(AScan(aSituacoes, {|x| x == "T"}) > 0)	//só demitidos
                        oStatement:SetString(nParamOrder++, 'D')    // 14
                        oStatement:SetIN(nParamOrder++, aRescrai)   // 15
                    Endif
                EndIf
            EndIf

            If (Len(aCategorias) > 0)
                oStatement:SetIN(nParamOrder++, aCategorias) // 16
            EndIf

            If (lTemCCT .and. !Empty(cCCT))
                oStatement:SetString(nParamOrder++, cCCT) // 17
            EndIf

            oStatement:SetString(nParamOrder++, " ") // 18

            oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 19
            Iif(lRotAbD, oStatement:SetUnsafe(nParamOrder++, cSR0GRP), oStatement:SetUnsafe(nParamOrder++, cRG2GRP)) // 20
        EndIf

        If (cVA == "1")
            oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1
            Iif(lRotAbE, oStatement:SetUnsafe(nParamOrder++, cR0CCT), oStatement:SetUnsafe(nParamOrder++, cRG2CCT)) // 2

            oStatement:SetString(nParamOrder++, "2") // 3
            oStatement:SetString(nParamOrder++, " ") // 4
            oStatement:SetString(nParamOrder++, " ") // 5

            If (!Empty(cProc))
                oStatement:SetString(nParamOrder++, cProc) // 6
            EndIf

            If (!Empty(cPer))
                cRtBen := fGetCalcRot("E") // VAL
                If (!Empty(cRtBen))
                    oStatement:SetString(nParamOrder++, cPer)    // 7
                    oStatement:SetString(nParamOrder++, cNroPag) // 8
                    oStatement:SetString(nParamOrder++, cRtBen)  // 9
                EndIf
            EndIf

            If (!Empty(cFil))
                oStatement:SetString(nParamOrder++, cFil) // 10
            EndIf

            If (!Empty(cCC))
                oStatement:SetString(nParamOrder++, cCC) // 11
            EndIf

            If (!Empty(cMatricula))
                oStatement:SetString(nParamOrder++, cMatricula) // 12
            EndIf

            If (Len(aSituacoes) > 0)
                oStatement:SetIN(nParamOrder++, aSituacoes) // 13
                If cPaisLoc == "BRA"	//Transferência entre empresas/filiais deixa RA_SITFOLH="D"
                    oStatement:SetString(nParamOrder++, 'D')    // 14
                    oStatement:SetIN(nParamOrder++, aRescrai)   // 15
                EndIf
            EndIf

            If (Len(aCategorias) > 0)
                oStatement:SetIN(nParamOrder++, aCategorias) // 16
            EndIf

            If (lTemCCT .and. !Empty(cCCT))
                oStatement:SetString(nParamOrder++, cCCT) // 17
            EndIf

            oStatement:SetString(nParamOrder++, " ") // 18

            oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 19
            Iif(lRotAbE,oStatement:SetUnsafe(nParamOrder++, cSR0GRP), oStatement:SetUnsafe(nParamOrder++, cRG2GRP)) // 20
        EndIf

        If (cVT == "1")
            oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1
            Iif(lRotAb8, oStatement:SetUnsafe(nParamOrder++, cR0CCT), oStatement:SetUnsafe(nParamOrder++, cRG2CCT)) // 2

            oStatement:SetString(nParamOrder++, "0") // 3
            oStatement:SetString(nParamOrder++, " ") // 4
            oStatement:SetString(nParamOrder++, " ") // 5

            If (!Empty(cProc))
                oStatement:SetString(nParamOrder++, cProc) // 6
            EndIf

            If (!Empty(cPer))
                cRtBen := fGetCalcRot("8") // VTR
                If (!Empty(cRtBen))
                    oStatement:SetString(nParamOrder++, cPer)    // 7
                    oStatement:SetString(nParamOrder++, cNroPag) // 8
                    oStatement:SetString(nParamOrder++, cRtBen)  // 9
                EndIf
            EndIf

            If (!Empty(cFil))
                oStatement:SetString(nParamOrder++, cFil) // 10
            EndIf

            If (!Empty(cCC))
                oStatement:SetString(nParamOrder++, cCC) // 11
            EndIf

            If (!Empty(cMatricula))
                oStatement:SetString(nParamOrder++, cMatricula) // 12
            EndIf

            If (Len(aSituacoes) > 0)
                oStatement:SetIN(nParamOrder++, aSituacoes) // 13
                If cPaisLoc == "BRA"	//Transferência entre empresas/filiais deixa RA_SITFOLH="D"
                    oStatement:SetString(nParamOrder++, 'D')    // 14
                    oStatement:SetIN(nParamOrder++, aRescrai)   // 15
                EndIf
            EndIf

            If (Len(aCategorias) > 0)
                oStatement:SetIN(nParamOrder++, aCategorias) // 16
            EndIf

            If (lTemCCT .and. !Empty(cCCT))
                oStatement:SetString(nParamOrder++, cCCT) // 17
            EndIf

            oStatement:SetString(nParamOrder++, " ") // 18

            oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 19
            Iif(lRotAb8, oStatement:SetUnsafe(nParamOrder++, cSR0GRP), oStatement:SetUnsafe(nParamOrder++, cRG2GRP)) // 20
        EndIf

        // Outros beneficios
        If (cOB == "1")
            oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1
            oStatement:SetUnsafe(nParamOrder++, cNULLCCT) // 2
            oStatement:SetString(nParamOrder++, " ") // 3

            If (!Empty(cProc))
                oStatement:SetString(nParamOrder++, cProc) // 4
            EndIf

            If !(Empty(cPer))
                oStatement:SetString(nParamOrder++, cPer) // 5
            EndIf

            If (!Empty(cFil))
                oStatement:SetString(nParamOrder++, cFil) // 6
            EndIf

            If (!Empty(cCC))
                oStatement:SetString(nParamOrder++, cCC) // 7
            EndIf

            If (!Empty(cMatricula))
                oStatement:SetString(nParamOrder++, cMatricula) // 8
            EndIf

            If (Len(aSituacoes) > 0)
                oStatement:SetIN(nParamOrder++, aSituacoes) // 9
                If cPaisLoc == "BRA"	//Transferência entre empresas/filiais deixa RA_SITFOLH="D"
                    oStatement:SetString(nParamOrder++, 'D')    // 10
                    oStatement:SetIN(nParamOrder++, aRescrai)   // 11
                EndIf
            EndIf

            If (Len(aCategorias) > 0)
                oStatement:SetIN(nParamOrder++, aCategorias) // 12
            EndIf

            oStatement:SetString(nParamOrder++, " ") // 13
        EndIf

        // Plano de saúde titular
        If (cPS == "1")
            oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1
            Iif(lRotAbC, oStatement:SetUnsafe(nParamOrder++, cRHRCCT), oStatement:SetUnsafe(nParamOrder++, cRHSCCT)) // 2

            oStatement:SetString(nParamOrder++, " ") // 3
            oStatement:SetString(nParamOrder++, "1") // 4
            oStatement:SetString(nParamOrder++, " ") // 5

            If (!Empty(cProc))
                oStatement:SetString(nParamOrder++, cProc) // 6
            EndIf

            If !(Empty(cPer))
                oStatement:SetUnsafe(nParamOrder++, cConcat) // 7
                oStatement:SetString(nParamOrder++, cPer)    // 8
                oStatement:SetString(nParamOrder++, " ")     // 9
                oStatement:SetUnsafe(nParamOrder++, cConcat) // 10
                oStatement:SetString(nParamOrder++, cPer)    // 11
                oStatement:SetUnsafe(nParamOrder++, cConcat) // 12
                oStatement:SetString(nParamOrder++, cPer)    // 13
            EndIf

            If (!Empty(cFil))
                oStatement:SetString(nParamOrder++, cFil) // 14
            EndIf

            If (!Empty(cCC))
                oStatement:SetString(nParamOrder++, cCC) // 15
            EndIf

            If (!Empty(cMatricula))
                oStatement:SetString(nParamOrder++, cMatricula) // 16
            EndIf

            If (Len(aSituacoes) > 0)
                oStatement:SetIN(nParamOrder++, aSituacoes) // 17
                If cPaisLoc == "BRA"	//Transferência entre empresas/filiais deixa RA_SITFOLH="D"
                    oStatement:SetString(nParamOrder++, 'D')    // 18
                    oStatement:SetIN(nParamOrder++, aRescrai)   // 19
                EndIf
            EndIf

            If (Len(aCategorias) > 0)
                oStatement:SetIN(nParamOrder++, aCategorias) // 20
            EndIf

            If (lTemCCT .and. !Empty(cCCT))
                oStatement:SetString(nParamOrder++, cCCT) // 21
            EndIf

            oStatement:SetString(nParamOrder++, " ") // 22
        EndIf

        // Agregados
        If (cAgr == "1" .AND. cPS == "1")
            oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1
            Iif(lRotAbC, oStatement:SetUnsafe(nParamOrder++, cRHRCCT), oStatement:SetUnsafe(nParamOrder++, cRHSCCT)) // 2

            oStatement:SetString(nParamOrder++, " ") // 3
            oStatement:SetString(nParamOrder++, "3") // 4
            oStatement:SetString(nParamOrder++, " ") // 5

            If (!Empty(cProc))
                oStatement:SetString(nParamOrder++, cProc) // 6
            EndIf

            If !(Empty(cPer))
                oStatement:SetUnsafe(nParamOrder++, cConcat) // 7
                oStatement:SetString(nParamOrder++, cPer)    // 8
                oStatement:SetString(nParamOrder++, " ")     // 9
                oStatement:SetUnsafe(nParamOrder++, cConcat) // 10
                oStatement:SetString(nParamOrder++, cPer)    // 11
                oStatement:SetUnsafe(nParamOrder++, cConcat) // 12
                oStatement:SetString(nParamOrder++, cPer)    // 13
            EndIf

            If (!Empty(cFil))
                oStatement:SetString(nParamOrder++, cFil) // 14
            EndIf

            If (!Empty(cCC))
                oStatement:SetString(nParamOrder++, cCC) // 15
            EndIf

            If (!Empty(cMatricula))
                oStatement:SetString(nParamOrder++, cMatricula) // 16
            EndIf

            If (Len(aSituacoes) > 0)
                oStatement:SetIN(nParamOrder++, aSituacoes) // 17
                If cPaisLoc == "BRA"	//Transferência entre empresas/filiais deixa RA_SITFOLH="D"
                    oStatement:SetString(nParamOrder++, 'D')    // 18
                    oStatement:SetIN(nParamOrder++, aRescrai)   // 19
                EndIf
            EndIf

            If (Len(aCategorias) > 0)
                oStatement:SetIN(nParamOrder++, aCategorias) // 20
            EndIf

            If (lTemCCT .and. !Empty(cCCT))
                oStatement:SetString(nParamOrder++, cCCT) // 21
            EndIf
            
            oStatement:SetString(nParamOrder++, " ") // 22
        EndIf

        // Dependentes
        If (cDep == "1" .and. cPS == "1")
            oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1
            Iif(lRotAbC, oStatement:SetUnsafe(nParamOrder++, cRHRCCT), oStatement:SetUnsafe(nParamOrder++, cRHSCCT)) // 2

            oStatement:SetString(nParamOrder++, " ") // 3
            oStatement:SetString(nParamOrder++, "2") // 4
            oStatement:SetString(nParamOrder++, " ") // 5
            oStatement:SetString(nParamOrder++, " ") // 6

            If (!Empty(cProc))
                oStatement:SetString(nParamOrder++, cProc) // 7
            EndIf

            If !(Empty(cPer))
                oStatement:SetUnsafe(nParamOrder++, cConcat) // 8
                oStatement:SetString(nParamOrder++, cPer)    // 9
                oStatement:SetString(nParamOrder++, " ")     // 10
                oStatement:SetUnsafe(nParamOrder++, cConcat) // 11
                oStatement:SetString(nParamOrder++, cPer)    // 12
                oStatement:SetUnsafe(nParamOrder++, cConcat) // 13
                oStatement:SetString(nParamOrder++, cPer)    // 14
            EndIf

            If (!Empty(cFil))
                oStatement:SetString(nParamOrder++, cFil) // 15
            EndIf

            If (!Empty(cCC))
                oStatement:SetString(nParamOrder++, cCC) // 16
            EndIf

            If (!Empty(cMatricula))
                oStatement:SetString(nParamOrder++, cMatricula) // 17
            EndIf

            If (Len(aSituacoes) > 0)
                oStatement:SetIN(nParamOrder++, aSituacoes) // 18
                If cPaisLoc == "BRA"	//Transferência entre empresas/filiais deixa RA_SITFOLH="D"
                    oStatement:SetString(nParamOrder++, 'D')    // 19
                    oStatement:SetIN(nParamOrder++, aRescrai)   // 20
                EndIf
            EndIf

            If (Len(aCategorias) > 0)
                oStatement:SetIN(nParamOrder++, aCategorias) // 21
            EndIf

            If (lTemCCT .and. !Empty(cCCT))
                oStatement:SetString(nParamOrder++, cCCT) // 22
            EndIf

            oStatement:SetString(nParamOrder++, " ") // 23
        EndIf

        // Executa a query e retorna o alias criado
        cAlias := oStatement:OpenAlias()

        while !(cAlias)->(Eof())
            Do Case
                Case Trim((cAlias)->Tipo) == "PS"
                    cTipoBen := Iif(AllTrim((cAlias)->TpBen) == "1",OemToAnsi(STR0004),OemToAnsi(STR0005))
                Case Trim((cAlias)->Tipo) == "Outros"
                    cTipoBen := fDescRCC("S011",(cAlias)->TpBen,1,2,3,30)
                OtherWise
                    cTipoBen := (cAlias)->Tipo
            EndCase

            Do Case
                Case Trim((cAlias)->TIPO) == "PS"
                    cDescBen2 := fDesPlan(AllTrim((cAlias)->TpBen),AllTrim((cAlias)->CodBen),AllTrim((cAlias)->CodBen2),(cAlias)->CodDes)
                Case Trim((cAlias)->TIPO) == "Outros"
                    cDescBen2 := fDesc('RIS',(cAlias)->TpBen + PadR((cAlias)->CodBen, nTamRISCod),'RIS_DESC')
                OtherWise
                    cDescBen2 := (cAlias)->DescBen
            EndCase

            // Cria o JSON com as informações da linha
            jData := JSONObject():New()

            jData["Tipo"]           := (cAlias)->Tipo
            jData["RA_FILIAL"]      := IIf(jLGPD["RA_FILIAL"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),    (cAlias)->RA_FILIAL)
            jData["RA_MAT"]         := IIf(jLGPD["RA_MAT"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),	    (cAlias)->RA_MAT)
            jData["NomeFunc"]       := IIf(jLGPD["RA_NOME"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->NomeFunc),	    (cAlias)->NomeFunc)
            jData["RA_CC"]          := IIf(jLGPD["RA_CC"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),	    (cAlias)->RA_CC)
            jData["CodBen"]         := (cAlias)->CodBen
            jData["TpBen"]          := cTipoBen
            jData["ValCal"]         := (cAlias)->ValCal
            jData["ValFunc"]        := (cAlias)->ValFunc
            jData["ValEmp"]         := (cAlias)->ValEmp
            jData["CodBen2"]        := (cAlias)->CodBen2
            jData["DescBen2"]       := cDescBen2
            jData["CodDes"]         := (cAlias)->CodDes
            jData["DescBen"]        := (cAlias)->DescBen
            jData["OrigemPLA"]      := (cAlias)->OrigemPLA
            jData["LinhaMat"]       := (cAlias)->LinhaMat
            jData["FuncCCT"]        := (cAlias)->FuncCCT
            jData["TemCCT"]         := Iif(lTemCCT,1,0)
            jData["FilialExec"]     := cFilExec
            jData["EmpresaExec"]    := cEmpExec

            // Adiciona os campos customizados no JSON
            GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

            // Adiciona as informações da linha
            self:oData:appendData(jData)

            (cAlias)->( dbSkip() )
        enddo
        
        (cAlias)->( DBCloseArea() )

        // Limpa os objetos da memória
        jData := NIL
        FwFreeObj(jData)
        FwFreeObj(jLGPD)
        FwFreeArray(aAllFields)
        FwFreeArray(aFieldsNR)
        FwFreeArray(aPDFields)
        FwFreeArray(aPDFieldsNR)
    Endif

    // Limpa os objetos da memória
    FwFreeObj(JParams)
    FwFreeObj(oStatement)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)
return self:oData

method getSchema() as object class GPER011SRATReportsBusinessObject
    local aFieldsSRA as array
    
    aFieldsSRA := { "RA_FILIAL" }
    
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:addProperty("RA_MAT",      STR0006,           "string",        STR0006,        "RA_MAT")
    self:oSchema:addProperty("RA_CC",       STR0007,           "string",        STR0007,        "RA_CC")
    self:oSchema:addProperty("Tipo",        STR0008,           "string",        STR0008,        "Tipo")
    self:oSchema:addProperty("NomeFunc",    STR0009,           "string",        STR0010,        "NomeFunc")
    self:oSchema:addProperty("CodBen",      STR0011,           "string",        STR0012,        "CodBen")
    self:oSchema:addProperty("TpBen",       STR0013,           "string",        STR0014,        "TpBen")
    self:oSchema:addProperty("ValCal",      STR0015,           "number",        STR0015,        "ValCal")
    self:oSchema:addProperty("ValFunc",     STR0016,           "number",        STR0016,        "ValFunc")
    self:oSchema:addProperty("ValEmp",      STR0017,           "number",        STR0017,        "ValEmp")
    self:oSchema:addProperty("CodBen2",     STR0011 + " 2",    "string",        STR0012 + " 2", "CodBen2")
    self:oSchema:addProperty("DescBen2",    STR0018 + " 2",    "string",        STR0019 + " 2", "DescBen2")
    self:oSchema:addProperty("CodDes",      STR0020,           "string",        STR0021,        "CodDes")
    self:oSchema:addProperty("DescBen",     STR0022,           "string",        STR0019,        "DescBen")
    self:oSchema:addProperty("OrigemPLA",   STR0023,           "string",        STR0023,        "OrigemPLA")
    self:oSchema:addProperty("LinhaMat",    STR0024,           "number",        STR0025,        "LinhaMat")
    self:oSchema:addProperty("FuncCCT",     STR0026,           "string",        STR0026,        "FuncCCT")
    self:oSchema:addProperty("TemCCT",      STR0027,           "number",        STR0028,        "TemCCT")
    self:oSchema:addProperty("FilialExec",  STR0029,           "string",        STR0030,        "FilialExec")
    self:oSchema:addProperty("EmpresaExec", STR0031,           "string",        STR0032,       "EmpresaExec")

    // Parâmetros
    self:oSchema:addParameter("PAR_PROCESSO",   STR0033 + " ?",            "string", .F.)
    self:oSchema:addParameter("PAR_PERIODO",    STR0034 + " ?",            "string", .F.)
    self:oSchema:addParameter("PAR_NROPAG",     STR0035 + " ?",            "string", .F.)
    self:oSchema:addParameter("PAR_FILIAL",     STR0036 + " ?",            "string", .F.)
    self:oSchema:addParameter("PAR_CC",         STR0037 + " ?",            "string", .F.)
    self:oSchema:addParameter("PAR_MAT",        STR0006 + " ?",            "string", .F.)
    self:oSchema:addParameter("PAR_SITUACOES",  STR0038 + " ?",            "string", .T.)
    self:oSchema:addParameter("PAR_CATEGORIAS", STR0039 + " ?",            "string", .T.)
    self:oSchema:addParameter("PAR_CCT",        STR0026 + " ?",            "string", .F.)
    self:oSchema:addParameter("PAR_VR",         STR0040 + " ?",            "string", .F.)
    self:oSchema:addParameter("PAR_VA",         STR0041 + " ?",            "string", .F.)
    self:oSchema:addParameter("PAR_VT",         STR0042 + " ?",            "string", .F.)
    self:oSchema:addParameter("PAR_OB",         STR0043 + " ?",            "string", .F.)
    self:oSchema:addParameter("PAR_PS",         STR0044 + " ?",            "string", .F.)
    self:oSchema:addParameter("PAR_DEP",        STR0045 + " ?",            "string", .F.)
    self:oSchema:addParameter("PAR_AGR",        STR0046 + " ?",            "string", .F.)

    self:setCustomURL("PAR_FILIAL",    		"/api/rh/smartview/v1/options/GPEParams/getBranches",                   2)
	self:setCustomURL("PAR_MAT",            "/api/framework/v1/genericLookupService/smartview/SRA",                 2)
    self:setCustomURL("PAR_SITUACOES", 		"/api/rh/smartview/v1/options/GPEParams/getSX5/31",     				2)
    self:setCustomURL("PAR_CATEGORIAS",   	"/api/rh/smartview/v1/options/GPEParams/getSX5/28",     				2)
    self:setCustomURL("PAR_PROCESSO",       "/api/framework/v1/genericLookupService/smartview/RCJ",                 2)
    self:setCustomURL("PAR_CC",             "/api/framework/v1/genericLookupService/smartview/CTT",                 2)
    self:setCustomURL("PAR_CCT",            "/api/framework/v1/genericLookupService/smartview/SWY",                 2)
    self:setCustomURL("PAR_VR",             "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                  1)
    self:setCustomURL("PAR_VA",             "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                  1)
    self:setCustomURL("PAR_VT",             "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                  1)
    self:setCustomURL("PAR_OB",             "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                  1)
    self:setCustomURL("PAR_PS",             "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                  1)
    self:setCustomURL("PAR_DEP",            "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                  1)
    self:setCustomURL("PAR_AGR",            "/api/rh/smartview/v1/options/GPEParams/getSNOptions",                  1)

return self:oSchema

/*/{Protheus.doc} fChkCCT
//Verifica se a pergunta e campos da CCT existem
@author Leandro Drumond
@since 13/06/2022
/*/
Static Function fChkCCT()
Local lRet 		:= .F.
Local oSX1

If SR0->(ColumnPos("R0_CODCCT")) > 0
	//Verifica se existe a pergunta GPCR14
	oSX1 := FWSX1Util():New()
	oSX1:AddGroup("GPER011")
	oSX1:SearchGroup()
	
	If (Len(oSX1:aGrupo) >= 1 .And. Len(oSX1:aGrupo[1][2]) >= 16)
		lRet := .T.
	EndIf

	FreeObj(oSX1)
EndIf

Return lRet

/*/{Protheus.doc} fPerAbert
Verifica se o período é ativo
@author Gabriel de Souza Almeida
@version P12
@param cPer, caracter, período
@param cRot, caracter, roteiro
@return lógico, lRet, .T. quando o periodo está ativo
/*/
Static Function fPerAbert(cProc,cPer,cRot)
	Local lRet := .T.
	Local aPerAbert := {}
	Local nAux := 0
	
	fRetPerComp(Substr(cPer,5,2),Substr(cPer,1,4),,cProc,cRot,@aPerAbert)
	
	If Len(aPerAbert)>0 .AND. Len(aPerAbert[1])>1
		For nAux := 1 To Len(aPerAbert)
			lRet := (cPer == aPerAbert[nAux][1])
			If lRet
				Exit
			EndIf
		Next
	Else
		lRet := .F.
	EndIf
Return lRet
