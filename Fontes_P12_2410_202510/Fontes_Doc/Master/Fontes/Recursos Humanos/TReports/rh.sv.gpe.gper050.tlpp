#Include "TOTVS.FRAMEWORK.TREPORTS.INTEGRATEDPROVIDER.th"
#Include "TLPP-CORE.th"
#Include "TLPP-REST.th"
#Include "rh.sv.gpe.gper050.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} GPER050TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Crédito Trabalhador
    @type Class
    @version 12.1.2510
    @author karina.alves
    @since 20/08/2025
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, RUO, SRK", name="Crédito do Trabalhador", customTables = "SRA, RUO", country="ALL", initialRelease="12.1.2310")
Class GPER050TReportsBusinessObject From IntegratedProvider
    // Define os métodos da classe
    Public Method new()       As Object
    Public Method getData()   As Object
    Public Method getSchema() As Object
EndClass

/*/{Protheus.doc} GPER050TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2510
    @author karina.alves
    @since 14/09/2025
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER050TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a área
    self:appendArea(STR0001) // "RH"

    // Define o nome e descrição do objeto de negócio
    self:setDisplayName(STR0002) // "Crédito do Trabalhador"
    self:setDescription(STR0003) // "Este objeto de negócio emite o Relatório Crédito do Trabalhador."

Return self

/*/{Protheus.doc} GPER050TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author karina.alves
    @since 14/09/2025
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER050TReportsBusinessObject
    // Declaração das variáveis locais
    Local cQuery              As Character // Query para busca dos registros
    Local cQuery2             As Character // Query para busca dos registros
    Local cQuery3             As Character // Query para busca dos registros
    Local cAlias              As Character // Alias do arquivo temporário
    Local cAlias2             As Character // Alias do arquivo temporário
    Local cAlias3             As Character // Alias do arquivo temporário
    Local cCustomFields       As Character // Campos customizados no formato SQL
    Local cName               As Character // Nome real do campo
    Local cSituacao           As Character // Situação
    Local cFiltro             As Character // Filtro do objeto de negócio
    Local cValidFil           As Character // Filiais válidas
    Local cCategoria          As Character // Categoria
    Local cAcessaSRA          As Character // Valida acesso a SRA
    Local cAcessaRUO          As Character // Valida acesso a RUO
    Local cAcessaSRK          As Character // Valida acesso a SRK
    Local cJoinSRA            As Character // Join da Query RUO com SRA
    Local cJoinSRJ            As Character // Join da Query RUO com SRJ
    Local cFilExec            As Character // Filial Execução
    Local cEmpExec            As Character // Empresa Execução
    Local cExibeGraf          As Character // Exibe Gráfico
    Local cNrContratoAnt      As Character // Guarda número do contrato anterior
    Local cDescVerb           As Character // Descrição Verba
    Local cDescBanc           As Character // Descrição Banco
    Local cNumContrato        As Character // Número do Contrato
    Local cVerba              As Character // Verba
    Local cBanco              As Character // Banco   
    Local cStatus             As Character // Status 
    Local jData               As JSON      // Informações de uma linha do ON
    Local jLGPD               As JSON      // Indica quais campos devem ser ofuscados
    Local jParams             As JSON      // Parâmetros
    Local jContratos          As JSON      // Contratos
    Local jDetalhes           As JSON      // Detalhes
    Local oExec               As Object    // Instância da classe FwExecStatement
    Local oExec2              As Object    // Instância da classe FwExecStatement
    Local oExec3              As Object    // Instância da classe FwExecStatement
    Local nParamOrder         As Numeric   // Ordem dos bind parameters da query
    Local nAux                As Numeric   // Contador auxiliar para percorrer vetores
    Local nFields             As Numeric   // Contador de campos
    Local nValContrato        As Numeric   // Valor Contrato
    Local nTotParcela         As Numeric   // Total Parcelas
    Local nPos                As Numeric   // Posição
    Local nPos1               As Numeric   // Posição
    Local aPDFields           As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields          As Array     // Estrutura de todos os campos do ON
    Local aCampos             as Array     // Campos
    Local aListCargos         as Array     // Array que não deixa os valores duplicar no gráfico
    Local lObfuscated         As Logical   // Flag que indica se tem algum campo para ofuscar
    Local lIntSRK             As Logical   // Integrado com SRK     
    Local dDataIntSRK         As Date      // Data de Integração com a SRK
    Local dDataInicio         As Date      // Data Início Contrato
    Local dDataFim            As Date      // Data Fim Contrato
    Local dDtUltMov           As Date      // Data Última Movimentação
    
    //Parâmetros
    Private cFilialDe           As Character // Parâmetro Filial De
    Private cFilialAte          As Character // Parâmetro Filial Até
    Private cCCDe               As Character // Parâmetro Centro de Custo De
    Private cCCAte              As Character // Parâmetro Centro de Custo Até   
    Private cMatDe              As Character // Parâmetro Matrícula De
    Private cMatAte             As Character // Parâmetro Matrícula Até
    Private cNomeDe             As Character // Parâmetro Nome De
    Private cNomeAte            As Character // Parâmetro Nome Até
    Private cFuncaoDe           As Character // Parâmetro Função De
    Private cFuncaoAte          As Character // Parâmetro Função Até
    Private aCategorias         As Array     // Parâmetro Categorias
    Private aSituacoes          As Array     // Parâmetro Situações

    Private aTotCargo           As Array // Totalizador por cargo
    Private nTot                As Numeric // Total de Empréstimos

    // Inicialização das variáveis
    cQuery              := ""
    cQuery2             := ""
    cQuery3             := ""
    cAlias              := ""
    cAlias2             := ""
    cAlias3             := ""
    cFiltro             := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    jParams             := oFilter:getParameters()
    jData               := JSONObject():New()
    jLGPD               := JSONObject():New()
    jContratos          := JSONObject():New()
    jDetalhes           := JSONObject():New()
    oExec               := NIL
    oExec2              := NIL
    oExec3              := NIL
    nParamOrder         := 1
    nAux                := 0
    nFields             := 1
    aPDFields           := {}
    cFilialDe           := ""
    cFilialAte          := ""
    cMatDe              := ""
    cMatAte             := ""
    cCCDe               := ""
    cCCAte              := "" 
    aCategorias         := {}
    aSituacoes          := {}
    aAllFields          := {}
    aCampos             := {}
    aListCargos         := {}
    lObfuscated         := .F.
    lIntSRK             := .F.
    cSituacao           := ""
    cValidFil           := fValidFil()
    cAcessaSRA          := &("{ || " + ChkRH("GPER050","SRA","2") + "}")
    cAcessaRUO          := &("{ || " + ChkRH("GPER050","RUO","2") + "}")
    cAcessaSRK          := &("{ || " + ChkRH("GPER050","SRK","2") + "}")
    cFilExec            := AllTrim(FwFilialName())
    cEmpExec            := AllTrim(FwEmpName(cEmpAnt))
    cExibeGraf          := '1'
    cNrContratoAnt      := ''
    cDescVerb           := ""
    cDescBanc           := ""
    dDataIntSRK         := Date()
    cNumContrato        := ""
    dDataInicio         := Date()
    dDataFim            := Date()
    nValContrato        := 0
    nTotParcela         := 0
    cVerba              := ""
    cBanco              := ""
    dDtUltMov           := Date()
    cStatus             := ""
    nPos                := 0
    nPos1               := 0    
    aTotCargo           := {}
    nTot                := 0

	// Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cFilialDe   := IIf(jParams:hasProperty("FilialDe")       .and. Len(jParams["FilialDe"]) > 0      .and.!Empty(jParams["FilialDe"][1]),       jParams["FilialDe"][1],           "")
        cFilialAte  := IIf(jParams:hasProperty("FilialAte")      .and. Len(jParams["FilialAte"]) > 0     .and.!Empty(jParams["FilialAte"][1]),      jParams["FilialAte"][1],          Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCCDe       := IIf(jParams:hasProperty("CCDe")           .and. Len(jParams["CCDe"]) > 0          .and.!Empty(jParams["CCDe"][1]),           jParams["CCDe"][1],               "")
        cCCAte      := IIf(jParams:hasProperty("CCAte")          .and. Len(jParams["CCAte"]) > 0         .and.!Empty(jParams["CCAte"][1]),          jParams["CCAte"][1],              Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cMatDe      := IIf(jParams:hasProperty("MatriculaDe")    .and. Len(jParams["MatriculaDe"]) > 0   .and.!Empty(jParams["MatriculaDe"][1]),    jParams["MatriculaDe"][1],        "")
        cMatAte     := IIf(jParams:hasProperty("MatriculaAte")   .and. Len(jParams["MatriculaAte"]) > 0  .and.!Empty(jParams["MatriculaAte"][1]),   jParams["MatriculaAte"][1],       Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cNomeDe     := IIf(jParams:hasProperty("NomeDe")         .and. Len(jParams["NomeDe"]) > 0        .and.!Empty(jParams["NomeDe"][1]),         jParams["NomeDe"][1],             "")
        cNomeAte    := IIf(jParams:hasProperty("NomeAte")        .and. Len(jParams["NomeAte"]) > 0       .and.!Empty(jParams["NomeAte"][1]),        jParams["NomeAte"][1],            Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
        cFuncaoDe   := IIf(jParams:hasProperty("FuncaoDe")       .and. Len(jParams["FuncaoDe"]) > 0      .and.!Empty(jParams["FuncaoDe"][1]),       jParams["FuncaoDe"][1],           "")
        cFuncaoAte  := IIf(jParams:hasProperty("FuncaoAte")      .and. Len(jParams["FuncaoAte"]) > 0     .and.!Empty(jParams["FuncaoAte"][1]),      jParams["FuncaoAte"][1],          Replicate("Z", GetSX3Cache("RA_CODFUNC", "X3_TAMANHO")))
        aCategorias := jParams["Categorias"]
        aSituacoes  := jParams["Situacoes"]
        cExibeGraf  := IIf(jParams:hasProperty("ExibeGraf")      .and. Len(jParams["ExibeGraf"]) > 0     .and.!Empty(jParams["ExibeGraf"][1]),      jParams["ExibeGraf"][1],           "1") // 1 - Sim e 2 - Não
    EndIf

    // Passa as situações no vetor para uma string
    If (Len(aSituacoes) > 0)
        cSituacao := ""
        AEval(aSituacoes, {|x| cSituacao += IIf(x == NIL .or. Empty(x), " ", x)})
    EndIf

    // Passa as situações no vetor para uma string
    If (Len(aCategorias) > 0)
        cCategoria := ""
        AEval(aCategorias, {|x| cCategoria += IIf(x == NIL .or. Empty(x), " ", x)})
    EndIf

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aCategorias)
        aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
    Next nAux  

    cJoinSRA := FWJoinFilial("RUO", "SRA")
    cJoinSRJ := FWJoinFilial("RUO", "SRJ")

    BEGIN SEQUENCE

        fCalcPercCargo()

        //Busca por todos os contratos
        cQuery = "SELECT DISTINCT "
        cQuery +=     "RUO.RUO_FILIAL, "
        cQuery +=     "RUO.RUO_MAT, "
        cQuery +=     "RUO.RUO_CATEFD, "
        cQuery +=     "SRA.RA_CIC, "
        cQuery +=     "SRA.RA_NOME, "
        cQuery +=     "SRA.RA_ADMISSA, "
        cQuery +=     "SRA.RA_CODFUNC, "
        cQuery +=     "SRJ.RJ_DESC "   
        cQuery +=     "? "
        cQuery += "FROM "
        cQuery +=     RetSQLName("RUO") + " RUO "
        cQuery += " LEFT JOIN " + RetSqlName("SRA") + " SRA ON "
        cQuery +=       cJoinSRA
        cQuery +=       " AND SRA.RA_MAT = RUO.RUO_MAT "
        cQuery +=       " AND SRA.D_E_L_E_T_ = ? " 
        cQuery += " LEFT JOIN " + RetSQLName("SRJ") + " SRJ ON "
        cQuery +=       cJoinSRJ
        cQuery +=       " AND SRJ.RJ_FUNCAO = SRA.RA_CODFUNC "
        cQuery +=       " AND SRJ.D_E_L_E_T_ = ? " 
        cQuery += "WHERE "
        cQuery +=     "SRA.RA_FILIAL       BETWEEN ? AND ? " 
        cQuery +=     "AND SRA.RA_MAT      BETWEEN ? AND ? "  
        cQuery +=     "AND SRA.RA_CC       BETWEEN ? AND ? " 
        cQuery +=     "AND SRA.RA_CODFUNC  BETWEEN ? AND ? "
        cQuery +=     "AND SRA.RA_NOME     BETWEEN ? AND ? " 

        if Len(aSituacoes) > 0
            cQuery += "AND SRA.RA_SITFOLH IN (?)  " 
        EndIf

        If Len(aCategorias) > 0
            cQuery += "AND SRA.RA_CATFUNC IN (?)  " 
        EndIf

        cQuery +=     "AND RUO.D_E_L_E_T_ = ?  "
        cQuery += "GROUP BY RUO.RUO_FILIAL, RUO.RUO_MAT, RUO.RUO_CATEFD,SRA.RA_CIC,SRA.RA_NOME,SRA.RA_ADMISSA,SRA.RA_CODFUNC,SRJ.RJ_DESC    "
        cQuery +=       " ? " 

        cQuery := ChangeQuery(cQuery)
        oExec := FwExecStatement():New(cQuery)

        // Captura e define os campos personalizados das tabelas 
        cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA, RUO"})
        oExec:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))

        oExec:SetString(nParamOrder++, " ")

        oExec:SetString(nParamOrder++, " ")

        // Filial
        oExec:SetString(nParamOrder++, cFilialDe)
        oExec:SetString(nParamOrder++, cFilialAte)

        // Matrícula
        oExec:SetString(nParamOrder++, cMatDe)
        oExec:SetString(nParamOrder++, cMatAte)

        //CC
        oExec:SetString(nParamOrder++, cCCDe)
        oExec:SetString(nParamOrder++, cCCAte)

        //Função
        oExec:SetString(nParamOrder++, cFuncaoDe)
        oExec:SetString(nParamOrder++, cFuncaoAte)

        //Nome
        oExec:SetString(nParamOrder++, cNomeDe)
        oExec:SetString(nParamOrder++, cNomeAte)

        if Len(aSituacoes) > 0
            oExec:SetIN(nParamOrder++, aSituacoes)
        EndIf

        If Len(aCategorias) > 0
            oExec:SetIN(nParamOrder++, aCategorias)
        EndIf
        
        oExec:SetString(nParamOrder++, " ")
        
        oExec:SetUnsafe(nParamOrder++, cFiltro)

        cAlias := oExec:OpenAlias()

        // Percorre todos os registros
        While (!(cAlias)->(EOF()))
            If (Eval(cAcessaSRA) .and. Eval(cAcessaRUO) .and. Eval(cAcessaSRK) .and. (cAlias)->RUO_FILIAL $ cValidFil)

                nParamOrder := 1
                lIntSRK     := .F.
                jContratos["Contratos"] := {}
                jDetalhes["Detalhes"] := {}
                cNumContrato := ''
                dDataInicio  := Date()
                dDataFim     := Date()
                nValContrato := 0
                nTotParcela  := 0
                cVerba       := ''
                cBanco       := ''
                dDtUltMov    := Date()
                cStatus      :=''
                cNrContratoAnt := ''

                jData := JSONObject():New()
                jData["PercFuncao"] := 0
                jData["ListaFuncao"] := ''

                // Armazena a quantidade de funcionários com empréstimo por cargo
                If (nPos := AScan(aTotCargo, {|x| x[1] == (cAlias)->RUO_FILIAL+' - '+(cAlias)->RA_CODFUNC})) > 0

                    If (nPos1 := AScan(aListCargos, {|x| x[1] == (cAlias)->RUO_FILIAL+' - '+(cAlias)->RA_CODFUNC})) > 0
                        jData["PercFuncao"] := 0
                        jData["ListaFuncao"] := ''
                    else
                        AAdd(aListCargos,{(cAlias)->RUO_FILIAL+' - '+(cAlias)->RA_CODFUNC})
                        jData["PercFuncao"] := (aTotCargo[nPos,2] / nTot) * 100
                        jData["ListaFuncao"] := (cAlias)->RUO_FILIAL+' - '+(cAlias)->RA_CODFUNC
                    endif
                EndIf

                jData["FilialExec"]     := Trim(cFilExec)
                jData["EmpresaExec"]    := Trim(cEmpExec)
                jData["Filial"]         := Trim(Iif(jLGPD["RUO_FILIAL"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RUO_FILIAL),  (cAlias)->RUO_FILIAL))
                jData["Matricula"]      := Trim(Iif(jLGPD["RUO_MAT"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RUO_MAT),     (cAlias)->RUO_MAT))
                jData["CPF"]            := Trim(Iif(jLGPD["RA_CIC"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CIC),     (cAlias)->RA_CIC))
                jData["Nome"]           := Trim(Iif(jLGPD["RA_NOME"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),    (cAlias)->RA_NOME))
                jData["CatESocial"]     := Trim(Iif(jLGPD["RUO_CATEFD"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RUO_CATEFD), (cAlias)->RUO_CATEFD))
                jData["DataAdm"]        := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_ADMISSA)
                jData["CodFuncao"]      := Trim(Iif(jLGPD["RA_CODFUNC"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CODFUNC), (cAlias)->RA_CODFUNC))
                jData["DescFuncao"]     := Trim(Iif(jLGPD["RJ_DESC"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RJ_DESC),    (cAlias)->RJ_DESC))
                jData["ListaFuncao"]    :=  (cAlias)->RUO_FILIAL+' - '+(cAlias)->RA_CODFUNC

                //Pega todos os contratos na RU0 do usuário
                cQuery2 = "SELECT "
                cQuery2 +=     "RUO.RUO_NRCONT, "
                cQuery2 +=     "RUO.RUO_DTINIC, "
                cQuery2 +=     "RUO.RUO_DTFIMC, "
                cQuery2 +=     "RUO.RUO_VLTOT, "
                cQuery2 +=     "RUO.RUO_PARCEL, "
                cQuery2 +=     "RUO.RUO_PD, "
                cQuery2 +=     "RUO.RUO_DTREF, "
                cQuery2 +=     "RUO.RUO_COMPET, "
                cQuery2 +=     "RUO.RUO_VLPARC, "
                cQuery2 +=     "RUO.RUO_PARPAG, "
                cQuery2 +=     "RUO.RUO_BCOCON "
                cQuery2 += "FROM "
                cQuery2 +=     RetSQLName("RUO") + " RUO "
                cQuery2 += "WHERE "
                cQuery2 +=     "RUO.RUO_FILIAL = ?  " 
                cQuery2 +=     "AND RUO.RUO_MAT = ? "
                cQuery2 += "ORDER BY RUO.RUO_NRCONT"

                cQuery2 := ChangeQuery(cQuery2)
                oExec2 := FwExecStatement():New(cQuery2)

                oExec2:SetString(nParamOrder++, (cAlias)->RUO_FILIAL) 
                oExec2:SetString(nParamOrder++, (cAlias)->RUO_MAT)  

                cAlias2 := oExec2:OpenAlias()

                While (!(cAlias2)->(EOF()))

                    lIntSRK := .F.
                    dDataIntSRK :=  totvs.framework.treports.date.stringToTimeStamp('')

                    if cNrContratoAnt == '' .Or. (cNrContratoAnt != (cAlias2)->RUO_NRCONT)
                        
                        if cNrContratoAnt != ''
                            AAdd(jContratos["Contratos"], {;
                                    "NumContrato":   cNumContrato ,;
                                    "DataInicio":    dDataInicio  ,;
                                    "DataFim":       dDataFim     ,;
                                    "ValContrato":   nValContrato ,;
                                    "TotParcela":    nTotParcela  ,;
                                    "Verba":         cVerba       ,;
                                    "Banco":         cBanco       ,;
                                    "DtUltMov":      dDtUltMov    ,;
                                    "Situacao":      cStatus      ,;
                                    "Detalhes":      jDetalhes["Detalhes"];
                            }) 

                            jData["Contratos"]   :=  jContratos["Contratos"]

                            cNumContrato := ''
                            dDataInicio  := Date()
                            dDataFim     := Date()
                            nValContrato := 0
                            nTotParcela  := 0
                            cVerba       := ''
                            cBanco       := ''
                            dDtUltMov    := Date()
                            cStatus      :=''
                            jDetalhes["Detalhes"] := {}

                        endif

                        cNrContratoAnt := (cAlias2)->RUO_NRCONT
                        nParamOrder := 1

                        //Pesquisa na SRK
                        //Busca Dados na SRK, se encontrar, pegar dados da SRK, se não, pegar da RUO
                        cQuery3 = "SELECT "
                        cQuery3 +=     "SRK.RK_PD, "
                        cQuery3 +=     "SRK.RK_VALORTO, "
                        cQuery3 +=     "SRK.RK_PARCELA, "
                        cQuery3 +=     "SRK.RK_VALORPA, "
                        cQuery3 +=     "SRK.RK_PARCPAG, "
                        cQuery3 +=     "SRK.RK_VLRPAGO, "
                        cQuery3 +=     "SRK.RK_DTMOVI, "
                        cQuery3 +=     "SRK.RK_NRCONTR, "
                        cQuery3 +=     "SRK.RK_STATUS, "
                        cQuery3 +=     "SRK.RK_PERINI, "
                        cQuery3 +=     "SRK.RK_DTREF, "
                        cQuery3 +=     "SRK.RK_BCOCONS "
                        cQuery3 += "FROM "
                        cQuery3 +=     RetSQLName("SRK") + " SRK "
                        cQuery3 += "WHERE "
                        cQuery3 +=     "SRK.RK_FILIAL =  ? " 
                        cQuery3 +=     "AND SRK.RK_MAT = ? "
                        cQuery3 +=     "AND SRK.RK_NRCONTR = ? " 
                        cQuery3 += "ORDER BY SRK.RK_NUMPAGO "    

                        cQuery3 := ChangeQuery(cQuery3)
                        oExec3 := FwExecStatement():New(cQuery3)       

                        oExec3:SetString(nParamOrder++, (cAlias)->RUO_FILIAL)        

                        oExec3:SetString(nParamOrder++, (cAlias)->RUO_MAT)

                        oExec3:SetString(nParamOrder++, (cAlias2)->RUO_NRCONT)

                        cAlias3 := oExec3:OpenAlias()

                        While (!(cAlias3)->(EOF()))

                            lIntSRK := .T.
                            dDataIntSRK := totvs.framework.treports.date.stringToTimeStamp((cAlias3)->RK_DTREF)

                            cDescVerb := (cAlias3)->RK_PD + ' - ' + DescPd((cAlias3)->RK_PD, (cAlias)->RUO_FILIAL) 
                            cDescBanc := Trim((cAlias3)->RK_BCOCONS) + ' - ' + DescBco(Trim((cAlias3)->RK_BCOCONS), (cAlias)->RUO_FILIAL)

                            cNumContrato  := Trim(Iif(jLGPD["RK_NRCONTR"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias3)->RK_NRCONTR), (cAlias3)->RK_NRCONTR))
                            dDataInicio   := totvs.framework.treports.date.stringToTimeStamp((cAlias2)->RUO_DTINIC)
                            dDataFim      := totvs.framework.treports.date.stringToTimeStamp((cAlias2)->RUO_DTFIMC)
                            nValContrato  := Iif(jLGPD["RK_VALORTO"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias3)->RK_VALORTO), (cAlias3)->RK_VALORTO)
                            nTotParcela   := Iif(jLGPD["RK_PARCELA"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias3)->RK_PARCELA), (cAlias3)->RK_PARCELA)
                            cVerba        := Trim(Iif(jLGPD["RK_PD"],      FwProtectedDataUtil():ValueAsteriskToAnonymize(cDescVerb),     cDescVerb))
                            cBanco        := Iif(jLGPD["RK_BCOCONS"], FwProtectedDataUtil():ValueAsteriskToAnonymize(cDescBanc), cDescBanc)
                            dDtUltMov     := totvs.framework.treports.date.stringToTimeStamp((cAlias3)->RK_DTMOVI)
                            cStatus      := Trim(Iif(jLGPD["RK_STATUS"], FwProtectedDataUtil():ValueAsteriskToAnonymize(fDescStatus((cAlias3)->RK_STATUS)), fDescStatus((cAlias3)->RK_STATUS)))

                            (cAlias3)->(DBSkip())
                        enddo

                        // Não achou na SRK, pega os dados da RUO
                        if !lIntSRK

                            dDataIntSRK := totvs.framework.treports.date.stringToTimeStamp('')
                            cDescVerb := (cAlias2)->RUO_PD + ' - ' + DescPd((cAlias2)->RUO_PD, (cAlias)->RUO_FILIAL) 
                            cDescBanc := Trim((cAlias2)->RUO_BCOCON) + ' - ' + DescBco(Trim((cAlias2)->RUO_BCOCON), (cAlias)->RUO_FILIAL)

                            cNumContrato := Trim(Iif(jLGPD["RUO_NRCONT"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias2)->RUO_NRCONT), (cAlias2)->RUO_NRCONT))
                            dDataInicio  := totvs.framework.treports.date.stringToTimeStamp((cAlias2)->RUO_DTINIC)
                            dDataFim     := totvs.framework.treports.date.stringToTimeStamp((cAlias2)->RUO_DTFIMC)
                            nValContrato := Iif(jLGPD["RUO_VLTOT"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias2)->RUO_VLTOT), (cAlias2)->RUO_VLTOT)
                            nTotParcela  := Iif(jLGPD["RUO_PARCEL"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias2)->RUO_PARCEL), (cAlias2)->RUO_PARCEL)
                            cVerba       := Trim(Iif(jLGPD["RUO_PD"], FwProtectedDataUtil():ValueAsteriskToAnonymize(cDescVerb), cDescVerb))
                            cBanco       := Iif(jLGPD["RUO_BCOCON"], FwProtectedDataUtil():ValueAsteriskToAnonymize(cDescBanc), cDescBanc)
                            dDtUltMov    := totvs.framework.treports.date.stringToTimeStamp((cAlias2)->RUO_DTREF)
                            cStatus      := ' - ' 
                        endif                        
                    endif

                    cDescBanc := Trim((cAlias2)->RUO_BCOCON) + ' - ' + DescBco(Trim((cAlias2)->RUO_BCOCON), (cAlias)->RUO_FILIAL)

                    //Adiciona os Detalhes
                    AAdd(jDetalhes["Detalhes"], {;
                            "Competencia":  Trim(Iif(jLGPD["RUO_COMPET"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias2)->RUO_COMPET),  (cAlias2)->RUO_COMPET)) ,;
                            "ValParcela" :  Iif(jLGPD["RUO_VLPARC"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias2)->RUO_VLPARC),  (cAlias2)->RUO_VLPARC),;
                            "ParPagas"   :  Iif(jLGPD["RUO_PARPAG"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias2)->RUO_PARPAG),  (cAlias2)->RUO_PARPAG),;  
                            "BancoDet"   :  Iif(jLGPD["RUO_BCOCON"], FwProtectedDataUtil():ValueAsteriskToAnonymize(cDescBanc),  cDescBanc),;  
                            "IntSRK"     :  Iif(lIntSRK,STR0047,STR0048),; //Sim//Não
                            "DtIntSRK"   :  dDataIntSRK;  
                    })                    

                    (cAlias2)->(DBSkip())
                enddo

                AAdd(jContratos["Contratos"], {;
                        "NumContrato":   cNumContrato ,;
                        "DataInicio":    dDataInicio  ,;
                        "DataFim":       dDataFim     ,;
                        "ValContrato":   nValContrato ,;
                        "TotParcela":    nTotParcela  ,;
                        "Verba":         cVerba       ,;
                        "Banco":         cBanco       ,;
                        "DtUltMov":      dDtUltMov    ,;
                        "Situacao":      cStatus    ,;
                        "Detalhes":      jDetalhes["Detalhes"];
                })                       

                jData["Contratos"]   :=  jContratos["Contratos"]   
            
                // Adiciona os campos customizados no JSON
                GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

                // Adiciona as informações da linha
                self:oData:appendData(jData) 
            endif

            (cAlias)->(DBSkip()) 
        Enddo
    END SEQUENCE

    // Fecha a área da tabela temporária
    If !Empty(cAlias)
        (cAlias)->(DBCloseArea())
    Endif

    // Fecha a área da tabela temporária
    If !Empty(cAlias2)
        (cAlias2)->(DBCloseArea())
    Endif

    // Fecha a área da tabela temporária
    If !Empty(cAlias3)
        (cAlias3)->(DBCloseArea())
    Endif

    // Limpa os objetos da memória 
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
	FwFreeObj(jParams)
    FwFreeObj(jContratos)
    FwFreeObj(jDetalhes)
    FwFreeObj(oExec)
    FwFreeObj(oExec2)
    FwFreeObj(oExec3)
    FwFreeArray(aAllFields)
    FwFreeArray(aPDFields)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)
    FwFreeArray(aTotCargo)
Return self:oData

/*/{Protheus.doc} GPER050TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author karina.alves
    @since 14/09/2025
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER050TReportsBusinessObject
    // Declaração das variáveis locais
    Local aFields  As Array // Campos das propriedades aninhadas

    // Inicialização das variáveis
    aFields := {}

    // Define os parâmetros do relatório
    self:oSchema:addParameter("FilialDe",      STR0004 + ' ' + STR0005,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0004 + ' ' + STR0005) //  "Filial De"
    self:oSchema:addParameter("FilialAte",     STR0004 + ' ' + STR0006,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0004 + ' ' + STR0006) //  "Filial Até"
    self:oSchema:addParameter("CCDe",          STR0007 + ' ' + STR0005,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0007 + ' ' + STR0005) //  "Centro de Custo De"
    self:oSchema:addParameter("CCAte",         STR0007 + ' ' + STR0006,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0007 + ' ' + STR0006) //  "Centro de Custo Até"
    self:oSchema:addParameter("MatriculaDe",   STR0008 + ' ' + STR0005,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0008 + ' ' + STR0005) //  "Matrícula De"
    self:oSchema:addParameter("MatriculaAte",  STR0008 + ' ' + STR0006,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0008 + ' ' + STR0006) //  "Matrícula Até"
    self:oSchema:addParameter("NomeDe",        STR0009 + ' ' + STR0005,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0009 + ' ' + STR0005) //  "Nome De"
    self:oSchema:addParameter("NomeAte",       STR0009 + ' ' + STR0006,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0009 + ' ' + STR0006) //  "Nome Até"
    self:oSchema:addParameter("FuncaoDe",      STR0010 + ' ' + STR0005,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0010 + ' ' + STR0005) //  "Função De"
    self:oSchema:addParameter("FuncaoAte",     STR0010 + ' ' + STR0006,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0010 + ' ' + STR0006) //  "Função Até"
    self:oSchema:addParameter("Situacoes",     STR0011,                     "string", .T., NIL, NIL, NIL, NIL, NIL, STR0011)                 //  "Situações"
    self:oSchema:addParameter("Categorias",    STR0012,                     "string", .T., NIL, NIL, NIL, NIL, NIL, STR0012)                 //  "Categorias"
    self:oSchema:addParameter("ExibeGraf",     STR0013,                     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0013)                 //  "Exibe Gráfico"

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("FilialDe",        "/api/rh/smartview/v1/options/GPEParams/getBranches",       2)
    self:setCustomURL("FilialAte",       "/api/rh/smartview/v1/options/GPEParams/getBranches",       2)
    self:setCustomURL("CCDe",            "api/framework/v1/genericLookupService/smartview/CTT",      2)
    self:setCustomURL("CCAte",           "api/framework/v1/genericLookupService/smartview/CTT",      2)
    self:setCustomURL("MatriculaDe",     "/api/framework/v1/genericLookupService/smartview/SRA",     2)
    self:setCustomURL("MatriculaAte",    "/api/framework/v1/genericLookupService/smartview/SRA",     2)
    self:setCustomURL("FuncaoDe",        "/api/framework/v1/genericLookupService/smartview/SRJ",     2)
    self:setCustomURL("FuncaoAte",       "/api/framework/v1/genericLookupService/smartview/SRJ",     2)
    self:setCustomURL("Situacoes",       "/api/rh/smartview/v1/options/GPEParams/getSX5/31",         2)
    self:setCustomURL("Categorias",      "/api/rh/smartview/v1/options/GPEParams/getSX5/28",         2)
    self:setCustomURL("ExibeGraf",       "/api/rh/smartview/v1/options/GPEParams/getSNOptions",      1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",   STR0004 + ' ' + STR0014,        "string", STR0004 + ' ' + STR0014,            "FilialExec",   NIL, NIL, NIL, .F.) // "Filial Execução"
    self:oSchema:addProperty("EmpresaExec",  STR0015 + ' ' + STR0014,        "string", STR0015 + ' ' + STR0014,            "EmpresaExec",  NIL, NIL, NIL, .F.) // "Empresa Execução"
    self:oSchema:addProperty("Filial",       STR0004,                        "string", STR0004,                            "Titulo",       NIL, NIL, NIL, .F.) // "Filial"
    self:oSchema:addProperty("Matricula",    STR0008,                        "string", STR0008,                            "Matricula",    NIL, NIL, NIL, .F.) // "Matrícula"
    self:oSchema:addProperty("CPF",          STR0037,                        "string", STR0037,                            "CPF",          NIL, NIL, NIL, .F.) // "CPF"
    self:oSchema:addProperty("Nome",         STR0016,                        "string", STR0016,                            "Nome",         NIL, NIL, NIL, .F.) // "Nome"
    self:oSchema:addProperty("CatESocial",   STR0038,                        "string", STR0038,                            "CatESocial",   NIL, NIL, NIL, .F.) // "Categoria Esocial"
    self:oSchema:addProperty("DataAdm",      STR0017,                        "date",   STR0017,                            "DataAdm",      NIL, NIL, NIL, .F.) // "Data Admissão"
    self:oSchema:addProperty("CodFuncao",    STR0018 + ' ' + STR0019,        "string", STR0018 + ' ' + STR0019,            "CodFuncao",    NIL, NIL, NIL, .F.) // "Código Função"
    self:oSchema:addProperty("DescFuncao",   STR0020 + ' ' + STR0019,        "string", STR0020 + ' ' + STR0019,            "DescFuncao",   NIL, NIL, NIL, .F.) // "Descrição Função"
    self:oSchema:addProperty("ListaFuncao",  STR0049 + ' ' + STR0019,        "string", STR0049 + ' ' + STR0019,            "ListaFuncao",  NIL, NIL, NIL, .F.) // "Lista Função"
    self:oSchema:addProperty("PercFuncao",   STR0050 + ' ' + STR0019,        "number", STR0050 + ' ' + STR0019,            "PercFuncao",   NIL, NIL, NIL, .F.) // "Percentual Função"

    //Contratos
    aCampos := {}
    AAdd(aCampos, {"NumContrato",    STR0021 + ' ' + STR0022,    "string",      "NumContrato"}) // "Número Contrato"
    AAdd(aCampos, {"DataInicio",     STR0023 + ' ' + STR0024,    "date",        "DataInicio"})  // "Data Início"
    AAdd(aCampos, {"DataFim",        STR0023 + ' ' + STR0025,    "date",        "DataFim"})     // "Data Fim"
    AAdd(aCampos, {"ValContrato",    STR0026 + ' ' + STR0022,    "number",      "ValContrato"}) // "Valor Contrato"
    AAdd(aCampos, {"TotParcela",     STR0027 + ' ' + STR0028,    "string",      "TotParcela"})  // "Total Parcela"
    AAdd(aCampos, {"Verba",          STR0029,                    "string",      "Verba"})       // Verba
    AAdd(aCampos, {"Banco",          STR0030,                    "string",      "Banco"})       // Banco
    AAdd(aCampos, {"DtUltMov",       STR0031,                    "date",        "DtUltMov"})    // "Data Última Movimentação"
    AAdd(aCampos, {"Situacao",       STR0039,                    "string",      "Situacao"})    // Situação
    AAdd(aCampos, {"Detalhes",       STR0046,                    "string",      "Detalhes"})    // Detalhes
    self:addNestedProperty("Contratos",  STR0040,  STR0040, NIL, aCampos) // Contratos

    aCampos := {}
    AAdd(aCampos, {"Competencia",   STR0032,                  "string", STR0032}) // "Competência"
    AAdd(aCampos, {"ValParcela",    STR0026 + ' ' + STR0028,  "number", STR0026 + ' ' + STR0028}) // "Valor Parcela"
    AAdd(aCampos, {"ParPagas",      STR0028 + ' ' + STR0033,  "number", STR0028 + ' ' + STR0033}) // "Parcela Paga"
    AAdd(aCampos, {"BancoDet",      STR0030 + ' ' + STR0046,  "string", STR0030 + ' ' + STR0046}) // "Banco Detalhes"
    AAdd(aCampos, {"IntSRK",        STR0035 + ' ' + STR0034,  "string", STR0035 + ' ' + STR0034}) // "Integrado RGB"
    AAdd(aCampos, {"DtIntSRK",      STR0036 + ' ' + STR0034,  "date",   STR0036 + ' ' + STR0034}) // "Data Integração RGB"

    self:transformInNested("Detalhes", NIL, aCampos) // Detalhes

Return self:oSchema

/*/{Protheus.doc} GPER050TReportsBusinessObject
    Retorna Descrição do Status
    @type Method
    @version 12.1.2510
    @author karina.alves
    @since 05/09/2025
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Static Function fDescStatus(cCod)
    Local cDesc

    cDesc := '-'

    if cCod == '1'
        cDesc := STR0041 //"Solicitado"
    elseif cCod == '2'
        cDesc := STR0042 //"Ativo"
    elseif cCod == '3'
        cDesc := STR0043 //"Pago"
    elseif cCod == '4'
        cDesc := STR0044 //"Suspenso"
    elseif cCod == '5'
        cDesc := STR0045 //"Descontinuado"
    endif
Return cDesc


/*/{Protheus.doc} GPER050TReportsBusinessObject
    Preenche os arrays com os valores totais por cargo
    @type Method
    @version 12.1.2510
    @author karina.alves
    @since 14/09/2025
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Static Function fCalcPercCargo()
    Local cQuery        As Character
    Local cJoinSRA      As Character
    Local cAlias        As Character    
    Local oExec         As Object
    Local nParamOrder   As Numeric    
    Local nPos          As Numeric

    cQuery      := ''
    cAlias      := ''
    cJoinSRA    := FWJoinFilial("SRA", "RUO")
    oExec       := NIL
    nParamOrder := 1    
    nPos        := 0    

    //Montagem da query de busca que busca os valores
    //Busca por todos os contratos
    cQuery = "SELECT "
    cQuery +=     "RUO.RUO_FILIAL, "
    cQuery +=     "SRA.RA_CODFUNC, "
    cQuery +=     "SRA.RA_MAT "
    cQuery += "FROM "
    cQuery +=     RetSQLName("RUO") + " RUO "
    cQuery += " LEFT JOIN " + RetSqlName("SRA") + " SRA ON "
    cQuery +=       cJoinSRA
    cQuery +=       " AND SRA.RA_MAT = RUO.RUO_MAT "
    cQuery +=       " AND SRA.D_E_L_E_T_ = ? "   
    cQuery += "WHERE "
    cQuery +=     "SRA.RA_FILIAL       BETWEEN ? AND ? " 
    cQuery +=     "AND SRA.RA_MAT      BETWEEN ? AND ? "  
    cQuery +=     "AND SRA.RA_CC       BETWEEN ? AND ? " 
    cQuery +=     "AND SRA.RA_CODFUNC  BETWEEN ? AND ? "
    cQuery +=     "AND SRA.RA_NOME     BETWEEN ? AND ? " 

    if Len(aSituacoes) > 0
        cQuery += "AND SRA.RA_SITFOLH IN (?)  " 
    EndIf

    If Len(aCategorias) > 0
        cQuery += "AND SRA.RA_CATFUNC IN (?)  " 
    EndIf
        
    cQuery +=     "AND RUO.D_E_L_E_T_ = ?  "
    cQuery += "GROUP BY  RUO.RUO_FILIAL, SRA.RA_CODFUNC, RA_MAT"

    cQuery := ChangeQuery(cQuery)
    oExec := FwExecStatement():New(cQuery)

    oExec:SetString(nParamOrder++, " ") 

    // Filial
    oExec:SetString(nParamOrder++, cFilialDe)
    oExec:SetString(nParamOrder++, cFilialAte)

    // Matrícula
    oExec:SetString(nParamOrder++, cMatDe)
    oExec:SetString(nParamOrder++, cMatAte)

    //CC
    oExec:SetString(nParamOrder++, cCCDe)
    oExec:SetString(nParamOrder++, cCCAte)

    //Função
    oExec:SetString(nParamOrder++, cFuncaoDe)
    oExec:SetString(nParamOrder++, cFuncaoAte)

    //Nome
    oExec:SetString(nParamOrder++, cNomeDe)
    oExec:SetString(nParamOrder++, cNomeAte)   

    if Len(aSituacoes) > 0
        oExec:SetIN(nParamOrder++, aSituacoes) 
    EndIf

    If Len(aCategorias) > 0
        oExec:SetIN(nParamOrder++, aCategorias) 
    EndIf
    
    oExec:SetString(nParamOrder++, " ")
        
    cAlias := oExec:OpenAlias()

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))

        If (nPos := AScan(aTotCargo, {|x| x[1] == (cAlias)->RUO_FILIAL+' - '+(cAlias)->RA_CODFUNC})) == 0
            AAdd(aTotCargo,{(cAlias)->RUO_FILIAL+' - '+(cAlias)->RA_CODFUNC , 1 })
        else
            aTotCargo[nPos,2] := aTotCargo[nPos,2] + 1
        endif

        nTot ++

        (cAlias)->(DBSkip()) 
    enddo

     // Fecha a área da tabela temporária
    If !Empty(cAlias)
        (cAlias)->(DBCloseArea())
    Endif

    // Libera os objetos da memória
    FwFreeObj(oExec)
return Nil
