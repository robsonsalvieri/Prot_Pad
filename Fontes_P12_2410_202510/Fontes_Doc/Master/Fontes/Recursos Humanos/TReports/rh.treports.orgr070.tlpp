#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "rh.treports.orgr070.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAORG", tables="RDK, RD4, RCL", name="Impressão da Visão", country="ALL", initialRelease="12.1.2210") //"Impressão da Visão"
class ORGR070ReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass
 
method new() as object class ORGR070ReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)// "Impressão da Visão"
return self
 
method getDescription() as character class ORGR070ReportsBusinessObject
return STR0002 //"Objeto contendo informações da estrutura da visão selecionada."
 
method getAreas() as array class ORGR070ReportsBusinessObject
return {"RH"}
 
method getData(nPage as numeric, oFilter as object) as object class ORGR070ReportsBusinessObject
    local cAlias        as character
    local cQuery        as character
    local cFilExec      as character
	local cEmpExec      as character
    local cFiltroClean  as character
    local cVisao        as character 
    local cDesloca      as character
    local lDepto        as logical  
    local nNPosto       as numeric
    local nOPosto       as numeric
	local nNiveis       as numeric 
    Local nParamOrder   as Numeric
    Local oStatement    as Object
    
    cFiltroClean  := fGetFiltroClean(oFilter:getSQLExpression())
    cVisao        := fGetParams(cFiltroClean, "RDK_CODIGO" 	, "C")
	nNiveis       := fGetParams(cFiltroClean, "PAR_NIVEIS"	, "N")
    lDepto        := .F.

    //Variáveis com o nome da Filial e Grupo de Empresa
	cFilExec := AllTrim(FWFilialName())
	cEmpExec := AllTrim(FWEmpName(cEmpAnt))

    cDBMS   := Upper(TcGetDb())
	cLength := If( "MSSQL" $ cDBMS , "%Len(RD4.RD4_CHAVE)%" , "%Length(trim(RD4.RD4_CHAVE))%")

    DbSelectArea( "RDK" )
    DbSetOrder( RetOrdem( "RDK", "RDK_FILIAL+RDK_CODIGO" ) )
    If DbSeek( xFilial("RDK") + cVisao )
        lDepto := RDK->RDK_HIERAR == "1"	
    EndIf

    cQuery := "SELECT "
    cQuery += "RDK.RDK_FILIAL, RDK.RDK_CODIGO, RDK.RDK_DESC, "  
    cQuery += "RD4.RD4_CODIGO, RD4.RD4_FILIDE, RD4.RD4_CODIDE , RD4.RD4_DESC, RD4.RD4_CHAVE, "
    cQuery += "'*' AS cDESLOCA, "
    cQuery += "0 AS nNPOSTO, 0 AS nOPOSTO "
    cQuery += "FROM " + RetSqlName("RDK") + " RDK "
    cQuery += "LEFT JOIN " + RetSqlName("RD4") + " RD4 " 
    cQuery += "ON " + FWJoinFilial( "RD4", "RDK" ) + " "
    cQuery += " AND RD4.RD4_CODIGO = RDK.RDK_CODIGO "
    cQuery += " AND (LEN(RD4.RD4_CHAVE) <= ? OR ? = ?) "         // nNiveis * 3 | nNiveis | 0
    cQuery += " AND RD4.D_E_L_E_T_ = ? "                         // " "
    cQuery += "WHERE RDK.RDK_CODIGO = ? AND RDK.D_E_L_E_T_ = ? " // cVisao | " "
    cQuery += " GROUP BY "
    cQuery += "RDK.RDK_FILIAL, RDK.RDK_CODIGO, RDK.RDK_DESC, RD4.RD4_CODIGO, RD4.RD4_FILIDE, RD4.RD4_CODIDE , RD4.RD4_DESC, RD4.RD4_CHAVE "
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Define o valor dos bind parameters
    nParamOrder := 1
    oStatement:SetNumeric(nParamOrder++, nNiveis * 3)
    oStatement:SetNumeric(nParamOrder++, nNiveis)
    oStatement:SetNumeric(nParamOrder++, 0)
    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetString(nParamOrder++, cVisao)
    oStatement:SetString(nParamOrder++, " ")

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    while !(cAlias)->(Eof())
        //Atualizacao do numero de Ocupantes/Postos nos registros
        GetPostInfo((cAlias)->RD4_CODIDE, lDepto, @nOposto, @nNposto )
        
        //Informação de Identação dos subniveis
        cDesloca := Replicate(' ',Len(Alltrim((cAlias)->RD4_CHAVE))*3)+"|___"

        self:oData:appendData({ "RDK_CODIGO": (cAlias)->RDK_CODIGO,;
                                "RDK_DESC"  : (cAlias)->RDK_DESC,;
                                "RD4_CODIGO": (cAlias)->RD4_CODIGO,;
                                "RD4_DESC"  : (cAlias)->RD4_DESC,;
                                "RD4_FILIDE": (cAlias)->RD4_FILIDE,;
                                "RD4_CODIDE": (cAlias)->RD4_CODIDE,;
                                "RD4_CHAVE" : (cAlias)->RD4_CHAVE,;
                                "DESLOCA"   : cDESLOCA,;
                                "OPOSTO"    : nOPOSTO,;  
                                "NPOSTO"    : nNPOSTO,;                                                                
                                "FilialExec": cFilExec,;
			                    "EmpresaExec": cEmpExec })                         
                
        (cAlias)->( dbSkip() )
    end

    (cAlias)->( DBCloseArea() )
return self:oData 
 
method getSchema() as object class ORGR070ReportsBusinessObject
    local aFieldsRDK as array
    local aFieldsRD4 as array
      
    aFieldsRDK := { "RDK_CODIGO", "RDK_DESC" }
    aFieldsRD4 := { "RD4_CODIGO", "RD4_DESC", "RD4_FILIDE" , "RD4_CODIDE", "RD4_CHAVE" }
     
    self:oSchema:aliasToSchema("RDK", aFieldsRDK) 
    self:oSchema:aliasToSchema("RD4", aFieldsRD4)
    self:oSchema:addProperty("FilialExec", "Filial de Execução", "string", "Filial Exec", "FilialExec")
    self:oSchema:addProperty("EmpresaExec", "Empresa de Execução", "string", "Empresa Exec", "EmpresaExec")
    self:oSchema:addProperty("NPOSTO", "Numero de Postos", "number", "Numero de Postos", "NPOSTO")
    self:oSchema:addProperty("OPOSTO", "Ocupantes do Posto", "number", "Ocupantes do Posto", "OPOSTO")
    self:oSchema:addProperty("DESLOCA", "Deslocamento de niveis", "string", "Deslocamento de niveis", "DESLOCA")
    	
    //Parâmetros
    self:oSchema:addProperty("PAR_NIVEIS", "Parâmetro Niveis", "number", "Quantidade de Níveis", "PAR_NIVEIS")

return self:oSchema


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ORGR070   ºAutor  ³Rogerio Ribeiro     º Data ³  12/08/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºFuncao    ³GetPostInfo												  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GetPostInfo(cCodPar, lDepto, nOcup, nPostos)

	Local cFilPar
    Local cRCLALias
	Local cSQBALias
	Local cAliasQry
	Local cFilRCLSQB:= FWJoinFilial("SQB", "RCL")
    Local cQuery := ""
    Local nParamOrder := 1
    Local oStatement

	cRCLALias:= RetSqlName("RCL")
	cSQBALias:= RetSqlName("SQB")
	
	If lDepto
		cFilPar := xFilial("SQB")
		cQuery := "SELECT "
		cQuery +=     "SUM(RCL_OPOSTO) AS RCL_OPOSTO, "
		cQuery +=     "SUM(RCL_NPOSTO) AS RCL_NPOSTO "
		cQuery += "FROM "
		cQuery +=     cRCLALias + " RCL "
		cQuery +=     "INNER JOIN " 
        cQuery +=         cSQBALias + " SQB "
        cQuery +=         "ON "
		cQuery +=             cFilRCLSQB + " "
		cQuery +=             "AND SQB.QB_DEPTO   = RCL.RCL_DEPTO "
		cQuery +=             "AND SQB.D_E_L_E_T_ = ? " // " "
		cQuery += "WHERE "
		cQuery +=     "SQB.QB_FILIAL  = ? AND " // cFilPar
		cQuery +=     "SQB.QB_DEPTO   = ? AND " // cCodPar
		cQuery +=     "RCL.D_E_L_E_T_ = ?  "    // " "
		cQuery += "GROUP BY "
		cQuery +=     "SQB.QB_DEPTO "
	Else
		cFilPar := xFilial("RCL")
		cQuery := "SELECT "
		cQuery +=     "RCL_OPOSTO, "
		cQuery +=     "RCL_NPOSTO "
		cQuery += "FROM "
		cQuery +=     cRCLALias + " "
		cQuery += "WHERE "
		cQuery +=     "RCL_FILIAL = ? AND " // cFilPar
		cQuery +=     "RCL_POSTO  = ? AND " // cCodPar
		cQuery +=     "D_E_L_E_T_ = ? "     // " "
	EndIf

    // Realiza adequações na query conforme o banco de dados usado no sistema
    cQuery  := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Define o valor dos bind parameters
    nParamOrder := 1
    If (lDepto)
        oStatement:SetString(nParamOrder++, " ")
    EndIf

    oStatement:SetString(nParamOrder++, cFilPar)
    oStatement:SetString(nParamOrder++, cCodPar)
    oStatement:SetString(nParamOrder++, " ")

    // Executa a query e retorna o alias criado
    cAliasQry := oStatement:OpenAlias()

    nOcup   := (cAliasQry)->RCL_OPOSTO
    nPostos := (cAliasQry)->RCL_NPOSTO

	(cAliasQry)->(DBCloseArea())
Return 

/*/{Protheus.doc} fGetFiltroClean
//Limpa a string de filtro para transformar em array.
@author Caio Kretzer
@since 05/07/2023
/*/
Static Function fGetFiltroClean(cFiltro)
Return StrTran(StrTran(StrTran(StrTran(cFiltro, " AND ", "=")," = ","="),"(",""),")","")

/*/{Protheus.doc} fGetParams
//Retorna o Parâmetro solicitado (se existir)
@author Caio Kretzer
@since 05/07/2023
/*/
Static Function fGetParams(cFiltro, cParam, cTipo)
    Local aParams := StrTokArr2(cFiltro, "=")
    Local nPos
    Local cRet    := ""

	// default para caso não venha parâmetro
	If cTipo == 'N'
		cRet := 1
	Endif
    
    If Len(aParams) > 1
        nPos    := aScan(aParams, cParam)
        cRet := Iif(cTipo == 'C', StrTran(aParams[nPos+1], "'", ""), Val(aParams[nPos+1]))
    Endif
Return cRet
