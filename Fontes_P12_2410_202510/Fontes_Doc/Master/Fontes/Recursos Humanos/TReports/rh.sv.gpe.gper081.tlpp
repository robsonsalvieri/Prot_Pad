#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper081.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER081TReportsBusinessObject:new()
    Classe do objeto de negócio do relatório Folha Fechada - Comparação de Verbas (GPER081).
    @type Class
    @version 12.1.2410
    @author caio.kretzer
    @since 14/01/2025
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRD, SRA, CTT, SRV", customTables="SRA, SRD, SRV", name="Rel. Folha Fechada - Comparação de Verbas", country="ALL", initialRelease="12.1.2310")
Class GPER081TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object

	@Get("/api/rh/smartview/v1/options/GPER081/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} GPER081TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author caio.kretzer
    @since 14/01/2025
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER081TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Folha Fechada - Comparação de Verbas"

Return self

/*/{Protheus.doc} GPER081TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author caio.kretzer
    @since 14/01/2025
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER081TReportsBusinessObject
Return STR0003 // "Relatório Folha Fechada - Comparação de Verbas"

/*/{Protheus.doc} GPER081TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author caio.kretzer
    @since 14/01/2025
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER081TReportsBusinessObject
    // Declaração das variáveis locais
    Local cAlias          As Character // Alias do arquivo temporário principal
    Local cQuery          As Character // Query a ser executada
    Local cWhere          As Character // Cláusula where da query
    Local cFilExec        As Character // Nome da filial de execução
    Local cEmpExec        As Character // Nome da empresa de execução
    Local cFilSRASRD      As Character // Condição de ligação entre SRA e SRD
    Local cFilCTTSRA      As Character // Condição de ligação entre CTT e SRA
    Local cFilSRVSRD      As Character // Condição de ligação entre SRV e SRD
    Local cProcesso       As Character // Parâmetro Processo
    Local cFilialDe       As Character // Parâmetro Filial De
    Local cFilialAte      As Character // Parâmetro Filial De
    Local cMatDe          As Character // Parâmetro Matrícula De
    Local cMatAte         As Character // Parâmetro Matrícula Até
    Local cCcDe           As Character // Parâmetro Centro de Custo De
    Local cCcAte          As Character // Parâmetro Centro de Custo Até
    Local cName           As Character // Nome real do campo
    Local nFields         As Numeric
    Local jParams         As JSON      // Parâmetros do relatório
    Local jLGPD           As JSON      // Indica quais campos devem ser ofuscados
    Local jData           As JSON
    Local aVerbas         As Array     // Parâmetro Verbas
    Local aCategorias     As Array     // Vetor de categorias
    Local aSituacoes      As Array     // Vetor de situações
    Local aPDFields       As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields      As Array     // Estrutura de todos os campos do ON
    Local nParamOrder     As Numeric   // Ordem dos bind parameters da query
    Local oStatement      As Object    // Instância da classe FwExecStatement
    Local nX              As Numeric
    Local lObfuscated     As Logical   // Flag que indica se tem algum campo para ofuscar
    Local dDataRef        As Date      // Data de referência
    Local cPeridiocidade  As Character // Peridiocidade
    Local cTipoDataPer    As Character // Tipo de Data Periodo   
    Local cAgrupador      As Character // Agrupador
    Local nMesSub         As Numeric   // Meses subtraídos
    Local nNumMeses       As Numeric   // Número de meses
    Local dStartDate1     As Date      // Data Inicial 1
    Local dEndDate1       As Date      // Data Final 1
    Local dStartDate2     As Date      // Data Inicial 2
    Local dEndDate2       As Date      // Data Final 2
    Local dStartDate3     As Date      // Data Inicial 3
    Local dEndDate3       As Date      // Data Final 3
    Local dStartDate4     As Date      // Data Inicial 4
    Local dEndDate4       As Date      // Data Final 4
    Local dStartDate5     As Date      // Data Inicial 5
    Local dEndDate5       As Date      // Data Final 5
    Local dStartDate6     As Date      // Data Inicial 6
    Local dEndDate6       As Date      // Data Final 6
    Local cCampoAgrupa    As Character // Campo de Agrupamento
    Local cDescAgrupa     As Character // Descrição do Agrupamento
    Local cCustomFields   As Character // Campos customizados

    // Inicialização das variáveis locais
    cAlias          := GetNextAlias()
    cQuery          := ""
    cWhere          := ""
    cFilExec        := AllTrim(FwFilialName())
    cEmpExec        := AllTrim(FwEmpName(cEmpAnt))
    cFilSRASRD      := FWJoinFilial("SRA", "SRD")
    cFilCTTSRA      := FWJoinFilial("CTT", "SRA")
    cFilSRVSRD      := FWJoinFilial("SRV", "SRD")
    cProcesso       := ""
    cFilialDe       := ""
    cFilialAte      := ""
    cMatDe          := ""
    cMatAte         := ""
    cCcDe           := ""
    cCcAte          := ""
    jParams         := oFilter:getParameters()
    aVerbas         := {}
    aCategorias     := {}
    aSituacoes      := {}
    nParamOrder     := 1
    oStatement      := NIL
    aPDFields       := {}
    lObfuscated     := .F.
    aAllFields      := {}
    nFields         := 1
    cName           := ""
    jLGPD           := JSONObject():New()
    dDataRef        := dDatabase
    nX              := 1
    cPeridiocidade  := ""
    cTipoDataPer    := ""
    cAgrupador      := ""
    nMesSub         := 0
    nNumMeses       := 0
    dStartDate1     := dDatabase
    dEndDate1       := dDatabase
    dStartDate2     := dDatabase
    dEndDate2       := dDatabase
    dStartDate3     := dDatabase
    dEndDate3       := dDatabase
    dStartDate4     := dDatabase
    dEndDate4       := dDatabase
    dStartDate5     := dDatabase
    dEndDate5       := dDatabase
    dStartDate6     := dDatabase
    dEndDate6       := dDatabase
    cCampoAgrupa    := ""
    cDescAgrupa     := ""
    cCustomFields   := ""

    // Captura o valor dos parâmetros
    cProcesso       := IIf(!Empty(jParams["PAR_PROCESSO"][1]),       jParams["PAR_PROCESSO"][1],       "")
    cFilialDe       := IIf(!Empty(jParams["PAR_FILIALDE"][1]),       jParams["PAR_FILIALDE"][1],       "")
    cFilialAte      := IIf(!Empty(jParams["PAR_FILIALATE"][1]),      jParams["PAR_FILIALATE"][1],      Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
    cMatDe          := IIf(!Empty(jParams["PAR_MATDE"][1]),          jParams["PAR_MATDE"][1],          "")
    cMatAte         := IIf(!Empty(jParams["PAR_MATATE"][1]),         jParams["PAR_MATATE"][1],         Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
    cCcDe           := IIf(!Empty(jParams["PAR_CCDE"][1]),           jParams["PAR_CCDE"][1],           "")
    cCcAte          := IIf(!Empty(jParams["PAR_CCATE"][1]),          jParams["PAR_CCATE"][1],          Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
    cPeridiocidade  := IIf(!Empty(jParams["peridiocidade"][1]),      jParams["peridiocidade"][1],      "1")
    cTipoDataPer    := IIf(!Empty(jParams["tipoDataPer"][1]),        jParams["tipoDataPer"][1],        "1")
    cAgrupador      := IIf(!Empty(jParams["agrupador"][1]),          jParams["agrupador"][1],          "1")
    aVerbas         := jParams["PAR_VERBAS"]
    aCategorias     := jParams["PAR_CATEGORIAS"]
    aSituacoes      := jParams["PAR_SITUACOES"]

    // Verifica se precisa fazer o tratamento para  LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    If cPeridiocidade == "1" // Mensal
        nNumMeses := 0
    ElseIf cPeridiocidade == "2" // Bimestral
        nNumMeses := 1
        If cTipoDataPer == "1"
            If Month(dDataRef) % 2 == 0
                nMesSub := 1
            Endif
        Endif
    ElseIf cPeridiocidade == "3" // Trimestral
        nNumMeses := 2
        If cTipoDataPer == "1"
            If Month(dDataRef) % 3 == 0
                nMesSub := 2
            ElseIf Month(dDataRef) % 3 == 2
                nMesSub := 1
            Endif
        Endif
    Else // Semestral
        nNumMeses := 5
        If cTipoDataPer == "1"
            If Month(dDataRef) % 6 == 0
                nMesSub := 5
            ElseIf Month(dDataRef) % 6 != 1
                nMesSub := (Month(dDataRef) % 6) - 1
            Endif
        Endif
    Endif

    // Define dDataRef como o primeiro dia do mês ajustado
    dDataRef := FirstDate(MonthSub(dDataRef, nMesSub))

    If cTipoDataPer == "2"
        dStartDate1 := MonthSub(dDataRef, nNumMeses)
        dEndDate1   := dDataRef
    Else
        dStartDate1 := dDataRef
        dEndDate1   := MonthSum(dStartDate1, nNumMeses)
    Endif

    dEndDate2   := MonthSub(dStartDate1, 1)
    dStartDate2 := MonthSub(dEndDate2, nNumMeses)
    dEndDate3   := MonthSub(dStartDate2, 1)
    dStartDate3 := MonthSub(dEndDate3, nNumMeses)
    dEndDate4   := MonthSub(dStartDate3, 1)
    dStartDate4 := MonthSub(dEndDate4, nNumMeses)
    dEndDate5   := MonthSub(dStartDate4, 1)
    dStartDate5 := MonthSub(dEndDate5, nNumMeses)
    dEndDate6   := MonthSub(dStartDate5, 1)
    dStartDate6 := MonthSub(dEndDate6, nNumMeses)

    For nX:=1 To Len(aSituacoes)
        If Empty(aSituacoes[nX])
            aSituacoes[nX] := ' '
        Endif
    Next nX

    If Len(aSituacoes) > 0
        cWhere += " AND SRA.RA_SITFOLH IN (?) "      
    Endif    

    If Len(aCategorias) > 0
        cWhere += " AND SRA.RA_CATFUNC IN (?) "
    Endif 

    If Len(aVerbas) > 0
        cWhere += " AND SRD.RD_PD IN (?) "
    Endif

    If !Empty(cProcesso)
        cWhere += " AND SRD.RD_PROCES = ? "
    Endif

    cCampoAgrupa        := "''"
    cDescAgrupa         := "''"
    If cAgrupador == "2" // Centro de Custo
        cCampoAgrupa    := "SRA.RA_CC"
        cDescAgrupa     := "CTT.CTT_DESC01" 
    ElseIf cAgrupador == "3" // Matrícula
        cCampoAgrupa    := "SRA.RA_MAT"
        cDescAgrupa     := "SRA.RA_NOME" 
    Endif

    cQuery += " SELECT "
    cQuery += " SRD.RD_FILIAL, "
    cQuery += " SRD.RD_PD, "
    cQuery += " SRV.RV_DESC, "
    cQuery += " ? CAMPOAGRUPA," // 1
    cQuery += " ? DESCAGRUPA," // 2
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_HORAS ELSE 0 END) HORAS1, "     // 3 e 4
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_VALINFO ELSE 0 END) VALINFO1, " // 5 e 6
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_VALOR ELSE 0 END) VALOR1, "     // 7 e 8
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_HORAS ELSE 0 END) HORAS2, "     // 9 e 10
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_VALINFO ELSE 0 END) VALINFO2, " // 11 e 12
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_VALOR ELSE 0 END) VALOR2, "     // 13 e 14
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_HORAS ELSE 0 END) HORAS3, "     // 15 e 16
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_VALINFO ELSE 0 END) VALINFO3, " // 17 e 18
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_VALOR ELSE 0 END) VALOR3, "     // 19 e 20
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_HORAS ELSE 0 END) HORAS4, "     // 21 e 22
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_VALINFO ELSE 0 END) VALINFO4, " // 23 e 24
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_VALOR ELSE 0 END) VALOR4, "     // 25 e 26
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_HORAS ELSE 0 END) HORAS5, "     // 27 e 28
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_VALINFO ELSE 0 END) VALINFO5, " // 29 e 30
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_VALOR ELSE 0 END) VALOR5, "     // 31 e 32
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_HORAS ELSE 0 END) HORAS6, "     // 33 e 34
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_VALINFO ELSE 0 END) VALINFO6, " // 35 e 36
    cQuery += " SUM(CASE WHEN RD_DATARQ BETWEEN ? AND ? THEN RD_VALOR ELSE 0 END) VALOR6 "      // 37 e 38
    cQuery += " ? " // 39
    cQuery += " FROM " + RetSqlName("SRD") + " SRD "
    cQuery += " INNER JOIN " + RetSqlName("SRA") + " SRA "
    cQuery += "    ON " + cFilSRASRD + " "
    cQuery += "   AND SRA.RA_MAT        = SRD.RD_MAT "
    cQuery += "   AND SRA.RA_CC        BETWEEN ? AND ? " // 40 e 41
    cQuery += "   AND SRA.D_E_L_E_T_    = ? " // 42
    cQuery += " LEFT JOIN " + RetSqlName("CTT") + " CTT "
    cQuery += "    ON " + cFilCTTSRA + " "
    cQuery += "   AND CTT.CTT_CUSTO     = SRA.RA_CC "
    cQuery += "   AND CTT.D_E_L_E_T_    = ? " // 43
    cQuery += " INNER JOIN " + RetSqlName("SRV") + " SRV "
    cQuery += "    ON " + cFilSRVSRD + " "
    cQuery += "   AND SRV.RV_COD        = SRD.RD_PD "
    cQuery += "   AND SRV.D_E_L_E_T_    = ? " // 44
    cQuery += " WHERE SRD.RD_FILIAL     BETWEEN ? AND ? " // 45 e 46
    cQuery += "   AND SRD.RD_MAT        BETWEEN ? AND ? " // 47 e 48
    cQuery += "   AND SRD.RD_DATARQ     BETWEEN ? AND ? " // 49 e 50
    cQuery += cWhere // 51, 52, 53 e 54
    cQuery += "   AND SRD.D_E_L_E_T_    = ? " // 55
    cQuery += " GROUP BY SRD.RD_FILIAL, ? ? SRD.RD_PD, SRV.RV_DESC " // 56 e 57
    cQuery += " ORDER BY SRD.RD_FILIAL, ? ? SRD.RD_PD DESC " // 58 e 59
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Captura e define os campos personalizados das tabelas 
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRD", "SRV"})

    oStatement:SetUnsafe(nParamOrder++, cCampoAgrupa) // 1
    oStatement:SetUnsafe(nParamOrder++, cDescAgrupa) // 2

    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate1), 1, 6)) // 3
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate1), 1, 6)) // 4
    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate1), 1, 6)) // 5
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate1), 1, 6)) // 6
    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate1), 1, 6)) // 7
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate1), 1, 6)) // 8

    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate2), 1, 6)) // 9
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate2), 1, 6)) // 10
    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate2), 1, 6)) // 11
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate2), 1, 6)) // 12
    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate2), 1, 6)) // 13
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate2), 1, 6)) // 14

    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate3), 1, 6)) // 15
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate3), 1, 6)) // 16
    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate3), 1, 6)) // 17
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate3), 1, 6)) // 18
    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate3), 1, 6)) // 19
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate3), 1, 6)) // 20

    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate4), 1, 6)) // 21
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate4), 1, 6)) // 22
    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate4), 1, 6)) // 23
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate4), 1, 6)) // 24
    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate4), 1, 6)) // 25
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate4), 1, 6)) // 26

    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate5), 1, 6)) // 27
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate5), 1, 6)) // 28
    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate5), 1, 6)) // 29
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate5), 1, 6)) // 30
    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate5), 1, 6)) // 31
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate5), 1, 6)) // 32

    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate6), 1, 6)) // 33
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate6), 1, 6)) // 34
    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate6), 1, 6)) // 35
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate6), 1, 6)) // 36
    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate6), 1, 6)) // 37
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate6), 1, 6)) // 38

    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 39

    oStatement:SetString(nParamOrder++, cCCDe) // 40
    oStatement:SetString(nParamOrder++, cCCAte) // 41

    oStatement:SetString(nParamOrder++, " ") // 42
    oStatement:SetString(nParamOrder++, " ") // 43
    oStatement:SetString(nParamOrder++, " ") // 44

    oStatement:SetString(nParamOrder++, cFilialDe) // 45
    oStatement:SetString(nParamOrder++, cFilialAte) // 46

    oStatement:SetString(nParamOrder++, cMatDe) // 47
    oStatement:SetString(nParamOrder++, cMatAte) // 48

    oStatement:SetString(nParamOrder++, Substr(DTOS(dStartDate6), 1, 6)) // 49
    oStatement:SetString(nParamOrder++, Substr(DTOS(dEndDate1), 1, 6)) // 50
    
    //Itens do Where
    If Len(aSituacoes) > 0
        oStatement:SetIN(nParamOrder++, aSituacoes) // 51
    EndIf

    If Len(aCategorias) > 0
        oStatement:SetIN(nParamOrder++, aCategorias) // 52
    EndIf

    If Len(aVerbas) > 0
        oStatement:SetIN(nParamOrder++, aVerbas) // 53
    EndIf

    If !Empty(cProcesso)
        oStatement:SetString(nParamOrder++, cProcesso) // 54
    Endif

    oStatement:SetString(nParamOrder++, " ") // 55

    oStatement:SetUnsafe(nParamOrder++, Iif(cCampoAgrupa!="''",cCampoAgrupa+", ","")) // 56
    oStatement:SetUnsafe(nParamOrder++, Iif(cDescAgrupa!="''",cDescAgrupa+", ","")) // 57

    oStatement:SetUnsafe(nParamOrder++, Iif(cCampoAgrupa!="''",cCampoAgrupa+", ","")) // 58
    oStatement:SetUnsafe(nParamOrder++, Iif(cDescAgrupa!="''",cDescAgrupa+", ","")) // 59

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))

        // Cria o JSON com as informações da linha
        jData := JSONObject():New()
        jData["FilialExec"]       := Trim(cFilExec)
        jData["EmpresaExec"]      := Trim(cEmpExec)
        jData["DTAGRP1DE"]        := Substr(DtoC(dStartDate1),4,7)
        jData["DTAGRP1ATE"]       := Substr(DtoC(dEndDate1),4,7)
        jData["DTAGRP2DE"]        := Substr(DtoC(dStartDate2),4,7)
        jData["DTAGRP2ATE"]       := Substr(DtoC(dEndDate2),4,7)
        jData["DTAGRP3DE"]        := Substr(DtoC(dStartDate3),4,7)
        jData["DTAGRP3ATE"]       := Substr(DtoC(dEndDate3),4,7)
        jData["DTAGRP4DE"]        := Substr(DtoC(dStartDate4),4,7)
        jData["DTAGRP4ATE"]       := Substr(DtoC(dEndDate4),4,7)
        jData["DTAGRP5DE"]        := Substr(DtoC(dStartDate5),4,7)
        jData["DTAGRP5ATE"]       := Substr(DtoC(dEndDate5),4,7)
        jData["DTAGRP6DE"]        := Substr(DtoC(dStartDate6),4,7)
        jData["DTAGRP6ATE"]       := Substr(DtoC(dEndDate6),4,7)
        jData["RD_FILIAL"]        := Trim(Iif(jLGPD["RD_FILIAL"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RD_FILIAL), (cAlias)->RD_FILIAL)) 
        jData["RD_PD"]            := Trim(Iif(jLGPD["RD_PD"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RD_PD),     (cAlias)->RD_PD)) 
        jData["RV_DESC"]          := Trim(Iif(jLGPD["RV_DESC"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RV_DESC),   (cAlias)->RV_DESC)) 
        jData["CAMPOAGRUPA"]      := Trim((cAlias)->CAMPOAGRUPA) 
        jData["DESCAGRUPA"]       := Trim((cAlias)->DESCAGRUPA) 
        jData["HORAS1"]           := (cAlias)->HORAS1
        jData["VALINFO1"]         := (cAlias)->VALINFO1
        jData["VALOR1"]           := (cAlias)->VALOR1
        jData["HORAS2"]           := (cAlias)->HORAS2
        jData["VALINFO2"]         := (cAlias)->VALINFO2
        jData["VALOR2"]           := (cAlias)->VALOR2
        jData["HORAS3"]           := (cAlias)->HORAS3
        jData["VALINFO3"]         := (cAlias)->VALINFO3
        jData["VALOR3"]           := (cAlias)->VALOR3
        jData["HORAS4"]           := (cAlias)->HORAS4
        jData["VALINFO4"]         := (cAlias)->VALINFO4
        jData["VALOR4"]           := (cAlias)->VALOR4
        jData["HORAS5"]           := (cAlias)->HORAS5
        jData["VALINFO5"]         := (cAlias)->VALINFO5
        jData["VALOR5"]           := (cAlias)->VALOR5
        jData["HORAS6"]           := (cAlias)->HORAS6
        jData["VALINFO6"]         := (cAlias)->VALINFO6
        jData["VALOR6"]           := (cAlias)->VALOR6

        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jParams)
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)
    FwFreeArray(aVerbas)
    FwFreeArray(aPDFields)
    FwFreeArray(aAllFields)
Return self:oData

/*/{Protheus.doc} GPER081TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author caio.kretzer
    @since 14/01/2025
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER081TReportsBusinessObject

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec"   , STR0004, "string",    STR0004, "FilialExec") // Filial de Execução
    self:oSchema:addProperty("EmpresaExec"  , STR0005, "string",    STR0005, "EmpresaExec") // Empresa de Execução
    self:oSchema:addProperty("DTAGRP1DE"    , STR0006, "string",    STR0006, "DTAGRP1DE") // Dt Agr. 1 De
    self:oSchema:addProperty("DTAGRP1ATE"   , STR0007, "string",    STR0007, "DTAGRP1ATE") // Dt Agr. 1 Até
    self:oSchema:addProperty("DTAGRP2DE"    , STR0008, "string",    STR0008, "DTAGRP2DE") // Dt Agr. 2 De
    self:oSchema:addProperty("DTAGRP2ATE"   , STR0009, "string",    STR0009, "DTAGRP2ATE") // Dt Agr. 2 Até
    self:oSchema:addProperty("DTAGRP3DE"    , STR0010, "string",    STR0010, "DTAGRP3DE") // Dt Agr. 3 De
    self:oSchema:addProperty("DTAGRP3ATE"   , STR0011, "string",    STR0011, "DTAGRP3ATE") // Dt Agr. 3 Até
    self:oSchema:addProperty("DTAGRP4DE"    , STR0012, "string",    STR0012, "DTAGRP4DE") // Dt Agr. 4 De
    self:oSchema:addProperty("DTAGRP4ATE"   , STR0013, "string",    STR0013, "DTAGRP4ATE") // Dt Agr. 4 Até
    self:oSchema:addProperty("DTAGRP5DE"    , STR0014, "string",    STR0014, "DTAGRP5DE") // Dt Agr. 5 De
    self:oSchema:addProperty("DTAGRP5ATE"   , STR0015, "string",    STR0015, "DTAGRP5ATE") // Dt Agr. 5 Até
    self:oSchema:addProperty("DTAGRP6DE"    , STR0016, "string",    STR0016, "DTAGRP6DE") // Dt Agr. 6 De
    self:oSchema:addProperty("DTAGRP6ATE"   , STR0017, "string",    STR0017, "DTAGRP6ATE") // Dt Agr. 6 Até
    self:oSchema:addProperty("RD_FILIAL"    , STR0018, "string",    STR0018, "RD_FILIAL") // Filial
    self:oSchema:addProperty("RD_PD"        , STR0019, "string",    STR0019, "RD_PD") // Verba
    self:oSchema:addProperty("RV_DESC"      , STR0020, "string",    STR0020, "RV_DESC") //Descrição da Verba
    self:oSchema:addProperty("CAMPOAGRUPA"  , STR0021, "string",    STR0021, "CAMPOAGRUPA") // Agrupamento
    self:oSchema:addProperty("DESCAGRUPA"   , STR0022, "string",    STR0022, "DESCAGRUPA") // Descrição do Agrupamento
    self:oSchema:addProperty("HORAS1"       , STR0023, "number",    STR0023, "HORAS1") // Horas 1
    self:oSchema:addProperty("VALINFO1"     , STR0024, "number",    STR0024, "VALINFO1") // Val Info 1
    self:oSchema:addProperty("VALOR1"       , STR0025, "number",    STR0025, "VALOR1") // Valor 1
    self:oSchema:addProperty("HORAS2"       , STR0026, "number",    STR0026, "HORAS2") // Horas 2
    self:oSchema:addProperty("VALINFO2"     , STR0027, "number",    STR0027, "VALINFO2") // Val Info 2
    self:oSchema:addProperty("VALOR2"       , STR0028, "number",    STR0028, "VALOR2") // Valor 2
    self:oSchema:addProperty("HORAS3"       , STR0029, "number",    STR0029, "HORAS3") // Horas 3
    self:oSchema:addProperty("VALINFO3"     , STR0030, "number",    STR0030, "VALINFO3") // Val Info 3
    self:oSchema:addProperty("VALOR3"       , STR0031, "number",    STR0031, "VALOR3") // Valor 3
    self:oSchema:addProperty("HORAS4"       , STR0032, "number",    STR0032, "HORAS4") // Horas 4
    self:oSchema:addProperty("VALINFO4"     , STR0033, "number",    STR0033, "VALINFO4") // Val Info 4
    self:oSchema:addProperty("VALOR4"       , STR0034, "number",    STR0034, "VALOR4") // Valor 4
    self:oSchema:addProperty("HORAS5"       , STR0035, "number",    STR0035, "HORAS5") // Horas 5
    self:oSchema:addProperty("VALINFO5"     , STR0036, "number",    STR0036, "VALINFO5") // Val Info 5
    self:oSchema:addProperty("VALOR5"       , STR0037, "number",    STR0037, "VALOR5") // Valor 5
    self:oSchema:addProperty("HORAS6"       , STR0038, "number",    STR0038, "HORAS6") // Horas 6
    self:oSchema:addProperty("VALINFO6"     , STR0039, "number",    STR0039, "VALINFO6") // Val Info 6
    self:oSchema:addProperty("VALOR6"       , STR0040, "number",    STR0040, "VALOR6") // Valor 6

    // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_PROCESSO",       STR0041, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0066) // Processo e Código do Processo
    self:oSchema:addParameter("PAR_FILIALDE",       STR0042, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0042) // Filial de e Filial de
    self:oSchema:addParameter("PAR_FILIALATE",      STR0043, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0043) // Filial até e Filial até
    self:oSchema:addParameter("PAR_MATDE",          STR0044, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0044) // Matrícula de e Matrícula de
    self:oSchema:addParameter("PAR_MATATE",         STR0045, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0045) // Matrícula até e Matrícula até
    self:oSchema:addParameter("PAR_CCDE",           STR0046, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0067) // Centro de Custo de e Código do Centro de Custo de
    self:oSchema:addParameter("PAR_CCATE",          STR0047, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0068) // Centro de Custo até e Código do Centro de Custo até
    self:oSchema:addParameter("PAR_SITUACOES",      STR0048, "string", .T., NIL, NIL, NIL, NIL, NIL, STR0069) // Situações e Código da Situação
    self:oSchema:addParameter("PAR_CATEGORIAS",     STR0049, "string", .T., NIL, NIL, NIL, NIL, NIL, STR0070) // Categorias e Código da Categoria
    self:oSchema:addParameter("PAR_VERBAS",         STR0050, "string", .T., NIL, NIL, NIL, NIL, NIL, STR0071) // Verbas e Código da Verba
    self:oSchema:addParameter("peridiocidade",      STR0051, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0072) // Peridiocidade e Peridiocidade de exibição do relatório
    self:oSchema:addParameter("tipoDataPer",        STR0052, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0073) // Tipo de Data de Período e No "padrão", o período segue sempre o padrão de período. POr exemplo, em bimestre: Janeiro e Fevereiro, Março e Abril, e assim por diante. No "competência atual é fim", o período segue a competência atual até o fim do período. Por exemplo, se estivermos em Janeiro em bimestre: Dezembro e Janeiro, Outubro e Novembro, e assim por diante.
    self:oSchema:addParameter("agrupador",          STR0053, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0074) // Agrupador e Agrupador do Relatório
    self:oSchema:addParameter("impGrafico",         STR0063, "string", .F., NIL, NIL, NIL, NIL, NIL, STR0063) // Imprimir Gráfico e Imprimir Gráfico

    // Define os endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_PROCESSO",       "api/framework/v1/genericLookupService/smartview/RCJ",                                       2)
    self:setCustomURL("PAR_MATDE",          "api/framework/v1/genericLookupService/smartview/SRA",                                       2)
    self:setCustomURL("PAR_MATATE",         "api/framework/v1/genericLookupService/smartview/SRA",                                       2)
    self:setCustomURL("PAR_CCDE",           "api/framework/v1/genericLookupService/smartview/CTT",                                       2)
    self:setCustomURL("PAR_CCATE",          "api/framework/v1/genericLookupService/smartview/CTT",                                       2)
    self:setCustomURL("PAR_VERBAS",         "api/framework/v1/genericLookupService/smartview/SRV",                                       2)
    self:setCustomURL("PAR_FILIALDE",       "/api/rh/smartview/v1/options/GPEParams/getBranches",                                        2)
    self:setCustomURL("PAR_FILIALATE",      "/api/rh/smartview/v1/options/GPEParams/getBranches",                                        2)
    self:setCustomURL("PAR_CATEGORIAS",     "/api/rh/smartview/v1/options/GPEParams/getSX5/28",                                          2)
    self:setCustomURL("PAR_SITUACOES",      "/api/rh/smartview/v1/options/GPEParams/getSX5/31",                                          2)
    self:setCustomURL("peridiocidade",      "/api/rh/smartview/v1/options/GPER081/peridiocidade",                                        1)
    self:setCustomURL("tipoDataPer",        "/api/rh/smartview/v1/options/GPER081/tipoDataPer",                                          1)
    self:setCustomURL("agrupador",          "/api/rh/smartview/v1/options/GPER081/agrupador",                                            1)
    self:setCustomURL("impGrafico",         "/api/rh/smartview/v1/options/GPER081/impGrafico",                                           1)

Return self:oSchema


/*/{Protheus.doc} GPER081TReportsBusinessObject::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2410
    @author caio.kretzer
    @since 22/01/2025
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() As Variant Class GPER081TReportsBusinessObject
    // Declaração das variáveis locais
    Local jPath     As JSON    // Path parameters da requisição
    Local jResponse As JSON    // Resposta da requisição
    Local jItem     As JSON    // Item do valor do parâmetro
    Local nValues   As Numeric // Contador dos valores do parâmetro
    Local aValues   As Array   // Valores do parâmetro

    // Inicialização das variáveis
    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Verifica qual parâmetro foi solicitado
    If cParam == "peridiocidade"
        // Monta o vetor de valores do parâmetro		
        AAdd(aValues, FwHTTPEncode(STR0054)) // "Mensal"
        AAdd(aValues, FwHTTPEncode(STR0055)) // "Bimestral"
        AAdd(aValues, FwHTTPEncode(STR0056)) // "Trimestral"
        AAdd(aValues, FwHTTPEncode(STR0057)) // "Semestral"
    ElseIf cParam == "tipoDataPer"
        // Monta o vetor de valores do parâmetro
        AAdd(aValues, FwHTTPEncode(STR0058)) // "Padrão"
        AAdd(aValues, FwHTTPEncode(STR0059)) // "Competência atual é Fim"
    ElseIf cParam == "agrupador"
        // Monta o vetor de valores do parâmetro
        AAdd(aValues, FwHTTPEncode(STR0060)) // "Filial"
        AAdd(aValues, FwHTTPEncode(STR0061)) // "Centro de Custo"
        AAdd(aValues, FwHTTPEncode(STR0062)) // "Funcionário"
    ElseIf cParam == "impGrafico"
        // Monta o vetor de valores do parâmetro
        AAdd(aValues, FwHTTPEncode(STR0064)) // "Sim"
        AAdd(aValues, FwHTTPEncode(STR0065)) // "Não"
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL
