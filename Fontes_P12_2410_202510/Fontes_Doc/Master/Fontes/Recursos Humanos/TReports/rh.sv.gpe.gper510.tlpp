#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.sv.gpe.gper510.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA", "SRD", name="Relatório Acumulados", country="ALL", initialRelease="12.1.2210")
class GPER510TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getWherePar() as character
    private method getParSX1() as array
    private method formatFuncJson() as array
    private method formatTotJson() as array
    
    protected data ojItems as json
    protected data aPdTot as array
    protected data aPdTotCC as array
    protected data aPdTotEmp as array

    @Get("/api/rh/smartview/v1/options/GPER510SV/:param")
    Public Method getComboBox() as variant
endclass
 
method new() as object class GPER510TReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Relatório Acumulados"
    self:aPdTot    := {}
    self:aPdTotCC  := {}
    self:aPdTotEmp := {}
return self
 
method getDescription() as character class GPER510TReportsBusinessObject
return STR0003 //"Objeto contendo informacoes do acumulado"
 
method getAreas() as array class GPER510TReportsBusinessObject
return {STR0002} //"RH"
 
method getData(nPage as numeric, oFilter as object) as object class GPER510TReportsBusinessObject
    local aInfo as array
    local aOfusca as array
    local aFldRel as array
    local cAlias as character
    local cQuery as character
    local cWhere as character
    local cLastFil as character
    local cLastCC as character
    local cLastCCMov as character
    local cDescCCRD as character
    local cDescCCRA as character
    local cAcessaSRD as character
    local cAcessaSRA as character
    local cTipAfas as character
    local cValidFil as character
    local cChave as character
    local dDtAfas as date 
	local dDtPesqAf as date
    local lOfusca as logical
    local nSituacoes as numeric
    local nI as numeric
    local nPOrder as numeric
    local nPd as numeric
    local jParams as json
    local aCategoria as array    
    local aSituacoes  as array 
    local aVerbas as array
    local aPdHoriz as array
    local lTotFil as logical
    local lTotEmp as logical
    local lTotCC as logical
    local lHoriz as logical
    local lMovCC as logical
    local lVerCC as logical
 
    Private cSituacao as character

    Static __cEmpAnt := cEmpAnt
    Static __cWhere  := ""
    Static oStatement

    jParams         := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    cOrdem          := "SRA.RA_FILIAL, SRA.RA_MAT"
    cAcessaSRD      := &("{ || " + ChkRH("GPER510","SRD","2") + "}")
    cAcessaSRA      := &("{ || " + ChkRH("GPER510","SRA","2") + "}")
    cValidFil       := fValidFil()
    aOfusca	        := If(FindFunction('ChkOfusca'), ChkOfusca(), { .T., .F. }) //[2]Ofuscamento
    aFldRel	        := If(aOfusca[2], FwProtectedDataUtil():UsrNoAccessFieldsInList( {"RA_NOME"} ), {})
    lOfusca	        := Len(aFldRel) > 0
    aCategoria      := {}
    aSituacoes      := {}
    aVerbas         := {}
    aPdHoriz        := {}
    lVerCC          := .F.
    nPOrder         := 4
    
    lHoriz := Iif(cValToChar(jParams['MV_PAR13', 1]) == "1", .F., .T.) //Converte para caractere, pelo protheus retorna númerico

    aVerbas := IIf(jParams:hasProperty("MV_PAR20"), jParams["MV_PAR20"], {})
    // Limita 8 verbas no relatório horizontal
    If lHoriz 
        For nPd := 1 To Len(aVerbas)
            If aVerbas[nPd] != NIL .And. !Empty(aVerbas[nPd])
                aAdd(aPdHoriz, aVerbas[nPd])
            EndIf
            If Len(aPdHoriz) == 8
                Exit
            EndIf    
        Next nPd
    EndIf

    //Retorna vazio pois ainda não foi criado o vertical ou caso seja horizontal mas sem verbas
    If !lHoriz .Or. ( lHoriz .And. Empty(aPdHoriz) )
        self:ojItems := JsonObject():new()
        self:ojItems["LMSN"] := .T.
        self:oData:appendData(self:ojItems)
        return self:oData
    EndIf

    aCategoria := IIf(jParams:hasProperty("MV_PAR19"), jParams["MV_PAR19"], {})
    aSituacoes := IIf(jParams:hasProperty("MV_PAR18"), jParams["MV_PAR18"], {})

    For nI := 1 To Len(aCategoria)
        aCategoria[nI] := IIf(aCategoria[nI] == NIL .Or. Empty(aCategoria[nI]), "M", aCategoria[nI])
    Next nI

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nSituacoes := 1 To Len(aSituacoes)
        aSituacoes[nSituacoes] := IIf(aSituacoes[nSituacoes] == NIL .Or. Empty(aSituacoes[nSituacoes]), " ", aSituacoes[nSituacoes])
    Next nSituacoes

    If !Empty(jParams['MV_ORDER', 1])
		cOrdem := Iif(cValToChar(jParams['MV_ORDER', 1]) == "1", "SRA.RA_FILIAL, SRA.RA_MAT", Iif(cValToChar(jParams['MV_ORDER', 1]) == "2", "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_MAT", ;
                  Iif(cValToChar(jParams['MV_ORDER', 1]) == "3", "SRA.RA_FILIAL, SRA.RA_NOME, SRA.RA_MAT", "SRD.RD_FILIAL, SRD.RD_CC, SRD.RD_MAT" )))
        If cValToChar(jParams['MV_ORDER', 1]) $ "2/4"
            lVerCC := .T.
        EndIf      
	EndIf

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(__cWhere == cWhere)
        cQuery := " SELECT  SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_CC, SRA.RA_HRSMES, SRA.RA_SALARIO, SRA.RA_CATFUNC, SRA.RA_SITFOLH, "
        cQuery += "         SRD.RD_PD, SRD.RD_TIPO1, SRD.RD_TIPO2, SRD.RD_HORAS, SRD.RD_VALOR, SRD.RD_DATARQ, SRD.RD_DATPGT, SRD.RD_CC, SRV.RV_DESC, SRV.RV_TIPOCOD, SRV.RV_CODFOL "
        cQuery += " FROM    " + RetSqlName("SRA") + " SRA "
        cQuery += "         INNER JOIN " + RetSqlName("SRD") + " SRD ON " + FWJoinFilial( "SRD", "SRA" ) + " AND SRD.RD_MAT = SRA.RA_MAT AND SRD.D_E_L_E_T_ = ? "
        cQuery += "         INNER JOIN " + RetSqlName("SRV") + " SRV ON " + FWJoinFilial( "SRV", "SRD" ) + " AND SRV.RV_COD = SRD.RD_PD AND SRV.D_E_L_E_T_ = ? "
        cQuery += " WHERE   SRA.D_E_L_E_T_ = ? "
        
        cWhere := self:getWherePar(jParams) 

        If !Empty(cWhere)
            cQuery += cWhere    
        EndIf

        cQuery += " ORDER BY " + cOrdem

        __cEmpAnt  := cEmpAnt
        __cWhere   := cWhere
        cQuery     := ChangeQuery(cQuery) 
        oStatement := FwExecStatement():New(cQuery)

    EndIf

    oStatement:SetString(1, ' ') //SRD.D_E_L_E_T_
    oStatement:SetString(2, ' ') //SRV.D_E_L_E_T_
    oStatement:SetString(3, ' ') //SRA.D_E_L_E_T_

    If !Empty(jParams['MV_PAR01', 1])
		oStatement:SetString(nPOrder++, jParams["MV_PAR01", 1])
	EndIf
	If !Empty(jParams['MV_PAR02', 1])
		oStatement:SetString(nPOrder++, jParams["MV_PAR02", 1])
	EndIf
    If !Empty(jParams['MV_PAR03', 1])
		oStatement:SetString(nPOrder++, jParams["MV_PAR03", 1])
	EndIf
	If !Empty(jParams['MV_PAR04', 1])
		oStatement:SetString(nPOrder++, jParams["MV_PAR04", 1])
	EndIf
    If !Empty(jParams['MV_PAR05', 1])
		oStatement:SetString(nPOrder++, jParams["MV_PAR05", 1])
	EndIf
	If !Empty(jParams['MV_PAR06', 1])
		oStatement:SetString(nPOrder++, jParams["MV_PAR06", 1])
	EndIf
    If !Empty(jParams['MV_PAR07', 1])
		oStatement:SetString(nPOrder++, jParams["MV_PAR07", 1])
	EndIf
	If !Empty(jParams['MV_PAR08', 1])
		oStatement:SetString(nPOrder++, jParams["MV_PAR08", 1])
	EndIf
    If !Empty(jParams['MV_PAR09', 1])
		oStatement:SetString(nPOrder++, SubStr(jParams['MV_PAR09', 1],3,4) + SubStr(jParams['MV_PAR09', 1],1,2))
	EndIf
	If !Empty(jParams['MV_PAR10', 1])
		oStatement:SetString(nPOrder++, SubStr(jParams['MV_PAR10', 1],3,4) + SubStr(jParams['MV_PAR10', 1],1,2))
	EndIf
    If !Empty(jParams['MV_PAR11', 1])
		oStatement:SetString(nPOrder++, jParams["MV_PAR11", 1])
	EndIf
	If !Empty(jParams['MV_PAR12', 1])
		oStatement:SetString(nPOrder++, jParams["MV_PAR12", 1])
	EndIf
    If !Empty(jParams['MV_PAR19'])
        oStatement:SetIn(nPOrder++, aCategoria)
    EndIf 
    If !Empty(jParams['MV_PAR18'])
        oStatement:SetIn(nPOrder++, aSituacoes)
    EndIf 
    If Len(aPdHoriz) > 0 
        oStatement:SetIn(nPOrder++, aPdHoriz)
    EndIf 

    cAlias := oStatement:OpenAlias()
    dbSelectArea( cAlias )

    While !(cAlias)->(Eof())

        If cLastFil != (cAlias)->RA_FILIAL
            If !((cAlias)->RA_FILIAL $ cValidFil) .Or. !Eval(cAcessaSRA) .Or. !Eval(cAcessaSRD)
                (cAlias)->( dbSkip() )
                Loop
            EndIf
            cLastFil := (cAlias)->RA_FILIAL
            cLastCC  := cLastCCMov := ""
            fInfo(@aInfo, (cAlias)->RA_FILIAL)
        EndIf
        
        //C.Custo do cadastro ou movimento
        If !cValToChar(jParams['MV_ORDER', 1]) == "4" .And. cLastCC != ( cAlias )->RA_CC
            cDescCCRA := RTrim(fDesc("CTT", ( cAlias )->RA_CC, "CTT_DESC01",, (cAlias)->RA_FILIAL))
            cLastCC   := ( cAlias )->RA_CC
            lMovCC    := .F.
        ElseIf cLastCCMov != ( cAlias )->RD_CC
            cDescCCRD  := RTrim(fDesc("CTT", ( cAlias )->RD_CC, "CTT_DESC01",, (cAlias)->RA_FILIAL))
            cLastCCMov := ( cAlias )->RD_CC
            lMovCC    := .T.
        EndIf

        //Situação histórica 
        If cValToChar(jParams['MV_PAR24', 1]) == "1" .And. !Empty(jParams['MV_PAR18']) .And. !cChave == (cAlias)->RA_FILIAL+(cAlias)->RA_MAT+(cAlias)->RD_DATARQ
            SRA->(dbSeek( (cAlias)->RA_FILIAL + (cAlias)->RA_MAT ) )
            cChave    := (cAlias)->RA_FILIAL+(cAlias)->RA_MAT+(cAlias)->RD_DATARQ
            cTipAfas  := ""
		    dDtAfas   := CtoD("")
			dDtPesqAf := CtoD("01/" + Right((cAlias)->RD_DATARQ,2) + "/" + Left((cAlias)->RD_DATARQ,4),"DDMMYY")
			fChkAfas((cAlias)->RA_FILIAL, (cAlias)->RA_MAT, dDtPesqAf, @dDtAfas,, @cTipAfas)

            If (!Empty(SRA->RA_DEMISSA) .And. MesAno(SRA->RA_DEMISSA) <= MesAno(dDtPesqAf))					
			    cTipAfas := "D"
			EndIf			
			
            If !(cTipAfas $ cSituacao)
                While !(cAlias)->(Eof()) .And. cChave == (cAlias)->RA_FILIAL+(cAlias)->RA_MAT+(cAlias)->RD_DATARQ
			        (cAlias)->( dbSkip() )
                EndDo
				Loop
			EndIf
		EndIf

        lTotFil := .F.
        lTotEmp := .F.
        lTotCC  := .F.
            
        self:ojItems := JsonObject():new()
        self:ojItems["BranchName"] := RTrim( aInfo[01] ) 
        self:ojItems["CompanyName"] := RTrim( aInfo[02] )
        self:ojItems["CompanyCode"] := __cEmpAnt
        self:ojItems["RA_FILIAL"] := (cAlias)->RA_FILIAL
        self:ojItems["RA_MAT"] := (cAlias)->RA_MAT
        self:ojItems["RA_NOME"] := If(lOfusca, Replicate('*',30), RTrim( (cAlias)->RA_NOME ))
        self:ojItems["CostCenterCode"] := AllTrim(If(!cValToChar(jParams['MV_ORDER', 1]) == "4", (cAlias)->RA_CC, (cAlias)->RD_CC))
        self:ojItems["CostCenterDescription"] := If(!cValToChar(jParams['MV_ORDER', 1]) == "4", cDescCCRA, cDescCCRD )
        self:ojItems["RA_HRSMES"] := If(cValToChar(jParams['MV_PAR17', 1]) == "1", (cAlias)->RA_HRSMES , 0 )
        self:ojItems["RA_SALARIO"] := If(cValToChar(jParams['MV_PAR17', 1]) == "1", (cAlias)->RA_SALARIO , 0 )
        self:ojItems["RD_PD"] := (cAlias)->RD_PD
        self:ojItems["RD_HORAS"] := (cAlias)->RD_HORAS
        self:ojItems["RD_TIPO1"] := (cAlias)->RD_TIPO1
        self:ojItems["RD_TIPO2"] := (cAlias)->RD_TIPO2
        self:ojItems["RD_VALOR"] := (cAlias)->RD_VALOR
        self:ojItems["RD_DATARQ"] := (cAlias)->RD_DATARQ
        self:ojItems["RD_DATPGT"] := FwTimeStamp( 5, sToD((cAlias)->RD_DATPGT, "00:00:00" ))
        self:ojItems["DESCPD"] := RTrim((cAlias)->RV_DESC)
        self:ojItems["TPPD"] := (cAlias)->RV_TIPOCOD 
        self:ojItems["LMSN"] := .F.

        If lHoriz
            self:ojItems["ADetFunc"] := self:formatFuncJson(cAlias, @lTotFil, @lTotEmp, @lTotCC, lMovCC, lVerCC)
        EndIf

        If lTotCC
            self:ojItems["ADetCC"] := self:formatTotJson("2")
        EndIf

        If lTotFil 
            self:ojItems["ADetFil"] := self:formatTotJson("1")
        EndIf

        If lTotEmp
            self:ojItems["ADetEmp"] := self:formatTotJson("3")
        EndIf    

        self:oData:appendData(self:ojItems)

        (cAlias)->( dbSkip() )
    EndDo
 
(cAlias)->( DBCloseArea() )

return self:oData
 
method getSchema() as object class GPER510TReportsBusinessObject

    local nI as numeric
    Local aParSX1 as array
    local aFieldsSRD as array
    local aFieldsSRA as array
    local aFields as array
    local aTotFil as array
    local aTotCC as array
    local aTotEmp as array

    aFieldsSRD := { "RD_PD", "RD_HORAS", "RD_VALOR", "RD_DATARQ", "RD_DATPGT", "RD_TIPO1", "RD_TIPO2" }
    aFieldsSRA := { "RA_FILIAL", "RA_MAT", "RA_NOME", "RA_HRSMES", "RA_SALARIO" }
    
    self:oSchema:aliasToSchema("SRD", aFieldsSRD)   
    self:oSchema:aliasToSchema("SRA", aFieldsSRA) 
    self:oSchema:addProperty("CompanyName", "Nome Empresa", "string", "Nome Emp.", "CompanyName",,,, .F.) 
    self:oSchema:addProperty("BranchName", "Nome Filial", "string", "Nome Fil.", "BranchName",,,, .F.)  
    self:oSchema:addProperty("CompanyCode", "Código Empresa", "string", "Cod. Emp", "CompanyCode",,,, .F.) 
    self:oSchema:addProperty("CostCenterCode", "Código Centro de Custo", "string", "Código CC.", "CostCenterCode",,,, .F.) 
    self:oSchema:addProperty("CostCenterDescription", "Descrição Centro de Custo", "string", "Desc. CC.", "CostCenterDescription",,,, .F.) 
    self:oSchema:addProperty("DESCPD", "Descrição da verba", "string", "Desc. Verba", "DESCPD",,,, .F.) 
    self:oSchema:addProperty("TPPD", "Tipo da verba", "string", "Tp. Verba", "TPPD",,,, .F.) 
    self:oSchema:addProperty("LMSN", "Aviso", "boolean", "Aviso", "LMSN",,,, .F.) 

    //Perguntas do SX1
    aParSX1 := self:getParSX1("GPR510")    

    For nI := 1 To Len(aParSX1)
        self:oSchema:addParameter( aParSX1[nI][1], aParSX1[nI][2], aParSX1[nI][3], aParSX1[nI][4] )                                         
    Next nI
    
    self:addParameter("MV_ORDER", "Ordem ?", "string", .F.) 

    aSize(aParSX1, 0)
   
    aFields := {}
    aTotFil := {}
    aTotCC  := {}
    aTotEmp := {}

    aAdd(aFields, {"Periodo", "Periodo", "string", "Periodo"})
	aAdd(aFields, {"CodPD1", "Cod PD", "string", "Cod PD"})
	aAdd(aFields, {"DescPD1", "Desc PD", "string", "Desc PD"})
	aAdd(aFields, {"ValorPD1", "Valor PD", "number", "Valor PD"})
	aAdd(aFields, {"Filial", "FIlial", "string", "Filial"})
    aAdd(aFields, {"Mat", "Matricula", "string", "Matricula"})
    aAdd(aFields, {"Horas", "Horas", "number", "Horas"})
    aAdd(aFields, {"TipoPD", "Tipo da verba", "string", "Tipo da verba"})
    aAdd(aFields, {"Count", "Contador", "number", "Contador"})

    aAdd(aTotFil, {"Filial", "Filial", "string", "Filial"})
	aAdd(aTotFil, {"CodPd", "Cod Pd", "string", "Cod Pd"})
	aAdd(aTotFil, {"DescPd", "Desc Pd", "string", "Desc Pd"})
	aAdd(aTotFil, {"Periodo", "Periodo", "string", "Periodo"})
    aAdd(aTotFil, {"Total", "Total", "number", "Total"})
    aAdd(aTotFil, {"Horas", "Horas", "number", "Horas"})
    aAdd(aTotFil, {"TipoPD", "Tipo da verba", "string", "Tipo da verba"})

    aAdd(aTotCC, {"Filial", "Filial", "string", "Filial"})
	aAdd(aTotCC, {"CodPd", "Cod Pd", "string", "Cod Pd"})
	aAdd(aTotCC, {"DescPd", "Desc Pd", "string", "Desc Pd"})
	aAdd(aTotCC, {"Periodo", "Periodo", "string", "Periodo"})
    aAdd(aTotCC, {"Total", "Total", "number", "Total"})
    aAdd(aTotCC, {"Horas", "Horas", "number", "Horas"})
    aAdd(aTotCC, {"TipoPD", "Tipo da verba", "string", "Tipo da verba"})
    aAdd(aTotCC, {"CodeCC", "C.Custo", "string", "C.Custo"})

    aAdd(aTotEmp, {"Filial", "Filial", "string", "Filial"})
	aAdd(aTotEmp, {"CodPd", "Cod Pd", "string", "Cod Pd"})
	aAdd(aTotEmp, {"DescPd", "Desc Pd", "string", "Desc Pd"})
	aAdd(aTotEmp, {"Periodo", "Periodo", "string", "Periodo"})
    aAdd(aTotEmp, {"Total", "Total", "number", "Total"})
    aAdd(aTotEmp, {"Horas", "Horas", "number", "Horas"})
    aAdd(aTotEmp, {"TipoPD", "Tipo da verba", "string", "Tipo da verba"})

	self:addNestedProperty("ADetFunc", "ADetFunc", "ADetFunc", , aFields)
    self:addNestedProperty("ADetFil", "ADetFil", "ADetFil", , aTotFil)
    self:addNestedProperty("ADetCC", "ADetCC", "ADetCC", , aTotCC)
    self:addNestedProperty("ADetEmp", "ADetEmp", "ADetEmp", , aTotEmp)

    If FWLibVersion() >= "20231121" 
        //Loockups
        self:setCustomURL("MV_PAR01", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MV_PAR02", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("MV_PAR04", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MV_PAR06", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MV_PAR18", "api/framework/v1/genericLookupService/smartview/31", 2)
        self:setCustomURL("MV_PAR19", "api/framework/v1/genericLookupService/smartview/28", 2)
        self:setCustomURL("MV_PAR20", "api/framework/v1/genericLookupService/smartview/SRV", 2)
        //Combos
        self:setCustomURL("MV_PAR13", "/api/framework/treports/integratedprovider/v1/options/GPR510/MV_PAR13", 1) //Formato                      
        self:setCustomURL("MV_PAR15", "/api/framework/treports/integratedprovider/v1/options/GPR510/MV_PAR15", 1) //Sintetico/Analitico 
        self:setCustomURL("MV_PAR17", "/api/framework/treports/integratedprovider/v1/options/GPR510/MV_PAR17", 1) //Salario do Cadastro
        self:setCustomURL("MV_PAR22", "/api/framework/treports/integratedprovider/v1/options/GPR510/MV_PAR16", 1) //Imprimir Totais 
        self:setCustomURL("MV_PAR23", "/api/framework/treports/integratedprovider/v1/options/GPR510/MV_PAR16", 1) //Impr.Total Liquido
        self:setCustomURL("MV_PAR14", "/api/rh/smartview/v1/options/GPER510SV/MV_PAR14", 1) //Horizontal Listar 
        self:setCustomURL("MV_PAR24", "/api/rh/smartview/v1/options/GPER510SV/MV_PAR24", 1) //Situacao Historica/Atual 
        self:setCustomURL("MV_ORDER", "/api/rh/smartview/v1/options/GPER510SV/MV_ORDER", 1) //Ordem
    EndIf    

return self:oSchema

/*/{Protheus.doc} getComboBox
Metodo que retorna o combobox dos parâmetros
@type  Metodo
@author Bruno Costa
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() as Variant Class GPER510TReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    Local cParam    As Character 
    local aValues   as Array   
     

    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    jItem     := NIL
    nValues   := 0
    cParam    := ""
    aValues   := {}

    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    jResponse["data"] := {}

    // Monta o vetor de valores do parâmetro
    If cParam == "MV_ORDER"
        AAdd(aValues, EncodeUTF8("Matricula"))
        AAdd(aValues, EncodeUTF8("C.Custo do Funcionário"))
        AAdd(aValues, EncodeUTF8("Nome"))
        AAdd(aValues, EncodeUTF8("C.Custo do Movimento"))
    ElseIf cParam == "MV_PAR14"
        AAdd(aValues, EncodeUTF8("Valores"))
        AAdd(aValues, EncodeUTF8("Horas"))
    ElseIf cParam == "MV_PAR24"
        AAdd(aValues, EncodeUTF8("Sit.Historica"))
        AAdd(aValues, EncodeUTF8("Sit.Atual"))
    EndIf 

    If (Len(aValues) > 0)
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)

Return NIL

/*/{Protheus.doc} getParSX1
Metodo que retorna as perguntas do SX1
@type  Metodo
@author Bruno Costa
@return array
@since 08/12/2023
/*/    
method getParSX1( cPerg as character ) as array class GPER510TReportsBusinessObject

    Local oObjSX1 as object    
    Local nI as numeric
    Local cParMult as character
    Local cParNum as character
    Local aPerSX1 as array
    Local aRet as array
    Local aParOut as array
        
  /*mv_par01    Filial  De                            
    mv_par02    Filial  Ate                              
    mv_par03    Centro de Custo De                       
    mv_par04    Centro de Custo Ate                      
    mv_par05    Matricula De                             
    mv_par06    Matricula Ate                            
    mv_par07    Nome De                                  
    mv_par08    Nome Ate                                 
    mv_par09    Data De                                  
    mv_par10    Data Ate                                 
    mv_par11    Numero da Semana De                      
    mv_par12    Numero da Semana Ate                     
    mv_par13    Formato Vertical / Horizontal            
    mv_par14    Listar Valores / Horas                   
    mv_par15    Relatorio Analitico ou Sintetica         
    mv_par16    Se lista todos os codigos encontrados    
    mv_par17    Lista Salario do Cadastro ?              
    mv_par18    Cria String com Situacao do Funcionario  
    mv_par19    Cria String contendo Categorias          
    mv_par20    Codigos a Listar                         
    mv_par21    Cont. Codigos a Listar                   
    mv_par22    Imprimir Totais                          
    mv_par23    Imprimir Liquidos                        
    mv_par24    Situacao Historica / Atual   
    mv_par25    Saltar pagina em C.Custo ? */

    oObjSX1  := FWSX1Util():New() 
    aRet     := {}
    cParMult := "MV_PAR18*MV_PAR19*MV_PAR20" // Multivalorado
    cParNum  := "MV_PAR22*MV_PAR23*MV_PAR24" // Numericos que serão strings
    aParOut  := {"MV_PAR16", "MV_PAR21", "MV_PAR25"} // Não serão usados

    If oObjSX1:ExistPergunte( cPerg )
        oObjSX1:AddGroup(Alltrim(cPerg))
        oObjSX1:SearchGroup()
        aPerSX1 := oObjSX1:GetGroup(Alltrim(cPerg))
    
        For nI := 1 to Len(aPerSX1[2])
            nScan := Ascan( aParOut, Upper(alltrim( aPerSX1[2][nI]:CX1_VAR01 ) )  )
            If nScan == 0
                aadd( aRet , { Upper(alltrim( aPerSX1[2][nI]:CX1_VAR01 )) ,; //ID
                               alltrim( aPerSX1[2][nI]:CX1_PERGUNT ) ,; // Descrição
                               Iif( aPerSX1[2][nI]:CX1_TIPO == "C" .Or. Upper(alltrim( aPerSX1[2][nI]:CX1_VAR01 )) $ cParNum, "string", Iif( aPerSX1[2][nI]:CX1_TIPO == "N", "number" , "date" )) ,; //Tipo
                               Iif( Upper(alltrim( aPerSX1[2][nI]:CX1_VAR01 )) $ cParMult, .T. , .F. ); //Indica se aceita múltiplos valores
                             })
            EndIf            
        Next nI
    EndIF

    oObjSX1:Destroy()
    oObjSX1 := nil
    FreeObj(oObjSX1)

    aSize(aPerSX1, 0)

Return aRet

/*/{Protheus.doc} getWherePar
Metodo que retorna a consulta conforme os parâmetros
@type  Metodo
@author Bruno Costa
@return character
@since 08/12/2023
/*/
method getWherePar( jPar as json) as character class GPER510TReportsBusinessObject
    
    Local cRet as character
    
    If !Empty(jPar['MV_PAR01', 1])
		cRet += " AND SRD.RD_FILIAL >= ?"
	EndIf
    If !Empty(jPar['MV_PAR02', 1])
		cRet += " AND SRD.RD_FILIAL <= ?"
	EndIf 
    If !Empty(jPar['MV_PAR03', 1])
		cRet += " AND SRD.RD_CC >= ?"
	EndIf
	If !Empty(jPar['MV_PAR04', 1])
		cRet += " AND SRD.RD_CC <= ?"
	EndIf
    If !Empty(jPar['MV_PAR05', 1])
		cRet += " AND SRD.RD_MAT >= ?"
	EndIf
    If !Empty(jPar['MV_PAR06', 1])
		cRet += " AND SRD.RD_MAT <= ?"
	EndIf
    If !Empty(jPar['MV_PAR07', 1])
		cRet += " AND SRA.RA_NOME >= ?"
	EndIf
    If !Empty(jPar['MV_PAR08', 1])
		cRet += " AND SRA.RA_NOME <= ?"
	EndIf
    If !Empty(jPar['MV_PAR09', 1])
		cRet += " AND SRD.RD_DATARQ >= ?"
	EndIf
    If !Empty(jPar['MV_PAR10', 1])
		cRet += " AND SRD.RD_DATARQ <= ?"
	EndIf
    If !Empty(jPar['MV_PAR11', 1])
		cRet += " AND SRD.RD_SEMANA >= ?"
	EndIf
    If !Empty(jPar['MV_PAR12', 1])
		cRet += " AND SRD.RD_SEMANA <= ?"
	EndIf
    If !Empty(jPar['MV_PAR19'])
        cRet += " AND SRA.RA_CATFUNC IN (?)"
    EndIf 
    If !Empty(jPar['MV_PAR18'])
        cRet += " AND SRA.RA_SITFOLH IN (?)"
    EndIf 
    If !Empty(jPar['MV_PAR20'])
        cRet += " AND SRD.RD_PD IN (?)"
    EndIf
Return cRet

/*/{Protheus.doc} formatFuncJson
Metodo que retorna o objeto aninhado com o acumulado por funcionário conforme os parâmetros
@type  Metodo
@author Bruno Costa
@return character
@since 05/02/2024
/*/
method formatFuncJson(cAlias as character, lTotFil as logical, lTotEmp as logical, lTotCC as logical, lMovCC as logical, lVerCC as logical) as array class GPER510TReportsBusinessObject

    Local aDetFunc := {}
    Local cMat     := (cAlias)->RA_MAT
    Local cDat     := (cAlias)->RD_DATARQ
    local cFil     := (cAlias)->RA_FILIAL
    local cCC      := If(lMovCC, (cAlias)->RD_CC, (cAlias)->RA_CC)
    Local nIdPd    := 0
    local cLastFil := ""

    While !(cAlias)->(Eof()) 
        If cLastFil != (cAlias)->RA_FILIAL
            cLastFil := (cAlias)->RA_FILIAL
        EndIf
        If cMat == (cAlias)->RA_MAT .And. cFil == (cAlias)->RA_FILIAL 

            Aadd(self:aPdTot, {(cAlias)->RA_FILIAL, (cAlias)->RD_PD, RTrim((cAlias)->RV_DESC), (cAlias)->RD_DATARQ, (cAlias)->RD_VALOR, (cAlias)->RD_HORAS, (cAlias)->RV_TIPOCOD})
            Aadd(self:aPdTotCC, {(cAlias)->RA_FILIAL, (cAlias)->RD_PD, RTrim((cAlias)->RV_DESC), (cAlias)->RD_DATARQ, (cAlias)->RD_VALOR, (cAlias)->RD_HORAS, (cAlias)->RV_TIPOCOD, AllTrim(If(lMovCC, (cAlias)->RD_CC, (cAlias)->RA_CC))})
            Aadd(self:aPdTotEmp, {(cAlias)->RA_FILIAL, (cAlias)->RD_PD, RTrim((cAlias)->RV_DESC), (cAlias)->RD_DATARQ, (cAlias)->RD_VALOR, (cAlias)->RD_HORAS, (cAlias)->RV_TIPOCOD})
            If cDat <> (cAlias)->RD_DATARQ
                nIdPd := 0
                cDat  := (cAlias)->RD_DATARQ
            EndIf
            aAdd(aDetFunc, {;
                "Periodo": (cAlias)->RD_DATARQ,;
                "CodPD1": (cAlias)->RD_PD,;
                "DescPD1": RTrim((cAlias)->RV_DESC),;
                "ValorPD1": (cAlias)->RD_VALOR,;
                "Filial": (cAlias)->RA_FILIAL,;
                "Mat": (cAlias)->RA_MAT,; 
                "Horas": (cAlias)->RD_HORAS,;
                "TipoPD": (cAlias)->RV_TIPOCOD,;
                "Count": nIdPd++ }) 
        Else   
            (cAlias)->( dbSkip(-1) )
            (cAlias)->( dbGoTop() )
            Exit
        EndIf    
        (cAlias)->( dbSkip() )
   
        lTotFil := If( (cAlias)->RA_FILIAL != cFil, .T., .F. )
        lTotEmp := If( (cAlias)->(Eof()), .T., .F.)

        If lVerCC 
            lTotCC := If( If(lMovCC, (cAlias)->RD_CC, (cAlias)->RA_CC) != cCC, .T., IF((cAlias)->RA_FILIAL != cFil, .T., .F.) )
        EndIf

    EndDo   

Return aDetFunc

/*/{Protheus.doc} formatTotJson
Metodo que retorna os totalizadores da filial, C.Custo e Empresa
@type  Metodo
@author Bruno Costa
@return character
@since 05/02/2024
/*/
method formatTotJson(cTot as character) as array class GPER510TReportsBusinessObject

    Local aTotFil as array
    Local aTotCC as array
    Local aTotEmp as array
    local aCustomApd as array
    local nValPd as numeric  
    local nValHor as numeric
    local nI as numeric
    local cChave as character
    
    If cTot == "1"
        aCustomApd  := self:aPdTot
        self:aPdTot := {}
        aTotFil     := {}
        If Len(aCustomApd) > 0
            aSort( aCustomApd,,,{ |x,y| x[2] + x[4] < y[2] + y[4] } )
            cChave := ""
            For nI := 1 To Len(aCustomApd)
                If cChave <> aCustomApd[nI][1]+aCustomApd[nI][2]+aCustomApd[nI][4] //Filial+Verba+Periodo
                    cChave := aCustomApd[nI][1]+aCustomApd[nI][2]+aCustomApd[nI][4]
                    Aeval( aCustomApd, { |X| nValPd += If( X[2]=aCustomApd[nI][2] .And. X[4]=aCustomApd[nI][4], x[5], 0 ) } )
                    Aeval( aCustomApd, { |X| nValHor += If( X[2]=aCustomApd[nI][2] .And. X[4]=aCustomApd[nI][4], x[6], 0 ) } )
                    aAdd(aTotFil, {;
                        "Filial": aCustomApd[nI][1],;
                        "CodPd" : aCustomApd[nI][2],;
                        "DescPd": aCustomApd[nI][3],;
                        "Periodo": aCustomApd[nI][4],;
                        "Total": nValPd,;
                        "Horas": nValHor,;
                        "TipoPD": aCustomApd[nI][7];
                        })
                    nValPd  := 0
                    nValHor := 0
                EndIf
            Next nI
            return aTotFil
        EndIf 
    ElseIf cTot == "2"    
        aCustomApd    := self:aPdTotCC
        self:aPdTotCC := {}
        aTotCC        := {}
        If Len(aCustomApd) > 0
            aSort( aCustomApd,,,{ |x,y| x[2] + x[4] < y[2] + y[4] } )
            cChave := ""
            For nI := 1 To Len(aCustomApd)
                If cChave <> aCustomApd[nI][1]+aCustomApd[nI][2]+aCustomApd[nI][4]+aCustomApd[nI][8] //Filial+Verba+Periodo+C.Custo
                    cChave := aCustomApd[nI][1]+aCustomApd[nI][2]+aCustomApd[nI][4]+aCustomApd[nI][8]
                    Aeval( aCustomApd, { |X| nValPd += If( X[2]=aCustomApd[nI][2] .And. X[4]=aCustomApd[nI][4] .And. X[8]=aCustomApd[nI][8], x[5], 0 ) } )
                    Aeval( aCustomApd, { |X| nValHor += If( X[2]=aCustomApd[nI][2] .And. X[4]=aCustomApd[nI][4] .And. X[8]=aCustomApd[nI][8], x[6], 0 ) } )
                    aAdd(aTotCC, {;
                        "Filial": aCustomApd[nI][1],;
                        "CodPd" : aCustomApd[nI][2],;
                        "DescPd": aCustomApd[nI][3],;
                        "Periodo": aCustomApd[nI][4],;
                        "Total": nValPd,;
                        "Horas": nValHor,;
                        "TipoPD": aCustomApd[nI][7],;
                        "CodeCC": aCustomApd[nI][8];
                        })
                    nValPd  := 0
                    nValHor := 0
                EndIf
            Next nI   
            return aTotCC
        EndIf     
    Else    
        aCustomApd     := self:aPdTotEmp
        self:aPdTotEmp := {}
        aTotEmp        := {}
        If Len(aCustomApd) > 0
            aSort( aCustomApd,,,{ |x,y| x[2] + x[4] < y[2] + y[4] } )
            cChave := ""
            For nI := 1 To Len(aCustomApd)
                If cChave <> aCustomApd[nI][2]+aCustomApd[nI][4] //Verba+Periodo
                    cChave := aCustomApd[nI][2]+aCustomApd[nI][4]
                    Aeval( aCustomApd, { |X| nValPd += If( X[2]=aCustomApd[nI][2] .And. X[4]=aCustomApd[nI][4], x[5], 0 ) } )
                    Aeval( aCustomApd, { |X| nValHor += If( X[2]=aCustomApd[nI][2] .And. X[4]=aCustomApd[nI][4], x[6], 0 ) } )
                    aAdd(aTotEmp, {;
                        "Filial": aCustomApd[nI][1],;
                        "CodPd" : aCustomApd[nI][2],;
                        "DescPd": aCustomApd[nI][3],;
                        "Periodo": aCustomApd[nI][4],;
                        "Total": nValPd,;
                        "Horas": nValHor,;
                        "TipoPD": aCustomApd[nI][7];
                        })
                    nValPd  := 0
                    nValHor := 0
                EndIf
            Next nI   
        EndIf 
    EndIf 

return aTotEmp
