#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "TOTVS.CH"
#Include "rh.sv.pon.ponr190.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPON", tables="SRA,SR6,SRJ,SQB,CTT,SP9,SPI", name="Rel. Extrato Banco de Horas", country="ALL", initialRelease="12.1.2210")
Class PONR190TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    Public Method new() as object
    Public Method getDescription() as character
    Public Method getData() as object
    Public Method getSchema() as object
EndClass

Method new() Class PONR190TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0001) // RH
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002) // Rel. Extrato Banco de Horas
Return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type   Method
@author Marco Nakazawa
@since  09/10/2023
/*/
Method getDescription() as character class PONR190TReportsBusinessObject
Return (STR0003) // Este relatório apresenta o extrato do banco de horas por funcionário

Method getData(nPage as numeric, oFilter as object) as object class PONR190TReportsBusinessObject
    Local aFilAcess     := StrTokArr(fvalidfil(),"/")               as Array
    Local aFieldsSRA    := {}                                       as Array
    Local aFieldsSR6    := {}                                       as Array
    Local aFieldsSRJ    := {}                                       as Array
    Local aFieldsSQB    := {}                                       as Array
    Local aFieldsCTT    := {}                                       as Array
    Local aLGPDSRA      := {}                                       as Array
    Local aLGPDSR6      := {}                                       as Array
    Local aLGPDSRJ      := {}                                       as Array
    Local aLGPDSQB      := {}                                       as Array
    Local aLGPDCTT      := {}                                       as Array
    Local aInfo         := {}                                       as Array
    Local cAliasQry     := GetNextAlias()                           as Character
    Local cJoiSRASPI    := "%" + FWJoinFilial("SRA", "SPI") + "%"   as Character
    Local cJoiSRASR6    := "%" + FWJoinFilial("SRA", "SR6") + "%"   as Character
    Local cJoiSRASRJ    := "%" + FWJoinFilial("SRA", "SRJ") + "%"   as Character
    Local cJoiSRASQB    := "%" + FWJoinFilial("SRA", "SQB") + "%"   as Character
    Local cJoiSRACTT    := "%" + FWJoinFilial("SRA", "CTT") + "%"   as Character
    Local cJoiSPISP9    := "%" + FWJoinFilial("SPI", "SP9") + "%"   as Character
    Local cLeftSP4      := "%%"                                     as Character
    Local cSitFol       := ""                                       as Character
    Local cCateg        := ""                                       as Character
    Local cLGPDSRA      := ""                                       as Character
    Local cMatrAnt      := ""                                       as Character
    Local cFilialAnt    := ""                                       as Character
    Local cFilDe        := ""                                       as Character
    Local cFilAte       := ""                                       as Character
    Local cCcDe         := ""                                       as Character
    Local cCcAte        := ""                                       as Character
    Local cTurnoDe      := ""                                       as Character
    Local cTurnoAte     := ""                                       as Character
    Local cMatDe        := ""                                       as Character
    Local cMatAte       := ""                                       as Character
    Local cSitFunc      := ""                                       as Character
    Local cCatFunc      := ""                                       as Character
    Local cWhere        := ""                                       as Character
    Local cQrySit       := ""                                       as Character
    Local cQryCat       := ""                                       as Character
    Local cGrpEmpNom    := FWGrpName(cEmpAnt)                       as Character
    Local cEmpNome      := ""                                       as Character
    Local cFilialIn     := ""                                       as Character
    Local cEvtAut       := ""                                       as Character
    Local dPerIni       := Date()                                   as Date
    Local dPerFim       := Date()                                   as Date
    Local lLGPDSR6      := .F.                                      as Logical
    Local lHrsValori    := .F.                                      as Logical
    Local lLGPDSRJ      := .F.                                      as Logical
    Local lLGPDSQB      := .F.                                      as Logical
    Local lLGPDCTT      := .F.                                      as Logical
    Local nCredito      := 0                                        as Numeric
    Local nDebito       := 0                                        as Numeric
    Local nSaldo        := 0                                        as Numeric
    Local nSaldoAnt     := 0                                        as Numeric
    Local nSaldoAtu     := 0                                        as Numeric
    Local nI            := 0                                        as Numeric
    Local lTelaBH       := .F.										as Logical
	Local cSRAAlias		:= ""										as Character
	Local cMarca		:= ""										as Character
	Local cJoinSRA		:= "%%"										as Character
    
    // Definição da paginação
    self:setPageSize(50)
    
    aFieldsSRA := {"RA_FILIAL", "RA_MAT", "RA_NOME", "RA_CATFUNC", "RA_CC", "RA_TNOTRAB","RA_CODFUNC","RA_DEPTO"}
    aFieldsSR6 := {"R6_DESC"}
    aFieldsSRJ := {"RJ_DESC"}
    aFieldsSQB := {"QB_DESCRIC"}
    aFieldsCTT := {"CTT_DESC01"}
    
    aLGPDSRA := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsSRA)
    aLGPDSR6 := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsSR6)
    aLGPDSRJ := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsSRJ)
    aLGPDSQB := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsSQB)
    aLGPDCTT := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsCTT)
    
    For nI := 1 To Len(aFieldsSRA)
        If(aScan(aLGPDSRA, aFieldsSRA[nI]) == 0, cLGPDSRA += aFieldsSRA[nI] + "*", Nil)
    Next
    
    lLGPDSR6 := Empty(aLGPDSR6)
    lLGPDSRJ := Empty(aLGPDSRJ)
    lLGPDSQB := Empty(aLGPDSQB)
    lLGPDCTT := Empty(aLGPDCTT)
    
    // Carrega os parâmetros informados
    oJParams    := oFilter:getParameters()
    cFilDe      := oJParams["filial1",1]																								// Filial De?
    cFilAte     := oJParams["filial2",1]																								// Filial Até?
    cCcDe       := oJParams["cc1",1]																									// Centro de Custo De?
    cCcAte      := oJParams["cc2",1]																									// Centro de Custo Até?
    cTurnoDe    := oJParams["turno1",1]																									// Turno De?
    cTurnoAte   := oJParams["turno2",1]																									// Turno Até?
    cMatDe      := oJParams["mat1",1]																									// Matricula De?
    cMatAte     := oJParams["mat2",1]																									// Matricula Até?
    dPerIni     := sToD(StrTran(SubStr(oJParams["per1",1],1,10),"-",""))																// Período Inicial?
    dPerFim     := sToD(StrTran(SubStr(oJParams["per2",1],1,10),"-",""))																// Período Final?
    cSitFunc    := oJParams["sitfunc",1]																								// Situação Funcionário?
    cCatFunc    := oJParams["catfunc",1]																								// Categoria Funcionário?
    lHrsValori  := If(ValType(oJParams["hrsvalori",1]) == "C",If(oJParams["hrsvalori",1] == "true", .T., .F.), oJParams["hrsvalori",1])	// Imprimi Horas Valorizadas?
    cEvtAut     := oJParams["evtaut",1]																									// Eventos Autorizados?
    
    //Tratamento para quando chamada pela tela de gestão de banco de horas
    If AllTrim(cFilDe) == "PONM110" .And. !Empty(cFilAte) .And. !Empty(cCcDe)
        lTelaBH := .T.
        cSRAAlias := cFilAte
        cMarca  := cCcDe
    EndIf
    
    // Filtro das Filiais
	If !lTelaBH
		If (!Empty(cFilDe) .And. !Empty(cFilAte)) .Or. (Empty(cFilDe) .And. !Empty(cFilAte) .Or. (!Empty(cFilDe) .And. Empty(cFilAte)))
			If (!Empty(cFilDe) .And. Empty(cFilAte), cFilAte := Replicate("Z",FWGETTAMFILIAL), Nil)
				For nI := 1 To Len(aFilAcess)
					If aFilAcess[nI] >= cFilDe .And. aFilAcess[nI] <= cFilAte
						If nI == 1 .Or. Empty(cFilialIn)
							cFilialIn := "'" + aFilAcess[nI] + "'"
						Else
							cFilialIn += ",'" + aFilAcess[nI] + "'"
						EndIf
					EndIf
				Next
				
				If !Empty(cFilialIn)
					cWhere := " SRA.RA_FILIAL IN (" + cFilialIn + ")"
				Else
				cWhere := " SRA.RA_FILIAL IN ('" + StrTran(fvalidfil(),"/","','") + "')"
			EndIf
		ElseIf Empty(cFilDe) .And. Empty(cFilAte)
			cWhere := " SRA.RA_FILIAL IN ('" + StrTran(fvalidfil(),"/","','") + "')"
		EndIf
    EndIf
	
	// Filtro das datas
	If(dPerIni > dPerFim, dPerFim := dPerIni, Nil)
	cWhere += If(!lTelaBH, " AND ", " " ) + "(SPI.PI_DATA >= '" + dToS(dPerIni) + "' AND SPI.PI_DATA <= '" + dToS(dPerFim) + "')"
	
	If !lTelaBH
		// Filtro da Matrícula
		If !Empty(cMatDe)
			cWhere += " AND SRA.RA_MAT >= '" + SubStr(cMatDe,1,TamSx3("RA_MAT")[1]) + "'"
		EndIf
		
		If!Empty(cMatAte)
			cWhere += " AND SRA.RA_MAT <= '" + SubStr(cMatAte,1,TamSx3("RA_MAT")[1]) + "'"
		EndIf
		
		// Filtro do Centro de Custo
		If !Empty(cCcDe)
			cWhere += " AND SRA.RA_CC >= '" + SubStr(cCcDe,1,TamSx3("RA_CC")[1]) + "'"
		EndIf
		
		If!Empty(cCcAte)
			cWhere += " AND SRA.RA_CC <= '" + SubStr(cCcAte,1,TamSx3("RA_CC")[1]) + "'"
		EndIf
		
		// Filtro do Turno
		If !Empty(cTurnoDe)
			cWhere += " AND SRA.RA_TNOTRAB >= '" + SubStr(cTurnoDe,1,TamSx3("RA_TNOTRAB")[1]) + "'"
		EndIf
		
		If!Empty(cTurnoAte)
			cWhere += " AND SRA.RA_TNOTRAB <= '" + SubStr(cTurnoAte,1,TamSx3("RA_TNOTRAB")[1]) + "'"
		EndIf
		
		// Filtro da Situação dos funcionários
		If !Empty(cSitFunc)
			cSitFunc := StrTran(cSitFunc,",","")
			For nI:=1 to Len(cSitFunc)
				cQrySit += "'"+Subs(cSitFunc, nI, 1)+"'"
				If ( nI + 1) <= Len(cSitFunc)
					cQrySit += "," 
				EndIf
			Next nI
			
			cWhere += " AND SRA.RA_SITFOLH IN (" + cQrySit + ")"
		EndIf
		
		// Filtro da Categoria dos funcionários
		If !Empty(cCatFunc)
			cCatFunc := StrTran(cCatFunc,",","")
			
			For nI:=1 to Len(cCatFunc)
				cQryCat += "'"+Subs(cCatFunc, nI, 1)+"'"
				If ( nI+1 ) <= Len(cCatFunc)
					cQryCat += "," 
				EndIf
			Next nI
			
			cWhere += " AND SRA.RA_CATFUNC IN (" + cQryCat + ")"
		EndIf
	EndIf
	
    // Eventos Autorizados
    If !Empty(cEvtAut) .And. cEvtAut == "1"
        cLeftSP4 := "%LEFT JOIN " + RetSqlName("SP4") + " SP4 ON " + FWJoinFilial("SP9", "SP4") + " AND " + "SP4.P4_CODAUT = SP9.P9_CODIGO%"
        cWhere += " AND (SP4.P4_CODAUT <> ' ' OR SP9.P9_IDPON LIKE '%A%')"
    ElseIf !Empty(cEvtAut) .And. cEvtAut == "2"
        cLeftSP4 := "%LEFT JOIN " + RetSqlName("SP4") + " SP4 ON " + FWJoinFilial("SP9", "SP4") + " AND " + "SP4.P4_CODNAUT = SP9.P9_CODIGO%"
        cWhere += " AND (SP4.P4_CODNAUT <> ' ' OR SP9.P9_IDPON LIKE '%N%')"
    EndIf
	
    cWhere := "%" + cWhere + "%"
	
	If lTelaBH
		cJoinSRA := "INNER JOIN " + cSRAAlias + " TMP "
		cJoinSRA += "ON SRA.RA_FILIAL = TMP.RA_FILIAL AND "
		cJoinSRA += "SRA.RA_MAT = TMP.RA_MAT AND "
		cJoinSRA += "TMP.OK = '" + cMarca + "' "
		cJoinSRA := "%" + cJoinSRA + "%"
	EndIf
	
    BeginSql alias cAliasQry
        SELECT
            SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_CATFUNC, SRA.RA_CODFUNC, SRA.RA_DEPTO, SRA.RA_CC, SRA.RA_TNOTRAB, SRA.RA_SITFOLH,
            SR6.R6_DESC,
            SRJ.RJ_DESC,
            SPI.PI_DATA, SPI.PI_PD, SPI.PI_QUANT, SPI.PI_QUANTV, SPI.PI_STATUS, SPI.PI_DTBAIX,
            SQB.QB_DESCRIC,
            CTT.CTT_DESC01,
            SP9.P9_DESC, SP9.P9_TIPOCOD
        FROM %table:SRA% SRA
		%exp:cJoinSRA%
        INNER JOIN %table:SPI% SPI ON %exp:cJoiSRASPI% AND SPI.PI_MAT = SRA.RA_MAT AND SPI.%NotDel%
        INNER JOIN %table:SP9% SP9 ON %exp:cJoiSPISP9% AND SP9.P9_CODIGO = SPI.PI_PD AND SP9.%NotDel%
        %exp:cLeftSP4%
        LEFT JOIN %table:SR6% SR6 ON %exp:cJoiSRASR6% AND SR6.R6_TURNO = SRA.RA_TNOTRAB AND SR6.%NotDel%
        LEFT JOIN %table:SRJ% SRJ ON %exp:cJoiSRASRJ% AND SRJ.RJ_FUNCAO = SRA.RA_CODFUNC AND SRJ.%NotDel%
        LEFT JOIN %table:SQB% SQB ON %exp:cJoiSRASQB% AND SQB.QB_DEPTO = SRA.RA_DEPTO AND SQB.%NotDel%
        LEFT JOIN %table:CTT% CTT ON %exp:cJoiSRACTT% AND CTT.CTT_CUSTO = SRA.RA_CC AND CTT.%NotDel%
        WHERE %exp:cWhere%
			AND SRA.%notDel%
        GROUP BY SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_CATFUNC, SRA.RA_CODFUNC, SRA.RA_DEPTO, SRA.RA_CC, SRA.RA_TNOTRAB, SRA.RA_SITFOLH, SR6.R6_DESC, SRJ.RJ_DESC, SPI.PI_DATA, SPI.PI_PD, SPI.PI_QUANT, SPI.PI_QUANTV, SPI.PI_STATUS, SPI.PI_DTBAIX, SQB.QB_DESCRIC, CTT.CTT_DESC01, SP9.P9_DESC, SP9.P9_TIPOCOD 
        ORDER BY SRA.RA_FILIAL, SRA.RA_MAT, SPI.PI_DATA        
    EndSql
	
    While !(cAliasQry)->(Eof())
        If (Empty(cMatrAnt) .And. Empty(cFilialAnt)) .Or. ((cAliasQry)->RA_FILIAL <> cFilialAnt .Or. (cAliasQry)->RA_MAT <> cMatrAnt)
            cFilialAnt  := (cAliasQry)->RA_FILIAL
            cMatrAnt    := (cAliasQry)->RA_MAT
            nTotCred    := 0
            nTotDeb     := 0
            nSaldo      := 0
            nSaldoAnt   := 0
            nSaldoAtu   := 0
            aInfo := {}
			fInfo(@aInfo, (cAliasQry)->RA_FILIAL)
            nSaldoAnt := fBuscaSald(cFilialAnt, cMatrAnt, dPerIni, lHrsValori)
            cEmpNome := AllTrim(FWCompanyName(cEmpAnt,(cAliasQry)->RA_FILIAL))
        EndIf
		
        nCredito    := 0
        nDebito     := 0
		
        If (cAliasQry)->P9_TIPOCOD $ "1*3"
            nTotCred    := If(lHrsValori, SomaHoras(nTotCred,(cAliasQry)->PI_QUANTV), SomaHoras(nTotCred,(cAliasQry)->PI_QUANT))
            nCredito    := If(lHrsValori, (cAliasQry)->PI_QUANTV, (cAliasQry)->PI_QUANT)
        Else
            nTotDeb     := If(lHrsValori, SomaHoras(nTotDeb,(cAliasQry)->PI_QUANTV), SomaHoras(nTotDeb,(cAliasQry)->PI_QUANT))
            nDebito     := If(lHrsValori, (cAliasQry)->PI_QUANTV, (cAliasQry)->PI_QUANT)
        EndIf
		
        If Empty((cAliasQry)->PI_STATUS)
            nSaldo := SubHoras(nTotCred,nTotDeb)
        EndIf
		
        nSaldoAtu := If(nSaldoAnt < 0, SubHoras(nSaldo, nSaldoAnt*-1), SomaHoras(nSaldo, nSaldoAnt))
		
        cSitFol	:= AllTrim((cAliasQry)->(fDesc("SX5", "31" + RA_SITFOLH, "X5_DESCRI", 15, RA_FILIAL)))
        cCateg	:= AllTrim((cAliasQry)->(fDesc("SX5", "28" + RA_CATFUNC, "X5_DESCRI", 11, RA_FILIAL)))
		
        self:oData:appendData({;
            "GRPEMPRESA": 	AllTrim(cGrpEmpNom),;
            "EMPRESA": 		cEmpNome,;
            "ENDERECO": 	AllTrim(If(Len(aInfo) > 0, aInfo[04], SM0->M0_ENDCOB)),;
            "CNPJ": 		Transform(If(Len(aInfo) > 0, aInfo[08], SM0->M0_CGC),'@!R NN.NNN.NNN/NNNN-99'),;
            "RA_FILIAL": 	(cAliasQry)->(If("RA_FILIAL" $ cLGPDSRA, Anonimiza(RA_FILIAL), AllTrim(RA_FILIAL))),;
            "RA_MAT": 		(cAliasQry)->(If("RA_MAT" 	 $ cLGPDSRA, Anonimiza(RA_MAT), AllTrim(RA_MAT))),;
            "FILMAT": 		(cAliasQry)->(If("RA_FILIAL" $ cLGPDSRA .Or. "RA_MAT" $ cLGPDSRA, Anonimiza(RA_MAT), AllTrim(RA_FILIAL) + " - " + RA_MAT)),;
            "RA_NOME": 		(cAliasQry)->(If("RA_NOME" 	 $ cLGPDSRA, Anonimiza(RA_NOME), AllTrim(RA_NOME))),;
            "RA_CATFUNC": 	If("RA_CATFUNC" $ cLGPDSRA, Anonimiza(cCateg), cCateg),;
            "RA_CC": 		(cAliasQry)->(If("RA_CC"	 $ cLGPDSRA, Anonimiza(RA_CC), AllTrim(RA_CC))),;
            "RA_CODFUNC": 	(cAliasQry)->(If("RA_CODFUNC"$ cLGPDSRA, Anonimiza(RA_CODFUNC), AllTrim(RA_CODFUNC))),;
            "RA_DEPTO": 	(cAliasQry)->(If("RA_DEPTO"  $ cLGPDSRA, Anonimiza(RA_DEPTO), AllTrim(RA_DEPTO))),;
            "RA_TNOTRAB": 	(cAliasQry)->(If("RA_TNOTRAB"$ cLGPDSRA, Anonimiza(RA_TNOTRAB), AllTrim(RA_TNOTRAB))),;
            "RA_SITFOLH": 	If("RA_SITFOLH" $ cLGPDSRA, Anonimiza(cSitFol), cSitFol),;
            "DESCTURNO": 	(cAliasQry)->(If(lLGPDSR6 .Or. "RA_TNOTRAB" $ cLGPDSRA, Anonimiza(R6_DESC), AllTrim(R6_DESC))),;
            "DESCFUNCAO": 	(cAliasQry)->(If(lLGPDSRJ .Or. "RA_CODFUNC" $ cLGPDSRA, Anonimiza(RJ_DESC), AllTrim(RJ_DESC))),;
            "PI_DATA": 		totvs.framework.treports.date.dateToTimeStamp(sToD((cAliasQry)->PI_DATA)),;
            "PI_PD": 		(cAliasQry)->PI_PD,;
            "PI_QUANT": 	(cAliasQry)->PI_QUANT,;
            "PI_QUANTV": 	(cAliasQry)->PI_QUANTV,;
            "PI_STATUS": 	If((cAliasQry)->PI_STATUS == "B", "Baixado", "Pendente"),;
            "PI_DTBAIX": 	totvs.framework.treports.date.dateToTimeStamp(sToD((cAliasQry)->PI_DTBAIX)),;
            "DESCDPTO": 	(cAliasQry)->(If(lLGPDSQB .Or. "RA_DEPTO" $ cLGPDSRA, Anonimiza(QB_DESCRIC), AllTrim(QB_DESCRIC))),;
            "DESCCC": 		(cAliasQry)->(If(lLGPDCTT .Or. "RA_CC" $ cLGPDSRA, Anonimiza(CTT_DESC01), AllTrim(CTT_DESC01))),;
            "DESCEVT": 		(cAliasQry)->P9_DESC,;
            "P9_TIPOCOD": 	(cAliasQry)->P9_TIPOCOD,;
            "CREDITO": 		StrTran(StrZero(nCredito, 5, 2), ".", ":"),;
            "DEBITO": 		StrTran(StrZero(nDebito , 5, 2), ".", ":"),;
            "TOTALCRE": 	StrTran(StrZero(nTotCred, 5, 2), ".", ":"),;
            "TOTALDEB": 	StrTran(StrZero(nTotDeb , 5, 2), ".", ":"),;
            "SALDO": 		StrTran(If(nSaldo >= 0, StrZero(nSaldo, 5, 2), StrZero(nSaldo, 6, 2)), ".", ":"),;
            "SALDOANT": 	StrTran(If(nSaldoAnt >= 0, StrZero(nSaldoAnt, 5, 2),StrZero(nSaldoAnt, 6, 2)), ".", ":"),;
            "SALDOATUAL": 	StrTran(If(nSaldoAtu >= 0, StrZero(nSaldoAtu, 5, 2),StrZero(nSaldoAtu, 6, 2)), ".", ":");
        })
		
        (cAliasQry)->(dbSkip())
    EndDo
    
    self:setHasNext(.F.) 
	
    (cAliasQry)->(DBCloseArea())
Return self:oData

/*/{Protheus.doc} getSchema
Metodo que retorna a estrutura dos campos que serão enviados para o Smart View
@type   Method
@author Marco Nakazawa
@since  09/10/2023
/*/
Method getSchema() as object class PONR190TReportsBusinessObject
    Local aFieldsSRA  := {}
    Local aFieldsSPI  := {}
    Local aFieldsSP9  := {}
    
    aFieldsSRA := {"RA_FILIAL", "RA_MAT", "RA_NOME", "RA_CATFUNC", "RA_CC", "RA_TNOTRAB","RA_CODFUNC"}
    aFieldsSPI := {"PI_DATA","PI_PD","PI_QUANT","PI_QUANTV","PI_STATUS","PI_DTBAIX"}
    aFieldsSP9 := {"P9_TIPOCOD"}
    
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("SPI", aFieldsSPI)
    self:oSchema:aliasToSchema("SP9", aFieldsSP9)
    
    self:addParameter("filial1",            STR0004,    "string",   .F.) // Filial De?
    self:addParameter("filial2",            STR0005,    "string",   .F.) // Filial Até?
    self:addParameter("cc1",                STR0006,    "string",   .F.) // Centro de Custo De?
    self:addParameter("cc2",                STR0007,    "string",   .F.) // Centro de Custo Até?
    self:addParameter("turno1",             STR0008,    "string",   .F.) // Turno De?
    self:addParameter("turno2",             STR0009,    "string",   .F.) // Turno Até?
    self:addParameter("mat1",               STR0010,    "string",   .F.) // Matricula De?
    self:addParameter("mat2",               STR0011,    "string",   .F.) // Matricula Até?
    self:addParameter("per1",               STR0012,    "date",     .F.) // Período Inicial?
    self:addParameter("per2",               STR0013,    "date",     .F.) // Período Final?
    self:addParameter("sitfunc",            STR0014,    "string",   .F.) // Situação Funcionário?
    self:addParameter("catfunc",            STR0015,    "string",   .F.) // Categoria Funcionário?
    self:addParameter("hrsvalori",          STR0016,    "boolean",  .F.) // Utilizar Horas Valorizadas?
    self:addParameter("evtaut",             STR0017,    "string",   .F.) // Eventos Autorizados?
    
    self:oSchema:addProperty("GRPEMPRESA",      STR0018,    "string",   STR0018,    "GRPEMPRESA")   // Grupo de Empresas
    self:oSchema:addProperty("EMPRESA",         STR0019,    "string",   STR0019,    "EMPRESA")      // Empresa
    self:oSchema:addProperty("ENDERECO",        STR0020,    "string",   STR0020,    "ENDERECO")     // Endereço
    self:oSchema:addProperty("CNPJ",            STR0021,    "string",   STR0021,    "CNPJ")         // CNPJ
    self:oSchema:addProperty("SALDO",           STR0022,    "string",   STR0022,    "SALDO")        // Saldo
    self:oSchema:addProperty("SALDOANT",        STR0023,    "string",   STR0023,    "SALDO")        // Saldo Período Anterior
    self:oSchema:addProperty("SALDOATUAL",      STR0024,    "string",   STR0024,    "SALDOATUAL")   // Saldo Atual
    self:oSchema:addProperty("TOTALDEB",        STR0025,    "string",   STR0025,    "TOTALDEB")     // Total Débito
    self:oSchema:addProperty("TOTALCRE",        STR0026,    "string",   STR0026,    "TOTALCRE")     // Total Crédito
    self:oSchema:addProperty("FILMAT",          STR0027,    "string",   STR0027,    "FILMAT")       // Filial Matrícula
    self:oSchema:addProperty("DESCDPTO",        STR0028,    "string",   STR0028,    "DESCDPTO")     // Descrição Departamento
    self:oSchema:addProperty("DESCTURNO",       STR0029,    "string",   STR0029,    "DESCTURNO")    // Descrição Turno
    self:oSchema:addProperty("DESCFUNCAO",      STR0030,    "string",   STR0030,    "DESCFUNCAO")   // Descrição Função
    self:oSchema:addProperty("DESCCC",          STR0031,    "string",   STR0031,    "DESCCC")       // Descrição Centro de Custro
    self:oSchema:addProperty("DESCEVT",         STR0032,    "string",   STR0032,    "DESCEVT")      // Descrição Evento
    self:oSchema:addProperty("CREDITO",         STR0033,    "string",   STR0033,    "CREDITO")      // Horas Crédito
    self:oSchema:addProperty("DEBITO",          STR0034,    "string",   STR0034,    "DEBITO")       // Horas Débito
Return self:oSchema

/*/{Protheus.doc} fBuscaSald
Função para retornar o saldo do período anterior
@type   Function
@author Marco Nakazawa
@since  09/10/2023
/*/
Static Function fBuscaSald(cFil, cMat, dDataIni, lHrsValori)
Local cSPIQry       := GetNextAlias()                           as Character
Local cJoiSPISP9    := "%" + FWJoinFilial("SPI", "SP9") + "%"   as Character
Local nSaldoAnt     := 0                                        as Numeric

Default cFil        := ""
Default cMat        := ""
Default dDataIni    := Date()
Default lHrsValori  := .F.

If !Empty(cFil) .And. !Empty(cMat)
    cWhere := "%SPI.PI_STATUS <> 'B'"
    cWhere += " AND SPI.PI_DATA < '" + dToS(dDataIni) + "'"
    cWhere += " AND SPI.PI_FILIAL = '" + xFilial("SPI", cFil) + "'"
    cWhere += " AND SPI.PI_MAT = '" + cMat + "'%"

    BeginSql alias cSPIQry
        SELECT
            SPI.PI_DATA,
            SPI.PI_PD,
            SPI.PI_QUANT,
            SPI.PI_QUANTV,
            SP9.P9_TIPOCOD
        FROM %table:SPI% SPI
        INNER JOIN %table:SP9% SP9 ON %exp:cJoiSPISP9% AND SP9.P9_CODIGO = SPI.PI_PD
        WHERE %exp:cWhere%
          AND SPI.%notDel%
        ORDER BY SPI.PI_DATA        
    EndSql

    While !(cSPIQry)->(Eof())
        If (cSPIQry)->P9_TIPOCOD $ "1*3"
            If(lHrsValori, nSaldoAnt := SomaHoras(nSaldoAnt, (cSPIQry)->PI_QUANTV), nSaldoAnt := SomaHoras(nSaldoAnt, (cSPIQry)->PI_QUANT))
        Else
            If(lHrsValori, nSaldoAnt := SubHoras(nSaldoAnt, (cSPIQry)->PI_QUANTV), nSaldoAnt := SubHoras(nSaldoAnt, (cSPIQry)->PI_QUANT))
        EndIf

        (cSPIQry)->(dbSkip())
    EndDo

    (cSPIQry)->(DBCloseArea())
EndIf

Return nSaldoAnt

Static Function Anonimiza(cConteudo)
Return SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(AllTrim(cConteudo)), 1, 10)
