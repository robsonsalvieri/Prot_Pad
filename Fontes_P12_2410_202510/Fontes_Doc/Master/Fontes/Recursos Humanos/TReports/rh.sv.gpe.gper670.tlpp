#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.sv.gpe.gper670.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="CTT, RCH, SRA, SRC, SRD, SRV", name="Relatório do Resumo da folha de pagamento por competência", country="ALL", initialRelease="12.1.2310")
class GPER670TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getParams() as Numeric
    private method getWherePar() as character

    @Get("/api/rh/smartview/v1/options/gper670/:param")
    Public Method getComboBox() As Variant

    @Get("/api/rh/smartview/v1/options/gper670/getRot670")
    Public Method getRot670() As Variant

endclass
 
method new() as object class GPER670TReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Relatório do resumo da folha de pagamento por competência"
return self
 
method getDescription() as character class GPER670TReportsBusinessObject
return STR0003 //"Objeto contendo informações do resumo da folha de pagamento por competência"
 
method getAreas() as array class GPER670TReportsBusinessObject
return {STR0002}//"RH"

/*/{Protheus.doc} GPER670TReportsBusinessObject:getData()
Captura os dados do objeto de negócio.
@type Method
@version 12
@author Bruno Costa
@since 14/03/2025
@return Object, Dados do objeto de negócio
/*/
method getData(nPage as numeric, oFilter as object) as object class GPER670TReportsBusinessObject
Local jParams as json
Local jVerbas as json 
Local jPdCont as json 
Local jPdFil as json 
Local jPdEmp as json
Local jData as json 
Local aInfo as array
Local lOcorr as logical
Local cAcessaSRA as character
Local cKeyPdFil as character 
Local cKeyContr as character
Local cLastFil as character
Local cSitFolh as character
Local cFilProc as character
Local cTpContr as character
Local cMatProc as character
Local cAlias as character
Local cWhere as character
Local cWSRD as character
Local cKey as character
Local cAno as character
Local cMes as character
Local nDescontos as numeric
Local nProventos as numeric
Local nTransTot as numeric
Local nTransDet as numeric
Local nTotGeral as numeric
Local nTransInd as numeric
Local nTransInt as numeric
Local nAfaGeral as numeric
Local nFerGeral as numeric
Local nDemGeral as numeric
Local nSNGeral as numeric
Local nPdGeral as numeric
Local nPdTotal as numeric
Local nContrat as numeric
Local nAfasTot as numeric
Local nAfasInd as numeric
Local nAfasDet as numeric
Local nAfasInt as numeric
Local nTGeral as numeric
Local nNorInt as numeric
Local nFerInd as numeric
Local nFerInt as numeric
Local nTotInd as numeric
Local nDemInd as numeric
Local nDemInt as numeric
Local nFerTot as numeric
Local nFerDet as numeric
Local nDemTot as numeric
Local nDemDet as numeric
Local nNorTot as numeric
Local nNorInd as numeric
Local nNorDet as numeric
Local nTotSit as numeric
Local nTotDet as numeric
Local nTotInt as numeric
Local nNormal as numeric
Local nTransf as numeric
Local nBases as numeric
Local nAfast as numeric
Local nRef as numeric
Local nFer as numeric
Local nDem as numeric
Local nPdC as numeric
Local nPD as numeric

Private cAnoMes as character

Static oStatement
Static __cEmpAnt := cEmpAnt
Static __cWhere  := ""

jParams := oFilter:getParameters() 

If Empty(jParams['MESANO', 1]) .Or. (Len(jParams['ROT']) .And. Empty(jParams['ROT', 1])) .Or. (Len(jParams['PROC']) .And. Empty(jParams['PROC', 1]))
    self:oData:appendData({"LMSN": .T.})
    return self:oData
EndIf

cAno := Substr(jParams['MESANO', 1], 3, 6)  
cMes := Substr(jParams['MESANO', 1], 1, 2)

If Val(cAno) < 1900 .Or. (Val(cMes) < 1 .Or. Val(cMes) > 12)
    self:oData:appendData({"LMSN": .T.})
    return self:oData
EndIf

cAnoMes    := cAno + cMes
cAcessaSRA := &( "{||" + ChkRH( "GPER670", "SRA", "2" ) + "}")
jData      := JSonObject():new()
cWhere     := self:getWherePar(jParams, "SRC.RC")
cWSRD      := self:getWherePar(jParams, "SRD.RD")
lOcorr     := !Empty(jParams['IMPREF', 1]) .And. jParams['IMPREF', 1] == "2"

If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(cWhere == __cWhere)

    cQuery := "SELECT SRC.RC_FILIAL AS FILIAL, SRC.RC_MAT AS MAT, SRC.RC_PD AS PD, SUM(SRC.RC_HORAS) HORAS, SUM(SRC.RC_VALOR) VALOR, SRC.RC_PROCES AS PROCES, COUNT(SRC.RC_PD) OCORR, SRA.RA_TPCONTR, SRV.RV_DESC, SRV.RV_TIPO, SRV.RV_TIPOCOD, RCH.RCH_DTFIM, RCH.RCH_DTINI " 
    cQuery += "FROM " + RetSqlName("SRC") + " SRC "
    cQuery += "INNER JOIN " + RetSqlName("RCH") + " RCH ON " + FWJoinFilial( "RCH", "SRC" ) + " AND RCH.RCH_PROCES = SRC.RC_PROCES AND RCH.RCH_PER = SRC.RC_PERIODO AND RCH.RCH_NUMPAG = SRC.RC_SEMANA AND RCH.RCH_ROTEIR = SRC.RC_ROTEIR AND RCH.D_E_L_E_T_ = ? "     
    cQuery += "INNER JOIN " + RetSqlName("SRA") + " SRA ON  SRA.RA_FILIAL = SRC.RC_FILIAL AND SRA.RA_MAT = SRC.RC_MAT AND SRA.D_E_L_E_T_ = ? "
    cQuery += "INNER JOIN " + RetSqlName("SRV") + " SRV ON " + FWJoinFilial( "SRV", "SRC" ) + " AND SRV.RV_COD = SRC.RC_PD AND SRV.D_E_L_E_T_ = ? "
    cQuery += "WHERE SRC.D_E_L_E_T_ = ? "
    
    If !Empty(cWhere)
        cQuery += cWhere    
    EndIf
    
    cQuery += "GROUP BY SRC.RC_FILIAL, SRC.RC_MAT, SRC.RC_PD, SRC.RC_PROCES, SRA.RA_TPCONTR, SRV.RV_DESC, SRV.RV_TIPOCOD, SRV.RV_TIPO, RCH.RCH_DTFIM, RCH.RCH_DTINI "
    cQuery += "UNION " 

    cQuery += "SELECT SRD.RD_FILIAL AS FILIAL, SRD.RD_MAT AS MAT, SRD.RD_PD AS PD, SUM(SRD.RD_HORAS) HORAS, SUM(SRD.RD_VALOR) VALOR, SRD.RD_PROCES AS PROCES, COUNT(SRD.RD_PD) OCORR, SRA.RA_TPCONTR, SRV.RV_DESC, SRV.RV_TIPO, SRV.RV_TIPOCOD, RCH.RCH_DTFIM,RCH.RCH_DTINI "
    cQuery += "FROM " + RetSqlName("SRD") + " SRD "
    cQuery += "INNER JOIN " + RetSqlName("RCH") + " RCH ON " + FWJoinFilial( "RCH", "SRD" ) + " AND RCH.RCH_PROCES = SRD.RD_PROCES AND RCH.RCH_PER = SRD.RD_PERIODO AND RCH.RCH_NUMPAG = SRD.RD_SEMANA AND RCH.RCH_ROTEIR = SRD.RD_ROTEIR AND RCH.D_E_L_E_T_ = ? "       
    cQuery += "INNER JOIN " + RetSqlName("SRA") + " SRA ON  SRA.RA_FILIAL = SRD.RD_FILIAL AND SRA.RA_MAT = SRD.RD_MAT AND SRA.D_E_L_E_T_ = ? "
    cQuery += "INNER JOIN " + RetSqlName("SRV") + " SRV ON " + FWJoinFilial( "SRV", "SRD" ) + " AND SRV.RV_COD = SRD.RD_PD AND SRV.D_E_L_E_T_ = ? "
    cQuery += "WHERE SRD.D_E_L_E_T_ = ? "

    If !Empty(cWSRD)
        cQuery += cWSRD    
    EndIf

    cQuery += "GROUP BY SRD.RD_FILIAL, SRD.RD_MAT, SRD.RD_PD, SRD.RD_PROCES, SRA.RA_TPCONTR, SRV.RV_DESC, SRV.RV_TIPOCOD, RV_TIPO, RCH.RCH_DTFIM, RCH.RCH_DTINI "
    cQuery += "ORDER BY FILIAL, RA_TPCONTR, MAT, RV_TIPOCOD, PD" 

    cQuery     := ChangeQuery(cQuery)
    __cEmpAnt  := cEmpAnt
    __cWhere   := cWhere
    oStatement := FwExecStatement():New(cQuery)

EndIf

oStatement:SetString(1, " ") //RCH.D_E_L_E_T_
oStatement:SetString(2, " ") //SRA.D_E_L_E_T_
oStatement:SetString(3, " ") //SRV.D_E_L_E_T_
oStatement:SetString(4, " ") //SRC.D_E_L_E_T_

nPOrder := 5

//Preenche os bind da query SRC
nPOrder := self:getParams(jParams, oStatement, nPOrder)

oStatement:SetString(nPOrder++, " ") //RCH.D_E_L_E_T_
oStatement:SetString(nPOrder++, " ") //SRA.D_E_L_E_T_
oStatement:SetString(nPOrder++, " ") //SRV.D_E_L_E_T_
oStatement:SetString(nPOrder++, " ") //SRD.D_E_L_E_T_

//Preenche os bind da query SRD
self:getParams(jParams, oStatement, nPOrder)

cAlias := oStatement:OpenAlias()

jData["Empresa"]      := AllTrim(FwEmpName(cEmpAnt))
jData["Filial"]       := AllTrim(FwFilialName())
jData["LMSN"]         := .F.
jData["Contrato"]     := {}
jData["Verbas"]       := {}
jData["VerbasGerais"] := {}
jData["SitGeral"]     := {}
jData["VerbasTodos"]  := {}
jVerbas               := JSONObject():New()
jPdCont               := JSONObject():New()
jPdFil                := JSONObject():New()
jPdEmp                := JSONObject():New()

While !(cAlias)->(Eof())

    If cLastFil != (cAlias)->FILIAL
        If !((cAlias)->FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA) 
            (cAlias)->( dbSkip() )
            Loop
        EndIf
        fInfo(@aInfo, (cAlias)->FILIAL)
        cLastFil := (cAlias)->FILIAL
    EndIf
    
    If cFilProc != (cAlias)->FILIAL .Or. cTpContr != (cAlias)->RA_TPCONTR
        cTpContr := (cAlias)->RA_TPCONTR
        nContrat++
        
        AAdd(jData["Contrato"], {;
            "Empresa":    RTrim(aInfo[02]),;
            "CodEmp":     cEmpAnt,;
            "Filial":     RTrim(aInfo[03]),;
            "CodFil":     (cAlias)->FILIAL,;
            "TpContrato": (cAlias)->RA_TPCONTR;
        })    

        jData["Contrato"][nContrat]["Valores"]  := {}
        jData["Contrato"][nContrat]["Situacao"] := {}

        AAdd(jData["Contrato"][nContrat]["Situacao"], {;
            "Normal":       0,;
            "Afastados":    0,;
            "Ferias":       0,;
            "Transferidos": 0,;
            "Demitidos":    0,;
            "Todos":        0;
        })
        nTotSit := nNormal := nAfast := nFer := nDem := nTransf := 0
    EndIf

    If cMatProc != (cAlias)->MAT .Or. nTotSit == 0
        cSitFolh := RetSituacao((cAlias)->FILIAL, (cAlias)->MAT, .F., sToD((cAlias)->RCH_DTFIM), Nil, Nil, Nil, sToD((cAlias)->RCH_DTINI))[1]

        If (cAlias)->RA_TPCONTR == "1"
            IIf(Empty(cSitFolh), (nNormal++, nNorTot++, nNorInd++, nSNGeral++), IIf(cSitFolh == "A", (nAfast++, nAfasTot++, nAfasInd++, nAfaGeral++),;
             IIf(cSitFolh == "F", (nFer++, nFerTot++, nFerInd++, nFerGeral++), IIf(cSitFolh == "D", (nDem++, nDemTot++, nDemInd++, nDemGeral++), (nTransf++, nTransTot++, nTransInd++, nTGeral++)))))
            nTotInd++
        ElseIf (cAlias)->RA_TPCONTR == "2"
            IIf(Empty(cSitFolh), (nNormal++, nNorTot++, nNorDet++, nSNGeral++), IIf(cSitFolh == "A", (nAfast++, nAfasTot++, nAfasDet++, nAfaGeral++),;
             IIf(cSitFolh == "F", (nFer++, nFerTot++, nFerDet++, nFerGeral++), IIf(cSitFolh == "D", (nDem++, nDemTot++, nDemDet++, nDemGeral++), (nTransf++, nTransTot++, nTransDet++, nTGeral++)))))                 
            nTotDet++
        Else
            IIf(Empty(cSitFolh), (nNormal++, nNorTot++, nNorInt++, nSNGeral++), IIf(cSitFolh == "A", (nAfast++, nAfasTot++, nAfasInt++, nAfaGeral++),;
             IIf(cSitFolh == "F", (nFer++, nFerTot++, nFerInt++, nFerGeral++), IIf(cSitFolh == "D", (nDem++, nDemTot++, nDemInt++, nDemGeral++), (nTransf++, nTransTot++, nTransInt++, nTGeral++)))))
             nTotInt++
        EndIf

        nTotSit++
        nTotGeral++

        jData["Contrato"][nContrat]["Situacao"][1]["Normal"]       := nNormal
        jData["Contrato"][nContrat]["Situacao"][1]["Afastados"]    := nAfast
        jData["Contrato"][nContrat]["Situacao"][1]["Ferias"]       := nFer
        jData["Contrato"][nContrat]["Situacao"][1]["Transferidos"] := nTransf
        jData["Contrato"][nContrat]["Situacao"][1]["Demitidos"]    := nDem
        jData["Contrato"][nContrat]["Situacao"][1]["Todos"]        := nTotSit
    
    EndIf

    nProventos := IIf((cAlias)->RV_TIPOCOD == "1", (cAlias)->VALOR, 0)
    nDescontos := IIf((cAlias)->RV_TIPOCOD == "2", (cAlias)->VALOR, 0)
    nBases     := IIf((cAlias)->RV_TIPOCOD $ "3/4", (cAlias)->VALOR, 0)
    nRef       := IIf(lOcorr, (cAlias)->OCORR, (cAlias)->HORAS)
    cKey       := (cAlias)->PD + ((cAlias)->FILIAL) + (cAlias)->RA_TPCONTR
    cKeyContr  := (cAlias)->PD + (cAlias)->RA_TPCONTR
    cKeyPdFil  := (cAlias)->PD + ((cAlias)->FILIAL)

    If (jVerbas:hasProperty(cKey))
        jData["Contrato"][nContrat]["Valores"][jVerbas[cKey]]["Bases"]      += nBases
        jData["Contrato"][nContrat]["Valores"][jVerbas[cKey]]["Proventos"]  += nProventos
        jData["Contrato"][nContrat]["Valores"][jVerbas[cKey]]["Descontos"]  += nDescontos
        jData["Contrato"][nContrat]["Valores"][jVerbas[cKey]]["Referencia"] += nRef
    Else
        jVerbas[cKey] := ++nPD

        AAdd(jData["Contrato"][nContrat]["Valores"], {;
            "Verba":      (cAlias)->PD,;
            "DescPd":     RTrim((cAlias)->RV_DESC),;
            "Referencia": nRef,;
            "Bases":      nBases,;
            "Proventos":  nProventos,;
            "Descontos":  nDescontos,;
            "TipoPd":     (cAlias)->RV_TIPOCOD;
        })

    EndIf

    If (jPdCont:hasProperty(cKeyContr))
        jData["Verbas"][jPdCont[cKeyContr]]["Bases"]       += nBases
        jData["Verbas"][jPdCont[cKeyContr]]["Proventos"]   += nProventos
        jData["Verbas"][jPdCont[cKeyContr]]["Descontos"]   += nDescontos
        jData["Verbas"][jPdCont[cKeyContr]]["Referencia"]  += nRef
        jData["Verbas"][jPdCont[cKeyContr]]["SitInd"]      := nNorInd
        jData["Verbas"][jPdCont[cKeyContr]]["SitIndAfas"]  := nAfasInd
        jData["Verbas"][jPdCont[cKeyContr]]["SitIndFer"]   := nFerInd
        jData["Verbas"][jPdCont[cKeyContr]]["SitIndDem"]   := nDemInd
        jData["Verbas"][jPdCont[cKeyContr]]["SitIndTrans"] := nTransInd
        jData["Verbas"][jPdCont[cKeyContr]]["SitIndTodos"] := nTotInd
        jData["Verbas"][jPdCont[cKeyContr]]["SitDet"]      := nNorDet
        jData["Verbas"][jPdCont[cKeyContr]]["SitDetAfas"]  := nAfasDet
        jData["Verbas"][jPdCont[cKeyContr]]["SitDetFer"]   := nFerDet
        jData["Verbas"][jPdCont[cKeyContr]]["SitDetDem"]   := nDemDet
        jData["Verbas"][jPdCont[cKeyContr]]["SitDetTrans"] := nTransDet
        jData["Verbas"][jPdCont[cKeyContr]]["SitDetTodos"] := nTotDet
        jData["Verbas"][jPdCont[cKeyContr]]["SitInt"]      := nNorInt
        jData["Verbas"][jPdCont[cKeyContr]]["SitIntAfas"]  := nAfasInt
        jData["Verbas"][jPdCont[cKeyContr]]["SitIntFer"]   := nFerInt
        jData["Verbas"][jPdCont[cKeyContr]]["SitIntDem"]   := nDemInt
        jData["Verbas"][jPdCont[cKeyContr]]["SitIntTrans"] := nTransInt
        jData["Verbas"][jPdCont[cKeyContr]]["SitIntTodos"] := nTotInt
         
         //self:Monta("Verbas", jPdCont[cKeyContr] )
    Else
        jPdCont[cKeyContr] := ++nPdC
    
        AAdd(jData["Verbas"], {;
            "Filial":      (cAlias)->FILIAL,;
            "TpContrato":  (cAlias)->RA_TPCONTR,;
            "Verba":       (cAlias)->PD,;
            "DescPd":      RTrim((cAlias)->RV_DESC),;
            "Referencia":  nRef,;
            "Bases":       nBases,;
            "Proventos":   nProventos,;
            "Descontos":   nDescontos,;
            "TipoPd":      (cAlias)->RV_TIPOCOD,;
            "SitInd":      nNorInd,;
            "SitIndAfas":  nAfasInd,;
            "SitIndFer":   nFerInd,;
            "SitIndDem":   nDemInd,;
            "SitIndTrans": nTransInd,;
            "SitIndTodos": nTotInd,;
            "SitDet":      nNorDet,;
            "SitDetAfas":  nAfasDet,;
            "SitDetFer":   nFerDet,;
            "SitDetDem":   nDemDet,;
            "SitDetTrans": nTransDet,;
            "SitDetTodos": nTotDet,;
            "SitInt":      nNorInt,;
            "SitIntAfas":  nAfasInt,;
            "SitIntFer":   nFerInt,;
            "SitIntDem":   nDemInt,;
            "SitIntTrans": nTransInt,;
            "SitIntTodos": nTotInt;
        })

    EndIf

    If (jPdFil:hasProperty(cKeyPdFil))
        jData["VerbasGerais"][jPdFil[cKeyPdFil]]["Bases"]       += nBases
        jData["VerbasGerais"][jPdFil[cKeyPdFil]]["Proventos"]   += nProventos
        jData["VerbasGerais"][jPdFil[cKeyPdFil]]["Descontos"]   += nDescontos
        jData["VerbasGerais"][jPdFil[cKeyPdFil]]["Referencia"]  += nRef
        jData["VerbasGerais"][jPdFil[cKeyPdFil]]["SitNGeral"]   := nSNGeral
        jData["VerbasGerais"][jPdFil[cKeyPdFil]]["SitTotAfas"]  := nAfaGeral
        jData["VerbasGerais"][jPdFil[cKeyPdFil]]["SitTotFer"]   := nFerGeral
        jData["VerbasGerais"][jPdFil[cKeyPdFil]]["SitTotDem"]   := nDemGeral
        jData["VerbasGerais"][jPdFil[cKeyPdFil]]["SitTotTrans"] := nTGeral

    Else
        jPdFil[cKeyPdFil] := ++nPdGeral
    
        AAdd(jData["VerbasGerais"], {;
            "Empresa":     RTrim(aInfo[02]),;
            "CodEmp":      cEmpAnt,;
            "Filial":      RTrim(aInfo[03]),;
            "CodFil":      (cAlias)->FILIAL,;
            "Verba":       (cAlias)->PD,;
            "DescPd":      RTrim((cAlias)->RV_DESC),;
            "Referencia":  nRef,;
            "Bases":       nBases,;
            "Proventos":   nProventos,;
            "Descontos":   nDescontos,;
            "TipoPd":      (cAlias)->RV_TIPOCOD,;
            "SitNGeral":   nSNGeral,;
            "SitTotAfas":  nAfaGeral,;
            "SitTotFer":   nFerGeral,;
            "SitTotDem":   nDemGeral,;
            "SitTotTrans": nTGeral;  
        })

    EndIf

    If (jPdEmp:hasProperty((cAlias)->PD))
        jData["VerbasTodos"][jPdEmp[(cAlias)->PD]]["Bases"]      += nBases
        jData["VerbasTodos"][jPdEmp[(cAlias)->PD]]["Proventos"]  += nProventos
        jData["VerbasTodos"][jPdEmp[(cAlias)->PD]]["Descontos"]  += nDescontos
        jData["VerbasTodos"][jPdEmp[(cAlias)->PD]]["Referencia"] += nRef

    Else
        jPdEmp[(cAlias)->PD] := ++nPdTotal
    
        AAdd(jData["VerbasTodos"], {;
            "Empresa":     RTrim(aInfo[02]),;
            "CodEmp":      cEmpAnt,;
            "Verba":       (cAlias)->PD,;
            "DescPd":      RTrim((cAlias)->RV_DESC),;
            "Referencia":  nRef,;
            "Bases":       nBases,;
            "Proventos":   nProventos,;
            "Descontos":   nDescontos,;
            "TipoPd":      (cAlias)->RV_TIPOCOD;  
        })

    EndIf

    cFilProc := (cAlias)->FILIAL
    cMatProc := (cAlias)->MAT

    (cAlias)->(dbSkip()) 

    If (cAlias)->(Eof())
        AAdd(jData["SitGeral"], {;
            "Filial":       cFilProc,;
            "Normal":       nNorTot,;
            "Afastados":    nAfasTot,;
            "Ferias":       nFerTot,;
            "Transferidos": nTransTot,;
            "Demitidos":    nDemTot,;
            "Todos":        nTotGeral;
        })

    ElseIf cFilProc != (cAlias)->FILIAL
        nSNGeral := nAfaGeral := nFerGeral := nDemGeral := nTGeral := 0
    EndIf 
EndDo

If Len(jData["Contrato"]) > 0
    self:oData:appendData(jData)
EndIf   

(cAlias)->(DBCloseArea())

Return self:oData

/*/{Protheus.doc} GPER670TReportsBusinessObject:getSchema()
Define o schema do objeto de negócio.
@type Method
@version 12
@author Bruno Costa
@since 14/03/2025
@return Object, Schema do objeto de negócio
/*/
method getSchema() as object class GPER670TReportsBusinessObject
Local aFields as array

self:oSchema:addProperty("Empresa", STR0004, "string",  STR0004, "Empresa",,,, .F.) //Nome da empresa
self:oSchema:addProperty("Filial",  STR0005, "string",  STR0005, "Filial" ,,,, .F.) //Nome da filial
self:oSchema:addProperty("LMSN",    STR0080, "boolean", STR0080, "LMSN",,,, .F.)    //Avisos

//Adiciona a nested property Contrato
aFields := {}
AAdd(aFields, {"Empresa",    STR0004, "string", STR0004}) //"Nome da empresa"
AAdd(aFields, {"CodEmp",     STR0006, "string", STR0006}) //"Código da empresa"
AAdd(aFields, {"Filial",     STR0007, "string", STR0007}) //"Filial"
AAdd(aFields, {"CodFil",     STR0008, "string", STR0008}) //"Código da filial"
AAdd(aFields, {"TpContrato", STR0009, "string", STR0009}) //"Tipo de contrato"
AAdd(aFields, {"Valores",    STR0010, "string", STR0010}) //Valores
AAdd(aFields, {"Situacao",   STR0011, "number", STR0011}) //Situação
self:addNestedProperty("Contrato", STR0012, STR0012, Nil, aFields) //Contrato

//Adiciona a nested property Valores no segundo nível do objeto Contrato
aFields := {}
AAdd(aFields, {"Verba",      STR0013, "string", STR0013}) //"Verba"
AAdd(aFields, {"DescPd",     STR0014, "string", STR0014}) //"Descrição da verba"
AAdd(aFields, {"Referencia", STR0015, "number", STR0015}) //"Referência"
AAdd(aFields, {"Bases",      STR0016, "number", STR0016}) //"Bases"
AAdd(aFields, {"Proventos",  STR0017, "number", STR0017}) //"Proventos"
AAdd(aFields, {"Descontos",  STR0018, "number", STR0018}) //"Deduçãos"
AAdd(aFields, {"TipoPd",     STR0054, "string", STR0054}) //"Tipo da verba"
self:transformInNested("Valores", Nil, aFields)

//Adiciona a nested property Situacao no segundo nível do objeto Contrato
aFields := {}
AAdd(aFields, {"Normal",       STR0019, "number", STR0019}) //"Normal"
AAdd(aFields, {"Afastados",    STR0020, "number", STR0020}) //"Afastados"
AAdd(aFields, {"Ferias",       STR0021, "number", STR0021}) //"Ferias"
AAdd(aFields, {"Transferidos", STR0022, "number", STR0022}) //"Transferidos"
AAdd(aFields, {"Demitidos",    STR0023, "number", STR0023}) //"Demitidos"
AAdd(aFields, {"Todos",        STR0024, "number", STR0024}) //"Todos"
self:transformInNested("Situacao", Nil, aFields)

//Adiciona a nested property Verbas
aFields := {}
AAdd(aFields, {"Filial",      STR0007, "string", STR0007}) //"Filial"
AAdd(aFields, {"TpContrato",  STR0009, "string", STR0009}) //"Tipo de contrato"
AAdd(aFields, {"Verba",       STR0013, "string", STR0013}) // "Verba"
AAdd(aFields, {"DescPd",      STR0014, "string", STR0014}) // "Descrição da verba"
AAdd(aFields, {"Referencia",  STR0015, "number", STR0015}) // "Referência"
AAdd(aFields, {"Bases",       STR0016, "number", STR0016}) // "Bases"
AAdd(aFields, {"Proventos",   STR0017, "number", STR0017}) // "Proventos"
AAdd(aFields, {"Descontos",   STR0018, "number", STR0018}) // "Descontos"
AAdd(aFields, {"TipoPd",      STR0054, "string", STR0054}) //"Tipo da verba"
AAdd(aFields, {"SitInd",      STR0055, "number", STR0055}) //Sit. Normal Indeterminado
AAdd(aFields, {"SitIndAfas",  STR0056, "number", STR0056}) //Sit. Afast Indeterminado
AAdd(aFields, {"SitIndFer",   STR0057, "number", STR0057}) //Sit. Férias Indeterminado
AAdd(aFields, {"SitIndDem",   STR0058, "number", STR0058}) //Sit. Demitido Indeterminado
AAdd(aFields, {"SitIndTrans", STR0059, "number", STR0059}) //Sit. Transferido Indeterminado
AAdd(aFields, {"SitIndTodos", STR0060, "number", STR0060}) //Sit. Todos Indeterminado
AAdd(aFields, {"SitDet",      STR0061, "number", STR0061}) //Sit. Normal Determinado
AAdd(aFields, {"SitDetAfas",  STR0062, "number", STR0062}) //Sit. Afast Determinado
AAdd(aFields, {"SitDetFer",   STR0063, "number", STR0063}) //Sit. Férias Determinado
AAdd(aFields, {"SitDetDem",   STR0064, "number", STR0064}) //Sit. Demitido Determinado
AAdd(aFields, {"SitDetTrans", STR0065, "number", STR0065}) //Sit. Transferido Determinado
AAdd(aFields, {"SitDetTodos", STR0066, "number", STR0066}) //Sit. Todos Determinado
AAdd(aFields, {"SitInt",      STR0067, "number", STR0067}) //Sit. Normal Intermitente
AAdd(aFields, {"SitIntAfas",  STR0068, "number", STR0068}) //Sit. Afast Intermitente
AAdd(aFields, {"SitIntFer",   STR0069, "number", STR0069}) //Sit. Férias Intermitente
AAdd(aFields, {"SitIntDem",   STR0070, "number", STR0070}) //Sit. Demitido Intermitente
AAdd(aFields, {"SitIntTrans", STR0071, "number", STR0071}) //Sit. Transferido Intermitente
AAdd(aFields, {"SitIntTodos", STR0072, "number", STR0072}) //Sit. Todos Intermitente
self:addNestedProperty("Verbas", STR0025, STR0025, Nil, aFields) //Verbas

//Adiciona a nested property Verbas Gerais
aFields := {}
AAdd(aFields, {"Empresa",     STR0004, "string", STR0004}) //Nome da empresa
AAdd(aFields, {"CodEmp",      STR0006, "string", STR0006}) //Código da empresa
AAdd(aFields, {"Filial",      STR0007, "string", STR0007}) //Filial
AAdd(aFields, {"CodFil",      STR0008, "string", STR0008}) //Código da filial
AAdd(aFields, {"Verba",       STR0013, "string", STR0013}) //Verba
AAdd(aFields, {"DescPd",      STR0014, "string", STR0014}) //Descrição da verba
AAdd(aFields, {"Referencia",  STR0015, "number", STR0015}) //Referência
AAdd(aFields, {"Bases",       STR0016, "number", STR0016}) //Bases
AAdd(aFields, {"Proventos",   STR0017, "number", STR0017}) //Proventos
AAdd(aFields, {"Descontos",   STR0018, "number", STR0018}) //Descontos
AAdd(aFields, {"TipoPd",      STR0054, "string", STR0054}) //Tipo da verba
AAdd(aFields, {"SitNGeral",   STR0073, "number", STR0073}) //Sit. Normal Geral
AAdd(aFields, {"SitTotAfas",  STR0074, "number", STR0074}) //Sit. Afast Geral
AAdd(aFields, {"SitTotFer",   STR0075, "number", STR0075}) //Sit. Férias Geral
AAdd(aFields, {"SitTotDem",   STR0076, "number", STR0076}) //Sit. Demitido Geral
AAdd(aFields, {"SitTotTrans", STR0077, "number", STR0077}) //Sit. Transferido Geral
self:addNestedProperty("VerbasGerais", STR0078, STR0078, Nil, aFields) //Verbas Gerais

//Adiciona a nested property Verbas Todos contratos
aFields := {}
AAdd(aFields, {"Empresa",     STR0004, "string", STR0004}) //Nome da empresa
AAdd(aFields, {"CodEmp",      STR0006, "string", STR0006}) //Código da empresa
AAdd(aFields, {"Verba",       STR0013, "string", STR0013}) //Verba
AAdd(aFields, {"DescPd",      STR0014, "string", STR0014}) //Descrição da verba
AAdd(aFields, {"Referencia",  STR0015, "number", STR0015}) //Referência
AAdd(aFields, {"Bases",       STR0016, "number", STR0016}) //Bases
AAdd(aFields, {"Proventos",   STR0017, "number", STR0017}) //Proventos
AAdd(aFields, {"Descontos",   STR0018, "number", STR0018}) //Descontos
AAdd(aFields, {"TipoPd",      STR0054, "string", STR0054}) //Tipo da verba
self:addNestedProperty("VerbasTodos", STR0079, STR0079, Nil, aFields) //Verbas Todos

//Adiciona a nested property SitGeral
aFields := {}
AAdd(aFields, {"Filial",       STR0007, "string", STR0007}) //"Filial"
AAdd(aFields, {"Normal",       STR0019, "number", STR0019}) //"Normal"
AAdd(aFields, {"Afastados",    STR0020, "number", STR0020}) //"Afastados"
AAdd(aFields, {"Ferias",       STR0021, "number", STR0021}) //"Férias"
AAdd(aFields, {"Transferidos", STR0022, "number", STR0022}) //"Transferidos"
AAdd(aFields, {"Demitidos",    STR0023, "number", STR0023}) //"Demitidos"
AAdd(aFields, {"Todos",        STR0024, "number", STR0024}) //"Todos"
self:addNestedProperty("SitGeral", STR0026, STR0026, Nil, aFields) //Situação geral

self:addParameter("MESANO",   STR0027, "string", .F.) //Competência MM/AAAA ?
self:addParameter("FILDE",    STR0028, "string", .F.) //Filial De ?
self:addParameter("FILATE",   STR0029, "string", .F.) //Filial Até ?
self:addParameter("CCDE",     STR0030, "string", .F.) //Centro de Custo De ?
self:addParameter("CCATE",    STR0031, "string", .F.) //Centro de Custo Até ?
self:addParameter("MATDE",    STR0032, "string", .F.) //Matrícula De ?
self:addParameter("MATATE",   STR0033, "string", .F.) //Matrícula Até ?
self:addParameter("NOMEDE",   STR0034, "string", .F.) //Nome De ?
self:addParameter("NOMEATE",  STR0035, "string", .F.) //Nome Até ?
self:addParameter("SITFOL",   STR0036, "string", .T.) //Situações a Imp. ?
self:addParameter("CATFUNC",  STR0037, "string", .T.) //Categorias a Imp. ?
self:addParameter("TOTFIL",   STR0038, "string", .F.) //Impr. Total Filial ?
self:addParameter("TOTEMP",   STR0039, "string", .F.) //Impr. Total Empresa ?
self:addParameter("IMPREF",   STR0040, "string", .F.) //Imprime Referência ?
self:addParameter("TPCONT",   STR0041, "string", .F.) //Qual Tipo Contrato ?
self:addParameter("IMPBASES", STR0042, "string", .F.) //Imprimi Bases ?
self:addParameter("ROT",      STR0043, "string", .T.) //Quais roteiros ?
self:addParameter("PROC",     STR0044, "string", .T.) //Quais processos ?

If FWLibVersion() >= "20231121" 
    //Combos
    self:setCustomURL("TOTFIL", "/api/rh/smartview/v1/options/gper670/TOTFIL", 1)
    self:setCustomURL("TOTEMP", "/api/rh/smartview/v1/options/gper670/TOTEMP", 1)
    self:setCustomURL("IMPREF", "/api/rh/smartview/v1/options/gper670/IMPREF", 1)
    self:setCustomURL("TPCONT", "/api/rh/smartview/v1/options/gper670/TPCONT", 1)
    self:setCustomURL("IMPBASES", "/api/rh/smartview/v1/options/gper670/IMPBASES", 1)

    //Lookups     
    self:setCustomURL("PROC", "api/framework/v1/genericLookupService/smartview/RCJ", 2)
    self:setCustomURL("ROT", "api/rh/smartview/v1/options/gper670/getRot670", 2)
    self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
    self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
    self:setCustomURL("CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("SITFOL", "api/framework/v1/genericLookupService/smartview/31", 2)
    self:setCustomURL("CATFUNC", "api/framework/v1/genericLookupService/smartview/28", 2)

EndIf    

return self:oSchema

/*/{Protheus.doc} getComboBox
Metodo que retorna o combobox dos parâmetros
@type  Metodo
@author Bruno Costa
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
@since 17/03/2025
/*/
Method getComboBox() As Variant Class GPER670TReportsBusinessObject
Local jPath as json
Local jResponse as json
Local jItem as json
Local nValues as numeric
Local aValues as array
Local cParam as character

jPath     := oRest:getPathParamsRequest()
jResponse := JSONObject():New()
aValues   := {}

// Captura os path parameters da requisição
If (jPath != Nil)
    cParam := jPath["param"]
EndIf

jResponse["data"]        := {}
jResponse["nextPageUrl"] := Nil

If (cParam == "IMPREF")
    AAdd(aValues, FwHTTPEncode(STR0045)) //"Em Horas"
    AAdd(aValues, FwHTTPEncode(STR0046)) //"Ocorrência"
ElseIf (cParam == "TPCONT")
    AAdd(aValues, FwHTTPEncode(STR0047)) //"Indeterminado"
    AAdd(aValues, FwHTTPEncode(STR0048)) //"Determinado"
    AAdd(aValues, FwHTTPEncode(STR0049)) //"Intermitente"
    AAdd(aValues, FwHTTPEncode(STR0024)) //"Todos"
Else
    AAdd(aValues, FwHTTPEncode(STR0050)) //"Sim"
    AAdd(aValues, FwHTTPEncode(STR0051)) //"Não"
EndIf

If (Len(aValues) > 0)
    For nValues := 1 To Len(aValues)
        jItem := {;
            "key":   CValToChar(nValues),;
            "label": aValues[nValues];
        }
        AAdd(jResponse["data"], jItem)
    Next nValues
EndIf

oRest:setResponse(jResponse)
oRest:setStatusCode(200)
oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

FwFreeObj(jPath)
FwFreeObj(jResponse)
FwFreeObj(jItem)
FwFreeArray(aValues)

Return Nil

/*/{Protheus.doc} getWherePar
Metodo que retorna a consulta conforme os parâmetros
@type  Metodo
@author Bruno Costa
@return character
@since 17/03/2025
/*/
method getWherePar(jPar as json, cRefTab as character) as character class GPER670TReportsBusinessObject
    
Local cRet as character

If Len(jPar['PROC']) .And. !Empty(jPar['PROC', 1])
    cRet += " AND " + cRefTab + "_PROCES IN (?) "
EndIf

If Len(jPar['ROT']) .And. !Empty(jPar['ROT', 1])
    cRet += " AND " + cRefTab + "_ROTEIR IN (?) "
EndIf

cRet += " AND " + cRefTab + "_PERIODO = ? "

If !Empty(jPar['FILDE', 1])
    cRet += " AND SRA.RA_FILIAL >= ? "
EndIf

If !Empty(jPar['FILATE', 1])
    cRet += " AND SRA.RA_FILIAL <= ? "
EndIf

If !Empty(jPar['CCDE', 1])
    cRet += " AND SRA.RA_CC >= ? "
EndIf

If !Empty(jPar['CCATE', 1])
    cRet += " AND SRA.RA_CC <= ? "
EndIf

If !Empty(jPar['MATDE', 1])
    cRet += " AND SRA.RA_MAT >= ? "
EndIf

If !Empty(jPar['MATATE', 1])
    cRet += " AND SRA.RA_MAT <= ? "
EndIf

If !Empty(jPar['NOMEDE', 1])
    cRet += " AND SRA.RA_NOME >= ? "
EndIf

If !Empty(jPar['NOMEATE', 1])
    cRet += " AND SRA.RA_NOME <= ? "
EndIf

If Len(jPar['SITFOL']) > 0 
    cRet += " AND SRA.RA_SITFOLH IN (?)  "
EndIf 

If Len(jPar['CATFUNC']) > 0 .And. !Empty(jPar['CATFUNC', 1])
    cRet += " AND SRA.RA_CATFUNC IN (?)  "
EndIf 

If !Empty(jPar['TPCONT', 1]) .AND. jPar['TPCONT', 1] <> "4"
     cRet += " AND SRA.RA_TPCONTR = ? "
EndIf

Return cRet

/*/{Protheus.doc} getParams
Metodo que preenche os binds do statement
@type  Metodo
@author Bruno Costa
@return Numeric
@since 17/03/2025
/*/
method getParams( jParams as json, oStatement as object, nPOrder as numeric) as Numeric class GPER670TReportsBusinessObject
Local aCategoria as array
Local aSituacoes as array
Local aRoteiros as array
Local aProcesso as array
Local aParams as array
Local nSituacoes as numeric
Local nI as numeric

aParams := {"PROC", "ROT", "MESANO", "FILDE", "FILATE", "CCDE", "CCATE", "MATDE", "MATATE", "NOMEDE", "NOMEATE", "SITFOL", "CATFUNC", "TPCONT"}

For nI := 1 To Len(aParams)
    If Len(jParams[aParams[nI]]) > 0 .And. (!Empty(jParams[aParams[nI], 1]) .Or. aParams[nI] == "SITFOL")
        Do Case
            Case aParams[nI] == "PROC"
                aProcesso := IIf(jParams:hasProperty("PROC"), jParams["PROC"], {})
                oStatement:SetIn(nPOrder++, aProcesso)
            Case aParams[nI] == "ROT"
                aRoteiros := IIf(jParams:hasProperty("ROT"), jParams["ROT"], {})
                oStatement:SetIn(nPOrder++, aRoteiros)
            Case aParams[nI] == "MESANO"
                oStatement:SetString(nPOrder++, cAnoMes)
            Case aParams[nI] == "SITFOL"
                aSituacoes := IIf(jParams:hasProperty("SITFOL"), jParams["SITFOL"], {})
                For nSituacoes := 1 To Len(aSituacoes)
                    aSituacoes[nSituacoes] := IIf(aSituacoes[nSituacoes] == Nil .Or. Empty(aSituacoes[nSituacoes]), " ", aSituacoes[nSituacoes])
                Next nSituacoes
                oStatement:SetIn(nPOrder++, aSituacoes)
            Case aParams[nI] == "CATFUNC"
                aCategoria := IIf(jParams:hasProperty("CATFUNC"), jParams["CATFUNC"], {})
                oStatement:SetIn(nPOrder++, aCategoria)
            Case aParams[nI] == "TPCONT" 
                If jParams['TPCONT', 1] <> "4"
                    oStatement:SetString(nPOrder++, jParams["TPCONT", 1])
                EndIf    
            Otherwise
                oStatement:SetString(nPOrder++, jParams[aParams[nI], 1])
        EndCase
    EndIf
Next nI

Return nPOrder

/*/{Protheus.doc} getRot670
Metodo que retorna os roteiros para o lookup 
@type  Metodo
@author Bruno Costa
@return Variant, Retorno nulo pré-fixado
@since 17/03/2025
/*/
Method getRot670() As Variant Class GPER670TReportsBusinessObject
Local aRotCalc as array
Local jQParams := oRest:getQueryRequest() as json 
Local jResponse := JSONObject():New() as json
Local jItem as json
Local cQuery as character
Local cAlias as character
Local cSearch as character
Local nPage as numeric
Local nCount as numeric
Local nTotal as numeric
Local nPOrder as numeric
Local nPageSize as numeric
Local lOpeSoc as logical

Static oStatSRY
Static __cSearch := ""

nPage     := 1
nPageSize := 10
nCount    := 1
nPOrder   := 1
lOpeSoc   := cPaisLoc == "BRA" .And. SuperGetMv("MV_OPESOC", NIL, .F.)
aRotCalc  := {'1', '2', '6', '9', '7', '5', 'F'}
If(lOpeSoc, AAdd(aRotCalc, 'O'), Nil)

//Captura os query parameters da requisição
If (jQParams != NIL)
    nPage     := IIf(jQParams:hasProperty("page"),     Val(jQParams["page"]),     nPage)
    nPageSize := IIf(jQParams:hasProperty("pageSize"), Val(jQParams["pageSize"]), nPageSize)

    nPage     := IIf(nPage     <= 0, 1,   nPage)
    nPageSize := IIf(nPageSize <= 0, 10, nPageSize)

    If (jQParams:hasProperty("q"))
        cSearch := "(UPPER(RY_CALCULO) LIKE UPPER(?) OR UPPER(RY_DESC) LIKE UPPER(?)) AND "     
    EndIf
EndIf

jResponse["data"]             := {}
jResponse["nextPageUrl"]      := NIL
jResponse["remainingRecords"] := 0
jResponse["keyProperty"]      := "RY_CALCULO"
jResponse["descriptor"]       := {;
    "RY_CALCULO": FwHttpEncode(STR0052),;
    "RY_DESC":    FwHttpEncode(STR0053)} //"Roteiro" ### "Descrição do Roteiro"

If oStatSRY == Nil .Or. !(cSearch == __cSearch)
    cQuery    := "SELECT SRY.RY_CALCULO, SRY.RY_DESC FROM " + RetSQLName("SRY") + " SRY " + "WHERE " + cSearch + "SRY.RY_TIPO IN (?) AND SRY.D_E_L_E_T_ = ? GROUP BY RY_CALCULO, RY_DESC "
    cQuery    := ChangeQuery(cQuery)
    __cSearch := cSearch
    oStatSRY  := FwExecStatement():New(cQuery)
EndIf

If (jQParams:hasProperty("q"))
    oStatSRY:SetString(nPOrder++, "%" + jQParams["q"] + "%")
    oStatSRY:SetString(nPOrder++, "%" + jQParams["q"] + "%")
EndIf

oStatSRY:SetIn(nPOrder++, aRotCalc)
oStatSRY:SetString(nPOrder++, " ")

cAlias := oStatSRY:OpenAlias()

Count To nTotal
(cAlias)->(DBGoTop())

While !(cAlias)->(EoF()) .And. nCount <= nPageSize
    jItem := {;
        "RY_CALCULO": FwHttpEncode((cAlias)->RY_CALCULO),;
        "RY_DESC":    FwHttpEncode(RTrim((cAlias)->RY_DESC));
    }
    AAdd(jResponse["data"], jItem)

    (cAlias)->(DBSkip())
    nCount++
EndDo

//Define a quantidade de registros restantes e a URL da próxima página
jResponse["remainingRecords"] := IIf(nCount > nPageSize .And. !(cAlias)->(EoF()), nTotal - (nPage * nPageSize), 0)
jResponse["nextPageUrl"]      := IIf(nCount > nPageSize .And. !(cAlias)->(EoF()), oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQParams:hasProperty("q"), "&q=" + jQParams["q"], "") + IIf(jQParams:hasProperty("pageSize"), "&pageSize=" + CValToChar(nPageSize), ""), NIL)

oRest:setResponse(jResponse)
oRest:setStatusCode(200)
oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

(cAlias)->(DBCloseArea())

FwFreeObj(jQParams)
FwFreeObj(jResponse)
FwFreeObj(jItem)
FwFreeObj(oStatSRY)

Return NIL
