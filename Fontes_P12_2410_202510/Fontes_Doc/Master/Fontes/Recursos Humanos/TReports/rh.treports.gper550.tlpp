#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.treports.gper550.ch"


namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRH,SRA,SRR,SRV,SR6,CTT", customTables="SRA,SRH,SRR", name="Relatório de Valores de Ferias", country="ALL", initialRelease="12.1.2210")
class GPER550TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getWherePar() as character  
    private method getParams() as Variant

    @Get("/api/rh/smartview/v1/options/gpersv550/")
    Public Method getComboOrder() As Variant

endclass
 
method new() as object class GPER550TReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Férias Calculadas"
return self
 
method getDescription() as character class GPER550TReportsBusinessObject
return STR0003 //"Objeto contendo valores de férias calculadas"
 
method getAreas() as array class GPER550TReportsBusinessObject
return {STR0002}//"RH"
 
method getData(nPage as numeric, oFilter as object) as object class GPER550TReportsBusinessObject
    local cAlias as character 
    local cQuery as character  
    local cWhere as character
    local cOrdem as character
    local cTpPd as character
    local cLastFil as character
    local cFilProc as character
    local cFilName as character
    local cNameEmp as character
    local cCodEmp as character
    local cIdLqFer as character
    local cLastCC as character
    local cLastTur as character
    local lTotFil as logical
    local lTotCC as logical
    local lTotTur as logical
    local nProven as numeric
    local nDescon as numeric
    local nBases as numeric
    local nOrdem as numeric
    local nPosRec as numeric
    local jParams as json
    local cCustomFields as character // Campos customizados no formato SQL
    local jData as JSON // Informações a serem adicionadas no ON
    local aAllFields as array     // Estrutura de todos os campos do ON
    local aPDFields as array     // Campos que podem ser exibidos de acordo com a LGPD
    local lObfuscated as logical   // Flag que indica se tem algum campo para ofuscar

    Static oStatement
    Static __cEmpAnt   := cEmpAnt
    Static __cWhere    := ""

    /*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ?
    ? Variaveis de Acesso do Usuario                               ?
    ?ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ?*/
	Local cAcessaSRA := &( " { || " + ChkRH( "GPER550" , "SRA" , "2" ) + " } " )
    Local aOfusca		:= If(FindFunction('ChkOfusca'), ChkOfusca(), { .T., .F. }) //[2]Ofuscamento
    Local aFldRel		:= If(aOfusca[2], FwProtectedDataUtil():UsrNoAccessFieldsInList( {"RA_NOME"} ), {})

    Private lOfusca	 := Len(aFldRel) > 0

    jParams  := oFilter:getParameters() //metodo para retorno do json dos parametros
    lTotFil  := .F.
    lTotCC   := .F.
    lTotTur  := .F.
    cWhere   := ""
    cOrdem   := "SRA.RA_FILIAL , SRA.RA_MAT" //Ordenação 1: Matricula
    cFilName := ""
    cNameEmp := ""
    cNameCC  := ""
    cLastFil := "_cLastFil"
    cFilProc := "_cLastFil"
    cLastCC  := ""
    cLastTur := ""
    cCodEmp  := cEmpAnt
    cIdLqFer := "0102"

    cCustomFields   := ""
    jData           := NIL 
    aAllFields      := {}
    aPDFields       := {}
    lObfuscated     := .F.

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields       := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated     := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields      := self:getStructFields()
    
    cWhere := self:getWherePar(jParams)

    If !Empty(jParams['ORDER', 1])
		nOrdem := Val(jParams['ORDER', 1]) 
	EndIf

    If nOrdem == 2//Ordenação: Centro de Custo
		cOrdem := "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_MAT"
	ElseIf nOrdem == 3//Ordenação: Nome
		cOrdem := "SRA.RA_FILIAL , SRA.RA_NOME"
	ElseIf nOrdem == 4//Ordenação: CC+ Nome
		cOrdem := "SRA.RA_FILIAL , SRA.RA_CC , SRA.RA_NOME"
	ElseIf nOrdem == 5 //Ordenação: Turno
		cOrdem := "SRA.RA_FILIAL, SRA.RA_TNOTRAB, SRA.RA_MAT"
	EndIf    

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(__cWhere == cWhere)
    
        cQuery  := " SELECT SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_TNOTRAB, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_DEPIR, "
        cQuery  +=      "SRH.RH_DATABAS, SRH.RH_DBASEAT, SRH.RH_DATAINI, SRH.RH_DATAFIM, SRH.RH_DTRECIB, SRH.RH_PERC13S, SRH.RH_DFERIAS, SRH.RH_DABONPE, SRH.RH_SALMES, SRH.RH_SALDIA ,SRH.RH_SALHRS, SRH.RH_TIPCAL, "
        cQuery  +=      "SRR.RR_PD, SRV.RV_DESC, SRV.RV_TIPOCOD, SRV.RV_CODFOL, SRR.RR_HORAS, SRR.RR_VALOR"
        cQuery  += " ? "
        cQuery  += " FROM " + RetSqlName("SRH") + " SRH "    
        cQuery  +=      "INNER JOIN " + RetSqlName("SRA") + " SRA ON " + FWJoinFilial( "SRA", "SRH" ) + " AND SRA.RA_MAT = SRH.RH_MAT AND SRH.D_E_L_E_T_ = ' ' "
        cQuery  +=      "INNER JOIN " + RetSqlName("SRR") + " SRR ON " + FWJoinFilial( "SRR", "SRA" ) + " AND SRR.RR_MAT = SRH.RH_MAT AND SRR.RR_DATA = SRH.RH_DATAINI AND SRR.D_E_L_E_T_ = ' ' "
        cQuery  +=      "INNER JOIN " + RetSqlName("SRV") + " SRV ON " + FWJoinFilial( "SRV", "SRR" ) + " AND SRV.RV_COD = SRR.RR_PD AND SRV.D_E_L_E_T_ = ' ' "
        cQuery  +=      "WHERE SRH.D_E_L_E_T_ = ? "

        If !Empty(cWhere)
            cQuery += cWhere    
        EndIf

        __cEmpAnt  := cEmpAnt
        __cWhere   := cWhere
        cQuery     := ChangeQuery(cQuery) 
        oStatement := FwExecStatement():New(cQuery)

    EndIf

    // Captura e define os campos personalizados das tabelas SRA, SRH e SRR
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRH", "SRR"})
    oStatement:SetUnsafe(1, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados

    oStatement:SetString(2, ' ') //SRH.D_E_L_E_T_
    
    //Preenche os bind da query SRD
    self:getParams(jParams, oStatement)

    cAlias := oStatement:OpenAlias()
    dbSelectArea( cAlias )

    While !(cAlias)->(Eof())

        //Consiste Filiais e Acessos
        If cLastFil != (cAlias)->RA_FILIAL
            cFilName := RTrim( FWEmpName(cCodEmp)) + " / " + RTrim( FWFilRazSocial(, (cAlias)->RA_FILIAL) )
            cNameEmp := RTrim( FWEmpName(cCodEmp))
            If !((cAlias)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA) 
                (cAlias)->( dbSkip() )
                Loop
            EndIf
        EndIf

        lTotFil := .F.
        lTotCC  := .F.
        lTotTur := .F.

        cLastFil := (cAlias)->RA_FILIAL 
        cLastCC  := (cAlias)->RA_CC
        cLastTur := (cAlias)->RA_TNOTRAB

        nPosRec   := (cAlias)->( Recno() )

        (cAlias)->( dbSkip() )
        lTotFil := IF((cAlias)->RA_FILIAL != cLastFil, .T., .F. )
        If nOrdem == 2 .OR. nOrdem == 4
            lTotCC := IF((cAlias)->RA_CC != cLastCC, .T., IF((cAlias)->RA_FILIAL != cLastFil, .T., .F.))
        EndIf
        If nOrdem == 5
            lTotTur := IIf((cAlias)->RA_TNOTRAB != cLastTur, .T., IF((cAlias)->RA_FILIAL != cLastFil, .T., .F.))
        EndIf
        (cAlias)->( dbGoTop() )

        If nPosRec > 1
            (cAlias)->( dbSkip(nPosRec-1) )
        EndIf
      
        cTpPd   := ""
        nProven := nDescon := nBases := 0

        cTpPd   := If (cIdLqFer != (cAlias)->RV_CODFOL, (cAlias)->RV_TIPOCOD, "0") 
        nProven := If (cTpPd == "1", (cAlias)->RR_VALOR,0) //Proventos
	    nDescon := If (cTpPd == "2",(cAlias)->RR_VALOR,0)  //Descontos
	    nBases  := If (cTpPd $ "3/4",(cAlias)->RR_VALOR,0) //Bases  
        
        jData := {;
                "FILNAME": cFilName, ;
                "EMPNAME": cNameEmp, ;
                "CODEMP": cCodEmp, ;
                "RA_FILIAL": (cAlias)->RA_FILIAL,;
                "RA_CC": RTrim( (cAlias)->RA_CC ),;
                "CCDESC" : RTrim( FDesc("CTT", (cAlias)->RA_CC, "CTT->CTT_DESC01") ),;
                "RA_TNOTRAB": RTrim( (cAlias)->RA_TNOTRAB ),;
                "TNODESC" : RTrim( FDesc("SR6", (cAlias)->RA_TNOTRAB, "SR6->R6_DESC") ),;
                "RA_MAT": (cAlias)->RA_MAT,;
                "RA_NOME": If(lOfusca, Replicate('*',30), RTrim( (cAlias)->RA_NOME )),;             
                "RA_DEPIR": RTrim( (cAlias)->RA_DEPIR ),;
                "DATABAS": FwTimeStamp( 5, sToD((cAlias)->RH_DATABAS), "00:00:00" ), ;
                "DBASEAT": FwTimeStamp( 5, sToD((cAlias)->RH_DBASEAT), "00:00:00" ), ;
                "DATAINI": FwTimeStamp( 5, sToD((cAlias)->RH_DATAINI), "00:00:00" ), ;
                "DATAFIM": FwTimeStamp( 5, sToD((cAlias)->RH_DATAFIM), "00:00:00" ), ;
                "DTRECIB": FwTimeStamp( 5, sToD((cAlias)->RH_DTRECIB), "00:00:00" ), ;
                "RH_PERC13S": (cAlias)->RH_PERC13S,;
                "RH_DFERIAS": (cAlias)->RH_DFERIAS,;
                "RH_DABONPE": (cAlias)->RH_DABONPE,;
                "RH_SALMES": (cAlias)->RH_SALMES,;
                "RH_SALDIA": (cAlias)->RH_SALDIA,;
                "RH_SALHRS": (cAlias)->RH_SALHRS,;
                "RH_TIPCAL": (cAlias)->RH_TIPCAL,;
                "RR_PD": (cAlias)->RR_PD,;
                "RVDESC": RTrim((cAlias)->RV_DESC),;
                "TPPD": cTpPd,;
                "REF": (cAlias)->RR_HORAS,;
                "RR_VALOR": (cAlias)->RR_VALOR,;
                "PROVENTOS": nProven,;
                "DESCONTOS": nDescon,;
                "BASES": nBases,;
                "TOTFIL": If(lTotFil, .T., .F.),;
                "TOTCC": If(lTotCC, .T., .F.),;
                "TOTTURN": If(lTotTur, .T., .F.)}

        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)

        (cAlias)->( dbSkip() ) 
    EndDo

    (cAlias)->( DBCloseArea() )

    // Libera os objetos da memória
    jData := NIL
    FwFreeObj(jData) 

return self:oData
 
method getSchema() as object class GPER550TReportsBusinessObject
    local aFieldsSRA as array
    local aFieldsSRH as array
    local aFieldsSRR as array

    aFieldsSRA := { "RA_FILIAL", "RA_CC", "RA_TNOTRAB", "RA_MAT", "RA_NOME", "RA_DEPIR"}
    aFieldsSRH := { "RH_PERC13S","RH_DFERIAS","RH_DABONPE","RH_SALMES","RH_SALDIA","RH_SALHRS","RH_TIPCAL"}
    aFieldsSRR := { "RR_PD","RR_VALOR"}

    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("SRH", aFieldsSRH)
    self:oSchema:aliasToSchema("SRR", aFieldsSRR)

    self:oSchema:addProperty("FILNAME", "Nome do Grupo/Filial", "string", "D. Grupo/Filial", "FILNAME",,,, .F.) 
    self:oSchema:addProperty("EMPNAME", "Nome Empresa", "string", "Nome Empresa", "NAMEEMP",,,, .F.)
    self:oSchema:addProperty("CODEMP", "Cod Empresa", "string", "Cod. Empresa", "CODEMP",,,, .F.) 
    self:oSchema:addProperty("CCDESC", "Descrição do Centro de Custo", "string", "Desc. CC.", "CCDESC",,,, .F.) 
    self:oSchema:addProperty("TNODESC", "Descrição do turno", "string", "Desc. Turno", "TNODESC",,,, .F.) 
    self:oSchema:addProperty("DATABAS", "Data base férias inicio", "date", "Dt. Base Ini.", "DATABAS",,,, .F.) 
    self:oSchema:addProperty("DBASEAT", "Data base férias fim", "date", "Dt. Base Fim", "DBASEAT",,,, .F.) 
    self:oSchema:addProperty("DATAINI", "Inicio férias", "date", "Início Fer.", "DATAINI",,,, .F.) 
    self:oSchema:addProperty("DATAFIM", "Fim férias", "date", "Fim Fer.", "DATAFIM",,,, .F.) 
    self:oSchema:addProperty("DTRECIB", "Data recebimento férias", "date", "Dt. Recebto.", "DTRECIB",,,, .F.) 
    self:oSchema:addProperty("TPPD", "Tipo da verba", "string", "Tp. Verba", "TPPD",,,, .F.) 
    self:oSchema:addProperty("REF", "Referência horas/dias", "number", "Ref.", "TPPD",,,, .F.) 
    self:oSchema:addProperty("PROVENTOS", "Valor dos proventos", "number", "Proventos", "PROVENTOS",,,, .F.) 
    self:oSchema:addProperty("DESCONTOS", "Valor dos descontos", "number", "Descontos", "DESCONTOS",,,, .F.) 
    self:oSchema:addProperty("BASES", "Valor das bases", "number", "Bases", "BASES",,,, .F.) 
    self:oSchema:addProperty("RVDESC", "Descrição da verba", "string", "Desc. Verba", "RVDESC",,,, .F.)
    self:oSchema:addProperty("TOTFIL", "Tot. Filial", "boolean", "Tot. Filial", "TOTFIL",,,, .F.) 
    self:oSchema:addProperty("TOTCC", "Tot. CC", "boolean", "Tot. CC", "TOTCC",,,, .F.)
    self:oSchema:addProperty("TOTTURN", "Tot. Turno", "boolean", "Tot. Turno", "TOTTURN",,,, .F.)

    self:addParameter("FILDE","Filial De ?","string", .F.) 
    self:addParameter("FILATE","Filial Até ?","string", .F.) 
    self:addParameter("CCDE","Centro de Custo De ?","string", .F.) 
    self:addParameter("CCATE","Centro de Custo Até ?","string", .F.) 
    self:addParameter("TURNO","Turno ?","string", .F.) 
    self:addParameter("MATDE","Matricula De ?","string", .F.) 
    self:addParameter("MATATE","Matricula Até ?","string", .F.) 
    self:addParameter("NOME","Nome ?","string", .F.) 
    self:addParameter("FERDE","Período de Férias De?","date", .F.)
    self:addParameter("FERATE","Período de Férias Até?","date", .F.)
    self:addParameter("PAGDE","Período de Pagamento De?","date", .F.) 
    self:addParameter("PAGATE","Período de Pagamento Ate?","date", .F.) 
    self:addParameter("SITFOL","Situações a Imprimir ?","string", .T.) 
    self:addParameter("CATFUNC","Categorias a Imprimir ?","string", .T.) 
    self:addParameter("TPFER","Tipo de Férias ?","string", .F.) 
    self:addParameter("ORDER", "Ordem de Impressão ?", "string", .F.) 
    self:addParameter("TPREL", "Tipo de Relatório ?", "string", .F.) 

    //setCustomURL disponível na LIB 20231121. 
    //Com a atualização no protheus, tanto o design quanto a visão de dados, estão mostrando os mesmos parâmetros não sendo possível criar o combo dentro do design
    If FWLibVersion() >= "20231121" 
        //Combos
        self:setCustomURL("TPREL", "/api/framework/treports/integratedprovider/v1/options/GPR550/MV_PAR15", 1)
        self:setCustomURL("TPFER", "/api/framework/treports/integratedprovider/v1/options/GPR550/MV_PAR20", 1)
        self:setCustomURL("ORDER", "/api/rh/smartview/v1/options/gpersv550/", 1)

        //Loockups
        self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("TURNO", "api/framework/v1/genericLookupService/smartview/SR6", 2)
        self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("SITFOL", "api/framework/v1/genericLookupService/smartview/31", 2)
        self:setCustomURL("CATFUNC", "api/framework/v1/genericLookupService/smartview/28", 2)

    EndIf    

return self:oSchema



/*/{Protheus.doc} getComboOrder
Metodo que retorna o combobox dos valores de ordem para o parâmetro ORDER
@type  Metodo
@author Bruno Costa
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboOrder() as Variant Class GPER550TReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    local aValues   as Array   

    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    jResponse["data"] := {}

    // Monta o vetor de valores do parâmetro
    AAdd(aValues, EncodeUTF8("Matrícula"))
    AAdd(aValues, EncodeUTF8("Centro de Custo"))
    AAdd(aValues, EncodeUTF8("Nome"))
    AAdd(aValues, EncodeUTF8("C.Custo + Nome"))
    AAdd(aValues, EncodeUTF8("Turno"))

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL

/*{Protheus.doc} getWherePar
Metodo que retorna a consulta conforme os parâmetros
@type  Metodo
@author Leandro Drumond
@return character
@since 31/10/2024
*/
method getWherePar( jParams as json) as character class GPER550TReportsBusinessObject
    
    Local cRet as character

    If !Empty(jParams['FILDE', 1])
		cRet += " AND SRA.RA_FILIAL >= ?"
	EndIf
    
    If !Empty(jParams['FILATE', 1])
		cRet += " AND SRA.RA_FILIAL <= ?"
	EndIf

	If !Empty(jParams['CCDE', 1])
		cRet += " AND SRA.RA_CC >= ?"
	EndIf
    
	If !Empty(jParams['CCATE', 1])
		cRet += " AND SRA.RA_CC <= ?"
	EndIf

    If !Empty(jParams['TURNO', 1])
		cRet += " AND SRA.RA_TNOTRAB = ?"
	EndIf

    If !Empty(jParams['MATDE', 1])
		cRet += " AND SRA.RA_MAT >= ?"
	EndIf

    If !Empty(jParams['MATATE', 1])
		cRet += " AND SRA.RA_MAT <= ?"
	EndIf

    If !Empty(jParams['NOME', 1])
		cRet += " AND SRA.RA_NOME >= ?"
	EndIf

    If !Empty(jParams['FERDE', 1])
		cRet += " AND SRH.RH_DATAINI >= ?"
	EndIf

    If !Empty(jParams['FERATE', 1])
		cRet += " AND SRH.RH_DATAFIM <= ?"
	EndIf

    If !Empty(jParams['PAGDE', 1])
		cRet += " AND SRH.RH_DTRECIB >= ?" 
	EndIf
    
    If !Empty(jParams['PAGATE', 1])
		cRet += " AND SRH.RH_DTRECIB <= ?" 
	EndIf
    
    If !Empty(jParams['SITFOL'])
        cRet += " AND SRA.RA_SITFOLH IN (?)"
    EndIf 

    If !Empty(jParams['CATFUNC'])
        cRet += " AND SRA.RA_CATFUNC IN (?)"
    EndIf 

    If !Empty(jParams['TPFER']) 
        cRet += " AND SRH.RH_TIPCAL IN (?)" 
    EndIf 

Return cRet

/*/{Protheus.doc} getParams
Metodo que preenche os binds do statement
@type  Metodo
@author Leandro Drumond
@return Numeric
@since 23/10/2024
/*/
method getParams( jParams as json, oStatement as object) as Variant class GPER550TReportsBusinessObject

    local aCategoria 
    local aSituacoes
    local aTipCalFer
    local nSituacoes
    local nPOrder       := 3
    
    If !Empty(jParams['FILDE', 1])
        oStatement:SetString(nPOrder++, jParams['FILDE',1])
	EndIf
    
    If !Empty(jParams['FILATE', 1])
		oStatement:SetString(nPOrder++, jParams['FILATE',1])
	EndIf

	If !Empty(jParams['CCDE', 1])
		oStatement:SetString(nPOrder++, jParams['CCDE',1])
	EndIf
    
	If !Empty(jParams['CCATE', 1])
		oStatement:SetString(nPOrder++, jParams['CCATE',1])
	EndIf

    If !Empty(jParams['TURNO', 1])
		oStatement:SetString(nPOrder++, jParams['TURNO',1])
	EndIf

    If !Empty(jParams['MATDE', 1])
		oStatement:SetString(nPOrder++, jParams['MATDE',1])
	EndIf

    If !Empty(jParams['MATATE', 1])
		oStatement:SetString(nPOrder++, jParams['MATATE',1])
	EndIf

    If !Empty(jParams['NOME', 1])
		oStatement:SetString(nPOrder++, jParams['NOME',1])
	EndIf

    If !Empty(jParams['FERDE', 1])
        oStatement:setDate(nPOrder++, SToD(StrTran(SubStr(jParams['FERDE', 1], 1, 10), "-" )))
	EndIf

    If !Empty(jParams['FERATE', 1])
		oStatement:setDate(nPOrder++, SToD(StrTran(SubStr(jParams['FERATE', 1], 1, 10), "-" )))
	EndIf

    If !Empty(jParams['PAGDE', 1])
		oStatement:setDate(nPOrder++, SToD(StrTran(SubStr(jParams['PAGDE', 1], 1, 10), "-" )))
	EndIf
    
    If !Empty(jParams['PAGATE', 1])
		oStatement:setDate(nPOrder++, SToD(StrTran(SubStr(jParams['PAGATE', 1], 1, 10), "-" )))
	EndIf

    If !Empty(jParams['SITFOL'])
        aSituacoes    := IIf(jParams:hasProperty("SITFOL"),  jParams["SITFOL"],  {})
        For nSituacoes := 1 To Len(aSituacoes)
            aSituacoes[nSituacoes] := IIf(aSituacoes[nSituacoes] == NIL .Or. Empty(aSituacoes[nSituacoes]), " ", aSituacoes[nSituacoes])
        Next nSituacoes

        oStatement:SetIn(nPOrder++, aSituacoes)
    EndIf

    If !Empty(jParams['CATFUNC'])
        aCategoria    := IIf(jParams:hasProperty("CATFUNC"), jParams["CATFUNC"], {})
        oStatement:SetIn(nPOrder++, aCategoria)
    EndIf
    
    If !Empty(jParams['TPFER']) 
        aTipCalFer := IF( jParams['TPFER'][1] == "1" , {'N','P'}, IF( jParams['TPFER'][1] == "2" , {'C'} , {'N','C','P'}))
        oStatement:SetIn(nPOrder++, aTipCalFer)
    EndIf    

Return Nil
