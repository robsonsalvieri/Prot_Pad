#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper076.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER076TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório de Dependentes (GPER076)
    @type Class
    @version 12.1.2310
    @author karina.alves
    @since 12/03/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SRB, C9V", customTables="ALL", name="Relatório de Dependentes", country="ALL", initialRelease="12.1.2310")
Class GPER076TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} GPER076TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author karina.alves
    @since 12/03/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER076TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Visão de Dependentes"
Return self

/*/{Protheus.doc} GPER076TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author karina.alves
    @since 12/03/2024
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER076TReportsBusinessObject
Return STR0003 // "Este objeto de negócio traz as informações da Visão de Dependentes."

/*/{Protheus.doc} GPER076TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author karina.alves
    @since 12/03/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER076TReportsBusinessObject
    // Declaração das variáveis locais    
    Local cQuery        As Character // Query do relatório
    Local cAlias        As Character // Alias do arquivo temporário
    Local cFilDe        As Character // Parâmetro Filial De   
    Local cFilAte       As Character // Parâmetro Filial Ate  
    Local cMatDe        As Character // Parâmetro Matrícula De
    Local cMatAte       As Character // Parâmetro Matrícula Até
    Local cNomeDe       As Character // Parâmetro Nome De
    Local cNomeAte      As Character // Parâmetro Nome Até
    Local cDeptoDe      As Character // Parâmetro Depto. De
    Local cDeptoAte     As Character // Parâmetro Depto. Até
    Local cCustomFields As Character // Campos customizados no formato SQL
    Local cName         As Character // Nome real do campo
    Local cFiltro       As Character // Filtro do objeto de negócio
    Local JParams       As JSON      // Parâmetros do objeto de negócio
    Local jData         As JSON      // Informações a serem adicionadas no ON
    Local jLGPD         As JSON      // Indica quais campos devem ser ofuscados
    Local aCategorias   As Array     // Categorias
    Local aSituacoes    As Array     // Situações     
	Local aPDFields     As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields    As Array     // Estrutura de todos os campos do ON
    Local aPDFieldsNR   As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR     As Array     // Campos com nome diferente do real
    Local oStatement    As Object    // Objeto usada para utilização da classe FwExecStatement   
    Local nParamOrder   As Numeric   // Contador de parâmetros da query
    Local nAux          As Numeric   // Contador auxiliar
    Local nFields       As Numeric   // Contador de campos
    Local lExiste       As Logical   // Indica se existe Dependente no TAF
    Local lObfuscated   As Logical   // Flag que indica se tem algum campo para ofuscar

    // Inicialização das variáveis locais
    cQuery        := ""
    cAlias        := ""
    cFilDe        := ""
    cFilAte       := ""
    cMatDe        := ""
    cMatAte       := ""
    cNomeDe       := ""
    cNomeAte      := ""
    cDeptoDe      := ""
    cDeptoAte     := ""
    cCustomFields := ""
    cName         := ""
    cFiltro       := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    jParams       := oFilter:getParameters()
    jData         := NIL
    jLGPD         := JSONObject():New()
    aCategorias   := {}
    aSituacoes    := {}   
    aPDFields     := {}
    aAllFields    := {}
    aPDFieldsNR   := {}
    aFieldsNR     := {}
    oStatement    := NIL
    nParamOrder   := 1
    nAux          := 1
    nFields       := 1
    lExiste       := .F.
    lObfuscated   := .F.

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RA_FILIAL", "RA_MAT", "RA_NOME", "RB_COD", "RB_NOME", "RA_CIC", "RB_CIC", "RB_SEXO", "RB_GRAUPAR", "RB_TIPIR", "RB_TIPSF", "RB_PLSAUDE", "RB_INCT"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    BEGIN SEQUENCE
        // Captura o valor dos parâmetros
        If (jParams != NIL)
            cFilDe      := IIf(jParams:hasProperty("PAR_FILIALDE")    .and. Len(jParams["PAR_FILIALDE"])    > 0 .and.!Empty(jParams["PAR_FILIALDE"][1]),    jParams["PAR_FILIALDE"][1], "")
            cFilAte     := IIf(jParams:hasProperty("PAR_FILIALATE")   .and. Len(jParams["PAR_FILIALATE"])   > 0 .and.!Empty(jParams["PAR_FILIALATE"][1]),   jParams["PAR_FILIALATE"][1], Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
            cMatDe      := IIf(jParams:hasProperty("PAR_MATDE")    .and. Len(jParams["PAR_MATDE"])    > 0 .and.!Empty(jParams["PAR_MATDE"][1]),    jParams["PAR_MATDE"][1],    "")
            cMatAte     := IIf(jParams:hasProperty("PAR_MATATE")   .and. Len(jParams["PAR_MATATE"])   > 0 .and.!Empty(jParams["PAR_MATATE"][1]),   jParams["PAR_MATATE"][1],   Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))           
            cNomeDe     := IIf(jParams:hasProperty("PAR_NOMEDE")   .and. Len(jParams["PAR_NOMEDE"])   > 0 .and.!Empty(jParams["PAR_NOMEDE"][1]),   jParams["PAR_NOMEDE"][1],   "")
            cNomeAte    := IIf(jParams:hasProperty("PAR_NOMEATE")  .and. Len(jParams["PAR_NOMEATE"])  > 0 .and.!Empty(jParams["PAR_NOMEATE"][1]),  jParams["PAR_NOMEATE"][1],  Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
            cDeptoDe    := IIf(jParams:hasProperty("PAR_DEPDE")    .and. Len(jParams["PAR_DEPDE"])   > 0 .and.!Empty(jParams["PAR_DEPDE"][1]),    jParams["PAR_DEPDE"][1],    "")
            cDeptoAte   := IIf(jParams:hasProperty("PAR_DEPATE")   .and. Len(jParams["PAR_DEPATE"])  > 0 .and.!Empty(jParams["PAR_DEPATE"][1]),   jParams["PAR_DEPATE"][1],   Replicate("Z", GetSX3Cache("RA_DEPTO", "X3_TAMANHO")))
            aSituacoes  := IIf(jParams:hasProperty("PAR_SIT"), jParams["PAR_SIT"], {})
            aCategorias := IIf(jParams:hasProperty("PAR_CAT"), jParams["PAR_CAT"], {})
        EndIf               

        // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aSituacoes)
            aSituacoes[nAux] := IIf(aSituacoes[nAux] == NIL .or. Empty(aSituacoes[nAux]), " ", aSituacoes[nAux])
        Next nAux


        // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aCategorias)
            aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
        Next nAux         

        // Montagem da query de busca
        cQuery += "SELECT "
        cQuery +=     "RA_CIC, "
        cQuery +=     "RA_MAT, "
        cQuery +=     "RA_FILIAL, "
        cQuery +=     "RA_NOME, "
        cQuery +=     "RB_COD, "
        cQuery +=     "RB_NOME, "
        cQuery +=     "RB_CIC, "
        cQuery +=     "RB_DTNASC, "
        cQuery +=     "RB_SEXO, "
        cQuery +=     "RB_GRAUPAR, "
        cQuery +=     "RB_TIPIR, "
        cQuery +=     "RB_TIPSF, "
        cQuery +=     "RB_PLSAUDE, "
        cQuery +=     "RB_INCT, "
        cQuery +=     "C9V_ID, "
        cQuery +=     "C9V_VERSAO "
        cQuery +=     "? " // 1
        cQuery += "FROM "
        cQuery +=     RetSQLName("SRA") + " SRA "
        cQuery +=     "INNER JOIN "
        cQuery +=         RetSQLName("SRB") + " SRB "
        cQuery +=         "ON " 
        cQuery +=             "SRB.RB_FILIAL = SRA.RA_FILIAL AND "
        cQuery +=             "SRB.RB_MAT = SRA.RA_MAT AND "
        cQuery +=             "SRB.D_E_L_E_T_ = ? " // 2
        cQuery +=     "LEFT JOIN "
        cQuery +=         RetSQLName("C9V") + " C9V "
        cQuery +=         "ON " 
        cQuery +=             "C9V.C9V_FILIAL = SRA.RA_FILIAL AND "
        cQuery +=             "C9V.C9V_CPF = SRA.RA_CIC AND "
        cQuery +=             "C9V.D_E_L_E_T_ = ? " // 3
        cQuery += "WHERE "
        cQuery +=     "RA_FILIAL BETWEEN ? AND ? "                              // 4 e 5
        cQuery +=     "AND RA_DEPTO BETWEEN ? AND ? "                           // 6 e 7 
        cQuery +=     "AND RA_MAT BETWEEN ? AND ? "                             // 8 e 9
        cQuery +=     "AND RA_NOME BETWEEN ? AND ? "                            // 10 e 11
        cQuery +=     IIf(Len(aSituacoes) > 0, " AND RA_SITFOLH IN (?) ", " ")  // 12
        cQuery +=     IIf(Len(aCategorias) > 0, " AND RA_CATFUNC IN (?) ", " ") // 13
        cQuery +=     "AND SRA.D_E_L_E_T_ = ? "                                 // 14
        cQuery +=     "? "                                                      // 15
        cQuery := ChangeQuery(cQuery)
    
        // Instancia a classe
        oStatement := FwExecStatement():New(cQuery)

        // Captura e define os campos personalizados das tabelas SRA, SRB, C9V
        cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRB", "C9V"})
        oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

        // Define o valor dos bind parameters
        oStatement:SetString(nParamOrder++, " ")       // 2
        oStatement:SetString(nParamOrder++, " ")       // 3
        oStatement:SetString(nParamOrder++, cFilDe)    // 4
        oStatement:SetString(nParamOrder++, cFilAte)   // 5
        oStatement:SetString(nParamOrder++, cDeptoDe)  // 6
        oStatement:SetString(nParamOrder++, cDeptoAte) // 7
        oStatement:SetString(nParamOrder++, cMatDe)    // 8
        oStatement:SetString(nParamOrder++, cMatAte)   // 9
        oStatement:SetString(nParamOrder++, cNomeDe)   // 10
        oStatement:SetString(nParamOrder++, cNomeAte)  // 11

        If (Len(aSituacoes) > 0)
            oStatement:SetIn(nParamOrder++, aSituacoes) // 12
        EndIf     

        If (Len(aCategorias) > 0)
            oStatement:SetIn(nParamOrder++, aCategorias) // 13
        EndIf

        oStatement:SetString(nParamOrder++, " ")     // 14
        oStatement:SetUnsafe(nParamOrder++, cFiltro) // 15

        // Executa a query e retorna o alias criado
        cAlias := oStatement:OpenAlias()

        While (!(cAlias)->(EOF()))    
            If (!Empty((cAlias)->RB_CIC))
                lExiste:= ExistDepenTAF((cAlias)->RB_CIC, (cAlias)->RA_FILIAL, (cAlias)->C9V_ID, (cAlias)->C9V_VERSAO)
            Else
                lExiste := .F.          
            EndIf  

            // Adiciona as informações da linha
            jData := {;
                "OriginPlatform": "Protheus",;                                                                                                                                                                                                                                                                                                                                                                          // Plataforma Origem
                "NOMEFILIAL":     Trim(IIf(jLGPD["RA_FILIAL"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),  (cAlias)->RA_FILIAL)),;                                                                                                                                                                                                                                                           // Filial
                "RAMATFUNC":      Trim(IIf(jLGPD["RA_MAT"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),     (cAlias)->RA_MAT)),;                                                                                                                                                                                                                                                              // Matrícula
                "RANOME":         Trim(IIf(jLGPD["RA_NOME"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),    (cAlias)->RA_NOME)),;                                                                                                                                                                                                                                                             // Nome
                "RBCOD":          Trim(IIf(jLGPD["RB_COD"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_COD),     (cAlias)->RB_COD)),;                                                                                                                                                                                                                                                              // Código
                "RBNOME":         Trim(IIf(jLGPD["RB_NOME"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_NOME),    (cAlias)->RB_NOME)),;                                                                                                                                                                                                                                                             // Nome Dependente
                "RACIC":          Trim(IIf(jLGPD["RA_CIC"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CIC),     IIf(!Empty((cAlias)->RA_CIC), Transform((cAlias)->RA_CIC,  "@R 999.999.999-99"), ''))),;                                                                                                                                                                                          // CPF Funcionário
                "RBCIC":          Trim(IIf(jLGPD["RB_CIC"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_CIC),     IIf(!Empty((cAlias)->RB_CIC), Transform((cAlias)->RB_CIC,  "@R 999.999.999-99"), ''))),;                                                                                                                                                                                          // CPF
                "RBSEXO":         Trim(IIf(jLGPD["RB_SEXO"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_SEXO),    Iif((cAlias)->RB_SEXO    == 'F', STR0006,            Iif((cAlias)->RB_SEXO    == 'M', STR0007,'')))),;                                                                                                                                                                            // Sexo
                "RBGRAUPAR":      Trim(IIf(jLGPD["RB_GRAUPAR"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_GRAUPAR), Iif((cAlias)->RB_GRAUPAR == 'C', STR0008, Iif((cAlias)->RB_GRAUPAR == 'F', STR0009,       Iif((cAlias)->RB_GRAUPAR == 'E', STR0010,          Iif((cAlias)->RB_GRAUPAR == 'P', STR0011,          Iif((cAlias)->RB_GRAUPAR == 'O', STR0012, ''))))))),;                             // Grau Parentesco
                "RBTIPIR":        Trim(IIf(jLGPD["RB_TIPIR"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_TIPIR),   Iif((cAlias)->RB_TIPIR   == '1', STR0013,        Iif((cAlias)->RB_TIPIR   == '2', STR0014, Iif((cAlias)->RB_TIPIR   == '3', STR0015,      Iif((cAlias)->RB_TIPIR   == '4', STR0016, '')))))),;                                                                                    // Tipo IR
                "RBTIPSF":        Trim(IIf(jLGPD["RB_TIPSF"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_TIPSF),   Iif((cAlias)->RB_TIPSF   == '1', STR0013,        Iif((cAlias)->RB_TIPSF   == '2', STR0017, Iif((cAlias)->RB_TIPSF   == '3', STR0016, ''))))),;                                                                                                                                    // Tipo SF
                "RBPLSAUDE":      Trim(IIf(jLGPD["RB_PLSAUDE"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_PLSAUDE), Iif((cAlias)->RB_PLSAUDE == '1', STR0004,                 Iif((cAlias)->RB_PLSAUDE == '2', STR0005,'')))),;                                                                                                                                                                       // Plano Saúde
                "RBINCT":         Trim(IIf(jLGPD["RB_INCT"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_INCT),    Iif((cAlias)->RB_INCT    == '1', STR0005,                 Iif((cAlias)->RB_INCT    == '2', STR0004,'')))),;                                                                                                                                                                       // Incapac
                "RBDTNASC":       IIf(!Empty((cAlias)->RB_DTNASC), SubStr(FwTimeStamp(2,SToD((cAlias)->RB_DTNASC)), 1, 10), ""),;                                                                                                                                                                                                                                                                                       // Data de Nascimento
                "INTTAF":         Iif(lExiste, STR0004, STR0005);                                                                                                                                                                                                                                                                                                                                                       // Integra TAF
            }

            // Adiciona os campos customizados no JSON
            GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

            // Adiciona as informações da linha
            self:oData:appendData(jData)

            // Pula para o próximo registro
            (cAlias)->(DBSkip())
        End      
    END SEQUENCE

    // Fecha a área da tabela temporária
    If !Empty(cAlias)
        (cAlias)->(DBCloseArea())
    Endif

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)
    FwFreeArray(aAllFields)
    FwFreeArray(aPDFields)
Return self:oData

/*/{Protheus.doc} GPER076TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author karina.alves
    @since 01/02/2024
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER076TReportsBusinessObject
    // Define os parâmetros do relatório 
    self:oSchema:addParameter("PAR_FILIALDE",    STR0018,    "string", .F.) // "Filial De"
    self:oSchema:addParameter("PAR_FILIALATE",   STR0019,    "string", .F.) // "Filial Até"
    self:oSchema:addParameter("PAR_MATDE",       STR0020,    "string", .F.) // "Matrícula De"
    self:oSchema:addParameter("PAR_MATATE",      STR0021,    "string", .F.) // "Matrícula Até"
    self:oSchema:addParameter("PAR_NOMEDE",      STR0022,    "string", .F.) // "Nome Funcionário De"
    self:oSchema:addParameter("PAR_NOMEATE",     STR0023,    "string", .F.) // "Nome Funcionário Até"
    self:oSchema:addParameter("PAR_SIT",         STR0024,    "string", .T.) // "Situações"
    self:oSchema:addParameter("PAR_CAT",         STR0025,    "string", .T.) // "Categorias"
    self:oSchema:addParameter("PAR_DEPDE",       STR0026,    "string", .F.) // "Depto. De"
    self:oSchema:addParameter("PAR_DEPATE",      STR0027,    "string", .F.) // "Depto. Até"

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_FILIALDE",      "/api/rh/smartview/v1/options/GPEParams/getBranches",                                     2)
    self:setCustomURL("PAR_FILIALATE",     "/api/rh/smartview/v1/options/GPEParams/getBranches",                                     2)
    self:setCustomURL("PAR_MATDE",         "/api/framework/v1/genericLookupService/smartview/SRA",                                   2)
    self:setCustomURL("PAR_MATATE",        "/api/framework/v1/genericLookupService/smartview/SRA",                                   2)
    self:setCustomURL("PAR_SIT",           "/api/rh/smartview/v1/options/GPEParams/getSX5/31",                                       2)
    self:setCustomURL("PAR_CAT",           "/api/rh/smartview/v1/options/GPEParams/getSX5/28",                                       2)
    self:setCustomURL("PAR_DEPDE",         "/api/framework/v1/genericLookupService/smartview/SQB",                                   2)
    self:setCustomURL("PAR_DEPATE",        "/api/framework/v1/genericLookupService/smartview/SQB",                                   2)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("NOMEFILIAL",       STR0028,      "string",    STR0028,     "NOMEFILIAL",             NIL, NIL, NIL, .F.) //  "Filial"
    self:oSchema:addProperty("RAMATFUNC",        STR0029,      "string",    STR0029,     "RAMATFUNC",              NIL, NIL, NIL, .F.) //  "Matrícula"
    self:oSchema:addProperty("RACIC",            STR0030,       "string",   STR0030,     "RAMATFUNC",              NIL, NIL, NIL, .F.) //  "CPF Funcionário"
    self:oSchema:addProperty("RANOME",           STR0031,      "string",    STR0031,     "RANOME",                 NIL, NIL, NIL, .F.) //  "Nome Funcionário"
    self:oSchema:addProperty("RBCOD",            STR0032,      "string",    STR0032,     "RBCOD",                  NIL, NIL, NIL, .F.) //  "Código"
    self:oSchema:addProperty("RBNOME",           STR0033,      "string",    STR0033,     "RBNOME",                 NIL, NIL, NIL, .F.) //  "Nome"
    self:oSchema:addProperty("RBCIC",            STR0034,      "string",    STR0034,     "RBCIC",                  NIL, NIL, NIL, .F.) //  "CPF"
    self:oSchema:addProperty("RBDTNASC",         STR0035,      "string",    STR0035,     "RBDTNASC",               NIL, NIL, NIL, .F.) //  "Data Nasc."
    self:oSchema:addProperty("RBSEXO",           STR0036,      "string",    STR0036,     "RBSEXO",                 NIL, NIL, NIL, .F.) //  "Sexo"
    self:oSchema:addProperty("RBGRAUPAR",        STR0037,      "string",    STR0037,     "RBGRAUPAR",              NIL, NIL, NIL, .F.) //  "Grau Parent."
    self:oSchema:addProperty("RBTIPIR",          STR0038,      "string",    STR0038,     "RBTIPIR",                NIL, NIL, NIL, .F.) //  "Tipo Dep. IR"
    self:oSchema:addProperty("RBTIPSF",          STR0039,      "string",    STR0039,     "RBTIPSF",                NIL, NIL, NIL, .F.) //  "Tipo Dep. SF"
    self:oSchema:addProperty("RBPLSAUDE",        STR0040,      "string",    STR0040,     "RBPLSAUDE",              NIL, NIL, NIL, .F.) //  "Pl. Saúde"
    self:oSchema:addProperty("RBINCT",           STR0041,      "string",    STR0041,     "RBINCT",                 NIL, NIL, NIL, .F.) //  "Dep. Incapac."
    self:oSchema:addProperty("INTTAF",           STR0042,      "string",    STR0042,     "INTTAF",                 NIL, NIL, NIL, .F.) //  "Int. TAF"
Return self:oSchema

/*/{Protheus.doc} ExistDepenTAF
    Função que verifica se os dependentes do funcionario foram integrados no TAF
    @type Function
    @version 12.1.2310
    @author karina.alves
    @since 15/03/2024
    @param cCPF, Character, CPF do Dependente
    @param cFilialFunc, Character, Filial
    @param cIDFunc, Character, ID do funcionário no TAF
    @param cVersao, Character, Versão do funcionário no TAF
    @return Logical, Flag de sucesso na  validação (.T. para integrado | .F. para não integrado)
/*/
Static Function ExistDepenTAF(cCPFDepen As Character, cFilialFunc As Character, cIDFunc As Character, cVersao As Character) As Logical
    // Declaração das variáveis clocais
    Local lSuccess          As Logical        //Booleano que armazena se dependente se encontra ou não no TAF
    Local nParamOrder       As Numeric        //Contador de parâmetros da query
    Local cQueryC9Y         As Character   //Query de dependentes na tabela C9Y
    Local cQueryT2F         As Character   //Query de dependentes na tabela T2F
    Local cQueryT3TT1U      As Character   //Query de dependentes na tabela T3T
    Local oStatementC9Y     As Object      //Objeto usada para utilização da classe FwExecStatement  
    Local oStatementT2F     As Object      //Objeto usada para utilização da classe FwExecStatement
    Local oStatementT3TT1U  As Object      //Objeto usada para utilização da classe FwExecStatement
    Local cAliasC9Y         As Character   //Alias do arquivo temporário
    Local cAliasT2F         As Character   //Alias do arquivo temporário
    Local cAliasT3TT1U      As Character   //Alias do arquivo temporário

    // Inicialização das variáveis
    lSuccess  := .F.
    nParamOrder := 1
    cQueryC9Y := ""
    cQueryT2F := ""
    cQueryT3TT1U := ""
    oStatementC9Y := NIL
    oStatementT2F := NIL
    oStatementT3TT1U := NIL    
    cAliasC9Y := ""
    cAliasT2F := ""
    cAliasT3TT1U := ""

    // Montagem da query de busca na C9Y - S2200
    cQueryC9Y += "SELECT "
    cQueryC9Y += "   C9Y_CPFDEP "
    cQueryC9Y += "FROM "
    cQueryC9Y +=     RetSQLName("C9Y") + " C9Y " 
    cQueryC9Y += "WHERE "
    cQueryC9Y += "   C9Y_FILIAL IN (?) "      // 1
    cQueryC9Y += "   AND C9Y_ID IN (?) "      // 2
    cQueryC9Y += "   AND C9Y_VERSAO IN (?) "  // 3
    cQueryC9Y += "   AND C9Y_CPFDEP IN (?) "  // 4
    cQueryC9Y += "   AND C9Y.D_E_L_E_T_ = ? " // 5
    cQueryC9Y := ChangeQuery(cQueryC9Y)
    
    // Instancia a classe
    oStatementC9Y := FwExecStatement():New(cQueryC9Y)

    // Define o valor dos bind parameters
    oStatementC9Y:SetString(nParamOrder++, cFilialFunc) // 1
    oStatementC9Y:SetString(nParamOrder++, cIDFunc)     // 2
    oStatementC9Y:SetString(nParamOrder++, cVersao)     // 3
    oStatementC9Y:SetString(nParamOrder++, cCPFDepen)   // 4
    oStatementC9Y:SetString(nParamOrder++, " ")         // 5

    // Executa a query e retorna o alias criado
    cAliasC9Y := oStatementC9Y:OpenAlias()

    If !Empty((cAliasC9Y)->C9Y_CPFDEP)        
        lSuccess  := .T.     
    EndIf

    // Fecha a área da tabela temporária
    if !Empty(cAliasC9Y)
        (cAliasC9Y)->(DBCloseArea())
    Endif

    //Se dependente nao encontrado, procura na T2F
    if !lSuccess
        nParamOrder:= 1
        // Montagem da query de busca na T2F - S2300
        cQueryT2F+= "SELECT "
        cQueryT2F += "   T2F_CPFDEP "
        cQueryT2F += "FROM "
        cQueryT2F +=     RetSQLName("T2F") + " T2F " 
        cQueryT2F += "WHERE "
        cQueryT2F += "   T2F_FILIAL IN (?) "      // 1
        cQueryT2F += "   AND T2F_ID IN (?) "      // 2
        cQueryT2F += "   AND T2F_VERSAO IN (?) "  // 3
        cQueryT2F += "   AND T2F_CPFDEP IN (?) "  // 4
        cQueryT2F += "   AND T2F.D_E_L_E_T_ = ? " // 5
        cQueryT2F := ChangeQuery(cQueryT2F)
        
        // Instancia a classe
        oStatementT2F := FwExecStatement():New(cQueryT2F)

        // Define o valor dos bind parameters
        oStatementT2F:SetString(nParamOrder++, cFilialFunc) // 1
        oStatementT2F:SetString(nParamOrder++, cIDFunc)     // 2
        oStatementT2F:SetString(nParamOrder++, cVersao)     // 3
        oStatementT2F:SetString(nParamOrder++, cCPFDepen)   // 4
        oStatementT2F:SetString(nParamOrder++, " ")         // 5

        // Executa a query e retorna o alias criado
        cAliasT2F := oStatementT2F:OpenAlias()

        If !Empty((cAliasT2F)->T2F_CPFDEP)     
            lSuccess  := .T.     
        EndIf

        // Fecha a área da tabela temporária
        if !Empty(cAliasT2F)
            (cAliasT2F)->(DBCloseArea())
        Endif
    EndIf

    //Se dependente nao encontrado, procura na T3T e T1U
    if !lSuccess
        nParamOrder:= 1
        // Montagem da query de busca na T3T com T1U - S2205
        cQueryT3TT1U += "SELECT "
        cQueryT3TT1U += "   T3T_CPFDEP "
        cQueryT3TT1U += "FROM "
        cQueryT3TT1U +=     RetSQLName("T3T") + " T3T " 
        cQueryT3TT1U += " INNER JOIN "
        cQueryT3TT1U +=     RetSQLName("T1U") + " T1U "
        cQueryT3TT1U += " ON " 
        cQueryT3TT1U += "   T1U.T1U_FILIAL  = T3T.T3T_FILIAL AND "
        cQueryT3TT1U += "   T1U.T1U_ID      = T3T.T3T_ID AND "
        cQueryT3TT1U += "   T1U.T1U_VERSAO  = T3T.T3T_VERSAO AND "
        cQueryT3TT1U += "   T1U_ATIVO       = ? AND " // 1
        cQueryT3TT1U += "   T1U_STATUS      = ? AND " // 2
        cQueryT3TT1U += "   T1U.D_E_L_E_T_  = ? "     // 3
        cQueryT3TT1U += "WHERE "
        cQueryT3TT1U += "   T3T_FILIAL = ? "          // 4
        cQueryT3TT1U += "   AND T3T_ID = ? "          // 5
        cQueryT3TT1U += "   AND T3T_CPFDEP = ? "      // 6
        cQueryT3TT1U += "   AND T3T.D_E_L_E_T_ = ?"   // 7
        cQueryT3TT1U := ChangeQuery(cQueryT3TT1U)
        
        // Instancia a classe
        oStatementT3TT1U := FwExecStatement():New(cQueryT3TT1U)

        // Define o valor dos bind parameters
        oStatementT3TT1U:SetString(nParamOrder++, "1")         // 1
        oStatementT3TT1U:SetString(nParamOrder++, "4")         // 2
        oStatementT3TT1U:SetString(nParamOrder++, " ")         // 3
        oStatementT3TT1U:SetString(nParamOrder++, cFilialFunc) // 4
        oStatementT3TT1U:SetString(nParamOrder++, cIDFunc)     // 5
        oStatementT3TT1U:SetString(nParamOrder++, cCPFDepen)   // 6
        oStatementT3TT1U:SetString(nParamOrder++, " ")         // 7

        // Executa a query e retorna o alias criado
        cAliasT3TT1U := oStatementT3TT1U:OpenAlias()

        If !Empty((cAliasT3TT1U)->T3T_CPFDEP)    
            lSuccess  := .T.     
        EndIf

        // Fecha a área da tabela temporária
        if !Empty(cAliasT3TT1U)
            (cAliasT3TT1U)->(DBCloseArea())
        Endif
    EndIf    

    // Limpa os objetos da memória
    FwFreeObj(oStatementC9Y) 
    FwFreeObj(oStatementT2F)
    FwFreeObj(oStatementT3TT1U)
Return lSuccess
