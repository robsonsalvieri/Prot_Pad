#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "FWLIBVERSION.CH"
#include "TREPORTS_GPER690.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="CTT,RCE,RHH,SRA", name="Conferência de dissídio - Atualização Salarial", country="ALL", initialRelease="12.1.2210", customTables='RHH,SRA')
class GPER690RHHTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getWherePar() as character

    @Get("/api/rh/smartview/v1/options/gpersv690/")
    Public Method getComboOrder() As Variant
endclass
 
method new() class GPER690RHHTReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Conferência de dissídio - Atualização Salarial"

    //self:setIsLookUp(.T.)
    self:setIsCBoxLookup(.T., .F.)
return self
 
method getDescription() as character class GPER690RHHTReportsBusinessObject
return STR0002 //"Objeto contendo informações das atualizações salarias decorrentes de dissídio"
 
method getAreas() as array class GPER690RHHTReportsBusinessObject
return {"RH"}//"RH"
 
method getData(nPage as numeric, oFilter as object) as object class GPER690RHHTReportsBusinessObject
    local cAlias as character
    local cWhere as character  
    local cQuery as character
    local cLastFil as character
    local cDescCTT as character
    local cCodCCT as character
    local cCustomFields as character
    local lTemCCt as logical
    local lOfusca as logical
    local jParams as json
    local cVerbaOrig as character
    local nPOrder as numeric
    local aFldRel as array
    local aAllCpo as array
    local jData as json 

    //Variaveis de Acesso do Usuario
	Local cAcessaSRA := &( " { || " + ChkRH( "GPER135" , "SRA" , "2" ) + " } " )

    Static oStatement
    Static __cEmpAnt := cEmpAnt
    Static __cWhere  := ""

    jParams    := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    cLastFil   := "_cLastFil"
    cWhere     := self:getWherePar(jParams)
    cVerbaOrig := "000"
    nPOrder    := 4
    aFldRel	   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    aAllCpo    := self:getStructFields()
    lTemCCt    := RCE->(ColumnPos("RCE_CCT")) > 0
    lOfusca    := Len(aFldRel) != Len(self:getArrayFields())

    cQuery  := " SELECT RHH.RHH_FILIAL, RHH.RHH_MAT, RHH.RHH_DTACOR, RHH.RHH_CC, RHH.RHH_ITEM, RHH.RHH_CLVL, " 
    cQuery  += "        RHH.RHH_SINDIC, SRA.RA_FILIAL, SRA.RA_NOME, SRA.RA_ADMISSA, SRA.RA_DEMISSA, RHH.RHH_VL, " 
    cQuery  += "        RHH.RHH_CALC, RHH.RHH_INDICE " 
    cQuery  +=  " ? "
    cQuery  += " FROM	" + RetSqlName("RHH") + " RHH "
    cQuery  += "        INNER JOIN " + RetSqlName("SRA") + " SRA ON " + FWJoinFilial( "SRA", "RHH" ) + " AND SRA.RA_MAT = RHH.RHH_MAT AND SRA.D_E_L_E_T_ = ? "
    cQuery  += " WHERE	RHH.RHH_VB = ? "
    
    If !Empty(cWhere)
        cQuery += cWhere    
    EndIf

    //Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        cQuery += " AND " + oFilter:getSQLExpression()
    Endif

    cQuery  += " AND	RHH.D_E_L_E_T_	= ? "
    cQuery  += " ORDER BY RHH.RHH_FILIAL, RHH.RHH_MAT, RHH.RHH_DTACOR "

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(__cWhere == cWhere)
        cQuery := ChangeQuery(cQuery)
        
        __cEmpAnt  := cEmpAnt
        __cWhere   := cWhere
        oStatement := FwExecStatement():New(cQuery)
    EndIf

    //Captura e define os campos personalizados das tabelas RHH, SRA
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"RHH", "SRA"})
    oStatement:SetUnsafe(1, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados
    oStatement:SetString(2, ' ') //SRA.D_E_L_E_T_
    oStatement:SetString(3, cVerbaOrig)

    If !Empty(jParams['FILDE', 1])
		oStatement:SetString(nPOrder++, jParams["FILDE", 1])
	EndIf

    If !Empty(jParams['FILATE', 1])
		oStatement:SetString(nPOrder++, jParams["FILATE", 1])
	EndIf

    If !Empty(jParams['MATDE', 1])
		oStatement:SetString(nPOrder++, jParams["MATDE", 1])
	EndIf

    If !Empty(jParams['MATATE', 1])
		oStatement:SetString(nPOrder++, jParams["MATATE", 1])
	EndIf

    If !Empty(jParams['CCDE', 1])
		oStatement:SetString(nPOrder++, jParams["CCDE", 1])
	EndIf

    If !Empty(jParams['CCATE', 1])
		oStatement:SetString(nPOrder++, jParams["CCATE", 1])
	EndIf

    If !Empty(jParams['DTACORDE', 1])
		oStatement:SetString(nPOrder++, StrTran( SubStr(ArrTokStr(jParams["DTACORDE"]), 1, 10), "-" ))
	EndIf
    
    If !Empty(jParams['DTACORATE', 1])
		oStatement:SetString(nPOrder++, StrTran( SubStr(ArrTokStr(jParams["DTACORATE"]), 1, 10), "-" ))
	EndIf

    oStatement:SetString(nPOrder++, ' ') //RHH.D_E_L_E_T_
    
    cAlias := oStatement:OpenAlias()   	

    dbSelectArea( cAlias )

    While !(cAlias)->(Eof())

        //Consiste Filiais e Acessos
        If cLastFil != ( cAlias )->RA_FILIAL
            If !((cAlias)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
                (cAlias)->( dbSkip() )
                Loop
            EndIf
        EndIf
        
        cLastFil := ( cAlias )->RA_FILIAL

        cDescCTT    := DescCc( (cAlias)->RHH_CC, (cAlias)->RHH_FILIAL )
        cCodCCT     := If(lTemCCT .and. !Empty((cAlias)->RHH_SINDIC), fDesc("RCE",(cAlias)->RHH_SINDIC, "RCE_CCT",,(cAlias)->RHH_FILIAL) , " ")

        jData := {;
            "RHH_FILIAL": (cAlias)->RHH_FILIAL,;
            "RHH_MAT": (cAlias)->RHH_MAT,;
            "RHH_DTACOR": Iif( !Empty((cAlias)->RHH_DTACOR), FwTimeStamp( 5, sToD( (cAlias)->RHH_DTACOR ), "00:00:00" ), Nil),;
            "RA_NOME": RTrim( (cAlias)->RA_NOME ),;
            "RA_ADMISSA": FwTimeStamp( 5, sToD( (cAlias)->RA_ADMISSA ), "00:00:00" ),; 
            "RA_DEMISSA": Iif( !Empty((cAlias)->RA_DEMISSA), FwTimeStamp( 5, sToD( (cAlias)->RA_DEMISSA ), "00:00:00" ), Nil),;                
            "SALANT": (cAlias)->RHH_VL,;
            "SALNEW": (cAlias)->RHH_CALC,;
            "RHH_CC": (cAlias)->RHH_CC,;
            "RHH_ITEM": (cAlias)->RHH_ITEM,;
            "RHH_CLVL": (cAlias)->RHH_CLVL,;
            "RHH_SINDIC": (cAlias)->RHH_SINDIC,;
            "RCE_CCT": cCodCCT,;
            "CTT_DESC01": RTrim( cDescCTT ),;
            "PERCREAJ": (cAlias)->RHH_INDICE;
        }

          //Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllCpo, jData, cAlias, aFldRel, lOfusca)

        self:oData:appendData(jData)

        (cAlias)->(dbSkip())

    Enddo
 
(cAlias)->( DBCloseArea() )

return self:oData
 
method getSchema() as object class GPER690RHHTReportsBusinessObject
    local aFieldsSRA as array
    local aFieldsRHH as array
    
    aFieldsRHH := { "RHH_FILIAL", "RHH_MAT", "RHH_DTACOR", "RHH_CC", "RHH_ITEM", "RHH_CLVL", "RHH_SINDIC" }
    aFieldsSRA := { "RA_NOME", "RA_ADMISSA", "RA_DEMISSA" }
    
    self:oSchema:aliasToSchema("CTT", "CTT_DESC01")
    self:oSchema:aliasToSchema("RCE", "RCE_CCT")
    self:oSchema:aliasToSchema("RHH", aFieldsRHH)
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)

    self:oSchema:addProperty("SALANT", "Salário Anterior", "number", "Salário Anterior", "SALANT")
    self:oSchema:addProperty("SALNEW", "Novo Salário", "number", "Novo Salário", "SALNEW")
    self:oSchema:addProperty("PERCREAJ", "Perc. Reajuste", "number", "Perc. Reajuste", "PERCREAJ")

    self:addParameter("FILDE","Filial De ?","string", .F.) 
    self:addParameter("FILATE","Filial Até ?","string", .F.) 
    self:addParameter("MATDE","Matricula De ?","string", .F.) 
    self:addParameter("MATATE","Matricula Até ?","string", .F.) 
    self:addParameter("CCDE","Centro de Custo De ?","string", .F.)
    self:addParameter("CCATE","Centro de Custo Até ?","string", .F.)
    self:addParameter("DTACORDE","Data de Acordo De ?","date", .F.)
    self:addParameter("DTACORATE","Data de Acordo Até ?","date", .F.)

    //setCustomURL disponível na LIB 20231121.
    If FWLibVersion() >= "20231121" 

        //Loockups
        self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2)

    EndIf    

return self:oSchema

method getWherePar( jPar as json) as character class GPER690RHHTReportsBusinessObject
    
    local cRet as character

    If !Empty(jPar['FILDE', 1])
		cRet += " AND SRA.RA_FILIAL >= ? "
	EndIf

    If !Empty(jPar['FILATE', 1])
		cRet += " AND SRA.RA_FILIAL <= ? "
	EndIf

    If !Empty(jPar['MATDE', 1])
		cRet += " AND SRA.RA_MAT >= ? "
	EndIf

    If !Empty(jPar['MATATE', 1])
		cRet += " AND SRA.RA_MAT <= ? "
	EndIf

    If !Empty(jPar['CCDE', 1])
		cRet += " AND SRA.RA_CC >= ? "
	EndIf

    If !Empty(jPar['CCATE', 1])
		cRet += " AND SRA.RA_CC <= ? "
	EndIf
    
    If !Empty(jPar['DTACORDE', 1])
        cRet += " AND RHH.RHH_DTACOR >= ? "
	EndIf

    If !Empty(jPar['DTACORATE', 1])
        cRet += " AND RHH.RHH_DTACOR <= ? "
	EndIf
    
Return cRet
