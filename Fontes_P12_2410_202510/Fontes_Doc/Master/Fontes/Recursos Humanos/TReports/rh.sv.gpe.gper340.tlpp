#Include "TOTVS.FRAMEWORK.TREPORTS.INTEGRATEDPROVIDER.th"
#Include "TLPP-CORE.th"
#Include "TLPP-REST.th"
#Include "rh.sv.gpe.gper340.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} GPER340TReportsBusinessObject:new()
    Classe do objeto de negócio do Informe de Rendimentos
    @type Class
    @version 12.1.2510
    @author karina.alves
    @since 09/07/2025
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SRJ, CTT", name="Cargos e Salários", customTables = "SRA", country="ALL", initialRelease="12.1.2310")
Class GPER340TReportsBusinessObject From IntegratedProvider
    // Define os métodos da classe
    Public Method new()       As Object
    Public Method getData()   As Object
    Public Method getSchema() As Object

    @Get("/api/rh/smartview/v1/options/GPER340/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} GPER340TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2510
    @author karina.alves
    @since 14/07/2025
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER340TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a área
    self:appendArea(STR0001) // "RH"

    // Define o nome e descrição do objeto de negócio
    self:setDisplayName(STR0002) // "Cargos e Salários"
    self:setDescription(STR0003) // "Este objeto de negócio traz as informações do Relatório de Cargos e Salários."

Return self

/*/{Protheus.doc} GPER340TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author karina.alves
    @since 14/07/2025
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER340TReportsBusinessObject
    // Declaração das variáveis locais
    Local cQuery              As Character // Query para busca dos registros
    Local cAlias              As Character // Alias do arquivo temporário
    Local cCustomFields       As Character // Campos customizados no formato SQL
    Local cName               As Character // Nome real do campo
    Local cSituacao           As Character // Situação
    Local cFiltro             As Character // Filtro do objeto de negócio
    Local cValidFil           As Character // Filiais válidas
    Local cCategoria          As Character // Categoria
    Local cAcessaSRA          As Character // Valida acesso a SRA
    Local cJoinSRJ            As Character // Join da Query SRA com SRJ
    Local cJoinCTT            As Character // Join da Query SRA com CTT
    Local cSemana             As Character
    Local cFilExec            As Character // Filial Execução
    Local cEmpExec            As Character // Empresa Execução
    Local cAnoMes             As Character
    Local cAntCC              As Character // Guarda CC aanterior
    Local cExibeGraf          As Character
    Local jData               As JSON      // Informações de uma linha do ON
    Local jLGPD               As JSON      // Indica quais campos devem ser ofuscados
    Local jParams             As JSON      // Parâmetros
    Local oExec               As Object    // Instância da classe FwExecStatement
    Local nParamOrder         As Numeric   // Ordem dos bind parameters da query
    Local nAux                As Numeric   // Contador auxiliar para percorrer vetores
    Local nFields             As Numeric   // Contador de campos
    Local aInfo               As Numeric   // Variável aInfo, para dados da empresa e filial
    Local nSalario            As Numeric   // Variável preenchida conforme salário
    Local nSalMes             As Numeric   // Variável preenchida conforme salário por mês
    Local nSalDia             As Numeric   // Variável preenchida conforme salário por dia
    Local nSalHora            As Numeric   // Variável preenchida conforme salário por hora
    Local nX                  As Numeric   // Variavel contador auxiliar
    Local nPos                As Numeric   // Posição do array
    Local aPDFields           As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aOfusca             As Array     // Informações para ofuscar
    Local aAllFields          As Array     // Estrutura de todos os campos do ON
    Local aPerAtual           As Array
    Local lObfuscated         As Logical   // Flag que indica se tem algum campo para ofuscar
    Local lImpTFilEmp         As Logical   // Imprime Totlizador por Filial
    Local aTCC                As Array
    Local aTFIL               As Array
    Local nTEmpresa           As Numeric   // Total da Empresa

    //Parâmetros
    Private cFilialDe           As Character // Parâmetro Filial De
    Private cFilialAte          As Character // Parâmetro Filial Até
    Private cCCDe               As Character // Parâmetro Centro de Custo De
    Private cCCAte              As Character // Parâmetro Centro de Custo Até   
    Private cMatDe              As Character // Parâmetro Matrícula De
    Private cMatAte             As Character // Parâmetro Matrícula Até
    Private cNomeDe             As Character // Parâmetro Nome De
    Private cNomeAte            As Character // Parâmetro Nome Até
    Private cFuncaoDe           As Character // Parâmetro Função De
    Private cFuncaoAte          As Character // Parâmetro Função Até
    Private aCategorias         As Array     // Parâmetro Categorias
    Private aSituacoes          As Array     // Parâmetro Situações
    Private cImpTotFil          As Character // Parâmetro Imprime Totais Por Filial
    Private cQualSal            As Character // Parâmetro Sobre Qual Salário
    Private cSobreSal           As Character // Parâmetro Sobre Salário
    Private cImp                As Character // Parâmetro Imprimir
    private aTotCargo           As Array     // Controla o total por cargo

    // Inicialização das variáveis
    cQuery              := ""
    cAlias              := ""
    cFiltro             := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    jParams             := oFilter:getParameters()
    jData               := JSONObject():New()
    jLGPD               := JSONObject():New()
    oExec               := NIL
    nParamOrder         := 1
    nAux                := 0
    nFields             := 1
    aPDFields           := {}
    aOfusca             := IIf(FindFunction("ChkOfusca"), ChkOfusca(), {.T., .F., {"", ""}}) 
    cFilialDe           := ""
    cFilialAte          := ""
    cMatDe              := ""
    cMatAte             := ""
    cCCDe               := ""
    cCCAte              := "" 
    aCategorias         := {}
    aSituacoes          := {}
    aAllFields          := {}
    aInfo               := {}
    aPerAtual           := {}
    aTCC                := {}
    aTFIL               := {}
    aTotCargo           := {}
    lObfuscated         := .F.
    cSituacao           := ""
    cSemana             := ""
    cValidFil           := fValidFil()
    cAcessaSRA          := &("{ || " + ChkRH("GPER340","SRA","2") + "}")
    lImpTFilEmp         := .F.
    cFilExec            := AllTrim(FwFilialName())
    cEmpExec            := AllTrim(FwEmpName(cEmpAnt))
    nTEmpresa           := 0
    cAnoMes             := ""
    cAntCC              := ""
    cExibeGraf          := '1'

	// Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cFilialDe   := IIf(jParams:hasProperty("FilialDe")       .and. Len(jParams["FilialDe"]) > 0      .and.!Empty(jParams["FilialDe"][1]),       jParams["FilialDe"][1],           "")
        cFilialAte  := IIf(jParams:hasProperty("FilialAte")      .and. Len(jParams["FilialAte"]) > 0     .and.!Empty(jParams["FilialAte"][1]),      jParams["FilialAte"][1],          Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCCDe       := IIf(jParams:hasProperty("CCDe")           .and. Len(jParams["CCDe"]) > 0          .and.!Empty(jParams["CCDe"][1]),           jParams["CCDe"][1],               "")
        cCCAte      := IIf(jParams:hasProperty("CCAte")          .and. Len(jParams["CCAte"]) > 0         .and.!Empty(jParams["CCAte"][1]),          jParams["CCAte"][1],              Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cMatDe      := IIf(jParams:hasProperty("MatriculaDe")    .and. Len(jParams["MatriculaDe"]) > 0   .and.!Empty(jParams["MatriculaDe"][1]),    jParams["MatriculaDe"][1],        "")
        cMatAte     := IIf(jParams:hasProperty("MatriculaAte")   .and. Len(jParams["MatriculaAte"]) > 0  .and.!Empty(jParams["MatriculaAte"][1]),   jParams["MatriculaAte"][1],       Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cNomeDe     := IIf(jParams:hasProperty("NomeDe")         .and. Len(jParams["NomeDe"]) > 0        .and.!Empty(jParams["NomeDe"][1]),         jParams["NomeDe"][1],             "")
        cNomeAte    := IIf(jParams:hasProperty("NomeAte")        .and. Len(jParams["NomeAte"]) > 0       .and.!Empty(jParams["NomeAte"][1]),        jParams["NomeAte"][1],            Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
        cFuncaoDe   := IIf(jParams:hasProperty("FuncaoDe")       .and. Len(jParams["FuncaoDe"]) > 0      .and.!Empty(jParams["FuncaoDe"][1]),       jParams["FuncaoDe"][1],           "")
        cFuncaoAte  := IIf(jParams:hasProperty("FuncaoAte")      .and. Len(jParams["FuncaoAte"]) > 0     .and.!Empty(jParams["FuncaoAte"][1]),      jParams["FuncaoAte"][1],          Replicate("Z", GetSX3Cache("RA_CODFUNC", "X3_TAMANHO")))
        cImpTotFil  := IIf(jParams:hasProperty("ImpTotFil")      .and. Len(jParams["ImpTotFil"]) > 0     .and.!Empty(jParams["ImpTotFil"][1]),      jParams["ImpTotFil"][1],          "1") // 1 - Sim e 2 - Não
        cQualSal    := IIf(jParams:hasProperty("QualSal")        .and. Len(jParams["QualSal"]) > 0       .and.!Empty(jParams["QualSal"][1]),        jParams["QualSal"][1],            "1") // 1 - Mês e 2 - Hora
        cSobreSal   := IIf(jParams:hasProperty("SobreSal")       .and. Len(jParams["SobreSal"]) > 0      .and.!Empty(jParams["SobreSal"][1]),       jParams["SobreSal"][1],           "1") // 1 - Composto e 2 - Base
        cImp        := IIf(jParams:hasProperty("Imp")            .and. Len(jParams["Imp"]) > 0           .and.!Empty(jParams["Imp"][1]),            jParams["Imp"][1],                "1") // 1 - Analítico e 2 - Sintético
        aCategorias := jParams["Categorias"]
        aSituacoes  := jParams["Situacoes"]
        cExibeGraf  := IIf(jParams:hasProperty("ExibeGraf")      .and. Len(jParams["ExibeGraf"]) > 0     .and.!Empty(jParams["ExibeGraf"][1]),      jParams["ExibeGraf"][1],           "1") // 1 - Sim e 2 - Não
    EndIf

    // Passa as situações no vetor para uma string
    If (Len(aSituacoes) > 0)
        cSituacao := ""
        AEval(aSituacoes, {|x| cSituacao += IIf(x == NIL .or. Empty(x), " ", x)})
    EndIf

    // Passa as situações no vetor para uma string
    If (Len(aCategorias) > 0)
        cCategoria := ""
        AEval(aCategorias, {|x| cCategoria += IIf(x == NIL .or. Empty(x), " ", x)})
    EndIf

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aCategorias)
        aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
    Next nAux

    //Sempre imprimir totalizador filial quando for sintetico ou parâmetro tiver igual a Sim
    if cImp == '2' .or. cImpTotFil == '1'
        lImpTFilEmp := .T.
    endif

    cJoinSRJ := FWJoinFilial("SRA", "SRJ")
    cJoinCTT := FWJoinFilial("SRA", "CTT")

    BEGIN SEQUENCE

        //Função que preenche os valores totais para que o cálculo seja realizado por funcionário depois
        fCalculaTotais(@aTCC, @aTFIL, @nTEmpresa)

        //Montagem da query de busca
        cQuery += "SELECT "
        cQuery +=     "SRA.RA_FILIAL, "
        cQuery +=     "SRA.RA_CC, "
        cQuery +=     "SRA.RA_HRSMES, "
        cQuery +=     "SRA.RA_MAT, "
        cQuery +=     "SRA.RA_NOME, "
        cQuery +=     "SRA.RA_SITFOLH, "
        cQuery +=     "SRA.RA_CATFUNC, "
        cQuery +=     "SRA.RA_ADMISSA, "
        cQuery +=     "SRA.RA_CODFUNC, "
        cQuery +=     "SRA.RA_SALARIO, "
        cQuery +=     "SRA.RA_PROCES, "
        cQuery +=     "CTT.CTT_DESC01, "
        cQuery +=     "SRJ.RJ_FUNCAO, "
        cQuery +=     "SRJ.RJ_DESC,  "
        cQuery +=     "SRJ.RJ_MAOBRA "
        cQuery +=     "? "
        cQuery += "FROM "
        cQuery +=     RetSQLName("SRA") + " SRA "
        cQuery += " LEFT JOIN " + RetSqlName("SRJ") + " SRJ ON "
        cQuery +=       cJoinSRJ
        cQuery +=       " AND SRJ.RJ_FUNCAO = SRA.RA_CODFUNC "
        cQuery +=       " AND SRJ.D_E_L_E_T_ = ? " 
        cQuery += " LEFT JOIN " + RetSQLName("CTT") + " CTT ON "
        cQuery +=       cJoinCTT
        cQuery +=       " AND CTT.CTT_CUSTO = SRA.RA_CC "
        cQuery +=       " AND CTT.D_E_L_E_T_ = ? " 
        cQuery += "WHERE "
        cQuery +=     "SRA.RA_FILIAL       BETWEEN ? AND ? " 
        cQuery +=     "AND SRA.RA_MAT      BETWEEN ? AND ? "  
        cQuery +=     "AND SRA.RA_CC       BETWEEN ? AND ? " 
        cQuery +=     "AND SRA.RA_CODFUNC  BETWEEN ? AND ? "
        cQuery +=     "AND SRA.RA_NOME     BETWEEN ? AND ? " 

        if Len(aSituacoes) > 0
            cQuery += "AND SRA.RA_SITFOLH IN (?)  " 
        EndIf

        If Len(aCategorias) > 0
            cQuery += "AND SRA.RA_CATFUNC IN (?)  " 
        EndIf

        cQuery +=     "AND SRA.D_E_L_E_T_ = ?  "
        cQuery +=       " ? " 
        cQuery +=     " ORDER BY SRA.RA_FILIAL, SRA.RA_CC " 

        cQuery := ChangeQuery(cQuery)
        oExec := FwExecStatement():New(cQuery)

        // Captura e define os campos personalizados das tabelas 
        cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA"})
        oExec:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))

        oExec:SetString(nParamOrder++, " ")        

        oExec:SetString(nParamOrder++, " ")        

        // Filial
        oExec:SetString(nParamOrder++, cFilialDe)
        oExec:SetString(nParamOrder++, cFilialAte)

        // Matrícula
        oExec:SetString(nParamOrder++, cMatDe)
        oExec:SetString(nParamOrder++, cMatAte)

        //CC
        oExec:SetString(nParamOrder++, cCCDe)
        oExec:SetString(nParamOrder++, cCCAte)

        //Função
        oExec:SetString(nParamOrder++, cFuncaoDe)
        oExec:SetString(nParamOrder++, cFuncaoAte)

        //Nome
        oExec:SetString(nParamOrder++, cNomeDe)
        oExec:SetString(nParamOrder++, cNomeAte)   

        if Len(aSituacoes) > 0
            oExec:SetIN(nParamOrder++, aSituacoes) 
        EndIf

        If Len(aCategorias) > 0
            oExec:SetIN(nParamOrder++, aCategorias) 
        EndIf
        
        oExec:SetString(nParamOrder++, " ")
        
        oExec:SetUnsafe(nParamOrder++, cFiltro) 

        cAlias := oExec:OpenAlias()

        // Percorre todos os registros
        While (!(cAlias)->(EOF()))
            If (Eval(cAcessaSRA) .and. (cAlias)->RA_FILIAL $ cValidFil)

                // Posiciona SRA para chamada das funcoes do GPEXCAL1
                dbSelectArea("SRA")
                dbSetOrder(1)
                dbSeek((cAlias)->RA_FILIAL + (cAlias)->RA_MAT)

                nSalario    := 0
                nSalMes     := 0
                nSalDia     := 0
                nSalHora    := 0

                // Salário Composto
                If cSobreSal == '1'  .And. MV_MODFOL <> '2'
                    // Calcula Salario Incorporado Mes , Dia , Hora do Funcionario  
                    fSalInc(@nSalario,@nSalMes,@nSalHora,@nSalDia,.T.)
                Else
                    If Type("cAnoMes") == "U" .OR. Empty(cAnoMes)
                        If Type("aPerAtual") == "U" .OR. Empty(aPerAtual) .OR. !(aPerAtual[1,8] ==  (cAlias)->RA_PROCES)
                            fGetPerAtual( @aPerAtual, xFilial("RCH"), (cAlias)->RA_PROCES, fGetRotOrdinar() )
                            If Empty(aPerAtual)
                                fGetPerAtual( @aPerAtual, xFilial("RCH"), (cAlias)->RA_PROCES, fGetCalcRot('9') )
                            EndIf
                        EndIf

                        If !Empty(aPerAtual)
                            cAnoMes := AnoMes(aPerAtual[1,6])
                            cSemana := aPerAtual[1,2]
                        Else
                            (cAlias)->(dbSkip())
                            Loop
                        EndIf
                    Endif

                    fSalario(@nSalario,@nSalHora,@nSalDia,@nSalMes,"A",cAnoMes, cSemana)

                    If cQualSal == '1' // 1-Mes
                        nSalMes := nSalario
                    Else // 2-Hora
                        nSalHora := Round(nSalario / (cAlias)->RA_HRSMES,MsDecimais(1))
                    EndIf
                Endif

                jData := JSONObject():New()
                jData["FilialExec"]   := Trim(cFilExec)
                jData["EmpresaExec"]  := Trim(cEmpExec)
                jData["Filial"]       := Trim(Iif(jLGPD["RA_FILIAL"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),  (cAlias)->RA_FILIAL)) 
                jData["Matricula"]    := Trim(Iif(jLGPD["RA_MAT"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),     (cAlias)->RA_MAT)) 
                jData["CentroCusto"]  := Trim(Iif(jLGPD["RA_CC"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),      (cAlias)->RA_CC)) 
                jData["DescCC"]       := Trim(Iif(jLGPD["CTT_DESC01"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01), (cAlias)->CTT_DESC01)) 
                jData["CodFuncao"]    := Trim(Iif(jLGPD["RA_CODFUNC"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CODFUNC), (cAlias)->RA_CODFUNC)) 
                jData["DescFuncao"]   := Trim(Iif(jLGPD["RJ_DESC"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RJ_DESC),    (cAlias)->RJ_DESC)) 
                jData["DataAdm"]      := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_ADMISSA)
                jData["Nome"]         := Trim(Iif(jLGPD["RA_NOME"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),    (cAlias)->RA_NOME)) 

                If cQualSal == '1' // 1-Mes
                    jData["SalarioNom"] := nSalMes
                else // 2 - Hora
                    jData["SalarioNom"] := nSalHora
                endif

                jData["MaoObra"] := fBuscaDescMO((cAlias)->RJ_MAOBRA)

                //Procura pela filial e CC para fazer o cálculo
                jData["PercCC"] := '0'
                For nX := 1 To Len(aTCC)
                    If aTCC[nX,1] = (cAlias)->RA_FILIAL+(cAlias)->RA_CC
                        jData["PercCC"] := IIf(cQualSal == '1',  ((nSalMes / aTCC[nX,2] )*100), ((nSalHora / aTCC[nX,2] )*100))
                    EndIf
                Next nX

                jData["PercFil"] := 0
                jData["PercEmp"] := 0
                jData["TotEmp"] := 0.00

                jData["FilCargo"] := (cAlias)->RA_FILIAL+' - '+(cAlias)->RA_CODFUNC
                
                If (nPos := AScan(aTotCargo, {|x| x[1] == (cAlias)->RA_FILIAL+' - '+(cAlias)->RA_CODFUNC})) > 0
                    jData["TotEmp"] := Round((aTotCargo[nPos,2]/nTEmpresa) * 100,2)
                EndIf
                
                If lImpTFilEmp
                    For nX := 1 To Len(aTFIL)
                        If aTFIL[nX,1] = (cAlias)->RA_FILIAL
                            jData["PercFil"] := IIf(cQualSal == '1', (nSalMes/ aTFIL[nX,2] )*100 , (nSalHora / aTFIL[nX,2] )*100)
                            jData["PercEmp"] := IIf(cQualSal == '1',  (nSalMes / nTEmpresa )* 100, ( nSalHora / nTEmpresa )* 100)   
                        EndIf   
                    Next nX
                endif
                
                // Adiciona os campos customizados no JSON
                GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

                // Adiciona as informações da linha
                self:oData:appendData(jData) 
            endif

            (cAlias)->(DBSkip()) 
        Enddo
    END SEQUENCE

     // Fecha a área da tabela temporária
    If !Empty(cAlias)
        (cAlias)->(DBCloseArea())
    Endif  

    // Limpa os objetos da memória 
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
	FwFreeObj(jParams)
    FwFreeObj(oExec)
    FwFreeArray(aAllFields)
    FwFreeArray(aPDFields)
    FwFreeArray(aInfo)
    FwFreeArray(aCategorias)
    FwFreeArray(aSituacoes)
    FwFreeArray(aTFIL)
    FwFreeArray(aTCC)
Return self:oData

/*/{Protheus.doc} GPER340TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author karina.alves
    @since 14/07/2025
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER340TReportsBusinessObject
    // Declaração das variáveis locais
    Local aFields  As Array // Campos das propriedades aninhadas

    // Inicialização das variáveis
    aFields := {}

    // Define os parâmetros do relatório
    self:oSchema:addParameter("FilialDe",      STR0004 + ' ' + STR0005,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0004 + ' ' + STR0005) //  "Filial De"
    self:oSchema:addParameter("FilialAte",     STR0004 + ' ' + STR0006,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0004 + ' ' + STR0006) //  "Filial Até"
    self:oSchema:addParameter("CCDe",          STR0007 + ' ' + STR0005,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0007 + ' ' + STR0005) //  "Centro de Custo De"
    self:oSchema:addParameter("CCAte",         STR0007 + ' ' + STR0006,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0007 + ' ' + STR0006) //  "Centro de Custo Até"
    self:oSchema:addParameter("MatriculaDe",   STR0008 + ' ' + STR0005,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0008 + ' ' + STR0005) //  "Matrícula De"
    self:oSchema:addParameter("MatriculaAte",  STR0008 + ' ' + STR0006,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0008 + ' ' + STR0006) //  "Matrícula Até"
    self:oSchema:addParameter("NomeDe",        STR0009 + ' ' + STR0005,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0009 + ' ' + STR0005) //  "Nome De"
    self:oSchema:addParameter("NomeAte",       STR0009 + ' ' + STR0006,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0009 + ' ' + STR0006) //  "Nome Até"
    self:oSchema:addParameter("FuncaoDe",      STR0010 + ' ' + STR0005,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0010 + ' ' + STR0005) //  "Função De"
    self:oSchema:addParameter("FuncaoAte",     STR0010 + ' ' + STR0006,     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0010 + ' ' + STR0006) //  "Função Até"
    self:oSchema:addParameter("Situacoes",     STR0011,                     "string", .T., NIL, NIL, NIL, NIL, NIL, STR0011)                 //  "Situações"
    self:oSchema:addParameter("Categorias",    STR0012,                     "string", .T., NIL, NIL, NIL, NIL, NIL, STR0012)                 //  "Categorias"
    self:oSchema:addParameter("ImpTotFil",     STR0013,                     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0013)                 //  "Imprimir Totais Por Filial"
    self:oSchema:addParameter("QualSal",       STR0014,                     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0014)                 //  "Sobre Qual Salário"
    self:oSchema:addParameter("SobreSal",      STR0015,                     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0015)                 //  "Sobre Salário"
    self:oSchema:addParameter("Imp",           STR0016,                     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0016)                 //  "Imprimir"
    self:oSchema:addParameter("ExibeGraf",     STR0034,                     "string", .F., NIL, NIL, NIL, NIL, NIL, STR0034)                 //  "Exibe Gráfico"

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("FilialDe",        "/api/rh/smartview/v1/options/GPEParams/getBranches",       2)
    self:setCustomURL("FilialAte",       "/api/rh/smartview/v1/options/GPEParams/getBranches",       2)
    self:setCustomURL("CCDe",            "api/framework/v1/genericLookupService/smartview/CTT",      2)
    self:setCustomURL("CCAte",           "api/framework/v1/genericLookupService/smartview/CTT",      2)
    self:setCustomURL("MatriculaDe",     "/api/framework/v1/genericLookupService/smartview/SRA",     2)
    self:setCustomURL("MatriculaAte",    "/api/framework/v1/genericLookupService/smartview/SRA",     2)
    self:setCustomURL("FuncaoDe",        "/api/framework/v1/genericLookupService/smartview/SRJ",     2)
    self:setCustomURL("FuncaoAte",       "/api/framework/v1/genericLookupService/smartview/SRJ",     2)
    self:setCustomURL("Situacoes",       "/api/rh/smartview/v1/options/GPEParams/getSX5/31",         2)
    self:setCustomURL("Categorias",      "/api/rh/smartview/v1/options/GPEParams/getSX5/28",         2)
    self:setCustomURL("ImpTotFil",       "/api/rh/smartview/v1/options/GPEParams/getSNOptions",      1)
    self:setCustomURL("QualSal",         "/api/rh/smartview/v1/options/GPER340/QualSal",             1)
    self:setCustomURL("SobreSal",        "/api/rh/smartview/v1/options/GPER340/SobreSal",            1)
    self:setCustomURL("Imp",             "/api/rh/smartview/v1/options/GPER340/Imp",                 1)
    self:setCustomURL("ExibeGraf",       "/api/rh/smartview/v1/options/GPEParams/getSNOptions",      1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",   STR0004 + ' ' + STR0033,        "string", STR0004 + ' ' + STR0033,            "FilialExec",   NIL, NIL, NIL, .F.) // "Filial Execução"
    self:oSchema:addProperty("EmpresaExec",  STR0027 + ' ' + STR0033,        "string", STR0027 + ' ' + STR0033,            "EmpresaExec",  NIL, NIL, NIL, .F.) // "Empresa Execução"
    self:oSchema:addProperty("Filial",       STR0004,                        "string", STR0004,                            "Titulo",       NIL, NIL, NIL, .F.) // "Filial"
    self:oSchema:addProperty("CentroCusto",  STR0007,                        "string", STR0007,                            "CentroCusto",  NIL, NIL, NIL, .F.) // "Centro de Custo"
    self:oSchema:addProperty("DescCC",       STR0029 + ' ' + STR0007,        "string", STR0029 + ' ' + STR0007,            "DescCC",       NIL, NIL, NIL, .F.) // "Descrição Centro de Custo"
    self:oSchema:addProperty("Matricula",    STR0008,                        "string", STR0008,                            "Matricula",    NIL, NIL, NIL, .F.) // "Matrícula"
    self:oSchema:addProperty("Nome",         STR0009,                        "string", STR0009,                            "Nome",         NIL, NIL, NIL, .F.) // "Nome"
    self:oSchema:addProperty("DataAdm",      STR0023,                        "date",   STR0023,                            "DataAdm",      NIL, NIL, NIL, .F.) // "Data Admissão"
    self:oSchema:addProperty("CodFuncao",    STR0028 + ' ' + STR0010,        "string", STR0028 + ' ' + STR0010,            "CodFuncao",    NIL, NIL, NIL, .F.) // "Código Função"
    self:oSchema:addProperty("DescFuncao",   STR0029 + ' ' + STR0010,        "string", STR0029 + ' ' + STR0010,            "DescFuncao",   NIL, NIL, NIL, .F.) // "Descrição Função"
    self:oSchema:addProperty("MaoObra",      STR0024,                        "string", STR0024,                            "MaoObra",      NIL, NIL, NIL, .F.) // "Mão de Obra"
    self:oSchema:addProperty("SalarioNom",   STR0025,                        "number", STR0025,                            "SalarioNom",   NIL, NIL, NIL, .F.) // "Salário Nominal"
    self:oSchema:addProperty("PercCC",       STR0026 + ' ' +STR0007 ,        "number", STR0026 + ' ' + STR0007,            "PercCC",       NIL, NIL, NIL, .F.) // "Percentual Centro de Custo"
    self:oSchema:addProperty("PercFil",      STR0026 + ' ' +STR0004 ,        "number", STR0026 + ' ' + STR0004,            "PercFil",      NIL, NIL, NIL, .F.) // "Percentual Filial"
    self:oSchema:addProperty("PercEmp",      STR0026 + ' ' +STR0027 ,        "number", STR0026 + ' ' + STR0027,            "PercEmp",      NIL, NIL, NIL, .F.) // "Percentual Empresa"
    self:oSchema:addProperty("TotEmp",       STR0030 + ' ' +STR0027 ,        "number", STR0030 + ' ' + STR0027,            "TotEmp",       NIL, NIL, NIL, .F.) // "Total Empresa"
    self:oSchema:addProperty("FilCargo",     STR0004 + ' ' +STR0035 ,        "string", STR0004 + ' ' + STR0035,            "FilCargo",     NIL, NIL, NIL, .F.) // "Filial Cargo"

Return self:oSchema

/*/{Protheus.doc} GPER340TReportsBusinessObject::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2510
    @author karina.alves
    @since 14/07/2025
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() As Variant Class GPER340TReportsBusinessObject
    // Declaração das variáveis locais
    Local jPath     As JSON    // Path parameters da requisição
    Local jResponse As JSON    // Resposta da requisição
    Local jItem     As JSON    // Item do valor do parâmetro
    Local nValues   As Numeric // Contador dos valores do parâmetro
    Local aValues   As Array   // Valores do parâmetro

    // Inicialização das variáveis
    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Verifica qual parâmetro foi solicitado
    If (cParam == "QualSal")
        AAdd(aValues, FwHTTPEncode(STR0017)) // "Mês"
        AAdd(aValues, FwHTTPEncode(STR0018)) // "Hora"
    elseif (cParam == "SobreSal")
        AAdd(aValues, FwHTTPEncode(STR0019)) // "Composto"
        AAdd(aValues, FwHTTPEncode(STR0020)) // "Base"
    elseif (cParam == "Imp")
        AAdd(aValues, FwHTTPEncode(STR0021)) // "Analítico"
        AAdd(aValues, FwHTTPEncode(STR0022)) // "Sintético" 
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL

/*/{Protheus.doc} GPER340TReportsBusinessObject::getComboBox
    Retorna descrição mão de obra
    @type Method
    @version 12.1.2510
    @author karina.alves
    @since 14/07/2025
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Static Function fBuscaDescMO(cMaoObra)
    Local cDesc As Character

    If Left(cMaoObra ,1 ) == "D"
        cDesc := STR0031    //"DIR"
    Elseif Left(cMaoObra ,1 ) == "I"
        cDesc := STR0032    //"IND"
    Else
        cDesc := "  "
    Endif

Return cDesc

/*/{Protheus.doc} GPER340TReportsBusinessObject::getComboBox
    Preenche os arrays com os valores totais por filial, empresa e CC
    @type Method
    @version 12.1.2510
    @author karina.alves
    @since 14/07/2025
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Static Function fCalculaTotais(aTCC, aTFIL, nTEmpresa)
    Local cQuery        As Character
    Local cJoinSRJ      As Character
    Local cAlias        As Character
    Local cAnoMes       As Character
    Local cSemana       As Character
    Local oExec         As Object
    Local nParamOrder   As Numeric
    Local nSalario      As Numeric
    Local nSalMes       As Numeric
    Local nSalDia       As Numeric
    Local nSalHora      As Numeric
    Local nTFilial      As Numeric
    Local nTCCTO        As Numeric
    Local nPos          As Numeric
    Local aPerAtual     As Array
    Local cFANT         As Character // Controla filial anterior
    Local cCANT         As Character // Controla CC anterior

    cQuery      := ''
    cAlias      := ''
    cJoinSRJ    := FWJoinFilial("SRA", "SRJ")
    oExec       := NIL
    nParamOrder := 1
    nSalario    := 0
    nSalMes     := 0
    nSalDia     := 0
    nSalHora    := 0
    cAnoMes     := ''
    cSemana     := ''
    aPerAtual   := {}
    cFANT       := ""
    cCANT       := ""
    nTFilial    := 0
    nTCCTO      := 0
    nPos        := 0

    //Montagem da query de busca que busca os valores
    cQuery += "SELECT "
    cQuery +=     "SRA.RA_FILIAL, "
    cQuery +=     "SRA.RA_CC, "
    cQuery +=     "SRA.RA_HRSMES, "
    cQuery +=     "SRA.RA_MAT, "
    cQuery +=     "SRA.RA_SALARIO, "
    cQuery +=     "SRA.RA_PROCES, "
    cQuery +=     "SRA.RA_CODFUNC "
    cQuery += "FROM "
    cQuery +=     RetSQLName("SRA") + " SRA "
    cQuery += " LEFT JOIN " + RetSqlName("SRJ") + " SRJ ON "
    cQuery +=       cJoinSRJ
    cQuery +=       " AND SRJ.RJ_FUNCAO = SRA.RA_CODFUNC "
    cQuery +=       " AND SRJ.D_E_L_E_T_ = ? " 
    cQuery += "WHERE "
    cQuery +=     "SRA.RA_FILIAL       BETWEEN ? AND ? " 
    cQuery +=     "AND SRA.RA_MAT      BETWEEN ? AND ? "  
    cQuery +=     "AND SRA.RA_CC       BETWEEN ? AND ? " 
    cQuery +=     "AND SRA.RA_CODFUNC  BETWEEN ? AND ? "
    cQuery +=     "AND SRA.RA_NOME     BETWEEN ? AND ? " 

    if Len(aSituacoes) > 0
        cQuery += "AND SRA.RA_SITFOLH IN (?)  " 
    EndIf

    If Len(aCategorias) > 0
        cQuery += "AND SRA.RA_CATFUNC IN (?)  " 
    EndIf
        
    cQuery +=     "AND SRA.D_E_L_E_T_ = ?  "
    cQuery +=     " ORDER BY SRA.RA_FILIAL, SRA.RA_CC " 

    cQuery := ChangeQuery(cQuery)
    oExec := FwExecStatement():New(cQuery)

    oExec:SetString(nParamOrder++, " ") 

    // Filial
    oExec:SetString(nParamOrder++, cFilialDe)
    oExec:SetString(nParamOrder++, cFilialAte)

    // Matrícula
    oExec:SetString(nParamOrder++, cMatDe)
    oExec:SetString(nParamOrder++, cMatAte)

    //CC
    oExec:SetString(nParamOrder++, cCCDe)
    oExec:SetString(nParamOrder++, cCCAte)

    //Função
    oExec:SetString(nParamOrder++, cFuncaoDe)
    oExec:SetString(nParamOrder++, cFuncaoAte)

    //Nome
    oExec:SetString(nParamOrder++, cNomeDe)
    oExec:SetString(nParamOrder++, cNomeAte)   

    if Len(aSituacoes) > 0
        oExec:SetIN(nParamOrder++, aSituacoes) 
    EndIf

    If Len(aCategorias) > 0
        oExec:SetIN(nParamOrder++, aCategorias) 
    EndIf
    
    oExec:SetString(nParamOrder++, " ")
        
    cAlias := oExec:OpenAlias()

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))

        // Posiciona SRA para chamada das funcoes do GPEXCAL1
        dbSelectArea("SRA")
        dbSetOrder(1)
        dbSeek((cAlias)->RA_FILIAL + (cAlias)->RA_MAT)

        nSalario    := 0
        nSalMes     := 0
        nSalDia     := 0
        nSalHora    := 0

        // Salário Composto
        If cSobreSal == '1'  .And. MV_MODFOL <> '2'
            // Calcula Salario Incorporado Mes , Dia , Hora do Funcionario  
            fSalInc(@nSalario,@nSalMes,@nSalHora,@nSalDia,.T.)
        Else
            If Type("cAnoMes") == "U" .OR. Empty(cAnoMes)
                If Type("aPerAtual") == "U" .OR. Empty(aPerAtual) .OR. !(aPerAtual[1,8] ==  (cAlias)->RA_PROCES)
                    fGetPerAtual( @aPerAtual, xFilial("RCH"), (cAlias)->RA_PROCES, fGetRotOrdinar() )
                    If Empty(aPerAtual)
                        fGetPerAtual( @aPerAtual, xFilial("RCH"), (cAlias)->RA_PROCES, fGetCalcRot('9') )
                    EndIf
                EndIf

                If !Empty(aPerAtual)
                    cAnoMes := AnoMes(aPerAtual[1,6])
                    cSemana := aPerAtual[1,2]
                Else
                    (cAlias)->(dbSkip())
                    Loop
                EndIf
            Endif

            cTipoRot := ""

            fSalario(@nSalario,@nSalHora,@nSalDia,@nSalMes,"A",cAnoMes, cSemana)

            If cQualSal == '1' // 1-Mes
                nSalMes := nSalario
            Else // 2-Hora
                nSalHora := Round(nSalario / (cAlias)->RA_HRSMES,MsDecimais(1))
            EndIf
        Endif

        If Empty(cFANT)
            cFANT := (cAlias)->RA_FILIAL
            cCANT := (cAlias)->RA_CC
        EndIf

        //Calcula as % por empresa, filial e CC
        nTEmpresa += IIf( cQualSal == '1' , nSalMes , nSalHora )

        If (cAlias)->RA_FILIAL == cFANT
            nTFilial += IIf( cQualSal == '1' , nSalMes , nSalHora )
        Else
            AADD(aTFIL ,{cFANT ,nTFilial})
            nTFilial := IIf( cQualSal == '1' , nSalMes , nSalHora )
        EndIf

        If (cAlias)->RA_FILIAL == cFANT .And. (cAlias)->RA_CC  == cCANT
            nTCCTO  += IIf( cQualSal == '1' , nSalMes , nSalHora )
        Else
            AADD(aTCC ,{cFANT+cCANT,nTCCTO })
            nTCCTO  := IIf( cQualSal == '1' , nSalMes , nSalHora )
        EndIf

        cCANT := (cAlias)->RA_CC
        cFANT := (cAlias)->RA_FILIAL 

        // Armazena o total por cargo
        If (nPos := AScan(aTotCargo, {|x| x[1] == (cAlias)->RA_FILIAL+' - '+(cAlias)->RA_CODFUNC})) == 0
            AAdd(aTotCargo,{(cAlias)->RA_FILIAL+' - '+(cAlias)->RA_CODFUNC , IIf( cQualSal == '1' , nSalMes , nSalHora )})
        else
            aTotCargo[nPos,2] := aTotCargo[nPos,2] + IIf( cQualSal == '1' , nSalMes , nSalHora )
        EndIf

        (cAlias)->(DBSkip()) 
    enddo

    If nTFilial > 0
        AADD(aTFIL , {cFANT ,nTFilial})
        AADD(aTCC  , {cFANT + cCANT ,nTCCTO})
    EndIf   

     // Fecha a área da tabela temporária
    If !Empty(cAlias)
        (cAlias)->(DBCloseArea())
    Endif

    // Libera os objetos da memória
    FwFreeObj(oExec)
    FwFreeArray(aPerAtual)
return Nil
