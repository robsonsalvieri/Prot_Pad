#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.sv.gpe.gper390.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,SRF", customTables="SRA,SRF",name="Relatório de Férias Vencidas no Mês", country="ALL", initialRelease="12.1.2210")

class GPER390TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getWherePar() as character
    private method getCalcSRH() as numeric

    @Get("/api/rh/smartview/v1/options/gpersv390/:param")
    Public Method getComboBox() As Variant
endclass
 
method new() class GPER390TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) //RH

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001)//"Relatório de Férias Vencidas no Mês"

    //self:setIsLookUp(.T.)
    self:setIsCBoxLookup(.T., .F.)

return self
 
method getDescription() as character class GPER390TReportsBusinessObject
return STR0003//"Objeto com os registros do Relatório de Férias Vencidas no Mês"
 
method getData(nPage as numeric, oFilter as object) as object class GPER390TReportsBusinessObject

    local cAlias as character 
    local cQuery as character  
    local cWhere as character  
    local cLastFil as character
    local cProcAnt as character
    local cFilName as character
    local cNameEmp as character
    local cFilProc as character
    local cPeriodo as character
    local cSemana as character
    local cAnoMes as character
    local cCodEmp as character
    local cOrdem as character
    local lHasNext as logical
    local lDobrada as logical
    local lTemNormal as logical
    local lTemPendente as logical
    local lRCHComp := If(FWModeAccess("RCH", 3) == "C", .T., .F.) as logical
    local nFor as numeric
    local nDiasMax as numeric
    local nDiasFalta as numeric
    local nDiasAntec as numeric
    local nDiasVenc as numeric
    local nPOrder as numeric
    local nSituacoes as numeric
    local nDiasCalc as numeric
    local dDataRef as date
    local dLimMax as date
    local dFimpAqui as date
    local dPrpAqui as date
    local jParams as json
    local aPerFerias as array
    local aCategoria as array    
    local aSituacoes  as array
    local cCustomFields as character // Campos customizados no formato SQL
    local jData as JSON // Informações a serem adicionadas no ON
    local aAllFields as array     // Estrutura de todos os campos do ON
    local aPDFields as array     // Campos que podem ser exibidos de acordo com a LGPD
    local lObfuscated as logical   // Flag que indica se tem algum campo para ofuscar

    //Variaveis de Acesso do Usuario
	Local cAcessaSRA := &( " { || " + ChkRH( "GPER390" , "SRA" , "2" ) + " } " )

    Private aCodFol := {}
    Private aTabFer := {} 

    Static oStatement
    Static __cEmpAnt := cEmpAnt
    Static __cWhere  := ""

    /*
	ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	³ Carregar os Mnemonicos 	                                   ³
	ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
	SetMnemonicos(NIL,NIL,.T.)

    jParams         := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    cWhere          := ""
    lHasNext        := .F.
    lDobrada        := .F.
    cLastFil        := "_cLastFil"
    cOrdem          := "SRA.RA_FILIAL, SRA.RA_MAT " //Ordenação 1: Matricula
    nFor            := 0
    dFimpAqui       := cTod("")
    dPrpAqui        := cTod("")
    aPerFerias      := {}
    cTipoRot        := ""
    nPOrder         := 4
    nSituacoes      := 0
    aCategoria      := {}
    aSituacoes      := {}

    cCustomFields   := ""
    jData           := NIL 
    aAllFields      := {}
    aPDFields       := {}
    lObfuscated     := .F.

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields       := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated     := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields      := self:getStructFields()
    
    self:setPageSize(100)

    cWhere := self:getWherePar(jParams) 

    If !Empty(jParams['FERDOBRO', 1])
		lDobrada := jParams['FERDOBRO', 1] == "1"
	EndIf

    If !Empty(jParams['DATALIMITE', 1])
		nDiasMax := jParams['DATALIMITE', 1]
	EndIf

    If !Empty(jParams['ORDER', 1])
		cOrdem := Iif(jParams['ORDER', 1] == "1", "SRA.RA_FILIAL, SRA.RA_MAT", "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_MAT")
	EndIf

    If !Empty(jParams['MESREF', 1])
        dDataRef := StoD(StrTran( SubStr(jParams['MESREF', 1],1, 10), "-" ))
	EndIf
    
    cQuery := "SELECT  SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_CC, SRA.RA_PROCES, MIN(SRF.RF_DATABAS) RF_DATABAS"
    cQuery += " ? "
    cQuery += " FROM    " + RetSqlName("SRA") + " SRA "
    cQuery += "         INNER JOIN " + RetSqlName("SRF") + " SRF ON SRF.RF_FILIAL = SRA.RA_FILIAL AND SRF.RF_MAT = SRA.RA_MAT AND SRF.D_E_L_E_T_ = ? "
    cQuery += " WHERE   SRF.RF_STATUS = ? "
    
    If !Empty(cWhere)
        cQuery += cWhere    
    EndIf

    cQuery += " AND SRA.D_E_L_E_T_	= ? "
    cQuery += " GROUP BY SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_CC, SRA.RA_PROCES"
    cQuery += " ? " 

    cQuery += " ORDER BY " + cOrdem

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(cWhere == __cWhere)
        cQuery     := ChangeQuery(cQuery)
        __cEmpAnt  := cEmpAnt
        __cWhere   := cWhere
        oStatement := FwExecStatement():New(cQuery)
    EndIf

    aCategoria    := IIf(jParams:hasProperty("CATFUNC"), jParams["CATFUNC"], {})
    aSituacoes    := IIf(jParams:hasProperty("SITFOL"),  jParams["SITFOL"],  {})

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nSituacoes := 1 To Len(aSituacoes)
        aSituacoes[nSituacoes] := IIf(aSituacoes[nSituacoes] == NIL .Or. Empty(aSituacoes[nSituacoes]), " ", aSituacoes[nSituacoes])
    Next nSituacoes

    // Captura e define os campos personalizados das tabelas SRA e SRF
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRF"})
    oStatement:SetUnsafe(1, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados

    oStatement:SetString(2, ' ')
    oStatement:SetString(3, '1')
    
    If !Empty(jParams['FILDE', 1])
		oStatement:SetString(nPOrder++, jParams["FILDE", 1])
	EndIf
	If !Empty(jParams['FILATE', 1])
		oStatement:SetString(nPOrder++, jParams["FILATE", 1])
	EndIf
    If !Empty(jParams['MATDE', 1])
		oStatement:SetString(nPOrder++, jParams["MATDE", 1])
	EndIf
    If !Empty(jParams['MATATE', 1])
		oStatement:SetString(nPOrder++, jParams["MATATE", 1])
	EndIf
    If !Empty(jParams['CCDE', 1])
		oStatement:SetString(nPOrder++, jParams["CCDE", 1])
	EndIf
    If !Empty(jParams['CCATE', 1])
		oStatement:SetString(nPOrder++, jParams["CCATE", 1])
	EndIf
    If !Empty(jParams['CATFUNC'])
        oStatement:SetIn(nPOrder++, aCategoria)
    EndIf 
    If !Empty(jParams['SITFOL'])
        oStatement:SetIn(nPOrder++, aSituacoes)
    EndIf 

    oStatement:SetString(nPOrder++, ' ') //D_E_L_E_T_

    oStatement:SetUnsafe(nPOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // GROUP BY

    cAlias := oStatement:OpenAlias()
    dbSelectArea( cAlias )

    While !(cAlias)->(Eof())

        //Consiste Filiais e Acessos
        If cLastFil != ( cAlias )->RA_FILIAL
            cLastFil := ( cAlias )->RA_FILIAL
            cFilName := RTrim( FWEmpName(cCodEmp) ) + " / " + RTrim( FWFilRazSocial(, (cAlias)->RA_FILIAL) )
            cNameEmp := RTrim( FWEmpName(cCodEmp) )
            If !((cAlias)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
                (cAlias)->( dbSkip() )
                Loop
            EndIf
        EndIf

        SRA->(dbSeek( (cAlias)->(RA_FILIAL) + (cAlias)->(RA_MAT) ) )

        aPerFerias := {}
        Calc_Fer( @aPerFerias, dDataRef,,,,,,.F., StoD((cAlias)->(RF_DATABAS)))

        If cProcAnt != (cAlias)->RA_PROCES .OR. (!lRCHComp .AND. cProcAnt == (cAlias)->RA_PROCES .AND. cFilProc != (cAlias)->RA_FILIAL)
            cFilProc  := (cAlias)->RA_FILIAL
			cProcAnt  := (cAlias)->RA_PROCES 
			fGetLastPer(@cPeriodo, @cSemana, (cAlias)->RA_PROCES, fGetCalcRot('3'), .T., .F., @cAnoMes)
        EndIf

        For nFor := 1 to Len(aPerFerias)

            lTemNormal   := !(Month(aPerFerias[nFor][1]) <> Month(dDataRef) .Or. Year(aPerFerias[nFor][1]) >= Year(dDataRef))
            lTemPendente := !(Month(aPerFerias[nFor][13]) <> Month(dDataRef) .Or. Year(aPerFerias[nFor][13]) >= Year(dDataRef))

            If !lTemNormal .And. !lTemPendente
                Loop
            EndIf

            dLimMax := CtoD("")

            If cPeriodo == AnoMes(dDataRef)
                nDiasCalc := self:getCalcSRH(cAlias, dDataRef, aPerFerias[nFor][1])
            EndIf

            If lDobrada //Férias dobradas
                dFimpAqui := aPerFerias[nFor][1]
                dPrpAqui  := fCalcFimAq(dFimpAqui+1)
                dLimMax	  := dPrpAqui - nDiasMax
                If dLimMax + 365 > dDataRef
                    Loop
                EndIf
            EndIf

            nDiasFalta := aPerFerias[nFor][15] //Dias de Faltas Vencidas
            nDiasAntec := aPerFerias[nFor][14] //Dias de Ferias Antecipadas
            nDiasVenc  := aPerFerias[nFor][03]  - nDiasFalta - nDiasAntec - nDiasCalc //Dias de ferias vencidas

            If nDiasVenc > 0
                jData := {;
                        "FILNAME":    cFilName,;
                        "NAMEEMP":    cNameEmp,;
                        "CODEMP":     cCodEmp,;
                        "RA_FILIAL":  SRA->RA_FILIAL,;
                        "RA_MAT":     SRA->RA_MAT,;
                        "RA_NOME":    SRA->RA_NOME,;
                        "RA_CC":      SRA->RA_CC,;
                        "RF_IVENPEN": If(lTemPendente, totvs.framework.treports.date.dateToTimeStamp(aPerFerias[nFor][12]), totvs.framework.treports.date.dateToTimeStamp(aPerFerias[nFor][1])),;
                        "RF_DVENPEN": If(lTemPendente, aPerFerias[nFor][11], Max(nDiasVenc,0)),;
                        "DI_FALTA":   If(lTemPendente, 0, nDiasFalta),;
                        "FE_ANTE":    If(lTemPendente, 0, nDiasAntec + nDiasCalc)}

                // Adiciona os campos customizados no JSON
                GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

                // Adiciona as informações da linha
                self:oData:appendData(jData)
            EndIf 

            nDiasCalc := 0

        Next nFor

        (cAlias)->( dbSkip() )
    EndDo

    (cAlias)->(DbCloseArea())

    // Libera os objetos da memória
    jData := NIL
    FwFreeObj(jData) 

return self:oData

method getSchema() as object class GPER390TReportsBusinessObject
    local aFieldsSRA as array

    aFieldsSRA := { "RA_FILIAL", "RA_MAT", "RA_NOME", "RA_CC" }
    aFieldsSRF := { "RF_IVENPEN", "RF_DVENPEN" }
    
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("SRF", aFieldsSRF)

    self:oSchema:addProperty("FILNAME", "Nome do Grupo/Filial", "string", "D. Grupo/Filial", "FILNAME",,,, .F.)
    self:oSchema:addProperty("NAMEEMP", "Nome Empresa", "string", "Nome. Empresa", "NAMEEMP",,,, .F.)
    self:oSchema:addProperty("CODEMP", "Cod Empresa", "string", "Cod. Empresa", "CODEMP",,,, .F.)
    self:oSchema:addProperty("DI_FALTA", "Dias Falta", "number", "D. Faltas", "nDiasFalta")
    self:oSchema:addProperty("FE_ANTE", "Ferias Antecipadas", "number", "Fer. Antecip", "nDiasAntec")
    
    self:addParameter("FILDE","Filial De ?","string", .F.) 
    self:addParameter("FILATE","Filial Até ?","string", .F.) 
    self:addParameter("CCDE","Centro de Custo De ?","string", .F.)
    self:addParameter("CCATE","Centro de Custo Até ?","string", .F.)
    self:addParameter("MATDE","Matricula De ?","string", .F.) 
    self:addParameter("MATATE","Matricula Até ?","string", .F.) 
    self:addParameter("SITFOL","Situação ?","string", .T.)  
    self:addParameter("CATFUNC","Categoria ?","string", .T.)  
    self:addParameter("MESREF","Data Ref. ?","date", .F.) 
    self:addParameter("FERDOBRO","Somente Férias Dobrada ?","string", .F.)
    self:addParameter("DATALIMITE","Dias p/somar em Data Ref. ?","number", .F.)
    self:addParameter("ORDER", "Ordem de Impressão ?", "string", .F.)

    //setCustomURL disponível na LIB 20231121.
    If FWLibVersion() >= "20231121" 
        self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("SITFOL", "api/framework/v1/genericLookupService/smartview/31", 2)
        self:setCustomURL("CATFUNC", "api/framework/v1/genericLookupService/smartview/28", 2)
        self:setCustomURL("FERDOBRO", "/api/rh/smartview/v1/options/gpersv390/FERDOBRO", 1)
        self:setCustomURL("ORDER", "/api/rh/smartview/v1/options/gpersv390/ORDER", 1)
    EndIf    

return self:oSchema

/*/{Protheus.doc} getWherePar
Metodo que retorna a consulta conforme os parâmetros
@type  Metodo
@author Bruno Costa
@return character
@since 22/12/2023
/*/
method getWherePar( jPar as json) as character class GPER390TReportsBusinessObject
    
    local cRet as character

    If !Empty(jPar['FILDE', 1])
		cRet += " AND SRA.RA_FILIAL >= ?"
	EndIf
	If !Empty(jPar['FILATE', 1])
		cRet += " AND SRA.RA_FILIAL <= ?"
	EndIf
    If !Empty(jPar['MATDE', 1])
		cRet += " AND SRA.RA_MAT >= ?"
	EndIf
    If !Empty(jPar['MATATE', 1])
		cRet += " AND SRA.RA_MAT <= ?"
	EndIf
    If !Empty(jPar['CCDE', 1])
		cRet += " AND SRA.RA_CC >= ?"
	EndIf
    If !Empty(jPar['CCATE', 1])
		cRet += " AND SRA.RA_CC <= ?"
	EndIf
    If !Empty(jPar['CATFUNC'])
        cRet += " AND SRA.RA_CATFUNC IN (?)"
    EndIf 
    If !Empty(jPar['SITFOL'])
        cRet += " AND SRA.RA_SITFOLH IN (?)"
    EndIf 

Return cRet

/*/{Protheus.doc} getComboBox
Metodo que retorna o combobox dos parâmetros
@type  Metodo
@author Bruno Costa
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() as Variant Class GPER390TReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    Local cParam    As Character 
    local aValues   as Array   
     

    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    jItem     := NIL
    nValues   := 0
    cParam    := ""
    aValues   := {}

    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    jResponse["data"] := {}

    // Monta o vetor de valores do parâmetro
    If cParam == "ORDER"
        AAdd(aValues, EncodeUTF8("Filial + Matrícula"))
        AAdd(aValues, EncodeUTF8("Filial + Matrícula + Centro de Custo"))
    Else
        AAdd(aValues, EncodeUTF8("Sim"))
        AAdd(aValues, EncodeUTF8("Não"))   
    EndIf 

    If (Len(aValues) > 0)
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)

Return NIL

/*/{Protheus.doc} getCalcSRH
Metodo que retorna a quantidade de dias de férias calculados no mês
@type  Metodo
@author Bruno Costa
@return numeric
@since 03/01/2025
/*/
method getCalcSRH(cAlias as character, dDataRef as date, dtBaseFer as date) as numeric class GPER390TReportsBusinessObject

    local aAreaSRH as array
    local nDiasCalc as numeric
    
    aAreaSRH := SRH->(GetArea()) //RH_FILIAL+RH_MAT+DTOS(RH_DATABAS)+DTOS(RH_DATAINI)
    SRH->(dbSetOrder(1))

    If SRH->(dbSeek((cAlias)->RA_FILIAL+(cAlias)->RA_MAT+dToS(dtBaseFer)))
        While SRH->(!Eof()) .And. SRH->RH_FILIAL+SRH->RH_MAT+dToS(SRH->RH_DATABAS) == (cAlias)->RA_FILIAL+(cAlias)->RA_MAT+dToS(dtBaseFer)
            If AnoMes(SRH->RH_DATAINI) == AnoMes(dDataRef)
                nDiasCalc += SRH->RH_DFERIAS + SRH->RH_DABONPE
            EndIf
            SRH->(dbSkip())
        EndDo
    EndIf

    RestArea( aAreaSRH )

return nDiasCalc
