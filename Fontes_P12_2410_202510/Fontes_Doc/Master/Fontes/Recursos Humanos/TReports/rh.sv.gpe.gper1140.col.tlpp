#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'rh.sv.gpe.gper1140.col.ch'

#DEFINE SX1GRUPO "GPR1140"

namespace totvs.protheus.rh.treportsintegratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.com.customerAaccountStatement

@description Clase para la creación del Objeto de Negocio para TReports
@author oscar.lopez
@since 04/06/205
@version 1.0
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider(active=.T., team='SIGAGPE', tables='SRA, SRF, SRV', name="Informe de Vacaciones", country='COL', initialRelease='12.1.2210', customTables='SRA, SRF, SRV')
class GPER1140TReportsBusinessObjectCOL from totvs.framework.treports.integratedprovider.IntegratedProvider

	Public Method new() as object
	Public Method getData() as object
	Public Method getSchema() as object

	Protected data aFields		as array
	Protected data aStruct		as array
	Protected data nRecno		as numeric
	Protected data jItems		as json
	Protected data lExistDic	as logical

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instancia de la Clase

@Return object: self
@author oscar.lopez
@since 19/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class GPER1140TReportsBusinessObjectCOL

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0001)  // 'Informe de Vacaciones'

	//Define a descrição do Objeto de Negócio
	self:setDescription(STR0001)  // 'Informe de Vacaciones'

	// Define a Área
	self:appendArea(STR0002) // 'Gestión de Personal'

	// Define se as perguntas terao lookup
	self:setIsLookUp(.T.)

	//Indica o pergunte que será utilizado no relatório
    self:setPergunte(SX1GRUPO)

	// Indica o pergunte que será utilizado no relatório
	self:lExistDic := .T.
	If !self:setPergunte(SX1GRUPO)
		self:setErrorStatus(400,"Smart View", I18N(STR0003, {SX1GRUPO})) //"¡El Grupo de Preguntas GPR1140, no fue encontrado!"
		FWLogMsg("WARN", , "Smart View", , , , I18N(STR0003, {SX1GRUPO}), , , ) //"¡El Grupo de Preguntas GPR1140, no fue encontrado!"
		self:lExistDic := .F.
	EndIf

	self:aFields := {"RA_NOME", "RA_CIC", "RA_MAT", "RA_ADMISSA", "RV_COD", "RV_DESCDET", "RF_DFERANT", "RF_DFERAAT", "RA_SALARIO"}

	self:aStruct := getStrutObj(self:aFields)

Return (self)

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negocio

@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author oscar.lopez
@since 04/06/205
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class GPER1140TReportsBusinessObjectCOL

	Local nX			as numeric
	Local jParams 		as json
	Local oQryVac		as object
	Local cQryVac		as character
	Local cAlias		as character
	Local cMatIni		as character
	Local cMatFin		as character
	Local nParQry		as numeric
	Local cAnioCor		as character
	Local cTipoSGBD		as character
	Local cExpDataBa	as character
	Local cFilExec		as character
	Local cEmpExec		as character

	Local nDiasProp		as numeric
	Local nDiasTom		as numeric
	Local nSalario		as numeric
	Local dFechaIng		as date
	Local dFechaFin		as date
	Local nDias			as numeric
	Local nSumAno		as numeric
	Local nDiaPend		as numeric
	Local nResult		as numeric
	Local nContador		as numeric

	If !self:lExistDic
	   Return self:oData 
	EndIf 

	//Obtiene los datos de los parámetros
	jParams := oFilter:getParameters()

	nDiasProp	:= 0
	nSalario	:= 0
	dFechaIng	:= CtoD("//")
	dFechaFin	:= CtoD("//")
	cAnioCor	:= AllTrim(Str(Year(Date()) - 2))
	cTipoSGBD	:= AllTrim(Upper(TcGetDb()))
	cExpDataBa	:= "SUBSTR(RF_DATABAS, 1, 4) "
	nDias		:= 0
	nSumAno		:= 0
	nDiaPend	:= 0
	nDiasTom	:= 0
	nResult		:= 0
	cQryVac		:= ""
	cAlias		:= ''
	nX			:= 0
	nParQry		:= 1
	dFechaFin	:= SToD(StrTran(SubStr(jParams['MV_PAR01'][1],1,10),"-",""))
	cMatIni		:= jParams['MV_PAR02'][1]
	cMatFin		:= jParams['MV_PAR03'][1]
	cFilExec	:= AllTrim(FwFilialName())
	cEmpExec	:= AllTrim(FwEmpName(cEmpAnt))

	If cTipoSGBD <> "ORACLE"
		cExpDataBa := "SUBSTRING(RF_DATABAS, 1, 4) "
	EndIf

	cQryVac += " SELECT DISTINCT SRA.RA_NOME, SRA.RA_CIC, SRA.RA_MAT, SRA.RA_ADMISSA, SRV.RV_COD, SRV.RV_DESCDET "
	cQryVac += " , SUM(SRF.RF_DFERANT) AS RF_DFERANT, SUM(SRF.RF_DFERANT) AS RF_DFERAAT, SRA.RA_SALARIO "
	cQryVac += " FROM ? SRF "
	cQryVac += " INNER JOIN ? SRA "
	cQryVac += " ON SRA.RA_MAT = SRF.RF_MAT AND SRA.D_E_L_E_T_ = ? AND SRA.RA_FILIAL = ? "
	cQryVac += " INNER JOIN ? SRV "
	cQryVac += " ON SRV.RV_COD = SRF.RF_PD AND SRV.D_E_L_E_T_ = ? AND SRV.RV_FILIAL = ? "
	cQryVac += " WHERE "
	cQryVac += cExpDataBa + " <= ? "
	cQryVac += " AND SRF.D_E_L_E_T_ = ? AND SRF.RF_FILIAL = ? "
	cQryVac += " AND SRA.RA_MAT BETWEEN ? AND ? "
	cQryVac += " GROUP BY SRA.RA_NOME, SRA.RA_CIC, SRA.RA_MAT, SRA.RA_ADMISSA, SRV.RV_COD, SRV.RV_DESCDET, SRA.RA_SALARIO "
	cQryVac += " ORDER BY SRA.RA_NOME ASC "

	oQryVac := FwExecStatement():New(cQryVac)
	oQryVac:SetUnsafe(nParQry++, RetSQLName("SRF")	)
	oQryVac:SetUnsafe(nParQry++, RetSQLName("SRA")	)
	oQryVac:SetString(nParQry++, ' '				) // SRA.D_E_L_E_T_
	oQryVac:SetString(nParQry++, FwxFilial("SRA")	) // Sucursal SRA
	oQryVac:SetUnsafe(nParQry++, RetSQLName("SRV")	)
	oQryVac:SetString(nParQry++, ' '				) // SRV.D_E_L_E_T_
	oQryVac:SetString(nParQry++, FwxFilial("SRV")	) // Sucursal SRV
	oQryVac:SetString(nParQry++, cAnioCor			) // Dos Años Menos que el año presente
	oQryVac:SetString(nParQry++, ' '				) // SRF.D_E_L_E_T_
	oQryVac:SetString(nParQry++, FwxFilial("SRF")	) // Sucursal SRF
	oQryVac:SetString(nParQry++, cMatIni			) // De matricula
	oQryVac:SetString(nParQry++, cMatFin			) // A Matricula

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAlias := oQryVac:OpenAlias()

	nContador := 0

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !(cAlias)->(EoF())
		nContador++
		self:jItems := JsonObject():new()

		For nX := 1 To Len(self:aStruct)
			If (self:aStruct[nX][3] == 'date')
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp(StoD(( cAlias )->&(self:aStruct[nX][5])))
			Else
				self:jItems[self:aStruct[nX][1]] := (cAlias )->&( self:aStruct[nX][5])
			Endif
		Next nX

		nDiasTom	:= (cAlias)->RF_DFERANT
		nDiasProp	:= (cAlias)->RF_DFERAAT
		nSalario	:= (cAlias)->RA_SALARIO
		dFechaIng	:= SToD((cAlias)->RA_ADMISSA)
		nDias		:= Dias360(dFechaIng, dFechaFin ) + 1
		nSumAno		:= Round((nDias / 360) * 15, 2)
		nDiaPend	:= Round((nSumAno - nDiasProp), 2)
		nResult		:= Round((nSalario / 30 * nDiaPend), 2)

		self:jItems["NDIATOT"] := nSumAno
		self:jItems["NDIAPEN"] := nDiaPend
		self:jItems["NVALVAC"] := nResult
		self:jItems["NDIATOM"] := nDiasTom

		If nContador == 1
			self:jItems["FilialExec"] := cFilExec
			self:jItems["EmpresExec"] := cEmpExec
		EndIf

		// Inclui os dados no objeto paea retorno ao SmartView
		self:oData:appendData(self:jItems)

		(cAlias )->(DBSkip())

	EndDo
	(cAlias)->(DBCloseArea())

	FWFreeObj(oQryVac)

Return (self:oData)

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema

@description Retorna la estructura de los campos
@return object: self:oSchema
@author oscar.lopez
@since 04/06/205
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class GPER1140TReportsBusinessObjectCOL

	Local nIteration as numeric

	// Adiciona las propiedades de los campos que serán retornados para Smart View
	For nIteration := 1 To Len( self:aStruct )
		self:addProperty(self:aStruct[nIteration][1], self:aStruct[nIteration][2], self:aStruct[nIteration][3], self:aStruct[nIteration][4], self:aStruct[nIteration][5])
	Next nIteration

	self:addProperty("NDIAPEN", STR0007, "number", STR0007, "NDIAPEN") //"Días Pendientes"
	self:addProperty("NDIATOT", STR0004, "number", STR0004, "NDIATOT") // "Total Vac."
	self:addProperty("NDIATOM", STR0005, "number", STR0005, "NDIATOM") // "Vac. Tomadas"
	self:addProperty("NVALVAC", STR0006, "number", STR0006, "NVALVAC") // "Valor vacaciones"
	self:addProperty("FilialExec",	STR0008, "string", STR0008, "FilialExec") //"Sucursal en ejecución"
	self:addProperty("EmpresExec",	STR0009, "string", STR0009, "EmpresExec") //"Empresa en ejecución"

Return (self:oSchema)
