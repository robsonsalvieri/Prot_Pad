#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.sv.gpe.gper1033.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SRV, SRR, SRH", name="Recibo de Férias", country="ALL", initialRelease="12.1.2210")
class GPER1033TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    public method getMedia() as variant
    private method getWherePar() as character
    private method geraREQ() as variant
    
    @Get("/api/rh/smartview/v1/options/gpersv1033/:param")
    Public Method getComboBox() as variant
endclass
 
method new() class GPER1033TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) //"RH"

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //"Recibo de Férias"

return self
 
method getDescription() as character class GPER1033TReportsBusinessObject
return STR0003 //"Objeto com os registros do Recibo de Férias"
 
method getData(nPage as numeric, oFilter as object) as object class GPER1033TReportsBusinessObject

    local cAlias as character 
    local cQuery as character  
    local cWhere as character  
    local cLastFil as character
    local cOrdem as character
    local cUltCalc as character
    local cExtLiq as character
    local cFilRec as character
    local cMatRec as character
    local nProvento as numeric
    local nDesconto as numeric
    local nTotDes as numeric
    local nTotProv as numeric
    local nLiqFer as numeric
    local nPOrder as numeric
    local jParams as json
    local jData :=  JSonObject():new() as json 
    local aInfo as array
    local aTipoCod := {"1", "2"} as array
	local cAcessaSRA := &( " { || " + ChkRH( "GPER1033" , "SRA" , "2" ) + " } " ) as character
    local dDtIni := CtoD("//") as date
    local dDtFim := CtoD("//") as date
    local lTAE as logical

    Private lGp1033 as logical

    Static oStatement
    Static __cEmpAnt := cEmpAnt
    Static __cWhere  := ""

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    If !Empty(jParams['IMPMEDIA', 1]) .And. Len(jParams['IMPMEDIA', 1]) > 1
        lGp1033 := .T.
        lTAE := SubStr(jParams['IMPMEDIA', 1], 2, 1) == "2"
        jParams['IMPMEDIA', 1] := SubStr(jParams['IMPMEDIA', 1], 1, 1)
    EndIf

    cWhere  := self:getWherePar(jParams)
    cOrdem  := "SRA.RA_FILIAL, SRA.RA_MAT, SRH.RH_DATAINI, SRH.RH_DATAFIM" //Ordenação 1: Matricula
    nPOrder := 1

    If !Empty(jParams['ORDER', 1])
		cOrdem := Iif(jParams['ORDER', 1] == "1", "SRA.RA_FILIAL, SRA.RA_MAT, SRH.RH_DATAINI, SRH.RH_DATAFIM", Iif(jParams['ORDER', 1] == "2", "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_MAT, SRH.RH_DATAINI, SRH.RH_DATAFIM", ;
                  Iif(jParams['ORDER', 1] == "3", "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_NOME, SRA.RA_MAT, SRH.RH_DATAINI, SRH.RH_DATAFIM", "SRA.RA_FILIAL, SRA.RA_NOME, SRA.RA_MAT, SRH.RH_DATAINI, SRH.RH_DATAFIM" )))
	EndIf
    
    cQuery := "SELECT SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_CC, SRA.RA_NOME, SRA.RA_CODFUNC, SRA.RA_DEPTO, SRA.RA_CATFUNC, SRA.RA_SALARIO, SRA.RA_BCDEPSA, SRA.RA_CTDEPSA, "
    cQuery += "SRH.RH_FILIAL, SRH.RH_MAT, SRH.RH_DATABAS, SRH.RH_DBASEAT, SRH.RH_DATAINI, SRH.RH_DATAFIM, SRH.RH_DTRECIB, SRH.RH_DFERIAS, SRH.RH_DABONPE, SRH.RH_DIALREM, SRH.RH_DIALRE1, SRH.RH_PERIODO, SRH.RH_NPAGTO, SRH.RH_ROTEIR, "
    cQuery += "SRR.RR_PD, SRR.RR_VALOR, SRR.RR_DATA, SRR.RR_MAT, SRR.RR_FILIAL, SRR.RR_HORAS, "
    cQuery += "SRV.RV_COD, SRV.RV_TIPOCOD, SRV.RV_DESC "
    cQuery += "FROM " + RetSqlName("SRA") + " SRA "
    cQuery += "INNER JOIN " + RetSqlName("SRH") + " SRH ON SRH.RH_FILIAL = SRA.RA_FILIAL AND SRH.RH_MAT = SRA.RA_MAT AND SRH.D_E_L_E_T_ = ? "
    cQuery += "INNER JOIN " + RetSqlName("SRR") + " SRR ON SRR.RR_FILIAL = SRA.RA_FILIAL AND SRR.RR_MAT = SRA.RA_MAT AND SRR.RR_DATA = SRH.RH_DATAINI AND SRR.D_E_L_E_T_ = ? "
    cQuery += "INNER JOIN " + RetSqlName("SRV") + " SRV ON " + FWJoinFilial( "SRV", "SRR" ) + " AND SRV.RV_COD = SRR.RR_PD  AND (RV_TIPOCOD IN (?)) AND SRV.RV_CODFOL != ? AND SRV.D_E_L_E_T_ = ? "
    cQuery += "WHERE "

    If !Empty(cWhere)
        cQuery += cWhere    
    EndIf

    cQuery += "SRA.D_E_L_E_T_ = ? "
    cQuery += "ORDER BY " + cOrdem

    If oStatement == Nil .Or. !(__cWhere == cWhere) .Or. __cEmpAnt <> cEmpAnt 
        cQuery     := ChangeQuery(cQuery)
        __cEmpAnt  := cEmpAnt
        __cWhere   := cWhere
        oStatement := FwExecStatement():New(cQuery)
    EndIf

    oStatement:SetString(nPOrder++, " ")
    oStatement:SetString(nPOrder++, " ")
    oStatement:SetIn(nPOrder++, aTipoCod)
    oStatement:SetString(nPOrder++, "0102")
    oStatement:SetString(nPOrder++, " ")

    If !("RA_FILIAL" $ jParams['FILDE', 1])
        If !Empty(jParams['FILDE', 1])
            oStatement:SetString(nPOrder++, jParams["FILDE", 1])
        EndIf
        If !Empty(jParams['FILATE', 1])
            oStatement:SetString(nPOrder++, jParams["FILATE", 1])
        EndIf
    EndIf  
    If !("RA_MAT" $ jParams['MATDE', 1])  
        If !Empty(jParams['MATDE', 1])
            oStatement:SetString(nPOrder++, jParams["MATDE", 1])
        EndIf
        If !Empty(jParams['MATATE', 1])
            oStatement:SetString(nPOrder++, jParams["MATATE", 1])
        EndIf
    EndIf   
    If !("RA_CC" $ jParams['CCDE', 1]) 
        If !Empty(jParams['CCDE', 1])
            oStatement:SetString(nPOrder++, jParams["CCDE", 1])
        EndIf
        If !Empty(jParams['CCATE', 1])
            oStatement:SetString(nPOrder++, jParams["CCATE", 1])
        EndIf
    EndIF    
    If !Empty(jParams['DTPGODE', 1])
        oStatement:setDate(nPOrder++, SToD(StrTran(SubStr(jParams['DTPGODE', 1], 1, 10), "-" )))
	EndIf
    If !Empty(jParams['DTPGOATE', 1])
        oStatement:setDate(nPOrder++, SToD(StrTran(SubStr(jParams['DTPGOATE', 1], 1, 10), "-" )))
	EndIf
    If !Empty(jParams['PERFERDE', 1])
        oStatement:setDate(nPOrder++, SToD(StrTran(SubStr(jParams['PERFERDE', 1], 1, 10), "-" )))
	EndIf
    If !Empty(jParams['PERFERATE', 1])
        oStatement:setDate(nPOrder++, SToD(StrTran(SubStr(jParams['PERFERATE', 1], 1, 10), "-" )))
	EndIf
    
    oStatement:SetString(nPOrder++, " ")

    cAlias := oStatement:OpenAlias()  

    //Retorna vazio para tratar mensagem no design
    If (cAlias)->(Eof())
        self:oData:appendData({"LMSN": .F.})
        return self:oData
    EndIf

    While !(cAlias)->(Eof())

        //Consiste Filiais e Acessos
        If cLastFil != ( cAlias )->RA_FILIAL
            cLastFil := (cAlias)->RA_FILIAL
            fInfo(@aInfo, (cAlias)->RA_FILIAL)
            If !((cAlias)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
                (cAlias)->( dbSkip() )
                Loop
            EndIf
        EndIf

        nProvento := 0.00
        nDesconto := 0.00

        If cUltCalc != (cAlias)->(RA_FILIAL+RA_MAT+RH_DATAINI)
            cUltCalc := (cAlias)->(RA_FILIAL+RA_MAT+RH_DATAINI)
            nTotDes  := 0
            nTotProv := 0
            jData    := {;
                "branchCode":            (cAlias)->RA_FILIAL,;
                "employeeName":          (cAlias)->RA_NOME,;
                "employeeCode":          (cAlias)->RA_MAT,;
                "costCenterCode":        (cAlias)->RA_CC,;
                "functionCode":          RTrim((cAlias)->RA_CODFUNC) + " - " + Alltrim(fDesc('SRJ', (cAlias)->RA_CODFUNC, 'RJ_DESC',, (cAlias)->RA_FILIAL)),;
                "departmentName":        If(!Empty((cAlias)->RA_DEPTO), RTrim((cAlias)->RA_DEPTO) + " - " + Alltrim(fDesc('SQB', (cAlias)->RA_DEPTO, 'QB_DESCRIC',, (cAlias)->RA_FILIAL)), ""),;
                "employeeTypeCode":      fDesc("SX5", "28"+(cAlias)->RA_CATFUNC, fDescSX5(2), 20),;
                "employeeSalary":        (cAlias)->RA_SALARIO,;
                "branchCity":            RTrim(aInfo[05]),; 
                "branchName":            RTrim(aInfo[03]),; 
                "paymentbankcode":       Substr((cAlias)->RA_BCDEPSA, 1, 3),;
                "branchbankpayment":     Substr((cAlias)->RA_BCDEPSA, 4, 5),;
                "paymentaccountnumber":  (cAlias)->RA_CTDEPSA,;
                "startAcquisition":      totvs.framework.treports.date.stringToTimeStamp((cAlias)->RH_DATABAS),;
                "endAcquisition":        totvs.framework.treports.date.stringToTimeStamp((cAlias)->RH_DBASEAT),;
                "startVacation":         totvs.framework.treports.date.stringToTimeStamp((cAlias)->RH_DATAINI),;
                "endVacation":           totvs.framework.treports.date.stringToTimeStamp((cAlias)->RH_DATAFIM),;
                "vacationPaymentDate":   totvs.framework.treports.date.stringToTimeStamp((cAlias)->RH_DTRECIB),;
                "vacationNumberDays":    (cAlias)->RH_DFERIAS,;
                "cashBenefitNumberDays": (cAlias)->RH_DABONPE,;
                "numberOfDaysPaidLeave": (cAlias)->(RH_DIALRE1 + RH_DIALREM),;
                "LMSN": .T.}

            jData["ADetMedia"] := {}
            jData["ADetRec"]   := {}

            If !Empty(jParams['IMPMEDIA', 1]) .And. jParams['IMPMEDIA', 1] == "1"
                self:getMedia(cAlias, @jData)
            EndIf
            
        EndIf  
        
        // Verifica se a verba é provento ou desconto
        If (cAlias)->RR_DATA == (cAlias)->RH_DATAINI 
            If (cAlias)->RV_TIPOCOD == "1"
                nProvento := (cAlias)->RR_VALOR
                nTotProv  := nTotProv + nProvento
            ElseIf (cAlias)->RV_TIPOCOD == "2"
                nDesconto := (cAlias)->RR_VALOR
                nTotDes   := nTotDes + nDesconto
            EndIf
        EndIf

        nLiqFer := nTotProv - nTotDes
        cExtLiq := EXTENSO(nLiqFer, .F., 1)
        cExtLiq := Iif(!Empty(cExtLiq), cExtLiq, "ZERO CENTAVO") 

        aAdd(jData["ADetRec"], {;
            "eventCode":           (cAlias)->RR_PD,;
            "eventHour":           (cAlias)->RR_HORAS,;
            "eventDescription":    (cAlias)->RV_DESC,;
            "sumIncomeEmployee":   nProvento,;
            "sumDiscount":         nDesconto,;
            "totalamountearnings": nTotProv,;
            "sumDiscountShift":    nTotDes,;
            "liqFer":              nLiqFer,;
            "extLiqFer":           AllTrim(cExtLiq)})
        
        cFilRec := (cAlias)->RA_FILIAL
        cMatRec := (cAlias)->RA_MAT
        dDtIni  := sTod((cAlias)->RH_DATAINI)
        dDtFim  := sTod((cAlias)->RH_DATAFIM)
        
        (cAlias)->( dbSkip() )

        If (cAlias)->(Eof()) .Or. cUltCalc != (cAlias)->(RA_FILIAL+RA_MAT+RH_DATAINI)
            self:oData:appendData(jData)
            If lTAE
                self:geraREQ(cFilRec, cMatRec, dDtIni, dDtFim)
            EndIf
        EndIf

    EndDo

    (cAlias)->(DbCloseArea())

return self:oData

method getSchema() as object class GPER1033TReportsBusinessObject
    
    local aFieldsMed := {} as array
    local aFieldsRec := {} as array

    self:oSchema:addProperty("costCenterCode", STR0004, "string", STR0004, "costCenterCode",,,, .F.)               //Centro de Custo
    self:oSchema:addProperty("functionCode", STR0005, "string", STR0005, "functionCode",,,, .F.)                   //Código da Função
    self:oSchema:addProperty("departmentName", STR0006, "string", STR0006, "departmentName",,,, .F.)               //Departamento
    self:oSchema:addProperty("employeeTypeCode", STR0007, "string", STR0007, "employeeTypeCode",,,, .F.)           //Categoria
    self:oSchema:addProperty("employeeSalary", STR0008, "number", STR0008, "employeeSalary",,,, .F.)               //Salário
    self:oSchema:addProperty("employeeCode", STR0009, "string", STR0009, "employeeCode",,,, .F.)                   //Matrícula
    self:oSchema:addProperty("branchCode", STR0010, "string", STR0010, "branchCode",,,, .F.)                       //Filial
    self:oSchema:addProperty("employeeName", STR0011, "string", STR0011, "employeeName",,,, .F.)                   //Nome
    self:oSchema:addProperty("branchCity", STR0012, "string", STR0012, "branchCity",,,, .F.)                       //Cidade da Filial
    self:oSchema:addProperty("branchName", STR0013, "string", STR0013, "branchName",,,, .F.)                       //Nome da Filial
    self:oSchema:addProperty("paymentbankcode", STR0014, "string", STR0014, "paymentbankcode",,,, .F.)             //Banco
    self:oSchema:addProperty("branchbankpayment", STR0015, "string", STR0015, "branchbankpayment",,,, .F.)         //Agência
    self:oSchema:addProperty("paymentaccountnumber", STR0016, "string",STR0016, "paymentaccountnumber",,,, .F.)    //Conta
    self:oSchema:addProperty("startAcquisition", STR0017, "date", STR0017, "startAcquisition",,,, .F.)             //Data Base Ini. Férias
    self:oSchema:addProperty("endAcquisition", STR0018, "date", STR0018, "endAcquisition",,,, .F.)                 //Data Base Fim Férias
    self:oSchema:addProperty("startVacation", STR0019, "date", STR0019, "startVacation",,,, .F.)                   //Início das Férias
    self:oSchema:addProperty("endVacation", STR0020, "date", STR0020, "endVacation",,,, .F.)                       //Fim das Férias
    self:oSchema:addProperty("vacationPaymentDate", STR0021, "date", STR0021, "vacationPaymentDate",,,, .F.)       //Data Pgto. Férias
    self:oSchema:addProperty("vacationNumberDays", STR0022, "number", STR0022, "vacationNumberDays",,,, .F.)       //Dias de Férias
    self:oSchema:addProperty("cashBenefitNumberDays", STR0023, "number", STR0023, "cashBenefitNumberDays",,,, .F.) //Dia de abono Pec.
    self:oSchema:addProperty("numberOfDaysPaidLeave", STR0024, "number", STR0024, "numberOfDaysPaidLeave",,,, .F.) //Dias de Lic. Remunerada
    self:oSchema:addProperty("LMSN", "Aviso", "boolean", "Aviso", "LMSN",,,, .F.)
    
    self:addParameter("IMDTRFER", STR0025, "string", .F.) //Imp. Dt. Rec. Férias ?
    self:addParameter("PERFERDE", STR0026, "date", .F.)   //Período Férias De ?
    self:addParameter("PERFERATE", STR0027, "date", .F.)  //Período Férias Até ?
    self:addParameter("FILDE", STR0028, "string", .F.)    //Filial De ?
    self:addParameter("FILATE", STR0029, "string", .F.)   //Filial Até ?
    self:addParameter("MATDE", STR0030, "string", .F.)    //Matrícula De ?
    self:addParameter("MATATE", STR0031, "string", .F.)   //Matrícula Até ?
    self:addParameter("CCDE", STR0032, "string", .F.)     //Centro de Custo De ?
    self:addParameter("CCATE", STR0033, "string", .F.)    //Centro de Custo Até ?
    self:addParameter("DTPGODE", STR0034, "date", .F.)    //Data de pagamento De ?
    self:addParameter("DTPGOATE", STR0035, "date", .F.)   //Data de pagamento Até ?
    self:addParameter("IMPMEDIA", STR0036, "string", .F.) //Imprime Detalhe das Médias ?
    self:addParameter("ORDER", STR0037, "string", .F.)    //Ordem de Impressão ?

    aAdd(aFieldsMed, {"Filial", STR0010, "string", STR0010})    //Filial
    aAdd(aFieldsMed, {"Matricula", STR0009, "string", STR0009}) //Matrícula
    aAdd(aFieldsMed, {"Verba", STR0038, "string", STR0038})     //Verba
	aAdd(aFieldsMed, {"Horas", STR0039, "number", STR0039})     //Horas
	aAdd(aFieldsMed, {"Valor", STR0040, "number", STR0040})     //Valor

    aAdd(aFieldsRec, {"eventCode", STR0038, "string", STR0038})           //Verba
    aAdd(aFieldsRec, {"eventHour", STR0039, "number", STR0039})           //Horas
    aAdd(aFieldsRec, {"eventDescription", STR0041, "string", STR0041})    //Descrição da verba
	aAdd(aFieldsRec, {"sumIncomeEmployee", STR0042, "number", STR0042})   //Proventos
    aAdd(aFieldsRec, {"sumDiscount", STR0043, "number", STR0043})         //Descontos
	aAdd(aFieldsRec, {"totalamountearnings", STR0044, "number", STR0044}) //Total Proventos
    aAdd(aFieldsRec, {"sumDiscountShift", STR0045, "number", STR0045})    //Total Descontos
    aAdd(aFieldsRec, {"liqFer", STR0046, "number", STR0046})              //Líquido das Férias
    aAdd(aFieldsRec, {"extLiqFer", STR0047, "string", STR0047})           //Valor por extenso

	self:addNestedProperty("ADetMedia", STR0048, STR0048,, aFieldsMed) //Detalhamento das Médias
    self:addNestedProperty("ADetRec", STR0049, STR0049,, aFieldsRec)   //Detalhamento do Recibo

    //setCustomURL disponível na LIB 20231121.
    If FWLibVersion() >= "20231121" 
        self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("ORDER", "/api/rh/smartview/v1/options/gpersv1033/ORDER", 1)
        self:setCustomURL("IMDTRFER", "/api/rh/smartview/v1/options/gpersv1033/IMDTRFER", 1) 
        self:setCustomURL("IMPMEDIA", "/api/rh/smartview/v1/options/gpersv1033/IMPMEDIA", 1) 
    EndIf    

return self:oSchema

/*/{Protheus.doc} getWherePar
Metodo que retorna a consulta conforme os parâmetros
@type  Metodo
@author Vitor Fernandes Moraes
@return character
@since 03/01/2024
/*/
method getWherePar( jPar as json) as character class GPER1033TReportsBusinessObject
    
    Local cRet as character

    If !(lGp1033 .And. "RA_FILIAL" $ jPar['FILDE', 1])
        If !Empty(jPar['FILDE', 1])
            cRet += " RA_FILIAL >= ? AND "
        EndIf
        If !Empty(jPar['FILATE', 1])
            cRet += " RA_FILIAL <= ? AND "
        EndIf
    Else
        cRet += jPar['FILDE', 1] + " AND " 
    EndIf    
    If !(lGp1033 .And. "RA_MAT" $ jPar['MATDE', 1])
        If !Empty(jPar['MATDE', 1])
            cRet += " RA_MAT >= ? AND "
        EndIf
        If !Empty(jPar['MATATE', 1])
            cRet += " RA_MAT <= ? AND "
        EndIf
    Else
         cRet += jPar['MATDE', 1] + " AND " 
    EndIf 
    If !(lGp1033 .And. "RA_CC" $ jPar['CCDE', 1])   
         If !Empty(jPar['CCDE', 1])
            cRet += " RA_CC >= ? AND "
        EndIf  
        If !Empty(jPar['CCATE', 1])
            cRet += " RA_CC <= ? AND "
        EndIf
    Else
        cRet += jPar['CCDE', 1] + " AND " 
    EndIf 
    If !Empty(jPar['DTPGODE', 1])
		cRet += " RH_DTRECIB >= ? AND "
	EndIf
    If !Empty(jPar['DTPGOATE', 1])
		cRet += " RH_DTRECIB <= ? AND "
	EndIf
    If !Empty(jPar['PERFERDE', 1])
		cRet += " RH_DATAINI >= ? AND "
	EndIf
    If !Empty(jPar['PERFERATE', 1])
		cRet += " RH_DATAINI <= ? AND "
	EndIf

Return cRet

/*/{Protheus.doc} getComboBox
Metodo que retorna o combobox dos parâmetros
@type  Metodo
@author Bruno Costa
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() as Variant Class GPER1033TReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    Local cParam    As Character 
    local aValues   as Array   
     
    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    jItem     := NIL
    nValues   := 0
    cParam    := ""
    aValues   := {}

    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    jResponse["data"] := {}

    // Monta o vetor de valores do parâmetro
    If cParam == "ORDER"
        AAdd(aValues, EncodeUTF8("Matrícula"))
        AAdd(aValues, EncodeUTF8("C.Custo + Matrícula"))
        AAdd(aValues, EncodeUTF8("C.Custo + Nome"))
        AAdd(aValues, EncodeUTF8("Nome"))
    Else
        AAdd(aValues, EncodeUTF8("Sim"))
        AAdd(aValues, EncodeUTF8("Não"))   
    EndIf 

    If (Len(aValues) > 0)
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)

Return NIL

/*/{Protheus.doc} getMedia
Metodo que verifica as médias das férias do funcionário
@type  Metodo
@author Bruno Costa
@return character
@since 08/10/2024
/*/
method getMedia(cAlias as character, jData as json, dDtRef as date, cPerMed as character, cNrPgto as character, cRot as character, cTipo as character, lRec as logical, nDiasAvi as numeric) as variant class GPER1033TReportsBusinessObject
local aArea    := GetArea() as array
local aPdMedia := {} as array
local aTipoCod := {"99MD", "ZZ01", "ZZ02", "ZZ03", "ZZ04"} as array
local cAliasSRP as character 
local cQuery as character 
local cRotRes := fGetCalcRot("4") as character
local lTemTransf as logical
local nPOrder := 1 as numeric
local nI as numeric

Static oStatMedia

DEFAULT dDtRef   := StoD((cAlias)->RH_DATAINI)
DEFAULT cPerMed  := (cAlias)->RH_PERIODO
DEFAULT cNrPgto  := (cAlias)->RH_NPAGTO
DEFAULT cRot     := (cAlias)->RH_ROTEIR
DEFAULT cTipo    := "1"
DEFAULT lRec     := .F.
DEFAULT nDiasAvi := 0

DbSelectArea("SRA")
SRA->(dbSetOrder(1))

If SRA->(dbSeek((cAlias)->RA_FILIAL + (cAlias)->RA_MAT)) 
    If fTemHMed(dDtRef, Nil, cPerMed, cNrPgto, cRot, @lTemTransf)
        cQuery := "SELECT SRP.RP_FILIAL, SRP.RP_MAT, SRP.RP_PD, SRP.RP_HORAS, SRP.RP_VALATU, SRP.RP_DATARQ, SRP.RP_TIPO " 
        cQuery += "FROM " + RetSqlName("SRP") + " SRP "
        cQuery += "WHERE SRP.RP_FILIAL = ? AND SRP.RP_MAT = ? AND SRP.RP_ID = ? AND SRP.RP_DATARQ IN (?) AND SRP.RP_PD <> ? "
        If cRot <> cRotRes
            cQuery += "AND SRP.RP_TIPO = ? "
        EndIf
        cQuery += "AND SRP.D_E_L_E_T_ = ? "
        cQuery += "ORDER BY SRP.RP_DATARQ"
        
        If oStatMedia == Nil 
            cQuery := ChangeQuery(cQuery)
            
            oStatMedia := FwExecStatement():New(cQuery)
        EndIf

        oStatMedia:SetString(nPOrder++, If(lTemTransf, RJK->RJK_FILIAL, SRA->RA_FILIAL))
        oStatMedia:SetString(nPOrder++, If(lTemTransf, RJK->RJK_MAT, SRA->RA_MAT))
        oStatMedia:SetString(nPOrder++, RJK->RJK_ID)
        oStatMedia:SetIn(nPOrder++, aTipoCod)
        oStatMedia:SetString(nPOrder++, "999")
        If cRot <> cRotRes
            oStatMedia:SetString(nPOrder++, cTipo)
        EndIf
        oStatMedia:SetString(nPOrder++, " ")
        
        cAliasSRP := oStatMedia:OpenAlias()  

        While (cAliasSRP)->(!Eof())
            If cRot <> cRotRes
                If (nPos := aScan(aPdMedia, {|x| x[7] == (cAliasSRP)->RP_PD})) == 0 
                    aAdd(aPdMedia, {;
                        (cAliasSRP)->RP_FILIAL,;
                        (cAliasSRP)->RP_MAT,;
                        (cAliasSRP)->RP_PD + ' - ' + RetValSrv((cAliasSRP)->RP_PD, If(lTemTransf, RJK->RJK_FILIAL, SRA->RA_FILIAL), "RV_DESC"),;
                        (cAliasSRP)->RP_HORAS,;
                        If(!lRec .And. (cAlias)->(RH_DFERIAS+RH_DABONPE) < 30, ((cAliasSRP)->RP_VALATU / 30) * (cAlias)->(RH_DFERIAS+RH_DABONPE), (cAliasSRP)->RP_VALATU),;
                        (cAliasSRP)->RP_PD+(cAliasSRP)->RP_DATARQ,;
                        (cAliasSRP)->RP_PD})
                ElseIf aPdMedia[nPos, 6] <> (cAliasSRP)->RP_PD+(cAliasSRP)->RP_DATARQ
                    aPdMedia[nPos, 4] += (cAliasSRP)->RP_HORAS
                    aPdMedia[nPos, 5] += If(!lRec .And. (cAlias)->(RH_DFERIAS+RH_DABONPE) < 30, ((cAliasSRP)->RP_VALATU / 30) * (cAlias)->(RH_DFERIAS+RH_DABONPE), (cAliasSRP)->RP_VALATU)
                    aPdMedia[nPos, 6] := (cAliasSRP)->RP_PD+(cAliasSRP)->RP_DATARQ
                EndIf
            Else 
                If (nPos := aScan(aPdMedia, {|x| x[7] == (cAliasSRP)->RP_PD + (cAliasSRP)->RP_TIPO})) == 0 
                    aAdd(aPdMedia, {;
                        (cAliasSRP)->RP_FILIAL,;
                        (cAliasSRP)->RP_MAT,;
                        (cAliasSRP)->RP_PD + ' - ' + RetValSrv((cAliasSRP)->RP_PD, If(lTemTransf, RJK->RJK_FILIAL, SRA->RA_FILIAL), "RV_DESC"),;
                        (cAliasSRP)->RP_HORAS,;
                        If((cAliasSRP)->RP_TIPO == "4" .And. nDiasAvi > 0, ((cAliasSRP)->RP_VALATU / 30) * nDiasAvi, (cAliasSRP)->RP_VALATU),;
                        (cAliasSRP)->RP_PD+(cAliasSRP)->RP_TIPO+(cAliasSRP)->RP_DATARQ,;
                        (cAliasSRP)->RP_PD+(cAliasSRP)->RP_TIPO,;
                        (cAliasSRP)->RP_TIPO})
                ElseIf aPdMedia[nPos, 6] <> (cAliasSRP)->RP_PD+(cAliasSRP)->RP_TIPO+(cAliasSRP)->RP_DATARQ
                    aPdMedia[nPos, 4] += (cAliasSRP)->RP_HORAS
                    aPdMedia[nPos, 5] += If((cAliasSRP)->RP_TIPO == "4" .And. nDiasAvi > 0, ((cAliasSRP)->RP_VALATU / 30) * nDiasAvi, (cAliasSRP)->RP_VALATU)
                    aPdMedia[nPos, 6] := (cAliasSRP)->RP_PD+(cAliasSRP)->RP_TIPO+(cAliasSRP)->RP_DATARQ
                EndIf
            EndIf    
            (cAliasSRP)->(dbSkip())
        EndDo

        If cRot <> cRotRes
            For nI := 1 To Len(aPdMedia)
                aAdd(jData["ADetMedia"], {;
                    "Filial": aPdMedia[nI][1],;
                    "Matricula": aPdMedia[nI][2],;
                    "Verba": aPdMedia[nI][3],;
                    "Horas": aPdMedia[nI][4],;
                    "Valor": aPdMedia[nI][5]}) 
            Next nI    
        Else
            For nI := 1 To Len(aPdMedia)
                aAdd(jData["ADetMedia"], {;
                    "Filial": aPdMedia[nI][1],;
                    "Matricula": aPdMedia[nI][2],;
                    "Verba": aPdMedia[nI][3],;
                    "Horas": aPdMedia[nI][4],;
                    "Valor": aPdMedia[nI][5],;
                    "Tipo" : aPdMedia[nI][8]}) 
            Next nI   
        EndIf 

        (cAliasSRP)->( dbCloseArea() )

    EndIf    
EndIf    

SRA->(dbCloseArea())

RestArea(aArea)

Return Nil

/*/{Protheus.doc} geraREQ
Metodo para criar o registro na REQ para controle do que enviar no GPER1033
@type  Metodo
@author Bruno Costa
@return character
@since 10/02/2025
/*/
method geraREQ(cFilRec as character, cMatRec as character, dDtIni as date, dDtFim as date) as variant class GPER1033TReportsBusinessObject
local nDoc := "SVGPER1033"+cFilRec+cMatRec as character

DbSelectArea("REQ")
DbSetOrder(1)
RecLock("REQ", !REQ->(DbSeek(cFilRec + cMatRec + nDoc)))
REQ->REQ_FILIAL := cFilRec
REQ->REQ_MAT	:= cMatRec
REQ->REQ_DTINI	:= dDtIni
REQ->REQ_DTFIM 	:= dDtFim
REQ->REQ_NDOC	:= nDoc
REQ->REQ_TPDOC	:= "1"
REQ->REQ_ID 	:= 0
REQ->REQ_DTINTE	:= Date()
REQ->REQ_STATUS	:= "N"

REQ->(MsUnLock())

Return Nil
