#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "PONCALEN.CH"
#Include "TOTVS.CH"
#Include "rh.sv.pon.ponr210.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPON", tables="SRA", name="Rel. Absenteísmo", country="ALL", initialRelease="12.1.2210")
Class PONR210TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	Public Method new() as object
	Public Method getDescription() as character
	Public Method getData() as object
	Public Method getSchema() as object
	
	protected data ojItems as json
	
EndClass

Method new() Class PONR210TReportsBusinessObject
	_Super:new()
	
	//Define a Área
	self:appendArea(STR0001) // "RH"
	
	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0002) // "Rel. Absenteísmo"
	
Return self

Method getDescription() as character class PONR210TReportsBusinessObject
Return (STR0003) // "Objeto com os eventos de absenteísmo gerados pelo apontamento, informações sobre os abonos realizados no período e afastamentos."

Method getData(nPage as numeric, oFilter as object) as object class PONR210TReportsBusinessObject
	Local cAliasSRA 	:= ""										as character
	Local cLGPDSRA		:= ""										as character
	Local cCateg		:= ""										as character
	Local cSitFol		:= ""										as character
	Local cLastFil		:= "***"									as character
	Local cFilDe		:= ""										as character
	Local cFilAte		:= ""										as character
	Local cMatDe		:= ""										as character
	Local cMatAte		:= ""										as character
	Local cCCDe			:= ""										as character
	Local cCCAte		:= ""										as character
	Local cGrpEmpNom    := FWGrpName(cEmpAnt)                       as character
	Local cEmpNome		:= ""										as character
	Local cQuery		:= ""										as character
	Local dPerIni		:= cToD("") 								as date
	Local dPerFim		:= cToD("") 								as date
	Local dDataDe		:= cToD("") 								as date
	Local dDataAte		:= cToD("") 								as date
	Local nI	 		:= 0 										as numeric
	Local nHorasPre		:= 0										as numeric
	Local nHrsReal		:= 0 										as numeric
	Local nHrsNTrab		:= 0 										as numeric
	Local nParamOrder	:= 1 										as numeric
	Local aPeriodos		:= {} 										as array
	Local aAfast		:= {} 										as array
	Local aFieldsSRA	:= {}										as array
	Local aLGPDSRA		:= {}										as array
	Local aLancamentos	:= {} 										as array
	Local aAbonos		:= {} 										as array
	Local lTemApo		:= .F.										as logical
	Local lShowEvent	:= .T.										as logical
	Local lAfastPrev	:= .T.										as logical
	Local lFerAfast		:= .T.										as logical
	Local lCentesimal	:= .F.										as logical
	local oStatement												as Object
	
	// Verificação da anonimização dos dados
	aFieldsSRA := {"RA_FILIAL", "RA_MAT", "RA_NOME", "RA_CATFUNC", "RA_CC", "RA_TNOTRAB", "RA_CODFUNC", "RA_DEPTO", "RA_SITFOLH"}
	aLGPDSRA := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsSRA)
	
	For nI := 1 To Len(aFieldsSRA)
		If(aScan(aLGPDSRA, aFieldsSRA[nI]) == 0, cLGPDSRA += aFieldsSRA[nI] + "*", Nil)
	Next
	
	// Parâmetros
	// Carrega os parâmetros informados
	oJParams	:= oFilter:getParameters()
	dDataDe		:= sToD(StrTran(SubStr(oJParams["per1", 1], 1, 10), "-", ""))
	dDataAte	:= sToD(StrTran(SubStr(oJParams["per2", 1], 1, 10), "-", ""))
	lShowEvent	:= If(!Empty(oJParams["showEvent", 1])  .And. oJParams["showEvent", 1]  == "2", .F., .T.)
	lAfastPrev	:= If(!Empty(oJParams["afastHPrev", 1]) .And. oJParams["afastHPrev", 1] == "2", .F., .T.)
	lFerAfast	:= If(!Empty(oJParams["ferAfast", 1])   .And. oJParams["ferAfast", 1]   == "2", .F., .T.)
	lCentesimal	:= If(!Empty(oJParams["FormatH", 1])    .And. oJParams["FormatH", 1]    == "2", .T., .F.)
	
	// Validação do filtro "Filial De" e "Filial Até" para utilização na Query
	If (!Empty(oJParams['filial1', 1]), cFilDe := oJParams['filial1', 1], cFilDe := Space(GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
	If (!Empty(oJParams['filial2', 1]), cFilAte := oJParams['filial2', 1], cFilAte := Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))

	// Validação do filtro "Matrícula De" e "Matrícula Até" para utilização na Query
	If (!Empty(oJParams['mat1', 1]), cMatDe := oJParams['mat1', 1], cMatDe := Space(GetSX3Cache("RA_MAT", "X3_TAMANHO")))
	If (!Empty(oJParams['mat2', 1]), cMatAte := oJParams['mat2', 1], cMatAte := Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))

	// Validação do filtro "Centro de Custo De" e "Centro de Custo Até" para utilização na Query
	If (!Empty(oJParams['cCusto1', 1]), cCCDe := oJParams['cCusto1', 1], cCCDe := Space(GetSX3Cache("RA_CC", "X3_TAMANHO")))
	If (!Empty(oJParams['cCusto2', 1]), cCCAte := oJParams['cCusto2', 1], cCCAte := Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
		
	// Filtro das datas
	If(dDataDe > dDataAte, dDataAte := dDataDe, Nil)
	
	cQuery :=	"SELECT "
	cQuery +=	"RA_FILIAL, "
	cQuery +=	"RA_MAT, "
	cQuery +=	"RA_NOME, "
	cQuery +=	"RA_CATFUNC, "
	cQuery +=	"RA_CODFUNC, "
	cQuery +=	"RA_DEPTO, "
	cQuery +=	"RA_CC, "
	cQuery +=	"RA_SITFOLH, "
	cQuery +=	"RA_TNOTRAB, "
	cQuery +=	"SRA.R_E_C_N_O_ AS RECNO, "
	cQuery +=	"R6_DESC, "
	cQuery +=	"RJ_DESC, "
	cQuery +=	"QB_DESCRIC, "
	cQuery +=	"CTT_DESC01 "
	cQuery +=	"FROM " + RetSQLName("SRA") + " SRA "
	cQuery +=	"LEFT JOIN " + RetSQLName("SR6") + " SR6 ON " + FWJoinFilial("SR6", "SRA") + " AND SR6.R6_TURNO = SRA.RA_TNOTRAB AND SR6.D_E_L_E_T_ = ? "	// 1
	cQuery +=	"LEFT JOIN " + RetSQLName("SRJ") + " SRJ ON " + FWJoinFilial("SRJ", "SRA") + " AND SRJ.RJ_FUNCAO = SRA.RA_CODFUNC AND SRJ.D_E_L_E_T_ = ? "	// 2
	cQuery +=	"LEFT JOIN " + RetSQLName("SQB") + " SQB ON " + FWJoinFilial("SQB", "SRA") + " AND SQB.QB_DEPTO = SRA.RA_DEPTO AND SQB.D_E_L_E_T_ = ? "		// 3
	cQuery +=	"LEFT JOIN " + RetSQLName("CTT") + " CTT ON " + FWJoinFilial("CTT", "SRA") + " AND CTT.CTT_CUSTO = SRA.RA_CC AND CTT.D_E_L_E_T_ = ? "		// 4
	cQuery +=	"WHERE RA_FILIAL BETWEEN ? AND ? "			// 5 e 6
	cQuery +=	"AND SRA.RA_CC BETWEEN ? AND ? "			// 7 e 8
	cQuery +=	"AND SRA.RA_MAT BETWEEN ? AND ? "			// 9 e 10
	cQuery +=	"AND SRA.RA_ADMISSA <= ? "					// 11
	cQuery +=	"AND (RA_DEMISSA = ? OR RA_DEMISSA > ? ) "	// 12 e 13
	cQuery +=	"AND SRA.D_E_L_E_T_ = ? "					// 14

	cQuery := ChangeQuery(cQuery)

	oStatement := FwExecStatement():New(cQuery)
	
	oStatement:SetString(nParamOrder++,  " ")				// 1
	oStatement:SetString(nParamOrder++,  " ")				// 2
	oStatement:SetString(nParamOrder++,  " ")				// 3
	oStatement:SetString(nParamOrder++,  " ")				// 4
	oStatement:SetString(nParamOrder++,  cFilDe)			// 5
	oStatement:SetString(nParamOrder++,  cFilAte)			// 6
	oStatement:SetString(nParamOrder++,  cCCDe)				// 7
	oStatement:SetString(nParamOrder++,  cCCAte)			// 8
	oStatement:SetString(nParamOrder++,  cMatDe)			// 9
	oStatement:SetString(nParamOrder++,  cMatAte)			// 10
	oStatement:SetString(nParamOrder++,  dToS(dDataAte))	// 11
	oStatement:SetString(nParamOrder++,  " ")				// 12
	oStatement:SetString(nParamOrder++,  dToS(dDataDe))		// 13
	oStatement:SetString(nParamOrder++,  " ")				// 14

	cAliasSRA := oStatement:OpenAlias()

	While (cAliasSRA)->(!Eof())
		
		//Posiciona no Funcionário
		SRA->(dbGoTo((cAliasSRA)->RECNO))
		
		cCateg	:= (cAliasSRA)->(fDesc("SX5", "28" + RA_CATFUNC, "X5_DESCRI", 11, RA_FILIAL))
		cSitFol	:= (cAliasSRA)->(fDesc("SX5", "31" + RA_SITFOLH, "X5_DESCRI", 15, RA_FILIAL))
		
		If (cAliasSRA)->RA_FILIAL != cLastFil
			cLastFil := (cAliasSRA)->RA_FILIAL
			// Obtem as datas do Periodo em Aberto
			GetPonMesDat( @dPerIni, @dPerFim, cLastFil )
			
			// Busca as informações da empresa do funcionário
			cEmpNome := AllTrim(FWCompanyName(cEmpAnt, (cAliasSRA)->RA_FILIAL))
		EndIf
		
		aPeriodos := Monta_per(dDataDe, dDataAte, (cAliasSRA)->RA_FILIAL, (cAliasSRA)->RA_MAT, dPerIni, dPerFim )
		
		For nI := 1 To Len(aPeriodos)
			
			nHorasPre := 0		// Horas Previstas
			nHrsNTrab := 0		// Horas Não Trabalhadas
			nHrsAbo := 0		// Horas abonadas
			aAfast := {0, {}}	// [1] Soma das Horas de afastamento, [2] afastamentos detalhados
			aLancamentos := {}
			aAbonos := {}
			
			// Calcula as horas previstas e de afastamento
			fHrsPrev(aPeriodos[nI], @nHorasPre, @aAfast, lAfastPrev, lFerAfast)
			
			lTemApo := GetLanc(SRA->RA_FILIAL, SRA->RA_MAT, aPeriodos[nI][3], aPeriodos[nI][4], @nHrsNTrab, @aLancamentos)
			
			If lTemApo
				nHrsAbo := GetAbo(SRA->RA_FILIAL, SRA->RA_MAT, aPeriodos[nI][3], aPeriodos[nI][4], @aAbonos)
			EndIf
			
			// Calcula as horas efetivamente trabalhadas
			nHrsReal := SubHoras(nHorasPre, nHrsNTrab) //  - Horas não trabalhadas dos eventos 
			
			If lAfastPrev
				nHrsReal := SubHoras(nHrsReal, aAfast[1]) // - Horas de afastamento
			EndIf
			
			// Formata as informações para o appendData
			self:ojItems := JsonObject():new()
			
			self:ojItems["GRPEMPRESA"]		:= AllTrim(cGrpEmpNom)
			self:ojItems["EMPRESA"]			:= cEmpNome
			self:ojItems["RA_FILIAL"]		:= If("RA_FILIAL" $ cLGPDSRA, Anonimiza((cAliasSRA)->RA_FILIAL), AllTrim((cAliasSRA)->RA_FILIAL))
			self:ojItems["RA_MAT"]			:= If("RA_MAT" $ cLGPDSRA, Anonimiza((cAliasSRA)->RA_MAT), (cAliasSRA)->RA_MAT)
			self:ojItems["RA_NOME"]			:= If("RA_NOME" $ cLGPDSRA, Anonimiza((cAliasSRA)->RA_NOME), AllTrim((cAliasSRA)->RA_NOME))
			self:ojItems["RA_CATFUNC"]		:= If("RA_CATFUNC" $ cLGPDSRA, Anonimiza(cCateg), cCateg)
			self:ojItems["RA_CODFUNC"]		:= If("RA_CODFUNC" $ cLGPDSRA, Anonimiza((cAliasSRA)->RA_CODFUNC), AllTrim((cAliasSRA)->RA_CODFUNC))
			self:ojItems["DESCFUNCAO"]		:= If("RA_CODFUNC" $ cLGPDSRA, Anonimiza((cAliasSRA)->RJ_DESC), AllTrim((cAliasSRA)->RJ_DESC))
			self:ojItems["RA_DEPTO"]		:= If("RA_DEPTO" $ cLGPDSRA, Anonimiza((cAliasSRA)->RA_DEPTO), AllTrim((cAliasSRA)->RA_DEPTO))
			self:ojItems["DESCDPTO"]		:= If("RA_DEPTO" $ cLGPDSRA, Anonimiza((cAliasSRA)->QB_DESCRIC), AllTrim((cAliasSRA)->QB_DESCRIC))
			self:ojItems["RA_CC"]			:= If("RA_CC" $ cLGPDSRA, Anonimiza((cAliasSRA)->RA_CC), AllTrim((cAliasSRA)->RA_CC))
			self:ojItems["DESCCC"]			:= If("RA_CC" $ cLGPDSRA, Anonimiza((cAliasSRA)->CTT_DESC01), AllTrim((cAliasSRA)->CTT_DESC01))
			self:ojItems["RA_SITFOLH"]		:= If("RA_SITFOLH" $ cLGPDSRA, Anonimiza(cSitFol), cSitFol)
			self:ojItems["RA_TNOTRAB"]		:= If("RA_TNOTRAB" $ cLGPDSRA, Anonimiza((cAliasSRA)->RA_TNOTRAB), AllTrim((cAliasSRA)->RA_TNOTRAB))
			self:ojItems["R6_DESC"]			:= If("RA_TNOTRAB" $ cLGPDSRA, Anonimiza((cAliasSRA)->R6_DESC), AllTrim((cAliasSRA)->R6_DESC))
			self:ojItems["PERIODO"]			:= StrZero(Month(aPeriodos[nI, 1]), 2) + '/' + Transf(Year(aPeriodos[nI, 1]), '9999')
			self:ojItems["HPREVISTAS"]		:= If(lCentesimal, fConvHr(nHorasPre, "D"), nHorasPre)
			self:ojItems["HREALIZADAS"]		:= If(lCentesimal, fConvHr(nHrsReal, "D"), nHrsReal)
			self:ojItems["HREALPER"]		:= fConvHr(nHrsReal, "D") / fConvHr(nHorasPre, "D")
			self:ojItems["HNTRAB"]			:= If(lCentesimal, fConvHr(nHrsNTrab, "D"), nHrsNTrab)
			self:ojItems["HNTRABPER"]		:= fConvHr(nHrsNTrab, "D") / fConvHr(nHorasPre, "D")
			self:ojItems["HABONADAS"]		:= If(lCentesimal, fConvHr(nHrsAbo, "D"), nHrsAbo)
			self:ojItems["HABOPER"]			:= fConvHr(nHrsAbo, "D") / fConvHr(nHrsNTrab, "D")
			self:ojItems["HAFAST"]			:= If(lCentesimal, fConvHr(aAfast[1], "D"), aAfast[1])
			
			If lAfastPrev
				self:ojItems["HAFASTPER"]	:= fConvHr(aAfast[1], "D") / fConvHr(nHorasPre, "D")
			EndIf
			
			If lShowEvent
				self:ojItems["Detalhes"] := FormatDet(aLancamentos, nHrsNTrab, aAbonos, nHrsAbo, aAfast, lCentesimal)
			EndIf
			
			self:oData:appendData(self:ojItems)
			
		Next nI
		
		(cAliasSRA)->(dbSkip())
	EndDo
	
	(cAliasSRA)->(dbCloseArea())
	
Return self:oData

/*/{Protheus.doc} getSchema
Metodo que retorna a estrutura dos campos que serão enviados para o Smart View
@type Method
@author Cícero Alves
@since 11/12/2023
/*/
Method getSchema() as object class PONR210TReportsBusinessObject
	
	Local aFieldsSRA	:= {}
	Local aFieldsDet	:= {}
	
	// Informações da empresa
	self:oSchema:addProperty("GRPEMPRESA",	STR0004, "string", STR0004, "GRPEMPRESA") 	// "Grupo de Empresas"
	self:oSchema:addProperty("EMPRESA",		STR0005, "string", STR0005, "EMPRESA") 		// "Empresa"
	
	// Informações do Funcionário
	aFieldsSRA := {"RA_FILIAL", "RA_MAT", "RA_NOME", "RA_CATFUNC", "RA_CODFUNC", "RA_DEPTO", "RA_CC", "RA_SITFOLH", "RA_TNOTRAB"}
	
	self:oSchema:aliasToSchema("SRA", aFieldsSRA)
	
	self:oSchema:addProperty("DESCDPTO",	STR0006, "string", STR0006,	"DESCDPTO") 	// "Descrição Departamento"
	self:oSchema:addProperty("DESCCC",		STR0007, "string", STR0007,	"DESCCC") 		// "Descrição Centro de Custo"
	self:oSchema:addProperty("DESCFUNCAO",	STR0008, "string", STR0008,	"DESCFUNCAO") 	// "Descrição Função"
	
	// Informações do Período
	self:oSchema:addProperty("PERIODO",		STR0009, "string", STR0009, "PERIODO") 		// "Período"
	self:oSchema:addProperty("HPREVISTAS",	STR0010, "number", STR0010,	"HPREVISTAS") 	// "Horas Previstas"
	self:oSchema:addProperty("HREALIZADAS",	STR0011, "number", STR0011,	"HREALIZADAS") 	// "Horas Realizadas"
	self:oSchema:addProperty("HREALPER",	STR0014, "number", STR0012, "HREALPER") 	// "Percentual de Horas Realizadas" - "% Hrs. Realizadas"
	self:oSchema:addProperty("HNTRAB",		STR0013, "number", STR0013,	"HNTRAB") 		// "Horas Não Trabalhadas"
	self:oSchema:addProperty("HNTRABPER",	STR0015, "number", STR0016,	"HNTRABPER") 	// "Percentual de Horas Não Trabalhadas" - "% Hrs. Não Trabalhadas"
	self:oSchema:addProperty("HABONADAS",	STR0017, "number", STR0017,	"HABONADAS") 	// "Horas Abonadas"
	self:oSchema:addProperty("HABOPER",		STR0018, "number", STR0019,	"HABOPER") 		// "Percentual de Horas Abonadas" - "% de Horas Abonadas"
	self:oSchema:addProperty("HAFAST",		STR0020, "number", STR0020,	"HAFAST") 		// "Horas de Afastamento"
	self:oSchema:addProperty("HAFASTPER",	STR0021, "number", STR0022,	"HAFASTPER") 	// "Percentual de Horas de Afastamento" - "% de Hrs. de Afast."
	
	// Detalhes (Apontamento, Abono e Afastamento)
	aAdd(aFieldsDet, {"TIPO",		STR0023, "string", STR0024}) // "Tipo do Lançamento" - "Tipo"
	aAdd(aFieldsDet, {"CODIGO",		STR0025, "string", STR0025}) // "Evento"
	aAdd(aFieldsDet, {"HORAS",		STR0026, "number", STR0026}) // "Horas do Evento"
	aAdd(aFieldsDet, {"DESCRICAO", 	STR0027, "string", STR0027}) // "Descrição do Evento"
	aAdd(aFieldsDet, {"PERCENTUAL", STR0028, "number", STR0028}) // "Percentual Evento"
	
	self:addNestedProperty("Detalhes", STR0029, STR0029, , aFieldsDet) // "Detalhes"
	
	// Parâmetros do reltório
	self:addParameter("filial1",	STR0030, "string", .F.) // Filial De?
	self:addParameter("filial2",	STR0031, "string", .F.) // Filial Até?
	self:addParameter("mat1",		STR0032, "string", .F.) // Matrícula De?
	self:addParameter("mat2",		STR0033, "string", .F.) // Matrícula Até?
	self:addParameter("cCusto1",	STR0034, "string", .F.) // Centro de Custo De?
	self:addParameter("cCusto2",	STR0035, "string", .F.) // Centro de Custo Até?
	self:addParameter("per1",		STR0036, "date",   .F.) // Período Inicial?
	self:addParameter("per2",		STR0037, "date",   .F.) // Período Final?
	self:addParameter("showEvent",	STR0038, "string", .F.) // "Discrimina Eventos?"
	self:addParameter("afastHPrev",	STR0039, "string", .F.) // "Afastamento como Hora Prevista?"
	self:addParameter("ferAfast",	STR0040, "string", .F.) // "Férias como Afastamento"
	self:addParameter("FormatH",	STR0041, "string", .F.) // "Horas em?"
	
Return self:oSchema


/*/{Protheus.doc} fHrsPrev
Calcula as horas previstas e as horas de afastamento para o funcioário no período com base no calendário do ponto
@type Static Function
@author Cícero Alves
@since 12/12/2023
@param aPeriodo, Array, Informações do período de apontamento [1] = Data inicial do período, [2] = Data final do período, [3] = Data inicial para busca, [4] = Data Final para busca
@param nHoras, Numérico, Deve ser passado por referência; Total das horas previstas para o funcionário no período
@param aAfast, Array, Deve ser passado por referência; Afastamentos ocorridos no período [n, 1] = Total de horas; [n, 2] = Array com o detalhamento do afastamento
@param lAfastPrev, Lógico, Se considera os afastamentos como parte das horas previstas
@param lFerAfast, Lógico, Se considera as Férias como um afastamento
/*/
Static Function fHrsPrev(aPeriodo, nHoras, aAfast, lAfastPrev, lFerAfast)
	
	Local aTabCalend 	:= {}
	Local aTurnos		:= {}
	Local aTabPadrao	:= {}
	Local cDescAfas		:= ""
	Local cTipoAfas		:= ""
	Local nPosAfast		:= 0
	Local nI			:= 0
	
	DEFAULT lAfastPrev := .T.
	DEFAULT lFerAfast := .T.
	
	aAfast := {0, {}}
	nHoras := 0
	
	If !CriaCalend(	aPeriodo[1] 		,;	//01 -> Data Inicial do Periodo
					aPeriodo[2]			,;	//02 -> Data Final do Periodo
					SRA->RA_TNOTRAB		,;	//03 -> Turno Para a Montagem do Calendario
					SRA->RA_SEQTURN		,;	//04 -> Sequencia Inicial para a Montagem Calendario
					@aTabPadrao			,;	//05 -> Array Tabela de Horario Padrao
					@aTabCalend			,;	//06 -> Array com o Calendario de Marcacoes
					SRA->RA_FILIAL     	,;	//07 -> Filial para a Montagem da Tabela de Horario
					SRA->RA_MAT   		,;	//08 -> Matricula para a Montagem da Tabela de Horario
					SRA->RA_CC   		,;	//09 -> Centro de Custo para a Montagem da Tabela
					@aTurnos			,;	//10 -> Array com as Trocas de Turno
					NIL					,;	//11 -> Array com Todas as Excecoes do Periodo
					NIL					,;	//12 -> Se executa Query para a Montagem da Tabela Padrao
					.T.					,;	//13 -> Se executa a funcao se sincronismo do calendario
					NIL			 		;	//14 -> Se Forca a Criacao de Novo Calendario
				)
		Return
	EndIf
	
	For nI := 1 To Len(aTabCalend)
		
		If Empty(aTabCalend[nI][CALEND_POS_HRS_TRABA]) .Or. aTabCalend[nI][CALEND_POS_DATA] < aPeriodo[3];
			.Or. aTabCalend[nI][CALEND_POS_DATA] < SRA->RA_ADMISSA .Or. (!Empty(SRA->RA_DEMISSA) .And. aTabCalend[nI][CALEND_POS_DATA] > SRA->RA_DEMISSA)
			LOOP
		ElseIf aTabCalend[nI][CALEND_POS_DATA] > aPeriodo[4] .Or. SRA->( RA_SITFOLH $ 'D*T' .And. aTabCalend[nI][CALEND_POS_DATA] > RA_DEMISSA )
			EXIT
		EndIf
		
		If aTabCalend[nI][CALEND_POS_TIPO_DIA] == 'S' .Or. ; 
			(aTabCalend[nI][CALEND_POS_AFAST] .And. aTabCalend[nI][CALEND_POS_TIPO_ORIG_DIA] == 'S' )
			
			If aTabCalend[nI][CALEND_POS_AFAST]
				
				If aTabCalend[nI][CALEND_POS_TIP_AFAST] == "F" .And. !lFerAfast
					LOOP
				EndIf
				
				aAfast[1] := SomaHoras(aAfast[1], aTabCalend[nI][CALEND_POS_HRS_TRABA])
				
				cTipoAfas := Posicione("SR8", RetOrder("SR8","R8_FILIAL+R8_NUMID"), SRA->RA_FILIAL + aTabCalend[nI][CALEND_POS_R8ID], "R8_TIPOAFA") 
				cDescAfas := fDesc( "RCM", cTipoAfas, "RCM_DESCRI", 30, SRA->RA_FILIAL, , .F. )
				
				If (nPosAfast := aScan(aAfast[2], {|x| x[1] == cTipoAfas})) > 0
					aAfast[2][nPosAfast][2] := SomaHoras(aAfast[2][nPosAfast][2], aTabCalend[nI][CALEND_POS_HRS_TRABA])
				Else
					aAdd(aAfast[2], {cTipoAfas, aTabCalend[nI][CALEND_POS_HRS_TRABA], cDescAfas})
				EndIf
				
				// Adiciona o afastamento nas horas previstas
				If lAfastPrev
					nHoras := SomaHoras(nHoras, aTabCalend[nI][CALEND_POS_HRS_TRABA])
				EndIf
			Else
				nHoras := SomaHoras(nHoras, aTabCalend[nI][CALEND_POS_HRS_TRABA])
			EndIf
		EndIf
	Next nI
	
Return

/*/{Protheus.doc} GetLanc
Busca as horas não trabalhadas nas tabelas de apontamento
@type Static Function
@author Cícero Alves
@since 12/12/2023
@param cFilFunc, Caracter, Filial do funcinário
@param cMatFunc, Caracter, Matrícula do funcinário
@param dDataIni, Data, Data inicial da busca
@param dDataFim, Data, Data final da busca
@param nHrsNTrab, Numérico, Deve ser passado por referência; Total de horas não trabalhadas no período
@return lTemApo, Lógico, Indica se encontrou registros
/*/
Static Function GetLanc(cFilFunc, cMatFunc, dDataIni, dDataFim, nHrsNTrab, aLancamentos)
	Local cAliasSPC		:= ""
	Local lTemApo		:= .F.
	Local nI			:= 0
	Local nParamOrder	:= 1
	
	Static oStatLanc
	Static cQueryLanc	:= ""
	
	nHrsNTrab := 0
	aLancamentos := {}
	
	Default cFilFunc := SRA->RA_FILIAL
	Default cMatFunc := SRA->RA_MAT
	
	// Busca os eventos da SPC e SPH
	If oStatLanc == Nil
		cQueryLanc :=	"SELECT "
		cQueryLanc +=		"PC_FILIAL, "
		cQueryLanc +=		"PC_MAT, "
		cQueryLanc +=		"P9_DESC, "
		cQueryLanc +=		"CASE WHEN PC_PDI <> ' ' THEN "
		cQueryLanc +=			"PC_PDI "
		cQueryLanc +=		"ELSE "
		cQueryLanc +=			"PC_PD "
		cQueryLanc +=		"END AS PC_PD, "
		cQueryLanc +=		"CASE WHEN PC_QUANTI > 0 THEN "
		cQueryLanc +=			"SUM((FLOOR(PC_QUANTI) * 60) + ((PC_QUANTI - FLOOR(PC_QUANTI)) * 100)) "
		cQueryLanc +=		"ELSE "
		cQueryLanc +=			"SUM((FLOOR(PC_QUANTC) * 60) + ((PC_QUANTC - FLOOR(PC_QUANTC)) * 100)) "
		cQueryLanc +=		"END AS PC_QUANTC "
		cQueryLanc +=	"FROM " + RetSQLName("SPC") + " SPC "
		cQueryLanc +=	"INNER JOIN " + RetSQLName("SRA") + " SRA ON SRA.RA_FILIAL = SPC.PC_FILIAL AND SRA.RA_MAT = SPC.PC_MAT AND SRA.D_E_L_E_T_ = ? "	// 1
		cQueryLanc +=	"INNER JOIN " + RetSQLName("SP9") + " SP9 ON " + FWJoinFilial("SP9", "SPC") + " AND SP9.P9_CODIGO = (CASE WHEN PC_PDI <> ? THEN PC_PDI ELSE PC_PD END) AND SP9.D_E_L_E_T_ = ? " // 2 e 3
		cQueryLanc +=	"WHERE SPC.PC_FILIAL = ? "												// 4
		cQueryLanc +=		"AND SPC.PC_MAT = ? "												// 5
		cQueryLanc +=		"AND SPC.PC_DATA BETWEEN ? AND ? "									// 6 e 7
		cQueryLanc +=		"AND SPC.PC_DATA >= SRA.RA_ADMISSA "
		cQueryLanc +=		"AND (SPC.PC_DATA <= RA_DEMISSA OR SRA.RA_DEMISSA = ?) "			// 8
		cQueryLanc +=		"AND SPC.D_E_L_E_T_ = ? "											// 9
		cQueryLanc +=		"AND SP9.P9_TIPOCOD = ? "											// 10
		cQueryLanc +=		"AND SP9.P9_IDPON NOT IN (?) "										// 11
		cQueryLanc +=	"GROUP BY PC_FILIAL, PC_MAT, PC_PD, PC_PDI, PC_QUANTI, P9_DESC "
		cQueryLanc +=	"UNION ALL "
		cQueryLanc +=	"SELECT SPH.PH_FILIAL, SPH.PH_MAT, SP9.P9_DESC, "
		cQueryLanc +=		"CASE WHEN PH_PDI <> ' ' THEN "
		cQueryLanc +=			"PH_PDI "
		cQueryLanc +=		"ELSE "
		cQueryLanc +=			"PH_PD "
		cQueryLanc +=		"END AS PH_PD, "
		cQueryLanc +=		"CASE WHEN PH_QUANTI > 0 THEN "
		cQueryLanc +=			"SUM((FLOOR(PH_QUANTI) * 60) + ((PH_QUANTI - FLOOR(PH_QUANTI)) * 100)) "
		cQueryLanc +=		"ELSE "
		cQueryLanc +=			"SUM((FLOOR(PH_QUANTC) * 60) + ((PH_QUANTC - FLOOR(PH_QUANTC)) * 100)) "
		cQueryLanc +=		"END AS PH_QUANTC "
		cQueryLanc +=	"FROM " + RetSQLName("SPH") + " SPH "
		cQueryLanc +=	"INNER JOIN " + RetSQLName("SRA") + " SRA ON SRA.RA_FILIAL = SPH.PH_FILIAL AND SRA.RA_MAT = SPH.PH_MAT AND SRA.D_E_L_E_T_ = ? "	// 12
		cQueryLanc +=	"INNER JOIN " + RetSQLName("SP9") + " SP9 ON " + FWJoinFilial("SP9", "SPH") + " AND SP9.P9_CODIGO = (CASE WHEN PH_PDI <> ? THEN PH_PDI ELSE PH_PD END) AND SP9.D_E_L_E_T_ = ? " // 13 e 14
		cQueryLanc +=	"WHERE SPH.PH_FILIAL = ? "												// 15
		cQueryLanc +=		"AND SPH.PH_MAT = ? "												// 16
		cQueryLanc +=		"AND SPH.PH_DATA BETWEEN ? AND ? "									// 17 e 18
		cQueryLanc +=		"AND SPH.PH_DATA >= SRA.RA_ADMISSA "
		cQueryLanc +=		"AND (SPH.PH_DATA <= RA_DEMISSA OR SRA.RA_DEMISSA = ?) "			// 19
		cQueryLanc +=		"AND SPH.D_E_L_E_T_ = ? "											// 20
		cQueryLanc +=		"AND SP9.P9_TIPOCOD = ? "											// 21
		cQueryLanc +=		"AND SP9.P9_IDPON NOT IN (?) "										// 22
		cQueryLanc +=	"GROUP BY PH_FILIAL, PH_MAT, PH_PD, PH_PDI, PH_QUANTI, SP9.P9_DESC "
		cQueryLanc +=	"ORDER BY PC_FILIAL, PC_MAT, PC_PD "

		cQueryLanc := ChangeQuery(cQueryLanc)

		oStatLanc := FwExecStatement():New(cQueryLanc)
	EndIf

	For nI := 1 To 2
		oStatLanc:SetString(nParamOrder++, " ")				// 1 ou 12
		oStatLanc:SetString(nParamOrder++, " ")				// 2 ou 13
		oStatLanc:SetString(nParamOrder++, " ")				// 3 ou 14
		oStatLanc:SetString(nParamOrder++, cFilFunc)		// 4 ou 15
		oStatLanc:SetString(nParamOrder++, cMatFunc)		// 5 ou 16
		oStatLanc:SetString(nParamOrder++, dToS(dDataIni))	// 6 ou 17
		oStatLanc:SetString(nParamOrder++, dToS(dDataFim))	// 7 ou 18
		oStatLanc:SetString(nParamOrder++, " ")				// 8 ou 19
		oStatLanc:SetString(nParamOrder++, " ")				// 9 ou 20
		oStatLanc:SetString(nParamOrder++, "2")				// 10 ou 21
		oStatLanc:SetIn(nParamOrder++, {'005A', '006A'})	// 11 ou 22
	Next

	cAliasSPC := oStatLanc:OpenAlias()
	
	If lTemApo	:= (cAliasSPC)->(!EoF())
		
		While (cAliasSPC)->(!EoF())
			
			//Totaliza as horas não trabalhadas
			nHrsNTrab += (cAliasSPC)->PC_QUANTC
			
			// Guarda o detalhamento dos apontamentos
			(cAliasSPC)->(aAdd(aLancamentos,{PC_PD, Min2Hrs(PC_QUANTC), P9_DESC}))
			
			(cAliasSPC)->(dbSkip())
		EndDo
		
		// A query retorna em minutos, então é necessário Converter em horas 
		nHrsNTrab := Min2Hrs(nHrsNTrab)
		
	EndIf
	
	(cAliasSPC)->(dbCloseArea())
	
Return lTemApo

/*/{Protheus.doc} GetAbo
Busca as horas não trabalhadas nas tabelas de apontamento
@type Static Function
@author Cícero Alves
@since 12/12/2023
@param cFilFunc, Caracter, Filial do funcinário
@param cMatFunc, Caracter, Matrícula do funcinário
@param dDataIni, Data, Data inicial da busca
@param dDataFim, Data, Data final da busca
@param aHrsAbo, Array, Deve ser passado por referência; horas abonadas no período [n, 1] = Quantidade de registros na tabela; [n, 2] = Total de horas Abonadas
/*/
Static Function GetAbo(cFilFunc, cMatFunc, dDataIni, dDataFim, aAbonos)
	Local nHrsAbo		:= 0
	Local nI			:= 0
	Local nParamOrder	:= 1
	Local cAliasAbo		:= ""

	Static oStatAbo
	Static cQueryAbo	:= ""
	
	aAbonos := {}
	
	Default cFilFunc := SRA->RA_FILIAL
	Default cMatFunc := SRA->RA_MAT
	
	//Busca os abonos
	If oStatAbo == Nil
		cQueryAbo :=	"SELECT "
		cQueryAbo += 		"SPC.PC_FILIAL, "
		cQueryAbo +=		"SPC.PC_MAT, "
		cQueryAbo +=		"SPC.PC_ABONO, "
		cQueryAbo +=		"SUM((FLOOR(SPC.PC_QTABONO) * 60) + ((SPC.PC_QTABONO - FLOOR(SPC.PC_QTABONO)) * 100)) AS PC_QTABONO, "
		cQueryAbo +=		"SP6.P6_DESC "
		cQueryAbo +=	"FROM " + RetSQLName("SPC") + " SPC "
		cQueryAbo +=	"INNER JOIN " + RetSQLName("SRA") + " SRA ON SRA.RA_FILIAL = SPC.PC_FILIAL AND SRA.RA_MAT = SPC.PC_MAT AND SRA.D_E_L_E_T_ = ? "
		cQueryAbo +=	"INNER JOIN " + RetSQLName("SP6") + " SP6 ON " + FWJoinFilial("SP6", "SPC") + " AND SP6.P6_CODIGO = SPC.PC_ABONO AND SP6.D_E_L_E_T_ = ? "
		cQueryAbo +=	"INNER JOIN " + RetSQLName("SP9") + " SP9 ON " + FWJoinFilial("SP9", "SPC") + " AND SP9.P9_CODIGO = SPC.PC_PD AND SP9.D_E_L_E_T_ = ? "
		cQueryAbo +=	"WHERE SPC.PC_FILIAL = ? "
		cQueryAbo +=		"AND SPC.PC_MAT = ? "
		cQueryAbo +=		"AND SPC.PC_DATA BETWEEN ? AND ? "
		cQueryAbo +=		"AND SPC.PC_DATA >= SRA.RA_ADMISSA "
		cQueryAbo +=		"AND (SPC.PC_DATA <= RA_DEMISSA OR SRA.RA_DEMISSA = ?) "
		cQueryAbo +=		"AND SPC.D_E_L_E_T_ = ? "
		cQueryAbo +=		"AND SP9.P9_TIPOCOD = ? "
		cQueryAbo +=		"AND SP9.P9_IDPON NOT IN (?) "
		cQueryAbo +=	"GROUP BY SPC.PC_FILIAL, SPC.PC_MAT, PC_ABONO, P6_DESC "
		cQueryAbo +=	"UNION ALL "
		cQueryAbo +=	"SELECT "
		cQueryAbo +=		"SPH.PH_FILIAL, "
		cQueryAbo +=		"SPH.PH_MAT, "
		cQueryAbo +=		"SPH.PH_ABONO, "
		cQueryAbo +=		"SUM((FLOOR(SPH.PH_QTABONO) * 60) + ((SPH.PH_QTABONO - FLOOR(SPH.PH_QTABONO)) * 100)) AS PC_QTABONO, "
		cQueryAbo +=		"SP6.P6_DESC "
		cQueryAbo +=	"FROM " + RetSQLName("SPH") + " SPH "
		cQueryAbo +=	"INNER JOIN " + RetSQLName("SRA") + " SRA ON SRA.RA_FILIAL = SPH.PH_FILIAL AND SRA.RA_MAT = SPH.PH_MAT AND SRA.D_E_L_E_T_ = ? "
		cQueryAbo +=	"INNER JOIN " + RetSQLName("SP6") + " SP6 ON " + FWJoinFilial("SP6", "SPH") + " AND SP6.P6_CODIGO = SPH.PH_ABONO AND SP6.D_E_L_E_T_ = ? "
		cQueryAbo +=	"INNER JOIN " + RetSQLName("SP9") + " SP9 ON " + FWJoinFilial("SP9", "SPH") + " AND SP9.P9_CODIGO = SPH.PH_PD AND SP9.D_E_L_E_T_ = ? "
		cQueryAbo +=	"WHERE SPH.PH_FILIAL = ? "
		cQueryAbo +=		"AND SPH.PH_MAT = ? "
		cQueryAbo +=		"AND SPH.PH_DATA BETWEEN ? AND ? "
		cQueryAbo +=		"AND SPH.PH_DATA >= SRA.RA_ADMISSA "
		cQueryAbo +=		"AND (SPH.PH_DATA <= RA_DEMISSA OR SRA.RA_DEMISSA = ?) "
		cQueryAbo +=		"AND SPH.D_E_L_E_T_ = ? "
		cQueryAbo +=		"AND SP9.P9_TIPOCOD = ? "
		cQueryAbo +=		"AND SP9.P9_IDPON NOT IN (?) "
		cQueryAbo +=	"GROUP BY SPH.PH_FILIAL, SPH.PH_MAT, SPH.PH_ABONO, SP6.P6_DESC "
		cQueryAbo +=	"ORDER BY PC_FILIAL, PC_MAT, PC_ABONO "

		cQueryAbo := ChangeQuery(cQueryAbo)

		oStatAbo := FwExecStatement():New(cQueryAbo)
	EndIf

	For nI := 1 To 2
		oStatAbo:SetString(nParamOrder++, " ")				// 1 ou 12
		oStatAbo:SetString(nParamOrder++, " ")				// 2 ou 13
		oStatAbo:SetString(nParamOrder++, " ")				// 3 ou 14
		oStatAbo:SetString(nParamOrder++, cFilFunc)			// 4 ou 15
		oStatAbo:SetString(nParamOrder++, cMatFunc)			// 5 ou 16
		oStatAbo:SetString(nParamOrder++, dToS(dDataIni))	// 6 ou 17
		oStatAbo:SetString(nParamOrder++, dToS(dDataFim))	// 7 ou 18
		oStatAbo:SetString(nParamOrder++, " ")				// 8 ou 19
		oStatAbo:SetString(nParamOrder++, " ")				// 9 ou 20
		oStatAbo:SetString(nParamOrder++, "2")				// 10 ou 21
		oStatAbo:SetIn(nParamOrder++, {'005A', '006A'})		// 11 ou 22
	Next

	cAliasAbo := oStatAbo:OpenAlias()
	
	While (cAliasAbo)->(!EoF())
		
		//Totaliza as horas abonadas
		nHrsAbo += (cAliasAbo)->PC_QTABONO
		
		// Guarda o detalhamento dos abonos
		(cAliasAbo)->(aAdd(aAbonos, {PC_ABONO, Min2Hrs(PC_QTABONO), P6_DESC}))
		
		(cAliasAbo)->(dbSkip())
	EndDo
	
	// A query retorna em minutos, então é necessário Converter em horas 
	nHrsAbo := Min2Hrs(nHrsAbo)
	
	(cAliasAbo)->(dbCloseArea())
	
Return nHrsAbo

Static Function FormatDet(aLancamentos, nHrsNTrab, aAbonos, nHrsAbo, aAfast, lCentesimal)
	
	Local aDetalhes := {}
	Local lInclui	:= .F.
	Local nI := 0
	
	DEFAULT aLancamentos 	:= {}
	DEFAULT aAbonos 		:= {}
	DEFAULT aAfast 			:= {0, {}}
	DEFAULT lCentesimal 	:= .F.
	
	lInclui := Len(aLancamentos) > 0 .Or. Len(aAbonos) > 0 .Or. Len(aAfast[2]) > 0 
	
	If Empty(aLancamentos) .And. lInclui
		aAdd(aDetalhes, {"TIPO": "1"}) // Para aparecer o cabeçalho no relatório
	Else
		For nI := 1 To Len(aLancamentos)
			aAdd(aDetalhes, {;
				"TIPO": "1",;
				"CODIGO": aLancamentos[nI][1],;
				"HORAS": If(lCentesimal, fConvHr(aLancamentos[nI][2], "D"), aLancamentos[nI][2]),;
				"DESCRICAO": aLancamentos[nI][3],;
				"PERCENTUAL": fConvHr(aLancamentos[nI][2], "D") / fConvHr(nHrsNTrab, "D");
			})
		Next nI
	EndIf
	
	If Empty(aAbonos) .And. lInclui
		aAdd(aDetalhes, {"TIPO": "2"}) // Para aparecer o cabeçalho no relatório
	Else
		For nI := 1 To Len(aAbonos)
			aAdd(aDetalhes, {;
				"TIPO": "2",;
				"CODIGO": aAbonos[nI][1],;
				"HORAS": If(lCentesimal, fConvHr(aAbonos[nI][2], "D"), aAbonos[nI][2]),;
				"DESCRICAO": aAbonos[nI][3],;
				"PERCENTUAL": fConvHr(aAbonos[nI][2], "D") / fConvHr(nHrsAbo, "D");
			})
		Next nI
	EndIf
	
	If Empty(aAfast[2]) .And. lInclui
		aAdd(aDetalhes, {"TIPO": "3"}) // Para aparecer o cabeçalho no relatório
	Else
		For nI := 1 To Len(aAfast[2])
			aAdd(aDetalhes, {;
				"TIPO": "3",;
				"CODIGO": aAfast[2][nI][1],;
				"HORAS": If(lCentesimal, fConvHr(aAfast[2][nI][2], "D"), aAfast[2][nI][2]),;
				"DESCRICAO": aAfast[2][nI][3],;
				"PERCENTUAL": fConvHr(aAfast[2][nI][2], "D") / fConvHr(aAfast[1], "D");
			})
		Next nI
	EndIf
	
Return aDetalhes

Static Function Anonimiza(cConteudo)
Return SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(AllTrim(cConteudo)), 1, 10)
