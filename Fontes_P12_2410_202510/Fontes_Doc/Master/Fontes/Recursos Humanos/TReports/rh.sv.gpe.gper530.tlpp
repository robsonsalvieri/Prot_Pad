#Include "TOTVS.FRAMEWORK.TREPORTS.INTEGRATEDPROVIDER.th"
#Include "TLPP-CORE.th"
#Include "TLPP-REST.th"
#Include "rh.sv.gpe.gper530.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER530TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório de Dedução do Salário Maternidade 13º Salário (GPER530).
    @type Class
    @version 12.1.2410
    @author karina.alves
    @since 11/12/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,CTT", customTables="SRA,CTT", name="Relatório de Dedução do Salário Maternidade 13 Salário", country="ALL", initialRelease="12.1.2310")
Class GPER530TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getData()        As Object
    Public Method getSchema()      As Object

    @Get("/api/rh/smartview/v1/options/GPER530/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} GPER530TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author karina.alves
    @since 11/12/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER530TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a área do objeto de negócio
    self:appendArea(STR0001) // "RH"

    // Define o nome e descrição do objeto de negócio
    self:setDisplayName(STR0002) // "Relatório de Dedução do Salário Maternidade 13º Salário"
    self:setDescription(STR0003) // "Este objeto de negócio emite o Relatório de Dedução do Salário Maternidade 13º Salário."
Return self

/*/{Protheus.doc} GPER530TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author karina.alves
    @since 11/12/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER530TReportsBusinessObject
    // Declaração das variáveis locais
    Local aPDFields            As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields           As Array     // Estrutura de todos os campos do ON
    Local aCodFol              As Array
    Local cAlias               As Character // Alias do arquivo temporário
    Local cQuery               As Character // Query a ser executada
    Local cFilExec             As Character // Nome da filial de execução
    Local cEmpExec             As Character // Nome da empresa de execução
    Local cCustomFields        As Character // Campos customizados no formato SQL
    Local cFilialDe            As Character // Parâmetro Filial De
    Local cFilialAte           As Character // Parâmetro Filial Ate
    Local cCCDe                As Character // Parâmetro CC De
    Local cCCAte               As Character // Parâmetro CC Ate
    Local cMatDe               As Character // Parâmetro Mat De
    Local cMatAte              As Character // Parâmetro Mat Ate
    Local cNomeDe              As Character // Parâmetro Nome De
    Local cNomeAte             As Character // Parâmetro Nome Ate
    Local cDataRef             As Character // Parâmetro Data de referência
    Local cFiltro              As Character // Filtro do objeto de negócio
    Local cName                As Character
    Local cFilAnterior         As Character
    Local cValidFil            As Character
    Local cAcessaSRA           As Character
    Local cAcessaSRC           As Character
    Local jParams              As JSON      // Parâmetros do relatório
    Local jData                As JSON      // Informações a serem adicionadas no ON
    Local jLGPD                As JSON      // Indica quais campos devem ser ofuscados
    Local nParamOrder          As Numeric   // Contador de Parâmetros
    Local nFields              As Numeric   // Contador de campos
    Local nDiasSM              As Numeric
    Local nValDed13            As Numeric
    Local nValDed              As Numeric
    Local nAvos                As Numeric
    Local nVl13Sal             As Numeric
    Local oExec                As Object
    Local lObfuscated          As Logical   // Flag que indica se tem algum campo para ofuscar
    Local lAchou               As Logical
    Local lAchouDed            As Logical
    Local dDataRef             As Date
    Local bCondDed             As CodeBlock

    // Inicialização das variáveis
    aPDFields            := {}
    aAllFields           := {}
    aCodFol              := {}
    cAlias               := ""
    cQuery               := ""
    cFilExec             := AllTrim(FwFilialName())
    cEmpExec             := AllTrim(FwEmpName(cEmpAnt))
    cCustomFields        := ""
    cName                := ""
    cFilialDe            := ""
    cFilialAte           := ""
    cCCDe                := ""
    cCCAte               := ""
    cMatDe               := ""
    cMatAte              := ""
    cNomeDe              := ""
    cNomeAte             := ""
    cDataRef             := ""
    cAcessaSRA           := &("{|| " + ChkRH("GPER530", "SRA", "2") + "}")
    cAcessaSRC           := &("{|| " + ChkRH("GPER530", "SRC", "2") + "}")
    cFilAnterior         := ""
    cValidFil            := fValidFil()
    cFiltro              := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    jParams              := oFilter:getParameters()
    jData                := JSONObject():New()
    jLGPD                := JSONObject():New()
    nParamOrder          := 1
    nFields              := 1
    lObfuscated          := .F.
    lAchou               := .F.
    lAchouDed            := .F.
    nDiasSM              := 0
    nValDed13            := 0
    nValDed              := 0
    nAvos                := 0
    nVl13Sal             := 0
    dDataRef             := CToD("")
    bCondDed             := {|| (.T.)}

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cFilialDe     := IIf(jParams:hasProperty("PAR_FILIALDE")  .and. Len(jParams["PAR_FILIALDE"])  > 0 .and.!Empty(jParams["PAR_FILIALDE"][1]),  jParams["PAR_FILIALDE"][1],  "")
        cFilialAte    := IIf(jParams:hasProperty("PAR_FILIALATE") .and. Len(jParams["PAR_FILIALATE"]) > 0 .and.!Empty(jParams["PAR_FILIALATE"][1]), jParams["PAR_FILIALATE"][1], Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCCDe         := IIf(jParams:hasProperty("PAR_CCDE")      .and. Len(jParams["PAR_CCDE"])      > 0 .and.!Empty(jParams["PAR_CCDE"][1]),      jParams["PAR_CCDE"][1],      "")
        cCCAte        := IIf(jParams:hasProperty("PAR_CCATE")     .and. Len(jParams["PAR_CCATE"])     > 0 .and.!Empty(jParams["PAR_CCATE"][1]),     jParams["PAR_CCATE"][1],     Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cMatDe        := IIf(jParams:hasProperty("PAR_MATDE")     .and. Len(jParams["PAR_MATDE"])     > 0 .and.!Empty(jParams["PAR_MATDE"][1]),     jParams["PAR_MATDE"][1],     "")
        cMatAte       := IIf(jParams:hasProperty("PAR_MATATE")    .and. Len(jParams["PAR_MATATE"])    > 0 .and.!Empty(jParams["PAR_MATATE"][1]),    jParams["PAR_MATATE"][1],    Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cNomeDe       := IIf(jParams:hasProperty("PAR_NOMEDE")    .and. Len(jParams["PAR_NOMEDE"])    > 0 .and.!Empty(jParams["PAR_NOMEDE"][1]),    jParams["PAR_NOMEDE"][1],    "")
        cNomeAte      := IIf(jParams:hasProperty("PAR_NOMEATE")   .and. Len(jParams["PAR_NOMEATE"])   > 0 .and.!Empty(jParams["PAR_NOMEATE"][1]),   jParams["PAR_NOMEATE"][1],   Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
        cDataRef      := IIf(jParams:hasProperty("PAR_DATAREF")   .and. Len(jParams["PAR_DATAREF"])   > 0 .and.!Empty(jParams["PAR_DATAREF"][1]),   jParams["PAR_DATAREF"][1],   FwTimeStamp(6, Date()))
    EndIf

    // Trata as variáveis de data para ficarem no formato do banco
    cDataRef := StrTran(SubStr(cDataRef, 1, 10), "-", "")

    // Converte a data para o tipo data
    dDataRef := SToD(cDataRef)

    // Montagem da query de busca
    cQuery := "SELECT "
    cQuery +=     "RA_FILIAL, "
    cQuery +=     "RA_CC, "
    cQuery +=     "CTT_DESC01, "
    cQuery +=     "RA_MAT, "
    cQuery +=     "RA_NOME, "
    cQuery +=     "RA_NSOCIAL, "
    cQuery +=     "SRA.R_E_C_N_O_ REGSRA "
    cQuery +=     "? " // cCustomFields
    cQuery += "FROM "
    cQuery +=     RetSQLName("SRA") + " SRA "
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("CTT") + " CTT "
    cQuery +=         "ON "
    cQuery +=             FwJoinFilial("CTT", "SRA") + " "
    cQuery +=             "AND CTT_CUSTO = RA_CC "
    cQuery +=             "AND CTT.D_E_L_E_T_ = ? " // " "
    cQuery += "WHERE "
    cQuery +=       "RA_FILIAL   BETWEEN ? AND ? " // cFilialDe e cFilialAte
    cQuery +=       "AND RA_CC   BETWEEN ? AND ? " // cCCDe e cCCAte
    cQuery +=       "AND RA_MAT  BETWEEN ? AND ? " // cMatDe e cMatAte
    cQuery +=       "AND RA_NOME BETWEEN ? AND ? " // cNomeDe e cNomeAte
    cQuery +=       "AND EXISTS ( "
    cQuery +=           "SELECT "
    cQuery +=               "1 AS EXISTE"
    cQuery +=           "FROM "
    cQuery +=               RetSQLName("SR8") + " SR8 "
    cQuery +=               "INNER JOIN "
    cQuery +=                   RetSQLName("RCM") + " RCM "
    cQuery +=                   "ON "
    cQuery +=                       FwJoinFilial("RCM", "SR8") + " "
    cQuery +=                       "AND RCM_TIPO                       = R8_TIPOAFA "
    cQuery +=                       "AND SUBSTRING(RCM_CODSEF, 1, 1)    = ? "  // "Q"
    cQuery +=                       "AND (RCM_CODSEF != ? OR RCM_DIASEM > ?) " // "Q1" | 0
    cQuery +=           "WHERE "
    cQuery +=               FwJoinFilial("SR8", "SRA") + " "
    cQuery +=               "AND R8_MAT         = RA_MAT "
    cQuery +=               "AND SR8.D_E_L_E_T_ = ? "  // " "
    cQuery +=           ") "
    cQuery +=       "AND SRA.D_E_L_E_T_ = ? " // " "
    cQuery +=       "? "                      // cFiltro
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe FwExecStatement()
    oExec := FwExecStatement():New(cQuery)

    // Captura e define os campos personalizados das tabelas SRA
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "CTT"})
    oExec:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    // Define os valores dos bind parameters
    oExec:SetString(nParamOrder++,  " ")
    oExec:SetString(nParamOrder++,  cFilialDe)
    oExec:SetString(nParamOrder++,  cFilialAte)
    oExec:SetString(nParamOrder++,  cCCDe)
    oExec:SetString(nParamOrder++,  cCCAte)
    oExec:SetString(nParamOrder++,  cMatDe)
    oExec:SetString(nParamOrder++,  cMatAte)
    oExec:SetString(nParamOrder++,  cNomeDe)
    oExec:SetString(nParamOrder++,  cNomeAte)
    oExec:SetString(nParamOrder++,  "Q")
    oExec:SetString(nParamOrder++,  "Q1")
    oExec:SetNumeric(nParamOrder++, 0)
    oExec:SetString(nParamOrder++,  " ")
    oExec:SetString(nParamOrder++,  " ")
    oExec:SetUnsafe(nParamOrder++,  cFiltro)

    // Executa a query e captura o alias
    cAlias := oExec:OpenAlias()

    // Abre a tabela SRA e SRC
    DBSelectArea("SRA")
    DBSelectArea("SRC")
    DBSetOrder(1)

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))
        // Consiste controle de troca de filial, acesso à tabela SRA, verifica se a filial é válida e captura os códigos de folha da mesma
        If ((cAlias)->RA_FILIAL == cFilAnterior .or. (Eval(cAcessaSRA) .and. (cAlias)->RA_FILIAL $ cValidFil .and. Fp_CodFol(@aCodFol, (cAlias)->RA_FILIAL)))
            // Atualiza a filial atual
            cFilAnterior := (cAlias)->RA_FILIAL

            // Posiciona no cadastro do funcionário para usar na função fPgMater() chamada dentro da função fDiasSm13()
            SRA->(DBGoTo((cAlias)->REGSRA))

            // Inicializa variáveis a cada funcionário e apura os dias de licença maternidade ocorridos no ano da data de referencia
            nDiasSM   := fDiasSm13(Left(DToS(dDataRef), 4), (cAlias)->RA_FILIAL, (cAlias)->RA_MAT)
            nValDed13 := 0
            lAchou    := .F.
            lAchouDed := .F.
            bCondDed  := {|| (.T.)}

            If (nDiasSM > 0)
                // Verifica a existência da verba de dedução de Sal. Maternidade para 13º salário
                If (!Empty(aCodFol[670][1]))
                    SRC->(DBSetOrder(1))
                    lAchouDed := SRC->(DBSeek((cAlias)->RA_FILIAL + (cAlias)->RA_MAT + aCodFol[670][1]))
                    If (lAchouDed)
                        // Condição para testar a verba de dedução sal. maternidade 13º sal. caso exista
                        bCondDed := {|| (SRC->RC_PD == aCodFol[670][1])}
                    EndIf
                EndIf

                // Se não encontrou a verba de dedução de sal. maternidade para 13º salário, pesquisa o movimento inteiro para apurar a dedução
                If (!lAchouDed)
                    lAchou := SRC->(DBSeek((cAlias)->RA_FILIAL + (cAlias)->RA_MAT))
                EndIf

                If (lAchou .or. lAchouDed)
                    While (SRC->(!EOF()) .and. (cAlias)->RA_FILIAL + (cAlias)->RA_MAT == SRC->RC_FILIAL + SRC->RC_MAT .and. Eval(bCondDed))
                        If (!lAchouDed)
                            // Consiste controle de acesso à tabela SRC
                            If (Eval(cAcessaSRC))
                                // Verifica 13º salario indenizado e 13 º salário aviso prévio
                                // Média 13º salario rescisão e média 13º salario sobre aviso indenizado rescisão
                                If (SRC->RC_PD == aCodFol[114][1] .or. SRC->RC_PD == aCodFol[115][1] .or. SRC->RC_PD == aCodFol[251][1] .or. SRC->RC_PD == aCodFol[253][1])
                                    If (SRC->RC_PD == aCodFol[114][1] .or. SRC->RC_PD == aCodFol[115][1])
                                        nAvos += Int(SRC->RC_HORAS)
                                    EndIf

                                    nVl13Sal += SRC->RC_VALOR
                                EndIf
                            EndIf
                        Else
                            // Acumula os valores já calculados de dedução de salário maternidade para 13º salário
                            nValDed13 += SRC->RC_VALOR
                        EndIf

                        // Pula para o próximo registro
                        SRC->(DBSkip())
                    End
                EndIf
            EndIf

            If ((nVl13Sal > 0 .or. nValDed13 > 0) .and. nDiasSM > 0)
                If (nDiasSM > 0 .and. (nAvos > 0 .or. nValDed13 > 0))
                    nValDed := IIf(nValDed13 == 0, Round(((nVl13Sal / nAvos) / 30) * nDiasSM, 2), nValDed13)

                    If (nAvos == 0 .and. nValDed13 > 0)
                        nAvos := nDiasSM / 30
                    EndIf
                EndIf
            EndIf

            // Inclui o registro apenas se houver algum valor
            If (nAvos > 0 .and. nDiasSM > 0 .and. nValDed > 0)
                jData                 := JSONObject():New()
                jData["FilialExec"]   := cFilExec
                jData["EmpresaExec"]  := cEmpExec
                jData["RA_FILIAL"]    := RTrim((cAlias)->RA_FILIAL)
                jData["RA_MAT"]       := RTrim((cAlias)->RA_MAT)
                jData["RA_NOME"]      := RTrim(IIf(jLGPD["RA_NOME"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),    (cAlias)->RA_NOME))
                jData["RA_NSOCIAL"]   := RTrim(IIf(jLGPD["RA_NSOCIAL"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NSOCIAL), (cAlias)->RA_NSOCIAL))
                jData["CTT_DESC01"]   := RTrim((cAlias)->CTT_DESC01)
                jData["RA_AVOS13"]    := Round(nAvos, 0)
                jData["RA_DIASAFAST"] := nDiasSM
                jData["RA_VLRDEDUD"]  := nValDed

                // Adiciona os campos customizados no JSON
                GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

                // Adiciona as informações da linha
                self:oData:appendData(jData)
            EndIf
        EndIf

        // Zera os valores
        nVl13Sal := 0
        nDiasSM  := 0
        nAvos    := 0
        nValDed  := 0

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())
    oExec:Destroy()
    oExec := NIL

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
    FwFreeObj(jParams)
    FwFreeArray(aAllFields)
    FwFreeArray(aPDFields)
Return self:oData

/*/{Protheus.doc} GPER530TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2410
    @author karina.alves
    @since 11/12/2024
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER530TReportsBusinessObject
    // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_FILIALDE",  STR0004, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Filial De"
        STR0015) // "Código da Filial inicial para filtrar os dados."
    self:oSchema:addParameter("PAR_FILIALATE", STR0005, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Filial Até"
        STR0016) // "Código da Filial final para filtrar os dados."
    self:oSchema:addParameter("PAR_CCDE",      STR0006, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "C.C. De"
        STR0017) // "Código do Centro de Custo inicial para filtrar os dados."
    self:oSchema:addParameter("PAR_CCATE",     STR0007, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "C.C. Até"
        STR0018) // "Código do Centro de Custo final para filtrar os dados."
    self:oSchema:addParameter("PAR_MATDE",     STR0008, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Matrícula De"
        STR0019) // "Código da Matrícula inicial para filtrar os dados."
    self:oSchema:addParameter("PAR_MATATE",    STR0009, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Matrícula Até"
        STR0020) // "Código da Matrícula final para filtrar os dados."
    self:oSchema:addParameter("PAR_NOMEDE",    STR0010, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Nome De"
        STR0021) // "Nome inicial para filtrar os dados."
    self:oSchema:addParameter("PAR_NOMEATE",   STR0011, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Nome Até"
        STR0022) // "Nome final para filtrar os dados."
    self:oSchema:addParameter("PAR_DATAREF",   STR0012, "date",   .F., NIL, NIL, NIL, NIL, NIL,; // "Data de Referência"
        STR0023) // "Data de referência que será usada como ano de referência."
    self:oSchema:addParameter("PAR_QUEBRA",    STR0013, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Quebra por"
        STR0024) // "Quebra/agrupamento dos registros do Relatório."
    self:oSchema:addParameter("PAR_GRAFICO",   STR0014, "string", .F., NIL, NIL, NIL, NIL, NIL,; // "Exibe Gráfico"
        STR0025) // "Controla se exibe o Gráfico Totalizador de Valor Deduzido por Filial no final do Relatório."

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_FILIALDE",  "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("PAR_FILIALATE", "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("PAR_CCDE",      "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("PAR_CCATE",     "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("PAR_MATDE",     "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("PAR_MATATE",    "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("PAR_QUEBRA",    "/api/rh/smartview/v1/options/GPER530/PAR_QUEBRA",      1)
    self:setCustomURL("PAR_GRAFICO",   "/api/rh/smartview/v1/options/GPEParams/getSNOptions",  1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",   STR0026, "string", STR0026, "FilialExec",   NIL, NIL, NIL, .F.) // "Filial de Execução"
    self:oSchema:addProperty("EmpresaExec",  STR0027, "string", STR0027, "EmpresaExec",  NIL, NIL, NIL, .F.) // "Empresa de Execução"
    self:oSchema:addProperty("RA_FILIAL",    STR0028, "string", STR0028, "RA_FILIAL",    NIL, NIL, NIL, .T.) // "Filial"
    self:oSchema:addProperty("RA_CC",        STR0029, "string", STR0029, "RA_CC",        NIL, NIL, NIL, .T.) // "Código do Centro de Custo"
    self:oSchema:addProperty("CTT_DESC01",   STR0030, "string", STR0030, "RA_CC",        NIL, NIL, NIL, .T.) // "Descrição do Centro de Custo"
    self:oSchema:addProperty("RA_MAT",       STR0031, "string", STR0031, "RA_MAT",       NIL, NIL, NIL, .T.) // "Matrícula"
    self:oSchema:addProperty("RA_NOME",      STR0032, "string", STR0032, "RA_NOME",      NIL, NIL, NIL, .T.) // "Nome"
    self:oSchema:addProperty("RA_NSOCIAL",   STR0033, "string", STR0033, "RA_NSOCIAL",   NIL, NIL, NIL, .T.) // "Nome Social"
    self:oSchema:addProperty("RA_AVOS13",    STR0034, "number", STR0034, "RA_AVOS13",    NIL, NIL, NIL, .T.) // "Avos 13"
    self:oSchema:addProperty("RA_DIASAFAST", STR0035, "number", STR0035, "RA_DIASAFAST", NIL, NIL, NIL, .T.) // "Dias Afastamento"
    self:oSchema:addProperty("RA_VLRDEDUD",  STR0036, "number", STR0036, "RA_VLRDEDUD",  NIL, NIL, NIL, .T.) // "Valor Deduzido"
Return self:oSchema

/*/{Protheus.doc} GPER490Params::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2410
    @author arthur.sales
    @since 12/12/2024
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() As Variant Class GPER530TReportsBusinessObject
    // Declaração das variáveis locais
    Local jPath     As JSON    // Path parameters da requisição
    Local jResponse As JSON    // Resposta da requisição
    Local jItem     As JSON    // Item do valor do parâmetro
    Local nValues   As Numeric // Contador dos valores do parâmetro
    Local aValues   As Array   // Valores do parâmetro

    // Inicialização das variáveis
    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Verifica qual parâmetro foi solicitado
    If (cParam == "PAR_QUEBRA")
        // Monta o vetor de valores do parâmetro
        AAdd(aValues, FwHTTPEncode(STR0028)) // "Filial"
        AAdd(aValues, FwHTTPEncode(STR0037)) // "Filial + Centro de Custo"
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL
