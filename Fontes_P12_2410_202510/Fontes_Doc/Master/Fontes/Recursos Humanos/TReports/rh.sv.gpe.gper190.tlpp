#Include "totvs.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper190.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER190TReportsBusinessObject
    Classe do objeto de negócio do Relatorio Contrato Experiência / Exame Medico.
    @type Class
    @version 12.1.2310
    @author tp.yuri.mportoli
    @since 07/11/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,CTT", customTables="SRA,CTT", name="Relatório Contrato Experiência / Exame Medico.", country="ALL", initialRelease="12.1.2310")
Class GPER190TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object

    @Get("/api/rh/smartview/v1/options/GPER190/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} new
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author tp.yuri.mporto
    @since 07/11/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER190TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Relatório Contrato Experiência/Exame Médico"

Return self

/*/{Protheus.doc} getDescription
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author tp.yuri.mporto
    @since 07/11/2024
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER190TReportsBusinessObject
Return STR0003  //"Este objeto de negócio traz as informações do Relatório Contrato Experiência/Exame Médico."

 
 
 
/*/{Protheus.doc} getData
   Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author tp.yuri.mporto
    @since 07/11/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER190TReportsBusinessObject


    // Declaração das variáveis locais
    Local cAlias     As Character       // Alias do arquivo temporário principal
    Local cFilExec   As Character       // Nome da filial de execução
    Local cEmpExec   As Character       // Nome da empresa de execução
    Local cAcessaSRA As Character       // Macro execução para validar acesso à tabela SRA
    Local cValidFil  As Character       // Filiais válidas
    Local cAuxMat    As Character       // Matrícula do funcionário anterior
    Local cAuxFil    As Character       // Filial do funcionário anterior
    Local aInfo      As Array           // Informações da filial
    Local cName         As Character    // Nome real do campo
    Local aPDFields     As Array        // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields    As Array        // Estrutura de todos os campos do ON
    Local lObfuscated   As Logical      // Flag que indica se tem algum campo para ofuscar
    Local jData         As JSON         // Informações a serem adicionadas no ON
    Local jLGPD         As JSON         // Indica quais campos devem ser ofuscados
    Local nFields       As Numeric

    // Inicialização das variáveis locais
    cAlias      := QueryDef(oFilter, self:getCustomFields())
    cFilExec    := AllTrim(FwFilialName())
    cEmpExec    := AllTrim(FwEmpName(cEmpAnt))
    cAcessaSRA  := &("{|| " + ChkRH("GPER190", "SRA", "2") + "}")
    cValidFil   := fValidFil()
    cAuxMat     := (cAlias)->(RA_MAT)
    cAuxFil     := (cAlias)->(RA_FILIAL)
    aInfo       := {}
    nFields     := 1
    aPDFields   := {}
    aAllFields  := {}
    lObfuscated := .F.
    jData       := NIL
    jLGPD       := JSONObject():New()

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getRealName())  //Realname lê o nome dos campos do sistema.
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))
   
        // Verifica o acesso à tabela SRA, se a filial é válida e se encontrou as informações da filial
        If ((!Empty(aInfo) .and. (cAlias)->RA_FILIAL == cAuxFil) .or. (Eval(cAcessaSRA) .and. (cAlias)->RA_FILIAL $ cValidFil .and. fInfo(@aInfo, (cAlias)->RA_FILIAL)))


            // Adiciona as informações da linha
            jData := {;
                "FilialExec": cFilExec,;
                "EmpresaExec": cEmpExec,;
                "Branch": IIf(jLGPD["RA_FILIAL"],                   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),                Trim((cAlias)->RA_FILIAL)),;
                "EmployeeCode": IIf(jLGPD["RA_MAT"],                FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),                   Trim((cAlias)->RA_MAT)),;
                "EmployeeCostCenter": IIf(jLGPD["RA_CC"],           FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),                    Trim((cAlias)->RA_CC)),;
                "EmployeeName": IIf(jLGPD["RA_NOME"],               FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),                  Trim((cAlias)->RA_NOME)),;
                "EmployeeCategory": IIf(jLGPD["RA_CATFUNC"],        FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CATFUNC),               Trim((cAlias)->RA_CATFUNC)),;
                "EmployeeAdmission": IIf(jLGPD["RA_ADMISSA"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_ADMISSA),         totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_ADMISSA)),;
                "EmployeeDateTest1": IIf(jLGPD["RA_VCTOEXP"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_VCTOEXP),         totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_VCTOEXP)),;
                "EmployeeDateTest2": IIf(jLGPD["RA_VCTEXP2"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_VCTEXP2),         totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_VCTEXP2)),;
                "MedicalExamination": IIf(jLGPD["RA_EXAMEDI"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_EXAMEDI),         totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_EXAMEDI)),;
                "EmployeeCostCenterDesc": IIf(jLGPD["CTT_DESC01"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01),               Trim((cAlias)->CTT_DESC01));
                }



            // Adiciona os campos customizados no JSON
            GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

            // Adiciona as informações da linha
            self:oData:appendData(jData)


        EndIf


        // Pula para o próximo registro
        (cAlias)->(DBSkip())

        // Verifica se trocou de funcionário
        If (!Empty(cAuxFil + cAuxMat) .and. cAuxFil + cAuxMat != (cAlias)->(RA_FILIAL + RA_MAT))
            // Atualiza a filial e matrícula do funcionário e reinicia o contador de dependentes
            cAuxFil   := (cAlias)->(RA_FILIAL)
            cAuxMat   := (cAlias)->(RA_MAT)
        EndIf
    End

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
    FwFreeArray(aAllFields)
    FwFreeArray(aPDFields)
    FwFreeArray(aInfo)
Return self:oData



/*/{Protheus.doc} getSchema
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author tp.yuri.mporto
    @since 07/11/2024
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER190TReportsBusinessObject
    // Define os parâmetros do relatório
    self:oSchema:addParameter("BranchCodeFrom",     STR0008,            "string", .F.)//"Filial De"
    self:oSchema:addParameter("BranchCodeTo",       STR0009,            "string", .F.)//"Filial Até"
    self:oSchema:addParameter("CostCenterCodeFrom", STR0010,            "string", .F.)//"C.C. De"
    self:oSchema:addParameter("CostCenterCodeTo",   STR0022,            "string", .F.)//"C.C. Até"
    self:oSchema:addParameter("EmployeeCodeFrom",   STR0011,            "string", .F.)//"Matrícula De"
    self:oSchema:addParameter("EmployeeCodeTo",     STR0012,            "string", .F.)//"Matrícula Até"
    self:oSchema:addParameter("EmployeeNameFrom",   STR0013,            "string", .F.)//"Nome De"
    self:oSchema:addParameter("EmployeeNameTo",     STR0014,            "string", .F.)//"Nome Até"
    self:oSchema:addParameter("ReportType",         STR0015,            "string", .F.)//"Tipo do relatório"
    self:oSchema:addParameter("EmployeeSituation",  STR0016,            "string", .T.)//"Situações"
    self:oSchema:addParameter("EmployeeCategory",   STR0017,            "string", .T.)//"Categoria do Func."
    self:oSchema:addParameter("EmployeeProcess",    STR0018,            "string", .T.)//"Seleção de processos"
    self:oSchema:addParameter("InitialPeriod",      STR0019,            "date",   .F.)//"Data Inicial"
    self:oSchema:addParameter("FinalPeriod",        STR0020,            "date",   .F.)//"Data Final"
    self:oSchema:addParameter("BreakBy",            STR0021,            "string", .F.)//"Quebra por"
    

    
    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("BranchCodeFrom",     "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("BranchCodeTo",       "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("CostCenterCodeFrom", "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("CostCenterCodeTo",   "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("EmployeeCodeFrom",   "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("EmployeeCodeTo",     "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("EmployeeSituation",  "/api/rh/smartview/v1/options/GPEParams/getSX5/31",     2)
    self:setCustomURL("EmployeeCategory",   "/api/rh/smartview/v1/options/GPEParams/getSX5/28",     2)
    self:setCustomURL("EmployeeProcess",    "/api/framework/v1/genericLookupService/smartview/RCJ", 2)
    self:setCustomURL("ReportType",         "/api/rh/smartview/v1/options/GPER190/ReportType",      1)
    self:setCustomURL("BreakBy",            "/api/rh/smartview/v1/options/GPER190/BreakBy",         1)

    
    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",                STR0023  , "string",   STR0023  ,    "FilialExec",     NIL, NIL, NIL, .F.) //"FilialExec",    
    self:oSchema:addProperty("EmpresaExec",               STR0024  , "string",   STR0024  ,    "EmpresaExec",    NIL, NIL, NIL, .F.) //"Empresa Exec",       
    self:oSchema:addProperty("Branch",                    STR0006  , "string",   STR0006  ,    "RA_FILIAL",      NIL, NIL, NIL, .T.) // "Filial",            
    self:oSchema:addProperty("EmployeeCode",              STR0025  , "string",   STR0025  ,    "RA_MAT",         NIL, NIL, NIL, .T.) // "Matrícula",         
    self:oSchema:addProperty("EmployeeCostCenter",        STR0026  , "string",   STR0026  ,    "RA_CC",          NIL, NIL, NIL, .T.) // "Centro de Custo",   
    self:oSchema:addProperty("EmployeeName",              STR0027  , "string",   STR0027  ,    "RA_NOME",        NIL, NIL, NIL, .T.) // "Nome do Func.",     
    self:oSchema:addProperty("EmployeeCategory",          STR0028  , "string",   STR0028  ,    "RA_CATFUNC",     NIL, NIL, NIL, .T.) // "Categoria do Func.",
    self:oSchema:addProperty("EmployeeAdmission",         STR0029  , "date",     STR0029  ,    "RA_ADMISSA",     NIL, NIL, NIL, .T.) // "Data Admissão",     
    self:oSchema:addProperty("EmployeeDateTest1",         STR0030  , "date",     STR0030  ,    "RA_VCTOEXP",     NIL, NIL, NIL, .T.) // "Exp 1 Periodo",     
    self:oSchema:addProperty("EmployeeDateTest2",         STR0031  , "date",     STR0031  ,    "RA_VCTEXP2",     NIL, NIL, NIL, .T.) // "Exp 2 Periodo",     
    self:oSchema:addProperty("MedicalExamination",        STR0032  , "date",     STR0032  ,    "RA_EXAMEDI",     NIL, NIL, NIL, .T.) // "Exame Medico",      
    self:oSchema:addProperty("EmployeeCostCenterDesc",    STR0033  , "string",   STR0033  ,    "CTT_DESC01",     NIL, NIL, NIL, .T.) // "Centro Custo",      

Return self:oSchema



/*/{Protheus.doc} QueryDef
    Define a query de busca dos registros.
    @type Function
    @version 12.1.2310
    @author tp.yuri.mporto
    @since 07/11/2024
    @param oFilter, Object, Filtro do objeto de negócio
    @return Character, Alias do arquivo temporário
/*/
Static Function QueryDef(oFilter As Object, aCustomFields as Array) As Character
    // Declaração das variáveis locais
    Local aSituacoes    As Array     // Situações do funcionário
    Local aCategoria    As Array     // Situações do funcionário
    Local aProcesso     As Array     // Situações do funcionário
    Local cAlias        As Character // Alias do arquivo temporário
    Local cCCAte        As Character // Parâmetro C.C. Até
    Local cCCDe         As Character // Parâmetro C.C. De
    Local cFilialAte    As Character // Parâmetro Filial Até
    Local cFilialDe     As Character // Parâmetro Filial De
    Local cMatriculaAte As Character // Parâmetro Matrícula Até
    Local cMatriculaDe  As Character // Parâmetro Matrícula De
    Local cNomeDe       As Character // Parâmetro Nome De
    Local cNomeAte      As Character // Parâmetro Nome Ate
    Local cDtPeriodoDe  As Character // Parâmetro Periodo de    
    Local cDtPeriodoAte As Character // Parâmetro Periodo Ate
    
    //Local cPrintSDep    As Character // Parâmetro Imp. Func. S/ Depend. (1 = Sim | 2 = Não)
    Local cQuery        As Character // Query para busca dos registros
    Local JParams       As JSON      // Parâmetros do objeto de negócio
    Local oStatement    As Object    // Instância da classe FwExecStatement
    Local nParamOrder   As Numeric   // Ordem dos bind parameters da query
    Local nSituacoes    As Numeric   // Contador para percorrer as situações
    Local cCustomFields As Character // Campos customizados no formato SQL
    Local nCategoria    As Numeric   //variavel de controle
    Local nProcesso     As Numeric   //variavel de controle
    Local cFilter       As Character // Filtro do objeto de negócio
    Local cOrdem        As Character // Ordem da query
    Local cReportType   As Character // tipo do relatório
    Local cBreakBy      As Character // quebra do relatório

    // Inicialização das variáveis
    cFilter       := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    aSituacoes    := {}
    aCategoria    := {}
    aProcesso     := {}
    cAlias        := ""
    cCCAte        := ""
    cCCDe         := ""
    cFilialAte    := ""
    cFilialDe     := ""
    cMatriculaAte := ""
    cMatriculaDe  := ""
    cQuery        := ""
    jParams       := oFilter:getParameters()
    oStatement    := NIL
    nParamOrder   := 1
    nSituacoes    := 0
    nCategoria    := 0
    nProcesso     := 0
    cCustomFields := ""
    cOrdem        := ""

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        
        cFilialDe     := IIf(jParams:hasProperty("BranchCodeFrom")     .and. Len(jParams["BranchCodeFrom"])     > 0 .and. !Empty(jParams["BranchCodeFrom"][1]),     jParams["BranchCodeFrom"][1],     "")
        cFilialAte    := IIf(jParams:hasProperty("BranchCodeTo")       .and. Len(jParams["BranchCodeTo"])       > 0 .and. !Empty(jParams["BranchCodeTo"][1]),       jParams["BranchCodeTo"][1],       Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCCDe         := IIf(jParams:hasProperty("CostCenterCodeFrom") .and. Len(jParams["CostCenterCodeFrom"]) > 0 .and. !Empty(jParams["CostCenterCodeFrom"][1]), jParams["CostCenterCodeFrom"][1], "")
        cCCAte        := IIf(jParams:hasProperty("CostCenterCodeTo")   .and. Len(jParams["CostCenterCodeTo"])   > 0 .and. !Empty(jParams["CostCenterCodeTo"][1]),   jParams["CostCenterCodeTo"][1],   Replicate("Z", GetSX3Cache("RA_CC",   "X3_TAMANHO")))
        cMatriculaDe  := IIf(jParams:hasProperty("EmployeeCodeFrom")   .and. Len(jParams["EmployeeCodeFrom"])   > 0 .and. !Empty(jParams["EmployeeCodeFrom"][1]),   jParams["EmployeeCodeFrom"][1],   "")
        cMatriculaAte := IIf(jParams:hasProperty("EmployeeCodeTo")     .and. Len(jParams["EmployeeCodeTo"])     > 0 .and. !Empty(jParams["EmployeeCodeTo"][1]),     jParams["EmployeeCodeTo"][1],     Replicate("Z", GetSX3Cache("RA_MAT",  "X3_TAMANHO")))
        cNomeDe       := IIf(jParams:hasProperty("EmployeeNameFrom")   .and. Len(jParams["EmployeeNameFrom"])   > 0 .and. !Empty(jParams["EmployeeNameFrom"][1]),   jParams["EmployeeNameFrom"][1],   "")
        cNomeAte      := IIf(jParams:hasProperty("EmployeeNameTo")     .and. Len(jParams["EmployeeNameTo"])     > 0 .and. !Empty(jParams["EmployeeNameTo"][1]),     jParams["EmployeeNameTo"][1],     Replicate("Z", GetSX3Cache("RA_NOME",  "X3_TAMANHO")))
        cDtPeriodoDe  := IIf(jParams:hasProperty("InitialPeriod")      .and. Len(jParams["InitialPeriod"])      > 0 .and. !Empty(jParams["InitialPeriod"][1]),      jParams["InitialPeriod"][1],      FwTimeStamp(6, sToD("")))
        cDtPeriodoAte := IIf(jParams:hasProperty("FinalPeriod")        .and. Len(jParams["FinalPeriod"])        > 0 .and. !Empty(jParams["FinalPeriod"][1]),        jParams["FinalPeriod"][1],        FwTimeStamp(6, sToD("")))
        aSituacoes    := IIf(jParams:hasProperty("EmployeeSituation")  .and. Len(jParams["EmployeeSituation"])  > 0 ,         jParams["EmployeeSituation"], {})
        aCategoria    := IIf(jParams:hasProperty("EmployeeCategory")   .and. Len(jParams["EmployeeCategory"])   > 0 ,         jParams["EmployeeCategory"],  {})
        aProcesso     := IIf(jParams:hasProperty("EmployeeProcess")    .and. Len(jParams["EmployeeProcess"])    > 0 ,         jParams["EmployeeProcess"],   {})
        cReportType   := IIf(jParams:hasProperty("ReportType")         .and. Len(jParams["ReportType"])         > 0 ,         jParams["ReportType"][1] ,    "")
        cBreakBy      := IIf(jParams:hasProperty("BreakBy")            .and. Len(jParams["BreakBy"])            > 0 ,         jParams["BreakBy"][1] ,       "")
        
    EndIf

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nSituacoes := 1 To Len(aSituacoes)
        aSituacoes[nSituacoes] := IIf(aSituacoes[nSituacoes] == NIL .or. Empty(aSituacoes[nSituacoes]), " ", aSituacoes[nSituacoes])
    Next nSituacoes

    For nCategoria := 1 To Len(aCategoria)
        aCategoria[nCategoria] := IIf(aCategoria[nCategoria] == NIL .or. Empty(aCategoria[nCategoria]), " ", aCategoria[nCategoria])
    Next nCategoria

    For nProcesso  := 1 To Len(aProcesso )
        aProcesso [nProcesso ] := IIf(aProcesso [nProcesso ] == NIL .or. Empty(aProcesso [nProcesso ]), " ", aProcesso [nProcesso ])
    Next nProcesso 


    // Trata as variáveis de data para ficarem no formato do banco
    cDtPeriodoDe    := StrTran(SubStr(cDtPeriodoDe, 1, 10), "-", "")
    cDtPeriodoAte   := StrTran(SubStr(cDtPeriodoAte, 1, 10), "-", "")


    cQuery += "    SELECT SRA.RA_FILIAL,        "
    cQuery += "        SRA.RA_CC,       "
    cQuery += "        SRA.RA_MAT,      "
    cQuery += "        SRA.RA_NOME,     "
    cQuery += "        SRA.RA_SITFOLH,      "
    cQuery += "        SRA.RA_CATFUNC,      "
    cQuery += "        SRA.RA_ADMISSA,      "
    cQuery += "        SRA.RA_VCTOEXP,      "
    cQuery += "        SRA.RA_VCTEXP2,      "
    cQuery += "        SRA.RA_EXAMEDI,      "
    cQuery += "        SRA.RA_PROCES,       "
    cQuery += "        CTT.CTT_DESC01       "
    cQuery += "         ? "                                                          // 1
    cQuery += "   FROM  "+ RetSQLName("SRA") + " SRA "
    cQuery += "   LEFT JOIN "    
    cQuery +=         RetSqlName("CTT") + " CTT "
    cQuery += "        ON "
    cQuery +=             FWJoinFilial("CTT", "SRA") + " "
    cQuery += "        AND CTT.CTT_CUSTO  = SRA.RA_CC "
    cQuery += "        AND CTT.D_E_L_E_T_ = ? "                                     // 2
    cQuery += "   WHERE "
    cQuery += "        SRA.RA_FILIAL         BETWEEN ? AND ? "
    cQuery += "        AND SRA.RA_CC         BETWEEN ? AND ? "                      //6
    cQuery += "        AND SRA.RA_MAT        BETWEEN ? AND ? "
    cQuery += "        AND SRA.RA_NOME       BETWEEN ? AND ? "                      //10
    If cReportType == "2"
        cQuery += "         AND SRA.RA_EXAMEDI   BETWEEN ? AND ? "                 //12
    Else
        cQuery += "        AND (SRA.RA_VCTOEXP   BETWEEN ? AND ? "                 //12     
        cQuery += "        OR  SRA.RA_VCTEXP2    BETWEEN ? AND ? )"                //14     
    EndIf
    cQuery += "     " + IIf(Len(aSituacoes) > 0,  " AND RA_SITFOLH  IN (?)", " ")   //15
    cQuery += "     " + IIf(Len(aCategoria) > 0,  " AND RA_CATFUNC  IN (?)", " ") 
    cQuery += "     " + IIf(Len(aProcesso) > 0,  " AND RA_PROCES  IN (?)", " ")    //17    
    cQuery += "        AND SRA.D_E_L_E_T_ = ? "                                     //18
    cQuery += "         ?   "                                                       //19
    cQuery := ChangeQuery(cQuery)


    // Instancia a classe
    oStatement := FWExecStatement():New(cQuery)

    // Captura e define os campos personalizados das tabelas SRA e CTT
    cCustomFields := GPESmartViewUtils():GetCustomFields(aCustomFields, {"SRA","CTT"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1
    
    // Define o valor dos bind parameters
    oStatement:SetString(nParamOrder++, ' ')                //2 - D_E_L_E_T_ CTT
    oStatement:SetString(nParamOrder++, cFilialDe)          //
    oStatement:SetString(nParamOrder++, cFilialAte)         //
    oStatement:SetString(nParamOrder++, cCCDe)              //5
    oStatement:SetString(nParamOrder++, cCCAte)             //
    oStatement:SetString(nParamOrder++, cMatriculaDe)       //
    oStatement:SetString(nParamOrder++, cMatriculaAte)      //
    oStatement:SetString(nParamOrder++, cNomeDe)            //
    oStatement:SetString(nParamOrder++, cNomeAte)           //10
    oStatement:SetString(nParamOrder++, cDtPeriodoDe)       //11 - RA_VCTOEXP ou RA_EXAMEDI
    oStatement:SetString(nParamOrder++, cDtPeriodoAte)      //12 - RA_VCTOEXP ou RA_EXAMEDI
    If cReportType <> "2"
        oStatement:SetString(nParamOrder++, cDtPeriodoDe)   //13 - RA_VCTEXP2
        oStatement:SetString(nParamOrder++, cDtPeriodoAte)  //14 - RA_VCTEXP2
    EndIf
    If (Len(aSituacoes) > 0)
        oStatement:SetIn(nParamOrder++, aSituacoes)         //15
    EndIf 
   	If (Len(aCategoria) > 0)
        oStatement:SetIn(nParamOrder++, aCategoria)         //16
    EndIf 
    If (Len(aProcesso) > 0) 
        oStatement:SetIn(nParamOrder++, aProcesso)         //17
    EndIf 
    oStatement:SetString(nParamOrder++, ' ')                //18 - D_E_L_E_T_
    oStatement:SetUnsafe(nParamOrder++, cFilter)            //19


    // Executa a query e retorna o alias criado     
    cAlias := oStatement:OpenAlias()


    // Limpa os objetos da memória
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeArray(aSituacoes)
    FwFreeArray(aCategoria)
    FwFreeArray(aProcesso)
Return cAlias

    


/*/{Protheus.doc} getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2310
    @author tp.yuri.mporto
    @since 07/11/2024
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() As Variant Class GPER190TReportsBusinessObject
    // Declaração das variáveis locais
    Local jPath     As JSON         // Path parameters da requisição
    Local jResponse As JSON         // Resposta da requisição
    Local jItem     As JSON         // Item do valor do parâmetro
    Local nValues   As Numeric      // Contador dos valores do parâmetro
    Local aValues   As Array        // Valores do parâmetro
    Local cParam    As Character    // Nome do parâmetro

    // Inicialização das variáveis
    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Verifica qual parâmetro foi solicitado
    If (cParam == "ReportType")
        // Monta o vetor de valores do parâmetro
        AAdd(aValues, FwHTTPEncode(STR0004 ))     // "Contrato Experiência" str000
        AAdd(aValues, FwHTTPEncode(STR0005))      // "Exame Médico"
    ElseIf (cParam == "BreakBy")
        AAdd(aValues, FwHTTPEncode(STR0006))      // "Filial"
        AAdd(aValues, FwHTTPEncode(STR0007))      // "Centro Custo"
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL





