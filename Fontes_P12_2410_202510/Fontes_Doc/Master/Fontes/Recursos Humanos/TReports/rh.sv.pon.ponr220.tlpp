#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "TOTVS.CH"
#Include "rh.sv.pon.ponr220.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPON", tables="SRA,SPF,SRJ,SQB,CTT", name="Rel. Troca de Turno", country="ALL", initialRelease="12.1.2210")
Class PONR220TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    Public Method new() as object
    Public Method getDescription() as character
    Public Method getData() as object
    Public Method getSchema() as object
EndClass

Method new() Class PONR220TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0001) // RH
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002) // Rel. Troca de Turno
Return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type   Method
@author Marco Nakazawa
@since  28/09/2023
/*/
Method getDescription() as character class PONR220TReportsBusinessObject
Return (STR0003) // Este relatório apresenta as trocas de turno feitas em um determinado período.

Method getData(nPage as numeric, oFilter as object) as object class PONR220TReportsBusinessObject
	Local aFilAcess		:= StrTokArr(fvalidfil(),"/")				as Array
	Local aInfo         := {}										as Array
    Local aFieldsSRA    := {}										as Array
    Local aFieldsSR6    := {}										as Array
    Local aFieldsSRJ    := {}										as Array
    Local aFieldsSQB    := {}										as Array
    Local aFieldsCTT    := {}                                       as Array
    Local aLGPDSRA      := {}										as Array
    Local aLGPDSR6      := {}										as Array
    Local aLGPDSRJ      := {}										as Array
    Local aLGPDSQB      := {}										as Array
    Local aLGPDCTT      := {}										as Array
	Local cAliasQry		:= GetNextAlias()							as Character
	Local cJoiSRASPF	:= "%" + FWJoinFilial("SRA", "SPF") + "%"	as Character
	Local cJoiSRASRJ	:= "%" + FWJoinFilial("SRA", "SRJ") + "%"	as Character
	Local cJoiSRASQB	:= "%" + FWJoinFilial("SRA", "SQB") + "%"	as Character
	Local cJoiSRACTT	:= "%" + FWJoinFilial("SRA", "CTT") + "%"	as Character
	Local cJoiSR6SPF	:= "%" + FWJoinFilial("SR6", "SPF") + "%"	as Character
	Local cJoiSPASPF	:= "%" + FWJoinFilial("SPA", "SPF") + "%"	as Character
	Local cFilDe		:= ""										as Character
	Local cFilAte		:= ""										as Character
	Local cMatDe		:= ""										as Character
	Local cMatAte		:= ""										as Character
	Local cTurnoDe		:= ""										as Character
	Local cTurnoPara	:= ""										as Character
	Local cRegraDe		:= ""										as Character
	Local cRegraPara	:= ""										as Character
	Local cMesAno		:= ""										as Character
	Local cFilialIn		:= ""										as Character
	Local cJoiSR6DE		:= ""										as Character
	Local cJoiSR6PA		:= ""										as Character
	Local cJoiSPADE		:= ""										as Character
	Local cJoiSPAPA		:= ""										as Character
    Local cEmpNome		:= ""										as Character
    Local cMatrAnt		:= ""										as Character
    Local cFilialAnt	:= ""										as Character
    Local cGrpEmpNom    := FWGrpName(cEmpAnt)                       as Character
	Local cLGPDSRA		:= ""										as Character
	Local dPerIni       := Date()                                   as Date
    Local dPerFim       := Date()                                   as Date
	Local lHasNext		:= .F.										as Logical
    Local lLGPDSR6      := .F.										as Logical
    Local lLGPDSRJ      := .F.										as Logical
    Local lLGPDSQB      := .F.										as Logical
    Local lLGPDCTT      := .F.										as Logical
	local nI			:= 0										as Numeric
	local nPageStart	:= 0										as Numeric
	local nPageEnd		:= 0										as Numeric
	local oJparams													as Json

	// Verificação da anonimização dos dados
	aFieldsSRA := {"RA_FILIAL", "RA_MAT", "RA_NOME", "RA_CATFUNC", "RA_CC", "RA_TNOTRAB","RA_CODFUNC","RA_DEPTO"}
    aFieldsSR6 := {"R6_DESC"}
    aFieldsSRJ := {"RJ_DESC"}
    aFieldsSQB := {"QB_DESCRIC"}
    aFieldsCTT := {"CTT_DESC01"}

    aLGPDSRA := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsSRA)
    aLGPDSR6 := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsSR6)
    aLGPDSRJ := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsSRJ)
    aLGPDSQB := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsSQB)
    aLGPDCTT := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsCTT)

	For nI := 1 To Len(aFieldsSRA)
        If(aScan(aLGPDSRA, aFieldsSRA[nI]) == 0, cLGPDSRA += aFieldsSRA[nI] + "*", Nil)
    Next
    
    lLGPDSR6 := Empty(aLGPDSR6)
    lLGPDSRJ := Empty(aLGPDSRJ)
    lLGPDSQB := Empty(aLGPDSQB)
    lLGPDCTT := Empty(aLGPDCTT)

	// Alteração dos apelidos das tabelas usadas no INNER JOIN
	cJoiSR6DE := StrTran(cJoiSR6SPF, "SR6", "SR6DE")
	cJoiSR6PA := StrTran(cJoiSR6SPF, "SR6", "SR6PA")
	cJoiSPADE := StrTran(cJoiSPASPF, "SPA", "SPADE")
	cJoiSPAPA := StrTran(cJoiSPASPF, "SPA", "SPAPA")
    
    // Definição da paginação
    self:setPageSize(50)
    nPageStart  := ((nPage - 1) * self:getPageSize()) + 1
    nPageEnd    := nPage * self:getPageSize()

	// Carrega os parâmetros informados
    oJParams	:= oFilter:getParameters()
    cFilDe		:= oJParams["filial1",1]									// Filial De?
    cFilAte		:= oJParams["filial2",1]									// Filial Até?
    cMatDe		:= oJParams["mat1",1]										// Matrícula De?
    cMatAte		:= oJParams["mat2",1]										// Matrícula Até?
    dPerIni		:= sToD(StrTran(SubStr(oJParams["per1",1],1,10),"-",""))	// Período Inicial?
    dPerFim		:= sToD(StrTran(SubStr(oJParams["per2",1],1,10),"-",""))	// Período Final?
    cTurnoDe	:= oJParams["turno1",1]										// Turno De?
    cTurnoPara	:= oJParams["turno2",1]										// Turno Para?
    cRegraDe	:= oJParams["regra1",1]										// Regra De?
    cRegraPara	:= oJParams["regra2",1]										// Regra Para?

    // Filtro das Filiais
    If (!Empty(cFilDe) .And. !Empty(cFilAte)) .Or. (Empty(cFilDe) .And. !Empty(cFilAte) .Or. (!Empty(cFilDe) .And. Empty(cFilAte)))
        If (!Empty(cFilDe) .And. Empty(cFilAte), cFilAte := Replicate("Z",FWGETTAMFILIAL), Nil)

        For nI := 1 To Len(aFilAcess)
            If aFilAcess[nI] >= cFilDe .And. aFilAcess[nI] <= cFilAte
                If nI == 1 .Or. Empty(cFilialIn)
                    cFilialIn := "'" + aFilAcess[nI] + "'"
                Else
                    cFilialIn += ",'" + aFilAcess[nI] + "'"
                EndIf
            EndIf
        Next

        If !Empty(cFilialIn)
            cWhere := " SRA.RA_FILIAL IN (" + cFilialIn + ")"
        Else
            cWhere := " SRA.RA_FILIAL IN ('" + StrTran(fvalidfil(),"/","','") + "')"
        EndIf
    ElseIf Empty(cFilDe) .And. Empty(cFilAte)
        cWhere := " SRA.RA_FILIAL IN ('" + StrTran(fvalidfil(),"/","','") + "')"
    EndIf

	// Filtro da Matrícula
    If !Empty(cMatDe)
        cWhere += " AND SRA.RA_MAT >= '" + SubStr(cMatDe,1,TamSx3("RA_MAT")[1]) + "'"
    EndIf
    
    If!Empty(cMatAte)
        cWhere += " AND SRA.RA_MAT <= '" + SubStr(cMatAte,1,TamSx3("RA_MAT")[1]) + "'"
    EndIf
	
	// Filtro das datas
    If(dPerIni > dPerFim, dPerFim := dPerIni, Nil)
    cWhere	+= " AND (SPF.PF_DATA >= '" + dToS(dPerIni) + "' AND SPF.PF_DATA <= '" + dToS(dPerFim) + "')"

	// Filtro do Turno De
    If !Empty(cTurnoDe)
        cWhere += " AND SR6DE.R6_TURNO IN ('" + StrTran(cTurnoDe, ",", "','") + "')"
    EndIf
    
	// Filtro do Turno Para
    If!Empty(cTurnoPara)
		cWhere += " AND SR6PA.R6_TURNO IN ('" + StrTran(cTurnoPara, ",", "','") + "')"
    EndIf

	// Filtro da Regra De
    If !Empty(cRegraDe)
		cWhere += " AND SPADE.PA_CODIGO IN ('" + StrTran(cRegraDe, ",", "','") + "')"
    EndIf
    
	// Filtro da Regra para
    If!Empty(cRegraPara)
		cWhere += " AND SPAPA.PA_CODIGO IN ('" + StrTran(cRegraPara, ",", "','") + "')"
    EndIf

	cWhere := "%" + cWhere + "%"

    BeginSql alias cAliasQry
        SELECT * FROM (
            SELECT
			  SRA.RA_FILIAL,
			  SRA.RA_MAT,
			  SRA.RA_NOME,
			  SRA.RA_CATFUNC,
			  SRA.RA_CODFUNC,
			  SRA.RA_DEPTO,
			  SRA.RA_CC,
			  SRA.RA_SITFOLH,
			  SPF.PF_DATA,
			  SPF.PF_TURNODE,
			  SPF.PF_TURNOPA,
			  SPF.PF_REGRADE,
			  SPF.PF_REGRAPA,
			  SPF.PF_TRFOBS,
			  SR6DE.R6_DESC AS DESCTURNODE,
			  SR6PA.R6_DESC AS DESCTURNOPA,
			  SPADE.PA_DESC AS DESCREGRADE,
			  SPAPA.PA_DESC AS DESCREGRAPA,			  
			  SQB.QB_DESCRIC,
			  CTT.CTT_DESC01,
			  SRJ.RJ_DESC,
			  ROW_NUMBER() OVER(ORDER BY PF_FILIAL ASC) AS LINHA
            FROM %table:SRA% SRA
			INNER JOIN %table:SPF% SPF ON %exp:cJoiSRASPF% AND SPF.PF_MAT = SRA.RA_MAT AND SPF.%NotDel%
			INNER JOIN %table:SR6% SR6DE ON %exp:cJoiSR6DE% AND SR6DE.R6_TURNO = SPF.PF_TURNODE AND SR6DE.%NotDel%
			INNER JOIN %table:SR6% SR6PA ON %exp:cJoiSR6PA% AND SR6PA.R6_TURNO = SPF.PF_TURNOPA AND SR6PA.%NotDel%
			INNER JOIN %table:SPA% SPADE ON %exp:cJoiSPADE% AND SPADE.PA_CODIGO = SPF.PF_REGRADE AND SPADE.%NotDel%
			INNER JOIN %table:SPA% SPAPA ON %exp:cJoiSPAPA% AND SPAPA.PA_CODIGO = SPF.PF_REGRAPA AND SPAPA.%NotDel%
			LEFT JOIN %table:SRJ% SRJ ON %exp:cJoiSRASRJ% AND SRJ.RJ_FUNCAO = SRA.RA_CODFUNC AND SRJ.%NotDel%
			LEFT JOIN %table:SQB% SQB ON %exp:cJoiSRASQB% AND SQB.QB_DEPTO = SRA.RA_DEPTO AND SQB.%NotDel%
			LEFT JOIN %table:CTT% CTT ON %exp:cJoiSRACTT% AND CTT.CTT_CUSTO = SRA.RA_CC AND CTT.%NotDel%
			WHERE %exp:cWhere%
			  AND SRA.%notDel%
			  AND ((SPF.PF_TURNODE <> SPF.PF_TURNOPA) OR (SPF.PF_REGRADE <> SPF.PF_REGRAPA))
		) as SPF
		WHERE LINHA BETWEEN %Exp:cValToChar(nPageStart)% AND %Exp:cValToChar(nPageEnd)%    
	EndSql

	While !(cAliasQry)->(Eof())
		// Descrição situação da folha e Descrição
        cSitFol	:= AllTrim((cAliasQry)->(fDesc("SX5", "31" + RA_SITFOLH, "X5_DESCRI", 15, RA_FILIAL)))
        cCateg	:= AllTrim((cAliasQry)->(fDesc("SX5", "28" + RA_CATFUNC, "X5_DESCRI", 11, RA_FILIAL)))
		
		// Formata a data da troca de turno para o período (Mês/Ano)
		cMesAno := MesAno(sToD((cAliasQry)->PF_DATA))
		cMesAno := Substr(cMesAno,5,2) + "/" + Substr(cMesAno,1,4)
		
		// Busca as informações da empresa do funcionário
		If (Empty(cMatrAnt) .And. Empty(cFilialAnt)) .Or. ((cAliasQry)->RA_FILIAL <> cFilialAnt .Or. (cAliasQry)->RA_MAT <> cMatrAnt)
			cEmpNome := AllTrim(FWCompanyName(cEmpAnt,(cAliasQry)->RA_FILIAL))
			fInfo(@aInfo, (cAliasQry)->RA_FILIAL)
		EndIf

		self:oData:appendData({;
            "GRPEMPRESA": AllTrim(cGrpEmpNom),;
			"EMPRESA": cEmpNome,;
			"CNPJ": Transform(If(Len(aInfo) > 0, aInfo[08], SM0->M0_CGC),'@!R NN.NNN.NNN/NNNN-99'),;
			"RA_FILIAL": If("RA_FILIAL" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasQry)->RA_FILIAL),1,10), AllTrim((cAliasQry)->RA_FILIAL)),;
            "RA_MAT": If("RA_MAT" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasQry)->RA_MAT),1,10), AllTrim((cAliasQry)->RA_MAT)),;
            "RA_NOME": If("RA_NOME" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasQry)->RA_NOME),1,10), AllTrim((cAliasQry)->RA_NOME)),;
            "RA_CATFUNC": If("RA_CATFUNC" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(cCateg),1,10), cCateg),;
            "RA_CODFUNC": If("RA_CODFUNC" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasQry)->RA_CODFUNC),1,10), AllTrim((cAliasQry)->RA_CODFUNC)),;
            "RA_DEPTO": If("RA_DEPTO" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasQry)->RA_DEPTO),1,10), AllTrim((cAliasQry)->RA_DEPTO)),;
            "RA_CC": If("RA_CC" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasQry)->RA_CC),1,10), AllTrim((cAliasQry)->RA_CC)),;
            "RA_SITFOLH": If("RA_SITFOLH" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize(cSitFol),1,10), cSitFol),;
            "PF_DATA": totvs.framework.treports.date.dateToTimeStamp(sToD((cAliasQry)->PF_DATA)),;
            "PF_TURNODE": (cAliasQry)->PF_TURNODE,;
            "PF_TURNOPA": (cAliasQry)->PF_TURNOPA,;
            "PF_REGRADE": (cAliasQry)->PF_REGRADE,;
            "PF_REGRAPA": (cAliasQry)->PF_REGRAPA,;
            "PF_TRFOBS": (cAliasQry)->PF_TRFOBS,;
            "PERIODO": cMesAno,;
            "DESCTURNODE": (cAliasQry)->DESCTURNODE,;
            "DESCTURNOPA": (cAliasQry)->DESCTURNOPA,;
            "DESCREGRADE": (cAliasQry)->DESCREGRADE,;
            "DESCREGRAPA": (cAliasQry)->DESCREGRAPA,;
            "DESCDPTO": If(lLGPDSQB .Or. "RA_DEPTO" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasQry)->QB_DESCRIC),1,10), AllTrim((cAliasQry)->QB_DESCRIC)),;
            "DESCCC": If(lLGPDCTT .Or. "RA_CC" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasQry)->CTT_DESC01),1,10), AllTrim((cAliasQry)->CTT_DESC01)),;
			"DESCFUNCAO": If(lLGPDSRJ .Or. "RA_CODFUNC" $ cLGPDSRA, SubStr(FwProtectedDataUtil():ValueAsteriskToAnonymize((cAliasQry)->RJ_DESC),1,10), AllTrim((cAliasQry)->RJ_DESC));
		})
        
		(cAliasQry)->(dbSkip())
	EndDo

	self:setHasNext(lHasNext)

	(cAliasQry)->( DBCloseArea())
Return self:oData
 
/*/{Protheus.doc} getSchema
Metodo que retorna a estrutura dos campos que serão enviados para o Smart View
@type   Method
@author Marco Nakazawa
@since  28/09/2023
/*/
Method getSchema() as object class PONR220TReportsBusinessObject
    Local aFieldsSRA  := {}
    Local aFieldsSPF  := {}

	aFieldsSRA := {"RA_FILIAL", "RA_MAT", "RA_NOME", "RA_CATFUNC", "RA_CODFUNC", "RA_DEPTO", "RA_CC", "RA_SITFOLH"}
    aFieldsSPF := {"PF_DATA","PF_TURNODE", "PF_TURNOPA", "PF_REGRADE", "PF_REGRAPA", "PF_TRFOBS"}

    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("SPF", aFieldsSPF)

	// Definição dos parâmetros do reltório
    self:addParameter("filial1",	STR0004,	"string",	.F.) // Filial De?
    self:addParameter("filial2",	STR0005,	"string",	.F.) // Filial Até?
    self:addParameter("mat1",		STR0006,	"string",	.F.) // Matrícula De?
    self:addParameter("mat2",		STR0007,	"string",	.F.) // Matrícula Até?
    self:addParameter("per1",		STR0008,	"date",		.F.) // Período Inicial?
    self:addParameter("per2",		STR0009,	"date",		.F.) // Período Final?
    self:addParameter("turno1",		STR0010,	"string",	.F.) // Turno De?
    self:addParameter("turno2",		STR0011,	"string",	.F.) // Turno Para?
    self:addParameter("regra1",		STR0012,	"string",	.F.) // Regra De?
    self:addParameter("regra2",		STR0013,	"string",	.F.) // Regra Para?

	// Definição dos campos "customizados" que serão enviados para o relatório
    self:oSchema:addProperty("GRPEMPRESA",  STR0014,	"string",	STR0014,	"GRPEMPRESA")	// Grupo de Empresas
    self:oSchema:addProperty("EMPRESA",		STR0015,	"string",	STR0015,	"EMPRESA")		// Empresa
    self:oSchema:addProperty("CNPJ",		STR0016,	"string",	STR0016,	"CNPJ")			// CNPJ
    self:oSchema:addProperty("DESCTURNODE",	STR0017,	"string",	STR0017,	"DESCTURNODE")	// Descrição Turno De
    self:oSchema:addProperty("DESCTURNOPA",	STR0018,	"string",	STR0018,	"DESCTURNOPA")	// Descrição Turno Para
    self:oSchema:addProperty("DESCREGRADE",	STR0019,	"string",	STR0019,	"DESCREGRADE")	// Descrição Regra De
    self:oSchema:addProperty("DESCREGRAPA",	STR0020,	"string",	STR0020,	"DESCREGRAPA")	// Descrição Regra Para
    self:oSchema:addProperty("DESCDPTO",	STR0021,	"string",	STR0021,	"DESCDPTO")		// Descrição Departamento
    self:oSchema:addProperty("DESCCC",		STR0022,	"string",	STR0022,	"DESCCC")		// Descrição Centro de Custo
    self:oSchema:addProperty("DESCFUNCAO",	STR0023,	"string",	STR0023,	"DESCFUNCAO")	// Descrição Função
    self:oSchema:addProperty("PERIODO",		STR0024,	"string",	STR0024,	"PERIODO")		// Período
Return self:oSchema
