#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "rh.treports.gper010.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SR0, RG2, RHR, RHS, SRA, RIQ, RIR, RHK, RHM, RHL, RFO, SRN, SRB", customTables="SLY, SRA", name="Rel. Benefícios por Entidade", country="ALL", initialRelease="12.1.2210")
class GPER010TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass

method new() class GPER010TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) // RH
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) // Rel. Benefícios por Entidade

    // Indica que no objeto Schema que o LookUp será do tipo Key-Value (combobox)
    self:setIsCBoxLookup(.T., .F.)
return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type  Metodo
@author Squad Rotinas
@since  Julho 05,2023
/*/
method getDescription() as character class GPER010TReportsBusinessObject
return (STR0003) // Emite um relatorio de Benefícios por Entidade
 
method getData(nPage as numeric, oFilter as object) as object class GPER010TReportsBusinessObject
    local cAlias 		as Character
    local cQuery 		as Character    
    local cVr 			as Character
    local cVa 			as Character
    local cPlano 		as Character
    local cOutros 		as Character
    local cDependentes 	as Character
    local cAgregados 	as Character
	Local nParamOrder 	as Numeric
	Local oStatement 	as Object
	Local nX 			as Numeric
	Local cCustomFields As Character // Campos customizados no formato SQL
    Local cName         As Character // Nome real do campo
    Local aPDFields     As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR   As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR     As Array     // Campos com nome diferente do real
    Local aAllFields    As Array     // Estrutura de todos os campos do ON
    Local lObfuscated   As Logical   // Flag que indica se tem algum campo para ofuscar
    Local jData         As JSON      // Informações a serem adicionadas no ON
    Local jLGPD         As JSON      // Indica quais campos devem ser ofuscados
    Local nFields       As Numeric

	//Variáveis com o nome da Filial e Grupo de Empresa
	Local cFilExec := AllTrim(FWFilialName()) as Character
	Local cEmpExec := AllTrim(FWEmpName(cEmpAnt)) as Character

    //Local cFiltroClean  := fGetFiltroClean(oFilter:getSQLExpression())
    Local jParams       := oFilter:getParameters() as JSON

	Local cCriterio     := jParams["criterio"][1] as Character
	Local cEntidade     := getEntidade(jParams["entidade"][1]) as Character

    Local lVr           := .F. as Logical
	Local lVa           := .F. as Logical
    Local lVrVa		    := .F. as Logical
	Local lPlano        := .F. as Logical
	Local lOutros       := .F. as Logical
	Local lDependentes  := .F. as Logical
	Local lAgregados    := .F. as Logical
	Local aCategorias   := jParams["PAR_CATEGORIAS"] as Array
    Local aSituacoes    := jParams["PAR_SITUACOES"] as Array
	Local cPerDe		:= jParams["periodoDe"][1] as Character
	Local cPerAte		:= jParams["periodoAte"][1] as Character
	Local aIntervalo	:= GetInterval(cPerDe,cPerAte,cCriterio) as Array
    
	Local lTemAberto	:= .F. as Logical
	Local lTemFechado	:= .F. as Logical
	Local cJoinBen		:= '' as Character
	Local cFunJoin		:= '' as Character
	Local cWhere 		:= '' as Character
	Local aVrVa			:= {} as Array
	Local aPeriodos		:= {} as Array
	Local aConsultas	:= {} as Array
	Local aTpPlanos		:= {'1'} as Array //1=Titular;2=Dependente;3=Agregado
	Local cDefault		:= '' as Character
	Local cCampo		:= '' as Character

    Local lEntidade := .T. as Logical // Flag da entidade (.T. = Válida | .F. = Inválida)

	cVr       	:= IIf(Len(jParams["PAR_VR"])       > 0 .and.!Empty(jParams["PAR_VR"][1]),       jParams["PAR_VR"][1],       "1")
	cVa       	:= IIf(Len(jParams["PAR_VA"])       > 0 .and.!Empty(jParams["PAR_VA"][1]),       jParams["PAR_VA"][1],       "1")
	cPlano      := IIf(Len(jParams["PAR_PS"])       > 0 .and.!Empty(jParams["PAR_PS"][1]),       jParams["PAR_PS"][1],       "1")
	cOutros     := IIf(Len(jParams["PAR_OUTROS"])   > 0 .and.!Empty(jParams["PAR_OUTROS"][1]),   jParams["PAR_OUTROS"][1],   "1")
	cDependentes:= IIf(Len(jParams["PAR_DEP"])      > 0 .and.!Empty(jParams["PAR_DEP"][1]),      jParams["PAR_DEP"][1],      "1")
	cAgregados  := IIf(Len(jParams["PAR_AGR"])      > 0 .and.!Empty(jParams["PAR_AGR"][1]),      jParams["PAR_AGR"][1],      "1")

    // Define as flags de controle do relatório
    lVr          := cVr == "1"
    lVa          := cVa == "1"
    lVrVa        := lVr .or. lVa
    lPlano       := cPlano == "1"
    lOutros      := cOutros == "1"
    lDependentes := cDependentes == "1"
    lAgregados   := cAgregados == "1"

	nParamOrder := 1
	oStatement	:= NIL
	nX 			:= 1

	cCustomFields := ""
    cName         := ""
    aPDFields     := {}
    aPDFieldsNR   := {}
    aFieldsNR     := {}
    aAllFields    := {}
	aWherePer	  := {}
    lObfuscated   := .F.
    jData         := NIL
    jLGPD         := JSONObject():New()

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RA_SALARIO", "RA_CATFUNC", "RA_CODFUNC", "RJ_DESC", "RB7_SALARI", "RB7_CATEG", "RB7_FUNCAO"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Verifica se a entidade é válida
    If (!"|" + cEntidade + "|" $ "|SQB|CTT|SQ3|SRJ|SR6|RCE|RCL|SM0|SA1|ABS|TDX|")
        lEntidade := .F.
        self:setErrorStatus(400, "Entidade inválida", "Verifique a entidade informada!")
        FwLogMsg("WARN", NIL, "SmartView", NIL, NIL, NIL, "Entidade inválida", NIL, NIL, NIL)
    EndIf

	For nX:=1 To Len(aSituacoes)
        If Empty(aSituacoes[nX])
            aSituacoes[nX] := ' '
        Endif
    Next nX

	If(lVrVa .Or. lPlano .Or. lOutros) .And. lEntidade
		cCampo		:= EntityField(cEntidade)
		If !Empty(cCampo)
			cFunJoin	:= EntityJoin(cEntidade) + GetFuncJoin(cEntidade)
					
			If Len(aSituacoes) > 0
				cWhere += " AND SRA.RA_SITFOLH IN (?) "
			Endif
			If Len(aCategorias) > 0
				cWhere += " AND SRA.RA_CATFUNC IN (?) "
			Endif
			
			cDefault := " SELECT SLY.LY_FILIAL, SLY.LY_ALIAS, SLY.LY_CHVENT,"+ cCampo + " AS ENTDESCR, SLY.LY_TIPO, "
			cDefault += " SLY.LY_CODIGO, @DESCRICAO@ AS DESCRICAO, SLY.LY_DTINI, SLY.LY_DTFIM, @ORIGEM@ AS ORIGEM, @TABELA@ AS TABELA,"
			cDefault += " SUM(@VALOR@) AS VALOR, SUM(@VLREMP@) AS VALOREMP, SUM(@VLRFUN@) AS VALORFUNC " 
    		cDefault += " ? "
			cDefault += " FROM "+ RetSqlName('SLY') + " SLY "
			cDefault += " @JOIN_ENTIDADE@"
			cDefault += " @JOIN_BENEFICIO@"
			cDefault += " WHERE SLY.LY_FILIAL 	= ? "
			
			If(Len(aIntervalo) == 2)				
				cDefault += " AND SLY.LY_DTINI >= ? AND SLY.LY_DTFIM <= ? "
			EndIf
			
			cDefault += " AND SLY.LY_ALIAS 		= ? "		
			cDefault += cWhere		
			cDefault += " AND SLY.D_E_L_E_T_ 	= ? "		
			
			If(cCampo == "'VAZIO'" .OR. cCampo == "''")
				cDefault += " GROUP BY SLY.LY_FILIAL, SLY.LY_ALIAS, SLY.LY_CHVENT, SLY.LY_TIPO, SLY.LY_CODIGO,@DESCRICAO@, SLY.LY_DTINI, SLY.LY_DTFIM "
    			cDefault += " ? "
			Else		
				cDefault += " GROUP BY SLY.LY_FILIAL, SLY.LY_ALIAS, SLY.LY_CHVENT,"+ cCampo +", SLY.LY_TIPO, SLY.LY_CODIGO, @DESCRICAO@, SLY.LY_DTINI, SLY.LY_DTFIM "
    			cDefault += " ? "
			EndIf
					
			If(lVrVa)
				
				If(lVr)
					aAdd(aVrVa,'D')//VR
				EndIf			
				If(lVa)
					aAdd(aVrVa,'E')//VA
				EndIf
				
				aPeriodos := GetPeriods(SubStr(aIntervalo[1],1,6),SubStr(aIntervalo[2],1,6),aVrVa)
				
				lTemAberto := (aScan(aPeriodos,{|x|(x[2])}) > 0 )
				lTemFechado:= (aScan(aPeriodos,{|x|(!x[2])}) > 0 )
				
				If(lTemAberto)				
					aAdd(aConsultas,cDefault)			
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@ORIGEM@'		, "'VRVA'")
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@TABELA@'		, "'VRVA'")
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@VALOR@'		, 'SR0.R0_VALCAL')
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@VLREMP@'		, 'SR0.R0_VLREMP')
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@VLRFUN@'		, 'SR0.R0_VLRFUNC')
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@DESCRICAO@'	, 'RFO.RFO_DESCR')						
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@JOIN_ENTIDADE@', cFunJoin)			 
					
					cJoinBen := " INNER JOIN "+RetSqlName('SR0')+" SR0 "
					cJoinBen += "    ON SR0.R0_FILIAL 	= ? "
					cJoinBen += " 	AND SR0.R0_MAT 		= SRA.RA_MAT"
					cJoinBen += " 	AND "+ GetVrVAJoin(lVr,lVa) +" "
					cJoinBen += "   AND SR0.R0_CODIGO 	= SLY.LY_CODIGO "
					cJoinBen += "   AND SR0.D_E_L_E_T_ 	= SLY.D_E_L_E_T_ "
					cJoinBen += " INNER JOIN "+RetSqlName('RFO')+" RFO "
					cJoinBen += " 	 ON RFO.RFO_FILIAL 	= ? "
					cJoinBen += " 	AND RFO.RFO_CODIGO 	= SLY.LY_CODIGO "
					cJoinBen += " 	AND "+ GetVrVAJoin(lVr,lVa,'RFO.RFO_TPVALE') +""
					cJoinBen += " 	AND RFO.D_E_L_E_T_ 	= SR0.D_E_L_E_T_ "
		
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@JOIN_BENEFICIO@'	, cJoinBen)
				EndIf
				
				If(lTemFechado)			
					aAdd(aConsultas,cDefault)
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@ORIGEM@'		, "'VRVA'")
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@TABELA@'		, "'VRVA'")			
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@VALOR@'		, 'RG2.RG2_VALCAL')
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@VLREMP@'		, 'RG2.RG2_CUSEMP')
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@VLRFUN@'		, 'RG2.RG2_CUSFUN')
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@DESCRICAO@'	, 'RFO.RFO_DESCR')						
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@JOIN_ENTIDADE@', cFunJoin)			 
					
					cJoinBen := " INNER JOIN "+RetSqlName('RG2')+" RG2 "
					cJoinBen += "    ON RG2.RG2_FILIAL 	= ? "
					cJoinBen += "   AND RG2.RG2_MAT 	= SRA.RA_MAT "
					cJoinBen += "   AND RG2.RG2_CODIGO 	= SLY.LY_CODIGO AND "
					
					aWherePer := GetWhere(aPeriodos)				
					If Len(aWherePer) > 0
						cJoinBen += 'RG2.RG2_PERIOD IN (?) AND '
					EndIf
					
					cJoinBen += GetVrVAJoin(lVr,lVa,'RG2_TPVALE') +" "
					cJoinBen += "  AND RG2.D_E_L_E_T_ 	= SLY.D_E_L_E_T_ "	
					
					cJoinBen += " INNER JOIN "+RetSqlName('RFO')+" RFO "
					cJoinBen += "    ON RFO.RFO_FILIAL 	= ? "
					cJoinBen += "   AND "+ GetVrVAJoin(lVr,lVa,'RFO_TPVALE') +" "
					cJoinBen += "   AND RFO.RFO_CODIGO  = SLY.LY_CODIGO "
					cJoinBen += "   AND RFO.D_E_L_E_T_ 	= SLY.D_E_L_E_T_ "
					
					aTail(aConsultas):= StrTran(aTail(aConsultas), '@JOIN_BENEFICIO@'	, cJoinBen)			
				EndIf			
							
			EndIf
			
			If(lOutros)
				aAdd(aConsultas,cDefault)			
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@ORIGEM@'		, "'OUTR'")
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@TABELA@'		, "'OUTR'")
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@VALOR@'		, 'RIR.RIR_VALCAL')
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@VLREMP@'		, 'RIR.RIR_VLREMP')
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@VLRFUN@'		, 'RIR.RIR_VLRFUN')
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@DESCRICAO@'	, 'RIS.RIS_DESC')						
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@JOIN_ENTIDADE@', cFunJoin)							
					
				
				cJoinBen := " INNER JOIN "+ RetSqlName('RIR')+" RIR "
				cJoinBen += "    ON RIR.RIR_FILIAL 	= ? "
				cJoinBen += "   AND RIR.RIR_MAT 	= SRA.RA_MAT"
				cJoinBen += "   AND RIR.RIR_TPBENE 	= SLY.LY_TIPO "
				cJoinBen += "   AND RIR.RIR_COD 	= SLY.LY_CODIGO "
				cJoinBen += "   AND RIR.RIR_PERIOD 	>= ? "
				cJoinBen += "   AND RIR.RIR_PERIOD 	<= ? "
				cJoinBen += "   AND RIR.D_E_L_E_T_ 	= SLY.D_E_L_E_T_ "
				
				cJoinBen += " INNER JOIN "+ RetSqlName('RIS')+" RIS "
				cJoinBen += "    ON RIS.RIS_FILIAL 	= ? "
				cJoinBen += "   AND RIS.RIS_TPBENE 	= SLY.LY_TIPO "
				cJoinBen += "   AND RIS.RIS_COD 	= SLY.LY_CODIGO "
				cJoinBen += "   AND RIS.D_E_L_E_T_ 	= SLY.D_E_L_E_T_ "
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@JOIN_BENEFICIO@'	, cJoinBen)
				
				aAdd(aConsultas,cDefault)			
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@ORIGEM@'		, "'OUTR'")
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@TABELA@'		, "'OUTR'")
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@VALOR@'		, 'RIQ.RIQ_VALCAL')
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@VLREMP@'		, 'RIQ.RIQ_VLREMP')
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@VLRFUN@'		, 'RIQ.RIQ_VLRFUN')
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@DESCRICAO@'	, 'RIS.RIS_DESC')						
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@JOIN_ENTIDADE@', cFunJoin)							
					
				cJoinBen := " INNER JOIN "+ RetSqlName('RIQ')+" RIQ "
				cJoinBen += "    ON RIQ.RIQ_FILIAL 	= ? "
				cJoinBen += "   AND RIQ.RIQ_MAT		= SRA.RA_MAT "
				cJoinBen += "   AND RIQ.RIQ_TPBENE 	= SLY.LY_TIPO "
				cJoinBen += "   AND RIQ.RIQ_COD 	= SLY.LY_CODIGO "
				cJoinBen += "   AND RIQ.D_E_L_E_T_ 	= SLY.D_E_L_E_T_ "
				
				cJoinBen += " INNER JOIN "+ RetSqlName('RIS')+" RIS "
				cJoinBen += "    ON RIS.RIS_FILIAL 	= ? "
				cJoinBen += "   AND RIS.RIS_TPBENE 	= SLY.LY_TIPO "
				cJoinBen += "   AND RIS.RIS_COD 	= SLY.LY_CODIGO "
				cJoinBen += "   AND RIS.D_E_L_E_T_ 	= SLY.D_E_L_E_T_ "
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@JOIN_BENEFICIO@'	, cJoinBen)			
			EndIf		
			
			
			If(lPlano)
				aAdd(aConsultas,cDefault)			
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@ORIGEM@'		, 'RHR.RHR_ORIGEM')
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@TABELA@'		, "'PLAN'")
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@VALOR@'		, '0')
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@VLREMP@'		, 'RHR.RHR_VLREMP')
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@VLRFUN@'		, 'RHR.RHR_VLRFUN')
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@DESCRICAO@'	, "SG0.G0_DESCR")						
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@JOIN_ENTIDADE@', cFunJoin)			
				
				cJoinBen := " INNER JOIN "+ RetSqlName('SG0')+" SG0 "
				cJoinBen += "    ON SG0.G0_FILIAL 	= ? "
				cJoinBen += "   AND SG0.G0_CODIGO 	= SLY.LY_CODIGO "
				cJoinBen += "   AND SG0.D_E_L_E_T_  = SLY.D_E_L_E_T_ "
				cJoinBen += "   AND SLY.LY_TIPO 	= ? "
				
				cJoinBen += " INNER JOIN "+ RetSqlName('SJX')+" SJX "
				cJoinBen += "    ON SJX.JX_FILIAL 	= ? "
				cJoinBen += "   AND SJX.JX_CODIGO 	= SG0.G0_CODIGO "
				cJoinBen += "   AND SJX.D_E_L_E_T_ 	= SG0.D_E_L_E_T_ "
				
				If(lDependentes)
					aAdd(aTpPlanos,'2')										
				EndIf		
				If(lAgregados)
					aAdd(aTpPlanos,'3')						
				EndIf
				
				cJoinBen += " INNER JOIN "+ RetSqlName('RHR')+" RHR "
				cJoinBen += "    ON RHR.RHR_FILIAL 	= ? "
				cJoinBen += "   AND RHR.RHR_MAT 	= SRA.RA_MAT "
				cJoinBen += "   AND "+TpPlano(aTpPlanos)
				cJoinBen += "   AND RHR.RHR_CODFOR 	= SJX.JX_CODFORN "
				cJoinBen += "   AND RHR.RHR_TPPLAN 	= SJX.JX_TPPLANO "
				cJoinBen += "   AND RHR.RHR_PLANO 	= SJX.JX_PLANO "
				cJoinBen += "   AND RHR.RHR_PD 		= SJX.JX_PD "
				cJoinBen += "   AND RHR.D_E_L_E_T_ 	= ? "
				
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@JOIN_BENEFICIO@'	, cJoinBen)
				aTail(aConsultas)+=', RHR.RHR_ORIGEM'
				
				aAdd(aConsultas,cDefault)			
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@ORIGEM@'		, 'RHS.RHS_ORIGEM')
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@TABELA@'		, "'PLAN'")
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@VALOR@'		, '0')
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@VLREMP@'		, 'RHS.RHS_VLREMP')
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@VLRFUN@'		, 'RHS.RHS_VLRFUN')
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@DESCRICAO@'	, "SG0.G0_DESCR")						
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@JOIN_ENTIDADE@', cFunJoin)			
				
				cJoinBen := " INNER JOIN "+ RetSqlName('SG0')+" SG0 "
				cJoinBen += "    ON SG0.G0_FILIAL 	= ? "
				cJoinBen += "   AND SG0.G0_CODIGO 	= SLY.LY_CODIGO "
				cJoinBen += "   AND SG0.D_E_L_E_T_ 	= SLY.D_E_L_E_T_ "
				cJoinBen += "   AND SLY.LY_TIPO 	= ? "
				
				cJoinBen += " INNER JOIN "+ RetSqlName('SJX')+" SJX "
				cJoinBen += "    ON SJX.JX_FILIAL 	= ? "
				cJoinBen += "   AND SJX.JX_CODIGO 	= SG0.G0_CODIGO "
				cJoinBen += "   AND SJX.D_E_L_E_T_ 	= SG0.D_E_L_E_T_ "
				
				cJoinBen += " INNER JOIN "+ RetSqlName('RHS')+" RHS "
				cJoinBen += "    ON RHS.RHS_FILIAL 	= ? "
				cJoinBen += "   AND RHS.RHS_MAT		= SRA.RA_MAT"
				cJoinBen += "   AND "+ TpPlano(aTpPlanos,'RHS.RHS_ORIGEM')
				cJoinBen += "   AND RHS.RHS_CODFOR 	= SJX.JX_CODFORN "
				cJoinBen += "   AND RHS.RHS_TPPLAN 	= SJX.JX_TPPLANO "
				cJoinBen += "   AND RHS.RHS_PLANO 	= SJX.JX_PLANO "
				cJoinBen += "   AND RHS.RHS_PD 		= SJX.JX_PD "
				cJoinBen += "   AND RHS.D_E_L_E_T_  = ? "
				
				aTail(aConsultas):= StrTran(aTail(aConsultas), '@JOIN_BENEFICIO@'	, cJoinBen)
				aTail(aConsultas)+=', RHS.RHS_ORIGEM'
			EndIf
			
			If(Len(aConsultas) > 0 )			
				cQuery := GetUnion(aConsultas)
			EndIf		
			
			cQueryFim := " SELECT LY_FILIAL, LY_ALIAS, LY_CHVENT, ENTDESCR, LY_TIPO, LY_CODIGO, DESCRICAO, LY_DTINI, LY_DTFIM, "
			cQueryFim += " ORIGEM, TABELA, SUM(VALOR) AS VALOR, SUM(VALOREMP) AS VALOREMP, SUM(VALORFUNC) AS VALORFUNC "
    		cQueryFim += " ? "
			cQueryFim += " FROM (  "
			cQueryFim += 	cQuery
			cQueryFim += " ) RESULTADO "
			cQueryFim += " GROUP BY LY_FILIAL, LY_ALIAS, LY_CHVENT, ENTDESCR, LY_TIPO, LY_CODIGO, ORIGEM, TABELA, "
			cQueryFim += " DESCRICAO, LY_DTINI, LY_DTFIM "
    		cQueryFim += " ? "
			cQueryFim += " ORDER BY LY_FILIAL, LY_ALIAS, LY_CHVENT, LY_TIPO, LY_CODIGO "

			cQueryFim := ChangeQuery(cQueryFim)
			// Instancia a classe
			oStatement := FwExecStatement():New(cQueryFim)

			// Captura e define os campos personalizados das tabelas SLY e SRA
			cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SLY", "SRA"})
			oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))

			If(lVrVa)
				
				If(lTemAberto)
					oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))

					oStatement:SetString(nParamOrder++, FwxFilial('SR0'))
					If(lVR)
						oStatement:SetString(nParamOrder++, 'VR')
						oStatement:SetString(nParamOrder++, '1')
					EndIf
					
					If(lVA)
						oStatement:SetString(nParamOrder++, 'VA')
						oStatement:SetString(nParamOrder++, '2')
					EndIf
					
					oStatement:SetString(nParamOrder++, FwxFilial('RFO'))

					If(lVR)
						oStatement:SetString(nParamOrder++, 'VR')
						oStatement:SetString(nParamOrder++, '1')
					EndIf
					
					If(lVA)
						oStatement:SetString(nParamOrder++, 'VA')
						oStatement:SetString(nParamOrder++, '2')
					EndIf
					
					oStatement:SetString(nParamOrder++, FWxFilial('SLY'))
					If(Len(aIntervalo) == 2)		
						oStatement:SetString(nParamOrder++, aIntervalo[1])	
						oStatement:SetString(nParamOrder++, aIntervalo[2])		
					EndIf
					oStatement:SetString(nParamOrder++, cEntidade)		
					If Len(aSituacoes) > 0
						oStatement:SetIN(nParamOrder++, aSituacoes)
					Endif
					If Len(aCategorias) > 0
						oStatement:SetIN(nParamOrder++, aCategorias)
					Endif
					oStatement:SetString(nParamOrder++, " ")
					
					oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))
				EndIf
				
				If(lTemFechado)
					oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))

					oStatement:SetString(nParamOrder++, FwxFilial('RG2'))

					If Len(aWherePer) > 0
						oStatement:SetIn(nParamOrder++, aWherePer)
					EndIf

					If(lVR)
						oStatement:SetString(nParamOrder++, 'VR')
						oStatement:SetString(nParamOrder++, '1')
					EndIf
					
					If(lVA)
						oStatement:SetString(nParamOrder++, 'VA')
						oStatement:SetString(nParamOrder++, '2')
					EndIf

					oStatement:SetString(nParamOrder++, FwxFilial('RFO'))

					If(lVR)
						oStatement:SetString(nParamOrder++, 'VR')
						oStatement:SetString(nParamOrder++, '1')
					EndIf
					
					If(lVA)
						oStatement:SetString(nParamOrder++, 'VA')
						oStatement:SetString(nParamOrder++, '2')
					EndIf

					oStatement:SetString(nParamOrder++, FWxFilial('SLY'))
					If(Len(aIntervalo) == 2)		
						oStatement:SetString(nParamOrder++, aIntervalo[1])		
						oStatement:SetString(nParamOrder++, aIntervalo[2])	
					EndIf
					oStatement:SetString(nParamOrder++, cEntidade)		
					If Len(aSituacoes) > 0
						oStatement:SetIN(nParamOrder++, aSituacoes)
					Endif
					If Len(aCategorias) > 0
						oStatement:SetIN(nParamOrder++, aCategorias)
					Endif
					oStatement:SetString(nParamOrder++, " ")

					oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))
				EndIf			
							
			EndIf
			
			If(lOutros)
				oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))

				oStatement:SetString(nParamOrder++, FwxFilial('RIR'))
				oStatement:SetString(nParamOrder++, SubStr(aIntervalo[1],1,6))
				oStatement:SetString(nParamOrder++, SubStr(aIntervalo[2],1,6))
				oStatement:SetString(nParamOrder++, FwxFilial('RIS'))
				
				oStatement:SetString(nParamOrder++, FWxFilial('SLY'))
				If(Len(aIntervalo) == 2)
					oStatement:SetString(nParamOrder++, aIntervalo[1])		
					oStatement:SetString(nParamOrder++, aIntervalo[2])		
				EndIf
				oStatement:SetString(nParamOrder++, cEntidade)		
				If Len(aSituacoes) > 0
					oStatement:SetIN(nParamOrder++, aSituacoes)
				Endif
				If Len(aCategorias) > 0
					oStatement:SetIN(nParamOrder++, aCategorias)
				Endif
				oStatement:SetString(nParamOrder++, " ")
				oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))
				oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))
				oStatement:SetString(nParamOrder++, FwxFilial('RIQ'))
				oStatement:SetString(nParamOrder++, FwxFilial('RIS'))
				oStatement:SetString(nParamOrder++, FWxFilial('SLY'))
				If(Len(aIntervalo) == 2)		
					oStatement:SetString(nParamOrder++, aIntervalo[1])		
					oStatement:SetString(nParamOrder++, aIntervalo[2])		
				EndIf
				oStatement:SetString(nParamOrder++, cEntidade)	
				If Len(aSituacoes) > 0
					oStatement:SetIN(nParamOrder++, aSituacoes)
				Endif
				If Len(aCategorias) > 0
					oStatement:SetIN(nParamOrder++, aCategorias)
				Endif
				oStatement:SetString(nParamOrder++, " ")	
				oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))
					
			EndIf		
			
			If(lPlano)
				oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))
				oStatement:SetString(nParamOrder++, FwxFilial('SG0'))
				oStatement:SetString(nParamOrder++, 'PS')
				oStatement:SetString(nParamOrder++, FwxFilial('SJX'))
				oStatement:SetString(nParamOrder++, FwxFilial('RHR'))
				oStatement:SetString(nParamOrder++, " ")
				
				oStatement:SetString(nParamOrder++, FWxFilial('SLY'))
				If(Len(aIntervalo) == 2)		
					oStatement:SetString(nParamOrder++, aIntervalo[1])		
					oStatement:SetString(nParamOrder++, aIntervalo[2])			
				EndIf
				oStatement:SetString(nParamOrder++, cEntidade)
				If Len(aSituacoes) > 0
					oStatement:SetIN(nParamOrder++, aSituacoes)
				Endif
				If Len(aCategorias) > 0
					oStatement:SetIN(nParamOrder++, aCategorias)
				Endif
				oStatement:SetString(nParamOrder++, " ")
				oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))
				oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))
				oStatement:SetString(nParamOrder++, FwxFilial('SG0'))
				oStatement:SetString(nParamOrder++, 'PS')
				oStatement:SetString(nParamOrder++, FwxFilial('SJX'))
				oStatement:SetString(nParamOrder++, FwxFilial('RHS'))
				oStatement:SetString(nParamOrder++, ' ')

				oStatement:SetString(nParamOrder++, FWxFilial('SLY'))
				If(Len(aIntervalo) == 2)		
					oStatement:SetString(nParamOrder++, aIntervalo[1])		
					oStatement:SetString(nParamOrder++, aIntervalo[2])		
				EndIf
				oStatement:SetString(nParamOrder++, cEntidade)
				If Len(aSituacoes) > 0
					oStatement:SetIN(nParamOrder++, aSituacoes)
				Endif
				If Len(aCategorias) > 0
					oStatement:SetIN(nParamOrder++, aCategorias)
				Endif
				oStatement:SetString(nParamOrder++, " ")	

				oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))
			EndIf
			
			oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))

			// Executa a query e retorna o alias criado
    		cAlias := oStatement:OpenAlias()

			while !(cAlias)->(Eof())

				// Cria o JSON com as informações da linha
        		jData := JSONObject():New()

				jData["LY_FILIAL"]		:= IIf(jLGPD["LY_FILIAL"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->LY_FILIAL),	(cAlias)->LY_FILIAL)
				jData["LY_ALIAS"]		:= IIf(jLGPD["LY_ALIAS"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->LY_ALIAS),    	(cAlias)->LY_ALIAS)
				jData["LY_CHVENT"]		:= IIf(jLGPD["LY_CHVENT"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->LY_CHVENT),   	(cAlias)->LY_CHVENT)
				jData["ENTDESCR"]		:= (cAlias)->ENTDESCR
				jData["LY_TIPO"]		:= IIf(jLGPD["LY_TIPO"],   	FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->LY_TIPO),    	(cAlias)->LY_TIPO)
				jData["LY_CODIGO"]		:= IIf(jLGPD["LY_CODIGO"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->LY_CODIGO),   	(cAlias)->LY_CODIGO)
				jData["DESCRICAO"]		:= (cAlias)->DESCRICAO
				jData["LY_DTINI"]		:= totvs.framework.treports.date.stringToTimeStamp((cAlias)->LY_DTINI)
				jData["LY_DTFIM"]		:= totvs.framework.treports.date.stringToTimeStamp((cAlias)->LY_DTFIM)
				jData["ORIGEM"]			:= (cAlias)->ORIGEM
				jData["TABELA"]			:= (cAlias)->TABELA
				jData["VALOR"]			:= (cAlias)->VALOR
				jData["VALOREMP"]		:= (cAlias)->VALOREMP
				jData["VALORFUNC"]		:= (cAlias)->VALORFUNC
				jData["FilialExec"]		:= cFilExec
				jData["EmpresaExec"]	:= cEmpExec

				// Adiciona os campos customizados no JSON
        		GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

				// Adiciona as informações da linha
        		self:oData:appendData(jData)

				(cAlias)->( dbSkip() )
			enddo
			(cAlias)->( DBCloseArea() )

			// Limpa os objetos da memória
			jData := NIL
			FwFreeObj(jData)
			FwFreeObj(jLGPD)
			FwFreeArray(aAllFields)
			FwFreeArray(aFieldsNR)
			FwFreeArray(aPDFields)
			FwFreeArray(aPDFieldsNR)
		EndIf
	EndIf

return self:oData
 
method getSchema() as object class GPER010TReportsBusinessObject
    local aFieldsSLY as array
    
    aFieldsSLY := { "LY_FILIAL", "LY_ALIAS", "LY_TIPO", "LY_CODIGO", "LY_DTINI", "LY_DTFIM" }
    
    self:oSchema:aliasToSchema("SLY", aFieldsSLY)

    self:oSchema:addProperty("LY_CHVENT",   STR0004, "string", STR0004, "LY_CHVENT")
    self:oSchema:addProperty("ENTDESCR",    STR0005, "string", STR0006, "ENTDESCR")
    self:oSchema:addProperty("DESCRICAO",   STR0007, "string", STR0008, "DESCRICAO")
    self:oSchema:addProperty("ORIGEM",      STR0009, "string", STR0009, "ORIGEM")
    self:oSchema:addProperty("TABELA",      STR0010, "string", STR0010, "TABELA")
    self:oSchema:addProperty("VALOR",       STR0011, "number", STR0011, "VALOR")
    self:oSchema:addProperty("VALOREMP",    STR0012, "number", STR0012, "VALOREMP")
    self:oSchema:addProperty("VALORFUNC",   STR0013, "number", STR0013, "VALORFUNC")
    self:oSchema:addProperty("FilialExec",  STR0014, "string", STR0015, "FilialExec")
    self:oSchema:addProperty("EmpresaExec", STR0016, "string", STR0017, "EmpresaExec")

    // Propriedades usadas como parâmetro

    // Parâmetros
    self:oSchema:addParameter("criterio",       STR0018 + " ?",           "string", .F.)
    self:oSchema:addParameter("entidade",       STR0019 + " ?",           "string", .F.)
    self:oSchema:addParameter("periodoDe",      STR0020 + " ?",           "string", .F.)
    self:oSchema:addParameter("periodoAte",     STR0021 + " ?",           "string", .F.)
    self:oSchema:addParameter("PAR_SITUACOES",  STR0022,                  "string", .T.)
    self:oSchema:addParameter("PAR_CATEGORIAS", STR0023,                  "string", .T.)
    self:oSchema:addParameter("PAR_VR", 	    STR0024 + " " + STR0025,  "string", .F.)
    self:oSchema:addParameter("PAR_VA", 	    STR0024 + " " + STR0026,  "string", .F.)
    self:oSchema:addParameter("PAR_OUTROS",     STR0024 + " " + STR0027,  "string", .F.)
    self:oSchema:addParameter("PAR_PS", 	    STR0024 + " " + STR0028,  "string", .F.)
    self:oSchema:addParameter("PAR_DEP", 	    STR0024 + " " + STR0029,  "string", .F.)
    self:oSchema:addParameter("PAR_AGR", 	    STR0024 + " " + STR0030,  "string", .F.)


	// Define os endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("criterio",       	"api/framework/v1/genericLookupService/smartview/SJQ",                                      2)
    self:setCustomURL("entidade",          	"/api/rh/smartview/v1/options/GPER010/entidade",                                       		1)
    self:setCustomURL("PAR_SITUACOES", 		"/api/rh/smartview/v1/options/GPEParams/getSX5/31",                                         2)
    self:setCustomURL("PAR_CATEGORIAS",   	"/api/rh/smartview/v1/options/GPEParams/getSX5/28",                                         2)
    self:setCustomURL("PAR_VR",         	"/api/rh/smartview/v1/options/GPER010/PAR_VR",                                           	1)
    self:setCustomURL("PAR_VA",         	"/api/rh/smartview/v1/options/GPER010/PAR_VA",                                           	1)
    self:setCustomURL("PAR_OUTROS",         "/api/rh/smartview/v1/options/GPER010/PAR_OUTROS",                                          1)
    self:setCustomURL("PAR_PS",         	"/api/rh/smartview/v1/options/GPER010/PAR_PS",                                           	1)
    self:setCustomURL("PAR_DEP",         	"/api/rh/smartview/v1/options/GPER010/PAR_DEP",                                           	1)
    self:setCustomURL("PAR_AGR",         	"/api/rh/smartview/v1/options/GPER010/PAR_AGR",                                           	1)
return self:oSchema


/*/{Protheus.doc} fGetFiltroClean
//Limpa a string de filtro para transformar em array.
@author Caio Kretzer
@since 05/07/2023
/*/
Static Function fGetFiltroClean(cFiltro)
Return StrTran(StrTran(StrTran(StrTran(cFiltro, " AND ", "=")," = ","="),"(",""),")","")

/*/{Protheus.doc} fGetParams
//Retorna o parametro solicitado (se existir)
@author Caio Kretzer
@since 05/07/2023
/*/
Static Function fGetParams(cFiltro, cParam, cTipo)
    Local aParams := StrTokArr2(cFiltro, "=")
    Local nPos
    Local cRet    := ""

	// default para caso não venha parâmetro
	If cTipo == 'N'
		cRet := 1
	Endif
    
    If Len(aParams) > 1
        nPos    := aScan(aParams, cParam)
        cRet := Iif(cTipo == 'C', StrTran(aParams[nPos+1], "'", ""), Val(aParams[nPos+1]))
    Endif
Return cRet


/*/{Protheus.doc} GetUnion
	Retorna uma consulta a partir do vetor fornecido
@author PHILIPE.POMPEU
@since 08/05/2015
@version P12
@param aQueries, vetor, vetor contendo um conjunto de queries à serem unidas
@param cUnionType, caractere, tipo do union(UNION, UNION ALL). Default: UNION
@return cResult, retorna uma string formada à partir de cada uma das posições de aQueries concatenadas com cUnionType
/*/
Static Function GetUnion(aQueries,cUnionType)
	Local cResult	:= ''	
	Local nI := 0
	Default cUnionType := 'UNION'
	
	for nI:= 1 to Len(aQueries)
		cResult += aQueries[nI]		
		If(nI < len(aQueries))
			cResult+=' ' + cUnionType + ' ' 
		EndIf		
	next nI
	
Return cResult

/*/{Protheus.doc} GetVrVAJoin
(long_description)
@author PHILIPE.POMPEU
@since 08/05/2015
@version P12
@param lVR, ${param_type}, (Descrição do parâmetro)
@param lVA, ${param_type}, (Descrição do parâmetro)
@return cResult, ${return_description}
/*/
Static Function GetVrVAJoin(lVR,lVA,cCampo)
	Local cResult := ''
	Local aTemp	:= {}
	Local nI := 0
	Default cCampo = 'SR0.R0_TPVALE'
	
	If(lVR)
		aAdd(aTemp,"(SLY.LY_TIPO = ? AND "+ cCampo +" = ?)")
	EndIf
	
	If(lVA)
		aAdd(aTemp,"(SLY.LY_TIPO = ? AND "+ cCampo +" = ?)")
	EndIf
	
	for nI:= 1 to Len(aTemp)
		cResult+= aTemp[nI]
		If(nI < len(aTemp))
			cResult+=' OR '
		EndIf
	next nI
	
	cResult:='('+ cResult + ')'
	
Return cResult


/*/{Protheus.doc} GetInterval
(long_description)
@author philipe.pompeu
@since 11/05/2015
@version P12
@param cDe, caractere, (Descrição do parâmetro)
@param cAte, caractere, (Descrição do parâmetro)
@param cCriterio, caractere, (Descrição do parâmetro)
@return aResult, ${return_description}
/*/
Static Function GetInterval(cDe,cAte,cCriterio)	
	Local aResult := {}
	Local aArea 	:= SJQ->(GetArea())
	Local xTemp
	Default cCriterio := ''	
			
	
	If(Empty(cCriterio))		
		cCriterio := PadR(cCriterio,TamSx3("JQ_CODIGO")[1])	
	EndIf
	
	If(Empty(cDe))
		SJQ->(dbSetOrder(3))	
		If(SJQ->(DbSeek(FwxFilial('SJQ')+ cCriterio)))
			cDe := SJQ->JQ_PERINI
		EndIf			
	EndIf
	
	If(Empty(cAte))
		SJQ->(dbSetOrder(3))
		If(SJQ->(DbSeek(FwxFilial('SJQ')+ cCriterio)))
			cAte := SJQ->JQ_PERFIM
		EndIf		
	EndIf		
	
	xTemp := cDe + '01'	
	aAdd(aResult,xTemp)	
	xTemp := cAte + '01'	
	xTemp := SToD(xTemp)
	xTemp := LastDate(xTemp)
	xTemp := DtoS(xTemp)	
	aAdd(aResult,xTemp)	
	
	RestArea(aArea)
Return aResult


/*/{Protheus.doc} EntityJoin
	Faz o Join com a Entidade informada em cEntidade
@author PHILIPE.POMPEU
@since 25/05/2015
@version P12
@param cEntidade, caractere, Entidade que deve ser feito o join
@return cJoin, Join com a Entidade informada
/*/
Static Function EntityJoin(cEntidade)
	Local cJoin := ''
	Local lIntegServ := SuperGetMV('MV_TECXRH',.F.,.F.)
	Default cEntidade = ''
	
	cJoin	:='INNER JOIN '
	Do Case
		Case (cEntidade == 'SQB')			
			cJoin	+= RetSqlName('SQB') + ' SQB ON ('
			cJoin	+= 'SQB.QB_FILIAL = SLY.LY_FILENT AND SQB.QB_DEPTO = SLY.LY_CHVENT AND SQB.D_E_L_E_T_ = SLY.D_E_L_E_T_)'
		Case (cEntidade == 'CTT')			
			cJoin	+= RetSqlName('CTT') + ' CTT ON ('
			cJoin	+= 'CTT.CTT_FILIAL = SLY.LY_FILENT AND CTT.CTT_CUSTO = SLY.LY_CHVENT AND CTT.D_E_L_E_T_ = SLY.D_E_L_E_T_)'			
		Case (cEntidade == 'SQ3')			
			cJoin	+= RetSqlName('SQ3') + ' SQ3 ON ('
			cJoin	+= 'SQ3.Q3_FILIAL = SLY.LY_FILENT AND SQ3.Q3_CARGO = SLY.LY_CHVENT AND SQ3.D_E_L_E_T_ = SLY.D_E_L_E_T_)'
		Case (cEntidade == 'SRJ')						
			cJoin	+= RetSqlName('SRJ') + ' SRJ ON ('
			cJoin	+= 'SRJ.RJ_FILIAL = SLY.LY_FILENT AND SRJ.RJ_FUNCAO = SLY.LY_CHVENT AND SRJ.D_E_L_E_T_ = SLY.D_E_L_E_T_)'
		Case (cEntidade == 'SR6')			
			cJoin	+= RetSqlName('SR6') + ' SR6 ON ('
			cJoin	+= 'SR6.R6_FILIAL = SLY.LY_FILENT AND SR6.R6_TURNO = SLY.LY_CHVENT AND SR6.D_E_L_E_T_ = SLY.D_E_L_E_T_)'
		Case (cEntidade == 'RCE')			
			cJoin	+= RetSqlName('RCE') + ' RCE ON ('
			cJoin	+= 'RCE.RCE_FILIAL = SLY.LY_FILENT AND RCE.RCE_CODIGO = SLY.LY_CHVENT AND RCE.D_E_L_E_T_ = SLY.D_E_L_E_T_)'		
		Case (cEntidade == 'RCL')			
			cJoin	+= RetSqlName('RCL') + ' RCL ON ('
			cJoin	+= 'RCL.RCL_FILIAL = SLY.LY_FILENT AND RCL.RCL_POSTO = SLY.LY_CHVENT AND RCL.D_E_L_E_T_ = SLY.D_E_L_E_T_)'
		Case (cEntidade == 'SM0')
			cJoin := ''			
		OtherWise
			If(lIntegServ)
				Do Case
					Case (cEntidade == 'SA1')						
						cJoin	+= RetSqlName('SA1') + ' SA1 ON ('
						cJoin	+= 'SA1.A1_FILIAL = SLY.LY_FILENT AND SA1.A1_COD = SLY.LY_CHVENT AND SA1.D_E_L_E_T_ = SLY.D_E_L_E_T_)'
					Case (cEntidade == 'ABS')						
						cJoin	+= RetSqlName('ABS') + ' ABS ON ('
						cJoin	+= 'ABS.ABS_FILIAL = SLY.LY_FILENT AND ABS.ABS_LOCAL = SLY.LY_CHVENT AND ABS.D_E_L_E_T_ = SLY.D_E_L_E_T_)'
					Case (cEntidade == 'TDX')						
						cJoin	+= RetSqlName('TDX') + ' TDX ON ('
						cJoin	+= 'TDX.TDX_FILIAL = SLY.LY_FILENT AND TDX.TDX_COD = SLY.LY_CHVENT AND TDX.D_E_L_E_T_ = SLY.D_E_L_E_T_)'			
				EndCase
			EndIf		
	EndCase
Return cJoin

/*/{Protheus.doc} EntityField
Retorna o campo que contém a descrição da Entidade
@author PHILIPE.POMPEU
@since 25/05/2015
@version P12
@param cEntidade, caractere, Entidade que o campo deve vir
@return cCampo, Campo na entidade que contem a sua descrição
/*/
Static Function EntityField(cEntidade)
	Local cCampo := ''
	Local lIntegServ := SuperGetMV('MV_TECXRH',.F.,.F.)	
	Default cEntidade = ''
	Do Case
		Case (cEntidade == 'SQB')
			cCampo	:='QB_DESCRIC'			
		Case (cEntidade == 'CTT')			
			cCampo	:= 'CTT_DESC01'						
		Case (cEntidade == 'SQ3')
			cCampo	:='Q3_DESCSUM'			
		Case (cEntidade == 'SRJ')
			cCampo	:='RJ_DESC'			
		Case (cEntidade == 'SR6')
			cCampo	:='R6_DESC'			
		Case (cEntidade == 'RCE')
			cCampo	:='RCE_DESCRI'					
		Case (cEntidade == 'RCL')		
			cCampo	:="''"			
		Case (cEntidade == 'SM0')
			cCampo	:="'VAZIO'"			
		OtherWise
			If(lIntegServ)
				Do Case
					Case (cEntidade == 'SA1')
						cCampo:='A1_NOME'						
					Case (cEntidade == 'ABS')
						cCampo:='ABS_DESCRI'						
					Case (cEntidade == 'TDX')	
						cCampo:="''"									
				EndCase
			EndIf		
	EndCase	
Return cCampo

/*/{Protheus.doc} GetFuncJoin
(long_description)
@author PHILIPE.POMPEU
@since 25/05/2015
@version P12
@param cEntidade, caractere, (Descrição do parâmetro)
@return ${return}, ${return_description}
/*/
Static Function GetFuncJoin(cEntidade)
	Local cFunJoin	:= ''
	Default cEntidade = ''
		
	cFunJoin := 'INNER JOIN '
	cFunJoin += RetSqlName('SRA') + ' SRA ON ('		
	
	If(cEntidade <> 'SM0')
		cFunJoin += " SRA.RA_FILIAL ='"+ FWxFilial("SRA") +"' AND "			
	EndIf		
	Do Case
		Case (cEntidade == 'SQB')
			cFunJoin += 'SRA.RA_DEPTO = SQB.QB_DEPTO AND'
		Case (cEntidade == 'CTT')				 	
			cFunJoin += 'SRA.RA_CC = CTT.CTT_CUSTO AND'			
		Case (cEntidade == 'SQ3')
			cFunJoin += 'SRA.RA_CARGO = SQ3.Q3_CARGO AND'
		Case (cEntidade == 'SRJ')
			cFunJoin += 'SRA.RA_CODFUNC = SRJ.RJ_FUNCAO AND'
		Case (cEntidade == 'SR6')
			cFunJoin += 'SRA.RA_TNOTRAB = SR6.R6_TURNO AND'
		Case (cEntidade == 'RCE')
			cFunJoin += 'SRA.RA_SINDICA = RCE.RCE_CODIGO AND'		
		Case (cEntidade == 'RCL')		
			cFunJoin += 'SRA.RA_POSTO = RCL.RCL_POSTO AND'
		Case (cEntidade == 'SM0')
			cFunJoin += 'SRA.RA_FILIAL = LY_CHVENT AND'		
	EndCase	
	cFunJoin +=' SRA.D_E_L_E_T_ = SLY.D_E_L_E_T_'
	cFunJoin+=")"
	
Return cFunJoin

/*/{Protheus.doc} TpPlano
@author PHILIPE.POMPEU
@since 27/05/2015
@version P12
@param aTpPlano, array, (Descrição do parâmetro)
@return ${return}, ${return_description}
/*/
Static Function TpPlano(aTpPlano,cCampo)	
	Local cTemp := ''	
	Local nI := 0
	Default cCampo := 'RHR.RHR_ORIGEM'
	
	for nI:= 1 to Len(aTpPlano)
		cTemp+="'"+aTpPlano[nI]+"'"
		If(nI < Len(aTpPlano))
			cTemp+=','
		EndIf
	next nI
		
Return (cCampo + " IN("+cTemp+")")


Static Function GetPeriods(cDe,cAte,aVrVa)
	Local cMyAlias	:= GetNextAlias()
	Local aResult	:= {}
	Local aUmPeriodo	:= {}
	Local lEstaAberto	:= .F.
	Local cWhere := ""
	Local cQueryPer := ""
	Local oStatementP := NIL as Object
	Local nParamOrderP := 1 as Numeric
	
	If !(Empty(cDe))
		cWhere += " RCH_PER >= ? AND "
	EndIf
	
	If !(Empty(cAte))
		cWhere += " RCH_PER <= ? AND "
	EndIf
		
	cQueryPer += " SELECT DISTINCT RCH_FILIAL, RCH_PER, RCH_DTFECH, RCH_PERSEL, RCH_DTINI, RCH_DTFIM "
	cQueryPer += " FROM "+RetSqlName("RCH")+" RCH "
	cQueryPer += " WHERE RCH_ROTEIR IN ( "
	cQueryPer += "     SELECT RY_CALCULO "
	cQueryPer += "     FROM "+RetSqlName("SRY")+" SRY "
	cQueryPer += "     WHERE SRY.RY_FILIAL 	= ? "
	cQueryPer += "       AND RY_TIPO 		IN (?) "
	cQueryPer += "       AND SRY.D_E_L_E_T_ = ? "
	cQueryPer += " ) "
	cQueryPer += " AND ((RCH_DTFECH = ? AND RCH_PERSEL = ?) OR (RCH_DTFECH <> ?))
	cQueryPer += " AND "+cWhere
	cQueryPer += " RCH_FILIAL = ? AND RCH.D_E_L_E_T_ = ? "

	cQueryPer := ChangeQuery(cQueryPer)
	// Instancia a classe
	oStatementP := FwExecStatement():New(cQueryPer)

	oStatementP:SetString(nParamOrderP++, xFilial("SRY"))
	oStatementP:SetIn(nParamOrderP++, aVrVa)
	oStatementP:SetString(nParamOrderP++, " ")
	oStatementP:SetString(nParamOrderP++, " ")
	oStatementP:SetNumeric(nParamOrderP++, 1)
	oStatementP:SetString(nParamOrderP++, " ")
	If !(Empty(cDe))
		oStatementP:SetString(nParamOrderP++, cDe)
	EndIf
	
	If !(Empty(cAte))
		oStatementP:SetString(nParamOrderP++, cAte)
	EndIf
	oStatementP:SetString(nParamOrderP++, xFilial("RCH"))
	oStatementP:SetString(nParamOrderP++, " ")

	cMyAlias := oStatementP:OpenAlias()
	
	while ( (cMyAlias)->(!Eof()) )
		If !(aScan(aResult,{|x|x[1] == (cMyAlias)->RCH_PER }) > 0)
			aUmPeriodo := {}
			aAdd(aUmPeriodo,(cMyAlias)->RCH_PER)
			
			lEstaAberto := Empty((cMyAlias)->RCH_DTFECH)
			
			aAdd(aUmPeriodo,lEstaAberto)
			aAdd(aUmPeriodo,{(cMyAlias)->RCH_DTINI,(cMyAlias)->RCH_DTFIM})
			
			aAdd(aResult,aUmPeriodo)
		EndIf
		(cMyAlias)->(dbSkip())
	End
	(cMyAlias)->(dbCloseArea())
Return (aResult)


Static Function GetWhere(aPeriodos)
	Local aTemp	:= {}
	Local nI
	
	for nI:= 1 to Len(aPeriodos)		
		If(!aPeriodos[nI,2])// Estiver fechado			
			aAdd(aTemp,"'"+aPeriodos[nI,1]+"'")			
		EndIf
	next nI
Return aTemp



/*/{Protheus.doc} getEntidade
	Busca a entidade conforme combobox
@author caio.kretzer
@since 22/11/2023
@version P12
@param cEnt, caractere
@return tabela da entidade
/*/
Static Function getEntidade(cEnt)
	If cEnt == '1'
		Return "SQB"
	ElseIf cEnt == '2'
		Return "CTT"
	ElseIf cEnt == '3'
		Return "SQ3"
	ElseIf cEnt == '4'
		Return "SRJ"
	ElseIf cEnt == '5'
		Return "SR6"
	ElseIf cEnt == '6'
		Return "RCE"
	ElseIf cEnt == '7'
		Return "RCL"
	ElseIf cEnt == '8'
		Return "SM0"
	ElseIf cEnt == '9'
		Return "SA1"
	ElseIf cEnt == '10'
		Return "ABS"
	Endif
Return "TDX"
