#Include "TOTVS.FRAMEWORK.TREPORTS.INTEGRATEDPROVIDER.th"
#Include "TLPP-CORE.th"
#Include "TLPP-REST.th"
#Include "RH.SV.PON.PONR070.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} PONR070TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório de Refeições.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 15/04/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPON", tables="SP5, SPN, SR6, SPM, SRA, CTT", customTables="SRA, CTT, SR6, SPM", name="Relatório de Refeições", country="ALL", initialRelease="12.1.2310")
Class PONR070TReportsBusinessObject From IntegratedProvider
    // Define os métodos da classe
    Public Method new()       As Object
    Public Method getData()   As Object
    Public Method getSchema() As Object
EndClass

/*/{Protheus.doc} PONR070TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 15/04/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class PONR070TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a área
    self:appendArea(STR0001) // "RH"

    // Define o nome e descrição do objeto de negócio
    self:setDisplayName(STR0002) // "Refeições"
    self:setDescription(STR0003) // "Este objeto de negócio traz as informações do Relatório de Refeições."
Return self

/*/{Protheus.doc} PONR070TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 15/04/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class PONR070TReportsBusinessObject
    // Declaração das variáveis locais
    Local cAlias      As Character // Alias do arquivo temporário principal
    Local cFilExec    As Character // Nome da filial de execução
    Local cEmpExec    As Character // Nome da empresa de execução
    Local cAccessSRA  As Character // Macro execução para validar acesso à tabela SRA
    Local cValidFil   As Character // Filiais válidas
    Local cPrevBranch As Character // Filial anterior
    Local aInfo       As Array     // Informações da filial
    Local aPDFields   As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields  As Array     // Estrutura de todos os campos do ON
    Local lObfuscated As Logical   // Flag que indica se tem algum campo para ofuscar
    Local jData       As JSON      // Informações a serem adicionadas no ON

    // Inicialização das variáveis locais
    cAlias      := ""
    cFilExec    := AllTrim(FwFilialName())
    cEmpExec    := AllTrim(FwEmpName(cEmpAnt))
    cAccessSRA  := &("{|| " + ChkRH("PONR070", "SRA", "2") + "}")
    cValidFil   := fValidFil()
    cPrevBranch := ""
    aInfo       := {}
    aPDFields   := {}
    aAllFields  := {}
    lObfuscated := .F.
    jData       := NIL

    // Cria o arquivo temporário
    cAlias := QueryDef(oFilter, self)

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))
        // Verifica o acesso à tabela SRA, se a filial é válida e se encontrou as informações da filial
        If ((!Empty(aInfo) .and. (cAlias)->RA_FILIAL == cPrevBranch) .or. (Eval(cAccessSRA) .and. (cAlias)->RA_FILIAL $ cValidFil .and. fInfo(@aInfo, (cAlias)->RA_FILIAL)))
            // Cria o JSON com as informações da linha
            jData := JSONObject():New()
            jData["FilialExec"]        := Trim(cFilExec)
            jData["EmpresaExec"]       := Trim(cEmpExec)
            jData["OriginPlatform"]    := "Protheus"
            jData["BranchCode"]        := Trim(IIf(lObfuscated .and. AScan(aPDFields, "RA_FILIAL")  <= 0, FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),  (cAlias)->RA_FILIAL))
            jData["EmployeeCode"]      := Trim(IIf(lObfuscated .and. AScan(aPDFields, "RA_MAT")     <= 0, FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),     (cAlias)->RA_MAT))
            jData["Name"]              := Trim(IIf(lObfuscated .and. AScan(aPDFields, "RA_NOME")    <= 0, FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),    IIf(!Empty((cAlias)->RA_NSOCIAL), (cAlias)->RA_NSOCIAL, (cAlias)->RA_NOME)))
            jData["CostCenterCode"]    := Trim(IIf(lObfuscated .and. AScan(aPDFields, "RA_CC")      <= 0, FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),      (cAlias)->RA_CC))
            jData["CostCenterDesc"]    := Trim(IIf(lObfuscated .and. AScan(aPDFields, "CTT_DESC01") <= 0, FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01), (cAlias)->CTT_DESC01))
            jData["EmployeeShiftCode"] := Trim(IIf(lObfuscated .and. AScan(aPDFields, "RA_TNOTRAB") <= 0, FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_TNOTRAB), (cAlias)->RA_TNOTRAB))
            jData["EmployeeShiftDesc"] := Trim(IIf(lObfuscated .and. AScan(aPDFields, "R6_DESC")    <= 0, FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->R6_DESC),    (cAlias)->R6_DESC))
            jData["MealTypeCode"]      := Trim(IIf(lObfuscated .and. AScan(aPDFields, "TIPOREF")    <= 0, FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->TIPOREF),    (cAlias)->TIPOREF))
            jData["MealTypeDesc"]      := Trim(IIf(lObfuscated .and. AScan(aPDFields, "PM_DESCREF") <= 0, FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->PM_DESCREF), (cAlias)->PM_DESCREF))
            jData["MealDate"]          := IIf(lObfuscated      .and. AScan(aPDFields, "P5_DATA")    <= 0, FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->DATA),       totvs.framework.treports.date.stringToTimeStamp((cAlias)->DATA))
            jData["MealTime"]          := IIf(lObfuscated      .and. AScan(aPDFields, "P5_HORA")    <= 0, FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->HORA),       StrTran(StrZero((cAlias)->HORA, 5, 2), ".", ":"))
            jData["Origin"]            := IIf(lObfuscated      .and. AScan(aPDFields, "P5_FLAG")    <= 0, FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->FLAG),       IIf((cAlias)->FLAG == "E", "Relógio", IIf((cAlias)->FLAG == "I", "Manual", "")))
            jData["ReferenceValue"]    := IIf(lObfuscated      .and. AScan(aPDFields, "P5_VALREF")  <= 0, 0, (cAlias)->VALREF)
            jData["EmployeeDiscount"]  := IIf(lObfuscated      .and. AScan(aPDFields, "P5_DESCFUN") <= 0, 0, (cAlias)->DESCFUN)
            jData["CompanyDiscount"]   := IIf(lObfuscated      .and. AScan(aPDFields, "P5_VALREF")  <= 0, 0, (cAlias)->DESC_EMP)
            jData["MealQuantity"]      := 1
            jData["BenefitCode"]       := ""
            jData["BenefitDesc"]       := ""

            // Adiciona os campos customizados no JSON
            GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

            // Adiciona as informações da linha
            self:oData:appendData(jData)
        EndIf

        // Atualiza a filial
        cPrevBranch := (cAlias)->RA_FILIAL

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    // Limpa os vetores e objetos da memória
    FwFreeArray(aInfo)
    FwFreeArray(aPDFields)
    FwFreeArray(aAllFields)
    jData := NIL // Se não atribuir nulo à variável, a referência do último JSON adicionado será limpada e o último registro ficará em branco
    FwFreeObj(jData)
Return self:oData

/*/{Protheus.doc} PONR070TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 15/04/2024
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class PONR070TReportsBusinessObject
    // Define os parâmetros do relatório
    self:oSchema:addParameter("DateFrom",           "Data De",         "date",   .F.)
    self:oSchema:addParameter("DateTo",             "Data Até",        "date",   .F.)
    self:oSchema:addParameter("BranchCodeFrom",     "Filial De",       "string", .F.)
    self:oSchema:addParameter("BranchCodeTo",       "Filial Até",      "string", .F.)
    self:oSchema:addParameter("CostCenterCodeFrom", "CC. De",          "string", .F.)
    self:oSchema:addParameter("CostCenterCodeTo",   "CC. Até",         "string", .F.)
    self:oSchema:addParameter("ShiftCodeFrom",      "Turno De",        "string", .F.)
    self:oSchema:addParameter("ShiftCodeTo",        "Turno Até",       "string", .F.)
    self:oSchema:addParameter("EmployeeCodeFrom",   "Matrícula De",    "string", .F.)
    self:oSchema:addParameter("EmployeeCodeTo",     "Matrícula Até",   "string", .F.)
    self:oSchema:addParameter("NameFrom",           "Nome De",         "string", .F.)
    self:oSchema:addParameter("NameTo",             "Nome Até",        "string", .F.)
    self:oSchema:addParameter("EmployeeSituation",  "Situações",       "string", .T.)
    self:oSchema:addParameter("EmployeeCategory",   "Categorias",      "string", .T.)
    self:oSchema:addParameter("RefectoryTimeFrom",  "Hr. Refeit. De",  "number", .F.)
    self:oSchema:addParameter("RefectoryTimeTo",    "Hr. Refeit. Até", "number", .F.)
    self:oSchema:addParameter("MealType",           "Tp. Refeição",    "string", .F.)
    self:oSchema:addParameter("ReportType",         "Tp. Relatório",   "string", .F.)
    self:oSchema:addParameter("DayToDay",           "Dia a Dia",       "string", .F.)
    self:oSchema:addParameter("BreakBy" ,           "Quebra por",      "string", .F.)

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("BranchCodeFrom",     "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("BranchCodeTo",       "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("CostCenterCodeFrom", "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("CostCenterCodeTo",   "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("ShiftCodeFrom",      "/api/framework/v1/genericLookupService/smartview/SR6", 2)
    self:setCustomURL("ShiftCodeTo",        "/api/framework/v1/genericLookupService/smartview/SR6", 2)
    self:setCustomURL("EmployeeCodeFrom",   "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("EmployeeCodeTo",     "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("EmployeeSituation",  "/api/rh/smartview/v1/options/GPEParams/getSX5/31",     2)
    self:setCustomURL("EmployeeCategory",   "/api/rh/smartview/v1/options/GPEParams/getSX5/28",     2)
    self:setCustomURL("MealType",           "/api/framework/v1/genericLookupService/smartview/SPM", 2)
    self:setCustomURL("ReportType",         "/api/rh/smartview/v1/options/PONR070/ReportType",      1)
    self:setCustomURL("DayToDay",           "/api/rh/smartview/v1/options/GPEParams/getSNOptions",  1)
    self:setCustomURL("BreakBy",            "/api/rh/smartview/v1/options/PONR070/BreakBy",         1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",        "Filial de Execução",    "string", "Filial Exec",           "FilialExec",      NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpresaExec",       "Empresa de Execução",   "string", "Empresa Exec",          "EmpresaExec",     NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("OriginPlatform",    "Plataforma de Origem",  "string", "Plataforma Origem",     "OriginPlatform",  NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("BranchCode",        "Filial",                "string", "Filial",                "RA_FILIAL",       NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("EmployeeCode",      "Matrícula",             "string", "Matrícula",             "RA_MAT",          NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("Name",              "Nome",                  "string", "Nome",                  "RA_NOME",         NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("CostCenterCode",    "Cód. Centro de Custo",  "string", "Cód. Centro de Custo",  "RA_CC",           NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("CostCenterDesc",    "Desc. Centro de Custo", "string", "Desc. Centro de Custo", "CTT_DESC01",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("EmployeeShiftCode", "Turno",                 "string", "Turno",                 "RA_TNOTRAB",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("EmployeeShiftDesc", "Desc. Turno",           "string", "Desc. Turno",           "R6_DESC",         NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("MealDate",          "Dt. Refeição",          "date",   "Dt. Refeição",          "P5_DATA",         NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("MealTime",          "Hr. Refeição",          "string", "Hr. Refeição",          "P5_HORA",         NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("MealTypeCode",      "Tipo Refeição",         "string", "Tipo Refeição",         "P5_TIPOREF",      NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("MealTypeDesc",      "Desc. Refeição",        "string", "Desc. Refeição",        "PM_DESCREF",      NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("Origin",            "Origem",                "string", "Origem",                "P5_FLAG",         NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("MealQuantity",      "Qtd. Refeições",        "number", "Qtd. Refeições",        "MealQuantity",    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("ReferenceValue",    "Vlr. Referência",       "number", "Vlr. Referência",       "P5_VALREF",       NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmployeeDiscount",  "Desconto Fun.",         "number", "Desconto Fun.",         "P5_DESCFUN",      NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("CompanyDiscount",   "Desconto Emp.",         "number", "Desconto Emp.",         "CompanyDiscount", NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("BenefitCode",       "Cód. Benefício",        "string", "Cód. Benefício",        "BenefitCode",     NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("BenefitDesc",       "Desc. Benefício",       "string", "Desc. Benefício",       "BenefitDesc",     NIL, NIL, NIL, .F.)
Return self:oSchema

/*/{Protheus.doc} QueryDef
    Define a query de busca dos registros.
    @type Function
    @version 12.1.2310
    @author arthur.sales
    @since 15/04/2024
    @param oFilter, Object, Filtro do objeto de negócio
    @return Character, Alias do arquivo temporário
/*/
Static Function QueryDef(oFilter As Object, oSelf As Object) As Character
    // Declaração das variáveis locais
    Local cQuery              As Character // Query para busca dos registros
    Local cAlias              As Character // Alias do arquivo temporário
    Local cFilter             As Character // Filtro do objeto de negócio
    Local cDateFrom           As Character // Parâmetro Data De
    Local cDateTo             As Character // Parâmetro Data Até
    Local cBranchCodeFrom     As Character // Parâmetro Filial De
    Local cBranchCodeTo       As Character // Parâmetro Filial Até
    Local cCostCenterCodeFrom As Character // Parâmetro Centro de Custo De
    Local cCostCenterCodeTo   As Character // Parâmetro Centro de Custo Até
    Local cShiftCodeFrom      As Character // Parâmetro Turno De
    Local cShiftCodeTo        As Character // Parâmetro Turno Até
    Local cEmployeeCodeFrom   As Character // Parâmetro Matrícula De
    Local cEmployeeCodeTo     As Character // Parâmetro Matrícula Até
    Local cNameFrom           As Character // Parâmetro Nome De
    Local cNameTo             As Character // Parâmetro Nome Até
    Local cMealType           As Character // Parâmetro Tipo de Refeição
    Local cCustomFields       As Character // Campos customizados no formato SQL
    Local aEmployeeSituation  As Array     // Parâmetro Situações
    Local aEmployeeCategory   As Array     // Parâmetro Categorias
    Local jParams             As JSON      // Parâmetros do objeto de negócio
    Local oStatement          As Object    // Instância da classe FwExecStatement
    Local nParamOrder         As Numeric   // Ordem dos bind parameters da query
    Local nAux                As Numeric   // Contador auxiliar para percorrer vetores
    Local nRefectoryTimeFrom  As Numeric   // Parâmetro Hora Refeitório De
    Local nRefectoryTimeTo    As Numeric   // Parâmetro Hora Refeitório Até

    // Inicialização das variáveis
    cQuery              := ""
    cAlias              := ""
    cFilter             := IIf(oFilter:hasFilter(), " WHERE " + oFilter:getSQLExpression(), "")
    cDateFrom           := ""
    cDateTo             := ""
    cBranchCodeFrom     := ""
    cBranchCodeTo       := ""
    cCostCenterCodeFrom := ""
    cCostCenterCodeTo   := ""
    cShiftCodeFrom      := ""
    cShiftCodeTo        := ""
    cEmployeeCodeFrom   := ""
    cEmployeeCodeTo     := ""
    cNameFrom           := ""
    cNameTo             := ""
    cMealType           := ""
    cCustomFields       := ""
    aEmployeeSituation  := {}
    aEmployeeCategory   := {}
    jParams             := oFilter:getParameters()
    oStatement          := NIL
    nParamOrder         := 1
    nAux                := 0
    nRefectoryTimeFrom  := 0
    nRefectoryTimeTo    := 0

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cDateFrom           := IIf(jParams:hasProperty("DateFrom")           .and. Len(jParams["DateFrom"])          > 0 .and. !Empty(jParams["DateFrom"][1]),           jParams["DateFrom"][1],           FwTimeStamp(6, CToD("")))
        cDateTo             := IIf(jParams:hasProperty("DateTo")             .and. Len(jParams["DateTo"])            > 0 .and. !Empty(jParams["DateTo"][1]),             jParams["DateTo"][1],             FwTimeStamp(6, CToD("")))
        cBranchCodeFrom     := IIf(jParams:hasProperty("BranchCodeFrom")     .and. Len(jParams["BranchCodeFrom"])    > 0 .and. !Empty(jParams["BranchCodeFrom"][1]),     jParams["BranchCodeFrom"][1],     "")
        cBranchCodeTo       := IIf(jParams:hasProperty("BranchCodeTo")       .and. Len(jParams["BranchCodeTo"])      > 0 .and. !Empty(jParams["BranchCodeTo"][1]),       jParams["BranchCodeTo"][1],       Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCostCenterCodeFrom := IIf(jParams:hasProperty("CostCenterCodeFrom") .and. Len(jParams["CostCenterCodeFrom"])> 0 .and. !Empty(jParams["CostCenterCodeFrom"][1]), jParams["CostCenterCodeFrom"][1], "")
        cCostCenterCodeTo   := IIf(jParams:hasProperty("CostCenterCodeTo")   .and. Len(jParams["CostCenterCodeTo"])  > 0 .and. !Empty(jParams["CostCenterCodeTo"][1]),   jParams["CostCenterCodeTo"][1],   Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cShiftCodeFrom      := IIf(jParams:hasProperty("ShiftCodeFrom")      .and. Len(jParams["ShiftCodeFrom"])     > 0 .and. !Empty(jParams["ShiftCodeFrom"][1]),      jParams["ShiftCodeFrom"][1],      "")
        cShiftCodeTo        := IIf(jParams:hasProperty("ShiftCodeTo")        .and. Len(jParams["ShiftCodeTo"])       > 0 .and. !Empty(jParams["ShiftCodeTo"][1]),        jParams["ShiftCodeTo"][1],        Replicate("Z", GetSX3Cache("RA_TNOTRAB", "X3_TAMANHO")))
        cEmployeeCodeFrom   := IIf(jParams:hasProperty("EmployeeCodeFrom")   .and. Len(jParams["EmployeeCodeFrom"])  > 0 .and. !Empty(jParams["EmployeeCodeFrom"][1]),   jParams["EmployeeCodeFrom"][1],   "")
        cEmployeeCodeTo     := IIf(jParams:hasProperty("EmployeeCodeTo")     .and. Len(jParams["EmployeeCodeTo"])    > 0 .and. !Empty(jParams["EmployeeCodeTo"][1]),     jParams["EmployeeCodeTo"][1],     Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cNameFrom           := IIf(jParams:hasProperty("NameFrom")           .and. Len(jParams["NameFrom"])          > 0 .and. !Empty(jParams["NameFrom"][1]),           jParams["NameFrom"][1],           "")
        cNameTo             := IIf(jParams:hasProperty("NameTo")             .and. Len(jParams["NameTo"])            > 0 .and. !Empty(jParams["NameTo"][1]),             jParams["NameTo"][1],             Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
        nRefectoryTimeFrom  := IIf(jParams:hasProperty("RefectoryTimeFrom")  .and. Len(jParams["RefectoryTimeFrom"]) > 0 .and. !Empty(jParams["RefectoryTimeFrom"][1]),  jParams["RefectoryTimeFrom"][1],  00.00)
        nRefectoryTimeTo    := IIf(jParams:hasProperty("RefectoryTimeTo")    .and. Len(jParams["RefectoryTimeTo"])   > 0 .and. !Empty(jParams["RefectoryTimeTo"][1]),    jParams["RefectoryTimeTo"][1],    23.59)
        cMealType           := IIf(jParams:hasProperty("MealType")           .and. Len(jParams["MealType"])          > 0 .and. !Empty(jParams["MealType"][1]),           jParams["MealType"][1],           "")
        aEmployeeSituation  := IIf(jParams:hasProperty("EmployeeSituation"), jParams["EmployeeSituation"], {})
        aEmployeeCategory   := IIf(jParams:hasProperty("EmployeeCategory"),  jParams["EmployeeCategory"],  {})
    EndIf

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aEmployeeSituation)
        aEmployeeSituation[nAux] := IIf(aEmployeeSituation[nAux] == NIL .Or. ValType(aEmployeeSituation[nAux]) == "J", " ", aEmployeeSituation[nAux])
    Next nAux

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aEmployeeCategory)
        aEmployeeCategory[nAux] := IIf(aEmployeeCategory[nAux] == NIL .Or. ValType(aEmployeeCategory[nAux]) == "J", " ", aEmployeeCategory[nAux])
    Next nAux

    // Trata as variáveis de data para ficarem no formato do banco
    cDateFrom := StrTran(SubStr(cDateFrom, 1, 10), "-", "")
    cDateTo   := StrTran(SubStr(cDateTo,   1, 10), "-", "")

    // Montagem da query de busca
	cQuery :=	"SELECT "
	cQuery +=		"Q1.*, "
	cQuery +=		"CTT_DESC01, "
	cQuery +=		"R6_DESC, "
	cQuery +=		"PM_DESCREF "
	cQuery +=		"? "	// 1
	cQuery +=	"FROM "
	cQuery +=		"( "
	cQuery +=			"SELECT "
	cQuery +=				"RA_FILIAL, "
	cQuery +=				"RA_MAT, "
	cQuery +=				"RA_NOME, "
	cQuery +=				"RA_NSOCIAL, "
	cQuery +=				"RA_CC, "
	cQuery +=				"RA_TNOTRAB, "
	cQuery +=				"P5_DATA AS DATA, "
	cQuery +=				"P5_HORA AS HORA, "
	cQuery +=				"P5_TIPOREF AS TIPOREF, "
	cQuery +=				"P5_FLAG AS FLAG, "
	cQuery +=				"P5_VALREF AS VALREF, "
	cQuery +=				"P5_DESCFUN AS DESCFUN, "
	cQuery +=				"P5_VALREF - P5_DESCFUN AS DESC_EMP "
	cQuery +=				"? "	// 2
	cQuery +=			"FROM " + RetSQLName("SRA") + " SRA "
	cQuery +=			"INNER JOIN " + RetSQLName("SP5") + " SP5 ON " + FwJoinFilial("SP5", "SRA") + " AND P5_MAT = RA_MAT AND SP5.D_E_L_E_T_ = ? " // 3
	cQuery +=			"WHERE RA_FILIAL BETWEEN ? AND ? "				// 4 e 5
	cQuery +=				"AND RA_MAT BETWEEN ? AND ? "				// 6 e 7
	cQuery +=				"AND RA_NOME BETWEEN ? AND ? "				// 8 e 9
	cQuery +=				"AND RA_CC BETWEEN ? AND ? "				// 10 e 11
	cQuery +=				"AND RA_TNOTRAB BETWEEN ? AND ? "			// 12 e 13
	cQuery +=				"AND RA_CATFUNC IN (?) "					// 14
	cQuery +=				"AND RA_SITFOLH IN (?) "					// 15
	cQuery +=				"AND (RA_DEMISSA = ? OR RA_DEMISSA >= ?) "	// 16 e 17
	cQuery +=				"AND P5_DATA BETWEEN ? AND ? "				// 18 e 19
	cQuery +=				"AND P5_HORA BETWEEN ? AND ? "				// 20 e 21
	cQuery +=				"AND (? = ? OR P5_TIPOREF = ?) "			// 22, 23 e 24
	cQuery +=				"AND SRA.D_E_L_E_T_ = ? "					// 25
	cQuery +=			"UNION ALL "
	cQuery +=			"SELECT "
	cQuery +=				"RA_FILIAL, "
	cQuery +=				"RA_MAT, "
	cQuery +=				"RA_NOME, "
	cQuery +=				"RA_NSOCIAL, "
	cQuery +=				"RA_CC, "
	cQuery +=				"RA_TNOTRAB, "
	cQuery +=				"PN_DATA AS DATA, "
	cQuery +=				"PN_HORA AS HORA, "
	cQuery +=				"PN_TIPOREF AS TIPOREF, "
	cQuery +=				"PN_FLAG AS FLAG, "
	cQuery +=				"PN_VALREF AS VALREF, "
	cQuery +=				"PN_DESCFUN AS DESCFUN, "
	cQuery +=				"PN_VALREF - PN_DESCFUN AS DESC_EMP "
	cQuery +=				"? "	// 26
	cQuery +=			"FROM " + RetSQLName("SRA") + " SRA "
	cQuery +=			"INNER JOIN " + RetSQLName("SPN") + " SPN ON " + FwJoinFilial("SPN", "SRA") + " AND PN_MAT = RA_MAT AND SPN.D_E_L_E_T_ = ? "	// 27
	cQuery +=			"WHERE RA_FILIAL BETWEEN ? AND ? "				// 28 e 29
	cQuery +=				"AND RA_MAT BETWEEN ? AND ? "				// 30 e 31
	cQuery +=				"AND RA_NOME BETWEEN ? AND ? "				// 32 e 33
	cQuery +=				"AND RA_CC BETWEEN ? AND ? "				// 34 e 35
	cQuery +=				"AND RA_TNOTRAB BETWEEN ? AND ? "			// 36 e 37
	cQuery +=				"AND RA_CATFUNC IN (?) "					// 38
	cQuery +=				"AND RA_SITFOLH IN (?) "					// 39
	cQuery +=				"AND (RA_DEMISSA = ? OR RA_DEMISSA >= ?) "	// 40 e 41
	cQuery +=				"AND PN_DATA BETWEEN ? AND ? "				// 42 e 43
	cQuery +=				"AND PN_HORA BETWEEN ? AND ? "				// 44 e 45
	cQuery +=				"AND (? = ? OR PN_TIPOREF = ?) "			// 46, 47 e 48
	cQuery +=				"AND SRA.D_E_L_E_T_ = ? "						// 49
	cQuery +=		") Q1 "
	cQuery +=		"INNER JOIN " + RetSQLName("CTT") + " CTT ON " + StrTran(FwJoinFilial("CTT", "SRA"), "SRA.") + " AND CTT_CUSTO = RA_CC AND CTT.D_E_L_E_T_ = ? " 			// 50
	cQuery +=		"INNER JOIN " + RetSQLName("SR6") + " SR6 ON " + StrTran(FwJoinFilial("SR6", "SRA"), "SRA.") + " AND R6_TURNO = RA_TNOTRAB AND SR6.D_E_L_E_T_ = ? "			// 51
	cQuery +=		"INNER JOIN " + RetSQLName("SPM") + " SPM ON " + StrTran(FwJoinFilial("SPM", "SP5"), "SP5.P5", "RA") + "AND PM_TIPOREF = TIPOREF AND SPM.D_E_L_E_T_ = ? "	// 52
	cQuery +=		"? "	// 53
	
	cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Captura e define os campos personalizados das tabelas CTT, SR6 e SPM
    cCustomFields := GPESmartViewUtils():GetCustomFields(oSelf:getCustomFields(), {"CTT", "SR6", "SPM"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    // Define o valor dos bind parameters comuns para as duas queries unidas
    For nAux := 1 To 2
        // Captura e define os campos personalizados da tabela SRA
        cCustomFields := GPESmartViewUtils():GetCustomFields(oSelf:getCustomFields(), {"SRA"})
        oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))	// 2 ou 26
		oStatement:SetString(nParamOrder++, " ")													// 3 ou 27
        oStatement:SetString(nParamOrder++,  cBranchCodeFrom)										// 4 ou 28
        oStatement:SetString(nParamOrder++,  cBranchCodeTo)											// 5 ou 29
        oStatement:SetString(nParamOrder++,  cEmployeeCodeFrom)										// 6 ou 30
        oStatement:SetString(nParamOrder++,  cEmployeeCodeTo)										// 7 ou 31
        oStatement:SetString(nParamOrder++,  cNameFrom)												// 8 ou 32
        oStatement:SetString(nParamOrder++,  cNameTo)												// 9 ou 33
        oStatement:SetString(nParamOrder++,  cCostCenterCodeFrom)									// 10 ou 34
        oStatement:SetString(nParamOrder++,  cCostCenterCodeTo)										// 11 ou 35
        oStatement:SetString(nParamOrder++,  cShiftCodeFrom)										// 12 ou 36
        oStatement:SetString(nParamOrder++,  cShiftCodeTo)											// 13 ou 37

		If (Len(aEmployeeCategory) > 0)
			oStatement:SetIn(nParamOrder++, aEmployeeCategory)										// 14 ou 38
		Else
			oStatement:SetUnsafe(nParamOrder++, GPESmartViewUtils():GetSQLIN(fCategoria(NIL, .F.)))	// 14 ou 38
		EndIf

		If (Len(aEmployeeSituation) > 0)
			oStatement:SetIn(nParamOrder++, aEmployeeSituation)										// 15 ou 39
        Else
			oStatement:SetUnsafe(nParamOrder++, GPESmartViewUtils():GetSQLIN(fSituacao(NIL, .F.)))	// 15 ou 39
		EndIf

		oStatement:SetString(nParamOrder++, " ")													// 16 ou 40
        oStatement:SetDate(nParamOrder++,    SToD(cDateFrom))										// 17 ou 41
        oStatement:SetDate(nParamOrder++,    SToD(cDateFrom))										// 18 ou 42
        oStatement:SetDate(nParamOrder++,    SToD(cDateTo))											// 19 ou 43
        oStatement:SetNumeric(nParamOrder++, nRefectoryTimeFrom)									// 20 ou 44
        oStatement:SetNumeric(nParamOrder++, nRefectoryTimeTo)										// 21 ou 45
        oStatement:SetString(nParamOrder++,  cMealType)												// 22 ou 46
		oStatement:SetString(nParamOrder++, " ")													// 23 ou 47
        oStatement:SetString(nParamOrder++,  cMealType)												// 24 ou 48
		oStatement:SetString(nParamOrder++, " ")													// 25 ou 49
    Next nAux

	oStatement:SetString(nParamOrder++, " ")		// 50
	oStatement:SetString(nParamOrder++, " ")		// 51
	oStatement:SetString(nParamOrder++, " ")		// 52

    // Define o valor do parâmetro filtro que está fora do UNION
    oStatement:SetUnsafe(nParamOrder++, cFilter)	// 53

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Limpa os objetos da memória
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeArray(aEmployeeSituation)
    FwFreeArray(aEmployeeCategory)
Return cAlias
