#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper440.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER440TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Ficha de Salário-Família
    @type Class
    @version 12.1.2510
    @author caio.kretzer
    @since 10/07/2025
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SRB", customTables="SRA", name="Relatório Ficha de Salário-Família", country="ALL", initialRelease="12.1.2310")
Class GPER440TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} GPER440TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2510
    @author caio.kretzer
    @since 10/07/2025
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER440TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Ficha de Salario-Familia"

Return self

/*/{Protheus.doc} GPER440TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2510
    @author caio.kretzer
    @since 10/07/2025
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER440TReportsBusinessObject
Return STR0003 // "Este objeto de negócio traz as informações do Relatório de Ficha de Salário-Família"

/*/{Protheus.doc} GPER440TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2510
    @author caio.kretzer
    @since 10/07/2025
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER440TReportsBusinessObject
    // Declaração das variáveis locais
    Local cFilExec          As Character // Nome da filial de execução
    Local cEmpExec          As Character // Nome da empresa de execução
    Local cEmpAnt           As Character // Nome da empresa de execução
    Local cQuery            As Character // Query do relatório    
    Local cAlias            As Character // Alias do arquivo temporário
    Local aSituacoes        As Array     // Parâmetro Situações
    Local aCategorias       As Array     // Parâmetro Categorias
    Local cFilParDe         As Character // Filial do parâmetro
    Local cFilParAte        As Character // Filial do parâmetro
    Local cCcDePar          As Character 
    Local cCcAtePar         As Character 
    Local cMatDePar         As Character 
    Local cMatAtePar        As Character 
    Local nVias             As Numeric 
    Local cValidFil         As Character // Filiais válidas
    Local cAcessaSRA        As Character // Macro execução para validar acesso à tabela SRA
    Local nParamOrder       As Numeric   // Contador de parâmetros da query

    Local JParams           As JSON      // Parâmetros do objeto de negócio
    Local jDepBase          As JSON      // Registros do objeto de negócio
    Local oStatement        As Object    // Objeto usada para utilização da classe FwExecStatement
    Local cCustomFields     As Character // Campos customizados no formato SQL
    Local cName             As Character // Nome do campo customizado
    Local aPDFields         As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR       As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR         As Array     // Campos com nome diferente do real
    Local aAllFields        As Array     // Estrutura de todos os campos do ON
    Local lObfuscated       As Logical   // Flag que indica se tem algum campo para ofuscar
    Local jData             As JSON      // Informações a serem adicionadas no ON
    Local jNewDep           As JSON      // Novo registro a ser adicionado no ON
    Local jLGPD             As JSON      // Indica quais campos devem ser ofuscados
    Local nFields           As Numeric   // Contador de campos
    Local aDependentes      As Array     // Array de Filtro Dep

    Local aInfo             As Array     // Array com as informações da filial
    Local cChave            As Character // Chave de concatenação para verificar se é o mesmo funcionário
    Local cFilAntSv         As Character // Filial do último registro processado
    Local cPicCGC           As Character
    Local cPicCEP           As Character
    Local cPicIE            As Character
    Local nAux              As Numeric   // Contador auxiliar
    Local nTamDep           As Numeric   // Tamanho do array de dependentes
    Local nX                As Numeric   // Contador auxiliar para dependentes
    Local nLength           As Numeric   // Contador de dependentes

    // Inicialização das variáveis locais
    cQuery          := ""
    cAlias          := ""
    aSituacoes      := {}
    aCategorias     := {}
    cFilParDe       := ""
    cFilParAte      := ""
    cCcDePar        := ""
    cCcAtePar       := ""
    cMatDePar       := ""
    cMatAtePar      := ""
    nVias           := 1

    cPicCGC         := Alltrim(X3Picture("A1_CGC"))
    cPicCEP         := Alltrim(X3Picture("A1_CEP"))
    cPicIE          := Alltrim(X3Picture("A1_INSCR"))

    cValidFil       := fValidFil()
    cAcessaSRA      := &("{|| " + ChkRH("GPER440", "SRA", "2") + "}")
    jParams         := oFilter:getParameters()
    oStatement      := NIL
    nParamOrder     := 1
    cFilExec        := AllTrim(FwFilialName())
    cEmpExec        := AllTrim(FwEmpName(cEmpAnt))

    jDepBase        := JSONObject():New()
    jNewDep         := JSONObject():New()

    cCustomFields   := ""
    cName           := ""
    aPDFieldsNR     := {}
    aFieldsNR       := {}
    jData           := NIL
    jLGPD           := JSONObject():New()
    nFields         := 1
    aDependentes    := Iif(cPaisLoc == "CHI",{2,3},{1,2})
    
    cFilSRBSRA      := FWJoinFilial("SRB", "SRA")
    
    aInfo           := {}

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {;
        "RA_FILIAL" , "RA_NOME" , "RA_ENDEREC" , "RA_COMPLEM", "RA_BAIRRO" , "RA_MUNICIP" , "RA_ESTADO" , "RA_NUMCP" ,; 
        "RA_NUMCP" , "RA_UFCP" , "RA_SERCP" , "RA_CIC" , "RA_RG" , "RA_MAT" , "RA_CC", "RA_CEP",;
        "RB_NOME", "RB_DTNASC", "RB_LOCNASC", "RB_CARTORI", "RB_NREGCAR", "RB_NUMLIVR", "RB_NUMFOLH",;
        "RB_NUMAT", "RB_DTENTRA", "RB_DTBAIXA";
    }
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    BEGIN SEQUENCE
        // Captura o valor dos parâmetros
        If (jParams != NIL)
            cFilParDe	:= IIf(jParams:hasProperty("PAR_FILIALDE")  .and. Len(jParams["PAR_FILIALDE"])  > 0 .and.!Empty(jParams["PAR_FILIALDE"][1]),    jParams["PAR_FILIALDE"][1], "")
            cFilParAte	:= IIf(jParams:hasProperty("PAR_FILIALATE") .and. Len(jParams["PAR_FILIALATE"]) > 0 .and.!Empty(jParams["PAR_FILIALATE"][1]),   jParams["PAR_FILIALATE"][1],"ZZ")
            cCcDePar	:= IIf(jParams:hasProperty("PAR_CCDE")      .and. Len(jParams["PAR_CCDE"])      > 0 .and.!Empty(jParams["PAR_CCDE"][1]),        jParams["PAR_CCDE"][1],     "")
            cCcAtePar	:= IIf(jParams:hasProperty("PAR_CCATE")     .and. Len(jParams["PAR_CCATE"])     > 0 .and.!Empty(jParams["PAR_CCATE"][1]),       jParams["PAR_CCATE"][1],    "ZZZZZZZZZZ")
            cMatDePar	:= IIf(jParams:hasProperty("PAR_MATDE")     .and. Len(jParams["PAR_MATDE"])     > 0 .and.!Empty(jParams["PAR_MATDE"][1]),       jParams["PAR_MATDE"][1],    "")
            cMatAtePar	:= IIf(jParams:hasProperty("PAR_MATATE")    .and. Len(jParams["PAR_MATATE"])    > 0 .and.!Empty(jParams["PAR_MATATE"][1]),      jParams["PAR_MATATE"][1],   "ZZZZZZ")
            aSituacoes  := IIf(jParams:hasProperty("PAR_SIT"),  jParams["PAR_SIT"], {})
            aCategorias := IIf(jParams:hasProperty("PAR_CAT"),  jParams["PAR_CAT"], {})
            nVias       := IIf(jParams:hasProperty("PAR_NUMVIAS")   .and. Len(jParams["PAR_NUMVIAS"])    > 0 .and. jParams["PAR_NUMVIAS"][1] > 0,      jParams["PAR_NUMVIAS"][1],   1)
        EndIf

        // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aSituacoes)
            aSituacoes[nAux] := IIf(aSituacoes[nAux] == NIL .or. Empty(aSituacoes[nAux]), " ", aSituacoes[nAux])
        Next nAux

        // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
        For nAux := 1 To Len(aCategorias)
            aCategorias[nAux] := IIf(aCategorias[nAux] == NIL .or. Empty(aCategorias[nAux]), " ", aCategorias[nAux])
        Next nAux

		cQuery += " SELECT SRA.RA_FILIAL, SRA.RA_NOME, SRA.RA_ENDEREC, SRA.RA_COMPLEM, SRA.RA_BAIRRO, SRA.RA_MUNICIP, SRA.RA_ESTADO, SRA.RA_NUMCP, SRA.RA_NUMCP, "
        cQuery += " SRA.RA_UFCP, SRA.RA_SERCP, SRA.RA_CIC, SRA.RA_RG, SRA.RA_MAT, SRA.RA_CC, SRA.RA_ADMISSA, SRA.RA_DEMISSA, SRA.RA_CEP, "
        cQuery += " SRB.RB_NOME, SRB.RB_DTNASC, SRB.RB_LOCNASC, SRB.RB_CARTORI, SRB.RB_NREGCAR, SRB.RB_NUMLIVR, SRB.RB_NUMFOLH, "
        cQuery += " SRB.RB_NUMAT, SRB.RB_DTENTRA, SRB.RB_DTBAIXA "
		cQuery += " ? "
		cQuery += " FROM "+RetSQLName("SRA")+" SRA "
		cQuery += " INNER JOIN "+RetSQLName("SRB")+" SRB "
		cQuery += " 	ON "+cFilSRBSRA+" "
		cQuery += "    AND SRB.RB_MAT       = SRA.RA_MAT "
		cQuery += "    AND SRB.RB_TIPSF     IN (?) " // 1
		cQuery += "    AND SRB.RB_GRAUPAR   IN (?) " // 2
		cQuery += "    AND SRB.D_E_L_E_T_   = ? " // 3
		cQuery += " WHERE SRA.RA_FILIAL 	BETWEEN ? AND ? " // 4 e 5
		cQuery += "   AND SRA.RA_MAT        BETWEEN ? AND ? " // 6 e 7
		cQuery += "   AND SRA.RA_CC         BETWEEN ? AND ? " // 8 e 9
		cQuery +=     IIf(Len(aSituacoes)   > 0, " AND RA_SITFOLH IN (?) ", " ") // 10
		cQuery +=     IIf(Len(aCategorias)  > 0, " AND RA_CATFUNC IN (?) ", " ") // 11
		cQuery += "   AND SRA.D_E_L_E_T_    = ?" // 12
		cQuery += "   ORDER BY SRA.RA_FILIAL, SRA.RA_MAT "

		cQuery := ChangeQuery(cQuery)

		// Instancia a classe
		oStatement := FwExecStatement():New(cQuery)

		// Captura e define os campos personalizados das tabelas SRA
		cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA"})
		oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, ""))
        
        oStatement:SetIn(nParamOrder++, aDependentes) // 1
        
        oStatement:SetIn(nParamOrder++, {"F","E"}) // 2

		oStatement:SetString(nParamOrder++, " ") // 3

		oStatement:SetString(nParamOrder++, cFilParDe) // 4
		oStatement:SetString(nParamOrder++, cFilParAte) // 5
		oStatement:SetString(nParamOrder++, cMatDePar) // 6
		oStatement:SetString(nParamOrder++, cMatAtePar) // 7
		oStatement:SetString(nParamOrder++, cCCDePar) // 8
		oStatement:SetString(nParamOrder++, cCCAtePar) // 9
		If (Len(aSituacoes) > 0)
			oStatement:SetIn(nParamOrder++, aSituacoes) // 10
		EndIf
		If (Len(aCategorias) > 0)
			oStatement:SetIn(nParamOrder++, aCategorias) // 11
		EndIf

		oStatement:SetString(nParamOrder++, " ") // 12

		// Executa a query e retorna o alias criado
		cAlias := oStatement:OpenAlias()

		cChave := ""

        cFilAntSv := ""

        jDepBase := JSONObject():New()
        jDepBase["depNome"]       := ""
        jDepBase["depDtNasc"]     := Nil
        jDepBase["depLcNasc"]     := ""
        jDepBase["depCartorio"]   := ""
        jDepBase["depNRegCart"]   := ""
        jDepBase["depNumLivro"]   := ""
        jDepBase["depNumFol"]     := ""
        jDepBase["depNumMat"]     := ""
        jDepBase["depDtEntra"]    := Nil
        jDepBase["depDtBaixa"]    := Nil

		While (!(cAlias)->(EOF()))
			
			//Consiste Filiais e Acessos
			If (cAlias)->RA_FILIAL != cFilAntSv
				If !fInfo(@aInfo,(cAlias)->RA_FILIAL)
					Exit
				Endif
				dbSelectArea(cAlias)
				cFilAntSv := (cAlias)->RA_FILIAL
			EndIf
			
			If !((cAlias)->RA_FILIAL $ cValidFil) .Or. !Eval(cAcessaSRA)
				(cAlias)->( dbSkip() )
				Loop
			EndIf

			If !Empty(cChave)
                For nTamDep := Len(jData["dependentes"]) To 9
                    aAdd(jData["dependentes"],jDepBase)
                Next nTamDep

                // Adiciona os campos customizados no JSON
                GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)
                
                For nX := 1 To nVias
                    self:appendData(jData)
                Next nX

			Endif
			
			cChave := (cAlias)->RA_FILIAL + (cAlias)->RA_MAT

			jData := JSONObject():New()
			jData["FilialExec"]     := Trim(cFilExec)
			jData["EmpresaExec"]    := Trim(cEmpExec)
            jData["EmpNome"]        := Trim(aInfo[3])
            jData["EmpEndereco"]    := Trim(aInfo[4])
            jData["EmpCidade"]      := Trim(aInfo[5])
            jData["EmpUF"]          := Trim(aInfo[6])
            jData["EmpBairro"]      := Trim(aInfo[13])
            jData["EmpCEP"]         := Iif(cPaisLoc == "BRA", Transform(Left(aInfo[7],8),'@R #####-###'), Transform(Left(aInfo[7],8), cPicCEP))
            jData["EmpCGC"]         := Iif(cPaisLoc == "BRA", Left(If(Len(aInfo) >= 27 .And. !Empty( aInfo[27]), aInfo[27], aInfo[8]),14), Transform(Left(aInfo[8],14),cPicCGC))
            jData["EmpIE"]          := Iif(cPaisLoc == "BRA", Transform(Val(aInfo[9]),'@E 99,999,999,999,999'), Transform(Val(aInfo[9]), cPicIE))

            jData["FuncFil"]        := Trim(Iif(jLGPD["RA_FILIAL"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),    (cAlias)->RA_FILIAL)) 
            jData["FuncNome"]       := Trim(Iif(jLGPD["RA_NOME"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),      Left((cAlias)->RA_NOME,25))) 
            jData["FuncEndereco"]   := Trim(Iif(jLGPD["RA_ENDEREC"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_ENDEREC),   Left((cAlias)->RA_ENDEREC,30) + ' ' + Left((cAlias)->RA_COMPLEM,3)))
            jData["FuncBairro"]     := Trim(Iif(jLGPD["RA_BAIRRO"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_BAIRRO),    (cAlias)->RA_BAIRRO)) 
            jData["FuncCidade"]     := Trim(Iif(jLGPD["RA_MUNICIP"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MUNICIP),   (cAlias)->RA_MUNICIP)) 
            jData["FuncUF"]         := Trim(Iif(jLGPD["RA_ESTADO"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_ESTADO),    (cAlias)->RA_ESTADO)) 
            jData["FuncCEP"]        := Trim(Iif(jLGPD["RA_NUMCP"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NUMCP), Iif(cPaisLoc == "BRA", Transform(Left((cAlias)->RA_CEP,8),'@R #####-###'),Transform(Left((cAlias)->RA_CEP,8), cPicCEP))))
            jData["FuncCTPS"]       := Trim(Iif(jLGPD["RA_NUMCP"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NUMCP),     (cAlias)->RA_NUMCP)) 
            jData["FuncUFCTPS"]     := Trim(Iif(jLGPD["RA_UFCP"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_UFCP),      (cAlias)->RA_UFCP)) 
            jData["FuncSerie"]      := Trim(Iif(jLGPD["RA_SERCP"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_SERCP),     (cAlias)->RA_SERCP)) 
            jData["FuncCIC"]        := Trim(Iif(jLGPD["RA_CIC"],        FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CIC),       (cAlias)->RA_CIC)) 
            jData["FuncRG"]         := Trim(Iif(jLGPD["RA_RG"],         FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_RG),        (cAlias)->RA_RG)) 
            jData["FuncMAT"]        := Trim(Iif(jLGPD["RA_MAT"],        FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),       (cAlias)->RA_MAT)) 
            jData["FuncCC"]         := Trim(Iif(jLGPD["RA_CC"],         FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),        (cAlias)->RA_CC)) 
            jData["FuncAdmissa"]    := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_ADMISSA)
            jData["FuncDemissa"]    := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RA_DEMISSA)
            jData["dependentes"]    := {}

            nLength := 0

			While !(cAlias)->(EOF()) .AND. cChave == (cAlias)->RA_FILIAL + (cAlias)->RA_MAT

                jNewDep := JSONObject():New()
                jNewDep["depOrdenacao"]  := StrZero(nLength+1,2)
                jNewDep["depNome"]       := (cAlias)->RB_NOME
                jNewDep["depDtNasc"]     := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RB_DTNASC)
                jNewDep["depLcNasc"]     := Iif(jLGPD["RB_LOCNASC"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_LOCNASC), (cAlias)->RB_LOCNASC)
                jNewDep["depCartorio"]   := Iif(jLGPD["RB_CARTORI"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_CARTORI), (cAlias)->RB_CARTORI)
                jNewDep["depNRegCart"]   := Iif(jLGPD["RB_NREGCAR"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_NREGCAR), (cAlias)->RB_NREGCAR)
                jNewDep["depNumLivro"]   := Iif(jLGPD["RB_NUMLIVR"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_NUMLIVR), (cAlias)->RB_NUMLIVR)
                jNewDep["depNumFol"]     := Iif(jLGPD["RB_NUMFOLH"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_NUMFOLH), (cAlias)->RB_NUMFOLH)
                jNewDep["depNumMat"]     := Iif(jLGPD["RB_NUMAT"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_NUMAT), (cAlias)->RB_NUMAT)
                jNewDep["depDtEntra"]    := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RB_DTENTRA)
                jNewDep["depDtBaixa"]    := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RB_DTBAIXA)

				Aadd(jData['dependentes'], jNewDep)
				nLength += 1

				(cAlias)->( dbSkip() )
			End
		End

		If !Empty(cChave)
            For nTamDep := Len(jData["dependentes"]) To 9
                aAdd(jData["dependentes"],jDepBase)
            Next nTamDep

            // Adiciona os campos customizados no JSON
            GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

			self:appendData(jData)
		Endif

    END SEQUENCE

    // Fecha a área da tabela temporária
    If !Empty(cAlias)
        (cAlias)->(DBCloseArea())
    Endif

    // Limpa os objetos da memória
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeObj(jDepBase)     
    FwFreeObj(jNewDep)
    FwFreeObj(jLGPD)
    FwFreeObj(jData)
    FwFreeArray(aSituacoes)
    FwFreeArray(aCategorias)
    FwFreeArray(aInfo)
    FwFreeArray(aAllFields)
Return self:oData

/*/{Protheus.doc} GPER440TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2510
    @author caio.kretzer
    @since 10/07/2025
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER440TReportsBusinessObject
    // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_FILIALDE",       STR0004,        "string", .F.)
    self:oSchema:addParameter("PAR_FILIALATE",      STR0005,        "string", .F.)
    self:oSchema:addParameter("PAR_CCDE",           STR0006,        "string", .F.)
    self:oSchema:addParameter("PAR_CCATE",          STR0007,        "string", .F.)
    self:oSchema:addParameter("PAR_MATDE",          STR0008,        "string", .F.)
    self:oSchema:addParameter("PAR_MATATE",         STR0009,        "string", .F.)
    self:oSchema:addParameter("PAR_SIT",            STR0010,        "string", .T.)
    self:oSchema:addParameter("PAR_CAT",            STR0011,        "string", .T.)
    self:oSchema:addParameter("PAR_NUMVIAS",        STR0012,        "number", .F.)

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("PAR_FILIALDE",       "/api/rh/smartview/v1/options/GPEParams/getBranches",                                     2)
    self:setCustomURL("PAR_FILIALATE",      "/api/rh/smartview/v1/options/GPEParams/getBranches",                                     2)
    self:setCustomURL("PAR_CCDE",           "/api/framework/v1/genericLookupService/smartview/CTT",                                   2)
    self:setCustomURL("PAR_CCATE",          "/api/framework/v1/genericLookupService/smartview/CTT",                                   2)
    self:setCustomURL("PAR_MATDE",          "/api/framework/v1/genericLookupService/smartview/SRA",                                   2)
    self:setCustomURL("PAR_MATATE",         "/api/framework/v1/genericLookupService/smartview/SRA",                                   2)
    self:setCustomURL("PAR_SIT",            "/api/rh/smartview/v1/options/GPEParams/getSX5/31",                                       2)
    self:setCustomURL("PAR_CAT",            "/api/rh/smartview/v1/options/GPEParams/getSX5/28",                                       2)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",      STR0013, "string",  STR0013,    STR0013, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpresaExec",     STR0014, "string",  STR0014,    STR0014, NIL, NIL, NIL, .F.) 
    self:oSchema:addProperty("EmpNome",         STR0015, "string",  STR0015,    STR0015, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpCidade",       STR0016, "string",  STR0016,    STR0016, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpEndereco",     STR0017, "string",  STR0017,    STR0017, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpUF",           STR0050, "string",  STR0050,    STR0050, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpBairro",       STR0018, "string",  STR0018,    STR0018, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpCEP",          STR0019, "string",  STR0019,    STR0019, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpCGC",          STR0020, "string",  STR0020,    STR0020, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpIE",           STR0021, "string",  STR0021,    STR0021, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncFil",         STR0022, "string",  STR0022,    STR0022, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncNome",        STR0023, "string",  STR0023,    STR0023, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncEndereco",    STR0024, "string",  STR0024,    STR0024, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncBairro",      STR0025, "string",  STR0025,    STR0025, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncCidade",      STR0026, "string",  STR0026,    STR0026, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncUF",          STR0027, "string",  STR0027,    STR0027, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncCEP",         STR0028, "string",  STR0028,    STR0028, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncCTPS",        STR0029, "string",  STR0029,    STR0029, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncUFCTPS",      STR0030, "string",  STR0030,    STR0030, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncSerie",       STR0031, "string",  STR0031,    STR0031, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncCIC",         STR0032, "string",  STR0032,    STR0032, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncRG",          STR0033, "string",  STR0033,    STR0033, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncMAT",         STR0034, "string",  STR0034,    STR0034, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncCC",          STR0035, "string",  STR0035,    STR0035, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncAdmissa",     STR0036, "date",    STR0036,    STR0036, NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("FuncDemissa",     STR0037, "date",    STR0037,    STR0037, NIL, NIL, NIL, .F.)

    aFields := {}
    AAdd(aFields, {"depOrdenacao",  STR0049,                "string",   STR0049})
    AAdd(aFields, {"depNome",       STR0038,                "string",   STR0038})
    AAdd(aFields, {"depDtNasc",     STR0039,                "date",     STR0039})
    AAdd(aFields, {"depLcNasc",     STR0040,                "string",   STR0040})
    AAdd(aFields, {"depCartorio",   STR0041,                "string",   STR0041})
    AAdd(aFields, {"depNRegCart",   STR0042,                "string",   STR0042})
    AAdd(aFields, {"depNumLivro",   STR0043,                "string",   STR0043})
    AAdd(aFields, {"depNumFol",     STR0044,                "string",   STR0044})
    AAdd(aFields, {"depNumMat",     STR0045,                "string",   STR0045})
    AAdd(aFields, {"depDtEntra",    STR0046,                "date",     STR0046})
    AAdd(aFields, {"depDtBaixa",    STR0047,                "date",     STR0047})
    self:addNestedProperty("dependentes", STR0048, STR0048, NIL, aFields)

Return self:oSchema
