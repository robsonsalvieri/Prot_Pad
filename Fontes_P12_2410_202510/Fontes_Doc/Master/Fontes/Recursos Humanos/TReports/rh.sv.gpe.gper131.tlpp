#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.sv.gpe.gper131.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA,SRF,SRH", customTables = "SRA,SRF,SRH", name="Solicitação da 1ª parcela do 13º salário", country="ALL", initialRelease="12.1.2310")
class GPER131TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getWherePar() as character
    private method geraREQ() as variant

    @Get("/api/rh/smartview/v1/options/GPER131/:param")
    Public Method getComboBox() as variant
endclass
 
method new() as object class GPER131TReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Solicitação da 1ª parcela do 13º salário"
return self
 
method getDescription() as character class GPER131TReportsBusinessObject
return STR0003 //"Objeto contendo informações da solicitação da 1ª parcela do 13º salário"
 
method getAreas() as array class GPER131TReportsBusinessObject
return {STR0002} //"RH"
 
method getData(nPage as numeric, oFilter as object) as object class GPER131TReportsBusinessObject
local aInfo as array
local aParams as array
local aAllFields as array     // Estrutura de todos os campos do ON
local aPDFields as array     // Campos que podem ser exibidos de acordo com a LGPD
local cAlias as character
local cDescCTT as character
local cQuery as character
local cLastFil as character
local cDescDep as character
local cValidFil as character
local cAcessaSRA as character
local cCustomFields as character // Campos customizados no formato SQL
local cLastCC as character
local cLastDep as character
local cWhere as character
local cOrdem as character
local cNome as character
local cDtIni as character
local cDtFim as character
local dDtIni as date 
local dDtFim as date
local dDtDe as date 
local dDtAte as date 
local nVias as numeric
local nI as numeric
local nPOrder as numeric
local jParams as json
local jData as json // Informações a serem adicionadas no ON
local lTAE as logical
local lObfuscated as logical   // Flag que indica se tem algum campo para ofuscar

Private lGp1033 as logical

Static oStatement
Static __cEmpAnt := cEmpAnt 
Static __cWhere  := ""

jParams    := oFilter:getParameters() 
cOrdem     := "SRA.RA_FILIAL, SRA.RA_MAT"
cAcessaSRA := &( " { || " + ChkRH( "GPER130" , "SRA" , "2" ) + " } " )
cValidFil  := fValidFil()
nVias      := 1
nPOrder    := 18
aParams    := {"IMPDEM", "FILDE", "FILATE", "MATDE", "MATATE", "CCDE", "CCATE", "NOMEDE", "NOMEATE"}
aAllFields := {}
aPDFields  := {}
dDtDe      := Date()
dDtAte     := Date()+30

// Verifica se precisa fazer o tratamento para LGPD
aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
aAllFields  := self:getStructFields()

//Tratamento para verificar se chamad foi do GPER1033 e se esta enviando para o TAE
If !Empty(jParams['IMPDEM', 1]) .And. Len(jParams['IMPDEM', 1]) > 1
    lGp1033 := .T.
    lTAE := SubStr(jParams['IMPDEM', 1], 2, 1) == "2"
    jParams['IMPDEM', 1] := SubStr(jParams['IMPDEM', 1], 1, 1)
EndIf

cWhere := self:getWherePar(jParams)  

If !Empty(jParams['ORDER', 1])
    cOrdem := Iif(jParams['ORDER', 1] == "1", "SRA.RA_FILIAL, SRA.RA_MAT", Iif(jParams['ORDER', 1] == "2", "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_MAT", ;
                Iif(jParams['ORDER', 1] == "3", "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_NOME", "SRA.RA_FILIAL, SRA.RA_NOME, SRA.RA_MAT" )))
EndIf

If !Empty(jParams['NVIAS', 1]) .And. jParams['NVIAS', 1] > 0
    nVias := jParams['NVIAS', 1] 
EndIf

If !Empty(jParams['CPERDE', 1]) .And. !Empty(jParams['CPERATE', 1])
    dDtDe  := SToD(StrTran(SubStr(jParams['CPERDE', 1], 1, 10), "-" ))
    dDtAte := SToD(StrTran(SubStr(jParams['CPERATE', 1], 1, 10), "-" ))
EndIf 

cQuery := "SELECT SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_CC, SRA.RA_DEPTO, SRA.RA_NOMECMP, SRA.RA_NSOCIAL, "
cQuery += "SRF.RF_DATABAS, SRF.RF_DATAFIM, SRF.RF_PERC13S, SRF.RF_PER13S2, SRF.RF_PER13S3, "
cQuery += "SRH.RH_DATABAS, SRH.RH_DBASEAT, SRH.RH_DFERIAS, SRH.RH_PERC13S "
cQuery += " ? "
cQuery += "FROM " + RetSqlName("SRA") + " SRA "
cQuery += "LEFT JOIN " + RetSqlName("SRF") + " SRF ON SRF.RF_FILIAL = SRA.RA_FILIAL AND SRF.RF_MAT = SRA.RA_MAT AND SRF.RF_STATUS = ? AND ( RF_PERC13S > ? OR  SRF.RF_PER13S2 > ? OR  SRF.RF_PER13S3 > ?) AND ((SRF.RF_DATAINI BETWEEN ? AND ? ) OR (SRF.RF_DATINI2 BETWEEN ? AND ?) OR (SRF.RF_DATINI3 BETWEEN ? AND ?)) AND SRF.D_E_L_E_T_ = ? "
cQuery += "LEFT JOIN " + RetSqlName("SRH") + " SRH ON SRH.RH_FILIAL = SRA.RA_FILIAL AND SRH.RH_MAT = SRA.RA_MAT AND SRH.RH_PERC13S > ? AND SRH.RH_DATAINI >= ? AND SRH.RH_DATAFIM <= ? AND SRH.D_E_L_E_T_ = ? "
cQuery += "WHERE SRA.D_E_L_E_T_ = ? "

If !EMPTY(cWhere)
    cQuery += cWhere    
EndIf

cQuery += "ORDER BY " + cOrdem

If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(__cWhere == cWhere)
    cQuery     := ChangeQuery(cQuery)
    __cEmpAnt  := cEmpAnt
    __cWhere   := cWhere
    oStatement := FwExecStatement():New(cQuery)
EndIf

// Captura e define os campos personalizados das tabelas SRA SRF SRH
cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRF", "SRH"})
oStatement:SetUnsafe(1, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados
oStatement:SetString(2, "1") //Status
oStatement:SetNumeric(3, 0)  //RF_PERC13S
oStatement:SetNumeric(4, 0)  //RF_PER13S2
oStatement:SetNumeric(5, 0)  //RF_PER13S3
oStatement:setDate(6, dDtDe)
oStatement:setDate(7, dDtAte)
oStatement:setDate(8, dDtDe)
oStatement:setDate(9, dDtAte)
oStatement:setDate(10, dDtDe)
oStatement:setDate(11, dDtAte)
oStatement:SetString(12, " ") //SRF.D_E_L_E_T_
oStatement:SetNumeric(13, 0)  //RH_PERC13S
oStatement:setDate(14, dDtDe)
oStatement:setDate(15, dDtAte)
oStatement:SetString(16, " ") //SRH.D_E_L_E_T_
oStatement:SetString(17, " ") //SRA.D_E_L_E_T_

For nI := 1 To Len(aParams)
    If !Empty(jParams[aParams[nI], 1]) 
        If !aParams[nI] == "IMPDEM"
            If ("RA_FILIAL" $ jParams[aParams[nI] , 1]) .or.;
               ("RA_MAT" $ jParams[aParams[nI] , 1]) .or.;
               ("RA_CC" $ jParams[aParams[nI] , 1]) .or.;
               ("RA_NOME" $ jParams[aParams[nI] , 1]) 
                Loop 
            Else
                oStatement:SetString(nPOrder++, jParams[aParams[nI], 1])
            EndIf
        ElseIf cValToChar(jParams[aParams[nI], 1]) == "2"
            oStatement:SetString(nPOrder++, "D")
        EndIf          
    EndIf
Next nI

cAlias := oStatement:OpenAlias() 

while !(cAlias)->(Eof())

    If cLastFil != (cAlias)->RA_FILIAL
        If !((cAlias)->RA_FILIAL $ cValidFil) .Or. !Eval(cAcessaSRA) 
            (cAlias)->( dbSkip() )
            Loop
        EndIf
        cLastFil := (cAlias)->RA_FILIAL
        cLastCC  := ""
        cLastDep := ""
        fInfo(@aInfo, (cAlias)->RA_FILIAL)
    EndIf

    If Empty((cAlias)->RA_DEPTO) .And. cLastCC != (cAlias)->RA_CC 
        cDescCTT := RTrim(DescCc((cAlias)->RA_CC, (cAlias)->RA_FILIAL))
        cLastCC := (cAlias)->RA_CC 
    ElseIf !Empty((cAlias)->RA_DEPTO) .And. cLastDep != (cAlias)->RA_DEPTO   
        cDescDep := RTrim(FDesc("SQB", (cAlias)->RA_DEPTO, "SQB->QB_DESCRIC",, (cAlias)->RA_FILIAL))
        cLastDep := (cAlias)->RA_DEPTO 
    EndIf 

    If (cAlias)->RH_DFERIAS > 0 .And. (cAlias)->RF_DATABAS <= (cAlias)->RH_DATABAS
        dDtIni := StoD((cAlias)->RH_DATABAS)
        dDtFim := StoD((cAlias)->RH_DBASEAT)
        cDtIni := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RH_DATABAS)
        cDtFim := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RH_DBASEAT)
    Else
        dDtIni := StoD((cAlias)->RF_DATABAS)
        dDtFim := StoD((cAlias)->RF_DATAFIM)
        cDtIni := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RF_DATABAS)
        cDtFim := totvs.framework.treports.date.stringToTimeStamp((cAlias)->RF_DATAFIM)
    EndIf 

    cNome := RTrim(IF(Empty((cAlias)->RA_NSOCIAL), (cAlias)->RA_NOMECMP, (cAlias)->RA_NSOCIAL))

    If lTAE
        self:geraREQ(cAlias, dDtIni, dDtFim)
    EndIf 
    
    For nI := 1 to nVias
        jData := {;
            "BranchCode": (cAlias)->RA_FILIAL,; 
            "EmployeeCode": (cAlias)->RA_MAT,;
            "StartAcquisition": cDtIni,; 
            "EndAcquisition": cDtFim,; 
            "BranchCity": RTrim(aInfo[05]),; 
            "BranchName": RTrim(aInfo[03]),; 
            "CompanyName": RTrim(aInfo[02]),;
            "CostCenterDescription": cDescCTT,; 
            "EmployeeName": cNome,; 
            "DepartmentName": cDescDep;
        }
        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona as informações da linha
        self:oData:appendData(jData)
    Next nI
    
    (cAlias)->(dbSkip())
EndDo

(cAlias)->(DBCloseArea())

// Libera os objetos da memória
jData := NIL
FwFreeObj(jData)

return self:oData
 
method getSchema() as object class GPER131TReportsBusinessObject
    
self:oSchema:addProperty("BranchCode", STR0004, "string", STR0004, "BranchCode",,,, .F.)                       //Codigo da Filial 
self:oSchema:addProperty("EmployeeCode", STR0005, "string", STR0005, "EmployeeCode",,,, .F.)                   //Código da matrícula
self:oSchema:addProperty("StartAcquisition", STR0006, "date", STR0006, "StartAcquisition",,,, .F.)             //Data inicial do período aquisitivo
self:oSchema:addProperty("EndAcquisition", STR0007, "date", STR0007, "EndAcquisition",,,, .F.)                 //Data final do período aquisitivo
self:oSchema:addProperty("BranchCity", STR0008, "string", STR0008, "BranchCity",,,, .F.)                       //Cidade da filial
self:oSchema:addProperty("BranchName", STR0009, "string", STR0009, "BranchName",,,, .F.)                       //Nome da filial
self:oSchema:addProperty("CompanyName", STR0010, "string", STR0010, "CompanyName",,,, .F.)                     //Nome da empresa
self:oSchema:addProperty("CostCenterDescription", STR0011, "string", STR0011, "CostCenterDescription",,,, .F.) //Descrição Centro de Custo
self:oSchema:addProperty("EmployeeName", STR0012, "string", STR0012, "EmployeeName",,,, .F.)                   //Nome do funcionário 
self:oSchema:addProperty("DepartmentName", STR0013, "string", STR0013,"DepartmentName",,,, .F.)                //Descrição do Departamento

self:addParameter("FILDE", STR0014, "string", .F.)   //Filial De ?
self:addParameter("FILATE", STR0015, "string", .F.)  //Filial Até ?
self:addParameter("MATDE", STR0016, "string", .F.)   //Matrícula De ?
self:addParameter("MATATE", STR0017, "string", .F.)  // Matrícula Até ?
self:addParameter("CCDE", STR0018, "string", .F.)    //Centro de Custo De ?
self:addParameter("CCATE", STR0019, "string", .F.)   //Centro de Custo Até 
self:addParameter("CPERDE", STR0020, "date", .F.)    //Período Férias De ?
self:addParameter("CPERATE", STR0021, "date", .F.)   //Período Férias Até ?
self:addParameter("NOMEDE", STR0031, "string", .F.)  //Nome De ?
self:addParameter("NOMEATE", STR0032, "string", .F.) //Nome Até ?
self:addParameter("IMPDEM", STR0022, "string", .F.)  //Imprimi Demitidos ?
self:addParameter("NVIAS", STR0023, "number", .F.)   //Nº de Vias ?
self:addParameter("ORDER", STR0024, "string", .F.)   //Ordem ?

//setCustomURL disponível na LIB 20231121.
If FWLibVersion() >= "20231121"        
    self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
    self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
    self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2) 
    self:setCustomURL("IMPDEM", "/api/rh/smartview/v1/options/GPER131/IMPDEM", 1) 
    self:setCustomURL("ORDER", "/api/rh/smartview/v1/options/GPER131/ORDER", 1) 
EndIf 
return self:oSchema

/*/{Protheus.doc} getWherePar
Metodo que retorna a consulta conforme os parâmetros
@type  Metodo
@author Bruno Costa
@return character
@since 27/02/2025
/*/
method getWherePar( jPar as json) as character class GPER131TReportsBusinessObject
    
Local cRet as character

If(cValToChar(jPar['IMPDEM', 1]) == "2") 
    cRet += " AND SRA.RA_SITFOLH <> ?"
EndIf
If !(lGp1033 .And. "RA_FILIAL" $ jPar['FILDE', 1])
    If !Empty(jPar['FILDE', 1])
        cRet += " AND SRA.RA_FILIAL >= ?"
    EndIf
    If !Empty(jPar['FILATE', 1])
        cRet += " AND SRA.RA_FILIAL <= ?"
    EndIf
Else 
    cRet += " AND " + jPar['FILDE', 1]
EndIf

If !(lGp1033 .And. "RA_MAT" $ jPar['MATDE', 1])
    If !Empty(jPar['MATDE', 1])
        cRet += " AND SRA.RA_MAT >= ?"
    EndIf
    If !Empty(jPar['MATATE', 1])
        cRet += " AND SRA.RA_MAT <= ?"
    EndIf
Else 
    cRet += " AND " + jPar['MATDE', 1]
EndIf

If !(lGp1033 .And. "RA_CC" $ jPar['CCDE', 1])
    If !Empty(jPar['CCDE', 1])
        cRet += " AND SRA.RA_CC >= ?"
    EndIf
    If !Empty(jPar['CCATE', 1])
        cRet += " AND SRA.RA_CC <= ?"
    EndIf
Else 
    cRet += " AND " + jPar['CCDE', 1]
EndIf 

If !(lGp1033 .And. "RA_NOME" $ jPar['NOMEDE', 1])
    If !Empty(jPar['NOMEDE', 1])
        cRet += " AND SRA.RA_NOMECMP >= ?"
    EndIf
    If !Empty(jPar['NOMEATE', 1])
        cRet += " AND SRA.RA_NOMECMP <= ?"
    EndIf
Else 
    cRet += " AND " + jPar['NOMEDE', 1]
EndIf

Return cRet

/*/{Protheus.doc} getComboBox
Metodo que retorna o combobox dos parâmetros
@type  Metodo
@author Bruno Costa
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() as Variant Class GPER131TReportsBusinessObject

local jPath     as JSON     
local jResponse as JSON      
local jItem     as JSON      
local nValues   as Numeric   
Local cParam    As Character 
local aValues   as Array   

jPath     := oRest:getPathParamsRequest()
jResponse := JsonObject():new()
aValues   := {}

If (jPath != NIL)
    cParam := jPath["param"]
EndIf

jResponse["data"] := {}

// Monta o vetor de valores do parâmetro
If cParam == "ORDER"
    AAdd(aValues, EncodeUTF8(STR0025)) //Matrícula
    AAdd(aValues, EncodeUTF8(STR0026)) //C.Custo + Matrícula"
    AAdd(aValues, EncodeUTF8(STR0027)) //C.Custo + Nome"
    AAdd(aValues, EncodeUTF8(STR0028)) //Nome
Else
    AAdd(aValues, EncodeUTF8(STR0029)) //Sim
    AAdd(aValues, EncodeUTF8(STR0030)) //Não  
EndIf 

If (Len(aValues) > 0)
    For nValues := 1 To Len(aValues)
        jItem := {;
            "key":   CValToChar(nValues),;
            "label": aValues[nValues];
        }
        AAdd(jResponse["data"], jItem)
    Next nValues
EndIf

// Define a resposta da requisição
oRest:setResponse(jResponse)
oRest:setStatusCode(200)
oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

// Libera os objetos da memória
FwFreeObj(jPath)
FwFreeObj(jResponse)
FwFreeObj(jItem)
FwFreeArray(aValues)

Return NIL

/*/{Protheus.doc} geraREQ
Metodo para criar o registro na REQ para controle do que enviar no GPER1033
@type  Metodo
@author Leandro Drumond
@return character
@since 27/03/2025
/*/
method geraREQ(cAlias as character, dDtIni as date, dDtFim as date) as variant class GPER131TReportsBusinessObject
local nDoc := "SVGPER131"+(cAlias)->RA_FILIAL+(cAlias)->RA_MAT  as character

DbSelectArea("REQ")
DbSetOrder(1)
RecLock("REQ", !REQ->(DbSeek((cAlias)->(RA_FILIAL + RA_MAT + nDoc))))
REQ->REQ_FILIAL := (cAlias)->RA_FILIAL 
REQ->REQ_MAT	:= (cAlias)->RA_MAT
REQ->REQ_DTINI	:= dDtIni
REQ->REQ_DTFIM 	:= dDtFim
REQ->REQ_NDOC	:= nDoc
REQ->REQ_TPDOC	:= "2"
REQ->REQ_ID 	:= 0
REQ->REQ_DTINTE	:= Date()
REQ->REQ_STATUS	:= "N"

REQ->(MsUnLock())

Return Nil
