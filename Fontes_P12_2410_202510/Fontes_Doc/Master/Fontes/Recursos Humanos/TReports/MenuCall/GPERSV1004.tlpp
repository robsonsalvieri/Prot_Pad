#Include "TOTVS.ch"
#Include "GPERSV1004.CH"

/*/{Protheus.doc} GPERSV1004
    Chamada do objeto de negócios rh.sv.gpe.gper1004 e relatório no Smart View pela função totvs.framework.treports.callTReports
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @return Variant, Retorno nulo pré-fixado
/*/
Function GPERSV1004()

    // Declaração das variáveis locais
    local lSuccess      As Logical
    local oSmartView    As Object
    local lConfirmed    As Logical

    // Inicialização das variáveis
    oSmartView := NIL
    lConfirmed := .F.
    
    // Valida a release do Protheus
    If GetRpoRelease() > "12.1.2210"
        
        // Exibe a tela de configuração de faixas de tarefas
        lConfirmed := GP1004ConfigFaixas()
        
        // Se o usuário confirmou as faixas, prossegue com o SmartView
        If lConfirmed
            // Instancia a classe callSmartView()
            oSmartView := totvs.framework.smartview.callSmartView():new("rh.sv.gpe.gper1004")
            oSmartView:setShowWizard(.T.)
            lSuccess := oSmartView:executeSmartView(.T.)

            // Verifica se ocorreu algum erro na execução do artefato do Smart View
            If !lSuccess
                Conout(oSmartView:getError())
            EndIf  

            // Destrói o objeto e o libera da memória
            oSmartView:destroy()
            FwFreeObj(oSmartView)
        EndIf
    EndIf

Return

/*/{Protheus.doc} GP1004ConfigFaixas
    Tela para configuração das faixas de tarefas do relatório GPER1004
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @return Logical, .T. se confirmado, .F. se cancelado
/*/
Static Function GP1004ConfigFaixas()

    // Declaração das variáveis locais
    Local oDlg          As Object
    Local oGet          As Object
    Local oGroup        As Object
    Local oFont         As Object
    Local nOpca         As Numeric
    Local aAdvSize      As Array
    Local aInfoAdvSize  As Array
    Local aObjCoords    As Array
    Local aObjSize      As Array
    
    // Variáveis para a GetDados
    Private aHeader     As Array
    Private aCols       As Array
    Private aAlter      As Array
    Private nUsado      As Numeric
    Private aRotina     As Array 

    // Valida se a tabela RUG existe
    If !AliasInDic("RUG")
        Help(,, "Atenção",, STR0015 + CRLF + ;
            STR0016 + CRLF + ;
            STR0017, 1, 0)
        Return .F.
    EndIf

    // Inicialização das variáveis
    nOpca           := 0
    
    // Inicializa aRotina (necessário para MSGetDados)
    aRotina := {{ STR0001, "", 0, 1, , .F.},;   //"Pesquisar"
                { STR0002, "", 0, 2},;          //"Visualizar"
                { STR0003, "", 0, 3},;          //"Incluir"
                { STR0004, "", 0, 4},;          //"Alterar"
                { STR0005, "", 0, 5}};          //"Excluir"

    // Monta as Dimensoes dos Objetos
    aAdvSize        := MsAdvSize()
    aInfoAdvSize    := {aAdvSize[1], aAdvSize[2], aAdvSize[3], aAdvSize[4], 0, 0}
    aObjCoords      := {}
    AAdd(aObjCoords, {015, 020, .T., .F.})
    AAdd(aObjCoords, {000, 000, .T., .T.})
    aObjSize        := MsObjSize(aInfoAdvSize, aObjCoords)
    
    // Inicializa as estruturas da GetDados
    fInitHeader()
    fLoadFaixasTarefa()

    While .T.
        nOpca := 0
        
        DEFINE FONT oFont NAME "Arial" SIZE 0, -11 BOLD
        DEFINE MSDIALOG oDlg TITLE OemtoAnsi(STR0006) ; //"Calcular para Tarefas"
            From aAdvSize[7], 0 To aAdvSize[6], aAdvSize[5] OF oMainWnd PIXEL

        @ (aObjSize[1,1]+3), aObjSize[1,2] GROUP oGroup TO (aObjSize[1,3] + 20), (aObjSize[1,4]) OF oDlg PIXEL
        oGroup:oFont := oFont

        @ (aObjSize[1,1]+08), (aObjSize[2,2]+05) SAY STR0007 ; //"Faixas de Tarefas: Configure os percentuais e valores de aumento por faixa."
            SIZE 200, 7 PIXEL FONT oFont COLOR CLR_HBLUE OF oDlg

        oGet := MSGetDados():New(aObjSize[2,1]+20, aObjSize[2,2], aObjSize[2,3], aObjSize[2,4], 3, ;
            "GP1004LinOkSV", "GP1004TudOkSV", "", .T.)

        ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg, ;
            {|| nOpca := 1, If(oGet:TudoOk(), oDlg:End(), nOpca := 0)}, ;
            {|| oDlg:End()})

        If nOpca == 1
            // Salva as faixas na tabela
            If fSaveFaixasTarefa()
                Exit
            EndIf
        Else
            Return .F.
        EndIf
    EndDo

Return .T.

/*/{Protheus.doc} fInitHeader
    Inicializa o aHeader da GetDados das faixas de tarefas
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
/*/
Static Function fInitHeader()

    aHeader := {}
    aAlter  := {}
    nUsado  := 4

    AAdd(aHeader, {STR0008, "TAREFADE",      "@E 999,999,999,999.99", 15, 2, "Positivo()", "", "N", "", ""})
    AAdd(aHeader, {STR0009, "TAREFAATE",     "@E 999,999,999,999.99", 15, 2, "Positivo()", "", "N", "", ""})
    AAdd(aHeader, {STR0010, "TAREFAPERC",    "@E 999.999999",         10, 6, "Positivo()", "", "N", "", ""})
    AAdd(aHeader, {STR0011, "TAREFAVAL",     "@E 999,999,999.9999",   12, 4, "Positivo()", "", "N", "", ""})

    aAlter := {"TAREFADE", "TAREFAATE", "TAREFAPERC", "TAREFAVAL"}

Return

/*/{Protheus.doc} fLoadFaixasTarefa
    Carrega as faixas de tarefas da tabela RUG
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
/*/
Static Function fLoadFaixasTarefa()

    Local cCodUltimo As Character
    Local aArea      As Array
    
    // Salva área atual
    aArea := GetArea()
    aCols := {}
    
    // Busca o último código inserido pelo usuário atual
    cCodUltimo := PADR(fGetLastUserCodeTarefa(), TAMSX3("RUG_CODIGO")[1])

    If !Empty(cCodUltimo)
        // Garante que a área RUG está aberta
        DbSelectArea("RUG")
        RUG->(DbSetOrder(1)) // RUG_FILIAL + RUG_CODIGO + RUG_ITEM
        
        If RUG->(DbSeek(xFilial("RUG") + cCodUltimo))
            While RUG->(!Eof()) .And. RUG->RUG_FILIAL == xFilial("RUG") .And. ;
                  RUG->RUG_CODIGO == cCodUltimo .And. RUG->RUG_ORIGEM == "2"
                
                // Adiciona a faixa ao aCols
                AAdd(aCols, {RUG->RUG_FAIXAD, RUG->RUG_FAIXAA, RUG->RUG_PERCEN, RUG->RUG_VALOR, .F.})
                
                RUG->(DbSkip())
            EndDo
        EndIf
    EndIf
    
    // Se não encontrou dados, inicializa com linha em branco
    If Len(aCols) == 0
        AAdd(aCols, {0.01, 0.00, 0.00, 0.00, .F.})
    EndIf

    // Restaura área
    RestArea(aArea)

Return

/*/{Protheus.doc} fGetLastUserCodeTarefa
    Busca o último código inserido pelo usuário atual para tarefas
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @return Character, Último código UUID utilizado pelo usuário
/*/
Static Function fGetLastUserCodeTarefa()

    Local cQuery        As Character
    Local cCodigo       As Character
    Local aArea         As Array
    Local oStatement    As Object
    Local cAlias        As Character
    
    // Salva área atual
    aArea   := GetArea()
    cCodigo := ""
    
    cQuery := " SELECT RUG_CODIGO "
    cQuery += " FROM " + RetSqlName("RUG") + " RUG "
    cQuery += " WHERE RUG.RUG_FILIAL = ? "
    cQuery += "   AND RUG.RUG_USER = ? "
    cQuery += "   AND RUG.RUG_ORIGEM = ? "
    cQuery += "   AND RUG.D_E_L_E_T_ = ? "
    cQuery += " ORDER BY RUG.RUG_DATA DESC, RUG.RUG_HORA DESC "
    
    // Instancia a classe FwExecStatement
    oStatement := FwExecStatement():New(cQuery)
    
    // Define o valor dos bind parameters
    oStatement:SetString(1, xFilial("RUG"))
    oStatement:SetString(2, __cUserID)
    oStatement:SetString(3, "2")
    oStatement:SetString(4, " ")
    
    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()
    
    If (cAlias)->(!Eof())
        cCodigo := AllTrim((cAlias)->RUG_CODIGO)
    EndIf
    
    (cAlias)->(DbCloseArea())
    
    // Limpa o objeto da memória
    FwFreeObj(oStatement)
    
    // Restaura área
    RestArea(aArea)

Return cCodigo

/*/{Protheus.doc} fSaveFaixasTarefa
    Salva as faixas de tarefas na tabela RUG
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @return Logical, .T. se salvou com sucesso
/*/
Static Function fSaveFaixasTarefa()

    Local nI        As Numeric
    Local lRetorno  As Logical
    Local cCodigo   As Character
    Local dDataAtual As Date
    Local cHoraAtual As Character
    Local cItem     As Character
    Local lTemDados As Logical
    Local nItemSeq  As Numeric
    
    lRetorno   := .T.
    lTemDados  := .F.
    dDataAtual := Date()
    cHoraAtual := Time()
    cCodigo    := FwUuidV4() // Gera um UUID único para este conjunto de faixas
    nItemSeq   := 0
    
    Begin Transaction
    
        // Verifica se há dados válidos para gravar
        For nI := 1 To Len(aCols)
            If !aCols[nI, 5] // Se não está deletado
                If aCols[nI, 1] > 0 .Or. aCols[nI, 2] > 0 .Or. aCols[nI, 3] > 0 .Or. aCols[nI, 4] > 0
                    lTemDados := .T.
                    Exit
                EndIf
            EndIf
        Next nI
        
        If !lTemDados
            Help(,, "Atenção",, STR0012, 1, 0) //"É necessário informar pelo menos uma faixa de tarefa válida."
            lRetorno := .F.
        Else
            DbSelectArea("RUG")
            
            // Grava cada faixa na tabela
            For nI := 1 To Len(aCols)
                If !aCols[nI, 5] // Se não está deletado
                    If aCols[nI, 1] > 0 .Or. aCols[nI, 2] > 0 .Or. aCols[nI, 3] > 0 .Or. aCols[nI, 4] > 0
                        
                        nItemSeq++
                        cItem := StrZero(nItemSeq, TAMSX3("RUG_ITEM")[1])
                        
                        RecLock("RUG", .T.)
                        RUG->RUG_FILIAL := xFilial("RUG")
                        RUG->RUG_CODIGO := cCodigo
                        RUG->RUG_ITEM   := cItem
                        RUG->RUG_DATA   := dDataAtual
                        RUG->RUG_HORA   := cHoraAtual
                        RUG->RUG_ORIGEM := "2" // Origem 2 = Tarefas
                        RUG->RUG_USER   := __cUserID
                        RUG->RUG_FAIXAD := aCols[nI, 1]
                        RUG->RUG_FAIXAA := aCols[nI, 2]
                        RUG->RUG_PERCEN := aCols[nI, 3]
                        RUG->RUG_VALOR  := aCols[nI, 4]
                        MsUnlock()
                        
                    EndIf
                EndIf
            Next nI
        EndIf
    
    End Transaction

Return lRetorno

/*/{Protheus.doc} GP1004LinOkSV
    Validação de linha da GetDados
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @return Logical, .T. se linha válida
/*/
Function GP1004LinOkSV()

    Local lRet      As Logical
    Local nFxIni    As Numeric
    Local nFxFim    As Numeric

    lRet   := .T.
    nFxIni := aCols[n, 1]
    nFxFim := aCols[n, 2]

    // Desconsidera faixas deletadas
    If aCols[n, 5] == .F.
        If aCols[n, 1] > aCols[n, 2]
            Help(" ", 1, "GR200FAIXA")
            lRet := .F.
            Return lRet
        EndIf

        If n > 1
            If (aCols[n, 1] <= aCols[n-1, 2]) .And. !aCols[n-1, 5]
                Help(,, "Atenção",, STR0013 + CRLF + ;
                    STR0014, 1, 0)
                lRet := .F.
                Return lRet
            EndIf
        EndIf

        If (aCols[n, 3] = 0) .And. (aCols[n, 4] = 0)
            Help(" ", 1, "GR200ZEROS")
            lRet := .F.
            Return lRet
        EndIf
    EndIf

Return lRet

/*/{Protheus.doc} GP1004TudOkSV
    Validação geral da GetDados
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @return Logical, .T. se válido
/*/
Function GP1004TudOkSV()
Return .T.
