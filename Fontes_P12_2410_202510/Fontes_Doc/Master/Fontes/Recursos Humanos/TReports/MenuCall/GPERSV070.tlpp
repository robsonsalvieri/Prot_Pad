#INCLUDE "PROTHEUS.CH"    
#Include "FWBROWSE.CH"
#INCLUDE "FWLIBVERSION.CH"
#include "gpersv070.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} GPERSV070

Chamada do objeto de negócios rh.sv.gper070, 
@author  Bruno Costa
@since   28/10/2024
@version 1.0
/*/
Function GPERSV070()

    If( lCfgSV() )
        
        fCallProvSV()

    EndIf    

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} lCfgSV
@author			Bruno Costa
@since			28/10/2024
@version		1.0
/*/
//---------------------------------------------------------------------
Static Function lCfgSV()

    local nOpc  	as numeric
	local lRet	    as logical
	
	If GetRpoRelease() >= "12.1.2310" .AND. FWLibVersion() >= "20240226" 
       
        If totvs.framework.smartview.util.isConfig()
			lRet := .T.
        Else
            nOpc := Aviso( STR0003, STR0004 + CRLF + CRLF +; //##"Smart View!" ##"Não existe configuração do Smart View."
								STR0005 + CRLF + CRLF +; //"Para atualização dessa funcionalidade, será necessário instalar e configurar o Smart View."
								STR0006, { STR0007, STR0008 }, 3 ) //##"Siga as instruções da documentação." ##"Sair" ##"Abrir"

			If nOpc == 2 //Doc
				ShellExecute( "Open", "https://tdn.totvs.com/pages/releaseview.action?pageId=626636542", "", "", 1 ) 
			EndIf
        EndIf           
	Else
		Aviso( STR0001, STR0002, { STR0007 }, 2 ) //##"Rotina indisponível" ##"Esta funcionalidade está disponível apenas para a versão igual ou superior a 12.1.2310 e LIB igual ou superior a 20240226." ##"Sair"
	EndIf

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} fCallProvSV
Função para abrir o relatório/visão de dados da provisão de férias no smart view 
@author	 Bruno Costa
@since	 28/10/2024
@version 1.0
/*/
//---------------------------------------------------------------------
Function fCallProvSV()
local aOns     := {} as array
local aColumns := {} as array
local lRet     := .T. as logical     
local oBrowse as Object
local oModal as Object
local oPanelGrid as object
Local bOk     := {||lRet := .T., oModal:oOwner:End()}           
Local bCancel := {||lRet := .F., oModal:oOwner:End()}

AAdd(aColumns, FWBrwColumn():New())
aColumns[1]:SetData( &("{||oBrowse:oData:aArray[oBrowse:At(), 1]}") )
aColumns[1]:SetTitle(STR0011) //"Descrição"
aColumns[1]:SetSize(40)
aColumns[1]:SetType("C")
aColumns[1]:SetPicture("@!")	

AAdd(aColumns, FWBrwColumn():New())
aColumns[2]:SetData( &("{||oBrowse:oData:aArray[oBrowse:At(), 2]}") )
aColumns[2]:SetTitle(STR0012) //"Tipo"
aColumns[2]:SetSize(15)
aColumns[2]:SetType("C")
aColumns[2]:SetPicture("@!")

aAdd(aOns, {STR0013, STR0014}) //"Relatório Provisão de Férias/13º Salário"###"Relatório"
aAdd(aOns, {STR0015, STR0016}) //"Provisão de Férias/13º Salário"###"Visão de Dados" 

oModal := FWDialogModal():New()

oModal:SetFreeArea(300, 105)
oModal:setTitle(STR0017) //"Smart View - Escolha o recurso para execução"
oModal:SetEscClose(.F.)
oModal:createDialog()
oModal:AddButton(STR0018, bOk, STR0018,, .T., .F., .T., {|| .T.}) //Confirmar
oModal:AddButton(STR0019, bCancel, STR0019,, .T., .F., .T.,)      //Fechar

oPanelGrid		 :=	TPanel():New(0, 0, "", oModal:GetPanelMain(),, .F., .F.,,, 0, 0, .T., .F.)
oPanelGrid:Align := CONTROL_ALIGN_ALLCLIENT
oBrowse          := FWBrowse():New() 

oBrowse:SetOwner(oPanelGrid)
oBrowse:SetDataArray()
oBrowse:DisableFilter()
oBrowse:DisableConfig()
oBrowse:DisableReport()
oBrowse:SetColumns(aColumns) 
oBrowse:SetArray(aOns)
oBrowse:Activate()
oModal:Activate()

If lRet .And. Len(aOns) > 0

    If FwNoAccent(aOns[oBrowse:nAt][2]) == FwNoAccent(STR0014) //Relatorio
        oSmartView := totvs.framework.smartview.callSmartView():new("rh.sv.gpe.gper070.rel.rep", "report")
        oSmartView:setParam("TPPROV", "1")
        oSmartView:setParam("TPFORMA", "1")
        oSmartView:setParam("TPRELA", "1")
        oSmartView:setParam("ORDER", "1")
    Else
        oSmartView := totvs.framework.smartview.callSmartView():new("rh.sv.gpe.gper090.visao.dg", "data-grid")
        oSmartView:setParam("TPPROV", "1")
        oSmartView:setParam("ORDER", "1")
    EndIf

    If !oSmartView:executeSmartView(.T.)
        //"Smart View!"###"Não foi possível abrir o Smart View."###"Verifique se o servidor REST está no ar ou contate o administrador do sistema."
        Help( " ", 1, OemToAnsi(STR0003), Nil, OemToAnsi(STR0009), 1, 0, Nil, Nil, Nil, Nil, Nil, { OemToAnsi(STR0010) } )
    EndIf  

    oSmartView:destroy()
    
EndIf

Return 
