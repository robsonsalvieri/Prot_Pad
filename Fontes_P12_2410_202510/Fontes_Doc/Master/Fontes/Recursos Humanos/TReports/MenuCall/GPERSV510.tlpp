#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWLIBVERSION.CH"
#include "gpersv510.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} GPERSV510

Chamada do objeto de negócios rh.sv.gper510, para abrir o relátorio e a visão de dados no Smart View pela função totvs.framework.treports.callTReports  

@author  Bruno Costa
@since   05/02/20224
@version 1.0
/*/

Function GPERSV510()

    Local lSuccess  As Logical
    Local cError
    local oSmartView as object

    If( lCfgSV() )

        If FWLibVersion() >= "20240226" // Nova classe para execução dos recursos no Protheus
            oSmartView := totvs.framework.smartview.callSmartView():new("rh.sv.gpe.gper510")
            oSmartView:setParam("MV_PAR13", 2)
            oSmartView:setParam("MV_PAR14", 1)
            oSmartView:setParam("MV_PAR15", 2)
            oSmartView:setParam("MV_PAR17", 2)
            oSmartView:setParam("MV_PAR22", 1)
            oSmartView:setParam("MV_PAR23", 1)
            oSmartView:setParam("MV_PAR24", 2)
            oSmartView:setParam("MV_ORDER", 1)
                    
            lSuccess := oSmartView:executeSmartView(.T.)
            If !lSuccess
                Conout(oSmartView:getError())
            EndIf  
 
             oSmartView:destroy()
		Else
            lSuccess := totvs.framework.treports.callTReports("rh.sv.gpe.gper510",,,,,.F.,,.T., @cError)
            If !lSuccess
                Conout(cError)
            EndIf
        EndIF
    EndIf    

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} lCfgSV
@author			Bruno Costa
@since			02/10/2023
@version		1.0
/*/
//---------------------------------------------------------------------
Static Function lCfgSV()

    Local nOpc  	as numeric
    Local lSuccess  As Logical
	Local lRet	    as logical

	lSuccess  := .F.

	If GetRpoRelease() >= "12.1.2210" .AND. FWLibVersion() >= "20231009"
        lSuccess := totvs.framework.smartview.util.isConfig()
        If lSuccess
			lRet := .T.
        Else
            nOpc := Aviso( STR0003, STR0004 + CRLF + CRLF +; //##"Smart View!" ##"Não existe configuração do Smart View."
								STR0005 + CRLF + CRLF +; //"Para atualização dessa funcionalidade, será necessário instalar e configurar o Smart View."
								STR0006, { STR0007, STR0008 }, 3 ) //##"Siga as instruções da documentação." ##"Sair" ##"Abrir"

			If nOpc == 2 //Doc
				ShellExecute( "Open", "https://tdn.totvs.com/pages/releaseview.action?pageId=626636542", "", "", 1 ) 
			EndIf
        EndIf           
	Else
		Aviso( STR0001, STR0002, { STR0007 }, 2 ) //##"Rotina indisponível" ##"Esta funcionalidade está disponível apenas para a versão igual ou superior a 12.1.2210 e LIB igual ou superior a 20231009." ##"Sair"
	EndIf

Return lRet
