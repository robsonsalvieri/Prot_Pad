#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWLIBVERSION.CH"
#include "gpersv080.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} GPERSV080

Chamada do objeto de negócios rh.sv.gper080, 
@author  Bruno Costa
@since   23/05/2024
@version 1.0
/*/

Function GPERSV080()

    local lSuccess  As Logical
    local oSmartView as object

    If( lCfgSV() )
        
        oSmartView := totvs.framework.smartview.callSmartView():new("rh.sv.gpe.gper080")
        oSmartView:setParam("MV_MEDIA", "5")
        oSmartView:setParam("MV_LINHA", "1")
        oSmartView:setParam("MV_DATREF",FwTimeStamp( 5, LastDate(Date()), "00:00:00" ))
        oSmartView:setForceParams(.T.)

        lSuccess := oSmartView:executeSmartView(.T.)
        If !lSuccess
            Conout(oSmartView:getError())
        EndIf  
 
        oSmartView:destroy()
    EndIf    

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} lCfgSV
@author			Bruno Costa
@since			23/05/2024
@version		1.0
/*/
//---------------------------------------------------------------------
Static Function lCfgSV()

    local nOpc  	as numeric
    local lSuccess  As Logical
	local lRet	    as logical
	
	If GetRpoRelease() >= "12.1.2310" .AND. FWLibVersion() >= "20240226" 
        lSuccess := totvs.framework.smartview.util.isConfig()
        If lSuccess
			lRet := .T.
        Else
            nOpc := Aviso( STR0003, STR0004 + CRLF + CRLF +; //##"Smart View!" ##"Não existe configuração do Smart View."
								STR0005 + CRLF + CRLF +; //"Para atualização dessa funcionalidade, será necessário instalar e configurar o Smart View."
								STR0006, { STR0007, STR0008 }, 3 ) //##"Siga as instruções da documentação." ##"Sair" ##"Abrir"

			If nOpc == 2 //Doc
				ShellExecute( "Open", "https://tdn.totvs.com/pages/releaseview.action?pageId=626636542", "", "", 1 ) 
			EndIf
        EndIf           
	Else
		Aviso( STR0001, STR0002, { STR0007 }, 2 ) //##"Rotina indisponível" ##"Esta funcionalidade está disponível apenas para a versão igual ou superior a 12.1.2310 e LIB igual ou superior a 20240226." ##"Sair"
	EndIf

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} FConfigSV
Função para verificar se o smart view está configurado para chamada do relatório nas rotinas de cálculo
@author	 Bruno Costa
@since	 02/07/2024
@version 1.0
/*/
//---------------------------------------------------------------------
Function fConfigSV()

local lRet as logical
    
If GetRpoRelease() >= "12.1.2310" .AND. FWLibVersion() >= "20240226" 
    lRet := totvs.framework.smartview.util.isConfig()
EndIf
    
Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} fCallMediaSV
Função para abrir o relatório de médias no smart view através das rotinas de cálculo
@author	 Bruno Costa
@since	 02/07/2024
@version 1.0
/*/
//---------------------------------------------------------------------
Function fCallMediaSV(cTipo, dRef, dDFim, lTransf)

local lImpSV as logical
local nQtMes as numeric
local cFil as character
local cMat as character

DEFAULT dDFim   := CTOD("//")
DEFAULT lTransf := .F.

    If ( lImpSV := MsgYesNo(OemToAnsi(STR0009), OemToAnsi(STR0010)) ) //"Deseja imprimir médias pelo Smart View?" ### "Atenção"

        If cTipo == "I"
            nQtMes := DateDiffMonth(dRef, dDFim)
            cTipo  := If(nQtMes > 0, cTipo + cValToChar(nQtMes), cTipo)
        EndIF

        cFil := If(lTransf, RJK->RJK_FILIAL, SRA->RA_FILIAL)
        cMat := If(lTransf, RJK->RJK_MAT, SRA->RA_MAT)

        oSmartView := totvs.framework.smartview.callSmartView():new("rh.sv.gpe.gper080.rel.rep", "report")
		oSmartView:setParam("MV_FILDE", cFil, "Hidden")
		oSmartView:setParam("MV_FILATE", cFil, "Hidden")
		oSmartView:setParam("MV_MATDE", cMat, "Hidden")
		oSmartView:setParam("MV_MATATE", cMat, "Hidden")
        oSmartView:setParam("MV_FUNCDE", "", "Hidden")
        oSmartView:setParam("MV_FUNCATE", "", "Hidden")
        oSmartView:setParam("MV_CCDE", "", "Hidden")
        oSmartView:setParam("MV_CCATE", "", "Hidden")
        oSmartView:setParam("MV_DEPDE", "", "Hidden")
        oSmartView:setParam("MV_DEPATE", "", "Hidden")
        oSmartView:setParam("MV_NOMEDE", "", "Hidden")
        oSmartView:setParam("MV_NOMEATE", "", "Hidden")
		oSmartView:setParam("MV_MEDIA", cTipo, "Hidden")
		oSmartView:setParam("MV_LINHA", "1")
		oSmartView:setParam("MV_DATREF", totvs.framework.treports.date.dateToTimeStamp(dRef))
		oSmartView:setForceParams(.T.)
        
		If !oSmartView:executeSmartView(.T.)
            //"Smart View!"###"Não foi possível abrir o Smart View."###"Verifique se o servidor REST está no ar ou contate o administrador do sistema."
	        Help( " ", 1, OemToAnsi(STR0003), Nil, OemToAnsi(STR0011), 1, 0, Nil, Nil, Nil, Nil, Nil, { OemToAnsi(STR0012) } )
		EndIf  
	
		oSmartView:destroy()
    EndIf

Return lImpSV
