#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWLIBVERSION.CH"
#include "gpemsv040ap.ch"

/*/{Protheus.doc} GPERSV040AP
Chamada do aviso prévio indenizado e trabalhado do smart view
@author  Vitor Fernandes Moraes
@since   02/01/2023
@version 1.0
/*/

Function GPEMSV040AP()

    If( lCfgSV() )

        fCallAvisoSV()
    EndIf 

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} lCfgSV
@author			Vitor Fernandes Moraes
@since			02/01/2023
@version		1.0
/*/
//---------------------------------------------------------------------
Static Function lCfgSV()

    Local nOpc  	as numeric
    Local lSuccess  As Logical
	Local lRet	    as logical

	lSuccess  := .F.

	If GetRpoRelease() >= "12.1.2210" .AND. FWLibVersion() >= "20231009"
        lSuccess := totvs.framework.smartview.util.isConfig()
        If lSuccess
			lRet := .T.
        Else
            nOpc := Aviso( STR0003, STR0004 + CRLF + CRLF +; //##"Smart View!" ##"Não existe configuração do Smart View."
								STR0005 + CRLF + CRLF +; //"Para atualização dessa funcionalidade, será necessário instalar e configurar o Smart View."
								STR0006, { STR0007, STR0008 }, 3 ) //##"Siga as instruções da documentação." ##"Sair" ##"Abrir"

			If nOpc == 2 //Doc
				ShellExecute( "Open", "https://tdn.totvs.com/pages/releaseview.action?pageId=626636542", "", "", 1 ) 
			EndIf
        EndIf           
	Else
		Aviso( STR0001, STR0002, { STR0007 }, 2 ) //##"Rotina indisponível" ##"Esta funcionalidade está dispon?vel apenas para a versão igual ou superior a 12.1.2210 e LIB igual ou superior a 20231009." ##"Sair"
	EndIf

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} fCallAvisoSV
Função para abrir a tela com os dois relatórios de aviso Prévio com objetos diferentes
@author	 Bruno Costa
@since	 20/05/2025
@version 1.0
/*/
//---------------------------------------------------------------------
Static Function fCallAvisoSV()
local aOns     := {} as array
local aColumns := {} as array
local lRet     := .T. as logical     
local oBrowse as Object
local oModal as Object
local oPanelGrid as object
Local bOk     := {||lRet := .T., oModal:oOwner:End()}           
Local bCancel := {||lRet := .F., oModal:oOwner:End()}

AAdd(aColumns, FWBrwColumn():New())
aColumns[1]:SetData( &("{||oBrowse:oData:aArray[oBrowse:At(), 1]}") )
aColumns[1]:SetTitle(STR0009) //"Descrição"
aColumns[1]:SetSize(40)
aColumns[1]:SetType("C")
aColumns[1]:SetPicture("@!")	

AAdd(aColumns, FWBrwColumn():New())
aColumns[2]:SetData( &("{||oBrowse:oData:aArray[oBrowse:At(), 2]}") )
aColumns[2]:SetTitle(STR0010) //"Tipo"
aColumns[2]:SetSize(15)
aColumns[2]:SetType("C")
aColumns[2]:SetPicture("@!")

aAdd(aOns, {STR0011, STR0013}) //"Aviso Prévio Indenizado"###"Relatório"
aAdd(aOns, {STR0012, STR0013}) //"Aviso Prévio Trabalhado"###"Relatório" 

oModal := FWDialogModal():New()

oModal:SetFreeArea(300, 105)
oModal:setTitle(STR0014) //"Smart View - Escolha o recurso para execução"
oModal:SetEscClose(.F.)
oModal:createDialog()
oModal:AddButton(STR0015, bOk, STR0015,, .T., .F., .T., {|| .T.}) //Confirmar
oModal:AddButton(STR0016, bCancel, STR0016,, .T., .F., .T.,)      //Fechar

oPanelGrid		 :=	TPanel():New(0, 0, "", oModal:GetPanelMain(),, .F., .F.,,, 0, 0, .T., .F.)
oPanelGrid:Align := CONTROL_ALIGN_ALLCLIENT
oBrowse          := FWBrowse():New() 

oBrowse:SetOwner(oPanelGrid)
oBrowse:SetDataArray()
oBrowse:DisableFilter()
oBrowse:DisableConfig()
oBrowse:DisableReport()
oBrowse:SetColumns(aColumns) 
oBrowse:SetArray(aOns)
oBrowse:Activate()
oModal:Activate()

If lRet .And. oBrowse:nAt > 0

    If oBrowse:nAt == 1 //Aviso Prévio Indenizado
        oSmartView := totvs.framework.smartview.callSmartView():new("rh.sv.gpe.gpem040ai.rel.rep", "report")
    Else
        oSmartView := totvs.framework.smartview.callSmartView():new("rh.sv.gpe.gpem040at.rel.rep", "report")
    EndIf

    //"Smart View!"###"Não foi possível abrir o Smart View."###"Verifique se o servidor REST está no ar ou contate o administrador do sistema."
    If(!oSmartView:executeSmartView(.T.), Help(" ", 1, OemToAnsi(STR0003), Nil, OemToAnsi(STR0017), 1, 0, Nil, Nil, Nil, Nil, Nil, {OemToAnsi(STR0018)}), Nil)

    oSmartView:destroy()
    
EndIf

Return 
