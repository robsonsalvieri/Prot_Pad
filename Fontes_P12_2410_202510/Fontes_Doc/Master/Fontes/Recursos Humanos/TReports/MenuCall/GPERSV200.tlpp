#Include "TOTVS.ch"
#Include "GPERSV200.ch"

/*/{Protheus.doc} GPERSV200
    Chamada do objeto de negócios rh.sv.gpe.gper200 e relatório no Smart View pela função totvs.framework.treports.callTReports
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @return Variant, Retorno nulo pré-fixado
/*/
Function GPERSV200()

    // Declaração das variáveis locais
    local lSuccess      As Logical
    local oSmartView    As Object
    local lConfirmed    As Logical

    // Inicialização das variáveis
    oSmartView := NIL
    lConfirmed := .F.


    If !ChkFile("RUG")
        MsgInfo(OemToAnsi(STR0025)) //"Tabela RUG não encontrada. Execute o UPDDISTR - atualizador de dicionário e base de dados."
        Return
    Endif
    
    // Valida a release do Protheus
    If GetRpoRelease() > "12.1.2210"
        
        // Exibe a tela de configuração de faixas salariais
        lConfirmed := GP200ConfigFaixas()
        
        // Se o usuário confirmou as faixas, prossegue com o SmartView
        If lConfirmed
            // Instancia a classe callSmartView()
            oSmartView := totvs.framework.smartview.callSmartView():new("rh.sv.gpe.gper200")
            oSmartView:setShowWizard(.T.)
            lSuccess := oSmartView:executeSmartView(.T.)

            // Verifica se ocorreu algum erro na execução do artefato do Smart View
            If !lSuccess
                Conout(oSmartView:getError())
            EndIf  

            // Destrói o objeto e o libera da memória
            oSmartView:destroy()
            FwFreeObj(oSmartView)
        EndIf
    EndIf

Return

/*/{Protheus.doc} GP200ConfigFaixas
    Tela para configuração das faixas salariais do relatório GPER200
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @return Logical, .T. se confirmado, .F. se cancelado
/*/
Static Function GP200ConfigFaixas()

    // Declaração das variáveis locais
    Local oGet          As Object
    Local oGroup        As Object
    Local oFont         As Object
    Local oComboCateg   As Object
    Local oPisoMes      As Object
    Local oPisoHora     As Object
    Local oPisoDia      As Object
    Local nOpca         As Numeric
    Local aAdvSize      As Array
    Local aInfoAdvSize  As Array
    Local aObjCoords    As Array
    Local aObjSize      As Array
    Local nPisoMes      As Numeric
    Local nPisoHora     As Numeric
    Local nPisoDia      As Numeric
    Local cCategoria    As Character
    Local cCatMensal    As Character
    Local lGestPubl 	As Logical
    Local aCategItems   As Array
    Local cCatSelected  As Character
    
    // Variáveis para a GetDados
    Private aHeader     As Array
    Private aCols       As Array
    Private aAlter      As Array
    Private nUsado      As Numeric
    Private aRotina     As Array 
    Private oDlg        As Object 

    // Inicialização das variáveis
    nOpca           := 0
    nPisoMes        := 0
    nPisoHora       := 0
    nPisoDia        := 0
    cCategoria      := "M"
    cCatMensal      := "M*C*P*A*E*S*I"
    lGestPubl       := if(ExistFunc("fUsaGFP"),fUsaGFP(),.f.)
    aCategItems     := {}
    cCatSelected    := ""

    IF	lGestPubl //Se Gestao de Folha Publica - SIGAGFP	
        cCatMensal += "*0*1*2*3*4*5*6*7*8*9"
    EndIf
    
    // Carrega as categorias disponíveis
    aCategItems := fGetCategorias()
    
    // Inicializa aRotina (necessário para MSGetDados)
    aRotina := {{ STR0001, "", 0, 1, , .F.},;
                { STR0002, "", 0, 2},;
                { STR0003, "", 0, 3},;
                { STR0004, "", 0, 4},;
                { STR0005, "", 0, 5}}

    // Monta as Dimensoes dos Objetos
    aAdvSize        := MsAdvSize()
    aInfoAdvSize    := {aAdvSize[1], aAdvSize[2], aAdvSize[3], aAdvSize[4], 0, 0}
    aObjCoords      := {}
    AAdd(aObjCoords, {015, 020, .T., .F.})
    AAdd(aObjCoords, {000, 000, .T., .T.})
    aObjSize        := MsObjSize(aInfoAdvSize, aObjCoords)
    
    // Inicializa as estruturas da GetDados
    fInitHeader()
        fLoadFaixas(@nPisoMes, @nPisoHora, @nPisoDia, @cCategoria, aCategItems, @cCatSelected)

    While .T.
        nOpca := 0
        
        DEFINE FONT oFont NAME "Arial" SIZE 0, -11 BOLD
        DEFINE MSDIALOG oDlg TITLE OemtoAnsi(STR0006) ;
            From aAdvSize[7], 0 To aAdvSize[6], aAdvSize[5] OF oMainWnd PIXEL

        @ (aObjSize[1,1]+3), aObjSize[1,2] GROUP oGroup TO (aObjSize[1,3] + 35), (aObjSize[1,4]) OF oDlg PIXEL
        oGroup:oFont := oFont

        @ (aObjSize[1,1]+08), (aObjSize[2,2]+05) SAY STR0007 ;
            SIZE 200, 7 PIXEL FONT oFont COLOR CLR_HBLUE OF oDlg

        // Campo Categoria
        @ (aObjSize[2,1]+06), (aObjSize[2,2]+05) SAY STR0023 ;
            SIZE 55, 07 PIXEL FONT oFont OF oDlg
        @ (aObjSize[2,1]+02), (aObjSize[2,2]+05*10) COMBOBOX oComboCateg VAR cCatSelected ;
            ITEMS aCategItems SIZE 80, 11 OF oDlg PIXEL ;
            VALID GP200CategChange(cCatSelected, @nPisoMes, @nPisoHora, @nPisoDia, @cCategoria, aCategItems, oPisoMes, oPisoHora, oPisoDia)

        // Campo de pisos salariais
        @ (aObjSize[2,1]+21), (aObjSize[2,2]+05) SAY STR0008 ;
            SIZE 55, 07 PIXEL FONT oFont OF oDlg
        @ (aObjSize[2,1]+17), (aObjSize[2,2]+05*10) MSGET oPisoMes VAR nPisoMes ;
            SIZE 57, 11 OF oDlg PIXEL PICTURE "@E 999,999,999.99" ;
            WHEN cCategoria $ cCatMensal VALID GP200Piso(@nPisoMes, @nPisoHora, @nPisoDia, cCategoria, cCatMensal, oPisoMes, oPisoHora, oPisoDia)

        @ (aObjSize[2,1]+21), (aObjSize[2,4]/3+5) SAY STR0009 ;
            SIZE 55, 07 PIXEL FONT oFont OF oDlg
        @ (aObjSize[2,1]+17), (aObjSize[2,4]/3+5*10) MSGET oPisoHora VAR nPisoHora ;
            SIZE 57, 11 OF oDlg PIXEL PICTURE "@E 999,999,999.99" ;
            WHEN cCategoria $ "H*T*G*J" VALID GP200Piso(@nPisoMes, @nPisoHora, @nPisoDia, cCategoria, cCatMensal, oPisoMes, oPisoHora, oPisoDia)

        @ (aObjSize[2,1]+21), (aObjSize[2,4]/3*2+5) SAY STR0010 ;
            SIZE 55, 07 PIXEL FONT oFont OF oDlg
        @ (aObjSize[2,1]+17), (aObjSize[2,4]/3*2+5*10) MSGET oPisoDia VAR nPisoDia ;
            SIZE 57, 11 OF oDlg PIXEL PICTURE "@E 999,999,999.99" ;
            WHEN cCategoria $ "D" VALID GP200Piso(@nPisoMes, @nPisoHora, @nPisoDia, cCategoria, cCatMensal, oPisoMes, oPisoHora, oPisoDia)

        oGet := MSGetDados():New(aObjSize[2,1]+35, aObjSize[2,2], aObjSize[2,3], aObjSize[2,4], 3, ;
            "GP200LinOkSV", "GP200TudOkSV", "", .T.)

        ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg, ;
            {|| nOpca := 1, If(oGet:TudoOk(), oDlg:End(), nOpca := 0)}, ;
            {|| oDlg:End()})

        If nOpca == 1
            // Salva as faixas e piso salarial na tabela
            If fSaveFaixas(nPisoMes, nPisoHora, nPisoDia, cCategoria)
                Exit
            EndIf
        Else
            Return .F.
        EndIf
    EndDo

Return .T.

/*/{Protheus.doc} fGetCategorias
    Busca as categorias da tabela SX5 (tabela 28)
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @return Array, Array com as categorias
/*/
Static Function fGetCategorias()
    Local aCategItems   As Array
    Local cQuery        As Character
    Local cAlias        As Character
    Local oStatement    As Object
    Local nParamOrder   As Numeric
    
    aCategItems := {}
    nParamOrder := 1
    
    cQuery := " SELECT X5_CHAVE, X5_DESCRI "
    cQuery += " FROM " + RetSqlName("SX5") + " SX5 "
    cQuery += " WHERE SX5.X5_FILIAL = ? "
    cQuery += "   AND SX5.X5_TABELA = ? "
    cQuery += "   AND SX5.D_E_L_E_T_ = ? "
    cQuery += "   AND SX5.X5_CHAVE NOT BETWEEN '0' AND '9' "
    cQuery += " ORDER BY SX5.X5_CHAVE "
    
    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)
    
    // Define o valor dos bind parameters
    oStatement:SetString(nParamOrder++, xFilial("SX5"))
    oStatement:SetString(nParamOrder++, "28")
    oStatement:SetString(nParamOrder++, " ")
    
    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()
    
    While (cAlias)->(!Eof())
        AAdd(aCategItems, AllTrim((cAlias)->X5_CHAVE) + " - " + AllTrim((cAlias)->X5_DESCRI))
        (cAlias)->(DbSkip())
    EndDo
    
    (cAlias)->(DbCloseArea())
    FwFreeObj(oStatement)
    
    // Se não encontrou registros, retorna categorias padrão
    If Len(aCategItems) == 0
        AAdd(aCategItems, "M - Mensalista")
        AAdd(aCategItems, "H - Horista")
        AAdd(aCategItems, "D - Diarista")
    EndIf
    
Return aCategItems

/*/{Protheus.doc} GP200CategChange
    Função chamada quando a categoria é alterada
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
/*/
Function GP200CategChange(cCatSelected, nPisoMes, nPisoHora, nPisoDia, cCategoria, aCategItems, oPisoMes, oPisoHora, oPisoDia)
    
    Local cCatMensal As Character
    Local nPos       As Numeric
    Local cCatAux    As Character
    
    cCatMensal := "M*C*P*A*E*S*I"
    
    // Se cCatSelected é string, procura a posição no array
    If ValType(cCatSelected) == "C" .And. !Empty(cCatSelected)
        nPos := AScan(aCategItems, {|x| AllTrim(x) == AllTrim(cCatSelected)})
        
        If nPos > 0
            cCatAux := SubStr(aCategItems[nPos], 1, 1)
            cCategoria := cCatAux
            
            // Recalcula os pisos baseado na nova categoria
            GP200Piso(nPisoMes, nPisoHora, nPisoDia, cCatAux, cCatMensal, oPisoMes, oPisoHora, oPisoDia)
            
            // Controla a habilitação dos campos conforme a categoria
            If ValType(oPisoMes) == "O" .And. ValType(oPisoHora) == "O" .And. ValType(oPisoDia) == "O"
                If cCatAux $ cCatMensal
                    oPisoMes:Enable()
                    oPisoHora:Disable()
                    oPisoDia:Disable()
                ElseIf cCatAux $ "H*T*G*J"
                    oPisoMes:Disable()
                    oPisoHora:Enable()
                    oPisoDia:Disable()
                ElseIf cCatAux $ "D"
                    oPisoMes:Disable()
                    oPisoHora:Disable()
                    oPisoDia:Enable()
                EndIf
                
                // Atualiza os valores nos objetos
                oPisoMes:Refresh()
                oPisoHora:Refresh()
                oPisoDia:Refresh()
            EndIf
        EndIf
    EndIf
    
Return .T.

/*/{Protheus.doc} fInitHeader
    Inicializa o aHeader da GetDados das faixas salariais
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
/*/
Static Function fInitHeader()

    aHeader := {}
    aAlter  := {}
    nUsado  := 7

    AAdd(aHeader, {STR0011, "GP200DE",      "@E 999,999,999,999.99", 15, 2, "Positivo()", "", "N", "", ""})
    AAdd(aHeader, {STR0012, "GP200ATE",     "@E 999,999,999,999.99", 15, 2, "Positivo()", "", "N", "", ""})
    AAdd(aHeader, {STR0013, "GP200PERCEN",  "@E 999.999999",         10, 6, "Positivo()", "", "N", "", ""})
    AAdd(aHeader, {STR0014, "GP200VAL",     "@E 999,999,999,999.99", 15, 2, "Positivo()", "", "N", "", ""})
    AAdd(aHeader, {STR0015, "GP200DT1",     "",                       8, 0, "GP200Dt1()", "", "D", "", ""})
    AAdd(aHeader, {STR0016, "GP200DT2",     "",                       8, 0, "GP200DT2()", "", "D", "", ""})

    aAlter := {"GP200DE", "GP200ATE", "GP200PERCEN", "GP200VAL", "GP200DT1", "GP200DT2"}

Return

/*/{Protheus.doc} fLoadFaixas
    Carrega as faixas salariais da tabela RUG
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @param nPisoMes Valor do piso mensal
    @param nPisoHora Valor do piso por hora
    @param nPisoDia Valor do piso diário
    @param cCategoria Categoria selecionada
    @param aCategItems Array com itens das categorias
    @param nCatSelected Posição da categoria selecionada
/*/
Static Function fLoadFaixas(nPisoMes, nPisoHora, nPisoDia, cCategoria, aCategItems, cCatSelected)

    Local cCodUltimo As Character
    Local aArea      As Array
    Local cCategoriaTab As Character
    Local nPos       As Numeric
    
    // Salva área atual
    aArea := GetArea()
    aCols := {}
    
    // Busca o último código inserido pelo usuário atual
    cCodUltimo := PADR(fGetLastUserCode() , TAMSX3("RUG_CODIGO")[1])

    If !Empty(cCodUltimo)
        // Garante que a área RUG está aberta
        DbSelectArea("RUG")
        RUG->(DbSetOrder(1)) // RUG_FILIAL + RUG_CODIGO + RUG_ITEM
        
        If RUG->(DbSeek(xFilial("RUG") + cCodUltimo))
            While RUG->(!Eof()) .And. RUG->RUG_FILIAL == xFilial("RUG") .And. ;
                  RUG->RUG_CODIGO == cCodUltimo .And. RUG->RUG_ORIGEM == "1"
                
                // Carrega os valores do piso da primeira linha encontrada
                If Len(aCols) == 0
                    nPisoMes  := RUG->RUG_PISOM
                    nPisoHora := RUG->RUG_PISOH
                    nPisoDia  := RUG->RUG_PISOD
                    
                    // Carrega a categoria salva
                    cCategoriaTab := AllTrim(RUG->RUG_CATEG)
                    If !Empty(cCategoriaTab)
                        cCategoria := cCategoriaTab
                        
                        // Encontra a posição da categoria no combo e retorna o valor completo
                        nPos := AScan(aCategItems, {|x| SubStr(x, 1, 1) == cCategoria})
                        If nPos > 0
                            cCatSelected := aCategItems[nPos]
                        EndIf
                    EndIf
                EndIf
                
                // Adiciona a faixa ao aCols
                AAdd(aCols, {RUG->RUG_FAIXAD, RUG->RUG_FAIXAA, RUG->RUG_PERCEN, RUG->RUG_VALOR, ;
                             RUG->RUG_ADMDE, RUG->RUG_ADMATE, .F.})
                
                RUG->(DbSkip())
            EndDo
        EndIf
    EndIf
    
    // Restaura área
    RestArea(aArea)

Return

/*/{Protheus.doc} fGetLastUserCode
    Busca o último código inserido pelo usuário atual
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @return Character, Último código UUID utilizado pelo usuário
/*/
Static Function fGetLastUserCode()

    Local cQuery        As Character
    Local cCodigo       As Character
    Local cAlias        As Character
    Local aArea         As Array
    Local oStatement    As Object
    Local nParamOrder   As Numeric
    
    // Salva área atual
    aArea       := GetArea()
    cCodigo     := ""
    nParamOrder := 1

    cQuery := " SELECT RUG_CODIGO "
    cQuery += " FROM " + RetSqlName("RUG") + " RUG "
    cQuery += " WHERE RUG.RUG_FILIAL = ? "
    cQuery += "   AND RUG.RUG_USER = ? "
    cQuery += "   AND RUG.RUG_ORIGEM = ? "
    cQuery += "   AND RUG.D_E_L_E_T_ = ? "
    cQuery += " ORDER BY RUG.RUG_DATA DESC, RUG.RUG_HORA DESC "
    
    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Define o valor dos bind parameters
    oStatement:SetString(nParamOrder++, xFilial("RUG"))
    oStatement:SetString(nParamOrder++, __cUserID)
    oStatement:SetString(nParamOrder++, "1")
    oStatement:SetString(nParamOrder++, " ")
    
    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()
    
    If (cAlias)->(!Eof())
        cCodigo := AllTrim((cAlias)->RUG_CODIGO)
    EndIf
    
    // Fecha a área do alias temporário
    (cAlias)->(DbCloseArea())
    
    // Limpa o objeto da memória
    FwFreeObj(oStatement)
    
    // Restaura área
    RestArea(aArea)

Return cCodigo

/*/{Protheus.doc} fSaveFaixas
    Salva as faixas salariais e piso salarial na tabela RUG
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @param nPisoMes Valor do piso mensal
    @param nPisoHora Valor do piso por hora
    @param nPisoDia Valor do piso diário
    @param cCategoria Categoria selecionada
    @return Logical, .T. se salvou com sucesso
/*/
Static Function fSaveFaixas(nPisoMes, nPisoHora, nPisoDia, cCategoria)

    Local nI        As Numeric
    Local lRetorno  As Logical
    Local cCodigo   As Character
    Local dDataAtual As Date
    Local cHoraAtual As Character
    Local cItem     As Character
    Local lTemDados As Logical
    Local nItemSeq  As Numeric
    
    lRetorno   := .T.
    lTemDados  := .F.
    dDataAtual := Date()
    cHoraAtual := Time()
    cCodigo    := FwUuidV4() // Gera um UUID único para este conjunto de faixas
    nItemSeq   := 0
    
    Begin Transaction
    
        // Verifica se há dados válidos para gravar
        For nI := 1 To Len(aCols)
            If !aCols[nI, 7] // Se não está deletado
                If aCols[nI, 1] > 0 .Or. aCols[nI, 2] > 0 .Or. aCols[nI, 3] > 0 .Or. aCols[nI, 4] > 0
                    lTemDados := .T.
                    Exit
                EndIf
            EndIf
        Next nI
        
        If !lTemDados
            Help(,, "Atenção",, STR0017, 1, 0) //"É necessário informar pelo menos uma faixa salarial válida."
            lRetorno := .F.
        Else
            DbSelectArea("RUG")
            
            // Grava cada faixa na tabela
            For nI := 1 To Len(aCols)
                If !aCols[nI, 7] // Se não está deletado
                    If aCols[nI, 1] > 0 .Or. aCols[nI, 2] > 0 .Or. aCols[nI, 3] > 0 .Or. aCols[nI, 4] > 0
                        
                        nItemSeq++
                        cItem := StrZero(nItemSeq, TAMSX3("RUG_ITEM")[1])
                        
                        RecLock("RUG", .T.)
                        RUG->RUG_FILIAL := xFilial("RUG")
                        RUG->RUG_CODIGO := cCodigo
                        RUG->RUG_ITEM   := cItem
                        RUG->RUG_DATA   := dDataAtual
                        RUG->RUG_HORA   := cHoraAtual
                        RUG->RUG_ORIGEM := "1"
                        RUG->RUG_USER   := __cUserID
                        RUG->RUG_CATEG  := cCategoria
                        RUG->RUG_PISOM  := nPisoMes
                        RUG->RUG_PISOH  := nPisoHora
                        RUG->RUG_PISOD  := nPisoDia
                        RUG->RUG_FAIXAD := aCols[nI, 1]
                        RUG->RUG_FAIXAA := aCols[nI, 2]
                        RUG->RUG_PERCEN := aCols[nI, 3]
                        RUG->RUG_VALOR  := aCols[nI, 4]
                        RUG->RUG_ADMDE  := aCols[nI, 5]
                        RUG->RUG_ADMATE := aCols[nI, 6]
                        MsUnlock()
                        
                    EndIf
                EndIf
            Next nI
        EndIf
    
    End Transaction

Return lRetorno

/*/{Protheus.doc} GP200Piso
    Calcula o valor do piso salarial em mês/horas/dias conforme categoria funcional
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @param nPisoMes Valor do piso mensal
    @param nPisoHora Valor do piso por hora
    @param nPisoDia Valor do piso diário
    @param cCategoria Categoria selecionada
    @param cCatMensal Categorias mensais
    @param oPisoMes Objeto do campo piso mensal
    @param oPisoHora Objeto do campo piso por hora
    @param oPisoDia Objeto do campo piso diário
/*/
Function GP200Piso(nPisoMes, nPisoHora, nPisoDia, cCategoria, cCatMensal, oPisoMes, oPisoHora, oPisoDia)

    Local nPisoMesAux  As Numeric
    Local nPisoHoraAux As Numeric
    Local nPisoDiaAux  As Numeric
    
    // Garante que os valores sejam numéricos
    nPisoMesAux  := Val(Transform(nPisoMes, "@E 999999999.99"))
    nPisoHoraAux := Val(Transform(nPisoHora, "@E 999999999.99"))
    nPisoDiaAux  := Val(Transform(nPisoDia, "@E 999999999.99"))

    If ValType(cCategoria) == "C" .And. ValType(cCatMensal) == "C"
        If cCategoria $ cCatMensal
            nPisoHora := Round(nPisoMesAux / 220, 2)
            nPisoDia  := Round(nPisoMesAux / 30, 2)
        ElseIf cCategoria $ "H*T*G*J"
            nPisoMes  := Round(nPisoHoraAux * 220, 2)
            nPisoDia  := Round((nPisoHoraAux * 220) / 30, 2)
        ElseIf cCategoria $ "D"
            nPisoMes  := Round(nPisoDiaAux * 30, 2)
            nPisoHora := Round((nPisoDiaAux * 30) / 220, 2)
        EndIf
        
        // Atualiza os valores nos objetos se foram passados
        If ValType(oPisoMes) == "O"
            oPisoMes:Refresh()
        EndIf
        If ValType(oPisoHora) == "O"
            oPisoHora:Refresh()
        EndIf
        If ValType(oPisoDia) == "O"
            oPisoDia:Refresh()
        EndIf
    EndIf

Return .T.

/*/{Protheus.doc} GP200LinOk
    Validação de linha da GetDados
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @return Logical, .T. se linha válida
/*/
Function GP200LinOkSV()

    Local lRet      As Logical
    Local nX        As Numeric
    Local dDtIni    As Date
    Local dDtFim    As Date
    Local nFxIni    As Numeric
    Local nFxFim    As Numeric

    lRet   := .T.
    dDtIni := aCols[n, 5]
    dDtFim := aCols[n, 6]
    nFxIni := aCols[n, 1]
    nFxFim := aCols[n, 2]

    // Desconsidera faixas deletadas
    If aCols[n, 7] == .F.
        If aCols[n, 1] > aCols[n, 2]
            Help(" ", 1, "GR200FAIXA")
            lRet := .F.
            Return lRet
        EndIf

        If n > 1
            If Empty(aCols[n, 5]) .And. Empty(aCols[n, 6])
                If (aCols[n, 1] <= aCols[n-1, 2]) .And. !aCols[n-1, 7]
                    Help(,, "Atenção",, STR0018 + CRLF + ;
                        STR0019, 1, 0)
                    lRet := .F.
                    Return lRet
                EndIf
            EndIf
        EndIf

        If (aCols[n, 3] = 0) .And. (aCols[n, 4] = 0)
            Help(" ", 1, "GR200ZEROS")
            lRet := .F.
            Return lRet
        EndIf

        If !Empty(aCols[n, 5]) .And. Empty(aCols[n, 6]) .Or. ;
           Empty(aCols[n, 5]) .And. !Empty(aCols[n, 6])
            MsgAlert(STR0020, "Atenção")
            lRet := .F.
            Return lRet
        EndIf

        For nX := 1 To Len(aCols)
            If nX # n .And. aCols[nX, 7] == .F. .And. n > nX .And. !Empty(aCols[nX, 5]) .And. !Empty(aCols[nX, 6])
                // Permite datas iguais, só valida se há sobreposição real de períodos
                If (dDtIni < aCols[nX, 5] .And. dDtFim < aCols[nX, 5]) .Or. ;
                   (dDtIni > aCols[nX, 6])
                    lRet := .T.
                Else
                    // Se há sobreposição de datas, verifica se as faixas salariais também se sobrepõem
                    If (nFxIni < aCols[nX, 1] .And. nFxFim < aCols[nX, 1]) .Or. ;
                       (nFxIni > aCols[nX, 2])
                        lRet := .T.
                    Else
                        // Só exibe erro se há sobreposição tanto de datas quanto de faixas salariais
                        MsgAlert(STR0021, "Atenção")
                        lRet := .F.
                        Exit
                    EndIf
                EndIf
            EndIf
        Next nX

        // Permite que as datas sejam iguais, só verifica se data inicial é posterior à final
        If !Empty(aCols[n, 6]) .And. aCols[n, 7] == .F.
            If aCols[n, 6] < aCols[n, 5]
                MsgAlert(STR0022, "Atenção")
                lRet := .F.
            EndIf
        EndIf
    EndIf

Return lRet

/*/{Protheus.doc} GP200TudOk
    Validação geral da GetDados
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @return Logical, .T. se válido
/*/
Function GP200TudOkSV()
Return .T.

/*/{Protheus.doc} GP200Dt1
    Validação da data inicial de admissão
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @return Logical, .T. se válido
/*/
Function GP200Dt1()

    Local lRet As Logical

    lRet := .T.

    If !Empty(aCols[n, 6]) .And. aCols[n, 7] == .F.
        If aCols[n, 6] < M->GPR200DT1
            lRet := .F.
        EndIf
    EndIf

Return lRet

/*/{Protheus.doc} GP200Dt2
    Validação da data final de admissão
    @type Function
    @version 12.1.2410
    @author guilherme.suttanni
    @since 28/07/2025
    @return Logical, .T. se válido
/*/
Function GP200Dt2()

    Local lRet As Logical

    lRet := .T.

    If !Empty(aCols[n, 6]) .And. aCols[n, 7] == .F.
        If aCols[n, 5] > M->GPR200DT2
            lRet := .F.
        EndIf
    EndIf

Return lRet
