#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "TOTVS.CH"
#Include "rh.sv.gpe.gper079.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER079TReportsBusinessObject:new()
	Classe do objeto de negócio da Ficha de Registro (GPER079).
	@type Class
	@version 12.1.2410
	@author karina.alves
	@since 07/01/2025
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SRK, CTT, SRJ", customTables="SRA, SRK", name="Relatório de Saldo Devedor - Empréstimo", country="ALL", initialRelease="12.1.2310")
Class GPER079TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
	// Define os métodos da classe
	Public Method new()            As Object
	Public Method getDescription() As Character
	Public Method getData()        As Object
	Public Method getSchema()      As Object

	@Get("/api/rh/smartview/v1/options/GPER079/:param")
    Public Method getComboBox() As Variant
EndClass

/*/{Protheus.doc} GPER079TReportsBusinessObject:new()
	Instancia a classe do objeto de negócio.
	@type Method
	@version 12.1.2410
	@author karina.alves
	@since 07/01/2025
	@return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER079TReportsBusinessObject
	_Super:new()

	//Define a Área
	self:appendArea(STR0001) // "RH"

	// Define o nome do objeto de negócio
	self:setDisplayName(STR0002) // "Relatório de Saldo Devedor - Empréstimo"
Return self

/*/{Protheus.doc} GPER079TReportsBusinessObject:getDescription()
	Retorna a descrição do objeto de negócio.
	@type Method
	@version 12.1.2410
	@author karina.alves
	@since 07/01/2025
	@return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER079TReportsBusinessObject
Return STR0003 // "Este objeto de negócio emite o Saldo Devedor - Empréstimo."

/*/{Protheus.doc} GPER079TReportsBusinessObject:getData()
	Captura os dados do objeto de negócio.
	@type Method
	@version 12.1.2410
	@author karina.alves
	@since 07/01/2025
	@return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER079TReportsBusinessObject
	// Declaração das variáveis locais
	Local aPDFields            as Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR          as Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aAllFields           as Array     // Estrutura de todos os campos do ON
    Local aFieldsNR            as Array     // Campos com nome diferente do real	
	Local aInfo                as Array     // Array com dados da empresa e filial
	Local cAlias               as Character // Alias do arquivo temporário	
	Local cQuery               as Character // Query a ser executada
    Local cCustomFields        as Character // Campos customizados no formato SQL 
	Local cName                as Character // Nome do campo   
    Local cFilialDe            as Character // Parâmetro Filial De
	Local cFilialAte           as Character // Parâmetro Filial Ate
	Local cMatDe 			   as Character // Parâmetro Mat De
	Local cMatAte 			   as Character // Parâmetro Mat Ate
	Local cCCDe                as Character // Parâmetro CC De
	Local cCCAte               as Character // Parâmetro CC De
	Local cFiltro              as Character // Filtro do objeto de negócio	
	Local cJoin                as Character // Join SRA e SRK
	Local cJoinCTT             as Character // Join SRA e CTT
	Local cJoinSRJ             as Character // Join SRA e SRJ
	Local cAcessaSRK           as Character // Valida acesso a SRK 	
	Local cValidFil            as Character // Filiais válidas	
	Local cStatus              as Character // Situação do Empréstimo
	Local cFilAnterior         as Character // Filial anterior
	Local cEConsig			   as Character
	Local cWhere			   as Character	
	Local jParams              as JSON      // Parâmetros do relatório
    Local jData                as JSON      // Informações a serem adicionadas no ON
	Local jRegistros           as JSON      // Parâmetros do objeto de negócio
    Local jLGPD                as JSON      // Indica quais campos devem ser ofuscados
	Local nParamOrder          as Numeric   // Contador de Parâmetros
    Local nFields              as Numeric   // Contador de campos
	Local nParcAbert           as Numeric   // Quantidade Parcelas em Aberto	
	Local nValNPgto			   as Numeric
    Local lObfuscated          as Logical   // Flag que indica se tem algum campo para ofuscar
	Local lCpoNPgto			   as Logical

	Static oExec
	Static __cWhere := ""

	// Inicialização das variáveis
    aPDFields   := {}
    aPDFieldsNR := {}
    aAllFields  := {}
    aFieldsNR   := {}	
	aInfo       := {}  
	cAlias      := GetNextAlias()	
    cValidFil   := fValidFil()
    cFiltro     := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
	cAcessaSRK  := &("{ || " + ChkRH("GPER079","SRK","2") + "}")
	jParams     := oFilter:getParameters()
    jData 		:= JSONObject():New()
	jRegistros 	:= JSONObject():New()
    jLGPD       := JSONObject():New()
	nParamOrder := 1    
    nFields     := 1
	lCpoNPgto   := cPaisLoc == "BRA" .And. RCK->(ColumnPos("RCK_NPAG") > 0)

	If lCpoNPgto
		DbSelectArea("RCK")
		DbSetOrder(RetOrdem("RCK", "RCK_FILIAL+RCK_MAT+RCK_NUMID+STR(RCK_SEQUEN)"))
	EndIF	
	// Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"CTT_DESC01", "RJ_DESFUNC"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

	// Captura o valor dos parâmetros
	If (jParams != NIL)
		cFilialDe  := IIf(jParams:hasProperty("PAR_FILIALDE")  .and. Len(jParams["PAR_FILIALDE"])  > 0 .and. !Empty(jParams["PAR_FILIALDE"][1]),  jParams["PAR_FILIALDE"][1], "")
		cFilialAte := IIf(jParams:hasProperty("PAR_FILIALATE") .and. Len(jParams["PAR_FILIALATE"]) > 0 .and. !Empty(jParams["PAR_FILIALATE"][1]), jParams["PAR_FILIALATE"][1], Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cMatDe     := IIf(jParams:hasProperty("PAR_MATDE")     .and. Len(jParams["PAR_MATDE"])  > 0    .and. !Empty(jParams["PAR_MATDE"][1]),     jParams["PAR_MATDE"][1], "")
        cMatAte    := IIf(jParams:hasProperty("PAR_MATATE")    .and. Len(jParams["PAR_MATATE"]) > 0 	.and. !Empty(jParams["PAR_MATATE"][1]),    jParams["PAR_MATATE"][1], Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
		cCCDe      := IIf(jParams:hasProperty("PAR_CCDE")      .and. Len(jParams["PAR_CCDE"])  > 0     .and. !Empty(jParams["PAR_CCDE"][1]),      jParams["PAR_CCDE"][1], "")
        cCCAte     := IIf(jParams:hasProperty("PAR_CCATE")     .and. Len(jParams["PAR_CCATE"]) > 0     .and. !Empty(jParams["PAR_CCATE"][1]),     jParams["PAR_CCATE"][1], Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
		cStatus    := IIf(jParams:hasProperty("PAR_STATUS")    .and. Len(jParams["PAR_STATUS"]) > 0    .and. !Empty(jParams["PAR_STATUS"][1]),    jParams["PAR_STATUS"][1], "5") //Todos
		cEConsig   := IIf(jParams:hasProperty("PAR_ECONSI")    .and. Len(jParams["PAR_ECONSI"]) > 0    .and. !Empty(jParams["PAR_ECONSI"][1]),    jParams["PAR_ECONSI"][1], "1") //Não
		cWhere     := cFilialDe+cFilialAte+cMatDe+cMatAte+cCCDe+cStatus+cEConsig
	Endif

	cJoin    := FWJoinFilial("SRK", "SRA")
	cJoinCTT := FWJoinFilial("SRA", "CTT")
	cJoinSRJ := FWJoinFilial("SRA", "SRJ")

	// Montagem da query de busca
	cQuery := " SELECT RK_NUMID, RK_PARCPAG, RK_PARCELA, RK_FILIAL, RA_CC, CTT_DESC01 AS CTT_CC, RK_MAT, RA_NOME, "
	cQuery += "  RA_CODUNIC, RA_CODFUNC, SRJ.RJ_DESC AS RJ_DESFUNC, RK_NUMID, RK_PARCELA, RK_PARCPAG, RK_VALORTO, " 
	cQuery += "  RK_VLRPAGO, RK_VALORPA, RK_STATUS "
	If lCpoNPgto
		cQuery += ", RK_CONSFGT "
	EndIf
	cQuery += " ? " // 1
	cQuery += " FROM " + retsqlname("SRK") + " SRK "
	cQuery += " INNER JOIN " + retsqlname("SRA") + " SRA ON "
	cQuery +=       cJoin
	cQuery +=       " AND SRA.RA_MAT = SRK.RK_MAT"  
	cQuery +=       " AND SRA.D_E_L_E_T_ = ? "  // 2
	cQuery += " LEFT JOIN " + retsqlname("CTT") + " CTT ON "
	cQuery +=       cJoinCTT
	cQuery +=       " AND CTT.CTT_CUSTO = SRA.RA_CC "
	cQuery +=       " AND CTT.D_E_L_E_T_ = ? " // 3
	cQuery += " LEFT JOIN " + retsqlname("SRJ") + " SRJ ON "
	cQuery +=       cJoinSRJ
	cQuery +=       " AND SRJ.RJ_FUNCAO = SRA.RA_CODFUNC "
	cQuery +=       " AND SRJ.D_E_L_E_T_ = ? " // 4
	cQuery += " WHERE "
	cQuery +=       " RK_FILIAL  BETWEEN ? AND ? " // 5 e 6
	cQuery +=       " AND RK_MAT BETWEEN ? AND ? " // 7 e 8
	cQuery +=       " AND RA_CC BETWEEN ? AND ? " // 9 e 10
	If cStatus <> "5"
		cQuery += " AND RK_STATUS = ? " // 11
	EndIf	
	If lCpoNPgto .And. cEConsig == "2" 
		cQuery += " AND RK_CONSFGT = ? " // 12
	EndIf
    cQuery +=       " AND SRK.D_E_L_E_T_ = ? "  // 13                                     
    cQuery +=       " ? " // 14                                                            
	
	If oExec == Nil .Or. !(__cWhere == cWhere)
		__cWhere := cWhere
		oExec := FwExecStatement():New(ChangeQuery(cQuery))
	EndIf 

    // Captura e define os campos personalizados das tabelas SRA, SRK
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRK"})
    oExec:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    // Define o valor dos bind parameters
	// 2, 3 e 4
	oExec:SetString(nParamOrder++, " ")     
	oExec:SetString(nParamOrder++, " ")  
	oExec:SetString(nParamOrder++, " ")  

    // 5 e 6
	oExec:SetString(nParamOrder++, cFilialDe)
    oExec:SetString(nParamOrder++, cFilialAte)

	// 7 e 8
	oExec:SetString(nParamOrder++, cMatDe)
    oExec:SetString(nParamOrder++, cMatAte)

	// 9 e 10
	oExec:SetString(nParamOrder++, cCCDe)
    oExec:SetString(nParamOrder++, cCCAte)

	// 11
	If cStatus <> "5"
		oExec:SetString(nParamOrder++, cStatus)
	EndIf
    
	// 12
	If lCpoNPgto .And. cEConsig == "2"
		oExec:SetString(nParamOrder++, "1")
	EndIf 

	// 13
    oExec:SetString(nParamOrder++, " ")

	// 14 
    oExec:SetUnsafe(nParamOrder++, cFiltro) 

	cAlias := oExec:OpenAlias()

	// Percorre todos os registros
	While (!(cAlias)->(EOF()))	

		nParcAbert := 0
		nValNPgto  := 0

		If (cAlias)->RK_FILIAL # cFilAnterior
			If !((cAlias)->RK_FILIAL $ cValidFil) .Or. !Eval(cAcessaSRK) 
				(cAlias)->(dbSkip())
				Loop
			EndIf
			cFilAnterior := (cAlias)->RK_FILIAL	
			fInfo(@aInfo, (cAlias)->RK_FILIAL)
		EndIf	

		// Adiciona as informações	
		nParcAbert := ((cAlias)->RK_PARCELA - (cAlias)->RK_PARCPAG)	

		jData := JSONObject():New()
		jData["OriginPlatform"]  := "Protheus"
		jData["EMPRESA"]         := Left(AllTrim(aInfo[3]),60)
		jData["RK_FILIAL"]       := Trim(IIf(jLGPD["RK_FILIAL"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RK_FILIAL),       (cAlias)->RK_FILIAL))
		jData["RA_CC"]           := Trim(IIf(jLGPD["RA_CC"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),           (cAlias)->RA_CC))
		jData["CTT_DESC01"]      := Trim(IIf(jLGPD["CTT_DESC01"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_CC),          (cAlias)->CTT_CC))
		jData["RK_MAT"]          := Trim(IIf(jLGPD["RK_MAT"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RK_MAT),          (cAlias)->RK_MAT))
		jData["RA_NOME"]         := Trim(IIf(jLGPD["RA_NOME"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),         (cAlias)->RA_NOME))
		jData["RA_CODUNIC"]      := Trim(IIf(jLGPD["RA_CODUNIC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CODUNIC),      (cAlias)->RA_CODUNIC))
		jData["RA_CODFUNC"]      := Trim(IIf(jLGPD["RA_CODFUNC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CODFUNC),      (cAlias)->RA_CODFUNC))
		jData["RJ_DESFUNC"]      := Trim(IIf(jLGPD["RJ_DESFUNC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RJ_DESFUNC),      (cAlias)->RJ_DESFUNC))
		jData["RK_NUMID"]        := Trim(IIf(jLGPD["RK_NUMID"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RK_NUMID),        (cAlias)->RK_NUMID))
		jData["RK_PARCELA"]      := IIf(jLGPD["RK_PARCELA"], 0, (cAlias)->RK_PARCELA)
		jData["RK_PARCPAG"]      := IIf(jLGPD["RK_PARCPAG"], 0, (cAlias)->RK_PARCPAG)
		jData["PCABERTO"]        := nParcAbert
		jData["RK_VALORTO"]      := IIf(jLGPD["RK_VALORTO"], 0, (cAlias)->RK_VALORTO)
		jData["RK_VLRPAGO"]      := IIf(jLGPD["RK_VLRPAGO"], 0, (cAlias)->RK_VLRPAGO)
		jData["RK_VALORPA"]      := IIf(jLGPD["RK_VALORPA"], 0, (cAlias)->RK_VALORPA)
		jData["STATUS"]          := fStatus((cAlias)->RK_STATUS) 
		If lCpoNPgto .And. (cAlias)->RK_CONSFGT == "1"
			RCK->(dbGoTop())
			If RCK->(DbSeek((cAlias)->RK_FILIAL + (cAlias)->RK_MAT + (cAlias)->RK_NUMID))
				While RCK->(!Eof()) .And. RTrim(RCK->(RCK_FILIAL + RCK_MAT + RCK_NUMID )) == RTrim((cAlias)->RK_FILIAL + (cAlias)->RK_MAT + (cAlias)->RK_NUMID) .And. RCK->RCK_NPAG > 0
					nValNPgto += RCK->RCK_NPAG
					RCK->(DbSkip())
				EndDo
			EndIf	
			jData["RCK_NPAG"] := nValNPgto
		Else 
			jData["RCK_NPAG"] := 0
		EndIf 
		// Adiciona os campos customizados no JSON
		GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

		// Adiciona as informações da linha
		self:oData:appendData(jData)			
					
		// Pula para o próximo registro
		(cAlias)->(DBSkip())
		
	EndDo

	// Fecha a área da tabela temporária
	(cAlias)->(DBCloseArea())
	
	If lCpoNPgto
		RCK->(DBCloseArea())
	EndIF	

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
	FwFreeObj(jParams)
    FwFreeArray(aAllFields)
    FwFreeArray(aPDFields)
    FwFreeArray(aPDFieldsNR)
    FwFreeArray(aFieldsNR)
	FwFreeArray(aInfo)
Return self:oData

/*/{Protheus.doc} GPER079TReportsBusinessObject:getSchema()
	Define o schema do objeto de negócio.
	@type Method
	@version 12.1.2410
	@author karina.alves
	@since 02/01/2025
	@return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER079TReportsBusinessObject

	// Define os parâmetros do relatório
	self:oSchema:addParameter("PAR_FILIALDE"              , STR0009              , "string", .F.) // "Filial De"
	self:oSchema:addParameter("PAR_FILIALATE"             , STR0010              , "string", .F.) // "Filial Ate"
	self:oSchema:addParameter("PAR_CCDE"                  , STR0011              , "string", .F.) // "C.C. De"
	self:oSchema:addParameter("PAR_CCATE"                 , STR0012              , "string", .F.) // "C.C. Ate"
	self:oSchema:addParameter("PAR_MATDE"                 , STR0013              , "string", .F.) // "Matrícula De" 
	self:oSchema:addParameter("PAR_MATATE"                , STR0014              , "string", .F.) // "Matrícula Ate"
	self:oSchema:addParameter("PAR_STATUS"                , STR0015              , "string", .F.) // "Status"
	self:oSchema:addParameter("PAR_ECONSI"                , STR0032              , "string", .F.) // "Status"

	// Define endpoints dos parâmetros de tipo lookup/combobox
	self:setCustomURL("PAR_FILIALDE",         "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
	self:setCustomURL("PAR_FILIALATE",        "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
	self:setCustomURL("PAR_CCDE",             "/api/framework/v1/genericLookupService/smartview/CTT", 2)
	self:setCustomURL("PAR_CCATE",            "/api/framework/v1/genericLookupService/smartview/CTT", 2)
	self:setCustomURL("PAR_MATDE",            "/api/framework/v1/genericLookupService/smartview/SRA", 2)
	self:setCustomURL("PAR_MATATE",           "/api/framework/v1/genericLookupService/smartview/SRA", 2)
	self:setCustomURL("PAR_STATUS",           "/api/rh/smartview/v1/options/GPER079/Status",          1)
	self:setCustomURL("PAR_ECONSI",           "/api/rh/smartview/v1/options/GPER079/eConsignado",     1)

	// Adiciona as propriedades do objeto de negócio
	self:oSchema:addProperty("EMPRESA",         STR0016,          "string",       STR0016,        "EMPRESA") //  "Empresa" 
	self:oSchema:addProperty("RK_FILIAL",       STR0017,          "string",       STR0017,        "RK_FILIAL") //  "Filial" 
	self:oSchema:addProperty("RA_CC",           STR0018,          "string",       STR0018,        "RA_CC") //  "Cod. C.C."
	self:oSchema:addProperty("CTT_DESC01",      STR0019,          "string",       STR0019,        "CTT_DESC01") //  "Des. C.C."  
	self:oSchema:addProperty("RK_MAT",          STR0020,          "string",       STR0020,        "RK_MAT") //  "Matrícula"
	self:oSchema:addProperty("RA_NOME",         STR0021,          "string",       STR0021,        "RA_NOME") //  "Nome"
	self:oSchema:addProperty("RA_CODUNIC",      STR0022,          "string",       STR0022,        "RA_CODUNIC") //  "Matrícula eSoc"
	self:oSchema:addProperty("RA_CODFUNC",      STR0023,          "string",       STR0023,        "RA_CODFUNC") //  "Cod. Função"
	self:oSchema:addProperty("RJ_DESFUNC",      STR0024,          "string",       STR0024,        "RJ_DESFUNC") //  "Des. Função"
	self:oSchema:addProperty("RK_NUMID",        STR0025,          "string",       STR0025,        "RK_NUMID") //  "ID Empres."
	self:oSchema:addProperty("RK_PARCELA",      STR0026,          "number",       STR0026,        "RK_PARCELA") //  "Qtde. Parcelas"
	self:oSchema:addProperty("RK_PARCPAG",      STR0027,          "number",       STR0027,        "RK_PARCPAG") //  "Parcelas Pagas"
	self:oSchema:addProperty("PCABERTO",        STR0028,          "number",       STR0028,        "PCABERTO") //  "Parcelas Abert"
	self:oSchema:addProperty("RK_VALORTO",      STR0029,          "number",       STR0029,        "RK_VALORTO") //  "Valor Total"
	self:oSchema:addProperty("RK_VLRPAGO",      STR0030,          "number",       STR0030,        "RK_VLRPAGO") //  "Valor Pago"
	self:oSchema:addProperty("RK_VALORPA",      STR0031,          "number",       STR0031,        "RK_VALORPA") //  "Valor Parcela"
	self:oSchema:addProperty("STATUS",          STR0015,          "string",       STR0015,        "STATUS") //  "Status"
	self:oSchema:addProperty("RCK_NPAG",        STR0033,          "number",       STR0033,        "RCK_NPAG") //  "Status"
Return self:oSchema

/*/{Protheus.doc} GPER079TReportsBusinessObject::getComboBox
    Retorna os valores de ComboBox do parâmetro requisitado.
    @type Method
    @version 12.1.2410
    @author karina.alves
    @since 02/01/2025
    @return Variant, Retorno nulo pré-fixado
    @obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() As Variant Class GPER079TReportsBusinessObject
    // Declaração das variáveis locais
    Local jPath     As JSON    // Path parameters da requisição
    Local jResponse As JSON    // Resposta da requisição
    Local jItem     As JSON    // Item do valor do parâmetro
    Local nValues   As Numeric // Contador dos valores do parâmetro
    Local aValues   As Array   // Valores do parâmetro

    // Inicialização das variáveis
    jPath     := oRest:getPathParamsRequest()
    jResponse := JSONObject():New()
    jItem     := NIL
    nValues   := 0
    aValues   := {}

    // Captura os path parameters da requisição
    If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    // Cria o atributo data e nextPageUrl no JSON de resposta
    jResponse["data"]        := {}
    jResponse["nextPageUrl"] := NIL

    // Verifica qual parâmetro foi solicitado
    If (cParam == "Status")
        // Monta o vetor de valores do parâmetro		
        AAdd(aValues, FwHTTPEncode(STR0004)) // "Solicitado"
        AAdd(aValues, FwHTTPEncode(STR0005)) // "Ativo"
		AAdd(aValues, FwHTTPEncode(STR0006)) // "Pago"
		AAdd(aValues, FwHTTPEncode(STR0007)) // "Suspenso"
		AAdd(aValues, FwHTTPEncode(STR0008)) // "Todos"
	Else 
		AAdd(aValues, FwHTTPEncode(STR0034)) // "Não"
        AAdd(aValues, FwHTTPEncode(STR0035)) // "Sim"
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL

/*/{Protheus.doc} fStatus
    Retorna o Status do Empréstimo
    @type Function
    @version 12.1.2410
    @author karina.alves
    @since 07/01/2025
    @param cStatus, Character, Status que vem do banco
    @return Character, Situação
/*/
Static Function fStatus(cStatus As Character) As Character
	Local cDesc as Character

	cDesc := ""

	Do Case
		Case cStatus == '1' //"Solicitado"
			cDesc := FwHTTPEncode(STR0004)
		Case cStatus== '2' //"Ativo"
			cDesc := FwHTTPEncode(STR0005)
		Case cStatus== '3' //"Pago"
			cDesc := FwHTTPEncode(STR0006)
		Case cStatus== '4' //"Suspenso"
			cDesc := FwHTTPEncode(STR0007)
	EndCase
Return cDesc
