#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "FWLIBVERSION.CH"
#include "rh.sv.treports.gper400.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SRA, SRF", customTables="SRA, SRF", name="Programação de Férias", country="ALL", initialRelease="12.1.2210")
class GPER400TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
    private method getCalcSRH() as numeric
    private method getWherePar() as character    
 
    @Get("/api/rh/smartview/v1/options/gpersv400/:param")
    Public Method getComboBox() As Variant
endclass

method new() class GPER400TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) //RH
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) // Programação de Férias

return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type  Metodo
@author Bruno Costa
@since 31/10/2023
/*/
method getDescription() as character class GPER400TReportsBusinessObject
return (STR0003) // Objeto contendo informações das Programações de Férias
 
method getData(nPage as numeric, oFilter as object) as object class GPER400TReportsBusinessObject
    
    local cQuery as character    
    local cAlias as character 
    local cLastFil as character
    local cFilProc as character
    local cLastCC as character
    local cOrdem as character
	local cWhere as character 
    local cFilName as character
    local cNameEmp as character
    local cNameCC as character
    local cAcessaSRA as character
    local cDia2501 as character
    local cRot as character
    local cProcAnt as character
    local lCatEsta as logical
    local lMod2 as logical
    local lDobro as logical
    local lUnicoPer as logical
    local lRCHComp as logical
    local lProc as logical
    local nOrdem as numeric
    local nDiasCalc as numeric
    local nDiasMax as numeric
    local nDIdeal as numeric
    local nTipFer as numeric
    local nPerc13 as numeric
    local nDiasP1 as numeric
    local nDiasAP1 as numeric
    local nDiasP2 as numeric
    local nDiasAP2 as numeric
    local nDiasP3 as numeric
    local nDiasAP3 as numeric
    Local nQtPer as numeric
    local nFerVenc as numeric
	local nFalVen as numeric
	local nFerProp as numeric
	local nFalProp as numeric
	local nTotal as numeric
    local nSaldo as numeric
    local nDiasFer1 as numeric
    local nDiasFer2 as numeric
    local nDiasFer3	as numeric
    local nDiasAbo1 as numeric
    local nDiasAbo2 as numeric
    local nDiasAbo3 as numeric
    local nCnt1 as numeric
    local nDiasAboN	as numeric
    local nDMes12 as numeric
    local nDMes01 as numeric
    local nPOrder as numeric
    local nI as numeric
    Local dDataRef as date
    local dProgDe as date
    local dProgAte as date
    local dDIniP1 as date
    local dDFimP1 as date
    local dDIniP2 as date
    local dDFimP2 as date
    local dDIniP3 as date
    local dDFimP3 as date
    local dLimdeal as date
    local dLimMax as date
    local DFimPAqui as date
    local DPRPAqui as date
    local dDtIProg1 as date 
    local dDtIProg2 as date
    local dDtIProg3 as date 
    local dDataFim as date
    local aOfusca as array
    local aFldRel as array
    local aCategoria as array    
    local aSituacoes  as array 
    local aParams as array
    local jParams as json
    local aStatus as array
    local cCustomFields as character // Campos customizados no formato SQL
    local jData as JSON // Informações a serem adicionadas no ON
    local aAllFields as array     // Estrutura de todos os campos do ON
    local aPDFields as array     // Campos que podem ser exibidos de acordo com a LGPD
    local lObfuscated as logical   // Flag que indica se tem algum campo para ofuscar

    Private lOfusca	as logical 
    Private lUltSemana as logical
    Private nDiasDir as numeric
    Private nPosSem as numeric 
    Private aTabFer	as array  
    Private aPeriodo as array   
    Private aCodFol	as array  
    Private cProcesso as character
    Private cPeriodo as character
    Private cSemana as character
    Private cAnoMes	as character

    Static __cEmpAnt := cEmpAnt
    Static oStatement
    Static __cWhere  := ""

    DFimPAqui  := CTOD("//")
    DPRPAqui   := CTOD("//")
    dDtIProg1  := CTOD("//")
    dDtIProg2  := CTOD("//")
    dDtIProg3  := CTOD("//")
    dDataFim   := CTOD("//")
    dDataRef   := CTOD("//")
    dProgDe    := CTOD("//")
    dProgAte   := CTOD("//")
    dLimdeal   := CTOD("//")
    dLimMax	   := CTOD("//")
    lRCHComp   := IF(FWModeAccess("RCH", 3) == "C", .T., .F.)
    cAcessaSRA := &( " { || " + ChkRH( "GPER400" , "SRA" , "2" ) + " } " )
    cDia2501   := SuperGetMv("MV_DIA2501", ,"N")
    aOfusca	   := If(FindFunction('ChkOfusca'), ChkOfusca(), { .T., .F. }) //[2]Ofuscamento
    aFldRel	   := If(aOfusca[2], FwProtectedDataUtil():UsrNoAccessFieldsInList( {"RA_NOME"} ), {})
    lOfusca	   := Len(aFldRel) > 0
    aCodFol	   := If(cPaisLoc=="BRA",ARRAY(1),{}) // Incializada para nao ser carregada em Calc_Fer
    aParams    := {"FILDE", "FILATE", "MATDE", "MATATE", "CCDE", "CCATE", "CATFUNC", "SITUACAO"}

    SetMnemonicos(NIL,NIL,.T.)
    lMetFalP 	:= If( Type("lMetFalP") == "U", .F. , lMetFalP)
    lMetFalV 	:= If( Type("lMetFalV") == "U", .F. , lMetFalV)
    lMetadeFal	:= If( Type("lMetadeFal") == "U", .F. , lMetadeFal)

    jParams  := oFilter:getParameters() //Metodo para retorno do json dos parâmetros
    lMod2    := oFilter:hasFilter() .And. oFilter:getRawFilter()["values"][1] == "MOD2"
    cOrdem   := "SRA.RA_FILIAL, SRA.RA_CC, SRA.RA_MAT, SRF.RF_DATABAS"
    nOrdem   := 1
    nTipFer  := 3
    nDiasMax := 45
    nDIdeal  := 30  
    nQtPer   := 1
    nPOrder  := 5
    aStatus  := {" ","1"}
    cCustomFields := ""
    jData         := NIL 
    aAllFields    := {}
    aPDFields     := {}
    lObfuscated   := .F.

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields    := self:getStructFields()
    
    aCategoria := IIf(jParams:hasProperty("CATFUNC"), jParams["CATFUNC"], {})
    aSituacoes := IIf(jParams:hasProperty("SITUACAO"), jParams["SITUACAO"], {})
    
    // Percorre as situações/categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nI := 1 To Len(aSituacoes)
        aSituacoes[nI] := IIf(aSituacoes[nI] == NIL .Or. Empty(aSituacoes[nI]), " ", aSituacoes[nI])
    Next nI

    For nI := 1 To Len(aCategoria)
        aCategoria[nI] := IIf(aCategoria[nI] == NIL .Or. Empty(aCategoria[nI]), "M", aCategoria[nI])
    Next nI

    If !Empty(jParams['TPFER', 1])
        nTipFer := jParams['TPFER', 1]
    EndIf

    If !Empty(jParams['PROGDE', 1])
		dProgDe := sToD(StrTran( SubStr(jParams['PROGDE', 1], 1, 10), "-" ))
	EndIf
	
	If !Empty(jParams['PROGATE', 1])
		dProgAte := sToD(StrTran( SubStr(jParams['PROGATE', 1], 1, 10), "-" ))
	EndIf

    If !Empty(jParams['QTDPER', 1])
        nQtPer := IF(jParams['QTDPER', 1] > 3, 3, jParams['QTDPER', 1]) 
    EndIf

    If !Empty(jParams['LIMMAX', 1])
        nDiasMax := jParams['LIMMAX', 1]
    EndIf

    If !Empty(jParams['LIMIDEAL', 1])
        nDIdeal := jParams['LIMIDEAL', 1]
    EndIf

    If !Empty(jParams['DATAREF', 1])
		dDataRef := sToD(StrTran( SubStr(jParams['DATAREF', 1], 1, 10), "-" ))
	EndIf
    dDataRef := If(EMPTY(dDataRef), dDataBase, dDataRef)	

    If !Empty(jParams['ORDER', 1])
		nOrdem := Val(jParams['ORDER', 1]) 
	EndIf

	If nOrdem == 2
		cOrdem := "SRA.RA_FILIAL , SRA.RA_MAT, SRF.RF_DATABAS"
	ElseIf nOrdem == 3
		cOrdem := "SRA.RA_FILIAL , SRA.RA_NOME , SRA.RA_MAT, SRF.RF_DATABAS"
	ElseIf nOrdem == 4
		cOrdem := "SRA.RA_FILIAL , SRA.RA_CC , SRA.RA_NOME, SRF.RF_DATABAS"
	ElseIf nOrdem == 5
		cOrdem := "SRF.RF_FILIAL , SRF.RF_DATABAS , SRF.RF_MAT, SRA.RA_CC"
	ElseIf nOrdem == 6
		cOrdem := "SRF.RF_FILIAL , SRF.RF_DATAINI , SRF.RF_MAT"
	EndIf

    If oStatement == Nil .Or. __cEmpAnt <> cEmpAnt .Or. !(__cWhere == cWhere)
        
        cQuery := " SELECT	SRA.R_E_C_N_O_ AS RECNO, SRA.RA_NOME, SRA.RA_CC, SRA.RA_ADMISSA, SRA.RA_PROCES, SRA.RA_HRSEMAN, SRA.RA_HOPARC, SRA.RA_CATFUNC, "
        cQuery += "         SRF.RF_FILIAL, SRF.RF_MAT, SRF.RF_DATABAS, SRF.RF_DATAFIM, SRF.RF_DFALVAT, SRF.RF_DFALAAT, SRF.RF_DIASDIR, SRF.RF_DFERVAT, " 
        cQuery += "         SRF.RF_DFERAAT, SRF.RF_PERC13S, SRF.RF_DFERANT, SRF.RF_TEMABPE, SRF.RF_DATAINI, SRF.RF_DATINI2, SRF.RF_DATINI3, SRF.RF_DFEPRO1, " 
        cQuery += "         SRF.RF_DFEPRO2, SRF.RF_DFEPRO3, SRF.RF_DABPRO1, SRF.RF_DABPRO2, SRF.RF_DABPRO3 "
        cQuery +=  "           ? "
        cQuery += " FROM	" + RetSqlName("SRF") + " SRF "
        cQuery += "         INNER JOIN " + RetSqlName("SRA") + " SRA ON " + FWJoinFilial( "SRA", "SRF" ) + " AND SRA.RA_MAT = SRF.RF_MAT AND SRA.D_E_L_E_T_ = ? "
        cQuery += " WHERE	SRF.RF_STATUS IN (?) "
        cQuery += " AND     SRF.D_E_L_E_T_ = ? "

        cWhere := self:getWherePar(jParams) 

        If !EMPTY(cWhere)
            cQuery += cWhere    
        EndIf

        cQuery += " ORDER BY " + cOrdem

        __cEmpAnt  := cEmpAnt
        __cWhere   := cWhere
        cQuery     := ChangeQuery(cQuery) 
        oStatement := FwExecStatement():New(cQuery)

    EndIf

    // Captura e define os campos personalizados das tabelas SRA e SRF
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"SRA", "SRF", "SRH"})
    oStatement:SetUnsafe(1, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1 - inclusão de campos personalizados

    oStatement:SetString(2, ' ') //SRA.D_E_L_E_T_
    oStatement:SetIn(3, aStatus)
    oStatement:SetString(4, ' ') //SRF.D_E_L_E_T_
       
    For nI := 1 To Len(aParams)
        If Len(jParams[aParams[nI]]) > 0 .And. ( !Empty(jParams[aParams[nI], 1]) .or. aParams[nI] == "SITUACAO" )
            If(aParams[nI] $ "CATFUNC*SITUACAO")
                oStatement:SetIn(nPOrder++, IIf(aParams[nI] == "CATFUNC", aCategoria, aSituacoes))
            Else
                oStatement:SetString(nPOrder++, jParams[aParams[nI], 1]) 
            EndIf            
        EndIf
    Next nI

    cAlias := oStatement:OpenAlias()
    dbSelectArea( cAlias )

    While !(cAlias)->(Eof())

        dbSelectArea("SRA")
		SRA->(dbGoTo((cAlias)->RECNO))

        //Consiste Filiais e Acessos
        If cLastFil != ( cAlias )->RF_FILIAL
            cFilName := RTrim( FWEmpName(__cEmpAnt) ) + " / " + RTrim( FWFilRazSocial(, (cAlias)->RF_FILIAL) )
            cNameEmp := RTrim( FWEmpName(__cEmpAnt) )
            If !((cAlias)->RF_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA) 
                (cAlias)->( dbSkip() )
                Loop
            EndIf
            If cLastFil != ( cAlias )->RF_FILIAL .AND. xFilial("RCA", cLastFil) != xFilial("RCA", ( cAlias )->RF_FILIAL) 
				SetMnemonicos(NIL,NIL,.T.)
			EndIf
        EndIf

        If cLastCC != ( cAlias )->RA_CC
            cNameCC := RTrim( (cAlias)->RA_CC ) + " - " + RTrim( FDesc("CTT", (cAlias)->RA_CC, "CTT->CTT_DESC01") )
        EndIf

        cLastFil := ( cAlias )->RF_FILIAL
        cLastCC  := ( cAlias )->RA_CC

        If cProcAnt != ( cAlias )->RA_PROCES .OR. ( !lRCHComp .AND. cProcAnt == ( cAlias )->RA_PROCES .AND. cFilProc != ( cAlias )->RF_FILIAL )
            cFilProc  := ( cAlias )->RF_FILIAL
			cProcesso := ( cAlias )->RA_PROCES 
            lProc     := .F.
			cProcAnt  := cProcesso
			cRot 	  := fGetCalcRot('3')

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Carrega o periodo atual de calculo (aberto)                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			fGetLastPer( @cPeriodo, @cSemana , cProcesso, cRot , .T., .F., @cAnoMes )

			aPeriodo := {}

			If lProc := Empty(cPeriodo)
                FwLogMsg("WARN",, "SmartView",,, , STR0004 + " - " + STR0005, , ,) //"A configuração do período esta incorreta - Verifique o cadastro de períodos."
                (cAlias)->(DbSkip())
                Loop
			EndIf

            fCarPeriodo( cPeriodo, cRot, @aPeriodo, @lUltSemana, @nPosSem)
            
			If lProc := Len(aPeriodo) == 0
                FwLogMsg("WARN",, "SmartView",,, , STR0006 + " - " + STR0005, , ,) //"Não há roteiro de Férias cadastrado - Verifique o cadastro de períodos."
				(cAlias)->(DbSkip())
                Loop
			Else
		    	dDataDe  := aPeriodo[nPosSem,3]
		    	dDataAte := aPeriodo[nPosSem,4]
			EndIf
		EndIf

        If lProc
            (cAlias)->( dbSkip() )
            Loop
        EndIf
		
        fTab_Fer(@aTabFer)

		If len(aTabFer) <> 0
			nDiasDir := aTabFer[3]
		EndIf

        nDiasCalc := 0

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ Calculo dos Dias de Ferias Vencidas e a Vencer               ³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        nDiasVenc := nDiasProp := nDiasAbono := 0
        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ Calculo dias de ferias pela data de inicio das ferias progrmadas³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        nDiaVen	:= 0 									
        nDiaPro	:= 0

        aPerFerias := {}
        Calc_Fer(@aPerFerias,dDataRef,,,,,,.F., sToD((cAlias)->RF_DATABAS))	

        //Posicionar no periodo aberto atual
        nPosFer	:= aScan(aPerFerias,{ |X| X[8] == "1" .And.  sToD((cAlias)->RF_DATABAS) = X[1] })

        If nPosFer <= 0
            (cAlias)->(DbSkip())
            Loop
        EndIf
        
        dDIniP1   := dDIniP2 := dDIniP3 := CTOD("//")
        dDFimP1   := dDFimP2 := dDFimP3 := CTOD("//")
        nDiasP1   := nDiasP2 := nDiasP3 := 0
        nDiasAP1  := nDiasAP2 := nDiasAP3 := 0
        nPerc13   := 0
        lCatEsta  := (cAlias)->RA_CATFUNC $ "E/G"
        nDiasVenc := aPerFerias[nPosFer][3]
        nDiasProp := aPerFerias[nPosFer][4]
        lUnicoPer := .F.

        If !lMod2
            If nDiasVenc > 0 .And. aPerFerias[nPosFer][14] > 0
                nDiasVenc := Max(nDiasVenc - aPerFerias[nPosFer][14], 0)
            Elseif nDiasVenc == 0 .And. nDiasProp > 0 .And. aPerFerias[nPosFer][14] > 0
                nDiasProp := nDiasProp - aPerFerias[nPosFer][14]
            Endif
        EndIf

        If ! Empty((cAlias)->RF_DATAINI)
            nDiaVen := (cAlias)->RF_DFERVAT
        Endif

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ Calculo dias de  faltas                                      ³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        nDFaltaV := (cAlias)->RF_DFALVAT
        nDFaltaP := (cAlias)->RF_DFALAAT
        
        //Dt. Referência futura: Possui dias vencidos, não possui dias proporcionais.. converte faltas proporcionais em faltas vencidas
        If nDiasVenc > 0 .And. Empty(nDiasProp) 
            If Empty(nDFaltaV) .And. nDFaltaP > 0
                nDFaltaV := nDFaltaP
                nDFaltaP := 0
            EndIf
        EndIf

        TabFaltas(@nDFaltaV)
        lMetFalV := lMetadeFal
        TabFaltas(@nDFaltaP)
        lMetFalP := lMetadeFal
        If !lMetFalP
            nDFaltaP := (nDFaltaP / 30) * nDiasProp
        Endif

        dDtIProg1 := sToD((cAlias)->RF_DATAINI)
        dDtIProg2 := sToD((cAlias)->RF_DATINI2)
		dDtIProg3 := sToD((cAlias)->RF_DATINI3)
		nDiasFer1 := (cAlias)->RF_DFEPRO1
		nDiasFer2 := (cAlias)->RF_DFEPRO2
		nDiasFer3 := (cAlias)->RF_DFEPRO3
		nDiasAbo1 := (cAlias)->RF_DABPRO1
		nDiasAbo2 := (cAlias)->RF_DABPRO2
		nDiasAbo3 := (cAlias)->RF_DABPRO3
		lUnicoPer := If((nDiasFer1+nDiasAbo1) == nDiaVen , .T., lUnicoPer)
            
        If (cAlias)->RF_TEMABPE == "S" .And. !Empty((cAlias)->RF_DATAINI) .And. nDiasAbo1 == 0 .And. nDiasFer1 == 0
			nDiasAboN := Min(nDiaVen - (cAlias)->RF_DFERANT, aTabFer[3])
			nDiasAbo1 := ( nDiasAboN - If(!lMetFalV, nDFaltaV, nDiasAboN / 2 )) / 3
			nDiasAbo1 := Int(nDiasAbo1)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Se Ferias forem  gozadas em  um  unico periodo, proporcionaliza³
			//³ o abono pecuniario, se nao, utiliza o que for informado nos cam³
			//³ pos de dias de ferias e abono de cada  periodo                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		ElseIf (cAlias)->RF_TEMABPE == "S" .And. lUnicoPer
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Garantir que os dias de gozo e abono pecuniario estejam iguais ³
			//³ ao total de dias de ferias vencidas antes de fazer a proporcio-³
			//³ nalizacao dos dias de abono pecuniario em funcao das faltas no ³
			//³ periodo aquisitivo.                                            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nDiasAbo1 := int( ( nDiaVen - IF(!lMetFalV, nDFaltaV, nDiaVen / 2 )) / 3 )
		EndIf

        //--------------------------------------------------------------------//
        //  Impressao nTipFer = 1-Nao programadas, 2-Programadas e 3-Ambas //
        //--------------------------------------------------------------------//
        If nTipFer == 1 .AND.  ;			
            (((nDiasFer1 + nDiasFer2 + nDiasFer3 + nDiasAbo1 + nDiasAbo2 + nDiasAbo3) >= (IIf(Empty((cAlias)->RF_DATAINI),nDiasVenc,nDiaVen) + nDiasProp)) .OR.; //todos os dias estao programados
            (!Empty(dDtIProg1) .AND. !Empty(dDtIProg2) .AND. !Empty(dDtIProg3))) //possui todas as programacoes
            (cAlias)->(dbSkip())
            Loop
        ElseIf Empty(dDtIProg1) .And. Empty(dDtIProg2) .And. Empty(dDtIProg3) .And. nTipFer == 2
            (cAlias)->(dbSkip())
            Loop
        EndIf

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ Consiste Ferias Programdas no Intervalo Definido			 ³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        If (nTipFer == 2 .Or. (nTipFer == 3 .And. (!Empty(dDtIProg1) .Or. !Empty(dDtIProg2) .Or. !Empty(dDtIProg3)))) .And.;
            (dDtIProg1 < dProgDe .Or. dDtIProg1 > dProgAte) .And.;
            (dDtIProg2 < dProgDe .Or. dDtIProg2 > dProgAte) .And.;
            (dDtIProg3 < dProgDe .Or. dDtIProg3 > dProgAte)

            (cAlias)->(dbSkip())
            Loop
        EndIf

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ Periodo Aquisitivo                                           ³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        DFimPAqui := aPerFerias[nPosFer][2]
        DPRPAqui  := fCalcFimAq(DFimPAqui+1)
        
        If cPeriodo == AnoMes( dDataRef )
            nDiasCalc := self:getCalcSRH( cAlias, dDataRef )
            Iif( nDiasVenc > 0, nDiasVenc -= nDiasCalc, nDiasProp -= nDiasCalc )
        EndIf

		dLimdeal := DFimPAqui + nDIdeal						    	//-- Data limite Ideal
		dLimMax	 := DPRPAqui - nDiasMax 						    //-- Data Limite Maximo
		nFerVenc := nDiasVenc 										//-- Ferias Vencidas
		nFalVen	 := nDFaltaV										//-- Faltas Vencidas
		nFerProp := nDiasProp										//-- Ferias Proporcionais
		nFalProp := nDFaltaP										//-- Faltas Proporcionais
		nTotal	 := (nDiasVenc + nDiasProp - If(!lMetFalV, nDFaltaV, nDiasVenc / 2) - If(!lMetFalP, nDFaltaP, nDiasProp / 2)) //-- Total de Ferias
        nSaldo   := 0

        If nDiasCalc > 0 .And. nTotal == 0
            (cAlias)->( dbSkip() )
            Loop
        EndIf

        If (cAlias)->RF_PERC13S > 0
		    nPerc13	:= (cAlias)->RF_PERC13S
        EndIf

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³Campos de Programacao de ferias                               ³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        For nCnt1 := 1 To nQtPer
            dDtIniProg := If(nCnt1 == 1, dDtIProg1, If(nCnt1 == 2, dDtIProg2, dDtIProg3))
            nDiasAbono := If(nCnt1 == 1, nDiasAbo1, If(nCnt1 == 2, nDiasAbo2, nDiasAbo3))
            nDiasFePro := If(nCnt1 == 1, If(nDiasFer1 == 0, nDiaVen, If( lUnicoPer, (nDiaVen - If(!lMetFalV, nDFaltaV, nDiaVen / 2) - nDiasAbo1), nDiasFer1)), If(nCnt1 == 2, nDiasFer2, nDiasFer3))
            lDobro     := dDataBase > dLimMax
            nSaldo     += nDiasFePro + nDiasAbono
            //-- Abater Dias de Abono 
            If (cAlias)->RF_DFEPRO1 == 0  .And. (cAlias)->RF_DABPRO1 == 0 
                nDiasFePro -= ( (cAlias)->RF_DFERANT + If(!lMetFalV, nDFaltaV, (cAlias)->RF_DFERANT / 2) + nDiasAbono)
            EndIf

            nDiasFePro := Max(nDiasFePro, 0)
            nDiasFePro := Min(nDiasFePro, nDiasDir)

            //-- Periodo de gozo de ferias
            If !Empty(dDtIniProg) 
                If nDiasFePro > 0
                    dDataFim := dDtIniProg + (nDiasFePro - 1)
                ElseIf nDiaVen > 0
                    dDataFim := dDtIniProg + (nDiaVen - 1)
                EndIf
                
                lDobro := .F.
                //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                //³ Verifica se Deve Considerar dias 24/12, 25/12, 31/12 e 01/01 ³
                //| como licenca remunerada.                                     |
                //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                fChkLicRem( dDtIniProg, dDataFim, @nDMes12, @nDMes01, cDia2501)
                If nDMes12 + nDMes01 > 0
                    dDataFim += nDMes12 + nDMes01
                EndIf

                If !lMod2
                    If nCnt1 == 1
                        dDIniP1  := dDtIniProg
                        dDFimP1  := dDataFim
                        nDiasP1  := nDiasFePro
                        nDiasAP1 := nDiasAbono
                    ElseIf nCnt1 == 2
                        dDIniP2  := dDtIniProg
                        dDFimP2  := dDataFim
                        nDiasP2  := nDiasFePro
                        nDiasAP2 := nDiasAbono
                    Else
                        dDIniP3  := dDtIniProg
                        dDFimP3  := dDataFim
                        nDiasP3  := nDiasFePro
                        nDiasAP3 := nDiasAbono
                    EndIf
                EndIf
            EndIf
               
            If lMod2 .And. nSaldo <= nTotal .And. ((nTipFer == 1 .And. Empty(dDtIniProg) ) .Or. (nTipFer == 2 .And. !Empty(dDtIniProg) ) .Or. nTipFer == 3 )
                If Empty(dDtIniProg) 
                    dDataFim := dDtIniProg := CTOD("31/12/9999")
                    nDiasFePro := 0
                    nDiasAbono := 0
                EndIf

                jData := {;
                    "FILNAME": cFilName, ;
                    "NAMEEMP": cNameEmp, ;
                    "CODEMP": __cEmpAnt, ;
                    "RF_FILIAL": (cAlias)->RF_FILIAL, ;
                    "RF_MAT": (cAlias)->RF_MAT, ;
                    "RA_NOME": If(lOfusca, Replicate('*',30), RTrim( (cAlias)->RA_NOME )), ;
                    "ADMISSA": FwTimeStamp( 5, sToD((cAlias)->RA_ADMISSA), "00:00:00" ), ;
                    "RA_CC": RTrim( (cAlias)->RA_CC ), ;
                    "DESCCTT": cNameCC, ;
                    "ESTAG": lCatEsta, ;
                    "DBASEFER": FwTimeStamp( 5, sToD((cAlias)->RF_DATABAS), "00:00:00" ), ;
                    "PERAQUIFIM": FwTimeStamp( 5, DFimPAqui, "00:00:00" ), ;
                    "DLIMIDEAL": FwTimeStamp( 5, dLimdeal, "00:00:00" ), ;
                    "DLIMMAX": FwTimeStamp( 5, dLimMax, "00:00:00" ), ;
                    "FERVENC": nFerVenc, ;
                    "ABTFALTA": nFalVen, ;
                    "FERPROP": nFerProp, ;	
                    "FALPROP": nFalProp, ;
                    "DECTERC": nPerc13, ;
                    "DATINI1": FwTimeStamp( 5, dDtIniProg, "00:00:00" ), ;
                    "DATFIM1": FwTimeStamp( 5, dDataFim, "00:00:00" ), ;
                    "NDIASP1": nDiasFePro, ;
                    "NDIASAP1": nDiasAbono, ;
                    "DATINI2": FwTimeStamp( 5, CTOD("31/12/9999"), "00:00:00" ), ;
                    "DATFIM2": FwTimeStamp( 5, CTOD("31/12/9999"), "00:00:00" ), ;
                    "NDIASP2": nDiasP2, ;
                    "NDIASAP2": nDiasAP2, ;
                    "DATINI3": FwTimeStamp( 5, CTOD("31/12/9999"), "00:00:00" ), ;
                    "DATFIM3": FwTimeStamp( 5, CTOD("31/12/9999"), "00:00:00" ), ;
                    "NDIASP3": nDiasP3, ;
                    "NDIASAP3": nDiasAP3, ;
                    "TOTALFER": nTotal - nSaldo, ;
                    "FERDOBR": lDobro;
                }
                // Adiciona os campos customizados no JSON
                GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

                // Adiciona as informações da linha
                self:oData:appendData(jData)

            EndIf        
            If nTotal == nSaldo
                nSaldo++
            EndIf                
        Next nCnt1

        If !lMod2
            jData := {;
                "FILNAME": cFilName, ;
                "NAMEEMP": cNameEmp, ;
                "CODEMP": __cEmpAnt, ;
                "RF_FILIAL": (cAlias)->RF_FILIAL, ;
                "RF_MAT": (cAlias)->RF_MAT, ;
                "RA_NOME": If(lOfusca, Replicate('*',30), RTrim( (cAlias)->RA_NOME )), ;
                "ADMISSA": FwTimeStamp( 5, sToD((cAlias)->RA_ADMISSA), "00:00:00" ), ;
                "RA_CC": RTrim( (cAlias)->RA_CC ), ;
                "DESCCTT": cNameCC, ;
                "ESTAG": lCatEsta, ;
                "DBASEFER": FwTimeStamp( 5, sToD((cAlias)->RF_DATABAS), "00:00:00" ), ;
                "PERAQUIFIM": FwTimeStamp( 5, DFimPAqui, "00:00:00" ), ;
                "DLIMIDEAL": FwTimeStamp( 5, dLimdeal, "00:00:00" ), ;
                "DLIMMAX": FwTimeStamp( 5, dLimMax, "00:00:00" ), ;
                "FERVENC": nFerVenc, ;
                "ABTFALTA": nFalVen, ;
                "FERPROP": nFerProp, ;	
                "FALPROP": nFalProp, ;
                "DECTERC": nPerc13, ;
                "DATINI1": If(Empty(dDIniP1), FwTimeStamp( 5, CTOD("31/12/9999"), "00:00:00" ), FwTimeStamp( 5, dDIniP1, "00:00:00" )), ;
                "DATFIM1": If(Empty(dDFimP1), FwTimeStamp( 5, CTOD("31/12/9999"), "00:00:00" ), FwTimeStamp( 5, dDFimP1, "00:00:00" )), ;
                "NDIASP1": nDiasP1, ;
                "NDIASAP1": nDiasAP1, ;
                "DATINI2": If(Empty(dDIniP2), FwTimeStamp( 5, CTOD("31/12/9999"), "00:00:00" ), FwTimeStamp( 5, dDIniP2, "00:00:00" )), ;
                "DATFIM2": If(Empty(dDFimP2), FwTimeStamp( 5, CTOD("31/12/9999"), "00:00:00" ), FwTimeStamp( 5, dDFimP2, "00:00:00" )), ;
                "NDIASP2": nDiasP2, ;
                "NDIASAP2": nDiasAP2, ;
                "DATINI3": If(Empty(dDIniP3), FwTimeStamp( 5, CTOD("31/12/9999"), "00:00:00" ), FwTimeStamp( 5, dDIniP3, "00:00:00" )), ;
                "DATFIM3": If(Empty(dDFimP3), FwTimeStamp( 5, CTOD("31/12/9999"), "00:00:00" ), FwTimeStamp( 5, dDFimP3, "00:00:00" )), ;
                "NDIASP3": nDiasP3, ;
                "NDIASAP3": nDiasAP3, ;
                "TOTALFER": nTotal, ;
                "FERDOBR": lDobro;
            }
            // Adiciona os campos customizados no JSON
            GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

            // Adiciona as informações da linha
            self:oData:appendData(jData)
        EndIf    
                
        (cAlias)->(DbSkip())
    EndDo

    (cAlias)->(DbCloseArea())

    // Libera os objetos da memória
    jData := NIL
    FwFreeObj(jData)    
    
return self:oData
 
method getSchema() as object class GPER400TReportsBusinessObject

    self:oSchema:addProperty("RA_NOME", STR0007, "string", STR0007, "RA_NOME",,,, .F.)
    self:oSchema:addProperty("RA_CC", STR0008, "string", STR0008, "RA_CC",,,, .F.)
    self:oSchema:addProperty("RF_FILIAL", STR0009, "string", STR0009, "RF_FILIAL",,,, .F.)
    self:oSchema:addProperty("RF_MAT", STR0010, "string", STR0010, "Matricula",,,, .F.)
    self:oSchema:addProperty("FILNAME", STR0011, "string", STR0012, "FILNAME",,,, .F.) 
    self:oSchema:addProperty("DESCCTT", STR0013, "string", STR0014, "DESCCTT",,,, .F.) 
    self:oSchema:addProperty("NAMEEMP", STR0015, "string", STR0016, "NAMEEMP",,,, .F.)
    self:oSchema:addProperty("CODEMP", STR0017, "string", STR0018, "CODEMP",,,, .F.) 
    self:oSchema:addProperty("ADMISSA", STR0019, "date", STR0019, "ADMISSA",,,, .F.)
    self:oSchema:addProperty("DBASEFER", STR0020, "date", STR0020, "DBASEFER",,,, .F.)
    self:oSchema:addProperty("ESTAG", STR0021, "boolean", STR0021, "ESTAG",,,, .F.) 
    self:oSchema:addProperty("PERAQUIFIM", STR0022, "date", STR0023, "PERAQUIFIM",,,, .F.) 
    self:oSchema:addProperty("DLIMIDEAL", STR0024, "date", STR0025, "DLIMIDEAL",,,, .F.) 
    self:oSchema:addProperty("DLIMMAX", STR0026, "date", STR0027, "DLIMMAX",,,, .F.)
    self:oSchema:addProperty("FERVENC", STR0028, "number", STR0028, "FERVENC",,,, .F.)
    self:oSchema:addProperty("ABTFALTA", STR0029, "number", STR0030, "ABTFALTA",,,, .F.)
    self:oSchema:addProperty("FERPROP", STR0031, "number", STR0032, "FERPROP",,,, .F.)   
    self:oSchema:addProperty("FALPROP", STR0033, "number", STR0034, "FALPROP",,,, .F.)     
    self:oSchema:addProperty("DECTERC", STR0035, "number", STR0036, "DECTERC",,,, .F.)
    self:oSchema:addProperty("DATINI1", STR0037, "date", STR0038, "DATINI1",,,, .F.)
    self:oSchema:addProperty("DATINI2", STR0039, "date", STR0040, "DATINI2",,,, .F.)
    self:oSchema:addProperty("DATINI3", STR0041, "date", STR0042, "DATINI3",,,, .F.)
    self:oSchema:addProperty("DATFIM1", STR0043, "date", STR0044, "DATFIM1",,,, .F.)
    self:oSchema:addProperty("DATFIM2", STR0045, "date", STR0046, "DATFIM2",,,, .F.)
    self:oSchema:addProperty("DATFIM3", STR0047, "date", STR0048, "DATFIM3",,,, .F.)
    self:oSchema:addProperty("NDIASP1", STR0049, "number", STR0050, "NDIASP1",,,, .F.)
    self:oSchema:addProperty("NDIASP2", STR0051, "number", STR0052, "NDIASP2",,,, .F.)
    self:oSchema:addProperty("NDIASP3", STR0053, "number", STR0054, "NDIASP3",,,, .F.)
    self:oSchema:addProperty("NDIASAP1", STR0055, "number", STR0055, "NDIASAP1",,,, .F.)
    self:oSchema:addProperty("NDIASAP2", STR0056, "number", STR0056, "NDIASAP2",,,, .F.)
    self:oSchema:addProperty("NDIASAP3", STR0057, "number", STR0057, "NDIASAP3",,,, .F.)
    self:oSchema:addProperty("TOTALFER", STR0058, "number", STR0059,"TOTALFER",,,, .F.) 
    self:oSchema:addProperty("FERDOBR", STR0060, "boolean", STR0060, "FERDOBR",,,, .F.) 
    self:oSchema:addProperty("MOD", STR0084, "string", STR0084, "MOD",,,, .T.) 

    self:addParameter("FILDE", STR0061, "string", .F.) 
    self:addParameter("FILATE", STR0062, "string", .F.) 
    self:addParameter("MATDE", STR0063, "string", .F.) 
    self:addParameter("MATATE", STR0064, "string", .F.)
    self:addParameter("CCDE", STR0065, "string", .F.)
    self:addParameter("CCATE", STR0066, "string", .F.)
    self:addParameter("TPFER", STR0067, "number", .F.)
    self:addParameter("PROGDE", STR0068, "date", .F.) 
    self:addParameter("PROGATE", STR0069, "date", .F.) 
    self:addParameter("SITUACAO", STR0070, "string", .T.) 
    self:addParameter("CATFUNC", STR0071, "string", .T.) 
    self:addParameter("QTDPER", STR0072, "number", .F.) 
    self:addParameter("LIMMAX", STR0073, "number", .F.)
    self:addParameter("LIMIDEAL", STR0074, "number", .F.)   
    self:addParameter("DATAREF", STR0075, "date", .F.)
    self:addParameter("ORDER", STR0076, "string", .F.)  
    self:addParameter("TOTEMP", STR0077, "string", .F.)   

    //setCustomURL disponível na LIB 20231121.
    If FWLibVersion() >= "20231121" 
        //Combos
        self:setCustomURL("TPFER", "api/framework/treports/integratedprovider/v1/options/GP400RA/MV_PAR08", 1)
        self:setCustomURL("QTDPER", "/api/rh/smartview/v1/options/gpersv400/QTDPER", 1)
        self:setCustomURL("ORDER", "/api/rh/smartview/v1/options/gpersv400/ORDER", 1)
        self:setCustomURL("TOTEMP", "/api/rh/smartview/v1/options/gpersv400/TOTEMP", 1)

        //Loockups
        self:setCustomURL("FILDE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("FILATE", "api/rh/smartview/v1/options/GPEParams/getBranches", 2)
        self:setCustomURL("MATDE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("MATATE", "api/framework/v1/genericLookupService/smartview/SRA", 2)
        self:setCustomURL("CCDE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("CCATE", "api/framework/v1/genericLookupService/smartview/CTT", 2)
        self:setCustomURL("SITUACAO", "api/framework/v1/genericLookupService/smartview/31", 2)
        self:setCustomURL("CATFUNC", "api/framework/v1/genericLookupService/smartview/28", 2)
    EndIf         

return self:oSchema

/*/{Protheus.doc} getCalcSRH
Metodo que retorna a quantidade de dias calculados no mês.
@type  Metodo
@author Allyson Luiz Mesashi
/*/
method getCalcSRH( cAlias as character, dDataRef as date ) as numeric class GPER400TReportsBusinessObject

    local aAreaSRH as array
    local nDiasCalc as numeric
    
    aAreaSRH := SRH->( GetArea() )//RH_FILIAL+RH_MAT+DTOS(RH_DATABAS)+DTOS(RH_DATAINI)
    SRH->( dbSetOrder(1) )

    If SRH->( dbSeek( (cAlias)->RF_FILIAL+(cAlias)->RF_MAT+(cAlias)->RF_DATABAS ) )
        While SRH->( !Eof() ) .And. SRH->RH_FILIAL+SRH->RH_MAT+dToS( SRH->RH_DATABAS ) == (cAlias)->RF_FILIAL+(cAlias)->RF_MAT+(cAlias)->RF_DATABAS
            If AnoMes( SRH->RH_DATAINI ) == AnoMes( dDataRef )
                nDiasCalc += SRH->RH_DFERIAS+SRH->RH_DABONPE
            EndIf
            SRH->( dbSkip() )
        End
    EndIf

    RestArea( aAreaSRH )

return nDiasCalc

/*/{Protheus.doc} getComboBox
Metodo que retorna o combobox dos valores de ordem para o parâmetro ORDER e TOTEMP
@type  Metodo
@author Bruno Costa
@return Variant, Retorno nulo pré-fixado
@obs Path parameters: param (nome do parâmetro solicitado)
/*/
Method getComboBox() as Variant Class GPER400TReportsBusinessObject

    local jPath     as JSON     
    local jResponse as JSON      
    local jItem     as JSON      
    local nValues   as Numeric   
    local cParam    As Character 
    local aValues   as Array   

    jPath     := oRest:getPathParamsRequest()
    jResponse := JsonObject():new()
    jItem     := NIL
    nValues   := 0
    cParam    := ""
    aValues   := {}

     If (jPath != NIL)
        cParam := jPath["param"]
    EndIf

    jResponse["data"] := {}

    // Monta o vetor de valores do parâmetro
    If cParam == "ORDER"
        AAdd(aValues, EncodeUTF8(STR0078)) //C.Custo
        AAdd(aValues, EncodeUTF8(STR0010)) //Filial
        AAdd(aValues, EncodeUTF8(STR0007)) //Matrícula
        AAdd(aValues, EncodeUTF8(STR0079)) //C.Custo + Nome
        AAdd(aValues, EncodeUTF8(STR0080)) //Filial + Dt.Base
        AAdd(aValues, EncodeUTF8(STR0081)) //Filial + Dt.Inicio
    ElseIf cParam == "TOTEMP"
        AAdd(aValues, EncodeUTF8(STR0082)) //Sim
        AAdd(aValues, EncodeUTF8(STR0083)) //Não
    ElseIf cParam =="QTDPER"
        aAdd(aValues, EncodeUTF8(STR0085)) //1 Período
        aAdd(aValues, EncodeUTF8(STR0086)) //2 Períodos 
        aAdd(aValues, EncodeUTF8(STR0087)) //3 Períodos
    EndIf

    If (Len(aValues) > 0)
        // Percorre os valores adicionando cada item no JSON de resposta
        For nValues := 1 To Len(aValues)
            jItem := {;
                "key":   CValToChar(nValues),;
                "label": aValues[nValues];
            }
            AAdd(jResponse["data"], jItem)
        Next nValues
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Libera os objetos da memória
    FwFreeObj(jPath)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
    FwFreeArray(aValues)
Return NIL

/*/{Protheus.doc} getWherePar
Metodo que retorna a consulta conforme os parâmetros
@type  Metodo
@author Bruno Costa
@return character
@since 07/06/2024
/*/
method getWherePar( jPar as json) as character class GPER400TReportsBusinessObject
    
    Local cRet as character

    If !Empty(jPar['FILDE', 1])
		cRet += " AND SRA.RA_FILIAL >= ?"
	EndIf
    If !Empty(jPar['FILATE', 1])
		cRet += "  AND SRA.RA_FILIAL <= ?"
	EndIf 
    If !Empty(jPar['MATDE', 1])
		cRet += " AND SRA.RA_MAT  >= ?"
	EndIf
	If !Empty(jPar['MATATE', 1])
		cRet += " AND SRA.RA_MAT <= ?"
	EndIf
    If !Empty(jPar['CCDE', 1])
		cRet += " AND SRA.RA_CC >= ?"
	EndIf
    If !Empty(jPar['CCATE', 1])
		cRet += " AND SRA.RA_CC <= ?"
	EndIf
    If !Empty(jPar['CATFUNC'])
        cRet += " AND SRA.RA_CATFUNC IN (?)"
    EndIf 
    If !Empty(jPar['SITUACAO'])
        cRet += " AND SRA.RA_SITFOLH IN (?)"
    EndIf
Return cRet
