#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.pon.ponr020.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} PONR020TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Eventos Apontados no Período.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 05/03/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPON", tables="SRA, CTT, SRJ, SX5, SPH, SPC", customTables="SRA, CTT, SRJ, SR6", name="Relatório Eventos Apontados no Período", country="ALL", initialRelease="12.1.2310")
Class PONR020TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} PONR020TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 05/03/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class PONR020TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Eventos Apontados no Período"
Return self

/*/{Protheus.doc} PONR020TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 05/03/2024
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class PONR020TReportsBusinessObject
Return STR0003 // "Este objeto de negócio traz as informações do Relatório de Eventos Apontados no Período."

/*/{Protheus.doc} PONR020TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 05/03/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class PONR020TReportsBusinessObject
    // Declaração das variáveis locais
    Local cAlias        As Character // Alias do arquivo temporário principal
    Local cFilExec      As Character // Nome da filial de execução
    Local cEmpExec      As Character // Nome da empresa de execução
	Local cCateg		As Character // Categoria do funcionário
    Local cAccessSRA    As Character // Macro execução para validar acesso à tabela SRA
    Local cAccessSPH    As Character // Macro execução para validar acesso à tabela SPH
    Local cAccessSPC    As Character // Macro execução para validar acesso à tabela SPC
    Local cValidFil     As Character // Filiais válidas
    Local cPrevBranch   As Character // Filial anterior
    Local cPrevShift    As Character // Turno anterior
    Local cCompBranch   As Character // Filial para comparação
    Local cAuxBranch    As Character // Filial auxiliar
    Local cAuxShift     As Character // Turno auxiliar
    Local cAuthorized   As Character // Códigos de eventos autorizados
    Local cEvent        As Character // Evento auxiliar para comparação
    Local cFundsType    As Character // Parâmetro Tipo Verbas
    Local cPaid         As Character // Parâmetro Abonados
    Local cDayToDay     As Character // Parâmetro Dia a dia
    Local cHoursType    As Character // Parâmetro Horas Em
    Local cCustomFields As Character // Campos customizados no formato SQL
    Local aInfo         As Array     // Informações da filial
    Local aEvents       As Array     // Parâmetro Eventos
    Local aDataAuth     As Array     // Informações dos eventos autorizados apontados no período
    Local aDataUnauth   As Array     // Informações dos eventos não autorizados apontados no período
    Local aFldObfusc    As Array     // Informações dos campos para verificar ofuscamento
    Local aObfuscate    As Array     // Informações para ofuscar campos
	Local aPDFields     As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aAllFields    As Array     // Estrutura de todos os campos do ON
    Local nData         As Numeric   // Contador para percorrer as informações dos eventos
    Local nMaxLen       As Numeric   // Contador que armazena o maior tamanho de vetor entre autorizados e não autorizados
    Local lBranchChange As Logical   // Indica se houve troca de filial
    Local lAccumulated  As Logical   // Indica se a impressão é por acumulado
    Local lObfName      As Logical   // Se o campo RA_NOME vai ser ofuscado
    Local lObfCodRole   As Logical   // Se o campo RA_CODFUNC vai ser ofuscado
    Local lObfDescRole  As Logical   // Se o campo RA_DESCFUN vai ser ofuscado
    Local lObfuscated   As Logical   // Flag que indica se tem algum campo para ofuscar
    Local dInitialPer   As Date      // Parâmetro Data inicial do período
    Local dFinalPer     As Date      // Parâmetro Data final do período
    Local dInitial      As Date      // Data auxiliar inicial do período
    Local dFinal        As Date      // Data auxiliar final do período
    Local jEvents       As JSON      // Eventos apontados no período
    Local jData         As JSON      // Informações a serem adicionadas no ON

    // Inicialização das variáveis locais
    cAlias        := ""
    cFilExec      := AllTrim(FwFilialName())
    cEmpExec      := AllTrim(FwEmpName(cEmpAnt))
    cAccessSRA    := &("{|| " + ChkRH("PONR020", "SRA", "2") + "}")
    cAccessSPH    := &("{|| " + ChkRH("PONR020", "SPH", "2") + "}")
    cAccessSPC    := &("{|| " + ChkRH("PONR020", "SPC", "2") + "}")
    cValidFil     := fValidFil()
    cPrevBranch   := ""
    cPrevShift    := ""
    cCompBranch   := ""
    cAuxBranch    := ""
    cAuxShift     := ""
    cAuthorized   := ""
    cEvent        := ""
    cFundsType    := ""
    cPaid         := ""
    cDayToDay     := ""
    cHoursType    := ""
    cCustomFields := ""
    aInfo         := {}
    aEvents       := {}
    aDataAuth     := {}
    aDataUnauth   := {}
    aFldObfusc    := FwProtectedDataUtil():UsrNoAccessFieldsInList({"RA_NOME", "RA_CODFUNC", "RA_DESCFUN"})
    aObfuscate    := IIf(FindFunction("ChkOfusca"), ChkOfusca(), {.T.,.F.}) // [1] Acesso | [2]Ofusca
    aPDFields     := {}
    aAllFields    := {}
    nData         := 0
    nMaxLen       := 0
    lBranchChange := .F.
    lAccumulated  := .F.
    lObfuscated   := .F.
    lObfName      := IIf(aObfuscate[2] .and. AScan(aFldObfusc, {|x| x:cField == "RA_NOME"})    > 0, FwProtectedDataUtil():IsFieldInList("RA_NOME"),    .F.)
    lObfCodRole   := IIf(aObfuscate[2] .and. AScan(aFldObfusc, {|x| x:cField == "RA_CODFUNC"}) > 0, FwProtectedDataUtil():IsFieldInList("RA_CODFUNC"), .F.)
    lObfDescRole  := IIf(aObfuscate[2] .and. AScan(aFldObfusc, {|x| x:cField == "RA_DESCFUN"}) > 0, FwProtectedDataUtil():IsFieldInList("RA_DESCFUN"), .F.)
    dInitialPer   := CToD("")
    dFinalPer     := CToD("")
    dInitial      := CToD("")
    dFinal        := CToD("")
    jEvents       := JSONObject():New()
    jData         := NIL

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Cria o arquivo temporário
    cAlias := QueryDef(oFilter, @dInitialPer, @dFinalPer, @lAccumulated, @aEvents, @cFundsType, @cPaid, @cDayToDay, @cHoursType, self)

    // Se o tipo de impressão for acumulado, o intervalo de datas será conforme os parâmetros,
    // caso contrário será de acordo com o parâmetro MV_PAPONTA
    If (lAccumulated)
        dInitial := dInitialPer
        dFinal   := dFinalPer
    Else
        PerAponta(@dInitial, @dFinal)
    EndIf

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))
        // Se o funcionário está demitido, verifica se a data demissão é anterior à data do período inicial
        If (!Empty((cAlias)->RA_DEMISSA) .and. SToD((cAlias)->RA_DEMISSA) < dInitial)
            (cAlias)->(DBSkip())
            LOOP
        EndIf

        // Posiciona no funcionário para busca dos abonos na função fAbonos()
        SRA->(DBGoTO((cAlias)->SRA_RECNO))

        // Reinicia o valor das variáveis
        lBranchChange := .F.
        aDataAuth     := {}
        aDataUnauth   := {}

        // Verifica se houve troca de filial
        If ((cAlias)->RA_FILIAL != cPrevBranch)
            // Reinicia o valor das variáveis e indica que houve troca de filial
            cAuthorized   := ""
            cPrevBranch   := (cAlias)->RA_FILIAL
            lBranchChange := .T.

            // Posiciona na SP9 (Eventos) e captura os eventos autorizados
            DBSelectArea("SP9")
            DBSetOrder(1) // P9_FILIAL + P9_CODIGO
            If (MsSeek(FwXFilial("SP9", cPrevBranch)))
                cCompBranch := SP9->P9_FILIAL
                While (!SP9->(EOF()) .and. SP9->P9_FILIAL == cCompBranch)
                    If (SubStr(SP9->P9_IDPON, 4, 1) == "A")
                        cAuthorized += SP9->P9_CODIGO + "A"
                    EndIf

                    // Pula para o próximo registro
                    SP9->(DBSkip())
                EndDo

                // Posiciona na SP6 (Abonos) e captura os eventos autorizados
                DBSelectArea("SP6")
                DBGoTop()
                While (!SP6->(EOF()))
                    If (!Empty(SP6->P6_EVENTO))
                        If (SP9->(MsSeek(FwXFilial("SP9", (cAlias)->RA_FILIAL) + SP6->P6_EVENTO, .F.)))
                            cAuthorized += SP6->P6_EVENTO + "A"
                        EndIf
                    EndIf

                    // Pula para o próximo registro
                    SP6->(DBSkip())
                End
            EndIf
        EndIf

        // Carregar as Horas Extras se houver quebra de filial ou de turno
        If (lBranchChange .or. (cAlias)->RA_TNOTRAB != cPrevShift)
            // Varre a tabela SP4 (Horas Extras)
            If (SP4->(MsSeek((cAuxBranch  := FwXFilial("SP4", (cAlias)->RA_FILIAL)) + (cAuxShift := (cAlias)->RA_TNOTRAB), .F.)) .or. ;
                SP4->(MsSeek((cAuxBranch  := FwXFilial("SP4", (cAlias)->RA_FILIAL)) + (cAuxShift := Space(3)),             .F.)))
                While (!SP4->(EOF()) .and. cAuxBranch + cAuxShift == SP4->(P4_FILIAL + P4_TURNO))
                    If (!Empty(SP4->P4_CODAUT))
                        If (SP9->(MsSeek(FwXFilial("SP9", (cAlias)->RA_FILIAL) + SP4->P4_CODAUT, .F.)))
                            cAuthorized += SP4->P4_CODAUT + "A"
                        EndIf
                    EndIf

                    // Pula para o próximo registro
                    SP4->(DBSkip())
                End
            EndIf

            cPrevShift := (cAlias)->RA_TNOTRAB
        EndIf

        // Verifica o acesso à tabela SRA, se a filial é válida e se encontrou as informações da filial
        If ((!Empty(aInfo) .and. (cAlias)->RA_FILIAL == cPrevBranch) .or. (Eval(cAccessSRA) .and. (cAlias)->RA_FILIAL $ cValidFil .and. fInfo(@aInfo, (cAlias)->RA_FILIAL)))
            If (lAccumulated)
                DBSelectArea("SPH")
                If (MsSeek((cAlias)->(RA_FILIAL + RA_MAT)))
                    While (!EOF() .and. SPH->(PH_FILIAL + PH_MAT) == (cAlias)->(RA_FILIAL + RA_MAT))
                        // Captura o código do evento
                        cEvent := Trim(IIf(!Empty(SPH->PH_PDI), SPH->PH_PDI, SPH->PH_PD))

                        // Listar apenas os eventos selecionados no parâmetro
                        If (Len(aEvents) > 0)
                            If (AScan(aEvents, {|x| Trim(x) == cEvent}) <= 0)
                                SPH->(DBSkip())
                                LOOP
                            EndIf
                        EndIf

                        // Valida se está dentro do intervalo de datas
                        If (SPH->PH_DATA >= dInitial .and. SPH->PH_DATA <= dFinal)
                            If (SPH->PH_QTABONO <= 0 .or. SPH->PH_QTABONO > 0 .and. cPaid == "1")
                                // Consiste controle de acessos
                                If (SPH->(Eval(cAccessSPH)))
                                    // Códigos autorizados
                                    If (cEvent $ cAuthorized)
                                        If (cFundsType $ "1|3")
                                            fSoma(@aDataAuth, "SPH", cDayToDay == "2", cAuthorized)
                                        EndIf

                                    // Códigos não autorizados
                                    Else
                                        If (cFundsType $ "2|3")
                                            fSoma(@aDataUnauth, "SPH", cDayToDay == "2", cAuthorized)
                                        EndIf
                                    EndIf
                                EndIf
                            EndIf
                        EndIf

                        SPH->(DBSkip())
                    End
                EndIf
            Else
                DBSelectArea("SPC")
                If (MsSeek((cAlias)->(RA_FILIAL + RA_MAT)))
                    While (!EOF() .and. SPC->(PC_FILIAL + PC_MAT) == (cAlias)->(RA_FILIAL + RA_MAT))
                        // Captura o código do evento
                        cEvent := Trim(IIf(!Empty(SPC->PC_PDI), SPC->PC_PDI, SPC->PC_PD))

                        // Listar apenas os eventos selecionados nos parâmetros
                        If (Len(aEvents) > 0)
                            If (AScan(aEvents, {|x| Trim(x) == cEvent}) <= 0)
                                SPC->(DBSkip())
                                LOOP
                            EndIf
                        EndIf

                        If (SPC->PC_DATA >= dInitialPer .and. SPC->PC_DATA <= dFinalPer)
                            If (SPC->PC_QTABONO <= 0 .or. SPC->PC_QTABONO > 0 .and. cPaid == "1")
                                // Consiste controle de acessos e filiais válidas
                                If (SPC->(Eval(cAccessSPC)))
                                    // Códigos autorizados
                                    If (cEvent $ cAuthorized)
                                        If (cFundsType $ "1|3")
                                            fSoma(@aDataAuth, "SPC", cDayToDay == "2", cAuthorized)
                                        EndIf

                                    // Códigos não autorizados
                                    Else
                                        If (cFundsType $ "2|3")
                                            fSoma(@aDataUnauth, "SPC", cDayToDay == "2", cAuthorized)
                                        EndIf
                                    EndIf
                                EndIf
                            EndIf
                        EndIf

                        SPC->(DBSkip())
                    End
                EndIf
            EndIf

            // Se não tiver informações de eventos apontados, pula para o próximo registro
            If (Len(aDataAuth) == 0 .and. Len(aDataUnauth) == 0)
                (cAlias)->(DBSkip())
                LOOP
            EndIf

            // Reinicia o valor do vetor de eventos
            jEvents["data"] := {}

            // Captura o tamanho do maior vetor e depois cria um vetor com o mesmo tamanho
            nMaxLen := Max(Len(aDataAuth), Len(aDataUnauth))
            For nData := 1 To nMaxLen
                AAdd(jEvents["data"], {;
                    "EventCodeAuth":          NIL,;
                    "EventDescriptionAuth":   NIL,;
                    "DayAuth":                NIL,;
                    "CalculatedHoursAuth":    NIL,;
                    "InformedHoursAuth":      NIL,;
                    "EventCodeUnauth":        NIL,;
                    "EventDescriptionUnauth": NIL,;
                    "DayUnauth":              NIL,;
                    "CalculatedHoursUnauth":  NIL,;
                    "InformedHoursUnauth":    NIL;
                })
            Next nData

            // Percorre os eventos autorizados adicionando as informações ao JSON de eventos
            // aDataAuth: {Código do evento, Horas informadas, Horas calculadas, Data do apontamento}
            For nData := 1 To Len(aDataAuth)
                jEvents["data"][nData]["EventCodeAuth"]        := Trim(aDataAuth[nData][1])
                jEvents["data"][nData]["EventDescriptionAuth"] := Trim(DescPdPon(aDataAuth[nData][1]))
                jEvents["data"][nData]["DayAuth"]              := Trim(IIf(cDayToDay == "2", StrZero(Day(aDataAuth[nData][4]), 2), ""))
                jEvents["data"][nData]["CalculatedHoursAuth"]  := Trim(Transform(IIf(cHoursType == "2", fConvHr(aDataAuth[nData][3], "D"), aDataAuth[nData][3]), "999999.99"))
                jEvents["data"][nData]["InformedHoursAuth"]    := Trim(Transform(IIf(cHoursType == "2", fConvHr(aDataAuth[nData][2], "D"), aDataAuth[nData][2]), "999999.99"))
            Next nData

            // Percorre os eventos não autorizados adicionando as informações ao JSON de eventos
            // aDataUnauth: {Código do evento, Horas informadas, Horas calculadas, Data do apontamento}
            For nData := 1 To Len(aDataUnauth)
                jEvents["data"][nData]["EventCodeUnauth"]        := Trim(aDataUnauth[nData][1])
                jEvents["data"][nData]["EventDescriptionUnauth"] := Trim(DescPdPon(aDataUnauth[nData][1]))
                jEvents["data"][nData]["DayUnauth"]              := Trim(IIf(cDayToDay == "2", StrZero(Day(aDataUnauth[nData][4]), 2), ""))
                jEvents["data"][nData]["CalculatedHoursUnauth"]  := Trim(Transform(IIf(cHoursType == "2", fConvHr(aDataUnauth[nData][3], "D"), aDataUnauth[nData][3]), "999999.99"))
                jEvents["data"][nData]["InformedHoursUnauth"]    := Trim(Transform(IIf(cHoursType == "2", fConvHr(aDataUnauth[nData][2], "D"), aDataUnauth[nData][2]), "999999.99"))
            Next nData

            For nData := 1 To nMaxLen
                cCateg	:= (cAlias)->(fDesc("SX5", "28" + RA_CATFUNC, "X5_DESCRI", 11, RA_FILIAL))

                // Adiciona as informações da linha
                jData := {;
                    "FilialExec":             Trim(cFilExec),;
                    "EmpresaExec":            Trim(cEmpExec),;
                    "Branch":                 Trim((cAlias)->RA_FILIAL),;
                    "CostCenterCode":         Trim((cAlias)->RA_CC),;
                    "CostCenterDesc":         Trim((cAlias)->CTT_DESC01),;
                    "EmployeeCode":           Trim((cAlias)->RA_MAT),;
                    "EmployeeName":           Trim(IIf(lObfName,     Replicate("*", 15), (cAlias)->RA_NOME)),;
                    "EmployeeRoleCode":       Trim(IIf(lObfCodRole,  Replicate("*", 15), (cAlias)->RA_CODFUNC)),;
                    "EmployeeRoleDesc":       Trim(IIf(lObfDescRole, Replicate("*", 15), (cAlias)->RJ_DESC)),;
                    "EmployeeCard":           Trim((cAlias)->RA_CHAPA),;
                    "EmployeeCat":            Trim(cCateg),;
                    "EmployeeShiftCode":      Trim((cAlias)->RA_TNOTRAB),;
                    "EmployeeShiftDesc":      Trim((cAlias)->R6_DESC),;
                    "EmployeeHoursMonth":     cValToChar((cAlias)->RA_HRSMES),;
                    "OriginPlatform":         "Protheus",;
                    "EventCodeAuth":          jEvents["data"][nData]["EventCodeAuth"],;
                    "EventDescriptionAuth":   jEvents["data"][nData]["EventDescriptionAuth"],;
                    "DayAuth":                jEvents["data"][nData]["DayAuth"],;
                    "CalculatedHoursAuth":    jEvents["data"][nData]["CalculatedHoursAuth"],;
                    "InformedHoursAuth":      jEvents["data"][nData]["InformedHoursAuth"],;
                    "EventCodeUnauth":        jEvents["data"][nData]["EventCodeUnauth"],;
                    "EventDescriptionUnauth": jEvents["data"][nData]["EventDescriptionUnauth"],;
                    "DayUnauth":              jEvents["data"][nData]["DayUnauth"],;
                    "CalculatedHoursUnauth":  jEvents["data"][nData]["CalculatedHoursUnauth"],;
                    "InformedHoursUnauth":    jEvents["data"][nData]["InformedHoursUnauth"];
                }

                // Adiciona os campos customizados no JSON
                GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

                // Adiciona as informações da linha
                self:oData:appendData(jData)
            Next nData
        EndIf

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    // Limpa os vetores e objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jEvents)
    FwFreeArray(aInfo)
    FwFreeArray(aEvents)
    FwFreeArray(aDataAuth)
    FwFreeArray(aDataUnauth)
    FwFreeArray(aFldObfusc)
    FwFreeArray(aObfuscate)
    FwFreeArray(aAllFields)
    FwFreeArray(aPDFields)
Return self:oData

/*/{Protheus.doc} PONR020TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 05/03/2024
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class PONR020TReportsBusinessObject
    // Define os parâmetros do relatório
    self:oSchema:addParameter("PeriodFrom",         "Período Inicial", "date",   .F.)
    self:oSchema:addParameter("PeriodTo",           "Período Final",   "date",   .F.)
    self:oSchema:addParameter("BranchCodeFrom",     "Filial De",       "string", .F.)
    self:oSchema:addParameter("BranchCodeTo",       "Filial Até",      "string", .F.)
    self:oSchema:addParameter("CostCenterCodeFrom", "C.C. De",         "string", .F.)
    self:oSchema:addParameter("CostCenterCodeTo",   "C.C. Até",        "string", .F.)
    self:oSchema:addParameter("ShiftCodeFrom",      "Turno De",        "string", .F.)
    self:oSchema:addParameter("ShiftCodeTo",        "Turno Até",       "string", .F.)
    self:oSchema:addParameter("EmployeeCodeFrom",   "Matrícula De",    "string", .F.)
    self:oSchema:addParameter("EmployeeCodeTo",     "Matrícula Até",   "string", .F.)
    self:oSchema:addParameter("EmployeeNameFrom",   "Nome De",         "string", .F.)
    self:oSchema:addParameter("EmployeeNameTo",     "Nome Até",        "string", .F.)
    self:oSchema:addParameter("EmployeeSituation",  "Situações",       "string", .T.)
    self:oSchema:addParameter("EmployeeCategory",   "Categorias",      "string", .T.)
    self:oSchema:addParameter("DayToDay",           "Dia a dia",       "string", .F.)
    self:oSchema:addParameter("ReportType",         "Tipo do rel.",    "string", .F.)
    self:oSchema:addParameter("Paid",               "Abonados",        "string", .F.)
    self:oSchema:addParameter("FundsType",          "Tipo Verbas",     "string", .F.)
    self:oSchema:addParameter("PrintType",          "Impressão",       "string", .F.)
    self:oSchema:addParameter("Events",             "Eventos",         "string", .T.)
    self:oSchema:addParameter("HoursType",          "Horas Em",        "string", .F.)
    self:oSchema:addParameter("BreakBy",            "Quebra Por",      "string", .F.)

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("BranchCodeFrom",     "/api/rh/smartview/v1/options/GPEParams/getBranches",           2)
    self:setCustomURL("BranchCodeTo",       "/api/rh/smartview/v1/options/GPEParams/getBranches",           2)
    self:setCustomURL("CostCenterCodeFrom", "/api/framework/v1/genericLookupService/smartview/CTT",         2)
    self:setCustomURL("CostCenterCodeTo",   "/api/framework/v1/genericLookupService/smartview/CTT",         2)
    self:setCustomURL("ShiftCodeFrom",      "/api/framework/v1/genericLookupService/smartview/SR6",         2)
    self:setCustomURL("ShiftCodeTo",        "/api/framework/v1/genericLookupService/smartview/SR6",         2)
    self:setCustomURL("EmployeeCodeFrom",   "/api/framework/v1/genericLookupService/smartview/SRA",         2)
    self:setCustomURL("EmployeeCodeTo",     "/api/framework/v1/genericLookupService/smartview/SRA",         2)
    self:setCustomURL("EmployeeSituation",  "/api/rh/smartview/v1/options/GPEParams/getSX5/31",             2)
    self:setCustomURL("EmployeeCategory",   "/api/rh/smartview/v1/options/GPEParams/getSX5/28",             2)
    self:setCustomURL("DayToDay",           "/api/rh/smartview/v1/options/GPEParams/getSNOptions?option=2", 1)
    self:setCustomURL("ReportType",         "/api/rh/smartview/v1/options/PONR020/ReportType",              1)
    self:setCustomURL("Paid",               "/api/rh/smartview/v1/options/GPEParams/getSNOptions",          1)
    self:setCustomURL("FundsType",          "/api/rh/smartview/v1/options/PONR020/FundsType",               1)
    self:setCustomURL("PrintType",          "/api/rh/smartview/v1/options/PONR020/PrintType",               1)
    self:setCustomURL("Events",             "/api/framework/v1/genericLookupService/smartview/SP9",         2)
    self:setCustomURL("HoursType",          "/api/rh/smartview/v1/options/PONR020/HoursType",               1)
    self:setCustomURL("BreakBy",            "/api/rh/smartview/v1/options/PONR020/BreakBy",                 1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",             "Filial de Execução",                 "string", "Filial Exec",                        "FilialExec",             NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EmpresaExec",            "Empresa de Execução",                "string", "Empresa Exec",                       "EmpresaExec",            NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("Branch",                 "Filial",                             "string", "Filial",                             "RA_FILIAL",              NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("CostCenterCode",         "Cód. Centro de Custo",               "string", "Cód. Centro de Custo",               "RA_CC",                  NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("CostCenterDesc",         "Desc. Centro de Custo",              "string", "Desc. Centro de Custo",              "CTT_DESC01",             NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("EmployeeCode",           "Matrícula",                          "string", "Matrícula",                          "RA_MAT",                 NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("EmployeeName",           "Nome do Func.",                      "string", "Nome do Func.",                      "RA_NOME",                NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("EmployeeRoleCode",       "Cód Função do Func.",                "string", "Cód Função do Func.",                "RA_CODFUNC",             NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("EmployeeRoleDesc",       "Desc. Função do Func.",              "string", "Desc. Função do Func.",              "RJ_DESC",                NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("EmployeeCard",           "Chapa do Func.",                     "string", "Chapa do Func.",                     "RA_CHAPA",               NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("EmployeeCat",            "Categ. do Func.",                    "string", "Categ. do Func.",                    "RA_CATFUNC",             NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("EmployeeShiftCode",      "Cód. Turno do Func.",                "string", "Cód. Turno do Func.",                "RA_TNOTRAB",             NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("EmployeeShiftDesc",      "Desc. Turno do Func.",               "string", "Desc. Turno do Func.",               "R6_DESC",                NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("EmployeeHoursMonth",     "Horas/Mês do Func.",                 "string", "Horas/Mês do Func.",                 "RA_HRSMES",              NIL, NIL, NIL, .T.)
    self:oSchema:addProperty("OriginPlatform",         "Plataforma origem",                  "string", "Plataforma origem",                  "originPlatform",         NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EventCodeAuth",          "Código do evento Autorizado",        "string", "Código do evento Autorizado",        "EventCodeAuth",          NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EventDescriptionAuth",   "Descrição do evento Autorizado",     "string", "Descrição do evento Autorizado",     "EventDescriptionAuth",   NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("DayAuth",                "Dia Autorizado",                     "string", "Dia Autorizado",                     "DayAuth",                NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("CalculatedHoursAuth",    "Horas calculadas Autorizado",        "string", "Horas calculadas Autorizado",        "CalculatedHoursAuth",    NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("InformedHoursAuth",      "Horas informadas Autorizado",        "string", "Horas informadas Autorizado",        "InformedHoursAuth",      NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EventCodeUnauth",        "Código do evento Não Autorizado",    "string", "Código do evento Não Autorizado",    "EventCodeUnauth",        NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("EventDescriptionUnauth", "Descrição do evento Não Autorizado", "string", "Descrição do evento Não Autorizado", "EventDescriptionUnauth", NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("DayUnauth",              "Dia Não Autorizado",                 "string", "Dia Não Autorizado",                 "DayUnauth",              NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("CalculatedHoursUnauth",  "Horas calculadas Não Autorizado",    "string", "Horas calculadas Não Autorizado",    "CalculatedHoursUnauth",  NIL, NIL, NIL, .F.)
    self:oSchema:addProperty("InformedHoursUnauth",    "Horas informadas Não Autorizado",    "string", "Horas informadas Não Autorizado",    "InformedHoursUnauth",    NIL, NIL, NIL, .F.)
Return self:oSchema

/*/{Protheus.doc} QueryDef
    Define a query de busca dos registros.
    @type Function
    @version 12.1.2310
    @author arthur.sales
    @since 05/03/2024
    @param oFilter, Object, Filtro do objeto de negócio
    @param dInitialPer, Date, Data inicial do período (referência)
    @param dFinalPer, Date, Data final do período (referência)
    @param lAccumulated, Logical, Flag de impressão de acumulado (referência)
    @param aEvents, Array, Eventos selecionados (referência)
    @param cFundsType, Character, Tipo de verba (referência)
    @param cPaid, Character, Se imprime abonados (referência)
    @param cDayToDay, Character, Se imprime dia a dia (referência)
    @param cHoursType, Character, Tipo de hora (referência)
    @param oSelf, Object, Instância da classe do ON
    @return Character, Alias do arquivo temporário
/*/
Static Function QueryDef(oFilter As Object, dInitialPer As Date, dFinalPer As Date, lAccumulated As Logical, aEvents As Array, cFundsType As Character, cPaid As Character, cDayToDay As Character, cHoursType As Character, oSelf As Object) As Character
    // Declaração das variáveis locais
    Local cQuery              As Character // Query para busca dos registros
    Local cAlias              As Character // Alias do arquivo temporário
    Local cFilter             As Character // Filtro do objeto de negócio
    Local cPeriodFrom         As Character // Parâmetro Período Inicial
    Local cPeriodTo           As Character // Parâmetro Período Final
    Local cBranchCodeFrom     As Character // Parâmetro Filial De
    Local cBranchCodeTo       As Character // Parâmetro Filial Até
    Local cCostCenterCodeFrom As Character // Parâmetro C.C. De
    Local cCostCenterCodeTo   As Character // Parâmetro C.C. Até
    Local cShiftCodeFrom      As Character // Parâmetro Turno De
    Local cShiftCodeTo        As Character // Parâmetro Turno Até
    Local cEmployeeCodeFrom   As Character // Parâmetro Matrícula De
    Local cEmployeeCodeTo     As Character // Parâmetro Matrícula Até
    Local cEmployeeNameFrom   As Character // Parâmetro Nome De
    Local cEmployeeNameTo     As Character // Parâmetro Nome Até
    Local cPrintType          As Character // Parâmetro Impressão
    Local aEmployeeSituation  As Array     // Parâmetro Situações
    Local aEmployeeCategory   As Array     // Parâmetro Categorias
    Local jParams             As JSON      // Parâmetros do objeto de negócio
    Local oStatement          As Object    // Instância da classe FwExecStatement
    Local nParamOrder         As Numeric   // ordem dos bind parameters da query
    Local nAux                As Numeric   // Contador auxiliar para percorrer vetores

    // Inicialização das variáveis
    cQuery              := ""
    cAlias              := ""
    cFilter             := IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    cPeriodFrom         := ""
    cPeriodTo           := ""
    cBranchCodeFrom     := ""
    cBranchCodeTo       := ""
    cCostCenterCodeFrom := ""
    cCostCenterCodeTo   := ""
    cShiftCodeFrom      := ""
    cShiftCodeTo        := ""
    cEmployeeCodeFrom   := ""
    cEmployeeCodeTo     := ""
    cEmployeeNameFrom   := ""
    cEmployeeNameTo     := ""
    cPrintType          := ""
    aEmployeeSituation  := {}
    aEmployeeCategory   := {}
    jParams             := oFilter:getParameters()
    oStatement          := NIL
    nParamOrder         := 1
    nAux                := 0

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cPeriodFrom          := IIf(jParams:hasProperty("PeriodFrom")         .and. Len(jParams["PeriodFrom"])         > 0 .and. !Empty(jParams["PeriodFrom"][1]),         jParams["PeriodFrom"][1],         FwTimeStamp(6, CToD("")))
        cPeriodTo            := IIf(jParams:hasProperty("PeriodTo")           .and. Len(jParams["PeriodTo"])           > 0 .and. !Empty(jParams["PeriodTo"][1]),           jParams["PeriodTo"][1],           FwTimeStamp(6, CToD("")))
        cBranchCodeFrom      := IIf(jParams:hasProperty("BranchCodeFrom")     .and. Len(jParams["BranchCodeFrom"])     > 0 .and. !Empty(jParams["BranchCodeFrom"][1]),     jParams["BranchCodeFrom"][1],     "")
        cBranchCodeTo        := IIf(jParams:hasProperty("BranchCodeTo")       .and. Len(jParams["BranchCodeTo"])       > 0 .and. !Empty(jParams["BranchCodeTo"][1]),       jParams["BranchCodeTo"][1],       Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCostCenterCodeFrom  := IIf(jParams:hasProperty("CostCenterCodeFrom") .and. Len(jParams["CostCenterCodeFrom"]) > 0 .and. !Empty(jParams["CostCenterCodeFrom"][1]), jParams["CostCenterCodeFrom"][1], "")
        cCostCenterCodeTo    := IIf(jParams:hasProperty("CostCenterCodeTo")   .and. Len(jParams["CostCenterCodeTo"])   > 0 .and. !Empty(jParams["CostCenterCodeTo"][1]),   jParams["CostCenterCodeTo"][1],   Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cShiftCodeFrom       := IIf(jParams:hasProperty("ShiftCodeFrom")      .and. Len(jParams["ShiftCodeFrom"])      > 0 .and. !Empty(jParams["ShiftCodeFrom"][1]),      jParams["ShiftCodeFrom"][1],      "")
        cShiftCodeTo         := IIf(jParams:hasProperty("ShiftCodeTo")        .and. Len(jParams["ShiftCodeTo"])        > 0 .and. !Empty(jParams["ShiftCodeTo"][1]),        jParams["ShiftCodeTo"][1],        Replicate("Z", GetSX3Cache("RA_TNOTRAB", "X3_TAMANHO")))
        cEmployeeCodeFrom    := IIf(jParams:hasProperty("EmployeeCodeFrom")   .and. Len(jParams["EmployeeCodeFrom"])   > 0 .and. !Empty(jParams["EmployeeCodeFrom"][1]),   jParams["EmployeeCodeFrom"][1],   "")
        cEmployeeCodeTo      := IIf(jParams:hasProperty("EmployeeCodeTo")     .and. Len(jParams["EmployeeCodeTo"])     > 0 .and. !Empty(jParams["EmployeeCodeTo"][1]),     jParams["EmployeeCodeTo"][1],     Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cEmployeeNameFrom    := IIf(jParams:hasProperty("EmployeeNameFrom")   .and. Len(jParams["EmployeeNameFrom"])   > 0 .and. !Empty(jParams["EmployeeNameFrom"][1]),   jParams["EmployeeNameFrom"][1],   "")
        cEmployeeNameTo      := IIf(jParams:hasProperty("EmployeeNameTo")     .and. Len(jParams["EmployeeNameTo"])     > 0 .and. !Empty(jParams["EmployeeNameTo"][1]),     jParams["EmployeeNameTo"][1],     Replicate("Z", GetSX3Cache("RA_NOME", "X3_TAMANHO")))
        cDayToDay            := IIf(jParams:hasProperty("DayToDay")           .and. Len(jParams["DayToDay"])           > 0 .and. !Empty(jParams["DayToDay"][1]),           jParams["DayToDay"][1],           "1") // Não
        cPaid                := IIf(jParams:hasProperty("Paid")               .and. Len(jParams["Paid"])               > 0 .and. !Empty(jParams["Paid"][1]),               jParams["Paid"][1],               "1") // Sim (abonados)
        cFundsType           := IIf(jParams:hasProperty("FundsType")          .and. Len(jParams["FundsType"])          > 0 .and. !Empty(jParams["FundsType"][1]),          jParams["FundsType"][1],          "3") // Ambas
        cPrintType           := IIf(jParams:hasProperty("PrintType")          .and. Len(jParams["PrintType"])          > 0 .and. !Empty(jParams["PrintType"][1]),          jParams["PrintType"][1],          "1") // Movimento
        cHoursType           := IIf(jParams:hasProperty("HoursType")          .and. Len(jParams["HoursType"])          > 0 .and. !Empty(jParams["HoursType"][1]),          jParams["HoursType"][1],          "1") // Sexagesimal
        aEmployeeSituation   := IIf(jParams:hasProperty("EmployeeSituation"), jParams["EmployeeSituation"], {})
        aEmployeeCategory    := IIf(jParams:hasProperty("EmployeeCategory"),  jParams["EmployeeCategory"],  {})
        aEvents              := IIf(jParams:hasProperty("Events"),            jParams["Events"],            {})
    EndIf

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aEmployeeSituation)
        aEmployeeSituation[nAux] := IIf(aEmployeeSituation[nAux] == NIL .or. Empty(aEmployeeSituation[nAux]), " ", aEmployeeSituation[nAux])
    Next nAux

    // Percorre as categorias para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aEmployeeCategory)
        aEmployeeCategory[nAux] := IIf(aEmployeeCategory[nAux] == NIL .or. Empty(aEmployeeCategory[nAux]), " ", aEmployeeCategory[nAux])
    Next nAux

    // Percorre os eventos para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nAux := 1 To Len(aEvents)
        aEvents[nAux] := IIf(aEvents[nAux] == NIL .or. Empty(aEvents[nAux]), " ", aEvents[nAux])
    Next nAux

    // Trata as variáveis de data para ficarem no formato do banco
    cPeriodFrom := StrTran(SubStr(cPeriodFrom, 1, 10), "-", "")
    cPeriodTo   := StrTran(SubStr(cPeriodTo,   1, 10), "-", "")

    // Captura o intervalo de datas
    dInitialPer := SToD(cPeriodFrom)
    dFinalPer   := SToD(cPeriodTo)

    // Verifica se a impressão é por acumulado
    lAccumulated := cPrintType == "2"

    // Monta a query de busca
    cQuery += "SELECT "
    cQuery +=     "RA_FILIAL, "
    cQuery +=     "RA_MAT, "
    cQuery +=     "RA_NOME, "
    cQuery +=     "RA_CC, "
    cQuery +=     "CTT_DESC01, "
    cQuery +=     "RA_CODFUNC, "
    cQuery +=     "RJ_DESC, "
    cQuery +=     "RA_TNOTRAB, "
    cQuery +=     "RA_HRSMES, "
    cQuery +=     "RA_CHAPA, "
    cQuery +=     "RA_CATFUNC, "
    cQuery +=     "RA_DEMISSA, "
    cQuery +=     "R6_DESC, "
    cQuery +=     "SRA.R_E_C_N_O_ SRA_RECNO "
    cQuery +=     "? " // 1
    cQuery += "FROM "
    cQuery +=     RetSQLName("SRA") + " SRA "
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("CTT") + " CTT "
    cQuery +=         "ON "
    cQuery +=             FwJoinFilial("SRA", "CTT") + " "
    cQuery +=             "AND CTT_CUSTO = RA_CC "
    cQuery +=             "AND CTT.D_E_L_E_T_ = ? " // 2
    cQuery +=     "INNER JOIN "
    cQuery +=         RetSQLName("SRJ") + " SRJ "
    cQuery +=         "ON "
    cQuery +=             FwJoinFilial("SRA", "SRJ") + " "
    cQuery +=             "AND RJ_FUNCAO = RA_CODFUNC "
    cQuery +=             "AND SRJ.D_E_L_E_T_ = ? " // 3
    cQuery +=     "LEFT JOIN "
    cQuery +=         RetSQLName("SR6") + " SR6 "
    cQuery +=         "ON "
    cQuery +=             FwJoinFilial("SRA", "SR6") + " "
    cQuery +=             "AND R6_TURNO = RA_TNOTRAB "
    cQuery +=             "AND SR6.D_E_L_E_T_ = ? " // 4
    cQuery += "WHERE "
    cQuery +=     "RA_FILIAL      BETWEEN ? AND ? "                                // 5 e 6
    cQuery +=     "AND RA_CC      BETWEEN ? AND ? "                                // 7 e 8
    cQuery +=     "AND RA_MAT     BETWEEN ? AND ? "                                // 9 e 10
    cQuery +=     "AND RA_NOME    BETWEEN ? AND ? "                                // 11 e 12
    cQuery +=     "AND RA_TNOTRAB BETWEEN ? AND ? "                                // 13 e 14
    cQuery +=     IIf(Len(aEmployeeSituation) > 0, " AND RA_SITFOLH IN (?) ", " ") // 15
    cQuery +=     IIf(Len(aEmployeeCategory)  > 0, " AND RA_CATFUNC IN (?) ", " ") // 16
    cQuery +=     "AND SRA.D_E_L_E_T_ = ? "                                        // 17
    cQuery +=     "? "                                                             // 18
    cQuery += "ORDER BY "
    cQuery +=     "RA_FILIAL "
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FwExecStatement():New(cQuery)

    // Captura e define os campos personalizados das tabelas SRA, CTT, SRJ, SR6
    cCustomFields := GPESmartViewUtils():GetCustomFields(oSelf:getCustomFields(), {"SRA", "CTT", "SRJ", "SR6"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    // Define o valor dos bind parameters
    oStatement:SetString(nParamOrder++, " ")                 // 2
    oStatement:SetString(nParamOrder++, " ")                 // 3
    oStatement:SetString(nParamOrder++, " ")                 // 4
    oStatement:SetString(nParamOrder++, cBranchCodeFrom)     // 5
    oStatement:SetString(nParamOrder++, cBranchCodeTo)       // 6
    oStatement:SetString(nParamOrder++, cCostCenterCodeFrom) // 7
    oStatement:SetString(nParamOrder++, cCostCenterCodeTo)   // 8
    oStatement:SetString(nParamOrder++, cEmployeeCodeFrom)   // 9
    oStatement:SetString(nParamOrder++, cEmployeeCodeTo)     // 10
    oStatement:SetString(nParamOrder++, cEmployeeNameFrom)   // 11
    oStatement:SetString(nParamOrder++, cEmployeeNameTo)     // 12
    oStatement:SetString(nParamOrder++, cShiftCodeFrom)      // 13
    oStatement:SetString(nParamOrder++, cShiftCodeTo)        // 14

    If (Len(aEmployeeSituation) > 0)
        oStatement:SetIn(nParamOrder++, aEmployeeSituation)  // 15
    EndIf

    If (Len(aEmployeeCategory) > 0)
        oStatement:SetIn(nParamOrder++, aEmployeeCategory)   // 16
    EndIf

    oStatement:SetString(nParamOrder++, " ")                 // 17
    oStatement:SetUnsafe(nParamOrder++, cFilter)             // 18

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Limpa os objetos da memória
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeArray(aEmployeeSituation)
    FwFreeArray(aEmployeeCategory)
Return cAlias

/*/{Protheus.doc} fSoma
    Função responsável por somar as horas dos eventos apontados no período.
    @type Function
    @version 12.1.2310
    @author arthur.sales
    @since 06/03/2024
    @param aData, Array, Armazena as informações dos apontamentos dos eventos
    @param cAlias, Character, Alias da tabela de origem
    @param lImpDia, Logical, Se imprime dia a dia
    @param cAuthorized, Character, Códigos de eventos autorizados
    @return Variant, Retorno nulo pré-fixado
/*/
Static Function fSoma(aData As Array, cAlias As Character, lImpDia As Logical, cAuthorized As Character) As Variant
    // Declaração das variáveis locais
    Local cPrefix As Character // Prefixo da tabela alvo
    Local cCod    As Character // Código do evento
    Local nRet    As Numeric   // Contador auxiliar para busca de registros no vetor
    Local nVal1   As Numeric   // Quantidade informada
    Local nVal2   As Numeric   // Quantidade calculada
    Local nDia    As Numeric   // Dia referente ao apontamento
    Local nAbonos As Numeric   // Contador auxiliar para percorrer os abonos
    Local aAbonos As Array     // Informações dos abonos

    // Inicialização das variáveis
    cPrefix := ""
    cCod    := ""
    nRet    := 0
    nVal1   := 0
    nVal2   := 0
    nDia    := 0
    nAbonos := 0
    aAbonos := {}

    cPrefix := PrefixoCpo(cAlias) + "_"

    cCod := IIf(!Empty((cAlias)->&(cPrefix + "PDI")), (cAlias)->&(cPrefix + "PDI"), (cAlias)->&(cPrefix + "PD"))

    If (lImpDia)
        nDia := (cAlias)->&(cPrefix + "DATA")
    EndIf

    nVal1 := (cAlias)->&(cPrefix + "QUANTI")
    nVal2 := (cAlias)->&(cPrefix + "QUANTC")

    // Verifica se o elemento já existe
    nRet := AScan(aData, {|x| x[1] == cCod .and. x[4] == nDia})
    If (nRet == 0)
        AAdd(aData, {cCod, nVal1, nVal2, nDia})
    Else
        aData[nRet][2] := SomaHoras(aData[nRet][2], nVal1)
        aData[nRet][3] := SomaHoras(aData[nRet][3], nVal2)
    EndIf

    aAbonos := {}

    // Adiciona os abonos
    If (cCod $ cAuthorized)
        fAbonos((cAlias)->&(cPrefix + "DATA"), (cAlias)->&(cPrefix + "PD"), , @aAbonos, (cAlias)->&(cPrefix + "TPMARCA"), (cAlias)->&(cPrefix + "CC"))

        // Percorre todos os abonos e somente soma aqueles que tiverem eventos associados
        For nAbonos := 1 To Len(aAbonos)
            // Para cada abono procura pelo motivo
            If (SP6->(MsSeek(fFilFunc("SP6") + aAbonos[nAbonos][1], .F.)))
                // Se o motivo estiver associado a um evento soma-o
                If (!Empty(SP6->P6_EVENTO))
                    cCod  := SP6->P6_EVENTO
                    nVal1 := aAbonos[nAbonos][2]
                    nVal2 := 0

                    // Verifica se o elemento já existe
                    If (nRet := AScan(aData, {|x| x[1] == cCod .and. x[4] == nDia})) == 0
                        AAdd(aData, {cCod, nVal1, nVal2, nDia})
                    Else
                        aData[nRet][2] := SomaHoras(aData[nRet][2], nVal1)
                        aData[nRet][3] := SomaHoras(aData[nRet][3], nVal2)
                    EndIf
                EndIf
            EndIf
        Next nAbonos
    EndIf

    // Limpa o vetor da memória
    FwFreeArray(aAbonos)
Return NIL
