#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.gpe.gper430.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} GPER430TReportsBusinessObject:new()
    Classe do objeto de negócio do Relatório Declaração de Encargos de Família para fins de IR.
    @type Class
    @version 12.1.2310
    @author arthur.sales
    @since 12/12/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="SR8, SRA, CTT, RCM, SRV, SQB, SQ3", customTables="SRA, SRB", name="Relatório Declaração de Encargos de Família para fins de IR", country="ALL", initialRelease="12.1.2310")
Class GPER430TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} GPER430TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 12/12/2023
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class GPER430TReportsBusinessObject
    // Chama o construtor da classe super
    _Super:new()

    // Define a Área
    self:appendArea(STR0001) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0002) // "Declaração de Encargos de Família para fins de IR"

Return self

/*/{Protheus.doc} GPER430TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 12/12/2023
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class GPER430TReportsBusinessObject
Return STR0003 // "Este objeto de negócio traz as informações do Relatório de Declaração de Encargos de Família para fins de IR."

/*/{Protheus.doc} GPER430TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 12/12/2023
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class GPER430TReportsBusinessObject
    // Declaração das variáveis locais
    Local dDtRef     As Date            // Data de referência
    Local cAlias     As Character       // Alias do arquivo temporário principal
    Local cFilExec   As Character       // Nome da filial de execução
    Local cEmpExec   As Character       // Nome da empresa de execução
    Local cAcessaSRA As Character       // Macro execução para validar acesso à tabela SRA
    Local cValidFil  As Character       // Filiais válidas
    Local cCampo     As Character       // Se o país for o Equador é o conteúdo do campo RB_DEPPLR, caso contrário RB_TIPIR
    Local cGrauDep   As Character       // Grau de dependência do dependente
    Local cPicCGC    As Character       // Picture do campo A1_CGC
    Local cPicCEP    As Character       // Picture do campo A1_CEP
    Local cPicCIC    As Character       // Picture do campo RA_CIC
    Local cAuxSexo   As Character       // Auxiliar para o sexo do funcionário (se for M fica 'o', se for F fica 'a')
    Local cEstCiv    As Character       // Estado civil do funcionário
    Local cAuxMat    As Character       // Matrícula do funcionário anterior
    Local cAuxFil    As Character       // Filial do funcionário anterior
    Local aInfo      As Array           // Informações da filial
    Local nLimIR1    As Numeric         // Limite 1 de idade pra o IR
    Local nLimIR2    As Numeric         // Limite 2 de idade pra o IR
    Local nCountDep  As Numeric         // Contador de dependentes de um funcionários
    Local cName         As Character    // Nome real do campo
    Local aPDFields     As Array        // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR   As Array        // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR     As Array        // Campos com nome diferente do real
    Local aAllFields    As Array        // Estrutura de todos os campos do ON
    Local lObfuscated   As Logical      // Flag que indica se tem algum campo para ofuscar
    Local jData         As JSON         // Informações a serem adicionadas no ON
    Local jLGPD         As JSON         // Indica quais campos devem ser ofuscados
    Local nFields       As Numeric
    Local lMostraDep    As Logical
    Local cChave        As Character
    Local lNovoFunc     As Logical

    // Inicialização das variáveis locais
    dDtRef      := CToD("")
    cAlias      := QueryDef(oFilter, @dDtRef, self:getCustomFields())
    cFilExec    := AllTrim(FwFilialName())
    cEmpExec    := AllTrim(FwEmpName(cEmpAnt))
    cAcessaSRA  := &("{|| " + ChkRH("GPER430", "SRA", "2") + "}")
    cValidFil   := fValidFil()
    cCampo      := ""
    cGrauDep    := ""
    cPicCGC     := AllTrim(X3Picture("A1_CGC"))
    cPicCEP     := AllTrim(X3Picture("A1_CEP"))
    cPicCIC     := AllTrim(X3Picture("RA_CIC"))
    cAuxSexo    := ""
    cEstCiv     := ""
    cAuxMat     := (cAlias)->(RA_MAT)
    cAuxFil     := (cAlias)->(RA_FILIAL)
    aInfo       := {}
    nLimIR1     := IIf(cPaisLoc <> "BRA", 18,    21)
    nLimIR2     := IIf(cPaisLoc <> "BRA", -9999, 24)
    nCountDep   := 1
    nFields     := 1
    aPDFields   := {}
    aPDFieldsNR := {}
    aFieldsNR   := {}
    aAllFields  := {}
    lObfuscated := .F.
    jData       := NIL
    jLGPD       := JSONObject():New()
    lMostraDep  := .T.
    cChave      := ""
    lNovoFunc   := .F.

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {;
        "RA_FILIAL", "RA_MAT", "RA_CC", "RA_NOME", "RA_ENDEREC", "RA_NUMENDE", "RA_COMPLEM",;
        "RA_BAIRRO", "RA_MUNICIP", "RA_ESTADO", "RA_CEP", "RA_RG", "RA_NUMCP", "RA_SERCP",;
        "RA_CIC", "RB_MAT", "RB_NOME", "RB_DTNASC";
    }
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))

        lMostraDep  := .T.
        If cChave != (cAlias)->(RA_FILIAL + RA_MAT)
            cChave := (cAlias)->(RA_FILIAL + RA_MAT)
            lNovoFunc := .T.
        Else
            lNovoFunc := .F.
        Endif

        BEGIN SEQUENCE
            // Verifica o acesso à tabela SRA, se a filial é válida e se encontrou as informações da filial
            If ((!Empty(aInfo) .and. (cAlias)->RA_FILIAL == cAuxFil) .or. (Eval(cAcessaSRA) .and. (cAlias)->RA_FILIAL $ cValidFil .and. fInfo(@aInfo, (cAlias)->RA_FILIAL)))
                // Reinicia o valor das variáveis
                cGrauDep := ""
                cEstCiv  := ""

                // Verifica se o registro possui dependente (pode ser que não tenha por causa do parâmetro Imp. Func. S/ Depend.)
                If ((cAlias)->RB_MAT != NIL .and. !Empty((cAlias)->RB_MAT))
                    // Filtra a data da baixa pela data de referência
                    If !((AnoMes(SToD((cAlias)->RB_DTBAIXA)) >= AnoMes(dDtRef) .Or. Empty((cAlias)->RB_DTBAIXA)))
                        lMostraDep := .F.
                    EndIf

                    // Captura o tipo de dependente para filtrar de acordo com a data de nascimento
                    cCampo := IIf(cPaisLoc <> "EQU", (cAlias)->RB_TIPIR, (cAlias)->RB_DEPPLR)
                    If !(cCampo == "1" .Or.;
                        (cCampo == "2" .And. DateDiffYear(dDtRef, SToD((cAlias)->RB_DTNASC)) <= nLimIR1) .Or. ;
                        (cCampo == "3" .And. DateDiffYear(dDtRef, SToD((cAlias)->RB_DTNASC)) <= nLimIR2))
                        lMostraDep := .F.
                    EndIf

                    // Captura o grau de dependência
                    If ((cAlias)->RB_GRAUPAR $ "C/1")
                        cGrauDep := STR0004 // "Cônjuge"
                    ElseIf ((cAlias)->RB_GRAUPAR $ "F/2")
                        cGrauDep := STR0005 // "Filho(a)"
                    ElseIf ((cAlias)->RB_GRAUPAR $ "E/3")
                        cGrauDep := STR0006 // "Enteado"
                    ElseIf ((cAlias)->RB_GRAUPAR $ "P/4")
                        cGrauDep := STR0007 // "Pai/Mãe"
                    Else
                        cGrauDep := STR0008 // "Outros"
                    EndIf
                EndIf

                // Captura o estado civil do funcionário
                cAuxSexo := IIf((cAlias)->RA_SEXO == "M", "o", "a")
                If ((cAlias)->RA_ESTCIVI == "C")
                    cEstCiv := IIf(cPaisLoc == "BRA", "Casad" + cAuxSexo, STR0009)     // "Casado(a)"
                ElseIf ((cAlias)->RA_ESTCIVI == "D")
                    cEstCiv := IIf(cPaisLoc == "BRA", "Divorciad" + cAuxSexo, STR0010) // "Divorciado"
                ElseIf ((cAlias)->RA_ESTCIVI == "M")
                    cEstCiv := STR0011                                                 // "Un.Estável"
                ElseIf ((cAlias)->RA_ESTCIVI == "Q")
                    cEstCiv := IIf(cPaisLoc == "BRA", "Desquitad" + cAuxSexo, STR0012) // "Desquitado"
                ElseIf ((cAlias)->RA_ESTCIVI == "S")
                    cEstCiv := IIf(cPaisLoc == "BRA", "Solteir" + cAuxSexo, STR0013)   // "Solteiro"
                ElseIf ((cAlias)->RA_ESTCIVI == "V")
                    cEstCiv := IIf(cPaisLoc == "BRA", "Viuv" + cAuxSexo, STR0014)      // "Viuvo(a)"
                EndIf

                If lNovoFunc .OR. lMostraDep
                    // Adiciona as informações da linha
                    jData := {;
                        "FilialExec":                cFilExec,;
                        "EmpresaExec":               cEmpExec,;
                        "Branch":                    IIf(jLGPD["RA_FILIAL"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_FILIAL),    (cAlias)->RA_FILIAL),;
                        "EmployeeCode":              IIf(jLGPD["RA_MAT"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MAT),       (cAlias)->RA_MAT),;
                        "EmployeeCostCenter":        IIf(jLGPD["RA_CC"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CC),        (cAlias)->RA_CC),;
                        "EmployeeName":              IIf(jLGPD["RA_NOME"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NOME),      Left((cAlias)->RA_NOME, 57)),;
                        "EmployeeAddress":           IIf(jLGPD["RA_ENDEREC"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_ENDEREC),   (cAlias)->RA_ENDEREC),;
                        "EmployeeAddressNumber":     IIf(jLGPD["RA_NUMENDE"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NUMENDE),   (cAlias)->RA_NUMENDE),;
                        "EmployeeAddressComplement": IIf(jLGPD["RA_COMPLEM"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_COMPLEM),   (cAlias)->RA_COMPLEM),;
                        "EmployeeNeighborhood":      IIf(jLGPD["RA_BAIRRO"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_BAIRRO),    (cAlias)->RA_BAIRRO),;
                        "EmployeeCity":              IIf(jLGPD["RA_MUNICIP"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_MUNICIP),   (cAlias)->RA_MUNICIP),;
                        "EmployeeState":             IIf(jLGPD["RA_ESTADO"],   FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_ESTADO),    (cAlias)->RA_ESTADO),;
                        "EmployeeZipCode":           IIf(jLGPD["RA_CEP"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CEP),       IIf(cPaisLoc == "BRA", Transform(Left((cAlias)->RA_CEP, 8), "@R #####-###"), Transform(Left((cAlias)->RA_CEP, 8), cPicCEP))),;
                        "EmployeeRG":                IIf(jLGPD["RA_RG"],       FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_RG),        (cAlias)->RA_RG),;
                        "EmployeeMaritalStatus":     cEstCiv,;
                        "WorkRegisterNumber":        IIf(jLGPD["RA_NUMCP"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_NUMCP),     (cAlias)->RA_NUMCP),;
                        "WorkRegisterSeries":        IIf(jLGPD["RA_SERCP"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_SERCP),     (cAlias)->RA_SERCP),;
                        "EmployeeID":                IIf(jLGPD["RA_CIC"],      FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RA_CIC),       Transform(Left((cAlias)->RA_CIC, 11), cPicCIC)),;
                        "DependentCode":             Iif(lMostraDep,IIf(!Empty((cAlias)->RB_MAT), StrZero(nCountDep, 2), ""),""),;
                        "DependentName":             Iif(lMostraDep,IIf(jLGPD["RB_NOME"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RB_NOME),      (cAlias)->RB_NOME),""),;
                        "DependentBirthDate":        Iif(lMostraDep,IIf(!Empty((cAlias)->RB_DTNASC), SubStr(FwTimeStamp(2, SToD((cAlias)->RB_DTNASC)), 1, 10), ""),""),;
                        "DependentKinship":          Iif(lMostraDep,cGrauDep,""),;
                        "CompanyName":               aInfo[3],;
                        "CompanyCNPJ":               IIf(cPaisLoc == "BRA", IIf(!Empty(Len(aInfo) >= 27 .and. aInfo[27]), aInfo[27], Transform(aInfo[8], "@R ##.###.###/####-##")), Transform(Left(aInfo[8], 14), cPicCGC)),;
                        "CompanyAddress":            aInfo[4],;
                        "CompanyCity":               aInfo[5],;
                        "CompanyState":              aInfo[6],;
                        "CompanyZipCode":            IIf(cPaisLoc == "BRA", Transform(Left(aInfo[7], 8), "@R #####-###"), Transform(Left(aInfo[7], 8), cPicCEP)),;
                        "CompanyStateReg":           Left(aInfo[9], 14),;
                        "OriginPlatform":            "Protheus";
                    }

                    // Adiciona os campos customizados no JSON
                    GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

                    // Adiciona as informações da linha
                    self:oData:appendData(jData)

                    // Soma um no contador de dependentes
                    nCountDep++
                Endif
            EndIf
        END SEQUENCE

        // Pula para o próximo registro
        (cAlias)->(DBSkip())

        // Verifica se trocou de funcionário
        If (!Empty(cAuxFil + cAuxMat) .and. cAuxFil + cAuxMat != (cAlias)->(RA_FILIAL + RA_MAT))
            // Atualiza a filial e matrícula do funcionário e reinicia o contador de dependentes
            cAuxFil   := (cAlias)->(RA_FILIAL)
            cAuxMat   := (cAlias)->(RA_MAT)
            nCountDep := 1
        EndIf
    End

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
    FwFreeArray(aAllFields)
    FwFreeArray(aFieldsNR)
    FwFreeArray(aPDFields)
    FwFreeArray(aPDFieldsNR)
    FwFreeArray(aInfo)
Return self:oData

/*/{Protheus.doc} GPER430TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author arthur.sales
    @since 12/12/2023
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class GPER430TReportsBusinessObject
    // Define os parâmetros do relatório
    self:oSchema:addParameter("BranchCodeFrom",       STR0015,   "string", .F.) // "Filial De"
    self:oSchema:addParameter("BranchCodeTo",         STR0016,   "string", .F.) // "Filial Até"
    self:oSchema:addParameter("CostCenterCodeFrom",   STR0017,   "string", .F.) // "C.C. De"
    self:oSchema:addParameter("CostCenterCodeTo",     STR0018,   "string", .F.) // "C.C. Até"
    self:oSchema:addParameter("EmployeeCodeFrom",     STR0019,   "string", .F.) // "Matrícula De"
    self:oSchema:addParameter("EmployeeCodeTo",       STR0020,   "string", .F.) // "Matrícula Até"
    self:oSchema:addParameter("ReferenceDate",        STR0021,   "date",   .F.) // "Data Referência"
    self:oSchema:addParameter("EmployeeSituation",    STR0022,   "string", .T.) // "Situações"
    self:oSchema:addParameter("PrintWithoutDep",      STR0023,   "string", .F.) // "Imp. Func. s/ Depend."

    // Define endpoints dos parâmetros de tipo lookup/combobox
    self:setCustomURL("BranchCodeFrom",     "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("BranchCodeTo",       "/api/rh/smartview/v1/options/GPEParams/getBranches",   2)
    self:setCustomURL("CostCenterCodeFrom", "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("CostCenterCodeTo",   "/api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("EmployeeCodeFrom",   "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("EmployeeCodeTo",     "/api/framework/v1/genericLookupService/smartview/SRA", 2)
    self:setCustomURL("EmployeeSituation",  "/api/rh/smartview/v1/options/GPEParams/getSX5/31",     2)
    self:setCustomURL("PrintWithoutDep",    "/api/rh/smartview/v1/options/GPEParams/getSNOptions",  1)

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("FilialExec",                STR0024,      "string",  STR0054,     "FilialExec",     NIL, NIL, NIL, .F.) // "Filial Exec",            
    self:oSchema:addProperty("EmpresaExec",               STR0025,      "string",  STR0055,     "EmpresaExec",    NIL, NIL, NIL, .F.) // "Empresa Exec",           
    self:oSchema:addProperty("Branch",                    STR0026,      "string",  STR0026,     "RA_FILIAL",      NIL, NIL, NIL, .T.) // "Filial",                 
    self:oSchema:addProperty("EmployeeCode",              STR0027,      "string",  STR0027,     "RA_MAT",         NIL, NIL, NIL, .T.) // "Matrícula",              
    self:oSchema:addProperty("EmployeeCostCenter",        STR0028,      "string",  STR0028,     "RA_CC",          NIL, NIL, NIL, .T.) // "Centro de Custo",        
    self:oSchema:addProperty("EmployeeName",              STR0029,      "string",  STR0029,     "RA_NOME",        NIL, NIL, NIL, .T.) // "Nome do Func.",          
    self:oSchema:addProperty("EmployeeAddress",           STR0030,      "string",  STR0030,     "RA_ENDEREC",     NIL, NIL, NIL, .T.) // "Endereço",               
    self:oSchema:addProperty("EmployeeAddressNumber",     STR0031,      "string",  STR0031,     "RA_NUMENDE",     NIL, NIL, NIL, .T.) // "Num. Endereço Func.",    
    self:oSchema:addProperty("EmployeeAddressComplement", STR0032,      "string",  STR0032,     "RA_COMPLEM",     NIL, NIL, NIL, .T.) // "Comp. Endereço Func.",   
    self:oSchema:addProperty("EmployeeNeighborhood",      STR0033,      "string",  STR0033,     "RA_BAIRRO",      NIL, NIL, NIL, .T.) // "Bairro do Func.",        
    self:oSchema:addProperty("EmployeeCity",              STR0034,      "string",  STR0034,     "RA_MUNICIP",     NIL, NIL, NIL, .T.) // "Município do Func.",     
    self:oSchema:addProperty("EmployeeState",             STR0035,      "string",  STR0035,     "RA_ESTADO",      NIL, NIL, NIL, .T.) // "Estado do Func.",        
    self:oSchema:addProperty("EmployeeZipCode",           STR0036,      "string",  STR0036,     "RA_CEP",         NIL, NIL, NIL, .T.) // "CEP do Func.",           
    self:oSchema:addProperty("EmployeeRG",                STR0037,      "string",  STR0037,     "RA_RG",          NIL, NIL, NIL, .T.) // "RG do Func.",            
    self:oSchema:addProperty("EmployeeMaritalStatus",     STR0038,      "string",  STR0038,     "RA_ESTCIVI",     NIL, NIL, NIL, .T.) // "Estado Civil Func.",     
    self:oSchema:addProperty("WorkRegisterNumber",        STR0039,      "string",  STR0039,     "RA_NUMCP",       NIL, NIL, NIL, .T.) // "Num. Cart. Prof.",       
    self:oSchema:addProperty("WorkRegisterSeries",        STR0040,      "string",  STR0040,     "RA_SERCP",       NIL, NIL, NIL, .T.) // "Série Cart. Prof.",      
    self:oSchema:addProperty("EmployeeID",                STR0041,      "string",  STR0041,     "RA_CIC",         NIL, NIL, NIL, .T.) // "CPF do Func.",           
    self:oSchema:addProperty("DependentCode",             STR0042,      "string",  STR0042,     "RB_COD",         NIL, NIL, NIL, .T.) // "Código Dependente",      
    self:oSchema:addProperty("DependentName",             STR0043,      "string",  STR0043,     "RB_NOME",        NIL, NIL, NIL, .T.) // "Nome Dependente",        
    self:oSchema:addProperty("DependentBirthDate",        STR0044,      "string",  STR0044,     "RB_DTNASC",      NIL, NIL, NIL, .T.) // "Dt. Nasc. Dependente",   
    self:oSchema:addProperty("DependentKinship",          STR0045,      "string",  STR0045,     "RB_GRAUPAR",     NIL, NIL, NIL, .T.) // "Grau Parent. Dependente",
    self:oSchema:addProperty("CompanyName",               STR0046,      "string",  STR0046,     "M0_NOMECOM",     NIL, NIL, NIL, .F.) // "Nome da Empresa",        
    self:oSchema:addProperty("CompanyCNPJ",               STR0047,      "string",  STR0047,     "M0_CGC",         NIL, NIL, NIL, .F.) // "CNPJ da Empresa",        
    self:oSchema:addProperty("CompanyAddress",            STR0048,      "string",  STR0048,     "M0_ENDCOB",      NIL, NIL, NIL, .F.) // "Endereço da Empresa",    
    self:oSchema:addProperty("CompanyCity",               STR0049,      "string",  STR0049,     "M0_CIDCOB",      NIL, NIL, NIL, .F.) // "Cidade da Empresa",      
    self:oSchema:addProperty("CompanyState",              STR0050,      "string",  STR0050,     "M0_ESTCOB",      NIL, NIL, NIL, .F.) // "Estado da Empresa",      
    self:oSchema:addProperty("CompanyZipCode",            STR0051,      "string",  STR0051,     "M0_CEPCOB",      NIL, NIL, NIL, .F.) // "CEP da Empresa",         
    self:oSchema:addProperty("CompanyStateReg",           STR0052,      "string",  STR0052,     "M0_INSC",        NIL, NIL, NIL, .F.) // "Insc. Estadual da Emp.", 
    self:oSchema:addProperty("OriginPlatform",            STR0053,      "string",  STR0053,     "OriginPlatform", NIL, NIL, NIL, .F.) // "Plataforma Origem",      
Return self:oSchema    

/*/{Protheus.doc} QueryDef
    Define a query de busca dos registros.
    @type Function
    @version 12.1.2310
    @author arthur.sales
    @since 12/12/2023
    @param oFilter, Object, Filtro do objeto de negócio
    @param dDtRef, Date, Parâmetro data de referência (passada por referência)
    @return Character, Alias do arquivo temporário
/*/
Static Function QueryDef(oFilter As Object, dDtRef As Date, aCustomFields as Array) As Character
    // Declaração das variáveis locais
    Local aSituacoes    As Array     // Situações do funcionário
    Local cAlias        As Character // Alias do arquivo temporário
    Local cCCAte        As Character // Parâmetro C.C. Até
    Local cCCDe         As Character // Parâmetro C.C. De
    Local cDtRef        As Character // Parâmetro Data de Referência
    Local cFieldEqu     As Character // Campos exclusivos do equador
    Local cFilialAte    As Character // Parâmetro Filial Até
    Local cFilialDe     As Character // Parâmetro Filial De
    Local cJoinSRASRB   As Character // Se o parâmetro Imp. Func. S/ Depend. for sim, será um LEFT JOIN, caso contrário um INNER JOIN
    Local cMatriculaAte As Character // Parâmetro Matrícula Até
    Local cMatriculaDe  As Character // Parâmetro Matrícula De
    Local cPrintSDep    As Character // Parâmetro Imp. Func. S/ Depend. (1 = Sim | 2 = Não)
    Local cQuery        As Character // Query para busca dos registros
    Local JParams       As JSON      // Parâmetros do objeto de negócio
    Local oStatement    As Object    // Instância da classe FwExecStatement
    Local nParamOrder   As Numeric   // Ordem dos bind parameters da query
    Local nSituacoes    As Numeric   // Contador para percorrer as situações
    Local cCustomFields As Character // Campos customizados no formato SQL

    // Inicialização das variáveis
    aSituacoes    := {}
    cAlias        := ""
    cCCAte        := ""
    cCCDe         := ""
    cDtRef        := ""
    cFieldEqu     := IIf(cPaisLoc == "EQU" .and. FieldPos("RB_DEPPLR") > 0, "RB_DEPPLR", "' '")
    cFilialAte    := ""
    cFilialDe     := ""
    cJoinSRASRB   := ""
    cMatriculaAte := ""
    cMatriculaDe  := ""
    cPrintSDep    := ""
    cQuery        := ""
    jParams       := oFilter:getParameters()
    oStatement    := NIL
    nParamOrder   := 1
    nSituacoes    := 0

    // Captura o valor dos parâmetros
    If (jParams != NIL)
        cDtRef        := IIf(jParams:hasProperty("ReferenceDate")      .and. Len(jParams["ReferenceDate"])      > 0 .and. !Empty(jParams["ReferenceDate"][1]),      jParams["ReferenceDate"][1],      FwTimeStamp(6, CToD("")))
        cFilialDe     := IIf(jParams:hasProperty("BranchCodeFrom")     .and. Len(jParams["BranchCodeFrom"])     > 0 .and. !Empty(jParams["BranchCodeFrom"][1]),     jParams["BranchCodeFrom"][1],     "")
        cFilialAte    := IIf(jParams:hasProperty("BranchCodeTo")       .and. Len(jParams["BranchCodeTo"])       > 0 .and. !Empty(jParams["BranchCodeTo"][1]),       jParams["BranchCodeTo"][1],       Replicate("Z", GetSX3Cache("RA_FILIAL", "X3_TAMANHO")))
        cCCDe         := IIf(jParams:hasProperty("CostCenterCodeFrom") .and. Len(jParams["CostCenterCodeFrom"]) > 0 .and. !Empty(jParams["CostCenterCodeFrom"][1]), jParams["CostCenterCodeFrom"][1], "")
        cCCAte        := IIf(jParams:hasProperty("CostCenterCodeTo")   .and. Len(jParams["CostCenterCodeTo"])   > 0 .and. !Empty(jParams["CostCenterCodeTo"][1]),   jParams["CostCenterCodeTo"][1],   Replicate("Z", GetSX3Cache("RA_CC", "X3_TAMANHO")))
        cMatriculaDe  := IIf(jParams:hasProperty("EmployeeCodeFrom")   .and. Len(jParams["EmployeeCodeFrom"])   > 0 .and. !Empty(jParams["EmployeeCodeFrom"][1]),   jParams["EmployeeCodeFrom"][1],   "")
        cMatriculaAte := IIf(jParams:hasProperty("EmployeeCodeTo")     .and. Len(jParams["EmployeeCodeTo"])     > 0 .and. !Empty(jParams["EmployeeCodeTo"][1]),     jParams["EmployeeCodeTo"][1],     Replicate("Z", GetSX3Cache("RA_MAT", "X3_TAMANHO")))
        cPrintSDep    := IIf(jParams:hasProperty("PrintWithoutDep")    .and. Len(jParams["PrintWithoutDep"])    > 0 .and. !Empty(jParams["PrintWithoutDep"][1]),    jParams["PrintWithoutDep"][1],     "2")
        aSituacoes    := IIf(jParams:hasProperty("EmployeeSituation"), jParams["EmployeeSituation"], {})
    EndIf

    // Percorre as situações para tratar registro nulo ou em branco (caso não tratar dá erro no banco)
    For nSituacoes := 1 To Len(aSituacoes)
        aSituacoes[nSituacoes] := IIf(aSituacoes[nSituacoes] == NIL .or. Empty(aSituacoes[nSituacoes]), " ", aSituacoes[nSituacoes])
    Next nSituacoes

    // Trata as variáveis de data para ficarem no formato do banco
    cDtRef := StrTran(SubStr(cDtRef, 1, 10), "-", "")

    // Transforma a data para o tipo data
    dDtRef := SToD(cDtRef)

    // Verifica se deve imprimir funcionário sem dependente
    cJoinSRASRB := IIf(cPrintSDep == "1", "LEFT", "INNER")

    // Monta a query de busca
    // Bind parameters: 1 = Campo RB_DEPPLR | 2 = Tipo do join entre SRA e SRB | 3 = Filial de | 4 = Filial até |
    // 5 = C.C. de | 6 = C.C. até | 5 = Matrícula de | 6 = Matrícula até | 7 = Situações
    cQuery += " SELECT "
    cQuery += "     RA_FILIAL, "
    cQuery += "     RA_MAT, "
    cQuery += "     RA_CC, "
    cQuery += "     RA_NOME, "
    cQuery += "     RA_ENDEREC, "
    cQuery += "     RA_NUMENDE, "
    cQuery += "     RA_COMPLEM, "
    cQuery += "     RA_BAIRRO, "
    cQuery += "     RA_MUNICIP, "
    cQuery += "     RA_ESTADO, "
    cQuery += "     RA_ESTCIVI, "
    cQuery += "     RA_CEP, "
    cQuery += "     RA_RG, "
    cQuery += "     RA_NUMCP, "
    cQuery += "     RA_SERCP, "
    cQuery += "     RA_UFCP, "
    cQuery += "     RA_CIC, "
    cQuery += "     RA_SEXO, "
    cQuery += "     RB_COD, "
    cQuery += "     RB_NOME, "
    cQuery += "     RB_DTNASC, "
    cQuery += "     RB_GRAUPAR, "
    cQuery += "     RB_DTBAIXA, "
    cQuery += "     RB_TIPIR, "
    cQuery += "     ? AS RB_DEPPLR, "
    cQuery += "     RB_MAT "
    cQuery += "     ? "
    cQuery += " FROM "
    cQuery += "     " + RetSQLName("SRA") + " SRA "
    cQuery += "     ? JOIN "
    cQuery += "         " + RetSQLName("SRB") + " SRB "
    cQuery += "         ON "
    cQuery += "             " + FwJoinFilial("SRA", "SRB") + " "
    cQuery += "             AND RB_MAT          = RA_MAT "
    cQuery += "             AND SRB.D_E_L_E_T_  = ? "
    cQuery += " WHERE "
    cQuery += "     RA_FILIAL BETWEEN ? AND ? "
    cQuery += "     AND RA_CC     BETWEEN ? AND ? "
    cQuery += "     AND RA_MAT    BETWEEN ? AND ? "
    cQuery += "     " + IIf(Len(aSituacoes) > 0, " AND RA_SITFOLH IN (?)", " ")
    cQuery += "     " + IIf(oFilter:hasFilter(), oFilter:getSQLExpression(), " ")
    cQuery += "     AND SRA.D_E_L_E_T_ = ? "
    cQuery := ChangeQuery(cQuery)

    // Instancia a classe
    oStatement := FWExecStatement():New(cQuery)

    // Define o valor dos bind parameters
    oStatement:SetUnsafe(nParamOrder++, cFieldEqu)
    
    // Captura e define os campos personalizados das tabelas SRA e SQB
    cCustomFields := GPESmartViewUtils():GetCustomFields(aCustomFields, {"SRA", "SRB"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) //

    oStatement:SetUnsafe(nParamOrder++, cJoinSRASRB)
    oStatement:SetString(nParamOrder++, " ")
    oStatement:SetString(nParamOrder++, cFilialDe)
    oStatement:SetString(nParamOrder++, cFilialAte)
    oStatement:SetString(nParamOrder++, cCCDe)
    oStatement:SetString(nParamOrder++, cCCAte)
    oStatement:SetString(nParamOrder++, cMatriculaDe)
    oStatement:SetString(nParamOrder++, cMatriculaAte)
    If (Len(aSituacoes) > 0)
        oStatement:SetIn(nParamOrder++, aSituacoes)
    EndIf
    oStatement:SetString(nParamOrder++, " ")

    // Executa a query e retorna o alias criado
    cAlias := oStatement:OpenAlias()

    // Limpa os objetos da memória
    FwFreeObj(jParams)
    FwFreeObj(oStatement)
    FwFreeArray(aSituacoes)
Return cAlias
