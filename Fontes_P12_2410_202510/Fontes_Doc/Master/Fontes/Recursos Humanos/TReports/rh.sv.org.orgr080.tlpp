#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "rh.sv.org.orgr080.ch"

namespace totvs.protheus.rh.treportsintegratedprovider

/*/{Protheus.doc} ORGR080TReportsBusinessObject:new()
    Classe do objeto de negócio do relatório Controle Orçamentário (ORGR080).
    @type Class
    @version 12.1.2310
    @author caio.kretzer
    @since 06/02/2024
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAORG", tables="RCL,RCJ,SRJ,CTT,SQB,SRA", customTables="RCL, RCJ, CTT, SQB, SRJ", name="Rel. Controle Orçamentário", country="ALL", initialRelease="12.1.2210")
Class ORGR080TReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    // Define os métodos da classe
    Public Method new()            As Object
    Public Method getDescription() As Character
    Public Method getData()        As Object
    Public Method getSchema()      As Object
EndClass

/*/{Protheus.doc} ORGR080TReportsBusinessObject:new()
    Instancia a classe do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 06/02/2024
    @return Object, Instância do objeto de negócio
/*/
Method new() As Object Class ORGR080TReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0002) // "RH"

    // Define o nome do objeto de negócio
    self:setDisplayName(STR0003) // "Relatório de Controle Orçamentário"

Return self

/*/{Protheus.doc} ORGR080TReportsBusinessObject:getDescription()
    Retorna a descrição do objeto de negócio.
    @type Method
    @version 12.1.2210
    @author caio.kretzer
    @since 06/02/2024
    @return Character, Descrição do objeto de negócio
/*/
Method getDescription() As Character Class ORGR080TReportsBusinessObject
Return STR0001 // "Este objeto de negócio emite a Impressão do Relatório de Controle Orçamentário."

/*/{Protheus.doc} ORGR080TReportsBusinessObject:getData()
    Captura os dados do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 06/02/2024
    @return Object, Dados do objeto de negócio
/*/
Method getData(nPage As Numeric, oFilter As Object) As Object Class ORGR080TReportsBusinessObject
    // Declaração das variáveis locais
    Local cAlias        As Character // Alias do arquivo temporário
    Local cQuery        As Character // Query a ser executada
    Local cFilter       As Character // Filtro do relatório
    Local cWhere        As Character // Cláusula where da query
    Local cFilExec      As Character // Nome da filial de execução
    Local cEmpExec      As Character // Nome da empresa de execução
    Local jParams       As JSON      // Parâmetros do relatório

    Local cFil          As Character
    Local cDepartamento As Character
    Local cCentroCusto  As Character
    Local cPosto        As Character
    Local cProcesso     As Character
    Local cTipoPosto    As Character

    Local cJoinRCJ      As Character
    Local cJoinCTT      As Character
    Local cJoinSQB      As Character
    Local cJoinSRJ      As Character
    Local cJoinSRA      As Character

    Local nParamOrder As Numeric
    Local oStatement As Object

    Local cCustomFields As Character // Campos customizados no formato SQL
    Local cName         As Character // Nome real do campo
    Local aPDFields     As Array     // Campos que podem ser exibidos de acordo com a LGPD
    Local aPDFieldsNR   As Array     // Campos com nome diferente do real que podem ser exibidos de acordo com a LGPD
    Local aFieldsNR     As Array     // Campos com nome diferente do real
    Local aAllFields    As Array     // Estrutura de todos os campos do ON
    Local lObfuscated   As Logical   // Flag que indica se tem algum campo para ofuscar
    Local jData         As JSON      // Informações a serem adicionadas no ON
    Local jLGPD         As JSON      // Indica quais campos devem ser ofuscados
    Local nFields       As Numeric   // Contador de campos

    // Inicialização das variáveis
    cAlias    := GetNextAlias()
    cQuery    := ""
    cFilter     := IIf(oFilter:hasFilter(), "AND " + oFilter:getSQLExpression(), "")
    cWhere    := ""
    cFilExec  := AllTrim(FwFilialName())
    cEmpExec  := AllTrim(FwEmpName(cEmpAnt))
    jParams   := oFilter:getParameters()
    nParamOrder := 1
    oStatement  := NIL
    cCustomFields := ""
    cName         := ""
    aPDFields     := {}
    aPDFieldsNR   := {}
    aFieldsNR     := {}
    aAllFields    := {}
    lObfuscated   := .F.
    jData         := NIL
    jLGPD         := JSONObject():New()
    nFields       := 1

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:getArrayFields())
    lObfuscated := Len(aPDFields) != Len(self:getArrayFields())
    aAllFields  := self:getStructFields()

    // Adiciona no JSON todos os campos como atributos e com o conteúdo sendo uma flag que indica se o campo deve ser ofuscado
    For nFields := 1 To Len(aAllFields)
        cName        := Trim(aAllFields[nFields]:getName())
        jLGPD[cName] := AScan(aPDFields, {|x| Trim(x) == cName}) <= 0
    Next nFields

    // Tratamento de LGPD para os campos em que o nome da propriedade não é o nome real do campo
    aFieldsNR := {"RCL_FILIAL", "RCL_CC", "CTT_DESC01", "RCL_DEPTO", "QB_DESCRIC", "RCL_POSTO", "RJ_DESC", "RCL_PROCES", "RCJ_DESCRI", "RCL_TPOSTO", "RCL_OPOSTO", "RCL_NPOSTO", "RCL_SALAR"}
    aPDFieldsNR := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aFieldsNR)
    For nFields := 1 To Len(aFieldsNR)
        cName        := Trim(aFieldsNR[nFields])
        jLGPD[cName] := AScan(aPDFieldsNR, {|x| Trim(x) == cName}) <= 0
    Next nFields

    cFil            := IIf(jParams:hasProperty("PAR_FILIAL")       .and. Len(jParams["PAR_FILIAL"]) > 0       .and. !Empty(jParams["PAR_FILIAL"][1]),           jParams["PAR_FILIAL"][1],       "")
    cDepartamento   := IIf(jParams:hasProperty("PAR_DEPARTAMENTO") .and. Len(jParams["PAR_DEPARTAMENTO"]) > 0 .and. !Empty(jParams["PAR_DEPARTAMENTO"][1]),     jParams["PAR_DEPARTAMENTO"][1], "")
    cCentroCusto    := IIf(jParams:hasProperty("PAR_CENTROCUSTO")  .and. Len(jParams["PAR_CENTROCUSTO"]) > 0  .and. !Empty(jParams["PAR_CENTROCUSTO"][1]),      jParams["PAR_CENTROCUSTO"][1],  "")
    cPosto          := IIf(jParams:hasProperty("PAR_POSTO")        .and. Len(jParams["PAR_POSTO"]) > 0        .and. !Empty(jParams["PAR_POSTO"][1]),            jParams["PAR_POSTO"][1],        "")
    cProcesso       := IIf(jParams:hasProperty("PAR_PROCESSO")     .and. Len(jParams["PAR_PROCESSO"]) > 0     .and. !Empty(jParams["PAR_PROCESSO"][1]),         jParams["PAR_PROCESSO"][1],     "")
    cTipoPosto      := IIf(jParams:hasProperty("PAR_TIPOPOSTO")    .and. Len(jParams["PAR_TIPOPOSTO"]) > 0    .and. !Empty(jParams["PAR_TIPOPOSTO"][1]),        jParams["PAR_TIPOPOSTO"][1],    "")
    
    cJoinRCJ  :=  FWJoinFilial("RCJ", "RCL")
    cJoinCTT  :=  FWJoinFilial("CTT", "RCL")
    cJoinSQB  :=  FWJoinFilial("SQB", "RCL")
    cJoinSRJ  :=  FWJoinFilial("SRJ", "RCL")
    cJoinSRA  :=  FWJoinFilial("SRA", "RCL")

    // Define a cláusula where de acordo com os parâmetros
    If (!Empty(cFil))
        cWhere += " RCL_FILIAL = ? AND " // 7
    EndIf

    If (!Empty(cDepartamento))
        cWhere += " RCL_DEPTO = ? AND " // 8
    EndIf

    If (!Empty(cPosto))
        cWhere += " RCL_POSTO = ? AND " // 9
    EndIf

    If (!Empty(cCentroCusto))
        cWhere += " RCL_CC = ? AND " // 10
    EndIf

    If (!Empty(cProcesso))
        cWhere += " RCL_PROCES = ? AND " // 11
    EndIf

    If (!Empty(cTipoPosto) .AND. cTipoPosto != "1")
        cWhere += " RCL_TPOSTO = ? AND " // 12
    EndIf

    // Montagem da query de busca
    cQuery := " SELECT RCL.RCL_FILIAL, RCL.RCL_CC, RCL.RCL_DEPTO, RCL.RCL_POSTO, RCL.RCL_TPOSTO, "
    cQuery += "     RCL.RCL_SALAR, RCL.RCL_NPOSTO, RCL.RCL_OPOSTO, "
	cQuery += " 	CTT.CTT_DESC01, SQB.QB_DESCRIC, SALFUN, RCJ_DESCRI, "
	cQuery += " 	RCL_PROCES, SRJ.RJ_DESC "
	cQuery += " 	? " // 1
    cQuery += " FROM "+RetSqlName("RCL")+"  RCL "
    cQuery += " INNER JOIN "+RetSqlName("RCJ")+" RCJ "
    cQuery += "     ON "+cJoinRCJ+" "
    cQuery += "     AND RCJ_CODIGO = RCL_PROCES "
    cQuery += "     AND RCJ.D_E_L_E_T_ = ? " // 2
    cQuery += " INNER JOIN "+RetSqlName("CTT")+" CTT "
    cQuery += "     ON "+cJoinCTT+" "
    cQuery += "     AND CTT_CUSTO = RCL_CC "
    cQuery += "     AND CTT.D_E_L_E_T_ = ? " // 3
    cQuery += " INNER JOIN "+RetSqlName("SQB")+" SQB "
    cQuery += "     ON "+cJoinSQB+" "
    cQuery += "     AND SQB.QB_DEPTO = RCL_DEPTO "
    cQuery += "     AND SQB.D_E_L_E_T_ = ? " // 4
    cQuery += " INNER JOIN "+RetSqlName("SRJ")+" SRJ "
    cQuery += "     ON "+cJoinSRJ+" "
    cQuery += "     AND RJ_FUNCAO = RCL_FUNCAO "
    cQuery += "     AND SRJ.D_E_L_E_T_ = ? " // 5
    cQuery += " LEFT JOIN  "
    cQuery += "         (SELECT RA_FILIAL, RA_POSTO, SUM (RA_SALARIO) SALFUN, D_E_L_E_T_  "
    cQuery += "         FROM "+RetSqlName("SRA")+" SRA  "
    cQuery += "         GROUP BY RA_FILIAL, RA_POSTO, D_E_L_E_T_) SRA "
    cQuery += "     ON "+cJoinSRA+" "
    cQuery += "     AND SRA.RA_POSTO = RCL_POSTO "
    cQuery += "     AND SRA.D_E_L_E_T_ = ?	 " // 6
    cQuery += " WHERE "
    cQuery += cWhere
    cQuery += " RCL.D_E_L_E_T_ = ? " // 13
    cQuery += " ? " // 14

    cQuery := ChangeQuery(cQuery)

	// Instancia a classe
	oStatement := FwExecStatement():New(cQuery)

    // Captura e define os campos personalizados das tabelas RCL, RCJ, CTT, SQB, SRJ
    cCustomFields := GPESmartViewUtils():GetCustomFields(self:getCustomFields(), {"RCL", "RCJ", "CTT", "SQB", "SRJ"})
    oStatement:SetUnsafe(nParamOrder++, IIf(!Empty(cCustomFields), "," + cCustomFields, "")) // 1

    // Definições dos valores dos parâmetros
    oStatement:SetString(nParamOrder++, " ")  // 2
    oStatement:SetString(nParamOrder++, " ")  // 3
    oStatement:SetString(nParamOrder++, " ")  // 4
    oStatement:SetString(nParamOrder++, " ")  // 5
    oStatement:SetString(nParamOrder++, " ")  // 6

    // Define a cláusula where de acordo com os parâmetros
    If (!Empty(cFil))
        oStatement:SetString(nParamOrder++, FWxFilial("SRA",cFil)) // 7
    EndIf

    If (!Empty(cDepartamento))
        oStatement:SetString(nParamOrder++, cDepartamento) // 8
    EndIf

    If (!Empty(cPosto))
        oStatement:SetString(nParamOrder++, cPosto) // 9
    EndIf

    If (!Empty(cCentroCusto))
        oStatement:SetString(nParamOrder++, cCentroCusto) // 10
    EndIf

    If (!Empty(cProcesso))
        oStatement:SetString(nParamOrder++, cProcesso) // 11
    EndIf

    If (!Empty(cTipoPosto) .AND. cTipoPosto != "1")
        oStatement:SetString(nParamOrder++, cValToChar(Val(cTipoPosto)-1)) // 12
    EndIf

    oStatement:SetString(nParamOrder++, " ")     // 13
    oStatement:SetUnsafe(nParamOrder++, cFilter) // 14

	// Executa a query e retorna o alias criado
	cAlias := oStatement:OpenAlias()

    // Percorre todos os registros
    While (!(cAlias)->(EOF()))

        // Adiciona as informações da linha
        jData := {;
            "Filial":           Trim(IIf(jLGPD["RCL_FILIAL"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RCL_FILIAL), (cAlias)->RCL_FILIAL)),;
            "CentroCusto":      Trim(IIf(jLGPD["RCL_CC"],     FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RCL_CC),     (cAlias)->RCL_CC)),;
            "CentroCustoDesc":  Trim(IIf(jLGPD["CTT_DESC01"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->CTT_DESC01), (cAlias)->CTT_DESC01)),;
            "Departamento":     Trim(IIf(jLGPD["RCL_DEPTO"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RCL_DEPTO),  (cAlias)->RCL_DEPTO)),;
            "DepartamentoDesc": Trim(IIf(jLGPD["QB_DESCRIC"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->QB_DESCRIC), (cAlias)->QB_DESCRIC)),;
            "CodPosto":         Trim(IIf(jLGPD["RCL_POSTO"],  FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RCL_POSTO),  (cAlias)->RCL_POSTO)),;
            "Posto":            Trim(IIf(jLGPD["RJ_DESC"],    FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RJ_DESC),    (cAlias)->RJ_DESC)),;
            "CodProcesso":      Trim(IIf(jLGPD["RCL_PROCES"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RCL_PROCES), (cAlias)->RCL_PROCES)),;
            "Processo":         Trim(IIf(jLGPD["RCJ_DESCRI"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RCJ_DESCRI), (cAlias)->RCJ_DESCRI)),;
            "TipoPosto":        Trim(IIf(jLGPD["RCL_TPOSTO"], FwProtectedDataUtil():ValueAsteriskToAnonymize((cAlias)->RCL_TPOSTO), (cAlias)->RCL_TPOSTO)),;
            "VagaOcupada":      IIf(jLGPD["RCL_OPOSTO"], 0, (cAlias)->RCL_OPOSTO),;
            "TotalVagas":       IIf(jLGPD["RCL_OPOSTO"], 0, (cAlias)->RCL_NPOSTO),;
            "SalarioPosto":     IIf(jLGPD["RCL_OPOSTO"], 0, (cAlias)->RCL_SALAR),;
            "TotalSalFunc":     IIf(jLGPD["RCL_OPOSTO"], 0, (cAlias)->SALFUN),;
            "FilialExec":       cFilExec                        ,; //Filial de Execução
            "EmpresaExec":      cEmpExec                        ;  //Empresa de Execução
        }

        // Adiciona os campos customizados no JSON
        GPESmartViewUtils():SetCustomFields(aAllFields, jData, cAlias, aPDFields, lObfuscated)

        // Adiciona o registro no vetor de registros do objeto de negócio
        self:oData:appendData(jData)

        // Pula para o próximo registro
        (cAlias)->(DBSkip())
    End

    // Fecha a área da tabela temporária
    (cAlias)->(DBCloseArea())

    // Limpa os objetos da memória
    jData := NIL
    FwFreeObj(jData)
    FwFreeObj(jLGPD)
    FwFreeArray(aAllFields)
    FwFreeArray(aFieldsNR)
    FwFreeArray(aPDFields)
    FwFreeArray(aPDFieldsNR)
Return self:oData

/*/{Protheus.doc} ORGR080TReportsBusinessObject:getSchema()
    Define o schema do objeto de negócio.
    @type Method
    @version 12.1.2310
    @author caio.kretzer
    @since 06/02/2024
    @return Object, Schema do objeto de negócio
/*/
Method getSchema() As Object Class ORGR080TReportsBusinessObject


    // Define os parâmetros do relatório
    self:oSchema:addParameter("PAR_FILIAL"          , STR0004            , "string", .F.) // "Filial"
    self:oSchema:addParameter("PAR_DEPARTAMENTO"    , STR0005            , "string", .F.) // "Departamento"
    self:oSchema:addParameter("PAR_CENTROCUSTO"     , STR0006            , "string", .F.) // "Centro de Custo"
    self:oSchema:addParameter("PAR_POSTO"           , STR0007            , "string", .F.) // "Posto" 
    self:oSchema:addParameter("PAR_PROCESSO"        , STR0008            , "string", .F.) // "Processo"
    self:oSchema:addParameter("PAR_TIPOPOSTO"       , STR0009            , "string", .F.) // "Tipo Posto"
    self:oSchema:addParameter("PAR_AGRUPARPOR"      , STR0010            , "string", .F.) // "Agrupar por"

    // Adiciona as propriedades do objeto de negócio
    self:oSchema:addProperty("Filial",                      STR0011,        "string", "Filial"                , "Filial") // "Código da Filial"
    self:oSchema:addProperty("CentroCusto",                 STR0012,        "string", "Centro de Custo"       , "CentroCusto") // "Centro de Custo"
    self:oSchema:addProperty("CentroCustoDesc",             STR0013,        "string", "Centro de Custo Desc"  , "CentroCustoDesc") // "Centro de Custo Desc"
    self:oSchema:addProperty("Departamento",                STR0014,        "string", "Departamento"          , "Departamento") // "Departamento"
    self:oSchema:addProperty("DepartamentoDesc",            STR0015,        "string", "DepartamentoDesc"      , "DepartamentoDesc") // "DepartamentoDesc"
    self:oSchema:addProperty("CodPosto",                    STR0016,        "string", "Código do Posto"       , "CodPosto") // "Código do Posto"
    self:oSchema:addProperty("Posto",                       STR0017,        "string", "Posto"                 , "Posto") // "Posto"
    self:oSchema:addProperty("CodProcesso",                 STR0018,        "string", "Código Processo"       , "CodProcesso") // "Código Processo"
    self:oSchema:addProperty("Processo",                    STR0019,        "string", "Processo"              , "Processo") // "Processo"
    self:oSchema:addProperty("TipoPosto",                   STR0020,        "string", "Tipo Posto"            , "TipoPosto") // "Tipo Posto"
    self:oSchema:addProperty("VagaOcupada",                 STR0021,        "number", "Vaga Ocupada"          , "VagaOcupada") // "Vaga Ocupada"
    self:oSchema:addProperty("TotalVagas",                  STR0022,        "number", "Total Vagas"           , "TotalVagas") // "Total Vagas"
    self:oSchema:addProperty("SalarioPosto",                STR0023,        "number", "Salario Posto"         , "SalarioPosto") // "Salario Posto"
    self:oSchema:addProperty("TotalSalFunc",                STR0024,        "number", "Total Salário Func"    , "TotalSalFunc") // "Total Salário Func"
    self:oSchema:addProperty("FilialExec",                  STR0025,        "string", "Filial de Execução"    , "FilialExec") // "Filial de Execução"
    self:oSchema:addProperty("EmpresaExec",                 STR0026,        "string", "Empresa de Execução"   , "EmpresaExec") // "Empresa de Execução"

    self:setCustomURL("PAR_FILIAL",    		    "/api/rh/smartview/v1/options/GPEParams/getBranches",                   2)
	self:setCustomURL("PAR_DEPARTAMENTO",       "/api/framework/v1/genericLookupService/smartview/SQB",                 2)
    self:setCustomURL("PAR_CENTROCUSTO",        "/api/framework/v1/genericLookupService/smartview/CTT",                 2)
    self:setCustomURL("PAR_POSTO",              "/api/framework/v1/genericLookupService/smartview/RCL",   				2)
    self:setCustomURL("PAR_PROCESSO",           "/api/framework/v1/genericLookupService/smartview/RCJ",                 2)
    self:setCustomURL("PAR_TIPOPOSTO",          "/api/rh/smartview/v1/options/ORGR080/PAR_TIPOPOSTO",                   1)
    self:setCustomURL("PAR_AGRUPARPOR",         "/api/rh/smartview/v1/options/ORGR080/PAR_AGRUPARPOR",                  1)

Return self:oSchema
