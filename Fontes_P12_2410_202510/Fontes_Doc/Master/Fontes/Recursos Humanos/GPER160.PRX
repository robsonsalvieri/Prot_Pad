#INCLUDE "PROTHEUS.CH"
#INCLUDE "GPER160.CH"
#INCLUDE "REPORT.CH"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณ GPER160  ณ Autor ณ Marcos Stiefano            ณ Data ณ 08/12/95 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ Extrato de Tarefas                                              ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณSintaxe   ณ GPER160(void)                                                   ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametrosณ                                                                 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ Generico                                                        ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณProgramador ณ Data   ณ BOPS      ณ  Motivo da Alteracao                     ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณCecilia     ณ16/05/14ณTPQAJ1     ณIncluido o fonte da 11 para a 12 e        ณฑฑ
ฑฑณ            ณ        ณ           ณefetuada a limpeza.                       ณฑฑ
ฑฑณMauricio T. ณ24/07/14ณTQDPY8     ณExclusao da funca GeraTarefa e alteracao  ณฑฑ
ฑฑณ            ณ        ณ           ณgrupo de pergunta de GP160R para GP160R1  ณฑฑ
ฑฑณ            ณ        ณ           ณpara exclusao do Pergunte 08-Atualiza Moviณฑฑ
ฑฑณ            ณ        ณ           ณmento.                                    ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

/*


ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออออปฑฑ
ฑฑบPrograma  ณGPER160   บAutor  ณMicrosiga           บ Data ณ  06/27/06     บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออออนฑฑ
ฑฑบDesc.     ณ  Extrato de Tarefas                                          บฑฑ
ฑฑบ          ณ                                                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP  - Release 4                                              บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/

Function GPER160()
Local oReport 
Local aAreaSRO	:= SRO->( GetArea())
	
Private oBreakCC
Private cAlias		:= "SRA"

//-- Interface de impressao
Pergunte("GP160R1",.F.)

   	oReport := ReportDef()
 	oReport:PrintDialog()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Variaveis utilizadas para parametros                         ณ
//ณ mv_par01        //  Filial                                   ณ
//ณ mv_par02        //  Centro de Custo                          ณ
//ณ mv_par03        //  Matricula                                ณ
//ณ mv_par04        //  Situacao do Funcionario                  ณ
//ณ mv_par05        //  Relatorio Por Data ou Tarefa             ณ
//ณ mv_par06        //  Data Inicial da Tarefa                   ณ
//ณ mv_par07        //  Data Final   da Tarefa                   ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

RestArea( aAreaSRO )
Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGPER160   บAutor  ณMicrosiga           บ Data ณ  06/27/06   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ                                                            บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Static Function ReportDef()
Local oReport 
Local cDesc1  	:= STR0001				//"Extrato de Tarefas                "
Local aOrd    	:= {STR0004,STR0005}	//"Matricula"###"Centro de Custo"
Local cTitFil
Local oBreakCC
Local cTitMat
Local oBreakMAT

DEFINE REPORT oReport NAME "GPER160" TITLE OemToAnsi(STR0001) PARAMETER "GP160R1" ACTION {|oReport| R160Imp(oReport)} DESCRIPTION OemtoAnsi(STR0040)  TOTAL IN COLUMN
	DEFINE SECTION oSRA OF oReport TITLE OemToAnsi(STR0001) ORDERS aOrd TABLES "SRA","SRJ" TOTAL IN COLUMN TOTAL TEXT STR0021 

		TRPosition():New(oSRA,"SI3",1,{|| RhFilial("SI3",(cAlias)->RA_FILIAL)+(cAlias)->RA_CC},.T.)
		TRPosition():New(oSRA,"SRJ",1,{|| RhFilial("SRJ",(cAlias)->RA_FILIAL)+(cAlias)->RA_CODFUNC},.T.)

		DEFINE CELL NAME "RA_FILIAL" 	OF oSRA ALIAS "SRA" TITLE STR0028 
		DEFINE CELL NAME "RA_MAT" 		OF oSRA ALIAS "SRA" TITLE STR0004
		DEFINE CELL NAME "RA_NOME" 		OF oSRA ALIAS "SRA" TITLE STR0029
		DEFINE CELL NAME "RA_CC" 		OF oSRA ALIAS "SRA" TITLE STR0005	 
		DEFINE CELL NAME "I3_DESC" 		OF oSRA ALIAS "SI3"	TITLE STR0030
		DEFINE CELL NAME "RA_CODFUNC"	OF oSRA ALIAS "SRA" TITLE STR0031
		DEFINE CELL NAME "RJ_DESC" 		OF oSRA ALIAS "SRJ"	TITLE STR0032


	DEFINE SECTION oSRO OF oSRA TITLE STR0034 TABLES "SRO","SRX" TOTAL IN COLUMN TOTAL TEXT STR0021
		TRPosition():New(oSRO,"SRX",1,{|| RhFilial("SRX",SRO->RO_FILIAL)+"59" + SRO->RO_CODTAR + "1"  },.T.)

		DEFINE CELL NAME "RO_DATA" 		OF oSRO ALIAS "SRO" TITLE STR0033 		SIZE 12 ALIGN LEFT
		DEFINE CELL NAME "RO_CODTAR" 	OF oSRO ALIAS "SRO" TITLE STR0034 
		DEFINE CELL NAME "RX_TXT"		OF oSRO ALIAS "SRX" TITLE STR0035 		SIZE 20
		DEFINE CELL NAME "RO_VALOR" 	OF oSRO ALIAS "SRO" TITLE STR0036		Size 20
		DEFINE CELL NAME "RO_QUANT" 	OF oSRO ALIAS "SRO" TITLE STR0037		Size 20
		DEFINE CELL NAME "RO_VALTOT"	OF oSRO ALIAS "SRO" TITLE STR0038		Size 20

		DEFINE BREAK oBreakCC OF oSRO WHEN  {|| Dtos(SRO->RO_DATA) + SRO->RO_CODTAR } TITLE STR0015 //"TOTAL DA TAREFA --> "

		oBreakCC:OnBreak({|x,y| cTitFil := fAjustaMsg(x)})  // retornando a mensagem da quebra ( total da tarefa )
	   	oBreakCC:SetTotalText( {|| cTitFil } )
	
		DEFINE FUNCTION FROM oSRO:Cell("RO_VALOR")	FUNCTION SUM TITLE STR0036  PICTURE "@E 99,999,999.9999" BREAK oBreakCC NO END SECTION
		DEFINE FUNCTION FROM oSRO:Cell("RO_QUANT") 	FUNCTION SUM TITLE STR0037  PICTURE "@E 99,999,999.9999" BREAK oBreakCC NO END SECTION
		DEFINE FUNCTION FROM oSRO:Cell("RO_VALTOT") FUNCTION SUM TITLE STR0039 	PICTURE "@E 99,999,999.9999" BREAK oBreakCC NO END SECTION

		DEFINE BREAK oBreakMAT OF oReport WHEN  {|| (cAlias)->RA_FILIAL + (cAlias)->RA_MAT } TITLE STR0015 	//"TOTAL DA TAREFA --> "
		oBreakMat:SetPageBreak() 
		
		oBreakMat:OnBreak({|x,y| cTitMat := STR0021+Substr(x,3,6)})  // retornando a mensagem da quebra ( total da tarefa )
   		oBreakMat:SetTotalText( {|| cTitMat } )

		DEFINE FUNCTION FROM oSRO:Cell("RO_VALOR")	FUNCTION SUM TITLE STR0036  PICTURE "@E 99,999,999.9999" BREAK oBreakMat NO END SECTION
		DEFINE FUNCTION FROM oSRO:Cell("RO_QUANT") 	FUNCTION SUM TITLE STR0037  PICTURE "@E 99,999,999.9999" BREAK oBreakMat NO END SECTION
		DEFINE FUNCTION FROM oSRO:Cell("RO_VALTOT") FUNCTION SUM TITLE STR0039 	PICTURE "@E 99,999,999.9999" BREAK oBreakMat NO END SECTION
Return oReport


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGPER160   บAutor  ณMicrosiga           บ Data ณ  06/27/06   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ                                                            บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Static Function R160Imp(oReport)
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ  Declaracao de variaveis                                         ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

Local oSection1 	:= oReport:Section(1)
Local oSection2 	:= oReport:Section(1):Section(1)
Local cSitQuery		:= ""
Local nReg			:= 0   
Local nOrdem		:= oSection1:GetOrder()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Define Variaveis Locais (Programa)                           ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Local cSeek    	:= ""
Local nOrd     	:= 1
Local cDataAnt 	:= ""
Local cCodAnt  	:= ""
Local cAcessaSRA:= &( " { || " + ChkRH( "GPER160" , "SRA" , "2" ) + " } " )
Local cAcessaSRO:= &( " { || " + ChkRH( "GPER160" , "SRO" , "2" ) + " } " )
Local cCcAnt    := Space(9)
Local cOrdem 	:= ""
Local cIndCond	:= ""
Local cCond		:= ""
Local cTitCC	:= ""

Private aTarefas    := {}      // Array que contera os valores de tarefa
Private aCodFol 	:= {}	   // Matriz com Codigo da folha

oSection2:SetLeftMargen(10)


//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Carregando variaveis mv_par?? para Variaveis do Sistema.     ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
cFilPar    := mv_par01
cCcPar     := mv_par02
cMatPar    := mv_par03
cSituacao  := mv_par04
nDataTar   := mv_par05
dDataDe    := mv_par06
dDataAte   := mv_par07

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Faz filtro no arquivo...                                                 ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
cAlias	:= GetNextAlias()

//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
MakeSqlExpr("GP160R1")

//-- Modifica variaveis para a Query
For nReg:=1 to Len(cSituacao)
	cSitQuery += "'"+Subs(cSituacao,nReg,1)+"'"
	If ( nReg+1 ) <= Len(cSituacao)
		cSitQuery += "," 
	Endif
Next nReg     
cSitQuery := "%" + cSitQuery + "%"

If nOrdem == 1
	cOrdem := "%RA_FILIAL,RA_MAT%"
ElseIf nOrdem == 2
	cOrdem := "%RA_FILIAL, RA_CC, RA_MAT%"
Endif

BEGIN REPORT QUERY oSection1

BeginSql alias cAlias

SELECT SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_CC, SRA.RA_CODFUNC, SRA.RA_NOME 
FROM %table:SRA% SRA 
WHERE	SRA.RA_SITFOLH IN (%exp:Upper(cSitQuery)%) AND
	  	SRA.%notDel%   
		ORDER BY %exp:cOrdem%
EndSql

END REPORT QUERY oSection1 PARAM mv_par01, mv_par02, mv_par03
	

cFilialAnt := Space(FWGETTAMFILIAL)
cFuncaoAnt := "    "

//-- Define o total da regua da tela de processamento do relatorio
dbSelectArea(cAlias)
oReport:SetMeter(500)
dbGotop()

While (cAlias)->(!EOF())
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Verifica Quebra de Filial                                    ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If (cAlias)->RA_FILIAL # cFilialAnt
		dbSelectArea(cAlias)
		cFilialAnt := (cAlias)->RA_FILIAL

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Carrega Variaveis Codigos da Folha							 ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If !FP_CODFOL(@aCodFol,(cAlias)->RA_FILIAL)
			Exit
		Endif
	Endif

	/*
	ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	ณConsiste Filiais e Acessos                                             ณ
	ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู*/
	IF !( (cAlias)->RA_FILIAL $ fValidFil() .and. Eval( cAcessaSRA ) )
		dbSelectArea(cAlias)
      	dbSkip()
       	Loop
	EndIF
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Busca Descricao do Centro de Custo                           ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If (cAlias)->RA_CC # cCcAnt
		DescCC((cAlias)->RA_CC,(cAlias)->RA_FILIAL)
		cCcAnt := (cAlias)->RA_CC
	Endif

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Busca Descricao da Funcao do Funcionario                     ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If (cAlias)->RA_CODFUNC # cFuncaoAnt
		DescFun((cAlias)->RA_CODFUNC,(cAlias)->RA_FILIAL)
		cFuncaoAnt := (cAlias)->RA_CODFUNC
	Endif

	//-- Verifica se o usuแrio cancelou a impressใo do relatorio
	If oReport:Cancel()
		Exit
	EndIf               

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Monta Variavel para o Seek() e o dbSetOrder()                ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If nDataTar == 1
		cSeek := (cAlias)->RA_FILIAL + (cAlias)->RA_MAT + DTOS( dDataDe )
		nOrd  := 1
	Else
		cSeek := (cAlias)->RA_FILIAL + (cAlias)->RA_MAT + "001"
		nOrd  := 2
	Endif

	dbSelectArea( "SRO" )
	dbSetOrder(nOrd)
	dbSeek( cSeek , .T. )
	cDataAnt := DTOS( SRO->RO_DATA )
	cCodAnt  := SRO->RO_CODTAR

	oSection1:Init()
                   
	//-- Incrementa a r้gua da tela de processamento do relat๓rio
	oReport:IncMeter()
	lPass:= .t.
	While !EOF() .And.  (cAlias)->RA_FILIAL + (cAlias)->RA_MAT == SRO->RO_FILIAL + SRO->RO_MAT .And. SRO->RO_DATA <= dDataAte
	   	If SRO->RO_DATA >= dDataDe .And. SRO->RO_DATA <= dDataAte 		
			If lPass
				oSection1:PrintLine()   
				oSection2:Init()
				lPass:= .f.
			Endif
	
			/*
			ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			ณConsiste Filiais e Acessos                                             ณ
			ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู*/
			IF !( SRO->RO_FILIAL $ fValidFil() .and. Eval( cAcessaSRO ) )
				dbSelectArea("SRO")
		      	dbSkip()
		       	Loop
			EndIF
			oSection2:PrintLine()   
	 	Endif		
        dbSelectArea("SRO")
		dbSkip()
	Enddo

	oSection2:Finish()
	oSection1:Finish()

	dbSelectArea(cAlias)
	dbSkip()
EndDo

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGPER160   บAutor  ณMicrosiga           บ Data ณ  09/12/06   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณImpressao do rodape /totalizador das informacoes.           บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Static Function fAjustaMsg(x)
	
Return(STR0020+"  "+Substr(x,9,3)+" - "+DescTA(Substr(x,9,3),(cAlias)->RA_FILIAL) ) //"TOTAL TAREFAA---> "
