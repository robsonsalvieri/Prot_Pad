#INCLUDE "TBICONN.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "TCFWFUN.CH"
#INCLUDE "APWEBEX.CH"
#INCLUDE "TCFWDEF.CH"
 
/*/
зддддддддддбддддддддддбдддддддбддддддддддддддддддддддддддддбддддддбдддддддддд©
ЁPrograma  ЁTCFWFUN   Ё Autor Ё Marinaldo de Jesus	       Ё Data Ё04/09/2001Ё
цддддддддддеддддддддддадддддддаддддддддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁWeb Functions do Terminal de Consulta de Funcionarios            Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Void>                                                           Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      Ё Gen┌rico                                                        Ё
цддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё                     ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.      Ё
цддддддддддддбддддддддбдддддддддддбдддддддддддддддддддддддддддддддддддддддддд╢
ЁProgramador Ё Data   Ё BOPS      Ё  Motivo da Alteracao                     Ё
цддддддддддддеддддддддедддддддддддедддддддддддддддддддддддддддддддддддддддддд╢
ЁRicardo D.  Ё05/05/05ЁFN1783     ЁAjuste em TCFWUPDPSW() na gravacao da     Ё
Ё            Ё        Ё/2005      Ёsenha alterada pelo funcionario.          Ё 
ЁEmerson G.R.Ё26/07/05Ё082069     ЁNao permit.acesso a opcoes desabil.no menuЁ
Ё            Ё        Ё	          Ёmesmo qdo.acessadas diretamente p/Browser.Ё
ЁRicardo D.  Ё14/10/05ЁFN3234     ЁRetirar o teste de opcoes desabil.no menu Ё
Ё            Ё        Ё/2005      Ёp/a funcao TCFWESPELHO.                   Ё
ЁKelly       Ё15/02/08Ё140980     ЁAjuste para utilizacao de verbas de em-   Ё
Ё            Ё        Ё           Ёpresas diferentes no recibo de pagto.     Ё
ЁMarcelo     Ё16/06/08Ё146179     ЁAjuste na funcao TCFW013 para priorizar o Ё
Ё            Ё        Ё           Ёuso do rdmake recibo no RHOnLine caso ele Ё
Ё            Ё        Ё           Ёesteja compilado no RPO.                  Ё
ЁJonatas     Ё26/11/08Ё007427/2008ЁAjuste na funcao TCFW013 para utilizar o  Ё
Ё            Ё        Ё           Ёrdmake RECIBO.PRX somente se habilitado   Ё
Ё            Ё        Ё           Ёvia parametro MV_TCFDEMO.                 Ё
ЁRenata      Ё26/01/09Ё142201/2008ЁAjuste na funcao TCFW016 e TCFWIR016 para,Ё
Ё            Ё        Ё           Ёde acordo com os dados informados, paginarЁ
Ё            Ё        Ё           Ёnos informes o nome do Responsavel de     Ё
Ё            Ё        Ё           Ёacordo com a empresa\filial do usuario.   Ё
ЁRenata      Ё03/03/09Ё005130/2009ЁAjuste na funcao TCFWIR016, no ano da dataЁ
Ё            Ё        Ё           Ёpara visualizacao dos informes.		     Ё
ЁLeandro Dr. Ё17/04/09Ё008118/2009ЁTratamento do parametro MV_TCF013A, usado Ё
Ё            Ё        Ё           Ёpara restringir as opcoes do demonstrativoЁ
Ё            Ё        Ё           Ёde pagamento.                             Ё
ЁLuciana   . Ё01/09/09Ё020084/2009ЁInclusao dos campos valor de saque e saldoЁ
Ё            Ё        Ё           Ёreal na consulta de extrato do FGTS       Ё
ЁAllyson M.  Ё08/09/09Ё021321/2009ЁAjuste em TCFW010() para nao complementar Ё
Ё            Ё        Ё           Ёinformacao do campo quando o mesmo estiverЁ
Ё            Ё        Ё           Ёvazio na tabela SRA                       Ё
ЁMauricio MR Ё26/11/09Ё00000027196ЁCriada uma nova categoria para reconhecer Ё
Ё            Ё        Ё/2009      Ёfuncoes Web de usuario com o prefixo "B_" Ё
ЁAlex        Ё29/01/10Ё00000025836Ё AdaptaГЦo GestЦo Corporativa, controles: Ё
Ё            Ё        Ё       2009Ё Grupo de campos de filial.               Ё
ЁMauricio MR Ё21/06/10Ё00000013667ЁRealizado ajuste para permitir acessar    Ё
Ё            Ё        Ё/2010      Ёmais de uma empresa/funcao sem perda de   Ё
Ё            Ё        Ё           Ёconexao.                                  Ё
ЁAllyson M   Ё19/05/11Ё0000011915/ЁAjuste para buscar o arquivo GRHPER.FCH   Ё
Ё            Ё--------Ё       2011Ёda empresa do funcionario. 				 Ё
ЁAllyson M   Ё07/11/11Ё0000028555/ЁAjuste para reinicializar tabela RBC -    Ё
Ё            Ё--------Ё       2011Ёconfiguracao de menus.	 				 Ё
ЁLeandro Dr. Ё15/03/12Ё     TEPWXCЁAjuste na montagem da URL do frame.       Ё
ЁAllyson M   Ё27/07/12Ё 19042/2012ЁAjuste para permitir o acesso a funciona- Ё
Ё            Ё        Ё     TFLDMTЁrios transferidos.   					 Ё
ЁLuis Artuso Ё15/10/12Ё025208/2012ЁAjuste para verificar a data de acesso ao Ё
Ё            Ё        Ё     TFVQMGЁportal, para funcionarios demitidos.		 Ё
ЁLuis Artuso Ё19/10/12Ё025855/2012ЁAjuste para nao permitir funcionarios de- Ё
Ё            Ё        Ё     TFXHERЁmitidos consultem o portal ao clicar no   Ё
Ё            Ё        Ё           Ёbotao 'voltar' do navegador.              Ё
ЁClaudinei S.Ё21/03/13Ё006696/2013ЁAjuste para buscar o arquivo GRHPER.FCH daЁ
Ё            Ё        Ё     TGXGO6Ёfilial caso MV_TCFFIL esteja .T.          Ё
ЁFabio G.    Ё28/03/13Ё		TGWDR8ЁAjuste p/ Liberar acesso Portal RhOnline  Ё
Ё            Ё        Ё     	  Ёpara funcionario demitidos caso o param.  Ё
Ё            Ё        Ё     	  ЁMV_TCFVREN estaja habilitado com S.       Ё
ЁGustavo M.  Ё16/05/13Ё		THCXCHЁAjuste na exibicao do Informe de RendimentЁ
Ё            Ё        Ё     	  Ёcaso utilizado o parametro MV_TCFVREN.    Ё
ЁAllyson M.	 Ё22/05/13Ё     THFJCKЁAjuste na utilizacao das tabelas XX8 e XX9Ё
ЁAllyson M.	 Ё14/08/13Ё     THPPUXЁAjuste p/ nao perder os dados de login ao Ё
Ё            Ё        Ё     	  Ёexecutar a rotina de alterao de senha.    Ё
ЁGustavo M.	 Ё10/01/13Ё     TIBVTYЁAjuste para utilizar o param MV_TCFVREN 	 Ё
Ё            Ё        Ё     	  Ёapenas em func. demitidos.			     Ё
ЁMatheus M.	 Ё15/12/15Ё     TTTSGKЁAjuste para imprimir o logo utilizado     Ё
Ё            Ё        Ё     	  Ёcorretamente no portal GCH.		         Ё
юддддддддддддаддддддддадддддддддддадддддддддддддддддддддддддддддддддддддддддды
 

зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW001  	ЁAutor Ё Marinaldo de Jesus   Ё Data Ё04/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁIndex do Terminal de Consulta                               Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW001()													Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁTerminal de Consulta                                        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW001()

Local cHtml
Local nHandle
Local cFileMsg	    := "TCFEMP.MSG"

Private aEmpresas   := GetEmpSigaMat()
Private cMatInic	:= ""
Private nTamLogin   := 0
Private aMsgEmp	    := {}

HttpFreeSession()

TCF INIT cHtml

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Ponto de Entrada p/ Alterar conteudo da variavel cMatInc	  |
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ExistBlock("TCFSTRLOGIN")
		ExecBlock("TCFSTRLOGIN",.F.,.F.)
	Else
		cMatInic := STR0045	//"Matr&iacute;cula" 
		nTamLogin:= TamSx3("RA_MAT")[1]
	Endif

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁCarrega variavel com o Conteudo do arquivo de Informacoes   daЁ
	Ёempresa que sera mostrado no RH OnLine a Todos os FuncionariosЁ
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	nHandle := fOpen( cFileMsg )
	aMsgEmp := GetAllTxtFile( nHandle )
	fClose( nHandle )

 	cHtml := H_TCFH004()

TCF END cHtml

Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFWINIT  	ЁAutor Ё Marinaldo de Jesus   Ё Data Ё25/08/2004Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁInit do Terminal de Consultas							    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW001()													Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁTerminal de Consulta                                        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFWINIT()

Local cHtml

HttpFreeSession()
TCF INIT cHtml

	cHtml := W_TCFW001()

TCF END cHtml

Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW002 	ЁAutor Ё Marinaldo de Jesus   Ё Data Ё04/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁiFrame com o Logo da Empresa/Foto e Nome do Funcionario     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW002()                             					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico                                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW002()

Local cHtml := ""

Private cNomeFun
Private lShowFoto

WEB EXTENDED INIT cHtml START "TCFWInSite"

	TCF INIT cHtml

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define o Modulo em Uso                          					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		SetModulo( "SIGAGPE" , "GPE" )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Posiciona no Funcionario                     				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		SRA->( dbSetOrder( RetOrdem("SRA") ) )
		SRA->( MsSeek( xFilial( "SRA" , TCFWGetFil() ) + TCFWGetMat() ) )
		
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Carrega no Nome do Funcionario               				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		cNomeFun := Capital( AllTrim( SRA->RA_NOME ) )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Carrega a Foto do Funcionario								   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		HttpSession->cBmpPict := RetPictFun( @lShowFoto )
		
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Carrega o Logo da Empresa/Filial							   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		HttpSession->cLogoEmp := RetLogoEmp()
	
		cHtml += H_TCFH002()

	TCF END cHtml
	
WEB EXTENDED END

Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁRetPictFun	ЁAutor Ё Marinaldo de Jesus   Ё Data Ё19/09/2003Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRetornar a Foto do Funcionario                              Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<Vide Parametros Formais>               					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>               					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁFoto do Funcionario                							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁRhOnLine                                                    Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function RetPictFun( lShowFoto )

Local cBmpPict

/*/
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё Carrega a Foto do Funcionario								   Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
IF !Empty( cBmpPict := Upper( AllTrim( SRA->RA_BITMAP ) ) )

	lShowFoto	:= .T.
	cBmpPict	:= "W_TCFPictFun.apw?IdPictFun="+cBmpPict

Else

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Se nao Existir Foto Para o Funcionario Carrega Imagem padrao Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	lShowFoto	:= .F.
	cBmpPict	:= ( TcfRetDirImg()+"/foto.gif" )

EndIF

Return( cBmpPict )

/*/
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁTCFPictFun	  Ё Autor ЁMarinaldo de Jesus Ё Data Ё25/03/2002Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁChamada via http , recebe via URL o parametro ID, contendo oЁ
Ё          Ёcodigo da foto do funcionario. Retorna a Imagem 			Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁSIGATCF                                                     Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFPictFun()

Local cIMGCode 		:= HttpGet->IdPictFun
Local cIMGSource 	:= ""

Begin Sequence

	IF Empty( cIMGCode )
		Break
	EndIF

	// Carrega a imagem do repositorio em variavel de memoria
	cIMGSource := LoadReposit( cIMGCode )
	FechaReposit() //For┤o o Fechamento o Repositorio de Imangens

	IF ( Len( cIMGSource ) == 0 )
		Break
	EndIF
	
	IF ( Left( cIMGSource , 2 ) == "BM" )
		// Identifica se И BMP ou JPG pelos 2 primeiros bytes
		HttpCTType( "application/xbitmap" )
	Else
		// Se nЦo for BMP ... И JPG ! 
		HttpCTType( "image/jpeg" )
	EndIF
	
	// Seta tamanho da imagem para header HTTP de saida
	HttpCTLen( Len( cIMGSource ) )
		
	// Manda a imagem via HTTPSend 
	cIMGSource := HttpSend( cIMGSource )

End Sequence

// Caso a httpsend nЦo envie toda a imagem , retorna 
// o conteudo remanescente.
Return( cIMGSource )

/*/{Protheus.doc} RetLogoEmp
Retornar no Logo da Empresa de acordo com o grupo de empresas e filial passados
@author  Marinaldo de Jesus
@since   19/09/2003
/*/
Function RetLogoEmp(lPortal, EmpPortal, FilPortal, cNome, lGPER030 )
	
	Local cLogoEmp
	Local cExtension
	Local lShowLogo  	:= .F.
	Local lShowLogo2	:= .F.
	
	Default cNome 		:= "LGRO"
	Default lPortal 	:= .F.
	Default EmpPortal 	:= cEmpAnt
	Default FilPortal	:= cFilAnt
	Default lGPER030	:= .F.
	
	If lPortal
		cNome := "LGRECIBO"
	Endif
	
	Begin Sequence
		
		// Carrega o Logo da Empresa/Filial
		cLogoEmp 	:= ( cNome + EmpPortal + FilPortal )
		
		// Procuro Primeiro pelo Tipo JPG
		cExtension	:= ".JPG"
		IF !lGPER030 .And. (lShowLogo := File( cLogoEmp + cExtension ))
			Break
		EndIF
		
		IF !lGPER030 .And. (lShowLogo2 := File( rTrim(cLogoEmp) + cExtension ))
			Break
		EndIF	
		
		// Se nao encontrei JPG, Procuro BMP
		cExtension	:= ".BMP"
		IF ( lShowLogo := File( cLogoEmp + cExtension ) )
			Break
		EndIF
		
		IF ( lShowLogo2 := File( rTrim(cLogoEmp) + cExtension ) )
			Break
		EndIF
		
		// Carrega o Logo da Empresa
		cLogoEmp := ( cNome + EmpPortal )
		
		// Procuro Primeiro pelo Tipo JPG
		cExtension	:= ".JPG"
		IF !lGPER030 .And.  (lShowLogo := File( cLogoEmp + cExtension ) )
			Break
		EndIF
		
		IF !lGPER030 .And.  (lShowLogo2 := File( rTrim(cLogoEmp) + cExtension ) )
			Break
		EndIF
		
		// Se nao encontrei JPG, Procuro BMP
		cExtension	:= ".BMP"
		IF ( lShowLogo := File( cLogoEmp + cExtension ) )
			Break
		EndIF
		
		IF ( lShowLogo2 := File( rTrim(cLogoEmp) + cExtension ) )
			Break
		EndIF
		
		// Carrega Logo Generico
		If lPortal
			cLogoEmp := "Logo"
		Else
			cLogoEmp := "LGRO"
		EndIf
		
		// Procuro Primeiro pelo Tipo JPG
		cExtension	:= ".JPG"
		IF !lGPER030 .And. (lShowLogo := File( cLogoEmp + ".JPG" ) )
			Break
		EndIF
		
		// Se nao encontrei JPG, Procuro BMP
		cExtension	:= ".BMP"
		IF ( lShowLogo := File( cLogoEmp + cExtension ) )
			Break
		EndIF
		
		// Carrego o Logo Padrao
		cExtension	:= ".JPG"
		cLogoEmp	:= ( TcfRetDirImg() + "/" + cLogoEmp + cExtension )
		
	End Sequence
	
	IF lShowLogo .And. ! lGPER030
		cLogoEmp := "W_TCFLogoEmp.apw?IdLogoEmp=" + ( cLogoEmp + cExtension )
	ElseIf lShowLogo2 .And. ! lGPER030
		cLogoEmp := "W_TCFLogoEmp.apw?IdLogoEmp=" + ( rTrim(cLogoEmp) + cExtension )
	ElseIf lShowLogo .And. lGPER030
		cLogoEmp := cLogoEmp + cExtension
	ElseIf lShowLogo2 .And. lGPER030
		cLogoEmp := rTrim(cLogoEmp) + cExtension
	EndIF
	
Return( cLogoEmp )

/*/
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁW_TCFLogoEmp  Ё Autor ЁMarinaldo de Jesus Ё Data Ё25/08/2004Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁChamada via http , recebe via URL o parametro ID, contendo oЁ
Ё          Ёnome do arquivo do Logo. Retorna a Imagem 					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁSIGATCF                                                     Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFLogoEmp()

Local cLogoFile		:= HttpGet->IdLogoEmp
Local cLogoSource 	:= ""

Local nLenLogoSource

Begin Sequence

	IF Empty( cLogoFile )
		Break
	EndIF

	// Carrega a imagem do repositorio em variavel de memoria
	cLogoSource := TCFLoadLogo( cLogoFile )

	nLenLogoSource := Len( cLogoSource )
	IF ( nLenLogoSource == 0 )
		Break	//NЦo Tem Imagem alguma a enviar
	EndIF
	
	IF ( Left( cLogoSource , 2 ) == "BM" )
		// Identifica se И BMP ou JPG pelos 2 primeiros bytes
		HttpCTType( "application/xbitmap" )
	Else
		// Se nЦo for BMP ... И JPG ! 
		HttpCTType( "image/jpeg" )
	EndIF
	
	// Seta tamanho da imagem para header HTTP de saida
	HttpCTLen( nLenLogoSource )
		
	// Manda a imagem via HTTPSend 
	cLogoSource := HttpSend( cLogoSource )

End Sequence

// Caso a httpsend nЦo envie toda a imagem , retorna 
// o conteudo remanescente.
Return( cLogoSource )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFLoadLogo	ЁAutor Ё Marinaldo de Jesus   Ё Data Ё25/08/2004Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁObtem o Binario com os dados do Logo da Empresa             Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ё<vide parametros formais>									Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁTerminal de Consulta                                        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function TCFLoadLogo( cLogoFile )

Local nHandle 		:= fOpen( cLogoFile )
Local aAllLogoFile	:= GetAllTxtFile( nHandle )
Local cBinaryFile	:= ""

Local nLoop
Local nLoops

nLoops := Len( aAllLogoFile )
For nLoop := 1 To nLoops
	cBinaryFile += aAllLogoFile[ nLoop ]
Next nLoop
fClose( nHandle )

Return( cBinaryFile )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW003		ЁAutor Ё Marinaldo de Jesus   Ё Data Ё04/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁIndex do Terminal de Consulta                               Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW003()													Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁTerminal de Consulta                                        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW003()

Local cHtml

TCF INIT cHtml

	cHtml := H_TCFH003()

TCF END cHtml

Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW004		ЁAutor Ё Marinaldo de Jesus   Ё Data Ё04/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁTela de Entrada do Terminal de Consultas                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW004()													Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁTerminal de Consulta                                        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW004()

Local	cHtml
Private aEmpresas := GetEmpSigaMat()
Private cMatInic	:= ""
Private nTamLogin   := 0

HttpFreeSession()

TCF INIT cHtml

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Ponto de Entrada p/ Alterar conteudo da variavel cMatInc	  |
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ExistBlock("TCFSTRLOGIN")
		ExecBlock("TCFSTRLOGIN",.F.,.F.)
	Else
		cMatInic := STR0045	//"Matr&iacute;cula" 
		nTamLogin:= TamSx3("RA_MAT")[1]
	Endif

 	cHtml := ExecInPage("TCFH004")// H_TCFH004()

TCF END cHtml

Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW005		ЁAutor Ё Marinaldo de Jesus   Ё Data Ё04/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁiFrame do Terminal de Consultas								Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW005()													Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁTerminal de Consulta                                        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW005()

	Local cHtml := ""
	
	WEB EXTENDED INIT cHtml START "TCFWInSite"
	
		TCF INIT cHtml
	                   
			cHtml += H_TCFH005()
		
		TCF END cHtml	
	
	WEB EXTENDED END

Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW006		ЁAutor Ё Marinaldo de Jesus   Ё Data Ё04/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁiFrame de Informacoes do Funcionario para Terminal de ConsulЁ
Ё          Ёtas														    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW006()	                            					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico                                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW006()

Local cHtml 	:= ""

Local cMsgFun
Local cFileMsg
Local nHandle

Private aMsgFun		:= {}

WEB EXTENDED INIT cHtml START "TCFWInSite"

	TCF INIT cHtml

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define o Modulo em Uso                          					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		SetModulo( "SIGAGPE" , "GPE" )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Posiciona no Funcionario                     				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		SRA->( dbSetOrder( RetOrdem("SRA") ) )
		SRA->( MsSeek( xFilial( "SRA" , TCFWGetFil() ) + TCFWGetMat() ) )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Carrega a Mensagem Para o Funcionario						   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		IF Empty( cMsgFun := MsMm(SRA->RA_TCFMSG) )
			aAdd( aMsgFun , "<center><b>" + STR0035 + "</b></center>" ) //"Bem Vindo ao RH OnLine"
		Else
			cFileMsg := ( CriaTrab( NIL , .F. ) + ".MSG" )
			MemoWrite( cFileMsg , cMsgFun )
			nHandle := fOpen( cFileMsg )
			aMsgFun := GetAllTxtFile( nHandle )
			fClose( nHandle )
			fErase( cFileMsg )
		EndIF	
		
		cHtml += H_TCFH006()

	TCF END cHtml
	
WEB EXTENDED END

Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW007    	ЁAutor Ё Marinaldo de Jesus   Ё Data Ё04/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁiFrame de Menu de Opcoes do Terminal de Consultas           Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW007()                                					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico                                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW007()

Local aUnique		:= { "RBC_FILIAL" , "RBC_CODMNU" }
Local cFilRBC		:= ""
Local cRotina		:= ""
Local cDescri		:= ""
Local cHtml 		:= ""

Private aMenuTcf	:= {}

WEB EXTENDED INIT cHtml START "TCFWInSite"

	TCF INIT cHtml

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define o Modulo em Uso                          					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		SetModulo( "SIGAGPE" , "GPE" )
	
		//-- Reinicializa a variavel statica do POSALIAS
		//-- Util na troca de empresas ...
		fSrvStatic()
		RstPosAlias("RBC")
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Posiciona no Menu conforme Filial            				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		RBC->( MsSeek( ( cFilRBC := xFilial("RBC") ) ) )
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Carrega o Array aMenuTcf com Informacoes para a Montagem   doЁ
		Ё Menu do RH OnLine											   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		While RBC->( !Eof() .and. ( RBC_FILIAL == cFilRBC ) )
			
			IF RBC->( UniqueKey( aUnique ) )
				aAdd( aMenuTcf , {} )
			EndIF
					
			IF ( RBC->RBC_STATUS == "A" )
				
				IF !Empty( cRotina := AllTrim(RBC->RBC_ROTINA) )
					IF FindFunction( "w_"+cRotina )
						/*/
						здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						Ё Web Functions w_?	ou W_?									   Ё
						юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
						cRotina := "w_"+cRotina+".APW"
					ElseIF FindFunction( "l_"+cRotina )
						/*/
						здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						Ё UserHtml Functions l_? ou L_?								   Ё
						юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
						cRotina := "l_"+cRotina+".APW"
					ElseIF FindFunction( "h_"+cRotina )
						/*/
						здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						Ё Html Functions h_? ou H_?								   	   Ё
						юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
						cRotina := "h_"+cRotina+".APW"
					ElseIF FindFunction( ALLTRIM(UPPER("B_"+cRotina)) )
						/*/
						здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						Ё Web User Functions b_? 								   	   Ё
						юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
						cRotina := "B_"+cRotina+".APW"						
					Else
						//"Fun&ccedil;&atilde;o n&atilde;o encontrada no Reposit&oacute;rio"
						//"Existem inconsist&ecirc;ncias a serem verificadas:"
						cRotina := "w_TCFNoFunction.APW?NoFunction="+cRotina
					EndIF	
				Else
					cRotina := "#"
				EndIF
				
				cDescri := Capital( AllTrim( RBC->RBC_DESCRI ) )
					
				If Len( aMenuTcf ) > 0
					aAdd( aMenuTcf[Len( aMenuTcf ) ],	{; 
									  						cDescri ,; //Descricao do Menu
															cRotina	 ; //Programa a Ser Executado
									 					};
						   )
				EndIf				
			EndIF
			
			RBC->( dbSkip() )
		
		End While
		
		cHtml += H_TCFH007()

	TCF END cHtml	
			
WEB EXTENDED END

Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW008   	ЁAutor Ё Marinaldo de Jesus   Ё Data Ё11/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁOpcoes do Espelho de Ponto Para o Terminal de Consulta      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW008()													Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁTerminal de Consulta                                        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW008()

Local cHtml			:= ""

Private aPeriodos	:= {}

WEB EXTENDED INIT cHtml START "TCFWInSite"

	TCF INIT cHtml
 
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Opcao habilitada Configurador de Menu          					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		If TcfChkMenu()
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Define o Modulo em Uso                          					 Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			SetModulo( "SIGAPON" , "PON" )
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Inicializa as Static do SIGAPON                 					 Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			PonDestroyStatic()
		 
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Posiciona no Funcionario                     				   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			SRA->( dbSetOrder( RetOrdem("SRA") ) )
			SRA->( MsSeek( xFilial( "SRA" , TCFWGetFil() ) + TCFWGetMat() ) )
			
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁRetorna os Periodos de Apontamento Validos prara o FuncionarioЁ
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			aPeriodos := {}
			SPO->(DbGoTop())
			IF !Empty( aPeriodos := GetPerAponta( GetMv("MV_TCFMARC",,12) , TCFWGetFil() , TCFWGetMat() , .F. , NIL , NIL , RetPerSPO( TCFWGetFil() ) ) )
				cHtml += H_TCFH008()
			Else
				//"Consultar Marca&ccedil;&otilde;es"###"N&atilde;o existem marca&ccedil;&otilde;es neste per&iacute;odo" 
				cHtml += HtmlDefault( STR0020 , STR0009 ) 
			EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Opcao nao habilitada Configurador de Menu        					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		Else
			//"Consultar Marca&ccedil;&otilde;es"###"OpГЦo nЦo estА habilitada no menu do Terminal de Consultas"
			cHtml += HtmlDefault( STR0020 , STR0041 ) 
		EndIf	
		
	TCF END cHtml
	
WEB EXTENDED END

Return( cHtml )

Web Function TCFWEspelho()

Local cHtml			:= ""

WEB EXTENDED INIT cHtml START "TCFWInSite"

	TCF INIT cHtml

			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Define o Modulo em Uso                          					 Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			SetModulo( "SIGAPON" , "PON" )
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Inicializa as Static do SIGAPON                 					 Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			PonDestroyStatic()
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Posiciona no Funcionario                     				   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			SRA->( dbSetOrder( RetOrdem("SRA") ) )
			SRA->( MsSeek( xFilial( "SRA" , TCFWGetFil() ) + TCFWGetMat() ) )
			
			If ExistBlock("IMPESP")
				IF EmptyHtml( cHtml += u_IMPESP( .T. , TCFWGetFil() , TCFWGetMat() , HttpPost->cPerAponta ) )
					//"Consultar Marca&ccedil;&otilde;es"###"N&atilde;o existem marca&ccedil;&otilde;es neste per&iacute;odo"
					cHtml += HtmlDefault( STR0020 , STR0009 ) 
				EndIF			
			Else
				/*/
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				ЁChama o Ponr010 para gerar o Codigo HTML correspondente ao  EsЁ
				Ёpelho de Ponto												   Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				IF EmptyHtml( cHtml += Ponr010( .T. , TCFWGetFil() , TCFWGetMat() , HttpPost->cPerAponta ) )
					//"Consultar Marca&ccedil;&otilde;es"###"N&atilde;o existem marca&ccedil;&otilde;es neste per&iacute;odo"
					cHtml += HtmlDefault( STR0020 , STR0009 ) 
				EndIF
			EndIf

	TCF END cHtml
			
WEB EXTENDED END

Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW009   	ЁAutor Ё Marinaldo de Jesus   Ё Data Ё04/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁiFrame com os Dados do Rodape do Terminal de Consultas      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW009()	                            					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico                                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW009()

Local cHtml

TCF INIT cHtml

	cHtml := H_TCFH009()

TCF END cHtml

Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW010		ЁAutor Ё Marinaldo de Jesus   Ё Data Ё04/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁInformacoes do Cadastro de Funcionarios (Consulta)          Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW010()	                            					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico                                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW010()

Local aDicFch		:= {}
Local aDicion		:= {}
Local cFile			:= If( SuperGetMv("MV_TCFFIL",, .F., )== .T., "GRHPER" + cEmpAnt + cFilAnt + ".FCH", "GRHPER" + cEmpAnt + ".FCH")
Local cBuffer		:= ""
Local cConteudo		:= ""
Local cHtml			:= ""
Local cPict			:= ""
Local cXF3			:= "XM0"
Local cTCFCxFx		:= SuperGetMv("MV_TCFCXFX",,'')
Local lCapital		:= .T.
Local nHdl			:= 0
Local nDicion		:= 0
Local nForDicion	:= 0
Local nTamArq		:= 0
Local uVar			
Local uVarAux 

Private aFunc := {}

WEB EXTENDED INIT cHtml START "TCFWInSite"

	TCF INIT cHtml
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Opcao habilitada Configurador de Menu          					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		If TcfChkMenu()
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Define o Modulo em Uso                          					 Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			SetModulo( "SIGAGPE" , "GPE" )
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁMonta Array com as Informacoes do Funcionario a Ser   MostradoЁ
			Ёno Terminal de Consultas									   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			IF File( cFile )
			
				nHdl := fOpen(cfile,64)
				fSeek(nHdl,0,0)
				nTamArq := fSeek(nHdl,0,2)
				fSeek(nHdl,0,0)
				nForDicion := ( nTamArq / 63 )
			
				For nDicion := 1 To nForDicion
					cBuffer := Space(63)
					fRead(nHdl,@cBuffer,63)
					aAdd(aDicFch,cBuffer)
				Next nDicion
			
				fClose(nHdl)
			
				aEval( aDicFch ,{ |x| IF(SubStr(x,32,1) == "X" ,aAdd(aDicion,{SubStr(x,04,12),SubStr(x,44,10),"","",""}),"")})
				
				IF ( ( nForDicion := Len( aDicion ) ) > 0 )
			
					// Alimenta o aDicion com os dados do X3_F3
					SX3->( dbSetOrder(2) )
			
					For nDicion := 1 To nForDicion
						IF SX3->( MsSeek( aDicion[nDicion,2] ) )
							aDicion[ nDicion , 3 ] := SX3->( IF( Empty( X3_F3 ) , ( IF ( !Empty( X3_CBOX ) , "CBOX" ,"" ) ) , IF( SubStr( X3_F3 , 1 , 1 ) == "X" .and. !( AllTrim( Upper( X3_F3 ) ) $ cXF3 ) , "SRX" , "F3" ) ) )
							aDicion[ nDicion , 4 ] := SX3->X3_PICTURE
							aDicion[ nDicion , 5 ] := SX3->X3_F3
						EndIF	
					Next nDicion                  
			
					SX3->( dbSetOrder(1) )
		
					/*/
					здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					Ё Posiciona no Funcionario                     				   Ё
					юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
					SRA->( dbSetOrder( RetOrdem("SRA") ) )
					IF SRA->( MsSeek( xFilial( "SRA" , TCFWGetFil() ) + TCFWGetMat() ) )
						// Preencher os campos do SRA conforme as tabelas ou arquivos
						For nDicion := 1 To nForDicion
							cPict	:= ""
							uVarAux	:= ""
							uVar	:= SRA->( &( aDicion[ nDicion , 2 ] ) )
							IF !Empty(uVar) .And. !Empty( aDicion[ nDicion , 3 ] )
								uVarAux := TcfTabela( TCFWGetFil() , aDicion[ nDicion , 3 ] , AllToChar( uVar ) , aDicion[ nDicion , 2 ] , aDicion[ nDicion , 5 ] , @cPict )
							EndIF
							uVar := AllToChar( uVar , aDicion[ nDicion , 4 ] )
							IF !Empty( uVarAux )
								uVar += ( " - " +  AllToChar( uVarAux , cPict ) )
							EndIF	
							If AllTrim(aDicion[ nDicion , 2 ]) $ cTCFCxFx .or. cTCFCxFx == '*'
								lCapital := .F.
							Else
								lCapital := .T.
							EndIf
							aAdd( aFunc , { ( SubStr( aDicion[ nDicion , 1 ] , 01 , 12 ) + ":" ) , uVar , lCapital } )
						Next nDicion
					EndIF	
			
					cHtml += H_TCFH010()
			
				Else
			
					//"Consultar Dados Cadastrais""###"Falta definir os campos do Cadastro de Funcionarios""
					cHtml += HtmlDefault( STR0021 , STR0004 )
			
				EndIF
			
			Else
				 //"Consultar Dados Cadastrais""###"Falta definir os campos do Cadastro de Funcionarios""
				cHtml += HtmlDefault( STR0021 , STR0004 )
			EndIF
        
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Opcao nao habilitada Configurador de Menu        					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		Else
			//"Consultar Dados Cadastrais""###"OpГЦo nЦo estА habilitada no menu do Terminal de Consultas"
			cHtml += HtmlDefault( STR0021 , STR0041 ) 
		EndIf	
		
	TCF END cHtml
	
WEB EXTENDED END

Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW011    	ЁAutor Ё Marinaldo de Jesus   Ё Data Ё04/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁInformacoes de Ferias do Funcionario (Consulta)           	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW011()                                					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico                                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW011()

Local cHtml				:= ""
Local dIniAquisFerias	:= Ctod("//")
Local dFimAquisFerias	:= Ctod("//")
Local dIniIdealFerias	:= Ctod("//")
Local dFimIdealFerias	:= Ctod("//")

Private aFerias:= {}

WEB EXTENDED INIT cHtml START "TCFWInSite"

	TCF INIT cHtml
            
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Opcao habilitada Configurador de Menu          					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		If TcfChkMenu()
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Define o Modulo em Uso                          					 Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			SetModulo( "SIGAGPE" , "GPE" )
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Posiciona no Funcionario                     				   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			SRF->( dbSetOrder( RetOrdem("SRF") ) )
			IF SRF->( MsSeek( xFilial( "SRF" , TCFWGetFil() ) + TCFWGetMat() ) )
			
				/*/
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				ЁMonta Array com as Informacoes de Ferias do Funcionario       Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				While SRF->( !Eof() .and. ( RF_FILIAL + RF_MAT ) == ( TCFWGetFil() + TCFWGetMat() ) )
			
					dIniAquisFerias := SRF->RF_DATABAS
					dFimAquisFerias := fCalcFimAq( dIniAquisFerias )
					dIniIdealFerias := dFimAquisFerias + 30
					dFimIdealFerias := ( fCalcFimAq( dFimAquisFerias + 1 ) - 45 )
			
					SRF->(;
							 aAdd(	aFerias,	{;
													Dtoc( dIniAquisFerias ),;									//Data Base
													AllTrim( Str( RF_DFERVAT - RF_DFERANT ) ) ,;				//Dias de Ferias Vencidas
													AllTrim( Str( RF_DFERAAT ) ) ,;								//Dias de Ferias Proporcionais		
													Dtoc(dIniIdealFerias) + " a " + Dtoc(dFimIdealFerias),;	//Periodo para Gozo
													Dtoc(SRF->RF_DATAINI),;										//Data Inicial das Ferias
													Str(SRF->RF_DFEPRO1),;										//Dias de Ferias Programadas
													Str(SRF->RF_DABPRO1);										//Dias de Abonos Programados
												};
								   );
						  )		   	
					
					/*/
					здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					ЁSe Tiver Gozo de Ferias Dividido em mais de um Periodo        Ё
					юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
					IF !Empty( SRF->RF_DATINI2 )
						SRF->( aAdd(aFerias,{"","","","",Dtoc(RF_DATINI2),AllTrim( Str(RF_DFEPRO2) ), AllTrim( Str(RF_DABPRO2))}) )
					EndIF
					
					IF !Empty( SRF->RF_DATINI3 ) 
						SRF->( aAdd(aFerias,{"","","","",Dtoc(RF_DATINI3),AllTrim( Str(RF_DFEPRO3) ), AllTrim( Str(RF_DABPRO3))}) )
					EndIF
					
					SRF->( dbSkip() )
			
				End While
			
				cHtml += H_TCFH011()
			
			Else
			
				//"Consultar Dados de F&eacute;rias"###"N&atilde;o h&aacute; dados de F&eacute;rias a serem visualizados"
				cHtml += HtmlDefault( STR0005 , STR0002 ) 
			
			EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Opcao nao habilitada Configurador de Menu        					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		Else
			//"Consultar Dados de F&eacute;rias"###"OpГЦo nЦo estА habilitada no menu do Terminal de Consultas"
			cHtml += HtmlDefault( STR0005 , STR0041 ) 
		EndIf	
			
	TCF END cHtml
	
WEB EXTENDED END

Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW012		ЁAutor Ё Marinaldo de Jesus   Ё Data Ё11/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁInformacoes de FGTS do Funcionario (Consulta)           	Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW011()	                                				Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico                                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW012()

Local cHtml	  := ""

Private aFgts := {}

WEB EXTENDED INIT cHtml START "TCFWInSite"

	TCF INIT cHtml
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Opcao habilitada Configurador de Menu          					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		If TcfChkMenu()
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Define o Modulo em Uso                          					 Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			SetModulo( "SIGAGPE" , "GPE" )
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Posiciona no Funcionario                     				   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			SRS->( dbSetOrder( RetOrdem("SRS") ) )
			IF SRS->( MsSeek( xFilial( "SRS" , TCFWGetFil() ) + TCFWGetMat() ) )
		
				/*/
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				ЁMonta Array com as Informacoes de FGTS do Funcionario         Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				While SRS->( !Eof() .and. ( RS_FILIAL + RS_MAT ) == ( TCFWGetFil() + TCFWGetMat() ) )
			
					SRS->( aAdd(aFgts,	{; 
											( RS_MES + "/" + RS_ANO ),;
											Transform(RS_SALANT,"@E 999,999,999.99"),;
											Transform(RS_PERJUR,"@E 99.99"),;
											Transform(RS_VALJUR,"@E 999,999,999.99"),;
											Transform(RS_VALDEP,"@E 999,999,999.99"),; 
											Transform(RS_VALSAQ,"@E 999,999,999.99"),; 
											Transform(RS_SALATU,"@E 999,999,999.99"),;
											Transform(RS_SALREAL,"@E 999,999,999.99");
										 };
								);
						  )
			
					SRS->( dbSkip() )
			
				End While
			
				/*/
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				ЁOrdena o Array aFgts em Ordem Decrescente de Data             Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				IF !Empty( aFgts )
					aSort( aFgts ,,, { |x,y| SubStr(x[1],-4)+SubStr(x[1],1,2) > SubStr(y[1],-4)+SubStr(y[1],1,2) } )
				EndIF
			
				cHtml += H_TCFH012()
			
			Else
			
				 //"Consultar Dados de FGTS"###"N&atilde;o h&aacute; dados de FGTS a serem visualizados" 
				 cHtml += HtmlDefault( STR0022 , STR0001 )
			
			EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Opcao nao habilitada Configurador de Menu        					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		Else
			//"Consultar Dados de FGTS"###"OpГЦo nЦo estА habilitada no menu do Terminal de Consultas"
			cHtml += HtmlDefault( STR0022 , STR0041 ) 
		EndIf	
		
	TCF END cHtml
	
WEB EXTENDED END

Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW013   	ЁAutor Ё Marinaldo de Jesus   Ё Data Ё11/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁTela de Opcoes do Demonstrativo de Pagammento do TCF        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW013()													Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico                                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW013()

Local cHtml := ""

WEB EXTENDED INIT cHtml START "TCFWInSite"

	TCF INIT cHtml
            
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Definie opcoes de impressao do demonstrativo de pagamento.
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		HttpSession->cOpcRec := SuperGetMv("MV_TCF013A",,"01.02.03.04.05")
		
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Opcao habilitada Configurador de Menu          					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		If TcfChkMenu()
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Define o Modulo em Uso                          					 Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			SetModulo( "SIGAGPE" , "GPE" )
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Posiciona no Funcionario                     				   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			SRA->( dbSetOrder( RetOrdem("SRA") ) )
			SRA->( MsSeek( xFilial( "SRA" , TCFWGetFil() ) + TCFWGetMat() ) )
			
			cHtml += H_TCFH013()

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Opcao nao habilitada Configurador de Menu        					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		Else
			//"Demonstrativo de pagamento"###"OpГЦo nЦo estА habilitada no menu do Terminal de Consultas"
			cHtml += HtmlDefault( STR0023 , STR0041 ) 
		EndIf	
		
	TCF END cHtml
	
WEB EXTENDED END
		
Return( cHtml )
		
Web Function TCFWShowDemo()
		
Local cHtml			:= ""
Local cSvHtml		:= ""
Local nRecnoSRA		:= 0
Local lRecibo		:= ( SuperGetMv("MV_TCFDEMO",,"2") == "1" ) // Define se utiliza o rdmake RECIBO.PRX na emissao do recibo de pagto.
		
WEB EXTENDED INIT cHtml START "TCFWInSite"

	TCF INIT cHtml

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define o Modulo em Uso                          					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		SetModulo( "SIGAGPE" , "GPE" )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Posiciona no Funcionario                     				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		SRA->( dbSetOrder( RetOrdem("SRA") ) )
		IF SRA->( MsSeek( xFilial( "SRA" , TCFWGetFil() ) + TCFWGetMat() ) )
		
			nRecnoSRA := SRA->( Recno() )
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁChama o Gper030 para gerar o Codigo HTML correspondente ao  DeЁ
			Ёmonstrativo de Pagamento									   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			cSvHtml := cHtml
			If lRecibo .And. ExistBlock("RECIBO") 
			
				IF EmptyHtml( cHtml += u_Recibo(.T.,TCFWGetFil(),TCFWGetMat(),StrZero(Val(HttpPost->cMes),2) + StrZero(Val(HttpPost->cAno),4),Val(HttpPost->cRecTipo),HttpPost->cSemana) )
					HttpPost->cSemana := StrZero( Val( HttpPost->cSemana ) , TamSx3( "RC_SEMANA" ) [1] )
					cHtml	:= cSvHtml
					SRA->( dbGoto( nRecnoSRA ) )
					IF EmptyHtml( cHtml += u_Recibo(.T.,TCFWGetFil(),TCFWGetMat(),StrZero(Val(HttpPost->cMes),2) + StrZero(Val(HttpPost->cAno),4),Val(HttpPost->cRecTipo),HttpPost->cSemana) )
						//"Demonstrativo de Pagamento" ###"N&atilde;o existem valores a serem visualizados"
						cHtml += HtmlDefault( STR0023 , STR0003 )
					EndIF
				EndIF			
			
			Else
			
				IF EmptyHtml( cHtml += Gper030(.T.,TCFWGetFil(),TCFWGetMat(),StrZero(Val(HttpPost->cMes),2) + StrZero(Val(HttpPost->cAno),4),Val(HttpPost->cRecTipo),HttpPost->cSemana) )
					HttpPost->cSemana := StrZero( Val( HttpPost->cSemana ) , TamSx3( "RC_SEMANA" ) [1] )
					cHtml	:= cSvHtml
					SRA->( dbGoto( nRecnoSRA ) )
					IF EmptyHtml( cHtml += Gper030(.T.,TCFWGetFil(),TCFWGetMat(),StrZero(Val(HttpPost->cMes),2) + StrZero(Val(HttpPost->cAno),4),Val(HttpPost->cRecTipo),HttpPost->cSemana) )
						//"Demonstrativo de Pagamento" ###"N&atilde;o existem valores a serem visualizados"
						cHtml += HtmlDefault( STR0023 , STR0003 )
					EndIF
				EndIF

			EndIF
		
		Else
	
			//"Demonstrativo de Pagamento" ###"N&atilde;o existem valores a serem visualizados"
			cHtml += HtmlDefault( STR0023 , STR0003 )
	
		EndIF	

	TCF END cHtml
	
WEB EXTENDED END

Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW014   	ЁAutor Ё Marinaldo de Jesus   Ё Data Ё11/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁTela de Cadastro/Alteracao de Senhas						Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW014()													Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico                                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW014()

Local cHtml
Local cMatric	  := TCFWGetMat() 

Private aEmpresas := GetEmpSigaMat()

If ValType(cMatric) == "U"
	HttpFreeSession()
EndIf

TCF INIT cHtml

	cHtml := H_TCFH014()

TCF END cHtml	

Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW015		ЁAutor Ё Marinaldo de Jesus   Ё Data Ё17/12/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁiFrame de Informacoes Empresa no Terminal de Consultas      Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW015()	                            					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico                                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW015()

Local cFileMsg	:= "TCFEMP.MSG"

Local cHtml
Local nHandle

Private aMsgEmp	:= {}

TCF INIT cHtml

	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	ЁCarrega variavel com o Conteudo do arquivo de Informacoes   daЁ
	Ёempresa que sera mostrado no RH OnLine a Todos os FuncionariosЁ
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	nHandle := fOpen( cFileMsg )
	IF (;
			!File( cFileMsg );
			.or.;
			Empty( aMsgEmp := GetAllTxtFile( nHandle ) ); 
		)	
		aAdd( aMsgEmp , "<center><b>" + STR0035 + "</b></center>" ) //"Bem Vindo ao RH OnLine"
	EndIF
	fClose( nHandle )
	
	cHtml := H_TCFH015()

TCF END cHtml
	
Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFPonQdHr	ЁAutor Ё Marinaldo de Jesus   Ё Data Ё28/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCalendario de Marcacoes                                     Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ёw_TCFPonQdHr()												Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁTerminal de Consulta                                        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFPonQdHr()

Local cHtml

WEB EXTENDED INIT cHtml START "TCFWInSite"

	TCF INIT cHtml
        
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Opcao habilitada Configurador de Menu          					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		If TcfChkMenu()
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Define o Modulo em Uso                          					 Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			SetModulo( "SIGAPON" , "PON" )
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Inicializa as Static do SIGAPON                 					 Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			PonDestroyStatic()
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Posiciona no Funcionario                     				   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			SRA->( dbSetOrder( RetOrdem("SRA") ) )
			SRA->( MsSeek( xFilial( "SRA" , TCFWGetFil() ) + TCFWGetMat() ) )
			
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁChama o u_PonQdHr para gerar o Codigo HTML correspondente   aoЁ
			ЁQuadro de Horario do Funcionario (Baseado no Calendario de MarЁ
			Ёcacoes)													   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			IF EmptyHtml( cHtml := u_PonQdHr( .T. , TCFWGetFil() , TCFWGetMat() ) )
				//"Calend&aacute;rio de Marca&ccedil;&otilde;es"###"N&atilde;o existem informac&otilde;es de calend&aacute;rio a serem visualizadas"
				cHtml := HtmlDefault( STR0025 , STR0011 )
			EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Opcao nao habilitada Configurador de Menu        					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		Else
			//"Calend&aacute;rio de Marca&ccedil;&otilde;es"###"OpГЦo nЦo estА habilitada no menu do Terminal de Consultas"
			cHtml += HtmlDefault( STR0025 , STR0041 ) 
		EndIf	
		
	TCF END cHtml
	
WEB EXTENDED END
		
Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFExtraBh	ЁAutor Ё Marinaldo de Jesus   Ё Data Ё28/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁExtrato do Banco de Horas                                   Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ёw_TCFExtraBh()												Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁTerminal de Consulta                                        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFExtraBh()
                
Local cHtml

WEB EXTENDED INIT cHtml START "TCFWInSite"

	TCF INIT cHtml
        
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Opcao habilitada Configurador de Menu          					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		If TcfChkMenu()
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Define o Modulo em Uso                          					 Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			SetModulo( "SIGAPON" , "PON" )
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Inicializa as Static do SIGAPON                 					 Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			PonDestroyStatic()
		
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			Ё Posiciona no Funcionario                     				   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			SRA->( dbSetOrder( RetOrdem("SRA") ) )
			SRA->( MsSeek( xFilial( "SRA" , TCFWGetFil() ) + TCFWGetMat() ) )
			
			/*/
			здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			ЁChama o u_ExtraBh para gerar o Codigo HTML correspondente   aoЁ
			ЁExtrato de Banco de Horas									   Ё
			юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
			IF EmptyHtml( cHtml := u_ExtraBh( .T. , TCFWGetFil() , TCFWGetMat() ) )
				//"Banco de Horas" ###"N&atilde;o existem informac&otilde;es de Banco de Horas a serem visualizadas"
				cHtml := HtmlDefault( STR0024 , STR0010 )
			EndIF

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Opcao nao habilitada Configurador de Menu        					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		Else
			//"Banco de Horas"###"OpГЦo nЦo estА habilitada no menu do Terminal de Consultas"
			cHtml += HtmlDefault( STR0024 , STR0041 ) 
		EndIf	
		
	TCF END cHtml
	
WEB EXTENDED END
	
Return( cHtml )

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁW_TCFWRETURNЁAutor Ё Marinaldo de Jesus   Ё Data Ё04/09/2001Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁRetorna a Pagina de Entrada do Terminal de Consultas        Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   Ёw_TcfReturn()                            					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico                                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFWRETURN()

Local cHtml
//Local cDataHorSec := StrTran(dtoc(DATE()),"/","")+StrTran(time(),":","")+LTRIM(str(seconds(),5))

HttpFreeSession()

If Select("XX8") > 0
	XX8->( dbCloseArea() )
EndIf
If Select("XX9") > 0
	XX9->( dbCloseArea() )
EndIf

TCF INIT cHtml

	cHtml := W_TCFWINIT()
	/*cHtml := '<html>'																	+ CRLF
	cHtml += '<head>'																	+ CRLF
	cHtml += '<script language="JavaScript">'											+ CRLF
	cHtml +=	'window.open("w_TCFWINIT.apw?DataHorSec=<%='+cDataHorSec+'%>","blank")' + CRLF
	cHtml += '</script>'																+ CRLF
	cHtml += '</head>' 																	+ CRLF
	cHtml += '</html>' 																	+ CRLF*/

TCF END cHtml

Return( cHtml )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁTCFWInSiteЁ Autor Ё Marinaldo de Jesus    Ё Data Ё25/09/2001Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁValidacao do Acesso ao Terminal de Consulta                 Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁSIGATCF                                                     Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function TCFWInSite( cReturn , lLogout )

Local aEmpresas
Local cHtml
Local cEmp
Local cFil
Local cMatricula
Local cPassWord
Local cInconsistencia
Local cSraPassWord
Local lAllOk
Local lChkEnv
Local cVisRen	:= SuperGetMv("MV_TCFVREN", ,"N") // Define se usuАrio demitido pode acessar Portal para visualizar rendimento.

Begin Sequence
	IF ( ValType( HttpPost->cMatricula ) == "U" .AND. TCFGelAllOk() )
		Break
	EndIF

	cEmp := TCFGetPstEmp()
	IF Empty( cEmp )
		cInconsistencia := STR0039	//"Empresa inv&aacute;lida"
		Break
	EndIF

	cFil := TCFGetPstFil()
	IF Empty( cFil ) 
		cInconsistencia := STR0034	//"Filial inv&aacute;lida"
		Break
	EndIF

	IF Empty( HttpPost->cMatricula )
		cInconsistencia := STR0008	//"Matricula inv&aacute;lida"
		Break
	EndIF
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Posiciona no Funcionario                     				   Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
	SRA->( dbSetOrder( RetOrdem("SRA") ) )
	SRA->( MsSeek( xFilial( "SRA" , cFil ) + HttpPost->cMatricula ) )
	
	If ( cVisRen <> "S" )
		If SRA->RA_SITFOLH == 'D' .and. (!Empty(SRA->RA_DEMISSA) .AND. dDataBase >= SRA->RA_DEMISSA) .AND.  !( SRA->RA_RESCRAI $ '30/31' )
			cInconsistencia := STR0049 //'FuncinАrio com status de demitido'
		EndIf	
	EndIf


	aEmpresas	:= GetEmpSigaMat()
	IF (;
			Empty( aEmpresas );
			.or.;
			( aScan( aEmpresas , { |x| x[1] == HttpPost->cEmpFil } ) == 0 );
		)	
		cInconsistencia := STR0028 //"Empresa ou Filial inv&aacute;lida"
		Break
	EndIF
	
	/*/
	здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	Ё Prepara o ambiente a ser utilizado pelo usuario 					 Ё
	юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/    
	lChkEnv := fRhOnPrepEnv( aEmpresas, , , cEmp, cFil )

	If (lChkEnv)
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define o Modulo em Uso                          					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		SetModulo( "SIGAGPE" , "GPE" )
	
		If ExistBlock("TCFVERMAT") 
			ExecBlock("TCFVERMAT",.F.,.F.)
		Else
			HttpPost->cMatricula := Padr( HttpPost->cMatricula , TamSx3("RA_MAT")[1] )
		Endif
	
		SRA->( dbSetOrder( RetOrdem("SRA") ) )
		IF (;
				Empty( HttpPost->cMatricula );
				.or.;
				SRA->( !MsSeek( xFilial( "SRA" , cFil ) + HttpPost->cMatricula , .F. ) );
			)	
			cInconsistencia := STR0006 //"Usu&aacute;rio n&atilde;o autorizado"
			Break
		EndIF
	
		IF (;
				Empty( HttpPost->cPassWord );
				.and.;
				Empty( TCFWGetPas() );
			)
			cInconsistencia := STR0038	//"Senha Inv&aacute;lida"
			Break
		EndIF
	
		cPassWord 		:= APXTo64( Upper( AllTrim( HttpPost->cPassWord ) ) )
		cPassWord 		:= IF( ( cPassWord == "00" ) , "" , cPassWord )
		cSraPassWord	:= APXTo64( Embaralha( Upper( AllTrim( SRA->RA_SENHA ) ) , 1 ) )
		IF (;
				Empty( cPassWord );
				.or.;
				!( cSraPassWord == cPassWord );
			)	
			cInconsistencia := STR0038	//"Senha Inv&aacute;lida"
			Break
		EndIF    
	
		TCFWSetEmp( cEmp )
		TCFWSetFil( cFil )
		TCFWSetMat( HttpPost->cMatricula )
		TCFWSetPas( HttpPost->cPassWord )
		TCFSetAllOk( .T. )
	
	Else
		cInconsistencia := OemToAnsi(STR0047) //"Falha na abertura de Empresa/Filial! Verifique configuraГЦo EMPSELECT e/ou arquivo SIGAMAT.EMP"
	EndIf

End Sequence

TCF INIT cHtml

	DEFAULT lLogout := .T.
	IF !Empty( cInconsistencia )
		cHtml := HtmlDefault( STR0012 , cInconsistencia , cReturn , lLogout , .F. )  //"Existem inconsist&ecirc;ncias a serem verificadas:"
	ElseIF !( TCFGelAllOk() )
		cHtml := HtmlDefault( STR0012 , NIL , cReturn , lLogout , .F. )
	EndIF

TCF END cHtml

Return( cHtml )

/*/
зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁTcfStart  Ё Autor Ё Marinaldo de Jesus    Ё Data Ё25/09/2001Ё
цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁValidacao do Acesso ao Terminal de Consulta                 Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁSIGATCF                                                     Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TcfStart()

Local cHtml

IF EmptyHtml( ( cHtml := TCFWInSite() ) )
	TCF INIT cHtml
		cHtml := W_TCFW005()
	TCF END cHtml
EndIF

Return( cHtml )

/*/
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁTCFNoFunction Ё Autor ЁMarinaldo de Jesus Ё Data Ё25/03/2002Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁRetorna Codigo Padrao Caso Nao Tenha Encontrada a Funcao    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁSIGATCF                                                     Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFNoFunction()

Local cHtml		  := ""

DEFAULT HttpGet->NoFunction := ""

WEB EXTENDED INIT cHtml START "TCFWInSite"

	TCF INIT cHtml

		HttpGet->NoFunction := Space( 01 ) + HttpGet->NoFunction + Space( 01 )
		
		//"Existem inconsist&ecirc;ncias a serem verificadas:"
		//"Fun&ccedil;&atilde;o n&atilde;o encontrada no Reposit&oacute;rio"
		cHtml := HtmlDefault( STR0012 , ( STR0032 + HttpGet->NoFunction + STR0033 ) )

	TCF END cHtml
	
WEB EXTENDED END

Return( cHtml )

/*/
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁTCFWUPDPSW	  Ё Autor ЁMarinaldo de Jesus Ё Data Ё25/03/2002Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁAlteracao da Senha no Terminal de Consulta					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁSIGATCF                                                     Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFWUPDPSW()

Local cHtml			:= ""
Local cMatric		:= TCFWGetMat()

Local aEmpresas
Local aPassWordAsc

Local cFil
Local cTemp
Local cReturn
Local cInconsistencia
Local cPassWordChar
Local cSraPassWord

Local dAdmissa
Local dNascimento

Local lCpfOk
Local lPassW
Local lAdmis
Local lNasci

Local nX
Local nPos
Local nLenPassWord
Local nPassWSeq
Local cTCFSDoc		:= SuperGetMv( "MV_TCFSDOC" , .F. , "1" ) // '1' = Utiliza CPF   '2'= Utiliza RG
Local cDescSDoc		:= OemToAnsi( AllTrim( RetTitle( If( cTCFSDoc=="2", "RA_RG", "RA_CIC" ) ) ) )

WEB EXTENDED INIT cHtml START "TcfUpdStart"

	TCF INIT cHtml

		HttpPost->cPassWord			:= APXTo64( Upper( AllTrim( HttpPost->cPassWord ) ) )
		HttpPost->cNewPassWord		:= APXTo64( Upper( AllTrim( HttpPost->cNewPassWord ) ) )
		HttpPost->cConNewPassWord	:= APXTo64( Upper( AllTrim( HttpPost->cConNewPassWord ) ) )
	
		Begin Sequence
	
			IF Empty( HttpPost->cNewPassWord ) 
				cInconsistencia := STR0017  + "<br><br>" + CRLF		//"A senha n&atilde;o foi preenchida"
				Break
			EndIF
	
			IF !( HttpPost->cNewPassWord == HttpPost->cConNewPassWord )
				cInconsistencia := STR0014 + "<br><br>" + CRLF		//"Senhas inv&aacute;lidas"
				Break
			EndIF
	
			SRA->( MsSeek( xFilial( "SRA" , TCFWGetFil() ) + TCFWGetMat() ) )
			lCpfOk	:= ( Ap64ToX( APXTo64(Upper(AllTrim(HttpPost->cCPF))) ) == Upper( AllTrim(SRA->RA_CIC) ) )
			IF !( lCpfOk )
				If cPaisLoc <> "BRA"
					cInconsistencia := cDescSDoc+STR0048 + "<br><br>" + CRLF //"{Titulo do RA_CIC conforme localizacao} inv&aacute;lido"
				Else
					cInconsistencia := STR0013 + "<br><br>" + CRLF		     //"CPF inv&aacute;lido"
				EndIf
				Break
			EndIF
	
			dAdmissa	:= Ctod(Day2Str(HttpPost->cDiaAdmissao)+"/"+Month2Str(HttpPost->cMesAdmissao)+"/"+Year2Str(HttpPost->cAnoAdmissao),"DDMMYY")
			lAdmis		:= ( dAdmissa == SRA->RA_ADMISSA )
			IF !( lAdmis )
				cInconsistencia := STR0015 + "<br><br>" + CRLF	//"Data de admiss&atilde;o inv&aacute;lida"
				Break
			EndIF
	
			dNascimento	:= Ctod(Day2Str(HttpPost->cDiaNascimento)+"/"+Month2Str(HttpPost->cMesNascimento)+"/"+Year2Str(HttpPost->cAnoNascimento),"DDMMYY")
			lNasci		:= ( dNascimento == SRA->RA_NASC )
			IF !( lNasci )
				cInconsistencia := STR0016 + "<br><br>" + CRLF	//"Data de nascimento inv&aacute;lida"
				Break
			EndIF
	
			cTemp := Ap64ToX( HttpPost->cNewPassWord )
			nLenPassWord	:= Len( cTemp )
			IF ( nLenPassWord < GetSx3Cache( "RA_SENHA" , "X3_TAMANHO" ) )
				HttpPost->cNewPassWord := "__LENINVALIDO__"
			EndIF
			IF ( HttpPost->cNewPassWord <> "__LENINVALIDO__" )
				aPassWordAsc := {}
				For nX := 1 To nLenPassWord
					cPassWordChar := SubStr( cTemp , nX , 1 )
					IF (;
							Empty( StrTran(cTemp,cPassWordChar,"") );
							.or.;
							( Len( StrTran(cTemp,cPassWordChar,"") ) <= 3 );
						)	
						HttpPost->cNewPassWord := "__CHARIGUAL__"
						Exit
					EndIF
					aAdd( aPassWordAsc , Asc( cPassWordChar ) ) 
				Next nX
				IF ( HttpPost->cNewPassWord <> "__CHARIGUAL__" )
					nX := 0
					IF (;
							!Empty( aPassWordAsc );
							.and.;
							( Len( aPassWordAsc ) > 1 );
						)	
						IF ( aPassWordAsc[1] < aPassWordAsc[2] )
							aEval( aPassWordAsc , { |x,y| IF( ( y == 1 ) .or. ( ( x - nX ) <>  1 ) , nX := x , ( ++nPassWSeq , nX := x ) ) } )
						Else
							aEval( aPassWordAsc , { |x,y| IF( ( y == 1 ) .or. ( ( x - nX ) <> -1 ) , nX := x , ( ++nPassWSeq , nX := x ) ) } )
						EndIF
					EndIF
					IF ( nPassWSeq == ( Len( cTemp ) - 1 ) )
						HttpPost->cNewPassWord := "__PASSWSEQUENC__"
					ElseIF ( cTemp == AllTrim( SRA->RA_MAT ) )
						HttpPost->cNewPassWord := "__PASSWEQUALMAT__"
					EndIF
				EndIF
				cTemp := ""
			EndIF
	
			lPassW := !( HttpPost->cNewPassWord $ "__CHARIGUAL__*__LENINVALIDO__*__PASSWEQUALMAT__*__PASSWSEQUENC__" ) 
			IF !( lPassW )
				Break
			EndIF
	
			cSraPassWord := APXTo64( AllTrim( Embaralha( Upper( AllTrim( SRA->RA_SENHA ) ) , 1 ) ) )
			IF !( cSraPassWord == HttpPost->cPassWord )
				cInconsistencia := STR0007 + "<br><br>" + CRLF	//"Senha Anterior Inv&aacute;lida"
				Break
			EndIF		
	
			IF !SRA->( RecLock( "SRA" , .F. ) )
				cInconsistencia := STR0040	//"N&atilde;o foi  poss&iacutevem gravar a nova senha."
				Break
			EndIF
			SRA->RA_SENHA := Embaralha( Ap64ToX( HttpPost->cNewPassWord ) , 0 )
			SRA->( MsUnLock() )

			If ValType(cMatric) == "U"
				HttpFreeSession()
			EndIf

			cReturn := '<p align="right"><a href="javascript:window.close()"><img src="'+TcfRetDirImg()+'/confirmar.gif" border="0"></a></p>'
			cHtml	:= HtmlDefault( STR0026 , STR0027 , cReturn ) //"Alterar/Cadastrar Senha"###"Senha Alterada/Cadastrada com Sucesso:"
	
		End Sequence
	
		IF !( lPassW )
			IF ( HttpPost->cNewPassWord == "__CHARIGUAL__" )
				cInconsistencia := STR0018 + "<br><br>" + CRLF	//"N&atilde;o podem existir mais de 3 carecteres iguais na senha"
			ElseIF ( HttpPost->cNewPassWord == "__LENINVALIDO__" )
				cInconsistencia := STR0019 + "<br><br>" + CRLF	//"O n&uacute;mero de caracteres da senha &eacute; menor que 6 (seis)"
			ElseIF ( HttpPost->cNewPassWord == "__PASSWSEQUENC__" )
				cInconsistencia := STR0037 + "<br><br>" + CRLF	//"A Senha n&atilde;o pode conter Caracteres Sequenciais"
			ElseIF ( HttpPost->cNewPassWord == "__PASSWEQUALMAT__" )
				cInconsistencia := STR0036 + "<br><br>" + CRLF	//"A Senha n&atilde;o pode ser igual a Matricula"
			EndIF
		EndIF
	
		IF !Empty( cInconsistencia )
			cReturn := '<p align="right"><a href="W_TCFW014.APW"><img src="'+TcfRetDirImg()+'/confirmar.gif" border="0"></a></p>'
			cHtml	:= HtmlDefault( STR0012 , cInconsistencia , cReturn )  //"Existem inconsist&ecirc;ncias a serem verificadas:"
		EndIF

	TCF END cHtml
	
WEB EXTENDED END

Return( cHtml )

/*/
зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддбддддддбдддддддддд©
ЁFun┤┘o    ЁTcfUpdStart	  Ё Autor ЁMarinaldo de Jesus Ё Data Ё25/03/2002Ё
цддддддддддеддддддддддддддадддддддадддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤┘o ЁValidacao do Acesso ao Terminal de Consulta					Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                                                            Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁSIGATCF                                                     Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function TcfUpdStart()

Local cReturn := '<p align="right"><a href="W_TCFW014.APW"><img src="'+TcfRetDirImg()+'/confirmar.gif" border="0"></a></p>'

Return( TCFWInSite( cReturn , .F. ) )

/*/
зддддддддддбддддддддддддддбддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁInTcfwFunExec ЁAutor ЁMarinaldo de Jesus   Ё Data Ё24/08/2004Ё
цддддддддддеддддддддддддддаддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁExecutar Funcoes Dentro de TCFWFUN                           Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁInTcfwFunExec( cExecIn , aFormParam )						 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁuRet                                                 	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁGenerico 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Function InTcfwFunExec( cExecIn , aFormParam )
         
Local uRet

DEFAULT cExecIn		:= ""
DEFAULT aFormParam	:= {}

IF !Empty( cExecIn )
	cExecIn	:= BldcExecInFun( cExecIn , aFormParam )
	uRet	:= cExecIn
	CheckExecForm( @uRet )
EndIF

Return( uRet )

/*/
зддддддддддбддддддддддддддбддддддбдддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTcfChkMenu    ЁAutor ЁEmerson Grassi Rocha Ё Data Ё25/07/2005Ё
цддддддддддеддддддддддддддаддддддадддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁCheca se a opcao esta habilitada na config. do menu (RBC)    Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁTcfChkMenu()												 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ<Vide Parametros Formais>									 Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁRetorno   ЁlRet = .T. ou .F.                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁObserva┤└oЁ                                                      	     Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁUso       ЁSIGATCF 													 Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Static Function TcfChkMenu()

Local lRet 		:= .F.
	
RBC->( dbGoTop() )
While RBC->( !Eof() )
	If Upper(Alltrim(RBC->RBC_ROTINA)) $ Upper(Procname(1))
		If RBC->RBC_STATUS == "A"
			lRet := .T.
			Exit
		EndIf
	EndIf
					
	RBC->( dbskip() )
EndDo      

Return lRet

/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFW016   	ЁAutor Ё Tatiane Vieira MatiasЁ Data Ё19/01/2006Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁTela de Opcoes do Informe de Rendimentos do TCF             Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFW016()									   			    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico                                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   							Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFW016()

Local cHtml
Local nHdl				:= 0.00
Local cFile				:= "GRHINF2.FCH"
Local nTamArq			:= 0.00
Local nFor				:= 0.00
Local nX				:= 0.00
Local n					:= 0
Local cBuffer			:= ""
Local cAnoBase			:= ""
Local aDados			:= {}

Private aAnos			:= {}

TCF INIT cHtml

	//Le o arquivo com as configuracoes do Informe de Rendimento e armazena em array
	IF File(cFile)
		nHdl := Fopen(cfile,64)
		fSeek(nHdl,0,0)
		nTamArq := fSeek(nHdl,0,2)
		fSeek(nHdl,0,0)
		nFor := nTamArq / (80+FwGetTamFilial)

		For nX := 1 To nFor
			cBuffer := Space(81+FwGetTamFilial)
			fRead(nHdl,@cBuffer,81+FwGetTamFilial)
			aAdd(aDados, { Substr(cBuffer,77,2) 	,;	// Empresa
							Substr(cBuffer,80,FwGetTamFilial)	,;	// Filial
							Substr(cBuffer,1,4)     ,;  // Ano Base
							.F. 					})  // GDDELETED 
		Next nX                              	
		
		fClose(nHdl)
		aDados:= aSort(aDados,,,{|x,y| x[1]+x[2]+x[3] < y[1]+y[2]+y[3]}) //Empresa + Filial + AnoBase		
	
		For n:= 1 To len(aDados)		
			If ( aDados[n,1] == cEmpAnt .and. aDados[n,2] == tcfwgetfil() ) .or. ;
				( aDados[n,1] == cEmpAnt .and. Empty(aDados[n,2]) ) .or. ;		
				( Empty(aDados[n,1]) .and. Empty(aDados[n,2] ) )
			  
				cAnoBase := aDados[n,3]

				If Ascan(aAnos,cAnoBase) == 0
					Aadd(aAnos,cAnoBase)     
				EndIf	
  	    	
  	    	 EndIf   
  	    Next n	 

	EndIf
  
	cHtml := H_TCFH016()

TCF END cHtml


Return( cHtml )


/*/
зддддддддддбддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFWIR016  	ЁAutor Ё Tatiane Vieira MatiasЁ Data Ё19/01/2006Ё
цддддддддддеддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁInforme de Rendimentos do TCF                               Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFWIR016()											    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico                                                    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   						    Ё
цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             								Ё
юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFWIR016()

Local cHtml
Local cAno 			:= HttpPost->cAno
Local dDtVisualiza 	:= CTOD("//")
Local nHdl			:= 0.00
Local cFile			:= "GRHINF2.FCH"
Local nTamArq		:= 0.00
Local nFor			:= 0.00
Local nX			:= 0.00
Local n1			:= 0.00
Local cBuffer		:= ""
Local dDataInf		:= CTOD("//")
Local cNomeResp	 	:= ""
Local cDataHorSec	:= StrTran(dtoc(DATE()),"/","")+StrTran(time(),":","")+LTRIM(str(seconds(),5))
Local aDados		:= {}

TCF INIT cHtml

	IF File(cFile)
		nHdl := Fopen(cfile,64)
		fSeek(nHdl,0,0)
		nTamArq := fSeek(nHdl,0,2)
		fSeek(nHdl,0,0)
		nFor := nTamArq / (80+FwGetTamFilial)

		For nX := 1 To nFor
			cBuffer := Space(81+FwGetTamFilial)
			fRead(nHdl,@cBuffer,81+FwGetTamFilial)
			If Substr(cBuffer,1,4) = cAno 
				aAdd(aDados, { Substr(cBuffer,77,2)	,;     	    // Empresa
									Substr(cBuffer,80,FwGetTamFilial)	,;	// Filial
									Substr(cBuffer,1,4) 	,; // Ano Base
									CTOD(Substr(cBuffer,6,2) + "/" + Substr(cBuffer,8,2) + "/"  + Strzero(Val(Substr(cBuffer,1,4))+1,4) )		,; // Data Liberacao
								    CTOD(Substr(cBuffer,11,2) + "/" + Substr(cBuffer,13,2) + "/" + Strzero(Val(Substr(cBuffer,1,4))+1,4) ) 	,; // Data do Innforme de Rendimento
									Substr(cBuffer,16,60)	,; // Responsavel
									.F. 					}) // GDDELETED 
    		EndIf
		Next nX                              	

		aDados:= aSort(aDados,,,{|x,y| x[1]+x[2]+x[3] < y[1]+y[2]+y[3]}) //Empresa + Filial + AnoBase
		fClose(nHdl)

  		If nPos := Ascan(aDados,{|X|,X[1] == cEmpAnt .and. X[2] == tcfwgetfil() })  > 0
			nPos := Ascan(aDados,{|X|,X[1] == cEmpAnt .and. X[2] == tcfwgetfil() })
			dDtVisualiza := aDados[nPos,4]
			cNomeResp	 := aDados[nPos,6]
			dDataInf	 := aDados[nPos,5]
		 ElseIf nPos:= Ascan(aDados,{|X|, X[1] == cEmpAnt .and. Empty([2]) } )  > 0 
				nPos:= Ascan(aDados,{|X|, X[1] == cEmpAnt .and. Empty([2]) } ) 
				dDtVisualiza := aDados[nPos,4]
				cNomeResp	 := aDados[nPos,6]
				dDataInf	 := aDados[nPos,5]
		 ElseIf nPos:= Ascan(aDados,{|X|, Empty(X[1]) .and. Empty(X[2] ) })  > 0 
				nPos:= Ascan(aDados,{|X|, Empty(X[1]) .and. Empty(X[2] ) })  
				dDtVisualiza := aDados[nPos,4]
				cNomeResp	 := aDados[nPos,6]
				dDataInf	 := aDados[nPos,5]
		EndIf
	
   EndIf
    
	If  dDtVisualiza > Date() 
		//"Informe de Rendimentos" ###"N&atilde;o existem valores a serem visualizados"
		cHtml += HtmlDefault( STR0043 , STR0003 )
	Else           
		/*
		 Foi criado dois frames: o mainFrame onde sera visualizado o Informe de rendimento e 
		 o bottomFrame que sera uma versao para impressao e sera escondido.
		*/                                                                
	
		cHtml += '<html>' + CRLF
		cHtml += '<frameset rows="*,1" cols="*" framespacing="0" frameborder="NO" border="0">' + CRLF
		cHtml += '  <frame src="W_TCFWInfRend.APW?DataHorSec='+cDataHorSec+'&cFrame=1&cAno=' + cAno + '&cResp=' + cNomeResp + '&DtInf=' + DTOC(dDataInf) + '" name="mainFrame">' + CRLF
		cHtml += '  <frame src="W_TCFWInfRend.APW?DataHorSec='+cDataHorSec+'&cFrame=2&cAno=' + cAno + '&cResp=' + cNomeResp + '&DtInf=' + DTOC(dDataInf) + '" name="bottomFrame">' + CRLF
		cHtml += '</frameset>' + CRLF
		cHtml += '<noframes><body style="background-color: transparent;">' + CRLF
		cHtml += '</body></noframes>' + CRLF
		cHtml += '</html>' + CRLF
	EndIf

TCF END cHtml

Return( cHtml )

/*/
зддддддддддбдддддддддддддддбддддддбддддддддддддддддддддддбддддддбдддддддддд©
ЁFun┤└o    ЁTCFWInfRend    ЁAutor Ё Tatiane Vieira MatiasЁ Data Ё19/01/2006Ё
цддддддддддедддддддддддддддаддддддаддддддддддддддддддддддаддддддадддддддддд╢
ЁDescri┤└o ЁChamada da funcao para montar o html do Informe de Rendimento  Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁSintaxe   ЁW_TCFWShowInfRend()								 			   Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Uso      ЁGenerico                                                       Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё Retorno  Ё                                   					  		   Ё
цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
ЁParametrosЁ                             						  		   Ё
юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
Web Function TCFWInfRend()
		
Local cHtml			:= ""
Local cSvHtml		:= ""
Local nRecnoSRA	:= 0    

//Pega os parametros passados na url - Ano e qual o frame que esta montando
Local cAno			:= HttpGet->cAno

Private cFrame		:= HttpGet->cFrame
Private cNomeResp	:= HttpGet->cResp
Private dDataInf	:= HttpGet->DtInf

WEB EXTENDED INIT cHtml START "TCFWInSite"

	TCF INIT cHtml

		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Define o Modulo em Uso                          					 Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		SetModulo( "SIGAGPE" , "GPE" )
	
		/*/
		здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		Ё Posiciona no Funcionario                     				   Ё
		юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
		SRA->( dbSetOrder( RetOrdem("SRA") ) )
		IF SRA->( MsSeek( xFilial( "SRA" , TCFWGetFil() ) + TCFWGetMat() ) )
		
			nRecnoSRA := SRA->( Recno() )
		
				/*/
				здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				ЁChama o Gpem580 para gerar o Codigo HTML correspondente ao    Ё
				ЁInforme de Rendimentos  									   			Ё
				юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды/*/
				cSvHtml := cHtml
				IF EmptyHtml( cHtml += Gpem580(.T.,TCFWGetFil(),TCFWGetMat(),cAno,  ))
					//"Informe de Rendimentos" ###"N&atilde;o existem valores a serem visualizados"
					cHtml += HtmlDefault( STR0043 , STR0003 )
				EndIF

		Else
	
			//"Informe de Rendimentos" ###"N&atilde;o existem valores a serem visualizados"
			cHtml += HtmlDefault( STR0043 , STR0003 )
	
		EndIF	

	TCF END cHtml
	
WEB EXTENDED END

Return( cHtml )
Web Function TCFW000()
	Local cHtml := ""

	WEB EXTENDED INIT cHtml START "TCFWInSite"
		TCF INIT cHtml
			SetModulo("SIGAGPE" , "GPE")
			SRA->(dbSetOrder(RetOrdem("SRA")))
			SRA->(MsSeek(xFilial("SRA", TCFWGetFil()) + TCFWGetMat()))
			
			Private cNomeFun := AllTrim(SRA->RA_NOME)
			Private cPicture := RetPictFun(.T.)
			Private aMenuTCF := GetTCFMenu()
					
 			cHtml += H_TCFH000()
		TCF END cHtml	
	WEB EXTENDED END

Return( cHtml )



Static Function GetTCFMenu()
	Local aUnique	:= { "RBC_FILIAL" , "RBC_CODMNU" }
	Local cFilRBC	:= ""
	Local cRotina	:= ""
	Local cDescri	:= ""
	Local cVisRen	:= SuperGetMv("MV_TCFVREN", ,"N")
	Local lIncluir	:= .T. 
	Local aMenuTcf	:= {}	
               
	DBSelectArea("RBC")
	RBC->( MsSeek( ( cFilRBC := xFilial("RBC") ) ) )

	While RBC->( !Eof() .and. ( RBC_FILIAL == cFilRBC ) )		
		
		IF RBC->( UniqueKey( aUnique ) )
			aAdd(aMenuTcf , {})
		EndIF
				
		IF ( RBC->RBC_STATUS == "A" )			
			IF !Empty( cRotina := AllTrim(RBC->RBC_ROTINA) )
				If ( cVisRen == "S" ) .And. SRA->RA_SITFOLH == 'D'  .And.  !( SRA->RA_RESCRAI $ '30/31' )
					IF FindFunction( "w_"+cRotina )							//Web Functions w_?	ou W_?
						cRotina := "w_"+cRotina+".APW"
						If (Upper(AllTrim(cRotina)) <> "W_TCFW016.APW")
							lIncluir:= .F.
						EndIf	
					EndIf	
				Else	
					IF FindFunction( "w_"+cRotina )							//Web Functions w_?	ou W_?
						cRotina := "w_"+cRotina+".APW"					
					ElseIF FindFunction( "l_"+cRotina )						//UserHtml Functions l_? ou L_?
						cRotina := "l_"+cRotina+".APW"
					ElseIF FindFunction( "h_"+cRotina )						//Html Functions h_? ou H_?
						cRotina := "h_"+cRotina+".APW"
					ElseIF FindFunction( ALLTRIM(UPPER("B_"+cRotina)) )		//Web User Functions b_?
						cRotina := "B_"+cRotina+".APW"						
					Else													//"FunГЦo nЦo encontrada no repositСrio
						cRotina := "w_TCFNoFunction.APW?NoFunction="+cRotina
					EndIF
				EndIf		
			Else
				cRotina := "#"
			EndIF
			
			IF lIncluir
				cDescri := Capital( AllTrim( RBC->RBC_DESCRI ) )
					
				aAdd( aMenuTcf[Len( aMenuTcf ) ],	{; 
								  						cDescri ,; //Descricao do Menu
														cRotina	 ; //Programa a Ser Executado
								 					};
					   )  
			Else
				lIncluir:= .T.
			Endif
		EndIF
		
		RBC->( dbSkip() )
	End While
Return aMenuTcf
