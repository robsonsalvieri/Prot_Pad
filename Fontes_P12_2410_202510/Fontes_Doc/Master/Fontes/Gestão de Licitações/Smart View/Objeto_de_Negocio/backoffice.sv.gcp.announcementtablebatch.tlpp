#include "msobject.ch"
#include "protheus.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.gcp.announcementtablebatch.ch"

namespace totvs.protheus.backoffice.gcp.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGCP", tables="CO1,CO2,CP3,CO3,SB1", customTables="All", name="Quadro do Edital-Por Lote", country="ALL")

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} announcementtableBatchReportsBusinessObjects
Classe para criação do Objeto de Negócio Listagem de contrato de parceria
 
GCPR002 - Quadro do Edital                                  
        - Announcement Table ( announcementtable )        

@author Leandro Nishihata
@since 24/08/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  

class announcementtableBatchReportsBusinessObjects from totvs.protheus.backoffice.gcp.smartView.integratedProvider.GCPIntegratedProvider

    public method new() as object
    public method getSchema() as object

	protected method getQuery() as character
    protected method setCustomInfoToData()

endclass

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Leandro Nishihata
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method new() class announcementtableBatchReportsBusinessObjects        

    _Super:new()
    self:setDisplayName(STR0002)  // "Quadro do Edital - por lote" 
    self:setDescription(STR0003)  // "Quadro do Edital de licitações - Lote"
	self:setPergunte("GCR002")
	self:enableFilterByBranch("CO1")
  
return self
    
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class announcementtableBatchReportsBusinessObjects        

	self:aliasToSchema("CO1", {"CO1_FILIAL","CO1_CODEDT","CO1_NUMPRO","CO1_ETAPA","CO1_MODALI","CO1_AVAL"})
	self:aliasToSchema("CO2", {"CO2_CODPRO","CO2_ITEM","CO2_QUANT"})
	self:aliasToSchema("CO3", {"CO3_CODIGO","CO3_LOJA","CO3_VLUNIT","CO3_TIPO","CO3_CLASS","CO3_STATUS"})
	self:aliasToSchema("CO6", {"CO6_NOME"})
	self:aliasToSchema("SA1", {"A1_NOME"})
	self:aliasToSchema("SA2", {"A2_NOME"})
	self:aliasToSchema("SB1", {"B1_DESC"})
	self:aliasToSchema("CP3", {"CP3_LOTE"})	

	self:gcpAddProperty("DESCPROD"		,STR0006	,"string") // "Desc produto"
	self:gcpAddProperty("NOMFOR"		,STR0008	,"string") // "Forn/Cliente"
	self:gcpAddProperty("VALOR_TOTAL"	,STR0009	,"string") // "Valor Total"

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class announcementtableBatchReportsBusinessObjects

local cQuery		as character
local nX	:= 1	as numeric

    cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += " FROM " + RetSQLName("CO1") + " CO1 "

	cQuery += " INNER JOIN " + RetSQLName("CO2") + " CO2 "
	cQuery += " 	ON	" + FwJoinFilial('CO2','CO1')
	cQuery += " 	AND CO2_CODEDT		= CO1_CODEDT "
	cQuery += " 	AND CO2_NUMPRO		= CO1_NUMPRO "
	cQuery += " 	AND CO2.D_E_L_E_T_	= ? "

	cQuery += " INNER JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += " 	ON	" + FwJoinFilial('SB1','CO2')
	cQuery += " 	AND	SB1.B1_COD		= CO2.CO2_CODPRO  "
	cQuery += " 	AND SB1.D_E_L_E_T_	= ? "

	cQuery += " LEFT JOIN " + RetSQLName("CO3") + " CO3 "
	cQuery += " 	ON	" + FwJoinFilial('CO3','CO2')
	cQuery += "		AND	CO3.CO3_CODEDT	= CO2.CO2_CODEDT "
	cQuery += "		AND CO3.CO3_NUMPRO	= CO2.CO2_NUMPRO "
    cQuery += "		AND CO3.CO3_LOTE	= CO2.CO2_LOTE "
	cQuery += "		AND CO3.CO3_ITEM	= CO2.CO2_ITEM "
	cQuery += "		AND CO3.D_E_L_E_T_	= ? "

	cQuery += " LEFT JOIN "+ RetSQLName("CP3") +" CP3 "
	cQuery += " 	ON	" + FwJoinFilial('CP3','CO2')
	cQuery += " 	AND	CP3.CP3_CODEDT	= CO2.CO2_CODEDT "
	cQuery += "		AND CP3.CP3_NUMPRO	= CO2.CO2_NUMPRO "
	cQuery += "		AND CP3.CP3_LOTE	= CO2.CO2_LOTE "
	cQuery += "		AND CP3.CP3_REVISA	= CO2.CO2_REVISA "
	cQuery += "		AND CP3.D_E_L_E_T_	= ? "

	cQuery += " LEFT JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += " 	ON	" + FwJoinFilial('SA2','CO3')
	cQuery += " 	AND	SA2.A2_COD		= CO3.CO3_CODIGO "
	cQuery += " 	AND	SA2.A2_LOJA		= CO3.CO3_LOJA "
	cQuery += " 	AND SA2.D_E_L_E_T_	= ? "

	cQuery += " LEFT JOIN " + RetSQLName("SA1") + " SA1 "
	cQuery += " 	ON	" + FwJoinFilial('SA1','CO3')
	cQuery += " 	AND	SA1.A1_COD		= CO3.CO3_CODIGO "
	cQuery += " 	AND	SA1.A1_LOJA		= CO3.CO3_LOJA "
	cQuery += " 	AND SA1.D_E_L_E_T_	= ? "

	cQuery += " LEFT JOIN " + RetSQLName("CO6") + " CO6 "
	cQuery += " 	ON	" + FwJoinFilial('CO6','CO3')
	cQuery += " 	AND	CO6.CO6_CODIGO	= CO3.CO3_CODIGO "
	cQuery += " 	AND	CO6.CO6_LOJA	= CO3.CO3_LOJA "
	cQuery += " 	AND CO6.D_E_L_E_T_	= ? "

	cQuery += " WHERE "
	cQuery += "		CO1_FILIAL		IN (?) "
	cQuery += " AND CO1_CODEDT		>= ? "
	cQuery += " AND CO1_CODEDT		<= ? "
	cQuery += " AND CO1_NUMPRO		>= ? "
	cQuery += " AND CO1_NUMPRO		<= ? "
	cQuery += " AND CO1_DTABER		>= ? "
	cQuery += " AND CO1_DTABER		<= ? "
	cQuery += " AND CO1_AVAL		= ? "
	cQuery += " AND CO1.D_E_L_E_T_	= ? "
	cQuery += self:setFilterOnWhere()	

	cQuery += " ORDER BY " + SqlOrder(CO1->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "CO1")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := '2'
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

    cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method setCustomInfoToData(oFilter as object) class announcementtableBatchReportsBusinessObjects

local aSX5		:= {}	as array
local cModCli	:= ""	as character
local nX		:= 0	as numeric

	self:jData["CO1_FILIAL"]	:= alltrim((self:cAlias)->CO1_FILIAL) + " - " + alltrim(self:getInfoEmp("M0_FILIAL", (self:cAlias)->CO1_FILIAL))
	self:jData["DESCPROD"]		:= (self:cAlias)->B1_DESC
	self:jData["VALOR_TOTAL"]	:= alltrim(Transform((self:cAlias)->(CO3_VLUNIT * CO2_QUANT), "@E 9,999,999,999,999,999,999,999,999,999.99") )
	
	if !empty((self:cAlias)->CO1_MODALI)
		aSX5 := FwGetSX5("LF", alltrim((self:cAlias)->CO1_MODALI))
		for nX := 1 to len(aSX5)
			self:jData["CO1_MODALI"] := alltrim((self:cAlias)->CO1_MODALI) + ' - ' + alltrim(aSX5[nX][4])
		next nX
	endif

	self:jData["ETAPA"] := ""
	if !empty((self:cAlias)->CO1_ETAPA)
		aSX5 := FwGetSX5("LE", alltrim((self:cAlias)->CO1_ETAPA))
		for nX := 1 to len(aSX5)
			self:jData["CO1_ETAPA"] := alltrim((self:cAlias)->CO1_ETAPA) + ' - ' + alltrim(aSX5[nX][4])
		next nX
	endif

	cModCli := SuperGetMV("MV_GCPMCLI",,"LL\") 

	self:jData["NOMFOR"] := ""
	if !empty((self:cAlias)->(CO3_CODIGO))
		if (self:cAlias)->CO3_TIPO == '2'
			self:jData["NOMFOR"] := if(	(self:cAlias)->CO1_MODALI $ cModCli, alltrim((self:cAlias)->A1_NOME), alltrim((self:cAlias)->A2_NOME) )
		else
			self:jData["NOMFOR"] := alltrim((self:cAlias)->CO6_NOME)
		endif
	endif
	
	fwFreeArray(aSX5)

return
