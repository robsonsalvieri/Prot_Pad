#include "vkey.ch"
#include "rwmake.ch"
#include "protheus.ch"
#include "topconn.ch"
#include "wfprocess.ch"
#include "tbiconn.ch"
#include "tbicode.ch"
#include "fileio.ch"
//#include "poncalen.ch"
#include "loca054.ch"

#define MAXGETDAD 99999


/*/loca054.prw
itup business - totvs rental
r.a.t
@type function
@author frank zwarg fuga
@since 03/12/2020
@version p12
@history 03/12/2020, frank zwarg fuga, fonte produtizado.
/*/

FUNCTION LOCA054()
PRIVATE BFILTRABRW := {|| NIL}
PRIVATE AINDICE    := {}
PRIVATE CCONDFIL   := ""
PRIVATE ACAMPOZA0  := {}
PRIVATE ACORES     := {	{'FP5_SITUAC=="V"' ,'BR_AZUL'    },;    // VISTORIADO
						{'FP5_SITUAC=="C"' ,'BR_VERDE'   },;    // COMPLEMENTAR
						{'FP5_SITUAC=="R"' ,'BR_PRETO'   },;    // REVISADO
						{'FP5_SITUAC=="X"' ,'BR_BRANCO'  },;   	// CANCELADO
						{'FP5_SITUAC$"N/ "','BR_VERMELHO'} }  	// NÃO VISTORIADO

PRIVATE CCADASTRO := STR0001  //"MANUTENÇÃO DE R.A.T"
PRIVATE AROTINA	  := {}
// DSERLOCA-1982 - Frank em 30/04/2024
private lImplemento := .F.

aRotina := menudef(aRotina)

CCONDFIL   := " FP5_FILIAL = '" + XFILIAL("FP5") + "' "
BFILTRABRW := {||FILBROWSE("FP5",@AINDICE,@CCONDFIL)}

EVAL(BFILTRABRW)

DBSELECTAREA("FP5")
DBSETORDER(1)

MBROWSE(6,1,22,75,"FP5",,,,,,ACORES)

DBSELECTAREA("FP5")
DBCLEARFILTER()
RETURN .T.



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPROGRAMA  ³ LOCFRAT   º AUTOR ³ IT UP BUSINESS     º DATA ³ 28/06/2007 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDESCRICAO ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUSO       ³ ESPECIFICO GPO                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION LOCA05401(NOPC)

LOCAL NPOS       := 0
LOCAL NSTYLE     := 0
LOCAL ASIZE      := MSADVSIZE()
LOCAL ATEMP      := {}
LOCAL ABUTTONS   := {}
LOCAL ACPOSNAO   := {"FP5_FILIAL","FP5_PROJET","FP5_OBRA"  ,"FP5_OBS"   ,"FP5_DTVIS" ,"FP5_DADOS ","FP5_GESTOR",;
                     "FP5_RESPON","FP5_EMISSA","FP5_INLOCO","FP5_PRAZO ","FP5_SEQTRA","FP5_VRSERV","FP5_OPER  ",;
   "FP5_SITUAC","FP5_DTREAL","FP5_REVISA","FP5_COMPLE","FP5_OBSOPE","FP5_COD   ","FP5_OBSEQU",;
                     "FP5_OBSACE","FP5_OBSERV","FP5_CODCLI","FP5_LOJA"  ,"FP5_NOMCLI","FP5_PRIORI","FP5_TPPROJ","FP5_TPSERV"}
//LOCAL CALIAS , CCONDICAO , CFILTRO

PRIVATE CTPROJ   := ""
PRIVATE CTSERV   := ""
PRIVATE NTPROJ   := 1
PRIVATE NTSERV   := 1
PRIVATE OPRIORI
PRIVATE OTPROJ
PRIVATE OTSERV
PRIVATE ATJTEMP  := {}
PRIVATE ATSERV   := {}
PRIVATE ATPROJ   := {}
PRIVATE CPROJETO := FP5->FP5_PROJET
PRIVATE COBRA	 := FP5->FP5_OBRA
PRIVATE CCODRAT  := IIF(EMPTY(FP5->FP5_COD) , GETSXENUM("FP5","FP5_COD") , FP5->FP5_COD)
PRIVATE CCODREV  := FP5->FP5_REVISA
PRIVATE CCOMPLE  := IIF(NOPC==8 , IIF(EMPTY(FP5->FP5_COMPLE),STRZERO(1,3),STRZERO(VAL(FP5->FP5_COMPLE)+1,3)) , FP5->FP5_COMPLE)

PRIVATE CHORA    := TIME()
PRIVATE CGESTOR  := CRIAVAR("A3_COD")
PRIVATE CRESPON  := CRIAVAR("FP5_RESPON")
PRIVATE DDATAVIS := CTOD("  /  /  ") 							// FP5_DTVIS  - DATA DA VISTORIA
PRIVATE DPRAZO   := CTOD("  /  /  ") 							// FP5_PRAZO  - PRAZO PARA EXECUÇÃO DA VISTORIA
PRIVATE DREALIZA := CTOD("  /  /  ") 							// FP5_DTREAL - DATA DA REALIZAÇÃO DA VISTORIA
PRIVATE DEMISSAO := DDATABASE        							// EMISSAO DA VISTORIA
PRIVATE CPRIORI  := ""
PRIVATE APRIORI  := {" " , STR0010 , STR0011 , STR0012}  //"A=ALTA"###"M=MEDIA"###"B=BAIXA"
PRIVATE CINLOCO  := ""
PRIVATE AINLOCO  := {" " , STR0013  , STR0014}  //"S=SIM"###"N=NÃO"
PRIVATE AEMAIL   := {" " , STR0013  , STR0015}  //"S=SIM"###"N=NAO"
PRIVATE CEMAIL   := ""
PRIVATE CSITUA   := ""
PRIVATE ASITUA   := {" " , STR0016 , STR0017 , STR0018 , STR0019 , STR0020}  //"V=VISTORIADO"###"N=NAO VISTORIADO"###"C=COMPLEMENTAR"###"R=REVISADO"###"X=CANCELADO"
PRIVATE CDESENH  := CRIAVAR("FP5_DESEN")
PRIVATE NVLRSERV := CRIAVAR("FP5_VRSERV")
PRIVATE CCHAVE   := IIF(EMPTY(FP5->FP5_COD) , XFILIAL("FP5")+CPROJETO+COBRA+SPACE(6)+CCODREV+CCOMPLE , XFILIAL("FP5")+CPROJETO+COBRA+CCODRAT+CCODREV+CCOMPLE)

PRIVATE BCOND    := IIF(EMPTY(FP5->FP5_COD) ,;
                    {|| XFILIAL("FP5") + FP5->FP5_PROJET + FP5->FP5_OBRA  + FP5->FP5_COD + FP5->FP5_REVISA + FP5->FP5_COMPLE == XFILIAL("FP5") + CPROJETO + COBRA + SPACE(6) + CCODREV + CCOMPLE },;
                    {|| XFILIAL("FP5") + FP5->FP5_PROJET + FP5->FP5_OBRA  + FP5->FP5_COD + FP5->FP5_REVISA + FP5->FP5_COMPLE == XFILIAL("FP5") + CPROJETO + COBRA + CCODRAT  + CCODREV + CCOMPLE } )

PRIVATE NINDICE  := 1
PRIVATE LINCLUI  := IIF(NOPC==3 , .T. , .F.)
PRIVATE LALTERA	 := IIF(NOPC==4 , .T. , .F.)
PRIVATE LEDITA	 := IIF(LINCLUI .OR. LALTERA , .T. , .F.)
PRIVATE CTITULO  := "R.A.T"
PRIVATE COBSSERV := "" 	    									// FP1_ESCOPO
PRIVATE COBS     := ""     										// FP5_OBS
PRIVATE COBSOPER := ""     										// FP5_OBSOPE
PRIVATE COBSEQUIP:= ""     										// FP5_OBSEQU
PRIVATE COBSACESS:= ""     										// FP5_OBSACE
PRIVATE OBUTOBS
PRIVATE OBUTPRJ
PRIVATE AHEADER  := {}
PRIVATE ACOLS    := {}
PRIVATE ATELA[0][0] , AGETS[0]
PRIVATE OFONT1   := TFONT():NEW("ARIAL",08,14,,.T.,,,,.T.,.F.)
PRIVATE OGETZA8  := NIL
PRIVATE AGETZA8	 := {}

//AADD( ABUTTONS , {"" , {|| U_DESCSERV()} , "" , "DESC.SERVIÇO"} )

REGTOMEMORY("FP5",.T.) 											// CABECALHO MEDICAO

DO CASE
CASE NOPC == 2 .OR. NOPC == 5
	NSTYLE  := 0
CASE NOPC == 3 .OR. NOPC == 4
	NSTYLE := GD_INSERT+GD_UPDATE+GD_DELETE
CASE NOPC == 6
	NSTYLE := GD_INSERT+GD_UPDATE+GD_DELETE
CASE NOPC == 8
	NSTYLE := GD_INSERT+GD_UPDATE+GD_DELETE
ENDCASE

// --> DADOS DO CLIENTE
DBSELECTAREA("FP0")
DBSETORDER(1)
MSSEEK( XFILIAL("FP0") + CPROJETO + COBRA )

CCODCLI := FP0->FP0_CLI
CLOJA   := FP0->FP0_LOJA

DBSELECTAREA("SA1")
DBSETORDER(1)
MSSEEK(XFILIAL("SA1")  + CCODCLI + CLOJA )

CNOMECLI  := ALLTRIM(SA1->A1_NOME)
CCONTATO  := ALLTRIM(SA1->A1_CONTATO)
CTELEFONE := "("+ALLTRIM(SA1->A1_DDD)+") "+ALLTRIM(SA1->A1_TEL)
CNUMERFAX := "("+ALLTRIM(SA1->A1_DDD)+") "+ALLTRIM(SA1->A1_FAX)
CCLIENTE  := CCODCLI + "/" + CLOJA + "-" + CNOMECLI

// --> LOCAL DA OBRA.
IF FP0->FP0_TIPOSE = "T" 						// --> ALTERADO ITUP MAIO/2020   ( ERA: IF FP0->FP0_TIPOSE $ "T/L" )
	DBSELECTAREA("ZA6")
	DBSETORDER(1)
	MSSEEK( XFILIAL("ZA6") + CPROJETO + COBRA )
	CLOCAL   := ZA6->ZA6_NOMORI
	CENDER   := ZA6->ZA6_ENDORI
	CBAIRR   := ZA6->ZA6_BAIORI
	CMUNIC   := ZA6->ZA6_MUNORI
	CESTAD   := ZA6->ZA6_ESTORI
	CCONTATO := ZA6->ZA6_CONORI
	CTELE    := "("+ALLTRIM(ZA6->ZA6_DDDORI)+") "+ALLTRIM(ZA6->ZA6_TELORI)
	COBS     := ZA6->ZA6_OBSVIA
	DDATAVIS := ZA6->ZA6_DTVIS
	DPRAZO   := ZA6->ZA6_DTVIS
	COBSSERV := ZA6->ZA6_OBSVIA
ELSE
	DBSELECTAREA("FP1")
	DBSETORDER(1)
	MSSEEK( XFILIAL("FP1") + CPROJETO + COBRA )
	CLOCAL   := FP1->FP1_NOMORI
	CENDER   := FP1->FP1_ENDORI
	CBAIRR   := FP1->FP1_BAIORI
	CMUNIC   := FP1->FP1_MUNORI
	CESTAD   := FP1->FP1_ESTORI
	CCONTATO := FP1->FP1_CONORI
	CTELE    := "("+ALLTRIM(FP1->FP1_DDDORI)+") "+ALLTRIM(FP1->FP1_TELORI)
	COBS     := FP1->FP1_ESCOPO
	DDATAVIS := FP1->FP1_DTVIS
	DPRAZO   := FP1->FP1_DTVIS
	COBSSERV := FP1->FP1_ESCOPO
ENDIF

CGESTOR     := FP5->FP5_GESTOR
CNOMEGE     := SA3->(POSICIONE("SA3",1,XFILIAL("SA3") + CGESTOR , "A3_NOME" ) )
COBS        := FP5->FP5_OBS
COBSOPER    := FP5->FP5_OBSOPE
COBSEQUIP   := FP5->FP5_OBSEQU
COBSACESS   := FP5->FP5_OBSACE
CSITUA      := FP5->FP5_SITUAC
DINCLUSA	:= CTOD("//")

IF NOPC == 8								// COMPLEMENTAR
	CSITUA  := "C"
ENDIF

IF NOPC == 6								// REVISÃO
	CSITUA  := "R"
ENDIF

(LOCXCONV(1))->(DBSETORDER(2))
IF (LOCXCONV(1))->(DBSEEK("ZA0_TIPOSE")) 				// SE EXISTIR O CAMPO
	IF     FP0->FP0_TIPOSE $ "E"
		CTSERV := STR0021 		// "E=EQUIPAMENTO"	// ALLTRIM(SX3->X3_CBOX) //ARMAZENA COMBO DO CAMPO
	ELSEIF FP0->FP0_TIPOSE $ "T"
		CTSERV := STR0022  // "T=TRANSPORTE"
	ELSE
		CTSERV := STR0023 //"L=LOCAÇÃO"
	ENDIF
ELSE
	CTSERV := STR0024 //"T=TRANSPORTE;E=EQUIPAMENTO"	// ARMAZENA SEGUNDO DOCUMENTAÇÃO //CASO CONTRARIO
ENDIF

ATJTEMP := SEPARA(CTSERV,";") 				// CONSTROI ARRAY DE ACORDO COM O COMBO DO CAMPO
ATSERV  := ACLONE(ATJTEMP) 					// COPIA ARRAY PARA ARRAY STATIC
NTSERV  := IIF( ALLTRIM(FP5->FP5_TPSERV) <> "",ASCAN(ATSERV,{|X| SUBSTR(X,1,1) == ALLTRIM(FP5->FP5_TPSERV) }) , ASCAN(ATSERV,{|X| SUBSTR(X,1,1) == ALLTRIM(FP0->FP0_TIPOSE) }) )

(LOCXCONV(1))->(DBSETORDER(2))
IF (LOCXCONV(1))->(DBSEEK("ZA0_TPRAT")) 				// SE EXISTIR O CAMPO
	CTPROJ := ALLTRIM(GetSx3Cache(&(LOCXCONV(2)),"X3_CBOX")) 		// ARMAZENA COMBO DO CAMPO
ELSE                               			// CASO CONTRARIO
	CTPROJ := STR0025 // "E=ENGENHARIA;P=PROJETOS"	// ARMAZENA SEGUNDO DOCUMENTAÇÃO
ENDIF
ATJTEMP := SEPARA(CTPROJ,";")    			// CONSTROI ARRAY DE ACORDO COM O COMBO DO CAMPO
ATPROJ  := ACLONE(ATJTEMP)        			// COPIA ARRAY PARA ARRAY STATIC
NTPROJ  := IIF( EMPTY(FP5->FP5_TPPROJ) , "" , FP5->FP5_TPPROJ)
CPRIORI := FP5->FP5_PRIORI

//IF !EMPTY(FP5->FP5_COD)
	CRESPON  := FP5->FP5_RESPON
	CINLOCO  := FP5->FP5_INLOCO
	DDATAVIS := FP5->FP5_DTVIS
	DPRAZO   := FP5->FP5_PRAZO
	DREALIZA := FP5->FP5_DTREAL
	DINCLUSA := FP5->FP5_EMISSA
	NVLRSERV := FP5->FP5_VRSERV
	IF !EMPTY(FP5->FP5_OBSERV)
		COBSSERV := FP5->FP5_OBSERV
	ENDIF
//ENDIF

//SET KEY VK_F11 TO TELAOBS()
//SET KEY VK_F12 TO TELAPRJ()

CCODREV2 := IIF(NOPC==6 , IIF(EMPTY(FP5->FP5_REVISA),STRZERO(1,3),STRZERO(VAL(FP5->FP5_REVISA)+1,3)), FP5->FP5_REVISA)

DEFINE MSDIALOG OTELA FROM 0, 0 TO ASIZE[6],ASIZE[5] TITLE CTITULO OF GETWNDDEFAULT() PIXEL

	OPANEL := TPANEL():NEW(0, 0, "", OTELA,, .F., .F.,,,0,150, .F., .F. )
	OPANEL:ALIGN:= CONTROL_ALIGN_TOP

	OPANEL4 := TPANEL():NEW(0, 0, "",OTELA,, .F., .F.,,,0,120, .F., .F. )
	OPANEL4:ALIGN:= CONTROL_ALIGN_TOP

	@ 005 ,005 TO 070,ASIZE[3] OF OPANEL PIXEL

	@ 011 ,010 SAY      OEMTOANSI(STR0026)             SIZE 100,08 OF OPANEL PIXEL FONT OFONT1 // "NRO. PROJETO"
	@ 010 ,060 MSGET    OPROJETO  VAR CPROJETO    WHEN .F.     SIZE 170,08 OF OPANEL PIXEL FONT OFONT1

	@ 010, 230 BUTTON   OBUTPRJ  PROMPT "F12" ACTION TELAPRJ() SIZE 012,10 OF OPANEL PIXEL

	@ 011 ,245 SAY      OEMTOANSI(STR0027)                     SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "OBRA"
	@ 010 ,265 MSGET    OOBRA     VAR COBRA       WHEN .F.     SIZE 040,08 OF OPANEL PIXEL FONT OFONT1

	@ 011 ,310 SAY      OEMTOANSI(STR0028)                 SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "NRO. RAT"
	@ 010 ,340 MSGET    OCODRAT   VAR CCODRAT     WHEN .F.     SIZE 030,08 OF OPANEL PIXEL FONT OFONT1

	@ 011 ,375 SAY      OEMTOANSI(STR0029)                  SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "REVISÃO"
	@ 010 ,405 MSGET    OCODREV   VAR CCODREV2    WHEN .F.     SIZE 030,08 OF OPANEL PIXEL FONT OFONT1

	@ 011 ,440 SAY      OEMTOANSI(STR0030)                     SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "COMP"
	@ 010 ,465 MSGET    OCOMPLE   VAR CCOMPLE     WHEN .F.     SIZE 030,08 OF OPANEL PIXEL FONT OFONT1

	@ 025 ,010 SAY      OEMTOANSI(STR0031)                   SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "LOCAL"
	@ 025 ,060 MSGET    OLOCAL    VAR CLOCAL      WHEN .F.     SIZE 130,08 OF OPANEL PIXEL FONT OFONT1

	@ 025 ,200 SAY      OEMTOANSI(STR0032)                 SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "CLIENTE "
	@ 025 ,230 MSGET    OCLIENTE  VAR CCLIENTE    WHEN .F.     SIZE 265,08 OF OPANEL PIXEL FONT OFONT1

	@ 040 ,010 SAY      OEMTOANSI(STR0033)                SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "ENDEREÇO"
	@ 040 ,060 MSGET    OENDER    VAR CENDER      WHEN .F.     SIZE 130,08 OF OPANEL PIXEL FONT OFONT1

	@ 040 ,200 SAY      OEMTOANSI(STR0034)                  SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "BAIRRO"
	@ 040 ,230 MSGET    OBAIRR    VAR CBAIRR      WHEN .F.     SIZE 080,08 OF OPANEL PIXEL FONT OFONT1

	@ 040 ,315 SAY      OEMTOANSI(STR0035)               SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "MUNICIPIO"
	@ 040 ,350 MSGET    OMUNIC    VAR CMUNIC      WHEN .F.     SIZE 060,08 OF OPANEL PIXEL FONT OFONT1

	@ 040 ,410 SAY      OEMTOANSI(STR0036)                  SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "ESTADO"
	@ 040 ,440 MSGET    OMUNIC    VAR CESTAD      WHEN .F.     SIZE 055,08 OF OPANEL PIXEL FONT OFONT1

	@ 055 ,010 SAY      OEMTOANSI(STR0037)                 SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "CONTATO"
	@ 055 ,060 MSGET    OCONTATO  VAR CCONTATO    WHEN .F.     SIZE 130,08 OF OPANEL PIXEL FONT OFONT1

	@ 055 ,200 SAY      OEMTOANSI(STR0038)                    SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "FONE"
	@ 055 ,230 MSGET    OTELE     VAR CTELE       WHEN .F.     SIZE 080,08 OF OPANEL PIXEL FONT OFONT1

	@ 080 ,010 SAY      OEMTOANSI(STR0039)                  SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "GESTOR"
	@ 080 ,070 MSGET    OGESTOR   VAR CGESTOR     F3 "SA3"     SIZE 070,08 OF OPANEL PIXEL FONT OFONT1

	@ 080 ,150 MSGET    ONOMEGE   VAR CNOMEGE     WHEN .F.     SIZE 080,08 OF OPANEL PIXEL FONT OFONT1

	@ 080 ,245 SAY      OEMTOANSI(STR0040)             SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "RESPONSAVEL"
	@ 080 ,290 MSGET    ORESPON   VAR CRESPON                  SIZE 070,08 OF OPANEL PIXEL FONT OFONT1

	@ 080 ,365 SAY      OEMTOANSI(STR0041)                 SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "IN LOCO"
	@ 080 ,395 COMBOBOX OINLOCO   VAR CINLOCO    ITEMS AINLOCO SIZE 050,08 OF OPANEL PIXEL FONT OFONT1

	@ 095 ,010 SAY      OEMTOANSI(STR0042)     SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "DATA PROG. VISTORIA"
	@ 095 ,070 MSGET    ODATAVIS  VAR DDATAVIS                 SIZE 070,08 OF OPANEL PIXEL FONT OFONT1

	@ 095 ,150 SAY      OEMTOANSI(STR0043)                SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "SITUAÇÃO"
	@ 095 ,200 COMBOBOX OSITUA    VAR CSITUA     ITEMS ASITUA WHEN (NOPC != 8 .AND. NOPC != 6) SIZE 050,008 OF OPANEL PIXEL FONT OFONT1

	@ 095 ,270 SAY      OEMTOANSI(STR0044)                         SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "ENVIA E-MAIL"
	@ 095 ,320 COMBOBOX OEMAIL    VAR CEMAIL     ITEMS AEMAIL               SIZE 050,08 OF OPANEL PIXEL FONT OFONT1

	@ 110 ,010 SAY      OEMTOANSI(STR0045 )                   SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "PRAZO DA VISTORIA"
	@ 110 ,070 MSGET    OPRAZO    VAR DPRAZO                                SIZE 070,08 OF OPANEL PIXEL FONT OFONT1

	@ 110 ,150 SAY      OEMTOANSI(STR0046)                                 SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "HORA"
	@ 110 ,200 MSGET    OHORA     VAR CHORA                                 SIZE 040,08 OF OPANEL PIXEL FONT OFONT1

	@ 110 ,260 SAY      OEMTOANSI(STR0047)                           SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "PRIORIDADE"
	@ 110 ,310 COMBOBOX OPRIORI   VAR CPRIORI    ITEMS APRIORI              SIZE 070,08 OF OPANEL PIXEL FONT OFONT1

	@ 110 ,380 SAY      OEMTOANSI(STR0048)                           SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "TP PROJETO"
	@ 110 ,430 COMBOBOX OTPROJ    VAR NTPROJ     ITEMS ATPROJ               SIZE 050,08 OF OPANEL PIXEL FONT OFONT1

	@ 125 ,010 SAY      OEMTOANSI(STR0049)                   SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "DT REALI. VISTORIA"
	@ 125 ,070 MSGET    OREALIZA  VAR DREALIZA                              SIZE 070,08 OF OPANEL PIXEL FONT OFONT1

	@ 125 ,150 SAY      OEMTOANSI(STR0050)                                SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "VALOR"
	@ 125 ,200 MSGET    OVLRSERV  VAR NVLRSERV PICTURE("@E 999,999,999.99") SIZE 070,08 OF OPANEL PIXEL FONT OFONT1

	@ 125 ,280 SAY      OEMTOANSI(STR0051)                           SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "TP SERVIÇO"
	@ 125 ,330 COMBOBOX OTSERV    VAR CTSERV     ITEMS ATSERV               SIZE 050,08 OF OPANEL PIXEL FONT OFONT1

	@ 140 ,010 SAY      OEMTOANSI(STR0052)                      SIZE 070,08 OF OPANEL PIXEL FONT OFONT1 // "DT INCLUSÃO RAT"
	@ 140 ,070 MSGET    OINCLUSA  VAR DINCLUSA    WHEN .F.                  SIZE 070,08 OF OPANEL PIXEL FONT OFONT1
	@ 140, 200 BUTTON   OBUTOBS PROMPT STR0053 ACTION TELAOBS() SIZE 070,10 OF OPANEL PIXEL  // "OBSERVAÇÕES <F11>"

	// --> MONTANDO AHEADER PARA GETDADOS
	ATEMP   := LOCA036("FP5",ACPOSNAO)
	AHEADER := ACLONE(ATEMP[1])
	// --> MONTANDO ACOLS  PARA GETDADOS
	ATEMP   := LOCA03601("FP5",AHEADER,4,NINDICE,CCHAVE, BCOND )
	aCols	:= {}
	ACOLS   := ACLONE(ATEMP[1])

	IF EMPTY(ACOLS)
		ACOLS0:={}
		FOR NPOS:=1 TO LEN(AHEADER)
			FP5->(AADD(ACOLS0,CRIAVAR(AHEADER[NPOS,2])))
		NEXT NPOS
		AADD(ACOLS0,.F.  )  				// DELETED
		AADD(ACOLS,ACOLS0)
	ENDIF

	ACOLS[1][1] := "001"

	OGETZA8:=MSNEWGETDADOS():NEW(10, 10, 100, 100, NSTYLE , "ALLWAYSTRUE" , "ALLWAYSTRUE" , "+FP5_ITEM" ,,,,, MAXGETDAD ,, OPANEL4 , AHEADER , ACOLS)
	OGETZA8:OBROWSE:ALIGN := CONTROL_ALIGN_ALLCLIENT

	IF EXISTBLOCK("LC060BUT") 				// PONTO DE ENTRADA PARA ALTERAÇÃO DE BOTÕES NO AÇÕES RELACIONADAS
		ABUTTONS := EXECBLOCK("LC060BUT",.T.,.T.,{ABUTTONS})
	ENDIF

ACTIVATE MSDIALOG OTELA CENTERED ON INIT (ENCHOICEBAR(OTELA ,{||IF(NOPC<>2,FSALVAR1(OTELA, NOPC),.T.),OTELA:END()},{||ROLLBACKSX8(),OTELA:END()},,@ABUTTONS))
//ACTIVATE MSDIALOG ODLG ON INIT (ENCHOICEBAR(ODLG,{||LOK:=.T.,ODLG:END()},{||ODLG:END()},,@ABUTTONS))
SET KEY VK_F11  TO

FP5->(DBSETFILTER({|| FP5_ITEM == "001"} , " FP5_ITEM == '001' "))

RETURN NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPROGRAMA  ³ LEGRAT    º AUTOR ³ IT UP BUSINESS     º DATA ³ 28/06/2007 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDESCRICAO ³ LEGENDA                                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUSO       ³  ESPECIFICO GPO                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION LOCA05402()
Local lRet := .T.

	BRWLEGENDA(CCADASTRO,"LEGENDA",{{"BR_AZUL" 	   , STR0054},; //"VISTORIADO"
									{"BR_VERDE"    , STR0055},; //"COMPLEMENTAR"
									{"BR_VERMELHO" , STR0056},; // "NAO VISTORIADO"
									{"BR_BRANCO "  , STR0057},; //"CANCELADO"
									{"BR_PRETO"    , STR0058}}) //"REVISADO"
RETURN lRet



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPROGRAMA  ³ FSALVAR1  º AUTOR ³ IT UP BUSINESS     º DATA ³ 28/06/2007 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDESCRICAO ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUSO       ³ ESPECIFICO GPO                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC FUNCTION FSALVAR1(OTELA, NOPC)

LOCAL NPOS      := 0
LOCAL AVISTORIA := {}
//LOCAL XEMAIL  := CEMAIL

//BEGIN TRANSACTION

	DBSELECTAREA("FP5")
	DBSETORDER(2)

	FOR NPOS := 1 TO LEN(OGETZA8:ACOLS)
		IF !OGETZA8:ACOLS[NPOS,LEN(OGETZA8:AHEADER)+1]
			IF MSSEEK(XFILIAL("FP5") + CPROJETO +COBRA +  CCODREV2 + CCOMPLE + GDFIELDGET("FP5_ITEM",NPOS,,OGETZA8:AHEADER,OGETZA8:ACOLS))
				RECLOCK("FP5",.F.)
			ELSE
				RECLOCK("FP5",.T.)
			ENDIF
			FP5->FP5_FILIAL := XFILIAL("FP5")
			FP5->FP5_COD    := CCODRAT
			FP5->FP5_REVISA := CCODREV2
			FP5->FP5_COMPLE := CCOMPLE
			FP5->FP5_PROJET := CPROJETO
			FP5->FP5_OBRA   := COBRA
			FP5->FP5_OBSERV := COBSSERV
			FP5->FP5_OBS    := COBS
			FP5->FP5_OBSOPE := COBSOPER
			FP5->FP5_OBSEQU := COBSEQUIP
			FP5->FP5_OBSACE := COBSACESS
			FP5->FP5_DTVIS  := DDATAVIS
			FP5->FP5_GESTOR := CGESTOR
			FP5->FP5_RESPON := CRESPON
			FP5->FP5_EMISSA := IIF(EMPTY(FP5->FP5_EMISSA),DDATABASE,FP5->FP5_EMISSA)
			FP5->FP5_INLOCO := CINLOCO
			FP5->FP5_PRAZO  := DPRAZO
			FP5->FP5_VRSERV := NVLRSERV
			FP5->FP5_SITUAC := CSITUA
			FP5->FP5_DTREAL := DREALIZA

			// TRATAMENTO CODIGO BEM
			FP5->FP5_CODST9 := OGETZA8:ACOLS[NPOS][ASCAN(OGETZA8:AHEADER,{|X|ALLTRIM(X[2])=="FP5_CODST9"})]
			FP5->FP5_ITEM   := OGETZA8:ACOLS[NPOS][ASCAN(OGETZA8:AHEADER,{|X|ALLTRIM(X[2])=="FP5_ITEM"  })]
			FP5->FP5_DESC   := OGETZA8:ACOLS[NPOS][ASCAN(OGETZA8:AHEADER,{|X|ALLTRIM(X[2])=="FP5_DESC"  })]
			FP5->FP5_COMP   := OGETZA8:ACOLS[NPOS][ASCAN(OGETZA8:AHEADER,{|X|ALLTRIM(X[2])=="FP5_COMP"  })]
			FP5->FP5_LARG   := OGETZA8:ACOLS[NPOS][ASCAN(OGETZA8:AHEADER,{|X|ALLTRIM(X[2])=="FP5_LARG"  })]
			FP5->FP5_ALTU   := OGETZA8:ACOLS[NPOS][ASCAN(OGETZA8:AHEADER,{|X|ALLTRIM(X[2])=="FP5_ALTU"  })]
			FP5->FP5_DIAM   := OGETZA8:ACOLS[NPOS][ASCAN(OGETZA8:AHEADER,{|X|ALLTRIM(X[2])=="FP5_DIAM"  })]
			FP5->FP5_PESO   := OGETZA8:ACOLS[NPOS][ASCAN(OGETZA8:AHEADER,{|X|ALLTRIM(X[2])=="FP5_PESO"  })]
			FP5->FP5_DESEN  := OGETZA8:ACOLS[NPOS][ASCAN(OGETZA8:AHEADER,{|X|ALLTRIM(X[2])=="FP5_DESEN" })]
			FP5->FP5_DIAMCA := OGETZA8:ACOLS[NPOS][ASCAN(OGETZA8:AHEADER,{|X|ALLTRIM(X[2])=="FP5_DIAMCA"})]
			FP5->FP5_QUANTC := OGETZA8:ACOLS[NPOS][ASCAN(OGETZA8:AHEADER,{|X|ALLTRIM(X[2])=="FP5_QUANTC"})]

			FP5->FP5_CODCLI := CCODCLI
			FP5->FP5_LOJA   := CLOJA
			FP5->FP5_NOMCLI := CNOMECLI
			FP5->FP5_PRIORI := CPRIORI

			// TRATAMENTO DO RETORNO DO VALOR DO VETOR
			FP5->FP5_TPPROJ := NTPROJ

			MSUNLOCK()
		ELSE
			IF MSSEEK(XFILIAL("FP5") + CPROJETO +COBRA + CCODREV + CCOMPLE + ;
				OGETZA8:ACOLS[NPOS][ASCAN(OGETZA8:AHEADER,{|X|ALLTRIM(X[2])=="FP5_ITEM"})] )

				RECLOCK("FP5",.F.)
				FP5->(DBDELETE())
				MSUNLOCK()
			ENDIF

		ENDIF

		IF FP0->FP0_TIPOSE $ "T"
			ZA7->(DBCLEARFILTER())
			ZA7->(DBSETFILTER({|| ALLTRIM(FP5->FP5_PROJET) == ALLTRIM(ZA7->ZA7_PROJET) .AND. ALLTRIM(FP5->FP5_ITEM) $ ALLTRIM(ZA7->ZA7_SEQCAR)},"ALLTRIM(FP5->FP5_PROJET) == ALLTRIM(ZA7->ZA7_PROJET) .AND. ALLTRIM(FP5->FP5_ITEM) $ ALLTRIM(ZA7->ZA7_SEQCAR)"))
			ZA7->(DBGOTOP())

			IF !EMPTY(ZA7->ZA7_PROJET)
				RECLOCK("ZA7",.F.)
				ZA7_CARGA  := FP5->FP5_DESC
				ZA7_COMP   := FP5->FP5_COMP
				ZA7_LARG   := FP5->FP5_LARG
				ZA7_ALTU   := FP5->FP5_ALTU
				ZA7_QTD    := FP5->FP5_QUANTC
				ZA7_PESO   := FP5->FP5_PESO
				MSUNLOCK()
			ELSE
				RECLOCK("ZA7",.T.)
				ZA7_FILIAL := XFILIAL("ZA7")
				ZA7_PROJET := FP5->FP5_PROJET
				ZA7_OBRA   := FP5->FP5_OBRA
				ZA7_CARGA  := FP5->FP5_DESC
				ZA7_COMP   := FP5->FP5_COMP
		   		ZA7_LARG   := FP5->FP5_LARG
				ZA7_ALTU   := FP5->FP5_ALTU
				ZA7_QTD    := FP5->FP5_QUANTC
				ZA7_PESO   := FP5->FP5_PESO
				ZA7_SEQCAR := FP5->FP5_ITEM
				ZA7_SEQTRA := "001"
				MSUNLOCK()
			ENDIF
			ZA7->(DBCLEARFILTER())
		ENDIF
	NEXT NPOS

	DBSETORDER(1)

//END TRANSACTION

CONFIRMSX8()

IF CSITUA == "V"
	DBSELECTAREA("FP1")
	DBSETORDER(1)
	MSSEEK(XFILIAL("FP1") + FP5->FP5_PROJET + FP5->FP5_OBRA)
	AADD(AVISTORIA , FP1->(RECNO()))
 //	IF !(XEMAIL $ "N/ ")
 //		LOCA063(AVISTORIA,2,"")
 //	ENDIF
ENDIF

RETURN NIL



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPROGRAMA  ³ TELAOBS   º AUTOR ³ MICROSIGA          º DATA ³ 28/06/2007 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDESCRICAO ³ TELA DE DIGITAÇÃO DE OBSERVAÇÕES                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUSO       ³ ESPECIFICO GPO                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC FUNCTION TELAOBS()

LOCAL ODLG := NIL

DEFINE MSDIALOG ODLG FROM 050, 050 TO 600,830 TITLE STR0059   OF OMAINWND PIXEL STYLE DS_MODALFRAME STATUS //"TELA DE OBSERVACOES"
	@ 008 ,010 SAY OEMTOANSI("DESCRIÇÃO DO SERVIÇO")                        OF ODLG PIXEL //"DESCRIÇÃO DO SERVIÇO"
	@ 014 ,010 GET OOBSSERV  VAR COBSSERV                  SIZE 180,70      OF ODLG PIXEL MEMO

	@ 008, 260 SAY OEMTOANSI(STR0060)          OF ODLG PIXEL //"OBSERVAÇÕES (IMPRESSO NA PROPOSTA)"
	@ 014, 200 GET OOBS      VAR COBS                      SIZE 180,70      OF ODLG PIXEL MEMO

	@ 100, 010 SAY OEMTOANSI(STR0061)                       OF ODLG PIXEL //"DESCRIÇÃO DA OPERAÇÃO"
	@ 106, 010 GET OOBSOPER  VAR COBSOPER                  SIZE 180,70      OF ODLG PIXEL MEMO

	@ 100, 260 SAY OEMTOANSI(STR0062)                    OF ODLG PIXEL //"EQUIPAMENTOS NECESSARIOS"
	@ 106, 200 GET OOBSEQUIP VAR COBSEQUIP                 SIZE 180,70      OF ODLG PIXEL MEMO

	@ 180, 010 SAY OEMTOANSI(STR0063)                      OF ODLG PIXEL //"ACESSORIOS DE IÇAMENTO"
	@ 186, 010 GET OOBSACESS VAR COBSACESS                 SIZE 180,70      OF ODLG PIXEL MEMO

	DEFINE SBUTTON FROM 186,200 TYPE 1 ACTION (ODLG:END()) ENABLE OF ODLG
ACTIVATE DIALOG ODLG CENTERED

RETURN NIL



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPROGRAMA  ³ TELAPRJ   º AUTOR ³ IT UP BUSINESS     º DATA ³ 03/05/2008 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDESCRICAO ³ ROTINA PARA VISUALIZAÇÃO DE PROJETO DA TABELA ZA0          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUSO       ³ ESPECIFICO GPO                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC FUNCTION TELAPRJ()

LOCAL AAREAP := GETAREA()
LOCAL ZA0REC

DBSELECTAREA("FP0")
FP0->(DBSETORDER(1)) 						// ZA0_FILIAL+ZA0_PROJET
IF FP0->(DBSEEK(XFILIAL("FP0")+CPROJETO))
	ZA0REC := RECNO()
	AXVISUAL("FP0",ZA0REC,2)
ELSE
	MSGALERT(STR0064+ALLTRIM(CPROJETO)+STR0065+XFILIAL("FP0")+"]. VERIFIQUE ! " , STR0067) //"Projeto ["###"] não encontrado na filial ["###"]. Verifique ! "###"Rental"
ENDIF

RESTAREA(AAREAP)

RETURN NIL



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPROGRAMA  ³ LOCA060A º AUTOR ³ IT UP BUSINESS      º DATA ³ 03/04/2011 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDESCRICAO ³ ROTINA  PARA  FORNECER  A  LISTA DE OPCOES  (X3_CBOX)  DOS º±±
±±º          ³ CAMPOS FP5_TPSERV E FP5_TPPROJ                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUSO       ³ ESPECIFICO GPO                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION LOCA05403(COPL)

LOCAL CRET := ""

IF COPL == "1"
	(LOCXCONV(1))->(DBSETORDER(2))
	IF (LOCXCONV(1))->(DBSEEK("ZA0_TPRAT"))       				// SE EXISTIR O CAMPO
		CRET := ALLTRIM(GetSx3Cache(&(LOCXCONV(2)),"X3_CBOX")) 					// ARMAZENA COMBO DO CAMPO
	ELSE                      							// CASO CONTRARIO
		CRET := STR0068 //"E=ENGENHARIA;P=PROJETOS"				// ARMAZENA SEGUNDO DOCUMENTAÇÃO
	ENDIF
ENDIF

IF COPL == "2"
	(LOCXCONV(1))->(DBSETORDER(2))
	IF (LOCXCONV(1))->(DBSEEK("ZA0_TIPOSE")) 						// SE EXISTIR O CAMPO
		CRET := ALLTRIM(GetSx3Cache(&(LOCXCONV(2)),"X3_CBOX")) 					// ARMAZENA COMBO DO CAMPO
	ELSE            									// CASO CONTRARIO
		CRET := STR0069 //"L=LOCAÇÃO;T=TRANSPORTE;E=EQUIPAMENTO" 	// ARMAZENA SEGUNDO DOCUMENTAÇÃO
	ENDIF
ENDIF

RETURN CRET



// ======================================================================= \\
FUNCTION LOCA05404()
// ======================================================================= \\
// --> CHAMADA: MENU - "MANUTENÇÃO CONTRATO"
LOCAL   AAREA     := GETAREA()

PRIVATE ACAMPOZA0 := {}
PRIVATE LCOPYPRJ  := .F.
PRIVATE ADELVIAG  := {}
PRIVATE OBJT
PRIVATE OTGET2
PRIVATE MV_LOCX020

FP0->( DBSETORDER(1) )
IF FP0->( DBSEEK( XFILIAL("FP0") + FP5->FP5_PROJET ) )
	LOCA00105(4)
ENDIF

RESTAREA(AAREA)

RETURN NIL


/*
// ======================================================================= \\
STATIC FUNCTION GETSTRZAN(CTIPORO , CTIPOPI , CCAMPO , CVAZIO)
// ======================================================================= \\
// --> CHAMADA: NÃO EXISTE NESTE FONTE - DESCONTINUADA
LOCAL AAREA		:= GETAREA()
LOCAL CQUERY	:= ""
LOCAL LSEGUE	:= .F.
LOCAL CRET		:= "N"

LOCAL NCOMPZAN 	:= 0
LOCAL NLARGZAN 	:= 0
LOCAL NALTUZAN  := 0
LOCAL NPESOZAN  := 0

IF CVAZIO == "V" 						// --> VAZIO
	// CONJ. TRANSP
	NCOMPZAN  := N2COMP
	NLARGZAN  := N2LARG
	NALTUZAN  := N2ALTU
 	NPESOZAN  := N2PESO
ELSEIF CVAZIO == "C" 					// --> CARREGADO
	// DIMENS. TOT
	NCOMPZAN  := N3COMP
	NLARGZAN  := N3LARG
	NALTUZAN  := N3ALTU
 	NPESOZAN  := N3PESO
ENDIF

CQUERY := " SELECT ZAN.* "
CQUERY += " FROM "+RETSQLNAME("ZAN")+" ZAN "
CQUERY += " WHERE  ZAN.D_E_L_E_T_='' "
CQUERY += "   AND  ZAN.ZAN_FILIAL = '"+XFILIAL("ZAN")+"' "
CQUERY += "   AND  ZAN.ZAN_ALEMOA = 'N' AND ZAN.ZAN_BLOQSR = 'N' "
CQUERY += "   AND  ZAN.ZAN_TPPIST IN ('"+CTIPOPI+"','A') "
CQUERY += "   AND  ZAN.ZAN_TPROD = '"+CTIPORO+"' "
CQUERY += " ORDER BY ZAN.ZAN_NIVEL DESC "
CQUERY := CHANGEQUERY(CQUERY)
IF SELECT("TRB01") > 0
	TRB01->(DBCLOSEAREA())
ENDIF
TCQUERY CQUERY NEW ALIAS "TRB01"

NCONT := 0

DBSELECTAREA("TRB01")
TRB01->(DBGOTOP())
TRB01->(DBEVAL({|| NCONT++},,{|| !EOF()}))
TRB01->(DBGOTOP())

IF NCONT > 0
	WHILE TRB01->(!EOF())
		LSEGUE := .F.
		IF  (NCOMPZAN >= TRB01->ZAN_COMPDE .AND. NCOMPZAN <= TRB01->ZAN_COMPAT) .OR. ;
			(NLARGZAN >= TRB01->ZAN_LARGDE .AND. NLARGZAN <= TRB01->ZAN_LARGAT) .OR. ;
			(NALTUZAN >= TRB01->ZAN_ALTUDE .AND. NALTUZAN <= TRB01->ZAN_ALTUAT) .OR. ;
			(NPESOZAN >= TRB01->ZAN_PESODE .AND. NPESOZAN <= TRB01->ZAN_PESOAT)
			LSEGUE := .T.
		ENDIF
		IF !LSEGUE
			TRB01->(DBSKIP())
			LOOP
		ENDIF
		IF     CCAMPO == "FPD_CET"
			IF TRB01->ZAN_UTLCET == "S"
				CRET := "S"
			ENDIF
		ELSEIF CCAMPO == "FPD_DER"
			IF TRB01->ZAN_UTLDER == "S"
				CRET := "S"
			ENDIF
		ELSEIF CCAMPO == "FPD_DNIT"
			IF TRB01->ZAN_UTLDNT == "S"
				CRET := "S"
			ENDIF
		ENDIF
		EXIT
       	TRB01->(DBSKIP())
	ENDDO
ENDIF

IF SELECT("TRB01") > 0
	TRB01->(DBCLOSEAREA())
ENDIF

RESTAREA(AAREA)

RETURN(CRET)
*/


/*
// ======================================================================= \\
STATIC FUNCTION GETINTZAN(CTIPORO , CTIPOPI , CCAMPO , NDISTAN , CVAZIO)
// ======================================================================= \\
// --> CHAMADA: NÃO EXISTE NESTE FONTE - DESCONTINUADA
LOCAL AAREA		:= GETAREA()
LOCAL CQUERY	:= ""
LOCAL LSEGUE	:= .F.
LOCAL NRET		:= 0

LOCAL NCOMPZAN 	:= 0
LOCAL NLARGZAN 	:= 0
LOCAL NALTUZAN  := 0
LOCAL NPESOZAN  := 0

IF     CVAZIO == "V" 					// --> VAZIO
	// --> CONJ. TRANSP
	NCOMPZAN  := N2COMP
	NLARGZAN  := N2LARG
	NALTUZAN  := N2ALTU
 	NPESOZAN  := N2PESO
ELSEIF CVAZIO == "C" 					// --> CARREGADO
	// --> DIMENS. TOT
	NCOMPZAN  := N3COMP
	NLARGZAN  := N3LARG
	NALTUZAN  := N3ALTU
 	NPESOZAN  := N3PESO
ENDIF

CQUERY := " SELECT ZAN.* "
CQUERY += " FROM "+RETSQLNAME("ZAN")+" ZAN "
CQUERY += " WHERE  ZAN.D_E_L_E_T_='' "
CQUERY +=   " AND  ZAN.ZAN_FILIAL = '"+XFILIAL("ZAN")+"' "
CQUERY +=   " AND  ZAN.ZAN_ALEMOA = 'N' AND ZAN.ZAN_BLOQSR = 'N' "
CQUERY +=   " AND  ZAN.ZAN_TPPIST IN ('"+CTIPOPI+"','A') "
CQUERY +=   " AND  ZAN.ZAN_TPROD = ('"+CTIPORO+"') "
CQUERY += " ORDER BY ZAN.ZAN_NIVEL DESC "
CQUERY := CHANGEQUERY(CQUERY)
IF SELECT("TRB01") > 0
	TRB01->(DBCLOSEAREA())
ENDIF
TCQUERY CQUERY NEW ALIAS "TRB01"

NCONT := 0

DBSELECTAREA("TRB01")
TRB01->(DBGOTOP())
TRB01->(DBEVAL({|| NCONT++},,{|| !EOF()}))
TRB01->(DBGOTOP())

IF NCONT > 0
	WHILE TRB01->(!EOF())
		LSEGUE := .F.
		IF  (NCOMPZAN >= TRB01->ZAN_COMPDE .AND. NCOMPZAN <= TRB01->ZAN_COMPAT) .OR. ;
			(NLARGZAN >= TRB01->ZAN_LARGDE .AND. NLARGZAN <= TRB01->ZAN_LARGAT) .OR. ;
			(NALTUZAN >= TRB01->ZAN_ALTUDE .AND. NALTUZAN <= TRB01->ZAN_ALTUAT) .OR. ;
			(NPESOZAN >= TRB01->ZAN_PESODE .AND. NPESOZAN <= TRB01->ZAN_PESOAT)
			LSEGUE := .T.
		ENDIF
		IF !LSEGUE
			TRB01->(DBSKIP())
			LOOP
		ENDIF
		IF CCAMPO == "FPD_CRED" .AND. CVAZIO == "C"
			NRET := TRB01->ZAN_QTCRED
		ELSEIF CCAMPO == "FPD_TEMPO"
			NRET := NDISTAN/TRB01->ZAN_KMDIA
		ENDIF
		EXIT
       	TRB01->(DBSKIP())
	ENDDO
ENDIF

IF SELECT("TRB01") > 0
	TRB01->(DBCLOSEAREA())
ENDIF

RESTAREA(AAREA)

RETURN(NRET)
*/


/*
// ======================================================================= \\
STATIC FUNCTION VALDTFROTA(CFROTA , DDTINI , DDTFIM , COBRA , CSEQ)
// ======================================================================= \\
// --> CHAMADA: NÃO EXISTE NESTE FONTE - DESCONTINUADA
LOCAL CRET     := ""
LOCAL AAREA    := GETAREA()
LOCAL AAREAZLG := FPO->(GETAREA())
LOCAL CQUERY   := ""
LOCAL CEOL     := CHR(13)+CHR(10)

CQUERY := " SELECT ZLG.* "
CQUERY += " FROM "+RETSQLNAME("FPO")+" ZLG "
CQUERY += " WHERE  ZLG.D_E_L_E_T_='' "
CQUERY += "   AND  ZLG.FPO_FILIAL = '"+XFILIAL("FPO")+"' "
CQUERY += "   AND (ZLG.FPO_DTINI BETWEEN '"+DTOS(DDTINI)+"' AND '"+DTOS(DDTFIM)+"' "
CQUERY += "    OR  ZLG.FPO_DTFIM BETWEEN '"+DTOS(DDTINI)+"' AND '"+DTOS(DDTFIM)+"') "
CQUERY := CHANGEQUERY(CQUERY)
IF SELECT("TR02") > 0
	TR02->(DBCLOSEAREA())
ENDIF
TCQUERY CQUERY NEW ALIAS TR02
TCSETFIELD("TR02","FPO_DTINI","D",08,00)
TCSETFIELD("TR02","FPO_DTFIM","D",08,00)

WHILE !TR02->(EOF())
    IF TR02->FPO_STATUS == "1" .OR. TR02->FPO_STATUS == "E" .OR. TR02->FPO_STATUS == "S" .OR. TR02->FPO_STATUS == "A"
		TR02->(DBSKIP())
		LOOP
	ENDIF
	IF !EMPTY(TR02->FPO_CODBEM)
		TR02->(DBSKIP())
		LOOP
	ENDIF
	CRET += ALLTRIM(TR02->FPO_PROJET)+"-"+COBRA+"-"+CSEQ+" ("+ALLTRIM(CFROTA)+") - "+DTOC(TR02->FPO_DTINI)+" / "+DTOC(TR02->FPO_DTFIM)+CEOL
	TR02->(DBSKIP())
ENDDO

IF SELECT("TR02") > 0
	TR02->(DBCLOSEAREA())
ENDIF

RESTAREA(AAREAZLG)
RESTAREA(AAREA)

RETURN(CRET)
*/

// Montagem do arotina
// Frank Fuga em 21/11/23
Static Function menudef(aRotina)
	aRotina := {}
	AADD(AROTINA , {STR0002   , "AXPESQUI"     , 0 , 1}) //"PESQUISAR"
	AADD(AROTINA , {STR0003   , "LOCA05401(2)" , 0 , 2}) //"VISUALIZAR"
	AADD(AROTINA , {STR0004	  , "LOCA05401(4)" , 0 , 4}) //"MANUTENÇÃO R.A.T"
	AADD(AROTINA , {STR0005   , "LOCA05401(6)" , 0 , 4}) //"REVISÃO R.A.T"
	AADD(AROTINA , {STR0006	  , "LOCA05401(8)" , 0 , 4}) //"R.A.T COMPLEMENTAR"
	AADD(AROTINA , {STR0007   , "LOCR019()"	   , 0 , 5}) //"RELATORIO R.A.T"
	AADD(AROTINA , {STR0008   , "LOCA05404"    , 0 , 5}) //"MANUTENÇÃO CONTRATO"
	AADD(AROTINA , {STR0009   , "LOCA05402()"  , 0 , 5}) //"LEGENDA"
Return aRotina
