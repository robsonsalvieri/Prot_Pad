#INCLUDE "loca033.ch" 
/*/{PROTHEUS.DOC} LOCA033.PRW
ITUP BUSINESS - TOTVS RENTAL
VALIDA EDIÇÃO DA QUANTIDADE A SER LOCADA
@TYPE FUNCTION
@AUTHOR FRANK ZWARG FUGA
@SINCE 03/12/2020
@VERSION P12
@HISTORY 03/12/2020, FRANK ZWARG FUGA, FONTE PRODUTIZADO.
/*/

#INCLUDE "TOTVS.CH"   
#INCLUDE "TOPCONN.CH"

FUNCTION LOCA033()
LOCAL   _AAREAOLD := GETAREA()
LOCAL   _AAREASB1 := SB1->(GETAREA())
LOCAL   _LRET     := .F.
LOCAL   _CPRODUTO := ALLTRIM(ODLGPLA:ACOLS[ODLGPLA:NAT,ASCAN(ODLGPLA:AHEADER,{|X|ALLTRIM(X[2])=="FPA_PRODUT"})])
LOCAL   _CTIPOSER := ALLTRIM(ODLGPLA:ACOLS[ODLGPLA:NAT,ASCAN(ODLGPLA:AHEADER,{|X|ALLTRIM(X[2])=="FPA_TIPOSE"})])
LOCAL   _CAS      := ALLTRIM(ODLGPLA:ACOLS[ODLGPLA:NAT,ASCAN(ODLGPLA:AHEADER,{|X|ALLTRIM(X[2])=="FPA_AS"})])
LOCAL   CGRPAND   := ""

IF SBM->(FIELDPOS("BM_XACESS")) > 0
	CGRPAND := LOCA00189()
ELSE
	CGRPAND := SUPERGETMV("MV_LOCX014",.F.,"")
ENDIF

IF EMPTY(ALLTRIM(ODLGPLA:ACOLS[ODLGPLA:NAT,ASCAN(ODLGPLA:AHEADER,{|X|ALLTRIM(X[2])=="FPA_NFREM"})]))
	DBSELECTAREA("SB1")
	SB1->(DBSETORDER(1))
	IF SB1->(DBSEEK(XFILIAL("SB1") + _CPRODUTO))
		IF ALLTRIM(SB1->B1_GRUPO) $ ALLTRIM(CGRPAND)
			_LRET := .T.
		ENDIF
	ENDIF
ENDIF

IF ATISROTINA("LOCA001") 
	IF _CTIPOSER == "M"						// --> L=LOCACAO ; O=OUTROS ; Z=COBRANCA UNICA ; M=MAO DE OBRA ; S=SUBSTITUIDO 
		MSGALERT(STR0001 , STR0002)  //"PARA 'TIPO SERVICO' IGUAL 'MAO DE OBRA', NÃO É PERMITIDO A DIGITAÇÃO DO EQUIPAMENTO !"###"GPO - LOCEQTD.PRW"
		_LRET := .T. 
	ENDIF 
ENDIF 

// Não permitir editar campo de equipamento preenchido se houver pedido de venda de remessa, mesmo que sem nota

If !Empty(_CAS)
	FPZ->(DbSetOrder(2))
	If FPZ->(DbSeek(xFiliaL("FPZ")+_CAS))
		FPY->(DbSetOrder(1))
		If FPY->(DbSeek(FPZ->FPZ_FILIAL+FPZ->FPZ_PEDVEN))
			If FPY->FPY_STATUS=="1 " .and. FPY->FPY_TIPFAT=="R"
				_LRET := .T. 
			EndIf
		EndIf
	EndIf
EndIf

RESTAREA( _AAREASB1 )
RESTAREA( _AAREAOLD )

RETURN _LRET
