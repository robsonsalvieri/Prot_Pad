#INCLUDE "TOTVS.CH"
#INCLUDE "RPTDEF.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "LOCA005.CH"
#INCLUDE "FWBROWSE.CH"
#INCLUDE "FWPrintSetup.ch"
#INCLUDE "TOPCONN.CH"
#INCLUDE "FWMVCDEF.CH"

/*/{PROTHEUS.DOC} LOCA005.PRW
ITUP BUSINESS - TOTVS RENTAL
CONTROLE DE MINUTA
@TYPE FUNCTION
@AUTHOR FRANK ZWARG FUGA
@SINCE 03/12/2020
@VERSION P12
@HISTORY 03/12/2020, FRANK ZWARG FUGA, FONTE PRODUTIZADO.
Revitalizado em 28/01/25 Frank (documentação, variáveis e identação)
/*/

FUNCTION LOCA005
Local CFILTRO

Private LVERZBX								// HABILITA CONTROLE DE MINUTA
Private CCADASTRO := STR0001 //"CONTROLE DE MINUTA"
Private AROTINA   := menudef()
Private CDELFUNC  := ".T." 					// VALIDACAO PARA A EXCLUSAO. PODE-SE UTILIZAR EXECBLOCK
Private CSTRING   := "FPF"

	LVERZBX := GETMV("MV_LOCX097") 			// HABILITA CONTROLE DE MINUTA

	DBSELECTAREA(CSTRING)
	DBSETORDER(1)

	CFILTRO := "FPF_FILIAL='"+XFILIAL("FPF")+"'"

	MBROWSE(6,1,22,75,CSTRING,,,,,,LOCA00503(),,,,,,,,CFILTRO)
 
RETURN NIL

/*/{PROTHEUS.DOC} LOCA00503
ITUP BUSINESS - TOTVS RENTAL
LEGENDA DAS MINUTAS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00503(NRECNO)
Local NPOS,ASTATUS,ALEGENDA,ACORES

Private cCadastro := STR0004 //"Legenda"

	ASTATUS  := FSTATUS("MBROWSE") 				// MONTA OS STATUS

	ALEGENDA := {}
	FOR NPOS:=1 TO LEN(ASTATUS)
		AADD(ALEGENDA,{ASTATUS[LEN(ALEGENDA)+1,1],ASTATUS[LEN(ALEGENDA)+1,2]})
	NEXT

	IF NRECNO==NIL 
		ACORES := {} 
		FOR NPOS:=1 TO LEN(ASTATUS)
			AADD(ACORES,{ASTATUS[LEN(ACORES)+1,3],ASTATUS[LEN(ACORES)+1,1]})
		NEXT
		RETURN(ACORES)
	ELSE 
		BRWLEGENDA(CCADASTRO,OEMTOANSI("STATUS"),ALEGENDA)
		RETURN(.T.)
	ENDIF

RETURN


/*/{PROTHEUS.DOC} FSTATUS
ITUP BUSINESS - TOTVS RENTAL
IDENTIFICACAO DAS CORES PARA AS LEGENDAS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FSTATUS(CQUAL)  
LOCAL ASTATUS := {} 

	DO CASE
	CASE UPPER(CQUAL)==UPPER("MBROWSE")
		AADD(ASTATUS,{"BR_VERDE"   ,STR0026  ,'FPF_STATUS=="1"'}) //"MINUTA PREVISTA"
		AADD(ASTATUS,{"BR_AZUL"    ,STR0027  ,'FPF_STATUS=="2"'}) //"MINUTA CONFIRMADA"
		AADD(ASTATUS,{"BR_VERMELHO",STR0028  ,'FPF_STATUS=="3"'}) //"MINUTA BAIXADA"
		AADD(ASTATUS,{"BR_PINK"    ,STR0029  ,'FPF_STATUS=="4"'}) //"MINUTA ENCERRADA"
		AADD(ASTATUS,{"BR_PRETO"   ,STR0030  ,'FPF_STATUS=="5"'}) //"MINUTA CANCELADA"
		AADD(ASTATUS,{"BR_MARROM"  ,STR0031  ,'FPF_STATUS=="6"'}) //"MINUTA MEDIDA"
	CASE UPPER(CQUAL)==UPPER("FWBROWSE")
		AADD(ASTATUS,{"BR_VERDE"   ,STR0026  ,'OBROWSE:DATA():AARRAY[OBROWSE:NAT,NPOSSTA]=="1"'}) //"MINUTA PREVISTA"
		AADD(ASTATUS,{"BR_AZUL"    ,STR0027  ,'OBROWSE:DATA():AARRAY[OBROWSE:NAT,NPOSSTA]=="2"'}) //"MINUTA CONFIRMADA"
		AADD(ASTATUS,{"BR_VERMELHO",STR0028  ,'OBROWSE:DATA():AARRAY[OBROWSE:NAT,NPOSSTA]=="3"'}) //"MINUTA BAIXADA"
		AADD(ASTATUS,{"BR_PINK"    ,STR0029  ,'OBROWSE:DATA():AARRAY[OBROWSE:NAT,NPOSSTA]=="4"'}) //"MINUTA ENCERRADA"
		AADD(ASTATUS,{"BR_PRETO"   ,STR0030  ,'OBROWSE:DATA():AARRAY[OBROWSE:NAT,NPOSSTA]=="5"'}) //"MINUTA CANCELADA"
		AADD(ASTATUS,{"BR_MARROM"  ,STR0031  ,'OBROWSE:DATA():AARRAY[OBROWSE:NAT,NPOSSTA]=="6"'}) //"MINUTA MEDIDA"
	CASE UPPER(CQUAL)==UPPER("LOCA00516")
		AADD(ASTATUS,{"BR_VERDE"   ,STR0026  ,'OBROWSE:DATA():AARRAY[OBROWSE:NAT,8]=="1"'}) //"MINUTA PREVISTA"
		AADD(ASTATUS,{"BR_AZUL"    ,STR0027  ,'OBROWSE:DATA():AARRAY[OBROWSE:NAT,8]=="2"'}) //"MINUTA CONFIRMADA"
		AADD(ASTATUS,{"BR_VERMELHO",STR0028  ,'OBROWSE:DATA():AARRAY[OBROWSE:NAT,8]=="3"'}) //"MINUTA BAIXADA"
		AADD(ASTATUS,{"BR_PINK"    ,STR0029  ,'OBROWSE:DATA():AARRAY[OBROWSE:NAT,8]=="4"'}) //"MINUTA ENCERRADA"
		AADD(ASTATUS,{"BR_PRETO"   ,STR0030  ,'OBROWSE:DATA():AARRAY[OBROWSE:NAT,8]=="5"'}) //"MINUTA CANCELADA"
		AADD(ASTATUS,{"BR_MARROM"  ,STR0031  ,'OBROWSE:DATA():AARRAY[OBROWSE:NAT,8]=="6"'}) //"MINUTA MEDIDA"
	ENDCASE

RETURN(ASTATUS)

/*/{PROTHEUS.DOC} LOCA00559
ITUP BUSINESS - TOTVS RENTAL
IDENTIFICACAO DAS CORES PARA AS LEGENDAS
@AUTHOR DSERLOCA-3350 - Rossana - 24/06/2024
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00559() 
Local aRet := {}

	aRet := FSTATUS("LOCA00516")

RETURN(aRet)

/*/{PROTHEUS.DOC} LOCA00504
ITUP BUSINESS - TOTVS RENTAL
ACEITE DAS MINUTAS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00504() 

	ACEIT2() 

RETURN NIL

/*/{PROTHEUS.DOC} ACEIT2
ITUP BUSINESS - TOTVS RENTAL
ACEITE DAS MINUTAS CONSIDERANDO DATA E HORA
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION ACEIT2()
LOCAL AINFO,APOSOBJ
LOCAL ODLG, ACOLS0
LOCAL AAREA        := GETAREA()
LOCAL CPERG        := "LOCP071"
LOCAL CTITJAN      := STR0032 //"ACEITE DA MINUTA"
LOCAL AZLGSTATUS   := {}
LOCAL AOBJECTS     := {}
LOCAL ASIZEAUT     := MSADVSIZE()
LOCAL AHEADER      := {}
LOCAL ACOLS        := {}
LOCAL CBXSTAT      := ""
LOCAL NI           := 0 
LOCAL NZLGSTATUS   := 0 
Local nx		   := 0 //Adicionado especifico Makro 10/10/2024

PRIVATE ACPOSIM      := {"FPF_MINUTA","FPF_PROJET","FPF_OBRA","FPF_DATA","FPF_FROTA","FPF_AS","FPF_HORAI","FPF_HORAF","FPF_STATUS"} //Adicionado especifico Makro adicionado 09/10/2024
PRIVATE NRECNOATU  
PRIVATE CPROD
PRIVATE CPROA
PRIVATE COBRD
PRIVATE COBRA
PRIVATE CNASD
PRIVATE CNASA
PRIVATE CFROD
PRIVATE CFROA
PRIVATE DDATD
PRIVATE DDATA
PRIVATE AALTER       := {"FPF_HORAI" ,"FPF_HORAF", "FPF_STATUS"}
PRIVATE cCadastro := STR0005 //"Aceite da Minuta"
PRIVATE ACALTER := {} //Adicionado especifico Makro 10/10/2024
Private cEdicao	:= "FPF_HORAI;FPF_HORAF;FPF_STATUS" //Adicionado especifico Makro 10/10/2024
Private ARESPT	:= {} //Adicionado especifico Makro 10/10/2024
Private ARESPX	:= {} //Adicionado especifico Makro 10/10/2024

	IF ! PERGUNTE(CPERG,.T.) 
		RETURN NIL
	ENDIF

	NRECNOATU := IF(MV_PAR01==1,0,FPF->(RECNO())) 	// CONSIDERA PARAMETROS ABAIXO?
	CPROD     := MV_PAR02 						// PROJETO DE
	CPROA     := MV_PAR03 						// PROJETO ATE
	COBRD     := MV_PAR04 						// OBRA DE
	COBRA     := MV_PAR05 						// OBRA ATE
	CNASD     := MV_PAR06 						// AS DE
	CNASA     := MV_PAR07 						// AS ATE
	CFROD     := MV_PAR08 						// FROTA DE
	CFROA     := MV_PAR09 						// FROTA ATE
	DDATD     := MV_PAR10 						// DATA DE
	DDATA     := MV_PAR11 						// DATA ATE

	XZLGSTATUS := MONTACOMBO("FPO_STATUS") 		// CARREGA OS ITENS DO COMBOBOX DA SX3
	FOR NZLGSTATUS:=1 TO LEN(XZLGSTATUS)
		IF ALLTRIM(XZLGSTATUS[NZLGSTATUS,3]) $ "2;3;4;5;6;9;R"
			AADD(AZLGSTATUS, XZLGSTATUS[NZLGSTATUS,2] )
			CBXSTAT += IIF( EMPTY(CBXSTAT),"",";" ) + ALLTRIM( XZLGSTATUS[NZLGSTATUS,2] )
		ENDIF
	NEXT NZLGSTATUS 

	IF EXISTBLOCK("ZBXACTEL")				// PONTO DE ENTRADA PARA INCLUSAO DE CAMPOS NA TELA DE ACEITE DE MINUTA
		ARESPT   :=	{ACPOSIM,ACALTER} //Adicionado especifico Makro 10/10/2024
		ARESPT 	:= EXECBLOCK("ZBXACTEL", .T. , .T. ,{ARESPT}) //Adicionado especifico Makro 10/10/2024

		ARESP1 := ARESPT[1][1] //Adicionado especifico Makro 10/10/2024
		ARESP2 := ARESPT[1][2] //Adicionado especifico Makro 10/10/2024
		ACPOSIM := ARESP1 //Adicionado especifico Makro 10/10/2024
		ACALTER := ARESP2 //Adicionado especifico Makro 10/10/2024
		IF  LEN(ACALTER) <> 0 //Adicionado especifico Makro 10/10/2024
			For nx := 1 to LEN(ACALTER) //Adicionado especifico Makro 10/10/2024
				AADD(AALTER,ACALTER[nx])  //Adicionado especifico Makro 10/10/2024
				cEdicao += ";" + AllTrim(ACALTER[nx]) //Adicionado especifico Makro 10/10/2024
			Next //Adicionado especifico Makro 10/10/2024
		ENDIF //Adicionado especifico Makro 10/10/2024
	ENDIF

	AADD(AHEADER, {"", "SELECAO", "@BMP", 5, 0, /*VALID*/, /*USADO*/, "C" } )
	(LOCXCONV(1))->( DBSETORDER(2) )

	FOR NI := 1 TO LEN(ACPOSIM)
		IF (LOCXCONV(1))->( DBSEEK( ACPOSIM[NI], .T. ) )
			CVALID := ALLTRIM(GetSx3Cache(&(LOCXCONV(2)),"X3_VALID"))   
			CBOX   := ALLTRIM(GetSx3Cache(&(LOCXCONV(2)),"X3_CBOX"))    
			IF ALLTRIM( GetSx3Cache(&(LOCXCONV(2)),"X3_CAMPO") ) $ "FPF_HORAI;FPF_HORAF"      
				CVALID += IIF( EMPTY(CVALID), "", ".AND.") + "LOCA00512()"
			ENDIF
			IF ALLTRIM( GetSx3Cache(&(LOCXCONV(2)),"X3_CAMPO") ) == "FPF_STATUS"
				CBOX := CBXSTAT
			ENDIF
			
			AADD(AHEADER, {GetSx3Cache(&(LOCXCONV(2)),"X3_TITULO"), GetSx3Cache(&(LOCXCONV(2)),"X3_CAMPO"),;
			GetSx3Cache(&(LOCXCONV(2)),"X3_PICTURE"), GetSx3Cache(&(LOCXCONV(2)),"X3_TAMANHO"), GetSx3Cache(&(LOCXCONV(2)),"X3_DECIMAL"), CVALID, /*USADO*/,;
			GetSx3Cache(&(LOCXCONV(2)),"X3_TIPO"), GetSx3Cache(&(LOCXCONV(2)),"X3_F3"), /*SX3->X3_CONTEXT*/ ,CBOX,;
			GetSx3Cache(&(LOCXCONV(2)),"X3_RELACAO"), /*SX3->X3_WHEN*/, /*SX3->X3_VISUAL*/, GetSx3Cache(&(LOCXCONV(2)),"X3_VLDUSER"),;
			GetSx3Cache(&(LOCXCONV(2)),"X3_PICTVAR"), GetSx3Cache(&(LOCXCONV(2)),"X3_OBRIGAT") } )
		ENDIF
	NEXT NI 

	AADD(AHEADER, {"", "RECORD", "", 9, 0, /*VALID*/, /*USADO*/, "N" } )

	FMONTAZBX("QRYFPF",CPERG) 					// MONTA A QUERY	// FMONTAZBX("QRYZBX",CPERG) 

	WHILE ! EOF()
		ACOLS0 := {}
		AADD( ACOLS0, IIF(NRECNOATU==0, "LBNO", "LBOK") )

		FOR NI := 2 TO LEN( AHEADER ) - 1
			AADD( ACOLS0, FIELDGET( FIELDPOS( AHEADER[NI,2] ) ) )
			IF AHEADER[NI,2] == "FPF_HORAI " .AND. EMPTY( ATAIL( ACOLS0 ) )
				ATAIL( ACOLS0 ) := FQ5_HORINI
			ENDIF
			IF AHEADER[NI,2] == "FPF_HORAF " .AND. EMPTY( ATAIL( ACOLS0 ) )
				ATAIL( ACOLS0 ) := FQ5_HORFIM
			ENDIF
			IF AHEADER[NI,2] == "FPF_STATUS" 	//.AND. EMPTY( ATAIL( ACOLS0 ) )
				ATAIL( ACOLS0 ) := "R"
			ENDIF

			nPos := aScan((ACALTER), {|x| AllTrim(Upper(x)) == Alltrim(AHEADER[NI,2])}) //Adicionado especifico Makro 10/10/2024
			If  nPos > 0 //Adicionado especifico Makro 10/10/2024
					cIni := GETSX3CACHE(AHEADER[NI,2],"X3_RELACAO") //Adicionado especifico Makro 10/10/2024
					ATAIL( ACOLS0 ) := cIni	//Adicionado especifico Makro 10/10/2024
			EndIf //Adicionado especifico Makro 10/10/2024

		NEXT 

		AADD( ACOLS0, FPF_RECNO )				// RECNO
		AADD( ACOLS0, .F. )
		AADD( ACOLS, ACOLS0 )
		DBSKIP()
	ENDDO 

	DBCLOSEAREA()
	RESTAREA(AAREA) 

	IF LEN(ACOLS) == 0
		MSGALERT(STR0033 , STR0002) //"NÃO EXISTEM MINUTAS NO INTERVALO SELECIONADO."###"Rental"
		RETURN NIL
	ENDIF


	AADD(AOBJECTS,{100,010,.T., .T. } )  		// ENCHOICE
	AADD(AOBJECTS,{100,090,.T., .T. } )  		// MSGETDADOS

	AINFO   :={ASIZEAUT[1],ASIZEAUT[2],ASIZEAUT[3],ASIZEAUT[4],3,3}
	APOSOBJ :=MSOBJSIZE( AINFO, AOBJECTS, .T. , .F. )

	DEFINE MSDIALOG ODLG FROM ASIZEAUT[7],0 TO ASIZEAUT[6],ASIZEAUT[5] TITLE OEMTOANSI(CTITJAN) OF OMAINWND PIXEL
		OGETD := MSNEWGETDADOS():NEW( APOSOBJ[1][1], APOSOBJ[1][2], APOSOBJ[2][3], APOSOBJ[2][4], GD_UPDATE , /*CLINHAOK*/, /*CTUDOOK*/, /*CINICPOS*/, AALTER,,,,, "ALLWAYSTRUE()", ODLG, AHEADER, ACOLS )
		OGETD:OBROWSE:BLDBLCLICK   := {|| MUDASEL( OGETD ) }
		OGETD:OBROWSE:BHEADERCLICK := {| OOBJ, NCOL| MUDATOT( OOBJ, NCOL, OGETD ) }
	ACTIVATE MSDIALOG ODLG CENTERED ON INIT ENCHOICEBAR( ODLG, {|| TOKACEIT(OGETD, ODLG) },{|| CLOSE(ODLG)})

RETURN NIL

/*/{PROTHEUS.DOC} LOCA00505
ITUP BUSINESS - TOTVS RENTAL
BAIXA DAS MINUTAS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00505() 

	FZBXBAIX() 

RETURN

/*/{PROTHEUS.DOC} FZBXBAIX
ITUP BUSINESS - TOTVS RENTAL
BAIXA DAS MINUTAS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FZBXBAIX()
LOCAL CPERG      := "LOCP072"
LOCAL CTITJAN    := STR0034 //"BAIXA DA MINUTA"
LOCAL AOBJECTS , AINFO , APOSGET , APOSOBJ , ASIZEAUT 
LOCAL ABUTTONS   := {}
LOCAL BMARK      := { || IF(LOCA00510(OBROWSE),'LBOK','LBNO') }
LOCAL BLDBLCLICK := { |OBROWSE| LOCA00509(OBROWSE) }
LOCAL BHEADERCLI := { |OBROWSE| LOCA00511(OBROWSE) }
LOCAL OPANEL1 , OPANEL2 , ACOLS , ACOLS0 , AHEADER , BHEADER , AESTRU , CCAMPO , NSTATUS , ASTATUS , LOK := .F. 
LOCAL NMOTIP , XMOTIP , AMOTIP 
LOCAL NPOS       := 0 

PRIVATE CHALMI , CHALMF , CHJANI , CHJANF , COBS 
Private cCadastro := STR0006 //"Baixa Minuta"

//VALIDPERG(CPERG)
IF !PERGUNTE(CPERG,.T.)
	RETURN
ENDIF

PRIVATE NRECNOATU := IIF(MV_PAR01==1 , 0 , FPF->(RECNO()))  // CONSIDERA PARAMETROS ABAIXO?
PRIVATE CPROD     := MV_PAR02 								// PROJETO DE
PRIVATE CPROA     := MV_PAR03 								// PROJETO ATE
PRIVATE COBRD     := MV_PAR04 								// OBRA DE
PRIVATE COBRA     := MV_PAR05 								// OBRA ATE
PRIVATE CNASD     := MV_PAR06 								// AS DE
PRIVATE CNASA     := MV_PAR07 								// AS ATE
PRIVATE CFROD     := MV_PAR08 								// FROTA DE
PRIVATE CFROA     := MV_PAR09 								// FROTA ATE
PRIVATE DDATD     := MV_PAR10 								// DATA DE
PRIVATE DDATA     := MV_PAR11 								// DATA ATE
PRIVATE CTURD     := "   " 						 			// TURNO DE
PRIVATE CTURA     := "   " 									// TURNO ATE

	SX1->(DbSetOrder(1))
	If SX1->(DbSeek("LOCP072   12"))
		CTURD     := MV_PAR12 							
		CTURA     := MV_PAR13 							
	Else
		CTURD     := "   " 								
		CTURA     := "999" 								
	EndIf

	XMOTIP := MONTACOMBO("FPP_MOTIVO")  		// CARREGA OS ITENS DO COMBOBOX DA SX3
	AMOTIP := {}
	
	FOR NMOTIP:=1 TO LEN(XMOTIP)
		AADD(AMOTIP,XMOTIP[NMOTIP,2])
	NEXT

	PRIVATE NPOSMAR , NPOSPRO , NPOSOBR , NPOSDAT , NPOSFRO , NPOSNAS , NPOSMIN , NPOSMED , NPOSHOI , NPOSHOF , NPOSHOP , NPOSMOT , NPOSMIM , NPOSSTA
	PRIVATE NPOSALI , NPOSALF , NPOSJAI , NPOSJAF , NPOSOBS , AOBJGET

	PRIVATE OBROWSE , ODLG
	PRIVATE OSAYHRINI, OSAYHRFIM, OSAYHRPR, OSAYMOTPR, OSAYALMIN, OSAYALMFI, OSAYJANIN, OSAYJANFI, OSAYOBS, OSAYMIMAN
	PRIVATE OGETHRINI, OGETHRFIM, OGETHRPR, OGETMOTPR, OGETALMIN, OGETALMFI, OGETJANIN, OGETJANFI, OGETOBS, OGETMIMAN
	PRIVATE CHORAI,CHORAF,CHORAP,CMOTIP,CMINUM
	PRIVATE CMARCA
	PRIVATE ARECNOSZBX  						// GUARDA OS RECNOS DO ZBX

	CHORAI := SPACE(05)
	CHORAF := SPACE(05)
	CHORAP := SPACE(05)
	CMOTIP := SPACE(01)
	CMINUM := SPACE(06)

	CHALMI := SPACE(05)
	CHALMF := SPACE(05)
	CHJANI := SPACE(05)
	CHJANF := SPACE(05)
	COBS   := ""

	XMOTIP := MONTACOMBO("FPP_MOTIVO") 			// CARREGA OS ITENS DO COMBOBOX DA SX3
	AMOTIP := {}
	
	FOR NMOTIP:=1 TO LEN(XMOTIP)
		AADD(AMOTIP,XMOTIP[NMOTIP,2])
	NEXT

	AOBJGET := ARRAY(10)						// OBJETOS GET PARA EDIÇÃO

	CMARCA     := GETMARK()
	ARECNOSZBX := {} 						// GUARDA OS RECNOS DO ZBX
	ACOLS      := {} 

	FMONTAZBX("QRYFPF",CPERG) 				// MONTA A QUERY		// 

	QRYFPF->(DBGOTOP())
	WHILE QRYFPF->(!EOF())
		FPF->( DBGOTO( QRYFPF->FPF_RECNO ) )

		AESTRU:={}    
		CCAMPO:=""          ;QRYFPF->(AADD(AESTRU,{IF(NRECNOATU==0,SPACE(LEN(CMARCA)),CMARCA),""    ,""                             ,""                           ,""                              ,0,00                              ,0                               }));NPOSMAR:=LEN(AESTRU)
		CCAMPO:="FPF_PROJET";QRYFPF->(AADD(AESTRU,{&CCAMPO ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSPRO:=LEN(AESTRU)
		CCAMPO:="FPF_OBRA"  ;QRYFPF->(AADD(AESTRU,{&CCAMPO ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSOBR:=LEN(AESTRU)
		CCAMPO:="FPF_DATA"  ;QRYFPF->(AADD(AESTRU,{&CCAMPO ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSDAT:=LEN(AESTRU)
		CCAMPO:="FPF_FROTA" ;QRYFPF->(AADD(AESTRU,{&CCAMPO ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSFRO:=LEN(AESTRU)
		
		IF FPF->(FIELDPOS("FPF_XNOMRE")) > 0
			CCAMPO:="FPF_XNOMRE" ;QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSNOM:=LEN(AESTRU)
		ENDIF
		
		CCAMPO:="FPF_AS"    ;QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSNAS:=LEN(AESTRU)
		CCAMPO:="FPF_MINUTA";QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSMIN:=LEN(AESTRU)
		CCAMPO:="FPF_NROMED";QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSMED:=LEN(AESTRU)
		CCAMPO:="FPF_HORAI" ;QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSHOI:=LEN(AESTRU)
		CCAMPO:="FPF_HORAF" ;QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSHOF:=LEN(AESTRU)

		CCAMPO:="FPF_BXALMI" ;FPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSALI:=LEN(AESTRU)
		CCAMPO:="FPF_BXALMF" ;FPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSALF:=LEN(AESTRU)
		CCAMPO:="FPF_BXJANI" ;FPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSJAI:=LEN(AESTRU)
		CCAMPO:="FPF_BXJANF" ;FPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSJAF:=LEN(AESTRU)
		CCAMPO:="FPF_BXOBS"  ;FPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSOBS:=LEN(AESTRU)

		CCAMPO:="FPF_HRPARA";QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSHOP:=LEN(AESTRU)
		CCAMPO:="FPF_MOTIVO";QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSMOT:=LEN(AESTRU)
		CCAMPO:="FPF_MINUTM";QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSMIM:=LEN(AESTRU)
		CCAMPO:="FPF_STATUS";QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSSTA:=LEN(AESTRU)
	
		QRYFPF->(AADD(ARECNOSZBX,FPF_RECNO))  //GUARDA OS RECNOS DO ZBX
	
		ACOLS0:={}
		FOR NPOS:=1 TO LEN(AESTRU)
			AADD(ACOLS0,AESTRU[NPOS,1])
		NEXT
		AADD(ACOLS,ACOLS0)
	
		QRYFPF->(DBSKIP())
	ENDDO 
	
	IF LEN(ACOLS)==0
		MSGALERT(STR0033 , STR0002) //"NÃO EXISTEM MINUTAS NO INTERVALO SELECIONADO."###"Rental"
		RETURN
	ENDIF

	AHEADER := {}
	FOR NPOS:=2 TO LEN(AESTRU)
		BHEADER := "{||ACOLS[OBROWSE:AT(),"+STRZERO(NPOS,2)+"]}"
		AADD(AHEADER,{AESTRU[NPOS,3],&BHEADER,AESTRU[NPOS,4],AESTRU[NPOS,5],AESTRU[NPOS,6],AESTRU[NPOS,7],AESTRU[NPOS,8]})
	NEXT NPOS 
	
	IF OMAINWND:NCLIENTWIDTH > 800
		AOBJECTS:={}
		AADD(AOBJECTS,{100,010,.T., .T. } )	// ENCHOICE
		AADD(AOBJECTS,{100,090,.T., .T. } )	// MSGETDADOS
	ELSE
		AOBJECTS:={}
		AADD(AOBJECTS,{100,012,.T., .T. } )	// ENCHOICE
		AADD(AOBJECTS,{100,088,.T., .T. } )	// MSGETDADOS
	ENDIF
	
	ASIZEAUT := MSADVSIZE()
	AINFO    := {ASIZEAUT[1],ASIZEAUT[2],ASIZEAUT[3],ASIZEAUT[4],3,3}
	APOSOBJ  := MSOBJSIZE( AINFO, AOBJECTS, .T. , .F. )
	APOSGET  := MSOBJGETPOS((ASIZEAUT[3]-ASIZEAUT[1]),315,{{004,024,240,270}} )
	
	DEFINE MSDIALOG ODLG FROM ASIZEAUT[7],0 TO ASIZEAUT[6],ASIZEAUT[5] TITLE OEMTOANSI(CTITJAN) OF OMAINWND PIXEL
	@ APOSOBJ[1,1],APOSOBJ[1,2] MSPANEL OPANEL1 PROMPT "" SIZE 10,40 OF ODLG
	@ APOSOBJ[2,1],APOSOBJ[2,2] MSPANEL OPANEL2 PROMPT "" SIZE 10,10 OF ODLG
	OPANEL1:ALIGN:=CONTROL_ALIGN_TOP
	OPANEL2:ALIGN:=CONTROL_ALIGN_ALLCLIENT

	@ 005,005 SAY      OSAYHRINI PROMPT OEMTOANSI(STR0035 )  SIZE 050, 8 OF OPANEL1 PIXEL  //"HORA INICIAL:"
	@ 004,040 MSGET    OGETHRINI VAR CHORAI PICTURE "99:99"          SIZE  15, 8 OF OPANEL1 PIXEL VALID LOCA00520(ACOLS,AHEADER)

	@ 018,005 SAY      OSAYHRFIM PROMPT OEMTOANSI(STR0036   )  SIZE 050, 8 OF OPANEL1 PIXEL  //"HORA FINAL:"
	@ 018,040 MSGET    OGETHRFIM VAR CHORAF PICTURE "99:99"          SIZE  15, 8 OF OPANEL1 PIXEL VALID LOCA00520(ACOLS,AHEADER)

	@ 005,085 SAY      OSAYHRPR PROMPT OEMTOANSI(STR0037   )  SIZE 050, 8 OF OPANEL1 PIXEL  //"HORA PARADA:"
	@ 004,130 MSGET    OGETHRPR VAR CHORAP  PICTURE "99:99"          SIZE  15, 8 OF OPANEL1 PIXEL 

	@ 018,085 SAY      OSAYMOTPR PROMPT OEMTOANSI(STR0038 ) SIZE 050, 8 OF OPANEL1 PIXEL  //"MOTIVO PARADA:"
	@ 017,130 COMBOBOX OGETMOTPR VAR CMOTIP ITEMS AMOTIP             SIZE  50, 8 OF OPANEL1 PIXEL  // 3=TRANSPORTES;9=MAN PREV;0=MO;C=MAN CORR

	@ 005,195 SAY      OSAYALMIN PROMPT OEMTOANSI(STR0039) SIZE 050, 8 OF OPANEL1 PIXEL  //"ALMOÇO INICIAL:"
	@ 004,235 MSGET    OGETALMIN VAR CHALMI PICTURE "99:99"          SIZE  15, 8 OF OPANEL1 PIXEL VALID LOCA00520(ACOLS,AHEADER)

	@ 018,195 SAY      OSAYALMFI PROMPT OEMTOANSI(STR0040  ) SIZE 050, 8 OF OPANEL1 PIXEL  //"ALMOÇO FINAL:"
	@ 018,235 MSGET    OGETALMFI VAR CHALMF PICTURE "99:99"          SIZE  15, 8 OF OPANEL1 PIXEL VALID LOCA00520(ACOLS,AHEADER)

	@ 005,275 SAY      OSAYJANIN PROMPT OEMTOANSI(STR0041) SIZE 050, 8 OF OPANEL1 PIXEL  //"JANTAR INICIAL:"
	@ 004,315 MSGET    OGETJANIN VAR CHJANI PICTURE "99:99"          SIZE  15, 8 OF OPANEL1 PIXEL VALID LOCA00520(ACOLS,AHEADER)

	@ 018,275 SAY      OSAYJANFI PROMPT OEMTOANSI(STR0042  ) SIZE 050, 8 OF OPANEL1 PIXEL  //"JANTAR FINAL:"
	@ 018,315 MSGET    OGETJANFI VAR CHJANF PICTURE "99:99"          SIZE  15, 8 OF OPANEL1 PIXEL VALID LOCA00520(ACOLS,AHEADER)

	@ 005,355 SAY      OSAYOBS   PROMPT OEMTOANSI(STR0043           ) SIZE 050, 8 OF OPANEL1 PIXEL  //"OBS:"
	@ 004,370 GET      OGETOBS   VAR COBS MEMO                       SIZE 160,30 OF OPANEL1 PIXEL 

	@ 005,540 SAY      OSAYMIMAN PROMPT OEMTOANSI(STR0044 ) SIZE 050, 8 OF OPANEL1 PIXEL  //"MINUTA MANUAL:"
	@ 014,540 MSGET    OGETMIMAN VAR CMINUM PICTURE "@X"             SIZE 030, 8 OF OPANEL1 PIXEL 

	OBROWSE := FWBROWSE():NEW() 
	OBROWSE:SETDATAARRAY()
	OBROWSE:SETARRAY(ACOLS)
	OBROWSE:ADDMARKCOLUMNS(BMARK,BLDBLCLICK,BHEADERCLI)

	ASTATUS := FSTATUS("FWBROWSE")  		// MONTA OS STATUS
	
	FOR NSTATUS:=1 TO LEN(ASTATUS)
		OBROWSE:ADDLEGEND(ASTATUS[NSTATUS,3],ASTATUS[NSTATUS,1],ASTATUS[NSTATUS,2])
	NEXT

	IF EXISTBLOCK("ZBXBXBUT")				// PONTO DE ENTRADA PARA INCLUSAO DE BOTOES NAS ACOES RELACIONADAS DA BAIXA DA MINUTA
		ABUTTONS := EXECBLOCK("ZBXBXBUT",.T.,.T.,{ABUTTONS,ODLG,ACOLS})
	ENDIF

	OBROWSE:SETCOLUMNS(AHEADER)
	OBROWSE:SETOWNER(OPANEL2)
	OBROWSE:DISABLEREPORT()
	OBROWSE:DISABLECONFIG()
	OBROWSE:ACTIVATE()
	
	ACTIVATE MSDIALOG ODLG CENTERED ON INIT ENCHOICEBAR(ODLG,{||LOK:=.T.,IF(FBAIXAM(OBROWSE,CHORAI,CHORAF,CHORAP,CMOTIP,CMINUM, CHALMI, CHALMF, CHJANI, CHJANF, COBS),CLOSE(ODLG),)},{||LOK:=.F.,CLOSE(ODLG)},,ABUTTONS)

	IF !LOK
		IF EXISTBLOCK("ZBXCANBX")			// PONTO DE ENTRADA AO CANCELAR O PROCESSO DE BAIXA DE MINUTA
			EXECBLOCK("ZBXCANBX",.T.,.T.,{ACOLS})
		ENDIF	   
	ENDIF

RETURN 

/*/{PROTHEUS.DOC} LOCA00506
ITUP BUSINESS - TOTVS RENTAL
CANCELAMENTO DAS MINUTAS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00506() 

	FZBXCANC() 

RETURN

/*/{PROTHEUS.DOC} FZBXCANC
ITUP BUSINESS - TOTVS RENTAL
CANCELAMENTO DAS MINUTAS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FZBXCANC()
LOCAL CPERG      := "LOCP074"
LOCAL CTITJAN    := STR0045 //"CANCELAMENTO DA MINUTA"
LOCAL AOBJECTS , AINFO , APOSGET , APOSOBJ , ASIZEAUT 
LOCAL ABUTTONS   := {}
LOCAL BMARK      := { || IF(LOCA00510(OBROWSE),'LBOK','LBNO') }
LOCAL BLDBLCLICK := { |OBROWSE| LOCA00509(OBROWSE) }
LOCAL BHEADERCLI := { |OBROWSE| LOCA00511(OBROWSE) }
LOCAL ODLG , OPANEL1 , OPANEL2 , ACOLS , ACOLS0 , AHEADER , BHEADER , AESTRU , CCAMPO , NSTATUS , ASTATUS , LOK := .F. 
LOCAL CMOTCAN , OMOTCAND , CMOTCAND 
LOCAL NPOS       := 0 

//VALIDPERG(CPERG)
IF !PERGUNTE(CPERG,.T.)
	RETURN
ENDIF

PRIVATE NRECNOATU := IF(MV_PAR01==1,0,FPF->(RECNO()))  //CONSIDERA PARAMETROS ABAIXO?
PRIVATE CPROD     := MV_PAR02 				// PROJETO DE
PRIVATE CPROA     := MV_PAR03 				// PROJETO ATE
PRIVATE COBRD     := MV_PAR04 				// OBRA DE
PRIVATE COBRA     := MV_PAR05 				// OBRA ATE
PRIVATE CNASD     := MV_PAR06 				// AS DE
PRIVATE CNASA     := MV_PAR07 				// AS ATE
PRIVATE CFROD     := MV_PAR08 				// FROTA DE
PRIVATE CFROA     := MV_PAR09 				// FROTA ATE
PRIVATE DDATD     := MV_PAR10 				// DATA DE
PRIVATE DDATA     := MV_PAR11 				// DATA ATE
PRIVATE CMOTI     := MV_PAR12 				// MOTIVO DE CANCELAMENTO

CMOTCAN  := Space(TamSx3("TPJ_CODMOT")[1]) //SPACE(02) // 05/07/2023 - SIGALOC94-853 - Inicializa com o tamanho do campo
CMOTCAND := SPACE(40)

PRIVATE NPOSMAR,NPOSPRO,NPOSOBR,NPOSDAT,NPOSFRO,NPOSNAS,NPOSMIN,NPOSMED,NPOSHOI,NPOSHOF,NPOSHOP,NPOSMOT,NPOSMIM,NPOSSTA
PRIVATE OBROWSE
PRIVATE CMARCA
PRIVATE ARECNOSZBX 							// GUARDA OS RECNOS DO ZBX

	WHILE .T.
		CMARCA     := GETMARK()
		ARECNOSZBX := {} 						// GUARDA OS RECNOS DO ZBX
		ACOLS      := {}
		
		FMONTAZBX("QRYFPF",CPERG) 				// MONTA A QUERY	// FMONTAZBX("QRYZBX",CPERG) 

		QRYFPF->(DBGOTOP())
		WHILE QRYFPF->(!EOF())
			AESTRU:={}
			CCAMPO:=""          ;QRYFPF->(AADD(AESTRU,{IF(NRECNOATU==0,SPACE(LEN(CMARCA)),CMARCA),""    ,""                             ,""                           ,""                              ,0,00                              ,0                               }));NPOSMAR:=LEN(AESTRU)
			CCAMPO:="FPF_PROJET";QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSPRO:=LEN(AESTRU)
			CCAMPO:="FPF_OBRA"  ;QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSOBR:=LEN(AESTRU)
			CCAMPO:="FPF_DATA"  ;QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSDAT:=LEN(AESTRU)
			CCAMPO:="FPF_FROTA" ;QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSFRO:=LEN(AESTRU)
			CCAMPO:="FPF_AS"    ;QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSNAS:=LEN(AESTRU)
			CCAMPO:="FPF_MINUTA";QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSMIN:=LEN(AESTRU)
			CCAMPO:="FPF_NROMED";QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSMED:=LEN(AESTRU)
			CCAMPO:="FPF_HORAI" ;QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSHOI:=LEN(AESTRU)
			CCAMPO:="FPF_HORAF" ;QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSHOF:=LEN(AESTRU)
			CCAMPO:="FPF_HRPARA";QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSHOP:=LEN(AESTRU)
			CCAMPO:="FPF_MOTIVO";QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSMOT:=LEN(AESTRU)
			CCAMPO:="FPF_MINUTM";QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSMIM:=LEN(AESTRU)
			CCAMPO:="FPF_STATUS";QRYFPF->(AADD(AESTRU,{&CCAMPO           ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSSTA:=LEN(AESTRU)
		
			QRYFPF->(AADD(ARECNOSZBX,FPF_RECNO))  //GUARDA OS RECNOS DO ZBX
		
			ACOLS0 := {}
			
			FOR NPOS:=1 TO LEN(AESTRU)
				AADD(ACOLS0 , AESTRU[NPOS][1])
			NEXT NPOS 
			
			AADD(ACOLS,ACOLS0)
		
			QRYFPF->(DBSKIP())
		ENDDO 
		
		IF LEN(ACOLS)==0 .and. MV_PAR01 == 1
			MSGALERT(STR0033 , STR0002) //"NÃO EXISTEM MINUTAS NO INTERVALO SELECIONADO."###"Rental"
			RETURN
		ENDIF

		IF LEN(ACOLS)==0 .and. MV_PAR01 == 2
			MSGALERT(STR0182 , STR0002) //"Somente podem ser canceladas minutas com status previsto."###"Rental"
			RETURN
		ENDIF
		
		AHEADER := {}
		
		FOR NPOS:=2 TO LEN(AESTRU)
			BHEADER:="{||ACOLS[OBROWSE:AT(),"+STRZERO(NPOS,2)+"]}"
			AADD(AHEADER,{AESTRU[NPOS,3],&BHEADER,AESTRU[NPOS,4],AESTRU[NPOS,5],AESTRU[NPOS,6],AESTRU[NPOS,7],AESTRU[NPOS,8]})
		NEXT
		
		IF OMAINWND:NCLIENTWIDTH > 800
			AOBJECTS:={}
			AADD(AOBJECTS,{100,010,.T., .T. } )  //ENCHOICE
			AADD(AOBJECTS,{100,090,.T., .T. } )  //MSGETDADOS
		ELSE
			AOBJECTS:={}
			AADD(AOBJECTS,{100,012,.T., .T. } )  //ENCHOICE
			AADD(AOBJECTS,{100,088,.T., .T. } )  //MSGETDADOS
		ENDIF
		
		ASIZEAUT:=MSADVSIZE()
		AINFO   :={ASIZEAUT[1],ASIZEAUT[2],ASIZEAUT[3],ASIZEAUT[4],3,3}
		APOSOBJ :=MSOBJSIZE( AINFO, AOBJECTS, .T. , .F. )
		APOSGET :=MSOBJGETPOS((ASIZEAUT[3]-ASIZEAUT[1]),315,{{004,024,240,270}} )

		DEFINE MSDIALOG ODLG FROM ASIZEAUT[7],0 TO ASIZEAUT[6],ASIZEAUT[5] TITLE OEMTOANSI(CTITJAN) OF OMAINWND PIXEL
		@ APOSOBJ[1,1],APOSOBJ[1,2] MSPANEL OPANEL1 PROMPT "" SIZE 10,40 OF ODLG
		@ APOSOBJ[2,1],APOSOBJ[2,2] MSPANEL OPANEL2 PROMPT "" SIZE 10,10 OF ODLG
		OPANEL1:ALIGN:=CONTROL_ALIGN_TOP
		OPANEL2:ALIGN:=CONTROL_ALIGN_ALLCLIENT
	
		@ 005,005 SAY OEMTOANSI(STR0046) SIZE 100,8 OF OPANEL1 PIXEL //"MOTIVO DO CANCELAMENTO"
		@ 013,005 GET CMOTCAN SIZE 010,8 PICTURE "@!" F3 "TPJ" VALID(FVERMOTCAN(CMOTCAN,@CMOTCAND,OMOTCAND))
		@ 013,055 MSGET OMOTCAND VAR CMOTCAND SIZE 100,008 OF ODLG PIXEL WHEN .F.
	
		OBROWSE:=FWBROWSE():NEW()
		OBROWSE:SETDATAARRAY()
		OBROWSE:SETARRAY(ACOLS)
		OBROWSE:ADDMARKCOLUMNS(BMARK,BLDBLCLICK,BHEADERCLI)
		
		ASTATUS := FSTATUS("FWBROWSE") 		// MONTA OS STATUS
		FOR NSTATUS:=1 TO LEN(ASTATUS)
			OBROWSE:ADDLEGEND(ASTATUS[NSTATUS,3],ASTATUS[NSTATUS,1],ASTATUS[NSTATUS,2])
		NEXT
		
		OBROWSE:SETCOLUMNS(AHEADER)
		OBROWSE:SETOWNER(ODLG)
		OBROWSE:DISABLEREPORT()
		OBROWSE:DISABLECONFIG()
		OBROWSE:ACTIVATE()
		ACTIVATE MSDIALOG ODLG CENTERED ON INIT ENCHOICEBAR(ODLG,{||LOK:=.T.,IF(FCANCELAM(OBROWSE,CMOTCAN),CLOSE(ODLG),)},{||LOK:=.F.,CLOSE(ODLG)},,ABUTTONS)

		IF !LOK .OR. NRECNOATU>0
			EXIT
		ENDIF

	ENDDO 

RETURN


/*/{PROTHEUS.DOC} LOCA00507
ITUP BUSINESS - TOTVS RENTAL
ESTORNO DAS MINUTAS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00507() 

	FZBXESTO() 

RETURN

/*/{PROTHEUS.DOC} FZBXESTO
ITUP BUSINESS - TOTVS RENTAL
ESTORNO DAS MINUTAS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FZBXESTO()
LOCAL CPERG      := "LOCP074"
LOCAL CTITJAN    := STR0047 //"ESTORNO DA MINUTA"
LOCAL AOBJECTS,AINFO,APOSGET,APOSOBJ,ASIZEAUT
LOCAL ABUTTONS   := {}
LOCAL BMARK      := { || IF(LOCA00510(OBROWSE),'LBOK','LBNO') }
LOCAL BLDBLCLICK := { |OBROWSE| LOCA00509(OBROWSE) }
LOCAL BHEADERCLI := { |OBROWSE| LOCA00511(OBROWSE) }
LOCAL ODLG,ACOLS,ACOLS0,AHEADER,BHEADER,AESTRU,CCAMPO,NSTATUS,ASTATUS,LOK:=.F.
LOCAL NPOS       := 0 

//VALIDPERG(CPERG)
IF !PERGUNTE(CPERG,.T.)
	RETURN
ENDIF

PRIVATE NRECNOATU := IF(MV_PAR01==1,0,FPF->(RECNO()))  //CONSIDERA PARAMETROS ABAIXO?
PRIVATE CPROD     := MV_PAR02 				// PROJETO DE
PRIVATE CPROA     := MV_PAR03 				// PROJETO ATE
PRIVATE COBRD     := MV_PAR04 				// OBRA DE
PRIVATE COBRA     := MV_PAR05 				// OBRA ATE
PRIVATE CNASD     := MV_PAR06 				// AS DE
PRIVATE CNASA     := MV_PAR07 				// AS ATE
PRIVATE CFROD     := MV_PAR08 				// FROTA DE
PRIVATE CFROA     := MV_PAR09 				// FROTA ATE
PRIVATE DDATD     := MV_PAR10 				// DATA DE
PRIVATE DDATA     := MV_PAR11 				// DATA ATE

PRIVATE NPOSMAR,NPOSPRO,NPOSOBR,NPOSDAT,NPOSFRO,NPOSNAS,NPOSMIN,NPOSMED,NPOSHOI,NPOSHOF,NPOSHOP,NPOSMOT,NPOSMIM,NPOSSTA
PRIVATE OBROWSE
PRIVATE CMARCA
PRIVATE ARECNOSZBX 				   			// GUARDA OS RECNOS DO ZBX

	WHILE .T.
		CMARCA     := GETMARK()
		ARECNOSZBX := {} 						// GUARDA OS RECNOS DO ZBX
		ACOLS      := {}

		FMONTAZBX("QRYFPF",CPERG) 				// MONTA A QUERY	// FMONTAZBX("QRYZBX",CPERG) 

		QRYFPF->(DBGOTOP())
		WHILE QRYFPF->(!EOF())
			AESTRU:={}
			CCAMPO:=""          ;QRYFPF->(AADD(AESTRU,{IF(NRECNOATU==0,SPACE(LEN(CMARCA)),CMARCA),""    ,""                             ,""                           ,""                              ,0,00                              ,0                               }));NPOSMAR:=LEN(AESTRU)
			CCAMPO:="FPF_PROJET";QRYFPF->(AADD(AESTRU,{&CCAMPO                                   ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSPRO:=LEN(AESTRU)
			CCAMPO:="FPF_OBRA"  ;QRYFPF->(AADD(AESTRU,{&CCAMPO                                   ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSOBR:=LEN(AESTRU)
			CCAMPO:="FPF_DATA"  ;QRYFPF->(AADD(AESTRU,{&CCAMPO                                   ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSDAT:=LEN(AESTRU)
			CCAMPO:="FPF_FROTA" ;QRYFPF->(AADD(AESTRU,{&CCAMPO                                   ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSFRO:=LEN(AESTRU)
			CCAMPO:="FPF_AS"    ;QRYFPF->(AADD(AESTRU,{&CCAMPO                                   ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSNAS:=LEN(AESTRU)
			CCAMPO:="FPF_MINUTA";QRYFPF->(AADD(AESTRU,{&CCAMPO                                   ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSMIN:=LEN(AESTRU)
			CCAMPO:="FPF_NROMED";QRYFPF->(AADD(AESTRU,{&CCAMPO                                   ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSMED:=LEN(AESTRU)
			CCAMPO:="FPF_HORAI" ;QRYFPF->(AADD(AESTRU,{&CCAMPO                                   ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSHOI:=LEN(AESTRU)
			CCAMPO:="FPF_HORAF" ;QRYFPF->(AADD(AESTRU,{&CCAMPO                                   ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSHOF:=LEN(AESTRU)
			CCAMPO:="FPF_HRPARA";QRYFPF->(AADD(AESTRU,{&CCAMPO                                   ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSHOP:=LEN(AESTRU)
			CCAMPO:="FPF_MOTIVO";QRYFPF->(AADD(AESTRU,{&CCAMPO                                   ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSMOT:=LEN(AESTRU)
			CCAMPO:="FPF_MINUTM";QRYFPF->(AADD(AESTRU,{&CCAMPO                                   ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSMIM:=LEN(AESTRU)
			CCAMPO:="FPF_STATUS";QRYFPF->(AADD(AESTRU,{&CCAMPO                                   ,CCAMPO,GETSX3CACHE(CCAMPO,"X3_TITULO"),GETSX3CACHE(CCAMPO,"X3_TIPO"),GETSX3CACHE(CCAMPO,"X3_PICTURE"),1,GETSX3CACHE(CCAMPO,"X3_TAMANHO"),GETSX3CACHE(CCAMPO,"X3_DECIMAL")}));NPOSSTA:=LEN(AESTRU)
		
			QRYFPF->(AADD(ARECNOSZBX,FPF_RECNO))  //GUARDA OS RECNOS DO ZBX
		
			ACOLS0 := {} 

			FOR NPOS:=1 TO LEN(AESTRU)
				AADD(ACOLS0,AESTRU[NPOS,1])
			NEXT NPOS 

			AADD(ACOLS,ACOLS0)
		
			QRYFPF->(DBSKIP())
		ENDDO 
		
		IF LEN(ACOLS)==0
			MSGALERT(STR0033 , STR0002) //"NÃO EXISTEM MINUTAS NO INTERVALO SELECIONADO."###"Rental"
			RETURN
		ENDIF
		
		AHEADER := {} 

		FOR NPOS:=2 TO LEN(AESTRU)
			BHEADER:="{||ACOLS[OBROWSE:AT(),"+STRZERO(NPOS,2)+"]}"
			AADD(AHEADER,{AESTRU[NPOS,3],&BHEADER,AESTRU[NPOS,4],AESTRU[NPOS,5],AESTRU[NPOS,6],AESTRU[NPOS,7],AESTRU[NPOS,8]})
		NEXT
		
		IF OMAINWND:NCLIENTWIDTH > 800
			AOBJECTS:={}
			AADD(AOBJECTS,{100,010,.T., .T. } ) 	// ENCHOICE
			AADD(AOBJECTS,{100,090,.T., .T. } ) 	// MSGETDADOS
		ELSE
			AOBJECTS:={}
			AADD(AOBJECTS,{100,012,.T., .T. } ) 	// ENCHOICE
			AADD(AOBJECTS,{100,088,.T., .T. } ) 	// MSGETDADOS
		ENDIF

		ASIZEAUT := MSADVSIZE()
		AINFO    := {ASIZEAUT[1],ASIZEAUT[2],ASIZEAUT[3],ASIZEAUT[4],3,3}
		APOSOBJ  := MSOBJSIZE( AINFO, AOBJECTS, .T. , .F. )
		APOSGET  := MSOBJGETPOS((ASIZEAUT[3]-ASIZEAUT[1]),315,{{004,024,240,270}} )
		
		DEFINE MSDIALOG ODLG FROM ASIZEAUT[7],0 TO ASIZEAUT[6],ASIZEAUT[5] TITLE OEMTOANSI(CTITJAN) OF OMAINWND PIXEL
		OBROWSE:=FWBROWSE():NEW()
		OBROWSE:SETDATAARRAY()
		OBROWSE:SETARRAY(ACOLS)
		OBROWSE:ADDMARKCOLUMNS(BMARK,BLDBLCLICK,BHEADERCLI)
		
		ASTATUS := FSTATUS("FWBROWSE") 			// MONTA OS STATUS

		FOR NSTATUS:=1 TO LEN(ASTATUS)
			OBROWSE:ADDLEGEND(ASTATUS[NSTATUS,3],ASTATUS[NSTATUS,1],ASTATUS[NSTATUS,2])
		NEXT
		
		OBROWSE:SETCOLUMNS(AHEADER)
		OBROWSE:SETOWNER(ODLG)
		OBROWSE:DISABLEREPORT()
		OBROWSE:DISABLECONFIG()
		OBROWSE:ACTIVATE()
		ACTIVATE MSDIALOG ODLG CENTERED ON INIT ENCHOICEBAR(ODLG,{||LOK:=.T.,IF(FESTORNAM(OBROWSE),CLOSE(ODLG),)},{||LOK:=.F.,CLOSE(ODLG)},,ABUTTONS)

		IF !LOK .OR. NRECNOATU>0
			EXIT
		ENDIF

	ENDDO 

RETURN 

/*/{PROTHEUS.DOC} LOCA00508
ITUP BUSINESS - TOTVS RENTAL
TROCA EQUIPAMENTO
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00508() 
Local lRet := .T.

	If empty(FPF->FPF_FROTA)
		Help(Nil,Nil,alltrim(upper(Procname())),; 
			Nil,STR0171,1,0,Nil,Nil,Nil,Nil,Nil,; //"Inconsistência nos dados." 
			{STR0181}) //"Opção indisponível para registros sem a indentificação do bem."
		lRet := .F.
	Else
		FZBXTROC() 
	EndIf

Return lRet

/*/{PROTHEUS.DOC} FZBXTROC
ITUP BUSINESS - TOTVS RENTAL
TROCA EQUIPAMENTO
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FZBXTROC()
LOCAL ODLG
LOCAL OFROTAD,OFROTAP,ODESCD,ODESCP
LOCAL CFROTAD,CFROTAP,CDESCD,CDESCP,CF3 
LOCAL DDATAD,DDATAA,CAS

	IF FPF->(FPF_STATUS <> "1") 				// 1=PREVISTA,2=CONFIRMADA,3=BAIXADA,4=ENCERRADA,5=CANCELADA,6=MEDIDA
		MSGALERT(STR0048 , STR0002) //"TROCA PERMITIDA APENAS PARA MINUTA COM STATUS 1=PREVISTA OU 5=CANCELADA."###"RENTAL"
		RETURN
	ENDIF

	DDATAD  := FPF->FPF_DATA
	DDATAA  := FPF->FPF_DATA
	CAS     := FPF->FPF_AS
	CFROTAD := FPF->FPF_FROTA
	CFROTAP := FPF->(SPACE(LEN(FPF_FROTA)))
	CDESCD  := ""
	CDESCP  := ""

	IF SUBSTR(CAS,2,1) == "2"					// MAO DE OBRA
		CF3 := "SRA"
	ELSE
		CF3 := "ST9LOC"							// ST9MIN
	ENDIF 

	FTRAZDESC(CFROTAD,@CDESCD,ODESCD) 			// PREENCHE O CAMPO COM O NOME DO BEM
	FTRAZDESC(CFROTAP,@CDESCP,ODESCP) 			// PREENCHE O CAMPO COM O NOME DO BEM
	
	DEFINE MSDIALOG ODLG FROM 0,0 TO 200,700 PIXEL TITLE OEMTOANSI(STR0049) //"TROCA DE RECURSO"
	@ 005,005 SAY OEMTOANSI(STR0050)     SIZE 100,030 OF ODLG PIXEL //"DO RECURSO"
	@ 013,005 MSGET OFROTAD VAR CFROTAD       SIZE 080,008 OF ODLG PIXEL WHEN .F.
	@ 005,100 SAY OEMTOANSI(STR0051     ) SIZE 100,030 OF ODLG PIXEL //"DESCRIÇÃO"
	@ 013,100 MSGET ODESCD  VAR CDESCD        SIZE 200,008 OF ODLG PIXEL WHEN .F.
	@ 030,005 SAY OEMTOANSI(STR0052  ) SIZE 100,030 OF ODLG PIXEL //"PARA RECURSO"
	@ 038,005 MSGET OFROTAP VAR CFROTAP       SIZE 080,008 OF ODLG PIXEL F3 CF3 VALID(FVERFROTA(CFROTAD,CFROTAP,@CDESCP,ODESCP,CAS))
	@ 030,100 SAY OEMTOANSI(STR0051     ) SIZE 100,030 OF ODLG PIXEL //"DESCRIÇÃO"
	@ 038,100 MSGET ODESCP  VAR CDESCP        SIZE 200,008 OF ODLG PIXEL WHEN .F.
	@ 070,120 BUTTON STR0053       SIZE 50,15 ACTION ( IIF(FTROCAFRO(CFROTAD,CFROTAP,DDATAD,DDATAA,OFROTAP,CAS), CLOSE(ODLG), )) OF ODLG PIXEL //"OK"
	@ 070,180 BUTTON STR0016 SIZE 50,15 ACTION CLOSE(ODLG) OF ODLG PIXEL //"CANCELAR"
	ACTIVATE MSDIALOG ODLG CENTERED

RETURN NIL

/*/{PROTHEUS.DOC} FVERFROTA
ITUP BUSINESS - TOTVS RENTAL
VALIDACAO NA TROCA DO EQUIPAMENTO
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FVERFROTA(CFROTAD,CFROTAP,CDESCP,ODESCP,CAS)
LOCAL LRET := .T.

	FTRAZDESC(CFROTAP,@CDESCP,ODESCP) 			// PREENCHE O CAMPO COM O NOME DO BEM 

	IF ! EMPTY(CFROTAP) 
		IF !FVERSIGLAS({CFROTAD,CFROTAP},CAS) 
			LRET := .F. 
		ENDIF 
	ENDIF 

RETURN LRET 

/*/{PROTHEUS.DOC} FMONTAZBX
ITUP BUSINESS - TOTVS RENTAL
MONTAGEM DA QUERY USADA NAS DIVERSAS FUNCIONALIDADES DA MINUTA
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FMONTAZBX(CALIASQRY,CPERG) 
Local AESTRU , CQRY := "" 
Local NPOS := 0 
Local nx   := 0 
Local _cCampos
Local aBindParam

	_cCampos := "FPF_MINUTA,FPF_AS,FPF_DATA,FPF_FROTA,FPF_PROJET,FPF_OBRA,FPF_NROMED,FPF_HORAI,FPF_HORAF,FPF_HRPARA,"
	_cCampos += "FPF_MOTIVO,FPF_STATUS,FPF_MINUTM,FPF_FILIAL,FPF_RECNO,FQ5_HORINI,FQ5_HORFIM"
	
	if Type("ACPOSIM") = 'A'
		
		For nx := 1 To len(ACPOSIM) 
			If !Alltrim(ACPOSIM[nx]) $ (_cCampos) 
				_cCampos += ","+Alltrim(ACPOSIM[nx]) 
			EndIf 
		Next 

	endif

	aBindParam := {}
	cQry += " SELECT "
	cQry += " FPF_MINUTA, FPF_AS, FPF_DATA, FPF_FROTA, FPF_PROJET, FPF_OBRA," 
	cQry += " FPF_NROMED, FPF_HORAI, FPF_HORAF, FPF_HRPARA, FPF_MOTIVO, FPF_STATUS, FPF_MINUTM," 
	cQry += " FPF_FILIAL, ZBX.R_E_C_N_O_ AS FPF_RECNO," 
	cQry += " FQ5_HORINI, FQ5_HORFIM" 
	cQry += " FROM " + RETSQLNAME("FPF") + " ZBX" 
	cQry += " JOIN " + RETSQLNAME("FQ5") + " DTQ ON FQ5_FILIAL='"+XFILIAL("FQ5")+"' AND FQ5_AS=FPF_AS AND DTQ.D_E_L_E_T_=' '"
	cQry += " WHERE ZBX.D_E_L_E_T_=''" 
	cQry += " AND FPF_FILIAL = '" + XFILIAL("FPF") + "'" 
	do Case
	Case FwIsInCallStack("LOCA00504") //ACEITA MINUTA //UPPER(CPERG)==UPPER("LOCA00504")
		cQry += " AND FPF_STATUS = '1' " // 1=PREVISTA,2=CONFIRMADA,3=BAIXADA,4=ENCERRADA,5=CANCELADA,6=MEDIDA
	Case FwIsInCallStack("LOCA00505") //BAIXA MINUTA //UPPER(CPERG)==UPPER("LOCA00505")
		cQry += " AND FPF_STATUS = '2' " 	// 1=PREVISTA,2=CONFIRMADA,3=BAIXADA,4=ENCERRADA,5=CANCELADA,6=MEDIDA
	Case FwIsInCallStack("LOCA00506") //CANCELA MINUTA //UPPER(CPERG)==UPPER("LOCA00506")
		cQry += " AND FPF_STATUS = '1' " //AND FPF_MOTCAN = ? "
		//aadd(aBindParam, cMoti )
	Case FwIsInCallStack("LOCA00507") //"ESTORNA MINUTA" //UPPER(CPERG)==UPPER("LOCA00507")
		cQry += " AND FPF_STATUS IN ('2','3','4','5','6')"  
	EndCase
	If nRecNoatu == 0
		cQry += " AND FPF_PROJET BETWEEN ? AND ? " 
		aadd(aBindParam, cProd )
		aadd(aBindParam, cProa )
		cQry += " AND FPF_OBRA BETWEEN ? AND ? "   
		aadd(aBindParam, cObrd )
		aadd(aBindParam, cObra )
		cQry += " AND FPF_AS BETWEEN ? AND ? "     
		aadd(aBindParam, cNasd )
		aadd(aBindParam, cNasa )
		cQry += " AND FPF_FROTA BETWEEN ? AND ? "  
		aadd(aBindParam, cFrod ) 
		aadd(aBindParam, cFroa ) 
		cQry += " AND FPF_DATA BETWEEN ? AND ? "   
		aadd(aBindParam, dtos(dDatd) ) 
		aadd(aBindParam, dtos(dData) ) 
		If FwIsInCallStack("LOCA00505") //BAIXA MINUTA //UPPER(CPERG)==UPPER("LOCA00505")
			cQry += " AND FPF_TURNO BETWEEN ? AND ? "   
			aadd(aBindParam, cTurD ) 
			aadd(aBindParam, cTurA ) 
		EndIf
	ELSE
		cQry += " AND ZBX.R_E_C_N_O_ = ? "
		aadd(aBindParam, alltrim(str(nRecNoatu)) )  
	ENDIF
	cQry += " ORDER BY FPF_AS,FPF_DATA " 
	cQry := CHANGEQUERY(CQRY)

	If !SELECT(CALIASQRY)==0 
		(CALIASQRY)->(DBCLOSEAREA())  
	EndIf

	MPSysOpenQuery(cQry, cAliasQry, , ,aBindParam)

	DBSELECTAREA(cAliasQry)

	AESTRU := FPF->(DBSTRUCT())
	FOR NPOS:=1 TO LEN(AESTRU)
		IF AESTRU[NPOS,2]<>"C" .AND. AESTRU[NPOS,2]<>"M"
			If alltrim(AESTRU[NPOS,1]) $ _cCampos
				TCSETFIELD(CALIASQRY,AESTRU[NPOS,1],AESTRU[NPOS,2],AESTRU[NPOS,3],AESTRU[NPOS,4])
			ENDIF
		ENDIF
	NEXT NPOS 

RETURN

/*/{PROTHEUS.DOC} LOCA00509
ITUP BUSINESS - TOTVS RENTAL
VALIDACAO NA SELECAO DAS MINUTAS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00509(OBROWSE)
Local LRET

	IF EMPTY(OBROWSE:DATA():AARRAY[OBROWSE:AT(),NPOSMAR])
		OBROWSE:DATA():AARRAY[OBROWSE:AT(),NPOSMAR]:=CMARCA
	ELSE
		OBROWSE:DATA():AARRAY[OBROWSE:AT(),NPOSMAR]:=SPACE(01)
	ENDIF

	LRET := LOCA00510(OBROWSE)

RETURN(LRET)


/*/{PROTHEUS.DOC} LOCA00510
ITUP BUSINESS - TOTVS RENTAL
VALIDACAO NA SELECAO DAS MINUTAS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00510(OBROWSE)
Local LRET

	LRET := (!EMPTY(OBROWSE:DATA():AARRAY[OBROWSE:AT(),NPOSMAR])) 

RETURN(LRET)

/*/{PROTHEUS.DOC} LOCA00511
ITUP BUSINESS - TOTVS RENTAL
EXECUCAO NO CLIQUE DO CABEÇALHO NAS ROTINAS DE SELEÇÃO DAS MINUTAS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00511(OBROWSE)
Local NPOS

	FOR NPOS:=1 TO LEN(OBROWSE:DATA():AARRAY)
		
		IF EMPTY(OBROWSE:DATA():AARRAY[NPOS,NPOSMAR])
			OBROWSE:DATA():AARRAY[NPOS,NPOSMAR]:=CMARCA
		ELSE
			OBROWSE:DATA():AARRAY[NPOS,NPOSMAR]:=SPACE(01)
		ENDIF

	NEXT

	OBROWSE:REFRESH()

RETURN

/*/{PROTHEUS.DOC} MUDATOT
ITUP BUSINESS - TOTVS RENTAL
EXECUCAO NO CLIQUE DO CABEÇALHO NO ACEITE DA MINUTA
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION MUDATOT( OOBJ , NCOL , OGETD )
Local CSEL
Local N       := 1

Private ACOLS   := OGETD:ACOLS
Private AHEADER := OGETD:AHEADER

STATIC  NSECOND := 0

	IF INT( NSECOND - SECONDS() ) == 0
		RETURN .F.
	ENDIF

	IF NCOL == 1

		FOR N := 1 TO LEN( ACOLS )
			//ifranzoi - 01/07/2021
			CSEL := IIF( GDFIELDGET("SELECAO",N) == "LBOK ", "LBNO ", "LBOK " )
			GDFIELDPUT("SELECAO" , CSEL, N) 
		NEXT N 

	ELSE

		ASORT(ACOLS,,,{|_A1,_A2| _A1[NCOL] <= _A2[NCOL] })

	ENDIF

	OGETD:REFRESH()

	NSECOND := SECONDS()

RETURN .T.

/*/{PROTHEUS.DOC} TOKACEIT
ITUP BUSINESS - TOTVS RENTAL
VALIDA SE EXISTEM CONFLITOS NA PROGRAMAÇÃO DAS FROTAS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION TOKACEIT( OGETD, ODLG )
Local LRET     := .T.
Local NI, NJ, CMSG
Local NSELECTS := 0

Private ACOLS    := OGETD:ACOLS
Private AHEADER  := OGETD:AHEADER

	FQ5->( DBSETORDER(9) )

	FOR NI := 1 TO LEN( ACOLS )

		IF ALLTRIM(GDFIELDGET("SELECAO", NI)) == "LBOK" //"SELECAO"

			FQ5->( DBSEEK( XFILIAL("FQ5") + GDFIELDGET("FPF_AS", NI), .T. ) )
			NSELECTS++

			FOR NJ := 1 TO LEN( ACOLS )
				IF ALLTRIM(GDFIELDGET("SELECAO", NJ)) == "LBOK" .AND. NI != NJ //"SELECAO"
					IF ! EMPTY( GDFIELDGET("FPF_FROTA",NI) ) .AND. ;
						GDFIELDGET("FPF_DATA",NI) == GDFIELDGET("FPF_DATA",NJ) .AND. ;
						GDFIELDGET("FPF_FROTA",NI) == GDFIELDGET("FPF_FROTA",NJ) .AND. ;
							FVALID( GDFIELDGET("FPF_HORAI",NI), GDFIELDGET("FPF_HORAF",NI), GDFIELDGET("FPF_HORAI",NJ), GDFIELDGET("FPF_HORAF",NJ) ) .AND. ;
							FQ5->FQ5_TPAS != "F"

						CMSG := STR0055+ALLTRIM(GDFIELDGET("FPF_DATA",NI)) //"AS PROGRAMAÇÕES DA FROTA: "
						CMSG += " EM "+DTOC(GDFIELDGET("FPF_DATA",NJ))
						CMSG += STR0056 + GDFIELDGET("FPF_MINUTA",NI) + " E " + GDFIELDGET("FPF_MINUTA",NJ) //", MINUTAS "
						CMSG += STR0057 //" ESTÃO EM CONFLITO, VERIFIQUE!"

						AVISO( STR0058, CMSG, {"OK"}, 2 ) //"CONFLITO DE PROGRAMAÇÃO!"
						RETURN NIL
					ENDIF
				ENDIF
			NEXT NJ 

		ENDIF

	NEXT NI 

	IF NSELECTS == 0
		MSGALERT(STR0059 , STR0002) //"NÃO HÁ MINUTAS SELECIONADAS"###"Rental"
		RETURN NIL
	ENDIF

	IF LRET
		LRET := FACEITM2()
	ENDIF

	IF LRET
		ODLG:END()
	ENDIF

RETURN NIL 

/*/{PROTHEUS.DOC} FVALID
ITUP BUSINESS - TOTVS RENTAL
VALIDA SE EXISTE CONFLITO ENTRE OS INTERVALOS DE HORAS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FVALID( CINIX, CFIMX, CINIY, CFIMY )
Local BENTRE := { |XELEM, XINI, XFIM|  XELEM > XINI .AND. XELEM < XFIM } 

	IF EVAL( BENTRE,  CINIX, CINIY, CFIMY )		// CHINIX ESTÁ ENTRE CHINIY E CHFIMY ?
		RETURN .T.
	ENDIF

	IF EVAL( BENTRE,  CFIMX, CINIY, CFIMY )		// CHFIMX ESTÁ ENTRE CHINIY E CHFIMY ?
		RETURN .T.
	ENDIF

	IF EVAL( BENTRE,  CINIY, CINIX, CFIMX )		// CHINIY ESTÁ ENTRE CHINIX E CHFIMX ?
		RETURN .T.
	ENDIF

	IF EVAL( BENTRE,  CFIMY, CINIX, CFIMX )		// CHFIMY ESTÁ ENTRE CHINIX E CHFIMX ?
		RETURN .T.
	ENDIF

RETURN .F.

/*/{PROTHEUS.DOC} MUDASEL
ITUP BUSINESS - TOTVS RENTAL
CONTROLE SE MUDA A SELEÇÃO [MARCADO/DESMARCADO] OU EDITA A CÉLULA
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION MUDASEL( OGETD )
Private ACOLS   := OGETD:ACOLS
Private AHEADER := OGETD:AHEADER
Private N       := OGETD:NAT

	IF ! GDDELETED( N )
		IF ALLTRIM(AHEADER[OGETD:OBROWSE:COLPOS, 2]) $ cEdicao
			OGETD:EDITCELL()
		ELSE
			//ifranzoi - 01/07/2021
			GDFIELDPUT("SELECAO", IIF(GDFIELDGET("SELECAO") == "LBOK ", "LBNO ", "LBOK "), N ) 
		ENDIF
	ENDIF

RETURN NIL

/*/{PROTHEUS.DOC} ISHORA
ITUP BUSINESS - TOTVS RENTAL
VALIDAÇÃO SE O PARÂMETRO É UMA HORA VÁLIDA
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION ISHORA( CPARHORA, LQUIET )
Local CHORA  := ALLTRIM( CPARHORA )
Local NHORA, NMIN

DEFAULT LQUIET := .F.

	CHORA := STRTRAN( CHORA, ":", "" )

	IF LEN( CHORA ) > 4
		CHORA := LEFT( CHORA, 4 )
	ENDIF

	IF LEN( CHORA ) != 4
		IF ! LQUIET
			MSGALERT( STR0061 , STR0002) //"FORMATO DE HORA INVÁLIDO, VERIFIQUE!"###"Rental"
		ENDIF
		RETURN .F.
	ENDIF

	NHORA := VAL( LEFT(  CHORA, 2 ) )
	NMIN  := VAL( RIGHT( CHORA, 2 ) )

	IF NHORA > 24 .OR. NHORA < 0
		IF ! LQUIET
			MSGALERT( STR0061 , STR0002 ) //"FORMATO DE HORA INVÁLIDO, VERIFIQUE!"###"Rental"
		ENDIF
		RETURN .F.
	ENDIF

	IF NMIN > 59 .OR. NMIN < 0 .OR. ( NHORA==24 .AND. NMIN > 0 )
		IF ! LQUIET
			MSGALERT( STR0061 , STR0002 ) //"FORMATO DE HORA INVÁLIDO, VERIFIQUE!"###"Rental"
		ENDIF
		RETURN .F.
	ENDIF

	CPARHORA := CHORA		// CASO SEJA PASSADO O PARÂMETRO COM @, GUARDA VALOR NORMALIZADO

RETURN .T.

/*/{PROTHEUS.DOC} LOCA00512
ITUP BUSINESS - TOTVS RENTAL
VALIDAÇÃO DO LANÇAMENTO DAS HORAS INICIAL E FINAL
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00512()
Local XVAR  := READVAR()
Local CVAL  := ALLTRIM( &(XVAR) )

	IF EMPTY( CVAL )
		RETURN .T.
	ENDIF

	IF ! ISHORA( @CVAL )
		RETURN .F.
	ENDIF

	IF XVAR == "M->FPF_HORAI"
		COUTRAH := ALLTRIM( GDFIELDGET("FPF_HORAF") )
		IF ! EMPTY( COUTRAH )
			IF CVAL > COUTRAH
				MSGALERT( STR0062 , STR0002 ) //"HORA INICIAL NÃO PODE SER MAIOR QUE HORA FINAL, VERIFIQUE!"###"Rental"
				RETURN .F.
			ENDIF
		ENDIF
	ELSE
		COUTRAH := ALLTRIM( GDFIELDGET("FPF_HORAI") )
		IF ! EMPTY( COUTRAH )
			IF CVAL < COUTRAH
				MSGALERT( STR0063 , STR0002 ) //"HORA FINAL NÃO PODE SER MENOR QUE HORA INICIAL, VERIFIQUE!"###"Rental"
				RETURN .F.
			ENDIF
		ENDIF
	ENDIF

RETURN .T.

/*/{PROTHEUS.DOC} FACEITM2
ITUP BUSINESS - TOTVS RENTAL
ACEITE DAS MINUTAS COM DATA/HORA
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FACEITM2()
Local AAREA     := GETAREA()
Local NGRAVADOS := 0
Local CMSG      := ""
Local NI        := 0 
Local CTITULO   := ""
Local CGRPAND   := ""
Local nX :=0
Local _ZBXACVLD := EXISTBLOCK("ZBXACVLD")

	IF SBM->(FIELDPOS("BM_XACESS")) > 0
		CGRPAND := LOCA00189()
	ELSE
		CGRPAND := SUPERGETMV("MV_LOCX014",.F.,"")
	ENDIF

	IF ! MSGYESNO(STR0064,STR0002) //"CONFIRMA O ACEITE DAS MINUTAS SELECIONADAS ??"###"Rental"
		RETURN .F.
	ENDIF

	FOR NI := 1 TO LEN( ACOLS )

		IF ALLTRIM(GDFIELDGET("SELECAO", NI)) == "LBOK"		
			LGRAVA := .F.

			DBSELECTAREA("FQ5")
			FQ5->(DBCLEARFILTER())
			FQ5->(DBSETFILTER({|| ALLTRIM(FQ5->FQ5_AS) == ALLTRIM(GDFIELDGET('FPF_AS', NI)) .AND. FQ5_FILIAL =XFILIAL("FQ5") },"ALLTRIM(FQ5->FQ5_AS) == ALLTRIM(GDFIELDGET('FPF_AS', NI)) .AND. FQ5_FILIAL =XFILIAL('FQ5')"))
			FQ5->(DBGOTOP())
				
			IF EMPTY(FQ5->FQ5_CONTRA)	
				CMSG += STR0065+STRZERO(NI,3)+STR0066+ALLTRIM( GDFIELDGET("FPF_AS", NI) ) + STR0067 + CRLF //"LINHA: "###" - AS: "###", NÃO ENCONTRADA!"
				FQ5->(DBCLEARFILTER())
			ELSE
				FQ5->(DBCLEARFILTER())
				
				FPF->( DBGOTO( GDFIELDGET("RECORD", NI) ) )

				DO CASE
				CASE FPF->FPF_TIPOSE == "E" 				// EQUIPAMENTO
				CASE FPF->FPF_TIPOSE == "T" 				// TRANSPORTE

				CASE FPF->FPF_TIPOSE == "M" .Or. FPF->FPF_TIPOSE == "L"	// MAO DE OBRA // Locação
					LGRAVA  := .F.
					FQ5->( DBSETORDER(9) )	// FILIAL + AS + VIAGEM
					IF FQ5->( DBSEEK( XFILIAL("FQ5") + FPF->FPF_AS, .T. ) )
						CTITULO := STR0068+FPF->FPF_MINUTA+STR0069+DTOC( FPF->FPF_DATA ) //"MINUTA: "###" EM "
						LGRAVA  := .T.
					ENDIF

				ENDCASE

			ENDIF

			IF LGRAVA
				FPF->( DBGOTO( GDFIELDGET("RECORD", NI) ) )
				IF _ZBXACVLD //EXISTBLOCK("ZBXACVLD")	// PONTO DE ENTRADA PARA INCLUSAO DE BOTOES NAS ACOES RELACIONADAS DA BAIXA DA MINUTA
					LGRAVA := EXECBLOCK("ZBXACVLD",.T.,.T.,{GDFIELDGET("FPF_DATA", NI), GDFIELDGET("FPF_DATA", NI), GDFIELDGET("FPF_STATUS", NI), GDFIELDGET("FPF_HORAI", NI), GDFIELDGET("FPF_HORAF", NI)})
				ENDIF
				IF LGRAVA
					NGRAVADOS++
					IF RECLOCK("FPF", .F.)
						for nX := 1 to Len(aAlter)
							FPF->(FieldPut(FieldPos(aAlter[nX]), GdFieldGet( aAlter[nX], nI)))
						next nX
						FPF->FPF_STATUS := "2"  //1=PREVISTA,2=CONFIRMADA,3=BAIXADA,4=ENCERRADA,5=CANCELADA,6=MEDIDA
						FPF->FPF_DTOCOR := DDATABASE
						FPF->(MSUNLOCK())
					ENDIF
				ENDIF
			ENDIF

		ENDIF
	NEXT

	IF !EMPTY( CMSG )
		AVISO(STR0070, CMSG, {"OK"}, 3) //"OCORRÊNCIAS NO ACEITE DA MINUTA"
	ENDIF

	IF NGRAVADOS > 0
		MSGALERT(STR0071 , STR0002) //"ACEITE DE MINUTAS EXECUTADO COM SUCESSO."###"Rental"
	ELSE
		MSGALERT(STR0072 , STR0002) //"NENHUM ACEITE FOI EXECUTADO."###"Rental"
		RESTAREA(AAREA) 
		RETURN .F.
	ENDIF

	RESTAREA(AAREA) 

RETURN .T. 


/*/{PROTHEUS.DOC} FBAIXAM
ITUP BUSINESS - TOTVS RENTAL
VALIDACAO NA BAIXA DAS MINUTAS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FBAIXAM(OBROWSE,CHORAI,CHORAF,CHORAP,CMOTIP,CMINUM, CHALMI, CHALMF, CHJANI, CHJANF, COBS)
Local NPOS , NPOSAUX, ATABAUX  
Local AAREAZBX  := FPF->(GETAREA()) 
Local NGRAVADOS := 0
Local _ZBXBXFIM := EXISTBLOCK("ZBXBXFIM") 

Private cErros := ""
Private _CposAdic := {}

	DO CASE
	CASE EMPTY(CHORAI) .OR. EMPTY(CHORAF)
		MsgAlert(STR0073,STR0002) //"FAVOR INFORMAR HORA INICIAL E HORA FINAL !!"###"Rental")
		RETURN(.F.)
	CASE (EMPTY(CHORAP) .OR. CHORAP == "00:00") .AND. !EMPTY(CMOTIP) .AND. ALLTRIM(CHORAP) <> ":"
		MsgAlert(STR0075,STR0002) //'FAVOR INFORMAR O CAMPO HORA PARADA"###"Rental"
		RETURN(.F.)
	CASE !EMPTY(CHORAP) .AND. CHORAP != "00:00" .AND.  EMPTY(CMOTIP) .AND. ALLTRIM(CHORAP) <> ":"
		MsgAlert(STR0076,STR0002) //"FAVOR INFORMAR MOTIVO PARADA !!"###"Rental")
		RETURN(.F.)
	ENDCASE

	ATABAUX := OBROWSE:DATA():AARRAY
	NPOSAUX := 0
	AEVAL(ATABAUX,{|X|IF(X[1]==CMARCA,NPOSAUX++,)})

	DO CASE
	CASE NPOSAUX == 0
		MSGALERT(STR0077 , STR0002) //"ATENÇÃO ==> FAVOR SELECIONAR PELO MENOS UMA MINUTA."###"Rental"
		RETURN(.F.)
	ENDCASE

	IF EXISTBLOCK("ZBXBXVLD")	// PONTO DE ENTRADA PARA VALIDAR A BAIXA DA MINUTA
		IF !EXECBLOCK("ZBXBXVLD",.T.,.T.,{ATABAUX, ARECNOSZBX})
			RETURN(.F.)
		ENDIF
	ENDIF


	IF ! MSGYESNO(STR0078,STR0002) //"CONFIRMA A BAIXA DAS MINUTAS SELECIONADAS ?"###"Rental"
		RETURN(.F.)
	ENDIF

	FPF->(DBSETORDER(2))  						// 2=FPF_FILIAL+FPF_MINUTA

	FOR NPOS:=1 TO LEN(ATABAUX)
		IF ATABAUX[NPOS,NPOSMAR]==CMARCA
			IF FPF->(DBSEEK(XFILIAL("FPF") + ATABAUX[NPOS,NPOSMIN]))
				BEGIN TRANSACTION
					IF LOCA00513( FPF->FPF_MINUTA, CHORAI, CHORAF, CHORAP, CMOTIP, CMINUM, CHALMI, CHALMF, CHJANI, CHJANF, COBS, "" /*CMSGRET*/, .T. /*LPOSIC*/ )	// ATUALIZA ZBX-MINUTA
						NGRAVADOS++
						OBROWSE:DATA():AARRAY[NPOS,NPOSSTA] := FPF->FPF_STATUS
						OBROWSE:DATA():AARRAY[NPOS,NPOSHOI] := STRTRAN(CHORAI, ":", "" )
						OBROWSE:DATA():AARRAY[NPOS,NPOSHOF] := STRTRAN(CHORAF, ":", "" )
						OBROWSE:DATA():AARRAY[NPOS,NPOSHOP] := STRTRAN(CHORAP, ":", "" )
						OBROWSE:DATA():AARRAY[NPOS,NPOSMOT] := STRTRAN(CMOTIP, ":", "" )
						OBROWSE:DATA():AARRAY[NPOS,NPOSMIM] := CMINUM
						OBROWSE:DATA():AARRAY[NPOS,NPOSALI] := STRTRAN(CHALMI, ":", "" )
						OBROWSE:DATA():AARRAY[NPOS,NPOSALF] := STRTRAN(CHALMF, ":", "" )
						OBROWSE:DATA():AARRAY[NPOS,NPOSJAI] := STRTRAN(CHJANI, ":", "" )
						OBROWSE:DATA():AARRAY[NPOS,NPOSJAF] := STRTRAN(CHJANF, ":", "" )
						OBROWSE:DATA():AARRAY[NPOS,NPOSOBS] := COBS
						IF _ZBXBXFIM //EXISTBLOCK("ZBXBXFIM")	// PONTO DE ENTRADA NO FINAL DA BAIXA DA MINUTA
							EXECBLOCK("ZBXBXFIM",.T.,.T.,{FPF->FPF_MINUTA, OBROWSE})
						ENDIF
					ENDIF
				END TRANSACTION
			ENDIF
		ENDIF
	NEXT

	If !empty(cErros)
		MsgAlert(cErros,STR0119) // Atenção!
	EndIf

	OBROWSE:REFRESH(.T.)
	OBROWSE:SETFOCUS()

	IF NGRAVADOS>0
		MSGINFO(STR0079 , STR0002) //"BAIXA EXECUTADA COM SUCESSO."###"Rental"
	ELSE
		MSGALERT(STR0080 , STR0002) //"NENHUMA BAIXA FOI EXECUTADA."###"Rental"
	ENDIF

	RESTAREA(AAREAZBX)

RETURN(.T.) 

/*/{PROTHEUS.DOC} LOCA00513
ITUP BUSINESS - TOTVS RENTAL
ROTINA QUE REALIZA A BAIXA DA MINUTA
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00513(CMINUTA, CHORAI, CHORAF, CHORAP, CMOTIP, CMINUM, CHALMI, CHALMF, CHJANI, CHJANF, COBS, CMSGRET, LPOSIC)
Local LRET := .T.
Local l4035 := .F. // Card 4035 - Frank 24/09/2024 - Preparação Minuta x Medição
Local nTemp1 := 0
Local nTemp2 := 0

DEFAULT LPOSIC := .F.

	CMSGRET := ""

	IF ! LPOSIC
		FPF->( DBSETORDER() )
		IF ! FPF->( DBSEEK( XFILIAL("FPF") + CMINUTA ) )
			CMSGRET := STR0068+CMINUTA+STR0081 //"MINUTA: "###" NAO ENCONTRADA!"
			RETURN .F.
		ENDIF
	ENDIF

	// Card 4035 - Frank em 24/09/2024
	If FindFunction( "LOCA224B1" )
		IF FPF->(FIELDPOS("FPF_HTB")) > 0 .and. FPF->(FIELDPOS("FPF_HTR")) > 0 
	        If LOCA224B1("FPF_HTB", "FPF") .and. LOCA224B1("FPF_HTR", "FPF")
				l4035 := .T.
			EndIf
		EndIf
	EndIf

	// Card 4369 - Frank em 27/12/24
	If !LOCA005VLD(CHORAI, CHORAF)
		Return .F.
	EndIf

	IF FPF->( RECLOCK("FPF", .F. ) )
		FPF->FPF_FILIAL := XFILIAL("FPF")
		FPF->FPF_HREALI := STRTRAN(CHORAI,":","")
		FPF->FPF_HREALF := STRTRAN(CHORAF,":","")
		FPF->FPF_HRPARA := STRTRAN(CHORAP,":","")
		FPF->FPF_MOTIVO := CMOTIP
		FPF->FPF_MINUTM := CMINUM
		FPF->FPF_BXALMI := STRTRAN(CHALMI,":","")
		FPF->FPF_BXALMF := STRTRAN(CHALMF,":","")
		FPF->FPF_BXJANI := STRTRAN(CHJANI,":","")
		FPF->FPF_BXJANF := STRTRAN(CHJANF,":","")
		FPF->FPF_BXOBS  := COBS
		FPF->FPF_STATUS := "3"  //1=PREVISTA,2=CONFIRMADA,3=BAIXADA,4=ENCERRADA,5=CANCELADA,6=MEDIDA
		FPF->FPF_DTOCOR := DDATABASE

		// Card 4035 - Frank em 24/09/2024
		If l4035
			FPA->(dbSetOrder(3))
			If FPA->(dbSeek(xFilial("FPA")+FPF->FPF_AS))

				If cHoraF == "00:00"
					cHoraF := "24:00"
				EndIf
				If cHoraI == "00:00"
					cHoraI := "24:00"
				EndIf

				If cHoraF > cHoraI
					nTemp1 := val(strtran(CHORAF,":","")) - val(strtran(CHORAI,":",""))
					nTemp1 := strzero(nTemp1,4,0)
				ElseIf cHoraF == cHoraI
					nTemp1 := "2400"
				ElseIf cHoraF < cHoraI
					nTemp1a := 2400 - val(strtran(CHORAI,":",""))
					nTemp1b := val(strtran(CHORAF,":",""))
					nTemp1 := strzero(nTemp1a+nTemp1b,4,0)
				EndIf

				//nTemp1 := val(strtran(CHORAF,":","")) - val(strtran(CHORAI,":",""))
				//nTemp1 := strzero(nTemp1,4,0)
				FPF->FPF_HTB := nTemp1
				nTemp1 := strzero(val(nTemp1) - val(strtran(cHorap,":","")),4,0)
				nTemp2 := strzero(FPA->FPA_MINDIA,2)+":00"
				nTemp2 := val(strtran(nTemp2,":","")) - val(strtran(CHORAP,":",""))
				nTemp2 := strzero(nTemp2,4,0)
				FPF->FPF_HTR := If(nTemp1>=nTemp2,nTemp1,nTemp2)
				FPF->FPF_MINDIA := FPA->FPA_MINDIA
			EndIf
		EndIf

		FPF->(MSUNLOCK())
		LRET    := .T.
	ELSE
		CMSGRET := STR0082 //"NAO FOI POSSIVEL GRAVAR AS INFORMACOES"
		LRET    := .F.
	ENDIF

RETURN LRET

/*/{PROTHEUS.DOC} FVERMOTCAN
ITUP BUSINESS - TOTVS RENTAL
VALIDACAO NO MOTIVO DO CANCELAMENTO
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FVERMOTCAN(CMOTCAN , CMOTCAND , OMOTCAND) 
Local LRET := .T.

	CMOTCAND := POSICIONE("TPJ",1,XFILIAL("TPJ")+CMOTCAN,"TPJ_DESMOT")  //POSICIONA NO MOTIVO DO CANCELAMENTO

	IF VALTYPE(OMOTCAND)=="O"
		OMOTCAND:REFRESH()
	ENDIF

	IF EMPTY(CMOTCAND)
		MSGALERT(STR0083 , STR0002) //"MOTIVO DO CANCELAMENTO NÃO ENCONTRADO."###"Rental"
		LRET:=.F.
	ENDIF

RETURN(LRET)

/*/{PROTHEUS.DOC} FCANCELAM
ITUP BUSINESS - TOTVS RENTAL
VALIDACAO NO CANCELAMENTO DA MINUTA
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FCANCELAM(OBROWSE,CMOTCAN)
Local NPOS , NPOSAUX , ATABAUX 
Local AAREAZBX  := FPF->(GETAREA())
Local NGRAVADOS := 0 
Local _ZBXCANMI := EXISTBLOCK("ZBXCANMI")
Local CQRY

	DO CASE
	CASE EMPTY(CMOTCAN)
		MSGALERT(STR0084 , STR0002) //"FAVOR INFORMAR O MOTIVO DO CANCELAMENTO."###"Rental"
		RETURN(.F.)
	CASE CMOTCAN == "03"
		MSGALERT(STR0085 , STR0002) //'MOTIVO "03 - AS CANCELADA" NÃO PODE SER UTILIZADA EM CANCELAMENTOS MANUAIS.'###"Rental"
		RETURN(.F.)	
	ENDCASE

	ATABAUX := OBROWSE:DATA():AARRAY
	NPOSAUX := 0
	AEVAL(ATABAUX,{|X|IF(X[1]==CMARCA,NPOSAUX++,)})

	DO CASE
	CASE NPOSAUX==0
		MSGALERT(STR0086 , STR0002) //"ATENÇÃO ==> FAVOR SELECIONAR PELO MENOS UMA MINUTA."###"Rental"
		RETURN(.F.)
	ENDCASE

	IF !MSGYESNO(STR0087 , STR0002)  //"CONFIRMA O CANCELAMENTO DAS MINUTAS SELECIONADAS ??"###"Rental"
		RETURN(.F.)
	ENDIF

	FPF->(DBSETORDER(2))  //2=FPF_FILIAL+FPF_MINUTA

	FOR NPOS:=1 TO LEN(ATABAUX)
		IF ATABAUX[NPOS,NPOSMAR]==CMARCA

			IF FPF->(DBSEEK(XFILIAL("FPF") + ATABAUX[NPOS,NPOSMIN]))		
				// QUERY PARA VERIFICAR SE HOUVE MEDICOES - INICIO
				_LPODECANC := .T.

				/*
				+FPF->FPF_MINUTA+
				*/

				CQRY := "SELECT COUNT(*) AS TOT "
				CQRY += " FROM " + RETSQLNAME("FPP") + " ZLM"
				CQRY += " WHERE FPP_FILIAL = '" + XFILIAL("FPP") + "'"
				CQRY += " AND FPP_MINUTA = ? "
				CQRY += " AND FPP_DTMEDI <> '' "
				CQRY += " AND D_E_L_E_T_ = '' "
				CQRY := CHANGEQUERY(CQRY)
				aBindParam := {FPF->FPF_MINUTA}
				IF !SELECT("QZLM")==0 
					QZLM->(DBCLOSEAREA()) 
				ENDIF
				MPSysOpenQuery(CQRY,"QZLM",,,aBindParam)
			
				//DBUSEAREA(.T., "TOPCONN", TCGENQRY(,,CQRY), "QZLM", .F., .T.)
				IF QZLM->TOT > 0
					_LPODECANC := .F.
				ENDIF	
				QZLM->(DBCLOSEAREA())
				// QUERY PARA VERIFICAR SE HOUVE MEDICOES - FINAL 
				
				IF _LPODECANC
					IF FPF->(RECLOCK("FPF",.F.))
						FPF->FPF_STATUS := "5"  //1=PREVISTA,2=CONFIRMADA,3=BAIXADA,4=ENCERRADA,5=CANCELADA,6=MEDIDA
						FPF->FPF_MOTCAN := CMOTCAN  //MOTIVO DO CANCELAMENTO
						FPF->FPF_DTOCOR := DDATABASE
						FPF->FPF_FROTA := "" // LIMPA O CAMPO FROTA
						FPF->(MSUNLOCK())
		
						FKILLPROG( FPF->FPF_TIPOSE, FPF->FPF_MINUTA )	// REMOVE PROGRAMACAO
		
						IF _ZBXCANMI //EXISTBLOCK("ZBXCANMI")	// PONTO DE ENTRADA NO CANCELAMENTO DA MINUTA
							EXECBLOCK("ZBXCANMI",.T.,.T.,{FPF->FPF_MINUTA, FPF->FPF_STATUS, FPF->FPF_AS})
						ENDIF
						
						NGRAVADOS++
						OBROWSE:DATA():AARRAY[NPOS,NPOSSTA]:=FPF->FPF_STATUS
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	NEXT NPOS

	OBROWSE:REFRESH(.T.)
	OBROWSE:SETFOCUS()

	IF NGRAVADOS>0
		MSGALERT(STR0088 , STR0002) //"CANCELAMENTO EXECUTADO COM SUCESSO."###"Rental"
	ELSE
		MSGALERT(STR0089 , STR0002) //"NENHUM CANCELAMENTO FOI EXECUTADO."###"Rental"
	ENDIF

	RESTAREA(AAREAZBX)

RETURN(.T.) 

/*/{PROTHEUS.DOC} FESTORNAM
ITUP BUSINESS - TOTVS RENTAL
VALIDACAO NO ESTORNO DA MINUTA
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FESTORNAM(OBROWSE)
Local AVERZBX 
Local AVERZLM 
Local NPOS,NPOSAUX,ATABAUX
Local AAREAZBX := FPF->(GETAREA())
Local CSTATUS
Local NLIDOS := 0 , NGRAVADOS := 0 
Local _ZBXESTVL := EXISTBLOCK("ZBXESTVL")
Local _ZBXESTMI := EXISTBLOCK("ZBXESTMI")
Local _CQRYZLG
Local l4035 := .T.

	If FindFunction( "LOCA224B1" )
		IF FPF->(FIELDPOS("FPF_HTB")) > 0 .and. FPF->(FIELDPOS("FPF_HTR")) > 0
    	    If LOCA224B1("FPF_HTB", "FPF") .and. LOCA224B1("FPF_HTR", "FPF")
				l4035 := .T.
			EndIf
		EndIf
	EndIf

	ATABAUX := OBROWSE:DATA():AARRAY
	NPOSAUX := 0
	AEVAL(ATABAUX,{|X|IF(X[1]==CMARCA,NPOSAUX++,)})

	DO CASE
	CASE NPOSAUX==0
		MSGALERT(STR0086 , STR0002) //"ATENÇÃO ==> FAVOR SELECIONAR PELO MENOS UMA MINUTA."###"Rental"
		RETURN(.F.)
	ENDCASE

	IF !MSGYESNO(STR0090 , STR0002) //"CONFIRMA O ESTORNO DAS MINUTAS SELECIONADAS ??"###"Rental"
		RETURN(.F.)
	ENDIF

	FPF->(DBSETORDER(2))  //2=FPF_FILIAL+FPF_MINUTA

	FOR NPOS:=1 TO LEN(ATABAUX)
		IF ATABAUX[NPOS,NPOSMAR]==CMARCA
			CSTATUS:=""
			DO CASE
			CASE ATABAUX[NPOS,NPOSSTA]=="2"  //1=PREVISTA,2=CONFIRMADA,3=BAIXADA,4=ENCERRADA,5=CANCELADA,6=MEDIDA
			CSTATUS:="1"

			CASE ATABAUX[NPOS,NPOSSTA]=="3"  //1=PREVISTA,2=CONFIRMADA,3=BAIXADA,4=ENCERRADA,5=CANCELADA,6=MEDIDA
				IF ! FVERZLG(ATABAUX[NPOS,NPOSNAS],ATABAUX[NPOS,NPOSDAT])  //VERIFICA SE EXISTE PROGRAMAÇÃO
					CSTATUS := "2"
				ELSE
					CSTATUS := "2"
				ENDIF

			CASE ATABAUX[NPOS,NPOSSTA]=="5"  //1=PREVISTA,2=CONFIRMADA,3=BAIXADA,4=ENCERRADA,5=CANCELADA,6=MEDIDA
				AVERZBX := LOCA00514("FESTORNAM",ATABAUX[NPOS,NPOSFRO],ATABAUX[NPOS,NPOSFRO],ATABAUX[NPOS,NPOSDAT],ATABAUX[NPOS,NPOSDAT],ATABAUX[NPOS,NPOSNAS], ATABAUX[NPOS,NPOSHOI],ATABAUX[NPOS,NPOSHOF]) 		// VERIFICA SE EXISTE ZBX
				IF LEN(AVERZBX)==0
					CSTATUS := "1"
				ELSE
					cErro 	:= STR0091+DTOC(ATABAUX[NPOS,NPOSDAT])+STR0092+ALLTRIM(AVERZBX[1,3])+STR0093 //"Existe minuta no dia "
					CSTATUS := ""
				ENDIF
				IF EMPTY(CSTATUS)
					AVERZLM := LOCA00515("FESTORNAM",ATABAUX[NPOS,NPOSFRO],ATABAUX[NPOS,NPOSFRO],ATABAUX[NPOS,NPOSDAT],ATABAUX[NPOS,NPOSDAT],ATABAUX[NPOS,NPOSNAS])  //VERIFICA SE EXISTE ZLM
					IF LEN(AVERZLM)==0
						CSTATUS := "1"
					ELSE
						cErro 	:= STR0094+DTOC(ATABAUX[NPOS,NPOSDAT])+STR0092+ALLTRIM(AVERZLM[1,2])+STR0093 //" AS: "
						CSTATUS := ""
					ENDIF
				ENDIF
				//se tiver erro, mostra mensagem
				If Empty(CSTATUS)
					MsgAlert(cErro,STR0002) //"Rental"
				EndIf
			CASE ATABAUX[NPOS,NPOSSTA]=="6" // minuta medida
				/* ISSUE DSERLOCA-7449 
				Frank Z Fuga em 14/07/25
				Permitir retirar uma minuta de medida para baixada
				*/
				If FPF->(FIELDPOS("FPF_MEDSEQ")) > 0
					FPF->(dbSetOrder(2)) 
					If FPF->(dbSeek(xFilial("FPF") + ATABAUX[NPOS,NPOSMIN]))
						If  FPF->FPF_STATUS == '6'  
							FQK->(dbSetOrder(1))
							If FQK->(dbSeek(xFilial("FQK")+FPF->(FPF_NROMED+FPF_MEDSEQ)))
								If empty(FQK->FQK_NUMPV) 
									If !(FQK->FQK_SITUAC=='2' .and. FQK->FQK_VALTOT>0)
										cStatus := "3" // Voltar para baixada
									EndIf
								EndIf
							EndIf
						EndIf
					EndIf
				EndIF


			ENDCASE

			NLIDOS++
			IF !EMPTY(CSTATUS)
				FPF->(DBSETORDER(2))  					// 2=FPF_FILIAL+FPF_MINUTA
				IF FPF->(DBSEEK(XFILIAL("FPF") + ATABAUX[NPOS,NPOSMIN]))		
					
					LESTOK := .T.
					IF _ZBXESTVL //EXISTBLOCK("ZBXESTVL")			// PONTO DE ENTRADA PARA VALIDAR SE PODE REALIZAR O ESTORNO
					LESTOK := EXECBLOCK("ZBXESTVL",.T.,.T.,{CSTATUS, ATABAUX[NPOS,NPOSSTA]})
					ENDIF				    
					IF LESTOK 
						IF FPF->(RECLOCK("FPF",.F.))
							FPF->FPF_STATUS := CSTATUS  // 1=PREVISTA,2=CONFIRMADA,3=BAIXADA,4=ENCERRADA,5=CANCELADA,6=MEDIDA
							IF CSTATUS == "2"
								FPF->FPF_HREALI := ""
								FPF->FPF_HREALF := ""
								FPF->FPF_BXALMI := ""
								FPF->FPF_BXALMF := ""
								FPF->FPF_BXJANI := ""
								FPF->FPF_BXJANF := ""
								FPF->FPF_BXOBS  := ""
								FPF->FPF_HRPARA := ""
								FPF->FPF_MOTIVO := ""
								FPF->FPF_MINUTM := ""

								If l4035
									IF FPF->(FIELDPOS("FPF_HTB")) > 0 .and. FPF->(FIELDPOS("FPF_HTR")) > 0 .and. ;
									   FPF->(FIELDPOS("FPF_MINDIA")) > 0 
										FPF->FPF_HTB := ""
										FPF->FPF_HTR := ""
										FPF->FPF_MINDIA := 0
									EndIf
								EndIf

							ENDIF

							If cStatus == "3"

								/* ISSUE DSERLOCA-7449 
								Frank Z Fuga em 14/07/25
								Permitir retirar uma minuta de medida para baixada
								*/
								If FPF->(FIELDPOS("FPF_MEDSEQ")) > 0   
									FPF->FPF_NROMED := ""
									FPF->FPF_MEDSEQ := ""
								EndIf

							EndIf


							FPF->(MSUNLOCK())

							IF CSTATUS == "1" 
								IF ATABAUX[NPOS,NPOSSTA] == "5"
									FPF->(RECLOCK("FPF",.F.))
									FPF->FPF_MOTCAN := ""
									FPF->(MSUNLOCK())
									// RECUPERA A PROGRAMAÇÃO DIARIA DA MINUTA QUE ESTAVA CANCELADA

									/*
									+ FPF->FPF_AS      +
									+ FPF->FPF_MINUTA  +
									*/

									_CQRYZLG := " SELECT R_E_C_N_O_ AS REG FROM " + RETSQLNAME("FPO") + " "
									_CQRYZLG += " WHERE FPO_NRAS   = ? " 
									_CQRYZLG += "   AND FPO_MINUTA = ? " 
									_CQRYZLG += "   AND D_E_L_E_T_ = '*' " 
									IF SELECT("TRBDEL") > 0
										TRBDEL->(DBCLOSEAREA())
									ENDIF
									_CQRYZLG := CHANGEQUERY(_CQRYZLG)
									//TCQUERY _CQRYZLG NEW ALIAS "TRBDEL"
									aBindParam := {FPF->FPF_AS,FPF->FPF_MINUTA}
									MPSysOpenQuery(_CQRYZLG,"TRBDEL",,,aBindParam)

									SET DELETED OFF
									While !TRBDEL->(Eof())
										FPO->(dbGoto(TRBDEL->REG))
										If !FPO->(Eof())
											FPO->(RecLock("FPO",.F.))
											FPO->(dbRecall())
											FPO->(MsUnlock())
										EndIf
										TRBDEL->(DBSkip())
									EndDo
									TRBDEL->(DBCLOSEAREA())
									SET DELETED ON

							ELSEIF ATABAUX[NPOS,NPOSSTA] == "2" 
									// VOLTA O STATUS DA PROGRAMAÇÃO DIARIA PARA R=RESERVADO
									/*
									+ FPF->FPF_AS      +
									+ FPF->FPF_MINUTA  +
									*/
									_CQRYZLG := " SELECT R_E_C_N_O_ AS REG FROM " + RETSQLNAME("FPO") + " " 
									_CQRYZLG += " WHERE FPO_NRAS   = ? " 
									_CQRYZLG += "   AND FPO_MINUTA = ? " 
									_CQRYZLG += "   AND D_E_L_E_T_ = '' " 
									_CQRYZLG := CHANGEQUERY(_CQRYZLG)
									IF SELECT("TRBDEL") > 0
										TRBDEL->(DBCLOSEAREA())
									ENDIF
									//TCQUERY _CQRYZLG NEW ALIAS "TRBDEL"
									aBindParam := {FPF->FPF_AS, FPF->FPF_MINUTA}
									MPSysOpenQuery(_CQRYZLG,"TRBDEL",,,aBindParam)


									While !TRBDEL->(Eof())
										FPO->(dbGoto(TRBDEL->REG))
										If !FPO->(Eof())
											FPO->(RecLock("FPO",.F.))
											FPO->FPO_STATUS := "R"
											FPO->(MsUnlock())
										EndIf  
										TRBDEL->(DBSKIP())
									EndDo
									TRBDEL->(DBCLOSEAREA())
								ENDIF
							ENDIF

							IF _ZBXESTMI //EXISTBLOCK("ZBXESTMI")	// PONTO DE ENTRADA NO FINAL DA BAIXA DA MINUTA
								EXECBLOCK("ZBXESTMI",.T.,.T.,{FPF->FPF_MINUTA, CSTATUS, ATABAUX[NPOS,NPOSSTA], FPF->FPF_AS})
							ENDIF

							NGRAVADOS++
							OBROWSE:DATA():AARRAY[NPOS,NPOSSTA]:=FPF->FPF_STATUS
						
						ENDIF 
					ENDIF 
				ENDIF
			ENDIF
		ENDIF
	NEXT

	OBROWSE:REFRESH(.T.)
	OBROWSE:SETFOCUS()

	IF NGRAVADOS > 0
		MSGALERT(STR0095 , STR0002) //"ESTORNO EXECUTADO COM SUCESSO."###"Rental"
	ELSE
		MSGALERT(STR0096 , STR0002) //"NENHUM ESTORNO FOI EXECUTADO."###"Rental"
	ENDIF

	RESTAREA(AAREAZBX)

RETURN .T.
 
/*/{PROTHEUS.DOC} FTRAZDESC
ITUP BUSINESS - TOTVS RENTAL
APRESENTA A DESCRICAO DO EQUIPAMENTO NA ROTINA DE TROCA
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FTRAZDESC(CFROTA,CDESC,ODESC) 

	CDESC := POSICIONE("ST9",1,XFILIAL("ST9")+CFROTA,"T9_NOME")  //PREENCHE O CAMPO COM O NOME DO BEM

	IF EMPTY(CDESC)
		CDESC := POSICIONE("SRA",1,XFILIAL("SRA")+CFROTA,"RA_NOME")
	ENDIF

	IF VALTYPE(ODESC)=="O" 
		ODESC:REFRESH() 
	ENDIF 

RETURN 

/*/{PROTHEUS.DOC} FVERSIGLAS
ITUP BUSINESS - TOTVS RENTAL
VALIDA SE AS SIGLAS SÃO IGUAIS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FVERSIGLAS(ATROCA,CAS) 
Local AAREAST9 := ST9->(GETAREA())
Local AAREASRA := SRA->(GETAREA())
Local LRET     := .T.
Local CFAMILIA := ""

	ST9->(DBSETORDER(1))  							// T9_FILIAL+T9_CODBEM
	IF ST9->(DBSEEK(XFILIAL("ST9")+ATROCA[1]))
		CFAMILIA := ST9->T9_CODFAMI
	ENDIF

	IF !ST9->(DBSEEK(XFILIAL("ST9")+ATROCA[2]))  	// POSICIONA NO ST9
		SRA->(DBSETORDER(1))  						// RA_FILIAL+RA_MAT
		IF !SRA->(DBSEEK(XFILIAL("SRA")+ATROCA[2]))	// POSICIONA NO SRA
			MSGALERT(STR0097+ALLTRIM(ATROCA[2])+ STR0131, STR0002) //"O RECURSO SELECIONADO "###"É INVÁLIDO"###"Rental"
			LRET := .F.
		ENDIF
	ENDIF
	SRA->(RESTAREA(AAREASRA))
	ST9->(RESTAREA(AAREAST9))

RETURN(LRET)

/*/{PROTHEUS.DOC} FTROCAFRO
ITUP BUSINESS - TOTVS RENTAL
VALIDA NA TROCA DE EQUIPAMENTO
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FTROCAFRO(CFROTAD,CFROTAP,DDATAD,DDATAA,OFROTAP,CAS)
Local LRET := .T.
Local CMSG
Local NI
Local AVERZBX

	IF EMPTY(CFROTAP)
		MSGALERT(STR0100 , STR0002) //"FAVOR INFORMAR PARA QUAL RECURSO."###"Rental"
		OFROTAP:SETFOCUS()
		RETURN .F.
	ENDIF

	AVERZBX := LOCA00514("FTROCAFRO",CFROTAP,CFROTAP,DDATAD,DDATAA,CAS, FPF->FPF_HORAI, FPF->FPF_HORAF)  //VERIFICA SE EXISTE ZBX

	IF LEN(AVERZBX)==0
		LRET := MSGYESNO(STR0101+ALLTRIM(CFROTAD)+STR0102+ALLTRIM(CFROTAP)+" ?",STR0002) //"CONFIRMA A TROCA DO RECURSO "###" PARA "###"Rental"
	ELSE
		IF LEN(AVERZBX) == 1
			CMSG := STR0103 + DTOC(FPF->FPF_DATA) + " - " + FPF_HORAI + " - " + FPF_HORAF + STR0104 +CRLF+CRLF //"NO DIA "###" EXISTE A SEGUINTE PROGRAMACAO:"
		ELSE
			CMSG := STR0103 + DTOC(FPF->FPF_DATA) + " - " + FPF_HORAI + " - " + FPF_HORAF + STR0105 +CRLF+CRLF //"NO DIA "###" EXISTEM AS SEGUINTES PROGRAMACOES:"
		ENDIF
		FOR NI := 1 TO LEN( AVERZBX )
			CMSG += STR0068+ AVERZBX[NI,4] + STR0106+ALLTRIM( AVERZBX[NI,6] ) +STR0066+ALLTRIM( AVERZBX[NI,3] )+STR0107+TRANSFORM(AVERZBX[NI,7], "@R 99:99")+STR0108+TRANSFORM(AVERZBX[NI,8], "@R 99:99")+ CRLF  //"MINUTA: "###" - PROJETO: "###" - AS: "###" DAS "###" AS "
		NEXT

		LRET := .F.
		If !GetMV("MV_LOCX097",,.F.)
			CMSG += CRLF + STR0109 //"CONTINUA?"
			IF AVISO(STR0110, CMSG, {"SIM","NAO"}, 3 ) == 1 //"CONFLITO DE PROGRAMACAO"
				LRET := .T.
			ENDIF
		Else
			MsgAlert(cMSG,STR0074) //"Atenção"
		EndIF
	ENDIF

	IF LRET
		// --> TROCA NA TABELA DE TURNOS
		DBSELECTAREA("FPE")
		FPE->(DBCLEARFILTER())
		FPE->(DBSETFILTER({|| ALLTRIM(FPE->FPE_PROJET) == ALLTRIM(FPF->FPF_PROJET) .AND. ALLTRIM(FPE->FPE_OBRA) == ALLTRIM(FPF->FPF_OBRA) .AND. ALLTRIM(FPE->FPE_FROTA) == ALLTRIM(FPF->FPF_FROTA) .AND. FPE_FILIAL =XFILIAL('FPE') },"ALLTRIM(FPE->FPE_PROJET) == ALLTRIM(FPF->FPF_PROJET) .AND. ALLTRIM(FPE->FPE_OBRA) == ALLTRIM(FPF->FPF_OBRA) .AND. ALLTRIM(FPE->FPE_FROTA) == ALLTRIM(FPF->FPF_FROTA) .AND. FPE_FILIAL =XFILIAL('FPE')"))
		FPE->(DBGOTOP())
		IF ! EMPTY(FPE->FPE_PROJET)
			RECLOCK("FPE",.F.)
			FPE->FPE_FROTA := CFROTAP
			MSUNLOCK()
		ENDIF
		
		DBSELECTAREA("FQ5")
		FQ5->(DBCLEARFILTER())
		FQ5->(DBSETFILTER({|| ALLTRIM(FQ5->FQ5_AS) == ALLTRIM(FPF->FPF_AS)},"ALLTRIM(FQ5->FQ5_AS) == ALLTRIM(FPF->FPF_AS)"))
		FQ5->(DBGOTOP())
							
		DBSELECTAREA("FPS")
		FPS->(DBCLEARFILTER())
		FPS->(DBSETFILTER({|| ALLTRIM(FPS->FPS_PROJET) == ALLTRIM(FPF->FPF_PROJET) .AND. ALLTRIM(FPS->FPS_OBRA) == ALLTRIM(FPF->FPF_OBRA)  .AND. ALLTRIM(FPS->FPS_VIAGEM) == ALLTRIM(FQ5->FQ5_VIAGEM)},"ALLTRIM(FPS->FPS_PROJET) == ALLTRIM(FPF->FPF_PROJET) .AND. ALLTRIM(FPS->FPS_OBRA) == ALLTRIM(FPF->FPF_OBRA)  .AND. ALLTRIM(FPS->FPS_VIAGEM) == ALLTRIM(FQ5->FQ5_VIAGEM)"))
		FPS->(DBGOTOP())
		IF ! EMPTY(FPS->FPS_PROJET)
			RECLOCK("FPS",.F.)
			FPS->FPS_X5COD := CFROTAP
			MSUNLOCK()
		ENDIF 
				
		IF FPF->(RECLOCK("FPF",.F.))
			FPF->FPF_FROTA := CFROTAP
			FPF->(MSUNLOCK())

			XVIAGEM   := ""
			_ADADOSAS := GETAS( FPF->FPF_AS )
			IF LEN( _ADADOSAS ) > 0
				XVIAGEM := _ADADOSAS[1][2]	
			ENDIF

			LOCA00522( FPF->FPF_PROJET, XVIAGEM, , , , , , , CFROTAP )	// INTEGRAÇÃO DIÁRIO DE BORDO /OCORRÊNCIAS

			DO CASE
			CASE FPF->FPF_TIPOSE == "E"	// EQUIPAMENTO
				//FPO->( DBORDERNICKNAME( "MINUTAZLG" ) )
				FPO->( RetOrder("FPO","FPO_FILIAL+FPO_MINUTA") )
				IF FPO->( DBSEEK( XFILIAL("FPO") + FPF->FPF_MINUTA, .T. ) )
					RECLOCK("FPO",.F.)
					FPO->FPO_FROTA := CFROTAP
					FPO->(MSUNLOCK())
				ENDIF
			CASE FPF->FPF_TIPOSE == "T"
				//FPM->( DBORDERNICKNAME( "MINUTAZLE" ) )
				FPM->( RetOrder("FPM","FPM_FILIAL+FPM_MINUTA") )
				IF FPM->( DBSEEK( XFILIAL("FPM") + FPF->FPF_MINUTA, .T. ) )
					RECLOCK("FPM",.F.)
					FPM->FPM_FROTA := CFROTAP
					FPM->(MSUNLOCK())
				ENDIF
			ENDCASE
				
			IF EXISTBLOCK("ZBXTEQFM") //PONTO DE ENTRADA NO FINAL DA ATUALIZAÇÃO DE TROCA DE EQUIPAMENTO DA MINUTA.
				EXECBLOCK("ZBXTEQFM",.T.,.T.,{CFROTAP})
			ENDIF

		ENDIF

	ENDIF

RETURN LRET

/*/{PROTHEUS.DOC} FVERZLG
ITUP BUSINESS - TOTVS RENTAL
VALIDA NO ESTORNO DA MINUTA
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FVERZLG( CNRAS, DDATA ) 
Local AAREA    := GETAREA()
Local AAREAZLG := FPO->( GETAREA() )
Local LRET     := .F.

	FPO->( DBSETORDER(5) ) 							// FPO_FILIAL+FPO_NRAS+FPO_FROTA+FPO_CODBEM
	FPO->( DBSEEK(XFILIAL("FPO")+CNRAS) )
	WHILE FPO->(!EOF() .AND. FPO_NRAS==CNRAS) 		// WHILE FPO->(!EOF()) .AND. FPO->FPO_FILIAL == XFILIAL("FPO") .AND. FPO->FPO_NRAS == CNRAS
		IF FPO->(DDATA>=FPO_DTINI .AND. DDATA<=FPO_DTFIM)
			IF FPO->(RECLOCK("FPO",.F.))
				FPO->(DBDELETE())
				FPO->(MSUNLOCK())
			ENDIF
		ENDIF
		FPO->(DBSKIP())
	ENDDO 

	FPO->(RESTAREA(AAREAZLG)) 
	RESTAREA(AAREA) 

RETURN LRET

/*/{PROTHEUS.DOC} LOCA00514
ITUP BUSINESS - TOTVS RENTAL
VALIDA NO ESTORNO DA MINUTA
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00514(CROTINA,CFROTAD,CFROTAA,DDATAD,DDATAA,CAS, CHINI, CHFIM, CEXCEPT) 
Local ARET

	ARET := FVERZBX(CROTINA,CFROTAD,CFROTAA,DDATAD,DDATAA,CAS, CHINI, CHFIM, CEXCEPT) 

RETURN(ARET)


/*/{PROTHEUS.DOC} FVERZBX
ITUP BUSINESS - TOTVS RENTAL
VALIDA A EXISTENCIA DO REGISTRO
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FVERZBX(CROTINA,CFROTAD,CFROTAA,DDATAD,DDATAA,CAS, CHINI, CHFIM, CEXCEPT) 
Local ARET  := {}

	FMONTAVER("QRYVER",CROTINA,CFROTAD,CFROTAA,DDATAD,DDATAA,CAS, CHINI, CHFIM, CEXCEPT)  //MONTA A QUERY

	QRYVER->(DBGOTOP())
	WHILE QRYVER->(!EOF())
		DO CASE
		CASE UPPER(CROTINA)==UPPER("FESTORNAM")
			QRYVER->(AADD(ARET,{FPF_RECNO,FPF_DATA,FPF_AS,FPF_MINUTA,FPF_STATUS, FPF_PROJET, FPF_HORAI, FPF_HORAF}))
		CASE UPPER(CROTINA)==UPPER("ZA5LINOK")
			QRYVER->(AADD(ARET,{FPF_RECNO,FPF_DATA,FPF_AS,FPF_MINUTA,FPF_STATUS, FPF_PROJET, FPF_HORAI, FPF_HORAF}))
		CASE UPPER(CROTINA)==UPPER("LOCA013")
			QRYVER->(AADD(ARET,{FPF_RECNO,FPF_DATA,FPF_AS,FPF_MINUTA,FPF_STATUS, FPF_PROJET, FPF_HORAI, FPF_HORAF}))
		CASE UPPER(CROTINA)==UPPER("FTROCAFRO")
			QRYVER->(AADD(ARET,{FPF_RECNO,FPF_DATA,FPF_AS,FPF_MINUTA,FPF_STATUS, FPF_PROJET, FPF_HORAI, FPF_HORAF}))
		CASE UPPER(CROTINA)==UPPER("FACEMINUTA")
			QRYVER->(AADD(ARET,{FPF_RECNO,FPF_DATA,FPF_AS,FPF_MINUTA,FPF_STATUS, FPF_PROJET, FPF_HORAI, FPF_HORAF}))
		CASE UPPER(CROTINA)==UPPER("LOCA048")
			QRYVER->(AADD(ARET,{FPF_RECNO,FPF_DATA,FPF_AS,FPF_MINUTA,FPF_STATUS, FPF_PROJET, FPF_HORAI, FPF_HORAF}))
		ENDCASE
		QRYVER->(DBSKIP())
	ENDDO 

RETURN(ARET) 


/*/{PROTHEUS.DOC} FVERZBX
ITUP BUSINESS - TOTVS RENTAL
VALIDA A EXISTENCIA DO REGISTRO
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FMONTAVER(CALIASQRY,CROTINA,CFROTAD,CFROTAA,DDATAD,DDATAA,CAS, CHINI, CHFIM, CEXCEPT)
Local AESTRU
Local CQRY      := ""
Local NPOS      := 0 
Local _cCampos

DEFAULT CHINI   := "0000"
DEFAULT CHFIM   := "2400"
DEFAULT CEXCEPT := ""

	CHINI := ALLTRIM( CHINI )
	CHFIM := ALLTRIM( CHFIM )
		
	_cCampos := "FPF_MINUTA,FPF_AS,FPF_DATA,FPF_FROTA,FPF_PROJET,FPF_OBRA,FPF_NROMED,FPF_HORAI,FPF_HORAF,FPF_HRPARA,"
	_cCampos += "FPF_MOTIVO,FPF_STATUS,FPF_MINUTM,FPF_FILIAL,FPF_RECNO"

	CQRY := "SELECT"
	CQRY += " ZBX.FPF_MINUTA, ZBX.FPF_AS, ZBX.FPF_DATA, ZBX.FPF_FROTA, ZBX.FPF_PROJET, ZBX.FPF_OBRA," 
	CQRY += " ZBX.FPF_NROMED, ZBX.FPF_HORAI, ZBX.FPF_HORAF, ZBX.FPF_HRPARA, ZBX.FPF_MOTIVO, ZBX.FPF_STATUS, ZBX.FPF_MINUTM," 
	CQRY += " ZBX.FPF_FILIAL, ZBX.R_E_C_N_O_ AS FPF_RECNO" 
	CQRY += " FROM " + RETSQLNAME("FPF") + " ZBX" 
	CQRY += " WHERE ZBX.FPF_FILIAL = '" + XFILIAL("FPF") + "'" 
	DO CASE
	CASE UPPER(CROTINA)==UPPER("FESTORNAM")
		CQRY += " AND ZBX.FPF_AS <> '" 
		&('CQRY += CAS')
		CQRY += "'" 
	CASE UPPER(CROTINA)==UPPER("ZA5LINOK")
		CQRY += " AND ZBX.FPF_AS <> '" 
		&('CQRY += CAS') 
		CQRY += "'" 
		CQRY += " AND ZBX.FPF_STATUS IN ('2','3','4','5','6')"   	// 1=PREVISTA,2=CONFIRMADA,3=BAIXADA,4=ENCERRADA,5=CANCELADA,6=MEDIDA
	CASE UPPER(CROTINA)==UPPER("LOCA013")  									// GERA CONTRATO
		CQRY += " AND ZBX.FPF_AS <> '" 
		&('CQRY += CAS') 
		CQRY += "'" 
		CQRY += " AND ZBX.FPF_STATUS NOT IN ('5')"   				// 1=PREVISTA,2=CONFIRMADA,3=BAIXADA,4=ENCERRADA,5=CANCELADA,6=MEDIDA
	CASE UPPER(CROTINA)==UPPER("FTROCAFRO")  								// TROCA FROTA
		CQRY += " AND ZBX.FPF_AS <> '" 
		&('CQRY += CAS') 
		CQRY += "'" 
		CQRY += " AND ZBX.FPF_STATUS IN ('1','2','3','4','5','6')"   	// 1=PREVISTA,2=CONFIRMADA,3=BAIXADA,4=ENCERRADA,5=CANCELADA,6=MEDIDA
	CASE UPPER(CROTINA)==UPPER("FACEMINUTA")
		CQRY += " AND ZBX.FPF_STATUS IN ('2','3','4','6')"   		// 1=PREVISTA,2=CONFIRMADA,3=BAIXADA,4=ENCERRADA,5=CANCELADA,6=MEDIDA
	ENDCASE
	IF ! EMPTY( CEXCEPT )
		&('CQRY += " AND " + ALLTRIM(CEXCEPT)') 
	ENDIF
	IF UPPER(CROTINA) == "LOCA048"
		CQRY += " AND ZBX.FPF_AS = '" 
		&('CQRY += CAS') 
		CQRY += "'" 
		CQRY += " AND ZBX.FPF_DATA <= '" 
		&('CQRY += DTOS(DDATAA)') 
		CQRY += "' " 
		CQRY += " AND ZBX.FPF_STATUS = '3' " 
	ELSE
		CQRY += " AND ZBX.FPF_FROTA BETWEEN '" 
		&('CQRY += CFROTAD') 
		CQRY += "' AND '" 
		&('CQRY += CFROTAA') 
		CQRY += "'" 
		// Alterado por Frank em 19/10/23
		CQRY += " AND ZBX.FPF_DATA BETWEEN '" 
		&('cQry += DTOS(DDATAD)')
		CQRY += "' AND '" 
		&('cQry += DTOS(DDATAA)')
		CQRY += "' AND ZBX.FPF_HORAI <= '"
		&('cQry += CHFIM')
		CQRY += "' AND ZBX.FPF_HORAF >= '"
		&('cQry += CHINI')
		CQRY += "' "
	ENDIF
	CQRY += " AND ZBX.D_E_L_E_T_ = ' ' " 
	CQRY += " ORDER BY ZBX.FPF_FROTA , ZBX.FPF_DATA " 
	CQRY := CHANGEQUERY( CQRY )
		
	IF !SELECT(CALIASQRY)==0 
		(CALIASQRY)->(DBCLOSEAREA()) 
	ENDIF

	DBUSEAREA(.T., "TOPCONN", TCGENQRY(,,CQRY), CALIASQRY, .F., .T.)

	AESTRU := FPF->(DBSTRUCT())
	FOR NPOS:=1 TO LEN(AESTRU)
		IF AESTRU[NPOS,2]<>"C" .AND. AESTRU[NPOS,2]<>"M"
			If alltrim(AESTRU[NPOS,1]) $ _cCampos
				TCSETFIELD(CALIASQRY,AESTRU[NPOS,1],AESTRU[NPOS,2],AESTRU[NPOS,3],AESTRU[NPOS,4])
			ENDIF
		ENDIF
	NEXT NPOS 

RETURN 

/*/{PROTHEUS.DOC} LOCA00515
ITUP BUSINESS - TOTVS RENTAL
VALIDA A EXISTENCIA DO REGISTRO
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00515(CROTINA,CFROTAD,CFROTAA,DDATAD,DDATAA,CAS) 
Local ARET

	ARET := FVERZLM(CROTINA,CFROTAD,CFROTAA,DDATAD,DDATAA,CAS) 

RETURN(ARET)

/*/{PROTHEUS.DOC} FVERZLM
ITUP BUSINESS - TOTVS RENTAL
VALIDA A EXISTENCIA DO REGISTRO
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FVERZLM(CROTINA,CFROTAD,CFROTAA,DDATAD,DDATAA,CAS) 
Local ARET 		:= {} 
Local cAliasFpp	:= GetNextAlias()

	FMONTAZLM(cAliasFpp,CROTINA,CFROTAD,CFROTAA,DDATAD,DDATAA,CAS)  //MONTA A QUERY

	(cAliasFpp)->(DBGOTOP())
	WHILE (cAliasFpp)->(!EOF())
		DO CASE
		CASE UPPER(CROTINA)==UPPER("FESTORNAM")
			(cAliasFpp)->(AADD(ARET,{FPP_DTMEDI,FPN_AS}))
		ENDCASE
		(cAliasFpp)->(DBSKIP())
	ENDDO 

	(cAliasFpp)->(DbCloseArea())

RETURN(ARET) 


/*/{PROTHEUS.DOC} FMONTAZLM
ITUP BUSINESS - TOTVS RENTAL
MONTAGEM DA QUERY DA MEDICAO
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FMONTAZLM(CALIASQRY,CROTINA,CFROTAD,CFROTAA,DDATAD,DDATAA,CAS) 
Local AESTRU , CQRY := "" //, CCRLF := CHR(13)+CHR(10) 
Local NPOS := 0 
Local _cCampos := ""

	_cCampos := "FPN_COD,FPN_AS,FPP_FROTA,FPP_DTMEDI,FPN_FILIAL,FPN_RECNO"

	CQRY     += " SELECT ZLF.FPN_COD    , ZLF.FPN_AS , ZLM.FPP_FROTA , ZLM.FPP_DTMEDI , " 
	CQRY     += "        ZLF.FPN_FILIAL , ZLF.R_E_C_N_O_ AS FPN_RECNO" 
	CQRY     += " FROM "+RETSQLNAME("FPN")+" ZLF" 
	CQRY     += "        LEFT JOIN "+RETSQLNAME("FPP")+" ZLM ON ZLF.FPN_FILIAL = ZLM.FPP_FILIAL AND ZLM.FPP_COD = ZLF.FPN_COD AND ZLM.D_E_L_E_T_ = '' " 
	CQRY     += " WHERE  ZLF.D_E_L_E_T_ = ''" 
	CQRY     += "   AND  ZLF.FPN_FILIAL = '"  + XFILIAL("FPN") + "'" 
	DO CASE
	CASE UPPER(CROTINA)==UPPER("FESTORNAM")
		CQRY += "   AND  ZLF.FPN_AS = '" 
		&('CQRY += CAS') 
		CQRY += "'" 
		CQRY += "   AND  ZLM.FPP_FROTA  BETWEEN '" 
		&('CQRY += CFROTAD')      
		CQRY += "' AND '" 
		&('CQRY += CFROTAA')      
		CQRY += "'" 
		CQRY += "   AND  ZLM.FPP_DTMEDI BETWEEN '" 
		&('CQRY += DTOS(DDATAD)') 
		CQRY += "' AND '" 
		&('CQRY += DTOS(DDATAA)') 
		CQRY += "'" 
		CQRY += " ORDER BY ZLM.FPP_FROTA , ZLM.FPP_DTMEDI" 
	ENDCASE
	CQRY := CHANGEQUERY( CQRY )
	IF !SELECT(CALIASQRY)==0 
		(CALIASQRY)->(DBCLOSEAREA()) 
	ENDIF
	DBUSEAREA(.T., "TOPCONN", TCGENQRY(,,CQRY), CALIASQRY, .F., .T.)

	AESTRU := FPN->(DBSTRUCT())
	FOR NPOS:=1 TO LEN(AESTRU)
		IF AESTRU[NPOS][2]<>"C" .AND. AESTRU[NPOS][2]<>"M"
			If alltrim(AESTRU[NPOS,1]) $ _cCampos
				TCSETFIELD(CALIASQRY,AESTRU[NPOS,1],AESTRU[NPOS,2],AESTRU[NPOS,3],AESTRU[NPOS,4])
			ENDIF
		ENDIF
	NEXT NPOS 

RETURN

/*/{PROTHEUS.DOC} LOCA00516
ITUP BUSINESS - TOTVS RENTAL
VISUALIZA OS ERROS 
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00516(AERROS,CTITJAN) 				
Local NPOS , ACOLS , ACOLS0 , AHEADER , BHEADER , ODLG , ABUTTONS := {} 
Local NSTATUS , ASTATUS := FSTATUS("LOCA00516") 		// MONTA OS STATUS
Local lRet := .F.

Private OBROWSE

	ACOLS := {} 
	FOR NPOS:=1 TO LEN(AERROS)
		ACOLS0:={}
		AADD(ACOLS0,AERROS[NPOS,1])
		AADD(ACOLS0,AERROS[NPOS,2])
		AADD(ACOLS0,AERROS[NPOS,3])
		AADD(ACOLS0,AERROS[NPOS,4])
		AADD(ACOLS0,AERROS[NPOS,5])
		AADD(ACOLS0,AERROS[NPOS,6])
		AADD(ACOLS0,AERROS[NPOS,8])
		AADD(ACOLS0,AERROS[NPOS,7])
		AADD(ACOLS,ACOLS0)
	NEXT

	AHEADER := {}
	BHEADER := "{||ACOLS[OBROWSE:AT(),1]}";AADD(AHEADER,{OEMTOANSI(STR0163),&BHEADER,"C","@!",1,10,0}) //"EQUIPAMENTO"
	BHEADER := "{||ACOLS[OBROWSE:AT(),2]}";AADD(AHEADER,{OEMTOANSI(STR0135),&BHEADER,"C","@!",1,10,0}) //"NRO AS"
	BHEADER := "{||ACOLS[OBROWSE:AT(),3]}";AADD(AHEADER,{OEMTOANSI(STR0136),&BHEADER,"D","@E",1,08,0}) //"INÍCIO"
	BHEADER := "{||ACOLS[OBROWSE:AT(),4]}";AADD(AHEADER,{OEMTOANSI(STR0167),&BHEADER,"D","@E",1,08,0}) //"FIM"
	BHEADER := "{||ACOLS[OBROWSE:AT(),5]}";AADD(AHEADER,{OEMTOANSI(STR0164),&BHEADER,"D","@E",1,08,0}) //"DATA"
	BHEADER := "{||ACOLS[OBROWSE:AT(),6]}";AADD(AHEADER,{OEMTOANSI(STR0165),&BHEADER,"C","@!",1,10,0}) //"MINUTA"
	BHEADER := "{||ACOLS[OBROWSE:AT(),7]}";AADD(AHEADER,{OEMTOANSI(STR0168),&BHEADER,"C","@!",1,60,0}) //"INCONSISTÊNCIA"
	BHEADER := "{||ACOLS[OBROWSE:AT(),8]}";AADD(AHEADER,{OEMTOANSI(STR0166),&BHEADER,"C","@!",1,01,0}) //"STATUS"

	DEFINE MSDIALOG ODLG FROM 100,0 TO 500,1000 TITLE OEMTOANSI(CTITJAN) OF OMAINWND PIXEL
	OBROWSE:=FWBROWSE():NEW()
	OBROWSE:SETDATAARRAY()
	OBROWSE:SETARRAY(ACOLS)
	FOR NSTATUS:=1 TO LEN(ASTATUS)
		OBROWSE:ADDLEGEND(ASTATUS[NSTATUS,3],ASTATUS[NSTATUS,1],ASTATUS[NSTATUS,2])
	NEXT
	OBROWSE:SETCOLUMNS(AHEADER)
	OBROWSE:SETOWNER(ODLG)
	OBROWSE:DISABLEREPORT()
	OBROWSE:DISABLECONFIG()
	OBROWSE:ACTIVATE()
	ACTIVATE MSDIALOG ODLG CENTERED ON INIT ENCHOICEBAR(ODLG,{||lRet:=.T.,CLOSE(ODLG)},{||CLOSE(ODLG)},,ABUTTONS)

RETURN lRet

/*/{PROTHEUS.DOC} LOCA00517
ITUP BUSINESS - TOTVS RENTAL
RETORNA SE A FROTA ESTÁ EM USO
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00517( CTPPRJ , CFROTA , DDATA , CHINI , CHFIM , LQUIET ) 
DEFAULT CHINI  := "00:00" 
DEFAULT CHFIM  := "24:00" 
DEFAULT LQUIET := .F. 

	// --> CONSISTÊNCIAS DOS PARÂMETROS PASSADOS
	IF EMPTY(CTPPRJ) 
		IF ! LQUIET 
			MSGALERT(STR0111+ALLTRIM(FQ5->FQ5_AS)+STR0112+FQ5->FQ5_VIAGEM+"]." , STR0002)  //"FALTA O TIPO DO PROJETO (FP4_TIPOSE) REFERENTE A AS:["###"] - VIAGEM:["###"Rental"
		ENDIF 
		RETURN .T. 
	ENDIF 

	IF EMPTY(CFROTA) .AND. CTPPRJ <> "M" 
		IF ! LQUIET
			MSGALERT(STR0113 , STR0002) //"FALTA A FROTA"###"Rental"
		ENDIF
		RETURN .T. 
	ENDIF 

	IF EMPTY(DDATA) 
		IF ! LQUIET
			MSGALERT(STR0114 , STR0002)  //"FALTA A DATA A SER PESQUISADA"###"Rental"
		ENDIF
		RETURN .T. 
	ENDIF 

	IF ! ISHORA(@CHINI , LQUIET) .OR. !ISHORA(@CHFIM , LQUIET) 
		RETURN ARETORNO 
	ENDIF 

	// --> VERIFICAÇÃO DAS PROGRAMAÇÕES.
	DO CASE
	CASE CTPPRJ $ "E;G;P;L;M"		// EQUIPAMENTO E PLATAFORMA
		RETURN   FZLGEMUSO(CFROTA , DDATA , CHINI , CHFIM , LQUIET) 
	CASE CTPPRJ == "T"				// TRANSPORTE
		RETURN LOCA00518(CFROTA , DDATA , CHINI , CHFIM , LQUIET) 
	ENDCASE 

RETURN .F.

/*/{PROTHEUS.DOC} FZLGEMUSO
ITUP BUSINESS - TOTVS RENTAL
VERIFICA SE EXISTE PROGRAMAÇÃO
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FZLGEMUSO(CFROTA , DDATA , CHINI , CHFIM , LQUIET) 
Local AAREA    := GETAREA()
Local AAREAZLG := FPO->( GETAREA() )
Local AAREAZBX := FPF->( GETAREA() )
Local LRET     := .F.
Local CMSG     := ""

DEFAULT CHINI    := "0000"
DEFAULT CHFIM    := "2400"
DEFAULT LQUIET   := .F.

	FPO->( DBSETORDER(3) )		// FPO_FILIAL + FPO_FROTA + DTOS(FPO_DTINI) + FPO_CODBEM
	FPO->( DBSEEK( XFILIAL("FPO") + CFROTA + DTOS(DDATA), .T. ) )

	WHILE ! FPO->( EOF() ) .AND. FPO->FPO_FILIAL == XFILIAL("FPO") .AND. FPO->FPO_FROTA == CFROTA .AND. FPO->FPO_DTINI <= DDATA
		If FPO->FPO_STATUS != 'R'
			IF FVALID( CHINI, CHFIM, FPO->FPO_HRINI, FPO->FPO_HRFIM )
				IF ! LQUIET
					FPF->( DBSETORDER(1) )	//	FPF_FILIAL, FPF_FROTA, FPF_DATA, FPF_MINUTA, R_E_C_N_O_, D_E_L_E_T_
					FPF->( DBSEEK(XFILIAL("FPF")+CFROTA+DTOS(DDATA)), .T. )

					CMSG += STR0115+DTOC(DDATA) + STR0107 //"FROTA EM USO EM "###" DAS "
					CMSG += TRANSFORM(FPO->FPO_HRINI,"@R 99:99")+" AS "+TRANSFORM(FPO->FPO_HRFIM,"@R 99:99") + CRLF
					CMSG += STR0068+ FPF->FPF_MINUTA +STR0106 + ALLTRIM(FPO->FPO_PROJET) + STR0066 + FPO->FPO_NRAS //"MINUTA: "###" - PROJETO: "###" - AS: "
					MSGALERT( STR0116+CMSG , STR0002 ) //"CONFLITO DE PROGRAMAÇÃO: "###"Rental"
				ENDIF
				LRET := .T.
				EXIT
			ENDIF
		ENDIF

		FPO->( DBSKIP() )
	ENDDO 

	FPF->( RESTAREA(AAREAZBX) )
	FPO->( RESTAREA(AAREAZLG) )
	RESTAREA(AAREA) 

RETURN LRET

/*/{PROTHEUS.DOC} LOCA00518
ITUP BUSINESS - TOTVS RENTAL
VERIFICA SE EXISTE PROGRAMAÇÃO
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00518( CFROTA , DDATA , CHINI , CHFIM , LQUIET ) 
Local AAREA    := GETAREA()
Local AAREAZLE := FPM->( GETAREA() )
Local LRET     := .F.
Local CANOMES  := LEFT( DTOS( DDATA ), 6 )

DEFAULT CHINI    := "0000"
DEFAULT CHFIM    := "2400"
DEFAULT LQUIET   := .F.

	FPM->( DBSETORDER(1) )		// FPM_FILIAL + FPM_ANOMES + FPM_FROTA + DTOS(FPM_DTPROG) + FPM_CODBEM
	FPM->( DBSEEK( XFILIAL("FPM") + CANOMES + CFROTA + DTOS(DDATA), .T. ) )

	WHILE ! FPM->( EOF() ) .AND. FPM->FPM_FILIAL == XFILIAL("FPM") .AND. FPM->FPM_FROTA == CFROTA .AND. FPM->FPM_DTPROG <= DDATA
		IF FVALID( CHINI, CHFIM, FPM->FPM_HRINI, FPM->FPM_HRFIM )
			IF ! LQUIET
				MSGALERT(STR0117+DTOC(DDATA)+STR0107+TRANSFORM(FPM->FPM_HRINI,"@R 99:99")+" AND "+TRANSFORM(FPM->FPM_HRFIM,"@R 99:99") , STR0002)  //"FROTA EM USO NO PERÍODO EM "###" DAS "###"Rental"
			ENDIF
			LRET := .T.
			EXIT
		ENDIF
		FPM->( DBSKIP() )
	ENDDO 

	FPM->( RESTAREA(AAREAZLE) )
	RESTAREA(AAREA) 

RETURN LRET

/*/{PROTHEUS.DOC} FKILLPROG
ITUP BUSINESS - TOTVS RENTAL
REMOVE PROGRAMACAO
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION FKILLPROG( CTIPOSE , CMINUTA )
Local AAREA     := GETAREA()
Local nSetOrd	:= 0
Local CALIAS    := ""
Local CKEY      := ""
Local CCOND     := ""
Local CQUERY

	IF EMPTY( CTIPOSE ) .OR. EMPTY( CMINUTA )
		ALERT("(FKILLPROG) INFORME A MINUTA!")
		RETURN NIL
	ENDIF

	DO CASE
	CASE CTIPOSE $ "E;L;M"
		CALIAS    := "FPO"
		//CNICKNAME := "MINUTAZLG"
		nSetOrd := RetOrder("FPO","FPO_FILIAL+FPO_MINUTA")
		CKEY      := XFILIAL("FPO") + CMINUTA
		CCOND     := " ! FPO->( EOF() ) .AND. FPO->FPO_FILIAL=='"+XFILIAL('FPO')+"' .AND. FPO->FPO_MINUTA == '"+CMINUTA+"'"

	CASE CTIPOSE == "T"
		CALIAS    := "FPM"
		//CNICKNAME := "MINUTAZLE"
		nSetOrd := RetOrder("FPM","FPM_FILIAL+FPM_MINUTA")
		CKEY      := XFILIAL("FPM") + CMINUTA
		CCOND     := " ! FPM->( EOF() ) .AND. FPM->FPM_FILIAL=='"+XFILIAL('FPM')+"' .AND. FPM->FPM_MINUTA == '"+CMINUTA+"'"

		FPF->( DBSETORDER(2) )	// FILIAL + MINUTA
		IF FPF->( DBSEEK( XFILIAL("FPF") + CMINUTA ) )
			CALIASQRY := GETNEXTALIAS()

			/*
			+ FPF->FPF_AS +
			+ DTOS(FPF->FPF_DATA) +
			*/

			CQUERY := "SELECT R_E_C_N_O_ RECORD FROM "+RETSQLNAME("FPL")
			CQUERY += " WHERE FPL_FILIAL = '"+XFILIAL("FPL")+"'"
			CQUERY += " AND FPL_AS = ? "
			CQUERY += " AND FPL_DTINI = ? "
			CQUERY += " AND D_E_L_E_T_= ' '"
			CQUERY := CHANGEQUERY(CQUERY)
			aBindParam := {FPF->FPF_AS, DTOS(FPF->FPF_DATA)}
			CALIASQRY := MPSysOpenQuery(CQUERY,,,,aBindParam)
			//DBUSEAREA(.T., "TOPCONN", TCGENQRY(,,CQUERY), CALIASQRY, .F., .T.)

			WHILE ! (CALIASQRY)->( EOF() )
				FPL->( DBGOTO( (CALIASQRY)->RECORD ) )
				RECLOCK("FPL", .F.)
				FPL->( DBDELETE() )
				MSUNLOCK()
				(CALIASQRY)->( DBSKIP() )
			ENDDO 
			(CALIASQRY)->( DBCLOSEAREA() )

			/*
			+ FPF->FPF_AS +
			+ DTOS(FPF->FPF_DATA) +
			*/

			CQUERY := "SELECT R_E_C_N_O_ RECORD FROM "+RETSQLNAME("FPQ")
			CQUERY += " WHERE FPQ_FILIAL = '"+XFILIAL("FPQ")+"'"
			CQUERY += " AND FPQ_AS = ? "
			CQUERY += " AND FPQ_DATA = ? "
			CQUERY += " AND D_E_L_E_T_= ' '"
			CQUERY := CHANGEQUERY(CQUERY)
			aBindParam := {FPF->FPF_AS, DTOS(FPF->FPF_DATA) }
			CALIASQRY := MPSysOpenQuery(cQuery,,,,aBindParam)
			//DBUSEAREA(.T., "TOPCONN", TCGENQRY(,,CQUERY), CALIASQRY, .F., .T.)

			WHILE ! (CALIASQRY)->( EOF() )
				FPQ->( DBGOTO( (CALIASQRY)->RECORD ) )
				RECLOCK("FPQ", .F.)
				FPQ->( DBDELETE() )
				MSUNLOCK()
				(CALIASQRY)->( DBSKIP() )
			ENDDO 
			(CALIASQRY)->( DBCLOSEAREA() )
		ENDIF
	ENDCASE

	DBSELECTAREA( CALIAS )
	//DBORDERNICKNAME( CNICKNAME )
	(cAlias)->(DbSetOrder(nSetOrd))
	DBSEEK( CKEY, .T. )

	WHILE &( CCOND )
		RECLOCK( CALIAS, .F. )
		DBDELETE()
		DBSKIP()
	ENDDO 

RESTAREA(AAREA)

RETURN NIL

/*/{PROTHEUS.DOC} LOCA00519
ITUP BUSINESS - TOTVS RENTAL
REMOVE PROGRAMACAO E CANCELA MINUTA
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00519( CAS )
Local AAREA  := GETAREA()
Local LRET   := .F.

	IF EMPTY( CAS )
		RETURN .F.
	ENDIF

	FPF->( DBSETORDER(4) )	// FILIAL + AS + MINUTA
	FPF->( DBSEEK( XFILIAL("FPF") + CAS ) )
	WHILE !FPF->( EOF() ) .AND. FPF->FPF_FILIAL == XFILIAL("FPF") .AND. FPF->FPF_AS == CAS
		IF FPF->FPF_STATUS <> "1"
			LRET := .F.
		ENDIF
		FPF->( DBSKIP() )
	ENDDO

	IF LRET
		FPF->( DBSEEK( XFILIAL("FPF") + CAS ) )    
		WHILE ! FPF->( EOF() ) .AND. FPF->FPF_FILIAL == XFILIAL("FPF") .AND. FPF->FPF_AS == CAS
			RECLOCK("FPF",.F.)
			FPF->FPF_STATUS := "5"  						// 1=PREVISTA,2=CONFIRMADA,3=BAIXADA,4=ENCERRADA,5=CANCELADA,6=MEDIDA
			FPF->FPF_MOTCAN := "03"  						// MOTIVO DO CANCELAMENTO | SERVICO CANCELADO | CANCELAMENTO AS
			FPF->FPF_DTOCOR := DDATABASE
			MSUNLOCK()

			FKILLPROG( FPF->FPF_TIPOSE, FPF->FPF_MINUTA )	// REMOVE PROGRAMACAO

			FPF->( DBSKIP() )
		ENDDO 
	ENDIF 

	RESTAREA( AAREA )

RETURN LRET

/*/{PROTHEUS.DOC} GETAS
ITUP BUSINESS - TOTVS RENTAL
RETORNA NÚMERO DA AS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
STATIC FUNCTION GETAS(CAS) 
Local AAS      := {}
Local AAREA    := GETAREA()
Local AAREADTQ := FQ5->(GETAREA())

	IF ! EMPTY(CAS) 
		FQ5->(DBSETORDER(9)) 
		IF FQ5->( DBSEEK(XFILIAL("FQ5") + CAS , .T.) ) 
			AADD( AAS , {FQ5->FQ5_AS  ,  FQ5->FQ5_VIAGEM  ,  FQ5->(RECNO())} ) 		// DEIXAR O RECNO POR ÚLTIMO
		ENDIF 
	ENDIF 

	FQ5->( RESTAREA(AAREADTQ) ) 
	RESTAREA(AAREA) 

RETURN ACLONE(AAS) 

/*/{PROTHEUS.DOC} LOCA00520
ITUP BUSINESS - TOTVS RENTAL
VALIDACAO DOS HORARIOS
@AUTHOR ITUP
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
FUNCTION LOCA00520(ACOLS,AHEADER) 
Local _CTEMP1
Local _CTEMP2
Local _LBLOQ	:= .F.      
Local cMultiplo := SUPERGETMV("MV_LOCX314",.F.,"")
Local cErro     := ""
Local cHora     := "" 
Local lRet 		:= .T.
Local nX
Local ATABAUX 	:= OBROWSE:DATA():AARRAY
Local cIni1		:= ""
Local cFim1		:= ""

	IF EXISTBLOCK("ZBXBXVHR")	// PONTO DE ENTRADA PARA VALIDACAO DOS HORARIOS DA BAIXA DA MINUTA.
		_LBLOQ := EXECBLOCK("ZBXBXVHR",.T.,.T.,{ACOLS, AHEADER})
		IF _LBLOQ
			RETURN .F.
		ENDIF
	ENDIF

	// --> VALIDACAO DO HORARIO INICIAL E HORARIO FINAL -------------------------
	IF CHORAI <> "  :  "  .AND. !EMPTY(CHORAI)          
		_CTEMP1 := SUBSTR(CHORAI,1,2)+SUBSTR(CHORAI,4,2)
		IF _CTEMP1 >= "2400"
			MSGALERT(STR0118,STR0119) //"HORÁRIO INICIAL INVÁLIDO."###"ATENÇÃO!"
			RETURN .F.
		ENDIF
	ENDIF

	IF CHORAF <> "  :  "  .AND. !EMPTY(CHORAF)          
		_CTEMP2 := SUBSTR(CHORAF,1,2)+SUBSTR(CHORAF,4,2)
		IF _CTEMP2 >= "2400"
			MSGALERT(STR0120,STR0119) //"HORÁRIO FINAL INVÁLIDO."###"ATENÇÃO!"
			RETURN .F.
		ENDIF
	ENDIF

	IF CHORAI <> "  :  " .AND. CHORAF <> "  :  " .AND. !EMPTY(CHORAI) .AND. !EMPTY(CHORAF)
		
		IF _CTEMP1 == "2400"
			_CTEMP1 := "0000"
		ENDIF                            
		
		IF _CTEMP1 > "2400"
			MSGALERT(STR0118 , STR0002) //"HORÁRIO INICIAL INVÁLIDO."###"Rental"
			RETURN .F.
		ENDIF
		
		IF _CTEMP2 > "2400"
			MSGALERT(STR0120 , STR0002) //"HORÁRIO FINAL INVÁLIDO."###"Rental"
			RETURN .F.
		ENDIF

		cIni1 := _CTEMP1
		cFim1 := _CTEMP2
		
	ENDIF    

	// --> VALIDACAO DO ALMOÇO INICIAL E HORARIO FINAL --------------------------
	IF CHALMI <> "  :  "  .AND. !EMPTY(CHALMI)          
		_CTEMP1 := SUBSTR(CHALMI,1,2)+SUBSTR(CHALMI,4,2)
		IF _CTEMP1 >= "2400"
			MSGALERT(STR0123 , STR0002) //"HORÁRIO DE ALMOÇO INICIAL INVÁLIDO."###"Rental"
			RETURN .F.
		ENDIF
	ENDIF

	IF CHALMF <> "  :  "  .AND. !EMPTY(CHALMF)          
		_CTEMP2 := SUBSTR(CHALMF,1,2)+SUBSTR(CHALMF,4,2)
		IF _CTEMP2 >= "2400"
			MSGALERT(STR0124 , STR0002) //"HORÁRIO DE ALMOÇO FINAL INVÁLIDO."###"Rental"
			RETURN .F.
		ENDIF
	ENDIF

	IF CHALMI <> "  :  " .AND. CHALMF <> "  :  " .AND. !EMPTY(CHALMI) .AND. !EMPTY(CHALMF)
		IF _CTEMP1 >= "2400"
			_CTEMP1 := "0000"
		ENDIF                            
		
		IF _CTEMP1 >= "2400"
			MSGALERT(STR0123 , STR0002) //"HORÁRIO DE ALMOÇO INICIAL INVÁLIDO."###"Rental"
			RETURN .F.
		ENDIF
		
		IF _CTEMP2 >= "2400"
			MSGALERT(STR0124 , STR0002) //"HORÁRIO DE ALMOÇO FINAL INVÁLIDO."###"Rental"
			RETURN .F.
		ENDIF

	ENDIF    

	// --> VALIDACAO DO JANTAR INICIAL E HORARIO FINAL --------------------------
	IF CHJANI <> "  :  "  .AND. !EMPTY(CHJANI)          
		_CTEMP1 := SUBSTR(CHJANI,1,2)+SUBSTR(CHJANI,4,2)
		IF _CTEMP1 >= "2400"
			MSGALERT(STR0127,STR0002) //"HORÁRIO DE JANTAR INICIAL INVÁLIDO."###"Rental"
			RETURN .F.
		ENDIF
	ENDIF

	IF CHJANF <> "  :  "  .AND. !EMPTY(CHJANF)          
		_CTEMP2 := SUBSTR(CHJANF,1,2)+SUBSTR(CHJANF,4,2)
		IF _CTEMP2 >= "2400"
			MSGALERT(STR0128,STR0002) //"HORÁRIO DE JANTAR FINAL INVÁLIDO."###"Rental"
			RETURN .F.
		ENDIF
	ENDIF

	IF CHJANI <> "  :  " .AND. CHJANF <> "  :  " .AND. !EMPTY(CHJANI) .AND. !EMPTY(CHJANF)
		
		IF _CTEMP1 >= "2400"
			MSGALERT(STR0127,STR0002) //"HORÁRIO DE JANTAR INICIAL INVÁLIDO."###"Rental"
			RETURN .F.
		ENDIF
		
		IF _CTEMP2 >= "2400"
			MSGALERT(STR0128,STR0002) //"HORÁRIO DE JANTAR FINAL INVÁLIDO."###"Rental"
			RETURN .F.
		ENDIF

	ENDIF    

	// Validação do conteúdo do parâmetro MV_LOCX314.
	If !empty(cMultiplo)
		If val(cMultiplo) <> 0 .and. val(cMultiplo) <> 5 .and. val(cMultiplo) <> 10 .and. ;
			val(cMultiplo) <> 15 .and. val(cMultiplo) <> 20 .and. val(cMultiplo) <> 30
			Help(Nil,Nil,alltrim(upper(Procname())),; 
			Nil,STR0171,1,0,Nil,Nil,Nil,Nil,Nil,; //"Inconsistência nos dados." 
			{STR0170}) //Ajuste o parâmetro MV_LOCX314. Os valoes aceitos são: 0, 5, 10, 15, ou 20.
			lRet := .F.
		EndIf
		If lRet
			cHora := &(ReadVar())
			If !empty(cHora) .and. val(cMultiplo) == 0 .and. substr(cHora,4,2)<>"00"
				cErro := STR0174 //"Os minutos precisam ser 00"
				lRet  := .F.
			EndIf
			If !empty(cHora) .and. val(cMultiplo) == 5 .and. !substr(cHora,4,2)$"00,05,10,15,20,25,30,35,40,45,50,55"
				cErro := STR0175 //"Os minutos precisam ser multiplos de 05"
				lRet  := .F.
			EndIf
			If !empty(cHora) .and. val(cMultiplo) == 10 .and. !substr(cHora,4,2)$"00,10,20,30,40,50"
				cErro := STR0176 //"Os minutos precisam ser multiplos de 10"
				lRet  := .F.
			EndIf
			If !empty(cHora) .and. val(cMultiplo) == 15 .and. !substr(cHora,4,2)$"00,15,30,45"
				cErro := STR0177 //"Os minutos precisam ser multiplos de 15"
				lRet  := .F.
			EndIf
			If !empty(cHora) .and. val(cMultiplo) == 20 .and. !substr(cHora,4,2)$"00,20,40"
				cErro := STR0178 //"Os minutos precisam ser multiplos de 20"
				lRet  := .F.
			EndIf
			If !empty(cHora) .and. val(cMultiplo) == 30 .and. !substr(cHora,4,2)$"00,30"
				cErro := STR0179 //"Os minutos precisam ser multiplos de 30"
				lRet  := .F.
			EndIf
		EndIf

		If !empty(cErro)
			Help(Nil,	Nil,alltrim(upper(Procname()))+"A",; //"RENTAL: "
				Nil,STR0171,1,0,Nil,Nil,Nil,Nil,Nil,; //"Inconsistência nos dados." 
				{cErro}) 
		EndIf
	EndIf

	// Validar os conflitos com os turnos
	If lRet .and. empty(cErro)
		For nX := 1 to len(aTabAux)
			If aTabAux[nX,NPOSMAR] == cMarca

				// Valida o conflito de horas com os turnos 
				If !LOCA005VL2(aTabAux[nX,4], cIni1, cFim1, aTabAux[nX,6])
					lRet := .F.
					Exit
				EndIf
			EndIf
		Next
	EndIf

RETURN lRet

/*/{PROTHEUS.DOC} LOCA00521
@DESCRIPTION FILTRO PARA CONSULTA PADRAO ST9ZBX.
@TYPE FUNCTION
@AUTHOR IT UP BUSINESS
@SINCE 06/12/2018
@VERSION 1.0
/*/
FUNCTION LOCA00521( _DDATA, _CHRINI, _CHRFIM, _CFROTAD  )
Local _CFILTRO  := ""
Local _AAREA	  := GETAREA()
Local _CFAMILIA := ""
Local AFILT9QRY := {}

	ST9->(DBSETORDER(1))
	IF ST9->(DBSEEK(XFILIAL("ST9")+_CFROTAD))
		_CFAMILIA := ST9->T9_CODFAMI
	ENDIF

	_CFILTRO := "	T9_CODBEM NOT IN(SELECT DISTINCT ZLG.FPO_FROTA "
	_CFILTRO += "	FROM " + RETSQLNAME("FPO") + " ZLG "
	_CFILTRO += "	WHERE '" + DTOS(_DDATA) + "' BETWEEN ZLG.FPO_DTINI AND ZLG.FPO_DTFIM "
	_CFILTRO += "		AND ( "
	_CFILTRO += "			(FPO_HRINI >= '" + STRTRAN(_CHRINI,":","") + "' AND FPO_HRINI < '" + STRTRAN(_CHRFIM,":","") + "') "
	_CFILTRO += "			OR (FPO_HRFIM > '" + STRTRAN(_CHRINI,":","") + "' AND FPO_HRFIM <= '" + STRTRAN(_CHRFIM,":","") + "') "
	_CFILTRO += "			OR (FPO_HRINI >= '" + STRTRAN(_CHRINI,":","") + "' AND FPO_HRFIM <= '" + STRTRAN(_CHRFIM,":","") + "') "
	_CFILTRO += "			OR (FPO_HRINI <= '" + STRTRAN(_CHRINI,":","") + "' AND FPO_HRFIM >= '" + STRTRAN(_CHRFIM,":","") + "')) "
	_CFILTRO += "		AND ZLG.D_E_L_E_T_ = '' "

	IF EXISTBLOCK("FILT9QRY")	// PONTO DE ENTRADA PARA INCLUSAO DE FILTRO NA BUSCA DO EQUIPAMENTO. RETORNO[WHERE ZLG, WHERE ST9]
		AFILT9QRY := EXECBLOCK("FILT9QRY", .T., .T., {_DDATA, _CHRINI, _CHRFIM, _CFROTAD})
	ENDIF

	IF (LEN(AFILT9QRY) > 0) .AND. (VALTYPE(AFILT9QRY[1]) == "C") .AND. !(EMPTY(AFILT9QRY[1]))
		_CFILTRO += " " + AFILT9QRY[1] + " "
	ENDIF

	_CFILTRO += "	) "

	IF !EMPTY(_CFAMILIA)
		_CFILTRO += "   AND T9_CODFAMI = '"+_CFAMILIA+"' "
	ENDIF 

	IF (LEN(AFILT9QRY) > 1) .AND. (VALTYPE(AFILT9QRY[2]) == "C") .AND. !(EMPTY(AFILT9QRY[2]))
		_CFILTRO += " " + AFILT9QRY[2] + " "
	ENDIF

	RESTAREA(_AAREA)

RETURN "@"+_CFILTRO		// #LOCA05922( FPF->FPF_DATA, FPF->FPF_HORAI, FPF->FPF_HORAF )

/*/{PROTHEUS.DOC} LOCA00522
@DESCRIPTION FILTRO PARA CONSULTA PADRAO ST9ZBX.
@TYPE FUNCTION
@AUTHOR IT UP BUSINESS
@SINCE 06/12/2018
@VERSION 1.0
/*/
FUNCTION LOCA00522( CPROJETO, CVIAGEM, DDTINCL, CCODCLI, CLOJCLI, CNOMCLI, CPEDCLI, CSOLIC, CBEM, CMOTORIS, CAJUDANT )
Local AAREAX   := GETAREA()
Local AAREAST9 := ST9->(GETAREA())
Local AAREAZA0 := FP0->(GETAREA())
Local AAREADA4 := DA4->(GETAREA())
Local LST9

DEFAULT DDTINCL  := DDATABASE
DEFAULT CCODCLI  := ""
DEFAULT CLOJCLI  := ""
DEFAULT CNOMCLI  := ""
DEFAULT CPEDCLI  := ""
DEFAULT CSOLIC   := ""
DEFAULT CMOTORIS := ""
DEFAULT CAJUDANT := ""

	IF ! EMPTY( CMOTORIS )
		DA4->(DBSETORDER(1))
		IF DA4->(DBSEEK(XFILIAL("DA4")+CMOTORIS))
			CMOTORIS := DA4->DA4_NOME
		ELSE
			CMOTORIS := ""
		ENDIF		
		DA4->(RESTAREA(AAREADA4))
	ENDIF

	IF ! EMPTY( CAJUDANT )
		DA4->(DBSETORDER(1))
		IF DA4->( DBSEEK( XFILIAL("DA4")+CAJUDANT ) )
			CAJUDANT := DA4->DA4_NOME
		ELSE
			CAJUDANT := ""
		ENDIF		
		DA4->(RESTAREA(AAREADA4))
	ENDIF

	IF EMPTY( CCODCLI )
		FP0->(DBSETORDER(1))
		IF FP0->( DBSEEK( XFILIAL("FP0")+CPROJETO ) )
			DDTINCL := FP0->FP0_DATINC
			CCODCLI := FP0->FP0_CLI
			CLOJCLI := FP0->FP0_LOJA
			CNOMCLI := FP0->FP0_CLINOM
		ENDIF
		FP0->(RESTAREA(AAREAZA0))
	ENDIF

	ST9->(DBSETORDER(1))
	LST9 := ST9->( DBSEEK( XFILIAL("ST9") + CBEM ) )

	FQA->(DBSETORDER(1))
	IF ! FQA->( DBSEEK(XFILIAL("FQA") + CVIAGEM) )
		RECLOCK("FQA",.T.)
		FQA->FQA_FILIAL := XFILIAL("FQA")
		FQA->FQA_VIAGEM := CVIAGEM 
		FQA->FQA_PROJET := CPROJETO
		FQA->FQA_DTINCL := DDTINCL
	ELSE
		RECLOCK("FQA",.F.) 
	ENDIF

	FQA->FQA_CODCLI := CCODCLI
	FQA->FQA_LOJCLI := CLOJCLI
	FQA->FQA_CLINOM := CNOMCLI

	IF !EMPTY(CPEDCLI) 
		FQA->FQA_PEDCLI := CPEDCLI
	ENDIF

	IF !EMPTY(CSOLIC) 
		FQA->FQA_SOLICT := CSOLIC
	ENDIF

	IF LST9
		FQA->FQA_VEICUL := ST9->T9_NOME
		FQA->FQA_PLACA  := ST9->T9_PLACA
	ENDIF

	FQA->FQA_NOMMOT := CMOTORIS
	FQA->FQA_NOMAJU := CAJUDANT

	FQA->(MSUNLOCK()) 

	ST9->(RESTAREA(AAREAST9))

	RESTAREA(AAREAX)

RETURN NIL


/*/{PROTHEUS.DOC} LOCA00525
ITUP BUSINESS - TOTVS RENTAL
identifica se utiliza o processo de minuta
Se houver algum registro na FPA que contenha um equipamento e o status deste equipamento na ST9 estiver em branco = Minuta
@AUTHOR Frank Fuga em 11/11/2022
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
Function LOCA00525(cContrato,cAs)
Local lMinuta     := .F.
Local aAreaFPA    := FPA->(GetArea())
Local aArea       := GetArea()

Default cContrato := ""
Default cAs       := ""

	If !empty(cContrato)
		FPA->(dbSetOrder(1))
		FPA->(dbSeek(xFilial("FPA")+cContrato))
		While !FPA->(Eof()) .and. FPA->FPA_FILIAL == xFilial("FPA") .and. FPA->FPA_PROJET == cContrato
			If !empty(cAs)
				If FPA->FPA_AS <> cAs
					FPA->(dbSkip())
					Loop
				EndIf
			EndIf
			If !empty(FPA->FPA_GRUA)
				ST9->(dbSetOrder(1))
				If ST9->(dbSeek(xFilial("ST9")+FPA->FPA_GRUA))
					If empty(ST9->T9_STATUS)
						lMinuta := .T.
						exit // se pelo menos um equipamento estiver com status em branco = minuta
					EndIF
				EndIF
			EndIf
			FPA->(dbSkip())
		EndDo
	EndIF
	FPA->(RestArea(aAreaFPA))
	RestArea(aArea)
Return lMinuta

/*/{PROTHEUS.DOC} LOCA00526
ITUP BUSINESS - TOTVS RENTAL
atualiza o status da medicao no momento da inclusao do pedido de vendas
As variaveis de area (posicionamento das tabelas estao no locm)
@AUTHOR Frank Fuga em 11/11/2022
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
Function LOCA00526()
Local _lMinuta  := .F.
Local _lMedicao := .F.
	
	_lMinuta  := LOCA00525(FPN->FPN_PROJET)
	_lMedicao := FWIsInCallStack("LOCA048")

	If _lMinuta .and. _lMedicao
		// Já estará posicionado na FPN - Tabela da medição
		FPP->(dbSetOrder(1)) // Tabela dos itens da medição
		FPP->(dbSeek(xFilial("FPP")+FPN->FPN_COD))
		While !FPP->(Eof()) .and. FPP->FPP_FILIAL == xFilial("FPP") .and. FPP->FPP_COD == FPN->FPN_COD
			If !empty(FPP->FPP_MINUTA)
				FPF->(dbSetOrder(2)) // Minuta pelo indice do codigo da minuta
				FPF->(dbSeek(xFilial("FPF")+FPP->FPP_MINUTA))
				While !FPF->(Eof()) .and. FPF->FPF_FILIAL == xFilial("FPF") .and. FPF->FPF_MINUTA == FPP->FPP_MINUTA
					FPF->(RecLock("FPF",.F.))
					FPF->FPF_STATUS := "4"
					FPF->(MsUnlock())
					FPF->(dbSkip())
				EndDo
			EndIF
			FPP->(dbSkip())
		EndDo
	EndIF
Return

/*/{PROTHEUS.DOC} LOCA00527
ITUP BUSINESS - TOTVS RENTAL
atualiza o status da medicao no momento da exclusao do pedido de vendas
As variaveis de area (posicionamento das tabelas estao no locm)
@AUTHOR Frank Fuga em 11/11/2022
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
Function LOCA00527()
Local _lMinuta  := .F.
Local _lMedicao := .F.

	_lMinuta  := LOCA00525(FPA->FPA_PROJET)
	_lMedicao := FWIsInCallStack("LOCA048")

	If _lMinuta .and. _lMedicao
		// Já estará posicionado na FPN - Tabela da medição
		FPP->(dbSetOrder(1)) // Tabela dos itens da medição
		FPP->(dbSeek(xFilial("FPP")+FPN->FPN_COD))
		While !FPP->(Eof()) .and. FPP->FPP_FILIAL == xFilial("FPP") .and. FPP->FPP_COD == FPN->FPN_COD
			If !empty(FPP->FPP_MINUTA)
				FPF->(dbSetOrder(2)) // Minuta pelo indice do codigo da minuta
				FPF->(dbSeek(xFilial("FPF")+FPP->FPP_MINUTA))
				While !FPF->(Eof()) .and. FPF->FPF_FILIAL == xFilial("FPF") .and. FPF->FPF_MINUTA == FPP->FPP_MINUTA
					FPF->(RecLock("FPF",.F.))
					FPF->FPF_STATUS := "6"
					FPF->(MsUnlock())
					FPF->(dbSkip())
				EndDo
			EndIF
			FPP->(dbSkip())
		EndDo
	EndIF
Return

/*/{PROTHEUS.DOC} MenuDef
ITUP BUSINESS - TOTVS RENTAL
MONTAGEM DO MENU
@AUTHOR Frank Fuga em 17/11/23
24/01/25 - MELHORIA NA ESTRUTURA DO FONTE - FRANK FUGA
/*/
Static Function MenuDef
Local lLOC005A := EXISTBLOCK("LOC005A")

	aRotina := {} 

	AADD(AROTINA,{STR0003,"AXVISUAL"    ,0,2}) //"VISUALIZAR"
	ADD OPTION aRotina Title STR0137	Action 'VIEWDEF.LOCA005'	OPERATION 3 ACCESS 0 		//"Manutenção"ADD OPTION aRotina Title STR0004	Action 'VIEWDEF.ATFA002' 	OPERATION 2 ACCESS 0 		//"Visualizar" //"Incluir"
	ADD OPTION aRotina Title STR0138	Action 'VIEWDEF.LOCA005'	OPERATION 4 ACCESS 0 		//"Manutenção" //"Alterar"
	AADD(AROTINA,{STR0004,"LOCA00503"   ,0,6}) //"LEGENDA"
	AADD(AROTINA,{STR0005,"LOCA00504"   ,0,6}) //"ACEITA MINUTA"
	AADD(AROTINA,{STR0006,"LOCA00505"   ,0,6}) //"BAIXA MINUTA"
	AADD(AROTINA,{STR0007,"LOCA00506"   ,0,6}) //"CANCELA MINUTA"
	AADD(AROTINA,{STR0008,"LOCA00507"   ,0,6}) //"ESTORNA MINUTA"
	AADD(AROTINA,{STR0132,"LOCA00508"   ,0,6}) //"TROCA EQUIPAMENTO"

	IF lLOC005A
		AROTINAUSU := EXECBLOCK("LOC005A",.F.,.F.,{AROTINA}) 
		IF VALTYPE(AROTINAUSU)=="A"
			AROTINA := ACLONE(AROTINAUSU)
		ENDIF
	ENDIF

Return aRotina

/*/{Protheus.doc} ModelDef
Model para edição de função
@type function
@version 23.10
@author Alexandre Circenis
@since 10/14/2024
@return Object, rModelo de Alteração de Minuta
/*/
Static Function ModelDef()
Local oStruFPF		:= FWFormStruct( 1, 'FPF' )
Local oModel		// Modelo de dados que será construído

	oStruFPF:AddField('SEQGRU' ,'SEQGRU'  ,'SEQGRU', 'C', TamSx3("FPA_SEQGRU")[1], 0 )
	oStruFPF:SetProperty("SEQGRU"   , MODEL_FIELD_INIT , {|| Posicione("FPA",3,xFilial("FPA")+FPF->FPF_AS,"FPA_SEQGRU")})

	oStruFPF:SetProperty("FPF_DATA" , MODEL_FIELD_OBRIGAT, .T.)
	oStruFPF:SetProperty("FPF_FROTA", MODEL_FIELD_OBRIGAT, .F.)
	oStruFPF:SetProperty("FPF_AS"   , MODEL_FIELD_OBRIGAT, .T.)
	oStruFPF:SetProperty("FPF_HORAI", MODEL_FIELD_OBRIGAT, .T.)
	oStruFPF:SetProperty("FPF_HORAF", MODEL_FIELD_OBRIGAT, .T.)
	//oStruFPF:SetProperty("FPF_TURNO", MODEL_FIELD_OBRIGAT, 6)
	oStruFPF:SetProperty("FPF_DATA" , MODEL_FIELD_WHEN , {|| INCLUI })
	oStruFPF:SetProperty("FPF_FROTA", MODEL_FIELD_WHEN , {|| INCLUI })
	oStruFPF:SetProperty("FPF_AS"   , MODEL_FIELD_WHEN , {|| INCLUI })
	oStruFPF:SetProperty("FPF_STATUS"   , MODEL_FIELD_INIT , {|| "1"})
	oStruFPF:SetProperty("FPF_MINUTA"   , MODEL_FIELD_INIT , {|| FPROXIMAM() })

	oStruFPF:SetProperty("FPF_DATA" , MODEL_FIELD_VALID, FwBuildFeature( STRUCT_FEATURE_VALID, 'Loca00528()'))
	oStruFPF:SetProperty("FPF_FROTA", MODEL_FIELD_VALID, FwBuildFeature( STRUCT_FEATURE_VALID, 'Loca00529()'))
	oStruFPF:SetProperty("FPF_AS"   , MODEL_FIELD_VALID, FwBuildFeature( STRUCT_FEATURE_VALID, 'Loca00530()'))
	oStruFPF:SetProperty("FPF_HORAI", MODEL_FIELD_VALID, FwBuildFeature( STRUCT_FEATURE_VALID, 'Loca00531()'))
	oStruFPF:SetProperty("FPF_HORAF", MODEL_FIELD_VALID, FwBuildFeature( STRUCT_FEATURE_VALID, 'Loca00531()'))
	oStruFPF:SetProperty("FPF_TURNO", MODEL_FIELD_VALID, FwBuildFeature( STRUCT_FEATURE_VALID, 'Vazio() .or. Loca00533()'))

	//oModel := MPFormModel():New( 'ATFA440' , { |oModel| AF440Pos(oModel) } )
	oModel := MPFormModel():New( 'LOCA005', , { |oModel| Loca00534(oModel) } )
	oModel:SetVldActivate({ |oModel| Loca00535(oModel)})
	//Antiga enchoice
	oModel:AddFields( 'FPFMASTER', /*cOwner*/, oStruFPF )
	//Descrição do modelo
	oModel:SetDescription( STR0139 ) 		//"Cadastro de operações com controle de aprovação" //"Manutenção de Minuta"

Return oModel

/*/{Protheus.doc} ViewDef
VIEW para edição da minuta
@type function
@version 23.10
@author Alexandre Circenis
@since 10/14/2024
@return Object, View para edição da minuta
/*/
Static Function ViewDef()
Local oModel 	:= FWLoadModel( 'LOCA005' ) 		// Cria um objeto de Modelo de dados baseado no ModelDef() do fonte informado
Local oStruFPF	:= FWFormStruct( 2, 'FPF' ,{|x| Alltrim(x) $ "FPF_DATA,FPF_FROTA,FPF_AS,FPF_HORAI,FPF_HORAF,FPF_TURNO"} ) 		// Cria a estrutura a ser usada na View
Local oView// Interface de visualização construída

	oStruFPF:SetProperty("FPF_DATA" , MVC_VIEW_ORDEM, 1)
	oStruFPF:SetProperty("FPF_FROTA", MVC_VIEW_ORDEM, 2)
	oStruFPF:SetProperty("FPF_AS"   , MVC_VIEW_ORDEM, 3)
	oStruFPF:SetProperty("FPF_HORAI", MVC_VIEW_ORDEM, 4)
	oStruFPF:SetProperty("FPF_HORAF", MVC_VIEW_ORDEM, 5)
	oStruFPF:SetProperty("FPF_TURNO", MVC_VIEW_ORDEM, 6)

	oStruFPF:SetProperty("FPF_AS"   , MVC_VIEW_LOOKUP, "FQ5AS3")
	oStruFPF:SetProperty("FPF_TURNO"   , MVC_VIEW_LOOKUP, "FPEFPF")

	// Cria o objeto de View
	oView := FWFormView():New()
	// Define qual o Modelo de dados será utilizado na View
	oView:SetModel( oModel )
	// Adiciona no nosso View um controle do tipo formulário
	oView:AddField( 'VIEWFPF', oStruFPF, 'FPFMASTER' )
	// Criar um "box" horizontal para receber algum elemento da view
	oView:CreateHorizontalBox( 'TELA' , 100 )
	// Relaciona o identificador (ID) da View com o "box" para exibição
	oView:SetOwnerView( 'VIEWFPF', 'TELA' )
	/* Desabilita o novo botao "salvar e criar novo" */
	oView:SetCloseOnOK({ || .T. })
	oView:SetAfterViewActivate({|oView|SetaSeqGRU(oView)})

Return oView

/*/{Protheus.doc} Loca00528
Valida a Data (REservado para implementação futura)
@type function
@version 1.0
@author Alexandre Circenis
@since 10/14/2024
@return Logical, .T. se a data for valida
/*/
Function Loca00528(  )
REturn .T.

/*/{Protheus.doc} Loca00529
Valida a Frota (Equipamento, Grua, etc...)
@type function
@version 1.0
@author Alexandre Circenis
@since 10/14/2024
@return logical, .T. se o valor informado for valido
/*/
Function Loca00529(  )
Local Lret := .T.
Local aArea := GetArea()
Local cFrota := &(ReadVar())

	// Verificar se a Frota Existe
	dbSelectArea("ST9")
	dbSetOrder(1)
	// Verificar se a Frota Existe
	if dbSeek(xFilial("ST9")+cFrota)

	// Frota deve estar sem status para usar na minuta
		if !Empty(ST9->T9_STATUS)
			Help(Nil,Nil,alltrim(upper(Procname()))+"A",; //"RENTAL: "
				Nil,STR0140,1,0,Nil,Nil,Nil,Nil,Nil,; //"Inconsistência nos dados." //"Frota está com o status preenchido não poderá ser usada em minutas."
				{STR0141}) //"Falta o preenchimento do cliente, ou do prospect." //"Informe uma frota valida ou análise o status de frota informada."
			lRet := .F.
		EndIF
	else
		Help(Nil,Nil,Alltrim(upper(Procname()))+"B",; //"RENTAL: "
			Nil,STR0142,1,0,Nil,Nil,Nil,Nil,Nil,; //"Inconsistência nos dados." //"Frota não encontrada no cadastro de Equipamentos."
			{STR0143}) //"Falta o preenchimento do cliente, ou do prospect." //"Informe uma frota valida ou cadastre a frota faltante."
		lret := .F.
	endif
	RestArea(aArea)
Return lRet

/*/{Protheus.doc} Loca00530
Valida a AS informado
@type function
@version 1.0
@author Alexandre Circenis
@since 10/14/2024
@return Logical, .t. se o valor informado for valido
/*/
Function Loca00530(  )
Local Lret := .T.
Local aArea := GetArea()
Local cAS:= &(ReadVar())
Local cFrota := FWFldGet("FPF_FROTA")

	// Verificar se a Frota foi preenchida
	if !Empty(cFrota)

		// Verificar se AS existe
		dbSelectArea("FPA")
		dbSetOrder(3)
	// Frota deve estar sem status para usar na minuta
		if !dbSeek(xFilial("FPA")+cAS)
			Help(Nil,Nil,alltrim(upper(Procname()))+"A",; //"RENTAL: "
				Nil,STR0144,1,0,Nil,Nil,Nil,Nil,Nil,; //"Inconsistência nos dados." //"AS não encontrada."
				{STR0145}) //"Falta o preenchimento do cliente, ou do prospect." //"Informe uma AS valida."
			lRet := .F.
		elseif FPA->FPA_GRUA <> cFrota
			Help(Nil,Nil,alltrim(upper(Procname()))+"B",; //"RENTAL: "
				Nil,STR0146,1,0,Nil,Nil,Nil,Nil,Nil,; //"Inconsistência nos dados." //"A Frota informada e Frota de AS são diferentes."
				{STR0147}) //"Falta o preenchimento do cliente, ou do prospect." //"Informe uma AS cuja a frota seja igual a frota informada."
			lRet := .F.
		endif
	//else
		// 26/08/25 ISSUE 7918 - Frank
		//Help(Nil,Nil,Alltrim(upper(Procname()))+"C",; //"RENTAL: "
		//	Nil,STR0148,1,0,Nil,Nil,Nil,Nil,Nil,; //"Inconsistência nos dados." //"Frota não preenchida."
		//	{STR0149}) //"Falta o preenchimento do cliente, ou do prospect." //"Preencha o campo FROTA antes de informar a AS."
		//lret := .F.
	endif

	if lRet // Se tudo OK preencher os campos Projeto / Obra
		FWFldPut("FPF_PROJET", FPA->FPA_PROJET )
		FWFldPut("FPF_OBRA", FPA->FPA_OBRA)
		FWFldPut("FPF_TIPOSE", FPA->FPA_TIPOSE)
		FWFldPut("SEQGRU", FPA->FPA_SEQGRU)
	endif

	RestArea(aArea)
Return lRet

/*/{Protheus.doc} Loca00531
Valida as horas informadas
@type function
@version 1.0
@author Alexandre Circenis
@since 10/14/2024
@return Logical, .t. se a hora informada for valida
/*/
Function Loca00531()
Local cCampo := ReadVar()
Local LRET		:= .T.
Local CHRINI	:= ""
Local CHRFIM	:= ""
Local LVLDHRTU	:= SUPERGETMV("MV_LOCX258",.F.,.T.)
Local cMultiplo := SUPERGETMV("MV_LOCX314",.F.,"")
Local cErro     := ""
Local cHora     := ""

	if ATVLDHORA(&(ReadVar())) .and. cHrIni < "2400" .and. cHrFim < "2400"

		IF CCAMPO == "M->FPF_HORAI"
			CHRINI := &(ReadVar())
			CHRFIM := FWFldGet("FPF_HORAF")
		ELSEIF CCAMPO == "M->FPF_HORAF"
			CHRINI := FWFldGet("FPF_HORAI")
			CHRFIM := &(ReadVar())
		ENDIF

		IF LVLDHRTU

			// Validação do conteúdo do parâmetro MV_LOCX314.
			If !empty(cMultiplo)
				If val(cMultiplo) <> 0 .and. val(cMultiplo) <> 5 .and. val(cMultiplo) <> 10 .and. ;
					val(cMultiplo) <> 15 .and. val(cMultiplo) <> 20 .and. val(cMultiplo) <> 30
					Help(Nil,Nil,alltrim(upper(Procname())),; 
					Nil,STR0171,1,0,Nil,Nil,Nil,Nil,Nil,; //"Inconsistência nos dados." 
					{STR0170}) //Ajuste o parâmetro MV_LOCX314. Os valoes aceitos são: 0, 5, 10, 15, ou 20.
					lRet := .F.
				EndIf
				If lRet
					If ReadVar() == "M->FPF_HORAI"
						cHora := cHrIni
					else
						cHora := cHrFim
					EndIf
					If !empty(cHora) .and. val(cMultiplo) == 0 .and. substr(cHora,3,2)<>"00"
						cErro := STR0174 //"Os minutos precisam ser 00"
						lRet  := .F.
					EndIf
					If !empty(cHora) .and. val(cMultiplo) == 5 .and. !substr(cHora,3,2)$"00,05,10,15,20,25,30,35,40,45,50,55"
						cErro := STR0175 //"Os minutos precisam ser multiplos de 05"
						lRet  := .F.
					EndIf
					If !empty(cHora) .and. val(cMultiplo) == 10 .and. !substr(cHora,3,2)$"00,10,20,30,40,50"
						cErro := STR0176 //"Os minutos precisam ser multiplos de 10"
						lRet  := .F.
					EndIf
					If !empty(cHora) .and. val(cMultiplo) == 15 .and. !substr(cHora,3,2)$"00,15,30,45"
						cErro := STR0177 //"Os minutos precisam ser multiplos de 15"
						lRet  := .F.
					EndIf
					If !empty(cHora) .and. val(cMultiplo) == 20 .and. !substr(cHora,3,2)$"00,20,40"
						cErro := STR0178 //"Os minutos precisam ser multiplos de 20"
						lRet  := .F.
					EndIf
					If !empty(cHora) .and. val(cMultiplo) == 30 .and. !substr(cHora,3,2)$"00,30"
						cErro := STR0179 //"Os minutos precisam ser multiplos de 30"
						lRet  := .F.
					EndIf
				EndIf

				If !empty(cErro)
					Help(Nil,	Nil,alltrim(upper(Procname()))+"A",; //"RENTAL: "
						Nil,STR0171,1,0,Nil,Nil,Nil,Nil,Nil,; //"Inconsistência nos dados." 
						{cErro}) 
				EndIF

			EndIF

			IF !EMPTY(CHRFIM) .AND. LRET .AND. !EMPTY(CHRINI)
				/*If cHrIni > "2400"
					cErro := STR0173 //"Horário indevido"
				EndIf
				If cHrFim > "2400"
					cErro := STR0173 //"Horário indevido"
				EndIf
				If cErro <> ""
					Help(Nil,	Nil,alltrim(upper(Procname()))+"A",; //"RENTAL: "
						Nil,STR0171,1,0,Nil,Nil,Nil,Nil,Nil,; //"Inconsistência nos dados." 
						{cErro}) 
					lRet := .F.
				EndIF*/

				// Valida o conflito de horas com os turnos
				//if lRet
					lRet := LOCA005VL2(M->FPF_DATA, CHRINI, CHRFIM, M->FPF_AS)
				//EndIf

			ENDIF
		EndIf

	else
		lRet := .F.
	endif
RETURN LRET

/*/{Protheus.doc} Loca00533
Valida o Turno
@type function
@version 1.0
@author Alexandre Circenis
@since 10/14/2024
@return Logical, .T. se for um turno valido
/*/
Function Loca00533()
Local lRet := .T.
Local cAS := FWFldGet("FPF_AS")
Local cTurno := &(ReadVar())
Local aArea := GetArea()

	if !Empty(cAS)

		dbSelectArea("FPA")
		dbSetOrder(3)
		if dbSeek(xFilial("FPA")+cAS)
			dbSelectArea("FPE")
			dbSetOrder(1)
			if !dbSeek(xFilial("FPE")+FPA->(FPA_PROJET + FPA_OBRA + FPA_GRUA + FPA_SEQGRU) + cTurno)
				Help(Nil,	Nil,alltrim(upper(Procname()))+"B",; //"RENTAL: "
						Nil,STR0153,1,0,Nil,Nil,Nil,Nil,Nil,; //"Inconsistência nos dados." //"Turno não encontrado na tabela de turnos para a AS informada."
						{STR0154}) //"A hora inicial deve ser diferente da hora final." //"Verificar a tabela de turnos ou a AS informada."
				lRet := .F.
			endif
		else
			Help(Nil,	Nil,alltrim(upper(Procname()))+"B",; //"RENTAL: "
					Nil,STR0144,1,0,Nil,Nil,Nil,Nil,Nil,; //"Inconsistência nos dados." //"AS não encontrada."
					{STR0155}) //"A hora inicial deve ser diferente da hora final." //"Verificar o contrato pois a AS não está no contrato."
			lRet := .F.
		endif
	else
		Help(Nil,	Nil,alltrim(upper(Procname()))+"B",; //"RENTAL: "
				Nil,STR0156,1,0,Nil,Nil,Nil,Nil,Nil,; //"Inconsistência nos dados." //"AS não não informada."
				{STR0157}) //"A hora inicial deve ser diferente da hora final." //"Para informar o turno a AS deve ser informada."
		lRet := .F.

	endif

	RestArea(aArea)

Return lRet

/*/{Protheus.doc} LOCA00534
Valida o horario da minuta para eitar conflitos com outra minuta
@type function
@version 1.0
@author Alexandre Circenis
@since 10/14/2024
@param oModel, object, Modelo ativo
@return Logical, .T. o horario informado não presenta conflito
/*/
Function LOCA00534( oModel)
Local aArea := GetArea()
Local _cQuery := ""
Local lAlterar := oModel:GetOperation() = MODEL_OPERATION_UPDATE
Local aBindParam := {}
Local dData  := FWFldGet("FPF_DATA")
Local cFrota := FWFldGet("FPF_FROTA")
Local cHoraI := FWFldGet("FPF_HORAI")
Local cHoraF := FWFldGet("FPF_HORAF")
Local lRet   := .T.
Local cRecnox := alltrim(Str( omodel:AALLSUBMODELS[1]:ndataid ))

	//Caso a minuta esteja ativada, verifica encavalamento do equipamento
		aBindParam := {}
		_CQUERY := " SELECT COUNT(FPF_FROTA) TOT FROM "+RETSQLNAME("FPF")+" "
		_CQUERY += " WHERE FPF_FILIAL = ?  "
		Aadd(aBindParam, FwxFilial("FPF") )
		_CQUERY += " AND FPF_DATA = ? "
		Aadd(aBindParam, Dtos(dData) )
		_cQuery += " AND FPF_HORAI < ? AND FPF_HORAF > ? "
		Aadd(aBindParam, alltrim(cHoraF) )
		Aadd(aBindParam, alltrim(cHoraI) )
		_cQuery += " AND FPF_FROTA = ?  "
		Aadd(aBindParam, cFrota )
		if lAlterar
			_CQUERY += " AND R_E_C_N_O_ <> ? "
			Aadd(aBindParam, cRecnox)
		endif
		_CQUERY += " AND D_E_L_E_T_ = '' "
		_cQuery := changequery(_cQuery)
		MPSysOpenQuery(_CQUERY,"TRBVLD",,,aBindParam)

		IF TRBVLD->(!EOF())
			If TRBVLD->TOT > 0
				Help(Nil,	Nil,"Rental"+" "+Alltrim(Upper(Procname())),; // Rental
				Nil,STR0158,1,0,Nil,Nil,Nil,Nil,Nil,; // Conflito de equipamentos //"Conflito de equipamento"
				{STR0159 + AllTrim(cFrota) +; // o equipamento: //"O equipamento "
				" - " + ALLTRIM(POSICIONE("ST9",1,FwxFilial("ST9")+cFrota,"T9_NOME")) + STR0160}) // //" já encontra-se em minuta."
				lRet := .F.
			EndIf
		ENDIF
		TRBVLD->(DBCLOSEAREA())
		RestArea(aArea)

Return lRet

/*/{Protheus.doc} LOCA00535
Validação para permitir a alteração de uma minuta
@type function
@version 1.0
@author Alexandre Circenis
@since 10/14/2024
@param oModel, object, Model ativo
@return logical, .t. permite a alteração da minuta
/*/
Function LOCA00535( oModel)
Local aArea := GetArea()
Local lAlterar := oModel:GetOperation() = MODEL_OPERATION_UPDATE
Local lRet := .T.

	If lAlterar .and. !(FPF_STATUS $ '12')
		Help(Nil,	Nil,"Rental"+" "+Alltrim(Upper(Procname())),; // Rental
		Nil,STR0161,1,0,Nil,Nil,Nil,Nil,Nil,; // Conflito de equipamentos //"Só se pode alterar uma minuta que estaja Prevista ou Confirmada"
		{STR0162}) //"Analise a minuta que se quer alterar."
		lRet := .F.
	EndIf

	RestArea(aArea)

Return lRet

/*/{Protheus.doc} FPROXIMAM
TRAZ o PRÓXIMo codigo de  MINUTA
@type function
@version 1.0
@author Alexandre Circenis
@since 10/14/2024
@return cRet, caracter , Codigo sequencial da minuta
*/
STATIC FUNCTION FPROXIMAM()
LOCAL AAREA    := GETAREA()
LOCAL AAREAZBX := FPF->(GETAREA())
LOCAL CRET

	FPF->( DBSETORDER(2) ) 			// FPF_FILIAL+FPF_MINUTA

	WHILE .T.
		CRET := GETSXENUM("FPF","FPF_MINUTA")
		IF ! FPF->( DBSEEK(XFILIAL("FPF")+CRET) )
			EXIT
		ELSE
			CONFIRMSX8()
		ENDIF
	ENDDO

	RESTAREA(AAREAZBX)
	RESTAREA(AAREA)

RETURN CRET

/*/{Protheus.doc} SETASEQGRU
Inicializa o cmapo virtual SEQGRU com o valor da sequencia da FPA
@type function
@version 1.0
@author Alexandre Circenis
@since 10/14/2024
/*/
STATIC FUNCTION SETASEQGRU(oView)
	FWFldPut("SEQGRU", Posicione("FPA",3,xFilial("FPA")+FPF->FPF_AS,"FPA_SEQGRU"))
RETURN

/*/{Protheus.doc} LOCA005VLD
Valida se existe uma minuta superior conflitando as datas
@type function
@author Frank Zwarg Fuga
@since 27/12/2024
/*/
Static Function LOCA005VLD(cHorai,cHoraf)
Local lRet := .T.
	Processa({|| lRet := LOCA005VLX(cHorai,cHoraf)},STR0180)
Return lRet

/*/{Protheus.doc} LOCA005VLX
Valida se existe uma minuta superior conflitando as datas
@type function
@author Frank Zwarg Fuga
@since 27/12/2024
/*/
Static Function LOCA005VLX(cHorai,cHoraf)
Local lRet := .T.
Local aAreaFPF := FPF->(GetArea())
Local cQuery 
Local aBindParam := {}
Local lErro := .F.
Local cRecnox := FPF->(Recno())
Local dDatax := FPF->FPF_DATA
Local cTurnox := FPF->FPF_TURNO
Local aBaixa := {}
Local aExiste := {}
Local nX
Local nY
Local nTemp
Local aAreaFPA := FPA->(GetArea())

	ProcRegua(0)

	cHorai := StrTran(cHorai, ":", "" )
	cHoraf := StrTran(cHoraf, ":", "" )
	If substr(cHoraf,1,2) == "00"
		cHoraf := "24" + substr(cHoraf,3,2)
	EndIf

	cQuery := "SELECT FPF.FPF_MINUTA, FPF.R_E_C_N_O_ REG, FPF.FPF_DATA, FPF.FPF_TURNO, " 
	cQuery += "FPF.FPF_HORAI, FPF.FPF_HORAF, FPF.FPF_FROTA, FPF.FPF_AS "
	cQuery += "FROM " + RETSQLNAME("FPF") + " FPF " 
	cQuery += "WHERE FPF_FILIAL = '"+xFilial("FPF")+"' " 
	cQuery += "AND FPF_AS = ? " 
	cQuery += "AND FPF_FROTA = ? " 
	cQuery += "AND FPF.D_E_L_E_T_ = ''"
	cQuery := CHANGEQUERY(cQuery)

	If Select("TRBFPFX") > 0 
		TRBFPFX->(dbCloseArea())
	EndIf

	aBindParam := {FPF->FPF_AS, FPF->FPF_FROTA}
	MPSysOpenQuery(cQuery,"TRBFPFX",,,aBindParam)

	While TRBFPFX->(!EOF())

		IncProc()
		SysRefresh()

		// Mão de obra não precisa validar horarios - Frank em 12/09/25
		FPA->(dbSetOrder(3))
		FPA->(dbSeek(xFilial("FPA")+TRBFPFX->FPF_AS))
		If FPA->FPA_TIPOSE == "M"
			TRBFPFX->(dbSkip())
			Loop
		EndIf

		lErro := .F.

		// Validar se o registro não é do recno posicionado
		// Validar se a data é igual, ou maior
		// Validar se o horário confita
		// Validar se é de um turno diferente

		If TRBFPFX->REG <> cRecnox
			If TRBFPFX->FPF_DATA == dtos(dDatax)
				If TRBFPFX->FPF_TURNO <> cTurnox
					
					aBaixa := {}
					aExiste := {}

					nTemp := val(TRBFPFX->FPF_HORAI)
					While nTemp <= val(TRBFPFX->FPF_HORAF) 			
						aadd(aExiste,{nTemp})
						nTemp ++
					EndDo
					
					nTemp := val(cHorai)
					While nTemp <= val(cHoraf) 			
						aadd(aBaixa,{nTemp})
						nTemp ++
					EndDo

					For nX := 1 to len(aBaixa)
						For nY := 1 to len(aExiste)
							// 17:00 == 17:00 e existe um 17:01, se não existir é válido no mesmo horário
							If aBaixa[nX,1] == aExiste[nY,1] .and. ( (nX + 1) <= len(aBaixa) ) .and. ( (nY + 1) <= len(aExiste) )
								lErro := .T.
								Exit
							EndIf
						Next
						If lErro
							Exit
						EndIf
					Next

				EndIf
			EndIf
		EndIf

		If lErro
			If empty(cErros)
				cErros := STR0169 //"Conflito de horário na baixa, minuta(s): "
			Else
				cErros += ", "
			EndIf
			cErros += alltrim(TRBFPFX->FPF_MINUTA)
			lRet := .F.
		EndIf

		TRBFPFX->(dbSkip())
	EndDo

	TRBFPFX->(dbCloseArea())
	
	FPA->(RestArea(aAreaFPA))
	RestArea(aAreaFPF)
Return lRet 


/*/{Protheus.doc} LOCA005VL2
Valida se existe uma minuta superior conflitando as datas e horarios com a FPE (Turnos)
@type function
@author Frank Zwarg Fuga
@since 27/01/2025
/*/
Static Function LOCA005VL2(dData, cHoraI, cHoraF, cAs)
Local lRet := .T.
Local lConflito := .F.
Local cConflito := ""
Local aAreaFPF := FPF->(GetArea("FPF"))

	// Identifica se a nova rotina de turnos esta ativa
	If LOCA005N()
		If !empty(dData) .and. !empty(cHoraI) .and. !empty(cHoraF) .and. !empty(cAs)
			FPF->(dbSetOrder(4))
			FPF->(dbSeek(xFilial("FPF")+cAs))
			While !FPF->(Eof()) .and. FPF->(FPF_FILIAL+FPF_AS) == xFilial("FPF")+cAs
				If !empty(FPF->FPF_TURNO)
					If cHoraF < FPF->FPF_HORAF .and. cHoraF > FPF->FPF_HORAI .and. FPF->FPF_DATA == dData
						lConflito := .T.
						cConflito := STR0172 //"Conflito de horas com minutas de turno em período superior"
						Exit
					EndIf
				EndIf
				FPF->(dbSkip())
			EndDo
			If lConflito
				lRet := .F.
				Help(Nil,	Nil,alltrim(upper(Procname()))+"A",; //"RENTAL: "
				Nil,STR0171,1,0,Nil,Nil,Nil,Nil,Nil,; //"Inconsistência nos dados." 
				{cConflito}) 
			EndIf
		EndIf
	EndIF
	FPF->(RestArea(aAreaFPF))

Return lRet


/*/{PROTHEUS.DOC} LOCA005N
ITUP BUSINESS - TOTVS RENTAL
VALIDA SE A VERSAO DOS TURNOS É A VALIDA
@TYPE FUNCTION
@AUTHOR FRANK ZWARG FUGA
@SINCE 23/01/2025
/*/
Static Function LOCA005N
Local aFields
Local nX
Local cCampo
Local cCBox := ""
Local lAtivo := .F.

	aFields := FWSX3Util():GetListFieldsStruct( "FPE" , .F.)
	For nX := 1 To Len( aFields )
		cCampo := GetSx3Cache(alltrim(aFields[nX][01]),"X3_CAMPO")
		If alltrim(cCampo) == "FPE_DIASEM"
       		cCbox := GetSx3Cache(alltrim(aFields[nX][01]),"X3_CBOX")
		EndIf
	Next nX

	If At("M=", cCbox) > 0
		lAtivo := .T.
	EndIf

Return lAtivo  


// Passagem no advpr
Function LOCA005X1
Local lRet := .T.
Return lRet
