#INCLUDE "loca020.ch" 
#INCLUDE "PROTHEUS.CH"

/*/{PROTHEUS.DOC} LOCA020.PRW 
ITUP BUSINESS - TOTVS RENTAL
CALENDÁRIO FINANCEIRO PARA CONTROLE DE PERÍODOS DE LANÇAMENTOS DAS MEDIÇÕES
@TYPE FUNCTION
@AUTHOR FRANK ZWARG FUGA
@SINCE 03/12/2020
@VERSION P12
@HISTORY 03/12/2020, FRANK ZWARG FUGA, FONTE PRODUTIZADO.
/*/

FUNCTION LOCA020()
PRIVATE CCADASTRO := STR0001 //"CALENDÁRIO FINANCEIRO - MEDIÇÕES"
PRIVATE CDELFUNC  := ".T."
PRIVATE CSTRING   := "FQ0"

DBSELECTAREA(CSTRING)

PRIVATE AROTINA   := {{STR0002 ,"AXPESQUI"  ,0,1},; //"PESQUISAR"
                      {STR0003 ,"AXVISUAL"  ,0,2},; //"VISUALIZAR"
                      {STR0004 ,"LOCA02001" ,0,3},; //"INCLUIR"
                      {STR0005 ,"LOCA02001" ,0,4},; //"ALTERAR"
                      {STR0006 ,"AXDELETA"  ,0,5},; //"EXCLUIR"
                      {STR0007 ,"LOCA02001" ,0,6}} //"LEGENDA"

PRIVATE ACORES    := {{ "FQ0_STATUS=='A'", "BR_VERDE" },;
                      { "FQ0_STATUS=='B'", "BR_PRETO" }}

	MBROWSE( 6,1,22,75,CSTRING,,,,,,ACORES)

RETURN NIL



// ======================================================================= \\
FUNCTION LOCA02001(CALIAS, NREG, NOPC )
	// ======================================================================= \\
	// --> ROTINA DE INCLUSÃO, ALTERAÇÃO E LEGENDA DO CALENDÁRIO FINANCEIRO.
	DO CASE
	CASE NOPC == 3
		AXINCLUI(CALIAS, NREG, NOPC,,,,"LOCA02002(NREG, NOPC)")
	CASE NOPC == 4
		AXALTERA(CALIAS, NREG, NOPC,,,,,"LOCA02002(NREG, NOPC)")
	OTHERWISE
		BRWLEGENDA(STR0007 , CCADASTRO , {{ "BR_VERDE" , STR0008   },; //"LEGENDA"###"PERÍODO ABERTO"
											{ "BR_PRETO" , STR0009}}) //"PERÍODO BLOQUEADO"
	ENDCASE

RETURN NIL



// ======================================================================= \\
FUNCTION LOCA02002(NREG, NOPC)
// ======================================================================= \\
// --> VALIDAÇÕES DE INCLUSÃO E ALTERAÇÃO DO CALENDÁRIO FINANCEIRO
LOCAL LRET      := .T.
LOCAL CALIASTMP := GETNEXTALIAS()
Local CQUERY

	IF M->FQ0_DTINI > M->FQ0_DTFIM
		MSGALERT(STR0010 , STR0011)  //"DATA INVÁLIDA - A DATA DE TÉRMINO DEVE SER MAIOR QUE A DATA INICIAL"###"GPO - LCCALFIN.PRW"
		RETURN .F.
	ENDIF

	/*
	+DTOS(M->FQ0_DTINI)+
	+DTOS(M->FQ0_DTFIM)+
	STR( NREG )
	*/
	
	CQUERY     := " SELECT * FROM " + RETSQLNAME("FQ0") + " ZZF"
	CQUERY     += " WHERE  FQ0_FILIAL='"+XFILIAL("FQ0")+"'"
	CQUERY     +=   " AND  ( ? BETWEEN FQ0_DTINI AND FQ0_DTFIM  "
	CQUERY     +=     " OR ? BETWEEN FQ0_DTINI AND FQ0_DTFIM) "
	IF NOPC == 4
		CQUERY +=   " AND  ZZF.R_E_C_N_O_ != ? "
	ENDIF
	CQUERY     +=   " AND  ZZF.D_E_L_E_T_ = ''"
	CQUERY := CHANGEQUERY(CQUERY) 
	
	IF NOPC == 4
		aBindParam := {DTOS(M->FQ0_DTINI),DTOS(M->FQ0_DTFIM),STR( NREG )}
	else
		aBindParam := {DTOS(M->FQ0_DTINI),DTOS(M->FQ0_DTFIM)}
	ENDIF

	CALIASTMP := MPSysOpenQuery(cQuery,,,,aBindParam)
	//DBUSEAREA(.T.,"TOPCONN",TCGENQRY(,,CQUERY),CALIASTMP,.T.,.T.)
	TCSETFIELD(CALIASTMP, "FQ0_DTINI", "D",8,0)
	TCSETFIELD(CALIASTMP, "FQ0_DTFIM", "D",8,0)

	IF ! (CALIASTMP)->(EOF())
		MSGALERT(STR0012+DTOC((CALIASTMP)->FQ0_DTINI)+" A "+DTOC((CALIASTMP)->FQ0_DTFIM) , STR0011)  //"ESTE PERÍODO CONFLITA COM O PERÍODO "###"GPO - LCCALFIN.PRW"
		LRET := .F.
	ENDIF
	DBCLOSEAREA()

	DBSELECTAREA("FQ0")

RETURN LRET



// ======================================================================= \\
FUNCTION LOCA02003(DMEDICAO)
// ======================================================================= \\
// --> ROTINA DE VERIFICAÇÃO DA MEDIÇÃO COM O CALENDÁRIO FINANCEIRO
LOCAL LRET      := .T. 
LOCAL AAREA     := GETAREA() 
LOCAL CALIASTMP := GETNEXTALIAS() 
Local CQUERY

	/*
	+DTOS(DMEDICAO)+
	*/

	CQUERY := " SELECT FQ0_STATUS " 
	CQUERY += " FROM " + RETSQLNAME("FQ0") + " ZZF "
	CQUERY += " WHERE  FQ0_FILIAL='"+XFILIAL("FQ0")+"' "
	CQUERY +=   " AND  ? BETWEEN FQ0_DTINI AND FQ0_DTFIM "
	CQUERY +=   " AND  ZZF.D_E_L_E_T_ = '' "
	CQUERY := CHANGEQUERY(CQUERY) 
	aBindParam := {DTOS(DMEDICAO)}
	CALIASTMP := MPSysOpenQuery(cQuery,,,,aBindParam)
	
	//DBUSEAREA(.T.,"TOPCONN",TCGENQRY(,,CQUERY),CALIASTMP,.T.,.T.)
	TCSETFIELD(CALIASTMP , "FQ0_DTINI" , "D" , 8 , 0) 
	TCSETFIELD(CALIASTMP , "FQ0_DTFIM" , "D" , 8 , 0) 

	IF (CALIASTMP)->(EOF())
		MSGALERT(STR0013 , STR0011)  //"MEDIÇÃO FORA DO PERÍODO FINANCEIRO - PERÍODO NÃO EXISTE!"###"GPO - LCCALFIN.PRW"
		LRET := .F.
	ELSE
		IF (CALIASTMP)->FQ0_STATUS == "B"
			MSGALERT(STR0014 , STR0011) //"ESTE PERÍODO ESTÁ BLOQUEADO NO FINANCEIRO!"###"GPO - LCCALFIN.PRW"
			LRET := .F.
		ENDIF
	ENDIF
	(CALIASTMP)->(DBCLOSEAREA())

	RESTAREA(AAREA)

RETURN LRET
