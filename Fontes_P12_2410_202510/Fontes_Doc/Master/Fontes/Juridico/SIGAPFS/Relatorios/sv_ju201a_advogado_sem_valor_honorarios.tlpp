#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.JU201a.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NUE,RD0,NX0,NW0,NUU,NVE,NUS,NRF,NRN',;
	name='Relatório de advogados sem valor de honorarios',;
    country='ALL',;
    initialRelease="12.1.2210")

//------------------------------------------------------------------------------------
class JU201aTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()       as object
	public    method getData()   as object
	public    method getSchema() as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as Array
endclass

//------------------------------------------------------------------------------------
method new() class JU201aTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Advogados sem valor de honorários")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Advogados sem valor de honorários")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class JU201aTReportsBusinessObject
Local cQuery  := ""
Local aBind   := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := " SELECT DISTINCT #QueryFields#"
	cQuery +=   " FROM " + RetSQLName("NUE") + " NUE" 

	// RD0
	cQuery += " INNER JOIN " + RetSQLName("RD0") + " RD0"
	cQuery +=    " ON RD0.RD0_FILIAL = NUE.NUE_FILIAL"
	cQuery +=   " AND RD0.RD0_CODIGO = NUE.NUE_CPART2"
	cQuery +=   " AND RD0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NX0
	cQuery += " INNER JOIN " + RetSQLName("NX0") + " NX0" 

	// NW0
	cQuery += " INNER JOIN " + RetSQLName("NW0") + " NW0"
	cQuery +=   " ON NW0.NW0_FILIAL = NX0.NX0_FILIAL"
	cQuery +=   " AND NW0.NW0_PRECNF = NX0.NX0_COD"
	cQuery +=   " AND NW0.D_E_L_E_T_ = NX0.D_E_L_E_T_" 
	cQuery +=    " ON NW0.NW0_FILIAL = NUE.NUE_FILIAL"
	cQuery +=   " AND NW0.NW0_CTS = NUE.NUE_COD"
	cQuery +=   " AND NW0.D_E_L_E_T_ = NUE.D_E_L_E_T_"

	// NUU
	cQuery += " LEFT OUTER JOIN " + RetSQLName("NUU") + " NUU" 
	cQuery +=    " ON NUU.NUU_FILIAL = ?"

	AAdd(aBind, {xFilial("NUU"), "S"})

	cQuery +=   " AND NUU.NUU_CCLIEN = NUE.NUE_CCLIEN"
	cQuery +=   " AND NUU.NUU_CLOJA = NUE.NUE_CLOJA"
	cQuery +=   " AND NUU.NUU_CCASO = NUE.NUE_CCASO"
	cQuery +=   " AND NUU.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NVE
	cQuery += " INNER JOIN " + RetSQLName("NVE") + " NVE" 
	cQuery +=    " ON NVE.NVE_FILIAL = ?"

	AAdd(aBind, {xFilial("NVE"), "S"})

	cQuery +=   " AND NVE.NVE_CCLIEN = NUE.NUE_CCLIEN"
	cQuery +=   " AND NVE.NVE_LCLIEN = NUE.NUE_CLOJA"
	cQuery +=   " AND NVE.NVE_NUMCAS = NUE.NUE_CCASO"
	cQuery +=   " AND NVE.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NUS
	cQuery += " LEFT OUTER JOIN " + RetSQLName("NUS") + " NUS"
	cQuery +=   "  ON NUS.NUS_FILIAL = ?"

	AAdd(aBind, {xFilial("NUS"), "S"})

	cQuery +=   " AND NUS.NUS_CPART = NUE.NUE_CPART2"
	cQuery +=   " AND NUS.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NRF
	cQuery += " LEFT OUTER JOIN " + RetSQLName("NRF") + " NRF"
	cQuery +=   "  ON NRF.NRF_FILIAL = ?"

	AAdd(aBind, {xFilial("NRF"), "S"})

	cQuery +=   " AND NRF.NRF_COD = NUU.NUU_CTABH"
	cQuery +=   " AND NRF.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NRN
	cQuery += " LEFT OUTER JOIN " + RetSQLName("NRN") + " NRN"
	cQuery +=    " ON NRN.NRN_FILIAL = NUS.NUS_FILIAL"
	cQuery +=   " AND NRN.NRN_COD = NUS.NUS_CCAT"
	cQuery +=   " AND NRN.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// WHERE
	cQuery += " WHERE NUE.NUE_FILIAL = ?"

	AAdd(aBind, {xFilial("NUE"), "S"})

	cQuery +=   " AND NUE.NUE_ANOMES BETWEEN NUU.NUU_AMINI AND CASE WHEN NUU.NUU_AMFIM = ? THEN ? ELSE NUU.NUU_AMFIM END"

	AAdd(aBind, {' ', "S"})
	AAdd(aBind, {AnoMes(Date()), "S"})

	cQuery +=   " AND NUE.NUE_ANOMES BETWEEN NUS.NUS_AMINI AND CASE WHEN NUS.NUS_AMFIM = ? THEN ? ELSE NUS.NUS_AMFIM END"

	AAdd(aBind, {' ', "S"})
	AAdd(aBind, {AnoMes(Date()), "S"})

	//Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf

	cQuery +=   " AND NUE.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)

	self:setOrder("NUE_CCASO")
	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class JU201aTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class JU201aTReportsBusinessObject
Local aFields := {}

	Aadd(aFields,{"NRF.NRF_COD"                                   , Nil            })
	Aadd(aFields,{"NRF.NRF_DESC"                                  , Nil            })
	Aadd(aFields,{"NUE.NUE_CCASO"                                 , Nil            })
	Aadd(aFields,{"NUE.NUE_ANOMES"                                , Nil            })
	Aadd(aFields,{"NX0.NX0_COD"                                   , Nil            })
	Aadd(aFields,{"NUE.NUE_CCLIEN"                                , Nil            })
	Aadd(aFields,{"NUE.NUE_CLOJA"                                 , Nil            })
	Aadd(aFields,{"NVE.NVE_TITULO"                                , Nil            })
	Aadd(aFields,{"NRN.NRN_COD"                                   , Nil            })
	Aadd(aFields,{"NRN.NRN_DESC"                                  , Nil            })
	Aadd(aFields,{"RD0.RD0_SIGLA"                                 , Nil            })
	Aadd(aFields,{"RD0.RD0_NOME"                                  , Nil            })
	Aadd(aFields,{"NUS.NUS_AMINI"                                 , Nil            })
	Aadd(aFields,{"NUS.NUS_AMFIM"                                 , Nil            })
	Aadd(aFields,{"NUU.NUU_AMINI"                                 , Nil            })
	Aadd(aFields,{"NUU.NUU_AMFIM"                                 , Nil            })
	Aadd(aFields,{"NUE.NUE_CPART2"                                , Nil            })

Return aFields
