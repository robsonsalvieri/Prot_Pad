#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju201_limite_por_fatura.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NT0,NX8,NX0,NS7,CTO,NXR',;
	name='Relatório de Pré Fatura - SubReport Limite por Fatura',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju201_limite_por_faturaTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array

	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class ju201_limite_por_faturaTReportsBusinessObject

	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Pré Fatura - SubReport Limite por Fatura")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Pré Fatura - SubReport Limite por Fatura")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju201_limite_por_faturaTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		cQuery := "SELECT #QueryFields#"

		// NX8
		cQuery += " FROM " + RetSqlName("NX8") + " NX8"

		// NT0
		cQuery += " INNER JOIN " + RetSqlName("NT0") + " NT0"
		cQuery +=    " ON NT0.NT0_FILIAL = ?" // 1

		AAdd(aBind, {xFilial("NT0"), "S"})

		cQuery +=   " AND NT0.NT0_COD = NX8.NX8_CCONTR"
		cQuery +=   " AND NT0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// CTO_LIM
		cQuery += " LEFT OUTER JOIN " + RetSqlName("CTO") + " CTO_LIM"
		cQuery +=   " ON CTO_LIM.CTO_FILIAL = ?" // 2

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=  " AND CTO_LIM.CTO_MOEDA = NT0.NT0_CMOELI"
		cQuery +=  " AND CTO_LIM.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})
	
		// NXR_LIM
		cQuery += " LEFT OUTER JOIN " + RetSqlName("NXR") + " NXR_LIM"
		cQuery +=   " ON NXR_LIM.NXR_FILIAL = ?" // 3

		AAdd(aBind, {xFilial("NXR"), "S"})

		cQuery +=  " AND NXR_LIM.NXR_CMOEDA = NT0.NT0_CMOELI"
		cQuery +=  " AND NX8.NX8_CPREFT = NXR_LIM.NXR_CPREFT"
		cQuery +=  " AND NXR_LIM.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NX0
		cQuery += " INNER JOIN " + RetSqlName("NX0") + " NX0"
		cQuery +=   " ON NX0.NX0_FILIAL = ?" // 4

		AAdd(aBind, {xFilial("NX0"), "S"})

		cQuery +=  " AND NX0.NX0_COD = NX8.NX8_CPREFT"
		cQuery +=  " AND NX0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NS7_CTOPRE
		cQuery += " INNER JOIN " + RetSqlName("NS7") + " NS7_CTOPRE"
		cQuery +=    " ON NS7_CTOPRE.NS7_FILIAL = ?" // 5

		AAdd(aBind, {xFilial("NS7"), "S"})

		cQuery +=   " AND NS7_CTOPRE.NS7_COD = NX0.NX0_CESCR"
		cQuery +=   " AND NS7_CTOPRE.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NS7_CTOLIM
		cQuery += " INNER JOIN " + RetSqlName("NS7") + " NS7_CTOLIM"
		cQuery +=    " ON NS7_CTOLIM.NS7_FILIAL = ?" // 6

		AAdd(aBind, {xFilial("NS7"), "S"})

		cQuery +=   " AND NS7_CTOLIM.NS7_COD = NX0.NX0_CESCR"
		cQuery +=   " AND NS7_CTOLIM.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// CTO_PRE
		cQuery += " LEFT OUTER JOIN " + RetSqlName("CTO") + " CTO_PRE"
		cQuery +=   " ON CTO_PRE.CTO_FILIAL = ?" // 7

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=  " AND CTO_PRE.CTO_MOEDA = NX0.NX0_CMOEDA"
		cQuery +=  " AND CTO_PRE.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NXR_PRE
		cQuery += " LEFT OUTER JOIN " + RetSqlName("NXR") + " NXR_PRE"
		cQuery +=   " ON NXR_PRE.NXR_FILIAL = ?" // 8

		AAdd(aBind, {xFilial("NXR"), "S"})

		cQuery +=  " AND NXR_PRE.NXR_CPREFT = NX0.NX0_COD"
		cQuery +=  " AND NXR_PRE.NXR_CMOEDA = NX0.NX0_CMOEDA"
		cQuery +=  " AND NX0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// WHERE
		cQuery += " WHERE"
		cQuery +=   " NX8.NX8_FILIAL = ?" // 9

		AAdd(aBind, {xFilial("NX8"), "S"})

		If oFilter:hasFilter()
			cQuery += " AND ?"

			AAdd(aBind, {oFilter:getSQLExpression(), "U"})
		EndIf
		
		cQuery +=   " AND NX8.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)

		self:setQuery(cQuery)
	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju201_limite_por_faturaTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju201_limite_por_faturaTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NT0.NT0_VLRLIF"   , Nil})
	Aadd(aFields, {"NX8.NX8_CCONTR"   , Nil})
	Aadd(aFields, {"NX8.NX8_CPREFT"   , Nil})
	Aadd(aFields, {"NX8.NX8_VUTFAT"   , Nil})
	Aadd(aFields, {"NX8.NX8_VSLDPX"   , Nil})
	Aadd(aFields, {"NX8.NX8_VTS"      , Nil})
    Aadd(aFields, {"NX8.NX8_VTSVIN"   , Nil})
    Aadd(aFields, {"NX8.NX8_VTSNC"    , Nil})
	Aadd(aFields, {"CTO_LIM.CTO_SIMB" , "CTOLIM_SIMB" })
	Aadd(aFields, {"CTO_LIM.CTO_MOEDA", "CTOLIM_MOEDA"})
	Aadd(aFields, {"NXR_LIM.NXR_COTAC", "NXRLIM_COTAC"})
	Aadd(aFields, {"CTO_PRE.CTO_SIMB" , "CTOPRE_SIMB" })
	Aadd(aFields, {"CTO_PRE.CTO_MOEDA", "CTOPRE_MOEDA"})
	Aadd(aFields, {"NXR_PRE.NXR_COTAC", "NXRPRE_COTAC"})
Return aFields
