#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju201_adiantamento.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='SE1,NXG,NWF,CTO',;
	name='Relatório de Pré Fatura - SubReport Adiantamento',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju201_adiantamentoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array

	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class ju201_adiantamentoTReportsBusinessObject

	_Super:new()

	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Pré Fatura - SubReport Adiantamento")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Pré Fatura - SubReport Adiantamento")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju201_adiantamentoTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		cQuery := "SELECT #QueryFields#"

		// NXG
		cQuery += " FROM " + RetSQLName("NXG") + " NXG" 

		// NWF
		cQuery += " INNER JOIN " + RetSQLName("NWF") + " NWF" 
		cQuery +=    " ON NWF.NWF_FILIAL = ?" // 1

		AAdd(aBind, {xFilial("NWF"), "S"})

		cQuery +=   " AND NWF.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// SE1
		cQuery += " INNER JOIN " + RetSQLName("SE1") + " SE1" 
		cQuery +=    " ON SE1.E1_CLIENTE = NXG.NXG_CLIPG"
		cQuery +=   " AND SE1.E1_LOJA = NXG.NXG_LOJAPG"
		cQuery +=   " AND SE1.E1_NUM = NWF.NWF_TITULO"
		cQuery +=   " AND SE1.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// CTO
		cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO"
		cQuery +=    " ON CTO.CTO_FILIAL = ?" // 2

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=   " AND CTO.CTO_MOEDA = NWF.NWF_CMOE"
		cQuery +=   " AND CTO.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// WHERE
		cQuery += " WHERE"
		cQuery +=   " NXG.NXG_FILIAL = ?" // 3

		AAdd(aBind, {xFilial("NXG"), "S"})

		cQuery +=   " AND SE1.E1_TIPO = ?"

		AAdd(aBind, {"RA", "S"})

		If oFilter:hasFilter()
			cQuery += " AND ?"

			AAdd(aBind, {oFilter:getSQLExpression(), "U"})
		EndIf
		
		cQuery +=   " AND NXG.D_E_L_E_T_= ?"

		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)

		self:setQuery(cQuery)
	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju201_adiantamentoTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju201_adiantamentoTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"SE1.E1_TIPO"   , Nil})
	Aadd(aFields, {"SE1.E1_LOJA"   , Nil})
	Aadd(aFields, {"SE1.E1_SALDO"  , Nil})
	Aadd(aFields, {"SE1.E1_CLIENTE", Nil})
	Aadd(aFields, {"CTO.CTO_SIMB"  , Nil})
	Aadd(aFields, {"CTO.CTO_MOEDA" , Nil})
	Aadd(aFields, {"NXG.NXG_CPREFT", Nil})

Return aFields
