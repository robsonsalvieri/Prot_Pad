#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju201_valor_minimo.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NX0,CTO,NS7,NT1,NT0',;
	name='Relatório de Pré Fatura - SubReport Valor Mínimo',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju201_valor_minimoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array

	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class ju201_valor_minimoTReportsBusinessObject

	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Pré Fatura - SubReport Valor Mínimo")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Pré Fatura - SubReport Valor Mínimo")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju201_valor_minimoTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		cQuery := "SELECT #QueryFields#"

		// NT0
		cQuery += " FROM " + RetSQLName("NT0") + " NT0" 

		// CTO
		cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO" 
		cQuery +=    " ON CTO.CTO_FILIAL = ?" // 1

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=   " AND CTO.CTO_MOEDA = NT0.NT0_CMOEF"
		cQuery +=   " AND CTO.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"}) 

		// NX2
		cQuery += " INNER JOIN " + RetSQLName("NX2") + " NX2"
		cQuery +=    " ON NX2.NX2_FILIAL = ?" // 2

		AAdd(aBind, {xFilial("NX2"), "S"})

		cQuery +=   " AND NX2.NX2_CCONTR = NT0.NT0_COD"
		cQuery +=   " AND NX2.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// WHERE
		cQuery +=  " WHERE NT0.NT0_FILIAL = ?" // 3

		AAdd(aBind, {xFilial("NT0"), "S"})

		If oFilter:hasFilter()
			cQuery += " AND ?"

			AAdd(aBind, {oFilter:getSQLExpression(), "U"})
		EndIf

		cQuery +=    " AND NT0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)

		// ORDER BY
		self:setOrder("NT0_COD")

		self:setQuery(cQuery)
	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju201_valor_minimoTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju201_valor_minimoTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NT0.NT0_COD"   , Nil})
	Aadd(aFields, {"NT0.NT0_CIDIO" , Nil})
	Aadd(aFields, {"NT0.NT0_LIMEXH", Nil})
	Aadd(aFields, {"NT0.NT0_VLRBAS", Nil})
	Aadd(aFields, {"NT0.NT0_TPCEXC", Nil})
	Aadd(aFields, {"NT0.NT0_FIXEXC", Nil})
	Aadd(aFields, {"NX2.NX2_UTR"   , Nil})
	Aadd(aFields, {"NX2.NX2_UTCLI" , Nil})
	Aadd(aFields, {"NX2.NX2_CPREFT", Nil})
	Aadd(aFields, {"NX2.NX2_VALOR1", Nil})
	Aadd(aFields, {"CTO.CTO_SIMB"  , Nil})

Return aFields
