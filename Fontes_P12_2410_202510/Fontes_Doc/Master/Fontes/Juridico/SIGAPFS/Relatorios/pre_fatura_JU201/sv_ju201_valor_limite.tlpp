#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju201_valor_limite.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NX0,NX8,NX1,CTO,NXR,NT0',;
	name='Relatório de Pré Fatura - SubReport Valor Limite',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju201_valor_limiteTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array

	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class ju201_valor_limiteTReportsBusinessObject

	_Super:new()

	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Pré Fatura - SubReport Valor Limite")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Pré Fatura - SubReport Valor Limite")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju201_valor_limiteTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		cQuery := "SELECT #QueryFields#"

		// NX0
		cQuery += " FROM " + RetSqlName("NX0") + " NX0" 

		// NX8
		cQuery += " INNER JOIN " + RetSqlName("NX8") + " NX8"
		cQuery +=    " ON NX8.NX8_FILIAL = ?" // 1

		AAdd(aBind, {xFilial("NX8"), "S"})

		cQuery +=   " AND NX8.NX8_CPREFT = NX0.NX0_COD"
		cQuery +=   " AND NX8.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NX1
		cQuery += " INNER JOIN " + RetSqlName("NX1") + " NX1" 
		cQuery +=    " ON NX1.NX1_FILIAL = ?" // 2

		AAdd(aBind, {xFilial("NX1"), "S"})

		cQuery +=   " AND NX1.NX1_CPREFT = NX0.NX0_COD"
		cQuery +=   " AND NX1.NX1_CCONTR = NX8.NX8_CCONTR" 
		cQuery +=   " AND NX1.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// CTO_PRE
		cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO_PRE"
		cQuery +=    " ON CTO_PRE.CTO_FILIAL = ?" // 3

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=   " AND NX0.NX0_CMOEDA=CTO_PRE.CTO_MOEDA"
		cQuery +=   " AND CTO_PRE.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NXR_PRE
		cQuery += " LEFT OUTER JOIN " + RetSqlName("NXR") + " NXR_PRE"
		cQuery +=   " ON NXR_PRE.NXR_FILIAL = ?" // 4

		AAdd(aBind, {xFilial("NXR"), "S"})

		cQuery +=  " AND NXR_PRE.NXR_CPREFT = NX0.NX0_COD"
		cQuery +=  " AND NXR_PRE.NXR_CMOEDA = NX0.NX0_CMOEDA"
		cQuery +=  " AND NXR_PRE.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NT0
		cQuery += " INNER JOIN " + RetSqlName("NT0") + " NT0"
		cQuery +=    " ON NT0.NT0_FILIAL = ?" // 5

		AAdd(aBind, {xFilial("NT0"), "S"})

		cQuery +=   " AND NT0.NT0_COD = NX8.NX8_CCONTR"
		cQuery +=   " AND NT0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NXR_LIM
		cQuery += " LEFT OUTER JOIN " + RetSqlName("NXR") + " NXR_LIM"
		cQuery +=   " ON NXR_LIM.NXR_FILIAL = ?" // 6

		AAdd(aBind, {xFilial("NXR"), "S"})

		cQuery +=  " AND NXR_LIM.NXR_CPREFT = NX8.NX8_CPREFT"
		cQuery +=  " AND NXR_LIM.NXR_CMOEDA = NT0.NT0_CMOELI"
		cQuery +=  " AND NXR_LIM.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// CTO_LIM
		cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO_LIM"
		cQuery +=    " ON CTO_LIM.CTO_FILIAL = ?" // 7

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=   " AND CTO_LIM.CTO_MOEDA = NT0.NT0_CMOELI"
		cQuery +=   " AND CTO_LIM.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NS7
		cQuery += " INNER JOIN " + RetSqlName("NS7") + " NS7"
		cQuery +=    " ON NS7.NS7_FILIAL = ?" // 8

		AAdd(aBind, {xFilial("NS7"), "S"})

		cQuery +=   " AND NS7.NS7_COD = NT0.NT0_CESCR"
		cQuery +=   " AND NS7.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// WHERE
		cQuery += " WHERE"
		cQuery +=   " NX0.NX0_FILIAL = ?" // 9

		AAdd(aBind, {xFilial("NX0"), "S"})

		If oFilter:hasFilter()
			cQuery += " AND ?"

			AAdd(aBind, {oFilter:getSQLExpression(), "U"})
		EndIf

		cQuery +=   " AND NX0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)

		// ORDER BY
		self:setOrder("NX8_CCONTR")

		self:setQuery(cQuery)
	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju201_valor_limiteTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju201_valor_limiteTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NT0.NT0_COD"      , Nil})
	Aadd(aFields, {"NT0.NT0_NOME"     , Nil})
	Aadd(aFields, {"NX0.NX0_COD"      , Nil})
	Aadd(aFields, {"NX0.NX0_VLFATH"   , Nil})
	Aadd(aFields, {"NX8.NX8_VLRLI"    , Nil})
	Aadd(aFields, {"NX8.NX8_CCONTR"   , Nil})
	Aadd(aFields, {"NX8.NX8_UTLIMT"   , Nil})
	Aadd(aFields, {"NX8.NX8_VEXLIM"   , Nil})
	Aadd(aFields, {"CTO_LIM.CTO_SIMB ", "CTOLIM_SIMB"})
	Aadd(aFields, {"CTO_LIM.CTO_MOEDA", "CTOLIM_MOEDA"})
	Aadd(aFields, {"CTO_PRE.CTO_MOEDA", "CTOPRE_MOEDA"})
	Aadd(aFields, {"NXR_LIM.NXR_COTAC", "NXRLIM_COTAC"})
	Aadd(aFields, {"NXR_PRE.NXR_COTAC", "NXRPRE_COTAC"})

Return aFields
