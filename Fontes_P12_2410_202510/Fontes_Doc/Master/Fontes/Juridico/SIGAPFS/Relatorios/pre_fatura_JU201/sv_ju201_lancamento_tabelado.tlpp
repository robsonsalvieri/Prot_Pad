#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju201_lancamento_tabelado.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NW4,NX0,NS7,CTO,NXR',;
	name='Relatório de Pré Fatura - SubReport Lançamento Tabelado',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju201_lancamento_tabeladoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array

	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class ju201_lancamento_tabeladoTReportsBusinessObject

	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Pré Fatura - SubReport Lançamento Tabelado")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Pré Fatura - SubReport Lançamento Tabelado")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju201_lancamento_tabeladoTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		cQuery := "SELECT #QueryFields#"

		// NW4
		cQuery += " FROM " + RetSqlName("NW4") + " NW4"

		// NV4
		cQuery += " INNER JOIN " + RetSqlName("NV4") + " NV4"
		cQuery +=    " ON NV4.NV4_FILIAL = ?" // 1

		AAdd(aBind, {xFilial("NV4"), "S"})

		cQuery +=   " AND NV4.NV4_COD = NW4.NW4_CLTAB"
		cQuery +=   " AND NV4.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NX0
		cQuery += " INNER JOIN " + RetSqlName("NX0") + " NX0"
		cQuery +=    " ON NX0.NX0_FILIAL = ?" // 2

		AAdd(aBind, {xFilial("NX0"), "S"})

		cQuery +=   " AND NX0.NX0_COD = NW4.NW4_PRECNF"
		cQuery +=   " AND NX0.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NS7
		cQuery += " INNER JOIN " + RetSqlName("NS7") + " NS7"
		cQuery +=    " ON NS7.NS7_FILIAL = ?" // 3

		AAdd(aBind, {xFilial("NS7"), "S"})

		cQuery +=   " AND NS7.NS7_COD = NX0.NX0_CESCR"
		cQuery +=   " AND NS7.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// CTO_PRE
		cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO_PRE"
		cQuery +=    " ON CTO_PRE.CTO_FILIAL = ?" // 4

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=   " AND CTO_PRE.CTO_MOEDA = NX0.NX0_CMOEDA"
		cQuery +=   " AND CTO_PRE.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// CTO
		cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO"
		cQuery +=    " ON CTO.CTO_FILIAL = ?" // 5

		AAdd(aBind, {xFilial("CTO"), "S"})

		cQuery +=   " AND CTO.CTO_MOEDA = NV4.NV4_CMOEH"
		cQuery +=   " AND CTO.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NXR_TB
		cQuery += " LEFT OUTER JOIN " + RetSqlName("NXR") + " NXR_TB"
		cQuery +=   " ON NXR_TB.NXR_FILIAL = ?" // 6 

		AAdd(aBind, {xFilial("NXR"), "S"})

		cQuery +=  " AND NXR_TB.NXR_CPREFT = NX0.NX0_COD"
		cQuery +=  " AND NXR_TB.NXR_CMOEDA = NV4.NV4_CMOEH"
		cQuery +=  " AND NXR_TB.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// NXR_PRE
		cQuery += " LEFT OUTER JOIN " + RetSqlName("NXR") + " NXR_PRE"
		cQuery +=   " ON NXR_PRE.NXR_FILIAL = ?" // 7

		AAdd(aBind, {xFilial("NXR"), "S"})

		cQuery +=  " AND NXR_PRE.NXR_CMOEDA = NX0.NX0_CMOEDA"
		cQuery +=  " AND NXR_PRE.NXR_CPREFT = NX0.NX0_COD"
		cQuery +=  " AND NXR_PRE.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		// WHERE
		cQuery +=  " WHERE NW4.NW4_FILIAL = ?" // 8

		AAdd(aBind, {xFilial("NW4"), "S"})

		If oFilter:hasFilter()
			cQuery += " AND ?"

			AAdd(aBind, {oFilter:getSQLExpression(), "U"})
		EndIf
		
		cQuery +=  " AND NW4.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)

		// ORDER BY
		self:setOrder("NV4_CCLIEN,NV4_CMOEH,NV4_CCASO,NV4_DTLANC,NV4_CTPSRV")

		self:setQuery(cQuery)
	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju201_lancamento_tabeladoTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju201_lancamento_tabeladoTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NV4.NV4_CMOEH"    , Nil})
	Aadd(aFields, {"NV4.NV4_CCASO"    , Nil})
	Aadd(aFields, {"NV4.NV4_CLOJA"    , Nil})
	Aadd(aFields, {"NV4.NV4_CCLIEN"   , Nil})
	Aadd(aFields, {"NV4.NV4_DESCRI"   , Nil})
	Aadd(aFields, {"NV4.NV4_VLHFAT"   , Nil})
	Aadd(aFields, {"NV4.NV4_DTLANC"   , Nil})
	Aadd(aFields, {"NV4.NV4_CTPSRV"   , Nil})
	Aadd(aFields, {"NV4.NV4_COTAC1"   , Nil})
	Aadd(aFields, {"NV4.NV4_COTAC2"   , Nil})
	Aadd(aFields, {"NW4.NW4_CANC"     , Nil})
	Aadd(aFields, {"NW4.NW4_PRECNF"   , Nil})
	Aadd(aFields, {"CTO.CTO_SIMB"     , Nil})
	Aadd(aFields, {"CTO.CTO_MOEDA"    , Nil})
	Aadd(aFields, {"CTO_PRE.CTO_SIMB" , "CTOPRE_SIMB" })
	Aadd(aFields, {"CTO_PRE.CTO_MOEDA", "CTOPRE_MOEDA"})
	Aadd(aFields, {"NXR_TB.NXR_COTAC" , "NXR_TB_COTAC" })
	Aadd(aFields, {"NXR_PRE.NXR_COTAC", "NXR_PRE_COTAC"})

Return aFields
