#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"
#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "JURAPAD035.CH"

namespace totvs.protheus.legal.sigapfs.jurapad035_cash_flow.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team="SIGAPFS",;
	tables="FIV ,SED ,SE1 ,SE2 ,SA2 ,FK5 ,FKA ,FK1 ,FK7 ,OHB",;
	name="Cash-Flow",;
	country="ALL",;
	initialRelease="12.1.2210")

//------------------------------------------------------------------------------------
class jurapad035_cash_flowTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()         as object
	public    method getData()     as object
	public    method getSchema()   as object
	private   method getHistLanc() as character

	data lExistPergunte as logical
	data lPDUserAc      as logical

endclass

//------------------------------------------------------------------------------------
method new() class jurapad035_cash_flowTReportsBusinessObject
Local cPergunte := "JURAPAD035"

	_Super:new()

	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Cash-Flow")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Cash-Flow")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)
	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		//Indica o pergunte que será utilizado no relatório
		self:lExistPergunte := self:setPergunte(cPergunte)
		If !self:lExistPergunte // @12.1.2410
			self:setErrorStatus(400, "Dicionário de dados desatualizado!", "Grupo de perguntas " + cPergunte + " nao encontrado.")
		EndIf
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurapad035_cash_flowTReportsBusinessObject
Local aCashFl    := {}
Local aFilImpr   := {}
Local nLinha     as numeric
Local nPosFilImp as numeric
Local cTipoAnt   as character
Local cHistorico as character
Local cNomeEscr  as character
Local cSubImpr   as character
Local lForcImp1  as character
Local lForcImp2  as character
Local lAutomato  as logical
Local dData      as date
Local dDataHist  as date
Local oJsDados   as object
Local cFilialAt  := cFilAnt
Local cData      := DTOS(DATE())
Local cAlsTmp    := ""
Local cQuery     := ""
Local jParams    := oFilter:getParameters()
Local nPrazoB    := SuperGetMv( "MV_JPRAZOB" , .F. , 0 ,  ) // PRAZO BOLETO
Local nPrazoD    := SuperGetMv( "MV_JPRAZOD" , .F. , 0 ,  ) // PRAZO DEPOSITO
Local cRetPCC    := SuperGetMV( "MV_BX10925" , .F. , "1" ) // Define momento do tratamento da retencao dos impostos 1 = Na Baixa ou 2 = Na Emissao do Contas a Pagar
Local cRetISS    := SuperGetMV( "MV_MRETISS" , .F. , "1" )
Local aBind      := {}
Local cTituloTp  := "'"+MVNOTAFIS+"','"+MVDUPLIC+"','"+MVFATURA+"','"+MVCHEQUE+"','BOL','TF','RC', 'NDF'" //NF |DP |FT |CH´
Local oAutoSV    := JsonObject():new()

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	ElseIf self:lExistPergunte // @12.1.2410


		If oFilter:hasFilter()
			oAutoSV := oFilter:getRawFilter()
			If oAutoSV:HasProperty("conditions") .And. Len(oAutoSV["conditions"]) > 3
				lAutomato := oAutoSV["conditions"][1]["values"][1] == "ADVPR_SV_JURAPAD035"
			EndIf
		EndIf
		
		If !Empty(jParams['MV_PAR03'][1])
			cFilAnt  := JurGetDados("NS7", 1, xFilial("NS7") + jParams['MV_PAR03'][1], "NS7_CFILIA")
		EndIf

		cAlsTmp := GetNextAlias() // Variável estática

		// SALDO IMEDIATAMENTE ANTERIOR A DATA DO DIA
		cQuery := "SELECT ' ' CONTA, ' ' FAVORECIDO, ' ' DATA_HIST, ? HISTORICO, FIVFILT.FIV_MOEDA, 0 ENTRADAS, 0 SAIDAS," // SALDO DO DIA ANTERIOR

		AAdd(aBind, {"'" + STR0011 + "'", "U"})

		cQuery +=         " SUM(FIV_VALOR) SALDO, 0 R_E_C_N_O_ , FIV_FILIAL FILESCRI"
		cQuery += " FROM ("
		cQuery +=       " SELECT FIV.FIV_FILIAL, FIV.FIV_DATA, (CASE WHEN FIV.FIV_CARTEI = 'R' THEN FIV.FIV_VALOR ELSE -FIV.FIV_VALOR END) FIV_VALOR, FIV.FIV_MOEDA"
		cQuery +=         " FROM " + RetSqlName("FIV") + " FIV"
		cQuery +=        " INNER JOIN " + RetSqlName("SED") + " SED"
		cQuery +=           " ON ( ?"

		AAdd(aBind, {JurRelFilia("SED.ED_FILIAL", "FIV.FIV_FILIAL"), "U"})

		cQuery +=          " AND FIV.FIV_NATUR = SED.ED_CODIGO"
		cQuery +=          " AND SED.ED_CFJUR = ?"

		AAdd(aBind, {"1", "S"})

		cQuery +=          " AND SED.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=        " WHERE"

		If !Empty(jParams['MV_PAR03'][1])
			cQuery +=          " FIV.FIV_FILIAL = ? AND"

			AAdd(aBind, {xFilial("FIV"), "S"})
		EndIf

		cQuery +=            " FIV.FIV_TPSALD = ?"

		AAdd(aBind, {"3", "S"})

		cQuery +=          " AND FIV.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery +=       " ) FIVFILT"

		cQuery += " WHERE FIVFILT.FIV_DATA < ?"

		If lAutomato
			AAdd(aBind, {oAutoSV["conditions"][4]["values"][1], "S"})
		Else
			AAdd(aBind, {cData, "S"})
		EndIf

		cQuery +=   " AND FIVFILT.FIV_MOEDA = ?"

		AAdd(aBind, {jParams['MV_PAR01'][1], "S"})

		cQuery += " GROUP BY FIVFILT.FIV_FILIAL, FIVFILT.FIV_MOEDA"

		cQuery := JurTRepBin(cQuery, aBind)
		cQuery := ChangeQuery( cQuery )
		DbUseArea( .T. , 'TOPCONN' , TcGenQry( ,, cQuery ), cAlsTmp, .T., .F. )
		While (cAlsTmp)->( ! Eof() )
			aAdd(aCashFl, { "1", ;                                                        // 1 - Tipo
							Iif(lAutomato,oAutoSV["conditions"][4]["values"][1],cData), ; // 2 - Data
							(cAlsTmp)->CONTA, ;                                           // 3 - Conta
							(cAlsTmp)->FAVORECIDO, ;                                      // 4 - Favorecido
							(cAlsTmp)->DATA_HIST, ;                                       // 5 - Data usada no histórico
							(cAlsTmp)->HISTORICO, ;                                       // 6 - Histórico
							(cAlsTmp)->FIV_MOEDA, ;                                       // 7 - Moeda
							(cAlsTmp)->ENTRADAS , ;                                       // 8 - Valor de Entradas
							(cAlsTmp)->SAIDAS, ;                                          // 9 - Valor de Saídas
							(cAlsTmp)->SALDO, ;                                           // 10 - Saldo
							(cAlsTmp)->R_E_C_N_O_, ;                                      // 11 - Recno (Usado para busca de campo MEMO - Histórico)
							(cAlsTmp)->FILESCRI, ;                                        // 12 - Filial do Escritório
							""                   ;                                        // 13 - Tabela (Usado para busca de campo MEMO - Histórico)
						} )
			(cAlsTmp)->( DbSkip() )
		EndDo
		dbSelectArea(cAlsTmp)
		(cAlsTmp)->( DbCloseArea() )

		cAlsTmp := GetNextAlias()
		cQuery  := ""
		aBind   := {}

		//ENTRADAS E SAIDAS DO DIA - NECESSÁRIO CALCULAR O SALDO DA MOVIMENTAÇÃO DO DIA ANTERIOR MAIS AS ENTRADAS MENOS AS SAIDAS
		cQuery += " SELECT ' ' CONTA, ' ' FAVORECIDO, ' ' DATA_HIST, ? HISTORICO," // 'MOVIMENTO REALIZADO NO DIA'

		AAdd(aBind, {"'" + STR0029 + "'", "U"})

		cQuery +=          " FIVFILT.FIV_MOEDA, SUM(ENTRADAS) ENTRADAS, SUM(SAIDAS) SAIDAS, 0 SALDO, 0 R_E_C_N_O_, FIVFILT.FIV_FILIAL FILESCRI"
		cQuery +=  " FROM ("
		cQuery +=        " SELECT FIV.FIV_FILIAL, FIV.FIV_DATA,(CASE WHEN FIV.FIV_CARTEI = 'R' THEN FIV.FIV_VALOR ELSE 0 END) ENTRADAS,"
		cQuery +=            " (CASE WHEN FIV.FIV_CARTEI = 'P' THEN FIV.FIV_VALOR ELSE 0 END) SAIDAS, FIV.FIV_MOEDA"
		cQuery +=        " FROM " + RetSqlName("FIV") + " FIV"
		cQuery +=  " INNER JOIN  " + RetSqlName("SED") + " SED"
		cQuery +=          " ON ( ?"

		AAdd(aBind, {JurRelFilia("SED.ED_FILIAL", "FIV.FIV_FILIAL" ), "U"})

		cQuery +=         " AND FIV.FIV_NATUR = SED.ED_CODIGO"
		cQuery +=         " AND SED.ED_CFJUR = ?"

		AAdd(aBind, {"1", "S"})

		cQuery +=         " AND SED.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery +=       " WHERE"

		If !Empty(jParams['MV_PAR03'][1])
			cQuery +=     " FIV.FIV_FILIAL = ? AND"

			AAdd(aBind, {xFilial("FIV"), "S"})
		EndIf

		cQuery +=       " FIV.FIV_TPSALD = ?"

		AAdd(aBind, {"3", "S"})

		cQuery +=        " AND FIV.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery +=      " ) FIVFILT"
		cQuery +=  " WHERE FIVFILT.FIV_DATA = ?"
		If lAutomato
			AAdd(aBind, {oAutoSV["conditions"][4]["values"][1], "S"})
		Else
			AAdd(aBind, {cData, "S"})
		EndIf
		cQuery +=    " AND FIVFILT.FIV_MOEDA = ?"

		AAdd(aBind, {jParams['MV_PAR01'][1], "S"})

		cQuery += " GROUP BY FIVFILT.FIV_FILIAL, FIVFILT.FIV_MOEDA"

		cQuery := JurTRepBin(cQuery, aBind)
		cQuery := ChangeQuery( cQuery )
		DbUseArea( .T. , 'TOPCONN' , TcGenQry( ,, cQuery ), cAlsTmp, .T., .F. )
		While (cAlsTmp)->( ! Eof() )
			aAdd(aCashFl, { "2", ;                                                        // 1 - Tipo
							Iif(lAutomato,oAutoSV["conditions"][4]["values"][1],cData), ; // 2 - Data
							(cAlsTmp)->CONTA, ;                                           // 3 - Conta
							(cAlsTmp)->FAVORECIDO, ;                                      // 4 - Favorecido
							(cAlsTmp)->DATA_HIST, ;                                       // 5 - Data usada no histórico
							(cAlsTmp)->HISTORICO, ;                                       // 6 - Histórico
							(cAlsTmp)->FIV_MOEDA, ;                                       // 7 - Moeda
							(cAlsTmp)->ENTRADAS, ;                                        // 8 - Valor de Entradas
							(cAlsTmp)->SAIDAS, ;                                          // 9 - Valor de Saídas
							(cAlsTmp)->SALDO, ;                                           // 10 - Saldo
							(cAlsTmp)->R_E_C_N_O_, ;                                      // 11 - Recno (Usado para busca de campo MEMO - Histórico)
							(cAlsTmp)->FILESCRI, ;                                        // 12 - Filial do Escritório
							"" ;                                                          // 13 - Tabela (Usado para busca de campo MEMO - Histórico)
						} )
			(cAlsTmp)->( DbSkip() )
		EndDo
		dbSelectArea(cAlsTmp)
		(cAlsTmp)->( DbCloseArea() )

		cAlsTmp := GetNextAlias()
		cQuery  := ""
		aBind   := {}

		// CONTAS A RECEBER PENDENTE E AINDA NÃO VENCIDO
		cQuery += " SELECT SE1.E1_VENCTO DATA, SE1.E1_NATUREZ CONTA, SA1.A1_NOME FAVORECIDO, SE1.E1_VENCTO DATA_HIST,"
		cQuery +=        " SE1.E1_MOEDA, SE1.E1_SALDO - ((SE1.E1_IRRF + SE1.E1_ISS + SE1.E1_INSS) + SUM(COALESCE(FK4.FK4_VALOR,0))" // Os impostos IRF, ISS e INSS sempre serão regime de competencia
		cQuery += " + (SE1.E1_PIS + SE1.E1_COFINS + SE1.E1_CSLL - ("
		cQuery +=                                               " COALESCE((SELECT SUM(CASE WHEN SE5.E5_TIPODOC = 'ES'"
		cQuery +=                                                               " THEN COALESCE((SE5.E5_VRETPIS + SE5.E5_VRETCOF + SE5.E5_VRETCSL), 0) * -1"
		cQuery +=                                                               " ELSE COALESCE((SE5.E5_VRETPIS + SE5.E5_VRETCOF + SE5.E5_VRETCSL), 0)"
		cQuery +=                                                           " END) IMPRET"
		cQuery +=                                                           " FROM " + RetSqlName("SE5") + " SE5"
		cQuery +=                                                          " WHERE SE5.E5_FILIAL = SE1.E1_FILIAL"
		cQuery +=                                                            " AND SE5.E5_PREFIXO = SE1.E1_PREFIXO"
		cQuery +=                                                            " AND SE5.E5_NUMERO = SE1.E1_NUM"
		cQuery +=                                                            " AND SE5.E5_PARCELA = SE1.E1_PARCELA"
		cQuery +=                                                            " AND SE5.E5_TIPO = SE1.E1_TIPO"
		cQuery +=                                                            " AND SE5.E5_DTCANBX = ?"
		AAdd(aBind, {Space(1), "S"})
		cQuery +=                                                            " AND SE5.D_E_L_E_T_ = ?),0)"
		AAdd(aBind, {Space(1), "S"})
		cQuery +=                                               " )"
		cQuery +=   " )"
		cQuery +=  " ) ENTRADAS,"
		cQuery +=    " 0 SAIDAS, 0 SALDO, SE1.R_E_C_N_O_, SE1.E1_BOLETO BOLETO , SE1.E1_FILIAL FILESCRI"
		cQuery +=   " FROM " + RetSqlName("SE1") + " SE1"
		cQuery +=  " INNER JOIN " + RetSqlName("SA1") + " SA1"
		cQuery +=     " ON ?"

		AAdd(aBind, {JurRelFilia("SA1.A1_FILIAL", "SE1.E1_FILIAL"), "U"})

		cQuery +=    " AND SA1.A1_COD = SE1.E1_CLIENTE"
		cQuery +=    " AND SA1.A1_LOJA = SE1.E1_LOJA"
		cQuery +=    " AND SA1.D_E_L_E_T_ = ? "

		AAdd(aBind, {Space(1), "S"})

		cQuery +=  " INNER JOIN " + RetSqlName("FK7") + " FK7"
		cQuery +=     " ON FK7.FK7_ALIAS = ?"
		AAdd(aBind, {"SE1", "S"})
		cQuery +=    " AND FK7.FK7_FILTIT = SE1.E1_FILIAL"
		cQuery +=    " AND FK7.FK7_PREFIX = SE1.E1_PREFIXO"
		cQuery +=    " AND FK7.FK7_NUM = SE1.E1_NUM"
		cQuery +=    " AND FK7.FK7_PARCEL = SE1.E1_PARCELA"
		cQuery +=    " AND FK7.FK7_TIPO = SE1.E1_TIPO"
		cQuery +=    " AND FK7.FK7_CLIFOR = SE1.E1_CLIENTE"
		cQuery +=    " AND FK7.FK7_LOJA = SE1.E1_LOJA"
		cQuery +=    " AND FK7.D_E_L_E_T_ = ?"
		AAdd(aBind, {Space(1), "S"})
		cQuery +=   " LEFT JOIN " + RetSqlName("FK4") + " FK4"
		cQuery +=     " ON FK4.FK4_FILIAL = SE1.E1_FILIAL"
		cQuery +=    " AND FK4.FK4_IDORIG = FK7.FK7_IDDOC"
		cQuery +=    " AND FK4.FK4_IMPOS NOT IN (?)" // Não traz os impostos conhecidos pois eles são tratados nos campos E5_VRET da tabela SE5
		AAdd(aBind, {"'IRF', 'PIS', 'COF', 'CSL', 'ISS', 'INSS'", "U"})
		cQuery +=    " AND FK4.FK4_CODFKM <> ?"
		AAdd(aBind, {Space(1), "S"})
		cQuery +=    " AND FK4.D_E_L_E_T_ = ?"
		AAdd(aBind, {Space(1), "S"})
		// O CR funciona diferente do CP, por isso o Left Join com a tabela FKK
		cQuery +=   " LEFT JOIN " + RetSqlName("FKK") + " FKK"
		cQuery +=     " ON FKK.FKK_FILIAL = FK4.FK4_FILIAL"
		cQuery +=    " AND FKK_CODIGO = FK4.FK4_CODFKM" // Garante que trará apenas os impostos da FK4 que foram calculados pelo CFGTRIB
		cQuery +=    " AND FKK.D_E_L_E_T_ = ?"
		AAdd(aBind, {Space(1), "S"})
		cQuery +=  " WHERE"
		If !Empty(jParams['MV_PAR03'][1])
			cQuery +=   " SE1.E1_FILIAL = ? AND"
			AAdd(aBind, {xFilial("SE1"), "S"})
		EndIf
		cQuery +=   " ?"
		AAdd(aBind, {" SE1.E1_VENCTO >= '" + cData + "' ", "U"})
		cQuery +=   " AND SE1.E1_SALDO > ?"
		AAdd(aBind, {0, "N"})
		cQuery +=   " AND SE1.E1_MOEDA = ?"

		AAdd(aBind, {jParams['MV_PAR01'][1], "S"})

		cQuery +=   " AND SE1.E1_TIPO IN (?)"

		AAdd(aBind, {cTituloTp, "U"})
		If lAutomato
			cQuery += " AND SE1.E1_CLIENTE = ?"
			AAdd(aBind, {oAutoSV["conditions"][2]["values"][1], "S"})
			cQuery += " AND SE1.E1_LOJA = ?"
			AAdd(aBind, {oAutoSV["conditions"][3]["values"][1], "S"})
			cQuery += " AND SE1.E1_EMISSAO = ?"
			AAdd(aBind, {oAutoSV["conditions"][4]["values"][1], "S"})
		EndIf
		cQuery +=   " AND SE1.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})
		cQuery += " GROUP BY SE1.E1_FILIAL, SE1.E1_MOEDA, SE1.E1_VENCTO, SE1.E1_NATUREZ, SE1.E1_SALDO, SE1.E1_IRRF, SE1.E1_PIS,"
		cQuery +=          " SE1.E1_COFINS, SE1.E1_CSLL, SE1.E1_ISS, SE1.E1_INSS, SE1.R_E_C_N_O_, SE1.E1_BOLETO, SE1.E1_HIST,"
		cQuery +=          " SE1.E1_PREFIXO, SE1.E1_NUM, SE1.E1_PARCELA,  SE1.E1_TIPO, SA1.A1_NOME"

		cQuery := JurTRepBin(cQuery, aBind)
		cQuery := ChangeQuery( cQuery )
		DbUseArea( .T. , 'TOPCONN' , TcGenQry( ,, cQuery ), cAlsTmp, .T., .F. )
		While (cAlsTmp)->( ! Eof() )
			nPrazo := IIf((cAlsTmp)->BOLETO == "1", nPrazoB, nPrazoD)
			cDataPrazo := JurDtAdd((cAlsTmp)->DATA, "D", nPrazo)

			If STOD(cDataPrazo) >= STOD(cData)
				aAdd(aCashFl, { "3", ;                   // 1 - Tipo
				                cDataPrazo, ;            // 2 - Data
				                (cAlsTmp)->CONTA, ;      // 3 - Conta
				                (cAlsTmp)->FAVORECIDO, ; // 4 - Favorecido
				                (cAlsTmp)->DATA_HIST, ;  // 5 - Data usada no histórico
				                "", ;                    // 6 - Histórico
				                (cAlsTmp)->E1_MOEDA, ;   // 7 - Moeda
				                (cAlsTmp)->ENTRADAS, ;   // 8 - Valor de Entradas
				                (cAlsTmp)->SAIDAS, ;     // 9 - Valor de Saídas
				                (cAlsTmp)->SALDO, ;      // 10 - Saldo
				                (cAlsTmp)->R_E_C_N_O_, ; // 11 - Recno (Usado para busca de campo MEMO - Histórico)
				                (cAlsTmp)->FILESCRI, ;   // 12 - Filial do Escritório
				                "SE1" ;                  // 13 - Tabela (Usado para busca de campo MEMO - Histórico)
				              } )
			EndIf
			(cAlsTmp)->( DbSkip() )
		EndDo
		dbSelectArea(cAlsTmp)
		(cAlsTmp)->( DbCloseArea() )

		cAlsTmp := GetNextAlias()
		cQuery  := ""
		aBind   := {}

		// CONTAS A PAGAR PENDENTE E AINDA NÃO VENCIDO
		cQuery += " SELECT SE2.E2_VENCTO DATA, SE2.E2_NATUREZ CONTA, SA2.A2_NOME FAVORECIDO, SE2.E2_VENCTO DATA_HIST,"
		cQuery +=        " SE2.E2_MOEDA, 0 ENTRADAS,"
		If cRetPCC == "1" // MV_BX10925 = 1 -> Define se a retenção do PIS/COFINS e CSLL será na baixa do título principal.
			cSubImpr := "+ (SE2.E2_PIS - SE2.E2_VRETPIS) + (SE2.E2_COFINS - SE2.E2_VRETCOF) + (SE2.E2_CSLL - SE2.E2_VRETCSL)"
		EndIf
		If cRetISS == "2" // MV_MRETISS - 1 -> Define se a retenção do ISS será na baixa do título principal.
				cSubImpr += "+ (SE2.E2_ISS - SE2.E2_VRETISS)"
		EndIf
		cQuery +=    " CASE"
		cQuery +=         " WHEN SA2.A2_CALCIRF = '2' THEN SE2.E2_SALDO - ((SE2.E2_IRRF - SE2.E2_VRETIRF) ? + SUM(COALESCE(SE2IMP.E2_VALOR,0)) + SUM(COALESCE(FK4.FK4_VALOR,0)))"
		AAdd(aBind, {cSubImpr, "U"})
		cQuery +=         " ELSE SE2.E2_SALDO - (? + SUM(COALESCE(SE2IMP.E2_VALOR,0)) + SUM(COALESCE(FK4.FK4_VALOR,0)))"
		AAdd(aBind, {cSubImpr, "U"})
		cQuery +=    " END AS SAIDAS,"
		cQuery +=         " 0 SALDO, SE2.R_E_C_N_O_, SE2.E2_FILIAL FILESCRI"
		cQuery +=  " FROM " + RetSqlName("SE2") + " SE2"
		cQuery += " INNER JOIN " + RetSqlName("SA2") + " SA2"
		cQuery +=    " ON SA2.A2_FILIAL = ?"
		AAdd(aBind, {xFilial("SA2"), "S"})
		cQuery +=   " AND SA2.A2_COD = SE2.E2_FORNECE"
		cQuery +=   " AND SA2.A2_LOJA = SE2.E2_LOJA"
		cQuery +=   " AND SA2.D_E_L_E_T_ = ?"
		AAdd(aBind, {Space(1), "S"})
		cQuery += " INNER JOIN " + RetSqlName("FK7") + " FK7"
		cQuery +=    " ON FK7.FK7_ALIAS = ?"
		AAdd(aBind, {'SE2', "S"})
		cQuery +=   " AND FK7.FK7_FILTIT = SE2.E2_FILIAL"
		cQuery +=   " AND FK7.FK7_PREFIX = SE2.E2_PREFIXO"
		cQuery +=   " AND FK7.FK7_NUM = SE2.E2_NUM"
		cQuery +=   " AND FK7.FK7_PARCEL = SE2.E2_PARCELA"
		cQuery +=   " AND FK7.FK7_TIPO = SE2.E2_TIPO"
		cQuery +=   " AND FK7.FK7_CLIFOR = SE2.E2_FORNECE"
		cQuery +=   " AND FK7.FK7_LOJA = SE2.E2_LOJA"
		cQuery +=   " AND FK7.D_E_L_E_T_ = ?"
		AAdd(aBind, {Space(1), "S"})
		cQuery +=  " LEFT JOIN " + RetSqlName("FK4") + " FK4"
		cQuery +=    " ON FK4.FK4_FILIAL = SE2.E2_FILIAL"
		cQuery +=   " AND FK4.FK4_IDORIG = FK7.FK7_IDDOC"
		cQuery +=   " AND FK4.FK4_CODFKM <> ?"
		AAdd(aBind, {Space(1), "S"})
		cQuery +=   " AND EXISTS (SELECT FKK.R_E_C_N_O_"
		cQuery +=                 " FROM " + RetSqlName("FKK") + " FKK"
		cQuery +=                " WHERE FKK.FKK_FILIAL = FK4.FK4_FILIAL"
		cQuery +=                  " AND FKK.FKK_CODIGO = FK4.FK4_CODFKM"
		cQuery +=                  " AND FKK.FKK_FATGER = ?"
		AAdd(aBind, {"2", "S"})
		cQuery +=                  " AND FKK.D_E_L_E_T_ = ?)"
		AAdd(aBind, {Space(1), "S"})
		cQuery +=   " AND FK4.D_E_L_E_T_ = ?"
		AAdd(aBind, {Space(1), "S"})
		cQuery +=  " LEFT JOIN " + RetSqlName("SE2") + " SE2IMP"
		cQuery +=    " ON SE2IMP.E2_FILIAL = FK7.FK7_FILTIT"
		cQuery +=   " AND SE2IMP.E2_PREFIXO = FK7.FK7_PREFIX"
		cQuery +=   " AND SE2IMP.E2_NUM = FK7.FK7_NUM"
		cQuery +=   " AND SE2IMP.E2_TIPO = ?"
		AAdd(aBind, {"PR", "S"})
		cQuery +=   " AND SE2IMP.D_E_L_E_T_ = ?"
		AAdd(aBind, {Space(1), "S"})
		cQuery += " WHERE"
		If !Empty(jParams['MV_PAR03'][1])
			cQuery +=    " SE2.E2_FILIAL = ? AND"

			AAdd(aBind, {xFilial("SE2"), "S"})
		EndIf

		cQuery +=        " ?"
		AAdd(aBind, {" SE2.E2_VENCTO >= '" + cData + "' ", "U"})
		cQuery +=        " AND SE2.E2_SALDO > ?"

		AAdd(aBind, {0, "N"})

		cQuery +=        " AND SE2.E2_MOEDA = ?"

		AAdd(aBind, {jParams['MV_PAR01'][1], "S"})

		cQuery +=        " AND SE2.E2_TIPO IN (?)"

		AAdd(aBind, {cTituloTp, "U"})
		If lAutomato
			cQuery += " AND SE2.E2_FORNECE = ?"
			AAdd(aBind, {oAutoSV["conditions"][2]["values"][1], "S"})
			cQuery += " AND SE2.E2_LOJA = ?"
			AAdd(aBind, {oAutoSV["conditions"][3]["values"][1], "S"})
			cQuery += " AND SE2.E2_EMISSAO = ?"
			AAdd(aBind, {oAutoSV["conditions"][4]["values"][1], "S"})
		EndIf
		cQuery +=        " AND SE2.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})
		cQuery += " GROUP BY SE2.E2_FILIAL, SE2.E2_MOEDA, SE2.E2_VENCTO, SE2.E2_NATUREZ, SE2.E2_HIST, SE2.E2_SALDO, SE2.E2_IRRF, SE2.E2_PIS,"
		cQuery +=          " SE2.E2_COFINS, SE2.E2_CSLL, SE2.E2_ISS, SE2.E2_INSS, SE2.R_E_C_N_O_,"
		cQuery +=          " SA2.A2_CALCIRF, SA2.A2_NOME, SE2.E2_VRETIRF,SE2.E2_VRETPIS,SE2.E2_VRETCOF,SE2.E2_VRETCSL,SE2.E2_VRETISS"

		cQuery := JurTRepBin(cQuery, aBind)
		cQuery := ChangeQuery( cQuery )
		DbUseArea( .T. , 'TOPCONN' , TcGenQry( ,, cQuery ), cAlsTmp, .T., .F. )
		While (cAlsTmp)->( ! Eof() )

			aAdd(aCashFl, { "3", ;                   // 1 - Tipo
							(cAlsTmp)->DATA, ;       // 2 - Data
							(cAlsTmp)->CONTA, ;      // 3 - Conta
							(cAlsTmp)->FAVORECIDO, ; // 4 - Favorecido
							(cAlsTmp)->DATA_HIST, ;  // 5 - Data usada no histórico
							"", ;                    // 6 - Histórico
							(cAlsTmp)->E2_MOEDA, ;   // 7 - Moeda
							(cAlsTmp)->ENTRADAS, ;   // 8 - Valor de Entradas
							(cAlsTmp)->SAIDAS, ;     // 9 - Valor de Saídas
							(cAlsTmp)->SALDO, ;      // 10 - Saldo
							(cAlsTmp)->R_E_C_N_O_, ; // 11 - Recno (Usado para busca de campo MEMO - Histórico)
							(cAlsTmp)->FILESCRI, ;   // 12 - Filial do Escritório
							"SE2" ;                  // 13 - Tabela (Usado para busca de campo MEMO - Histórico)
							} )

			(cAlsTmp)->( DbSkip() )
		EndDo
		dbSelectArea(cAlsTmp)
		(cAlsTmp)->( DbCloseArea() )

		cAlsTmp  := GetNextAlias()
		cQuery   := ""
		cSubImpr := ""
		aBind    := {}

		// CONTAS A PAGAR PENDENTE E JÁ VENCIDO
		cQuery += " SELECT SE2.E2_NATUREZ CONTA, SA2.A2_NOME FAVORECIDO, SE2.E2_VENCTO DATA_HIST,"
		cQuery +=        " SE2.E2_MOEDA, 0 ENTRADAS,"
		If cRetPCC == "1" // MV_BX10925 = 1 -> Define se a retenção do PIS/COFINS e CSLL será na baixa do título principal.
			cSubImpr := "+ (SE2.E2_PIS - SE2.E2_VRETPIS) + (SE2.E2_COFINS - SE2.E2_VRETCOF) + (SE2.E2_CSLL - SE2.E2_VRETCSL)"
		EndIf
		If cRetISS == "2" // MV_MRETISS - 1 -> Define se a retenção do ISS será na baixa do título principal.
				cSubImpr += "+ (SE2.E2_ISS - SE2.E2_VRETISS)"
		EndIf
		cQuery +=    " CASE"
		cQuery +=         " WHEN SA2.A2_CALCIRF = '2' THEN SE2.E2_SALDO - ((SE2.E2_IRRF - SE2.E2_VRETIRF) ? + SUM(COALESCE(SE2IMP.E2_VALOR,0)) + SUM(COALESCE(FK4.FK4_VALOR,0)))"
		AAdd(aBind, {cSubImpr, "U"})
		cQuery +=         " ELSE SE2.E2_SALDO - (? + SUM(COALESCE(SE2IMP.E2_VALOR,0)) + SUM(COALESCE(FK4.FK4_VALOR,0)))"
		AAdd(aBind, {cSubImpr, "U"})
		cQuery +=    " END AS SAIDAS,"
		cQuery +=        " 0 SALDO, SE2.R_E_C_N_O_, SE2.E2_FILIAL FILESCRI"
		cQuery +=   " FROM " + RetSqlName("SE2") + " SE2"
		cQuery +=  " INNER JOIN " + RetSqlName("SA2") + " SA2"
		cQuery +=     " ON ( ?"

		AAdd(aBind, {JurRelFilia("SA2.A2_FILIAL", "SE2.E2_FILIAL"), "U"})

		cQuery +=    " AND SA2.A2_COD = SE2.E2_FORNECE"
		cQuery +=    " AND SA2.A2_LOJA = SE2.E2_LOJA"
		cQuery +=    " AND SA2.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})
		cQuery += " INNER JOIN " + RetSqlName("FK7") + " FK7"
		cQuery +=    " ON FK7.FK7_ALIAS = ?"
		AAdd(aBind, {'SE2', "S"})
		cQuery +=   " AND FK7.FK7_FILTIT = SE2.E2_FILIAL"
		cQuery +=   " AND FK7.FK7_PREFIX = SE2.E2_PREFIXO"
		cQuery +=   " AND FK7.FK7_NUM = SE2.E2_NUM"
		cQuery +=   " AND FK7.FK7_PARCEL = SE2.E2_PARCELA"
		cQuery +=   " AND FK7.FK7_TIPO = SE2.E2_TIPO"
		cQuery +=   " AND FK7.FK7_CLIFOR = SE2.E2_FORNECE"
		cQuery +=   " AND FK7.FK7_LOJA = SE2.E2_LOJA"
		cQuery +=   " AND FK7.D_E_L_E_T_ = ?"
		AAdd(aBind, {Space(1), "S"})
		cQuery +=  " LEFT JOIN " + RetSqlName("FK4") + " FK4"
		cQuery +=    " ON FK4.FK4_FILIAL = SE2.E2_FILIAL"
		cQuery +=   " AND FK4.FK4_IDORIG = FK7.FK7_IDDOC"
		cQuery +=   " AND FK4.FK4_CODFKM <> ?"
		AAdd(aBind, {Space(1), "S"})
		cQuery +=   " AND EXISTS (SELECT FKK.R_E_C_N_O_"
		cQuery +=                 " FROM " + RetSqlName("FKK") + " FKK"
		cQuery +=                " WHERE FKK.FKK_FILIAL = FK4.FK4_FILIAL"
		cQuery +=                  " AND FKK.FKK_CODIGO = FK4.FK4_CODFKM"
		cQuery +=                  " AND FKK.FKK_FATGER = ?"
		AAdd(aBind, {"2", "S"})
		cQuery +=                  " AND FKK.D_E_L_E_T_ = ?)"
		AAdd(aBind, {Space(1), "S"})
		cQuery +=   " AND FK4.D_E_L_E_T_ = ?"
		AAdd(aBind, {Space(1), "S"})
		cQuery +=  " LEFT JOIN " + RetSqlName("SE2") + " SE2IMP"
		cQuery +=    " ON SE2IMP.E2_FILIAL = FK7.FK7_FILTIT"
		cQuery +=   " AND SE2IMP.E2_PREFIXO = FK7.FK7_PREFIX"
		cQuery +=   " AND SE2IMP.E2_NUM = FK7.FK7_NUM"
		cQuery +=   " AND SE2IMP.E2_TIPO = ?"
		AAdd(aBind, {"PR", "S"})
		cQuery +=   " AND SE2IMP.D_E_L_E_T_ = ?"
		AAdd(aBind, {Space(1), "S"})
		cQuery += " WHERE"
		If !Empty(jParams['MV_PAR03'][1])
			cQuery +=    " SE2.E2_FILIAL = ? AND"

			AAdd(aBind, {xFilial("SE2"), "S"})
		EndIf
		cQuery +=        " SE2.E2_VENCTO < ?"

		AAdd(aBind, {cData, "S"})

		cQuery +=    " AND SE2.E2_SALDO > ?"

		AAdd(aBind, {0, "N"})

		cQuery +=    " AND SE2.E2_MOEDA = ?"

		AAdd(aBind, {jParams['MV_PAR01'][1], "S"})

		cQuery +=    " AND SE2.E2_TIPO IN (?)"

		AAdd(aBind, {cTituloTp, "U"})
		If lAutomato
			cQuery += " AND SE2.E2_FORNECE = ?"
			AAdd(aBind, {oAutoSV["conditions"][2]["values"][1], "S"})
			cQuery += " AND SE2.E2_LOJA = ?"
			AAdd(aBind, {oAutoSV["conditions"][3]["values"][1], "S"})
			cQuery += " AND SE2.E2_EMISSAO = ?"
			AAdd(aBind, {oAutoSV["conditions"][4]["values"][1], "S"})
		EndIf
		cQuery +=    " AND SE2.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery += " GROUP BY SE2.E2_FILIAL, SE2.E2_MOEDA,  SE2.E2_VENCTO, SE2.E2_NATUREZ, SA2.A2_NOME, SE2.E2_HIST, SE2.E2_SALDO, SE2.E2_IRRF,"
		cQuery +=          " SE2.E2_PIS, SE2.E2_COFINS, SE2.E2_CSLL, SE2.E2_ISS, SE2.E2_INSS, SE2.R_E_C_N_O_,"
		cQuery +=          " SA2.A2_CALCIRF, SE2.E2_VRETIRF,SE2.E2_VRETPIS,SE2.E2_VRETCOF,SE2.E2_VRETCSL,SE2.E2_VRETISS"

		cQuery := JurTRepBin(cQuery, aBind)
		cQuery := ChangeQuery( cQuery ) 
		DbUseArea( .T. , 'TOPCONN' , TcGenQry( ,, cQuery ), cAlsTmp, .T., .F. )
		While (cAlsTmp)->( ! Eof() )
			aAdd(aCashFl, { "3", ;                                                        // 1 - Tipo
			                Iif(lAutomato,oAutoSV["conditions"][4]["values"][1],cData), ; // 2 - Data
			                (cAlsTmp)->CONTA, ;                                           // 3 - Conta
			                (cAlsTmp)->FAVORECIDO, ;                                      // 4 - Favorecido
			                (cAlsTmp)->DATA_HIST, ;                                       // 5 - Data usada no histórico
			                "", ;                                                         // 6 - Histórico
			                (cAlsTmp)->E2_MOEDA, ;                                        // 7 - Moeda
			                (cAlsTmp)->ENTRADAS, ;                                        // 8 - Valor de Entradas
			                (cAlsTmp)->SAIDAS, ;                                          // 9 - Valor de Saídas
			                (cAlsTmp)->SALDO, ;                                           // 10 - Saldo
			                (cAlsTmp)->R_E_C_N_O_, ;                                      // 11 - Recno (Usado para busca de campo MEMO - Histórico)
			                (cAlsTmp)->FILESCRI, ;                                        // 12 - Filial do Escritório
			                "SE2" ;                                                       // 13 - Tabela (Usado para busca de campo MEMO - Histórico)
			              } ) 

			(cAlsTmp)->( DbSkip() )
		EndDo
		dbSelectArea(cAlsTmp)
		(cAlsTmp)->( DbCloseArea() )

		cAlsTmp  := GetNextAlias()
		cQuery   := ""
		cSubImpr := ""
		aBind    := {}

		// VALORES DE CONTAS A RECEBER RECEBIDOS NO DIA
		cQuery += " SELECT FK5.FK5_NATURE CONTA, SA1.A1_NOME FAVORECIDO, SE1.E1_VENCTO DATA_HIST,"
		cQuery +=          " FK5.FK5_MOEDA MOEDA, FK5.FK5_VALOR ENTRADAS,"
		cQuery +=          " 0 SAIDAS, 0 SALDO, FK5.R_E_C_N_O_, SE1.E1_BOLETO, FK5.FK5_FILIAL FILESCRI"
		cQuery +=   " FROM " + RetSqlName("FK5") + " FK5"
		cQuery +=  " INNER JOIN " + RetSqlName("FKA") + " FKA1"
		cQuery +=     " ON ?"

		AAdd(aBind, {JurRelFilia("FKA1.FKA_FILIAL", "FK5.FK5_FILIAL"), "U"})

		cQuery +=    " AND FKA1.FKA_IDORIG = FK5.FK5_IDMOV"
		cQuery +=    " AND FKA1.FKA_TABORI = ?"

		AAdd(aBind, {"FK5", "S"})

		cQuery +=    " AND FKA1.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery +=  " INNER JOIN " + RetSqlName("FKA") + " FKA2"
		cQuery +=     " ON ?"

		AAdd(aBind, {JurRelFilia("FKA2.FKA_FILIAL", "FKA1.FKA_FILIAL"), "U"})

		cQuery +=    " AND FKA2.FKA_IDPROC = FKA1.FKA_IDPROC"
		cQuery +=    " AND FKA2.FKA_TABORI = ?"

		AAdd(aBind, {"FK1", "S"})

		cQuery +=    " AND FKA2.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery +=  " INNER JOIN " + RetSqlName("FK1") + " FK1 "
		cQuery +=     " ON ?"

		AAdd(aBind, {JurRelFilia("FK1.FK1_FILIAL", "FKA2.FKA_FILIAL"), "U"})

		cQuery +=    " AND FK1.FK1_IDFK1 = FKA2.FKA_IDORIG"
		cQuery +=    " AND FK1.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery +=  " INNER JOIN " + RetSqlName("FK7") + " FK7 "
		cQuery +=     " ON ?"

		AAdd(aBind, {JurRelFilia("FK7.FK7_FILIAL", "FK1.FK1_FILIAL"), "U"})

		cQuery +=    " AND FK7.FK7_IDDOC = FK1.FK1_IDDOC"
		cQuery +=    " AND FK7.FK7_ALIAS = ?"

		AAdd(aBind, {"SE1", "S"})

		cQuery +=    " AND FK7.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery +=  " INNER JOIN " + RetSqlName("SE1") + " SE1"
		cQuery +=     " ON ?"

		AAdd(aBind, {JurRelFilia("SE1.E1_FILIAL", "FK5.FK5_FILIAL"), "U"})

		cQuery +=    " AND SE1.E1_FILIAL ||'|'|| SE1.E1_PREFIXO ||'|'|| SE1.E1_NUM ||'|'|| SE1.E1_PARCELA ||'|'|| SE1.E1_TIPO ||'|'|| SE1.E1_CLIENTE ||'|'|| SE1.E1_LOJA = FK7.FK7_CHAVE"
		cQuery +=    " AND FK7.FK7_ALIAS = ?"

		AAdd(aBind, {"SE1", "S"})

		cQuery +=    " AND SE1.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery +=  " INNER JOIN " + RetSqlName("SA1") + " SA1"
		cQuery +=     " ON ? "

		AAdd(aBind, {JurRelFilia("SA1.A1_FILIAL", "SE1.E1_FILIAL"), "U"})

		cQuery +=    " AND SA1.A1_COD = SE1.E1_CLIENTE"
		cQuery +=    " AND SA1.A1_LOJA = SE1.E1_LOJA"
		cQuery +=    " AND SA1.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery +=  " WHERE"
		If !Empty(jParams['MV_PAR03'][1])
			cQuery +=    " FK5.FK5_FILIAL = ? AND"

			AAdd(aBind, {xFilial("FK5"), "S"})
		EndIf
		cQuery +=        " FK5.FK5_RECPAG = ?"

		AAdd(aBind, {"R", "S"})

		cQuery +=    " AND FK5.FK5_DATA = ?"

		AAdd(aBind, {cData, "S"})

		cQuery +=    " AND FK5.FK5_MOEDA = ?"

		AAdd(aBind, {jParams['MV_PAR01'][1], "S"})
		If lAutomato
			cQuery += " AND SE1.E1_CLIENTE = ?"
			AAdd(aBind, {oAutoSV["conditions"][2]["values"][1], "S"})
			cQuery += " AND SE1.E1_LOJA = ?"
			AAdd(aBind, {oAutoSV["conditions"][3]["values"][1], "S"})
			cQuery += " AND SE1.E1_EMISSAO = ?"
			AAdd(aBind, {oAutoSV["conditions"][4]["values"][1], "S"})
		EndIf
		cQuery +=    " AND FK5.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery += " GROUP BY FK5.FK5_FILIAL, FK5.FK5_MOEDA, FK5.FK5_NATURE, SA1.A1_NOME, SE1.E1_VENCTO, SE1.E1_HIST, FK5.FK5_VALOR, FK5.R_E_C_N_O_, SE1.E1_BOLETO"

		cQuery := JurTRepBin(cQuery, aBind)
		cQuery := ChangeQuery( cQuery )
		DbUseArea( .T. , 'TOPCONN' , TcGenQry( ,, cQuery ), cAlsTmp, .T., .F. )
		While (cAlsTmp)->( ! Eof() )
			aAdd(aCashFl, { "3", ;                                                        // 1 - Tipo
			                Iif(lAutomato,oAutoSV["conditions"][4]["values"][1],cData), ; // 2 - Data
			                (cAlsTmp)->CONTA, ;                                           // 3 - Conta
			                (cAlsTmp)->FAVORECIDO, ;                                      // 4 - Favorecido
			                (cAlsTmp)->DATA_HIST, ;                                       // 5 - Data usada no histórico
			                "", ;                                                         // 6 - Histórico
			                (cAlsTmp)->MOEDA, ;                                           // 7 - Moeda
			                (cAlsTmp)->ENTRADAS, ;                                        // 8 - Valor de Entradas
			                (cAlsTmp)->SAIDAS, ;                                          // 9 - Valor de Saídas
			                (cAlsTmp)->SALDO, ;                                           // 10 - Saldo
			                (cAlsTmp)->R_E_C_N_O_, ;                                      // 11 - Recno (Usado para busca de campo MEMO - Histórico)
			                (cAlsTmp)->FILESCRI, ;                                        // 12 - Filial do Escritório
			                "FK5" ;                                                       // 13 - Tabela (Usado para busca de campo MEMO - Histórico)
			              } )

			(cAlsTmp)->( DbSkip() )
		EndDo
		dbSelectArea(cAlsTmp)
		(cAlsTmp)->( DbCloseArea() )

		cAlsTmp := GetNextAlias()
		cQuery  := ""
		aBind   := {}

		// VALORES DE CONTAS A PAGAR PAGOS NO DIA
		cQuery += " SELECT FK5.FK5_NATURE CONTA, SA2.A2_NOME FAVORECIDO, SE2.E2_VENCTO DATA_HIST,"
		cQuery +=        " FK5.FK5_MOEDA MOEDA, 0 ENTRADAS, FK5.FK5_VALOR SAIDAS,"
		cQuery +=        " 0 SALDO, FK5.R_E_C_N_O_, FK5.FK5_FILIAL FILESCRI, 'FK5' ALIASTAB"
		cQuery +=   " FROM " + RetSqlName("FK5") + " FK5"
		cQuery +=  " INNER JOIN " + RetSqlName("FKA") + " FKA1"
		cQuery +=     " ON ?"

		AAdd(aBind, {JurRelFilia("FKA1.FKA_FILIAL", "FK5.FK5_FILIAL"), "U"})

		cQuery +=    " AND FKA1.FKA_IDORIG = FK5.FK5_IDMOV"
		cQuery +=    " AND FKA1.FKA_TABORI = ?"

		AAdd(aBind, {"FK5", "S"})

		cQuery +=    " AND FKA1.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery +=  " INNER JOIN " + RetSqlName("FKA") + " FKA2"
		cQuery +=     " ON ?"

		AAdd(aBind, {JurRelFilia("FKA2.FKA_FILIAL", "FKA1.FKA_FILIAL"), "U"})

		cQuery +=    " AND FKA2.FKA_IDPROC = FKA1.FKA_IDPROC"
		cQuery +=    " AND FKA2.FKA_TABORI = ?"

		AAdd(aBind, {"FK2", "S"})

		cQuery +=    " AND FKA2.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery +=  " INNER JOIN " + RetSqlName("FK2") + " FK2"
		cQuery +=     " ON ?"

		AAdd(aBind, {JurRelFilia("FK2.FK2_FILIAL", "FKA2.FKA_FILIAL" ), "U"})

		cQuery +=    " AND FK2.FK2_IDFK2 = FKA2.FKA_IDORIG"
		cQuery +=    " AND FK2.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery +=  " INNER JOIN " + RetSqlName("FK7") + " FK7"
		cQuery +=     " ON ?"

		AAdd(aBind, {JurRelFilia("FK7.FK7_FILIAL", "FK2.FK2_FILIAL"), "U"})

		cQuery +=    " AND FK7.FK7_IDDOC = FK2.FK2_IDDOC"
		cQuery +=    " AND FK7.FK7_ALIAS = ?"

		AAdd(aBind, {"SE2", "S"})

		cQuery +=    " AND FK7.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery += " INNER JOIN " + RetSqlName("SE2") + " SE2"
		cQuery +=    " ON ?"

		AAdd(aBind, {JurRelFilia("SE2.E2_FILIAL", "FK5.FK5_FILIAL"), "U"})

		cQuery +=   " AND SE2.E2_FILIAL ||'|'|| SE2.E2_PREFIXO ||'|'|| SE2.E2_NUM ||'|'|| SE2.E2_PARCELA ||'|'|| SE2.E2_TIPO ||'|'|| SE2.E2_FORNECE ||'|'|| SE2.E2_LOJA  = FK7.FK7_CHAVE"
		cQuery +=   " AND SE2.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery += " INNER JOIN " + RetSqlName("SA2") + " SA2"
		cQuery +=    " ON ?"

		AAdd(aBind, {JurRelFilia("SA2.A2_FILIAL", "FK5.FK5_FILIAL"), "U"})

		cQuery +=   " AND SA2.A2_COD = SE2.E2_FORNECE"
		cQuery +=   " AND SA2.A2_LOJA = SE2.E2_LOJA"
		cQuery +=   " AND SA2.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery += " WHERE"
		If !Empty(jParams['MV_PAR03'][1])
			cQuery +=    " FK5.FK5_FILIAL = ? AND"

			AAdd(aBind, {xFilial("FK5"), "S"})
		EndIf
		cQuery +=       " FK5.FK5_RECPAG = ?"

		AAdd(aBind, {"P", "S"})

		cQuery +=   " AND FK5.FK5_DATA = ?"
		If lAutomato
			AAdd(aBind, {oAutoSV["conditions"][4]["values"][1], "S"})
		Else
			AAdd(aBind, {cData, "S"})
		EndIf
		cQuery +=   " AND FK5.FK5_MOEDA = ?"

		AAdd(aBind, {jParams['MV_PAR01'][1], "S"})
		If lAutomato
			cQuery += " AND SE2.E2_FORNECE = ?"
			AAdd(aBind, {oAutoSV["conditions"][2]["values"][1], "S"})
			cQuery += " AND SE2.E2_LOJA = ?"
			AAdd(aBind, {oAutoSV["conditions"][3]["values"][1], "S"})
			cQuery += " AND SE2.E2_EMISSAO = ?"
			AAdd(aBind, {oAutoSV["conditions"][4]["values"][1], "S"})
		EndIf
		cQuery +=   " AND FK5.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)
		cQuery := ChangeQuery( cQuery )
		DbUseArea( .T. , 'TOPCONN' , TcGenQry( ,, cQuery ), cAlsTmp, .T., .F. )
		While (cAlsTmp)->( ! Eof() )
			aAdd(aCashFl, { "3", ;                   // 1 - Tipo
			                cData, ;                 // 2 - Data
			                (cAlsTmp)->CONTA, ;      // 3 - Conta
			                (cAlsTmp)->FAVORECIDO, ; // 4 - Favorecido
			                (cAlsTmp)->DATA_HIST, ;  // 5 - Data usada no histórico
			                "", ;                    // 6 - Histórico
			                (cAlsTmp)->MOEDA, ;      // 7 - Moeda
			                (cAlsTmp)->ENTRADAS, ;   // 8 - Valor de Entradas
			                (cAlsTmp)->SAIDAS, ;     // 9 - Valor de Saídas
			                (cAlsTmp)->SALDO, ;      // 10 - Saldo
			                (cAlsTmp)->R_E_C_N_O_, ; // 11 - Recno (Usado para busca de campo MEMO - Histórico)
			                (cAlsTmp)->FILESCRI, ;   // 12 - Filial do Escritório
			                "FK5" ;                  // 13 - Tabela (Usado para busca de campo MEMO - Histórico)
			              } )

			(cAlsTmp)->( DbSkip() )
		EndDo
		dbSelectArea(cAlsTmp)
		(cAlsTmp)->( DbCloseArea() )

		cAlsTmp := GetNextAlias()
		cQuery  := ""
		aBind   := {}

		// LANÇAMENTOS DE ENTRADAS DAS NATUREZAS DE CASHFLOW - BANCO/CAIXA NO DESTINO É ENTRADA
		cQuery += " SELECT OHB.OHB_NATDES CONTA , ' ' FAVORECIDO, OHB.OHB_DTLANC DATA_HIST,"
		cQuery +=        " CASE WHEN OHB.OHB_CMOEC = ? THEN OHB.OHB_CMOEC ELSE OHB.OHB_CMOELC END MOEDA,"

		AAdd(aBind, {jParams['MV_PAR01'][1], "S"})

		cQuery +=        " OHB.OHB_VALOR ENTRADAS, 0 SAIDAS, 0 SALDO, OHB.R_E_C_N_O_, OHB.OHB_FILIAL FILESCRI, 'OHB' ALIASTAB"
		cQuery +=   " FROM " + RetSqlName("OHB") + " OHB"
		cQuery +=  " INNER JOIN " + RetSqlName("SED") + " SED"
		cQuery +=     " ON (?"

		AAdd(aBind, {JurRelFilia("SED.ED_FILIAL", "OHB.OHB_FILIAL"), "U"})

		cQuery +=    " AND OHB.OHB_NATDES = SED.ED_CODIGO"
		cQuery +=    " AND SED.ED_CFJUR = ?"

		AAdd(aBind, {"1", "S"})

		cQuery +=    " AND SED.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery += " WHERE"
		If !Empty(jParams['MV_PAR03'][1])
			cQuery +=    " OHB.OHB_FILIAL = ? AND"

			AAdd(aBind, {xFilial("OHB"), "S"})
		EndIf
		cQuery +=            " OHB.OHB_DTLANC = ?"
		If lAutomato
			AAdd(aBind, {oAutoSV["conditions"][4]["values"][1], "S"})
		Else
			AAdd(aBind, {cData, "S"})
		EndIf
		cQuery +=        " AND OHB.OHB_ORIGEM NOT IN (?)" // (1 - CONTAS A PAGAR / 2 - CONTAS A RECEBER / 7 - Extato)

		AAdd(aBind, {"'1', '2', '7'", "U"})

		cQuery +=        " AND (OHB.OHB_CMOELC = ?"

		AAdd(aBind, {jParams['MV_PAR01'][1], "S"})

		cQuery +=        " OR   OHB.OHB_CMOEC  = ?) "

		AAdd(aBind, {jParams['MV_PAR01'][1], "S"})

		cQuery +=        " AND OHB.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery += " UNION ALL"

		// LANÇAMENTOS DE SAÍDAS DAS NATUREZAS DE CASHFLOW - BANCO/CAIXA NA ORIGEM É SAÍDA
		cQuery += " SELECT OHB.OHB_NATORI CONTA, ' ' FAVORECIDO, OHB.OHB_DTLANC DATA_HIST,"
		cQuery +=          "CASE WHEN OHB.OHB_CMOEC = ? THEN OHB.OHB_CMOEC  ELSE OHB.OHB_CMOELC END MOEDA,"

		AAdd(aBind, {jParams['MV_PAR01'][1], "S"})

		cQuery +=          " 0 ENTRADAS, OHB.OHB_VALOR SAIDAS, 0 SALDO, OHB.R_E_C_N_O_, OHB.OHB_FILIAL FILESCRI, 'OHB' ALIASTAB"
		cQuery +=   " FROM " + RetSqlName("OHB") + " OHB"
		cQuery +=  " INNER JOIN " + RetSqlName("SED") + " SED"
		cQuery +=     " ON ( ?"

		AAdd(aBind, {JurRelFilia("SED.ED_FILIAL", "OHB.OHB_FILIAL"), "U"})

		cQuery +=    " AND SED.ED_CODIGO = OHB.OHB_NATORI"
		cQuery +=    " AND SED.ED_CFJUR = ?"

		AAdd(aBind, {"1", "S"})

		cQuery +=    " AND SED.D_E_L_E_T_ = ?)"

		AAdd(aBind, {" ", "S"})

		cQuery += " WHERE"
		If !Empty(jParams['MV_PAR03'][1])
			cQuery +=        " OHB.OHB_FILIAL = ? AND"

			AAdd(aBind, {xFilial("OHB"), "S"})
		EndIf
		cQuery +=            " OHB.OHB_DTLANC = ?"
		If lAutomato
			AAdd(aBind, {oAutoSV["conditions"][4]["values"][1], "S"})
		Else
			AAdd(aBind, {cData, "S"})
		EndIf
		cQuery +=        " AND OHB.OHB_ORIGEM NOT IN (?)" // (1 - CONTAS A PAGAR / 2 - CONTAS A RECEBER / 7 - Extrato)
		AAdd(aBind, {"'1', '2', '7'", "U"})

		cQuery +=        " AND (OHB.OHB_CMOELC = ?"

		AAdd(aBind, {jParams['MV_PAR01'][1], "S"})

		cQuery +=        " OR   OHB.OHB_CMOEC  = ?)"

		AAdd(aBind, {jParams['MV_PAR01'][1], "S"})

		cQuery +=        " AND OHB.D_E_L_E_T_ = ?"

		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)
		cQuery := ChangeQuery( cQuery )

		DbUseArea( .T. , 'TOPCONN' , TcGenQry( ,, cQuery ), cAlsTmp, .T., .F. )
		While (cAlsTmp)->( ! Eof() )
			aAdd(aCashFl, { "3", ;                                                        // 1 - Tipo
			                Iif(lAutomato,oAutoSV["conditions"][4]["values"][1],cData), ; // 2 - Data
			                (cAlsTmp)->CONTA, ;                                           // 3 - Conta
			                (cAlsTmp)->FAVORECIDO, ;                                      // 4 - Favorecido
			                (cAlsTmp)->DATA_HIST, ;                                       // 5 - Data usada no histórico
			                "", ;                                                         // 6 - Histórico
			                (cAlsTmp)->MOEDA, ;                                           // 7 - Moeda
			                (cAlsTmp)->ENTRADAS, ;                                        // 8 - Valor de Entradas
			                (cAlsTmp)->SAIDAS, ;                                          // 9 - Valor de Saídas
			                (cAlsTmp)->SALDO, ;                                           // 10 - Saldo
			                (cAlsTmp)->R_E_C_N_O_, ;                                      // 11 - Recno (Usado para busca de campo MEMO - Histórico)
			                (cAlsTmp)->FILESCRI, ;                                        // 12 - Filial do Escritório
			                (cAlsTmp)->ALIASTAB ;                                         // 13 - Tabela (Usado para busca de campo MEMO - Histórico)
			              } )

			(cAlsTmp)->( DbSkip() )
		EndDo
		dbSelectArea(cAlsTmp)
		(cAlsTmp)->( DbCloseArea() )

		// Ordena por Escritório, Tipo(1, 2 ou 3) e Data
		aSort( aCashFl,,, { |aX,aY| aX[12]+aX[1]+aX[2] < aY[12]+aY[1]+aY[2] } )

		cFilAnt := cFilialAt
		
		If !Empty(aCashFl)
			cSimbMd   := Posicione("CTO", 1, xFilial("CTO") + jParams["MV_PAR01"][1], "CTO_SIMB") // Pega o Símbolo da Moeda selecionada no Parâmetro
		
			For nLinha = 1 to Len(aCashFl)

				cNomeEscr := Alltrim(JurGetDados("NS7", 4, xFilial("NS7") + aCashFl[nLinha][12] + cEmpAnt, "NS7_RAZAO")) // Pega o nome da filial do primeiro escritório

				//-----------------------
				// Controle para verificar se foi impresso o saldo anterior e o movimento do dia da filial para facilitar as quebras no detail
				//-----------------------.
				nPosFilImp := aScan(aFilImpr, {|aFil| aFil[1] == aCashFl[nLinha][12]} )
				If nPosFilImp == 0
					lForcImp1 := IIf(aCashFl[nLinha][01] != '1','T','F')
					lForcImp2 := IIF(aCashFl[nLinha][01] != '1' .And. aCashFl[nLinha][01] != '2','T','F')
					cTipoAnt  := aCashFl[nLinha][01]
					Aadd(aFilImpr, {aCashFl[nLinha][12], lForcImp1, lForcImp2})
					nPosFilImp := Len(aFilImpr)

				ElseIf aCashFl[nLinha][01] == '3' .And. cTipoAnt == '1'
					cTipoAnt  := ''
					lForcImp2 := 'T'

				Else
					lForcImp1 := aFilImpr[nPosFilImp][2]
					lForcImp2 := aFilImpr[nPosFilImp][3]
					cTipoAnt  := ''
				EndIf

				If aCashFl[nLinha][01] == '1' .Or. lForcImp1 == 'T'
					aFilImpr[nPosFilImp][2]  := 'F'
				EndIf

				If aCashFl[nLinha][01] == '2' .Or. lForcImp2 == 'T'
					aFilImpr[nPosFilImp][3]  := 'F'
				EndIf

				If aCashFl[nLinha][01] == '3' // Verifica o Tipo
					cHistorico := J035HisLanc(aCashFl[nLinha][13] , aCashFl[nLinha][11])
					cHistorico := "(" + DToC(StoD(aCashFl[nLinha][05])) + ") " + cHistorico 
				EndIf

				dData     := totvs.framework.treports.date.dateToTimeStamp(STOD(aCashFl[nLinha][02]))
				dDataHist := totvs.framework.treports.date.dateToTimeStamp(STOD(aCashFl[nLinha][05]))

				oJsDados := JsonObject():new()

				oJsDados["cTipo"]       := aCashFl[nLinha][01]          // 1 - Tipo
				oJsDados["dData"]       := dData                        // 2 - Data
				oJsDados["cConta"]      := aCashFl[nLinha][03]          // 3 - Conta
				oJsDados["cFavorecido"] := AllTrim(aCashFl[nLinha][04]) // 4 - Favorecido
				oJsDados["dDataHist"]   := dDataHist                    // 5 - Data usada no histórico
				oJsDados["cHistorico"]  := cHistorico                   // 6 - Histórico
				oJsDados["nValEntrada"] := aCashFl[nLinha][08]          // 8 - Valor de Entradas
				oJsDados["nValSaida"]   := aCashFl[nLinha][09]          // 9 - Valor de Saídas
				oJsDados["nSaldo"]      := aCashFl[nLinha][10]          // 10 - Saldo
				oJsDados["cFilEscrit"]  := aCashFl[nLinha][12]          // 12 - Filial do Escritório
				oJsDados["cTabela"]     := aCashFl[nLinha][13]          // 13 - Tabela (Usado para busca de campo MEMO - Histórico)
				oJsDados["cSimbMd"]     := cSimbMd                      // Simbolo da Moeda
				oJsDados["nPrazoB"]     := nPrazoB                      // Prazo Boleto
				oJsDados["nPrazoD"]     := nPrazoD                      // Prazo Deposito
				oJsDados["cNomeEscr"]   := cNomeEscr                    // Nome do Escritório
				oJsDados["lForcImp1"]   := lForcImp1        // Força a impressão do primeiro sub-detail
				oJsDados["lForcImp2"]   := lForcImp2        // Força a impressão do segundo sub-detail

				self:oData:appendData(oJsDados)

			Next nLinha
		EndIf
	Else
		self:setErrorStatus(400, "Dicionário de dados desatualizado!", "Grupo de perguntas JURAPAD035 nao encontrado.")
	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurapad035_cash_flowTReportsBusinessObject

	self:oSchema:addProperty("cTipo"      , "cTipo"      , "string" , "cTipo"             , "cTipo"      )
	self:oSchema:addProperty("dData"      , "dData"      , "date"   , "dData"             , "dData"      )
	self:oSchema:addProperty("cConta"     , "cConta"     , "string" , "cConta"            , "cConta"     )
	self:oSchema:addProperty("cFavorecido", "cFavorecido", "string" , "cFavorecido"       , "cFavorecido")
	self:oSchema:addProperty("dDataHist"  , "dDataHist"  , "date"   , "dDataHist"         , "dDataHist"  )
	self:oSchema:addProperty("cHistorico" , "cHistorico" , "string" , "cHistorico"        , "cHistorico" )
	self:oSchema:addProperty("nValEntrada", "nValEntrada", "number" , "nValEntrada"       , "nValEntrada")
	self:oSchema:addProperty("nValSaida"  , "nValSaida"  , "number" , "nValSaida"         , "nValSaida"  )
	self:oSchema:addProperty("nSaldo"     , "nSaldo"     , "number" , "nSaldo"            , "nSaldo"     )
	self:oSchema:addProperty("cFilEscrit" , "cFilEscrit" , "string" , "cFilEscrit"        , "cFilEscrit" )
	self:oSchema:addProperty("cTabela"    , "cTabela"    , "string" , "cTabela"           , "cTabela"    )
	self:oSchema:addProperty("cSimbMd"    , "cSimbMd"    , "string" , "cSimbMd"           , "cSimbMd"    )
	self:oSchema:addProperty("nPrazoB"    , "nPrazoB"    , "number" , "nPrazoB"           , "nPrazoB"    )
	self:oSchema:addProperty("nPrazoD"    , "nPrazoD"    , "number" , "nPrazoD"           , "nPrazoD"    )
	self:oSchema:addProperty("cNomeEscr"  , "cNomeEscr"  , "string" , "cNomeEscr"         , "cNomeEscr"  )
	self:oSchema:addProperty("lForcImp1"  , "lForcImp1"  , "string", "lForcImp1"          , "lForcImp1"  )
	self:oSchema:addProperty("lForcImp2"  , "lForcImp2"  , "string", "lForcImp2"          , "lForcImp2"  )

Return self:oSchema
