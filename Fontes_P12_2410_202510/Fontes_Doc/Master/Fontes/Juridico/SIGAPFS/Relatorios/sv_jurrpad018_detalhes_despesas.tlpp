#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.jurrpad018_detalhes_despesas.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NVY,RD0,NUS,CTO,SA1,NVE,NRH',;
	name='Relatório de detalhes de despesas',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class jurrpad018_detalhes_despesasTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object

	// Metodos próprios SIGAPFS
	protected method loadFields() as array

	data lExistPergunte as logical
	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class jurrpad018_detalhes_despesasTReportsBusinessObject
Local cLojaAuto := SuperGetMv("MV_JLOJAUT" , .F. , "2") //Indica se a Loja do Caso deve ser preenchida automaticamente. (1-Sim; 2-Não)
Local cPergunte := IIF(cLojaAuto == "1", "JURPAD018D", "JURPAD018C")

	_Super:new()
		
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Detalhes de despesas")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de detalhes de despesas")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		//Indica o pergunte que será utilizado no relatório
		self:lExistPergunte := self:setPergunte(cPergunte)
		If !self:lExistPergunte // @12.1.2410
			self:setErrorStatus(400, "Dicionário de dados desatualizado!", "Grupo de perguntas " + cPergunte + " nao encontrado.")
		EndIf
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurrpad018_detalhes_despesasTReportsBusinessObject
Local cLojaAuto as character
Local cQuery  as character
Local jParams as json
Local aBind := {}

	cLojaAuto := SuperGetMv("MV_JLOJAUT" , .F. , "2")

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	// NVY
	cQuery := "SELECT #QueryFields#"
	cQuery += " FROM " + RetSqlName("NVY") + " NVY"
	
	// RD0
	cQuery += " INNER JOIN " + RetSqlName("RD0") + " RD0"
	cQuery +=    " ON RD0.RD0_FILIAL = ?"

	AAdd(aBind, {xFilial("RD0"), "S"})

	cQuery +=   " AND RD0.RD0_CODIGO = NVY.NVY_CPART"
	cQuery +=   " AND RD0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})
	
	// NUS
	cQuery += " INNER JOIN " + RetSqlName("NUS") + " NUS"
	cQuery +=    " ON NUS.NUS_FILIAL = ?"

	AAdd(aBind, {xFilial("NUS"), "S"})

	cQuery +=   " AND NUS.NUS_CPART = NVY.NVY_CPART"
	cQuery +=   " AND NVY.NVY_ANOMES >= NUS.NUS_AMINI"
	cQuery +=   " AND NUS.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO
	cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO"
	cQuery +=    " ON CTO.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=   " AND CTO.CTO_MOEDA = NVY.NVY_CMOEDA"
	cQuery +=   " AND CTO.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// SA1
	cQuery += " INNER JOIN " + RetSqlName("SA1") + " SA1"
	cQuery +=    " ON SA1.A1_FILIAL = ?"

	AAdd(aBind, {xFilial("SA1"), "S"})

	cQuery +=   " AND SA1.A1_COD = NVY.NVY_CCLIEN"
	cQuery +=   " AND SA1.A1_LOJA = NVY.NVY_CLOJA"
	cQuery +=   " AND SA1.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NVE
	cQuery += " INNER JOIN " + RetSqlName("NVE") + " NVE"
	cQuery +=    " ON NVE.NVE_FILIAL = ?"

	AAdd(aBind, {xFilial("NVE"), "S"})

	cQuery +=   " AND NVE.NVE_CCLIEN = NVY.NVY_CCLIEN"
	cQuery +=   " AND NVE.NVE_LCLIEN = NVY.NVY_CLOJA"
	cQuery +=   " AND NVE.NVE_NUMCAS = NVY.NVY_CCASO"
	cQuery +=   " AND NVE.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NRH
	cQuery += " INNER JOIN " + RetSqlName("NRH") + " NRH"
	cQuery +=    " ON NRH.NRH_FILIAL = '" + xFilial("NRH") + "'"
	cQuery +=   " AND NRH.NRH_COD = NVY.NVY_CTPDSP"
	cQuery +=   " AND NRH.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// WHERE
	cQuery += " WHERE  NVY.NVY_FILIAL = ?"

	AAdd(aBind, {xFilial("NVY"), "S"})

	jParams := oFilter:getParameters()
	
	cQuery +=  " AND NVY.NVY_DATA >= ?" // Data Inicial
	cQuery +=  " AND NVY.NVY_DATA <= ?" // Data Final

	AAdd(aBind, {StrTran(SubStr(jParams['MV_PAR01'][1], 1, 10), "-", ""), "S"})
	AAdd(aBind, {StrTran(SubStr(jParams['MV_PAR02'][1], 1, 10), "-", ""), "S"})

	If !Empty(jParams['MV_PAR03'][1])
		cQuery += " AND NVY.NVY_CGRUPO = ?" // Grupo de Clientes

		AAdd(aBind, {jParams['MV_PAR03'][1], "S"})
	EndIf

	If cLojaAuto == "2" // Não é loja automática
		If !Empty(jParams['MV_PAR04'][1]) .And. !Empty(jParams['MV_PAR05'][1]) // Cliente e Loja
			cQuery += " AND NVY.NVY_CCLIEN = ? AND NVY.NVY_CLOJA = ?"

			AAdd(aBind, {jParams['MV_PAR04'][1], "S"})
			AAdd(aBind, {jParams['MV_PAR05'][1], "S"})
		EndIf

		If !Empty(jParams['MV_PAR06'][1]) // Moeda
			cQuery += " AND NVY.NVY_CMOEDA = ?"

			AAdd(aBind, {jParams['MV_PAR06'][1], "S"})
		EndIf

		If !Empty(jParams['MV_PAR07'][1]) // Tipo de Despesa
			cQuery += " AND NVY.NVY_CTPDSP = ?"

			AAdd(aBind, {jParams['MV_PAR07'][1], "S"})
		EndIf
	Else
		If !Empty(jParams['MV_PAR04'][1]) // Cliente e Loja automática
			cQuery += " AND NVY.NVY_CCLIEN = ? AND NVY.NVY_CLOJA = ?"

			AAdd(aBind, {jParams['MV_PAR04'][1], "S"})
			AAdd(aBind, {JurGetLjAt(), "S"})
		EndIf

		If !Empty(jParams['MV_PAR05'][1]) // Moeda
			cQuery += " AND NVY.NVY_CMOEDA = ?"

			AAdd(aBind, {jParams['MV_PAR05'][1], "S"})
		EndIf

		If !Empty(jParams['MV_PAR06'][1]) // Tipo de Despesa
			cQuery += " AND NVY.NVY_CTPDSP = ?"

			AAdd(aBind, {jParams['MV_PAR06'][1], "S"})
		EndIf
	EndIf

	cQuery +=   " AND NVY.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)
	
	// ORDER BY
	self:setOrder("NVY_CCLIEN, NVY_CLOJA, NVY_CCASO, NVY_ANOMES, NVY_CTPDSP, NVY_DATA, NVY_COD")

	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurrpad018_detalhes_despesasTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class jurrpad018_detalhes_despesasTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NVY_CCLIEN", Nil})
	Aadd(aFields, {"NVY_CLOJA" , Nil})
	Aadd(aFields, {"NVY_DATA"  , Nil})
	Aadd(aFields, {"NVY_ANOMES", Nil})
	Aadd(aFields, {"NVY_CTPDSP", Nil})
	Aadd(aFields, {"NVY_CCASO" , Nil})
	Aadd(aFields, {"RD0_SIGLA" , Nil})
	Aadd(aFields, {"NUS_CESCR" , Nil})
	Aadd(aFields, {"NVY_VALOR" , Nil})
	Aadd(aFields, {"NVY_CMOEDA", Nil})
	Aadd(aFields, {"NVY_SITUAC", Nil})
	Aadd(aFields, {"NVY_COD"   , Nil})
	Aadd(aFields, {"NVY_DESCRI", Nil})
	Aadd(aFields, {"CTO_SIMB"  , Nil})
	Aadd(aFields, {"A1_NOME"   , Nil})
	Aadd(aFields, {"NVE_TITULO", Nil})
	Aadd(aFields, {"NRH_DESC"  , Nil})

Return aFields
