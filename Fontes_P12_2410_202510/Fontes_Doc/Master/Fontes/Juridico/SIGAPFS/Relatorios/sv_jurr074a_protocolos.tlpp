#INCLUDE "JURR074A.CH"
#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.jurr074a_protocolos.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NXH,NXI',;
	name='Protocolos',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class jurr074a_protocolos from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()         as object
	public    method getData()     as object
	public    method getSchema()   as object


	data lExistPergunte as logical
	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class jurr074a_protocolos
Local cPergunte := "JURA074A"
	_Super:new()

	_Super:new()
		
	//Define a área  
	self:appendArea(STR0012)

	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0013)

	//Define a descrição do Objeto de Negócio
	self:setDescription(STR0014)
	
	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)
	self:lExistPergunte := self:setPergunte(cPergunte)

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurr074a_protocolos

Local jParams    := oFilter:getParameters()
Local aArea      := GetArea()
Local cQuery     := ""
Local cFaturas   := ""
Local cObs       := ""
Local aBind      := {}
Local cTemp      := GetNextAlias()
Local oJsDados   as object

	/*
		MV_PAR01 - Protocolo Inicial
		MV_PAR02 - Protocolo Final
	*/

	If !self:lPDUserAc
		self:setErrorStatus(400, STR0015, STR0016) // "Acesso Restrito", "Usuário com restrição de acesso a dados pessoais/sensíveis."
	ElseIf self:lExistPergunte // @12.1.2410

		cQuery := " SELECT NXH_FILIAL, NXH_COD, NXH_CTIPO, NXH_CESCR, NXH_FAT,"
		cQuery += " NXH_RZSOC, NXH_LOGRAD, NXH_BAIRRO, NXH_UF,"
		cQuery += " NXH_PAIS, NXH_PAIS, NXH_CEP, NXH_CONTAT,"
		cQuery += " NXH_CODCID, NXH.R_E_C_N_O_ RECNO"
		cQuery += " FROM " + RetSqlName("NXH") + " NXH"
		cQuery += " WHERE "
		cQuery += " NXH.NXH_FILIAL = ?"
		AAdd(aBind, {xFilial("NXH"), "S"})

		cQuery += " AND NXH.NXH_COD >= ?"
		AAdd(aBind, {jParams['MV_PAR01'][1], "S"})

		cQuery += " AND NXH.NXH_COD <= ?"
		AAdd(aBind, {jParams['MV_PAR02'][1], "S"})

		cQuery += " AND NXH.D_E_L_E_T_ = ?"
		AAdd(aBind, {" ", "S"})

		cQuery := JurTRepBin(cQuery, aBind)

		MPSysOpenQuery(cQuery, cTemp)

		While !(cTemp)->(Eof())

			cFaturas := " "

			NXH->( DbGoTo( (cTemp)->RECNO ) )
			cObs := StrTran(NXH->NXH_OBS, Chr(13) + Chr(10), " ")

			If NXI->(DbSeek(xFilial('NXI')+(cTemp)->NXH_COD))
				While !NXI->(Eof()) .and. xFilial('NXI') == NXI->NXI_FILIAL .and. NXI->NXI_CPROT == (cTemp)->NXH_COD
					cFaturas += NXI->NXI_CFAT + ' '
					NXI->(DbSkip())
				End
			Endif

			oJsDados := JsonObject():new()
			oJsDados["NXH_COD"]    := (cTemp)->NXH_COD
			oJsDados["NXH_CTIPO"]  := (cTemp)->NXH_CTIPO
			oJsDados["NXH_CESCR"]  := (cTemp)->NXH_CESCR
			oJsDados["NXH_FAT"]    := (cTemp)->NXH_FAT
			oJsDados["NXH_RZSOC"]  := (cTemp)->NXH_RZSOC
			oJsDados["NXH_LOGRAD"] := (cTemp)->NXH_LOGRAD
			oJsDados["NXH_BAIRRO"] := (cTemp)->NXH_BAIRRO
			oJsDados["NXH_UF"]     := (cTemp)->NXH_UF
			oJsDados["NXH_PAIS"]   := (cTemp)->NXH_PAIS
			oJsDados["NXH_CEP"]    := (cTemp)->NXH_CEP
			oJsDados["NXH_CONTAT"] := (cTemp)->NXH_CONTAT
			oJsDados["NXH_OBS"]    := cObs
			oJsDados["NXH_CODCID"] := (cTemp)->NXH_CODCID
			oJsDados["FATURAS"]    := cFaturas

			self:oData:appendData(oJsDados)

			(cTemp)->(DBSkip())
		EndDo

		(cTemp)->(DbCloseArea())

	Else
		self:setErrorStatus(400, STR0017, STR0018)
	EndIf

	RestArea(aArea)
Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurr074a_protocolos

	self:oSchema:addProperty("NXH_COD"   ,"NXH_COD"   , "string", "NXH_COD"   , "NXH_COD"   )
	self:oSchema:addProperty("NXH_CTIPO" ,"NXH_CTIPO" , "string", "NXH_CTIPO" , "NXH_CTIPO" )
	self:oSchema:addProperty("NXH_CESCR" ,"NXH_CESCR" , "string", "NXH_CESCR" , "NXH_CESCR" )
	self:oSchema:addProperty("NXH_FAT"   ,"NXH_FAT"   , "string", "NXH_FAT"   , "NXH_FAT"   )
	self:oSchema:addProperty("NXH_RZSOC" ,"NXH_RZSOC" , "string", "NXH_RZSOC" , "NXH_RZSOC" )
	self:oSchema:addProperty("NXH_LOGRAD","NXH_LOGRAD", "string", "NXH_LOGRAD", "NXH_LOGRAD")
	self:oSchema:addProperty("NXH_BAIRRO","NXH_BAIRRO", "string", "NXH_BAIRRO", "NXH_BAIRRO")
	self:oSchema:addProperty("NXH_CID"   ,"NXH_CID"   , "string", "NXH_CID"   , "NXH_CID"   )
	self:oSchema:addProperty("NXH_UF"    ,"NXH_UF"    , "string", "NXH_UF"    , "NXH_UF"    )
	self:oSchema:addProperty("NXH_PAIS"  ,"NXH_PAIS"  , "string", "NXH_PAIS"  , "NXH_PAIS"  )
	self:oSchema:addProperty("NXH_CEP"   ,"NXH_CEP"   , "string", "NXH_CEP"   , "NXH_CEP"   )
	self:oSchema:addProperty("NXH_CONTAT","NXH_CONTAT", "string", "NXH_CONTAT", "NXH_CONTAT")
	self:oSchema:addProperty("NXH_OBS"   ,"NXH_OBS"   , "string", "NXH_OBS"   , "NXH_OBS"   )
	self:oSchema:addProperty("NXH_CODCID","NXH_CODCID", "string", "NXH_CODCID", "NXH_CODCID")
	self:oSchema:addProperty("FATURAS"   ,"FATURAS"   , "string", "FATURAS"   , "FATURAS"   )


Return self:oSchema
