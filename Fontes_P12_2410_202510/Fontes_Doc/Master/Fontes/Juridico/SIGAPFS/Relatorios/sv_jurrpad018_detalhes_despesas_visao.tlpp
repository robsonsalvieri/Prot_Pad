#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.jurrpad018_detalhes_despesas_visao.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NVY,RD0,NUS,CTO,SA1,NVE,NRH',;
	name='Visão de dados detalhes de despesas',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class jurrpad018_detalhes_despesas_visaoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object

	data lExistPergunte as logical
	data lPDUserAc as logical

endclass

//------------------------------------------------------------------------------------
method new() class jurrpad018_detalhes_despesas_visaoTReportsBusinessObject
Local cLojaAuto := SuperGetMv("MV_JLOJAUT" , .F. , "2") //Indica se a Loja do Caso deve ser preenchida automaticamente. (1-Sim; 2-Não)
Local cPergunte := IIF(cLojaAuto == "1", "JURPAD018D", "JURPAD018C")

	_Super:new()

	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Visão Detalhes de despesas")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Visão de dados de detalhes de despesas")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		//Indica o pergunte que será utilizado no relatório
		self:lExistPergunte := self:setPergunte(cPergunte)
		If !self:lExistPergunte // @12.1.2410
			self:setErrorStatus(400, "Dicionário de dados desatualizado!", "Grupo de perguntas " + cPergunte + " nao encontrado.")
		EndIf
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurrpad018_detalhes_despesas_visaoTReportsBusinessObject
Local cQuery     := " "
Local cTemp      := GetNextAlias()
Local cLojaAuto  := SuperGetMv("MV_JLOJAUT" , .F. , "2")
Local jParams    as json
Local oJsDados   as object
Local aBind  	 := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	// SELECT
	cQuery := "SELECT NVY_FILIAL, NVY_CGRUPO, NVY_CCLIEN, NVY_CLOJA, A1_NOME, NVY_CCASO, NVE_TITULO, NVY_COD, NVY_DESCRI, NVY_DATA, NVY_ANOMES, NVY_CTPDSP, NRH_DESC, RD0_SIGLA, CTO_MOEDA, CTO_SIMB, NVY_VALOR,"
	cQuery +=  " CASE WHEN NVY_SITUAC = '1' THEN 'Pendente' ELSE 'Concluído' END NVY_SITUAC,"
	cQuery +=  " CASE WHEN NVZ_SITUAC = '1' THEN 'Conferencia' ELSE CASE WHEN NVZ_SITUAC = '2' THEN 'Faturado' ELSE CASE WHEN NVZ_SITUAC = '3' THEN 'WO' ELSE 'Pendente' END END END NVZ_SITUAC,"
	cQuery +=  " COALESCE(NVZ_CESCR, ' ') NVZ_CESCR, COALESCE(NVZ_CFATUR, ' ') NVZ_CFATUR, COALESCE(NVZ_PRECNF, ' ') NVZ_PRECNF, COALESCE(NVZ_CWO, ' ') NVZ_CWO"

	// NVY
	cQuery +=  " FROM " + RetSqlName("NVY") + " NVY"
	
	// RD0
	cQuery += " INNER JOIN " + RetSqlName("RD0") + " RD0"
	cQuery +=    " ON RD0.RD0_FILIAL = ? " // 1 - Filial
	AAdd(aBind, {xFilial("RD0"), "S"})

	cQuery +=   " AND RD0.RD0_CODIGO = NVY.NVY_CPART"
	cQuery +=   " AND RD0.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})
	
	// NUS
	cQuery += " INNER JOIN " + RetSqlName("NUS") + " NUS"
	cQuery +=    " ON NUS.NUS_FILIAL = ? " // 2 - Filial
	AAdd(aBind, {xFilial("NUS"), "S"})

	cQuery +=   " AND NUS.NUS_CPART = NVY.NVY_CPART"
	cQuery +=   " AND NVY.NVY_ANOMES >= NUS.NUS_AMINI"
	cQuery +=   " AND NUS.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// CTO
	cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO"
	cQuery +=    " ON CTO.CTO_FILIAL = ? " // 3 - Filial
	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=   " AND CTO.CTO_MOEDA = NVY.NVY_CMOEDA"
	cQuery +=   " AND CTO.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// SA1
	cQuery += " INNER JOIN " + RetSqlName("SA1") + " SA1"
	cQuery +=    " ON SA1.A1_FILIAL = ? " // 4 - Filial
	AAdd(aBind, {xFilial("SA1"), "S"})

	cQuery +=   " AND SA1.A1_COD = NVY.NVY_CCLIEN"
	cQuery +=   " AND SA1.A1_LOJA = NVY.NVY_CLOJA"
	cQuery +=   " AND SA1.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// NVE
	cQuery += " INNER JOIN " + RetSqlName("NVE") + " NVE"
	cQuery +=    " ON NVE.NVE_FILIAL = ? " // 5 - Filial
	AAdd(aBind, {xFilial("NVE"), "S"})

	cQuery +=   " AND NVE.NVE_CCLIEN = NVY.NVY_CCLIEN"
	cQuery +=   " AND NVE.NVE_LCLIEN = NVY.NVY_CLOJA"
	cQuery +=   " AND NVE.NVE_NUMCAS = NVY.NVY_CCASO"
	cQuery +=   " AND NVE.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// NRH
	cQuery += " INNER JOIN " + RetSqlName("NRH") + " NRH"
	cQuery +=    " ON NRH.NRH_FILIAL = ? " // 6 - Filial
	AAdd(aBind, {xFilial("NRH"), "S"})

	cQuery +=   " AND NRH.NRH_COD = NVY.NVY_CTPDSP"
	cQuery +=   " AND NRH.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	cQuery += " LEFT JOIN " + RetSqlName("NVZ") + " NVZ"
	cQuery +=    " ON NVZ.NVZ_FILIAL = ? " // 7 - Filial
	AAdd(aBind, {xFilial("NVZ"), "S"})

	cQuery +=   " AND NVZ_CDESP = NVY.NVY_COD"
	cQuery +=   " AND NVZ.NVZ_CANC = ?"
	AAdd(aBind, {'2', "S"})

	cQuery +=   " AND NVZ.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// WHERE
	cQuery += " WHERE  NVY.NVY_FILIAL = ? " // 8 - 
	AAdd(aBind, {xFilial("NUY"), "S"})

	jParams := oFilter:getParameters()
	
	cQuery +=  " AND NVY.NVY_DATA >= ? " // 9 - Data Inicial
	Aadd(aBind, {StrTran(SubStr(jParams['MV_PAR01'][1], 1, 10), "-", ""), "S"})

	cQuery +=  " AND NVY.NVY_DATA <= ? " // 10 - Data Final
	Aadd(aBind, {StrTran(SubStr(jParams['MV_PAR02'][1], 1, 10), "-", ""), "S"})

	If !Empty(jParams['MV_PAR03'][1])
		cQuery += " AND NVY.NVY_CGRUPO = '? " // Grupo de Clientes
		Aadd(aBind, {jParams['MV_PAR03'][1], "S"})
	EndIf

	If cLojaAuto == "2" // Não é loja automática
		If !Empty(jParams['MV_PAR04'][1]) .And. !Empty(jParams['MV_PAR05'][1]) // Cliente e Loja
			cQuery += " AND NVY.NVY_CCLIEN = ? " // Cliente
			Aadd(aBind, {jParams['MV_PAR04'][1], "S"})

			cQuery += " AND NVY.NVY_CLOJA = ? " // Loja
			Aadd(aBind, {jParams['MV_PAR05'][1], "S"})
		EndIf

		If !Empty(jParams['MV_PAR06'][1]) // Moeda
			cQuery += " AND NVY.NVY_CMOEDA = ? "
			Aadd(aBind, {jParams['MV_PAR06'][1], "S"})
		EndIf

		If !Empty(jParams['MV_PAR07'][1]) // Tipo de Despesa
			cQuery += " AND NVY.NVY_CTPDSP = ? "
			Aadd(aBind, {jParams['MV_PAR07'][1], "S"})
		EndIf
	Else
		If !Empty(jParams['MV_PAR04'][1]) // Cliente e Loja automática
			cQuery += " AND NVY.NVY_CCLIEN = ? " 
			cQuery += " AND NVY.NVY_CLOJA = ? "
			Aadd(aBind, {jParams['MV_PAR04'][1], "S"})
			Aadd(aBind, {JurGetLjAt(), "S"})
		EndIf

		If !Empty(jParams['MV_PAR05'][1]) // Moeda
			cQuery += " AND NVY.NVY_CMOEDA = ? "
			Aadd(aBind, {jParams['MV_PAR05'][1], "S"})
		EndIf

		If !Empty(jParams['MV_PAR06'][1]) // Tipo de Despesa
			cQuery += " AND NVY.NVY_CTPDSP = ? "
			Aadd(aBind, {jParams['MV_PAR06'][1], "S"})
		EndIf
	EndIf

	// Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND " + oFilter:getSQLExpression()
	EndIf

	cQuery +=   " AND NVY.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})
	
	// ORDER BY
	cQuery += "ORDER BY NVY_CCLIEN, NVY_CLOJA, NVY_CCASO, NVY_ANOMES, NVY_CTPDSP, NVY_DATA, NVY_COD"

	cQuery := JurTRepBin(cQuery, aBind)
	MPSysOpenQuery(cQuery, cTemp)

	While !(cTemp)->(Eof())
		oJsDados := JsonObject():new()
		oJsDados["NVY_FILIAL"] := (cTemp)->NVY_FILIAL
		oJsDados["NVY_CGRUPO"] := (cTemp)->NVY_CGRUPO
		oJsDados["NVY_CCLIEN"] := (cTemp)->NVY_CCLIEN
		oJsDados["NVY_CLOJA"]  := (cTemp)->NVY_CLOJA
		oJsDados["A1_NOME"]    := (cTemp)->A1_NOME
		oJsDados["NVY_CCASO"]  := (cTemp)->NVY_CCASO
		oJsDados["NVE_TITULO"] := (cTemp)->NVE_TITULO
		oJsDados["NVY_COD"]    := (cTemp)->NVY_COD
		oJsDados["NRH_DESC"]   := (cTemp)->NRH_DESC
		oJsDados["NVY_DATA"]   := totvs.framework.treports.date.dateToTimeStamp(STOD((cTemp)->NVY_DATA))
		oJsDados["NVY_ANOMES"] := (cTemp)->NVY_ANOMES
		oJsDados["NVY_CTPDSP"] := (cTemp)->NVY_CTPDSP
		oJsDados["NVY_DESCRI"] := AllTrim(Posicione("NVY", 1, (cTemp)->NVY_FILIAL + (cTemp)->NVY_COD, "NVY_DESCRI"))
		oJsDados["RD0_SIGLA"]  := (cTemp)->RD0_SIGLA
		oJsDados["CTO_MOEDA"]  := (cTemp)->CTO_MOEDA
		oJsDados["CTO_SIMB"]   := (cTemp)->CTO_SIMB
		oJsDados["NVY_VALOR"]  := (cTemp)->NVY_VALOR
		oJsDados["NVY_SITUAC"] := (cTemp)->NVY_SITUAC
		oJsDados["NVZ_SITUAC"] := (cTemp)->NVZ_SITUAC
		oJsDados["NVZ_CESCR"]  := (cTemp)->NVZ_CESCR
		oJsDados["NVZ_CFATUR"] := (cTemp)->NVZ_CFATUR
		oJsDados["NVZ_PRECNF"] := (cTemp)->NVZ_PRECNF
		oJsDados["NVZ_CWO"]    := (cTemp)->NVZ_CWO
		self:oData:appendData(oJsDados)

		(cTemp)->(DBSkip())
	EndDo

	(cTemp)->(DBCloseArea())

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurrpad018_detalhes_despesas_visaoTReportsBusinessObject

	self:oSchema:addProperty("NVY_FILIAL", FWSX3Util():GetDescription("NVY_FILIAL"), "string", "Filial"              , "NVY_FILIAL")
	self:oSchema:addProperty("NVY_CGRUPO", FWSX3Util():GetDescription("NVY_CGRUPO"), "string", "Grupo Cliente"       , "NVY_CGRUPO")
	self:oSchema:addProperty("NVY_CCLIEN", FWSX3Util():GetDescription("NVY_CCLIEN"), "string", "Código Cliente"      , "NVY_CCLIEN")
	self:oSchema:addProperty("NVY_CLOJA" , FWSX3Util():GetDescription("NVY_CLOJA" ), "string", "Endereço"            , "NVY_CLOJA" )
	self:oSchema:addProperty("A1_NOME"   , FWSX3Util():GetDescription("A1_NOME"   ), "string", "Nome"                , "A1_NOME"   )
	self:oSchema:addProperty("NVY_CCASO" , FWSX3Util():GetDescription("NVY_CCASO" ), "string", "Código Caso"         , "NVY_CCASO" )
	self:oSchema:addProperty("NVE_TITULO", FWSX3Util():GetDescription("NVE_TITULO"), "string", "Título Caso"         , "NVE_TITULO")
	self:oSchema:addProperty("NVY_COD"   , FWSX3Util():GetDescription("NVY_COD"   ), "string", "Código Despesa"      , "NVY_COD"   )
	self:oSchema:addProperty("NVY_DESCRI", FWSX3Util():GetDescription("NVY_DESCRI"), "memo"  , "Descrição"           , "NVY_DESCRI")
	self:oSchema:addProperty("NVY_DATA"  , FWSX3Util():GetDescription("NVY_DATA"  ), "date"  , "Data Despesa"        , "NVY_DATA"  )
	self:oSchema:addProperty("NVY_ANOMES", FWSX3Util():GetDescription("NVY_ANOMES"), "string", "Ano-Mês"             , "NVY_ANOMES")
	self:oSchema:addProperty("NVY_CTPDSP", FWSX3Util():GetDescription("NVY_CTPDSP"), "string", "Tipo Despesa"        , "NVY_CTPDSP")
	self:oSchema:addProperty("NRH_DESC"  , FWSX3Util():GetDescription("NRH_DESC"  ), "string", "Descrição Tipo"      , "NRH_DESC"  )
	self:oSchema:addProperty("RD0_SIGLA" , FWSX3Util():GetDescription("RD0_SIGLA" ), "string", "Participante"        , "RD0_SIGLA" )
	self:oSchema:addProperty("CTO_MOEDA" , FWSX3Util():GetDescription("CTO_MOEDA" ), "string", "Moeda"               , "CTO_MOEDA" )
	self:oSchema:addProperty("CTO_SIMB"  , FWSX3Util():GetDescription("CTO_SIMB"  ), "string", "Símbolo"             , "CTO_SIMB"  )
	self:oSchema:addProperty("NVY_VALOR" , FWSX3Util():GetDescription("NVY_VALOR" ), "number", "Valor"               , "NVY_VALOR" )
	self:oSchema:addProperty("NVY_SITUAC", FWSX3Util():GetDescription("NVY_SITUAC"), "string", "Situação da Despesa" , "NVY_SITUAC")
	self:oSchema:addProperty("NVZ_SITUAC", FWSX3Util():GetDescription("NVZ_SITUAC"), "string", "Situação Faturamento", "NVZ_SITUAC")
	self:oSchema:addProperty("NVZ_CESCR" , FWSX3Util():GetDescription("NVZ_CESCR" ), "string", "Escritório"          , "NVZ_CESCR" )
	self:oSchema:addProperty("NVZ_CFATUR", FWSX3Util():GetDescription("NVZ_CFATUR"), "string", "Fatura"              , "NVZ_CFATUR")
	self:oSchema:addProperty("NVZ_PRECNF", FWSX3Util():GetDescription("NVZ_PRECNF"), "string", "Pré-Fatura"          , "NVZ_PRECNF")
	self:oSchema:addProperty("NVZ_CWO"   , FWSX3Util():GetDescription("NVZ_CWO"   ), "string", "WO"                  , "NVZ_CWO"   )

Return self:oSchema
