#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.jurrpad031_wo_visao.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NUF,NXV,RD0,NW0,NVZ,NW4,NWE,NWD',;
	name='Relatório de WO',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class jurrpad031_wo_visaoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object

	data lExistPergunte as logical
	data lPDUserAc as logical
	
endclass

//------------------------------------------------------------------------------------
method new() class jurrpad031_wo_visaoTReportsBusinessObject
Local cPergunte := "JURAPAD031"

	_Super:new()

	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Visão de WO")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Visão de dados de WO")

	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

	If !self:lPDUserAc
		self:setErrorStatus(400, "Acesso restrito.", "Usuário com restrição de acesso a dados pessoais/sensíveis.")
	Else
		//Indica o pergunte que será utilizado no relatório
		self:lExistPergunte := self:setPergunte(cPergunte)
		If !self:lExistPergunte // @12.1.2410
			self:setErrorStatus(400, "Dicionário de dados desatualizado!", "Grupo de perguntas " + cPergunte + " nao encontrado.")
		EndIf
	EndIf

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurrpad031_wo_visaoTReportsBusinessObject
Local cQuery  as character
Local jParams as json
Local cTemp   := GetNextAlias()
Local aBind   := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	// NUF
	cQuery := "SELECT NUF.NUF_COD, CASE WHEN NUF.NUF_SITUAC = '1' THEN 'Ativo' ELSE 'Cancelado' END NUF_SITUAC, COALESCE(NUF.NUF_DTEMI, ' ') NUF_DTEMI,"
	cQuery +=   " NUF.NUF_CMOTEM, NXV.NXV_DESC MOTDESEMI, NUF.NUF_OBSEMI, RD0.RD0_SIGLA,"
	cQuery +=   " COALESCE(NUF.NUF_DTCAN, ' ') NUF_DTCAN, NUF.NUF_CMOTCA, NXVCAN.NXV_DESC MOTDESCAN, NUF.NUF_OBSCAN, COALESCE(NUF.NUF_USRCAN, ' ') NUF_USRCAN,"
	cQuery +=   " NUF.NUF_CESCR, NUF.NUF_CFATU,"
	cQuery +=   " CASE WHEN COUNT(NW0_CTS) > 0 THEN 'Sim' ELSE 'Não' END TEM_TS,"
	cQuery +=   " CASE WHEN COUNT(NVZ_CDESP) > 0 THEN 'Sim' ELSE 'Não' END TEM_DP,"
	cQuery +=   " CASE WHEN COUNT(NW4_CLTAB) > 0 THEN 'Sim' ELSE 'Não' END TEM_LT,"
	cQuery +=   " CASE WHEN COUNT(NWD_CFTADC) > 0 THEN 'Sim' ELSE 'Não' END TEM_FA,"
	cQuery +=   " CASE WHEN COUNT(NWE_CFIXO) > 0 THEN 'Sim' ELSE 'Não' END TEM_FX"
	cQuery +=  " FROM " + RetSqlName("NUF") + " NUF"

	// NXV EMISSÃO
	cQuery +=  " INNER JOIN " + RetSqlName("NXV") + " NXV"
	cQuery +=     " ON NXV.NXV_FILIAL = ?"

	AAdd(aBind, {xFilial("NXV"), "S"})

	cQuery +=    " AND NXV.NXV_COD = NUF.NUF_CMOTEM"
	cQuery +=    " AND NXV.D_E_L_E_T_ = ?"
	
	AAdd(aBind, {" ", "S"})

	// NXV CANCELAMENTO
	cQuery += " LEFT OUTER JOIN " + RetSqlName("NXV") + " NXVCAN"
	cQuery +=    " ON NXVCAN.NXV_FILIAL = ?"

	AAdd(aBind, {xFilial("NXV"), "S"})

	cQuery +=   " AND NXVCAN.NXV_COD = NUF.NUF_CMOTCA"
	cQuery +=   " AND NXVCAN.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// RD0
	cQuery += " INNER JOIN " + RetSqlName("RD0") + " RD0"
	cQuery +=    " ON RD0.RD0_FILIAL = ?"

	AAdd(aBind, {xFilial("RD0"), "S"})

	cQuery +=   " AND RD0.RD0_USER = NUF.NUF_USREMI"
	cQuery +=   " AND RD0.D_E_L_E_T_ = ?"
	
	AAdd(aBind, {" ", "S"})

	// NW0
	cQuery +=  " LEFT OUTER JOIN " + RetSqlName("NW0") + " NW0"
	cQuery +=    " ON NW0.NW0_FILIAL = ?"

	AAdd(aBind, {xFilial("NW0"), "S"})

	cQuery +=   " AND NW0.NW0_CWO = NUF.NUF_COD"
	cQuery +=   " AND NW0.D_E_L_E_T_ = ?"
	
	AAdd(aBind, {" ", "S"})

	// NVZ
	cQuery +=  " LEFT OUTER JOIN " + RetSqlName("NVZ") + " NVZ"
	cQuery +=    " ON NVZ.NVZ_FILIAL = ?"

	AAdd(aBind, {xFilial("NVZ"), "S"})

	cQuery +=   " AND NVZ.NVZ_CWO = NUF.NUF_COD"
	cQuery +=   " AND NVZ.D_E_L_E_T_ = ?"
	
	AAdd(aBind, {" ", "S"})

	// NW4
	cQuery +=  " LEFT OUTER JOIN " + RetSqlName("NW4") + " NW4"
	cQuery +=    " ON NW4.NW4_FILIAL = ?"

	AAdd(aBind, {xFilial("NW4"), "S"})

	cQuery +=   " AND NW4.NW4_CWO = NUF.NUF_COD"
	cQuery +=   " AND NW4.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NWE
	cQuery +=  " LEFT OUTER JOIN " + RetSqlName("NWE") + " NWE"
	cQuery +=    " ON NWE.NWE_FILIAL = ?"

	AAdd(aBind, {xFilial("NWE"), "S"})

	cQuery +=   " AND NWE.NWE_CWO = NUF.NUF_COD"
	cQuery +=   " AND NWE.D_E_L_E_T_ = ?"
	
	AAdd(aBind, {" ", "S"})

	// NWD
	cQuery +=  " LEFT OUTER JOIN " + RetSqlName("NWD") + " NWD"
	cQuery +=    " ON NWD.NWD_FILIAL = ?"

	AAdd(aBind, {xFilial("NWD"), "S"})

	cQuery +=   " AND NWD.NWD_CWO = NUF.NUF_COD"
	cQuery +=   " AND NWD.D_E_L_E_T_ = ?" 

	AAdd(aBind, {" ", "S"})

	// WHERE
	cQuery += " WHERE NUF.NUF_FILIAL = ?"

	AAdd(aBind, {xFilial("NUF"), "S"})

	jParams := oFilter:getParameters()
	
	cQuery +=  " AND NUF.NUF_DTEMI >= ? AND NUF.NUF_DTEMI <= ?"

	AAdd(aBind, {StrTran(SubStr(jParams['MV_PAR01'][1], 1, 10), "-", ""), "S"})
	AAdd(aBind, {StrTran(SubStr(jParams['MV_PAR02'][1], 1, 10), "-", ""), "S"})

	If jParams['MV_PAR03'][1] == 1 // somente os ativos
		cQuery += " AND NUF.NUF_SITUAC = ?"

		AAdd(aBind, {"1", "S"})
	Endif

	cQuery +=   " AND NUF.D_E_L_E_T_ = ?"
	
	AAdd(aBind, {" ", "S"})

	// GROUP BY
	cQuery += " GROUP BY NUF.NUF_COD, NUF.NUF_SITUAC, NUF.NUF_DTEMI, NUF.NUF_CMOTEM,"
	cQuery +=          " NUF.NUF_OBSEMI, NUF.NUF_DTCAN, NUF.NUF_OBSCAN, NUF.NUF_CFATU, NUF.NUF_CESCR,
	cQuery +=          " NUF.NUF_CMOTCA, NUF.NUF_USRCAN, RD0.RD0_SIGLA, NXV.NXV_COD, NXV.NXV_DESC, NXVCAN.NXV_COD, NXVCAN.NXV_DESC "
	
	// ORDER BY
	cQuery += " ORDER BY NUF.NUF_CMOTEM, NUF.NUF_COD"

	cQuery := JurTRepBin(cQuery, aBind)

	DBUseArea(.T., "TOPCONN", TCGenQry(NIL,NIL,cQuery), (cTemp) ,.F., .T. )

	While !(cTemp)->(Eof())
		oJsDados := JsonObject():new()
		oJsDados["NUF_COD"]         := (cTemp)->NUF_COD
		oJsDados["NUF_SITUAC"]      := (cTemp)->NUF_SITUAC
		oJsDados["NUF_DTEMI"]       := totvs.framework.treports.date.stringToTimeStamp((cTemp)->NUF_DTEMI)
		oJsDados["NUF_CMOTEM"]      := (cTemp)->NUF_CMOTEM
		oJsDados["MOTDESEMI"]       := AllTrim((cTemp)->MOTDESEMI)
		oJsDados["NUF_OBSEMI"]      := (cTemp)->NUF_OBSEMI
		oJsDados["RD0_SIGLA"]       := (cTemp)->RD0_SIGLA
		oJsDados["NUF_DTCAN"]       := totvs.framework.treports.date.stringToTimeStamp((cTemp)->NUF_DTCAN)
		oJsDados["NUF_CMOTCA"]      := (cTemp)->NUF_CMOTCA
		oJsDados["MOTDESCAN"]       := AllTrim((cTemp)->MOTDESCAN)
		oJsDados["NUF_OBSCAN"]      := (cTemp)->NUF_OBSCAN
		oJsDados["NUF_USRCAN"]      := IIF(Empty((cTemp)->NUF_USRCAN), '', Posicione("RD0", 1, xFilial("RD0") + JurUsuario((cTemp)->NUF_USRCAN), "RD0_SIGLA"))
		oJsDados["NUF_CESCR"]       := (cTemp)->NUF_CESCR
		oJsDados["NUF_CFATU"]       := (cTemp)->NUF_CFATU
		oJsDados["TEM_TS"]          := (cTemp)->TEM_TS
		oJsDados["TEM_DP"]          := (cTemp)->TEM_DP
		oJsDados["TEM_LT"]          := (cTemp)->TEM_LT
		oJsDados["TEM_FA"]          := (cTemp)->TEM_FA
		oJsDados["TEM_FX"]          := (cTemp)->TEM_FX
		self:oData:appendData(oJsDados)

		(cTemp)->(DBSkip())
	EndDo

	(cTemp)->(DBCloseArea())

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurrpad031_wo_visaoTReportsBusinessObject
  
	self:oSchema:addProperty("NUF_COD"        , "NUF_COD"   , "string", "Código WO"                , "NUF_COD"   )
	self:oSchema:addProperty("NUF_SITUAC"     , "NUF_SITUAC", "string", "Situação do WO"           , "NUF_SITUAC")
	self:oSchema:addProperty("NUF_DTEMI"      , "NUF_DTEMI" , "date"  , "Data Emissão"             , "NUF_DTEMI" )
	self:oSchema:addProperty("NUF_CMOTEM"     , "NUF_CMOTEM", "string", "Cód. Motivo Emissão"      , "NUF_CMOTEM")
	self:oSchema:addProperty("MOTDESEMI"      , "MOTDESEMI" , "string", "Desc. Motivo Emissão"     , "NXV_DESC"  )
	self:oSchema:addProperty("NUF_OBSEMI"     , "NUF_OBSEMI", "string", "Obs. Emissão"             , "NUF_OBSEMI")
	self:oSchema:addProperty("RD0_SIGLA"      , "RD0_SIGLA" , "string", "Participante Emissão"     , "RD0_SIGLA" )
	self:oSchema:addProperty("NUF_DTCAN"      , "NUF_DTCAN" , "date"  , "Data Cancelamento"        , "NUF_DTCAN" )
	self:oSchema:addProperty("NUF_CMOTCA"     , "NUF_CMOTCA", "string", "Cód. Motivo Cancelamento" , "NUF_CMOTCA")
	self:oSchema:addProperty("MOTDESCAN"      , "MOTDESCAN" , "string", "Desc. Motivo Cancelamento", "NXV_DESC"  )
	self:oSchema:addProperty("NUF_OBSCAN"     , "NUF_OBSCAN", "string", "Obs. Cancelamento"        , "NUF_OBSCAN")
	self:oSchema:addProperty("NUF_USRCAN"     , "NUF_USRCAN", "string", "Participante Cancelamento", "NUF_USRCAN")
	self:oSchema:addProperty("NUF_CESCR"      , "NUF_CESCR" , "string", "Escritório"               , "NUF_CESCR" )
	self:oSchema:addProperty("NUF_CFATU"      , "NUF_CFATU" , "string", "Fatura"                   , "NUF_CFATU" )
	self:oSchema:addProperty("TEM_TS"         , "TEM_TS"    , "string", "Tem TimeSheet"            , "TEM_TS"    )
	self:oSchema:addProperty("TEM_DP"         , "TEM_DP"    , "string", "Tem Despesa"              , "TEM_DP"    )
	self:oSchema:addProperty("TEM_LT"         , "TEM_LT"    , "string", "Tem Lanc. Tabelado"       , "TEM_LT"    )
	self:oSchema:addProperty("TEM_FA"         , "TEM_FA"    , "string", "Tem Fatura Adicional"     , "TEM_FA"    )
	self:oSchema:addProperty("TEM_FX"         , "TEM_FX"    , "string", "Tem Fixo"                 , "TEM_FX"    )

Return self:oSchema
