#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203_limite_por_fatura.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NXB,NXA,NT0,NXF,CTO',;
	name='Relatório de Fatura - SubReport Limite por Fatura',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju203_limite_por_faturaTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array
endclass

//------------------------------------------------------------------------------------
method new() class ju203_limite_por_faturaTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Fatura - SubReport Limite por Fatura")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Fatura - SubReport Limite por Fatura")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju203_limite_por_faturaTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQLName("NXB") + " NXB"

	// NXA
	cQuery += " INNER JOIN " + RetSQLName("NXA") + " NXA"
	cQuery +=    " ON NXA.NXA_FILIAL = ?"

	AAdd(aBind, {xFilial("NXA"), "S"})

	cQuery +=   " AND NXA.NXA_CESCR = NXB.NXB_CESCR"
	cQuery +=   " AND NXA.NXA_COD = NXB.NXB_CFATUR"
	cQuery +=   " AND NXA.D_E_L_E_T_= ?"

	AAdd(aBind, {" ", "S"})

	// NT0
	cQuery += " INNER JOIN " + RetSQLName("NT0") + " NT0"
	cQuery +=    " ON NT0.NT0_FILIAL = ?"

	AAdd(aBind, {xFilial("NT0"), "S"})

	cQuery +=   " AND NT0.NT0_COD = NXB.NXB_CCONTR"
	cQuery +=   " AND NT0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXF_LIM
	cQuery +=  " LEFT OUTER JOIN " + RetSQLName("NXF") + " NXF_LIM"
	cQuery +=    " ON NXF_LIM.NXF_FILIAL = ?"

	AAdd(aBind, {xFilial("NXF"), "S"})

	cQuery +=   " AND NXF_LIM.NXF_CMOEDA = NT0.NT0_CMOELI"
	cQuery +=   " AND NXF_LIM.NXF_CESCR = NXA.NXA_CESCR"
	cQuery +=   " AND NXF_LIM.NXF_CFATUR = NXA.NXA_COD"
	cQuery +=   " AND NXF_LIM.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO_LIM
	cQuery +=  " LEFT OUTER JOIN " + RetSQLName("CTO") + " CTO_LIM"
	cQuery +=    " ON CTO_LIM.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=   " AND CTO_LIM.CTO_MOEDA = NT0_CMOELI"
	cQuery +=   " AND CTO_LIM.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXF_FAT
	cQuery += " LEFT OUTER JOIN " + RetSQLName("NXF") + " NXF_FAT"
	cQuery +=    " ON NXF_FAT.NXF_FILIAL = ?"

	AAdd(aBind, {xFilial("NXF"), "S"})

	cQuery +=   " AND NXF_FAT.NXF_CFATUR = NXA.NXA_COD"
	cQuery +=   " AND NXF_FAT.NXF_CESCR = NXA.NXA_CESCR"
	cQuery +=   " AND NXF_FAT.NXF_CMOEDA = NXA.NXA_CMOEDA"
	cQuery +=   " AND NXF_FAT.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO_FAT
	cQuery +=  " LEFT OUTER JOIN " + RetSQLName("CTO") + " CTO_FAT"
	cQuery +=    " ON CTO_FAT.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=   " AND CTO_FAT.CTO_MOEDA = NXA.NXA_CMOEDA"
	cQuery +=   " AND CTO_FAT.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// WHERE
	cQuery += " WHERE NXB.NXB_FILIAL = ?" 

	AAdd(aBind, {xFilial("NXB"), "S"})

	// Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf

	cQuery += " AND NXB.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)
	
	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju203_limite_por_faturaTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju203_limite_por_faturaTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"CTO_FAT.CTO_SIMB"  , "FAT_SIMB"})
	Aadd(aFields, {"CTO_LIM.CTO_SIMB"  , "LIM_SIMB"})
	Aadd(aFields, {"NT0.NT0_VLRLIF"    , Nil})
	Aadd(aFields, {"NT0.NT0_CMOELI"    , Nil})
	Aadd(aFields, {"NXF_FAT.NXF_COTAC1", "FAT_COTAC1"})
	Aadd(aFields, {"NXF_LIM.NXF_COTAC1", "LIM_COTAC1"})
	Aadd(aFields, {"NXB.NXB_CESCR"     , Nil})
	Aadd(aFields, {"NXB.NXB_CFATUR"    , Nil})
	Aadd(aFields, {"NXB.NXB_CCONTR"    , Nil})
	Aadd(aFields, {"CTO_FAT.CTO_MOEDA" , "FAT_MOEDA"})
	Aadd(aFields, {"CTO_LIM.CTO_MOEDA" , "LIM_MOEDA"})
	Aadd(aFields, {"NXB.NXB_SLDANT"    , Nil})
	Aadd(aFields, {"NXB.NXB_SLDPRO"    , Nil})
	Aadd(aFields, {"NXB.NXB_VTS"       , Nil})

Return aFields
