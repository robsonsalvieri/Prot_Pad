#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203_totalizador_faturamento_por_caso.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NXC,NW0,NW4,NVZ',;
	name='Relatório de Fatura - SubReport Totalizador Faturamento',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju203_totalizador_faturamento_por_casoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object

endclass

//------------------------------------------------------------------------------------
method new() class ju203_totalizador_faturamento_por_casoTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Fatura - SubReport Totalizador Faturamento por Caso")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Fatura - SubReport Totalizador Faturamento por Caso")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju203_totalizador_faturamento_por_casoTReportsBusinessObject
Local cQuery   := ""
Local aBind    := {}
Local cTemp    := GetNextAlias()
Local oJsDados := Nil

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery +=  "SELECT NXC_CFATUR, NXC_CESCR, NXC_CCASO, NXC_CCLIEN, NXC_CLOJA,"
	cQuery +=  " (SELECT SUM(TOTAL) TOTALFAT FROM ("

	// NXD
	cQuery +=  " SELECT COALESCE(SUM(NXD_VLCORR), 0) TOTAL"
	cQuery +=    " FROM " + RetSQLName("NXD") + " NXD"
	cQuery +=  " WHERE NXD.NXD_FILIAL = ?"

	AAdd(aBind, {xFilial("NXD"), "S"})

	cQuery +=  " AND NXD.NXD_CESCR = NXC.NXC_CESCR"
	cQuery +=  " AND NXD.NXD_CFATUR = NXC.NXC_CFATUR"
	cQuery +=  " AND NXD.NXD_CCASO = NXC.NXC_CCASO"
	cQuery +=  " AND NXD.NXD_CCLIEN = NXC.NXC_CCLIEN"
	cQuery +=  " AND NXD.NXD_CLOJA = NXC.NXC_CLOJA"
	cQuery +=  " AND NXD.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery +=  " UNION"

	// NW4
	cQuery +=  " SELECT COALESCE(SUM(NW4_VALORH), 0) TOTAL"
	cQuery +=  " FROM " + RetSQLName("NW4") + " NW4"
	cQuery +=  " WHERE NW4.NW4_FILIAL = ?"

	AAdd(aBind, {xFilial("NW4"), "S"})

	cQuery +=  " AND NW4.NW4_CESCR = NXC.NXC_CESCR"
	cQuery +=  " AND NW4.NW4_CFATUR = NXC.NXC_CFATUR"
	cQuery +=  " AND NW4.NW4_CCASO = NXC.NXC_CCASO"
	cQuery +=  " AND NW4.NW4_CCLIEN = NXC.NXC_CCLIEN"
	cQuery +=  " AND NW4.NW4_CLOJA = NXC.NXC_CLOJA"
	cQuery +=  " AND NW4.NW4_SITUAC = ?"

	AAdd(aBind, {"2", "S"})

	cQuery +=  " AND NW4.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery +=  " UNION"

	// NVZ
	cQuery +=  " SELECT  COALESCE(SUM(NVZ_VALORD / CASE WHEN NVZ_COTAC2 = 0 THEN 1 ELSE NVZ_COTAC2 END), 0) TOTAL"
	cQuery +=  " FROM " + RetSQLName("NVZ") + " NVZ"
	cQuery +=  " WHERE NVZ.NVZ_FILIAL = ?"

	AAdd(aBind, {xFilial("NVZ"), "S"})

	cQuery +=  " AND NVZ.NVZ_CESCR = NXC.NXC_CESCR"
	cQuery +=  " AND NVZ.NVZ_CFATUR = NXC.NXC_CFATUR"
	cQuery +=  " AND NVZ.NVZ_CCASO = NXC.NXC_CCASO"
	cQuery +=  " AND NVZ.NVZ_CCLIEN = NXC.NXC_CCLIEN"
	cQuery +=  " AND NVZ.NVZ_CLOJA = NXC.NXC_CLOJA"
	cQuery +=  " AND NVZ.NVZ_SITUAC = ?"

	AAdd(aBind, {"2", "S"})

	cQuery +=  " AND NVZ.D_E_L_E_T_ = ?) A) TOTALFAT"

	AAdd(aBind, {" ", "S"})

	cQuery +=  " FROM " + RetSQLName("NXC") + " NXC"

	cQuery += " WHERE NXC.NXC_FILIAL = ?"

	AAdd(aBind, {xFilial("NXC"), "S"})

	// Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf

	cQuery +=   " AND NXC.D_E_L_E_T_  =  ?"

	AAdd(aBind, {" ", "S"}) 

	cQuery := JurTRepBin(cQuery, aBind)

	DBUseArea(.T., "TOPCONN", TCGenQry(NIL,NIL,cQuery), (cTemp) ,.F., .T. )

	Do While !(cTemp)->(Eof())
		oJsDados := JsonObject():new()
		oJsDados["NXC_CESCR"] := (cTemp)->NXC_CESCR
		oJsDados["NXC_CFATUR"] := (cTemp)->NXC_CFATUR
		oJsDados["NXC_CCASO"] := (cTemp)->NXC_CCASO
		oJsDados["NXC_CCLIEN"] := (cTemp)->NXC_CCLIEN
		oJsDados["NXC_CLOJA"] := (cTemp)->NXC_CLOJA
		oJsDados["TOTALFAT"] := (cTemp)->TOTALFAT
		self:oData:appendData(oJsDados)

		(cTemp)->(DBSkip())
	EndDo

	(cTemp)->(DBCloseArea())

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju203_totalizador_faturamento_por_casoTReportsBusinessObject

	self:oSchema:addProperty("NXC_CESCR", "Escritorio", "string", "Escritorio", "NXC_CESCR")
	self:oSchema:addProperty("NXC_CFATUR", "Fatura", "string", "Fatura", "NXC_CFATUR")
	self:oSchema:addProperty("NXC_CCASO", "Caso", "string", "Caso", "NXC_CCASO")
	self:oSchema:addProperty("NXC_CCLIEN", "Cliente", "string", "Cliente", "NXC_CCLIEN")
	self:oSchema:addProperty("NXC_CLOJA", "Loja", "string", "Loja", "NXC_CLOJA")
	self:oSchema:addProperty("TOTALFAT", "Totalizador", "number", "Totalizador", "NXC_VLTS")

Return self:oSchema
