#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203_totalizador_faturamento.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NXA,NW0,NW4,NVZ',;
	name='Relatório de Fatura - SubReport Totalizador Faturamento',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju203_totalizador_faturamentoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
endclass

//------------------------------------------------------------------------------------
method new() class ju203_totalizador_faturamentoTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Fatura - SubReport Totalizador Faturamento")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Fatura - SubReport Totalizador Faturamento")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju203_totalizador_faturamentoTReportsBusinessObject
Local cQuery   := ""
Local aBind    := {}
Local cTemp    := GetNextAlias()
Local oJsDados := Nil

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery +=  "SELECT NXA_COD, NXA_CESCR, "
	cQuery +=  "(SELECT SUM(TOTAL) TOTALFAT FROM ("

	// NXD
	cQuery +=  " SELECT COALESCE(SUM(NXD_VLCORR), 0) TOTAL"
	cQuery +=    " FROM " + RetSQLName("NXD") + " NXD"
	cQuery +=  " WHERE NXD.NXD_FILIAL = ?"

	AAdd(aBind, {xFilial("NXD"), "S"})

	cQuery +=  " AND NXD.NXD_CESCR = NXA.NXA_CESCR"
	cQuery +=  " AND NXD.NXD_CFATUR = NXA.NXA_COD"
	cQuery +=  " AND NXD.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery +=  " UNION"

	// NW4
	cQuery +=  " SELECT COALESCE(SUM(NW4_VALORH), 0) TOTAL"
	cQuery +=  " FROM " + RetSQLName("NW4") + " NW4"
	cQuery +=  " WHERE NW4.NW4_FILIAL = ?"

	AAdd(aBind, {xFilial("NW4"), "S"})

	cQuery +=  " AND NW4_CESCR = NXA.NXA_CESCR"
	cQuery +=  " AND NW4_CFATUR = NXA.NXA_COD"
	cQuery +=  " AND NW4_SITUAC = ?"

	AAdd(aBind, {"2", "S"})

	cQuery +=  " AND NW4.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery +=  " UNION"

	// NVZ
	cQuery +=  " SELECT  COALESCE(SUM(NVZ_VALORD / CASE WHEN NVZ_COTAC2 = 0 THEN 1 ELSE NVZ_COTAC2 END), 0) TOTAL"
	cQuery +=  " FROM " + RetSQLName("NVZ") + " NVZ"
	cQuery +=  " WHERE NVZ.NVZ_FILIAL = ?"

	AAdd(aBind, {xFilial("NVZ"), "S"})

	cQuery +=  " AND NVZ_CESCR = NXA.NXA_CESCR"
	cQuery +=  " AND NVZ_CFATUR = NXA.NXA_COD"
	cQuery +=  " AND NVZ_SITUAC = ?"

		AAdd(aBind, {"2", "S"})

	cQuery +=  " AND NVZ.D_E_L_E_T_ = ' ') A) TOTALFAT"

	cQuery +=  " FROM " + RetSQLName("NXA") + " NXA"

	cQuery += " WHERE NXA_FILIAL = ?"

	AAdd(aBind, {xFilial("NXA"), "S"})

	// Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf

	cQuery +=   " AND NXA.D_E_L_E_T_  =  ?"

	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)

	DBUseArea(.T., "TOPCONN", TCGenQry(NIL,NIL,cQuery), (cTemp) ,.F., .T. )

	If !(cTemp)->(Eof())
		oJsDados := JsonObject():new()
		oJsDados["TOTALFAT"] := (cTemp)->TOTALFAT
		self:oData:appendData(oJsDados)
	EndIf

	(cTemp)->(DBCloseArea()) 

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju203_totalizador_faturamentoTReportsBusinessObject

	self:oSchema:addProperty("NXA_CESCR", "Escritorio", "string", "Escritorio", "NXA_CESCR")
	self:oSchema:addProperty("NXA_COD", "Fatura", "string", "Fatura", "NXA_COD")
	self:oSchema:addProperty("TOTALFAT", "Totalizador", "number", "Totalizador", "NXA_VLHFAT")

Return self:oSchema
