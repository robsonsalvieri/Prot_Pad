#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203_totalizador_tempo.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NXA,NW0,NW4,NVZ',;
	name='Relatório de Fatura - SubReport Totalizador Faturamento',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju203_totalizador_tempoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
endclass

//------------------------------------------------------------------------------------
method new() class ju203_totalizador_tempoTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Fatura - SubReport Totalizador Tempo")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Fatura - SubReport Totalizador Tempo")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju203_totalizador_tempoTReportsBusinessObject
Local cQuery   := ""
Local aBind    := {}
Local cTemp    := GetNextAlias()
Local oJsDados := Nil

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := "SELECT"
	cQuery +=  " NXD_CESCR, NXD_CFATUR, NXD_CCLIEN, NXD_CLOJA, NXD_CCASO, NXD_CCONTR,"
	cQuery +=  " Sum(NXD.NXD_VLADVG) AS vTotalTS,"
	cQuery +=  " Sum(NXD.NXD_UTCLI+NXD.NXD_UTREV) AS vTotalTempo"
	cQuery +=  " FROM " + RetSQLName("NXD") + " NXD"
	
	// NXF
	cQuery +=  " LEFT JOIN " + RetSQLName("NXF") + " NXF_TS"
	cQuery +=    " ON NXF_TS.NXF_FILIAL = ?"

	AAdd(aBind, {xFilial("NXF"), "S"})

	cQuery +=   " AND NXF_TS.NXF_CESCR = NXD.NXD_CESCR"
	cQuery +=   " AND NXF_TS.NXF_CFATUR = NXD.NXD_CFATUR"
	cQuery +=   " AND NXF_TS.NXF_CMOEDA = NXD.NXD_CMOEDT"
	cQuery +=   " AND NXF_TS.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO_TS
	cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO_TS"
	cQuery +=    " ON CTO_TS.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})
	
	cQuery +=   " AND CTO_TS.CTO_MOEDA = NXD.NXD_CMOEDT"
	cQuery +=   " AND CTO_TS.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NT0
	cQuery += " INNER JOIN " + RetSQLName("NT0") + " NT0"
	cQuery +=    " ON NT0.NT0_FILIAL = ?"

	AAdd(aBind, {xFilial("NT0"), "S"})

	cQuery +=   " AND NT0.NT0_COD = NXD.NXD_CCONTR"
	cQuery +=   " AND NT0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NRA
	cQuery += " INNER JOIN " + RetSQLName("NRA") + " NRA"
	cQuery +=    " ON NRA.NRA_FILIAL = ?"

	AAdd(aBind, {xFilial("NRA"), "S"})

	cQuery +=   " AND NRA.NRA_COD = NT0.NT0_CTPHON"
	cQuery +=   " AND NRA.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// RD0
	cQuery += " INNER JOIN " + RetSQLName("RD0") + " RD0"
	cQuery +=    " ON RD0.RD0_FILIAL = ?"

	AAdd(aBind, {xFilial("RD0"), "S"})

	cQuery +=   " AND RD0.RD0_CODIGO = NXD.NXD_CPART"
	cQuery +=   " AND RD0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NRN
	cQuery += " INNER JOIN " + RetSQLName("NRN") + " NRN"
	cQuery +=    " ON NRN.NRN_FILIAL = ?"

	AAdd(aBind, {xFilial("NRN"), "S"})

	cQuery +=   " AND NRN.NRN_COD = NXD.NXD_CCATEG"
	cQuery +=   " AND NRN.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXF_FT
	cQuery +=  " LEFT JOIN " + RetSQLName("NXF") + " NXF_FT"
	cQuery +=    " ON NXF_FT.NXF_FILIAL = ?"

	AAdd(aBind, {xFilial("NXF"), "S"})

	cQuery +=   " AND NXF_FT.NXF_CESCR = NXD.NXD_CESCR"
	cQuery +=   " AND NXF_FT.NXF_CFATUR = NXD.NXD_CFATUR"
	cQuery +=   " AND NXF_FT.NXF_CMOEDA = NXD.NXD_CMOEDF"
	cQuery +=   " AND NXF_FT.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO_FT
	cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO_FT"
	cQuery +=    " ON CTO_FT.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=   " AND CTO_FT.CTO_MOEDA = NXD.NXD_CMOEDF"
	cQuery +=   " AND CTO_FT.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NV4
	cQuery +=  " LEFT JOIN " + RetSQLName("NV4") + " NV4"
	cQuery +=    " ON NV4.NV4_FILIAL = ?"

	AAdd(aBind, {xFilial("NV4"), "S"})

	cQuery +=   " AND NV4.NV4_COD = NXD.NXD_CODTAB"
	cQuery +=   " AND NV4.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NRD
	cQuery +=  " LEFT JOIN " + RetSQLName("NRD") + " NRD"
	cQuery +=    " ON NRD.NRD_FILIAL = ?"

	AAdd(aBind, {xFilial("NRD"), "S"})

	cQuery +=   " AND NRD.NRD_COD = NV4.NV4_CTPSRV"
	cQuery +=   " AND NRD.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NS7
	cQuery += " INNER JOIN " + RetSQLName("NS7") + " NS7"
	cQuery +=    " ON NS7.NS7_FILIAL = ?"

	AAdd(aBind, {xFilial("NS7"), "S"})

	cQuery +=   " AND NS7.NS7_COD = NXD.NXD_CESCR"
	cQuery +=   " AND NS7.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// WHERE
	cQuery += " WHERE NXD.NXD_FILIAL = ?"

	AAdd(aBind, {xFilial("NXD"), "S"})

	cQuery +=   " AND ( NXD.NXD_CODTAB = ?"

	AAdd(aBind, {" ", "S"})

	cQuery +=   " OR NRD.NRD_COBMAI = ?"

	AAdd(aBind, {"1", "S"})

	cQuery +=   " AND NXD.NXD_VLADVG > NV4.NV4_VLHFAT)"
	cQuery +=   " AND NRA.NRA_COBRAH = ?"

	AAdd(aBind, {"1", "S"})

	cQuery +=   " AND NXD.NXD_VLADVG > ?"

	AAdd(aBind, {0, "N"})

	// Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf

	cQuery +=   " AND NXD.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"}) 

	cQuery += " GROUP BY NXD_CESCR, NXD_CFATUR, NXD_CCLIEN, NXD_CLOJA, NXD_CCASO, NXD_CCONTR"

	cQuery := JurTRepBin(cQuery, aBind)

	DBUseArea(.T., "TOPCONN", TCGenQry(NIL,NIL,cQuery), (cTemp) ,.F., .T. )

	If !(cTemp)->(Eof())
		oJsDados := JsonObject():new()
		oJsDados["NXD_CCONTR"]  := (cTemp)->NXD_CCONTR
		oJsDados["NXD_CLOJA"]   := (cTemp)->NXD_CLOJA
		oJsDados["NXD_CCASO"]   := (cTemp)->NXD_CCASO
		oJsDados["NXD_CESCR"]   := (cTemp)->NXD_CESCR
		oJsDados["NXD_CFATUR"]  := (cTemp)->NXD_CFATUR
		oJsDados["NXD_CCLIEN"]  := (cTemp)->NXD_CCLIEN
		oJsDados["vTotalTS"]    := (cTemp)->vTotalTS
		oJsDados["vTotalTempo"] := (cTemp)->vTotalTempo
		self:oData:appendData(oJsDados)
	EndIf

	(cTemp)->(DBCloseArea())

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju203_totalizador_tempoTReportsBusinessObject

	self:oSchema:addProperty("NXD_CCONTR", "Contrato", "string", "NXD_CCONTR", "NXD_CCONTR")
	self:oSchema:addProperty("NXD_CLOJA", "Loja", "string", "NXD_CLOJA", "NXD_CLOJA")
	self:oSchema:addProperty("NXD_CCASO", "Caso", "string", "NXD_CCASO", "NXD_CCASO")
	self:oSchema:addProperty("NXD_CESCR", "Escritorio", "string", "NXD_CESCR", "NXD_CESCR")
	self:oSchema:addProperty("NXD_CFATUR", "Fatura", "string", "NXD_CFATUR", "NXD_CFATUR")
	self:oSchema:addProperty("NXD_CCLIEN", "Cliente", "string", "NXD_CCLIEN", "NXD_CCLIEN")
	self:oSchema:addProperty("vTotalTS", "Total TimeSheet", "number", "vTotalTS", "NXD_VLADVG")
	self:oSchema:addProperty("vTotalTempo", "Total Tempo", "number", "vTotalTempo", "NXD_VLADVG")

Return self:oSchema
