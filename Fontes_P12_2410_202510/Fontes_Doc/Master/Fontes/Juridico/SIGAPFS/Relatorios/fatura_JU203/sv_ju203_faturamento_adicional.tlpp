#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203_faturamento_adicional.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NS7,NXA,NWD,NVV,CTO',;
	name='Relatório de Fatura - SubReport Faturamento Adicional',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju203_faturamento_adicionalTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array
endclass

//------------------------------------------------------------------------------------
method new() class ju203_faturamento_adicionalTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Fatura - SubReport Faturamento Adicional")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Fatura - SubReport Faturamento Adicional")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju203_faturamento_adicionalTReportsBusinessObject
Local cQuery  := ""
Local aBind   := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQLName("NS7") + " NS7"

	// NXA
	cQuery+= " LEFT JOIN " + RetSQLName("NXA") + " NXA"
	cQuery +=    " ON NXA.NXA_FILIAL = ?"

	AAdd(aBind, {xFilial("NXA"), "S"})

	cQuery +=   " AND NXA.NXA_CESCR = NS7.NS7_COD"
	cQuery +=   " AND NXA.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NWD
	cQuery += " INNER JOIN " + RetSQLName("NWD") + " NWD"
	cQuery +=    " ON NWD.NWD_FILIAL = ?"

	AAdd(aBind, {xFilial("NWD"), "S"})

	cQuery +=   " AND NWD.NWD_CFATUR = NXA.NXA_COD"
	cQuery +=   " AND NWD.NWD_CESCR = NXA.NXA_CESCR"
	cQuery +=   " AND NWD.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NVV
	cQuery += " INNER JOIN " + RetSQLName("NVV") + " NVV"
	cQuery +=    " ON NVV.NVV_FILIAL = ?"

	AAdd(aBind, {xFilial("NVV"), "S"})

	cQuery +=   " AND NVV.NVV_COD = NWD.NWD_CFTADC"
	cQuery +=   " AND NVV.NVV_CESCR = NS7.NS7_COD"
	cQuery +=   " AND NVV.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO
	cQuery += " LEFT JOIN " + RetSQLName("CTO") + " CTO"
	cQuery +=   " ON CTO.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=  " AND CTO.CTO_MOEDA = NXA.NXA_CMOEDA" 
	cQuery +=  " AND CTO.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO_1
	cQuery += " LEFT JOIN " + RetSQLName("CTO") + " CTO_1"
	cQuery +=   " ON CTO_1.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=  " AND CTO_1.CTO_MOEDA = NVV.NVV_CMOE1"
	cQuery +=  " AND CTO_1.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO_2
	cQuery += " LEFT JOIN " + RetSQLName("CTO") + " CTO_2"
	cQuery +=    " ON CTO_2.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=   " AND CTO_2.CTO_MOEDA = NVV.NVV_CMOE2"
	cQuery +=   " AND CTO_2.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// CTO_3
	cQuery += " LEFT JOIN " + RetSQLName("CTO") + " CTO_3"
	cQuery +=   " ON CTO_3.CTO_FILIAL = ?"

	AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=  " AND CTO_3.CTO_MOEDA = NVV.NVV_CMOE4"
	cQuery +=  " AND CTO_3.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NS7_1
	cQuery += " LEFT JOIN " + RetSQLName("NS7") + " NS7_1"
	cQuery +=   " ON NS7_1.NS7_FILIAL = ?"

	AAdd(aBind, {xFilial("NS7"), "S"})

	cQuery +=   " AND NS7_1.NS7_COD = NXA.NXA_CESCR"
	cQuery +=   " AND NS7_1.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery += " WHERE"
	cQuery += " NS7.NS7_FILIAL = ?"

	AAdd(aBind, {xFilial("NS7"), "S"})

	// Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf

	cQuery += " AND NS7.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)

	// ORDER BY
	self:setOrder("NVV_CCLIEN")

	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju203_faturamento_adicionalTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju203_faturamento_adicionalTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NVV.NVV_CCLIEN"    , Nil         })
	Aadd(aFields, {"NVV.NVV_VALORH"    , Nil         })
	Aadd(aFields, {"NVV.NVV_VALORD"    , Nil         })
	Aadd(aFields, {"NWD.NWD_CFATUR"    , Nil         })
	Aadd(aFields, {"NWD.NWD_CESCR"     , Nil         })
	Aadd(aFields, {"CTO.CTO_MOEDA"     , "CTO_MOEDA" })
	Aadd(aFields, {"CTO.CTO_SIMB"      , "CTO_SIMB"  })
	Aadd(aFields, {"NWD.NWD_VALORH"    , Nil         })
	Aadd(aFields, {"NWD.NWD_VALORD"    , Nil         })
	Aadd(aFields, {"NWD.NWD_COTAC1"    , Nil         })
	Aadd(aFields, {"NWD.NWD_COTAC2"    , Nil         })
	Aadd(aFields, {"NWD.NWD_COTAC3"    , Nil         })
	Aadd(aFields, {"NWD.NWD_VALORT"    , Nil         })
	Aadd(aFields, {"NWD.NWD_COTAC4"    , Nil         })
	Aadd(aFields, {"NVV.NVV_VALORT"    , Nil         })
	Aadd(aFields, {"NXA.NXA_CMOEDA"    , Nil         })
	Aadd(aFields, {"NVV.NVV_CMOE1"     , Nil         })
	Aadd(aFields, {"NVV.NVV_CMOE2"     , Nil         })
	Aadd(aFields, {"NVV.NVV_CMOE4"     , Nil         })
	Aadd(aFields, {"NVV.NVV_DSPCAS"    , Nil         })
	Aadd(aFields, {"CTO_1.CTO_SIMB"    , "CTO_1_SIMB"})
	Aadd(aFields, {"CTO_2.CTO_SIMB"    , "CTO_2_SIMB"})
	Aadd(aFields, {"CTO_3.CTO_SIMB"    , "CTO_3_SIMB"})

Return aFields
