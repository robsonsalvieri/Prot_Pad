#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203_timesheet_nao_cobravel.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NUE,NW0,CTO,NXC,RD0,NV4,NRC,NRD,NS7,NTJ',;
	name='Relatório de Fatura - SubReport timesheet não cobrável',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class ju203_timesheet_nao_cobravelTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array
endclass

//------------------------------------------------------------------------------------
method new() class ju203_timesheet_nao_cobravelTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Relatório de Fatura - SubReport TimeSheet não Cobrável")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Relatório de Fatura - SubReport TimeSheet não Cobrável")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ju203_timesheet_nao_cobravelTReportsBusinessObject
Local cQuery := ""
Local aBind  := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQLName("NUE") + " NUE"

	// NW0
	cQuery += " INNER JOIN " + RetSQLName("NW0") + " NW0"
	cQuery +=    " ON NW0.NW0_FILIAL = ?"

		AAdd(aBind, {xFilial("NW0"), "S"})

	cQuery +=   " AND NW0.NW0_CTS = NUE.NUE_COD"
	cQuery +=   " AND NW0.D_E_L_E_T_= ?"

	AAdd(aBind, {" ", "S"})

	// CTO
	cQuery += " INNER JOIN " + RetSQLName("CTO") + " CTO"
	cQuery +=    " ON CTO.CTO_FILIAL = ?"

		AAdd(aBind, {xFilial("CTO"), "S"})

	cQuery +=   " AND CTO.CTO_MOEDA = NUE_CMOEDA"
	cQuery +=   " AND CTO.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NXC
	cQuery += " INNER JOIN " + RetSQLName("NXC") + " NXC"
	cQuery +=    " ON NXC.NXC_FILIAL = ?"

	AAdd(aBind, {xFilial("NXC"), "S"})

	cQuery +=   " AND NXC.NXC_CCLIEN = NUE.NUE_CCLIEN"
	cQuery +=   " AND NXC.NXC_CLOJA  = NUE.NUE_CLOJA"
	cQuery +=   " AND NXC.NXC_CCASO  = NUE.NUE_CCASO"
	cQuery +=   " AND NXC.NXC_CFATUR = NW0.NW0_CFATUR"
	cQuery +=   " AND NXC.NXC_CESCR  = NW0.NW0_CESCR"
	cQuery +=   " AND NXC.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// RD0
	cQuery += " INNER JOIN " + RetSQLName("RD0") + " RD0"
	cQuery +=    " ON RD0.RD0_FILIAL = ?"

	AAdd(aBind, {xFilial("RD0"), "S"})

	cQuery +=   " AND RD0.RD0_CODIGO = NUE.NUE_CPART2"
	cQuery +=   " AND RD0.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NV4
	cQuery +=  " LEFT OUTER JOIN " + RetSQLName("NV4") + " NV4"
	cQuery +=    " ON NV4.NV4_FILIAL = ?"

	AAdd(aBind, {xFilial("NV4"), "S"})

	cQuery +=   " AND NV4.NV4_COD = NUE.NUE_CLTAB"
	cQuery +=   " AND NV4.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NRC
	cQuery += " INNER JOIN " + RetSQLName("NRC") + " NRC"
	cQuery +=    " ON NRC.NRC_FILIAL = ?"

	AAdd(aBind, {xFilial("NRC"), "S"})

	cQuery +=   " AND NRC.NRC_COD = NUE.NUE_CATIVI"
	cQuery +=   " AND NRC.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NRD
	cQuery +=  " LEFT OUTER JOIN " + RetSQLName("NRD") + " NRD"
	cQuery +=    " ON NRD.NRD_FILIAL = ?"

	AAdd(aBind, {xFilial("NRD"), "S"})

	cQuery +=   " AND NRD.NRD_COD = NV4.NV4_CTPSRV"
	cQuery +=   " AND NRD.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NS7
	cQuery += " INNER JOIN " + RetSQLName("NS7") + " NS7"
	cQuery +=    " ON NS7.NS7_FILIAL = ?"

	AAdd(aBind, {xFilial("NS7"), "S"})

	cQuery +=   " AND NS7.NS7_COD = NXC.NXC_CESCR"
	cQuery +=   " AND NS7.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	// NTJ
	cQuery += " LEFT JOIN " + RetSQLName("NTJ") + " NTJ"
	cQuery +=    " ON NTJ.NTJ_FILIAL = ?"

	AAdd(aBind, {xFilial("NTJ"), "S"})

	cQuery +=   " AND NTJ.NTJ_CCONTR = NXC.NXC_CCONTR"
	cQuery +=   " AND NTJ.D_E_L_E_T_= ?"

	AAdd(aBind, {" ", "S"})

	// WHERE
	cQuery += " WHERE NUE.NUE_FILIAL = ?"

	AAdd(aBind, {xFilial("NUE"), "S"})

	cQuery +=   " AND (NRC.NRC_TEMPOZ = ? OR NUE.NUE_COBRAR = ? OR NTJ.NTJ_CTPATV = NUE.NUE_CATIVI"

	AAdd(aBind, {"2", "S"})
	AAdd(aBind, {"2", "S"})

	cQuery +=    " OR ((NUE.NUE_VALOR1 = ? AND NUE.NUE_UTR > ? AND NUE.NUE_CLTAB = ?)"

	AAdd(aBind, {0, "N"})
	AAdd(aBind, {0, "N"})
	AAdd(aBind, {" ", "S"})

 	cQuery +=    " OR (NRD.NRD_COBMAI = ? AND NUE.NUE_VALOR1 < NV4.NV4_VLHFAT)"

	AAdd(aBind, {"1", "S"})

 	cQuery +=    " OR (NRD.NRD_COBMAI = ?)))"

	AAdd(aBind, {"2", "S"})

	//Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	Else
		cQuery += " AND NUE_COD = ?"

		AAdd(aBind, {" ", "S"})
	EndIf

	cQuery +=   " AND NUE.D_E_L_E_T_ = ?"

	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)

	// ORDER BY
	self:setOrder("NUE_CCLIEN, NUE_CCASO, NUE_DATATS, NUE_CPART2")
	
	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class ju203_timesheet_nao_cobravelTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class ju203_timesheet_nao_cobravelTReportsBusinessObject
Local aFields := {}

	Aadd(aFields, {"NUE.NUE_COD"   , Nil}) 
	Aadd(aFields, {"NUE.NUE_CCLIEN", Nil})
	Aadd(aFields, {"NUE.NUE_CCASO" , Nil})
	Aadd(aFields, {"NUE.NUE_DATATS", Nil})
	Aadd(aFields, {"NUE.NUE_CPART2", Nil})
	Aadd(aFields, {"NXC.NXC_CFATUR", Nil})
	Aadd(aFields, {"NXC.NXC_CESCR" , Nil})
	Aadd(aFields, {"NUE.NUE_UTR"   , Nil})
	Aadd(aFields, {"NUE.NUE_CLOJA" , Nil})
	Aadd(aFields, {"RD0.RD0_SIGLA" , Nil})
	Aadd(aFields, {"NRC.NRC_TEMPOZ", Nil})
	Aadd(aFields, {"NUE.NUE_COBRAR", Nil})
	Aadd(aFields, {"NTJ.NTJ_CTPATV", Nil})
	Aadd(aFields, {"NUE.NUE_CATIVI", Nil})
	Aadd(aFields, {"NUE.NUE_DESC"  , Nil})
	Aadd(aFields, {"NUE.NUE_VALOR1", Nil})
	Aadd(aFields, {"NUE.NUE_CLTAB" , Nil})
	Aadd(aFields, {"NRD.NRD_COBMAI", Nil})
	Aadd(aFields, {"NV4.NV4_VLHFAT", Nil})

Return aFields
