#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203a.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., Team='SIGAPFS', Tables='NXA,NVN,SU5', Name='Carta de Cobrança (JU203a) - SubRelatorio Copia da Fatura', Country='ALL',initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class JU203aCopiaFaturaTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as array
endclass

//------------------------------------------------------------------------------------
method new() class JU203aCopiaFaturaTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Carta de Cobrança (JU203a) - SubRelatorio Cópia da Fatura")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Subreport referente aos dados da cópia da fatura.")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class JU203aCopiaFaturaTReportsBusinessObject
Local cQuery  := ""
Local aBind   := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQlName("NXA") + " NXA"
	cQuery += " INNER JOIN " + RetSQlName("NS7") + " NS7"
	cQuery +=    " ON NS7.NS7_FILIAL = ?" // xFilial("NS7")

	AAdd(aBind, {xFilial("NS7"), "S"})

	cQuery +=   " AND NS7.NS7_COD = NXA.NXA_CESCR"
	cQuery +=   " AND NS7.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	cQuery += " INNER JOIN " + RetSQlName("NVN") + " NVN"
	cQuery +=    " ON NVN.NVN_FILIAL = ?"

	AAdd(aBind, {xFilial("NVN"), "S"})

	cQuery +=   " AND NVN.NVN_CESCR = NXA.NXA_CESCR"
	cQuery +=   " AND NVN.NVN_CFATUR = NXA.NXA_COD"
	cQuery +=   " AND NVN.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	cQuery += " INNER JOIN " + RetSQlName("SU5") + " SU5"
	cQuery +=    " ON SU5.U5_FILIAL = ?"

	AAdd(aBind, {xFilial("SU5"), "S"})

	cQuery +=   " AND SU5.U5_CODCONT = NVN.NVN_CCONT"
	cQuery +=   " AND SU5.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	cQuery += " WHERE NXA.NXA_FILIAL = ?"

	AAdd(aBind, {xFilial("NXA"), "S"})

	//Concatena na query filtros criados na interface
	If oFilter:hasFilter()
		cQuery += " AND ?"

			AAdd(aBind, {oFilter:getSQLExpression(), "U"})
	EndIf

	cQuery +=   " AND NXA.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	cQuery := JurTRepBin(cQuery, aBind)
	
	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class JU203aCopiaFaturaTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class JU203aCopiaFaturaTReportsBusinessObject
Local aFields := {}

	Aadd(aFields,{"NXA.NXA_COD"   , Nil})
	Aadd(aFields,{"NXA.NXA_CESCR" , Nil})
	Aadd(aFields,{"NXA.NXA_CFTADC", Nil})
	Aadd(aFields,{"NXA.NXA_CJCONT", Nil})
	Aadd(aFields,{"NXA.NXA_RAZSOC", Nil})
	Aadd(aFields,{"NXA.NXA_CIDADE", Nil})
	Aadd(aFields,{"NXA.NXA_DTEMI" , Nil})
	Aadd(aFields,{"NXA.NXA_CIDIO2", Nil})
	Aadd(aFields,{"SU5.U5_CEP"    , Nil})
	Aadd(aFields,{"SU5.U5_MUN"    , Nil})
	Aadd(aFields,{"SU5.U5_EST"    , Nil})
	Aadd(aFields,{"SU5.U5_END"    , Nil})
	Aadd(aFields,{"SU5.U5_CONTAT" , Nil})
	Aadd(aFields,{"NVN.NVN_CFATAD", Nil})
	Aadd(aFields,{"NVN.NVN_DESC"  , Nil})
	Aadd(aFields,{"NVN.NVN_CJCONT", Nil})
	Aadd(aFields,{"NVN.NVN_CCONTR", Nil})
	Aadd(aFields,{"NXA.NXA_CCONTR", Nil})
	Aadd(aFields,{"NS7.NS7_RAZAO" , Nil})

Return aFields
