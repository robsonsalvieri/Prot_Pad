#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.legal.sigapfs.ju203a.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team='SIGAPFS', tables='NXA,CTO,NS7,RD0,SA6,SU5,SX5,NVN', name='Carta de Cobrança', country='ALL',initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class JU203aTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()       as object
	public    method getData()   as object
	public    method getSchema() as object
	
	// Metodos próprios SIGAPFS
	protected method loadFields() as Array
endclass

//------------------------------------------------------------------------------------
method new() class JU203aTReportsBusinessObject
	_Super:new()
	
	//Define a área
	self:appendArea("Pré-Faturamento Serviços")

	//Define o nome do Objeto de Negócio
	self:setDisplayName("Carta de Cobrança")

	//Define a descrição do Objeto de Negócio
	self:setDescription("Carta de Cobrança")

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class JU203aTReportsBusinessObject
Local cQuery  := ""
Local aBind   := {}

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	cQuery := "SELECT #QueryFields#"
	cQuery +=  " FROM " + RetSQLName("NXA") + " NXA"
	
	// CTO
	cQuery +=  " LEFT JOIN " + RetSQLName("CTO") + " CTO"
	cQuery +=    " ON CTO.CTO_FILIAL = NXA.NXA_FILIAL"
	cQuery +=   " AND CTO.CTO_MOEDA = NXA.NXA_CMOEDA"
	cQuery +=   " AND CTO.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// NS7
	cQuery += " INNER JOIN " + RetSQLName("NS7") + " NS7"
	cQuery +=    " ON NS7.NS7_FILIAL = NXA.NXA_FILIAL"
	cQuery +=   " AND NS7.NS7_COD = NXA.NXA_CESCR"
	cQuery +=   " AND NS7.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})
	
	// SA1
	cQuery += " INNER JOIN " + RetSQLName("SA1") + " SA1"
	cQuery +=    " ON SA1.A1_FILIAL = ?" // xFilial("SA1")

	AAdd(aBind, {xFilial("SA1"), "S"})

	cQuery +=   " AND SA1.A1_COD = NXA.NXA_CLIPG"
	cQuery +=   " AND SA1.A1_LOJA = NXA.NXA_LOJPG"
	cQuery +=   " AND SA1.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// RD0 - RD0_SOCIO
	cQuery += " INNER JOIN " + RetSQLName("RD0") + " RD0_SOCIO"
	cQuery +=    " ON RD0_SOCIO.RD0_FILIAL = NXA.NXA_FILIAL"
	cQuery +=   " AND RD0_SOCIO.RD0_CODIGO = NXA.NXA_CPART"
	cQuery +=   " AND RD0_SOCIO.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// RD0 - RD0_USUEMI
	cQuery += " INNER JOIN " + RetSQLName("RD0") + " RD0_EMI"
	cQuery +=    " ON RD0_EMI.RD0_FILIAL = NXA.NXA_FILIAL"
	cQuery +=   " AND RD0_EMI.RD0_CODIGO = NXA.NXA_USUEMI"
	cQuery +=   " AND RD0_EMI.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// SA6
	cQuery +=  " LEFT JOIN " + RetSQLName("SA6") + " SA6"
	cQuery +=    " ON SA6.A6_FILIAL = NXA.NXA_FILIAL"
	cQuery +=   " AND SA6.A6_COD = NXA.NXA_CBANCO"
	cQuery +=   " AND SA6.A6_AGENCIA = NXA.NXA_CAGENC"
	cQuery +=   " AND SA6.A6_NUMCON = NXA.NXA_CCONTA"
	cQuery +=   " AND SA6.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// SU5
	cQuery +=  " LEFT JOIN " + RetSQLName("SU5") + " SU5"
	cQuery +=    " ON SU5.U5_FILIAL = NXA.NXA_FILIAL"
	cQuery +=   " AND SU5.U5_CODCONT = NXA.NXA_CCONT"
	cQuery +=   " AND SU5.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// SX5
	cQuery +=  " LEFT JOIN " + RetSQLName("SX5") + " SX5"
	cQuery +=    " ON SX5.X5_FILIAL = SU5.U5_FILIAL"
	cQuery +=   " AND SX5.X5_TABELA = 'AX'"
	cQuery +=   " AND SX5.X5_CHAVE = SU5.U5_TRATA"
	cQuery +=   " AND SX5.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// NVN
	cQuery +=  " LEFT JOIN " + RetSQLName("NVN") + " NVN"
	cQuery +=    " ON NVN.NVN_FILIAL = NXA.NXA_FILIAL"
	cQuery +=   " AND NVN.NVN_CJCONT = NXA.NXA_CJCONT"
	cQuery +=   " AND NVN.NVN_CCONTR = NXA.NXA_CCONTR"
	cQuery +=   " AND NVN.NVN_CFATAD = NXA.NXA_CFTADC"
	cQuery +=   " AND NVN.NVN_CLIPG = NXA.NXA_CLIPG"
	cQuery +=   " AND NVN.NVN_LOJPG = NXA.NXA_LOJPG"
	cQuery +=   " AND NVN.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})

	// WHERE
	cQuery += " WHERE NXA.NXA_FILIAL = ?" 

	AAdd(aBind, {xFilial("NXA"), "S"})

	//Concatena na query filtros criados na interface
	If oFilter:hasFilter()

		cQuery += " AND ?" //oFilter:getSQLExpression()

		AAdd(aBind, {oFilter:getSQLExpression(), "U"})

	EndIf
	
	cQuery +=   " AND NXA.D_E_L_E_T_ = ?"
	AAdd(aBind, {" ", "S"})
	
	cQuery := JurTRepBin(cQuery, aBind)

	self:setQuery(cQuery)

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class JU203aTReportsBusinessObject
Local aStruct := JurTRepStruct(self:loadFields())
Local nFil    := 0

	For nFil := 1 To Len(aStruct)
		self:oSchema:addProperty(aStruct[nFil][1], aStruct[nFil][2], aStruct[nFil][3], aStruct[nFil][4], aStruct[nFil][5], aStruct[nFil][6], aStruct[nFil][7], aStruct[nFil][8])
	Next nFil

Return self:oSchema

//------------------------------------------------------------------------------------
method loadFields() as array class JU203aTReportsBusinessObject
Local aFields := {}
Local cValIss := IIf(GetNewPar("MV_DESCISS",.F.),"'TRUE'", "'FALSE'") // Se o cliente recolhe ISS

	Aadd(aFields,{"NXA.NXA_CESCR"                                   , "NXA_CESCR"    })
	Aadd(aFields,{"NXA.NXA_COD"                                     , Nil            })
	Aadd(aFields,{"NXA.NXA_TIPO"                                    , Nil            })
	Aadd(aFields,{"NXA.NXA_DTEMI"                                   , Nil            })
	Aadd(aFields,{"NXA.NXA_DTVENC"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_VLREMB"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_VLGROS"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_VLTRIB"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_VLTXAD"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_VLFATH"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_VLFATD"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_VLDESC"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_VGROSH"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_PIRRF"                                   , Nil            })
	Aadd(aFields,{"NXA.NXA_VLACRE"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_IRRF"                                    , Nil            })
	Aadd(aFields,{"NXA.NXA_PPIS"                                    , Nil            })
	Aadd(aFields,{"NXA.NXA_PIS"                                     , Nil            })
	Aadd(aFields,{"NXA.NXA_PCOFIN"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_COFINS"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_PCSLL"                                   , Nil            })
	Aadd(aFields,{"NXA.NXA_CSLL"                                    , Nil            })
	Aadd(aFields,{"NXA.NXA_PINSS"                                   , Nil            })
	Aadd(aFields,{"NXA.NXA_INSS"                                    , Nil            })
	Aadd(aFields,{"NXA.NXA_ISS"                                     , Nil            })
	Aadd(aFields,{"NXA.NXA_CCLIEN"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_CJCONT"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_CCONTR"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_DREFFH"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_DREFFD"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_DREFFT"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_RAZSOC"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_ENDENT"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_TPPAG"                                   , Nil            })
	Aadd(aFields,{"NXA.NXA_CGCCPF"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_PAIS"                                    , Nil            })
	Aadd(aFields,{"NXA.NXA_ESTADO"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_CIDADE"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_CEP"                                     , Nil            })
	Aadd(aFields,{"NXA.NXA_LOGRAD"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_VUADIA"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_VSADIA"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_FPAGTO"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_CFTADC"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_CIDIO2"                                  , Nil            })
	Aadd(aFields,{"NXA.NXA_FATACU"                                  , Nil            })
	Aadd(aFields,{"CTO.CTO_SIMB"                                    , Nil            })
	Aadd(aFields,{"NS7.NS7_CFILIA"                                  , Nil            })
	Aadd(aFields,{"NS7.NS7_RAZAO"                                   , Nil            })
	Aadd(aFields,{"NVN.NVN_CJCONT"                                  , Nil            })
	Aadd(aFields,{"NVN.NVN_CCONTR"                                  , Nil            })
	Aadd(aFields,{"NVN.NVN_CFATAD"                                  , Nil            })
	Aadd(aFields,{"RD0_SOCIO.RD0_NOME"                              , "NOME_SOCIO"   })
	Aadd(aFields,{"RD0_EMI.RD0_NOME"                                , "NOME_EMISSAO" })
	Aadd(aFields,{"RD0_EMI.RD0_SIGLA"                               , "SIGLA_EMISSAO"})
	Aadd(aFields,{"SA6.A6_COD"                                      , Nil            })
	Aadd(aFields,{"SA6.A6_AGENCIA"                                  , Nil            })
	Aadd(aFields,{"SA6.A6_NUMCON"                                   , Nil            })
	Aadd(aFields,{"SA6.A6_NOME"                                     , Nil            })
	Aadd(aFields,{"SA6.A6_MUN"                                      , Nil            })
	Aadd(aFields,{"SA6.A6_END"                                      , Nil            })
	Aadd(aFields,{"SA6.A6_EST"                                      , Nil            })
	Aadd(aFields,{"SU5.U5_CONTAT"                                   , Nil            })
	Aadd(aFields,{"SU5.U5_FONE"                                     , Nil            })
	Aadd(aFields,{"SU5.U5_FAX"                                      , Nil            })
	Aadd(aFields,{"SU5.U5_URL"                                      , Nil            })
	Aadd(aFields,{"SU5.U5_TRATA"                                    , Nil            })
	Aadd(aFields,{"SX5.X5_DESCRI"                                   , Nil            })
	Aadd(aFields,{"NXA.NXA_VLTOTD"                                  , Nil            })
	Aadd(aFields,{"SA1.A1_RECISS"                                   , Nil            })
	Aadd(aFields,{cValIss                                           , "USA_ISS"      })
	Aadd(aFields,{"NXA.NXA_VLFATH - NXA.NXA_VLDESC + NXA.NXA_VLACRE + NXA.NXA_VGROSH", "HONVLBRUT"})
	Aadd(aFields,{"(NXA.NXA_VLFATH - NXA.NXA_VLDESC + NXA.NXA_VLACRE + NXA.NXA_VGROSH) + NXA.NXA_VLTOTD", "BASTRIB"})
	Aadd(aFields,{"NXA.NXA_VLFATH + NXA.NXA_VLACRE - NXA.NXA_VLDESC -(NXA.NXA_IRRF + NXA.NXA_PIS + NXA.NXA_CSLL + NXA.NXA_COFINS + NXA.NXA_INSS ) + NXA.NXA_VGROSH + NXA.NXA_VLFATD", "SUBTOTVL"})
	Aadd(aFields,{"NXA.NXA_VLFATH + NXA.NXA_VLACRE - NXA.NXA_VLDESC -(NXA.NXA_IRRF + NXA.NXA_PIS + NXA.NXA_CSLL + NXA.NXA_COFINS + NXA.NXA_INSS ) + NXA.NXA_VLFATD - NXA.NXA_VUADIA + NXA.NXA_VSADIA + NXA.NXA_VGROSH", "TOTPAG"})
	
Return aFields
