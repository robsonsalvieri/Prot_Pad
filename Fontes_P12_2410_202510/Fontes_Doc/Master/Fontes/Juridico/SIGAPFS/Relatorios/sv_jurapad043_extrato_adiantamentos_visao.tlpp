#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"
#INCLUDE "JURAPAD043.CH"

NameSpace totvs.protheus.legal.sigapfs.jurapad043_extrato_adiantamentos_visao.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGAPFS',;
	tables='NWF,FK7,FK1,SA1,CTO',;
	name='Visão de Extrato de Adiantamento',;
	country='ALL',;
	initialRelease='12.1.2210')

//------------------------------------------------------------------------------------
class jurapad043_extrato_adiantamentos_visao from totvs.framework.treports.integratedprovider.IntegratedProvider
	public    method new()        as object
	public    method getData()    as object
	public    method getSchema()  as object
	
	data lPDUserAc as logical
	data lExistPergunte as logical
	
endclass

//------------------------------------------------------------------------------------
method new() class jurapad043_extrato_adiantamentos_visao
Local cPergunte := "JURPAD043B"

	_Super:new()

	//Define a área
	self:appendArea(STR0012) //"Pré-Faturamento Serviços"

	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0015) //"Visão de dados de Extrato de Adiantamento"

	//Define a descrição do Objeto de Negócio
	self:setDescription(STR0014) //"Demonstração dos dados do relatório de Extrato de Adiantamento"

	self:lExistPergunte := self:setPergunte(cPergunte)
	self:lPDUserAc := IIF(FindFunction("JPDUserAc"), JPDUserAc(), .T.) // Indica se o usuário possui acesso a dados sensíveis ou pessoais (LGPD)

Return self

//------------------------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class jurapad043_extrato_adiantamentos_visao
Local cQuery    := ""
Local aBind     := {}
Local jParams   := oFilter:getParameters()
Local cTemp     := GetNextAlias()
Local cDataIni  := ""
Local cDataFim  := ""
Local cSituac   := ""
Local cChave    := ""
Local cHist     := ""
Local nRelat    := 0
Local nSaldo    := 0
Local nFK5Valor := 0
Local nFK1Valor := 0
Local nCount    := 0
Local oJsDados  as object

	If !self:lPDUserAc
		self:setErrorStatus(400, STR0002, STR0001) // "Acesso restrito." / "Usuário com restrição de acesso a dados pessoais/sensíveis."
	Else
		If !self:lExistPergunte // @12.1.2410
			self:setErrorStatus(400, STR0005, STR0006) // "Dicionário de dados desatualizado!" / "Grupo de perguntas JURAPAD043 não encontrado."
		Else

			cDataIni := SubStr(StrTran(jParams['MV_PAR01'][1],"-",""),1,8)
			cDataFim := SubStr(StrTran(jParams['MV_PAR02'][1],"-",""),1,8)

			cSituac  := JurTRepVar(jParams['MV_PAR05'][1])
			nRelat   := JurTRepVar(jParams['MV_PAR08'][1])

			cQuery := " SELECT NWF_COD, NWF_DATAIN, NWF_CGRPCL, NWF_CCLIEN,"
			cQuery += " NWF_CLOJA, NWF_CCASO, NWF_TPADI, NWF_EXCLUS, NWF_CCLIAD, NWF_CLOJAD,"
			cQuery += " NWF_CMOE, NWF_VALOR, NWF_VALUTI, NWF_VALEST,"
			cQuery += " NWF_SALDO, NWF_COTACA, NWF_HIST, SA1.A1_NOME A1_NOME, SA1AD.A1_NOME A1_ADNOME,"
			cQuery += " NVE_TITULO, NWF_TITULO, CTO_SIMB, CTO_MOEDA,"
			cQuery += " A6_NOME"

			If nRelat == 1
				cQuery += " ,FK1_RECPAG, FK1_HISTOR, FK1_MOTBX"
				cQuery += " ,COALESCE(FK1_VALOR, 0) FK1_VALOR"
				cQuery += " ,COALESCE(FK5_VALOR, 0) FK5_VALOR"
			EndIf

			// FROM
			cQuery +=   " FROM " + RetSQLName("NWF") + " NWF"

			// SA1
			cQuery +=  " INNER JOIN " + RetSQLName("SA1") + " SA1"
			cQuery +=     " ON SA1.A1_FILIAL = ?"
			AAdd(aBind, {xFilial("SA1"), "S"})

			cQuery +=    " AND SA1.A1_COD = NWF.NWF_CCLIEN"
			cQuery +=    " AND SA1.A1_LOJA = NWF.NWF_CLOJA"
			cQuery +=    " AND SA1.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// SA1
			cQuery +=  " INNER JOIN " + RetSQLName("SA1") + " SA1AD"
			cQuery +=     " ON SA1AD.A1_FILIAL = ?"
			AAdd(aBind, {xFilial("SA1"), "S"})

			cQuery +=    " AND SA1AD.A1_COD = NWF.NWF_CCLIAD"
			cQuery +=    " AND SA1AD.A1_LOJA = NWF.NWF_CLOJAD"
			cQuery +=    " AND SA1AD.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			//NVE
			cQuery +=  " INNER JOIN " + RetSQLName("NVE") + " NVE"
			cQuery +=     " ON NVE.NVE_FILIAL = ?"
			AAdd(aBind, {xFilial("NVE"), "S"})

			cQuery +=    " AND NVE.NVE_NUMCAS = NWF.NWF_CCASO"
			cQuery +=    " AND NVE.NVE_CGRPCL = NWF.NWF_CGRPCL"
			cQuery +=    " AND NVE.NVE_CCLIEN = NWF.NWF_CCLIEN"
			cQuery +=    " AND NVE.NVE_LCLIEN = NWF.NWF_CLOJA"
			cQuery +=    " AND NVE.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// NS7
			cQuery +=  " INNER JOIN " + RetSqlName("NS7") + " NS7 "
			cQuery +=     " ON NS7.NS7_FILIAL = ?"
			AAdd(aBind, {xFilial("NS7"), "S"})

			cQuery +=    " AND NS7.NS7_COD = NWF.NWF_CESCR"
			cQuery +=    " AND NS7.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// SE1
			cQuery +=  " INNER JOIN " + RetSQLName("SE1") + " SE1"
			cQuery +=     " ON SE1.E1_FILIAL = NS7.NS7_CFILIA
			cQuery +=    " AND SE1.E1_NUM = NWF.NWF_TITULO"
			cQuery +=    " AND SE1.E1_CLIENTE = NWF.NWF_CCLIAD "
			cQuery +=    " AND SE1.E1_LOJA = NWF.NWF_CLOJAD "
			cQuery +=    " AND SE1.E1_TIPO = ?"
			AAdd(aBind, {"RA", "S"})

			cQuery +=    " AND SE1.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// CTO
			cQuery +=  " INNER JOIN " + RetSQLName("CTO") + " CTO"
			cQuery +=     " ON CTO.CTO_FILIAL = ?"
			AAdd(aBind, {xFilial("CTO"), "S"})

			cQuery +=    " AND CTO.CTO_MOEDA = NWF.NWF_CMOE"
			cQuery +=    " AND CTO.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			cQuery +=  " INNER JOIN " + RetSQLName("SA6") + " SA6"
			cQuery +=     " ON SA6.A6_FILIAL = ?"
			AAdd(aBind, {xFilial("SA6"), "S"})

			cQuery +=    " AND SA6.A6_COD = NWF.NWF_BANCO"
			cQuery +=    " AND SA6.A6_AGENCIA = NWF.NWF_AGENCI"
			cQuery +=    " AND SA6.A6_NUMCON = NWF.NWF_CONTA"
			cQuery +=    " AND SA6.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})


			// FK7
			cQuery +=  " INNER JOIN " + RetSQLName("FK7") + " FK7"
			cQuery +=     " ON FK7.FK7_FILTIT = SE1.E1_FILIAL"
			cQuery +=    " AND FK7.FK7_PREFIX = SE1.E1_PREFIXO"
			cQuery +=    " AND FK7.FK7_NUM = SE1.E1_NUM"
			cQuery +=    " AND FK7.FK7_PARCEL = SE1.E1_PARCELA"
			cQuery +=    " AND FK7.FK7_TIPO = SE1.E1_TIPO"
			cQuery +=    " AND FK7.FK7_CLIFOR = SE1.E1_CLIENTE"
			cQuery +=    " AND FK7.FK7_LOJA = SE1.E1_LOJA"
			cQuery +=    " AND FK7.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// FK1
			cQuery +=   " LEFT JOIN " + RetSQLName("FK1") + " FK1"
			cQuery +=     " ON FK1.FK1_FILIAL = FK7.FK7_FILIAL"
			cQuery +=    " AND FK1.FK1_IDDOC = FK7.FK7_IDDOC"
			cQuery +=    " AND FK1.D_E_L_E_T_ = ?"

			AAdd(aBind, {" ", "S"})
			// FK5
			cQuery +=   " LEFT JOIN " + RetSQLName("FK5") + " FK5"
			cQuery +=     " ON FK5.FK5_FILIAL = FK7.FK7_FILIAL"
			cQuery +=    " AND FK5.FK5_IDDOC = FK7.FK7_IDDOC"
			cQuery +=    " AND FK5.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			// WHERE
			cQuery +=  " WHERE NWF.NWF_FILIAL = ?"
			AAdd(aBind, {xFilial("NWF"), "S"})

			cQuery +=    " AND NWF.NWF_DTMOVI >= ?"
			AAdd(aBind, {cDataIni, "S"}) // MV_PAR01

			cQuery +=    " AND NWF.NWF_DTMOVI <= ?"
			AAdd(aBind, {cDataFim, "S"}) // MV_PAR02

			If !Empty(jParams['MV_PAR03'][1])
				cQuery +=    " AND NWF.NWF_CCLIAD = ?"
				AAdd(aBind, {jParams['MV_PAR03'][1], "S"})

				If !Empty(jParams['MV_PAR04'][1])
					cQuery +=    " AND NWF.NWF_CLOJAD = ?"
					AAdd(aBind, {jParams['MV_PAR04'][1], "S"})
				EndIf
			EndIf

			If cSituac == 2
				cQuery +=    " AND NWF.NWF_SITUAC = ?"
				AAdd(aBind, {"1", "S"})
			ElseIf cSituac == 3
				cQuery +=    " AND NWF.NWF_SITUAC = ?"
				AAdd(aBind, {"2", "S"})
			EndIf

			If !Empty(jParams['MV_PAR07'][1])
				cQuery +=   " AND NWF.NWF_CMOE = ?"
				Aadd(aBind, {jParams['MV_PAR07'][1], "S"})
			EndIf

			cQuery +=    " AND NWF.NWF_TITGER = ?"
			AAdd(aBind, {"1", "S"})

			cQuery +=    " AND NWF.D_E_L_E_T_ = ?"
			AAdd(aBind, {" ", "S"})

			If nRelat == 2
				cQuery +=  " GROUP BY NWF_COD, NWF_DATAIN, NWF_CGRPCL"
				cQuery +=  " , NWF_CCLIAD, NWF_CLOJAD, NWF_CCLIEN, NWF_CLOJA, NWF_CCASO"
				cQuery +=  " , NWF_TPADI, NWF_EXCLUS, NWF_CMOE"
				cQuery +=  " , NWF_VALOR, NWF_VALUTI, NWF_VALEST"
				cQuery +=  " , NWF_SALDO, NWF_COTACA, NWF_HIST"
				cQuery +=  " , SA1.A1_NOME, SA1AD.A1_NOME, NVE_TITULO, NWF_TITULO"
				cQuery +=  " , CTO_SIMB, CTO_MOEDA, A6_NOME"
			EndIf

			cQuery := JurTRepBin(cQuery, aBind)
			cQuery := ChangeQuery(cQuery)

			MPSysOpenQuery(cQuery, cTemp)

			While !(cTemp)->(Eof())

				If nRelat == 1 .OR. nRelat == 0
					If cChave <> (cTemp)->NWF_COD + (cTemp)->NWF_CCLIEN + (cTemp)->NWF_CLOJA + (cTemp)->NWF_CCASO
						nFK5Valor := Iif(nRelat == 1, (cTemp)->FK5_VALOR, 0)
						nFK1Valor := 0
						nSaldo   := 0
						nCount++
						cHist := (cTemp)->NWF_HIST
					Else
						nFK5Valor := 0
						nFK1Valor := Iif((cTemp)->FK1_MOTBX == 'CMP',;
							Iif((cTemp)->FK1_RECPAG == 'R', ((cTemp)->FK1_VALOR * -1), (cTemp)->FK1_VALOR),;
							Iif((cTemp)->FK1_RECPAG == 'P', ((cTemp)->FK1_VALOR * -1), (cTemp)->FK1_VALOR))
						nCount := 0
						cHist := Iif(nRelat == 1, (cTemp)->FK1_HISTOR, (cTemp)->NWF_HIST)
					EndIf
				Else
					nFK5Valor := Iif(nRelat == 1, (cTemp)->FK5_VALOR, 0)
					cHist := (cTemp)->NWF_HIST
				EndIf

				oJsDados := JsonObject():new()

				oJsDados["NWF_CCLIAD"] := (cTemp)->NWF_CCLIAD
				oJsDados["NWF_CLOJAD"] := (cTemp)->NWF_CLOJAD
				oJsDados["A1_ADNOME"]  := (cTemp)->A1_ADNOME

				oJsDados["NWF_DATAIN"] := totvs.framework.treports.date.dateToTimeStamp(STOD((cTemp)->NWF_DATAIN))
				oJsDados["NWF_TITULO"] := (cTemp)->NWF_TITULO
				oJsDados["NWF_TPADI"]  := Iif((cTemp)->NWF_TPADI == "1", STR0007, Iif((cTemp)->NWF_TPADI == '2', STR0008, STR0009)) // "Despesas", "Honorários", "Ambos"
				oJsDados["NWF_HIST"]   := cHist
				oJsDados["NWF_EXCLUS"] := Iif((cTemp)->NWF_EXCLUS == "1", STR0010, STR0011)
				oJsDados["A6_NOME"]    := (cTemp)->A6_NOME

				oJsDados["NWF_CGRPCL"] := (cTemp)->NWF_CGRPCL
				oJsDados["NWF_CCLIEN"] := (cTemp)->NWF_CCLIEN
				oJsDados["NWF_CLOJA"]  := (cTemp)->NWF_CLOJA
				oJsDados["A1_NOME"]    := (cTemp)->A1_NOME
				oJsDados["NWF_CCASO"]  := (cTemp)->NWF_CCASO
				oJsDados["CTO_SIMB"]   := (cTemp)->CTO_SIMB

				If nRelat == 1 // Extrato

					nSaldo += nFK5Valor +  nFK1Valor

					oJsDados["VALOR"]    := nFK5Valor +  nFK1Valor
					oJsDados["COTAC"]    := (cTemp)->NWF_COTACA
					oJsDados["VLCONV"]   := (nFK5Valor + nFK1Valor) * (cTemp)->NWF_COTACA
					oJsDados["SALDO"]    := nSaldo
				Else // Saldo
					oJsDados["VALOR"]    := (cTemp)->NWF_VALOR
					oJsDados["COTAC"]    := (cTemp)->NWF_COTACA
					oJsDados["VLCONV"]   := (cTemp)->NWF_VALOR * (cTemp)->NWF_COTACA
					oJsDados["SALDO"]    := (cTemp)->NWF_SALDO
					oJsDados["VALUTI"]   := (cTemp)->NWF_VALUTI
					oJsDados["VALEST"]   := (cTemp)->NWF_VALEST
				EndIf

				If  nCount == 1 .OR. nRelat == 2 .OR. (nCount == 0 .AND. (cTemp)->FK1_RECPAG $ "R|P")
					self:oData:appendData(oJsDados)
				EndIf

				cChave := (cTemp)->NWF_COD + (cTemp)->NWF_CCLIEN + (cTemp)->NWF_CLOJA + (cTemp)->NWF_CCASO

				If nCount == 0 .And. (nRelat == 1 .OR. nRelat == 0)
					(cTemp)->(DBSkip())
				ElseIf nRelat == 2
					(cTemp)->(DBSkip())
				EndIf
			EndDo

		EndIf

	EndIf

Return self:oData

//------------------------------------------------------------------------------------
method getSchema() as object Class jurapad043_extrato_adiantamentos_visao

	self:oSchema:addProperty("NWF_CCLIAD", FWSX3Util():GetDescription("NWF_CCLIAD"), "string" , FWSX3Util():GetDescription("NWF_CCLIAD"), "NWF_CCLIAD")
	self:oSchema:addProperty("NWF_CLOJAD", FWSX3Util():GetDescription("NWF_CLOJAD"), "string" , FWSX3Util():GetDescription("NWF_CLOJAD"), "NWF_CLOJAD")
	self:oSchema:addProperty("A1_ADNOME" , FWSX3Util():GetDescription("A1_NOME")   , "string" , FWSX3Util():GetDescription("A1_NOME")   , "A1_ADNOME" )
	self:oSchema:addProperty("NWF_DATAIN", FWSX3Util():GetDescription("NWF_DATAIN"), "date"   , FWSX3Util():GetDescription("NWF_DATAIN"), "NWF_DATAIN")
	self:oSchema:addProperty("NWF_TITULO", FWSX3Util():GetDescription("NWF_TITULO"), "string" , FWSX3Util():GetDescription("NWF_TITULO"), "NWF_TITULO")
	self:oSchema:addProperty("NWF_TPADI" , FWSX3Util():GetDescription("NWF_TPADI") , "string" , FWSX3Util():GetDescription("NWF_TPADI") , "NWF_TPADI" )
	self:oSchema:addProperty("NWF_HIST"  , FWSX3Util():GetDescription("NWF_HIST")  , "string" , FWSX3Util():GetDescription("NWF_HIST")  , "NWF_HIST"  )
	self:oSchema:addProperty("NWF_EXCLUS", FWSX3Util():GetDescription("NWF_EXCLUS"), "string" , FWSX3Util():GetDescription("NWF_EXCLUS"), "NWF_EXCLUS")
	self:oSchema:addProperty("A6_NOME"   , FWSX3Util():GetDescription("A6_NOME")   , "string" , FWSX3Util():GetDescription("A6_NOME")   , "A6_NOME"   )
	self:oSchema:addProperty("NWF_CGRPCL", FWSX3Util():GetDescription("NWF_CGRPCL"), "string" , FWSX3Util():GetDescription("NWF_CGRPCL"), "NWF_CGRPCL")
	self:oSchema:addProperty("NWF_CCLIEN", FWSX3Util():GetDescription("NWF_CCLIEN"), "string" , FWSX3Util():GetDescription("NWF_CCLIEN"), "NWF_CCLIEN")
	self:oSchema:addProperty("NWF_CLOJA" , FWSX3Util():GetDescription("NWF_CLOJA") , "string" , FWSX3Util():GetDescription("NWF_CLOJA") , "NWF_CLOJA" )
	self:oSchema:addProperty("A1_NOME"   , FWSX3Util():GetDescription("A1_NOME")   , "string" , FWSX3Util():GetDescription("A1_NOME")   , "A1_NOME"   )
	self:oSchema:addProperty("NWF_CCASO" , FWSX3Util():GetDescription("NWF_CCASO") , "string" , FWSX3Util():GetDescription("NWF_CCASO") , "NWF_CCASO" )
	self:oSchema:addProperty("CTO_SIMB"  , FWSX3Util():GetDescription("CTO_SIMB")  , "string" , FWSX3Util():GetDescription("CTO_SIMB")  , "CTO_SIMB"  )
	self:oSchema:addProperty("VALOR"     , FWSX3Util():GetDescription("NWF_VALOR")   , "number" , FWSX3Util():GetDescription("NWF_VALOR")   , "VALOR"   )
	self:oSchema:addProperty("SALDO"     , FWSX3Util():GetDescription("NWF_SALDO")   , "number" , FWSX3Util():GetDescription("NWF_SALDO")   , "SALDO"   )
	self:oSchema:addProperty("COTAC"     , FWSX3Util():GetDescription("NWF_COTAC")   , "number" , FWSX3Util():GetDescription("NWF_COTAC")   , "COTAC"   )
	self:oSchema:addProperty("VLCONV"    , STR0016, "number", STR0016, "VLCONV"   )
	self:oSchema:addProperty("VALUTI"    , FWSX3Util():GetDescription("NWF_VALUTI")  , "number" , FWSX3Util():GetDescription("NWF_VALUTI")   , "VALUTI"   )
	self:oSchema:addProperty("VALEST"    , FWSX3Util():GetDescription("NWF_VALEST")  , "number" , FWSX3Util():GetDescription("NWF_VALEST")   , "VALEST"   )


Return self:oSchema
