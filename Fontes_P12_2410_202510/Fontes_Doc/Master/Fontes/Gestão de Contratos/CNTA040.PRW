#INCLUDE "CNTA040.ch"

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Menu Funcional da rotina

@return aRotina, array, array com as opes do menu
@author jose.souza2
@since 23/05/2024
/*/
//-------------------------------------------------------------------------------------
Static Function MenuDef()
	Local aRotina := {}

	aRotina := { { oemtoansi(STR0017),"AxPesqui", 0 , 1,,.F.},; // "Pesquisar"
				 { oemtoansi(STR0009),"AxCadVis", 0 , 2},; // "Visualizar"
				 { oemtoansi(STR0010),"AxCadInc", 0 , 3},; //"Incluir"
				 { oemtoansi(STR0011),"AxCadAlt", 0 , 4},; //"Alterar"
				 { oemtoansi(STR0012),"AxCadDel", 0 , 5}}  //"Excluir"
Return aRotina

/*


Ŀ
Funao     CNTA040   Autor  Marcelo Custodio       Data 22.11.2005
Ĵ
Descriao  Manutencao do Cadastro de Tipo de Cauo                   
Ĵ
Sintaxe    CNTA040()                                                  
Ĵ
 Uso                                                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                    
ٱ

*/
Function CNTA040()            

AxCadastro("CN3",STR0001,"CN040DEL()","CN040Vld()")

Return

/*


Ŀ
Funao     CN040Vld  Autor  Marcelo Custodio       Data 22.11.2005
Ĵ
Descriao  Valida os campos financeiros                               
Ĵ
Sintaxe    CN040Vld()                                                 
Ĵ
 Uso                                                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                    
ٱ

*/
Function CN040Vld()
Local lRet   := .T.  
Local lIFL   := .T.
Local lVldCauc:= GetNewPar("MV_CNVCAUC","S") == "S"

dbSelectArea("CN8") 
dbSetOrder(3)//Tipo de Caucao

//Ŀ
//Valida a existencia do registro no cadastro de caucoes                   
//
If MsSeek(xFilial('CN8')+M->CN3_CODIGO, .F.)
	lRet := .F.
	Help( " ", 1, "CNTA040_03" )
Endif   
  
If lVldCauc       
	//Ŀ
	//PE: CN040IFL - Indica se a Indice/Freq/Ligao so obrigatorios           
	//|    Se no existir o PE, ser obrigatrio.								 
	//
	If ExistBlock("CN040IFL")
		lIFL := ExecBlock("CN040IFL",.F.,.F.)
		If ValType(lIFL) <> "L"
			lIFL := .T.
		EndIf
	EndIf

	If M->CN3_TIPFIN == "1"
		Help( " ", 1, "CNTA040_08",,STR0015,1,1)
		lRet := .F.
	ElseIf (AllTrim(M->CN3_TIPFIN) == "3" .And. AllTrim(M->CN3_LIGFIN) != "1")
		Help( " ", 1, "CNTA040_09",,STR0016,1,1)
		lRet := .F.
	EndIf   

	If lRet                                                  	

		if M->CN3_LIGFIN == "1"
			if Empty(M->CN3_ABATI) .Or. Empty(M->CN3_INDREJ) .Or. Empty(M->CN3_FRQREJ) .Or. Empty(M->CN3_TIPFIN)
				Help( " ", 1, "CNTA040_04" )
				lRet := .F.
			EndIf                                                      
		Else                             		
			if !Empty(M->CN3_ABATI) .Or. !Empty(M->CN3_INDREJ) .Or. !Empty(M->CN3_FRQREJ) .Or. !Empty(M->CN3_TIPFIN)
				Help( " ", 1, "CNTA040_05" )
				lRet := .F.
			EndIf
		EndIf
	EndIf
EndIf

Return lRet

/*


Ŀ
Funao     CN040DEL  Autor  Marcelo Custodio       Data 22.11.2005
Ĵ
Descriao  Validacao da exclusao do tipo de caucao                    
Ĵ
Sintaxe    CN040DEL()                                                 
Ĵ
 Uso                                                                  
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                    
ٱ

*/
Function CN040DEL()
Local lRet := .T.

dbSelectArea("CN8") 
dbSetOrder(3)//Tipo de Caucao

//Ŀ
//Valida a existencia do registro no cadastro de caucoes                   
//
If MsSeek(xFilial('CN8')+CN3->CN3_CODIGO, .F.)
	lRet := .F.
	Help( " ", 1, "CNTA040_01" )
Endif                    

return lRet

/*


Ŀ
Funao     CN040VldFin  Autor  Marcelo Custodio       Data 29.11.2005
Ĵ
Descriao  Valida tipo de caucao                                         
Ĵ
Sintaxe    CN040DEL()                                                    
Ĵ
 Uso                                                                     
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                        
Ĵ
                                                                       
ٱ

*/
Function CN040VldFin()
Local lRet    := .T.
Local cCampo  := AllTrim(Substr(ReadVar(),4,10))
Local cValor  := &(ReadVar())          
Local lVldCauc:= GetNewPar("MV_CNVCAUC","S") == "S"

If lVldCauc
	If cCampo == "CN3_LIGFIN" .And. cValor == "2"
		M->CN3_ABATI  := CriaVar("CN3_ABATI")
	  	M->CN3_INDREJ := CriaVar("CN3_INDREJ")
	  	M->CN3_TIPLIG := CriaVar("CN3_TIPFIN")
	  	M->CN3_FRQREJ := 0
	Else  
		If cCampo == "CN3_INDREJ" .And. M->CN3_LIGFIN <> "1" .And. !Empty(cValor)    
			Help( " ", 1, "CNTA040_02" )
			M->CN3_INDREJ := CriaVar("CN3_INDREJ")
			lRet := .F.  
		EndIf	
	EndIf           
	
	If lRet .And. cCampo == "CN3_FRQREJ"
		If M->CN3_FRQREJ < 0
			Help( " ", 1, "CNTA040_06" ) //-- "O conteudo do campo 'Frequencia' no pode preenchido com valor negativo !"
			lRet := .F.
		EndIf
	EndIf
EndIf

Return lRet       

/*


Ŀ
Funao     CN040Inclui  Autor  Sergio Silveira        Data 10/05/2006
Ĵ
Descriao  Rotina de inclusao do tipo de caucao, para chamada via SXB    
Ĵ
Sintaxe    CN040Inclui()                                                 
Ĵ
 Uso                                                                     
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                        
Ĵ
                                                                       
ٱ


*/

Function CN040Inclui() 


Local cOkFunc := "CN040Vld()"

Private aRotina := { { oemtoansi("Incluir"),"AxCadInc", 0 , 3} } //"Incluir"
 
//Ŀ
// Chama a funcao de inclusao                                              
//
xRet := AxInclui("CN3", CN3->( Recno() ),1,NIL,NIL,NIL,cOkFunc)

Return( xRet ) 

/*


Ŀ
Funao     CN040VlInd   Autor  Marcelo Custodio       Data 29.11.2005
Ĵ
Descriao  Rotina de validacao do indice, verifica se o indice atualiza  
           caucao                                                        
Ĵ
Sintaxe    CN040VlInd()                                                  
Ĵ
 Uso                                                                     
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.                
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                        
Ĵ
                                                                       
ٱ

*/
Function CN040VlInd()
Local lRet := .T.           
                 
If Posicione("CN6",1,xFilial("CN6")+M->CN3_INDREJ,"CN6_ATUCAU") != "1"
	Help( " ", 1, "CNTA040_07" )//"O ndice selecionado no atualiza cauo"
	lRet := .F.
EndIf    


Return lRet 


