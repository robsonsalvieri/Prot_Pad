#INCLUDE "tlpp-core.th"
#include "totvs.ch"
#INCLUDE 'agd.rest.utils.ch'

namespace agd.restUtils

/*/{Protheus.doc} agdRestUtils
Serviço para tratamento de requests rest
@type class
@version 12
@author jean.schulze
@since 14/01/2025
/*/
Class agdRestUtils
    public Method New()
    public Method getPageAndPageSize()
    public Method getRequestBody() 
	public Method getQueryParams() as json
    public Method setAPIResponse()
	public Method setError()
	public Method getIDWithPathParams() as Character 
	public Method isFilialInformado() as logical

	private Method convertToJsonFormat()
EndClass

/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@return variant, instância
/*/
Method New() Class agdRestUtils
Return Self

/*/{Protheus.doc} getPageAndPageSize
Obtem a Paginação
@type method
@version 12
@since 14/01/2025
@param nPage, numeric, numero da pagina
@param nPageSize, numeric, tamanho da pagina
@return variant, nil
/*/
Method getPageAndPageSize(nPage, nPageSize) Class agdRestUtils
	If (oRest:getQueryRequest():GetJsonText('pageSize') != 'null')
		nPageSize := Val(oRest:getQueryRequest():GetJsonText('pageSize'))
	Else
		nPageSize := 10
	EndIf

	If (oRest:getQueryRequest():GetJsonText('page') != 'null')
		nPage := Val(oRest:getQueryRequest():GetJsonText('page'))
	Else
		nPage := 1
	Endif
Return Nil


/*/{Protheus.doc} getRequestBody
Obtem o corpo da request
@type method
@version 12
@since 14/01/2025
@return variant, body
/*/
Method getRequestBody()  Class agdRestUtils
	Local cBody			:= oRest:getBodyRequest()
	Local cErrorParser	as Character
	Local jBody			as Json
	Local lError 		as Logical

	If Empty(cBody)
		lError := .T.
	Else
		jBody := JsonObject():New()
		cErrorParser := jBody:fromJson(DecodeUtf8(cBody))
		If !Empty(cErrorParser)
			FreeObj(jBody)
			lError := .T.
		EndIf
	EndIf

	If lError
		oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
		SetRestFault(403, FWHttpEncode(STR0001) )  //"Corpo de mensagem inválido"
	Endif
Return jBody

/*/{Protheus.doc} getQueryParams
Retorna o Query Params
@type method
@version 12
@author jean.schulze
@since 07/02/2025
@param oJsonQueryParams, object, param_description
@return object, return_description
/*/
Method getQueryParams() as json Class agdRestUtils
	Local nParam 	   as Numeric
	LOcal aParamsQuery as Array 
	Local jQueryParams := oRest:getQueryRequest() 
	Local oJsonReturn  := JsonObject():new()

	if jQueryParams <> nil

		aParamsQuery := jQueryParams:GetNames()
 
		for nParam := 1 to len(aParamsQuery)
			if aParamsQuery[nParam] != 'pageSize' .and. aParamsQuery[nParam] != 'page'
				oJsonReturn[aParamsQuery[nParam]] := jQueryParams:GetJsonText(aParamsQuery[nParam])
			endif
		next

	endif
	
Return oJsonReturn



/*/{Protheus.doc} setAPIResponse
Informa o retorno da API conforme classe herdada de agd-utils.service
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@param oRest, object, objeto rest
@param oAgdUtilsService, object, objeto agd-utils.service
@return variant, nil
/*/
Method setAPIResponse(oRest, oAgdUtilsService) Class agdRestUtils
    Default oRest := Nil
    Default oAgdUtilsService := nil

    If oRest <> Nil
        oRest:setKeyHeaderResponse("Content-Type", "application/json")

        If oAgdUtilsService:isSuccess()
            If ValType(oAgdUtilsService:getResponse()) == 'J'
				cJsonResponse := oAgdUtilsService:getResponse():toJson()
            ElseIf ValType(oAgdUtilsService:getResponse()) == 'C'
                cJsonResponse := Self:convertToJsonFormat(oAgdUtilsService:getResponse())
			Else
				cJsonResponse := oAgdUtilsService:getResponse()
            EndIf

            oRest:SetResponse(cJsonResponse)
        Else
			Self:setError(oRest, oAgdUtilsService:getMessageError(), oAgdUtilsService:getCodeError())
        EndIf
    EndIf
Return Nil

/*/{Protheus.doc} setError
description
@type method
@version 12
@author jean.schulze
@since 29/05/2025
@param oRest, object, param_description
@param pcMessage, variant, param_description
@param pcCode, variant, param_description
@return variant, return_description
/*/
Method setError(oRest, pcMessage, pcCode) Class agdRestUtils
    Local oJsonReponse as object

	Default oRest := Nil
    Default pcCode := 404
	Default pcMessage := ""

    oJsonReponse := JsonObject():New()
	oJsonReponse['code']    := Iif(pcCode >= 400, pcCode, 400)
	oJsonReponse['message'] := FWHttpEncode(pcMessage)
				
	oRest:SetResponse(FwJsonSerialize(oJsonReponse))
	oRest:setStatusCode(oJsonReponse['code'])
return nil

/*/{Protheus.doc} isFilialInformado
Verifica se a filial foi informada
@type method
@version 12
@author jean.schulze
@since 29/05/2025
@return logical, filial informada
/*/
Method isFilialInformado() as Logical Class agdRestUtils
	oJsonHeader := oRest:getHeaderRequest()

	If ValType(oJsonHeader) == "J" .And. (oJsonHeader:hasProperty('tenantId') .Or. oJsonHeader:hasProperty('tenantid'))
		return .t.
	else
		Self:setError(oRest, STR0002)	
	endif

return .F.

/*/{Protheus.doc} getIDWithPathParams
Retorna o Identificador conforme os parametros da Request
@type method
@version 12
@author jean.schulze
@since 29/05/2025
@param aParams, array, lista de campos
@return character, identificador 
/*/
Method getIDWithPathParams(aParams as Array) as Character Class agdRestUtils
	Local cIdentificacao as Character
	Local nX as numeric

	oJsonPath := oRest:getPathParamsRequest()

	if Self:isFilialInformado()

		For nX:= 1 to Len(aParams)
			If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty(aParams[nx][1])
				cIdentificacao += Padr(oJsonPath[aParams[nx][1]], TamSX3(aParams[nx][2])[1])
			EndIf
		Next	

		if Empty(cIdentificacao)
			Self:setError(oRest, STR0003)
		endif

	endif

Return cIdentificacao


/*/{Protheus.doc} convertToJsonFormat
Converte a mensagem de resposta para o formato JSON
@type method
@version 12
@author carlos.augusto
@since 01/07/2025
@param cResponseMsg, character, mensagem de resposta a ser convertida. 
@return character, mensagem formatada	
/*/
Method convertToJsonFormat(cResponseMsg) Class agdRestUtils
	Local cMessage as Character

	cMessage := '{ "message":"' + cResponseMsg + '"}'

Return cMessage
