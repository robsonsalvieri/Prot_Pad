#include "totvs.ch"
#include 'tlpp-core.th'
#include 'agd.maquina.estado.service.ch'

namespace agd.maquinaEstadoService
using namespace agd.utilsService
/*/{Protheus.doc} agdMaquinaEstadoService
Classe Utilitaria para Padronizar as Máquinas de Estado do Agro Distribuidor
@type class
@version 12
@author jean.schulze
@since 26/12/2024
/*/
class agdMaquinaEstadoService FROM agdUtilsService
 public Data cId             As Character
 public Data cTable          As Character
 public Data cField          As Character
 public Data aStatus         As Array /*POS1 'Nome', POS2 'Fluxo Disponivel'*/

 public method new() 
 
 public method getStatusAtual()  as Character
 public method getStatusMessage() as character
 public method getStatus() as Array
 public method getBrowserLegenda() as object


 protected method validaProximoStatus() as Logical
 protected method informarHistorico() 
 protected method solicitarHistoricoEmTela() as Logical
 protected method atualizarRepositorio()
 protected method invalidarStatus()
endclass
   
/*/{Protheus.doc} new
Constructor da Classe
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@return variant, Retorna a propria instancia
/*/
method new(pcId as Character, pcTable as Character, pcField as Character, aStatus as Array) class agdMaquinaEstadoService
	Self:cId     := pcId
	Self:cTable  := pcTable
	Self:cField  := pcField
	Self:aStatus := aStatus
return Self 

/*/{Protheus.doc} getStatus
Retorna a lista de status + status possíveis a partir de cada status
@type method
@version 12
@author jean.schulze
@since 16/01/2025
@return array, lista de status
/*/
method getStatus() as Array class agdMaquinaEstadoService
return Self:aStatus

/*/{Protheus.doc} getStatusMessage
Retorna a Label de Determinado Estado
@type method
@version 12
@author jean.schulze
@since 19/03/2025
@param cCodStatus, character, codigo do estado desejado
@return character, label
/*/
method getStatusMessage(cCodStatus as character) as character class agdMaquinaEstadoService
	Local aStatus := Self:getStatus()
	Local nPosStatus := aScan(aStatus, {|x| x[1] == cCodStatus })
	Local cRetorno   := ""
	if nPosStatus > 0 
		cRetorno := aStatus[nPosStatus][2]
	endif
return cRetorno


/*/{Protheus.doc} getStatusAtual
Retorna o Status Atual do Registro
@type method
@version 12
@author jean.schulze
@since 16/01/2025
@return variant, status atual
/*/
method getStatusAtual() as Character class agdMaquinaEstadoService
return POSICIONE( Self:cTable, 1, Self:cId, Self:cField )

/*/{Protheus.doc} getBrowserLegenda
Retorna um Browser com Legenda para a tabela da Máquina de Estado
@type method
@version 12
@author jean.schulze
@since 19/03/2025
@param aColorList, array, cores especificas
@return object, retorno do objeto de legenda
/*/
method getBrowserLegenda(aColorList as Array) as object class agdMaquinaEstadoService
	Local oMBrowse := FWMBrowse():New()
	Local aStatus := Self:getStatus()
	Local nX as numeric

	Default aColorList := {"YELLOW","BLUE","WHITE","GRAY","ORANGE","BROWN","PINK","BLACK","VIOLET","HGREEN","LBLUE", "GREEN", "RED"}

	oMBrowse:SetAlias( Self:cTable )

	For nX:= 1 to Len(aStatus)
		oMBrowse:AddLegend(Self:cField + "=='" + aStatus[nX][1]+"'" ,aColorList[nX] , aStatus[nX][2] ) 
	Next
	
return oMBrowse

/*/{Protheus.doc} validaProximoStatus
Verifica se o status de destino está disponível, conforme o status atual
@type method
@version 12
@author jean.schulze
@since 16/01/2025
@param nProximoStatus, numeric, status de destino
@return logical, se pode executar a ação
/*/
method validaProximoStatus(nProximoStatus as Character, aStatus as Array, cStatusAtual as Variant) as Logical class agdMaquinaEstadoService
	Local nPosStatus as numeric

	Default aStatus := Self:getStatus()
	Default cStatusAtual := Self:getStatusAtual()

	nPosStatus := aScan(aStatus, {|x| x[1] == cStatusAtual })

	if nPosStatus == 0 .OR. !aScan(aStatus[nPosStatus][3], {|x| x == nProximoStatus })
		Self:setError(STR0001) //"Status Inválido"
		return .F.
	endif

return .T.

/*/{Protheus.doc} informarHistorico
Informa o historico para o registro, conforme com o estado selecionado
@type method
@version 12
@author jean.schulze
@since 19/03/2025
@param pcStatus, character, codigo status
@param pcObservacao, character,observacao
@return variant, nil
/*/
method informarHistorico(pcStatus as Character, pcObservacao as Character) class agdMaquinaEstadoService
	Default pcObservacao := Self:getStatusMessage(pcStatus)
	if Self:isSuccess() 
		AGDGRAVAHIS("",Self:cTable,Self:cId,Self:getStatusMessage(pcStatus),.F., pcObservacao) 
	endif	
return nil

/*/{Protheus.doc} solicitarHistoricoEmTela
Solicita um histórico ao usuário via TELA Protheus
@type method
@version 12
@author jean.schulze
@since 19/03/2025
@param pcStatus, character, codigo status
@return logical, Histórico Salvo
/*/
method solicitarHistoricoEmTela(pcStatus as Character) as Logical class agdMaquinaEstadoService
return AGDGRAVAHIS("",Self:cTable,Self:cId,Self:getStatusMessage(pcStatus),.T.) 

/*/{Protheus.doc} atualizarRepositorio
Método que atualiza o repositorio da maquina de estado
@type method
@version 12
@author jean.schulze
@since 19/03/2025
@param oRepository, object, ponteiro do repositorio a ser ajustado
@param pcStatus, character, status
@param paExtraFields, array, lsita de campos extras a serem atualizados
@return variant, nil
/*/
method atualizarRepositorio(oRepository as object, pcStatus as Character, paExtraFields as Array) class agdMaquinaEstadoService
	Local nX as numeric
	Local aFieldsUpdate := {{Self:cField, pcStatus}}

	Default paExtraFields := {}

	For nX:= 1 to Len(paExtraFields)
		aAdd(aFieldsUpdate, paExtraFields[nX] )
	Next
		
	oRepository:updateRepository(aFieldsUpdate)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()

	FreeObj(oRepository)

return nil

/*/{Protheus.doc} invalidarStatus
Inválida Máquina de Estado
@type method
@version 12
@author jean.schulze
@since 20/03/2025
@param cError, character, erro
@return variant, void
/*/
method invalidarStatus(cError as character) class agdMaquinaEstadoService
	Self:setError(cError)
return nil
