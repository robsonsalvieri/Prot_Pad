#include "totvs.ch"
#include "tlpp-core.th"

namespace agd.comboService
using namespace agd.comboRepository
using namespace agd.utilsService

/*/{Protheus.doc} agdComboService
Serviço para montagem de combos dinâmicos
@type class
@version 1.0
@author rodrigo.nsoledade
@since 27/05/2025
/*/
class agdComboService FROM agdUtilsService
    public method New()
    public method listarCombo()
    private method customCombo() 
endclass

method New() class agdComboService
    _Super:New()
return Self

/*/{Protheus.doc} listarCombo
Executa consulta genérica de combo com base na tabela informada
@type method
@version 1.0
@author rodrigo.nsoledade
@since 27/05/2025
@param cTabela, character, tabela solicitada (ex: SA1, SB1)
@param nPage, numeric, número da página
@param nPageSize, numeric, tamanho da página
@param oParams, object, filtros da query string
/*/
method listarCombo(cTabela, nPage, nPageSize, oParams, cId) class agdComboService
    Local oRepository as object
    Default cId := ""

    cTabela := Upper(cTabela)

    If cTabela == "SM2"

        Self:customCombo(cTabela, oParams, cId)

    Else
        cTabelaAux  := IIf(Len(AllTrim(cTabela))==2,"SX5",cTabela)//verifica se é tabela do SX5

        oRepository := agdComboRepository():New(cTabelaAux)
        oRepository:buscar(cTabela, nPage, nPageSize, oParams, cId)
        Self:setFullResponse(oRepository)
        FreeObj(oRepository)
        
    EndIf
    
return nil

/*/{Protheus.doc} customCombo
Retorna estrutura JSON customizada para combos específicos como SM2
@type method
@version 12
@author rodrigo.nsoledade
@since 04/06/2025
@param cTabela, character, tabela solicitada
@return JsonObject, estrutura JSON da resposta
/*/
method customCombo(cTabela, oParams, cId) class agdComboService
    Local oJson   as object
    Local oItem   as json
    Local aMoedas := {} as array
    Local aItems  := {} as array
    Local nX      as numeric
    Local cFilter as character

    oJson   := JsonObject():New()
    oItem   := JsonObject():New()

    If oParams:hasProperty("filter")
        cFilter := UPPER(AllTrim(oParams["filter"]))
    EndIf

    If cTabela == "SM2"

        aMoedas := StrTokArr(AGD10MOEDA(), ";")//Funcao que cria ComboBox de moedas

        For nX := 1 to Len(aMoedas)

            If Empty(cFilter) .Or. !Empty(cFilter) .And. cFilter $ UPPER(aMoedas[nX])
                oItem := JsonObject():New()
                oItem["id"]        := Left(AllTrim(aMoedas[nX]), 1)
                oItem["descricao"] := SubStr(aMoedas[nX], At("=", aMoedas[nX]) + 1)
                AAdd(aItems, oItem)
            EndIf

        Next

        if !empty(cId)
            npos = AScan(aItems,{|x| x:GetJsonText('id') == cId})
            if npos > 0
                Self:setSuccess(aItems[npos])
            else
                Self:setError("Registro não encontrado", 401)
            EndIf
        else
            oJson["items"]            := aItems
            oJson["hasNext"]          := .F.
            oJson["remainingRecords"] := 0
            Self:setSuccess(oJson)
        EndIf
    EndIf

    FreeObj(oJson)
    FreeObj(oItem)

Return 
