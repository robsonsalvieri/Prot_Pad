#include "totvs.ch"
#include 'tlpp-core.th'

namespace agd.tituloFinanceiroRepository
using namespace agd.repositoryUtils /*shortCurt de funcao*/

/*/{Protheus.doc} agdTituloFinanceiroRepository
Repositorio da tabela NEK
@type class
@version 12
@author jean.schulze
@since 06/01/2025
/*/
class agdTituloFinanceiroRepository FROM agdRepositoryUtils
	public Data cChaveIDRepository As Object //chave unica para updates

	public Method New()
	public Method findByTituloAReceberInFechamento()
	public Method findByTituloAPagarInFechamento()
	public method findByTitulosFinanceiroDisponiveisFechamento()
	public Method existByBarterTituloFinanceiroAReceber() as logical
	public Method existByBarterTituloFinanceiroAPagar() as logical
	public Method existsByTituloBaixadoFechamentoFinanceiro() as logical
	public Method existByIdContratoTitulosBaixadosFinanceiro() as Logical
	public Method existByIdContratoFechamentoFinanceiro() as Logical
	public method existsByContratoOGBaixaFinanceiro() as Logical

	private method getQueryAllTitulosPedidoVendaDisponiveis() as Character
	private method getQueryAllTitulosContratoOriginacaoDisponiveis() as Character
	private method getFieldsTitulosAReceber() as Array
	private method getFieldsTitulosAPagar() as Array
	private Method getFields()
	private Method getQueryWithStatus()

endclass
/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author jean.schulze
@since 06/01/2025
@return variant, instância
/*/
Method New(pcChaveIDRepository as character) Class agdTituloFinanceiroRepository
	Self:cChaveIDRepository := pcChaveIDRepository
	_Super:New('NEK', Self:getFields(.T.),,Self:getQueryWithStatus())
Return Self

/*/{Protheus.doc} findByTituloAReceberInFechamento
Retorna se os fechamentos ao qual o titulo a pagar está vinculado
@type method
@version 12
@author jean.schulze
@since 14/03/2025
@param pcFilial, variant, filial E1
@param pcPrefix, variant, prefixo E1
@param pcTitulo, variant, num E1
@param pcParcel, variant, parcela E1
@param pcTipo, character, tipo E1
@return variant, nil
/*/
method findByTituloAReceberInFechamento(pcFilial, pcPrefix, pcTitulo, pcParcel, pcTipo) class agdTituloFinanceiroRepository
	Local aArea  As Array
	Local aQueryRequest As Array
	Local cWhereParam   As Character
	Local jResponseJson
	Local nI 		     as Numeric

	If Self:lOk
		aArea := FwGetArea()
		aQueryRequest := {}

		aFields := Self:getFields()

		cWhereParam := "NEK_CODMOD = '2' AND NEK_FILTIT = '"+pcFilial+"' AND NEK_PREFIX = '"+pcPrefix+"' AND "
		cWhereParam += "NEK_CODTIT = '"+pcTitulo+"' AND NEK_PARCEL = '"+pcParcel+"' AND NEK_TIPO = '"+pcTipo+"'"

		For nI := 1 to len(aFields)
			Self:AddMapFields(aFields[nI][1], aFields[nI][2] , aFields[nI][4], aFields[nI][4], {aFields[nI][2] , aFields[nI][3], TamSX3(aFields[nI][2])[1], 0})
		Next	

		Self:setPage(1)
		Self:setPageSize(100)
		Self:SetQuery(Self:getQueryDefault())
		Self:SetWhere(Self:getWhereDefault(cWhereParam))
		Self:SetUseSpaces(.T.)

		If Self:Execute()
			Self:FillGetResponse()
			if Self:lOk
				jResponseJson := Self:convertToJson(Self:getJSONResponse())
				Self:setSuccess(jResponseJson)
			Endif
		EndIf

		FwRestArea(aArea)
		FwFreeArray(aArea)
		FreeObj(jResponseJson)
	EndIf

return nil

/*/{Protheus.doc} findByTituloAPagarInFechamento
Retorna se os fechamentos ao qual o titulo a pagar está vinculado
@type method
@version 12
@author jean.schulze
@since 14/03/2025
@param pcFilial, variant, filial E2
@param pcPrefix, variant, prefixo E2
@param pcTitulo, variant, num E2
@param pcParcel, variant, parcela E2
@param pcTipo, variant, tipo E2
@param pcFornec, variant, fornecedor E2
@param pcLoja, variant, loja E2
@return variant, nil
/*/
method findByTituloAPagarInFechamento(pcFilial, pcPrefix, pcTitulo, pcParcel, pcTipo, pcFornec, pcLoja) class agdTituloFinanceiroRepository
	Local aArea  As Array
	Local aQueryRequest As Array
	Local cWhereParam   As Character
	Local jResponseJson
	Local nI 		     as Numeric

	If Self:lOk
		aArea := FwGetArea()
		aQueryRequest := {}

		aFields := Self:getFields()
		cWhereParam := "NEK_CODMOD = '1' AND NEK_FILTIT = '"+pcFilial+"' AND NEK_PREFIX = '"+pcPrefix+"' AND "
		cWhereParam += "NEK_CODTIT = '"+pcTitulo+"' AND NEK_PARCEL = '"+pcParcel+"' AND NEK_TIPO = '"+pcTipo+"'"

		For nI := 1 to len(aFields)
			Self:AddMapFields(aFields[nI][1], aFields[nI][2] , aFields[nI][4], aFields[nI][4], {aFields[nI][2] , aFields[nI][3], TamSX3(aFields[nI][2])[1], 0})
		Next

		Self:setPage(1)
		Self:setPageSize(100)
		Self:SetQuery(Self:getQueryDefault())
		Self:SetWhere(Self:getWhereDefault(cWhereParam))
		Self:SetUseSpaces(.T.)

		If Self:Execute()
			Self:FillGetResponse()
			if Self:lOk
				jResponseJson := Self:convertToJson(Self:getJSONResponse())
				Self:setSuccess(jResponseJson)
			Endif
		EndIf

		FwRestArea(aArea)
		FwFreeArray(aArea)
		FreeObj(jResponseJson)
	EndIf

return nil

/*/{Protheus.doc} existByBarterTituloFinanceiroAReceber
Verifica se existe relacao entre o titulo a Receber e o Processo de Barter
@type method
@version 12
@author jean.schulze
@since 14/03/2025
@param pcFilial, variant, filial E1
@param pcPrefix, variant, prefixo E1
@param pcTitulo, variant, num E1
@param pcParcel, variant, parcela E1
@param pcTipo, character, tipo E1
@param pcNegocio, variant, Codigo do Negocio
@return logical, Titulo vinculado a BARTER
/*/
method existByBarterTituloFinanceiroAReceber(pcFilial, pcPrefix, pcTitulo, pcParcel, pcTipo, pcNegocio) as logical class agdTituloFinanceiroRepository
	Local cQuery as Character
	Default pcNegocio := ""

	cQuery := " SELECT "
	cQuery += " NED.NED_FILIAL"
	cQuery += " FROM " + RetSqlName( "NED" ) + " NED"
	cQuery += " INNER JOIN " + RetSqlName( "SD2" ) + " SD2"
	cQuery += "          ON SD2.D2_FILIAL  = '" + xFilial('SD2')  + "'"
	cQuery += " 		AND SD2.D2_DOC      = NED.NED_DOC"
	cQuery += " 		AND SD2.D2_SERIE    = NED.NED_SERIE"
	cQuery += " 		AND SD2.D2_CLIENTE  = NED.NED_CLIENT"
	cQuery += "			AND SD2.D2_LOJA     = NED.NED_LOJA"
	cQuery += " 		AND SD2.D2_ITEM     = NED.NED_ITEMNF"
	cQuery += " 		AND SD2.D_E_L_E_T_  = ' ' "
	cQuery += " INNER JOIN " + RetSqlName( "SE1" ) + " SE1 "
	cQuery += " 		 ON SE1.E1_FILIAL  = '" + xFilial('SE1')  + "' "
	cQuery += " 		AND SE1.E1_NUM     = SD2.D2_DOC"
	cQuery += " 		AND SE1.E1_PREFIXO = SD2.D2_SERIE"
	cQuery += " 		AND SE1.E1_CLIENTE = SD2.D2_CLIENTE"
	cQuery += "			AND SE1.E1_LOJA    = SD2.D2_LOJA"
	cQuery += "			AND SE1.D_E_L_E_T_  = ' '"
	cQuery += " WHERE SE1.E1_FILIAL  = '" + pcFilial + "'"
	cQuery += "   AND SE1.E1_NUM     = '" + pcTitulo + "'"
	cQuery += "   AND SE1.E1_PREFIXO = '" + pcPrefix + "'"
	cQuery += "   AND SE1.E1_PARCELA = '" + pcParcel + "'"
	cQuery += "	  AND SE1.E1_TIPO    = '" + pcTipo + "'"
	cQuery += "	  AND SE1.D_E_L_E_T_  = ' '"

	if !Empty(pcNegocio)
		cQuery += "	AND NED.NED_CODBRT = '" + pcNegocio + "'"
	Endif

	if len(Self:freeQueryToArray(cQuery, {"NED_FILIAL"})) > 0
		return .t.
	EndIf
return .F.

/*/{Protheus.doc} existByBarterTituloFinanceiroAPagar
Verifica se existe relacao entre o titulo a Pagar e o Processo de Barter
@type method
@version 12
@author jean.schulze
@since 14/03/2025
@param pcFilial, variant, filial E2
@param pcPrefix, variant, prefixo E2
@param pcTitulo, variant, num E2
@param pcParcel, variant, parcela E2
@param pcTipo, variant, tipo E2
@param pcFornec, variant, fornecedor E2
@param pcLoja, variant, loja E2
@param pcNegocio, variant, Codigo do Negocio
@return logical, Titulo vinculado a BARTER
/*/
method existByBarterTituloFinanceiroAPagar(pcFilial, pcPrefix, pcTitulo, pcParcel, pcTipo, pcFornec, pcLoja, pcNegocio) as logical class agdTituloFinanceiroRepository
	Local cQuery as Character
	Default pcNegocio := ""

	cQuery := " SELECT"
	cQuery += " NEI.NEI_FILIAL"
	cQuery += " FROM " + RetSqlName( "NEF" ) + " NEF"
	cQuery += " INNER JOIN " + RetSqlName( "NEI" ) + " NEI"
	cQuery += " 		ON  NEI.NEI_FILIAL = '" + xFilial('NEI')  + "'"
	cQuery += " 		AND NEI.NEI_IDCTR = NEF.NEF_IDCTR"
	cQuery += " 		AND NEI.D_E_L_E_T_ = ' ' "
	cQuery += " INNER JOIN " + RetSqlName( "SE2" ) + " SE2"
	cQuery += " 		 ON SE2.E2_FILIAL  = '" + xFilial('SE2')  + "'"
	cQuery += " 		AND SE2.E2_NUM     = NEI.NEI_DOC"
	cQuery += " 		AND SE2.E2_PREFIXO = NEI.NEI_SERIE"
	cQuery += " 		AND SE2.E2_FORNECE = NEI.NEI_FORNEC"
	cQuery += "			AND SE2.E2_LOJA    = NEI.NEI_LOJA"
	cQuery += "			AND SE2.D_E_L_E_T_  = ' ' "
	cQuery += " WHERE SE2.E2_FILIAL  = '" + pcFilial + "'"
	cQuery += "   AND SE2.E2_NUM     = '" + pcTitulo + "'"
	cQuery += "   AND SE2.E2_PREFIXO = '" + pcPrefix + "'"
	cQuery += "   AND SE2.E2_PARCELA = '" + pcParcel + "'"
	cQuery += "   AND SE2.E2_TIPO    = '" + pcTipo + "'"
	cQuery += "   AND SE2.E2_FORNECE = '" + pcFornec + "'"
	cQuery += "	  AND SE2.E2_LOJA    = '" + pcLoja + "'"
	cQuery += "	  AND SE2.D_E_L_E_T_ = ' '"

	if !Empty(pcNegocio)
		cQuery += "	AND NEF.NEF_CODBRT = '" + pcNegocio + "'"
	Endif

	if len(Self:freeQueryToArray(cQuery, {"NEI_FILIAL"})) > 0
		return .T.
	EndIf
return .F.

/*/{Protheus.doc} existsByTituloBaixadoFechamentoFinanceiro
Verifica se algum titulo do fechamento financeiro está liquidado
@type method
@version 12
@author jean.schulze
@since 20/03/2025
@param pcCodigoFechamentoFinanceiro, variant, codigo do fechamento financeiro
@return logical, existe titulo baixado
/*/
method existsByTituloBaixadoFechamentoFinanceiro(pcIdFechamentoFinanceiro) as logical class agdTituloFinanceiroRepository
	Local cQuery as Character

	DBSelectArea("NEK")
	NEK->(DBSetOrder(1))
	If NEK->(dbSeek(pcIdFechamentoFinanceiro))

		cQuery := " SELECT"
		cQuery += " NEK_FILIAL"
		cQuery += " FROM " + RetSqlName( "NEK" ) + " NEK"
		cQuery += " INNER JOIN " + RetSqlName( "SE2" ) + " SE2"
		cQuery += " 		 ON SE2.E2_FILIAL  = NEK.NEK_FILTIT
		cQuery += " 		AND SE2.E2_NUM     = NEK.NEK_CODTIT"
		cQuery += " 		AND SE2.E2_PARCELA = NEK.NEK_PARCEL"
		cQuery += " 		AND SE2.E2_TIPO    = NEK.NEK_TIPO"
		cQuery += " 		AND SE2.E2_PREFIXO = NEK.NEK_PREFIX"
		cQuery += " 		AND SE2.E2_FORNECE = NEK.NEK_CODFOR"
		cQuery += "			AND SE2.E2_LOJA    = NEK.NEK_LOJA"
		cQuery += "			AND SE2.E2_SALDO   <> SE2.E2_VALOR"
		cQuery += "			AND SE2.D_E_L_E_T_  = ' '"
		cQuery += " WHERE NEK.NEK_FILIAL = '"+ NEK->NEK_FILIAL +"' "
		cQuery += "   AND NEK.NEK_CODFEC = '" + NEK->NEK_CODFEC + "' "
		cQuery += "   AND NEK.NEK_CODMOD  = '1' "
		cQuery += "	  AND NEK.D_E_L_E_T_ = ' ' "

		if len(Self:freeQueryToArray(cQuery, {"NEK_FILIAL"})) > 0
			return .T.
		EndIf

		cQuery := " SELECT"
		cQuery += " NEK_FILIAL"
		cQuery += " FROM " + RetSqlName( "NEK" ) + " NEK"
		cQuery += " INNER JOIN " + RetSqlName( "SE1" ) + " SE1"
		cQuery += " 		 ON SE1.E1_FILIAL  = NEK.NEK_FILTIT
		cQuery += " 		AND SE1.E1_NUM     = NEK.NEK_CODTIT"
		cQuery += " 		AND SE1.E1_SERIE   = NEK.NEK_SERTIT"
		cQuery += " 		AND SE1.E1_PREFIXO = NEK.NEK_PREFIX"
		cQuery += " 		AND SE1.E1_PARCELA = NEK.NEK_PARCEL"
		cQuery += " 		AND SE1.E1_TIPO    = NEK.NEK_TIPO"
		cQuery += " 		AND SE1.E1_CLIENTE = NEK.NEK_CODFOR"
		cQuery += "			AND SE1.E1_LOJA    = NEK.NEK_LOJA"
		cQuery += "			AND SE1.E1_SALDO   <> SE1.E1_VALOR"
		cQuery += "			AND SE1.D_E_L_E_T_  = ' '"
		cQuery += " WHERE NEK.NEK_FILIAL = '"+ NEK->NEK_FILIAL +"' "
		cQuery += "   AND NEK.NEK_CODFEC = '" + NEK->NEK_CODFEC + "' "
		cQuery += "   AND NEK.NEK_CODMOD  = '2'"
		cQuery += "	  AND NEK.D_E_L_E_T_ = ' '"

		if len(Self:freeQueryToArray(cQuery, {"NEK_FILIAL"})) > 0
			return .T.
		EndIf
	EndIf

return .F.

/*/{Protheus.doc} getQueryAllTitulosPedidoVendaDisponiveis
Query para retornar lista de titulos a receber ainda não utilizados em negociação barter
@type method
@version 12
@author Gilson.Venturi
@since 22/05/2025
@return character, query
/*/
Method getQueryAllTitulosPedidoVendaDisponiveis() as Character class agdTituloFinanceiroRepository
	Local cQuery   as character

	cQuery := " SELECT #QueryFields#"
	cQuery += " FROM " + RetSqlName( "NED" ) + " NED"
	cQuery += " INNER JOIN " + RetSqlName( "SD2" ) + " SD2"
	cQuery += "          ON SD2.D2_FILIAL  = '" + xFilial('SD2')  + "'"
	cQuery += " 		AND SD2.D2_DOC      = NED.NED_DOC"
	cQuery += " 		AND SD2.D2_SERIE    = NED.NED_SERIE"
	cQuery += " 		AND SD2.D2_CLIENTE  = NED.NED_CLIENT"
	cQuery += " 		AND SD2.D2_LOJA     = NED.NED_LOJA"
	cQuery += " 		AND SD2.D2_ITEM     = NED.NED_ITEMNF"
	cQuery += " 		AND SD2.D_E_L_E_T_  = ' '"
	cQuery += " INNER JOIN " + RetSqlName( "SE1" ) + " SE1"
	cQuery += " 		 ON SE1.E1_FILIAL  = '" + xFilial('SE1')  + "'"
	cQuery += " 		AND SE1.E1_NUM     = SD2.D2_DOC"
	cQuery += " 		AND SE1.E1_PREFIXO = SD2.D2_SERIE"
	cQuery += " 		AND SE1.E1_CLIENTE = SD2.D2_CLIENTE"
	cQuery += " 		AND SE1.E1_LOJA    = SD2.D2_LOJA"
	cQuery += " 		AND SE1.D_E_L_E_T_  = ' '"
	cQuery += " INNER JOIN " + RetSqlName( "SA1" ) + " SA1 "
	cQuery += " 		ON SA1.D_E_L_E_T_  = ' ' "
	cQuery += " 		AND SA1.A1_FILIAL = '" + xFilial('SA1') + "' "
	cQuery += " 		AND SA1.A1_COD = SE1.E1_CLIENTE "
	cQuery += " 		AND SA1.A1_LOJA = SE1.E1_LOJA"
	cQuery += "	WHERE #QueryWhere# "
	cQuery += "   AND NOT EXISTS (SELECT NEK.NEK_CODFEC"
	cQuery += "						FROM " + RetSqlName( "NEK" ) + " NEK"
	cQuery += "						INNER JOIN " + RetSqlName("NEJ") + " NEJ "
	cQuery += "						 ON NEJ_FILIAL = '"+xFilial('NEJ') +"' AND NEJ_CODIGO = NEK_CODFEC "
	cQuery += "						 AND NEJ.D_E_L_E_T_ = ' ' AND NEJ_STATUS <> '3' "//Status diferente de Cancelado
	cQuery += "						WHERE NEK.NEK_FILIAL = '" + xFilial('NEK')  + "'"
	cQuery += "						  AND NEK.NEK_PREFIX = SE1.E1_PREFIXO"
	cQuery += "						  AND NEK.NEK_CODTIT = SE1.E1_NUM"
	cQuery += "						  AND NEK.NEK_PARCEL = SE1.E1_PARCELA"
	cQuery += "						  AND NEK.NEK_TIPO   = SE1.E1_TIPO"
	cQuery += "						  AND NEK.D_E_L_E_T_ = ' ')"
	cQuery += "   GROUP BY #QueryFields# "

Return cQuery

/*/{Protheus.doc} getQueryAllTitulosContratoOriginacaoDisponiveis
Query para retornar lista de titulos a pagar ainda não utilizados em negociação barter
@type method
@version 12
@author Gilson.Venturi
@since 22/05/2025
@return character, query
/*/
Method getQueryAllTitulosContratoOriginacaoDisponiveis() as Character class agdTituloFinanceiroRepository
	Local cQuery   as character

	cQuery := " SELECT DISTINCT #QueryFields#"
	cQuery += " FROM " + RetSqlName( "NEF" ) + " NEF"
	cQuery += 	" INNER JOIN " + RetSqlName( "NEI" ) + " NEI"
	cQuery += 			" ON  NEI.NEI_FILIAL = '" + xFilial('NEI')  + "'"
	cQuery += 			" AND NEI.NEI_IDCTR = NEF.NEF_IDCTR"
	cQuery += 			" AND NEI.D_E_L_E_T_  = ' '"
	cQuery += " INNER JOIN " + RetSqlName( "SE2" ) + " SE2"
	cQuery += 			" ON SE2.E2_FILIAL  = '" + xFilial('SE2')  + "'"
	cQuery += 			" AND SE2.E2_NUM     = NEI.NEI_DOC"
	cQuery += 			" AND SE2.E2_PREFIXO = NEI.NEI_SERIE"
	cQuery += 			" AND SE2.E2_FORNECE = NEI.NEI_FORNEC"
	cQuery += 			" AND SE2.E2_LOJA    = NEI.NEI_LOJA"
	cQuery += 			" AND SE2.D_E_L_E_T_  = ' '"
	cQuery += " INNER JOIN " + RetSqlName( "SA2" ) + " SA2 " 
	cQuery += 			" ON SA2.A2_FILIAL = '" + xFilial('SA2') + "'"
	cQuery += 			" AND SA2.A2_COD = SE2.E2_FORNECE "
	cQuery += 			" AND SA2.A2_LOJA = SE2.E2_LOJA "
	cQuery += 			" AND SA2.D_E_L_E_T_  = ' '"
	cQuery += "	WHERE #QueryWhere# "
	cQuery += 	" AND NEF.D_E_L_E_T_  = ' '"
	cQuery +=   " AND NOT EXISTS (SELECT NEK.NEK_CODFEC"
	cQuery += 						" FROM " + RetSqlName( "NEK" ) + " NEK"
	cQuery += 						" INNER JOIN " + RetSqlName("NEJ") + " NEJ "
	cQuery += 							" ON NEJ_FILIAL = '"+xFilial('NEJ') +"' AND NEJ_CODIGO = NEK_CODFEC "
	cQuery += 							" AND NEJ.D_E_L_E_T_ = ' ' AND NEJ_STATUS <> '3' "//Status diferente de Cancelado
	cQuery += 								" WHERE NEK.NEK_FILIAL = '" + xFilial('NEK')  + "'"
	cQuery += 									" AND NEK.NEK_PREFIX = SE2.E2_PREFIXO"
	cQuery += 									" AND NEK.NEK_CODTIT = SE2.E2_NUM"
	cQuery += 									" AND NEK.NEK_PARCEL = SE2.E2_PARCELA"
	cQuery += 									" AND NEK.NEK_TIPO   = SE2.E2_TIPO"
	cQuery += 									" AND NEK.D_E_L_E_T_ = ' ')"

Return cQuery

/*/{Protheus.doc} agdTituloFinanceiroRepository::existByIdContratoTitulosBaixadosFinanceiro
Verifica se para o Id contrato tem titulos com liquidação
@type method
@version  P12
@author claudineia.reinert
@since 19/05/2025
@param pcIdCtr, character, Id do contrato, campo NEI_IDCTR
@return logical, valor logico
/*/
method existByIdContratoTitulosBaixadosFinanceiro(pcIdCtr) as Logical class agdTituloFinanceiroRepository
	Local cQuery as Character

	cQuery := " SELECT NEI_FILIAL "
	cQuery += " FROM " + RetSqlName( "NEI" ) + "  NEI "
	cQuery += " INNER JOIN " + RetSqlName( "SE2" ) + " SE2 ON SE2.D_E_L_E_T_  = ' ' "
	cQuery += " 	AND SE2.E2_FILIAL  = '" + FWxFilial('SE2') + "' "
	cQuery += " 	AND SE2.E2_NUM     = NEI.NEI_DOC "
	cQuery += " 	AND SE2.E2_PREFIXO = NEI.NEI_SERIE "
	cQuery += " 	AND SE2.E2_FORNECE = NEI.NEI_FORNEC "
	cQuery += " 	AND SE2.E2_LOJA    = NEI.NEI_LOJA "
	cQuery += " 	AND SE2.E2_SALDO <> SE2.E2_VALOR "
	cQuery += " WHERE NEI.D_E_L_E_T_ = ' ' "
	cQuery += " 	AND NEI.NEI_FILIAL = '" + FWxFilial('NEI')  + "' "
	cQuery += " 	AND NEI.NEI_IDCTR = '" + pcIdCtr  + "' "

	if len(Self:freeQueryToArray(cQuery, {"NEI_FILIAL"})) > 0
		return .t.
	EndIf
return .F.

/*/{Protheus.doc} agdTituloFinanceiroRepository::existByIdContratoFechamentoFinanceiro
Verifica se para o Id contrato tem titulos com fechamento financeiro
@type method
@version  P12
@author claudineia.reinert
@since 19/05/2025
@param pcIdCtr, character, Id do contrato, campo NEI_IDCTR
@return logical, valor logico
/*/
method existByIdContratoFechamentoFinanceiro(pcIdCtr) as Logical class agdTituloFinanceiroRepository
	Local cQuery as Character

	cQuery := " SELECT NEI_FILIAL "
	cQuery += " FROM " + RetSqlName( "NEI" ) + "  NEI "
	cQuery += " INNER JOIN " + RetSqlName( "SE2" ) + " SE2 ON SE2.D_E_L_E_T_  = ' ' "
	cQuery += " 	AND SE2.E2_FILIAL  = '" + xFilial('SE2')  + "' "
	cQuery += " 	AND SE2.E2_NUM     = NEI.NEI_DOC "
	cQuery += " 	AND SE2.E2_PREFIXO = NEI.NEI_SERIE "
	cQuery += " 	AND SE2.E2_FORNECE = NEI.NEI_FORNEC "
	cQuery += " 	AND SE2.E2_LOJA    = NEI.NEI_LOJA "
	cQuery += " INNER JOIN " + RetSqlName( "NEK" ) + " NEK ON NEK.D_E_L_E_T_  = ' ' "
	cQuery += " 	AND NEK.NEK_FILIAL = '" + xFilial('NEK')  + "' "
	cQuery += " 	AND NEK.NEK_PREFIX = SE2.E2_PREFIXO "
	cQuery += " 	AND NEK.NEK_CODTIT = SE2.E2_NUM "
	cQuery += " 	AND NEK.NEK_PARCEL = SE2.E2_PARCELA "
	cQuery += " 	AND NEK.NEK_TIPO   = SE2.E2_TIPO "
	cQuery += " 	AND NEK.NEK_CODFOR = SE2.E2_FORNECE "
	cQuery += " 	AND NEK.NEK_LOJA   = SE2.E2_LOJA "
	cQuery += " WHERE NEI.D_E_L_E_T_ = ' ' "
	cQuery += " 	AND NEI.NEI_FILIAL = '" + xFilial('NEI')  + "' "
	cQuery += " 	AND NEI.NEI_IDCTR = '" + pcIdCtr  + "' "

	if len(Self:freeQueryToArray(cQuery, {"NEI_FILIAL"})) > 0
		return .t.
	EndIf
return .F.

/*/{Protheus.doc} agdTituloFinanceiroRepository::existsByContratoOGBaixaFinanceiro
Verifica se para o contrato do OG tem titulos baixados, sendo que este contrato ainda não foi vinculado a negociação
@type method
@version  P12
@author claudineia.reinert
@since 02/06/2025
@param cFilCtr, character, filial do contrato
@param cCodCtr, character, codigo do contrato
@return logical, Valor logico
/*/
method existsByContratoOGBaixaFinanceiro(cFilCtr, cCodCtr) as Logical class agdTituloFinanceiroRepository
	Local lRet 		:= .F. //.f. = valor padrão não tem baixa de titulos
	Local cQuery 	:= ""
	Local cAliQry 	:= GetNextAlias()
	Local aParams   := {}
	Local oStateQry := nil
	Local nX  		:= 0

	cQuery := " SELECT COUNT(E2_NUM) AS COUNT_BAIXA "
	cQuery += " FROM "+ retSqlName('NJR') +" NJR "
	cQuery += " INNER JOIN "+ retSqlName('NJM') +" NJM ON NJM.D_E_L_E_T_ = ' ' "
	cQuery += 	" AND NJM.NJM_FILIAL = ? " ; aAdd(aParams, FWxFilial("NJM"))
	cQuery += 	" AND NJM.NJM_CODCTR = NJR_CODCTR "
	cQuery += " INNER JOIN "+ retSqlName('NJ0') +" NJ0 ON NJ0.D_E_L_E_T_ = ' ' AND NJ0.NJ0_FILIAL = ? " ; aAdd(aParams, FWxFilial("NJ0"))
	cQuery += 	" AND NJ0_CODENT = NJM_CODENT AND NJ0_LOJENT = NJM_LOJENT "
	cQuery += " INNER JOIN "+ retSqlName('SD1') +" SD1 ON SD1.D_E_L_E_T_ = ' ' AND SD1.D1_FILIAL = NJM_FILIAL  "
	cQuery += 	" AND NJM_DOCNUM = D1_DOC AND NJM_DOCSER = D1_SERIE  "
	cQuery += 	" AND D1_FORNECE = NJ0_CODFOR AND D1_LOJA = NJ0_LOJFOR "
	cQuery += 	" AND D1_TIPO = 'N'  AND SD1.D1_COD = NJM_CODPRO "
	cQuery += " INNER JOIN "+ retSqlName('SE2') +" SE2 "
	cQuery += 	" ON  SE2.E2_FILIAL  = SD1.D1_FILIAL "
	cQuery += 	" AND SE2.E2_NUM     = NJM_DOCNUM "
	cQuery += 	" AND SE2.E2_PREFIXO = NJM_DOCSER "
	cQuery +=	" AND SE2.E2_FORNECE = NJ0.NJ0_CODFOR "
	cQuery += 	" AND SE2.E2_LOJA    = NJ0.NJ0_LOJFOR "
	cQuery += 	" AND SE2.D_E_L_E_T_  = ' ' "
	cQuery += 	" AND SE2.E2_SALDO <> SE2.E2_VALOR "
	cQuery += " WHERE NJR.D_E_L_E_T_ = ' ' "
	cQuery += 	" AND NJR.NJR_FILIAL = ? " ; aAdd(aParams, cFilCtr)
	cQuery += 	" AND NJR_CODCTR = ? " ; aAdd(aParams, cCodCtr)

	oStateQry := FWPreparedStatement():New()
	oStateQry:SetQuery(cQuery)
	aEval(aParams, {|cValue| (nX++, oStateQry:SetString(nX, cValue))})
	cQuery := ChangeQuery(oStateQry:GetFixQuery())
	dbUseArea(.T.,"TOPCONN",TCGenQry(,,cQuery),cAliQry,.T.,.T.)

	If  !(cAliQry)->(Eof()) .and. (cAliQry)->(COUNT_BAIXA) > 0
		lRet := .T.
	EndIf

return lRet

/*/{Protheus.doc} getFields
Retorna a Lista de Campos do Repositório
@type method
@version 12
@author jean.schulze
@since 10/01/2025
@return variant, lista de campos
/*/
Method getFields(lFieldsAdd) Class agdTituloFinanceiroRepository
	Local aArrayFields := {}

	Default lFieldsAdd := .f.

	AAdd(aArrayFields, {'filial'                  , 'NEK_FILIAL', 'C', .T.})
	AAdd(aArrayFields, {'codigo'                  , 'NEK_CODFEC', 'C', .T.})
	AAdd(aArrayFields, {'item'   				  , 'NEK_ITEM'  , 'C', .T.})
	AAdd(aArrayFields, {'codigoBarter'            , 'NEK_CODBRT', 'C', .T.})
	AAdd(aArrayFields, {'codigoModo' 		      , 'NEK_CODMOD', 'C', .T.}) //1-Compra(A Pagar), 2- Venda(A Receber)
	AAdd(aArrayFields, {'notaFiscal'              , 'NEK_CODNF',  'C', .T.})
	AAdd(aArrayFields, {'serie'                   , 'NEK_SERNF',  'C', .T.})
	AAdd(aArrayFields, {'filialTitulo'            , 'NEK_FILTIT', 'C', .T.})
	AAdd(aArrayFields, {'tipo'                    , 'NEK_TIPO',   'C', .T.})
	AAdd(aArrayFields, {'prefixo'                 , 'NEK_PREFIX', 'C', .T.})
	AAdd(aArrayFields, {'numero'                  , 'NEK_CODTIT', 'C', .T.})
	AAdd(aArrayFields, {'parcela'                 , 'NEK_PARCEL', 'C', .T.})
	AAdd(aArrayFields, {'codigoClienteFornecedor' , 'NEK_CODFOR', 'C', .T.})
	AAdd(aArrayFields, {'lojaClienteFornecedor'   , 'NEK_LOJA',   'C', .T.})
	AAdd(aArrayFields, {'dataEmissao'             , 'NEK_DTEMIS', 'D', .T.})
	AAdd(aArrayFields, {'dataVencimento'          , 'NEK_DTVENC', 'D', .T.})
	AAdd(aArrayFields, {'valor'                   , 'NEK_VALOR',  'N', .T.})
	AAdd(aArrayFields, {'valorLiquido'            , 'NEK_LIQUID', 'N', .T.})
	If lFieldsAdd
		AAdd(aArrayFields, {'statusTitulo'            , 'STATUS',     'C', .T.})
		AAdd(aArrayFields, {'saldoAtual'              , 'SALDO',      'N', .T.})
		AAdd(aArrayFields, {'nomeClienteFornecedor'   , 'NOME_CLIFOR', 'C', .T.})
	EndIf


Return aArrayFields


Method getQueryWithStatus() Class agdTituloFinanceiroRepository
	Local cQuery as Character
	Local cJoinSe1 as Character
	Local cJoinSe2 as Character
	Local cJoinSa1 as Character
	Local cJoinSa2 as Character

	cJoinSe1 := " NEK_CODMOD = '2' "
	cJoinSe1 += " AND SE1.E1_FILIAL  = NEK.NEK_FILTIT "
	cJoinSe1 += " AND SE1.E1_NUM     = NEK.NEK_CODTIT "
	cJoinSe1 += " AND SE1.E1_SERIE   = NEK.NEK_SERTIT "
	cJoinSe1 += " AND SE1.E1_PREFIXO = NEK.NEK_PREFIX "
	cJoinSe1 += " AND SE1.E1_PARCELA = NEK.NEK_PARCEL "
	cJoinSe1 += " AND SE1.E1_TIPO    = NEK.NEK_TIPO "
	cJoinSe1 += " AND SE1.E1_CLIENTE = NEK.NEK_CODFOR "
	cJoinSe1 += " AND SE1.E1_LOJA    = NEK.NEK_LOJA "
	cJoinSe1 += " AND SE1.D_E_L_E_T_  = ' ' "

	cJoinSe2 := " NEK_CODMOD = '1' "
	cJoinSe2 += " AND SE2.E2_FILIAL  = NEK.NEK_FILTIT "
	cJoinSe2 += " AND SE2.E2_NUM     = NEK.NEK_CODTIT "
	cJoinSe2 += " AND SE2.E2_PARCELA = NEK.NEK_PARCEL "
	cJoinSe2 += " AND SE2.E2_TIPO    = NEK.NEK_TIPO "
	cJoinSe2 += " AND SE2.E2_PREFIXO = NEK.NEK_PREFIX "
	cJoinSe2 += " AND SE2.E2_FORNECE = NEK.NEK_CODFOR "
	cJoinSe2 += " AND SE2.E2_LOJA    = NEK.NEK_LOJA "
	cJoinSe2 += " AND SE2.D_E_L_E_T_  = ' ' "
	
	cJoinSa1 := " SA1.A1_COD = NEK.NEK_CODFOR AND SA1.A1_LOJA = NEK.NEK_LOJA "

	cJoinSa2 := " SA2.A2_COD = NEK.NEK_CODFOR AND SA2.A2_LOJA = NEK.NEK_LOJA "

	cQuery := " SELECT NEK.*, "
	cQuery += " CASE WHEN NEK.NEK_CODMOD = '2' THEN SE1.E1_STATUS "
	cQuery += "     WHEN NEK.NEK_CODMOD = '1' THEN SE2.E2_STATUS "
	cQuery += "     ELSE 'NA' END  AS STATUS , "
	cQuery += " CASE WHEN NEK.NEK_CODMOD = '2' THEN SE1.E1_SALDO "
	cQuery += "     WHEN NEK.NEK_CODMOD = '1' THEN SE2.E2_SALDO "
	cQuery += "     ELSE 0 END AS SALDO, "
	cQuery += " CASE WHEN NEK.NEK_CODMOD = '2' THEN SA1.A1_NOME "
	cQuery += "     WHEN NEK.NEK_CODMOD = '1' THEN SA2.A2_NOME "
	cQuery += "     ELSE 'NA' END AS NOME_CLIFOR "
	cQuery += " FROM  " + RetSqlName('NEK')  + " NEK "
	cQuery += " LEFT JOIN " + RetSqlName('SE1')  + " SE1  ON "+ Self:getWhereDefault(cJoinSe1, "SE1")
	cQuery += " LEFT JOIN " + RetSqlName('SE2')  + " SE2  ON "+ Self:getWhereDefault(cJoinSe2, "SE2")
	cQuery += " LEFT JOIN " + RetSqlName('SA1')  + " SA1  ON "+ Self:getWhereDefault(cJoinSa1, "SA1")
	cQuery += " LEFT JOIN " + RetSqlName('SA2')  + " SA2  ON "+ Self:getWhereDefault(cJoinSa2, "SA2")

return cQuery

/*/{Protheus.doc} getFieldsTitulosAReceber
Lista de Campos da Consulta de Titulos a Receber do Financeiro
@type method
@version 12
@author Gilson.Venturi
@since 22/05/2025
@return variant, lista de campos
/*/
method getFieldsTitulosAReceber() class agdTituloFinanceiroRepository
	Local aFields := {}

	AAdd(aFields, {"filial"         , "NED_FILIAL"  , "C", .T.})
	AAdd(aFields, {"codNegocio"     , "NED_CODBRT"  , "C", .T.})
	AAdd(aFields, {"codNota"        , "D2_DOC"      , "C", .T.})
	AAdd(aFields, {"serieNota"      , "D2_SERIE"    , "C", .T.})
	AAdd(aFields, {"filialTitulo"   , "E1_FILIAL"   , "C", .T.})
	AAdd(aFields, {"tipoTitulo"     , "E1_TIPO"     , "C", .T.})
	AAdd(aFields, {"prefixo"        , "E1_PREFIXO"  , "C", .T.})
	AAdd(aFields, {"codTitulo"      , "E1_NUM"      , "C", .T.})
	AAdd(aFields, {"parcela"        , "E1_PARCELA"  , "C", .T.})
	AAdd(aFields, {"codCliente"     , "E1_CLIENTE"  , "C", .T.})
	AAdd(aFields, {"serieTitulo"    , "E1_SERIE"    , "C", .T.})
	AAdd(aFields, {"loja"           , "E1_LOJA"     , "C", .T.})
	AAdd(aFields, {"dataEmissao"    , "E1_EMISSAO"  , "D", .T.})
	AAdd(aFields, {"dataVencimento" , "E1_VENCTO"   , "D", .T.})
	AAdd(aFields, {"valor"          , "E1_SALDO"    , "N", .T.})
	AAdd(aFields, {"saldo"          , "E1_SALDO"    , "N", .T.})
	AAdd(aFields, {'nomeCliente'    , "A1_NOME" , 'C', .T.})

Return aFields

/*/{Protheus.doc} getFieldsTitulosAPagar
Lista de Campos da Consulta de Titulos a pagar do Financeiro
@type method
@version 12
@author Gilson.Venturi
@since 22/05/2025
@return variant, lista de campos
/*/
method getFieldsTitulosAPagar() class agdTituloFinanceiroRepository
	Local aFields := {}

	AAdd(aFields, {"filial"         , "NEF_FILIAL"  , "C", .T.})
	AAdd(aFields, {"codNegocio"     , "NEF_CODBRT"  , "C", .T.})
	AAdd(aFields, {"codNota"        , "NEI_DOC"     , "C", .T.})
	AAdd(aFields, {"serieNota"      , "NEI_SERIE"   , "C", .T.})
	AAdd(aFields, {"filialTitulo"   , "E2_FILIAL"   , "C", .T.})
	AAdd(aFields, {"tipoTitulo"     , "E2_TIPO"     , "C", .T.})
	AAdd(aFields, {"prefixo"        , "E2_PREFIXO"  , "C", .T.})
	AAdd(aFields, {"codTitulo"      , "E2_NUM"      , "C", .T.})
	AAdd(aFields, {"parcela"        , "E2_PARCELA"  , "C", .T.})
	AAdd(aFields, {"codFornecedor"  , "E2_FORNECE"  , "C", .T.})
	AAdd(aFields, {"loja"           , "E2_LOJA"     , "C", .T.})
	AAdd(aFields, {"dataEmissao"    , "E2_EMISSAO"  , "D", .T.})
	AAdd(aFields, {"dataVencimento" , "E2_VENCTO"   , "D", .T.})
	AAdd(aFields, {"valor"          , "E2_SALDO"    , "N", .T.})
	AAdd(aFields, {"saldo"          , "E2_SALDO"    , "N", .T.})
	AAdd(aFields, {'nomeFornecedor' , "A2_NOME"     , 'C', .T.})

Return aFields

/*/{Protheus.doc} findByTitulosFinanceiroDisponiveisFechamento
Retorna lista paginada dos títulos a pagar e receber que ainda não foram utilizados em negociação barter
@type method
@version 12
@author Gilson.Venturi
@since 22/05/2025
@param nPage, numeric, pagina
@param nPageSize, numeric, tamanho pagina
@param oJsonQryParam, object, query param
@return variant, nil
/*/
Method findByTitulosFinanceiroDisponiveisFechamento(nPage, nPageSize, oJsonParam) class agdTituloFinanceiroRepository
	Local aArea  		As Array
	Local jResponseJson As Json
	Local cWhereParam   as Character
	Local cOrder        as Character

	Default nPage          := 1
	Default nPageSize      := 10

	If Self:lOk
		aArea  := FwGetArea()

		Self:setPage(nPage)
		Self:setPageSize(nPageSize)

		If oJsonParam:GetJsonText("modulo") == "1" //Compra - A PAGAR
			cOrder 		:= Self:getOrderQueryParam(oJsonParam, Self:getFieldsTitulosAPagar())
			cWhereParam := Self:getWhereQueryParam(oJsonParam, Self:getFieldsTitulosAPagar())

			Self:setFieldsConsulta(Self:getFieldsTitulosAPagar())
			Self:SetQuery(Self:getQueryAllTitulosContratoOriginacaoDisponiveis())

			Self:SetWhere(Self:getWhereDefault(cWhereParam,'NEF'))
		else
			cOrder 		:= Self:getOrderQueryParam(oJsonParam, Self:getFieldsTitulosAReceber())
			cWhereParam := Self:getWhereQueryParam(oJsonParam, Self:getFieldsTitulosAReceber())

			Self:setFieldsConsulta(Self:getFieldsTitulosAReceber())
			Self:SetQuery(Self:getQueryAllTitulosPedidoVendaDisponiveis())

			Self:SetWhere(Self:getWhereDefault(cWhereParam,'NED'))
		Endif

		Self:SetUseSpaces(.T.)

		if Self:Execute()
			Self:FillGetResponse()
			if Self:lOk
				jResponseJson := Self:convertToJson(Self:getJSONResponse())
				Self:setSuccess(jResponseJson)
			Endif
		Endif

		FwRestArea(aArea)
		FwFreeArray(aArea)
		FreeObj(jResponseJson)
	EndIf
Return Nil
