#include "totvs.ch"
#include 'tlpp-core.th'

namespace agd.instrucaoEmbarqueItemRepository
using namespace agd.repositoryUtils /*shortCurt de funcao*/

/*/{Protheus.doc} agdInstrucaoEmbarqueItemRepository
Repositorio da tabela NED
@type class
@version 12
@author jean.schulze
@since 06/01/2025
/*/
class agdInstrucaoEmbarqueItemRepository FROM agdRepositoryUtils
	public Data cChaveIDRepository As Object //chave unica para updates


	public Method New()
	public Method findByNotaFiscal()   as Character
	public Method findByPedidoItem()   as Character
	public Method sumByNegocioInsumo() as Numeric
	public Method updateRepository()
	public Method findByNegociacaoBarter()   as Character
	public method sumByDevolucaoNegocioInsumo() as Numeric
	public method sumByDevolucaoNFItemIE() as Numeric
	public method findAllNotasFiscaisBarter() as Array
	public method getFields() as Array

	private method getQueryJoin() as Array

endclass
/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author jean.schulze
@since 06/01/2025
@return variant, instância
/*/
Method New(pcChaveIDRepository as character) Class agdInstrucaoEmbarqueItemRepository
	Self:cChaveIDRepository := pcChaveIDRepository

	_Super:New("NED", Self:getFields(.T.), Self:getQueryJoin())

Return Self

/*/{Protheus.doc} findByNotaFiscal
Retorna ID do repositorio conforme ID de Nota Fiscal informado
@type method
@version 12
@author jean.schulze
@since 22/01/2025
@param pcIdNotaFiscal, character, id da nota fiscal
@return character, id do repositorio
/*/
method findByNotaFiscal(pcIdNotaFiscal as Character) as Character class agdInstrucaoEmbarqueItemRepository
	Local aArea	   := GetArea()
	Local cId      := ""

	dbSelectArea('NED')
	dbSetOrder(2) //novo indice
	NED->(DBGoTop())
	NED->(dbSeek(pcIdNotaFiscal))

	if pcIdNotaFiscal == NED->(NED_FILIAL+NED_DOC+NED_SERIE+NED_CLIENT+NED_LOJA+NED_CODPRO+NED_ITEMNF)
		cId := NED->NED_FILIAL+NED->NED_CODIEB+NED->NED_ITEM
	End

	Self:setSuccess(cId)
	RestArea(aArea)
return cId

/*/{Protheus.doc} findByPedidoItem
Retorna ID do Repositório quando pesquisado por pedido e item
@type method
@version 12
@author jean.schulze
@since 22/01/2025
@param pcCodPedido, character, codigo do pedido
@param pcIdItemPedido, variant, item do pedido
@return character, ID do Repositorio
/*/
method findByPedidoItem(pcCodPedido as Character, pcItemPedido as Character) as Character class agdInstrucaoEmbarqueItemRepository
	Local aArea	      := GetArea()
	Local cId         := ""
	Local cItemPedido := PadL(pcItemPedido, TamSX3( "NED_ITEMPV" )[1],"0")
	Local cCodPedido  := Padr(pcCodPedido, TamSX3("NEC_NUMPED")[1])

	dbSelectArea('NEC')
	dbSetOrder(retOrdem('NEC', 'NEC_FILIAL+NEC_NUMPED'))
	NEC->(DBGoTop())
	NEC->(dbSeek(xFilial('NEC')+cCodPedido))

	if xFilial('NEC')+cCodPedido == NEC->NEC_FILIAL+NEC->NEC_NUMPED

		dbSelectArea('NED')
		dbSetOrder(retOrdem('NED', 'NED_FILIAL+NED_CODIEB+NED_ITEMPV'))
		NED->(DBGoTop())
		NED->(dbSeek(xFilial('NED')+NEC->NEC_CODIGO+cItemPedido))

		if xFilial('NED')+NEC->NEC_CODIGO+cItemPedido == NED->(NED_FILIAL+NED_CODIEB+NED_ITEMPV)
			cId := NED->NED_FILIAL+NED->NED_CODIEB+NED->NED_ITEM
		endif

	endif

	Self:setSuccess(cId)
	RestArea(aArea)
return cId

/*/{Protheus.doc} sumByNegocioInsumo
Soma o quanto utilizado de insumo na IE
@type method
@version 12
@author jean.schulze
@since 06/02/2025
@param pcCodigoNegocio, character, codigo negocio
@param pcItemInsumo, character, codigo do item do insumo
@return numeric, quantidade utilizada
/*/
method sumByNegocioInsumo(pcCodigoNegocio as Character, pcItemInsumo as Character) as Numeric class agdInstrucaoEmbarqueItemRepository
	Local totalIEInsumo := 0
	Local aAreaAtual 	:= GetArea()
	Local cQuery		:= ""
	Local cAliQry 		:= GetNextAlias()
	Local oStatement 	:= nil

	cQuery := " SELECT SUM(NED_QTDIEB) AS QTDIEB "
	cQuery += " FROM "+ retSqlName('NED') +" NED "
	cQuery += " WHERE NED_FILIAL = ? "
	cQuery += 	" AND NED_CODBRT = ? "
	cQuery += 	" AND NED_ITEBRT = ? "
	cQuery += 	" AND NED_ITEMPV != ' ' "
	cQuery += 	" AND NED.D_E_L_E_T_ = '' "
	cQuery := ChangeQuery( cQuery )

	oStatement := FWPreparedStatement():New()
	oStatement:SetQuery(cQuery)
	oStatement:SetString(1, FWxFilial('NED'))
	oStatement:SetString(2, pcCodigoNegocio)
	oStatement:SetString(3, pcItemInsumo)
	cQuery := oStatement:GetFixQuery()
	dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAliQry, .T., .T.)

	If !(cAliQry)->(Eof())
		totalIEInsumo := (cAliQry)->QTDIEB
	EndIf
	(cAliQry)->(dbCloseArea())

	RestArea(aAreaAtual)

Return totalIEInsumo

/*/{Protheus.doc} updateRepository
Atualização do Repositório
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@param aFieldsSaveComand, array, lista de campos a serem salvos
@return variant, nil
/*/
method updateRepository(aFieldsSaveComand as Array) Class agdInstrucaoEmbarqueItemRepository
	Local nField		as Numeric

	dbSelectArea('NED')
	NED->(dbSetOrder(1))
	NED->(dbSeek(Self:cChaveIDRepository))

	If Self:cChaveIDRepository == NED->NED_FILIAL+NED->NED_CODIEB+NED->NED_ITEM
		RecLock("NED",.f.)
		For nField := 1 to len(aFieldsSaveComand)
			NED->( &(aFieldsSaveComand[nField][1]) ) :=  aFieldsSaveComand[nField][2]
		Next
		NED->(MsUnlock())
	endif

return nil

/*/{Protheus.doc} findByNegociacaoBarter
Retorna ID da negociacao barter
@type method
@version 12
@author lindembergson.pacheco
@since 20/02/2025
@param cIdNegocioBarter, character, id da negociacao barte
@return character, id do repositorio
/*/
method findByNegociacaoBarter(cIdNegocioBarter as Character) as Character class agdInstrucaoEmbarqueItemRepository
	Local aArea	   := GetArea()
	Local aAreaNEC   := NEC->(GetArea())
	Local cId      := ""

	dbSelectArea('NEC')
	NEC->(dbSetOrder(retOrdem('NEC', 'NEC_FILIAL+NEC_CODBRT')))
	NEC->(DBGoTop())
	IF NEC->(dbSeek(cIdNegocioBarter))
		if cIdNegocioBarter == NEC->(NEC_FILIAL+NEC_CODBRT)
			cId := NEC->NEC_FILIAL+NEC->NEC_CODBRT
		End
	ENDIF
	Self:setSuccess(cId)

	RestArea(aArea)
	RestArea(aAreaNEC)
return cId

/*/{Protheus.doc} sumByDevolucaoNegocioInsumo
Soma a quantidade devolvida para o item de insumo do barter
@type method
@version 12
@author claudineia.reinert
@since 12/03/2025
@param pcCodigoNegocio, character, codigo da negociação
@param pcItemInsumo, character, codigo do item do insumo na negociação
@return numeric, quantidade devolvida
/*/
method sumByDevolucaoNegocioInsumo(pcCodigoNegocio as Character, pcItemInsumo as Character) as Numeric class agdInstrucaoEmbarqueItemRepository
	Local nQtdDev 		:= 0
	Local cQuery	:= ""
	Local cAliQry 	:= GetNextAlias()

	cQuery := " SELECT SUM(NED_QTDDEV) AS QTDDEV "
	cQuery += " FROM "+ retSqlName('NED') +" NED "
	cQuery += " WHERE NED_FILIAL = ? "
	cQuery += 	" AND NED_CODBRT = ? "
	cQuery += 	" AND NED_ITEBRT = ? "
	cQuery += 	" AND NED.D_E_L_E_T_ = '' "
	cQuery := ChangeQuery( cQuery )

	oStatement := FWPreparedStatement():New()
	oStatement:SetQuery(cQuery)
	oStatement:SetString(1, FWxFilial('NED'))
	oStatement:SetString(2, pcCodigoNegocio)
	oStatement:SetString(3, pcItemInsumo)

	cQuery := oStatement:GetFixQuery()
	dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAliQry, .T., .T.)

	If !(cAliQry)->(Eof())
		nQtdDev := (cAliQry)->QTDDEV
	EndIf
	(cAliQry)->(dbCloseArea())

Return nQtdDev

/*/{Protheus.doc} sumByDevolucaoNFItemIE
Soma as devoluções referentes as NF de saida geradas para o item da instrução de embarque
@type method
@version 12
@author claudineia.reinert
@since 12/03/2025
@param pcCodigoNegocio, character, codigo da negociação
@param pcItemInsumo, character, codigo do item do insumo na negociação
@return numeric, quantidade devolvida
/*/
method sumByDevolucaoNFItemIE(pcCodigoIE as Character, pcItemIE as Character) as Numeric class agdInstrucaoEmbarqueItemRepository
	Local nQtdDev 		:= 0
	Local cQuery	:= ""
	Local cAliQry 	:= GetNextAlias()

	cQuery := " SELECT SUM(D1_QUANT) AS QTDDEV "
	cQuery += " FROM "+ retSqlName('NED') +" NED "
	cQuery += " INNER JOIN "+ retSqlName('SD2') +" SD2 "
	cQuery += 	" ON D2_FILIAL = NED_FILIAL "
	cQuery += 		" AND D2_DOC = NED_DOC "
	cQuery += 		" AND D2_SERIE = NED_SERIE "
	cQuery += 		" AND D2_CLIENTE = NED_CLIENT "
	cQuery += 		" AND D2_LOJA = NED_LOJA "
	cQuery += 		" AND D2_ITEM = NED_ITEMNF "
	cQuery += 		" AND D2_COD = NED_CODPRO "
	cQuery += 		" AND SD2.D_E_L_E_T_ = '' "
	cQuery += " INNER JOIN "+ retSqlName('SD1') +" SD1 "
	cQuery += 	" ON D1_FILIAL = NED_FILIAL "
	cQuery += 		" AND D1_NFORI = D2_DOC "
	cQuery += 		" AND D1_SERIORI = D2_SERIE "
	cQuery += 		" AND  D1_FORNECE = D2_CLIENTE "
	cQuery += 		" AND D1_LOJA = D2_LOJA "
	cQuery += 		" AND D1_ITEMORI = D2_ITEM "
	cQuery += 		" AND D1_COD = D2_COD "
	cQuery +=		" AND D1_TIPO = 'D' "
	cQuery += 		" AND SD1.D_E_L_E_T_ = '' "
	cQuery += " WHERE NED_FILIAL = ? "
	cQuery += 	" AND NED_CODIEB = ? "
	cQuery += 	" AND NED_ITEM = ? "
	cQuery += 	" AND NED.D_E_L_E_T_ = '' "
	cQuery := ChangeQuery( cQuery )

	oStatement := FWPreparedStatement():New()
	oStatement:SetQuery(cQuery)
	oStatement:SetString(1, FWxFilial('NED'))
	oStatement:SetString(2, pcCodigoIE)
	oStatement:SetString(3, pcItemIE)

	cQuery := oStatement:GetFixQuery()
	dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAliQry, .T., .T.)

	If !(cAliQry)->(Eof())
		nQtdDev := (cAliQry)->QTDDEV
	EndIf
	(cAliQry)->(dbCloseArea())

Return nQtdDev

/*/{Protheus.doc} findAllNotasFiscaisBarter
Retorna a lista das Notas Fiscais vinculadas ao Barter
@type method
@version 12
@author Gilson.Venturi
@since 31/03/2025
@param pcCodBarter, character, Codigo do Barter
@return array, lista de titulos
/*/
method findAllNotasFiscaisBarter(pcCodBarter as Character, pcCodVersao as Character,) as Array class agdInstrucaoEmbarqueItemRepository
	Local cQuery as Character
	Local aFields as Array

	aFields := {"NED_QTDIEB", "NED_QTDDEV", "F3_DTCANC", "F3_CODRSEF", "TOTINSTR"}

	cQuery := " SELECT "
	cQuery += " NED.NED_QTDIEB,"
	cQuery += " NED.NED_QTDDEV,"
	cQuery += " SF3.F3_DTCANC,"
	cQuery += " SF3.F3_CODRSEF,"
	cQuery += " ( SELECT SUM( NEB_QTDVEN ) "
	cQuery += "		FROM " + RetSqlName( "NEB" ) + " NEB"
	cQuery += "    WHERE NEB.NEB_FILIAL = '" + xFilial('NEB')  + "'"
	cQuery += "      AND NEB.NEB_CODBRT = NED.NED_CODBRT "
	cQuery += "      AND NEB.NEB_VERSAO = '" + pcCodVersao + "'"
	cQuery += "      AND NEB.D_E_L_E_T_ = ' ') AS TOTINSTR " //Total instrucao Negociada
	cQuery += " FROM " + RetSqlName( "NED" ) + " NED"

	cQuery += " INNER JOIN " + RetSqlName( "SD2" ) + " SD2"
	cQuery += "    ON SD2.D2_FILIAL  = '" + xFilial('SD2')  + "'"
	cQuery += "   AND SD2.D2_DOC     = NED.NED_DOC"
	cQuery += "   AND SD2.D2_SERIE   = NED.NED_SERIE"
	cQuery += "   AND SD2.D2_CLIENTE = NED.NED_CLIENT"
	cQuery += "   AND SD2.D2_LOJA    = NED.NED_LOJA"
	cQuery += "   AND SD2.D2_ITEM    = NED.NED_ITEMNF"
	cQuery += "   AND SD2.D_E_L_E_T_ = ' '"

	cQuery += " INNER JOIN " + RetSqlName( "SF2" ) + " SF2"
	cQuery += "    ON SF2.F2_FILIAL  = '" + xFilial('SF2')  + "'"
	cQuery += "   AND SF2.F2_DOC     = SD2.D2_DOC "
	cQuery += "   AND SF2.F2_SERIE   = SD2.D2_SERIE "
	cQuery += "   AND SF2.F2_CLIENTE = SD2.D2_CLIENTE "
	cQuery += "   AND SF2.F2_LOJA    = SD2.D2_LOJA "
	cQuery += "   AND SF2.D_E_L_E_T_ = ' ' "

	cQuery += " LEFT JOIN " + RetSqlName( "SF3" ) + " SF3"
	cQuery += "   ON SF3.F3_FILIAL  = '" + xFilial('SF3')  + "'"
	cQuery += "  AND SF3.F3_NFISCAL = SF2.F2_DOC "
	cQuery += "  AND SF3.F3_SERIE   = SF2.F2_SERIE "
	cQuery += "  AND SF3.F3_ESPECIE = SF2.F2_ESPECIE "
	cQuery += "  AND SF3.F3_CLIEFOR = SF2.F2_CLIENTE "
	cQuery += "  AND SF3.F3_LOJA    = SF2.F2_LOJA "
	cQuery += "  AND SF3.D_E_L_E_T_ = ' ' "

	cQuery += " WHERE NED.NED_FILIAL  = '" + xFilial('NED')  + "'"
	cQuery += "   AND NED.NED_CODBRT  = '" + pcCodBarter + "'"
	cQuery += "	  AND NED.D_E_L_E_T_  = ' '"

return Self:freeQueryToArray(cQuery, aFields)


/*/{Protheus.doc} getFields
Retorna os campos utilizados no repositório da NED
@type method
@version 12
@since 14/05/2025
@author rodrigo.nsoledade
@param lFieldsAdd, logical, adiciona campos extras (joins)
@return array
/*/
method getFields(lFieldsAdd) class agdInstrucaoEmbarqueItemRepository
	Local aFields := {}
	Default lFieldsAdd := .F.

	AAdd(aFields, {"filial",     "NED_FILIAL",  "C", .T.})
	AAdd(aFields, {"codigoIE",   "NED_CODIEB",  "C", .T.})
	AAdd(aFields, {"codNegocio", "NED_CODBRT",  "C", .T.})
	AAdd(aFields, {"itemNegoc",  "NED_ITEBRT",  "C", .T.})
	AAdd(aFields, {"itemIE",     "NED_ITEM",    "C", .T.})
	AAdd(aFields, {"itemNF",     "NED_ITEMNF",  "C", .T.})
	AAdd(aFields, {"itemPV",     "NED_ITEMPV",  "C", .T.})
	AAdd(aFields, {"doc",        "NED_DOC",     "C", .T.})
	AAdd(aFields, {"serie",      "NED_SERIE",   "C", .T.})
	AAdd(aFields, {"cliente",    "NED_CLIENT",  "C", .T.})
	AAdd(aFields, {"loja",       "NED_LOJA",    "C", .T.})
	AAdd(aFields, {"oper",       "NED_OPER",    "C", .T.})
	AAdd(aFields, {"tes",        "NED_TES",     "C", .T.})
	AAdd(aFields, {"codProd",    "NED_CODPRO",  "C", .T.})
	AAdd(aFields, {"um",         "NED_UM",      "C", .T.})
	AAdd(aFields, {"qtdIE",      "NED_QTDIEB",  "N", .T.})
	AAdd(aFields, {"qtdDev",     "NED_QTDDEV",  "N", .T.})
	AAdd(aFields, {"prcUnit",    "NED_PRCUNI",  "N", .T.})
	AAdd(aFields, {"valor",      "NED_VALOR",   "N", .T.})
	AAdd(aFields, {"frete",      "NED_FRETE",   "N", .T.})
	AAdd(aFields, {"entrega",    "NED_ENTREG",  "D", .T.})
	AAdd(aFields, {"lote",       "NED_LOTCTL",  "C", .T.})
	AAdd(aFields, {"local",      "NED_LOCAL",   "C", .T.})
	if lFieldsAdd
		AAdd(aFields, {"descProd",  "B1_DESC",     "C", .T.})
	endif
Return aFields

/*/{Protheus.doc} getQueryJoin
Retorna Array com os parametros usados no relacionamento entre tabelas
@type method
@version 12
@author rodrigo.nsoledade
@since 14/05/2025
@return array
/*/
method getQueryJoin() as Array class agdInstrucaoEmbarqueItemRepository
	Local aArrayJoin := {} as array
	Local cJoinSB1    as character

	cJoinSB1 := "SB1.D_E_L_E_T_ = ' ' AND SB1.B1_FILIAL = '" + FWxFilial("SB1") + "' AND SB1.B1_COD = NED.NED_CODPRO"

	AAdd(aArrayJoin, {"INNER", "SB1", cJoinSB1})

Return aArrayJoin
