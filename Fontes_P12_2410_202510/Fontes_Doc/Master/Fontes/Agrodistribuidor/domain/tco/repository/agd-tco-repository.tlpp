#INCLUDE 'totvs.ch'
#INCLUDE 'tlpp-core.th'
#INCLUDE 'fwmvcdef.ch'

namespace agd.TCORepository
using namespace agd.repositoryUtils

/*/{Protheus.doc} agdTCORepository
Repositorio do TCO
@type class
@version 12
@author jc.maldonado
@since 23/09/2025
/*/
class agdTCORepository FROM agdRepositoryUtils
	public Data cChaveIDRepository As Object //chave unica para updates

	public method new()

	public Method save()
	public Method findFeaturesDisponiveis() as array
	public Method isRepositoryChange()
	public method isSIGAAGDactive()        as logical
	public Method getPayloadFields()       as array
	public Method getFilterById()          as Array	
	public method getConfigPercentage()    as double
	public method getTotalActiveFeatures() as integer
	public method getTotalRegistrations()  as integer

	private method getFields()             as array
endClass

/*/{Protheus.doc} agdTCORepository::New
Construtor
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return object, Instancia da classe
/*/
method New(pcChaveIDRepository as character) class agdTCORepository
	Self:cChaveIDRepository := pcChaveIDRepository
	_Super:New('NE1', self:getFields())
return Self


/*/{Protheus.doc} save
Faz a inclusao do fechamento financeiro
@type method
@version 12
@author jc.maldonado
@since 03/06/2025
@param jPayload, json, payload recebido
/*/
method save(jPayload as json, lUpdate as logical) class agdTCORepository
	Local oModel           as object
	Local aFieldsCabecalho := Self:getFields()
	
	Default lUpdate := .F.

	if !lUpdate .or. (lUpdate .and. Self:existsById(Self:cChaveIDRepository))

		// Carrega o Model da rotina AGDA090
		oModel := FwLoadModel("AGDA090")
		oModel:SetOperation(IIF(lUpdate, MODEL_OPERATION_UPDATE, MODEL_OPERATION_INSERT))
		oModel:Activate()
	
		if Self:isSuccess()

			// Carregamento do cabeçalho
			aFields := Self:jsonToArrayFields(jPayload, aFieldsCabecalho)
			Self:setValueModel(oModel:GetModel("AGDA090_NE1"), aFields)
			    
			if Self:isSuccess()
				// Validação e commit
				If oModel:VldData()
					oModel:CommitData()
					Self:setSuccess(oModel:GetValue("AGDA090_NE1", "NE1_CODIGO"))
				Else
					Self:setErrorByErrorMessageModel(oModel, 404)
				EndIf
			Endif			

		endif

		oModel:DeActivate()
		oModel:Destroy()
		FWFreeObj(oModel)
	endif
Return

/*/{Protheus.doc} findFeaturesDisponiveis
Retorna a Lista das Features(DTOs) Disponiveis no AgroDistribuidor
@type method
@version 12
@author jean.schulze
@since 26/09/2025
@return variant, retorno
/*/
Method findFeaturesDisponiveis() Class agdTCORepository
	Local aFeatures := {}

	AAdd(aFeatures, "agdTCOBarterDTO()")
	AAdd(aFeatures, "agdTCOOriginacaoProtheusDTO()")
	AAdd(aFeatures, "agdTCOReceituarioDTO()")
	AAdd(aFeatures, "agdTCOComercializacaoTACDTO()")
	AAdd(aFeatures, "agdTCODTADTO()")
	AAdd(aFeatures, "agdTCOSolicitacaoExternaReceituarioDTO()")

Return aFeatures


/*/{Protheus.doc} getFields
Retorna a lista de campos do repositório
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@return variant, lista de campos
/*/
Method getFields() Class agdTCORepository
	Local aArrayFields := {}

	AAdd(aArrayFields, {'filial'    , 'NE1_FILIAL', 'C', .T.})
	AAdd(aArrayFields, {'codigo'    , 'NE1_CODIGO', 'C', .T.})
	AAdd(aArrayFields, {'status'    , 'NE1_STATUS', 'C', .T.})
	AAdd(aArrayFields, {'tipo'      , 'NE1_TIPO',   'C', .T.})
	AAdd(aArrayFields, {'ativo'     , 'NE1_ATIVO',  'C', .T.})
	AAdd(aArrayFields, {'data'      , 'NE1_DATA',   'D', .T.})
	AAdd(aArrayFields, {'hora'      , 'NE1_HORA',   'C', .T.})
	AAdd(aArrayFields, {'usuario'   , 'NE1_NOMUSU', 'C', .T.})
	AAdd(aArrayFields, {'detalhe'   , 'NE1_MSGOBS', 'C', .F.})
Return aArrayFields

/*/{Protheus.doc} getFilterById
Obtem Filtro para Get By ID
@type method
@version 12
@author jean.schulze
@since 09/05/2025
@param cCodigo, character, codigo Negocio
@param cVersao, character, versao negocio
@return array, lista de filtro
/*/
Method getFilterById(cCodigo) as Array Class agdTCORepository
    Local aFilterId := {}

	AAdd(aFilterId, {'NE1_CODIGO', cCodigo}) 

return aFilterId

/*/{Protheus.doc} isRepositoryChange
Verifica se modelo permite edicao
@type method
@version 12
@author carlos.augusto
@since 25/07/2025
@return logical, modelo permite não edicao
/*/
Method isRepositoryChange(cChaveIdFechamentoFinanceiro as character) Class agdTCORepository
	
	If Self:existsById(Self:cChaveIDRepository)
		Self:buildModelCheck("AGDA090")
	Endif

return nil

/*/{Protheus.doc} getPayloadFields
Retorna todos os campos disponiveis para o payload
@type method
@version 12
@author jean.schulze
@since 11/08/2025
@return array, array de campos
/*/
Method getPayloadFields() as Array Class agdTCORepository
	Local aFields:= {}
	
	AEval(Self:getFields(),{|field| AAdd(aFields, field)})

Return aFields

/*/{Protheus.doc} agdTCORepository::isSIGAAGDactive
Retorna se o modulo sigaagd está ativo
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return logical, .T. ativo, .F. inativo
/*/
method isSIGAAGDactive() class agdTCORepository as logical
return SUPERGETMV("MV_SIGAAGD", .f., .f.)

/*/{Protheus.doc} agdTCORepository::getConfigPercentage
Retorna a porcentagem de features configuradas
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return double, Porcentagem configurada
/*/
method getConfigPercentage() class agdTCORepository as double
	local cSQL            as character
    local cAlias          as character
    local aBindParams     as array
    local nPercent        as double
    local nActiveFeatures as numeric

    aBindParams := {}
    cSQL := "SELECT COUNT(NE1_CODIGO) TOTAL"
	cSQL += " FROM " + retSqlName("NE1")
	cSQL += " WHERE NE1_FILIAL = ? " ; aAdd(aBindParams, FWxFilial("NE1"))
	cSQL += " AND NE1_ATIVO = ?"     ; aAdd(aBindParams, '1') //'1'=ativo
	cSQL += " AND NE1_STATUS = ?"    ; aAdd(aBindParams, '2') //'2'=finalizado
	cSQL += " AND D_E_L_E_T_ != '*'"
	cSQL := changeQuery(cSQL)
    
	cAlias := GetNextAlias()
	dbUseArea(.T., "TOPCONN", TcGenQry2(,, cSQL, aBindParams), cAlias, .F., .T.)
	nConfiguredFeatures := (cAlias)->TOTAL
    (cAlias)->(DBcloseArea())

    nActiveFeatures := ::getTotalActiveFeatures()

    nPercent := Round((nConfiguredFeatures / nActiveFeatures) * 100, 2)
return nPercent

/*/{Protheus.doc} agdTCORepository::getTotalActiveFeatures
Retorna o total de features ativas
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return integer, Total features ativas
/*/
method getTotalActiveFeatures() class agdTCORepository as integer
	local cSQL        as character
	local aBindParams as array
	local cAlias      as character
	local nTotal      as integer

	aBindParams := {}

	cSQL := "SELECT COUNT(NE1_CODIGO) TOTAL"
	cSQL += " FROM " + retSqlName("NE1")
	cSQL += " WHERE NE1_FILIAL = ? " ; aAdd(aBindParams, FWxFilial("NE1"))
	cSQL += " AND NE1_ATIVO = ? "    ; aAdd(aBindParams, '1') //'1'=ativo
	cSQL += " AND D_E_L_E_T_ != '*'"
	cSQL := changeQuery(cSQL)

	cAlias := GetNextAlias()
	dbUseArea(.T., "TOPCONN", TcGenQry2(,, cSQL, aBindParams), cAlias, .F., .T.)

	nTotal := (cAlias)->TOTAL

	(cAlias)->(DBcloseArea())
return nTotal

/*/{Protheus.doc} agdTCORepository::getTotalRegistrations
Retorna o total de registros cadastrados
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return integer, Total registros cadastrados
/*/
method getTotalRegistrations() class agdTCORepository as integer
	local integer as numeric //Inicialmente retornara 0
return integer
