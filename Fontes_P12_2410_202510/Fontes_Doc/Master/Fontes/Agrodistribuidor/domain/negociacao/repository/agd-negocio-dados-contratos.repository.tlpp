#INCLUDE "totvs.ch"
#INCLUDE 'tlpp-core.th'
#INCLUDE "FWMVCDEF.CH"

namespace agd.dadosContratosNegocioRepository
using namespace agd.repositoryUtils /*shortCurt de funcao*/

/*/{Protheus.doc} agdDadosContratosNegocioRepository
Repositório de dados do contrato da Negociações Barter
@type class
@version 12
@author claudineia.reinert
@since 19/05/2025
/*/
class agdDadosContratosNegocioRepository FROM agdRepositoryUtils
	public Data cChaveIDRepository As Object //chave unica para updates

	public Method New()
	public Method save()
	public Method updateRepository()
	public Method delete()
	public Method getFilterById()	as Array
	public Method getFields() as array
	
	private Method getJoins()  as array

endclass

/*/{Protheus.doc} new
Constructor NEE
@type method
@version 12
@author claudineia.reinert
@since 19/05/2025
@return variant, instância
/*/
Method New(pcChaveIDRepository as character) Class agdDadosContratosNegocioRepository
	Self:cChaveIDRepository := pcChaveIDRepository
	_Super:New('NEE', Self:getFields(.t.), Self:getJoins())
Return Self

/*/{Protheus.doc} save
Salva um novo registro de dados do contrato para o negócio
@type method
@version 12
@author claudineia.reinert
@since 19/05/2025
@param jCmdRegistrar, json, comando
@return variant, nil
/*/
Method save(jCmdRegistrar)  Class agdDadosContratosNegocioRepository
	Local nField		as Numeric
	Local cError        as Character
	Local oModel 		as object

	oModel := FwLoadModel("AGDA011")
	oModel:SetOperation(MODEL_OPERATION_INSERT)
	oModel:Activate()

	aFields := self:getFields()

	For nField := 1 to len(aFields)
		lExists := jCmdRegistrar:hasProperty(lower(aFields[nField][1]))
		cValueProp := alltrim(jCmdRegistrar:GetJsonText(lower(aFields[nField][1])))

		if cValueProp == 'null'
			cValueProp := ""
		endif

		if aFields[nField][3] == 'N'
			cValueProp := Val(cValueProp)
		elseif aFields[nField][3] == 'D'
			cValueProp := Ctod(cValueProp)
		endif

		if lExists .and.!Empty(cValueProp)
			oModel:LoadValue("AGDA011_NEE", aFields[nField][2] , cValueProp)
		endif
	Next

	If oModel:VldData()
		oModel:CommitData()
		Self:setSuccess(oModel:GetValue("AGDA011_NEE", "NEE_CODBRT"))
	Else
		cError := cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + ' - '
		cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID ]) 	    + ' - '
		cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
		Self:setError(cError, 400)
	EndIf

	oModel:DeActivate()
	oModel:Destroy()
	oModel := NIL
	FreeObj(oModel)
Return nil

/*/{Protheus.doc} updateRepository
Atualiza Repositório
@type method
@version 12
@author claudineia.reinert
@since 19/05/2025
@param aFieldsSaveComand, array, lista de campos
@return variant, nil
/*/
method updateRepository(aFieldsSaveComand as Array) Class agdDadosContratosNegocioRepository
	Local nField		:= 0
	Local aArea			:= GetArea()
	Local oModel 		:= nil
	Local cError 		:= ""

	dbSelectArea('NEE')
	NEE->(dbSetOrder(1))
	NEE->(dbSeek(Self:cChaveIDRepository))

	If Self:cChaveIDRepository  == NEE->(NEE_FILIAL+NEE_CODBRT+NEE_VERSAO+NEE_ITEM)
		oModel := FwLoadModel("AGDA011")
		oModel:SetOperation(MODEL_OPERATION_UPDATE)
		oModel:Activate()

		For nField := 1 to len(aFieldsSaveComand)
			if oModel:HasField("AGDA011_NEE", aFieldsSaveComand[nField][1] )
				oModel:SetValue("AGDA011_NEE", aFieldsSaveComand[nField][1] , aFieldsSaveComand[nField][2])
			endif
		Next

		If oModel:VldData()
			oModel:CommitData()
			Self:setSuccess(Self:cChaveIDRepository)
		Else
			cError := cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + ' - '
			cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID ]) 	    + ' - '
			cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
			Self:setError(cError, 400)
		EndIf

	Else
		Self:setDefaultError(404)
	endif

	RestArea(aArea)

return nil

/*/{Protheus.doc} delete
Metodo deleção do registro da Tabela NEE
@type method
@version P12 
@author claudineia.reinert
@since 19/05/2025
/*/
Method delete() Class agdDadosContratosNegocioRepository
	Local oModel:= nil
	Local aAreaNEE := NEE->(GetArea())

	dbSelectArea('NEE')
	NEE->(dbSetOrder(1)) //NEE_FILIAL+NEE_CODBRT+NEE_VERSAO+NEE_ITEM
	If NEE->(dbSeek(self:cChaveIDRepository))

		oModel := FwLoadModel("AGDA011")
		oModel:SetOperation(MODEL_OPERATION_DELETE)
		oModel:Activate()
		If oModel:VldData()
			oModel:CommitData()
			Self:setSuccess(self:cChaveIDRepository)
		Else
			cError := cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + ' - '
			cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID ]) 	    + ' - '
			cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
			Self:setError(cError, 400)
		EndIf

		oModel:DeActivate()
		oModel:Destroy()
		oModel := NIL
		FreeObj(oModel)

	Else
		Self:setDefaultError(404)
	endif
	RestArea(aAreaNEE)
Return nil

/*/{Protheus.doc} getFilterById
Obtem o filtro para getById
@type method
@version 12
@author claudineia.reinert
@since 19/05/2025
@param cCodigoNegocio, character, codigo da negociação
@param cVersaoNegocio, character, versao da negociação
@param cItemContrato, character, item do contrato da negociação
@return array, lista de filtro
/*/
Method getFilterById(cCodigoNegocio as character, cVersaoNegocio as character, cItemContrato as character) as Array Class agdDadosContratosNegocioRepository
	Local aFilterId := {}

	Default cItemContrato := ""

	AAdd(aFilterId, {'NEE_CODBRT', cCodigoNegocio})
	AAdd(aFilterId, {'NEE_VERSAO', cVersaoNegocio})

	if !empty(cItemContrato)
		AAdd(aFilterId, {'NEE_ITEM',   cItemContrato})
	endif

return aFilterId

/*/{Protheus.doc} getFields
Retorna a lista de campos do repositório
@type method
@version 12
@author claudineia.reinert
@since 19/05/2025
@return variant, lista de campos
/*/
Method getFields(lFieldsAdd as logical) as array Class agdDadosContratosNegocioRepository
	Local aArrayFields := {}

	Default lFieldsAdd := .F.

	AAdd(aArrayFields, {'filial'                 , 'NEE_FILIAL'	, 'C', .T.})
	AAdd(aArrayFields, {'codigo'                 , 'NEE_CODBRT'	, 'C', .T.})
	AAdd(aArrayFields, {'versao'                 , 'NEE_VERSAO'	, 'C', .T.})
	AAdd(aArrayFields, {'item'	                 , 'NEE_ITEM'	, 'C', .T.})
	AAdd(aArrayFields, {'propriedade'       	 , 'NEE_CDPROP'	, 'C', .T.})
	AAdd(aArrayFields, {'lojaPropriedade'        , 'NEE_LJPROP'	, 'C', .T.})
	AAdd(aArrayFields, {'quantidade'	         , 'NEE_QTDSAC'	, 'N', .T.})
	AAdd(aArrayFields, {'quantidadeConvertUM'    , 'NEE_QTCONV' , 'N', .T.})
	AAdd(aArrayFields, {'local'                  , 'NEE_LOCAL'	, 'C', .T.})
	AAdd(aArrayFields, {'operacaoFiscal'         , 'NEE_OPEFIS'	, 'C', .T.})
	AAdd(aArrayFields, {'tes'                    , 'NEE_TESCTR'	, 'C', .T.})
	AAdd(aArrayFields, {'dataInicioEntrega'      , 'NEE_INIENT'	, 'D', .T.})
	AAdd(aArrayFields, {'dataFimEntrega'         , 'NEE_FIMENT' , 'D', .T.})
	AAdd(aArrayFields, {'CondicaoPagamento'      , 'NEE_CONDPG'	, 'C', .T.})
	AAdd(aArrayFields, {'diasDePagamento'        , 'NEE_DIASPG'	, 'N', .T.})
	AAdd(aArrayFields, {'tipoFrete'              , 'NEE_TPFRET' , 'C', .T.})
	AAdd(aArrayFields, {'tabelaClassificacao'    , 'NEE_TABELA'	, 'C', .T.})
	AAdd(aArrayFields, {'naturezaFinanceira'     , 'NEE_NATURE'	, 'C', .T.})
	AAdd(aArrayFields, {'codigoSafra'            , 'NEE_CODSAF'	, 'C', .T.})
	AAdd(aArrayFields, {'codigoFinalidade'       , 'NEE_CODFIN'	, 'C', .T.})
	AAdd(aArrayFields, {'descricaopropriedade'   , 'NEE_NMPROP' , 'C', .T.})
	AAdd(aArrayFields, {'descricaocondpgto'      , 'NEE_DESPG'  , 'C', .T.})

	If lFieldsAdd
		AAdd(aArrayFields, {'descricaotes'           , 'F4_TEXTO'  , 'C', .T.})
		AAdd(aArrayFields, {'descricaooperacaofiscal', 'X5_DESCRI' , 'C', .T.})
		AAdd(aArrayFields, {'descricaotabelaclass'   , 'NNI_DESCRI', 'C', .T.})
		AAdd(aArrayFields, {'descricaonatureza'      , 'ED_DESCRIC', 'C', .T.})
		AAdd(aArrayFields, {'descricaosafra'         , 'NJU_DESCRI', 'C', .T.})
	EndIf
Return aArrayFields

/*/{Protheus.doc} agdDadosContratosNegocioRepository::getJoins
Retorna uma array com os joins do repositório
@type method
@version 12
@author jc.maldonado
@since 03/09/2025
@return array, joins
/*/
Method getJoins() as array class agdDadosContratosNegocioRepository
	local aJoin as array

	aJoin := {;
		{"LEFT", "SF4", ::getWhereDefault("SF4.F4_CODIGO = NEE.NEE_TESCTR", "SF4")},;
		{"LEFT", "SX5", ::getWhereDefault("SX5.X5_TABELA = 'DJ' AND SX5.X5_CHAVE = NEE.NEE_OPEFIS", "SX5")},;
		{'LEFT', 'NNI', ::getWhereDefault('NNI.NNI_CODIGO = NEE.NEE_TABELA', 'NNI')},;
		{'LEFT', 'NJU', ::getWhereDefault('NJU.NJU_CODSAF = NEE.NEE_CODSAF', 'NJU')},;
		{"LEFT", "SED", ::getWhereDefault("SED.ED_CODIGO = NEE.NEE_NATURE", "SED")};
		}
Return aJoin
