#INCLUDE "totvs.ch"
#include 'tlpp-core.th'
#include 'agd.negocio.totalizador.service.ch'

namespace agd.negocioTotalizadorService
using namespace agd.utilsService
using namespace agd.negocioRepository
using namespace agd.negocioService 
using namespace agd.repositoryUtils 

/*/{Protheus.doc} agdNegocioTotalizadorService
Serviço de Totalização da Negociação - Barter
@type class
@version 12
@author Gilson.Venturi
@since 17/06/2025
/*/ 
class agdNegocioTotalizadorService FROM agdUtilsService
	public Data jSonDados  As jSon

	public method new()

	public method getMargemLucro() as Numeric
	public method getTotalVenda() as Numeric
	public method getTotalVendaOMoeda() as Numeric
	public method getTotalParidade() as Numeric
	public method getTotalImposto() as Numeric
	public method getTotais() as json
	public method totalizadores()

endclass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author Gilson.Venturi
@since 17/06/2025
@param pjSonDados, jSon, jSon do modelo da negociação
/*/
method new(pjSonDados as json) class agdNegocioTotalizadorService
	Self:jSonDados := JsonObject():new()
	Self:jSonDados:fromJson(pjSonDados:toJson())
	_Super:New()
return Self

/*/{Protheus.doc} getMargemLucro
Retorna valor do cálculo da Margem de Lucro
@type method
@version 12
@author Gilson.Venturi
@since 17/06/2025
@return variant, Valor Lucro
/*/
method getMargemLucro() class agdNegocioTotalizadorService
	Local nVlrCusto		:= 0
	Local nVlrVenda		:= 0
	Local nVlrLucro		:= 0
	Local nI			:= 0
	Local cNeaFormul	:= ''
	Local cFormula		:= ''
	Local lFUser		:= .F.
	Local oField		:= {}

	cNeaFormul := Self:jSonDados:getJsonObject("formula")

	If !Empty(cNeaFormul)
		SM4->(DbSetOrder(1))
		If SM4->(MsSeek(xFilial("SM4") +  cNeaFormul))
			cFormula := AllTrim(SM4->M4_FORMULA)
		else
			Self:setError(STR0001, 403) //"Fórmula não cadastrada."
			return 0
		EndIf
	EndIf

	if !Empty(cFormula)
		IF "U_" $ UPPER(cFormula) //se a formula for User Function
			lFUser := .T.
		else
			Self:setError(STR0002, 403) //"Fórmula precisa ser User Function."
			return 0
		ENDIF
	endif

	For nI := 1 To Len(Self:jSonDados["listInsumos"])
		oField := Self:jSonDados["listInsumos"][nI]
		if !Empty(oField:getJsonObject("codigoProduto"))
			nVlrCusto	+= oField:getJsonObject("precoCusto")
			nVlrVenda	+= oField:getJsonObject("precoFinal")
		endif
	Next

	IF lFUser
		nVlrLucro := &(cFormula)
		IF VALTYPE(nVlrLucro) != "N"
			Self:setError(STR0003, 403) //"O retorno da função deve ser numérico."
			return 0
		Endif
	else
		nVlrLucro := ROUND((1 - (nVlrCusto / nVlrVenda) ) * 100,2)
	Endif

return nVlrLucro

/*/{Protheus.doc} getTotalVenda
Retorna valor Total de Vendas
@type method
@version 12
@author Gilson.Venturi
@since 17/06/2025
@return variant, Valor Lucro
/*/
method getTotalVenda() class agdNegocioTotalizadorService
	Local nTotal	:= 0
	Local nI		:= 0

	For nI := 1 To Len(Self:jSonDados["listInsumos"])
		oField := Self:jSonDados["listInsumos"][nI]
		if !Empty(oField:getJsonObject("codigoProduto"))
			nTotal	+= oField:getJsonObject("valorTotal")
			nTotal	+= oField:getJsonObject("frete")
		endif
	Next

return nTotal

/*/{Protheus.doc} getTotalVendaOMoeda
Retorna valor Total de Vendas em Outra Moeda
@type method
@version 12
@author Gilson.Venturi
@since 17/06/2025
@return variant, Valor Lucro
/*/
method getTotalVendaOMoeda() class agdNegocioTotalizadorService
	Local nTotal	:= 0
	Local nI		:= 0

	If Self:jSonDados:getJsonObject("moedaRelacaoTroca") != 1
		For nI := 1 To Len(Self:jSonDados["listInsumos"])
			oField := Self:jSonDados["listInsumos"][nI]
			if !Empty(oField:getJsonObject("codigoProduto"))
				nTotal	+= oField:getJsonObject("valorTotalOM")
				nTotal	+= oField:getJsonObject("freteOM")
			endif
		Next
	Endif

return nTotal

/*/{Protheus.doc} getTotalParidade
Retorna valor Total da Paridade
@type method
@version 12
@author Gilson.Venturi
@since 17/06/2025
@return variant, Valor Lucro
/*/
method getTotalParidade() class agdNegocioTotalizadorService
	Local nTotal	:= 0
	Local nI		:= 0

	For nI := 1 To Len(Self:jSonDados["listInsumos"])
		oField := Self:jSonDados["listInsumos"][nI]
		if !Empty(oField:getJsonObject("codigoProduto"))
			nTotal	+= oField:getJsonObject("paridade")
		endif
	Next

return nTotal

/*/{Protheus.doc} getTotalImposto
Retorna valor total do imposto retido da negociação
@type method
@version 12
@author Gilson.Venturi
@since 17/06/2025
@return variant, Valor Lucro
/*/
method getTotalImposto() class agdNegocioTotalizadorService
	Local nTotal           := 0
	Local nI               := 0
	Local cCodigoNegocio   := Self:jSonDados:getJsonObject("codigo")

	oRepository := agdNegocioRepository():New() 
	If oRepository:existsByCode(cCodigoNegocio)
		aImpostos := AGDA10TIMP(,,.T.,cCodigoNegocio)

		For nI := 1 To Len(aImpostos)
			nTotal	+= aImpostos[nI]:getJsonObject("valortributofinal")
		Next
	Endif

return nTotal


/*/{Protheus.doc} totalizadores
totalizadores Negócio
@type method
@version 12
@author lindembergson.pacheco
@since 12/06/2025
@param Self:jSonDados, object, json
@return variant, nil
/*/
method totalizadores() class agdNegocioTotalizadorService

	Local oNegocioService		As Object
	Local oNegocioRepository 	As Object
	Local nX 					:= 0
	Local aNEB 					:= {}
	Local aJNEA					:= {}
	Local aJNEB					:= {}
	Local oNEA 					As Object
	Local oNEB 					As Object
	Local nTotal 				:= 0
	Local nTotaParidade 		:= 0
	Local nPrCusto				:= 0
	Local nVlrParid				:= 0
	Local nVlrCusto				:= 0
	Local nVlrVenda				:= 0
	Local nVlrLucro				:= 0
	Local nFrete 	:= 0
	Local cFormula				:= ""
	Local lFUser				:= .F.

	oNegocioService		:= agdNegocioService():new()
	oNegocioRepository 	:= agdNegocioRepository():New()

	aJNEA := oNegocioRepository:getFields()
	aJNEB := oNegocioRepository:getFieldsRelationship("listInsumos")

	oNEA := agdRepositoryUtils():New("NEA", aJNEA, , )
	oNEB := agdRepositoryUtils():New("NEB", aJNEB, , )

	//verifica se existe objeto formula
	if Self:jSonDados:hasProperty(oNEA:getJsonFieldName('NEA_FORMLU'))

		If !Empty(Self:jSonDados[oNEA:getJsonFieldName('NEA_FORMLU')])
			SM4->(DbSetOrder(1))
			If SM4->(MsSeek(xFilial("SM4") +  Self:jSonDados[oNEA:getJsonFieldName('NEA_FORMLU')]))
				cFormula := AllTrim(SM4->M4_FORMULA)
			else
				Self:setError(STR0001, 403) //"Fórmula não cadastrada."
				lRet	:= .F.
				return lRet
			EndIf
		EndIf

		if !Empty(cFormula)
			IF "U_" $ UPPER(cFormula) //se a formula for User Function
				lFUser := .T.
			else
				Self:setError(STR0002, 403) //"Fórmula precisa ser User Function."
				lRet	:= .F.
				return lRet
			ENDIF
		endif

	endif

	if Self:jSonDados:hasProperty('listinsumos')
		aNEB := Self:jSonDados['listinsumos']

		for nX := 1 to len(aNEB)

			//tratamento para retornor de valores por item
			if aNEB[nX]:hasProperty(oNEB:getJsonFieldName('NEB_CODPRO'))

				nPrCusto 	:= 0
				nVlrParid 	:= 0
				nFrete		:= 0

				if aNEB[nX]:hasProperty(oNEB:getJsonFieldName('NEB_FRETE'))
					nFrete := aNEB[nX][oNEB:getJsonFieldName('NEB_FRETE')]
				endif

				//atribuindo preco de custo - AGD10PRCST
				if aNEB[nX]:hasProperty(oNEB:getJsonFieldName('NEB_LOCAL'))
					nPrCusto := oNegocioService:getPrecoCustoProdut(aNEB[nX][oNEB:getJsonFieldName('NEB_CODPRO')], aNEB[nX][oNEB:getJsonFieldName('NEB_LOCAL')])
					aNEB[nX][oNEB:getJsonFieldName('NEB_PRCCUS')] := nPrCusto[1]
				else
					aNEB[nX][oNEB:getJsonFieldName('NEB_PRCCUS')] := nPrCusto
				endif

				//valores para calculo de margem de lucro
				if aNEB[nX]:hasProperty(oNEB:getJsonFieldName('NEB_PRCUNI'))
					nVlrVenda += aNEB[nX][oNEB:getJsonFieldName('NEB_PRCUNI')]
				endif

				nVlrCusto += aNEB[nX][oNEB:getJsonFieldName('NEB_PRCCUS')]

				//atribuindo paridade - FATNEBPARID
				if aNEB[nX]:hasProperty(oNEB:getJsonFieldName('NEB_VALOR'))
					if aNEB[nX][oNEB:getJsonFieldName('NEB_VALOR')] > 0
						if Self:jSonDados:hasProperty(oNEA:getJsonFieldName('NEA_VLRRTL'))
							nVlrParid := (aNEB[nX][oNEB:getJsonFieldName('NEB_VALOR')] + nFrete) /  Self:jSonDados[oNEA:getJsonFieldName('NEA_VLRRTL')]
						endif
						aNEB[nX][oNEB:getJsonFieldName('NEB_PARID')] := ROUND(nVlrParid,2)
					endif
				endif

			endif

			if aNEB[nX]:hasProperty(oNEB:getJsonFieldName('NEB_VALOR'))
				nTotal += aNEB[nX][oNEB:getJsonFieldName('NEB_VALOR')]
			endif

			if aNEB[nX]:hasProperty(oNEB:getJsonFieldName('NEB_PARID'))
				nTotaParidade += aNEB[nX][oNEB:getJsonFieldName('NEB_PARID')]
			endif

		next

	endif

	IF lFUser
		nVlrLucro := &(cFormula)
		IF VALTYPE(nVlrLucro) != "N"
			Self:setError(STR0003, 403) //"O retorno da função deve ser numérico."
			lRet	:= .F.
			return lRet
		ENDIF
	else
		nVlrLucro := ROUND((1 - (nVlrCusto / nVlrVenda) ) * 100,2)
	ENDIF

	Self:jSonDados[oNEA:getJsonFieldName('NEA_MRGLUC')] := nVlrLucro

	Self:jSonDados['totalvenda'] 	:=  ROUND(nTotal,2)
	Self:jSonDados['totalparidade']	:=  ROUND(nTotaParidade,2)

	Self:setSuccess(Self:jSonDados)

	FreeObj(Self:jSonDados)

Return nil

/*/{Protheus.doc} agdNegocioTotalizadorService::getTotais
Retorna um objeto json com os totais do negocio.
@type method
@version 12
@author jc.maldonado
@since 26/08/2025
@return json, objeto json de retorno
/*/
method getTotais() as json class agdNegocioTotalizadorService
	Local jTotais as json

	jTotais := JsonObject():new()
	jTotais["margemlucro"]          := ::getMargemLucro()
	jTotais["totalvenda"]           := ::getTotalVenda()
	jTotais["totalvendaoutramoeda"] := ::getTotalVendaOMoeda()
	jTotais["totalparidade"]        := ::getTotalParidade()
	jTotais["totalimposto"]         := ::getTotalImposto()
return jTotais
