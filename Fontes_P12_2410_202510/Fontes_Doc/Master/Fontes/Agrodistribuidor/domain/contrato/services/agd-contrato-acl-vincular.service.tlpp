#Include "agd.contrato.acl.vincular.service.ch"
#include "totvs.ch"
#INCLUDE "FWMVCDEF.CH"
#include 'tlpp-core.th'

namespace agd.ContratoAclVincularService

using namespace agd.utilsService
using namespace agd.contratoAclRepository
using namespace agd.movimentoContratoAclRepository
using namespace agd.dadosContratosNegocioRepository
using namespace agd.negocioService
using namespace agd.contratoAclService
using namespace agd.tituloFinanceiroRepository
using namespace agd.idService
using namespace agd.contratovincularrepository

/*/{Protheus.doc} agdContratoOriginacaoProtheusService
Servico para ajuste da entidade de ACL de Contratos do Barter a Vincular
@type class
@version P12  
@author scheronlini.martins
@since 25/04/2025
/*/
class agdContratoAclVincularService FROM agdUtilsService
	public Data cChaveIDACLContrato   As Character

	public method new()

	public method vincularContratoNEF()
	public method vincularContratoNEE()
	public method vincularContratoNEI()
	public method desvincularContrato()
	public method getQuantidadePermitidaAVincularNEE() as Numeric
	public method getQuantidadeConvertidaPermitidaAVincularNEE() as Numeric
	public method getSaldoAVincularNEE() as Numeric
	public method getSaldoQuantidadeConvertidaAVincularNEE() as Numeric
	public method validarContrato()
	public method vincular()
	public method listar()
	public method listarPorContrato()

	private method buildFilter() as character

endclass

/*/{Protheus.doc} agdContratoAclVincularService::new
Construtor
@type method
@version P12  
@author scheronlini.martins
@since 30/04/2025
@return variant, return_instancia
/*/
method new(pcChaveIDACLContrato as Character) class agdContratoAclVincularService
	Self:cChaveIDACLContrato := pcChaveIDACLContrato
	_Super:new("NEF")
return self

/*/{Protheus.doc} agdContratoAclVincularService::vincularContratoNEF
Faz o vinculo do contrato gravando na tabela NEF
@type method
@version P12  
@author scheronlini.martins
@since 30/04/2025
@param aFieldsNEF, array, lista de campos e valores da tabela NEF
/*/
method vincularContratoNEF(aFieldsNEF as Array) class agdContratoAclVincularService
	Local ocontratoAclRepository 	as Object

	ocontratoAclRepository:= agdContratoAclRepository():New()

	ocontratoAclRepository:save(aFieldsNEF)

	Self:setFullResponse(ocontratoAclRepository)

return

/*/{Protheus.doc} agdContratoAclVincularService::vincularContratoNEE
Faz o vinculo do contrato gravando na tabela NEE
@type method
@version P12  
@author scheronlini.martins
@since 30/04/2025
@param aFieldsNEE, array, lista de campos e valores da tabela NEE
@return variant, return_nil
/*/
method vincularContratoNEE(aFieldsNEE as Array) class agdContratoAclVincularService
	Local nField 		as Integer
	Local lRet			as Logical
	Local oModel 		as Object
	Local oModelNEE 	as Object

	if len(aFieldsNEE) > 0
		oModel := FWLoadModel( 'AGDA011' )
		oModel:SetOperation( 3 ) //3 – Inclusão / 4 – Alteração / 5 - Exclusão
		If lRet := oModel:Activate()
			oModelNEE := oModel:GetModel( 'AGDA011_NEE' )
			If !oModelNEE:IsEmpty() //Tem linhas informadas
				oModelNEE:AddLine()
			EndIf
			For nField := 1 to len(aFieldsNEE)
				If !oModelNEE:SetValue( aFieldsNEE[nField][1] , aFieldsNEE[nField][2])
					lRet := .F.
					Exit
				EndIf
			Next
			If lRet
				lRet := oModel:VldData()
				If lRet
					lRet := oModel:CommitData()
				EndIf
			EndIf
		EndIf
		If lRet
			Self:setSuccess()
		Else
			Self:setErrorByErrorMessageModel(oModel)
		EndIf
		oModel:DeActivate()
		FreeObj(oModel)
	EndIf

return nil

/*/{Protheus.doc} agdContratoAclVincularService::vincularContratoNEI
Faz o vinculo do contrato gravando na tabela NEI
@type method
@version P12  
@author scheronlini.martins
@since 30/04/2025
@param aFieldsNEI, array, lista de campos e valores da tabela NEE
/*/
method vincularContratoNEI(aFieldsNEI as Array) class agdContratoAclVincularService
	Local oMovimentocontratoAclRepository 	as Object

	oMovimentocontratoAclRepository:= agdMovimentoContratoAclRepository():New()

	oMovimentocontratoAclRepository:save(aFieldsNEI)

	Self:setFullResponse(oMovimentocontratoAclRepository)

return

/*/{Protheus.doc} agdContratoAclVincularService::desvincularContrato
Desvincula um registro ACL de contratos de compra com a negociação 
@type method
@version  P12
@author claudineia.reinert
@since 19/05/2025
@param lUsaTela, logical, se usa tela via protehus, Default .T.
@param cMsgObs, character, mensagem de observação quando não usa a tela via Protheus
/*/
method desvincularContrato(lUsaTela as Logical, cMsgObs as Character) class agdContratoAclVincularService
	Local oNegocioService 	as Object
	Local oContratoAclRepository 	as Object
	Local oDadosContratosNegocioRepository 	as Object
	Local oMovimentocontratoAclRepository 	as Object
	Local oTituloFinanceiroRepository 	as Object
	Local nContVinculos			 	as Integer
	Local cUltVersao			 	as Character
	//Local nQuant			 		as Numeric

	Default lUsaTela := .T.
	Default cMsgObs := ""

	dbSelectArea('NEF')
	NEF->(dbSetOrder(1)) //NEF_FILIAL+NEF_IDCTR+NEF_CODBRT++NEF_ITECTR
	If NEF->(dbSeek(Self:cChaveIDACLContrato)) .and. Self:cChaveIDACLContrato == NEF->(NEF_FILIAL+NEF_IDCTR+NEF_CODBRT+NEF_ITECTR)

		oTituloFinanceiroRepository := agdTituloFinanceiroRepository():New()

		If NEF->NEF_VINCTR != "1"
			Self:setError(STR0001) //"Este contrato foi gerado pela negociação, não é possível ser desvinculado. Somente contratos vinculados podem ser desvinculados."
			Return nil
		elseIf oTituloFinanceiroRepository:existByIdContratoTitulosBaixadosFinanceiro(NEF->NEF_IDCTR)  .or. oTituloFinanceiroRepository:existByIdContratoFechamentoFinanceiro(NEF->NEF_IDCTR)
			Self:setError(STR0002) //"Este contrato já possui movimentações com títulos baixados, não sendo possível desvincular o contrato."
			Return nil
		Else

			Begin Transaction
				If lUsaTela
					If !AGDGRAVAHIS(STR0004,"NEA", NEA->(NEA_FILIAL+NEA_CODIGO+NEA_VERSAO) + " Contrato: " + AllTrim(NEF->(NEF_CTREXT)), STR0003,.T.) //"Desvincular Contrato " //"DESVINCULAR"
						Self:setError(STR0005) //"Operação cancelada!"
						disarmTransaction()
						break
					endif
				Else
					AGDGRAVAHIS("","NEA",  NEA->(NEA_FILIAL+NEA_CODIGO+NEA_VERSAO) + " Contrato: " + AllTrim(NEF->(NEF_CTREXT)), STR0003 ,.F., cMsgObs) //"DESVINCULAR"
				endIf
				oMovimentocontratoAclRepository:= agdMovimentoContratoAclRepository():New()
				oMovimentocontratoAclRepository:deleteByIdCtr(NEF->NEF_IDCTR) //deleta registros NEI pelo NEI_IDCTR
				Self:setFullResponse(oMovimentocontratoAclRepository)

				If Self:isSuccess()

					oNegocioService := agdNegocioService():New()
					cUltVersao := oNegocioService:buscarVersaoAtualValida(NEF->NEF_CODBRT)
					oDadosContratosNegocioRepository := agdDadosContratosNegocioRepository():New(FWxFilial("NEE")+NEF->NEF_CODBRT+cUltVersao+NEF->NEF_ITECTR)

					oContratoAclRepository:= agdContratoAclRepository():New(NEF->(NEF_FILIAL+NEF_CODBRT+NEF_ITECTR+NEF_ITEM))
					nContVinculos := oContratoAclRepository:getCountVinculoItemBarter()
					nQtdItemNEF := NEF->NEF_QUANT

					oContratoAclService := agdContratoAclService():New(NEF->NEF_CODBRT)
					nQtdTotalNEF := oContratoAclService:sumQuantidadeByItemContrato(NEF->NEF_ITECTR)

					oContratoAclRepository:delete() //deleta registro NEF, cuidar que a NEF daqui em diante não existe
					Self:setFullResponse(oContratoAclRepository)

					If Self:isSuccess()

						If nContVinculos = 1
							//tem apenas um contrato vinculado
							oDadosContratosNegocioRepository:delete() //deleta tabela NEE
							Self:setFullResponse(oDadosContratosNegocioRepository)
						Else
							//tem mais de um contrato vinculado
							nSaldoNEE := nQtdTotalNEF - nQtdItemNEF
							oDadosContratosNegocioRepository:updateRepository({{"NEE_QTDSAC", nSaldoNEE}}) //Atualiza registro NEE
							Self:setFullResponse(oDadosContratosNegocioRepository)
						EndIF

					EndIf

				EndIf

				If !Self:isSuccess()
					disarmTransaction()
				EndIf

			End Transaction
			FreeObj(oContratoAclRepository)
			FreeObj(oMovimentocontratoAclRepository)
			FreeObj(oDadosContratosNegocioRepository)
			FreeObj(oNegocioService)

		Endif
	Else
		Self:setError(STR0006, 404) //"Registro não encontrado!"
		Return nil
	Endif

return nil


/*/{Protheus.doc} agdContratoAclVincularService::getQuantidadePermitidaAVincularNEE
Busca a quantidade permitida a ser vinculada no registro de dados de contrato(NEE) da negociação, para o campo NEE_QTDSAC, com base na quantidade enviada
sendo esta quantidade em unidade de medida de preço(NEA_UMPRC)
@type method
@version P12 
@author claudineia.reinert
@since 02/06/2025
@param cCodBrt, character, codigo da negociação de barter
@param cVersao, character, versao da negociação de barter
@param nQuantidade, numeric, quantidade disponivel a ser vinculada em unidade de medida de preço(NEA_UMPRC)
@return numeric, quantidade permitida a ser vinculada
/*/
method getQuantidadePermitidaAVincularNEE(cCodBrt, cVersao, nQuantidade) as Numeric class agdContratoAclVincularService
	Local nQtdPermitida := 0
	Local nSaldo := 0

	nSaldo := Self:getSaldoAVincularNEE(cCodBrt,cVersao)
	If nSaldo > 0 .and. nSaldo < nQuantidade
		nQtdPermitida := nSaldo
	ElseIf nQuantidade > 0 .and. nQuantidade <= nSaldo
		nQtdPermitida := nQuantidade
	EndIf

Return nQtdPermitida

/*/{Protheus.doc} agdContratoAclVincularService::getQuantidadeConvertidaPermitidaAVincularNEE
Busca a quantidade permitida a ser vinculada no registro de dados de contrato(NEE) da negociação, para o campo NEE_QTCONV, com base na quantidade enviada
sendo esta quantidade em unidade de medida de produto(B1_UM)
@type method
@version P12 
@author claudineia.reinert
@since 01/10/2025
@param cCodBrt, character, codigo da negociação de barter
@param cVersao, character, versao da negociação de barter
@param nQuantidade, numeric, quantidade disponivel a ser vinculada em unidade de medida de produto(B1_UM)
@return numeric, quantidade permitida a ser vinculada
/*/
method getQuantidadeConvertidaPermitidaAVincularNEE(cCodBrt, cVersao, nQuantidade) as Numeric class agdContratoAclVincularService
	Local nQtdPermitida := 0
	Local nSaldo := 0

	nSaldo := Self:getSaldoQuantidadeConvertidaAVincularNEE(cCodBrt,cVersao)
	If nSaldo > 0 .and. nSaldo < nQuantidade
		nQtdPermitida := nSaldo
	ElseIf nQuantidade > 0 .and. nQuantidade <= nSaldo
		nQtdPermitida := nQuantidade
	EndIf

Return nQtdPermitida

/*/{Protheus.doc} agdContratoAclVincularService::getSaldoAVincularNEE
Busca o saldo disponivel,, referente ao campo NEE_QTDSAC, a ser vinculada no registro de dados de contrato(NEE) da negociação
@type method
@version P12 
@author claudineia.reinert
@since 02/06/2025
@param cCodBrt, character, codigo da negociação de barter
@param cVersao, character, versao da negociação de barter
@return numeric, saldo disponivel
/*/
method getSaldoAVincularNEE(cCodBrt, cVersao) as Numeric class agdContratoAclVincularService
	Local oNegocioService := agdNegocioService():New()
	Local nQtdNegNEA := 0
	Local nTotNEE := 0
	Local nSaldo := 0

	oNegocioService:buscarPorId(cCodBrt,cVersao)
	nQtdNegNEA := oNegocioService:getResponse()['quantidadenegociada']
	nTotNEE := oNegocioService:getTotalParidadeNEE(cCodBrt, cVersao)
	nSaldo := nQtdNegNEA - nTotNEE

Return nSaldo

/*/{Protheus.doc} agdContratoAclVincularService::getSaldoQuantidadeConvertidaAVincularNEE
Busca o saldo disponivel,, referente ao campo NEE_QTCONV, a ser vinculada no registro de dados de contrato(NEE) da negociação
@type method
@version P12 
@author claudineia.reinert
@since 02/06/2025
@param cCodBrt, character, codigo da negociação de barter
@param cVersao, character, versao da negociação de barter
@return numeric, saldo disponivel
/*/
method getSaldoQuantidadeConvertidaAVincularNEE(cCodBrt, cVersao) as Numeric class agdContratoAclVincularService
	Local oNegocioService := agdNegocioService():New()
	Local nQtdNegNEA := 0
	Local nTotNEE := 0
	Local nSaldo := 0

	oNegocioService:buscarPorId(cCodBrt,cVersao)
	nQtdNegNEA := oNegocioService:getResponse()['quantidadeconvertum']
	nTotNEE := oNegocioService:getTotalParidadeConvertidaNEE(cCodBrt, cVersao)
	nSaldo := nQtdNegNEA - nTotNEE

Return nSaldo

/*/{Protheus.doc} agdContratoAclVincularService::validarContrato
Validação se contrato pode ou não ser vinculado a negociação.
@type method
@version P12 
@author claudineia.reinert
@since 02/06/2025
@param cFilCtr, character, Filial do contrato
@param cCodCtr, character, codigo do contrato
@return Logical, Valor logico
/*/
method validarContrato(cFilCtr, cCodCtr) class agdContratoAclVincularService
	Local oCtrAclRepos := agdContratoAclRepository():New()
	Local oTituloFinanceiroRepository := agdTituloFinanceiroRepository():New()
	Local oIdService := agdIdService():New()
	Local cIdContrato := oIdService:encode({ cFilCtr, cCodCtr })

	oCtrAclRepos:findByContratoExterno(cIdContrato)
	If oCtrAclRepos:isSuccess() //acho vinculo
		Self:setError(STR0007) //"Este contrato já foi vinculado em uma negociação, não sendo possível ser vinculado a esta negociação."
		return
	ElseIf oTituloFinanceiroRepository:existsByContratoOGBaixaFinanceiro(cFilCtr, cCodCtr)
		Self:setError(STR0008) //"Este contrato possui movimentações com titulos baixados, não sendo possível vincular a esta negociação."
		return
	Else
		Self:setSuccess()
	EndIF

Return nil

/*/{Protheus.doc} agdContratoAclVincularService::vincular
Metodo para vincular o contrato OG
@type method
@version P12
@author scheronlini.martins
@since 15/08/2025
@param cCodigoNegociacao, character, CodigoNegociacao
@param cVersao, character, Versao
@param cCodigoContratoext, character, CodigoContratoext
/*/
method vincular(cCodigoNegociacao,cVersao,cCodigoContratoext) class agdContratoAclVincularService
	Local lTela := .F.
	Local cAliasTmp:= AGDA032TMP()
	Local cMsgErro := ""

	DBSelectArea("NEA")
	IF NEA->(DBSeek(FWxFilial("NEA")+cCodigoNegociacao + cVersao))
		cMsgErro := AGDA032VLD()
		If Empty(cMsgErro)
			AGDA032INS(lTela,cCodigoContratoext)
			If !(cAliasTmp)->(Eof())
				if AGDA032CFM(lTela)
					self:setSuccess("Contrato "+ ALLTRIM(cCodigoContratoext)+" vinculado a negociação "+ ;
						ALLTRIM(cCodigoNegociacao)+ " versão "+ ALLTRIM(cVersao))
				endif
			Else
				Self:setError(STR0009, 404)//"Não encontrado contrato válido para vincular"
			EndIf
		Else
			Self:setError(cMsgErro, 404)
		EndIf
	Else
		Self:setError(STR0010, 404)//"Negociação não encontrada"
	EndIf

return

/*/{Protheus.doc} agdContratoAclVincularService::listar
Lista Contratos OG disponiveis para vinculo
@type method
@version P12
@author scheronlini.martins
@since 19/08/2025
@param nPage, numeric, página de resultados
@param nPageSize, numeric,registros por pagina
@param oJsonParam, object, param_objeto com parametros
@param cCodigoNegociacao,character, Codigo Negociacao
@param cVersao,character, Versao
@param cCodigoContratoext,character, Codigo Contrato externo
/*/
method listar(nPage, nPageSize, oJsonQryParam,cCodigoNegociacao,cVersao,cCodContratoExt) class agdContratoAclVincularService
	Local oContratoRepository As Object
	Local cFilter as character
	Default nPage     := 1
	Default nPageSize := 10
	Default oJsonQryParam := nil

	DBSelectArea("NEA")
	IF NEA->(DBSeek(FWxFilial("NEA")+cCodigoNegociacao+cVersao))

		IF Empty(cCodContratoExt)
			oContratoRepository := agdcontratovincularrepository():New(.F.)
		else
			oContratoRepository := agdcontratovincularrepository():New(.T.,alltrim(cCodContratoExt))
		endif

		If !Empty(cFilter := Self:buildFilter(oJsonQryParam, oContratoRepository:aFieldsRepository[2, 1]))
			oJsonQryParam['filter'] := cFilter
		EndIf

		oContratoRepository:find(nPage, nPageSize, oJsonQryParam)

		Self:setFullResponse(oContratoRepository)

		oContratoRepository:DeActivate()

	else
		Self:setError(STR0010, 404)//"Negociação não encontrada"
	EndIf
	FreeObj(oContratoRepository)

Return nil

/*/{Protheus.doc} agdContratoAclVincularService::buildFilter
Constrói o filtro no padrão OData para ser usada na consulta.
O método combina dinamicamente as seguintes condições com o operador "AND":
1. filtro padrão, vindo do parâmetro 'filterdefault' na requisição.
2. filtro de busca textual (contains), vindo do parâmetro 'filter'.
3. filtro de igualdade (eq) para busca por um ID específico.
@type method
@version 12
@author jc.maldonado
@since 7/21/2025
@param jQueryParam, json, jquery param
@param cId, character, id do registro
@param cIdField, character, campo com id
@return character, filtro
/*/
method buildFilter(jQueryParam as json, cIdField as character) as character class agdContratoAclVincularService
	local aFilters := {}                           as array
	local cFilter                                  as character
	local bTrimUp := {|cVal| upper(alltrim(cVal))} as codeBlock

	if jQueryParam:hasProperty('filter');
			.and. ! empty(jQueryParam['filter'])
		aAdd(aFilters, "contains(" + eval(bTrimUp, cIdField) + " , '" +  eval(bTrimUp, jQueryParam['filter']) + "')")
	endif


	if ! empty(aFilters)
		cFilter += ArrTokStr(aFilters, " AND ")
	endIf

	FWFreeArray(aFilters)
return cFilter
