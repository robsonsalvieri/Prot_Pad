#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'TOTVS.CH'
#DEFINE PATH_URL '/api/agd/v1/vendas-perdidas'
#DEFINE PATH_URL_ID '/api/agd/v1/vendas-perdidas/:codigonegocio/:iteminsumo/:item'


namespace agd.vendaPerdidaApi  /*nameSpace - para ser importado em shortcurt*/
using namespace agd.vendaPerdidaService /*shortCurt de funcao*/
using namespace agd.restUtils /*shortCurt de funcao*/

/*/{Protheus.doc} agdVendaPerdidaApi
Controller de Venda Perdida de Negócios Barter
@type class
@version 12
@author jean.schulze
@since 17/02/2025
/*/
class agdVendaPerdidaApi
  public Data nPage     As Integer
  public Data nPageSize As Integer

  public method new()

  @Get(PATH_URL)
  public method listar() as logical

  @Get(PATH_URL_ID)
  public method buscarPorId() as logical

  @Post(PATH_URL)
  public method registrar() as logical


endClass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return variant, instância
/*/
method new() class agdVendaPerdidaApi
return self

/*/{Protheus.doc} listar
Recurso REST para listar Venda Perdida de Negócios Barter
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return logical, true
/*/
method listar() as logical class agdVendaPerdidaApi
    Local oVendaPerdidaService As Object
    Local oRestService As Object
    Local oJsonQryParam := Nil

    oRestService := agdRestUtils():New()
    oVendaPerdidaService := agdVendaPerdidaService():New()

    oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oJsonQryParam := oRestService:getQueryParams()
     
    oVendaPerdidaService:listar(Self:nPage, Self:nPageSize, oJsonQryParam)

    oRestService:setAPIResponse(oRest, oVendaPerdidaService)

    FreeObj(oVendaPerdidaService)
    FreeObj(oRestService)
return .T.

/*/{Protheus.doc} buscarPorId
Recurso REST para buscar  Venda Perdida de Negócios Barter por ID
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return logical, true
/*/
method buscarPorId() as logical class agdVendaPerdidaApi
    Local oVendaPerdidaService As Object
    Local oRestService As Object

    Local cCodNegocio as Character
    Local cItemInsumo as Character
    Local cItem       as Character
    Local oJsonPath   As Object
  
    oRestService := agdRestUtils():New()
    oVendaPerdidaService := agdVendaPerdidaService():New()
         
    oJsonPath := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("codigonegocio")
        cCodNegocio := oJsonPath["codigonegocio"]
    EndIf

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("iteminsumo")
        cItemInsumo := oJsonPath["iteminsumo"]
    EndIf

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("item")
        cItem := oJsonPath["item"]
    EndIf

    oVendaPerdidaService:buscarPorId(cCodNegocio, cItemInsumo, cItem )
    
    oRestService:setAPIResponse(oRest, oVendaPerdidaService)

    FreeObj(oVendaPerdidaService)
    FreeObj(oRestService)
return .T.

/*/{Protheus.doc} registrar
Recurso REST para inserir Venda Perdida de Negócios Barter
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return logical, true
/*/
method registrar() as logical class agdVendaPerdidaApi
    Local oVendaPerdidaService As Object
    Local oRestService As Object
    Local jCmdRegistrar as Json     

    oRestService := agdRestUtils():New()
    oVendaPerdidaService := agdVendaPerdidaService():New()
        
    jCmdRegistrar := oRestService:getRequestBody() 

    oVendaPerdidaService:registrar(jCmdRegistrar)
    
    oRestService:setAPIResponse(oRest, oVendaPerdidaService)

    FreeObj(oVendaPerdidaService)
    FreeObj(oRestService)
    FreeObj(jCmdRegistrar)
return .T.
