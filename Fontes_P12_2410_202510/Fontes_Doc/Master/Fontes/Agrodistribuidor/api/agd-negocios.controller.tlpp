#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'TOTVS.CH'
#DEFINE PATH_URL '/api/agd/v1/negociacoes'
#DEFINE PATH_URL_ID '/api/agd/v1/negociacoes/:codigo/:versao'
#DEFINE PATH_URL_ID_CANCEL '/api/agd/v1/negociacoes/:codigo/:versao/cancelar'
#DEFINE PATH_URL_CONTRATO_QUANTIDADE '/api/agd/v1/negociacoes/contrato/:codigoContratoExterno/quantidade'
#DEFINE PATH_URL_CONTRATO_CODIGO '/api/agd/v1/negociacoes/contrato/:codigo/:itemContrato/:item'
#DEFINE PATH_URL_AUTH'/api/agd/v1/negociacoes/:codigo/:versao/autorizar'
#DEFINE PATH_URL_GERAR_CONTRATO '/api/agd/v1/negociacoes/:codigo/:versao/gerar-contratos'
#DEFINE PATH_URL_CALCULAR_IMPOSTOS '/api/agd/v1/negociacoes/calcularImpostos/:codigoproduto/:codigofornecedor/:codigofornecedorloja/:tes/:moedart/:valorrt'
#DEFINE PATH_URL_CONSULTAR_IMPOSTOS '/api/agd/v1/negociacoes/consultarImpostos/:codigonegocio'
#DEFINE PATH_URL_STATUS '/api/agd/v1/negociacoes/:codigo/:versao/status'
#DEFINE PATH_URL_TOTAIS '/api/agd/v1/negociacoes/:codigo/totais'
#DEFINE PATH_URL_PRECO_CUSTO '/api/agd/v1/negociacoes/preco-custo/:codproduto/:local'
#DEFINE PATH_URL_PARIDADE '/api/agd/v1/negociacoes/paridade/:valorfrete/:valorinsumo/:valorcommodity'
#DEFINE PATH_URL_TXMOEDA '/api/agd/v1/negociacoes/txmoeda/:moeda/:datacotacao'
#DEFINE PATH_URL_CALC_TOTAL '/api/agd/v1/negociacoes/calculo-totalizadores'
#DEFINE PATH_VERIFICAR_EDICAO '/api/agd/v1/negociacoes/:codigo/:versao/verificar-permissao-edicao'
#DEFINE PATH_URL_VERSIONAR '/api/agd/v1/negociacoes/:codigo/:versao/versionar'
#DEFINE PATH_VERIFICAR_EDICAO_VINCULAR '/api/agd/v1/negociacoes/:codigo/:versao/verificar-permissao-vincular-contrato'

namespace agd.negocioApi  /*nameSpace - para ser importado em shortcurt*/
using namespace agd.negocioService /*shortCurt de funcao*/
using namespace agd.restUtils /*shortCurt de funcao*/
using namespace agd.contratoAclService
using namespace agd.negocioStatusService
using namespace agd.negocioImpostosService
using namespace agd.repositoryUtils
using namespace agd.negocioTotalizadorService

/*/{Protheus.doc} agdNegocioApi
Controller de Negócios Barter
@type class
@version 12
@author jean.schulze
@since 13/01/2025
/*/
class agdNegocioApi
	public Data nPage     As Integer
	public Data nPageSize As Integer

	public method new()

	@Get(PATH_URL)
	public method listar() as logical

	@Get(PATH_URL_ID)
	public method buscarPorId() as logical

	@Get(PATH_URL_TOTAIS)
	public method totalizadores() as logical

	@Post(PATH_URL)
	public method registrar() as logical

	@Post(PATH_URL_VERSIONAR)
	public method versionar() as logical

	@Put(PATH_URL_ID_CANCEL)
	public method cancelar() as logical

	@Put(PATH_URL_CONTRATO_QUANTIDADE)
	public method atualizarQuantidadeContrato() as logical

	@Put(PATH_URL_CONTRATO_CODIGO)
	public method atualizarCodigoExternoContrato() as logical

	@Put(PATH_URL_GERAR_CONTRATO)
	public method gerarContratosNegociacao() as logical

	@Delete(PATH_URL_ID)
	Public Method deletar() as logical

	@Put(PATH_URL_AUTH)
	public method autorizar() as logical

	@Put(PATH_URL_ID)
	public method atualizarNegociacao() as logical

	@Get(PATH_URL_CALCULAR_IMPOSTOS)
	public method calcImpostos() as logical

	@Get(PATH_URL_CONSULTAR_IMPOSTOS)
	public method getImpostos() as logical

	@Get(PATH_URL_STATUS)
	public method getStatusNegociacao() as logical

	@Get(PATH_URL_PRECO_CUSTO)
	public method obterPrecoCusto() as logical

	@Get(PATH_URL_PARIDADE)
	public method obterParidade() as logical

	@Get(PATH_URL_TXMOEDA)
	public method obterTaxaMoeda() as logical

	@Post(PATH_URL_CALC_TOTAL)
	public method calcularTotais() as logical

	@Get(PATH_VERIFICAR_EDICAO)
	public method permiteAtualizar() as logical

	@Get(PATH_VERIFICAR_EDICAO_VINCULAR)
	public method permiteVincular() as logical

	private method getPathParamNegocio() as Array
	private method getPathParamContratoCompraBarter() as Array

endClass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return variant, inst ncia
/*/
method new() class agdNegocioApi
return self

/*/{Protheus.doc} listar
Recurso REST para listar Neg cios Barter
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return logical, true
/*/
method listar() as logical class agdNegocioApi
	Local oNegocioService As Object
	Local oRestService As Object
	Local oJsonQryParam := Nil

	oRestService := agdRestUtils():New()
	oNegocioService := agdNegocioService():New()

	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
	oJsonQryParam := oRestService:getQueryParams()

	oNegocioService:listar(Self:nPage, Self:nPageSize, oJsonQryParam)

	oRestService:setAPIResponse(oRest, oNegocioService)

	FreeObj(oNegocioService)
	FreeObj(oRestService)
return .T.

/*/{Protheus.doc} buscarPorId
Recurso REST para buscar Neg cio Barter por ID
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return logical, true
/*/
method buscarPorId() as logical class agdNegocioApi
	Local oNegocioService As Object
	Local oRestService As Object

	Local cIdentificacao as Character
	Local oJsonPath      As Object

	oRestService := agdRestUtils():New()
	oNegocioService := agdNegocioService():New()

	oJsonPath := oRest:getPathParamsRequest()

	If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("codigo")
		cIdentificacao := oJsonPath["codigo"]
	EndIf

	If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("versao")
		cVersao := oJsonPath["versao"]
	EndIf

	oNegocioService:buscarPorId(cIdentificacao, cVersao)

	oRestService:setAPIResponse(oRest, oNegocioService)

	FreeObj(oNegocioService)
	FreeObj(oRestService)
return .T.

/*/{Protheus.doc} registrar
Recurso REST para inserir negócio BARTER
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return logical, true
/*/
method registrar() as logical class agdNegocioApi
	Local oNegocioService As Object
	Local oRestService As Object
	Local jCmdRegistrar as Json

	oRestService := agdRestUtils():New()
	oNegocioService := agdNegocioService():New()

	jCmdRegistrar := oRestService:getRequestBody()

	oNegocioService:registrar(jCmdRegistrar)

	oRestService:setAPIResponse(oRest, oNegocioService)

	FreeObj(oNegocioService)
	FreeObj(oRestService)
	FreeObj(jCmdRegistrar)
return .T.

/*/{Protheus.doc} cancelar
Recurso Rest para cancelar a negociacao.
@type method
@version 12
@author jc.maldonado
@since 19/05/2025
@return logical, Allways True
/*/
method cancelar() As logical Class agdNegocioApi
	Local oNegocioStatusService as Object
	Local oRestService          as Object
	Local oJsonPath             as Json
	Local cIdNegocioBarter      as character
	Local cMotivo               as character

	oRestService := agdRestUtils():New()

	cIdNegocioBarter := oRestService:getIDWithPathParams(Self:getPathParamNegocio())

	jCmdAutorizar := oRestService:getRequestBody()

	if !Empty(cIdNegocioBarter)
		cIdNegocioBarter := FWxFilial("NEA") + cIdNegocioBarter
		oNegocioStatusService := agdNegocioStatusService():New(cIdNegocioBarter)
		if ValType(jCmdAutorizar) == "J" .And. jCmdAutorizar:hasProperty("motivo") .and. ValType(jCmdAutorizar["motivo"]) == "C"
			cMotivo := cValtoChar(jCmdAutorizar["motivo"] )
		EndIf
		oNegocioStatusService:cancelar(.F. ,cMotivo)

		oRestService:setAPIResponse(oRest, oNegocioStatusService)
	endif

	FreeObj(oNegocioStatusService)
	FreeObj(oRestService)
	FreeObj(oJsonPath)
Return .T.

/*/{Protheus.doc} atualizarQuantidadeContrato
Recurso Rest para atualizar a quantidade de Contrato de Negocio Barter
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return logical, true
/*/
method atualizarQuantidadeContrato() as logical class agdNegocioApi
	Local oContratoACLService As Object
	Local oRestService As Object
	Local jCmdAtualizar as Json
	Local cIdentificacao as Character
	Local oJsonPath      As Object

	oRestService := agdRestUtils():New()
	jCmdAtualizar := oRestService:getRequestBody()

	oJsonPath := oRest:getPathParamsRequest()

	If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("codigoContratoExterno")
		cIdentificacao := oJsonPath["codigoContratoExterno"]
	EndIf

	oContratoACLService := agdContratoAclService():New()
	oContratoACLService:getContratoByCodigoExterno(cIdentificacao)

	if oContratoACLService:isSuccess()
		oResponseContrato := oContratoACLService:getResponse()
		cIdContratoAcl = oResponseContrato['filial'] + oResponseContrato['codigo'] + oResponseContrato['itemlocalcontrato'] + oResponseContrato['item']
		oContratoACLService:atualizarQuantidadeContrato(cIdContratoAcl, jCmdAtualizar['quantidade'])
	endif

	oRestService:setAPIResponse(oRest, oContratoACLService)

	FreeObj(oContratoACLService)
	FreeObj(oRestService)
	FreeObj(jCmdAtualizar)
return .T.

/*/{Protheus.doc} atualizarCodigoExternoContrato
Recurso Rest para atualizar codigo externo do contrato de Negocio Barter
@type method
@version 12
@author Gilson.Venturi
@since 15/01/2025
@return logical, true
/*/
method atualizarCodigoExternoContrato() as logical class agdNegocioApi
	Local oContratoACLService   As Object
	Local oRestService          As Object
	Local jCmdAtualizar         As Json
	Local cIdContratoAcl        As Character

	oRestService   := agdRestUtils():New()
	jCmdAtualizar  := oRestService:getRequestBody()
	cIdContratoAcl := oRestService:getIDWithPathParams(Self:getPathParamContratoCompraBarter())

	if !Empty(cIdContratoAcl)
		cIdContratoAcl := FWxFilial('NEF') + cIdContratoAcl

		oContratoACLService := agdContratoAclService():New()
		oContratoACLService:atualizarCodigoExternoContrato(cIdContratoAcl, jCmdAtualizar['idContratoExterno'],jCmdAtualizar['desContratoExterno'])

		oRestService:setAPIResponse(oRest, oContratoACLService)
	endif

	FreeObj(oContratoACLService)
	FreeObj(oRestService)
	FreeObj(jCmdAtualizar)
return .T.

/*/{Protheus.doc} gerarContratosNegociacao
Gera os contratos de compra de uma negociacao barter
@type method
@version 12
@author jean.schulze
@since 29/05/2025
@return logical, true
/*/
method gerarContratosNegociacao() as logical class agdNegocioApi
	Local oContratoACLService As Object
	Local oRestService As Object
	Local cIdentificacao as Character

	oRestService := agdRestUtils():New()

	cIdentificacao := oRestService:getIDWithPathParams({{'codigo' ,'NEA_CODIGO'}})

	if !Empty(cIdentificacao)
		oContratoACLService := agdContratoAclService():New(cIdentificacao)
		oContratoACLService:gerarContratos()
		oRestService:setAPIResponse(oRest, oContratoACLService)
	endif

	FreeObj(oContratoACLService)
	FreeObj(oRestService)

return .T.


/*/{Protheus.doc} deletar
Recurso Rest para deletar negociacoes
@type method
@version 12
@author carlos.augusto
@since 21/05/2025
@return logical, true
/*/
method deletar() as logical class agdNegocioApi
	Local oNegocioService  as Object
	Local oRestService     as Object
	Local oJsonPath        as Json
	Local cIdNegocioBarter as character

	oRestService := agdRestUtils():New()

	cIdNegocioBarter := oRestService:getIDWithPathParams(Self:getPathParamNegocio())
	If !Empty(cIdNegocioBarter)
		cIdNegocioBarter := FWxFilial("NEA") + cIdNegocioBarter
		oNegocioService := agdNegocioService():New()
		oNegocioService:deletar(cIdNegocioBarter)

		oRestService:setAPIResponse(oRest, oNegocioService)
	EndIf

	FreeObj(oNegocioService)
	FreeObj(oRestService)
	FreeObj(oJsonPath)

return .T.

/*/{Protheus.doc} autorizar
Recurso Rest para autorizar uma negociação
@type method
@version P12
@author scheronlini.martins
@since 21/05/2025
@return logical, return_true
/*/
method autorizar() as logical class agdNegocioApi
	Local oNegocioStatusService   As Object
	Local oRestService          As Object
	Local cIdNegociacao        As Character
	Local jCmdAutorizar         As Json

	oRestService := agdRestUtils():New()

	cIdNegociacao := oRestService:getIDWithPathParams(Self:getPathParamNegocio())

	jCmdAutorizar := oRestService:getRequestBody()

	if !Empty(cIdNegociacao)
		cIdNegociacao := FWxFilial('NEA') + cIdNegociacao
		oNegocioStatusService  := agdNegocioStatusService():New(cIdNegociacao)

		if jCmdAutorizar["acao"] == 'aprovar'
			oNegocioStatusService:liberar(.F.,jCmdAutorizar["mensagem"] )
		elseif jCmdAutorizar["acao"] == 'recusar'
			oNegocioStatusService:recusar(.F., jCmdAutorizar["mensagem"])
		EndIf

		oRestService:setAPIResponse(oRest, oNegocioStatusService)
	endif

	FreeObj(oNegocioStatusService)
	FreeObj(oRestService)

return .T.

/*/{Protheus.doc} getPathParamNegocio
Retorna a chave de busca da rota do negócio
@type method
@version 12
@author jean.schulze
@since 30/05/2025
@return array, lista de campos
/*/
Method getPathParamNegocio() as array class agdNegocioApi
	Local aArraParams := {}

	AAdd(aArraParams, {'codigo' ,'NEA_CODIGO'})
	AAdd(aArraParams, {'versao' ,'NEA_VERSAO'})
return aArraParams

/*/{Protheus.doc} getPathParamContratoCompraBarter
Retorna a chave de busca da rota do contrato de compra do barter
@type method
@version 12
@author jean.schulze
@since 30/05/2025
@return array, return_description
/*/
Method getPathParamContratoCompraBarter() as array class agdNegocioApi
	Local aArraParams := {}

	AAdd(aArraParams, {'codigo' ,'NEF_CODBRT'})
	AAdd(aArraParams, {'itemContrato' ,'NEF_ITECTR'})
	AAdd(aArraParams, {'item' ,'NEF_ITEM'})

return aArraParams

/*/{Protheus.doc} agdNegocioApi::atualizarNegociacao
API REST responsavel pelo Update da Negociação
@type method
@version P12
@author scheronlini.martins
@since 30/05/2025
@return logical, return_.T.
/*/
method atualizarNegociacao() as logical class agdNegocioApi
	Local oNegocioService As Object
	Local oRestService As Object
	Local oJsonPath             As Object
	Local jCmdAtualizar as Json
	Local cIdNegociacao  As Character

	oRestService := agdRestUtils():New()

	oJsonPath := oRest:getPathParamsRequest()

	jCmdAtualizar := oRestService:getRequestBody()

	cIdNegociacao := oRestService:getIDWithPathParams(Self:getPathParamNegocio())


	if !Empty(cIdNegociacao)

		cIdNegociacao:=  xfilial("NEA") + cIdNegociacao
		oNegocioService := agdNegocioService():New()

		oNegocioService:setNegociacao(jCmdAtualizar, cIdNegociacao)

		oRestService:setAPIResponse(oRest, oNegocioService)
	endif

	FreeObj(oNegocioService)
	FreeObj(oRestService)
	FreeObj(jCmdAtualizar)

return .T.


/*/{Protheus.doc} getImpostos
Busca os impostos de uma negociação
@type method
@version P12
@author carlos.augusto
@since 23/06/2025
@return logical, return 
/*/
Method getImpostos() as logical class agdNegocioApi
	Local oRestService     As Object
	Local cCodigoNegocio   As Character
	Local oImpostosService As Object

	oRestService   := agdRestUtils():New()
	cCodigoNegocio := oRestService:getIDWithPathParams({{'codigonegocio','NEA_CODIGO'}})
	If !Empty(cCodigoNegocio)
		oImpostosService := agdNegocioImpostosService():New()
		oImpostosService:consultarImpostos(cCodigoNegocio)
		oRestService:setAPIResponse(oRest, oImpostosService)
	EndIf
	FWFreeObj(oImpostosService)
Return .T.


/*/{Protheus.doc} getImpostos
Calcula os impostos 
@type method
@version P12
@author carlos.augusto
@since 23/06/2025
@return logical, return
/*/
Method calcImpostos() as logical class agdNegocioApi
	Local cCodigoProduto     As Character
	Local cCodigoFornecedor  As Character
	Local cLojaFornecedor    As Character
	Local cTES               As Character
	Local nMoedaRT           As Numeric
	Local nValorRT           As Numeric

	Local cNatureza          As Character
	Local oRestService       As Object
	Local oImpostosService   As Object
	Local oJsonQryParam      As Object

	oRestService    := agdRestUtils():New()
	oJsonQryParam   := oRestService:getQueryParams()

	cCodigoProduto      := oRestService:getIDWithPathParams({{'codigoproduto',        'NEA_CODPRO'}})
	If !Empty(cCodigoProduto)
		cCodigoFornecedor   := oRestService:getIDWithPathParams({{'codigofornecedor',     'NEA_CODCLI'}})
		cLojaFornecedor     := oRestService:getIDWithPathParams({{'codigofornecedorloja', 'NEA_LOJCLI'}})
		cTES                := oRestService:getIDWithPathParams({{'tes',                  'F4_CODIGO'}})
		nMoedaRT            := Val(oRestService:getIDWithPathParams({{'moedart',          'NEA_MOEDRT'}}))
		nValorRT            := Val(oRestService:getIDWithPathParams({{'valorrt',          'NEA_VLRRT'}}))

		If ValType(oJsonQryParam) == "J" .And. oJsonQryParam:hasProperty("natureza")
			cNatureza := PadR( oJsonQryParam["natureza"], TamSX3("NEE_NATURE")[1])
		EndIf

		oImpostosService := agdNegocioImpostosService():New(cCodigoFornecedor,cLojaFornecedor,nMoedaRT,/* nTxMoeda */,cCodigoProduto,1,nValorRT,cNatureza,cTES)
		oImpostosService:fetchTaxes()                 //method new(cCGC as character, aValores as array, aProduto as array) class agdNegocioImpostosService
		oRestService:setAPIResponse(oRest, oImpostosService)
	EndIf

	FWFreeObj(oImpostosService)
Return


/*/{Protheus.doc} listar
Recurso REST para retornar status Recebimento/Expedicao de negociacao
@type method
@version 12
@author lindembergson.pacheco
@since 05/06/2025
@return logical, true
/*/
method getStatusNegociacao() as logical class agdNegocioApi

	Local oNegocioService As Object
	Local oRestService As Object

	Local cIdentificacao as Character
	Local oJsonPath      As Object

	oRestService := agdRestUtils():New()
	oNegocioService := agdNegocioService():New()

	oJsonPath := oRest:getPathParamsRequest()

	If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("codigo")
		cIdentificacao := oJsonPath["codigo"]
	EndIf

	If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("versao")
		cVersao := oJsonPath["versao"]
	EndIf

	oNegocioService:getStatusNegocio(cIdentificacao, cVersao)
	oRestService:setAPIResponse(oRest, oNegocioService)

	FreeObj(oNegocioService)
	FreeObj(oRestService)
return .T.

/*/{Protheus.doc} totalizadores
Recurso REST para buscar totalizadores de expedi  o, rebimento e financeiro
@type method
@version 12
@author rodrigo.nsoledade
@since 11/06/2025
@return logical, true
/*/
method totalizadores() as logical class agdNegocioApi
	Local oNegocioService as Object
	Local oRestService    as Object
	Local cCodBarter      as Character

	oRestService := agdRestUtils():New()

	cCodBarter := oRestService:getIDWithPathParams({{'codigo' ,'NEA_CODIGO'}})

	If !Empty(cCodBarter)
		oNegocioService := agdNegocioService():New()
		oNegocioService:getTotalizador(cCodBarter)
		oRestService:setAPIResponse(oRest, oNegocioService)
	EndIf

	FreeObj(oNegocioService)
	FreeObj(oRestService)
return .T.

/*/{Protheus.doc} obterPrecoCusto
Recurso Rest para obter Preço de custo
@type method
@version 12
@author Scheronlini Martins
@since 24/06/2025
@return logical, true
/*/
method obterPrecoCusto() as logical class agdNegocioApi
	Local oRestService    As Object
	Local oNegService     As Object
	Local oJsonPath       As Object
	Local aPrecoCusto     As Array
	Local jResponse       As Json
	Local oAgdUtils       As Object
	Local cCodProduto := ""
	Local cLocal      := ""

	oRestService := agdRestUtils():New()
	oNegService := agdNegocioService():New()
	oAgdUtils := agdRepositoryUtils():New()

	oJsonPath := oRest:getPathParamsRequest()
	cCodProduto := oRestService:getIDWithPathParams({{'codproduto' ,'NEB_CODPRO'}})
	cLocal := oRestService:getIDWithPathParams({{'local' ,'NEB_LOCAL'}})
	if !empty(cCodProduto) .and. !empty(cLocal )

		If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("codproduto") .And. oJsonPath:hasProperty("local")
			// Obtém o preço de custo
			aPrecoCusto := oNegService:getPrecoCustoProdut(cCodProduto, cLocal)

			// Monta JSON de resposta
			jResponse := JsonObject():New()
			jResponse["Moeda1"] := aPrecoCusto[1]
			jResponse["Moeda2"] := aPrecoCusto[2]
			jResponse["Moeda3"] := aPrecoCusto[3]
			jResponse["Moeda4"] := aPrecoCusto[4]
			jResponse["Moeda5"] := aPrecoCusto[5]

			// Marca sucesso e envia a resposta
			oAgdUtils:setSuccess(jResponse)

		EndIf
	endif

	oRestService:setAPIResponse(oRest, oAgdUtils)
	FreeObj(oRestService)
	FreeObj(oNegService)
	FreeObj(oAgdUtils)

Return .T.

/*/{Protheus.doc} obterParidade
Recurso Rest para obter valor da Paridade
@type method
@version 12
@author Scheronlini Martins
@since 24/06/2025
@return logical, true
/*/
method obterParidade() as logical class agdNegocioApi
	Local oRestService    As Object
	Local oNegService     As Object
	Local cValorCommodity As Character
	Local cValorInsumo     As Character
	Local cValorFrete     As Character
	Local oJsonPath       As Object
	Local nValorParidade  As Numeric
	Local jResponse       As Json
	Local oAgdUtils       As Object

	oRestService := agdRestUtils():New()
	oNegService := agdNegocioService():New()
	oAgdUtils := agdRepositoryUtils():New()

	oJsonPath := oRest:getPathParamsRequest()

	cValorInsumo :=  oRestService:getIDWithPathParams({{'valorinsumo' ,'NEB_VALOR'}})
	cValorCommodity :=  oRestService:getIDWithPathParams({{'valorcommodity' ,'NEA_VLRRTL'}})
	cValorFrete := oRestService:getIDWithPathParams({{'valorfrete' ,'NEB_FRETE'}})

	if !empty(cValorInsumo) .and. !empty(cValorCommodity ).and. !empty(cValorFrete)

		If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("valorinsumo") .And. oJsonPath:hasProperty("valorcommodity").And. oJsonPath:hasProperty("valorfrete")
			nValorParidade:= CALCPARID(val(cValorFrete), val(cValorInsumo),val(cValorCommodity))

			// Monta JSON de resposta
			jResponse := JsonObject():New()
			jResponse["paridade"] := nValorParidade

			// Marca sucesso e envia a resposta
			oAgdUtils:setSuccess(jResponse)
		EndIf
	endif

	oRestService:setAPIResponse(oRest, oAgdUtils)
	FreeObj(oRestService)
	FreeObj(oNegService)
	FreeObj(oAgdUtils)

Return .T.

/*/{Protheus.doc} agdNegocioApi::obterTaxaMoeda
API retorna a taxa Moeda
@type method
@version p12
@author scheronlini.martins
@since 27/07/2025
@return logical, return true
/*/
method obterTaxaMoeda() as logical class agdNegocioApi
	Local oAGDrestUtils   As Object
	Local jPathParams     As Json
	Local oAGDrepoUtil    As Object
	Local oNegocioService As Object
	Local dData           As Date
	Local nMoeda          As Numeric
	Local nTxMoeda        As Numeric
	Local jResponse       As Json

	oAGDrestUtils := agdRestUtils():New()
	if !oAGDrestUtils:isFilialInformado()
		FWFreeObj(oAGDrestUtils)
		return .T.
	endif

	jPathParams := oRest:getPathParamsRequest()
	dData       := Fw8601ToDate(jPathParams['datacotacao'])
	nMoeda      := val(jPathParams['moeda'])
	FWFreeObj(jPathParams)

	if Empty(dData)
		dData := date()
	endif

	oNegocioService := agdNegocioService():New()
	nTxMoeda        := oNegocioService:getTaxaMoeda(nMoeda, dData)

	oAGDrepoUtil    := agdRepositoryUtils():New()

	if oNegocioService:isSuccess()
		jResponse := JsonObject():New()
		jResponse["taxaMoeda"]   := nTxMoeda
		jResponse["dataCotacao"] := FWDateTo8601(dData)

		oAGDrepoUtil:setSuccess(jResponse)
	else
		oAGDrepoUtil:setError(oNegocioService:getMessageError())
	endif

	oAGDrestUtils:setAPIResponse(oRest, oAGDrepoUtil)

	FWFreeObj(jResponse)
	FWFreeObj(oNegocioService)
	FWFreeObj(oAGDrestUtils)
	FWFreeObj(oAGDrepoUtil)
Return .T.


/*/{Protheus.doc} registrar
Recurso REST para calcular totalizadores
@type method
@version 12
@author lindembergson.pacheco
@since 12/06/2025
@return logical, true
/*/
method calcularTotais() as logical class agdNegocioApi
	Local oTotalService As Object
	Local oRestService As Object
	Local oPayload as Json

	oRestService := agdRestUtils():New()
	oPayload :=  oRestService:getRequestBody()

	oTotalService := agdNegocioTotalizadorService():New(oPayload)

	oTotalService:totalizadores()

	oRestService:setAPIResponse(oRest, oTotalService)

	FreeObj(oTotalService)
	FreeObj(oRestService)
	FreeObj(oPayload)
return .T.

/*/{Protheus.doc} permiteAtualizar
Verifica se o modelo de negociacao permite edicao com a chave enviada
@type method
@version 12
@author carlos.augusto
@since 25/07/2025
@return logical, true
/*/
method permiteAtualizar() as logical class agdNegocioApi
	Local oNegocioService  as Object
	Local oRestService     as Object
	Local cIdNegocioBarter as character

	oRestService := agdRestUtils():New()

	cIdNegocioBarter := oRestService:getIDWithPathParams(Self:getPathParamNegocio())
	If !Empty(cIdNegocioBarter)
		cIdNegocioBarter := FWxFilial("NEA") + cIdNegocioBarter
		oNegocioService := agdNegocioService():New()
		oNegocioService:verificaAlteracaoModelo(cIdNegocioBarter)
		oRestService:setAPIResponse(oRest, oNegocioService)
	EndIf

	FreeObj(oNegocioService)
	FreeObj(oRestService)

Return .T.

/*/{Protheus.doc} versionar
Cria uma nova versão da negociação barter a partir de um código e versão existentes
@type method
@version 12
@since 05/08/2025
@author rodrigo.nsoledade
@return logical, true
/*/
method versionar() as logical class agdNegocioApi
	Local oRestService     As Object
	Local oNegocioService  As Object
	Local oJsonPath        As Object
	Local jCmdBody         As Json
	Local cCodigo          As Character
	Local cVersao          As Character
	Local cIdNegociacao    As Character
	Local cMsgHist          As Character

	oRestService    := agdRestUtils():New()
	oNegocioService := agdNegocioService():New()

	Begin Transaction

		oJsonPath := oRest:getPathParamsRequest()
		jCmdBody  := oRestService:getRequestBody()

		If ValType(oJsonPath) == "J"
			If oJsonPath:hasProperty("codigo")
				cCodigo := oJsonPath["codigo"]
			EndIf
			If oJsonPath:hasProperty("versao")
				cVersao := oJsonPath["versao"]
			EndIf
		EndIf

		// Monta a chave única
		cIdNegociacao := FWxFilial("NEA") + cCodigo + cVersao

		// Verifica se tem o motivo
		If ValType(jCmdBody) == "J" .And. jCmdBody:hasProperty("motivo") .And. !Empty(AllTrim(jCmdBody["motivo"]))
			cMsgHist := AllTrim(jCmdBody["motivo"])
			// Realiza o versionamento
			oNegocioService:gerarVersionamento(cIdNegociacao, .F., cMsgHist)
			oRestService:setAPIResponse(oRest, oNegocioService)
		Else
			oRestService:setError(oRest, "O motivo do versionamento deve ser informado.", 400)
		EndIf

		If !oNegocioService:isSuccess()
			disarmTransaction()
		EndIf

	End Transaction

	FreeObj(oRestService)
	FreeObj(oNegocioService)
	FreeObj(jCmdBody)

return .T.


/*/{Protheus.doc} permiteVincular
Verifica se o modelo de negociacao permite vincular contratos com a chave enviada
@type method
@version 12
@author carlos.augusto
@since 26/08/2025
@return logical, true
/*/
method permiteVincular() as logical class agdNegocioApi
	Local oNegocioService  as Object
	Local oRestService     as Object
	Local cIdNegocioBarter as character

	oRestService := agdRestUtils():New()

	cIdNegocioBarter := oRestService:getIDWithPathParams(Self:getPathParamNegocio())
	If !Empty(cIdNegocioBarter)
		cIdNegocioBarter := FWxFilial("NEA") + cIdNegocioBarter
		oNegocioService := agdNegocioService():New()
		oNegocioService:verificaVincularContrato(cIdNegocioBarter)
		oRestService:setAPIResponse(oRest, oNegocioService)
	EndIf

	FreeObj(oNegocioService)
	FreeObj(oRestService)

Return .T.

