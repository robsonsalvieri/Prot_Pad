#INCLUDE "APWEBSRV.CH"
#INCLUDE 'APWEBEX.CH'             
#INCLUDE "PROTHEUS.CH"
/*/


Ŀ
Funo    PLSMCTMD   Autor Alexander Santos        Data 07.03.2006  
Ĵ
Descrio  Web Service das funcionalidades do Plano de Saude            
ٱ


/*/
//Ŀ
//Definicao da estruturas utilizadas para profissiona de saude			   
//
WSSTRUCT EspRda
	WSDATA CodEsp			AS String OPTIONAL //Codigo da especialidade
	WSDATA DesEsp			AS String OPTIONAL //Nome da especialidade
	WSDATA CodCBOS			AS String OPTIONAL //Codigo CBOS
	WSDATA DesCBOS			AS String OPTIONAL //Descricao CBOS
ENDWSSTRUCT 
WSSTRUCT PesqEsp
	WSDATA Rda  			AS String OPTIONAL 
	WSDATA CodLoc  			AS String OPTIONAL 
	WSDATA Tp	  			AS String OPTIONAL 
ENDWSSTRUCT   


//Ŀ
//Definicao da estruturas utilizadas para mostrar os planos utilizados	   
//
WSSTRUCT PlanosRda
	WSDATA CodPlano			AS String OPTIONAL //Codigo do plano
	WSDATA DesPlano			AS String OPTIONAL //Nome do plano
ENDWSSTRUCT
//Ŀ
//Definicao da estruturas utilizadas pelas RDA                            
//
WSSTRUCT RDAView
	
	WSDATA TissVer			AS String OPTIONAL 			//Verso TISS
	WSDATA Code				AS String OPTIONAL 			//Codigo da RDA
	WSDATA Name				AS String OPTIONAL 			//Nome da RDA
	WSDATA RegAtend			AS String OPTIONAL 			//Regime de Atendimento
	WSDATA TpPrest			AS String OPTIONAL 			//Tipo de Prestador
	WSDATA CnpfCpf			AS String OPTIONAL 			//Cnpj ou Cpf
	WSDATA Cnes				AS String OPTIONAL 			//Codigo CNES
	WSDATA TpLograd 		AS String OPTIONAL 			//Tp Logradouro
	WSDATA Address			AS String OPTIONAL 			//Endereco
	WSDATA Numero			AS String OPTIONAL 			//Numero
	WSDATA Complen			AS String OPTIONAL 			//Complemento
	WSDATA District			AS String OPTIONAL 			//Municipio
	WSDATA Zone				AS String OPTIONAL 			//Bairro
	WSDATA State			AS String OPTIONAL 			//Estado
	WSDATA IBGE				AS String OPTIONAL 			//Codigo IBGE
	WSDATA Cep				AS String OPTIONAL 			//Cep
	WSDATA RegAns			AS String OPTIONAL 			//Registro ANS
	WSDATA Local			AS String OPTIONAL 			//Local de Atend.
	WSDATA Speciality		AS String OPTIONAL 			//Especialidade
	WSDATA PhoneNumber		AS String OPTIONAL 			//Telefone
	WSDATA SigCr			AS String OPTIONAL 			//Sigla CRM
	WSDATA CodCr	 		AS String OPTIONAL 			//Codigo CRM
	WSDATA EstCr			AS String OPTIONAL 			//Estado CRM
	WSDATA TipPE			AS String OPTIONAL 			//Pessoa juridica ou fisica
	WSDATA CodBB0			AS String OPTIONAL 			//Codigo do profissional de saude
	WSDATA CodOpe			AS String OPTIONAL 			//Codigo da Operadora da Rda
	WSDATA CodLoc			AS String OPTIONAL 			//Codigo Local de Atendimento
	WSDATA DesLoc			AS String OPTIONAL 			//Descricao do local de atendiento
	WSDATA CarSol			AS String OPTIONAL 			//Carater da Solicitacao
	WSDATA MEspRda		    AS Array Of EspRda OPTIONAL //Especialidade da rda conforme local de atendimento
	WSDATA RdaPlanos	    AS Array Of PlanosRda OPTIONAL //Planos
	WSDATA RdaRecno         AS Integer OPTIONAL		//Recno RDA
ENDWSSTRUCT
//
//Definicao da estruturas utilizadas pelas Familia
//
WSSTRUCT SFamilia
	WSDATA Code				AS String 	OPTIONAL //Matricula
	WSDATA Description		AS String 	OPTIONAL //Nome do beneficiario
	WSDATA Benefi			AS String 	OPTIONAL //Numero da Carteirinha
	WSDATA NomBen			AS String 	OPTIONAL //Nome
	WSDATA Familia 			AS String 	OPTIONAL //Familia
	WSDATA DatNas 			AS Date 	OPTIONAL //Dt Nascimento
	WSDATA Idade			AS String	OPTIONAL //Idade
	WSDATA Sexo				AS String 	OPTIONAL //Sexo
	WSDATA TipUsu			AS String 	OPTIONAL //Tipo de Usuario
	WSDATA DesUsu 			AS String 	OPTIONAL //Descricao do Tipo
	WSDATA EstCiv			AS String 	OPTIONAL //Estado Civil
	WSDATA Graupa			AS String 	OPTIONAL //Grau de parentesco
	WSDATA DatInc 			AS Date 	OPTIONAL //Data de Inclusao
	WSDATA CodPla			AS String 	OPTIONAL //Plano
	WSDATA DesPla 			AS String 	OPTIONAL //Descricao do Plano
	WSDATA Verpla			AS String 	OPTIONAL //Versao do Plano
	WSDATA DatCar			AS Date 	OPTIONAL //Data de Carencia
	WSDATA ResFam			AS String	OPTIONAL //Responsvel financeiro da famlia
ENDWSSTRUCT
//
//Definicao da estruturas subcontrato
//
WSSTRUCT SSubContrato
	WSDATA Code				AS String 					OPTIONAL //SubContrato + Versao
	WSDATA Description		AS String 					OPTIONAL //Sub-Contrato Formatado
	WSDATA SubCon			AS String 					OPTIONAL //SubContrato
	WSDATA VerSub 			AS String 					OPTIONAL //Versao SubContrato
	WSDATA NomSub			AS String 					OPTIONAL //Descricao SubContrato
	WSDATA DtVSub			AS Date	  					OPTIONAL //Data de Validade Subcontrato
	WSDATA Familia			AS Array of SFamilia		OPTIONAL //Familia
ENDWSSTRUCT
//
//Definicao da estruturas contrato
//
WSSTRUCT SContrato
	WSDATA Code				AS String 					OPTIONAL //Contrato + Versao
	WSDATA Description		AS String 					OPTIONAL //Contrato Formatado
	WSDATA TipCon			AS String 					OPTIONAL //Tipo Contrato
	WSDATA NumCon			AS String 					OPTIONAL //Contrato
	WSDATA VerCon			AS String 					OPTIONAL //Versao
	WSDATA NomCon			AS String 					OPTIONAL //Descricao Contrato
	WSDATA DatCon			AS Date 					OPTIONAL //Data Contrato
	WSDATA SubContrato		AS Array of SSubContrato	OPTIONAL //SubContrato
ENDWSSTRUCT
//
//Definicao da estruturas utilizadas pelas Empresa
//
WSSTRUCT SEmpresa
	WSDATA Code				AS String 				OPTIONAL //Codigo da Empresa
	WSDATA Description		AS String 				OPTIONAL //Nome da Empresa
	WSDATA Contrato			AS Array of SContrato 	OPTIONAL //Contrato
ENDWSSTRUCT
//
//Definicao da estruturas utilizadas pelas Empresa
//
WSSTRUCT SOperadora
	WSDATA Code				AS String 				OPTIONAL 	//Codigo da Operadora
	WSDATA Description		AS String 				OPTIONAL 	//Nome da Operadora
	WSDATA Empresa			AS Array of SEmpresa 	OPTIONAL 	//Empresa
ENDWSSTRUCT
//Ŀ
//Definicao da estruturas utilizadas para Produtos Guia Medico		     
//
WSSTRUCT ProdutosGM
	WSDATA cCode				AS String OPTIONAL 	//Codigo do Produto
	WSDATA cDescription 		AS String OPTIONAL 	//Versao do Produto
	WSDATA nValue				AS Integer OPTIONAL	//Valor
ENDWSSTRUCT
//Ŀ
//Definicao do Web Service de Controle do Usuario                         
//
WSSERVICE PLSMCTMD					DESCRIPTION "Rotinas referente ao Guia MEDICO"
	WSDATA UserCode					AS String
	WSDATA FunName					AS String
	WSDATA Tp						AS Integer OPTIONAL
	WSDATA CodEsp					AS String OPTIONAL
	WSDATA CodEst					AS String OPTIONAL
	WSDATA CodMun					AS String OPTIONAL
	WSDATA Recno					AS String OPTIONAL
	WSDATA LatLon					AS String OPTIONAL
	WSDATA WSNULL					AS String

	WSDATA ListParGM				AS Array Of GenericStruct
	WSDATA SigConselho				AS Array Of GenericStruct
	WSDATA ListEsp  				AS Array Of GenericStruct
	WSDATA Espec                    AS PesqEsp
	
	WSMETHOD getParGM				DESCRIPTION "Parametros para montagem do guia medico"
	WSMETHOD setLatLon				DESCRIPTION "Seta latitudo e longitude no endereco"
	WSMETHOD getEndLL				DESCRIPTION "Retorna endereco formatado e latitude longitude"
	WSMETHOD GetSigCon				DESCRIPTION "Lista a Sigla do Conselho"
	WSMETHOD GetSigEsp   			DESCRIPTION "Lista as Especialidades"
ENDWSSERVICE

/*/

Ŀ
Funo    getParGM  Autor   Alexander              Data 24.03.2012 
Ĵ
          Parametros para montagem do guia medico					   
ٱ


/*/
WSMETHOD getParGM WSRECEIVE UserCode, FunName, Tp, CodEsp, CodEst, CodMun WSSEND ListParGM WSSERVICE PLSMCTMD
LOCAL lRetorno 	:= .F.
LOCAL aArea 	:= GetArea()
LOCAL nI 	   	:= 1
LOCAL cSql		:= ""
local aSql		:= {}
local oFwQuery  := FWPreparedStatement():New()
local nFor		:= 0
local nQtdDad	:= 0

// Acesso ao metodo
If PrtChkUser( ::UserCode, "PLSMCTMD","getParGM" )
	
	// Executa consulta
	aSql := ExecBlock(::FunName, .F., .F., { PlAjusSQI(::Tp, "N", 1), PlAjusSQI(::CodEsp), PlAjusSQI(::CodEst) , PlAjusSQI(::CodMun) } )
	
	if !empty(aSql[1])
		//Pega o total de parmetros da query..
		nQtdDad := len(aSql[2])
		
		cSql := ChangeQuery(aSql[1])
		oFwQuery:SetQuery(cSql)
		
		//Percorremos o array com os parmetros e setamos na query
		for nFor := 1 to nQtdDad
			oFwQuery:SetString(nFor, aSql[2, nFor])
		next

		cSql   := oFwQuery:GetFixQuery()
		cAliasQry := MpSysOpenQuery(cSql)

		// Alimenta a matriz														
		If !(cAliasQry)->( Eof() )
			::ListParGM := {}
			lRetorno := .t.
			
			// Monta matriz
			While !(cAliasQry)->( Eof() )      
				AaDd( ::ListParGM, WsClassNew( "GenericStruct" ) )
				::ListParGM[nI]:Code		:= AllTrim((cAliasQry)->Codigo)
				::ListParGM[nI]:Description	:= AllTrim((cAliasQry)->Descricao)
				nI++
			(cAliasQry)->( DbSkip() )	
			EndDo
		EndIf
	
		// Fecha area
		(cAliasQry)->( DbCloseArea() )
	endif

	if !lRetorno
		SetSoapFault( "", "Nenhum registro encontrado" )
	endif
Else
	lRetorno := .F.
	SetSoapFault( "", "Usurio no autorizado" )
EndIf

RestArea( aArea )

Return lRetorno       

/*/

Ŀ
Funo    setLatLon Autor   Alexander              Data 24.03.2012 
Ĵ
          Seta latitude e longitude									   
ٱ


/*/
WSMETHOD setLatLon WSRECEIVE UserCode, Recno, LatLon WSSEND WSNULL WSSERVICE PLSMCTMD
LOCAL lRetorno 	:= .T.
LOCAL aArea 	:= GetArea()                               
::WSNULL 		:= ""
//
//Acesso ao metodo
//
If PrtChkUser( ::UserCode, "PLSMCTMD","setLatLon" )

	If BB8->( FieldPos("BB8_LATLON") ) > 0
		//
		//Posiciona no registro
		//
	    BB8->( DbGoTo( Val(::Recno) ) )
		//
		//Altera a latitude
		//
		BB8->( Reclock("BB8",.F.) )
			BB8->BB8_LATLON := AllTrim(StrTran(StrTran(::LatLon,')',''),'(',''))
		BB8->( MsUnlock() )
	EndIf	
Else
	lRetorno := .F.
	SetSoapFault( "", "Usurio no autorizado" )
EndIf

RestArea( aArea )
//
// Fim da Rotina
//
Return lRetorno
/*/

Ŀ
Funo    getEndLL  Autor   Alexander              Data 24.03.2012 
Ĵ
          Retorna latitude e longitude								   
ٱ


/*/
WSMETHOD getEndLL WSRECEIVE UserCode, Recno WSSEND WSNULL WSSERVICE PLSMCTMD
LOCAL lRetorno 	:= .T.
LOCAL aArea 	:= GetArea()                               
LOCAL cPais 	:= "BRASIL"
LOCAL cEstado 	:= ""
LOCAL cCidade 	:= ""
LOCAL cBairro 	:= ""
LOCAL cEnd		:= ""
LOCAL cNumero 	:= ""
LOCAL cEndBus	:= ""
LOCAL cPart1 	:= ""
LOCAL cPart2 	:= ""

::WSNULL := "true|"
//
//Acesso ao metodo
//
If PrtChkUser( ::UserCode, "PLSMCTMD","getEndLL" )
	//
	//Posiciona no registro
	//
    BB8->( DbGoTo( Val(::Recno) ) )
	//
	//Latitude e Longitude
	//
	If BB8->( FieldPos("BB8_LATLON") ) > 0
		cLatLon := AllTrim(BB8->BB8_LATLON)
	EndIf	                
	//
	//Endereco de busca
	//
	cEnd 	:= Iif(!Empty(BB8->BB8_END),AllTrim(BB8->BB8_END)+", ","")
	cNumero	:= Iif(!Empty(BB8->BB8_NR_END),AllTrim(BB8->BB8_NR_END)+", ","")
	cBairro	:= Iif(!Empty(BB8->BB8_BAIRRO),AllTrim(BB8->BB8_BAIRRO)+", ","")
	cCidade	:= Iif(!Empty(BB8->BB8_MUN),AllTrim(BB8->BB8_MUN)+", ","")
	cEstado	:= Iif(!Empty(BB8->BB8_EST),AllTrim(BB8->BB8_EST)+", ","")
	cTelefone	:= Iif(!Empty(BB8->BB8_TEL),AllTrim(BB8->(BB8_DDD+BB8_TEL)),AllTrim(BB8->BB8_FAX) )

	//cEndBus := cEnd+cNumero+cBairro+cCidade+cEstado+cPais
	cEndBus := cEnd+cNumero+cCidade+cEstado+cPais
	//
	//Endereco mostrado
	//
	cEstado	:= AllTrim(BB8->BB8_EST)
	cCidade	:= AllTrim(BB8->BB8_MUN)
	cBairro	:= AllTrim(BB8->BB8_BAIRRO)
	cEnd 	:= AllTrim(BB8->BB8_END)
	cNumero	:= AllTrim(BB8->BB8_NR_END)

	cPart1 := cEstado + ", " + capitalize(cCidade)
	If Empty(cEstado) .Or. Empty(cCidade)
		cPart1 := StrTran(cPart1,',','')
	EndIf

	cPart2 := capitalize(cEnd) + ", " + cNumero
	If Empty(cEnd) .Or. Empty(cNumero)
		cPart2 := StrTran(cPart2,',','')
	EndIf
	//
	// Endereco do hint
	//
	cShowEnd := cPart1 + "</br>" + capitalize(cBairro) + "</br>" + cPart2
	 
	::WSNULL := "true|" + cEndBus + "|" + cShowEnd + "|" + cLatLon + "|" + cTelefone
Else
	lRetorno := .F.
	SetSoapFault( "", "Usurio no autorizado" )
EndIf

RestArea( aArea )
//
// Fim da Rotina
//
Return lRetorno
/*/

Ŀ
Funo    getSigCon Autor   Luiz Couto             Data 13.07.2004 
Ĵ
          Retorna sigla												   
ٱ


/*/
WSMETHOD getSigCon WSRECEIVE UserCode WSSEND SigConselho WSSERVICE PLSMCTMD
LOCAL nI			:= 0
LOCAL lRetorno 		:= .T.
LOCAL aArea 		:= GetArea()
//Ŀ
// Valida acesso														   
//	    
If PrtChkUser( ::UserCode, "PLSMCTMD","getSigCon" )
	//Ŀ
	// Ordem																   
	//	    
    BAH->( DbSetOrder(1) )//BAH_FILIAL + BAH_CODIGO
    BAH->( DbGoTop() )
	//Ŀ
	// While para montar matriz											   
	//	    
	::SigConselho := {}
    While !BAH->( Eof() )
        nI++
		//Ŀ
		// Monta matriz														   
		//	    
		aAdd( ::SigConselho, WsClassNew( "GenericStruct" ) )
		::SigConselho[nI]:Code 		  	:= BAH->BAH_CODIGO
		::SigConselho[nI]:Description	:= AllTrim(BAH->BAH_CODIGO)
    BAH->( DbSkip() )
    EndDo
Else
	lRetorno := .F.
	SetSoapFault( "", "Usurio no autorizado" )
EndIf

RestArea( aArea )

Return lRetorno

/*


ͻ
Programa  getSigEsp Autor  Patricia Duca        Data   13/11/09   
͹
Desc.      Lista especialidades                                       
ͼ


*/

WSMETHOD getSigEsp WSRECEIVE UserCode,Espec WSSEND ListEsp WSSERVICE PLSMCTMD
LOCAL nI			:= 0
LOCAL lRetorno 		:= .T.
LOCAL aArea 		:= GetArea()
LOCAL cQuery    	:= ""           
//Ŀ
// Valida acesso														   
//	    
If PrtChkUser( ::UserCode, "PLSMCTMD","getSigEsp" )
                           
	If ::Espec:Tp <> nil .and. ::Espec:Tp <> '2' 
		cQuery:="SELECT BAX_CODIGO,BAX_CODLOC,BAX_CODESP CODESP, BAQ_DESCRI DESCRI"
		cQuery+="FROM "+RetSqlName("BAX")+" BAX "
		cQuery+="INNER JOIN "+RetSqlName("BAQ")+" BAQ ON BAQ_CODESP=BAX_CODESP AND BAQ_FILIAL='"+xFilial("BAQ")+"' AND BAQ.D_E_L_E_T_=' ' "
		cQuery+="WHERE BAX.D_E_L_E_T_=' ' AND  "
		cQuery+="BAX_CODIGO='"+::Espec:RDA+"'  AND "
		cQuery+="BAX_FILIAL='"+xfilial("BAX")+"' AND "
		cQuery+="BAX_CODINT='"+ PLSINTPAD()+"'AND "
		cQuery+="BAX_CODLOC='"+::Espec:CODLOC+"' "
		cQuery+="ORDER BY BAX_CODESP "
	Else	//na solicitacao tenho que mostrar todas as especialidades, pois qq medico ode solicitar de qq especialidade    
		cQuery:="SELECT BAQ_CODESP CODESP,BAQ_DESCRI DESCRI"
		cQuery+="FROM "+RetSqlName("BAQ")+" BAQ "
		cQuery+="WHERE BAQ.D_E_L_E_T_=' ' "
		cQuery+="ORDER BY BAQ_CODESP "
	Endif      
	OPEN QUERY cQuery ALIAS "GETSIGESP"
	
	GETSIGESP->( DbGoTop() )
	//Ŀ
	// While para montar matriz											   
	//	    
	::ListEsp := {}
    While !GETSIGESP->( Eof() )
        nI++
		//Ŀ
		// Monta matriz														   
		//	    
		aAdd( ::ListEsp, WsClassNew( "GenericStruct" ) )
		::ListEsp[nI]:Code 		  	:= GETSIGESP->CODESP
		::ListEsp[nI]:Description	:= AllTrim(GETSIGESP->DESCRI)
    GETSIGESP->( DbSkip() )
    EndDo
Else
	lRetorno := .F.
	SetSoapFault( "", "Usurio no autorizado" )
EndIf

CLOSE QUERY "GETSIGESP"

RestArea( aArea )
//
// Fim da Rotina
//	    
Return lRetorno
/*


ͻ
Programa  capitalizeAutor  Patricia Duca        Data   13/11/09   
͹
Desc.      Primeira letra maiuscula o resto minusculo				  
ͼ


*/
Static Function capitalize(cString)
//
// Primeiro caracter maiusculo
//	    
cString := Upper(SubStr(cString,1,1)) + Lower(SubStr(cString,2,Len(cString)))
//
// Fim da Rotina
//	    
Return cString


//Ŀ
// Para poder compilar													   
//	    
Function WSPLMCTMDS
Return
