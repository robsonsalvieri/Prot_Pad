#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
/*/ 


Ŀ
Funo     PLSRELT   Autor Alexander Santos        Data 29.04.2007  
Ĵ
Descrio  Web Service das funcionalidades do Plano de Saude            
ٱ


/*/                                 
//
//Strutura de parametros de entrada
//
WSSTRUCT SInPar
	WSDATA Rda						AS String 	OPTIONAL
	WSDATA RdaHos					AS String 	OPTIONAL
	WSDATA RdaMed					AS String 	OPTIONAL
	WSDATA Ano						AS String 	OPTIONAL
	WSDATA Mes						AS String 	OPTIONAL
	WSDATA Tp						AS String 	OPTIONAL
	WSDATA DtDe						AS Date 	OPTIONAL
	WSDATA DtAte					AS Date 	OPTIONAL
	WSDATA Guia						AS String 	OPTIONAL
	WSDATA Tipo						AS String 	OPTIONAL
	WSDATA CodLoc					AS String 	OPTIONAL
	WSDATA Recno					AS Integer 	OPTIONAL
	WSDATA Alias					AS String 	OPTIONAL
	WSDATA Matric					AS String 	OPTIONAL
	WSDATA CodEmp					AS String 	OPTIONAL
	WSDATA NumCon					AS String 	OPTIONAL
	WSDATA SubCon					AS String 	OPTIONAL
	WSDATA Operac					AS String 	OPTIONAL
	WSDATA Situac					AS String 	OPTIONAL
	WSDATA XmlFile					AS String 	OPTIONAL
	WSDATA NumAut					AS String 	OPTIONAL
	WSDATA Sequen					AS String 	OPTIONAL
	WSDATA Protocolo				AS String 	OPTIONAL
	WSDATA ChaveGen					AS String 	OPTIONAL // Chave Generica pra ser utilizada em qqer relatorio novo
	WSDATA DtDeO					AS Date		OPTIONAL // Caso haja outras datas envolvidas
	WSDATA DtAteO					AS Date		OPTIONAL // Caso haja outras datas envolvidas
	WSDATA Prorrog					AS String	OPTIONAL 
	WSDATA TpPortS					AS String  OPTIONAL
ENDWSSTRUCT
//Ŀ
//Definicao do Web Service de Controle do Usuario                         
//
WSSERVICE PLSRELT					DESCRIPTION "Relatorio do modulo Plano de Saude"
	WSDATA UserCode					AS String 
	WSDATA FunName					As String OPTIONAL
	WSDATA InPar					AS SInPar OPTIONAL
	WSDATA WSNULL					AS String OPTIONAL
	WSDATA ConteudoRetorno			AS Base64Binary OPTIONAL
	WSDATA UserLGPD                 AS String OPTIONAL
	WSDATA RotinLGPD                AS String OPTIONAL
	 
	WSMETHOD REL673S	DESCRIPTION "Demonstrativo de Anlise do Processamento de Conta"
	WSMETHOD REL018S	DESCRIPTION "Demosntrativo de Pagamento e Extrato de Pagamento e Odonto"
	WSMETHOD REL754S	DESCRIPTION "Extrato de Utilizao"
	WSMETHOD getRelGEN	DESCRIPTION "Exibe relatorio conforme processamento"
	WSMETHOD LogLGPD    DESCRIPTION "Registra acesso do usuario conforme LGPD"

ENDWSSERVICE
/*/

Ŀ
Funo    REL018S   Autor   Alexander Santos       Data 29.04.2007 
Ĵ
          Demosntrativo de Pagamento								   
ٱ


/*/
WSMETHOD REL018S WSRECEIVE UserCode, FunName, InPar WSSEND ConteudoRetorno WSSERVICE PLSRELT
LOCAL lRetorno := .T.
LOCAL aRet	   := {}	                            
//
//Verifica acesso														   
//
If PrtChkUser( ::UserCode, "PLSRELT", "REL018S" )
	Do Case
		Case ::InPar:Tp == '1'
		    ::ConteudoRetorno 	:= __RELDIR + "PLSR018S" + AllTrim(::InPar:Rda) + ".##R"
			aRet				:= PLSR018S(::InPar:Rda, ::InPar:Ano, ::InPar:Mes)
		Case  ::InPar:Tp == '2'
			If ExistBlock('PLSR018RD')
		    	::ConteudoRetorno 	:= __RELDIR + "PLSR018RD" + AllTrim(::InPar:Rda) + ".##R"
				aRet				:= ExecBlock("PLSR018RD",.f.,.f.,{::InPar:Rda, ::InPar:Ano, ::InPar:Mes} )
			EndIf	
		Case  ::InPar:Tp == '3'
		    ::ConteudoRetorno 	:= __RELDIR + "PLSR018OD" + AllTrim(::InPar:Rda) + ".##R"
			aRet				:= PLSR018OD(::InPar:Rda, ::InPar:Ano, ::InPar:Mes)
	EndCase
	//Ŀ
	//Caso nao tenho movimentacao											   
	//
	If !aRet[1]
		SetSoapFault( "", aRet[2] )
		Return .F.
	EndIf                                             
Else
	lRetorno := .F.
EndIf
//Ŀ
//Fim da Rotina														   
//
Return lRetorno
/*/

Ŀ
Funo    REL673S   Autor   Alexander Santos       Data 29.04.2007 
Ĵ
          Demonstrativo de Anlise da Conta Mdica					   
ٱ


/*/
WSMETHOD REL673S WSRECEIVE UserCode, FunName, InPar WSSEND ConteudoRetorno WSSERVICE PLSRELT
LOCAL lRetorno := .T.
LOCAL aRet	   := {}	                           
//Ŀ
// Valida acesso														   
//	    
If PrtChkUser( ::UserCode, "PLSRELT", "REL673S" )
	//Ŀ
	// Executa o relatorio													   
	//	    
	::ConteudoRetorno 	:= __RELDIR + "PLSR673S"+AllTrim(::InPar:Rda+::InPar:CodLoc)+".##R"
	aRet				:= PLSR673S(::InPar:Rda, ::InPar:CodLoc, ::InPar:Ano, ::InPar:Mes)
	//Ŀ
	// Verifica o retorno													   
	//	    
	If !aRet[1]
		SetSoapFault( "", aRet[2] )
		Return .F.
	EndIf
Else
	lRetorno := .F.
EndIf
//Ŀ
// Fim da rotina														   
//	    
Return lRetorno
/*/

Ŀ
Funo    REL754S   Autor   Alexander Santos       Data 29.04.2007 
Ĵ
          Demonstrativo de Anlise da Conta Mdica					   
ٱ


/*/
WSMETHOD REL754S WSRECEIVE UserCode, FunName, InPar WSSEND ConteudoRetorno WSSERVICE PLSRELT
LOCAL lRetorno 	:= .T.

If PrtChkUser( ::UserCode, "PLSRELT", "REL754S" )
	//Ŀ
	// Nome do arquivo														   
	//	    
	::ConteudoRetorno 	:= __RELDIR + "PLSR754S"+AllTrim(::InPar:Rda+::InPar:CodLoc)+".##R"
	aRet				:= PLSR754S(::InPar:Rda, ::InPar:CodLoc, DToC(::InPar:DtDe), DToC(::InPar:DtAte), ::InPar:Guia, ::InPar:Tipo)
	//Ŀ
	// Verifica o retorno													   
	//	    
	If !aRet[1]
		SetSoapFault( "", aRet[2] )
		Return .F.
	EndIf
Else
	lRetorno := .F.
EndIf
//Ŀ
// Fim da rotina														   
//	    
Return lRetorno     
/*/

Ŀ
Funo    getRelGEN Autor   Alexander Santos       Data 16.03.2012 
Ĵ
          Retorna arquivo htm  para impressao do relatorio	   		   
ٱ


/*/
WSMETHOD getRelGEN WSRECEIVE UserCode, FunName, InPar WSSEND WSNULL WSSERVICE PLSRELT
LOCAL lRetorno 	:= .T.           
LOCAL cArq			:= ""
LOCAL aMsg 		:= {}
LOCAL cMsgErro	:= ""


//
// Valida acesso
//	    
If PrtChkUser( ::UserCode, "PLSRELT", "getRelGEN" )
	//
	// Executa relatorio
	//
	If "PPRELST" $ ::FunName .AND. FindFunction("PLSTISSVER") // Se for os relatorios TISS eu passo a verso utilizada
		aRet 	:= ExecBlock(::FunName, .F., .F., { ::InPar, GetNewPar("MV_PWAITR",.T.) , PLSTISSVER(), GetNewPar("MV_IGUINE",.F.) } )		
	Else	    
		aRet 	:= ExecBlock(::FunName, .F., .F., { ::InPar, GetNewPar("MV_PWAITR",.T.)  } )
	EndIf
	
	//Pegar a mensagem de plugin para visualizar o PDF
	aMsg := PLSRETMSG(::InPar:TpPortS , nil, "POR", "PPLRELGEN")
	
	if valtype(aRet) <> "U"
		
		If EMPTY(aRet[2]) 
			cArq := IIf(ValType(aRet[1]) == "C", aRet[1], "") + iif(len(aMsg)>0, "|" + aMsg[1][2], "")
		Else
			cMsgErro := aRet[2]
		EndIf 
	endIf	
	//
	// Retorno
	//

	::WSNULL := cArq + "|" + cMsgErro
Else
	lRetorno := .F.
	SetSoapFault( "", "No autorizado a utilizao do metodo" )
EndIf
//
// Fim da rotina														   
//	    
Return lRetorno
//
// Para poder compilar													   
//	    
Function WSPLSRELT()
Return


//-------------------------------------------------------------------
/*/{Protheus.doc} LogLGPD
Loga os webservices acionados pelo Portal

@author sakai
@since 10/12/2020
@version P12
/*/
//-------------------------------------------------------------------
WSMETHOD LogLGPD WSRECEIVE UserCode,UserLGPD,RotinLGPD WSSEND WSNULL WSSERVICE PLSRELT

	LOCAL aArea 	:= GetArea()
	LOCAL lRetorno := .T.

	if PrtChkUser( self:UserCode , "PLSRELT","LogLGPD" )
		if !empty(self:UserLGPD) .And. !empty(self:RotinLGPD)
			PLogPortLG(self:RotinLGPD,self:UserLGPD)
			::WSNULL := "true|true" 
		else
			::WSNULL := "true|false"  
		endif
	else
		lRetorno := .F.
		SetSoapFault( "", "No autorizado a utilizao do metodo" )
	endIf

	RestArea( aArea )

Return lRetorno