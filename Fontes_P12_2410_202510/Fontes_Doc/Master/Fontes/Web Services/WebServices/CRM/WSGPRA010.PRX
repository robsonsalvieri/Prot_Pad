#INCLUDE "PROTHEUS.CH"
#INCLUDE "APWEBSRV.CH"

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณTipos de Questaoณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
#DEFINE UNICA_ESCOLHA 		"1"		//Unica Escolha
#DEFINE MULTIPLA_ESCOLHA 	"2"		//Multipla Escolha
#DEFINE DISSERTATIVA 		"3"		//Dissertativa

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณTipos de exibicao da alternativaณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
#DEFINE SEQUENCIAL 			"1"		//Sequencial
#DEFINE COLUNAS 				"2"		//Colunas
#DEFINE LINHAS 				"3"		//Linhas

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัอออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณWSGPRA010 บAutor  ณLuiz Felipe Couto    บ Data ณ  23/02/05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯอออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Este fonte possui as funcoes utilizadas para inclusao,      บฑฑ
ฑฑบ          ณ alteracao, exclusao e busca de Pesquisas.                   บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ                                                             บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPR     						                           บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑบAnalista  ณ Data/Bops/Ver ณManutencao Efetuada                      	   บฑฑ
ฑฑฬออออออออออุออออออออัออออออุอออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑศออออออออออฯออออออออฯออออออฯอออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณDefinicao do WebServices de Pesquisaณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
WSSERVICE GPRResearch DESCRIPTION "" //NAMESPACE "http://.apw"
	WSDATA UserCode			AS String									//Codigo do Usuario (seguranca WS)
	WSDATA QueryAddWhere	AS String OPTIONAL							//Filtro
	WSDATA PageLen			AS Integer OPTIONAL							//Limite de registros por Pagina (paginacao)
	WSDATA PageFirst		AS Integer OPTIONAL							//Primeira Pagina (paginacao)
	WSDATA IndexKey 		AS String OPTIONAL							//Ordenacao
	WSDATA ResearchCode		AS String									//Codigo da Pesquisa
	WSDATA ListOfResearch 	AS Array Of ResearchView					//Lista de Pesquisas
	WSDATA Research			AS ResearchView								//Pesquisa

	WSMETHOD BrwResearch DESCRIPTION ""
	WSMETHOD GetResearch DESCRIPTION ""
	WSMETHOD PutResearchResult DESCRIPTION ""
ENDWSSERVICE

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัอออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณBrwResearcบAutor  ณLuiz Felipe Couto    บ Data ณ  23/02/05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯอออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Retorna as Pesquisas cadastradas no sistema.                บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ ExpC1 : Codigo do Usuario WS                                บฑฑ
ฑฑบ          ณ ExpC2 : Filtro                                              บฑฑ
ฑฑบ          ณ ExpN3 : Limite de registros por pagina (paginacao)          บฑฑ
ฑฑบ          ณ ExpN4 : Primeira pagina (paginacao)                         บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPR     						                           บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑบAnalista  ณ Data/Bops/Ver ณManutencao Efetuada                      	   บฑฑ
ฑฑฬออออออออออุออออออออัออออออุอออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑศออออออออออฯออออออออฯออออออฯอออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
WSMETHOD BrwResearch WSRECEIVE UserCode, QueryAddWhere, PageLen, PageFirst, IndexKey WSSEND ListOfResearch WSSERVICE GPRResearch

Local lRetorno 				:= .T.					//Retorno da Funcao
Local aArea					:= GetArea()			//Area atual
Local cAliasSKC 			:= "SKC"				//Alias da tabela SKC
Local cQuery 				:= ""					//String para armazenagem da Query
Local aStruSKC 				:= {}					//Estrutura da tabela SKC
Local nI					:= 0					//Variavel de apoio
Local nJ					:= 0					//Variavel de apoio

SKC->( DbSetOrder( 1 ) )

DEFAULT ::QueryAddWhere 	:= ""					//Filtro
DEFAULT ::PageLen 			:= 0					//Limite de registros por pagina (paginacao)
DEFAULT ::PageFirst 		:= 0					//Primeira pagina (paginacao)
DEFAULT ::IndexKey 			:= SKC->( IndexKey() )	//Ordenacao

If ::UserCode == "MSALPHA"
	aStruSKC 	:= SKC->( DbStruct() )
	cAliasSKC 	:= GetNextAlias()
	
	cQuery 	:= "SELECT * "
	cQuery 	+= "FROM " + RetSqlName( "SKC" ) + " SKC "
	cQuery 	+= "WHERE "
	cQuery 	+= "SKC.KC_FILIAL = '" + xFilial( "SKC" ) + "' AND "
	cQuery 	+= "SKC.D_E_L_E_T_ = ' ' "
	cQuery 	:= WsQueryAdd( cQuery, ::QueryAddWhere )
	cQuery 	+= "ORDER BY " + WsSqlOrder( ::IndexKey )
	
	cQuery 	:= ChangeQuery( cQuery )
	
	DbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), cAliasSKC, .T., .T. )
	
	For nI := 1 To Len( aStruSKC )
		If aStruSKC[nI][2] <> "C" .AND. aStruSKC[nI][2] <> "M"
			TcSetField( cAliasSKC, aStruSKC[nI][1], aStruSKC[nI][2], aStruSKC[nI][3], aStruSKC[nI][4] )
		EndIf
	Next nI
	
	nI := 0
	
	While !( cAliasSKC )->( EOF() )
		nI++
	
		If ::PageFirst == 0 .OR. nI >= ::PageFirst
			If nJ == 0
				::ListOfResearch := {}
			EndIf
			
			aAdd( ::ListOfResearch, WsClassNew( "ResearchView" ) )

			nJ++

			GetResearch( @::ListOfResearch[nJ], cAliasSKC )
		EndIf
	
		If nJ >= ::PageLen .And. ::PageLen <> 0
			Exit
		EndIf
		
		( cAliasSKC )->( DbSkip() )
	End
	
	( cAliasSKC )->( DbCloseArea() )
Else
	lRetorno := .F.
	SetSoapFault( "BRWRESEARCH", "UserCode invalido." )
EndIf

RestArea( aArea )

Return lRetorno

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัอออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGetResearcบAutor  ณLuiz Felipe Couto    บ Data ณ  23/02/05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯอออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Retorna uma pesquisa cadastrada no sistema.                 บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ ExpC1 : Codigo do Usuario WS                                บฑฑ
ฑฑบ          ณ ExpC2 : Codigo da Pesquisa                                  บฑฑ
ฑฑบ          ณ ExpC3 : Filtro                                              บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPR     						                           บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑบAnalista  ณ Data/Bops/Ver ณManutencao Efetuada                      	   บฑฑ
ฑฑฬออออออออออุออออออออัออออออุอออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบMichel M. |08/05/07|8.11  |Bops:125285 - Corrigido a montagem do objeto บฑฑ
ฑฑบ          |        |      |Questionary para agrupar em Grupo e SubGrupo บฑฑ   
ฑฑบ          |        |      |as questoes em uma pesquisa.                 บฑฑ
ฑฑบ          |        |      |                                             บฑฑ
ฑฑศออออออออออฯออออออออฯออออออฯอออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
WSMETHOD GetResearch WSRECEIVE UserCode, ResearchCode, QueryAddWhere WSSEND Research WSSERVICE GPRResearch

Local lRetorno 				:= .T.			//Retorno da Funcao
Local aArea					:= GetArea()	//Area atual
Local cAliasSKC				:= "SKC"		//Alias da tabela SKC
Local cAliasSKD				:= "SKD"		//Alias da tabela SKD
Local cAliasSKE				:= "SKE"		//Alias da tabela SKE
Local cAliasSKF				:= "SKF"		//Alias da tabela SKF
Local cAliasSKG				:= "SKG"		//Alias da tabela SKG
Local cAliasSKH				:= "SKH"		//Alias da tabela SKH
Local cAliasSKI				:= "SKI"		//Alias da tabela SKI
Local cAliasSKP				:= "SKP"		//Alias da tabela SKP
Local cAliasSKR				:= "SKR"		//Alias da tabela SKR
Local nLenQuest				:= 0			//Ultima posicao do array de Questionario
Local nLenGroup				:= 0			//Ultima posicao do array de Grupo
Local nLenQst				:= 0			//Ultima posicao do array de Questao
Local nLenOption			:= 0			//Ultima posicao do array de Opcao
Local nLenAlt				:= 0			//Ultima posicao do array de Alternativa
Local nLenSubGroup			:= 0			//Ultima posicao do array de SubGrupo
Local nPosSubGroup			:= 0			//Posicao no array do SubGrupo
Local oObjTmp				:= 0			//Objeto de apoio (temporario)

DEFAULT ::QueryAddWhere 	:= ""			//Filtro

If ::UserCode == "MSALPHA"
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณTabelas:                     ณ
	//ณSKC - Pesquisa               ณ
	//ณSKD - Questionario x Pesquisaณ
	//ณSKE - Grupo                  ณ
	//ณSKF - Grupo x SubGrupo       ณ
	//ณSKG - Questao                ณ
	//ณSKH - Alternativa            ณ
	//ณSKI - Linha e Coluna         ณ
	//ณSKP - Questionario           ณ
	//ณSKR - Questao x Questionario ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณBusca Pesquisaณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	( cAliasSKC )->( DbSetOrder( 1 ) )
	
	If ( cAliasSKC )->( DbSeek( xFilial( "SKC" ) + ::ResearchCode ) )
		::Research := WsClassNew( "ResearchView" )
		
		GetResearch( @::Research, cAliasSKC )

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณBusca todos os Questionarios da Pesquisaณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		( cAliasSKD )->( DbSetOrder( 1 ) )
	
		If ( cAliasSKD )->( DbSeek( xFilial( "SKD" ) + ( cAliasSKC )->KC_CODPQ ) )
			::Research:ListOfQuestionary := {}
		
			While !( cAliasSKD )->( EOF() ) .AND. ( cAliasSKD )->KD_FILIAL == xFilial( "SKD" ) .AND. ;
				( cAliasSKD )->KD_CODPQ == ( cAliasSKC )->KC_CODPQ
				
				//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
				//ณBusca Dados do Questionarioณ
				//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
				( cAliasSKP )->( DbSetOrder( 1 ) )
				
				If ( cAliasSKP )->( DbSeek( xFilial( "SKP" ) + ( cAliasSKD )->KD_CODQT ) )
					aAdd( ::Research:ListOfQuestionary, WsClassNew( "QuestionaryView" ) )
					nLenQuest := Len( ::Research:ListOfQuestionary )
					
					GetQuest( @::Research:ListOfQuestionary[nLenQuest], cAliasSKP )
				EndIf

				//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
				//ณBusca as Questoes do Questionarioณ
				//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
				( cAliasSKR )->( DbSetOrder( 1 ) )
				
				If ( cAliasSKR )->( DbSeek( xFilial( "SKR" ) + ( cAliasSKD )->KD_CODQT ) )
					::Research:ListOfQuestionary[nLenQuest]:ListOfGroup 	:= {}
					::Research:ListOfQuestionary[nLenQuest]:ListOfQuestion 	:= {}
					::Research:ListOfQuestionary[nLenQuest]:ListOfItem 		:= {}

					While !( cAliasSKR )->( EOF() ) .AND. ( cAliasSKR )->KR_FILIAL == xFilial( "SKR" ) .AND. ;
						( cAliasSKR )->KR_CODQUE == ( cAliasSKD )->KD_CODQT
						
						aAdd( ::Research:ListOfQuestionary[nLenQuest]:ListOfItem, ( cAliasSKR )->KR_ITEM )
						
						//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
						//ณTipos de Busca:                                       ณ
						//ณ1 - Todas as questoes do Grupo                        ณ
						//ณ2 - Todas as questoes do Grupo e SubGrupo             ณ
						//|3 - Uma questao do Grupo                              |
						//ณ4 - Uma questao do Grupo e do SubGrupo                |
						//|5 - Todas as questoes independente do Grupo e SubGrupoณ
						//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
						Do Case
							//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
							//ณBusca todas as Questoes do Grupoณ
							//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
							Case !Empty( ( cAliasSKR )->KR_CODGRU ) .AND. ;
								Empty( ( cAliasSKR )->KR_CODSGR ) .AND. ;
								Empty( ( cAliasSKR )->KR_CODQST )
								
								//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
								//ณBusca dados do Grupoณ
								//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
								( cAliasSKE )->( DbSetOrder( 1 ) )
		
								If ( cAliasSKE )->( DbSeek( xFilial( "SKE" ) + ( cAliasSKR )->KR_CODGRU ) )
									aAdd( ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup, WsClassNew( "GroupView" ) )
									nLenGroup := Len( ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup )
									
									GetGroup( @::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup], cAliasSKE, cAliasSKR )

									//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
									//ณBusca dados do SubGrupoณ
									//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
									( cAliasSKF )->( DbSetOrder( 1 ) )
									
									If ( cAliasSKF )->( DbSeek( xFilial( "SKF" ) + ( cAliasSKE )->KE_CODGRP ) )
										::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup := {}
										
										While !( cAliasSKF )->( EOF() ) .AND. ;
											( cAliasSKF )->KF_FILIAL == xFilial( "SKF" ) .AND. ;
											( cAliasSKF )->KF_CODGRP == ( cAliasSKE )->KE_CODGRP
											
											aAdd( ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup, WsClassNew( "SubGroupView" ) )
											nLenSubGroup := Len( ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup )
											
											GetSubGroup( @::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup[nLenSubGroup], cAliasSKF, cAliasSKR )
											
											( cAliasSKF )->( DbSkip() )
										End
									EndIf

									//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
									//ณBusca dados da questaoณ
									//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
									( cAliasSKG )->( DbSetOrder( 2 ) )
									
									If ( cAliasSKG )->( DbSeek( xFilial( "SKG" ) + ( cAliasSKR )->KR_CODGRU ) )
										While !( cAliasSKG )->( EOF() ) .AND. ;
											( cAliasSKG )->KG_FILIAL == xFilial( "SKG" ) .AND. ;
											( cAliasSKG )->KG_CODGRP == ( cAliasSKR )->KR_CODGRU
											
											If Empty( ( cAliasSKG )->KG_CODSUBG )
												oObjTmp := ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]
												
												If Empty( oObjTmp:ListOfQuestion )
													oObjTmp:ListOfQuestion := {}
												EndIf
											Else
												nPosSubGroup := aScan( ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup, { |x| x:Code == ( cAliasSKG )->KG_CODSUBG } )
												
												If nPosSubGroup > 0
													oObjTmp := ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup[nPosSubGroup]
													
													If Empty( oObjTmp:ListOfQuestion )
														oObjTmp:ListOfQuestion := {}
													EndIf
												EndIf
											EndIf
											
											aAdd( oObjTmp:ListOfQuestion, WsClassNew( "QuestionView" ) )
											nLenQst := Len( oObjTmp:ListOfQuestion )
											
											GetQuestion( @oObjTmp:ListOfQuestion[nLenQst], cAliasSKG, cAliasSKE, cAliasSKF, cAliasSKR )
			
											//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
											//ณMultipla Escolha em Linhas ou Colunasณ
											//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
											If ( cAliasSKG )->KG_TIPOQST == MULTIPLA_ESCOLHA .AND. ;
												( ( cAliasSKG )->KG_TIPEXIB == LINHAS .OR. ;
												( cAliasSKG )->KG_TIPEXIB == COLUNAS )
												
												//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
												//ณLinhas ou Colunasณ
												//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
												( cAliasSKI )->( DbSetOrder( 1 ) )
												
												If ( cAliasSKI )->( DbSeek( xFilial( "SKI" ) + ;
													( cAliasSKG )->KG_CODQST ) )
													
													oObjTmp:ListOfQuestion[nLenQst]:ListOfOption := {}
													
													While !( cAliasSKI )->( EOF() ) .AND. ;
														( cAliasSKI )->KI_FILIAL == xFilial( "SKI" ) .AND. ;
														( cAliasSKI )->KI_CODQST == ( cAliasSKG )->KG_CODQST
														
														aAdd( oObjTmp:ListOfQuestion[nLenQst]:ListOfOption, WsClassNew( "OptionView" ) )
														nLenOption := Len( oObjTmp:ListOfQuestion[nLenQst]:ListOfOption )
														
														GetOption( @oObjTmp:ListOfQuestion[nLenQst]:ListOfOption[nLenOption], cAliasSKI )
														
														( cAliasSKI )->( DbSkip() )
													End
												EndIf
											EndIf
										
											//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
											//ณUnica Escolha e Multipla Escolhaณ
											//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
											If ( cAliasSKG )->KG_TIPOQST == UNICA_ESCOLHA .OR. ;
												( cAliasSKG )->KG_TIPOQST == MULTIPLA_ESCOLHA

												//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
												//ณBusca Alternativasณ
												//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
												( cAliasSKH )->( DbSetOrder( 1 ) )
												
												If ( cAliasSKH )->( DbSeek( xFilial( "SKH" ) + ( cAliasSKG )->KG_CODQST ) )
													oObjTmp:ListOfQuestion[nLenQst]:ListOfAlternative := {}
													
													While !( cAliasSKH )->( EOF() ) .AND. ;
														( cAliasSKH )->KH_FILIAL == xFilial( "SKH" ) .AND. ;
														( cAliasSKH )->KH_CODQST == ( cAliasSKG )->KG_CODQST
														
														aAdd( oObjTmp:ListOfQuestion[nLenQst]:ListOfAlternative, WsClassNew( "AlternativeView" ) )
														nLenAlt := Len( oObjTmp:ListOfQuestion[nLenQst]:ListOfAlternative )
														
														GetAlternative( @oObjTmp:ListOfQuestion[nLenQst]:ListOfAlternative[nLenAlt], cAliasSKH )
														
														( cAliasSKH )->( DbSkip() )
													End
												EndIf
											EndIf

											( cAliasSKG )->( DbSkip() )
										End
									EndIf
								EndIf
							//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
							//ณBusca todas as questoes do Grupo e SubGrupoณ
							//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
							Case !Empty( ( cAliasSKR )->KR_CODGRU ) .AND. !Empty( ( cAliasSKR )->KR_CODSGR ) .AND. ;
								Empty( ( cAliasSKR )->KR_CODQST )
		
								//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
								//ณBusca dados do Grupoณ
								//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
								( cAliasSKE )->( DbSetOrder( 1 ) )
		
								If ( cAliasSKE )->( DbSeek( xFilial( "SKE" ) + ( cAliasSKR )->KR_CODGRU ) )
									aAdd( ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup, WsClassNew( "GroupView" ) )
									nLenGroup := Len( ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup )
									
									GetGroup( @::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup], cAliasSKE, cAliasSKR )
								EndIf
		
								//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
								//ณBusca dados do SubGrupoณ
								//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
								( cAliasSKF )->( DbSetOrder( 1 ) )
		
								If ( cAliasSKF )->( DbSeek( xFilial( "SKF" ) + ( cAliasSKR )->KR_CODGRU + ( cAliasSKR )->KR_CODSGR ) )
									::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup := {}
									
									aAdd( ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup, WsClassNew( "SubGroupView" ) )
									nLenSubGroup := Len( ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup )
									
									GetSubGroup( @::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup[nLenSubGroup], cAliasSKF, cAliasSKR )
									
									//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
									//ณBusca dados da questaoณ
									//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
									( cAliasSKG )->( DbSetOrder( 2 ) )
									
									If ( cAliasSKG )->( DbSeek( xFilial( "SKG" ) + ( cAliasSKF )->KF_CODGRP + ( cAliasSKF )->KF_CODSUBG ) )
										::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup[nLenSubGroup]:ListOfQuestion := {}
										oObjTmp := ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup[nLenSubGroup]:ListOfQuestion
									
										While !( cAliasSKG )->( EOF() ) .AND. ;
											( cAliasSKG )->KG_FILIAL == xFilial( "SKG" ) .AND. ;
											( cAliasSKG )->KG_CODSUBG == ( cAliasSKF )->KF_CODSUBG
											
											aAdd( oObjTmp, WsClassNew( "QuestionView" ) )
											nLenQst := Len( oObjTmp )
											
											GetQuestion( @oObjTmp[nLenQst], cAliasSKG, cAliasSKE, cAliasSKF, cAliasSKR )
			
											//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
											//ณMultipla Escolha em Linhas ou Colunasณ
											//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
											If ( cAliasSKG )->KG_TIPOQST == MULTIPLA_ESCOLHA .AND. ;
												( ( cAliasSKG )->KG_TIPEXIB == LINHAS .OR. ;
												( cAliasSKG )->KG_TIPEXIB == COLUNAS )
												
												//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
												//ณLinhas ou Colunasณ
												//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
												( cAliasSKI )->( DbSetOrder( 1 ) )
												
												If ( cAliasSKI )->( DbSeek( xFilial( "SKI" ) + ;
													( cAliasSKG )->KG_CODQST ) )
													
													oObjTmp[nLenQst]:ListOfOption := {}
													
													While !( cAliasSKI )->( EOF() ) .AND. ;
														( cAliasSKI )->KI_FILIAL == xFilial( "SKI" ) .AND. ;
														( cAliasSKI )->KI_CODQST == ( cAliasSKG )->KG_CODQST
														
														aAdd( oObjTmp[nLenQst]:ListOfOption, WsClassNew( "OptionView" ) )
														nLenOption := Len( oObjTmp[nLenQst]:ListOfOption )
														
														GetOption( @oObjTmp[nLenQst]:ListOfOption[nLenOption], cAliasSKI )
														
														( cAliasSKI )->( DbSkip() )
													End
												EndIf
											EndIf
										
											//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
											//ณBusca Alternativasณ
											//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
											//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
											//ณUnica Escolha e Multipla Escolhaณ
											//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
											If ( cAliasSKG )->KG_TIPOQST == UNICA_ESCOLHA .OR. ;
												( cAliasSKG )->KG_TIPOQST == MULTIPLA_ESCOLHA

												( cAliasSKH )->( DbSetOrder( 1 ) )
												
												If ( cAliasSKH )->( DbSeek( xFilial( "SKH" ) + ( cAliasSKG )->KG_CODQST ) )
													oObjTmp[nLenQst]:ListOfAlternative := {}
													
													While !( cAliasSKH )->( EOF() ) .AND. ;
														( cAliasSKH )->KH_FILIAL == xFilial( "SKH" ) .AND. ;
														( cAliasSKH )->KH_CODQST == ( cAliasSKG )->KG_CODQST
														
														aAdd( oObjTmp[nLenQst]:ListOfAlternative, WsClassNew( "AlternativeView" ) )
														nLenAlt := Len( oObjTmp[nLenQst]:ListOfAlternative )
														
														GetAlternative( @oObjTmp[nLenQst]:ListOfAlternative[nLenAlt], cAliasSKH )
														
														( cAliasSKH )->( DbSkip() )
													End
												EndIf
											EndIf

											( cAliasSKG )->( DbSkip() )
										End
									EndIf
								EndIf
							//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
							//ณBusca a questao do Grupoณ
							//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
							Case !Empty( ( cAliasSKR )->KR_CODGRU ) .AND. Empty( ( cAliasSKR )->KR_CODSGR ) .AND. ;
								!Empty( ( cAliasSKR )->KR_CODQST )
		
								//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
								//ณBusca dados do Grupoณ
								//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
								( cAliasSKE )->( DbSetOrder( 1 ) )
		
								If ( cAliasSKE )->( DbSeek( xFilial( "SKE" ) + ( cAliasSKR )->KR_CODGRU ) )
									aAdd( ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup, WsClassNew( "GroupView" ) )
									nLenGroup := Len( ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup )
									
									GetGroup( @::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup], cAliasSKE, cAliasSKR )
								EndIf
		
								//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
								//ณBusca dados da questaoณ
								//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
								( cAliasSKG )->( DbSetOrder( 1 ) )
								
								If ( cAliasSKG )->( DbSeek( xFilial( "SKG" ) + ( cAliasSKR )->KR_CODQST ) )
									::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfQuestion := {}
									oObjTmp := ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfQuestion
								
									aAdd( oObjTmp, WsClassNew( "QuestionView" ) )
									nLenQst := Len( oObjTmp )
									
									GetQuestion( @oObjTmp[nLenQst], cAliasSKG, cAliasSKE, cAliasSKF, cAliasSKR )
	
									//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
									//ณMultipla Escolha em Linhas ou Colunasณ
									//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
									If ( cAliasSKG )->KG_TIPOQST == MULTIPLA_ESCOLHA .AND. ;
										( ( cAliasSKG )->KG_TIPEXIB == LINHAS .OR. ;
										( cAliasSKG )->KG_TIPEXIB == COLUNAS )
										
										//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
										//ณLinhas ou Colunasณ
										//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
										( cAliasSKI )->( DbSetOrder( 1 ) )
										
										If ( cAliasSKI )->( DbSeek( xFilial( "SKI" ) + ;
											( cAliasSKG )->KG_CODQST ) )
											
											oObjTmp[nLenQst]:ListOfOption := {}
											
											While !( cAliasSKI )->( EOF() ) .AND. ;
												( cAliasSKI )->KI_FILIAL == xFilial( "SKI" ) .AND. ;
												( cAliasSKI )->KI_CODQST == ( cAliasSKG )->KG_CODQST
												
												aAdd( oObjTmp[nLenQst]:ListOfOption, WsClassNew( "OptionView" ) )
												nLenOption := Len( oObjTmp[nLenQst]:ListOfOption )
												
												GetOption( @oObjTmp[nLenQst]:ListOfOption[nLenOption], cAliasSKI )
												
												( cAliasSKI )->( DbSkip() )
											End
										EndIf
									EndIf

									//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
									//ณBusca Alternativasณ
									//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
									//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
									//ณUnica Escolha e Multipla Escolhaณ
									//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
									If ( cAliasSKG )->KG_TIPOQST == UNICA_ESCOLHA .OR. ;
										( cAliasSKG )->KG_TIPOQST == MULTIPLA_ESCOLHA

										( cAliasSKH )->( DbSetOrder( 1 ) )
										
										If ( cAliasSKH )->( DbSeek( xFilial( "SKH" ) + ( cAliasSKG )->KG_CODQST ) )
											oObjTmp[nLenQst]:ListOfAlternative := {}
											
											While !( cAliasSKH )->( EOF() ) .AND. ;
												( cAliasSKH )->KH_FILIAL == xFilial( "SKH" ) .AND. ;
												( cAliasSKH )->KH_CODQST == ( cAliasSKG )->KG_CODQST
												
												aAdd( oObjTmp[nLenQst]:ListOfAlternative, WsClassNew( "AlternativeView" ) )
												nLenAlt := Len( oObjTmp[nLenQst]:ListOfAlternative )
												
												GetAlternative( @oObjTmp[nLenQst]:ListOfAlternative[nLenAlt], cAliasSKH )
												
												( cAliasSKH )->( DbSkip() )
											End
										EndIf
									EndIf
								EndIf
							//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
							//ณBusca a questao do Grupo e SubGrupoณ
							//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
							Case !Empty( ( cAliasSKR )->KR_CODGRU ) .AND. !Empty( ( cAliasSKR )->KR_CODSGR ) .AND. ;
								!Empty( ( cAliasSKR )->KR_CODQST )
		
								//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
								//ณBusca dados do Grupoณ
								//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
								( cAliasSKE )->( DbSetOrder( 1 ) )
		
								If ( cAliasSKE )->( DbSeek( xFilial( "SKE" ) + ( cAliasSKR )->KR_CODGRU ) )
									nLenGroup := aScan(::Research:ListOfQuestionary[nLenQuest]:ListOfGroup, {|Group| Group:Code == ( cAliasSKR )->KR_CODGRU })
									If nLenGroup <= 0 
										aAdd( ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup, WsClassNew( "GroupView" ) )
										nLenGroup := Len( ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup )
										GetGroup( @::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup], cAliasSKE, cAliasSKR )
									EndIf
								EndIf
		
								//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
								//ณBusca dados do SubGrupoณ
								//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
								( cAliasSKF )->( DbSetOrder( 1 ) )
		
								If ( cAliasSKF )->( DbSeek( xFilial( "SKF" ) + ( cAliasSKR )->KR_CODGRU + ( cAliasSKR )->KR_CODSGR ) )
									If ValType(::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup)<>"A"
										::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup := {}
									EndIf

									nLenSubGroup := aScan(::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup, {|SubGroup| SubGroup:Code == ( cAliasSKR )->KR_CODSGR})
									If nLenSubGroup <= 0
										aAdd( ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup, WsClassNew( "SubGroupView" ) )
										nLenSubGroup := Len( ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup )
										
										GetSubGroup( @::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup[nLenSubGroup], cAliasSKF, cAliasSKR )
									EndIf
									
									//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
									//ณBusca dados da questaoณ
									//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
									( cAliasSKG )->( DbSetOrder( 1 ) )
									
									If ( cAliasSKG )->( DbSeek( xFilial( "SKG" ) + ( cAliasSKR )->KR_CODQST ) )
										If ValType(::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup[nLenSubGroup]:ListOfQuestion)<>"A"
											::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup[nLenSubGroup]:ListOfQuestion := {}
										EndIf
										oObjTmp := ::Research:ListOfQuestionary[nLenQuest]:ListOfGroup[nLenGroup]:ListOfSubGroup[nLenSubGroup]:ListOfQuestion
									
										aAdd( oObjTmp, WsClassNew( "QuestionView" ) )
										nLenQst := Len( oObjTmp )
										
										GetQuestion( @oObjTmp[nLenQst], cAliasSKG, cAliasSKE, cAliasSKF, cAliasSKR )
		
										//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
										//ณMultipla Escolha em Linhas ou Colunasณ
										//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
										If ( cAliasSKG )->KG_TIPOQST == MULTIPLA_ESCOLHA .AND. ;
											( ( cAliasSKG )->KG_TIPEXIB == LINHAS .OR. ;
											( cAliasSKG )->KG_TIPEXIB == COLUNAS )
											
											//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
											//ณLinhas ou Colunasณ
											//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
											( cAliasSKI )->( DbSetOrder( 1 ) )
											
											If ( cAliasSKI )->( DbSeek( xFilial( "SKI" ) + ;
												( cAliasSKG )->KG_CODQST ) )
												
												oObjTmp[nLenQst]:ListOfOption := {}
												
												While !( cAliasSKI )->( EOF() ) .AND. ;
													( cAliasSKI )->KI_FILIAL == xFilial( "SKI" ) .AND. ;
													( cAliasSKI )->KI_CODQST == ( cAliasSKG )->KG_CODQST
													
													aAdd( oObjTmp[nLenQst]:ListOfOption, WsClassNew( "OptionView" ) )
													nLenOption := Len( oObjTmp[nLenQst]:ListOfOption )
													
													GetOption( @oObjTmp[nLenQst]:ListOfOption[nLenOption], cAliasSKI )
													
													( cAliasSKI )->( DbSkip() )
												End
											EndIf
										EndIf

										//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
										//ณBusca Alternativasณ
										//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
										//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
										//ณUnica Escolha e Multipla Escolhaณ
										//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
										If ( cAliasSKG )->KG_TIPOQST == UNICA_ESCOLHA .OR. ;
											( cAliasSKG )->KG_TIPOQST == MULTIPLA_ESCOLHA

											( cAliasSKH )->( DbSetOrder( 1 ) )
											
											If ( cAliasSKH )->( DbSeek( xFilial( "SKH" ) + ( cAliasSKG )->KG_CODQST ) )
												oObjTmp[nLenQst]:ListOfAlternative := {}
												
												While !( cAliasSKH )->( EOF() ) .AND. ;
													( cAliasSKH )->KH_FILIAL == xFilial( "SKH" ) .AND. ;
													( cAliasSKH )->KH_CODQST == ( cAliasSKG )->KG_CODQST
													
													aAdd( oObjTmp[nLenQst]:ListOfAlternative, WsClassNew( "AlternativeView" ) )
													nLenAlt := Len( oObjTmp[nLenQst]:ListOfAlternative )
													
													GetAlternative( @oObjTmp[nLenQst]:ListOfAlternative[nLenAlt], cAliasSKH )
													
													( cAliasSKH )->( DbSkip() )
												End
											EndIf
										EndIf
									EndIf
								EndIf
							//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
							//ณBusca a questao, independente do Grupo e SubGrupoณ
							//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
							Case Empty( ( cAliasSKR )->KR_CODGRU ) .AND. Empty( ( cAliasSKR )->KR_CODSGR ) .AND. ;
								!Empty( ( cAliasSKR )->KR_CODQST )
		
								//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
								//ณBusca dados da questaoณ
								//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
								( cAliasSKG )->( DbSetOrder( 1 ) )
								
								If ( cAliasSKG )->( DbSeek( xFilial( "SKG" ) + ( cAliasSKR )->KR_CODQST ) )
									oObjTmp := ::Research:ListOfQuestionary[nLenQuest]:ListOfQuestion
									aAdd( oObjTmp, WsClassNew( "QuestionView" ) )
									nLenQst := Len( oObjTmp )
									
									GetQuestion( @oObjTmp[nLenQst], cAliasSKG, cAliasSKE, cAliasSKF, cAliasSKR )
	
									//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
									//ณMultipla Escolha em Linhas ou Colunasณ
									//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
									If ( cAliasSKG )->KG_TIPOQST == MULTIPLA_ESCOLHA .AND. ;
										( ( cAliasSKG )->KG_TIPEXIB == LINHAS .OR. ;
										( cAliasSKG )->KG_TIPEXIB == COLUNAS )
										
										//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
										//ณLinhas ou Colunasณ
										//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
										( cAliasSKI )->( DbSetOrder( 1 ) )
										
										If ( cAliasSKI )->( DbSeek( xFilial( "SKI" ) + ;
											( cAliasSKG )->KG_CODQST ) )
											
											oObjTmp[nLenQst]:ListOfOption := {}
											
											While !( cAliasSKI )->( EOF() ) .AND. ;
												( cAliasSKI )->KI_FILIAL == xFilial( "SKI" ) .AND. ;
												( cAliasSKI )->KI_CODQST == ( cAliasSKG )->KG_CODQST
												
												aAdd( oObjTmp[nLenQst]:ListOfOption, WsClassNew( "OptionView" ) )
												nLenOption := Len( oObjTmp[nLenQst]:ListOfOption )
												
												GetOption( @oObjTmp[nLenQst]:ListOfOption[nLenOption], cAliasSKI )
												
												( cAliasSKI )->( DbSkip() )
											End
										EndIf
									EndIf

									//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
									//ณBusca Alternativasณ
									//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
									//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
									//ณUnica Escolha e Multipla Escolhaณ
									//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
									If ( cAliasSKG )->KG_TIPOQST == UNICA_ESCOLHA .OR. ;
										( cAliasSKG )->KG_TIPOQST == MULTIPLA_ESCOLHA

										( cAliasSKH )->( DbSetOrder( 1 ) )
										
										If ( cAliasSKH )->( DbSeek( xFilial( "SKH" ) + ( cAliasSKG )->KG_CODQST ) )
											oObjTmp[nLenQst]:ListOfAlternative := {}
											
											While !( cAliasSKH )->( EOF() ) .AND. ;
												( cAliasSKH )->KH_FILIAL == xFilial( "SKH" ) .AND. ;
												( cAliasSKH )->KH_CODQST == ( cAliasSKG )->KG_CODQST
												
												aAdd( oObjTmp[nLenQst]:ListOfAlternative, WsClassNew( "AlternativeView" ) )
												nLenAlt := Len( oObjTmp[nLenQst]:ListOfAlternative )
												
												GetAlternative( @oObjTmp[nLenQst]:ListOfAlternative[nLenAlt], cAliasSKH )
												
												( cAliasSKH )->( DbSkip() )
											End
										EndIf
									EndIf
								EndIf
						EndCase
						
						( cAliasSKR )->( DbSkip() )
					End
				EndIf
				
				( cAliasSKD )->( DbSkip() )
			End
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault( "GETRESEARCH", "Pesquisa invแlida." )
	EndIf
Else
	lRetorno := .F.
	SetSoapFault( "GETRESEARCH", "UserCode invalido." )
EndIf

RestArea( aArea )

Return lRetorno

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัอออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณPutResearcบAutor  ณLuiz Felipe Couto    บ Data ณ  09/03/05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯอออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Salva ou Finaliza uma pesquisa.                             บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ ExpC1 : Codigo do Usuario WS                                บฑฑ
ฑฑบ          ณ ExpC2 : Objeto com o Resultado da Pesquisa                  บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSIGAGPR     						                           บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑบAnalista  ณ Data/Bops/Ver ณManutencao Efetuada                      	   บฑฑ
ฑฑฬออออออออออุออออออออัออออออุอออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑศออออออออออฯออออออออฯออออออฯอออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
WSMETHOD PutResearchResult WSRECEIVE UserCode, Research WSSEND ResearchCode WSSERVICE GPRResearch

Local cAliasSKS := "SKS"						//Alias da Tabela SKS
Local cAliasSKT := "SKT"						//Alias da Tabela SKT
Local cAliasSKU := "SKU"						//Alias da Tabela SKU

Local lRetorno 	:= .T.							//Retorno da Funcao

Local aArea		:= GetArea()					//Area atual

If ::UserCode == "MSALPHA"
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณCria e Abre as Tabelas SKS, SKT e SKUณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DbSelectArea( cAliasSKS )
	DbSelectArea( cAliasSKT )
	DbSelectArea( cAliasSKU )
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณGrava Resultadoณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	PutResult( ::Research )
Else
	lRetorno := .F.
	SetSoapFault( "PUTRESEARCH", "UserCode invalido." )
EndIf

RestArea( aArea )

Return lRetorno