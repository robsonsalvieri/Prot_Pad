#INCLUDE "wsfat320.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
/*/


Ŀ
Funo    WSFAT320   Autor Eduardo Riera           Data 11.11.2003  
Ĵ
Descrio  Web Service responsavel pelos servicos operacionais de CRM   
          do representante comercial e clientes do representante        
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        CRM/Materiais/Portais                                        
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service                                                
//
WSSERVICE FTSELLERTASK             DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/ftsellertask.apw" //"Servio de consulta as tarefas do representante comercial ( <b>Restrio de representante comercial<b> )"
   WSDATA UserCode                 As String
   WSDATA SellerCode               As String   
   WSDATA Task                     As TaskView
   WSDATA ListOfTask               As Array Of TaskView     
   WSDATA TaskId                   As String
   WSDATA Header                   AS Array Of BrwHeader
   WSDATA QueryAddWhere            As String OPTIONAL
   WSDATA IndexKey                 As String
   WSDATA DateFrom                 As Date OPTIONAL
   WSDATA DateTo                   As Date OPTIONAL
   WSDATA ListOfStatus             As Array Of GenericStruct
   WSDATA ListOfPriority           As Array Of GenericStruct

   WSMETHOD GetHeader      DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
   WSMETHOD BrwTask        DESCRIPTION STR0003 //"Mtodo de listagem das tarefas do representante comercial"
   WSMETHOD GetTask        DESCRIPTION STR0004 //"Mtodo de consulta as informaes da tarefa do representante comercial"
   WSMETHOD PutTask        DESCRIPTION STR0005 //"Mtodo de atualizao das informaes da tarefa do representante comercial"
   WSMETHOD DelTask        DESCRIPTION STR0006 //"Mtodo de excluso das informaes da tarefa do representante comercial"
   WSMETHOD GetStatus      DESCRIPTION STR0007 //"Mtodo de listagem dos status das tarefas do representante comercial"
   WSMETHOD GetPriority    DESCRIPTION STR0008 //"Mtodo de listagem das prioridades das tarefas do representante comercial"
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 11.11.2003 
Ĵ
          Rotina de recuperacao do header                              
                                                                       
Ĵ
ParametrosExpC1: Nome da estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE NULLPARAM WSSEND Header WSSERVICE FTSELLERTASK

::Header := MtHeader("TASKVIEW")

Return(.T.)

/*/

Ŀ
Funo    GetStatus Autor   Eduardo Riera          Data 12.11.2003 
Ĵ
          Rotina de recuperacao dos status da tarefa                   
                                                                       
Ĵ
ParametrosNenhum                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve a lista de status de uma tarefa          
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetStatus WSRECEIVE NULLPARAM WSSEND ListOfStatus WSSERVICE FTSELLERTASK

Local aAux := RetSx3Box("AD8_STATUS",0,0,30)
Local nX      := 0

For nX := 1 To Len(aAux)
	aadd(::ListOfStatus,WsClassNew("GenericStruct"))
	::ListOfStatus[nX]:Code        := aAux[nX][2]
	::ListOfStatus[nX]:Description := aAux[nX][2]	
Next nX

Return(.T.)

/*/

Ŀ
Funo    GetPrioritAutor   Eduardo Riera          Data 12.11.2003 
Ĵ
          Rotina de recuperacao das prioridades de uma tarefa          
                                                                       
Ĵ
ParametrosNenhum                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve a lista de prioridades de uma tarefa     
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetPriority WSRECEIVE NULLPARAM WSSEND ListOfPriority WSSERVICE FTSELLERTASK

Local aAux := RetSx3Box("AD8_PRIOR",0,0,30)
Local nX      := 0

For nX := 1 To Len(aAux)
	aadd(::ListOfPriority,WsClassNew("GenericStruct"))
	::ListOfPriority[nX]:Code        := aAux[nX][2]
	::ListOfPriority[nX]:Description := aAux[nX][2]	
Next nX

Return(.T.)

/*/

Ŀ
Funo    BrwTask   Autor   Eduardo Riera          Data 11.11.2003 
Ĵ
          Rotina de recuperacao das tarefas de um vendedor             
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do vendedor                                    
          ExpD3: Data de emissao inicial                               
          ExpC4: Data de emissao final                                 
          ExpC5: Expressao a ser adicionada na Query                   
          ExpC6: Chave de retorno ( Expressao padrao CodeBase )        
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve as Tarefas de um vendedor                
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                      
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                    
Ĵ
 Cleber M.    13/08/07130776-Correcao na busca de tarefas para consi-
                            derar as datas e o status da tarefa.     
ٱ


/*/
WSMETHOD BrwTask WSRECEIVE UserCode,SellerCode,DateFrom,DateTo,QueryAddWhere,IndexKey WSSEND ListOfTask WSSERVICE FTSELLERTASK

Local aArea    := GetArea()
Local lRetorno := .T.
Local lQuery   := .F.
Local nX       := 0
Local cQuery   := ""
Local cAliasAD8:= "AD8"
Local cArquivo := ""
#IFDEF TOP
Local aStru    := {}
#ENDIF
//Ŀ
//Inicializa os parametros DEFAULTs                                       
//
dbSelectArea("AD8")
dbSetOrder(1)
DEFAULT ::IndexKey := AD8->(IndexKey())
DEFAULT ::DateFrom := dDataBase
DEFAULT ::DateTo   := dDataBase+90

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"FTSELLERTASK","BRWTASK","SA3",::SellerCode)
	#IFDEF TOP
		lQuery := .T.
		cAliasAD8 := "BRWTASK"
		aStru  := AD8->(dbStruct())
		cQuery := "SELECT AD8_FILIAL,AD8_TAREFA,AD8_TOPICO,AD8_DTINI,AD8_DTFIM,AD8_STATUS,AD8_PRIOR,AD8_PERC,AD8_CODMEM"
		cQuery += GetUserField("AD8")
		cQuery += "FROM "+RetSqlName("AD8")+" AD8,"
		cQuery += RetSqlName("SA3")+" SA3 "
		cQuery += "WHERE "
		cQuery += "SA3.A3_FILIAL='"+xFilial("SA3")+"' AND "
		cQuery += "SA3.A3_COD='"+::SellerCode+"' AND "
		cQuery += "SA3.D_E_L_E_T_=' ' AND "
		cQuery += "AD8.AD8_FILIAL='"+xFilial("AD8")+"' AND "
		cQuery += "AD8.AD8_CODUSR = SA3.A3_CODUSR AND (("
		cQuery += "AD8.AD8_DTINI >= '"+Dtos(::DateFrom)+"' AND "
		cQuery += "AD8.AD8_DTINI <= '"+Dtos(::DateTo)+"') "
		cQuery += "AND AD8.AD8_STATUS IN('1','2','3')) AND "
		cQuery += "AD8.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
	    cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)
	    
	    cQuery := ChangeQuery(cQuery)
	    
	    dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAD8)
	    
	    For nX := 1 To Len(aStru)
			If aStru[nX][2]<>"C" .And. FieldPos(aStru[nX][1])<>0
				TcSetField(cAliasAD8,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
			EndIf	    
	    Next nX
	#ELSE
		dbSelectArea("SA3")
		dbSetOrder(1)
		DbSeek(xFilial("SA3")+::SellerCode)		
		dbSelectArea("AD8")
		cArquivo := CriaTrab(,.F.)
		cQuery := "AD8_FILIAL=='"+xFilial("AD8")+"' .And. "
		cQuery += "AD8_CODUSR == '"+SA3->A3_CODUSR+"' .And."
		cQuery += "((AD8_DTINI >= '"+Dtos(::DateFrom)+"' .And. "
		cQuery += "AD8_DTINI <= '"+Dtos(::DateTo)+"') .OR. AD8_STATUS$'1,2,3' )"		
		IndRegua("AD8",cArquivo,::IndexKey,,cQuery)
		dbGotop()
	#ENDIF
	nX := 0
	While (cAliasAD8)->(!Eof()) .And. xFilial("AD8") == (cAliasAD8)->AD8_FILIAL
	
		aadd(::ListOfTask,WsClassNew("TaskView"))
		nX++
		
		GetTask(cAliasAD8,@::ListOfTask[nX])

		dbSelectArea(cAliasAD8)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasAD8)
		dbCloseArea()
		dbSelectArea("AD8")
	Else
		dbSelectArea("AD8")
		RetIndex("AD8")
		FErase(cArquivo+OrdBagExt())
	EndIf
	//Ŀ
	//Verifica se ha dados a serem retornados                                 
	//
	If Empty( ::ListOfTask )
		lRetorno := .F.
		SetSoapFault("BRWTASK",STR0009) //"Nao encontrado nenhuma tarefa para este representante"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetTask   Autor   Eduardo Riera          Data 11.11.2003 
Ĵ
          Rotina de recuperacao de uma tarefa do vendedor              
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do vendedor                                    
          ExpC3: Codigo da Tarefa                                      
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve uma tarefa cadastrada no sistema         
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetTask WSRECEIVE UserCode,SellerCode,TaskId WSSEND Task WSSERVICE FTSELLERTASK

Local aArea    := GetArea()
Local lRetorno := .T.

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"FTSELLERTASK","GETTASK","SA3",::SellerCode)

	dbSelectArea("AD8")
	dbSetOrder(1)
	If DbSeek(xFilial("AD8")+::TaskId)
	
		GetTask("AD8",@::Task)
		
	Else

		lRetorno := .F.
		SetSoapFault("GETTASK",STR0010) //"Codigo da tarefa invalida"
		
	EndIf	
	
Else

	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    PutTask   Autor   Eduardo Riera          Data 11.11.2003 
Ĵ
          Rotina de atualizacao dos dados da Tarefa                    
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do Vendedor                                    
          ExpO3: Objeto da Tarefa                                      
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo efetua a atualizacao das Tarefas do sistema      
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                      
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                    
Ĵ
 Cleber M.    18/05/07126090-Correcao na inclusao de tarefas com data
                            posterior a data atual.                  
ٱ


/*/
WSMETHOD PutTask WSRECEIVE UserCode,SellerCode,Task WSSEND TaskID WSSERVICE FTSELLERTASK

Local aArea    := GetArea()			//Guarda a area atual
Local aDados   := {}				//Array com os dados da tarefa
Local aErro    := {}				//Array com a descricao do erro
Local cErro    := ""				//String com o erro
Local lRetorno := .T.				//Retorno da funcao
Local nX       := 0					//Indica a operacao (3=inclusao, 4=alteracao)
PRIVATE lMsErroAuto    := .F.		//Indica se ocorreu erro na MsExecAuto
PRIVATE lAutoErrNoFile := .T.		//Usada dentro da MsExecAuto

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"FTSELLERTASK","PUTTASK","SA3",::SellerCode)

	aadd(aDados,{"AD8_TAREFA"  ,::Task:TaskID,Nil})
	aadd(aDados,{"AD8_TOPICO"  ,::Task:Subject,Nil})
	If ::Task:StartDate > dDataBase
		aadd(aDados,{"AD8_DTFIM"   ,::Task:EndDate,Nil})
		aadd(aDados,{"AD8_DTINI"   ,::Task:StartDate,Nil})
	Else
		aadd(aDados,{"AD8_DTINI"   ,::Task:StartDate,Nil})
		aadd(aDados,{"AD8_DTFIM"   ,::Task:EndDate,Nil})	
	EndIf
	aadd(aDados,{"AD8_STATUS"  ,::Task:StatusCode,Nil})
	aadd(aDados,{"AD8_PRIOR"   ,::Task:Priority,Nil})
	aadd(aDados,{"AD8_PERC"    ,::Task:PercentComplete,Nil})
	aadd(aDados,{"AD8_MEMO"    ,::Task:Note,Nil})
	
	PutUserFields("AD8",::Task:UserFields,@aDados)
	aDados := WsAutoOpc(aDados)
	
	//Ŀ
	//Atualizacao do cadastro                                                 
	//
	dbSelectArea("AD8")
	dbSetOrder(1)
	If Empty(::Task:TaskID) .Or. !DbSeek(xFilial("AD8")+::Task:TaskID)
		nX := 3
	Else
		nX := 4
	EndIf
	Fata320({{"A3_COD",::SellerCode,Nil}},{aDados})
	If lMsErroAuto
		aErro := GetAutoGRLog()
   		cErro := ""
	    For nX := 1 To Len(aErro)
	 		cErro += aErro[nX] + Chr(13)+Chr(10)
	    Next nX
		SetSoapFault("PUTTASK",cErro)
		lRetorno := .F.
	EndIf
		
	::TaskID := AD8->AD8_TAREFA
	
Else

	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    DelTask   Autor   Eduardo Riera          Data 11.11.2003 
Ĵ
          Rotina de atualizacao dos dados da Tarefa                    
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do Vendedor                                    
          ExpO3: Objeto da Tarefa                                      
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo efetua a atualizacao das Tarefas do sistema      
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD DelTask WSRECEIVE UserCode,SellerCode,Task WSSEND TaskID WSSERVICE FTSELLERTASK

Local aArea    := GetArea()
Local aDados   := {}
Local aErro    := {}
Local cErro    := ""
Local lRetorno := .T.
Local nX       := 0
PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"FTSELLERTASK","DELTASK","SA3",::SellerCode)

	aadd(aDados,{"AD8_TAREFA"  ,::Task:TaskID})
	aadd(aDados,{"AUTDELETA","S",Nil})
	
	PutUserFields("AD8",::Task:UserFields,@aDados)
	aDados := WsAutoOpc(aDados)		
	
	//Ŀ
	//Atualizacao do cadastro                                                 
	//
	dbSelectArea("AD8")
	dbSetOrder(1)
	If DbSeek(xFilial("AD8")+::Task:TaskID)
		nX := 4
		Fata320({{"A3_COD",::SellerCode,Nil}},{aDados})
		If lMsErroAuto
			aErro := GetAutoGRLog()
	   		cErro := ""
		    For nX := 1 To Len(aErro)
		 		cErro += aErro[nX] + Chr(13)+Chr(10)
		    Next nX
			SetSoapFault("DELTASK",cErro)
			lRetorno := .F.
		EndIf
	Else
		SetSoapFault("DELTASK",STR0011)		 //"Tarefa nao encontrada"
		lRetorno := .f.
	EndIf		
	::TaskID := AD8->AD8_TAREFA
	
Else

	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/


Ŀ
Funo    WSFAT320   Autor Eduardo Riera           Data 11.11.2003  
Ĵ
Descrio  Web Service responsavel pelo colocacao de tarefas de um clien
          te para um representante.                                     
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        CRM/Materiais/Portais                                        
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service                                                
//
WSSERVICE FTCUSTOMERTASK           DESCRIPTION STR0012 NAMESPACE "http://webservices.microsiga.com.br/ftcustomertask.apw" //"Servio de consulta das tarefas de um representante comercial ( <b>Restrio de cliente<b> )"
   WSDATA UserCode                 As String
   WSDATA CustomerId               As String   
   WSDATA Task                     As TaskView
   WSDATA ListOfTask               As Array Of TaskView     
   WSDATA TaskId                   As String
   WSDATA Header                   AS Array Of BrwHeader
   WSDATA QueryAddWhere            As String OPTIONAL
   WSDATA IndexKey                 As String OPTIONAL
   WSDATA DateFrom                 As Date OPTIONAL
   WSDATA DateTo                   As Date OPTIONAL
   WSDATA ListOfStatus             As Array Of GenericStruct   
   WSDATA ListOfPriority           As Array Of GenericStruct

   WSMETHOD GetHeader      DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
   WSMETHOD BrwTask        DESCRIPTION STR0003 //"Mtodo de listagem das tarefas do representante comercial"
   WSMETHOD GetTask        DESCRIPTION STR0004 //"Mtodo de consulta as informaes da tarefa do representante comercial"
   WSMETHOD PutTask        DESCRIPTION STR0005 //"Mtodo de atualizao das informaes da tarefa do representante comercial"
   WSMETHOD GetStatus      DESCRIPTION STR0007 //"Mtodo de listagem dos status das tarefas do representante comercial"
   WSMETHOD GetPriority    DESCRIPTION STR0008 //"Mtodo de listagem das prioridades das tarefas do representante comercial"

ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 11.11.2003 
Ĵ
          Rotina de recuperacao do header                              
                                                                       
Ĵ
ParametrosNenhum                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE NULLPARAM WSSEND Header WSSERVICE FTCUSTOMERTASK

::Header := MtHeader("TASKVIEW")

Return(.T.)

/*/

Ŀ
Funo    GetStatus Autor   Eduardo Riera          Data 12.11.2003 
Ĵ
          Rotina de recuperacao dos status da tarefa                   
                                                                       
Ĵ
ParametrosNenhum                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve a lista de status de uma tarefa          
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetStatus WSRECEIVE NULLPARAM WSSEND ListOfStatus WSSERVICE FTCUSTOMERTASK

Local aAux := RetSx3Box("AD8_STATUS",0,0,30)
Local nX      := 0

For nX := 1 To Len(aAux)
	aadd(::ListOfStatus,WsClassNew("GenericStruct"))
	::ListOfStatus[nX]:Code        := aAux[nX][2]
	::ListOfStatus[nX]:Description := aAux[nX][2]	
Next nX

Return(.T.)

/*/

Ŀ
Funo    GetPrioritAutor   Eduardo Riera          Data 12.11.2003 
Ĵ
          Rotina de recuperacao das prioridades de uma tarefa          
                                                                       
Ĵ
ParametrosNenhum                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve a lista de prioridades de uma tarefa     
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetPriority WSRECEIVE NULLPARAM WSSEND ListOfPriority WSSERVICE FTCUSTOMERTASK

Local aAux := RetSx3Box("AD8_PRIOR",0,0,30)
Local nX      := 0

For nX := 1 To Len(aAux)
	aadd(::ListOfPriority,WsClassNew("GenericStruct"))
	::ListOfPriority[nX]:Code        := aAux[nX][2]
	::ListOfPriority[nX]:Description := aAux[nX][2]	
Next nX

Return(.T.)

/*/

Ŀ
Funo    BrwTask   Autor   Eduardo Riera          Data 11.11.2003 
Ĵ
          Rotina de recuperacao das tarefas de um vendedor             
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do cliente                                     
          ExpC3: Data de emissao inicial                               
          ExpC4: Data de emissao final                                 
          ExpC5: Expressao a ser adicionada na Query                   
          ExpC6: Chave de retorno ( Expressao padrao CodeBase )        
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve as Tarefas de um vendedor                
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD BrwTask WSRECEIVE UserCode,CustomerID,DateFrom,DateTo,QueryAddWhere,IndexKey WSSEND ListOfTask WSSERVICE FTCUSTOMERTASK

Local aArea    := GetArea()
Local lRetorno := .T.
Local lQuery   := .F.
Local nX       := 0
Local cQuery   := ""
Local cAliasAD8:= "AD8"
Local cArquivo := ""
Local cCliente := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja    := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
#IFDEF TOP
	Local aStru    := {}
#ENDIF
//Ŀ
//Inicializa os parametros DEFAULTs                                       
//
dbSelectArea("AD8")
dbSetOrder(1)
DEFAULT ::IndexKey := AD8->(IndexKey())
DEFAULT ::DateFrom := dDataBase
DEFAULT ::DateTo   := dDataBase+90

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"FTCUSTOMERTASK","BRWTASK","SA1",::CustomerID)
	#IFDEF TOP
		lQuery := .T.
		cAliasAD8 := "BRWTASK"
		aStru  := AD8->(dbStruct())
		cQuery := "SELECT AD8_FILIAL,AD8_TAREFA,AD8_TOPICO,AD8_DTINI,AD8_DTFIM,AD8_STATUS,AD8_PRIOR,AD8_PERC,AD8_CODMEM"
		cQuery += GetUserField("AD8")
		cQuery += "FROM "+RetSqlName("AD8")+" AD8,"
		cQuery += RetSqlName("SA3")+" SA3,"
		cQuery += RetSqlName("SA1")+" SA1 "
		cQuery += "WHERE "
		cQuery += "SA1.A1_FILIAL='"+xFilial("SA1")+"' AND "
		cQuery += "SA1.A1_COD='"+cCliente+"' AND "
		cQuery += "SA1.A1_LOJA='"+cLoja+"' AND "
		cQuery += "SA1.D_E_L_E_T_=' ' AND "
		cQuery += "SA3.A3_FILIAL='"+xFilial("SA3")+"' AND "
		cQuery += "SA3.A3_COD=SA1.A1_VEND AND "
		cQuery += "SA3.D_E_L_E_T_=' ' AND "
		cQuery += "AD8.AD8_FILIAL='"+xFilial("AD8")+"' AND "
		cQuery += "AD8.AD8_CODUSR = SA3.A3_CODUSR AND (("
		cQuery += "AD8.AD8_DTINI >= '"+Dtos(::DateFrom)+"' AND "
		cQuery += "AD8.AD8_DTINI <= '"+Dtos(::DateTo)+"') "
		cQuery += "OR AD8.AD8_STATUS IN('1','2','3')) AND "
		cQuery += "AD8.AD8_CODCLI = '"+cCliente+"' AND "
		cQuery += "AD8.AD8_LOJCLI = '"+cLoja+"' AND "
		cQuery += "AD8.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
	    cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)
	    
	    cQuery := ChangeQuery(cQuery)
	    
	    dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAD8)
	    
	    For nX := 1 To Len(aStru)
			If aStru[nX][2]<>"C" .And. FieldPos(aStru[nX][1])<>0
				TcSetField(cAliasAD8,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
			EndIf
	    Next nX
	#ELSE
		dbSelectArea("SA1")
		dbSetOrder(1)
		DbSeek(xFilial("SA1")+::CustomerID)	

		dbSelectArea("SA3")
		dbSetOrder(1)
		DbSeek(xFilial("SA3")+SA1->A1_VEND)
		
		dbSelectArea("AD8")
		cArquivo := CriaTrab(,.F.)

		cQuery := "AD8_FILIAL=='"+xFilial("AD8")+"' .And. "
		cQuery += "AD8_CODUSR == '"+SA3->A3_CODUSR+"' .And. "
		cQuery += "AD8_CODCLI == '"+cCliente+"' .And. "
		cQuery += "AD8_LOJCLI == '"+cLoja+"' .And. "
		cQuery += "((AD8_DTINI >= '"+Dtos(::DateFrom)+"' .And. "
		cQuery += "AD8_DTINI <= '"+Dtos(::DateTo)+"') .OR. AD8_STATUS$'1,2,3' )"
		
		IndRegua("AD8",cArquivo,::IndexKey,,cQuery)
		dbGotop()		
	#ENDIF
	nX := 0
	While !Eof() .And. xFilial("AD8") == (cAliasAD8)->AD8_FILIAL
	
		aadd(::ListOfTask,WsClassNew("TaskView"))
		nX++
		
		GetTask(cAliasAD8,@::ListOfTask[nX])

		dbSelectArea(cAliasAD8)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasAD8)
		dbCloseArea()
		dbSelectArea("AD8")
	Else
		dbSelectArea("AD8")
		RetIndex("AD8")
		FErase(cArquivo+OrdBagExt())
	EndIf
	//Ŀ
	//Verifica se ha dados a serem retornados                                 
	//
	If Empty( ::ListOfTask )
		lRetorno := .F.
		SetSoapFault("BRWTASK",STR0013) //"Nao encontrado nenhuma tarefa deste representante para este cliente"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetTask   Autor   Eduardo Riera          Data 11.11.2003 
Ĵ
          Rotina de recuperacao de uma tarefa do vendedor              
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do cliente                                     
          ExpC3: Codigo da Tarefa                                      
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve uma tarefa cadastrada no sistema         
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetTask WSRECEIVE UserCode,CustomerID,TaskId WSSEND Task WSSERVICE FTCUSTOMERTASK

Local aArea    := GetArea()
Local lRetorno := .T.
Local cCliente := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja    := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"FTCUSTOMERTASK","GETTASK","SA1",::CustomerID)

	dbSelectArea("AD8")
	dbSetOrder(1)
	If DbSeek(xFilial("AD8")+::TaskId)
	
		If AD8->AD8_CODCLI == cCliente .And. AD8->AD8_LOJCLI == cLoja

			GetTask("AD8",@::Task)

		Else

			lRetorno := .F.
			SetSoapFault("GETTASK",STR0014) //"Codigo da tarefa invalida para este cliente"
		
		EndIf
		
	Else

		lRetorno := .F.
		SetSoapFault("GETTASK",STR0010) //"Codigo da tarefa invalida"
		
	EndIf	
	
Else

	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    PutTask   Autor   Eduardo Riera          Data 11.11.2003 
Ĵ
          Rotina de atualizacao dos dados da Tarefa                    
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do Cliente                                     
          ExpO3: Objeto da Tarefa                                      
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo efetua a atualizacao das Tarefas do sistema      
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD PutTask WSRECEIVE UserCode,CustomerID,Task WSSEND TaskID WSSERVICE FTCUSTOMERTASK

Local aArea    := GetArea()
Local aDados   := {}
Local aErro    := {}
Local cErro    := ""
Local lRetorno := .T.
Local nX       := 0
Local cCliente := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja    := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local cAliasAD8 := "AD8"

PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"FTCUSTOMERTASK","PUTTASK","SA1",::CustomerID)
	
	dbSelectArea("SA1")
	dbSetOrder(1)
	DbSeek(xFilial("SA1")+::CustomerId)
	
	If !Empty(SA1->A1_VEND)
		dbSelectArea("SA3")
		dbSetOrder(1)
		DbSeek(xFilial("SA3")+SA1->A1_VEND)
		//Ŀ
		//Atualizacao do cadastro                                                 
		//
		dbSelectArea("AD8")
		dbSetOrder(2)
		dbSeek(xFilial("AD8")+SA3->A3_CODUSR+Dtos(::Task:StartDate))
		While ( !Eof() .And. xFilial("AD8") == (cAliasAD8)->AD8_FILIAL .And.;
									(cAliasAD8)->AD8_CODUSR == SA3->A3_CODUSR .And.;
									(cAliasAD8)->AD8_DTINI  == ::Task:StartDate ) .And. ;
									(cAliasAD8)->AD8_CODCLI == cCliente .And. ;
									(cAliasAD8)->AD8_LOJCLI == cLoja
			If Empty( ::Task:TaskID )
				aAdd( aDados, {} )
				nX := Len( aDados )
				aadd(aDados[nX],{"AD8_TAREFA"  ,( cAliasAD8 )->AD8_TAREFA,Nil})
				aadd(aDados[nX],{"AD8_TOPICO"  ,( cAliasAD8 )->AD8_TOPICO,Nil})
				aadd(aDados[nX],{"AD8_DTINI"   ,( cAliasAD8 )->AD8_DTINI,Nil})
				aadd(aDados[nX],{"AD8_DTFIM"   ,( cAliasAD8 )->AD8_DTFIM,Nil})
				aadd(aDados[nX],{"AD8_STATUS"  ,( cAliasAD8 )->AD8_STATUS,Nil})
				aadd(aDados[nX],{"AD8_PRIOR"   ,( cAliasAD8 )->AD8_PRIOR,Nil})
				aadd(aDados[nX],{"AD8_PERC"    ,( cAliasAD8 )->AD8_PERC,Nil})
				aadd(aDados[nX],{"AD8_MEMO"    ,MSMM((cAliasAD8)->AD8_CODMEM),Nil})
				aadd(aDados[nX],{"AD8_CODCLI"  ,( cAliasAD8 )->AD8_CODCLI,".T."})
				aadd(aDados[nX],{"AD8_LOJCLI"  ,( cAliasAD8 )->AD8_LOJCLI,".T."})
				aDados[nX] := WsAutoOpc(aDados[nX])				
			EndIf
			
			dbSelectArea(cAliasAD8)
			dbSkip()
		EndDo
			
		aAdd( aDados, {} )
		nX := Len( aDados )
		aadd(aDados[nX],{"AD8_TAREFA"  ,::Task:TaskID,Nil})
		aadd(aDados[nX],{"AD8_TOPICO"  ,::Task:Subject,Nil})
		aadd(aDados[nX],{"AD8_DTINI"   ,::Task:StartDate,Nil})
		aadd(aDados[nX],{"AD8_DTFIM"   ,::Task:EndDate,Nil})
		aadd(aDados[nX],{"AD8_STATUS"  ,::Task:StatusCode,Nil})
		aadd(aDados[nX],{"AD8_PRIOR"   ,::Task:Priority,Nil})
		aadd(aDados[nX],{"AD8_PERC"    ,::Task:PercentComplete,Nil})
		aadd(aDados[nX],{"AD8_MEMO"    ,::Task:Note,Nil})
		aadd(aDados[nX],{"AD8_CODCLI"  ,cCliente,".T."})
		aadd(aDados[nX],{"AD8_LOJCLI"  ,cLoja,".T."})
		PutUserFields("AD8",::Task:UserFields,@aDados[nX])
		aDados[nX] := WsAutoOpc(aDados[nX])

		dbSelectArea(cAliasAD8)
		dbSetOrder(1)
		If Empty(::Task:TaskID) .Or. !DbSeek(xFilial("AD8")+::Task:TaskID)
			nX := 3
		Else
			nX := 4
		EndIf
		Fata320({{"A3_COD",SA1->A1_VEND,Nil}},aDados)
		If lMsErroAuto
			aErro := GetAutoGRLog()
	   		cErro := ""
		    For nX := 1 To Len(aErro)
		 		cErro += aErro[nX] + Chr(13)+Chr(10)
		    Next nX
			SetSoapFault("PUTTASK",cErro)
			lRetorno := .F.
		EndIf
			
		::TaskID := (cAliasAD8)->AD8_TAREFA
	Else
	
		lRetorno := .F.
		SetSoapFault("PUTTASK",STR0015) //"Nao ha vendedor cadastrado para este cliente"
	
	EndIf		
Else

	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)


/*/


Ŀ
Funo    WSFAT320   Autor Eduardo Riera           Data 12.11.2003  
Ĵ
Descrio  Web Service responsavel pelo calendrio do representante     
                                                                        
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        CRM/Materiais/Portais                                        
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service                                                
//
WSSERVICE FTSELLERCALENDAR         DESCRIPTION STR0016 NAMESPACE "http://webservices.microsiga.com.br/ftsellercalendar.apw" //"Servio de consulta ao calendrio do representante comercial ( <b>Restrio de representante comercial<b> )"
   WSDATA UserCode                 As String
   WSDATA SellerCode               As String   
   WSDATA Calendar                 As CalendarView
   WSDATA CalendarID               As String           
   WSDATA ListOfCalendar           As Array Of CalendarView   
   WSDATA Header                   AS Array Of BrwHeader
   WSDATA QueryAddWhere            As String OPTIONAL
   WSDATA IndexKey                 As String
   WSDATA DateFrom                 As Date OPTIONAL
   WSDATA DateTo                   As Date OPTIONAL

   WSMETHOD GetHeader     DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
   WSMETHOD BrwCalendar   DESCRIPTION STR0017 //"Mtodo de listagem do calendrio do representante comercial"
   WSMETHOD GetCalendar   DESCRIPTION STR0018 //"Mtodo de consulta do calendrio do representante comercial"
   WSMETHOD PutCalendar   DESCRIPTION STR0019 //"Mtodo de atualizao do calendrio do representante comercial"
   WSMETHOD DelCalendar   DESCRIPTION STR0020 //"Mtodo de excluso do calendrio do representante comercial"
   
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 12.11.2003 
Ĵ
          Rotina de recuperacao do header                              
                                                                       
Ĵ
ParametrosNenhum                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE NULLPARAM WSSEND Header WSSERVICE FTSELLERCALENDAR

::Header := MtHeader("CALENDARVIEW")

Return(.T.)

/*/

Ŀ
Funo    BrwCalendaAutor   Eduardo Riera          Data 12.11.2003 
Ĵ
          Rotina de recuperacao da agenda de um representante          
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do representante                               
          ExpD3: Data de Inicio                                        
          ExpD4: Data de termino                                       
          ExpC5: Expressao a ser adicionada na Query                   
          ExpC6: Chave de retorno ( Expressao padrao CodeBase )        
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve a agenda de um representante             
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD BrwCalendar WSRECEIVE UserCode,SellerCode,DateFrom,DateTo,QueryAddWhere,IndexKey WSSEND ListOfCalendar WSSERVICE FTSELLERCALENDAR

Local aArea    := GetArea()
Local lRetorno := .T.
Local lQuery   := .F.
Local nX       := 0
Local cQuery   := ""
Local cAliasAD7:= "AD7"
Local cArquivo := ""
#IFDEF TOP
	Local aStru    := {}
#ENDIF
//Ŀ
//Inicializa os parametros DEFAULTs                                       
//
dbSelectArea("AD7")
dbSetOrder(1)
DEFAULT ::IndexKey := AD7->(IndexKey())
DEFAULT ::DateFrom := dDataBase
DEFAULT ::DateTo   := dDataBase+90

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"FTSELLERCALENDAR","BRWCALENDAR","SA3",::SellerCode)
	#IFDEF TOP
		lQuery := .T.
		cAliasAD7 := "BRWCALENDAR"
		aStru  := AD7->(dbStruct())
		cQuery := "SELECT AD7_FILIAL,AD7_TOPICO,AD7_DATA,AD7_HORA1,AD7_HORA2,AD7_CODMEM,"
		cQuery += "AD7_NROPOR,AD7_CODCLI,AD7_LOJA,AD7_PROSPE,AD7_LOJPRO,AD7_CONTAT"
		cQuery += GetUserField("AD7")
		cQuery += "FROM "+RetSqlName("AD7")+" AD7 "
		cQuery += "WHERE "
		cQuery += "AD7.AD7_FILIAL='"+xFilial("AD7")+"' AND "
		cQuery += "AD7.AD7_VEND = '"+::SellerCode+"' AND "
		cQuery += "AD7.AD7_DATA >= '"+Dtos(::DateFrom)+"' AND "
		cQuery += "AD7.AD7_DATA <= '"+Dtos(::DateTo)+"' AND "
		cQuery += "AD7.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
	    cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)
	    
	    cQuery := ChangeQuery(cQuery)
	    
	    dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAD7)
	    
	    For nX := 1 To Len(aStru)
			If aStru[nX][2]<>"C" .And. FieldPos(aStru[nX][1])<>0
				TcSetField(cAliasAD7,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
			EndIf
	    Next nX
	#ELSE	
		dbSelectArea("AD7")
		cArquivo := CriaTrab(,.F.)

		cQuery := "AD7_FILIAL=='"+xFilial("AD7")+"' .AND. "
		cQuery += "AD7_VEND == '"+::SellerCode+"' .AND. "
		cQuery += "DToS( AD7_DATA ) >= '"+Dtos(::DateFrom)+"' .AND. "
		cQuery += "DToS( AD7_DATA ) <= '"+Dtos(::DateTo)+"' "
		
		IndRegua("AD7",cArquivo,::IndexKey,,cQuery)
		dbGotop()
	#ENDIF
	nX := 0
	While !Eof() .And. xFilial("AD7") == (cAliasAD7)->AD7_FILIAL
	
		If nX == 0
			::ListOfCalendar := {}
		EndIf
		aadd(::ListOfCalendar,WsClassNew("CalendarView"))
		nX++
		
		GetCalendar(cAliasAD7,@::ListOfCalendar[nX])

		dbSelectArea(cAliasAD7)
		dbSkip()
		
	EndDo
	If lQuery
		dbSelectArea(cAliasAD7)
		dbCloseArea()
		dbSelectArea("AD7")
	Else
		dbSelectArea("AD7")
		RetIndex("AD7")
		FErase(cArquivo+OrdBagExt())
	EndIf
	//Ŀ
	//Verifica se ha dados a serem retornados                                 
	//
	If Empty( ::ListOfCalendar )
		lRetorno := .F.
		SetSoapFault("BRWCALENDAR",STR0021) //"No encontrado nenhum compromisso."
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetCalend Autor   Eduardo Riera          Data 12.11.2003 
Ĵ
          Rotina de recuperacao de um item da agenda do vendedor       
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do vendedor                                    
          ExpC3: Codigo da Agenda                                      
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve um item da agenda do vendedor            
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetCalendar WSRECEIVE UserCode,SellerCode,CalendarId WSSEND Calendar WSSERVICE FTSELLERCALENDAR

Local aArea    := GetArea()
Local lRetorno := .T.

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"FTSELLERCALENDAR","GETCALENDAR","SA3",::SellerCode)

	dbSelectArea("AD7")
	dbSetOrder(1)
	If DbSeek(xFilial("AD7")+::SellerCode+::CalendarId)
	
		GetCalendar("AD7",@::Calendar)
		
	Else

		lRetorno := .F.
		SetSoapFault("GETCALENDAR",STR0022) //"Codigo da agenda invalido"
		
	EndIf	
	
Else

	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    PutCalend Autor   Eduardo Riera          Data 12.11.2003 
Ĵ
          Rotina de atualizacao da agenda de um representante          
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do Vendedor                                    
          ExpO3: Objeto da Agenda                                      
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo efetua a atualizacao das Tarefas do sistema      
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD PutCalendar WSRECEIVE UserCode,SellerCode,Calendar WSSEND CalendarID WSSERVICE FTSELLERCALENDAR

Local aArea    := GetArea()
Local aDados   := {}
Local aErro    := {}
Local cErro    := ""
Local lRetorno := .T.
Local nX       := 0
PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"FTSELLERCALENDAR","PUTCALENDAR","SA3",::SellerCode)

	aAdd(aDados,{"AD7_TOPICO" ,::Calendar:Subject,NIL})
	aAdd(aDados,{"AD7_DATA"   ,::Calendar:EventDate,NIL})
	aAdd(aDados,{"AD7_HORA1"  ,::Calendar:StartTime,NIL})	
	aAdd(aDados,{"AD7_HORA2"  ,::Calendar:EndTime,NIL})
	aAdd(aDados,{"AD7_MEMO"   ,::Calendar:Note,NIL})
	If !Empty(::Calendar:CustomerCode)
		aAdd(aDados,{"AD7_CODCLI" ,::Calendar:CustomerCode,NIL})
		aAdd(aDados,{"AD7_LOJA"   ,::Calendar:UnitCustomerCode,NIL})
	Else
		aAdd(aDados,{"AD7_PROSPE" ,::Calendar:ProspectCode,NIL})
		aAdd(aDados,{"AD7_LOJPRO" ,::Calendar:UnitProspectCode,NIL})
	EndIf
	If !Empty(::Calendar:Contact)
		aAdd(aDados,{"AD7_CONTAT" ,::Calendar:Contact,NIL})
	EndIf
	aAdd(aDados,{"AD7_VEND"   ,::SellerCode,NIL})	
	PutUserFields("AD7",::Calendar:UserFields,@aDados)
	aDados := WsAutoOpc(aDados)		
	
	//Ŀ
	//Atualizacao do cadastro                                                 
	//
	dbSelectArea("AD7")
	dbSetOrder(1)
	If !DbSeek(xFilial("AD7")+::SellerCode+Dtos(::Calendar:EventDate)+::Calendar:StartTime)
		nX := 3
	Else
		nX := 4
	EndIf
	Fata320({{"A3_COD",::SellerCode,Nil}},,{aDados})
	If lMsErroAuto
		aErro := GetAutoGRLog()
   		cErro := ""
	    For nX := 1 To Len(aErro)
	 		cErro += aErro[nX] + Chr(13)+Chr(10)
	    Next nX
		SetSoapFault("PUTCALENDAR",cErro)
		lRetorno := .F.
	EndIf
		
	::CalendarID := ::SellerCode+Dtos(::Calendar:EventDate)+::Calendar:StartTime
	
Else

	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    DelCalend Autor   Eduardo Riera          Data 18.12.2003 
Ĵ
          Rotina de exclusao da agenda de um representante             
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do Vendedor                                    
          ExpO3: Objeto da Agenda                                      
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo efetua a atualizacao das Tarefas do sistema      
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD DelCalendar WSRECEIVE UserCode,SellerCode,Calendar WSSEND CalendarID WSSERVICE FTSELLERCALENDAR

Local aArea    := GetArea()
Local aDados   := {}
Local aErro    := {}
Local cErro    := ""
Local lRetorno := .T.
Local nX       := 0
PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"FTSELLERCALENDAR","PUTCALENDAR","SA3",::SellerCode)

	aAdd( aDados, {} )
	nX := Len( aDados )
	aadd(aDados[nX],{"AD7_TOPICO" ,::Calendar:Subject})
	aadd(aDados[nX],{"AD7_DATA"   ,::Calendar:EventDate})
	aadd(aDados[nX],{"AD7_HORA1"  ,::Calendar:StartTime})	
	aadd(aDados[nX],{"AD7_HORA2"  ,::Calendar:EndTime})
	aadd(aDados[nX],{"AD7_MEMO"   ,::Calendar:Note})
	aadd(aDados[nX],{"AD7_CODCLI" ,::Calendar:CustomerCode})
	aadd(aDados[nX],{"AD7_LOJA"   ,::Calendar:UnitCustomerCode})
	aadd(aDados[nX],{"AD7_PROSPE" ,::Calendar:ProspectCode})
	aadd(aDados[nX],{"AD7_LOJPRO" ,::Calendar:UnitProspectCode})
	aadd(aDados[nX],{"AD7_CONTAT" ,::Calendar:Contact})
	aadd(aDados[nX],{"AD7_VEND"   ,::SellerCode})
	aadd(aDados[nX],{"AUTDELETA","S",Nil})
	PutUserFields("AD7",::Calendar:UserFields,@aDados[nX])
	aDados[nX] := WsAutoOpc(aDados[nX])		
	//Ŀ
	//Atualizacao do cadastro                                                 
	//
	dbSelectArea("AD7")
	dbSetOrder(1)
	If !DbSeek(xFilial("AD7")+::SellerCode+Dtos(::Calendar:EventDate)+::Calendar:StartTime)
		nX := 5
	EndIf
	Fata320({{"A3_COD",::SellerCode,Nil}},,aDados)
	If lMsErroAuto
		aErro := GetAutoGRLog()
   		cErro := ""
	    For nX := 1 To Len(aErro)
	 		cErro += aErro[nX] + Chr(13)+Chr(10)
	    Next nX
		SetSoapFault("PUTCALENDAR",cErro)
		lRetorno := .F.
	EndIf		
	::CalendarID := ::SellerCode+Dtos(::Calendar:EventDate)+::Calendar:StartTime
	
Else

	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetTask   Autor   Eduardo Riera          Data 11.11.2003 
Ĵ
          Metodo de recuperacao das tarefas                            
                                                                       
Ĵ
ParametrosExpC1: Alias                                                 
          ExpO2: Objeto que sera atualizado                            
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio Este metodo atualiza o objeto task passado por referencia    
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Static Function GetTask(cAlias,oObjeto)

oObjeto:TaskID                 := (cAlias)->AD8_TAREFA
oObjeto:Subject                := (cAlias)->AD8_TOPICO
oObjeto:StartDate              := (cAlias)->AD8_DTINI
oObjeto:EndDate                := (cAlias)->AD8_DTFIM
oObjeto:StatusCode             := (cAlias)->AD8_STATUS
oObjeto:StatusDescription      := X3Combo("AD8_STATUS",(cAlias)->AD8_STATUS)
oObjeto:Priority               := (cAlias)->AD8_PRIOR
oObjeto:PriorityDescription    := X3Combo("AD8_PRIOR",(cAlias)->AD8_PRIOR)
oObjeto:PercentComplete        := (cAlias)->AD8_PERC
oObjeto:Note                   := MSMM((cAlias)->AD8_CODMEM)

UserFields("AD8",@oObjeto:UserFields,cAlias)

Return(.T.)

/*/

Ŀ
Funo    GetCalendaAutor   Eduardo Riera          Data 12.11.2003 
Ĵ
          Metodo de recuperacao da agenda do representante             
                                                                       
Ĵ
ParametrosExpC1: Alias                                                 
          ExpO2: Objeto que sera atualizado                            
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio Este metodo atualiza o objeto Calend passado por referencia  
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Static Function GetCalendar(cAlias,oObjeto)

oObjeto:Subject                := (cAlias)->AD7_TOPICO
oObjeto:EventDate              := (cAlias)->AD7_DATA 
oObjeto:StartTime              := (cAlias)->AD7_HORA1
oObjeto:EndTime                := (cAlias)->AD7_HORA2 
oObjeto:Note                   := MSMM((cAlias)->AD7_CODMEM)
oObjeto:CustomerCode           := (cAlias)->AD7_CODCLI
oObjeto:UnitCustomerCode       := (cAlias)->AD7_LOJA
oObjeto:ProspectCode           := (cAlias)->AD7_PROSPE
oObjeto:UnitProspectCode       := (cAlias)->AD7_LOJPRO
oObjeto:Oportunity             := (cAlias)->AD7_NROPOR
oObjeto:Contact                := (cAlias)->AD7_CONTAT

UserFields("AD7",@oObjeto:UserFields,cAlias)

Return(.T.)
