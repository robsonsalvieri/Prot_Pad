#INCLUDE "PROTHEUS.CH"
#INCLUDE "APWIZARD.CH"
#INCLUDE "CONSULTA_CREDITO.CH"

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fConsWizar³Autor  ³ Luiz Couto            ³ Data ³01.04.2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de interface de processamento para consulta de credito³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpA1: Dados de Consulta                                     ³±±
±±³          ³    1: Nome                                                  ³±±
±±³          ³    2: Pessoa Fisica/Juridica                                ³±±
±±³          ³    3: Nome Fantasia                                         ³±±
±±³          ³    4: Endereco                                              ³±±
±±³          ³    5: CEP                                                   ³±±
±±³          ³    6: DDD                                                   ³±±
±±³          ³    7: Telefone                                              ³±±
±±³          ³    8: CGC/CNPJ                                              ³±±
±±³          ³    9: RG/Inscricao Estadual                                 ³±±
±±³          ³   10: Data de Nascimento                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que a operacao de consulta foi realizada com   ³±±
±±³          ³       sucesso                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo demonstra a interface de consulta de credito     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ ECOSSISTEMA/GENERICO                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/               


Function fConsWizard( aDadosCons )
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³CONSULTA_WIZARDºAutor  ³Marcelo Custodio    º Data ³  10/13/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Inclusao e alteracao das consultas:                             º±±
±±º          ³ Cheque+                 -Fisica                                 º±±
±±º          ³ Cheque+(CNPJ)           -Juridica                               º±±
±±º          ³ Informacao Cadastral    -Juridica                               º±±
±±º          ³ Desabonos               -Juridica                               º±±
±±º          ³ Informacao Completa     -Juridica                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/


Local lRetorno 		:= .T.
Local oFont
Local oWizard
Local aTexto 		:= {}
Local aTxtRadPar 	:= {}
Local aRadACSP 	:= {}
Local aAutoACSP 	:= {}
Local aRadAuto    := {}
Local aRadEquifax := {}
Local aChqACSP 	:= Array( 7 ) 

Local aInfoCMC7   := {} 
Local lContinua 	:= .T.
Local oGrRad01
Local oGrRad02
Local oGrRad03
Local oGrRad04
Local oGrRad05
Local oGetACSP01
Local oGetACSP02
Local oGetACSP03
Local oGetACSP04
Local oGetACSP05
Local oGetACSP06
Local oGetACSP07
Local oGetLog01
Local oGetLog02
Local aLogin 		:= {}
Local nGrRad01 		:= 1
Local nGrRad02 		:= 1
Local nGrRad03 		:= 1
Local nGrRad04 		:= 1
Local nGrRad05 		:= 1
Local lFlag 		:= .F.
Local lRetACSP 		:= .T.
Local cLogACSP 		:= ""
Local cSenACSP 		:= ""
Local lRetEquifax 	:= .T.
Local cLogEquifax 	:= ""
Local cSenEquifax 	:= ""
Local nOpcLogin 	:= 0 
Local cOrInf      := "" 

Local lContPELGC := .T. // lContinua so altera na existencia o PE LOGCONWZ 
Local lOk			:= .F. 
Local nX			:= 0                                                     

Local cMoeda      := ""  
Local cTpResult   := ""       

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Tipo de consulta de pessoa fisica ACSP. 1 - Relatorio ( CSR10 ), 2 - Dados ( CSR50 )  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lFisCSR50   := GetNewPar( "MV_ACSPPEF", "1" ) == "2" 

Local cTransacao  := "CSR10"
Local cVerTrans   := "05"


If cPaisLoc <> "BRA"   
	Aviso(STR0036,STR0037,{"OK"})
	Return
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o cliente ja esta habilitado junto ao ecossistema     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
EcoHandShake("MICROSIGA","SHOWECO")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona o SIGAMAT                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SM0")
dbSetOrder(1)
MsSeek(cEmpAnt+cFilAnt)

aAdd( aTexto, STR0001 )
	
DEFINE FONT oFont NAME "Arial" SIZE 0, -11 BOLD

DEFINE WIZARD oWizard TITLE STR0002 HEADER STR0003 MESSAGE STR0004 TEXT aTexto[1] NEXT { || .T. } FINISH { || .T. }

CREATE PANEL oWizard HEADER STR0005 MESSAGE STR0006 BACK { || .T. } NEXT { || IIf( ( ( nGrRad01 == 3 ) .AND. Len( AllTrim( aDadosCons[8] ) ) == 14 ), IIf( nGrRad01 == 3, ( oWizard:SetPanel( 6 ), .T. ), ( oWizard:SetPanel( 5 ), .T. ) ), IIf( nGrRad01 == 1, .T., IIf( nGrRad01 == 3, ( oWizard:SetPanel( 5 ), .T. ), ( MsgAlert( STR0025, STR0003 ), .F. ) ) ) ) } PANEL //"Não há nenhum serviço cadastrado para este Parceiro."###"Atenção"

aAdd( aTxtRadPar, "ACSP" )


@050, ( __DlgWidth( oWizard:GetPanel( 2 ) ) / 2 ) - 10 RADIO oGrRad01 VAR nGrRad01 ITEMS aTxtRadPar[1] SIZE 70, 10 OF oWizard:GetPanel( 2 ) PIXEL 	

Do Case
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Pessoa Fisica  				                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case Len( AllTrim( aDadosCons[8] ) ) <> 14
		//ACSP
		CREATE PANEL oWizard HEADER STR0005 MESSAGE STR0007 BACK { || .T. } NEXT { || IIf( nGrRad02 == 1 , ( oWizard:SetPanel( 9 ), oWizard:nPanel -= 1 ) , ), IIf( nGrRad02 == 5 , ( oWizard:SetPanel( 7 ), oWizard:nPanel -= 1 ) , ), IIf( nGrRad02 == 6 , ( oWizard:SetPanel( 9 ), oWizard:nPanel -= 1 ) , ), IIf( nGrRad02 == 4 , ( oWizard:SetPanel( 11 ), oWizard:nPanel -= 1 ) , ), .T. } PANEL
		
		oWizard:GetPanel( 3 ) 		
		
		aAdd( aRadACSP, {} )
		aAdd( aRadACSP[Len( aRadACSP )], STR0051 )
		aAdd( aRadACSP[Len( aRadACSP )], STR0017 )
		aAdd( aRadACSP[Len( aRadACSP )], STR0008 )
		aAdd( aRadACSP[Len( aRadACSP )], STR0049 )
		aAdd( aRadACSP[Len( aRadACSP )], STR0026 )
		aAdd( aRadACSP[Len( aRadACSP )], STR0052 )
		
		@000, 000 BITMAP oBitMap SIZE 285, 277 RESNAME "ACSP_PFE" PIXEL OF oWizard:GetPanel( 3 ) NOBORDER
		@000, 264 BITMAP oBitMap SIZE 055, 277 RESNAME "ACSP_PFD" PIXEL OF oWizard:GetPanel( 3 ) NOBORDER		
		@042, 170 RADIO oGrRad02 VAR nGrRad02 ITEMS aRadACSP[1][1], aRadACSP[1][2], aRadACSP[1][3], aRadACSP[1][4], aRadACSP[1][5], aRadACSP[1][6] SIZE 70, 10 OF oWizard:GetPanel( 3 ) PIXEL
		
		// ACSP Consulta cheques - opcoes 

		CREATE PANEL oWizard HEADER STR0005 MESSAGE STR0009 BACK { || .T. } NEXT { || If( nGrRad03 == 1, oWizard:SetPanel( 5 ), oWizard:SetPanel( 11 ) ) , oWizard:nPanel -= 1, .T. } PANEL
		
		oWizard:GetPanel( 4 ) 				
		
		aAdd( aRadACSP, {} )
		aAdd( aRadACSP[Len( aRadACSP )], STR0010 )
		aAdd( aRadACSP[Len( aRadACSP )], STR0011 )                   

		@000, 000 BITMAP oBitMap SIZE 285, 277 RESNAME "ACSP_PFE" PIXEL OF oWizard:GetPanel( 4 ) NOBORDER
		@000, 264 BITMAP oBitMap SIZE 055, 277 RESNAME "ACSP_PFD" PIXEL OF oWizard:GetPanel( 4 ) NOBORDER		
		@042, 170 RADIO oGrRad03 VAR nGrRad03 ITEMS aRadACSP[2][1], aRadACSP[2][2] SIZE 70, 10 OF oWizard:GetPanel( 4 ) PIXEL		
		
		//@042, 170 RADIO oGrRad03 VAR nGrRad03 ITEMS aRadACSP[2][1] SIZE 70, 10 OF oWizard:GetPanel( 4 ) PIXEL		
		
		// ACSP digitacao 
		
		CREATE PANEL oWizard HEADER STR0005 MESSAGE STR0012 BACK { || .T. } NEXT { || oWizard:SetPanel( 9 ), oWizard:nPanel -= 1, .T. } PANEL
		
		oWizard:GetPanel( 5 )
		
		aChqACSP[1] := PadR( "", 3 ) 
		aChqACSP[2] := PadR( "", 5 ) 
		aChqACSP[3] := PadR( "", 15 ) 
		aChqACSP[4] := PadR( "", 1 ) 
		aChqACSP[5] := PadR( "", 8 ) 
		aChqACSP[6] := PadR( "", 1 ) 
		
		@000, 000 BITMAP oBitMap SIZE 285, 277 RESNAME "ACSP_PFE" PIXEL OF oWizard:GetPanel( 5 ) NOBORDER
		@000, 264 BITMAP oBitMap SIZE 055, 277 RESNAME "ACSP_PFD" PIXEL OF oWizard:GetPanel( 5 ) NOBORDER		
		@032, 150 SAY STR0013 SIZE 030, 010 OF oWizard:GetPanel( 5 ) PIXEL
		@032, 190 MSGET oGetACSP01 VAR aChqACSP[1] SIZE 070,010 OF oWizard:GetPanel( 5 ) PIXEL

		@047, 150 SAY STR0014 SIZE 030, 010 OF oWizard:GetPanel( 5 ) PIXEL                   
		@047, 190 MSGET oGetACSP02 VAR aChqACSP[2] SIZE 070,010 OF oWizard:GetPanel( 5 ) PIXEL		
		
		@062, 150 SAY STR0015 SIZE 030, 010 OF oWizard:GetPanel( 5 ) PIXEL
		@062, 190 MSGET oGetACSP03 VAR aChqACSP[3] SIZE 070,010 OF oWizard:GetPanel( 5 ) PIXEL

		@077, 150 SAY STR0016 SIZE 030, 010 OF oWizard:GetPanel( 5 ) PIXEL
		@077, 190 MSGET oGetACSP04 VAR aChqACSP[4] SIZE 070,010 OF oWizard:GetPanel( 5 ) PIXEL

		@092, 150 SAY STR0017 SIZE 030, 010 OF oWizard:GetPanel( 5 ) PIXEL
		@092, 190 MSGET oGetACSP05 VAR aChqACSP[5] SIZE 070,010 OF oWizard:GetPanel( 5 ) PIXEL

		@107, 150 SAY STR0018 SIZE 050, 010 OF oWizard:GetPanel( 5 ) PIXEL
		@107, 190 MSGET oGetACSP06 VAR aChqACSP[6] SIZE 070,010 OF oWizard:GetPanel( 5 ) PIXEL
	

		//Equifax
		CREATE PANEL oWizard HEADER STR0005 MESSAGE STR0007 BACK { || oWizard:SetPanel( 3 ), .T. } NEXT { || oWizard:SetPanel( 10 ), oWizard:nPanel -= 1, .T. } PANEL
		 
		oWizard:GetPanel( 6 ) 				
		
		aAdd( aRadEquifax, {} )
		aAdd( aRadEquifax[Len( aRadEquifax )], "Equifax Pessoal" )
		
		@050, 020 BITMAP oBitMap SIZE 80, 30 RESNAME "Equifax" PIXEL OF oWizard:GetPanel( 6 ) NOBORDER
		@050, ( __DlgWidth( oWizard:GetPanel( 6 ) ) / 2 ) - 10 RADIO oGrRad02 VAR nGrRad02 ITEMS aRadEquifax[1][1] SIZE 70, 10 OF oWizard:GetPanel( 6 ) PIXEL
		
        //ACSP UseAuto - Opcoes
		CREATE PANEL oWizard HEADER STR0026 MESSAGE STR0053 BACK { || oWizard:SetPanel( 4 ), .T. } NEXT { || If(nGrRad05 == 2,oWizard:SetPanel( 8 ),), .T. } PANEL //"Consulta Auto"###"Selecione o tipo de consulta UseAuto"
		
		aAdd( aRadAuto, STR0054 )//"RENAVAM"##"Chassi"##"Placa"##"N. Motor"##"Caixa Cambio"##"Renavam"
		aAdd( aRadAuto, STR0064 )//"RENAVAM - Proprietario"##"Proprietario"
		aAdd( aRadAuto, STR0055 )//"Base Estadual"##"Chassi/UF"##"Placa/UF"
		aAdd( aRadAuto, STR0056 )//"Ocorrência Roubo/Furto"##"Chassi"##"Placa"
		aAdd( aRadAuto, STR0057 )//"DPVAT"##"Chassi"
		aAdd( aRadAuto, STR0058 )//"GRAVAME"##"Chassi"
		aAdd( aRadAuto, STR0059 )//"RENAVAM/DPVAT/GRAVAME"##"Chassi"
		
		@000, 000 BITMAP oBitMap SIZE 285, 277 RESNAME "ACSP_PFE" PIXEL OF oWizard:GetPanel( 7 ) NOBORDER
		@000, 264 BITMAP oBitMap SIZE 055, 277 RESNAME "ACSP_PFD" PIXEL OF oWizard:GetPanel( 7 ) NOBORDER		
		@042, 170 RADIO oGrRad05 VAR nGrRad05 ITEMS aRadAuto[1], aRadAuto[2], aRadAuto[3], aRadAuto[4], aRadAuto[5], aRadAuto[6] SIZE 70, 10 OF oWizard:GetPanel( 7 ) PIXEL

	//ACSP UseAuto
		CREATE PANEL oWizard HEADER STR0026 MESSAGE STR0027 BACK { || .T. } NEXT { || .T. } PANEL //"Consulta Auto"###"Digite os dados para consulta e o estado"
		
		oWizard:GetPanel( 8 ) 				
					         
		aAdd( aAutoACSP, PadR( "", 7  ) )//Placa
		aAdd( aAutoACSP, PadR( "", 2  ) )//UF
		aAdd( aAutoACSP, PadR( "", 21 ) )//Chassi
		aAdd( aAutoACSP, PadR( "", 21 ) )//Motor
		aAdd( aAutoACSP, PadR( "", 9  ) )//Renavam
		aAdd( aAutoACSP, PadR( "", 20 ) )//Caixa de Cambio

		@000, 000 BITMAP oBitMap SIZE 285, 277 RESNAME "ACSP_PFE" PIXEL OF oWizard:GetPanel( 8 ) NOBORDER
		@000, 264 BITMAP oBitMap SIZE 055, 277 RESNAME "ACSP_PFD" PIXEL OF oWizard:GetPanel( 8 ) NOBORDER				
		@020, 150 SAY STR0060 SIZE 030, 010 OF oWizard:GetPanel( 8 ) PIXEL //"Chassi"
		@020, 190 MSGET oGetACSP07 VAR aAutoACSP[3] SIZE 070,010 OF oWizard:GetPanel( 8 ) PICTURE "@!" PIXEL
		
		@035, 150 SAY STR0028 SIZE 030, 010 OF oWizard:GetPanel( 8 ) PIXEL //"Placa"
		@035, 190 MSGET oGetACSP07 VAR aAutoACSP[1] SIZE 070,010 OF oWizard:GetPanel( 8 ) WHEN (nGrRad05 <= 4) PICTURE "@!" PIXEL
	
		@050, 150 SAY STR0061 SIZE 030, 010 OF oWizard:GetPanel( 8 ) PIXEL //"Motor"
		@050, 190 MSGET oGetACSP07 VAR aAutoACSP[4] SIZE 070,010 OF oWizard:GetPanel( 8 ) WHEN (nGrRad05 == 1) PICTURE "@!" PIXEL
		                                          
		@065, 150 SAY STR0062 SIZE 030, 010 OF oWizard:GetPanel( 8 ) PIXEL //"Renavam"
		@065, 190 MSGET oGetACSP07 VAR aAutoACSP[5] SIZE 070,010 OF oWizard:GetPanel( 8 ) WHEN (nGrRad05 == 1) PICTURE "@!" PIXEL
		
		@080, 150 SAY STR0063 SIZE 030, 010 OF oWizard:GetPanel( 8 ) PIXEL //Cx Cambio"
		@080, 190 MSGET oGetACSP07 VAR aAutoACSP[6] SIZE 070,010 OF oWizard:GetPanel( 8 ) WHEN (nGrRad05 == 1) PICTURE "@!" PIXEL
		
		@095, 150 SAY STR0029 SIZE 030, 010 OF oWizard:GetPanel( 8 ) PIXEL //"Estado"
		@095, 190 MSGET oGetACSP07 VAR aAutoACSP[2] SIZE 070,010 OF oWizard:GetPanel( 8 ) WHEN (nGrRad05 == 3) PIXEL F3 "12" HASBUTTON 
		
		//Painel Final ACSP
		CREATE PANEL oWizard HEADER STR0019 MESSAGE "" BACK { || If( nGrRad02 == 2 .OR. nGrRad02 == 3 , If( nGrRad03 == 1,oWizard:SetPanel( 5 ),oWizard:SetPanel( 11 )), If( nGrRad02==1 .Or. nGrRad02==6,oWizard:SetPanel( 3 ), If( nGrRad02==4,oWizard:SetPanel( 11 ),If( nGrRad05==2,oWizard:SetPanel( 7 ),oWizard:SetPanel( 8 ) ) ) ) ) , oWizard:nPanel += 1, .T. } NEXT { || .T. } FINISH { || lFlag := .T. } EXEC {|| oWizard:SetFinish() } PANEL
		
		oWizard:GetPanel( 9 ) 				
		
		@000, 000 BITMAP oBitMap SIZE 285, 277 RESNAME "ACSP_PFE" PIXEL OF oWizard:GetPanel( 9 ) NOBORDER
		@000, 264 BITMAP oBitMap SIZE 055, 277 RESNAME "ACSP_PFD" PIXEL OF oWizard:GetPanel( 9 ) NOBORDER		
		@060, 170 SAY STR0020 SIZE 100, 040 OF oWizard:GetPanel( 9 ) PIXEL

		//Painel Final Equifax
		CREATE PANEL oWizard HEADER STR0019 MESSAGE "" BACK { || oWizard:SetPanel( 8 ), .T. } NEXT { || .T. } FINISH { || lFlag := .T. } EXEC {|| oWizard:SetFinish() } PANEL
		
		oWizard:GetPanel( 10 ) 						
		
		@050, 020 BITMAP oBitMap SIZE 80, 30 RESNAME "Equifax" PIXEL OF oWizard:GetPanel( 10 ) NOBORDER
		@060, 110 SAY STR0020 SIZE 100, 040 OF oWizard:GetPanel( 10 ) PIXEL                            
		
		// ACSP Cheque CMC-7 
		
		CREATE PANEL oWizard HEADER STR0005 MESSAGE STR0012 BACK { || If( nGrRad02 == 4,oWizard:SetPanel( 3 ),oWizard:SetPanel( 4 )), oWizard:nPanel += 1, .T. } NEXT { || oWizard:SetPanel( 9 ), oWizard:nPanel -= 1, .T. } PANEL

		oWizard:GetPanel( 11 ) 				
		
		aChqACSP[7] := PadR( "", 30 ) 
		
		@000, 000 BITMAP oBitMap SIZE 285, 277 RESNAME "ACSP_PFE" PIXEL OF oWizard:GetPanel( 11 ) NOBORDER
		@000, 264 BITMAP oBitMap SIZE 055, 277 RESNAME "ACSP_PFD" PIXEL OF oWizard:GetPanel( 11 ) NOBORDER		
		@053, 150 SAY STR0038 SIZE 040, 010 OF oWizard:GetPanel( 11 ) PIXEL // "Codigo CMC-7"
		@063, 150 MSGET oGetACSP01 VAR aChqACSP[7] SIZE 110,010 OF oWizard:GetPanel( 11 ) PIXEL
		
		//Painel Final "Dummy" - Este painel foi criado apenas para que o painel 10 nao seja classificado como "finish"
		CREATE PANEL oWizard HEADER STR0019 MESSAGE "" BACK { || oWizard:SetPanel( 8 ) , .T. } NEXT { || .T. } FINISH { || lFlag := .T. } EXEC {|| oWizard:SetFinish() } PANEL
		
		oWizard:GetPanel( 12 ) 
		
		@050, 020 BITMAP oBitMap SIZE 80, 30 RESNAME "Equifax" PIXEL OF oWizard:GetPanel( 12 ) NOBORDER
		@060, 110 SAY STR0020 SIZE 100, 040 OF oWizard:GetPanel( 12 ) PIXEL
		
		
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Pessoa Juridica                                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case Len( AllTrim( aDadosCons[8] ) ) == 14
		//ACSP
		CREATE PANEL oWizard HEADER STR0005 MESSAGE STR0021 BACK { || .T. } NEXT { || IIf( nGrRad02 == 6, oWizard:SetPanel( 4 ), IIf( nGrRad02 == 7, oWizard:SetPanel( 11 ), oWizard:SetPanel( 8 ) ) ), oWizard:nPanel -= 1, .T. } PANEL
		
		aAdd( aRadACSP, {} )
		aAdd( aRadACSP[Len( aRadACSP )], STR0047 )
		aAdd( aRadACSP[Len( aRadACSP )], STR0050 )  
		aAdd( aRadACSP[Len( aRadACSP )], STR0023 )
		aAdd( aRadACSP[Len( aRadACSP )], STR0048 )
		aAdd( aRadACSP[Len( aRadACSP )], STR0022 )
		aAdd( aRadACSP[Len( aRadACSP )], STR0017 )//adicionei o cheque pra pessoa juridica.
		aAdd( aRadACSP[Len( aRadACSP )], STR0049 )

		
		@000, 000 BITMAP oBitMap SIZE 285, 277 RESNAME "ACSP_PJE" PIXEL OF oWizard:GetPanel( 3 ) NOBORDER
		@000, 264 BITMAP oBitMap SIZE 055, 277 RESNAME "ACSP_PJD" PIXEL OF oWizard:GetPanel( 3 ) NOBORDER				
		@042,170 RADIO oGrRad02 VAR nGrRad02 ITEMS aRadACSP[1][1], aRadACSP[1][2], aRadACSP[1][3], aRadACSP[1][4], aRadACSP[1][5], aRadACSP[1][6], aRadACSP[1][7] SIZE 70, 10 OF oWizard:GetPanel( 3 ) PIXEL//mexi
                                        
		// ACSP Consulta cheques - opcoes            
     
		CREATE PANEL oWizard HEADER STR0005 MESSAGE STR0009 BACK { || .T. } NEXT { || If( nGrRad03 == 1, oWizard:SetPanel( 5 ), oWizard:SetPanel( 11 ) ) , oWizard:nPanel -= 1, .T. } PANEL
		
		aAdd( aRadACSP, {} )
		aAdd( aRadACSP[Len( aRadACSP )], STR0010 )
		aAdd( aRadACSP[Len( aRadACSP )], STR0011 )

		@000, 000 BITMAP oBitMap SIZE 285, 277 RESNAME "ACSP_PJE" PIXEL OF oWizard:GetPanel( 4 ) NOBORDER
		@000, 264 BITMAP oBitMap SIZE 055, 277 RESNAME "ACSP_PJD" PIXEL OF oWizard:GetPanel( 4 ) NOBORDER		
		@042, 170 RADIO oGrRad03 VAR nGrRad03 ITEMS aRadACSP[2][1], aRadACSP[2][2] SIZE 70, 10 OF oWizard:GetPanel( 4 ) PIXEL		
		
		CREATE PANEL oWizard HEADER STR0005 MESSAGE STR0012 BACK { || .T. } NEXT { || oWizard:SetPanel( 8 ), oWizard:nPanel -= 1, .T. } PANEL
		
		aChqACSP[1] := PadR( "", 3 ) 
		aChqACSP[2] := PadR( "", 5 ) 
		aChqACSP[3] := PadR( "", 15 ) 
		aChqACSP[4] := PadR( "", 1 ) 
		aChqACSP[5] := PadR( "", 8 ) 
		aChqACSP[6] := PadR( "", 1 ) 
		
		@000, 000 BITMAP oBitMap SIZE 285, 277 RESNAME "ACSP_PJE" PIXEL OF oWizard:GetPanel( 5 ) NOBORDER
		@000, 264 BITMAP oBitMap SIZE 055, 277 RESNAME "ACSP_PJD" PIXEL OF oWizard:GetPanel( 5 ) NOBORDER						
		@032, 150 SAY STR0013 SIZE 030, 010 OF oWizard:GetPanel( 5 ) PIXEL
		@032, 190 MSGET oGetACSP01 VAR aChqACSP[1] SIZE 070,010 OF oWizard:GetPanel( 5 ) PIXEL
		@047, 150 SAY STR0014 SIZE 030, 010 OF oWizard:GetPanel( 5 ) PIXEL
		@047, 190 MSGET oGetACSP02 VAR aChqACSP[2] SIZE 070,010 OF oWizard:GetPanel( 5 ) PIXEL
		@062, 150 SAY STR0015 SIZE 030, 010 OF oWizard:GetPanel( 5 ) PIXEL
		@062, 190 MSGET oGetACSP03 VAR aChqACSP[3] SIZE 070,010 OF oWizard:GetPanel( 5 ) PIXEL
		@077, 150 SAY STR0016 SIZE 030, 010 OF oWizard:GetPanel( 5 ) PIXEL
		@077, 190 MSGET oGetACSP04 VAR aChqACSP[4] SIZE 070,010 OF oWizard:GetPanel( 5 ) PIXEL
		@092, 150 SAY STR0017 SIZE 030, 010 OF oWizard:GetPanel( 5 ) PIXEL
		@092, 190 MSGET oGetACSP05 VAR aChqACSP[5] SIZE 070,010 OF oWizard:GetPanel( 5 ) PIXEL		
		@107, 150 SAY STR0018 SIZE 050, 010 OF oWizard:GetPanel( 5 ) PIXEL
		@107, 190 MSGET oGetACSP06 VAR aChqACSP[6] SIZE 070,010 OF oWizard:GetPanel( 5 ) PIXEL		

		CREATE PANEL oWizard HEADER STR0030 MESSAGE STR0031 BACK { || oWizard:SetPanel( 2 ), oWizard:nPanel++, .T. } NEXT { || oWizard:SetPanel( 9 ), oWizard:nPanel-- , .T. } PANEL //"Atencao"###"Selecione um dos servicos abaixo"


		aAdd( aRadACSP, {} )
		aAdd( aRadACSP[Len( aRadACSP )], "Relato" )
		
		@050, 020 BITMAP oBitMap SIZE 70, 30 RESNAME "SERASA" PIXEL OF oWizard:GetPanel( 6 ) NOBORDER
		@055, ( __DlgWidth( oWizard:GetPanel( 6 ) ) / 2 ) - 10 RADIO oGrRad03 VAR nGrRad03 ITEMS aRadACSP[Len( aRadACSP )][1] SIZE 70, 10 OF oWizard:GetPanel( 6 ) PIXEL

		//EQUIFAX
		CREATE PANEL oWizard HEADER STR0030 MESSAGE STR0031 BACK { || IIf( nGrRad01 == 3, ( oWizard:SetPanel( 4 ), oWizard:nPanel -= 1 ), ), .T. } NEXT { || oWizard:SetPanel( 10 ), oWizard:nPanel -= 1, .T. } PANEL //"Atencao"###"Selecione um dos servicos abaixo"

		aAdd( aRadEquifax, {} )
		aAdd( aRadEquifax[Len( aRadEquifax )], "Equifax Empresarial" )
		aAdd( aRadEquifax[Len( aRadEquifax )], "Equifax SCORE Empresarial" )
		
		@050, 020 BITMAP oBitMap SIZE 80, 30 RESNAME "EQUIFAX" PIXEL OF oWizard:GetPanel( 7 ) NOBORDER
		@055, ( __DlgWidth( oWizard:GetPanel( 7 ) ) / 2 ) - 10 RADIO oGrRad04 VAR nGrRad04 ITEMS aRadEquifax[Len( aRadEquifax )][1], aRadEquifax[Len( aRadEquifax )][2] SIZE 80, 10 OF oWizard:GetPanel( 7 ) PIXEL

		//Painel Final ACSP
		//CREATE PANEL oWizard HEADER STR0019 MESSAGE "" BACK { || oWizard:SetPanel( 4 ), .T. } NEXT { || .T. } FINISH { || lFlag := .T. } EXEC {|| oWizard:SetFinish() } PANEL

		CREATE PANEL oWizard HEADER STR0019 MESSAGE "" BACK { || If( nGrRad02 == 6, If( nGrRad03 == 1,oWizard:SetPanel( 5 ),oWizard:SetPanel( 11 )), If( nGrRad02 == 7,oWizard:SetPanel( 11 ),oWizard:SetPanel( 3 ) ) ), oWizard:nPanel += 1, .T. } NEXT { || .T. } FINISH { || lFlag := .T. } EXEC {|| oWizard:SetFinish() } PANEL
                  
		//BACK { || If( nGrRad02 == 1 .OR. nGrRad02 == 2, If( nGrRad03 == 1,oWizard:SetPanel( 5 ),oWizard:SetPanel( 10 )), If( nGrRad02==3 .Or. nGrRad02==4 .Or. nGrRad02==6,oWizard:SetPanel( 3 ), oWizard:SetPanel( 7 ) ) ) , oWizard:nPanel += 1, .T. }

		@000, 000 BITMAP oBitMap SIZE 285, 277 RESNAME "ACSP_PJE" PIXEL OF oWizard:GetPanel( 8 ) NOBORDER
		@000, 264 BITMAP oBitMap SIZE 055, 277 RESNAME "ACSP_PJD" PIXEL OF oWizard:GetPanel( 8 ) NOBORDER								
		@060, 170 SAY STR0020 SIZE 100, 040 OF oWizard:GetPanel( 8 ) PIXEL

		//Painel Final EQUIFAX
		CREATE PANEL oWizard HEADER STR0019 MESSAGE "" BACK { || IIf( nGrRad01 == 3, ( oWizard:SetPanel( 9 ), oWizard:nPanel -= 1 ), ), .T. } NEXT { || .T. } FINISH { || lFlag := .T. } EXEC {|| oWizard:SetFinish() } PANEL

		@050, 020 BITMAP oBitMap SIZE 80, 30 RESNAME "EQUIFAX" PIXEL OF oWizard:GetPanel( 10 ) NOBORDER
		@060, 110 SAY STR0020 SIZE 100, 040 OF oWizard:GetPanel( 10 ) PIXEL
		
		// ACSP Cheque CMC-7 
		
		CREATE PANEL oWizard HEADER STR0005 MESSAGE STR0012 BACK { || IIf(nGrRad02 == 7,oWizard:SetPanel( 3 ),oWizard:SetPanel( 4 )), oWizard:nPanel += 1, .T. } NEXT { || oWizard:SetPanel( 8 ), oWizard:nPanel -= 1, .T. } PANEL
		
		aChqACSP[7] := PadR( "", 30 ) 
		
		@000, 000 BITMAP oBitMap SIZE 285, 277 RESNAME "ACSP_PJE" PIXEL OF oWizard:GetPanel( 11 ) NOBORDER
		@000, 264 BITMAP oBitMap SIZE 055, 277 RESNAME "ACSP_PJD" PIXEL OF oWizard:GetPanel( 11 ) NOBORDER		
		                                                                               
		@053, 150 SAY STR0038 SIZE 040, 010 OF oWizard:GetPanel( 11 ) PIXEL
		@063, 150 MSGET oGetACSP01 VAR aChqACSP[7] SIZE 110,010 OF oWizard:GetPanel( 11 ) PIXEL
	
		//Painel Final "Dummy" - Este painel foi criado apenas para que o painel 11 nao seja classificado como "finish"
		CREATE PANEL oWizard HEADER STR0019 MESSAGE "" BACK { || oWizard:SetPanel( 7 ), .T. } NEXT { || .T. } FINISH { || lFlag := .T. } EXEC {|| oWizard:SetFinish() } PANEL
		
		@050, 020 BITMAP oBitMap SIZE 80, 30 RESNAME "Equifax" PIXEL OF oWizard:GetPanel( 12 ) NOBORDER
		@060, 110 SAY STR0020 SIZE 100, 040 OF oWizard:GetPanel( 12 ) PIXEL
		
EndCase

oWizard:oCancel:bAction := {|| lFlag:=.F., oWizard:oDlg:End() }

ACTIVATE WIZARD oWizard CENTERED
 
ll := ""
If lFlag
	Do Case
		Case nGrRad01 == 1 .AND. ( nGrRad02 == 1 .OR. nGrRad02 == 2 .OR. nGrRad02 == 3 .OR. nGrRad02 == 4 .OR. nGrRad02 == 5 .Or. nGrRad02 == 6 .Or. nGrRad02 == 7)
			While !lOk              
				If Empty(aLogin)
					aAdd( aLogin, PadR( "", 15 ) ) // Usuario
					aAdd( aLogin, PadR( "", 15 ) ) // Senha 
					AAdd( aLogin, .F. ) // Nao sou cliente
				EndIf
	
				If GetPassACSP( @aLogin )
					Do Case
						Case Len( AllTrim( aDadosCons[8] ) ) <> 14
							Do Case      
								Case nGrRad02 == 1
									cServiceID := "CN"//SCPC
								Case nGrRad02 == 2
									cServiceID := "CHEQUE"
								Case nGrRad02 == 3
									cServiceID := "CIN"//Consulta Integrada
								Case nGrRad02 == 4
									cServiceID := "CHEQUE+"
								Case nGrRad02 == 5
									Do Case
										Case nGrRad05 == 1//Consulta Renavam
											Do Case 
												Case !Empty(aAutoACSP[1])//Renavam por placa
													cServiceId := "RENA902"
												Case !Empty(aAutoACSP[3])//Renavam por chassi
													cServiceId := "RENA901"
												Case !Empty(aAutoACSP[4])//Renavam por Num Motor
													cServiceId := "RENA903"
												Case !Empty(aAutoACSP[5])//Renavam por Num Renavam
													cServiceId := "RENA913"
												Case !Empty(aAutoACSP[6])//Renavam por Num. Caixa de Cambio
													cServiceId := "RENA909"
											EndCase
										Case nGrRad05 == 2//Consulta Renavam por proprietario
											cServiceId := "RENA910"
										Case nGrRad05 == 3//Consulta Base Estadual por Placa/Chassi
											cServiceId := If(!Empty(aAutoACSP[1]),"RENA942","RENA941")
										Case nGrRad05 == 4//Consulta Ocorrencia Roubo/Furto por placa/chassi
											cServiceId := If(!Empty(aAutoACSP[1]),"RENA908","RENA907")
										Case nGrRad05 == 5//Consulta DPVAT por Chassi
											cServiceId := "RENA904"
										Case nGrRad05 == 6//Consulta GRAVAME por Chassi
											cServiceId := "RENA905"
										Case nGrRad05 == 7//Consulta RENAVAM/DPVAT/GRAVAME por Chassi
											cServiceId := "RENA906"
									End Case
								Case nGrRad02 == 6
									cServiceID := "USEFONE"									
							EndCase
						Case Len( AllTrim( aDadosCons[8] ) ) == 14
							Do Case              
								Case nGrRad02 == 1
									cServiceID := "ACE"//Informacao Completa                      
								Case nGrRad02 == 2
									cServiceID := "PACE"//Desabono Estadual
								Case nGrRad02 == 3
									cServiceID := "SCPCE"//SCPC Empresarial
								Case nGrRad02 == 4
									cServiceID := "ICA"//Informacao Cadastral
								Case nGrRad02 == 5
									cServiceID := "SINTPJ"//Sintese Cadastral
								Case nGrRad02 == 6
									cServiceID := "CHEQUE"
								Case nGrRad02 == 7
									cServiceID := "CHEQUE+"
							EndCase
					EndCase
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Chama a tela de GetPartnerWeb                                     ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If aLogin[3]
						ShowEco( "ACSP", cServiceID )
						lOk 		:= .T.
						lRetACSP := .F.
					Else
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Realiza o HandShake com cada Parceiro                             ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If EcoHandShake( "ACSP", cServiceID, @aLogin[1], @aLogin[2], .F. )
							lOk 		:= .T.
							lRetACSP := .T.          
						Else 
							ShowEco( "ACSP", cServiceID )
							lOk 		 := .F. 
							lRetACSP  := .F.
						EndIf
					EndIf
			
					If lRetACSP
						Do Case
							Case Len( AllTrim( aDadosCons[8] ) ) <> 14
								Do Case
									Case nGrRad02 == 3 .OR. nGrRad02 == 2 .OR. nGrRad02 == 4
										If nGrRad02 == 3
											cTipoCons := "CIN"           
										ElseIf nGrRad02 == 2
											If lFisCSR50 
												cTipoCons := "CHEQUE"
											Else 
												cTipoCons := "CE"
											EndIf 																															
										Else
											If lFisCSR50 
												cTransacao := "CSR50"
												cVerTrans    := "07" 
											Else 
												cTransacao := "CSR10"
												cVerTrans  := "05" 
											EndIf        
										EndIf  
										
										If nGrRad02 == 4
											cTipoCons := "CHEQUE+"	
											cTransacao := "CSR10"
											cVerTrans := "01" 										
										EndIf  
										
										aInfoCMC7 := { , , , } 
										cOrInf    := NIL 
					                              
										If nGrRad03 == 2 .OR. nGrRad02 == 4
											aInfoCMC7 := { Left( aChqACSP[7], 8 ), Substr(aChqACSP[7],9,10),Right( aChqACSP[7], 12 ) , }
											cOrInf := "C" 											
											
										EndIf 

										fConsACSP( { cTransacao, cVerTrans, aLogin[1], aLogin[2], cTipoCons, "2", "1" }, ;
													{ aDadosCons[8], aDadosCons[9], NIL }, ;
													{ aDadosCons[1], aDadosCons[10], aDadosCons[4], aDadosCons[5], aDadosCons[6], aDadosCons[7] }, ;
													{ "XX", NIL }, ;
													{ , , , }, ;                                    
													{ aChqACSP[1], aChqACSP[2], aChqACSP[3], aChqACSP[4], aChqACSP[5], aChqACSP[6] }, ;
													aInfoCMC7, ;
													cOrInf, NIL )         
													
														
									Otherwise
										Do Case
											Case nGrRad02 == 1
												
												If lFisCSR50 
													cTransacao := "CSR50"
													cVerTrans  := "08" 
													cTipoCons  := "SPC"
												Else 
													cTransacao := "CSR10"
													cVersTrans := "05" 
													cTipoCons  := "CN"
												EndIf
												
												fConsACSP( { cTransacao, cVerTrans, aLogin[1], aLogin[2], cTipoCons, "2", "1" }, ;
													{ aDadosCons[8], aDadosCons[9], NIL }, ;
													{ aDadosCons[1], aDadosCons[10], aDadosCons[4], aDadosCons[5], aDadosCons[6], aDadosCons[7] }, ;
													{ "XX", NIL }, ;
													{ , , , }, ;
													{ , , , , , }, ;
													{ , , , }, ;
													NIL, NIL )
												
											Case nGrRad02 == 4
											
												cTipoCons := "CEC"
						  						fConsACSP( { "CSR50", "08", aLogin[1], aLogin[2], cTipoCons, "2", "1" }, ;
													{ aDadosCons[8], aDadosCons[9], NIL }, ;
													{ aDadosCons[1], aDadosCons[10], aDadosCons[4], aDadosCons[5], aDadosCons[6], aDadosCons[7] }, ;
													{ "XX", NIL }, ;
													{ , , , }, ;
													{ , , , , , }, ;
													{ , , , }, ;
													NIL, NIL )             

											Case nGrRad02 == 5
												cTipoCons := cServiceID
												fConsAuto( { aLogin[1], aLogin[2], "CSR10", "02", cTipoCons, aDadosCons[8], aAutoACSP[1], aAutoACSP[2], aAutoACSP[3], aAutoACSP[4], aAutoACSP[5], aAutoACSP[6] } )
													
											Case nGrRad02 == 6
												cTipoCons := "USEFONE"
												
												If lFisCSR50 
													cTransacao := "CSR50"
													cVerTrans  := "02" 
												Else 
													cTransacao := "CSR10"
													cVerTrans := "04" 
												EndIf
												
												fConsPhone( { cTransacao, cVerTrans, aLogin[1], aLogin[2], "USEFONE", "2", aDadosCons[8], aDadosCons[6], aDadosCons[7], "", "", aDadosCons[5] } )

										EndCase
								EndCase
							Case Len( AllTrim( aDadosCons[8] ) ) == 14
								Do Case    
									Case nGrRad02 == 1
										cTipoCons := "ACE"
										cVerTrans := "05"   
									Case nGrRad02 == 2
										cTipoCons := "PACE"
										cVerTrans := "05"     
									Case nGrRad02 == 3
										cTipoCons := "SCPCE"
										cVerTrans := "05"            
									Case nGrRad02 == 4
								  		cTipoCons := "ICA"    
										cVerTrans  := "09"  
									Case nGrRad02 == 5
										cTipoCons := "SINTPJ"    
										cVerTrans := "05"
									Case nGrRad02 == 6      
										If lFisCSR50 
											cTipoCons := "CHEQUE"
											cTransacao := "CSR50"
											cVerTrans  := "07" 
										Else 
											cTipoCons := "CE"
											cTransacao := "CSR10"
											cVersTrans := "05" 
										EndIf        
									Case nGrRad02 == 7
								  		cTipoCons := "CHEQUE+"
										cTransacao := "CSR10"
										cVerTrans  := "01"        
								EndCase
								
								If cTipoCons == "CHEQUE" .or. cTipoCons == "CE"
								
									aInfoCMC7 := { , , , }
									cOrInf := NIL 
									
									If nGrRad03 == 2 .OR. nGrRad02 == 4
										aInfoCMC7 := { Left( aChqACSP[7], 8 ), Substr(aChqACSP[7],9,10),Right( aChqACSP[7], 12 ) , }
										cOrInf    := "C"										
									EndIf 
								
									fConsACSP( { cTransacao, cVerTrans, aLogin[1], aLogin[2], cTipoCons, "2", "2" }, ;
													{ aDadosCons[8], aDadosCons[9], NIL }, ;
													{ aDadosCons[1], aDadosCons[10], aDadosCons[4], aDadosCons[5], aDadosCons[6], aDadosCons[7] }, ;
													{ "XX", NIL }, ;     
													{ , , , }, ;
													{ aChqACSP[1], aChqACSP[2], aChqACSP[3], aChqACSP[4], aChqACSP[5], aChqACSP[6] }, ;
													aInfoCMC7, ;
													cOrInf, NIL)

									
													
								Else
									fConsACSP( { "CSR10", cVerTrans, aLogin[1], aLogin[2], cTipoCons, "2", "1" }, ;
												{ aDadosCons[8], aDadosCons[9], NIL }, ;
												{ aDadosCons[1], aDadosCons[10], aDadosCons[4], aDadosCons[5], aDadosCons[6], aDadosCons[7] }, ;
												{ "XX", NIL }, ;
												{ , , , }, ;
												{ , , , , , }, ;
												{ , , , }, ;
												NIL, NIL )
								EndIf
						EndCase
					EndIf
				Else
					lOk := .T.
				EndIf
			EndDo

		Case nGrRad01 == 3
			Do Case
				Case Len( AllTrim( aDadosCons[8] ) ) == 14
					Do Case
						Case nGrRad04 == 1	
							cTipoCons := "E"
							cServiceID := "EQUIFAXE"
						Case nGrRad04 == 2
							cTipoCons := "S"
							cServiceID := "EQUIFAXS"
					EndCase
				Case Len( AllTrim( aDadosCons[8] ) ) <> 14
					cTipoCons := "P"
					cServiceID := "EQUIFAXP"
			EndCase
				
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Realiza o HandShake com cada Parceiro                             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !EcoHandShake( "EQUIFAX", cServiceID, @cLogEquifax, @cSenEquifax )
				ShowEco( "EQUIFAX", cServiceID )
				
				lRetEQUIFAX := .F.
			EndIf
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica o login e senha                                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Empty( cLogEquifax ) .OR. lRetEquifax == .F.
				ShowEco( "EQUIFAX", cServiceID )
		
				lRetEQUIFAX := .F.
			EndIf

			fConsEquifax( { cLogEquifax, cSenEquifax, cTipoCons, aDadosCons[8] } )
			
	EndCase    
EndIf

Return lRetorno                      

	


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetPassACSP³Autor  ³ Sergio Silveira      ³ Data ³20/07/2005 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Funcao de captura do usuario / senha da associacao comercial ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpA1: Dados de Consulta                                     ³±±
±±³          ³    1: ExpC1 - Usuario                                       ³±±
±±³          ³    2: ExpC2 - Senha                                         ³±±
±±³          ³    3: ExpLl - Indica se e cliente ACSP                      ³±±
±±³          ³                                                             ³±±
±±³          ³    O array e passado por referencia e alimentado pela funcao³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica se confirmou a digitacao usuario / senha       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ ECOSSISTEMA/ACSP                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function GetPassACSP( aLogin ) 
  
Local lOk := .F. 

Local oDlgLogin 
Local oPanelLogin                   
Local oGetLog01
Local oGetLog02 
Local oBmpLogin 
Local lLOGCONWZ  := ExistBlock("LOGCONWZ")

If lLOGCONWZ
	aLogUsr := Execblock("LOGCONWZ",.F.,.F.)
	If (Type("aLogUsr") == "A" .And. Len(aLogUsr) == Len(aLogin))
		aLogin    := aClone(aLogUsr)
		lOk := .T.
	Else
		lLOGCONWZ := .F. // Se o retorno do PE for invalido nao utiliza ponto de entrada.
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Executa tela de login caso o retorno do PE LOGCONWZ esteja incorreto ou caso o PE nao exista³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lLOGCONWZ

	DEFINE MSDIALOG oDlgLogin TITLE STR0032 FROM 09, 00 TO 20, 42 //"Login ACSP"
	@000,000 MSPANEL oPanelLogin PROMPT "" SIZE __DlgWidth( oDlgLogin ), __DlgHeight( oDlgLogin ) OF oDlgLogin
	
	@020, 080 MSGET oGetLog01 VAR aLogin[1]          SIZE 070,010 OF oDlgLogin PIXEL 
	@035, 080 MSGET oGetLog02 VAR aLogin[2] PASSWORD SIZE 070,010 OF oDlgLogin PIXEL 
	
	DEFINE SBUTTON FROM 065, 103 TYPE 1 ACTION ( lOk := .T., oDlgLogin:End() ) ENABLE OF oPanelLogin
	DEFINE SBUTTON FROM 065, 133 TYPE 2 ACTION ( lOk := .F., oDlgLogin:End() ) ENABLE OF oPanelLogin						
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ O checkbox sera o ultimo objeto a ganhar o foco                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	@052, 005 CHECKBOX aLogin[3] PROMPT STR0039 PIXEL SIZE 80,09 //"Não sou cliente da ACSP" 
	
	@001, 000 BITMAP oBmpLogin RESNAME "ACSP_LOGINE" OF oPanelLogin SIZE 155, 093 NOBORDER PIXEL 
	@001, 150 BITMAP oBmpLogin RESNAME "ACSP_LOGIND" OF oPanelLogin SIZE 034, 093 NOBORDER PIXEL 
	oPanelLogin:Align  := CONTROL_ALIGN_ALLCLIENT
	
	ACTIVATE MSDIALOG oDlgLogin CENTERED
	
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Preenche o usuario com zeros a esquerda                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                                             
aLogin[1] := AllTrim( aLogin[1] ) 
                                                     
If Len( aLogin[1] ) < 8 
	aLogin[1] := PadL( aLogin[1], 8, "0" )  
EndIf	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ A senha da ACSP e sempre maiuscula                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aLogin[2] := Upper( aLogin[2] ) 

Return( lOk ) 
                    

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³FConsSeek ³ Autor ³ Sergio Silveira       ³ Data ³08/08/2005 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Seek em uma getdados sem dicionario de dados                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1 : Objeto corrente da getdados                          ³±±
±±³          ³ExpC2 : Texto da janela                                      ³±±
±±³          ³ExpA3 : aHeader      ( Opcional )                            ³±±
±±³          ³ExpA4 : aCols        ( Opcional )                            ³±±
±±³          ³ExpL1 : Ordena aCols ( Opcional )                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo abrir uma janela de pesquisa   ³±±
±±³          ³em browses de getdados poisicionando na llinha caso encontre ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Function FConsSeek( oGetDados , cTitulo , aHeaderPar , aColsPar , lOrdena )

Local aSvKeys		:= GetKeys()
Local aArea			:= GetArea()
Local aAreaSX3		:= SX3->( GetArea() )
Local aCpoBusca 	:= {}
Local aClassData	:= ClassDataArr( oGetDados )	
Local aComboBox		:= { AllTrim( STR0040) , AllTrim( STR0041) , AllTrim( STR0042 ) } //"Exata"###"Parcial"###"Contem"

Local bAscan		:= { || .F. }
Local bGdChgPict
Local bSet15
Local bSet24
Local bDialogInit

Local cPesq
Local cBusca
Local cAscan
Local cOpcAsc		:= aComboBox[3]	//"Contem"
                                 
Local nOpca			:= 0
Local nPos     		:= 0
Local nTipo    		:= 1
Local nBegin
Local nEnd			
Local nLoop
Local nLoops

Local lTpBusca 		:= .T.
Local lAcima		:= .F.
Local lNewGd   		:= .F.

Local oDlg
Local oPesq
Local oBusca
Local oTpBusca
Local oComboBox
Local oAcima

DEFAULT cTitulo := OemtoAnsi(STR0043) // "Pesquisa"
DEFAULT lOrdena := .F.      

Begin Sequence

	IF ( ( nPos := aScan( aClassData , { |eData| ( Upper( AllTrim( eData[1] ) ) == "CCLASSNAME" ) } ) ) > 0 )
		lNewGd := ( aClassData[ nPos , 2 ] == "MSNEWGETDADOS" )
	EndIF
	
	IF ( ( ValType( aHeaderPar ) == "A" ) .or. ( lNewGd ) )
		Private aHeader
		IF ( ValType( aHeaderPar ) == "A" )
			aHeader	:= aHeaderPar
		ElseIF ( lNewGd )
			aHeader	:= oGetDados:aHeader
		EndIF
	EndIF
	
	IF ( ( ValType( aColsPar ) == "A" ) .or. ( lNewGd ) )
		Private aCols
		IF ( ValType( aColsPar ) == "A" )
			aCols	:= aColsPar
		ElseIF ( lNewGd )
			aCols	:= oGetDados:aCols
		EndIF
	EndIF
	
	IF ( ( Type( "n" ) <> "N" ) .or. ( lNewGd ) )
		Private n
		IF ( Type( "n" ) <> "N" )
			IF ( lNewGd )
				n := oGetDados:oBrowse:nAt
			Else
				n := oGetDados:nAt
			EndIF
		ElseIF ( lNewGd )
			n := oGetDados:oBrowse:nAt
		EndIF
	EndIF	
	
	IF Empty( aHeader )
		Break
	EndIF

	nLoops := Len( aHeader )
	For nLoop := 1 TO nLoops
		AAdd( aCpoBusca , aHeader[nLoop][1] )
	Next nLoop
	
	cBusca	:= aCpoBusca[1]
	bGdChgPict := { || GdChgPict( @oDlg , nTipo , @oPesq , @cPesq , @oComboBox ) }
	DEFINE MSDIALOG oDlg TITLE OemtoAnsi(cTitulo)  FROM 00,0 TO 100,490 OF oMainWnd PIXEL
	
		@ 005,005 MSCOMBOBOX oBusca VAR cBusca ITEMS aCpoBusca SIZE 206, 36 OF oDlg PIXEL ON CHANGE ( nTipo := oBusca:nAt , Eval( bGdChgPict ) )
	
		@ 041,005 SAY OemToAnsi(STR0044) + AllTrim( Str( n ) ) SIZE 050,10 OF oDlg PIXEL // "Linha atual: " 
	
		@ 039,065 CHECKBOX oTpBusca Var lTpBusca Prompt OemtoAnsi( STR0045 )	SIZE 150,10 OF oDlg PIXEL VALID ( oAcima:SetFocus() , oAcima:Refresh() , .T. ) // "Busca a partir do item posicionado"
	
		@ 039,161 CHECKBOX oAcima Var lAcima Prompt OemtoAnsi(STR0046)		SIZE 050,10 OF oDlg PIXEL WHEN ( IF( !( lTpBusca ) ,lAcima := .F. , lAcima ) , ( lTpBusca ) .and. ( n > 1 ) ) // "Acima"
		
		@ 039,194 MSCOMBOBOX oComboBox VAR cOpcAsc ITEMS aComboBox SIZE 050,10 OF oDlg PIXEL
	
		bSet15 := { || nOpca := 1 , RestKeys( aSvKeys , .T. ) , oDlg:End() }
		oDlg:bSet15 := bSet15
		bSet24 := { || nOpca := 0 , RestKeys( aSvKeys , .T. ) , oDlg:End() }
		oDlg:bSet24 := bSet24

		bDialogInit := { || SetKey( 15 , bSet15 ) , SetKey( 24 , bSet24 ) , Eval( bGdChgPict ) }
	
		DEFINE SBUTTON oBut1 FROM 005,215 TYPE 1 ACTION Eval( bSet15 ) ENABLE OF oDlg
		DEFINE SBUTTON oBut1 FROM 020,215 TYPE 2 ACTION Eval( bSet24 ) ENABLE OF oDlg
		
	ACTIVATE MSDIALOG oDlg CENTERED ON INIT Eval( bDialogInit )
	
	IF ( nOpca == 1 )

		IF ( lTpBusca )
			IF ( lAcima )
				nBegin	:= 1
				nEnd	:= Max( n - 1 , 1 )
			Else
				nBegin	:= Min( n + 1 , Len( aCols ) )
				nEnd	:= Len( aCols )
			EndIF	
        Else
			nBegin	:= 1
			nEnd	:= Len( aCols )
		EndIF

		//-- Ordena aCols
		IF ( lOrdena )
			aSort( aCols , , , { |x,y| x[nTipo] < y[nTipo] } )
		EndIF
       
		Do Case
	
			Case aHeader[nTipo][8] $ "CM"
				IF ( cOpcAsc == aComboBox[1] )	//Exata
					cAscan := Padr( Upper( cPesq ) , TamSx3(aHeader[nTipo][2])[1] )
					bAscan := { |x| cAscan == Upper( x[ nTipo ] ) }
				ElseIF ( cOpcAsc == aComboBox[2] )	//Parcial
					cAscan := Upper( AllTrim( cPesq ) )
					bAscan := { |x| cAscan == Upper( SubStr( Alltrim( x[nTipo] ) , 1 , Len( cAscan ) ) ) }
				ElseIF ( cOpcAsc == aComboBox[3] )	//Contem
					cAscan := Upper( AllTrim( cPesq ) )
					bAscan := { |x| cAscan $ Upper( Alltrim( x[nTipo] ) ) }
				EndIF
			Case aHeader[nTipo][8] == "N"
				bAscan	:= {|x| cPesq == x[nTipo]}
			Case aHeader[nTipo][8] == "D"
				bAscan	:= {|x| Dtos(cPesq) == Dtos(x[nTipo])}
			OtherWise
				Break
		EndCase

		nPos := Ascan( aCols , bAscan , nBegin , nEnd )

		IF ( nPos > 0 )
			IF ( oGetDados:LinhaOk() )
				oGetDados:Goto( nPos )
				oGetDados:oBrowse:Refresh()
				oGetDados:oBrowse:SetFocus()
				IF ( ValType( oGetDados:oBrowse:bChange ) == "B" )
					Eval( oGetDados:oBrowse:bChange )
				EndIF
			EndIF
		Else
			Help(" ",1,"REGNOIS")
		EndIF
	
	EndIF

End Sequence
	
RestArea( aAreaSX3 )
RestArea( aArea )
RestKeys( aSvKeys , .T. )

Return( NIL )

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GdChgPict ³ Autor ³ Henry Fila  1a. Ver   ³ Data ³17/07/2002 ³±±
±±³          ³          ³ Autor ³ Marinaldo   2a. Ver   ³ Data ³09/01/2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Atualiza picture na funcao GdSeek                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³oDlg  	-> Dialog                                             ³±±
±±³          ³nTipo 	-> Indice em Header                                   ³±±
±±³          ³oPesq 	-> Objeto da Pesquisa                                 ³±±
±±³          ³cPesq		-> Variavel da Pesquisa                            ³±±
±±³          ³oComboBox -> Objeto Combo                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³lRet                                                         ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo tratar a picture do campo sele ³±±
±±³          ³cionado na funcao GdSeek                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³GdSeek                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function GdChgPict(oDlg,nTipo,oPesq,cPesq,oComboBox)

Local aCombo
Local aX3cBox
Local cF3
Local cBox
Local cPicture
Local cAlias
Local cSxbRet
Local lGdChgPict
Local nLoop
Local nLoops
Local nAt

Begin Sequence

	IF ( ValType( oPesq ) == "O" )
		oPesq:Disable()
		oPesq:Hide()
		oPesq:End()
		oPesq := NIL
		oDlg:Refresh()
	EndIF

	oComboBox:Disable()
	oComboBox:Hide()

	IF !( lGdChgPict := ( nTipo > 0 ) )
		Break
	EndIF

	IF ( aHeader[nTipo][8] <> "M" )
		cPesq	:= Space( aHeader[nTipo,4] ) 
	Else
		cPesq	:= Space( 80 )
	EndIF
	cPicture := aHeader[nTipo][3]

	@ 022,005 MSGET oPesq VAR cPesq SIZE 206, 10 PICTURE cPicture OF oDlg PIXEL

	oDlg:Refresh()          
	
	oComboBox:Show()
	oComboBox:Enable()
	

End Sequence                
	
Return( lGdChgPict )             


