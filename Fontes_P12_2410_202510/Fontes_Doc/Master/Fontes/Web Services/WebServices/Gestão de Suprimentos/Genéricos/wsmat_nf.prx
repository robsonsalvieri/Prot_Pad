#INCLUDE "wsmat_nf.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
/*/


Ŀ
Funo    WSMAT_NF   Autor Eduardo Riera           Data 22.08.2003  
Ĵ
Descrio  Web Service responsavel pelos documentos de entrada/saida    
                                                                        
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        CRM/Materiais/Portais                                        
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service                                                
//
WSSERVICE MtCustomerInvoice     DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/mtcustomerinvoice.apw" //"Servio de consulta dos documentos de entrada e sada ( <b>Restrio de cliente<b> )"
WSDATA HeaderType               AS String
WSDATA Header                   AS Array Of BrwHeader
WSDATA UserCode                 AS String
WSDATA RegisterDateFrom         As Date OPTIONAL
WSDATA RegisterDateTo           As Date OPTIONAL
WSDATA DeliveryDateFrom         As Date OPTIONAL
WSDATA DeliveryDateTo           As Date OPTIONAL
WSDATA PurchaseNumber           As String OPTIONAL
WSDATA CustomerOrSupplier       As Integer
WSDATA CustomerOrSupplierID     As String
WSDATA CustomerOrSupplierType   As String
WSDATA SerialNumber             As String
WSDATA InvoiceNumber            As String
WSDATA InvoiceType              As String
WSDATA DocumentType             As String OPTIONAL
WSDATA InvoiceHeader            As Array Of InvoiceHeaderView
WSDATA Invoice                  As InvoiceView
WSDATA WsNull                   As String
WSDATA QueryAddWhereNfe         As String OPTIONAL
WSDATA QueryAddWhereNfs         As String OPTIONAL
WSDATA IndexKeyNfe              As String OPTIONAL
WSDATA IndexKeyNfs              As String OPTIONAL
WSDATA OperationType            As String OPTIONAL

WSMETHOD GetHeader           DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
WSMETHOD BrwInvoice          DESCRIPTION STR0003 //"Mtodo de listagem dos documentos de entrada ou saida"
WSMETHOD GetInvoice          DESCRIPTION STR0004 //"Mtodo de consulta as informaes do documento de entrada ou saida"
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de recuperacao do header                              
                                                                       
Ĵ
ParametrosExpC1: Nome da Estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtCustomerInvoice

::Header := MtHeader(::HeaderType)
If Empty(::Header)
	::Header := FinHeader(::HeaderType)
EndIf

Return(.T.)

/*/

Ŀ
Funo    BrwPurchasAutor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de recuperacao dos documentos de entrada e saida      
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Fornecedor                                            
          ExpD3: Data Inicial                                          
          ExpD4: Data Final                                            
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve as cotacaoes em aberto do fornecedor     
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD BrwInvoice WSRECEIVE UserCode,CustomerOrSupplier,CustomerOrSupplierID,RegisterDateFrom,RegisterDateTo,DeliveryDateFrom,DeliveryDateTo,QueryAddWhereNfe,QueryAddWhereNfs,IndexKeyNfe,IndexKeyNfs,PurchaseNumber,OperationType WSSEND InvoiceHeader WSSERVICE MtCustomerInvoice

Local aArea    := GetArea()

Local lRetorno := .T.
Local lQuery   := .F.
Local lValido  := .F.
Local nX       := 0
Local cArquivo := ""
Local cChave   := ""
Local cQuery   := ""
Local cAliasSF1:= "SF1"
Local cAliasSF2:= "SF2"
Local cFornece := SubStr(::CustomerOrSupplierID,1,Len(SA2->A2_COD))
Local cLojaFor := SubStr(::CustomerOrSupplierID,Len(SA2->A2_COD)+1)
Local cCliente := SubStr(::CustomerOrSupplierID,1,Len(SA1->A1_COD))
Local cLojaCli := SubStr(::CustomerOrSupplierID,Len(SA1->A1_COD)+1)
Local dEntrini := ::DeliveryDateFrom
Local dEntrFim := ::DeliveryDateTo
Local dEmisini := ::RegisterDateFrom
Local dEmisFim := ::RegisterDateTo
Local cAlias   := IIf(::CustomerOrSupplier==1,"SA1","SA2")
Local aCampos  := {}
Local aStruSF1 := {}
Local aStruSF2 := {}
Local cVolume  := ""
Local nY       := 0

DEFAULT dEntrIni := dDataBase-30
DEFAULT dEntrFim := dDataBase
DEFAULT dEmisIni := dDataBase-30
DEFAULT dEmisFim := dDataBase

If PrtChkUser(::UserCode,"MtCustomerInvoice","BrwInvoice",cAlias,::CustomerOrSupplierID)
	//Ŀ
	//Pesquisa os documentos de entrada                                       
	//
	If Empty(::OperationType) .Or. ::OperationType=="E"
		dbSelectArea("SF1")
		dbSetOrder(2)

		lQuery := .T.
		cAliasSF1 := "BRWINVOICE"
		aStruSF1  := SF1->(dbStruct())

		cQuery := "SELECT F1_FILIAL,F1_TIPO,F1_FORNECE,F1_LOJA,F1_DOC,F1_SERIE,F1_FORMUL, "
		cQuery += "F1_DTDIGIT,F1_EMISSAO,F1_VALBRUT,F1_DESPESA,F1_SEGURO,F1_FRETE,F1_STATUS, "
		aCampos := MaFisRefLd("SF1","NF")
		For nY := 1 To Len(aCampos)
			If !Empty(aCampos[nY][2])
				cQuery += aCampos[nY][2]+","
			EndIf
			If !Empty(aCampos[nY][3])
				cQuery += aCampos[nY][3]+","
			EndIf
			If !Empty(aCampos[nY][4])
				cQuery += aCampos[nY][4]+","
			EndIf
		Next nY
		cQuery += "F1_PREFIXO,F1_DUPL "
		cQuery += GetUserField("SF1")
		cQuery += "FROM "+RetSqlName("SF1")+" SF1 "
		If !Empty(::PurchaseNumber)
			cQuery += ","+RetSqlName("SD1")+" SD1 "
		EndIf
		cQuery += "WHERE SF1.F1_FILIAL='"+xFilial("SF1")+"' AND "
		If ::CustomerOrSupplier==1
			cQuery += "SF1.F1_FORNECE='"+cCliente+"' AND "
			cQuery += "SF1.F1_LOJA='"+cLojaCli+"' AND "
			cQuery += "SF1.F1_TIPO IN('D','B') AND "
		Else
			cQuery += "SF1.F1_FORNECE='"+cFornece+"' AND "
			cQuery += "SF1.F1_LOJA='"+cLojaFor+"' AND "
			cQuery += "SF1.F1_TIPO NOT IN('D','B') AND "
		EndIf
		cQuery += "SF1.F1_EMISSAO >= '"+Dtos(dEmisIni)+"' AND "
		cQuery += "SF1.F1_EMISSAO <= '"+Dtos(dEmisFim)+"' AND "
		cQuery += "SF1.F1_DTDIGIT >= '"+Dtos(dEntrIni)+"' AND "
		cQuery += "SF1.F1_DTDIGIT <= '"+Dtos(dEntrFim)+"' AND "
		cQuery += "NOT ("+IsRemito(2,"SF1.F1_TIPODOC")+") AND "
		cQuery += "SF1.D_E_L_E_T_=' ' "
		If !Empty(::PurchaseNumber)
			cQuery += " AND "
			cQuery += "SD1.D1_FILIAL = '"+xFilial("SD1")+"' AND "
			cQuery += "SD1.D1_DOC = SF1.F1_DOC AND "
			cQuery += "SD1.D1_SERIE = SF1.F1_SERIE AND "
			cQuery += "SD1.D1_FORNECE = SF1.F1_FORNECE AND "
			cQuery += "SD1.D1_LOJA = SF1.F1_LOJA AND "
			cQuery += "SD1.D1_TIPO = SF1.F1_TIPO AND "
			cQuery += "SD1.D1_PEDIDO = '"+::PurchaseNumber+"' AND "
			cQuery += "SD1.D_E_L_E_T_=' ' "
		EndIf
		cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfe)
		cQuery += "ORDER BY "+WsSqlOrder(IIf(Empty(::IndexKeyNfe),SF1->(IndexKey()),::IndexKeyNfe))

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF1)

		For nY := 1 To Len(aStruSF1)
			If aStruSF1[nY][2] <> "C" .And. aStruSF1[nY][2] <> "M"
				TcSetField(cAliasSF1,aStruSF1[nY][1],aStruSF1[nY][2],aStruSF1[nY][3],aStruSF1[nY][4])
			EndIf
		Next nY

		nX := 0
		DEFAULT ::InvoiceHeader := {}
		While !Eof() .And. xFilial("SF1") == (cAliasSF1)->F1_FILIAL .And.;
				cFornece+cLojaFor == (cAliasSF1)->F1_FORNECE+(cAliasSF1)->F1_LOJA

			If IIf(::CustomerOrSupplier==1,(cAliasSF1)->F1_TIPO $ 'DB',!(cAliasSF1)->F1_TIPO $ 'DB') .And.;
					(cAliasSF1)->F1_EMISSAO >= dEmisIni .And.;
					(cAliasSF1)->F1_EMISSAO <= dEmisFim .And.;
					(cAliasSF1)->F1_DTDIGIT >= dEntrIni .And.;
					(cAliasSF1)->F1_DTDIGIT <= dEntrFim	

				If lQuery
					lValido := .T.
				Else
					If Empty(::PurchaseNumber)
						lValido := .T.
					Else
						lValido := .F.
						dbSelectArea("SD1")
						dbSetOrder(1)
						MsSeek(xFilial("SD1")+(cAliasSF1)->F1_DOC+(cAliasSF1)->F1_SERIE+(cAliasSF1)->F1_FORNECE+(cAliasSF1)->F1_LOJA)
						While !Eof() .And.;
							xFilial("SD1") == SD1->D1_FILIAL .And.;
							(cAliasSF1)->F1_DOC == SD1->D1_DOC .And.;
							(cAliasSF1)->F1_SERIE == SD1->D1_SERIE .And.;
							(cAliasSF1)->F1_FORNECE == SD1->D1_FORNECE .And.;
							(cAliasSF1)->F1_LOJA == SD1->D1_LOJA

							If SD1->D1_TIPO == (cAliasSF1)->F1_TIPO .And.;
								SD1->D1_FORMUL == (cAliasSF1)->F1_FORMUL .And.;
								SD1->D1_PEDIDO == ::PurchaseNumber
								
								lValido := .T.
								Exit
							EndIf

							dbSelectArea("SD1")
							dbSkip()

						EndDo
					EndIf
				EndIf
				If lValido
					aadd(::InvoiceHeader,WsClassNew("InvoiceHeaderView"))
					nX++
					GetNFEHead(@::InvoiceHeader[nX],cAliasSF1)
				EndIf
			EndIf
			dbSelectArea(cAliasSF1)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSF1)
			dbCloseArea()
			dbSelectArea("SF1")
		Else
			dbSelectArea("SF1")
			RetIndex("SF1")
			FErase(cArquivo+OrdBagExt())
		EndIf
	EndIf
	//Ŀ
	//Pesquisa os documentos de saida                                         
	//
	If Empty(::OperationType) .Or. ::OperationType=="S"
		dbSelectArea("SF2")
		dbSetOrder(2)

		lQuery := .T.
		cAliasSF2 := "BRWINVOICE"
		aStruSF2  := SF2->(dbStruct())

		cQuery := "SELECT F2_FILIAL,F2_TIPO,F2_CLIENTE,F2_LOJA,F2_DOC,F2_SERIE, "
		cQuery += "F2_EMISSAO,F2_VALBRUT,F2_DESPESA,F2_SEGURO,F2_FRETE,F2_TRANSP, "
		aCampos := MaFisRefLd("SF2","NF")
		For nY := 1 To Len(aCampos)
			If !Empty(aCampos[nY][2])
				cQuery += aCampos[nY][2]+","
			EndIf
			If !Empty(aCampos[nY][3])
				cQuery += aCampos[nY][3]+","
			EndIf
			If !Empty(aCampos[nY][4])
				cQuery += aCampos[nY][4]+","
			EndIf
		Next nY
		cVolume := "1"
		While SF2->(FieldPos("F2_ESPECI"+cVolume))<>0 .And. !Empty(SF2->(FieldGet(FieldPos("F2_ESPECI"+cVolume))))
			cQuery += "F2_ESPECI"+cVolume+","+"F2_ESPECI"+cVolume+","
			cVolume := Soma1( cVolume )
		EndDo
		cQuery += "F2_PREFIXO,F2_DUPL "
		cQuery += GetUserField("SF2")
		cQuery += "FROM "+RetSqlName("SF2")+" SF2 "
		If !Empty(::PurchaseNumber)
			cQuery += ","+RetSqlName("SD2")+" SD2 "
		EndIf
		cQuery += "WHERE SF2.F2_FILIAL='"+xFilial("SF2")+"' AND "
		If ::CustomerOrSupplier==1
			cQuery += "SF2.F2_CLIENTE='"+cCliente+"' AND "
			cQuery += "SF2.F2_LOJA='"+cLojaCli+"' AND "
			cQuery += "SF2.F2_TIPO NOT IN('D','B') AND "
		Else
			cQuery += "SF2.F2_CLIENTE='"+cFornece+"' AND "
			cQuery += "SF2.F2_LOJA='"+cLojaFor+"' AND "
			cQuery += "SF2.F2_TIPO IN('D','B') AND "
		EndIf
		cQuery += "SF2.F2_EMISSAO >= '"+Dtos(dEmisIni)+"' AND "
		cQuery += "SF2.F2_EMISSAO <= '"+Dtos(dEmisFim)+"' AND "
		cQuery += "SF2.F2_EMISSAO >= '"+Dtos(dEntrIni)+"' AND "
		cQuery += "SF2.F2_EMISSAO <= '"+Dtos(dEntrFim)+"' AND "
		cQuery += "NOT ("+IsRemito(2,"SF2.F2_TIPODOC")+") AND "
		cQuery += "SF2.D_E_L_E_T_=' ' "
		If !Empty(::PurchaseNumber)
			cQuery += " AND "
			cQuery += "SD2.D2_FILIAL = '"+xFilial("SD2")+"' AND "
			cQuery += "SD2.D2_DOC = SF2.F2_DOC AND "
			cQuery += "SD2.D2_SERIE = SF2.F2_SERIE AND "
			cQuery += "SD2.D2_CLIENTE = SF2.F2_CLIENTE AND "
			cQuery += "SD2.D2_LOJA = SF2.F2_LOJA AND "
			cQuery += "SD2.D2_PEDIDO = '"+::PurchaseNumber+"' AND "
			cQuery += "SD2.D_E_L_E_T_=' ' "
		EndIf
		cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfs)
		cQuery += "ORDER BY "+WsSqlOrder(IIf(Empty(::IndexKeyNfs),SF2->(IndexKey()),::IndexKeyNfs))

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF2)

		For nY := 1 To Len(aStruSF2)
			If aStruSF2[nY][2] <> "C" .And. aStruSF2[nY][2] <> "M"
				TcSetField(cAliasSF2,aStruSF2[nY][1],aStruSF2[nY][2],aStruSF2[nY][3],aStruSF2[nY][4])
			EndIf
		Next nY

		DEFAULT ::InvoiceHeader := {}	

		While !Eof() .And. xFilial("SF2") == (cAliasSF2)->F2_FILIAL .And.;
				cCliente+cLojaCli == (cAliasSF2)->F2_CLIENTE+(cAliasSF2)->F2_LOJA

			If IIf(::CustomerOrSupplier==1,!(cAliasSF2)->F2_TIPO $ 'DB',(cAliasSF2)->F2_TIPO $ 'DB') .And.;
					(cAliasSF2)->F2_EMISSAO >= dEmisIni .And.;
					(cAliasSF2)->F2_EMISSAO <= dEmisFim .And.;
					(cAliasSF2)->F2_EMISSAO >= dEntrIni .And.;
					(cAliasSF2)->F2_EMISSAO <= dEntrFim
				If lQuery
					If cChave <> (cAliasSF2)->F2_DOC+(cAliasSF2)->F2_SERIE+(cAliasSF2)->F2_CLIENTE+(cAliasSF2)->F2_LOJA
						cChave := (cAliasSF2)->F2_DOC+(cAliasSF2)->F2_SERIE+(cAliasSF2)->F2_CLIENTE+(cAliasSF2)->F2_LOJA
					lValido := .T.
				Else
						lValido := .F.
					EndIf
				Else
					If Empty(::PurchaseNumber)
						lValido := .T.
					Else
						dbSelectArea("SD2")
						dbSetOrder(1)
						MsSeek(xFilial("SD2")+(cAliasSF2)->F2_DOC+(cAliasSF2)->F2_SERIE+(cAliasSF2)->F2_CLIENTE+(cAliasSF2)->F2_LOJA)
						While !Eof() .And.;
							xFilial("SD2") == (cAliasSD2)->D2_FILIAL .And.;
							(cAliasSF2)->F2_DOC == (cAliasSD2)->D2_DOC .And.;
							(cAliasSF2)->F2_SERIE == (cAliasSD2)->D2_SERIE .And.;
							(cAliasSF2)->F2_CLIENTE == (cAliasSD2)->D2_CLIENTE .And.;
							(cAliasSF2)->F2_LOJA == (cAliasSD2)->D2_LOJA

							If (cAliasSD2)->D2_PEDIDO == ::PurchaseNumber
								lValido := .T.
								Exit
							EndIf
							dbSelectArea(cAliasSD2)
							dbSkip()
						EndDo
					EndIf
				EndIf
				If lValido
					aadd(::InvoiceHeader,WsClassNew("InvoiceHeaderView"))
					nX++
					GetNFSHead(@::InvoiceHeader[nX],cAliasSF2)
				EndIf
			EndIf
			dbSelectArea(cAliasSF2)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSF2)
			dbCloseArea()
			dbSelectArea("SF2")
		Else
			dbSelectArea("SF2")
			RetIndex("SF2")
			FErase(cArquivo+OrdBagExt())
		EndIf
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

//Funo para ajustar a Serie do Documento caso nao exista
Function wsmat_nfSerie(cSerie)
cserie := SubStr(cSerie,1,Len(SD1->D1_SERIE))
if cSerie == "" 
   cSerie := Space(Len(SD1->D1_SERIE))
EndIf   
Return(cSerie)

/*/

Ŀ
Funo    GetInvoiceAutor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de recuperacao dos documento de entrada / saida       
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Tipo do documento de entrada/saida                    
          ExpC3: Numero de Serie                                       
          ExpC4: Numero do documento                                   
          ExpC5: Formulario Proprio?                                   
          ExpN6: 1-Cliente ou 2-Fornecedor                             
          ExpC6: Codigo do cliente ou fornecedor                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve os documentos de entrad ou saida         
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetInvoice WSRECEIVE UserCode,InvoiceType,SerialNumber,InvoiceNumber,DocumentType,CustomerOrSupplier,CustomerOrSupplierID,QueryAddWhereNfe,QueryAddWhereNfs WSSEND Invoice WSSERVICE MtCustomerInvoice

Local aArea    := GetArea()
Local cAlias   := IIf(::CustomerOrSupplier==1,"SA1","SA2")
Local cES      := SubStr(::InvoiceType,1,1)
Local cFornece := SubStr(::CustomerOrSupplierID,1,Len(SA2->A2_COD))
Local cLojaFor := SubStr(::CustomerOrSupplierID,Len(SA2->A2_COD)+1)
Local cCliente := SubStr(::CustomerOrSupplierID,1,Len(SA1->A1_COD))
Local cLojaCli := SubStr(::CustomerOrSupplierID,Len(SA1->A1_COD)+1)
Local cSerie   := wsmat_nfSerie(::SerialNumber)
Local cDoc     := SubStr(::InvoiceNumber,1,Len(SD1->D1_DOC))
Local cTipo    := AllTrim(SubStr(::InvoiceType,3))
Local cFormul  := ::DocumentType
Local cAliasSD1:= "SD1"
Local cAliasSD2:= "SD2"
Local nX       := 0
Local lQuery   := .F.
Local lRetorno := .F.
Local aStruSD1 := {}
Local aStruSD2 := {}
Local cQuery   := ""
Local nY       := 0

DEFAULT cFormul := " "
If cPaisLoc=="BRA"
	cFormul  := IIf(cFormul=="S","S"," ")
Else                                            
	cFormul  := IIf(cFormul=="S","S","N")
EndIf	       

If PrtChkUser(::UserCode,"MtCustomerInvoice","GetInvoice",IIf(cTipo$"BD",Nil,cAlias),IIf(cTipo$"BD",Nil,::CustomerOrSupplierID))
	//Ŀ
	//Verifica se qual o tipo de documento                                    
	//
	If cES == "E"
		dbSelectArea("SF1")
		dbSetOrder(1)
		MsSeek(xFilial("SF1")+cDoc+cSerie+cFornece+cLojaFor+cTipo)
		While !Eof() .And. xFilial("SF1") == SF1->F1_FILIAL .And.;
			cDoc == SF1->F1_DOC .And.;
			cSerie == SF1->F1_SERIE .And.;
			cFornece == SF1->F1_FORNECE .And.;
			cLojaFor == SF1->F1_LOJA .And.;
			cTipo == SF1->F1_TIPO 
			If SF1->F1_FORMUL == cFormul
				lRetorno := .T.
				Exit
			EndIf
			dbSelectArea("SF1")
			dbSkip()
		EndDo
		If lRetorno
			::Invoice:InvoiceHeader := WsClassNew("InvoiceHeaderView")
			GetNfeHead(@::Invoice:InvoiceHeader,"SF1")
			//Ŀ
			//Pesquisa os Itens do documento de entrada                               
			//
			::Invoice:InvoiceItem:= {}
			dbSelectArea("SD1")
			dbSetOrder(1)

			aStruSD1 := SD1->(dbStruct())
			lQuery   := .T.
			cAliasSD1:= "GETINVOICE"

			cQuery := "SELECT * "
			cQuery += "FROM "+RetSqlName("SD1")+" SD1 "
			cQuery += "WHERE SD1.D1_FILIAL='"+xFilial("SD1")+"' AND "
			cQuery += "SD1.D1_DOC='"+cDoc+"' AND "
			cQuery += "SD1.D1_SERIE='"+cSerie+"' AND "
			cQuery += "SD1.D1_FORNECE='"+cFornece+"' AND "
			cQuery += "SD1.D1_LOJA='"+cLojaFor+"' AND "	
			cQuery += "SD1.D1_TIPO='"+cTipo+"' AND "
			cQuery += "SD1.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfe)
			cQuery += "ORDER BY "+SqlOrder(SD1->(IndexKey()))

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1)

			For nY := 1 To Len(aStruSD1)
				If aStruSD1[nY][2] <> "C" .And. aStruSD1[nY][2] <> "M"
					TcSetField(cAliasSD1,aStruSD1[nY][1],aStruSD1[nY][2],aStruSD1[nY][3],aStruSD1[nY][4])
				EndIf
			Next nY

			nX := 0
			While !Eof() .And.;
				xFilial("SD1") == (cAliasSD1)->D1_FILIAL .And.;
				cDoc == (cAliasSD1)->D1_DOC .And.;
				cSerie == (cAliasSD1)->D1_SERIE .And.;
				cFornece == (cAliasSD1)->D1_FORNECE .And.;
				cLojaFor == (cAliasSD1)->D1_LOJA

				If (cAliasSD1)->D1_TIPO == cTipo .And. (cAliasSD1)->D1_FORMUL == cFormul
					If nX == 0
						::Invoice:InvoiceItem:= {}
					EndIf
					aadd(::Invoice:InvoiceItem,WsClassNew("InvoiceItemView"))
					nX++
					GetNFEItem(@::Invoice:InvoiceItem[nX],cAliasSD1)
				EndIf
				dbSelectArea(cAliasSD1)
				dbSkip()
			EndDo
			If lQuery
				dbSelectArea(cAliasSD1)
				dbCloseArea()
				dbSelectArea("SD1")
			EndIf
		EndIf
	Else
		dbSelectArea("SF2")
		dbSetOrder(1)
		If MsSeek(xFilial("SF2")+cDoc+cSerie+cCliente+cLojaCli)
			lRetorno := .T.
			::Invoice:InvoiceHeader := WsClassNew("InvoiceHeaderView")
			GetNfSHead(@::Invoice:InvoiceHeader,"SF2")
			//Ŀ
			//Pesquisa os Itens do documento de saida                                 
			//
			::Invoice:InvoiceItem:= {}
			dbSelectArea("SD2")
			dbSetOrder(3)

			aStruSD2 := SD2->(dbStruct())
			lQuery   := .T.
			cAliasSD2:= "GETINVOICE"

			cQuery := "SELECT * "
			cQuery += "FROM "+RetSqlName("SD2")+" SD2 "
			cQuery += "WHERE SD2.D2_FILIAL='"+xFilial("SD2")+"' AND "
			cQuery += "SD2.D2_DOC='"+cDoc+"' AND "
			cQuery += "SD2.D2_SERIE='"+cSerie+"' AND "
			cQuery += "SD2.D2_CLIENTE='"+cCliente+"' AND "
			cQuery += "SD2.D2_LOJA='"+cLojaCli+"' AND "	
			cQuery += "SD2.D2_TIPO='"+cTipo+"' AND "
			cQuery += "SD2.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfs)
			cQuery += "ORDER BY "+SqlOrder(SD2->(IndexKey()))

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD2)

			For nY := 1 To Len(aStruSD2)
				If aStruSD2[nY][2] <> "C" .And. aStruSD2[nY][2] <> "M"
					TcSetField(cAliasSD2,aStruSD2[nY][1],aStruSD2[nY][2],aStruSD2[nY][3],aStruSD2[nY][4])
				EndIf
			Next nY

			nX := 0
			While !Eof() .And.;
				xFilial("SD2") == (cAliasSD2)->D2_FILIAL .And.;
				cDoc == (cAliasSD2)->D2_DOC .And.;
				cSerie == (cAliasSD2)->D2_SERIE .And.;
				cFornece == (cAliasSD2)->D2_CLIENTE .And.;
				cLojaFor == (cAliasSD2)->D2_LOJA

				If (cAliasSD2)->D2_TIPO == cTipo 
					If nX == 0
						::Invoice:InvoiceItem:= {}
					EndIf
					aadd(::Invoice:InvoiceItem,WsClassNew("InvoiceItemView"))
					nX++
					GetNFSItem(@::Invoice:InvoiceItem[nX],cAliasSD2)
				EndIf
				dbSelectArea(cAliasSD2)
				dbSkip()
			EndDo
			If lQuery
				dbSelectArea(cAliasSD2)
				dbCloseArea()
				dbSelectArea("SD2")
			EndIf
		EndIf
	EndIf
	If !lRetorno
		SetSoapFault("GETINVOICE",STR0005) //"Documento nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/


Ŀ
Funo    WSMAT_NF   Autor Eduardo Riera           Data 22.08.2003  
Ĵ
Descrio  Web Service responsavel pelos documentos de entrada/saida    
                                                                        
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        CRM/Materiais/Portais                                        
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service                                                
//
WSSERVICE MtSellerInvoice       DESCRIPTION STR0006 NAMESPACE "http://webservices.microsiga.com.br/mtsellerinvoice.apw" //"Servio de consulta dos documentos de entrada e sada ( <b>Restrio de representante comercial<b> )"
WSDATA HeaderType               AS String
WSDATA Header                   AS Array Of BrwHeader
WSDATA UserCode                 AS String
WSDATA RegisterDateFrom         As Date OPTIONAL
WSDATA RegisterDateTo           As Date OPTIONAL
WSDATA DeliveryDateFrom         As Date OPTIONAL
WSDATA DeliveryDateTo           As Date OPTIONAL
WSDATA PurchaseNumber           As String OPTIONAL
WSDATA CustomerID               As String
WSDATA SellerCode               As String
WSDATA CustomerType             As String
WSDATA SerialNumber             As String
WSDATA InvoiceNumber            As String
WSDATA InvoiceType              As String
WSDATA DocumentType             As String OPTIONAL
WSDATA InvoiceHeader            As Array Of InvoiceHeaderView
WSDATA Invoice                  As InvoiceView
WSDATA WsNull                   As String
WSDATA QueryAddWhereNfe         As String OPTIONAL
WSDATA QueryAddWhereNfs         As String OPTIONAL
WSDATA IndexKeyNfe              As String OPTIONAL
WSDATA IndexKeyNfs              As String OPTIONAL
WSDATA OperationType            As String OPTIONAL

WSMETHOD GetHeader           DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
WSMETHOD BrwInvoice          DESCRIPTION STR0003 //"Mtodo de listagem dos documentos de entrada ou saida"
WSMETHOD GetInvoice          DESCRIPTION STR0004 //"Mtodo de consulta as informaes do documento de entrada ou saida"

ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de recuperacao do header                              
                                                                       
Ĵ
ParametrosExpC1: Nome da Estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtSellerInvoice

::Header := MtHeader(::HeaderType)
If Empty(::Header)
	::Header := FinHeader(::HeaderType)
EndIf

Return(.T.)

/*/

Ŀ
Funo    BrwPurchasAutor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de recuperacao dos documentos de entrada e saida      
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Vendedor                                              
          ExpC2: Cliente                                               
          ExpD3: Emissao de                                            
          ExpD4: Emissao Ate                                           
          ExpD3: Entrega de                                            
          ExpD4: Entrega Ate                                           
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve as cotacaoes em aberto do fornecedor     
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD BrwInvoice WSRECEIVE UserCode,SellerCode,CustomerID,RegisterDateFrom,RegisterDateTo,DeliveryDateFrom,DeliveryDateTo,QueryAddWhereNfe,QueryAddWhereNfs,IndexKeyNfe,IndexKeyNfs,PurchaseNumber,OperationType WSSEND InvoiceHeader WSSERVICE MtSellerInvoice

Local aArea    := GetArea()
Local lRetorno := .T.
Local lQuery   := .F.
Local lValido  := .F.
Local nX       := 0
Local cArquivo := ""
Local cQuery   := ""
Local cAliasSF1:= "SF1"
Local cAliasSF2:= "SF2"
Local cCliente := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLojaCli := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local dEntrini := ::DeliveryDateFrom
Local dEntrFim := ::DeliveryDateTo
Local dEmisini := ::RegisterDateFrom
Local dEmisFim := ::RegisterDateTo
Local aCampos  := {}
Local aStruSF1 := {}
Local aStruSF2 := {}
Local cVolume  := ""
Local nY       := 0

DEFAULT dEntrIni := dDataBase-30
DEFAULT dEntrFim := dDataBase
DEFAULT dEmisIni := dDataBase-30
DEFAULT dEmisFim := dDataBase

If PrtChkUser(::UserCode,"MtSellerInvoice","BrwInvoice","SA3",::SellerCode)
	//Ŀ
	//Pesquisa os documentos de entrada                                       
	//
	If Empty(::OperationType) .Or. ::OperationType=="E"
		dbSelectArea("SF1")
		dbSetOrder(2)
		lQuery := .T.
		cAliasSF1 := "BRWINVOICE"
		aStruSF1  := SF1->(dbStruct())

		cQuery := "SELECT F1_FILIAL,F1_TIPO,F1_FORNECE,F1_LOJA,F1_DOC,F1_SERIE,F1_FORMUL, "
		cQuery += "F1_DTDIGIT,F1_EMISSAO,F1_VALBRUT,F1_DESPESA,F1_SEGURO,F1_FRETE,F1_STATUS, "
		aCampos := MaFisRefLd("SF1","NF")
		For nY := 1 To Len(aCampos)
			If !Empty(aCampos[nY][2])
				cQuery += aCampos[nY][2]+","
			EndIf
			If !Empty(aCampos[nY][3])
				cQuery += aCampos[nY][3]+","
			EndIf
			If !Empty(aCampos[nY][4])
				cQuery += aCampos[nY][4]+","
			EndIf
		Next nY
		cQuery += "F1_PREFIXO,F1_DUPL "
		cQuery += GetUserField("SF1")
		cQuery += "FROM "+RetSqlName("SF1")+" SF1 "
		If Empty(cCliente)
			cQuery += ","+RetSqlName("SA1")+" SA1 "
		EndIf
		If !Empty(::PurchaseNumber)
			cQuery += ","+RetSqlName("SD1")+" SD1 "
		EndIf
		cQuery += "WHERE SF1.F1_FILIAL='"+xFilial("SF1")+"' AND "
		cQuery += "SF1.F1_TIPO IN('D','B') AND "
		cQuery += "SF1.F1_EMISSAO >= '"+Dtos(dEmisIni)+"' AND "
		cQuery += "SF1.F1_EMISSAO <= '"+Dtos(dEmisFim)+"' AND "
		cQuery += "SF1.F1_DTDIGIT >= '"+Dtos(dEntrIni)+"' AND "
		cQuery += "SF1.F1_DTDIGIT <= '"+Dtos(dEntrFim)+"' AND "	
		cQuery += "NOT ("+IsRemito(2,"SF1.F1_TIPODOC")+") AND "
		If !Empty(cCliente)
			cQuery += "SF1.F1_FORNECE='"+cCliente+"' AND "
			cQuery += "SF1.F1_LOJA='"+cLojaCli+"' AND "
		Else
			cQuery += "SA1.A1_FILIAL='"+xFilial("SA1")+"' AND "
			cQuery += "SA1.A1_COD=SF1.F1_FORNECE AND "
			cQuery += "SA1.A1_LOJA=SF1.F1_LOJA AND "
			cQuery += "SA1.A1_VEND='"+::SellerCode+"' AND "
			cQuery += "SA1.D_E_L_E_T_=' ' AND "
		EndIf
		cQuery += "SF1.D_E_L_E_T_=' ' "
		If !Empty(::PurchaseNumber)
			cQuery += " AND "
			cQuery += "SD1.D1_FILIAL = '"+xFilial("SD1")+"' AND "
			cQuery += "SD1.D1_DOC = SF1.F1_DOC AND "
			cQuery += "SD1.D1_SERIE = SF1.F1_SERIE AND "
			cQuery += "SD1.D1_FORNECE = SF1.F1_FORNECE AND "
			cQuery += "SD1.D1_LOJA = SF1.F1_LOJA AND "
			cQuery += "SD1.D1_TIPO = SF1.F1_TIPO AND "
			cQuery += "SD1.D1_PEDIDO = '"+::PurchaseNumber+"' AND "
			cQuery += "SD1.D_E_L_E_T_=' ' "
		EndIf
		cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfe)
		cQuery += "ORDER BY "+WsSqlOrder(IIf(Empty(::IndexKeyNfe),SF1->(IndexKey()),::IndexKeyNfe))

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF1)

		For nY := 1 To Len(aStruSF1)
			If aStruSF1[nY][2] <> "C" .And. aStruSF1[nY][2] <> "M"
				TcSetField(cAliasSF1,aStruSF1[nY][1],aStruSF1[nY][2],aStruSF1[nY][3],aStruSF1[nY][4])
			EndIf
		Next nY

		nX := 0
		DEFAULT ::InvoiceHeader := {}
		While !Eof() .And. xFilial("SF1") == (cAliasSF1)->F1_FILIAL

			If (cAliasSF1)->F1_TIPO $ 'DB' .And.;
				(cAliasSF1)->F1_EMISSAO >= dEmisIni .And.;
				(cAliasSF1)->F1_EMISSAO <= dEmisFim .And.;
				(cAliasSF1)->F1_DTDIGIT >= dEntrIni .And.;
				(cAliasSF1)->F1_DTDIGIT <= dEntrFim

				If !lQuery
					dbSelectArea("SA1")
					dbSetOrder(1)
					MsSeek(xFilial("SA1")+cCliente+cLojaCli)
				EndIf
				If lQuery
					lValido := .T.
				Else
					If Empty(::PurchaseNumber)
						lValido := .T.
					Else
						lValido := .F.
						dbSelectArea("SD1")
						dbSetOrder(1)
						MsSeek(xFilial("SD1")+(cAliasSF1)->F1_DOC+(cAliasSF1)->F1_SERIE+(cAliasSF1)->F1_FORNECE+(cAliasSF1)->F1_LOJA)
						While !Eof() .And.;
							xFilial("SD1") == SD1->D1_FILIAL .And.;
							(cAliasSF1)->F1_DOC == SD1->D1_DOC .And.;
							(cAliasSF1)->F1_SERIE == SD1->D1_SERIE .And.;
							(cAliasSF1)->F1_FORNECE == SD1->D1_FORNECE .And.;
							(cAliasSF1)->F1_LOJA == SD1->D1_LOJA

							If SD1->D1_TIPO == (cAliasSF1)->F1_TIPO .And.;
								SD1->D1_FORMUL == (cAliasSF1)->F1_FORMUL .And.;
								SD1->D1_PEDIDO == ::PurchaseNumber

								lValido := .T.
					 			Exit
							EndIf

							dbSelectArea("SD1")
							dbSkip()

						EndDo
					EndIf
				EndIf
				If lValido

					If lQuery .Or. SA1->A1_VEND == ::SellerCode
						aadd(::InvoiceHeader,WsClassNew("InvoiceHeaderView"))
						nX++
						GetNFEHead(@::InvoiceHeader[nX],cAliasSF1)
					EndIf

				EndIf
			EndIf
			dbSelectArea(cAliasSF1)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSF1)
			dbCloseArea()
			dbSelectArea("SF1")
		Else
			dbSelectArea("SF1")
			RetIndex("SF1")
			FErase(cArquivo+OrdBagExt())
		EndIf
	EndIf
	//Ŀ
	//Pesquisa os documentos de saida                                         
	//
	If Empty(::OperationType) .Or. ::OperationType=="S"
		dbSelectArea("SF2")
		dbSetOrder(2)
		lQuery := .T.
		cAliasSF2 := "BRWINVOICE"
		aStruSF2  := SF2->(dbStruct())

		cQuery := "SELECT F2_FILIAL,F2_TIPO,F2_CLIENTE,F2_LOJA,F2_DOC,F2_SERIE, "
		cQuery += "F2_EMISSAO,F2_VALBRUT,F2_DESPESA,F2_SEGURO,F2_FRETE,F2_TRANSP, "
		aCampos := MaFisRefLd("SF2","NF")
		For nY := 1 To Len(aCampos)
			If !Empty(aCampos[nY][2])
				cQuery += aCampos[nY][2]+","
			EndIf
			If !Empty(aCampos[nY][3])
				cQuery += aCampos[nY][3]+","
			EndIf
			If !Empty(aCampos[nY][4])
				cQuery += aCampos[nY][4]+","
			EndIf
		Next nY
		cVolume := "1"
		While SF2->(FieldPos("F2_ESPECI"+cVolume))<>0 .And. !Empty(SF2->(FieldGet(FieldPos("F2_ESPECI"+cVolume))))
			cQuery += "F2_ESPECI"+cVolume+","+"F2_ESPECI"+cVolume+","
			cVolume := Soma1( cVolume )
		EndDo
		cQuery += "F2_PREFIXO,F2_DUPL "
		cQuery += GetUserField("SF2")
		cQuery += "FROM "+RetSqlName("SF2")+" SF2 "
		If Empty(cCliente)
			cQuery += ","+RetSqlName("SA1")+" SA1 "
		EndIf
		If !Empty(::PurchaseNumber)
			cQuery += ","+RetSqlName("SD2")+" SD2 "
		EndIf
		cQuery += "WHERE SF2.F2_FILIAL='"+xFilial("SF2")+"' AND "
		cQuery += "SF2.F2_TIPO NOT IN('D','B') AND "
		cQuery += "SF2.F2_EMISSAO >= '"+Dtos(dEmisIni)+"' AND "
		cQuery += "SF2.F2_EMISSAO <= '"+Dtos(dEmisFim)+"' AND "
		cQuery += "SF2.F2_EMISSAO >= '"+Dtos(dEntrIni)+"' AND "
		cQuery += "SF2.F2_EMISSAO <= '"+Dtos(dEntrFim)+"' AND "
		cQuery += "NOT ("+IsRemito(2,"SF2.F2_TIPODOC")+") AND " 
		If !Empty(cCliente)
			cQuery += "SF2.F2_CLIENTE='"+cCliente+"' AND "
			cQuery += "SF2.F2_LOJA='"+cLojaCli+"' AND "
		Else
			cQuery += "SA1.A1_FILIAL='"+xFilial("SA1")+"' AND "
			cQuery += "SA1.A1_COD=SF2.F2_CLIENTE AND "
			cQuery += "SA1.A1_LOJA=SF2.F2_LOJA AND "
			cQuery += "SA1.A1_VEND='"+::SellerCode+"' AND "
			cQuery += "SA1.D_E_L_E_T_=' ' AND "
		EndIf
		cQuery += "SF2.D_E_L_E_T_=' ' "
		If !Empty(::PurchaseNumber)
			cQuery += " AND "
			cQuery += "SD2.D2_FILIAL = '"+xFilial("SD2")+"' AND "
			cQuery += "SD2.D2_DOC = SF2.F2_DOC AND "
			cQuery += "SD2.D2_SERIE = SF2.F2_SERIE AND "
			cQuery += "SD2.D2_CLIENTE = SF2.F2_CLIENTE AND "
			cQuery += "SD2.D2_LOJA = SF2.F2_LOJA AND "
			cQuery += "SD2.D2_PEDIDO = '"+::PurchaseNumber+"' AND "
			cQuery += "SD2.D_E_L_E_T_=' ' "
		EndIf
		cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfs)
		cQuery += "ORDER BY "+WsSqlOrder(IIf(Empty(::IndexKeyNfs),SF2->(IndexKey()),::IndexKeyNfs))

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF2)

		For nY := 1 To Len(aStruSF2)
			If aStruSF2[nY][2] <> "C" .And. aStruSF2[nY][2] <> "M"
				TcSetField(cAliasSF2,aStruSF2[nY][1],aStruSF2[nY][2],aStruSF2[nY][3],aStruSF2[nY][4])
			EndIf
		Next nY

		DEFAULT ::InvoiceHeader := {}
		While !Eof() .And. xFilial("SF2") == (cAliasSF2)->F2_FILIAL 

			If !(cAliasSF2)->F2_TIPO $ 'DB' .And.;
				(cAliasSF2)->F2_EMISSAO >= dEmisIni .And.;
				(cAliasSF2)->F2_EMISSAO <= dEmisFim .And.;
				(cAliasSF2)->F2_EMISSAO >= dEntrIni .And.;
				(cAliasSF2)->F2_EMISSAO <= dEntrFim
				If !lQuery
					dbSelectArea("SA1")
					dbSetOrder(1)
					MsSeek(xFilial("SA1")+cCliente+cLojaCli)
				EndIf
				If lQuery
					lValido := .T.
				Else
					If Empty(::PurchaseNumber)
						lValido := .T.
					Else
						dbSelectArea("SD2")
						dbSetOrder(1)
						MsSeek(xFilial("SD2")+(cAliasSF2)->F2_DOC+(cAliasSF2)->F2_SERIE+(cAliasSF2)->F2_CLIENTE+(cAliasSF2)->F2_LOJA)
						While !Eof() .And.;
							xFilial("SD2") == (cAliasSD2)->D2_FILIAL .And.;
							(cAliasSF2)->F2_DOC == (cAliasSD2)->D2_DOC .And.;
							(cAliasSF2)->F2_SERIE == (cAliasSD2)->D2_SERIE .And.;
							(cAliasSF2)->F2_CLIENTE == (cAliasSD2)->D2_CLIENTE .And.;
							(cAliasSF2)->F2_LOJA == (cAliasSD2)->D2_LOJA

							If (cAliasSD2)->D2_PEDIDO == ::PurchaseNumber
								lValido := .T.
								Exit
							EndIf
							dbSelectArea(cAliasSD2)
							dbSkip()
						EndDo
					EndIf
				EndIf
				If lValido

					If lQuery .Or. SA1->A1_VEND == ::SellerCode
						aadd(::InvoiceHeader,WsClassNew("InvoiceHeaderView"))
						nX++
						GetNFSHead(@::InvoiceHeader[nX],cAliasSF2)
					EndIf
					
				EndIf
			EndIf
			dbSelectArea(cAliasSF2)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSF2)
			dbCloseArea()
			dbSelectArea("SF2")
		Else
			dbSelectArea("SF2")
			RetIndex("SF2")
			FErase(cArquivo+OrdBagExt())
		EndIf
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetInvoiceAutor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de recuperacao dos documento de entrada / saida       
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do vendedor                                    
          ExpC3: Tipo do documento de entrada/saida                    
          ExpC4: Numero de Serie                                       
          ExpC5: Numero do documento                                   
          ExpC6: Formulario Proprio?                                   
          ExpC7: Cliente                                               
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve os documentos de entrad ou saida         
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetInvoice WSRECEIVE UserCode,SellerCode,InvoiceType,SerialNumber,InvoiceNumber,DocumentType,CustomerID WSSEND Invoice WSSERVICE MtSellerInvoice

Local aArea    := GetArea()
Local cES      := SubStr(::InvoiceType,1,1)
Local cCliente := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLojaCli := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local cSerie   := wsmat_nfSerie(::SerialNumber)
Local cDoc     := SubStr(::InvoiceNumber,1,Len(SD1->D1_DOC))
Local cTipo    := AllTrim(SubStr(::InvoiceType,3))
Local cFormul  := ::DocumentType
Local cAliasSD1:= "SD1"
Local cAliasSD2:= "SD2"
Local nX       := 0
Local lQuery   := .F.
Local lRetorno := .F.
Local aStruSD1 := {}
Local aStruSD2 := {}
Local cQuery   := ""
Local nY       := 0

DEFAULT cFormul := " "
If cPaisLoc=="BRA"
	cFormul  := IIf(cFormul=="S","S"," ")
Else                                            
	cFormul  := IIf(cFormul=="S","S","N")
EndIf	       

If PrtChkUser(::UserCode,"MtSellerInvoice","GetInvoice","SA3",::SellerCode)
	//Ŀ
	//Verifica se qual o tipo de documento                                    
	//
	If cES == "E"
		dbSelectArea("SF1")
		dbSetOrder(1)
		MsSeek(xFilial("SF1")+cDoc+cSerie+cCliente+cLojaCli+cTipo)
		While !Eof() .And. xFilial("SF1") == SF1->F1_FILIAL .And.;
			cDoc == SF1->F1_DOC .And.;
			cSerie == SF1->F1_SERIE .And.;
			cCliente == SF1->F1_FORNECE .And.;
			cLojaCli == SF1->F1_LOJA .And.;
			cTipo == SF1->F1_TIPO 
			If SF1->F1_FORMUL == cFormul
				lRetorno := .T.
				Exit
			EndIf
			dbSelectArea("SF1")
			dbSkip()
		EndDo
		If lRetorno
			::Invoice:InvoiceHeader := WsClassNew("InvoiceHeaderView")
			GetNfeHead(@::Invoice:InvoiceHeader,"SF1")
			//Ŀ
			//Pesquisa os Itens do documento de entrada                               
			//
			::Invoice:InvoiceItem:= {}
			dbSelectArea("SD1")
			dbSetOrder(1)
			aStruSD1 := SD1->(dbStruct())
			lQuery   := .T.
			cAliasSD1:= "GETINVOICE"

			cQuery := "SELECT * "
			cQuery += "FROM "+RetSqlName("SD1")+" SD1 "
			cQuery += "WHERE SD1.D1_FILIAL='"+xFilial("SD1")+"' AND "
			cQuery += "SD1.D1_DOC='"+cDoc+"' AND "
			cQuery += "SD1.D1_SERIE='"+cSerie+"' AND "
			cQuery += "SD1.D1_FORNECE='"+cCliente+"' AND "
			cQuery += "SD1.D1_LOJA='"+cLojaCli+"' AND "	
			cQuery += "SD1.D1_TIPO='"+cTipo+"' AND "
			cQuery += "SD1.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfe)
			cQuery += "ORDER BY "+SqlOrder(SD1->(IndexKey()))

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1)

			For nY := 1 To Len(aStruSD1)
				If aStruSD1[nY][2] <> "C" .And. aStruSD1[nY][2] <> "M"
					TcSetField(cAliasSD1,aStruSD1[nY][1],aStruSD1[nY][2],aStruSD1[nY][3],aStruSD1[nY][4])
				EndIf
			Next nY

			nX := 0
			While !Eof() .And.;
				xFilial("SD1") == (cAliasSD1)->D1_FILIAL .And.;
				cDoc == (cAliasSD1)->D1_DOC .And.;
				cSerie == (cAliasSD1)->D1_SERIE .And.;
				cCliente == (cAliasSD1)->D1_FORNECE .And.;
				cLojaCli == (cAliasSD1)->D1_LOJA 

				If (cAliasSD1)->D1_TIPO == cTipo .And. (cAliasSD1)->D1_FORMUL == cFormul
					If nX == 0
						::Invoice:InvoiceItem:= {}
					EndIf
					aadd(::Invoice:InvoiceItem,WsClassNew("InvoiceItemView"))
					nX++
					GetNFEItem(@::Invoice:InvoiceItem[nX],cAliasSD1)
				EndIf
				dbSelectArea(cAliasSD1)
				dbSkip()
			EndDo
			If lQuery
				dbSelectArea(cAliasSD1)
				dbCloseArea()
				dbSelectArea("SD1")
			EndIf
		EndIf
	Else
		dbSelectArea("SF2")
		dbSetOrder(1)
		If MsSeek(xFilial("SF2")+cDoc+cSerie+cCliente+cLojaCli)
			lRetorno := .T.
			::Invoice:InvoiceHeader := WsClassNew("InvoiceHeaderView")
			GetNfSHead(@::Invoice:InvoiceHeader,"SF2")
			//Ŀ
			//Pesquisa os Itens do documento de saida                                 
			//
			::Invoice:InvoiceItem:= {}
			dbSelectArea("SD2")
			dbSetOrder(3)
			aStruSD2 := SD2->(dbStruct())
			lQuery   := .T.
			cAliasSD2:= "GETINVOICE"

			cQuery := "SELECT * "
			cQuery += "FROM "+RetSqlName("SD2")+" SD2 "
			cQuery += "WHERE SD2.D2_FILIAL='"+xFilial("SD2")+"' AND "
			cQuery += "SD2.D2_DOC='"+cDoc+"' AND "
			cQuery += "SD2.D2_SERIE='"+cSerie+"' AND "
			cQuery += "SD2.D2_CLIENTE='"+cCliente+"' AND "
			cQuery += "SD2.D2_LOJA='"+cLojaCli+"' AND "	
			cQuery += "SD2.D2_TIPO='"+cTipo+"' AND "
			cQuery += "SD2.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfs)
			cQuery += "ORDER BY "+SqlOrder(SD2->(IndexKey()))

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD2)

			For nY := 1 To Len(aStruSD2)
				If aStruSD2[nY][2] <> "C" .And. aStruSD2[nY][2] <> "M"
					TcSetField(cAliasSD2,aStruSD2[nY][1],aStruSD2[nY][2],aStruSD2[nY][3],aStruSD2[nY][4])
				EndIf
			Next nY

			nX := 0
			While !Eof() .And.;
				xFilial("SD2") == (cAliasSD2)->D2_FILIAL .And.;
				cDoc == (cAliasSD2)->D2_DOC .And.;
				cSerie == (cAliasSD2)->D2_SERIE .And.;
				cCliente == (cAliasSD2)->D2_CLIENTE .And.;
				cLojaCli == (cAliasSD2)->D2_LOJA

				If (cAliasSD2)->D2_TIPO == cTipo
					If nX == 0
	    				::Invoice:InvoiceItem:= {}
					EndIf
					aadd(::Invoice:InvoiceItem,WsClassNew("InvoiceItemView"))
					nX++
					GetNFSItem(@::Invoice:InvoiceItem[nX],cAliasSD2)
				EndIf
				dbSelectArea(cAliasSD2)
				dbSkip()
			EndDo
			If lQuery
				dbSelectArea(cAliasSD2)
				dbCloseArea()
				dbSelectArea("SD2")
			EndIf
		EndIf
	EndIf
	If !lRetorno
		SetSoapFault("GETINVOICE",STR0005) //"Documento nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)


/*/

Ŀ
Funo    GetNfeHeadAutor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de preenchimento do cabecalho do documento de entrada 
                                                                       
Ĵ
ParametrosExpO1: Objeto do cabecalho                                   
          ExpC2: Alias do SF1                                          
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio Este metodo devolve o objeto passado por parametro com os    
          dados do sf1 posicionado                                     
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Function GetNfeHead(oObjeto,cAliasSF1,aCab)

Local aArea     := GetArea()

Local aImpostos := {}
Local nY        := 0
Local cAlias    := IIf((cAliasSF1)->F1_TIPO$'DB',"SA1","SA2")
Local cAliasSE2 := "SE2"
Local cPrefixo  := ""
Local lQuery    := .F.
Local aStruSE2  := {}
Local cQuery    := ""

If aCab == Nil
	oObjeto:SerialNumber  := (cAliasSF1)->F1_SERIE
	oObjeto:InvoiceNumber := (cAliasSF1)->F1_DOC
	oObjeto:DocumentType  := (cAliasSF1)->F1_FORMUL
	oObjeto:InvoiceType   := "E/"+(cAliasSF1)->F1_TIPO
	oObjeto:RegisterDate  := (cAliasSF1)->F1_EMISSAO
	oObjeto:InvoiceDate   := (cAliasSF1)->F1_DTDIGIT
	oObjeto:DeliveryDate  := (cAliasSF1)->F1_DTDIGIT
	oObjeto:TotalValue    := (cAliasSF1)->F1_VALBRUT
	oObjeto:ExpensesValue := (cAliasSF1)->F1_DESPESA
	oObjeto:InsuranceValue:= (cAliasSF1)->F1_SEGURO
	oObjeto:FreightValue  := (cAliasSF1)->F1_FRETE
	oObjeto:InvoiceStatus := (cAliasSF1)->F1_STATUS
	
	oObjeto:FromRole := WsClassNew("GenericStruct")
	oObjeto:FromRole:Code        := (cAliasSF1)->F1_FORNECE+(cAliasSF1)->F1_LOJA
	oObjeto:FromRole:Description := Posicione(cAlias,1,xFilial(cAlias)+(cAliasSF1)->F1_FORNECE+(cAliasSF1)->F1_LOJA,IIF(cAlias=="SA1","A1_NOME","A2_NOME"))
	oObjeto:ToRole   := WsClassNew("GenericStruct")
	oObjeto:ToRole:Code          := cEmpAnt+cFilAnt
	oObjeto:ToRole:Description := SM0->M0_NOME

	aImpostos := MaFisImpLd("SF1","NF",cAliasSF1)
	For nY := 1 To Len(aImpostos)
		If nY == 1
			oObjeto:Taxes := {}
		EndIf
		aadd(oObjeto:Taxes,WsClassNew("TaxesView"))
		oObjeto:Taxes[nY]:TaxCode   := aImpostos[nY][1]
		oObjeto:Taxes[nY]:TaxBase   := aImpostos[nY][2]
		oObjeto:Taxes[nY]:TaxRate   := aImpostos[nY][3]
		oObjeto:Taxes[nY]:TaxAmount := aImpostos[nY][4]
	Next nY
	UserFields("SF1",@oObjeto:UserFields,cAliasSF1)
	//Ŀ
	//Pesquisa os titulos financeiros gerados                                 
	//
	cPrefixo	:= (cAliasSF1)->F1_PREFIXO
	If !Empty((cAliasSF1)->F1_DUPL) .And. !(cAliasSF1)->F1_TIPO $ 'DB'
		dbSelectArea("SE2")
		dbSetOrder(6)
		lQuery    := .T.
		aStruSE2  := SE2->(dbStruct())
		cAliasSE2 := "BRWINVOICESE2"
		cQuery    := "SELECT SE2.*,SE2.R_E_C_N_O_ SE2RECNO "
		cQuery    += "FROM "+RetSqlName("SE2")+" SE2 "
		cQuery    += "WHERE SE2.E2_FILIAL='"+xFilial("SE2")+"' AND "
		cQuery    += "SE2.E2_FORNECE='"+(cAliasSF1)->F1_FORNECE+"' AND "
		cQuery    += "SE2.E2_LOJA='"+(cAliasSF1)->F1_LOJA+"' AND "
		cQuery    += "SE2.E2_PREFIXO='"+cPrefixo+"' AND "
		cQuery    += "SE2.E2_NUM='"+(cAliasSF1)->F1_DUPL+"' AND "
		cQuery    += "SE2.E2_TIPO='NF ' AND "
		cQuery    += "SE2.D_E_L_E_T_=' ' "
		cQuery    += "ORDER BY "+SqlOrder(SE2->(IndexKey()))

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE2,.T.,.T.)
		For nY := 1 To Len(aStruSE2)
			If aStruSE2[nY][2]<>"C" .And. aStruSE2[nY][2]<>"M"
				TcSetField(cAliasSE2,aStruSE2[nY][1],aStruSE2[nY][2],aStruSE2[nY][3],aStruSE2[nY][4])
			EndIf
		Next nY

		nY := 0
		While ( !Eof() .And. xFilial("SE2") == (cAliasSE2)->E2_FILIAL .And.;
			(cAliasSF1)->F1_FORNECE == (cAliasSE2)->E2_FORNECE .And.;
			(cAliasSF1)->F1_LOJA == (cAliasSE2)->E2_LOJA .And.;
			cPrefixo == (cAliasSE2)->E2_PREFIXO .And.;
			(cAliasSF1)->F1_DUPL == (cAliasSE2)->E2_NUM )
			If (cAliasSE2)->E2_TIPO == MVNOTAFIS

				If nY == 0
					oObjeto:PaymentPlan := {}
				EndIf
				aadd(oObjeto:PaymentPlan,WsClassNew("BillView"))
				nY++
				GetCPBill(@oObjeto:PaymentPlan[nY],cAliasSE2,.T.)

			EndIf
			dbSelectArea(cAliasSE2)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSE2)
			dbCloseArea()
			dbSelectArea("SE2")
		EndIf
	EndIf
Else
	oObjeto:SerialNumber  := WsProcH(aCab,"F1_SERIE")
	oObjeto:InvoiceNumber := WsProcH(aCab,"F1_DOC")
	oObjeto:DocumentType  := WsProcH(aCab,"F1_FORMUL")
	oObjeto:InvoiceType   := "E/"+WsProcH(aCab,"F1_TIPO")
	oObjeto:RegisterDate  := WsProcH(aCab,"F1_EMISSAO")
	oObjeto:InvoiceDate   := WsProcH(aCab,"F1_DTDIGIT")
	oObjeto:DeliveryDate  := WsProcH(aCab,"F1_DTDIGIT")
	oObjeto:TotalValue    := WsProcH(aCab,"F1_VALBRUT")
	oObjeto:ExpensesValue := WsProcH(aCab,"F1_DESPESA")
	oObjeto:InsuranceValue:= WsProcH(aCab,"F1_SEGURO")
	oObjeto:FreightValue  := WsProcH(aCab,"F1_FRETE")

	oObjeto:FromRole := WsClassNew("GenericStruct")
	oObjeto:FromRole:Code        := WsProcH(aCab,"F1_FORNECE")+WsProcH(aCab,"F1_LOJA")
	oObjeto:FromRole:Description := Posicione(cAlias,1,xFilial(cAlias)+WsProcH(aCab,"F1_FORNECE")+WsProcH(aCab,"F1_LOJA"),IIF(cAlias=="SA1","A1_NOME","A2_NOME"))
	oObjeto:ToRole   := WsClassNew("GenericStruct")
	oObjeto:ToRole:Code          := cEmpAnt+cFilAnt
	oObjeto:ToRole:Description := SM0->M0_NOME

	aImpostos := MaFisImpLd("SF1","NF",cAliasSF1)
	For nY := 1 To Len(aImpostos)
		If nY == 1
			oObjeto:Taxes := {}
		EndIf
		aadd(oObjeto:Taxes,WsClassNew("TaxesView"))
		oObjeto:Taxes[nY]:TaxCode   := aImpostos[nY][1]
		oObjeto:Taxes[nY]:TaxBase   := aImpostos[nY][2]
		oObjeto:Taxes[nY]:TaxRate   := aImpostos[nY][3]
		oObjeto:Taxes[nY]:TaxAmount := aImpostos[nY][4]
	Next nY
	UserFields("SF1",@oObjeto:UserFields,cAliasSF1,@aCab)
EndIf
RestArea(aArea)
Return(.T.)

/*/

Ŀ
Funo    GetNfeItemAutor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de preenchimento do item do documento de entrada      
                                                                       
Ĵ
ParametrosExpO1: Objeto do cabecalho                                   
          ExpC2: Alias do SD1                                          
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio Este metodo devolve o objeto passado por parametro com os    
          dados do sd1 posicionado                                     
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                      
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                    
Ĵ
 Cleber M.    18/10/06111336- Inicializacao da classe LOTVIEW.       
ٱ


/*/
Static Function GetNfeItem(oObjeto,cAliasSD1,aItem)

Local aArea     := GetArea()	//Guarda a area atual
Local aImpostos := {}			//Array dos Impostos da NFE
Local nY        := 0			//Variavel usada em lacos For...Next

If aItem == Nil
	oObjeto:SequentialID       := (cAliasSD1)->D1_ITEM
	oObjeto:ProductCode        := (cAliasSD1)->D1_COD
	oObjeto:DescriptionProduct := Posicione("SB1",1,xFilial("SB1")+(cAliasSD1)->D1_COD,"B1_DESC")
	oObjeto:MeasureUnit        := (cAliasSD1)->D1_UM
	oObjeto:Quantity           := (cAliasSD1)->D1_QUANT
	oObjeto:UnitPrice          := (cAliasSD1)->D1_VUNIT
	oObjeto:TotalValue         := (cAliasSD1)->D1_TOTAL
	oObjeto:DiscountPercent    := (cAliasSD1)->D1_DESC
	oObjeto:DiscountValue      := (cAliasSD1)->D1_VALDESC
	oObjeto:ExpensesValue      := (cAliasSD1)->D1_DESPESA
	oObjeto:InsuranceValue     := (cAliasSD1)->D1_SEGURO
	oObjeto:FreightValue       := (cAliasSD1)->D1_VALFRE
	oObjeto:QualityControlDocument := (cAliasSD1)->D1_NUMCQ
	aImpostos := MaFisImpLd("SD1","IT",cAliasSD1)
	For nY := 1 To Len(aImpostos)
		If nY == 1
			oObjeto:Taxes := {}
		EndIf
		aadd(oObjeto:Taxes,WsClassNew("TaxesView"))
		oObjeto:Taxes[nY]:TaxCode   := aImpostos[nY][1]
		oObjeto:Taxes[nY]:TaxBase   := aImpostos[nY][2]
		oObjeto:Taxes[nY]:TaxRate   := aImpostos[nY][3]
		oObjeto:Taxes[nY]:TaxAmount := aImpostos[nY][4]
	Next nY
	If !Empty((cAliasSD1)->D1_PEDIDO)
		dbSelectArea("SC7")
		dbSetOrder(14)
		MsSeek(xFilial("SC7")+(cAliasSD1)->D1_PEDIDO+(cAliasSD1)->D1_ITEMPC)
		oObjeto:PurchaseOrder:=WsClassNew("PURCHASEORDERITEMVIEW")
		GetPOItem(oObjeto:PurchaseOrder,"SC7")
		oObjeto:PurchaseOrder:QuantityDelivered := (cAliasSD1)->D1_QTDPEDI
	EndIf
	If !Empty((cAliasSD1)->D1_LOTECTL) .Or. !Empty((cAliasSD1)->D1_NUMLOTE)
		oObjeto:LotIdentifier := WsClassNew( "LOTVIEW" )
		oObjeto:LotIdentifier:LotNumber    := (cAliasSD1)->D1_LOTECTL+(cAliasSD1)->D1_NUMLOTE
		oObjeto:LotIdentifier:PotencyLot   := (cAliasSD1)->D1_POTENCI 
		oObjeto:LotIdentifier:ValidityDate := (cAliasSD1)->D1_DTVALID
	EndIf
	UserFields("SD1",@oObjeto:UserFields,cAliasSD1)
Else
	oObjeto:SequentialID       := WsProcH(aItem,"D1_ITEM")
	oObjeto:ProductCode        := WsProcH(aItem,"D1_COD")
	oObjeto:DescriptionProduct := Posicione("SB1",1,xFilial("SB1")+WsProcH(aItem,"D1_COD"),"B1_DESC")
	oObjeto:MeasureUnit        := WsProcH(aItem,"D1_UM")
	oObjeto:Quantity           := WsProcH(aItem,"D1_QUANT")
	oObjeto:UnitPrice          := WsProcH(aItem,"D1_VUNIT")
	oObjeto:TotalValue         := WsProcH(aItem,"D1_TOTAL")
	oObjeto:DiscountPercent    := WsProcH(aItem,"D1_DESC")
	oObjeto:DiscountValue      := WsProcH(aItem,"D1_VALDESC")
	oObjeto:ExpensesValue      := WsProcH(aItem,"D1_DESPESA")
	oObjeto:InsuranceValue     := WsProcH(aItem,"D1_SEGURO")
	oObjeto:FreightValue       := WsProcH(aItem,"D1_VALFRE")
	oObjeto:QualityControlDocument := WsProcH(aItem,"D1_NUMCQ")
	aImpostos := MaFisImpLd("SD1","IT",cAliasSD1)
	For nY := 1 To Len(aImpostos)
		If nY == 1
			oObjeto:Taxes := {}
		EndIf
		aadd(oObjeto:Taxes,WsClassNew("TaxesView"))
		oObjeto:Taxes[nY]:TaxCode   := aImpostos[nY][1]
		oObjeto:Taxes[nY]:TaxBase   := aImpostos[nY][2]
		oObjeto:Taxes[nY]:TaxRate   := aImpostos[nY][3]
		oObjeto:Taxes[nY]:TaxAmount := aImpostos[nY][4]
	Next nY
	If !Empty(WsProcH(aItem,"D1_PEDIDO"))
		dbSelectArea("SC7")
		dbSetOrder(14)
		dbSeek(xFilial("SC7")+WsProcH(aItem,"D1_PEDIDO")+WsProcH(aItem,"D1_ITEMPC"))
		oObjeto:PurchaseOrder:=WsClassNew("PURCHASEORDERITEMVIEW")
		GetPOItem(oObjeto:PurchaseOrder,"SC7")
		oObjeto:PurchaseOrder:QuantityDelivered := WsProcH(aItem,"D1_QTDPEDI")
	EndIf
	If !Empty(WsProcH(aItem,"D1_LOTECTL")) .Or. !Empty(WsProcH(aItem,"D1_NUMLOTE"))
		oObjeto:LotIdentifier := WsClassNew( "LOTVIEW" )
		oObjeto:LotIdentifier:LotNumber    := WsProcH(aItem,"D1_LOTECTL") + WsProcH(aItem,"D1_NUMLOTE")
		oObjeto:LotIdentifier:PotencyLot   := WsProcH(aItem,"D1_POTENCI")
		oObjeto:LotIdentifier:ValidityDate := WsProcH(aItem,"D1_DTVALID")
	EndIf
	UserFields("SD1",@oObjeto:UserFields,cAliasSD1,@aItem)
EndIf

RestArea(aArea)
Return(.T.)

/*/

Ŀ
Funo    GetNfSHeadAutor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de preenchimento do cabecalho do documento de saida   
                                                                       
Ĵ
ParametrosExpO1: Objeto do cabecalho                                   
          ExpC2: Alias do SF2                                          
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio Este metodo devolve o objeto passado por parametro com os    
          dados do SF2 posicionado                                     
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Function GetNfSHead(oObjeto,cAliasSF2)

Local aArea     := GetArea()
Local aImpostos := {}
Local cAliasSE1 := "SE1"
Local cAlias    := IIf((cAliasSF2)->F2_TIPO$'DB',"SA2","SA1")
Local cVolume   := ""
Local cPrefixo  := ""
Local nY        := 0
Local lQuery    := .F.
Local oTemp
Local aStruSE1  := {}
Local cQuery    := ""

oObjeto:SerialNumber  := (cAliasSF2)->F2_SERIE
oObjeto:InvoiceNumber := (cAliasSF2)->F2_DOC
oObjeto:InvoiceType   := "S/"+(cAliasSF2)->F2_TIPO
oObjeto:RegisterDate  := (cAliasSF2)->F2_EMISSAO
oObjeto:InvoiceDate   := (cAliasSF2)->F2_EMISSAO
oObjeto:DeliveryDate  := (cAliasSF2)->F2_EMISSAO
oObjeto:TotalValue := (cAliasSF2)->F2_VALBRUT
oObjeto:ExpensesValue := (cAliasSF2)->F2_DESPESA
oObjeto:InsuranceValue:= (cAliasSF2)->F2_SEGURO
oObjeto:FreightValue  := (cAliasSF2)->F2_FRETE

oObjeto:ToRole := WsClassNew("GenericStruct")
oObjeto:ToRole:Code        := (cAliasSF2)->F2_CLIENTE+(cAliasSF2)->F2_LOJA
oObjeto:ToRole:Description := Posicione(cAlias,1,xFilial(cAlias)+(cAliasSF2)->F2_CLIENTE+(cAliasSF2)->F2_LOJA,IIF(cAlias=="SA1","A1_NOME","A2_NOME"))
oObjeto:FromRole := WsClassNew("GenericStruct")
oObjeto:FromRole:Code          := cEmpAnt+cFilAnt
oObjeto:FromRole:Description := SM0->M0_NOME

If !Empty((cAliasSF2)->F2_TRANSP)
	oObjeto:Carrier := WsClassNew("GenericStruct")
	oObjeto:Carrier:Code        := (cAliasSF2)->F2_TRANSP
	oObjeto:Carrier:Description := Posicione("SA4",1,xFilial("SA4")+(cAliasSF2)->F2_TRANSP,"A4_NOME")
EndIf

cVolume := "1"
oObjeto:PackagesVolumes := {}
While (cAliasSF2)->(FieldPos("F2_ESPECI"+cVolume))<>0 .And. !Empty((cAliasSF2)->(FieldGet(FieldPos("F2_ESPECI"+cVolume))))
	oTemp             := WsClassNew("GenericStruct")
	oTemp:Code        := cVolume
	oTemp:Description := (cAliasSF2)->(FieldGet(FieldPos("F2_ESPECI"+cVolume)))
	oTemp:Value       := (cAliasSF2)->(FieldGet(FieldPos("F2_VOLUME"+cVolume)))
	aadd( oObjeto:PackagesVolumes, oTemp )
 	cVolume := Soma1( cVolume )
EndDo

aImpostos := MaFisImpLd("SF2","NF",cAliasSF2)
For nY := 1 To Len(aImpostos)
	If nY == 1
		oObjeto:Taxes := {}
	EndIf
	aadd(oObjeto:Taxes,WsClassNew("TaxesView"))
	oObjeto:Taxes[nY]:TaxCode   := aImpostos[nY][1]
	oObjeto:Taxes[nY]:TaxBase   := aImpostos[nY][2]
	oObjeto:Taxes[nY]:TaxRate   := aImpostos[nY][3]
	oObjeto:Taxes[nY]:TaxAmount := aImpostos[nY][4]
Next nY
UserFields("SF2",@oObjeto:UserFields,cAliasSF2)
//Ŀ
//Pesquisa os titulos financeiros gerados                                 
//
cPrefixo	:= (cAliasSF2)->F2_PREFIXO
If !Empty((cAliasSF2)->F2_DUPL)
	dbSelectArea("SE1")
	dbSetOrder(2)
	lQuery    := .T.
	aStruSE1  := SE1->(dbStruct())
	cAliasSE1 := "BRWINVOICESE1"
	cQuery    := "SELECT * "
	cQuery    += "FROM "+RetSqlName("SE1")+" SE1 "
	cQuery    += "WHERE SE1.E1_FILIAL='"+xFilial("SE1")+"' AND "
	cQuery    += "SE1.E1_CLIENTE='"+(cAliasSF2)->F2_CLIENTE+"' AND "
	cQuery    += "SE1.E1_LOJA='"+(cAliasSF2)->F2_LOJA+"' AND "
	cQuery    += "SE1.E1_PREFIXO='"+cPrefixo+"' AND "
	cQuery    += "SE1.E1_NUM='"+(cAliasSF2)->F2_DUPL+"' AND "
	cQuery    += "SE1.E1_TIPO='NF ' AND "
	cQuery    += "SE1.D_E_L_E_T_=' ' "
	cQuery    += "ORDER BY "+SqlOrder(SE1->(IndexKey()))

	cQuery := ChangeQuery(cQuery)

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE1,.T.,.T.)
	For nY := 1 To Len(aStruSE1)
		If aStruSE1[nY][2]<>"C" .And. aStruSE1[nY][2]<>"M"
			TcSetField(cAliasSE1,aStruSE1[nY][1],aStruSE1[nY][2],aStruSE1[nY][3],aStruSE1[nY][4])
		EndIf
	Next nY

	nY := 0
	While ( !Eof() .And. xFilial("SE1") == (cAliasSE1)->E1_FILIAL .And.;
		(cAliasSF2)->F2_CLIENTE == (cAliasSE1)->E1_CLIENTE .And.;
		(cAliasSF2)->F2_LOJA == (cAliasSE1)->E1_LOJA .And.;
		cPrefixo == (cAliasSE1)->E1_PREFIXO .And.;
		(cAliasSF2)->F2_DUPL == (cAliasSE1)->E1_NUM )
		If (cAliasSE1)->E1_TIPO == MVNOTAFIS

			If nY == 0
				oObjeto:PaymentPlan := {}
			EndIf
			aadd(oObjeto:PaymentPlan,WsClassNew("BillView"))
			nY++
			GetCRBill(@oObjeto:PaymentPlan[nY],cAliasSE1,.T.)
		EndIf
		dbSelectArea(cAliasSE1)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasSE1)
		dbCloseArea()
		dbSelectArea("SE1")
	EndIf
EndIf
RestArea(aArea)
Return(.T.)

/*/

Ŀ
Funo    GetNfSItemAutor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de preenchimento do item do documento de Saida        
                                                                       
Ĵ
ParametrosExpO1: Objeto do cabecalho                                   
          ExpC2: Alias do SD2                                          
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio Este metodo devolve o objeto passado por parametro com os    
          dados do SD2 posicionado                                     
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Static Function GetNfsItem(oObjeto,cAliasSD2)

Local aArea     := GetArea()
Local aImpostos := {}
Local nY        := 0

oObjeto:SequentialID       := (cAliasSD2)->D2_ITEM
oObjeto:ProductCode        := (cAliasSD2)->D2_COD
oObjeto:DescriptionProduct := Posicione("SB1",1,xFilial("SB1")+(cAliasSD2)->D2_COD,"B1_DESC")
oObjeto:MeasureUnit        := (cAliasSD2)->D2_UM
oObjeto:Quantity           := (cAliasSD2)->D2_QUANT
oObjeto:UnitPrice          := (cAliasSD2)->D2_PRCVEN
oObjeto:TotalValue         := (cAliasSD2)->D2_TOTAL
oObjeto:DiscountPercent    := (cAliasSD2)->D2_DESC
oObjeto:DiscountValue      := (cAliasSD2)->D2_DESCON
oObjeto:ExpensesValue      := (cAliasSD2)->D2_DESPESA
oObjeto:InsuranceValue     := (cAliasSD2)->D2_SEGURO
oObjeto:FreightValue       := (cAliasSD2)->D2_VALFRE

aImpostos := MaFisImpLd("SD2","IT",cAliasSD2)
For nY := 1 To Len(aImpostos)
	If nY == 1
		oObjeto:Taxes := {}
	EndIf
	aadd(oObjeto:Taxes,WsClassNew("TaxesView"))
	oObjeto:Taxes[nY]:TaxCode   := aImpostos[nY][1]
	oObjeto:Taxes[nY]:TaxBase   := aImpostos[nY][2]
	oObjeto:Taxes[nY]:TaxRate   := aImpostos[nY][3]
	oObjeto:Taxes[nY]:TaxAmount := aImpostos[nY][4]
Next nY
If !Empty((cAliasSD2)->D2_PEDIDO)
	dbSelectArea("SC5")
	dbSetOrder(1)
	MsSeek(xFilial("SC5")+(cAliasSD2)->D2_PEDIDO)
	dbSelectArea("SC6")
	dbSetOrder(1)
	MsSeek(xFilial("SC6")+(cAliasSD2)->D2_PEDIDO+(cAliasSD2)->D2_ITEMPV+(cAliasSD2)->D2_COD)
	oObjeto:SalesOrder:=WsClassNew("SALESORDERITEMVIEW")
	GetPVItem(oObjeto:SalesOrder,"SC6","SC5")
	oObjeto:SalesOrder:QuantityDelivered := (cAliasSD2)->D2_QUANT
EndIf

If !Empty((cAliasSD2)->D2_LOTECTL) .Or. !Empty((cAliasSD2)->D2_NUMLOTE)
	oObjeto:LotIdentifier := WsClassNew( "LOTVIEW" )
	oObjeto:LotIdentifier:LotNumber    := (cAliasSD2)->D2_LOTECTL+(cAliasSD2)->D2_NUMLOTE
	oObjeto:LotIdentifier:PotencyLot   := (cAliasSD2)->D2_POTENCI 
	oObjeto:LotIdentifier:ValidityDate := (cAliasSD2)->D2_DTVALID
EndIf
UserFields("SD2",@oObjeto:UserFields,cAliasSD2)
RestArea(aArea)

Return(.T.)


/*/


Ŀ
Funo    WSMAT_NF   Autor Eduardo Riera           Data 22.08.2003  
Ĵ
Descrio  Web Service responsavel pelos documentos de entrada/saida    
                                                                        
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        CRM/Materiais/Portais                                        
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service                                                
//
WSSERVICE MtSupplierInvoice     DESCRIPTION STR0007 NAMESPACE "http://webservices.microsiga.com.br/mtsupplierinvoice.apw" //"Servio de consulta e atualizao dos documentos de entrada e sada ( <b>Restrio de fornecedor<b> )"
WSDATA HeaderType               AS String
WSDATA Header                   AS Array Of BrwHeader
WSDATA UserCode                 AS String
WSDATA RegisterDateFrom         As Date OPTIONAL
WSDATA RegisterDateTo           As Date OPTIONAL
WSDATA DeliveryDateFrom         As Date OPTIONAL
WSDATA DeliveryDateTo           As Date OPTIONAL
WSDATA PurchaseNumber           As String OPTIONAL
WSDATA CustomerOrSupplier       As Integer
WSDATA CustomerOrSupplierID     As String
WSDATA CustomerOrSupplierType   As String
WSDATA SerialNumber             As String
WSDATA InvoiceNumber            As String
WSDATA InvoiceType              As String
WSDATA DocumentType             As String OPTIONAL
WSDATA InvoiceHeader            As Array Of InvoiceHeaderView
WSDATA Invoice                  As InvoiceView
WSDATA WsNull                   As String
WSDATA QueryAddWhereNfe         As String OPTIONAL
WSDATA QueryAddWhereNfs         As String OPTIONAL
WSDATA IndexKeyNfe              As String OPTIONAL
WSDATA IndexKeyNfs              As String OPTIONAL
WSDATA OperationType            As String OPTIONAL

WSMETHOD GetHeader           DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
WSMETHOD BrwInvoice          DESCRIPTION STR0003 //"Mtodo de listagem dos documentos de entrada ou saida"
WSMETHOD GetInvoice          DESCRIPTION STR0004 //"Mtodo de consulta as informaes do documento de entrada ou saida"
WSMETHOD PutInvoice          DESCRIPTION STR0008 //"Mtodo de atualizao do documento de entrada"
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de recuperacao do header                              
                                                                       
Ĵ
ParametrosExpC1: Nome da Estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtSupplierInvoice

::Header := MtHeader(::HeaderType)
If Empty(::Header)
	::Header := FinHeader(::HeaderType)
EndIf

Return(.T.)

/*/

Ŀ
Funo    BrwPurchasAutor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de recuperacao dos documentos de entrada e saida      
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Fornecedor                                            
          ExpD3: Data Inicial                                          
          ExpD4: Data Final                                            
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve as cotacaoes em aberto do fornecedor     
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD BrwInvoice WSRECEIVE UserCode,CustomerOrSupplier,CustomerOrSupplierID,RegisterDateFrom,RegisterDateTo,DeliveryDateFrom,DeliveryDateTo,QueryAddWhereNfe,QueryAddWhereNfs,IndexKeyNfe,IndexKeyNfs,PurchaseNumber,OperationType WSSEND InvoiceHeader WSSERVICE MtSupplierInvoice

Local aArea    := GetArea()
Local lRetorno := .T.
Local lQuery   := .F.
Local lValido  := .F.
Local nX       := 0
Local cArquivo := ""
Local cQuery   := ""
Local cAliasSF1:= "SF1"
Local cAliasSF2:= "SF2"
Local cFornece := SubStr(::CustomerOrSupplierID,1,Len(SA2->A2_COD))
Local cLojaFor := SubStr(::CustomerOrSupplierID,Len(SA2->A2_COD)+1)
Local cCliente := SubStr(::CustomerOrSupplierID,1,Len(SA1->A1_COD))
Local cLojaCli := SubStr(::CustomerOrSupplierID,Len(SA1->A1_COD)+1)
Local dEntrini := ::DeliveryDateFrom
Local dEntrFim := ::DeliveryDateTo
Local dEmisini := ::RegisterDateFrom
Local dEmisFim := ::RegisterDateTo
Local cAlias   := IIf(::CustomerOrSupplier==1,"SA1","SA2")
Local aCampos  := {}
Local aStruSF1 := {}
Local aStruSF2 := {}
Local cVolume  := ""
Local nY       := 0
DEFAULT dEntrIni := dDataBase-30
DEFAULT dEntrFim := dDataBase
DEFAULT dEmisIni := dDataBase-30
DEFAULT dEmisFim := dDataBase

If PrtChkUser(::UserCode,"MtSupplierInvoice","BrwInvoice",cAlias,::CustomerOrSupplierID)
	//Ŀ
	//Pesquisa os documentos de entrada                                       
	//
	If Empty(::OperationType) .Or. ::OperationType=="E"
		dbSelectArea("SF1")
		dbSetOrder(2)
		lQuery := .T.
		cAliasSF1 := "BRWINVOICE"
		aStruSF1  := SF1->(dbStruct())

		cQuery := "SELECT F1_FILIAL,F1_TIPO,F1_FORNECE,F1_LOJA,F1_DOC,F1_SERIE,F1_FORMUL, "
		cQuery += "F1_DTDIGIT,F1_EMISSAO,F1_VALBRUT,F1_DESPESA,F1_SEGURO,F1_FRETE,F1_STATUS, "
		aCampos := MaFisRefLd("SF1","NF")
		For nY := 1 To Len(aCampos)
			If !Empty(aCampos[nY][2])
				cQuery += aCampos[nY][2]+","
			EndIf
			If !Empty(aCampos[nY][3])
				cQuery += aCampos[nY][3]+","
			EndIf
			If !Empty(aCampos[nY][4])
				cQuery += aCampos[nY][4]+","
			EndIf
		Next nY
		cQuery += "F1_PREFIXO,F1_DUPL "
		cQuery += GetUserField("SF1")
		cQuery += "FROM "+RetSqlName("SF1")+" SF1 "
		If !Empty(::PurchaseNumber)
			cQuery += ","+RetSqlName("SD1")+" SD1 "
		EndIf
		cQuery += "WHERE SF1.F1_FILIAL='"+xFilial("SF1")+"' AND "
		If ::CustomerOrSupplier==1
			cQuery += "SF1.F1_FORNECE='"+cCliente+"' AND "
			cQuery += "SF1.F1_LOJA='"+cLojaCli+"' AND "
			cQuery += "SF1.F1_TIPO IN('D','B') AND "
		Else
			cQuery += "SF1.F1_FORNECE='"+cFornece+"' AND "
			cQuery += "SF1.F1_LOJA='"+cLojaFor+"' AND "
			cQuery += "SF1.F1_TIPO NOT IN('D','B') AND "
		EndIf
		cQuery += "SF1.F1_EMISSAO >= '"+Dtos(dEmisIni)+"' AND "
		cQuery += "SF1.F1_EMISSAO <= '"+Dtos(dEmisFim)+"' AND "
		cQuery += "SF1.F1_DTDIGIT >= '"+Dtos(dEntrIni)+"' AND "
		cQuery += "SF1.F1_DTDIGIT <= '"+Dtos(dEntrFim)+"' AND "
		cQuery += "NOT ("+IsRemito(2,"SF1.F1_TIPODOC")+") AND "
		cQuery += "SF1.D_E_L_E_T_=' ' "
		If !Empty(::PurchaseNumber)
			cQuery += " AND "
			cQuery += "SD1.D1_FILIAL = '"+xFilial("SD1")+"' AND "
			cQuery += "SD1.D1_DOC = SF1.F1_DOC AND "
			cQuery += "SD1.D1_SERIE = SF1.F1_SERIE AND "
			cQuery += "SD1.D1_FORNECE = SF1.F1_FORNECE AND "
			cQuery += "SD1.D1_LOJA = SF1.F1_LOJA AND "
			cQuery += "SD1.D1_TIPO = SF1.F1_TIPO AND "
			cQuery += "SD1.D1_PEDIDO = '"+::PurchaseNumber+"' AND "
			cQuery += "SD1.D_E_L_E_T_=' ' "
		EndIf
		cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfe)
		cQuery += "ORDER BY "+WsSqlOrder(IIf(Empty(::IndexKeyNfe),SF1->(IndexKey()),::IndexKeyNfe))

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF1)

		For nY := 1 To Len(aStruSF1)
			If aStruSF1[nY][2] <> "C" .And. aStruSF1[nY][2] <> "M"
				TcSetField(cAliasSF1,aStruSF1[nY][1],aStruSF1[nY][2],aStruSF1[nY][3],aStruSF1[nY][4])
			EndIf
		Next nY

		nX := 0
		DEFAULT ::InvoiceHeader := {}
		While !Eof() .And. xFilial("SF1") == (cAliasSF1)->F1_FILIAL .And.;
				cFornece+cLojaFor == (cAliasSF1)->F1_FORNECE+(cAliasSF1)->F1_LOJA

			If IIf(::CustomerOrSupplier==1,(cAliasSF1)->F1_TIPO $ 'DB',!(cAliasSF1)->F1_TIPO $ 'DB') .And.;
					(cAliasSF1)->F1_EMISSAO >= dEmisIni .And.;
					(cAliasSF1)->F1_EMISSAO <= dEmisFim .And.;
					(cAliasSF1)->F1_DTDIGIT >= dEntrIni .And.;
					(cAliasSF1)->F1_DTDIGIT <= dEntrFim	

				If lQuery
					lValido := .T.
				Else
					If Empty(::PurchaseNumber)
						lValido := .T.
					Else
						lValido := .F.
						dbSelectArea("SD1")
						dbSetOrder(1)
						MsSeek(xFilial("SD1")+(cAliasSF1)->F1_DOC+(cAliasSF1)->F1_SERIE+(cAliasSF1)->F1_FORNECE+(cAliasSF1)->F1_LOJA)
						While !Eof() .And.;
							xFilial("SD1") == SD1->D1_FILIAL .And.;
							(cAliasSF1)->F1_DOC == SD1->D1_DOC .And.;
							(cAliasSF1)->F1_SERIE == SD1->D1_SERIE .And.;
							(cAliasSF1)->F1_FORNECE == SD1->D1_FORNECE .And.;
							(cAliasSF1)->F1_LOJA == SD1->D1_LOJA

							If SD1->D1_TIPO == (cAliasSF1)->F1_TIPO .And.;
								SD1->D1_FORMUL == (cAliasSF1)->F1_FORMUL .And.;
								SD1->D1_PEDIDO == ::PurchaseNumber

								lValido := .T.
					 			Exit
							EndIf

							dbSelectArea("SD1")
							dbSkip()

						EndDo
					EndIf
				EndIf
				If lValido
					aadd(::InvoiceHeader,WsClassNew("InvoiceHeaderView"))
					nX++
					GetNFEHead(@::InvoiceHeader[nX],cAliasSF1)
				EndIf
			EndIf
			dbSelectArea(cAliasSF1)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSF1)
			dbCloseArea()
			dbSelectArea("SF1")
		Else
			dbSelectArea("SF1")
			RetIndex("SF1")
			FErase(cArquivo+OrdBagExt())
		EndIf
	EndIf
	//Ŀ
	//Pesquisa os documentos de saida                                         
	//
	If Empty(::OperationType) .Or. ::OperationType=="S"
		dbSelectArea("SF2")
		dbSetOrder(2)
		lQuery := .T.
		cAliasSF2 := "BRWINVOICE"
		aStruSF2  := SF2->(dbStruct())

		cQuery := "SELECT F2_FILIAL,F2_TIPO,F2_CLIENTE,F2_LOJA,F2_DOC,F2_SERIE, "
		cQuery += "F2_EMISSAO,F2_VALBRUT,F2_DESPESA,F2_SEGURO,F2_FRETE,F2_TRANSP, "
		aCampos := MaFisRefLd("SF2","NF")
		For nY := 1 To Len(aCampos)
			If !Empty(aCampos[nY][2])
				cQuery += aCampos[nY][2]+","
			EndIf
			If !Empty(aCampos[nY][3])
				cQuery += aCampos[nY][3]+","
			EndIf
			If !Empty(aCampos[nY][4])
				cQuery += aCampos[nY][4]+","
			EndIf
		Next nY
		cVolume := "1"
		While SF2->(FieldPos("F2_ESPECI"+cVolume))<>0 .And. !Empty(SF2->(FieldGet(FieldPos("F2_ESPECI"+cVolume))))
			cQuery += "F2_ESPECI"+cVolume+","+"F2_ESPECI"+cVolume+","
			cVolume := Soma1( cVolume )
		EndDo
		cQuery += "F2_PREFIXO,F2_DUPL "
		cQuery += GetUserField("SF2")
		cQuery += "FROM "+RetSqlName("SF2")+" SF2 "
		If !Empty(::PurchaseNumber)
			cQuery += ","+RetSqlName("SD2")+" SD2 "
		EndIf
		cQuery += "WHERE SF2.F2_FILIAL='"+xFilial("SF2")+"' AND "
		If ::CustomerOrSupplier==1
			cQuery += "SF2.F2_CLIENTE='"+cCliente+"' AND "
			cQuery += "SF2.F2_LOJA='"+cLojaCli+"' AND "
			cQuery += "SF2.F2_TIPO NOT IN('D','B') AND "
		Else
			cQuery += "SF2.F2_CLIENTE='"+cFornece+"' AND "
			cQuery += "SF2.F2_LOJA='"+cLojaFor+"' AND "
			cQuery += "SF2.F2_TIPO IN('D','B') AND "
		EndIf
		cQuery += "NOT ("+IsRemito(2,"SF2.F2_TIPODOC")+") AND "
		cQuery += "SF2.F2_EMISSAO >= '"+Dtos(dEmisIni)+"' AND "
		cQuery += "SF2.F2_EMISSAO <= '"+Dtos(dEmisFim)+"' AND "
		cQuery += "SF2.F2_EMISSAO >= '"+Dtos(dEntrIni)+"' AND "
		cQuery += "SF2.F2_EMISSAO <= '"+Dtos(dEntrFim)+"' AND "
		cQuery += "SF2.D_E_L_E_T_=' ' "
		If !Empty(::PurchaseNumber)
			cQuery += " AND "
			cQuery += "SD2.D2_FILIAL = '"+xFilial("SD2")+"' AND "
			cQuery += "SD2.D2_DOC = SF2.F2_DOC AND "
			cQuery += "SD2.D2_SERIE = SF2.F2_SERIE AND "
			cQuery += "SD2.D2_CLIENTE = SF2.F2_CLIENTE AND "
			cQuery += "SD2.D2_LOJA = SF2.F2_LOJA AND "
			cQuery += "SD2.D2_PEDIDO = '"+::PurchaseNumber+"' AND "
			cQuery += "SD2.D_E_L_E_T_=' ' "
		EndIf
		cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfs)
		cQuery += "ORDER BY "+WsSqlOrder(IIf(Empty(::IndexKeyNfs),SF2->(IndexKey()),::IndexKeyNfs))

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF2)

		For nY := 1 To Len(aStruSF2)
			If aStruSF2[nY][2] <> "C" .And. aStruSF2[nY][2] <> "M"
				TcSetField(cAliasSF2,aStruSF2[nY][1],aStruSF2[nY][2],aStruSF2[nY][3],aStruSF2[nY][4])
			EndIf
		Next nY

		DEFAULT ::InvoiceHeader := {}	
		While !Eof() .And. xFilial("SF2") == (cAliasSF2)->F2_FILIAL .And.;
				cCliente+cLojaCli == (cAliasSF2)->F2_CLIENTE+(cAliasSF2)->F2_LOJA

			If IIf(::CustomerOrSupplier==1,!(cAliasSF2)->F2_TIPO $ 'DB',(cAliasSF2)->F2_TIPO $ 'DB') .And.;
					(cAliasSF2)->F2_EMISSAO >= dEmisIni .And.;
					(cAliasSF2)->F2_EMISSAO <= dEmisFim .And.;
					(cAliasSF2)->F2_EMISSAO >= dEntrIni .And.;
					(cAliasSF2)->F2_EMISSAO <= dEntrFim
				If lQuery
					lValido := .T.
				Else
					If Empty(::PurchaseNumber)
						lValido := .T.
					Else
						dbSelectArea("SD2")
						dbSetOrder(1)
						MsSeek(xFilial("SD2")+(cAliasSF2)->F2_DOC+(cAliasSF2)->F2_SERIE+(cAliasSF2)->F2_CLIENTE+(cAliasSF2)->F2_LOJA)
						While !Eof() .And.;
							xFilial("SD2") == (cAliasSD2)->D2_FILIAL .And.;
							(cAliasSF2)->F2_DOC == (cAliasSD2)->D2_DOC .And.;
							(cAliasSF2)->F2_SERIE == (cAliasSD2)->D2_SERIE .And.;
							(cAliasSF2)->F2_CLIENTE == (cAliasSD2)->D2_CLIENTE .And.;
							(cAliasSF2)->F2_LOJA == (cAliasSD2)->D2_LOJA

							If (cAliasSD2)->D2_PEDIDO == ::PurchaseNumber
								lValido := .T.
								Exit
							EndIf
							dbSelectArea(cAliasSD2)
							dbSkip()
						EndDo
					EndIf
				EndIf
				If lValido
					aadd(::InvoiceHeader,WsClassNew("InvoiceHeaderView"))
					nX++
					GetNFSHead(@::InvoiceHeader[nX],cAliasSF2)
				EndIf
			EndIf
			dbSelectArea(cAliasSF2)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSF2)
			dbCloseArea()
			dbSelectArea("SF2")
		Else
			dbSelectArea("SF2")
			RetIndex("SF2")
			FErase(cArquivo+OrdBagExt())
		EndIf
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetInvoiceAutor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de recuperacao dos documento de entrada / saida       
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Tipo do documento de entrada/saida                    
          ExpC3: Numero de Serie                                       
          ExpC4: Numero do documento                                   
          ExpC5: Formulario Proprio?                                   
          ExpN6: 1-Cliente ou 2-Fornecedor                             
          ExpC6: Codigo do cliente ou fornecedor                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve os documentos de entrad ou saida         
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetInvoice WSRECEIVE UserCode,InvoiceType,SerialNumber,InvoiceNumber,DocumentType,CustomerOrSupplier,CustomerOrSupplierID,QueryAddWhereNfe,QueryAddWhereNfs WSSEND Invoice WSSERVICE MtSupplierInvoice

Local aArea    := GetArea()

Local cAlias   := IIf(::CustomerOrSupplier==1,"SA1","SA2")
Local cES      := SubStr(::InvoiceType,1,1)
Local cFornece := SubStr(::CustomerOrSupplierID,1,Len(SA2->A2_COD))
Local cLojaFor := SubStr(::CustomerOrSupplierID,Len(SA2->A2_COD)+1)
Local cCliente := SubStr(::CustomerOrSupplierID,1,Len(SA1->A1_COD))
Local cLojaCli := SubStr(::CustomerOrSupplierID,Len(SA1->A1_COD)+1)
Local cSerie   := wsmat_nfSerie(::SerialNumber)
Local cDoc     := SubStr(::InvoiceNumber,1,Len(SD1->D1_DOC))
Local cTipo    := AllTrim(SubStr(::InvoiceType,3))
Local cFormul  := ::DocumentType
Local cAliasSD1:= "SD1"
Local cAliasSD2:= "SD2"
Local nX       := 0
Local lQuery   := .F.
Local lRetorno := .F.
Local aStruSD1 := {}
Local aStruSD2 := {}
Local cQuery   := ""
Local nY       := 0

DEFAULT cFormul := " "
If cPaisLoc=="BRA"
	cFormul  := IIf(cFormul=="S","S"," ")
Else                                            
	cFormul  := IIf(cFormul=="S","S","N")
EndIf	       

If PrtChkUser(::UserCode,"MtSupplierInvoice","GetInvoice",cAlias,::CustomerOrSupplierID)
	//Ŀ
	//Verifica se qual o tipo de documento                                    
	//
	If cES == "E"
		dbSelectArea("SF1")
		dbSetOrder(1)
		MsSeek(xFilial("SF1")+cDoc+cSerie+cFornece+cLojaFor+cTipo)
		While !Eof() .And. xFilial("SF1") == SF1->F1_FILIAL .And.;
			cDoc == SF1->F1_DOC .And.;
			cSerie == SF1->F1_SERIE .And.;
			cFornece == SF1->F1_FORNECE .And.;
			cLojaFor == SF1->F1_LOJA .And.;
			cTipo == SF1->F1_TIPO 
			If SF1->F1_FORMUL == cFormul
				lRetorno := .T.
				Exit
			EndIf
			dbSelectArea("SF1")
			dbSkip()
		EndDo
		If lRetorno
			::Invoice:InvoiceHeader := WsClassNew("InvoiceHeaderView")
			GetNfeHead(@::Invoice:InvoiceHeader,"SF1")
			//Ŀ
			//Pesquisa os Itens do documento de entrada                               
			//
			::Invoice:InvoiceItem:= {}
			dbSelectArea("SD1")
			dbSetOrder(1)

			aStruSD1 := SD1->(dbStruct())
			lQuery   := .T.
			cAliasSD1:= "GETINVOICE"

			cQuery := "SELECT * "
			cQuery += "FROM "+RetSqlName("SD1")+" SD1 "
			cQuery += "WHERE SD1.D1_FILIAL='"+xFilial("SD1")+"' AND "
			cQuery += "SD1.D1_DOC='"+cDoc+"' AND "
			cQuery += "SD1.D1_SERIE='"+cSerie+"' AND "
			cQuery += "SD1.D1_FORNECE='"+cFornece+"' AND "
			cQuery += "SD1.D1_LOJA='"+cLojaFor+"' AND "	
			cQuery += "SD1.D1_TIPO='"+cTipo+"' AND "
			cQuery += "SD1.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfe)
			cQuery += "ORDER BY "+SqlOrder( "D1_FILIAL+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA+D1_ITEM+D1_COD" )

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1)

			For nY := 1 To Len(aStruSD1)
				If aStruSD1[nY][2] <> "C" .And. aStruSD1[nY][2] <> "M"
					TcSetField(cAliasSD1,aStruSD1[nY][1],aStruSD1[nY][2],aStruSD1[nY][3],aStruSD1[nY][4])
				EndIf
			Next nY

			nX := 0
			While !Eof() .And.;
				xFilial("SD1") == (cAliasSD1)->D1_FILIAL .And.;
				cDoc == (cAliasSD1)->D1_DOC .And.;
				cSerie == (cAliasSD1)->D1_SERIE .And.;
				cFornece == (cAliasSD1)->D1_FORNECE .And.;
				cLojaFor == (cAliasSD1)->D1_LOJA

				If (cAliasSD1)->D1_TIPO == cTipo .And. (cAliasSD1)->D1_FORMUL == cFormul
					If nX == 0
						::Invoice:InvoiceItem:= {}
					EndIf
					aadd(::Invoice:InvoiceItem,WsClassNew("InvoiceItemView"))
					nX++
					GetNFEItem(@::Invoice:InvoiceItem[nX],cAliasSD1)
				EndIf
				dbSelectArea(cAliasSD1)
				dbSkip()
			EndDo
			If lQuery
				dbSelectArea(cAliasSD1)
				dbCloseArea()
				dbSelectArea("SD1")
			EndIf
		EndIf
	Else
		dbSelectArea("SF2")
		dbSetOrder(1)
		If MsSeek(xFilial("SF2")+cDoc+cSerie+cCliente+cLojaCli)
			lRetorno := .T.
			::Invoice:InvoiceHeader := WsClassNew("InvoiceHeaderView")
			GetNfSHead(@::Invoice:InvoiceHeader,"SF2")
			//Ŀ
			//Pesquisa os Itens do documento de saida                                 
			//
			::Invoice:InvoiceItem:= {}
			dbSelectArea("SD2")
			dbSetOrder(3)

			aStruSD2 := SD2->(dbStruct())
			lQuery   := .T.
			cAliasSD2:= "GETINVOICE"

			cQuery := "SELECT * "
			cQuery += "FROM "+RetSqlName("SD2")+" SD2 "
			cQuery += "WHERE SD2.D2_FILIAL='"+xFilial("SD2")+"' AND "
			cQuery += "SD2.D2_DOC='"+cDoc+"' AND "
			cQuery += "SD2.D2_SERIE='"+cSerie+"' AND "
			cQuery += "SD2.D2_CLIENTE='"+cCliente+"' AND "
			cQuery += "SD2.D2_LOJA='"+cLojaCli+"' AND "	
			cQuery += "SD2.D2_TIPO='"+cTipo+"' AND "
			cQuery += "SD2.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfs)
			cQuery += "ORDER BY "+SqlOrder(SD2->(IndexKey()))

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD2)

			For nY := 1 To Len(aStruSD2)
				If aStruSD2[nY][2] <> "C" .And. aStruSD2[nY][2] <> "M"
					TcSetField(cAliasSD2,aStruSD2[nY][1],aStruSD2[nY][2],aStruSD2[nY][3],aStruSD2[nY][4])
				EndIf
			Next nY

			nX := 0
			While !Eof() .And.;
				xFilial("SD2") == (cAliasSD2)->D2_FILIAL .And.;
				cDoc == (cAliasSD2)->D2_DOC .And.;
				cSerie == (cAliasSD2)->D2_SERIE .And.;
				cFornece == (cAliasSD2)->D2_CLIENTE .And.;
				cLojaFor == (cAliasSD2)->D2_LOJA

				If (cAliasSD2)->D2_TIPO == cTipo 
					If nX == 0
						::Invoice:InvoiceItem:= {}
					EndIf
					aadd(::Invoice:InvoiceItem,WsClassNew("InvoiceItemView"))
					nX++
					GetNFSItem(@::Invoice:InvoiceItem[nX],cAliasSD2)
				EndIf
				dbSelectArea(cAliasSD2)
				dbSkip()
			EndDo
			If lQuery
				dbSelectArea(cAliasSD2)
				dbCloseArea()
				dbSelectArea("SD2")
			EndIf
		EndIf
	EndIf
	If !lRetorno
		SetSoapFault("GETINVOICE",STR0005) //"Documento nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    PutInvoiceAutor   Eduardo Riera          Data 03.09.2003 
Ĵ
          Rotina de atualizacao do documento de entrada                
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Numero de Serie                                       
          ExpC3: Numero do documento                                   
          ExpC4: Formulario Proprio?                                   
          ExpN5: 1-Cliente ou 2-Fornecedor                             
          ExpC6: Codigo do cliente ou fornecedor                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo atualiza o documento de entrada                  
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD PutInvoice WSRECEIVE UserCode,CustomerOrSupplier,CustomerOrSupplierID,Invoice WSSEND WsNull WSSERVICE MtSupplierInvoice

Local aArea     := GetArea()
Local aStruSD1  := {}
Local aCabPE    := {}
Local aCab      := {}
Local aLinha    := {}
Local aItens    := {}
Local aImpostos := {}
Local aNoDeleted:= {}
Local aErro     := {}
Local cErro     := ""
Local cAlias    := IIf(::CustomerOrSupplier==1,"SA1","SA2")
Local cAliasSD1 := ""
Local cQuery    := ""
Local nX        := 0
Local nY        := 0
Local nOpc      := 0
Local lRetorno  := .T.
Local lQuery    := .F.
Local dData     := dDataBase

PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.
DEFAULT ::Invoice:InvoiceHeader:DocumentType := "N"
DEFAULT ::Invoice:InvoiceHeader:Taxes := {}

DEFAULT WSNULL := ""

If PrtChkUser(::UserCode,"MtSupplierInvoice","PutInvoice",cAlias,::CustomerOrSupplierID)
	//Ŀ
	//Analisa o cabecalho do documento de entrada                             
	//
	dDataBase := ::Invoice:InvoiceHeader:DeliveryDate
	aadd(aCab,{"F1_SERIE",Padr(::Invoice:InvoiceHeader:SerialNumber,Len(SF1->F1_SERIE)),Nil})
	aadd(aCab,{"F1_DOC",Padr(::Invoice:InvoiceHeader:InvoiceNumber,Len(SF1->F1_DOC)),Nil})
	aadd(aCab,{"F1_FORMUL",::Invoice:InvoiceHeader:DocumentType,Nil})
	aadd(aCab,{"F1_TIPO",::Invoice:InvoiceHeader:InvoiceType,Nil})
	aadd(aCab,{"F1_EMISSAO",::Invoice:InvoiceHeader:RegisterDate,Nil})
	aadd(aCab,{"F1_DESPESA",::Invoice:InvoiceHeader:ExpensesValue,Nil})
	aadd(aCab,{"F1_SEGURO",::Invoice:InvoiceHeader:InsuranceValue,Nil})
	aadd(aCab,{"F1_FRETE",::Invoice:InvoiceHeader:FreightValue,Nil})
	aadd(aCab,{"F1_VALBRUT",::Invoice:InvoiceHeader:TotalValue,Nil})
	aadd(aCab,{"F1_FORNECE",SubStr(::Invoice:InvoiceHeader:FromRole:Code,1,Len(SF1->F1_FORNECE)),Nil})
	aadd(aCab,{"F1_LOJA",SubStr(::Invoice:InvoiceHeader:FromRole:Code,Len(SF1->F1_FORNECE)+1),Nil})

	aImpostos := MaFisRefLd("SF1","NF")
	For nY := 1 To Len(::Invoice:InvoiceHeader:Taxes)
		nX := aScan(aImpostos,{|x| x[1] == ::Invoice:InvoiceHeader:Taxes[nY]:TaxCode})
		If nX <> 0
			If !Empty(aImpostos[nX][2])
				aadd(aCab,{aImpostos[nX][2],::Invoice:InvoiceHeader:Taxes[nY]:TaxCode,Nil})
			EndIf
			If !Empty(aImpostos[nX][3])
				aadd(aCab,{aImpostos[nX][3],::Invoice:InvoiceHeader:Taxes[nY]:TaxRate,Nil})
			EndIf
			If !Empty(aImpostos[nX][4])
				aadd(aCab,{aImpostos[nX][4],::Invoice:InvoiceHeader:Taxes[nY]:TaxAmount,Nil})
			EndIf
		EndIf
	Next nY
	PutUserFields("SF1",::Invoice:InvoiceHeader:UserFields,aCab)
	//Ŀ
	//Analisa os itens do documento de entrada                                
	//
	For nX := 1 To Len(::Invoice:InvoiceItem)
		aadd(aNoDeleted,::Invoice:InvoiceItem[nX]:SequentialID+::Invoice:InvoiceItem[nX]:ProductCode)
		aLinha := {}
		aadd(aLinha,{"D1_ITEM",::Invoice:InvoiceItem[nX]:SequentialID,Nil})
		aadd(aLinha,{"D1_COD",::Invoice:InvoiceItem[nX]:ProductCode,Nil})
		If ::Invoice:InvoiceItem[nX]:MeasureUnit != NIL
			aadd(aLinha,{"D1_UM",::Invoice:InvoiceItem[nX]:MeasureUnit,Nil})
		Else
			aadd(aLinha,{"D1_UM",GetAdvFVal("SB1","B1_UM",xFilial("SB1")+::Invoice:InvoiceItem[nX]:ProductCode,1),Nil})
		Endif
		aadd(aLinha,{"D1_QUANT",::Invoice:InvoiceItem[nX]:Quantity,Nil})
		aadd(aLinha,{"D1_VUNIT",::Invoice:InvoiceItem[nX]:UnitPrice,Nil})
		aadd(aLinha,{"D1_TOTAL",::Invoice:InvoiceItem[nX]:TotalValue,Nil})
		If ::Invoice:InvoiceItem[nX]:DiscountPercent != Nil
			aadd(aLinha,{"D1_DESC",::Invoice:InvoiceItem[nX]:DiscountPercent,Nil})
 		EndIf
		aadd(aLinha,{"D1_VALDESC",::Invoice:InvoiceItem[nX]:DiscountValue,Nil})
		aadd(aLinha,{"D1_DESPESA",::Invoice:InvoiceItem[nX]:ExpensesValue,Nil})
		aadd(aLinha,{"D1_SEGURO",::Invoice:InvoiceItem[nX]:InsuranceValue,Nil})
		aadd(aLinha,{"D1_VALFRE",::Invoice:InvoiceItem[nX]:FreightValue,Nil})
		If ::Invoice:InvoiceItem[nX]:PurchaseOrder != Nil
			aadd(aLinha,{"D1_PEDIDO",::Invoice:InvoiceItem[nX]:PurchaseOrder:PurchaseOrderID,Nil})
			aadd(aLinha,{"D1_ITEMPC",::Invoice:InvoiceItem[nX]:PurchaseOrder:SequentialID,Nil})
		EndIf
		If ::Invoice:InvoiceItem[nX]:LotIdentifier != Nil
			aadd(aLinha,{"D1_LOTECTL",SubStr(::Invoice:InvoiceItem[nX]:LotIdentifier:LotNumber,1,Len(SD1->D1_LOTECTL)),Nil})
			aadd(aLinha,{"D1_NUMLOTE",SubStr(::Invoice:InvoiceItem[nX]:LotIdentifier:LotNumber,Len(SD1->D1_LOTECTL)+1),Nil})	
			aadd(aLinha,{"D1_POTENCI",::Invoice:InvoiceItem[nX]:LotIdentifier:PotencyLot,Nil})
			aadd(aLinha,{"D1_DTVALID",::Invoice:InvoiceItem[nX]:LotIdentifier:ValidityDate,Nil})
		EndIf
		aImpostos := MaFisRefLd("SF1","NF")
		For nY := 1 To Len(::Invoice:InvoiceItem[nX]:Taxes)
			nZ := aScan(aImpostos,{|x| x[1] == ::Invoice:InvoiceItem[nX]:Taxes[nY]:TaxCode})
			If nZ <> 0
				If !Empty(aImpostos[nZ][2])
					aadd(aLinha,{aImpostos[nZ][2],::Invoice:InvoiceItem[nX]:Taxes[nY]:TaxCode,Nil})
				EndIf
				If !Empty(aImpostos[nZ][3])
					aadd(aLinha,{aImpostos[nZ][3],::Invoice:InvoiceItem[nX]:Taxes[nY]:TaxRate,Nil})
				EndIf
				If !Empty(aImpostos[nZ][4])
					aadd(aLinha,{aImpostos[nZ][4],::Invoice:InvoiceItem[nX]:Taxes[nY]:TaxAmount,Nil})
				EndIf
			EndIf
		Next nY
		PutUserFields("SD1",::Invoice:InvoiceItem[nX]:UserFields,aLinha)
		aadd(aItens,aLinha)
	Next nX
	//Ŀ
	//Verifica se a operacao                                                  
	//
	dbSelectArea("SF1")
	dbSetOrder(1)
	If MsSeek(xFilial("SF1")+Padr(::Invoice:InvoiceHeader:InvoiceNumber, Len(SF1->F1_DOC))+PadR( ::Invoice:InvoiceHeader:SerialNumber, 3 )+::Invoice:InvoiceHeader:FromRole:Code+::Invoice:InvoiceHeader:InvoiceType)
		dbSelectArea("SD1")
		dbSetOrder(1)

		aStruSD1 := SD1->(dbStruct())
		lQuery   := .T.
		cAliasSD1:= GetNextAlias()

		cQuery := "SELECT * "
		cQuery += "FROM "+RetSqlName("SD1")+" SD1 "
		cQuery += "WHERE SD1.D1_FILIAL='"+xFilial("SD1")+"' AND "
		cQuery += "SD1.D1_DOC='"+::Invoice:InvoiceHeader:InvoiceNumber+"' AND "
		cQuery += "SD1.D1_SERIE='"+::Invoice:InvoiceHeader:SerialNumber+"' AND "
		cQuery += "SD1.D1_FORNECE='"+SubStr(::Invoice:InvoiceHeader:FromRole:Code,1,Len(SF1->F1_FORNECE))+"' AND "
		cQuery += "SD1.D1_LOJA='"+SubStr(::Invoice:InvoiceHeader:FromRole:Code,Len(SF1->F1_FORNECE)+1)+"' AND "	
		cQuery += "SD1.D1_TIPO='"+::Invoice:InvoiceHeader:InvoiceType+"' AND "
		cQuery += "SD1.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfe)
		cQuery += "ORDER BY "+SqlOrder( "D1_FILIAL+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA+D1_ITEM+D1_COD" )

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1)

		For nX := 1 To Len(aStruSD1)
			If aStruSD1[nX][2] <> "C" .And. aStruSD1[nX][2] <> "M"
				TcSetField(cAliasSD1,aStruSD1[nX][1],aStruSD1[nX][2],aStruSD1[nX][3],aStruSD1[nX][4])
			EndIf
		Next nX

		While !Eof() .And. ;
			xFilial("SD1") == (cAliasSD1)->D1_FILIAL .And.;
				Padr(::Invoice:InvoiceHeader:InvoiceNumber,Len(SF1->F1_DOC)) == (cAliasSD1)->D1_DOC .And.;
				Padr(::Invoice:InvoiceHeader:SerialNumber,3 ) == (cAliasSD1)->D1_SERIE .And.;
				SubStr(::Invoice:InvoiceHeader:FromRole:Code,1,Len(SF1->F1_FORNECE)) == (cAliasSD1)->D1_FORNECE .And.;
				SubStr(::Invoice:InvoiceHeader:FromRole:Code,Len(SF1->F1_FORNECE)+1) == (cAliasSD1)->D1_LOJA

				If (cAliasSD1)->D1_TIPO == ::Invoice:InvoiceHeader:InvoiceType .And. (cAliasSD1)->D1_FORMUL == " " // Formulario Proprio e gravado branco na base
					If aScan( aNoDeleted, AllTrim( ( cAliasSD1 )->D1_ITEM ) + AllTrim( ( cAliasSD1 )->D1_COD ) ) == 0
						aadd(aItens,{{"D1_ITEM",(cAliasSD1)->D1_ITEM},;
								{"D1_COD",(cAliasSD1)->D1_COD},;
								{"AUTDELETA","S"}})
					EndIf
				EndIf

				dbSelectArea(cAliasSD1)
				dbSkip()
			EndDo
			If lQuery
				dbSelectArea(cAliasSD1)
				dbCloseArea()
				dbSelectArea("SD1")
			EndIf
		nOpc := 4
	Else
		nOpc := 3
	EndIf
	//--- Ponto de Entrada: Para Incluir campos no cabec. da N.F. Entrada (SF1)
	If ExistBlock("WSMT140A")
		aCabPE := Execblock("WSMT140A",.F.,.F.,{aCab})
		aCab   := If(ValType(aCabPE)=="A",aCabPE,aCab)
	EndIf
	aCab := WsAutoOpc(aCab)
	aItens := WsAutoOpc(aItens)
	//Ŀ
	//Protege a ordem do SC7 para a chamada da Rotina Automatica
	//
	dbSelectArea("SC7")
	dbSetOrder(1)
	Mata140(aCab,aItens,nOpc)
	If lMsErroAuto
		aErro := GetAutoGRLog()
		For nX := 1 To Len(aErro)
			cErro += aErro[nX] + Chr(13)+Chr(10)
		Next nX
		SetSoapFault("PUTINVOICE",cErro)
		lRetorno := .F.
	EndIf
EndIf
dDataBase := dData
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    MTSimulationInvoiceAutor  Kleber D.Gomes Data 21.09.2005 
Ĵ
          WebService de simulacao do documento de entrada.             
                                                                       
Ĵ
Parametros                                                             
Ĵ
Retorno                                                                
Ĵ
Descrio Este metodo simula a atualizacao do documento de entrada     
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
//Ŀ
//Definicao do Web Service de Controle do Usuario                         
//
WSSERVICE MtSimulationSupplierInvoice DESCRIPTION STR0009 NAMESPACE "http://webservices.microsiga.com.br/mtsimulationsupplierinvoice.apw" //"Servio de simulao da nota fiscal de entrada. <br><br><i>Este servio pode ser utilizado para recuperar o preenchimento automtico de algumas informaes da nota fiscal de entrada. Salientamos que este servio executa as customizao presentes no sistema, possibilitando seu uso.</i>"
WSDATA UserCode                  AS String
WSDATA CustomerOrSupplier        As Integer
WSDATA CustomerOrSupplierID      As String
WSDATA OldInvoice                AS InvoiceView
WSDATA NewInvoice    	  	     AS InvoiceView
WSDATA QueryAddWhereNfe          As String OPTIONAL

WSMETHOD PutInvoice DESCRIPTION STR0010 //"Mtodo de simulao da atualizao da nota fiscal de entrada"

ENDWSSERVICE

/*/

Ŀ
Funo    PutInvoice   Autor  Kleber Dias Gomes   Data  23.09.2005 
Ĵ
          Metodo de simulacao de atualizacao de uma nota fiscal.       
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Dados da Nota Fiscal                                  
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo simula a atualizacao de uma nota fiscal.         
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD PutInvoice WSRECEIVE UserCode,CustomerOrSupplier,CustomerOrSupplierID,OldInvoice WSSEND NewInvoice WSSERVICE MtSimulationSupplierInvoice
                               
Local aArea     := GetArea()
Local aStruSD1  := {}
Local aCabPE    := {}
Local aCab      := {}
Local aLinha    := {}
Local aItens    := {}
Local aImpostos := {}
Local aNoDeleted:= {}
Local aErro     := {}
Local cErro     := ""
Local cAlias    := IIf(::CustomerOrSupplier==1,"SA1","SA2")
Local cAliasSD1 := ""
Local cQuery    := ""
Local nX        := 0
Local nY        := 0
Local nOpc      := 0
Local lRetorno  := .T.
Local lQuery    := .F.
Local dData     := dDataBase

PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.
DEFAULT ::OldInvoice:InvoiceHeader:DocumentType := "N"
DEFAULT ::OldInvoice:InvoiceHeader:Taxes := {}

If PrtChkUser(::UserCode,"MtSimulationSupplierInvoice","PutInvoice",cAlias,::CustomerOrSupplierID)
	//Ŀ
	//Analisa o cabecalho do documento de entrada                             
	//
	dDataBase := ::OldInvoice:InvoiceHeader:DeliveryDate
	aadd(aCab,{"F1_SERIE",Padr(::OldInvoice:InvoiceHeader:SerialNumber,Len(SF1->F1_SERIE)),Nil})
	aadd(aCab,{"F1_DOC",Padr(::OldInvoice:InvoiceHeader:InvoiceNumber,Len(SF1->F1_DOC)),Nil})
	aadd(aCab,{"F1_FORMUL",::OldInvoice:InvoiceHeader:DocumentType,Nil})
	aadd(aCab,{"F1_TIPO",::OldInvoice:InvoiceHeader:InvoiceType,Nil})
	aadd(aCab,{"F1_EMISSAO",::OldInvoice:InvoiceHeader:RegisterDate,Nil})
	aadd(aCab,{"F1_DESPESA",::OldInvoice:InvoiceHeader:ExpensesValue,Nil})
	aadd(aCab,{"F1_SEGURO",::OldInvoice:InvoiceHeader:InsuranceValue,Nil})
	aadd(aCab,{"F1_FRETE",::OldInvoice:InvoiceHeader:FreightValue,Nil})
	aadd(aCab,{"F1_VALBRUT",::OldInvoice:InvoiceHeader:TotalValue,Nil})
	aadd(aCab,{"F1_FORNECE",SubStr(::CustomerOrSupplierID,1,Len(SF1->F1_FORNECE)),Nil})
	aadd(aCab,{"F1_LOJA",SubStr(::CustomerOrSupplierID,Len(SF1->F1_FORNECE)+1),Nil})
	
	aImpostos := MaFisRefLd("SF1","NF")
	For nY := 1 To Len(::OldInvoice:InvoiceHeader:Taxes)
		nX := aScan(aImpostos,{|x| x[1] == ::OldInvoice:InvoiceHeader:Taxes[nY]:TaxCode})
		If nX <> 0
			If !Empty(aImpostos[nX][2])
				aadd(aCab,{aImpostos[nX][2],::OldInvoice:InvoiceHeader:Taxes[nY]:TaxCode,Nil})
			EndIf
			If !Empty(aImpostos[nX][3])
				aadd(aCab,{aImpostos[nX][3],::OldInvoice:InvoiceHeader:Taxes[nY]:TaxRate,Nil})
			EndIf
			If !Empty(aImpostos[nX][4])
				aadd(aCab,{aImpostos[nX][4],::OldInvoice:InvoiceHeader:Taxes[nY]:TaxAmount,Nil})
			EndIf
		EndIf
	Next nY
	PutUserFields("SF1",::OldInvoice:InvoiceHeader:UserFields,aCab)

	aCab := WsAutoOpc( aCab )
	//Ŀ
	//Analisa os itens do documento de entrada                                
	//
	For nX := 1 To Len(::OldInvoice:InvoiceItem)
		aadd(aNoDeleted,::OldInvoice:InvoiceItem[nX]:SequentialID+::OldInvoice:InvoiceItem[nX]:ProductCode)
		aLinha := {}
		aadd(aLinha,{"D1_ITEM",::OldInvoice:InvoiceItem[nX]:SequentialID,Nil})
		aadd(aLinha,{"D1_COD",::OldInvoice:InvoiceItem[nX]:ProductCode,Nil})
		aadd(aLinha,{"D1_UM",::OldInvoice:InvoiceItem[nX]:MeasureUnit,Nil})
		aadd(aLinha,{"D1_QUANT",::OldInvoice:InvoiceItem[nX]:Quantity,Nil})
		aadd(aLinha,{"D1_VUNIT",::OldInvoice:InvoiceItem[nX]:UnitPrice,Nil})

		If ::OldInvoice:InvoiceItem[nX]:DiscountPercent != Nil
			aadd(aLinha,{"D1_DESC",::OldInvoice:InvoiceItem[nX]:DiscountPercent,Nil})
		EndIf
		
		aadd(aLinha,{"D1_VALDESC",::OldInvoice:InvoiceItem[nX]:DiscountValue,Nil})
		aadd(aLinha,{"D1_DESPESA",::OldInvoice:InvoiceItem[nX]:ExpensesValue,Nil})
		aadd(aLinha,{"D1_SEGURO",::OldInvoice:InvoiceItem[nX]:InsuranceValue,Nil})
		aadd(aLinha,{"D1_VALFRE",::OldInvoice:InvoiceItem[nX]:FreightValue,Nil})

		If ::OldInvoice:InvoiceItem[nX]:PurchaseOrder != Nil
			aadd(aLinha,{"D1_PEDIDO",::OldInvoice:InvoiceItem[nX]:PurchaseOrder:PurchaseOrderID,Nil})
			aadd(aLinha,{"D1_ITEMPC",::OldInvoice:InvoiceItem[nX]:PurchaseOrder:SequentialID,Nil})
		Endif

		If ::OldInvoice:InvoiceItem[nX]:LotIdentifier != Nil
			aadd(aLinha,{"D1_LOTECTL",SubStr(::OldInvoice:InvoiceItem[nX]:LotIdentifier:LotNumber,1,Len(SD1->D1_LOTECTL)),Nil})
			aadd(aLinha,{"D1_NUMLOTE",SubStr(::OldInvoice:InvoiceItem[nX]:LotIdentifier:LotNumber,Len(SD1->D1_LOTECTL)+1),Nil})	
			aadd(aLinha,{"D1_POTENCI",::OldInvoice:InvoiceItem[nX]:LotIdentifier:PotencyLot,Nil})
			aadd(aLinha,{"D1_DTVALID",::OldInvoice:InvoiceItem[nX]:LotIdentifier:ValidityDate,Nil})
		EndIf
		aImpostos := MaFisRefLd("SF1","NF")
		For nY := 1 To Len(::OldInvoice:InvoiceItem[nX]:Taxes)
			nZ := aScan(aImpostos,{|x| x[1] == ::OldInvoice:InvoiceItem[nX]:Taxes[nY]:TaxCode})
			If nZ <> 0
				If !Empty(aImpostos[nZ][2])
					aadd(aLinha,{aImpostos[nZ][2],::OldInvoice:InvoiceItem[nX]:Taxes[nY]:TaxCode,Nil})
				EndIf
				If !Empty(aImpostos[nZ][3])
					aadd(aLinha,{aImpostos[nZ][3],::OldInvoice:InvoiceItem[nX]:Taxes[nY]:TaxRate,Nil})
				EndIf
				If !Empty(aImpostos[nZ][4])
					aadd(aLinha,{aImpostos[nZ][4],::OldInvoice:InvoiceItem[nX]:Taxes[nY]:TaxAmount,Nil})
				EndIf
			EndIf
		Next nY
		PutUserFields("SD1",::OldInvoice:InvoiceItem[nX]:UserFields,aLinha)
		aLinha := WsAutoOpc( aLinha )
		aadd(aItens,aLinha)
	Next nX
	//Ŀ
	//Verifica se a operacao                                                  
	//
	dbSelectArea("SF1")
	dbSetOrder(1)
	If MsSeek(xFilial("SF1")+PadR( ::OldInvoice:InvoiceHeader:InvoiceNumber, Len(SF1->F1_DOC))+PadR( ::OldInvoice:InvoiceHeader:SerialNumber, 3 )+::CustomerOrSupplierID+::OldInvoice:InvoiceHeader:InvoiceType)
		dbSelectArea("SD1")
		dbSetOrder(1)

		aStruSD1 := SD1->(dbStruct())
		lQuery   := .T.
		cAliasSD1:= GetNextAlias()

		cQuery := "SELECT * "
		cQuery += "FROM "+RetSqlName("SD1")+" SD1 "
		cQuery += "WHERE SD1.D1_FILIAL='"+xFilial("SD1")+"' AND "
		cQuery += "SD1.D1_DOC='"+::OldInvoice:InvoiceHeader:InvoiceNumber+"' AND "
		cQuery += "SD1.D1_SERIE='"+::OldInvoice:InvoiceHeader:SerialNumber+"' AND "
		cQuery += "SD1.D1_FORNECE='"+SubStr(::OldInvoice:InvoiceHeader:FromRole:Code,1,Len(SF1->F1_FORNECE))+"' AND "
		cQuery += "SD1.D1_LOJA='"+SubStr(::OldInvoice:InvoiceHeader:FromRole:Code,Len(SF1->F1_FORNECE)+1)+"' AND "	
		cQuery += "SD1.D1_TIPO='"+::OldInvoice:InvoiceHeader:InvoiceType+"' AND "
		cQuery += "SD1.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfe)
		cQuery += "ORDER BY "+SqlOrder( "D1_FILIAL+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA+D1_ITEM+D1_COD" )

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1)

		For nX := 1 To Len(aStruSD1)
			If aStruSD1[nX][2] <> "C" .And. aStruSD1[nX][2] <> "M"
				TcSetField(cAliasSD1,aStruSD1[nX][1],aStruSD1[nX][2],aStruSD1[nX][3],aStruSD1[nX][4])
			EndIf
		Next nX

		While !Eof() .And. ;
			xFilial("SD1") == (cAliasSD1)->D1_FILIAL .And.;
				PadR( ::OldInvoice:InvoiceHeader:InvoiceNumber, Len(SF1->F1_DOC)) == (cAliasSD1)->D1_DOC .And.;
				PadR( ::OldInvoice:InvoiceHeader:SerialNumber, 3 ) == (cAliasSD1)->D1_SERIE .And.;
				SubStr(::OldInvoice:InvoiceHeader:FromRole:Code,1,Len(SF1->F1_FORNECE)) == (cAliasSD1)->D1_FORNECE .And.;
				SubStr(::OldInvoice:InvoiceHeader:FromRole:Code,Len(SF1->F1_FORNECE)+1) == (cAliasSD1)->D1_LOJA

				If aScan( aNoDeleted, AllTrim( ( cAliasSD1 )->D1_ITEM ) + AllTrim( ( cAliasSD1 )->D1_COD ) ) == 0
					AAdd(aItens,{ {"D1_ITEM",(cAliasSD1)->D1_ITEM},{"D1_COD",(cAliasSD1)->D1_COD},{"AUTDELETA","S"} })
				EndIf

				dbSelectArea(cAliasSD1)
				dbSkip()
			EndDo
			If lQuery
				dbSelectArea(cAliasSD1)
				dbCloseArea()
				dbSelectArea("SD1")
			EndIf

		nOpc := 4
	Else
		nOpc := 3
	EndIf
	//--- Ponto de Entrada: Para Incluir campos no cabec. da N.F. Entrada (SF1)
	If ExistBlock("WSMT140A")
		aCabPE := Execblock("WSMT140A",.F.,.F.,{aCab})
		aCab   := If(ValType(aCabPE)=="A",aCabPE,aCab)
	EndIf
	aCab := WsAutoOpc(aCab)
	aItens := WsAutoOpc(aItens)
	//Ŀ
	//Protege a ordem do SC7 para a chamada da Rotina Automatica
	//
	dbSelectArea("SC7")
	dbSetOrder(1)
	Mata140(@aCab,@aItens,nOpc,.T.)

	If lMsErroAuto
		aErro := GetAutoGRLog()
		For nX := 1 To Len(aErro)
			cErro += aErro[nX] + Chr(13)+Chr(10)
		Next nX
		SetSoapFault("PUTINVOICE",cErro)
		lRetorno := .F.
	EndIf
	If lRetorno
		aCab := WsAutoOpc(@aCab,.T.)
		For nX := 1 To Len(aItens)
			aItens[nX] := WsAutoOpc(@aItens[nX],.T.)
		Next nX
		GetNFEHead(::NewInvoice:InvoiceHeader,"SF1",aCab)
		For nX := 1 To Len(aItens)
			AAdd(::NewInvoice:InvoiceItem,WSClassNew("InvoiceItemView"))
			GetNFEItem(::NewInvoice:InvoiceItem[nX],"SD1",aItens[nX])
		Next nX
	Endif
EndIf
dDataBase := dData
RestArea(aArea)
Return(lRetorno)

/*/


Ŀ
Funo    WSMAT_NF   Autor Eduardo Riera           Data 27.11.2006  
Ĵ
Descrio  Web Service responsavel pelos documentos de entrada/saida    
                                                                        
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        CRM/Materiais/Portais                                        
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service                                                
//
WSSERVICE MtInvoice     DESCRIPTION STR0011 NAMESPACE "http://webservices.microsiga.com.br/mtinvoice.apw" //"Servio de consulta dos documentos de entrada e sada"
WSDATA HeaderType               AS String
WSDATA Header                   AS Array Of BrwHeader
WSDATA UserCode                 AS String
WSDATA RegisterDateFrom         As Date OPTIONAL
WSDATA RegisterDateTo           As Date OPTIONAL
WSDATA DeliveryDateFrom         As Date OPTIONAL
WSDATA DeliveryDateTo           As Date OPTIONAL
WSDATA PurchaseNumber           As String OPTIONAL
WSDATA CustomerOrSupplier       As Integer
WSDATA CustomerOrSupplierID     As String
WSDATA CustomerOrSupplierType   As String
WSDATA SerialNumber             As String
WSDATA InvoiceNumber            As String
WSDATA InvoiceType              As String
WSDATA DocumentType             As String OPTIONAL
WSDATA InvoiceHeader            As Array Of InvoiceHeaderView
WSDATA Invoice                  As InvoiceView
WSDATA WsNull                   As String
WSDATA QueryAddWhereNfe         As String OPTIONAL
WSDATA QueryAddWhereNfs         As String OPTIONAL
WSDATA IndexKeyNfe              As String OPTIONAL
WSDATA IndexKeyNfs              As String OPTIONAL
WSDATA OperationType            As String OPTIONAL
WSDATA FederalID                As String  //CNPJ

WSMETHOD GetHeader           DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
WSMETHOD BrwInvoice          DESCRIPTION STR0003 //"Mtodo de listagem dos documentos de entrada ou saida"
WSMETHOD GetInvoice          DESCRIPTION STR0004 //"Mtodo de consulta as informaes do documento de entrada ou saida"
WSMETHOD GetInvoiceFederalID DESCRIPTION STR0012 //"Mtodo de consulta as informaes do documento de entrada ou saida por CNPJ"
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de recuperacao do header                              
                                                                       
Ĵ
ParametrosExpC1: Nome da Estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtInvoice

::Header := MtHeader(::HeaderType)
If Empty(::Header)
	::Header := FinHeader(::HeaderType)
EndIf

Return(.T.)

/*/

Ŀ
Funo    BrwPurchasAutor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de recuperacao dos documentos de entrada e saida      
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Fornecedor                                            
          ExpD3: Data Inicial                                          
          ExpD4: Data Final                                            
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve as cotacaoes em aberto do fornecedor     
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD BrwInvoice WSRECEIVE UserCode,CustomerOrSupplier,CustomerOrSupplierID,RegisterDateFrom,RegisterDateTo,DeliveryDateFrom,DeliveryDateTo,QueryAddWhereNfe,QueryAddWhereNfs,IndexKeyNfe,IndexKeyNfs,PurchaseNumber,OperationType WSSEND InvoiceHeader WSSERVICE MtInvoice

Local aArea    := GetArea()

Local lRetorno := .T.
Local lQuery   := .F.
Local lValido  := .F.
Local nX       := 0
Local cArquivo := ""
Local cQuery   := ""
Local cAliasSF1:= "SF1"
Local cAliasSF2:= "SF2"
Local cFornece := SubStr(::CustomerOrSupplierID,1,Len(SA2->A2_COD))
Local cLojaFor := SubStr(::CustomerOrSupplierID,Len(SA2->A2_COD)+1)
Local cCliente := SubStr(::CustomerOrSupplierID,1,Len(SA1->A1_COD))
Local cLojaCli := SubStr(::CustomerOrSupplierID,Len(SA1->A1_COD)+1)
Local dEntrini := ::DeliveryDateFrom
Local dEntrFim := ::DeliveryDateTo
Local dEmisini := ::RegisterDateFrom
Local dEmisFim := ::RegisterDateTo
Local cAlias   := IIf(::CustomerOrSupplier==1,"SA1","SA2")
Local aCampos  := {}
Local aStruSF1 := {}
Local aStruSF2 := {}
Local cVolume  := ""
Local nY       := 0

DEFAULT dEntrIni := dDataBase-30
DEFAULT dEntrFim := dDataBase
DEFAULT dEmisIni := dDataBase-30
DEFAULT dEmisFim := dDataBase

If PrtChkUser(::UserCode,"MtInvoice","BrwInvoice",cAlias,::CustomerOrSupplierID)
	//Ŀ
	//Pesquisa os documentos de entrada                                       
	//
	If Empty(::OperationType) .Or. ::OperationType=="E"
		dbSelectArea("SF1")
		dbSetOrder(2)
		lQuery := .T.
		cAliasSF1 := "BRWINVOICE"
		aStruSF1  := SF1->(dbStruct())

		cQuery := "SELECT F1_FILIAL,F1_TIPO,F1_FORNECE,F1_LOJA,F1_DOC,F1_SERIE,F1_FORMUL, "
		cQuery += "F1_DTDIGIT,F1_EMISSAO,F1_VALBRUT,F1_DESPESA,F1_SEGURO,F1_FRETE,F1_STATUS, "
		aCampos := MaFisRefLd("SF1","NF")
		For nY := 1 To Len(aCampos)
			If !Empty(aCampos[nY][2])
				cQuery += aCampos[nY][2]+","
			EndIf
			If !Empty(aCampos[nY][3])
				cQuery += aCampos[nY][3]+","
			EndIf
			If !Empty(aCampos[nY][4])
				cQuery += aCampos[nY][4]+","
			EndIf
		Next nY
		cQuery += "F1_PREFIXO,F1_DUPL "
		cQuery += GetUserField("SF1")
		cQuery += "FROM "+RetSqlName("SF1")+" SF1 "
		If !Empty(::PurchaseNumber)
			cQuery += ","+RetSqlName("SD1")+" SD1 "
		EndIf
		cQuery += "WHERE SF1.F1_FILIAL='"+xFilial("SF1")+"' AND "
		If ::CustomerOrSupplier==1
			cQuery += "SF1.F1_FORNECE='"+cCliente+"' AND "
			cQuery += "SF1.F1_LOJA='"+cLojaCli+"' AND "
			cQuery += "SF1.F1_TIPO IN('D','B') AND "
		Else
			cQuery += "SF1.F1_FORNECE='"+cFornece+"' AND "
			cQuery += "SF1.F1_LOJA='"+cLojaFor+"' AND "
			cQuery += "SF1.F1_TIPO NOT IN('D','B') AND "
		EndIf
		cQuery += "SF1.F1_EMISSAO >= '"+Dtos(dEmisIni)+"' AND "
		cQuery += "SF1.F1_EMISSAO <= '"+Dtos(dEmisFim)+"' AND "
		cQuery += "SF1.F1_DTDIGIT >= '"+Dtos(dEntrIni)+"' AND "
		cQuery += "SF1.F1_DTDIGIT <= '"+Dtos(dEntrFim)+"' AND "
		cQuery += "NOT ("+IsRemito(2,"SF1.F1_TIPODOC")+") AND " 
		cQuery += "SF1.D_E_L_E_T_=' ' "
		If !Empty(::PurchaseNumber)
			cQuery += " AND "
			cQuery += "SD1.D1_FILIAL = '"+xFilial("SD1")+"' AND "
			cQuery += "SD1.D1_DOC = SF1.F1_DOC AND "
			cQuery += "SD1.D1_SERIE = SF1.F1_SERIE AND "
			cQuery += "SD1.D1_FORNECE = SF1.F1_FORNECE AND "
			cQuery += "SD1.D1_LOJA = SF1.F1_LOJA AND "
			cQuery += "SD1.D1_TIPO = SF1.F1_TIPO AND "
			cQuery += "SD1.D1_PEDIDO = '"+::PurchaseNumber+"' AND "
			cQuery += "SD1.D_E_L_E_T_=' ' "
		EndIf
		cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfe)
		cQuery += "ORDER BY "+WsSqlOrder(IIf(Empty(::IndexKeyNfe),SF1->(IndexKey()),::IndexKeyNfe))

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF1)

		For nY := 1 To Len(aStruSF1)
			If aStruSF1[nY][2] <> "C" .And. aStruSF1[nY][2] <> "M"
				TcSetField(cAliasSF1,aStruSF1[nY][1],aStruSF1[nY][2],aStruSF1[nY][3],aStruSF1[nY][4])
			EndIf
		Next nY

		nX := 0
		DEFAULT ::InvoiceHeader := {}
		While !Eof() .And. xFilial("SF1") == (cAliasSF1)->F1_FILIAL .And.;
				cFornece+cLojaFor == (cAliasSF1)->F1_FORNECE+(cAliasSF1)->F1_LOJA

			If IIf(::CustomerOrSupplier==1,(cAliasSF1)->F1_TIPO $ 'DB',!(cAliasSF1)->F1_TIPO $ 'DB') .And.;
					(cAliasSF1)->F1_EMISSAO >= dEmisIni .And.;
					(cAliasSF1)->F1_EMISSAO <= dEmisFim .And.;
					(cAliasSF1)->F1_DTDIGIT >= dEntrIni .And.;
					(cAliasSF1)->F1_DTDIGIT <= dEntrFim	

				If lQuery
					lValido := .T.
				Else
					If Empty(::PurchaseNumber)
						lValido := .T.
					Else
						lValido := .F.
						dbSelectArea("SD1")
						dbSetOrder(1)
						MsSeek(xFilial("SD1")+(cAliasSF1)->F1_DOC+(cAliasSF1)->F1_SERIE+(cAliasSF1)->F1_FORNECE+(cAliasSF1)->F1_LOJA)
						While !Eof() .And.;
							xFilial("SD1") == SD1->D1_FILIAL .And.;
							(cAliasSF1)->F1_DOC == SD1->D1_DOC .And.;
							(cAliasSF1)->F1_SERIE == SD1->D1_SERIE .And.;
							(cAliasSF1)->F1_FORNECE == SD1->D1_FORNECE .And.;
							(cAliasSF1)->F1_LOJA == SD1->D1_LOJA

							If SD1->D1_TIPO == (cAliasSF1)->F1_TIPO .And.;
								SD1->D1_FORMUL == (cAliasSF1)->F1_FORMUL .And.;
								SD1->D1_PEDIDO == ::PurchaseNumber

								lValido := .T.
					 			Exit
							EndIf

							dbSelectArea("SD1")
							dbSkip()

						EndDo
					EndIf
				EndIf
				If lValido
					aadd(::InvoiceHeader,WsClassNew("InvoiceHeaderView"))
					nX++
					GetNFEHead(@::InvoiceHeader[nX],cAliasSF1)
				EndIf
			EndIf
			dbSelectArea(cAliasSF1)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSF1)
			dbCloseArea()
			dbSelectArea("SF1")
		Else
			dbSelectArea("SF1")
			RetIndex("SF1")
			FErase(cArquivo+OrdBagExt())
		EndIf
	EndIf
	//Ŀ
	//Pesquisa os documentos de saida                                         
	//
	If Empty(::OperationType) .Or. ::OperationType=="S"
		dbSelectArea("SF2")
		dbSetOrder(2)

		lQuery := .T.
		cAliasSF2 := "BRWINVOICE"
		aStruSF2  := SF2->(dbStruct())

		cQuery := "SELECT F2_FILIAL,F2_TIPO,F2_CLIENTE,F2_LOJA,F2_DOC,F2_SERIE, "
		cQuery += "F2_EMISSAO,F2_VALBRUT,F2_DESPESA,F2_SEGURO,F2_FRETE,F2_TRANSP, "
		aCampos := MaFisRefLd("SF2","NF")
		For nY := 1 To Len(aCampos)
			If !Empty(aCampos[nY][2])
				cQuery += aCampos[nY][2]+","
			EndIf
			If !Empty(aCampos[nY][3])
				cQuery += aCampos[nY][3]+","
			EndIf
			If !Empty(aCampos[nY][4])
				cQuery += aCampos[nY][4]+","
			EndIf
		Next nY
		cVolume := "1"
		While SF2->(FieldPos("F2_ESPECI"+cVolume))<>0 .And. !Empty(SF2->(FieldGet(FieldPos("F2_ESPECI"+cVolume))))
			cQuery += "F2_ESPECI"+cVolume+","+"F2_ESPECI"+cVolume+","
			cVolume := Soma1( cVolume )
		EndDo
		cQuery += "F2_PREFIXO,F2_DUPL "
		cQuery += GetUserField("SF2")
		cQuery += "FROM "+RetSqlName("SF2")+" SF2 "
		If !Empty(::PurchaseNumber)
			cQuery += ","+RetSqlName("SD2")+" SD2 "
		EndIf
		cQuery += "WHERE SF2.F2_FILIAL='"+xFilial("SF2")+"' AND "
		If ::CustomerOrSupplier==1
			cQuery += "SF2.F2_CLIENTE='"+cCliente+"' AND "
			cQuery += "SF2.F2_LOJA='"+cLojaCli+"' AND "
			cQuery += "SF2.F2_TIPO NOT IN('D','B') AND "
		Else
			cQuery += "SF2.F2_CLIENTE='"+cFornece+"' AND "
			cQuery += "SF2.F2_LOJA='"+cLojaFor+"' AND "
			cQuery += "SF2.F2_TIPO IN('D','B') AND "
		EndIf
		cQuery += "SF2.F2_EMISSAO >= '"+Dtos(dEmisIni)+"' AND "
		cQuery += "SF2.F2_EMISSAO <= '"+Dtos(dEmisFim)+"' AND "
		cQuery += "SF2.F2_EMISSAO >= '"+Dtos(dEntrIni)+"' AND "
		cQuery += "SF2.F2_EMISSAO <= '"+Dtos(dEntrFim)+"' AND "
		cQuery += "NOT ("+IsRemito(2,"SF2.F2_TIPODOC")+") AND "
		cQuery += "SF2.D_E_L_E_T_=' ' "
		If !Empty(::PurchaseNumber)
			cQuery += " AND "
			cQuery += "SD2.D2_FILIAL = '"+xFilial("SD2")+"' AND "
			cQuery += "SD2.D2_DOC = SF2.F2_DOC AND "
			cQuery += "SD2.D2_SERIE = SF2.F2_SERIE AND "
			cQuery += "SD2.D2_CLIENTE = SF2.F2_CLIENTE AND "
			cQuery += "SD2.D2_LOJA = SF2.F2_LOJA AND "
			cQuery += "SD2.D2_PEDIDO = '"+::PurchaseNumber+"' AND "
			cQuery += "SD2.D_E_L_E_T_=' ' "
		EndIf
		cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfs)
		cQuery += "ORDER BY "+WsSqlOrder(IIf(Empty(::IndexKeyNfs),SF2->(IndexKey()),::IndexKeyNfs))

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF2)

		For nY := 1 To Len(aStruSF2)
			If aStruSF2[nY][2] <> "C" .And. aStruSF2[nY][2] <> "M"
				TcSetField(cAliasSF2,aStruSF2[nY][1],aStruSF2[nY][2],aStruSF2[nY][3],aStruSF2[nY][4])
			EndIf
		Next nY

		DEFAULT ::InvoiceHeader := {}
		While !Eof() .And. xFilial("SF2") == (cAliasSF2)->F2_FILIAL .And.;
				cCliente+cLojaCli == (cAliasSF2)->F2_CLIENTE+(cAliasSF2)->F2_LOJA

			If IIf(::CustomerOrSupplier==1,!(cAliasSF2)->F2_TIPO $ 'DB',(cAliasSF2)->F2_TIPO $ 'DB') .And.;
					(cAliasSF2)->F2_EMISSAO >= dEmisIni .And.;
					(cAliasSF2)->F2_EMISSAO <= dEmisFim .And.;
					(cAliasSF2)->F2_EMISSAO >= dEntrIni .And.;
					(cAliasSF2)->F2_EMISSAO <= dEntrFim
				If lQuery
					lValido := .T.
				Else
					If Empty(::PurchaseNumber)
						lValido := .T.
					Else
						dbSelectArea("SD2")
						dbSetOrder(1)
						MsSeek(xFilial("SD2")+(cAliasSF2)->F2_DOC+(cAliasSF2)->F2_SERIE+(cAliasSF2)->F2_CLIENTE+(cAliasSF2)->F2_LOJA)
						While !Eof() .And.;
							xFilial("SD2") == (cAliasSD2)->D2_FILIAL .And.;
							(cAliasSF2)->F2_DOC == (cAliasSD2)->D2_DOC .And.;
							(cAliasSF2)->F2_SERIE == (cAliasSD2)->D2_SERIE .And.;
							(cAliasSF2)->F2_CLIENTE == (cAliasSD2)->D2_CLIENTE .And.;
							(cAliasSF2)->F2_LOJA == (cAliasSD2)->D2_LOJA

							If (cAliasSD2)->D2_PEDIDO == ::PurchaseNumber
								lValido := .T.
								Exit
							EndIf
							dbSelectArea(cAliasSD2)
							dbSkip()
						EndDo
					EndIf
				EndIf
				If lValido
					aadd(::InvoiceHeader,WsClassNew("InvoiceHeaderView"))
					nX++
					GetNFSHead(@::InvoiceHeader[nX],cAliasSF2)
				EndIf
			EndIf
			dbSelectArea(cAliasSF2)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSF2)
			dbCloseArea()
			dbSelectArea("SF2")
		Else
			dbSelectArea("SF2")
			RetIndex("SF2")
			FErase(cArquivo+OrdBagExt())
		EndIf
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetInvoiceAutor   Eduardo Riera          Data 22.08.2003 
Ĵ
          Rotina de recuperacao dos documento de entrada / saida       
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Tipo do documento de entrada/saida                    
          ExpC3: Numero de Serie                                       
          ExpC4: Numero do documento                                   
          ExpC5: Formulario Proprio?                                   
          ExpN6: 1-Cliente ou 2-Fornecedor                             
          ExpC6: Codigo do cliente ou fornecedor                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve os documentos de entrad ou saida         
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetInvoice WSRECEIVE UserCode,InvoiceType,SerialNumber,InvoiceNumber,DocumentType,CustomerOrSupplier,CustomerOrSupplierID,QueryAddWhereNfe,QueryAddWhereNfs WSSEND Invoice WSSERVICE MtInvoice

Local aArea    := GetArea()
Local cAlias   := IIf(::CustomerOrSupplier==1,"SA1","SA2")
Local cES      := SubStr(::InvoiceType,1,1)
Local cFornece := SubStr(::CustomerOrSupplierID,1,Len(SA2->A2_COD))
Local cLojaFor := SubStr(::CustomerOrSupplierID,Len(SA2->A2_COD)+1)
Local cCliente := SubStr(::CustomerOrSupplierID,1,Len(SA1->A1_COD))
Local cLojaCli := SubStr(::CustomerOrSupplierID,Len(SA1->A1_COD)+1)
Local cSerie   := wsmat_nfSerie(::SerialNumber)
Local cDoc     := SubStr(::InvoiceNumber,1,Len(SD1->D1_DOC))
Local cTipo    := AllTrim(SubStr(::InvoiceType,3))
Local cFormul  := ::DocumentType
Local cAliasSD1:= "SD1"
Local cAliasSD2:= "SD2"
Local nX       := 0
Local lQuery   := .F.
Local lRetorno := .F.
Local aStruSD1 := {}
Local aStruSD2 := {}
Local cQuery   := ""
Local nY       := 0
DEFAULT cFormul := " "
If cPaisLoc=="BRA"
	cFormul  := IIf(cFormul=="S","S"," ")
Else                                            
	cFormul  := IIf(cFormul=="S","S","N")
EndIf	       

If PrtChkUser(::UserCode,"MtInvoice","GetInvoice",IIf(cTipo$"BD",Nil,cAlias),IIf(cTipo$"BD",Nil,::CustomerOrSupplierID))
	//Ŀ
	//Verifica se qual o tipo de documento                                    
	//
	If cES == "E"
		dbSelectArea("SF1")
		dbSetOrder(1)
		MsSeek(xFilial("SF1")+cDoc+cSerie+cFornece+cLojaFor+cTipo)
		While !Eof() .And. xFilial("SF1") == SF1->F1_FILIAL .And.;
			cDoc == SF1->F1_DOC .And.;
			cSerie == SF1->F1_SERIE .And.;
			cFornece == SF1->F1_FORNECE .And.;
			cLojaFor == SF1->F1_LOJA .And.;
			cTipo == SF1->F1_TIPO 
			If SF1->F1_FORMUL == cFormul
				lRetorno := .T.
				Exit
			EndIf
			dbSelectArea("SF1")
			dbSkip()
		EndDo
		If lRetorno
			::Invoice:InvoiceHeader := WsClassNew("InvoiceHeaderView")
			GetNfeHead(@::Invoice:InvoiceHeader,"SF1")
			//Ŀ
			//Pesquisa os Itens do documento de entrada                               
			//
			::Invoice:InvoiceItem:= {}
			dbSelectArea("SD1")
			dbSetOrder(1)
			aStruSD1 := SD1->(dbStruct())
			lQuery   := .T.
			cAliasSD1:= "GETINVOICE"

			cQuery := "SELECT * "
			cQuery += "FROM "+RetSqlName("SD1")+" SD1 "
			cQuery += "WHERE SD1.D1_FILIAL='"+xFilial("SD1")+"' AND "
			cQuery += "SD1.D1_DOC='"+cDoc+"' AND "
			cQuery += "SD1.D1_SERIE='"+cSerie+"' AND "
			cQuery += "SD1.D1_FORNECE='"+cFornece+"' AND "
			cQuery += "SD1.D1_LOJA='"+cLojaFor+"' AND "	
			cQuery += "SD1.D1_TIPO='"+cTipo+"' AND "
			cQuery += "SD1.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfe)
			cQuery += "ORDER BY "+SqlOrder(SD1->(IndexKey()))

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1)

			For nY := 1 To Len(aStruSD1)
				If aStruSD1[nY][2] <> "C" .And. aStruSD1[nY][2] <> "M"
					TcSetField(cAliasSD1,aStruSD1[nY][1],aStruSD1[nY][2],aStruSD1[nY][3],aStruSD1[nY][4])
				EndIf
			Next nY

			nX := 0
			While !Eof() .And.;
				xFilial("SD1") == (cAliasSD1)->D1_FILIAL .And.;
				cDoc == (cAliasSD1)->D1_DOC .And.;
				cSerie == (cAliasSD1)->D1_SERIE .And.;
				cFornece == (cAliasSD1)->D1_FORNECE .And.;
				cLojaFor == (cAliasSD1)->D1_LOJA
				
				If (cAliasSD1)->D1_TIPO == cTipo .And. (cAliasSD1)->D1_FORMUL == cFormul
					If nX == 0
						::Invoice:InvoiceItem:= {}
					EndIf
					aadd(::Invoice:InvoiceItem,WsClassNew("InvoiceItemView"))
					nX++
					GetNFEItem(@::Invoice:InvoiceItem[nX],cAliasSD1)
				EndIf
				dbSelectArea(cAliasSD1)
				dbSkip()
			EndDo
			If lQuery
				dbSelectArea(cAliasSD1)
				dbCloseArea()
				dbSelectArea("SD1")
			EndIf
		EndIf
	Else
		dbSelectArea("SF2")
		dbSetOrder(1)
		If MsSeek(xFilial("SF2")+cDoc+cSerie+cCliente+cLojaCli)
			lRetorno := .T.
			::Invoice:InvoiceHeader := WsClassNew("InvoiceHeaderView")
			GetNfSHead(@::Invoice:InvoiceHeader,"SF2")
			//Ŀ
			//Pesquisa os Itens do documento de saida                                 
			//
			::Invoice:InvoiceItem:= {}
			dbSelectArea("SD2")
			dbSetOrder(3)
			aStruSD2 := SD2->(dbStruct())
			lQuery   := .T.
			cAliasSD2:= "GETINVOICE"

			cQuery := "SELECT * "
			cQuery += "FROM "+RetSqlName("SD2")+" SD2 "
			cQuery += "WHERE SD2.D2_FILIAL='"+xFilial("SD2")+"' AND "
			cQuery += "SD2.D2_DOC='"+cDoc+"' AND "
			cQuery += "SD2.D2_SERIE='"+cSerie+"' AND "
			cQuery += "SD2.D2_CLIENTE='"+cCliente+"' AND "
			cQuery += "SD2.D2_LOJA='"+cLojaCli+"' AND "	
			cQuery += "SD2.D2_TIPO='"+cTipo+"' AND "
			cQuery += "SD2.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfs)
			cQuery += "ORDER BY "+SqlOrder(SD2->(IndexKey()))

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD2)

			For nY := 1 To Len(aStruSD2)
				If aStruSD2[nY][2] <> "C" .And. aStruSD2[nY][2] <> "M"
					TcSetField(cAliasSD2,aStruSD2[nY][1],aStruSD2[nY][2],aStruSD2[nY][3],aStruSD2[nY][4])
				EndIf
			Next nY

			nX := 0
			While !Eof() .And.;
				xFilial("SD2") == (cAliasSD2)->D2_FILIAL .And.;
				cDoc == (cAliasSD2)->D2_DOC .And.;
				cSerie == (cAliasSD2)->D2_SERIE .And.;
				cFornece == (cAliasSD2)->D2_CLIENTE .And.;
				cLojaFor == (cAliasSD2)->D2_LOJA

				If (cAliasSD2)->D2_TIPO == cTipo 
					If nX == 0
						::Invoice:InvoiceItem:= {}
					EndIf
					aadd(::Invoice:InvoiceItem,WsClassNew("InvoiceItemView"))
					nX++
					GetNFSItem(@::Invoice:InvoiceItem[nX],cAliasSD2)
				EndIf
				dbSelectArea(cAliasSD2)
				dbSkip()
			EndDo
			If lQuery
				dbSelectArea(cAliasSD2)
				dbCloseArea()
				dbSelectArea("SD2")
			EndIf
		EndIf
	EndIf
	If !lRetorno
		SetSoapFault("GETINVOICE",STR0005) //"Documento nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)


/*/

Ŀ
Funo    GetInvoiceAutor   Eduardo Riera          Data 27.11.2006 
Ĵ
          Rotina de recuperacao dos documento de entrada / saida       
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Tipo do documento de entrada/saida                    
          ExpC3: Numero de Serie                                       
          ExpC4: Numero do documento                                   
          ExpC5: Formulario Proprio?                                   
          ExpN6: 1-Cliente ou 2-Fornecedor                             
          ExpC6: CNPJ                                                  
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve os documentos de entrad ou saida         
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetInvoiceFederalID WSRECEIVE UserCode,InvoiceType,SerialNumber,InvoiceNumber,DocumentType,CustomerOrSupplier,FederalID,QueryAddWhereNfe,QueryAddWhereNfs WSSEND Invoice WSSERVICE MtInvoice

Local aArea    := GetArea()
Local cES      := SubStr(::InvoiceType,1,1)
Local cFornece := ""
Local cLojaFor := ""
Local cCliente := ""
Local cLojaCli := ""
Local cSerie   := wsmat_nfSerie(::SerialNumber)
Local cDoc     := SubStr(::InvoiceNumber,1,Len(SD1->D1_DOC))
Local cTipo    := AllTrim(SubStr(::InvoiceType,3))
Local cFormul  := ::DocumentType
Local cAliasSD1:= "SD1"
Local cAliasSD2:= "SD2"
Local nX       := 0
Local lQuery   := .F.
Local lRetorno := .F.
Local aStruSD1 := {}
Local aStruSD2 := {}
Local cQuery   := ""
Local nY       := 0

DEFAULT cFormul := " "

If cPaisLoc=="BRA"
	cFormul  := IIf(cFormul=="S","S"," ")
Else                                            
	cFormul  := IIf(cFormul=="S","S","N")
EndIf	       

If PrtChkUser(::UserCode,"MtInvoice","GetInvoiceFederalID")
	//Ŀ
	//Posiciciona o cliente/Fornecedor                                        
	//
	If ::CustomerOrSupplier==1
		dbSelectArea("SA1")
		dbSetOrder(3)
		If !MsSeek(xFilial("SA1")+::FederalID)
			SetSoapFault("GETINVOICEFEDERALID",STR0013) //"Numero de identificao do cliente/fornecedor no encontrado"
		Else
			cCliente := SA1->A1_COD
			cLojaCli := SA1->A1_LOJA
			cFornece := SA1->A1_COD
			cLojaFor := SA1->A1_LOJA
		EndIf
	Else
		dbSelectArea("SA2")
		dbSetOrder(3)
		If !MsSeek(xFilial("SA2")+::FederalID)
			SetSoapFault("GETINVOICEFEDERALID",STR0013) //"Numero de identificao do cliente/fornecedor no encontrado"
			cCliente := SA2->A2_COD
			cLojaCli := SA2->A2_LOJA
			cFornece := SA2->A2_COD
			cLojaFor := SA2->A2_LOJA
		EndIf
	EndIf
	//Ŀ
	//Verifica se qual o tipo de documento                                    
	//
	If cES == "E"
		dbSelectArea("SF1")
		dbSetOrder(1)
		MsSeek(xFilial("SF1")+cDoc+cSerie+cFornece+cLojaFor+cTipo)
		While !Eof() .And. xFilial("SF1") == SF1->F1_FILIAL .And.;
			cDoc == SF1->F1_DOC .And.;
			cSerie == SF1->F1_SERIE .And.;
			cFornece == SF1->F1_FORNECE .And.;
			cLojaFor == SF1->F1_LOJA .And.;
			cTipo == SF1->F1_TIPO 
			If SF1->F1_FORMUL == cFormul
				lRetorno := .T.
				Exit
			EndIf
			dbSelectArea("SF1")
			dbSkip()
		EndDo
		If lRetorno
			::Invoice:InvoiceHeader := WsClassNew("InvoiceHeaderView")
			GetNfeHead(@::Invoice:InvoiceHeader,"SF1")
			//Ŀ
			//Pesquisa os Itens do documento de entrada                               
			//
			::Invoice:InvoiceItem:= {}
			dbSelectArea("SD1")
			dbSetOrder(1)
			aStruSD1 := SD1->(dbStruct())
			lQuery   := .T.
			cAliasSD1:= "GETINVOICEFEDERALID"

			cQuery := "SELECT * "
			cQuery += "FROM "+RetSqlName("SD1")+" SD1 "
			cQuery += "WHERE SD1.D1_FILIAL='"+xFilial("SD1")+"' AND "
			cQuery += "SD1.D1_DOC='"+cDoc+"' AND "
			cQuery += "SD1.D1_SERIE='"+cSerie+"' AND "
			cQuery += "SD1.D1_FORNECE='"+cFornece+"' AND "
			cQuery += "SD1.D1_LOJA='"+cLojaFor+"' AND "	
			cQuery += "SD1.D1_TIPO='"+cTipo+"' AND "
			cQuery += "SD1.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfe)
			cQuery += "ORDER BY "+SqlOrder(SD1->(IndexKey()))

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1)

			For nY := 1 To Len(aStruSD1)
				If aStruSD1[nY][2] <> "C" .And. aStruSD1[nY][2] <> "M"
					TcSetField(cAliasSD1,aStruSD1[nY][1],aStruSD1[nY][2],aStruSD1[nY][3],aStruSD1[nY][4])
				EndIf
			Next nY

			nX := 0
			While !Eof() .And.;
				xFilial("SD1") == (cAliasSD1)->D1_FILIAL .And.;
				cDoc == (cAliasSD1)->D1_DOC .And.;
				cSerie == (cAliasSD1)->D1_SERIE .And.;
				cFornece == (cAliasSD1)->D1_FORNECE .And.;
				cLojaFor == (cAliasSD1)->D1_LOJA

				If (cAliasSD1)->D1_TIPO == cTipo .And. (cAliasSD1)->D1_FORMUL == cFormul
					If nX == 0
						::Invoice:InvoiceItem:= {}
					EndIf
					aadd(::Invoice:InvoiceItem,WsClassNew("InvoiceItemView"))
					nX++
					GetNFEItem(@::Invoice:InvoiceItem[nX],cAliasSD1)
				EndIf
				dbSelectArea(cAliasSD1)
				dbSkip()
			EndDo
			If lQuery
				dbSelectArea(cAliasSD1)
				dbCloseArea()
				dbSelectArea("SD1")
			EndIf
		EndIf
	Else
		dbSelectArea("SF2")
		dbSetOrder(1)
		If MsSeek(xFilial("SF2")+cDoc+cSerie+cCliente+cLojaCli)
			lRetorno := .T.
			::Invoice:InvoiceHeader := WsClassNew("InvoiceHeaderView")
			GetNfSHead(@::Invoice:InvoiceHeader,"SF2")
			//Ŀ
			//Pesquisa os Itens do documento de saida                                 
			//
			::Invoice:InvoiceItem:= {}
			dbSelectArea("SD2")
			dbSetOrder(3)

			aStruSD2 := SD2->(dbStruct())
			lQuery   := .T.
			cAliasSD2:= "GETINVOICEFEDERALID"

			cQuery := "SELECT * "
			cQuery += "FROM "+RetSqlName("SD2")+" SD2 "
			cQuery += "WHERE SD2.D2_FILIAL='"+xFilial("SD2")+"' AND "
			cQuery += "SD2.D2_DOC='"+cDoc+"' AND "
			cQuery += "SD2.D2_SERIE='"+cSerie+"' AND "
			cQuery += "SD2.D2_CLIENTE='"+cCliente+"' AND "
			cQuery += "SD2.D2_LOJA='"+cLojaCli+"' AND "	
			cQuery += "SD2.D2_TIPO='"+cTipo+"' AND "
			cQuery += "SD2.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhereNfs)
			cQuery += "ORDER BY "+SqlOrder(SD2->(IndexKey()))

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD2)

			For nY := 1 To Len(aStruSD2)
				If aStruSD2[nY][2] <> "C" .And. aStruSD2[nY][2] <> "M"
					TcSetField(cAliasSD2,aStruSD2[nY][1],aStruSD2[nY][2],aStruSD2[nY][3],aStruSD2[nY][4])
				EndIf
			Next nY

			nX := 0
			While !Eof() .And.;
				xFilial("SD2") == (cAliasSD2)->D2_FILIAL .And.;
				cDoc == (cAliasSD2)->D2_DOC .And.;
				cSerie == (cAliasSD2)->D2_SERIE .And.;
				cFornece == (cAliasSD2)->D2_CLIENTE .And.;
				cLojaFor == (cAliasSD2)->D2_LOJA

				If (cAliasSD2)->D2_TIPO == cTipo
					If nX == 0
						::Invoice:InvoiceItem:= {}
					EndIf
					aadd(::Invoice:InvoiceItem,WsClassNew("InvoiceItemView"))
					nX++
					GetNFSItem(@::Invoice:InvoiceItem[nX],cAliasSD2)
				EndIf
				dbSelectArea(cAliasSD2)
				dbSkip()
			EndDo
			If lQuery
				dbSelectArea(cAliasSD2)
				dbCloseArea()
				dbSelectArea("SD2")
			EndIf
		EndIf
	EndIf
	If !lRetorno .Or. nX == 0
		lRetorno := .F.
		SetSoapFault("GETINVOICEFEDERALID",STR0005) //"Documento nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)
