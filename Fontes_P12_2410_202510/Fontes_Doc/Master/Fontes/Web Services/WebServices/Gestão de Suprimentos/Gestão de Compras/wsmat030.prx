#INCLUDE "wsmat030.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
/*/


Ŀ
Funo    WSMAT030   Autor Eduardo Riera           Data 18.03.2003  
Ĵ
Descrio  Web Service responsavel pela manutencao das informacoes      
          de clientes                                                   
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        Web Service                                                  
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
Tatiana       07/11/06112442Alteracao na query para ajuste de campos  
ٱ


/*/
//Ŀ
//Definicao do Web Service                                                
//
WSSERVICE MtCustomer            DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/mtcustomer.apw" //"Servio de consulta e atualizao das informaes da tabela de clientes ( <b>Restrio de cliente</b> ) <br><br><i>Este servio tem por objetivo possibilitar a consulta e atualizao das informaes da tabela de clientes pelos prprios clientes, facilitando o relacionamento entre o fornecedor e seus clientes.</i>"
WSDATA UserCode                 AS String
WSDATA CustomerID               AS String
WSDATA Customer                 As CustomerView
WSDATA Header                   As Array Of BrwHeader
WSDATA HeaderType               As String
WSDATA CustomerCode             As String
WSDATA TypeOfAddress            As Array Of GenericStruct
WSDATA TypeOfPhone              As Array Of GenericStruct

WSMETHOD GetHeader        DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
WSMETHOD GetTypeOfAddress DESCRIPTION STR0003 //"Mtodo que descreve os tipos de endereos"
WSMETHOD GetTypeOfPhone   DESCRIPTION STR0004 //"Mtodo que descreve os tipos de telefones"
WSMETHOD GetCustomer      DESCRIPTION STR0005 //"Mtodo de consulta as informaes do cliente"
WSMETHOD PutCustomer      DESCRIPTION STR0006 //"Mtodo de atualizao das informaes de cliente. Este mtodo necessita que todos os dados sejam passados na estrutura, mesmo aqueles que no sero alterados, uma vez que todas as informaes sero sobrepostas"
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 29.05.2003 
Ĵ
          Rotina de recuperacao dos headers deste WS                   
                                                                       
Ĵ
ParametrosExpC1: Nome da Estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtCustomer

Local nX := 0 

::Header := MtHeader(::HeaderType)

If Empty(::Header)
	::Header := {}
	aadd(::Header,WSClassNew("BrwHeader"))
	nX++
	::Header[nX]:HeaderTitle  := RetTitle("A1_COD")
	::Header[nX]:HeaderField   := "CODE"
	::Header[nX]:HeaderPicture := SX3->X3_PICTURE
	::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
	::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
	::Header[nX]:HeaderType    := SX3->X3_TIPO
	::Header[nX]:HeaderComboBox:= X3Cbox()
	::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
	
	aadd(::Header,WSClassNew("BrwHeader"))
	nX++
	::Header[nX]:HeaderTitle  := RetTitle("A1_NOME")
	::Header[nX]:HeaderField   := "DESCRIPTION"
	::Header[nX]:HeaderPicture := SX3->X3_PICTURE
	::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
	::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
	::Header[nX]:HeaderType    := SX3->X3_TIPO
	::Header[nX]:HeaderComboBox:= X3Cbox()
	::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
EndIf
Return(.T.)

/*/

Ŀ
Funo    GetCustomeAutor   Eduardo Riera          Data 29.05.2003 
Ĵ
          Rotina de obtencao dos dados de clientes                     
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo de identificacao do cliente                    
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve os dados de um cliente                   
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetCustomer WSRECEIVE UserCode,CustomerID WSSEND Customer WSSERVICE MtCustomer

Local aArea    := GetArea()
Local lRetorno := .T.
Local cCliente := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja    := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local nX       := 0

If PrtChkUser(::UserCode,"MTCUSTOMER","GetCustomer","SA1",::CustomerId )
	dbSelectArea("SA1")
	dbSetOrder(1)
	If MsSeek(xFilial("SA1")+cCliente+cLoja)	
		::Customer:CustomerCode     := SA1->A1_COD
		::Customer:UnitCustomerCode := SA1->A1_LOJA
		::Customer:Name             := SA1->A1_NOME
		::Customer:NickName         := SA1->A1_NREDUZ
		::Customer:FederalID        := SA1->A1_CGC
		::Customer:StateID          := SA1->A1_INSCR
		::Customer:DistrictID       := SA1->A1_INSCRM
		::Customer:HomePage         := SA1->A1_HPAGE
		::Customer:Email            := SA1->A1_EMAIL
		::Customer:PriceTableCode   := SA1->A1_TABELA
		::Customer:PaymentPlanCode  := SA1->A1_COND
		::Customer:SellerCode       := SA1->A1_VEND
		UserFields("SA1",@::Customer:UserFields)

		::Customer:Addresses := {}
		nX := 0
		If !Empty(SA1->A1_END)
			aadd(::Customer:Addresses,WsClassNew("AddressView"))
			nX++
			::Customer:Addresses[nX]:TypeOfAddress := "1"
			::Customer:Addresses[nX]:Address       := FisGetEnd(SA1->A1_END)[1]
			::Customer:Addresses[nX]:AddressNumber := AllTrim(Str(FisGetEnd(SA1->A1_END)[2],8))
			::Customer:Addresses[nX]:District      := SA1->A1_MUN
			::Customer:Addresses[nX]:State         := SA1->A1_EST
			::Customer:Addresses[nX]:ZipCode       := SA1->A1_CEP
			::Customer:Addresses[nX]:Zone          := SA1->A1_BAIRRO
		EndIf

		If !Empty(SA1->A1_ENDCOB)

			aadd(::Customer:Addresses,WsClassNew("AddressView"))
			nX++
			::Customer:Addresses[nX]:TypeOfAddress := "2"
			::Customer:Addresses[nX]:Address       := FisGetEnd(SA1->A1_ENDCOB)[1]
			::Customer:Addresses[nX]:AddressNumber := AllTrim(Str(FisGetEnd(SA1->A1_ENDCOB)[2],8))
			::Customer:Addresses[nX]:District      := SA1->A1_MUNC
			::Customer:Addresses[nX]:State         := SA1->A1_ESTC
			::Customer:Addresses[nX]:ZipCode       := SA1->A1_CEPC
			::Customer:Addresses[nX]:Zone          := SA1->A1_BAIRROC
		EndIf

		If !Empty(SA1->A1_ENDREC)		
			aadd(::Customer:Addresses,WsClassNew("AddressView"))
			nX++
			::Customer:Addresses[nX]:TypeOfAddress:= "3"
			::Customer:Addresses[nX]:Address       := FisGetEnd(SA1->A1_ENDREC)[1]
			::Customer:Addresses[nX]:AddressNumber := AllTrim(Str(FisGetEnd(SA1->A1_ENDREC)[2],8))
			::Customer:Addresses[nX]:District      := SA1->A1_MUNE
			::Customer:Addresses[nX]:State         := SA1->A1_ESTE
			::Customer:Addresses[nX]:ZipCode       := SA1->A1_CEPE
			::Customer:Addresses[nX]:Zone          := SA1->A1_BAIRROE
		EndIf

		::Customer:Phones := {}
		nX := 0
		If !Empty(SA1->A1_TEL)
			aadd(::Customer:Phones,WsClassNew("PhoneView"))
			nX++
			::Customer:Phones[nX]:TypeOfPhone     := "1"
			::Customer:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA1->A1_TEL,SA1->A1_DDD,SA1->A1_DDI)[3],15))
			::Customer:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA1->A1_TEL,SA1->A1_DDD,SA1->A1_DDI)[2],15))
			::Customer:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA1->A1_TEL,SA1->A1_DDD,SA1->A1_DDI)[1],15))

			If ::Customer:Phones[nX]:CountryAreaCode == "0"
				::Customer:Phones[nX]:CountryAreaCode := NIL
			EndIf

			If ::Customer:Phones[nX]:LocalAreaCode == "0"
				::Customer:Phones[nX]:LocalAreaCode := NIL
			EndIf
		EndIf

		If !Empty(SA1->A1_FAX)
			aadd(::Customer:Phones,WsClassNew("PhoneView"))
			nX++
			::Customer:Phones[nX]:TypeOfPhone     := "2"
			::Customer:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA1->A1_FAX,SA1->A1_DDD,SA1->A1_DDI)[3],15))
			::Customer:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA1->A1_FAX,SA1->A1_DDD,SA1->A1_DDI)[2],15))
			::Customer:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA1->A1_FAX,SA1->A1_DDD,SA1->A1_DDI)[1],15))

			If ::Customer:Phones[nX]:CountryAreaCode == "0"
				::Customer:Phones[nX]:CountryAreaCode := NIL
			EndIf

			If ::Customer:Phones[nX]:LocalAreaCode == "0"
				::Customer:Phones[nX]:LocalAreaCode := NIL
			Endif
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("GETCUSTOMER",STR0007) //"Cliente nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    PutCustomeAutor   Eduardo Riera          Data 29.05.2003 
Ĵ
          Rotina de atualizacao dos dados de clientes                  
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo de identificacao do cliente                    
          ExpO3: Estrutura a ser gravada                               
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo atualiza os dados de um cliente                  
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
Ĵ
Analista   Data/Bops/Ver Manutencao Efetuada                      	   
Ĵ
Cleber M. 27/03/07120870Incluida funcao de ajuste do dicionario(SX3) 
                        pois no campo A1_CDRDES nao pode ser retirada
                        a obrigatoriedade dele no ATUSX.             
ٱ


/*/
WSMETHOD PutCustomer WSRECEIVE UserCode,CustomerID,Customer WSSEND CustomerCode WSSERVICE MtCustomer

Local aArea    := GetArea()
Local aDados   := {}
Local aErro    := {}
Local lRetorno := .T.
Local cCliente := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja    := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local cErro    := ""
Local nX       := 0
PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"MTCUSTOMER","PutCustomer","SA1",::CustomerId)
	dbSelectArea("SA1")
	dbSetOrder(1)
	If MsSeek(xFilial("SA1")+cCliente+cLoja)			
		If ( !Empty( ::Customer:CustomerCode) )
			aadd(aDados,{"A1_COD",::Customer:CustomerCode,})
		EndIf
		If ( !Empty( ::Customer:UnitCustomerCode) )
			aadd(aDados,{"A1_LOJA",::Customer:UnitCustomerCode,})
		EndIf
		aadd(aDados,{"A1_NOME",::Customer:Name,})
		aadd(aDados,{"A1_NREDUZ",::Customer:NickName,})
		aadd(aDados,{"A1_CGC",::Customer:FederalID,})
		aadd(aDados,{"A1_INSCR",::Customer:StateID,})
		aadd(aDados,{"A1_INSCRM",::Customer:DistrictID,})
		aadd(aDados,{"A1_HPAGE",::Customer:HomePage,})
		aadd(aDados,{"A1_EMAIL",::Customer:Email,})

		For nX := 1 To Len(::Customer:Addresses)
			Do Case
			Case ::Customer:Addresses[nX]:TypeOfAddress == "1"
				aadd(aDados,{"A1_END",AllTrim(::Customer:Addresses[nX]:Address)+", "+AllTrim(::Customer:Addresses[nX]:AddressNumber),})
				aadd(aDados,{"A1_MUN",::Customer:Addresses[nX]:District,})
				aadd(aDados,{"A1_EST",::Customer:Addresses[nX]:State,})
				aadd(aDados,{"A1_CEP",::Customer:Addresses[nX]:ZipCode,})
				aadd(aDados,{"A1_BAIRRO",::Customer:Addresses[nX]:Zone,})
			Case ::Customer:Addresses[nX]:TypeOfAddress == "2"
				If Empty(::Customer:Addresses[nX]:Address)
					aadd(aDados,{"A1_ENDCOB","",Nil})
				Else
					aadd(aDados,{"A1_ENDCOB",AllTrim(::Customer:Addresses[nX]:Address)+", "+AllTrim(::Customer:Addresses[nX]:AddressNumber),})
					aadd(aDados,{"A1_BAIRROC",::Customer:Addresses[nX]:Zone,})
				EndIf
				aadd(aDados,{"A1_MUNC",::Customer:Addresses[nX]:District,})
				aadd(aDados,{"A1_ESTC",::Customer:Addresses[nX]:State,})
				aadd(aDados,{"A1_CEPC",::Customer:Addresses[nX]:ZipCode,})
			Case ::Customer:Addresses[nX]:TypeOfAddress == "3"
				If Empty(::Customer:Addresses[nX]:Address)
					aadd(aDados,{"A1_ENDREC","",Nil})
				Else				
					aadd(aDados,{"A1_ENDREC",AllTrim(::Customer:Addresses[nX]:Address)+", "+AllTrim(::Customer:Addresses[nX]:AddressNumber),})
				EndIf
				aadd(aDados,{"A1_MUNE",::Customer:Addresses[nX]:District,})
				aadd(aDados,{"A1_ESTE",::Customer:Addresses[nX]:State,})
				aadd(aDados,{"A1_CEPE",::Customer:Addresses[nX]:ZipCode,})
				aadd(aDados,{"A1_BAIRROE",::Customer:Addresses[nX]:Zone,})
			OtherWise
				lRetorno := .F.
				SetSoapFault("PUTCUSTOMER",STR0008) //"TypeOfAddress invalido"
			EndCase
		Next nX

		For nX := 1 To Len(::Customer:Phones)
			Do Case
			Case ::Customer:Phones[nX]:TypeOfPhone == "1"
				If ACJ->(LastRec()) == 0
					//aadd(aDados,{"A1_TEL",::Customer:Phones[nX]:CountryAreaCode+" "+::Customer:Phones[nX]:LocalAreaCode+" "+::Customer:Phones[nX]:PhoneNumber,})	
					aadd(aDados,{"A1_TEL",::Customer:Phones[nX]:PhoneNumber,})	
				Else				
					aadd(aDados,{"A1_TEL",::Customer:Phones[nX]:PhoneNumber,})
					aadd(aDados,{"A1_DDD",::Customer:Phones[nX]:LocalAreaCode,})
					aadd(aDados,{"A1_DDI",::Customer:Phones[nX]:CountryAreaCode,})	
				EndIf
			Case ::Customer:Phones[nX]:TypeOfPhone == "2"
				aadd(aDados,{"A1_FAX",::Customer:Phones[nX]:PhoneNumber,})
			OtherWise
				lRetorno := .F.
				SetSoapFault("PUTCUSTOMER",STR0009)				 //"TypeOfPhone invalido"
			EndCase
		Next nX
		If lRetorno
			//Ŀ
			//Gravacao ao do cadastro de clientes                                     
			//
			PutUserFields("SA1",@::Customer:UserFields,@aDados)
			//Ŀ
			//Atualizacao do cadastro de clientes                                     
			//
			aDados := WsAutoOpc(aDados)
			Mata030(aDados,4)
			::CustomerCode := SA1->A1_COD
			If lMsErroAuto
				aErro := GetAutoGRLog()
				cErro := ""
				For nX := 1 To Len(aErro)
					cErro += aErro[nX] + Chr(13)+Chr(10)
				Next nX	
				SetSoapFault("PUTCUSTOMER",cErro)
				lRetorno := .F.
			EndIf
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("PUTCUSTOMER",STR0007) //"Cliente nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetTypeOfAAutor   Eduardo Riera          Data 04.08.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfAddress WSRECEIVE NULLPARAM WSSEND TypeOfAddress WSSERVICE MtCustomer

aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
::TypeOfAddress[1]:Code := "1"
::TypeOfAddress[1]:Description := STR0010 //"COMERCIAL"
aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
::TypeOfAddress[2]:Code := "2"
::TypeOfAddress[2]:Description := STR0011 //"COBRANCA"
aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
::TypeOfAddress[3]:Code := "3"
::TypeOfAddress[3]:Description := STR0012 //"ENTREGA"

Return(.T.)


/*/

Ŀ
Funo    GetTypeOfAAutor   Eduardo Riera          Data 04.08.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfPhone WSRECEIVE NULLPARAM WSSEND TypeOfPhone WSSERVICE MtCustomer

aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[1]:Code := "1"
::TypeOfPhone[1]:Description := STR0010 //"COMERCIAL"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[2]:Code := "2"
::TypeOfPhone[2]:Description := STR0013 //"FAX"

Return(.T.)

/*/


Ŀ
Funo    WSMAT030   Autor Eduardo Riera           Data 18.03.2003  
Ĵ
Descrio  Web Service responsavel pela manutencao das informacoes      
          de clientes                                                   
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        Web Service                                                  
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service                                                
//
WSSERVICE MtSellerCustomer      DESCRIPTION STR0014 NAMESPACE "http://webservices.microsiga.com.br/mtsellercustomer.apw" //"Servio de consulta e manuteno das informaes da tabela de clientes ( <b>Restrio de representante comercial</b> ) <br><br><i>Este servio tem por objetivo possibilitar a consulta, incluso, atualizao e excluso das informaes da tabela de clientes pelos representantes comerciais, facilitando o relacionamento entre eles.</i>"
WSDATA UserCode                 AS String
WSDATA CustomerID               AS String
WSDATA Customer                 As CustomerView
WSDATA Header                   As Array Of BrwHeader
WSDATA HeaderType               As String
WSDATA CustomerCode             As String
WSDATA SellerCode               As String
WSDATA TypeOfAddress            As Array Of GenericStruct
WSDATA TypeOfPhone              As Array Of GenericStruct
WSDATA ListOfCustomer           As Array Of GenericView2 OPTIONAL
WSDATA PageLen                  AS Integer OPTIONAL
WSDATA PageFirst                AS Integer OPTIONAL
WSDATA NameLike                 As String OPTIONAL
WSDATA NickNameLike             As String OPTIONAL
WSDATA QueryAddWhere            As String OPTIONAL
WSDATA IndexKey                 As String OPTIONAL

WSMETHOD GetHeader        DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
WSMETHOD GetTypeOfAddress DESCRIPTION STR0015 //"Mtodo que descreve os tipos de endereo"
WSMETHOD GetTypeOfPhone   DESCRIPTION STR0016 //"Mtodo que descreve os tipos de telefone"
WSMETHOD GetCustomer      DESCRIPTION STR0017 //"Mtodo de consulta as informaes de cliente"
WSMETHOD BrwCustomer      DESCRIPTION STR0018 //"Mtodo de listagem das informaes de clientes. Este mtodo demonstra todos os clientes vinculados a um representante comercial ou que no tenham representante comercial. A listagem demonstra algumas informaes do cliente sendo necessrio a utilizao do mtodo GetCustomer para complementar as informaes. Recomenda-se que a utilizao conjunta destes mtodos seja feita com prudncia, pois poder acarretar sobrecarga na linha de transmisso"
WSMETHOD PutCustomer      DESCRIPTION STR0019 //"Mtodo de incluso e atualizao das informaes de cliente. Este mtodo necessita que todas as informaes sejam passadas, mesmo as que no sero alteradas, uma vez que todas as informaes sero sobrepostas"
WSMETHOD DelCustomer      DESCRIPTION STR0020 //"Mtodo de excluso das informaes de cliente. Este mtodo necessita apenas da identificao do cliente para efetuar a excluso"
WSMETHOD BrwHeader		  DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 29.05.2003 
Ĵ
          Rotina de recuperacao dos headers deste WS                   
                                                                       
Ĵ
ParametrosExpC1: Nome da Estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtSellerCustomer

Local nX := 0 

::Header := MtHeader(::HeaderType)

If Empty(::Header)
	::Header := {}
	aadd(::Header,WSClassNew("BrwHeader"))
	nX++
	::Header[nX]:HeaderTitle  := RetTitle("A1_COD")
	::Header[nX]:HeaderField   := "CODE"
	::Header[nX]:HeaderPicture := SX3->X3_PICTURE
	::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
	::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
	::Header[nX]:HeaderType    := SX3->X3_TIPO
	::Header[nX]:HeaderComboBox:= X3Cbox()
	::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)

	aadd(::Header,WSClassNew("BrwHeader"))
	nX++
	::Header[nX]:HeaderTitle  := RetTitle("A1_NOME")
	::Header[nX]:HeaderField   := "DESCRIPTION"
	::Header[nX]:HeaderPicture := SX3->X3_PICTURE
	::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
	::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
	::Header[nX]:HeaderType    := SX3->X3_TIPO
	::Header[nX]:HeaderComboBox:= X3Cbox()	
	::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
EndIf

Return(.T.)

/*/

Ŀ
Funo    BrwHeader Autor   Vendas Cliente         Data 14.04.2008 
Ĵ
          Rotina de recuperacao dos headers deste WS                   
                                                                       
Ĵ
ParametrosExpC1: Nome da Estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD BrwHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtSellerCustomer

Local nX := 0 

::Header := MtHeader(::HeaderType)

If Empty(::Header)
	::Header := {}
	aadd(::Header,WSClassNew("BrwHeader"))
	nX++
	::Header[nX]:HeaderTitle  := RetTitle("A1_COD")
	::Header[nX]:HeaderField   := "CODE"
	::Header[nX]:HeaderPicture := SX3->X3_PICTURE
	::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
	::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
	::Header[nX]:HeaderType    := SX3->X3_TIPO
	::Header[nX]:HeaderComboBox:= X3Cbox()
	::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)

	aadd(::Header,WSClassNew("BrwHeader"))
	nX++
	::Header[nX]:HeaderTitle  := RetTitle("A1_NOME")
	::Header[nX]:HeaderField   := "DESCRIPTION"
	::Header[nX]:HeaderPicture := SX3->X3_PICTURE
	::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
	::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
	::Header[nX]:HeaderType    := SX3->X3_TIPO
	::Header[nX]:HeaderComboBox:= X3Cbox()	
	::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
EndIf

Return(.T.)

/*/

Ŀ
Funo    GetCustomeAutor   Eduardo Riera          Data 29.05.2003 
Ĵ
          Rotina de obtencao dos dados de clientes                     
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Vendedor                                              
          ExpC3: Cliente                                               
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve os dados de um cliente                   
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetCustomer WSRECEIVE UserCode,SellerCode,CustomerID WSSEND Customer WSSERVICE MtSellerCustomer

Local aArea    := GetArea()
Local lRetorno := .T.
Local cCliente := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja    := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local nX       := 0

If PrtChkUser(::UserCode,"MtSellerCustomer","GetCustomer","SA3",::SellerCode)
	dbSelectArea("SA1")
	dbSetOrder(1)
	If MsSeek(xFilial("SA1")+cCliente+cLoja)
		::Customer:CustomerCode     := SA1->A1_COD
		::Customer:UnitCustomerCode := SA1->A1_LOJA
		::Customer:Name             := SA1->A1_NOME
		::Customer:NickName         := SA1->A1_NREDUZ
		::Customer:FederalID        := SA1->A1_CGC
		::Customer:StateID          := SA1->A1_INSCR
		::Customer:DistrictID       := SA1->A1_INSCRM
		::Customer:HomePage         := SA1->A1_HPAGE
		::Customer:Email            := SA1->A1_EMAIL
		::Customer:PriceTableCode   := SA1->A1_TABELA
		::Customer:PaymentPlanCode  := SA1->A1_COND
		::Customer:SellerCode       := SA1->A1_VEND
		UserFields("SA1",@::Customer:UserFields)

		::Customer:Addresses := {}
		nX := 0
		If !Empty(SA1->A1_END)
			aadd(::Customer:Addresses,WsClassNew("AddressView"))
			nX++
			::Customer:Addresses[nX]:TypeOfAddress := "1"
			::Customer:Addresses[nX]:Address       := FisGetEnd(SA1->A1_END)[1]
			::Customer:Addresses[nX]:AddressNumber := AllTrim(Str(FisGetEnd(SA1->A1_END)[2],8))
			::Customer:Addresses[nX]:District      := SA1->A1_MUN
			::Customer:Addresses[nX]:State         := SA1->A1_EST
			::Customer:Addresses[nX]:ZipCode       := SA1->A1_CEP
			::Customer:Addresses[nX]:Zone          := SA1->A1_BAIRRO
		EndIf

		If !Empty(SA1->A1_ENDCOB)

			aadd(::Customer:Addresses,WsClassNew("AddressView"))
			nX++
			::Customer:Addresses[nX]:TypeOfAddress := "2"
			::Customer:Addresses[nX]:Address       := FisGetEnd(SA1->A1_ENDCOB)[1]
			::Customer:Addresses[nX]:AddressNumber := AllTrim(Str(FisGetEnd(SA1->A1_ENDCOB)[2],8))
			::Customer:Addresses[nX]:District      := SA1->A1_MUNC
			::Customer:Addresses[nX]:State         := SA1->A1_ESTC
			::Customer:Addresses[nX]:ZipCode       := SA1->A1_CEPC
			::Customer:Addresses[nX]:Zone          := SA1->A1_BAIRROC
		EndIf

		If !Empty(SA1->A1_ENDENT)		
			aadd(::Customer:Addresses,WsClassNew("AddressView"))
			nX++
			::Customer:Addresses[nX]:TypeOfAddress:= "3"
			::Customer:Addresses[nX]:Address       := FisGetEnd(SA1->A1_ENDENT)[1]
			::Customer:Addresses[nX]:AddressNumber := AllTrim(Str(FisGetEnd(SA1->A1_ENDENT)[2],8))
			::Customer:Addresses[nX]:District      := SA1->A1_MUNE
			::Customer:Addresses[nX]:State         := SA1->A1_ESTE
			::Customer:Addresses[nX]:ZipCode       := SA1->A1_CEPE
			::Customer:Addresses[nX]:Zone          := SA1->A1_BAIRROE
		EndIf

		::Customer:Phones := {}
		nX := 0
		If !Empty(SA1->A1_TEL)
			aadd(::Customer:Phones,WsClassNew("PhoneView"))
			nX++
			::Customer:Phones[nX]:TypeOfPhone     := "1"
			::Customer:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA1->A1_TEL,SA1->A1_DDD,SA1->A1_DDI)[3],15))
			::Customer:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA1->A1_TEL,SA1->A1_DDD,SA1->A1_DDI)[2],15))
			::Customer:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA1->A1_TEL,SA1->A1_DDD,SA1->A1_DDI)[1],15))
			
			If ::Customer:Phones[nX]:CountryAreaCode == "0"
				::Customer:Phones[nX]:CountryAreaCode := NIL
			EndIf

			If ::Customer:Phones[nX]:LocalAreaCode == "0"
				::Customer:Phones[nX]:LocalAreaCode := NIL
			EndIf
		EndIf

		If !Empty(SA1->A1_FAX)
			aadd(::Customer:Phones,WsClassNew("PhoneView"))
			nX++
			::Customer:Phones[nX]:TypeOfPhone     := "2"
			::Customer:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA1->A1_FAX,SA1->A1_DDD,SA1->A1_DDI)[3],15))
			::Customer:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA1->A1_FAX,SA1->A1_DDD,SA1->A1_DDI)[2],15))
			::Customer:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA1->A1_FAX,SA1->A1_DDD,SA1->A1_DDI)[1],15))

			If ::Customer:Phones[nX]:CountryAreaCode == "0"
				::Customer:Phones[nX]:CountryAreaCode := NIL
			EndIf

			If ::Customer:Phones[nX]:LocalAreaCode == "0"
				::Customer:Phones[nX]:LocalAreaCode := NIL
			EndIf
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("GETCUSTOMER",STR0007) //"Cliente nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    PutCustomeAutor   Eduardo Riera          Data 29.05.2003 
Ĵ
          Rotina de atualizacao dos dados de clientes                  
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Vendedor                                              
          ExpC3: Cliente                                               
          ExpO4: Estrutura a ser gravada                               
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo atualiza os dados de um cliente                  
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
Ĵ
Analista   Data/Bops/Ver Manutencao Efetuada                      	   
Ĵ
Cleber M. 05/04/0695814 Incluida funcao de ajuste do dicionario(SX3) 
Cleber M. 04/06/07126890Correcao nos uso dos campos Cliente/Loja re- 
                        cebidos pelo metodo respeitando sempre o ta- 
                        manho definido no SX3.                       
ٱ


/*/
WSMETHOD PutCustomer WSRECEIVE UserCode,SellerCode,Customer WSSEND CustomerCode WSSERVICE MtSellerCustomer

Local aArea    := GetArea()		//Guarda a area atual
Local aDados   := {}			//Array com os campos/dados do cliente
Local aErro    := {}			//Array com a descricao do erro
Local lRetorno := .T.			//Indica retorno da funcao
Local cErro    := ""			//Descricao do erro com quebra de linhas
Local nX       := 0				//Usada em lacos For..Next
Local cCliente		:= ""		//Cod. Cliente 
Local cLoja			:= ""		//Loja
Local nTamA1_COD	:= TamSX3("A1_COD")[1]		//Tamanho do campo A1_COD	
Local nTamA1_LOJA	:= TamSX3("A1_LOJA")[1]		//Tamanho do campo A1_LOJA	
PRIVATE lMsErroAuto := .F.		//Indica se houve erro durante MsExecAuto
PRIVATE lAutoErrNoFile := .T.	//Indica se ocorreu erro no arquivo

If PrtChkUser(::UserCode,"MtSellerCustomer","PutCustomer","SA3",::SellerCode)
	cCliente := PadR(::Customer:CustomerCode, nTamA1_COD)
	cLoja := PadR(::Customer:UnitCustomerCode, nTamA1_LOJA)
	If( !Empty( cCliente ) )
		aadd(aDados,{"A1_COD",cCliente,})
	EndIf
	If( !Empty( cLoja ) )
		aadd(aDados,{"A1_LOJA",cLoja,})
	EndIf
	aadd(aDados,{"A1_NOME",::Customer:Name,})
	aadd(aDados,{"A1_NREDUZ",::Customer:NickName,})
	aadd(aDados,{"A1_CGC",::Customer:FederalID,})
	aadd(aDados,{"A1_INSCR",::Customer:StateID,})
	aadd(aDados,{"A1_INSCRM",::Customer:DistrictID,})
	aadd(aDados,{"A1_HPAGE",::Customer:HomePage,})
	aadd(aDados,{"A1_EMAIL",::Customer:Email,})

	For nX := 1 To Len(::Customer:Addresses)
		Do Case
		Case ::Customer:Addresses[nX]:TypeOfAddress == "1"
			aadd(aDados,{"A1_END",AllTrim(::Customer:Addresses[nX]:Address)+", "+AllTrim(::Customer:Addresses[nX]:AddressNumber),})
			aadd(aDados,{"A1_MUN",::Customer:Addresses[nX]:District,})
			aadd(aDados,{"A1_EST",::Customer:Addresses[nX]:State,})
			aadd(aDados,{"A1_CEP",::Customer:Addresses[nX]:ZipCode,})
			aadd(aDados,{"A1_BAIRRO",::Customer:Addresses[nX]:Zone,})
		Case ::Customer:Addresses[nX]:TypeOfAddress == "2"
			If Empty(::Customer:Addresses[nX]:Address)
				aadd(aDados,{"A1_ENDCOB","",Nil})
			Else
				aadd(aDados,{"A1_ENDCOB",AllTrim(::Customer:Addresses[nX]:Address)+", "+AllTrim(::Customer:Addresses[nX]:AddressNumber),})
			EndIf
			If !Empty(::Customer:Addresses[nX]:Zone)  
				aadd(aDados,{"A1_BAIRROC",::Customer:Addresses[nX]:Zone,})
			EndIf
			aadd(aDados,{"A1_MUNC",::Customer:Addresses[nX]:District,})
			aadd(aDados,{"A1_ESTC",::Customer:Addresses[nX]:State,})
			aadd(aDados,{"A1_CEPC",::Customer:Addresses[nX]:ZipCode,})
		Case ::Customer:Addresses[nX]:TypeOfAddress == "3"
			If Empty(::Customer:Addresses[nX]:Address)
				aadd(aDados,{"A1_ENDENT","",Nil})
			Else
				aadd(aDados,{"A1_ENDENT",AllTrim(::Customer:Addresses[nX]:Address)+", "+AllTrim(::Customer:Addresses[nX]:AddressNumber),})
			EndIf
			aadd(aDados,{"A1_MUNE",::Customer:Addresses[nX]:District,})
			aadd(aDados,{"A1_ESTE",::Customer:Addresses[nX]:State,})
			aadd(aDados,{"A1_CEPE",::Customer:Addresses[nX]:ZipCode,})
			aadd(aDados,{"A1_BAIRROE",::Customer:Addresses[nX]:Zone,})
		OtherWise
			lRetorno := .F.
			SetSoapFault("PUTCUSTOMER",STR0008) //"TypeOfAddress invalido"
		EndCase
	Next nX

	For nX := 1 To Len(::Customer:Phones)
		Do Case
		Case ::Customer:Phones[nX]:TypeOfPhone == "1"
				If ACJ->(LastRec()) == 0
					//aadd(aDados,{"A1_TEL",::Customer:Phones[nX]:CountryAreaCode+" "+::Customer:Phones[nX]:LocalAreaCode+" "+::Customer:Phones[nX]:PhoneNumber,})	
					aadd(aDados,{"A1_TEL",::Customer:Phones[nX]:PhoneNumber,})	
				Else				
					aadd(aDados,{"A1_TEL",::Customer:Phones[nX]:PhoneNumber,})
					aadd(aDados,{"A1_DDD",::Customer:Phones[nX]:LocalAreaCode,})
					aadd(aDados,{"A1_DDI",::Customer:Phones[nX]:CountryAreaCode,})	
				EndIf
		Case ::Customer:Phones[nX]:TypeOfPhone == "2"
			aadd(aDados,{"A1_FAX",::Customer:Phones[nX]:PhoneNumber,})
		OtherWise
			lRetorno := .F.
			SetSoapFault("PUTCUSTOMER",STR0009)				 //"TypeOfPhone invalido"
		EndCase
	Next nX
	If lRetorno
		//Ŀ
		//Gravacao ao do cadastro de clientes                                     
		//
		PutUserFields("SA1",@::Customer:UserFields,@aDados)
		//Ŀ
		//Atualizacao do cadastro de clientes                                     
		//
		aDados := WsAutoOpc(aDados)
		dbSelectArea("SA1")
		dbSetOrder(1)                   
		If !Empty( cCliente ) .AND. !Empty( cLoja ) .AND. MsSeek(xFilial("SA1")+cCliente+cLoja)
			Mata030(aDados,4)
		Else
			Mata030(aDados,3)
		EndIf
		::CustomerCode := SA1->A1_COD
		If lMsErroAuto
			aErro := GetAutoGRLog()
			cErro := ""
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX	
			SetSoapFault("PUTCUSTOMER",cErro)
			lRetorno := .F.
		EndIf
	EndIf

Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    DelCustomeAutor   Eduardo Riera          Data 23.01.2004 
Ĵ
          Rotina de exclusao dos dados de clientes                     
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Vendedor                                              
          ExpO3: Estrutura a ser gravada                               
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo exclui os dados de um cliente                    
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD DelCustomer WSRECEIVE UserCode,SellerCode,Customer WSSEND CustomerCode WSSERVICE MtSellerCustomer

Local aArea    := GetArea()
Local aDados   := {}
Local aErro    := {}
Local lRetorno := .T.
Local cErro    := ""
Local nX       := 0
PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"MtSellerCustomer","DelCustomer","SA3",::SellerCode)
	If( !Empty( ::Customer:CustomerCode) )
		aadd(aDados,{"A1_COD",::Customer:CustomerCode,})
	EndIf
	If( !Empty( ::Customer:UnitCustomerCode) )
		aadd(aDados,{"A1_LOJA",::Customer:UnitCustomerCode,})
	EndIf
	If lRetorno
		//Ŀ
		//Gravacao ao do cadastro de clientes                                     
		//
		PutUserFields("SA1",@::Customer:UserFields,@aDados)
		//Ŀ
		//Atualizacao do cadastro de clientes                                     
		//
		aDados := WsAutoOpc(aDados)
		dbSelectArea("SA1")
		dbSetOrder(1)                   
		If !Empty( ::Customer:CustomerCode ) .AND. !Empty( ::Customer:UnitCustomerCode ) .AND. MsSeek(xFilial("SA1")+::Customer:CustomerCode+::Customer:UnitCustomerCode)
			Mata030(aDados,5)
		Else
			SetSoapFault("DELCUSTOMER",STR0021) //"Cliente no localizado"
			lRetorno := .F.
		EndIf
		::CustomerCode := SA1->A1_COD
		If lMsErroAuto
			aErro := GetAutoGRLog()
			cErro := ""
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX	
			SetSoapFault("DELCUSTOMER",cErro)
			lRetorno := .F.
		EndIf
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetTypeOfAAutor   Eduardo Riera          Data 04.08.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfAddress WSRECEIVE NULLPARAM WSSEND TypeOfAddress WSSERVICE MtSellerCustomer

aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
::TypeOfAddress[1]:Code := "1"
::TypeOfAddress[1]:Description := "COMERCIAL"
aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
::TypeOfAddress[2]:Code := "2"
::TypeOfAddress[2]:Description := "COBRANCA"
aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
::TypeOfAddress[3]:Code := "3"
::TypeOfAddress[3]:Description := STR0012 //"ENTREGA"

Return(.T.)


/*/

Ŀ
Funo    GetTypeOfAAutor   Eduardo Riera          Data 04.08.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfPhone WSRECEIVE NULLPARAM WSSEND TypeOfPhone WSSERVICE MtSellerCustomer

aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[1]:Code := "1"
::TypeOfPhone[1]:Description := STR0010 //"COMERCIAL"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[2]:Code := "2"
::TypeOfPhone[2]:Description := STR0013 //"FAX"

Return(.T.)

/*/

Ŀ
Funo    BrwCustomeAutor   Eduardo Riera          Data 20.08.2003 
Ĵ
          Rotina de browse de clientes                                 
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do Vendedor                                    
          ExpN3: Tamanho da pagina a ser retornada                     
          ExpN4: Numero da Pagina                                      
          ExpC5: Expressao Like para o Nome do cliente                 
          ExpC6: Expressao Like para o Nome reduzido do cliente        
          ExpC7: Expressao a ser adicionada na clausura Where          
          ExpC8: Expressao em codebase da Expressao de Indice          
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                      
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                    
Ĵ
Tatiana       07/11/06112442Alterada a query para retirar o select * 
                            da tabela SA1 						   
Cleber M.     27/07/07129642Inclusao do GetUserFields() na query para
                            incluir os campos de usuario na consulta.
ٱ
 

/*/
WSMETHOD BrwCustomer WSRECEIVE UserCode,SellerCode,PageLen,PageFirst,NameLike,NickNameLike,QueryAddWhere,IndexKey WSSEND ListOfCustomer WSSERVICE MtSellerCustomer

Local aArea    := GetArea()				//Guarda a area atual
Local aStruSA1 := {}					//Array com estrutura do arquivo SA1
Local aGrupos  := {}					//Array com os grupos de representantes
Local lRetorno := .T.					//Retorno da funcao
Local lQuery   := .F.					//Indica se executou query
Local nX       := 0						//Usada em lacos For...Next
Local nY       := 0						//Usada em lacos For...Next
Local cAliasSA1:= "SA1"					//Alias do arquivo SA1
Local cArquivo := ""					//Nome do arquivo
Local cQuery   := ""					//Query a executar
Local cIn      := ""					//Variavel auxiliar
#IFNDEF TOP
Local bFilGrupo:= Nil					//Codeblock de filtro de grupo
Local cFilGrupo:= "{ || SA3->A3_COD=='" + ::SellerCode + "'"	//Condicao de filtragem de grupo
Local cFiltro  := ""					//Filtro a executar em DBF
#ELSE

#ENDIF

SA1->(dbSetOrder(1))

DEFAULT ::IndexKey  := SA1->(IndexKey())
DEFAULT ::PageLen   := 0
DEFAULT ::PageFirst := 0

//Ŀ
// Ponto de Entrada para adicionar filtro na query 
//
If ExistBlock("WSMT030")
	::QueryAddWhere := ExecBlock( "WSMT030", .F., .F., { 1, ::SellerCode } )
EndIf

If PrtChkUser(::UserCode,"MtSellerCustomer","BrwCustomer","SA3",::SellerCode)
	//Ŀ
	// Adiciona os grupos que estao abaixo deste representante                
	//
	If !Empty( aGrupos := FtReprEst( ::SellerCode ) )
		#IFDEF TOP
			cIn := "( "
		#ENDIF
		For nX := 1 to Len( aGrupos )
			#IFDEF TOP
				cIn       += "'" + aGrupos[ nX, 1 ] + "',"
			#ELSE
				cFilGrupo += ".OR.SA3->A3_GRPREP=='" + aGrupos[ nX, 1 ] + "'" 			
			#ENDIF
		Next nX  	
		#IFDEF TOP
			cIn := Left( cIn, Len( cIn ) - 1 ) + ") "	
		#ELSE
			cFilGrupo += " } "
			bFilGrupo := &cFilGrupo
			dbSelectArea("SA3")
			dbSetOrder(1)
			MsSeek(xFilial("SA3"))
			While !Eof() .AND. SA3->A3_FILIAL == xFilial("SA3")
				If Eval(bFilGrupo)
					cFiltro += ".OR.A1_VEND=='"+SA3->A3_COD+"'"
				EndIf
				dbSelectArea("SA3")
				dbSkip()			
			EndDo			
		#ENDIF

	EndIf	
	dbSelectArea("SA1")
	dbSetOrder(1)
	#IFDEF TOP
		lQuery    := .T.
		cAliasSA1 := "BRWCUSTOMER"
		aStruSA1  := SA1->(dbStruct())

		cQuery := "SELECT 	A1_FILIAL,	A1_COD,		A1_LOJA,	A1_NOME,	A1_NREDUZ,	A1_CGC,		A1_INSCR,	A1_INSCRM,	A1_HPAGE,"
		cQuery += "			A1_EMAIL,	A1_TABELA,	A1_COND,	A1_VEND,	A1_END,		A1_MUN,		A1_EST,		A1_CEP,"
		cQuery += "			A1_BAIRRO,	A1_ENDCOB,	A1_MUNC,	A1_ESTC,	A1_CEPC,	A1_ENDREC,	A1_MUNE,	A1_ESTE,"
		cQuery += "			A1_CEPE,	A1_BAIRROE,	A1_TEL,		A1_DDD,		A1_DDI,		A1_FAX "
		cQuery += GetUserFields("SA1")
		cQuery += "FROM "+RetSqlName("SA1")+" SA1 "
		cQuery += "WHERE "
		cQuery += "SA1.A1_FILIAL='"+xFilial("SA1")+"' AND "
		If !Empty(::NameLike)
			cQuery += "SA1.A1_NOME LIKE '%"+::NameLike+"%' AND "
		EndIf
		If !Empty(::NickNameLike)
			cQuery += "SA1.A1_NREDUZ LIKE '%"+::NickNameLike+"%' AND "
		EndIf
		cQuery += "(SA1.A1_VEND='"+Space(Len(SA1->A1_VEND))+"' OR SA1.A1_VEND IN("
		cQuery += "SELECT A3_COD "
		cQuery += "FROM "+RetSqlName("SA3")+" SA3 "
		cQuery += "WHERE SA3.A3_FILIAL='"+xFilial("SA3")+"' AND "
		cQuery += "SA3.A3_COD = '"+::SellerCode+"' AND "		
		If !Empty(cIn)
			cQuery += "SA3.A3_GRPREP = "+cIN+" AND "
		EndIf
		cQuery += "SA3.D_E_L_E_T_=' ' )) AND "
		cQuery += "SA1.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
		cQuery += "ORDER BY "+SqlOrder(::IndexKey)

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSA1)

		For nX := 1 To Len(aStruSA1)
			If aStruSA1[nX][2]<>"C" .And. FieldPos(aStruSA1[nX][1])<>0
				TcSetField(cAliasSA1,aStruSA1[nX][1],aStruSA1[nX][2],aStruSA1[nX][3],aStruSA1[nX][4])
			EndIf
		Next nx

	#ELSE
		cArquivo := CriaTrab(,.F.)
		If Empty(cFiltro)
			cFiltro  := "A1_FILIAL='"+xFilial("SA1")+"' .AND. (A1_VEND=='"+Space(Len(SA1->A1_VEND))+"'.OR.A1_VEND=='"+::SellerCode+"')"
		Else
			cFiltro  := "A1_FILIAL='"+xFilial("SA1")+"' .AND. (A1_VEND=='"+Space(Len(SA1->A1_VEND))+"'.OR.A1_VEND=='"+::SellerCode+"'"+cFiltro+")"
		EndIf
		
		If ExistBlock("WSMT030")
			cFiltro := ExecBlock( "WSMT030", .F., .F., { 2, ::SellerCode } )
		EndIf
		
		IndRegua("SA1",cArquivo,::IndexKey,,cFiltro)
		dbGotop()
	#ENDIF
	nX := 0
	While !Eof() .AND. (cAliasSA1)->A1_FILIAL=xFilial("SA1")
		If ( (Empty(::NameLike) .OR. ::NameLike$(cAliasSA1)->A1_NOME) .AND.;
				(Empty(::NickNameLike) .OR. ::NickNameLike$(cAliasSA1)->A1_NREDUZ) )
			nY++
			If ::PageFirst==0 .OR. nY >= ::PageFirst
				aadd(::ListOfCustomer,WsClassNew("GenericView2"))
				nX++				
				::ListOfCustomer[nX]:Code        := (cAliasSA1)->A1_COD
				::ListOfCustomer[nX]:Unit        := (cAliasSA1)->A1_LOJA
				::ListOfCustomer[nX]:Description := (cAliasSA1)->A1_NOME
				UserFields("SA1",@::ListOfCustomer[nX]:UserFields,cAliasSA1)
				If nX >= ::PageLen .AND. ::PageLen <> 0
					Exit
				EndIf
			EndIf
		EndIf
		dbSelectArea(cAliasSA1)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasSA1)
		dbCloseArea()
		dbSelectArea("SA1")
	Else
		RetIndex("SA1")
		FErase(cArquivo+OrdBagExt())	
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)


/*/


Ŀ
Funo    WSMAT030   Autor Eduardo Riera           Data 05.01.2004  
Ĵ
Descrio  Web Service responsavel pela manutencao das informacoes      
          de clientes                                                   
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        Web Service                                                  
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service                                                
//
WSSERVICE MtSystemCustomer      DESCRIPTION STR0022 NAMESPACE "http://webservices.microsiga.com.br/mtsystemcustomer.apw" //"Servio de consulta e manuteno das informaes da tabela de clientes<br><br><i>Este servio tem por objetivo possibilitar a consulta, incluso, atualizao e excluso das informaes da tabela de clientes pelos usurios habilitados.</i>"
WSDATA UserCode                 AS String
WSDATA CustomerID               AS String
WSDATA Customer                 As CustomerView
WSDATA Header                   As Array Of BrwHeader
WSDATA HeaderType               As String
WSDATA CustomerCode             As String
WSDATA TypeOfAddress            As Array Of GenericStruct
WSDATA TypeOfPhone              As Array Of GenericStruct
WSDATA ListOfCustomer           As Array Of GenericView2 OPTIONAL
WSDATA PageLen                  AS Integer OPTIONAL
WSDATA PageFirst                AS Integer OPTIONAL
WSDATA NameLike                 As String OPTIONAL
WSDATA NickNameLike             As String OPTIONAL
WSDATA QueryAddWhere            As String OPTIONAL
WSDATA IndexKey                 As String OPTIONAL

WSMETHOD GetHeader        DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
WSMETHOD GetTypeOfAddress DESCRIPTION STR0003 //"Mtodo que descreve os tipos de endereos"
WSMETHOD GetTypeOfPhone   DESCRIPTION STR0004 //"Mtodo que descreve os tipos de telefones"
WSMETHOD GetCustomer      DESCRIPTION STR0017 //"Mtodo de consulta as informaes de cliente"
WSMETHOD BrwCustomer      DESCRIPTION STR0023 //"Mtodo de listagem das informaes de clientes. Este mtodo demonstra todos os clientes existentes. A listagem demonstra algumas informaes do cliente sendo necessrio a utilizao do mtodo GetCustomer para complementar as informaes. Recomenda-se que a utilizao conjunta destes mtodos seja feita com prudncia, pois poder acarretar sobrecarga na linha de transmisso"
WSMETHOD PutCustomer      DESCRIPTION STR0019 //"Mtodo de incluso e atualizao das informaes de cliente. Este mtodo necessita que todas as informaes sejam passadas, mesmo as que no sero alteradas, uma vez que todas as informaes sero sobrepostas"
WSMETHOD DelCustomer      DESCRIPTION STR0020 //"Mtodo de excluso das informaes de cliente. Este mtodo necessita apenas da identificao do cliente para efetuar a excluso"
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 29.05.2003 
Ĵ
          Rotina de recuperacao dos headers deste WS                   
                                                                       
Ĵ
ParametrosExpC1: Nome da Estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtSystemCustomer
Local nX := 0

::Header := MtHeader(::HeaderType)

If Empty(::Header)
	::Header := {}
	aadd(::Header,WSClassNew("BrwHeader"))
	nX++
	::Header[nX]:HeaderTitle  := RetTitle("A1_COD")
	::Header[nX]:HeaderField   := "CODE"
	::Header[nX]:HeaderPicture := SX3->X3_PICTURE
	::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
	::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
	::Header[nX]:HeaderType    := SX3->X3_TIPO
	::Header[nX]:HeaderComboBox:= X3Cbox()
	::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)

	aadd(::Header,WSClassNew("BrwHeader"))
	nX++
	::Header[nX]:HeaderTitle  := RetTitle("A1_NOME")
	::Header[nX]:HeaderField   := "DESCRIPTION"
	::Header[nX]:HeaderPicture := SX3->X3_PICTURE
	::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
	::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
	::Header[nX]:HeaderType    := SX3->X3_TIPO
	::Header[nX]:HeaderComboBox:= X3Cbox()	
	::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
EndIf
Return(.T.)

/*/

Ŀ
Funo    GetCustomeAutor   Eduardo Riera          Data 05.01.2004 
Ĵ
          Rotina de obtencao dos dados de clientes                     
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Cliente                                               
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve os dados de um cliente                   
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetCustomer WSRECEIVE UserCode,CustomerID WSSEND Customer WSSERVICE MtSystemCustomer

Local aArea    := GetArea()
Local lRetorno := .T.
Local cCliente := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja    := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local nX       := 0

If PrtChkUser(::UserCode,"MtSystemCustomer","GetCustomer","USER",::UserCode)
	dbSelectArea("SA1")
	dbSetOrder(1)
	If MsSeek(xFilial("SA1")+cCliente+cLoja)
		::Customer:CustomerCode     := SA1->A1_COD
		::Customer:UnitCustomerCode := SA1->A1_LOJA
		::Customer:Name             := SA1->A1_NOME
		::Customer:NickName         := SA1->A1_NREDUZ
		::Customer:People	    	:= SA1->A1_PESSOA
		::Customer:FederalID        := SA1->A1_CGC
		::Customer:StateID          := SA1->A1_INSCR
		::Customer:DistrictID       := SA1->A1_INSCRM
		::Customer:HomePage         := SA1->A1_HPAGE
		::Customer:Email            := SA1->A1_EMAIL
		::Customer:PriceTableCode   := SA1->A1_TABELA
		::Customer:PaymentPlanCode  := SA1->A1_COND
		::Customer:SellerCode       := SA1->A1_VEND
		::Customer:Tipo			    := SA1->A1_TIPO
		UserFields("SA1",@::Customer:UserFields)

		::Customer:Addresses := {}
		nX := 0
		If !Empty(SA1->A1_END)
			aadd(::Customer:Addresses,WsClassNew("AddressView"))
			nX++
			::Customer:Addresses[nX]:TypeOfAddress := "1"
			::Customer:Addresses[nX]:Address       := FisGetEnd(SA1->A1_END)[1]
			::Customer:Addresses[nX]:AddressNumber := AllTrim(Str(FisGetEnd(SA1->A1_END)[2],8))
			::Customer:Addresses[nX]:District      := SA1->A1_MUN
			::Customer:Addresses[nX]:State         := SA1->A1_EST
			::Customer:Addresses[nX]:ZipCode       := SA1->A1_CEP
			::Customer:Addresses[nX]:Zone          := SA1->A1_BAIRRO
		EndIf

		If !Empty(SA1->A1_ENDCOB)

			aadd(::Customer:Addresses,WsClassNew("AddressView"))
			nX++
			::Customer:Addresses[nX]:TypeOfAddress := "2"
			::Customer:Addresses[nX]:Address       := FisGetEnd(SA1->A1_ENDCOB)[1]
			::Customer:Addresses[nX]:AddressNumber := AllTrim(Str(FisGetEnd(SA1->A1_ENDCOB)[2],8))
			::Customer:Addresses[nX]:District      := SA1->A1_MUNC
			::Customer:Addresses[nX]:State         := SA1->A1_ESTC
			::Customer:Addresses[nX]:ZipCode       := SA1->A1_CEPC
			::Customer:Addresses[nX]:Zone          := ""
		EndIf

		If !Empty(SA1->A1_ENDREC)		
			aadd(::Customer:Addresses,WsClassNew("AddressView"))
			nX++
			::Customer:Addresses[nX]:TypeOfAddress:= "3"
			::Customer:Addresses[nX]:Address       := FisGetEnd(SA1->A1_ENDREC)[1]
			::Customer:Addresses[nX]:AddressNumber := AllTrim(Str(FisGetEnd(SA1->A1_ENDREC)[2],8))
			::Customer:Addresses[nX]:District      := SA1->A1_MUNE
			::Customer:Addresses[nX]:State         := SA1->A1_ESTE
			::Customer:Addresses[nX]:ZipCode       := SA1->A1_CEPE
			::Customer:Addresses[nX]:Zone          := SA1->A1_BAIRROE
		EndIf

		::Customer:Phones := {}
		nX := 0
		If !Empty(SA1->A1_TEL)
			aadd(::Customer:Phones,WsClassNew("PhoneView"))
			nX++
			::Customer:Phones[nX]:TypeOfPhone     := "1"
			::Customer:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA1->A1_TEL,SA1->A1_DDD,SA1->A1_DDI)[3],15))
			::Customer:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA1->A1_TEL,SA1->A1_DDD,SA1->A1_DDI)[2],15))
			::Customer:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA1->A1_TEL,SA1->A1_DDD,SA1->A1_DDI)[1],15))

			If ::Customer:Phones[nX]:CountryAreaCode == "0"
				::Customer:Phones[nX]:CountryAreaCode := NIL
			EndIf

			If ::Customer:Phones[nX]:LocalAreaCode == "0"
				::Customer:Phones[nX]:LocalAreaCode := NIL
			EndIf
		EndIf

		If !Empty(SA1->A1_FAX)
			aadd(::Customer:Phones,WsClassNew("PhoneView"))
			nX++
			::Customer:Phones[nX]:TypeOfPhone     := "2"
			::Customer:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA1->A1_FAX,SA1->A1_DDD,SA1->A1_DDI)[3],15))
			::Customer:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA1->A1_FAX,SA1->A1_DDD,SA1->A1_DDI)[2],15))
			::Customer:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA1->A1_FAX,SA1->A1_DDD,SA1->A1_DDI)[1],15))

			If ::Customer:Phones[nX]:CountryAreaCode == "0"
				::Customer:Phones[nX]:CountryAreaCode := NIL
			EndIf

			If ::Customer:Phones[nX]:LocalAreaCode == "0"
				::Customer:Phones[nX]:LocalAreaCode := NIL
			EndIf
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("GETCUSTOMER",STR0007) //"Cliente nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    PutCustomeAutor   Eduardo Riera          Data 29.05.2003 
Ĵ
          Rotina de atualizacao dos dados de clientes                  
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpO2: Estrutura a ser gravada                               
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo atualiza os dados de um cliente                  
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
Ĵ
Analista   Data/Bops/Ver Manutencao Efetuada                      	   
Ĵ
Cleber M. 04/06/07126890Correcao nos uso dos campos Cliente/Loja re- 
                        cebidos pelo metodo respeitando sempre o ta- 
                        manho definido no SX3.                       
ٱ


/*/
WSMETHOD PutCustomer WSRECEIVE UserCode,Customer WSSEND CustomerCode WSSERVICE MtSystemCustomer

Local aArea    := GetArea()						//Guarda a area atual
Local aDados   := {}							//Array com os dados a serem processados
Local aErro    := {}							//Array com a descricao do erro da ExecAuto
Local lRetorno := .T.							//Indica o retorno da funcao
Local cErro    := ""							//String com o erro
Local nX       := 0								//Usada em lacos For...Next
Local cCliente		:= ""						//Cod. Cliente 
Local cLoja			:= ""						//Loja
Local nTamA1_COD	:= TamSX3("A1_COD")[1]		//Tamanho do campo A1_COD	
Local nTamA1_LOJA	:= TamSX3("A1_LOJA")[1]		//Tamanho do campo A1_LOJA
PRIVATE lMsErroAuto := .F.						//Indica se houve erro durante MsExecAuto
PRIVATE lAutoErrNoFile := .T.					//Indica se ocorreu erro no arquivo

If PrtChkUser(::UserCode,"MtSystemCustomer","PutCustomer","USER",::UserCode)
	cCliente := PadR(::Customer:CustomerCode, nTamA1_COD)
	cLoja := PadR(::Customer:UnitCustomerCode, nTamA1_LOJA)
	If( !Empty( cCliente ) )
		aadd(aDados,{"A1_COD",cCliente,})
	EndIf
	If( !Empty( cLoja ) )
		aadd(aDados,{"A1_LOJA",cLoja,})
	EndIf
	aadd(aDados,{"A1_NOME",::Customer:Name,})
	aadd(aDados,{"A1_NREDUZ",::Customer:NickName,})
	aadd(aDados,{"A1_PESSOA",::Customer:People,})
	aadd(aDados,{"A1_CGC",::Customer:FederalID,})
	aadd(aDados,{"A1_INSCR",::Customer:StateID,})
	aadd(aDados,{"A1_INSCRM",::Customer:DistrictID,})
	aadd(aDados,{"A1_HPAGE",::Customer:HomePage,})
	aadd(aDados,{"A1_EMAIL",::Customer:Email,})
	If( !Empty( ::Customer:Tipo ) )
		aadd(aDados,{"A1_TIPO",::Customer:Tipo,})
	EndIf
	
	For nX := 1 To Len(::Customer:Addresses)
		Do Case
		Case ::Customer:Addresses[nX]:TypeOfAddress == "1"
			aadd(aDados,{"A1_END",AllTrim(::Customer:Addresses[nX]:Address)+", "+AllTrim(::Customer:Addresses[nX]:AddressNumber),})
			aadd(aDados,{"A1_MUN",::Customer:Addresses[nX]:District,})
			aadd(aDados,{"A1_EST",::Customer:Addresses[nX]:State,})
			aadd(aDados,{"A1_CEP",::Customer:Addresses[nX]:ZipCode,})
			aadd(aDados,{"A1_BAIRRO",::Customer:Addresses[nX]:Zone,})
		Case ::Customer:Addresses[nX]:TypeOfAddress == "2"
			If Empty(::Customer:Addresses[nX]:Address)
				aadd(aDados,{"A1_ENDCOB","",Nil})
			Else
				aadd(aDados,{"A1_ENDCOB",AllTrim(::Customer:Addresses[nX]:Address)+", "+AllTrim(::Customer:Addresses[nX]:AddressNumber),})
			EndIf
			aadd(aDados,{"A1_MUNC",::Customer:Addresses[nX]:District,})
			aadd(aDados,{"A1_ESTC",::Customer:Addresses[nX]:State,})
			aadd(aDados,{"A1_CEPC",::Customer:Addresses[nX]:ZipCode,})
		Case ::Customer:Addresses[nX]:TypeOfAddress == "3"
			If Empty(::Customer:Addresses[nX]:Address)
				aadd(aDados,{"A1_ENDREC","",Nil})
			Else
				aadd(aDados,{"A1_ENDREC",AllTrim(::Customer:Addresses[nX]:Address)+", "+AllTrim(::Customer:Addresses[nX]:AddressNumber),})
			EndIf
			aadd(aDados,{"A1_MUNE",::Customer:Addresses[nX]:District,})
			aadd(aDados,{"A1_ESTE",::Customer:Addresses[nX]:State,})
			aadd(aDados,{"A1_CEPE",::Customer:Addresses[nX]:ZipCode,})
			aadd(aDados,{"A1_BAIRROE",::Customer:Addresses[nX]:Zone,})
		OtherWise
			lRetorno := .F.
			SetSoapFault("PUTCUSTOMER",STR0008) //"TypeOfAddress invalido"
		EndCase
	Next nX

	For nX := 1 To Len(::Customer:Phones)
		Do Case
		Case ::Customer:Phones[nX]:TypeOfPhone == "1"
				If ACJ->(LastRec()) == 0
					aadd(aDados,{"A1_TEL",::Customer:Phones[nX]:PhoneNumber,})	
				Else				
					aadd(aDados,{"A1_TEL",::Customer:Phones[nX]:PhoneNumber,})
					aadd(aDados,{"A1_DDD",::Customer:Phones[nX]:LocalAreaCode,})
					aadd(aDados,{"A1_DDI",::Customer:Phones[nX]:CountryAreaCode,})	
				EndIf
		Case ::Customer:Phones[nX]:TypeOfPhone == "2"
			aadd(aDados,{"A1_FAX",::Customer:Phones[nX]:PhoneNumber,})
		OtherWise                                             
			lRetorno := .F.
			SetSoapFault("PUTCUSTOMER",STR0009)				 //"TypeOfPhone invalido"
		EndCase
	Next nX
	If lRetorno
		//Ŀ
		//Gravacao ao do cadastro de clientes                                     
		//
		PutUserFields("SA1",@::Customer:UserFields,@aDados)
		//Ŀ
		//Atualizacao do cadastro de clientes                                     
		//
		aDados := WsAutoOpc(aDados)
		dbSelectArea("SA1")
		dbSetOrder(1)                   
		If !Empty( cCliente ) .AND. !Empty( cLoja ) .AND. MsSeek(xFilial("SA1")+cCliente+cLoja)
			Mata030(aDados,4)
		Else
			Mata030(aDados,3)
		EndIf
		::CustomerCode := SA1->A1_COD
		If lMsErroAuto
			aErro := GetAutoGRLog()
			cErro := ""
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX	
			SetSoapFault("PUTCUSTOMER",cErro)
			lRetorno := .F.
		EndIf
	EndIf

Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetTypeOfAAutor   Eduardo Riera          Data 04.08.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfAddress WSRECEIVE NULLPARAM WSSEND TypeOfAddress WSSERVICE MtSystemCustomer

aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
::TypeOfAddress[1]:Code := "1"
::TypeOfAddress[1]:Description := STR0010 //"COMERCIAL"
aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
::TypeOfAddress[2]:Code := "2"
::TypeOfAddress[2]:Description := STR0011 //"COBRANCA"
aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
::TypeOfAddress[3]:Code := "3"
::TypeOfAddress[3]:Description := STR0012 //"ENTREGA"

Return(.T.)


/*/

Ŀ
Funo    GetTypeOfAAutor   Eduardo Riera          Data 04.08.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfPhone WSRECEIVE NULLPARAM WSSEND TypeOfPhone WSSERVICE MtSystemCustomer

aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[1]:Code := "1"
::TypeOfPhone[1]:Description := STR0010 //"COMERCIAL"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[2]:Code := "2"
::TypeOfPhone[2]:Description := STR0013 //"FAX"

Return(.T.)

/*/

Ŀ
Funo    BrwCustomeAutor   Eduardo Riera          Data 20.08.2003 
Ĵ
          Rotina de demonstracao do Browse de Clientes                 
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpN2: Tamanho da pagina a ser retornada                     
          ExpN3: Numero da Pagina                                      
          ExpC4: Expressao Like para o Nome do cliente                 
          ExpC5: Expressao Like para o Nome reduzido do cliente        
          ExpC6: Expressao a ser adicionada na clausura Where          
          ExpC7: Expressao em codebase da Expressao de Indice          
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD BrwCustomer WSRECEIVE UserCode,PageLen,PageFirst,NameLike,NickNameLike,QueryAddWhere,IndexKey WSSEND ListOfCustomer WSSERVICE MtSystemCustomer

Local aArea    := GetArea()

Local lRetorno := .T.
Local lQuery   := .F.
Local nX       := 0
Local nY       := 0
Local cAliasSA1:= "SA1"
Local cArquivo := ""
#IFNDEF TOP
Local cFiltro  := ""
#ELSE
Local aStruSA1 := {}
Local cQuery   := ""
#ENDIF

SA1->(dbSetOrder(1))

DEFAULT ::IndexKey  := SA1->(IndexKey())
DEFAULT ::PageLen   := 0
DEFAULT ::PageFirst := 0

If PrtChkUser(::UserCode,"MtSystemCustomer","BrwCustomer","USER",::UserCode)
	//Ŀ
	// Adiciona os grupos que estao abaixo deste representante                
	//
	dbSelectArea("SA1")
	dbSetOrder(1)
	#IFDEF TOP
		lQuery    := .T.
		cAliasSA1 := "BRWCUSTOMER"
		aStruSA1  := SA1->(dbStruct())

		cQuery := "SELECT 	A1_FILIAL,	A1_COD,		A1_LOJA,	A1_NOME,	A1_NREDUZ,	A1_PESSOA,	A1_CGC,		A1_INSCR,	A1_INSCRM,	A1_HPAGE,"
		cQuery += "			A1_EMAIL,	A1_TABELA,	A1_COND,	A1_VEND,	A1_END,		A1_MUN,		A1_EST,		A1_CEP,"
		cQuery += "			A1_BAIRRO,	A1_ENDCOB,	A1_MUNC,	A1_ESTC,	A1_CEPC,	A1_ENDREC,	A1_MUNE,	A1_ESTE,"
		cQuery += "			A1_CEPE,	A1_BAIRROE,	A1_TEL,		A1_DDD,		A1_DDI,		A1_FAX,		A1_TIPO "
		cQuery += GetUserFields("SA1")
		cQuery += "FROM "+RetSqlName("SA1")+" SA1 "
		cQuery += "WHERE "
		cQuery += "SA1.A1_FILIAL='"+xFilial("SA1")+"' AND "
		If !Empty(::NameLike)
			cQuery += "SA1.A1_NOME LIKE '%"+::NameLike+"%' AND "
		EndIf
		If !Empty(::NickNameLike)
			cQuery += "SA1.A1_NREDUZ LIKE '%"+::NickNameLike+"%' AND "
		EndIf		
		cQuery += "SA1.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
		cQuery += "ORDER BY "+SqlOrder(::IndexKey)

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSA1)

		For nX := 1 To Len(aStruSA1)
			If aStruSA1[nX][2]<>"C"
				TcSetField(cAliasSA1,aStruSA1[nX][1],aStruSA1[nX][2],aStruSA1[nX][3],aStruSA1[nX][4])
			EndIf
		Next nx

	#ELSE
		cArquivo := CriaTrab(,.F.)
		If Empty(cFiltro)
			cFiltro  := "A1_FILIAL='"+xFilial("SA1")+"' "
		Else
			cFiltro  := "A1_FILIAL='"+xFilial("SA1")+"' "
		EndIf
		IndRegua("SA1",cArquivo,::IndexKey,,cFiltro)
		dbGotop()
	#ENDIF
	nX := 0
	While !Eof() .AND. (cAliasSA1)->A1_FILIAL=xFilial("SA1")
		If ( (Empty(::NameLike) .OR. ::NameLike$(cAliasSA1)->A1_NOME) .AND.;
				(Empty(::NickNameLike) .OR. ::NickNameLike$(cAliasSA1)->A1_NREDUZ) )
			nY++
			If ::PageFirst==0 .OR. nY >= ::PageFirst
				aadd(::ListOfCustomer,WsClassNew("GenericView2"))
				nX++				
				::ListOfCustomer[nX]:Code        := (cAliasSA1)->A1_COD
				::ListOfCustomer[nX]:Unit        := (cAliasSA1)->A1_LOJA
				::ListOfCustomer[nX]:Description := (cAliasSA1)->A1_NOME
				UserFields("SA1",@::ListOfCustomer[nX]:UserFields,cAliasSA1)
				If nX >= ::PageLen .AND. ::PageLen <> 0
					Exit
				EndIf
			EndIf
		EndIf
		dbSelectArea(cAliasSA1)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasSA1)
		dbCloseArea()
		dbSelectArea("SA1")
	Else
		RetIndex("SA1")
		FErase(cArquivo+OrdBagExt())	
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    DelCustomeAutor   Eduardo Riera          Data 23.01.2004 
Ĵ
          Rotina de exclusao dos dados de clientes                     
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpO2: Estrutura a ser gravada                               
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo exclui os dados de um cliente                    
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD DelCustomer WSRECEIVE UserCode,Customer WSSEND CustomerCode WSSERVICE MtSystemCustomer

Local aArea    := GetArea()
Local aDados   := {}
Local aErro    := {}
Local lRetorno := .T.
Local cErro    := ""
Local nX       := 0
PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"MtSystemCustomer","DelCustomer")
	If( !Empty( ::Customer:CustomerCode) )
		aadd(aDados,{"A1_COD",::Customer:CustomerCode,})
	EndIf
	If( !Empty( ::Customer:UnitCustomerCode) )
		aadd(aDados,{"A1_LOJA",::Customer:UnitCustomerCode,})
	EndIf
	If lRetorno
		//Ŀ
		//Gravacao ao do cadastro de clientes                                     
		//
		PutUserFields("SA1",@::Customer:UserFields,@aDados)
		//Ŀ
		//Atualizacao do cadastro de clientes                                     
		//
		aDados := WsAutoOpc(aDados)
		dbSelectArea("SA1")
		dbSetOrder(1)                   
		If !Empty( ::Customer:CustomerCode ) .AND. !Empty( ::Customer:UnitCustomerCode ) .AND. MsSeek(xFilial("SA1")+::Customer:CustomerCode+::Customer:UnitCustomerCode)
			Mata030(aDados,5)
		Else
			SetSoapFault("DELCUSTOMER",STR0021) //"Cliente no localizado"
			lRetorno := .F.
		EndIf
		::CustomerCode := SA1->A1_COD
		If lMsErroAuto
			aErro := GetAutoGRLog()
			cErro := ""
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX	
			SetSoapFault("DELCUSTOMER",cErro)
			lRetorno := .F.
		EndIf
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

Function wsmat030()
Return
