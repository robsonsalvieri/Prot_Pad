#INCLUDE "wsmat040.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
/*/


Ŀ
Funo    WSMAT040   Autor Eduardo Riera           Data 01.09.2003  
Ĵ
Descrio  Web Service responsavel pela manutencao das informacoes      
          de vendedores                                                 
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        Web Service                                                  
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service                                                
//
WSSERVICE MtSeller              DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/mtseller.apw" //"Servio de consulta e atualizao da tabela de representantes comerciais"
WSDATA UserCode                 AS String
WSDATA SellerCode               AS String
WSDATA Commission               As Array Of CommissionView OPTIONAL
WSDATA RegisterDateFrom         As Date OPTIONAL
WSDATA RegisterDateTo           As Date OPTIONAL
WSDATA ReceivableDateFrom       As Date OPTIONAL
WSDATA ReceivableDateTo         As Date OPTIONAL
WSDATA Seller                   As SellerView
WSDATA Header                   As Array Of BrwHeader
WSDATA HeaderType               As String
WSDATA SellerId                 As String
WSDATA TypeOfPhone              As Array Of GenericStruct
WSDATA QueryAddWhere            As String OPTIONAL
WSDATA SellerPassword           As String
WSDATA Branchs                  As Array Of GenericStruct

WSMETHOD GetHeader        DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
WSMETHOD GetTypeOfPhone   DESCRIPTION STR0003 //"Mtodo que descreve os tipo de telefones"
WSMETHOD GetSeller        DESCRIPTION STR0004 //"Mtodo de consulta as informaes cadastrais do representante comercial"
WSMETHOD GetCommission    DESCRIPTION STR0005 //"Mtodo de consulta a relao de comisses de um perodo. "
WSMETHOD PutSeller        DESCRIPTION STR0006 //"Mtodo de atualizao das informaes cadastrais do representante comercial"
WSMETHOD GetLogin         DESCRIPTION STR0007 //"Mtodo de Login do representante comercial. Este mtodo verifica a senha na tabela de informaes cadastrais do representante comercial, retornando a lista de empresas que o representante possui acesso. "
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 01.09.2003 
Ĵ
          Rotina de recuperacao dos headers deste WS                   
                                                                       
Ĵ
ParametrosExpC1: Nome da estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtSeller

::Header := MtHeader(::HeaderType)

Return(.T.)

/*/

Ŀ
Funo    GetSeller Autor   Eduardo Riera          Data 01.09.2003 
Ĵ
          Rotina de obtencao dos dados de vendedores                   
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo de identificacao do vendedor                   
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve os dados de um vendedor                  
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetSeller WSRECEIVE UserCode,SellerCode WSSEND Seller WSSERVICE MtSeller

Local aArea    := GetArea()
Local lRetorno := .T.
Local nX       := 0

If PrtChkUser(::UserCode,"MTSELLER","GetSeller","SA3",::SellerCode)
	dbSelectArea("SA3")
	dbSetOrder(1)
	If MsSeek(xFilial("SA3")+::SellerCode)	
		::Seller:SellerCode  := SA3->A3_COD
		::Seller:Name        := SA3->A3_NOME
		::Seller:NickName    := SA3->A3_NREDUZ
		::Seller:FederalID   := SA3->A3_CGC
		::Seller:StateID     := SA3->A3_INSCR
		::Seller:DistrictID  := SA3->A3_INSCRM
		::Seller:HomePage    := SA3->A3_HPAGE
		::Seller:Email       := SA3->A3_EMAIL
		::Seller:SalesSupervisorCode := SA3->A3_SUPER
		::Seller:SalesManagerCode    := SA3->A3_GEREN
		UserFields("SA3",@::Seller:UserFields)

		::Seller:Addresses := WsClassNew("AddressView")
		::Seller:Addresses:TypeOfAddress := "1"
		::Seller:Addresses:Address       := FisGetEnd(SA3->A3_END)[1]
		::Seller:Addresses:AddressNumber := AllTrim(Str(FisGetEnd(SA3->A3_END)[2],8))
		::Seller:Addresses:District      := SA3->A3_MUN
		::Seller:Addresses:State         := SA3->A3_EST
		::Seller:Addresses:ZipCode       := SA3->A3_CEP
		::Seller:Addresses:Zone          := SA3->A3_BAIRRO

		::Seller:Phones := {}
		nX := 0
		If !Empty(SA3->A3_TEL)
			aadd(::Seller:Phones,WsClassNew("PhoneView"))
			nX++
			::Seller:Phones[nX]:TypeOfPhone     := "1"
			::Seller:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA3->A3_TEL)[3],15))
			::Seller:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA3->A3_TEL)[2],15))
			::Seller:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA3->A3_TEL)[1],15))
			
			//Ŀ
			//Se a FisGetTel retornar zero pega o DDD e DDI direto 
			//dos seus respectivos campos.                         
			//
			If ::Seller:Phones[nX]:LocalAreaCode == "0"
				::Seller:Phones[nX]:LocalAreaCode := Alltrim(SA3->A3_DDDTEL)
			EndIf			
			If ::Seller:Phones[nX]:CountryAreaCode == "0"
				::Seller:Phones[nX]:CountryAreaCode := Alltrim(SA3->A3_DDI)
			EndIf						
			
		EndIf

		If !Empty(SA3->A3_FAX)
			aadd(::Seller:Phones,WsClassNew("PhoneView"))
			nX++
			::Seller:Phones[nX]:TypeOfPhone     := "2"
			::Seller:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA3->A3_FAX)[3],15))
			::Seller:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA3->A3_FAX)[2],15))
			::Seller:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA3->A3_FAX)[1],15))			
			
			//Ŀ
			//Se a FisGetTel retornar zero pega o DDD e DDI direto 
			//dos seus respectivos campos.                         
			//
			If ::Seller:Phones[nX]:LocalAreaCode == "0"
				::Seller:Phones[nX]:LocalAreaCode := Alltrim(SA3->A3_DDDTEL)
			EndIf			
			If ::Seller:Phones[nX]:CountryAreaCode == "0"
				::Seller:Phones[nX]:CountryAreaCode := Alltrim(SA3->A3_DDI)
			EndIf						
			
		EndIf	
	Else
		lRetorno := .F.
		SetSoapFault("GETSELLER",STR0008) //"Vendedor nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    PutSeller Autor   Eduardo Riera          Data 01.09.2003 
Ĵ
          Rotina de atualizacao dos dados de vendedor                  
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo de identificacao do vendedor                   
          ExpO3: Estrutura a ser gravada                               
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo atualiza os dados de um vendedor                 
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD PutSeller WSRECEIVE UserCode,SellerCode,Seller WSSEND SellerId WSSERVICE MtSeller

Local aArea    := GetArea()
Local aDados   := {}
Local aErro    := {}
Local lRetorno := .T.
Local cErro    := ""
Local nX       := 0
PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.

DEFAULT ::SellerId  := ""

If PrtChkUser(::UserCode,"MTSELLER","PutSeller","SA3",::SellerCode)
	If !Empty(::Seller:SellerCode)
		aadd(aDados,{"A3_COD",::Seller:SellerCode,})
	EndIf
	aadd(aDados,{"A3_NOME",::Seller:Name,})
	aadd(aDados,{"A3_NREDUZ",::Seller:NickName,})
	aadd(aDados,{"A3_CGC",::Seller:FederalID,})
	aadd(aDados,{"A3_INSCR",::Seller:StateID,})
	aadd(aDados,{"A3_INSCRM",::Seller:DistrictID,})
	aadd(aDados,{"A3_HPAGE",::Seller:HomePage,})
	aadd(aDados,{"A3_EMAIL",::Seller:Email,})
	aadd(aDados,{"A3_END",AllTrim(::Seller:Addresses:Address)+", "+AllTrim(::Seller:Addresses:AddressNumber),})
	aadd(aDados,{"A3_MUN",::Seller:Addresses:District,})
	aadd(aDados,{"A3_EST",::Seller:Addresses:State,})
	aadd(aDados,{"A3_CEP",::Seller:Addresses:ZipCode,})
	aadd(aDados,{"A3_BAIRRO",::Seller:Addresses:Zone,})		
	For nX := 1 To Len(::Seller:Phones)
		Do Case
		Case ::Seller:Phones[nX]:TypeOfPhone == "1"
			If !Empty( ::Seller:Phones[nX]:PhoneNumber )
				aadd(aDados,{"A3_TEL",::Seller:Phones[nX]:PhoneNumber,})
			EndIf
		Case ::Seller:Phones[nX]:TypeOfPhone == "2"
			If !Empty( ::Seller:Phones[nX]:PhoneNumber )
				aadd(aDados,{"A3_FAX",::Seller:Phones[nX]:PhoneNumber,})
			EndIf
		OtherWise
			lRetorno := .F.
			SetSoapFault("PUTSELLER",STR0009) //"TypeOfPhone invalido"
		EndCase
	Next nX
	If lRetorno
		//Ŀ
		//Variaveis de usuario                                                    
		//
		PutUserFields("SA3",@::Seller:UserFields,@aDados)
		//Ŀ
		//Atualizacao do cadastro de vendedores                                   
		//
		dbSelectArea("SA3")
		dbSetOrder(1)
		If MsSeek(xFilial("SA3")+::SellerCode) .And. !Empty(::SellerCode)
			Mata040(aDados,4)
		Else
			Mata040(aDados,3)
		EndIf
		::SellerId := SA3->A3_COD
		If lMsErroAuto
			aErro := GetAutoGRLog()
			cErro := ""
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX
			SetSoapFault("PUTSELLER",cErro)
			lRetorno := .F.
		EndIf
	EndIf

Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)


/*/

Ŀ
Funo    GetTypeOfAAutor   Eduardo Riera          Data 04.08.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfPhone WSRECEIVE NULLPARAM WSSEND TypeOfPhone WSSERVICE MtSeller

aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[1]:Code := "1"
::TypeOfPhone[1]:Description := STR0010 //"COMERCIAL"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[2]:Code := "2"
::TypeOfPhone[2]:Description := STR0011 //"FAX"

Return(.T.)

/*/

Ŀ
Funo    GetCommissAutor   Eduardo Riera          Data 01.09.2003 
Ĵ
          Rotina de retorno dos dados da comissao de um vendedor       
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo de identificacao do vendedor                   
          ExpD3: Emissao de                                            
          ExpD4: Emissao Ate                                           
          ExpD5: Vencto  de                                            
          ExpD6: Vencto  Ate                                           
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo retorna a comissao do vendedor em um periodo     
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                      
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                    
Ĵ
 Cleber M.    23/10/06111610-Inclusao do P.E. WSMT040A para filtro de
                            comissoes.                               
ٱ


/*/
WSMETHOD GetCommission WSRECEIVE UserCode,SellerCode,RegisterDateFrom,RegisterDateTo,ReceivableDateFrom,ReceivableDateTo,QueryAddWhere WSSEND Commission WSSERVICE MtSeller

Local aArea     := GetArea()	//Guarda a area atual
Local cQuery    := ""			//Conteudo da query a ser executada
Local cAliasSE3 := "SE3"		//Alias da tabela SE3
Local cAliasSA1 := "SA1"		//Alias da tabela SA1
Local cArquivo  := ""			//Arquivo de trabalho
Local lQuery    := .F.			//Indica se executou a query
Local lRetorno  := .T.			//Indica retorno da funcao
Local nX        := 0			//Variavel usada em lacos For...Next
Local aStruSE3  := {}			//Estrutura do arq. SE3

If Empty(::ReceivableDateFrom) .Or. Empty(::ReceivableDateTo)
	DEFAULT ::ReceivableDateFrom := FirstDay(dDataBase-30)
	DEFAULT ::ReceivableDateTo   := LastDay(dDataBase-30)
EndIf

If PrtChkUser(::UserCode,"MTSELLER","GetCommission","SA3",::SellerCode)
	dbSelectArea("SA1")
	dbSelectArea("SE3")
	dbSetOrder(1)

	aStruSE3  := SE3->(dbStruct())
	cAliasSE3 := "Getcommission"
	cAliasSA1 := cAliasSE3
	lQuery    := .T.

	cQuery := "SELECT SE3.*,A1_NOME,A1_NREDUZ "
	cQuery += "FROM "+RetSqlName("SE3")+ " SE3, "
	cQuery += RetSqlName("SA1")+ " SA1 "
	cQuery += "WHERE SE3.E3_FILIAL='"+xFilial("SE3")+"' AND "
	cQuery += "SE3.E3_VEND='"+::SellerCode+"' AND "
	If !Empty(::RegisterDateFrom)
		cQuery += "SE3.E3_EMISSAO>='"+Dtos(::RegisterDateFrom)+"' AND "
		cQuery += "SE3.E3_EMISSAO<='"+Dtos(::RegisterDateTo)+"' AND "
	EndIf
	If !Empty(::ReceivableDateFrom)
		cQuery += "SE3.E3_VENCTO>='"+Dtos(::ReceivableDateFrom)+"' AND "
		cQuery += "SE3.E3_VENCTO<='"+Dtos(::ReceivableDateTo)+"' AND "
	EndIf
	cQuery += "SE3.D_E_L_E_T_=' ' AND "
	cQuery += "SA1.A1_FILIAL = '"+xFilial("SA1")+"' AND "
	cQuery += "SA1.A1_COD = SE3.E3_CODCLI AND "
	cQuery += "SA1.A1_LOJA = SE3.E3_LOJA AND "
	
	//Ŀ
	// Ponto de Entrada para filtrar comissoes 
	//
	If ExistBlock("WSMT040A") 
		cQuery += ExecBlock( "WSMT040A", .F., .F., { 1 } )
	EndIf 
	
	cQuery += "SA1.D_E_L_E_T_=' ' "
	cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
	cQuery += "ORDER BY "+SqlOrder(SE3->(IndexKey()))

	cQuery := ChangeQuery(cQuery)
	MemoWrite("WSMAT040.SQL", cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE3)

	For nX := 1 To Len(aStruSE3)
		If aStruSE3[nX][2] <> "C"
			TcSetField(cAliasSE3,aStruSE3[nX][1],aStruSE3[nX][2],aStruSE3[nX][3],aStruSE3[nX][4])
		EndIf
	Next nX

	dbSelectArea(cAliasSE3)
	nX := 0
	While !Eof() .And. (cAliasSE3)->E3_FILIAL == xFilial("SE3")
		If nX == 0
			::Commission := {}
		EndIf
		aadd(::Commission,WsClassNew("CommissionView"))
		nX++
		If !lQuery
			dbSelectArea("SA1")
			dbSetOrder(1)
			MsSeek(xFilial("SA1")+(cAliasSE3)->E3_CODCLI+(cAliasSE3)->E3_LOJA)
		EndIf
		::Commission[nX]:SellerCode            := (cAliasSE3)->E3_VEND
		::Commission[nX]:CustomerCode          := (cAliasSE3)->E3_CODCLI+(cAliasSE3)->E3_LOJA
		::Commission[nX]:Name                  := (cAliasSA1)->A1_NOME
		::Commission[nX]:NickName              := (cAliasSA1)->A1_NREDUZ
		::Commission[nX]:Document              := (cAliasSE3)->E3_PREFIXO+(cAliasSE3)->E3_NUM+(cAliasSE3)->E3_PARCELA+(cAliasSE3)->E3_TIPO+(cAliasSE3)->E3_SEQ
		::Commission[nX]:SalesOrder            := (cAliasSE3)->E3_PEDIDO
		::Commission[nX]:RegisterDate          := (cAliasSE3)->E3_EMISSAO
		::Commission[nX]:ReceivableDate        := (cAliasSE3)->E3_VENCTO
		::Commission[nX]:PaymentDate           := (cAliasSE3)->E3_DATA
		::Commission[nX]:CommissionBase        := (cAliasSE3)->E3_BASE
		::Commission[nX]:CommissionPercentage  := (cAliasSE3)->E3_PORC
		::Commission[nX]:CommissionValue       := (cAliasSE3)->E3_COMIS
		::Commission[nX]:GeneratedBy           := IIF((cAliasSE3)->E3_BAIEMI=="B",STR0012,STR0013) //"PAGAMENTO"###"EMISSAO"
		
		dbSelectArea("SE1")
		dbSetOrder(1)
		If MsSeek(xFilial("SE1")+(cAliasSE3)->E3_PREFIXO+(cAliasSE3)->E3_NUM+(cAliasSE3)->E3_PARCELA+(cAliasSE3)->E3_TIPO)
			::Commission[nX]:BillRealDueDate := SE1->E1_VENCREA
		EndIf
		If (cAliasSE3)->E3_BAIEMI=="B"
			dbSelectArea("SE5")
			dbSetOrder(7)
			If MsSeek(xFilial("SE5")+(cAliasSE3)->E3_PREFIXO+(cAliasSE3)->E3_NUM+(cAliasSE3)->E3_PARCELA+(cAliasSE3)->E3_TIPO+(cAliasSE3)->E3_CODCLI+(cAliasSE3)->E3_LOJA+(cAliasSE3)->E3_SEQ)
				While !Eof() .And. xFilial("SE5") == SE5->E5_FILIAL .And.;
					(cAliasSE3)->E3_PREFIXO == SE5->E5_PREFIXO .And.;
					(cAliasSE3)->E3_NUM == SE5->E5_NUMERO .And.;
					(cAliasSE3)->E3_PARCELA == SE5->E5_PARCELA .And.;
					(cAliasSE3)->E3_TIPO == SE5->E5_TIPO .And.;
					(cAliasSE3)->E3_CODCLI == SE5->E5_CLIFOR .And.;
					(cAliasSE3)->E3_LOJA == SE5->E5_LOJA .And.;
					(cAliasSE3)->E3_SEQ == SE5->E5_SEQ
					
					If (SE5->E5_RECPAG=="R" .And. SE5->E5_TIPODOC<>"ES") .Or. (SE5->E5_RECPAG=="P" .And. SE5->E5_TIPODOC=="ES")
						::Commission[nX]:BillPaidDate := SE5->E5_DATA
					EndIf
									
					dbSelectArea("SE5")
					dbSkip()
				EndDo
			EndIf
		EndIf
		
		UserFields("SE3",@::Commission[nX]:UserFields)

		dbSelectArea(cAliasSE3)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasSE3)
		dbCloseArea()
		dbSelectArea("SE3")
	Else
		RetIndex("SE3")
		dbClearFilter()
		FErase(cArquivo+OrdBagExt())
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetLogin  Autor   Eduardo Riera          Data 16.09.2003 
Ĵ
          Rotina de validacao do login do vendedor pelo portal         
                                                                       
Ĵ
ParametrosExpC1: Codigo do vendedor                                    
          ExpC2: Senha do vendedor                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo retorna a comissao do vendedor em um periodo     
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetLogin WSRECEIVE SellerCode,SellerPassWord WSSEND Branchs WSSERVICE MtSeller

Local aArea		:= GetArea()
Local lRetorno	:= .T.
Local cBkpAnt	:= cFilAnt
Local nX		:= 0
Local nY		:= 0
Local aSM0		:= FWLoadSM0()

DBSelectArea("SA3")
DBSetOrder(1)

For nY := 1 To Len(aSM0)
	If cEmpAnt == aSM0[nY,1]
		cFilAnt := aSM0[nY,2]
		If MsSeek(xFilial("SA3")+::SellerCode)
			If SA3->A3_SENHA == ::SellerPassWord
				If nX == 0
					::Branchs := {}
				EndIf
				nX++
				aadd(::Branchs,WsClassNew("GenericStruct"))
				::Branchs[nX]:Code := aSM0[nY,2]
				::Branchs[nX]:Description := aSM0[nY,7] 
			EndIf
		EndIf
	EndIf
Next nY

If nX == 0 
	lRetorno := .F.
EndIf

cFilAnt := cBkpAnt

RestArea(aArea)
Return(lRetorno)



/*/


Ŀ
Funo    WSMAT040   Autor Eduardo Riera           Data 01.09.2003  
Ĵ
Descrio  Web Service responsavel pela manutencao das informacoes      
          de vendedores                                                 
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        Web Service                                                  
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service                                                
//
WSSERVICE MtCustomerSeller      DESCRIPTION STR0014 NAMESPACE "http://webservices.microsiga.com.br/mtcustomerseller.apw" //"Servio de consulta da tabela de representantes comerciais"
WSDATA UserCode                 AS String
WSDATA SellerCode               AS String
WSDATA CustomerID               AS String
WSDATA Seller                   As SellerView
WSDATA Header                   As Array Of BrwHeader
WSDATA HeaderType               As String
WSDATA TypeOfPhone              As Array Of GenericStruct

WSMETHOD GetHeader        DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
WSMETHOD GetTypeOfPhone   DESCRIPTION STR0003 //"Mtodo que descreve os tipo de telefones"
WSMETHOD GetSeller        DESCRIPTION STR0004 //"Mtodo de consulta as informaes cadastrais do representante comercial"
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 01.09.2003 
Ĵ
          Rotina de recuperacao dos headers deste WS                   
                                                                       
Ĵ
ParametrosExpC1: Nome da estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtCustomerSeller

::Header := MtHeader(::HeaderType)

Return(.T.)

/*/

Ŀ
Funo    GetSeller Autor   Eduardo Riera          Data 01.09.2003 
Ĵ
          Rotina de obtencao dos dados de vendedores                   
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Cliente                                               
          ExpC3: Codigo de identificacao do vendedor                   
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve os dados de um vendedor                  
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetSeller WSRECEIVE UserCode,CustomerID,SellerCode WSSEND Seller WSSERVICE MtCustomerSeller

Local aArea    := GetArea()
Local lRetorno := .T.
Local nX       := 0

If PrtChkUser(::UserCode,"MtCustomerSeller","GetSeller","SA1",::CustomerID)
	dbSelectArea("SA3")
	dbSetOrder(1)
	If MsSeek(xFilial("SA3")+::SellerCode)	
		::Seller:SellerCode  := SA3->A3_COD
		::Seller:Name        := SA3->A3_NOME
		::Seller:NickName    := SA3->A3_NREDUZ
		::Seller:FederalID   := SA3->A3_CGC
		::Seller:StateID     := SA3->A3_INSCR
		::Seller:DistrictID  := SA3->A3_INSCRM
		::Seller:HomePage    := SA3->A3_HPAGE
		::Seller:Email       := SA3->A3_EMAIL
		::Seller:SalesSupervisorCode := SA3->A3_SUPER
		::Seller:SalesManagerCode    := SA3->A3_GEREN
		UserFields("SA3",@::Seller:UserFields)

		::Seller:Addresses := WsClassNew("AddressView")
		::Seller:Addresses:TypeOfAddress := "1"
		::Seller:Addresses:Address       := FisGetEnd(SA3->A3_END)[1]
		::Seller:Addresses:AddressNumber := AllTrim(Str(FisGetEnd(SA3->A3_END)[2],8))
		::Seller:Addresses:District      := SA3->A3_MUN
		::Seller:Addresses:State         := SA3->A3_EST
		::Seller:Addresses:ZipCode       := SA3->A3_CEP
		::Seller:Addresses:Zone          := SA3->A3_BAIRRO

		::Seller:Phones := {}
		nX := 0
		If !Empty(SA3->A3_TEL)
			aadd(::Seller:Phones,WsClassNew("PhoneView"))
			nX++
			::Seller:Phones[nX]:TypeOfPhone     := "1"
			::Seller:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA3->A3_TEL)[3],15))
			::Seller:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA3->A3_TEL)[2],15))
			::Seller:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA3->A3_TEL)[1],15))	

			//Ŀ
			//Se a FisGetTel retornar zero pega o DDD e DDI direto 
			//dos seus respectivos campos.                         
			//
			If ::Seller:Phones[nX]:LocalAreaCode == "0"
				::Seller:Phones[nX]:LocalAreaCode := Alltrim(SA3->A3_DDDTEL)
			EndIf			
			If ::Seller:Phones[nX]:CountryAreaCode == "0"
				::Seller:Phones[nX]:CountryAreaCode := Alltrim(SA3->A3_DDI)
			EndIf			
			
		EndIf

		If !Empty(SA3->A3_FAX)
			aadd(::Seller:Phones,WsClassNew("PhoneView"))
			nX++
			::Seller:Phones[nX]:TypeOfPhone     := "2"
			::Seller:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA3->A3_FAX)[3],15))
			::Seller:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA3->A3_FAX)[2],15))
			::Seller:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA3->A3_FAX)[1],15))

			//Ŀ
			//Se a FisGetTel retornar zero pega o DDD e DDI direto 
			//dos seus respectivos campos.                         
			//
			If ::Seller:Phones[nX]:LocalAreaCode == "0"
				::Seller:Phones[nX]:LocalAreaCode := Alltrim(SA3->A3_DDDTEL)
			EndIf			
			If ::Seller:Phones[nX]:CountryAreaCode == "0"
				::Seller:Phones[nX]:CountryAreaCode := Alltrim(SA3->A3_DDI)
			EndIf						
			
		EndIf
			
	Else
		lRetorno := .F.
		SetSoapFault("GETSELLER",STR0008) //"Vendedor nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetTypeOfAAutor   Eduardo Riera          Data 04.08.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfPhone WSRECEIVE NULLPARAM WSSEND TypeOfPhone WSSERVICE MtCustomerSeller

aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[1]:Code := "1"
::TypeOfPhone[1]:Description := STR0010 //"COMERCIAL"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[2]:Code := "2"
::TypeOfPhone[2]:Description := STR0011 //"FAX"

Return(.T.)

Function wsmat040()
Return