#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#Include "totvs.ch"
#Include "monitorng.ch"

//---------------------------------------------------------------------
/*/{Protheus.doc} GPEMonitorNG
@description	Serviço para Integração Quírons - NG
@author		    caio.kretzer, raquel.andrade
@since			21/03/2024
/*/
//---------------------------------------------------------------------
class GPEMonitorNG

    // RJP - Tabela de Fila NG  
    // Listagem dos registros 
	@Get("api/rh/v1/monitor-ng")
	public method getListaRJP()

    // Limpa os dados para fazer o reenvio
	@Post("api/rh/v1/monitor-ng/reenviar/:NREGRJP")
	public method reenviarRJP()

    // Deleta todos os registros da tabela RJP - Tabela de Fila NG  
	@Delete("api/rh/v1/monitor-ng/:NREGRJP")
	public method deletarRJP()

    // RU7 - Histórico Envio Quirons
    // Listagem dos registros 
    @Get("api/rh/v1/monitor-ng/historico/:NREGRJP")
	public method getListaRU7()

    // Exclui a linha posicionada
	@Delete("api/rh/v1/monitor-ng/historico/:NREGRJP")
	public method deletarRU7()

    // Exclui todos os registros da tabela (soft and hard delete)
	@Delete("api/rh/v1/monitor-ng/historico")
	public method deletarRU7EmLote()	

    // Consulta F3 - LookUp 
    @Get("api/rh/v1/monitor-ng/filiais")
	public method getF3Filial()

	@Get("api/rh/v1/monitor-ng/filiais/:CCDFIL")
	public method getRetFilial()

	public method new() constructor

endclass


//-------------------------------------------------------------------------------//
// new                                                                           //
//-------------------------------------------------------------------------------//
method new() class GPEMonitorNG
return self

/*/{Protheus.doc} GPEMonitorNG::getListaRJP
Retorna uma lista com os registros da tabela RJP - Tabela de Fila NG . 
@author caio.kretzer, raquel.andrade
@since 21/03/2024
/*/
method getListaRJP() class GPEMonitorNG

	Local cAlias											as Character		
	Local cQuery											as Character
	Local nErroCod 		:= 0								as Numeric
	Local nParamOrder   := 1                        		as Numeric
	Local oStatement										as Object
	Local oItem												as Object
	Local jQueryParams 	:= oRest:getQueryRequest()			as Json
	Local cFilBsc     	:= jQueryParams['filialFtr'] 		as Character	
	Local cTabelaFtr    := jQueryParams['tabelaFtr']  		as Character
	Local dDtGer     	:= jQueryParams['dataGeracaoFtr']  	as Date	
	
	Private cErro											as Character
	Private cErroDesc										as Character
	Private cErroTipo										as Character
	Private oResponse := JsonObject():New()					as Json

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	Begin Sequence

		If jQueryParams:hasProperty("forcaErro")
			break
		EndIf

		oResponse["items"] := {}

		cQuery := " SELECT "
        cQuery += "	CASE  	"	
		cQuery += "WHEN RJP.RJP_DTIN = ''  THEN 'PE' "
		cQuery += "WHEN RJP.RJP_DTIN <> '' AND ISNULL(CONVERT(VARCHAR(1024),CONVERT(VARBINARY(1024),RJP.RJP_RTN)),'') = '%Registro Integrado com sucesso!%' THEN 'PR' "
		cQuery += "WHEN RJP.RJP_DTIN <> '' AND ISNULL(CONVERT(VARCHAR(1024),CONVERT(VARBINARY(1024),RJP.RJP_RTN)),'') <> '%Registro Integrado com sucesso!%' THEN 'FA' "
	    cQuery += " END AS RJP_STATUS"
        cQuery += " ,RJP_FILIAL, RJP_FIL, RJP_MAT, RJP_TAB, RJP_KEY, RJP_DATA, RJP_HORA, "
		cQuery += " RJP_DTIN, RJP_HORAIN, RJP_OPER, RJP_USER, RJP.R_E_C_N_O_ REGRJP "
		cQuery += " FROM "+RetSqlName("RJP")+" RJP "
		cQuery += " WHERE RJP.D_E_L_E_T_ = '' "
		If !Empty(cFilBsc) 
			cQuery += " AND RJP.RJP_FILIAL = ? "
		EndIf
		If  !Empty(dDtGer)
			cQuery += " AND RJP.RJP_DATA = ? "
		EndIf
		If  !Empty(cTabelaFtr) .And. cTabelaFtr != "ALL"
			cQuery += " AND RJP.RJP_TAB = ? "
		EndIf
		cQuery += " ORDER BY RJP.RJP_FILIAL "
		
		cQuery := ChangeQuery(cQuery)
		
		oStatement := FwExecStatement():New(cQuery)
		If !Empty(cFilBsc) 
			oStatement:SetString(nParamOrder++, FwxFilial("RJP",cFilBsc))
		EndIf
		If  !Empty(dDtGer)
        	oStatement:SetString(nParamOrder++, Substr(StrTran(dDtGer,'-',''),1,08))
		EndIf
		If  !Empty(cTabelaFtr) .And. cTabelaFtr != "ALL"
        	oStatement:SetString(nParamOrder++, AllTrim(cTabelaFtr))
		EndIf

		cAlias 	:= oStatement:OpenAlias()

		While !(cAlias)->(Eof())

			oItem := JsonObject():new()

			oItem["CRJPSTATUS"]	:= RTrim((cAlias)->RJP_STATUS)
            oItem["CRJPFILIAL"]	:= RTrim((cAlias)->RJP_FILIAL)
			oItem["CRJPFIL"]	:= RTrim((cAlias)->RJP_FIL)
			oItem["CRJPMAT"]	:= RTrim((cAlias)->RJP_MAT)
			oItem["CRJPTAB"]	:= RTrim((cAlias)->RJP_TAB)
			oItem["CRJPKEY"]	:= RTrim((cAlias)->RJP_KEY)
			oItem["DRJPDATA"]	:= fDateToJson(StoD((cAlias)->RJP_DATA))
			oItem["CRJPHORA"]	:= RTrim((cAlias)->RJP_HORA)
			oItem["DRJPDTIN"]	:= fDateToJson(StoD((cAlias)->RJP_DTIN))
			oItem["CRJPHORAIN"]	:= RTrim((cAlias)->RJP_HORAIN)
			oItem["CRJPOPER"]	:= RTrim((cAlias)->RJP_OPER)
			oItem["CRJPUSER"]	:= RTrim((cAlias)->RJP_USER)
			oItem["NREGRJP"]	:= (cAlias)->REGRJP

			Aadd(oResponse["items"], oItem)
			
			(cAlias)->(DbSkip())
		EndDo

		(cAlias)->(DbCloseArea())

		If jQueryParams:hasProperty("forcaMsg")
			cErro		:= "Mensagem"
			cErroDesc	:= "Mensagem Teste"
			cErroTipo	:= "success"
		Endif

		recover

		nErroCod 	:= 500
		cErro 		:= OemToAnsi(STR0001) //"Atenção - Inconsistência"  
		cErroDesc 	:= OemToAnsi(STR0002) //"Não foi retornar dados da tabela de Fila (RJP) !"   
		cErroTipo	:= "error"
	
	End Sequence

	fSetResponse(nErroCod, cErro, cErroDesc, cErroTipo)

Return


/*/{Protheus.doc} GPEMonitorNG::reenviarRJP
Limpa os dados da RJP - Tabela de Fila NG para fazer o reenvio.
@author caio.kretzer, raquel.andrade
@since 21/03/2024
/*/
method reenviarRJP() class GPEMonitorNG
	
	Local nErroCod 		:= 0							as Numeric
	Local jPathParams 	:= oRest:getPathParamsRequest()	as Json
	Local jQueryParams 	:= oRest:getQueryRequest()		as Json
	
	Private cErro										as Character
	Private cErroDesc									as Character
	Private cErroTipo									as Character
	Private oResponse := JsonObject():New()				as Json

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	Begin Sequence

		If jQueryParams:hasProperty("forcaErro")
			break
		EndIf

		DbSelectArea("RJP")
        RJP->(DbSetOrder(1)) // RJP_FILIAL+RJP_FIL+RJP_MAT
		RJP->(dbGoTo(Val(jPathParams["NREGRJP"])))

		RecLock('RJP',.F.)
		RJP->RJP_DTIN   := StoD('')
		RJP->RJP_HORAIN := ''
		RJP->RJP_RTN    := ''
		RJP->(MsUnlock())

		recover

		nErroCod 	:= 500
		cErro 		:= OemToAnsi(STR0001)  //"Atenção - Inconsistência"  
		cErroDesc 	:= OemToAnsi(STR0003)  //"Não foi possível atualizar os dados para realizar o reenvio!"
		cErroTipo	:= "error"		

	End Sequence

	fSetResponse(nErroCod, cErro, cErroDesc, cErroTipo)

Return


/*/{Protheus.doc} GPEMonitorNG::deletarRJP
Exclui o registro da tabela RJP - Tabela de Fila NG.
@author caio.kretzer, raquel.andrade
@since 21/03/2024
/*/
method deletarRJP() class GPEMonitorNG
	
	Local nErroCod 		:= 0							as Numeric
	Local jPathParams 	:= oRest:getPathParamsRequest()	as Json
	Local jQueryParams 	:= oRest:getQueryRequest()		as Json
	
	Private cErro										as Character
	Private cErroDesc									as Character
	Private cErroTipo									as Character
	Private oResponse := JsonObject():New()				as Json

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	Begin Sequence

		If jQueryParams:hasProperty("forcaErro")
			break
		EndIf

		DbSelectArea("RJP")
        RJP->(DbSetOrder(1)) // RJP_FILIAL+RJP_FIL+RJP_MAT
		RJP->(dbGoTo(Val(jPathParams["NREGRJP"])))

		RecLock('RJP',.F.)
		RJP->(dbDelete())
    	RJP->(MsUnlock())

		recover

		nErroCod 	:= 500
		cErro 		:= OemToAnsi(STR0001) //"Atenção - Inconsistência"  
		cErroDesc 	:= OemToAnsi(STR0004) //"Não foi possível excluir os dados!"
		cErroTipo	:= "error"	

	End Sequence

	fSetResponse(nErroCod, cErro, cErroDesc, cErroTipo)

Return

/*/{Protheus.doc} GPEMonitorNG::getListaRU7
Retorna uma lista com os registros da tabela RU7 - Histórico Envio Quirons.
@author caio.kretzer, raquel.andrade
@since 21/03/2024
/*/
method getListaRU7() class GPEMonitorNG

	Local cAlias										as Character
    Local jPathParams   := oRest:getPathParamsRequest() as Json
	Local jQueryParams 	:= oRest:getQueryRequest()		as Json
    Local nErroCod      := 0                        	as Numeric
    Local nParamOrder   := 1                        	as Numeric
	Local oItem											as Object
    
    Private cErro                                   	as Character
    Private cErroDesc                               	as Character
    Private cErroTipo                               	as Character
	Private oResponse := JsonObject():New()        		as Json

    oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

    Begin Sequence

		If jQueryParams:hasProperty("forcaErro")
			break
		EndIf

        DbSelectArea("RJP")
        RJP->(DbSetOrder(1)) // RJP_FILIAL+RJP_FIL+RJP_MAT
        RJP->(dbGoTo(Val(jPathParams["NREGRJP"])))

        cQuery := " SELECT RU7.R_E_C_N_O_ REGRU7 "
        cQuery += " FROM "+RetSqlName("RU7")+" RU7 "
        cQuery += " WHERE RU7.D_E_L_E_T_    = '' "
        cQuery += "   AND RU7.RU7_ID        = ? "
        cQuery += "   AND RU7.RU7_FILIAL    = ? "

        cQuery := ChangeQuery(cQuery)
       
        oStatement := FwExecStatement():New(cQuery)
        oStatement:SetString(nParamOrder++, RJP->(RJP_FIL+RJP_MAT+RJP_TAB+RJP_OPER))
        oStatement:SetString(nParamOrder++, FWxFilial("RU7",RJP->RJP_FILIAL))

        cAlias  := oStatement:OpenAlias()

        oResponse["items"] := {}

        DbSelectArea("RU7")
        While !(cAlias)->(Eof())

            RU7->(DbGoTo((cAlias)->REGRU7))
            
            oItem := JsonObject():new()
            
            oItem["CRU7FILIAL"]     := RU7->RU7_FILIAL
            oItem["CRU7ID"]         := RU7->RU7_ID
            oItem["DRU7DTIN"]       := fDateToJson(RU7->RU7_DTIN)
            oItem["CRU7HORAIN"]     := RU7->RU7_HORAIN
            oItem["CRU7SEQ"]        := RU7->RU7_SEQ
            oItem["CRU7MENS"]       := RU7->RU7_MENS
            oItem["CRU7RTN"]        := RU7->RU7_RTN
            oItem["CRU7CODRET"]     := RU7->RU7_CODRET
            oItem["CRU7SUCCESS"]    := RU7->RU7_SUCESS

            Aadd(oResponse["items"], oItem)
            
            (cAlias)->(DbSkip())
        EndDo

        (cAlias)->(DbCloseArea())

        recover

        nErroCod    := 500
        cErro       := OemToAnsi(STR0001) //"Atenção - Inconsistência"  
        cErroDesc   := OemToAnsi(STR0005) //"Não foi retornar dados da tabela de Fila  (RU7) !"    
        cErroTipo   := "error"

    End Sequence

    fSetResponse(nErroCod, cErro, cErroDesc, cErroTipo)

Return

/*/{Protheus.doc} GPEMonitorNG::deletarRU7
Exclui a linha posicionada
@author caio.kretzer, raquel.andrade
@since 21/03/2024
/*/
method deletarRU7() class GPEMonitorNG

	Local cAlias										as Character
    Local jPathParams   := oRest:getPathParamsRequest() as Json
	Local jQueryParams 	:= oRest:getQueryRequest()		as Json
    Local nErroCod      := 0                        	as Numeric
    Local nParamOrder   := 1                        	as Numeric
    
    Private cErro                                   	as Character
    Private cErroDesc                               	as Character
    Private cErroTipo                               	as Character
	Private oResponse := JsonObject():New()         	as Json

    oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

    Begin Sequence

		If jQueryParams:hasProperty("forcaErro")
			break
		EndIf

        DbSelectArea("RJP")
        RJP->(DbSetOrder(1)) // RJP_FILIAL+RJP_FIL+RJP_MAT
        RJP->(dbGoTo(Val(jPathParams["NREGRJP"])))

        cQuery := " SELECT RU7.R_E_C_N_O_ REGRU7 "
        cQuery += " FROM "+RetSqlName("RU7")+" RU7 "
        cQuery += " WHERE RU7.D_E_L_E_T_    = '' "
        cQuery += "   AND RU7.RU7_ID        = ? "
        cQuery += "   AND RU7.RU7_FILIAL    = ? "

        cQuery := ChangeQuery(cQuery)

        oStatement := FwExecStatement():New(cQuery)
        oStatement:SetString(nParamOrder++, RJP->(RJP_FIL+RJP_MAT+RJP_TAB+RJP_OPER))
        oStatement:SetString(nParamOrder++, FWxFilial("RU7",RJP->RJP_FILIAL))

        cAlias  := oStatement:OpenAlias()

        DbSelectArea("RU7")
        While !(cAlias)->(Eof())

            RU7->(DbGoTo((cAlias)->REGRU7))
            If Reclock("RU7", .F.)
                RU7->(DbDelete())
                RU7->(MsUnlock())
            EndIf
            
            (cAlias)->(DbSkip())
        EndDo

        (cAlias)->(DbCloseArea())

        recover

        nErroCod    := 500
        cErro       := OemToAnsi(STR0001) //"Atenção - Inconsistência"  
        cErroDesc   := OemToAnsi(STR0006) //"Não foi possível excluir os dados!"
        cErroTipo   := "error"

    End Sequence

    fSetResponse(nErroCod, cErro, cErroDesc, cErroTipo)

Return

/*/{Protheus.doc} GPEMonitorNG::deletarRU7EmLote
Exclui todos os registros da tabela RU7 - soft ou hard delete
@author caio.kretzer, raquel.andrade
@since 21/03/2024
/*/
method deletarRU7EmLote() class GPEMonitorNG

	Local cAlias													as Character
	Local cQryDel													as Character    
    Local nErroCod      := 0                       			 		as Numeric
    Local nParamOrder   := 1                        				as Numeric    
	Local lRegRU7		:= .F.										as Logical
	Local jQueryParams  := oRest:getQueryRequest()  				as Json
	Local lExcPerm      := jQueryParams['CDELETAPERM'] == "true" 	as Logical
    
    Private cErro                                   				as Character
    Private cErroDesc                               				as Character
    Private cErroTipo                               				as Character
	Private oResponse := JsonObject():New()         				as Json

    oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

    Begin Sequence

		If jQueryParams:hasProperty("forcaErro")
			break
		EndIf

        cQuery := " SELECT RU7.R_E_C_N_O_ REGRU7 "
        cQuery += " FROM "+RetSqlName("RU7")+" RU7 "
        cQuery += " WHERE RU7.D_E_L_E_T_    = '' "
        cQuery += "   AND RU7.RU7_FILIAL    BETWEEN ? AND ? "
        cQuery += "   AND RU7.RU7_DTIN      BETWEEN ? AND ? "

        cQuery := ChangeQuery(cQuery)
   
        oStatement := FwExecStatement():New(cQuery)
        oStatement:SetString(nParamOrder++, FwxFilial("RU7",jQueryParams["CRU7FILIALDE"]))
        oStatement:SetString(nParamOrder++, FwxFilial("RU7",jQueryParams["CRU7FILIALATE"]))
        oStatement:SetString(nParamOrder++, Substr(StrTran(jQueryParams["DRU7DTINDE"],'-',''),1,08))
        oStatement:SetString(nParamOrder++, Substr(StrTran(jQueryParams["DRU7DTINATE"],'-',''),1,08))

        cAlias  := oStatement:OpenAlias()

		lRegRU7 	:= !(cAlias)->(Eof()) 

		If lRegRU7
			DbSelectArea("RU7")
			While !(cAlias)->(Eof())

				RU7->(DbGoTo((cAlias)->REGRU7))
				If Reclock("RU7", .F.)
					RU7->(DbDelete())
					RU7->(MsUnlock())
				EndIf
				
				(cAlias)->(DbSkip())
			EndDo

			If lExcPerm
				cQryDel := "DELETE FROM " + RetSqlName("RU7") + " WHERE D_E_L_E_T_ = '*'"
				TcSqlExec( cQryDel )
			EndIf
		EndIf

		If !lRegRU7
			nErroCod    := 400
			cErro       := OemToAnsi(STR0001) //"Atenção - Inconsistência"  
			cErroDesc   := OemToAnsi(STR0011) //"Não foi possível localizar registros com os parâmetros informados!"
			cErroTipo   := "error"
		EndIf

		(cAlias)->(dbCloseArea())

        recover

        nErroCod    := 500
        cErro       := OemToAnsi(STR0001) //"Atenção - Inconsistência"  
        cErroDesc   := OemToAnsi(STR0006) // "Não foi possível excluir os dados!"
        cErroTipo   := "error"        

    End Sequence

    fSetResponse(nErroCod, cErro, cErroDesc, cErroTipo)

Return

/*/{Protheus.doc} GPEMonitorNG::getF3Filial
Retorna uma lista de filiais
@author raquel.andrade
@since 21/03/2024
/*/
method getF3Filial() class GPEMonitorNG
	
	Local aSM0										as Array
	Local cSearch      	:= "" 						as Character 
	Local cMsg			:= ""						as Character
	Local cMsgDesc		:= ""						as Character
	Local cMsgTipo		:= ""						as Character  
    Local jQueryParams 	:= oRest:getQueryRequest()	as Json
	Local nErroCod 		:= 0 						as Numeric
	Local nSM0         	:= 1						as Numeric
	Local oItem										as Object

	Private oResponse := JsonObject():New()			as Json

	cSearch := IIf(jQueryParams:hasProperty("filter"), jQueryParams["filter"], "")

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	Begin Sequence

		If jQueryParams:hasProperty("forcaErro")
			break
		EndIf

		aSM0      := FwLoadSM0()

		oResponse["items"] := {}

		// Percorre as filiais adicionando cada item no JSON de resposta
		For nSM0 := 1 To Len(aSM0)
			// Pula as filiais de empresa diferente da empresa logada
			If (aSM0[nSM0][SM0_GRPEMP] != cEmpAnt)
				LOOP
			EndIf

			// Se o termo de busca estiver preenchido, filtra as filiais com base nele
			If (!Empty(cSearch) .And. !(Upper(cSearch) $ Upper(aSM0[nSM0][SM0_CODFIL])) .And. !(Upper(cSearch) $ Upper(aSM0[nSM0][SM0_NOMRED])))
				LOOP
			EndIf

			oItem := JSonObject():new()
			oItem["CDFILIAL"]		:= AllTrim(aSM0[nSM0][SM0_CODFIL])
			oItem["CDFILDESC"]		:= AllTrim(aSM0[nSM0][SM0_NOMRED])

			aAdd(oResponse["items"], oItem)

		Next nSM0

		recover

		nErroCod 	:= 500
		cMsg 		:= OemToAnsi(STR0001) //"Atenção - Inconsistência"  
		cMsgDesc 	:= OemToAnsi(STR0007) //"Não foi possível recuperar o arquivo de Filiais!"
		cMsgTipo	:= "error"

	End Sequence

	fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)

Return

/*/{Protheus.doc} GPEMonitorNG::getRetFilial
Retorna 1 Código de Filial.
@author raquel.andrade
@since 21/03/2024
/*/
method getRetFilial() class GPEMonitorNG

	Local aSM0											as Array    
    Local cFilSelect	:= ""							as Character
	Local cMsg			:= ""							as Character
	Local cMsgDesc		:= ""							as Character
	Local cMsgTipo		:= ""							as Character
	Local cSearch      	:= "" 							as Character 	
	Local jPathParams   := oRest:getPathParamsRequest() as Json
	Local jQueryParams 	:= oRest:getQueryRequest()		as Json	
	Local nErroCod 		:= 0 							as Numeric
	Local nPosFil       := 0							as Numeric

	Private oResponse := JsonObject():New()				as Json

	cSearch := IIf(jQueryParams:hasProperty("filter"), jQueryParams["filter"], "")

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	cFilSelect	:= jPathParams["CCDFIL"]

	Begin Sequence

		If jQueryParams:hasProperty("forcaErro")
			break
		EndIf

		aSM0      := FwLoadSM0()	

		If Len(aSM0) > 0
			nPosFil		:=  aScan(aSM0, { |x| x[1] == cEmpAnt .And. cFilSelect $ X[2]  }) 
			If nPosFil > 0
				oResponse["CDFILIAL"] := aSM0[nPosFil][2]
			Else
				nErroCod 	:= 400
				cMsg 		:= OemToAnsi(STR0008) // "Código de Filial informado não existe!" 	
				cMsgDesc 	:= OemToAnsi(STR0009) // "Informe um Código de Filial existente!"  			
				cMsgTipo	:= "erro"
			EndIf
		EndIf

		recover

		nErroCod 	:= 500
		cMsg 		:= OemToAnsi(STR0001)  //"Atenção - Inconsistência"  
		cMsgDesc 	:= OemToAnsi(STR0010) //"Não foi possível recuperar o arquivo de Filiais!"
		cMsgTipo	:= "erro"

	End Sequence

	fSetResponse(nErroCod, cMsg , cMsgDesc, cMsgTipo)

Return


/* CONVERTE DATA PARA O TIPO LIDO NO POUI */
Static Function fDateToJson(cData)
	If ValType(cData) == "D"
		cData := DtoS(cData)
	Endif

	If !Empty(cData)
		cData := Left(cData,4) + '-' + Subs(cData,5,2) + '-' + Right(cData,2)
	Endif
Return cData

/* Seta a resposta para o PO-UI. 
Foi necessária a criação para passar no issue x cobertura de todas as APIs */
Static Function fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)

	if nErroCod > 0
		oRest:setStatusCode(nErroCod)
		oRest:setFault(fGetJsonErro(nErroCod, cMsg, cMsgDesc, cMsgTipo))
	else
		oRest:setResponse(fGetJsonMsg(cMsg, cMsgDesc, cMsgTipo))
	endif
Return


/* RETORNA O JSON DE ERRO PARA O POUI */
Static Function fGetJsonErro(nErroCod, cMsg, cMsgDesc, cMsgTipo)
	Local oPouiError                 		:= JsonObject():new() as Json

	oPouiError["message"]         	:= cMsg
	oPouiError["detailedMessage"] 	:= cMsgDesc
	oPouiError["type"]            	:= cMsgTipo

Return EncodeUTF8(FwCutOff(oPouiError:toJson()))


/* RETORNA O JSON PARA O POUI (COM ALGUMA MENSAGEM, SE NECESSARIO) */
Static Function fGetJsonMsg(cMsg, cMsgDesc, cMsgTipo)

	Local oItemMsg 			  as Json

	Default cMsg 		:= ""
	Default cMsgDesc 	:= ""
	Default cMsgTipo 	:= ""

	If !Empty(cMsg)
		oResponse["_messages"] 		:= {}
		oItemMsg                 	:= JsonObject():new()
		oItemMsg["message"]         := cMsg
		oItemMsg["detailedMessage"] := cMsgDesc
		oItemMsg["type"]            := cMsgTipo

		aadd(oResponse["_messages"],oItemMsg)
	EndIf

Return EncodeUTF8(FwCutOff(oResponse:toJson()))

