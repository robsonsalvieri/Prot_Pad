#INCLUDE "WSXLIB.ch"
#INCLUDE "TBICONN.CH"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"

/*
ͳ
Data Fonte Sustentao ChangeSet 
ĳ  
    12/08/2014          246079    
ͱ
*/
Static __aHead := {}

WSSTRUCT FieldStruct
	WSDATA FldName    AS String
	WSDATA FldType    AS String
	WSDATA FldSize    AS Integer
	WSDATA FldDec     AS Integer	
ENDWSSTRUCT

WSSTRUCT FieldView
	WSDATA FldTag     As Array Of String
ENDWSSTRUCT

WSSTRUCT TableView	
	WSDATA TableStruct     AS Array Of FieldStruct
	WSDATA TableData       AS Array Of FieldView
ENDWSSTRUCT

WSSTRUCT UserField
	WSDATA UserName    	AS String
	WSDATA UserTitle   	AS String OPTIONAL
	WSDATA UserTag     	AS String OPTIONAL
	WSDATA UserType    	AS String
	WSDATA UserSize    	AS Integer OPTIONAL
	WSDATA UserDec     	AS Integer OPTIONAL
	WSDATA UserOblig   	AS Boolean OPTIONAL //Obrigatorio
	WSDATA UserPicture 	AS String OPTIONAL
	WSDATA UserF3      	AS String OPTIONAL
	WSDATA UserComboBox	AS String OPTIONAL
ENDWSSTRUCT

WSSTRUCT BrwHeader
	WSDATA HeaderTitle    AS String
	WSDATA HeaderField    AS String
	WSDATA HeaderPicture  AS String 
	WSDATA HeaderSize     AS Integer	
	WSDATA HeaderDec      AS Integer
	WSDATA HeaderType     AS String
	WSDATA HeaderComboBox AS String  OPTIONAL
	WSDATA HeaderOblig    AS Boolean OPTIONAL //Obrigatorio	
	WSDATA HeaderF3       AS String OPTIONAL
ENDWSSTRUCT

WSSTRUCT GenericStruct
	WSDATA Code        AS String
	WSDATA Description AS String
	WSDATA Value       AS Float OPTIONAL
ENDWSSTRUCT

WSSTRUCT GenericView
	WSDATA Code        AS String
	WSDATA Description AS String
	WSDATA Value       AS Float OPTIONAL
	WSDATA UserFields  As Array Of UserField OPTIONAL
ENDWSSTRUCT

WSSTRUCT GenericView2
	WSDATA Code        AS String
	WSDATA Unit        AS String	
	WSDATA Description AS String
	WSDATA UserFields  As Array Of UserField OPTIONAL
ENDWSSTRUCT



/*/

Ŀ
Funo    TstBrowse Autor   Eduardo Riera          Data 14.05.2003 
Ĵ
          Rotina de demonstracao dos dados de um browse                
                                                                       
Ĵ
ParametrosExpA1: Array com da strutura de Header                       
          ExpA2: Array com o objeto a ser demonstrado                  
          ExpC3: Arquivo de destino                                    
          ExpC4: Titulo                                           [OPC]
Ĵ
Retorno   ExpA1: Array com a estrutura de browse [Linha][Coluna]       
                                                                       
Ĵ
Descrio Esta rotina demonstra as informacoes contidas em um array    
          seguindo a estrutura BrwHeader                               
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Function TstBrowse(aHeader,aValores,cArquivo,cTitulo)

Local nX       := 0
Local nY       := 0
Local nTamanho := 0
Local cLinha   := ""
Local cPicture := ""
Local xValor   := 0
Local aLinha   := {}
Local aRetorno := {}
Local aGrid    := {}
Private oObjeto

DEFAULT cArquivo := "TSTBROWSE.TXT"
DEFAULT cTitulo  := "NO TITLE"

For nX := 1 To Len(aValores)
	If nX == 1
		aRetorno := {}		
		For nY := 1 To Len(aHeader)
			oObjeto  := aValores[nX]
			xValor   := STR0001+aHeader[nY]:cHEADERType+aHeader[nY]:cHEADERFIELD //"oObjeto:"
			xValor   := &xValor
			cPicture := aHeader[nY]:cHEADERPICTURE
			If Empty(cPicture)
				cPicture := "@X"
			EndIf
			nTamanho := Len(TransForm(xValor,AllTrim(cPicture)))
			nTamanho := Max(nTamanho,Len(aHeader[nY]:cHEADERTITLE))
			cLinha   += PadR(aHeader[nY]:cHEADERTITLE,nTamanho)
			aadd(aRetorno,PadR(aHeader[nY]:cHEADERTITLE,nTamanho))
			cLinha   += "|"
	   Next nX
	   aadd(aLinha,cLinha)
	   aadd(aLinha,"")
	   aadd(aGrid,aRetorno)
	EndIf
	cLinha := ""
	aRetorno := {}
	For nY := 1 To Len(aHeader)	
		oObjeto  := aValores[nX]
		xValor   := STR0001+aHeader[nY]:cHEADERType+aHeader[nY]:cHEADERFIELD //"oObjeto:"
		xValor   := &xValor
		cPicture := aHeader[nY]:cHEADERPICTURE
		If Empty(cPicture)
			cPicture := "@X"
		EndIf		
		nTamanho := Len(TransForm(xValor,AllTrim(cPicture)))
		nTamanho := Max(nTamanho,Len(aHeader[nY]:cHEADERTITLE))
		cLinha   += PadR(TransForm(xValor,cPicture),nTamanho)
		aadd(aRetorno,PadR(TransForm(xValor,cPicture),nTamanho))
		cLinha   += "|"
   Next nX
   aadd(aLinha,cLinha)
	aadd(aGrid,aRetorno)
Next nX
cLinha := MemoRead(cArquivo)+Chr(13)+Chr(10)+Repl("=",80)+Chr(13)+Chr(10)
cLinha += cTitulo+Chr(13)+Chr(10)
For nX := 1 To Len(aLinha)
	cLinha += aLinha[nX]+Chr(13)+Chr(10)
Next nX
Memowrit(cArquivo,cLinha)
Return(aGrid)

/*/

Ŀ
Funo    UserFieldsAutor   Eduardo Riera          Data 14.05.2003 
Ĵ
          Rotina para preenchimento do campos de usuario               
                                                                       
Ĵ
ParametrosExpC1: Alias da tabela                                       
          ExpA2: Array dos itens da estrutura a ser preenchida         
          ExpC3: Alias do cursor da tabela                        (OPC)
          ExpA4: Array da rotina automatica                       (OPC)
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio Esta rotina preenche o array passado no segundo parametro    
          com todos os campos da tabela existente                      
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Function UserFields(cAlias,aObjeto,cCursor,aAuto)

Local aArea    := GetArea()
Local aAreaSX3 := SX3->(GetArea())
Local aUserStru:= {}
Local nX       := 0
Local nY       := 0
Local cTemp	   := ""

DEFAULT cCursor := cAlias
//Ŀ
//Definicao dos campos de usuario configurados                            
//
If ExistBlock("WSUSRFIELD")
	aUserStru := ExecBlock("WSUSRFIELD",.F.,.F.,{cAlias})
EndIf
For nX := 1 To Len(aUserStru)
	dbSelectArea("SX3")
	dbSetOrder(2)
	If MsSeek(aUserStru[nX])
		nY++
		If nY == 1
			aObjeto := {}
		EndIf
		aadd(aObjeto,WSClassNew("UserField"))
		aObjeto[nY]:UserTitle   := X3Titulo()
		aObjeto[nY]:UserName    := SX3->X3_CAMPO
		aObjeto[nY]:UserType    := SX3->X3_TIPO
		aObjeto[nY]:UserSize    := SX3->X3_TAMANHO
		aObjeto[nY]:UserDec     := SX3->X3_DECIMAL
		aObjeto[nY]:UserOblig   := X3Obrigat(aUserStru[nX])
		aObjeto[nY]:UserPicture := SX3->X3_PICTURE
		aObjeto[nY]:UserF3      := SX3->X3_F3
		aObjeto[nY]:UserComboBox:= X3CBox()
		Do Case
			Case SX3->X3_TIPO == "N"
				If aAuto == Nil
					aObjeto[nY]:UserTag     := Str((cCursor)->(FieldGet(FieldPos(SX3->X3_CAMPO))),SX3->X3_TAMANHO,SX3->X3_DECIMAL)
				Else
					aObjeto[nY]:UserTag     := Str(WSProcH(aAuto,SX3->X3_CAMPO),SX3->X3_TAMANHO,SX3->X3_DECIMAL)
				EndIf
			Case SX3->X3_TIPO == "D"
				If aAuto == Nil
					aObjeto[nY]:UserTag     := DTOS((cCursor)->(FieldGet(FieldPos(SX3->X3_CAMPO))))
				Else
					aObjeto[nY]:UserTag     := DTOS(WSProcH(aAuto,SX3->X3_CAMPO))
				EndIf
			Case SX3->X3_TIPO == "M" .and. "INCLUI" $ SX3->X3_RELACAO .and. !(SX3->X3_CONTEXT == "V")
				// campos do tipo MEMO tero no inicializador padro para recuperar o campo de cdigo do memo,
				// por isso foi realizado este WORKAROUND para executar como code block    
				cTemp := SX3->X3_RELACAO
				cTemp := strTran(cTemp, "INCLUI", ".F.")
				cTemp := strTran(cTemp, cAlias+"->", cCursor+"->")
				aObjeto[nY]:UserTag     	:= &(cTemp)
			OtherWise  
				If aAuto == Nil
					aObjeto[nY]:UserTag     := (cCursor)->(FieldGet(FieldPos(SX3->X3_CAMPO)))
				Else
					aObjeto[nY]:UserTag     := WSProcH(aAuto,SX3->X3_CAMPO)
				EndIf
		EndCase
		aObjeto[nY]:UserTag := IIf(aObjeto[nY]:UserTag==Nil,"",aObjeto[nY]:UserTag)
	EndIf
Next nX
RestArea(aAreaSX3)
RestArea(aArea)
Return(.T.)

/*/

Ŀ
Funo    PutUserFieAutor   Eduardo Riera          Data 29.05.2003 
Ĵ
          Rotina para atualizacao dos campos de usuario                
                                                                       
Ĵ
ParametrosExpC1: Alias da tabela                                       
          ExpA2: Array dos itens da estrutura                          
          ExpA3: Array no formato                                      
                [1] Campo                                              
                [2] Conteudo                                           
                [3] Nil                                                
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio Este Mtodo atualiza a senha do usuario                      
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                      
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                    
Ĵ
 Cleber M.    29/11/06113101-Troca do comando StoD para CtoD afim de 
                            converter corretamente os campos de usua-
                            rio do tipo data.                        
ٱ
Ŀ
 Programador   Data    FNC    Motivo da Alteracao                    
Ĵ
 Allyson M.   30/10/0925918/Usar StoD caso a funcao seja chamada pela
                        2009rotina de manutencao do curriculo no     
                            Portal do Candidato.                     
ٱ


/*/
Function PutUserFields(cCursor,aObjeto,aDados)

Local aArea    := GetArea()			//Area atual
Local aAreaSX3 := SX3->(GetArea())	//Area do SX3
Local nX       := 0					//Usada em lacos For...Next
Local nY       := 0					//Usada em lacos For...Next
Local xValor   := 0					//Valor do campo contido no aObjeto
Local nPos		:= 0				//Posicao encontrada no array
DEFAULT aObjeto := {}

For nX := 1 To Len(aObjeto)
	//Ŀ
	// Tratamento para tirar o '-' do campo CEP 
	//
	nPos := aScan( aObjeto, { |x| AllTrim(Upper(x:USERPICTURE)) == "@R 99999-999" } )
	If nPos <> 0
		aObjeto[nPos]:USERTAG := Replace(aObjeto[nPos]:USERTAG, "-", "")
	EndIf
     
	nY := aScan(aDados,{|x| AllTrim(Upper(x[1])) == AllTrim(Upper(aObjeto[nX]:UserName))})
   	If nY <> 0 .OR. __cInternet == 'AUTOMATICO'
		xValor := aObjeto[nX]:UserTag
		Do Case
			Case aObjeto[nX]:UserType == "N"
				xValor     := StrTran(xValor,".")
				xValor     := Replace(xValor,",",".")
				xValor     := Val(xValor)
			Case aObjeto[nX]:UserType == "D"
			    //Se o processo anterior for uma das rotinas de manutencao do curriculo do Portal do Candidato
			    //e o formato da data for aaaammdd, usa StoD, senao usa CtoD
				If ( ProcName(1) $ "SETHISTORY-SETCOURSES-SETGRADUATION-SETLANGUAGES-SETCERTIFICATION" .AND.;
				 (SubStr(xValor,1,3) $ '200-199-198-197-196-195-194-193-192-191') )
					xValor     := StoD(xValor)
				Else
					xValor     := CtoD(xValor, "ddmmyy")
				EndIf
		EndCase	
		If !Empty(xValor)
			aAdd(aDados,{AllTrim(Upper(aObjeto[nX]:UserName)),xValor,Nil})
		EndIf
	EndIf
Next nX
RestArea(aAreaSX3)
RestArea(aArea)
Return(.T.)

/*/

Ŀ
Funo    WsAutoOpc Autor   Eduardo Riera          Data 03.09.2003 
Ĵ
          Analise dos campos opcionais do array da rotina automatica   
                                                                       
Ĵ
ParametrosExpA1: Array da rotina automatica                            
          ExpL2: Considera os campos vazios como nulos                 
          ExpB3: Criterio variavel                                     
Ĵ
Retorno   ExpA: Array da rotina automatcia sem os campos opcionais     
                                                                       
Ĵ
Descrio Este Mtodo atualiza a senha do usuario                      
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Function WsAutoOpc(aAuto,lEmpty,bAdd)

Local nX := 0
Local aReturn := {}
DEFAULT lEmpty := .F.
DEFAULT bAdd   := {|| .T.}

For nX := 1 To Len(aAuto)
	If ((aAuto[nX][2] <> Nil .And. !lEmpty) .Or. (lEmpty .And. !Empty(aAuto[nX][2]) )) .And. Eval(bAdd)
		aadd(aReturn,aAuto[nX])	
	EndIf
Next nX

Return(aReturn)

/*/

Ŀ
Funo    WsQueryAddAutor   Eduardo Riera          Data 03.10.2003 
Ĵ
          Analise da query a ser adicionada                            
                                                                       
Ĵ
ParametrosExpC1: Query Atual                                           
          ExpC2: Expressao a ser adicionada                            
Ĵ
Retorno   Query alterada                                               
                                                                       
Ĵ
Descrio Este Mtodo atualiza a senha do usuario                      
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Function WsQueryAdd(cQuery,cAdd)

Local lDeleted := ""
Local nAt      := 0
Local nInicio  := 0
Local nFim     := 0
Local lFim     := .F.
Local cString  := ""

DEFAULT cADD := ""
lDeleted := "@DELETED@"$cAdd

If lDeleted 
	cAdd := StrTran(cAdd,"@DELETED@","")
	nAt := At("D_E_L_E_T_",cQuery)
	While nAt <> 0
		nInicio := nAt-1
		While SubStr(cQuery,nInicio,1)<>" "
			nInicio--
		EndDo
		If SubStr(cQuery,nInicio-3,3)=="AND"
			nInicio -= 4
		EndIf
		nFim := nAt+10
		lFim := .F.
		cString := " "
		While !Empty(cString) .Or. cString == " "
			cString := SubStr(cQuery,nFim,1)
			nFim++
			If cString == "'"
				nFim +=2
				Exit
			EndIf
		EndDo
		cQuery := SubStr(cQuery,1,nInicio)+SubStr(cQuery,nFim)
		nAt := At("D_E_L_E_T_",cQuery)
	EndDo	
EndIf

If !Empty(cAdd)
	cQuery += " AND "+cAdd
EndIf

Return(cQuery)

/*/

Ŀ
Funo    WsSqlOrderAutor   Eduardo Riera          Data 09.10.2003 
Ĵ
          Analise a Ordem passada                                      
                                                                       
Ĵ
ParametrosExpC1: Chave                                                 
Ĵ
Retorno   Chave alterada                                               
                                                                       
Ĵ
Descrio Este Mtodo atualiza a senha do usuario                      
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Function WsSqlOrder(cIndex)

If( Substr( cIndex, 1, 1 ) == "@" )
	cIndex	:= SubStr( cIndex, 2 )
Else
	cIndex	:= SqlOrder(cIndex)
EndIf
Return(cIndex)

/*/

Ŀ
Funo    GetUserFilAutor   Eduardo Riera          Data 14.05.2003 
Ĵ
          Rotina de retorno dos campos de usuario                      
                                                                       
Ĵ
ParametrosExpC1: Alias da tabela                                       
                                                                       
Ĵ
Retorno   ExpC1: Expressao para ser utilizada na query                 
                                                                       
Ĵ
Descrio Esta rotina retorna uma string com os campos a serem adicio- 
          nados na query list.                                         
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Function GetUserFields(cAlias)

Local aArea    := GetArea()
Local aAreaSX3 := SX3->(GetArea())
Local aUserStru:= {}
Local cQuery   := " "
Local nX       := 0
//Ŀ
//Definicao dos campos de usuario configurados                            
//
If ExistBlock("WSUSRFIELD")
	aUserStru := ExecBlock("WSUSRFIELD",.F.,.F.,{cAlias})
EndIf
For nX := 1 To Len(aUserStru)
	cQuery += ","+AllTrim(aUserStru[nX])+" "
Next nX
RestArea(aAreaSX3)
RestArea(aArea)
Return(cQuery)

/*/

Ŀ
Funo    MsAuto2EncAutor   Eduardo Riera          Data 14.05.2003 
Ĵ
          Rotina de tranferencia de variaveis da interface modelo 1 pa-
          para um array de rotina automatica                           
Ĵ
ParametrosExpC1: Alias da tabela                                       
                                                                       
                                                                       
Ĵ
Retorno   ExpA1: Array da Rotina automatica                            
                                                                       
Ĵ
Descrio Esta rotina converte os dados das variaveis de memoria da    
          Enchoice para um array da rotina automatica                  
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Function MsAuto2Ench(cAlias)

Local aCab   := {}
Local nX     := 0
Local bCampo := {|nX| (cAlias)->(FieldName(nX)) }

For nX := 1 To (cAlias)->(FCount())
	aadd(aCab,{(cAlias)->(FieldName(nX)),M->&(EVAL(bCampo,nX)),Nil})
Next nX

Return(aCab)

/*/

Ŀ
Funo    MsAuto2Gd Autor   Eduardo Riera          Data 14.05.2003 
Ĵ
          Rotina de tranferencia do aCols para um array da rotina auto-
          matica                                                       
Ĵ
ParametrosExpA1: aHeader                                               
          ExpA2: aCols                                                 
                                                                       
Ĵ
Retorno   ExpA1: Array da Rotina automatica                            
                                                                       
Ĵ
Descrio Esta rotina converte os dados dos dados do aCols para um     
          array da rotina automatica                                   
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Function MsAuto2Gd(aHeader,aCols)

Local aItem  := {}
Local aItens := {}
Local nX     := 0
Local nY     := 0

For nX := 1 To Len(aCols)
	aItem := {}
	If !aCols[nX][Len(aHeader)+1]
		For nY := 1 To Len(aHeader)
			aadd(aItem,{aHeader[nY][2],aCols[nX][nY],Nil})
		Next nY
		aadd(aItens,aItem)
	EndIf
Next nX

Return(aItens)

/*/

Ŀ
Funo    MsAuto2GDbAutor   Eduardo Riera          Data 14.05.2003 
Ĵ
          Rotina de tranferencia do Getdb para um array da rotina auto-
          matica                                                       
Ĵ
ParametrosExpA1: aHeader                                               
          ExpC2: Alias do arquivo temporario                           
                                                                       
Ĵ
Retorno   ExpA1: Array da Rotina automatica                            
                                                                       
Ĵ
Descrio Esta rotina converte os dados dos dados do aCols para um     
          array da rotina automatica                                   
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Function MsAuto2GDb(aHeader,cAlias)

Local aArea  := GetArea()
Local aItem  := {}
Local aItens := {}
Local nY     := 0

dbSelectArea(cAlias)
dbGotop()
While !Eof()
	aItem := {}
	For nY := 1 To Len(aHeader)
		aadd(aItem,{aHeader[nY][2],FieldGet(FieldPos(aHeader[nY][2])),Nil})
	Next nY
	aadd(aItens,aItem)
	dbSelectArea(cAlias)	
	dbSkip()
EndDo

RestArea(aArea)
Return(aItens)

/*/

Ŀ
Funo    WsProcH   Autor   Eduardo Riera          Data 21.12.2004 
Ĵ
          Rotina de busca do valor do campo dentro do aCols            
Ĵ
ParametrosExpA1: aHeader                                               
          ExpC2: Campo                                                 
Ĵ
Retorno   ExpA1: Valor do campo                                        
Ĵ
Descrio Esta rotina busca o valor do campo dentro do aCols           
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Function WsProcH( aCab, cCampo )

Local uRetorno 	:= Nil
Local nX 		:= aScan( aCab, { |x| AllTrim( x[1] ) == AllTrim( cCampo ) } )

If nX == 0
	uRetorno := CriaVar( cCampo, .F. )
Else
	uRetorno := aCab[nX][2]
EndIf

Return( uRetorno )

/*/

Ŀ
Funo    WsPutHead Autor   Eduardo Riera          Data 06.01.2004 
Ĵ
          Funcao de preenchimento do header padrao dos portais         
                                                                       
Ĵ
ParametrosExpA1: Informe o array com os dados do Header                
          ExpC2: Informe o nome do campo do SX3                        
          ExpC3: Informe o nome do campo do Header                     
                                                                       
                                                                       
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio Esta funcao preenche o array passado como parametro          
                                                                       
                                                                       
Ĵ
Uso        Portais/Web Services                                        
ٱ


/*/
Function WsPutHead(aHeader,cSX3,cHeader)

Local nX := Len(aHeader)

cHeader := Upper(cHeader)
cSx3    := Upper(cSX3)

aadd(aHeader,WSClassNew("BrwHeader"))
nX++ 
aHeader[nX]:HeaderTitle  := RetTitle(cSX3)
aHeader[nX]:HeaderField   := cHeader
aHeader[nX]:HeaderPicture := SX3->X3_PICTURE
aHeader[nX]:HeaderSize    := SX3->X3_TAMANHO
aHeader[nX]:HeaderDec     := SX3->X3_DECIMAL
aHeader[nX]:HeaderType    := SX3->X3_TIPO
aHeader[nX]:HeaderComboBox:= X3CBox()   
aHeader[nX]:HeaderOblig   := X3Obrigat(SX3->X3_CAMPO)
aHeader[nX]:HeaderF3      := SX3->X3_F3

Return(.T.)


/*/

Ŀ
Funo    WsChgUrl   Autor  Eduardo Riera          Data 15.01.2004 
Ĵ
          Rotina de troca de url para o portal do sistema              
                                                                       
Ĵ
ParametrosExpO1: Objeto WS                                             
          ExpC2: Nome do servio                                       
          ExpC3: Nome da chave a ser pesquisada                        
Ĵ
Retorno   ExpC1: Nenhuma                                               
                                                                       
Ĵ
Descrio Esta funcao realiza a troca da URL passado por referencia.   
                                                                       
Ĵ
Uso       Ecossistema/Portal                                           
ٱ


/*/
Function WsChgUrl(oObjeto,cWsService,cLocation,lJob,cEmpWS)

Local cWsUrl    := ""
Local cUrl      := ""

DEFAULT lJob 	:= .T.
DEFAULT cEmpWS	:= ""

Do Case
	Case cLocation == "UrlEcossistema"
		cUrl      := GetSrvProfString("URLEcossistema","")		
	Otherwise
		If !Empty(cEmpWs)
			cUrl := GetPvProfString("WebServices", "EMP"+cEmpWS, "", GetAdv97())
		EndIf

		If Empty(cEmpWs) .Or. Empty(cUrl)
			If Empty(cLocation)
				If lJob
					cUrl      := GetJobProfString("WebServiceLocation","http://webservices.microsiga.com.br")
				Else
					cUrl      := GetSrvProfString("WebServiceLocation","http://webservices.microsiga.com.br")
				EndIf
			Else
				If lJob
					cUrl      := GetJobProfString(cLocation,"")
				Else
					cUrl      := GetSrvProfString(cLocation,"")
				EndIf
			EndIf
		EndIf
EndCase

If !Empty(cUrl)
	cWsUrl := cUrl+"/"+cWSService
	oObjeto:_URL := cWsUrl
EndIf

Return(.T.)

/*/

Ŀ
Funo    SigaWsStar Autor  Luiz Couto             Data 27.05.2004 
Ĵ
          Rotina de Start Web Services                                 
Ĵ
Parametros                                                             
Ĵ
Retorno   ExpL1: Retorno                                               
Ĵ
Descrio Esta funcao realiza a troca da URL passado por referencia.   
                                                                       
Ĵ
Uso       Ecossistema/Portal                                           
ٱ


/*/
Function SigaWSStart()

Return .T.

//Essa funcao so sera utilizada no MATA410.PRX
Function WS_HEAD( lGet, cEmp, aArray, cAlias )

Local nAtEmp := 0
Local nAtAlias := 0
Local lRetorno := .T.

nAtAlias := aScan( __aHead, { |x| x[1] == cAlias } )

If nAtAlias == 0
	aAdd( __aHead, { cAlias, {} } )
	
	nAtAlias := Len( __aHead )
EndIf

If lGet
	nAtEmp := aScan( __aHead[nAtAlias][2], { |x| x[1] == cEmp } )

	If nAtEmp > 0
		aArray := aClone( __aHead[nAtAlias][2][nAtEmp][2] )
		lRetorno := .T.
	Else
		aArray := {}
		lRetorno := .F.
	EndIf
Else
	nAtEmp := aScan( __aHead[nAtAlias][2], { |x| x[1] == cEmp } )

	If nAtEmp > 0
		__aHead[nAtAlias][2][nAtEmp][2] := aClone( aArray )
	Else                                   
		aAdd( __aHead[nAtAlias][2], { cEmp, aClone( aArray ) } )
	Endif                                     
		
	lRetorno := .T.
EndIf

Return lRetorno

/*/

Ŀ
Funo    WsChangeMo Autor  Eduardo Riera          Data 08.10.2004 
Ĵ
          Rotina de Troca de Modulo                                    
Ĵ
ParametrosExp C1 : Nome do Modulo                                      
Ĵ
Retorno   ExpL1: Retorno                                               
Ĵ
Descrio Esta funcao realiza a troca do modulo                        
Ĵ
Uso       Ecossistema/Portal                                           
ٱ


/*/
Function WsChangeModule( cModName )

Local lRetorno 	:= .T.
Local nX 		:= 0

Static aModulos := RetModName()

nX := aScan( aModulos,{ |x| AllTrim( x[2] ) == cModName } )

If nX <> 0
	nModulo := aModulos[nX][1]
	cModulo := AllTrim( SubStr( aModulos[nX][2], 5 ) )
Else
	nModulo := 0
	cModulo := "CFG"
EndIf

Return lRetorno


