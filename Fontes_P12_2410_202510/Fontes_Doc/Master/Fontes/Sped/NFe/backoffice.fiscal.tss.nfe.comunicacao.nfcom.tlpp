#include 'tlpp-core.th'
#include 'tlpp-object.th'
#include 'totvs.ch'
#include 'nfcom.ch'

#DEFINE ENTER CHR(13)+CHR(10)
#DEFINE EVCANCELAMENTO "110111"
#DEFINE NPOSDOC 1
#DEFINE NPOSSERIE 2
#DEFINE NPOSCLIFOR 3
#DEFINE NPOSLOJA 4
#DEFINE NPOSCHV 5
#DEFINE NPOSEMISSAO 6

//-----------------------------------------------------------------------
/*/{Protheus.doc}  NFCom
    Classe responsavel pela Interface da NFCom (Nota Fiscal Fatura de Servi√ßo de Comunica√ß√£o Eletr√¥nica).

    @author Felipe Duarte Luna
    @since 30.05.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------

namespace backoffice.fiscal.tss.nfe.nfcom

Class NFCom

	Public Data cIdEntidade as character
	Public Data cVersaoTSS 	as character
	Public Data aParametros as array
	Public Data oNFComCancelada as object

	Private Data cAliasBrowser 	  as character
	Private Data cQueryDocumentos as character
	Private Data cAliasDocumentos as character
	Private Data lThemeDark as logical


	Public  Method New() constructor
	Private Method MontaTelaParametros()
    Private Method ProcessaDados()
	Private Method MontaQueryDocumentos()
	Private Method MontaTelaNFCom()
	Private Method ThemaSistema()

	Protected Method Active(cIdEnt as character, cVersaoTSS as character, lUsaColab as logical)
	Protected Method MontaTransmissao()
	Protected Method MonitorFaixa()
	Protected Method SpedDanfcom()
	Protected Method VisualizaDocumento(nOpc as numeric)
	Protected Method NFComLegenda()

EndClass

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method New
    Metodo construtor da classe NFCom.

    @author Felipe Duarte Luna
    @since 30.05.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method New() class NFCom

	::lThemeDark := .F.
	::cAliasBrowser := ""
	::aParametros := {}
	::oNFComCancelada := nil

Return

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method Active
    Metodo que Inicializa os Objeto da classe NFCom.

    @author Felipe Duarte Luna
    @since 30.05.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method Active(cIdEnt as character, cVersaoTSS as character, lUsaColab as logical) class NFCom

	If GetSX3Cache("F3_PROTOC","X3_TAMANHO") < 17
		Alert(STR0067+ENTER+ENTER+STR0068, STR0001) //"Para acessar esta rotina de NFCom, o tamanho do campo de protocolo no sistema (F3_PROTOC) da Tabela SF3 deve ser maior ou igual √† 17." " Por favor, atualize o sistema com o devido pacote de dicionario de expedi√ß√£o continua do m√≥dulo de livros fiscais e reiniciar o Protheus." "NFCOM"
		Return
	EndIf

	If Empty(cIdEnt)
		::cIdEntidade := getCfgEntidade() // Tratamento ao clicar pela 1∫ vez na rotina NFe-Sefaz, quando o serviÁo do TSS n„o estiver ativo.
	Else
		::cIdEntidade := cIdEnt		
	EndIf
	::cVersaoTSS := cVersaoTSS

	::oNFComCancelada := NFComCanceladas():New(::cIdEntidade, ::cVersaoTSS)

	::cAliasBrowser := GetNextAlias()

	::ThemaSistema()
	::MontaTelaParametros()

Return

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method MontaTelaParametros
    Monta a Tela de Parametros Inicial da classe NFCom.

    @author Felipe Duarte Luna
    @since 30.05.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method MontaTelaParametros() class NFCom

	Local nTamTipoCli := 20 as numeric
	Local nTamSerie := FWSX3Util():GetFieldStruct("F2_SERIE")[3]
	Local aPergunte := {} as array
	local cOldCadastro := "" as character

	aAdd(aPergunte,{2,STR0002,Space(nTamTipoCli),{STR0003,STR0004} ,80 ,,.T.,}) // "Tipo de NFCom " + "1-SaÌda" + "2-Entrada"
	aAdd(aPergunte,{2,STR0024,space(18),{ STR0031, STR0032, STR0033, STR0034, STR0035},80,".T.",.T.,".T."}) //"Filtra"###"1-Autorizadas"###"2-Sem filtro"###"3-N„o Autorizadas"###"4-Transmitidas"###"5-N„o Transmitidas"
	aAdd(aPergunte,{1,STR0005,space(nTamSerie),"",".T.","",".T.",30,.F.}) //"SÈrie da Nota Fiscal "
	aAdd(aPergunte,{1,STR0006,date(),"",".T.","",".T.",80,.F.}) 		  //"Dt. Emiss„o Inicial "
	aAdd(aPergunte,{1,STR0007,date(),"",".T.","",".T.",80,.F.}) 		  //"Dt. Emiss„o Final "

	if type("cCadastro") == "C"
		cOldCadastro := cCadastro
	endIf

	cCadastro := STR0036 //"Filtro para exibiÁ„o da(s) NFCom"
	if ParamBox(aPergunte, ::cIdEntidade + " - " + ::cVersaoTSS,::aParametros,,,,,,,STR0001+alltrim(FWGrpCompany())+alltrim(FWCodFil()),.T.,.T.) //"NFCom"
		::ProcessaDados()
	endif

	cCadastro := cOldCadastro
	aSize(aPergunte, 0)
Return

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method ProcessaDados
    Processa Conforme Parametros Informados para Montagem do Browser das Notas NFCom.

    @author Felipe Duarte Luna
    @since 30.05.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method ProcessaDados() class NFCom

	::MontaQueryDocumentos()

	(::cAliasDocumentos)->(DBGOTOP())
	if (::cAliasDocumentos)->(!EOF()) .AND. (::cAliasDocumentos)->(!BOF())
		(::cAliasDocumentos)->(DBCLOSEAREA())
		::MontaTelaNFCom()
	else
		(::cAliasDocumentos)->(DBCLOSEAREA())
		FWAlertInfo(STR0037,STR0001) //"N„o existem registros relacionados ao filtro selecionado." + "NFCom"
	endif

Return

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method MontaQueryDocumentos
    Query que Busca os documentos a serem listados no FWBrowser da NFCom.

    @author Felipe Duarte Luna
    @since 02.06.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method MontaQueryDocumentos() class NFCom

	Local oStatement := nil as object
	Local cFimpNota	 := "" as character
	Local nOrder	 := 1 as Numeric

	if SubStr(Alltrim(::aParametros[1]),1,1) == "1"

		::cQueryDocumentos := "SELECT SF2.F2_DOC,SF2.F2_EMISSAO,SF2.F2_SERIE,SF2.F2_CLIENTE,SF2.F2_LOJA,SF2.F2_CHVNFE, SF2.F2_FIMP,"
		::cQueryDocumentos += " SF2.R_E_C_N_O_ AS RECNOTA,SA1.A1_NOME,SA1.A1_PESSOA,SA1.A1_CGC,SA1.R_E_C_N_O_ AS RECCLIFOR,"
		::cQueryDocumentos += " SFX.FX_TIPOMOV,SFX.FX_DOC,SFX.FX_SERIE,SFX.FX_ESPECIE,SFX.FX_CLIFOR,SFX.FX_LOJA, SFX.R_E_C_N_O_ AS RECNOSFX"
		::cQueryDocumentos += " FROM "+RetSqlName("SF2")+" SF2"
		::cQueryDocumentos += " LEFT JOIN "+RetSqlName("SA1")+" SA1 ON SA1.A1_COD = SF2.F2_CLIENTE AND SA1.A1_LOJA = SF2.F2_LOJA AND SA1.A1_FILIAL = '"+FWxFilial("SA1")+"' AND SA1.D_E_L_E_T_ = ''"
		::cQueryDocumentos += " LEFT JOIN "+RetSqlName("SFX")+" SFX ON SFX.FX_FILIAL = SF2.F2_FILIAL AND SFX.FX_DOC = SF2.F2_DOC AND SFX.FX_SERIE = SF2.F2_SERIE"
		::cQueryDocumentos += " AND SFX.FX_CLIFOR = SF2.F2_CLIENTE AND SFX.FX_LOJA = SF2.F2_LOJA AND SFX.D_E_L_E_T_ = ''"
		::cQueryDocumentos += " WHERE SF2.D_E_L_E_T_ = ?"
		::cQueryDocumentos += " AND SF2.F2_FILIAL = ?"
		::cQueryDocumentos += " AND SF2.F2_ESPECIE = ?"

		If !Empty(Self:aParametros[3])
			::cQueryDocumentos += " AND SF2.F2_SERIE = ?"
		EndIf

		If !Empty(Self:aParametros[4]) .And. !Empty(Self:aParametros[5])
			::cQueryDocumentos += " AND SF2.F2_EMISSAO BETWEEN ? AND ?"
		EndIf

		If SubStr(Self:aParametros[2],1,1) <> "2"
			::cQueryDocumentos += " AND SF2.F2_FIMP = ?"
		EndIf

	elseif SubStr(Alltrim(::aParametros[1]),1,1) == "2"

		::cQueryDocumentos := "SELECT SF1.F1_DOC,SF1.F1_EMISSAO,SF1.F1_SERIE,SF1.F1_FORNECE,SF1.F1_LOJA,SF1.F1_CHVNFE, SF1.F1_FIMP,"
		::cQueryDocumentos += " SF1.R_E_C_N_O_ AS RECNOTA,SA2.A2_NOME,SA2.A2_TIPO,SA2.A2_CGC,SA2.R_E_C_N_O_ AS RECCLIFOR,"
		::cQueryDocumentos += " SFX.FX_TIPOMOV,SFX.FX_DOC,SFX.FX_SERIE,SFX.FX_ESPECIE,SFX.FX_CLIFOR,SFX.FX_LOJA, SFX.R_E_C_N_O_ AS RECNOSFX"
		::cQueryDocumentos += " FROM "+RetSqlName("SF1")+" SF1"
		::cQueryDocumentos += " LEFT JOIN "+RetSqlName("SA2")+" SA2 ON SA2.A2_COD = SF1.F1_FORNECE AND SA2.A2_LOJA = SF1.F1_LOJA AND SA2.A2_FILIAL = '"+FWxFilial("SA2")+"' AND SA2.D_E_L_E_T_ = ''"
		::cQueryDocumentos += " LEFT JOIN "+RetSqlName("SFX")+" SFX ON SFX.FX_FILIAL = SF1.F1_FILIAL AND SFX.FX_DOC = SF1.F1_DOC AND SFX.FX_SERIE = SF1.F1_SERIE"
		::cQueryDocumentos += " AND SFX.FX_CLIFOR = SF1.F1_FORNECE AND SFX.FX_LOJA = SF1.F1_LOJA AND SFX.D_E_L_E_T_ = ''"
		::cQueryDocumentos += " WHERE SF1.D_E_L_E_T_ = ?"
		::cQueryDocumentos += " AND SF1.F1_FILIAL = ?"
		::cQueryDocumentos += " AND SF1.F1_ESPECIE = ?"

		If !Empty(Self:aParametros[3])
			::cQueryDocumentos += " AND SF1.F1_SERIE = ?"
		EndIf

		If !Empty(Self:aParametros[4]) .And. !Empty(Self:aParametros[5])
			::cQueryDocumentos += " AND SF1.F1_EMISSAO BETWEEN ? AND ?"
		EndIf

		If SubStr(Self:aParametros[2],1,1) <> "2"
			::cQueryDocumentos += " AND SF1.F1_FIMP = ?"
		EndIf

	endif

	oStatement := FWExecStatement():New(ChangeQuery(::cQueryDocumentos))
	oStatement:SetString(nOrder,"")

	nOrder++
	if SubStr(Alltrim(::aParametros[1]),1,1) == "1"
		oStatement:SetString(nOrder,FWXFilial("SF2"))
	elseif SubStr(Alltrim(::aParametros[1]),1,1) == "2"
		oStatement:SetString(nOrder,FWXFilial("SF1"))
	endif

	nOrder++
	oStatement:SetString(nOrder,"NFCOM")

	If !Empty(Self:aParametros[3])
		nOrder++
		oStatement:SetString(nOrder,::aParametros[3])
	EndIf

	If !Empty(Self:aParametros[4]) .And. !Empty(Self:aParametros[5])
		nOrder++
		oStatement:SetDate(nOrder,::aParametros[4])
		nOrder++
		oStatement:SetDate(nOrder,::aParametros[5])
	EndIf

	If SubStr(Self:aParametros[2],1,1) <> "2"
		nOrder++
		If SubStr(Self:aParametros[2],1,1) == "5"
			cFimpNota := ""
		ElseIf SubStr(Self:aParametros[2],1,1) == "4"
			cFimpNota := "T"
		ElseIf SubStr(Self:aParametros[2],1,1) == "3"
			cFimpNota := "N"
		ElseIf SubStr(Self:aParametros[2],1,1) == "1"
			cFimpNota := "S"
		EndIf
		oStatement:SetString(nOrder,cFimpNota)
	EndIf

	::cQueryDocumentos := oStatement:GetFixQuery()
	::cAliasDocumentos := oStatement:OpenAlias()

	oStatement:Destroy()
	FreeObj(oStatement)
	oStatement := nil
Return

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method MontaTelaNFCom
    Monta tela Browser que lista os documentos aptos a serem eviados pelo NFCom.

    @author Felipe Duarte Luna
    @since 02.06.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method MontaTelaNFCom() class NFCom

	Local oDlgNotas as Object
	LOcal oColumns as object
	Local aColumns := {} as Array
	Local nContFlds as Numeric
	Local oBrowseNotas as Object
	Local aFields := {} as Array
	Local aStructCampo := {} as Array
	Local aSize := {} as array
	Local cTitle := "" as character
	Local aSeek := {} as array
	Local aIndex := {} as array
	Local nOpcDoc := 0 as numeric

	aSize := MsAdvSize()

	cTitle := STR0001 + " - " + ::cIdEntidade + " - " + ::cVersaoTSS // NFCom - Entidade - Vers„o TSS

	if SubStr(Alltrim(::aParametros[1]),1,1) == "1"
		nOpcDoc := 1
		aAdd( aSeek, { "Documento" , {{"","C", GetSX3Cache( "F2_DOC","X3_TAMANHO"), 0 , "Documento",,}}} )
		aAdd( aSeek, { "Chave da NFE" , {{"","C", GetSX3Cache( "F2_CHVNFE","X3_TAMANHO") , 0 , "Chave da NFE",,}}} )
		aAdd( aIndex, "F2_DOC" )
		aAdd( aIndex, "F2_CHVNFE" )

	elseif SubStr(Alltrim(::aParametros[1]),1,1) == "2"
		nOpcDoc := 2
		aAdd( aSeek, { "Documento" , {{"","C", GetSX3Cache( "F1_DOC","X3_TAMANHO"), 0 , "Documento",,}}} )
		aAdd( aSeek, { "Chave da NFE" , {{"","C", GetSX3Cache( "F1_CHVNFE","X3_TAMANHO") , 0 , "Chave da NFE",,}}} )
		aAdd( aIndex, "F1_DOC" )
		aAdd( aIndex, "F1_CHVNFE" )

	endif

	DEFINE MSDIALOG oDlgNotas TITLE cTitle FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL

	oBrowseNotas := FWFormBrowse():New()
	oBrowseNotas:SetDataQuery()
	oBrowseNotas:SetAlias(::cAliasBrowser)
	oBrowseNotas:SetOwner(oDlgNotas)
	oBrowseNotas:SetDescription(cTitle)
	oBrowseNotas:SetShowLimit(.T.)
	oBrowseNotas:SetQueryIndex(aIndex)
	oBrowseNotas:SetQuery(::cQueryDocumentos)
	oBrowseNotas:SetSeek(,aSeek)
	oBrowseNotas:SetDescription( STR0038) //"Transmiss„o NFCom - Nota Fiscal Fatura de ServiÁo de ComunicaÁ„o EletrÙnica"
	If SubStr(Alltrim(::aParametros[1]),1,1) == "1"
		oBrowseNotas:AddLegend("F2_FIMP == 'T'","BR_AZUL"		, STR0039) 	  //"NFCom Transmitida"
		oBrowseNotas:AddLegend("F2_FIMP == ' '","BR_VERMELHO"	, STR0040) 	  //"NFCom N„o Transmitida"
		oBrowseNotas:AddLegend("F2_FIMP == 'S'","BR_VERDE"		, STR0041) 	  // "NFCom Autorizada"
		oBrowseNotas:AddLegend("F2_FIMP == 'N'","BR_PRETO"		, STR0042)    //"NFCom N„o autorizada"
	ElseIf SubStr(Alltrim(::aParametros[1]),1,1) == "2"
		oBrowseNotas:AddLegend("F1_FIMP == 'T'","BR_AZUL"		, STR0039) 	  //"NFCom Transmitida"
		oBrowseNotas:AddLegend("F1_FIMP == ' '","BR_VERMELHO"	, STR0040) 	  //"NFCom N„o Transmitida"
		oBrowseNotas:AddLegend("F1_FIMP == 'S'","BR_VERDE"		, STR0041) 	  // "NFCom Autorizada"
		oBrowseNotas:AddLegend("F1_FIMP == 'N'","BR_PRETO"		, STR0042)    //"NFCom N„o autorizada"
	EndIf
	oBrowseNotas:DisableDetails()

	aColumns    := {}
	aFields     := {}

	if SubStr(Alltrim(::aParametros[1]),1,1) == "1"

		aAdd(aFields,{"F2_DOC",STR0009}) 	 // "Documento"
		aAdd(aFields,{"F2_SERIE",STR0010})   // "Serie"
		aAdd(aFields,{"F2_CLIENTE",STR0011}) // "Cliente/Fornecedor"
		aAdd(aFields,{"F2_LOJA",STR0012}) 	 // "Loja"
		aAdd(aFields,{"F2_CHVNFE",STR0014})  // "Chave da NFcom"
		aAdd(aFields,{"F2_EMISSAO",STR0015}) // "Data de Emiss„o"

	elseif SubStr(Alltrim(::aParametros[1]),1,1) == "2"

		aAdd(aFields,{"F1_DOC",STR0009}) 	 // "Documento"
		aAdd(aFields,{"F1_SERIE",STR0010})   // "Serie"
		aAdd(aFields,{"F1_FORNECE",STR0011}) // "Cliente/Fornecedor"
		aAdd(aFields,{"F1_LOJA",STR0012}) 	 // "Loja"
		aAdd(aFields,{"F1_CHVNFE",STR0014})  // "Chave da NFCom"
		aAdd(aFields,{"F1_EMISSAO",STR0015}) // "Data de Emiss„o"

	endif

	For nContFlds := 1 To Len(aFields)

		aStructCampo := FWSX3Util():GetFieldStruct(aFields[nContFlds,1])
		oColumns :=  FWBrwColumn():New()
		if Alltrim(aStructCampo[2]) == "D"
			oColumns:SetData(&("{|| DtoC(StoD("+aFields[nContFlds,1]+"))}"))
		else
			oColumns:SetData(&("{|| "+aFields[nContFlds,1]+" }"))
		endif
		oColumns:SetTitle( aFields[nContFlds,2])
		oColumns:SetSize(aStructCampo[3])
		oColumns:SetType(aStructCampo[2])
		oColumns:SetPicture(Alltrim(GetSX3Cache(aFields[nContFlds,1],"X3_PICTURE")))

		aAdd(aColumns,oColumns)
	Next

	oBrowseNotas:AddButton(STR0016,{||oDlgNotas:End()},,,,.F.) // Sair
	if !lUsaColab
		oBrowseNotas:AddButton(STR0017,{|| SpedNFeCfg('62')},,,,.F.) // "Wiz.Config."
	endif
	oBrowseNotas:AddButton( STR0018,{|| SpedNFePar('62')},,,,.F.) // "Par‚metros NFCom"
	oBrowseNotas:AddButton( STR0019,{|| (::MontaTransmissao(oBrowseNotas))},,,,.F.) // "Transmitir"
	oBrowseNotas:AddButton( STR0020,{|| (::MonitorFaixa(oBrowseNotas))},,,,.F.) // "Monitorar"
	oBrowseNotas:AddButton( STR0044,{|| ::SpedDanfcom()},,,,.F.) // "Imprimir DANFCom"
	oBrowseNotas:AddButton( STR0021,{|| SpedNFe5Mnt( , '62')},,,,.F.) // "Consultar chave"
	oBrowseNotas:AddButton( STR0022,{|| ::VisualizaDocumento(nOpcDoc) },,,,.F.) // "Visualizar Documento"
	oBrowseNotas:AddButton( STR0043,{|| (::oNFComCancelada:MontaTelaCancelamentos())},,,,.F.) // "Cancelamentos"
	oBrowseNotas:AddButton( STR0023,{|| ::NFComLegenda() },,,,.F.) // "Legendas"
	oBrowseNotas:AddButton( STR0069,{|| oBrowseNotas:Refresh(.T.)},,,,.F.) // "Atualizar tela (refresh)"
	oBrowseNotas:SetColumns(aColumns)
	oBrowseNotas:Activate()

	ACTIVATE MSDIALOG oDlgNotas

	oBrowseNotas := FwFreeObj(oBrowseNotas)
	oBrowseNotas := nil
	oDlgNotas := FwFreeObj(oDlgNotas)
	oDlgNotas := nil
	self:aParametros := FwFreeArray(self:aParametros)
	aColumns := FwFreeArray(aColumns)
	aFields := FwFreeArray(aFields)
	aSize := aSize(aSize, 0)
	aStructCampo := aSize(aStructCampo, 0)
	aSeek := aSize(aSeek, 0)
	aIndex := aSize(aSeek, 0)
Return

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method MonitorFaixa
	Monta Tela Principal do Monitor - Schemas da NFcom.

    @author Gabriel De Jesus Silva
    @since 11.06.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method MonitorFaixa(oBrowseNotas as Object) Class NFCom
	Default oBrowseNotas := Nil

	SpedNFe6Mnt(,,,,,"62")

	oBrowseNotas:Refresh(.T.)
Return

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method MontaTransmissao
	Monta Tela Principal de Transmissao NFcom.

    @author Felipe Duarte Luna
    @since 02.06.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method MontaTransmissao( oBrowseNotas as Object ) class NFCom

	Local lRetorno := .T. as Logical
	Local lNFCom   := .T. as Logical
	Local aNotas   :=  {} as Array
	
	SpedNFeRe2(NIL, Nil, Nil, .F., @lRetorno , aNotas, lNFCom ) // NFCom chamdada do mÈtodo Remessa da rotina NFe - Sefaz
	
	//Ativa e atualiza o browse
	oBrowseNotas:Refresh(.T.)

Return

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method VisualizaDocumento
	Exibe o Documento selecionado no Brouser inicial

    @author Felipe Duarte Luna
    @since 02.06.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method VisualizaDocumento(nOpc as numeric) class NFCom

	if nOpc == 1

		DBSELECTAREA("SF2")
		SF2->(DBGOTO((::cAliasBrowser)->RECNOTA))
		Mc090Visual("SF2",SF2->(RECNO()),1)

	elseif nOpc == 2
		DBSELECTAREA("SF1")
		SF1->(DBGOTO((::cAliasBrowser)->RECNOTA))
		A103NFiscal("SF1",SF1->(RECNO()),1)
	endif
Return

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method ThemaSistema
	Recebe o Thema selecionado para ajuste de cores do SCSS das Telas

    @author Felipe Duarte Luna
    @since 02.06.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method ThemaSistema() class NFCom

	Local lRelease2410 	:= GetRpoRelease() >= "12.1.2410" as logical
	Local oTheme := nil as object

	If lRelease2410
		oTheme := totvs.framework.css.ProtheusTheme():New()
		if Alltrim(oTheme:CTHEME) == "DARK"
			::lThemeDark := .T.
		endif
	EndIf
	FreeObj(oTheme)
	oTheme := nil
Return

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method NFComLegenda
	Legenda do Status da NFCom

    @author Felipe Duarte Luna
    @since 05.06.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method NFComLegenda() class NFCom	
	Local aLegenda := {} as array

	AADD(aLegenda, {"BR_AZUL"		, STR0025})		//1-Transmitidos
	AADD(aLegenda, {"DISABLE"		, STR0026})		//2-N„o Transmitidos
	AADD(aLegenda, {"BR_VERDE"		, STR0027})		//3-Autorizados
	AADD(aLegenda, {"BR_PRETO"		, STR0028})		//4-N„o Autorizados
	AADD(aLegenda, {"BR_LARANJA"	, STR0029})		//5-Cancelados

	BrwLegenda( STR0030, STR0045, aLegenda) // "Legenda do Status da NFCom: " + "Legenda"
	aSize(aLegenda, 0)
Return	

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method SpedDanfcom
	MÈtodo que chama a impress„o do DANFCOM, com base no ambiente e entidade configurados.

	@author Antonio Marfil
	@since 18.06.2025
	@version 12.1.2410
/*///-----------------------------------------------------------------------

Method SpedDanfcom() class NFCom

	Local cIdEnt 		:= getCfgEntidade()
	Local aConfig		:= getCfgNFCom()
	Default cAmb		:= "2"

	If SubStr(Alltrim(aConfig[1]),1,1) == "1"
		cAmb := "1"
	EndIf

	tssExecRdm("PrtNfcom", .F., cIdEnt ,cAmb)

	//Limpa arquivos temporarios .rel da pasta MV_RELT
	SpedCleRelt()

Return()

//-----------------------------------------------------------------------
/*/{Protheus.doc} SpedCleRelt
Limpa arquivos temporarios .rel da pasta MV_RELT

@author Antonio Marfil
@since 18.06.2015
@version 12.1.2410

@param	Null

/*/
//-----------------------------------------------------------------------
Static Function SpedCleRelt()
	Local cPath		:= SuperGetMV('MV_RELT',,"\SPOOL\")
	Local aArquivos := {}
	Local nX		:= 0
	Local cNome		:= ""

	aArquivos := Directory(cPath + "*.rel", "D")

	For nX := 1 to Len(aArquivos)
		cNome := LOWER(aArquivos[nX,1])
		If AT("danfe", cNome) > 0
			FERASE(cPath + cNome)
		EndIf
	Next nX

	aSize(aArquivos, 0)
Return

/*/{Protheus.doc} NFComCanceladas
Classe que gerencia as NFCom canceladas, permitindo a transmiss„o e visualizaÁ„o dos cancelamentos.
@type class
@version 12.1.2410
@author fs.martinez
@since 6/20/2025
/*/
Class NFComCanceladas

	Public Data oMrkBrowse as object
	Protected Data cAliasCanc as character
	Private	Data cIdEntidade as character
	Private	Data cVersaoTSS as character

	Public	Method New() constructor
	Private Method TransmiteCanceladas()
	Private Method ShowJustificativa()
	Private Method ShowCancResult()
	Private Method UpdNFComCanceladas()

	Protected Method MontaTelaCancelamentos()
	Protected Method TransmiteNFComCanceladas()
	Protected Method NFComProcessaCanceladas()

EndClass

/*/{Protheus.doc} NFComCanceladas::New
MÈtodo construtor da classe NFComCanceladas.
@type method
@version 12.1.2410
@author fs.martinez
@since 17/6/2025
@param cIdEntidade, character, Codigo da entidade configurada no TSS.
@param cVersaoTSS, character, Vers„o do TSS configurada no Protheus.
/*/
Method New(cIdEntidade as character, cVersaoTSS as character) class NFComCanceladas

	default cIdEntidade := getCfgEntidade()
	default cVersaoTSS := getVersaoTSS()

	self:cAliasCanc	:= ""
	self:oMrkBrowse	:= nil
	self:cIdEntidade:= cIdEntidade 
	self:cVersaoTSS := cVersaoTSS

Return

/*/{Protheus.doc} NFCom::MontaTelaCancelamentos
MÈtodo respons·vel por montar a tela de cancelamentos das NFCom. Monta a tela de cancelamentos das NFCom, permitindo ao usu·rio filtrar por data de cancelamento e sÈrie, e exibir as NFCom canceladas.
@type method
@version 12.1.2410
@author fs.martinez
@since 17/6/2025
/*/
Method MontaTelaCancelamentos() class NFComCanceladas
	local nX			:= 0 as integer
	local nParam		:= 1 as integer
	Local nTamSerie 	:= FWSX3Util():GetFieldStruct("F2_SERIE")[3] as integer
	Local cParName   	:= SM0->M0_CODIGO + SM0->M0_CODFIL + "nfcomcanceladas" as character
	local cFieldColumns := "F3_NFISCAL,F3_SERIE,F3_CLIEFOR,F3_LOJA,F3_EMISSAO,F3_DTCANC,F3_CHVNFE,F3_PROTOC" as character
	local cQuery 		:= "" as character
	local cOldCadastro	:= "" as character
	local aColumns 		:= {} as array
	Local aPerg     	:= {} as array
	local aSize			:= {} as array
	local aFields		:= {} as array
	local aOldArea 		:= getArea() as array
	Local aParam    	:= {ctod("//"),ctod("//"),Space(nTamSerie)} as array // Inicializa os parametros com data inicial, data final e serie
	local aStru			:= SF3->(dbStruct()) as array
	local oQueryCanc	:= nil as object
	
	private oDlgCanc		:= nil as object

	aadd(aPerg,{1,STR0047,aParam[1],"99/99/9999",".T.","",".T.",50,.T.}) // "Data Inicial Cancelamento"
	aadd(aPerg,{1,STR0048,aParam[2],"99/99/9999",".T.","",".T.",50,.T.}) // "Data Final Cancelamento"
	aAdd(aPerg,{1,STR0049,aParam[3],"",".T.","",".T.",30,.F.}) // "SÈrie da NFCom"
	
	aParam[1] := ParamLoad(cParName,aPerg,1,aParam[1])
	aParam[2] := ParamLoad(cParName,aPerg,2,aParam[2])
	aParam[3] := ParamLoad(cParName,aPerg,3,aParam[3])

	if type("cCadastro") == "C"
		cOldCadastro := cCadastro
	endIf

	cCadastro := STR0050 // "Filtro para exibiÁ„o de NFCom cancelada(s)"
	If ParamBox(aPerg,self:cIdEntidade + " - " + self:cVersaoTSS,@aParam,,,,,,,cParName,.T.,.T.)
		aFields := StrTokArr2(cFieldColumns,",")
		For nX := 1 To Len(aFields)
			if Len(aStru := FWSX3Util():GetFieldStruct(aFields[nX])) >= 4
				AAdd(aColumns,FWBrwColumn():New())
				nPos := Len(aColumns)
				aColumns[nPos]:SetData( &("{||"+aStru[1]+"}") )
				if Alltrim(aStru[2]) == "D"
					aColumns[nPos]:SetData( &("{|| StoD("+aStru[1]+")}") )
				endif
				aColumns[nPos]:SetTitle(RetTitle(aStru[1]))
				aColumns[nPos]:SetSize(aStru[3])
				aColumns[nPos]:SetDecimal(aStru[4])
				aColumns[nPos]:SetPicture(aStru[5])
				aColumns[nPos]:SetAlign( 0 )
			endIf
		Next nX

		aFields := aSize(aFields, 0)

		cQuery := "SELECT ?"
		cQuery += " FROM " + RetSqlName("SF3")
		cQuery += " WHERE F3_FILIAL = ?"
		cQuery += " AND F3_DTCANC >= ?"
		cQuery += " AND F3_DTCANC <= ?"
		cQuery += " AND F3_ESPECIE = ?"
		cQuery += " AND F3_CODRET <> ?"
		cQuery += " AND F3_CODRSEF NOT IN (?)"
		cQuery += " AND F3_CHVNFE  <> ?"
		cQuery += " AND F3_PROTOC  <> ?"
		if !empty(MV_PAR03)
			cQuery += " AND F3_SERIE = ?"
		endIf
		
		oQueryCanc := FwExecStatement():New(ChangeQuery(cQuery))
		oQueryCanc:SetUnsafe(nParam++ , "' ' MARK," + cFieldColumns) //Campos das query
		oQueryCanc:SetString(nParam++ , xFilial("SF3") ) 	// Filial
		oQueryCanc:SetString(nParam++ , dtos(MV_PAR01) ) 	// Dt Cancelamento Inicial
		oQueryCanc:SetString(nParam++ , dtos(MV_PAR02) ) 	// Dt Cancelamento Inicial
		oQueryCanc:SetString(nParam++ , 'NFCOM' ) 			// Especie
		oQueryCanc:SetString(nParam++ , 'T'  ) 				// Codigo de Retorno TSS
		oQueryCanc:setIn(nParam++, StrTokArr2( strTran(RetCodDene(),"'","")+",101,102,151,155", ',' , .F.) )
		oQueryCanc:SetString(nParam++ , "" ) 				// Chave da NFCom
		oQueryCanc:SetString(nParam++ , "" ) 				// Protocolo de AutorizaÁ„o
		if !empty(MV_PAR03)
			oQueryCanc:SetString(nParam++ , allTrim(MV_PAR03) ) 				// Serie
		endIf
		self:cAliasCanc := GetNextAlias()
		
		aSize := MsAdvSize()
		
		DEFINE MSDIALOG oDlgCanc TITLE STR0051 FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL // "Transmiss„o de NFCom cancelada(s)"

			self:oMrkBrowse := FWMarkBrowse():New()
			self:oMrkBrowse:SetOwner(oDlgCanc)
			self:oMrkBrowse:SetDataQuery(.T.)
			self:oMrkBrowse:SetTemporary(.T.)
			self:oMrkBrowse:SetQuery(oQueryCanc:getFixQuery())
			self:oMrkBrowse:SetAlias(self:cAliasCanc)
			self:oMrkBrowse:SetColumns(aColumns)
			self:oMrkBrowse:SetFieldMark("MARK")
			self:oMrkBrowse:SetMark("X", self:cAliasCanc, "MARK")
			self:oMrkBrowse:SetAllMark( { || self:oMrkBrowse:AllMark() } )
			self:oMrkBrowse:SetDescription(STR0052) // "NFCom cancelada(s) pendente(s) de transmiss„o"
			self:oMrkBrowse:AddButton(STR0019, { || (self:TransmiteNFComCanceladas(), self:oMrkBrowse:Refresh(.T.)) } , nil, 2) // "Transmitir"
			self:oMrkBrowse:AddButton(STR0016, { || oDlgCanc:End() }, nil, 8) // "Sair"

			self:oMrkBrowse:SetIgnoreARotina(.T.)
			self:oMrkBrowse:SetMenuDef("")
			self:oMrkBrowse:DisableConfig()
			self:oMrkBrowse:DisableDetails()
			self:oMrkBrowse:DisableReport()

			self:oMrkBrowse:Activate()
		
		ACTIVATE MSDIALOG oDlgCanc
		
	EndIf  

	restArea(aOldArea)
	cCadastro := cOldCadastro

	self:oMrkBrowse := FwFreeObj(self:oMrkBrowse)
	oQueryCanc := FwFreeObj(oQueryCanc)
	oDlgCanc := FwFreeObj(oDlgCanc)
	aOldArea := aSize(aOldArea, 0)
	aPerg := aSize(aPerg, 0)
	aParam := aSize(aParam, 0)
	aStru := aSize(aStru, 0)
	aColumns := FwFreeArray(aColumns)
	aDicStruct := FwFreeArray(aDicStruct)
	aSize := aSize(aSize, 0)

return nil

/*/{Protheus.doc} NFCom::TransmiteNFComCanceladas
MÈtodo respons·vel por transmitir as NFCom canceladas selecionadas pelo usu·rio.
@type method
@version 12.1.2410
@author fs.martinez
@since 17/6/2025
/*/
method TransmiteNFComCanceladas() Class NFComCanceladas
	local cJustificativa as character
	local aNFComCanceladas as array
	local oMsgRun as object
	
	(self:cAliasCanc)->(dbGoTop())
	if (self:cAliasCanc)->(!EOF()) .and. self:ShowJustificativa(@cJustificativa)
		FWMsgRun(, {|oMsgRun| aNFComCanceladas := self:NFComProcessaCanceladas(@oMsgRun, cJustificativa) },, STR0053) //"Transmitindo NFCom cancelada(s)... Por favor, aguarde."
		if self:showCancResult(aNFComCanceladas)
			self:oMrkBrowse:GoTo(1,.T.)
		endIf
		oMsgRun := FwFreeObj(oMsgRun)
	endIf

	aNFComCanceladas := FwFreeArray(aNFComCanceladas)
	aNFComCanceladas := nil
return 

/*/{Protheus.doc} NFCom::ShowJustificativa
MÈtodo respons·vel por exibir a tela para preenchimento de justificativa de cancelamento das NFCom.
@type method
@version 12.1.2410
@author fs.martinez
@since 17/6/2025
@param cXjust, character, Vari·vel que receber· a justificativa do cancelamento.
@return logical, Retorna .T. se o usu·rio continuar com a justificativa preenchida, ou .F. se cancelar a operaÁ„o.
/*/
Method ShowJustificativa(cXjust as character) Class NFComCanceladas
	local lContinua as logical

	default cXjust	:= Space(200)
	
	while .T.
		DEFINE MSDIALOG oDlg TITLE STR0054 FROM 0,0 TO  180, 380 PIXEL // "Justificativa de Cancelamento"
			DEFINE FONT oFont BOLD
			@ 5,5 SAY oSay PROMPT STR0055 OF oDlg FONT oFont PIXEL SIZE 180, 030 // "Informe a justificativa do cancelamento da(s) NFCom:"
			@ 20,5 GET oGet VAR cXjust SIZE 175,30 MULTILINE OF oDlg PIXEL
			oTButton1 := TButton():New( 070, 060, STR0056,oDlg,{|| (lContinua := .T.,oDlg:End())},40,10,,,.F.,.T.,.F.,,.F.,,,.F. ) // "OK"
			oTButton2 := TButton():New( 070, 110, STR0057,oDlg,{||(lContinua :=.F.,oDlg:End())},40,10,,,.F.,.T.,.F.,,.F.,,,.F. ) // "Cancelar"
		ACTIVATE MSDIALOG oDlg CENTERED
		
		if !lContinua .or. lContinua .and. !empty(cXjust)
			exit
		endIf
	end

	oDlg := FwFreeObj(oDlg)
	oFont := FwFreeObj(oFont)
	oSay := FwFreeObj(oSay)
	oGet := FwFreeObj(oGet)
	oTButton1 := FwFreeObj(oTButton1)
	oTButton2 := FwFreeObj(oTButton2)

return lContinua

/*/{Protheus.doc} NFCom::NFComProcessaCanceladas
MÈtodo responsavel por processar as NFCom canceladas, gerando o XML de cancelamento e transmitindo para a SEFAZ.
@type method
@version 12.1.2410
@author fs.martinez
@since 17/6/2025
@param oMsgRun, object, Objeto de mensagem para exibir o progresso da transmiss„o das NFCom canceladas.
@param cJustificativa, character, DescriÁ„o da justificativa do cancelamento das NFCom.
@return array, Array com as NFCom canceladas e o resultado da transmiss„o.
/*/
method NFComProcessaCanceladas(oMsgRun as object, cJustificativa as character) Class NFComCanceladas
	local cXmlEvento 	:= ""  		as character
	local cMsg		 	:= ""  		as character
	local cError	 	:= ""  		as character
	local aNFComCanc	:= {} 		as array
	local aRetorno		:= {} 		as array
	local lHasMark					as logical
	
	default oMsgRun 		:= nil
	default cJustificativa	:= STR0058 //"Cancelamento de NFCom por emiss„o indevida."
	
	(self:cAliasCanc)->(dbGoTop())
	if (self:cAliasCanc)->(!EOF())

		cXmlEvento +=	'<envEvento>'
		cXmlEvento +=		'<eventos>'
		
		while (self:cAliasCanc)->(!EOF())
			if !empty((self:cAliasCanc)->MARK)
				cXmlEvento +=	'<detEvento>'
				cXmlEvento +=		'<tpEvento>' + EVCANCELAMENTO + '</tpEvento>'
				cXmlEvento +=		'<chNFe>' + allTrim((self:cAliasCanc)->F3_CHVNFE) + '</chNFe>'
				cXmlEvento +=		'<evCancNFCom>'
				cXmlEvento +=			'<nprot>' + allTrim((self:cAliasCanc)->F3_PROTOC) + '</nprot>'
				cXmlEvento +=			'<xJust>' + cJustificativa + '</xJust>'
				cXmlEvento +=		'</evCancNFCom>'
				cXmlEvento +=	'</detEvento>'
				lHasMark := .T.

				aAdd(aNFComCanc, Array(6))
				aTail(aNFComCanc)[NPOSDOC] 		:= (self:cAliasCanc)->F3_NFISCAL
				aTail(aNFComCanc)[NPOSSERIE] 	:= (self:cAliasCanc)->F3_SERIE
				aTail(aNFComCanc)[NPOSCLIFOR] 	:= (self:cAliasCanc)->F3_CLIEFOR
				aTail(aNFComCanc)[NPOSLOJA] 	:= (self:cAliasCanc)->F3_LOJA
				aTail(aNFComCanc)[NPOSCHV] 		:= allTrim((self:cAliasCanc)->F3_CHVNFE)
				aTail(aNFComCanc)[NPOSEMISSAO] 	:= (self:cAliasCanc)->F3_EMISSAO
			endif
			(self:cAliasCanc)->(dbSkip())
		end
		cXmlEvento +=		'</eventos>'
		cXmlEvento +=	'</envEvento>'

		cMsg := STR0059 // "Nenhuma NFCom marcada para transmiss„o."
		if lHasMark
			cMsg := ""
			aRetorno := TSSEnvEven(@cError, nil, RetIdEnti(), cXmlEvento, @cMsg)
			if len(aRetorno) > 0
				self:UpdNFComCanceladas(aNFComCanc, aRetorno)
			endIf
		endIf
	endIf

return {cMsg, cError, aNFComCanc}

/*/{Protheus.doc} NFCom::UpdNFComCanceladas
Metodo responsavel por atualizar as NFCom canceladas com o resultado da transmiss„o.
@type method
@version 12.1.2410
@author fs.martinez
@since 17/6/2025
@param aNFComCanc, array, Array com as NFCom canceladas.
@param aRetorno, array, Array com o resultado da transmiss„o das NFCom canceladas.
/*/
Method UpdNFComCanceladas(aNFComCanc as array, aRetorno as array) Class NFComCanceladas
	local cChave as character
	local nPos as integer
	local nI as integer

	for nI := 1 to len(aRetorno)
		cChave := allTrim(aRetorno[nI])
		cChave := substr(cChave,at("ID"+EVCANCELAMENTO, cChave)+len("ID"+EVCANCELAMENTO), 44)
		if (nPos := aScan(aNFComCanc, {|x| x[NPOSCHV] == cChave})) > 0
			NFComUpdF3({xFilial("SF3"), aNFComCanc[nI,NPOSEMISSAO], aNFComCanc[nI,NPOSSERIE], aNFComCanc[nI,NPOSDOC], aNFComCanc[nI,NPOSCLIFOR],aNFComCanc[nI,NPOSLOJA]})
		endIf
	next nI

Return

/*/{Protheus.doc} NFCom::ShowCancResult
Metodo responsavel por exibir o resultado da transmiss„o das NFCom canceladas.
@type method
@version 12.1.2410
@author fs.martinez
@since 17/6/2025
@param aResult, array, Array com o resultado da transmiss„o das NFCom canceladas.
@return logical, lRet, Retorna .T. se a transmiss„o foi bem sucedida e .F. caso contr·rio.
/*/
Method ShowCancResult(aResult as array) Class NFComCanceladas
	local cMsg as character
	local cNFComCanceladas as character
	local nI as integer
	local lRet as logical
	local aNFComCanceladas as array

	if len(aResult) >= 3
		cMsg := STR0060 + replic(ENTER,3) + STR0061 + aResult[1] + replic(ENTER,3) + STR0062 + aResult[2] // "Foi encontrado o seguinte erro na transmiss„o: " + "Mensagem de aviso: " + "Erro: "
		if empty(aResult[1]) .and. empty(aResult[2])
			aNFComCanceladas := aResult[3]
			
			for nI := 1 to len(aNFComCanceladas)
				cNFComCanceladas += alltrim(aNFComCanceladas[nI,NPOSSERIE]) + "-" + alltrim(aNFComCanceladas[nI,NPOSDOC]) + CHR(13)+CHR(10)
			Next nI			
			
			cMsg := STR0063 + cvalToChar(len(aNFComCanceladas)) +  STR0064 + replic(ENTER,2) + cNFComCanceladas + replic(ENTER,2) // "Foi(ram) transmitida(s) " + " NFCom cancelada(s):"
			cMsg += STR0065 //"Monitore este(s) documento(s) para atualizaÁ„o de status."
			lRet := .T.
			aNFComCanceladas := aSize(aNFComCanceladas, 0)
		endif
		Aviso(STR0001,cMsg,{STR0056},3) //NFCom - OK
	endif

Return lRet
