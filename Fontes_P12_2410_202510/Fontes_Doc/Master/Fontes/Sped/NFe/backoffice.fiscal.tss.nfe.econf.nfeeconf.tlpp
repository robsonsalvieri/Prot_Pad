#include 'tlpp-core.th'
#include 'tlpp-object.th'
#include 'totvs.ch'
#include 'nfeeconf.ch'

/*/{Protheus.doc} NfeEConf
    Classe Responsável pelas 
    @author Ricardo Henrique de Mello Lima
    @since 15/08/2024
    @version 12.1.2310
    /*/

//namespace totvs.tss.transmissao.econf
namespace backoffice.fiscal.tss.nfe.econf
Class NfeEConf

	Public Data cIdEntidade as character
	Public Data cVersaoTSS as character
	Public Data aParametros as array
	public Data cEventoTransmissao as character
	public Data cEventoCancelamento as character
	public Data cEventoMonitor as character
	public Data cEventoSelecionado as character
	Private Data aTpagamentos as array
	Private Data aBandeiras as array
	Private Data aListaUF as array
	Private Data aTipoEvento as array
	Private Data aIndPag as array
	Private Data cAliasBrowser as character
	Private Data cQueryDocumentos as character
	Private Data cAliasDocumentos as character
	Public Data cTipoCli as character
	Public Data cChaveDoc as character
	Public Data cCnpjNota as character
	Public Data aBrwDados as array
	Public Data aRetEvento as array
	Public Data aBrwMonitor as array

	Public Data dGetData as date
	Public Data cCNPJPag as character
	Public Data cCNPJF as character
	Public Data cCNPJR as character
	Public Data cUFRec as character
	Public Data cAutCart as character
	Public Data nValor as numeric
	Private Data lThemeDark as logical

	Private Data oPanelDadosBrowser  as object
	Public Data oOk as object
	Public Data oNo as object
	Public Data oBrowserTitulos as object
	Public Data oWorkAreaContainer as object
	Public Data oWorkAreaMain as object
	Public Data oGrpBrowser as object
	Public Data oFont as object

	Private Data oComboEvento as object
	Private Data oComboIndPag as object
	Private Data oComboPagametos as object
	Private Data oTGetData as object
	Private Data oCNPJPag as object
	Private Data oComboUFP as object
	Private Data oCNPJF as object
	Private Data oBandeira as object
	Private Data oCNPJR as object
	Private Data oComboUFR as object
	Private Data oAutCart as object
	Private Data oValor as object

	Private Data oTSayTransacao as object
	Private Data oTSayComboIndPag as object
	Private Data oTSayComboPagamentos as object
	Private Data oTSayGetData as object
	Private Data oTSayCNPJPag as object
	Private Data oTSayComboUFP as object
	Private Data oTSayCNPJF as object
	Private Data oTSayBandeira as object
	Private Data oTSayCNPJR as object
	Private Data oTSayComboUFR as object
	Private Data oTSayAutCart as object
	Private Data oTSayValor as object

	Private Data oButtonAdd as object
	Private Data oButtonDel as object

	Public Method New() constructor
	Protected Method Active(cIdEnt as character, cVersaoTSS as character, lUsaColab as logical)
	Private Method MontaTelaParametros()
	Private Method MontaQueryDocumentos()
	Protected Method ValidaFormulario() as logical
	Private Method ProcessaDados()
	Private Method MontaTelaDocumentos()
	Protected Method MontaTransmissao()
	Protected Method VisualizaDocumento(nOpc as numeric)
	Private Method ThemaSistema()

	Protected Method SetTrocaEvento()
	Protected Method TrocarEvento()
	Protected Method MontaBrowser()
	Protected Method MonitoraEvento(cEvento as character ) as array
	Protected Method AdicionaEvento(cTPagamentos as character,cUFPag as character,cBandeira as character,cUFRec as character,cIndPag as character)
	Protected Method RemoveEvento()
	Protected Method EnviaEvento(cXml as character) as logical
	Protected Method PreparaEvento() as logical
	Protected Method SelecionaCancelamento(nOpc as numeric)
	Protected Method RefreshBrowserMonitor()
	Protected Method MostraMenssagem(cTitulo as character, cSubTitulo as character, cMenssagem as character)

EndClass

/*/{Protheus.doc} New
	Método Construtor da Classe
	@author Ricardo Henrique de Mello Lima
	@since 23/08/2024
	@version 12.1.2310
	/*/
Method New() class NfeEConf

	::cEventoTransmissao := "110750"
	::cEventoCancelamento := "110751"
	::cEventoMonitor := "110000"
	::cEventoSelecionado := ""
	::cTipoCli := ""
	::cChaveDoc := ""
	::cCnpjNota := ""
	::aBrwDados := {}
	::aRetEvento := {}
	::aBrwMonitor := {}

	::dGetData := date()
	::cCNPJPag := Space(20)
	::cCNPJF := Space(20)
	::cCNPJR := Space(20)
	::cUFRec := Space(20)
	::cAutCart := Space(20)
	::nValor := 0
	::lThemeDark := .F.

	::oOk := nil
	::oNo := nil
	::oBrowserTitulos := nil
	::oWorkAreaContainer := nil
	::oWorkAreaMain := nil
	::oGrpBrowser := nil

	::oPanelDadosBrowser := nil
	::oComboEvento := nil
	::oComboIndPag := nil
	::oComboPagametos := nil
	::oTGetData := nil
	::oCNPJPag := nil
	::oComboUFP := nil
	::oCNPJF := nil
	::oBandeira := nil
	::oCNPJR := nil
	::oComboUFR := nil
	::oAutCart := nil
	::oValor := nil

	::oTSayTransacao := nil
	::oTSayComboIndPag := nil
	::oTSayComboPagamentos := nil
	::oTSayGetData := nil
	::oTSayCNPJPag := nil
	::oTSayComboUFP := nil
	::oTSayCNPJF := nil
	::oTSayBandeira := nil
	::oTSayCNPJR := nil
	::oTSayComboUFR := nil
	::oTSayAutCart := nil
	::oTSayValor := nil
	::oFont := TFont():New(,,-12,.T.,.F.,,,,,)

	::aTpagamentos := {}
	::aBandeiras := {}
	::aListaUF := {}
	::aTipoEvento := {}
	::aIndPag := {}
	::cAliasBrowser := ""
	::aParametros := {}

Return

/*/{Protheus.doc} Active
	Metodo que monta o processo
	@author Ricardo Henrique de Mello Lima
	@since 23/08/2024
	@version 12.1.2310
	/*/
Method Active(cIdEnt as character, cVersaoTSS as character, lUsaColab as logical) class NfeEConf

	::cIdEntidade := cIdEnt
	::cVersaoTSS := cVersaoTSS

	aAdd(::aTpagamentos,STR0054) // '01-Dinheiro'
	aAdd(::aTpagamentos,STR0055) // '02-Cheque'
	aAdd(::aTpagamentos,STR0056) // '03-Cartão de Crédito'
	aAdd(::aTpagamentos,STR0057) // '04-Cartão de Débito'
	aAdd(::aTpagamentos,STR0058) // '05-Cartão da Loja'
	aAdd(::aTpagamentos,STR0059) // '10-Vale Alimetação'
	aAdd(::aTpagamentos,STR0060) // '11-Vale Refeição'
	aAdd(::aTpagamentos,STR0061) // '12-Vale Presente'
	aAdd(::aTpagamentos,STR0062) // '13-Vale Combustível'
	aAdd(::aTpagamentos,STR0063) // '14-Duplicata Mercantil'
	aAdd(::aTpagamentos,STR0064) // '16-Depósito Bancário'
	aAdd(::aTpagamentos,STR0065) // '17-Pagamento Instantâneo (PIX) Dinámico'
	aAdd(::aTpagamentos,STR0066) // '18-Transferência Bancária'
	aAdd(::aTpagamentos,STR0067) // '19-Cashback'
	aAdd(::aTpagamentos,STR0068) // '20-Pagamento Instantâneo (PIX) Estático'
	aAdd(::aTpagamentos,STR0069) // '21-Crédito Loja'
	aAdd(::aTpagamentos,STR0070) // '22-Pagamento Não Informado'
	aAdd(::aTpagamentos,STR0071) // '90-Sem Pagamento'
	aAdd(::aTpagamentos,STR0072) // '99-Outros'

	aAdd(::aBandeiras,'')
	aAdd(::aBandeiras,STR0073) // '01-Visa')
	aAdd(::aBandeiras,STR0074) // '02-Mastercard')
	aAdd(::aBandeiras,STR0075) // '03-American Express')
	aAdd(::aBandeiras,STR0076) // '04-Sorocred')
	aAdd(::aBandeiras,STR0077) // '05-Diners Club')
	aAdd(::aBandeiras,STR0078) // '06-Elo')
	aAdd(::aBandeiras,STR0079) // '07-Hipercard')
	aAdd(::aBandeiras,STR0080) // '08-Aura')
	aAdd(::aBandeiras,STR0081) // '09-Cabal')
	aAdd(::aBandeiras,STR0082) // '10-Alelo')
	aAdd(::aBandeiras,STR0083) // '11-Banes Card')
	aAdd(::aBandeiras,STR0084) // '12-CalCard')
	aAdd(::aBandeiras,STR0085) // '13-Credz')
	aAdd(::aBandeiras,STR0086) // '14-Discover')
	aAdd(::aBandeiras,STR0087) // '15-GoodCard')
	aAdd(::aBandeiras,STR0088) // '16-GreenCard')
	aAdd(::aBandeiras,STR0089) // '17-Hiper')
	aAdd(::aBandeiras,STR0090) // '18-JcB')
	aAdd(::aBandeiras,STR0091) // '19-Mais')
	aAdd(::aBandeiras,STR0092) // '20-MaxVan')
	aAdd(::aBandeiras,STR0093) // '21-Policard')
	aAdd(::aBandeiras,STR0094) // '22-RedeCompras')
	aAdd(::aBandeiras,STR0095) // '23-Sodexo')
	aAdd(::aBandeiras,STR0096) // '24-ValeCard')
	aAdd(::aBandeiras,STR0097) // '25-Verocheque')
	aAdd(::aBandeiras,STR0098) // '26-VR')
	aAdd(::aBandeiras,STR0099) // '27-Ticket')
	aAdd(::aBandeiras,STR0100) // '99-Outros')

	aAdd(::aListaUF,(''))
	aAdd(::aListaUF,('AC'))
	aAdd(::aListaUF,('AL'))
	aAdd(::aListaUF,('AP'))
	aAdd(::aListaUF,('AM'))
	aAdd(::aListaUF,('BA'))
	aAdd(::aListaUF,('CE'))
	aAdd(::aListaUF,('DF'))
	aAdd(::aListaUF,('ES'))
	aAdd(::aListaUF,('GO'))
	aAdd(::aListaUF,('MA'))
	aAdd(::aListaUF,('MT'))
	aAdd(::aListaUF,('MS'))
	aAdd(::aListaUF,('MG'))
	aAdd(::aListaUF,('PA'))
	aAdd(::aListaUF,('PB'))
	aAdd(::aListaUF,('PR'))
	aAdd(::aListaUF,('PE'))
	aAdd(::aListaUF,('PI'))
	aAdd(::aListaUF,('RJ'))
	aAdd(::aListaUF,('RN'))
	aAdd(::aListaUF,('RS'))
	aAdd(::aListaUF,('RO'))
	aAdd(::aListaUF,('RR'))
	aAdd(::aListaUF,('SC'))
	aAdd(::aListaUF,('SP'))
	aAdd(::aListaUF,('SE'))
	aAdd(::aListaUF,('TO'))

	::aTipoEvento := {'',STR0101,STR0102,STR0103} // {'','110750-Transmitir Evento','110751-Cancelar Evento','110000-Monitor'}
	::aIndPag := {STR0104,STR0105} // {"0-Pagamento à Vista","1-Pagamento à Prazo"}
	::cAliasBrowser := GetNextAlias()
	::ThemaSistema()
	::MontaTelaParametros()

Return

/*/{Protheus.doc} MontaTelaParametros
	Monta a Tela de Parametros do envio doevento de Conciliação Financeira ECONF
	@author Ricardo Henrique de Mello Lima
	@since 23/08/2024
	@version 12.1.2310
	/*/
Method MontaTelaParametros() class NfeEConf

	Local nTamTipoCli := 20 as numeric
	Local nTamCli := FWSX3Util():GetFieldStruct("A1_COD")[3]
	Local nTamLojaCli := FWSX3Util():GetFieldStruct("A1_LOJA")[3]
	Local nTamDoc := FWSX3Util():GetFieldStruct("F2_DOC")[3]
	Local nTamSerie := FWSX3Util():GetFieldStruct("F2_SERIE")[3]
	Local aPergunte := {} as array

	aAdd(aPergunte,{2,STR0038,Space(nTamTipoCli),{STR0125,STR0126} ,80 ,,.T.,}) // "Tipo de NFe"###"1-Saída"###"2-Entrada"
	aAdd(aPergunte,{1,STR0039,space(nTamCli),"",".T.",, ".T.",50,.T.}) // "Cliente/Fornecedor"
	aAdd(aPergunte,{1,STR0040,Space(nTamLojaCli),"",".T.","",".T.",30,.T.}) // "Loja"
	aAdd(aPergunte,{1,STR0041,space(nTamSerie),"",".T.","",".T.",30,.T.}) // "Série da Nota Fiscal"
	aAdd(aPergunte,{1,STR0042,Space(nTamDoc),"",".T.","",".T.",30,.T.}) // "Nota Fiscal Inicial"
	aAdd(aPergunte,{1,STR0043,space(nTamDoc),"",".T.","",".T.",30,.T.}) // "Nota Fiscal Final"
	aAdd(aPergunte,{1,STR0044,date(),"",".T.","",".T.",80,.T.}) // "Dt. de emissão inicial"
	aAdd(aPergunte,{1,STR0045,date(),"",".T.","",".T.",80,.T.}) // "Dt. de emissão final"

	if ParamBox(aPergunte, ::cIdEntidade + " - " + ::cVersaoTSS,::aParametros,{|| ::ValidaFormulario()},,,,,,"NfeEConf"+alltrim(FWGrpCompany())+alltrim(FWCodFil()),.T.,.T.)
		::ProcessaDados()
	endif
Return

/*/{Protheus.doc} ValidaFormulario
	Valida Formulario
	@author Ricardo Henrique de Mello Lima
	@since 23/08/2024
	@version 12.1.2310
	/*/
Method ValidaFormulario() class NfeEConf

	Local lValid := .T. as logical

	if ::aParametros[7] > ::aParametros[8]
		lValid := .F.
	endif

	if ::aParametros[5] > ::aParametros[6]
		lValid := .F.
	endif

	if !lValid
		FWAlertInfo(STR0106,STR0001) // 'Revise os Parâmetros Para Prosseguir' 'ECONF'
	endif

Return lValid

/*/{Protheus.doc} MontaQueryDocumentos
	Monta Query e Alias com os Documentos Selecionados
	@author Ricardo Henrique de Mello Lima
	@since 24/08/2024
	@version 12.1.2310
	/*/
Method MontaQueryDocumentos() class NfeEConf

	Local oStatement := nil as object

	if SubStr(Alltrim(::aParametros[1]),1,1) == "1"

		::cQueryDocumentos := "SELECT SF2.F2_DOC,SF2.F2_EMISSAO,SF2.F2_SERIE,SF2.F2_CLIENTE,SF2.F2_LOJA,SF2.F2_CHVNFE,"
		::cQueryDocumentos += " SF2.R_E_C_N_O_ AS RECNOTA,SA1.A1_NOME,SA1.A1_PESSOA,SA1.A1_CGC,SA1.R_E_C_N_O_ AS RECCLIFOR"
		::cQueryDocumentos += " FROM "+RetSqlName("SF2")+" SF2"
		::cQueryDocumentos += " LEFT JOIN "+RetSqlName("SA1")+" SA1 ON SA1.A1_COD = SF2.F2_CLIENTE AND SA1.A1_LOJA = SF2.F2_LOJA AND SA1.A1_FILIAL = '"+FWxFilial("SA1")+"' AND SA1.D_E_L_E_T_ = ''"
		::cQueryDocumentos += " WHERE SF2.D_E_L_E_T_ = ?"
		::cQueryDocumentos += " AND SF2.F2_FILIAL = ?"
		::cQueryDocumentos += " AND SF2.F2_EMISSAO BETWEEN ? AND ?"
		::cQueryDocumentos += " AND SF2.F2_DOC BETWEEN ? AND ?"
		::cQueryDocumentos += " AND SF2.F2_SERIE = ?"
		::cQueryDocumentos += " AND SF2.F2_CLIENTE = ?"
		::cQueryDocumentos += " AND SF2.F2_LOJA = ?"
		::cQueryDocumentos += " AND SF2.F2_TIPO = ?"
		::cQueryDocumentos += " AND SF2.F2_CHVNFE <> ?"

	elseif SubStr(Alltrim(::aParametros[1]),1,1) == "2"

		::cQueryDocumentos := "SELECT SF1.F1_DOC,SF1.F1_EMISSAO,SF1.F1_SERIE,SF1.F1_FORNECE,SF1.F1_LOJA,SF1.F1_CHVNFE,"
		::cQueryDocumentos += " SF1.R_E_C_N_O_ AS RECNOTA,SA2.A2_NOME,SA2.A2_TIPO,SA2.A2_CGC,SA2.R_E_C_N_O_ AS RECCLIFOR"
		::cQueryDocumentos += " FROM "+RetSqlName("SF1")+" SF1"
		::cQueryDocumentos += " LEFT JOIN "+RetSqlName("SA2")+" SA2 ON SA2.A2_COD = SF1.F1_FORNECE AND SA2.A2_LOJA = SF1.F1_LOJA AND SA2.A2_FILIAL = '"+FWxFilial("SA2")+"' AND SA2.D_E_L_E_T_ = ''"
		::cQueryDocumentos += " WHERE SF1.D_E_L_E_T_ = ?"
		::cQueryDocumentos += " AND SF1.F1_FILIAL = ?"
		::cQueryDocumentos += " AND SF1.F1_EMISSAO BETWEEN ? AND ?"
		::cQueryDocumentos += " AND SF1.F1_DOC BETWEEN ? AND ?"
		::cQueryDocumentos += " AND SF1.F1_SERIE = ?"
		::cQueryDocumentos += " AND SF1.F1_FORNECE = ?"
		::cQueryDocumentos += " AND SF1.F1_LOJA = ?"
		::cQueryDocumentos += " AND SF1.F1_TIPO = ?"
		::cQueryDocumentos += " AND SF1.F1_CHVNFE <> ?"

	endif

	oStatement := FWExecStatement():New(ChangeQuery(::cQueryDocumentos))
	oStatement:SetString(1,"")
	if SubStr(Alltrim(::aParametros[1]),1,1) == "1"
		oStatement:SetString(2,FWXFilial("SF2"))
	elseif SubStr(Alltrim(::aParametros[1]),1,1) == "2"
		oStatement:SetString(2,FWXFilial("SF1"))
	endif
	oStatement:SetDate(3,::aParametros[7])
	oStatement:SetDate(4,::aParametros[8])
	oStatement:SetString(5,::aParametros[5])
	oStatement:SetString(6,::aParametros[6])
	oStatement:SetString(7,::aParametros[4])
	oStatement:SetString(8,::aParametros[2])
	oStatement:SetString(9,::aParametros[3])
	oStatement:SetString(10,"N")
	oStatement:SetString(11,"")

	::cQueryDocumentos := oStatement:GetFixQuery()
	::cAliasDocumentos := oStatement:OpenAlias()

	oStatement:Destroy()
	FreeObj(oStatement)
Return

/*/{Protheus.doc} ProcessaDados
	Valida se existe dados no filtro inicial e monta a tela de Transmissão
	@author Ricardo Henrique de Mello Lima
	@since 24/08/2024
	@version 12.1.2310
	/*/
Method ProcessaDados() class NfeEConf

	::MontaQueryDocumentos()

	(::cAliasDocumentos)->(DBGOTOP())
	if (::cAliasDocumentos)->(!EOF()) .AND. (::cAliasDocumentos)->(!BOF())
		(::cAliasDocumentos)->(DBCLOSEAREA())
		::MontaTelaDocumentos()
	else
		(::cAliasDocumentos)->(DBCLOSEAREA())
		FWAlertInfo(STR0107,STR0001) // "Não existem registros relacionados ao filtro selecionado."  "ECONF"
	endif

Return
/*/{Protheus.doc} MontaTelaDocumentos
	Metodo que monta a tela que lista os documentos aptos a serem eviados pelo econf
	@author Ricardo Henrique de Mello Lima
	@since 24/08/2024
	@version 12.1.2310
	/*/
Method MontaTelaDocumentos() class NfeEConf

	Local oDlgNotas as Object
	LOcal oColumns as object
	Local aColumns := {} as Array
	Local nContFlds as Numeric
	Local oBrowseNotas as Object
	Local aFields := {} as Array
	Local aStructCampo := {} as Array
	Local aSize := {} as array
	Local cTitle := "" as character
	Local aSeek := {} as array
	Local aIndex := {} as array
	Local nOpcDoc := 0 as numeric

	aSize := MsAdvSize()

	cTitle := STR0001 + " - " + ::cIdEntidade + " - " + ::cVersaoTSS // Econf

	if SubStr(Alltrim(::aParametros[1]),1,1) == "1"
		nOpcDoc := 1
		aAdd( aSeek, { "Documento" , {{"","C", GetSX3Cache( "F2_DOC","X3_TAMANHO"), 0 , "Documento",,}}} )
		aAdd( aSeek, { "Chave da NFE" , {{"","C", GetSX3Cache( "F2_CHVNFE","X3_TAMANHO") , 0 , "Chave da NFE",,}}} )
		aAdd( aIndex, "F2_DOC" )
		aAdd( aIndex, "F2_CHVNFE" )

	elseif SubStr(Alltrim(::aParametros[1]),1,1) == "2"
		nOpcDoc := 2
		aAdd( aSeek, { "Documento" , {{"","C", GetSX3Cache( "F1_DOC","X3_TAMANHO"), 0 , "Documento",,}}} )
		aAdd( aSeek, { "Chave da NFE" , {{"","C", GetSX3Cache( "F1_CHVNFE","X3_TAMANHO") , 0 , "Chave da NFE",,}}} )
		aAdd( aIndex, "F1_DOC" )
		aAdd( aIndex, "F1_CHVNFE" )

	endif

	DEFINE MSDIALOG oDlgNotas TITLE cTitle FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL

	oBrowseNotas := FWFormBrowse():New()
	oBrowseNotas:SetDataQuery()
	oBrowseNotas:SetAlias(::cAliasBrowser)
	oBrowseNotas:SetOwner(oDlgNotas)
	oBrowseNotas:SetDescription(cTitle)
	oBrowseNotas:SetShowLimit(.T.)
	oBrowseNotas:SetQueryIndex(aIndex)
	oBrowseNotas:SetQuery(::cQueryDocumentos)
	oBrowseNotas:SetSeek(,aSeek)
	oBrowseNotas:SetMenuDef("")
	oBrowseNotas:DisableDetails()

	aColumns    := {}
	aFields     := {}

	if SubStr(Alltrim(::aParametros[1]),1,1) == "1"

		aAdd(aFields,{"F2_DOC",STR0026}) // "Documento"
		aAdd(aFields,{"F2_SERIE",STR0028})  // "Serie"
		aAdd(aFields,{"F2_CLIENTE",STR0039}) // "Cliente/Fornecedor"
		aAdd(aFields,{"F2_LOJA",STR0040}) // "Loja"
		aAdd(aFields,{"F2_CHVNFE",STR0108}) // "Chave da Nfe"
		aAdd(aFields,{"F2_EMISSAO",STR0109}) // "Data de Emissão"

	elseif SubStr(Alltrim(::aParametros[1]),1,1) == "2"

		aAdd(aFields,{"F1_DOC",STR0026}) // "Documento"
		aAdd(aFields,{"F1_SERIE",STR0028})  // "Serie"
		aAdd(aFields,{"F1_FORNECE",STR0039}) // "Cliente/Fornecedor"
		aAdd(aFields,{"F1_LOJA",STR0040}) // "Loja"
		aAdd(aFields,{"F1_CHVNFE",STR0108}) // "Chave da Nfe"
		aAdd(aFields,{"F1_EMISSAO",STR0109}) // "Data de Emissão"

	endif

	For nContFlds := 1 To Len(aFields)

		aStructCampo := FWSX3Util():GetFieldStruct(aFields[nContFlds,1])
		oColumns :=  FWBrwColumn():New()
		if Alltrim(aStructCampo[2]) == "D"
			oColumns:SetData(&("{|| DtoC(StoD("+aFields[nContFlds,1]+"))}"))
		else
			oColumns:SetData(&("{|| "+aFields[nContFlds,1]+" }"))
		endif
		oColumns:SetTitle( aFields[nContFlds,2])
		oColumns:SetSize(aStructCampo[3])
		oColumns:SetType(aStructCampo[2])
		oColumns:SetPicture(Alltrim(GetSX3Cache(aFields[nContFlds,1],"X3_PICTURE")))

		aAdd(aColumns,oColumns)
	Next

	oBrowseNotas:AddButton(STR0046,{||oDlgNotas:End()},,,,.F.) // Sair
	if !lUsaColab
		oBrowseNotas:AddButton(STR0047,{|| SpedNFeCfg()},,,,.F.) // "Wiz.Config."
	endif
	oBrowseNotas:AddButton(STR0048,{|| SpedCCePar()},,,,.F.) // "Parametros"
	oBrowseNotas:AddButton(STR0049,{|| (::MontaTransmissao())},,,,.F.) // "Transmissao"
	oBrowseNotas:AddButton(STR0050,{|| ::VisualizaDocumento(nOpcDoc) },,,,.F.) // "Visualiza Doc."
	oBrowseNotas:SetColumns(aColumns)
	oBrowseNotas:Activate()

	ACTIVATE MSDIALOG oDlgNotas
Return

/*/{Protheus.doc} MontaTransmissao
	Metodo responsavel pela criação da tela da transmissão 
	@author Ricardo Henrique de Mello Lima
	@since 24/08/2024
	@version 12.1.2310
	/*/
Method MontaTransmissao() class NfeEConf

	Local oSize := nil as object
	Local lHasButton := .T. as logical
	Local cCodCliFor := "" as character
	Local cLojaCliFor := "" as character
	Local cTipoCliDesc := "" as character
	Local cNomeCliFor := "" as character
	Local cNumDoc := "" as character
	Local cDocSerie := "" as character
	Local dDocEmissao := date() as date
	Local cTitulo := "" as character
	Local cMaskCNPJ := "@R 999.999.999-99" as character
	Local cEvento := "" as character
	Local cUFRec := "" as character
	Local cBandeira := "" as character
	Local cIndPag := "" as character
	Local cUFPag := "" as character
	Local cTPagamentos := "" as character
	Local lValBuild := GetBuild() < "7.00.240223P-20250329" as logical

	Local aButtons := {} as array

	Local oGrpDadosNota := nil as object
	Local oGrpFormulario := nil as object
	Local oPanelDadosNota := nil as object

	Local oSayCodCli := nil as object
	Local oSayNomeCli := nil as object
	Local oSayPessoa := nil as object
	Local oSayNF := nil as object
	Local oSayChave := nil as object

	cTitulo := STR0001 + " - " + STR0002+::cIdEntidade + " - " +STR0003+ ::cVersaoTSS

	::cEventoSelecionado := ""

	if SubStr(Alltrim(::aParametros[1]),1,1) == "1"

		::cTipoCli := Alltrim((::cAliasBrowser)->A1_PESSOA)
		::cChaveDoc := Alltrim((::cAliasBrowser)->F2_CHVNFE)
		::cCnpjNota := Alltrim((::cAliasBrowser)->A1_CGC)
		cNomeCliFor := Alltrim((::cAliasBrowser)->A1_NOME)
		cNumDoc := Alltrim((::cAliasBrowser)->F2_DOC)
		cCodCliFor := Alltrim((::cAliasBrowser)->F2_CLIENTE)
		cLojaCliFor := Alltrim((::cAliasBrowser)->F2_LOJA)
		cDocSerie := Alltrim((::cAliasBrowser)->F2_SERIE)
		dDocEmissao := Stod(Alltrim((::cAliasBrowser)->F2_EMISSAO))

	elseif SubStr(Alltrim(::aParametros[1]),1,1) == "2"

		::cTipoCli := Alltrim((::cAliasBrowser)->A2_TIPO)
		::cChaveDoc := Alltrim((::cAliasBrowser)->F1_CHVNFE)
		::cCnpjNota := Alltrim((::cAliasBrowser)->A2_CGC)
		cNomeCliFor := Alltrim((::cAliasBrowser)->A2_NOME)
		cNumDoc := Alltrim((::cAliasBrowser)->F1_DOC)
		cCodCliFor := Alltrim((::cAliasBrowser)->F1_FORNECE)
		cLojaCliFor := Alltrim((::cAliasBrowser)->F1_LOJA)
		cDocSerie := Alltrim((::cAliasBrowser)->F1_SERIE)
		dDocEmissao := Stod(Alltrim((::cAliasBrowser)->F1_EMISSAO))

	endif

	if ::cTipoCli == 'F'
		cTipoCliDesc := STR0110 // 'Pessoa Fisica'
	elseif ::cTipoCli == 'J'
		cTipoCliDesc:= STR0111 // 'Pessoa Juridica'
		cMaskCNPJ := "@R! NN.NNN.NNN/NNNN-99"
	endif

	oSize := FwDefSize():New(.T.,.F.,,)

	oSize:AddObject( "MAIN",100,100,.T.,.T. ) // Totalmente dimensionavel
	oSize:lProp 	:= .T. // Proporcional
	oSize:aMargins 	:= { 3, 3, 3, 3 } // Espaco ao lado dos objetos 0, entre eles 3
	oSize:Process() // Dispara os calculos

	::oWorkAreaMain := FWDialogModal():New()
	::oWorkAreaMain:SetEscClose(.T.)
	::oWorkAreaMain:SetTitle(cTitulo)
	::oWorkAreaMain:setPos(oSize:aWindSize[1] / 2,oSize:aWindSize[2] / 2 )
	::oWorkAreaMain:setSize(oSize:aWindSize[3] / 2,oSize:aWindSize[4] / 2)
	::oWorkAreaMain:CreateDialog()

	::oWorkAreaContainer := FWUIWorkArea():new(::oWorkAreaMain:GetPanelMain())
	::oWorkAreaContainer:CreateHorizontalBox("MAIN", 100 , .F.)
	::oWorkAreaContainer:SetBoxCols("MAIN" , {"WIDFORM", "WIDBROWSER"} )
	::oWorkAreaContainer:Activate()

	::oPanelDadosBrowser := ::oWorkAreaContainer:GetPanel("WIDBROWSER")
	oPanelDadosNota := ::oWorkAreaContainer:GetPanel("WIDFORM")


	// WIDCLIENTE
	oGrpDadosNota := TGroup():New(02,02,70,(oPanelDadosNota:NWIDTH / 2) - 3,,::oWorkAreaContainer:GetPanel("WIDFORM"),,,.T.)
	if !::lThemeDark
		oGrpDadosNota:SetCss("TGroup{background-color: white;}")
	endif
	oSayCodCli := TSay():New(01, 01, {|| STR0004+" "+cCodCliFor+" - "+STR0005+" "+cLojaCliFor }, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	oSayNomeCli := TSay():New(02, 01, {|| STR0006+" "+Alltrim(cNomeCliFor)}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	oSayPessoa := TSay():New(03, 01, {|| cTipoCliDesc + " - " +Transform(Alltrim(::cCnpjNota),cMaskCNPJ)}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	oSayNF:= TSay():New(03, 18, {|| STR0007+" "+cNumDoc+ " - "+cDocSerie+" - "+ DToc(dDocEmissao)}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	oSayChave := TSay():New(04, 01, {|| STR0008+" "+ ::cChaveDoc}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)

	// WIDGET FORMULARIO

	oGrpFormulario := TGroup():New(75,02,(oPanelDadosNota:NHEIGHT / 2) - 3,(oPanelDadosNota:NWIDTH / 2) - 3,,::oWorkAreaContainer:GetPanel("WIDFORM"),,,.T.)
	if !::lThemeDark
		oGrpFormulario:SetCss("TGroup{background-color: white;}")
	endif
	::oTSayTransacao := TSay():New(06, 01, {||STR0009}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	::oComboEvento := TComboBox():New(85,07,{|u|if(PCount()>0,cEvento:=u,cEvento)},;
		::aTipoEvento,120,20,::oWorkAreaContainer:GetPanel("WIDFORM"),,{||(::SetTrocaEvento(cEvento),::TrocarEvento())},,,,.T.,,,,,,,,,'cEvento',,)

	::oTSayComboIndPag := TSay():New(08, 01, {||STR0010}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	::oComboIndPag := TComboBox():New(112,07,{|u|if(PCount()>0,cIndPag:=u,cIndPag)},;
		::aIndPag,120,20,::oWorkAreaContainer:GetPanel("WIDFORM"),,{||},,,,.T.,,,,,,,,,'cIndPag',,)

	::oTSayComboPagamentos := TSay():New(10, 01, {||STR0011}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	::oComboPagametos := TComboBox():New(139,07,{|u|if(PCount()>0,cTPagamentos:=u,cTPagamentos)},;
		::aTpagamentos,120,20,::oWorkAreaContainer:GetPanel("WIDFORM"),,{||},,,,.T.,,,,,,,,,'cTPagamentos',,)

	::oTSayGetData := TSay():New(12, 01, {||STR0012}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	::oTGetData := TGet():New( 163, 07, { | u | If( PCount() == 0, ::dGetData, ::dGetData := u ) },::oWorkAreaContainer:GetPanel("WIDFORM"),;
		060, 010, "@D",, 0, 16777215,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F. ,,"::dGetData",,,,lHasButton,,,,)
	if ::lThemeDark .and. lValBuild
		::oTGetData:SetCss("TGet{background-color: black;}")
	endif

	::oTSayCNPJPag := TSay():New(14, 01, {||STR0013}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	::oCNPJPag := TGet():New(190,07, { | u | If( PCount() == 0, ::cCNPJPag, ::cCNPJPag := u ) },::oWorkAreaContainer:GetPanel("WIDFORM"), ;
		100, 010, "@R! NN.NNN.NNN/NNNN-99",, 0, 16777215,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F. ,,"::cCNPJPag",,,,lHasButton,,,,)
	if ::lThemeDark .and. lValBuild
		::oCNPJPag:SetCss("TGet{background-color: black;}")
	endif

	::oTSayComboUFP := TSay():New(16, 01, {||STR0014}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	::oComboUFP := TComboBox():New(217,07,{|u|if(PCount()>0,cUFPag:=u,cUFPag)},::aListaUF,100,20,::oWorkAreaContainer:GetPanel("WIDFORM"),,{||};
		,,,,.T.,,,,,,,,,'cUFPag',,)

	::oTSayCNPJF := TSay():New(18, 01, {||STR0015}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	::oCNPJF := TGet():New(245,07, { | u | If( PCount() == 0, ::cCNPJF, ::cCNPJF := u ) },::oWorkAreaContainer:GetPanel("WIDFORM"), ;
		100, 010, "@R! NN.NNN.NNN/NNNN-99",, 0, 16777215,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F. ,,"::cCNPJF",,,,lHasButton,,,,)
	if ::lThemeDark .and. lValBuild
		::oCNPJF:SetCss("TGet{background-color: black;}")
	endif

	::oTSayBandeira := TSay():New(8, 25, {||STR0016}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	::oBandeira := TComboBox():New(112,200,{|u|if(PCount()>0,cBandeira:=u,cBandeira)},::aBandeiras,100,20,::oWorkAreaContainer:GetPanel("WIDFORM"),,{||};
		,,,,.T.,,,,,,,,,'cBandeira',,)

	::oTSayCNPJR := TSay():New(10, 25, {||STR0017}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	::oCNPJR := TGet():New(139,200, { | u | If( PCount() == 0, ::cCNPJR, ::cCNPJR := u ) },::oWorkAreaContainer:GetPanel("WIDFORM"), ;
		100, 010, "@R! NN.NNN.NNN/NNNN-99",, 0, 16777215,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F. ,,"::cCNPJR",,,,lHasButton,,,,)
	if ::lThemeDark .and. lValBuild
		::oCNPJR:SetCss("TGet{background-color: black;}")
	endif

	::oTSayComboUFR := TSay():New(12, 25, {||STR0051}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	::oComboUFR := TComboBox():New(164,200,{|u|if(PCount()>0,cUFRec:=u,cUFRec)},::aListaUF,100,20,::oWorkAreaContainer:GetPanel("WIDFORM"),,{||};
		,,,,.T.,,,,,,,,,'cUFRec',,)

	::oTSayAutCart := TSay():New(14, 25, {||STR0018}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	::oAutCart := TGet():New(190,200, { | u | If( PCount() == 0, ::cAutCart, ::cAutCart := u ) },::oWorkAreaContainer:GetPanel("WIDFORM"), ;
		100, 010, "@E 99999999999999",, 0, 16777215,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F. ,,"::cAutCart",,,,lHasButton,,,,)
	if ::lThemeDark .and. lValBuild
		::oAutCart:SetCss("TGet{background-color: black;}")
	endif

	::oTSayValor := TSay():New(16, 25, {||STR0019}, ::oWorkAreaContainer:GetPanel("WIDFORM"),,::oFont)
	::oValor := TGet():New(217,200, { | u | If( PCount() == 0, ::nValor, ::nValor := u ) },::oWorkAreaContainer:GetPanel("WIDFORM"), ;
		100, 010, "@E 99,999,999,999.99",, 0, 16777215,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F. ,,"::nValor",,,,lHasButton,,,,)
	if ::lThemeDark .and. lValBuild
		::oValor:SetCss("TGet{background-color: black;}")
	endif

	::oButtonAdd := TButton():New( 245, 200, STR0053,::oWorkAreaContainer:GetPanel("WIDFORM"),{||(::AdicionaEvento(cTPagamentos,cUFPag,cBandeira,cUFRec,cIndPag),::oBrowserTitulos:DrawSelect())}, 40,10,,,.F.,.T.,.F.,,.F.,,,.F. )
	::oButtonDel := TButton():New( 245, 245, STR0052,::oWorkAreaContainer:GetPanel("WIDFORM"),{||(::RemoveEvento(),::oBrowserTitulos:DrawSelect())}, 40,10,,,.F.,.T.,.F.,,.F.,,,.F. )

 	// Browser

	::oGrpBrowser := TGroup():New(02,02,(::oPanelDadosBrowser:NHEIGHT / 2) - 3,(::oPanelDadosBrowser:NWIDTH / 2) - 3,,::oWorkAreaContainer:GetPanel("WIDBROWSER"),,,.T.)
	if !::lThemeDark
		::oGrpBrowser:SetCss("TGroup{background-color: white;}")
	endif
	::TrocarEvento()
	aAdd(aButtons,{,STR0036,{|| ::oWorkAreaMain:DeActivate() },STR0112,1,.T.,.T.}) // "Sair da Rotina"
	aAdd(aButtons,{,STR0035,{|| ::RefreshBrowserMonitor() },STR0113,1,.T.,.T.}) //"Atualiza Browser Monitor"
	aAdd(aButtons,{,STR0033,{|| ::MostraMenssagem(STR0114,"","") },STR0114,1,.T.,.T.}) //'ECONF - Visualiza Evento'
	aAdd(aButtons,{,STR0037,{|| ::PreparaEvento()},STR0115,1,.T.,.T.}) // "Transmitir Eventos"

	::oWorkAreaMain:addButtons(aButtons)
	::oWorkAreaMain:Activate()

Return

/*/{Protheus.doc} MostraDocumento
	Exibe o Documento selecionado no Brouser inicial
	@author Ricardo Henrique de Mello Lia
	@since 26/08/2024
	@version 12.1.2310
	/*/
Method VisualizaDocumento(nOpc as numeric) class NfeEConf

	if nOpc == 1

		DBSELECTAREA("SF2")
		SF2->(DBGOTO((::cAliasBrowser)->RECNOTA))
		Mc090Visual("SF2",SF2->(RECNO()),1)

	elseif nOpc == 2
		DBSELECTAREA("SF1")
		SF1->(DBGOTO((::cAliasBrowser)->RECNOTA))
		A103NFiscal("SF1",SF1->(RECNO()),1)
	endif
Return

/*/{Protheus.doc} SetTrocaEvento
	Seta o evento Selecionado ao acionar o controle de evento
	@author Ricardo Henrique de Mello Lima
	@since 25/08/2024
	@version 12.1.2310
	/*/
Method SetTrocaEvento(cValue as character) class NfeEConf
	if Alltrim(cValue) == ""
		::cEventoSelecionado := ""
	else
		::cEventoSelecionado := Substr(Alltrim(cValue),1,6)
	endif
Return


/*/{Protheus.doc} TrocarEvento
	Metodo responsalvel por Mudar o comportamento ao trocar o tipo de evento
	@author Ricardo Henrique de Mello Lima
	@since 25/08/2024
	@version 12.1.2310
	/*/
Method TrocarEvento() class NfeEConf

	if ::cEventoSelecionado == ""

		::oComboEvento:Select(1)
		::oComboIndPag:Select(1)
		::oComboPagametos:Select(1)
		::oComboUFP:Select(1)
		::oComboUFR:Select(1)
		::oBandeira:Select(1)
		::oComboIndPag:lVisibleControl := .F.
		::oComboPagametos:lVisibleControl := .F.
		::oComboUFP:lVisibleControl := .F.
		::oComboUFR:lVisibleControl := .F.
		::oBandeira:lVisibleControl := .F.
		::oTGetData:lVisibleControl := .F.
		::oCNPJPag:lVisibleControl := .F.
		::oCNPJF:lVisibleControl := .F.
		::oCNPJR:lVisibleControl := .F.
		::oAutCart:lVisibleControl := .F.
		::oValor:lVisibleControl := .F.
		::oButtonAdd:lVisibleControl := .F.
		::oButtonDel:lVisibleControl := .F.

		::oTSayComboIndPag:lVisibleControl := .F.
		::oTSayComboPagamentos:lVisibleControl := .F.
		::oTSayGetData:lVisibleControl := .F.
		::oTSayCNPJPag:lVisibleControl := .F.
		::oTSayComboUFP:lVisibleControl := .F.
		::oTSayCNPJF:lVisibleControl := .F.
		::oTSayBandeira:lVisibleControl := .F.
		::oTSayCNPJR:lVisibleControl := .F.
		::oTSayComboUFR:lVisibleControl := .F.
		::oTSayAutCart:lVisibleControl := .F.
		::oTSayValor:lVisibleControl := .F.
		FreeObj(::oBrowserTitulos)

	endif

	if ::cEventoSelecionado == ::cEventoMonitor

		FreeObj(::oBrowserTitulos)
		::oComboEvento:Select(4)
		::oComboIndPag:Select(1)
		::oComboPagametos:Select(1)
		::oComboUFP:Select(1)
		::oComboUFR:Select(1)
		::oBandeira:Select(1)
		::oComboIndPag:lVisibleControl := .F.
		::oComboPagametos:lVisibleControl := .F.
		::oComboUFP:lVisibleControl := .F.
		::oComboUFR:lVisibleControl := .F.
		::oBandeira:lVisibleControl := .F.
		::oTGetData:lVisibleControl := .F.
		::oCNPJPag:lVisibleControl := .F.
		::oCNPJF:lVisibleControl := .F.
		::oCNPJR:lVisibleControl := .F.
		::oAutCart:lVisibleControl := .F.
		::oValor:lVisibleControl := .F.
		::oButtonAdd:lVisibleControl := .F.
		::oButtonDel:lVisibleControl := .F.

		::oTSayComboIndPag:lVisibleControl := .F.
		::oTSayComboPagamentos:lVisibleControl := .F.
		::oTSayGetData:lVisibleControl := .F.
		::oTSayCNPJPag:lVisibleControl := .F.
		::oTSayComboUFP:lVisibleControl := .F.
		::oTSayCNPJF:lVisibleControl := .F.
		::oTSayBandeira:lVisibleControl := .F.
		::oTSayCNPJR:lVisibleControl := .F.
		::oTSayComboUFR:lVisibleControl := .F.
		::oTSayAutCart:lVisibleControl := .F.
		::oTSayValor:lVisibleControl := .F.
		::MontaBrowser()

	endif


	if ::cEventoSelecionado == ::cEventoTransmissao
		FreeObj(::oBrowserTitulos)

		::oComboEvento:Select(2)
		::oComboIndPag:Select(1)
		::oComboPagametos:Select(1)
		::oComboUFP:Select(1)
		::oComboUFR:Select(1)
		::oBandeira:Select(1)

		::oComboIndPag:lVisibleControl := .T.
		::oComboPagametos:lVisibleControl := .T.
		::oComboUFP:lVisibleControl := .T.
		::oComboUFR:lVisibleControl := .T.
		::oBandeira:lVisibleControl := .T.
		::oTGetData:lVisibleControl := .T.
		::oCNPJPag:lVisibleControl := .T.
		::oCNPJF:lVisibleControl := .T.
		::oCNPJR:lVisibleControl := .T.
		::oAutCart:lVisibleControl := .T.
		::oValor:lVisibleControl := .T.
		::oButtonAdd:lVisibleControl := .T.
		::oButtonDel:lVisibleControl := .T.

		::oTSayComboIndPag:lVisibleControl := .T.
		::oTSayComboPagamentos:lVisibleControl := .T.
		::oTSayGetData:lVisibleControl := .T.
		::oTSayCNPJPag:lVisibleControl := .T.
		::oTSayComboUFP:lVisibleControl := .T.
		::oTSayCNPJF:lVisibleControl := .T.
		::oTSayBandeira:lVisibleControl := .T.
		::oTSayCNPJR:lVisibleControl := .T.
		::oTSayComboUFR:lVisibleControl := .T.
		::oTSayAutCart:lVisibleControl := .T.
		::oTSayValor:lVisibleControl := .T.
		::MontaBrowser()

	endif

	if ::cEventoSelecionado == ::cEventoCancelamento

		FreeObj(::oBrowserTitulos)
		::oComboEvento:Select(3)
		::oComboIndPag:Select(1)
		::oComboPagametos:Select(1)
		::oComboUFP:Select(1)
		::oComboUFR:Select(1)
		::oBandeira:Select(1)
		::oComboIndPag:lVisibleControl := .F.
		::oComboPagametos:lVisibleControl := .F.
		::oComboUFP:lVisibleControl := .F.
		::oComboUFR:lVisibleControl := .F.
		::oBandeira:lVisibleControl := .F.
		::oTGetData:lVisibleControl := .F.
		::oCNPJPag:lVisibleControl := .F.
		::oCNPJF:lVisibleControl := .F.
		::oCNPJR:lVisibleControl := .F.
		::oAutCart:lVisibleControl := .F.
		::oValor:lVisibleControl := .F.
		::oButtonAdd:lVisibleControl := .F.
		::oButtonDel:lVisibleControl := .F.

		::oTSayComboIndPag:lVisibleControl := .F.
		::oTSayComboPagamentos:lVisibleControl := .F.
		::oTSayGetData:lVisibleControl := .F.
		::oTSayCNPJPag:lVisibleControl := .F.
		::oTSayComboUFP:lVisibleControl := .F.
		::oTSayCNPJF:lVisibleControl := .F.
		::oTSayBandeira:lVisibleControl := .F.
		::oTSayCNPJR:lVisibleControl := .F.
		::oTSayComboUFR:lVisibleControl := .F.
		::oTSayAutCart:lVisibleControl := .F.
		::oTSayValor:lVisibleControl := .F.
		::MontaBrowser()

	endif

Return


/*/{Protheus.doc} MontaBrowser
	Monta o Browser conforme o tipo de Transmissao selecionado
	@author Ricardo Henrique de Mello Lima
	@since 25/08/2024
	@version 12.1.2310
	/*/
Method MontaBrowser() class NfeEConf

	Local aRetEventoEnvio := {} as array
	Local aRetEventoCancel := {} as array
	Local aEventosRetorno := {} as array
	Local aEnvioCancel := {} as array
	Local nCount := 0 as numeric
	Local nCountC := 0 as numeric
	Local nIDtotal := 0 as numeric
	Local cSequencia := "" as character
	Local cAmbiente := "" as character
	Local cStatus := "" as character
	Local cError := "" as character
	Local nTamID := 0 as numeric
	Local cSeq := "" as character
	Local cDocumento := "" as character
	Local cSerie := "" as character
	Local cMsg := "" as character
	Local cEvento := "" as character
	Local nQtdLinha := 0 as numeric

	nQtdLinha := Len(::aBrwDados)
	FreeObj(::oBrowserTitulos)

	if ::cEventoSelecionado == ::cEventoTransmissao

		::oOK := LoadBitmap(GetResources(),'br_verde')
		::oNO := LoadBitmap(GetResources(),'br_vermelho')
		::aBrwDados := { {.F.,'','','','','','','','','','',0}}

		::oBrowserTitulos := TCBrowse():New( 05 , 05, ::oPanelDadosBrowser:NWIDTH / 2 - 10, ::oPanelDadosBrowser:NHEIGHT / 2 - 10 ,, {'',STR0010,STR0011,STR0012,STR0013,STR0014,STR0015,STR0016,STR0017,STR0051,STR0018,STR0019},;
			{20,100,100,100,100,100,100,100,100,100,100,100},::oWorkAreaContainer:GetPanel("WIDBROWSER"),,,,,{||},,,,,,,.F.,,.T.,,.F.,,, )
		::oBrowserTitulos:SetArray(::aBrwDados)
		::oBrowserTitulos:bLine := {||{ If(::aBrwDados[::oBrowserTitulos:nAt,01],::oOK,::oNO),;
			::aBrwDados[::oBrowserTitulos:nAt,02],;
			::aBrwDados[::oBrowserTitulos:nAt,03],;
			::aBrwDados[::oBrowserTitulos:nAt,04],;
			::aBrwDados[::oBrowserTitulos:nAt,05],;
			::aBrwDados[::oBrowserTitulos:nAt,06],;
			::aBrwDados[::oBrowserTitulos:nAt,07],;
			::aBrwDados[::oBrowserTitulos:nAt,08],;
			::aBrwDados[::oBrowserTitulos:nAt,09],;
			::aBrwDados[::oBrowserTitulos:nAt,10],;
			::aBrwDados[::oBrowserTitulos:nAt,11],;
			Transform(::aBrwDados[::oBrowserTitulos:nAT,12],'@E 99,999,999,999.99') } }

		::oBrowserTitulos:DrawSelect()
		::oBrowserTitulos:Refresh()
		::oBrowserTitulos:GoPosition(nQtdLinha)

	endif

	if ::cEventoSelecionado == ::cEventoCancelamento


		aRetEventoEnvio := ::MonitoraEvento(::cEventoTransmissao)
		if Alltrim(aRetEventoEnvio[1]) == ""
			aRetEventoCancel := ::MonitoraEvento(::cEventoCancelamento)

			for nCount := 1 to len(aRetEventoEnvio[3])

				lAchou := .F.

				for nCountC := 1 to Len(aRetEventoCancel[3])
					if Substr(Alltrim(aRetEventoEnvio[3,nCount,3]),9,Len(Alltrim(aRetEventoEnvio[3,nCount,3]))) == Substr(Alltrim(aRetEventoCancel[3,nCountC,3]),9,Len(Alltrim(aRetEventoCancel[3,nCountC,3])))
						lAchou := .T.
						exit
					endif
				next

				if !lAchou
					aAdd(aEnvioCancel,{;
						aRetEventoEnvio[3,nCount,1],;
						aRetEventoEnvio[3,nCount,2],;
						aRetEventoEnvio[3,nCount,3],;
						aRetEventoEnvio[3,nCount,4],;
						aRetEventoEnvio[3,nCount,5],;
						aRetEventoEnvio[3,nCount,6],;
						aRetEventoEnvio[3,nCount,7],;
						aRetEventoEnvio[3,nCount,8],;
						aRetEventoEnvio[3,nCount,9],;
						aRetEventoEnvio[3,nCount,10];
						})
				endif
			next

			if Len(aEnvioCancel) >= 1
				::aBrwDados := {}
				for nCount := 1 to Len(aEnvioCancel)
					nIDtotal := Len(alltrim(aEnvioCancel[nCount,3]))
					cSequencia := SubStr(Alltrim(aEnvioCancel[nCount,3]),nIDtotal-1,2)
					if(aEnvioCancel[nCount,4] == 1)
						cAmbiente := STR0116 // '1-Produção'
					elseif (aEnvioCancel[nCount,4] == 2)
						cAmbiente := STR0117 // '2-Homologação'
					endif
					if aEnvioCancel[nCount,5] == "6"
						cStatus := STR0118 // 'Autorizado'
					else
						cStatus := STR0119 // "Nao Autorizado"
					endif
					aAdd(::aBrwDados,{.F.,;
						cSequencia,;
						cAmbiente,;
						cStatus,;
						aEnvioCancel[nCount,2],;
						aEnvioCancel[nCount,7],;
						aEnvioCancel[nCount,8]})
				next
			endif

			if !Empty(::aBrwDados)
				::oOk := LoadBitmap( GetResources(), "LBOK" )
				::oNo := LoadBitmap( GetResources(), "LBNO" )

				::oBrowserTitulos := TCBrowse():New( 05 , 05, ::oPanelDadosBrowser:NWIDTH / 2 - 10, ::oPanelDadosBrowser:NHEIGHT / 2 - 10 ,, {'',STR0020,STR0021,STR0022,STR0023,STR0024,STR0025},;
					{20,50,70,35,70,70,70},::oWorkAreaContainer:GetPanel("WIDBROWSER"),,,,,{||},,,,,,,.F.,,.T.,,.F.,,, )
				::oBrowserTitulos:SetArray(::aBrwDados)
				::oBrowserTitulos:bLine := {||{ If(::aBrwDados[::oBrowserTitulos:nAt,01],::oOK,::oNO),;
					::aBrwDados[::oBrowserTitulos:nAt,02],;
					::aBrwDados[::oBrowserTitulos:nAt,03],;
					::aBrwDados[::oBrowserTitulos:nAt,04],;
					::aBrwDados[::oBrowserTitulos:nAt,05],;
					::aBrwDados[::oBrowserTitulos:nAt,06],;
					::aBrwDados[::oBrowserTitulos:nAt,07]}}

				::oBrowserTitulos:bLDblClick := {|| (::SelecionaCancelamento(1),::oBrowserTitulos:Refresh()) }
				::oBrowserTitulos:bHeaderClick := {|| (::SelecionaCancelamento(2),::oBrowserTitulos:Refresh()) }

				::oBrowserTitulos:DrawSelect()
				::oBrowserTitulos:Refresh()
				::oBrowserTitulos:GoPosition(nQtdLinha)
			endif
		else
			FwAlertError(Alltrim(aRetEventoEnvio[1]) +CHR(13)+CHR(10)+Alltrim(aRetEventoEnvio[2]),STR0001)
			::cEventoSelecionado := ""
			::TrocarEvento()
		endif
	endif

	if ::cEventoSelecionado == ::cEventoMonitor

		::oOK := LoadBitmap(GetResources(),'br_verde')
		::oNO := LoadBitmap(GetResources(),'br_vermelho')

		::aBrwMonitor := {}
		for nCountC := 1 to 2

			if nCountC == 1
				cEvento := ::cEventoTransmissao
			elseif nCountC == 2
				cEvento := ::cEventoCancelamento
			endif

			aEventosRetorno := TSSMonEven(@cError,,::cIdEntidade,::cChaveDoc,::cChaveDoc,cEvento,@cMsg)

			if cError == "" .and. !Empty(aEventosRetorno)
				for nCount := 1 to Len(aEventosRetorno)

					nTamID := Len(aEventosRetorno[nCount,3])
					cSeq := SubStr(Alltrim(aEventosRetorno[nCount,3]),nTamID-2,2)
					cDocumento := SubStr(Alltrim(aEventosRetorno[nCount,3]),34,9)
					cSerie := SubStr(Alltrim(aEventosRetorno[nCount,3]),31,3)

					aAdd(::aBrwMonitor,{iif(aEventosRetorno[nCount,1] == 6,.T.,.f.),;
						iif(cEvento == ::cEventoTransmissao,STR0120,STR0121),; // Transmissão' , Cancelamento'),;
						cSeq,;
						cDocumento,;
						cSerie,;
						aEventosRetorno[nCount,2],;
						aEventosRetorno[nCount,3],;
						iif(aEventosRetorno[nCount,5] == "6",STR0118,STR0119),; // Autorizado","Nao Autorizada,;
						aEventosRetorno[nCount,6],;
						aEventosRetorno[nCount,7],;
						aEventosRetorno[nCount,8]})
				next
			endif
		next
		if !Empty(::aBrwMonitor)
			aSort(::aBrwMonitor,,,{|x,y| x[3] < y[3] })

			::oBrowserTitulos := TCBrowse():New( 05 , 05, ::oPanelDadosBrowser:NWIDTH / 2 - 10 , ::oPanelDadosBrowser:NHEIGHT / 2 - 10  ,, {'',STR0027,STR0020,STR0026,STR0028,STR0023,STR0030,STR0031,STR0027,STR0032,STR0033},;
				{10,50,50,70,50,80,160,70,90,20,350},::oWorkAreaContainer:GetPanel("WIDBROWSER"),,,,,{||},,,,,,,.F.,,.T.,,.F.,,, )
			::oBrowserTitulos:SetArray(::aBrwMonitor)
			::oBrowserTitulos:bLine := {||{ If(::aBrwMonitor[::oBrowserTitulos:nAt,01],::oOK,::oNO),;
				::aBrwMonitor[::oBrowserTitulos:nAt,02],;
				::aBrwMonitor[::oBrowserTitulos:nAt,03],;
				::aBrwMonitor[::oBrowserTitulos:nAt,04],;
				::aBrwMonitor[::oBrowserTitulos:nAt,05],;
				::aBrwMonitor[::oBrowserTitulos:nAt,06],;
				::aBrwMonitor[::oBrowserTitulos:nAt,07],;
				::aBrwMonitor[::oBrowserTitulos:nAt,08],;
				::aBrwMonitor[::oBrowserTitulos:nAt,09],;
				::aBrwMonitor[::oBrowserTitulos:nAt,10],;
				::aBrwMonitor[::oBrowserTitulos:nAt,11];
				}}

			::oBrowserTitulos:DrawSelect()
			::oBrowserTitulos:Refresh()
			::oBrowserTitulos:GoPosition(nQtdLinha)
		else
			::cEventoSelecionado := ""
			::TrocarEvento()
		endif
	endif

Return

/*/{Protheus.doc} MonitoraEvento
	Metodo que Monitora evento
	@author Ricardo Henrique de Mello Lima
	@since 25/08/2024
	@version 12.1.2310
	/*/
Method MonitoraEvento(cEvento as character ) as array class NfeEConf

	Local cError := "" as character
	Local cMsg := "" as character
	Local aRetornoMonitoramento := {} as array

	aRetornoMonitoramento := TSSMonEven(@cError,, ::cIdEntidade, ::cChaveDoc, ::cChaveDoc, cEvento, @cMsg )

Return ({cError,cMsg,aRetornoMonitoramento})

/*/{Protheus.doc} AdicionaEvento
	Adiciona Evento a ser Transmitido no Browser de Transmissão de Eventos
	@author Ricrado Henrique de Mello Lima
	@since 25/08/2024
	@version 12.1.2310
	/*/
Method AdicionaEvento(cTPagamentos as character,cUFPag as character,cBandeira as character,cUFRec as character,cIndPag as character) class NfeEConf

	if ::aBrwDados[1,1] == .F.
		aDel(::aBrwDados,1)
		::aBrwDados := {}
	endif


	aAdd(::aBrwDados,{;
		.T.,;
		cIndPag,;
		cTPagamentos,;
		::dGetData,;
		::cCNPJPag,;
		cUFPag,;
		::cCNPJF,;
		cBandeira,;
		::cCNPJR,;
		cUFRec,;
		::cAutCart,;
		::nValor;
		})

	::oOK := LoadBitmap(GetResources(),'br_verde')
	::oNO := LoadBitmap(GetResources(),'br_vermelho')

	::oBrowserTitulos:SetArray(::aBrwDados)
	::oBrowserTitulos:bLine := {||{ If(::aBrwDados[::oBrowserTitulos:nAt,01],::oOK,::oNO),;
		::aBrwDados[::oBrowserTitulos:nAt,02],;
		::aBrwDados[::oBrowserTitulos:nAt,03],;
		::aBrwDados[::oBrowserTitulos:nAt,04],;
		::aBrwDados[::oBrowserTitulos:nAt,05],;
		::aBrwDados[::oBrowserTitulos:nAt,06],;
		::aBrwDados[::oBrowserTitulos:nAt,07],;
		::aBrwDados[::oBrowserTitulos:nAt,08],;
		::aBrwDados[::oBrowserTitulos:nAt,09],;
		::aBrwDados[::oBrowserTitulos:nAt,10],;
		::aBrwDados[::oBrowserTitulos:nAt,11],;
		Transform(::aBrwDados[::oBrowserTitulos:nAT,12],'@E 99,999,999,999.99') } }

	::oBrowserTitulos:DrawSelect()
	::oBrowserTitulos:GoBottom()
	::oBrowserTitulos:Refresh()

	::dGetData := date()
	::cCNPJPag := space(14)
	::cCNPJF := space(14)
	::cCNPJR := space(14)
	::cAutCart := space(20)
	::nValor := 0

	::oComboIndPag:Select(1)
	::oComboPagametos:Select(1)
	::oComboUFP:Select(1)
	::oComboUFR:Select(1)
	::oBandeira:Select(1)

	::oTGetData:Refresh()
	::oCNPJPag:Refresh()
	::oCNPJF:Refresh()
	::oCNPJR:Refresh()
	::oAutCart:Refresh()
	::oValor:Refresh()

Return

/*/{Protheus.doc} RemoveEvento
	Remove Item do Browser do evento de Transmissão 
	@author Ricardo Henrique de Mello LIma
	@since 28/08/2024
	@version 12.1.2310
	/*/
Method RemoveEvento() class NfeEconf

	if Len(::aBrwDados) <= 1
		::aBrwDados := {{.F.,'','','','','','','','','','',0}}
	else
		aDel(::aBrwDados,::oBrowserTitulos:nAt)
		aSize(::aBrwDados,Len(::aBrwDados)-1)
	endif

	::oOK := LoadBitmap(GetResources(),'br_verde')
	::oNO := LoadBitmap(GetResources(),'br_vermelho')

	::oBrowserTitulos:SetArray(::aBrwDados)
	::oBrowserTitulos:bLine := {||{ If(::aBrwDados[::oBrowserTitulos:nAt,01],::oOK,::oNO),;
		::aBrwDados[::oBrowserTitulos:nAt,02],;
		::aBrwDados[::oBrowserTitulos:nAt,03],;
		::aBrwDados[::oBrowserTitulos:nAt,04],;
		::aBrwDados[::oBrowserTitulos:nAt,05],;
		::aBrwDados[::oBrowserTitulos:nAt,06],;
		::aBrwDados[::oBrowserTitulos:nAt,07],;
		::aBrwDados[::oBrowserTitulos:nAt,08],;
		::aBrwDados[::oBrowserTitulos:nAt,09],;
		::aBrwDados[::oBrowserTitulos:nAt,10],;
		::aBrwDados[::oBrowserTitulos:nAt,11],;
		Transform(::aBrwDados[::oBrowserTitulos:nAT,12],'@E 99,999,999,999.99') } }

	::oBrowserTitulos:DrawSelect()
	::oBrowserTitulos:GoBottom()
	::oBrowserTitulos:Refresh()

Return

/*/{Protheus.doc} PreparaEvento
	envia o Evento Selecionado a API do TSS
	@author Ricardo Henrique de Mello Lima
	@since 25/08/2024
	@version 12.1.2310
	/*/
Method PreparaEvento() as logical class NfeEConf

	Local lRetorno := .F. as logical
	Local cXml := "" as character
	Local nCount := 0 as numeric

	if Alltrim(::cEventoSelecionado)  == "" .or. ::cEventoSelecionado == ::cEventoMonitor
		lRetorno := .F.
	else

		for nCount := 1 to len(::aBrwDados)
			if ::aBrwDados[nCount,1]
				lRetorno := .T.
				exit
			endif
		next

		if lRetorno

			if ::cEventoSelecionado == ::cEventoTransmissao

				cXml := '<envEvento>'
				cXml += '	<eventos>'
				cXml += '   	<detEvento>'
				cXml += '       	<tpEvento>'+::cEventoSelecionado+'</tpEvento>'
				cXml += '        	<chnfe>'+::cChaveDoc+'</chnfe>'
				cXml += '        	<descEvento>ECONF</descEvento>'

				for nCount := 1 to Len(::aBrwDados)
					if ::aBrwDados[nCount,1]
						cXml += ' <detPag>'
						cXml += ' 	<!---Informar ou CNPJ ou CPF, nunca os dois-->'
						if ::cTipoCli == 'J'
							cXml += '   <CNPJ>'+::cCnpjNota+'</CNPJ>'
						else
							cXml += ' <CPF>'+::cCnpjNota+'</CPF>'
						endif
						cXml += '   <indPag>'+SubStr(Alltrim(::aBrwDados[nCount,2]),1,1)+'</indPag>'
						cXml += '   <UF>'+Alltrim(::aBrwDados[nCount,6])+'</UF>'
						cXml += '   <vPag>'+StrTran(Alltrim(Transform(self:aBrwDados[nCount,12],'@E 99999999999.99')),',','.')+'</vPag>'
						cXml += '   <tPag>'+SubStr(Alltrim(::aBrwDados[nCount,3]),1,2)+'</tPag>'
						cXml += '   <xPag>'+SubStr(Alltrim(::aBrwDados[nCount,3]),4,Len(::aBrwDados[nCount,3]))+'</xPag>'
						cXml += '   <dPag>'+Alltrim(DtoS(::aBrwDados[nCount,4]))+'</dPag>'
						cXml += '   <CNPJPag>'+Alltrim(::aBrwDados[nCount,5])+'</CNPJPag>'
						cXml += '   <UFPag>'+Alltrim(::aBrwDados[nCount,6])+'</UFPag>'
						cXml += '   <CNPJIF>'+Alltrim(::aBrwDados[nCount,7])+'</CNPJIF>'
						cXml += '   <tBand>'+Substr(Alltrim(::aBrwDados[nCount,8]),1,2)+'</tBand>'
						cXml += '   <cAut>'+Alltrim(::aBrwDados[nCount,11])+'</cAut>'
						cXml += '   <CNPJReceb>'+Alltrim(::aBrwDados[nCount,9])+'</CNPJReceb>'
						cXml += '   <UFReceb>'+Alltrim(::aBrwDados[nCount,10])+'</UFReceb>'
						cXml += ' </detPag>'
					endif
				next
				cXml += '    	</detEvento>'
				cXml += '    </eventos>'
				cXml += '</envEvento>'

				MsgRun(STR0122,STR0123,{||( lRetorno := ::EnviaEvento(cXml))}) //"Transmitindo Eventos","Processando"
				if lRetorno
					::cEventoSelecionado := ::cEventoMonitor
					::TrocarEvento()
				endif
			elseif ::cEventoSelecionado == ::cEventoCancelamento

				::aRetEvento := {}

				for nCount := 1 to len(::aBrwDados)

					if ::aBrwDados[nCount,1]

						cXml := '<envEvento>'
						cXml += '    <eventos>'
						cXml += '        <detEvento>'
						cXml += '            <tpEvento>'+::cEventoSelecionado+'</tpEvento>'
						cXml += '            <chnfe>'+::cChaveDoc+'</chnfe>'
						cXml += '            <descEvento>Cancelamento Conciliacao Financeira</descEvento>'
						cXml += '            <nSeqEvento>'+Alltrim(::aBrwDados[nCount,2])+'</nSeqEvento>'
						cXml += '        </detEvento>'
						cXml += '    </eventos>'
						cXml += '</envEvento>'

						MsgRun(STR0122,STR0123,{||( lRetorno := ::EnviaEvento(cXml))}) //"Transmitindo Eventos","Processando"
						if lRetorno
							::cEventoSelecionado := ::cEventoMonitor
							::TrocarEvento()
						endif
					endif
				next
			endif
		endif
	endif

Return lRetorno

/*/{Protheus.doc} EnviaEvento
	Envia Evento
	@author Ricardo Henrique de Mello Lima
	@since 29/08/2024
	@version 12.1.2310
	/*/
Method EnviaEvento(cXml as character) as logical class NfeEconf

	Local cError := "" as character
	Local lRetorno := .T. as logical

	aAdd(::aRetEvento,{TSSEnvEven(@cError,,::cIdEntidade,cXml)})

	if Alltrim(cError) != ""
		FwAlertInfo(cError+CHR(13)+CHR(10)+STR0124,STR0001) // "Evento não pode ser enviados" "ECONF"
		lRetorno := .F.
	endif

Return lRetorno

/*/{Protheus.doc} Seleciona Cancelamento
	Metodo responsavel por fazer acionar o click do checkbox do browser cancelar eventos Transmissao
	@author Ricardo Henrique de Mello Lima
	@since 25/08/2024
	@version 12.1.2310
	/*/
Method SelecionaCancelamento(nOpc as numeric) class NfeEConf

	Local nLinha := 0 as numeric
	Local nCount := 0 as numeric

	if nOpc == 1
		nLinha := ::oBrowserTitulos:nAt
		if Alltrim(::aBrwDados[nLinha,2]) != ""
			if ::aBrwDados[nLinha,1]
				::aBrwDados[nLinha,1] := .F.
			else
				::aBrwDados[nLinha,1] := .T.
			endif
		endif
	endif

	if nOpc == 2
		for nCount := 1  to Len(::aBrwDados)
			if Alltrim(::aBrwDados[nCount,2]) != ""
				if ::aBrwDados[nCount,1]
					::aBrwDados[nCount,1] := .F.
				else
					::aBrwDados[nCount,1] := .T.
				endif
			endif
		next
	endif
Return

/*/{Protheus.doc} RereshBrowserMonitor
	(long_description)
	@author user
	@since 01/09/2024
	@version version
	@param param_name, param_type, param_descr
	@return return_var, return_type, return_description
	/*/
Method RefreshBrowserMonitor() class NfeEConf

	Local aRetornoEvento := {} as array

	if ::cEventoSelecionado == ::cEventoMonitor
		aRetornoEvento := ::MonitoraEvento(::cEventoSelecionado)
		if Alltrim(aRetornoEvento[1]) == ""
			FreeObj(::oBrowserTitulos)
			::MontaBrowser()
		else
			FwAlertError(Alltrim(aRetornoEvento[1]) +CHR(13)+CHR(10)+Alltrim(aRetornoEvento[2]),STR0001)
		endif
	endif

Return

/*/{Protheus.doc} MostraMenssagem
	Mostra Dados do evento no Monitor de eventos
	@author Ricardo Henrique de Mello Lima
	@since 01/09/2024
	@version 12.1.2310
	/*/
Method MostraMenssagem(cTitulo as character, cSubTitulo as character, cMenssagem as character) class NfeEConf

	Local oModal := nil as object
	Local oContainer := nil as object
	Local oTMultLine := nil as Object
	Local nLinha := 0 as numeric

	if ::cEventoSelecionado == ::cEventoMonitor

		nLinha := ::oBrowserTitulos:nAt

		cMenssagem := STR0027+" - "+Alltrim(::aBrwMonitor[nLinha,2])+CHR(13)+CHR(10)
		cMenssagem += STR0020+": "+Alltrim(::aBrwMonitor[nLinha,3])+CHR(13)+CHR(10)
		cMenssagem += STR0026+": "+Alltrim(::aBrwMonitor[nLinha,4])+CHR(13)+CHR(10)
		cMenssagem += STR0028+": "+Alltrim(::aBrwMonitor[nLinha,5])+CHR(13)+CHR(10)
		cMenssagem += STR0029+": "+Alltrim(::aBrwMonitor[nLinha,6])+CHR(13)+CHR(10)
		cMenssagem += STR0030+": "+Alltrim(::aBrwMonitor[nLinha,7])+CHR(13)+CHR(10)
		cMenssagem += STR0022+": "+Alltrim(::aBrwMonitor[nLinha,8])+CHR(13)+CHR(10)
		cMenssagem += STR0025+": "+Alltrim(::aBrwMonitor[nLinha,9])+CHR(13)+CHR(10)
		cMenssagem += STR0031+": "+cValtoChar(::aBrwMonitor[nLinha,10])+CHR(13)+CHR(10)
		cMenssagem += STR0033+": "+Alltrim(::aBrwMonitor[nLinha,11])+CHR(13)+CHR(10)

		oModal := FWDialogModal():New()
		oModal:SetEscClose(.T.)
		oModal:SetTitle(cTitulo)
		oModal:setSubTitle(cSubTitulo)

		oModal:setSize(250,250)

		oModal:CreateDialog()
		oModal:addCloseButton(nil,"Fechar")

		oContainer := TPanel():New(,,,oModal:GetPanelMain())
		if ::lThemeDark
			oContainer:SetCss("TPanel{background-color : white;}")
		endif
		oContainer:Align := CONTROL_ALIGN_ALLCLIENT

		oTMultLine := TMultiget():New(01,01,{| u | if( pCount() > 0, cMenssagem := u, cMenssagem)},oContainer,245,180,,,,,,.T.)
		oTMultLine:lReadOnly := .T.
		oModal:Activate()
	endif

Return

/*/{Protheus.doc} ThemaSistema
	Recebe o Thema selecionado para ajuste de cores do SCSS das Telas
	@author Ricardo Henrique de Mello Lima
	@since 08/11/2024
	@version 12.1.2410
	/*/
Method ThemaSistema() class NfeEConf

	Local lRelease2410 	:= GetRpoRelease() >= "12.1.2410" as logical
	Local oTheme := nil as object

	If lRelease2410
		oTheme := totvs.framework.css.ProtheusTheme():New()
		if Alltrim(oTheme:CTHEME) == "DARK"
			::lThemeDark := .T.
		endif
	EndIf

	FreeObj(oTheme)

Return

