#include "tlpp-core.th"
#INCLUDE "TOTVS.ch"

namespace totvs.protheus.backoffice.tss.engine.tributaveis

//-----------------------------------------------------------------------
/*/{Protheus.doc}  TSSTCIntegration
    Classe responsavel pela Integracao TSS com Configurador de Tributos.

    @author Felipe Duarte Luna
    @since 21.01.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Class TSSTCIntegration 

    Private Data cResquestReturn                              as character
    Private Data jNfTaxDatas                                  as json
    Private Data oNFDados                                     as Object
    Private Data oTaxGeneric                                  as json

    Public  Method New()                                      Constructor    // Metodo construtor da classe TCIProcessing
    Public  Method Destroy()                                                 // Metodo para limpar os objetos e arrays da classe TSSTCIntegration.
    Public  Method SetInfoNfs(aIdTribs)                                      // Metodo responsavel de Buscar os dados da NF(NF-e,NFS-e, Ct-e etc.) quando escriturada pelo Configurador de Tributos.    
    Public  Method GetNFDataId()                              as Json        // Metodo responsavel por retornar os dados dos tributos por item em estrutura Json, Retorno identico da Classe TCWritten.
    Public  Method GetTax(cIdTrib, cImposto)                  as Json        // Metodo responsavel por retornar os dados do tributo especificado em estrutura Json.
    Public  Method GetBaseTax(cIdTrib, cImposto)              as Json        // Metodo responsavel por retornar as regras base do tributo especificado em estrutura Json.
    Public  Method GetAliquotaTax(cIdTrib, cImposto)          as Json        // Metodo responsavel por retornar as regras de alíquota do tributo especificado em estrutura Json.
    Public  Method GetEscrituracaoRuleTax(cIdTrib, cImposto)  as Json        // Metodo responsavel por retornar as regras de escrituração do tributo especificado em estrutura Json.
    Public  Method GetNFConfigTributos()                      as logical     // Metodo responsavel por retornar o conteudo do atributo lNFConfigTributos, de acordo com o ID do tributo.

    Private Method CreateTCIWritten()                         as logical     // Metodo responsavel de criar a instancia da classe TCIWritten
    Private Method DestroyTCIWritten()                        as logical     // Metodo responsavel de destruir a instancia da classe TCIWritten
    Private Method ApplyTaxRule()                             as logical     // Metodo responsavel de aplicar as regras de tributacao ao Imposto
    Private Method GetTaxCodesJson()                          as character   // Metodo responsavel de retornar os codigos de tributos em formato Json

EndClass

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method New
    Metodo construtor da classe TSSTCIntegration.

    @author Felipe Duarte Luna
    @since 21.01.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method New() Class TSSTCIntegration
    self:cResquestReturn          := ""
    self:jNfTaxDatas              := JsonObject():New()
    self:oTaxGeneric              := JsonObject():New()
Return 

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method destroy
    Metodo para limpar os objetos e arrays da classe TSSTCIntegration.

    @author Felipe Duarte Luna
    @since 22.01.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method Destroy() Class TSSTCIntegration
    self:cResquestReturn := ""
    FreeObj(self:jNfTaxDatas)
    self:jNfTaxDatas := nil
    FreeObj(self:oTaxGeneric)
    self:oTaxGeneric := nil
Return

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method CreateTCIWritten
    Metodo responsavel de criar a instancia da classe TCIWritten.
    @return .T. se a instancia foi criada com sucesso.

    @author Felipe Duarte Luna
    @since 21.01.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method CreateTCIWritten() as logical Class TSSTCIntegration
    self:oNFDados := totvs.protheus.backoffice.fiscal.tciclass.TCIWritten():New()
Return .T.

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method DestroyTCIWritten
    Metodo responsavel de destruir a instancia da classe TCIWritten.
    @return .T. se a instancia foi destruida com sucesso.

    @author Felipe Duarte Luna
    @since 22.01.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method DestroyTCIWritten() as logical Class TSSTCIntegration
    If ValType(self:oNFDados) == 'O'
        self:oNFDados:destroy()
        self:oNFDados := Nil
    EndIf
Return .T.

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method GetNFDataId
    Metodo responsavel por retornar os dados dos tributos por item em estrutura Json,
    Retorno identico da Classe TCWritten.
    @return Json - Dados dos tributos dos itens com respectivos impostos.

    @author Felipe Duarte Luna
    @since 21.01.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method GetNFDataId() as Json Class TSSTCIntegration
Return self:jNfTaxDatas

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method GetTax
    Metodo responsavel por retornar os dados do tributo especificado em estrutura Json.
    @param cIdTrib - ID do tributo.
    @param cImposto - Código do imposto.
    @return Json - Dados do tributo especificado.

    @author Felipe Duarte Luna
    @since 26.01.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method GetTax(cIdTrib as character , cImposto as character) as Json Class TSSTCIntegration
Return self:oTaxGeneric[cIdTrib][cImposto]

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method GetBaseTax
    Metodo responsavel por retornar as regras base do tributo especificado em estrutura Json.
    @param cIdTrib - ID do tributo.
    @param cImposto - Código do imposto.
    @return Json - Regras base do tributo especificado.

    @author Felipe Duarte Luna
    @since 26.01.2025
    @version 12.1.2410 
/*///-----------------------------------------------------------------------
Method GetBaseTax(cIdTrib as character , cImposto as character) as Json Class TSSTCIntegration
Return self:oTaxGeneric[cIdTrib][cImposto]:GetJsonObject("regras_base")

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method GetAliquotaTax
    Metodo responsavel por retornar as regras de alíquota do tributo especificado em estrutura Json.
    @param cIdTrib - ID do tributo.
    @param cImposto - Código do imposto.
    @return Json - Regras de alíquota do tributo especificado.

    @author Felipe Duarte Luna
    @since 27.01.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method GetAliquotaTax(cIdTrib as character , cImposto as character) as Json Class TSSTCIntegration
Return self:oTaxGeneric[cIdTrib][cImposto]:GetJsonObject("regras_aliquota")

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method GetEscrituracaoRuleTax
    Metodo responsavel por retornar as regras de escrituração do tributo especificado em estrutura Json.
    @param cIdTrib - ID do tributo.
    @param cImposto - Código do imposto.
    @return Json - Regras de escrituração do tributo especificado.
    
    @author Felipe Duarte Luna
    @since 27.01.2025
    @version 12.1.2410 
/*///-----------------------------------------------------------------------
Method GetEscrituracaoRuleTax(cIdTrib as character , cImposto as character) as Json Class TSSTCIntegration
Return self:oTaxGeneric[cIdTrib][cImposto]:GetJsonObject("regras_escrituracao")

//-----------------------------------------------------------------------
/*/{Protheus.doc} Method GetNFConfigTributos
    Metodo responsavel por retornar o conteudo do atributo lNFConfigTributos, de acordo com o ID do tributo.
    @param cIdTrib - ID do tributo.
    @return Logical - Conteudo do atributo lNFConfigTributos.
    
    @author Felipe Duarte Luna
    @since 28/01/2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method GetNFConfigTributos(cIdTrib as character) as logical Class TSSTCIntegration
Return self:oTaxGeneric[cIdTrib]:GetJsonObject("lNFConfigTributos")

//-----------------------------------------------------------------------
/*/{Protheus.doc} GetInfoNfs
    Metodo responsavel de Buscar os dados da NF(NF-e,NFS-e, Ct-e etc.) quando escriturada pelo Configurador de Tributos.    
    @param cValue - ID da NF.

    @author Felipe Duarte Luna
    @since 21/01/2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method SetInfoNfs(aIdTribs as array ) Class TSSTCIntegration
    Local lVldExc    := FindClass("totvs.protheus.backoffice.fiscal.tciclass.TCIWritten")
    Local aIds       := {}            as array
    Local nArraySize := Len(aIdTribs) as integer

    aIds := aIdTribs

    If ( lVldExc .And. Len(aIds) > 0 )
        If self:CreateTCIWritten()        
            self:oNFDados:SetId(aIds)
            self:oNFDados:setDataItems({"regras_base", "regras_aliquota", "regras_escrituracao"})
            // Recupera os dados dos registros definidos pelos IDs. Retorno uma String em formato Json
            self:cResquestReturn := self:oNFDados:GetDataId()
            self:jNfTaxDatas:FromJson( self:cResquestReturn)

            If ( self:jNfTaxDatas:HasProperty('dados_Id')  )
                FWLogMsg('INFO',, 'TSSTCIntegration', FunName(), '', '01', "[" + DtoC( date() ) + " - " + time() + " ] " + allTrim("Retorno Configurador de Tributos, "  +"Contador dos itens Pesquisados Array: " + cValToChar(nArraySize)+ CHR(13) + CHR(10) + self:cResquestReturn ) , 0, 0, {})
                self:ApplyTaxRule()
            EndIf

            self:DestroyTCIWritten()
        EndIf    
    EndIf   
return

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method ApplyTaxRule
    Metodo responsavel de aplicar as regras de tributacao ao Imposto.

    @param cValue - ID da NF.

    @author Felipe Duarte Luna
    @since 25/01/2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method ApplyTaxRule() as logical Class TSSTCIntegration
    
    Local cTaxCode      := ''                  as character
    Local cObjectName   := ''                  as character
    Local cIDNota       := ''                  as character
    Local i, j                                 as integer
    Local aTaxItems     := {}                  as array
    Local oItem         := JsonObject():New()  as json
    Local oTaxCodes     := JsonObject():New()  as json
    Local aIDNotas      := {}                  as array

    oTaxCodes:FromJson(self:GetTaxCodesJson())
    aIDNotas := self:jNfTaxDatas['dados_Id']:GetNames()

    For j := 1 To Len(aIDNotas)
        cIDNota := aIDNotas[j]
        If self:jNfTaxDatas['dados_Id']:HasProperty(cIDNota)
            aTaxItems := self:jNfTaxDatas['dados_Id'][cIDNota]:GetNames()

            If !self:oTaxGeneric:HasProperty(cIDNota)
                self:oTaxGeneric[cIDNota] := JsonObject():New()
                If ( !self:jNfTaxDatas['dados_Id'][aIDNotas[j]]:HasProperty("error") .And. !self:jNfTaxDatas['dados_Id'][aIDNotas[j]]:HasProperty("Aviso") )
                    self:oTaxGeneric[cIDNota]['lNFConfigTributos'] := .T.
                Else
                    self:oTaxGeneric[cIDNota]['lNFConfigTributos'] := .F.
                    self:oTaxGeneric[cIDNota]['error'] := self:jNfTaxDatas['dados_Id']:GetJsonObject(cIDNota)
                    LOOP    
                EndIf
            EndIf

            // Percorrer os Impostos fiscais do IDNota atual
            For i := 1 To Len(aTaxItems)
                oItem := self:jNfTaxDatas['dados_Id'][cIDNota]:GetJsonObject(aTaxItems[i])
                cTaxCode    := oItem:GetJsonObject("codigo_tributo_relacionado")
                cObjectName := oTaxCodes:GetJsonText(cTaxCode)
                If !Empty(cTaxCode) .And. Upper(cObjectName) != "NULL"
                    self:oTaxGeneric[cIDNota][cObjectName] := oItem
                EndIf
            Next
        EndIf
    Next

    aSize(aTaxItems, 0)
    FreeObj(oItem)
    oItem := nil
    FreeObj(oTaxCodes)
    oTaxCodes := nil
    
Return

//-----------------------------------------------------------------------
/*/{Protheus.doc}  Method GetTaxCodesJson
    Metodo responsavel por retornar os códigos de tributos em formato Json.

    @return Character - String Json contendo os códigos de tributos.

    @author Felipe Duarte Luna
    @since 27.01.2025
    @version 12.1.2410
/*///-----------------------------------------------------------------------
Method GetTaxCodesJson() Class TSSTCIntegration
    Local cTaxCodesJson := '{"000002": "FUNRUR", "000003": "SENAR", "000004": "AFRMM", "000005": "FABOV", ' + ;
        '"000006": "FACST", "000007": "FAMAD", "000008": "FASEMT", "000009": "FETHAB", "000010": "FUNDER", ' + ;
        '"000011": "FUNDESS", "000012": "IMAMT", "000013": "SEST", "000014": "TPDP", "000015": "PIS", ' + ;
        '"000016": "COFINS", "000017": "II", "000018": "IRF", "000019": "INSS", "000020": "ISS", ' + ;
        '"000021": "ICMS", "000022": "IPI", "000023": "CIDE", "000024": "CPRB", "000025": "FEEF", ' + ;
        '"000026": "CSL", "000027": "PROTEG", "000028": "FUMIPQ", "000029": "CRDPRE", "000030": "CRPRST", ' + ;
        '"000031": "CPPROD", "000032": "CRDPCT", "000033": "SECP15", "000034": "SECP20", "000035": "SECP25", ' + ;
        '"000036": "INSSPT", "000037": "DIFAL", "000038": "CMP", "000039": "ANTEC", "000040": "FECPICP", ' + ;
        '"000041": "FCPST", "000042": "FCPCMPP", "000043": "COFRET", "000044": "COFST", "000045": "PISRET", ' + ;
        '"000046": "PISST", "000047": "ISSBI", "000048": "PISMAJ", "000049": "COFMAJ", "000050": "DED", ' + ;
        '"000051": "FRTAUT", "000052": "DZFICM", "000053": "DZFPI", "000054": "DZFCOF", "000055": "ESTICM", ' + ;
        '"000056": "ICMSST", "000057": "FRTEMB", "000058": "CRDOUT", "000060": "IBS_ESTADUAL", "000061": "IBS_MUNICIPAL", ' + ;
        '"000062": "CBS_FEDERAL", "000063": "IMPOSTO_SELETIVO", "000064": "IBS_EST_REGULAR", "000065": "IBS_MUN_REGULAR", ' + ;
        '"000066": "CBS_REGULAR", "000067": "IBS_CRED_PRESUMIDO", "000068": "CBS_CRED_PRESUMIDO", "000069": "IBS_MONOFASICO", ' + ;
        '"000070": "CBS_MONOFASICO", "000071": "FUST", "000072": "FUNTTE"}' as character

Return cTaxCodesJson
