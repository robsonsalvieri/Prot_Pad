#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'classificadorfiscal.ch'

static jRegrasCache := JsonObject():New()
static jPerfisCache := JsonObject():New()

Namespace totvs.protheus.backoffice.fiscal.Classifier

//-------------------------------------------------------------------
/*/{Protheus.doc} FISGravaRegra
Classe que contém as operações para gravar a regra de calculo
da regra da Systax
@author  Erich buttner
@since   09.06.2022
@version 1.0
/*/
//-------------------------------------------------------------------
CLASS FISGravaRegra

    data setcod as character
    data setcodoper as character
    data username as character
    data senha as character
    data url as character

    data rest_client as object
    data rToken as character
    data error as numeric
    data error_description as character
    data JSONResult as json
    data cIdCenario as character
    data cCodProd as character
    data nOriProd as numeric
    data cPontAtu as character
    data nBaseComp as numeric
    data jRet as json
    data oJsonRetor as object
    data cCJECod as character
    data oRegraBase as object
    data oRegraAliquota as object
    data oRegraEscrituracao as object
    data oStatementPerfis as object

    public data aRegras as array
    public data aOrigensEDestinos as array
    public data aOperacoes as array
 
    Public METHOD New() CONSTRUCTOR
    Public METHOD FISGravaRegra(cCodRegra,cDesc,cTributo,cCodAliquota,cCodBase,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino,cTributoMajorado,aFormulas) as array
    Public METHOD FISGetDados(oJsonRegra,aRegraAliquota,aRegraBase,aRegraEscrituracao,lIPI,aRegisterAliquot,aRegisterBase)
    Public METHOD GetPerfis(cIDcenario, cIDProduto, cIDOrigem, jTaxSystax) as json
    METHOD setcod(setcod)
    METHOD setcodoper(setcodoper)
    METHOD setUserName(username)
    METHOD setSenha(senha)
    METHOD setUrl(url)
    METHOD AtuCJE(cCJECod)
    Public METHOD gravaPerfOperacao()
    METHOD FISGetPerfOper()
    METHOD ProcessaTributosfilhos(cTax, cTributo, aRegisterAliquot, aRegisterBase)
    METHOD gravaMajoracao()
    METHOD gravaTributo()
    Public METHOD GravaPerfilOrigemEDestino()
    METHOD AtualizaCodigoRegraDeCalculo()
    Public METHOD ExcluiRegraDeCalculo() as logical
    Public METHOD ExcluiPerfilDeOperacao() as logical
    Private method BuscaApelidoCenario(cIDCenario) as character
    Public method InativaRegraDeCalculo(cCodigo,cTributos) as logical

END CLASS

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe
@author  Erich Buttner
@since   09.06.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD New() CLASS FISGravaRegra

Local aParam := {"CODREGREGRA","URLPRINCIPAL","USERNAMEDEFAULT","SENHADEFAULT","CODPERFOPER"}
Local cParametro := GetConfigClassif(aParam)
Local oParametro := JsonObject():New()

If oParametro:FromJson(cParametro) == Nil

    ::setcod(oParametro:CODREGREGRA)
    ::setcodoper(oParametro:CODPERFOPER)
    ::seturl(oParametro:URLPRINCIPAL)
    ::setSenha(oParametro:SENHADEFAULT)
    ::setUserName(oParametro:USERNAMEDEFAULT)

    ::AtualizaCodigoRegraDeCalculo()

    FreeObj(oParametro)

    FWFreeArray(aParam)

EndIf

self:aRegras := {}
self:aOrigensEDestinos := {}
self:aOperacoes := {}

Return

//-----------------------------------------------------------------
 /*/{Protheus.doc} FISGetDados()
Método para obter os dados dos impostos no Json
@author 	Erich Buttner
@since 		09.06.2022
@version 	1.0
/*/
//-----------------------------------------------------------------
METHOD FISGetDados(oJsonRegra,aRegraAliquota,aRegraBase,aRegraEscrituracao,lIPI,aRegisterAliquot,aRegisterBase) CLASS FISGravaRegra

    Local cIDcenario            as character
    Local cIDProduto            as character
    Local cIDOrigem             as character
    Local oJsonDet              as json
    Local nE                    as numeric
    Local nEB                   as numeric
    Local nEC                   as numeric
    Local aJson                 := {} as array
    Local aRegraRet             := {} as array
    Local cStrJson              as character
    Local cCodBase              as character
    Local cCodRegra             as character
    Local nPosAliq              as numeric
    Local nPosBase              as numeric
    Local nPosEscr              as numeric
    Local cCodEscrituracao      as character
    Local cCodAliquota          as character
    Local cCodNatOper           as character
    Local cFinalidade           as character
    Local cDescOper             as character
    Local oJsonRetor            as object
    Local cPerfilProduto        as character
    Local cPerfilParticipantes  as character
    Local cPerfilOrigemDestino  as character
    Local cPerfilOperacao       as character
    Local cTributo              as character
    Local cDesc                 as character
    Local cTributoMajorado      as character
    Local jTaxSystax            as json
    Local nPerred               as numeric
    Local cDescred              as character
    Local cNovoPerfilOriDes     as character
    Local jOrigemEDestino       as json
    Local jPerfis               as json
    Local cEntSai               as character
    Local cApelido              as character

    Default lIPI := .F.

    oJsonRetor  := JsonObject():New()
    oJsonRetor["retorno"]   := {}

    BEGIN TRANSACTION

    For nEB := 1 to Len(oJsonRegra["cenario"])

        cNovoPerfilOriDes := ''
        cEntSai := oJsonRegra["cenario"][nEB]["ent_sai"]
        cIDCenario := oJsonRegra["cenario"][nEB]["id_cenario"]
        cApelido := self:BuscaApelidoCenario(cIDCenario)

        For nEC := 1 to Len(oJsonRegra["cenario"][nEB]["retorno"])

            cIDProduto := oJsonRegra["cenario"][nEB]["retorno"][nEC]["cod_prod"]
            cIDOrigem  := oJsonRegra["cenario"][nEB]["retorno"][nEC]["origem_produto"]

            aJson := oJsonRegra["cenario"][nEB]["retorno"][nEC]:GetNames()

            For nE := 1 to Len(aJson)                

                cStrJson := aJson[nE]

                If lIPI
                    If ( cStrJson != "ipi" )
                        loop
                    EndIf
                EndIf

                If ValType(oJsonRegra["cenario"][nEB]["retorno"][nEC][cStrJson]) == "J"

                    cCodAliquota        := ""
                    cCodBase            := ""
                    cCodEscrituracao    := ""
                    cDescred            := ""                    
                    nPerred             := 0

                    nPosAliq := aScan(aRegraAliquota, {|x| Upper(x[1]) == Upper(cStrJson)})

                    If nPosAliq > 0 
                        cCodAliquota := aRegraAliquota[nPosAliq][2]
                    EndIf

                    nPosBase := aScan(aRegraBase, {|x| Upper(x[1]) == Upper(cStrJson)})

                    If nPosBase > 0 
                        cCodBase := aRegraBase[nPosBase][2]
                    EndIf
                    
                    // 1 - Entradas
                    // 4 - Devolução de Saídas
                    if ( cEntSai $ '1|4' .and. !('credito' $ cStrJson) )
                        nPosEscr := aScan(aRegraEscrituracao, {|x| Upper(x[1]) == Upper('credito_' + cStrJson)})
                    else
                        nPosEscr := aScan(aRegraEscrituracao, {|x| Upper(x[1]) == Upper(cStrJson)})
                    endif

                    If nPosEscr > 0 
                        cCodEscrituracao := aRegraEscrituracao[nPosEscr][2]
                    EndIf

                    jPerfis := self:GetPerfis(cIDcenario, cIDProduto, cIDOrigem, oJsonRegra["cenario"][nEB]["retorno"][nEC])

                    cPerfilProduto          := AllTrim(jPerfis["perfil_produto"])
                    cPerfilParticipantes    := AllTrim(jPerfis["perfil_participantes"])
                    cPerfilOrigemDestino    := AllTrim(jPerfis["perfil_origemdestino"])
                    cCodNatOper             := ""
                    cFinalidade             := ""

                    FreeObj(jPerfis)

                    jOrigemEDestino := JsonObject():New()
                    jOrigemEDestino['codigo'] := ''
                    jOrigemEDestino['incluido'] := .F.

                    If ( Empty(cPerfilOrigemDestino) )
                        If ( Empty(cNovoPerfilOriDes) )
                            cNovoPerfilOriDes := self:GravaPerfilOrigemEDestino(oJsonRegra["cenario"][nEB], @jOrigemEDestino)
                        EndIf

                        cPerfilOrigemDestino := cNovoPerfilOriDes
                    EndIf

                    jOrigemEDestino['codigo'] := cPerfilOrigemDestino
                    
                    Aadd(self:aOrigensEDestinos, jOrigemEDestino)

                    If ValType(oJsonRegra["cenario"][nEB]["cod_natureza_operacao"]) <> "U"
                        cCodNatOper := oJsonRegra["cenario"][nEB]["cod_natureza_operacao"]
                        cDescOper   := cCodNatOper + " - " + cApelido
                    EndIf

                    If ValType(oJsonRegra["cenario"][nEB]["finalidade"]) <> 'U'
                        cFinalidade := oJsonRegra["cenario"][nEB]["finalidade"]
                        cDescOper   += cFinalidade + " - " + cApelido
                    EndIf
                    
                    If !Empty(cCodNatOper) .Or. !Empty(cFinalidade) 
                        cPerfilOperacao := ::gravaPerfOperacao(cCodNatOper,cFinalidade,cDescOper)
                    EndIf

                    cTributo := IiF(Upper(cStrJson) == "COFINS","COF",Upper(cStrJson))

                    cTributoMajorado := Self:ProcessaTributosfilhos(cTributo,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino, aRegisterAliquot, aRegisterBase)
                    jTaxSystax := oJsonRegra["cenario"][nEB]["retorno"][nEC][cStrJson]

                    If jTaxSystax:hasproperty('p_red_bc') .or. jTaxSystax:hasproperty('p_red_bc_st')
                        nPerred := Iif(jTaxSystax:hasproperty('p_red_bc'), val(jTaxSystax["p_red_bc"]), val(jTaxSystax["p_red_bc_st"]))
                        cDescred   := upper(cStrJson) + " - " + str(Val(AllTrim(oJsonRegra["cenario"][nEB]["retorno"][nEC][cStrJson]["aliquota"])),5,2)+"%" + ;
                        " - " + str(nPerred,5,2)+"%" + " REDUCAO"
                    //Else
                    //    cDescred := upper(cStrJson) + " - " + str(Val(AllTrim(oJsonRegra["cenario"][nEB]["retorno"][nEC][cStrJson]["aliquota"])),5,2)+"%"
                    EndIf

                    cDesc := Upper(cTributo) + ' - PROD ' + cPerfilProduto + ' - PART ' + cPerfilParticipantes + ' - OPER ' + cPerfilOperacao + ' - ORIG-DEST ' + cPerfilOrigemDestino + ' ' + cDescred

                    cCodRegra   := "TRB"+AllTrim(::setcod)

                    If nPosAliq > 0    
                        aRegraRet := ::FISGravaRegra(@cCodRegra,cDesc,cTributo,cCodAliquota,cCodBase,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino, cTributoMajorado)
                    Else
                        loop
                    EndIf

                    If aRegraRet[1]
                                            
                        oJsonDet    := JsonObject():New()

                        oJsonDet["imposto"] :=  upper(cStrJson)
                        oJsonDet["codigo"]  :=  upper(cCodRegra)

                        aadd(oJsonRetor["retorno"],oJsonDet)

                        FREEOBJ( oJsonDet )

                        If aRegraRet[2]
                            ::setcod := ::AtuCJE(AllTrim(::setcod),"CODREGREGRA")                            
                        EndIf

                        //Processa tributos que dependem do tributo principal
                        If LOWER(cTributo) =='icms'
                            cTributoMajorado := Self:ProcessaTributosfilhos('icmsst',cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino, aRegisterAliquot, aRegisterBase, cCodRegra)                            
                            cTributoMajorado := Self:ProcessaTributosfilhos('difal',cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino, aRegisterAliquot, aRegisterBase, cCodRegra)
                        endif
                        
                    EndIf

                    FWFreeArray(aRegraRet)

                EndIF
                
            Next nE

            FWFreeArray(aJson)

        Next nEC

    Next nEB

    END TRANSACTION

    if ( ValType(self:oStatementPerfis) == 'O' )
        FreeObj(self:oStatementPerfis)
    endif

Return oJsonRetor

/*/{Protheus.doc} Processafilhos
    (long_description)
    @author user
    @since 22/12/2023
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method ProcessaTributosfilhos(cTributo,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino, aRegisterAliquot, aRegisterBase, cTribOrigem) CLASS FISGravaRegra
    Local cTributoMajorado as Character
    Local cTax as character    

    cTax := lower(cTributo)

    Do CASE
        Case cTax == 'icms'
            cTributoMajorado := Self:gravaMajoracao(cTributo,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino, aRegisterAliquot, aRegisterBase)            
        Case cTax == 'icmsst'            
            cTributoMajorado := Self:gravaMajoracao(cTributo,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino, aRegisterAliquot, aRegisterBase)
            Self:gravaTributo(cTributo,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino, aRegisterAliquot, aRegisterBase, cTributoMajorado, cTribOrigem)
        Case cTax == 'difal'
            cTributoMajorado := Self:gravaMajoracao(cTributo,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino, aRegisterAliquot, aRegisterBase)
            Self:gravaTributo(cTributo,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino, aRegisterAliquot, aRegisterBase, cTributoMajorado, cTribOrigem)
        //Case cTax == 'cofins'
        //    cTributoMajorado := Self:gravaMajoracao(cTributo,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino, aRegisterAliquot, aRegisterBase)
        //Case cTax == 'pis'
        //    cTributoMajorado := Self:gravaMajoracao(cTributo,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino, aRegisterAliquot, aRegisterBase)
    EndCase

Return cTributoMajorado

/*/{Protheus.doc} gravaMajoracao
    (long_description)
    @author user
    @since 22/12/2023
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method gravaMajoracao(cTributo,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino, aRegisterAliquot, aRegisterBase) CLASS FISGravaRegra

    Local cTributoMajorado as Character
    Local nPosAliqTax as numeric
    Local nPosBaseTax as numeric
    Local aRegraRet as array
    Local cDesc as character
    Local cCodRegra as character
    Local cTaxMajorado as character   
    Local cTax as character
    Local cCodAliquota as character
    Local cCodBase as character

    cTax := lower(cTributo)

    Do CASE
        Case cTax == 'icms'
            cTaxMajorado := 'fecpic'            
        Case cTax == 'icmsst'
            cTaxMajorado := 'fcpst'            
        Case cTax == 'difal' .or. cTax == 'cmp'
            cTaxMajorado := 'fcpcmp'
        //Case cTax == 'cof'
        //    cTaxMajorado := 'pismaj'
        //Case cTax == 'pis'
        //    cTaxMajorado := 'cofmaj'
    EndCase

    If .not. Empty(cTaxMajorado)
        nPosAliqTax := ascan(aRegisterAliquot, {|x| x[1] == cTaxMajorado})
        nPosBaseTax := ascan(aRegisterBase, {|x| x[1] == cTaxMajorado})

        If nPosAliqTax > 0 .and. nPosBaseTax > 0

            cCodAliquota := aRegisterAliquot[nPosAliqTax][2]
            cCodBase := aRegisterBase[nPosBaseTax][2]

            cDesc := upper(cTax) +" - Majoração "
            
            If cTax == 'icms' .or. cTax == 'icmsst' .or. cTax == 'difal' .or. cTax == 'cmp' 
                cDesc := upper(cTaxMajorado) +" - Fundo de Combate à Pobreza "
            Endif
            
            cCodRegra   := "TRB"+AllTrim(::setcod)
            
            aRegraRet := ::FISGravaRegra(@cCodRegra,cDesc,upper(cTaxMajorado),cCodAliquota,cCodBase,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino)

            If aRegraRet[1]
                
                cTributoMajorado := cCodRegra

                If aRegraRet[2]
                    ::setcod := ::AtuCJE(AllTrim(::setcod),"CODREGREGRA")
                EndIf                
            EndIf
        Endif
    Endif
    
Return cTributoMajorado

/*/{Protheus.doc} gravaTributo
    (long_description)
    @author user
    @since 22/12/2023
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method gravaTributo(cTributo,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino, aRegisterAliquot, aRegisterBase, cTributoMajorado, cTribOrigem) CLASS FISGravaRegra
    
    Local cCodRegra as character
    Local nPosAliqTax as numeric
    Local nPosBaseTax as numeric
    Local aRegraRet as array
    Local cDesc as character
    Local cTax as character
    Local cCodAliquota as character
    Local cCodBase as character
    Local cTributoCadastrado as character
    Local aformulas as array
    Local cFormulaTrib := ''


    cTax := lower(cTributo)
    aformulas := {'','',''}

    If .not. Empty(cTributo)
        nPosAliqTax := ascan(aRegisterAliquot, {|x| x[1] == cTax})
        nPosBaseTax := ascan(aRegisterBase, {|x| x[1] == cTax})

        If nPosAliqTax > 0 .and. nPosBaseTax > 0

            cCodAliquota := aRegisterAliquot[nPosAliqTax][2]
            cCodBase := aRegisterBase[nPosBaseTax][2]

            cDesc := Upper(cTributo) + ' - PROD ' + cPerfilProduto + ' - PART ' + cPerfilParticipantes + ' - OPER ' + cPerfilOperacao + ' - ORIG-DEST ' + cPerfilOrigemDestino

            cCodRegra   := "TRB"+AllTrim(::setcod)

            If !EMPTY( cTribOrigem )
                If Empty(cTributoMajorado)
                    cFormulaTrib := " ( B:BASTRIB * A:ALQTRIB ) - VAL:TRIORI "
                    cFormulaTrib := STRTRAN(cFormulaTrib , "TRIORI",cTribOrigem)
                    aformulas[1] := cFormulaTrib
                Else
                    cFormulaTrib := " ( B:BASTRIB * ( A:ALQTRIB + ALQ:ALQTRIBMAJ ) ) - VAL:TRIORI"
                    cFormulaTrib := STRTRAN(cFormulaTrib , "TRIORI",cTribOrigem)
                    cFormulaTrib := STRTRAN(cFormulaTrib , "ALQTRIBMAJ",cTributoMajorado)
                    aformulas[1] := cFormulaTrib
                Endif
                
            Endif

            aRegraRet := ::FISGravaRegra(@cCodRegra,cDesc,upper(cTributo),cCodAliquota,cCodBase,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino, cTributoMajorado, aformulas)

            If aRegraRet[1]
                
                cTributoCadastrado := cCodRegra

                If aRegraRet[2]
                    ::setcod := ::AtuCJE(AllTrim(::setcod),"CODREGREGRA")
                EndIf                
            EndIf
        Endif
    Endif
Return cTributoCadastrado

//-----------------------------------------------------------------
 /*/{Protheus.doc} FISGravaRegra()
Método para gravar a regra de calculo no configurador de tributos
@author 	Erich Buttner
@since 		09.06.2022
@version 	1.0
/*/
//-----------------------------------------------------------------
METHOD FISGravaRegra(cCodRegra,cDesc,cTributo,cCodAliquota,cCodBase,cCodEscrituracao,cPerfilProduto,cPerfilParticipantes,cPerfilOperacao,cPerfilOrigemDestino,cTributoMajorado,aFormulas) CLASS FISGravaRegra

Local oModel	            as object
Local lExist                as Logical
Local aRegraRet             := {}
Local cTributado            := ""
Local cIsento               := ""
Local cOutros               := ""
Local cChaveMD5             as character
Local oMd5Classification    as object
Local jMd5Find              as json
Local jChave                as json
Local aErro                 := {} as array
Local jRegra                as json

Default cCodRegra           := "TRB"+AllTrim(::setcod)
Default cTributoMajorado    := ""
Default aformulas           := {'','',''}

cDesc := Substr(cDesc,1,TamSX3('F2B_DESC')[1])

oModel := FWLoadModel('FISA160')

oModel:SetOperation(MODEL_OPERATION_INSERT)
oModel:Activate()

oModel:SetValue('FISA160','F2B_TRIB'  , cTributo)
oModel:SetValue('FISA160','F2B_REGRA' , cCodRegra)
oModel:SetValue('FISA160','F2B_DESC'  , cDesc)
oModel:SetValue('FISA160','F2B_VIGINI', dDataBase)
oModel:SetValue('FISA160','F2B_RBASE' , cCodBase)
oModel:SetValue('FISA160','F2B_RALIQ' , cCodAliquota)
oModel:SetValue('FISA160','F2B_CODESC', cCodEscrituracao)
oModel:SetValue('FISA160','F2B_PEROD' , cPerfilOrigemDestino)
oModel:SetValue('FISA160','F2B_PERFPA', cPerfilParticipantes)
oModel:SetValue('FISA160','F2B_PERFOP', cPerfilOperacao)
oModel:SetValue('FISA160','F2B_PERFPR', cPerfilProduto)

If !Empty(cTributoMajorado)
    oModel:SetValue('FISA160','F2B_TRBMAJ', cTributoMajorado)
EndIf

cTributado  := aformulas[1]
cIsento     := aformulas[2]
cOutros     := aformulas[3]

If !Empty(cTributado)
    cTributado := STRTRAN(cTributado,"BASTRIB",cCodBase)
    cTributado := STRTRAN(cTributado,"ALQTRIB",cCodAliquota)        
    Fsa160Form("",cTributado)
    //xForBtnAct(cTributado, "FORMULCAL", .F., "VIEW_FORMULA")
EndIf
//If !Empty(cIsento)
//    Fsa160Form(,,cIsento)
//EndIf
//If !Empty(cOutros)
//    Fsa160Form(,,,cOutros)
//Endif

jChave := JsonObject():New()
jChave['F2B_TRIB'] := oModel:GetValue('FISA160', 'F2B_TRIB')
jChave['F2B_RBASE'] := oModel:GetValue('FISA160', 'F2B_RBASE')
jChave['F2B_RALIQ'] := oModel:GetValue('FISA160', 'F2B_RALIQ')
jChave['F2B_PEROD'] := oModel:GetValue('FISA160', 'F2B_PEROD')
jChave['F2B_PERFPA'] := oModel:GetValue('FISA160', 'F2B_PERFPA')
jChave['F2B_PERFOP'] := oModel:GetValue('FISA160', 'F2B_PERFOP')
jChave['F2B_PERFPR'] := oModel:GetValue('FISA160', 'F2B_PERFPR')
jChave['F2B_RFIN'] := oModel:GetValue('FISA160', 'F2B_RFIN')
jChave['F2B_RAPUR'] := oModel:GetValue('FISA160', 'F2B_RAPUR')
jChave['F2B_RND'] := oModel:GetValue('FISA160', 'F2B_RND')
jChave['F2B_RBASES'] := oModel:GetValue('FISA160', 'F2B_RBASES')
jChave['F2B_TRBMAJ'] := oModel:GetValue('FISA160', 'F2B_TRBMAJ')
jChave['F2B_DEDDEP'] := oModel:GetValue('FISA160', 'F2B_DEDDEP')
jChave['F2B_DEDPRO'] := oModel:GetValue('FISA160', 'F2B_DEDPRO')
jChave['F2B_CODESC'] := oModel:GetValue('FISA160', 'F2B_CODESC')
jChave['F2B_MAXMIN'] := oModel:GetValue('FISA160', 'F2B_MAXMIN')
jChave['F2B_RGGUIA'] := oModel:GetValue('FISA160', 'F2B_RGGUIA')
jChave['F2B_VLRMAX'] := oModel:GetValue('FISA160', 'F2B_VLRMAX')
jChave['F2B_VLRMIN'] := oModel:GetValue('FISA160', 'F2B_VLRMIN')
jChave['F2B_OPRMIN'] := oModel:GetValue('FISA160', 'F2B_OPRMIN')
jChave['F2B_OPRMAX'] := oModel:GetValue('FISA160', 'F2B_OPRMAX')

jMd5Find := JsonObject():New()
jMd5Find['cache'] := jRegrasCache
jMd5Find['table'] := 'F2B'
jMd5Find['index'] := 8
jMd5Find['codeField'] := 'F2B_REGRA'
jMd5Find['key'] := jChave
jMd5Find['ruleTypes'] := '8,11,12'

oMd5Classification := Md5Classification():New()
oMd5Classification:Find(jMd5Find)
lExist := oMd5Classification:GetFound()
cChaveMD5 := oMd5Classification:GetMd5Key()

if ( lExist )
    cCodRegra := oMd5Classification:GetCode()
endif

aRegraRet := { lExist, .F., {} }

if ( !lExist )
    If oModel:Activate()
        If oModel:VldData()
            oModel:CommitData()
            oModel:DeActivate()
            aRegraRet[1] := .T.
            aRegraRet[2] := .T.
            aRegraRet[3] := {}
        Else
            aErro := oModel:GetErrorMessage()
            
            aRegraRet[1] := .F.
            aRegraRet[2] := .F.
            aRegraRet[3] := {}
            aadd(aRegraRet[3],oModel:aErrorMessage[1]+"-"+oModel:aErrorMessage[2]+"-"+oModel:aErrorMessage[3]+"-"+oModel:aErrorMessage[4]+"-"+oModel:aErrorMessage[5]+"-"+oModel:aErrorMessage[6])
        EndIf
    EndIf
endif

oModel:DeActivate()
oModel:Destroy()		
FwFreevar( oModel )
FreeObj(oModel)

oMd5Classification:Destroy()
FwFreevar(oMd5Classification)
FwFreevar(jMd5Find) 
FwFreevar(jChave)   

if ( aRegraRet[1] )
    jRegrasCache[cChaveMD5] := cCodRegra
endif

jRegra := JsonObject():New()
jRegra['codigo'] := cCodRegra
jRegra['incluido'] := aRegraRet[2]
jRegra['md5'] := cChaveMD5

if ( Len(aErro) > 0 )
    jRegra['erro'] := JsonObject():New()
    jRegra['erro']['formularioDeOrigem'] := AllToChar(aErro[1]) 
    jRegra['erro']['campoDeOrigem'] := AllToChar(aErro[2])
    jRegra['erro']['formularioDeErro'] := AllToChar(aErro[3])
    jRegra['erro']['campoDeErro'] := AllToChar(aErro[4])
    jRegra['erro']['erro'] := AllToChar(aErro[5])
    jRegra['erro']['mensagemDoErro'] := AllToChar(aErro[6])
    jRegra['erro']['mensagemDaSolucao'] := AllToChar(aErro[7])
    jRegra['erro']['valorAtribuido'] := AllToChar(aErro[8])
    jRegra['erro']['valorAnterior'] := AllToChar(aErro[9])
endif

Aadd(self:aRegras, jRegra)

ASize(aErro, 0)

Return aRegraRet

//-------------------------------------------------------------------
/*/{Protheus.doc} setUserName
Método para passar o nome do usuário que irá utilizar a api.
@author  Erich Buttner
@since   16.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method setUserName(username) Class FISGravaRegra

    Self:username := username

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} setSenha
Método para passar a senha do usuário que irá utilizar a api.
@author  Erich Buttner
@since   16.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method setSenha(senha) Class FISGravaRegra

    Self:senha := senha

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} setUrl
Método para passar a senha do usuário que irá utilizar a api.
@author  Erich Buttner
@since   16.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method setUrl(Url) Class FISGravaRegra

    Self:Url := Url

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} setcod
Método para passar o código da regra de base do configurador.
@author  Erich Buttner
@since   22.06.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method setcod(setcod) Class FISGravaRegra

    Self:setcod := setcod

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} setcod
Método para passar o código da regra de base do configurador.
@author  Erich Buttner
@since   22.06.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method setcodoper(setcodoper) Class FISGravaRegra

    Self:setcodoper := setcodoper

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} AtuCJE
Método para passar o código da regra de alíquota no Configurador.
@author  Erich Buttner
@since   11.06.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method AtuCJE(cCJECod,cParametro) Class FISGravaRegra

DbSelectArea("CJE")
CJE->(DbSetOrder(1))

If CJE->(MsSeek(xFilial("CJE")+cParametro))
    RecLock("CJE", .F.)
        CJE->CJE_CONTEU := AllTrim(Padl(cValToChar(Val(cCJECod)+1),3,"0"))
        cCJECod := CJE->CJE_CONTEU 
    CJE->(MsUnlock())

    Do Case
        Case cParametro == 'CODREGREGRA'
            self:setcod(cCJECod)
        Case cParametro == 'CODPERFOPER'
            self:setcodoper(cCJECod)
    EndCase
EndIF

CJE->(DbCloseArea())

Return cCJECod

//-------------------------------------------------------------------
/*/{Protheus.doc} GetPerfis
Retorna os IDs de perfis de produto, participante, origem e destino.

@author Erich Buttner
@since 11.06.2022
@version 2.0
@param cIDcenario, character, ID do cenário
@param cIDProduto, character, ID do produto
@param cIDOrigem, character, ID de origem
@param jRetorno, json, Objeto JSON contendo as informações da propriedade "retorno"
@return json, Objeto JSON contendo os IDs dos perfis 
/*/
//-------------------------------------------------------------------
Method GetPerfis(cIDcenario as character, cIDProduto as character, cIDOrigem as character, jRetorno as json) as json Class FISGravaRegra

    local cQry      as character
    local jPerfis   as json
    local cAlias    as character
    local cChaveMD5 as character

    Default jRetorno := Nil

    jPerfis := JsonObject():New()

    cChaveMD5 := MD5(cIDcenario + cIDProduto + cIDOrigem)

    if ( jPerfisCache:HasProperty(cChaveMD5) )
        jPerfis['perfil_produto']       := jPerfisCache[cChaveMD5]['perfil_produto']
        jPerfis['perfil_participantes'] := jPerfisCache[cChaveMD5]['perfil_participantes']
        jPerfis['perfil_origemdestino'] := jPerfisCache[cChaveMD5]['perfil_origemdestino']

        return jPerfis
    endif

    if ( self:oStatementPerfis == nil )
        cQry += ' SELECT CJG_IDPPAR, CJG_IDPORI, CJG_IDPPRO '
        cQry += ' FROM ' + RetSqlName('CJG') + ' CJG '
        cQry += ' WHERE CJG_FILIAL = ? '
        cQry += '   AND CJG_IDCENA = ? '
        cQry += '   AND CJG.D_E_L_E_T_ = ? '

        self:oStatementPerfis := FwExecStatement():New(ChangeQuery(cQry))
    endif

    self:oStatementPerfis:SetString(1, xFilial('CJG'))
    self:oStatementPerfis:SetString(2, cIdCenario)
    self:oStatementPerfis:SetString(3, ' ')

    cQry := self:oStatementPerfis:GetFixQuery()

    cAlias := self:oStatementPerfis:OpenAlias(GetNextAlias())

    jPerfis['perfil_produto']       := ''
    jPerfis['perfil_participantes'] := ''
    jPerfis['perfil_origemdestino'] := ''

    if ( !(cAlias)->(Eof()) )
        jPerfis['perfil_produto']       := (cAlias)->CJG_IDPPRO
        jPerfis['perfil_participantes'] := (cAlias)->CJG_IDPPAR
        jPerfis['perfil_origemdestino'] := (cAlias)->CJG_IDPORI
    endif

    If jRetorno != Nil .And. jRetorno:HasProperty('grupo_produto_associado') .And. !Empty(jRetorno['grupo_produto_associado'])
        jPerfis['perfil_produto'] := jRetorno['grupo_produto_associado']
    EndIf

    jPerfisCache[cChaveMD5] := JsonObject():New()
    jPerfisCache[cChaveMD5]['perfil_produto'] := jPerfis['perfil_produto']
    jPerfisCache[cChaveMD5]['perfil_participantes'] := jPerfis['perfil_participantes']
    jPerfisCache[cChaveMD5]['perfil_origemdestino'] := jPerfis['perfil_origemdestino']

    (cAlias)->(DbCloseArea())

Return jPerfis

//-------------------------------------------------------------------
/*/{Protheus.doc} gravaPerfOperacao
Método para passar o código da regra de base no Configurador.
@author  Erich Buttner
@since   11.06.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method gravaPerfOperacao(cCodNatOper,cFinalidade,cDescOper) Class FISGravaRegra

Local oModel	    as object
Local oCFOP		    as object
Local cQry          := ""
Local aCfop         := {}
Local lExist        as Logical
Local cDscOper      := SubStr(cDescOper,1,TamSX3("F20_DESC")[1])
Local cCodPerfOper  := ::FISGetPerfOper(cDscOper,@lExist)
Local nX            := 0
Local cAliasProd    := ""
Local oStatement    as object
Local lIncluido     as logical
Local aErro         := {} as array
Local jOperacao     as json

If Empty(cCodPerfOper)
    cCodPerfOper := "OPE" + AllTrim(::setcodoper)
EndIf

cQry += "SELECT CJF_CFOP, CJF_TIPO FROM "+ RetSqlName("CJF") +" CJF "
cQry += "WHERE CJF_FILIAL = ? "
cQry += "AND ((CJF_TIPO = ? "
cQry += "AND CJF_COD = ? ) "
cQry += "OR (CJF_TIPO = ? "
cQry += "AND CJF_COD = ? )) "
cQry += "AND CJF.D_E_L_E_T_ = ' ' "

oStatement := FwExecStatement():New( ChangeQuery(cQry) )

oStatement:SetString(1, xFilial("CJF"))
oStatement:SetString(2, '1')
oStatement:SetString(3, cCodNatOper)
oStatement:SetString(4, '2')
oStatement:SetString(5, cFinalidade)

cAliasProd := oStatement:OpenAlias(GetNextAlias())

While !(cAliasProd)->(Eof())

    aAdd(aCfop, {(cAliasProd)->CJF_CFOP})
    (cAliasProd)->(DbSkip())

EndDo

If !lExist

    If Len(aCfop) > 0

        oModel := FWLoadModel('FISA165')
        oCFOP  := oModel:GetModel("FISA165CFOP")

        oModel:SetOperation(MODEL_OPERATION_INSERT)
        oModel:Activate()

        //Start FISA165
        oModel:SetValue('FISA165','F20_CODIGO', cCodPerfOper)
        oModel:SetValue('FISA165','F20_DESC', cDscOper)
        oModel:SetValue('FISA165','F20_TIPO', "03")

        //Start FISA165CFOP
        For nX := 1 to Len(aCFOP)
            IF !(nX == 1)
                oCFOP:AddLine()
            Endif
            oModel:SetValue('FISA165CFOP','F23_CFOP', aCFOP[nX][1])
            oModel:SetValue('FISA165CFOP','F23_CODIGO', cCodPerfOper)
        Next

        oModel:SetValue('FISA165TPOPER','F26_TPOPER', "TODOS")
        oModel:SetValue('FISA165TPOPER','F26_CODIGO', cCodPerfOper)

        If oModel:Activate()
            If oModel:VldData()
                oModel:CommitData()
                oModel:DeActivate()

                self:AtuCJE(AllTrim(::setcodoper),"CODPERFOPER")

                lIncluido := .T.
            Else
                aErro := oModel:GetErrorMessage()
            EndIf
        EndIF

        oModel:DeActivate()
        oModel:Destroy()		
        FwFreevar( oModel )
        FreeObj(oCFOP)

        FWFreeArray(aCfop)

    EndIf

EndIf

jOperacao := JsonObject():New()
jOperacao['codigo'] := cCodPerfOper
jOperacao['incluido'] := lIncluido

if ( Len(aErro) > 0 )
    jOperacao['erro'] := JsonObject():New()
    jOperacao['erro']['formularioDeOrigem'] := AllToChar(aErro[1]) 
    jOperacao['erro']['campoDeOrigem'] := AllToChar(aErro[2])
    jOperacao['erro']['formularioDeErro'] := AllToChar(aErro[3])
    jOperacao['erro']['campoDeErro'] := AllToChar(aErro[4])
    jOperacao['erro']['erro'] := AllToChar(aErro[5])
    jOperacao['erro']['mensagemDoErro'] := AllToChar(aErro[6])
    jOperacao['erro']['mensagemDaSolucao'] := AllToChar(aErro[7])
    jOperacao['erro']['valorAtribuido'] := AllToChar(aErro[8])
    jOperacao['erro']['valorAnterior'] := AllToChar(aErro[9])
endif

Aadd(self:aOperacoes, jOperacao)

ASize(aErro, 0)

(cAliasProd)->(DBCLOSEAREA())

FreeObj(oStatement)

Return cCodPerfOper

//-------------------------------------------------------------------
/*/{Protheus.doc} FISGetPerfOper
Método para passar o código da regra de base no Configurador.
@author  Erich Buttner
@since   11.06.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method FISGetPerfOper(cDescOper,lExist) Class FISGravaRegra

    Local cQry          := ""
    Local cCodigo       := ""
    Local oStatement    := nil

    cQry += " SELECT F20_CODIGO "
    cQry += " FROM ? F20 "
    cQry += " WHERE F20_FILIAL = ? "
    cQry += "   AND F20_DESC = ? "
    cQry += "   AND F20_TIPO = ? "
    cQry += "   AND F20.D_E_L_E_T_ = ? "
    cQry += " GROUP BY F20_CODIGO "

    oStatement := FwExecStatement():New( ChangeQuery(cQry) )

    oStatement:SetUnsafe(1, RetSqlName("F20"))
    oStatement:SetString(2, xFilial("F20"))
    oStatement:SetString(3, cDescOper)
    oStatement:SetString(4, "03")
    oStatement:SetString(5, Space(1))

    cCodigo := oStatement:ExecScalar('F20_CODIGO')

    If !Empty(cCodigo)

        lExist  := .T.

    EndIf

    FreeObj(oStatement)

Return cCodigo

/*/{Protheus.doc} GravaPerfilOrigemEDestino

Método responsável por gravar o Perfil de Origem e Destino caso ainda não exista

@author Juliano Fernandes
@since 15/02/2024
@version 12.1.2310
/*/
method GravaPerfilOrigemEDestino(jCenario as json, jOrigemEDestino as json) class FISGravaRegra

    local oPerfilOrigemEDestino as object
    local cUFOrigem as character
    local cUFDestino as character
    local cCodigoPerfil as character
    local aErro as array

    if ( jCenario:hasProperty('uf_origem') .and. !Empty(jCenario['uf_origem']) )
        cUFOrigem := jCenario['uf_origem']
    endif

    if ( jCenario:hasProperty('uf_destino') .and. !Empty(jCenario['uf_destino']) )
        cUFDestino := jCenario['uf_destino']
    endif

    if ( !Empty(cUFOrigem) .and. !Empty(cUFDestino) )
        oPerfilOrigemEDestino := OriginAndDestinationProfile():New()

        oPerfilOrigemEDestino:AddItem(cUFOrigem, cUFDestino)

        oPerfilOrigemEDestino:Save()

        cCodigoPerfil := oPerfilOrigemEDestino:GetCode()

        jOrigemEDestino['incluido'] := oPerfilOrigemEDestino:GetCreated()

        aErro := oPerfilOrigemEDestino:GetError()

        if ( Len(aErro) > 0 )
            jOrigemEDestino['erro'] := JsonObject():New()
            jOrigemEDestino['erro']['formularioDeOrigem'] := AllToChar(aErro[1]) 
            jOrigemEDestino['erro']['campoDeOrigem'] := AllToChar(aErro[2])
            jOrigemEDestino['erro']['formularioDeErro'] := AllToChar(aErro[3])
            jOrigemEDestino['erro']['campoDeErro'] := AllToChar(aErro[4])
            jOrigemEDestino['erro']['erro'] := AllToChar(aErro[5])
            jOrigemEDestino['erro']['mensagemDoErro'] := AllToChar(aErro[6])
            jOrigemEDestino['erro']['mensagemDaSolucao'] := AllToChar(aErro[7])
            jOrigemEDestino['erro']['valorAtribuido'] := AllToChar(aErro[8])
            jOrigemEDestino['erro']['valorAnterior'] := AllToChar(aErro[9])
        endif
        
        ASize(aErro, 0)

        oPerfilOrigemEDestino:Destroy()

        FwFreevar(oPerfilOrigemEDestino)
    endif

return cCodigoPerfil

/*/{Protheus.doc} AtualizaCodigoRegraDeCalculo

Método responsável por atualizar o código de Regra de Cálculo
caso esteja desatualizado na tabela CJE

@author Juliano Fernandes
@since 12/03/2024
@version 12.1.2310
/*/
method AtualizaCodigoRegraDeCalculo() class FISGravaRegra

    local cQry as character
    local cCodRegra as character
    local oStatement as object

    cQry += " SELECT F2B.F2B_REGRA "
    cQry += " FROM " + RetSqlName('F2B') + " F2B "
    cQry += " WHERE F2B.F2B_FILIAL = ? "
    cQry += "   AND F2B.F2B_REGRA LIKE ? "
    cQry += "   AND F2B.D_E_L_E_T_ = ' ' "
    cQry += " ORDER BY F2B.F2B_REGRA DESC "  

    oStatement := FwExecStatement():New( ChangeQuery(cQry) )

    oStatement:SetString(1, xFilial('F2B'))
    oStatement:SetString(2, 'TRB%')

    cCodRegra := oStatement:ExecScalar('F2B_REGRA')

    cCodRegra := AllTrim(StrTran(cCodRegra, 'TRB', ''))

    self:AtuCJE(cCodRegra, 'CODREGREGRA')

    FreeObj(oStatement)

return

/*/{Protheus.doc} ExcluiRegraDeCalculo

Método responsável por excluir uma Regra de Cálculo

@author Juliano Fernandes
@since 18/06/2024
@version 12.1.2310
/*/
method ExcluiRegraDeCalculo(jRegraDeCalculo as json, jRegra as json) as logical class FISGravaRegra

	local lOk as logical
	local oModel as object
	local aErro as array

    lOk := .T.

    DbSelectArea('F2B')
    F2B->(DbSetOrder(7)) // F2B_FILIAL+F2B_REGRA+F2B_ALTERA
    if ( !F2B->( DbSeek( FWxFilial('F2B') + jRegraDeCalculo['codigo'] + '2' ) ) )
        lOk := .F.

        jRegra['mensagemDoErro'] := STR0254 //'Regra de Cálculo não pode ser excluída'
        jRegra['codigoDoRegistro'] := jRegraDeCalculo['codigo']
        jRegra['detalheDoErro'] := STR0237 //'Registro não encontrado'
    endif

    if ( lOk )
        oModel := FWLoadModel('FISA160')
        oModel:SetOperation(MODEL_OPERATION_DELETE)
        oModel:Activate()

        if ( oModel:VldData() )
            jRegrasCache:DelName(F2B->F2B_CHVMD5)
            oModel:CommitData()
        else
            lOk := .F.

            aErro := oModel:GetErrorMessage()

            jRegra['mensagemDoErro'] := STR0254 //'Regra de Cálculo não pode ser excluída'
            jRegra['codigoDoRegistro'] := jRegraDeCalculo['codigo']
            jRegra['detalheDoErro'] := AllToChar(aErro[6])

            ASize(aErro, 0)
        endif

        oModel:Deactivate()

        oModel:Destroy()

        FreeObj(oModel) 
    endif

    F2B->(DbCloseArea())

return lOk

/*/{Protheus.doc} ExcluiPerfilDeOperacao

Método responsável por excluir um Perfil de Operação

@author Juliano Fernandes
@since 18/06/2024
@version 12.1.2310
/*/
method ExcluiPerfilDeOperacao(jPerfilDeOperacao as json, jRegra as json) as logical class FISGravaRegra

	local lOk as logical
	local oModel as object
	local aErro as array
    local cCodigo as character

    lOk := .T.

    cCodigo := PadR(jPerfilDeOperacao['codigo'], TamSX3('F20_CODIGO')[1])

    DbSelectArea('F20')
    F20->(DbSetOrder(1)) // F20_FILIAL+F20_CODIGO+F20_TIPO

    if ( !F20->( DbSeek( FWxFilial('F20') + cCodigo + '03' ) ) )
        lOk := .F.

        jRegra['mensagemDoErro'] := STR0255 //'Perfil de Operação não pode ser excluído'
        jRegra['codigoDoRegistro'] := jPerfilDeOperacao['codigo']
        jRegra['detalheDoErro'] := STR0237 //'Registro não encontrado'	
    endif

    if ( lOk )
        oModel := FWLoadModel('FISA165')
        oModel:SetOperation(MODEL_OPERATION_DELETE)
        oModel:Activate()

        if ( oModel:VldData() )
            oModel:CommitData()
        else
            lOk := .F.

            aErro := oModel:GetErrorMessage()

            jRegra['mensagemDoErro'] := STR0255 //'Perfil de Operação não pode ser excluído'
            jRegra['codigoDoRegistro'] := jPerfilDeOperacao['codigo']
            jRegra['detalheDoErro'] := AllToChar(aErro[6])

            ASize(aErro, 0)
        endif

        oModel:Deactivate()

        oModel:Destroy()

        FreeObj(oModel) 
    endif

    F20->(DbCloseArea())    

return lOk

/*/{Protheus.doc} BuscaApelidoCenario
Método responsável por buscar o apelido de um cenário
@author Juliano Fernandes
@since 11/03/2025
@version 12.1.2410
@param cIdCenario, character, ID do cenário
@return character, Retorna o apelido do cenário
/*/
method BuscaApelidoCenario(cIdCenario as character) as character class FISGravaRegra

    local cApelido as character
    local jCenario as json

    DbSelectArea('CJG')
    CJG->(DbSetOrder(2)) // CJG_FILIAL+CJG_IDCENA+CJG_IDPPRO+CJG_IDPPAR
    if ( CJG->(DbSeek(FWxFilial('CJG') + PadR(cIdCenario,FWSX3Util():GetFieldStruct('CJG_IDCENA')[3]))) )
        jCenario := JsonObject():New()
        
        if ( jCenario:FromJson(CJG->CJG_JSON) == nil )
            cApelido := jCenario['cenarios'][1]['apelido']
        endif

        FWFreeObj(jCenario)
        jCenario := nil
    endif

    CJG->(DbCloseArea())

return cApelido

/*/{Protheus.doc} InativaRegraDeCalculo
Método responsável por inativar uma regra de cálculo
@author Juliano Fernandes
@since 10/03/2025
@version 12.1.2410
@param jRegraDeCalculo, json, Regra de Cálculo que deve ser inativada
@param cTributos, character, Tributos que devem ser inativados
@return logical, Retorna .T. se a regra de cálculo foi inativada com sucesso
/*/
method InativaRegraDeCalculo(jRegraDeCalculo as json, cTributos as character) as logical class FISGravaRegra

	local lOk as logical
	local oModel as object
    local dVigFim as date

    DbSelectArea('F2B')
    F2B->(DbSetOrder(7)) // F2B_FILIAL+F2B_REGRA+F2B_ALTERA

    if ( F2B->( DbSeek( FWxFilial('F2B') + jRegraDeCalculo['codigo'] + '2' ) ) )
        lOk := Empty(F2B->F2B_VIGFIM) .and. AllTrim(F2B->F2B_TRIB) $ cTributos
    endif

    if ( lOk )
        oModel := FWLoadModel('FISA160')
        oModel:SetOperation(MODEL_OPERATION_UPDATE)
        oModel:Activate()

        dVigFim := Max(oModel:GetValue('FISA160', 'F2B_VIGINI'), DaySub(dDataBase, 1))

        oModel:LoadValue('FISA160', 'F2B_VIGFIM', dVigFim)

        if ( oModel:VldData() )
            oModel:CommitData()
        endif

        oModel:Deactivate()

        oModel:Destroy()

        FreeObj(oModel) 
    endif

    F2B->(DbCloseArea())

return lOk
