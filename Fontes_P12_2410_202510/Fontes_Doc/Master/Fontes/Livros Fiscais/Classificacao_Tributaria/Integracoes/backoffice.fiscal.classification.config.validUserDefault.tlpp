#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'classificadorfiscal.ch'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//-------------------------------------------------------------------
/*/{Protheus.doc} FISVerificaAcesso
Classe que contém as verificações de primeiro acesso 
da regra da Systax
@author  Erich buttner
@since   09.06.2022
@version 1.0
/*/
//-------------------------------------------------------------------
CLASS FISVerificaAcesso

    data jQuery as json
 
    Public METHOD New() CONSTRUCTOR

    @Get('/GetUserSenha')
    Public METHOD GetUserSenha()
    @Post('/GravaUserSenha')
    Public METHOD GravaUserSenha()

END CLASS

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe
@author  Erich Buttner
@since   09.06.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD New() CLASS FISVerificaAcesso



Return

//-----------------------------------------------------------------
 /*/{Protheus.doc} GetUserSenha()
Método para verificar se o usuario e senha de acesso ao classificador 
preenchido na tabela de parametros
@author 	Erich Buttner
@since 		07.07.2022
@version 	1.0
/*/
//-----------------------------------------------------------------
METHOD GetUserSenha(cMockServer) CLASS FISVerificaAcesso

Local cQry          := ""
Local oJBody        := JsonObject():New()
Local cJson         := ""
Local jQuery        := oRest:getQueryRequest()
Local oStatement    as object
Local cAliasPart    as character
Local oRet          as object

Default cMockServer := ""

If !Empty(jQuery['mockserver']) .and. !(jQuery['mockserver'] == Nil)
    cMockServer := jQuery["mockserver"]
EndIf

cQry += " SELECT CJE_PARAM, CJE_CONTEU "
cQry += " FROM "+RetSqlName("CJE")+" CJE "
cQry += " WHERE CJE_FILIAL = ? "
cQry += " AND CJE_PARAM IN ('SENHADEFAULT','USERNAMEDEFAULT') "
cQry += " AND CJE.D_E_L_E_T_ = ' ' "

oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

oStatement:setString(1, xFilial("CJE"))

cQry := oStatement:getFixQuery()

cAliasPart := MPSysOpenQuery(cQry)

If (cAliasPart)->(EoF())

    oJBody['senhadefault']      := ""
    oJBody['usernamedefault']   := ""
    oJBody['Result']            := .F.
    oJBody['dic']               := .T.

EndIf

While (cAliasPart)->(!EoF())

    oJBody[Lower(AllTrim((cAliasPart)->CJE_PARAM))] := AllTrim((cAliasPart)->CJE_CONTEU)

    If Empty(AllTrim((cAliasPart)->CJE_CONTEU))
        oJBody['Result']    := .F.
        oJBody['dic']       := .T.
    Else
        oJBody['Result']    := .T.
        oJBody['dic']       := .T.
    EndIf   
    (cAliasPart)->(DbSkip())

EndDo

FREEOBJ( oStatement )
(cAliasPart)->(DbCloseArea())

oRet := FISTokenClassificador():New()
oRet:VldUserSenha(oJBody['usernamedefault'],IIF(!Empty(oJBody['senhadefault']),(rc4crypt(AllTrim(oJBody['senhadefault']),"123456789",.f.)),oJBody['senhadefault']), cMockServer)

If oRet:lRet
    oJBody['Result']    := .T.
    oJBody['dic']       := .T.
Else
    oJBody['Result']    := .F.
    oJBody['dic']       := .T.
EndIf

cJson := oJBody:toJson()

FREEOBJ( oJBody )
FREEOBJ( oRet )

Return oRest:setResponse(cJson)

//-----------------------------------------------------------------
 /*/{Protheus.doc} GravaUserSenha()
Método para gravar na tabela de parametros o usuário e senha default 
de acesso ao classificador
@author 	Erich Buttner
@since 		07.07.2022
@version 	1.0
/*/
//-----------------------------------------------------------------
METHOD GravaUserSenha(cMockServer) CLASS FISVerificaAcesso

Local oJBody        := JsonObject():New()
Local jQuery        := oRest:getQueryRequest()
Local cUsuario      as character
Local cSenha        as character
Local cJson         as character
Local oRet          as object

Default cMockServer := ""

cUsuario        := jQuery['usuario']
cSenha          := jQuery['senha']

If !Empty(jQuery['mockserver']) .and. !(jQuery['mockserver'] == Nil)
    cMockServer := jQuery["mockserver"]
EndIf

oRet := FISTokenClassificador():New()
oRet:VldUserSenha(cUsuario,cSenha,cMockServer)

If oRet:lRet
    DbSelectArea("CJE")
    DbSetOrder(1)

    If CJE->(MsSeek(xFilial("CJE")+"USERNAMEDEFAULT"))
        If Empty(CJE->CJE_CONTEU)

            RecLock("CJE",.F.)
            CJE->CJE_PARAM := "USERNAMEDEFAULT"
            CJE->CJE_CONTEU := cUsuario
            CJE->CJE_TIPO   := "1"
            CJE->(MsUnLock())

            oJBody['ResultUsuario']   := STR0201 //"Parametro USERNAMEDEFAULT gravado com sucesso!"
            oJBody['Sucesso_usuario'] := .T.
        Else
            RecLock("CJE",.F.)
            CJE->CJE_CONTEU := cUsuario
            CJE->(MsUnLock())

            oJBody['ResultUsuario']   := STR0202 //"Parametro USERNAMEDEFAULT ja está preenchido!"
            oJBody['Sucesso_usuario'] := .T.
        EndIf
    EndIf

    If CJE->(MsSeek(xFilial("CJE")+"SENHADEFAULT"))
        If Empty(CJE->CJE_CONTEU)

            RecLock("CJE",.F.)
            CJE->CJE_PARAM := "SENHADEFAULT"
            CJE->CJE_CONTEU := rc4crypt(AllTrim(cSenha),"123456789",.f.)
            CJE->CJE_TIPO   := "1"
            CJE->(MsUnLock())

            oJBody['ResultSenha']   := STR0203 //"Parametro SENHADEFAULT gravado com sucesso!"
            oJBody['Sucesso_senha'] := .T.
        Else
            RecLock("CJE",.F.)
            CJE->CJE_CONTEU := rc4crypt(AllTrim(cSenha),"123456789",.f.)
            CJE->(MsUnLock())

            oJBody['ResultSenha']   := STR0204 //"Parametro SENHADEFAULT já está preenchido!"
            oJBody['Sucesso_senha'] := .T.
        EndIf
    EndIf

    CJE->(DbCloseArea())
Else

    oJBody['ResultUsuario']     := STR0205 //"Não foi possivel a gravação do parametro USERNAMEDEFAULT, usuário inválido"
    oJBody['Sucesso_usuario']   := .F.
    oJBody['ResultSenha']       := STR0206 //"Não foi possivel a gravação do parametro SENHADEFAULT, senha inválida"
    oJBody['Sucesso_senha']     := .F.

EndIf

cJson := oJBody:toJson()

FREEOBJ( oJBody )
FREEOBJ( ::jQuery )
FREEOBJ( jQuery )
FREEOBJ( oRet )

Return oRest:setResponse(cJson)
