#include 'FWMVCdef.ch'
#include 'tlpp-core.th'
#include 'classificadorfiscal.ch'

static jOriginAndDestinationProfileCache := JsonObject():New()

Namespace totvs.protheus.backoffice.fiscal.Classifier

/*/{Protheus.doc} OriginAndDestinationProfile

Classe para gravação do Perfil de Origem e Destino no Configurador de Tributos

@author Juliano Fernandes
@since 15/02/2024
@version 12.1.2310
/*/
class OriginAndDestinationProfile

    private data cCode as character
    private data cDescription as character
    private data cType as character
    private data aFederativeUnits as array
    private data cSearchKey as character
    private data lCreated as logical
    private data aError as array

    public method New() constructor

    public method AddItem()
    public method Save()
    public method Destroy()
    public method GetCode() as character
    public method GetCreated() as logical
    public method GetError() as array
    public method Delete() as logical
    
    private method SetCode()
    private method SetDescription()
    private method SetSearchKey()
    private method Find() as logical
    private method SearchCache() as logical
    private method AddCache()

endclass

/*/{Protheus.doc} New

Método construtor da classe OriginAndDestinationProfile

@author Juliano Fernandes
@since 15/02/2024
@version 12.1.2310
/*/
method New() class OriginAndDestinationProfile

    self:cCode := ''
    self:cDescription := ''
    self:cType := '01'
    self:aFederativeUnits := {}
    self:cSearchKey := ''
    self:lCreated := .F.
    self:aError := {}

return self

/*/{Protheus.doc} SetCode

Método responsável por criar o código do novo Perfil de Origem e Destino

@author Juliano Fernandes
@since 15/02/2024
@version 12.1.2310
/*/
method SetCode(cOrigin as character, cDestination as character) class OriginAndDestinationProfile

    self:cCode := cOrigin + 'X' + cDestination

return

/*/{Protheus.doc} SetDescription

Método responsável por criar a descrição do novo Perfil de Origem e Destino

@author Juliano Fernandes
@since 15/02/2024
@version 12.1.2310
/*/
method SetDescription(cOrigin as character, cDestination as character) class OriginAndDestinationProfile

    self:cDescription := cOrigin + 'X' + cDestination

return

/*/{Protheus.doc} SetSearchKey

Método responsável por criar a chave de busca do novo Perfil de Origem e Destino

@author Juliano Fernandes
@since 21/02/2024
@version 12.1.2310
/*/
method SetSearchKey() class OriginAndDestinationProfile

    self:cSearchKey := AllTrim(FWxFilial('F21')) + self:cCode + self:cType

return

/*/{Protheus.doc} AddItem

Método responsável por criar um novo ítem de Perfil de Origem e Destino

@author Juliano Fernandes
@since 15/02/2024
@version 12.1.2310
/*/
method AddItem(cOrigin as character, cDestination as character) class OriginAndDestinationProfile

    local jItem as json

    if ( !Empty(cOrigin) .and. !Empty(cDestination) )
        if ( Empty(self:cCode) )
            self:SetCode(cOrigin, cDestination)
        endif

        if ( Empty(self:cDescription) )
            self:SetDescription(cOrigin, cDestination)
        endif

        if ( Empty(self:cSearchKey) )
            self:SetSearchKey()
        endif

        jItem := JsonObject():New()
        jItem['cProfileType'] := '01'
        jItem['cOrigin'] := cOrigin
        jItem['cDestination'] := cDestination

        Aadd(self:aFederativeUnits, jItem)
    endif

return

/*/{Protheus.doc} Save

Método responsável por salvar um novo Perfil de Origem e Destino

@author Juliano Fernandes
@since 15/02/2024
@version 12.1.2310
/*/
method Save() class OriginAndDestinationProfile

    local oModel as object
    local oModelItem as object
    local jItem as json
    local i as numeric
    local nLength as numeric

    if ( !self:Find() )
        oModel := FWLoadModel('FISA163')
        oModel:SetOperation(MODEL_OPERATION_INSERT)

        oModel:Activate()

        oModel:SetValue('FISA163', 'F20_CODIGO', self:cCode)
        oModel:SetValue('FISA163', 'F20_DESC', self:cDescription)
        oModel:SetValue('FISA163', 'F20_TIPO', self:cType)

        nLength := len(self:aFederativeUnits)

        oModelItem := oModel:GetModel('FISA163ORIGEMDESTINO')

        for i := 1 to nLength
            jItem := self:aFederativeUnits[i]

            oModelItem:SetValue('F21_CODIGO', self:cCode)
            oModelItem:SetValue('F21_TIPOPF', jItem['cProfileType'])
            oModelItem:SetValue('F21_UFORI', jItem['cOrigin'])
            oModelItem:SetValue('F21_UFDEST', jItem['cDestination'])

            /*
            O trecho de código abaixo pode ser utilizado caso futuramente seja 
            necessário gravar mais de um item no Perfil de Origem e Destino.
            if ( i < nLength )
                oModelItem:AddLine()
            endif
            */

            FwFreevar(jItem)
        next i

        if ( oModel:VldData() )
            oModel:CommitData()

            self:AddCache(.T.)

            self:lCreated := .T.
        else
            self:aError := oModel:GetErrorMessage()
        endif

        oModel:DeActivate()
        oModel:Destroy()

        FwFreevar(oModel)
        FwFreevar(oModelItem)
    endif

return

/*/{Protheus.doc} Find

Método responsável por verificar a existência de um Perfil de Origem e Destino

@author Juliano Fernandes
@since 15/02/2024
@version 12.1.2310
/*/
method Find() as logical class OriginAndDestinationProfile

    local lFound as logical
    local cKey as character

    lFound := self:SearchCache()

    if ( !lFound )
        cKey := FWxFilial('F21') + PadR(self:cCode, TamSX3('F21_CODIGO')[1]) + self:cType
        lFound := MemoryCache('UF_' + self:cCode,, {|| !Empty(Posicione('F21', 2, cKey, 'F21_CODIGO'))}, .T.)
        self:AddCache(lFound)
    endif

return lFound

/*/{Protheus.doc} SearchCache

Método responsável por buscar no cache se o Perfil de Origem e Destino existe

@author Juliano Fernandes
@since 21/02/2024
@version 12.1.2310
/*/
method SearchCache() as logical class OriginAndDestinationProfile

    local lFound as logical
    local xResult as variant

    xResult := jOriginAndDestinationProfileCache[self:cSearchKey]

    if ( ValType(xResult) == 'L' )
        lFound := xResult
    endif

return lFound

/*/{Protheus.doc} AddCache

Método responsável por adicionar no cache o Perfil de Origem e Destino

@author Juliano Fernandes
@since 21/02/2024
@version 12.1.2310
/*/
method AddCache(lFound as logical) class OriginAndDestinationProfile

    jOriginAndDestinationProfileCache[self:cSearchKey] := lFound

return

/*/{Protheus.doc} GetCode

Método responsável por retornar o código do Perfil de Origem e Destino

@author Juliano Fernandes
@since 14/03/2024
@version 12.1.2310
/*/
method GetCode() as character class OriginAndDestinationProfile
return self:cCode

/*/{Protheus.doc} GetCreated

Método responsável por retornar se o Perfil de Origem e Destino foi incluído

@author Juliano Fernandes
@since 22/05/2024
@version 12.1.2310
/*/
method GetCreated() as logical class OriginAndDestinationProfile
return self:lCreated

/*/{Protheus.doc} GetError

Método responsável por retornar o erro ao incluir um novo Perfil de Origem e Destino

@author Juliano Fernandes
@since 22/05/2024
@version 12.1.2310
/*/
method GetError() as array class OriginAndDestinationProfile
return self:aError

/*/{Protheus.doc} Delete

Método responsável por deletar um Perfil de Origem e Destino

@author Juliano Fernandes
@since 18/06/2024
@version 12.1.2310
/*/
method Delete(jOriginAndDestinationProfile as json, jRule as json) as logical class OriginAndDestinationProfile

	local lOk as logical
	local oModel as object
	local aError as array
    local cCode as character

    lOk := .T.

    cCode := PadR(jOriginAndDestinationProfile['codigo'], TamSX3('F20_CODIGO')[1])

    DbSelectArea('F20')
    F20->(DbSetOrder(1)) // F20_FILIAL+F20_CODIGO+F20_TIPO

    if ( !F20->( DbSeek( FWxFilial('F20') + cCode + '01' ) ) )
        lOk := .F.

        jRule['mensagemDoErro'] := STR0238 //'Perfil de Origem e Destino não pode ser excluído'
        jRule['codigoDoRegistro'] := jOriginAndDestinationProfile['codigo']
        jRule['detalheDoErro'] := STR0237 //'Registro não encontrado'	
    endif

    if ( lOk )
        oModel := FWLoadModel('FISA163')
        oModel:SetOperation(MODEL_OPERATION_DELETE)
        oModel:Activate()

        if ( oModel:VldData() )
            jOriginAndDestinationProfileCache:DelName(AllTrim(F20->F20_FILIAL) + AllTrim(F20->F20_CODIGO) + AllTrim(F20->F20_TIPO))
            oModel:CommitData()
        else
            lOk := .F.

            aError := oModel:GetErrorMessage()

            jRule['mensagemDoErro'] := STR0238 //'Perfil de Origem e Destino não pode ser excluído'
            jRule['codigoDoRegistro'] := jOriginAndDestinationProfile['codigo']
            jRule['detalheDoErro'] := AllToChar(aError[6])

            ASize(aError, 0)
        endif

        oModel:Deactivate()

        oModel:Destroy()

        FreeObj(oModel) 
    endif

    F20->(DbCloseArea())    

return lOk

/*/{Protheus.doc} Destroy

Método responsável por destruir um objeto instanciado pela classe OriginAndDestinationProfile

@author Juliano Fernandes
@since 15/02/2024
@version 12.1.2310
/*/
method Destroy() class OriginAndDestinationProfile

    self:cCode := ''
    self:cDescription := ''
    self:cType := ''
    ASize(self:aFederativeUnits, 0)
    self:cSearchKey := ''
    self:lCreated := .F.
    ASize(self:aError, 0)
    
return 
