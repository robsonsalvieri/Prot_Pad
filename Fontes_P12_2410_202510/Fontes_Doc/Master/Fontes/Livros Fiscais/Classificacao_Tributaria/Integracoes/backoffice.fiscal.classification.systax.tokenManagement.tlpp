#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//-------------------------------------------------------------------
/*/{Protheus.doc} FISAAPIClassificador
Classe que contém as operações referente a integração com a API da 
Systax
@author  Yuri Fortunato Palacio
@since   22.02.2021
@version 1.0
/*/
//-------------------------------------------------------------------
CLASS FISTokenClassificador

    Public data url as character
    data username as character
    data senha as character
    data cUsr as character
    data cUsrFr as character
    data cSenFr as character
    Public data lRet as logical
    
    data rest_client as object
    data token as character
    Public data refresh_token as character
    data error as numeric
    data error_description as character
    data regras as object
    data JSONResult as json

    PUBLIC METHOD New() CONSTRUCTOR
    Public METHOD tokenSys(cUsr)
    Public METHOD tokenSysFront(cUsrFr,cSenFr)
    Public METHOD VldUserSenha()
    METHOD setUserName(username)
    METHOD setSenha(senha)
    METHOD setUrl(url)

END CLASS

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe
@author  Erich Buttner
@since   16.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD New() CLASS FISTokenClassificador

Local aParam := {"URLPRINCIPAL","USERNAMEDEFAULT","SENHADEFAULT"}
Local cParametro := GetConfigClassif(aParam)
Local oParametro := JsonObject():New()

If oParametro:FromJson(cParametro) == Nil

    ::seturl(oParametro:URLPRINCIPAL)
    ::setSenha(oParametro:SENHADEFAULT)
    ::setUserName(oParametro:USERNAMEDEFAULT)

    FreeObj(oParametro)

    FWFreeArray(aParam)

EndIf

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} tokenSysFront
Método para realizar o login do usuário e receber o token
com credenciais vindas do front
@author  Julia
@since   
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD tokenSysFront(cUsrFr,cSenFr,cMockServer) CLASS FISTokenClassificador

Local aHeaderStr    := {}
Local oResult       := Nil
Local cToken        := ""
Local lRet          := .F.
Local cResult       := ""

Default cMockServer := ""

If Empty(cMockServer)
    ::rest_client := FwRest():New(::url)
    ::rest_client:setPath("/auth/access-token")
Else
    ::rest_client := FwRest():New(cMockServer)
    ::rest_client:setPath("/token")
EndIf

Aadd(aHeaderStr, "Authorization: Basic " + Encode64(cUsrFr + ":" + cSenFr))
lRet := ::rest_client:Get( aHeaderStr )

If lRet
    cResult := ::rest_client:GetResult()

    oResult := JsonObject():New()

    If oResult:FromJson(cResult) == Nil
        cToken := oResult:GetJsonObject("token")
    EndIf

    FreeObj(oResult)
EndIf

FWFreeArray(aHeaderStr)

Return cToken


//-------------------------------------------------------------------
/*/{Protheus.doc} tokenSys
Método para realizar o login do usuário e receber o toekn 
para implementar uma integração contínua
@author  Erich Buttner
@since   16.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD tokenSys(cUsr,cMockServer) CLASS FISTokenClassificador

Local aHeaderStr    := {}
Local oResult       := Nil
Local cToken        := ""
Local lRet          := .F.
Local cResult       := ""

Default cMockServer := ""

If Empty(cMockServer)
    ::rest_client := FwRest():New(::url)
    ::rest_client:setPath("/auth/access-token")
Else
    ::rest_client := FwRest():New(cMockServer)
    ::rest_client:setPath("/token")
EndIf

Aadd(aHeaderStr, "Authorization: Basic " + Encode64(::username + ":" + ::senha))

lRet := ::rest_client:Get( aHeaderStr )

If lRet
    cResult := ::rest_client:GetResult()

    oResult := JsonObject():New()

    If oResult:FromJson(cResult) == Nil

        ::token := oResult:GetJsonObject("token")

        ::refresh_token := RefreshToken(::url, ::token, cMockServer)
        DbSelectArea( "CJD" )
        CJD->( DbSetOrder( 2 ) )
        If CJD->( MsSeek( xFilial( "CJD" ) + cUsr ) )
            RecLock( "CJD", .F. )
            CJD->CJD_TOKEN  := ::token
            CJD->CJD_RFTOKE := ::refresh_token
            CJD->CJD_DTRET  := dDataBase
            CJD->CJD_HRTOKE := Time()
            CJD->( MsUnLock() )
        ElseIf Len(self:token) <= 250
            RecLock( "CJD", .T. )
            CJD->CJD_FILIAL := xFilial( "CJD" )
            CJD->CJD_USRSIS := cUsr
            CJD->CJD_TOKEN  := ::token
            CJD->CJD_RFTOKE := ::refresh_token
            CJD->CJD_DTRET  := dDataBase
            CJD->CJD_HRTOKE := Time()
            CJD->( MsUnLock() )
        EndIf

        CJD->(DbCloseArea())

    EndIf

    FreeObj(oResult)
    cToken := ::refresh_token
EndIf

FWFreeArray(aHeaderStr)

Return cToken

//-------------------------------------------------------------------
/*/{Protheus.doc} setUserName
Método para passar o nome do usuário que irá utilizar a api.
@author  Erich Buttner
@since   16.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method setUserName(username) Class FISTokenClassificador

    Self:username := username

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} setSenha
Método para passar a senha do usuário que irá utilizar a api.
@author  Erich Buttner
@since   16.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method setSenha(senha) Class FISTokenClassificador

    Self:senha := senha

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} setUrl
Método para passar a senha do usuário que irá utilizar a api.
@author  Erich Buttner
@since   16.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method setUrl(Url) Class FISTokenClassificador

    Self:Url := Url

Return

/*/{Protheus.doc} RefreshToken()
    (long_description)
    @type  Static Function
    @author Erich Buttner
    @since 16/03/2022
    @version version
    @param param, param_type, param_descr
    @return return, return_type, return_description
    @example
    (examples)
    @see (links_or_references)
    /*/

Method VldUserSenha(cUsuario,cSenha,cMockServer) Class FISTokenClassificador

Local aHeaderStr := {}

Default cMockServer := ""

If Empty(cMockServer)
    ::rest_client := FwRest():New(::url)
    ::rest_client:setPath("/auth/access-token")
Else
    ::rest_client := FwRest():New(cMockServer)
    ::rest_client:setPath("/token")
EndIf

Aadd(aHeaderStr, "Authorization: Basic " + Encode64(AllTrim(cUsuario) + ":" + AllTrim(cSenha)))

::lRet := ::rest_client:Get( aHeaderStr )

FWFreeArray(aHeaderStr)

Return ::lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} GetConfig
Método para passar o refresh token do usuário que irá utilizar a api.
@author  Erich Buttner
@since   17.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Function GetConfigClassif(aParametros)

Local cAlias        := ""
Local cQry          := ""
Local oJson         := JsonObject():New()
Local aArea         := GetArea()
Local aParams       := {} as array
Local nY            as integer
Local oStatement    as object

cQry := "SELECT CJE_PARAM, CJE_CONTEU "
cQry += "FROM "+RetSqlName("CJE")
cQry += " WHERE CJE_FILIAL = ? "
cQry += " AND CJE_PARAM IN ( ? ) "
cQry += " AND D_E_L_E_T_ = ' ' "

For nY := 1 to len(aParametros)
    Aadd(aParams, AllTrim(aParametros[nY]))

    cAlias := BuscaRegistroCJE(aParametros[nY])

    If (cAlias)->(EOF())
        GravaRegistroCJE(aParametros[nY], "", "1", 0)
    EndIf

    (cAlias)->(DbCloseArea())
Next nY

oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

oStatement:SetString(1, xFilial("CJE"))
oStatement:SetIn(2, aParams)

cQry := oStatement:GetFixQuery()

cAlias := MPSysOpenQuery(cQry)

While (cAlias)->(!EoF())
    
    cConteudo := Iif(AllTrim((cAlias)->CJE_PARAM) $ "SENHADEFAULT" .And. !Empty(AllTrim((cAlias)->CJE_CONTEU)), rc4crypt(AllTrim((cAlias)->CJE_CONTEU),"123456789",.f.), AllTrim((cAlias)->CJE_CONTEU))
    
    oJson[AllTrim((cAlias)->CJE_PARAM)] := AllTrim(cConteudo)

    (cAlias)->(DbSkip())
EndDo

cJson := oJson:toJson()

(cAlias)->(DbCloseArea())

FreeObj(oStatement)
FreeObj(oJson)
FWFreeArray(aParams)

RestArea(aArea)

Return cJson

/*/{Protheus.doc} RefreshToken()
    (long_description)
    @type  Static Function
    @author Erich Buttner
    @since 16/03/2022
    @version version
    @param param, param_type, param_descr
    @return return, return_type, return_description
    @example
    (examples)
    @see (links_or_references)
/*/

Function ConsultTok(cUsr,cMockServer)

Local cToken    as character
Local oTokenAtu as object

Default cMockServer := ""

oTokenAtu := FISTokenClassificador():New()

DbSelectArea("CJD")
CJD->(DbSetOrder(2))

If CJD->(MsSeek(xFilial("CJD")+cUsr))
    If (CJD_DTRET < Date()) .Or. (CJD->CJD_DTRET == Date() .And. Hrs2Min(Time())-Hrs2Min(CJD->CJD_HRTOKE) > 60 )
        oTokenAtu:tokenSys(cUsr, cMockServer)
        cToken := oTokenAtu:refresh_token
    Else
        cToken := CJD->CJD_RFTOKE
    EndIf
Else
    oTokenAtu:tokenSys(cUsr, cMockServer)
    cToken := oTokenAtu:refresh_token
EndIf

CJD->(DbCloseArea())

FreeObj(oTokenAtu)

Return cToken

/*/{Protheus.doc} RefreshToken()
    (long_description)
    @type  Static Function
    @author Erich Buttner
    @since 16/03/2022
    @version version
    @param param, param_type, param_descr
    @return return, return_type, return_description
    @example
    (examples)
    @see (links_or_references)
    /*/
Static Function RefreshToken(url,cToken,cMockServer)

Local aHeaderRfh        := {}
Local oResultRfh        := Nil
Local oRest_Refresh     := Nil
Local cRefresh_token    := ""
Local lRet              := .F.
Local cResult           := ""
    
If Empty(cMockServer)
    oRest_Refresh := FwRest():New(url)
    oRest_Refresh:setPath("/auth/refresh-token")
Else
    oRest_Refresh := FwRest():New(cMockServer)
    oRest_Refresh:setPath("/token")
EndIf

Aadd(aHeaderRfh, "Authorization: Bearer " + cToken)

lRet := oRest_Refresh:Get( aHeaderRfh )

If lRet
    cResult := oRest_Refresh:GetResult()

    oResultRfh := JsonObject():New()

    If oResultRfh:FromJson(cResult) == Nil
        cRefresh_token := oResultRfh:GetJsonObject("token")
    EndIf
EndIf

FreeObj(oResultRfh)
FreeObj(oRest_Refresh)
FWFreeArray(aHeaderRfh)
    
Return cRefresh_token

//-------------------------------------------------------------------
/*/{Protheus.doc} BuscaRegistroCJE
Função que realiza a busca de registro na tabela CJE - 
Control Config Classificador  

@author  Juliano Fernandes
@since   30/11/2023
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function BuscaRegistroCJE(cParam)

  Local oStatement  as object
  Local cQry        as character 
  Local cAliasQry   as character

  cQry := "SELECT R_E_C_N_O_ CJE_RECNO "
  cQry += "FROM " + RetSqlName("CJE") + " CJE "
  cQry += "WHERE CJE_FILIAL = ? "
  cQry += " AND CJE_PARAM = ? "
  cQry += " AND D_E_L_E_T_ = ' ' "

  oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

  oStatement:SetString( 1, xFilial("CJE") )
  oStatement:SetString( 2, cParam )

  cQry := oStatement:GetFixQuery()

  cAliasQry := MPSysOpenQuery(cQry)

  FreeObj(oStatement)

Return cAliasQry

//-------------------------------------------------------------------
/*/{Protheus.doc} GravaRegistroCJE
Função que realiza a gravação de registro na tabela CJE - 
Control Config Classificador  

@author  Juliano Fernandes
@since   30/11/2023
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function GravaRegistroCJE(cParam, cConteu, cTipo, nCJERecno)

    DbSelectArea("CJE")

    If ( nCJERecno == 0 )
        RecLock("CJE", .T.)

        CJE->CJE_FILIAL := xFilial("CJE")
        CJE->CJE_PARAM  := cParam
        CJE->CJE_TIPO   := cTipo
    
    EndIf

    CJE->CJE_CONTEU := cConteu

    CJE->(MsUnlock())

    CJE->(DbCloseArea())

Return
