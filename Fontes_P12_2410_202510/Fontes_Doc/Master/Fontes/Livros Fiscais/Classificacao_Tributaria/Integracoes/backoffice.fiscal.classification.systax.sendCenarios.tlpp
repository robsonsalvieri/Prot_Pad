#include 'tlpp-core.th'
#include 'classificadorfiscal.ch'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//-------------------------------------------------------------------
/*/{Protheus.doc} FISEnviaCenario
Classe que contém as operações referente a integração com a API da 
Systax no envío de cenários
@author  Yuri Gimenes da Costa
@since   31.03.2021
@version 1.0
/*/
//-------------------------------------------------------------------
CLASS FISEnviaCenario

    data url as character
    data username as character 
    data senha as character 
    
    data rest_client as object
    data produto as character
    data rToken as character
    data error as numeric
    data error_description as character
    data regras as object
    data JSONResult as json
    data id_prod_classificador
    data aPerPart as array
    data cPerfil as character
    data cod_ncm as character
    data cod_perfil as character
    data cCodPart as character
    data aOriDest as array
    data cGrpProd as character
    data aNatOper as array
    data aFinalid as array
    data cEmpresa as character
    data jRetorno as json
    data oJEnvia as object

    Public METHOD New() CONSTRUCTOR
    Public METHOD enviaCenario(cEntSai,cEmpresa,aCodPart,aOridest,cGrpProd,aNatOper,aFinalid,lEnvia) //(nEntSai,cEmpresa,aParticipante,aOridest,cGrpProd,aNatOper,aFinalid)
    Public METHOD gravaCenario(cEntSai,cEmpresa,aCodPart,aOridest,cGrpProd,aNatOper,aFinalid)
    METHOD enviaCenarioSystax(oJEnvia) 
    METHOD divideCenarios()
    METHOD setUserName(username)
    METHOD setSenha(senha)
    METHOD setUrl(url)
    Public METHOD GetResponseMessage() as character

END CLASS

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe
@author  Erich Buttner
@since   16.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD New() CLASS FISEnviaCenario

Local aParam := {"URLPRINCIPAL","USERNAMEDEFAULT","SENHADEFAULT"}
Local cParametro := GetConfigClassif(aParam)
Local oParametro := JsonObject():New()

If oParametro:FromJson(cParametro) == Nil

    ::seturl(oParametro:URLPRINCIPAL)
    ::setSenha(oParametro:SENHADEFAULT)
    ::setUserName(oParametro:USERNAMEDEFAULT)

    FreeObj(oParametro)

    FWFreeArray(aParam)

EndIf

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} enviaCenario
Método para motar e enviar o cenário de operação 
cEmpresa -> Segmento da empresa Principal da operação
cCodPart -> Código do perfil de participante selecionado
aOriDest -> Array com os códigos de perfis de origem e destino selecionados
cGrpProd -> Código do perfil de produto selecionado
aNatOper -> Array com as naturezas de operação selecionadas
aFinalid -> Array com as finalidades selecionadas
@author  Yuri Gimenes
@since   18.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD enviaCenario(cEntSai,cEmpresa,aCodPart,aOriDest,cGrpProd,aNatOper,aFinalid,lEnvia,oJsonEnvia,cCodCena,cEmpresa_dest,cMockServer,cPath) CLASS FISEnviaCenario

Local oClasCena     as object
Local oClsVlCna     as object
Local oClassNat     as object
Local oJsonOper     as json
Local oJsonNat      as json
Local oJsonFin      as json
Local oJsonCenario  as json
Local oJsVlCena     as json
Local jSCena        as json //Utilizado para quebrar o Json de cenário //Não usa
Local jRetorno      as json
Local cJsonCena     as Character
Local cJsVlCena     as Character
Local cJsonNat      as Character
Local nY            as numeric
local cJsonFin      as Character

oClasCena     := FISCriaCenario():New()
oClsVlCna     := FISValidaCenario():New()
oClassNat     := FISgetNaturezaOperacao():New()
jRetorno      := JsonObject():New()
jSCena        := JsonObject():New()

jSCena['cenarios'] := {}

//Atribui o DEFAULT ao Array da finalidade
If ( Empty(aFinalid) )
    aFinalid := {{"0","2"}}
EndIf

DEFAULT cMockServer := ""
DEFAULT cPath := ""

If oJsonEnvia == Nil 
    if ( len(aOridest) > 0 )

        /*======================================================================================
            Inicio do bloco de verificação dos dados a serem levados para o cenário
        ======================================================================================*/      
        
        oJsonOper     := JsonObject():New()
        oJsonCenario  := JsonObject():New()
        oJsVlCena     := JsonObject():New()
        oJsonNat      := JsonObject():New()
        oJsonFin      := JsonObject():New()

        oJsonOper["operacao"] := {}

        for nY := 1 to len(aOridest)
            aadd(oJsonOper["operacao"], aOridest[nY])           
        next nY

        //Obtem os dados da Natureza de operação - FISX005C.tlpp
        cJsonNat := oClassNat:GetNaturFinCena(aNatOper)
        oJsonNat:FromJSON(cJsonNat)        

        //Obtem os dados da Natureza de Finalidade - FISX005C.tlpp
        cJsonFin := oClassNat:GetNaturFinCena(aFinalid)
        oJsonFin:FromJSON(cJsonFin)

        //Cria os cenários - FISX005A.tlpp
        cJsonCena := oClasCena:CriaCenario(Val(cEntSai),cEmpresa,cGrpProd,oJsonNat,oJsonFin,oJsonOper,cEmpresa_dest)
        oJsonCenario:FromJSON(cJsonCena)

        //Valida os cenários antes de enviá-los - FISX005D.tlpp
        cJsVlCena := oClsVlCna:FisValidaCenario(cGrpProd,aCodPart,@oJsonCenario)
        oJsVlCena:FromJSON(cJsVlCena)

        /*======================================================================================
            Fim do bloco de verificação dos dados a serem levados para o cenário
        ======================================================================================*/

        //----------------------------------------------------------------------------------|
        //Verifico se ficou algum cenário no Json oJsonCenario                              |
        //----------------------------------------------------------------------------------|
        If len(oJsonCenario['cenarios']) > 0

            //----------------------------------------------------------------------------------|
            //Divido a quantidade de cenários em blocos de 100 cenários cada                    |
            //----------------------------------------------------------------------------------|
            oJsonCenario := ::divideCenarios(oJsonCenario)
            //----------------------------------------------------------------------------------|

            If lEnvia

                jRetorno := ::enviaCenarioSystax(oJsonCenario,,aCodPart,,cGrpProd,aNatOper,aFinalid,cMockServer,cPath)             
            
            elseif !lEnvia
                

                If len(oJsonCenario['cenarios']) > 1 .and. oJsonCenario['cenarios'][1]:hasproperty('cenarios') .and.  ValType(oJsonCenario['cenarios'][1]['cenarios']) == "A"

                    jSCena := oJsonCenario

                Else

                    jSCena['cenarios'] := {}
                    aAdd(jSCena['cenarios'],oJsonCenario )

                EndIf

                jRetorno := ::gravaCenario(aCodPart,'',cGrpProd,aNatOper,aFinalid,jSCena,,,,cMockServer)
            EndIf
        else
            jRetorno["sucesso"] := .F.
            jRetorno["erro"] := STR0211 //"Já existem cenários semelhantes criados! Verifique as informações e tente novamente"
        Endif

        FreeObj(oJsonOper)
        FreeObj(oJsonCenario)            
        FreeObj(oJsVlCena)
        FreeObj(oJsonNat)
        FreeObj(oJsonFin)

    endif
Else
    jRetorno := ::enviaCenarioSystax(oJsonEnvia,cCodCena,,,,,,cMockServer,cPath)
EndIf   

cJsonRet := jRetorno:toJSON()

FreeObj(jRetorno)
FreeObj(oClassNat)
FreeObj(oClasCena)
FreeObj(oClsVlCna)
FreeObj(jSCena)

Return cJsonRet

//-------------------------------------------------------------------
/*/{Protheus.doc} enviaCenarioSystax
Método para motar e enviar o cenário de operação 
cEmpresa -> Segmento da empresa Principal da operação
cCodPart -> Código do perfil de participante selecionado
aOriDest -> Array com os códigos de perfis de origem e destino selecionados
cGrpProd -> Código do perfil de produto selecionado
aNatOper -> Array com as naturezas de operação selecionadas
aFinalid -> Array com as finalidades selecionadas
@author  Yuri Gimenes
@since   03.10.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD enviaCenarioSystax(oJsonCenario,cCodCena,aCodPart,cOriDest,cGrpProd,aNatOper,aFinalid,cMockServer,cPath) CLASS FISEnviaCenario

Local aHeaderCena   := {}
Local oResult 	    := Nil
Local jRetorno      := JsonObject():New()
Local nYB,nE     	as integer
Local cArrayTotal   as character
Local cArrayOk      as character
Local nTamIdCena    := TamSx3("CJG_IDENVI")[1]
Local cIdCena       as character
Local oCenar        as object     
Local lRet          as logical
Local cResult       as character
Local cHoraEnv      as character
Local dDtEnvio      as date
Local cJson         as character
Local cMessage      as character
Local lError        as logical

Default aCodPart    := {"","","",""}
Default cOriDest    := ""
Default cGrpProd    := ""
Default aNatOper    := {}
Default aFinalid    := {}
Default cCodCena    := ""
Default cMockServer := ""
Default cPath       := ""

self:rest_client := FwRest():New(self:url)
self:rest_client:setPath("/cadastro-geral/cenarios")

If !Empty(cMockServer)
    self:rest_client := FwRest():New(cMockServer)
    self:rest_client:setPath(cPath)
EndIf

self:rToken := AllTrim(ConsultTok(cUserName,cMockServer))

If ValType(self:rToken) <> 'U'

    Aadd(aHeaderCena, "Authorization:" + self:rToken)
    
    For nE := 1 To Len(oJsonCenario['cenarios'])

        oCenar := JsonObject():New()
        oCenar['cenarios'] := {}
        Aadd(oCenar['cenarios'], oJsonCenario['cenarios'][nE])

        if ( !Empty(cMockServer) )
            oCenar := oJsonCenario
        endif            

        self:rest_client:SetPostParams(oCenar:toJson())
        self:rest_client:SetChkStatus(.F.)

        lRet := self:rest_client:Post( aHeaderCena )

        jRetorno["sucesso"] := .F.
        jRetorno["erro"] := STR0212 //"Não foi possivel obter o Retorno do POST"

        If lRet

            cResult := self:rest_client:GetResult()

            oResult := JsonObject():New()

            If oResult:FromJson(cResult) == Nil

                If oResult:GetJsonObject("success")

                    cMessage := ''

                    //Grava CJG
                    DbSelectArea( "CJG" )
                    CJG->( DbSetOrder( 1 ) )

                    If oResult["total_ok"] > 0
                        cArrayOk := "itens_ok"
                        cArrayTotal := "total_ok"
                    ElseIf oResult["total_error"] > 0
                        cArrayOk := "itens_error"
                        cArrayTotal := "total_error"
                    EndIf

                    For nYB := 1 to oResult[cArrayTotal]

                        dDtEnvio := SToD('')
                        cHoraEnv := ''                    
                        cIdCena := ''

                        If ValType(oResult[cArrayOk]) == 'A'
                            cIdCena := cValToChar(oResult[cArrayOk][nYB]['id'])
                        EndIf

                        cMessage := DecodeUTF8(self:GetResponseMessage(oResult, nYB, @lError), 'CP1252')

                        if ( !Empty(cIdCena) .and. !lError )
                            dDtEnvio := Date()
                            cHoraEnv := Time()

                            if ( !Empty(cMockServer) )
                                dDtEnvio := SToD("20240101")
                                cHoraEnv := '12:00:00'
                            endif                            
                        endif

                        If CJG->( MsSeek( xFilial( "CJG" ) + Padr(cCodCena,nTamIdCena) ) )
                            RecLock( "CJG", .F. )

                            CJG->CJG_IDCENA := cIdCena
                            CJG->CJG_DTENV  := dDtEnvio
                            CJG->CJG_HRENV  := cHoraEnv

                            if ( CJG->(FieldPos('CJG_JSRET') > 0) )
                                CJG->CJG_JSRET := DecodeUTF8(oResult:ToJson(), 'CP1252')
                            endif

                            CJG->( MsUnLock() )
                        Else                            
                            If RecLock( "CJG", .T. )

                                cIdEnvio := GetSxeNum("CJG","CJG_IDENVI")
                                
                                CJG->CJG_FILIAL := xFilial("CJG")
                                CJG->CJG_IDENVI := cIdEnvio
                                CJG->CJG_IDPORI := cOriDest
                                CJG->CJG_IDPPRO := cGrpProd
                                CJG->CJG_IDPPAR := aCodPart[1][1]

                                If !Empty(cIdCena)
                                    CJG->CJG_IDCENA := cIdCena
                                    CJG->CJG_DTENV  := dDtEnvio
                                    CJG->CJG_HRENV  := cHoraEnv
                                EndIf

                                oJsCenario := JsonObject():New()
                                oJsCenario["cenarios"] := {}
                                aadd(oJsCenario["cenarios"], oJsonCenario['cenarios'][nYB])
                                cJson := oJsCenario:toJson()
                                FreeObj(oJsCenario)

                                CJG->CJG_JSON := cJson

                                if ( CJG->(FieldPos('CJG_JSRET') > 0) )
                                    CJG->CJG_JSRET := DecodeUTF8(oResult:ToJson(), 'CP1252')
                                endif

                                CJG->( MsUnLock() )

                                ConfirmSx8()

                            EndIf

                        EndIf

                    Next nYB

                    jRetorno["sucesso"] := Empty(cMessage)
                    jRetorno["erro"] := cMessage
                    jRetorno["data"] := Iif(!Empty(cIdCena),dDtEnvio,SToD(""))
                    jRetorno["hora"] := Iif(!Empty(cIdCena),cHoraEnv,"")
                    jRetorno["id_cenario"] := cIdCena
                    
                    CJG->(DbCloseArea())
                
                else
                    jRetorno["sucesso"] := .F.
                    jRetorno["erro"] := Iif(Empty(cMockServer),::rest_client:GetResult(),"")
                EndIf
            EndIf

            FreeObj(oResult)

        EndIf

        FreeObj(oCenar)

        if ( !Empty(cMockServer) )
            exit
        endif
    Next nE
EndIf

Return jRetorno 

//-------------------------------------------------------------------
/*/{Protheus.doc} gravaCenario
Método para motar e enviar o cenário de operação 
cEmpresa -> Segmento da empresa Principal da operação
cCodPart -> Código do perfil de participante selecionado
aOriDest -> Array com os códigos de perfis de origem e destino selecionados
cGrpProd -> Código do perfil de produto selecionado
aNatOper -> Array com as naturezas de operação selecionadas
aFinalid -> Array com as finalidades selecionadas
@author  Yuri Gimenes
@since   03.10.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD gravaCenario(aCodPart,cOriDest,cGrpProd,aNatOper,aFinalid,oJsonCena,cHoraEnv,cDtEnvio,cIdCena,cMockServer) CLASS FISEnviaCenario

Local oJsCenario    := JsonObject():New()
Local nY            as numeric
Local nYB           as numeric
Local oJsonRet      := JsonObject():New()
Local oJsonRetDt    := JsonObject():New()
Local cIdEnvio      as character
Local cJson         as character

Default cHoraEnv    := ""
Default cDtEnvio    := ""
Default cIdCena     := ""
Default cMockServer := ""

//Grava CJG
DbSelectArea( "CJG" )
CJG->( DbSetOrder( 1 ) ) //Criar um NOVO INDICE

oJsonRet['cenarios'] := {}
oJsCenario["cenarios"] := {}

For nY := 1 to len(oJsonCena['cenarios'])

    For nYB := 1 to len(oJsonCena['cenarios'][nY]['cenarios'])

        If RecLock( "CJG", .T.)

            cIdEnvio := GetSxeNum("CJG","CJG_IDENVI")
            
            CJG->CJG_FILIAL := xFilial("CJG")
            CJG->CJG_IDENVI := cIdEnvio
            CJG->CJG_IDPORI := cOriDest
            CJG->CJG_IDPPRO := cGrpProd
            CJG->CJG_IDPPAR := aCodPart[1][1]

            aadd(oJsCenario["cenarios"],oJsonCena["cenarios"][nY]['cenarios'][nYB])

            cJson := oJsCenario:toJson()

            aSize(oJsCenario["cenarios"],0)

            CJG->CJG_JSON := cJson

            CJG->( MsUnLock() )

            ConfirmSx8()

            oJsonRetDt['id_cenario'] := Iif(Empty(cMockServer),cIdEnvio, "" )

            aadd(oJsonRet['cenarios'], oJsonRetDt)

        EndIf

    Next nYB

Next nY

If len(oJsonRet['cenarios']) > 0
    oJsonRet['sucesso'] := .T.
EndIf

CJG->(DbCloseArea())

Return oJsonRet

//-------------------------------------------------------------------
/*/{Protheus.doc} divideCenarios
Método para dividir os cenários em blocos de 100 cenários cada

@author  Yuri Gimenes
@since   07.10.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD divideCenarios(oJsonCenario) CLASS FISEnviaCenario

Local oJsCenario    := JsonObject():New()
Local nY            as numeric
Local oJsonRet      := JsonObject():New()

oJsCenario['cenarios']  := {}
oJsonRet['cenarios']    := {}

For nY := 1 to len(oJsonCenario['cenarios'])

    aadd(oJsonRet['cenarios'], oJsonCenario['cenarios'][nY])

    If len(oJsonRet['cenarios']) == 99 .Or. Len(oJsonCenario['cenarios']) <= 99
    
        aadd(oJsCenario['cenarios'], oJsonRet)

        FREEOBJ( oJsonRet )
        oJsonRet := JsonObject():New()
        oJsonRet['cenarios']       := {}

    EndIf

Next nY

//Verifico se restou alguma coisa o oJsonRet e incluo com os demais
If len(oJsonRet['cenarios']) > 0
    aadd(oJsCenario['cenarios'], oJsonRet)
Else
    oJsCenario :=  oJsonCenario
EndIf

Return oJsCenario

//-------------------------------------------------------------------
/*/{Protheus.doc} setUserName
Método para passar o nome do usuário que irá utilizar a api.
@author  Erich Buttner
@since   16.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method setUserName(username) Class FISEnviaCenario

    Self:username := username

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} setSenha
Método para passar a senha do usuário que irá utilizar a api.
@author  Erich Buttner
@since   16.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method setSenha(senha) Class FISEnviaCenario

    Self:senha := senha

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} setUrl
Método para passar a senha do usuário que irá utilizar a api.
@author  Erich Buttner
@since   16.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method setUrl(Url) Class FISEnviaCenario

    Self:Url := Url

Return

/*/{Protheus.doc } Method GetResponseMessage
    Método responsável por obter a mensagem da Systax ao enviar um cenário

    @type method
    @version P12.1.2410
    @author Juliano Fernandes
    @since 08/08/2024
    @param jResponse, json, Json com a resposta da Systax no envio de cenário
    @param nPos, numeric, Posição do array que deseja obter a mensagem
/*/
method GetResponseMessage(jResponse as json, nPos as numeric, lError as logical) as character class FISEnviaCenario

    local cMessage as character
    local jResponseSystax as json
    local lDuplicity as logical

    default nPos := 1
    default lError := .F.

    lError := .F.

    if ( jResponse:HasProperty('itens_ok') .and. Len(jResponse['itens_ok']) > 0 )
        jResponseSystax := jResponse['itens_ok'][nPos]
    elseif ( jResponse:HasProperty('itens_error') .and. Len(jResponse['itens_error']) > 0 )
        jResponseSystax := jResponse['itens_error'][nPos]

        lDuplicity := jResponseSystax:HasProperty('message') .and. jResponseSystax['message'] == 'duplicity'

        lError := !lDuplicity
    endif

    if ( ValType(jResponseSystax) == 'J' .and. jResponseSystax:HasProperty('validacao_parametros_cenario') )
        if ( jResponseSystax['validacao_parametros_cenario']:HasProperty('message') )
            cMessage := jResponseSystax['validacao_parametros_cenario']['message']
        endif
    endif 

return cMessage
