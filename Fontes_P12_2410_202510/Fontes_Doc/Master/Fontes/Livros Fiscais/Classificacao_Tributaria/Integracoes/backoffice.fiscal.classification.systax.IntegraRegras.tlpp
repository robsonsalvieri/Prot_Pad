#include 'tlpp-core.th'
#include 'tlpp-rest.th'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//----------------------------------------------------------------------------------------------------
/*/{Protheus.doc} GetRegrasSystax
Classe que contém os Gets para serem utilizados na tela de Retorno das Regras Systax

@author  Thiago Moreira
@since   06.04.2023
@version 1.0
/*/
//-----------------------------------------------------------------------------------------------------
Class IntegraRegras

    data cMockServer as character
    data cEndPoint as character
    
    public method new() constructor

    @GET("/GetRegrasSystax")    
    public method getRegrasSystax()

End Class

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe

@author  Thiago Moreira
@since   06.04.2023
@version 1.0
/*/
//-------------------------------------------------------------------
method new() class IntegraRegras
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getRegrasSystax()
Método para retornar as regras da Systax

        
@author  Thiago Moreira
@since   06.04.2023
@version 1.0
/*/
//-------------------------------------------------------------------
method getRegrasSystax() class IntegraRegras

    Local jRetRegras as json
    Local oRetRegra as object
    Local jQueryParams as json
    Local jHeaderResponse as json

    jQueryParams := oRest:GetQueryRequest()

	If ( jQueryParams:HasProperty('mockserver') )
		self:cMockServer := jQueryParams['mockserver']

        If ( jQueryParams:HasProperty('endpoint') )
            self:cEndPoint := jQueryParams['endpoint']
        EndIf

        If ( jQueryParams:HasProperty('database') )
            dDataBase := SToD(jQueryParams['database'])
        EndIf
    EndIf

	jHeaderResponse := JsonObject():New()
	jHeaderResponse['Content-Type'] := 'application/json;charset=cp1252'
	oRest:SetHeaderResponse(jHeaderResponse)
    FreeObj(jHeaderResponse)

    oRetRegra := FISRetornaRegra():New()

    jRetRegras := oRetRegra:FISRetornaRegra(,,,,self:cMockServer,self:cEndPoint)

    FreeObj(oRetRegra)
    oRetRegra := Nil

Return oRest:setResponse(jRetRegras)
