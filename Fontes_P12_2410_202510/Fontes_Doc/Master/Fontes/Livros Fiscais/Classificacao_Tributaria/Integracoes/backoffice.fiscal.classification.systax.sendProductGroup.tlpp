#INCLUDE "protheus.ch"
#INCLUDE "topconn.ch"
#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "classificadorfiscal.ch"

#DEFINE DEFAULT_REQUEST_TIMEOUT 120
#DEFINE DEFAULT_RETRY_ATTEMPTS  3
#DEFINE STATUS_CODE_BAD_REQUEST 400

Namespace totvs.protheus.backoffice.fiscal.Classifier
using Namespace totvs.protheus.backoffice.fiscal.lib

/*/{Protheus.doc} ProductGroup
Classe responsável por enviar o grupo de produtos que deve ser associado ao cenário já existente
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Class SendProductGroup

    Public Data cError      as character
    Public Data iStatusCode as integer

    Private Data aHeader                as array
    Private Data cUrl                   as character
    Private Data cUserName              as character
    Private Data cPassword              as character
    Private Data cAccessToken           as character
    Private Data iRequestTimeOut        as integer
    Private Data iRetryAttempts         as integer
    Private Data lGrvLogEnvProdSystax   as logical

    Private Data cMockServer            as character
    Private Data cPath                  as character

    Public Method New() Constructor

    Public Method SetMockServer()
    Public Method SetPath()    

    Public Method SetUrl(cUrl)
    Public Method SetPassword(cPassword)
    Public Method SetUserName(cUsername)
    Public Method SetRequestTimeOut(iTimeOut)
    Public Method SetAccessToken(cAccessToken)
    Public Method SetRequestHeader(aHeader)
    Public Method SetRetryAttempts(iAttempts)
    Public Method SetGrvLogEnvProdSystax(lGrvLogEnvProdSystax)
    Public Method PostAssociationDisassociation(jBody) as logical

    Private Method LoadParameters()

End Class

/*/{Protheus.doc} New
Método construtor da classe
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method New() Class SendProductGroup

    ::LoadParameters()

Return

/*/{Protheus.doc} SetMockServer
Método responsável por definir o mockserver
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method SetMockServer(cMockServer as character) Class SendProductGroup

    Default cMockServer := ''

    ::cMockServer := cMockServer

Return

/*/{Protheus.doc} SetMockServer
Método responsável por definir path para o mockserver
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method SetPath(cPath as character) Class SendProductGroup

    Default cPath := ''

    ::cPath := cPath

Return

/*/{Protheus.doc} SetMockServer
Método responsável por carregar todos os parâmetros necessários para a ulilização da classe
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method LoadParameters() Class SendProductGroup

    Local cParameters := GetConfigClassif({"URLPRINCIPAL", "USERNAMEDEFAULT", "SENHADEFAULT", "GRVLOGENVPRODSYSTAX"})
    Local oParameters := JsonObject():New()

    If oParameters:FromJson(cParameters) == Nil
        ::SetUrl(oParameters:URLPRINCIPAL)
        ::SetPassword(oParameters:SENHADEFAULT)
        ::SetUserName(oParameters:USERNAMEDEFAULT)
        ::SetGrvLogEnvProdSystax(&(oParameters:GRVLOGENVPRODSYSTAX))
    EndIf

    ::SetRequestTimeOut()
    ::SetRetryAttempts()
    ::SetAccessToken()
    ::SetRequestHeader()

    FwFreeObj(oParameters)

Return

/*/{Protheus.doc} PostAssociationDisassociation
Método responsável por Associar / desassociar o grupo de produtos ao cenário já existente
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method PostAssociationDisassociation(jBody as json) as logical Class SendProductGroup
    
    Local aError    as array
    Local iX        as integer
    Local jResponse as json
    Local oRestProd as object
    Local lRet      as logical

    Default jBody := Nil

    oRestProd           := FwRest():New(::cUrl)
    oRestProd:nTimeOut  := ::iRequestTimeOut

    oRestProd:SetPath("/cadastro-geral/cenarios/grupos/operations-assoc")

    If !Empty(::cMockServer)
        oRestProd           := FwRest():New(::cMockServer)
        oRestProd:nTimeOut  := ::iRequestTimeOut

        oRestProd:SetPath(::cPath)

        If ( ::cPath == '/endpoint-invalido' )
            oRestProd:nTimeOut := 10 // 10 segundos
        EndIf
    EndIf

    oRestProd:SetChkStatus(.F.)
    oRestProd:SetPostParams(jBody:ToJson())    

    For iX := 1 To ::iRetryAttempts
        If !lRet
            lRet := oRestProd:Post(::aHeader)

            If !lRet
                If iX == ::iRetryAttempts
                    ::cError        := oRestProd:cInternalError
                    ::iStatusCode   := Val(oRestProd:GetHTTPCode())
                    Return .F.
                EndIf
                
                aError := { {'', 'Classificacao Tributaria - sendProductGroup.tlpp - PostAssociationDisassociation'},;
                            {'', 'Error: ' + oRestProd:cInternalError},;
                            {'', 'Retrying in 5 seconds... Attempt: ' + CValToChar(iX)} }

                FWLogMsg("INFO",, "PostAssociationDisassociation",,,,,,, aError)
                Sleep(5000)
            EndIf
        EndIf
    Next

    jResponse := JsonObject():New()

    If jResponse:FromJson(oRestProd:GetResult()) == Nil
        If jResponse:HasProperty("success") .And. !jResponse["success"]
            ::iStatusCode   := STATUS_CODE_BAD_REQUEST
            ::cError        := STR0314 // "Falha na requisição"
            
            If jResponse:HasProperty("message")
                ::cError := jResponse["message"]
            EndIf

            Return .F.
        EndIf
    EndIf

Return .T.

/*/{Protheus.doc} SetRequestHeader
Método responsável por definir o header da requisição
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method SetRequestHeader(aHeader as array) Class SendProductGroup

    Default aHeader := {    "Content-Type: application/json; charset=UTF-8",;
                            "User-Agent: Chrome/65.0 (compatible; Protheus " + GetBuild() + ")",;
                            "Authorization: Bearer " + ::cAccessToken  }

    ::aHeader := aHeader

Return

/*/{Protheus.doc} SetAccessToken
Método responsável por definir o token da requisição
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method SetAccessToken(cAccessToken as character) Class SendProductGroup

    Default cAccessToken := AllTrim(ConsultTok(::cUserName))

    ::cAccessToken := cAccessToken

Return

/*/{Protheus.doc} SetUserName
Método responsável por definir o username da requisição
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method SetUserName(cUserName as character) Class SendProductGroup

    Default cUserName := "" 

    ::cUsername := cUserName

Return

/*/{Protheus.doc} SetRetryAttempts
Método responsável por definir a quantidade de tentativas da requisição
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method SetRetryAttempts(iAttempts as integer) Class SendProductGroup

    Default iAttempts := DEFAULT_RETRY_ATTEMPTS 

    ::iRetryAttempts := iAttempts

Return

/*/{Protheus.doc} SetRequestTimeOut
Método responsável por definir o timeout da requisição
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method SetRequestTimeOut(iTimeOut as integer) Class SendProductGroup

    Default iTimeOut := DEFAULT_REQUEST_TIMEOUT 

    ::iRequestTimeOut := iTimeOut

Return

/*/{Protheus.doc} SetPassword
Método responsável por definir o password da requisição
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method SetPassword(cPassword as character) Class SendProductGroup

    Default cPassword := "" 

    ::cPassword := cPassword

Return

/*/{Protheus.doc} SetUrl
Método responsável por definir a url base da requisição
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method SetUrl(cUrl as character) Class SendProductGroup

    Default cUrl := "" 

    ::cUrl := cUrl

Return

/*/{Protheus.doc} SetUrl
Método responsável por definir a gravação de log
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method setGrvLogEnvProdSystax(lGrvLogEnvProdSystax as logical) Class SendProductGroup

    ::lGrvLogEnvProdSystax := lGrvLogEnvProdSystax

Return
