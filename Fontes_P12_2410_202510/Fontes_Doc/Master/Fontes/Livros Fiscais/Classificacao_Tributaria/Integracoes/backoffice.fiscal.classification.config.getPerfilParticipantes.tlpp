#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'
#include 'tlpp-rest.th'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//---------------------------------------------------------------------------------------
/*/{Protheus.doc} FisGetParticipante
Classe que contém as operações para obter os dados de todos os participantes da Filial
@author  Yuri Gimenes da Costa
@since   07.07.2022
@version 1.0
/*/
//---------------------------------------------------------------------------------------

Class FisGetParticipante

    data jQuery as json

	Public METHOD New() CONSTRUCTOR

	@Get("/getParticipante")
	Public METHOD getParticipante()

End Class

//-------------------------------------------------------------------
/*/{Protheus.doc} new()
Método construtor da classe
@author  Yuri Gimenes da Costa
@since   07.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------

METHOD new() Class FisGetParticipante


Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getParticipante
Método para obter os dados dos participantes e coloca-los em arquivo Json

@Obs    Passar via QueryParam a filial logada em sistema

@author  Yuri Gimenes da Costa
@since   07.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------

METHOD getParticipante() Class FisGetParticipante

Local cJson      as character
Local cAliasPart as character
Local jQuery     as object

jQuery := oRest:getQueryRequest()

//Realiza query dos perfis
cAliasPart := consultaPerfis(jQuery)

//Transforma dados os perfis em string com formato json
cJson := retornaParticipantesEmJson(jQuery, cAliasPart)

FreeObj(jQuery)

Return oRest:setResponse(cJson)

/*/{Protheus.doc} criaFiltroPerfis
    Cria filtro de perfis a serem consultados
    @type  Static Function
    @author Rafael
    @since 29/11/2022
    @version 12.1.2210
    @param jQuery, json, Json da requisição 
    @return array, Retorna array contendo tipo, perfil e uf a serem filtrados    
/*/
Static Function criaFiltroPerfis(jQuery) as ARRAY
Local cfilGroup as character
Local cTipo     as character
Local cPerfil   as character
Local cUF       as character
Local cFilter   as character
Local aAux      as array
Local nx        as numeric

cfilGroup  := IIF(jQuery[ 'filGroup' ] <> Nil .And. !( '*' $ jQuery[ 'filGroup' ]) , jQuery[ 'filGroup' ], '' )
aAux       := StrTokArr2(cfilGroup, ',' )

for nX := 1 To Len(aAux)
    If ('TIPO:' $ Upper(aAux[nX]) )
        cTipo += IIF(Empty(cTipo), '',',') + AllTrim(StrTran(Upper(aAux[nX]),'TIPO:',''))
        If !Empty(cTipo)
            cTipo := StrTran(cTipo,'CLIENTE','2')
            cTipo := StrTran(cTipo,'FORNECEDOR','1')
        EndIf //tratar para mudar cliente para 2 e fornecedor para 1
    ElseIf ('PERFIL:' $ Upper(aAux[nX]))
        cPerfil += IIF(Empty(cPerfil), '',',') + AllTrim(StrTran(Upper(aAux[nX]),'PERFIL:',''))
    ElseIf ('UF:' $ Upper(aAux[nX]))
        cUF += IIF(Empty(cUF), '',',') + AllTrim(StrTran(Upper(aAux[nX]),'UF:',''))
    Else
        If ( !Empty(aAux[nX]) )
            cFilter := '%' + aAux[nX] + '%'
        EndIf
    EndIf
Next

Return {cTipo, cPerfil, cUF, cFilter}

/*/{Protheus.doc} consultaPerfis
    Realiza consulta dos perfis de participantes 
    @type  Static Function
    @author Rafael
    @since 29/11/2022
    @version version    
    @param jQuery , Json, parametros enviados na requisição
    
    @return cAliasPart, character, Retorna a alias aberto pela consulta
    
/*/
Static Function consultaPerfis(jQuery) as character
Local aFiltro_perfis as array
Local aPaginas       as array
Local cTipo          as character
Local cPerfil        as character
Local cUF            as character
Local cFilter        as character
Local nRegIni        as numeric
Local nRegFim        as numeric
Local cAliasPart     as character

    //Processa filtro para os perfis
    aFiltro_perfis := criaFiltroPerfis(jQuery)
    cTipo          := aFiltro_perfis[1]
    cPerfil        := aFiltro_perfis[2]
    cUF            := aFiltro_perfis[3]
    cFilter        := aFiltro_perfis[4]

    //Processa itens e paginas
    aPaginas := gerarPaginas(jQuery)
    nRegIni  := aPaginas[1]
    nRegFim  := aPaginas[2] + 1 //Soma mais um registro para retorno do HasNext

    cAliasPart := QueryPerfisParticipantes(cTipo, cPerfil, cUF, cFilter, nRegIni, nRegFim)

    FWFreeArray(aFiltro_perfis)
    FWFreeArray(aPaginas)

Return cAliasPart


/*/{Protheus.doc} retornaParticipantesEmJson
    Transforma dados os perfis em string com formato json
    @type  Static Function
    @author Rafael
    @since 29/11/2022
    @version version
    @param jQuery , Json, parametros enviados na requisição
    @param cAliasPart, string, Alias a ser processado
    @return cJson, string, string com formato json 
/*/
Static Function retornaParticipantesEmJson(jQuery ,cAliasPart as character) as character
Local oJBody     as object
Local oJsAcumula as object
Local lOk        as logical
Local lhasNext   as logical
Local nCount     as numeric
Local nRegFim    as numeric
Local cJson      as character

    oJBody        := JsonObject():New()   
    oJsAcumula    := JsonObject():New()
    nRegFim       := gerarPaginas(jQuery)[2]
        
    oJBody["participantes"] := ""
    oJsAcumula 				:= {}

    While (cAliasPart)->(!EoF())

        //Verifica se possui mais registros a serem processados do que será impresso para controle do hasNext
        nCount ++
        IF nCount > nRegFim
            lhasNext := .T.
            Exit
        Endif

        oJBodyDt := ProcessaCorpoMensagem(cAliasPart)
        
        aadd(oJsAcumula,oJBodyDt)
            
        lOk := .T.
       
        ( cAliasPart )->(DbSkip())

        //Destroi Objeto
        FREEOBJ( oJBodyDt )        
    End

    ( cAliasPart )->( DBCloseArea() )

    oJBody["participantes"] := oJsAcumula

    oJBody["success"] := lOk
    oJBody["hasNext"] := lhasNext

    cJson := oJBody:toJson()
    FREEOBJ( oJBody )
    FREEOBJ( oJsAcumula )

Return cJson

/*/{Protheus.doc} gerarPaginas
    Trata itens e paginas a serem exibidos
    @type  Static Function
    @author Rafael
    @since 29/11/2022
    @version version
    @param 
    jQuery, json, parametros enviados na requisição    

    @return Retorna array com numero inicial e final de itens por pagina    
/*/
Static Function gerarPaginas(jQuery) as array
Local nPage     as numeric
Local nPageSize as numeric
Local nRegIni   as numeric
Local nRegFim   as numeric

nPage     := IIF(jQuery:HasProperty('page')    ,Val(jQuery[ 'page' ])    , 1 )
nPageSize := IIF(jQuery:HasProperty('pageSize'),Val(jQuery[ 'pageSize' ]), 15 )

nRegIni     := ( ( nPage - 1 ) * nPageSize ) + 1
nRegFim     := (nPage * nPageSize)

Return {nRegIni, nRegFim}


/*/{Protheus.doc} nomeStaticFunction
    (long_description)
    @type  Static Function
    @author user
    @since 29/11/2022
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    @example
    (examples)
    @see (links_or_references)
/*/
Static Function ProcessaCorpoMensagem(cAliasPart) as object
Local oJBodyDt      as object
Local oJDetalhes    as object
Local cCodPerfil    := (cAliasPart)->F20_CODIGO
Local cADetPart     as character

    oJBodyDt := JsonObject():New()
    oJBodyDt["perfil"] 	    := (cAliasPart)->F20_CODIGO
    oJBodyDt["desc_perfil"] := AllTrim((cAliasPart)->F20_DESC)
    oJBodyDt["detalhes"]    := {}

    cADetPart := DetPerfPart(cCodPerfil)

    While (cADetPart)->(!EoF())

        oJDetalhes := JsonObject():New()
        oJDetalhes["clifor"]    := (cADetPart)->F22_CLIFOR
        oJDetalhes["loja"] 	    := (cADetPart)->F22_LOJA
        oJDetalhes["tipo"] 	    := IIF( AllTrim((cADetPart)->F22_TPPART) == '1','FORNECEDOR', 'CLIENTE' )
        oJDetalhes["razsoc"]    := Alltrim( (cADetPart)->RAZAO_SOCIAL )
        oJDetalhes["uf"] 	    := (cADetPart)->UF

        aadd(oJBodyDt["detalhes"], oJDetalhes )
        
        FREEOBJ( oJDetalhes )

        (cADetPart)->(DbSkip())
    
    EndDo

    ( cADetPart )->( DBCloseArea() )

Return oJBodyDt

/*/{Protheus.doc} DetPerfPart
    (long_description)
    @type  Static Function
    @author Erich Buttner
    @since 31/08/2023
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    @example
    (examples)
    @see (links_or_references)
/*/
Static Function DetPerfPart(cCodPerfil)

Local cQry as character
Local cADetPart as character
Local cTpPartFornecedor as character
Local cTpPartCliente as character

// F22_TPPART: 1=Fornecedor;2=Cliente
cTpPartFornecedor := '1'
cTpPartCliente := '2'

cQry += " SELECT F22.F22_CLIFOR, F22.F22_LOJA, F22.F22_TPPART, SA2.A2_NOME AS RAZAO_SOCIAL, SA2.A2_EST AS UF "
cQry += " FROM " + RetSqlName("F22") + " F22 "
cQry += "   INNER JOIN " + RetSqlName("SA2") + " SA2 "
cQry += "       ON  SA2.A2_FILIAL = ? "
cQry += "       AND SA2.A2_COD = F22.F22_CLIFOR "
cQry += "       AND SA2.A2_LOJA = F22.F22_LOJA "
cQry += "       AND SA2.D_E_L_E_T_ = ' ' "
cQry += " WHERE F22.F22_FILIAL = ? "
cQry += "   AND F22.F22_CODIGO = ? "
cQry += "   AND F22.F22_TPPART = ? "
cQry += "   AND F22.D_E_L_E_T_ = ' ' "
cQry += " GROUP BY F22.F22_CLIFOR, F22.F22_LOJA, F22.F22_TPPART, SA2.A2_NOME, SA2.A2_EST "
cQry += " UNION "
cQry += " SELECT F22.F22_CLIFOR, F22.F22_LOJA, F22.F22_TPPART, SA1.A1_NOME AS RAZAO_SOCIAL, SA1.A1_EST AS UF "
cQry += " FROM " + RetSqlName("F22") + " F22 "
cQry += "   INNER JOIN " + RetSqlName("SA1") + " SA1 "
cQry += "       ON  SA1.A1_FILIAL = ? "
cQry += "       AND SA1.A1_COD = F22.F22_CLIFOR "
cQry += "       AND SA1.A1_LOJA = F22.F22_LOJA "
cQry += "       AND SA1.D_E_L_E_T_ = ' ' "
cQry += " WHERE F22.F22_FILIAL = ? "
cQry += "   AND F22.F22_CODIGO = ? "
cQry += "   AND F22.F22_TPPART = ? "
cQry += "   AND F22.D_E_L_E_T_ = ' ' "
cQry += " GROUP BY F22.F22_CLIFOR, F22.F22_LOJA, F22.F22_TPPART, SA1.A1_NOME, SA1.A1_EST "

cQry := ChangeQuery(cQry)

oQryTmp := FwExecStatement():New(cQry)
oQryTmp:SetString(1, FwxFilial("SA2"))
oQryTmp:SetString(2, FwxFilial("F22"))
oQryTmp:SetString(3, cCodPerfil)
oQryTmp:SetString(4, cTpPartFornecedor)
oQryTmp:SetString(5, FwxFilial("SA1"))
oQryTmp:SetString(6, FwxFilial("F22"))
oQryTmp:SetString(7, cCodPerfil)
oQryTmp:SetString(8, cTpPartCliente)

cADetPart := oQryTmp:OpenAlias(GetNextAlias())

Return cADetPart

/*/{Protheus.doc} QueryPerfisParticipantes
Monta a query conforme os filtros simples e avançados informados pelo usuário na tela de Perfis Participantes

@type  Static Function
@author Juliano Fernandes
@since 17/11/2023
@version 1.0
@param cTipo, character, Tipo (1 = Fornecedor, 2 = Cliente)
@param cPerfil, character, Códigos de perfis
@param cUF, character, Siglas dos Estados
@param cFilter, character, Filtro simples
@param nRegIni, numeric, Registro inicial
@param nRegFim, numeric, Registro final
@return cAliasQuery, character, Alias com os registros localizados
/*/
Static Function QueryPerfisParticipantes(cTipo as character, cPerfil as character, cUF as character, cFilter as character, nRegIni as numeric, nRegFim as numeric) as character

    Local aBind         as array
    Local aPerfil       as array
    Local aUF           as array
    Local cQry          as character
    Local cAliasQuery   as character
    Local lCliente      as logical
    Local lFornecedor   as logical
    Local nI            as numeric
    Local oQryTmp       as object

    lFornecedor := ( Empty(cTipo) .Or. "1" $ cTipo )
    lCliente := ( Empty(cTipo) .Or. "2" $ cTipo )

    If ( !Empty(cPerfil) )
        aPerfil := StrTokArr(cPerfil, ",")
    EndIf

    If ( !Empty(cUF) )
        aUF := StrTokArr(cUF, ",")
    EndIf

    cQry += " SELECT LINE_NUMBER, F20_CODIGO, F20_DESC FROM (SELECT ROW_NUMBER() OVER( ORDER BY F20_CODIGO ) "
    cQry += " 	LINE_NUMBER, F20_CODIGO, F20_DESC "
    cQry += " 	FROM ( "
    cQry += "           SELECT F20.F20_CODIGO, F20.F20_DESC "
    cQry += "           FROM " + RetSqlName("F20") + " F20 "
    cQry += "           	INNER JOIN " + RetSqlName("F22") + " F22 ON F22.F22_FILIAL = ? AND F22.F22_CODIGO = F20.F20_CODIGO AND F22.F22_CLIFOR <> 'TODOS' AND F22.D_E_L_E_T_ = ' ' "
    cQry += "           	LEFT JOIN " + RetSqlName("SA1") + " SA1 ON SA1.A1_FILIAL = ? AND SA1.A1_COD = F22.F22_CLIFOR AND SA1.A1_LOJA = F22.F22_LOJA AND SA1.D_E_L_E_T_ = ' ' AND F22.F22_TPPART = '2' "
    cQry += "           	LEFT JOIN " + RetSqlName("SA2") + " SA2 ON SA2.A2_FILIAL = ? AND SA2.A2_COD = F22.F22_CLIFOR AND SA2.A2_LOJA = F22.F22_LOJA AND SA2.D_E_L_E_T_ = ' ' AND F22.F22_TPPART = '1' "
    cQry += "           WHERE F20.F20_FILIAL = ? "
    cQry += "               AND F20.F20_TIPO = '02' "

    aBind := {}
    Aadd(aBind, FWxFilial("F22"))
    Aadd(aBind, FWxFilial("SA1"))
    Aadd(aBind, FWxFilial("SA2"))
    Aadd(aBind, FWxFilial("F20"))

    If ( lCliente .And. !lFornecedor )
        cQry += "           AND SA1.A1_COD IS NOT NULL "
    EndIf

    If ( lFornecedor .And. !lCliente )
        cQry += "           AND SA2.A2_COD IS NOT NULL "
    EndIf

    If ( !Empty(cFilter) )
        cQry  += " 		    AND ( UPPER(F20.F20_CODIGO) LIKE ? OR UPPER(F20.F20_DESC) LIKE ? ) "

        Aadd(aBind, Upper(cFilter))
        Aadd(aBind, Upper(cFilter))
    EndIf

    If ( !Empty(cPerfil) )
        cQry += " 		    AND F20.F20_CODIGO IN ( ? ) "

        Aadd(aBind, aPerfil)
    EndIf

    If ( !Empty(cUF) )
        cQry += "           AND ( SA1.A1_EST IN ( ? ) OR SA2.A2_EST IN ( ? ) ) "        

        Aadd(aBind, aUF)
        Aadd(aBind, aUF)
    EndIf

    cQry += "               AND F20.D_E_L_E_T_ = ' ' "
    cQry += "           GROUP BY F20.F20_CODIGO, F20.F20_DESC "
    cQry += " 	) TAB_TEMP "
    cQry += " 	WHERE F20_CODIGO <> '' "
    cQry += " ) TAB WHERE LINE_NUMBER BETWEEN ? AND ? "
    cQry += " GROUP BY LINE_NUMBER, F20_CODIGO, F20_DESC "

    Aadd(aBind, nRegIni)
    Aadd(aBind, nRegFim)

    cQry := ChangeQuery(cQry)

    oQryTmp := FwExecStatement():New(cQry)

    For nI := 1 To Len(aBind)
        Do Case
            Case ValType(aBind[nI]) == "C"
                oQryTmp:SetString(nI, aBind[nI])
            Case ValType(aBind[nI]) == "N"
                oQryTmp:SetNumeric(nI, aBind[nI])
            Case ValType(aBind[nI]) == "A"
                oQryTmp:SetIn(nI, aBind[nI])
        EndCase
    Next nI

	cAliasQuery := oQryTmp:OpenAlias(GetNextAlias())  

    FreeObj(oQryTmp)

    FWFreeArray(aBind)
    FWFreeArray(aPerfil)
    FWFreeArray(aUF)

Return cAliasQuery
