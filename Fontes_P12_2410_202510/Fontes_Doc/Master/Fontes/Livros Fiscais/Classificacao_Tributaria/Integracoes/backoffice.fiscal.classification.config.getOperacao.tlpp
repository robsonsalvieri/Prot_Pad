#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#INCLUDE "msobject.ch"
#include "tlpp-core.th"
#include 'tlpp-rest.th'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//-------------------------------------------------------------------
/*/{Protheus.doc} FISObtPerfilOperacao
Classe que contém a operação para obter os dados do perfil de origem e destino
utilizado na montagem do Json de cenário a ser enviado para a Systax
@author  Yuri Gimenes da Costa
@since   30.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
CLASS FISObtPerfilOperacao

    data aPerfil        as array
    data cPerfil        as character
    data oJsonOp        as object

    data oStatementOper as object

    PUBLIC METHOD New() CONSTRUCTOR
    PUBLIC METHOD GetPfOper(aPerfil)
    METHOD GetOper(aPerfil)
    
    METHOD SetStatementOper()

END CLASS

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe
@author  Yuri Gimenes
@since   30.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD New() CLASS FISObtPerfilOperacao

self:aPerfil := {}
self:cPerfil := ""
self:oJsonOp := JsonObject():New()

Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} GetPfPart()
Método para obter os dados de origem e destino (UF x UF)
@author  Yuri Gimenes
@since   30.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD GetPfOper(aPerfil) CLASS FISObtPerfilOperacao

Local nY as numeric
Local cJson

For nY := 1 to Len(aPerfil)
    
    self:oJsonOp := ::GetOper(aPerfil[nY]) 

Next nY

cJson := self:oJsonOp:toJSON( )

FreeObj(::oStatementOper)

Return cJson

//-------------------------------------------------------------------
/*/{Protheus.doc} GetOper
Método que faz a busca dos dados do perfil de origem e destino
@author  Yuri Gimenes
@since   30.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD GetOper(aPerfil) CLASS FISObtPerfilOperacao

Local oJson         as object
Local oJsonDt       as object

If ( ::oStatementOper == Nil )
    ::SetStatementOper()
EndIf

::oStatementOper:setString(1, xFilial("F20"))
::oStatementOper:setString(2, xFilial("F21"))
::oStatementOper:setString(3, aPerfil[1])

cQry := ::oStatementOper:getFixQuery()

cAliasPart := MPSysOpenQuery(cQry)

oJson := JsonObject():New()
oJson["perfil"]   := (cAliasPart)->F20_CODIGO
oJson["descricao"]:= (cAliasPart)->F20_DESC
oJson["operacao"] := {}

While (cAliasPart)->(!EoF())

    oJsonDt := JsonObject():New()

    oJsonDt["origem"]  := (cAliasPart)->F21_UFORI
    oJsonDt["destino"] := (cAliasPart)->F21_UFDEST

    aadd(oJson["operacao"],oJsonDt)

    FreeObj(oJsonDt)   

    (cAliasPart)->(DbSkip())

End

(cAliasPart)->(DbCloseArea())

Return oJson

//-------------------------------------------------------------------
/*/{Protheus.doc} SetStatementOper
Método para obter o statement da consulta GetOper
@author  Juliano Fernandes
@since   06.12.2023
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD SetStatementOper() CLASS FISObtPerfilOperacao

    Local cQry as character

    cQry := " SELECT F20_CODIGO, F20_DESC, F21_UFORI, F21_UFDEST "
    cQry += " FROM " + RetSqlName("F21") + " F21 "
    cQry += "   INNER JOIN " + RetSqlName("F20") + " F20 ON F20_CODIGO = F21_CODIGO AND F20_TIPO = F21_TIPOPF AND F20_FILIAL = ? AND F21.D_E_L_E_T_ = ' ' "
    cQry += " WHERE F21_FILIAL = ? "
    cQry += "   AND F21_CODIGO = ? "
    cQry += "   AND F21.D_E_L_E_T_ = ' ' "
    cQry += " GROUP BY F20_CODIGO, F20_DESC, F21_UFORI, F21_UFDEST "

    Self:oStatementOper := FWPreparedStatement():New( ChangeQuery(cQry) )

Return
