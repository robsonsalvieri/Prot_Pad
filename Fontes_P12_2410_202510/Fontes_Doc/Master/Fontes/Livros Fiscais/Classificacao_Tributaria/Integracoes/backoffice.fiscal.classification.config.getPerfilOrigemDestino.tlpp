#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'
#include 'tlpp-rest.th'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//-------------------------------------------------------------------
/*/{Protheus.doc} FisGetOrigemDestino
Classe que contém get do perfil de origem x Destino
da regra da Systax

@author  Yuri Gimenes da Costa
@since   07.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Class FisGetOrigemDestino
    public method new() constructor

    @Get("/GetOrigemDestino")    
    public method GetOrigemDestino()
End Class

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe
@author  Yuri Gimenes da Costa
@since   07.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method new() class FisGetOrigemDestino

return

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método para obter os dados das Origens e Destinos e coloca-los em arquivo Json

@Obs    Passar via QueryParam a filial logada em sistema

@author  Yuri Gimenes da Costa
@since   07.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method GetOrigemDestino() class FisGetOrigemDestino

Local cQry          as character
Local filGroup      as character
Local cUfOrigem     as character
Local cUfDestino    as character
Local oJBody        as object
Local oJBodyDt      as object
Local cJson         as character
Local cQryDetail    as character
Local lOk           as logical
Local jQuery        as object
Local nPage         as numeric
Local nPageSize     as numeric
Local nRegIni       as numeric
Local nRegFim       as numeric
Local nX            as numeric
Local oStmDetail    as object
Local aAux          as array
Local cFilter       as character
Local oStatement    as object
Local cAliasOriDest as character
Local nCount        as integer
Local aUfOrigem     as array
Local aUfDestino    as array
Local nIndex        as integer

cQry        := ""
oJBody      := JsonObject():New()
oJBodyDt    := JsonObject():New()
jQuery      := oRest:GetQueryRequest()
oStmDetail  := Nil

nPage       :=	IIF(jQuery['page'] <> Nil,Val(jQuery['page']), 1 )
nPageSize   :=	IIF(jQuery['pageSize'] <> Nil,Val(jQuery['pageSize']), 15 )

filGroup    := IIF(jQuery['filGroup'] <> Nil .and. !('*' $ jQuery['filGroup']) , jQuery['filGroup'], '')
aAux		:= StrTokArr2(filGroup,',')

for nX := 1 To Len(aAux)
    If ('UF ORIGEM: ' $ Upper(aAux[nX]) .Or. 'UF_ORIGEM:' $ Upper(aAux[nX]))
        cUfOrigem += IIF(Empty(cUfOrigem), '',',') + Iif('UF_ORIGEM:' $ Upper(aAux[nX]),StrTran( Upper(aAux[nX]) ,'UF_ORIGEM:',''), StrTran( Upper(aAux[nX]) ,'UF ORIGEM: ',''))
    ElseIf ('UF DESTINO: ' $ Upper(aAux[nX] ) .Or. 'UF_DESTINO:' $ Upper(aAux[nX] ))
        cUfDestino += IIF(Empty(cUfDestino), '',',') + Iif('UF_DESTINO:' $ Upper(aAux[nX]),StrTran( Upper(aAux[nX]) ,'UF_DESTINO:',''), StrTran( Upper(aAux[nX]) ,'UF DESTINO: ',''))
    Else
        If ( !Empty(aAux[nX]) )
            cFilter := '%' + aAux[nX] + '%'
        EndIf
    EndIf
Next

nRegIni := ( ( nPage - 1 ) * nPageSize ) + 1
nRegFim := nPage * nPageSize

cQry += " SELECT F20_CODIGO, F20_DESC FROM ( "
cQry += "   SELECT ROW_NUMBER() OVER( ORDER BY F20_CODIGO, F20_DESC ) LINE_NUMBER, "
cQry += "       F20_CODIGO, F20_DESC "
cQry += "   FROM " + RetSqlName("F20") + " F20 "
cQry += "   INNER JOIN " + RetSqlName("F21") + " F21 ON F21.F21_FILIAL = ? AND F21.F21_TIPOPF = ? AND F21.F21_CODIGO = F20_CODIGO AND F21.D_E_L_E_T_ = ' ' "

If ( !Empty(cUfOrigem)) 
    aUfOrigem := StrTokArr(cUfOrigem, ",")
    cQry += " AND F21_UFORI IN ( ? ) "
EndIf

If ( !Empty(cUfDestino)) 
    aUfDestino := StrTokArr(cUfDestino, ",")
    cQry += " AND F21_UFDEST IN ( ? ) "
EndIf

cQry += "   WHERE F20_FILIAL = ? AND F20_TIPO = ? "

If ( !Empty(cFilter) )
    cQry += " AND ( 
    cQry += "   UPPER(F20_CODIGO) LIKE ? OR "
    cQry += "   UPPER(F20_DESC) LIKE ? "
    cQry += " ) "
EndIf

cQry += " AND F20.D_E_L_E_T_ = ' ' "

cQry += "  GROUP BY F20_CODIGO, F20_DESC "
cQry += " ) TAB "
cQry += " WHERE LINE_NUMBER BETWEEN ? AND ? "
cQry += " GROUP BY F20_CODIGO, F20_DESC "
 
oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

oStatement:setString(++nIndex, xFilial('F21'))
oStatement:setString(++nIndex, '01')

If ( !Empty(cUfOrigem)) 
    oStatement:setIn(++nIndex, aUfOrigem)
EndIf

If ( !Empty(cUfDestino)) 
    oStatement:setIn(++nIndex, aUfDestino)
EndIf

oStatement:setString(++nIndex, xFilial('F20'))
oStatement:setString(++nIndex, '01')

If ( !Empty(cFilter) )
    oStatement:setString(++nIndex, Upper(cFilter))
    oStatement:setString(++nIndex, Upper(cFilter))
EndIf

oStatement:setNumeric(++nIndex, nRegIni)
oStatement:setNumeric(++nIndex, nRegFim + 1) //Soma mais um registro para retorno do HasNext

cQry := oStatement:getFixQuery()

cAliasOriDest := MPSysOpenQuery(cQry)

oJBody["items"] := {}

While (cAliasOriDest)->(!EoF()) .And. nCount < nPageSize
    nCount++

    oJBodyDt    := JsonObject():New()

    oJBodyDt["perfil"]      := (cAliasOriDest)->F20_CODIGO
    oJBodyDt["descricao"]   := AllTrim(Upper((cAliasOriDest)->F20_DESC))
    oJBodyDt["ufOrigem"]    := ""
    oJBodyDt["ufDestino"]   := ""

    // busca detalhes
    cQryDetail := GetDetail(oStmDetail,(cAliasOriDest)->F20_CODIGO)
        
    While (cQryDetail)->(!EoF())

        oJBodyDt["ufOrigem"]  += AllTrim((cQryDetail)->F21_UFORI) + '|'
        oJBodyDt["ufDestino"] += AllTrim((cQryDetail)->F21_UFDEST) + '|'

        (cQryDetail)->(dbSkip())
    End

    ( cQryDetail )->( DBCloseArea() )

    AADD(oJBody["items"],oJBodyDt)

    lOk := .T.

    FREEOBJ( oJBodyDt )

    (cAliasOriDest)->(DbSkip())
End

FreeObj(oStatement)

oJBody["success"] := lOk
oJBody["hasNext"] := !(cAliasOriDest)->(EoF())

( cAliasOriDest )->( DBCloseArea() )

cJson := oJBody:toJson()
FREEOBJ( oJBody )

Return oRest:setResponse(cJson)

//-------------------------------------------------------------------
/*/{Protheus.doc} GetDetail()
Retorna as UFs origem e destino fora da query principal para manter
a paginação

@author  almeida.veronica
@since   14.09.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function GetDetail(oStmDetail as object, cCodigo as character)

Local cDetail as character
Local cQry as character

cDetail	:= GetNextAlias()

If oStmDetail == nil
	cQry := " SELECT F21.F21_UFORI , F21.F21_UFDEST "
    cQry += " FROM " + RetSqlName("F21") + " F21 " 
    cQry += " WHERE F21.F21_FILIAL = ? AND F21.F21_CODIGO = ? AND F21.F21_TIPOPF = ? AND F21.D_E_L_E_T_ = ' ' "

    cQry := ChangeQuery(cQry)
    oStmDetail:=FWPreparedStatement():New(cQry)
Endif

oStmDetail:SetString(1,xFilial('F21'))
oStmDetail:SetString(2,cCodigo)
oStmDetail:SetString(3,'01')

cQry := oStmDetail:GetFixQuery()

cDetail := MpSysOpenQuery(cQry,cDetail)

FreeObj(oStmDetail)

Return cDetail
