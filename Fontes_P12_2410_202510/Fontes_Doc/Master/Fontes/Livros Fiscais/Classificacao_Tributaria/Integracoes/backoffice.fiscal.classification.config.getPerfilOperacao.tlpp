#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'
#include 'tlpp-rest.th'
Namespace totvs.protheus.backoffice.fiscal.Classifier

//---------------------------------------------------------------------------------------
/*/{Protheus.doc} FisGetPerfilOperacoes
Classe que contém as operações para obter os dados da tela de seleção de perfis - aba operação
@author  Thiago Moreira
@since   19.07.2022
@version 1.0
/*/
//---------------------------------------------------------------------------------------
Class FisGetPerfilOperacoes
    public method new() constructor

    @Get("/GetPerfilOperacoes")    
    public method GetPerfilOperacoes()
End Class

method new() class FisGetPerfilOperacoes


return

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método para obter os dados dos perfis de operações e colocá-los em arquivo Json

@Obs    Passar via QueryParam a filial logada em sistema

@author  Thiago Moreira
@since   19.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------

METHOD GetPerfilOperacoes() Class FisGetPerfilOperacoes

Local cQry          := ""
Local oJBody        := JsonObject():New()
Local oJBodyDt      
Local lOk           as logical
Local oStatement    as object
Local cAliasEnquad  as character

cQry := "SELECT F20_CODIGO, F20_DESC, F20_NATOPE, CJH_DESCR"+CRLF
cQry += "FROM "+RetSqlName("F20")+" F20"+CRLF
cQry += "LEFT JOIN "+RetSqlName("CJH")+" CJH"+CRLF
cQry += "   ON  CJH_FILIAL = ?"+CRLF
cQry += "   AND F20_NATOPE = CJH_COD"+CRLF
cQry += "   AND CJH.D_E_L_E_T_ = ' '"+CRLF
cQry += "WHERE F20_TIPO = ?"+CRLF
cQry += "AND F20_FILIAL = ?"+CRLF
cQry += "AND F20.D_E_L_E_T_ = ' '"+CRLF
cQry += "ORDER BY F20_CODIGO, F20_DESC ASC"

oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

oStatement:setString(1, xFilial("CJH"))
oStatement:setString(2, '03')
oStatement:setString(3, xFilial("F20"))

cQry := oStatement:getFixQuery()

cAliasEnquad := MPSysOpenQuery(cQry)

oJBody["perfil_operacoes"] := {}

While (cAliasEnquad)->(!EoF())

    oJBodyDt := JsonObject():New()

    oJBodyDt['codigo']              := AllTrim((cAliasEnquad)->F20_CODIGO) 
    oJBodyDt['desc']                := AllTrim((cAliasEnquad)->F20_DESC)
    oJBodyDt['codigo_natureza']     := AllTrim((cAliasEnquad)->F20_NATOPE)
    oJBodyDt['descricao_natureza']  := AllTrim((cAliasEnquad)->CJH_DESCR)

    aadd(oJBody["perfil_operacoes"],oJBodyDt)

    FREEOBJ( oJBodyDt )

    (cAliasEnquad)->(DbSkip())

    lOk := .T.

End

(cAliasEnquad)->( DBCloseArea() )

oJBody["success"] := lOk

FreeObj(oStatement)

Return oRest:setResponse(oJBody)
