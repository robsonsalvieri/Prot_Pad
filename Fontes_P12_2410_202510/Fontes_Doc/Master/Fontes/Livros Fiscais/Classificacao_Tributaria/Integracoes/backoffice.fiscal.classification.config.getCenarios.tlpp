#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'classificadorfiscal.ch'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//----------------------------------------------------------------------------------------------------
/*/{Protheus.doc} FisGetMonitorCenario
Classe que contém as operações para obter os dados do status dos cenários salvos e/ou enviados
@author  Yuri Gimenes da Costa
@since   19.07.2022
@version 1.0
/*/
//-----------------------------------------------------------------------------------------------------
Class FisGetMonitorCenario

    data jQuery as json

    public method new() constructor

    @Get("/GetMonitorCenario")    
    public method GetMonitorCenario()

    @Get('/cenarios/status/:idCenario')
    public method GetStatusById() as json

    private method GetParams() as json
    
    public method GetJsonFieldsForQuery() as character
    public method GetScenarioStatus() as character

End Class

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe
@author  Yuri Gimenes da Costa
@since   19.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method new() class FisGetMonitorCenario


return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetMonitorCenario()
Método para obter os dados dos status dos cenários e devolve-los 
em um Json

@Obs    Passar via QueryParam a filial da busca que será realizada
        
@author  Yuri Gimenes da Costa
@since   19.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method GetMonitorCenario() class FisGetMonitorCenario

Local cQry              := ""
Local oJBody            := JsonObject():New()
Local oJBodyDt          as object
Local oJProd            as object
Local oStatement        as object
Local cJson             as character
Local jQuery            := oRest:GetQueryRequest()
Local nPage             as numeric
Local nPageSize         as numeric
Local filGroup          as character
Local lOk               as logical
Local cDB               as character
Local nCount            as integer
Local lJsRet            as logical
Local oJsonCenario      as object
Local jRetornoSystax    as json
Local cErroSystax       as character
Local oFISEnviaCenario  as object
Local cJsonFields       as character

DbSelectArea('CJG')
lJsRet := CJG->(FieldPos('CJG_JSRET') > 0)
CJG->(DbCloseArea())

nPage       := IIF(jQuery['page'] <> Nil,Val(jQuery['page']), 1 )
nPageSize   := IIF(jQuery['pageSize'] <> Nil,Val(jQuery['pageSize']), 15 )
filGroup    := IIF(jQuery['filGroup'] <> Nil .and. !('*' $ jQuery['filGroup']) , jQuery['filGroup'], '')
lAutomato   := Iif(jQuery['automato'] <> Nil, .T., .F.)

nRegIni := ( ( nPage - 1 ) * nPageSize ) + 1
nRegFim := nPage * nPageSize

cDB := Upper(TCGetDB())

cQry += " SELECT F24_CDPROD, B1_DESC, CJG.CJG_DTENV, CJG.CJG_HRENV, CJG_IDENVI, CJG_IDPPRO, CJG_IDPPAR, CJG_IDPORI, CJG_IDCENA, "
cQry += " F20_DESC AS NOME, "

cJsonFields := self:GetJsonFieldsForQuery(cDB, lJsRet)
cQry += ' ? '

cQry += " FROM ( "//+RetSqlName("CJG")+" CJG "
cQry += "   SELECT ROW_NUMBER() OVER( ORDER BY CJG.R_E_C_N_O_ ) LINE_NUMBER, "
cQry += "   CJG.R_E_C_N_O_ CJG_RECNO,CJG.CJG_DTENV,CJG.CJG_HRENV,CJG.CJG_IDENVI,CJG.CJG_IDPPRO,CJG.CJG_IDPPAR,CJG.CJG_IDPORI,CJG.CJG_IDCENA, "
cQry += "   CJG.CJG_JSON "

if ( lJsRet )
    cQry += " , CJG.CJG_JSRET "
endif

cQry += "   FROM " + RetSqlName("CJG") + " CJG "
cQry += "   WHERE CJG.D_E_L_E_T_ = ' '"
cQry += "   AND CJG_FILIAL = ?"

If ( !Empty(filGroup) .And. 'status:' $ filGroup) 

    If (filGroup $ 'status: ' + Lower(STR0103) .And. !(filGroup $ 'status: ' + Lower(STR0104))) .Or. (filGroup $ 'status:enviado'/*utilizado somente para automação */)
        cQry += " AND CJG_DTENV <> ' ' AND CJG_HRENV <> ' ' "
    ElseIf (filGroup $ 'status: ' + Lower(STR0104) .And. !(filGroup $ 'status: ' + Lower(STR0103))) .Or. (filGroup $ 'status:a_enviar'/*utilizado somente para automação */)
        cQry += " AND CJG_DTENV = ' ' AND CJG_HRENV = ' ' "
    EndIf

ElseIf (!Empty(filGroup) .And. !('status: ' $ filGroup))

	cQry += " AND ( "
	cQry += "   UPPER(CJG_IDENVI) LIKE '%" + Upper(filGroup) + "%' OR " 
	cQry += "   UPPER(CJG_DTENV)  LIKE '%" + Upper(filGroup) + "%' OR " 
	cQry += "   UPPER(CJG_HRENV)  LIKE '%" + Upper(filGroup) + "%' OR " 
	cQry += "   UPPER(CJG_IDPPRO) LIKE '%" + Upper(filGroup) + "%' OR " 
	cQry += "   UPPER(CJG_IDPPAR) LIKE '%" + Upper(filGroup) + "%' "
	cQry += " ) "

EndIf

cQry += " ) CJG "
cQry += " INNER JOIN "+RetSqlName("F20")+" F20 ON F20_FILIAL = ? AND F20_CODIGO = CJG_IDPPAR AND F20_TIPO = '02' AND F20.D_E_L_E_T_ = ' ' "
cQry += " INNER JOIN "+RetSqlName("F24")+" F24 ON F24_FILIAL = ? AND F24_CODIGO = CJG_IDPPRO AND F24.D_E_L_E_T_ = ' '"
cQry += " INNER JOIN "+RetSqlName("SB1")+" SB1 ON B1_FILIAL = ? AND B1_COD = F24_CDPROD AND SB1.D_E_L_E_T_ = ' ' "

cQry += " WHERE LINE_NUMBER BETWEEN " + cValTochar(nRegIni) + " AND " + cValTochar(nRegFim + 1) + " " //Soma mais um registro para retorno do HasNext

cQry += " ORDER BY CJG_IDENVI, F24_CDPROD"
 

oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

oStatement:setUnsafe(1, cJsonFields)
oStatement:setString(2, xFilial("CJG"))
oStatement:setString(3, xFilial("F20"))
oStatement:setString(4, xFilial("F24"))
oStatement:setString(5, xFilial("SB1"))

cQry := oStatement:getFixQuery()

cAliasMonitor := MPSysOpenQuery(cQry)

oJBody["cenarios"] := {}

While (cAliasMonitor)->(!EoF()) .And. nCount < nPageSize

    nCount++

    cIdEnvio :=  (cAliasMonitor)->CJG_IDENVI
    oJsonCenario	:= JsonObject():New()
    cJson := AllTrim((cAliasMonitor)->JSON)
	oJsonCenario:FromJSON( cJson )
    cErroSystax := ''

    if ( lJsRet )
        jRetornoSystax := JsonObject():New()

        if ( jRetornoSystax:FromJson( AllTrim((cAliasMonitor)->JSRET) ) == nil )
            oFISEnviaCenario := FISEnviaCenario():New()
            cErroSystax := oFISEnviaCenario:GetResponseMessage(jRetornoSystax)
            FreeObj(oFISEnviaCenario)
        endif
        
        FreeObj(jRetornoSystax)
    endif

    oJBodyDt := JsonObject():New()
    
    oJBodyDt["id_envio"]            := AllTrim((cAliasMonitor)->CJG_IDENVI)
    oJBodyDt["status"]              := self:GetScenarioStatus((cAliasMonitor)->CJG_IDCENA, cErroSystax)
    oJBodyDt["data_envio"]          := DtoC(StoD((cAliasMonitor)->CJG_DTENV))
    oJBodyDt["hora_envio"]          := AllTrim((cAliasMonitor)->CJG_HRENV )
    oJBodyDt["id_perfil_prod"]      := AllTrim((cAliasMonitor)->CJG_IDPPRO)
    oJBodyDt["id_perfil_part"]      := AllTrim((cAliasMonitor)->CJG_IDPPAR)
    oJBodyDt["id_perfil_orde"]      := AllTrim((cAliasMonitor)->CJG_IDPORI)
    oJBodyDt["id_cenario"]          := AllTrim((cAliasMonitor)->CJG_IDCENA)
    oJBodyDt["nome_part"]           := AllTrim((cAliasMonitor)->NOME)
    oJBodyDt["uf_origem"]           := AllTrim(oJsonCenario["cenarios"][1]["uf_origem"])
    oJBodyDt["uf_destino"]          := AllTrim(oJsonCenario["cenarios"][1]["uf_destino"])
    oJBodyDt["status_code_systax"]  := '0'
    oJBodyDt["status_systax"]       := STR0231 // 'Clique em Verificar status Systax para obter o status'
    oJBodyDt["produtos"]            := {}
    oJBodyDt["json_cenario"]        := cJson
    oJBodyDt["erro_systax"]         := AllTrim(cErroSystax)
    
    While (cAliasMonitor)->(!EoF()) .And. cIdEnvio == (cAliasMonitor)->CJG_IDENVI

        oJProd := JsonObject():New()
        oJProd["cod_prod"] := AllTrim((cAliasMonitor)->F24_CDPROD)
        oJProd["descricao"] := AllTrim((cAliasMonitor)->B1_DESC)
        aadd(oJBodyDt["produtos"], oJProd )
        
        FREEOBJ( oJProd )

        (cAliasMonitor)->(DbSkip())
    
    EndDo

    If lAutomato

        oJBodyDt["id_envio"]          := "TESTE"
        oJBodyDt["status"]            := "enviado"   
        oJBodyDt["data_envio"]        := ""
        oJBodyDt["hora_envio"]        := ""
        oJBodyDt["id_cenario"]        := "AUTOMACAO"

    EndIf

    aadd(oJBody["cenarios"],oJBodyDt)

    FREEOBJ( oJBodyDt )
    FREEOBJ( oJsonCenario )

    lOk := .T.

End

If len(oJBody["cenarios"]) > 0
    oJBody["success"] :=.T.
else
    oJBody["success"] := .F.
EndIF

oJBody["success"] := lOk
oJBody["hasNext"] := !(cAliasMonitor)->(EoF())

(cAliasMonitor)->(DbCloseArea())
FreeObj(oStatement)

Return oRest:setResponse(oJBody)

/*/{Protheus.doc} GetStatusById
Método responsável por verificar o status do cenário na Systax
@author Juliano Fernandes
@since 06/05/2024
@version 12.1.2310
@return jResponse, json, Json contendo as informações do status do cenário na Systax
/*/
method GetStatusById() as json class FisGetMonitorCenario

    local jHeaderResponse as json
    local jResponse as json
    local oRestClient as object
    local aHeader as array
    local cResult as character
    local jResult as json
    local cStatus as character
    local jParams as json
    
    jParams := self:GetParams()

    jResponse := JsonObject():New()
    jResponse['success'] := .F.
    jResponse['id'] := jParams['id']
    jResponse['statusCode'] := '0'
    jResponse['status'] := EncodeUTF8(STR0227) // 'Erro ao buscar status do cenário'

    oRestClient := FwRest():New(jParams['url'])
	oRestClient:SetPath(jParams['path'])

    if ( !Empty(jParams['token']) )
        aHeader := {}
		Aadd(aHeader, 'Content-Type: application/json; charset=UTF-8')
		Aadd(aHeader, 'Content-Length: <calculated when request is sent>')
		Aadd(aHeader, 'Accept: */*')
		Aadd(aHeader, 'User-Agent: Chrome/65.0 (compatible; Protheus ' + GetBuild() + ')')
		Aadd(aHeader, 'Authorization: Bearer ' + jParams['token'])

        if ( oRestClient:Get(aHeader) )
            cResult := oRestClient:GetResult()

            jResult := JsonObject():New()

            if ( jResult:FromJson(cResult) == nil )
                cStatus := jResult['msg']

                if ( jResult:HasProperty('motivo') )
                    cStatus += ' - ' + jResult['motivo']
                endif

                jResponse['success'] := .T.
                jResponse['statusCode'] := jResult['status']
                jResponse['status'] := cStatus
            endif
        endif
    endif

    if ( !FwIsInCallStack('GetMonitorCenario') )
        jHeaderResponse := JsonObject():New()
        jHeaderResponse['Content-Type'] := 'application/json; charset=UTF-8'
        oRest:SetHeaderResponse(jHeaderResponse)
    
        oRest:SetResponse(jResponse)

        FwFreeVar(jParams)
        FwFreeVar(jResponse)
    endif

    FwFreeVar(jHeaderResponse)
    FwFreeVar(oRestClient)
    FwFreeVar(jResult)
    FwFreeArray(aHeader)

return jResponse

/*/{Protheus.doc} GetParams
Método responsável por obter os parâmetros necessários para buscar o status do cenário na Systax
@author Juliano Fernandes
@since 09/05/2024
@version 12.1.2310
@return jParams, json, Json contendo os parâmetros necessários a busca do status
/*/
method GetParams() as json class FisGetMonitorCenario

    local jParams as json
    local jPathParamsRequest as json
    local jQueryParamsRequest as json
    local cCJEParam as character
    local jCJEParam as json
    local cMockServer as character
    local cUrl as character
    local cId as character
    local cPath as character

    jPathParamsRequest := oRest:GetPathParamsRequest()
    jQueryParamsRequest := oRest:GetQueryRequest()

    if ( Empty(cId) .and. jPathParamsRequest:HasProperty('idCenario') )
        cId := jPathParamsRequest['idCenario']
    endif

    cCJEParam := GetConfigClassif({'URLPRINCIPAL'})
    
    jCJEParam := JsonObject():New()
    if ( jCJEParam:FromJson(cCJEParam) == nil )
        cUrl := jCJEParam:URLPRINCIPAL
    endif

	if ( jQueryParamsRequest:HasProperty('mockserver') )
        cMockServer := jQueryParamsRequest['mockserver']
		cUrl := cMockServer
	endif

    cPath := '/cadastro-geral/cenarios/status/' + cId

	if ( jQueryParamsRequest:HasProperty('endpoint') )
		cPath := jQueryParamsRequest['endpoint']
	endif

    jParams := JsonObject():New()
    jParams['id'] := cId
    jParams['url'] := cUrl
    jParams['path'] := cPath
    jParams['token'] := AllTrim(ConsultTok(cUserName, cMockServer))    
    jParams['mockserver'] := cMockServer

return jParams

/*/{Protheus.doc} GetJsonFieldsForQuery
Método responsável por obter os campos json convertidos para utilização na query
@author Juliano Fernandes
@since 25/07/2024
@version 12.1.2310
@param cDbType, character, Tipo do banco de dados em que o sistema está sendo executado
@param lJsRet, logical, Define se o campo CJB_JSRET existe na base de dados
@return cQry, character, Campos json convertidos para utilização na query
/*/
method GetJsonFieldsForQuery(cDbType as character, lJsRet as logical) as character class FisGetMonitorCenario

    local cQry as character 

    if ( "ORACLE" $ cDbType )
        cQry += " UTL_RAW.CAST_TO_VARCHAR2(DBMS_LOB.SUBSTR(CJG_JSON, 4000,1)) JSON "

        if ( lJsRet )
            cQry += ", UTL_RAW.CAST_TO_VARCHAR2(DBMS_LOB.SUBSTR(CJG_JSRET, 4000,1)) JSRET "
        endif    
    elseif ( "POSTGRES" $ cDbType )
        cQry += " encode(CJG_JSON,'escape') JSON "

        if ( lJsRet )
            cQry += ", encode(CJG_JSRET,'escape') JSRET "
        endif      
    else
        cQry += " ISNULL(CAST(CAST(CJG_JSON AS VARBINARY(8000)) AS VARCHAR(8000)),'') AS [JSON] "

        if ( lJsRet )
            cQry += ", ISNULL(CAST(CAST(CJG_JSRET AS VARBINARY(8000)) AS VARCHAR(8000)),'') AS [JSRET] "
        endif      
    endif 

return cQry

/*/{Protheus.doc} GetScenarioStatus
Método responsável por obter o status do cenário
@author Juliano Fernandes
@since 12/08/2024
@version 12.1.2310
@param cScenarioId, character, ID do cenário
@param cError, character, Erro ocorrido no envio do cenário
@return cStatus, character, Status do cenário
/*/
method GetScenarioStatus(cScenarioId as character, cError as character) as character class FisGetMonitorCenario

    local cStatus as character

    do case
        case ( !Empty(cError) )
            cStatus := 'erro'
        case ( !Empty(cScenarioId) )
            cStatus := 'enviado'
        otherwise
            cStatus := 'a enviar'
    endcase

return cStatus
