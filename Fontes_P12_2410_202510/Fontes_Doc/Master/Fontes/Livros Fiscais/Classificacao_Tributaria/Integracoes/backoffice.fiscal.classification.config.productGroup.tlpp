#INCLUDE "protheus.ch"
#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "classificadorfiscal.ch"

#DEFINE STATUS_CODE_CREATED     201

Namespace totvs.protheus.backoffice.fiscal.Classifier

/*/{Protheus.doc} ProductGroup
Classe responsável por montar os dados para o consumo da api de Associação de 
Perfis de produtos em cenários
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Class ProductGroup

    Private Data cError         as character
    Private Data cMockServer    as character
    Private Data cPath          as character

    Public Method New() Constructor

    Public Method SetMockServer()
    Public Method SetPath()

    Public Method ProductGroupsAssociationDisassociation()

End Class

/*/{Protheus.doc} New
Método construtor da classe
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method New() Class ProductGroup
Return

/*/{Protheus.doc} SetMockServer
Método responsável por definir o mockserver
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method SetMockServer(cMockServer as character) Class ProductGroup

    Default cMockServer := ''

    ::cMockServer := cMockServer

Return

/*/{Protheus.doc} SetPath
Método responsável por definir o path para o mockserver
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method SetPath(cPath as character) Class ProductGroup

    Default cPath := ''

    ::cPath := cPath

Return

/*/{Protheus.doc} ProductGroupsAssociationDisassociation
Método responsável por montar os dados e enviar para a classe SendProductGroup para
associar um perfil de produto a um cenário
@author Thiago Moreira / Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
/*/
Method ProductGroupsAssociationDisassociation(jData as json) Class ProductGroup

    Local jResponse         as json
    Local oSendProductGroup as object
    
    jResponse := JsonObject():New()

    oSendProductGroup := SendProductGroup():New()

    If !Empty(::cMockServer)
        oSendProductGroup:SetMockServer(::cMockServer)
        oSendProductGroup:SetPath(::cPath)
    EndIf

    If !oSendProductGroup:PostAssociationDisassociation(jData)
        jResponse["code"]               := oSendProductGroup:iStatusCode
        jResponse["message"]            := oSendProductGroup:cError
        jResponse["detailedMessage"]    := ""

        Return jResponse
    EndIf
    
    jResponse["code"]               := STATUS_CODE_CREATED
    jResponse["message"]            := STR0313 // "Associação realizada com sucesso."
    jResponse["detailedMessage"]    := ""

    FwFreeObj(oSendProductGroup)

Return jResponse
