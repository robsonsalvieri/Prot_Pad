#include 'Protheus.ch'
#include 'tlpp-core.th'
#include 'classificadorfiscal.ch'
#INCLUDE 'FWLIBVERSION.CH'

Static lMetrica    :=  FwLibVersion() >= "20200727"

Namespace totvs.protheus.backoffice.fiscal.Classifier

/*/{Protheus.doc} ValidClassifierLicense

    Classe para executar validação se o cliente possui o pacote da Classificação Tributária contratato.
    A verificação é feita nos pacotes cadastrados no license. 
    
    @author Juliano Fernandes
    @since 03/07/2024
    @version 12.1.2310
/*/
class ValidClassifierLicense

    private data aClassifierLicenseIds as array
    private data aErrorMessage as array

    public method New() constructor
    public method GetErrorMessage() as array
    public method Valid() as logical
    public method Destroy()

    private method SetErrorMessage()
    private method SetClassifierLicenseIds()

endclass

/*/{Protheus.doc} New

    Método construtor da classe ValidClassifierLicense
    
    @author Juliano Fernandes
    @since 03/07/2024
    @version 12.1.2310
/*/
method New() class ValidClassifierLicense

    self:SetClassifierLicenseIds()
    self:SetErrorMessage()

return self

/*/{Protheus.doc} SetClassifierLicenseIds

    Método que define os IDs cadastrados no License Server para autorizar o uso da Classificação Tributária
    
    @author Juliano Fernandes
    @since 03/07/2024
    @version 12.1.2310
/*/
method SetClassifierLicenseIds() class ValidClassifierLicense
    
    self:aClassifierLicenseIds := {}
    Aadd(self:aClassifierLicenseIds, 4096) // Classificação Tributária 5k regras
    Aadd(self:aClassifierLicenseIds, 4097) // Classificação Tributária 10k regras
    Aadd(self:aClassifierLicenseIds, 4098) // Classificação Tributária 20k regras
    Aadd(self:aClassifierLicenseIds, 4219) // Classificação Tributária 50k regras
    Aadd(self:aClassifierLicenseIds, 4220) // Classificação Tributária 100k regras
    Aadd(self:aClassifierLicenseIds, 4221) // Classificação Tributária 250k regras
    Aadd(self:aClassifierLicenseIds, 4222) // Classificação Tributária 500k regras
    Aadd(self:aClassifierLicenseIds, 4223) // Classificação Tributária 750k regras

return

/*/{Protheus.doc} GetErrorMessage

    Método que obtém a mensagem de erro
    
    @author Juliano Fernandes
    @since 03/07/2024
    @version 12.1.2310
/*/
method GetErrorMessage() as array class ValidClassifierLicense
return Aclone(self:aErrorMessage)

/*/{Protheus.doc} SetErrorMessage

    Método que define a mensagem de erro
    
    @author Juliano Fernandes
    @since 10/07/2024
    @version 12.1.2310
/*/
method SetErrorMessage() class ValidClassifierLicense

    self:aErrorMessage := {}
    Aadd(self:aErrorMessage, STR0272) // "Prezado Cliente,"
    Aadd(self:aErrorMessage, STR0273) // "Esta funcionalidade é uma oferta e para adquirir, por favor, entre em contato com seu Executivo de Contas."

return self:aErrorMessage

/*/{Protheus.doc} Valid

    Método que verifica se possui a licença para uso da Classificação Tributária
    
    @author Juliano Fernandes
    @since 03/07/2024
    @version 12.1.2310
/*/
method Valid() as logical class ValidClassifierLicense

    local lValid as logical
    local i as numeric
    //local aError := {} as array

    for i := 1 to Len(self:aClassifierLicenseIds)
        if ( !lValid )
            lValid := FWLSEnable( self:aClassifierLicenseIds[i] )
        endif
    next i

    IF lMetrica .AND. lValid
        FWLsPutAsyncInfo("LS006",RetCodUsr(),"09","classFisLoginOk")
        // aError := { {'', 'FWLsPutAsyncInfo(LS006,RetCodUsr(),09,classFisLoginOk)'}}
        // FWLogMsg("INFO",, "ClasFisTelem",,,,,,, aError)
    EndIf

return lValid

/*/{Protheus.doc} Destroy

    Realiza a destruição do objeto instanciado pela classe ValidClassifierLicense
    
    @author Juliano Fernandes
    @since 03/07/2024
    @version 12.1.2310
/*/
method Destroy() class ValidClassifierLicense

    Asize(self:aClassifierLicenseIds, 0)
    Asize(self:aErrorMessage, 0)

return
