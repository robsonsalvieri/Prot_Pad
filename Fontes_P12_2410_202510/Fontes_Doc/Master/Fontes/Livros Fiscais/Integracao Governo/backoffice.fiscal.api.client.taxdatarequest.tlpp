#INCLUDE "TLPP-CORE.TH"
#INCLUDE "PROTHEUS.CH"

namespace totvs.protheus.backoffice.fiscal.api.integration

//Classe que fará acesso aos endpoints no servidor da Receita Federal para consultar dados da Reforma Tributária
Class ConsumptionTaxData

    Private data aHeaderRT as array
    Private data cHostRT as character
    Private data oRestGovServer as object
    Private data oResponseGovServer as json
    Private data oUnicode as object

    Public Method new() as object
    Public Method getAliquotaUniao(cEffectiveDate as character) as logical
    Public Method getAliquotaUf(cCodeUf as character, cEffectiveDate as character) as logical
    Public Method getAliquotaMunicipio(cCodeMunicipio as character, cEffectiveDate as character) as logical
    Public Method getListaClassTribCST(cCodeCst as character, cEffectiveDate as character) as logical
    Public Method getListaClassTribIs(cEffectiveDate as character) as logical
    Public Method getListaClassTribCbSIbs(cEffectiveDate as character) as logical
    Public Method getListaSituacaoTribIs(cEffectiveDate as character) as logical
    Public Method getListaSituacaoTribCbSIbs(cEffectiveDate as character) as logical
    Public Method getInformacoesNcm(cCodeNcm as character, cEffectiveDate as character) as logical
    Public Method getInformacoesNbs(cCodeNbs as character, cEffectiveDate as character) as logical
    Public Method getVersao() as logical
    Public Method getListaUfs() as logical
    Public Method getListaMunicipiosUf(cSiglaUf as character) as logical
    Public Method getFundamentosLegais(cEffectiveDate as character) as logical
    Public Method getResponse() as json
    Public Method destroy()
    Private Method encodeToCp1252(cStringUtf8 as character) as character

EndClass

/*----------------------------------------------------------------------------
{Protheus.doc} new
@description Método construtor da classe
@author Carlos Eduardo Nonato
@since 22/09/2025
@return Instância da classe ConsumptionTaxData
-----------------------------------------------------------------------------*/
Method new() as object class ConsumptionTaxData
    ::aHeaderRT :=  {'Content-Type: application/json', 'Authorization: No Auth'} 
    ::cHostRT := 'https://piloto-cbs.tributos.gov.br/servico/calculadora-consumo/api' 
    ::oRestGovServer := FwRest():New(::cHostRT) 
    ::oResponseGovServer := JsonObject():New()
    ::oUnicode := tUnicode():New()
return self

/*----------------------------------------------------------------------------
{Protheus.doc} getAliquotaUniao
@description Obtém a alíquota padrão ou de referência para CBS
@author Carlos Eduardo Nonato
@since 22/09/2025
@param cEffectiveDate - Data de vigência da alíquota no formato AAAAMMDD
@return lSuccess - .t. se a consulta foi realizada com sucesso (HTTP code 200)
-----------------------------------------------------------------------------*/
Method getAliquotaUniao(cEffectiveDate as character) as logical class ConsumptionTaxData
    Local lSuccess as logical
    Local cQueryParameter as character
    Local cResult as character
    Default cEffectiveDate := dtos(dDataBase)

    cQueryParameter := 'data='+transform(cEffectiveDate, '@R 9999-99-99')

    ::oRestGovServer:SetPath('/calculadora/dados-abertos/aliquota-uniao')

    lSuccess := ::oRestGovServer:get(::aHeaderRT, cQueryParameter)

    cResult := self:encodeToCp1252(::oRestGovServer:GetResult())

    ::oResponseGovServer:FromJson(cResult)

return lSuccess

/*----------------------------------------------------------------------------
{Protheus.doc} getAliquotaUf
@description Obtém a alíquota padrão ou de referência para IBS Estadual
@author Carlos Eduardo Nonato
@since 22/09/2025
@param cCodeUf - Código da Unidade Federativa (UF)
@param cEffectiveDate - Data de vigência da alíquota no formato AAAAMMDD
@return lSuccess - .t. se a consulta foi realizada com sucesso (HTTP code 200)
-----------------------------------------------------------------------------*/
Method getAliquotaUf(cCodeUf as character, cEffectiveDate as character) as logical class ConsumptionTaxData
    Local lSuccess as logical
    Local cQueryParameter as character
    Local cResult as character
    Default cEffectiveDate := dtos(dDataBase)

    if cCodeUf != nil
        cQueryParameter := 'codigoUf='+cCodeUf+'&'
    endif    
    cQueryParameter += 'data='+transform(cEffectiveDate, '@R 9999-99-99')

    ::oRestGovServer:SetPath('/calculadora/dados-abertos/aliquota-uf')

    lSuccess := ::oRestGovServer:get(::aHeaderRT, cQueryParameter)

    cResult := self:encodeToCp1252(::oRestGovServer:GetResult())

    ::oResponseGovServer:FromJson(cResult)

return lSuccess

/*----------------------------------------------------------------------------
{Protheus.doc} getAliquotaMunicipio
@description Obtém a alíquota padrão ou de referência para IBS Municipal
@author Carlos Eduardo Nonato
@since 22/09/2025
@param cCodeMunicipio - Código do Município 
@param cEffectiveDate - Data de vigência da alíquota no formato AAAAMMDD
@return lSuccess - .t. se a consulta foi realizada com sucesso (HTTP code 200)
-----------------------------------------------------------------------------*/
Method getAliquotaMunicipio(cCodeMunicipio as character, cEffectiveDate as character) as logical class ConsumptionTaxData
    Local lSuccess as logical
    Local cQueryParameter as character
    Local cResult as character
    Default cEffectiveDate := dtos(dDataBase)

    if cCodeMunicipio != nil
        cQueryParameter := 'codigoMunicipio='+cCodeMunicipio+'&'
    endif
    cQueryParameter += 'data='+transform(cEffectiveDate, '@R 9999-99-99')

    ::oRestGovServer:SetPath('/calculadora/dados-abertos/aliquota-municipio')

    lSuccess := ::oRestGovServer:get(::aHeaderRT, cQueryParameter)

    cResult := self:encodeToCp1252(::oRestGovServer:GetResult())

    ::oResponseGovServer:FromJson(cResult)

return lSuccess

/*----------------------------------------------------------------------------
{Protheus.doc} getListaClassTribCST
@description Obtém a lista das classificações tributárias (cClassTrib) 
             cadastradas com base no código CST
@author Carlos Eduardo Nonato
@since 22/09/2025
@param cCodeCst - Código da Classificação Tributária 
@param cEffectiveDate - Data de vigência da alíquota no formato AAAAMMDD
@return lSuccess - .t. se a consulta foi realizada com sucesso (HTTP code 200)
-----------------------------------------------------------------------------*/
Method getListaClassTribCST(cCodeCst as character, cEffectiveDate as character) as logical class ConsumptionTaxData
    Local lSuccess as logical
    Local cQueryParameter as character
    Local cResult as character
    Default cCodeCst := ' '
    Default cEffectiveDate := dtos(dDataBase)

    if !empty(cCodeCst)
        cQueryParameter := 'data='+transform(cEffectiveDate, '@R 9999-99-99')

        ::oRestGovServer:SetPath('/calculadora/dados-abertos/classificacoes-tributarias/' + cCodeCst)

        lSuccess := ::oRestGovServer:get(::aHeaderRT, cQueryParameter)

        cResult := self:encodeToCp1252(::oRestGovServer:GetResult())

    else
        //Essa condicao foi criada porque estava dando erro no retorno da API, deverá ser retirada quando o endpoint for corrigido
        cResult := '{ "title": "Bad Request", "status": 400, "detail": "Required path variable [idSituacaoTributaria] is not present." }'
    endif

    ::oResponseGovServer:FromJson(cResult)

return lSuccess

/*----------------------------------------------------------------------------
{Protheus.doc} getListaClassTribIs
@description Obtém a lista das classificações tributárias (cClassTrib) 
             cadastradas vigentes em um determinada data para Is
@author Carlos Eduardo Nonato
@since 22/09/2025
@param cEffectiveDate - Data de vigência da alíquota no formato AAAAMMDD
@return lSuccess - .t. se a consulta foi realizada com sucesso (HTTP code 200)
-----------------------------------------------------------------------------*/
Method getListaClassTribIs(cEffectiveDate as character) as logical class ConsumptionTaxData
    Local lSuccess as logical
    Local cQueryParameter as character
    Local cResult as character
    Default cEffectiveDate := dtos(dDataBase)

    //Monta a passagem dos parâmetros
    cQueryParameter := 'data='+transform(cEffectiveDate, '@R 9999-99-99')

    ::oRestGovServer:SetPath('/calculadora/dados-abertos/classificacoes-tributarias/imposto-seletivo')

    lSuccess := ::oRestGovServer:get(::aHeaderRT, cQueryParameter)

    cResult := self:encodeToCp1252(::oRestGovServer:GetResult())

    ::oResponseGovServer:FromJson(cResult)

return lSuccess

/*----------------------------------------------------------------------------
{Protheus.doc} getListaClassTribCbSIbs
@description Obtém a lista das classificações tributárias (cClassTrib) 
             cadastradas vigentes em um determinada data para CBS e IBS
@author Carlos Eduardo Nonato
@since 22/09/2025
@param cEffectiveDate - Data de vigência da alíquota no formato AAAAMMDD
@return lSuccess - .t. se a consulta foi realizada com sucesso (HTTP code 200)
-----------------------------------------------------------------------------*/
Method getListaClassTribCbSIbs(cEffectiveDate as character) as logical class ConsumptionTaxData
    Local lSuccess as logical
    Local cQueryParameter as character
    Local cResult as character
    Default cEffectiveDate := dtos(dDataBase)

    //Monta a passagem dos parâmetros
    cQueryParameter := 'data='+transform(cEffectiveDate, '@R 9999-99-99')

    ::oRestGovServer:SetPath('/calculadora/dados-abertos/classificacoes-tributarias/cbs-ibs')

    lSuccess := ::oRestGovServer:get(::aHeaderRT, cQueryParameter)

    cResult := self:encodeToCp1252(::oRestGovServer:GetResult())

    ::oResponseGovServer:FromJson(cResult)

return lSuccess

/*----------------------------------------------------------------------------
{Protheus.doc} getListaSituacaoTribCbSIbs
@description Obtém a lista das situações tributárias (cSituacaoTrib) 
             cadastradas vigentes em um determinada data para CBS e IBS
@author Carlos Eduardo Nonato
@since 22/09/2025
@param cEffectiveDate - Data de vigência da alíquota no formato AAAAMMDD
@return lSuccess - .t. se a consulta foi realizada com sucesso (HTTP code 200)
-----------------------------------------------------------------------------*/
Method getListaSituacaoTribCbSIbs(cEffectiveDate as character) as logical class ConsumptionTaxData
    Local lSuccess as logical
    Local cResult as character
    Local cQueryParameter as character
    Default cEffectiveDate := dtos(dDataBase)

    //Monta a passagem dos parâmetros
    cQueryParameter := 'data='+transform(cEffectiveDate, '@R 9999-99-99')

    ::oRestGovServer:SetPath('/calculadora/dados-abertos/situacoes-tributarias/cbs-ibs')

    lSuccess := ::oRestGovServer:get(::aHeaderRT, cQueryParameter)

    cResult := self:encodeToCp1252(::oRestGovServer:GetResult())

    ::oResponseGovServer:FromJson(cResult)

return lSuccess

/*----------------------------------------------------------------------------
{Protheus.doc} getListaSituacaoTribIs
@description Obtém a lista das situações tributárias (cSituacaoTrib) 
             cadastradas vigentes em uma determinada data para Is
@author Carlos Eduardo Nonato
@since 22/09/2025
@param cEffectiveDate - Data de vigência da alíquota no formato AAAAMMDD
@return lSuccess - .t. se a consulta foi realizada com sucesso (HTTP code 200)
-----------------------------------------------------------------------------*/
Method getListaSituacaoTribIs(cEffectiveDate as character) as logical class ConsumptionTaxData
    Local lSuccess as logical
    Local cQueryParameter as character
    Local cResult as character
    Default cEffectiveDate := dtos(dDataBase)

    //Monta a passagem dos parâmetros
    cQueryParameter := 'data='+transform(cEffectiveDate, '@R 9999-99-99')

    ::oRestGovServer:SetPath('/calculadora/dados-abertos/situacoes-tributarias/imposto-seletivo')

    lSuccess := ::oRestGovServer:get(::aHeaderRT, cQueryParameter)

    cResult := self:encodeToCp1252(::oRestGovServer:GetResult())

    ::oResponseGovServer:FromJson(cResult)

return lSuccess

/*----------------------------------------------------------------------------
{Protheus.doc} getInformacoesNcm
@description Obtém informações sobre a NCM em relação ao Imposto Seletivo
@author Carlos Eduardo Nonato
@since 22/09/2025
@param cCodeNcm - Código da NCM 
@param cEffectiveDate - Data de vigência da alíquota no formato AAAAMMDD
@return lSuccess - .t. se a consulta foi realizada com sucesso (HTTP code 200)
-----------------------------------------------------------------------------*/
Method getInformacoesNcm(cCodeNcm as character, cEffectiveDate as character) as logical class ConsumptionTaxData
    Local lSuccess as logical
    Local cQueryParameter as character
    Local cResult as character
    default cEffectiveDate := dtos(dDataBase)

    //Monta a passagem dos parâmetros
    if cCodeNcm != nil
        cQueryParameter := 'ncm=' + cCodeNcm + '&'
    endif    
    cQueryParameter += 'data='+transform(cEffectiveDate, '@R 9999-99-99')

    ::oRestGovServer:SetPath('/calculadora/dados-abertos/ncm')

    lSuccess := ::oRestGovServer:get(::aHeaderRT, cQueryParameter)

    cResult := self:encodeToCp1252(::oRestGovServer:GetResult())

    ::oResponseGovServer:FromJson(cResult)

return lSuccess

/*----------------------------------------------------------------------------
{Protheus.doc} getInformacoesNbs
@description Obtém informações sobre a NBS em relação ao Imposto Seletivo
@author Carlos Eduardo Nonato
@since 22/09/2025
@param cCodeNbs - Código da NBS
@param cEffectiveDate - Data de vigência da alíquota no formato AAAAMMDD
@return lSuccess - .t. se a consulta foi realizada com sucesso (HTTP code 200)
-----------------------------------------------------------------------------*/
Method getInformacoesNbs(cCodeNbs as character, cEffectiveDate as character) as logical class ConsumptionTaxData
    Local lSuccess as logical
    Local cQueryParameter as character
    Local cResult as character
    default cEffectiveDate := dtos(dDataBase)

    //Monta a passagem dos parâmetros
    if cCodeNbs != nil
        cQueryParameter := 'nbs=' + cCodeNbs + '&'
    endif    
    cQueryParameter += 'data='+transform(cEffectiveDate, '@R 9999-99-99')

    ::oRestGovServer:SetPath('/calculadora/dados-abertos/nbs')

    lSuccess := ::oRestGovServer:get(::aHeaderRT, cQueryParameter)

    cResult := self:encodeToCp1252(::oRestGovServer:GetResult())    

    ::oResponseGovServer:FromJson(cResult)

return lSuccess

/*----------------------------------------------------------------------------
{Protheus.doc} getVersao
@description Obtém a versão do aplicativo e do banco de dados
@author Carlos Eduardo Nonato
@since 22/09/2025
@return lSuccess - .t. se a consulta foi realizada com sucesso (HTTP code 200)
-----------------------------------------------------------------------------*/
Method getVersao() as logical class ConsumptionTaxData
    Local lSuccess as logical
    Local cResult as character

    ::oRestGovServer:SetPath('/calculadora/dados-abertos/versao')

    lSuccess := ::oRestGovServer:get(::aHeaderRT)

    cResult := self:encodeToCp1252(::oRestGovServer:GetResult())

    ::oResponseGovServer:FromJson(cResult)

return lSuccess

/*----------------------------------------------------------------------------
{Protheus.doc} getListaUfs
@description Obtém a lista das unidades federativas cadastradas
@author Carlos Eduardo Nonato
@since 22/09/2025
@return lSuccess - .t. se a consulta foi realizada com sucesso (HTTP code 200)
-----------------------------------------------------------------------------*/
Method getListaUfs() as logical class ConsumptionTaxData
    Local lSuccess as logical
    Local cResult as character
    ::oRestGovServer:SetPath('/calculadora/dados-abertos/ufs')

    lSuccess := ::oRestGovServer:get(::aHeaderRT)

    cResult := self:encodeToCp1252(::oRestGovServer:GetResult())
    
    ::oResponseGovServer:FromJson(cResult)

return lSuccess

/*---------------------------------------------------------------------------------
{Protheus.doc} getListaMunicipiosUf
@description Obtém a lista dos municípios cadastrados com base na sigla 
             de uma unidade federativa
@author Carlos Eduardo Nonato
@since 22/09/2025
@param cSiglaUf - Sigla da Unidade Federativa (UF)
@return lSuccess - .t. se a consulta foi realizada com sucesso (HTTP code 200)
----------------------------------------------------------------------------------*/
Method getListaMunicipiosUf(cSiglaUf as character) as logical class ConsumptionTaxData
    Local lSuccess as logical
    Local cQueryParameter as character
    Local cResult as character

    if cSiglaUf != nil
        cQueryParameter := 'siglaUf=' + cSiglaUf
    endif

    ::oRestGovServer:SetPath('/calculadora/dados-abertos/ufs/municipios')

    lSuccess := ::oRestGovServer:get(::aHeaderRT, cQueryParameter)

    cResult := self:encodeToCp1252(::oRestGovServer:GetResult())
    
    ::oResponseGovServer:FromJson(cResult)

return lSuccess

/*----------------------------------------------------------------------------
{Protheus.doc} getFundamentosLegais
@description Obtém a lista das fundamentações legais vigentes em um 
             determinada data
@author Carlos Eduardo Nonato
@since 22/09/2025
@param cEffectiveDate - Data de vigência da alíquota no formato AAAAMMDD
@return lSuccess - .t. se a consulta foi realizada com sucesso (HTTP code 200)
-----------------------------------------------------------------------------*/
Method getFundamentosLegais(cEffectiveDate as character) as logical class ConsumptionTaxData
    Local lSuccess as logical
    Local cQueryParameter as character
    Local cResult as character
    Default cEffectiveDate := dtos(dDataBase)

    //Monta a passagem dos parâmetros
    cQueryParameter := 'data=' + transform(cEffectiveDate, '@R 9999-99-99')

    ::oRestGovServer:SetPath('/calculadora/dados-abertos/fundamentacoes-legais')

    lSuccess := ::oRestGovServer:get(::aHeaderRT, cQueryParameter)

    cResult := self:encodeToCp1252(::oRestGovServer:GetResult())

    ::oResponseGovServer:FromJson(cResult)

return lSuccess

/*----------------------------------------------------------------------------
{Protheus.doc} getResponse
@description Retorna o objeto JSON com a resposta da última consulta
@author Carlos Eduardo Nonato
@since 22/09/2025
@return Objeto JSON com a resposta da última consulta
-----------------------------------------------------------------------------*/
Method getResponse() as json class ConsumptionTaxData
Local oReturn := JsonObject():new() as json

oReturn:FromJson(::oResponseGovServer:ToJson())

return oReturn

/*----------------------------------------------------------------------------
{Protheus.doc} Destroy
@description Método destrutor dos objetos da classe
@author Carlos Eduardo Nonato
@since 22/09/2025
-----------------------------------------------------------------------------*/
Method Destroy() class ConsumptionTaxData
    FreeObj(::oResponseGovServer)
    ::oResponseGovServer := nil
    
    FreeObj(::oRestGovServer)
    ::oRestGovServer := nil
Return

/*----------------------------------------------------------------------------
{Protheus.doc} self:encodeToCp1252
@description Converte uma string de UTF-8 para Windows-1252
@author Carlos Eduardo Nonato
@since 02/10/2025
@param cStringUtf8 - String em UTF-8 a ser convertida
@return String convertida para Windows-1252
-----------------------------------------------------------------------------*/
Method encodeToCp1252(cStringUtf8 as character) as character class ConsumptionTaxData
    Local cStringCp1252 := cStringUtf8 as character

    ::oUnicode:ConvertEncoding(@cStringCp1252, 'utf-8', 'cp1252')

return cStringCp1252    
