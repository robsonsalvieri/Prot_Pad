#Include 'protheus.ch'

namespace backoffice.fiscal.escrita.documentos

Static lModeloNFSENacional:= SuperGetMV("MV_NFSENAC",.F.,"1") == '2' as Logical //- "2-NFS-e Nacional", "1-NFS-e Prefeitura" o Default conforme Fouglas seria 1
Static lMTMODNOT          := ExistBlock("MTMODNOT",,.T.)             as logical
Static nTamDocGrupo       := TamSx3('F2_DOC')[1]                     as numeric
Static nTamSerieGrupo     := TamSx3('F2_SERIE')[1]                   as numeric
//------------------------------------------------------------------------------
//-  RetTypeDoc()
//- 	Função responsável por retornar o tamanho do modelo ou da serie do documento
//- 	@type 		Function
//- 	@author		Nilton Rodrigues
//- 	@since 		27/11/2024
//- 	@Param		cModelo - indica o modelo de documento fiscal 
//-                 nTipo   - Indica qual tamanho será retornado
//-                    1 - Tamanho do campo documento
//-                    2 - Tamanho do campo série
//-                    3 - código do modelo do documento
//-                    4 - opções de comboBox
//-                 lEmissor - indica se para a operação 1 ou 2 está gerando 
//-                 pelo sistema a numeração de nota fiscal.
//- 	@return		Tamanho de acordo com a opção informada
//------------------------------------------------------------------------------
Function RetTypeDoc(cModelo as character, nTipo as numeric, lEmissor as logical)
	Local xRet
	Local oJsonModelo      := JsonObject():New()    as object
	Local aRet             := {}                    as array
	Local nTamDocDefault   := 9                     as numeric
	Local nTamSerieDefault := 3                     as numeric
	Local nX                                        as numeric
	Local cCodigoBranco    := Space(2)              as character 

	cModelo := Alltrim(cModelo)

	//----------------------------------------------------------------------------------------------------------------------------
	//- Tabela Documentos Fiscais do ICMS
	//- https://www.sped.fazenda.gov.br/spedtabelas/AppConsulta/publico/aspx/ConsultaTabelasExternas.aspx?CodSistema=SpedFiscal
	//----------------------------------------------------------------------------------------------------------------------------
	//- Json com a definição dos tamanho de cada modelo de documento
	//- O array dentro do json possui a seguinte configuração
	//- [1] = Tamanho do documento
	//- [2] = Tamanho da série
	//- [3] = codigo do modelo da NF  de acordo com o Convenio 31/99.
	//- [4] = Opção de comboBox
	//- [5] = Indica se Nota fiscal deve ajustar tamanho em função do grupo (serviço)
	//----------------------------------------------------------------------------------------------------------------------------
    oJsonModelo["ACT"   ] := {nTamDocDefault,nTamSerieDefault,"24","Autorização de Carregamento e Transporte",.F.}
    oJsonModelo["BPA"   ] := {nTamDocDefault,nTamSerieDefault,"14","Bilhete de Passagem Aquaviário"          ,.F.}
    oJsonModelo["BPE"   ] := {nTamDocDefault,nTamSerieDefault,"63","Bilhete de passagem eletrônico - BP-e"   ,.F.}
    oJsonModelo["BPECF" ] := {nTamDocDefault,nTamSerieDefault,"2E","Bilhete de Passagem emitido por ECF"     ,.F.}
    oJsonModelo["BPF"   ] := {nTamDocDefault,nTamSerieDefault,"16","Bilhete de Passagem Ferroviário"         ,.F.}
    oJsonModelo["BPNB"  ] := {nTamDocDefault,nTamSerieDefault,"15","Bilhete de Passagem e Nota de Bagagem"   ,.F.}
    oJsonModelo["BPR"   ] := {nTamDocDefault,nTamSerieDefault,"13","Bilhete de passagem rodoviário"          ,.F.}
    oJsonModelo["BRPA"  ] := {nTamDocDefault,nTamSerieDefault,"30","Bilhete/Recibo do Passageiro"            ,.F.}
    oJsonModelo["CA"    ] := {nTamDocDefault,nTamSerieDefault,"10","Conh. Aéreo"                             ,.F.}
    oJsonModelo["CF"    ] := {nTamDocDefault,nTamSerieDefault,"02","Cupon Fiscal"                            ,.F.}
    oJsonModelo["CFECF" ] := {nTamDocDefault,nTamSerieDefault,"2D","Cupom Fiscal emitido por ECF"            ,.F.}
    oJsonModelo["CFEE"  ] := {nTamDocDefault,nTamSerieDefault,"60","Cupom Fiscal Eletrônico CF-e-ECF"        ,.F.}
    oJsonModelo["CTA"   ] := {nTamDocDefault,nTamSerieDefault,"09","Conh.Transp. Aquaviário de Cargas"       ,.F.}
    oJsonModelo["CTCA"  ] := {nTamDocDefault,nTamSerieDefault,"8B","Conh.Transp. Cargas Avulso"              ,.F.}
    oJsonModelo["CTE"   ] := {nTamDocDefault,nTamSerieDefault,"57","Conh.Transp. Eletrônico"                 ,.F.}
    oJsonModelo["CTEOS" ] := {nTamDocDefault,nTamSerieDefault,"67","Conh.Transp. Eletrônico - CT-e OS"       ,.F.}
    oJsonModelo["CTF"   ] := {nTamDocDefault,nTamSerieDefault,"11","Conh.Transp.Ferroviario de Cargas"       ,.F.}
    oJsonModelo["CTM"   ] := {nTamDocDefault,nTamSerieDefault,"26","Conh.Transp.Multimodal"                  ,.F.}
    oJsonModelo["CTR"   ] := {nTamDocDefault,nTamSerieDefault,"08","Conh.Transp.Rodoviario de Cargas"        ,.F.}
    oJsonModelo["DT"    ] := {nTamDocDefault,nTamSerieDefault,"17","Despacho de Transporte"                  ,.F.}
    oJsonModelo["ECF"   ] := {nTamDocDefault,nTamSerieDefault,"02","Cupon Fiscal gerado pelo SIGALOJA"       ,.F.}
    oJsonModelo["GNRE"  ] := {nTamDocDefault,nTamSerieDefault,"23","GNRE"                                    ,.F.}
    oJsonModelo["MC"    ] := {nTamDocDefault,nTamSerieDefault,"25","Manifesto de Carga"                      ,.F.}
    oJsonModelo["NF"    ] := {nTamDocDefault,nTamSerieDefault,"01","Nota Fiscal"                             ,.F.}
    oJsonModelo["NF3E"  ] := {nTamDocDefault,nTamSerieDefault,"66","NF Energia Eletrica Eletrônica"          ,.F.}
    oJsonModelo["NFA"   ] := {nTamDocDefault,nTamSerieDefault,"1B","NF Avulsa"                               ,.F.}
    oJsonModelo["NFCE"  ] := {nTamDocDefault,nTamSerieDefault,"65","NF Eletrônica Consumidor Final NFC-e"    ,.F.}
    oJsonModelo["NFCEE" ] := {nTamDocDefault,nTamSerieDefault,"06","Nota Fiscal/Conta de Energia Elétrica"   ,.F.}
    oJsonModelo["NFCF"  ] := {nTamDocDefault,nTamSerieDefault,"02","NF de venda a Consumidor Final"          ,.F.}
    oJsonModelo["NFCFG" ] := {nTamDocDefault,nTamSerieDefault,"28","NF/conta de fornecimento de gas"         ,.F.}
    oJsonModelo["NFCOM" ] := {nTamDocDefault,nTamSerieDefault,"62","NF Fatura de Serviços de Comunicação"    ,.F.}
    oJsonModelo["NFE"   ] := {nTamDocDefault,nTamSerieDefault,"01","NF Entrada"                              ,.F.}
    oJsonModelo["NFFA"  ] := {nTamDocDefault,nTamSerieDefault,"29","NF/Conta de fornecimento de Água"        ,.F.}
    oJsonModelo["NFP"   ] := {nTamDocDefault,nTamSerieDefault,"04","NF de Produtor"                          ,.F.}
    oJsonModelo["NFSC"  ] := {nTamDocDefault,nTamSerieDefault,"21","NF Servico de Comunicacao"               ,.F.}
    oJsonModelo["NFST"  ] := {nTamDocDefault,nTamSerieDefault,"07","NF Servico de Transporte"                ,.F.}
    oJsonModelo["NFTFC" ] := {nTamDocDefault,nTamSerieDefault,"27","NF Transp.Ferroviário de Cargas"         ,.F.}
    oJsonModelo["NTSC"  ] := {nTamDocDefault,nTamSerieDefault,"21","NF Servico de Comunicacao"               ,.F.}
    oJsonModelo["NTST"  ] := {nTamDocDefault,nTamSerieDefault,"22","NF Servico de Telecomunicacoes"          ,.F.}
    oJsonModelo["OCC"   ] := {nTamDocDefault,nTamSerieDefault,"20","Ordem de Coleta de Carga"                ,.F.}
    oJsonModelo["RMD"   ] := {nTamDocDefault,nTamSerieDefault,"18","Resumo Movimento Diario"                 ,.F.}
    oJsonModelo["SATCE" ] := {nTamDocDefault,nTamSerieDefault,"59","Cupom Fiscal Eletrônico - SAT"           ,.F.}
    oJsonModelo["SPED"  ] := {nTamDocDefault,nTamSerieDefault,"55","NF Eletrônica do SEFAZ."                 ,.F.}
	//-----------------------------------------------------------------------------------------------------------------------------
	//- Os tipos abaixo não possuem código na Tabela Documentos Fiscais do ICMS
	//-----------------------------------------------------------------------------------------------------------------------------
	//oJsonModelo["3A"    ] := {nTamDocDefault,nTamSerieDefault,cCodigoBranco,"NF de Servico Simplificada"     ,.F.}
	oJsonModelo["NFPS"  ] := {nTamDocDefault,nTamSerieDefault,cCodigoBranco,"NF Prestacao de Servico"        ,.F.}
	oJsonModelo["NFS"   ] := {nTamDocDefault,nTamSerieDefault,cCodigoBranco,"NF Servico"                     ,.F.}
	oJsonModelo["NFSE"  ] := {nTamDocDefault,nTamSerieDefault,cCodigoBranco,"NF Servico Eletrônica"          ,.T.}
	oJsonModelo["RPS"   ] := {nTamDocDefault,nTamSerieDefault,cCodigoBranco,"Recibo Provisorio de Servicos"  ,.F.}

	//------------------------------------------------------------------------------
	//- montagem das opções de comboBox
	//------------------------------------------------------------------------------
	If nTipo == 4
		aRet := oJsonModelo:GetNames()
		xRet := ""
		For nX := 1 to Len(aRet)-1
			xRet += aRet[nX]+'='+oJsonModelo[aRet[nX]][4]+';'
		Next nX
		//------------------------------------------------------------------------------
		//- adiciona o ultimo registro do array na composição do Combo
		//------------------------------------------------------------------------------
		nX := Len(aRet)
		xRet += aRet[nX]+'='+oJsonModelo[aRet[nX]][4]
	Else
		//------------------------------------------------------------------------------
	    //- usado para o modelo não fiscal
        //- uso interno, não deve ser documentado ou utilizado em operações que gerem livros fiscais
		//------------------------------------------------------------------------------
        oJsonModelo["DOCNF" ] := {nTamDocDefault,nTamSerieDefault,"ZZ","Documento Não Fiscal.",.F.}
		//------------------------------------------------------------------------------
		//- Inicializa o default
		//------------------------------------------------------------------------------
		If nTipo == 3
			xRet := '01'
		ElseIf nTipo == 1
			xRet := nTamDocDefault
		ElseIf nTipo == 2
			xRet := nTamSerieDefault
		EndIf
		//----------------------------------------------
		//- verifica a existência do modelo informado
		//----------------------------------------------
		If !(aRet := oJsonModelo[cModelo]) == nil
			//------------------------------------------------------------------------------------------------
			//- tratamento de execução de notas de serviço
			//- TSS MV_NFSENAC - INDICA O USO DA MODALIDADE DA PREFEITURA SE É OU NÃO PROVEDOR OU NACIONAL
			//- Esse parâmetro será atualizado de forma automática pelo TSS
			//------------------------------------------------------------------------------------------------
			If lEmissor .and. nTipo <= 2 .and. aRet[5]
				//------------------------------------------------------------------------------------------------
				//- Quando for nota fiscal Nacional é forçado o ajuste do tamanho do campo/serie, ou seja, 
				//- se for nota fiscal de serviço nacional ajusta o tamanho do grupo do campo, imaginando 
				//- que o cliente tenha ajustado aos tamanhos maximos permitido para atender a legislação
				//------------------------------------------------------------------------------------------------
				If lModeloNFSENacional
					aRet[1] := nTamDocGrupo
					aRet[2] := nTamSerieGrupo
				EndIf

			EndIf

			xRet := aRet[nTipo]

			//---------------------------------------------------------------------------------------------------------------------------
			//- Conforme consulta realizada ao SEFAZ-MG (chamado TILOA6), NF Avulsa deve ter o modelo 01 e não 1B dessa forma
			//- executo o ponto de entrada MTMODNOT somente para a espécie NFA no estado de MG verificando se a chamada é do MATA940
			//---------------------------------------------------------------------------------------------------------------------------
			If nTipo == 3 .and. lMTMODNOT .and. cModelo == "NFA" .and. SuperGetMV("MV_ESTADO") == "MG"
				xRet := IsNotaFiscalAvulsaMGValida(xRet, "MATA940")
			EndIf
		ElseIf nTipo == 3 .and. lMTMODNOT
			xRet := Execblock("MTMODNOT",.F.,.F.,cModelo)
		EndIf
	EndIf
	FwFreeArray(aRet)

	FwFreeObj(oJsonModelo)

Return xRet

//------------------------------------------------------------------------------
//- IsNotaFiscalAvulsaMGValida()
//- Função responsável executar o ponto de entrada MTMODNOT somente para a rotina especificada
//- @type Function
//- @author Rafael Oliveira
//- @since 31/05/2025
//- @param cModeloRet - Modelo do documento retornado
//- @param cRotina - Nome da rotina que está chamando a função
//- @return Modelo do documento retornado
//------------------------------------------------------------------------------
Function IsNotaFiscalAvulsaMGValida(cModeloRet as character, cRotina as character) as character

Local cRet := "1B" as character
	
	If IsInCallStack(cRotina)
		cRet := Execblock("MTMODNOT",.F.,.F.,cModeloRet)
	Endif

Return cRet
