#INCLUDE "Matr955.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³FUN‡…O    ³ MATR955  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 23/06/95   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³DESCRI‡…O ³Emiss„o da Lista de Codigo de Emitentes Modelo P10            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³SINTAXE   ³ MATR955(void)                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ USO      ³ Generico                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Marcos Simidu³05/01/98³13678A³Impressao loja Cli/Fornec.                ³±±
±±³ Edson   M.   ³09/03/98³XXXXXX³Acerto da comprssao.                      ³±±
±±³ Marcos Simidu³18/01/98³XXXXXX³Ordem Alfa.-Lista fornecedores de mesmo   ³±±
±±³              ³        ³XXXXXX³de mesmo nome mas de CGC diferentes.      ³±±
±±³ Marcos Simidu³18/01/98³XXXXXX³Ordem Cod.-Lista fornec.varias vezes.     ³±±
±±³ Andreia      ³05/11/99³24340a³Troca de CGC por CNPJ.                    ³±±
±±³ Mauro Vajman ³26/07/06³      ³Adaptacao para execucao sobre varias      ³±±
±±³              ³        ³      ³filiais, a criterio do usuario            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Matr955
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local 	wnrel
Local 	lVerpesssen := Iif(FindFunction("Verpesssen"),Verpesssen(),.T.)

Private	lEnd	:=	.F.
Private tamanho	:=	"M"
Private	limite 	:=	132
Private	titulo	:=	STR0001 //"Lista de C¢digos de Emitentes"
Private	nomeprog:=	"MATR955"

Private	aReturn :=	{STR0004,1,STR0005,2,2,1,"",1} //"Zebrado"###"Administra‡„o"

Private	cDesc1	:=	STR0002 //"Este relat¢rio imprimir  a Lista de C¢digos de Emitentes"
Private	cDesc2	:=	STR0003 //"Modelo P10 ,conforme os parƒmetros informados."
Private	cDesc3	:=	""
Private	cPerg   :=	"MTR955"
Private	cString	:=	"SF3"

Private nLastKey:=	0

If lVerpesssen
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	wnrel := "MATR955"
	wnrel := SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.F.,"",.T.,Tamanho)
	If nLastKey==27
		dbClearFilter()
		Return
	Endif
	SetDefault(aReturn,cString)
	If nLastKey==27
		dbClearFilter()
		Return
	Endif

	RptStatus({|lEnd| R955Imp(@lEnd,wnRel,cString,Tamanho)},titulo)

	dbClearFilter()

	If aReturn[5] == 1
		Set Printer To
		dbCommitAll()
		OurSpool(wnrel)
	Endif

	MS_FLUSH()
EndIf

Return   

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R955Imp  ³ Autor ³ Juan Jose Pereira     ³ Data ³ 18.12.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime Relatorio                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R955Imp(lEnd,wnRel,cString,Tamanho)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local	aCampos 	:=	{}
Local 	aTam		:=	{}

Local	cChave	    :=	"" 
Local	cAlias		:=	Alias()
Local	cSeek		:=	""

Local 	lServico 	:= 	.T.
Local 	cFilterUser	:=	aReturn[7]
Local	cName		:=	""
Local	cQryAd		:=	""

#IFDEF TOP
	Local aStruSF3	:=	{}
	Local cQuery	:=	""
	Local nX		:=	0
	Local lFirstFil	:= .T.
#ELSE
	Local cCondicao :=	""
#ENDIF

Local aFilsCalc := {}
Local nForFilial
Local cFilBack := cFilAnt
Local cCod := ""
Local nCol := 0
Private cAliasSF3	:=	"SF3"
Private lQuery 	:= 	.F.
Private cIndex	:=	""

Private cabec1
Private cabec2
Private cabec3
Private cNomArq            

Private CbTxt
Private CbCont

Private lFirstPage	:=	.T.
Private li	:=	80
PRIVATE	nLimPag	     :=58
Private nPg	:= 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega parametros                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MTR955",.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01             // Da Data                              ³
//³ mv_par02             // Ate a Data                           ³
//³ mv_par03             // Livro Selecionado                    ³
//³ mv_par04             // Pagina Inicial                       ³
//³ mv_par05             // Ordem ? Alfabetica/Codigo            ³
//³ mv_par06             // Considera NF Servico ?               ³
//³ mv_par07             // Seleciona filial? (1=Sim/2=Nao)      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dDtIni		:=	mv_par01
dDtFim		:=	mv_par02
cNrLivro	:=	mv_par03
nPg			:=	IIf(Empty(mv_par04),1,mv_par04)
nOrdem		:=	mv_par05
lServico 	:= (MV_PAR06==1)     
lMovimento:=.F.  //controla se houve movimento no periodo   
               
#IFDEF TOP
	aFilsCalc := MatFilCalc( mv_par07 == 1, , , (mv_par07 == 1 .and. mv_par08 == 1), , 2 )
#ELSE        
	aFilsCalc := MatFilCalc( mv_par07 == 1 )
#ENDIF

If aFilsCalc == {}
  Return
EndIf  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria array para gerar arquivo de trabalho                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aTam	:=	TamSX3("A2_COD")
AADD(aCampos,{ "CODIGO"	,"C",aTam[1],aTam[2]})
aTam	:=	TamSX3("A2_LOJA")
AADD(aCampos,{ "LOJA"	,"C",aTam[1],aTam[2]})
AADD(aCampos,{ "NOME"  ,"C",30, 0 } )
aTam	:=	TamSX3("A2_EST")
AADD(aCampos,{ "UF"    ,"C",atam[1],aTam[2] } )
aTam	:=	TamSX3("A2_CGC")
AADD(aCampos,{ "CGC"	  ,"C",atam[1],aTam[2] } )
aTam	:=	TamSX3("A2_INSCR")
AADD(aCampos,{ "INSCRI","C",atam[1],aTam[2] } )

For nForFilial := 1 To Len( aFilsCalc )

	If aFilsCalc[ nForFilial, 1 ]
	
		cFilAnt := aFilsCalc[ nForFilial, 2 ]
					
		#IFDEF TOP
			If lFirstFil .or. MV_PAR08 == 2					
		#ENDIF
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Cria arquivo de trabalho                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cNomArq :=	CriaTrab(aCampos)
		dbUseArea( .T.,, cNomArq,"TRB", if(.T. .OR. .F., !.F., NIL), .F. )
		cChave	:=	If(nOrdem==1,"NOME+CGC","CODIGO+LOJA+CGC")
		IndRegua("TRB",cNomArq,cChave,,,STR0006) //"Selecionando Registros..."
		#IFDEF TOP
				lFirstFil	:= .F.
			EndIf		
		#ENDIF
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Posiciona arquivos SA1/SA2.                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SA1")
		dbSetOrder(1)
		dbSelectArea("SA2")
		dbSetOrder(1)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Processando as movimentacoes.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SF3")
		dbSetOrder(4)
		ProcRegua(LastRec())
		#IFDEF TOP
		    If TcSrvType()<>"AS/400"
			    lQuery 		:= .T.
				cAliasSF3	:= "SF3_MTR955"
				aStruSF3	:= SF3->(dbStruct())
				cQuery		:= "SELECT SF3.F3_FILIAL,SF3.F3_CLIEFOR,SF3.F3_LOJA,SF3.F3_NFISCAL,SF3.F3_SERIE, "

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Insere os campos do filtro na query³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !Empty(aReturn[7])
					For nX := 1 To SF3->(FCount())
						cName := SF3->(FieldName(nX))
						If AllTrim( cName ) $ aReturn[7]
					    	If aStruSF3[nX,2] <> "M"  
					    		cName := "SF3." + cName
					    		If !cName $ cQuery .And. !cName $ cQryAd
						      		cQryAd += cName +","
						       	Endif 	
						   	EndIf
						EndIf 			       	
					Next nX
				Endif    
				
				cQuery		+= cQryAd + "SF3.F3_TIPO "
		
				cQuery 		+= "FROM " + RetSqlName("SF3") + " SF3 "
				cQuery 		+= "WHERE SF3.F3_FILIAL = '" + xFilial("SF3") + "' AND "				
				cQuery 		+= "SF3.F3_ENTRADA >= '" + Dtos(dDtIni) + "' AND "
				cQuery 		+= "SF3.F3_ENTRADA <= '" + Dtos(dDtFim) + "' AND "	
				If !(lServico) 
					cQuery	+= "SF3.F3_TIPO <> 'S' AND "
				Endif
				cQuery 		+= "F3_DTLANC = '" + Dtos(Ctod("")) + "' AND "
				If cNrLivro <> "*"
					cQuery 	+= "SF3.F3_NRLIVRO = '" + cNrLivro + "' AND "
				EndIf	
				cQuery 		+= "SUBSTRING(SF3.F3_CFO,1,1) < '5' AND "							
				cQuery 		+= "SF3.D_E_L_E_T_ = ' ' "
				cQuery 		+= "ORDER BY " + SqlOrder(SF3->(IndexKey()))
				cQuery 		:= ChangeQuery(cQuery)                       
				
				If Select(cAliasSF3) > 0
					dbSelectArea(cAliasSF3)
					dbCloseArea()
				EndIf				
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF3)
				For nX := 1 To len(aStruSF3)
					If aStruSF3[nX][2] <> "C" .And. FieldPos(aStruSF3[nX][1])<>0
						TcSetField(cAliasSF3,aStruSF3[nX][1],aStruSF3[nX][2],aStruSF3[nX][3],aStruSF3[nX][4])
					EndIf
				Next nX
				
				dbSelectArea(cAliasSF3)	
			Else
		#ENDIF
			    cIndex    := CriaTrab(NIL,.F.)
			    cCondicao := 'F3_FILIAL == "' + xFilial("SF3") + '" .And. '
			   	cCondicao += 'DTOS(F3_ENTRADA) >= "' + DTOS(dDtIni) + '" .And. DTOS(F3_ENTRADA) <= "' + DTOS(dDtFim) + '" '
				If !(lServico) 
					cCondicao += '.And. F3_TIPO <> "S" '
				Endif
				cCondicao += '.And. Empty(F3_DTLANC) '
				If cNrLivro <> "*"
					cCondicao += '.And. F3_NRLIVRO == "' + cNrLivro + '" '
				EndIf	                                                        
				cCondicao	+= '.And. SubStr(F3_CFO,1,1) < "5" '
					   	
			    IndRegua(cAliasSF3,cIndex,SF3->(IndexKey()),,cCondicao)
			    dbSelectArea(cAliasSF3)
			    ProcRegua(LastRec())
			    dbGoTop()
		#IFDEF TOP
			Endif    
		#ENDIF
		
		Do While !((cAliasSF3)->(Eof()))
		
			IncProc(STR0006)
			
			If Interrupcao(@lEnd)
				Exit
			Endif
		         
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Considera filtro do usuario                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lQuery .And. !Empty(cFilterUser)
				dbSelectArea(cAliasSF3)
				If !(&cFilterUser)
					(cAliasSF3)->(dbSkip())
					Loop   
				Endif
			Endif

			If (cAliasSF3)->F3_TIPO $ "DB"
				SA1->(dbSeek(F3Filial("SA1")+(cAliasSF3)->F3_CLIEFOR+(cAliasSF3)->F3_LOJA))
				If nOrdem == 1
					cSeek := Substr(SA1->A1_NOME,1,30)+SA1->A1_CGC
				Else
					cSeek := SA1->A1_COD+SA1->A1_LOJA+SA1->A1_CGC
				Endif
				If !(TRB->(dbSeek(cSeek)))
					RecLock("TRB",.T.)
					TRB->CODIGO	:= SA1->A1_COD
					TRB->LOJA	:= SA1->A1_LOJA
					TRB->NOME	:= Substr(SA1->A1_NOME,1,30)
					TRB->UF		:= SA1->A1_EST
					TRB->CGC	:= SA1->A1_CGC
					TRB->INSCRI	:= SA1->A1_INSCR
				EndIf
			Else             
				SA2->(dbSeek(F3Filial("SA2")+(cAliasSF3)->F3_CLIEFOR+(cAliasSF3)->F3_LOJA))
				If nOrdem==1
					cSeek := Substr(SA2->A2_NOME,1,30)+SA2->A2_CGC
				Else
					cSeek := SA2->A2_COD+SA2->A2_LOJA+SA2->A2_CGC
				Endif
				If !(TRB->(dbSeek(cSeek)))
					RecLock("TRB",.T.)
					TRB->CODIGO	:= SA2->A2_COD
					TRB->LOJA	:= SA2->A2_LOJA
					TRB->NOME	:= Substr(SA2->A2_NOME ,1,30)
					TRB->UF		:= SA2->A2_EST
					TRB->CGC	:= SA2->A2_CGC
					TRB->INSCRI	:= SA2->A2_INSCR
				Endif
			EndIf 
			lMovimento:=.T.
			(cAliasSF3)->(dbSkip())
			
		EndDo          
		
		// Chama função de impressão        
		#IFDEF TOP
   			If MV_PAR08 == 2
		#ENDIF
		_PrintP10()
		#IFDEF TOP
  			EndIf
		#ENDIF
 	
	Endif
	
 	If FWModeAccess("SF3",3)=="C"  .And. aFilsCalc[ nForFilial, 1 ]
  		Exit
 	EndIf    
	
Next nForFilial

// Chama função de impressão somente se for consolidado
#IFDEF TOP
If MV_PAR08 == 1
	_PrintP10()
EndIf
#ENDIF


// Restaura a Filial Corrente
cFilAnt := cFilBack

dbSelectArea(cAlias)
dbSetOrder(1)

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ _PrintP10    ³Autor ³ LMDT Consultoria     ³Data³ 27/03/14 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Realiza impressão da filial ou do grupo                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR930,MATR931                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function _PrintP10()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas na segunda regua de processamento - impressao       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TRB")
dbGotop()
SetRegua(LastRec())

Do While !Eof() .and. !lEnd
	IncRegua()
	If Interrupcao(@lEnd)
		Exit
	EndIf

	If li > 55
		Cabecalho(cFilAnt)
	EndIf
	
	cCod := CODIGO+" / "+LOJA
	nCol := Round((25 - Len(cCod)) / 2,0)
	If nCol < 1
		cCod := Substr(CODIGO+"/"+LOJA,1,25)
		nCol := 1
	EndIf
	
	@ Li,000 pSay "|"
	@ Li,nCol pSay cCod
	@ Li,026 pSay "|"
	@ Li,028 pSay Substr(NOME,1,37)
	@ Li,063 pSay "|"
	@ Li,069 pSay UF
	@ Li,077 pSay "|"
	@ Li,083 pSay CGC		Picture If(Len(Alltrim(CGC))==14,"@R! NN.NNN.NNN/NNNN-99","@R 999.999.999-99")
	@ Li,104 pSay "|"
	@ Li,109 pSay INSCRI	Picture PesqPict("SA2","A2_INSCR")
	@ Li,131 pSay "|"
	Li++
	dbSkip()
EndDo

If lMovimento== .F.
	Cabecalho(cFilAnt)
   	cLinha:=FmtLin(Array(5),"|#########################|####################################|#############|##########################|##########################|",,,Li,.F.)
	R95xFillPage(Limite,cLinha,STR0017,@Li,nLimPag) //"*** NAO HOUVE MOVIMENTO ***"
Endif

If !li==80 .and. !lEnd
	@ li,00 pSay __PrtThinLine()
EndIf                    

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Fecha as areas de trabalho³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TRB")
dbCloseArea()
Ferase(cNomarq+GetDBExtension())
Ferase(cNomarq+OrdBagExt())

If lQuery
	dbSelectArea(cAliasSF3)
	dbCloseArea()
Else
   	dbSelectArea("SF3")
	RetIndex("SF3")
	dbClearFilter()
	Ferase(cIndex+OrdBagExt())
Endif
Li := 80 //Quebra pagina por filial 	

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R93xFillPage ³Autor ³ Juan Jose Pereira    ³Data³ 03/12/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Preenche pagina sem movimento                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR930,MATR931                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION R95xFillPage(cTamanho,cLayOut,cMsg,nLin,nLimPag)

Local nTamx,cFiller,j,cImprime

cImprime:=Stuff(cLayOut,2,Len(cMsg),cMsg)
FmtLin(,cImprime,,,@nLin)
nTamx:=Int(cTamanho/(nLimPag-nLin))
cFiller:=Replic("*",nTamx)
j:=5
while nLin<nLimPag 
	cImprime:=Stuff(cLayOut,j,Len(cFiller),cFiller)
	FmtLin(,cImprime,,,@nLin)
	j:=j+nTamx
end

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³CABECALHO ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 23/06/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Emiss„o do Cabecalho p/ Lista de Codigo de Emitentes        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Cabecalho()  	                                       	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Cabecalho(cFilRpt)
Local cCgc := ""

If !lFirstPage
	@ Li,00 pSay __PrtThinLine()
Else
	lFirstPage:=.f.
EndIf

cCgc := Posicione("SM0",1,cEmpAnt+cFilRpt,"M0_CGC")   
Li := 0             

@ Li++,00 	pSay AvalImp(132)
@ Li,00		pSay __PrtThinLine()
Li++  
@ Li,00 	pSay STR0007 //"|                                                   LISTA DE CODIGOS DE EMITENTES"
@ Li,131 	pSay "|"
Li++
@ Li,00 	pSay STR0008+SM0->M0_NOMECOM //"| FIRMA : "
@ Li,131 	pSay "|"
Li++
@ Li,00 	pSay STR0009+InscrEst() //"| INSC. EST. : "
@ Li,40 	pSay STR0010+TRANSFORM(cCgc,"@R! NN.NNN.NNN/NNNN-99") //"C.N.P.J. : " 
@ Li,131 	pSay "|"
Li++
@ Li,00 	pSay STR0011+TRANSFORM(nPg,"@E 9,99999999") //"| FOLHA: "
@ Li,40 	pSay STR0012+ DTOC(dDtIni)+STR0013+DTOC(dDtFim) //"PERIODO : "###" ATE "
@ Li,131 	pSay "|"
Li++
@ Li,00 	pSay __PrtThinLine()
//		                    0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19       200       210       220
//     		                01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
Li++
@ Li,00     pSay STR0014 //"|        CODIGO DO        |    EMITENTE DO DOCUMENTO FISCAL    |   UNIDADE   |   INSCRICAO NO C.N.P.J.  |         INSCRICAO        |"
Li++
@ Li,00 	pSay STR0015 //"|        EMITENTE         |                                    |     DA      |                          |          ESTADUAL        |"
Li++
@ Li,00 	pSay STR0016 //"|                         |                                    |  FEDERACAO  |                          |                          |"
Li++
@ Li,00 	pSay           "--------------------------+------------------------------------+-------------+--------------------------+---------------------------"
Li++
//		        0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19       200       210       220
//     		  01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
nPg++

Return
