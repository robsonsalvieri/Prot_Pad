#INCLUDE "Matr975.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR975  ³ Autor ³ Juan Jose Pereira     ³ Data ³ 15.10.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Relacao Trimestral das Operacoes Interestaduais            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MATR975(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MATR975()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local	wnrel		:=	"MATR975"  
Local	tamanho		:=	"G" 
Local	titulo		:=	STR0001 //"Listagem de Opera‡oes Interestaduais"
Local	cDesc1		:=	STR0002 //"Emiss„o da Listagem de Opera‡oes Interestaduais por Estado"
Local	cDesc2		:=	STR0003 //"Modelo P12"
Local	cDesc3		:=	""
Local 	lVerpesssen := Iif(FindFunction("Verpesssen"),Verpesssen(),.T.)

Local cRelease as character
Local cEndWeb		:= "https://tdn.totvs.com/display/PROT/Tabela+SFT+-+Livro+Fiscal+por+Item+de+NF"

Private aReturn 	:= { STR0004, 1, STR0005, 2, 2, 1, "", 1 } //"Zebrado"###"Administra‡„o"
Private nomeprog	:=	"MATR975"
Private cPerg   	:=	"MTR975"
Private cString		:=	"SF3" 

cRelease 	:=  GetRPORelease()     

If !IsBlind() 
	If FindFunction("DlgRelVer")
		DlgRelVer("MATR975","Relatorio Lancamentos fiscais",cEndWeb )
	EndIf
EndIf
             

If lVerpesssen
	Pergunte(cPerg,.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                               ³
	//³ mv_par01             // da Data                                    ³
	//³ mv_par02             // ate a Data                                 ³ 
	//³ mv_par03             // do Estado                                  ³ 
	//³ mv_par04             // ate Estado                                 ³ 
	//³ mv_par05             // Livro Selecionado                          ³
	//³ mv_par06             // Numero Pagina inicial                      ³ 
	//³ mv_par07             // Qtd Paginas/Feixe                          ³ 
	//³ mv_par08             // Quebra Numeracao? Nao Quebra / Por Estado  ³ 
	//³ mv_par09             // Imprime devolucoes? Sim / Nao              ³ 
	//³ mv_par10             // Listar Estado     ? Sim / Nao              ³ 
	//³ mv_par11             // Estado Origem/Dest?                        ³ 
	//³ mv_par12             // Seleciona filiais?                         ³  
	//³ mv_par12             // Aglutina Seleção de filiais por CNPJ+IE?   ³ 
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nLastKey :=0
	wnrel    :=SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.F.,"",.F.,Tamanho,,.F.)

	If nLastKey == 27
		dbClearFilter()
		Return
	Endif

	SetDefault(aReturn,cString)

	If nLastKey == 27
		dbClearFilter()
		Return
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Recebe valores dos Parametros                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Pergunte(cPerg,.F.)
	Private	dDtIni		:=	mv_par01
	Private	dDtFim		:=	mv_par02

	Private	cEstIni		:=	mv_par03
	Private	cEstFim		:=	mv_par04
	Private	cLivro		:=	mv_par05
	Private	cEstDest	:=	mv_par11

	Private	nPagIni		:=	mv_par06
	Private	nQtFeixe	:=	mv_par07

	Private	lQuebra		:=	(mv_par08==2)
	Private	lDevolucoes :=	(mv_par09==1)
	Private	lListEst	:=	(mv_par10==1)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Executa relatorio                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RptStatus({|lEnd| R975Imp(@lEnd,wnRel,cString,Tamanho)},titulo)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Restaura Ambiente                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SF3")
	dbSetOrder(1)
	dbClearFilter()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Spool de Impressao                                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If aReturn[5] == 1
		Set Printer To 	
		ourspool(wnrel)
	Endif

	MS_FLUSH()
EndIf

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³R975LayOut³ Autor ³ Juan Jose Pereira     ³ Data ³ 15.10.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Lay-out do modelo P12                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R975LayOut()

Local aL:=Array(20)
aL[01]:="#"
aL[02]:=STR0006 //"ESTADO DESTINATARIO  ###############################                                               LISTAGEM DE OPERACOES INTERESTADUAIS                                                           LP1 - MODELO P12"
aL[03]:=" "
aL[04]:=STR0007 //"EMITENTE #####################################################                                                                                                                                      PAGINA #######"
aL[05]:=" "
aL[06]:=STR0008 //"ENDERECO ############################################ CNPJ   #####################                                                                                                    PERIODO DE ####### A #######"
aL[07]:=" "
aL[08]:=STR0009 //"CIDADE #################################### UF ## CEP ######### INSCRICAO ESTADUAL ################                                                                                           EMISSAO ############"
aL[09]:="------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------"
aL[10]:=STR0010 //"NR.NF     SER EMISSAO    RAZAO SOCIAL                                                                      C.N.P.J.                       VALOR CONTABIL      BASE DE CALCULO     VALOR DO ICMS       VALOR DO IPI"
aL[11]:=STR0011 //"                         ENDERECO                                                                          INSCRICAO ESTADUAL                                                     VALOR ICMS SUBST.      IS/N TRIB"
aL[12]:=STR0012 //"                         CIDADE                                   UF     CEP                                                                                  DESP.ACESSORIAS     VT.SUBST.              B.C.SUBST"
aL[13]:="------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------"
aL[14]:="######### ### ########## #############################################################################     ##################             ##############      ##############      ##############    ##############"
aL[15]:="                         #############################################################################     #########################                                              ##############    ##############" 
aL[16]:="                         #################################        ##     #########                                                                            ##############      ##############    ##############"
aL[17]:=" "
aL[18]:="######################                                                                                                                    ##############      ##############      ##############    ##############"
aL[19]:="                                                                                                                                                                                  ##############    ##############"
aL[20]:="                                                                                                                                                              ##############      ##############    ##############"
//       123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x
//                10        20        30        40        50        60        70        80        90        110       120       130       140       150       160       170       180       190       200       210       220
Return (aL)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R975Imp  ³ Autor ³ Juan Jose Pereira     ³ Data ³ 15.10.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Impressao do programa                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R975Imp(lEnd,WnRel,cString,Tamanho)

Local	aEstrut		:=	{}
Local	aTotais		:=	Array(8)
Local	aTotGer		:=	Array(8)
Local	aL			:=	R975LayOut()
Local 	aAcumula	:=	Array(8)
Local 	aTam		:=	{}                
Local	aDados
           

Local	cAliasSF3	:=	"SF3"

Local 	cArqTrab
Local 	cIndex1
Local 	cIndex2
Local 	cSvAlias	:=	Alias()
Local	cMV_ESTADO	:=	GetMv("MV_ESTADO")
Local	cCidade
Local	cCEP
Local	cInscr
Local	cRazSoc
Local	cCGC
Local	cEstado

Local 	nLin		:=	80
Local	nPag		:=	nPagIni
Local	nSaltoPg	:=	52

Local	lMudouEst	:=	.F.
Local	lDevVenda	:=	.F.


#IFDEF TOP
	Local aStruSF3	:=	{}
	Local cQuery	:=	""
	Local nX		:=	0
#ELSE
	Local cIndex    :=	""
	Local cCondicao :=	""
#ENDIF

// Variaveis utilizadas no processamento por Filiais

//Gestão Corporativa
Local nForFilial := 0
Local aFilsCalc  := {} 
Local lFiliais   := Iif( MV_PAR12==1 , .T. , .F. ) 
Local lFilAgl    := Iif( MV_PAR13==1 , .T. , .F. )


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gestao Corporativa      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
If lFiliais 
	If MV_PAR12==2               
  
			aFilsCalc  := MatFilCalc(lFiliais)
	else
		lFilAgl:=(lFiliais .and. mv_par12 == 1) 
		aFilsCalc := MatFilCalc(lFiliais,,,lFilAgl,,Iif(MV_PAR13 == 1,2,0)) //Parametro Exp6 Valida apenas CNPJ+IE iguais
	Endif
Else
	aFilsCalc:={{.T.,cFilAnt}}
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria arquivo temporario                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AADD(aEstrut,{"UF"			,"C"	,02	,0})
AADD(aEstrut,{"CIDADE"		,"C"	,15	,0})
AADD(aEstrut,{"CEP"			,"C"	,08	,0})
AADD(aEstrut,{"CGC"			,"C"	,14	,0})

aTam		:=	TamSX3("A1_INSCR")
AADD(aEstrut,{"INSCR"		,"C"	,aTam[1],0})
aTam		:=	TamSX3("A1_END")
AADD(aEstrut,{"END"			,"C"	,aTam[1],0})
aTam		:=	TamSX3("A1_NOME")
AADD(aEstrut,{"NOME"		,"C"	,aTam[1],0})

AADD(aEstrut,{"NOTA"		,"C"	,TamSX3("F2_DOC")[1]	,0})
AADD(aEstrut,{"SERIE"		,"C"	,03	,0})
AADD(aEstrut,{"EMISSAO"		,"D"	,08	,0})
AADD(aEstrut,{"CLIENTE"		,"C"	,06	,0})
AADD(aEstrut,{"LOJA"		,"C"	,02	,0})
AADD(aEstrut,{"VALCONT"		,"N"	,13	,2})
AADD(aEstrut,{"BASECALC"	,"N"	,13	,2})
AADD(aEstrut,{"ICMS"		,"N"	,13	,2})
AADD(aEstrut,{"IPI"			,"N"	,13	,2})
AADD(aEstrut,{"ISENTAS"		,"N"	,13	,2})
AADD(aEstrut,{"ICMSRET"		,"N"	,13	,2})
AADD(aEstrut,{"BASERET"		,"N"	,13	,2})
AADD(aEstrut,{"DESPESA"		,"N"	,13	,2})
AADD(aEstrut,{"TIPO"		,"C"	,01	,0})

cArqTrab	:=	CriaTrab(aEstrut)
dbUseArea(.T.,,cArqTrab,"LIS",.T.,.F.)

cIndex1		:=	Substr("A"+CriaTrab(NIL,.F.),1,8)
cIndex2		:=	Substr("B"+CriaTrab(NIL,.F.),1,8)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ordem Portaria CAT 32/96 Clausula 9						     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

IndRegua("LIS",cIndex1,"UF+CEP+CGC+NOTA+SERIE",,,STR0013) //"Criando Indice..."
IndRegua("LIS",cIndex2,"CGC+NOTA+SERIE",,,STR0013) //"Criando Indice..."
dbClearIndex()
dbSetIndex(cIndex1+OrdBagExt())
dbSetIndex(cIndex2+OrdBagExt())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Alimenta arquivo temporario                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SF3")
dbSetOrder(1)               
ProcRegua(LastRec())

If 	!Empty(aFilsCalc)
	
	For nForFilial := 1 to len( aFilsCalc )	
	
	     If aFilsCalc[nForFilial][1]
	
			cFilAnt := aFilsCalc[nForFilial][2]  
				//SM0->( DbSeek( cEmpAnt + CFILANT ) )
				
			#IFDEF TOP
		    If TcSrvType()<>"AS/400"
		       
		       lQuery 		:= .T.
				cAliasSF3	:= "SF3_MTR975"
				aStruSF3	:= SF3->(dbStruct())
				cQuery		:= "SELECT SF3.F3_FILIAL,SF3.F3_ENTRADA,SF3.F3_NFISCAL,SF3.F3_SERIE,"
				cQuery		+= Iif(SerieNfId("SF3",3,"F3_SERIE")<>"F3_SERIE","SF3.F3_SDOC, ","")
				cQuery		+= "SF3.F3_CLIEFOR,SF3.F3_LOJA,SF3.F3_CFO,  "
				cQuery		+= "SF3.F3_ALIQICM, SF3.F3_ESTADO,SF3.F3_TIPO,SF3.F3_DESPESA,SF3.F3_DESPESA,SF3.F3_VALCONT,SF3.F3_BASEICM,  "
				cQuery		+= "SF3.F3_VALICM, SF3.F3_VALIPI,SF3.F3_ISENICM, SF3.F3_ICMSRET, SF3.F3_BASERET, SF3.F3_OUTRICM  "
				cQuery 		+= "FROM " + RetSqlName("SF3") + " SF3 "
				cQuery 		+= "WHERE SF3.F3_FILIAL = '" + cFilAnt + "' AND "
				cQuery 		+= "SF3.F3_ENTRADA >= '" + Dtos(dDtIni) + "' AND "
				cQuery 		+= "SF3.F3_ENTRADA <= '" + Dtos(dDtFim) + "' AND "
				If !lListEst
					cQuery	+= "SF3.F3_ESTADO <> '" + cMV_ESTADO + "' AND "
				Endif
			
				If !Empty(cEstDest)
				
				   	If lListEst
						cQuery	+= "SF3.F3_ESTADO = '" + cEstDest + "' AND "
					Else
						cQuery	+= "SF3.F3_ESTADO <> '" + cEstDest + "' AND "			
					Endif		
				Endif	
				
				If cLivro <> "*"
				   cQuery 	+= "SF3.F3_NRLIVRO = '" + cLivro + "' AND "
				EndIf	
				
				If !lDevolucoes
					cQuery	+= "Substring(SF3.F3_CFO,1,1) >= '5' AND SF3.F3_TIPO <> 'D' AND "
				Else	                                
					cQuery	+= "(Substring(SF3.F3_CFO,1,1) >= '5' OR (Substring(SF3.F3_CFO,1,1) <= '5' AND SF3.F3_TIPO = 'D')) AND "
				Endif
						
				cQuery 		+= "SF3.F3_ESTADO >= '" + cEstIni + "' AND "
				cQuery 		+= "SF3.F3_ESTADO <= '" + cEstFim + "' AND "
				cQuery 		+= "SF3.F3_TIPO <> 'S' AND "
				cQuery 		+= "SF3.F3_DTCANC = '" + Dtos(Ctod("")) + "' AND "
				cQuery 		+= "SF3.D_E_L_E_T_ = ' ' "
				cQuery 		+= "ORDER BY " + SqlOrder(SF3->(IndexKey()))
				cQuery 		:= ChangeQuery(cQuery)
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF3)
				
				For nX := 1 To len(aStruSF3)
					If aStruSF3[nX][2] <> "C" .And. FieldPos(aStruSF3[nX][1])<>0
						TcSetField(cAliasSF3,aStruSF3[nX][1],aStruSF3[nX][2],aStruSF3[nX][3],aStruSF3[nX][4])
					EndIf
				Next nX
						
			
					
			Else
			
			#ENDIF
		    
				cIndex    		:= CriaTrab(NIL,.F.)
				cCondicao 		:= 'F3_FILIAL == "' + xFilial("SF3") + '" .And. '
				cCondicao 		+= 'DTOS(F3_ENTRADA) >= "' + DTOS(dDtIni) + '" .And. DTOS(F3_ENTRADA) <= "' + DTOS(dDtFim) + '" .And. '
				
				If !lListEst
					cCondicao	+= 'F3_ESTADO <> "' + cMV_ESTADO + '" .And. '
				Endif
				
				If !Empty(cEstDest)
				
				   If lListEst
					  cCondicao	+= 'F3_ESTADO = "' + cEstDest + '" .And. '
				   Else
					  cCondicao	+= 'F3_ESTADO <> "' + cEstDest + '" .And. '
				   Endif		
				   
				Endif	
				
				If cLivro <> "*"
				   cCondicao 	+= ' F3_NRLIVRO == "' + cLivro + '" .And. '
				EndIf	                                                        
				
				If !lDevolucoes
					cCondicao	+= 'Substr(F3_CFO,1,1) >= "5" .And. F3_TIPO <> "D" .And. '
				Else	                                
					cCondicao	+= '(Substr(F3_CFO,1,1) >= "5" .Or. (Substr(F3_CFO,1,1) <= "5" .And. F3_TIPO = "D")) .And. '
				Endif               
				
				cCondicao 		+= 'F3_ESTADO >= "' + cEstIni + '" .And. '
				cCondicao 		+= 'F3_ESTADO <= "' + cEstFim + '" .And. '
				cCondicao 		+= 'F3_TIPO <> "S" .And. '
				cCondicao		+= 'Empty(F3_DTCANC) '
					   	
				IndRegua(cAliasSF3,cIndex,SF3->(IndexKey()),,cCondicao)
				dbSelectArea(cAliasSF3)
				ProcRegua(LastRec())
				dbGoTop()
					
			#IFDEF TOP
			   Endif
			#ENDIF
		                                   
				Do While !((cAliasSF3)->(Eof())).AND. (cAliasSF3)->F3_FILIAL == cFilAnt
					
				   IncProc(STR0017)
				   
				   If Interrupcao(@lEnd)
				      Exit
				   Endif
					
					lDevVenda		:=	(Substr((cAliasSF3)->F3_CFO,1,1)<"5" .And. (cAliasSF3)->F3_TIPO=="D")
					
					If lDevVenda
					   SA1->(dbSeek(F3Filial("SA1")+(cAliasSF3)->F3_CLIEFOR+(cAliasSF3)->F3_LOJA))
					   cCEP		:=	SA1->A1_CEP
					   cCGC		:=	SA1->A1_CGC
					   cInscr		:=	SA1->A1_INSCR
					   cCidade		:=	SA1->A1_MUN
					   cEnd		:=	SA1->A1_END
					   cRazSoc		:=	SA1->A1_NOME
					Else
						
						If (cAliasSF3)->F3_TIPO $ "DB"
							SA2->(dbSeek(F3Filial("SA2")+(cAliasSF3)->F3_CLIEFOR+(cAliasSF3)->F3_LOJA))
							cCEP	:=	SA2->A2_CEP
							cCGC	:=	SA2->A2_CGC
							cInscr	:=	SA2->A2_INSCR
							cCidade	:=	SA2->A2_MUN
							cEnd	:=	SA2->A2_END
							cRazSoc	:=	SA2->A2_NOME
						Else
							SA1->(dbSeek(F3Filial("SA1")+(cAliasSF3)->F3_CLIEFOR+(cAliasSF3)->F3_LOJA))
							cCEP	:=	SA1->A1_CEP
							cCGC	:=	SA1->A1_CGC
							cInscr	:=	SA1->A1_INSCR
							cCidade	:=	SA1->A1_MUN
							cEnd	:=	SA1->A1_END
							cRazSoc	:=	SA1->A1_NOME
						Endif
					Endif
					
					dbSelectArea("LIS")
					
					If dbSeek((cAliasSF3)->F3_NFISCAL+(cAliasSF3)->F3_SERIE,.F.)
					   RecLock("LIS",.F.)
					Else
						
						If 	lDevVenda
						   	SF1->(dbSetOrder(1))
						   	SF1->(dbSeek(cFilAnt+(cAliasSF3)->F3_NFISCAL+(cAliasSF3)->F3_SERIE+(cAliasSF3)->F3_CLIEFOR+(cAliasSF3)->F3_LOJA))
						Else
							SF2->(dbSetOrder(1))
							SF2->(dbSeek(cFilAnt+(cAliasSF3)->F3_NFISCAL+(cAliasSF3)->F3_SERIE+(cAliasSF3)->F3_CLIEFOR+(cAliasSF3)->F3_LOJA))
						Endif
						RecLock("LIS",.T.)
						LIS->UF 		:= (cAliasSF3)->F3_ESTADO
						LIS->CIDADE		:= cCidade
						LIS->CEP 		:= cCep
						LIS->CGC 		:= cCGC
						LIS->NOTA 		:= (cAliasSF3)->F3_NFISCAL
						LIS->SERIE 	:= (cAliasSF3)->&(SerieNfId("SF3",3,"F3_SERIE"))
						LIS->EMISSAO 	:= (cAliasSF3)->F3_ENTRADA
						LIS->CLIENTE 	:= (cAliasSF3)->F3_CLIEFOR
						LIS->LOJA 		:= (cAliasSF3)->F3_LOJA
						LIS->TIPO 		:= (cAliasSF3)->F3_TIPO		
						LIS->INSCR  	:= cInscr
						LIS->END    	:= cEnd
						LIS->NOME   	:= cRazSoc
						LIS->DESPESA 	:= (cAliasSF3)->F3_DESPESA
					Endif
					
					LIS->VALCONT 		+=	((cAliasSF3)->F3_VALCONT*If(lDevVenda,-1,1))
					LIS->BASECALC 		+=	((cAliasSF3)->F3_BASEICM*If(lDevVenda,-1,1))
					LIS->ICMS 			+=	((cAliasSF3)->F3_VALICM*If(lDevVenda,-1,1))
					LIS->IPI 			+=	((cAliasSF3)->F3_VALIPI*If(lDevVenda,-1,1))
					LIS->ISENTAS 		+=	(((cAliasSF3)->F3_ISENICM+(cAliasSF3)->F3_OUTRICM)*If(lDevVenda,-1,1))
					LIS->ICMSRET 		+=	((cAliasSF3)->F3_ICMSRET*If(lDevVenda,-1,1))
					LIS->BASERET 		+=	((cAliasSF3)->F3_BASERET*If(lDevVenda,-1,1))
					MsUnlock()
					dbSelectArea("SF3")
					(cAliasSF3)->(dbSkip())
				EndDo
		 EndIF
		(cAliasSF3)->(DbCloseArea())
	Next nForFilial

EndIF

////////////////////////////////////////////////////////////////////////////////////////////////////////////
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime listagem                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

dbSelectArea("LIS")
dbSetOrder(1)  //"UF+CEP+CGC+NOTA+SERIE"
dbGotop()                            
SetRegua(LastRec())
cCEP		:=	LIS->CEP
cCidade		:=	LIS->CIDADE
cEstado 	:=	LIS->UF
Afill(aTotais,0)
Afill(aTotGer,0)
Afill(aAcumula,0)

If !Empty(cEstado)
	R975Cabec(aL,@nLin,@nPag,lMudouEst)	
EndIf	
				
While !Eof()       
    
    IncRegua()

	If Interrupcao(@lEnd)
		Exit
	Endif
	
	If (cEstado!=LIS->UF .and. lQuebra)
		lMudouEst	:=	.T.
	
		If 	nLin>nSaltoPg
			R975Cabec(aL,@nLin,@nPag,lMudouEst)
		Endif   
		R975Rodape(aL,@nLin,@aTotais,STR0014) //"TOTAL DA FOLHA"           
	
		If	nLin>nSaltoPg
			R975Cabec(aL,@nLin,@nPag,lMudouEst)
		Endif   
		R975Rodape(aL,@nLin,@aTotGer,STR0015)	 //"TOTAL DO ESTADO"
		nLin := 80    
		R975Cabec(aL,@nLin,@nPag,lMudouEst)
		cEstado		:=	LIS->UF
		lMudouEst	:=	.F.
	Endif
	
	If	nLin>nSaltoPg .and. !lMudouEst	
		R975Rodape(aL,@nLin,@aTotais,STR0014) //"TOTAL DA FOLHA"           
		R975Cabec(aL,@nLin,@nPag,lMudouEst)	
		lMudouEst	:=	.F.
	Endif
	
	aDados		:=	Array(9)
	aDados[1]	:=	LIS->NOTA
	aDados[2]	:=	LIS->SERIE
	aDados[3]	:=	LIS->EMISSAO			
	aDados[4]	:=	LIS->NOME
	aDados[5]	:=	Transform(LIS->CGC,"@R! NN.NNN.NNN/NNNN-99")
	aDados[6]	:=	Transform(LIS->VALCONT,"@E 99999999999.99")
	aDados[7]	:=	Transform(LIS->BASECALC,"@E 99999999999.99")
	aDados[8]	:=	Transform(LIS->ICMS,"@E 99999999999.99")
	aDados[9]	:=	Transform(LIS->IPI,"@E 99999999999.99")
	FmtLin(@aDados,aL[14],"@E)",,@nLin)
	aDados		:=	Array(4)
	aDados[1]	:=	LIS->END
	aDados[2]	:=	LIS->INSCR
	aDados[3]	:=	Transform(LIS->ICMSRET,"@E 99999999999.99")
	aDados[4]	:=	Transform(LIS->ISENTAS,"@E 99999999999.99")
	FmtLin(@aDados,aL[15],"@E)",,@nLin)
	aDados		:=	Array(6)
	aDados[1]	:=	LIS->CIDADE
	aDados[2]	:=	LIS->UF
	aDados[3]	:=	Transform(LIS->CEP,"@R 99999-999")
	aDados[4]	:=	Transform(LIS->DESPESA,"@E 99999999999.99")
	aDados[5]	:=	Transform(LIS->ICMSRET,"@E 99999999999.99")
	aDados[6]	:=	Transform(LIS->BASERET,"@E 99999999999.99")
	FmtLin(@aDados,aL[16],"@E)",,@nLin)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Acumula totais                                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aTotais[1]	+=	LIS->VALCONT
	aTotais[2]	+=	LIS->BASECALC
	aTotais[3]	+=	LIS->ICMS
	aTotais[4]	+=	LIS->IPI
	aTotais[5]	+=	LIS->ISENTAS
	aTotais[6]	+=	LIS->BASERET
	aTotais[7]	+=	LIS->ICMSRET
	aTotais[8]	+=	LIS->DESPESA
		
	aTotGer[1]	+=	LIS->VALCONT
	aTotGer[2]	+=	LIS->BASECALC
	aTotGer[3]	+=	LIS->ICMS
	aTotGer[4]	+=	LIS->IPI
	aTotGer[5]	+=	LIS->ISENTAS
	aTotGer[6]	+=	LIS->BASERET
	aTotGer[7]	+=	LIS->ICMSRET
	aTotGer[8]	+=	LIS->DESPESA
			
	aAcumula[1]	+=	LIS->VALCONT
	aAcumula[2]	+=	LIS->BASECALC
	aAcumula[3]	+=	LIS->ICMS
	aAcumula[4]	+=	LIS->IPI
	aAcumula[5]	+=	LIS->ISENTAS
	aAcumula[6]	+=	LIS->BASERET
	aAcumula[7]	+=	LIS->ICMSRET
	aAcumula[8]	+=	LIS->DESPESA
				
dbSkip()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Salta uma linha na mudanca de CEP                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Eof().and.cCEP!=LIS->CEP.and.cEstado==LIS->UF
	   	nLin++
		cCEP	:=	LIS->CEP
	Endif
EndDo

If 	LastRec()>0
	R975Rodape(aL,@nLin,@aTotais,STR0014) //"TOTAL DA FOLHA"
	
	If lQuebra
	
		If nLin>nSaltoPg
			R975Cabec(aL,@nLin,@nPag,.F.)	
		Endif 
		R975Rodape(aL,@nLin,@aTotGer,STR0015)	 //"TOTAL DO ESTADO"
	Endif
	nLin++
	
	If nLin>nSaltoPg
	   R975Cabec(aL,@nLin,@nPag,.F.)	
	Endif
	R975Rodape(aL,@nLin,@aAcumula,STR0016)	 //"TOTAL GERAL"
Endif
			
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Apaga arquivo temporario                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

dbSelectArea("LIS")
dbCloseArea()
Ferase(cArqTrab)
Ferase(cIndex1+OrdBagExt())
Ferase(cIndex2+OrdBagExt())
dbSelectArea(cSvAlias)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³R975Cabec ³ Autor ³ Juan Jose Pereira     ³ Data ³ 15.10.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cabecalho do modelo P12                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R975Cabec(aL,nLin,nPag,lMudouEst)

Local cPagina
Local cPerIni
Local cPerFim
Local cDtEmissao

cPerIni		:=	StrZero(Month(dDtIni),2)+"/"+Substr(StrZero(Year(dDtIni),4),3,4)
cPerFim		:=	StrZero(Month(dDtFim),2)+"/"+Substr(StrZero(Year(dDtFim),4),3,4)
cDtEmissao	:=	Substr(DtoC(dDataBase),1,5)+"/"+StrZero(Year(dDataBase),4)
cPagina		:=	Transform(StrZero(nPag,6),"@R 999.999")

nLin		:=	0
@nLin,00 PSAY AvalImp(220)
nLin++

SX5->(dbSeek(xFilial()+"12"+LIS->UF))
FmtLin({X5Descri()},aL[2],,,@nLin)
FmtLin(,aL[3],,,@nLin)
FmtLin({SM0->M0_NOMECOM,cPagina},aL[4],,,@nLin)
FmtLin(,aL[5],,,@nLin)
FmtLin({IIf(!Empty(SM0->M0_ENDENT),SM0->M0_ENDENT,SM0->M0_ENDCOB),Transform(SM0->M0_CGC,"@R! NN.NNN.NNN/NNNN-99"),cPerIni,cPerFim},aL[6],,,@nLin)
FmtLin(,aL[7],,,@nLin)
If !Empty(SM0->M0_ENDENT)
	FmtLin({Alltrim(SM0->M0_CIDENT),Alltrim(SM0->M0_ESTENT),Alltrim(Transform(SM0->M0_CEPENT,"@R 99999-999")),InscrEst(),cDtEmissao},aL[8],,,@nLin)
Else
	FmtLin({Alltrim(SM0->M0_CIDCOB),Alltrim(SM0->M0_ESTCOB),Alltrim(Transform(SM0->M0_CEPCOB,"@R 99999-999")),InscrEst(),cDtEmissao},aL[8],,,@nLin)
Endif
FmtLin(,aL[9],,,@nLin)
FmtLin(,aL[10],,,@nLin)
FmtLin(,aL[11],,,@nLin)
FmtLin(,aL[12],,,@nLin)
FmtLin(,aL[13],,,@nLin)
NovaPg(@nPag,nQtFeixe,.F.)

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³R975Rodape³ Autor ³ Juan Jose Pereira     ³ Data ³ 15.10.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rodape do modelo P12                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function R975Rodape(aL,nLin,aTotal,cTotal)

Local aDados

nLin++
aDados		:=	Array(5)
aDados[1]	:=	Transform(cTotal,"@!")
aDados[2]	:=	Transform(aTotal[1],"@E 99999999999.99")
aDados[3]	:=	Transform(aTotal[2],"@E 99999999999.99")
aDados[4]	:=	Transform(aTotal[3],"@E 99999999999.99")
aDados[5]	:=	Transform(aTotal[4],"@E 99999999999.99")
FmtLin(@aDados,aL[18],"@E)",,@nLin)
aDados		:=	Array(2)
aDados[1]	:=	Transform(aTotal[7],"@E 99999999999.99")
aDados[2]	:=	Transform(aTotal[5],"@E 99999999999.99")
FmtLin(@aDados,aL[19],"@E)",,@nLin)
aDados		:=	Array(3)
aDados[1]	:=	Transform(aTotal[8],"@E 99999999999.99")
aDados[2]	:=	Transform(aTotal[7],"@E 99999999999.99")
aDados[3]	:=	Transform(aTotal[6],"@E 99999999999.99")
FmtLin(@aDados,aL[20],"@E)",,@nLin)
Afill(aTotal,0)

Return(NIL)
