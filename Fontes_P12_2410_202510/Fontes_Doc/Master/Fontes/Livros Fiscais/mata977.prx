#INCLUDE "MATA977.ch"
#INCLUDE "PROTHEUS.ch"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA977  ³ Autor ³ Edstron Everson Correia³ Data ³ 23/07/2001     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Gera arquivo de Importacao do Demonstrativo de Notas Fiscais      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MATA977()                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Mata977

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Salva a Integridade dos dados de Entrada                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aSave	:=	{Alias(),IndexOrd(),Recno()}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nOpc		:=0	, oDlg
Local cTitulo	:=	""
Local cText1	:=	""
Local cText2	:=	""
Local aCAP		:=	{STR0001,STR0002,STR0003} //"Confirma"###"Abandona"###"Parƒmetros"
Local lVerpesssen := Iif(FindFunction("Verpesssen"),Verpesssen(),.T.)

Private lEnd		:=.F.
Private lAbortPrint :=.F.
Private cPerg	   :="MTA977"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para montar Get.                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aListBox	:={}
Private	aMsg		:={}
Private	aSel		:={}
Private	aValid	    :={}
Private	aConteudo   :={}

Private cArqTip0	:=""
Private cArqTip1    :=""
Private cArqTip2	:=""
Private cArqTip3    :=""
Private cArqTip9    :=""
Private aCamTip0	:={}
Private aCamTip1	:={}
Private aCamTip2	:={}
Private aCamTip3	:={}
Private aCamTip9	:={}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Janela Principal                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cTitulo	:=	"DNF"
cText1	:=	STR0004 //"Este programa gera arquivo pr‚-formatado para importacao do DNF - Demonstrativo de Notas Fiscais."
If lVerpesssen

	While .T.
		nOpc :=	0
		DEFINE MSDIALOG oDlg TITLE OemtoAnsi(cTitulo) FROM  315,245 TO 400,780 PIXEL OF oMainWnd
		@ 03,05 TO 23,265 LABEL "" OF oDlg  PIXEL
		@ 09,12 SAY OemToAnsi(cText1) SIZE 250, 8 OF oDlg PIXEL
		DEFINE SBUTTON FROM 27, 177 TYPE 5 ACTION (nOpc:=3,oDlg:End()) ENABLE OF oDlg
		DEFINE SBUTTON FROM 27, 206 TYPE 1 ACTION (nOpc:=1,oDlg:End()) ENABLE OF oDlg
		DEFINE SBUTTON FROM 27, 235 TYPE 2 ACTION (nOpc:=2,oDlg:End()) ENABLE OF oDlg
		ACTIVATE MSDIALOG oDlg
		Pergunte(cPerg,.f.)
		Do Case
			Case nOpc==1
				Processa({|lEnd| A977ProcDNF() })
			Case nOpc==3
				Pergunte(cPerg,.t.)
				Loop
		EndCase
		Exit
	End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura area                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea(aSave[1])
	dbSetOrder(aSave[2])
	dbGoto(aSave[3])

EndIf 
Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A977ProcDNF  ³ Autor ³Edstron E. Correia ³ Data ³ 23/07/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Processamento dos dados utilizados no DNF                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION A977ProcDNF(lImpressao)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa variaveis                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cArqBkp     :=""
Local cMsg	      :=""
Local cTipo       :=""
Local cCond       :=""
Local cCond2      :=""
Local cChav       :=""
Local cChav2      :=""
Local cChav3      :=""
Local cChav4      :=""
Local cChav5      :=""
Local cChav6      :=""
Local cChav7      :=""
Local cChavOld    :=""
Local cProd       :=""
Local cProd2      :=""
Local cNcm        :=""
Local cTec        :=""
Local cSkip       :=""
Local cSkip2      :=""
Local cQuery      :=""
Local cDir        := MV_PAR07
Local nValTot     :=0
Local nRegEmp     :=SM0->(RecNo())
Local aEmpresa	  :={}
Local lIntegra    :=IF(GetMV("MV_EASY")=="S",.T.,.F.)
Local lProc       :=.F.
Local lSeq        :=.T.
Local j           :=1
Local nItem       :=1
Local	nAcValcont	:=	0
Local	nAcValIpi	:=	0
Local	nAcValOut	:=	0
Local	lOkDi		:=	.F.
Local	nX			:= 0
Local	i			:= 0
Private aDNFRG2   :={}
Private aDNFRG3   :={}
Private cAlias	  :=Alias()
Private aReturn   :={STR0007, 1,STR0008, 2, 2, 1, "",1 } //"Zebrado"###"Administra‡„o"
Private Nomeprog  :="MATA977"
Private nHandle	  :=-1
Private cFile	  :=""
Private nLin      :=1
Private cAliasSD1   :="SD1"
Private cAliasSD2   :="SD2"
Private cAliasSWN   :="SWN"
Private nSeq        :=1
Private cCodUni     :=""
Private cCGC1       :=""


PRIVATE lIpiBruto:=IIF(GETMV("MV_IPIBRUT")=="S",.T.,.F.)
PRIVATE nFatConv :=GetMv("MV_CONVDNF")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros         ³
//³ mv_par01            // Demonstrativo Tipo ?  ³
//³ mv_par02            // Mes de Apuracao    ?  ³
//³ mv_par03            // Ano de Apuracao    ?  ³
//³ mv_par04            // Filial de          ?  ³
//³ mv_par05            // Filial ate         ?  ³
//³ mv_par06            // Numero da D.I.     ?  ³
//³ mv_par07            // Diretório          ?  ³
//³ mv_par08            // Versão             ?  ³
//³ mv_par09            // Seleciona Filiais  ?  ³
//³ mv_par10            // Aglutina por CNPJ  ?  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Private cDemo	 :=STRZERO((mv_par01-1),1)
Private	nMes     :=mv_par02
Private nAno	 :=mv_par03
Private	cFilIni  :=mv_par04
Private	cFilFim  :=mv_par05
Private nLastKey :=0
Private dDtIni   :=CTOD("//")
Private dDtFim   :=CTOD("//")

Default	lImpressao	:=	.F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao do Primeiro e Ultimo dia do mes                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dDtIni	:=	CTOD("01/"+STRZERO(nMes,2)+"/"+STRZERO(nAno,4),"ddmmyyyy")
dDtFim	:=	CTOD("01/"+STRZERO(nMes,2)+"/"+STRZERO(nAno,4),"ddmmyyyy")
dDtFim	:=	UltimoDia(dDtFim)

If (SD1->(FieldPos (SuperGetMv ("MV_NUMDI")))==0)
	If AllTrim(SuperGetMv ("MV_NUMDI")) <> ""
		Alert (STR0019+SuperGetMv ("MV_NUMDI")+STR0020+STR0029+STR0031, STR0022) 
	ElseIf Empty(mv_par06)
		Alert (STR0032,STR0022)
	ElseIf !Empty(mv_par06)
		Alert (STR0030+STR0031, STR0022) 
	EndIf
Else
	lOkDi	:=	.T.
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Digitacao de Codigos NCM ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lEnd	:=	A977DigNCM()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializa processo de criacao do(s) arquivo(s) TXT por filial para DNF³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lEnd
	#IFDEF TOP
		cCGC := if(!empty(SM0->M0_CGC),SM0->M0_CGC,replic("0",14))
	   	aFilsCalc	:= MatFilCalc( (MV_PAR09==1), , , (MV_PAR09==1 .and. MV_PAR10==1), , 3 )  
		For nX := 1 to Len(aFilsCalc)
			If !aFilsCalc[nX][1]
				Loop
			EndIf
			SM0->(dbSeek(cEmpAnt+aFilsCalc[nX][2],.T.))
			aAdd(aEmpresa,{SM0->M0_CGC,SM0->M0_CODIGO,SM0->M0_CODFIL})
	   	Next	    	
		SM0->(dbGoTo(nRegEmp))
		// Se não selecionou na MatFilCalc ou se o parametro está configurado para não selecionar filiais
		If Len(aEmpresa) == 0 .OR. MV_PAR09 == 2		
			aEmpresa	:= {}
			aAdd(aEmpresa,{SM0->M0_CGC,SM0->M0_CODIGO,SM0->M0_CODFIL})
		EndIf
    #ELSE   
	dbSelectArea("SM0")
	dbSeek(cEmpAnt+cFilIni,.T.)
	cCGC := if(!empty(SM0->M0_CGC),SM0->M0_CGC,replic("0",14))
	
	While !Eof() .and. SM0->M0_CODIGO == cEmpAnt .and. SM0->M0_CODFIL <= cFilFim
		aAdd(aEmpresa,{SM0->M0_CGC,SM0->M0_CODIGO,SM0->M0_CODFIL})
		dbSkip()
	endDo
	SM0->(dbGoTo(nRegEmp))
	cFilAnt := SM0->M0_CODFIL
    #ENDIF
	
	aEmpresa := asort(aEmpresa,,,{|x,y| x[3] < y[3] })
	
	While j <=  len(aEmpresa)
			
		cAliasSD1   :="SD1"
		cAliasSD2   :="SD2"
		cAliasSWN   :="SWN"
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Cria arquivos temporarios para processamento do DNF³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		A977Temp()
		
		#IFDEF TOP
			cFile := AllTrim(cDir)+"DNF"+cEmpAnt+cFilAnt+".TXT"
			// Se não deve aglutinar as informações em um único arquivo
			IF MV_PAR10 == 2 
		#ENDIF
		cFile := AllTrim(cDir)+"DNF"+aEmpresa[j,2]+aEmpresa[j,3]+".TXT"
		#IFDEF TOP
			EndIf
		#ENDIF
		If !File(cFile)
			nHandle:=MsFCreate(cFile)
		Else
			#IFDEF TOP
				// Se não deve aglutinar as informações em um único arquivo
				IF MV_PAR10 == 2 
			#ENDIF
			If !lImpressao
				cArqBkp:=StrTran(cFile,".TXT",".#XT")
				cMsg := STR0009 + cFile + STR0010              //"O arquivo "###" ja existe,"
				cMsg += STR0011+cArqBkp                        //"o sistema gera um Backup "
				nOpcB:=Aviso("Drive",cMsg,{STR0012,STR0013}) //"Abandonar"###"Continuar"
				If nOpcB==1.Or.nLastKey==27
					lEnd:=.T.
				Else
					If File(cArqBkp)
						Ferase(cArqBkp)
					Endif
					FRename(cFile,cArqBkp)
					nHandle:=MSFCREATE(cFile)
				Endif
			Endif
			#IFDEF TOP
				Else
					nHandle:= fOpen(cFile, 2)				
				EndIf
			#ENDIF
		Endif
		if lEnd
			Exit
		Endif
		
		dbSelectArea("SM0")
		dbSeek(aEmpresa[j,2]+aEmpresa[j,3],.T.)
		
		cCGC := if(!empty(SM0->M0_CGC),SM0->M0_CGC,replic("0",14))
		
		while cCGC == SM0->M0_CGC
			
			cFilAnt 	:= SM0->M0_CODFIL
			cCGC1 	:= if(!empty(SM0->M0_CGC),SM0->M0_CGC,replic("0",14))
			If lIntegra
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Processamento Parceria (Average) ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If nSeq==1
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Define chaves e restricoes para o Processamento das Notas Fiscais de Entrada³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cChav  :=xFilial("SD1")+DTOS(dDtIni)
					cChav2 :=xFilial("SF1")+(cAliasSD1)->D1_DOC+(cAliasSD1)->D1_SERIE
					cCond  :="(cAliasSD1)->D1_FILIAL==xFilial('SD1') .AND. DTOS((cAliasSD1)->D1_EMISSAO)>=DTOS(dDtIni) .AND. DTOS((cAliasSD1)->D1_EMISSAO)<=DTOS(dDtFim) .AND. !(cAliasSD1)->(EOF())"
					cCond2 :="(cAliasSWN)->WN_FILIAL==xFilial('SWN') .AND. (cAliasSWN)->WN_DOC==(cAliasSD1)->D1_DOC .AND. (cAliasSWN)->WN_SERIE==(cAliasSD1)->D1_SERIE .AND. ((cAliasSWN)->WN_TEC+(cAliasSWN)->WN_EX_NCM+(cAliasSWN)->WN_EX_NBM)==(cAliasSD1)->D1_TEC"
					cProd  :=xFilial("SB1")+(cAliasSD1)->D1_COD
					cProd2 :=xFilial("SB1")+(cAliasSWN)->WN_PRODUTO
					cSkip  :="(cAliasSD1)->(DBSKIP())"
					cSkip2 :="(cAliasSWN)->(DBSKIP())"
					DBSELECTAREA("SD1")
					SD1->(DBSETORDER(3))
					#IFDEF TOP
						cAliasSD1 := "AliasSD1"
						aStruSD1  := SD1->(dbStruct())
						cQuery    := "SELECT * "
						cQuery    += "FROM "+RetSqlName("SD1")+" SD1 "
						cQuery    += "WHERE SD1.D1_FILIAL='"+xFilial("SD1")+"' AND "
						cQuery    += "SD1.D1_EMISSAO >= '"+DTOS(dDtIni)+"' AND "
						cQuery    += "SD1.D1_EMISSAO <= '"+DTOS(dDtFim)+"' AND "
						cQuery    += "SD1.D_E_L_E_T_=' ' "
						cQuery    += "ORDER BY "+SqlOrder(SD1->(IndexKey()))
						
						cQuery    := ChangeQuery(cQuery)
						
						If select(cAliasSD1)=0
							dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1,.T.,.T.)
						Endif
						
						For nX := 1 To Len(aStruSD1)
							If ( aStruSD1[nX][2] <> "C" )
								TcSetField(cAliasSD1,aStruSD1[nX][1],aStruSD1[nX][2],aStruSD1[nX][3],aStruSD1[nX][4])
							EndIf
						Next nX
					#ELSE
						cAliasSD1 :="SD1"
						(cAliasSD1)->(DBSEEK(cChav,.T.))
					#ENDIF
				Else
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Define chaves e restricoes para o Processamento das Notas Fiscais de Saida³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cChav  :=xFilial("SD2")+DTOS(dDtIni)
					cChav2 :=xFilial("SF2")+(cAliasSD2)->D2_DOC+(cAliasSD2)->D2_SERIE
					cFil   :=(cAliasSD2)->D2_FILIAL
					cDoc   :=(cAliasSD2)->D2_DOC
					cSer   :=(cAliasSD2)->D2_SERIE
					cCond  :="(cAliasSD2)->D2_FILIAL==xFilial('SD2') .AND. DTOS((cAliasSD2)->D2_EMISSAO)>=DTOS(dDtIni) .AND. DTOS((cAliasSD2)->D2_EMISSAO)<=DTOS(dDtFim) .AND. !(cAliasSD2)->(EOF())"
					cCond2 :="(cAliasSD2)->D2_FILIAL==cFil .AND. (cAliasSD2)->D2_DOC==cDoc .AND. (cAliasSD2)->D2_SERIE==cSER .AND. !(cAliasSD2)->(EOF())"
					cProd  :=xFilial("SB1")+(cAliasSD2)->D2_COD
					cSkip  :="(cAliasSD2)->(DBSKIP())"
					DBSELECTAREA("SD2")
					SD2->(DBSETORDER(5))
					#IFDEF TOP
						cAliasSD2 := "AliasSD2"
						aStruSD2  := SD2->(dbStruct())
						cQuery    := "SELECT * "
						cQuery    += "FROM "+RetSqlName("SD2")+" SD2 "
						cQuery    += "WHERE SD2.D2_FILIAL='"+xFilial("SD2")+"' AND "
						cQuery    += "SD2.D2_EMISSAO >= '"+DTOS(dDtIni)+"' AND "
						cQuery    += "SD2.D2_EMISSAO <= '"+DTOS(dDtFim)+"' AND "
						cQuery    += "SD2.D_E_L_E_T_=' ' "
						cQuery    += "ORDER BY "+SqlOrder(SD2->(IndexKey()))
						
						cQuery    := ChangeQuery(cQuery)
						
						If select(cAliasSD2)=0
							dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD2,.T.,.T.)
						Endif
						
						For nX := 1 To Len(aStruSD2)
							If ( aStruSD2[nX][2] <> "C" )
								TcSetField(cAliasSD2,aStruSD2[nX][1],aStruSD2[nX][2],aStruSD2[nX][3],aStruSD2[nX][4])
							EndIf
						Next nX
					#ELSE
						(cAliasSD2)->(DBSEEK(cChav,.T.))
					#ENDIF
				Endif
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Laco while de acordo com a restricao estabelecida³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				While &cCond
					If nSeq==1
						cProd  :=xFilial("SB1")+(cAliasSD1)->D1_COD
						cFil   :=(cAliasSD1)->D1_FILIAL
						cDoc   :=(cAliasSD1)->D1_DOC
						cSer   :=(cAliasSD1)->D1_SERIE
						cTec   :=(cAliasSD1)->D1_TEC
					Else
						cProd  :=xFilial("SB1")+(cAliasSD2)->D2_COD
						cFil   :=(cAliasSD2)->D2_FILIAL
						cDoc   :=(cAliasSD2)->D2_DOC
						cSer   :=(cAliasSD2)->D2_SERIE
					Endif
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Processamento Parceria (Average) ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					SB1->(DBSETORDER(1))
					SB1->(DBSEEK(cProd,.F.))
					
					If nSeq==1
						
						DBSELECTAREA("SWN")
						SWN->(DBSETORDER(1))
						#IFDEF TOP
							cAliasSWN := "AliasSWN"
							aStruSWN  := SWN->(dbStruct())
							cQuery    := "SELECT * "
							cQuery    += "FROM "+RetSqlName("SWN")+" SWN "
							cQuery    += "WHERE SWN.WN_FILIAL='"+cFil+"' AND "
							cQuery    += "SWN.WN_DOC='"+cDoc+"' AND "
							cQuery    += "SWN.WN_SERIE='"+cSer+"' AND "
							cQuery    += "(SWN.WN_TEC||SWN.WN_EX_NCM||SWN.WN_EX_NBM)='"+cTec+"' AND "
							cQuery    += "SWN.D_E_L_E_T_=' ' "
							cQuery    += "ORDER BY "+SqlOrder(SWN->(IndexKey()))
							cQuery    := ChangeQuery(cQuery)
							
							If select(cAliasSWN)=0
								dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSWN,.T.,.T.)
							Endif
							
							For nX := 1 To Len(aStruSWN)
								If ( aStruSWN[nX][2] <> "C" )
									TcSetField(cAliasSWN,aStruSWN[nX][1],aStruSWN[nX][2],aStruSWN[nX][3],aStruSWN[nX][4])
								EndIf
							Next nX
						#ELSE
							(cAliasSWN)->(dbSeek(xFilial("SWN")+(cAliasSD1)->D1_DOC+(cAliasSD1)->D1_SERIE+(cAliasSD1)->D1_TEC))
						#ENDIF
					Endif
					
					nItem :=1
					While &cCond2
						If nSeq==1
							If SUBS(UPPER(cProd),3,4)<>'PROD'
								(cAliasSD1)->(DBSKIP())
								Loop
							Endif
							cProd2 :=xFilial("SB1")+(cAliasSWN)->WN_PRODUTO
						Else
							cProd2 :=xFilial("SB1")+(cAliasSD2)->D2_COD
						Endif
						SB1->(DBSETORDER(1))
						SB1->(DBSEEK(cProd2,.F.))
						cNcm  :=STRTRAN(ALLTRIM(SB1->B1_POSIPI),".","")
						If !Empty(cNcm)
							lProc :=.F.
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Verifica se o Codigo  NCM do item da Nota Fiscal esta³
							//³contido no grupo de Codigos NCM que atendem o DNF    ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							FOR i=3 TO Len(aConteudo)
								If AT(cNcm,aConteudo[i])<>0 .And. !Empty (cNcm)
									lProc :=.T.
									If cNcm$"39233000/39235000/76121000/76129019/83079000/83091000/83099000"
										cCodUni :="11"
									Else
										cCodUni :="12"
									Endif
									exit
								Endif
							NEXT
							If lProc
								If nSeq==1
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³Verifica se existe a Nota Fiscal no SF1 para³
									//³gravacao dos seus dados no arquivo Tipo 1   ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									cChav2 :=xFilial("SF1")+(cAliasSD1)->D1_DOC+(cAliasSD1)->D1_SERIE
									SF1->(DBSETORDER(1))
									SF1->(DBSEEK(cChav2,.F.))
									If !SF1->(FOUND())
										(cAliasSWN)->(DBSKIP())
										Loop
									Endif
									
									If (cAliasSD1)->D1_TIPO $ "DB"
										SA1->(DBSETORDER(1))
										SA1->(dbSeek(xFilial("SA1")+SF1->F1_FORNECE+SF1->F1_LOJA))
										cTipo :=SA1->A1_TIPO
									Else
										SA2->(DBSETORDER(1))
										SA2->(dbSeek(xFilial("SA2")+SF1->F1_FORNECE+SF1->F1_LOJA))
										cTipo :=SA2->A2_TIPO
									Endif
									
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³Verifica se o adquirente eh do tipo Importacao/Exportacao³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									If !cTipo $ "X"
										(cAliasSWN)->(DBSKIP())
										Loop
									Endif
									
									If (cAliasSWN)->WN_QUANT=0
										(cAliasSWN)->(DBSKIP())
										Loop
									Endif
									
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³Verifica se existe a Nota Fiscal no SF3 para³
									//³gravacao dos seus dados no arquivo Tipo 1   ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									cChav3 :=xFilial("SD1")+(cAliasSD1)->D1_FORNECE+(cAliasSD1)->D1_LOJA+(cAliasSD1)->D1_DOC+(cAliasSD1)->D1_SERIE
									SF3->(DBSETORDER(4))
									SF3->(DBSEEK(cChav3,.F.))
									If !SF3->(FOUND())
										(cAliasSWN)->(DBSKIP())
										Loop
									Endif
									
									nAcValcont	:=	0
									nAcValIpi	:=	0
									nAcValOut	:=	0
									
									While SF3->(.Not. Eof ()) .And. (cChav3==(xFilial("SF3")+SF3->F3_CLIEFOR+SF3->F3_LOJA+SF3->F3_NFISCAL+SF3->F3_SERIE))
										nAcValcont	+= SF3->F3_VALCONT
										nAcValIpi	+= SF3->F3_VALIPI
										nAcValOut	+= SF3->F3_ISENIPI+SF3->F3_OUTRIPI
										SF3->(DbSkip ())
									EndDo
									
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³Gravacao do arquivo Tipo 1³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									cChav4 :=cCGC+"E"+(cAliasSD1)->D1_DOC+(cAliasSD1)->D1_SERIE
									If !(cArqTip1)->(dbSeek(cChav4,.F.))
										RecLock(cArqTip1,.T.)
										(cArqTip1)->TIPO      :="1"
										(cArqTip1)->CNPJ      :=cCGC
										(cArqTip1)->NOTA      :=SF1->F1_DOC
										(cArqTip1)->SERIE     :=SF1->F1_SERIE
										(cArqTip1)->SDOC      :=SerieNfId("SF1",2,"F1_SERIE")
										(cArqTip1)->EMISSAO   :=A977DatChr(SF1->F1_EMISSAO)
										(cArqTip1)->EFETIVA   :=A977DatChr(SF1->F1_EMISSAO)
										(cArqTip1)->CFOP      :=A977Fill(A977Num2Chr(Val(SubStr((cAliasSD1)->D1_CF,1,4)),04,0),04)
										(cArqTip1)->VALNF     :=A977Fill(A977Num2Chr(nAcValCont,14,2),14)
										(cArqTip1)->VALIPI    :=A977Fill(A977Num2Chr(nAcValIpi,14,2),14)
										(cArqTip1)->ISENTO    :=A977Fill(A977Num2Chr(nAcValOut,14,2),14)
										If Empty(SF3->F3_DTCANC)
											(cArqTip1)->CANCEL    :="N"
										Else
											(cArqTip1)->CANCEL    :="S"
										Endif
										(cArqTip1)->MOVIMEN   :="E"
										
										cChav5 :=xFilial("SF8")+(cAliasSD1)->D1_DOC+(cAliasSD1)->D1_SERIE+SF1->F1_FORNECE+SF1->F1_LOJA
										SF8->(DBSETORDER(2))
										If SF8->(DBSEEK(cChav5))
											(cArqTip1)->COMPL     :="S"
											(cArqTip1)->NFVINCU   :=RetNf(SF8->F8_NFDIFRE,6,"N")
											(cArqTip1)->SERVINCU  :=A977Fill(A977Num2Chr(Val(SF8->F8_SEDIFRE),03,0),03)
											(cArqTip1)->SDOCV		 :=A977Fill(A977Num2Chr(Val(SerieNfId("SF8",2,"F8_SEDIFRE")),03,0),03)
											(cArqTip1)->DTVINCU   :=A977DatChr(SF8->F8_DTDIGIT)
										Else
											(cArqTip1)->COMPL     :="N"
											(cArqTip1)->NFVINCU   :=A977Fill(A977Num2Chr(Val("0"),06,0),06)
											(cArqTip1)->SERVINCU  :=A977Fill(A977Num2Chr(Val("0"),03,0),03)
											(cArqTip1)->DTVINCU   :=A977DatChr(CTOD("//"))
										Endif
										(cArqTip1)->(MsUnlock())
									Endif
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³Gravacao do arquivo Tipo 3³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									RecLock(cArqTip3,.T.)
									(cArqTip3)->TIPO       	:= "3"
									(cArqTip3)->CNPJ       	:= cCGC
									(cArqTip3)->NOTA       	:= (cAliasSD1)->D1_DOC
									(cArqTip3)->SERIE      	:= (cAliasSD1)->D1_SERIE
									(cArqTip3)->SDOC       	:= SerieNfId(cAliasSD1,2,"D1_SERIE")
									(cArqTip3)->EMISSAO    	:= A977DatChr((cAliasSD1)->D1_EMISSAO)
									(cArqTip3)->CFOP       	:= A977Fill(A977Num2Chr(Val(SubStr((cAliasSD1)->D1_CF,1,4)),04,0),04)
									(cArqTip3)->ITEM       	:= A977Fill(A977Num2Chr(nItem,02,0),02)
									nItem :=nItem+1
									(cArqTip3)->CODMERC     := A977Fill((cAliasSWN)->WN_PRODUTO,20)
									SB1->(DBSETORDER(1))
									SB1->(DBSEEK(xFilial("SB1")+(cAliasSWN)->WN_PRODUTO,.F.))
									(cArqTip3)->DESMERC     :=A977Fill(SB1->B1_DESC,45)
									(cArqTip3)->NCM         :=STRTRAN(ALLTRIM(SB1->B1_POSIPI),".","")
									DBSELECTAREA("SB1")
									If MV_PAR08==2
										SB5->(DbSeek (xFilial ("SB5")+SB1->B1_COD))
										(cArqTip3)->PRODDNF     :=A977Fill(A977Num2Chr(SB5->B5_PRODDNF,3,0),03,0)
									Else
                               			(cArqTip3)->ESPECIE     :=A977Fill(B1_ESPECIE,02,0)
			             			EndIf
									(cArqTip3)->QUANTIDADE  :=A977Fill(A977Num2Chr((cAliasSWN)->WN_QUANT,14,3),14)
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³Soma do Valor Unitario acrescido de IPI e DESPESAS correspondentes ao item³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									nValTot :=(cAliasSWN)->WN_PRUNI
									nValTot +=((cAliasSWN)->WN_VALIPI/(cAliasSWN)->WN_QUANT)
									nValTot +=((cAliasSWN)->WN_DESPESA/(cAliasSWN)->WN_QUANT)
									(cArqTip3)->VALUNIT     :=A977Fill(A977Num2Chr(nValTot,14,4),14)
									(cArqTip3)->VALTOT      :=A977Fill(A977Num2Chr((nValTot*(cAliasSWN)->WN_QUANT),14,2),14)
									(cArqTip3)->BASEIPI     :=A977Fill(A977Num2Chr((cAliasSWN)->WN_IPIBASE,14,2),14)
									(cArqTip3)->VALIPI      :=A977Fill(A977Num2Chr((cAliasSWN)->WN_VALIPI,14,2),14)
									If !SX5->(DBSEEK(xFilial("SX5")+"62"+SB1->B1_UM))
										If (SAH->(MsSeek (xFilial ("SAH")+AllTrim (SB1->B1_UM))))
											(cArqTip2)->UNIDADE     :=A977Fill(SAH->AH_DESCPO, 10)
										Else
											(cArqTip2)->UNIDADE     :=A977Fill("", 10)
										EndIf
									Else
										(cArqTip2)->UNIDADE     :=A977Fill(SX5->X5_DESCRI,10)
									EndIf
									If (existblock("DNF63RG3"))
										aDNFRG3 :=ExecBlock("DNF63RG3",.F.,.F.)
										If Len(aDNFRG3)==3
											(cArqTip3)->CODUNID     :=aDNFRG3[1]
											(cArqTip3)->FATOR       :=A977Fill(A977Num2Chr(aDNFRG3[2],14,3),14)
											(cArqTip3)->QTDEUNIPAD  :=A977Fill(A977Num2Chr(aDNFRG3[3],14,3),14)
										Endif
									Else
										If SB1->B1_UM=="UN"
											(cArqTip3)->CODUNID     :="11"
										ElseIf SB1->B1_UM=="KG"
											(cArqTip3)->CODUNID     :="12"
										Endif
										(cArqTip3)->FATOR       :=A977Fill(A977Num2Chr(&nFatConv,14,3),14)
										If SB1->B1_TIPCONV=="M"
											(cArqTip3)->QTDEUNIPAD  :=A977Fill(A977Num2Chr((&nFatConv*(cAliasSWN)->WN_QUANT),14,3),14)
										Else
											(cArqTip3)->QTDEUNIPAD  :=A977Fill(A977Num2Chr((&nFatConv/(cAliasSWN)->WN_QUANT),14,3),14)
										Endif
									Endif
									SW6->(DBSETORDER(1))
									SW6->(DBSEEK(xFilial("SW6")+(cAliasSWN)->WN_HAWB,.F.))
									(cArqTip3)->NUMERODI    :=STRZERO(VAL(SW6->W6_HAWB),10)
									(cArqTip3)->MOVIMEN     :="E"
								Else
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³Verifica se existe a Nota Fiscal no SF2 para³
									//³gravacao dos seus dados no arquivo Tipo 1   ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									cChav2 :=xFilial("SF2")+(cAliasSD2)->D2_DOC+(cAliasSD2)->D2_SERIE
									SF2->(DBSETORDER(1))
									SF2->(DBSEEK(cChav2,.F.))
									If !SF2->(FOUND())
										(cAliasSD2)->(DBSKIP())
										Loop
									Endif
									
									If (cAliasSD2)->D2_TIPO $ "DB"
										SA2->(DBSETORDER(1))
										SA2->(dbSeek(xFilial("SA2")+SF2->F2_CLIENTE+SF2->F2_LOJA))
										cTipo :=SA2->A2_TIPO
										cCGC1 :=SA2->A2_CGC
									Else
										SA1->(DBSETORDER(1))
										SA1->(dbSeek(xFilial("SA1")+SF2->F2_CLIENTE+SF2->F2_LOJA))
										cTipo :=SA1->A1_TIPO
										cCGC1 :=SA1->A1_CGC
									Endif
									cCGC1 :=If(cTipo=="X",cCGC,cCGC1)
									
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³Verifica se existe a Nota Fiscal no SF3 para³
									//³gravacao dos seus dados no arquivo Tipo 1   ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									cChav3 :=xFilial("SF3")+(cAliasSD2)->D2_CLIENTE+(cAliasSD2)->D2_LOJA+(cAliasSD2)->D2_DOC+(cAliasSD2)->D2_SERIE
									SF3->(DBSETORDER(4))
									SF3->(DBSEEK(cChav3,.F.))
									If !SF3->(FOUND()) .or. Len(Alltrim(cCGC)) < 14
										(cAliasSD2)->(DBSKIP())
										Loop
									Endif
									
									nAcValcont	:=	0
									nAcValIpi	:=	0
									nAcValOut	:=	0
									
									While SF3->(.Not. Eof ()) .And. (cChav3==(xFilial("SF3")+SF3->F3_CLIEFOR+SF3->F3_LOJA+SF3->F3_NFISCAL+SF3->F3_SERIE))
										nAcValcont	+= SF3->F3_VALCONT
										nAcValIpi	+= SF3->F3_VALIPI
										nAcValOut	+= SF3->F3_ISENIPI+SF3->F3_OUTRIPI
										SF3->(DbSkip ())
									EndDo
									
									If (cAliasSD2)->D2_QUANT=0
										(cAliasSD2)->(DBSKIP())
										Loop
									Endif
									
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³Gravacao do arquivo Tipo 1³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									cChav4 :=cCGC+"S"+(cAliasSD2)->D2_DOC+(cAliasSD2)->D2_SERIE
									If !(cArqTip1)->(dbSeek(cChav4,.F.))
										RecLock(cArqTip1,.T.)
										(cArqTip1)->TIPO      :="1"
										(cArqTip1)->CNPJ      :=cCGC
										(cArqTip1)->NOTA      :=SF2->F2_DOC
										(cArqTip1)->SERIE     :=SF2->F2_SERIE
										(cArqTip1)->SDOC      := SerieNfId("SF2",2,"F2_SERIE")
										(cArqTip1)->EMISSAO   :=A977DatChr(SF2->F2_EMISSAO)
										(cArqTip1)->EFETIVA   :=A977DatChr(SF2->F2_EMISSAO)
										(cArqTip1)->CFOP      :=A977Fill(A977Num2Chr(Val(SubStr((cAliasSD2)->D2_CF,1,4)),04,0),04)
										(cArqTip1)->VALNF     :=A977Fill(A977Num2Chr(nAcValCont,14,2),14)
										(cArqTip1)->VALIPI    :=A977Fill(A977Num2Chr(nAcValIpi,14,2),14)
										(cArqTip1)->ISENTO    :=A977Fill(A977Num2Chr(nAcValOut,14,2),14)
										If Empty(SF3->F3_DTCANC)
											(cArqTip1)->CANCEL    :="N"
										Else
											(cArqTip1)->CANCEL    :="S"
										Endif
										(cArqTip1)->MOVIMEN   :="S"
										If !Empty(SF2->F2_NFORI)
											(cArqTip1)->COMPL      	:=	"S"
											(cArqTip1)->NFVINCU    	:=	RetNf(SF2->F2_NFORI,6,"N")
											(cArqTip1)->SERVINCU   	:=	A977Fill(A977Num2Chr(Val(SF2->F2_SERIORI),03,0),03)
											(cArqTip1)->SDOCV		  	:=	A977Fill(A977Num2Chr(Val(SerieNfId("SF2",2,"F2_SERIORI")),03,0),03)
											cChav5 :=xFilial("SF2")+SF2->F2_NFORI+SF2->F2_SERIORI
											SF2->(DBSETORDER(1))
											SF2->(DBSEEK(cChav5,.F.))
											(cArqTip1)->DTVINCU   :=A977DatChr(SF2->F2_EMISSAO)
										Else
											(cArqTip1)->COMPL     :="N"
											(cArqTip1)->NFVINCU   :=RetNf(SF2->F2_NFORI,6,"N")
											(cArqTip1)->SERVINCU  :=A977Fill(A977Num2Chr(Val(SF2->F2_SERIORI),03,0),03)
											(cArqTip1)->SDOCV	  	 :=A977Fill(A977Num2Chr(Val(SerieNfId("SF2",2,"F2_SERIORI")),03,0),03)
											(cArqTip1)->DTVINCU   :=A977DatChr(CTOD("//"))
										Endif
										(cArqTip1)->(MsUnlock())
									Endif
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³Gravacao do arquivo Tipo 2³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									RecLock(cArqTip2,.T.)
									(cArqTip2)->TIPO        :="2"
									(cArqTip2)->CNPJ        :=cCGC
									(cArqTip2)->NOTA        :=(cAliasSD2)->D2_DOC
									(cArqTip2)->SERIE       :=(cAliasSD2)->D2_SERIE
									(cArqTip2)->SDOC        :=SerieNfId(cAliasSD2,2,"D2_SERIE")
									(cArqTip2)->EMISSAO     :=A977DatChr((cAliasSD2)->D2_EMISSAO)
									(cArqTip2)->CFOP        :=A977Fill(A977Num2Chr(Val(SubStr((cAliasSD2)->D2_CF,1,4)),04,0),04)
									(cArqTip2)->ITEM        :=A977Fill(A977Num2Chr(nItem,02,0),02)
									nItem :=nItem+1
									(cArqTip2)->CODMERC     :=A977Fill((cAliasSD2)->D2_COD,20)
									SB1->(DBSETORDER(1))
									SB1->(DBSEEK(xFilial("SB1")+(cAliasSD2)->D2_COD,.F.))
									(cArqTip2)->DESMERC     :=A977Fill(SB1->B1_DESC,45)
									(cArqTip2)->NCM         :=STRTRAN(ALLTRIM(SB1->B1_POSIPI),".","")
									DBSELECTAREA("SB1")
									If MV_PAR08==2
										SB5->(DbSeek (xFilial ("SB5")+SB1->B1_COD))
										(cArqTip2)->PRODDNF     :=A977Fill(A977Num2Chr(SB5->B5_PRODDNF,3,0),03,0)
									Else
                               			(cArqTip3)->ESPECIE     :=A977Fill(B1_ESPECIE,02,0)
			             			EndIf
									(cArqTip2)->QUANTIDADE  :=A977Fill(A977Num2Chr((cAliasSD2)->D2_QUANT,14,3),14)
									(cArqTip2)->VALUNIT     :=A977Fill(A977Num2Chr((cAliasSD2)->D2_PRCVEN,14,4),14)
									(cArqTip2)->VALTOT      :=A977Fill(A977Num2Chr((cAliasSD2)->D2_TOTAL,14,2),14)
									(cArqTip2)->BASEIPI     :=A977Fill(A977Num2Chr((cAliasSD2)->D2_BASEIPI,14,2),14)
									(cArqTip2)->VALIPI      :=A977Fill(A977Num2Chr((cAliasSD2)->D2_VALIPI,14,2),14)
									If !SX5->(DBSEEK(xFilial("SX5")+"62"+SB1->B1_UM))
										If (SAH->(MsSeek (xFilial ("SAH")+AllTrim (SB1->B1_UM))))
											(cArqTip2)->UNIDADE     :=A977Fill(SAH->AH_DESCPO, 10)
										Else
											(cArqTip2)->UNIDADE     :=A977Fill("", 10)
										EndIf
									Else
										(cArqTip2)->UNIDADE     :=A977Fill(SX5->X5_DESCRI,10)
									EndIf
									If (existblock("DNF63RG2"))
										aDNFRG2 :=ExecBlock("DNF63RG2",.F.,.F.)
										If Len(aDNFRG2)==3
											(cArqTip2)->CODUNID     :=aDNFRG2[1]
											(cArqTip2)->FATOR       :=A977Fill(A977Num2Chr(aDNFRG2[2],14,3),14)
											(cArqTip2)->QTDEUNIPAD  :=A977Fill(A977Num2Chr(aDNFRG2[3],14,3),14)
										Endif
									Else
										If SB1->B1_UM=="UN"
											(cArqTip2)->CODUNID     :="11"
										ElseIf SB1->B1_UM=="KG"
											(cArqTip2)->CODUNID     :="12"
										Endif
										(cArqTip2)->FATOR       :=A977Fill(A977Num2Chr(&nFatConv,14,3),14)
										If SB1->B1_TIPCONV=="M"
											(cArqTip2)->QTDEUNIPAD  :=A977Fill(A977Num2Chr((&nFatConv*(cAliasSD2)->D2_QUANT),14,3),14)
										Else
											(cArqTip2)->QTDEUNIPAD  :=A977Fill(A977Num2Chr((&nFatConv/(cAliasSD2)->D2_QUANT),14,3),14)
										Endif
									Endif
									(cArqTip2)->MOVIMEN     :="S"
								Endif
							Endif
						Endif
						If nSeq==1
							(cAliasSWN)->(DBSKIP())
						Else
							(cAliasSD2)->(DBSKIP())
						Endif
					Enddo
					If nSeq==1
						(cAliasSD1)->(DBSKIP())
					Else
						(cAliasSD2)->(DBSKIP())
					Endif
				Enddo
			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Processamento Microsiga ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If nSeq==1
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Define chaves e restricoes para o Processamento das Notas Fiscais de Entrada³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cChav    :=xFilial("SD1")+DTOS(dDtIni)
					cChav2   :=xFilial("SF1")+(cAliasSD1)->D1_DOC+(cAliasSD1)->D1_SERIE
					cCond    :="(cAliasSD1)->D1_FILIAL==xFilial('SD1') .AND. DTOS((cAliasSD1)->D1_EMISSAO)>=DTOS(dDtIni) .AND. DTOS((cAliasSD1)->D1_EMISSAO)<=DTOS(dDtFim) .AND. !(cAliasSD1)->(EOF())"
					cProd    :=xFilial("SB1")+(cAliasSD1)->D1_COD
					cSkip    :="(cAliasSD1)->(DBSKIP())"
					nItem    :=1
					cChavOld :=""
					DBSELECTAREA("SD1")
					SD1->(DBSETORDER(3))
					#IFDEF TOP
						cAliasSD1 := "AliasSD1"
						aStruSD1  := SD1->(dbStruct())
						cQuery    := "SELECT * "
						cQuery    += "FROM "+RetSqlName("SD1")+" SD1 "
						cQuery    += "WHERE SD1.D1_FILIAL='"+xFilial("SD1")+"' AND "
						cQuery    += "SD1.D1_EMISSAO >= '"+DTOS(dDtIni)+"' AND "
						cQuery    += "SD1.D1_EMISSAO <= '"+DTOS(dDtFim)+"' AND "
						cQuery    += "SD1.D_E_L_E_T_=' ' "
						cQuery    += "ORDER BY "+SqlOrder(SD1->(IndexKey()))
						
						cQuery    := ChangeQuery(cQuery)
						
						If select(cAliasSD1)=0
							dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1,.T.,.T.)
						Endif
						
						For nX := 1 To Len(aStruSD1)
							If ( aStruSD1[nX][2] <> "C" )
								TcSetField(cAliasSD1,aStruSD1[nX][1],aStruSD1[nX][2],aStruSD1[nX][3],aStruSD1[nX][4])
							EndIf
						Next nX
					#ELSE
						(cAliasSD1)->(DBSEEK(cChav,.T.))
					#ENDIF
				Else
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Define chaves e restricoes para o Processamento das Notas Fiscais de Saida³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cChav    :=xFilial("SD2")+DTOS(dDtIni)
					If select(cAliasSD2) > 0
						cChav2   :=xFilial("SF2")+(cAliasSD2)->D2_DOC+(cAliasSD2)->D2_SERIE
						cProd    :=xFilial("SB1")+(cAliasSD2)->D2_COD
	                EndIf
					cCond    :="(cAliasSD2)->D2_FILIAL==xFilial('SD2') .AND. DTOS((cAliasSD2)->D2_EMISSAO)>=DTOS(dDtIni) .AND. DTOS((cAliasSD2)->D2_EMISSAO)<=DTOS(dDtFim) .AND. !(cAliasSD2)->(EOF())"
					cSkip    :="(cAliasSD2)->(DBSKIP())"
					nItem    :=1
					cChavOld :=""
					DBSELECTAREA("SD2")
					SD2->(DBSETORDER(5))
					#IFDEF TOP
						cAliasSD2 := "AliasSD2"
						aStruSD2  := SD2->(dbStruct())
						cQuery    := "SELECT * "
						cQuery    += "FROM "+RetSqlName("SD2")+" SD2 "
						cQuery    += "WHERE SD2.D2_FILIAL='"+xFilial("SD2")+"' AND "
						cQuery    += "SD2.D2_EMISSAO >= '"+DTOS(dDtIni)+"' AND "
						cQuery    += "SD2.D2_EMISSAO <= '"+DTOS(dDtFim)+"' AND "
						cQuery    += "SD2.D_E_L_E_T_=' ' "
						cQuery    += "ORDER BY "+SqlOrder(SD2->(IndexKey()))
						
						cQuery    := ChangeQuery(cQuery)
						
						If select(cAliasSD2)=0
							dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD2,.T.,.T.)
						Endif
						
						For nX := 1 To Len(aStruSD2)
							If ( aStruSD2[nX][2] <> "C" )
								TcSetField(cAliasSD2,aStruSD2[nX][1],aStruSD2[nX][2],aStruSD2[nX][3],aStruSD2[nX][4])
							EndIf
						Next nX
						(cAliasSD2)->(DbGoTop ())
					#ELSE
						(cAliasSD2)->(DBSEEK(cChav,.T.))
					#ENDIF
				Endif
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Laco while de acordo com a restricao estabelecida³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				While &cCond
					If nSeq==1
						cProd  :=xFilial("SB1")+(cAliasSD1)->D1_COD
					Else
						cProd  :=xFilial("SB1")+(cAliasSD2)->D2_COD
					Endif
					SB1->(DBSETORDER(1))
					SB1->(DBSEEK(cProd,.F.))
					cNcm  :=STRTRAN(ALLTRIM(SB1->B1_POSIPI),".","")
					lProc :=.F.
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Verifica se o Codigo  NCM do item da Nota Fiscal esta³
					//³contido no grupo de Codigos NCM que atendem o DNF    ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					FOR i=3 TO Len(aConteudo)
						IF AT(cNcm,aConteudo[i])<>0 .And. !Empty (cNcm)
							lProc :=.T.
							If cNcm$"39233000/39235000/76121000/76129019/83079000/83091000/83099000"
								cCodUni :="11"
							Else
								cCodUni :="12"
							Endif
							exit
						ENDIF
					NEXT
					If lProc
						If nSeq==1
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Verifica se existe a Nota Fiscal no SF1 para³
							//³gravacao dos seus dados no arquivo Tipo 1   ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							cChav2   :=xFilial("SF1")+(cAliasSD1)->D1_DOC+(cAliasSD1)->D1_SERIE
							If cChav2<>cChavOld
								nItem :=1
							Endif
							cChavOld :=xFilial("SF1")+(cAliasSD1)->D1_DOC+(cAliasSD1)->D1_SERIE
							SF1->(DBSETORDER(1))
							SF1->(DBSEEK(cChav2,.F.))
							If !SF1->(FOUND())
								(cAliasSD1)->(DBSKIP())
								Loop
							Endif
							
							If (cAliasSD1)->D1_TIPO $ "DB"
								SA1->(DBSETORDER(1))
								SA1->(dbSeek(xFilial("SA1")+SF1->F1_FORNECE+SF1->F1_LOJA))
								cTipo :=SA1->A1_TIPO
							Else
								SA2->(DBSETORDER(1))
								SA2->(dbSeek(xFilial("SA2")+SF1->F1_FORNECE+SF1->F1_LOJA))
								cTipo :=SA2->A2_TIPO
							Endif
							
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Verifica se o adquirente eh do tipo Importacao/Exportacao³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If !cTipo $ "X"
								(cAliasSD1)->(DBSKIP())
								Loop
							Endif
							
							If (cAliasSD1)->D1_QUANT=0
								(cAliasSD1)->(DBSKIP())
								Loop
							Endif
							
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Verifica se existe a Nota Fiscal no SF3 para³
							//³gravacao dos seus dados no arquivo Tipo 1   ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							cChav3 :=xFilial("SF3")+(cAliasSD1)->D1_FORNECE+(cAliasSD1)->D1_LOJA+(cAliasSD1)->D1_DOC+(cAliasSD1)->D1_SERIE
							SF3->(DBSETORDER(4))
							SF3->(DBSEEK(cChav3,.F.))
							If !SF3->(FOUND())
								(cAliasSD1)->(DBSKIP())
								Loop
							Endif
							
							nAcValcont	:=	0
							nAcValIpi	:=	0
							nAcValOut	:=	0
							
							While SF3->(.Not. Eof ()) .And. (cChav3==(xFilial("SF3")+SF3->F3_CLIEFOR+SF3->F3_LOJA+SF3->F3_NFISCAL+SF3->F3_SERIE))
								nAcValcont	+= SF3->F3_VALCONT
								nAcValIpi	+= SF3->F3_VALIPI
								nAcValOut	+= SF3->F3_ISENIPI+SF3->F3_OUTRIPI
								SF3->(DbSkip ())
							EndDo
							
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Gravacao do arquivo Tipo 1³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							cChav4 :=cCGC+"E"+(cAliasSD1)->D1_DOC+(cAliasSD1)->D1_SERIE
							If !(cArqTip1)->(dbSeek(cChav4,.F.))
								RecLock(cArqTip1,.T.)
								(cArqTip1)->TIPO      :="1"
								(cArqTip1)->CNPJ      :=cCGC
								(cArqTip1)->NOTA      :=SF1->F1_DOC
								(cArqTip1)->SERIE     :=SF1->F1_SERIE
								(cArqTip1)->SDOC      :=SerieNfId("SF1",2,"F1_SERIE")
								(cArqTip1)->EMISSAO   :=A977DatChr(SF1->F1_EMISSAO)
								(cArqTip1)->EFETIVA   :=A977DatChr(SF1->F1_EMISSAO)
								(cArqTip1)->CFOP      :=A977Fill(A977Num2Chr(Val(SubStr((cAliasSD1)->D1_CF,1,4)),04,0),04)
								(cArqTip1)->VALNF     :=A977Fill(A977Num2Chr(nAcValCont,14,2),14)
								(cArqTip1)->VALIPI    :=A977Fill(A977Num2Chr(nAcValIpi,14,2),14)
								(cArqTip1)->ISENTO    :=A977Fill(A977Num2Chr(nAcValOut,14,2),14)
								If Empty(SF3->F3_DTCANC)
									(cArqTip1)->CANCEL    :="N"
								Else
									(cArqTip1)->CANCEL    :="S"
								Endif
								(cArqTip1)->MOVIMEN   :="E"
								
								cChav5 :=xFilial("SF8")+(cAliasSD1)->D1_DOC+(cAliasSD1)->D1_SERIE+SF1->F1_FORNECE+SF1->F1_LOJA
								SF8->(DBSETORDER(2))
								If SF8->(DBSEEK(cChav5))
									(cArqTip1)->COMPL     :="S"
									(cArqTip1)->NFVINCU   :=RetNf(SF8->F8_NFDIFRE,6,"N")
									(cArqTip1)->SERVINCU  :=A977Fill(A977Num2Chr(Val(SF8->F8_SEDIFRE),03,0),03)
									(cArqTip1)->SDOCV  	 :=A977Fill(A977Num2Chr(Val(SerieNfId("SF8",2,"F8_SEDIFRE")),03,0),03)
									(cArqTip1)->DTVINCU   :=A977DatChr(SF8->F8_DTDIGIT)
								Else
									(cArqTip1)->COMPL     :="N"
									(cArqTip1)->NFVINCU   :=A977Fill(A977Num2Chr(Val("0"),06,0),06)
									(cArqTip1)->SERVINCU  :=A977Fill(A977Num2Chr(Val("0"),03,0),03)
									(cArqTip1)->DTVINCU   :=A977DatChr(CTOD("//"))
								Endif
								(cArqTip1)->(MsUnlock())
							Endif
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Gravacao do arquivo Tipo 3³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							RecLock(cArqTip3,.T.)
							(cArqTip3)->TIPO        :="3"
							(cArqTip3)->CNPJ        :=cCGC
							(cArqTip3)->NOTA        :=(cAliasSD1)->D1_DOC
							(cArqTip3)->SERIE       :=(cAliasSD1)->D1_SERIE
							(cArqTip3)->SDOC        :=(cAliasSD1)->D1_SERIE
							(cArqTip3)->EMISSAO     :=A977DatChr((cAliasSD1)->D1_EMISSAO)
							(cArqTip3)->CFOP        :=A977Fill(A977Num2Chr(Val(SubStr((cAliasSD1)->D1_CF,1,4)),04,0),04)
							(cArqTip3)->ITEM        :=A977Fill(A977Num2Chr(nItem,02,0),02)
							nItem :=nItem+1
							(cArqTip3)->CODMERC     :=A977Fill((cAliasSD1)->D1_COD,20)
							SB1->(DBSETORDER(1))
							SB1->(DBSEEK(xFilial("SB1")+(cAliasSD1)->D1_COD,.F.))
							(cArqTip3)->DESMERC     :=A977Fill(SB1->B1_DESC,45)
							(cArqTip3)->NCM         :=cNcm
							DBSELECTAREA("SB1")
							If (MV_PAR08==2)
								SB5->(DbSeek (xFilial ("SB5")+SB1->B1_COD))
								(cArqTip3)->PRODDNF     :=A977Fill(A977Num2Chr(SB5->B5_PRODDNF,3,0),03,0)
							Else
                               	(cArqTip3)->ESPECIE     :=A977Fill(B1_ESPECIE,02,0)
	             			EndIf
							(cArqTip3)->QUANTIDADE  :=A977Fill(A977Num2Chr((cAliasSD1)->D1_QUANT,14,3),14)
							(cArqTip3)->VALUNIT     :=A977Fill(A977Num2Chr((cAliasSD1)->D1_VUNIT,14,4),14)
							(cArqTip3)->VALTOT      :=A977Fill(A977Num2Chr((cAliasSD1)->D1_TOTAL,14,2),14)
							(cArqTip3)->BASEIPI     :=A977Fill(A977Num2Chr((cAliasSD1)->D1_BASEIPI,14,2),14)
							(cArqTip3)->VALIPI      :=A977Fill(A977Num2Chr((cAliasSD1)->D1_VALIPI,14,2),14)
							If !SX5->(DBSEEK(xFilial("SX5")+"62"+SB1->B1_UM))
								If (SAH->(MsSeek (xFilial ("SAH")+AllTrim (SB1->B1_UM))))
									(cArqTip3)->UNIDADE     :=A977Fill(SAH->AH_DESCPO, 10)
								Else
									(cArqTip3)->UNIDADE     :=A977Fill("", 10)
								EndIf
							Else
								(cArqTip3)->UNIDADE     :=A977Fill(SX5->X5_DESCRI,10)
							EndIf
							If (existblock("DNF63RG3"))
								aDNFRG3 :=ExecBlock("DNF63RG3",.F.,.F.)
								If Len(aDNFRG3)==3
									(cArqTip3)->CODUNID     :=aDNFRG3[1]
									(cArqTip3)->FATOR       :=A977Fill(A977Num2Chr(aDNFRG3[2],14,3),14)
									(cArqTip3)->QTDEUNIPAD  :=A977Fill(A977Num2Chr(aDNFRG3[3],14,3),14)
								Endif
							Else
								(cArqTip3)->CODUNID     :=cCodUni
								(cArqTip3)->FATOR       :=A977Fill(A977Num2Chr(&nFatConv,14,3),14)
								If SB1->B1_TIPCONV=="M"
									(cArqTip3)->QTDEUNIPAD  :=A977Fill(A977Num2Chr((&nFatConv*(cAliasSD1)->D1_QUANT),14,3),14)
								Else
									(cArqTip3)->QTDEUNIPAD  :=A977Fill(A977Num2Chr((&nFatConv/(cAliasSD1)->D1_QUANT),14,3),14)
								Endif
							Endif
							(cArqTip3)->NUMERODI    :=Iif (lOkDi, StrZero (Val ((cAliasSD1)->(FieldGet (FieldPos (SuperGetMv ("MV_NUMDI"))))),10), StrZero (Val (MV_PAR06),10))
							(cArqTip3)->MOVIMEN     :="E"
						Else
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Verifica se existe a Nota Fiscal no SF2 para³
							//³gravacao dos seus dados no arquivo Tipo 1   ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							cChav2   :=xFilial("SF2")+(cAliasSD2)->D2_DOC+(cAliasSD2)->D2_SERIE
							If cChav2<>cChavOld
								nItem :=1
							Endif
							cChavOld :=xFilial("SF2")+(cAliasSD2)->D2_DOC+(cAliasSD2)->D2_SERIE
							SF2->(DBSETORDER(1))
							SF2->(DBSEEK(cChav2,.F.))
							If !SF2->(FOUND())
								(cAliasSD2)->(DBSKIP())
								Loop
							Endif
							
							If (cAliasSD2)->D2_TIPO $ "DB"
								SA2->(DBSETORDER(1))
								SA2->(dbSeek(xFilial("SA2")+SF2->F2_CLIENTE+SF2->F2_LOJA))
								cTipo :=SA2->A2_TIPO
								cCGC1 :=SA2->A2_CGC
							Else
								SA1->(DBSETORDER(1))
								SA1->(dbSeek(xFilial("SA1")+SF2->F2_CLIENTE+SF2->F2_LOJA))
								cTipo :=SA1->A1_TIPO
								cCGC1 :=SA1->A1_CGC
							Endif
							cCGC1 :=If(cTipo=="X", cCGC, cCGC1)
							
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Verifica se existe a Nota Fiscal no SF3 para³
							//³gravacao dos seus dados no arquivo Tipo 1   ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							cChav3 :=xFilial("SF3")+(cAliasSD2)->D2_CLIENTE+(cAliasSD2)->D2_LOJA+(cAliasSD2)->D2_DOC+(cAliasSD2)->D2_SERIE
							SF3->(DBSETORDER(4))
							SF3->(DBSEEK(cChav3,.F.))
							If !SF3->(FOUND()) .or. Len(Alltrim(cCGC)) < 14
								(cAliasSD2)->(DBSKIP())
								Loop
							Endif
							
							nAcValcont	:=	0
							nAcValIpi	:=	0
							nAcValOut	:=	0
							
							While SF3->(.Not. Eof ()) .And. (cChav3==(xFilial("SF3")+SF3->F3_CLIEFOR+SF3->F3_LOJA+SF3->F3_NFISCAL+SF3->F3_SERIE))
								nAcValcont	+= SF3->F3_VALCONT
								nAcValIpi	+= SF3->F3_VALIPI
								nAcValOut	+= SF3->F3_ISENIPI+SF3->F3_OUTRIPI
								SF3->(DbSkip ())
							EndDo
							
							If (cAliasSD2)->D2_QUANT=0
								(cAliasSD2)->(DBSKIP())
								Loop
							Endif
							
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Gravacao do arquivo Tipo 1³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							cChav4 :=cCGC+"S"+(cAliasSD2)->D2_DOC+(cAliasSD2)->D2_SERIE
							If !(cArqTip1)->(dbSeek(cChav4,.F.))
								RecLock(cArqTip1,.T.)
								(cArqTip1)->TIPO      :="1"
								(cArqTip1)->CNPJ      :=cCGC
								(cArqTip1)->NOTA      :=SF2->F2_DOC
								(cArqTip1)->SERIE     :=SF2->F2_SERIE
								(cArqTip1)->SDOC      :=SerieNfId("SF2",2,"F2_SERIE")
								(cArqTip1)->EMISSAO   :=A977DatChr(SF2->F2_EMISSAO)
								(cArqTip1)->EFETIVA   :=A977DatChr(SF2->F2_EMISSAO)
								(cArqTip1)->CFOP      :=A977Fill(A977Num2Chr(Val(SubStr((cAliasSD2)->D2_CF,1,4)),04,0),04)
								(cArqTip1)->VALNF     :=A977Fill(A977Num2Chr(nAcValCont,14,2),14)
								(cArqTip1)->VALIPI    :=A977Fill(A977Num2Chr(nAcValIpi,14,2),14)
								(cArqTip1)->ISENTO    :=A977Fill(A977Num2Chr(nAcValOut,14,2),14)
								If Empty(SF3->F3_DTCANC)
									(cArqTip1)->CANCEL    :="N"
								Else
									(cArqTip1)->CANCEL    :="S"
								Endif
								(cArqTip1)->MOVIMEN   :="S"
								If !Empty(SF2->F2_NFORI)
									(cArqTip1)->COMPL     :="S"
									(cArqTip1)->NFVINCU   :=RetNf(SF2->F2_NFORI,6,"N")
									(cArqTip1)->SERVINCU  :=A977Fill(A977Num2Chr(Val(SF2->F2_SERIORI),03,0),03)
									(cArqTip1)->SDOCV  	 :=A977Fill(A977Num2Chr(Val(SerieNfId("SF2",2,"F2_SERIORI")),03,0),03)
									cChav5 :=xFilial("SF2")+SF2->F2_NFORI+SF2->F2_SERIORI
									SF2->(DBSETORDER(1))
									SF2->(DBSEEK(cChav5,.F.))
									(cArqTip1)->DTVINCU   :=A977DatChr(SF2->F2_EMISSAO)
								Else
									(cArqTip1)->COMPL     :="N"
									(cArqTip1)->NFVINCU   :=RetNf(SF2->F2_NFORI,6,"N")
									(cArqTip1)->SERVINCU  :=A977Fill(A977Num2Chr(Val(SF2->F2_SERIORI),03,0),03)
									(cArqTip1)->SDOCV     :=A977Fill(A977Num2Chr(Val(SerieNfId("SF2",2,"F2_SERIORI")),03,0),03)
									(cArqTip1)->DTVINCU   :=A977DatChr(CTOD("//"))
								Endif
								(cArqTip1)->(MsUnlock())
							Endif
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Gravacao do arquivo Tipo 2³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							RecLock(cArqTip2,.T.)
							(cArqTip2)->TIPO        :="2"
							(cArqTip2)->CNPJ        :=cCGC
							(cArqTip2)->NOTA        :=(cAliasSD2)->D2_DOC
							(cArqTip2)->SERIE       :=(cAliasSD2)->D2_SERIE
							(cArqTip2)->SDOC        :=SerieNfId(cAliasSD2,2,"D2_SERIE")
							(cArqTip2)->EMISSAO     :=A977DatChr((cAliasSD2)->D2_EMISSAO)
							(cArqTip2)->CFOP        :=A977Fill(A977Num2Chr(Val(SubStr((cAliasSD2)->D2_CF,1,4)),04,0),04)
							(cArqTip2)->ITEM        :=A977Fill(A977Num2Chr(nItem,02,0),02)
							nItem :=nItem+1
							(cArqTip2)->CODMERC     :=A977Fill((cAliasSD2)->D2_COD,20)
							SB1->(DBSETORDER(1))
							SB1->(DBSEEK(xFilial("SB1")+(cAliasSD2)->D2_COD,.F.))
							(cArqTip2)->DESMERC     :=A977Fill(SB1->B1_DESC,45)
							(cArqTip2)->NCM         :=cNcm
							DBSELECTAREA("SB1")
							If MV_PAR08==2
								SB5->(DbSeek (xFilial ("SB5")+SB1->B1_COD))
								(cArqTip2)->PRODDNF     :=A977Fill(A977Num2Chr(SB5->B5_PRODDNF,3,0),03,0)
							Else
                       			(cArqTip3)->ESPECIE     :=A977Fill(B1_ESPECIE,02,0)
	             			EndIf
							(cArqTip2)->QUANTIDADE  :=A977Fill(A977Num2Chr((cAliasSD2)->D2_QUANT,14,3),14)
							(cArqTip2)->VALUNIT     :=A977Fill(A977Num2Chr((cAliasSD2)->D2_PRCVEN,14,4),14)
							(cArqTip2)->VALTOT      :=A977Fill(A977Num2Chr((cAliasSD2)->D2_TOTAL,14,2),14)
							(cArqTip2)->BASEIPI     :=A977Fill(A977Num2Chr((cAliasSD2)->D2_BASEIPI,14,2),14)
							(cArqTip2)->VALIPI      :=A977Fill(A977Num2Chr((cAliasSD2)->D2_VALIPI,14,2),14)
							If !SX5->(DBSEEK(xFilial("SX5")+"62"+SB1->B1_UM))
								If (SAH->(MsSeek (xFilial ("SAH")+AllTrim (SB1->B1_UM))))
									(cArqTip2)->UNIDADE     :=A977Fill(SAH->AH_DESCPO, 10)
								Else
									(cArqTip2)->UNIDADE     :=A977Fill("", 10)
								EndIf
							Else
								(cArqTip2)->UNIDADE     :=A977Fill(SX5->X5_DESCRI,10)
							EndIf
							If (existblock("DNF63RG2"))
								aDNFRG2 :=ExecBlock("DNF63RG2",.F.,.F.)
								If Len(aDNFRG2)==3
									(cArqTip2)->CODUNID     :=aDNFRG2[1]
									(cArqTip2)->FATOR       :=A977Fill(A977Num2Chr(aDNFRG2[2],14,3),14)
									(cArqTip2)->QTDEUNIPAD  :=A977Fill(A977Num2Chr(aDNFRG2[3],14,3),14)
								Endif
							Else
								(cArqTip2)->CODUNID     :=cCodUni
								(cArqTip2)->FATOR       :=A977Fill(A977Num2Chr(&nFatConv,14,3),14)
								If SB1->B1_TIPCONV=="M"
									(cArqTip2)->QTDEUNIPAD  :=A977Fill(A977Num2Chr((&nFatConv*(cAliasSD2)->D2_QUANT),14,3),14)
								Else
									(cArqTip2)->QTDEUNIPAD  :=A977Fill(A977Num2Chr((&nFatConv/(cAliasSD2)->D2_QUANT),14,3),14)
								Endif
							Endif
							(cArqTip2)->MOVIMEN     :="S"
						Endif
					Endif
					If nSeq==1
						(cAliasSD1)->(DBSKIP())
					Else
						(cAliasSD2)->(DBSKIP())
					Endif
				Enddo
			Endif
			nSeq++
			If nSeq==3
				If select(cAliasSD2) > 0
					(cAliasSD2)->(dbCloseArea())
				Endif	
				J++
				if J > len(aEmpresa)
					exit
				endif
				dbSelectArea("SM0")
				dbSeek(aEmpresa[j,2]+aEmpresa[j,3],.T.)
				nSeq :=1
			Endif
		endDo
		SM0->(dbGoTo(nRegEmp))
		cFilAnt := SM0->M0_CODFIL
		
		If (lImpressao)
			ImpConfDnf ()
		Else
			(cArqTip1)->(DBGOTOP())
			While !(cArqTip1)->(EOF())
				If lSeq
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Monta base de Dados do Registro Tipo 0³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					A977Tipo0()
					lSeq :=.F.
				Endif
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Monta base de Dados do Registro Tipo 1³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				A977Tipo1()
				cChav7 :=(cArqTip1)->MOVIMEN+(cArqTip1)->NOTA+(cArqTip1)->SERIE+(cArqTip1)->CNPJ
				If (cArqTip1)->MOVIMEN=="E"
					(cArqTip3)->(dbseek(cChav7,.f.))
					While cChav7==(cArqTip3)->MOVIMEN+(cArqTip3)->NOTA+(cArqTip3)->SERIE+(cArqTip3)->CNPJ
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Monta base de Dados do Registro Tipo 3³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						A977Tipo3()
						(cArqTip3)->(dbskip())
					Enddo
				Else
					(cArqTip2)->(dbseek(cChav7,.f.))
					While cChav7==(cArqTip2)->MOVIMEN+(cArqTip2)->NOTA+(cArqTip2)->SERIE+(cArqTip2)->CNPJ
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Monta base de Dados do Registro Tipo 2³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						A977Tipo2()
						(cArqTip2)->(dbskip())
					Enddo
				Endif
				(cArqTip1)->(dbskip())
			Enddo
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Monta base de Dados do Registro Tipo 9³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cDemo=="2"
				A977Tipo0()
				A977Tipo9()
			Else
				A977Tipo9()
				lSeq :=.T.
			Endif
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Fecha arquivo texto                                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If nHandle >= 0
				FClose(nHandle)
			Endif
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Apaga arquivos temporarios                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If File(cArqTip1+".DBF")
			dbSelectArea(cArqTip1)
			dbCloseArea()
			Ferase(cArqTip1+".DBF")
			Ferase(cArqTip1+OrdBagExt())
		Endif
		
		If File(cArqTip2+".DBF")
			dbSelectArea(cArqTip2)
			dbCloseArea()
			Ferase(cArqTip2+".DBF")
			If File(cArqTip2+OrdBagExt())
				Ferase(cArqTip2+OrdBagExt())
			Endif
		Endif
		
		If File(cArqTip3+".DBF")
			dbSelectArea(cArqTip3)
			dbCloseArea()
			Ferase(cArqTip3+".DBF")
			If File(cArqTip3+OrdBagExt())
				Ferase(cArqTip3+OrdBagExt())
			Endif
		Endif
		cArqTip0	:=""
		cArqTip1    :=""
		cArqTip2	:=""
		cArqTip3    :=""
		cArqTip9    :=""
		aCamTip0	:={}
		aCamTip1	:={}
		aCamTip2	:={}
		aCamTip3	:={}
		aCamTip9	:={}
	Enddo
	
	#IFDEF TOP
		If select(cAliasSD2) > 0
			(cAliasSD2)->(dbCloseArea())
		EndIf
		If select(cAliasSD2) > 0
			(cAliasSD2)->(dbCloseArea())
		EndIf
		If select(cAliasSWN) > 0
			(cAliasSWN)->(dbCloseArea())
		EndIf
	#ENDIF
Endif
dbSelectArea(cAlias)

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³  A977DigNCM  ³ Autor ³Edstron E. Correia ³ Data ³ 23/07/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta Gets p/digitacao dos codigos de NCM                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION A977DigNCM()

LOCAL cMsg	  :=""
LOCAL cValid  :=""
Local i		  :=1
LOCAL nOpcA	  :=2
LOCAL lRet	  :=.F.
LOCAL oDlgGet
LOCAL oListBox

AADD(aListBox,STR0006)  ; AADD(aSel,.f.) ; AADD(aMsg,"") ; AADD(aValid,"")                 //"CODIGOS NCM UTILIZADOS NA DNF-DECLARACAO DE NOTAS FISCAIS"
AADD(aListBox," "); AADD(aSel,.f.) ; AADD(aMsg,"") ; AADD(aValid,"")
cMsg:=STR0018                                                                                  //"Informe os Codigos NCM"
AADD(aListBox,STR0007); AADD(aSel,.t.) ; AADD(aMsg,cMsg) ; AADD(aValid,"A977Valid('NCM')") //"01 - Codigos NCM"
AADD(aListBox,STR0025); AADD(aSel,.t.) ; AADD(aMsg,cMsg) ; AADD(aValid,"A977Valid('NCM')") //"02 - Codigos NCM"
AADD(aListBox,STR0026); AADD(aSel,.t.) ; AADD(aMsg,cMsg) ; AADD(aValid,"A977Valid('NCM')") //"03 - Codigos NCM"
AADD(aListBox,STR0027); AADD(aSel,.t.) ; AADD(aMsg,cMsg) ; AADD(aValid,"A977Valid('NCM')") //"04 - Codigos NCM"
AADD(aListBox,STR0028); AADD(aSel,.t.) ; AADD(aMsg,cMsg) ; AADD(aValid,"A977Valid('NCM')") //"05 - Codigos NCM"
//
For i:=1 to Len(aListBox)
	aListBox[i]:=OemToAnsi(aListBox[i])
	aMsg[i]:=OemToAnsi(aMsg[i])
Next

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Le informacoes do arquivo de configuracao DNF.CFP ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
A977LoadIni("DNF.CFP")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ativa ListBox com opcoes para o array da configuracao ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE MSDIALOG oDlgGet TITLE OemtoAnsi(STR0008) FROM  180,110 TO 400,700 PIXEL OF oMainWnd //"DNF"
@ 10, 20 SAY Oemtoansi(STR0005) SIZE 100, 07 OF oDlgGet PIXEL
@ 26, 18 TO 091, 277 LABEL "" OF oDlgGet PIXEL
@ 33,22 LISTBOX oListBox VAR cVar FIELDS HEADER "" ON DBLCLICK (A977GetList(oListBox)) SIZE 250,50 PIXEL
oListBox:SetArray(aListBox)
oListBox:bLine := { ||{aListBox[oListBox:nAt]}}

DEFINE SBUTTON FROM 095, 220 TYPE 1 ACTION (nOpca:=1,oDlgGet:End()) ENABLE OF oDlgGet
DEFINE SBUTTON FROM 095, 248 TYPE 2 ACTION (nOpca:=2,oDlgGet:End()) ENABLE OF oDlgGet

ACTIVATE MSDIALOG oDlgGet

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava itens no arquivo de configuracao DNF.CFP ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOpcA==1
	A977GravaIt("DNF.CFP")
Else
	lRet	:=	.T.
Endif

Return(lRet)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A977GetList ³Autor³ Juan Jose Pereira     ³ Data ³ 27.03.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Ativa Get para edicao de Elemento relacionado ao ListBox    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA977                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION A977GetList(oListBox)

Local oDlgGet2
Local nOpcaGet	:=	2
Local lUpdated	:=	.F.
Local bValid
Private cCampo	:=	"cCpoItem"

nAt	:=	oListBox:nAt

Private cValid	:=	"{||"+aValid[nAt]+"}"

If aSel[nAt]
	&cCampo	:=	aConteudo[nAt]
	bValid	:=	&(cValid)
	DEFINE MSDIALOG oDlgGet2 TITLE aMsg[nAt] FROM  300,100 TO 400,620 PIXEL OF oListBox
	@ 08, 20 TO 27, 237 LABEL "" OF oDlgGet2 PIXEL
	
	@ 15, 24 MSGET &cCampo PICTURE "@!" VALID Eval(bValid) SIZE 210, 08 OF oDlgGet2 PIXEL
	
	DEFINE SBUTTON FROM 032, 182 TYPE 1 ACTION (lUpdated:=.t.,oDlgGet2:End()) ENABLE OF oDlgGet2
	DEFINE SBUTTON FROM 032, 210 TYPE 2 ACTION (lUpdated:=.f.,oDlgGet2:End()) ENABLE OF oDlgGet2
	
	ACTIVATE MSDIALOG oDlgGet2
	If lUpdated
		aConteudo[nAt]	:=	StrTran(&cCampo,'"',"'")
	Endif
Endif

RETURN
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A977GravaIt  ³ Autor ³ Juan Jose Pereira ³ Data ³ 27.07.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava arquivo de configuracao .CFP                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION A977GravaIt(ArqIni)

LOCAL cArqBkp
LOCAL aGravar	:=	{}
LOCAL i

cArqBkp	:=	StrTran(ArqIni,".CFP",".#CF")
If File(cArqBkp)
	Ferase(cArqBkp)
Endif
FRename(ArqIni,cArqBkp)
nHandle	:=	MSFCREATE(ArqIni)

For i	:=	1 to Len(aListBox)
	If IsDigit(Substr(aListBox[i],2,4))
		AADD(aGravar,"["+Substr(aListBox[i],2,4)+"]="+Rtrim(aConteudo[i])+Chr(13)+Chr(10))
	EndIf
Next
For i	:=	1 to Len(aGravar)
	FWrite(nHandle,aGravar[i],Len(aGravar[i]))
Next
FClose(nHandle)

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A977LoadIni ³Autor³     Marcos Simidu     ³ Data ³ 27.03.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Carrega conteudo do arquivo DNF.CFP 	                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA977                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION A977LoadIni(ArqIni)

Local i	:=	0
Local cConteudo :=	""

If File(ArqIni)
	For i	:=	1 to Len(aListBox)
		cConteudo	:=	""
		If IsDigit(Substr(aListBox[i],2,4))
			cConteudo	:=	A977LeDNF(Substr(aListBox[i],2,4))
			If Len(cConteudo)<254
				cConteudo	:=	cConteudo+Space(254-Len(cConteudo))
			EndIf
		Endif
		AADD(aConteudo,cConteudo)
	Next
Else
	For i:=1 to Len(aListBox)
		AADD(aConteudo,Space(254))
	Next
Endif

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A977LeDNF()  ³ Autor ³ Juan Jose Pereira ³ Data ³ 27.07.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Le Arquivo DNF.CFP 	                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A977LeDNF(cCPO,lEnd)

Local cIni		:=	""
LOCAL cArqIni	:=	"DNF.CFP"
LOCAL cConteudo:=	""
LOCAL	i:=0

lEnd	:=	If(lEnd==NIL,.F.,lEnd)
cCPO	:=	"["+cCPO+"]="

If !File(cArqIni)
	Help(" ",1,"DNF.CFP")
	lContinua:=	.f.
	lEnd		:=	.T.
	Return(cIni)
Else
	cConteudo	:=	MemoRead(cArqIni)
	nLinhas		:=	MlCount(cConteudo,254)
	For i:=1 to nLinhas
		cLinha	:=	AllTrim(MemoLine(cConteudo,254,i))
		If cCPO$cLinha
			cIni	:=	Substr(cLinha,8)
			Exit
		Endif
	Next
Endif

Return(cIni)



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³  A977Valid() ³ Autor ³Edstron E. Correia ³ Data ³ 24.07.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao do ListBox.                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A977Valid(cCampo)
Local lRet	    := .T.
LOCAL cConteudo	:=	&(ReadVar())
Local nIni	    :=	1
Local nFim	    :=	1

cConteudo	:=	Alltrim(cConteudo)
cCampo		:=	(If(cCampo==NIL," ",cCampo))
If cCampo=="NCM"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se os caracteres digitados sao validos³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nIni=1 TO Len(cConteudo)
		If !Substr(cConteudo,nIni,1)$"0123456789/"
			lRet:=.F.
			exit
		Endif
	Next
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se os codigos NCM possuem 8 digitos ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nIni=1 TO Len(cConteudo)
		nFim :=AT("/",cConteudo)-1
		If Len(Substr(cConteudo,nIni,nFim))<>8
			lRet:=.F.
			exit
		Endif
		cConteudo :=Substr(cConteudo,nFim+2,Len(cConteudo))
		If Len(cConteudo)==0
			exit
		Endif
		nIni :=0
	Next
Endif

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³  R977Temp  ³Autor³ Edstron E. Correia ³ Data ³ 10.09.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Cria arquivos temporarios para DNF                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A977Temp()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria Arquivo Temporario Tipo 1 - Dados Mestre da Nota Fiscal ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AADD(aCamTip1,{"TIPO"		,"C"	,01	,0	})
AADD(aCamTip1,{"CNPJ"		,"C"	,14	,0	})
AADD(aCamTip1,{"NOTA"		,"C"	,TamSX3("F2_DOC")[1]	,0	})
AADD(aCamTip1,{"SERIE"		,"C"	,TamSX3("F2_SERIE")[1]	,0	})
AADD(aCamTip1,{"SDOC"		,"C"	,03	,0	})
AADD(aCamTip1,{"EMISSAO"		,"C"	,08	,0	})
AADD(aCamTip1,{"EFETIVA"		,"C"	,08	,0	})
AADD(aCamTip1,{"CFOP"		,"C"	,04	,0	})
AADD(aCamTip1,{"VALNF"		,"C"	,14	,0	})
AADD(aCamTip1,{"VALIPI"		,"C"	,14	,0	})
AADD(aCamTip1,{"ISENTO"		,"C"	,14	,0	})
AADD(aCamTip1,{"CANCEL"		,"C"	,01	,0	})
AADD(aCamTip1,{"MOVIMEN"		,"C"	,01	,0	})
AADD(aCamTip1,{"COMPL"		,"C"	,01	,0	})
AADD(aCamTip1,{"NFVINCU"		,"C"	,TamSX3("F2_DOC")[1]	,0	})
AADD(aCamTip1,{"SERVINCU"	,"C"	,TamSX3("F2_SERIE")[1]	,0	})
AADD(aCamTip1,{"SDOCV"		,"C"	,03	,0	})
AADD(aCamTip1,{"DTVINCU"		,"C"	,08	,0	})
cArqTip1	:=	CriaTrab(aCamTip1)
dbUseArea(.T.,,cArqTip1,cArqTip1,.F.,.F.)
IndRegua(cArqTip1,cArqTip1,"CNPJ+MOVIMEN+NOTA+SERIE",,,STR0015) //"Indexando Arquivo Tipo 1 - Mestre"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria Arquivo Temporario Tipo 2 - Dados dos Produtos/Nota Fiscal de Saida ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AADD(aCamTip2,{"TIPO"		,"C"	,01	,0	})
AADD(aCamTip2,{"CNPJ"		,"C"	,14	,0	})
AADD(aCamTip2,{"NOTA"		,"C"	,TamSX3("F2_DOC")[1]	,0	})
AADD(aCamTip2,{"SERIE"		,"C"	,TamSX3("F2_SERIE")[1]	,0	})
AADD(aCamTip2,{"SDOC"		,"C"	,03	,0	})
AADD(aCamTip2,{"EMISSAO"	,"C"	,08	,0	})
AADD(aCamTip2,{"CFOP"		,"C"	,04	,0	})
AADD(aCamTip2,{"ITEM"		,"C"	,02	,0	})
AADD(aCamTip2,{"CODMERC"	,"C"	,20	,0	})
AADD(aCamTip2,{"DESMERC"	,"C"	,45	,0	})
AADD(aCamTip2,{"NCM"		,"C"	,08	,0	})
AADD(aCamTip2,{"PRODDNF"	,"C"	,03	,0	})
AADD(aCamTip2,{"ESPECIE"	,"C"	,02	,0	})
AADD(aCamTip2,{"QUANTIDADE"	,"C"	,14	,0	})
AADD(aCamTip2,{"VALUNIT"	,"C"	,14	,0	})
AADD(aCamTip2,{"VALTOT"	    ,"C"	,14	,0	})
AADD(aCamTip2,{"BASEIPI"	,"C"	,14	,0	})
AADD(aCamTip2,{"VALIPI"	    ,"C"	,14	,0	})
AADD(aCamTip2,{"UNIDADE"	,"C"	,10	,0	})
AADD(aCamTip2,{"CODUNID"	,"C"	,02	,0	})
AADD(aCamTip2,{"FATOR"	    ,"C"	,14	,0	})
AADD(aCamTip2,{"QTDEUNIPAD"	,"C"	,14	,0	})
AADD(aCamTip2,{"MOVIMEN"	,"C"	,01	,0	})
cArqTip2	:=	CriaTrab(aCamTip2)
dbUseArea(.T.,,cArqTip2,cArqTip2,.F.,.F.)
IndRegua(cArqTip2,cArqTip2,"MOVIMEN+NOTA+SERIE+CNPJ",,,STR0016) //"Indexando Arquivo Tipo 2"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria Arquivo Temporario Tipo 3 - Dados dos Produtos/Nota Fiscal de Entrada ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AADD(aCamTip3,{"TIPO"		,"C"	,01	,0	})
AADD(aCamTip3,{"CNPJ"		,"C"	,14	,0	})
AADD(aCamTip3,{"NOTA"		,"C"	,TamSX3("F2_DOC")[1]	,0	})
AADD(aCamTip3,{"SERIE"		,"C"	,TamSX3("F2_SERIE")[1]	,0	})
AADD(aCamTip3,{"SDOC"		,"C"	,03	,0	})
AADD(aCamTip3,{"EMISSAO"	,"C"	,08	,0	})
AADD(aCamTip3,{"CFOP"		,"C"	,04	,0	})
AADD(aCamTip3,{"ITEM"		,"C"	,02	,0	})
AADD(aCamTip3,{"CODMERC"	,"C"	,20	,0	})
AADD(aCamTip3,{"DESMERC"	,"C"	,45	,0	})
AADD(aCamTip3,{"NCM"		,"C"	,08	,0	})
AADD(aCamTip3,{"PRODDNF"	,"C"	,03	,0	})
AADD(aCamTip3,{"ESPECIE"	,"C"	,02	,0	})
AADD(aCamTip3,{"QUANTIDADE"	,"C"	,14	,0	})
AADD(aCamTip3,{"VALUNIT"	,"C"	,14	,0	})
AADD(aCamTip3,{"VALTOT"	    ,"C"	,14	,0	})
AADD(aCamTip3,{"BASEIPI"	,"C"	,14	,0	})
AADD(aCamTip3,{"VALIPI"	    ,"C"	,14	,0	})
AADD(aCamTip3,{"UNIDADE"	,"C"	,10	,0	})
AADD(aCamTip3,{"CODUNID"	,"C"	,02	,0	})
AADD(aCamTip3,{"FATOR"	    ,"C"	,14	,0	})
AADD(aCamTip3,{"QTDEUNIPAD"	,"C"	,14	,0	})
AADD(aCamTip3,{"MOVIMEN"	,"C"	,01	,0	})
AADD(aCamTip3,{"NUMERODI"	,"C"	,10	,0	})
cArqTip3	:=	CriaTrab(aCamTip3)
dbUseArea(.T.,,cArqTip3,cArqTip3,.F.,.F.)
IndRegua(cArqTip3,cArqTip3,"MOVIMEN+NOTA+SERIE+CNPJ",,,STR0017) //"Indexando Arquivo Tipo 3"
Return(.t.)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³  A977Grava   ³Autor ³     Marcos Simidu    ³Data³ 10/02/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava registro no arquivo texto.                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A977Grava(cConteudo)

cConteudo += Chr(13)+Chr(10)

If !lEnd
	FWrite(nHandle,cConteudo,Len(cConteudo))
Endif

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A977Tipo0    ³Autor ³ Edstron E. Correia  ³ Data ³ 25/07/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Grava Registro Tipo 0 - Dados do Declarante                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A977Tipo0()
Local cRegistro, cVar :=""
cRegistro   := A977Fill(STRZERO(nLin,6),6)
cRegistro   += A977Fill("0",1)
cRegistro   += A977Fill(A977Num2Chr(Val(SM0->M0_CGC),14,0),14)
cVar        := SM0->M0_NOMECOM
cVar        := A977Chk(cVar)
cRegistro   += A977Fill(cVar,65)
cRegistro   += A977Fill((STRZERO(nMes,2)+STRZERO(nAno,4)),6)
cRegistro   += A977Fill(cDemo,1)
If MV_PAR08==1
	cRegistro   += A977Fill("DNF001.1",8)
Else
	cRegistro   += A977Fill("DNF001.2",8)
EndIf
cRegistro   += A977Fill("00000000000000",14)
cRegistro 	:= A977Fill(cRegistro,115)
nLin        :=nLin+1
a977Grava(cRegistro)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A977Tipo1    ³Autor ³ Andreia dos Santos  ³ Data ³ 30/08/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Grava Registro Tipo 1 - Dados Mestre da Nota Fiscal         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A977Tipo1()

Local cRegistro
cRegistro   :=A977Fill(STRZERO(nLin,6),06)
cRegistro   +=(cArqTip1)->TIPO
cRegistro   +=(cArqTip1)->CNPJ
cRegistro   +=RetNf((cArqTip1)->NOTA,6,"N")
cRegistro   +=STRZERO(VAL((cArqTip1)->SDOC),3)
cRegistro   +=(cArqTip1)->EMISSAO
cRegistro   +=(cArqTip1)->EFETIVA
cRegistro   +=(cArqTip1)->CFOP
cRegistro   +=(cArqTip1)->VALNF
cRegistro   +=(cArqTip1)->VALIPI
cRegistro   +=(cArqTip1)->ISENTO
cRegistro   +=(cArqTip1)->CANCEL
cRegistro   +=(cArqTip1)->MOVIMEN
cRegistro   +=(cArqTip1)->COMPL
cRegistro   +=(cArqTip1)->NFVINCU
cRegistro   +=(cArqTip1)->SDOCV
cRegistro   +=(cArqTip1)->DTVINCU
cRegistro   :=A977Fill(cRegistro,112)
nLin        :=nLin+1
a977Grava(cRegistro)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A977Tipo2    ³Autor ³ Andreia dos Santos  ³ Data ³ 30/08/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Grava Registro Tipo 2 - Dados dos Produtos                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A977Tipo2()

Local cRegistro, cVar :=""
cRegistro   :=A977Fill(STRZERO(nLin,6),06)
cRegistro   +=(cArqTip2)->TIPO
cRegistro   +=(cArqTip2)->CNPJ
cRegistro   +=RetNf((cArqTip2)->NOTA,6,"N")
cRegistro   +=STRZERO(VAL((cArqTip2)->SDOC),3)
cRegistro   +=(cArqTip2)->EMISSAO
cRegistro   +=(cArqTip2)->CFOP
cRegistro   +=(cArqTip2)->ITEM
cRegistro   +=(cArqTip2)->CODMERC
cVar        :=(cArqTip2)->DESMERC
cVar        :=A977Chk(cVar)
cRegistro   +=A977Fill(cVar,45)
cRegistro   +=(cArqTip2)->NCM
If MV_PAR08==2
	cRegistro   +=(cArqTip2)->PRODDNF
Else
	cRegistro   +=(cArqTip2)->ESPECIE
EndIf
cRegistro   +=(cArqTip2)->QUANTIDADE
cRegistro   +=(cArqTip2)->VALUNIT
cRegistro   +=(cArqTip2)->VALTOT
cRegistro   +=(cArqTip2)->BASEIPI
cRegistro   +=(cArqTip2)->VALIPI
cRegistro   +=(cArqTip2)->UNIDADE
If (MV_PAR08==1)
	cRegistro   +=(cArqTip2)->CODUNID
EndIf
cRegistro   +=(cArqTip2)->FATOR
cRegistro   +=(cArqTip2)->QTDEUNIPAD
If MV_PAR08==2
	cRegistro   :=A977Fill(cRegistro,228)
Else
	cRegistro   :=A977Fill(cRegistro,229)
EndIf
nLin        :=nLin+1
a977Grava(cRegistro)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A977Tipo3    ³Autor ³ Andreia dos Santos  ³ Data ³ 30/08/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Grava Registro Tipo 3 - Dados dos Produtos                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A977Tipo3()

Local cRegistro, cVar := ""
cRegistro   :=A977Fill(STRZERO(nLin,6),06)
cRegistro   +=(cArqTip3)->TIPO
cRegistro   +=(cArqTip3)->CNPJ
cRegistro   +=RetNf((cArqTip3)->NOTA,6,"N")
cRegistro   +=STRZERO(VAL((cArqTip3)->SDOC),3)
cRegistro   +=(cArqTip3)->EMISSAO
cRegistro   +=(cArqTip3)->CFOP
cRegistro   +=(cArqTip3)->ITEM
cRegistro   +=(cArqTip3)->CODMERC
cVar        :=(cArqTip3)->DESMERC
cVar        :=A977Chk(cVar)
cRegistro   +=A977Fill(cVar,45)                 // (cArqTip3)->DESMERC
cRegistro   +=(cArqTip3)->NCM
If MV_PAR08==2
	cRegistro   +=(cArqTip3)->PRODDNF
Else
	cRegistro   +=(cArqTip3)->ESPECIE
EndIf
cRegistro   +=(cArqTip3)->QUANTIDADE
cRegistro   +=(cArqTip3)->VALUNIT
cRegistro   +=(cArqTip3)->VALTOT
cRegistro   +=(cArqTip3)->BASEIPI
cRegistro   +=(cArqTip3)->VALIPI
cRegistro   +=(cArqTip3)->UNIDADE
If (MV_PAR08==1)
	cRegistro   +=(cArqTip3)->CODUNID
EndIf
cRegistro   +=(cArqTip3)->FATOR
cRegistro   +=(cArqTip3)->QTDEUNIPAD
cRegistro   +=(cArqTip3)->NUMERODI
If MV_PAR08==2
	cRegistro   :=A977Fill(cRegistro,238)
Else
	cRegistro   :=A977Fill(cRegistro,239)
EndIf
nLin        :=nLin+1
a977Grava(cRegistro)
Return



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A977Tipo9    ³Autor ³ Edstron E. Correia  ³ Data ³ 25/07/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Grava Registro Tipo 9 - Totais                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A977Tipo9()
Local cRegistro
cRegistro   := A977Fill(STRZERO(nLin,6),6)
cRegistro   += A977Fill("9",1)
cRegistro   += A977Fill(STRZERO(nLin,12),12)
cRegistro 	:= A977Fill(cRegistro,19)
a977Grava(cRegistro)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A977Fill     ³Autor ³ Andreia dos Santos  ³ Data ³ 30/08/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Encaixa conteudo em espaco especificado                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A977Fill(cConteudo,nTam)

cConteudo	:=If(cConteudo==NIL,"",cConteudo)
If Len(cConteudo)>nTam
	cRetorno	:=	Substr(cConteudo,1,nTam)
Else
	cRetorno	:=	cConteudo+Space(nTam-Len(cConteudo))
Endif

Return (cRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A977Num2Chr  ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Converte numerico para formato binario                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A977Num2Chr(nValor,nTam,nDec)

Local nInteiro
LOCAL nDecimal
LOCAL cInteiro
LOCAL cDecimal
LOCAL cValor

nDec	:=	If(nDec==NIL,2,nDec)

nInteiro	:=	NoRound (nValor, 0)
nDecimal	:=	Abs(nValor-nInteiro)

cInteiro	:=	StrZero(nInteiro,nTam-nDec)
cDecimal	:=	StrZero(nDecimal*(10^nDec),nDec)

cValor	:=	cInteiro+cDecimal

Return (cValor)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A977DatChr   ³Autor ³ Edstron E. Correia  ³ Data ³ 27/07/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Converte data para formato binario                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A977DatChr(dData)

Local dDatNew

dDatNew :=STRZERO(DAY(dData),2)+STRZERO(MONTH(dData),2)+STRZERO(YEAR(dData),4)
Return (dDatNew)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A977Chk      ³Autor ³ Edstron E. Correia  ³ Data ³ 12/09/01            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Corrige o conteudo de uma variavel qdo. existirem caracteres especiais ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A977Chk(cNewVar)

cNewVar :=STRTRAN(cNewVar,CHR(33)," ")  // = !
cNewVar :=STRTRAN(cNewVar,CHR(34)," ")  // = "
cNewVar :=STRTRAN(cNewVar,CHR(35)," ")  // = #
cNewVar :=STRTRAN(cNewVar,CHR(36)," ")  // = $
cNewVar :=STRTRAN(cNewVar,CHR(37)," ")  // = %
cNewVar :=STRTRAN(cNewVar,CHR(38)," ")  // = &
cNewVar :=STRTRAN(cNewVar,CHR(39)," ")  // = '
cNewVar :=STRTRAN(cNewVar,CHR(40)," ")  // = (
cNewVar :=STRTRAN(cNewVar,CHR(41)," ")  // = )
cNewVar :=STRTRAN(cNewVar,CHR(42)," ")  // = *
cNewVar :=STRTRAN(cNewVar,CHR(43)," ")  // +
cNewVar :=STRTRAN(cNewVar,CHR(44)," ")  // ,
cNewVar :=STRTRAN(cNewVar,CHR(45)," ")  // -
cNewVar :=STRTRAN(cNewVar,CHR(46)," ")  // .
cNewVar :=STRTRAN(cNewVar,CHR(47)," ")  // /
cNewVar :=STRTRAN(cNewVar,CHR(58)," ")  // :
cNewVar :=STRTRAN(cNewVar,CHR(59)," ")  // ;
cNewVar :=STRTRAN(cNewVar,CHR(60)," ")  // <
cNewVar :=STRTRAN(cNewVar,CHR(61)," ")  // =
cNewVar :=STRTRAN(cNewVar,CHR(62)," ")  //= >
cNewVar :=STRTRAN(cNewVar,CHR(63)," ")  //= ?
cNewVar :=STRTRAN(cNewVar,CHR(64)," ")  //= @
cNewVar :=STRTRAN(cNewVar,CHR(91)," ")  //= [
cNewVar :=STRTRAN(cNewVar,CHR(92)," ")  //= \
cNewVar :=STRTRAN(cNewVar,CHR(93)," ")  //= ]
cNewVar :=STRTRAN(cNewVar,CHR(94)," ")  // = ^
cNewVar :=STRTRAN(cNewVar,CHR(95)," ")  // = _
cNewVar :=STRTRAN(cNewVar,CHR(96)," ")  // = `
cNewVar :=STRTRAN(cNewVar,CHR(123)," ") // = {
cNewVar :=STRTRAN(cNewVar,CHR(124)," ") //= |
cNewVar :=STRTRAN(cNewVar,CHR(125)," ") //= }
cNewVar :=STRTRAN(cNewVar,CHR(126)," ") //= ~
cNewVar :=STRTRAN(cNewVar,CHR(127)," ") //= 
cNewVar :=STRTRAN(cNewVar,CHR(128),"C") //= €
cNewVar :=STRTRAN(cNewVar,CHR(129),"u") //= 
cNewVar :=STRTRAN(cNewVar,CHR(130),"e") //= ‚
cNewVar :=STRTRAN(cNewVar,CHR(131),"a") //= ƒ
cNewVar :=STRTRAN(cNewVar,CHR(132),"a") //= „
cNewVar :=STRTRAN(cNewVar,CHR(133),"a") //= …
cNewVar :=STRTRAN(cNewVar,CHR(134),"a") //= †
cNewVar :=STRTRAN(cNewVar,CHR(135),"c") //= ‡
cNewVar :=STRTRAN(cNewVar,CHR(136),"e") //= ˆ
cNewVar :=STRTRAN(cNewVar,CHR(137),"e") //= ‰
cNewVar :=STRTRAN(cNewVar,CHR(138),"e") //= Š
cNewVar :=STRTRAN(cNewVar,CHR(139),"i") //= ‹
cNewVar :=STRTRAN(cNewVar,CHR(140),"i") //= Œ
cNewVar :=STRTRAN(cNewVar,CHR(141),"i") //= 
cNewVar :=STRTRAN(cNewVar,CHR(142),"A") //= 
cNewVar :=STRTRAN(cNewVar,CHR(143),"A") //= 
cNewVar :=STRTRAN(cNewVar,CHR(144),"E") //= 
cNewVar :=STRTRAN(cNewVar,CHR(145)," ") //= ‘
cNewVar :=STRTRAN(cNewVar,CHR(146)," ") //= ’
cNewVar :=STRTRAN(cNewVar,CHR(147),"o") //= “
cNewVar :=STRTRAN(cNewVar,CHR(148),"o") //= ”
cNewVar :=STRTRAN(cNewVar,CHR(149),"o") //= •
cNewVar :=STRTRAN(cNewVar,CHR(150),"u") //= –
cNewVar :=STRTRAN(cNewVar,CHR(151),"u") //= —
cNewVar :=STRTRAN(cNewVar,CHR(152),"y") //= ˜
cNewVar :=STRTRAN(cNewVar,CHR(153),"O") //= ™
cNewVar :=STRTRAN(cNewVar,CHR(154),"U") //= š
cNewVar :=STRTRAN(cNewVar,CHR(155)," ") //= ›
cNewVar :=STRTRAN(cNewVar,CHR(156)," ") //= œ
cNewVar :=STRTRAN(cNewVar,CHR(157)," ") //= 
cNewVar :=STRTRAN(cNewVar,CHR(158)," ") //= 
cNewVar :=STRTRAN(cNewVar,CHR(159),"f") //= Ÿ
cNewVar :=STRTRAN(cNewVar,CHR(161),"i") //= ¡
cNewVar :=STRTRAN(cNewVar,CHR(162),"o") //= ¢
cNewVar :=STRTRAN(cNewVar,CHR(163),"u") //= £
cNewVar :=STRTRAN(cNewVar,CHR(164),"n") //= ¤
cNewVar :=STRTRAN(cNewVar,CHR(165),"N") //= ¥
cNewVar :=STRTRAN(cNewVar,CHR(166)," ") //= ¦
cNewVar :=STRTRAN(cNewVar,CHR(167)," ") //= §
cNewVar :=STRTRAN(cNewVar,CHR(168)," ") //= ¨
cNewVar :=STRTRAN(cNewVar,CHR(169)," ") //= ©
cNewVar :=STRTRAN(cNewVar,CHR(170)," ") //= ª
cNewVar :=STRTRAN(cNewVar,CHR(171)," ") //= «
cNewVar :=STRTRAN(cNewVar,CHR(172)," ") //= ¬
cNewVar :=STRTRAN(cNewVar,CHR(173)," ") //= ­
cNewVar :=STRTRAN(cNewVar,CHR(174)," ") //= ®
cNewVar :=STRTRAN(cNewVar,CHR(175)," ") //= ¯
cNewVar :=STRTRAN(cNewVar,CHR(176)," ") //= °
cNewVar :=STRTRAN(cNewVar,CHR(177)," ") //= ±
cNewVar :=STRTRAN(cNewVar,CHR(178)," ") //= ²
cNewVar :=STRTRAN(cNewVar,CHR(179)," ") //= ³
cNewVar :=STRTRAN(cNewVar,CHR(180)," ") //= ´
cNewVar :=STRTRAN(cNewVar,CHR(181)," ") //= µ
cNewVar :=STRTRAN(cNewVar,CHR(182)," ") //= ¶
cNewVar :=STRTRAN(cNewVar,CHR(183)," ") //= ·
cNewVar :=STRTRAN(cNewVar,CHR(184)," ") //= ¸
cNewVar :=STRTRAN(cNewVar,CHR(185)," ") //= ¹
cNewVar :=STRTRAN(cNewVar,CHR(186)," ") //= º
cNewVar :=STRTRAN(cNewVar,CHR(187)," ") //= »
cNewVar :=STRTRAN(cNewVar,CHR(188)," ") //= ¼
cNewVar :=STRTRAN(cNewVar,CHR(189)," ") //= ½
cNewVar :=STRTRAN(cNewVar,CHR(190)," ") //= ¾
cNewVar :=STRTRAN(cNewVar,CHR(191)," ") //= ¿
cNewVar :=STRTRAN(cNewVar,CHR(192)," ") //= À
cNewVar :=STRTRAN(cNewVar,CHR(193)," ") //= Á
cNewVar :=STRTRAN(cNewVar,CHR(194)," ") //= Â
cNewVar :=STRTRAN(cNewVar,CHR(195)," ") //= Ã
cNewVar :=STRTRAN(cNewVar,CHR(196)," ") //= Ä
cNewVar :=STRTRAN(cNewVar,CHR(197)," ") //= Å
cNewVar :=STRTRAN(cNewVar,CHR(198)," ") //= Æ
cNewVar :=STRTRAN(cNewVar,CHR(199)," ") //= Ç
cNewVar :=STRTRAN(cNewVar,CHR(200)," ") //= È
cNewVar :=STRTRAN(cNewVar,CHR(201)," ") //= É
cNewVar :=STRTRAN(cNewVar,CHR(202)," ") //= Ê
cNewVar :=STRTRAN(cNewVar,CHR(203)," ") //= Ë
cNewVar :=STRTRAN(cNewVar,CHR(204)," ") //= Ì
cNewVar :=STRTRAN(cNewVar,CHR(205)," ") //= Í
cNewVar :=STRTRAN(cNewVar,CHR(206)," ") //= Î
cNewVar :=STRTRAN(cNewVar,CHR(207)," ") //= Ï
cNewVar :=STRTRAN(cNewVar,CHR(208)," ") //= Ğ
cNewVar :=STRTRAN(cNewVar,CHR(209)," ") //= Ñ
cNewVar :=STRTRAN(cNewVar,CHR(210)," ") //= Ò
cNewVar :=STRTRAN(cNewVar,CHR(211)," ") //= Ó
cNewVar :=STRTRAN(cNewVar,CHR(212)," ") //= Ô
cNewVar :=STRTRAN(cNewVar,CHR(213)," ") //= Õ
cNewVar :=STRTRAN(cNewVar,CHR(214)," ") //= Ö
cNewVar :=STRTRAN(cNewVar,CHR(215)," ") //= ×
cNewVar :=STRTRAN(cNewVar,CHR(216)," ") //= Ø
cNewVar :=STRTRAN(cNewVar,CHR(217)," ") //= Ù
cNewVar :=STRTRAN(cNewVar,CHR(218)," ") //= Ú
cNewVar :=STRTRAN(cNewVar,CHR(219)," ") //= Û
cNewVar :=STRTRAN(cNewVar,CHR(220)," ") //= Ü
cNewVar :=STRTRAN(cNewVar,CHR(221)," ") //= İ
cNewVar :=STRTRAN(cNewVar,CHR(222)," ") //= Ş
cNewVar :=STRTRAN(cNewVar,CHR(223)," ") //= ß
cNewVar :=STRTRAN(cNewVar,CHR(224)," ") //= à
cNewVar :=STRTRAN(cNewVar,CHR(225)," ") //= á
cNewVar :=STRTRAN(cNewVar,CHR(226)," ") //= â
cNewVar :=STRTRAN(cNewVar,CHR(227)," ") //= ã
cNewVar :=STRTRAN(cNewVar,CHR(228)," ") //= ä
cNewVar :=STRTRAN(cNewVar,CHR(229)," ") //= å
cNewVar :=STRTRAN(cNewVar,CHR(230)," ") //= æ
cNewVar :=STRTRAN(cNewVar,CHR(231)," ") //= ç
cNewVar :=STRTRAN(cNewVar,CHR(232)," ") //= è
cNewVar :=STRTRAN(cNewVar,CHR(233)," ") //= é
cNewVar :=STRTRAN(cNewVar,CHR(234)," ") //= ê
cNewVar :=STRTRAN(cNewVar,CHR(235)," ") //= ë
cNewVar :=STRTRAN(cNewVar,CHR(236)," ") //= ì
cNewVar :=STRTRAN(cNewVar,CHR(237)," ") //= í
cNewVar :=STRTRAN(cNewVar,CHR(238)," ") //= î
cNewVar :=STRTRAN(cNewVar,CHR(239)," ") //= ï
cNewVar :=STRTRAN(cNewVar,CHR(240)," ") //= ğ
cNewVar :=STRTRAN(cNewVar,CHR(241)," ") //= ñ
cNewVar :=STRTRAN(cNewVar,CHR(242)," ") //= ò
cNewVar :=STRTRAN(cNewVar,CHR(243)," ") //= ó
cNewVar :=STRTRAN(cNewVar,CHR(244)," ") //= ô
cNewVar :=STRTRAN(cNewVar,CHR(245)," ") //= õ
cNewVar :=STRTRAN(cNewVar,CHR(246)," ") //= ö
cNewVar :=STRTRAN(cNewVar,CHR(247)," ") //= ÷
cNewVar :=STRTRAN(cNewVar,CHR(248)," ") //= ø
cNewVar :=STRTRAN(cNewVar,CHR(249)," ") //= ù
cNewVar :=STRTRAN(cNewVar,CHR(250)," ") //= ú
cNewVar :=STRTRAN(cNewVar,CHR(251)," ") //= û
cNewVar :=STRTRAN(cNewVar,CHR(252)," ") //= ü
cNewVar :=STRTRAN(cNewVar,CHR(253)," ") //= ı
cNewVar :=STRTRAN(cNewVar,CHR(254)," ") //= ş
cNewVar :=STRTRAN(cNewVar,CHR(255)," ") //= ÿ
While AT("  ",cNewVar)>0
	  cNewVar :=STRTRAN(cNewVar,"  "," ") //= ÿ
Enddo
cVar    :=cNewVar
Return (cVar)

