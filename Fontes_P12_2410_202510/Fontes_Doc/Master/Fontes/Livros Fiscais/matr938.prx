#INCLUDE "MATR938.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MATR938   ³ Autor ³Gustavo G. Rueda       ³ Data ³17/06/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Mapa de apuracao dos atacadistas                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³MATR938 ()      			                   	              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum   				     	               		          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³NIL            				     	               		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³SIGAFIS                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function MATR938 ()
	Local	wnrel		:=	"MATR938"	
	Local	cDesc1		:=	STR0001	//"Mapa de Apuração dos atacadistas."
	Local	cDesc2		:=	STR0002	//"Emite o Mapa de Apuração dos atacadistas conforme regime Especial do ICMS - Decreto 22.958 de 08/10/2004."
	Local	cDesc3		:=	""
	Local	Tamanho		:=	"G"
	Local	cAlias		:=	"SF3"
	Local	Titulo		:=	STR0001	//"Mapa de Apuração dos atacadistas."
	Local	lEnd		:=	.F.		//'Controle de cancelamento do relatorio'
	Local	lRet		:=	.T.
	Local	cPerg		:=	"MTR938"
	Local 	lVerpesssen := Iif(FindFunction("Verpesssen"),Verpesssen(),.T.)

	Private Limite		:=	220
	//
	Private	aReturn		:=	{STR0003, 1, STR0004, 1, 2, 1, '', 1}		//"Zebrado"###"Administracao"
	Private	nLastKey	:=	0  //' Controla o cancelamento da SetPrint e SetDefault'
	//
	
	If lVerpesssen
		Pergunte (cPerg, .F.)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Envia controle para a funcao SETPRINT                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		wnrel	:=	SetPrint (cAlias, wnrel, cPerg, @titulo, cDesc1, cDesc2, cDesc3, .F.,, .F., Tamanho,, .T.)
		//
		If (nLastKey==27)
			Set Filter to
			Return (lRet)
		Endif
		//
		SetDefault (aReturn, cAlias)
		//
		If (nLastKey==27)
			Set Filter to
			Return (lRet)
		Endif
		//
		RptStatus({|lEnd| R938Imp (@lEnd, wnRel, cAlias, Titulo, Tamanho)}, Titulo)
		//
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Se impressao em disco, chama o gerenciador de impressao...          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (aReturn[5]==1)
			DbCommitAll ()
			OurSpool (wnrel)
		Endif
		MS_FLUSH()
	EndIf
		
Return (lRet)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³R938Imp   ³ Autor ³Gustavo G. Rueda       ³ Data ³17/06/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Imprime a Relacao de Retencao de IRRF                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1 - Variavel de controle para cancelamento do relatorio ³±±
±±³          ³ExpC1 - Nome do Arquivo (TMSR430)                           ³±±
±±³          ³ExpC2 - Alias utilizado na Filtragem (DTY)                  ³±±
±±³          ³ExpC3 - Titulo do Relatorio                                 ³±±
±±³          ³ExpN1 - Tamanho do Relatorio (G)                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³NIL                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MATR938                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R938Imp (lEnd, wnRel, cAlias, Titulo, Tamanho)
	Local	lRet	:=	.T.
	Local	aLayCabec	:=	R938Lay()[1]
	Local	aLayResumo	:=	R938Lay()[2]
	Local	nLi			:=	0
	Local	nFolha		:=	1
	Local	dDtIni		:=	MV_PAR01
	Local	dDtFim		:=	MV_PAR02
	Local	cAliasSf3	:=	"SF3"
	Local	cQuery		:=	""
	Local	cSul		:=	"PR/SC/RS/"
	Local	cSudeste	:=	"ES/MG/RJ/SP/"
	Local	cNorte		:=	"AC/AM/AP/PA/RO/RR/TO/"
	Local	cNordeste	:=	"BA/MA/PE/RN/AL/CE/PB/PI/SE/"
	Local	cCentOeste	:=	"DF/GO/MT/MS/"
	Local	cEntSai		:=	""
	Local	nTotPag		:=	0
	Local	nTotGeral	:=	0
	Local	nI			:=	0
	Local	nNumLin		:=	63
	Local	cFilterUser := aReturn[7]
	Local   lConsFinal  := .F. 
	//
	#IFDEF TOP
		Local	aStruSF3  	:= 	SF3->(dbStruct())
		Local	nSf3		:=	0
	#ELSE
		Local	cIndSF3		:=	""
		Local	cChave		:=	""
	#ENDIF
	//
	DbSelectArea ("SF3")
	//
	#IFDEF TOP
		If (TcSrvType ()<>"AS/400")
		    cAliasSF3	:= 	"R938Imp"
		    //
	    	cQuery	:=	"SELECT * "
			cQuery	+=	"FROM "
			cQuery	+=	RetSqlName ("SF3")+" "
			cQuery	+=	"WHERE "
			cQuery	+=	"F3_FILIAL='"+xFilial ("SF3")+"' AND "
			cQuery	+=	"F3_ENTRADA>='"+DToS (dDtIni)+"' AND "
			cQuery	+=	"F3_ENTRADA<='"+DToS (dDtFim)+"' AND "
			cQuery	+=	"D_E_L_E_T_=' ' AND "
			cQuery	+=	"F3_VALICM>0 "
			//
			cQuery	+=	"ORDER BY F3_NFISCAL, F3_SERIE "
			//
			cQuery 	:= 	ChangeQuery (cQuery)
	    	//
			DbUseArea (.T., "TOPCONN", TcGenQry (,,cQuery), cAliasSF3, .T., .T.)
			//
			For nSF3 := 1 To (Len (aStruSF3))
				If (aStruSF3[nSF3][2]<>"C") .And. (FieldPos (aStruSF3[nSF3][1])>0)
					TcSetField (cAliasSF3, aStruSF3[nSF3][1], aStruSF3[nSF3][2], aStruSF3[nSF3][3], aStruSF3[nSF3][4])
				EndIf
			Next (nSF3)
		Else
	#ENDIF	 
			cIndSF3		:=	CriaTrab (NIL,.F.)
			cChave		:=	IndexKey ()
			cQuery		:=	"SF3->F3_FILIAL=='"+xFilial ("SF3")+"' "
			cQuery		+=	".And. (DToS (SF3->F3_ENTRADA)>='"+DToS (dDtIni)+"') .And. (DToS (SF3->F3_ENTRADA)<='"+DToS (dDtFim)+"') "
			cQuery		+=	".And. SF3->F3_VALICM>0 "
			//
			IndRegua (cAliasSF3, cIndSF3, cChave,, cQuery, STR0005) //"Selec.Notas fiscais..."
	#IFDEF TOP
		Endif
	#ENDIF
	//
	DbSelectArea (cAliasSf3)
		SetRegua ((cAliasSf3)->(LastRec ()))
		(cAliasSf3)->(DbGoTop ())
	//
	Do While !(cAliasSf3)->(Eof ())
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Considera filtro do usuario                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(cFilterUser) .And. !(&cFilterUser)
			dbSkip()
			Loop
		EndIf
		
		cCfop		:=	AllTrim ((cAliasSf3)->F3_CFO)
		cTipo		:=	AllTrim ((cAliasSf3)->F3_TIPO)
		//
		If ((Left (cCfop, 1)>="5") .And. !cTipo$'D') .Or.;
		 	((Left (cCfop, 1)<="3") .And. cTipo$'D')
			SA1->(DbSeek (xFilial ("SA1")+(cAliasSf3)->F3_CLIEFOR+(cAliasSf3)->F3_LOJA))
			cUf			:=	SA1->A1_EST
			cEmitente	:=	Left (AllTrim (SA1->A1_NOME), 37)
			lConsFinal	:= Iif( SA1->A1_TIPO=="F", .T.,.F.)
		Else
			SA2->(DbSeek (xFilial ("SA2")+(cAliasSf3)->F3_CLIEFOR+(cAliasSf3)->F3_LOJA))
			cUf			:=	SA2->A2_EST
			cEmitente	:=	Left (AllTrim (SA2->A2_NOME), 37)
			lConsFinal	:=  .T.
		EndIf
		
		If !lConsFinal 
			dbSkip()
			Loop
		EndIf
		cNota		:=	(cAliasSf3)->F3_NFISCAL+"/"+(cAliasSf3)->F3_SERIE
		nAliqIcm	:=	(cAliasSf3)->F3_ALIQICM
		nVlrNf		:=	(cAliasSf3)->F3_VALCONT
		//
		If (cTipo$'D')
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Quando for devolucao, conforme Suzete da SEFAZ-SE, deve-se anular a operacao, ou seja,³
			//³deve-se emitir a devolucao nos moldes da NF Original. Manter os mesmos percentuais.   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If (Left (cCfop, 1)>="5")
				If (cUf$(cSul+cSudeste))
					nPercIcms	:=	5
				ElseIf (cUf$(cNorte+cNordeste+cCentOeste) .Or. Left (AllTrim ((cAliasSf3)->F3_CFO), 1)=="5")
					nPercIcms	:=	2
				EndIf
				cEntSai		:=	"SD"
			Else
				nPercIcms	:=	7
				cEntSai	:=	"ED"
			EndIf
		Else
			If (Left (cCfop, 1)>="5")
				nPercIcms	:=	7
				cEntSai		:=	"SN"
			Else
				If (cUf$(cSul+cSudeste))
					nPercIcms	:=	5
				ElseIf (cUf$(cNorte+cNordeste+cCentOeste) .Or. Left (AllTrim ((cAliasSf3)->F3_CFO), 1)=="1")
					nPercIcms	:=	2
				EndIf
				cEntSai	:=	"EN"
			EndIf
		EndIf
		//
		nVlrIcms	:=	(nPercIcms*nVlrNf)/100*(Iif (SubStr (cEntSai, 2, 1)$"D", -1, 1))
		nTotPag		+=	nVlrIcms
		nTotGeral	+=	nVlrIcms
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Imprimindo Variaveis ja calculadas.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (nLi>nNumLin .Or. nLi==0)
			If (nLi<>0)
				aBase	:=	{nTotPag}
				FmtLin (, aLayCabec[1],,,@nLi)
				FmtLin (aBase, aLayResumo[2],,,@nLi)
				nTotPag	:=	0
			EndIf
			R938Cabec (aLayCabec, @nLi, dDtIni, @nFolha)
		EndIf
		//
		If (SubStr (cEntSai, 1, 1)$"S")
	    	aBase	:=	{"", cNota, cEmitente, Iif (SubStr (cEntSai, 2, 1)$"D", STR0006, ""), cUf, Transform (nAliqIcm, "@E 99.99"), Transform (nVlrNf, "@E 999,999,999,999.99"), Transform (nPercIcms, "@E 99.99"), Transform (nVlrIcms, "@E 999,999,999,999.99")}	//"DEVOLUCAO"
	 	Else
		 	aBase	:=	{cNota, "", cEmitente, Iif (SubStr (cEntSai, 2, 1)$"D", STR0006, ""), cUf, Transform (nAliqIcm, "@E 99.99"), Transform (nVlrNf, "@E 999,999,999,999.99"), Transform (nPercIcms, "@E 99.99"), Transform (nVlrIcms, "@E 999,999,999,999.99")}	//"DEVOLUCAO"
	 	EndIf
		FmtLin (aBase, aLayResumo[1],,,@nLi)
		//
		IncRegua ()
		(cAliasSf3)->(DbSkip ())
	EndDo
	//
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Imprimindo totalizador geral.      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (nLi<=nNumLin .And. nLi>0)
		aBase	:=	{nTotPag}
		nTotPag	:=	0
		FmtLin (, aLayCabec[1],,,@nLi)
		FmtLin (aBase, aLayResumo[2],,,@nLi)
	ElseIf (nLi>nNumLin)
		R938Cabec (aLayCabec, @nLi, dDtIni, @nFolha)
		//
		aBase	:=	{nTotPag}
		nTotPag	:=	0
		FmtLin (, aLayCabec[1],,,@nLi)
		FmtLin (aBase, aLayResumo[2],,,@nLi)
	EndIf
	//
	aBase	:=	{}
	For nI := nLi	To nNumLin
		FmtLin (aBase, aLayResumo[3],,,@nLi)
	Next (nI)
	aBase	:=	{nTotGeral}
	FmtLin (, aLayCabec[1],,,@nLi)
	FmtLin (aBase, aLayResumo[4],,,@nLi)
	//
	#IFDEF TOP
		(cAliasSf3)->(DbCloseArea ())
	#ELSE
	  	DbSelectArea (cAliasSf3)
		RetIndex (cAliasSf3)
		Set Filter to
		Ferase (cIndSF3+OrdBagExt())
	#ENDIF
Return (lRet)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³R938Lay   ³ Autor ³Gustavo G. Rueda       ³ Data ³17/06/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Monta o layout em um array para impressao                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³NIL                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MATR938                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R938Lay ()
	Local	aRet	:=	{{},{}}
	//
	               //         1         2         3         4         5         6         7         8         9         10        11        12        13        14        15        16        17        18        19        20        21        22
                   //1234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
	aAdd (aRet[1], __PrtThinLine())
	aAdd (aRet[1], STR0007)	//"                                                         REGIME ESPECIAL ICMS                                       Folha.....:      #####"
	aAdd (aRet[1], STR0008)	//"                                                   MAPA DE APURAÇÃO DOS ATACADISTAS                                 Dt Emissão: ##########"
	aAdd (aRet[1], STR0009)	//"Contribuinte: ############################################################################################################################"
	aAdd (aRet[1], STR0010)	//"CACESE......: #################### CNPJ: #################################################################################################"
	aAdd (aRet[1], STR0011)	//"Endereço....: ############################################################################################################################"
	aAdd (aRet[1], STR0012)	//"Mês/Ano.....: ############################################################################################################################"
	aAdd (aRet[1], STR0013)	//"Num. NF        Num. NF                                                               Aliq.         Valor da NF               Valor do ICMS"
	aAdd (aRet[1], STR0014)	//"Entrada        Saída          Emitente                               DEVOLUCAO  UF  Origem             (R$)     ICMS %            (R$)    "
	//
	aAdd (aRet[2], STR0015)	//"#############  #############  #####################################  #########  ##  #####%  ##################  #####%  ##################"
	aAdd (aRet[2], STR0016)	//"                                                                                                     TOTAL ICMS DEVIDO  ##################"
	aAdd (aRet[2], STR0017)	//"                                                                                                                                          "
	aAdd (aRet[2], STR0018)	//"                                                                                                           TOTAL GERAL  ##################"
Return (aRet)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³R938Cabec ³ Autor ³Gustavo G. Rueda       ³ Data ³17/06/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Monta o cabecalho da pagina                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³aCabec -> Array com o layout.                               ³±±
±±³          ³nLin -> Numero da linha para impressao.                     ³±±
±±³          ³dDtRef -> Data que compoe o cabecalho da pagina.            ³±±
±±³          ³nFolha -> Numero da folha a ser gerada o cabecalho.         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³NIL                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MATR938                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R938Cabec (aCabec, nLi, dDtRef, nFolha)
	Local	aBase	:=	{}
	Local	cCacese	:=	MV_PAR03
	//
	nLi	:=	0
	@ nLi,000 PSAY aValImp(Limite)
    //
    FmtLin(, aCabec[1],,,@nLi)
    //
    aBase	:=	{StrZero (nFolha++, 5)}
	FmtLin(aBase, aCabec[2],,,@nLi)
	//
	aBase	:=	{StrZero (Day (dDataBase), 2)+"/"+StrZero (Month (dDataBase), 2)+"/"+StrZero (Year (dDataBase), 4)}
	FmtLin(aBase, aCabec[3],,,@nLi)
	//	
	FmtLin(, aCabec[1],,,@nLi)
	//
	aBase	:=	{Upper (SM0->M0_NOMECOM)}
	FmtLin(aBase, aCabec[4],,,@nLi)
	//
	aBase	:=	{Left (cCacese, 10),Transform (SM0->M0_CGC, "@R! NN.NNN.NNN/NNNN-99")}
	FmtLin(aBase, aCabec[5],,,@nLi)
	//
	aBase	:=	{SM0->M0_ENDENT}
	FmtLin(aBase, aCabec[6],,,@nLi)
	//
	aBase	:=	{StrZero (Month (dDtRef), 2)+"/"+StrZero (Year (dDtRef), 4)}
	FmtLin(aBase, aCabec[7],,,@nLi)
	//
	FmtLin(, aCabec[1],,,@nLi)
	//
	FmtLin(, aCabec[8],,,@nLi)
	FmtLin(, aCabec[9],,,@nLi)
	//
	FmtLin(, aCabec[1],,,@nLi)
Return (.T.)
