#INCLUDE "MATA080.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMBROWSE.CH"
#INCLUDE "FWMVCDEF.CH"
#Include "FWEditPanel.CH"

STATIC cDbType			:= AllTrim(Upper(TcGetDb()))
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA080  ³ Autor ³ Claudinei M. Benzi    ³ Data ³ 05/09/91 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de atualizacao do cadastro de Tipos de entrada e  ³±±
±±³          ³ saida                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico.                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MATA080(aRotauto,nOpcAuto)

Private cCadastro:= STR0006		//"Atualiza‡„o de TES"
Private aAutoCab := Iif(ValType(aRotAuto) == "A", aClone(aRotAuto), {})
Private aCpoAltSF4  := {} // Vetor usado na gravacao do historico de alteracoes
Private aMemos	:= {}	//

STATIC lHistFiscal := HistFiscal()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada para Tratamento de Campos Memo - Virtuais   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "MT080MEM" ) 
	aMemUser := ExecBlock( "MT080MEM", .F., .F. ) 
	If ValType( aMemUser ) == "A"
		AEval( aMemUser, { |x| AAdd( aMemos, x ) } )
	EndIf 	
EndIf

If cPaisLoc == "PTG"
	AjustaPTG()
Endif

If cPaisLoc <> "BRA"
	CriaSFB(.T.)
	CriaSF4(.T.)
	CriaSFC(.T.)
EndIf

Default nOpcAuto := 3

Private l080Auto := (ValType(aRotAuto) == "A")
Private aRotina  := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( l080Auto )
    If ValType(nOpcAuto)=="N"
		MBrowseAuto(nOpcAuto,aAutoCab,"SF4")
    Endif
Else
	mBrowse( 6, 1,22,75,"SF4")
EndIf

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A080Deleta³ Autor ³ Jorge Queiroz         ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para exclusao de TES                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A080Deleta(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA080                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A080Deleta(cAlias,nReg,nOpc)

Local aAC        	:= { STR0007,STR0008 }		//"Abandona"###"Confirma"
Local aButtons   	:= {}
Local aButtonUsr 	:= {}
Local aPosEnch   	:= {}
Local cArqs      	:= ""
Local cMsgError  	:= ""
Local nOpcA      	:= 0
Local nX         	:= 0
Local oDlg 
Local cQuery		:= "" 
Local cQryADB		:= GetNextAlias() 
Local lRet			:= .T.	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aTELA[0][0],aGETS[0]

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada para incluir Botoes na ToolBar.               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( ExistBlock("MA080BUT") )
	aButtonUsr := ExecBlock("MA080BUT",.F.,.F.,{nOpc})
	If ( ValType(aButtonUsr) == "A" )
		For nX := 1 To Len(aButtonUsr)
			Aadd(aButtons,aClone(aButtonUsr[nX]))
		Next nX
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia para processamento dos Gets                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SoftLock(cAlias)

If Type("l080Auto") # "L" .or. !l080Auto
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM 9,0 TO TranslateBottom(.F.,28),80 OF oMainWnd
	aPosEnch := {,,(oDlg:nClientHeight - 4)/2,}
	nOpcA:=EnChoice( cAlias, nReg, nOpc,,"AC",OemToAnsi(STR0009),,aPosEnch )		//"Quanto … exclus„o?"
	nOpc := 1
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| If(Ma080Valid(nOpc),(nOpca := 2,oDlg:End()),)},{|| nOpca := 1,oDlg:End()},,aButtons)
Else
	nOpcA := 2
EndIf


IF nOpcA == 2 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se esta associado a um contrato parceria            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	#IFDEF TOP   
		cQuery := "SELECT ADB_TES, ADB_TESCOB " 
		cQuery += "	FROM " + RetSQLName("ADB") + " ADB " 
		cQuery += "	WHERE "
		cQuery += "	ADB.ADB_TES = '" + SF4->F4_CODIGO + "' OR "
		cQuery += "	ADB.ADB_TESCOB = '" + SF4->F4_CODIGO + "' AND "
		cQuery += "	ADB.D_E_L_E_T_ = ' ' "
		
		cQuery := ChangeQuery( cQuery )
		DbUseArea( .T., "TopConn", TCGenQry(,,cQuery), cQryADB, .F., .F. )
	
		If (cQryADB)->(!EOF())
			Aviso( STR0018, STR0029,; 		// Atenção "Esta TES está associada a um contrado de parceria e não será possível sua exclusão "	
				{STR0020 }, 2 ) 			//Ok 
			lRet := .F.
		EndIf 
		(cQryADB)->(DbCloseArea())		
	#ENDIF
	
	If lRet
		dbSelectArea( cAlias )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se existe movimento este TES                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cArqs := a080Movim(F4_CODIGO,.F.)
	
		If empty(cArqs)
	
			Begin Transaction
				dbSelectArea( cAlias )
				RecLock(cAlias,.F.,.t.)
				If !FKDelete(@cMsgError)
				   RollBackDelTran(cMsgError)
				Else
					lOk := .T.
				EndIf
				MsUnlock()
			End Transaction
			If lOk
				MT080AltOk()
			EndIf
			If ExistBlock("MT080EXC")
				ExecBlock("MT080EXC",.F.,.F.)
			EndIf
		Else
			HELP(" ",1,"NaoExclui",,cArqs,4,1)
		Endif
	EndIf	
Else
	MsUnLock()
EndIf

dbSelectArea( cAlias )

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A080IniCpo³ Autor ³ Claudinei M. Benzi    ³ Data ³ 02/10/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Inicializa o tipo do TES                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA080                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/        
  
Function A080IniCpo

Local nEndereco
Local cReadVar  := &(ReadVar())
Local cCampo    := AllTrim( ReadVar() )
Local cDigito   := ""
Local lRet      := .T. 
Local nLoop     := 0
Local nAscDig   := 0 
Local lUsaCfps	:= GetNewPar("MV_USACFPS",.F.)

If M->F4_CODIGO <= "500"
	M->F4_TIPO := "E"
Else
	M->F4_TIPO := "S"
EndIf

If cCampo == "M->F4_CODIGO"
                              
	For nLoop := 1 To Len( cReadVar ) 
	      
		cDigito := SubStr( cReadVar, nLoop, 1 ) 
		nAscDig := Asc( cDigito ) 
	
		If nLoop == 1 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ No primeiro digito, permite apenas numeros                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			lRet := ( nAscDig >= 48 .And. nAscDig <= 57 )
		Else 	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ No demais digitos, permite apenas numeros e letras maiusculas  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			lRet := ( ( nAscDig >= 48 .And. nAscDig <= 57 ) .Or. ( nAscDig >= 65 .And. nAscDig <= 90 ) )
		EndIf 												
	
		If !lRet 
			Aviso( STR0018, STR0019, { STR0020 }, 2 ) // "Atencao", "Tipo de entrada / saida invalido", "Ok" 
			Exit	
		EndIf 	

	Next nLoop 

ElseIf cCampo == "M->F4_CF" .Or. cCampo == "M->F4_CFNOVO"
	If !Empty(cReadVar) .And. !(SubStr(cReadVar,1,3)$"999#000")
		If M->F4_CODIGO <= "500"                                                             //Validação de CFOP também ao Mercado Internacional.
			if !IIF(cPaisLoc == "BRA", Substr(cReadVar,1,1) $ "1/2/3"+IIf(lUsaCfps,"/9",""), Substr(cReadVar,1,1) $ "1/2/3/4"+IIf(lUsaCfps,"/9",""))
				Help(" ",1,"F4_CFERR")
				lRet := .F.		
			EndIf
		Else                                                                                  //Validação de CFOP também ao Mercado Internacional.
			if !IIF(cPaisLoc == "BRA", Substr(cReadVar,1,1) $ "5/6/7"+IIf(lUsaCfps,"/9",""), Substr(cReadVar,1,1) $ "5/6/7/8/9"+IIf(lUsaCfps,"/9",""))
				Help(" ",1,"F4_CFERR")
				lRet := .F.		
			EndIf
		EndIf
	Endif
EndIf

If lRet 
	if Type("aGets") != "U"
		nEndereco := Ascan(aGets,{ |x| Subs(x,9,7) == "F4_TIPO" } )
		If nEndereco > 0
			aTela[Val(Subs(aGets[nEndereco],1,2))][Val(Subs(aGets[nEndereco],3,1))*2] := M->F4_TIPO
		EndIf
		nEndereco := Ascan(aGets,{ |x| Subs(x,9,10) == "F4_CODIPAM" } )
		If nEndereco > 0
			aTela[Val(Subs(aGets[nEndereco],1,2))][Val(Subs(aGets[nEndereco],3,1))*2] := M->F4_CODIPAM
		EndIf
	endif
EndIf 	
	
Return( lRet ) 

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A080Tipo  ³ Autor ³ Claudinei M. Benzi    ³ Data ³ 13/01/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica o tipo de tes digitado. Permite apenas E-entrada  ³±±
±±³          ³ ou S-saida                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := A080Tipo()                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpL1 = Devolvido pela funcao                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA080                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A080Tipo()

Local cVar := &(ReadVar())

If (M->F4_CODIGO <= "500" .And. cVar != "E") .Or. (M->F4_CODIGO > "500" .And. cVar != "S")
	Help(" ",1,"F4_TIPO")
	Return .F.
EndIf

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A080Movim ³ Autor ³ Gilson do Nascimento  ³ Data ³05/05/93  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se existe movimento para este TES a excluir       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC:=A080Movim(ExpC1)                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC  = String contendo os arquivos que usam este TES      ³±±
±±³          ³ ExpC1 = Codigo do TES em questao.                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA080 / A080Deleta                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
STATIC FUNCTION A080Movim(cTES,lRastroEnd)
Local aArea     := GetArea()
Local cFilSav   := cFilAnt
Local cCampo    := ""
Local cCampoPrd := ""
Local cArquivos := "SD1SD2SC6SB6SD6"
Local cAliasArq := ""
Local cAliasTop := ""
Local cQuery    := ""
Local cString   := ""
Local nTam      := Len(cArquivos)
Local nX        := 0
Local nY        := 0
Local lExcl     := FWModeAccess("SF4",1) == "E" .AND. FWModeAccess("SF4",2) == "E" .AND. FWModeAccess("SF4",3)=="E"
Local lTotComp  := FWModeAccess("SF4",1) == "C" .AND. FWModeAccess("SF4",2) == "C" .AND. FWModeAccess("SF4",3)=="C"
Local cUnid		:= Alltrim(FWUnitBusiness())
Local aFiliais  := {}
Local lContinua := .T.

DEFAULT lRastroEnd:= .F.

Iif(lExcl,aAdd(aFiliais,{.T.,cFilAnt}),aFiliais := MatFilCalc(.F.,,,,,,lTotComp))

For nY := 1 To Len(aFiliais)
	cFilAnt := aFiliais[nY,2]
	
	//Se for totalmente exclusivo, ou se for totalmente compartilhado processará
	//Agora se for compartilhamento parcial, preciso verificar a unidade de negócio, uma vez que a MatFilCalc sempre retornará todas as unidades
	If lExcl .OR. lTotComp .OR. (FWModeAccess("SF4",2) == 'C' .OR. (FWModeAccess("SF4",2) == 'E' .AND. cUnid == Alltrim(FWUnitBusiness())) )
		For nX := 1 To nTam Step 3
			cAliasArq := Subs(cArquivos,nX,3)
			cCampo    := Subs(cAliasArq,2,2)+"_TES"
			cCampoPrd := Subs(cAliasArq,2,2)+"_COD"	
			cFiliais  := Subs(cAliasArq,2,2)+"_FILIAL"
			#IFDEF TOP
				//  Valida utilizacao em TES de produto com rastreabilidade e enderecamento
				If lRastroEnd
					If cAliasArq $ IIf(cTes<"500","SD1","SD2")
						dbSelectArea(cAliasArq)
						cAliasTop := GetNextAlias()
						cQuery := "SELECT DISTINCT "+cCampoPrd+" FROM " + RetSqlName(cAliasArq) + " WHERE "
						cQuery += cFiliais + "='" + xFilial(cAliasArq) + "' AND "
						cQuery += cCampo + "='" + cTes + "' AND D_E_L_E_T_=' '"
						cQuery := ChangeQuery(cQuery)
						dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasTop,.F.,.T. )
						While !Eof()
							If Rastro(&(cCampoPrd)) .Or. Localiza(&(cCampoPrd))
								cString += cAliasArq+" , "
							EndIf
							dbSkip()
						EndDo
						dbSelectArea(cAliasTop)
						dbCloseArea()
						dbSelectArea(cAliasArq)
					EndIf
				Else
					Iif(nX==1,cQuery := "SELECT CASE ",)
					If !cAliasArq $ Iif(cTes<"500","SD2/SC6","SD1")
						cQuery += " WHEN EXISTS (SELECT 1 FROM " + RetSqlName(cAliasArq) + " WHERE "
						#IFDEF SHELL
							If ( cAliasArq == "SD1" )
								cQuery += "D1_CANCEL<>'S' AND "
							ElseIf ( cAliasArq == "SD2" )
								cQuery += "D2_CANCEL<>'S' AND "
							EndIf
						#ELSE
							cQuery += cFiliais + "='" + xFilial(cAliasArq) + "' AND "
							cQuery += cCampo + "=F4_CODIGO AND "
						#ENDIF
						cQuery += "D_E_L_E_T_='') THEN " + Str(nX) + " "
						If nX == 13
							cQuery += "ELSE 0 END AS NRESULT "
							cQuery += "FROM " + RetSqlName("SF4") + " WHERE F4_FILIAL='" + xFilial("SF4") + "' AND F4_CODIGO='" + cTes + "' AND D_E_L_E_T_=''"
							dbSelectArea(cAliasArq)
							cQuery := ChangeQuery( cQuery )
							cAliasTop := GetNextAlias()
							dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasTop,.F.,.T. )
							cString += Iif((cAliasTop)->NRESULT==01,"SD1,","")
							cString += Iif((cAliasTop)->NRESULT==04,"SD2,","")
							cString += Iif((cAliasTop)->NRESULT==07,"SB6,","")
							cString += Iif((cAliasTop)->NRESULT==10,"SC6,","")
							cString += Iif((cAliasTop)->NRESULT==13,"SD6,","")
							lContinua := (cAliasTop)->NRESULT==0
							dbSelectArea(cAliasTop)
							dbCloseArea()
							dbSelectArea(cAliasArq)
							If !lContinua
								Exit
							EndIf
						EndIf
					EndIf
				EndIf
			#ELSE
				dbSelectArea(cAliasArq)
				dbGotop()
				While !eof()
					If lRastroEnd
						//  Valida utilizacao em TES de produto com rastreabilidade e enderecamento
						If cAliasArq $ "SD1/SD2"
							If &(cCampo) == cTes .And. (FWModeAccess("SF4",3)=="C" .Or. xFilial("SF4") == &(cFiliais)) .And.;
								(Rastro(&(cCampoPrd)) .Or. Localiza(&(cCampoPrd)))
								cString += cAliasArq+" ,"
								Exit
							EndIf
						EndIf
					Else
						#IFDEF SHELL
							If	(cAliasArq == "SD1" .and. D1_CANCEL != "S") .Or.;
								(cAliasArq == "SD2" .and. D2_CANCEL != "S")
								cString += cAliasArq+" ,"
								Exit
							EndIf
						#ELSE
							If &(cCampo) == cTes .And. (FWModeAccess("SF4",3)=="C" .Or. xFilial("SF4") == &(cFiliais))
								cString += cAliasArq+" ,"
								Exit
							Endif
						#ENDIF
					EndIf
					dbSkip()
				EndDo
			#ENDIF
		Next nX

	EndIF

	If !lContinua
		Exit
	EndIf
Next nY

cString:=iif(Subs(cString,Len(cString),1)==",",Subs(cString,1,Len(cString)-1),cString)

cFilAnt := cFilSav
RestArea(aArea)
Return(cString)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A081Visual³ Autor ³ Jose Lucas            ³ Data ³ 01.07.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa para visualizacao do Tex e Amarracao TesxImpostos ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A081Visual(ExpC1,ExpN1)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A081Visual(cAlias,nReg,nOpc)

Local aArea       := GetArea()
Local aSize       := MsAdvSize(.t.)
Local aInfo       := {}
Local aObjects    := {}
Local aObj        := {}
Local aNoFields   := {"FC_TES"}
Local bCampo      := {|nCPO| Field(nCPO)}
Local cSeek       := ""
Local cWhile      := ""
Local nX          := 0
Local nOpcA       := 0
Local oDlg
Local aHeadCC7	:= {}
Local aColsCC7	:= {}
Local aHeadF09	:= {}
Local aColsF09	:= {}
Local lIpm     	:= AliasIndic("F09") .AND. !Empty(Alltrim(GetNewPar("MV_UFIPM",'')))
Local lAjICMS     := Iif(CPaisLoc = "BRA", .T., .F.)
Local lCalcImpV   := GetMV("MV_GERIMPV")=="S"
Local aFolders	  := {}
Local cOk         := ""
Local aButtons	  := {}
Local aButtonUsr  := {}
Local bSkip		  := {|| CC7->CC7_TPREG == "NA" }
Local aCmpsCC7	  := {"CC7_CODLAN"}
Local aCmpsF09	  := {}
Local oFolTES	  := NIL
Local aRetCopy    := If(Type("aRotina")=="A",ACLONE(aRotina),{})

DEFAULT nOpc := 2

PRIVATE oGetd
PRIVATE aTELA[0][0]
PRIVATE aGETS[0]
PRIVATE aRotina

SaveInter()


If SF4->(LastRec()) <> 0 .And. SF4->F4_FILIAL == xFilial("SF4")

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de Entrada para incluir Botoes na ToolBar.               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( ExistBlock("MA080BUT") )
		aButtonUsr := ExecBlock("MA080BUT",.F.,.F.,{nOpc})
		If ( ValType(aButtonUsr) == "A" )
			For nX := 1 To Len(aButtonUsr)
				aadd(aButtons,aClone(aButtonUsr[nX]))
			Next nX
		EndIf
	EndIf

	aCols   := {}
	aHeader := {}
	n       := 1
	
	If lCalcImpV
		aAdd(aFolders,STR0023)	//"Impostos variáveis"
	
		dbSelectArea("SFC")
		dbSetOrder(1)
		If dbSeek(xFilial("SFC")+SF4->F4_CODIGO)
			cSeek  := xFilial("SFC")+SF4->F4_CODIGO
			cWhile := "SFC->FC_FILIAL+SFC->FC_TES"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Sintaxe da FillGetDados(nOpcX,Alias,nOrdem,cSeek,bSeekWhile,uSeekFor,aNoFields,aYesFields,lOnlyYes,cQuery,bMontCols,lEmpty,aHeaderAux,aColsAux,bAfterCols,bBeforeCols,bAfterHeader,cAliasQry |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			FillGetDados(nOpc,"SFC",1,cSeek,{|| &cWhile },,aNoFields,,,,,,,,,,,)
		Else
			FillGetDados(nOpc,"SFC",1,,,,aNoFields,,,,,.T.,,,,,)
			aCols[1][aScan(aHeader,{|x| Trim(x[2])=="FC_SEQ"})] := StrZero(1,Len(SFC->FC_SEQ))
		EndIf
	EndIf
	
	If lAjICMS
		Private	oNewGetDad
		
		aAdd(aFolders,STR0024)	//"Lançamentos da Apuração de ICMS"
		aMHead("CC7","CC7_TES/",@aHeadCC7)
	 	CC7->(dbSeek(xFilial("CC7")+SF4->F4_CODIGO))  
		aMAcols(nOpc,"CC7",@aColsCC7,aHeadCC7,{|| CC7->CC7_FILIAL == xFilial("CC7") .And. SF4->F4_FILIAL == xFilial("SF4") .And. CC7->CC7_TES==SF4->F4_CODIGO},bSkip)
		
			aAdd(aCmpsCC7,"CC7_IFCOMP")
			aAdd(aCmpsCC7,"CC7_CLANAP")
			aAdd(aCmpsCC7,"CC7_CODREF")
			aAdd(aCmpsCC7,"CC7_CLANC")
			If CC7->(FieldPos("CC7_GUIA"))>0
				aAdd(aCmpsCC7,"CC7_GUIA")
			EndIf
			If CC7->(FieldPos("CC7_CODIPI"))>0
				aAdd(aCmpsCC7,"CC7_CODIPI")
			EndIf	
	EndIf
	
	IF lIpm

		Private	oGetDadIpm	
		aAdd(aFolders,'Índice de Participação dos Município')	//"Lançamentos da Apuração de ICMS"
		aMHead("F09","F09_TES/",@aHeadF09)
		F09->(dbSeek(xFilial("F09")+SF4->F4_CODIGO))  
		aMAcols(nOpc,"F09",@aColsF09,aHeadF09,{||F09->F09_FILIAL == xFilial("F09") .And. SF4->F4_FILIAL == xFilial("SF4") .And. F09->F09_TES==SF4->F4_CODIGO})
		
			aAdd(aCmpsF09,"F09_UF")
			aAdd(aCmpsF09,"F09_CODIPM")
			aAdd(aCmpsF09,"F09_DSCIPM")
								
	EndIF
	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Salva a integridade dos campos de Bancos de Dados    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX := 1 to FCount()
		If Type("aMemos") == "A"
			If Empty( nScan := AScan( aMemos, { |x| x[1] == FieldName( nX ) } ) )
				M->&(EVAL(bCampo,nX)) := FieldGet(nX)
			Else
				cCodMem := FieldGet(nX)
				cMemo   := MSMM( cCodMem )
				M->&(aMemos[nScan,2]) := cMemo
			EndIf
		Else
			M->&(EVAL(bCampo,nX)) := FieldGet(nX)
		EndIf
	Next nX
	
	aRotina  := {}
	aRotina  := MenuDef()	
	aObjects := {}
	AAdd( aObjects, { 100, 100, .T., .T. } )
	AAdd( aObjects, { 100, 060, .T., .T. } )
	aInfo := { aSize[1],aSize[2],aSize[3],aSize[4],2 ,1 }
	aObj  := MsObjSize( aInfo, aObjects, .t., .f. )

	DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
	EnChoice( "SF4", nReg, nOpc, , , , , {aObj[1,1],aObj[1,2],aObj[1,3],aObj[1,4]}, , 3,,,,oDlg)
	
	oFolTES := TFolder():New(aObj[2,1],aObj[2,2],aFolders,{},oDlg,,,,.t.,.f.,aObj[2,4],aObj[2,3],)
	
	cOk	:=	"{||nOpcA:=1"
	If lCalcImpV
		A081Cab(nOpc)
		oGetd   := MsGetDados():New(0,0,aObj[2,3]-aObj[2,1]-13,aObj[2,4],nOpc,,,"",,,,,"A081FldOk",,,,,oFolTES:aDialogs[1])
		cOk	+=	",if(oGetd:TudoOk(),oDlg:End(),nOpcA := 0)"
	EndIf

	If lAjICMS
		oNewGetDad	:=	MsNewGetDados():New(0,0,aObj[2,3]-aObj[2,1]-13,aObj[2,4],0,"AllwaysTrue","AllwaysTrue",,aCmpsCC7,/*freeze*/,990,/*fieldok*/,/*superdel*/,/*delok*/,oFolTES:aDialogs[Iif(lCalcImpV,2,1)],@aHeadCC7,@aColsCC7)
		cOk	+=	Iif(lCalcImpV,"",",oDlg:End()")                                                                                                    
	EndIf
	
	If lIpm
		oGetDadIpm	:=	MsNewGetDados():New(0,0,aObj[2,3]-aObj[2,1]-13,aObj[2,4],0,"AllwaysTrue","AllwaysTrue",,aCmpsF09,/*freeze*/,990,/*fieldok*/,/*superdel*/,/*delok*/,oFolTES:aDialogs[Iif(lCalcImpV,3,2)],@aHeadF09,@aColsF09)
		cOk	+=	Iif(lCalcImpV,"",",oDlg:End()")
	EndIf	
		
	cOk	+=	"}"
	
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,&(cOk),{||oDlg:End()},,aButtons)
	
	RestInter()
	RestArea(aArea)
Else
	IF SF4->F4_FILIAL <> xFilial("SF4")
		Help(" ",1,"A000FI")
	Endif
EndIf

aRotina:=AClone(aRetCopy)
Return( nOpcA )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A081Altera³ Autor ³ Jose Lucas            ³ Data ³ 01.07.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa exclusivo ao MATA081 para alteracao               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A081Altera(ExpC1,ExpN1)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Mata081                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A081Altera(cAlias,nReg,nOpc)

Local aSize     := MsAdvSize() 
Local aInfo     := {} 
Local aObjects  := {} 
Local aObj      := {}
Local aNoFields := {"FC_TES"}
Local bCampo    := {|nCPO| Field(nCPO)}
Local bCondicao := {|| .T.}
Local cSeek     := ""
Local cWhile    := ""
Local cCampo    := ""
Local lGravaOk  := .T.
Local nX,nZ,nY  := 0
Local nOpca     := 0
Local nRecSF4   := SF4->(Recno())
Local oDlg
Local aHeadCC7  := {}
Local aColsCC7  := {}
Local aHeadF09  := {}
Local aColsF09  := {}
Local lIpm      := AliasIndic("F09") .AND. !Empty(Alltrim(GetNewPar("MV_UFIPM",'')))
Local lAjICMS   := Iif(cPaisLoc = "BRA", .T., .F.)
Local lCalcImpV := GetMV("MV_GERIMPV")=="S"
Local aFolders  :=	{}
Local cOk       := ""
Local aButtons  := {}
Local aButtonUsr:= {}
Local nPosX     := 0
Local bSkip     := {|| CC7->CC7_TPREG == "NA" }
Local aCmpsCC7  := {"CC7_CODLAN"}
local aCmpsF09	:= {}
Local bCampoSF4 := { |x| SF4->(Field(x)) }
Local aCmps     := {}
Local lAlt      := .F.
Local oFolTES	:= NIL
Local aCmpsSFC  := {}

PRIVATE oGetd
PRIVATE aTELA[0][0],aGETS[0]

If SoftLock("SF4") .And. SF4->(LastRec()) <> 0 .And. SF4->F4_FILIAL == xFilial("SF4")

	aHeader := {}
	aCols	:= {}

	If lCalcImpV
		aAdd(aFolders,STR0023)	//"Impostos variáveis"

		dbSelectArea("SFC")
		dbSetOrder(1)
		If dbSeek(xFilial("SFC")+SF4->F4_CODIGO)
			cSeek    := xFilial("SFC")+SF4->F4_CODIGO
			cWhile   := "SFC->FC_FILIAL+SFC->FC_TES"
			bCondicao:= {|| If( SoftLock("SFC") , .T. , .F. ) }
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Sintaxe da FillGetDados(nOpcX,Alias,nOrdem,cSeek,bSeekWhile,uSeekFor,aNoFields,aYesFields,lOnlyYes,cQuery,bMontCols,lEmpty,aHeaderAux,aColsAux,bAfterCols,bBeforeCols,bAfterHeader,cAliasQry |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			FillGetDados(nOpc,"SFC",1,cSeek,{|| &cWhile },bCondicao,aNoFields,,,,,,,,,,,)
		Else
			FillGetDados(nOpc,"SFC",1,,,,aNoFields,,,,,.T.,,,,,)
			aCols[1][aScan(aHeader,{|x| Trim(x[2])=="FC_SEQ"})] := StrZero(1,Len(SFC->FC_SEQ))
		EndIf

		aEval(aHeader,{|x|IIF(Trim(x[2])<>"FC_SEQ",AAdd(aCmpsSFC,x[2]),)})
	EndIf
	
	If lAjICMS
		Private	oNewGetDad
		
		aAdd(aFolders,STR0024)	//"Lançamentos da Apuração de ICMS"	
		aMHead("CC7","CC7_TES/",@aHeadCC7)
		CC7->(dbSeek(xFilial("CC7")+SF4->F4_CODIGO))
		aMAcols(nOpc,"CC7",@aColsCC7,aHeadCC7,{|| CC7->CC7_FILIAL == xFilial("CC7") .And. SF4->F4_FILIAL == xFilial("SF4") .And. CC7->CC7_TES==SF4->F4_CODIGO},bSkip)
		
			aAdd(aCmpsCC7,"CC7_IFCOMP")
			aAdd(aCmpsCC7,"CC7_CLANAP")
			aAdd(aCmpsCC7,"CC7_CODREF")
			aAdd(aCmpsCC7,"CC7_CLANC")
			If CC7->(FieldPos("CC7_GUIA"))>0
				aAdd(aCmpsCC7,"CC7_GUIA")
			EndIf
			If CC7->(FieldPos("CC7_CODIPI"))>0
				aAdd(aCmpsCC7,"CC7_CODIPI")
			EndIf
			
	EndIf
	
	IF lIpm

		Private	oGetDadIpm	
		aAdd(aFolders,'Índice de Participação dos Município')	//"Lançamentos da Apuração de ICMS"
		aMHead("F09","F09_TES/",@aHeadF09)
		F09->(dbSeek(xFilial("F09")+SF4->F4_CODIGO))  
		aMAcols(nOpc,"F09",@aColsF09,aHeadF09,{||F09->F09_FILIAL == xFilial("F09") .And. SF4->F4_FILIAL == xFilial("SF4") .And. F09->F09_TES==SF4->F4_CODIGO})
		
		aAdd(aCmpsF09,"F09_UF")
		aAdd(aCmpsF09,"F09_CODIPM")
		aAdd(aCmpsF09,"F09_DSCIPM")	
							
	EndIF
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de Entrada para incluir Botoes na ToolBar.               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( ExistBlock("MA080BUT") )
		aButtonUsr := ExecBlock("MA080BUT",.F.,.F.,{nOpc})
		If ( ValType(aButtonUsr) == "A" )
			For nX := 1 To Len(aButtonUsr)
				aadd(aButtons,aClone(aButtonUsr[nX]))
			Next nX
		EndIf
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Salva a integridade dos campos de Bancos de Dados    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SF4")
	For nX := 1 to FCount()
		nPosX := aScan(aAutoCab,{|aX|AllTrim(Eval(bCampo,nX))$aX[1]})
		If nPosX<>0
			M->&(Eval(bCampo,nX)) := aAutoCab[nPosX,2]
		Else
			M->&(Eval(bCampo,nX)) := FieldGet(nX)
		EndIf
	Next nX

	aCmps :=  RetCmps("SF4",bCampoSF4)	
	AAdd( aObjects, { 100, 100, .T., .T. } )
	AAdd( aObjects, { 100,  60, .T., .T. } )
	aInfo := { aSize[1],aSize[2],aSize[3],aSize[4],3,3 }
	aObj  := MsObjSize( aInfo, aObjects, .T. )
	
	DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
	
	EnChoice( "SF4", nReg, nOpc, , , , , aObj[1], , 3 )
	
	oFolTES := TFolder():New(aObj[2,1],aObj[2,2],aFolders,{},oDlg,,,,.t.,.f.,aObj[2,4],aObj[2,3],)

	cOk	:=	"{||nOpcA:=1"
	If lCalcImpV
		A081Cab(nOpc)
		oGetd:=MsGetDados():New(0,0,aObj[2,3]-aObj[2,1]-13,aObj[2,4],nOpc,"A081LinOk","A081TudOk","+FC_SEQ",.T.,aCmpsSFC,,,,,,,,oFolTES:aDialogs[1])
		oGetd:oBrowse:bGotFocus:={|| A081Cab(nOpc)}
	//	If lAjICMS
			cOk	+=	",if(A081TudOk(,nOpc).And.AjusteLOK(),If(!obrigatorio(aGets,aTela),nOpca := 0,oDlg:End()),nOpca := 0)"

//		EndIf
	EndIf
	
	If lAjICMS
		oNewGetDad := MsNewGetDados():New(0,0,aObj[2,3]-aObj[2,1]-13,aObj[2,4],GD_UPDATE+GD_INSERT+GD_DELETE,"AjusteLOK","AllwaysTrue","+CC7_SEQ",aCmpsCC7,/*freeze*/,990,"A080FildOK",/*superdel*/,/*delok*/,oFolTES:aDialogs[Iif(lCalcImpV,2,1)],@aHeadCC7,@aColsCC7)
		cOk	+=	Iif(lCalcImpV,"",",Iif(A081TudOk(,nOpc).And.AjusteLOK(),If(!obrigatorio(aGets,aTela),nOpca := 0,oDlg:End()),nOpca:=0)")

	EndIf
	
	If lIpm
		oGetDadIpm := MsNewGetDados():New(0,0,aObj[2,3]-aObj[2,1]-13,aObj[2,4],GD_UPDATE+GD_INSERT+GD_DELETE,"IpmLOK","AllwaysTrue",,aCmpsF09,/*freeze*/,990,/*fieldok*/,/*superdel*/,/*delok*/,oFolTES:aDialogs[Iif(lCalcImpV,3,2)],@aHeadF09,@aColsF09)
		cOk	+=	Iif(lCalcImpV,"",",Iif(A081TudOk(,nOpc).And.IpmLOK(),If(!obrigatorio(aGets,aTela),nOpca := 0,oDlg:End()),nOpca:=0)")
	EndIf		
	
	cOk	+=	"}"

	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,&(cOk),{||oDlg:End()},,aButtons)
	
	If nOpcA == 1
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se o registro em questao esta sendo usado    ³
		//³por outra estacao                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Begin Transaction
	
			If lHistFiscal .And. Altera 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se houve alteracao em algum campo.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
				For nY := 1 to SF4->(FCount())
						If !(M->&( eVal( bCampoSF4, nY)) == SF4->&( eVal( bCampoSF4, nY)))
							lAlt := .T.
						EndIf
				Next nY
			EndIf	
			
			lGravaOk := A081Grava(nRecSF4, lAlt)
			IF lGravaOk
				If lAjICMS
					GrvAjuste(.T.)
				Endif
				
				If lIpm
					GrvIpm(.T.)
				Endif
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³  Processa Gatilhos                                   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				EvalTrigger()
			EndIf
	
		End Transaction

		If lGravaOk 
			MT080AltOk()					
		EndIf	

		If lHistFiscal .And. Altera .And. lGravaOk .And. lAlt 							
			GrvHistFis("SF4", "SS0", aCmps) 
  		EndIf		
		
	EndIf
Else
	IF SF4->F4_FILIAL <> xFilial("SF4")
		Help(" ",1,"A000FI")
	Endif
EndIf

Return( nOpcA ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A081Inclui³ Autor ³ Jose Lucas            ³ Data ³ 01.07.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa exclusivo para inclusao no MATA081                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A081Inclui(ExpC1,ExpN1)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpL1 = Define se a inclusao foi efetuado pelo MATA416     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A081Inclui(cAlias,nReg,nOpc)

Local aSize      := MsAdvSize() 
Local aInfo      := {} 
Local aObjects   := {} 
Local aObj       := {}
Local aNoFields  := {"FC_TES"}
Local bCampo     := {|nCPO| Field(nCPO)}
Local nSaveSX8   := GetSx8Len()
Local nOpcA      := 0
Local nX         := 0
Local lGravaOk   := .T.
Local lInit      := .F.
Local oDlg 
Local aHeadCC7   := {}
Local aColsCC7   := {}
Local aHeadF09	:= {}
Local aColsF09	:= {}
Local lIpm     	:= AliasIndic("F09") .AND. !Empty(Alltrim(GetNewPar("MV_UFIPM",'')))
Local lAjICMS    := Iif(CPaisLoc = "BRA", .T., .F.) 
Local lCalcImpV  := GetMV("MV_GERIMPV")=="S"
Local aFolders	 :=	{}
Local cOk        := ""
Local aButtons	 := {}
Local aButtonUsr := {}
Local bSkip		 := {|| CC7->CC7_TPREG == "NA" }
Local aCmpsCC7	 := {"CC7_CODLAN"}
Local aCmpsF09	 := {}
Local oFolTES	 := NIL
Local lCopySF4	 := Iif('SF4' $ Upper(GetNewPar("MV_FISCOPY",'')), .T.,.F.)	
Local aCmpsSFC   := {}

SaveInter()

If nOpc == Nil
	Private aRotina := {{ STR0003,  ""  , 0 , 3}}
	nOpc := 1
EndIf

PRIVATE oGetd
PRIVATE aTela[0][0]
PRIVATE aGets[0]

aHeader := {}
aCols   := {}

If lCalcImpV
	aAdd(aFolders,STR0023)	//"Impostos variáveis"

	dbSelectArea("SFC") 
	dbSetOrder(1)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Sintaxe da FillGetDados(nOpcX,Alias,nOrdem,cSeek,bSeekWhile,uSeekFor,aNoFields,aYesFields,lOnlyYes,cQuery,bMontCols,lEmpty,aHeaderAux,aColsAux,bAfterCols,bBeforeCols,bAfterHeader,cAliasQry |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	FillGetDados(nOpc,"SFC",1,,,,aNoFields,,,,,.T.,,,,,)
	aCols[1][aScan(aHeader,{|x| Trim(x[2])=="FC_SEQ"})] := StrZero(1,Len(SFC->FC_SEQ))

	aEval(aHeader,{|x|IIF(Trim(x[2])<>"FC_SEQ",AAdd(aCmpsSFC,x[2]),)})
EndIf

If lAjICMS
	Private	oNewGetDad
	
	aAdd(aFolders,STR0024)	//"Lançamentos da Apuração de ICMS"	
	aMHead("CC7","CC7_TES/",@aHeadCC7)
	aMAcols(nOpc,"CC7",@aColsCC7,aHeadCC7,{||CC7->CC7_FILIAL == xFilial("CC7") .And. SF4->F4_FILIAL == xFilial("SF4") .And.CC7->CC7_TES==SF4->F4_CODIGO},bSkip)
	
		aAdd(aCmpsCC7,"CC7_IFCOMP")
		aAdd(aCmpsCC7,"CC7_CLANAP")
		aAdd(aCmpsCC7,"CC7_CODREF")
		aAdd(aCmpsCC7,"CC7_CLANC")
		If CC7->(FieldPos("CC7_GUIA"))>0
			aAdd(aCmpsCC7,"CC7_GUIA")
		EndIf
		If CC7->(FieldPos("CC7_CODIPI"))>0
			aAdd(aCmpsCC7,"CC7_CODIPI")
		EndIf
EndIf

IF lIpm

	Private	oGetDadIpm	
	aAdd(aFolders,'Índice de Participação dos Município')	//"Lançamentos da Apuração de ICMS"
	aMHead("F09","F09_TES/",@aHeadF09)
	aMAcols(nOpc,"F09",@aColsF09,aHeadF09,{||F09->F09_FILIAL == xFilial("F09") .And. SF4->F4_FILIAL == xFilial("SF4") .And. F09->F09_TES==SF4->F4_CODIGO})
	
	aAdd(aCmpsF09,"F09_UF")
	aAdd(aCmpsF09,"F09_CODIPM")
	aAdd(aCmpsF09,"F09_DSCIPM")
						
EndIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada para incluir Botoes na ToolBar.               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( ExistBlock("MA080BUT") )
	aButtonUsr := ExecBlock("MA080BUT",.F.,.F.,{nOpc})
	If ( ValType(aButtonUsr) == "A" )
		For nX := 1 To Len(aButtonUsr)
			aadd(aButtons,aClone(aButtonUsr[nX]))
		Next nX
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Salva a integridade dos campos de Bancos de Dados    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SF4")
dbSetOrder(1)

For nX := 1 to FCount()
	M->&(Field(nX)):= FieldGet(nX)
	lInit := .F.
	If ExistIni(EVAL(bCampo,nX))
		lInit := .T.
		M->&(EVAL(bCampo,nX)):= InitPad(SX3->X3_RELACAO)
		If ValType(M->&(EVAL(bCampo,nX))) = "C"
			M->&(EVAL(bCampo,nX)):= PADR(M->&(EVAL(bCampo,nX)),SX3->X3_TAMANHO)
		Endif
		If M->&(EVAL(bCampo,nX)) == NIL
			lInit := .F.
		EndIf
	EndIf
	If !lInit
		IF ValType(M->&(EVAL(bCampo,nX))) == "C"
			M->&(EVAL(bCampo,nX)) := SPACE(LEN(M->&(EVAL(bCampo,nX))))
		ElseIf ValType(M->&(EVAL(bCampo,nX))) == "N"
			M->&(EVAL(bCampo,nX)) := 0
		ElseIf ValType(M->&(EVAL(bCampo,nX))) == "D"
			If ! "_DATA" $ Trim(EVAL(bCampo,nX))
				M->&(EVAL(bCampo,nX)) := dDataBase
			Else
				M->&(EVAL(bCampo,nX)) := cTod("  /  /  ")
			Endif
		ElseIf ValType(M->&(EVAL(bCampo,nX))) == "L"
			M->&(EVAL(bCampo,nX)) := .F.
		EndIf
	EndIf
Next nX

aObjects := {}
AAdd( aObjects, { 100, 100, .T., .T. } ) 
AAdd( aObjects, { 100,  60, .T., .T. } )
aInfo := { aSize[1],aSize[2],aSize[3],aSize[4],3,3 }
aObj  := MsObjSize( aInfo, aObjects, .T. ) 

DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL 

EnChoice( "SF4", nReg, nOpc, , , , , aObj[1], , 3 )

oFolTES := TFolder():New(aObj[2,1],aObj[2,2],aFolders,{},oDlg,,,,.t.,.f.,aObj[2,4],aObj[2,3],)

cOk	:=	"{||nOpcA:=1"
If lCalcImpV
	A081Cab(nOpc)
	oGetd:=MsGetDados():New(0,0,aObj[2,3]-aObj[2,1]-13,aObj[2,4],nOpc,"A081LinOk","A081TudOk","+FC_SEQ",.T.,aCmpsSFC,,,,,,,,oFolTES:aDialogs[1])
	oGetd:oBrowse:bGotFocus:={|| A081Cab(nOpc)}	

	//If lAjICMS
		cOk	+=	",if(A081TudOk(,nOpc).And.AjusteLOK(),If(!obrigatorio(aGets,aTela),nOpca := 0,oDlg:End()),nOpca := 0)"

	//EndIf
EndIf
If lAjICMS
	oNewGetDad := MsNewGetDados():New(0,0,aObj[2,3]-aObj[2,1]-13,aObj[2,4],GD_UPDATE+GD_INSERT+GD_DELETE,"AjusteLOK","AllwaysTrue","+CC7_SEQ",aCmpsCC7,/*freeze*/,990,"A080FildOK",/*superdel*/,/*delok*/,oFolTES:aDialogs[Iif(lCalcImpV,2,1)],@aHeadCC7,@aColsCC7)
	cOk	+=	Iif(lCalcImpV,"",",Iif(A081TudOk(,nOpc).And.AjusteLOK(),If(!obrigatorio(aGets,aTela),nOpca := 0,oDlg:End()),nOpca:=0)") 

EndIf

If lIpm
	oGetDadIpm := MsNewGetDados():New(0,0,aObj[2,3]-aObj[2,1]-13,aObj[2,4],GD_UPDATE+GD_INSERT+GD_DELETE,"IpmLOK","AllwaysTrue","",aCmpsF09,/*freeze*/,990,/*fieldok*/,/*superdel*/,/*delok*/,oFolTES:aDialogs[Iif(lCalcImpV,3,2)],@aHeadF09,@aColsF09)
	cOk	+=	Iif(lCalcImpV,"",",Iif(A081TudOk(,nOpc).And.IpmLOK(),If(!obrigatorio(aGets,aTela),nOpca := 0,oDlg:End()),nOpca:=0)")	
EndIf

cOk	+=	"}"

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,&(cOk),{||oDlg:End()},,aButtons)

If nOpcA == 1
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³  Posiciona o arquivo de Produto                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Begin Transaction
		lGravaOk := A081Grava()
		If lGravaOk
			If lAjICMS
				GrvAjuste()
			Endif
			If lIpm
				GrvIpm()
			Endif		
			While( GetSx8Len() > nSaveSx8 )
				ConfirmSX8()
			EndDo
			EvalTrigger()
		EndIf
	End Transaction

	If lGravaOk
		MT080AltOk()			
	EndIf
	
	//Trecho que irá realizar a cópia do TES para as demais filiais.
	If lCopySF4 .AND. lGravaOk 	
		Conout(time())
		//Inicia transação
		//Begin Transaction		
		
		CopySF4(QrySF4(SF4->F4_CODIGO))
				
		//End Transaction
		//Fecha transação 
		//Movido controle de Transação para dentro da função
		Conout(time())
	EndIF
	
Endif


dbSelectArea("SF4")
RestInter()
Return( nOpcA )

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A081Deleta³ Autor ³ Jose Lucas            ³ Data ³ 01.07.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de exclusao de Tes e as Amarracoes Tes x Impostos ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A081Deleta(ExpC1,ExpN1)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A081Deleta(cAlias,nReg,nOpc)

Local aSize    := MsAdvSize()
Local aInfo    := {}
Local aObjects := {}
Local aObj     := {}
Local aNoFields:= {"FC_TES"}
Local bCampo   := {|nCPO| Field(nCPO)}
Local bCondicao:= {|| .T.}
Local cSeek    := ""
Local cWhile   := ""
Local cArqs	   := ""
Local cCampo   := ""
Local nOpcA    := 0
Local nX,nY,nZ  := 0
Local oDlg
Local aHeadCC7	:= {}
Local aColsCC7	:= {}
Local aHeadF09	  := {}
Local aColsF09	  := {}
Local lIpm     	 := AliasIndic("F09") .AND. !Empty(Alltrim(GetNewPar("MV_UFIPM",'')))
Local lAjICMS   := Iif(cPaisLoc = "BRA", .T., .F.)
Local lCalcImpV := GetMV("MV_GERIMPV")=="S"
Local aFolders	:=	{}
Local cOk       := ""
Local aButtons	:= {}
Local aButtonUsr:= {}
Local bSkip		:= {|| CC7->CC7_TPREG == "NA" }
Local lRet		:= .T.
Local cQuery	:= "" 
Local cQryADB	:= GetNextAlias()
Local cQrySCK	:= "SCKAUX"
Local cQryAA5	:= "TABAA5"
Local cQrySDX	:= "SDXAUX"
Local bCampoSF4 := { |x| SF4->(Field(x)) }
Local aCmps     := {}
Local aCmpsSFC  := {}
Local oFolTES   := NIL
Local oMontQry:=nil
Local nPos:=0
Local lSF4ex:=FwModeAcces("SF4",1) == "E" .Or. FwModeAcces("SF4",2) == "E" .OR. FwModeAcces("SF4",3) == "E"
PRIVATE aTELA[0][0],aGETS[0]
PRIVATE oGetd

If SoftLock("SF4") .And. SF4->(LastRec()) <> 0 .And. SF4->F4_FILIAL == xFilial("SF4")

	aHeader := {}
	aCols   := {}
	
	If lCalcImpV
		aAdd(aFolders,STR0023)	//"Impostos variáveis"

		dbSelectArea("SFC")
		dbSetOrder(1)
		If dbSeek(xFilial("SFC")+SF4->F4_CODIGO)
			cSeek    := xFilial("SFC")+SF4->F4_CODIGO
			cWhile   := "SFC->FC_FILIAL+SFC->FC_TES"
			bCondicao:= {|| If( SoftLock("SFC") , .T. , .F. ) }
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Sintaxe da FillGetDados(nOpcX,Alias,nOrdem,cSeek,bSeekWhile,uSeekFor,aNoFields,aYesFields,lOnlyYes,cQuery,bMontCols,lEmpty,aHeaderAux,aColsAux,bAfterCols,bBeforeCols,bAfterHeader,cAliasQry |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			FillGetDados(nOpc,"SFC",1,cSeek,{|| &cWhile },bCondicao,aNoFields,,,,,,,,,,,)
		Else
			FillGetDados(nOpc,"SFC",1,,,,aNoFields,,,,,.T.,,,,,)
			aCols[1][aScan(aHeader,{|x| Trim(x[2])=="FC_SEQ"})] := StrZero(1,Len(SFC->FC_SEQ))
		EndIf
	EndIf
	
	If lAjICMS
		Private	oNewGetDad
		
		aAdd(aFolders,STR0024)	//"Lançamentos da Apuração de ICMS"	
		aMHead("CC7","CC7_TES/",@aHeadCC7)
		CC7->(dbSeek(xFilial("CC7")+SF4->F4_CODIGO))
		aMAcols(nOpc,"CC7",@aColsCC7,aHeadCC7,{||CC7->CC7_FILIAL == xFilial("CC7") .And. SF4->F4_FILIAL == xFilial("SF4") .And. CC7->CC7_TES==SF4->F4_CODIGO},bSkip)
	EndIf
	
	
	If lIpm
		Private	oGetDadIpm
		
		aAdd(aFolders,'Índice de Participação dos Município')	//"Lançamentos da Apuração de ICMS"	
		aMHead("F09","F09_TES/",@aHeadF09)
		F09->(dbSeek(xFilial("F09")+SF4->F4_CODIGO))
		aMAcols(nOpc,"F09",@aColsF09,aHeadF09,{||F09->F09_FILIAL == xFilial("F09") .And. SF4->F4_FILIAL == xFilial("SF4") .And. F09->F09_TES==SF4->F4_CODIGO})	
	EndIF

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de Entrada para incluir Botoes na ToolBar.               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( ExistBlock("MA080BUT") )
		aButtonUsr := ExecBlock("MA080BUT",.F.,.F.,{nOpc})
		If ( ValType(aButtonUsr) == "A" )
			For nX := 1 To Len(aButtonUsr)
				aadd(aButtons,aClone(aButtonUsr[nX]))
			Next nX
		EndIf
	EndIf

	dbSelectArea("SF4")
	For nX := 1 to FCount()
		M->&(EVAL(bCampo,nX)) := FieldGet(nX)
	Next nX
	
	aObjects := {}
	AAdd( aObjects, { 100, 100, .T., .T. } )
	AAdd( aObjects, { 100,  60, .T., .T. } )
	aInfo := { aSize[1],aSize[2],aSize[3],aSize[4],3,3 }
	aObj  := MsObjSize( aInfo, aObjects, .T. )
	
	DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
	
	EnChoice( "SF4", nReg, nOpc, , , , , aObj[1], , 3 )

	oFolTES := TFolder():New(aObj[2,1],aObj[2,2],aFolders,{},oDlg,,,,.t.,.f.,aObj[2,4],aObj[2,3],)
	
	cOk	:=	"{||nOpcA:=2"
	If lCalcImpV
		A081Cab(nOpc)
		oGetd   := MsGetDados():New(0,0,aObj[2,3]-aObj[2,1]-13,aObj[2,4],nOpc,"A081LinOk","A081TudOk","",,,,,,,,,,oFolTES:aDialogs[1])
		cOk	+=	",if(A081TudOk(,nOpc),oDlg:End(),nOpca := 0)"
	EndIf

	If lAjICMS
		oNewGetDad	:=	MsNewGetDados():New(0,0,aObj[2,3]-aObj[2,1]-13,aObj[2,4],0,"AllwaysTrue","AllwaysTrue",,{"CC7_CODLAN"},/*freeze*/,990,/*fieldok*/,/*superdel*/,/*delok*/,oFolTES:aDialogs[Iif(lCalcImpV,2,1)],@aHeadCC7,@aColsCC7)
		cOk	+=	Iif(lCalcImpV,"",",oDlg:End()")
	EndIf
	
	If lIpm
		oGetDadIpm	:=	MsNewGetDados():New(0,0,aObj[2,3]-aObj[2,1]-13,aObj[2,4],0,"AllwaysTrue","AllwaysTrue",,,/*freeze*/,990,/*fieldok*/,/*superdel*/,/*delok*/,oFolTES:aDialogs[Iif(lCalcImpV,3,2)],@aHeadF09,@aColsF09)		
	EndIf
	
	cOk	+=	"}"

	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,&(cOk),{||oDlg:End()},,aButtons)
	
	If nOpcA == 2
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se esta associado a um contrato parceria            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		#IFDEF TOP   
			cQuery := "SELECT ADB_TES, ADB_TESCOB " 
			cQuery += "	FROM " + RetSQLName("ADB") + " ADB " 
			cQuery += "	WHERE "
			If FwModeAcces("ADB",1) == "E" .Or. FwModeAcces("ADB",2) == "E" .OR. FwModeAcces("ADB",3) == "E"
			    cQuery += "ADB.ADB_FILIAL = '"+xFilial("ADB")+"' AND "
			EndIf 
			cQuery += "	ADB.ADB_TES = '" + SF4->F4_CODIGO + "' OR "
			cQuery += "	ADB.ADB_TESCOB = '" + SF4->F4_CODIGO + "' AND "
			cQuery += "	ADB.D_E_L_E_T_ = ' ' "
			
			cQuery := ChangeQuery( cQuery )
			DbUseArea( .T., "TopConn", TCGenQry(,,cQuery), cQryADB, .F., .F. )
		
			If (cQryADB)->(!EOF())
				Aviso( STR0018, STR0029,; 		// Atenção "Esta TES está associada a um contrado de parceria e não será possível sua exclusão "	
					{STR0020 }, 2 ) 			//Ok 
				lRet := .F.
			EndIf
			(cQryADB)->(DbCloseArea())	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se esta associado a um serviço - SIGATEC            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lRet
				cQuery := "SELECT AA5_TES " 
				cQuery += "	FROM " + RetSQLName("AA5") + " AA5 " 
				cQuery += "	WHERE "
				If FwModeAcces("AA5",1) == "E" .Or. FwModeAcces("AA5",2) == "E" .OR. FwModeAcces("AA5",3) == "E"
				    cQuery += "AA5.AA5_FILIAL = '"+xFilial("AA5")+"' AND "
				EndIf 
				cQuery += "	AA5.AA5_TES = '" + SF4->F4_CODIGO + "' AND "
				cQuery += "	AA5.D_E_L_E_T_ = '' "
				
				cQuery := ChangeQuery( cQuery )
				DbUseArea( .T., "TopConn", TCGenQry(,,cQuery), cQryAA5, .F., .F. )
				
				If (cQryAA5)->(!EOF())
					Aviso( STR0018,STR0031,{STR0020 }, 2 )// "Esta TES está associada a um serviço no módulo Gestão de Serviços e não será possível sua exclusão."
					lRet:= .F.	
				EndIf	
				(cQryAA5)->(DbCloseArea())	
			EndIf   
			
			If lRet
				cQuery := "SELECT COUNT(CK_TES) QTD " 
				cQuery += "	FROM " + RetSQLName("SCK") + " SCK " 
				cQuery += "	WHERE "
				cQuery += "SCK.CK_FILIAL = ? AND " 
				If lSF4ex
					cQuery += "(SCK.CK_FILENT = ? OR "
					cQuery += "SCK.CK_FILVEN = ?) AND "
				endif
				cQuery += "	SCK.CK_TES = ? AND "
				cQuery += "	SCK.D_E_L_E_T_ = ? "
				
				//cQuery := ChangeQuery( cQuery )
				oMontQry := FwExecStatement():New(cQuery)
				//DbUseArea( .T., "TopConn", TCGenQry(,,cQuery), cQrySCK, .F., .F. )

				oMontQry:SetString(nPos += 1, xFilial("SCK"))

				if lSf4ex
					oMontQry:SetString(nPos += 1, cfilant)
					oMontQry:SetString(nPos += 1, cfilant)
				endif 

				oMontQry:SetString(nPos += 1, SF4->F4_CODIGO)
				oMontQry:SetString(nPos += 1, " ")

				cQrySCK := oMontQry:OpenAlias()  

				If (cQrySCK)->QTD > 0
					Aviso( STR0018, STR0032,; 		// Atenção "Esta TES está associada a um orçamento e não será possível sua exclusão."	
						{STR0020 }, 2 ) 			//Ok 
					lRet := .F.
				EndIf 
				(cQrySCK)->(DbCloseArea())	
			EndIf
			
			If lRet
				cQuery := "SELECT COUNT(DX_TES) QTD " 
				cQuery += "	FROM " + RetSQLName("SDX") + " SDX " 
				cQuery += "	WHERE "
				If FwModeAcces("SDX",1) == "E" .Or. FwModeAcces("SDX",2) == "E" .OR. FwModeAcces("SDX",3) == "E"
				    cQuery += "SDX.DX_FILIAL = '"+xFilial("SDX")+"' AND "
				EndIf
				cQuery += "	SDX.DX_TES = '" + SF4->F4_CODIGO + "' AND "
				cQuery += "	SDX.D_E_L_E_T_ = ' ' "
				
				cQuery := ChangeQuery( cQuery )
				DbUseArea( .T., "TopConn", TCGenQry(,,cQuery), cQrySDX, .F., .F. )
			
				If (cQrySDX)->QTD > 0
					Aviso( STR0018, STR0033,; 		// Atenção “Esta TES está associada a (Itens da programação de Entrega) e não será possível sua exclusão.”	
						{STR0020 }, 2 ) 			//Ok 
					lRet := .F.
				EndIf 
				(cQrySDX)->(DbCloseArea())	
			EndIf  
					
		#ENDIF
	
		If lRet
			dbSelectArea( cAlias )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se existe movimento este TES                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cArqs := a080Movim(SF4->F4_CODIGO)
			
			If Empty(cArqs)
				
				Begin Transaction
			
					If lCalcImpV
						dbSelectArea( "SFC" )
						dbSeek(xFilial("SFC")+SF4->F4_CODIGO)
						While !Eof() .And.  FC_FILIAL+FC_TES == xFilial("SFC")+SF4->F4_CODIGO
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿*/
							//³  Deleta Itens do Tes.                                ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							RecLock("SFC",.F.,.T.)
							dbDelete()
							MsUnLock()
							dbSelectArea("SFC")
							dbSkip()
						Enddo
					EndIf
			
					If lRet .And. ExistBlock("MA080VLD")
						lRet := ExecBlock("MA080VLD",.F.,.F., { nOpcA } )
					EndIf
		
					If lRet
						If lAjICMS
							dbSelectArea( "CC7" )
							dbSeek(xFilial("CC7")+SF4->F4_CODIGO)
							While !Eof() .And.  CC7_FILIAL+CC7_TES == xFilial("CC7")+SF4->F4_CODIGO
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³  Deleta Itens do Tes.                                ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								RecLock("CC7",.F.,.T.)
								dbDelete()
								MsUnLock()
								dbSelectArea("CC7")
								dbSkip()
							Enddo
						EndIf
						
						If lIpm
							dbSelectArea( "F09" )
							dbSeek(xFilial("F09")+SF4->F4_CODIGO)
							While !Eof() .And.  F09_FILIAL+F09_TES == xFilial("F09")+SF4->F4_CODIGO
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³  Deleta Itens do Tes.                                ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								RecLock("F09",.F.,.T.)
								dbDelete()
								MsUnLock()
								dbSelectArea("F09")
								dbSkip()
							Enddo
						EndIf			
																				
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Gravacao do Historico da SF4.³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
						If lHistFiscal					
							aCmps :=  RetCmps("SF4",bCampoSF4)	
			 				GrvHistFis("SF4", "SS0", aCmps)								
				  		EndIf						
  						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Deletando na SF4.         ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ						
						dbSelectArea("SF4")
						RecLock("SF4",.F.,.T.)
						dbDelete()
						MsUnLock()

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Deleta os campos Memos Virtuais					  				  ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If Type("aMemos") == "A"
							For nX := 1 to Len(aMemos)
								cVar := 'SF4->'+AllTrim(aMemos[nX][1])
								MSMM(&cVar,,,,2)
							Next nX
						EndIf					

					EndIf
						
				End Transaction

				MT080AltOk()
	
				If ExistBlock("MT081EXC")
					ExecBlock("MT081EXC",.F.,.F.)
				EndIf
			Else
				HELP(" ",1,"NaoExclui",,cArqs,4,1)
			Endif
		Endif
	EndIf
Else
	IF SF4->F4_FILIAL <> xFilial("SF4")
		Help(" ",1,"A000FI")
	Endif
EndIf

Return( nOpcA )

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A081LinOk ³ Autor ³ Jose Lucas            ³ Data ³ 01.07.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Consistencia nas de linhas da Amarrcao Tes X Impostos.     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ ExpN1 = A081LinOk                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = Valor devolvido pela fun‡„o                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function A081LinOk(o)

Local lRet    := .T.
Local nPosImp := Ascan(aHeader,{|x| Trim(x[2])=="FC_IMPOSTO"})
Local nPosSeq := Ascan(aHeader,{|x| Trim(x[2])=="FC_SEQ"})
Local nPosDupl:= Ascan(aHeader,{|x| Trim(x[2])=="FC_INCDUPL"})
Local nPosNota:= Ascan(aHeader,{|x| Trim(x[2])=="FC_INCNOTA"})
Local nPosCred:= Ascan(aHeader,{|x| Trim(x[2])=="FC_CREDITA"})
Local nPosCalc:= Ascan(aHeader,{|x| Trim(x[2])=="FC_CALCULO"})
Local nPosLiq := Ascan(aHeader,{|x| Trim(x[2])=="FC_LIQUIDO"})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ verifica se o ultimo elemento do array e' Logico     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aCols[n][Len(aCols[n])]
	Return .T.
EndIf

If lRet
	If nPosImp > 0 .And. nPosSeq > 0
		lRet := !Empty(aCols[n][nPosImp]) .And. !Empty(aCols[n][nPosSeq])
	EndIf

	If lRet .And. ( nPosImp > 0 .And. nPosSeq > 0 .And. nPosDupl > 0 .And. nPosCred > 0 .And. nPosCalc > 0 .And. nPosLiq > 0 )
		If Empty(aCols[n][nPosImp])
			If !Empty(aCols[n][nPosDupl]) .or. !Empty(aCols[n][nPosNota]) .or. !Empty(aCols[n][nPosCred]) .or. !Empty(aCols[n][nPosCalc])
				lRet := .F.
			EndIf
		Else
			If Empty(aCols[n][nPosDupl]) .or. Empty(aCols[n][nPosNota]) .or. Empty(aCols[n][nPosCred]) .or. Empty(aCols[n][nPosCalc])
				lRet := .F.
			EndIf
		EndIf
	EndIf
	If !lRet
		Help(" ",1,"CHECKCOLS")
	Endif
EndIf

Return( lRet )

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A081TudOk ³ Autor ³ Jose Lucas            ³ Data ³ 01.07.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Consistencia geral dos itens da Amarracao Tes x Impostos   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ ExpN1 = A081TudOk                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = Valor devolvido pela fun‡„o                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/
Function A081TudOk(o,nOpc)

Local nx
Local ny
local lRet := .T.
Local nMaxArray
Local nPosInc  := Ascan(aHeader,{|x| Alltrim(x[2]) == 'FC_INCIMP'})
Local nPosCal  := Ascan(aHeader,{|x| Alltrim(x[2]) == 'FC_CALCULO'})
Local nPosIMP  := Ascan(aHeader,{|x| Alltrim(x[2]) == 'FC_IMPOSTO'})
Local nPosSeq  := Ascan(aHeader,{|x| Trim(x[2])=="FC_SEQ"})
Local nPosDupl := Ascan(aHeader,{|x| Trim(x[2])=="FC_INCDUPL"})
Local nPosNota := Ascan(aHeader,{|x| Trim(x[2])=="FC_INCNOTA"})
Local nPosCred := Ascan(aHeader,{|x| Trim(x[2])=="FC_CREDITA"})
Local nPosCalc := Ascan(aHeader,{|x| Trim(x[2])=="FC_CALCULO"})
Local nPosLiq  := Ascan(aHeader,{|x| Trim(x[2])=="FC_LIQUIDO"})
Local nPosCpo  := 0
Local lCopySF4	:= Iif('SF4' $ Upper(GetNewPar("MV_FISCOPY",'')), .T.,.F.)
Local cMsgCopy	:= '' 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Validacao da TES                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lRet := Ma080Valid(nOpc)

If lRet
	If ExistBlock("MA081VLD")
		lRet := ExecBlock("MA081VLD",.F.,.F., { nOpc } )
	EndIf
EndIf

If lRet .And. nPosInc > 0 .and. nPosCal > 0 .and. nPosImp > 0

	nMaxArray := Len(aCols)
	
	If nMaxArray == 1
		If	Empty(aCols[nMaxArray][1])
			lRet := .F.
			Help(" ",1,"A410SEMREG")
		EndIf
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verificar se algum imposto por total incide em outro, se for o ³
	//³caso, o imposto base deve ser por total tambem.                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nPosCpo := Ascan(aHeader,{|x| Trim(x[2])=="FC_CALCULO"})
	If lRet .And. nPosCpo > 0 
		If Ascan(aCols,{|x| x[nPosCal] == 'I' .And. !Empty(x[nPosInc]) .And. !x[Len(x)]}) > 0
			For nX:=1 To nMaxArray
				If aCols[nX][nPosCal]	==	'I' .And. !Empty(aCols[nX][nPosInc]) .And. !aCols[nX][Len(aCols[nX])]
					If Ascan(aCols,{|x,y| x[nPosImp] $ aCols[nX][nPosInc] .And. x[nPosCal] <> 'I'  .And. !x[Len(x)]},,nX-1) > 0
						Help(" ",1,"A081INCIMP")
						lRet := .F.
						Exit
					Endif
				Endif
			Next
		Endif
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ verifica se o ultimo elemento do array esta em branco³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( lRet .And. nPosImp > 0 .And. nPosSeq > 0 .And. nPosDupl > 0 .And. nPosCred > 0 .And. nPosCalc > 0 .And. nPosLiq > 0 )
		For ny := 1 To Len(aCols)   
			If aCols[nY][Len(aCols[nY])]
			    lRet := .T.
			Else
				If Empty(aCols[ny][nPosImp])
					If !Empty(aCols[ny][nPosDupl]) .or. !Empty(aCols[ny][nPosNota]) .or. !Empty(aCols[ny][nPosCred]) .or. !Empty(aCols[ny][nPosCalc])
						lRet := .F.
					EndIf
				Else
					If Empty(aCols[ny][nPosDupl]) .or. Empty(aCols[ny][nPosNota]) .or. Empty(aCols[ny][nPosCred]) .or. Empty(aCols[ny][nPosCalc])
						lRet := .F.
					EndIf
				EndIf
			EndIf
		Next ny
		If !lRet
			Help(" ",1,"CHECKCOLS")
		Endif
	EndIf
	
	
	
		
EndIf

Return( lRet )

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A081Seq   ³ Autor ³ Jose Lucas            ³ Data ³ 01.07.98³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Validar a Sequencia.                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA081                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A081Seq()
Return(.T.)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A081Cab  ³ Autor ³ Jose Lucas            ³ Data ³ 01.07.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Consiste Dados da cabecalho do Tes                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MatA081                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A081Cab(nOpc)
Return(.T.)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A081Grava ³ Autor ³ Jose Lucas            ³ Data ³ 01.07.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Grava todas informacoes nos arquivos SF4 e SFC             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Mata081                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A081Grava(nRecSF4,lAlt)
LOCAL nX, nMaxArray, bCampo,aAnterior:={}, nCntDel:=0
LOCAL nRegx := 0, cVar                       
LOCAL ny := 0    
LOCAL i  := 0 
Local bCampoSF4  := { |x| SF4->(Field(x)) }
Local nZ
Local cIdHist    := IdHistFis()
Local lCopySF4	:= Iif('SF4' $ Upper(GetNewPar("MV_FISCOPY",'')), .T.,.F.)
Local cMsgCopy	:= ''

DEFAULT nRecSF4 := 0
DEFAULT lAlt    := .F.

bCampo := {|nCPO| Field(nCPO) }

If Altera
	SF4->(dbGoto(nRecSF4))
Endif	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica o numero de itens do Tes                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aCols) == 1
	If aCols[1][1] =="01" .And. Empty(aCols[1][2]) .And.;
			Empty(aCols[1][3]) .And. Empty(aCols[1][4]) .And.;
			Empty(aCols[1][5])
		aCols := {}
	EndIf
EndIf

nMaxArray := Len(aCols)
If nMaxArray > 1
	For ny := 1 to Len(aHeader)
		If Empty(aCols[nMaxArray][ny])
			If Trim(aHeader[ny][2]) == "FC_SEQ" .Or.;
					Trim(aHeader[ny][2]) == "FC_IMPOSTO"
				nMaxArray--
				Exit
			EndIf
		Endif
	Next ny

	If nMaxArray <= 0
		Return .F.
	Endif

	For nx=1 to nMaxArray
		If aCols[nx][Len(aCols[nx])]
			nCntDel++
		Endif
	Next nx

	If nCntDel >= nMaxArray
		Return .F.
	Endif
EndIf	

If Inclui .And.	SF4->(dbSeek(xFilial("SF4")+M->F4_CODIGO))

	MsgInfo("Código de Tipo de Entrada\Saída já existente!")
	Return .F.
	
EndIf

//Verifica aqui se o código do TES já exite em outra filial que será replicado
IF lCopySF4 .AND. Inclui
	cMsgCopy	:= VldReplica(M->F4_CODIGO)
	If len(alltrim(cMsgCopy)) > 0
		IF !IsBlind()
			MSGSTOP('TES não será gravado, pois já está cadastrado na(s) filial(ais) abaixo:'  +  chr(13)+chr(10) + chr(13)+chr(10)+   cMsgCopy,'Atenção')
		EndIF
		Return .F.
	EndIF
EndIF

	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava o arquivo de Cabecalho SF4.                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RecLock("SF4",IIF(inclui,.T.,.F.))
For i := 1 TO FCount()
	If "FILIAL"$Field(i)
		FieldPut(i,xFilial())
	Else
		FieldPut(i,M->&(EVAL(bCampo,i)))
	EndIf
Next i


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Grava os campos Memos Virtuais					  				  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Type("aMemos") == "A"
	For nX := 1 to Len(aMemos)
		cVar := aMemos[nX][2]
		cVar1:= aMemos[nX][1]
		cAliasMemo := If(len(aMemos[nX]) == 3,aMemos[nX][3],Nil)
		MSMM(&cVar1,TamSx3(aMemos[nX][2])[1],,&cVar,1,,,'SF4',aMemos[nX][1],cAliasMemo)
	Next nX
EndIf


If cPaisLoc=="PTG" .And. Empty(M->F4_CF)
	If M->F4_CODIGO>="500"
	    Replace F4_CF With "511"
   		Replace F4_TEXTO With "Venda"
	Else
    	Replace F4_CF With "111"
    	Replace F4_TEXTO With "Compra"
    EndIf	
EndIf                                

If lAlt .And. lHistFiscal
	Replace F4_IDHIST With cIdHist
EndIf	

MsUnLock()

SF4->(FkCommit())
cTes := M->F4_CODIGO

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava arquivo SFC Amarracao Tes x Impostos.         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SFC")
If dbSeek(xFilial("SFC")+cTes,.F.)
	While !Eof() .And. FC_FILIAL+FC_TES == xFilial("SFC")+cTes   
		RecLock("SFC",.F.)
		dbDelete()
		MsUnLock()
		dbSkip()
	End
Endif
                   
dbSelectArea("SFC")
For nx := 1 to nMaxArray
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Apenas da append quando o acols nao estiver deletado  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ! aCols[nx][Len(aCols[nx])]
		RecLock("SFC",.T.)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza dados - SFC                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		Replace FC_FILIAL     With xFilial("SFC")
	   Replace FC_TES        With SF4->F4_CODIGO

		For ny := 1 to Len(aHeader)
			cVar := Trim(aHeader[ny][2])
			If aHeader[ny][10] # "V"
				Replace &cVar. With aCols[nx][ny]
			Endif
		Next ny  
	   If lHistFiscal
			Replace FC_IDHIST With cIdHist
		EndIf	
		
		MsUnLock()
	EndIf
Next nx

dbSelectArea("SF4")
If ExistBlock("MT080GRV")
	ExecBlock("MT080GRV",.F.,.F.)
EndIf


Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A081ValidC³ Autor ³ Armando T. Buchina    ³ Data ³ 23/01/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida o Registro para ver se ja existe                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nao Ha.                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA081                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A081ValidC()

Local cVar:=ReadVar(),nReg,cAl:=Alias(), nCount := 0 , lRet := .t., nX := 0
Local lFound := .f.

If cVar == "M->FC_IMPOSTO"
	For nCount := 1 to Len( aHeader )
		If aHeader[ nCount , 2 ] == "FC_IMPOSTO"
			For nX := 1 to Len( aCols )
				If aCols[ nX , nCount ] == &cVar .and. nX != n
					lRet := .f.
					Exit
				EndIf
			Next nX
			Exit
		EndIf
	Next nCount
	If !lRet
		Help(" ",1,"A081TES1")
	Endif

	dbSelectArea("SF9")
	dbSeek(cFilial+&cVar)
	If Eof()
		Help(" ",1,"A081TES2")
		dbSelectArea("SFC")
		lRet := .f.
	Endif
ElseIf cVar == "M->FC_SEQ"	
	For nCount := 1 to Len( aHeader )
		If Trim(aHeader[ nCount , 2 ]) == "FC_SEQ"
			For nX := 1 to Len( aCols )
				If Val(aCols[nX][nCount]) != nX .or. Val(&cVar) != nX
					If n == nx
						&cVar := StrZero(nX,2)
					Endif
					aCols[nX][nCount] := StrZero( nX ,2 )	
				EndIf
			Next nX
			Exit
		EndIf
	Next nCount

EndIf

dbSelectArea(cAl)

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A081ValidI³ Autor ³ Armando T. Buchina    ³ Data ³ 23/01/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida na Linha o Imposto da Incidencia                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nao Ha.                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA081                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A081ValidImp( cSeqCalc, cImpsInc )

Return ( .T. )

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A081Mod3  ³ Autor ³ Armando T. Buchina    ³ Data ³ 23/01/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chama a Modelo3 Padrao para edicao                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Opcap                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA081                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A081Mod3(cOpcao)

Local lRet := .t.
Local nI   := 0 

nOpcx := Val( cOpcao )

dbSelectArea("SF4")

For ni:= 1 to FCount()
	&("M->"+FieldName(ni)):=If((cOpcao == "3") ,CriaVar(FieldName(ni)),FieldGet(ni))
Next

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montando aHeader                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE nUsado  := 0, aHeader := {}, nRegSF4 := SF4->( Recno() )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se existe algum dado no arquivo                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOpcx # 3
	dbSelectArea( "SFC" )
	dbSeek(xFilial()+SF4->F4_CODIGO)
	nCnt := 0
	While !Eof() .And. FC_TES==SF4->F4_CODIGO
		nCnt := nCnt + 1
		dbSkip()
	EndDo
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta o cabecalho                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
A081aHead()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona ponteiro do arquivo cabeca e inicializa variaveis  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOpcA := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Quando for inclusao criar com 1 elemento                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aCpoEnChoice := {"F4_CODIGO"}
aCols  := {}
nCnt   := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gerar o array aCols com os itens do pedido de venda          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
A081aCols()

lRet:=Modelo3(cCadastro,"SF4","SFC",aCpoEnChoice,"A081LinOk()","A081TudOk()",nOpcx,nOpcx)

A081Grava(lRet)

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A081aHead ³ Autor ³ Armando T. Buchina    ³ Data ³ 23/01/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta aHeader com os dados do arquivo                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA081                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A081aHead()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Salva a Integridade dos campos de Bancos de Dados            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(1)
dbSeek("SFC")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta o array aHeader para a GetDados()                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
While !Eof() .And. (X3_ARQUIVO == "SFC")
	IF X3USO(X3_USADO) .And. cNivel >= X3_NIVEL .and. X3_CAMPO != "FC_TES"
		nUsado:=nUsado+1
		AADD(aHeader,{ Trim(X3Titulo()), ;
			X3_CAMPO,;
			X3_PICTURE, ;
			X3_TAMANHO, ;
			X3_DECIMAL, ;
			"AllwaysTrue()" , ;
			X3_USADO,;
			X3_TIPO, ;
			X3_ARQUIVO,;
			X3_CONTEXT } )
	Endif
	dbSkip()
Enddo

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A081aCols ³ Autor ³ Armando T. Buchina    ³ Data ³ 23/01/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta aCols   com os dados do arquivo                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA081                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A081aCols()

Local _nI := 0 

If nOpcx == 3
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta o array aCols com 1 item em branco (inclusao)          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aCols:={Array(nUsado+1)}
	aCols[1,nUsado+1]:=.F.
	For _ni:=1 to nUsado
		IF aHeader[_ni][8] == "C"
			If Trim(aHeader[_ni][2]) == "FC_SEQ"
				aCOLS[1][_ni] := Repl("0",aHeader[_ni][4]-1)+"1"
			Else
				aCOLS[1][_ni] := SPACE(aHeader[_ni][4])
			Endif
		elseif aHeader[_ni][8] == "N"
			aCOLS[1][_ni] := 0
		elseif aHeader[_ni][8] == "D"
			aCOLS[1][_ni] := dDataBase
		elseif aHeader[_ni][8] == "M"
			aCOLS[1][_ni] := ""
		else
			aCOLS[1][_ni] := .F.
		Endif	
		If aHeader[_ni,10] # "V" .and. Trim(aHeader[_ni][2]) <> "FC_SEQ"
			aCols[1,_ni]:=CriaVar(aHeader[_ni,2])
		EndIf
	Next
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta o array aCols com os itens                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aCols:={}
	dbSelectArea("SFC")
	dbSetOrder(1)
	dbSeek( xFilial() + M->F4_CODIGO )
	While !Eof() .And. FC_TES==M->F4_CODIGO
		AADD(aCols,Array(nUsado+1))
		For _ni:=1 to nUsado
			aCols[Len(aCols),_ni]:=FieldGet(FieldPos(aHeader[_ni,2]))
		Next
		aCols[Len(aCols),nUsado+1]:=.F.
		dbSkip()
	End
Endif

If Len(aCols)  == 0
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta o array aCols com 1 item em branco (inclusao)          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aCols:={Array(nUsado+1)}
	aCols[1,nUsado+1]:=.F.
	For _ni:=1 to nUsado
		aCols[1,_ni]:=CriaVar(aHeader[_ni,2])
	Next
	Return .T.
Endif

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A081Form  ³ Autor ³ Jose Lucas            ³ Data ³ 22/01/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina verificadora da formula digitada                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA080                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A081Form()

Local cForm:=&(ReadVar()) , cVar := ReadVar()
Local lRet:=.T., cFile
Local cNomePath := GetMV("MV_DIRRDMK")

If "EXECBLOCK" $ Upper( cForm )
	Help( " ",1,"A081FRM1")
	lRet := .F.
End	

cForm := AllTrim( cNomePath+cForm )

If !ExistBlock(cForm)
	Help( " ",1,"A081FRM2")
	lRet := .F.
Endif

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³a080SXBInclui³ Autor ³ Edson Maricate        ³ Data ³ 05/06/2000 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Executa a rotina de exclusao atraves do SXB.                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA080,SXB                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a080SXBInclui()

Local aArea	:= GetArea()

PRIVATE aRotina
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Salva a Intercafe Anterior                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SaveInter()
dbSelectArea("SF4")

If GetMv("MV_GERIMPV") == "S"
	aRotina := { { OemToAnsi(STR0001),"AxPesqui"  ,0,1},;		//"Pesquisar"			
		{ OemToAnsi(STR0002),"A081Visual",0,2},;		//"Visualizar"
		{ OemToAnsi(STR0003),"A081Inclui",0,3},;		//"Incluir"
		{ OemToAnsi(STR0004),"A081Altera",0,4,20},;  //"Alterar"
		{ OemToAnsi(STR0005),"A081Deleta",0,5,21}}	//"Excluir"
	A081Inclui("SF4",RecNo(),3)
Else
	aRotina := { { STR0001,	"AxPesqui"  , 0 , 1},;		//"Pesquisar"
		{ STR0002,	"AxVisual"  , 0 , 2},;		//"Visualizar"
		{ STR0003,  "AxInclui"  , 0 , 3},;		//"Incluir"
		{ STR0004,  "AxAltera"  , 0 , 4, 15},;	//"Alterar"
		{ STR0005,  "A080Deleta", 0 , 5, 16} }	//"Excluir"
	AxInclui("SF4",RecNo(),3)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a Interface Anterior                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aArea)
RestInter()

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³a080SXBVisual³ Autor ³ Edson Maricate        ³ Data ³ 05/06/2000 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Executa a rotina de Visualizacao atraves do SXB                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA080,SXB                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a080SXBVisual()

Local aArea	:= GetArea()

PRIVATE aRotina

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Salva a Intercafe Anterior                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SaveInter()
dbSelectArea("SF4")

If GetMv("MV_GERIMPV") == "S"
	aRotina := {	{ OemToAnsi(STR0001),"AxPesqui"  ,0,1},;		//"Pesquisar"			
					{ OemToAnsi(STR0002),"A081Visual",0,2},;		//"Visualizar"
					{ OemToAnsi(STR0003),"A081Inclui",0,3},;		//"Incluir"
					{ OemToAnsi(STR0004),"A081Altera",0,4,20},;  //"Alterar"
					{ OemToAnsi(STR0005),"A081Deleta",0,5,21}}	//"Excluir"
	A081Visual("SF4",RecNo(),2)
Else

	aRotina := {	{ STR0001,	"AxPesqui"  , 0 , 1},;		//"Pesquisar"
					{ STR0002,	"AxVisual"  , 0 , 2},;		//"Visualizar"
					{ STR0003,  "AxInclui"  , 0 , 3},;		//"Incluir"
					{ STR0004,  "AxAltera"  , 0 , 4, 15},;	//"Alterar"
					{ STR0005,  "A080Deleta", 0 , 5, 16} }	//"Excluir"
	AxVisual("SF4",RecNo(),2)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a Interface Anterior                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aArea)
RestInter()

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³a080Livro    ³ Autor ³ Wagner Xavier         ³ Data ³ 15/09/2000 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Valida a rotina do livro                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA080,SXB                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a080Livro()

Local lRet := .F.

If Vazio() .or. ExistBlock(Alltrim(M->F4_LIVRO), Iif( cPaisLoc="BRA",.F.,.T.) )
	lRet := .T.
Endif

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ a080Imp  ³ Autor ³ Percy A Horna         ³ Data ³ 21/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Permite a escolha de varios impostos                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA080                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a080Imp(cImposto)

Local oDlgPed, oDlgTran, nI, nAt
Private _lReturn := .T.
Private lConfirmo   := .F.
Private nTarget, aTarget, nSource, aSource, aBkTarget
Private nPosInc

nSource   := 0
nTarget   := 0
aSource   := {}
aTarget   := {}
aBkTarget := {}
nAt       := 0

DbSelectArea("SFB")
If BOF() .And. EOF()
	Help(" ",1,"NORECNO")
	_lReturn := .F.
Else

	nPosInc := Ascan(aHeader,{|x| Trim(x[2])=="FC_INCIMP"})

	If AT(";",aCols[n][nPosInc]) <= 0
		AADD(aBkTarget,SubStr(aCols[n][nPosInc],1,3 ))
	Else
		For nI := 1 To Len(Alltrim(aCols[n][nPosInc])) Step 4
			AADD(aBkTarget,SubStr(aCols[n][nPosInc],nI,AT(";",aCols[n][nPosInc])-1 ))
		Next
	EndIf

	SFB->(dbSeek(xFilial()))
	Do While .Not. SFB->(Eof())
		If 	Ascan(aBkTarget,SFB->FB_CODIGO) > 0
			AADD(aTarget,SFB->FB_CODIGO + " - " + SFB->FB_DESCR)
		Else
			AADD(aSource,SFB->FB_CODIGO + " - " + SFB->FB_DESCR)
		EndIf
		SFB->(dbSkip())
	Enddo

	lConfirmo   := .F.
	oFntBox := TFont():New( "Courier New", 6,0)
	cCab    := STR0015   //"Impostos"
	DEFINE MSDIALOG oDlgPed FROM 105,074 To 300,712 TITLE STR0016 PIXEL//"Seleccione los impuestos"
	oCab1 := TSay():New(006,010,{||cCab},oDlgPed,,oFntBox,,,,.t.)
	oCab2 := TSay():New(006,180,{||cCab},oDlgPed,,oFntBox,,,,.t.)
	oSource := TListBox():New( 014,010,{|u| If(PCount()==0,nSource,nSource:=u)},aSource,130,65,,oDlgPed,,,,.T.,,,oFntBox)
	oTarget := TListBox():New( 014,180,{|u| If(PCount()==0,nTarget,nTarget:=u)},aTarget,130,65,,oDlgPed,,,,.T.,,,oFntBox)
	@ 020,146 Button OemToAnsi(STR0003) + " >" Size 29,12 Action AddItem() of oDlgPed PIXEL  //"_Incluir >>"
	@ 034,146 Button "< " + OemToAnsi(STR0013) Size 29,12 Action RemItem() of oDlgPed PIXEL  //"<< _Sacar"
	@ 048,146 Button OemToAnsi(STR0014) + " >>" Size 29,12 Action AddAll() of oDlgPed PIXEL  //"Todos ->"
	@ 062,146 Button "<< " + OemToAnsi(STR0014) Size 29,12 Action RemAll() of oDlgPed PIXEL  //"<- Todos"
	DEFINE SBUTTON FROM 083,250  Type 1 Action (lConfirmo   := .T.,_lReturn := .T.,oDlgPed:End()) enable
	DEFINE SBUTTON FROM 083,280  Type 2 Action oDlgPed:End() enable
	Activate DIALOG oDlgPed CENTERED
	oFntBox:End()

	If lConfirmo
		Processa({|| CargaImp()},,OemToAnsi(STR0017))  //"Cargando Impuestos..."
	EndIf

EndIf

Return(_lReturn)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ AddItem  º Autor ³ Percy A Horna      º Data ³  26/11/2001 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³ Adiciona um item a lista target e remove da lista Source   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso		 ³ Espec¡fico para clientes Microsiga						  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AddItem()

Local nNewTam

If nSource != 0
	aAdd(aTarget,aSource[nSource])
	oTarget:SetItems(aTarget)
	nNewTam := Len(aSource) - 1
	aSource := aSize(aDel(aSource,nSource),nNewTam)
	If nSource   >  Len(aSource)
		nDown  := Len(aSource)
	Else
		nDown := nSource
	EndIf
	oSource:SetItems(aSource)
	If nDown  >  0
		oSource:Select(nDown)
	EndIf
	oSource:SetFocus()
EndIf

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ AddItem  º Autor ³ Percy A Horna      º Data ³  26/11/2001 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³ Remove um item da lista Target e adiciona a lista Source   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso		 ³ Espec¡fico para clientes Microsiga						  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function RemItem()

Local nNewTam

If nTarget != 0
	aAdd(aSource,aTarget[nTarget])
	aSort(aSource)
	oSource:SetItems(aSource)
	nNewTam := Len(aTarget) - 1
	aTarget := aSize(aDel(aTarget,nTarget), nNewTam)
	If nTarget   >  Len(aTarget)
		nDown  := Len(aTarget)
	Else
		nDown := nTarget
	EndIf
	oTarget:SetItems(aTarget)
	If nDown  >  0
		oTarget:Select(nDown)
	EndIf
	oTarget:SetFocus()
EndIf

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ AddAll   º Autor ³ Percy A Horna      º Data ³  26/11/2001 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³ Adiciona todos os itens da lista Source para a lista targetº±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso		 ³ Espec¡fico para clientes Microsiga						  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AddAll()

Local nB

For nB   := 1 To Len(aSource)
	Aadd(aTarget,aSource[nB])
Next

aSource  := {}
oSource:SetItems(aSource)
oTarget:SetItems(aTarget)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFun‡„o    ³ RemAll   º Autor ³ Percy A Horna      º Data ³  26/11/2001 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³ Remove todos os itens da lista target                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso		 ³ Espec¡fico para clientes Microsiga						  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function RemAll()

Local nB

For nB   := 1 To Len(aTarget)
	Aadd(aSource,aTarget[nB])
Next

aTarget  := {}
oSource:SetItems(aSource)
oTarget:SetItems(aTarget)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFun‡„o    ³ CargaImp º Autor ³ Percy A Horna      º Data ³  22/11/2001 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³ Remove um item da lista Target e adiciona na lista Source  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso		 ³ Espec¡fico para clientes Microsiga						  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function CargaImp

Local nI
ProcRegua(Len(aTarget))

aCols[n][nPosInc] := ""
For nI := 1  To Len(aTarget)
	IncProc()
	If Empty(aCols[n][nPosInc])
		aCols[n][nPosInc] := SubStr(aTarget[nI],1,3)
	Else
		If AT(SubStr(aTarget[nI],1,3),aCols[n][nPosInc]) <= 0
			aCols[n][nPosInc] := Alltrim(aCols[n][nPosInc]) + ";" +  SubStr(aTarget[nI],1,3)
		EndIf
	EndIf
Next

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A080Manut³ Autor ³ Kleber Dias Gomes     ³ Data ³17/10/2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Inclui e Altera TES                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: Alias posicionado                                   ³±±
±±³          ³ ExpN1: Numero do registro posicionado                      ³±±
±±³          ³ ExpN2: Opcao do menu selecionada                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA080                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A080Manut(cAlias,nReg,nOpc)

Local aButtons  	:= {}
Local aButtonUsr	:= {}
Local nX        	:= 0
Local lIpm    		:= AliasIndic("F09") .AND. !Empty(Alltrim(GetNewPar("MV_UFIPM",'')))
Local lAjICMS   	:= Iif(cPaisLoc = "BRA", .T., .F.)
Local lCalcImpV 	:= GetMV("MV_GERIMPV")=="S"
Local aParam		:= {{|| .T.}, {|| .T.}, {|| .T.}, {||MT080AltOk()}}	//Bloco de codigo executado apos a transacao da inclusao do preco
Local lAcessoAlt	:= VerSenha(15)
Local lAcessoExc	:= VerSenha(16)

If (lAjICMS .Or. lIpm .OR. lCalcImpV) .And. Len(aAutoCab) == 0
	Do Case
		Case nOpc==2;cCadastro := OemToAnsi(STR0039)
		Case nOpc==3;cCadastro := OemToAnsi(STR0040)
		Case nOpc==4;cCadastro := OemToAnsi(STR0041)
		Case nOpc==5;cCadastro := OemToAnsi(STR0038) 
		OtherWise 
			cCadastro := OemToAnsi(STR0006) //"Atualiza‡„o de Tes e Amarra‡”es"
	EndCase
	Set Key VK_F12 To
	Do Case
		Case nOpc==2;A081Visual(cAlias,nReg,nOpc)
		Case nOpc==3;A081Inclui(cAlias,nReg,nOpc)
		Case nOpc==4;Iif(lAcessoAlt, A081Altera(cAlias,nReg,nOpc), MsgAlert("Sem permissão de acesso a alterar, Verifique com o administrador do Sistema"))
		Case nOpc==5;Iif(lAcessoExc, A081Deleta(cAlias,nReg,nOpc), MsgAlert("Sem permissão de acesso a excluir, Verifique com o administrador do Sistema"))
	EndCase
	Return Nil
ElseIf nOpc = 5 

	If lAcessoExc
		A080Deleta(cAlias,nReg,nOpc)
	Else
		MsgAlert("Sem permissão de acesso a excluir, Verifique com o administrador do Sistema")
	EndIf

	Return Nil

EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada para incluir Botoes na ToolBar.               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( ExistBlock("MA080BUT") )
	aButtonUsr := ExecBlock("MA080BUT",.F.,.F.,{nOpc})
	If ( ValType(aButtonUsr) == "A" )
		For nX := 1 To Len(aButtonUsr)
			aadd(aButtons,aClone(aButtonUsr[nX]))
		Next nX
	EndIf
EndIf

If nOpc == 2
	AxVisual(cAlias,nReg,nOpc,,,,,aButtons)
ElseIf nOpc == 3 
	AxInclui(cAlias, nReg, nOpc, NIL, NIL, NIL, "Ma080Valid(nOpc)", NIL, "Mt080Grv", aButtons, aParam, aAutoCab)
ElseIf nOpc == 4 
	If lAcessoAlt
		AxAltera(cAlias, nReg, nOpc, NIL, NIL, NIL, NIl, "Ma080Valid(nOpc)" , "Mt080Grv", NIL, aButtons, aParam, aAutoCab)
	Else
		MsgAlert("Sem permissão de acesso a alterar, Verifique com o administrador do Sistema")
	EndIf
EndIf

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Ma080Valid ³ Autor ³ Kleber Dias Gomes    ³ Data ³ 17/10/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao.                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Mata080                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ma080Valid(nOpc)

Local lRet := .T.
Local cArqs:= ""
Local lVldFECP := .F.
Local cCRLF := chr(13)+chr(10)+chr(13)+chr(10) 
Local lCompraFut := AliasInDic("DHQ") .And. SF4->(ColumnPos("F4_EFUTUR") > 0) .And. FindFunction("A103VldTES")

If ExistTemplate("MA080VLD")
	lRet := ExecTemplate("MA080VLD",.F.,.F., { nOpc } )
EndIf

If lRet .And. ExistBlock("MA080VLD")
	lRet := ExecBlock("MA080VLD",.F.,.F., { nOpc } )
EndIf

If cPaisLoc == "BRA"
	lVldFECP := M->F4_ISEFECP # SF4->F4_ISEFECP 
EndIf

// Validacao na alteracao da TES
If lRet .And. nOpc == 4 .And. (M->F4_ESTOQUE # SF4->F4_ESTOQUE .Or. lVldFECP)  
		
	If lVldFECP  
		lRet := Aviso("Atenção",STR0035 + cCRLF + STR0036 + cCRLF + STR0037,{"Sim","Não"},3) == 1
	Else
		cArqs := a080Movim(M->F4_CODIGO,.T.)
	EndIf
	
	If !Empty(cArqs)
		MsgAlert(OemToAnsi(STR0021)+cArqs)
		lRet:=.F.	
	EndIf
EndIf

If lRet .And. (nOpc == 3 .Or. nOpc == 4 ) .And. lCompraFut 
	lRet := A103VldTES()
EndIf

Return lRet


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Marco Bianchi         ³ Data ³01/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³	  1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Local aRotina := {}

ADD OPTION aRotina TITLE STR0001  ACTION 'AxPesqui'    OPERATION 1 ACCESS 0 //"Pesquisar"
ADD OPTION aRotina TITLE STR0002  ACTION 'A080Manut'   OPERATION 2 ACCESS 0 //"Visualizar"
ADD OPTION aRotina TITLE STR0003  ACTION 'A080Manut'   OPERATION 3 ACCESS 0 //"Incluir"
ADD OPTION aRotina TITLE STR0004  ACTION 'A080Manut'   OPERATION 4 ACCESS 0 //"Alterar"
ADD OPTION aRotina TITLE STR0005  ACTION 'A080Manut'   OPERATION 5 ACCESS 0 //"Excluir"
ADD OPTION aRotina TITLE STR0022  ACTION 'A080WizFac()'  OPERATION 4 ACCESS 0 //"Facilitador"
ADD OPTION aRotina TITLE STR0043  ACTION 'COPYTES()'     OPERATION 4 ACCESS 0 //"Replicar TES"
ADD OPTION aRotina TITLE STR0044  ACTION 'A080Copy()'    OPERATION 4 ACCESS 0 //"Copia TES Selecionada"

				
				
If lHistFiscal
	ADD OPTION aRotina TITLE STR0030  ACTION 'A080Hist'    OPERATION 1 ACCESS 0 //"Histórico
EndIf

If ExistBlock("MA080MNU")
	aRotina := ExecBlock("MA080MNU",.F.,.F.,{aRotina})
EndIf

Return(aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Ma080Grv   ³ Autor ³ Eduardo Perusso Riera³ Data ³06.12.2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao.                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Mata080                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Mt080Grv()

If ExistBlock("MT080GRV")
	ExecBlock("MT080GRV",.F.,.F.)
EndIf

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³A080WizFac³ Autor ³ Gustavo G. Rueda      ³ Data ³27/11/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada da rotina que permite uma manutencao facil no      ³±±
±±³          ³ cadastro.                                                  ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³.T.                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A080WizFac()
Local	cCmps	:=	""
Local	aPar	:=	{}
Local	cMVA080FAC	:=	GetNewPar("MV_A080FAC","")

cCmps	:=	"F4_ICM/F4_IPI/F4_DUPLIC/F4_ESTOQUE/F4_CF/F4_TEXTO/F4_CFEXT/F4_BASEICM/F4_BASEIPI/F4_LFICM/F4_LFIPI/F4_DESTACA/F4_INCIDE/"
cCmps	+=	"F4_COMPL/F4_IPIFRET/F4_ISS/F4_LFISS/F4_NRLIVRO/F4_CONSUMO/F4_FORMULA/F4_AGREG/F4_INCSOL/F4_CIAP/F4_DESPIPI/F4_LIVRO/"
cCmps	+=	"F4_ATUATF/F4_TPIPI/F4_STDESC/F4_BSICMST/F4_CREDST/F4_BASEISS/F4_DESPICM/F4_SITTRIB/F4_PISCOF/F4_PISCRED/F4_BASEPIS/"
cCmps	+=	"F4_BASECOF/F4_IPILICM/F4_ICMSDIF/F4_MSBLQL/F4_TRFICM/F4_OBSICM/F4_OBSSOL/F4_PICMDIF/F4_SELO/F4_ISSST/F4_PISFISC/"
cCmps	+=	"F4_CONTSOC/F4_CTIPI/F4_CRPRST/F4_TRANFIL/F4_RETISS/F4_REGDSTA/F4_AFRMM/F4_CRDPRES/F4_TPREG/F4_DSPRDIC/F4_CRDEST/"
cCmps	+=	"F4_PISBRUT/F4_COFBRUT/F4_PISDSZF/F4_COFDSZF/F4_LFICMST/F4_AGRPIS/F4_AGRCOF/F4_BCRDPIS/F4_BCRDCOF/F4_MKPCMP/F4_FRETAUT/"
cCmps	+=	"F4_ICMSST/F4_BENSATF/F4_CRPRELE/F4_IPIPC/F4_AGRRETC/F4_PSCFST/F4_IPIOBS/F4_CFPS/F4_CRDTRAN/F4_CALCFET/F4_MKPSOL/"
cCmps	+=	"F4_COMPRED/F4_CSTPIS/F4_CSTCOF/F4_RGESPST/F4_CLFDSUL/F4_ESTCRED/"
cCmps	+=	cMVA080FAC

aAdd(aPar,{"SF4","F4_CODIGO+' - '+F4_TEXTO", cCmps,""})

MATA984(aPar[1,1],aPar[1,2],aPar[1,3],,aPar[1,4])

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³aMHead    ³ Autor ³ Gustavo G. Rueda      ³ Data ³05/12/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao para montagem do HEADER do GETDADOS                 ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³.T.                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³cAlias -> Alias da tabela base para montagem do HEADER      ³±±
±±³          ³cNCmps -> Campos que nao serao considerados no HEADER       ³±±
±±³          ³aH -> array no qual o HEADER serah montado                  ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function aMHead(cAlias,cNCmps,aH)
Local	lRet	:=	.T.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Salva a Integridade dos campos de Bancos de Dados            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(1)
dbSeek(cAlias)
While !Eof() .And. (X3_ARQUIVO==cAlias)
	IF X3USO(X3_USADO) .And. cNivel >= X3_NIVEL .and. !(AllTrim(X3_CAMPO)+"/"$cNCmps)
		AADD(aH,{ Trim(X3Titulo()), ;
			AllTrim(X3_CAMPO),;
			X3_PICTURE,;
			X3_TAMANHO,;
			X3_DECIMAL,;
			X3_VALID,;
			X3_USADO,;
			X3_TIPO,;
			X3_F3,;
			X3_CONTEXT,;
			X3_CBOX,;
			X3_RELACAO})
	Endif
	dbSkip()
Enddo
Return lRet
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³aMAcols   ³ Autor ³ Gustavo G. Rueda      ³ Data ³05/12/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao para montagem do ACOLS do GETDADOS                  ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³.T.                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³nOpc -> Opcao do AROTINA                                    ³±±
±±³          ³cAlias -> Alias da tabela base para montagem do HEADER      ³±±
±±³          ³aC -> array no qual o ACOLS serah montado                   ³±±
±±³          ³aH -> array no qual o HEADER serah montado                  ³±±
±±³          ³bCond -> Condicao de loop do while                          ³±±
±±³          ³bSkip -> Condicao para ignorar um registro isolado          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function aMAcols(nOpc,cAlias,aC,aH,bCond,bSkip)
Local	lRet	:=	.T.
Local	nI		:=	0

DEFAULT bSkip 	:= {|| .F. }

dbSelectArea(cAlias)
dbSetOrder(1)
If nOpc#3 .And. !Eof()	//nopc==3 indica INCLUSAO
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta o array aCols com os itens                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aC	:=	{}
	While !Eof() .And. Eval(bCond)
		IF Eval(bSkip)
			dbSkip()
			Loop
		EndIf
		aAdd(aC,Array(Len(aH)+1))
		For nI := 1 To Len(aH)
			aC[Len(aC),nI] := FieldGet(FieldPos(aH[nI,2]))
		Next
		aC[Len(aC),Len(aH)+1] := .F.
		dbSkip()
	End	
Else
	aC				:=	{Array(Len(aH)+1)}
	aC[1,Len(aH)+1]	:=	.F.
	For nI := 1 To Len(aH)
		If aH[nI,10]#"V"
			aC[1,nI]	:=	CriaVar(aH[nI,2])
		EndIf

		If "_SEQ"$aH[nI,2]
			aC[1,nI]	:=	StrZero(1,aH[nI,4])
		EndIf
	Next
EndIf
Return lRet
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³AjusteLOK ³ Autor ³ Gustavo G. Rueda      ³ Data ³05/12/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao de validacao da linha do GETDADOS                   ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³.T.                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AjusteLOK
Local	lRet		:=	.T.
Local	aColsNew	:=	{}
Local	aHeadNew	:=	{}
Local	nTamChv		:=	0
Local	nTamCLanAp	:=	0
Local	cAjuste		:=	""
Local	nPos		:=	0
Local	nPosCLanAp	:=	0
Local	nI			:=	0
Local	nTamIPI		:=	0
Local	nPosIPI		:=	0
Local	nPosRefl	:=	0


If cPaisloc <> "BRA"
	Return lRet
EndIf

aColsNew	:=	aClone(oNewGetDad:aCols)
aHeadNew	:=	aClone(oNewGetDad:aHeader)
nTamChv		:=	TamSx3("CC6_CODAJU")[1]
nTamCLanAp	:=	TamSx3("CDO_CODAJU")[1]
nTamIPI		:=	TamSx3("CCK_CODAJU")[1]
nPos		:=	aScan(aHeadNew,{|aX| aX[2]=="CC7_CODLAN"})
nPosCLanAp	:=	aScan(aHeadNew,{|aX| aX[2]=="CC7_CLANAP"})
nPosIPI		:=	aScan(aHeadNew,{|aX| aX[2]=="CC7_CODIPI"})
nPosIFCOMP	:=	aScan(aHeadNew,{|aX| aX[2]=="CC7_IFCOMP"})

nPosRefl	:=	aScan(aHeadNew,{|aX| aX[2]=="CC7_CODREF"})
If aColsNew[oNewGetDad:nAt,Len(aColsNew[oNewGetDad:nAt])]
	nPos	:=	0
EndIf

If nPos > 0
	
	If !Empty( cAjuste		:=	Right(aColsNew[oNewGetDad:nAt,nPos],nTamChv) )
	
		For nI := 1 To Len(aColsNew)
			If nI#oNewGetDad:nAt .And. !aColsNew[nI,Len(aColsNew[nI])] .And. Right(aColsNew[nI,nPos],nTamChv)==cAjuste 
				If aColsNew[oNewGetDad:nAt,nPos] == aColsNew[nI,nPos] 
					If Substring(aColsNew[oNewGetDad:nAt,nPos],3,1) == '9'
						If aColsNew[oNewGetDad:nAt,nPosRefl] == aColsNew[nI,nPosRefl]
							lRet	:=	.F.
							Exit	
						Endif
					Else
						if aColsNew[oNewGetDad:nAt,nPosIFCOMP] == aColsNew[nI,nPosIFCOMP]
							lRet	:=	.F.
							Exit
						Endif
					Endif
				Endif	
			EndIf
		Next nI
		
		If !lRet
			Alert(STR0025+cAjuste+STR0026)	//"Codigo de ajuste ["###"] não pode ser repetido em um mesmo documento fiscal. Considera-se como cÓdigo de ajuste os ultimos 6 dígitos do código do lançamento fiscal do documento."
		Else
			If lRet .And. ExistBlock("MA080VLD")
				lRet := ExecBlock("MA080VLD",.F.,.F., { nPos } )
			EndIf		
		EndIf
	Endif
EndIf

If nPosCLanAp > 0
	
	If !Empty( cAjuste		:=	Right(aColsNew[oNewGetDad:nAt,nPosCLanAp],nTamCLanAp) )
	
		For nI := 1 To Len(aColsNew)
			If nI#oNewGetDad:nAt .And. !aColsNew[nI,Len(aColsNew[nI])] .And. Right(aColsNew[nI,nPosCLanAp],nTamCLanAp)==cAjuste 
				If aColsNew[oNewGetDad:nAt,nPosCLanAp] == aColsNew[nI,nPosCLanAp]
					lRet	:=	.F.
					Exit
				Endif	
			EndIf
		Next nI
		
		If !lRet
			Alert(STR0025+cAjuste+STR0026)	//"Codigo de ajuste ["###"] não pode ser repetido em um mesmo documento fiscal. Considera-se como cÓdigo de ajuste os ultimos 6 dígitos do código do lançamento fiscal do documento."
		Else
			If lRet .And. ExistBlock("MA080VLD")
				lRet := ExecBlock("MA080VLD",.F.,.F., { nPosCLanAp } )
			EndIf		
		EndIf
	Endif
EndIf

If nPosIPI > 0
	
	If !Empty( cAjuste		:=	Right(aColsNew[oNewGetDad:nAt,nPosIPI],nTamIPI) )
	
		For nI := 1 To Len(aColsNew)
			If nI#oNewGetDad:nAt .And. !aColsNew[nI,Len(aColsNew[nI])] .And. Right(aColsNew[nI,nPosIPI],nTamIPI)==cAjuste 
				If aColsNew[oNewGetDad:nAt,nPosIPI] == aColsNew[nI,nPosIPI]
					lRet	:=	.F.
					Exit
				Endif	
			EndIf
		Next nI
		
		If !lRet
			Alert(STR0025+cAjuste+STR0042)	//"Codigo de ajuste ["###"] não pode ser repetido no mesmo documento fiscal."
		Else
			If lRet .And. ExistBlock("MA080VLD")
				lRet := ExecBlock("MA080VLD",.F.,.F., { nPosIPI } )
			EndIf		
		EndIf
	Endif
EndIf


Return lRet
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³GrvAjuste ³ Autor ³ Gustavo G. Rueda      ³ Data ³05/12/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao de gravacao das informacoes do acols do GETDADOS na ³±±
±±³          ³  tabela CC7.                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³.T.                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function GrvAjuste(lAltera)
Local	lRet		:=	.T.
Local	aColsNew	:={}	//aClone(oNewGetDad:aCols)
Local	aHeadNew	:={}//	aClone(oNewGetDad:aHeader)
Local	nPosSeq	:=	0
Local	nPosCA		:=	0
Local	nPosDR		:=	0
Local	nPosIFCOMP	:=	0
Local	nPosDCCOMP	:=	0
Local	nPosClan	:=	0
Local	nPosCodRef	:=	0
Local	nI			:=	0
Local	lAchouCC7	:=	.F.
Local   lCC7Guia	:= CC7->(FieldPos("CC7_GUIA"))>0
Local	lCC7Clanc	:= CC7->(FieldPos("CC7_CLANC"))>0
Local   lCC7IPI		:= CC7->(FieldPos("CC7_CODIPI"))>0
Local	nPosGuia	:=	0
Local	nPosIPI		:=	0

Default	lAltera	:=	.F.

dbSelectArea("CC7")
dbSetOrder(1)

If cPaislOc=="BRA"
	aColsNew	:=	aClone(oNewGetDad:aCols)
	aHeadNew	:=	aClone(oNewGetDad:aHeader)
EndIf

If lAltera
	CC7->(dbSeek(xFilial("CC7")+SF4->F4_CODIGO))
	While !CC7->(Eof()) .And. CC7->(CC7_FILIAL+CC7_TES)==xFilial("CC7")+SF4->F4_CODIGO
		RecLock("CC7",.F.)
		dbDelete()
		MsUnLock()
		CC7->(FKCommit())
		CC7->(dbSkip())
	End
EndIf

nPosSeq	:=	aScan(aHeadNew,{|aX| aX[2]=="CC7_SEQ"})
nPosCA		:=	aScan(aHeadNew,{|aX| aX[2]=="CC7_CODLAN"})
nPosDR		:=	aScan(aHeadNew,{|aX| aX[2]=="CC7_DESCR"})
nPosIFCOMP	:=	aScan(aHeadNew,{|aX| aX[2]=="CC7_IFCOMP"})
nPosClan	:=	aScan(aHeadNew,{|aX| aX[2]=="CC7_CLANAP"})
nPosCodRef	:=	aScan(aHeadNew,{|aX| aX[2]=="CC7_CODREF"})
nPosDCCOMP	:=	aScan(aHeadNew,{|aX| aX[2]=="CC7_CLANC"})
nPosGuia	:=	aScan(aHeadNew,{|aX| aX[2]=="CC7_GUIA"})
nPosIPI		:=	aScan(aHeadNew,{|aX| aX[2]=="CC7_CODIPI"})

For nI := 1 To Len(aColsNew)
	If IIf(nPosCA > 0, !Empty(aColsNew[nI,nPosCA]) ,.F. ) .Or.  IIf(nPosClan > 0, !Empty(aColsNew[nI,nPosClan]) , .F. )  .OR. IIf(nPosIPI > 0, !Empty(aColsNew[nI,nPosIPI]) , .F. )  

		If aColsNew[nI,Len(aColsNew[nI])]
			Loop
		Else
			RecLock("CC7",.T.)
			CC7->CC7_FILIAL	:=	xFilial("CC7")
			CC7->CC7_TES	:=	SF4->F4_CODIGO
			CC7->CC7_SEQ	:=	aColsNew[nI,nPosSeq]
			CC7->CC7_CODLAN	:=	aColsNew[nI,nPosCA]
			CC7->CC7_DESCR	:=	aColsNew[nI,nPosDR]
			CC7->CC7_IFCOMP	:=	aColsNew[nI,nPosIFCOMP]
			If nPosClan > 0
				CC7->CC7_CLANAP	:= aColsNew[nI,nPosClan]
			EndIf
			If nPosCodRef > 0
				CC7->CC7_CODREF	:= aColsNew[nI,nPosCodRef]
			EndIf
			If lCC7Clanc .And. nPosDCCOMP > 0
				CC7->CC7_CLANC	:= aColsNew[nI,nPosDCCOMP]
			EndIf
			If lCC7Guia
				CC7->CC7_GUIA	:= aColsNew[nI,nPosGuia]
			EndIf
			If lCC7IPI .And. nPosIPI > 0
				CC7->CC7_CODIPI	:= aColsNew[nI,nPosIPI]
			EndIf

			MsUnLock()
			CC7->(FKCommit())			
		EndIf
	EndIf
Next nI

Return lRet
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³CodCC6    ³ Autor ³ Gustavo G. Rueda      ³ Data ³05/12/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao utilizada na validacao de alguns campos da tabela CC7³±±
±±³          ³ para montar o ultimo campo (CC7_CODLAN) com a chave princi-³±±
±±³          ³ pal da tabela.                                             ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³.T.                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function CodCC6
Local	lRet	:=	.T.
Local	cUf		:=	IIF (!Empty(M->CC6_STUF) , M->CC6_STUF , GetMv("MV_ESTADO") )

M->CC6_CODLAN	:=	cUf+M->CC6_REFLEX+M->CC6_TPAPUR+M->CC6_RESPON+M->CC6_INFLUE+M->CC6_ORIGEM+M->CC6_CODAJU

Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³CodCDO    ³ Autor ³ Microsiga S.A         ³ Data ³05/12/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Incrementa o codigo de ajuste CDO                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³.T.                                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function CodCDO

M->CDO_CODAJU:=	M->CDO_UF+M->CDO_TPAPUR+M->CDO_UTILI+M->CDO_SEQUEN

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MATA080   ºAutor  ³Mary C. Hergert     º Data ³  07/08/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Ajusta os cadastros dos impostos de Portugal                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SigaFis                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AjustaPTG()

Local aRecnoSFF		:= {}             
Local aRecSFFEIC	:= {}             
Local aTipoIVA		:= {}
  
Local cFilSFF		:= xFilial("SFF") 

Local lIntEIC 		:= (SuperGetMV("MV_EASY") == "S")

Local nX			:= 0

CriaSFB(.T.)
CriaSF4(.T.)
CriaSFC(.T.)
CriaSFF(.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Altera os títulos da tabela SFF - PLano IVA³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SFFPTG(aRecnoSFF,cFilSFF,lIntEIC,aRecSFFEIC,@aTipoIVA)
SFF->(dbSetOrder(14))

For nX := 1 to Len(aRecnoSFF)
	If SFF->(dbSeek(xFilial("SFF")+aRecnoSFF[nX][21]+aRecnoSFF[nX][28]+aRecnoSFF[nX][29]))
		If Len(aTipoIVA)>0 .And. (!(aTipoIVA[1] $ SFF->FF_CONCEPT) .And. !(aTipoIVA[2] $ SFF->FF_CONCEPT))
			RecLock("SFF",.F.)
			SFF->FF_CONCEPT := aRecnoSFF[nX][13]
			MsUnLock()
		Endif
	Endif	
Next

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MT080AltOk³ Autor ³ Nunzio Autorino Junior³ Data ³ 31/03/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Responsavel em enviar os dados de cadastro de Tipos de     ³±±
±±³			 ³ entrada e saida    										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA080                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MT080AltOk()

	Local cTipo 		:= ""				   							//Como os dados serao integrados no processo offline    
	Local oProcessOff 	:= Nil				   							//Objeto do tipo LJCProcessoOffLine
	Local lAmbOffLn 	:= SuperGetMv("MV_LJOFFLN", Nil, .F.)			//Identifica se o ambiente esta operando em offline
	
	//Verifica se o ambiente esta em off-line
	If lAmbOffLn
		//Instancia o objeto LJCProcessoOffLine
		oProcessOff := LJCProcessoOffLine():New("014")
		
		//Determina o tipo de operacao 
		If INCLUI
			cTipo := "INSERT"
		ElseIf ALTERA
			cTipo := "UPDATE"
		Else
			cTipo := "DELETE"
			
			//Considera os registros deletados
			SET DELETED OFF
		EndIf
			    
		If !Empty(cTipo)
			//Insere os dados do processo (registro da tabela)
			oProcessOff:Inserir("SF4", xFilial("SF4") + SF4->F4_CODIGO, 1, cTipo)	
		
			//Processa os dados 
			oProcessOff:Processar()	
		EndIf
		
		//Desconsidera os registros deletados
		SET DELETED ON
	EndIf
	
Return Nil
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ ma080IncTrn  ºAutor  ³Paulo Benedet         º Data ³ 25/09/08 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Inserir registro e atualizar tabela de sincronizacao          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ Nao ha                                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ Nao ha                                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºAplicacao ³ Funcao chamada pelo XB_CONTEM do tipo 03 da consulta padrao   º±±
±±º          ³ do cadastro de TES                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ma080IncTrn()
Local aParam := {{|| .T.}, {|| .T.}, {|| .T.}, {|| mt080AltOk()}} //Bloco de codigo executado apos a transacao da inclusao do cliente
Local cAlias := "SF4" // Alias em uso
Local nReg   := SF4->(RecNo()) // Numero do registro
Local nOpc   := 3 // Inclusao

axInclui(cAlias,nReg,nOpc,/*aAcho*/,/*cFunc*/,/*aCpos*/,".T.",.T.,/*cTransact*/,/*aButtons*/,aParam,/*aRotAuto*/,.F.)

Return


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ 	 A080ALMOV  º Autor ³    Bruno Schmidt     º Data ³ 17/10/12 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Quando Alterar o campo F4_ESTOQUE , para concluir essa 	 	 º±±
±±º			 ³alteracao o sistema verificar se possuir registros SBF e SB8   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ Nao ha                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
FUNCTION A080ALMOV(cTES)
Local cString := ""
Local cCampo  := ReadVar()
Local lRet    :=.T.

If !(FWIsInCallStack("A080COPY")) .And. !(FWIsInCallStack("A081Inclui"))
	If cCampo == "M->F4_ESTOQUE"
		If M->F4_ESTOQUE <> SF4->F4_ESTOQUE
			cString := A080Movim(cTES,.F.)
			If !Empty(cString) 
				HELP(" ",1,"F4_ESTOQUE2",,cString,4,1)
				lRet := .F.
			EndIf
		EndIf
	ElseIf cPaisLoc == "BRA" .And. cCampo == "M->F4_ISEFECP"
		If M->F4_ISEFECP <> SF4->F4_ISEFECP
			cString := A080Movim(cTES,.F.)
			If !Empty(cString)
				HELP(" ",1,"F4_ISEFECP2",,cString,4,1)
				lRet := .F.
			EndIf
		EndIf
	EndIf
EndIf

Return lRet
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ A080Hist     ºAutor  ³Wemerson Randolfo     º Data ³ 03/07/12 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Visualizacao do historico das alteracoes                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ Nao ha                                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ .T. ou .F.                                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºAplicacao ³ Funcao chamada pelo menu                                      º±±
±±º          ³                                                               º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A080Hist
Local lRet

lRet := HistOperFis("SS0",SF4->F4_CODIGO,SF4->F4_TEXTO,"S0_CODIGO")
Return lRet   


//-------------------------------------------------------------------
/*/{Protheus.doc} IpmLOK()
Função que faz validação das linhas da aba de IPM. Não poderá ter
mais de um código para o mesmo Estado. 
			 	
@author Erick G. Dias
@since 08/10/15
@version 11.80
/*/
//-------------------------------------------------------------------
Function IpmLOK()
Local	lRet		:=	.T.
Local	aColsNew	:=	aClone(oGetDadIpm:aCols)
Local	aHeadNew	:=	aClone(oGetDadIpm:aHeader)
Local	nTamUf		:=	TamSx3("F09_UF")[1]
Local	nTamCod	:=	TamSx3("F09_CODIPM")[1]
Local	cUf			:=	""
Local	cCodIpm	:=	""
Local	nPosUf		:=	aScan(aHeadNew,{|aX| aX[2]=="F09_UF"})
Local	nPos		:=	aScan(aHeadNew,{|aX| aX[2]=="F09_CODIPM"})
Local	nI			:=	0

If aColsNew[oGetDadIpm:nAt,Len(aColsNew[oGetDadIpm:nAt])]
	nPos	:=	0
EndIf

If nPos > 0
	cUf	:= 	Right(aColsNew[oGetDadIpm:nAt,nPosUf],nTamUf) 
	cCodIpm	:= 	Right(aColsNew[oGetDadIpm:nAt,nPos],nTamCod)
	If !Empty(cUf+cCodIpm)
	
		For nI := 1 To Len(aColsNew)
			If nI#oGetDadIpm:nAt .And. !aColsNew[nI,Len(aColsNew[nI])] .And. Right(aColsNew[nI,nPosUf],nTamUf)==cUf
				If aColsNew[oGetDadIpm:nAt,nPosUf] == aColsNew[nI,nPosUf]
					lRet	:=	.F.
					Exit
				Endif	
			EndIf
		Next nI
		
		If !lRet
			Alert('Já existe um código cadastrado para a UF :'+cUf)	//"Codigo de ajuste ["###"] não pode ser repetido em um mesmo documento fiscal. Considera-se como cÓdigo de ajuste os ultimos 6 dígitos do código do lançamento fiscal do documento."
		Else
			If lRet .And. ExistBlock("MA080VLD")
				lRet := ExecBlock("MA080VLD",.F.,.F., { nPos } )
			EndIf		
		EndIf
	Endif
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} GrvIpm()
Função que fará gravação das informações do IPM na tabela F09
			 	
@author Erick G. Dias
@since 08/10/15
@version 11.80
/*/
//-------------------------------------------------------------------
Static Function GrvIpm(lAltera)
Local	lRet		:=	.T.
Local	aColsNew	:=	aClone(oGetDadIpm:aCols)
Local	aHeadNew	:=	aClone(oGetDadIpm:aHeader)
Local	nPosUF		:=	aScan(aHeadNew,{|aX| aX[2]=="F09_UF"})
Local	nPosCod	:=	aScan(aHeadNew,{|aX| aX[2]=="F09_CODIPM"})
Local	nPosDesc	:=	aScan(aHeadNew,{|aX| aX[2]=="F09_DSCIPM"})
Local	nI			:=	0
Local	lAchouF09	:=	.F.

Default	lAltera	:=	.F.

dbSelectArea("F09")
dbSetOrder(1)

If lAltera
	F09->(dbSeek(xFilial("F09")+SF4->F4_CODIGO))
	While !F09->(Eof()) .And. F09->(F09_FILIAL+F09_TES)==xFilial("F09")+SF4->F4_CODIGO
		RecLock("F09",.F.)
		dbDelete()
		MsUnLock()
		F09->(FKCommit())
		F09->(dbSkip())
	End
EndIf

For nI := 1 To Len(aColsNew)	 
	If !Empty(aColsNew[nI,nPosCod]) 

		If aColsNew[nI,Len(aColsNew[nI])]
			Loop
		Else
			RecLock("F09",.T.)
			F09->F09_FILIAL	:=	xFilial("F09")
			F09->F09_TES		:=	SF4->F4_CODIGO
			F09->F09_UF		:=	aColsNew[nI,nPosUF]
			F09->F09_CODIPM	:=	aColsNew[nI,nPosCod]
			F09->F09_DSCIPM	:=	aColsNew[nI,nPosDesc]
			
			MsUnLock()
			F09->(FKCommit())			
		EndIf
	EndIf
Next nI
Return lRet


//-------------------------------------------------------------------
/*/ {Protheus.doc} cBoxMotICM
Cria opcoes para o campo F4_MOTICMS (X3_CBOX) 

@author Natalia Sartori
@since 04/07/2014
@version 1.0
/*/
//-------------------------------------------------------------------

Function cBoxMotICM

Local nI	:= 0
Local cRet	:= ""
Local aOpc	:= {}

AADD(aOpc,"1=Táxi")
AADD(aOpc,"2=Deficiente Físico")
AADD(aOpc,"3=Produtor Agropecuário") 
AADD(aOpc,"4=Frotista/Locadora")
AADD(aOpc,"5=Diplomático/Consular") 
AADD(aOpc,"6=Utilitários e Motocicletas")
AADD(aOpc,"7=SUFRAMA")
AADD(aOpc,"8=Venda a Orgãos Públicos")
AADD(aOpc,"9=Outros.(NT2011/004)")
AADD(aOpc,"10=Deficiente Condutor")
AADD(aOpc,"11=Deficiente não Condutor")
AADD(aOpc,"12=Orgão fomento/desenvolvimento agrário")
AADD(aOpc,"16=Olimpíadas Rio 2016")
AADD(aOpc,"90=Solicitado pelo fisco")

For nI:=1 To Len(aOpc)
	cRet += IIf(empty(cRet),'',';') + aOpc[nI]
Next

Return (cRet)

/*/ {Protheus.doc} cBoxCrdAcu
	(Cria opcoes para o campo F2P_CREDAC (X3_CBOX))

	@type Function
	@author Flavio Luiz Vicco
	@since 09/07/2018
	
	@param l_RetArray, logico, se retorna como array ou caracter para o campo X3_CBOX

	@return xRetorno, caracter ou array, retorna o combobox do campo F4_CREDACU ou do campo F2P_CREDAC

	@obs #vitor01
	/*/
Function cBoxCrdAcu(l_RetArray)

	Local nCount := 0

	Local xRetorno := Nil

	Local aCBox := {}

	Default l_RetArray := .F.

	Aadd(aCBox,{"1=Exportação Direta de Mercadorias/Serviços","Decorrente de Exportações Diretas"})
	Aadd(aCBox,{"2=Outra Hipótese","Decorrente de Outros Motivos"})
	Aadd(aCBox,{"3=Não se aplica",""})
	Aadd(aCBox,{"4=Exportação Indireta","Decorrente de Exportações Indiretas"})
	Aadd(aCBox,{"5=Isenção","Decorrente de Isenção"})
	Aadd(aCBox,{"6=Diferimento","Decorrente de Diferimento"})
	Aadd(aCBox,{"7=Redução de Base de Cálculo","Decorrente de Redução de Base de Cálculo"})

	// Ordena o array
	ASort(aCBox,,,{|x,y| x[1] < y[1] })

	// Se o retorno for array
	If l_RetArray
		// Incluiu o array para a variavel do retorno
		xRetorno := aClone(aCBox)
	Else	// Se não
		// Variavel será caracter
		xRetorno := ""

		// Retorna todo o array como caracter
		AEval(aCBox,{|x| xRetorno += IIf(Empty(xRetorno),'',';') + x[1] })
	EndIf

Return xRetorno

/*/ {Protheus.doc} cBoxISSST(nOpc)
	(Cria opcoes para o campo F4_ISSST (X3_CBOX))

	@type Function
	@author Bruce Egnor
	@Data 24/08/18	
	@param l_RetArray, logico, se retorna como array ou caracter para o campo X3_CBOX

	@return xRetorno, caracter ou array, retorna o combobox do campo F4_ISSST

	
	/*/

Function cBoxISSST(nOpc)
Local nI   := 1
Local cRet := ""
Local aOpc := {}
 
AADD(aOpc,"1=Dentro Municipio")
AADD(aOpc,"2=Fora Municipio")
AADD(aOpc,"3=Isenção")
AADD(aOpc,"4=Imune")
AADD(aOpc,"5=Exigibilidade Susp. Judicial")
AADD(aOpc,"6=Exigibilidade Susp. Proc. Adm")
AADD(aOpc,"7=Não tributável")
AADD(aOpc,"8=Micro Empreendedor Individual MEI")
AADD(aOpc,"9=Isenção Parcial")
AADD(aOpc,"A=Imunidade Objetiva")

For nI:=1 To Len(aOpc)
	If nOpc==0
		cRet += IIf(empty(cRet),' ',';') + Left(aOpc[nI],2)
	Else
		cRet += IIf(empty(cRet),'',';') + aOpc[nI]
	Endif
Next

If nOpc==0
	cRet := &("Pertence('"+cRet+"')")
Endif

Return (cRet)

//---------------------------------------------------------------------------------------------------
/*/{Protheus.doc} CopySF4

@author Erick G. Dias
@since 04/05/2017
@version 12.1.7
@param cAliasQry, Caracter, Alias da Query executada na SF4, com TES selecionados para realizar a cópia
@description Função que irá realizar a cópia da SF4 para demais filiais 

/*/
//----------------------------------------------------------------------------------------------------
Static Function CopySF4(cAliasQry,lMark)
Local aEstrut	:=SF4->(dbStruct()) //Monta estrutura da SF4
Local aAreaSM0  := SM0->(GetArea())
Local aSm0		:= {}
Local cTabela	:= 'SF4'
Local cFilCad	:= cFilAnt
Local cEmpCad	:= cEmpAnt
Local nCont		:= 0
Local lProc		:= .T.
Local oTmpLog	:= CriaTxtLog()
Local lRet		:= .T.
Local aCmpLog	:= {}
Local aIndex	:= {}

Default lMark := .F.

If lMark	
	aSm0 	:= MatFilCalc( .T.,,,,,,.T. ) //Seleciona Filial
	IndRegua (cAliasQry,cAliasQry,"F4_CODIGO",,"MARK=='X'")	// Filtra somente registros marcados
Else	
	aSm0 	:= MatFilCalc( .F.,,,,,,.T. )
Endif

//Inicia transação
Begin Transaction
		
//Laço nas filiais da empresa logada
For nCont := 1 to Len(aSm0)
	
	If lMark
		//Processa somente Filiais Marcadas
		lProc := aSm0[nCont][1]
	Else
		//Processa Todas Filiais
		lProc := .T.
	Endif
	
	If lProc
		SM0->(DbGoTop ())
		SM0->(MsSeek (cEmpCad+aSm0[nCont][2], .T.))	//Pego a filial mais proxima
		cFilAnt := FWGETCODFILIAL		
		
		//A filial logada é desconsiderada
		IF cFilAnt <> cFilCad
		
			//Laço de TES que serão copiados para as filiais			
			(cAliasQry)->(DbGoTop ())			
						
			Do While !(cAliasQry)->(Eof ())
				
				//Verifica se o TES já existe na filial de destino, somente irá incluir se não existir					
				IF !SF4->(MsSeek(xFilial("SF4")+ (cAliasQry)->F4_CODIGO))	
					//Função que realizar cópia para filial de destino
					If ExistBlock("MA080VLD")
						lRet := ExecBlock("MA080VLD",.F.,.F., { 3 } )
					EndIf
					If lRet
						GrvTab(aEstrut, cAliasQry, cTabela, oTmpLog,lMark)				
					EndIf
				Else
					If lCanUseBulk
						oTmpLog:AddData({xFilial("SF4"),"NAO CADASTRADO",FWCompany(),FWUnitBusiness(),(cAliasQry)->F4_CODIGO})
					EndIf	
				EndiF
						
				(cAliasQry)->(dbSkip())
				Loop
			EndDo				
		EndIF
	EndIF
	
Next nCont

If lCanUseBulk
	oTmpLog:Close()
    oTmpLog:Destroy()
    oTmpLog := nil
EndIf


//Fecha alias e restaura filial logada
IF lMark
	//Limpa Objeto e Arquivo Temporario
	If !Empty(cAliasQry)    
		Ferase(cAliasQry+GetDBExtension())
		Ferase(cAliasQry+OrdBagExt())
		cAliasQry := ""
	Endif	
Else
	(cAliasQry)->(DbCloseArea ())
Endif

RestArea (aAreaSM0)
cFilAnt := FWGETCODFILIAL

//Fecha transação 		
End Transaction

aAdd(aCmpLog,{"FILIAL",		"Filial",			"C",	TamSx3("F4_FILIAL")[1],	"@!","LEFT",'{|| .T.'})
aAdd(aCmpLog,{"STATUS",		"Status",			"C",	20,						"@!","LEFT",'{|| .T.'})
aAdd(aCmpLog,{"EMPRESA",	"Empresa",			"C",	15,						"@!","LEFT",'{|| .T.'})
aAdd(aCmpLog,{"UNIDNEG",	"Unid. Negocio",	"C",	15,						"@!","LEFT",'{|| .T.'})
aAdd(aCmpLog,{"TES",		"Tp. Ent. Sai.",	"C",	TamSx3("F4_CODIGO")[1],	"@!","LEFT",'{|| .T.'})

Aadd( aIndex, "FILIAL+STATUS+EMPRESA+UNIDNEG+TES" )

FisTelLog(cAliasTemp, aCmpLog, aIndex)
Return .T.

//---------------------------------------------------------------------------------------------------
/*/{Protheus.doc} GrvTab
 
@author Erick G. Dias
@since 04/05/2017
@version 12.1.7
@param aEstrut, Array, Estrutura da tabela que será copiada
@param cAliasQry, Caracter, Alias da Query executada da tabela que será copiada
@param cTabela, Caracter, Nome da tabela que será copiada
@description Função que irá realizar a cópia da SF4 para demais filiais 

/*/
//----------------------------------------------------------------------------------------------------
Static Function GrvTab(aEstrut, cAliasQry, cTabela, oTmpLog,lMark)

Local nCont	:= 0
Local cLinha	:= ''

//Inclui nova linha na tabela
RecLock(cTabela,.T.)

//Gravação do campo _FILIAL será sempre com retorno do xFilial()
&(cTabela + "->" + Iif(Substr(cTabela,1,1) == 'S', Substr(cTabela,2,2) ,cTabela )   + "_FILIAL" ) := xFilial(cTabela)

//Laço nos campos da tabela estrutura do SX3
For nCont := 1 to Len(aEstrut)

	//Ignora campo de Marcação
	If lMark .And. aEstrut[nCont][1] =='MARK'
		Loop
	Endif
	
	If aEstrut[nCont][2] <> 'M'
	
		//Para campo tipo Date preciso utilizar a função TcSetField para que não ocorra erro de Type Mismatch
		If aEstrut[nCont][2] == 'D'		
			TcSetField(cAliasQry,aEstrut[nCont][1],"D",8,0)
		EndiF
		
		//Campo Filial não será copiado, já foi gravado anteriormente com conteúdo do xFilial
		If !( Alltrim(aEstrut[nCont][1]) $ "F4_FILIAL/F4_USERLGI/F4_USERLGA" )
			&(cTabela + "->" +aEstrut[nCont][1] )	:= 	(cAliasQry)->&(aEstrut[nCont][1])	
		EndIF
		
	EndIf
	
Next nCont

MsUnLock()

cLinha	:= 'TES replicada para Grupo de Empresa: ' + FWGrpCompany()
cLinha += Iif(FWModeAccess("SF4",1) == 'C' .OR. Empty(Alltrim(FWCompany())) ,''    , ', Empresa: ' + FWCompany()  )
cLinha += Iif(FWModeAccess("SF4",2) == 'C' .OR. Empty(Alltrim(FWUnitBusiness())) ,''  , ', Unidade de Negócio: ' + FWUnitBusiness()  )
cLinha += Iif(FWModeAccess("SF4",3) == 'C' .OR. Empty(Alltrim(FWFilial())),''  , ' e Filial ' + FWFilial()  )
cLinha	+=' : Código do TES = '			+ (cAliasQry)->F4_CODIGO 
cLinha	+=', Descrição = ' 				+ (cAliasQry)->F4_TEXTO + ' '
cLinha	+= Chr (13)+Chr (10)


If lCanUseBulk
	oTmpLog:AddData({xFilial("SF4"),"SUCESSO",FWCompany(),FWUnitBusiness(),(cAliasQry)->F4_CODIGO})
EndIf	

Return

//---------------------------------------------------------------------------------------------------
/*/{Protheus.doc} QrySF4
@author Erick G. Dias
@since 04/05/2017
@version 12.1.7
@param cCodSF4, Caracter, Estrutura da tabela que será copiada
@return	cAliasSF4, Caracter, Query realizada na SF4
@description Função que faz seleção do código de TES que deverá ser copiado 

/*/
//----------------------------------------------------------------------------------------------------
Static Function QrySF4(cCodSF4)

Local cAliasSF4	:= GetNextAlias()
Local cSlctSF4	:= ''
Local aStructTrb 	:= SF4->(DbStruct())
Local nX:=0
Local aCpos		:= {}

//Posiciona os campos do tipo Memo no final da tabela para não ocasionar erro na gravação do campo Memo
For nX:=1 To Len(aStructTrb)
	If aStructTrb[nX][2] <> "M"
		AAdd(aCpos,aStructTrb[nX])
	EndIf
Next nX	

aStructTrb := aClone(aCpos)

cSlctSF4:='%'

For nX:=1 To Len(aStructTrb) 
	cSlctSF4 += aStructTrb[nX,1]+", "
Next nX

//Query SF4
cSlctSF4 += " ' ' AS MARK "
cSlctSF4 +='%'
   	
   	
BeginSql Alias cAliasSF4	
	SELECT			    
		%Exp:cSlctSF4%
	FROM 
		%Table:SF4% SF4
	WHERE
		SF4.F4_FILIAL=%xFilial:SF4% AND		
		SF4.F4_CODIGO=%Exp:cCodSF4% and
		SF4.%NotDel%	
EndSql


Return cAliasSF4

//---------------------------------------------------------------------------------------------------
/*/{Protheus.doc} VldReplica
@author Erick G. Dias
@since 04/05/2017
@version 12.1.7
@description Função que faz validação se  tes  já existe em alguma filial 

/*/
//----------------------------------------------------------------------------------------------------
Static Function VldReplica(cCodTEs)

Local aAreaSM0  := SM0->(GetArea())
Local aSm0		:= MatFilCalc( .F.,,,,,,.T. )
Local nCont		:= 0
Local cMsg		:= ''
Local cEmpCad	:= cEmpAnt
Local cFilCad	:= cFilAnt

For nCont := 1 to Len(aSm0)

	SM0->(DbGoTop ())
	SM0->(MsSeek (cEmpCad+aSm0[nCont][2], .T.))	//Pego a filial mais proxima
	cFilAnt := FWGETCODFILIAL	
	
	//A filial logada é desconsiderada
	IF cFilAnt <> cFilCad
				
		//Verifica se o TES já existe na filial de destino, somente irá incluir se não existir
		IF  SF4->(MsSeek(xFilial("SF4")+ cCodTEs))	
			cMsg	+= 'TES  já existe na filial: ' + cFilAnt 	+ chr(13)+chr(10)
		EndiF
		
	EndIF

Next nCont

RestArea (aAreaSM0)
cFilAnt := FWGETCODFILIAL

Return cMsg

//---------------------------------------------------------------------------------------------------
/*/{Protheus.doc} CriaTxtLog
@author Erick G. Dias
@since 04/05/2017
@version 12.1.7
@description Função responsável por criar diretório e arquivo de log.	  

/*/
//----------------------------------------------------------------------------------------------------
Static Function CriaTxtLog()

Local aCampos		:= {}
Local oTmpLog
Public cAliasTemp 	:= ""
Public lCanUseBulk 	:= .F.
Public oTempTab

AADD(aCampos,{"FILIAL" 		,"C",TamSX3("F4_FILIAL")[1] ,0})
AADD(aCampos,{"STATUS"	 	,"C",20 					,0})
AADD(aCampos,{"EMPRESA" 	,"C",15 					,0})
AADD(aCampos,{"UNIDNEG" 	,"C",15 					,0})
AADD(aCampos,{"TES" 		,"C",TamSX3("F4_CODIGO")[1]	,0})

oTempTab := FwTemporaryTable():New("TRB")
oTempTab:SetFields(aCampos)
oTempTab:AddIndex("01", {"FILIAL", "STATUS", "EMPRESA","UNIDNEG","TES"} )

oTempTab:Create()

__cTblName := UPPER(oTempTab:GetRealName())

lCanUseBulk := FwBulk():CanBulk()

if lCanUseBulk
	cAliasTemp := __cTblName
	//- AJUSTA NOME DA TABELA 
	If 'MSSQL' $ cDbType
		cAliasTemp := oTempTab:cTableName
	EndIf 
	//- cria o objeto de FWBulk
	If oTmpLog == nil 
		oTmpLog := FwBulk():New(cAliasTemp)
		if lCanUseBulk
			oTmpLog:SetFields(aCampos)
		endif
	EndIF 
endif	


Return oTmpLog

//-------------------------------------------------------------------
/*/{Protheus.doc} MTA080GNRE()
Função que realiza validação do campo Guia na CC7. 
Certifica que os campos tipo de imposto e natureza financeira estão preenchidos
corretamente na CC6 e CDO.
			 	
@author Erick G. Dias
@since 10/08/2017
@version 11.80
/*/
//-------------------------------------------------------------------
Function MTA080GNRE()

Local lRet	:= .T.
Local cCODLAN	:= GDFieldGet("CC7_CODLAN")
Local cCLANAP	:= GDFieldGet("CC7_CLANAP")

IF CC6->(FieldPos("CC6_TPIMP")) > 0 .AND. CC6->(FieldPos("CC6_NATURE")) > 0 .AND. CDO->(FieldPos("CDO_TPIMP")) > 0 .AND. CDO->(FieldPos("CDO_NATURE")) > 0

	dbSelectArea('CC6')
	dbSetOrder(1)
	dbSelectArea('CDO')
	dbSetOrder(1)
	
	If !Empty(Alltrim(cCODLAN))
		//Validação na tabela CC6
		//CC6_FILIAL+CC6_CODLAN	
		IF CC6->(MsSeek(xFilial("CC6")+cCODLAN))
			If	Empty(CC6->CC6_TPIMP) .Or. Empty(CC6->CC6_NATURE)
				Help(,,"MTA080GNRE",,"O Tipo de Imposto e/ou Código de Natureza não estão preenchidos no cadastro do código de lançamento",1,0)
				lRet := .F.				
			EndIf
			// Para geracao de GNRE via documento o codigo deve ser obrigatoriamente individualizado pois caso contrario eh impossivel
			// determinar qual GNRE sera demonstrada na apuracao.
			If CC6->CC6_AGRUPA == " |1"
				Help(,,"MTA080GNRE",,"Para geração de GNRE por operação o código de lançamento deve ser configurado como individualizado.",1,0)
				lRet := .F.
			EndIf
		EndIF
		
	ElseIF !Empty(Alltrim(cCLANAP))
		//Validação na tabela CDO
		//CDO_FILIAL+CDO_CODAJU		                                                                                                                                           
		IF CDO->(MsSeek(xFilial("CDO")+cCLANAP)) 
			If Empty(CDO->CDO_TPIMP) .Or. Empty(CDO->CDO_NATURE)
				Help(,,"MTA080GNRE",,"O Tipo de Imposto e/ou Código de Natureza não estão preenchidos no cadastro do código de lançamento",1,0)			
				lRet	:= .F.
			EndIf
			// Para geracao de GNRE via documento o codigo deve ser obrigatoriamente individualizado pois caso contrario eh impossivel
			// determinar qual GNRE sera demonstrada na apuracao.
			If CDO->CDO_AGRUPA == " |1"
				Help(,,"MTA080GNRE",,"Para geração de GNRE por operação o código de lançamento deve ser configurado como individualizado.",1,0)
				lRet := .F.
			EndIf
		EndIF	
	EndIF

Else

	lRet := .F.
	
EndIF

Return lRet


//-------------------------------------------------------------------
/*/{Protheus.doc} CopyTES
função para Marcar TES e copiar para diversas Filiais

@author Rafael dos Santos
@since 20.12.2017
@version 1.0

/*/
//-------------------------------------------------------------------
Function COPYTES()

Local aColumns		:= {}
Local nX			:= 0 
Local cAliasF4		:= GetNextAlias()
Local oDialogMK	 	:= Nil
Local oMrkBrowse 	:= Nil
Local cAliasTemp 	:= GetNextAlias()
Local cSlctSF4		:= "% "
Local lMarcar  		:= .F.
Local aStructTrb 	:= SF4->(DbStruct())
Local aCampoGrid 	:= {"F4_CODIGO", "F4_CF", "F4_TIPO", "F4_ICM","F4_LFICM", "F4_IPI","F4_LFIPI","F4_ISS","F4_LFISS","F4_TEXTO"}
Local aSeek			:= {}
Local aFiltro		:= {}
Local aRetCopy   	:= If(Type("aRotina")=="A",ACLONE(aRotina),{})
Local aCpos		:= {}

//Define Menu
aRotina := {}

//Posiciona os campos do tipo Memo no final da tabela para não ocasionar erro na gravação do campo Memo
For nX:=1 To Len(aStructTrb)
	If aStructTrb[nX][2] <> "M"
		AAdd(aCpos,aStructTrb[nX])
	EndIf
Next nX	

aStructTrb := aClone(aCpos)

For nX:=1 To Len(aStructTrb)  //Colunas que serão exibidas no browse
    If !aStructTrb[nX][1]=="MARK" .And. Ascan(aCampoGrid,aStructTrb[nX][1])>0   
		AAdd(aColumns,FWBrwColumn():New())
        aColumns[Len(aColumns)]:SetData( &("{||"+aStructTrb[nX][1]+"}") )
        aColumns[Len(aColumns)]:SetTitle(RetTitle(aStructTrb[nX][1])) 
        aColumns[Len(aColumns)]:SetSize(aStructTrb[nX][3]) 
        aColumns[Len(aColumns)]:SetDecimal(aStructTrb[nX][4])
        aColumns[Len(aColumns)]:SetPicture(PesqPict("SF4",aStructTrb[nX][1]))

		aAdd(aFiltro	,{aStructTrb[nX][1];
			,aStructTrb[nX][1];
			,aStructTrb[nX][2];
			,aStructTrb[nX][3];
			,aStructTrb[nX][4];
			,PesqPict("SF4",aStructTrb[nX][1])})
			
    EndIf
	//Query SF4
	cSlctSF4 += aStructTrb[nX,1]+", "
Next nX

//Query SF4
cSlctSF4 += " ' ' AS MARK "
cSlctSF4 +='%'

BeginSql Alias cAliasF4	
	COLUMN F4_DTFIMNT AS DATE
	SELECT
		%Exp:cSlctSF4%
	FROM 
		%Table:SF4% SF4
	WHERE
		SF4.F4_FILIAL=%xFilial:SF4% AND
		SF4.%NotDel%
	ORDER BY SF4.F4_CODIGO
EndSql

aAdd(aStructTrb, {'MARK', 'C', 1, 0})
oTmpTable := FWTemporaryTable():New(cAliasTemp, aStructTrb)
oTmpTable:AddIndex("IN1", {'F4_CODIGO'})
oTmpTable:Create()

While !(cAliasF4)->(Eof()) 
	dbSelectArea(cAliasTemp)
	RecLock(cAliasTemp, .T. )
		For nX := 1 To fCount()
			If (cAliasTemp)->(FieldName(nX)) != "MARK"
				If ValType(FieldGet(nX)) == "L"
           			cVal := If((cAliasF4)->(FieldGet(nX)=="T"), ".T.", ".F.") // Converte campo tipo logico em caractere para leitura.
				Else	
					(cAliasTemp)->(FieldPut(nX, (cAliasF4)->(FieldGet(nX)))) // Grava todos os campos do SF4 no TRB 
				EndIf
			EndIf	
		Next
	(cAliasTemp)->(MsUnlock())
	(cAliasF4)->(dbSkip())	
EndDO

//Fecha query
If Select(cAliasF4) > 0
    DbSelectArea(cAliasF4)
    DbCloseArea()
EndIf

//Inicia Browse
If !(cAliasTemp)->(Eof())

	aAdd(aSeek,{"CODIGO",{{"","C",003,0,"CODIGO","@!"}} } )

	oMrkBrowse := FWMarkBrowse():New()	
	oMrkBrowse:SetOwner(oDialogMK)
	oMrkBrowse:SetDescription("Replicar TES")
	
	oMrkBrowse:SetMenuDef("")
	oMrkBrowse:ForceQuitButton()
	oMrkBrowse:DisableConfig(.F.)
	oMrkBrowse:DisableReport(.F.)
	oMrkBrowse:DisableDetails(.T.)
	oMrkBrowse:SetWalkThru(.F.)			

	oMrkBrowse:oBrowse:SetUseFilter(.T.)
	oMrkBrowse:oBrowse:SetDBFFilter() 
	oMrkBrowse:oBrowse:SetFieldFilter(aFiltro)
	oMrkBrowse:oBrowse:SetSeek(.T.,aSeek)

	oMrkBrowse:SetAlias(cAliasTemp)	
	oMrkBrowse:SetColumns(aColumns)

	oMrkBrowse:SetFieldMark("MARK")
	oMrkBrowse:SetMark('X', cAliasTemp, "MARK")
	oMrkBrowse:SetAllMark( { || .T. } )
	oMrkBrowse:bAllMark := { || InvertSel(cAliasTemp,oMrkBrowse:Mark(),lMarcar := !lMarcar,.F. ), oMrkBrowse:Refresh(.T.)  }

	oMrkBrowse:AddButton("Copiar", { || FwMsgRun(,{|oSay|CopySF4(cAliasTemp,.T.)},'Processando',"",),Alert("Processo Finalizado"),oMrkBrowse:GetOwner():End()},,2 )
	oMrkBrowse:AddButton("Inverter Seleção", { || InvertSel(cAliasTemp,oMrkBrowse:Mark(),lMarcar := !lMarcar,.T. ),oMrkBrowse:Refresh(.T.)},,2 )	
	oMrkBrowse:AddButton("Cancelar", { || oMrkBrowse:GetOwner():End()},,2 )

	oMrkBrowse:Activate()
	
Else
	Help(" ",1,"RECNO")
EndIf

//Restaura Menu
aRotina:=AClone(aRetCopy)
oTmpTable:Delete()

Return


/*-------------------------------------------------------------------
{Protheus.doc} InvertSel
Regra para marcação dos registros de notas

@author Rafael S Oliveira
@since 20/12/2017
@version 1.0
-------------------------------------------------------------------*/
Static Function InvertSel(cAliasF4,cMarca,lMarcar, lInvert)
Local aAreaSF4  := (cAliasF4)->( GetArea() )

(cAliasF4)->( dbGoTop() )
While !(cAliasF4)->( Eof() )
	RecLock( (cAliasF4), .F. )
	
	If lInvert
		(cAliasF4)->MARK := IIf( (cAliasF4)->MARK == cMarca , '  ',cMarca )
	Else
		(cAliasF4)->MARK := IIf( lMarcar, cMarca, '  ' )
	Endif
	
	MsUnlock()
	(cAliasF4)->( dbSkip() )
EndDo

RestArea( aAreaSF4 )
Return .T.


/*-------------------------------------------------------------------
{Protheus.doc} A080Copy
Função de copiar TES selecionado.

@author Rafael S Oliveira
@since 16/03/2018
@version 1.0
-------------------------------------------------------------------*/
Function A080Copy(cAlias,nReg,nOpc)
	
	AxInclui("SF4",SF4->(Recno()),3,/*<aAcho>*/,"COPIARSF4",/*<aCpos>*/,"COPIAVLD()",.F.,/*<cTransact>*/,/*<aButtons>*/,/*<aParam>*/,/*<aAuto>*/,/*<lVirtual>*/,.T.)

Return nil

//Função para carregamento dos campos em variáveis de memória
Function COPIARSF4()

Local bCampo 	:= { |nCPO| Field(nCPO) }
Local nCountCpo	:= 0

DbSelectArea("SF4")

For nCountCpo := 1 TO SF4->(FCount())

	If !( AllTrim(FieldName( nCountCpo )) $ "F4_CODIGO/F4_USERLGI/F4_USERLGA" )

		//Inputa o valor do campo posicionado, na variável de memória
		M->&(EVAL(bCampo, nCountCpo)) := FieldGet(nCountCpo)

	EndIf

Next nCountCpo
//valida se existe codigo que esta sendo copiado

Return nil

/*-------------------------------------------------------------------
{Protheus.doc} COPIAVLD
Valida Função de copiar TES selecionado.

@author Rafael S Oliveira
@since 16/03/2018
@version 1.0
-------------------------------------------------------------------*/
Function COPIAVLD()	
Local lRet		:= .T.

	If SF4->(dbSeek(xFilial("SF4")+M->F4_CODIGO))

		MsgInfo("Código de Tipo de Entrada\Saída já existente!")
		lRet := .F.
	Endif

	If lRet .And. ExistBlock("MA080VLD")
		lRet := ExecBlock("MA080VLD",.F.,.F., { 3 } )
	EndIf		

Return lRet

/*-------------------------------------------------------------------
{Protheus.doc} A080VLTE3
Valida TES poder 3
-------------------------------------------------------------------*/
Function A080VLTE3()
Local lRet:= .T.	

If Altera .or. Inclui 
    MsgRun(STR0050,STR0051,{||A080VTERC(@lRet)}) //"Validando configuração TES poder terceiros...","Aguarde..."
EndIf

return lret

/*-------------------------------------------------------------------
{Protheus.doc} A080VLTE3
Valida TES poder 3
-------------------------------------------------------------------*/
Function A080VTERC(lRet)
local cQuery 
Local cAlSF4 

Default	lRet := .T.

cQuery := "SELECT F4_CODIGO CODIGO, F4_PODER3 PODER3 FROM "
cQuery += RetSqlName('SF4') + " WHERE F4_FILIAL = '" + xFilial('SF4') + "' AND "
cQuery += " F4_TESDV = '" + M->F4_CODIGO + "' AND D_E_L_E_T_ = ' '"

cAlSF4 := GetNextAlias()
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlSF4,.T.,.T.)
dbSelectArea(cAlSF4)
while !EOF() 
	If	(M->F4_PODER3 	    == 'N' .and.    (cAlSF4)->PODER3 <> 'N') 	.or.;
		(M->F4_PODER3		== 'R' .and. 	(cAlSF4)->PODER3 <> 'D')	.or.;
		(M->F4_PODER3 		== 'D' .and.	(cAlSF4)->PODER3 <> 'R') 
		MsgInfo(STR0045+M->F4_CODIGO+STR0046+ (cAlSF4)->CODIGO+STR0047) //"A TES "##" é de devolução da TES "##" e não esta compativel para poder terceiros"
		lRet := .F.
		Exit
	EndIf
	Dbskip()
Enddo 
If !Empty(M->F4_TESDV)  
	dbselectarea('SF4')
	dbsetorder(1)
	If Dbseek(xFilial('SF4')+M->F4_TESDV)
		If	(M->F4_PODER3	== 'N' .and.	F4_PODER3 <> 'N')	.or.; 
			(M->F4_PODER3	== 'R' .and. 	F4_PODER3 <> 'D')	.or.;
			(M->F4_PODER3 	== 'D' .and.	F4_PODER3 <> 'R') 
			MsgInfo(STR0045+M->F4_CODIGO+STR0048+ M->F4_TESDV+STR0049)//"A TES "##" não esta compativel com a TES "##" na configuração de poder de terceiros"
			lRet := .F.
		EndIf
	EndIf
EndIF 	

Return lRet

/*/{Protheus.doc} FisTelLog
	(long_description)
	@type  Function
	@author Erich Buttner
	@since 16/02/2023
	@version version
	@param param_name, param_type, param_descr
	@return return_var, return_type, return_description
	@example
	(examples)
	@see (links_or_references)
	/*/
Function FisTelLog(cAliasTemp,aCmpLog,aIndex)

Local aColumns	:= {}
Local aFilter	:= {}
Local nX		:= 0
Local oColuna	:= Nil

Default aIndex	:= {}
Default aCmpLog	:= {}

For nX := 1 To  Len(aCmpLog)
	oColuna := FWBrwColumn():New()	    // Cria objeto
	oColuna:SetEdit(.F.)    	   	    // Indica se <E9> editavel
	oColuna:SetTitle(aCmpLog[nX][2])	// Define titulo
	oColuna:SetType(aCmpLog[nX][3])	    // Define tipo
	oColuna:SetSize(aCmpLog[nX][4])	    // Define tamanho
	oColuna:SetPicture(aCmpLog[nX][5])  // Define picture
	oColuna:SetAlign(aCmpLog[nX][6])    // Define alinhamento
	oColuna:SetData(&('{||'+aCmpLog[nX][1]+'}'))

	Aadd(aColumns,oColuna)
	aAdd(aFilter, {aCmpLog[nX][1],;
		 aCmpLog[nX][2],;
		 aCmpLog[nX][3],; 
		 aCmpLog[nX][4],; 
		 0,; 
		 aCmpLog[nX][5]} )
Next nX

oBrowse := FWmBrowse():New()
oBrowse:SetDescription("Tela de Log")
oBrowse:SetDataTable(.T.)
oBrowse:SetAlias(oTempTab:GetAlias())
oBrowse:SetQueryIndex(aIndex)
oBrowse:SetTemporary(.T.)
oBrowse:OptionReport(.T.)
oBrowse:SetColumns(aColumns)
oBrowse:SetFieldFilter(aFilter)
oBrowse:SetMenuDef('')
oBrowse:DisableDetails()
oBrowse:ForceQuitButton()
oBrowse:SetUseFilter(.T.)

oBrowse:AddButton("Fechar", { || oBrowse:GetOwner():End()},,2 )

oBrowse:Activate()

oTempTab:Delete()
FreeObj(oTempTab)

Return .T.
/*-------------------------------------------------------------------
{Protheus.doc} A080FildOK
Valida campo do getdados da CC7

@author Matheus Massarotto
@since 23/03/2023
@version 1.0
-------------------------------------------------------------------*/
Function A080FildOK
Local cCampo	:= ReadVar()
Local cConteudo := ""
Local nPos
Local nPosLen
Local nX

If cCampo == "M->CC7_CODLAN"

	nPos		:= aScan(oNewGetDad:aHeader,{|aX| aX[2]=="CC7_CODLAN"})
	cConteudo 	:= &cCampo
	nPosLen		:= len(oNewGetDad:aCols)

	for nX:=1 to nPosLen
		if oNewGetDad:NAT<>nX .and. !empty(cConteudo) .and. oNewGetDad:aCols[nX][nPos] == cConteudo .and. !oNewGetDad:aCols[nX][len(oNewGetDad:aCols[nX])];
			.and. Substring(oNewGetDad:aCols[nX][nPos],3,1) <> '9'
			Alert(STR0052+cConteudo+STR0053)
			exit
		endif
	next

Endif

Return .T.
