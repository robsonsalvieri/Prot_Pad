#INCLUDE "TLPP-CORE.TH"
#INCLUDE "tlpp-rest.th"
#Include "PROTHEUS.CH" 

namespace totvs.protheus.backoffice.fiscal.simulator.utils


class SimulatorTools
    private data dSystemDateBkp as date
    private data cBranchBkp  as character

    public method new() constructor

    public method setSystemDate(dDate)
    public method restSystemDate()

    public method setBranch(cBranch)
    public method restBranch()

endclass

/*/{Protheus.doc} new
    Construtor da classe.
    @author anedino.santos
    @since 05/03/2025
    @version 12.1.2510
    @return self, object, instância da classe
/*/
method new() class SimulatorTools
    self:dSystemDateBkp := CtoD(" / / ")
return self

/*/{Protheus.doc} setSystemDate
    Define a data do sistema com um novo valor guardando a data atual para ser
    restaurada posteriormente. Caso o parâmetro `dDate` não seja válido manterá
    a data atual do Sistema.
    @author anedino.santos
    @since 05/03/2025
    @version 12.1.2510
    @param dDate, date, nova data a ser definiada no Sistema
    @return dDataBase, date, retorna a data que será usada no Sistema.
/*/
method setSystemDate(dDate) class SimulatorTools
    if !Empty(dDate) .and. valtype(dDate) == "D"
        self:dSystemDateBkp := dDataBase
        dDataBase := dDate
    endif
return dDataBase

/*/{Protheus.doc} restSystemDate
    Restaura a data do Sistema antes de ser executado o método setSystemaDate.
    Caso o método setSystemaDate não tenha sido executado antes deste, não ocorrerá
    restauração da data e o método retornará a data corrente do Sistema.
    @author anedino.santos
    @since 05/03/2025
    @version 12.1.2510
    @return dDataBase, date, retorna a data que será usada no Sistema.
/*/
method restSystemDate() class SimulatorTools
    if !Empty(self:dSystemDateBkp)
        dDataBase := self:dSystemDateBkp
    endif
return dDataBase

/*/{Protheus.doc} setBranch
    Altera a filial corrente do Sistema conforme parâmetro guardando-a em um
    atributo de backup para posteriormente restaurar o seu valor. Caso `cBranch`
    não seja válido será mantida a filial corrente.
    @author anedino.santos
    @since 05/03/2025
    @version 12.1.2510
    @param cBranch, caracter, filial que deve ser definida em lugar da filial corrente.
    @return cFilAnt, caracter, filial corrente do Sistema.
/*/
method setBranch(cBranch) class SimulatorTools
    if !Empty(cBranch) .and. valtype(cBranch) == "C"
        self:cBranchBkp := cFilAnt
        cFilAnt := cBranch
    endif
return cFilAnt

/*/{Protheus.doc} restBranch
    Restaura a filial do Sistema conforme atributo de backup da classe. Esse
    método só tem efeito usado após o método `setBranch` do contrário ele apenas
    retornará a filial corrente do Sistema.
    @author anedino.santos
    @since 05/03/2025
    @version 12.1.2510
    @return return_var, return_type, return_description
/*/
method restBranch() class SimulatorTools
    if !Empty(self:cBranchBkp)
        cFilAnt := self:cBranchBkp
    endif
return cFilAnt
