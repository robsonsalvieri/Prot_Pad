#INCLUDE "TOTVS.CH"
#INCLUDE "tlpp-rest.th"
#INCLUDE "TLPP-CORE.TH"


using namespace totvs.protheus.backoffice.fiscal.simulator.diagnose.service

namespace totvs.protheus.backoffice.fiscal.simulator.diagnose


/*/{Protheus.doc} SimulatorDiagnose
    Classe responsável por processar a requisição de diagostico do simulador
    @author Alexandre.Esteves
    @since 22/10/2024
    @version 12.1.2410
/*/
Class SimulatorDiagnose
    private data jRequestBody as json
    private data cResponseBody as character

    public method new() Constructor

    @Post(endpoint="/api/fiscal/v1/simulator/diagnose")
    public method diagnose()

    private method makeRequestJson() as json
    private method runDiagnosis() as json
    private method finish()

EndClass


/*/{Protheus.doc} new
    Método que instância objeto da classe.
    @author Alexandre.Esteves
    @since 22/10/2024
    @version 12.1.2410
    @return self, object, instância da classe
/*/
Method new() class SimulatorDiagnose
    self:jRequestBody  := JsonObject():New() 
    self:cResponseBody := ""
Return


/*/{Protheus.doc} diagnose
    Método reponsável por responder a requisição de diagnóstico do simulador.
    @author Alexandre.Esteves
    @since 22/10/2024
    @version 12.1.2410
    @return Nil, Nil, sem retorno
/*/
Method diagnose() class SimulatorDiagnose

    self:jRequestBody := self:makeRequestJson(oRest:getBodyRequest())
    if self:jRequestBody <> Nil
        try
            self:runDiagnosis()
            oRest:setResponse(self:cResponseBody)
        catch oError
            SetRestFault(500, FWHttpEncode(oError:description), .T.)
        finally
            self:finish()
        endtry
    endif

Return

/*/{Protheus.doc} makeRequestJson
    Checa se o corpo da requisição não está vazio e se sua estrutura está em 
    formato json para criar o objeto json da requisição.
    @author Alexandre.Esteves
    @since 22/10/2024
    @version 12.1.2410
    @param cBody, character, string em formato json com os dados do corpo da requisição
    @return jBody, json, objeto json conforme corpo da requisição.
/*/
Method makeRequestJson(cBody) class SimulatorDiagnose
    local jBody as json
    local xErro as variant

    if !Empty(cBody)
        jBody := JsonObject():New()
        xErro := jBody:fromJson(cBody)
        if xErro <> Nil
            FwFreeObj(jBody)
            jBody := Nil
            SetRestFault(400, FWHttpEncode("Estrutura de body da requisição deve ser em formato json. Erro: "+xErro))
        endif
    else
        SetRestFault(400, FWHttpEncode("Erro: Body da requisição vazio."))
    endif
Return jBody


/*/{Protheus.doc} runDiagnosis
    Executa o serviço de calculo.
    @author Alexandre.Esteves
    @since 22/10/2024
    @version 12.1.2410
/*/
Method runDiagnosis() class SimulatorDiagnose
    local oDiagService as object

     ///aqui será o ponto que irei fazer os processamentos do diagnostico
    oDiagService := DiagnoseService():New(self:jRequestBody)
    
    self:cResponseBody := oDiagService:runDiagnosisEngine()
    // Libero o objeto
    //oDiagService:clear()
    FwFreeObj(oDiagService)
    oDiagService := Nil
Return

/*/{Protheus.doc} finish
    Finaliza o processamento do end-point.
    @author Alexandre.Esteves
    @since 22/10/2024
    @version 12.1.2410
/*/
Method finish() class SimulatorDiagnose
    FwFreeObj(self:jRequestBody)
    self:jRequestBody := Nil
Return
