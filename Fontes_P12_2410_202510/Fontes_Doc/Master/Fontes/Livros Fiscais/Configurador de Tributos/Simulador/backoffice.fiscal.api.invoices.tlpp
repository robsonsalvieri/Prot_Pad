#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"

using namespace totvs.protheus.backoffice.fiscal.simulator.dao

/*-------------------------------------------------------------
API que Consulta os documentos de nota fiscal acordo com o periodo do movimento das notas.
@author alexandre.esteves
@since 27/01/2025
@version 12.1.2510
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/v1/simulator/invoices', description='API de consulta com as lista das notas fiscais ')
Function FisGetInvoice()

oRest:setResponse( GetResponse())

Return

/*-------------------------------------------------------------
API de consulta da nota fiscal pelo numero exato da notao.
@author alexandre.esteves
@since 27/01/2025
@version 12.1.2510
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/v1/simulator/invoices/:id', description='API de consulta da nota fiscal pelo numero exato da nota')
Function FisGetInvoiceByID()

oRest:setResponse( GetResponse()) 

Return 

/*-------------------------------------------------------------------
{Protheus.doc} GetResponse
Função responsável por preencher o Json com com informações referente a lista de notas fiscais
@author alexandre.esteves
@since 27/01/2025
@version 12.1.2510
-------------------------------------------------------------------*/
Static Function GetResponse()
Local nPos          := 0                         as numeric
Local nPageAux      := 0                         as numeric
Local nPageSize     := 10                        as numeric
Local nPage         := 1                         as numeric
Local lHasNext      := .f.                       as logical
Local lFilial       := .f.                       as logical
Local lTpMov        := .f.                       as logical
Local lDtIni        := .f.                       as logical
Local lDtFim        := .f.                       as logical
Local oJsonResp     := JsonObject():New()        as object
Local oResult       := Nil                       as object
Local oJParams      := oRest:getQueryRequest()   as json //Json com os parâmetros (query param) 
Local cAlias        := ""                        as character
Local cRet          := ""                        as character
Local cApiReq       := "INVOICES"                as character
Local cFil          := ""                        as character
Local cTpMov        := ""                        as character
Local cDtIni        := ""                        as character
Local cDtFim        := ""                        as character
Local cParticipante := ""                        as character
Local cFilter       := ""                        as character
Local cId           := ""                        as character
Local cSerie        := ""                        as character


if valtype(oJParams) == 'J'
    if valtype( oJParams['page'] ) != 'U' .and. Val(oJParams['page']) > 0
        nPage := Val(oJParams['page'])
    endif
    if valtype( oJParams['pageSize'] ) != 'U' .and. Val(oJParams['pageSize']) > 0
        nPageSize := Val(oJParams['pageSize'])
    endif
    if valtype( oJParams['filial'] ) != 'U' .and. !Empty(Alltrim(oJParams['filial']))
        cFil := Alltrim(oJParams['filial'])
        lFilial := .T.
    endif
    if valtype( oJParams['tipomovimento'] ) != 'U' .and. !Empty(Alltrim(oJParams['tipomovimento']))
        cTpMov := Alltrim(oJParams['tipomovimento'])
        lTpMov := .T.
    endif
    if valtype( oJParams['periodoinicio'] ) != 'U' .and. !Empty(Alltrim(oJParams['periodoinicio']))
        cDtIni := Alltrim(oJParams['periodoinicio'])
        lDtIni := .T.
    endif
    if valtype( oJParams['periodofim'] ) != 'U' .and. !Empty(Alltrim(oJParams['periodofim']))
        cDtFim := Alltrim(oJParams['periodofim'])
        lDtFim := .T.
    endif
    if valtype( oJParams['participante'] ) != 'U' .and. !Empty(Alltrim(oJParams['participante']))
        cParticipante := oJParams['participante']
    endif
    if valtype( oJParams['serie'] ) != 'U' .and. !Empty(Alltrim(oJParams['serie']))
        cSerie := oJParams['serie']
    endif
    if valtype( oJParams['filter'] ) != 'U' .and. !Empty(Alltrim(oJParams['filter']))
        cfilter := oJParams['filter']
    endif

endif

FWFreeObj(oJParams)
oJParams := Nil

if !Empty(alltrim(oRest:getPathParamsRequest()['id']))
    cId := alltrim(oRest:getPathParamsRequest()['id'])
endif

If  lFilial .and. lTpMov .and. lDtIni .and. lDtFim

    oResult := SimulatorInvoiceDAO():New()
    cAlias  := oResult:loadInvoices(nPage,nPageSize,cApiReq,cId,cfilter,cFil,cTpMov,cDtIni,cDtFim,cParticipante,cSerie)
    
    If Empty(cId)

        While (cAlias)->(!Eof())
            nPageAux++
            if nPageAux == 1
                oJsonResp["items"] := {}
            endif
            if nPageAux <= nPageSize
                aAdd( oJsonResp["items"], JsonObject():New() )
                nPos++ 
                oJsonResp["items"][nPos]["invoice"]         :=   AllTrim((cAlias)->NF)
                oJsonResp["items"][nPos]["serie"]           :=   AllTrim((cAlias)->SERIE)
                oJsonResp["items"][nPos]["participant"]     :=   AllTrim((cAlias)->PARTICIPANTE)
                oJsonResp["items"][nPos]["store"]           :=   AllTrim((cAlias)->LOJA)
                oJsonResp["items"][nPos]["companyname"]     :=   AllTrim((cAlias)->RAZAOSOCIAL)
                oJsonResp["items"][nPos]["typeofinvoice"]   :=   AllTrim((cAlias)->ESPECIE)
                oJsonResp["items"][nPos]["issuedate"]       :=   FWTimeStamp(3, SToD((cAlias)->DTEMISSAO),"00:00:00") 
                oJsonResp["items"][nPos]["state"]           :=   AllTrim((cAlias)->UF) 
                oJsonResp["items"][nPos]["grossvalue"]      :=   (cAlias)->VALBRUT
                oJsonResp["items"][nPos]["netvalue"]        :=   (cAlias)->VALMERC

            Else
                lHasNext := .t.
                Exit
            EndIf
            (cAlias)->(DbSkip())
        EndDo
        oJsonResp["hasNext"]    := lHasNext
        If lHasNext
            oJsonResp["nextPageHref"] := "/api/fiscal/v1/simulator/invoices?page=" + cValtoChar(nPage + 1) + "&pageSize=" + cValtoChar(nPageSize)
        Else
            oJsonResp["nextPageHref"] := ""
        EndIf
    Else
        If !Empty(AllTrim((cAlias)->NF)) .AND. (cAlias)->(!Eof())
                oJsonResp["invoice"]         :=   AllTrim((cAlias)->NF)
                oJsonResp["serie"]           :=   AllTrim((cAlias)->SERIE)
                oJsonResp["participant"]     :=   AllTrim((cAlias)->PARTICIPANTE)
                oJsonResp["store"]           :=   AllTrim((cAlias)->LOJA)
                oJsonResp["companyname"]     :=   AllTrim((cAlias)->RAZAOSOCIAL)
                oJsonResp["typeofinvoice"]   :=   AllTrim((cAlias)->ESPECIE)
                oJsonResp["issuedate"]       :=   FWTimeStamp(3, SToD((cAlias)->DTEMISSAO),"00:00:00") 
                oJsonResp["state"]           :=   AllTrim((cAlias)->UF) 
                oJsonResp["grossvalue"]      :=   (cAlias)->VALBRUT
                oJsonResp["netvalue"]        :=   (cAlias)->VALMERC
        Else
            oRest:setStatusCode(404)
            oJsonResp['code'] := 404
            oJsonResp['detailMessage'] := ''
            oJsonResp['message'] := "O  documento fiscal não foi encontrado."
        Endif
    Endif
    (cAlias)->(DBCloseArea())
    FWFreeObj(oResult)
    oResult := Nil
Else
    oRest:setStatusCode(400)
    oJsonResp['code'] := 400
    oJsonResp['detailMessage'] := ''
    oJsonResp['message'] := "Parâmetros da consulta não atendidos."

Endif

cRet := oJsonResp:toJson()

FWFreeObj(oJsonResp)
oJsonResp := Nil

Return cRet

