#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"

/*-------------------------------------------------------------
API que verifica se existe o campo no dicionario de dados SX3 
@author alexandre.esteves
@since 03/02/2025
@version 12.1.2510
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/v1/simulator/check-field/:field', description='API que verifica se existe o campo no dicionario de dados SX3')
Function FisGetFieldID()

oRest:setResponse( GetResponse()) 

Return 

/*-------------------------------------------------------------------
{Protheus.doc} GetResponse
Função que irá processar a requisição e retornar o json referente a existencia do campo
@author alexandre.esteves
@since 03/02/2025
@version 12.1.2510
-------------------------------------------------------------------*/
Static Function GetResponse()

Local oJsonResp     := JsonObject():New()        as object
Local cRet          := ""                        as character
Local cField        := ""                        as character


if !Empty(alltrim(oRest:getPathParamsRequest()['field']))
    cfield := alltrim(oRest:getPathParamsRequest()['field'])

    if CheckField(Upper(cfield))
        // status code 200 com o retorno do próprio campo
        oJsonResp["field"] := cfield
    else
        oRest:setStatusCode(404)
        oJsonResp["code"] := 404
        oJsonResp["message"] := "Campo " +cfield+ " não encontrado."
        oJsonResp["detailedMessage"] := "O campo pesquisado não foi encontrada no dicionário de dados SX3 - não existe."
    endif
endif

cRet := oJsonResp:toJson()
FWFreeObj(oJsonResp)
oJsonResp := Nil
Return cRet

/*-------------------------------------------------------------------
{Protheus.doc} CheckField
Função que irá verificar se o campo da requisição existe na tabela SX3.
@author alexandre.esteves
@since 03/02/2025
@version 12.1.2510
-------------------------------------------------------------------*/
Static Function CheckField(cCampo)
Return len(FWSX3Util():GetFieldStruct( cCampo )) > 0

