#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"

Static oGetRProd := Nil        as object
Static oRetOrig  := Nil        as object

/*-------------------------------------------------------------
API de consulta a lista de Produtos.
@author adilson.roberto
@parâmetros de entrada do endpoint
cfilter = filtro na lista
@since 13/10/2024
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/v1/simulator/products', description='API de consulta a lista de produtos')
Function GSimuProd()
Local oJsResponse        as json
Local cfilter     := oRest:getQueryRequest()['filter']  as character
Local cFil        := oRest:getQueryRequest()['filial']  as character

oJsResponse := SimuProd(cfilter, cFil)
oRest:setResponse( (oJsResponse ))
FWFreeObj(oJsResponse)
FWFreeObj(oRetOrig)
oJsResponse := Nil
oRetOrig := Nil
Return

/*-------------------------------------------------------------
API de consulta Produto pelo id.
@author adilson.roberto
@parâmetros de entrada do endpoint
cId = Produto a ser consultado
@since 13/10/2024
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/v1/simulator/products/:id', description='API de consulta a lista de de produtos pelo código')
Function GSimProdId()
Local oJsResponse        as json
Local cId         := oRest:getPathParamsRequest()['id'] as character
Local cFil        := oRest:getQueryRequest()['filial']  as character
//O parâmetro id é obrigatório
if valtype(cId) != 'U' .and. !empty(cId)
    oJsResponse := SimuProdId(cId, cFil)
endif    
oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)
FWFreeObj(oRetOrig)
oJsResponse := Nil
oRetOrig := Nil
Return 

/*-------------------------------------------------------------------
{Protheus.doc} GET SimuProd
Função responsável por preencher o Json com com informações referente a lista de Produtos
@author adilson.roberto
@parâmetros de entrada
cfilter = filtro na lista
@since 13/10/2024
-------------------------------------------------------------------*/
Static Function SimuProd(cfilter as character, cFil as character)
Local nPos         := 0                         as numeric
Local nPageAux     := 0                         as numeric
Local nPageSize    := 10                        as numeric
Local lHasNext     := .f.                       as logical
Local nPage        := 1                         as numeric
Local oJsonResp    := JsonObject():New()        as object
Local cAlias       := ""                        as character
Local oJParams     := oRest:getQueryRequest()   as json //Json com os parâmetros (query param)    
Local cDescOrig    := ""                        as character
Local cRet         := ""                        as character

Default cfilter := ""
Default cFil    := ""

if valtype(oJParams) == 'J'
    if valtype( oJParams['page'] ) != 'U' .and. Val(oJParams['page']) > 0
        nPage := Val(oJParams['page'])
    endif
    if valtype( oJParams['pageSize'] ) != 'U' .and. Val(oJParams['pageSize']) > 0
        nPageSize := Val(oJParams['pageSize'])
    endif
endif

GetProd(@cAlias,nPage,nPageSize,"",cfilter, cFil)

//Retorna um Json com as origens
If oRetOrig == Nil
    RetOrig(@oRetOrig)
Endif

// Iniciando o While
While (cAlias)->(!Eof())
    nPageAux++
    cDescOrig := ""
    if nPageAux == 1
        oJsonResp["items"] := {}
    endif
    if nPageAux <= nPageSize
        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++
        If !Empty(AllTrim((cAlias)->B1_ORIGEM))
            cDescOrig := AllTrim(oRetOrig[AllTrim((cAlias)->B1_ORIGEM)][4])
        Endif    
        oJsonResp["items"][nPos]["id"]             :=   Left(AllTrim((cAlias)->B1_COD),30)
        oJsonResp["items"][nPos]["descit"]         :=   Left(AllTrim((cAlias)->B1_DESC),40)
        oJsonResp["items"][nPos]["unitit"]         :=   AllTrim((cAlias)->B1_UM)        
        oJsonResp["items"][nPos]["orige"]          :=   AllTrim((cAlias)->B1_ORIGEM)
        oJsonResp["items"][nPos]["origdesc"]       :=   Left(AllTrim((cAlias)->B1_ORIGEM) + "-" + cDescOrig,30)
    Else
        lHasNext := .t.
        Exit
    EndIf
    (cAlias)->(DbSkip())
EndDo
oJsonResp["hasNext"]    := lHasNext
If lHasNext
    oJsonResp["nextPageHref"] := "/api/fiscal/v1/simulator/products?page=" + cValtoChar(nPage + 1) + "&pageSize=" + cValtoChar(nPageSize)
Else
    oJsonResp["nextPageHref"] := ""
EndIf
(cAlias)->(DBCloseArea())
cRet := oJsonResp:toJson()

FWFreeObj(oJsonResp)
oJsonResp := Nil

Return cRet

/*-------------------------------------------------------------------
{Protheus.doc} GET SimuProdId
Função responsável por preencher o Json com com informações referente o Produto 
buscado pelo ID
@author adilson.roberto
cId = Produto a ser consultado
@since 13/10/2024
-------------------------------------------------------------------*/
Static Function SimuProdId(cId as character, cFil as character)
Local nPageSize    := 10                        as numeric
Local nPage        := 1                         as numeric
Local oJsonResp    := JsonObject():New()        as object
Local cAlias       := ""                        as character
Local cDescOrig    := ""                        as character
Local cRet         := ""                        as character

Default cId    := ""
Default cFil   := ""    

GetProd(@cAlias,nPage,nPageSize,cId,"",cFil)
//Retorna um Json com as origens
If oRetOrig == Nil
    RetOrig(@oRetOrig)
Endif

If !Empty(AllTrim((cAlias)->B1_COD)) .AND. (cAlias)->(!Eof())
    If !Empty(AllTrim((cAlias)->B1_ORIGEM))        
        cDescOrig := AllTrim(oRetOrig[AllTrim((cAlias)->B1_ORIGEM)][4])
    Endif 
    oJsonResp["id"]             :=   Left(AllTrim((cAlias)->B1_COD),30)
    oJsonResp["descit"]         :=   Left(AllTrim((cAlias)->B1_DESC),40)
    oJsonResp["unitit"]         :=   AllTrim((cAlias)->B1_UM)
    oJsonResp["orige"]          :=   AllTrim((cAlias)->B1_ORIGEM)
    oJsonResp["origdesc"]       :=   Left(AllTrim((cAlias)->B1_ORIGEM) + "-" + cDescOrig,30)
Else
    oJsonResp['code'] := 400
    oJsonResp['detailMessage'] := ''
    oJsonResp['message'] := "O código do produto não foi encontrado."

EndIf
(cAlias)->(DBCloseArea())
cRet := oJsonResp:toJson()

FWFreeObj(oJsonResp)
oJsonResp := Nil
Return cRet

/*--------------------------------------------------------------------
{Protheus.doc} GetProd()
(Responsável por executar a consulta aos campos da tabela SB1)
@author adilson.roberto
@since 11/10/2024
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetProd(cAlias as character, nPage as numeric, nPageSize as numeric, cId as character, cfilter as character, cFil as character)
Local cQuery    := ''  as character

Default cId     := ""
Default cfilter := ""
Default cFil    := ""

If oGetRProd == Nil
    cQuery := " SELECT        "
    cQuery += " SB1.B1_COD,   "
    cQuery += " SB1.B1_DESC,  "
    cQuery += " SB1.B1_UM,    "
    cQuery += " SB1.B1_ORIGEM "
    cQuery += " FROM " + RetSqlName('SB1') + " SB1 "
    cQuery += " WHERE  SB1.B1_FILIAL = ? "
    cQuery += " 	AND  SB1.D_E_L_E_T_ = ?"
    If Empty( cId ) 
        If Empty(cfilter)
            cQuery += " ORDER BY SB1.B1_COD "
            cQuery += " OFFSET ( ? * ? ) ROWS "
            cQuery += " FETCH NEXT ? ROWS ONLY "
        Else
            cQuery += " AND SB1.B1_COD LIKE(?) " "
        Endif    
    Else
        cQuery += " AND SB1.B1_COD = ? "
    EndIf
    oGetRProd := FwExecStatement():New(cQuery)
Endif

If oGetRProd != Nil
    oGetRProd:SetString(1, xFilial("SB1",cFil))
    oGetRProd:SetString(2, " ")
    If Empty( cId )
        If !Empty(cfilter)
            oGetRProd:SetString(3, Alltrim(cFilter + '%'))
        Else    
	        oGetRProd:SetUnsafe(3, cValToChar(nPage-1))
            oGetRProd:SetUnsafe(4, cValToChar(nPageSize))
            oGetRProd:SetUnsafe(5, cValToChar(nPageSize+1))
        Endif    
    Else 
        oGetRProd:SetString(3, cId)    
    Endif
    cAlias := oGetRProd:OpenAlias()
    oGetRProd:Destroy()
    oGetRProd:= Nil
Endif
Return 

/*--------------------------------------------------------------------
{Protheus.doc} RetOrig()
(Responsável por consultar e retornar a origem do produto)
@author adilson.roberto
@since 12/12/2024
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function RetOrig(oRetOrig as object)
Local aRetOrig     := {}                        as array
Local nPass        := 0                         as numeric

oRetOrig := JsonObject():New()

aRetOrig:= FWGetSX5( "S0" )

For nPass := 1 To Len(aRetOrig)
    oRetOrig[Alltrim(aRetOrig[nPass,3])] := aRetOrig[nPass]
Next

aSize(aRetOrig,0)
aRetOrig := Nil

Return
