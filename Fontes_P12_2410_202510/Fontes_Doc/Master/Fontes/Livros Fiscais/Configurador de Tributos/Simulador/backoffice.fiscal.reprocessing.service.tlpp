#INCLUDE "TOTVS.CH"
#INCLUDE "TLPP-CORE.TH"

using namespace totvs.protheus.backoffice.fiscal.tciclass

namespace totvs.protheus.backoffice.fiscal.reprocessing

/*/{Protheus.doc} SimulatorReprocessingManager

	Gerenciador de Reprocessamento do Simulador
	Gerencia a rotina de Reprocessamento (MATA930) disponilizando dados e 
	manipulando fluxos.

	@author anedino.santos
	@since 03/03/2025
	@version 12.1.2510
/*/
class SimulatorReprocessingManager

    private data jReprocessingTaxes as json
    private data lF2BTeste as logical

	// construtor e destrutor
    public method new() Constructor
	public method destroy()

    public method setF2BTeste(lValue as logical)
    public method getF2BTeste()

	// fluxo e processamento
	public method runReprocessing(aPergunte as array)
    public method loadReprocessingTaxes()
	public method getTaxesAndAdjustments()

	private method loadTaxes()
	private method loadTaxesPerItem()

endclass

/*/{Protheus.doc} new

    Construtor que inicializa os atributos da classe.

    @author anedino.santos
    @since 24/03/2025
    @version 12.1.2410
    @return self, object, instância de SimulatorReprocessingManager
/*/
method new() class SimulatorReprocessingManager
	self:jReprocessingTaxes := JsonObject():New()
    self:jReprocessingTaxes["tributos"] := JsonObject():New()
    self:jReprocessingTaxes["itens"] := JsonObject():New()
    self:jReprocessingTaxes["ajustes"] := JsonObject():New()
return self


/*/{Protheus.doc} destroy

    Finaliza o objeto limpando os atributos.

    @author anedino.santos
    @since 24/03/2025
    @version 12.1.2410
    @return Nil, Nil, sem retorno
/*/
method destroy() class SimulatorReprocessingManager
    FwFreeObj(self:jReprocessingTaxes)
    self:jReprocessingTaxes := Nil
return


/*/{Protheus.doc} runReprocessing

    Executa o reprocessamento.

    @author anedino.santos
    @since 24/03/2025
    @version 12.1.2410
    @return Nil, Nil, sem retorno
/*/
method runReprocessing(aPergunte as array) class SimulatorReprocessingManager
	MATA930(.T., aPergunte, self)
return


/*/{Protheus.doc} loadReprocessingTaxes

    Carrega os dados dos tributos obtidos pelo reprocessamento.

    @author anedino.santos
    @since 24/03/2025
    @version 12.1.2410
    @return Nil, Nil, sem retorno
/*/
method loadReprocessingTaxes() class SimulatorReprocessingManager
	self:jReprocessingTaxes["tributos"] := self:loadTaxes()
	self:jReprocessingTaxes["itens"] := self:loadTaxesPerItem({"detalhe_livro"})
return


/*/{Protheus.doc} loadTaxes

    Carrega os dados dos tributos calculados pelo configurador de forma aglutinada.

    @author anedino.santos
    @since 24/03/2025
    @version 12.1.2410
    @return jTributes, json, objeto json com os dados dos tributos aglutinados.
/*/
method loadTaxes() class SimulatorReprocessingManager
    local oTCIPro as object
    local jTributes as json

    oTCIPro := TCIProcessing():New()
    jTributes := JsonObject():New()
    jTributes:fromJson(oTCIPro:getSpreadSheetData())

    FwFreeObj(oTCIPro)
    oTCIPro := Nil

Return jTributes

/*/{Protheus.doc} loadTaxesPerItem

    Carrega os dados dos tributos calculados pelo configurador por item.

    @author anedino.santos
    @since 24/03/2025
    @version 12.1.2410
    @aparam aDetail, array, array com os detalhes que devem ser considerados no retorno dos tributos.
    @return jTributes, json, objeto json com os dados dos tributos por item.
/*/
method loadTaxesPerItem(aDetail) class SimulatorReprocessingManager
    local oTCIPro as object
    local jTributes as json

    default aDetail := {}

    oTCIPro := TCIProcessing():New()
    jTributes := JsonObject():New()

    if len(aDetail) > 0
        oTCIPro:setDataItems(aDetail)
    endif

    jTributes:fromJson(oTCIPro:getDataItems())

    FwFreeObj(oTCIPro)
    oTCIPro := Nil

    aSize(aDetail, 0)
    aDetail := Nil
Return jTributes


/*/{Protheus.doc} getTaxesAndAdjustments

    Retorna os tributos e os lançamentos fiscais calculados pelo configurador.

    @author anedino.santos
    @since 24/03/2025
    @version 12.1.2410
    @return jTributes, json, objeto json com os dados dos tributos e lançamentos fiscais.
/*/
method getTaxesAndAdjustments() class SimulatorReprocessingManager
return self:jReprocessingTaxes


/*/{Protheus.doc} setF2Bteste

    Define se será considerada regras de calculo do configurador em fase de teste

    @author anedino.santos
    @since 24/03/2025
    @version 12.1.2410
    @return Nil, Nil, sem retorno
/*/
method setF2Bteste(lValue as logical) class SimulatorReprocessingManager
    self:lF2BTeste := lValue
return


/*/{Protheus.doc} getF2BTeste

    Retorna se considera ou não tributos do configurador em fase de teste.

    @author anedino.santos
    @since 24/03/2025
    @version 12.1.2410
    @return lF2BTeste, logical, define se considera regras em teste.
/*/
method getF2BTeste() class SimulatorReprocessingManager
return self:lF2BTeste
