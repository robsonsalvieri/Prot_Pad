#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"

/*/{Protheus.doc} FISGetFornecedor
Função responsável por retornar a consulta de Participantes

@type function
@author Alexandre Esteves
@since 23/09/2024
@version 1.0
@return Nil, Não há retorno
/*/
@GET(endpoint="/api/fiscal/v1/simulator/vendor", description='Realiza a consulta de todos os fornecedores')
Function FISGetFornecedor()

	Local jQueryParams  := oRest:GetQueryRequest()      as json
	Local jResponse     := GetResponse(jQueryParams)    as json

	oRest:SetKeyHeaderResponse("Content-Type", "application/json")
	oRest:SetStatusCode(200)
	oRest:SetResponse(jResponse)

	FWFreeObj(jQueryParams)
	FWFreeObj(jResponse)

Return

/*/{Protheus.doc} TAFGetFornecedorById
Função responsável por Realiza a consulta de um código de Fornecedor

@type function
@author Alexandre Esteves
@since 23/09/2024
@version 1.0
@return Nil, Não há retorno
/*/
@GET(endpoint="/api/fiscal/v1/simulator/vendor/:id", description='Realiza a consulta de um código de Fornecedor') 
Function FISGetFornecedorById()

	Local jPathParams   := oRest:GetPathParamsRequest()     as json
	Local jQueryParams  := oRest:GetQueryRequest()      as json
	Local jResponse     := GetResponse(jQueryParams, jPathParams)    as json

	oRest:SetKeyHeaderResponse("Content-Type", "application/json")
	oRest:SetStatusCode(200)
	oRest:SetResponse(jResponse)

	FWFreeObj(jPathParams)
	FWFreeObj(jResponse)

Return

/*/{Protheus.doc} GetResponse
Função responsável por retornar o JSON de resposta com os dados de Fornecedor

@type static function
@author Alexandre Esteves
@since 23/09/2024
@version 1.0
@param jQueryParams, json, Objeto JSON contendo os query params
@param jPathParams, json, Objeto JSON contendo os path params
@return json, Retorna um objeto JSON com os codigos dos Fornecedores
/*/
Static Function GetResponse(jQueryParams as json, jPathParams as json) as json

	Local aItems            := {}	as array
	Local cSA2              := ""	as character
	Local cId               := ""  	as character
	Local cFilter           := ""  	as character
	Local cFil				:= ""   as character
	Local iPage				:= 0	as integer
	Local iPageSize			:= 0	as integer
	Local lHasNext			:= .F.	as logical
	Local jItems            := Nil  as json
	Local jResponse         := Nil  as json

	Default jQueryParams    := Nil
	Default jPathParams     := Nil

	iPage 		:= 1
	iPageSize 	:= 30

	If jQueryParams != Nil

		If jQueryParams:HasProperty("filter")
			cFilter := AllTrim(jQueryParams["filter"])
		EndIf

		If jQueryParams:HasProperty("filial")
			cFil:= AllTrim(jQueryParams["filial"])
		EndIf

		If jQueryParams:HasProperty("page")
			iPage := Val(jQueryParams["page"])
		EndIf

		If jQueryParams:HasProperty("pageSize")
			iPageSize := Val(jQueryParams["pageSize"])
		EndIf
	EndIf

	If jPathParams != Nil
		If jPathParams:HasProperty("id")
			cId := AllTrim(jPathParams["id"])
		EndIf
	EndIf

	cSA2		:= GetFornecedorCodes(cId, cFilter, cFil, iPage, iPageSize)
	jResponse 	:= JSONObject():New()

	If !Empty(cSA2)
		While !(cSA2)->(EOF()) 
			If (cSA2)->ROW_NUMBER > iPageSize
				lHasNext := .T.
				Exit
			EndIf

			jItems 				  := JSONObject():New()
			jItems["id"]          := (cSA2)->A2_COD
			jItems["store"]       := (cSA2)->A2_LOJA
			jItems["description"] := AllTrim((cSA2)->A2_NOME)
			jItems["type"] 		  := ""

			AAdd(aItems, jItems)

			(cSA2)->(DbSkip())
		EndDo
	EndIf

	If jQueryParams != Nil
		jResponse["hasNext"]      := lHasNext
		jResponse["items"]        := aItems
		jResponse["nextPageHref"] := IIF(lHasNext, "/api/fiscal/v1/simulator/vendor?" + "page" + "=" + cValToChar(iPage + 1) + "&" + "pageSize" +  "=" + cValToChar(iPageSize), "")
	EndIf

	If jPathParams != Nil .And. !Empty(aItems)
		jResponse := aItems[1]
	EndIf

	(cSA2)->(DBCloseArea())

Return jResponse

/*/{Protheus.doc} GetFornecedorCodes
Função responsável por retornar um alias contendo os registros da consulta de Fornecedor

@type static function
@author Alexandre Esteves
@since 23/09/2024
@version 1.0
@param cId, character, Codigo do Fornecedor
@param cFilter, character, Filtro de pesquisa
@param iPage, integer, Página da consulta
@param iPageSize, integer, Quantidade de registros por página
@return character, Retorna um alias contendo os registros da consulta de Fornecedores
/*/
Static Function GetFornecedorCodes(cId as character, cFilter as character,  cFil as character, iPage as integer, iPageSize as integer) as character

	Local cQuery		:= ""   as character
	Local cSA2			:= ""   as character
	Local iBind			:= 0    as integer
	Local oQuery		:= Nil  as object

	Default cId    		:= ""
	Default cFilter     := ""
	Default cFil		:= ""
	Default iPage    	:= 1
	Default iPageSize	:= 30


	cQuery := " SELECT ROW_NUMBER() OVER (ORDER BY SA2.A2_COD) ROW_NUMBER, "
	cQuery += " 	SA2.A2_COD, SA2.A2_LOJA, SA2.A2_NOME "
	cQuery += " 	FROM (  "
	cQuery += " 		SELECT SA2.A2_COD, SA2.A2_LOJA, SA2.A2_NOME "
	cQuery += "     		FROM " + RetSQLName("SA2") + " SA2 "
	cQuery += "     		WHERE "
	
	If !Empty(cFil)
		cQuery += "         		 SA2.A2_FILIAL = ? AND "
	EndIf

	If !Empty(cId)
		cQuery += "         		 SA2.A2_COD = ? AND "
	EndIf

	If !Empty(cFilter)
		cQuery += "         		 SA2.A2_COD LIKE(?) AND "
	EndIf

	cQuery += "     		SA2.D_E_L_E_T_ = ? "

	If Empty(cId)
		cQuery += " 		ORDER BY SA2.A2_COD "
		cQuery += " 		OFFSET ? ROWS "
		cQuery += " 		FETCH NEXT ? ROWS ONLY "
	EndIf

	cQuery += " 	) SA2 "

	oQuery := FwExecStatement():New(cQuery)

	If oQuery != Nil

		If !Empty(cFil)
			oQuery:SetString(++iBind, xFilial("SA2",cFil))
		EndIf

		If !Empty(cId)
			oQuery:SetString(++iBind, cId)
		EndIf

		If !Empty(cFilter)
			oQuery:SetString(++iBind, cFilter + '%')
		EndIf

		oQuery:SetString(++iBind, " ")

		If Empty(cId)
			oQuery:SetUnsafe(++iBind, cValToChar((iPage - 1) * iPageSize))
			oQuery:SetUnsafe(++iBind, cValToChar(iPageSize + 1))
		EndIf

		cSA2 := oQuery:OpenAlias()
	EndIf

	oQuery:Destroy()
	oQuery:= Nil

Return cSA2
