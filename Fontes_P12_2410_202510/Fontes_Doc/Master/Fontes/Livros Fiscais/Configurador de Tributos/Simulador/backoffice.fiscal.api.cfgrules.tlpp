#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"

/*/{Protheus.doc} FISGetCfgRules
Função responsável por retornar a consulta de Participantes

@type function
@author Alexandre Esteves
@since 21/10/2024
@version 1.0
@return Nil, Não há retorno
/*/
@GET(endpoint="/api/fiscal/v1/simulator/cfgrules", description='Realiza a consulta de todas as regras de cálculo do Configurador de Tributos')
Function FISGetCfgRules()

	Local jQueryParams  := oRest:GetQueryRequest()      as json
	Local jResponse     := GetResponse(jQueryParams)    as json

	oRest:SetKeyHeaderResponse("Content-Type", "application/json")
	oRest:SetStatusCode(200)
	oRest:SetResponse(jResponse)

	FWFreeObj(jQueryParams)
	FWFreeObj(jResponse)

Return

/*/{Protheus.doc} FISGetCfgRulesById
Função responsável por Realiza a consulta de um código de Fornecedor

@type function
@author Alexandre Esteves
@since 21/10/2024
@version 1.0
@return Nil, Não há retorno
/*/
@GET(endpoint="/api/fiscal/v1/simulator/cfgrules/:id", description='Realiza a consulta de um código de regra de cálculo do Configurador de Tributos') 
Function FISGetCfgRulesById()

	Local jPathParams   := oRest:GetPathParamsRequest()     as json
	Local jQueryParams  := oRest:GetQueryRequest()      as json
	Local jResponse     := GetResponse(jQueryParams, jPathParams)    as json

	oRest:SetKeyHeaderResponse("Content-Type", "application/json")
	oRest:SetStatusCode(200)
	oRest:SetResponse(jResponse)

	FWFreeObj(jPathParams)
	FWFreeObj(jResponse)

Return

/*/{Protheus.doc} GetResponse
Função responsável por retornar o JSON de resposta com os dados das regras de cálculo 
do Configurador de tributos
@type static function
@author Alexandre Esteves
@since 21/10/2024
@version 1.0
@param jQueryParams, json, Objeto JSON contendo os query params
@param jPathParams, json, Objeto JSON contendo os path params
@return json, Retorna um objeto JSON com os codigos dos Fornecedores
/*/
Static Function GetResponse(jQueryParams as json, jPathParams as json) as json

	Local aItems            := {}	as array
	Local cF2B              := ""	as character
	Local cId               := ""  	as character
	Local cFilter           := ""  	as character
	Local cFil				:= ""   as character
	Local iPage				:= 0	as integer
	Local iPageSize			:= 0	as integer
	Local lHasNext			:= .F.	as logical
	Local jItems            := Nil  as json
	Local jResponse         := Nil  as json

	Default jQueryParams    := Nil
	Default jPathParams     := Nil

	iPage 		:= 1
	iPageSize 	:= 10

	If jQueryParams != Nil

		If jQueryParams:HasProperty("filter")
			cFilter := AllTrim(jQueryParams["filter"])
		EndIf

		If jQueryParams:HasProperty("filial")
			cFil:= AllTrim(jQueryParams["filial"])
		EndIf

		If jQueryParams:HasProperty("page")
			iPage := Val(jQueryParams["page"])
		EndIf

		If jQueryParams:HasProperty("pageSize")
			iPageSize := Val(jQueryParams["pageSize"])
		EndIf
	EndIf

	If jPathParams != Nil
		If jPathParams:HasProperty("id")
			cId := AllTrim(jPathParams["id"])
		EndIf
	EndIf

	cF2B		:= GetCfgRulesCodes(cId,cFilter,cFil, iPage, iPageSize)
	jResponse 	:= JSONObject():New()

	If !Empty(cF2B)
		While !(cF2B)->(EOF()) 
			If (cF2B)->ROW_NUMBER > iPageSize
				lHasNext := .T.
				Exit
			EndIf

			jItems 						:= JSONObject():New()
			jItems["id"]          		:= (cF2B)->F2B_REGRA
			jItems["ruledescription"] 	:= AllTrim((cF2B)->F2B_DESC)
			jItems["tax"]          		:= (cF2B)->F2B_TRIB
			jItems["taxdescription"]  	:= AllTrim((cF2B)->F2E_DESC)

			AAdd(aItems, jItems)

			(cF2B)->(DbSkip())
		EndDo
	EndIf

	If jQueryParams != Nil
		jResponse["hasNext"]      := lHasNext
		jResponse["items"]        := aItems
		jResponse["nextPageHref"] := IIF(lHasNext, "/api/fiscal/v1/simulator/cfgrules?" + "page" + "=" + cValToChar(iPage + 1) + "&" + "pageSize" +  "=" + cValToChar(iPageSize), "")
	EndIf

	If jPathParams != Nil .And. !Empty(aItems)
		jResponse := aItems[1]
	EndIf

	(cF2B)->(DBCloseArea())

Return jResponse

/*/{Protheus.doc} GetCfgRulesCodes
Função responsável por retornar um alias contendo os registros da consulta das regras de calculo do configurador de tributos

@type static function
@author Alexandre Esteves
@since 23/09/2024
@version 1.0
@param cId, character, Codigo da regra de calculo
@param cFilter, character, Filtro de pesquisa
@param iPage, integer, Página da consulta
@param iPageSize, integer, Quantidade de registros por página
@return character, Retorna um alias contendo os registros da consulta de Fornecedores
/*/
Static Function GetCfgRulesCodes(cId as character, cFilter as character, cFil as character, iPage as integer, iPageSize as integer) as character

	Local cQuery		:= ""   as character
	Local cF2B			:= ""   as character
	Local iBind			:= 0    as integer
	Local oQuery		:= Nil  as object

	Default cId    		:= ""
	Default cFilter     := ""
	Default cFil		:= ""
	Default iPage    	:= 1
	Default iPageSize	:= 10


	cQuery := " SELECT ROW_NUMBER() OVER (ORDER BY F2B.F2B_REGRA) ROW_NUMBER, "
	cQuery += " 	F2B.F2B_REGRA, F2B.F2B_DESC, F2B.F2B_TRIB, F2B.F2E_DESC "
	cQuery += " 	FROM ( "
	cQuery += " 		SELECT F2B.F2B_REGRA, F2B.F2B_DESC, F2B.F2B_TRIB, F2E.F2E_DESC "
	cQuery += "     		FROM " + RetSQLName("F2B") + " F2B "
	cQuery += "     		INNER JOIN " + RetSQLName("F2E") + " F2E ON ( F2E_FILIAL = ? AND F2B.F2B_TRIB = F2E.F2E_TRIB AND F2E.D_E_L_E_T_ = ? ) "

	cQuery += " WHERE "

	If !Empty(cFil)
		cQuery += " F2B.F2B_FILIAL = ? AND "
	EndIf

	If !Empty(cId)
		cQuery += " F2B.F2B_REGRA = ?  AND "
	EndIf

	If !Empty(cFilter)
		cQuery += " F2B.F2B_REGRA = ?  AND "
	EndIf

	cQuery += " F2B.F2B_ALTERA = '2' AND "

	cQuery += " F2B.D_E_L_E_T_ =  ? "

	If Empty(cId)
		cQuery += " 		ORDER BY F2B.F2B_REGRA "
		cQuery += " 		OFFSET ? ROWS "
		cQuery += " 		FETCH NEXT ? ROWS ONLY "
	EndIf

	cQuery += " 	) F2B "

	oQuery := FwExecStatement():New(cQuery)

	If oQuery != Nil

		oQuery:SetString(++iBind, xFilial("F2E"))
		oQuery:SetString(++iBind, " ") // F2E DELETE

		If !Empty(cFil)
			oQuery:SetString(++iBind, xFilial("F2B",cFil))
		Endif

		If !Empty(cId)
			oQuery:SetString(++iBind, cId)
		EndIf

		If !Empty(cFilter)
			oQuery:SetString(++iBind, cFilter)
		EndIf

		oQuery:SetString(++iBind, " ") // F2B DELETE

		If Empty(cId)
			oQuery:SetUnsafe(++iBind, cValToChar((iPage - 1) * iPageSize))
			oQuery:SetUnsafe(++iBind, cValToChar(iPageSize + 1))
		EndIf


		cF2B := oQuery:OpenAlias()
	EndIf

	oQuery:Destroy()
	oQuery:= Nil

Return cF2B
