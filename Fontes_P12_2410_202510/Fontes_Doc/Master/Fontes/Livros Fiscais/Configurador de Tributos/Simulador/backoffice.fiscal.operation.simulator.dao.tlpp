#INCLUDE "TLPP-CORE.TH"
#INCLUDE "tlpp-rest.th"
#Include "PROTHEUS.CH"

namespace totvs.protheus.backoffice.fiscal.simulator.dao

/*/{Protheus.doc} SimulatorInvoiceDAO
    Classe responsavel por fornecer os dados do movimento por periodo para as API's de serie, nota e participante 
    @author alexandre.esteves
    @since 15/01/2025
    @version 12.1.2510
/*/
class SimulatorInvoiceDAO

    Public Method new() Constructor 
    Public Method loadInvoices(nPage,nPageSize,cApiReq,cId,cfilter,cFil,cTpMov,cDtIni,cDtFim,cParticipante,cSerie) as character
    Public Method getTaxesPerItem(jInvoiceFilter) as character

    private Method validateInvoiceFilter(jInvoiceFilter) as logical

endclass

/*/{Protheus.doc} new
    Método construtor da classe.
    @author alexandre.esteves
    @since 23/01/2025
    @version 12.1.2510
    @param 
    @return Nil, Nil, Nil
/*/
Method new() class SimulatorInvoiceDAO
return nil

/*/{Protheus.doc} loadInvoices
    Método para retornar o Alias com os dados de consulta ao banco de dados 
    referente a requisição da API que o chamou.
    @author alexandre.esteves
    @since 23/01/2025
    @version 12.1.2510
    @param nPage         , numeric, numero da pagina
    @param nPageSize     , numeric, tamanho total da paginação
    @param cApiReq       , string , Variavél que indica de qual API esta sendo chamado a requisição.
    @param cId           , string , Id do registro do path params da requisição
    @param cFilter       , string , Filtro da Requisição
    @param cFil          , string , Código da Filial 
    @param cTpMov        , string , Tipo de Movimento E= Entrada, S= Saida
    @param cDtIni        , string , Data Inicial do Periodo da Movimentação
    @param cDtFim        , string , Data Final do Periodo da Movimentação
    @param cParticipante , string , Participante da Nota Fiscal
    @param cSerie        , string , série da nota fiscal
    @return cAlias       , String, Alias com o retorno da query
/*/

Method loadInvoices(nPage as numeric, ;
                    nPageSize as numeric, ;
                    cApiReq as character, ;
                    cId as character, ;
                    cFilter as character, ;
                    cFil as character, ;
                    cTpMov as character, ;
                    cDtIni as character, ;
                    cDtFim as character, ;
                    cParticipante as character, ;
                    cSerie as character ) as character class SimulatorInvoiceDAO

Local cQuery := ""  as character
Local cAlias := ""  as character
Local oQuery := Nil as object
Local nBind  := 0   as integer
Local cTipo  := 'N' as character

Default cParticipante := "" 
Default cSerie        := "" 


cQuery := " SELECT "

if cTpMov == 'E'

    If cApiReq == 'INVOICES'

        cQuery += ' SF1.F1_DOC NF , '
        cQuery += ' SF1.F1_SERIE SERIE , '
        cQuery += ' SF1.F1_FORNECE PARTICIPANTE, '
        cQuery += ' SF1.F1_LOJA LOJA, ' 
        cQuery += ' SF1.F1_ESPECIE ESPECIE , '
        cQuery += ' SF1.F1_DTDIGIT DTEMISSAO , '
        cQuery += ' SF1.F1_EST UF , ' 
        cQuery += ' SF1.F1_VALBRUT VALBRUT, ' 
        cQuery += ' SF1.F1_VALMERC VALMERC,  '
        cQuery += ' SA2.A2_NOME RAZAOSOCIAL  '

    endif

    If cApiReq == 'INVOICESERIES'
        cQuery += ' SF1.F1_SERIE SERIE '
    Endif

    If cApiReq == 'PARTICIPANT'
        cQuery += ' SF1.F1_FORNECE PARTICIPANTE , SF1.F1_LOJA LOJA, SA2.A2_NOME RAZAOSOCIAL  '
    Endif

    cQuery += " FROM " + RetSqlName('SF1') + " SF1 "

    If cApiReq == 'INVOICES' .OR.  cApiReq == 'PARTICIPANT'
    	cQuery += " INNER JOIN " + RetSQLName("SA2") + " SA2 ON ( SA2.A2_FILIAL = ? AND SF1.F1_FORNECE = SA2.A2_COD AND SF1.F1_LOJA = SA2.A2_LOJA AND SA2.D_E_L_E_T_ = ? ) "
    Endif

    cQuery += " WHERE  "
	cQuery += "   SF1.F1_FILIAL = ? AND "
    
    If !Empty(cId)
        If cApiReq == 'INVOICES'
		    cQuery += "  SF1.F1_DOC = ? AND "
        Endif
        If cApiReq == 'INVOICESERIES'
		    cQuery += "  SF1.F1_SERIE = ? AND "
        Endif
        If cApiReq == 'PARTICIPANT'
		    cQuery += "  SF1.F1_FORNECE = ? AND "
        Endif
	EndIf

	If !Empty(cFilter)
         If cApiReq == 'INVOICES'
		    cQuery += " SF1.F1_DOC LIKE(?) AND "
         Endif
         If cApiReq == 'INVOICESERIES'
		    cQuery += " SF1.F1_SERIE LIKE(?) AND "
         Endif
        If cApiReq == 'PARTICIPANT'
		    cQuery += " SF1.F1_FORNECE LIKE(?) AND "
         Endif
	EndIf

    cQuery += " SF1.F1_DTDIGIT >= ? AND "
    cQuery += " SF1.F1_DTDIGIT <= ? AND "
    if !Empty(cParticipante)
        cQuery += " SF1.F1_FORNECE = ? AND "
    Endif
    if !Empty(cSerie)
        cQuery += " SF1.F1_SERIE = ? AND "
    Endif

    cQuery += " SF1.F1_TIPO = ? AND "
    cQuery += " SF1.D_E_L_E_T_ = ? "

    If cApiReq == 'INVOICESERIES'
        cQuery += ' GROUP BY SF1.F1_SERIE '
    Endif

    If cApiReq == 'PARTICIPANT'
        cQuery += ' GROUP BY SF1.F1_FORNECE , SF1.F1_LOJA , SA2.A2_NOME '
    Endif
    
    
    If Empty( cId ) 
        cQuery += " ORDER BY 1 "
        cQuery += " OFFSET ( ? * ? ) ROWS "
        cQuery += " FETCH NEXT ? ROWS ONLY "
    EndIf

    oQuery := FwExecStatement():New(cQuery)

    If oQuery != Nil

        If cApiReq == 'INVOICES' .OR.  cApiReq == 'PARTICIPANT'
             oQuery:SetString(++nBind, xFilial("SA2"))
             oQuery:SetString(++nBind, " ")
        Endif

		oQuery:SetString(++nBind, xFilial("SF1",cFil))

		If !Empty(cId)
			oQuery:SetString(++nBind, cId)
		EndIf

		If !Empty(cFilter)
			oQuery:SetString(++nBind, cFilter + '%')
		EndIf

        oQuery:SetString(++nBind, StrTran(cDtIni, "-"))
        oQuery:SetString(++nBind, StrTran(cDtFim, "-"))

        If !Empty(cParticipante)
			oQuery:SetString(++nBind, cParticipante)
		EndIf
        If !Empty(cSerie)
			oQuery:SetString(++nBind, cSerie)
		EndIf

        oQuery:SetString(++nBind, cTipo)
		oQuery:SetString(++nBind, " ")

		If Empty(cId)
			oQuery:SetUnsafe(++nBind, cValToChar(nPage - 1))
            oQuery:SetUnsafe(++nBind, cValToChar(nPageSize))
			oQuery:SetUnsafe(++nBind, cValToChar(nPageSize + 1))
		EndIf

        cAlias := oQuery:OpenAlias()

    Endif

    oQuery:Destroy()
	oQuery:= Nil

Else

    If cApiReq == 'INVOICES'

        cQuery += ' SF2.F2_DOC NF , '
        cQuery += ' SF2.F2_SERIE SERIE , '
        cQuery += ' SF2.F2_CLIENTE PARTICIPANTE, '
        cQuery += ' SF2.F2_LOJA LOJA, '
        cQuery += ' SF2.F2_ESPECIE ESPECIE , ' 
        cQuery += ' SF2.F2_EMISSAO DTEMISSAO , '
        cQuery += ' SF2.F2_EST UF , ' 
        cQuery += ' SF2.F2_VALBRUT VALBRUT, ' 
        cQuery += ' SF2.F2_VALMERC VALMERC,  '
        cQuery += ' SA1.A1_NOME RAZAOSOCIAL  '

    endif

    If cApiReq == 'INVOICESERIES'
        cQuery += ' SF2.F2_SERIE SERIE '
    Endif

    If cApiReq == 'PARTICIPANT'
        cQuery += ' SF2.F2_CLIENTE PARTICIPANTE , SF2.F2_LOJA LOJA, SA1.A1_NOME RAZAOSOCIAL  '
    Endif

    cQuery += " FROM " + RetSqlName('SF2') + " SF2 "
    If cApiReq == 'INVOICES' .OR.  cApiReq == 'PARTICIPANT'
    	cQuery += " INNER JOIN " + RetSQLName("SA1") + " SA1 ON ( SA1.A1_FILIAL = ? AND SF2.F2_CLIENTE = SA1.A1_COD AND SF2.F2_LOJA = SA1.A1_LOJA AND SA1.D_E_L_E_T_ = ? ) "
    Endif
    
    cQuery += " WHERE  "
	cQuery += "   SF2.F2_FILIAL = ? AND "

    
    If !Empty(cId)
        If cApiReq == 'INVOICES'
		    cQuery += "  SF2.F2_DOC = ? AND "
        Endif
        If cApiReq == 'INVOICESERIES'
		    cQuery += "  SF2.F2_SERIE = ? AND "
        Endif
        If cApiReq == 'PARTICIPANT'
		    cQuery += "  SF2.F2_CLIENTE = ? AND "
        Endif
	EndIf

	If !Empty(cFilter)
         If cApiReq == 'INVOICES'
		    cQuery += " SF2.F2_DOC LIKE(?) AND "
         Endif
         If cApiReq == 'INVOICESERIES'
		    cQuery += " SF2.F2_SERIE LIKE(?) AND "
         Endif
        If cApiReq == 'PARTICIPANT'
		    cQuery += " SF2.F2_CLIENTE LIKE(?) AND "
        Endif
	EndIf

    cQuery += " SF2.F2_EMISSAO >= ? AND "
    cQuery += " SF2.F2_EMISSAO <= ? AND "
    if !Empty(cParticipante)
        cQuery += " SF2.F2_CLIENTE = ? AND "
    Endif
    if !Empty(cSerie)
        cQuery += " SF2.F2_SERIE = ? AND "
    Endif

    cQuery += " SF2.F2_TIPO = ? AND "
    cQuery += " SF2.D_E_L_E_T_ = ? "
    
    If cApiReq == 'INVOICESERIES'
        cQuery += ' GROUP BY SF2.F2_SERIE '
    Endif

    If cApiReq == 'PARTICIPANT'
        cQuery += ' GROUP BY SF2.F2_CLIENTE , SF2.F2_LOJA , SA1.A1_NOME '
    Endif
    
    If Empty( cId ) 
        cQuery += " ORDER BY 1 "
        cQuery += " OFFSET ( ? * ? ) ROWS "
        cQuery += " FETCH NEXT ? ROWS ONLY "
    EndIf

    oQuery := FwExecStatement():New(cQuery)

    If oQuery != Nil

        If cApiReq == 'INVOICES' .OR.  cApiReq == 'PARTICIPANT'
             oQuery:SetString(++nBind, xFilial("SA1"))
             oQuery:SetString(++nBind, " ")
        Endif

		oQuery:SetString(++nBind, xFilial("SF2",cFil))

		If !Empty(cId)
			oQuery:SetString(++nBind, cId)
		EndIf

		If !Empty(cFilter)
			oQuery:SetString(++nBind, cFilter + '%')
		EndIf

        oQuery:SetString(++nBind, StrTran(cDtIni, "-"))
        oQuery:SetString(++nBind, StrTran(cDtFim, "-"))

        If !Empty(cParticipante)
			oQuery:SetString(++nBind, cParticipante)
		EndIf
        If !Empty(cSerie)
			oQuery:SetString(++nBind, cSerie)
		EndIf

        oQuery:SetString(++nBind, cTipo)
		oQuery:SetString(++nBind, " ")

		If Empty(cId)
			oQuery:SetUnsafe(++nBind, cValToChar(nPage - 1))
            oQuery:SetUnsafe(++nBind, cValToChar(nPageSize))
			oQuery:SetUnsafe(++nBind, cValToChar(nPageSize + 1))
		EndIf

        cAlias := oQuery:OpenAlias()

    Endif

    oQuery:Destroy()
	oQuery:= Nil

Endif


return cAlias


/*/{Protheus.doc} getTaxesPerItem

    Consulta a tabela SFT retornando o alias com apenas os campos de tributos 
    conforme o parâmetro de filtro. Caso o parâmetro de filtro não esteja no 
    padrão esperado retornará uma string em branco (vazia).

    @author anedino.santos
    @since 20/03/2025
    @version 12.1.2410
    @param `jInvoiceFilter`, json, objeto json com os campos de filtro
        jInvoiceFilter["filial"]
        jInvoiceFilter["movimento"]
        jInvoiceFilter["serie"]
        jInvoiceFilter["numero"]
        jInvoiceFilter["participante"]
        jInvoiceFilter["loja"]
    @return cAlias, character, alias aberto e posicionado no primeiro registro.
/*/
Method getTaxesPerItem(jInvoiceFilter) class SimulatorInvoiceDAO
    local cQuery as character
    local oExecStat as object
    local cAlias as character

    if self:validateInvoiceFilter(jInvoiceFilter)
        cQuery += "SELECT FT_ITEM, FT_TIPO, FT_SERIE, FT_NFISCAL, FT_CLIEFOR, FT_ESPECIE,"
        cQUery += "B1_COD, B1_DESC, B1_POSIPI, "
        cQuery += "FT_RECISS,FT_CSTISS,FT_ALIQICM,FT_BASEICM,FT_VALICM,FT_ISENICM,FT_OUTRICM,"
        cQuery += "FT_ICMSCOM,FT_ICMSDIF,FT_CLASFIS,FT_ALIQSOL,FT_BASERET,FT_ICMSRET,FT_ISENRET,FT_OUTRRET,"
        cQuery += "FT_PDDES,FT_DIFAL,FT_BASEDES,FT_PDORI,"
        cQUery += "FT_BASEIPI,FT_VALIPI,FT_ISENIPI,FT_OUTRIPI,FT_ALIQIPI,FT_CTIPI,"
        cQuery += "FT_BASEPIS,FT_ALIQPIS,FT_VALPIS,FT_CSTPIS,"
        cQuery += "FT_BRETPIS,FT_VRETPIS,FT_ARETPIS,"
        cQuery += "FT_BASEPS3,FT_ALIQPS3,FT_VALPS3,"
        cQuery += "FT_BASECOF,FT_ALIQCOF,FT_VALCOF,FT_CSTCOF,"
        cQuery += "FT_BASECF3,FT_ALIQCF3,FT_VALCF3,"
        cQuery += "FT_BRETCOF,FT_VRETCOF,FT_ARETCOF,"
        cQuery += "FT_BASECSL,FT_ALIQCSL,FT_VALCSL,"
        cQuery += "FT_BRETCSL,FT_VRETCSL,FT_ARETCSL,"
        cQuery += "FT_BASEIRR,FT_ALIQIRR,FT_VALIRR,"
        cQuery += "FT_BASEINS,FT_ALIQINS,FT_VALINS,"
        cQuery += "FT_PERCINP,FT_VALINP,FT_BASEINP,"
        cQuery += "FT_BASECPB,FT_ALIQCPB,FT_VALCPB,"
        cQuery += "FT_VLCIDE ,FT_BASECID,FT_ALQCIDE,"
        cQuery += "FT_BASFECP,FT_ALQFECP,FT_VALFECP,"
        cQuery += "FT_BSFCPST,FT_ALFCPST,FT_VFECPST,"
        cQuery += "FT_BSFCCMP,FT_ALFCCMP,FT_VFCPDIF,"
        cQuery += "FT_VALFDS ,FT_PRFDSUL,"
        cQuery += "FT_ALQFUM ,FT_VALFUM ,"
        cQuery += "FT_BSEFET ,FT_ALQFET ,FT_VALFET ,"
        cQuery += "FT_BSEFAC ,FT_ALQFAC ,FT_VALFAC ,"
        cQuery += "FT_BSEFAB ,FT_ALQFAB ,FT_VALFAB ,"
        cQuery += "FT_ALSENAR,FT_VLSENAR,FT_BSSENAR,"
        cQuery += "FT_ALQFEEF,FT_VALFEEF,FT_BASFEEF,"
        cQuery += "FT_VALFASE,FT_BASFASE,FT_ALIFASE,"
        cQuery += "FT_ALIIMA ,FT_VALIMA ,FT_BASIMA ,"
        cQuery += "FT_BASFUND,FT_ALIFUND,FT_VALFUND,"
        cQuery += "FT_ALIQPRO,FT_VALPRO ,FT_BASEPRO,"
        cQuery += "FT_ALQFMD ,FT_BASEFMD,FT_VALFMD,"
        cQuery += "FT_ALQFMP ,FT_BASEFMP,FT_VALFMP,"
        cQuery += "FT_ALIQFUN,FT_BASEFUN,FT_VALFUN "
        cQuery += "FROM ?  SFT "
        cQuery += "INNER JOIN ?  SB1"
        cQuery += "     ON FT_FILIAL = ? "
        cQuery += "     AND B1_FILIAL = ? "
        cQuery += "     AND FT_PRODUTO = B1_COD "
        cQuery += "WHERE "
        cQuery +=     "FT_FILIAL = ? "
        cQuery += "AND FT_TIPOMOV = ? "
        cQuery += "AND FT_SERIE = ? "
        cQuery += "AND FT_NFISCAL = ? "
        cQuery += "AND FT_CLIEFOR = ? "
        cQuery += "AND FT_LOJA = ? "
        cQuery += "AND SFT.D_E_L_E_T_ = ? "
        cQuery += "ORDER BY FT_ITEM"
        oExecStat := FwExecStatement():New(cQuery)
        oExecStat:SetUnsafe(1, RetSqlName('SFT'))
        oExecStat:SetUnsafe(2, RetSqlName('SB1'))
        oExecStat:SetString(3, xFilial("SFT"))
        oExecStat:SetString(4, xFilial("SB1"))
        oExecStat:SetString(5, jInvoiceFilter["filial"])
        oExecStat:SetString(6, jInvoiceFilter["movimento"])
        oExecStat:SetString(7, jInvoiceFilter["serie"])
        oExecStat:SetString(8, jInvoiceFilter["numero"])
        oExecStat:SetString(9, jInvoiceFilter["participante"])
        oExecStat:SetString(10, jInvoiceFilter["loja"])
        oExecStat:SetString(11, ' ')
        cAlias := oExecStat:openAlias()

        oExecStat:destroy()
        oExecStat := Nil
    endif
return cAlias


/*/{Protheus.doc} validateInvoiceFilter
    (long_description)
    @author anedino.santos
    @since 20/03/2025
    @version 12.1.2410
    @param jInvoiceFilter, json, objeto json que será validado
    @return lRet, logical, confirmação da validação do jInvoiceFilter. 
    True (.T.) se válido; False (.F.) se inválido.
/*/
Method validateInvoiceFilter(jInvoiceFilter) class SimulatorInvoiceDAO
    local lRet := .F.
    if jInvoiceFilter <> nil .and.;
        jInvoiceFilter:hasProperty("filial")
        jInvoiceFilter:hasProperty("movimento")
        jInvoiceFilter:hasProperty("serie")
        jInvoiceFilter:hasProperty("numero")
        jInvoiceFilter:hasProperty("participante")
        jInvoiceFilter:hasProperty("loja")
        lRet := .T.
    endif
Return lRet
