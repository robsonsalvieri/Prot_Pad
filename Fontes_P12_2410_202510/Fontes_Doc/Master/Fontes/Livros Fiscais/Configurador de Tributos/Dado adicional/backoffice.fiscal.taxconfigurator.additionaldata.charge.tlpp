#include 'tlpp-core.th'

//------------------------------------------------------------------
/*/{Protheus.doc} ChargeCk2

Função que será chamada para dar carga/atualizar a tabela CK2 (Dados Adicionais)

@author Douglas Dourado
@since 15/05/2025
@version 12.1.2510
/*/
//------------------------------------------------------------------
Function ChargeCk2()
    Local nTotReg := GetSumReg() as numeric
    Local aBaseAddData := GetAddData() as array

    If nTotReg < Len(aBaseAddData)
        UpdBaseAddData( aBaseAddData )
    EndIf

    aSize(aBaseAddData,0)
    aBaseAddData := nil

Return 

//------------------------------------------------------------------
/*/{Protheus.doc} UpdBaseAddData

Função que irá atualizar a tabela CK2 (Dados Adicionais) com os Dados Adicionais que não estão gravados no banco.

@author Douglas Dourado
@since 15/05/2025
@version 12.1.2510
/*/
//------------------------------------------------------------------
Static Function UpdBaseAddData(aBaseAddData as array)
    Local nX as numeric    
    Local cType as character
    Local cCode as character
    Local cTpOrigin as character
    Local cCbox as character
    Local cF3 as character
    Local cTabGen as character
    Local cFunOrigin as character
    Local cDescri as character
    Local cOwner as character    
    Local aAreaAnt  := GetArea()
    
    //Laço na base dos dados adicionais, verificando se estão gravadas no banco. Se não estiver, então o banco será atualizado.
    For nX := 1 to Len(aBaseAddData)

        cType      := aBaseAddData[nX][1] //CK2_TIPO
        cCode      := aBaseAddData[nX][2] //CK2_CODIGO
        cTpOrigin  := aBaseAddData[nX][3] //CK2_TPORIG
        cCbox      := aBaseAddData[nX][4] //CK2_CBOX
        cF3        := aBaseAddData[nX][5] //CK2_CPDRA
        cTabGen    := aBaseAddData[nX][6] //CK2_TABGEN
        cFunOrigin := aBaseAddData[nX][7] //CK2_FUNORI
        cDescri    := aBaseAddData[nX][8] //CK2_DESCRI
        cOwner     := aBaseAddData[nX][9] //CK2_OWNER

        If CheckAddDataExist( cType, cCode, cOwner )
            InsertCK2( cType , cCode, cTpOrigin, cCbox, cF3, cTabGen, cFunOrigin, cDescri, cOwner )
        EndIf
    Next

    RestArea(aAreaAnt)
    aSize(aAreaAnt,0)
	aAreaAnt := nil
Return

//------------------------------------------------------------------
/*/{Protheus.doc} CheckAddDataExist

Função que irá verificar se o registro já existe na tabela CK2 (Dados Adicionais).

@author Douglas Dourado
@since 15/05/2025
@version 12.1.2510
/*/
//------------------------------------------------------------------
Static Function CheckAddDataExist(cType as character , cCode as character, cOwner as character)
    Local nCountReg as numeric
    Local cQuery as character
    Local oCK2 as object
    Local nOrder := 1 as numeric

    cQuery := "SELECT COUNT(CK2_CODIGO) CHECKREG "
    cQuery += "FROM ? "
    cQuery += " WHERE CK2_FILIAL = ? AND CK2_TIPO = ? AND CK2_CODIGO = ? AND CK2_OWNER = ? AND D_E_L_E_T_ = ? "    
    oCK2   := FwExecStatement():New()
    oCK2:SetQuery(cQuery)
    
    oCK2:setUnSafe(nOrder++, RetSQLName("CK2"))
    oCK2:SetString(nOrder++, xFilial('CK2'))
    oCK2:SetString(nOrder++, cType )
    oCK2:SetString(nOrder++, cCode )
    oCK2:SetString(nOrder++, cOwner )
    oCK2:SetString(nOrder++, ' ')

    nCountReg := oCK2:ExecScalar('CHECKREG')

    FREEOBJ( oCK2 )

Return nCountReg == 0

//------------------------------------------------------------------
/*/{Protheus.doc} InsertCK2

Função que irá gravar os dados adicionais na tabela CK2 (Dados Adicionais).

@author Douglas Dourado
@since 15/05/2025
@version 12.1.2510
/*/
//------------------------------------------------------------------
Static Function InsertCK2( cType, cCode, cTpOrigin, cCbox, cF3, cTabGen, cFunOri, cDescri, cOwner )

    dbSelectArea("CK2")
    
    RecLock("CK2",.T.)
        CK2->CK2_FILIAL	    := xFilial("CK2")
        CK2->CK2_TIPO	    := cType
        CK2->CK2_CODIGO	    := cCode
        CK2->CK2_TPORIG	    := cTpOrigin
        CK2->CK2_CBOX	    := cCbox
        CK2->CK2_CPDRA	    := cF3
        CK2->CK2_TABGEN	    := cTabGen
        CK2->CK2_FUNORI	    := cFunOri
        CK2->CK2_DESCRI	    := cDescri
        CK2->CK2_OWNER	    := cOwner        
    MsUnLock()
    
Return

//------------------------------------------------------------------
/*/{Protheus.doc} GetSumReg

Função que será chamada para contar o total de registros da tabela CK2 (Somente Dados Adicionais padrões da TOTVS).

@author Douglas Dourado
@since 15/05/2025
@version 12.1.2510
/*/
//------------------------------------------------------------------
Static Function GetSumReg()    
    Local nCountReg as numeric
    Local cQuery as character
    Local oCK2  as object
    Local nOrder := 1 as numeric

    cQuery := "SELECT COUNT(CK2.CK2_CODIGO) TOTAL FROM ? CK2 "
    cQuery += "WHERE CK2.CK2_FILIAL = ? AND CK2.CK2_OWNER = ? AND D_E_L_E_T_ = ? "    
    oCK2   := FwExecStatement():New()
    oCK2:SetQuery(cQuery)

    oCK2:setUnSafe(nOrder++, RetSQLName("CK2"))
    oCK2:SetString(nOrder++, xFilial('CK2') )
    oCK2:SetString(nOrder++, '1') // Somente Dados Adicionais padrões da TOTVS
    oCK2:SetString(nOrder++, ' ')

    nCountReg := oCK2:ExecScalar('TOTAL')

    FREEOBJ(oCK2)

Return nCountReg

//------------------------------------------------------------------
/*/{Protheus.doc} GetAddData

Função que irá retornar os dados adicionais que serão gravados na tabela CK2 (Dados Adicionais).
Os dados adicionais são os mesmos que estão na tabela CK2, porém com o CK2_OWNER = '1' (Somente Dados Adicionais padrões da TOTVS).

As posicoes do array são: {"CK2_TIPO","CK2_CODIGO","CK2_TPORIG","CK2_CBOX","CK2_CPDRA","CK2_TABGEN","CK2_FUNORI","CK2_DESCRI","CK2_OWNER"}

@author Douglas Dourado
@since 15/05/2025
@version 12.1.2510
/*/
//------------------------------------------------------------------
Static Function GetAddData()
    Local aBaseAdditionalData  := {} as array    
   
    // Dados Adicionais Regra de escrituração
    aadd(aBaseAdditionalData,{"1","MOTDESICMS","FT","","","","cBoxMotICM()","Motivo da desoneracao do ICMS.","1"}) 

    // Perfil de Operação (VInculados no MVC FISA165)   
    aadd(aBaseAdditionalData,{"2","INDNATFRET" ,"FT","","","","getOpcIndNat()", "Indica a natureza do frete contratado","1"})                   // F4_INDNTFR
    aadd(aBaseAdditionalData,{"2","REGIMESPEC" ,"C" ,"1=Sim","","","","Indica se a operacao deve ser tratada  como Regime Especial.","1"})// F4_RGESPCI/F4_RGESPST/F4_NORESP     
    aadd(aBaseAdditionalData,{"2","ICMSSTNFSA" ,"C" ,"1=Sim","","","","Informar se o ICMS-ST da operacao anterior sera demonstrado na NF de saida, conforme artigo 274 do RICMS/SP, artigo 271 do RICMS/PR e artigo 29 (anexo3) do RICMS/SC.","1"}) //F4_ART274

    
Return aBaseAdditionalData
