#INCLUDE "PROTHEUS.CH"
#INCLUDE 'TLPP-CORE.TH'
#INCLUDE 'TLPP-REST.TH'

Namespace totvs.protheus.backoffice.fiscal.procedures

#DEFINE DEF_SPS_FROM_TPH                "0" // o pacote ZSPS será obtido a partir da Central de Atualizações
#DEFINE DEF_SPS_FROM_RPO       	 		"1"	// O pacote .ZSPS será obtido a partir do RPO
#DEFINE DEF_SPS_UPDATED                 "0" // status é ATUALIZADO
#DEFINE DEF_SPS_NOT_INSTALLED           "2" // status é NÃO INSTALADO
#DEFINE DEF_SPS_USER_TEST               "4" // status é TESTE
#DEFINE DEF_SPS_NOT_COMPATIBLE          "5" // status é NÃO COMPATÍVEL
#DEFINE DEF_SPS_PRIME                   "6" // status é [EMERGENCIAL]
#DEFINE DEF_SPS_INNOVATION              "7" // status é [PILOTO]

/*/{Protheus.doc} FISProcedure
	Realiza o gerenciamento de procedures no ambiente do FIS
	@author 	Matheus Bispo
	@since 		09/09/2024
	@version 	12.1.2310
/*/
Class FISProcedure

	Private Data cSignature as character
	Private Data cProcess as character
	Private Data cCompany as character
	Private Data cFrom as character
	Private Data lCanUse as logical

	Public Data jStatus as json
	Public Data cError as character
	Public Data cLog as character

	Private Method GetSignature() as character
	Private Method GetProcess() as json
	Private Method GetCanUse()
	Private Method SetStatus()

    Public Method New() Constructor
	Public Method IsInstalled() as logical
	Public Method IsUpdated() as logical
	Public Method IsInvalidPatch() as logical
	Public Method Install() as logical
	Public Method Uninstall() as logical
	Public Method Update() as logical
	Public Method ProcedureLog() as character

EndClass

/*/{Protheus.doc} New
	Cria a instancia da classe FISProcedure em um objeto
	@param		cProcess, character, codigo do processo
	@param		cCompany, character, empresa em que esta o processo
	@return		Self, Obejct, proprio objeto criado
	@type		Method
	@author		Matheus Bispo
	@since		09/09/2024
	@version	12.1.2310
/*/
Method New(cProcess as character, cCompany as character) class FISProcedure

	Default cProcess := ""
	Default cCompany := cEmpAnt

	::cError	:= ""
	::cLog		:= ""
	::cCompany	:= cCompany
	::cProcess 	:= cProcess
	::cFrom		:= DEF_SPS_FROM_RPO

	::GetCanUse()
	::SetStatus()

Return Self

/*/{Protheus.doc} GetCanUse
	Verifica se a classe FISProcedure pode ser usada
	@param		nil, nil, sem parametros
	@return		nil, nil, sem retornos
	@type  		Method
	@author 	Matheus Bispo
	@since 		05/09/2024
	@version 	12.1.2310
/*/
Method GetCanUse() class FISProcedure

	::lCanUse := FindFunction("SPSMigrated") .And. SPSMigrated()
	
	If !::lCanUse
		::cError += "Falha ao iniciar o processo de instalação de stored procedures, pois o ambiente não possui ou não está migrado para o novo gerenciador de stored procedures." + CRLF
	EndIf

Return

/*/{Protheus.doc} SetStatus
	Atualiza o status da procedure no ambiente
	@param		nil, nil, sem parametros
	@return		nil, nil, sem retornos
	@type 		Method
	@author 	Matheus Bispo
	@since 		09/09/2024
	@version 	12.1.2310
/*/
Method SetStatus() class FISProcedure

	If ::lCanUse
		::jStatus := EngSPSStatus(::cProcess, ::cCompany)
		::jStatus["signature"] := ::GetSignature()
	EndIf

Return

/*/{Protheus.doc} GetSignature
	Retorna a assinatura da rotina relacionada à procedure
	@param		nil, nil, sem parametros
	@return		cSignature, character, assinatura do pacote
	@type		Method
	@author		Matheus Bispo
	@since		09/09/2024
	@version	12.1.2310
/*/
Method GetSignature() as character class FISProcedure
	
	Local cEntryPoint 	:= "" as character
	Local cSignature	:= "" as character

	If ::lCanUse
		cEntryPoint := "EngSPS" + ::cProcess + "Signature"

		If FindFunction(cEntryPoint)
			cSignature := &(cEntryPoint + "()")
		EndIf
	EndIf

Return cSignature

/*/{Protheus.doc} GetProcess
	Retorna o processo da procedure
	@type		Method
	@author		Matheus Bispo
	@since		09/09/2024
	@version	12.1.2310
/*/
Method GetProcess() as json class FISProcedure
Return EngSPSGetProcess(::cFrom, ::jStatus["process"], ::cCompany)

/*/{Protheus.doc} IsInstalled
	Retorna se a procedure está instalada no ambiente
	@param		nil, nil, sem parametros
	@return		lIsInstalled, logical, caso o pacote esteja instalado, retorna .T.  Caso nao esteja, retorna .F.
	@type		Method
	@author		Matheus Bispo
	@since		09/09/2024
	@version	12.1.2310
/*/
Method IsInstalled() as logical class FISProcedure

	Local lIsInstalled := .F. as logical

	If ::lCanUse
		lIsInstalled := ::jStatus["status"] != DEF_SPS_NOT_INSTALLED
		
		If !lIsInstalled
			::cLog := "O processo "+ ::jStatus["process"] + " NÃO ESTÁ INSTALADO na base para a empresa " + ::cCompany + CRLF
		EndIf
	EndIf

Return lIsInstalled

/*/{Protheus.doc} IsUpdated
	Retorna se a procedure está atualizada no ambiente
	@param		nil, nil, sem parametros
	@return		lIsUpdated, logical, caso o pacote esteja atualizado, retorna .T.  Caso esteja desatualizado, retorna .F.
	@type		Method
	@author		Matheus Bispo
	@since		09/09/2024
	@version	12.1.2310
/*/
Method IsUpdated() as logical class FISProcedure

	Local lIsUpdated := .F. as logical

	If ::lCanUse
		lIsUpdated := ::jStatus["status"] == DEF_SPS_UPDATED
		
		If lIsUpdated
			::cLog := "O processo "+ ::jStatus["process"] + " está ATUALIZADO na base para a empresa " + ::cCompany + CRLF
			::cLog += ::ProcedureLog("Padrão ROBOPATCH")
		EndIf
	EndIf

Return lIsUpdated

/*/{Protheus.doc} Install
	Realiza a instalação da procedure no ambiente
	@param		nil, nil, sem parametros
	@return		logical, logical, caso algum erro .F., caso de certo .T.
	@type		Method
	@author		Matheus Bispo
	@since		09/09/2024
	@version	12.1.2310
/*/
Method Install() as logical class FISProcedure

	Local jProcedure 	:= Nil as json
	Local jInstall		:= Nil as json

	If !::lCanUse
		Return .F.
	EndIf

	jProcedure := ::GetProcess()

	If jProcedure["status"] == "FALSE"
		::cError += "Falha ao validar a procedure " + ::jStatus["process"] + CRLF + jProcedure["error"] + CRLF

		Return .F.
	EndIf

	jInstall := EngSPSInstall(::jStatus["process"], ::cCompany, ::cFrom)

	If !Empty(jInstall["error"])
		::cError += "Falha ao instalar a procedure " + ::jStatus["process"] + jInstall["error"] + CRLF

		Return .F.
	EndIf

	::cLog += "Procedure " + ::jStatus["process"] + " INSTALADA com SUCESSO no Ambiente " + CRLF
	::cLog += ::ProcedureLog("Padrão ROBOPATCH")

Return .T.

/*/{Protheus.doc} ProcedureLog
	Monta uma string com informacoes da procedures instalada no ambiente
	@param 		cPatchType, character, Informacao sobre o tipo de pacote
	@return		cLog, character, mensagem com informacoes do pacote (Codigo processo, IDSPS, Assinatura, Rotina, Versao, Tipo do pacote, Data de geracao)
	@type		Method
	@author		Matheus Bispo
	@since		09/09/2024
	@version	12.1.2310
/*/
Method ProcedureLog(cPatchType as character) as character class FISProcedure
	Local cLog := "" as character

    cLog += "Propriedades do pacote de procedure instalado: " 	+ CRLF
    cLog += "Codigo do processo        : " + ::jStatus["process"] 		+ CRLF
    cLog += "IDSPS                     : " + ::jStatus["idsps"] 		+ CRLF
    cLog += "Assinatura do processo    : " + ::jStatus["signature"] 	+ CRLF
	cLog += "Rotina dona do processo   : " + ::jStatus["routine"] 		+ CRLF
    cLog += "Versao                    : " + ::jStatus["version"] 		+ CRLF
    cLog += "Tipo do pacote            : " + cPatchType					+ CRLF
    cLog += "Data de geracao do pacote : " + ::jStatus["generation"]
Return cLog
