#INCLUDE "Matr935.ch"
#INCLUDE "PROTHEUS.CH"
 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³FUNCAO    ³ MATR935  ³ Autor ³ Liber de Esteban      ³ Data ³ 25/05/2006 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Registro de Armazens Gerais                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION MATR935()

Local oReport
Local lVerpesssen := Iif(FindFunction("Verpesssen"),Verpesssen(),.T.)

Private aFilsCalc:={}
Private cFilBack	:= cFilAnt

If lVerpesssen
	If TRepInUse()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Interface de impressao                                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oReport := ReportDef()
		oReport:PrintDialog()
	Else
		MATR935R3()
	EndIf
	cFilant:=cFilBAck
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³FUNCAO    ³ReportDef ³ Autor ³ Liber de Esteban      ³ Data ³ 10/05/2006 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³DESCRICAO ³ Definicao do componente                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function ReportDef()

Local oReport
Local oCabec
Local oDoctos
Local oMovtos

Local nPosSD1 := 0
Local nPosSD2 := 0

Local cPicD1QTD := PesqPict( "SD1", "D1_QUANT" )
Local cPicD2QTD := PesqPict( "SD2", "D2_QUANT" )
Local cPictSaldo := RetPictSld( cPicD1QTD )
Local aSX3FldSD1 := FWSX3Util():GetListFieldsStruct( "SD1" )
Local aSX3FldSD2 := FWSX3Util():GetListFieldsStruct( "SD2" )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

oReport := TReport():New("MATR935",STR0019,"MTR935",; //"Registro de Armazens Gerais"
{|oReport| ReportPrint(oReport)},STR0020) //"Este programa ir  emitir o Registro de Armazens Gerais."

Pergunte("MTR935",.F.)
oReport:SetLandscape() 
oReport:SetTotalInLine(.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da secao utilizada pelo relatorio                               ³
//³                                                                        ³
//³TRSection():New                                                         ³
//³ExpO1 : Objeto TReport que a secao pertence                             ³
//³ExpC2 : Descricao da seçao                                              ³
//³ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   ³
//³        sera considerada como principal para a seção.                   ³
//³ExpA4 : Array com as Ordens do relatório                                ³
//³ExpL5 : Carrega campos do SX3 como celulas                              ³
//³        Default : False                                                 ³
//³ExpL6 : Carrega ordens do Sindex                                        ³
//³        Default : False                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao da celulas da secao do relatorio                                ³
//³                                                                        ³
//³TRCell():New                                                            ³
//³ExpO1 : Objeto TSection que a secao pertence                            ³
//³ExpC2 : Nome da celula do relatório. O SX3 será consultado              ³
//³ExpC3 : Nome da tabela de referencia da celula                          ³
//³ExpC4 : Titulo da celula                                                ³
//³        Default : X3Titulo()                                            ³
//³ExpC5 : Picture                                                         ³
//³        Default : X3_PICTURE                                            ³
//³ExpC6 : Tamanho                                                         ³
//³        Default : X3_TAMANHO                                            ³
//³ExpL7 : Informe se o tamanho esta em pixel                              ³
//³        Default : False                                                 ³
//³ExpB8 : Bloco de código para impressao.                                 ³
//³        Default : ExpC2                                                 ³
//³                                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao do Cabecalho do relatorio                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

oCabec := TRSection():New(oReport,STR0019,{"SM0"},{STR0021}/*{Array com as ordens do relatório}*/,/*Campos do SX3*/,/*Campos do SIX*/) //"Registro de Armazens Gerais"###"Un. Medida + Cod. Produto"

TRCell():New(oCabec,"M0_NOMECOM","SM0",STR0022,/*Picture*/,50,/*lPixel*/,/*{|| code-block de impressao }*/) //"Estabelecimento"
TRCell():New(oCabec,"PERIODO"   ,     ,STR0023,/*Picture*/,30,/*lPixel*/,{|| DTOC(mv_par01) + STR0038 + DTOC(mv_par02)}) //"Período"###" a "

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao dos documentos de entrada e saida                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oDoctos := TRSection():New(oReport,STR0039,{"TMP"},/*{Array com as ordens do relatório}*/,/*Campos do SX3*/,/*Campos do SIX*/) //"Movimentações"
oDoctos:SetTotalInLine(.F.)

TRCell():New(oDoctos,"TMP_DATA"  ,"TMP",STR0024,/*cPicture*/     ,10,/*lPixel*/,/*{|| code-block de impressao }*/) //"Data"
TRCell():New(oDoctos,"TMP_DOC"   ,"TMP",STR0025,/*cPicture*/     ,TamSX3("F2_DOC")[1],/*lPixel*/,/*{|| code-block de impressao }*/) //"Doc."
TRCell():New(oDoctos,"TMP_NOME"  ,"TMP",STR0026,/*cPicture*/     ,30,/*lPixel*/,/*{|| code-block de impressao }*/) //"Depositante"
TRCell():New(oDoctos,"TMP_LOCAL" ,"TMP",STR0027,/*cPicture*/     ,02,/*lPixel*/,/*{|| code-block de impressao }*/) //"Armaz."
TRCell():New(oDoctos,"TMP_LOTE"  ,"TMP",STR0028,/*cPicture*/     ,06,/*lPixel*/,/*{|| code-block de impressao }*/) //"Lote"
TRCell():New(oDoctos,"TMP_UM"    ,"TMP",       ,/*cPicture*/     ,02,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oDoctos,"TMP_DESC"  ,"TMP",STR0029,/*cPicture*/     ,30,/*lPixel*/,/*{|| code-block de impressao }*/) //"Descrição"
nPosSD1 := aScan(aSX3FldSD1, {|x| x[1] == "D1_QUANT" })
TRCell():New(oDoctos,"TMP_QUANTE","TMP",STR0030,cPicD1QTD, aSX3FldSD1[nPosSD1][3],/*lPixel*/,/*{|| code-block de impressao }*/) //"Entrada"
nPosSD2 := aScan(aSX3FldSD2, {|x| x[1] == "D2_QUANT" })
TRCell():New(oDoctos,"TMP_QUANTS","TMP",STR0031,cPicD2QTD, aSX3FldSD2[nPosSD2][3],/*lPixel*/,/*{|| code-block de impressao }*/) //"Saida"

TRCell():New(oDoctos,"SALDO"     ,     ,STR0032, cPictSaldo, Len( cPictSaldo ) - 3,/*lPixel*/,{|| nSaldo }) //"Saldo"
TRCell():New(oDoctos,"OBS"       ,     ,STR0033,/*cPicture*/     ,30,/*lPixel*/,/*{|| code-block de impressao }*/) //"Observações"

//Desabilita celula que nao sera impressa
oDoctos:Cell(6):Disable()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Secao do resumo das movimentacoes                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oMovtos := TRSection():New(oReport,STR0034,{"TMP"},/*{Array com as ordens do relatório}*/,/*Campos do SX3*/,/*Campos do SIX*/) //"Resumo de Movimetações"
oMovtos:SetTotalInLine(.F.)

TRCell():New(oMovtos,"PRODUTO",,STR0035,/*Picture*/      ,50,/*lPixel*/,{|| cProduto  }) //"Produto"
TRCell():New(oMovtos,"ENTRADA",,STR0030,cPictSaldo, Len( cPictSaldo) - 3,/*lPixel*/,{|| nEntradas }) //"Entrada"
TRCell():New(oMovtos,"SAIDA"  ,,STR0031,cPictSaldo, Len( cPictSaldo) - 3,/*lPixel*/,{|| nSaidas   }) //"Saida"
TRCell():New(oMovtos,"SALDO"  ,,STR0032,cPictSaldo, Len( cPictSaldo) - 3,/*lPixel*/,{|| nSaldo }) //"Saldo"

//Totalizador geral de entradas, saidas e saldo
oMovtos:SetTotalText(STR0036) //"Total de Movimentações"
TRFunction():New(oMovtos:Cell("ENTRADA"),NIL,"SUM",/*oBreak*/,/*cTitulo*/,/*cPicture*/,,.T.,.F.,.F.)
TRFunction():New(oMovtos:Cell("SAIDA")  ,NIL,"SUM",/*oBreak*/,/*cTitulo*/,/*cPicture*/,,.T.,.F.,.F.)
TRFunction():New(oMovtos:Cell("SALDO")  ,NIL,"SUM",/*oBreak*/,/*cTitulo*/,/*cPicture*/,,.T.,.F.,.F.)

Return(oReport)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportPrint³ Autor ³Liber de Esteban       ³ Data ³10/05/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os  ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Report do Relatório                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport)

Local oBreak
Local oTotaliz
Local oCabec  := oReport:Section(1)
Local oDoctos := oReport:Section(2)
Local oMovtos := oReport:Section(3)

Local cUmAnt     :=	""
Local nPosProd   :=	0
Local i          :=	0
Local aMovimento :=	{}
Local lAcUm      := (mv_par09==1)
Local nRecSM0	:=SM0->(Recno())

Private nSaldo	 :=	 mv_par03
Private cProduto, nEntradas, nSaidas
Private cArqTemp, cArqIndx
Private lModoComp := FWModeAccess("SB1",3)=="C"  // Referente ao X2_MODO

// Ponto de entrada criado para que seja possivel aglutinar por CNPJ+IE mesmo que o SB1 seja EXCLUSIVO 
If ExistBlock("MTR935C")
	lModoComp := ExecBlock("MTR935C",.F.,.F.)
EndIf

IF MV_PAR10==1
	IF MV_PAR11==1
		If lModoComp
			aFilsCalc	:= MatFilCalc(mv_par10 == 1,,,mv_par11 == 1,,2) //Parametro Exp6 Valida apenas CNPJ+IE iguais
			If Len(aFilsCalc)==0
				aFilsCalc:={{.F.,cFilAnt}}
			Endif
		Else 
			//'MATR935MODO' ; 'Para utilização de Seleção de Filiais a Tabela de Produtos deve estar compartilhada. Para este relatório será utilizado a filial corrente [' ; ']!'
  			Help(nil,1,STR0040,nil,STR0041+cFilAnt+STR0042,3,0)
			aFilsCalc:={{.T.,cFilAnt}}
		Endif
	Else  
		//'MATR935CNPJ' ; 'A seleção de filiais esta disponivel apenas em conjunto com a opção de Aglutinar por CNPJ+IE. Para este relatório será utilizado a filial corrente [' ; ']!'
  		Help(nil,1,STR0043,nil,STR0044+cFilAnt+STR0042,3,0)
		aFilsCalc:={{.T.,cFilAnt}}
	Endif
Else
	aFilsCalc:={{.T.,cFilAnt}}
Endif
CriaTMP()
If aFilsCalc[1][2]<>cFilBack
	SM0->(DbSeek(cEmpAnt+aFilsCalc[1][2]))
Endif

If lAcUm
	//Totalizador de saldo por quebra de unidade de medida
	oBreak := TRBreak():New(oDoctos,oDoctos:Cell(06),STR0037 + cUmAnt,.F.) //"Total Por Unidade de Medida"
	oTotaliz:=TRFunction():New(oDoctos:Cell(10),NIL,"SUM",oBreak,/*cTitulo*/,/*cPicture*/,{|| (nEntUm - nSaiUm) },.F.,.F.)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime Cabecalho do relatorio                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oCabec:Init()
oCabec:PrintLine()
oCabec:Finish()
oReport:SkipLine()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime Arquivo Temporario                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TMP")
TMP->(dbGotop())

oDoctos:Init()

oReport:SetMeter(TMP->(LastRec()))

While !oReport:Cancel() .And. !(TMP->(Eof()))

	If lAcum // Se totaliza por unidade de medida
		cUmAnt := TMP->TMP_UM
		nEntUm := 0
		nSaiUm := 0
		nSaldo := 0
		While !Eof() .And. cUmAnt==TMP->TMP_UM
			nSaldo+=(TMP->TMP_QUANTE-TMP->TMP_QUANTS)
			nPosProd:=Ascan(aMovimento,{|x|x[1]==TMP->TMP_COD})
			If nPosProd>0
				aMovimento[nPosProd,2]+=TMP->TMP_QUANTE
				aMovimento[nPosProd,3]+=TMP->TMP_QUANTS
			Else
				AADD(aMovimento,{TMP->TMP_COD,TMP->TMP_QUANTE,TMP->TMP_QUANTS,TMP->TMP_DESC})
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Acumula total por Unidade de Medida.           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nEntUm := TMP->TMP_QUANTE
			nSaiUm := TMP->TMP_QUANTS
			oDoctos:PrintLine()
			oReport:IncMeter()
			dbSkip()
		EndDo		
	Else
		nSaldo+=(TMP->TMP_QUANTE-TMP->TMP_QUANTS)
		nPosProd:=Ascan(aMovimento,{|x|x[1]==TMP->TMP_COD})
		If nPosProd>0
			aMovimento[nPosProd,2]+=TMP->TMP_QUANTE
			aMovimento[nPosProd,3]+=TMP->TMP_QUANTS
		Else
			AADD(aMovimento,{TMP->TMP_COD,TMP->TMP_QUANTE,TMP->TMP_QUANTS,TMP->TMP_DESC})
		Endif
					
		oDoctos:PrintLine()
		oReport:IncMeter()
		dbSkip()
	EndIf

EndDo

oDoctos:Finish()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime Total de Movimentacoes                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nSaldo := 0
If Len(aMovimento)>0
	oMovtos:Init()
	For i:=1 to Len(aMovimento)
		cProduto  :=	aMovimento[i,4]
		nEntradas :=	aMovimento[i,2]
		nSaidas   :=	aMovimento[i,3]
		nSaldo	  :=	aMovimento[i,2]-aMovimento[i,3]
		oMovtos:PrintLine()
	Next
	oMovtos:Finish()
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Fecha tabela e deleta arquivos temporarios                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TMP")
dbCloseArea()
Ferase(cArqTemp+GetDBExtension())
Ferase(cArqIndx+OrdBagExt())

SM0->(DbGoto(nRecSM0))
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MATR935R3 ³ Autor ³ Juan Jose Pereira       ³ Data ³ 04.12.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Registro de Armazens Gerais (Release 3)                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ MATR935(void)                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Marcos Simidu³25/09/98³11796A³ Incl.parametro p/ def. se acumula unid.  ³±±
±±³ Marcos Simidu³03/11/98³XXXXXX³Acertos no ano com 4 bytes.               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Matr935R3()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local 	titulo		:=	STR0001 // "Registro de Armazens Gerais"
Local 	cDesc1		:=	STR0002 // "Este programa ir  emitir o Registro de Armazens Gerais."
Local 	cDesc2		:=	"", cDesc3 :="", wnrel, tamanho :="G", cString :="SD1"

Private aReturn		:=	{ STR0003, 1,STR0004, 1, 2, 1, "",1 } //"Zebrado"###"Administra‡ao"
Private aOrd		:=	""

Private cPerg		:=	"MTR935"
Private nomeprog	:=	"MATR935", nLastKey := 0
Private Limite		:=	220

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(cPerg,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01        	// Data de                               ³
//³ mv_par02        	// Data ate                              ³
//³ mv_par03        	// Saldo Inicial                         ³
//³ mv_par04        	// Folha Inicial                         ³
//³ mv_par05        	// De Produto                            ³
//³ mv_par06        	// Ate Produto                           ³
//³ mv_par07        	// Do Almox.                             ³
//³ mv_par08        	// Ate almoxarifado                      ³
//³ mv_par09        	// Totaliza p/ Unidade de Medida.        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private lModoComp := FWModeAccess("SB1",3)=="C"  // Referente ao X2_MODO

// Ponto de entrada criado para que seja possivel aglutinar por CNPJ+IE mesmo que o SB1 seja EXCLUSIVO 
If ExistBlock("MTR935C")
	lModoComp := ExecBlock("MTR935C",.F.,.F.)
EndIf

IF MV_PAR10==1
	IF MV_PAR11==1
		If lModoComp
			aFilsCalc	:= MatFilCalc(mv_par10 == 1,,,mv_par11 == 1,,2) //Parametro Exp6 Valida apenas CNPJ+IE iguais
		Else
			//'MATR935MODO' ; 'Para utilização de Seleção de Filiais a Tabela de Produtos deve estar compartilhada. Para este relatório será utilizado a filial corrente [' ; ']!'
  			Help(nil,1,STR0040,nil,STR0041+cFilAnt+STR0042,3,0)
			aFilsCalc:={{.T.,cFilAnt}}
		Endif
	Else
		//'MATR935CNPJ' ; 'A seleção de filiais esta disponivel apenas em conjunto com a opção de Aglutinar por CNPJ+IE. Para este relatório será utilizado a filial corrente [' ; ']!'
  		Help(nil,1,STR0043,nil,STR0044+cFilAnt+STR0042,3,0)
		aFilsCalc:={{.T.,cFilAnt}}
	Endif
Else
	aFilsCalc:={{.T.,cFilAnt}}
Endif
wnrel    		:=	"MATR935"            //Nome Default do relatorio em Disco
wnrel			:=	SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,"",.F.,Tamanho)
If nLastKey==27
	dbClearFilter()
	Return
Endif
SetDefault(aReturn,cString)
If nLastKey==27
	dbClearFilter()
	Return
Endif           

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa Relatorio                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RptStatus({|lEnd| R935ImpRel(@lEnd,wnRel,cString,Tamanho)},titulo)

If aReturn[5]==1
	Ourspool(wnrel)
Endif

MS_FLUSH()

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³R935ImpRel| Autor ³ Juan Jose Pereira     ³ Data ³ 01.11.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao executora do MATR980                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ R9350Imp                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R935ImpRel(lEnd,WnRel,cString,Tamanho)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao de variaveis                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aLinha	:=	{ }
Private nFolha

Lin :=	80
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Faz manualmente porque nao chama a funcao Cabec()                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
@ 0,0 PSAY AvalImp(Limite)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime o relatorio atraves de Looping que ordena os arquivos ³
//³ do SD1 e SD2 para que surjam por ordem  de  data  de  emissao ³
//³ dentro do periodo especificado em MV_PAR01 e MV_PAR02.        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dDtIni	:=	mv_par01
dDtFim	:=	mv_par02
nSaldo	:=	mv_par03
nFolha	:=	mv_par04
lTotUM	:=	(mv_par09==1)		// Def. se totaliza relatorio p/ Unid. Medida

Lin 	:=	R935Cabec(Lin)

If lTotUm
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Imprime relatorio totalizando por Unidade de Medida.          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	R935ImpUm()
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Lista relatorio sem considerear Unidade de Medida.            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	R935Imp()
Endif

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R935Cabec³ Autor ³ Juan Jose Pereira     ³ Data ³ 04.12.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime Cabecalho do Relatorio                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ R935Cabec(Lin)                                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R935Cabec(Lin)
Lin :=	0

@	Lin,000 PSAY chr(15)
@ ++Lin,000 PSAY Replicate("-",206)
@ ++Lin,000 PSAY "|"
@   Lin,205 PSAY "|"
@ ++Lin,000 PSAY STR0005+Dtoc(dDtIni)+STR0006+Dtoc(dDtFim)+STR0007 //"|  PERIODO : "###" a "###"                                             R E G I S T R O   D E   A R M A Z E N S   G E R A I S"
@   Lin,205 PSAY "|"
@ ++Lin,000 PSAY STR0008+StrZero(nFolha,5) //"|  FOLHA   : "
@   Lin,205 PSAY "|"
@ ++Lin,000 PSAY Replicate("-",206)
@ ++Lin,000 PSAY STR0009 //"|            |          |                                            |      |      |                             MERCADORIAS     VOLUMES                                 |                                   |"
@ ++Lin,000 PSAY STR0010 //"|    DATA    |   DOC.   |  D E P O S I T A N T E                     |ALMOX.| LOTE |-------------------------------------------------------------------------------------|                                   |"
@ ++Lin,000 PSAY STR0011 //"|            |  NUMERO  |                                            |      |      |  DESCRICAO                       |       ENTRADAS |         SAIDAS |          SALDO |  O B S E R V A C O E S            |"
@ ++Lin,000 PSAY Replicate("-",206)
//                         1         2         3         4         5         6         7         8         9         C         1         2         3         4         5         6         7         8         9         D         1         2
//               01234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890

Lin++

Return(Lin)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³  R935Imp ³ Autor ³     Marcos Simidu     ³ Data ³ 25.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime relatorio - Nao considera UM.                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R935Imp(lEnd,WnRel,cString,Tamanho)

Local aMovimento	:=	{}
Local nPosProd		:=	0

Local nEntradas		:=	0
Local nSaidas		:=	0
Local i				:=	0

Local cPicD1QTD := PesqPict( "SD1", "D1_QUANT" )
Local cPicD2QTD := PesqPict( "SD2", "D2_QUANT" )
Local cPictSaldo := RetPictSld( cPicD1QTD )

Private cArqTemp, cArqIndx

CriaTMP(lEnd)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime Arquivo Temporario                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TMP")
TMP->(dbGotop())
SetRegua(LastRec())

While !(TMP->(Eof())) .And. !lEnd

	IncRegua()
	If Interrupcao(@lEnd)
		Loop
	Endif
        
	nSaldo+=TMP->TMP_QUANTE - TMP->TMP_QUANTS
	nPosProd	:=	Ascan(aMovimento,{|x|x[1]==TMP->TMP_COD}) //cod.produto
	
	If nPosProd>0
		aMovimento[nPosProd,2]+=TMP->TMP_QUANTE
		aMovimento[nPosProd,3]+=TMP->TMP_QUANTS
	Else
		AADD(aMovimento,{TMP->TMP_COD,TMP->TMP_QUANTE,TMP->TMP_QUANTS})
	Endif

	@ Lin++,000 PSAY "| "+StrZero (Day(TMP->TMP_DATA), 2)+"/"+StrZero (Month (TMP->TMP_DATA), 2)+"/"+StrZero (Year (TMP->TMP_DATA), 4)+;
					" |  "+TMP->TMP_DOC+"  |  "+;
					Substr(TMP->TMP_NOME,1,40)+;
					"  |  "+TMP->TMP_LOCAL+"  |      |  "+Substr(TMP->TMP_DESC,1,30)+;
					Iif(TMP->TMP_QUANTE > 0, "  | "+Transform(TMP->TMP_QUANTE,cPicD1QTD)+ " |                | ", "  |                | " +;
     				Transform(TMP->TMP_QUANTS,cPicD2QTD) + " | " ) +Transform(nSaldo,cPictSaldo)+ " |                                   |";
				
	If Lin > 58
		@ Lin,000 PSAY Replicate("-",206)
		nFolha++
		Lin	:=	R935Cabec(Lin)
	Endif
	
    TMP->(DbSkip())
Enddo

If Len(aMovimento)>0
	If 58-Lin<Len(aMovimento)+4
		@ Lin,000 PSAY Replicate("-",206)
		Lin	:=	0
		nFolha++
		@ Lin  ,000 PSAY Chr(15)
		@ ++Lin,000 PSAY Replicate("-",206)
		@ ++Lin,000 PSAY "|"
		@   Lin,205 PSAY "|"
		@ ++Lin,000 PSAY STR0005+Dtoc(dDtIni)+STR0006+Dtoc(dDtFim)+STR0007 //"|  PERIODO : "###" a "###"                                             R E G I S T R O   D E   A R M A Z E N S   G E R A I S"
		@   Lin,205 PSAY "|"
		@ ++Lin,000 PSAY STR0008+StrZero(nFolha,5) //"|  FOLHA   : "
		@   Lin,205 PSAY "|"
		@ ++Lin,000 PSAY Replicate("-",206)
	Endif
	@ Lin,00 PSAY Replic("-",206)
	Lin++
	@ Lin,00 PSAY STR0012 //"| TOTAL DE MOVIMENTACOES                                                                                                                                                                                     |"
	Lin++
	@ Lin,00 PSAY Replic("-",206)
	Lin++
	@ Lin,00 PSAY STR0013 //"| PRODUTO                                                                                                             |       ENTRADAS |         SAIDAS |                                                    |"
	Lin++
	@ Lin,00 PSAY Replic("-",206)
	For i:=1 to Len(aMovimento)
	
		If Lin > 58
			@ ++Lin,000 PSAY Replicate("-",206)
			Lin	:=	0
			nFolha++
			@ Lin  ,000 PSAY Chr(15)
			@ ++Lin,000 PSAY Replicate("-",206)
			@ ++Lin,000 PSAY "|"
			@   Lin,205 PSAY "|"
			@ ++Lin,000 PSAY STR0005+Dtoc(dDtIni)+STR0006+Dtoc(dDtFim)+STR0007 //"|  PERIODO : "###" a "###"                                             R E G I S T R O   D E   A R M A Z E N S   G E R A I S"
			@   Lin,205 PSAY "|"
			@ ++Lin,000 PSAY STR0008+StrZero(nFolha,5) //"|  FOLHA   : "
			@   Lin,205 PSAY "|"
			@ ++Lin,000 PSAY Replicate("-",206)
		Endif
		SB1->(dbSeek(xFilial("SB1")+aMovimento[i,1]))
		cProduto	:=	SB1->B1_DESC
		cEntradas	:=	Transform(aMovimento[i,2],cPictSaldo)
		cSaidas		:=	Transform(aMovimento[i,3],cPictSaldo)
		Lin++
		@ Lin,00 PSAY "| "+cProduto+"                                                                                      | "+cEntradas+" | "+cSaidas+" |                                                    |"
		nEntradas	+=	aMovimento[i,2]
		nSaidas		+=	aMovimento[i,3]
	Next
	cEntradas	:=	Transform(nEntradas,cPictSaldo)
	cSaidas		:=	Transform(nSaidas,cPictSaldo)
	Lin++
	@ Lin,00 PSAY Replic("-",206)
	Lin++
	@ Lin,00 PSAY STR0014+cEntradas+" | "+cSaidas+" |                                                    |" //"| TOTAL                                                                                                               | "
Endif
Lin++
@ Lin,000 PSAY Replicate("-",206)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Fecha tabela e deleta arquivo e indice temporario            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TMP")
dbCloseArea()
Ferase(cArqTemp+GetDBExtension())
Ferase(cArqIndx+OrdBagExt())

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³R935ImpUm ³ Autor ³     Marcos Simidu     ³ Data ³ 25.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime relatorio - Totaliza por Unidade de Medida.        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R935ImpUm(lEnd,WnRel,cString,Tamanho)

Local cUmAnt		:=	""
Local cAcUM			:=	""

Local cEntradas		:=	""
Local cSaidas		:=	""
Local cSaldo		:=	""

Local nPosProd		:=	0
Local nAcUM			:=	0

Local i				:=	0

Local aMovimento	:=	{}

Local cPicD1QTD := PesqPict( "SD1", "D1_QUANT" )
Local cPicD2QTD := PesqPict( "SD2", "D2_QUANT" )
Local cPictSaldo := RetPictSld( cPicD1QTD )

Private lModoSB1 := FWModeAccess("SB1",3)=="C"  // Referente ao X2_MODO
Private cArqTemp, cArqIndx

CriaTMP(lEnd)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime Arquivo Temporario                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TMP")
TMP->(dbGotop())
SetRegua(LastRec())

While !(TMP->(Eof())) .And. !lEnd

	IncRegua()
	If Interrupcao(@lEnd)
		Loop
	Endif

	cUmAnt	:=TMP->TMP_UM
	nAcUm	:=0	 // Var. p/ totalizar Unidade de Medida.
	nSaldo	:=0
	
	While !Eof().And.cUmAnt==TMP->TMP_UM
		nSaldo+=(TMP->TMP_QUANTE-TMP->TMP_QUANTS)
		nPosProd:=Ascan(aMovimento,{|x|x[1]==TMP->TMP_COD})
		If nPosProd>0
			aMovimento[nPosProd,2]+=TMP->TMP_QUANTE
			aMovimento[nPosProd,3]+=TMP->TMP_QUANTS
		Else
			AADD(aMovimento,{TMP->TMP_COD,TMP->TMP_QUANTE,TMP->TMP_QUANTS,TMP->TMP_DESC})
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Acumula total por Unidade de Medida.           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nAcUm+=(TMP->TMP_QUANTE-TMP->TMP_QUANTS)
					
		@ Lin++,000 PSAY "| "+StrZero (Day(TMP->TMP_DATA), 2)+"/"+StrZero (Month (TMP->TMP_DATA), 2)+"/"+StrZero (Year (TMP->TMP_DATA), 4)+;
		" |  "+TMP->TMP_DOC+"  |  "+ Substr(TMP->TMP_NOME,1,40)+;
		"  |  "+TMP->TMP_LOCAL+"  |      |  "+Substr(TMP->TMP_DESC,1,30)+;
		"  | "+Transform(TMP->TMP_QUANTE,cPicD1QTD)+;
		" | "+Transform(TMP->TMP_QUANTS,cPicD2QTD)+;
		" |"+Transform(nSaldo,cPictSaldo)+;
		"  |                                   |"
		If Lin > 58
			@ Lin,000 PSAY Replicate("-",206)
			nFolha++
			Lin:=R935Cabec(Lin)
		Endif
		dbSkip()
	EndDo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Imprime totais por unidade de medida.         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//	If Abs(nAcUm) > 0
		@ Lin,00 PSAY Replic("-",206)
		cAcUm:=Transform(nAcUm,"@E 999,999,999.99")
		Lin++
		@ Lin,000 PSAY STR0017+cUmAnt //"| TOTAL POR UNID. MEDIDA "
		@ Lin,152 PSAY "|"+cAcUm+"  |"
		@ Lin,205 PSAY "|"
		Lin++
		@ Lin,00 PSAY Replic("-",206)
		Lin++
//	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Movimenta a Regua de processamento de registros              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IncRegua()
EndDo

If Len(aMovimento)>0
	If 58-Lin<Len(aMovimento)+4
		Lin:=0
		nFolha++
		@ Lin  ,000 PSAY Chr(15)
		@ ++Lin,000 PSAY Replicate("-",206)
		@ ++Lin,000 PSAY "|"
		@   Lin,205 PSAY "|"
		@ ++Lin,000 PSAY STR0005+Dtoc(dDtIni)+STR0006+Dtoc(dDtFim)+STR0007 //"|  PERIODO : "###" a "###"                                             R E G I S T R O   D E   A R M A Z E N S   G E R A I S"
		@   Lin,205 PSAY "|"
		@ ++Lin,000 PSAY STR0008+StrZero(nFolha,5) //"|  FOLHA   : "
		@   Lin,205 PSAY "|"
		@ ++Lin,000 PSAY Replicate("-",206)
	Endif
	@ Lin,00 PSAY Replic("-",206)
	Lin++
	@ Lin,00 PSAY STR0012 //"| TOTAL DE MOVIMENTACOES                                                                                                                                                                                     |"
	Lin++
	@ Lin,00 PSAY Replic("-",206)
	Lin++
	@ Lin,00 PSAY STR0018 //"| PRODUTO                                                                                                             |       ENTRADAS |         SAIDAS |          SALDO |                                   |"
	Lin++
	@ Lin,00 PSAY Replic("-",206)
	For i:=1 to Len(aMovimento)
		cProduto	:=	aMovimento[i,4]
		cEntradas	:=	Transform(aMovimento[i,2],cPictSaldo)
		cSaidas		:=	Transform(aMovimento[i,3],cPictSaldo)
		cSaldo		:=	Transform(aMovimento[i,2]-aMovimento[i,3],cPictSaldo)
		Lin++
		@ Lin,00 PSAY "| "+SubString(cProduto,1,30)+"                                                                                      | "+SubStr(cEntradas,6)+" | "+SubStr(cSaidas,6)+" |"+SubStr(cSaldo,6)+"  |                                   |"
	Next
Endif
  Lin++
@ Lin,000 PSAY Replicate("-",206)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Fecha tabela e deleta arquivo e indice temporario            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TMP")
dbCloseArea()
Ferase(cArqTemp+GetDBExtension())
Ferase(cArqIndx+OrdBagExt())

Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ CriaTMP  ³ Autor ³Liber de esteban               ³ Data ³ 11/05/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Cria tabelas temporarias a serem processadas no relatorio           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATR935                                                             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function CriaTMP(lEnd)

Local aEstrut		:=	{}
Local cAliasSD1		:= "SD1"
Local cAliasSD2		:= "SD2"

Local dDtIni		:=	mv_par01
Local dDtFim		:=	mv_par02
Local nXFil

Local cQuery		:=	""
Local aStruSD1		:=	{}
Local aStruSD2		:=	{}
Local nX			:=	0

Local aTamSX3		:= {}
Local nPosSD1		:= 0
Local nPosSD2		:= 0

Local aSX3FldSD1	:= FWSX3Util():GetListFieldsStruct( "SD1" )
Local aSX3FldSD2	:= FWSX3Util():GetListFieldsStruct( "SD2" )

Default lEnd := .F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria arquivo de trabalho p/ impressao.        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

AADD(aEstrut,{'TMP_DATA'	,'D',08,0}) // AADD(aCampos,{"DATA1","D",08,0})
AADD(aEstrut,{'TMP_DOC'		,'C',TamSX3('F2_DOC')[1],0}) // AADD(aCampos,{"DOC"  ,"C",06,0})

AADD(aTamSX3, TamSX3('A1_NOME')[1])
AADD(aTamSX3, TamSX3('A2_NOME')[1])
AADD(aEstrut,{'TMP_NOME'	,'C',IIF( aTamSX3[1] > aTamSX3[2], aTamSX3[1], aTamSX3[2] ),0}) // AADD(aCampos,{"NOME" ,"C",40,0})

AADD(aEstrut,{'TMP_LOCAL'	,'C',02,0}) // AADD(aCampos,{"LOCAL","C",02,0})
AADD(aEstrut,{'TMP_DESC'	,'C',TamSX3('B1_DESC')[1],0}) // AADD(aCampos,{"DESC" ,"C",30,0})

nPosSD1 := aScan(aSX3FldSD1, {|x| x[1] == "D1_QUANT" })
AADD(aEstrut,{'TMP_QUANTE'	,'N',aSX3FldSD1[nPosSD1][3],aSX3FldSD1[nPosSD1][4]}) // aTam:=TamSX3("D1_QUANT") AADD(aCampos,{"ENT" ,"N",aTam[1],aTam[2]})

nPosSD2 := aScan(aSX3FldSD2, {|x| x[1] == "D2_QUANT" })
AADD(aEstrut,{'TMP_QUANTS'	,'N',aSX3FldSD1[nPosSD2][3],aSX3FldSD1[nPosSD2][4]}) // aTam:=TamSX3("D2_QUANT")AADD(aCampos,{"SAI"  ,"N",aTam[1],aTam[2]})

AADD(aEstrut,{'TMP_COD'		,'C',TamSX3('D1_COD')[1],0}) // AADD(aCampos,{"COD"  ,"C",15,0})

nPosSD1 := aScan(aSX3FldSD1, {|x| x[1] == "D1_NUMSEQ" })
nPosSD2 := aScan(aSX3FldSD2, {|x| x[1] == "D2_NUMSEQ" })
AADD(aEstrut,{'TMP_SEQ'		,'C',;
	IIF( aSX3FldSD1[nPosSD1][3] > aSX3FldSD2[nPosSD2][3], aSX3FldSD1[nPosSD1][3], aSX3FldSD2[nPosSD2][3] ),0})

AADD(aEstrut,{'TMP_UM'		,'C',TamSX3('D1_UM')[1],0}) // AADD(aCampos,{"UM"   ,"C",02,0})

nPosSD1 := aScan(aSX3FldSD1, {|x| x[1] == "D1_NUMLOTE" })
nPosSD2 := aScan(aSX3FldSD2, {|x| x[1] == "D2_NUMLOTE" })
AADD(aEstrut,{'TMP_LOTE'	,'C',;
	IIF( aSX3FldSD1[nPosSD1][3] > aSX3FldSD2[nPosSD2][3], aSX3FldSD1[nPosSD1][3], aSX3FldSD2[nPosSD2][3] ),0})

AADD(aEstrut,{'TMP_FILIAL'	,'C',TamSX3('D1_FILIAL')[1],0}) 

cArqTemp := CriaTrab(aEstrut)
dbUseArea(.T.,,cArqTemp,'TMP',.T.,.F.)
cArqIndx := CriaTrab(NIL,.F.)
cChave := "TMP_FILIAL+TMP_UM+TMP_COD"
IndRegua('TMP',cArqIndx,cChave,,,STR0016) //"Selecionando registros ... "
For nXFil:=1 to Len(aFilsCalc)
	cFilAnt:=aFilsCalc[nXFil][2]
	dbSelectArea("SD1")
	dbSetOrder(6)
	ProcRegua(LastRec())

	lQuery 		:= .T.
	cAliasSD1	:= "SD1_MTR935"
	aStruSD1	:= SD1->(dbStruct())
	cQuery		:= "SELECT SD1.D1_DTDIGIT, SD1.D1_COD, SD1.D1_FORNECE, SD1.D1_FILIAL, SD1.D1_NUMSEQ, SD1.D1_UM, SD1.D1_NUMLOTE, "
	cQuery		+= "SD1.D1_LOCAL, SD1.D1_QUANT, SD1.D1_TES , SD1.D1_LOJA, SD1.D1_TIPO,SD1.D1_DOC, SB1.B1_DESC "
	cQuery 		+= "FROM " + RetSqlName("SD1") + " SD1, " + RetSqlName("SB1") + " SB1, "
	cQuery 		+= RetSqlName("SF4") + " SF4 "
	cQuery 		+= "WHERE SD1.D1_FILIAL = '" + xFilial("SD1") + "' AND "
	cQuery 		+= "SD1.D1_FILIAL = '" + xFilial("SD1") + "' AND "
	cQuery 		+= "SD1.D1_DTDIGIT >= '" + Dtos(dDtIni) + "' AND "
	cQuery 		+= "SD1.D1_DTDIGIT <= '" + Dtos(dDtFim) + "' AND "
	cQuery 		+= "SD1.D1_COD >= '" + mv_par05 + "' AND "
	cQuery 		+= "SD1.D1_COD <= '" + mv_par06 + "' AND "
	cQuery 		+= "SD1.D1_LOCAL >= '" + mv_par07 + "' AND "
	cQuery 		+= "SD1.D1_LOCAL <= '" + mv_par08 + "' AND "
	cQuery 		+= "SD1.D_E_L_E_T_ = ' ' AND "
	cQuery 		+= "SB1.B1_FILIAL = '" + xFilial("SB1") + "' AND "
	cQuery 		+= "SB1.B1_COD = SD1.D1_COD AND "
	cQuery 		+= "SB1.D_E_L_E_T_ = ' ' AND "
	cQuery 		+= "SF4.F4_FILIAL = '" + xFilial("SF4") + "' AND "
	cQuery 		+= "SF4.F4_CODIGO = SD1.D1_TES AND "
	cQuery 		+= "SF4.F4_ESTOQUE = 'S' AND "
	cQuery 		+= "SF4.F4_PODER3  = 'R' AND "
	cQuery 		+= "SF4.D_E_L_E_T_ = ' '
	cQuery		+= "ORDER BY 1, 2, 3"

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1)
	For nX := 1 To len(aStruSD1)
		If aStruSD1[nX][2] <> "C" .And. FieldPos(aStruSD1[nX][1])<>0
			TcSetField(cAliasSD1,aStruSD1[nX][1],aStruSD1[nX][2],aStruSD1[nX][3],aStruSD1[nX][4])
		EndIf
	Next nX
	dbSelectArea(cAliasSD1)	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Processando as Entradas³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	While !((cAliasSD1)->(Eof()))
		
		IncProc(STR0016) //"Selecionando Registros" 
		If Interrupcao(@lEnd)
			Exit
		Endif
	
		If !lQuery
			If (!SF4->(dbSeek(xFilial("SF4")+(cAliasSD1)->D1_TES))) .Or. (SF4->F4_ESTOQUE<>"S" .Or. SF4->F4_PODER3<>"R")
				(cAliasSD1)->(dbSkip())
				Loop
			Endif
			If (!SB1->(dbSeek(xFilial("SB1")+(cAliasSD1)->D1_COD)))
				(cAliasSD1)->(dbSkip())
				Loop
			Endif
			cDescr		:=	SB1->B1_DESC
		Else
			cDescr		:=	(cAliasSD1)->B1_DESC
		Endif
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Posiciona Cliente / Fornecedor.                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (cAliasSD1)->D1_TIPO $ "DB"
			If !SA1->(dbSeek(xFilial("SA1")+(cAliasSD1)->D1_FORNECE+(cAliasSD1)->D1_LOJA))
				(cAliasSD1)->(dbSkip())
				Loop
			Else 
				cNome := SA1->A1_NOME
			Endif
		Else
			If !SA2->(dbSeek(xFilial("SA2")+(cAliasSD1)->D1_FORNECE+(cAliasSD1)->D1_LOJA))
				(cAliasSD1)->(dbSkip())
				Loop
			Else
				cNome := SA2->A2_NOME
			Endif
		EndIf
	                      
		RecLock("TMP",.T.)
		Replace TMP->TMP_DATA	With (cAliasSD1)->D1_DTDIGIT  // Replace DATA1	With SD1->D1_DTDIGIT
		Replace TMP->TMP_DOC	With (cAliasSD1)->D1_DOC      // Replace DOC	With SD1->D1_DOC
		Replace TMP->TMP_NOME	With cNome                    // Replace NOME	With IIf(SD1->D1_TIPO$"DB",SA1->A1_NOME,SA2->A2_NOME)
		Replace TMP->TMP_LOCAL	With (cAliasSD1)->D1_LOCAL    // Replace LOCAL	With SD1->D1_LOCAL
		Replace TMP->TMP_DESC	With cDescr                   // Replace DESC	With SB1->B1_DESC
		Replace TMP->TMP_QUANTE	With (cAliasSD1)->D1_QUANT   // Replace ENT		With SD1->D1_QUANT
		Replace TMP->TMP_COD	With (cAliasSD1)->D1_COD     // Replace COD		With SD1->D1_COD
		Replace TMP->TMP_SEQ	With (cAliasSD1)->D1_NUMSEQ
		Replace TMP->TMP_UM		With (cAliasSD1)->D1_UM      // Replace UM		With SD1->D1_UM
		Replace TMP->TMP_LOTE	With (cAliasSD1)->D1_NUMLOTE // Replace LOTE	With SD1->D1_NUMLOTE
		Replace TMP->TMP_FILIAL	With (cAliasSD1)->D1_FILIAL // 
		MsUnlock()
	                         	
		(cAliasSD1)->(dbSkip())
	
	EndDo
	 
	dbSelectArea("SD2")
	dbSetOrder(5)
	ProcRegua(LastRec())
	                 
	lQuery 		:= .T.
	cAliasSD2	:= "SD2_MTR935"
	aStruSD2	:= SD2->(dbStruct())
	cQuery		:= "SELECT SD2.D2_EMISSAO, SD2.D2_COD, SD2.D2_CLIENTE, SD2.D2_FILIAL, SD2.D2_NUMSEQ, SD2.D2_UM, SD2.D2_NUMLOTE, "
	cQuery		+= "SD2.D2_LOCAL, SD2.D2_QUANT, SD2.D2_TES , SD2.D2_LOJA, SD2.D2_TIPO, SD2.D2_DOC,SB1.B1_DESC "
	cQuery 		+= "FROM " + RetSqlName("SD2") + " SD2, " + RetSqlName("SB1") + " SB1, "
	cQuery 		+= RetSqlName("SF4") + " SF4 "
	cQuery 		+= "WHERE SD2.D2_FILIAL = '" + xFilial("SD2") + "' AND "
	cQuery 		+= "SD2.D2_FILIAL = '" + xFilial("SD2") + "' AND "
	cQuery 		+= "SD2.D2_EMISSAO >= '" + Dtos(dDtIni) + "' AND "
	cQuery 		+= "SD2.D2_EMISSAO <= '" + Dtos(dDtFim) + "' AND "	
	cQuery 		+= "SD2.D2_COD >= '" + mv_par05 + "' AND "
	cQuery 		+= "SD2.D2_COD <= '" + mv_par06 + "' AND "
	cQuery 		+= "SD2.D2_LOCAL >= '" + mv_par07 + "' AND "
	cQuery 		+= "SD2.D2_LOCAL <= '" + mv_par08 + "' AND "
	cQuery 		+= "SD2.D_E_L_E_T_ = ' ' AND "
	cQuery 		+= "SB1.B1_FILIAL = '" + xFilial("SB1") + "' AND "
	cQuery 		+= "SB1.B1_COD = SD2.D2_COD AND "
	cQuery 		+= "SB1.D_E_L_E_T_ = ' ' AND "
	cQuery 		+= "SF4.F4_FILIAL = '" + xFilial("SF4") + "' AND "
	cQuery 		+= "SF4.F4_CODIGO = SD2.D2_TES AND "
	cQuery 		+= "SF4.F4_ESTOQUE = 'S' AND "
	cQuery 		+= "SF4.F4_PODER3  = 'D' AND "
	cQuery 		+= "SF4.D_E_L_E_T_ = ' '
	cQuery		+= "ORDER BY 1, 2, 3"
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD2)
	For nX := 1 To len(aStruSD2)
		If aStruSD2[nX][2] <> "C" .And. FieldPos(aStruSD2[nX][1])<>0
			TcSetField(cAliasSD2,aStruSD2[nX][1],aStruSD2[nX][2],aStruSD2[nX][3],aStruSD2[nX][4])
		EndIf
	Next nX
	dbSelectArea(cAliasSD2)	
	                                      
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Processando as Saidas  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	While !((cAliasSD2)->(Eof()))
		
		IncProc(STR0016) //"Selecionando Registros" 
		If Interrupcao(@lEnd)
			Exit
		Endif
	
		If !lQuery
			If (!SF4->(dbSeek(xFilial("SF4")+(cAliasSD2)->D2_TES))) .Or. (SF4->F4_ESTOQUE<>"S" .Or. SF4->F4_PODER3<>"D")
				(cAliasSD2)->(dbSkip())
				Loop
			Endif
			If (!SB1->(dbSeek(xFilial("SB1")+(cAliasSD2)->D2_COD)))
				(cAliasSD2)->(dbSkip())
				Loop
			Endif
			cDescr		:=	SB1->B1_DESC
		Else
			cDescr		:=	(cAliasSD2)->B1_DESC
		Endif
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Posiciona Cliente / Fornecedor.                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (cAliasSD2)->D2_TIPO $ "DB"
			If !SA2->(dbSeek(xFilial("SA2")	+(cAliasSD2)->D2_CLIENTE+(cAliasSD2)->D2_LOJA))
				(cAliasSD2)->(dbSkip())
				Loop
			Else 
				cNome := SA2->A2_NOME
			Endif
		Else
			If !SA1->(dbSeek(xFilial("SA1")	+(cAliasSD2)->D2_CLIENTE+(cAliasSD2)->D2_LOJA))
				(cAliasSD2)->(dbSkip())
				Loop
			Else
				cNome := SA1->A1_NOME
			Endif
		EndIf
	
		RecLock("TMP",.T.)
		Replace TMP->TMP_DATA	With (cAliasSD2)->D2_EMISSAO  //Replace DATA1	With SD2->D2_EMISSAO
		Replace TMP->TMP_DOC	With (cAliasSD2)->D2_DOC      //Replace DOC		With SD2->D2_DOC
		Replace TMP->TMP_NOME	With cNome                    //Replace NOME	With IIf(SD2->D2_TIPO$"DB",SA2->A2_NOME,SA1->A1_NOME)
		Replace TMP->TMP_LOCAL	With (cAliasSD2)->D2_LOCAL    //Replace LOCAL	With SD2->D2_LOCAL
		Replace TMP->TMP_DESC	With cDescr                   //Replace DESC	With SB1->B1_DESC
		Replace TMP->TMP_QUANTS	With (cAliasSD2)->D2_QUANT   //Replace SAI		With SD2->D2_QUANT 
		Replace TMP->TMP_COD	With (cAliasSD2)->D2_COD     //Replace COD		With SD2->D2_COD
		Replace TMP->TMP_SEQ	With (cAliasSD2)->D2_NUMSEQ  //
		Replace TMP->TMP_UM		With (cAliasSD2)->D2_UM  	  //Replace UM		With SD2->D2_UM
		Replace TMP->TMP_LOTE	With (cAliasSD2)->D2_NUMLOTE  //Replace LOTE	With SD2->D2_NUMLOTE	
		Replace TMP->TMP_FILIAL	With (cAliasSD2)->D2_FILIAL  
		MsUnlock()
	                         	
		(cAliasSD2)->(dbSkip())
	
	EndDo
	If lQuery
		dbSelectArea(cAliasSD1)
		dbCloseArea()
		dbSelectArea(cAliasSD2)
		dbCloseArea()
	Else
	   dbSelectArea("SD1")
		RetIndex("SD1")
		dbClearFilter()
		Ferase(cIndex+OrdBagExt())
	   dbSelectArea("SD2")
		RetIndex("SD2")
		dbClearFilter()
		Ferase(cIndex+OrdBagExt())
	Endif	
Next	

Return NIL

//-------------------------------------------------------------------
/*/{Protheus.doc} RetPictSld
Rotina que retorna a picture utilizada para o campos de saldo
@author  Yuri F. Palacio
@since   20.07.2021
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function RetPictSld( cPictRef )
	Local cRet := ""

	cRet := cPictRef
	cRet := SubStr(cRet, 4)
	cRet := "@E 999,999," + REPLICATE("9",  4 - At( ",", cRet )) + cRet
Return cRet
