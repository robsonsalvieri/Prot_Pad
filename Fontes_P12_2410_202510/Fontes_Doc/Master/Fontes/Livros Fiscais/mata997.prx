#INCLUDE "Mata997.ch"
#INCLUDE "PROTHEUS.CH"

/*/


Ŀ
Funo     MATA997   Autor  Andreia dos Santos     Data  10/02/99 
Ĵ
Descrio  Contabilizar notas fiscais digitadas.                      
Ĵ
 Uso       SIGAFIS                                                    
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                   
Ĵ
Guilherme     27/07/06xxxxxxInclusao de Parametro para processamento
Santos                      por filiais.                            
ٱ


/*/
Function MATA997
//Ŀ
// Salva a Integridade dos dados de Entrada                     
//
Local aSave	:=	{Alias(),IndexOrd(),Recno()}

//Ŀ
// Define Variaveis                                             
//

Local bProcesso		:= { |oSelf| a997Processa(oSelf) }
Local oTProces
      
Private cPerg	:=	"MTA997"
Private aRotina := MenuDef()

//Ŀ
// Variaveis utilizadas para parametros                     
// mv_par01            // Considera da data                 ADMIN
// mv_par02            // ate a data                        
// mv_par03            // Aglutina (Sim/Nao)                
// mv_par04            // Mostra lcto Contabil              
// mv_par05            // NF's (Entrada/Saida/Ambas)        
// mv_par06            // Seleciona Filiais                 
//
Pergunte(cPerg,.F.)

//Ŀ
// Janela Principal                                             
//
cTitulo	:=	STR0001 //"Contab. Notas Fiscais Manuais "
cText1	:=	STR0005 //"Este programa gera lancamentos contabeis para notas Fiscais "
cText2	:=	STR0006 //"entradas manualmente."

oTProces := tNewProcess():New( "MATA997" , STR0001 , bProcesso , STR0001 , "MTA997",,,,,.T.,.T.)
oTProces:SaveLog(OemToAnsi(STR0013)) 

//Ŀ
// Restaura area                                                
//
dbSelectArea(aSave[1])
dbSetOrder(aSave[2])
dbGoto(aSave[3])
Return

/*/


Ŀ
Funo     A997Processa  Autor  Andreia dos Santos Data  11/02/99 
Ĵ
Descrio  Processamento do MATA996                                   
ٱ


*/
STATIC FUNCTION A997Processa(oSelf)

Local nHdlPrv       := 0
Local cArquivo      := ""
Local nTotal    	:= 0
Local lDigita       := ( mv_par04== 1)
LOCAL lHeadProva    := .F.
Local lAglutina     := ( mv_par03 == 1)
Local lLancEmis     := ( mv_par06 == 2)
Local dBackBase     := dDataBase
Local dDataAnt      := CToD( "" )
Local cFilBack      := cFilAnt
Local aFilsCalc     := {}
Local aFlagCTB      := {}
Local aTabRecOri	:= {}
Local nForFilial    := 0
Local cQryAlias     := ""
Local cCodLEnt      := ""
Local cCodLSai      := ""
Local lProcessa		:= .T.

Private cLote   	:=	""


if Type("mv_par08") <> "U" .and. !empty(mv_par08)
	cCodLEnt := mv_par08
endif

if Type("mv_par09") <> "U" .and. !empty(mv_par09)
	cCodLSai := mv_par09	
endif

If (Empty(cCodLEnt) .And. Empty(cCodLSai)) .Or. (mv_par05 == 3 .And. (Empty(cCodLEnt) .Or. Empty(cCodLSai)))
	MsgAlert("No foram informados os cdigos de lanamento de entrada ou sada","Cdigo de Lanamento")
	lProcessa := .F.
EndIf

if mv_par05 == 3 .and. !empty(cCodLEnt) .and. !empty(cCodLSai)
	IF !VerPadrao(cCodLEnt)
		MsgAlert("No foi encontrada Cdigo de Lanamento Informado: "+ cCodLEnt,"Cdigo de Lanamento")
		lProcessa := .F.
	ElseIf !VerPadrao(cCodLSai)
		MsgAlert("No foi encontrada Cdigo de Lanamento Informado: "+ cCodLSai,"Cdigo de Lanamento")
		lProcessa := .F.
	Endif	
elseif mv_par05 == 1 .and. !empty(cCodLEnt) 
	If !VerPadrao(cCodLEnt)
		MsgAlert("No foi encontrada Cdigo de Lanamento Informado: "+ cCodLEnt,"Cdigo de Lanamento")
		lProcessa := .F.
	Endif	
elseif mv_par05 == 2 .and. !empty(cCodLSai)  
	IF !VerPadrao(cCodLSai)
		MsgAlert("No foi encontrada Cdigo de Lanamento Informado: "+ cCodLSai,"Cdigo de Lanamento")
		lProcessa := .F.
	Endif	
endif



aFilsCalc := MatFilCalc( MV_PAR07 == 1 )
If Empty( aFilsCalc )
	lProcessa := .F.
	//Return
EndIf

IF lProcessa

	For nForFilial := 1 to Len(aFilsCalc)

		If aFilsCalc[nForFilial][1]

			cFilAnt := aFilsCalc[nForFilial][2]

			If CanProcItvl(mv_par01, mv_par02,cFilAnt,cFilAnt,"MATA997")

				// Chama a rotina que monta a query para realizar o filtro dos daddos
				cQryAlias := CreateQry()
				(cQryAlias)->(DbGoTop())
						
				While (cQryAlias)->(!EOF()) 

					If lLancEmis
						//Ŀ
						// Verifica se mudou a data         				    
						//
						If !Empty(dDataAnt) .AND. dDataAnt <> (cQryAlias)->F3_ENTRADA  
							
							lHeadProva := .F.
							dDataBase  := dDataAnt
							
							If nTotal > 0
								RodaProva(nHdlPrv,nTotal)
								//Ŀ
								// Envia para Lanamento Contbil							  
								//
								cA100Incl(cArquivo,nHdlPrv,3,cLote,lDigita,lAglutina,,,,@aFlagCTB)
							Endif
							
							nTotal := 0
							dDataBase := dBackBase		
							
						EndIf
						
					EndIf

					
					dDataAnt  := (cQryAlias)->F3_ENTRADA
					
					//Ŀ
					// Encontra o numero do lote					  
					//
					If SX5->( dbSeek(xFilial("SX5")+"09"+"FIS") )
						cLote:=StrZero(INT(VAL(X5Descri())+1),4)
					Else
						cLote:="0001"
					Endif
						
					IF (cQryAlias)->FT_TIPOMOV == "E"

						//Ŀ
						// Manipula a database para efetuar o lancamento em outra data 
						//
						If lLancEmis
							dDataBase := (cQryAlias)->F3_ENTRADA
						EndIf

						DbSelectArea("SD1")
						SD1->(DbSetOrder(1))
									//D1_FILIAL    + D1_DOC                  + D1_SERIE              + D1_FORNECE              + D1_LOJA              + D1_COD              + D1_ITEM                                                                                                     
						SD1->(DbSeek( xFilial("SD1") + (cQryAlias)->FT_NFISCAL + (cQryAlias)->FT_SERIE + (cQryAlias)->FT_CLIEFOR + (cQryAlias)->FT_LOJA + (cQryAlias)->FT_PRODUTO + (cQryAlias)->FT_ITEM ))
																															
						dbSelectArea("SF1")
						SF1->(dbSetOrder(1))
							//F1_FILIAL          +F1_DOC                 +F1_SERIE             +F1_FORNECE             +F1_LOJA             +F1_TIPO                                                                                                            
						SF1->( dbSeek(xFilial("SF1")+(cQryAlias)->FT_NFISCAL+(cQryAlias)->FT_SERIE+(cQryAlias)->FT_CLIEFOR+(cQryAlias)->FT_LOJA))

						aAdd(aFlagCTB,{"F1_DTLANC",dDatabase,"SF1",SF1->(Recno()),0,0,0})
											
						If !lHeadProva
							nHdlPrv		:=	HeadProva(cLote,"MATA997",cUserName,@cArquivo)
							lHeadProva 	:=	.T.
						Endif
				
						aTabRecOri := {"SF1",SF1->(Recno())}
						nTotal	+=	DetProva(nHdlPrv,cCodLEnt,"MATA997",cLote,,,,,,,,@aFlagCTB,aTabRecOri) 
						aTabRecOri := {}

					    //Ŀ
				      	// Atualiza flag de Lanamento Contbil       
					    //
					    If LanceiCtb // Vem do DetProva
						    	RecLock("SF1")
						    	SF1->F1_DTLANC := dDatabase
							    MsUnlock( )
							
							DbSelectArea("SF3")
							SF3->(DbSetOrder(5))
							//SF3->(DbSeek(xFilial("SF3") + (cQryAlias)->FT_CLIEFOR + (cQryAlias)->FT_LOJA + (cQryAlias)->FT_NFISCAL + (cQryAlias)->FT_SERIE )) 
							//SF3->(DbSeek(xFilial("SF3") +  (cQryAlias)->FT_SERIE + (cQryAlias)->FT_NFISCAL + (cQryAlias)->FT_CLIEFOR + (cQryAlias)->FT_LOJA + (cQryAlias)->FT_IDENTF3 )) //F3_FILIAL+F3_SERIE+F3_NFISCAL+F3_CLIEFOR+F3_LOJA+F3_IDENTFT                                                                                                     
							SF3->(DbGoTo((cQryAlias)->SF3RECNO))
							RecLock("SF3")
								SF3->F3_DTLANC := dDatabase
							SF3->(MsUnlock( ))
							SF3->(DbCloseArea())
						EndIf	

						If (nPosReg  := aScan(aFlagCTB,{ |x| x[4] == SF1->(Recno()) })) > 0
							aFlagCTB := Adel(aFlagCTB,nPosReg)
							aFlagCTB := aSize(aFlagCTB,Len(aFlagCTB)-1)
						Endif

																		
						//Ŀ
						// Retorna a database                                          
						//
						If lLancEmis
							dDataBase := dBackBase
						EndIf	
					ELSEIF (cQryAlias)->FT_TIPOMOV == "S"
																						
						//Ŀ
						// Manipula a database para efetuar o lancamento em outra data 
						//
						If lLancEmis
							dDataBase := (cQryAlias)->F3_ENTRADA
						EndIf

						DbSelectArea("SD2")
						SD2->(DbSetOrder(3))
						SD2->(DbSeek(xFilial("SD2") + (cQryAlias)->FT_NFISCAL + (cQryAlias)->FT_SERIE + (cQryAlias)->FT_CLIEFOR + (cQryAlias)->FT_LOJA + (cQryAlias)->D2_COD + (cQryAlias)->D2_ITEM ))

						dbSelectArea("SF2")
						dbSetOrder(1)
						SF2->(dbSeek(xFilial("SF2") + (cQryAlias)->FT_NFISCAL + (cQryAlias)->FT_SERIE + (cQryAlias)->FT_CLIEFOR + (cQryAlias)->FT_LOJA))
										
						aAdd(aFlagCTB,{"F2_DTLANC",dDatabase,"SF2",SF2->(Recno()),0,0,0})
													
						If !lHeadProva
							nHdlPrv		:=	HeadProva(cLote,"MATA997",cUserName,@cArquivo)
							lHeadProva 	:=	.T.
						Endif
						
						aTabRecOri := {"SF2",SF2->(Recno())}
						nTotal	+=	DetProva(nHdlPrv,cCodLSai,"MATA997",cLote,,,,,,,,@aFlagCTB,aTabRecOri) 
						aTabRecOri := {}
					
						//Ŀ
						// Atualiza flag de Lanamento Contbil       
						//
						If LanceiCtb // Vem do DetProva
							RecLock("SF2")
							SF2->F2_DTLANC := dDatabase
							MsUnlock( )
						
							
							//Ŀ
							// Atualiza flag do livro 				       
							//
							DbSelectArea("SF3")
							SF3->(DbSetOrder(5))
							//SF3->(DbSeek(xFilial("SF3") +  (cQryAlias)->FT_CLIEFOR + (cQryAlias)->FT_LOJA + (cQryAlias)->FT_NFISCAL + (cQryAlias)->FT_SERIE )) 
							//SF3->(DbSeek(xFilial("SF3") +  (cQryAlias)->FT_SERIE + (cQryAlias)->FT_NFISCAL + (cQryAlias)->FT_CLIEFOR + (cQryAlias)->FT_LOJA + (cQryAlias)->FT_IDENTF3 ))  //F3_FILIAL+F3_SERIE+F3_NFISCAL+F3_CLIEFOR+F3_LOJA+F3_IDENTFT
							SF3->(DbGoTo((cQryAlias)->SF3RECNO))
							RecLock("SF3")
								SF3->F3_DTLANC := dDatabase
							SF3->(MsUnlock( ))
							SF3->(DbCloseArea())
						Endif
						
						If (nPosReg  := aScan(aFlagCTB,{ |x| x[4] == SF2->(Recno()) })) > 0
							aFlagCTB := Adel(aFlagCTB,nPosReg)
							aFlagCTB := aSize(aFlagCTB,Len(aFlagCTB)-1)
						Endif
																				
						//Ŀ
						// Retorna a database                                          
						//
						If lLancEmis
							dDataBase := dBackBase
						EndIf
					EndIF
						
					
					(cQryAlias)->(dbskip())
										
				EndDo
			

				//Ŀ
				// Manter este bloco mesmo quando lLancEmis := .T. para o caso da data nao alterar 
				// mas a filial mudar ou o arquivo SF3 chegar em Eof()                             
				//
				If lLancEmis
					dDataBase  := dDataAnt
				EndIf
					
				If nTotal > 0
					RodaProva(nHdlPrv,nTotal)
					//Ŀ
					// Envia para Lanamento Contbil					    
					//
					cA100Incl(cArquivo,nHdlPrv,3,cLote,lDigita,lAglutina,,,,@aFlagCTB)
				Endif
					
				If lLancEmis
					dDataBase  := dBackBase
				EndIf

				aFlagCTB := {}
				FreeProcItvl("MATA997")
			EndIf

		EndIf

	Next nForFilial

	cFilAnt := cFilBack
Endif

Return
/*/


Ŀ
Programa  MenuDef    Autor  Marco Bianchi          Data 01/09/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados         
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function MenuDef()

Private aRotina := { 	{ STR0007	,"AxPesqui" , 0 , 1}	,;	//"Localizar"
					 		{ STR0008	,"fA100Pag" , 0 , 3},;	//"Pagar"
					 		{ STR0009	,"fA100Rec" , 0 , 3},;	//"Receber"
							{ STR0010	,"fA100Can" , 0 , 5},;	//"Excluir"
							{ STR0011 	,"fA100Tran", 0 , 3},;	//"Transferir"
					 		{ STR0012	,"fA100Clas", 0 , 5}}	//"Classificar"

Return(aRotina)

/*************************************************************************************
**[[==============================================================================]]**
**|| Funo    |  CreateQry  | Autor | Luiz E.F. do Carmo     | Data | 08/01/2020 ||**
**||==============================================================================||**
**|| Descrio | Filtra os dados referente a pesquisa da rotina                   ||**
**[[==============================================================================]]**
*************************************************************************************/
Static Function CreateQry()

Local cQrySel   := ""
Local cQryFrm   := ""
Local cQryWhr   := ""
Local cAliasQry := ""

// FOI INCLUIDO 4 NOVOS PARAMETROS PARA FLITRAR OS DADOS PARA A CONTABILIZAO:
// MV_PAR10 = Nota de ?
// MV_PAR11 = Nota ate ?
// MV_PAR12 = Serie de ?
// MV_PAR13 = Seria ate?

cQrySel := " SF3.F3_FILIAL , SF3.F3_CFO , SF3.F3_ENTRADA, SF3.F3_DTLANC , SF3.F3_DTCANC, SFT.FT_NFISCAL, SFT.FT_SERIE, SFT.FT_CLIEFOR, SFT.FT_LOJA, SFT.FT_PRODUTO , SFT.FT_ITEM , SFT.FT_TIPOMOV , SFT.FT_IDENTF3, "
cQrySel += " SD1.D1_ORIGLAN, SD2.D2_ORIGLAN, SD1.D1_COD , SD1.D1_ITEM , SD2.D2_COD , SD2.D2_ITEM, SF3.R_E_C_N_O_ SF3RECNO "

cQryFrm := RETSQLNAME("SF3") + " SF3"
cQryFrm += " INNER JOIN " + RETSQLNAME("SFT") + " SFT ON SFT.FT_FILIAL = SF3.F3_FILIAL"
cQryFrm += " AND SFT.FT_NFISCAL = SF3.F3_NFISCAL"
cQryFrm += " AND SFT.FT_SERIE = SF3.F3_SERIE"
cQryFrm += " AND SFT.FT_CLIEFOR = SF3.F3_CLIEFOR"
cQryFrm += " AND SFT.FT_LOJA = SF3.F3_LOJA"
cQryFrm += " AND SFT.FT_IDENTF3 = SF3.F3_IDENTFT"
cQryFrm += " AND SFT.FT_ENTRADA = SF3.F3_ENTRADA"
cQryFrm += " AND SFT.FT_CFOP = SF3.F3_CFO"
cQryFrm += " AND SFT.FT_ESTADO = SF3.F3_ESTADO"
cQryFrm += " AND SFT.FT_ESPECIE = SF3.F3_ESPECIE"
// FAZ UM LEFT COM O SD1 - ENTRADAS 
cQryFrm += " LEFT OUTER JOIN " + RETSQLNAME("SD1") + " SD1 ON SD1.D1_FILIAL = SFT.FT_FILIAL "
cQryFrm += " AND SD1.D1_DOC = SFT.FT_NFISCAL "
cQryFrm += " AND SD1.D1_SERIE = SFT.FT_SERIE "
cQryFrm += " AND SD1.D1_FORNECE = SFT.FT_CLIEFOR "
cQryFrm += " AND SD1.D1_LOJA = SFT.FT_LOJA "
cQryFrm += " AND SD1.D1_COD = SFT.FT_PRODUTO "
cQryFrm += " AND SD1.D1_ITEM = SFT.FT_ITEM "
cQryFrm += " AND SD1.D1_FILIAL = " + ValToSQL( xFilial("SD1") ) 
cQryFrm += " AND SD1.D_E_L_E_T_ = ' ' "  
// FAZ UM LEFT COM O SD2 - SAIDAS
cQryFrm += " LEFT OUTER JOIN " + RETSQLNAME("SD2") + " SD2 ON SD2.D2_FILIAL = SFT.FT_FILIAL "
cQryFrm += " AND SD2.D2_DOC = SFT.FT_NFISCAL "
cQryFrm += " AND SD2.D2_SERIE = SFT.FT_SERIE "
cQryFrm += " AND SD2.D2_CLIENTE = SFT.FT_CLIEFOR "
cQryFrm += " AND SD2.D2_LOJA = SFT.FT_LOJA "
cQryFrm += " AND SD2.D2_COD = SFT.FT_PRODUTO "
cQryFrm += " AND SD2.D2_ITEM = SFT.FT_ITEM "
cQryFrm += " AND SD2.D2_FILIAL = " + ValToSQL( xFilial("SD2") ) 
cQryFrm += " AND SD2.D_E_L_E_T_ = ' ' "  

cQryWhr := " SFT.FT_FILIAL = " + ValToSQL( xFilial("SFT") ) 
cQryWhr += " AND SF3.F3_FILIAL = " + ValToSQL( xFilial("SF3") ) 
cQryWhr += " AND SFT.D_E_L_E_T_ = ' ' "
cQryWhr += " AND SF3.D_E_L_E_T_ = ' '"
cQryWhr += " AND (SD2.D2_ORIGLAN = 'LF' OR SD1.D1_ORIGLAN = 'LF') "
cQryWhr += " AND SFT.FT_ENTRADA BETWEEN " + ValToSQL(MV_PAR01) + " AND  " + ValToSQL(MV_PAR02)
cQryWhr += " AND SF3.F3_DTLANC = ' ' "
cQryWhr += " AND SF3.F3_DTCANC = ' ' "

// TRATA SE A QUERY VAI FILTRAR AS NOTAS DE ENTRADA OU SAIDA. PARA A OPCAO 3 = AMBAS, DESCONSIDERA ESTA FILTRO
IF MV_PAR05 == 1
	cQryWhr += " AND SFT.FT_TIPOMOV = 'E'"
ELSEIF MV_PAR05 == 2
 	cQryWhr += " AND SFT.FT_TIPOMOV = 'S'"
END IF 
//VALIDA SE OS NOVOS PARAMETROS ESTAO CRIADOS
IF !EMPTY(MV_PAR10) .AND. !EMPTY(MV_PAR11)
	cQryWhr += " AND SFT.FT_NFISCAL BETWEEN " + ValToSQL(MV_PAR10) + " AND  " + ValToSQL(MV_PAR11) 
ENDIF

IF !EMPTY(MV_PAR12) .AND. !EMPTY(MV_PAR13)
	cQryWhr += " AND SFT.FT_SERIE BETWEEN " + ValToSQL(MV_PAR12) + " AND  " + ValToSQL(MV_PAR13)
ENDIF
//Concatenar o % e executar a query.
cSelect := "%" + cQrySel   + "%"
cFrom   := "%" + cQryFrm   + "%"
cWhere  := "%" + cQryWhr   + "%"
cOrderBy := "%F3_FILIAL, F3_ENTRADA, F3_NFISCAL, F3_SERIE, F3_CLIEFOR, F3_LOJA, F3_CFO, F3_ALIQICM%" 

cAliasQry := GetNextAlias()

BeginSQL Alias cAliasQry
	COLUMN F3_ENTRADA AS DATE

	SELECT
		%Exp:cSelect%
	FROM
		%Exp:cFrom%
	WHERE
		%Exp:cWhere%
	ORDER BY 
		%Exp:cOrderBy%
EndSQL

RETURN(cAliasQry)

//---------------------------------------------------------------------------------
/*/{Protheus.doc} CTBDeleSF3
Tratamento para limpar a FLAG do campo F3_DTLANC
@author Gabriel Venditti Barreira
@since 15/01/2021
/*/
//---------------------------------------------------------------------------------

Function CTBDeleSF3(cKeySF3, xInFlag) // Essa funo  chamada no fonte CTBXATU.prw (Contbil)
Local aArea  := GetArea()

SF3->(dbSetOrder(5))
SF3->(DbSeek(xFilial("SF3") + cKeySF3 ))

While !SF3->(Eof()) .And. SF3->F3_FILIAL == xFilial("SF3") .And. cKeySF3 == (SF3->F3_SERIE + SF3->F3_NFISCAl + SF3->F3_CLIEFOR + SF3->F3_LOJA) 
	RecLock("SF3",.F.)
		F3_DTLANC := xInFlag
	SF3->(MsUnlock())
	SF3->(DbSkip())
EndDo

RestARea(aArea)

Return
