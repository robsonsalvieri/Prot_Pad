#INCLUDE "MATA995.CH"
#Include "FIVEWIN.Ch"

/*/


Ŀ
Funo     Mata995   Autor  Armando T. Buchina     Data  22/01/98 
Ĵ
Descrio  Programa de atualizao de Impostos                        
Ĵ
Sintaxe    Mata995()                                                  
Ĵ
 Uso       Generico                                                   
Ĵ
 PROGRAMADOR   DATA    BOPS  MOTIVO DA ALTERACAO                    
Ĵ
 Lucas        15/07/98XXXXXX Inclusao de Validacoes.                
 Lucas        23/10/98XXXXXX Incluso da A995Form() para validar os 
                             Rdmakes do Roteiro de Clculo.         
 Leonardo     18/08/99XXXXXX Acerto na funcao A995Form()            
ٱ


/*/
Function Mata995
//Ŀ
// Define o cabealho da tela de atualizaes                   
//

Local aRotAdic     := {} 
PRIVATE cCadastro  := STR0001 //"Atualizao de Impostos"
PRIVATE aCpoAltSFB := {} // Vetor usado na gravacao do historico de alteracoes
PRIVATE aCmps	   := {}
STATIC lHistFiscal := HistFiscal()

If lHistFiscal
	Aadd(aRotAdic,{ STR0011,"A995Hist", 0 , 0 })	//Historico
EndIf

If cPaisLoc <> "BRA"
	CriaSFB(.T.)
	CriaSF4(.T.)
	CriaSFC(.T.)
EndIf      


If cPaisloc == "ARG"
	dbSelectArea("SFB")
	dbSetOrder(1)
	//Ŀ
	//Corrigindo a funcao de calculo do IB3
	//
	If SFB->(DbSeek(xFilial("SFB")+"IB3"))
		RecLock("SFB",.F.)                
		If Empty(SFB->FB_FORMENT)
			SFB->FB_FORMENT := "M100XIBP"
		Endif
		If Alltrim(Upper(SFB->FB_FORMSAI)) == "M460XIB3"
			SFB->FB_FORMSAI := "M460XIBP"
		Endif
		MsUnLock()
	Endif
	If !dbSeek(xFilial("SFB")+"IB4")
		UpdIB4Arg("TU")
	EndIf

	If !dbSeek(xFilial("SFB")+"IB5")
		UpdIB4Arg("CA")
		UpdIB4Arg("CB") 
		UpdIB4Arg("CH") 
		UpdIB4Arg("CO") 
		UpdIB4Arg("ER") 
		UpdIB4Arg("FO") 
		UpdIB4Arg("JU") 
		UpdIB4Arg("LP") 
		UpdIB4Arg("LR") 
		UpdIB4Arg("ME") 
		UpdIB4Arg("MI") 
		UpdIB4Arg("NE") 
		UpdIB4Arg("SA") 
		UpdIB4Arg("SC") 
		UpdIB4Arg("SF") 
		UpdIB4Arg("RN")
	    UpdIB4Arg("SE")
	    UpdIB4Arg("SJ")
		UpdIB4Arg("TF")
	EndIf
ElseIf cPaisLoc == "BOL"

		If SFF->(RecCount()) == 0
			SFFBOL()
		EndIf

	If !SFB->(DbSeek(xFilial("SFB")+"ICE"))
		UPDICEBOL(.T.)
	EndIf
EndIf

//Ŀ
// Chama a Funcao de Cadastro                                   
//
If lHistFiscal
	axCadastro("SFB",cCadastro,"ma995delok()","ma995TdOk()",aRotAdic,,, {|| ma995TTs()}) 
ElseIf cPaisLoc == "RUS"
	RU09D08()
Else
	axCadastro("SFB",cCadastro,"ma995delok()","ma995TdOk()") 
EndIf

Return

/*/

Ŀ
Funo     A995Form  Autor  Armando T. Buchina     Data  22/01/98 
Ĵ
Descrio Rotina verificadora da formula digitada                     
Ĵ
 Uso      mata995                                                     
ٱ


/*/
Function A995Form(void)
LOCAL cForm:=&(ReadVar())


Return ExistBlock(AllTrim(cForm),.T.)

/*/

Ŀ
Funo    a995ValCpo  Autor  Armando T. Buchina     Data  22/01/98 
Ĵ
Descrio Rotina que verifica se o campo indicado esta cadastrado SX3 
Ĵ
 Uso      MATA995                                                     
ٱ


/*/
Function a995ValCpo()
Local lRet := .t. , nPos , cTipo := "" , cCampo := "", cHelp := ""

cTipo := Right( ReadVar() , 2 )
cCampo := &(ReadVar())

If cTipo == "EI"  // Campo de Entrada - Item
	nPos := SD1->( FieldPos( cCampo ) )
	cHelp := OemToAnsi(STR0005)+ "SD1 !"
ElseIf cTipo == "EC"  // Campo de Entrada - Cabecalho
	nPos := SF1->( FieldPos( cCampo ) )
	cHelp := OemToAnsi(STR0005)+"SF1 !"
ElseIf cTipo == "SI"  // Campo de Saida   - Item
	nPos := SD2->( FieldPos( cCampo ) )
	cHelp := OemToAnsi(STR0005)+"SD2 !"
ElseIf cTipo == "SC"  // Campo de Saida   - Cabecalho
	nPos := SF2->( FieldPos( cCampo ) )
	cHelp := OemToAnsi(STR0005)+"SF2 !"
EndIf

If nPos == 0		//Nao Achou o Campo
	Help( " ",1,"MA995CPO" ,, cHelp,1,1)
	lRet := .F.
EndIf
Return lRet
/*/


Ŀ
Funo    ma995DelOk Autor  Silvia Taguti          Data  06/06/02 
Ĵ
Descrio  Verifica se os dados que serao deletados                   
Ĵ
Sintaxe    MATA995()                                                  
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function ma995DelOk()

Local lRet := .T.
Local cFilDe, cFilAte
Local nX, nZ     := 1    
Local bCampoSFB  := { |x| SFB->(Field(x)) } 
If Type("aCmps") == "U"
	Private aCmps := {}
EndIf
//
//Verifica se o imposto esta amarrado com alguma TES
//
If  FWModeAccess("SFB",3)=="C"
 	cFilDe := " "
 	cFilAte := "zz"
Else
   cFilDe := xFilial("SFC")
   cFilAte := xFilial("SFC") 	
Endif

dbSelectArea("SFC")
DbSetOrder(1)                       
dbSeek(cFilDe,.T.)

While lRet .And. !SFC->(Eof()) .And. SFC->FC_FILIAL <= cFilAte                      
	If SFC->FC_IMPOSTO == SFB->FB_CODIGO
		lRet := .F.
		MessageDlg(STR0006,,1)	// "Existe Tes relacionado com este imposto"
	Endif
	SFC->(dbSkip())
Enddo		


If lHistFiscal .And. lRet   
   aCmps :=  RetCmps("SFB",bCampoSFB)	   
EndIf   
Return(lRet)



/*


ͻ
Programa  Ma995TdOk Autor  Mary C. Hergert      Data   05/09/09   
͹
Desc.     Verifica se os campos do livro fiscal informados no cadastro
          do imposto existem. Somente podera ser cadastrado um imposto
          que ja tenha os campos criados para nao induzir ao erro.    
͹
Uso       SigaFis - Localizado                                        
ͼ


*/
Function Ma995TdOk()

Local aCheca 	:= {}
Local cCpo 		:= M->FB_CPOLVRO
Local cCampo	:= ""
Local lRet		:= .T.
Local nX, nZ     := 1    
Local bCampoSFB  := { |x| SFB->(Field(x)) }
Local lAlt        := .F.
Local	dDataAlt 
Local	cHoraAlt 
If Type("aCmps") == "U"
	Private aCmps := {}
EndIf

Aadd(aCheca,{"SF1","F1_BASIMP"+cCpo})
Aadd(aCheca,{"SF2","F2_BASIMP"+cCpo})
Aadd(aCheca,{"SF3","F3_BASIMP"+cCpo})
Aadd(aCheca,{"SD1","D1_BASIMP"+cCpo})
Aadd(aCheca,{"SD2","D2_BASIMP"+cCpo})
Aadd(aCheca,{"SF1","F1_VALIMP"+cCpo})
Aadd(aCheca,{"SF2","F2_VALIMP"+cCpo})
Aadd(aCheca,{"SF3","F3_VALIMP"+cCpo})
Aadd(aCheca,{"SD1","D1_VALIMP"+cCpo})
Aadd(aCheca,{"SD2","D2_VALIMP"+cCpo})
Aadd(aCheca,{"SF3","F3_ALQIMP"+cCpo})
Aadd(aCheca,{"SD1","D1_ALQIMP"+cCpo})
Aadd(aCheca,{"SD2","D2_ALQIMP"+cCpo})

For nX := 1 to Len(aCheca)
	If (aCheca[nX][1])->(FieldPos(aCheca[nX][2])) == 0
		cCampo += aCheca[nX][2] + Chr(13) + Chr(10)
		lRet := .F.
	Endif
Next             

If lHistFiscal .And. lRet .And. Altera
	
	//Ŀ
	//Verifica se algum campo foi alterado.
	//
	aCpoAltSFB := {}
	For nX := 1 to FCount()
			aAdd( aCpoAltSFB, { eVal( bCampoSFB, nX), SFB->&( eVal( bCampoSFB, nX) ) } )
			If !(AllTrim(M->&( eVal( bCampoSFB, nX) )) == AllTrim(SFB->&( eVal( bCampoSFB, nX))))
				lAlt := .T.
			EndIf
	Next nX
  	
   If lAlt .And. lHistFiscal
	   aCmps 		 :=  RetCmps("SFB",bCampoSFB)	
		M->FB_IDHIST := IdHistFis()
	EndIf
	  
EndIf


If !lRet
	cTitulo 	:= STR0007	   														//"Campos inexistentes"
	cErro		:= STR0008 + Chr(13) + Chr(10) + Chr(13) + Chr(10) + cCampo		//"Os campos de livro fiscal abaixo no existem na base de dados. "
	cSolucao	:= STR0009	   														//"Os campos devero ser criados antes do cadastro do imposto varivel "
	cSolucao	+= STR0010	   								   						//"que os utilizar nos lanamentos fiscais para que essa rotina possa ser finalizada."
	xMagHelpFis(cTitulo,cErro,cSolucao)
Endif

Return lRet

/*


ͻ
Programa  Ma995NoTTsAutor  Wemerson R Bastos    Data   31/08/12   
͹
Desc.                                                                 
͹
Uso       SigaFis - Localizado                                        
ͼ


*/

Function ma995TTs()

If Type("aCmps") == "U"
	Private aCmps := {}
EndIf

If !Inclui .And. Len(aCmps)>0
	GrvHistFis("SFB", "SS8", aCmps) 
	aCmps := {}	
EndIf

Return .T.

/*


ͻ
Programa  MATA995   Autor  Wemerson Randolfo    Data   03/07/2012 
͹
Desc.      Funcao para chamar outra funcao de visualiza do            
           Historico das Operacoes Fiscais                            
͹
Uso        Diversos                                                   
ͼ

*/
Function A995Hist()
Local lRet

lRet := HistOperFis("SS8",SFB->FB_CODIGO,SFB->FB_DESCR,"S8_CODIGO")

Return lRet  


