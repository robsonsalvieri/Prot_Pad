#INCLUDE "Mata930.ch"
#INCLUDE "PROTHEUS.CH"

#define __CFO			1	// C¢digo do CFO
#define __ICMS  		2	// Aliquota do ICMS
#define __VALTOT		3	// Valor total da Mercadoria
#define __VALIPI		4 	// Valor do IPI
#define __VALICM		5	// Valor do ICMS
#define __ALIQIPI		6	// Aliquota de IPI
#define __TES			7	// C¢digo do TES
#define __ISS			8	// C¢digo de Servi‡o do ISS
#define __DESCON		9	// Desconto do Item
#DEFINE PULALINHA Chr(13) + Chr(10) 

// Referencias FIELDPOS
#DEFINE FP_F1_MSFIL 	01
#DEFINE FP_F2_MSFIL 	02
#DEFINE FP_F3_MSFIL 	03
#DEFINE FP_FT_MSFIL 	04
#DEFINE FP_FT_PAUTST 	05
#DEFINE FP_F2_SERSAT 	06
#DEFINE FP_FT_DESCFIS 	07
#DEFINE FP_FT_BSTANT 	08
#DEFINE FP_FT_PSTANT 	09
#DEFINE FP_FT_VSTANT 	10
#DEFINE FP_FT_BFCANTS 	11
#DEFINE FP_FT_PFCANTS 	12
#DEFINE FP_FT_VFCANTS 	13
#DEFINE FP_FT_VICPRST 	14
#DEFINE FP_FT_ALCP15 	15
#DEFINE FP_FT_ALCP20 	16
#DEFINE FP_FT_ALCP25 	17
#DEFINE FP_FT_BSCP15 	18
#DEFINE FP_FT_BSCP20 	19
#DEFINE FP_FT_BSCP25 	20
#DEFINE FP_FT_SECP15 	21
#DEFINE FP_FT_SECP20 	22
#DEFINE FP_FT_SECP25 	23
#DEFINE FP_FT_VLCP15 	24
#DEFINE FP_FT_VLCP20 	25
#DEFINE FP_FT_VLCP25 	26

// Referencia dos parametros
#DEFINE MV_LJLVFIS	    01  
#DEFINE MV_USACFPS      02  
#DEFINE MV_HISTFIS      03

STATIC aSX3       := A930LoadX3()
STATIC aSX6       := A930LoadX6()
STATIC lCmpApoEsp := CmpApoEsp()
STATIC lCmpTags   := CmpTagRess()

STATIC lFisLivro  := aSX6[MV_LJLVFIS] //(SuperGetMV("MV_LJLVFIS",,1) == 2)		// Utiliza novo conceito para geracao do SF3
STATIC lHistFis   := .F.
STATIC lMaFisItem := FindFunction("MaFisItem")
STATIC lCJ3       := AliasIndic("CJ3") .AND. FindFunction("FisXDelCJ3")
STATIC _cQryDKB	  
STATIC _cQryDKC
STATIC oItDKC 	  := Nil
STATIC oImpDKB	  := Nil
STATIC _oManager  := Nil
STATIC lSimuRepro := .F. // identifica se o reprocessamento foi executado pelo Simulador
STATIC _lCalcTG   := .F. // utilizado pelo Simulador para que seja calculado os tributos genéricos
STATIC _lF2BTeste := .F. // utilizado pelo Simulador para que seja considerada as regras tributárias em teste
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³ MATA930  ³ Autor ³ Eduardo / Edson       ³ Data ³08.08.2001  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Reprocessamento dos Livros Fiscais                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Indica se foi chamado por uma rotina automatica        ³±±
±±³          ³ExpA2: Parametros da rotina automatica                        ³±±
±±³          ³ExpA3: Parametros extras para manipulação da rotina           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Edilson      ³19/12/02³      ³ Implementada a rotina para que o programa³±±
±±³              ³        ³      ³ possa ser executada dentro do modulo     ³±±
±±³              ³        ³      ³ SIGALOJA.                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FUNCTION MATA930(lAutoA930,aPergA930,oManager)

LOCAL nOpcA        := 0
LOCAL aSays        := {}
LOCAL aButtons     := {}
Local bProcesso    :={|| }
Local oTProces
Local cFilOri      := FWCodFil()
Local cEmpOri      := FWGrpCompany()
Local lBlind	   := IsBlind()

Private lVersao101 := GetRpoRelease()>="R1.1"
DEFAULT lAutoA930 := .F.//Iif(IsBlind(),.T.,.F.)
DEFAULT aPergA930 := {}

lSimuRepro := simulatorReprocessing(oManager)

If (!lVersao101 .and. !lAutoA930) .And. !lBlind

	If (AMIIn(9,12,72))

   		Pergunte("MTA930",.T.)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ mv_par01 - Data Inicial                                        ³
		//³ mv_par02 - Data Final                                          ³
		//³ mv_par03 - Entradas / Saidas / Ambas                           ³
		//| mv_par12 - Da Filial                                           |
		//| mv_par13 - Ate a Filial	                                       |
		//| mv_par15 - Seleciona Filial                                    |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		AADD(aSays,STR0001 ) //"Esta  rotina ira refazer os Livros Fiscais referente ao periodo"
		AADD(aSays,STR0002 ) //"informado e o tipo, se Entrada ou Sa¡da. ATENCAO! Esta rotina"
		AADD(aSays,STR0003 ) //"devera ser executada em modo mono-usuario."
	
		AADD(aButtons,{ 5,.T.,{|o| Pergunte("MTA930",.T.) }} )
		AADD(aButtons,{ 1,.T.,{|o| nOpca:= 1, o:oWnd:End() }} )
		AADD(aButtons,{ 2,.T.,{|o| o:oWnd:End() }} )
	
		FormBatch(STR0004,aSays,aButtons,,190,395 ) //"Reprocessamento Fiscal"

   		If ( nOpcA == 1 )
			Ma930Param(,lAutoA930,)
   		EndIf
   EndIf
								
Else
	If (lAutoA930 .And. Len(aPergA930)==11) .Or. lBlind
		Ma930Param(,lAutoA930,,aPergA930,lBlind)
	Else
		bProcesso	:= { |oSelf|	oSelf:SaveLog(OemToAnsi(STR0004)), Ma930Param(oSelf,lAutoA930), oSelf:SaveLog(OemToAnsi(STR0008)) }		
		oTProces := tNewProcess():New( "MATA930" , STR0004 , bProcesso , STR0004 , "MTA930",,,,,.T.,.T.)		 		
	EndIf
EndIf 									
dbSelectArea("SM0")
SM0->(dbSeek(cEmpOri+cFilOri))
cFilant := cFilOri
Return(.T.)
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A930RPEntr³ Autor ³ Eduardo Riera         ³ Data ³08.08.2001 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de reprocessamento das notas fiscal de entrada        ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Indica se nao existe interface                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo reprocessar os livros fiscais  ³±±
±±³          ³das notas de entrada.                                        ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A930RPEntrada(lAuto,oObj,lSelFil,cFilAtu)

Local aOtimizacao := {}
Local aAreaSD2	  := {}
Local aAreaSD1	  := {}
Local cQuery      := ""
Local cAlias      := "SF1"
Local cIndex      := ""
Local lQuery      := .F.
Local lRefaz      := .F.
Local lUsaCfps	  := .F.
Local xWhile	  := ""
Local cFilOri	  := FWCodFil()
Local cFilIni 	  := Iif(Empty(MV_PAR12),cFilAnt,MV_PAR12)
Local cFilFin 	  := Iif(Empty(MV_PAR13),cFilAnt,MV_PAR13)
Local cCNAE		  := "" 
Local cCodSef     := "" 
Local cCodRet     := "" 
Local cProtoc     := "" 
Local cDescRet    := "" 
Local cAlsItem2   := ""
Local lFirst 	  := .T.
Local lMTA930F1   := ExistBlock("MTA930F1")
Local lMT930SF3   := ExistBlock("MT930SF3")
Local nItem       := 0
Local lRefTag     := .F.
Local lChkRefTag  := .T.
Local lRefApoEsp  := .F.
Local lChkRefApo  := .T.
Local lCmpDescFi  := aSX3[FP_FT_DESCFIS] //SFT->(FieldPos("FT_DESCFIS")) > 0
Local lRefDescFi  := .F.
Local lChkRefDes  := .T.
Local cChaveSF1	  := ""
Local lfPosF1MSF  := aSX3[FP_F1_MSFIL] //SF1->(FieldPos("F1_MSFIL")) > 0
Local lfPosF3MSF  := aSX3[FP_F3_MSFIL] // SF3->(FieldPos("F3_MSFIL")) > 0
Local lSF1ModoC   := FWModeAccess("SF1",3)=="C"
Local lSF3ModoC   := FWModeAccess("SF3",3)=="C"
Local cCpoSF1	  := ""
Local cF3CodIss	  := ""
Local cChaveDKB   := "" 
Local lDKB		  := Iif(FindFunction("VerTabXml"), VerTabXml(), .F.)
Local lAgrupIt	  := FindFunction("MafisAgrIt")
Local lProcDKB	  := .F. 
Local cItXmlDKC	  := ""
Local jRegCDA     := Nil

#IFDEF TOP
	Local nX          := 0
	Local aStru       := {}
#ENDIF	



Default	lSelFil	  := .F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Quando é automatico, efetua o reprocessamento apenas da filial atual -> CFILANT³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lAuto
	cFilIni := cFilAnt
	cFilFin := cFilAnt
	cFilAtu := cFilAnt
Endif

dbSelectArea("SM0")
SM0->(dbSeek(cEmpAnt+cFilAtu,.T.))
If !lAuto
   oObj:SetRegua1(SM0->(LastRec()))
Endif                             
	
While Iif(!lSelFil,!SM0->(Eof()) .And. FWGrpCompany() == cEmpAnt .And. FWCodFil() == cFilAtu, !SM0->(Eof()) .And. lFirst)

  IF lProc := IIF(lSelFil,IIF(AllTrim(SM0->M0_CODFIL) == AllTrim(cFilAtu),Eval({||cFilAnt := cFilAtu,  .T.}),.F.) ,.T.)
	If !lAuto
	   oObj:IncRegua1(AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL)
	Endif   
	
	Iif(!lSelFil,cFilAnt:= FWCodFil(),)
	lFirst:= .F.
	#IFDEF TOP
		aStru		:= {}
	#ENDIF
	aOtimizacao	:= {}
	cQuery		:= ""
   	cAlias		:= "SF1"
	cIndex		:= ""
	lQuery		:= .F.
	lRefaz		:= .F.
	nX			:= 0
	xWhile		:= ""
    aSX6        := A930LoadX6()
	aSX3        := A930LoadX3()
	lUsaCfps	:= aSX6[MV_USACFPS] //GetNewPar("MV_USACFPS",.F.)	
	cCpoSF1		:= ""
	dbSelectArea("SF1")
	dbSetOrder(1)
	#IFDEF TOP
		If ( TcSrvType()!="AS/400" )
			lQuery := .T.
			cAlias := "MA930GRVQ1"
			aStru  := SF1->(dbStruct())

			For nX := 1 to Len(aStru) 
				cCpoSF1 += "SF1."+aStru[nX][1]+"," + PULALINHA
			Next
			cQuery := " SELECT " + PULALINHA
			cQuery += " SF3.R_E_C_N_O_ SF3RECNO, " + PULALINHA
			cQuery += cCpoSF1 +" SF1.R_E_C_N_O_ SF1RECNO " + PULALINHA
			cQuery += " FROM "+ RetSqlName("SF1") + " SF1 " + PULALINHA
			cQuery += " LEFT JOIN " + PULALINHA
			cQuery += " (SELECT F3_FILIAL, F3_NFISCAL, F3_SERIE, F3_CLIEFOR, F3_LOJA, F3_ENTRADA, F3_FORMUL, MIN(R_E_C_N_O_) R_E_C_N_O_ " + PULALINHA
			cQuery += " FROM "+ RetSqlName("SF3") + " SF3 " + PULALINHA
			cQuery += " WHERE F3_FILIAL='"+ xFilial("SF3")+"' AND SF3.D_E_L_E_T_ = '' " + PULALINHA
			cQuery += " AND F3_ENTRADA BETWEEN '"+DTOS(MV_PAR01)+"' AND '"+DTOS(MV_PAR02)+"' " + PULALINHA
			cQuery += " AND F3_NFISCAL BETWEEN '"+MV_PAR04+"' AND '"+MV_PAR05+"' " + PULALINHA
			cQuery += " AND F3_SERIE BETWEEN '"+MV_PAR06+"' AND '"+MV_PAR07+"' " + PULALINHA
			cQuery += " AND F3_CLIEFOR BETWEEN '"+MV_PAR08+"' AND '"+MV_PAR09+"' " + PULALINHA
			cQuery += " AND F3_LOJA BETWEEN '"+MV_PAR10+"' AND '"+MV_PAR11+"' " + PULALINHA
			cQuery += " AND ( SUBSTRING(F3_CFO,1,1) < '5' OR F3_CFO = '999' )" + PULALINHA
			cQuery += " GROUP BY F3_FILIAL, F3_NFISCAL, F3_SERIE, F3_CLIEFOR, F3_LOJA, F3_ENTRADA, F3_FORMUL) " + PULALINHA
			cQuery += " SF3 ON (SF3.F3_FILIAL= '"+ xFilial("SF3")+"' AND SF3.F3_NFISCAL = SF1.F1_DOC AND SF3.F3_SERIE = SF1.F1_SERIE AND SF3.F3_CLIEFOR = SF1.F1_FORNECE AND SF3.F3_LOJA = SF1.F1_LOJA AND SF3.F3_ENTRADA = SF1.F1_DTDIGIT AND SF3.F3_FORMUL= SF1.F1_FORMUL ) " + PULALINHA
			
			If lSF1ModoC .And. lfPosF1MSF
				cQuery += "WHERE SF1.F1_MSFIL='"+cFilAnt+"' AND "
			Else
				cQuery += "WHERE SF1.F1_FILIAL='"+xFilial("SF1")+"' AND "
			EndIf
			
			cQuery += "SF1.F1_DTDIGIT>= '"+DTOS(MV_PAR01)+"' AND " + PULALINHA
			cQuery += "SF1.F1_DTDIGIT<= '"+DTOS(MV_PAR02)+"' AND " + PULALINHA
			cQuery += "SF1.F1_DOC>= '"+MV_PAR04+"' AND " + PULALINHA
			cQuery += "SF1.F1_DOC<= '"+MV_PAR05+"' AND " + PULALINHA
			cQuery += "SF1.F1_SERIE>= '"+MV_PAR06+"' AND " + PULALINHA
			cQuery += "SF1.F1_SERIE<= '"+MV_PAR07+"' AND " + PULALINHA
			cQuery += "SF1.F1_FORNECE>= '"+MV_PAR08+"' AND " + PULALINHA
			cQuery += "SF1.F1_FORNECE<= '"+MV_PAR09+"' AND " + PULALINHA
			cQuery += "SF1.F1_LOJA>= '"+MV_PAR10+"' AND " + PULALINHA
			cQuery += "SF1.F1_LOJA<= '"+MV_PAR11+"' AND " + PULALINHA
			cQuery += "SF1.F1_STATUS <> '' AND " + PULALINHA
			cQuery += "SF1.F1_STATUS <> 'B' AND " + PULALINHA // NOTAS FISCAIS COM BLOQUEIO
			cQuery += "SF1.F1_STATUS <> 'C' AND " + PULALINHA // NOTAS FISCAIS COM BLOQUEIO DE MOV.

			If cPaisLoc<>"BRA"
				cQuery += "NOT ("+IsRemito(3,'SF1.F1_TIPODOC')+ ") AND "
			EndIf
			cQuery += "SF1.D_E_L_E_T_ = '' " + PULALINHA
			cQuery += "ORDER BY "+SqlOrder(IndexKey()) + PULALINHA
			cQuery := ChangeQuery(cQuery)
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)	
			For nX := 1 To Len(aStru)
				If aStru[nX][2]<>"C"
					TcSetField(cAlias,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
				EndIf
			Next nX
		Else
	#ENDIF
			
		If lSF1ModoC .And. lfPosF1MSF			
			cQuery += "F1_MSFIL='"+cFilAnt+"'.AND."
		Else	
			cQuery += "F1_FILIAL='"+xFilial("SF1")+"'.AND."
		EndIf
				
		cQuery += "DTOS(F1_DTDIGIT)>='"+DTOS(MV_PAR01)+"'.AND."
		cQuery += "DTOS(F1_DTDIGIT)<='"+DTOS(MV_PAR02)+"'.AND."
		cQuery += "F1_DOC>='"+MV_PAR04+"'.AND."
		cQuery += "F1_DOC<='"+MV_PAR05+"'.AND."
		cQuery += "F1_SERIE>='"+MV_PAR06+"'.AND."
		cQuery += "F1_SERIE<='"+MV_PAR07+"'.AND."
		cQuery += "F1_FORNECE>='"+MV_PAR08+"'.AND."
		cQuery += "F1_FORNECE<='"+MV_PAR09+"'.AND."
		cQuery += "F1_LOJA>='"+MV_PAR10+"'.AND."
		cQuery += "F1_LOJA<='"+MV_PAR11+"' .AND."				
		If cPaisLoc<>"BRA"
			cQuery  += "!("+IsRemito(2,'F1_TIPODOC')+") .AND."			
		EndIf	
    	cQuery += "!EMPTY(F1_STATUS) AND !F1_STATUS$'B|C'""
		
		cIndex := CriaTrab(,.F.)
		IndRegua("SF1",cIndex,IndexKey(),,cQuery)
		dbGotop()
		#IFDEF TOP
		EndIf
		#ENDIF
	dbSelectArea(cAlias)
	
	If !lAuto
	   oObj:SetRegua2(SF1->(LastRec()))
	Endif   
	
	While ( !Eof() )
	
		lRefaz := .T.
	
		If lMTA930F1
			If !(ExecBlock ("MTA930F1",.F.,.F.,{cAlias}))
				lRefaz := .F.
			EndIf
		EndIf
		If lRefaz
			dbSelectArea("SF3")
			SF3->(dbSetOrder(5))
			If !Empty((cAlias)->SF3RECNO)            // Verifica se o Rercno do SF3 não está vazio
				SF3->( dbGoTo( (cAlias)->SF3RECNO ) )        // Posiciona no SF3
		
				If lSF3ModoC .And. lfPosF3MSF
				xWhile := "SF3->F3_MSFIL == '"+cFilAnt+"' .And. SF3->F3_NFISCAL == (cAlias)->F1_DOC .And. "
				Else	
				xWhile := "SF3->F3_FILIAL == xFilial('SF3') .And. SF3->F3_NFISCAL == (cAlias)->F1_DOC .And. "
				EndIf
			
				xWhile += "SF3->F3_SERIE == (cAlias)->F1_SERIE .And. SF3->F3_CLIEFOR == (cAlias)->F1_FORNECE .And. "
				xWhile += "SF3->F3_LOJA == (cAlias)->F1_LOJA .And. "
				If lUsaCfps
				xWhile += "(SubStr(SF3->F3_CFO,1,1)<'5' .Or. FisChkCfps('E',SF3->F3_CFO)) "
				
				Else
				xWhile += "(SubStr(SF3->F3_CFO,1,1)<'5' .Or. alltrim(SF3->F3_CFO)=='999')"	
				Endif
				xWhile := &('{||'+xWhile+'}')
				
				While !Eof() .And. Eval(xWhile)
					If (SF3->F3_REPROC == "N" .Or. alltrim(SF3->F3_CFO)=='999') .and. !lSimuRepro
						lRefaz := .F.
						Exit
					EndIf
					dbSelectArea("SF3")
					dbSkip()
				EndDo
			EndIf	
		EndIf
		If lRefaz
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Posicionando cadastro de clientes/fornecedores³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If (cAlias)->F1_TIPO $ "DB"
				SA1->(dbSetOrder(1))
				SA1->(MsSeek(xFilial("SA1")+(cAlias)->F1_FORNECE+(cAlias)->F1_LOJA))
			Else
				SA2->(dbSetOrder(1))
				SA2->(MsSeek(xFilial("SA2")+(cAlias)->F1_FORNECE+(cAlias)->F1_LOJA))
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Carrega a Nota Fiscal SF3 referente a Notas Fiscais de Entrada³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			MaFisIniNF(1,IIf(lQuery,(cAlias)->SF1RECNO,SF1->(RecNo())),@aOtimizacao,cAlias,((cAlias)->F1_IMPORT<>"S"), "MATA930", lHistFis, @cAlsItem2, _lCalcTG, _lF2BTeste)
            //Tratamento para que seja executado maFisScan() somente a primeira vez
            If lChkRefTag
                lRefTag     := RefTagRess()
                lChkRefTag  := .F.
            EndIF			
			//Tratamento para que seja executado maFisScan() somente a primeira vez
            If lChkRefApo
                lRefApoEsp  := RefApoEsp()
                lChkRefApo  := .F.
            EndIF			
			//Tratamento para que seja executado maFisScan() somente a primeira vez
            If lChkRefDes
                lRefDescFi  := RefDescFis()
                lChkRefDes  := .F.
            EndIF			

			Begin Transaction
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Efetua a exclusao dos registros referente a Nota Fiscal no SF3³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				cCodSef  := ""
				cCodRet  := ""
				cProtoc  := ""
				cDescRet := ""
				cCNAE 	 := ""
				lProcDKB := .F. 
				//-----------------------------------------------------------------------
				//Aqui deverá excluir DKB considerando a chave da tabela SF1 e recalcular
				//-----------------------------------------------------------------------
				If lDKB .And. !(cChaveDKB == xFilial("DKB") + (cAlias)->(F1_FORNECE + F1_LOJA + F1_DOC + F1_SERIE))
					cChaveDKB:= xFilial("DKB") + (cAlias)->(F1_FORNECE + F1_LOJA + F1_DOC + F1_SERIE)   
					oItDKC 	 := Nil
					oImpDKB	 := Nil
					FisxLDKC(xFilial("DKB"), (cAlias)->F1_FORNECE, (cAlias)->F1_LOJA, (cAlias)->F1_DOC, (cAlias)->F1_SERIE,@lProcDKB)
				EndIF

				dbSelectArea("SF3")
				SF3->(dbSetOrder(4))
				If !Empty((cAlias)->SF3RECNO)            // Verifica se o Rercno do SF3 não está vazio
					SF3->( dbGoTo( (cAlias)->SF3RECNO ) )        // Posiciona no SF3
				// Verifico se os campos "SF3->F3_CODRSEF,SF3->F3_CODRET,SF3->F3_PROTOC,SF3->F3_DESCRET" contem valor na tabela, se existir armazeno o conteudo na variavel "cCodSef,cCodRet,cProtoc,cDescRet" (que e previamente limpa).		
					If !Empty(SF3->F3_CODRSEF)
						cCodSef := SF3->F3_CODRSEF
					Endif					
					If !Empty(SF3->F3_CODRET)
						cCodRet := SF3->F3_CODRET
					Endif	
					If !Empty(SF3->F3_PROTOC)
						cProtoc := SF3->F3_PROTOC 
					Endif	
					If !Empty(SF3->F3_DESCRET)
						cDescRet:= SF3->F3_DESCRET
					Endif			
					If Alltrim(SF3->F3_CNAE) <> ""
						cCNAE := SF3->F3_CNAE
					Endif
					While !SF3->(Eof()) .And. Eval(xWhile)
						If Empty(SF3->F3_DTCANC)

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Efetua a exclusao dos registros referente a Nota Fiscal no SFT³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							dbSelectArea("SFT")
							SFT->(dbSetOrder(3))
							SFT->(MsSeek(xFilial("SFT")+"E"+SF3->F3_CLIEFOR+SF3->F3_LOJA+SF3->F3_SERIE+SF3->F3_NFISCAL+SF3->F3_IDENTFT))
							While !SFT->(Eof()) .And.;
								xFilial("SFT")+"E"+SF3->F3_CLIEFOR+SF3->F3_LOJA+SF3->F3_SERIE+SF3->F3_NFISCAL+SF3->F3_IDENTFT==;
								xFilial("SFT")+"E"+SFT->FT_CLIEFOR+SFT->FT_LOJA+SFT->FT_SERIE+SFT->FT_NFISCAL+SFT->FT_IDENTF3;
								.And. Empty(SFT->FT_DTCANC)

								nItem   := Iif(lMaFisItem,MaFisItem('IT_ITEM', SFT->FT_ITEM),0)
								//Realizado ajuste para diferença de tamanho de Item entre o D1_ITEM e D2_ITEM ou bancos com tamanho do _ITEM alterado
								If nItem == 0
									nItem := Iif(lMaFisItem,MaFisItem('IT_ITEM', AllTrim(SFT->FT_ITEM)),0)
								EndIf

								If nItem > 0
									//Fará Load dos valores antes de excluir a SFT
									If lCmpTags .AND. lRefTag .And. (SFT->FT_CSOSN =='500' .Or. SubStr(SFT->FT_CLASFIS,2,2) == '60')
											LoadTagRess(nItem)
									EndIF

									//Fará Load dos valores antes de excluir a SFT
									If lCmpApoEsp .AND. lRefApoEsp
											LoadApoEsp(nItem) 
									EndIF

									//Fará Load dos valores antes de excluir a SFT
									If lCmpDescFi .AND. lRefDescFi
											LoadDescFi(nItem)
									EndIF

									//Decreto 43.080/2002 RICMS-MG -- 
									if SFT->FT_PR43080 > 0
											MaFisLoad("IT_PR43080",SFT->FT_PR43080,nItem)
											MaFisRecal("IT_PR43080",nItem)
									endif

									//-----------------------------------------------------------------------
									//Aqui deverá excluir CJ3 considerando ID dos tributos genéricos da SFT
									//-----------------------------------------------------------------------
									If lCJ3 .AND. !EMPTY(SFT->FT_IDTRIB) .and. !lSimuRepro
										FisXDelCJ3(SFT->FT_IDTRIB, "2")
									EndIF

									//
									If nItem >0 .And. SFT->FT_TIPO $ "DB" .And. !Empty(MaFisRet(nItem,"IT_NFORI"))
										aAreaSD2 := SD2->(GetArea())
										SD2->(DbSetOrder(3))
										If SD2->(MSSeek(xFilial("SD2")+SFT->(FT_NFORI+FT_SERORI+FT_CLIEFOR+FT_LOJA+FT_PRODUTO+FT_ITEMORI)))
												MaFisLoad("IT_QTDORI",SD2->D2_QUANT,nItem)
												MaFisRecal("IT_QTDORI",nItem)
												MaFisLoad("LF_ITEMORI",SFT->FT_ITEMORI,nItem) //Refaz o Item Original
										EndIf
										RestArea(aAreaSD2)
									EndIf

									//Neste momento vinculo o item do XML com o item da NF
									If lProcDKB 
										cItXmlDKC := "" 
										cItXmlDKC := oItDKC[SFT->FT_ITEM]
										If !Empty(cItXmlDKC)
											MaFisAlt("IT_ITEMXML",cItXmlDKC,nItem)
										Endif	
									Endif

									If lRefaz .And. (cAlias)->F1_VALPEDG > 0
										MaFisALT("IT_VALPEDG",SFT->FT_VALPEDG,nItem)
									EndIf

									If lRefaz .And. Alltrim(SFT->FT_TIPO) == 'S' .AND. SF4->F4_LFISS $ "|T|O|I|"
						                cF3CodIss := SFT->FT_CODISS									 
										aAreaSD1 := SD1->(GetArea())
										dbSelectArea("SD1")
										SD1->(dbSetOrder(1))

										If SD1->(MsSeek(xFilial("SD1")+SFT->FT_NFISCAL+SFT->FT_SERIE+SFT->FT_CLIEFOR+SFT->FT_LOJA+SFT->FT_PRODUTO+SFT->FT_ITEM))
												MaFisAlt("IT_ALIQISS", SD1->D1_ALIQISS, nItem)
												MaFisAlt("IT_VALISS", SD1->D1_VALISS, nItem)
												IF EMPTY(cF3CodIss)
													MaFisAlt("IT_CODISS", SD1->D1_CODISS, nItem)
												ENDIF
										EndIF

										RestArea(aAreaSD1)
									EndIf
									if !lSimuRepro
										dbSelectArea("SFT")
										RecLock('SFT',.F.,.T.)
										SFT->(dbDelete())
										SFT->(MsUnlock())
										SFT->(FkCommit())
									endif								
								ENDIF
								SFT->(dbSkip())
							EndDo
							If nItem > 0 .and. (cAlias)->F1_VALPEDG > 0
								MaFisLoad("LF_VALPEDG",(cAlias)->F1_VALPEDG,nItem)
							EndIf
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Efetua a exclusao dos registros referente a Nota Fiscal no SF3³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							if !lSimuRepro
								dbSelectArea("SF3")
								RecLock('SF3',.F.,.T.)
								SF3->(dbDelete())
								SF3->(MsUnlock())
								SF3->(FkCommit())
							endif
						EndIf
						SF3->(dbSkip())
					EndDo
				Else
					//Não achou SF3, provavelmente a nota estava como gera livro não no momento da escrituração	
					cChaveSF1:=(cAlias)->F1_FILIAL+(cAlias)->F1_DOC+(cAlias)->F1_SERIE+(cAlias)->F1_FORNECE+(cAlias)->F1_LOJA

					//Função para buscar informações na SF1/SF2/SD1/SD2 caso seja preciso antes do reprocessamento
					m930NLvr(cAlias,"SD1",1,cChaveSF1,lMaFisItem)
				EndIf
				if !lSimuRepro				
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿				
					//³ Inicializa a gravacao nas funcoes Fiscais               ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					MaFisWrite()
					MAFISCDA(,,.T.,(cAlias)->("E"+F1_ESPECIE+F1_FORMUL+F1_DOC+F1_SERIE+F1_FORNECE+F1_LOJA),(cAlias)->F1_FORMUL,cAlias,.T.,@jRegCDA)
					MAFISCDA(,2,,(cAlias)->("E"+F1_ESPECIE+F1_FORMUL+F1_DOC+F1_SERIE+F1_FORNECE+F1_LOJA),(cAlias)->F1_FORMUL,cAlias,.T.,@jRegCDA)

					//Abaixo chamoa função que fará a exclusão e depois a inserção do novo registro da DKB
					If lProcDKB
						FisXDelDKB(xFilial("DKB"), (cAlias)->F1_FORNECE, (cAlias)->F1_LOJA, (cAlias)->F1_DOC, (cAlias)->F1_SERIE,lAgrupIt)
					Endif
					//
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Efetua a gravacao dos registros referente a Nota no SF3 ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					MaFisAtuSF3(1,"E",IIf(lQuery,(cAlias)->SF1RECNO,SF1->(RecNo())),"","",cCNAE,"MATA930",,cCodSef,,,cCodRet,cProtoc,cDescRet ) 
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Ponto de entrada, na gravacao do livro            ³
					//³ fiscal															³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If lMT930SF3
						dbSelectArea("SF3")
						dbSetOrder(1)
						MsSeek(xFilial("SF3")+Dtos((cAlias)->F1_DTDIGIT)+(cAlias)->F1_DOC+(cAlias)->F1_SERIE+(cAlias)->F1_FORNECE+(cAlias)->F1_LOJA)
						ExecBlock("MT930SF3",.f.,.f.)
					EndIf
				endif
				If lQuery .and. cAlsItem2 <> ""
   					dbSelectArea(cAlsItem2)
					dbCloseArea()
					dbSelectArea("SD1")
	    		Endif 
			End Transaction
			if lSimuRepro
				_oManager:loadReprocessingTaxes()
			endif
			MaFisEnd()
		EndIf
		dbSelectArea(cAlias)
		dbSkip()
		If !lAuto
			oObj:IncRegua2(STR0006+" "+Dtoc((cAlias)->F1_DTDIGIT)+" "+STR0007+" "+(cAlias)->F1_SERIE+"-"+(cAlias)->F1_DOC)		
		EndIf

		If Valtype(jRegCDA) == "J"
			FWFreeObj(jRegCDA)
			jRegCDA := Nil
		EndIf
	EndDo

	Iif(!lSelFil,SM0->(dbSkip()),)
	If ( lQuery )
		dbSelectArea(cAlias)
		dbCloseArea()
		dbSelectArea("SF1")
	Else
		dbSelectArea("SF1")
		RetIndex("SF1")
	EndIf
 EndIf  
 Iif(lSelFil,SM0->(dbSkip()),)
	
Enddo
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Volta a empresa anteriormente selecionada.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//If !lSelFil
dbSelectArea("SM0")
SM0->(dbSeek(cEmpAnt+cFilOri,.T.))
cFilAnt := FWCodFil()
//EndIF

Return( .T. )
/*/

±±³Fun‡„o    ³A930RPSaid³ Autor ³ Eduardo Riera         ³ Data ³08.08.2001 ³±±
±±³          ³Rotina de reprocessamento das notas fiscal de Saida          ³±±
±±³          ³                                                             ³±±
±±³Parametros³ExpL1: Indica se nao existe interface                        ³±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³Descri‡„o ³Esta rotina tem como objetivo reprocessar os livros fiscais  ³±±
±±³          ³das notas de saida.                                          ³±±
±±³Uso       ³ Materiais                                                   ³±±
/*/
Function A930RPSaida(lAuto,oObj,lSelFil, cFilAtu)

Local aOtimizacao := {}
Local aAreaSD1	  := {}
Local cQuery      := ""
Local cAlias      := "SF2"
Local cIndex      := ""
Local lQuery      := .F.
Local lRefaz      := .F.
Local lUsaCfps	  := .F.
Local xWhile	  := ""
Local nItem       := 0
Local lRefTag     := .F.
Local lChkRefTag  := .T.
Local lRefApoEsp  := .F.
Local lChkRefApo  := .T.
Local lCmpDescFi  := aSX3[FP_FT_DESCFIS] //SFT->(FieldPos("FT_DESCFIS")) > 0
Local lCmpPautSt  := aSX3[FP_FT_PAUTST] //SFT->(FieldPos("FT_PAUTST")) > 0
Local lRefDescFi  := .F.
Local lRefPautSt  := .F.
Local lChkPautSt  := .T.
Local lChkDescFi  := .T.

#IFDEF TOP
	Local nX          := 0
	Local aStru       := {}
#ENDIF

Local cFilOri		:= FWCodFil()
Local cFilIni 		:= Iif(Empty(MV_PAR12),cFilAnt,MV_PAR12)
Local cFilFin 		:= Iif(Empty(MV_PAR13),cFilAnt,MV_PAR13)
Local cOrigLan		:= ""
Local cCNAE			:= ""
Local aEstruSF2		:= {}
Local aSF2Zerado	:= {}
Local aSD2Zerado	:= {}
Local aSF2Back		:= {}
Local aSD2Back		:= {}
Local nInd			:= 0
Local nRecAntSD2	:= 0                                                        
Local lGerValor		:= .F.
Local cCodSef		:= "" 
Local cCodRet       := "" 
Local cProtoc       := "" 
Local cDescRet      := "" 
Local cAlsItem2		:= ""  
Local lFirst		:= .T.
Local cCFOP			:= ""
Local cSersat		:= ""
Local lMTA930F2     := ExistBlock("MTA930F2")
Local lMT930SF3     := ExistBlock("MT930SF3")
Local cChaveSF2		:= ""
Local lfPosF2MSF	:= aSX3[FP_F2_MSFIL]  //SF2->(FieldPos("F2_MSFIL")) > 0
Local lfPosF3MSF	:= aSX3[FP_F3_MSFIL]  //SF3->(FieldPos("F3_MSFIL")) > 0
Local lfPosF2SAT	:= aSX3[FP_F2_SERSAT] //SF2->(FieldPos("F2_SERSAT")) > 0
Local lSF2ModoC		:= FWModeAccess("SF2",3)=="C"
Local lSF3ModoC		:= FWModeAccess("SF3",3)=="C"
Local cCpoSF2		:= ""
Local cItemSFT      := ""
Local nTamItem		:= TamSX3("D1_ITEM")[1]
Local nTamDoc		:= TamSX3("D1_DOC")[1]
Local jRegCDA       := Nil

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Quando é automatico, efetua o reprocessamento apenas da filial atual -> CFILANT³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lAuto
	cFilIni := cFilAnt
	cFilFin := cFilAnt
	cFilAtu := cFilAnt
Endif

dbSelectArea("SM0")
SM0->(dbSeek(cEmpAnt+cFilAtu,.T.))
If !lAuto
   oObj:SetRegua1(SM0->(LastRec()))
Endif                             


While Iif(!lSelFil,!SM0->(Eof()) .And. FWGrpCompany() == cEmpAnt .And. FWCodFil() == cFilAtu, !SM0->(Eof()) .AND. lFirst)
	
  IF lProc := IIF(lSelFil,IIF(AllTrim(SM0->M0_CODFIL) == AllTrim(cFilAtu),Eval({||cFilAnt := cFilAtu,  .T.}),.F.) ,.T.)
	If !lAuto
	   oObj:IncRegua1(AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL)
	Endif   
	
	Iif(!lSelFil,cFilAnt:= FWCodFil(),lFirst:= .F.)
	#IFDEF TOP
		aStru       := {}
	#ENDIF
	aOtimizacao := {}
	cQuery      := ""
	cAlias      := "SF2"
	cIndex      := ""
	lQuery      := .F.
	lRefaz      := .F.
	nX          := 0
	xWhile	  	:= ""
	aSX6 		:= A930LoadX6()
	aSX3        := A930LoadX3()
	lUsaCfps	:= aSX6[MV_USACFPS] //GetNewPar("MV_USACFPS",.F.)
	cCpoSF2		:= ""

	dbSelectArea("SF2")
	dbSetOrder(1)
	#IFDEF TOP
		If ( TcSrvType()!="AS/400" )
			aStru  := SF2->(dbStruct())

           	For nX := 1 to Len(aStru) 
				cCpoSF2 += "SF2."+aStru[nX][1]+"," + PULALINHA
			Next

			lQuery := .T.
			cAlias := "A930RPSaida"
			cQuery := "SELECT " + PULALINHA
			cQuery +=  cCpoSF2 +" SF2.R_E_C_N_O_ SF2RECNO, " + PULALINHA
			cQuery += " SF3.R_E_C_N_O_ SF3RECNO " + PULALINHA
			cQuery += "FROM "+RetSqlName("SF2")+" SF2 "	+ PULALINHA
			cQuery += " LEFT JOIN " + PULALINHA
			cQuery += " (SELECT F3_FILIAL, F3_NFISCAL, F3_SERIE, F3_CLIEFOR, F3_LOJA, F3_ENTRADA, MIN(R_E_C_N_O_) R_E_C_N_O_ " + PULALINHA
			cQuery += " FROM "+ RetSqlName("SF3") + " SF3 " + PULALINHA
			cQuery += " WHERE F3_FILIAL='"+ xFilial("SF3")+"' AND SF3.D_E_L_E_T_ = '' " + PULALINHA
			cQuery += " AND F3_ENTRADA BETWEEN '"+DTOS(MV_PAR01)+"' AND '"+DTOS(MV_PAR02)+"' " + PULALINHA
			cQuery += " AND F3_NFISCAL BETWEEN '"+MV_PAR04+"' AND '"+MV_PAR05+"' " + PULALINHA
			cQuery += " AND F3_SERIE BETWEEN '"+MV_PAR06+"' AND '"+MV_PAR07+"' " + PULALINHA
			cQuery += " AND F3_CLIEFOR BETWEEN '"+MV_PAR08+"' AND '"+MV_PAR09+"' " + PULALINHA
			cQuery += " AND F3_LOJA BETWEEN '"+MV_PAR10+"' AND '"+MV_PAR11+"' " + PULALINHA
			cQuery += " AND SUBSTRING(F3_CFO,1,1) >='5' " + PULALINHA
			cQuery += " GROUP BY F3_FILIAL, F3_NFISCAL, F3_SERIE, F3_CLIEFOR, F3_LOJA, F3_ENTRADA) " + PULALINHA
			cQuery += " SF3 ON (SF3.F3_FILIAL= '"+ xFilial("SF3")+"' AND SF3.F3_NFISCAL = SF2.F2_DOC AND SF3.F3_SERIE = SF2.F2_SERIE AND SF3.F3_CLIEFOR = SF2.F2_CLIENTE AND SF3.F3_LOJA = SF2.F2_LOJA AND SF3.F3_ENTRADA = SF2.F2_EMISSAO) " + PULALINHA
			
			
			If lSF2ModoC .And. lfPosF2MSF
				cQuery += "WHERE SF2.F2_MSFIL='"+cFilAnt+"' AND "
			Else	
				cQuery += "WHERE SF2.F2_FILIAL='"+xFilial("SF2")+"' AND " + PULALINHA
			EndIf
			
			cQuery += "SF2.F2_EMISSAO>='"+DTOS(MV_PAR01)+"' AND " + PULALINHA
			cQuery += "SF2.F2_EMISSAO<='"+DTOS(MV_PAR02)+"' AND " + PULALINHA
			cQuery += "SF2.F2_DOC>='"+MV_PAR04+"' AND " + PULALINHA
			cQuery += "SF2.F2_DOC<='"+MV_PAR05+"' AND " + PULALINHA
			cQuery += "SF2.F2_SERIE>='"+MV_PAR06+"' AND " + PULALINHA
			cQuery += "SF2.F2_SERIE<='"+MV_PAR07+"' AND " + PULALINHA
			cQuery += "SF2.F2_CLIENTE>='"+MV_PAR08+"' AND " + PULALINHA
			cQuery += "SF2.F2_CLIENTE<='"+MV_PAR09+"' AND " + PULALINHA
			cQuery += "SF2.F2_LOJA>='"+MV_PAR10+"' AND "	 + PULALINHA
			cQuery += "SF2.F2_LOJA<='"+MV_PAR11+"' AND " + PULALINHA
			If cPaisLoc<>"BRA"			
				cQuery += " NOT ("+IsRemito(3,'SF2.F2_TIPODOC')+") AND "
			EndIf
			cQuery += "SF2.D_E_L_E_T_ ='' "  + PULALINHA
			cQuery += "ORDER BY "+SqlOrder(IndexKey())
	
			cQuery := ChangeQuery(cQuery)
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)
			For nX := 1 To Len(aStru)
				If aStru[nX][2] <> "C"
					TcSetField(cAlias,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
				EndIf
			Next nX
		Else
	#ENDIF
		
		If lSF2ModoC .And. lfPosF2MSF		
			cQuery += "F2_MSFIL='"+cFilAnt+"'.AND."
		Else	
			cQuery += "F2_FILIAL='"+xFilial("SF2")+"'.AND."
		EndIf
		
		cQuery += "DTOS(F2_EMISSAO)>='"+DTOS(MV_PAR01)+"'.AND."
		cQuery += "DTOS(F2_EMISSAO)<='"+DTOS(MV_PAR02)+"'.AND."
	   	If cPaisLoc<>"BRA"
			cQuery  += " !("+IsRemito(2,'F2_TIPODOC')+") .AND."
	 	EndIf
		cQuery += "F2_DOC>='"+MV_PAR04+"'.AND."
		cQuery += "F2_DOC<='"+MV_PAR05+"'.AND."
		cQuery += "F2_SERIE>='"+MV_PAR06+"'.AND."
		cQuery += "F2_SERIE<='"+MV_PAR07+"'.AND."
		cQuery += "F2_CLIENTE>='"+MV_PAR08+"'.AND."
		cQuery += "F2_CLIENTE<='"+MV_PAR09+"'.AND."
		cQuery += "F2_LOJA>='"+MV_PAR10+"'.AND."	
		cQuery += "F2_LOJA<='"+MV_PAR11+"' "
	
		cIndex := CriaTrab(,.F.)
		IndRegua('SF2',cIndex,IndexKey(),,cQuery)
		dbGotop()
		#IFDEF TOP
		EndIf
		#ENDIF
	dbSelectArea(cAlias)
	If !lAuto
	   oObj:SetRegua2(SF2->(LastRec()))
	Endif   

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Realiza a delecao dos registros provenientes do Mapa Resumo³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lFisLivro
		A930LjDelF3()
    EndIf
	
	While !Eof()
	
		lRefaz := .T.

		If lMTA930F2
			If !(ExecBlock ("MTA930F2",.F.,.F.,{cAlias}))
				lRefaz := .F.
			EndIf
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Desconsiderar F2 que tenham sido gerados pelo LOJA e NFs sobre cupom³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SD2->(dbSetOrder(3))
		If SD2->(dbSeek(xFilial("SD2")+(cAlias)->F2_DOC+(cAlias)->F2_SERIE+(cAlias)->F2_CLIENTE+(cAlias)->F2_LOJA))		
			 cOrigLan := SD2->D2_ORIGLAN
			 cCFOP    := SD2->D2_CF
			 // Somente realiza o tratamento caso nao tenha sido zerado os valores nas tabelas SD2 e SF2
			 If (cAlias)->F2_VALBRUT > 0 .AND. SD2->D2_TOTAL > 0 .And. AllTrim((cAlias)->F2_ESPECIE) <> "NFCE" .And. AllTrim((cAlias)->F2_ESPECIE) <> "SATCE"
			 	lGerValor := .T.
				If lFisLivro .AND. cPaisLoc =="BRA" .AND. cOrigLan == "LO" .AND. !Empty((cAlias)->F2_NFCUPOM) .AND. ;
					(cAlias)->F2_ECF <> "S"
					
					While !Eof().AND. SD2->D2_FILIAL = xFilial("SD2") .AND. SD2->D2_DOC = (cAlias)->F2_DOC .AND. ;
						SD2->D2_SERIE = (cAlias)->F2_SERIE .AND. SD2->D2_CLIENTE = (cAlias)->F2_CLIENTE .AND. ;
						SD2->D2_LOJA = (cAlias)->F2_LOJA        
						
						A930LjGrava("SD2", @aSD2Back, @aSD2Zerado)         // Faz o backup e em seguida zera os valores do array referente a SD2
					  	aSD2Zerado[Len(aSD2Zerado)][1] := SD2->(Recno())   // Guarda o recno do registro a ser zerado
					  	aSD2Back[Len(aSD2Back)][1] := SD2->(Recno())   	   // Guarda o recno do registro a ser atualizado
					  	For  nInd := 1 to Len(aSD2Zerado)                  // Retorna os valores da SD2 zerados
					  		SD2->( dbGoTo ( aSD2Zerado[nInd][1] ) )        // Posiciona no SD2 da NF
	   				   		Lj7GeraSL( "SD2", aSD2Zerado[nInd][2], .F., .T.)
	   				  	Next nInd
						SD2->(DbSkip())
			   		End
	   			EndIf
	   		 EndIf	
		EndIf

		If lRefaz .AND. IIf(!lFisLivro,(!(cAlias)->F2_ECF == "S" .AND. Empty((cAlias)->F2_NFCUPOM)) .OR. ((cAlias)->F2_ECF == "S" .AND. cOrigLan<>"LO" ),.T.)
			If !Empty((cAlias)->SF3RECNO)            // Verifica se o Rercno do SF3 não está vazio
				dbSelectArea("SF3")
				SF3->(dbSetOrder(4))
				SF3->( dbGoTo( (cAlias)->SF3RECNO ) )        // Posiciona no SF3
				// Verifico se os campos "SF3->F3_CODRSEF,SF3->F3_CODRET,SF3->F3_DESCRET" contem valor na tabela, se existir armazeno o conteudo na variavel "cCodSef,cCodRet,cProtoc,cDescRet" (que e previamente limpa).
				cCodSef := "" 
				If !Empty(SF3->F3_CODRSEF)
				cCodSef := SF3->F3_CODRSEF
				Endif
				cCodRet := "" 
				If !Empty(SF3->F3_CODRET)
					cCodRet := SF3->F3_CODRET
				Endif	
				cProtoc := "" 
				If !Empty(SF3->F3_PROTOC)
					cProtoc := SF3->F3_PROTOC 
				Endif	
				cDescRet:= "" 
				If !Empty(SF3->F3_DESCRET)
					cDescRet:= SF3->F3_DESCRET
				Endif
				If lSF3ModoC .And. lfPosF3MSF
					xWhile := "SF3->F3_MSFIL == '"+cFilAnt+"' .And. SF3->F3_NFISCAL == (cAlias)->F2_DOC .And. "
				Else	
					xWhile := "SF3->F3_FILIAL == xFilial('SF3') .And. SF3->F3_NFISCAL == (cAlias)->F2_DOC .And. "
				EndIf
				
				xWhile += "SF3->F3_SERIE == (cAlias)->F2_SERIE .And. SF3->F3_CLIEFOR == (cAlias)->F2_CLIENTE .And. "
				xWhile += "SF3->F3_LOJA == (cAlias)->F2_LOJA .And. "
				
				If lUsaCfps
					xWhile += "((Left(SF3->F3_CFO,1)>='5' .And. Left(SF3->F3_CFO,1)<='7') .Or. FisChkCfps('S',SF3->F3_CFO))"
				Else
					xWhile += " (Left(SF3->F3_CFO,1)>='5' .Or. Alltrim(SF3->F3_CFO)=='000') "
				Endif
				xWhile := &('{||'+xWhile+'}')

				While !Eof() .And. Eval(xWhile)
					If (SF3->F3_REPROC == "N" .Or. Alltrim(SF3->F3_CFO)=='000') .and. !lSimuRepro
						lRefaz := .F.
						Exit
					EndIf
					dbSelectArea("SF3")		
					dbSkip()
				EndDo
			EndIf	
			If lRefaz
			    If lGerValor .AND. lFisLivro .AND. cPaisLoc == "BRA" .AND. cOrigLan == "LO" .AND. ;
			    	!Empty((cAlias)->F2_NFCUPOM) .AND. (cAlias)->F2_ECF <> "S"
			       
				  	aEstruSF2  := SF2->(DbStruct())	        // Faz o backup e zera os valores do array referente a SF2
					For  nInd  := 1 To Len(aEstruSF2)
						If aEstruSF2[nInd,2] == "N"
	    					aADD(aSF2Back,{aEstruSF2[nInd,1], SF2->&(aEstruSF2[nInd,1]) })   
						EndIf
					Next nInd
					                 
				    Lj7GeraSL("SF2",aSF2Zerado, .F., .T.) 
				    
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Posicionando cadastro de clientes/fornecedores³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If (cAlias)->F2_TIPO $ "DB"
					SA2->(dbSetOrder(1))
					SA2->(MsSeek(xFilial("SA2")+(cAlias)->F2_CLIENTE+(cAlias)->F2_LOJA))
				Else
					SA1->(dbSetOrder(1))
					SA1->(MsSeek(xFilial("SA1")+(cAlias)->F2_CLIENTE+(cAlias)->F2_LOJA))
				Endif
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Carrega a Nota Fiscal SF3 referente a Notas Fiscais de Entrada³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				MaFisIniNF(2,IIf(lQuery,(cAlias)->SF2RECNO,SF2->(RecNo())),@aOtimizacao,cAlias,.T.,"MATA930", lHistFis, @cAlsItem2, _lCalcTG, _lF2BTeste)

                //Tratamento para que seja executado maFisScan() somente a primeira vez
                If lChkRefTag
                    lRefTag     := RefTagRess()
                    lChkRefTag  := .F.
                EndIF			
				
				//Tratamento para que seja executado maFisScan() somente a primeira vez
                If lChkRefApo
					lRefApoEsp     := RefApoEsp()
                    lChkRefApo  := .F.
                EndIF			

				//Tratamento para que seja executado maFisScan() somente a primeira vez
                If lChkDescFi
					lRefDescFi     := RefDescFis()
                    lChkDescFi  := .F.
                EndIF	

				//Tratamento para que seja executado maFisScan() somente a primeira vez
                If lChkPautSt
					lRefPautSt     := RefPautSt()
                    lChkPautSt := .F.
                EndIF	

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Efetua a exclusao dos registros referente a Nota Fiscal no SF3³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				Begin Transaction
					cCNAE := ""
					dbSelectArea("SF3")
					SF3->(dbSetOrder(4))
				    If !Empty((cAlias)->SF3RECNO)            // Verifica se o Rercno do SF3 não está vazio
						SF3->(dbGoTo( (cAlias)->SF3RECNO ) )        // Posiciona no SF3

						If Alltrim(SF3->F3_CNAE) <> ""
							cCNAE := SF3->F3_CNAE
						Endif
			  
						While !SF3->(Eof()) .And. Eval(xWhile)
							If Empty(SF3->F3_DTCANC)
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Efetua a exclusao dos registros referente a Nota Fiscal no SFT³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								dbSelectArea("SFT")
								SFT->(dbSetOrder(3))
								SFT->(MsSeek(xFilial("SFT")+"S"+SF3->F3_CLIEFOR+SF3->F3_LOJA+SF3->F3_SERIE+SF3->F3_NFISCAL+SF3->F3_IDENTFT))
								While !SFT->(Eof()) .And.;
									xFilial("SFT")+"S"+SF3->F3_CLIEFOR+SF3->F3_LOJA+SF3->F3_SERIE+SF3->F3_NFISCAL+SF3->F3_IDENTFT==;
									xFilial("SFT")+"S"+SFT->FT_CLIEFOR+SFT->FT_LOJA+SFT->FT_SERIE+SFT->FT_NFISCAL+SFT->FT_IDENTF3

									nItem   := Iif(lMaFisItem,MaFisItem('IT_ITEM',  Alltrim(SFT->FT_ITEM)), 0)
									//Realizado ajuste para diferença de tamanho de Item entre o D1_ITEM e D2_ITEM ou bancos com tamanho do _ITEM alterado
									If nItem == 0
										nItem   := Iif(lMaFisItem,MaFisItem('IT_ITEM',  SFT->FT_ITEM), 0)
									EndIf

									If nItem > 0
										//Fará Load dos valores antes de excluir a SFT
										If lCmpTags .AND. lRefTag .And. (SFT->FT_CSOSN =='500' .Or. SubStr(SFT->FT_CLASFIS,2,2) == '60')
												LoadTagRess(nItem)
										EndIF
										//Fará Load dos valores antes de excluir a SFT
										If lCmpApoEsp .AND. lRefApoEsp
												LoadApoEsp(nItem)
										EndIF
										//Fará Load dos valores antes de excluir a SFT
										If lCmpDescFi .AND. lRefDescFi
												LoadDescFi(nItem)
										EndIF

										//Fará Load dos valores antes de excluir a SFT
										If lCmpPautSt .AND. lRefPautSt
												LoadPautSt(nItem)
										EndIF	
										
										//Se a TES tributa ISS e o tipo da nota for Serviço, os livros serão refeitos de acordo com a SD2
										If Alltrim(SFT->FT_TIPO) == 'S' .AND. AllTrim(SF4->F4_LFISS) $ "|T|O|I|"
											aAreaSD2 := SD2->(GetArea())
											dbSelectArea("SD2")
											SD2->(dbSetOrder(3))

											If SD2->(MsSeek(xFilial("SD2")+SFT->FT_NFISCAL+SFT->FT_SERIE+SFT->FT_CLIEFOR+SFT->FT_LOJA+SFT->FT_PRODUTO+SFT->FT_ITEM))
												MaFisAlt("IT_ALIQISS", SD2->D2_ALIQISS, nItem)
												MaFisAlt("IT_VALISS", SD2->D2_VALISS, nItem)
												MaFisAlt("IT_CODISS", SD2->D2_CODISS, nItem)
											EndIF

											RestArea(aAreaSD2)
										EndIf

										If SFT->FT_TIPO $ "DB" .And. !Empty(MaFisRet(nItem,"IT_NFORI"))
											aAreaSD1 := SD1->(GetArea())
											SD1->(DbSetOrder(1))          
											If SD1->(MSSeek(xFilial("SD1")+SFT->(FT_NFORI+FT_SERORI+FT_CLIEFOR+FT_LOJA+FT_PRODUTO+FT_ITEMORI)))//
												iF nItem > 0
													MaFisLoad("IT_QTDORI",SD1->D1_QUANT,nItem)
													MaFisRecal("IT_QTDORI",nItem)
													MaFisLoad("LF_ITEMORI",SFT->FT_ITEMORI,nItem) //Refaz o Item Original											
												Endif
											EndIf
											RestArea(aAreaSD1)
										EndIf

										//Decreto 43.080/2002 RICMS-MG -- 
										if SFT->FT_PR43080 > 0
												MaFisLoad("IT_PR43080",SFT->FT_PR43080,nItem)
												MaFisRecal("IT_PR43080",nItem)
										endif

										if !lSimuRepro
											//-----------------------------------------------------------------------
											//Aqui deverá excluir CJ3 considerando ID dos tributos genéricos da SFT
											//-----------------------------------------------------------------------
											If lCJ3 .AND. !EMPTY(SFT->FT_IDTRIB)
												FisXDelCJ3(SFT->FT_IDTRIB, "2")
											EndIF

											RecLock('SFT',.F.,.T.)
											SFT->(dbDelete())
											SFT->(MsUnlock())
											SFT->(FkCommit())
											dbSelectArea("SFT")
										endif
										SFT->(dbSkip())
									ENDIF
								EndDo
								if !lSimuRepro
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³ Efetua a exclusao dos registros referente a Nota Fiscal no SF3³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
									RecLock('SF3',.F.,.T.)
									SF3->(dbDelete())
									SF3->(MsUnlock())
									SF3->(FkCommit())
								endif
							EndIf
							dbSelectArea("SF3")
							SF3->(dbSkip())
						EndDo
					Else
						//Não achou SF3, provavelmente a nota estava como gera livro não no momento da escrituração	
						cChaveSF2:=(cAlias)->F2_FILIAL+(cAlias)->F2_DOC+(cAlias)->F2_SERIE+(cAlias)->F2_CLIENTE+(cAlias)->F2_LOJA

						//Função para buscar informações na SF1/SF2/SD1/SD2 caso seja preciso antes do reprocessamento
						m930NLvr(cAlias,"SD2",3,cChaveSF2,lMaFisItem)
						
						//Funcao para ajustar a referencia IT_QTDORI quando a nota estava como gera livro não no momento da escrituração    
                        dbSelectArea("SD2")
                        SD2->(dbSetOrder(3)) //D2_FILIAL+D2_DOC+D2_SERIE+D2_CLIENTE+D2_LOJA+D2_COD+D2_ITEM
                        If SD2->(MsSeek(xFilial("SD2")+(cAlias)->F2_DOC+(cAlias)->F2_SERIE+(cAlias)->F2_CLIENTE+(cAlias)->F2_LOJA))
                            While !SD2->(Eof()) .And.;
                            xFilial("SD2")+SD2->D2_DOC+SD2->D2_SERIE+SD2->D2_CLIENTE+SD2->D2_LOJA==;
                            xFilial("SD2")+(cAlias)->F2_DOC+(cAlias)->F2_SERIE+(cAlias)->F2_CLIENTE+(cAlias)->F2_LOJA
                                nItem   := Iif(lMaFisItem,MaFisItem('IT_ITEM',  Alltrim(SD2->D2_ITEM)), 0)

                                If nItem > 0
                                    If SD2->D2_TIPO $ "DB" .And. !Empty(MaFisRet(nItem,"IT_NFORI"))
                                        aAreaSD1 := SD1->(GetArea())
                                        dbSelectArea("SD1")
										SD1->(DbSetOrder(1))
										SD1->(DbGoTop())
										cItemSFT := StrZero(Val(SD2->D2_ITEMORI), nTamItem)          
                                        If SD1->(MSSeek(xFilial("SD1")+PadR(SD2->D2_NFORI, nTamDoc)+SD2->D2_SERIORI+SD2->D2_CLIENTE+SD2->D2_LOJA+SD2->D2_COD+cItemSFT))
											MaFisLoad("IT_QTDORI",SD1->D1_QUANT,nItem)
											MaFisRecal("IT_QTDORI",nItem)
											MaFisLoad("LF_ITEMORI",cItemSFT,nItem) //Refaz o Item Original                                            
                                        EndIf
                                        RestArea(aAreaSD1)
                                    EndIf
                                EndIf
								SD2->(dbSkip())
                            EndDo
                        EndIf						
					EndIf
					if !lSimuRepro
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Inicializa a gravacao nas funcoes Fiscais               ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						MaFisWrite()
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Efetua a gravacao dos registros referente a Nota no SF3 ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						cSersat := ""
						If lfPosF2SAT
							cSersat := (cAlias)->F2_SERSAT
						Endif					
						If Empty(cCFOP) .Or. SubStr(cCFOP,1,1) >= "5"
								
							MAFISCDA(,,.T.,(cAlias)->("S"+F2_ESPECIE+"S"+F2_DOC+F2_SERIE+F2_CLIENTE+F2_LOJA),nil,cAlias,.T.,@jRegCDA)
							MAFISCDA(,2,,(cAlias)->("S"+F2_ESPECIE+"S"+F2_DOC+F2_SERIE+F2_CLIENTE+F2_LOJA),nil,cAlias,.T.,@jRegCDA)
						
							MaFisAtuSF3(1,"S",IIf(lQuery,(cAlias)->SF2RECNO,SF2->(RecNo())),cAlias,(cAlias)->F2_PDV,cCNAE,"MATA930",,cCodSef,cSersat,,cCodRet,cProtoc,cDescRet)

						EndIf
					endif
					
					If lGerValor .AND. lFisLivro .AND. cPaisLoc == "BRA" .AND. cOrigLan == "LO" 
						
						DBSelectArea("SF2") 
						DBSetOrder(1)
						Lj7GeraSL( "SF2", aSF2Back, .F., .T.)           //Retorna os valores da SF2 
					 	
					 	nRecAntSD2 := SD2->(Recno())                    //Guarda o ultimo recno da SD2
					   	For  nInd := 1 to Len(aSD2Back)                 //Retorna os valores da SD2
							SD2->( dbGoTo ( aSD2Back[nInd][1] ) )
	   				   		Lj7GeraSL( "SD2", aSD2Back[nInd][2], .F., .T.)
	   					Next nInd 
	   					 
	   					SD2->( dbGoTo ( nRecAntSD2 ) )
	   					
   			       	EndIf

					if !lSimuRepro
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Ponto de entrada, na gravacao do livro            ³
						//³ fiscal															³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If lMT930SF3
							dbSelectArea("SF3")
							dbSetOrder(1)
							MsSeek(xFilial("SF3")+Dtos((cAlias)->F2_EMISSAO)+(cAlias)->F2_DOC+(cAlias)->F2_SERIE+(cAlias)->F2_CLIENTE+(cAlias)->F2_LOJA+"5",.T.)
							ExecBlock("MT930SF3",.F.,.F.)
						EndIf
					endif
	
   					If lQuery .and. cAlsItem2 <> "" 
   						dbSelectArea(cAlsItem2)
					   	dbCloseArea()
					   	dbSelectArea("SD2")
	        		Endif  	
				End Transaction
				if lSimuRepro
					_oManager:loadReprocessingTaxes()
				endif
			EndIf
		EndIf
		dbSelectArea(cAlias)
		dbSkip()
		If !lAuto
			oObj:IncRegua2(STR0006+" "+Dtoc((cAlias)->F2_EMISSAO)+" "+STR0007+" "+(cAlias)->F2_SERIE+"-"+(cAlias)->F2_DOC)
		Endif

		If Valtype(jRegCDA) == "J"
			FWFreeObj(jRegCDA)
			jRegCDA := Nil
		EndIf	
	EndDo

	Iif(!lSelFil,SM0->(dbSkip()),)
	If lQuery
		dbSelectArea(cAlias)
		dbCloseArea()
		dbSelectArea("SF2")	
	Else
		dbSelectArea("SF2")
		RetIndex("SF2")
	EndIf
 EndIf   
  Iif(lSelFil,SM0->(dbSkip()),)	
Enddo
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Volta a empresa anteriormente selecionada.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//If !lSelFil
dbSelectArea("SM0")
SM0->(dbSeek(cEmpAnt+cFilOri,.T.))
cFilAnt := FWCodFil()
//EndIF

Return .T.


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Ma930ParamºAutor  ³Rodrigo Zatt        º Data ³  12/18/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao para verificar perguntas                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA930                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
*/
Static Function Ma930Param(oSelf,lAutoA930,lAuto,aPergA930,lBlind)
Local 	nForFilial 	:= 0  
Local 	nFil		:= 1
Local 	aFilsCalc  	:= {}
Local 	aEmpresa   	:= {}	
Local 	cFilIni		:= Iif(Empty(MV_PAR12),cFilAnt,MV_PAR12)
Local 	cFilFin		:= Iif(Empty(MV_PAR13),cFilAnt,MV_PAR13)
Local 	lSelFil 	:= Iif(Empty(MV_PAR15), IIF(VALTYPE(MV_PAR14) =="N",MV_PAR14==1,.F.),  IIF(VALTYPE(MV_PAR15) =="N",MV_PAR15==1,.F.))

Local aAreaSM0 := SM0->(GetArea())
Local cFilBak  := cFilAnt

Local cDtHoraIni := ""
Local cDtHoraFim := ""


Default	aPergA930	:=	{}
Default lBlind		:= IsBlind()

	If  lAutoA930
		mv_par01	:= IIF(Len(aPergA930) >= 1 ,ctod(aPergA930[1]),ctod("//"))
		mv_par02	:= IIF(Len(aPergA930) >= 2 ,ctod(aPergA930[2]),ctod("//"))
		mv_par03	:= IIF(Len(aPergA930) >= 3 ,aPergA930[3] ,"")
		mv_par04	:= IIF(Len(aPergA930) >= 4 ,aPergA930[4] ,"")
		mv_par05	:= IIF(Len(aPergA930) >= 5 ,aPergA930[5] ,"")
		mv_par06	:= IIF(Len(aPergA930) >= 6 ,aPergA930[6] ,"")
		mv_par07	:= IIF(Len(aPergA930) >= 7 ,aPergA930[7] ,"")
		mv_par08	:= IIF(Len(aPergA930) >= 8 ,aPergA930[8] ,"")
		mv_par09	:= IIF(Len(aPergA930) >= 9 ,aPergA930[9] ,"")
		mv_par10	:= IIF(Len(aPergA930) >= 10,aPergA930[10],"")
		mv_par11	:= IIF(Len(aPergA930) >= 11,aPergA930[11],"")
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifco a cPaisLoc, versao, parametro, e campos referente ao Historico de Operacoes³
	//³Fiscais. Roadmap 11.7.                                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cPaisLoc == "BRA" .And. aSX6[MV_HISTFIS] .and. !lSimuRepro

		Pergunte("MTA930",.F.)
		lHistFis := Iif(MV_PAR14==1,.T.,.F.)
	EndIf

cDtHoraIni := DtoC(Date()) + " - " + Time()

//Gestao corporativa
If lSelFil .And. !lAutoA930    //Seleciona Filial = Sim
	aFilsCalc := MatFilCalc( lSelFil,,,,,3)
	If Empty( aFilsCalc )
		Return
	EndIf
	For nForFilial := 1 To Len( aFilsCalc )
		If aFilsCalc[ nForFilial, 1 ]
			aAdd(aEmpresa,{ IIf(!Empty(aFilsCalc[nForFilial][4]), aFilsCalc[nForFilial][4], Replic("0",14) ), cEmpAnt, aFilsCalc[nForFilial][2] } )
		EndIf
    Next                                  
Else
	DbSelectArea ("SM0")
		SM0->(DbSeek (cEmpAnt+cFilIni, .T.))
	Do While !SM0->(Eof ()) .And. (FWGrpCompany()==cEmpAnt) .And. (FWCodFil()<=cFilFin) .And. !lAutoA930
		aAdd (aEmpresa, {SM0->M0_CGC,FWGrpCompany(),FWCodFil()})	
		SM0->(DbSkip ())
	EndDo
	SM0->(RestArea(aAreaSM0))
	IF lAutoA930
      aAdd (aEmpresa, {SM0->M0_CGC,FWGrpCompany(),FWCodFil()})
   EndIf
EndIf 

dbSelectArea("SM0")

While nFil <= Len(aEmpresa)    

	If (FisChkDt(mv_par01) .And. FisChkDt(mv_par02)) .Or. lSimuRepro
		If (mv_par03  == 2) .Or. (mv_par03  == 3)
			If lAutoA930
				If cPaisLoc <> "BRA"
					DelSaiRem(lAutoA930)
					A930RPSaida(lAutoA930)
				ElseIf cPaisLoc == "BRA"
					A930RPSaida(lAutoA930,,.F.)
				EndIf
			ElseIf lBlind
				A930RPSaida(lBlind,,.F.)
			Else
				If cPaisLoc <> "BRA"
					oObj := MsNewProcess():New({|lEnd| DelSaiRem(lAutoA930,oObj)},"","",.F.)
					oObj:Activate()
					oObj := MsNewProcess():New({|lEnd| A930RPSaida(lAutoA930,oObj)},"","",.F.)
					oObj:Activate()
				ElseIf cPaisLoc == "BRA"
					oObj := MsNewProcess():New({|lEnd| A930RPSaida(lAutoA930,oObj,lSelFil, aEmpresa[nFil][3])},"","",.F.)
					oObj:Activate() 
				EndIf
			EndIf
		Endif	
		If (mv_par03  == 1) .Or. (mv_par03  == 3)
			If lAutoA930
				If cPaisLoc <> "BRA"
					DelEntRem(lAutoA930)
					A930RPEntrada(lAutoA930)
				ElseIf cPaisLoc == "BRA"
					A930RPEntrada(lAutoA930,,.F.)
				EndIf
			ElseIf lBlind
				A930RPEntrada(lBlind,,.F.)
			Else
				If cPaisLoc <> "BRA"
					oObj := MsNewProcess():New({|lEnd| DelEntRem(lAutoA930,oObj)},"","",.F.)
					oObj:Activate()
					oObj := MsNewProcess():New({|lEnd| A930RPEntrada(lAutoA930,oObj)},"","",.F.)
					oObj:Activate()
				ElseIf cPaisLoc == "BRA"
					oObj := MsNewProcess():New({|lEnd| A930RPEntrada(lAutoA930,oObj,lSelFil, aEmpresa[nFil][3])},"","",.F.)
					oObj:Activate()
				EndIf
			EndIf
		EndIf
	EndIf
	nFil++
	DbSkip()
EndDo

cFilAnt := cFilBak
RestArea(aAreaSM0)

cDtHoraFim := DtoC(Date()) + " - " + Time()

If cPaisLoc == "BRA" .And. !lAutoA930 .And. !lBlind
	MsgInfo("Reprocessamento concluído." + PULALINHA + PULALINHA + "Início: " + cDtHoraIni + PULALINHA + "Término: " + cDtHoraFim)
EndIf

FreeObj(oItDKC)
FreeObj(oImpDKB)

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A930LjDelFºAutor  ³Microsiga           º Data ³ 08/05/2008  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Deleta o SF3 no periodo informado nos parametros mv_par01  º±±
±±º          ³ e mv_par02 para Mapa Resumo.                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA930                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A930LjDelF3()

Local dDataIni 	:= mv_par01				// Data inicial 
Local dDataFim 	:= mv_par02				// Data final
Local aArea		:= GetArea()			// Guarda a area corrente
Local lModoFpF3 := FWModeAccess("SF3",3)=="C" .And. aSX3[FP_F3_MSFIL] //SF3->(FieldPos("F3_MSFIL")) > 0
Local lModoFpFT := FWModeAccess("SFT",3)=="C" .And. aSX3[FP_FT_MSFIL] //SFT->(FieldPos("FT_MSFIL")) > 0

While dDataIni <= dDataFim
	DbSelectArea("SF3")
	DbSetOrder(1)
	If DbSeek(xFilial()+DTOS(dDataIni),.T.)
	
		If lModoFpF3
			xWhile := "F3_MSFIL == xFilial('SF3')"
		Else
			xWhile := "F3_FILIAL == xFilial('SF3')"
		Endif
		xWhile := &('{||'+xWhile+'}')
		
		While !Eof() .And. Eval(xWhile) .AND. !SF3->(EOF()) ;
			.AND. SF3->F3_ENTRADA >= dDataIni ;
			.AND. SF3->F3_ENTRADA <= dDataFim
			
			If SF3->F3_SERIE == "ECF" .AND. !Empty( F3_PDV ) .AND. Alltrim( SF3->F3_OBSERV ) <> "NF CANCELADA"
				SF3->(RecLOCK("SF3",.F.))
				SF3->(dbDelete())
				SF3->(MsUnlock())
			EndIf
			SF3->(DbSkip())
		End
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Apos deletar o registro no SF3, deleta os itens no SFT³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("SFT")
	DbSetOrder(2)
	If DbSeek(xFilial("SFT") + "S" + DTOS(dDataIni),.T.)

		If lModoFpFT
			xWhile := "FT_MSFIL == xFilial('SFT') "
		Else
			xWhile := "FT_FILIAL == xFilial('SFT') "
		Endif
		xWhile := &('{||'+xWhile+'}')

		While !Eof() .And. Eval(xWhile) .AND. !SFT->(EOF()) ;
			.AND. SFT->FT_ENTRADA >= dDataIni ;
			.AND. SFT->FT_ENTRADA <= dDataFim

			If SFT->FT_SERIE == "ECF" .AND. !Empty( SFT->FT_PDV ) .AND. Alltrim( SFT->FT_OBSERV ) <> "NF CANCELADA" 

				RecLock("SFT",.F.)
				dbDelete()
				MsUnlock()

			EndIf
			DbSkip()
		End
	EndIf
	dDataIni++
End

RestArea(aArea)

Return NIL
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³DelEntRem ºAutor  ³Cleber Stenio Alves º Data ³ 19/05/2009  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Deleta os Remitos de Entrada existentes no SF3             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA930                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function DelEntRem(lAuto,oObj,lSelFil)

Local cQuery      	:= ""
Local cAlias      	:= "SF1"
Local cIndex      	:= ""
Local lQuery      	:= .F.
Local cFilOri		:= FWCodFil()
Local cFilIni 		:= Iif(Empty(MV_PAR12),cFilAnt,MV_PAR12)
Local cFilFin 		:= Iif(Empty(MV_PAR13),cFilAnt,MV_PAR13)
Local lFirst		:= .T.

#IFDEF TOP
	Local nX          := 0
	Local aStru       := {}
#ENDIF		

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Quando é automatico, efetua o reprocessamento apenas da filial atual -> CFILANT³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lAuto
	cFilIni := cFilAnt
	cFilFin := cFilAnt
Endif

If !lSelFil
	dbSelectArea("SM0")
	SM0->(dbSeek(cEmpAnt+cFilIni,.T.))
	If !lAuto
	   oObj:SetRegua1(SM0->(LastRec()))
	Endif                             
EndIF
	
While Iif(!lSelFil,!SM0->(Eof()) .And. FWGrpCompany() == cEmpAnt .And. FWCodFil() <= cFilFin,lFirst)

	If !lAuto
	   oObj:IncRegua1(AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL)
	Endif   
	
	Iif(!lSelFil,cFilAnt:= FWCodFil(),lFirst:= .F.)
	#IFDEF TOP
		aStru		:= {}
	#ENDIF
	cQuery		:= ""
   	cAlias		:= "SF1"
	cIndex		:= ""
	lQuery		:= .F.
	nX			:= 0

	dbSelectArea("SF1")
	dbSetOrder(1)
	#IFDEF TOP
		If ( TcSrvType()!="AS/400" )
			lQuery := .T.
			cAlias := GetNextAlias()
			aStru  := SF1->(dbStruct())
			cQuery := "SELECT SF1.*"
			cQuery += "FROM "+RetSqlName("SF1")+" SF1, " + RetSqlName("SF3")+" SF3 "

			If FWModeAccess("SF1",3)=="C" .And. SF1->(FieldPos("F1_MSFIL")) > 0			
				cQuery += "WHERE SF1.F1_MSFIL='"+cFilAnt+"' AND "
			Else	
				cQuery += "WHERE SF1.F1_FILIAL='"+xFilial("SF1")+"' AND "
			EndIf
			
			If FWModeAccess("SF3",3)=="C" .And. SF3->(FieldPos("F3_MSFIL")) > 0			
				cQuery += "SF3.F3_MSFIL='"+cFilAnt+"' AND "
			Else	
				cQuery += "SF3.F3_FILIAL='"+xFilial("SF3")+"' AND "
			EndIf

			cQuery +=       "SF1.F1_DOC>='"+MV_PAR04+"' AND "
			cQuery +=       "SF1.F1_DOC<='"+MV_PAR05+"' AND "
			cQuery +=       "SF1.F1_SERIE>='"+MV_PAR06+"' AND "
			cQuery +=       "SF1.F1_SERIE<='"+MV_PAR07+"' AND "
			cQuery +=       "SF1.F1_FORNECE>='"+MV_PAR08+"' AND "
			cQuery +=       "SF1.F1_FORNECE<='"+MV_PAR09+"' AND "
			cQuery +=       "SF1.F1_LOJA>='"+MV_PAR10+"' AND "
			cQuery +=       "SF1.F1_LOJA<='"+MV_PAR11+"' AND "
			cQuery +=       "SF1.F1_DTDIGIT>='"+DTOS(MV_PAR01)+"' AND "
			cQuery +=       "SF1.F1_DTDIGIT<='"+DTOS(MV_PAR02)+"' AND "
			cQuery +=       "SF1.F1_STATUS <> '' AND "
			cQuery +=       "("+IsRemito(3,'SF1.F1_TIPODOC')+ ") AND "
			cQuery +=       "SF1.F1_DOC = SF3.F3_NFISCAL AND "
			cQuery +=       "SF1.F1_SERIE = SF3.F3_SERIE AND "
			cQuery +=       "SF1.F1_FORNECE = SF3.F3_CLIEFOR AND "
			cQuery +=       "SF1.F1_LOJA = SF3.F3_LOJA AND "
			cQuery +=       "SF1.D_E_L_E_T_ ='' AND SF3.D_E_L_E_T_ ='' "
	
			cQuery := ChangeQuery(cQuery)
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.F.,.T.)	
			For nX := 1 To Len(aStru)
				If aStru[nX][2]<>"C"
					TcSetField(cAlias,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
				EndIf
			Next nX
		Else
	#ENDIF
			
		If FWModeAccess("SF1",3)=="C" .And. SF1->(FieldPos("F1_MSFIL")) > 0			
			cQuery += "F1_MSFIL='"+cFilAnt+"'.AND."
		Else	
			cQuery += "F1_FILIAL='"+xFilial("SF1")+"'.AND."
		EndIf

		cQuery += "F1_DOC>='"+MV_PAR04+"'.AND."
		cQuery += "F1_DOC<='"+MV_PAR05+"'.AND."
		cQuery += "F1_SERIE>='"+MV_PAR06+"'.AND."
		cQuery += "F1_SERIE<='"+MV_PAR07+"'.AND."
		cQuery += "F1_FORNECE>='"+MV_PAR08+"'.AND."
		cQuery += "F1_FORNECE<='"+MV_PAR09+"'.AND."
		cQuery += "F1_LOJA>='"+MV_PAR10+"'.AND."
		cQuery += "F1_LOJA<='"+MV_PAR11+"' .AND."
		cQuery += "DTOS(F1_DTDIGIT)>='"+DTOS(MV_PAR01)+"'.AND."
		cQuery += "DTOS(F1_DTDIGIT)<='"+DTOS(MV_PAR02)+"'.AND."
		cQuery += "("+IsRemito(2,'F1_TIPODOC')+") .AND."
    	cQuery += "!EMPTY(F1_STATUS)"
		
		cIndex := CriaTrab(,.F.)
		IndRegua("SF1",cIndex,IndexKey(),,cQuery)
		#IFDEF TOP
		EndIf
		#ENDIF
	
	dbSelectArea(cAlias)
	(cAlias)->(DbGoTop())
	If !lAuto
	   oObj:SetRegua2(SF1->(LastRec()))
	Endif   
    SF3->(dbSetOrder(1))
	While !(cAlias)->(Eof())
		If SF3->(MsSeek(xFilial("SF3")+Dtos((cAlias)->F1_DTDIGIT)+(cAlias)->F1_DOC+(cAlias)->F1_SERIE+(cAlias)->F1_FORNECE+(cAlias)->F1_LOJA))
		   	If Empty(SF3->F3_DTCANC)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Efetua a exclusao dos registros referente a Nota Fiscal no SF3³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				RecLock('SF3',.F.,.T.)
				SF3->(dbDelete())
				SF3->(MsUnlock())
				SF3->(FkCommit())
			EndIf
        EndIf
		(cAlias)->(dbSkip())
	EndDo
	If !lAuto
		oObj:IncRegua2(STR0006+" "+Dtoc((cAlias)->F1_DTDIGIT)+" "+STR0007+" "+(cAlias)->F1_SERIE+"-"+(cAlias)->F1_DOC)		
	EndIf
	Iif(!lSelFil,SM0->(dbSkip()),)
	If ( lQuery )
		dbSelectArea(cAlias)
		dbCloseArea()
		dbSelectArea("SF1")
	Else
		dbSelectArea("SF1")
		RetIndex("SF1")
	EndIf
Enddo
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Volta a empresa anteriormente selecionada.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lSelFil
dbSelectArea("SM0")
SM0->(dbSeek(cEmpAnt+cFilOri,.T.))
cFilAnt := FWCodFil()
EndIF

Return( .T. )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³DelSaiRem ºAutor  ³Cleber Stenio Alves º Data ³ 19/05/2009  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Deleta os Remitos de Saida existentes no SF3               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA930                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function DelSaiRem(lAuto,oObj,lSelFil)

Local cQuery      	:= ""
Local cAlias      	:= "SF2"
Local cIndex      	:= ""
Local lQuery      	:= .F.
Local cFilOri		:= FWCodFil()
Local cFilIni 		:= Iif(Empty(MV_PAR12),cFilAnt,MV_PAR12)
Local cFilFin 		:= Iif(Empty(MV_PAR13),cFilAnt,MV_PAR13)
Local lFirst		:= .T.

#IFDEF TOP
	Local nX          := 0
	Local aStru       := {}
#ENDIF		

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Quando é automatico, efetua o reprocessamento apenas da filial atual -> CFILANT³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lAuto
	cFilIni := cFilAnt
	cFilFin := cFilAnt
Endif

If !lSelFil
dbSelectArea("SM0")
SM0->(dbSeek(cEmpAnt+cFilIni,.T.))
If !lAuto
   oObj:SetRegua1(SM0->(LastRec()))
Endif   
EndIF
	
While Iif(!lSelFil,!SM0->(Eof()) .And. FWGrpCompany() == cEmpAnt .And. FWCodFil() <= cFilFin,lFirst)

	If !lAuto
	   oObj:IncRegua1(AllTrim(SM0->M0_NOME)+"/"+SM0->M0_FILIAL)
	Endif   
	
	Iif(!lSelFil,cFilAnt:= FWCodFil(),lFirst:= .F.)
	#IFDEF TOP
		aStru		:= {}
	#ENDIF
	cQuery		:= ""
   	cAlias		:= "SF2"
	cIndex		:= ""
	lQuery		:= .F.
	nX			:= 0

	dbSelectArea("SF2")
	dbSetOrder(1)
	#IFDEF TOP
		If ( TcSrvType()!="AS/400" )
			lQuery := .T.
			cAlias := "MA930GRVQ1"
			aStru  := SF2->(dbStruct())
						cQuery := "SELECT SF2.*,SF2.R_E_C_N_O_  SF2RECNO "
			cQuery += "FROM "+RetSqlName("SF2")+" SF2, " + RetSqlName("SF3")+" SF3 "

			If FWModeAccess("SF2",3)=="C" .And. SF2->(FieldPos("F2_MSFIL")) > 0			
				cQuery += "WHERE SF2.F2_MSFIL='"+cFilAnt+"' AND "
			Else	
				cQuery += "WHERE SF2.F2_FILIAL='"+xFilial("SF2")+"' AND "
			EndIf
			
			If FWModeAccess("SF3",3)=="C" .And. SF3->(FieldPos("F3_MSFIL")) > 0			
				cQuery += "SF3.F3_MSFIL='"+cFilAnt+"' AND "
			Else	
				cQuery += "SF3.F3_FILIAL='"+xFilial("SF3")+"' AND "
			EndIf

			cQuery +=       "SF2.F2_DOC>='"+MV_PAR04+"' AND "
			cQuery +=       "SF2.F2_DOC<='"+MV_PAR05+"' AND "
			cQuery +=       "SF2.F2_SERIE>='"+MV_PAR06+"' AND "
			cQuery +=       "SF2.F2_SERIE<='"+MV_PAR07+"' AND "
			cQuery +=       "SF2.F2_CLIENTE>='"+MV_PAR08+"' AND "
			cQuery +=       "SF2.F2_CLIENTE<='"+MV_PAR09+"' AND "
			cQuery +=       "SF2.F2_LOJA>='"+MV_PAR10+"' AND "
			cQuery +=       "SF2.F2_LOJA<='"+MV_PAR11+"' AND "
			cQuery +=       "SF2.F2_EMISSAO>='"+DTOS(MV_PAR01)+"' AND "
			cQuery +=       "SF2.F2_EMISSAO<='"+DTOS(MV_PAR02)+"' AND "
			cQuery +=       "("+IsRemito(3,'SF2.F2_TIPODOC')+ ") AND "
			cQuery +=       "SF2.F2_DOC = SF3.F3_NFISCAL AND "
			cQuery +=       "SF2.F2_SERIE = SF3.F3_SERIE AND "
			cQuery +=       "SF2.F2_CLIENTE = SF3.F3_CLIEFOR AND "
			cQuery +=       "SF2.F2_LOJA = SF3.F3_LOJA AND "
			cQuery +=       "SF2.D_E_L_E_T_ ='' AND SF3.D_E_L_E_T_ ='' "
	
			cQuery := ChangeQuery(cQuery)
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)	
			For nX := 1 To Len(aStru)
				If aStru[nX][2]<>"C"
					TcSetField(cAlias,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
				EndIf
			Next nX
		Else
	#ENDIF
			
		If FWModeAccess("SF2",3)=="C" .And. SF2->(FieldPos("F2_MSFIL")) > 0			
			cQuery += "F2_MSFIL='"+cFilAnt+"'.AND."
		Else	
			cQuery += "F2_FILIAL='"+xFilial("SF2")+"'.AND."
		EndIf
				
		cQuery += "F2_DOC>='"+MV_PAR04+"'.AND."
		cQuery += "F2_DOC<='"+MV_PAR05+"'.AND."
		cQuery += "F2_SERIE>='"+MV_PAR06+"'.AND."
		cQuery += "F2_SERIE<='"+MV_PAR07+"'.AND."
		cQuery += "F2_CLIENTE>='"+MV_PAR08+"'.AND."
		cQuery += "F2_CLIENTE<='"+MV_PAR09+"'.AND."
		cQuery += "F2_LOJA>='"+MV_PAR10+"'.AND."
		cQuery += "F2_LOJA<='"+MV_PAR11+"' .AND."
		cQuery += "DTOS(F2_EMISSAO)>='"+DTOS(MV_PAR01)+"'.AND."
		cQuery += "DTOS(F2_EMISSAO)<='"+DTOS(MV_PAR02)+"'.AND."
		cQuery += "("+IsRemito(2,'F2_TIPODOC')+")"
		
		cIndex := CriaTrab(,.F.)
		IndRegua("SF2",cIndex,IndexKey(),,cQuery)
		#IFDEF TOP
		EndIf
		#ENDIF
	
	dbSelectArea(cAlias)
	(cAlias)->(DbGoTop())
	If !lAuto
	   oObj:SetRegua2(SF2->(LastRec()))
	Endif   

	SF3->(dbSetOrder(1))
	While !(cAlias)->(Eof())
		If SF3->(MsSeek(xFilial("SF3")+Dtos((cAlias)->F2_EMISSAO)+(cAlias)->F2_DOC+(cAlias)->F2_SERIE+(cAlias)->F2_CLIENTE+(cAlias)->F2_LOJA))
		   	If Empty(SF3->F3_DTCANC)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Efetua a exclusao dos registros referente a Nota Fiscal no SF3 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				RecLock('SF3',.F.,.T.)
				SF3->(dbDelete())
				SF3->(MsUnlock())
				SF3->(FkCommit())
			EndIf
        EndIf
		(cAlias)->(dbSkip())
	EndDo
	If !lAuto
		oObj:IncRegua2(STR0006+" "+Dtoc((cAlias)->F2_EMISSAO)+" "+STR0007+" "+(cAlias)->F2_SERIE+"-"+(cAlias)->F2_DOC)		
	EndIf
		
	Iif(!lSelFil,SM0->(dbSkip()),)
	If ( lQuery )
		dbSelectArea(cAlias)
		dbCloseArea()
		dbSelectArea("SF2")
	Else
		dbSelectArea("SF2")
		RetIndex("SF2")
	EndIf
Enddo
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Volta a empresa anteriormente selecionada.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lSelFil
dbSelectArea("SM0")
SM0->(dbSeek(cEmpAnt+cFilOri,.T.))
cFilAnt := FWCodFil()
EndIF

Return( .T. )
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A930LjGrava ºAutor  ³Vendas e CRM        º Data ³ 22/03/2009º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Faz o backup e zera os valores do array                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA930                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/ 

Static Function A930LjGrava( cAlias, aArrayBack, aArrayZero) 

Local aEstruAlias   := {}                                  // Variavel com a estrutura da array 
Local nCount        := 0                                   // Variavel contador
Local nZera         := 0                                   // Zerador 
Local lRet          := .T.                                 // Retorno 
	
aAdd(aArrayBack, {0,{}})
aAdd(aArrayZero, {0,{}})
aEstruAlias  := (cAlias)->(DbStruct())
For nCount := 1 To Len(aEstruAlias)
	If aEstruAlias[nCount,2] == "N"
		aAdd(aArrayBack[Len(aArrayBack)][2],{aEstruAlias[nCount,1], (cAlias)->&(aEstruAlias[nCount,1]) })  
		aAdd(aArrayZero[Len(aArrayZero)][2],{aEstruAlias[nCount,1], nZera })    
	EndIf
Next nCount   
	 
Return lRet	

Static Function SchedDef()

Local aOrd := {}
Local aParam := {}

aParam := {"P", "MTA930", "", aOrd,}

Return aParam

//-------------------------------------------------------------------
/*/{Protheus.doc} CmpTagRess

Função que verifica a existência de campos da SFT referente algumas Tags
do XML, referente ao Ressarcimento do ICMS ST

@author Erick Dias
@since 22/02/2019
@version 12.1.23
/*/
//------------------------------------------------------------------- 
Static Function CmpTagRess()

Local lRet    :=    aSX3[FP_FT_BSTANT]	 .and. ; //SFT->(FieldPos("FT_BSTANT")) > 0 .AND. ;
                   	aSX3[FP_FT_PSTANT]	 .and. ; //SFT->(FieldPos("FT_PSTANT")) > 0 .AND. ;
                   	aSX3[FP_FT_VSTANT]	 .and. ; //SFT->(FieldPos("FT_VSTANT")) > 0 .AND. ;
                   	aSX3[FP_FT_BFCANTS] .and. ; //SFT->(FieldPos("FT_BFCANTS")) > 0 .AND. ;
                   	aSX3[FP_FT_PFCANTS] .and. ; //SFT->(FieldPos("FT_PFCANTS")) > 0 .AND. ;
                   	aSX3[FP_FT_VFCANTS] .and. ; //SFT->(FieldPos("FT_VFCANTS")) > 0 .AND. ;										
                   	aSX3[FP_FT_VICPRST]  	     //SFT->(FieldPos("FT_VICPRST")) > 0
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} RefTagRess

Função que verifica a existência das referências na MATXFIS  referente algumas Tags
do XML, referente ao Ressarcimento do ICMS ST

@author Erick Dias
@since 22/02/2019
@version 12.1.23
/*/
//------------------------------------------------------------------- 
Static Function RefTagRess()

Local lRet  :=  !Empty(MaFisScan("IT_BSTANT",.F.)) .AND. ;
                !Empty(MaFisScan("IT_PSTANT",.F.)) .AND. ;
                !Empty(MaFisScan("IT_VSTANT",.F.)) .AND. ;				
				!Empty(MaFisScan("IT_BFCANTS",.F.)) .AND. ;
				!Empty(MaFisScan("IT_PFCANTS",.F.)) .AND. ;
				!Empty(MaFisScan("IT_VFCANTS",.F.)) .AND. ;
                !Empty(MaFisScan("IT_VICPRST",.F.))
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} LoadTagRess

Função que fará MaFisLoad nos campos das Tags do XML, são campos
referente ao Ressarcimento de ICMS. 
Pré requisito desta função é posicionar previamente a tabela SFT

//O MaFisLoad abaixo é devido a impossibilidade de criação de campos nas tabelas SD1 e SD2, pois são 
//informações referentes a Tags do XML do Ressarcimento de ICMS, e que precisamos manter estas informações
//caso a nota já tenha sido transmitida, por este motivo estou recuperando o valor antes da
//SFT ser deletada. É o mesmo processo que já é feito com o F3_CODRSEF armazenado na cCodSef

@author Erick Dias
@since 22/02/2019
@version 12.1.23
/*/
//------------------------------------------------------------------- 
Static Function LoadTagRess(nItem)  

MaFisLoad("IT_BSTANT" , SFT->FT_BSTANT,  nItem )
MaFisLoad("IT_PSTANT" , SFT->FT_PSTANT,  nItem )
MaFisLoad("IT_VSTANT" , SFT->FT_VSTANT,  nItem )
MaFisLoad("IT_VICPRST", SFT->FT_VICPRST, nItem )
MaFisLoad("IT_BFCANTS",  SFT->FT_BFCANTS, nItem )
MaFisLoad("IT_PFCANTS",  SFT->FT_PFCANTS, nItem )
MaFisLoad("IT_VFCANTS",  SFT->FT_VFCANTS, nItem )


Return

//-------------------------------------------------------------------
/*/{Protheus.doc} CmpApoEsp

Função que verifica a existência de campos da SFT referente Aposentadoria
Especial

@author Erich Buttner
@since 11/07/2019
@version 12.1.25
/*/
//------------------------------------------------------------------- 
Static Function CmpApoEsp()

Local lRet    :=    aSX3[FP_FT_ALCP15] .and. ;// SFT->(FieldPos("FT_ALCP15")) > 0 .AND. ;
                    aSX3[FP_FT_ALCP20] .and. ;// SFT->(FieldPos("FT_ALCP20")) > 0 .AND. ;
                    aSX3[FP_FT_ALCP25] .and. ;// SFT->(FieldPos("FT_ALCP25")) > 0 .AND. ;
                    aSX3[FP_FT_BSCP15] .and. ;// SFT->(FieldPos("FT_BSCP15")) > 0 .AND. ;
                    aSX3[FP_FT_BSCP20] .and. ;// SFT->(FieldPos("FT_BSCP20")) > 0 .AND. ;
                    aSX3[FP_FT_BSCP25] .and. ;// SFT->(FieldPos("FT_BSCP25")) > 0 .AND. ;										
					aSX3[FP_FT_SECP15] .and. ;// SFT->(FieldPos("FT_SECP15")) > 0 .AND. ;
					aSX3[FP_FT_SECP20] .and. ;// SFT->(FieldPos("FT_SECP20")) > 0 .AND. ;
					aSX3[FP_FT_SECP25] .and. ;// SFT->(FieldPos("FT_SECP25")) > 0 .AND. ;
					aSX3[FP_FT_VLCP15] .and. ;// SFT->(FieldPos("FT_VLCP15")) > 0 .AND. ; 
					aSX3[FP_FT_VLCP20] .and. ;// SFT->(FieldPos("FT_VLCP20")) > 0 .AND. ; 
					aSX3[FP_FT_VLCP25] 		  // SFT->(FieldPos("FT_VLCP25")) > 0  
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} RefApoEsp

Função que verifica a existência das referências na MATXFIS  referente Aposentadoria
Especial

@author Erich Buttner
@since 11/07/2019
@version 12.1.25
/*/
//------------------------------------------------------------------- 
Static Function RefApoEsp()

Local lRet  :=  !Empty(MaFisScan("IT_ALCP15",.F.)) .AND. ;
                !Empty(MaFisScan("IT_ALCP20",.F.)) .AND. ;
                !Empty(MaFisScan("IT_ALCP25",.F.)) .AND. ;				
				!Empty(MaFisScan("IT_BSCP15",.F.)) .AND. ;
				!Empty(MaFisScan("IT_BSCP20",.F.)) .AND. ;
				!Empty(MaFisScan("IT_BSCP25",.F.)) .AND. ;
				!Empty(MaFisScan("IT_SECP15",.F.)) .AND. ;
				!Empty(MaFisScan("IT_SECP20",.F.)) .AND. ;
				!Empty(MaFisScan("IT_SECP25",.F.)) .AND. ;
				!Empty(MaFisScan("IT_VLCP15",.F.)) .AND. ;
				!Empty(MaFisScan("IT_VLCP20",.F.)) .AND. ;
                !Empty(MaFisScan("IT_VLCP25",.F.))
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} LoadApoEsp

Função que fará MaFisLoad nos campos da Aposentadoria Especial.
Pré requisito desta função é posicionar previamente a tabela SFT

//O MaFisLoad abaixo é devido a impossibilidade de criação de campos nas tabelas SD1 e SD2, pois são 
//informações referentes a Tags do XML do Ressarcimento de ICMS, e que precisamos manter estas informações
//caso a nota já tenha sido transmitida, por este motivo estou recuperando o valor antes da
//SFT ser deletada. É o mesmo processo que já é feito com o F3_CODRSEF armazenado na cCodSef

@author Erich Buttner
@since 11/07/2019
@version 12.1.25
/*/
//------------------------------------------------------------------- 
Static Function LoadApoEsp(nItem)  
	IF SFT->FT_ALCP15 > 0
		MaFisLoad("IT_ALCP15" , SFT->FT_ALCP15,  nItem )
	Endif

	IF SFT->FT_ALCP20 > 0
		MaFisLoad("IT_ALCP20" , SFT->FT_ALCP20,  nItem )
	Endif

	IF SFT->FT_ALCP25 > 0
		MaFisLoad("IT_ALCP25" , SFT->FT_ALCP25,  nItem )				
	Endif

	IF SFT->FT_BSCP15 > 0
		MaFisLoad("IT_BSCP15" , SFT->FT_BSCP15,  nItem )
	Endif

	IF SFT->FT_BSCP20 > 0
		MaFisLoad("IT_BSCP20" , SFT->FT_BSCP20,  nItem )
	Endif

	IF SFT->FT_BSCP25 > 0
		MaFisLoad("IT_BSCP25" , SFT->FT_BSCP25,  nItem )
	Endif

	IF SFT->FT_SECP15 > 0
		MaFisLoad("IT_SECP15" , SFT->FT_SECP15,  nItem )
	Endif

	IF SFT->FT_SECP20 > 0
		MaFisLoad("IT_SECP20" , SFT->FT_SECP20,  nItem )
	Endif

	IF SFT->FT_SECP25 > 0
		MaFisLoad("IT_SECP25" , SFT->FT_SECP25,  nItem )
	Endif

	IF SFT->FT_VLCP15 > 0
		MaFisLoad("IT_VLCP15" , SFT->FT_VLCP15,  nItem )
	Endif

	IF SFT->FT_VLCP20 > 0
		MaFisLoad("IT_VLCP20" , SFT->FT_VLCP20,  nItem )
	Endif

	IF SFT->FT_VLCP25 > 0
		MaFisLoad("IT_VLCP25" , SFT->FT_VLCP25,  nItem )
	Endif

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} RefDescFis

Função que verifica a existência de campos da SFT referente Desconto
Fiscal

@author Erich Buttner
@since 27/08/2019
@version 12.1.25
/*/
//------------------------------------------------------------------- 
Static Function RefDescFis()

Local lRet  :=  !Empty(MaFisScan("IT_DESCFIS",.F.))

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} RefPautSt

Função que verifica a existência de campos da SFT referente Desconto
Fiscal

@author Yuri Gimenes
@since 07/01/2022
@version 12.1.33
/*/
//------------------------------------------------------------------- 
Static Function RefPautSt()

Local lRet  :=  !Empty(MaFisScan("IT_PAUTST",.F.))

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} LoadDescFi

Função que fará MaFisLoad nos campos da Aposentadoria Especial.
Pré requisito desta função é posicionar previamente a tabela SFT

//O MaFisLoad abaixo é devido a impossibilidade de criação de campos nas tabelas SD1 e SD2, pois são 
//informações referentes a Tags do XML do Ressarcimento de ICMS, e que precisamos manter estas informações
//caso a nota já tenha sido transmitida, por este motivo estou recuperando o valor antes da
//SFT ser deletada. É o mesmo processo que já é feito com o F3_CODRSEF armazenado na cCodSef

@author Erich Buttner
@since 27/08/2019
@version 12.1.25
/*/
//------------------------------------------------------------------- 
Static Function LoadDescFi(nItem)  
IF SFT->FT_DESCFIS > 0
	MaFisLoad("IT_DESCFIS" , SFT->FT_DESCFIS,  nItem )
Endif
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} LoadPautSt

Pré requisito desta função é posicionar previamente a tabela SFT

//O MafisLoad e MafisLf abaixo é devido a troca de valores no ANFItem[nItem][86][02] no MaFisIni 

@author Yuri Gimenes
@since 07/01/2022
@version 0.1

@Alteração - Edinei Cruz
@since 20/04/2022
@version 0.2
/*/
//------------------------------------------------------------------- 
	Static Function LoadPautSt(nItem)  
	IF SFT->FT_PAUTST > 0
		MaFisLoad("IT_PAUTST" , SFT->FT_PAUTST,  nItem)
		MaFisLF(nItem)
	Endif

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} M930NLvr

Função para guardar informações das referências antes da exclusão da SF3/SFT
quando a NF não possui livro e foi alterado o TES para gravar livro.

@param cAlias		- Alias temporário com os registros filtrados da SF1/SF2
@param cTabD1D2		- Tabela para decisão dos campos
@param nDbOrder		- Índice da tabela
@param cChvF1F2		- Chave para Seek na tabela SF1/SF2
@param lMaFisItem	- FindFunction para função MaFisItem

@author Renato Rezende
@since 02/07/2021
@version 12.1.33
/*/
//-------------------------------------------------------------------
Static Function m930NLvr(cAlias,cTabD1D2,nDbOrder,cChvF1F2,lMaFisItem)

Local cTes		:= ""
Local cItem		:= ""
Local cChvD1D2	:= ""
Local nItem		:= 0

(cTabD1D2)->(DbSetOrder(nDbOrder))

If (cTabD1D2)->(MsSeek(cChvF1F2,.T.))

	cTes:= Iif(cTabD1D2=="SD2",SD2->D2_TES,SD1->D1_TES)

	SF4->(DbSetOrder(1))
	//Validar se será gerado livro fiscal - Mesma validação que tem na função MaFisLF/xFisLF para gravar livro
	//Feita a validação apenas no primeiro iem, porque não existe nota gerando livro pela metade, ou todos os itens geram ou não
	If SF4->(MsSeek(xFilial("SF4")+cTes)) .And. (SF4->F4_LFICM <> "N" .Or. SF4->F4_LFIPI <> "N" .Or. SF4->F4_LFISS $"TIO")

		cChvD1D2:= IIF(cTabD1D2=="SD2",(cTabD1D2)->(D2_FILIAL+D2_DOC+D2_SERIE+D2_CLIENTE+D2_LOJA),(cTabD1D2)->(D1_FILIAL+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA))
		
		//Guardar informações para o reprocessamento não recalcular caso algum cadastro seja alterado
		While (cTabD1D2)->(!Eof()) .And. cChvD1D2 == cChvF1F2

			cItem:= IIF(cTabD1D2=="SD2",SD2->D2_ITEM,SD1->D1_ITEM)

			nItem   := Iif(lMaFisItem,MaFisItem('IT_ITEM',  Alltrim(cItem)), 0)
			//Realizado ajuste para diferença de tamanho de Item entre o D1_ITEM e D2_ITEM ou bancos com tamanho do _ITEM alterado
			If nItem == 0
				nItem   := Iif(lMaFisItem,MaFisItem('IT_ITEM',  cItem), 0)
			EndIf

			//Nota Fiscal de Saída
			If cTabD1D2 == "SD2"
				//Guardar Aliquota do ISS
				If SD2->D2_ALIQISS > 0 .And. nItem > 0
					MaFisAlt("IT_ALIQISS", SD2->D2_ALIQISS, nItem)
				EndIf

			//Nota Fiscal de Entrada
			Else
				//Guardar Aliquota do ISS
				If SD1->D1_ALIQISS > 0 .And. nItem > 0
					MaFisAlt("IT_ALIQISS", SD1->D1_ALIQISS, nItem)
				EndIf
			EndIf	

			(cTabD1D2)->(DbSkip())

			//Atualizando a Chave após o DBSkip, para que seja feita a verificação de igualdade no While, evitando Leitura da SD1/SD2 inteira
			cChvD1D2:= IIF(cTabD1D2=="SD2",(cTabD1D2)->(D2_FILIAL+D2_DOC+D2_SERIE+D2_CLIENTE+D2_LOJA),(cTabD1D2)->(D1_FILIAL+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA))

		EndDo
	EndIf
EndIf

Return nil


//-------------------------------------------------------------------
/*/{Protheus.doc} A930LoadX3
Funcao responsavel por realizar o cache dos parametros (SX3)

@return aRet - Array com o cache do SX3.

@author Rafael Oliveira
/*/
//-------------------------------------------------------------------
Function A930LoadX3()

Local nX := 0
Local nPos := 0
Local aAlias := {}
Local aRet := {}
Local bError
Local lErro	:= .F.
Local aFPCpo := {	{"SF1","F1_MSFIL"},;	//01
					{"SF2","F2_MSFIL"},;	//02
					{"SF3","F3_MSFIL"},;	//03
					{"SFT","FT_MSFIL"},;	//04
					{"SFT","FT_PAUTST"},;	//05
					{"SF2","F2_SERSAT"},;	//06
					{"SFT","FT_DESCFIS"},;	//07
					{"SFT","FT_BSTANT"},;	//08
					{"SFT","FT_PSTANT"},;	//09
					{"SFT","FT_VSTANT"},;	//10
					{"SFT","FT_BFCANTS"},;	//11
					{"SFT","FT_PFCANTS"},;	//12
					{"SFT","FT_VFCANTS"},;	//13							
					{"SFT","FT_VICPRST"},;	//14
					{"SFT","FT_ALCP15"},;	//15
					{"SFT","FT_ALCP20"},;	//16
					{"SFT","FT_ALCP25"},;	//17
					{"SFT","FT_BSCP15"},;	//18
					{"SFT","FT_BSCP20"},;	//19
					{"SFT","FT_BSCP25"},;	//20
					{"SFT","FT_SECP15"},;	//21
					{"SFT","FT_SECP20"},;	//22
					{"SFT","FT_SECP25"},;	//23
					{"SFT","FT_VLCP15"},;	//24
					{"SFT","FT_VLCP20"},;	//25
					{"SFT","FT_VLCP25"}}	//26

bError := ErrorBlock( {|| lErro := .T. } )

BEGIN SEQUENCE
	//Tentar executar alias indic
	FWAliasIndic("SA1")
END SEQUENCE

ErrorBlock( bError )

If !lErro
	For nX := 1 to Len(aFPCpo)
		nPos := aScan( aAlias , {|x| x[1] == aFPCpo[nX,01] } )
		If nPos == 0
			aAdd( aAlias , { aFPCpo[nX,01] , FWAliasIndic(aFPCpo[nX,01]) } )
			nPos := Len(aAlias)
		EndIf
		aAdd(aRet , IIf( aAlias[nPos,02] .And. (aFPCpo[nX,01])->(FieldPos(aFPCpo[nX,02])) > 0 , .T. , .F. ) )
	Next nX
EndIf

Return aRet



//-------------------------------------------------------------------
/*/{Protheus.doc} A930LoadX6
Funcao responsavel por realizar o cache dos parametros (SX6)

@return aRet - Array com o cache do SX6.

@author Rafael Oliveira
/*/
//-------------------------------------------------------------------
Function A930LoadX6()

Local nX := 0
Local aRet := {}

Local aParam := {{"MV_LJLVFIS",1},;	//01
				 {"MV_USACFPS",.F.},;	//02
				 {"MV_HISTFIS",.F.}}		//03					

For nX:=1 to Len(aParam)
	aAdd(aRet , GetNewPar((aParam[nX,1]),aParam[nX,2]))
Next nX


Return aRet
//-------------------------------------------------------------------
/*/{Protheus.doc} FisXDelDKB

Função que faz a exclusão dos dados da tabela DKB.
Aqui a hipótese para exclusão é via reprocessamento.

@author Adilson Roberto
@since 20/09/2022
@version 122210
/*/
//-------------------------------------------------------------------
Function FisXDelDKB(cFil, cFor, cLoj, cNfisc, cSer, lAgrupIt)
Local   cAliasDkB := GetNextAlias()
Local   cItXml	  := ""
Local nI		  := 0
Local aImp		  := {}
Local aBind		  := {}

Default cFil	  := ""
Default cFor	  := ""
Default cLoj	  := ""
Default cNfisc	  := ""
Default cSer	  := ""
Default lAgrupIt  := .F.
	
If _cQryDKB == nil 
	_cQryDKB := "SELECT  DKB.DKB_ITXML, DKB.R_E_C_N_O_ AS RECNO"
	_cQryDKB += " FROM " + RetSqlName('DKB') + " DKB"
	_cQryDKB += " WHERE DKB.DKB_FILIAL    = ?"
	_cQryDKB += " AND DKB.DKB_FORNEC      = ?"
	_cQryDKB += " AND DKB.DKB_LOJA        = ?"
	_cQryDKB += " AND DKB.DKB_DOC         = ?"
	_cQryDKB += " AND DKB.DKB_SERIE       = ?"
	_cQryDKB += " AND DKB.D_E_L_E_T_  	  = ?"
	_cQryDKB += " ORDER BY 1 "
EndIf 
aBind := {}
AADD(aBind,cFil)
AADD(aBind,cFor)
AADD(aBind,cLoj)
AADD(aBind,cNfisc)
AADD(aBind,cSer)
AADD(aBind,Space(1))
dbUseArea(.T.,'TOPCONN',TcGenQry2(,,_cQryDKB,aBind),cAliasDkB,.T.,.F.)
		
//Retorno objeto com os tributos por item do xml	
If lAgrupIt	
	oImpDKB:=  MafisAgrIt()
Endif
//Laço para excluir todas as linhas de calculos de impostos da tabela CKB
DbSelectArea("DKB")
While (cAliasDkB)->(!Eof())
	//Deleto as linhas da tabela DKB
	If !Empty((cAliasDkB)->RECNO)	
		DKB->( dbGoTo((cAliasDkB)->RECNO))
		DKB->(RecLock("DKB",.F.))
		DKB->(dbDelete())
		DKB->(MsUnLock())
		DKB->(FkCommit())
		cItXml := (cAliasDkB)->DKB_ITXML
	EndIf
	(cAliasDkB)->(dbSkip())	
	//Apendo as novas linhas
	If !cItXml == (cAliasDkB)->DKB_ITXML
		Iif(Valtype(oImpDKB) =='J', aImp:= oImpDKB[cItXml], {})
		If Len(aImp) > 0
			For nI:= 1 To Len(aImp)
				If RecLock("DKB",.T.)
					DKB->DKB_FILIAL	:= cFil
					DKB->DKB_DOC	:= cNfisc
					DKB->DKB_SERIE	:= cSer
					DKB->DKB_FORNEC	:= cFor
					DKB->DKB_LOJA	:= cLoj
					DKB->DKB_ITXML	:= cItXml
					DKB->DKB_IDTRIB	:= aImp[nI][4]
					DKB->DKB_DESCTR	:= aImp[nI][5]
					DKB->DKB_BASTRI	:= aImp[nI][1]
					DKB->DKB_ALQTRI	:= aImp[nI][2]
					DKB->DKB_VLRTRI	:= aImp[nI][3]
				Endif
				MsUnLock()
				DKB->(FkCommit())
			Next nI
		Endif	
	Endif
EndDo
(cAliasDkB)->(DbCloseArea())
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} FisxApdDKB

Função que faz a inclusão dos dados da tabela DKB.

@author Adilson Roberto
@since 20/09/2022
@version 122210
/*/
//-------------------------------------------------------------------
Function FisxLDKC(cFil, cFor, cLoj, cNfisc, cSer, lProcDKB)
Local cAliasDkC := GetNextAlias()
Local aBind		:= {}

Default cFil	 := ""
Default cFor	 := ""
Default cLoj	 := ""
Default cNfisc	 := ""
Default cSer	 := ""
Default lProcDKB := .F.
If _cQryDKC == nil 
	_cQryDKC := "SELECT DKC.DKC_FILIAL, DKC.DKC_FORNEC, DKC.DKC_SERIE, DKC.DKC_DOC, DKC.DKC_LOJA, DKC.DKC_ITEMNF, DKC.DKC_ITXML"
	_cQryDKC += " FROM " + RetSqlName('DKC') + " DKC"
	_cQryDKC += " WHERE DKC.DKC_FILIAL    = ?"
	_cQryDKC += " AND DKC.DKC_FORNEC      = ?"
	_cQryDKC += " AND DKC.DKC_LOJA        = ?"
	_cQryDKC += " AND DKC.DKC_DOC         = ?"
	_cQryDKC += " AND DKC.DKC_SERIE       = ?"
	_cQryDKC += " AND DKC.D_E_L_E_T_  	  = ?"
	_cQryDKC += " ORDER BY 1,2,3,4,5,6,7"
EndIf 
aBind := {}
AADD(aBind,cFil)
AADD(aBind,cFor)
AADD(aBind,cLoj)
AADD(aBind,cNfisc)
AADD(aBind,cSer)
AADD(aBind,Space(1))
dbUseArea(.T.,'TOPCONN',TcGenQry2(,,_cQryDKC,aBind),cAliasDkC,.T.,.F.)
If oItDKC == Nil
	oItDKC := JsonObject():New()
Endif	

//Laço para relacionar todos os itens da nota com seus respectivos itens do XML
While (cAliasDkC)->(!Eof())
	oItDKC[(cAliasDkC)->DKC_ITEMNF] := (cAliasDkC)->DKC_ITXML
	If !lProcDKB
		lProcDKB := .T.
	Endif	
	(cAliasDkC)->(DbSkip()) 
EndDo
(cAliasDkC)->(DbCloseArea())
Return

//-------------------------------------------------------------------


/*/{Protheus.doc} simulatorReprocessing
	Verifica se o reprocessamento foi executado pelo Simulador preenchendo as
	variáveis estáticas que auxiliam o processamento.
	@type  Static Function
	@author anedino.santos
	@since 03/03/2025
	@version 12.1.2510
	@param oManager, object, objeto gerenciador enviado pelo Simulador
	@return lRet, logical, identifica se o reprocessamento foi executado pelo Simulador
/*/
Static Function simulatorReprocessing(oManager)
	local lRet := .F.
	local cClassName := "TOTVS.PROTHEUS.BACKOFFICE.FISCAL.REPROCESSING.SIMULATORREPROCESSINGMANAGER"
	if lRet := (oManager <> Nil .and. GetClassName(oManager) == cClassName)
		_oManager := oManager
		_lF2BTeste := oManager:getF2BTeste()
		_lCalcTG   := .T.
	endif
Return lRet
