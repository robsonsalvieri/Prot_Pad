#INCLUDE "TLPP-CORE.TH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "TBICONN.CH"
#INCLUDE 'FILEIO.CH'
#INCLUDE 'FISA843.CH'

#Define Separador  "|"
#Define SepaTX     ","

/*/{Protheus.doc} FISA843
	Función principal donde se ingresa los filtros para la genearción de losa archivos csv, excel y detona 
    las funciones para crear los archivos con los datos requeridos
	@type  Function
	@author adrian.perez
	@since 20/06/2025
	@return nil
	/*/

Function FISA843()

Local   aArrSay := {}
Local   aArrBut := {}
Local   lExeFun:= .F.
Local   lAutomato 	:= IsBlind()

IF !lAutomato

    AADD(aArrSay, STR0041) //STR0041 "La rutina tiene el objetivo de generar los archivos CSV y XLSX donde la infromación"
    AADD(aArrSay, STR0042) //STR0042  "associada es del calculo de retenciones o autorretenciones para los impuestos:"
    AADD(aArrSay, STR0043) //STR0043 "ICA, Avisos y Tablero, Sobretasa Bomberil para la municipalidad de Bucaramanga.'

    AADD(aArrBut, {1, .T., {|| lExeFun := .T., FechaBatch()}})
    AADD(aArrBut, {2, .T., {|| lExeFun := .F., FechaBatch()}})


    FormBatch(STR0032, aArrSay, aArrBut)
EndiF
//Si se confirma 
If lExeFun .or. lAutomato
   
  fEjecutar()
    
EndIf

  aSize(aArrSay,0)
  aSize(aArrBut,0)
  

Return nil

/*/{Protheus.doc} fBuscaCamp
	Función que busca los campos correspondientes
	@type  Function
	@author adrian.perez
	@since 20/06/2025
    @param  cImpNomb, carácter, cadena con los codigos de impuestos por ejemplo "RCO|BMI"
    @return aImp, arreglo, contiene los campos del libro fiscal pertenecientes a los códigos del param cImpNomb
	@return nil
	/*/
function fBuscaCamp(cImpNomb)

Local aAreaAnt := GETAREA()
Local cCpoLvro :=""
Local aAux:={}
Local aImp:={}
Local nA:=0
Local nPos:=0 

Default cImpNomb:=""
 
    dbSelectArea("SFB")
    SFB->(dbGoTop())
    SFB->(dbSetOrder(1))

    aAux:=STRTOKARR( cImpNomb, Separador )

    For nA:=1 to len(aAux)
        If SFB->(MsSeek(xFilial("SFB")+aAux[nA]))
            cCpoLvro:= SFB->FB_CPOLVRO
            nPos:= ASCAN(aImp, cCpoLvro )
            If nPos==0 // evitar campo repeditos
             Aadd(aImp,cCpoLvro)
            EndIF
        EndIf 
       
    Next
    RestArea(aAreaAnt)   
    aSize(aAreaAnt,0)
    aSize(aAux,0)

Return aImp

/*/{Protheus.doc} fAcumulado
    Función para realizar los acumulados totales para retenciones y autoretenciones de acuerdo a los filtros
    generando los archivos csv y excel 
    @type  Function
    @author adrian.perez
    @since 21/06/2025
    @param  cCodMun, carácter, código de municipio Bucaramanga
    @param  dPedIni, fecha, fecha de inicio para filtro
    @param  dPedFin, fecha, fecha fin  para filtro
    @param  cEmail, carácter, email usado en autor retenciones
    @param  cDir, carácter, ruta donde se guardaran los archivos
    @param  cNombArch, carácter, nombre del archivo
    @param  aImpICA, arreglo, contiene los campos del libro fiscal associados al ICA 
    @param  aImpBMI, arreglo, contiene los campos del libro fiscal associados al Bomberil 
    @param  aImpAVT, arreglo, contiene los campos del libro fiscal associados al Avisos Tableros  
    @param  aFilsCalc, arreglo, sucursales
    @param  nAglu,numerico, indica si aglutina
    
    @return nil
    /*/  
 
Function fAcumulado(cCodMun,dPedIni,dPedFin,cEmail,cDir,cNombArch,aImpICA,aImpBMI,aImpAVT,aFilsCalc,nAglu)
Local   lAutomato 	:= IsBlind()
Local aAreaAnt := GETAREA()

Local nBasICA:=0
Local nValBICA:=0
Local nValSerICA:=0
Local nValOtrICA:=0

Local nBasBMI:=0
Local nValBBMI:=0
Local nValSerBMI:=0
Local nValOtrBMI:=0

Local nBasAVT:=0
Local nValBAVT:=0
Local nValSerAVT:=0
Local nValOtrAVT:=0

Local aAuxDat:={}
Local aReg:={}

Local nReg:=0
Local cDatAux:=""

Local cFilA2:=""
Local cFilA1:=""
Local nF:=1
Local lAglu:=.F.
Local nCuentaFil:=0
Local nAuxFil:=0
Local aGlu:={}

DEFAULT cCodMun     :=""
DEFAULT dPedIni     :=dDataBase
DEFAULT dPedFin     :=dDataBase
DEFAULT cEmail      :=""
DEFAULT cDir        :=""
DEFAULT cNombArch   :=""
DEFAULT aImpICA     :={}
DEFAULT aImpBMI     :={}
DEFAULT aImpAVT     :={}
DEFAULT aFilsCalc   :={}
DEFAULT nAglu       :=0

IF nAglu==1
    lAglu:=.T.
ENDIF

For nF:=1 to len(aFilsCalc)
    IF aFilsCalc[nF,1] 
    nCuentaFil++
    ENDIF
Next
ProcRegua(nCuentaFil)
For nF:=1 to len(aFilsCalc)


    IF aFilsCalc[nF,1] // Se seleccione la sucursal
        nAuxFil++
        cFilA2:=xFilial("SA2",aFilsCalc[nF,2])
        cFilA1:=xFilial("SA1",aFilsCalc[nF,2])
        //RETENCIONES
        dbSelectArea("SA2")
        SA2->(dbSetOrder(3))
       

        if SA2->(MsSeek(cFilA2))
            
            While !(SA2->(Eof())) .AND. cFilA2==SA2->A2_FILIAL
                
                fNValAcu(SA2->A2_COD,SA2->A2_LOJA,1,"F1","D1","A2","'NF','NDP'",aImpICA,aImpBMI,aImpAVT,@nBasICA,@nValBICA,@nValSerICA,@nValOtrICA,@nBasBMI,@nValBBMI,@nValSerBMI,@nValOtrBMI,@nBasAVT,@nValBAVT,@nValSerAVT,@nValOtrAVT,dPedIni,dPedFin,cCodMun,aFilsCalc[nF,2])
                fNValAcu(SA2->A2_COD,SA2->A2_LOJA,-1,"F2","D2","A2","NCP",aImpICA,aImpBMI,aImpAVT,@nBasICA,@nValBICA,@nValSerICA,@nValOtrICA,@nBasBMI,@nValBBMI,@nValSerBMI,@nValOtrBMI,@nBasAVT,@nValBAVT,@nValSerAVT,@nValOtrAVT,dPedIni,dPedFin,cCodMun,aFilsCalc[nF,2])

                // Aqui se registra por proveedor en caso de encontrar algo
                If (nBasICA<>0 .OR. nBasBMI<>0 .OR. nBasAVT<>0)
                        nReg++
                        Aadd(aAuxDat,nReg)
                        fDatosProv(@aAuxDat)
               
                        cDatAux:= nValBICA+nValSerICA+nValOtrICA+nValBBMI+nValSerBMI+nValOtrBMI+nValBAVT+nValSerAVT+nValOtrAVT                    
                      
                        Aadd(aAuxDat,nBasICA)  
                        Aadd(aAuxDat,nValBICA)
                        Aadd(aAuxDat,nValBAVT)
                        Aadd(aAuxDat,nValBBMI)

                        Aadd(aAuxDat,nValSerICA)
                        Aadd(aAuxDat,nValSerAVT)
                        Aadd(aAuxDat,nValSerBMI)

                        Aadd(aAuxDat,nValOtrICA)
                        Aadd(aAuxDat,nValOtrAVT)
                        Aadd(aAuxDat,nValOtrBMI)
                        //Columnas que no se informan 
                        Aadd(aAuxDat,"")
                        Aadd(aAuxDat,"")
                        Aadd(aAuxDat,"")
                        Aadd(aAuxDat,"")
                        Aadd(aAuxDat,"")
                        Aadd(aAuxDat,"")
                        Aadd(aAuxDat,"")
                        Aadd(aAuxDat,"")
                        Aadd(aAuxDat,"")
                        Aadd(aAuxDat,cDatAux) //TOTAL
                        
                        Aadd(aReg,aAuxDat)
                    
                        nBasICA:=0
                        nValBICA:=0
                        nValSerICA:=0
                        nValOtrICA:=0

                        nBasBMI:=0
                        nValBBMI:=0
                        nValSerBMI:=0
                        nValOtrBMI:=0

                        nBasAVT:=0
                        nValBAVT:=0
                        nValSerAVT:=0
                        nValOtrAVT:=0
                        aAuxDat:={}
                    
                ENDIF
              
                SA2->(dbSkip())
            EndDo
        EndIf
        
        //AUTORETENCIONES
        dbSelectArea("SA1")
        SA1->(dbSetOrder(3))
        if SA1->(MsSeek(cFilA1))
    
            While !(SA1->(Eof()))  .AND. cFilA1==SA1->A1_FILIAL

                fNValAcu(SA1->A1_COD,SA1->A1_LOJA,1,"F2","D2","A1","'NF','NDC'",aImpICA,aImpBMI,aImpAVT,@nBasICA,@nValBICA,@nValSerICA,@nValOtrICA,@nBasBMI,@nValBBMI,@nValSerBMI,@nValOtrBMI,@nBasAVT,@nValBAVT,@nValSerAVT,@nValOtrAVT,dPedIni,dPedFin,cCodMun,aFilsCalc[nF,2])
                fNValAcu(SA1->A1_COD,SA1->A1_LOJA,-1,"F1","D1","A1","NCC",aImpICA,aImpBMI,aImpAVT,@nBasICA,@nValBICA,@nValSerICA,@nValOtrICA,@nBasBMI,@nValBBMI,@nValSerBMI,@nValOtrBMI,@nBasAVT,@nValBAVT,@nValSerAVT,@nValOtrAVT,dPedIni,dPedFin,cCodMun,aFilsCalc[nF,2])

               SA1->(dbSkip())

             EndDo   
           
                // Aqui se registra acumulado del autorentciones en caso de encontrar algo
            If nBasICA<>0 .OR. nBasBMI<>0 .OR. nBasAVT<>0
                    nReg++
                    Aadd(aAuxDat,nReg) 
                    fDatosClie(cEmail,@aAuxDat,aFilsCalc[nF,2])
                            
                    cDatAux:= nValBICA+nValSerICA+nValOtrICA+nValBBMI+nValSerBMI+nValOtrBMI+nValBAVT+nValSerAVT+nValOtrAVT  
                   
                    Aadd(aAuxDat,nBasICA) 
                    //Columnas que no se informan 
                    Aadd(aAuxDat,"")
                    Aadd(aAuxDat,"")
                    Aadd(aAuxDat,"")
                    Aadd(aAuxDat,"")
                    Aadd(aAuxDat,"")
                    Aadd(aAuxDat,"")
                    Aadd(aAuxDat,"")
                    Aadd(aAuxDat,"")
                    Aadd(aAuxDat,"")
                    
                    Aadd(aAuxDat,nValBICA)
                    Aadd(aAuxDat,nValBAVT)
                    Aadd(aAuxDat,nValBBMI)

                    Aadd(aAuxDat,nValSerICA)
                    Aadd(aAuxDat,nValSerAVT)
                    Aadd(aAuxDat,nValSerBMI)

                    Aadd(aAuxDat,nValOtrICA)
                    Aadd(aAuxDat,nValOtrAVT)
                    Aadd(aAuxDat,nValOtrBMI)
                    Aadd(aAuxDat,cDatAux) //total
                    IF !lAglu
                     Aadd(aReg,aAuxDat)
                    ELSE
                        Aadd(aGlu,aAuxDat)
                    ENDIF
                    
                    nBasICA:=0
                    nValBICA:=0
                    nValSerICA:=0
                    nValOtrICA:=0

                    nBasBMI:=0
                    nValBBMI:=0
                    nValSerBMI:=0
                    nValOtrBMI:=0

                    nBasAVT:=0
                    nValBAVT:=0
                    nValSerAVT:=0
                    nValOtrAVT:=0
                    aAuxDat:={}
                  
            ENDIF
             
         endIf
        IF !lAglu 
            fCreaCSV(cDir,aFilsCalc[nF,2]+" "+cNombArch,aReg) 
            fExel(cDir,aFilsCalc[nF,2]+" "+cNombArch,aReg,aFilsCalc[nF,2])
            aSize(aReg,0)
            aSize(aAuxDat,0)
          
        ENDIF

    EndIF

    Next

    RestArea(aAreaAnt)
        IF lAglu
            
            fCreaCSV(cDir,cNombArch,aReg,aGlu) 
            fExel(cDir,cNombArch,aReg,"",aGlu)
            aSize(aReg,0)
            aSize(aAuxDat,0)
           
        ENDIF

     IF !lAutomato 	
        Help(" ",1,STR0032,,STR0035,4,5) //STR0032 "Archivo de Bucaramanga" //STR0035 "Se ha finalizado la generación de los archivos"
     ENDIF
     aSize(aAreaAnt,0)
     aSize(aGlu,0)
Return nil


/*/{Protheus.doc} fValores
	Función para realizar los acumulados totales para "bienes","servicio" u "otros" para los impuestos
	@type  Function
	@author adrian.perez
	@since 20/06/2025
    @param  aImp    ,arreglo, contiene los campos del libro fiscal pertenecientes a los códigos del param cImpNomb
    @param  nBase   ,numérico,  contiene el valor base ejemplo D1_BASIMP(X)/D2_BASIMP(X)
    @param  nValB   ,numérico,  contiene el valor el producto tipo "bienes" del impuesto del cuál se sacara el acumulado total 
    @param  nValSer   ,numérico,  contiene el valor el producto tipo "servicios" del impuesto del cuál se sacara el acumulado total 
    @param  nValOtr  ,numérico,  contiene el valor el producto tipo "otros" del impuesto del cuál se sacara el acumulado total 
    @param  cAlias   ,carácter,  indica en que tabla(SD1 o SD2 ) buscar los valores para bienes,servicio,otros 
    @param  nSigno   ,numérico,  indica si el valor se suma o se resta
    @param  cAliasAux   ,carácter,  alias de tabla principal a recorrer
	@return nil
	/*/  

function fValores(aImp,nBase,nValB,nValSer,nValOtr,cAlias,nSigno,cAliasAux)
Local nA:=0


 DEFAULT aImp   :={}
 DEFAULT nBase  :=0  
 DEFAULT nValB  :=0
 DEFAULT nValSer:=0  
 DEFAULT nValOtr:=0  
 DEFAULT cAlias :=""
 DEFAULT nSigno :=1
 DEFAULT cAliasAux:=""


   For nA:=1 to len(aImp)
        IF (cAliasAux)->&(cAlias+"_BASIMP"+aImp[nA])<>0  
          
            nBase+=((cAliasAux)->&(cAlias+"_BASIMP"+aImp[nA]) * nSigno )                                         
            fProdTip((cAliasAux)->&("TIPO"),@nValB,@nValSer,@nValOtr,(cAliasAux)->&(cAlias+"_VALIMP"+aImp[nA]),nSigno)
                
        endIf
   Next

return nil


/*/{Protheus.doc} fProdTip
	Función para identificar que tipo de prodcuto es  "Bien", "Servicio" u "otros"
	@type  Function
	@author adrian.perez
	@since 23/06/2025
    @param  cTip       ,carácter,  tipo de producto bien servicio u Otros
    @param  nValBien   ,numérico,  contiene el valor el producto tipo "bienes" del impuesto del cuál se sacara el acumulado total 
    @param  nValSer    ,numérico,  contiene el valor el producto tipo "servicios" del impuesto del cuál se sacara el acumulado total 
    @param  nValOtr    ,numérico,  contiene el valor el producto tipo "otros" del impuesto del cuál se sacara el acumulado total 
    @param  nVal       ,numérico,  valor total el cuál se sumara de acuerdo al tipo de producto
    @param  nSigno     ,numérico,  indica si el valor se suma o se resta
	@return nil
	/*/  

Function fProdTip(cTip,nValBien,nValSer,nValOtr,nVal,nSigno)

Local lCamp:= SB1->(ColumnPos("B1_TPCLAS")) > 0

 DEFAULT cTip       :=""
 DEFAULT nValBien   :=0  
 DEFAULT nValSer    :=0
 DEFAULT nValOtr    :=0  
 DEFAULT nVal       :=0  
 DEFAULT nSigno     :=1


    IF lCamp

            If cTip=="1"//bienes
                nValBien+=(nVal*nSigno)
            ElseIF cTip=="2"//servicios
                nValSer+=(nVal*nSigno)
            Else
                nValOtr+=(nVal*nSigno)
            EndIf


    ELSE // Se toma como otros en caso de no existir campo B1_TPCLAS
        nValOtr+=(nVal*nSigno)
    EndIF


return Nil

/*/{Protheus.doc} fCreaCSV
	Función para crear archivo CSV
	@type  Function
	@author adrian.perez
	@since 23/06/2025
    @param  cDir, carácter, ruta donde se guardaran los archivos
    @param  cNombArch, carácter, nombre del archivo
    @param  aReg,arreglo, contiene la información a agregar al CSV 
     @param  aGlu,arreglo, contiene la información a agregar al CSV cuando se aglutina
	@return nil
	/*/  


function fCreaCSV(cDir,cNombArch,aReg,aGlu)

Local nH:=1
Local nC:=1
Local cArchivo:= cDir+cNombArch+".csv"
Local oFile := FWFileWriter():New(cArchivo, .T.)
Local cDato:=""
Local nUlti:=0
Local aUlt:={}

DEFAULT cDir:=""
DEFAULT cNombArch:=""
DEFAULT aReg:={}
DEFAULT aGlu:={}

For nH:=1 to len(aReg)
 aReg[nH][1]:=nH
    for nC:=1 to len(aReg[nH])
        if ValType(aReg[nH][nC])=="N"
            cDato+=STR(aReg[nH][nC])+SepaTX
        else
            cDato+=(aReg[nH][nC])+SepaTX
        EndIf
    Next
    cDato+=CHR(13) + CHR(10)
    nUlti:=nH
Next

For nH:=1 to len(aGlu)
 nUlti++
 aGlu[nH][1]:=nUlti
    for nC:=1 to len(aGlu[nH])
        if ValType(aGlu[nH][nC])=="N"
            cDato+=STR(aGlu[nH][nC])+SepaTX
        else
            cDato+=(aGlu[nH][nC])+SepaTX
        EndIf
    Next
    cDato+=CHR(13) + CHR(10)
   
Next


If !oFile:Exists()
		oFile:Create()
		oFile:Close()
EndIf
If oFile:Open(FO_WRITE)
		oFile:Clear()
		oFile:Write(cDato)
		oFile:Close()	
EndIf
FreeObj(oFile)

aSize(aUlt,0)

return nil


/*/{Protheus.doc} fDatosProv
	Función para agregar datos del proveedor
	@type  Function
	@author adrian.perez
	@since 23/06/2025
    @param  aAuxDat, arreglo, se guarda la información del proveedor para posteriormente usar en la generación del excel
	@return nil
	/*/  

Function fDatosProv(aAuxDat)

Local cAux:=STR0038 // STR0038 "PERSONA JURIDICA "
Local  cTipoPer:=SA2->A2_PESSOA
Local  aAux:={}
Local  nPos:=0
Local  cDV:=""

DEFAULT aAuxDat:={}

    IF cTipoPer=="F"
        cAux:=STR0039 //Columna 2 TIPO CONTRIBUYENTE  //STR0039 "PERSONA NATURAL "
    EndiF
   
    Aadd(aAuxDat,cAux)

    cAux:=""
    IF cTipoPer=="F"     //Columna 3  NUMERO IDENTIFICACION CC
      cAux:=ALLTRIM(SA2->A2_PFISICA)
      cDV:=cAux
      cAux:=SUBSTR(cAux,1, Len(cAux)-1)
      Aadd(aAuxDat,cAux)
    ELSE
       cAux:=ALLTRIM(SA2->A2_CGC)
       cDV:=cAux
       cAux:=SUBSTR(cAux,1, Len(cAux)-1) 
       Aadd(aAuxDat,cAux)
    ENDIF
    

    cAux:=SUBSTR(cDV, Len(cDV), 1) // Columna 4 DV
   
    Aadd(aAuxDat,cAux)
    aAux:=FWGetSX5( "TB")

    nPos:= ASCAN(aAux, { |x| ALLTRIM(x[3]) == ALLTRIM(SA2->A2_TIPDOC)  })
    If nPos>0
        cAux:=SUBSTR(Trim(aAux[nPos][4]),1,4) // maximo de letras(4)ejemplo NITE (Persona Jurídica extranjera sin identificación tributaria.)
        Aadd(aAuxDat,cAux)
    else
        Aadd(aAuxDat,"")
    EndIf
        
    If cTipoPer=="F"
        cAux:=STRTRAN(SA2->A2_NOMEPRI+" "+SA2->A2_NOMEPES,",", "")// COLUMNA 6 NOMBRES
         Aadd(aAuxDat,cAux)

        cAux:=STRTRAN(SA2->A2_NOMEMAT+ " "+SA2->A2_NOMEPAT,",", "") // COLUMNA 7 APELIDOS
         Aadd(aAuxDat,cAux)
    ELSE
       
        Aadd(aAuxDat,"")
        Aadd(aAuxDat,"")
        
    ENDIF

    cAux:=" "
    If cTipoPer=="J"
     cAux:=  STRTRAN( SA2->A2_NOME,",", "")  // COLUMNA 8 RAZON SOCIAL
    EndIf

    Aadd(aAuxDat,cAux)
   

    cAux:=STRTRAN(SA2->A2_END,",", "")// COLUMNA 9 DIRECCIÓN
    Aadd(aAuxDat,cAux)

    cAux:=SA2->A2_COD_MUN //COLUMNA 10 CIUDAD
    Aadd(aAuxDat,cAux)

    cAux:=STRTRAN(SA2->A2_EMAIL,",", "")//COLUMNA 11 EMAIL
    Aadd(aAuxDat,cAux)
    aSize(aAux,0)
 
     
return nil

/*/{Protheus.doc} fDatosClie
	Función para agregar datos del cliente
	@type  Function
	@author adrian.perez
	@since 23/06/2025
    @parama cEmail  ,carácter, email usado para informar 
    @param  aAuxDat, arreglo, se guarda la información del cliente para posteriormente usar en la generación del excel
    @parama cFil  ,carácter, filial 
	@return nil
	/*/ 

Function fDatosClie(cEmail,aAuxDat,cFil)

Local cAux:=STR0038 //STR0038 "PERSONA JURIDICA "
Local  cDV:=""
Local  aDatEmp:=FWSM0Util():GetSM0Data( FWGrpCompany(),cFil)

DEFAULT cEmail:=""
DEFAULT aAuxDat:={}
DEFAULT cFil:=""

    Aadd(aAuxDat,cAux)

    cAux:=ALLTRIM(aDatEmp[10][2]) // SM0->M0_CGC   //Columna 3  NUMERO IDENTIFICACION CC 
    cDV:=cAux
    cAux:=SUBSTR(cAux,1, Len(cAux)-1)
    Aadd(aAuxDat,cAux)
     

    cAux:=SUBSTR(cDV,len(cDV), 1) // Columna 4 DV
    Aadd(aAuxDat,cAux)

    cAux:=STR0040      //STR0040 "NIT"
    Aadd(aAuxDat,cAux)
    
   //// COLUMNA 6 NOMBRES y 7 // COLUMNA 7 APELIDOS // No se llena en este caso
    Aadd(aAuxDat,"")
    Aadd(aAuxDat,"")

    cAux:=STRTRAN(aDatEmp[4][2],",", "") //SM0->M0_NOME // COLUMNA 8 RAZON SOCIAL
    Aadd(aAuxDat,cAux)
      
    cAux:=STRTRAN(aDatEmp[14][2],",", "") //SM0->M0_ENDENT  // COLUMNA 9 DIRECCIÓN 
    Aadd(aAuxDat,cAux)
    
    cAux:=aDatEmp[20][2]    //SM0->M0_CODMUN  //COLUMNA 10 CIUDAD
    Aadd(aAuxDat,cAux)

    cAux:=STRTRAN(cEmail,",", "")  //COLUMNA 11 EMAIL se usa el informado en el MV_PAR06 del grupo de preguntas FISA843 solo aplica para este caso
    Aadd(aAuxDat,cAux)
    aSize(aDatEmp,0)
return 

/*/{Protheus.doc} fExel
	Función para agregar datos del cliente
	@type  Function
	@author adrian.perez
	@since 27/06/2025
    @param  cDir, carácter, ruta donde se guardaran los archivos
    @param  cNombArch, carácter, nombre del archivo
    @param  aFilas, arreglo, contiene información para agregar en el excel
    @param  cFil, carácter, filial usada
    @param  aGlu, arreglo, contiene los registro de los datos de ventas cuando se aglutina la información
	@return nil 
	/*/ 
function fExel(cDir,cNombArch,aFilas,cFil,aGlu)

Local oExcel := FwMsExcelXlsx():New()
Local lWriteDb := .T.
Local nH:=1
Local nInc:=1
Local nJ:=1
DEFAULT cDir:=""
DEFAULT cNombArch:=""
DEFAULT aFilas:={}
DEFAULT cFil:=""
DEFAULT aGlu:={}

//Habilita dados de processamento no BD e com Bulk ocorrendo com até 200 mil registros.
lWriteDb := oExcel:SetWriteinDb(.T., 200000)
  
lRet := oExcel:IsWorkSheet(STR0036) //STR0036 "Hoja1"
oExcel:AddworkSheet(STR0036) //STR0036 "Hoja1" 
 
lRet := oExcel:IsWorkSheet(STR0036)//STR0036 "Hoja1" 
oExcel:AddTable (STR0036,STR0037,.F.) //STR0036 "Hoja1" //STR0037 "Tabla1"
oExcel:AddColumn(STR0036,STR0037,STR0001  ,1,1,.F.)  // "REGISTRO"
oExcel:AddColumn(STR0036,STR0037,STR0002  ,1,1,.F.)  // "TIPO CONTRIBUYENTE"
oExcel:AddColumn(STR0036,STR0037,STR0003  ,1,1,.F.)  // "NUMERO IDENTIFICACIÓN"
oExcel:AddColumn(STR0036,STR0037,STR0004  ,1,1,.F.)  // "DIGITO DE VERIFICACIÓN"
oExcel:AddColumn(STR0036,STR0037,STR0005  ,1,1,.F.)  // "TIPO DE IDENTIFICACIÓN"  
oExcel:AddColumn(STR0036,STR0037,STR0006  ,1,1,.F.)  // "NOMBRES" 
oExcel:AddColumn(STR0036,STR0037,STR0007  ,1,1,.F.)  // "APELLIDOS"  
oExcel:AddColumn(STR0036,STR0037,STR0008  ,1,1,.F.)  // "RAZON SOCIAL"
oExcel:AddColumn(STR0036,STR0037,STR0009  ,1,1,.F.)  // "DIRECCIÓN"
oExcel:AddColumn(STR0036,STR0037,STR0010  ,1,1,.F.)  // "CIUDAD" 
oExcel:AddColumn(STR0036,STR0037,STR0011  ,1,1,.F.)  // "CORREO ELECTRÓNICO" 

oExcel:AddColumn(STR0036,STR0037,STR0012  ,3,2,.F.,  "999,999,999,999,999.99") //"BASE DE RETENCIÓN O AUTORETENCIÓN "
oExcel:AddColumn(STR0036,STR0037,STR0013  ,3,2,.F.,  "999,999,999,999,999.99") //"COMPRA DE BIENES ICA"   
oExcel:AddColumn(STR0036,STR0037,STR0014  ,3,2,.F.,  "999,999,999,999,999.99") //"COMPRA DE BIENES AVISOS Y TABLEROS"
oExcel:AddColumn(STR0036,STR0037,STR0015  ,3,2,.F.,  "999,999,999,999,999.99") //"COMPRA DE BIENES SOBRETASA BOMBERIL" 
oExcel:AddColumn(STR0036,STR0037,STR0016  ,3,2,.F.,  "999,999,999,999,999.99") //"SERVICIOS ICA" 
oExcel:AddColumn(STR0036,STR0037,STR0017  ,3,2,.F.,  "999,999,999,999,999.99") //"SERVICIOS AVISOS Y TABLEROS"  
oExcel:AddColumn(STR0036,STR0037,STR0018  ,3,2,.F.,  "999,999,999,999,999.99") //"SERVICIOS SOBRETASA BOMBERIL" 
oExcel:AddColumn(STR0036,STR0037,STR0019  ,3,2,.F.,  "999,999,999,999,999.99") //"OTROS ICA"   
oExcel:AddColumn(STR0036,STR0037,STR0020  ,3,2,.F.,  "999,999,999,999,999.99")//"OTROS AVISOS Y TABLERO" 
oExcel:AddColumn(STR0036,STR0037,STR0021  ,3,2,.F.,  "999,999,999,999,999.99")//"OTROS SOBRETASA BOMBERIL" 

oExcel:AddColumn(STR0036,STR0037,STR0022  ,3,2,.F.,  "999,999,999,999,999.99")  //"VENTAS DE BIENES ICA" 
oExcel:AddColumn(STR0036,STR0037,STR0023  ,3,2,.F.,  "999,999,999,999,999.99") //"VENTA DE BIENES AVISOS Y TABLEROS"  
oExcel:AddColumn(STR0036,STR0037,STR0024  ,3,2,.F.,  "999,999,999,999,999.99")  //"VENTA DE BIENES SOBRETASA BOMBERIL"
oExcel:AddColumn(STR0036,STR0037,STR0025  ,3,2,.F.,  "999,999,999,999,999.99") //"SERVICIOS ICA"
oExcel:AddColumn(STR0036,STR0037,STR0026  ,3,2,.F.,  "999,999,999,999,999.99") //"SERVICIOS AVISOS Y TABLEROS" 
oExcel:AddColumn(STR0036,STR0037,STR0027  ,3,2,.F.,  "999,999,999,999,999.99") //"SERVICIOS SOBRETASA BOMBERIL"   

oExcel:AddColumn(STR0036,STR0037,STR0028  ,3,2,.F.,  "999,999,999,999,999.99") //"OTROS ICA" 
oExcel:AddColumn(STR0036,STR0037,STR0029  ,3,2,.F.,  "999,999,999,999,999.99") //"OTROS AVISOS Y TABLEROS" 
oExcel:AddColumn(STR0036,STR0037,STR0030  ,3,2,.F.,  "999,999,999,999,999.99") //"OTROS SOBRETASA BOMBERIL"
oExcel:AddColumn(STR0036,STR0037,STR0031  ,3,2,.F.,  "999,999,999,999,999.99") //"TOTAL" 

// FILAS
ProcRegua(len(aFilas))
For nH:=1 to len(aFilas)
    aFilas[nH][1]:=nInc
    oExcel:AddRow(STR0036,STR0037,aFilas[nH]) //STR0036 "Hoja1" STR0037 "Tabla1"
    nInc++

    IncProc(cFil+" "+STR0047 + str(nInc) ) //STR0047 Creando registro de excel ..
Next

For nJ:=1 to len(aGlu)
 nInc++
 aFilas[nJ][1]:=nInc
 oExcel:AddRow(STR0036,STR0037,aGlu[nJ]) //STR0036 "Hoja1" STR0037 "Tabla1"


Next
oExcel:SetFont("arial")
oExcel:SetFontSize(20)

oExcel:Activate()
oExcel:GetXMLFile(cDir+cNombArch+".xlsx")
oExcel:DeActivate()
 
FreeObj(oExcel)
aSize(aFilas,0)
aSize(aGlu,0)

return nil

/*/{Protheus.doc} fBorrarArchivo
	Función para borrar archivos, se utiliza para borrar archivos previos generados
    con el mismo nombre
	@type  Function
	@author adrian.perez
	@since 27/06/2025
    @param  cDir, carácter, ruta donde se guardaran los archivos
    @param  cNombArch, carácter, nombre del archivo
	@return logico, .T. si no existió impedimentos, .F. si existió impedimentos
	/*/ 

function fBorrarArchivo(cDir,cNombArch)

DEFAULT cDir:="" 
DEFAULT cNombArch:=""


IF file(cDir+cNombArch)
    IF FERASE( cDir+cNombArch) == -1

        Help(" ",1,STR0032,,STR0033+cDir+cNombArch+STR0034 + STR(FERROR()),4,5) //STR0032 "Archivo de Bucaramanga" //STR0033 "Verifique que no exista el archivo '" //STR0034 "', o en uso por otro porgrama error num:"
        RETURN .F.

    ENDIF

ENDIF

return .T.

/*/{Protheus.doc} fEjecutar
	Función con la que se recolecta los parametros y da comienzo a la generación de los archivos.
	@type  Function
	@author adrian.perez
	@since 28/06/2025
	@return nil
	/*/
function fEjecutar()

Local   cPerg:="FISA843" 
Local   dPedIni :=dDataBase
Local   dPedFin :=dDataBase
Local   cImpICA :=""
Local   cImpBMI :=""
Local   cImpAVT :=""
Local   cEmail  :=""
Local   cDir    :=""
Local   cNombArch:=""
Local   cCodMun:=""
Local   aImpICA:={}
Local   aImpBMI:={}
Local   aImpAVT:={}
Local   nSelSuc:=1
Local   nAglu:=1
Local  aFilsCalc:={}
Pergunte(cPerg, .T. )

    dPedIni:=            MV_PAR01                              //01	¿Periodo Inicial?     
    dPedFin:=            MV_PAR02                              //02	¿Periodo Final?  
    cImpICA:=            ALLTRIM(MV_PAR03)                     //03	¿Impuesto ICA?     
    cImpBMI:=            ALLTRIM(MV_PAR04)                     //04	¿Impuesto Bomberil?   
    cImpAVT:=            ALLTRIM(MV_PAR05)                     //05	¿Impuesto Aviso Tableros?  
    cEmail:=             ALLTRIM(MV_PAR06)                     //06	¿Email Responsable?    
    cDir:=               ALLTRIM(MV_PAR07)                     //07	¿Directorio destino?  
    cNombArch:=          STRTRAN(ALLTRIM( MV_PAR08 ),".","_")  //08	¿Nombre del archivo? 
    cCodMun:=            ALLTRIM( MV_PAR09 )                   //09	¿Código Municipio Bucaramanga?      
    nSelSuc:=             MV_PAR10                             //10	¿Selecciona sucursales?      
    nAglu:=               MV_PAR11                             //11 ¿Agrupa obligación?           

    IF !Empty(cImpICA)
       aImpICA:= fBuscaCamp(cImpICA)
    ENDIF

    IF !Empty(cImpBMI)
       aImpBMI:= fBuscaCamp(cImpBMI)
    ENDIF

    IF !Empty(cImpAVT)
       aImpAVT:= fBuscaCamp(cImpAVT)
    ENDIF
    
    IF nSelSuc==1
        aFilsCalc := MatFilCalc(.T.) 
    Else
        aadd(aFilsCalc,{.T., FWCodFil()})
    ENDIF


    IF fBorrarArchivo(cDir,cNombArch+".csv")
        IF    fBorrarArchivo(cDir,cNombArch+".xlsx")
            Processa({|| fAcumulado(cCodMun,dPedIni,dPedFin,cEmail,cDir,cNombArch,aImpICA,aImpBMI,aImpAVT,aFilsCalc,nAglu)})
        ENDIF
    ENDIF
aSize(aImpICA,0)
aSize(aImpBMI,0)
aSize(aImpAVT,0)
return nil


/*/{Protheus.doc} fNValAcu
	Función  para buscar los datos de retenciones y auto retenciones en las tablas SF1/SF2 SA1/SA2 SD1/SD2 
	@type  Function
	@author adrian.perez
	@since 20/06/2025
    @param  cClie      ,carácter,  cliente
    @param  cLoja      ,carácter,  tienda
    @param  nSigno      ,numérico,  indica si el valor se suma o se resta
    @param  cAliasF    , carácter,  alias de la tabla para documentos  "F1" o "F2"
    @param  cAliasD    , carácter,  alias de la tabla para items   "D1" o "D2"
    @param  cAliasA    , carácter,  alias de la tabla para cliente/proveedor   "A1" o "A2"
    @param  cEspecie    , carácter, especie del documento a buscar
    @param  aImpICA     , arreglo,  contiene los campos del libro fiscal associados al ICA 
    @param  aImpBMI     , arreglo,  contiene los campos del libro fiscal associados al Bomberil 
    @param  aImpAVT     , arreglo,  contiene los campos del libro fiscal associados al Avisos Tableros 

    @param  nBasICA     ,numérico,  contiene el valor base ejemplo D1_BASIMP(X)/D2_BASIMP(X) asociado al ICA
    @param  nValBICA   ,numérico,  contiene el valor el producto tipo "bienes" del impuesto del cuál se sacara el acumulado total   asociado al ICA
    @param  nValSerICA   ,numérico,  contiene el valor el producto tipo "servicios" del impuesto del cuál se sacara el acumulado total asociado al ICA
    @param  nValOtrICA  ,numérico,  contiene el valor el producto tipo "otros" del impuesto del cuál se sacara el acumulado total asociado al ICA

    @param  nBasBMI     ,numérico,  contiene el valor base ejemplo D1_BASIMP(X)/D2_BASIMP(X) asociado al BMI
    @param  nValBBMI   ,numérico,  contiene el valor el producto tipo "bienes" del impuesto del cuál se sacara el acumulado total   asociado al Bomberil
    @param  nValSerBMI   ,numérico,  contiene el valor el producto tipo "servicios" del impuesto del cuál se sacara el acumulado total asociado al Bomberil
    @param  nValOtrBMI  ,numérico,  contiene el valor el producto tipo "otros" del impuesto del cuál se sacara el acumulado total asociado al Bomberil

    @param  nBasAVT     ,numérico,  contiene el valor base ejemplo D1_BASIMP(X)/D2_BASIMP(X) asociado al AVT
    @param  nValBAVT   ,numérico,  contiene el valor el producto tipo "bienes" del impuesto del cuál se sacara el acumulado total   asociado al AVT
    @param  nValSerAVT   ,numérico,  contiene el valor el producto tipo "servicios" del impuesto del cuál se sacara el acumulado total asociado al AVT
    @param  nValOtrAVT  ,numérico,  contiene el valor el producto tipo "otros" del impuesto del cuál se sacara el acumulado total asociado al AVT

    @param  dPedIni     , fecha, fecha de inicio para filtro
    @param  dPedFin    , fecha, fecha fin  para filtro
    @param  cCodMun, carácter, código de municipio Bucaramanga
    @param  cFil, carácter, filial usada
	@return nil

	/*/  


function fNValAcu(cClie,cLoja,nSigno,cAliasF,cAliasD,cAliasA,cEspecie,aImpICA,aImpBMI,aImpAVT, nBasICA,nValBICA,nValSerICA,nValOtrICA,nBasBMI,nValBBMI,nValSerBMI,nValOtrBMI,nBasAVT,nValBAVT,nValSerAVT,nValOtrAVT,dPedIni,dPedFin,cCodMun,cFil)
//variables auxiliares para guardar valores en moneda diferente de 1
Local nBaICAux:=0
Local nVlBICAux:=0
Local nVlSICAux:=0
Local nVlOICAux:=0

Local nBaBMIux:=0
Local nVlBBMIux:=0
Local nVlSBMIux:=0
Local nVlOBMIux:=0

Local nBaAVTux:=0
Local nVlBAVTux:=0
Local nVlSAVTux:=0
Local nVlOAVTux:=0
Local nDec:=MsDecimais(1)

Local cAux:=""
Local oExec 	:=Nil
Local cTabSF:="SF1"
Local cTabSFcon:="SF1.F1_"

Local cTabSD:="SD1"
Local cTabSDcon:="SD1.D1_"

Local cAuxCliProv:="FORNECE"
Local lCamp:= SB1->(ColumnPos("B1_TPCLAS")) > 0
Local nCampo:=1
Local cQuery:=""
Local nTxAux:=0
Local nMoAux:=0
Local dDit:= dDataBase
Local cBarra:=STR0045
Local nInc:=0

Local cFilSF:=""
Local cFilSD:=""
Local cFilSB1:=""

IF cAliasF=="F2"
	cTabSF:="SF2"
    cTabSFcon:="SF2.F2_"
    cAuxCliProv:="CLIENTE"
    cBarra:=STR0046
ENDIF   

IF cAliasD=="D2"
    cTabSD:="SD2"
    cTabSDcon:="SD2.D2_"
    cAuxCliProv:="CLIENTE"
ENDIF

 DEFAULT  cClie        :=""
 DEFAULT  cLoja        :=""
 DEFAULT  nSigno        :=1  
 DEFAULT  cAliasF       :=""  
 DEFAULT  cAliasD       :=""  
 DEFAULT  cAliasA       :=""  
 DEFAULT  cEspecie      :=""  
 DEFAULT  aImpICA       :={}  
 DEFAULT  aImpBMI       :={}  
 DEFAULT  aImpAVT       :={}  
 DEFAULT  nBasICA       :=0  
 DEFAULT  nValBICA      :=0  
 DEFAULT  nValSerICA    :=0  
 DEFAULT  nValOtrICA    :=0  
 DEFAULT  nBasBMI       :=0   
 DEFAULT  nValBBMI      :=0   
 DEFAULT  nValSerBMI    :=0   
 DEFAULT  nValOtrBMI    :=0   
 DEFAULT  nBasAVT       :=0   
 DEFAULT  nValBAVT      :=0   
 DEFAULT  nValSerAVT    :=0   
 DEFAULT  nValOtrAVT    :=0   
 DEFAULT  dPedIni       :=dDataBase  
 DEFAULT  dPedFin       :=dDataBase  
 DEFAULT  cCodMun       :=""
 DEFAULT  cFil          :=""

        cAux += fCadenaSD(aImpICA,cTabSDcon,.T.) // BASIMP ICA
        cAux += fCadenaSD(aImpICA,cTabSDcon)  // VALIMP ICA

        cAux += fCadenaSD(aImpBMI,cTabSDcon,.T.) // BASIMP BMI
        cAux += fCadenaSD(aImpBMI,cTabSDcon)    // VALIMP BMI

        cAux += fCadenaSD(aImpAVT,cTabSDcon,.T.) // BASIMP AVT
        cAux += fCadenaSD(aImpAVT,cTabSDcon)   // VALIMP AVT

        cFilSB1 := xFilial("SB1", cFil)
        
        If cTabSF == "SF1"
            cFilSF := xFilial("SF1", cFil)
            cFilSD := xFilial("SD1", cFil)
            cQuery+= " SELECT F1_ESPECIE, F1_CODMUN, F1_FORNECE, F1_DOC, F1_LOJA, F1_SERIE, F1_MOEDA, F1_TXMOEDA, F1_DTDIGIT, "
		    cQuery+= "D1_COD, D1_CODMUN,"
        Else
            cFilSF := xFilial("SF2", cFil)
            cFilSD := xFilial("SD2", cFil)
            cQuery+= " SELECT F2_ESPECIE, F2_CODMUN, F2_CLIENTE, F2_DOC, F2_LOJA, F2_SERIE, F2_MOEDA, F2_TXMOEDA, F2_DTDIGIT, "
		    cQuery+= "D2_COD, D2_CODMUN,"
        EndIf

		cQuery +=" ? "

        IF lCamp
             cQuery+= " B1_TPCLAS TIPO "
        ELSE
           cQuery+= " '3' TIPO "
        ENDIF
        
        If cTabSF == "SF1"
            
            cQuery+=" FROM "+ RetSqlName("SF1") +" SF1 INNER JOIN "+RetSqlName("SD1") +" SD1 ON "
            cQuery+= "D1_DOC=F1_DOC AND D1_SERIE=F1_SERIE AND D1_FORNECE=F1_FORNECE AND D1_LOJA=F1_LOJA AND D1_ESPECIE=F1_ESPECIE " 
		    cQuery+= " AND D1_FILIAL=F1_FILIAL"
            cQuery+= " LEFT JOIN "+ RetSqlName("SB1")+" SB1 ON B1_COD = D1_COD "
            cQuery+= " WHERE "
            cQuery+= " F1_FILIAL= ? " //SF1/SF2
            cQuery+= " AND D1_FILIAL = ? " //SD1/SD2
            cQuery+= " AND SB1.B1_FILIAL = ? "
            cQuery+="  AND F1_FORNECE = ? "
            cQuery+= " AND F1_LOJA = ? "
            cQuery+= " AND F1_DTDIGIT >= ?"
            cQuery+= " AND F1_DTDIGIT <= ?"
            cQuery+= " AND F1_ESPECIE IN (?) " //'NF','NDP'
            cQuery+= " AND SF1.D_E_L_E_T_= ?"
            cQuery+= " AND SD1.D_E_L_E_T_=?"
            cQuery+= " AND SB1.D_E_L_E_T_=?"
            cQuery+= " ORDER BY F1_DOC DESC "
        Else
           
            cQuery+=" FROM "+ RetSqlName("SF2") +" SF2 INNER JOIN "+RetSqlName("SD2") +" SD2 ON "
            cQuery+= "D2_DOC=F2_DOC AND D2_SERIE=F2_SERIE AND D2_CLIENTE=F2_CLIENTE AND D2_LOJA=F2_LOJA AND D2_ESPECIE=F2_ESPECIE " 
		    cQuery+= " AND D2_FILIAL=F2_FILIAL"
            cQuery+= " LEFT JOIN "+ RetSqlName("SB1")+" SB1 ON B1_COD=D2_COD "
            
            cQuery+= " WHERE "
            cQuery+= " F2_FILIAL= ? " //SF1/SF2
            cQuery+= " AND D2_FILIAL= ? " //SD1/SD2
            cQuery+= " AND SB1.B1_FILIAL= ? "
            cQuery+="  AND F2_CLIENTE = ? "
            cQuery+= " AND F2_LOJA= ? "
            cQuery+= " AND F2_DTDIGIT>= ?"
            cQuery+= " AND F2_DTDIGIT<= ?"
            cQuery+= " AND F2_ESPECIE IN (?) " //'NF','NDP'
            cQuery+= " AND SF2.D_E_L_E_T_= ?"
            cQuery+= " AND SD2.D_E_L_E_T_=?"
            cQuery+= " AND SB1.D_E_L_E_T_=?"
            cQuery+= " ORDER BY F2_DOC DESC "
        EndIf

		cQuery := ChangeQuery(cQuery)
			
		oExec := FwExecStatement():New(cQuery)
		
        oExec:SetUnsafe(nCampo++, cAux) 
        oExec:SetString(nCampo++, cFilSF) 
        oExec:SetString(nCampo++, cFilSD) 
        oExec:SetString(nCampo++, cFilSB1)
		oExec:SetString(nCampo++, cClie) 
        oExec:SetString(nCampo++, cLoja) 
        oExec:setDate(nCampo++, dPedIni) 
        oExec:setDate(nCampo++, dPedFin) 
		oExec:SetIn(nCampo++, {cEspecie}) //Especie(NF,NCC,etc) 
		
		oExec:SetString(nCampo++, " ") //F1/F2 D_E_L_E_T_
        oExec:SetString(nCampo++, " ") //D1/D2  D_E_L_E_T_
        oExec:SetString(nCampo++, " ") //B1 D_E_L_E_T_


	    cAlias := oExec:OpenAlias()
        
        While !( (cAlias)->(Eof()) ) 
            nInc++
            IncProc(cFil+" "+cBarra + str(nInc) ) //STR0045 "Analizando Proveedores.."
        IF ((cAlias)->&(cAliasD+"_CODMUN")==cCodMun)  .OR.( Empty((cAlias)->&(cAliasD+"_CODMUN")) .AND.((cAlias)->&(cAliasF+"_CODMUN")==cCodMun) ) 
            If (cAlias)->&(cAliasF+"_MOEDA")==1
                fValores(aImpICA,@nBasICA,@nValBICA,@nValSerICA,@nValOtrICA,cAliasD,nSigno,cAlias)
                fValores(aImpBMI,@nBasBMI,@nValBBMI,@nValSerBMI,@nValOtrBMI,cAliasD,nSigno,cAlias)
                fValores(aImpAVT,@nBasAVT,@nValBAVT,@nValSerAVT,@nValOtrAVT,cAliasD,nSigno,cAlias)
            Else
                nTxAux:=(cAlias)->&(cAliasF+"_TXMOEDA")  
                nMoAux:= (cAlias)->&(cAliasF+"_MOEDA")
                dDit:=  (cAlias)->&(cAliasF+"_DTDIGIT")             
                fValores(aImpICA,@nBaICAux,@nVlBICAux,@nVlSICAux,@nVlOICAux,cAliasD,,cAlias)
                fValores(aImpBMI,@nBaBMIux,@nVlBBMIux,@nVlSBMIux,@nVlOBMIux,cAliasD,,cAlias)
                fValores(aImpAVT,@nBaAVTux,@nVlBAVTux,@nVlSAVTux,@nVlOAVTux,cAliasD,,cAlias)

                IF nBaICAux<>0
 
                    nBasICA+=   ( Round(xMoeda(nBaICAux ,nMoAux,1,dDit,nDec,nTxAux),nDec))*(nSigno)  
                    nValBICA+=   (Round(xMoeda(nVlBICAux ,nMoAux,1,dDit,nDec,nTxAux),nDec))*(nSigno)    
                    nValSerICA+= (Round(xMoeda(nVlSICAux ,nMoAux,1,dDit,nDec,nTxAux),nDec))*(nSigno)   
                    nValOtrICA+= (Round(xMoeda(nVlOICAux ,nMoAux,1,dDit,nDec,nTxAux),nDec))*(nSigno)    
                                    
                    nBaICAux:=0
                    nVlBICAux:=0
                    nVlSICAux:=0
                    nVlOICAux:=0
                ENDIF
                IF nBaBMIux<>0
                    nBasBMI+=    (Round(xMoeda(nBaBMIux ,nMoAux,1,dDit,nDec,nTxAux),nDec))*(nSigno)  
                    nValBBMI+=   (Round(xMoeda(nVlBBMIux ,nMoAux,1,dDit,nDec,nTxAux),nDec))*(nSigno)      
                    nValSerBMI+= (Round(xMoeda(nVlSBMIux ,nMoAux,1,dDit,nDec,nTxAux),nDec))*(nSigno)   
                    nValOtrBMI+= (Round(xMoeda(nVlOBMIux ,nMoAux,1,dDit,nDec,nTxAux),nDec))*(nSigno)  

                    nBaBMIux:=0
                    nVlBBMIux:=0
                    nVlSBMIux:=0
                    nVlOBMIux:=0

                ENDIF
                IF nBaAVTux<>0
                    nBasAVT+=    (Round(xMoeda(nBaAVTux ,nMoAux,1,dDit,nDec,nTxAux),nDec))*(nSigno)  
                    nValBAVT+=   (Round(xMoeda(nVlBAVTux ,nMoAux,1,dDit,nDec,nTxAux),nDec))*(nSigno)      
                    nValSerAVT+= (Round(xMoeda(nVlSAVTux ,nMoAux,1,dDit,nDec,nTxAux),nDec))*(nSigno)   
                    nValOtrAVT+= (Round(xMoeda(nVlOAVTux ,nMoAux,1,dDit,nDec,nTxAux),nDec))*(nSigno)   

                    nBaAVTux:=0
                    nVlBAVTux:=0
                    nVlSAVTux:=0
                    nVlOAVTux:=0

                ENDIF

            EndIf
        ENDIF

            (cAlias)->(DbSkip())
        EndDo

        (cAlias)->(DbCloseArea())
        
        oExec:Destroy()
        oExec := nil 

return nil

/*/{Protheus.doc} fCadenaSD
	Función crear una cadena con los BASIMP y VALIMP de la tabla asignada 
	@type  Function
	@author adrian.perez
	@since 28/06/2025
    @Param  aImp, arreglo, contiene los campos de los libros fiscales
    @param  cAlias      ,carácter,  nombre de la tabla
    @param  lBase      ,booleano,  indica si se crea los campos  BASIMP cuando es .T. y cuando es .F. se crean los campos VALIMP
	@return cCadena, carácter, retorna la cadena crea de los campos BASIMP y VALIMP
	/*/

function fCadenaSD(aImp,cAlias,lBase)
Local   cCadena:=""
Local   nA:=0
Local   cAuxBasVal:="VALIMP"


DEFAULT   aImp   :={}
DEFAULT   cAlias:=""
DEFAULT   lBase:=.F.

IF lBase
    cAuxBasVal:="BASIMP"
ENDIF

   For nA:=1 to len(aImp)
            cCadena+=cAlias+cAuxBasVal+aImp[nA]+", "
   Next

return cCadena
