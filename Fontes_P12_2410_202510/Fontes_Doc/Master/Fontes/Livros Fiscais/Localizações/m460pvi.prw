/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno	 ณ M460PVI	ณ Autor ณ MARCELLO GABRIEL     ณ Data ณ 24.09.2003 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ PERCEPCION VARIABLE SOBRE IVA   (URUGUAY)                   ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso		 ณ Generico 												   ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                      ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ PROGRAMADOR  ณ DATA   ณ BOPS ณ  MOTIVO DA ALTERACAO                    ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤลฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ              ณ        ณ      ณ                                         ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
FUNCTION M460PVI(cCalculo,nItem,aInfo)
Local lXFis,xRet

lXFis:=(MafisFound() .And. ProcName(1)<>"EXECBLOCK")
If lXfis
	xRet:=M460PVIN(cCalculo,nItem,aInfo)
Else
	xRet:=M460PVIA()
Endif
Return(xRet)
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณM460PVIA  บAutor  ณMarcello Gabriel    บFecha ณ  24/09/2003 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณCalculo PERCEPCION VARIABLE (Uruguai)                       บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
FUNCTION M460PVIA()
Local aItem
Local nBase:=0,nAliq:=0,cGrp,cImpIncid:=""
Local cDbf:=alias(),nOrd:=IndexOrd(),aVlr:={}

aItem:=ParamIxb[1]
aImp:=ParamIxb[2]
cImpIncid:=aImp[10]
If cModulo=="FRT" //Frontloja usa o arquivo SBI para cadastro de produtos
	cGrp:=Alltrim(SBI->BI_GRPPVI)
Else
	cGrp:=Alltrim(SB1->B1_GRPPVI)
Endif
dbselectarea("SFB")    // busca a aliquota padrao
if dbseek(xfilial()+aImp[1])
	nAliq:=SFB->FB_ALIQ
endif
nBase:=aItem[3]+aItem[4]
aVlr:=PVIValor(aImp[1],cGrp,SF4->F4_CF,SF4->F4_CODIGO,aItem[1],nBase)
If nBase>aVlr[1]  //neto < ficto
	aImp[4]:=0
Else
	nBase:=aVlr[2]
	aImp[4]:=aVlr[2]*(nAliq/100)
Endif
aImp[02]:=nAliq
aImp[03]:=nBase
dbSelectarea(cDbf)
dbSetOrder(nOrd)
Return(aImp)
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณM460PVIN  บAutor  ณMarcello Gabriel    บFecha ณ  24/09/2003 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณCalculo PERCEPCION VARIABLE (Uruguai)                       บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
FUNCTION M460PVIN(cCalculo,nItem,aInfo)
Local xRet
Local nBase:=0,nAliq:=0,cGrp
Local cDbf:=alias(),nOrd:=IndexOrd(),aVlr:={}

If cModulo=="FRT" //Frontloja usa o arquivo SBI para cadastro de produtos
	SBI->(DbSeek(xFilial("SBI")+MaFisRet(nItem,"IT_PRODUTO")))
	cGrp:=SBI->BI_GRPPVI
Else
	SB1->(DbSeek(xFilial("SB1")+MaFisRet(nItem,"IT_PRODUTO")))
	cGrp:=SB1->B1_GRPPVI
Endif
Do Case
	Case cCalculo=="B"
		xRet:=MaFisRet(nItem,"IT_VALMERC")
		If GetNewPar('MV_DESCSAI','1')=='2' 
			xRet	-= MaFisRet(nItem,"IT_DESCONTO")
		Endif
		aVlr:=PVIValor(aInfo[1],cGrp,MaFisRet(nItem,"IT_CF"),MaFisRet(nItem,"IT_TES"),MaFisRet(nItem,"IT_QUANT"),xRet)
		If xRet<=aVlr[1]  //neto < ficto
			xRet:=aVlr[2]
		Endif
	Case cCalculo=="A"
		dbselectarea("SFB")    // busca a aliquota padrao
		if dbseek(xfilial()+aInfo[1])
			nAliq:=SFB->FB_ALIQ
		endif
		xRet:=nALiq
	Case cCalculo=="V"
		nBase:=MaFisRet(nItem,"IT_BASEIV"+aInfo[2])
		nAliq:=MaFisRet(nItem,"IT_ALIQIV"+aInfo[2])
		xRet:=MaFisRet(nItem,"IT_VALMERC")
		aVlr:=PVIValor(aInfo[1],cGrp,MaFisRet(nItem,"IT_CF"),MaFisRet(nItem,"IT_TES"),MaFisRet(nItem,"IT_QUANT"),xRet)
		If xRet>aVlr[1]
			nAliq:=0
		Endif
		xRet:=aVlr[2]*(nAliq/100)
EndCase
dbSelectarea(cDbf)
dbSetOrder(nOrd)
Return(xRet)
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณPVIFicto  บAutor  ณMarcello Gabriel    บFecha ณ  24/09/2003 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณCalculo do "Ficto" para PERCEPCION VARIABLE (Uruguai)       บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function PVIFicto(cImp,cGrp,cCfo)
Local aArea:=GetArea()
Local nValor:=0
Local nRegSFF:=0,nOrdSFF:=0

DbSelectArea("SFF")
nOrdSFF:=IndexOrd()
nRegSFF:=Recno()
DbSetOrder(9)
If DbSeek(xFilial("SFF")+cImp+cGrp+cCfo)  //procura pela cfo solicitada
	nValor:=SFF->FF_IMPORTE
Else
	If !Empty(cCfo)   //se nao encontrada procura pelo "importe" geral - cfo em branco
		cCfo:=Space(TamSX3("FF_CFO")[1])
		If DbSeek(xFilial("SFF")+cImp+cGrp+cCfo)
			nValor:=SFF->FF_IMPORTE
		Endif
	Endif
Endif
DbSetOrder(nOrdSFF)
Dbgoto(nRegSFF)
RestArea(aArea)
Return(nValor)
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณPVIValor  บAutor  ณMarcello Gabriel    บFecha ณ  24/09/2003 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณCalculo Base/Valor para PERCEPCION VARIABLE (Uruguai)       บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function PVIValor(cImp,cGrp,cCfo,cTes,nQuant,nValor)
Local aArea:=GetArea()
Local nAliq:=0
Local nAliq1:=0
Local aAliq:={}
Local nBase:=0
Local nOrdSFC:=0,nRegSFC:=0
Local nRegSFB:=0
Local cImpIncid:="",nI:=0,nE:=0
Local aValor:={0,0}   //ficto+impostos,base

nRegSFB:=(SFB->(Recno()))
nOrdSFC:=(SFC->(IndexOrd()))
nRegSFC:=(SFC->(Recno()))
SFC->(DbSetOrder(2))
cImpIncid:=""
If (SFC->(DbSeek(xFilial("SFC")+cTes+cImp)))
	cImpIncid:=Alltrim(SFC->FC_INCIMP)
Endif
SFC->(DbSetOrder(nOrdSFC))
SFC->(DbGoto(nRegSFC))
//+---------------------------------------------------------------+
//ฆ Soma a Base de Cแlculo os Impostos Incidentes                 ฆ
//+---------------------------------------------------------------+
nI:=At(";",cImpIncid)
nI:=If(nI==0,Len(AllTrim(cImpIncid))+1,nI)
While nI>1
	If (SFB->(DbSeek(xFilial("SFB")+Substr(cImpIncid,1,nI-1))))
		nE:=(SFB->FB_ALIQ/100)
		nAliq+=nE
		Aadd(aAliq,nE)
	Endif
	cImpIncid:=Stuff(cImpIncid,1,nI,"")
	nI := At( ";",cImpIncid)
	nI:=If(nI==0,Len(AllTrim(cImpIncid))+1,nI)
Enddo
For nI:=Len(aAliq) To 2 Step -1
	nAliq1:=1
	For nE:=nI To 1 Step -1
		nAliq1:=nAliq1*aAliq[nE]
	Next
	nAliq+=nAliq1
Next
nAliq1:=0
If (SFB->(DbSeek(xFilial("SFB")+cImp)))
	nAliq1:=SFB->FB_ALIQ/100
Endif
SFB->(DbGoto(nRegSFB))
nBase:=PVIFicto(cImp,cGrp,cCfo)*nQuant
aValor[1]:=nBase*(1+nAliq)
nBase:=aValor[1]-nValor
aValor[2]:=(nBase*((nAliq-nAliq1)/nAliq1))+nBase
RestArea(aArea)
Return(aValor)
