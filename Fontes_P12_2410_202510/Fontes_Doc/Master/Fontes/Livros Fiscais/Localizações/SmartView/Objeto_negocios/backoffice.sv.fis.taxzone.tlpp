#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fis.taxzone.ch'

namespace custom.fiscal.taxzone.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.fis.taxzone
@description Classe para creación del Objeto de Negocio de Recibos pendientes por cobrador para TReports
@author Marcelo Hruschka
@since 25/11/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFIS', tables='SFH', name='Empresas por zona fiscal', country='ARG', initialRelease='12.1.2410', customTables='SFH' )
class taxzoneTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecno as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 25/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class taxzoneTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Empresas por zona fiscal'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Empresas por zona fiscal'

	// Define a Área
	self:appendArea( STR0002 ) // 'Libros Fiscales'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FISSV099' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// demais campos
	self:aFields := {'FH_AGENTE','FH_ZONFIS','FH_CLIENTE','FH_FORNECE','FH_LOJA','FH_NOME','FH_IMPOSTO','FH_PERCIBI','FH_ISENTO','FH_PERCENT','FH_APERIB','FH_ALIQ','FH_DECRETO','FH_SITUACA','FH_COEFMUL','FH_ITEM','FH_INIVIGE','FH_FIMVIGE','FH_TIPO','FH_REGIME','FH_CONCEP','FH_CLAPROD','FH_DTINI','FH_DTFIM','FH_VALEMB','FH_REGIMEN'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 25/11/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class taxzoneTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local cExp          as character

	cFiltro 			:= ''
	cAliasA			    := ''
	cExp                := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSFH := getCpoUser( aCustomFields, 'SFH', 'FH', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT SFH.FH_FILIAL, SFH.FH_AGENTE, SFH.FH_ZONFIS, SFH.FH_CLIENTE, SFH.FH_FORNECE, SFH.FH_LOJA, SFH.FH_NOME, SFH.FH_IMPOSTO, "
	cQuery += " SFH.FH_PERCIBI, SFH.FH_ISENTO, SFH.FH_PERCENT, SFH.FH_APERIB, SFH.FH_ALIQ, SFH.FH_DECRETO, SFH.FH_SITUACA, SFH.FH_COEFMUL, "
	cQuery += " SFH.FH_ITEM, SFH.FH_INIVIGE, SFH.FH_FIMVIGE, SFH.FH_TIPO, SFH.FH_REGIME, SFH.FH_CONCEP, SFH.FH_CLAPROD, SFH.FH_DTINI, SFH.FH_DTFIM, "
	cQuery += " SFH.FH_VALEMB, SFH.FH_REGIMEN, "
	cQuery += cCpoSFH
	cQuery += " SFH.R_E_C_N_O_ AS REG "
	cQuery += " FROM " + RetSqlName("SFH") + " SFH "
	cQuery += " WHERE SFH.FH_FILIAL = ? "
	cQuery += "	  AND SFH.FH_IMPOSTO BETWEEN ? AND ? "
	cQuery += "	  AND SFH.FH_ZONFIS BETWEEN ? AND ? "
	cQuery += "   AND SFH.D_E_L_E_T_ = ? "

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND " + aFiltro[nX, 1] + ' ' + aFiltro[nX, 2] + " " + aFiltro[nX, 3] + " "
	Next

	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )
	oExecA:SetString( 01, xFilial("SFH") )			// FH_FILIAL
	oExecA:SetString( 02, jParams['MV_PAR03'][1] )	// FH_IMPOSTO
	oExecA:SetString( 03, jParams['MV_PAR04'][1] )	// FH_IMPOSTO
	oExecA:SetString( 04, jParams['MV_PAR01'][1] )	// FH_ZONFIS
	oExecA:SetString( 05, jParams['MV_PAR02'][1] )	// FH_ZONFIS
	oExecA:SetString( 06, ' ' )						// SFH.D_E_L_E_T_

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )


		self:jItems := JsonObject():new( )
		self:nRecno := ( cAliasA )->REG

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nX][5] $ "FH_AGENTE,FH_PERCIBI,FH_ISENTO,FH_APERIB" )
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "S"
					self:jItems[self:aStruct[nX][1]] := STR0007 // "Sí"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "N"
					self:jItems[self:aStruct[nX][1]] := STR0008 // "No"
				Endif
			ElseIf ( self:aStruct[nX][5] == "FH_SITUACA" )
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "1"
					self:jItems[self:aStruct[nX][1]] := STR0009 // "Normal"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "2"
					self:jItems[self:aStruct[nX][1]] := STR0010 // "Riesgo"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "3"
					self:jItems[self:aStruct[nX][1]] := STR0011 // "Monotributartista"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "4"
					self:jItems[self:aStruct[nX][1]] := STR0012 // "Embargo"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "5"
					self:jItems[self:aStruct[nX][1]] := STR0013 // "Regímenes especiales"
				Endif
			ElseIf ( self:aStruct[nX][5] == "FH_TIPO" )
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "I"
					self:jItems[self:aStruct[nX][1]] := STR0014 // "Resp. Inscripto"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "N"
					self:jItems[self:aStruct[nX][1]] := STR0015 // "Resp. No Inscripto"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "X"
					self:jItems[self:aStruct[nX][1]] := STR0016 // "Exento"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "M"
					self:jItems[self:aStruct[nX][1]] := STR0017 // "Monotributarista"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "V"
					self:jItems[self:aStruct[nX][1]] := STR0018 // "Convenio Multilateral"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "*"
					self:jItems[self:aStruct[nX][1]] := STR0019 // "Indeterminado"
				Endif
			ElseIf ( self:aStruct[nX][5] == "FH_REGIMEN" )
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "1"
					self:jItems[self:aStruct[nX][1]] := "RG_23_14"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "2"
					self:jItems[self:aStruct[nX][1]] := "RG_27_00"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "3"
					self:jItems[self:aStruct[nX][1]] := "RG_33_99"
				Endif
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		//self:processData()
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 25/11/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class taxzoneTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
