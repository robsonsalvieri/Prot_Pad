#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fis.taxAnalysis.ch'

namespace custom.fiscal.taxAnalysis.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.fis.taxAnalysis
@description Classe para creación del Objeto de Negocio para TReports
@author Marcelo Hruschka
@since 25/11/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFIS', tables='SF3,SFB', name='Auxiliar de impuestos', country='COL', initialRelease='12.1.2410', customTables='SF3,SFB' )
class taxAnalysisTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 25/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class taxAnalysisTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Auxiliar de impuestos'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Auxiliar de impuestos'

	// Define a Área
	self:appendArea( STR0002 ) // 'Libros Fiscales'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FISSV996' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	//
	aAdd( aCpos, { 'BASE'   , STR0007, 'number', STR0007 } ) // 'Base Impuesto'
	aAdd( aCpos, { 'ALIQ'   , STR0008, 'number', STR0008 } ) // 'Alicuota'
	aAdd( aCpos, { 'VALOR'  , STR0009, 'number', STR0009 } ) // 'Valor Impuesto'
	aAdd( aCpos, { 'NOMBRE' , STR0010, 'string', STR0010 } ) // 'Nombre'
	aAdd( aCpos, { 'NIT'    , STR0011, 'string', STR0011 } ) // 'NIT'
	aAdd( aCpos, { 'CEDULA' , STR0012, 'string', STR0012 } ) // 'CEDULA'

	// demais campos
	self:aFields := {'F3_CLIEFOR','F3_LOJA','NOMBRE','NIT','CEDULA','F3_TIPOMOV','F3_ENTRADA','F3_NFISCAL','FB_CODIGO','FB_DESCR','BASE','ALIQ','VALOR'}
	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 25/11/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class taxAnalysisTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local cExp          as character

	cFiltro 			:= ''
	cAliasA			    := ''
	cExp                := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSF3 := getCpoUser( aCustomFields, 'SF3', 'SF3', ',' )
	cCpoSFB := getCpoUser( aCustomFields, 'SFB', 'SFB', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// posiciona no imposto
	DbSelectArea("SFB")
	DbSetOrder(1)   // FB_FILIAL+FB_CODIGO                                                                                                                                             
	If !DbSeek(xFilial("SFB")+jParams['MV_PAR01'][1])
		IIf(!self:setErrorStatus( 400, STR0013, STR0014 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Cod. Impuesto no encontrado!' // '¡Verifique el codigo del impuesto dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0013,,, ) // 'Cod. Impuesto no encontrado!'
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT F3_CLIEFOR, F3_LOJA,
	cQuery += " CASE F3_TIPOMOV
	cQuery += " WHEN 'C' THEN A2_NOME
	cQuery += " WHEN 'V' THEN A1_NOME
	cQuery += " END AS NOMBRE,
	cQuery += " CASE F3_TIPOMOV
	cQuery += " WHEN 'C' THEN A2_CGC
	cQuery += " WHEN 'V' THEN A1_CGC
	cQuery += " END AS NIT,
	cQuery += " CASE F3_TIPOMOV
	cQuery += " WHEN 'C' THEN A2_PFISICA
	cQuery += " WHEN 'V' THEN A1_PFISICA
	cQuery += " END AS CEDULA,
	cQuery += " F3_TIPOMOV,
	cQuery += " F3_ENTRADA, F3_NFISCAL, FB_CODIGO, FB_DESCR,"
	cQuery += " ? AS BASE,"
	cQuery += " ? AS ALIQ,"
	cQuery += " ? AS VALOR,"
	cQuery += " ? "
	cQuery += " ? "
	cQuery += " SF3.R_E_C_N_O_ AS REG"
	cQuery += " FROM " + RetSqlName("SF3") + " SF3"
	cQuery += " INNER JOIN " + RetSqlName("SFB") + " SFB ON SFB.FB_FILIAL = ? AND SFB.FB_CODIGO = ? AND SFB.D_E_L_E_T_ = ?"
	cQuery += " LEFT JOIN "  + RetSqlName("SA1") + " SA1 ON SA1.A1_FILIAL = ? AND SA1.A1_COD = SF3.F3_CLIEFOR AND SA1.A1_LOJA = SF3.F3_LOJA AND SF3.F3_TIPOMOV = ? AND SA1.D_E_L_E_T_ = ?"
	cQuery += " LEFT JOIN "  + RetSqlName("SA2") + " SA2 ON SA2.A2_FILIAL = ? AND SA2.A2_COD = SF3.F3_CLIEFOR AND SA2.A2_LOJA = SF3.F3_LOJA AND SF3.F3_TIPOMOV = ? AND SA2.D_E_L_E_T_ = ?"
	cQuery += " WHERE SF3.F3_FILIAL = ?"
	cQuery += "	  AND SF3.F3_ENTRADA BETWEEN ? AND ? "
	cQuery += "   AND SF3.D_E_L_E_T_ = ?"

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND ? " + ' ' + " ? ? "
	Next

	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )

    nSeq := 0
    oExecA:SetUnSafe( nSeq += 1, "SF3.F3_BASIMP" + SFB->FB_CPOLVRO )
    oExecA:SetUnSafe( nSeq += 1, "SF3.F3_ALQIMP" + SFB->FB_CPOLVRO )
    oExecA:SetUnSafe( nSeq += 1, "SF3.F3_VALIMP" + SFB->FB_CPOLVRO )
    oExecA:SetUnSafe( nSeq += 1, cCpoSF3)
    oExecA:SetUnSafe( nSeq += 1, cCpoSFB)
	oExecA:SetString( nSeq += 1, xFilial("SFB") )			// FB_FILIAL
	oExecA:SetString( nSeq += 1, jParams['MV_PAR01'][1] )	// FB_CODIGO
	oExecA:SetString( nSeq += 1, ' ' )						// SFB.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("SA1") )			// A1_FILIAL
	oExecA:SetString( nSeq += 1, 'V' )						// F3_TIPOMOV
	oExecA:SetString( nSeq += 1, ' ' )						// SA1.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("SA2") )			// A2_FILIAL
	oExecA:SetString( nSeq += 1, 'C' )						// F3_TIPOMOV
	oExecA:SetString( nSeq += 1, ' ' )						// SA2.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("SF3") )			// F3_FILIAL
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR02'][1] )[1] ) )	// F3_ENTRADA
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR03'][1] )[1] ) )	// F3_ENTRADA
	oExecA:SetString( nSeq += 1, ' ' )						// SF3.D_E_L_E_T_
	For nX := 1 To Len( aFiltro )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 1] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 2] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 3] )
	Next
	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )

		self:jItems := JsonObject():new( )

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nX][5] $ "F3_TIPOMOV" )
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "C"
					self:jItems[self:aStruct[nX][1]] := STR0015 // 'Compras'
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "V"
					self:jItems[self:aStruct[nX][1]] := STR0016 // 'Ventas'
				Endif
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		//self:processData()
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 25/11/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class taxAnalysisTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
