#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fis.taxagent.ch'

namespace custom.fiscal.taxagent.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.fis.taxagent
@description Classe para creación del Objeto de Negocio para TReports
@author Marcelo Hruschka
@since 11/12/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFIS', tables='SFZ', name='Agentes fiscales vs Impuestos', country='ARG', initialRelease='12.1.2410', customTables='SFZ' )
class taxagentTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecno as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 11/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class taxagentTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Agentes fiscales vs Impuestos'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Agentes fiscales vs Impuestos'

	// Define a Área
	self:appendArea( STR0002 ) // 'Libros Fiscales'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FISSV098' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// demais campos
	self:aFields := {'FZ_FILIAL','FZ_IMPOSTO','FZ_DESIMPO','FZ_TIPO','FZ_DESTIPO','FZ_DESTACA','FZ_ESPECIE','FZ_SERIE','FZ_TIPMOV'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 11/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class taxagentTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local cExp          as character

	cFiltro 			:= ''
	cAliasA			    := ''
	cExp                := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSFZ := getCpoUser( aCustomFields, 'SFZ', 'FZ', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT SFZ.FZ_FILIAL, SFZ.FZ_IMPOSTO, SFZ.FZ_DESIMPO, SFZ.FZ_TIPO, SFZ.FZ_DESTIPO, SFZ.FZ_DESTACA, SFZ.FZ_ESPECIE, SFZ.FZ_SERIE, SFZ.FZ_TIPMOV,"
	cQuery += cCpoSFZ
	cQuery += " SFZ.R_E_C_N_O_ AS REG"
	cQuery += " FROM " + RetSqlName("SFZ") + " SFZ"
	cQuery += " WHERE SFZ.FZ_FILIAL = ?"
	cQuery += "	  AND SFZ.FZ_IMPOSTO BETWEEN ? AND ?"
	cQuery += "   AND SFZ.D_E_L_E_T_ = ?"

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND " + aFiltro[nX, 1] + ' ' + aFiltro[nX, 2] + " " + aFiltro[nX, 3] + " "
	Next

	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )
	oExecA:SetString( 01, xFilial("SFZ") )			// FZ_FILIAL
	oExecA:SetString( 02, jParams['MV_PAR01'][1] )	// FZ_IMPOSTO
	oExecA:SetString( 03, jParams['MV_PAR02'][1] )	// FZ_IMPOSTO
	oExecA:SetString( 04, ' ' )						// SFZ.D_E_L_E_T_

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )


		self:jItems := JsonObject():new( )
		self:nRecno := ( cAliasA )->REG

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nX][5] $ "FZ_DESTACA" )
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "S"
					self:jItems[self:aStruct[nX][1]] := STR0007 // "Sí"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "N"
					self:jItems[self:aStruct[nX][1]] := STR0008 // "No"
				Endif
			ElseIf ( self:aStruct[nX][5] == "FZ_SERIE" )
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "A" 
					self:jItems[self:aStruct[nX][1]] := STR0009 // "Tipo A"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "B"
					self:jItems[self:aStruct[nX][1]] := STR0010 // "Tipo B"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "C"
					self:jItems[self:aStruct[nX][1]] := STR0011 // "Tipo C"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "E"
					self:jItems[self:aStruct[nX][1]] := STR0012 // "Exportacion"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "X"
					self:jItems[self:aStruct[nX][1]] := STR0013 // "Remito"
				Endif
			ElseIf ( self:aStruct[nX][5] == "FZ_TIPMOV" )
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "E"
					self:jItems[self:aStruct[nX][1]] := STR0014 // "Entradas"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "S"
					self:jItems[self:aStruct[nX][1]] := STR0015 // "Salidas"
				Endif
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		//self:processData()
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 11/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class taxagentTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
