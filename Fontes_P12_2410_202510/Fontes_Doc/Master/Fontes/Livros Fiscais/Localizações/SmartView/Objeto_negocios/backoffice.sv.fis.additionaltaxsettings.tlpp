#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fis.additionaltaxsettings.ch'

namespace custom.fiscal.additionaltaxsettings.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.fis.additionaltaxsettings
@description Classe para creación del Objeto de Negocio de Configuración adicional de impuestos para SMARTVIEW
@author Leonardo Pereira
@since 26/12/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFIS', tables='SFF', name='Configuración Adicional de Impuestos', country='ALL', initialRelease='12.1.2410', customTables='SFF' )
class additionaltaxsettingsTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Leonardo Pereira
@since 23/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class additionaltaxsettingsTReportsBusinessObject

	Local aCpos := { } as array

	_Super:new( )

	// Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Configuración Adicional de Impuestos'

	// Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Configuración Adicional de Impuestos'

	// Define a Área
	self:appendArea( STR0002 ) // 'Livros Fiscais'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FISSV994' )
		IIf( !self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'' ) // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// demais campos
	self:aFields := { 'FF_ITEM', 'FF_CONCEPT', 'FF_ALQINSC', 'FF_ALQNOIN', 'FF_IMPORTE', 'FF_FXDE', 'FF_FXATE', 'FF_RETENC', 'FF_PERC', 'FF_EXCEDE' }

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Leonardo Pereira
@since 23/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class additionaltaxsettingsTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		  as character
	Local cFiltro 		  as character
	Local cExp 			  as character
	Local cCpoSFF		  as character
	Local cAliasA       as character

	Local n1			     as numeric
	Local nSeq			  as numeric
	Local nPosExp		  as numeric

	Local jParams 		  as json

	Local aPDFields	  as array
	Local aFiltro		  as array
	Local aCustomFields as array

	Local lObfuscated	  as logical

	Local oExecA		  as object

	cFiltro 			:= ''
	cExp 				:= ''
	cAliasA			:= ''
	cExp				:= ''

	n1				   := 0
	nPosExp			:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSFF := getCpoUser( aCustomFields, 'SFF', 'FF', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { | x, y | x[1] + x[3] < y[1] + y[3] } )

		// Define os dados para conversão de moedas
		nPosExp := aScan( aFiltro, { | x | AllTrim( x[1] ) == 'FF_ITEM' } )
		If ( nPosExp > 0 )
			cExp := aFiltro[nPosExp, 3]

			// Exclui o elemento do Array
			aDel( aFiltro, nPosExp )

			// Redimensiona o Array
			aSize( aFiltro, ( Len( aFiltro ) - 1 ) )
		EndIf
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT "
	cQuery += " SFF.FF_ITEM, SFF.FF_CONCEPT, SFF.FF_ALQINSC, SFF.FF_ALQNOIN, SFF.FF_IMPORTE, SFF.FF_FXDE, SFF.FF_FXATE, SFF.FF_RETENC, SFF.FF_PERC, " + cCpoSFF + " SFF.FF_EXCEDE "

	cQuery += "FROM " + RetSqlName( 'SFF' ) + " SFF "

	cQuery += "WHERE SFF.FF_FILIAL = ? "
	cQuery += "   AND SFF.FF_NUM BETWEEN ? AND ? "

	If ( cExp == "'1'" )
		cQuery += "   AND SFF.FF_ITEM <= ? "
	ElseIf ( cExp == "'2'" )
		cQuery += "   AND SFF.FF_ITEM > ? "
	EndIf

	cQuery += "   AND SFF.FF_MESREF = ? "

	// Agrega os filtros na QUERY
	For n1 := 1 To Len( aFiltro )
		cQuery += "   AND " + aFiltro[n1, 1] + ' ' + aFiltro[n1, 2] + " " + aFiltro[n1, 3] + " "
	Next

	cQuery += "   AND SFF.D_E_L_E_T_ = ? "

	cQuery += "ORDER BY ? "

	cQuery := ChangeQuery( cQuery )

	oExecA := FwExecStatement( ):New( cQuery )

	nSeq := 0

	oExecA:SetString( nSeq += 1, FWxFilial( "SFF" ) )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR01'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR02'][1] )
	oExecA:SetNumeric( nSeq += 1, 12 )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR03'][1] )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetUnsafe( nSeq += 1, 'SFF.FF_FILIAL, SFF.FF_NUM, SFF.FF_ITEM' )

	cQuery := oExecA:getFixQuery( )

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )
		self:jItems := JsonObject():new( )

		For n1 := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[n1][5] ) == 0 )
				self:jItems[self:aStruct[n1][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[n1][5] ) )
			ElseIf ( self:aStruct[n1][3] == 'date' )
				self:jItems[self:aStruct[n1][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[n1][5] ) ) )
			Else
				self:jItems[self:aStruct[n1][1]] := ( cAliasA )->&( self:aStruct[n1][5] )
			EndIf
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )
	End

	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Leonardo Pereira
@since 23/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class additionaltaxsettingsTReportsBusinessObject

	Local n1 as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For n1 := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[n1][1], self:aStruct[n1][2], self:aStruct[n1][3], self:aStruct[n1][4], self:aStruct[n1][5] )
	Next

Return( self:oSchema )
