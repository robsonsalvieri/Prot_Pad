#include "SIGAWIN.CH"        // incluido pelo assistente de conversao do AP5 IDE em 09/09/99
#include "PROTHEUS.CH"
//Posicoes  do terceiro array recebido nos impostos a traves da matxfis...
#DEFINE X_IMPOSTO    01 //Nome do imposto
#DEFINE X_NUMIMP     02 //Sufixo do imposto

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calculo de impuesto  ICA 			    			      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ M460ica2                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Calculo de ICA por ítem                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function M460ica2(cCalculo,nItem,aInfo)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Declaracao de variaveis utilizadas no programa atraves da funcao    ³
	//³ SetPrvt, que criara somente as variaveis definidas pelo usuario,    ³
	//³ identificando as variaveis publicas do sistema utilizadas no codigo ³
	//³ Incluido pelo assistente de conversao do AP5 IDE                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local nDesconto	:=	0,lXFis,xRet,cImp
	Local nAliqSFB	:= 0
	Local nImporte	:= 0
	Local cCodMun   := ""
	Local cCodIca	:= ""
	Local lCodMun   := .F.
	Local nMoedSFF  := 1
	Local nTaxaMoed	:= 0
	Local nMoedaOri := 1
	Local cFunName	:= FunName()
	Local nVlItem	:= 0
	Local nVlImpor	:= 0
	Local lExSC6TP	:= SC6->(ColumnPos("C6_TPACTIV")) > 0
	Local lExSC6CM	:= SC6->(ColumnPos("C6_CODMUN")) > 0
	LOCAL nCols		:= 0
	Local nPosTPSC6	:=	0
	Local nPosCMSC6 :=  0
	Local nAliq :=0
	Local cCFO :=""
	Local lRet := .T.
	Local nBase :=0
	Local cTes  :=""
	Local nMoeda :=0
	Local nVal :=0
	Local aItemINFO :={}
	Local nPosTPSD2	:=	0
	Local nPosCMSD2 :=  0
	Local cVar 		:=  ReadVar()

	Default cCalculo :=""
	Default nItem	:=0
	Default aInfo	:={}

	lXfis:=(MaFisFound() .And. ProcName(1)<>"EXECBLOCK")

	
	If !LXFis
		aItemINFO	:= ParamIxb[1]
		xRet		:= ParamIxb[2]
		cImp		:=xRet[1]
		// Busca o CFO do Tes Correspondente - SF4
		dbSelectArea("SF4")
		cCFO := SF4->F4_CF
		cTes := SF4->F4_CODIGO
		IF cFunName $ "MATA468N|MATA461"
			If lExSC6TP .Or. lExSC6CM
				cCodIca := IIF(lExSC6TP, SC6->C6_TPACTIV, "")
				cCodMun := IIF(lExSC6CM, SC6->C6_CODMUN, "")
			EndIf
		EndIf	
	Else
		xRet:=0
		cCFO:= MaFisRet(nItem,"IT_CF")
		cTes:= MaFisRet(nItem,"IT_TES")
		cImp:=aInfo[X_IMPOSTO]
		nCols:=nItem
		IF cFunName $ "MATA410"
			nPosTPSC6	:=	aScan(aHeader,{|x| AllTrim(x[2])=="C6_TPACTIV"}) 
			nPosCMSC6	:=  aScan(aHeader,{|x| AllTrim(x[2])=="C6_CODMUN"})
			If nPosTPSC6 > 0 
				cCodIca	:= aCols[nCols][nPosTPSC6]
			EndIf
			If nPosCMSC6 > 0 
				cCodMun	:= aCols[nCols][nPosCMSC6]
			EndIf	
		Else
			nPosTPSD2	:=	aScan(aHeader,{|x| AllTrim(x[2])=="D2_TPACTIV"}) 
			nPosCMSD2	:=  aScan(aHeader,{|x| AllTrim(x[2])=="D2_CODMUN"})
			If  cVar == "M->D2_TPACTIV"
				cCodIca:= M->D2_TPACTIV
				If nPosCMSD2 > 0 
					cCodMun	:= aCols[nItem][nPosCMSD2]
				EndIf
			ElseIf cVar == "M->D2_CODMUN"
				cCodMun:= M->D2_CODMUN
				If nPosTPSD2 > 0 
					cCodIca	:= aCols[nItem][nPosTPSD2]
				EndIf
			Else
				If nPosTPSD2 > 0 
					cCodIca	:= aCols[nCols][nPosTPSD2]
				EndIf
				If nPosCMSD2 > 0 
					cCodMun	:= aCols[nCols][nPosCMSD2]
				EndIf
			EndIf		
		ENDIF
	EndIf	
	//Tratamiento para Calcular ICA
	Iif((SA1->A1_RETICA == "S"), lRet := .T., lRet := .F.)

	dbSelectArea("SFB")
	dbSetOrder(1)
	If SFB->( MsSeek(xFilial("SFB")+cImp))
		If If(!lXFis,.T.,cCalculo$"AB")
			nAliqSFB   := SFB->FB_ALIQ // Aliquota padrón
		EndIf
	EndIf

	If lRet
		If Empty(cCodIca)
			nAliq   := nAliqSFB  // Aliquota padrão
			lRet    := .T.
		Else
			dbSelectArea("SFF")
			dbSetOrder(18)
			If SFF->(MsSeek(xFilial("SFF")+cImp+cCodMun))
				While !Eof()
					If ( ALLTRIM(SFF->FF_COD_TAB) == ALLTRIM(cCodICA) ) .And.;
						SFF->(FF_IMPOSTO+FF_CODMUN) == cImp+cCodMun
						If FF_FLAG != "1"
							RecLock("SFF",.F.)
							Replace FF_FLAG With "1"
						Endif
						nAliq   := SFF->FF_ALIQ  // Alicuota de Zona Fiscal
						nImporte:= SFF->FF_IMPORTE
						nMoedSFF := SFF->FF_MOEDA	
						lCodMun := .T.
						lRet 	:= .T.
						Exit
					Else
						lRet := .F.
					EndIf
					SFF->(dbSkip())
				EndDo
			Endif
			If !lCodMun
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Metodo antigo de busca , pela zona fiscal (departamento) foi DESATIVADA 						³
				//³caso não tenha aliq. por cod.município, obtera' da SFB, que na Colombia devera' estar ZERADA.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nAliq   := nAliqSFB // Aliquota padrão
				lRet    := .T.
			EndIf
		Endif
		If lRet
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Efetua o calculo de ICA, taxa sera' calculada por mil (1000)  				 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !lXFis
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Calcula o imposto somente se o valor da base for maior ou igual a base minima    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If nImporte > 0 .And. (aItemINFO[3]) < nImporte
					nAliq := 0
				EndIf
				//Tira os descontos se for pelo liquido .Bruno
				If Subs(xRet[5],4,1) == "S"  .And. Len(xRet) >= 18 .And. ValType(xRet[18])=="N"
					nDesconto	:=	xRet[18]
				Else
					nDesconto	:=	0
				Endif

				xRet[02]  := nAliq // Alicuota de Zona Fiscal
				xRet[03]  := (aItemINFO[3] - nDesconto) //Valor ítem. 
				xRet[04]  := round(xRet[03] * ( xRet[02]/1000) ,2)

				nMoedaOri:= IIf(Type("lPedidos")	<> "U" .And. lPedidos , SC5->C5_MOEDA ,Max(SF2->F2_MOEDA,1))
				nVlItem := xMoeda(xRet[03],nMoedaOri ,1)
				nVlImpor:= xMoeda(nImporte,nMoedSFF,1)
				
				//Valor impuesto si valor supera límite, si no, es 0.
				If nVlItem >= nVlImpor
					xRet[04]  := xRet[04]
					If Len(xRet) > 20
						xRet[21] := nVlImpor
					EndIf
				Else
					xRet[04]  := 0
				EndIf
			Else
				Do Case
					Case cCalculo=="B"
						xRet:= 0
						If SuperGetMv('MV_DESCSAI','1')=='1'
							xRet	+= MaFisRet(nItem,"IT_DESCONTO")
						Endif
						//Tira os descontos se for pelo liquido
						SFC->(DbSetOrder(2))
						If SFC->(MsSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+aInfo[X_IMPOSTO]))
							If SFC->FC_LIQUIDO=="S"
								xRet-=MaFisRet(nItem,"IT_DESCONTO")
							Endif
						Endif
						xRet+= MaFisRet(nItem,"IT_VALMERC")
					Case cCalculo=="A"
						xRet:=nAliq
					Case cCalculo=="V"
						If Type("M->F2_MOEDA")<>"U"
						   	nMoeda := M->F2_MOEDA
						    nTaxaMoed := M->F2_TXMOEDA				
						ElseIf Type("M->C5_MOEDA")<>"U" 
							nMoeda := M->C5_MOEDA
						    nTaxaMoed := M->C5_TXMOEDA		
						EndIf
						If nTaxaMoed==0
							nTaxaMoed:= RecMoeda(nMoeda)
						EndIf	
						nVal:=0
						SFC->(DbSetOrder(2))
						If SFC->(MsSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+aInfo[X_IMPOSTO]))
							If SFC->FC_CALCULO=="I"
								nBase:=MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP])
								nVal	:=	MaFisRet(nItem,'IT_BASEIV'+aInfo[2])
							EndIf
						Endif

						nAliq:=MaFisRet(nItem,"IT_ALIQIV"+aInfo[X_NUMIMP])

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Calcula el impuesto solamente cuando el valor base es mayoor o igual que la base ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If xMoeda(nVal,nMoeda,1,Nil,Nil,nTaxaMoed) >= xMoeda(nImporte,nMoedSFF,1)
							xRet:=nVal * ( nAliq/1000)
						Else
							xRet:= 0
						EndIf
				EndCase
			Endif
		EndIf
	EndIf
Return( xRet )
