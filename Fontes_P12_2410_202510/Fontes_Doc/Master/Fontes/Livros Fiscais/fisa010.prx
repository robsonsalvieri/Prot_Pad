#INCLUDE "PROTHEUS.CH"
#INCLUDE "FISA010.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWADAPTEREAI.CH"

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณFISA010   ณ Autor ณ Eduardo Riera         ณ Data ณ03.11.2007ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณPrograma de atualizacao do cadastro de municipios           ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ Generico                                                   ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ PROGRAMADOR  ณ DATA   ณ BOPS ณ  MOTIVO DA ALTERACAO                   ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณLuis Enrํquez ณ05/12/18ณDMINA-1012ณRep. DMINA-253 Se realizan cambios  ณฑฑ
ฑฑณ              ณ        ณ          ณpara localizaci๓n, se agrega funcionณฑฑ
ฑฑณ              ณ        ณ          ณFisVldCiud(). (EUA)                 ณฑฑ
ฑฑณOscar G.      ณ12/05/20ณDMINA-8270ณSe crea PE FISA010MUN para carga de ณฑฑ
ฑฑณ              ณ        ณ          ณmunicip. cuando CC2 esta vacia.(MEX)ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function FISA010(aAuto,nOpcAuto)

//-- Variavel usada para verificar se o disparo da funcao IntegDef() pode ser feita manualmente
Local lIntegDef		:= FindFunction("GETROTINTEG") .And. FindFunction("FWHASEAI") .And. FWHasEAI("FISA010",.T.,,.T.)
Local lVerpesssen	:= Iif(FindFunction("Verpesssen"),Verpesssen(),.T.)
Local lF010Muni		:= ExistBlock("FISA010MUN")

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Define o cabecalho da tela de atualizacoes                   ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู   

PRIVATE cCadastro := IIf(cPaisLoc == "EUA", STR0010, STR0006)  //"Registro de Ciudades" - "Atualizao da tabela de municipios"
PRIVATE aRotina := MenuDef()  

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Endereca a funcao de BROWSE                                   ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

If lVerpesssen
	If GetBuild() < '7.00.101202A'
		AxCadastro("CC2",;
			cCadastro,;
			"Fis010Exl()",;
			"Fis010Vld()",;
			/*aRotAdic*/,;
			/**/,;
			/**/,;
			/**/,;
			{||Iif(lIntegDef,FwIntegDef("FISA010"),.T.)},;
			aAuto,;
			nOpcAuto,;
			/*aButtons*/)	

	Else  // EXIBE BROWSE ATRAVษS DO MVC
		If aAuto == Nil
			oMBrowse:= FWMBrowse():New()	
			oMBrowse:SetAlias("CC2")
			If cPaisLoc == "MEX" .And. lF010Muni
				DBSelectArea("CC2")
				If CC2->(RECCOUNT()) == 0
					ExecBlock("FISA010MUN", .F., .F., )
				EndIf
			EndIf
			oMBrowse:SetDescription(cCadastro)
			oMBrowse:Activate()
		Else
			FwMvcRotAuto(ModelDef(),"CC2",nOpcAuto,{{"FISA010_CC2",aAuto}})  //Chamada da rotina automatica atrav้s do MVC
		EndIf
	EndIf		
EndIf
Return(.T.)

//-------------------------------------
/*	Modelo de Dados
@author  	Jefferson Tomaz
@version 	P10 R1.4
@build		7.00.101202A
@since 		31/03/2011
@return 		oModel Objeto do Modelo*/
//-------------------------------------
Static Function ModelDef()
Local oModel 	:= Nil
Local oStruCC2	:= FwFormStruct(1,"CC2")
Local bPosValid	:= { |oMdl| Fis010Pos( oMdl ) } //Substitui็ใo da fun็ใo Fis010Exl
Local lIntGFE   := SuperGetMv("MV_INTGFE",,.F.)
Local lF1T		:= cPaisLoc == "BRA" .AND. AliasIndic("F1T")
Local oDetMun 	:= Nil
Local oCommit	:= FA010COMMIT():New()

If lIntGFE
	oStruCC2:AddField( ;                    // Ord. Tipo Desc.
	"IBGE Compl"                     , ;      // [01]  C   Titulo do campo
	"Cod.IBGE Compl "                , ;      // [02]  C   ToolTip do campo
	"CC2_CDIBGE"                     , ;      // [03]  C   Id do Field
	'C'                              , ;      // [04]  C   Tipo do campo
	7                                , ;      // [05]  N   Tamanho do campo
	0                                , ;      // [06]  N   Decimal do campo
	NIL                              , ;      // [07]  B   Code-block de valida็ใo do campo
	NIL                              , ;      // [08]  B   Code-block de valida็ใo When do campo
	NIL                              , ;      // [09]  A   Lista de valores permitido do campo
	NIL                              , ;      // [10]  L   Indica se o campo tem preenchimento obrigat๓rio
	FwBuildFeature( STRUCT_FEATURE_INIPAD,'TMS120CdUf(CC2->CC2_EST, "1") + CC2->CC2_CODMUN' ), ;   // [11]  B   Code-block de inicializacao do campo
	NIL                              , ;      // [12]  L   Indica se trata-se de um campo chave
	NIL                              , ;      // [13]  L   Indica se o campo pode receber valor em uma opera็ใo de update.
	.T.                              )        // [14]  L   Indica se o campo ้ virtual
	
	oStruCC2:AddTrigger( ;
	"CC2_CODMUN", ;                                                      // [01] Id do campo de origem
	"CC2_CDIBGE" , ;                                                      // [02] Id do campo de destino
	{|| .T. }   , ;                                                      // [03] Bloco de codigo de valida็ใo da execu็ใo do gatilho
	&( ' { | oModel |  TMS120CdUf(M->CC2_EST, "1") + M->CC2_CODMUN } ' ) )   // [04] Bloco de codigo de execu็ใo do gatilho
	
EndIf
oStruCC2:SetProperty('CC2_CODMUN'  ,MODEL_FIELD_OBRIGAT  , .T. )
   
oModel:= MpFormModel():New("FISA010",/*Pre-Validacao*/, bPosValid/*Pos-Validacao*/, /*Commit*/,/*Cancel*/)
oModel:AddFields("FISA010_CC2",Nil,oStruCC2,/*Pre-Validacao*/,/*Pos-Validacao*/,/*Carga*/)
oModel:SetDescription(IIf(cPaisLoc == "EUA", STR0010, STR0011)) ////"Registro de Ciudades" - "Registro de Municipios"
oModel:GetModel("FISA010_CC2"):SetDescription(STR0011) //"Registro de Municipios"
oModel:SetPrimaryKey({"CC2_EST","CC2_CODMUN"})    

//----------------------------------------------------------
//Se tabela F1T existe e o paํs ้ Brasil, serแ adicionada 
//----------------------------------------------------------
If lF1T
	oDetMun := FWFormStruct(1, "F1T")
	oModel:AddFields('FISA010_F1T', 'FISA010_CC2', oDetMun)
	oModel:SetRelation( 'FISA010_F1T', {{'F1T_FILIAL', 'xFilial("F1T")'}, {'F1T_UF', 'CC2_EST'}, {'F1T_CODMUN', 'CC2_CODMUN'}  }, F1T->( IndexKey(1)))
EndIF

oModel:InstallEvent("FA010COMMIT", /*cOwner*/, oCommit)

Return ( oModel )

//---------------------------------------
/*	Exibe browser de acordo com estrutura
@author  	Jefferson Tomaz
@version 	P10 R1.4
@build		7.00.101202A
@since 		31/03/2011
@return 		oView Objeto do View*/
//---------------------------------------

Static Function ViewDef()
	Local oModel	:= FwLoadModel("FISA010")
	Local oView		:= Nil
	Local oStruCC2	:= FwFormStruct(2,"CC2")
	Local oStruF1T	:= Nil
	Local lF1T		:= cPaisLoc == "BRA" .AND. AliasIndic("F1T")	
	
	oView := FwFormView():New()
	oView:SetModel(oModel)
	oView:AddField("FISA010_CC2", oStruCC2)
	
	//Verifica se a tabela F1T existe e se ้ Brasil, para criar estrutura da F1T e adicionar estrutura
	//Se tabela F1T existir entใo serใo criadas dias HorizontalBox, caso contrแrio serแ criada somente uma
	If lF1T
		oStruF1T	:= FwFormStruct(2,"F1T")
		oView:AddField('FISA010_F1T', oStruF1T)		
		oView:CreateHorizontalBox('SUPERIOR', 50)
		oView:CreateHorizontalBox('INFERIOR', 50)
		oStruF1T:RemoveField('F1T_UF')
		oStruF1T:RemoveField('F1T_CODMUN')
	Else
		oView:CreateHorizontalBox('SUPERIOR', 100)
	EndIF
	
	If cPaisLoc == "EUA"
		oView:SetFieldAction('CC2_CODMUN', { || FisVldCiud()} )
	EndIf

 	oView:SetOwnerView("FISA010_CC2","SUPERIOR")
	
	If lF1T
		//Adiciona a View
		oView:EnableTitleView('FISA010_CC2', "Dados Cadastrais")
		oView:EnableTitleView('FISA010_F1T'  , "Detalhamento do Cadastro de Municํpio")
		oView:SetOwnerView("FISA010_F1T","INFERIOR")
	EndIF		

Return ( oView )

//-------------------------------------
/*	MenuDef do Browser
@author  	Jefferson Tomaz
@version 	P10 R1.4
@build		7.00.101202A
@since 		31/03/2011
@return 		aRotina array com o MENUDEF*/
//-------------------------------------
Static Function MenuDef()
Local aRotina := {}

ADD OPTION aRotina TITLE STR0001  ACTION "PesqBrw"         OPERATION 1 ACCESS 0 // "Pesquisar"
ADD OPTION aRotina TITLE STR0002  ACTION "VIEWDEF.FISA010" OPERATION 2 ACCESS 0 // "Visualizar"
ADD OPTION aRotina TITLE STR0003  ACTION "VIEWDEF.FISA010" OPERATION 3 ACCESS 0 // "Incluir"
ADD OPTION aRotina TITLE STR0004  ACTION "VIEWDEF.FISA010" OPERATION 4 ACCESS 0 // "Alterar"
ADD OPTION aRotina TITLE STR0005  ACTION "VIEWDEF.FISA010" OPERATION 5 ACCESS 0 // "Excluir"

Return aRotina

//-----------------------------------------------------
/*	Valida em MVC a exclusใo do registro.
@author  	Jefferson Tomaz
@version 	P10 R1.4
@build		7.00.101202A
@since 		31/03/2011
@return 		lRetorno Valor l๓gico que indica o retorno*/
//------------------------------------------------------
Function Fis010Pos(oMdl) 
	Local lRetorno := .T.
	Local aAreaAtu   := GetArea()
	Local _aAlias    := {}
	Local nRecnoSM0  := SM0->(RecNo())
	Local aSM0CodFil := {}
	Local aCodFil    := {}
	Local cCpoFilial := ""
	Local cAliasTab  := ""
	Local nInd       := 0
	Local nYnd       := 0
	Local nOperation := 0
	Local lIntGFE    := SuperGetMv("MV_INTGFE",.F.,.F.)
	Local cIntGFE2   := SuperGetMv("MV_INTGFE2",.F.,"2")
	#IFNDEF TOP
		Local cArqInd    := ""
		Local nIndex     := 0
	#ENDIF
	
	nOperation := oMdl:GetOperation()
	
	If nOperation == 5 //Valida Exclusใo
	 
		SM0->(DBGoTop())
		SM0->(MSSEEK(cEmpAnt,.F.))
		SM0->( DbEval( {|| Aadd(aSM0CodFil,FWCodFil())},{||.T. },{|| FWGrpCompany() == cEmpAnt}  )   )// Preenche um array com as filiais
		SM0->(DBGoTo(nRecnoSM0))
		
		AADD( _aAlias,{"AC4","AC4_CODMUN","C4_EST"} )
		AADD( _aAlias,{"ACH","ACH_CODMUN","ACH_EST"} )
		AADD( _aAlias,{"SA3","A3_CODMUN","A3_EST"} )
		AADD( _aAlias,{"SA4","A4_COD_MUN","A4_EST"} )
		AADD( _aAlias,{"SCF","CF_CODMUN","CF_EST"} )
		AADD( _aAlias,{"SU2","U2_CODMUN","U2_EST"} )
		AADD( _aAlias,{"SU5","U5_CODMUN","U5_EST"} )
		AADD( _aAlias,{"SU7","U7_CODMUN","U7_EST"} )
		AADD( _aAlias,{"SUS","US_CODMUN","US_EST"} )
		AADD( _aAlias,{"SA1","A1_COD_MUN","A1_EST"} )
		AADD( _aAlias,{"SA1","A1_CODMUNC","A1_EST"} )
		AADD( _aAlias,{"SA1","A1_CODMUNE","A1_EST"} )
		
		If ExistBlock("FS010ADEL")
			aAliasAdic := ExecBlock("FS010ADEL", .F., .F.)
			If ValType(aAliasAdic) == "A"
				AEval(aAliasAdic,{|x| AAdd(_aAlias,x)})
			EndIf
		EndIf
		
		For nInd:=1 To Len(_aAlias)
			
			If !AliasIndic(_aAlias[nInd,1]) .Or. (_aAlias[nInd,1])->(FieldPos(_aAlias[nInd,2]))==0
				Loop
			EndIf
			
			cAliasTab:=_aAlias[nInd,1]
			cCpoFilial:=PrefixoCpo(cAliasTab)+"_FILIAL"
			lCompart  :=FWModeAccess(cAliasTab,3)=="C"
			
			#IFDEF TOP
				cAliasQry := GetNextAlias()
				cQuery    := ""
				
				cQuery += "SELECT COUNT(*) QTDBASE FROM " + RetSqlName( cAliasTab ) + " "
				cQuery += "WHERE "
				
				If lCompart
					cQuery += cCpoFilial+"='"+xFilial(cAliasTab)+"'"
				Else
					If LEN(aSM0CodFil)>0
						cQuery +=cCpoFilial+" Between '"+Space(Len(aSM0CodFil[1]))+"' And '"+aTail(aSM0CodFil)+"'"
					ELSE	                                                                                        
						cQuery +=cCpoFilial+" Between ' ' And '"+FWCodFil()+"'"
					ENDIF	
				EndIf
				
				cQuery +=" And "+_aAlias[nInd,3]+"='" + CC2->CC2_EST + "'"
				cQuery +=" And "+_aAlias[nInd,2]+"='" + CC2->CC2_CODMUN + "'"
				cQuery += "And D_E_L_E_T_=' '"
				
				cQuery := ChangeQuery( cQuery )
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry,.F.,.T. )
						
				If (cAliasQry)->QTDBASE > 0
					Help(" ",1,"NODELETA")
					lRetorno := .F.
				Endif
				
				(cAliasQry)->(dbCloseArea())
			#ELSE
				cAliasQry := cAliasTab
				cQuery    := ""
				
				If lCompart
					aCodFil:={ xFilial(cAliasTab) }
				Else
					aCodFil:=aSM0CodFil
				Endif
				
				For nYnd:=1 To Len(aCodFil)
					
					cQuery := cCpoFilial+"='"+aCodFil[nYnd]+"'"
					cQuery +=" .And. "+_aAlias[nInd,2]+"='" + CC2->CC2_CODMUN + "'"
					
					cArqInd   := CriaTrab(NIL,.F.)
					IndRegua(cAliasQry,cArqInd,cCpoFilial+"+"+_aAlias[nInd,2],,cQuery) //"Selecionando Registros ..."
					
					nIndex := RetIndex(cAliasTab)
					dbSetIndex(cArqInd+OrdBagExT())
					dbSetOrder(nIndex+1)
					dbGotop()
					
					If (cAliasQry)->(!Eof())
						Help(" ",1,"NODELETA")
						lRetorno := .F.
					Endif
					
					dbSelectArea(cAliasQry)
					dbClearFilter()
					RetIndex(cAliasTab)
					FErase(cArqInd+OrdBagExt())
					
					If !lRetorno
						Exit
					EndIf
				Next nYnd
			#ENDIF
		Next nInd
		
		If ExistBlock("FS010EXL")
			lRetorno := ExecBlock("FS010EXL",.F.,.F.)
		EndIf
	ElseIf cPaisLoc == "EUA" .And. (nOperation == 3 .OR. nOperation == 4)
			lRetorno := FisVldCiud()
	EndIf
	
	//Integra็ใo Protheus com SIGAGFE
	If lRetorno .And. lIntGFE == .T. .And. cIntGFE2 $ "1"
		If !InterGU7(nOperation)
			lRetorno := .F.
		EndIf
		
	EndIf
Return ( lRetorno )

//-----------------------------------------------------
/*/	Integra a tabela CC2(Protheus) com GU7(SIGAGFE) a cada registro novo
@author Felipe Machado de Oliveira
@version P11
@since 18/04/2013
/*/
//------------------------------------------------------
Static Function InterGU7(nOperation)
	Local aAreaGU7 := GU7->( GetArea() )
	Local lRet :=  .T.
	Local oModelGU7 := FWLoadModel("GFEA020")
	Local nTipoOpSetado
	Local cCodMunici := TMS120CDUF(M->CC2_EST,'1')+M->CC2_CODMUN
	Local cMsg := ""
	
	dbSelectArea("GU7")
	GU7->( dbSetOrder(1) )
	GU7->( dbSeek( xFilial("GU7")+cCodMunici ) )
	If !GU7->( Eof() ) .And. GU7->GU7_FILIAL == xFilial("GU7");
						 .And. AllTrim(GU7->GU7_NRCID) == AllTrim(cCodMunici)

		oModelGU7:SetOperation( MODEL_OPERATION_UPDATE )			
		nTipoOpSetado := MODEL_OPERATION_UPDATE
	Else	
		oModelGU7:SetOperation( MODEL_OPERATION_INSERT )
		nTipoOpSetado := MODEL_OPERATION_INSERT
	EndIf
	
	oModelGU7:Activate()
	
	If nOperation <> MODEL_OPERATION_DELETE
		oModelGU7:SetValue( "GFEA020_GU7", "GU7_NMCID" , SubStr(M->CC2_MUN,1,50) )
		
		If nTipoOpSetado == MODEL_OPERATION_UPDATE
			If nOperation == MODEL_OPERATION_INSERT
				oModelGU7:LoadValue( "GFEA020_GU7", "GU7_SIT", "1" )
			EndIf
		Else
			oModelGU7:SetValue( "GFEA020_GU7", "GU7_FILIAL", xFilial("CC2") )
			oModelGU7:SetValue( "GFEA020_GU7", "GU7_NRCID" , cCodMunici )
			oModelGU7:SetValue( "GFEA020_GU7", "GU7_CDUF"  , M->CC2_EST )
			oModelGU7:SetValue( "GFEA020_GU7", "GU7_CDPAIS", "105" )
		EndIf
	Else
		If nTipoOpSetado <> MODEL_OPERATION_INSERT
			oModelGU7:LoadValue( "GFEA020_GU7", "GU7_SIT", "2" )
		EndIf
	EndIf
	
	If nOperation != MODEL_OPERATION_DELETE .Or. nTipoOpSetado != MODEL_OPERATION_INSERT
		If oModelGU7:VldData()
			oModelGU7:CommitData()
		Else
			lRet := .F.
			cMsg := STR0009+CRLF+CRLF+oModelGU7:GetErrorMessage()[6]//"Inconsist๊ncia com o Frete Embarcador (SIGAGFE): "##
		EndIf
	EndIf
	
	oModelGU7:Deactivate()
	
	If !lRet
		Help( ,, STR0008,,cMsg, 1, 0 ) //"Aten็ใo"
	EndIf
	
	RestArea( aAreaGU7 )
	
Return lRet                 

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFISA010   บAutor  ณMicrosiga           บ Data ณ  06/05/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ                                                            บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Fis010Exl()
Local lRetorno := .T.
Local aAreaAtu:=GetArea()
Local _aAlias:={}
Local nRecnoSM0  := SM0->(RecNo())
Local aSM0CodFil:={},aCodFil
Local cCpoFilial
Local cAliasTab
Local nInd,nYnd

#IFNDEF TOP
	Local cArqInd    := ""
	Local nIndex     := 0
#ENDIF

SM0->(DBGoTop())
SM0->( DbEval( {|| Aadd(aSM0CodFil,FWCodFil())},{||.T. },{|| FWGrpCompany() == cEmpAnt}  )   )// Preenche um array com as filiais
SM0->(DBGoTo(nRecnoSM0))

AADD( _aAlias,{"AC4","AC4_CODMUN"} )
AADD( _aAlias,{"ACH","ACH_CODMUN"} )
AADD( _aAlias,{"SA3","A3_CODMUN"} )
AADD( _aAlias,{"SA4","A4_COD_MUN"} )
AADD( _aAlias,{"SCF","CF_CODMUN"} )
AADD( _aAlias,{"SU2","U2_CODMUN"} )
AADD( _aAlias,{"SU5","U5_CODMUN"} )
AADD( _aAlias,{"SU7","U7_CODMUN"} )
AADD( _aAlias,{"SUS","US_CODMUN"} )
AADD( _aAlias,{"SA1","A1_COD_MUN"} )
AADD( _aAlias,{"SA1","A1_CODMUNC"} )
AADD( _aAlias,{"SA1","A1_CODMUNE"} )

If ExistBlock("FS010ADEL")
	aAliasAdic := ExecBlock("FS010ADEL", .F., .F.)
	If ValType(aAliasAdic) == "A"
		AEval(aAliasAdic,{|x| AAdd(_aAlias,x)})
	EndIf
EndIf

For nInd:=1 To Len(_aAlias)
	
	If !AliasIndic(_aAlias[nInd,1]) .Or. (_aAlias[nInd,1])->(FieldPos(_aAlias[nInd,2]))==0
		Loop
	EndIf
	
	cAliasTab:=_aAlias[nInd,1]
	cCpoFilial:=PrefixoCpo(cAliasTab)+"_FILIAL"
	lCompart  :=FWModeAccess(cAliasTab,3)=="C"
	
	#IFDEF TOP
		cAliasQry := GetNextAlias()
		cQuery    := ""
		
		cQuery += "SELECT COUNT(*) QTDBASE FROM " + RetSqlName( cAliasTab ) + " "
		cQuery += "WHERE "
		
		If lCompart
			cQuery += cCpoFilial+"='"+xFilial(cAliasTab)+"'"
		Else
			cQuery +=cCpoFilial+" Between '"+Space(Len(aSM0CodFil[1]))+"' And '"+aTail(aSM0CodFil)+"'"
		EndIf
		
		cQuery +=" And "+_aAlias[nInd,2]+"='" + CC2->CC2_CODMUN + "'"
		cQuery += "And D_E_L_E_T_=' '"
		
		cQuery := ChangeQuery( cQuery )
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry,.F.,.T. )
		
		If (cAliasQry)->QTDBASE > 0
			Help(" ",1,"NODELETA")
			lRetorno := .F.
		Endif
		
		(cAliasQry)->(dbCloseArea())
	#ELSE
		cAliasQry := cAliasTab
		cQuery    := ""
		
		If lCompart
			aCodFil:={ xFilial(cAliasTab) }
		Else
			aCodFil:=aSM0CodFil
		Endif
		
		For nYnd:=1 To Len(aCodFil)
			
			cQuery := cCpoFilial+"='"+aCodFil[nYnd]+"'"
			cQuery +=" .And. "+_aAlias[nInd,2]+"='" + CC2->CC2_CODMUN + "'"
			
			cArqInd   := CriaTrab(NIL,.F.)
			IndRegua(cAliasQry,cArqInd,cCpoFilial+"+"+_aAlias[nInd,2],,cQuery) //"Selecionando Registros ..."
			
			nIndex := RetIndex(cAliasTab)
			dbSetIndex(cArqInd+OrdBagExT())
			dbSetOrder(nIndex+1)
			dbGotop()
			
			If (cAliasQry)->(!Eof())
				Help(" ",1,"NODELETA")
				lRetorno := .F.
			Endif
			
			dbSelectArea(cAliasQry)
			dbClearFilter()
			RetIndex(cAliasTab)
			FErase(cArqInd+OrdBagExt())
			
			If !lRetorno
				Exit
			EndIf
		Next nYnd
	#ENDIF
Next nInd

If ExistBlock("FS010EXL")
	lRetorno := ExecBlock("FS010EXL",.F.,.F.)
EndIf

Return (lRetorno)
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFis010Vld บAutor  ณMarcio Nunes   	 บ Data ณ  29/12/11   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Validacao se ja existe o estado mais codigo do municipio   บฑฑ
ฑฑบ          ณ para incluir o registro.                                   บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Fis010Vld()
Local lRet:=.T.     
Local aAreaCC2:=CC2->(GetArea())

CC2->(DbSetOrder(1))
If Inclui
	If ExistCpo("SX5","12"+M->CC2_EST) .And. CC2->(DbSeek(xfilial("CC2")+M->CC2_EST+M->CC2_CODMUN))
 			Help(" ", 1, "JAGRAVADO")
			lRet:=.F.
	EndIf
EndIf
RestArea(aAreaCC2)	
Return(lRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณIntegDef  บAutor  ณ Marcelo C. Coutinho  บ Data ณ  28/10/11   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Mensagem ฺnica						                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Mensagem ฺnica                                            	บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function IntegDef( cXML, nTypeTrans, cTypeMessage )
Local aRet := {}

//-- IntegDef transferida para fonte FISI010
aRet := FISI010( cXml, nTypeTrans, cTypeMessage)
Return aRet

//-------------------------------------------------------------------
/*/{Protheus.doc} Fisa010PDe
	
@author Erick Dias
@since 21/12/2012
/*/
//-------------------------------------------------------------------
Function Fisa010PDe()

Local lRet	:= .T.

IF M->CC2_PERMAT + M->CC2_PERSER >100
	Help("",1,"Help","",STR0007,1,0)		
	lRet	:= .F.
EndIf
Return(lRet) 

/*
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFisVldCiudบAutor  ณLuis Enrํquez         บ Data ณ 05/12/2018  บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Valida el que los dos primeros caracteres del contenido del  บฑฑ
ฑฑบ          ณ campo CC2_CODMUN sea igual al contenido del campo CC2_EST.   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ cExp1: Codigo de la Ciudad (CC2_CODMUN)                      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Campos CC2_CODMUN (EUA)                                      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function FisVldCiud()
	Local lRet	:= .F.

	If SubStr(AllTrim(M->CC2_CODMUN), 1, 2) == M->CC2_EST .And. Len(AllTrim(M->CC2_CODMUN)) > 2
		lRet := .T.
	Else
		Help(" ", 1, "ERRORCIUDAD")
		lRet := .F.
	EndIf
Return lRet

//-------------------------------------------------------------------
/*/ { Protheus.doc } FA010COMMIT
Classe interna implementando o FWModelEvent, para execu็ใo de fun็ใo
durante o commit.

@author fabiana.silva
@since 21/09/2020
/*/
//-------------------------------------------------------------------
Class FA010COMMIT FROM FWModelEvent
	Method New()
	Method InTTS()
End Class

Method New() Class FA010COMMIT
Return

Method InTTS(oSubModel, cModelId) Class FA010COMMIT
	If SuperGetMV("MV_JFSINC", .F., '2') == '1'
		JFILASINC(oSubModel:GetModel(), "CC2", "FISA010_CC2", "CC2_EST", "CC2_CODMUN")
	EndIf
Return