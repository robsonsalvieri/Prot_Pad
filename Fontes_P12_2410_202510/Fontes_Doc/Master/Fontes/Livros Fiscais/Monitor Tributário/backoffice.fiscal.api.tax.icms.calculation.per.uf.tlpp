#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"

Static oGetCreditDebitICMS := Nil

/*-------------------------------------------------------------
{Protheus.doc} GET MTCalculationUFICMSFilter
API de ICMS/ICMS ST/DIFAL por UF por parâmetros.
@author william.palma
@parâmetros de entrada do endpoint
@since 06/04/2025
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/icms/calculation-per-uf', description='ICMS/ICMS ST/DIFAL')
Function MTCalculationUFICMSFilter()
Local oJsResponse         as json
Local cBranches        := oRest:getQueryRequest()['branches'] as character
Local cPeriodStart     := oRest:getQueryRequest()['periodStart']  as character
Local cPeriodEnd       := oRest:getQueryRequest()['periodEnd']  as character

If ValType(cBranches) != 'U' .and. !Empty(cBranches) .and. ValType(cPeriodStart) != 'U' .and. !Empty(cPeriodStart) .and. ValType(cPeriodEnd) != 'U' .and. !Empty(cPeriodEnd)
    oJsResponse := CalculationUFICMSFilter(cBranches, cPeriodStart, cPeriodEnd)
ElseIf Empty(cBranches)
    oJsResponse := JsonObject():new()
    oJsResponse['code'] := 400
    oJsResponse['message'] := "Nenhuma Filial foi informada no parâmetro 'branches'."
    oJsResponse['detailedMessage'] := "Informar Filiais separadando por vírgula."
    oRest:setStatusCode(400)
ElseIf Empty(cPeriodStart)
    oJsResponse := JsonObject():new()
    oJsResponse['code'] := 400
    oJsResponse['message'] := "Nenhuma Data foi informada no parâmetro 'periodStart'."
    oJsResponse['detailedMessage'] := "Informar a data desejada para o período"
    oRest:setStatusCode(400)
Else
    oJsResponse := JsonObject():new()
    oJsResponse['code'] := 400
    oJsResponse['message'] := "Nenhuma Data foi informada no parâmetro 'periodEnd'."
    oJsResponse['detailedMessage'] := "Informar a data desejada para o período"
    oRest:setStatusCode(400)
Endif    
oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)
Return 


/*-------------------------------------------------------------------
{Protheus.doc} GET CalculationUFICMSFilter
Função responsável por preencher o Json com com informações referentes ao ICMS/ICMS ST/DIFAL
@author william.palma
@parâmetros de entrada do endpoint
@since 06/04/2025
-------------------------------------------------------------------*/
Static Function CalculationUFICMSFilter(cBranches as character, cPeriodStart as character, cPeriodEnd as character, cTipoMovICM)
Local nPos         := 0                         as numeric
Local nPageAux     := 0                         as numeric
Local nPageSize    := 28                        as numeric
Local lHasNext     := .f.                       as logical
Local nPage        := 1                         as numeric
Local oJsonResp    := JsonObject():New()        as object
Local cAlias       := getNextAlias()            as character
Local oJParams     := oRest:getQueryRequest()   as json //Json com os parâmetros (query param)    

If ValType(oJParams) == 'J'
    If ValType( oJParams['page'] ) != 'U' .and. Val(oJParams['page']) > 0
        nPage := Val(oJParams['page'])
    EndIf
    If ValType( oJParams['pageSize'] ) != 'U' .and. Val(oJParams['pageSize']) > 0
        nPageSize := Val(oJParams['pageSize'])
    EndIf
EndIf

GetCalculationUFICMSFilter(@cAlias, nPage, nPageSize, cBranches, cPeriodStart, cPeriodEnd)

// Iniciando o While
If (cAlias)->(!Eof())
    While (cAlias)->(!Eof())
        nPageAux++
        If nPageAux == 1
            oJsonResp["items"] := {}
        Endif
        If nPageAux <= nPageSize
            aAdd( oJsonResp["items"], JsonObject():New() )
            nPos++
            oJsonResp["items"][nPos]["ufCode"]        :=   (cAlias)->ESTADO
            oJsonResp["items"][nPos]["ufDescription"] :=   AllTrim((cAlias)->NOMEEST)
			
			oCalculation := JsonObject():New()
            If (cAlias)->CREDICM > 0
                oCalculation["credits"]        :=   (cAlias)->CREDICM
            EndIf
            If (cAlias)->DEBTICM > 0
                oCalculation["debits"]         :=   (cAlias)->DEBTICM
            EndIf
            If  (cAlias)->CREDICMST > 0   
                oCalculation["icmsStCredits"]  :=   (cAlias)->CREDICMST
            EndIf
            If (cAlias)->DEBTICMST > 0
                oCalculation["icmsStDebits"]   :=   (cAlias)->DEBTICMST
            EndIF
            If (cAlias)->DIFAL > 0
                oCalculation["difal"]          :=   (cAlias)->DIFAL
            EndIF

			oJsonResp["items"][nPos]["calculations"]  := oCalculation
        Else
            lHasNext := .t.
            Exit
        EndIf
    (cAlias)->(DbSkip())
    EndDo
oJsonResp["hasNext"]    := lHasNext
Else
    oJsonResp['code']       := 200
    oJsonResp["items"]      := {}
    oJsonResp["hasNext"]    := lHasNext
EndIf       
(cAlias)->(DBCloseArea())
Return oJsonResp:toJson()


/*--------------------------------------------------------------------
{Protheus.doc} GetCreditDebitICMSMSSQL()
(Responsável por executar a consulta aos campos da tabela CJZ)
@author william.palma
@since 06/04/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetCalculationUFICMSFilter(cAlias as character, nPage as numeric, nPageSize as numeric, cBranches as character, cPeriodStart as character, cPeriodEnd as character)
Local cQuery    := ''  as character

If oGetCreditDebitICMS == Nil
    cQuery := "SELECT CJZ.ESTADO AS ESTADO, CJZ.NOMEEST AS NOMEEST, SUM(CJZ.CREDICM) AS CREDICM, SUM(CJZ.DEBTICM) AS DEBTICM, "
    cQuery += "SUM(CJZ.CREDICMST) AS CREDICMST, SUM(CJZ.DEBTICMST) AS DEBTICMST, SUM(CJZ.DIFAL) AS DIFAL "
    cQuery += "FROM(SELECT "
    cQuery += "CJZ_ESTADO AS ESTADO, "
	cQuery += "CASE "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Acre' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Alagoas' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Amapá' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Amazonas' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Bahia' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Ceará' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Distrito Federal' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Espírito Santo' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Goiás' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Maranhão'  "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Mato Grosso' "
    cQuery += "WHEN CJZ_ESTADO = ? THEN 'Mato Grosso do Sul' "
    cQuery += "WHEN CJZ_ESTADO = ? THEN 'Minas Gerais' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Pará' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Paraíba' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Paraná' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Pernambuco' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Piauí' "
    cQuery += "WHEN CJZ_ESTADO = ? THEN 'Rio de Janeiro' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Rio Grande do Norte' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Rio Grande do Sul' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Rondônia' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Roraima' "
    cQuery += "WHEN CJZ_ESTADO = ? THEN 'Santa Catarina' "
    cQuery += "WHEN CJZ_ESTADO = ? THEN 'São Paulo' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Sergipe' "
	cQuery += "WHEN CJZ_ESTADO = ? THEN 'Tocantins' "
    cQuery += "WHEN CJZ_ESTADO = ? THEN 'Exterior' "
    cQuery += "ELSE ? END AS NOMEEST, "
    cQuery += "CASE WHEN CJZ_TIPOMO = ? THEN SUM(CJZ_VALICM) ELSE ? END AS CREDICM, "
    cQuery += "CASE WHEN CJZ_TIPOMO = ? THEN SUM(CJZ_VALICM) ELSE ? END AS DEBTICM, "
    cQuery += "CASE WHEN CJZ_TIPOMO = ? THEN SUM(CJZ_ICMSRE) ELSE ? END AS CREDICMST, "
    cQuery += "CASE WHEN CJZ_TIPOMO = ? THEN SUM(CJZ_ICMSRE) ELSE ? END AS DEBTICMST, "
    cQuery += "CASE WHEN CJZ_TIPOMO = ? THEN SUM(CJZ_DIFAL)  ELSE ? END AS DIFAL "
    cQuery += "FROM ? CJZ "
    cQuery += "WHERE CJZ_ENTRAD BETWEEN ? AND ? AND D_E_L_E_T_ = ? AND  CJZ_FILIAL IN (?) AND ((CJZ_VALICM > ?) OR (CJZ_DIFAL > ?) OR (CJZ_ICMSRE > ? AND CJZ_ESTADO LIKE (?))) "
    cQuery += "AND ((CJZ_VALICM > ?) OR (CJZ_ICMSRE > ? AND CJZ_ESTADO LIKE (?))) AND CJZ_TIPO != ? "
    cQuery += "GROUP BY CJZ_ESTADO, CJZ_TIPOMO) CJZ "
    cQuery += "GROUP BY CJZ.ESTADO, CJZ.NOMEEST "
    cQuery += "ORDER BY CJZ.ESTADO "
    cQuery += " OFFSET ( ? * ? ) ROWS "
    cQuery += " FETCH NEXT ? ROWS ONLY "
    oGetCreditDebitICMS := FwExecStatement():New()
    oGetCreditDebitICMS:SetQuery(ChangeQuery(cQuery))
Endif

If oGetCreditDebitICMS != Nil
    oGetCreditDebitICMS:setString(1, "AC")
    oGetCreditDebitICMS:setString(2, "AL")
    oGetCreditDebitICMS:setString(3, "AP")
    oGetCreditDebitICMS:setString(4, "AM")
    oGetCreditDebitICMS:setString(5, "BA")
    oGetCreditDebitICMS:setString(6, "CE")
    oGetCreditDebitICMS:setString(7, "DF")
    oGetCreditDebitICMS:setString(8, "ES")
    oGetCreditDebitICMS:setString(9, "GO")
    oGetCreditDebitICMS:setString(10,"MA")
    oGetCreditDebitICMS:setString(11,"MT")
    oGetCreditDebitICMS:setString(12,"MS")
    oGetCreditDebitICMS:setString(13,"MG")
    oGetCreditDebitICMS:setString(14,"PA")
    oGetCreditDebitICMS:setString(15,"PB")
    oGetCreditDebitICMS:setString(16,"PR")
    oGetCreditDebitICMS:setString(17,"PE")
    oGetCreditDebitICMS:setString(18,"PI")
    oGetCreditDebitICMS:setString(19,"RJ")
    oGetCreditDebitICMS:setString(20,"RN")
    oGetCreditDebitICMS:setString(21,"RS")
    oGetCreditDebitICMS:setString(22,"RO")
    oGetCreditDebitICMS:setString(23,"RR")
    oGetCreditDebitICMS:setString(24,"SC")
    oGetCreditDebitICMS:setString(25,"SP")
    oGetCreditDebitICMS:setString(26,"SE")
    oGetCreditDebitICMS:setString(27,"TO")
    oGetCreditDebitICMS:setString(28,"EX")
    oGetCreditDebitICMS:setString(29, "")
    oGetCreditDebitICMS:setString(30, "E")
    oGetCreditDebitICMS:SetNumeric(31, 0)
    oGetCreditDebitICMS:setString(32, "S")
    oGetCreditDebitICMS:SetNumeric(33, 0)
    oGetCreditDebitICMS:setString(34, "E")
    oGetCreditDebitICMS:SetNumeric(35, 0)
    oGetCreditDebitICMS:setString(36, "S")
    oGetCreditDebitICMS:SetNumeric(37, 0)
    oGetCreditDebitICMS:setString(38, "S")
    oGetCreditDebitICMS:SetNumeric(39, 0)
    oGetCreditDebitICMS:SetUnsafe(40, FIS319SqlName("CJZ"))
    oGetCreditDebitICMS:setString(41, StrTran(cPeriodStart, "-", ""))
    oGetCreditDebitICMS:setString(42, StrTran(cPeriodEnd, "-", ""))
    oGetCreditDebitICMS:setString(43, ' ')
    oGetCreditDebitICMS:SetIn(44, StrTokArr(cBranches, "," ))
    oGetCreditDebitICMS:SetNumeric(45, 0)
    oGetCreditDebitICMS:SetNumeric(46, 0)
    oGetCreditDebitICMS:SetNumeric(47, 0)
    oGetCreditDebitICMS:setString(48, GetSubtrib())
    oGetCreditDebitICMS:SetNumeric(49, 0)
    oGetCreditDebitICMS:SetNumeric(50, 0)
    oGetCreditDebitICMS:setString(51, GetSubtrib())
    oGetCreditDebitICMS:setString(52, "S")
    oGetCreditDebitICMS:SetUnsafe(53, cValToChar(nPage-1))
    oGetCreditDebitICMS:SetUnsafe(54, cValToChar(nPageSize))
    oGetCreditDebitICMS:SetUnsafe(55, cValToChar(nPageSize+1))
    oGetCreditDebitICMS:CBASEQUERY := oGetCreditDebitICMS:GETFIXQUERY()
    cAlias := oGetCreditDebitICMS:OpenAlias()
Endif
FWFreeObj(oGetCreditDebitICMS)
Return 
