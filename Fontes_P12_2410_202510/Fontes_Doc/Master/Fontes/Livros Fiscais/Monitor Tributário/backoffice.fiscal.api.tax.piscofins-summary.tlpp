#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"
#INCLUDE "backoffice.fiscal.api.tax.pis.cofins.summary.ch"
/*-------------------------------------------------------------
{Protheus.doc} GET MTPisCofinsRetention
API de Informações Resumidas da Retenção de PIS/COFINS
Obtém informações resumidas de PIS/COFINS no período selecionado, referentes a:
RETENÇÃO DO PIS (ENTRADA E SAÍDA)
RETENÇÃO DO COFINS (ENTRADA E SAÍDA)

@author Gabriela Luche
@parâmetros de entrada do endpoint
@since 31/03/2025
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/pis-cofins/summary/retention', description='API de Informações Resumidas da Retenção de PIS/COFINS')
Function MTPisCofinsRetention() 
      
Local oJsonResp         as json
Local cBranches        := oRest:getQueryRequest()['branches'] as character
Local cPeriodStart     := oRest:getQueryRequest()['periodStart']  as character
Local cPeriodEnd       := oRest:getQueryRequest()['periodEnd']  as character

If !Empty(cBranches) .and. !Empty(cPeriodStart) .and. !Empty(cPeriodEnd)
    oJsonResp := PisCofRetFilter(cBranches, cPeriodStart, cPeriodEnd)
Endif    

oRest:setResponse(oJsonResp) 
FWFreeObj(oJsonResp)

Return 
/*-------------------------------------------------------------
{Protheus.doc} GET MTPisCofinsCalc
API de valores de Informações Resumidas da Apuração de PIS/COFINS
Obtém informações resumidas da Apuração de PIS/COFINS no período selecionado referentes a:
VALORES OPERACIONAIS (ENTRADA E SAÍDA)
TOTAL DO PIS (CRÉDITOS E DÉBITOS)
TOTAL DO COFINS (CRÉDITOS E DÉBITOS)

@author Gabriela Luche
@parâmetros de entrada do endpoint
@since 31/03/2025
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/pis-cofins/summary/calculation', description='API de Informações Resumidas da Apuração de PIS/COFINS')
Function MTPisCofinsCalc() 
      
Local oJsonResp         as json
Local cBranches        := oRest:getQueryRequest()['branches'] as character
Local cPeriodStart     := oRest:getQueryRequest()['periodStart']  as character
Local cPeriodEnd       := oRest:getQueryRequest()['periodEnd']  as character

If !Empty(cBranches) .and. !Empty(cPeriodStart) .and. !Empty(cPeriodEnd)
    oJsonResp := PisCofCalcFilter(cBranches, cPeriodStart, cPeriodEnd)
Endif    

oRest:setResponse(oJsonResp) 
FWFreeObj(oJsonResp)

Return 

/*-------------------------------------------------------------------
{Protheus.doc} GET PisCofRetFilter
Função responsável por preencher o Json com os valores de retenção de Pis e Cofins por operação 
@author Gabriela Luche
@parâmetros de entrada do endpoint
@since 31/03/2025
-------------------------------------------------------------------*/
Static Function PisCofRetFilter(cBranches as character, cPeriodStart as character, cPeriodEnd as character)
Local oJsonResp    := JsonObject():New()   as object
Local cAlias       := getNextAlias()       as character
//--------------------------------
// Executa a query que retorna os dados para a API
//--------------------------------	 
GetPisCofRetQry(@cAlias,cBranches,cPeriodStart,cPeriodEnd)

If (cAlias)->(!Eof()) .and. (cAlias)->(RET_PIS_ENT+RET_PIS_SAI+RET_COF_ENT+RET_COF_SAI) > 0
    //--------------------------------	
    // Inicia o objeto Json
    //--------------------------------	 
    oJsonResp["pis"] := JsonObject():New()
    //--------------------------------
    // Alimenta o json com os dados retornado pela query
    //--------------------------------	     
    oJsonResp["pis"]["withheldAtEntry"]         :=   (cAlias)->RET_PIS_ENT   //Retenção Pis (Entrada)  
    oJsonResp["pis"]["withheldAtExit"]          :=   (cAlias)->RET_PIS_SAI  //Retenção Pis (Saída)

    oJsonResp["cofins"] := JsonObject():New()
    oJsonResp["cofins"]["withheldAtEntry"]      :=   (cAlias)->RET_COF_ENT  //Retenção Cofins (Entrada)  
    oJsonResp["cofins"]["withheldAtExit"]       :=   (cAlias)->RET_COF_SAI  //Retenção Cofins (Saída)
Else
    oJsonResp['code']           := 400
    oJsonResp['detailMessage']  := STR0001 //"Value not found"
    oJsonResp['message']        := STR0002 //"Dados não encontrados para o período e/ou filial informados."
EndIf    
//--------------------------------
// Fecha o alias
//--------------------------------	
(cAlias)->(DBCloseArea())
Return oJsonResp:toJson()
/*-------------------------------------------------------------------
{Protheus.doc} GET PisCofCalcFilter
Função responsável por preencher o Json com os valores calculados de Pis e Cofins por operação 
@author Gabriela Luche
@parâmetros de entrada do endpoint
@since 31/03/2025
-------------------------------------------------------------------*/
Static Function PisCofCalcFilter(cBranches as character, cPeriodStart as character, cPeriodEnd as character)
Local oJsonResp         := JsonObject():New()        as object
Local cAlias            := getNextAlias()            as character
//--------------------------------
// Executa a query que retorna os dados para a API
//--------------------------------	 
GetPisCofinsCalcQry(@cAlias,cBranches,cPeriodStart,cPeriodEnd)
//--------------------------------
// Alimenta o json com os dados retornado pela query
//--------------------------------	
If (cAlias)->(!Eof()) .and. (cAlias)->(OP_ENT+OP_SAI+TOT_PIS_CRD+TOT_PIS_DEB+TOT_COF_CRD+TOT_COF_DEB ) > 0
    //-------------------------------
    // Inicia o objeto Json
    //--------------------------------	  
    oJsonResp["operationalValue"] :=  JsonObject():New() 
    //--------------------------------
    // Alimenta o json com os dados retornado pela query
    //--------------------------------	        
    oJsonResp["operationalValue"]["entries"]    :=   (cAlias)->OP_ENT  //Valor Total Pis (Entrada)    
    oJsonResp["operationalValue"]["exits"]      :=   (cAlias)->OP_SAI  //Valor Total Pis (Saída)   
    
    oJsonResp["pisTotal"] := JsonObject():New()    
    oJsonResp["pisTotal"]["credits"]            :=   (cAlias)->TOT_PIS_CRD  //Valor Total Pis (Créditos)   
    oJsonResp["pisTotal"]["debts"]              :=   (cAlias)->TOT_PIS_DEB  //Valor Total Pis (Créditos)
    
    oJsonResp["cofinsTotal"] := JsonObject():New()  
    oJsonResp["cofinsTotal"]["credits"]         :=   (cAlias)->TOT_COF_CRD  //Valor Total Cofins (Créditos)
    oJsonResp["cofinsTotal"]["debts"]           :=   (cAlias)->TOT_COF_DEB  //Valor Total Cofins (Débitos)    
Else
    oJsonResp['code'] := 400
    oJsonResp['detailMessage']  := STR0001 //"Value not found"
    oJsonResp['message']        := STR0002 //"Dados não encontrados para o período e/ou filial informados."
EndIf    
//--------------------------------
// Fecha o alias
//--------------------------------	  
(cAlias)->(DBCloseArea())

Return oJsonResp:toJson()
/*--------------------------------------------------------------------
{Protheus.doc} GetPisCofRetQry()
Função responsável por preencher o Alias com as informações referentes aos valores de retenção de Pis e Cofins por operação
@author Gabriela Luche
@parâmetros de entrada do endpoint: cAlias, cBranches, cPeriodStart, cPeriodEnd
@since 31/03/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetPisCofRetQry(cAlias as character, cBranches as character, cPeriodStart as character, cPeriodEnd as character)
Local cQryCk0       as character
Local oGetPisCofRet as object
//--------------------------------
// Montagem da query com os dados de retenção de Pis e Cofins por operação
//--------------------------------
cQryCk0 := "SELECT "
cQryCk0 += "ISNULL(SUM(CASE WHEN CK0_TIPOMO = ? THEN CK0_VRETPI ELSE ? END), 0) AS RET_PIS_ENT, "
cQryCk0 += "ISNULL(SUM(CASE WHEN CK0_TIPOMO = ? THEN CK0_VRETCO ELSE ? END), 0) AS RET_COF_ENT, "
cQryCk0 += "ISNULL(SUM(CASE WHEN CK0_TIPOMO = ? THEN CK0_VRETPI ELSE ? END), 0) AS RET_PIS_SAI, "
cQryCk0 += "ISNULL(SUM(CASE WHEN CK0_TIPOMO = ? THEN CK0_VRETCO ELSE ? END), 0) AS RET_COF_SAI "
cQryCk0 += "FROM "+FIS319SqlName("CK0")+" CK0 " 
cQryCk0 += "WHERE "
cQryCk0 += "CK0_FILIAL IN ( ? ) "
cQryCk0 += "AND CK0_ENTRAD BETWEEN ? AND ? "
cQryCk0 += "AND CK0.D_E_L_E_T_ = ? "
cQryCk0 += "AND ( CK0_VRETPI > ? OR CK0_VRETCO > ?) "    
//--------------------------------
// Realiza o cache da query
//--------------------------------    
oGetPisCofRet := FwExecStatement():New()
oGetPisCofRet:SetQuery(ChangeQuery(cQryCk0))
//--------------------------------
// Montagem dos binds
//--------------------------------
oGetPisCofRet:setString(1, "E")
oGetPisCofRet:setNumeric(2, 0)
oGetPisCofRet:setString(3, "E")
oGetPisCofRet:setNumeric(4, 0)
oGetPisCofRet:setString(5, "S")
oGetPisCofRet:setNumeric(6, 0)
oGetPisCofRet:setString(7, "S")
oGetPisCofRet:setNumeric(8, 0)
oGetPisCofRet:SetIn(9, StrTokArr(cBranches, "," ))
oGetPisCofRet:setString(10, StrTran(cPeriodStart, "-", ""))
oGetPisCofRet:setString(11, StrTran(cPeriodEnd, "-", ""))
oGetPisCofRet:setString(12, " ")
oGetPisCofRet:setNumeric(13, 0)
oGetPisCofRet:setNumeric(14, 0)
oGetPisCofRet:CBASEQUERY := oGetPisCofRet:GETFIXQUERY()
cAlias := oGetPisCofRet:OpenAlias()
//--------------------------------
// Elimina da memória a instância do objeto
//--------------------------------
FWFreeObj(oGetPisCofRet)

Return
/*--------------------------------------------------------------------
{Protheus.doc} GetPisCofinsCalcQry()
Função responsável por preencher o Alias com as informações referentes aos valores de Pis e Cofins por operação
@author Gabriela Luche
@parâmetros de entrada do endpoint: cAlias, cBranches, cPeriodStart, cPeriodEnd
@since 31/03/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetPisCofinsCalcQry(cAlias as character, cBranches as character, cPeriodStart as character, cPeriodEnd as character)
Local cQryCk0           as character
Local oGetPisCofVal     as object
//--------------------------------
// Montagem da query com os valores de Pis e Cofins por operação
//--------------------------------
cQryCk0 := "SELECT "
cQryCk0 += "ISNULL(SUM(CASE WHEN CK0_TIPOMO = ? THEN CK0_VALCON ELSE ? END), 0) AS OP_ENT, "
cQryCk0 += "ISNULL(SUM(CASE WHEN CK0_TIPOMO = ? THEN CK0_VALCON ELSE ? END), 0) AS OP_SAI, "   
cQryCk0 += "ISNULL(SUM(CASE WHEN CK0_TIPOMO = ? THEN CK0_VALPIS ELSE ? END), 0) AS TOT_PIS_CRD, "
cQryCk0 += "ISNULL(SUM(CASE WHEN CK0_TIPOMO = ? THEN CK0_VALPIS ELSE ? END), 0) AS TOT_PIS_DEB, "
cQryCk0 += "ISNULL(SUM(CASE WHEN CK0_TIPOMO = ? THEN CK0_VALCOF ELSE ? END), 0) AS TOT_COF_CRD, "
cQryCk0 += "ISNULL(SUM(CASE WHEN CK0_TIPOMO = ? THEN CK0_VALCOF ELSE ? END), 0) AS TOT_COF_DEB "
cQryCk0 += "FROM "+FIS319SqlName("CK0")+" CK0 " 
cQryCk0 += "WHERE "
cQryCk0 += "CK0_FILIAL IN ( ? ) " 
cQryCk0 += "AND CK0_ENTRAD BETWEEN ? AND ? "
cQryCk0 += "AND CK0.D_E_L_E_T_ = ? "
cQryCk0 += "AND ( CK0_VALPIS > ? OR CK0_VALCOF > ? ) "    
//--------------------------------
// Realiza o cache da query
//--------------------------------    
oGetPisCofVal := FwExecStatement():New()
oGetPisCofVal:SetQuery(ChangeQuery(cQryCk0))

//--------------------------------
// Montagem dos binds
//--------------------------------
oGetPisCofVal:setString(1, "E")
oGetPisCofVal:setNumeric(2, 0)
oGetPisCofVal:setString(3, "S")
oGetPisCofVal:setNumeric(4, 0)
oGetPisCofVal:setString(5, "E")
oGetPisCofVal:setNumeric(6, 0)
oGetPisCofVal:setString(7, "S")
oGetPisCofVal:setNumeric(8, 0)
oGetPisCofVal:setString(9, "E")
oGetPisCofVal:setNumeric(10, 0)
oGetPisCofVal:setString(11, "S")
oGetPisCofVal:setNumeric(12, 0)
oGetPisCofVal:SetIn(13, StrTokArr(cBranches, "," ))
oGetPisCofVal:setString(14, StrTran(cPeriodStart, "-", ""))
oGetPisCofVal:setString(15, StrTran(cPeriodEnd, "-", ""))
oGetPisCofVal:setString(16, " ")
oGetPisCofVal:setNumeric(17, 0)
oGetPisCofVal:setNumeric(18, 0)
oGetPisCofVal:CBASEQUERY := oGetPisCofVal:GETFIXQUERY()
cAlias := oGetPisCofVal:OpenAlias()
//--------------------------------
// Elimina da memória a instância do objeto
//--------------------------------
FWFreeObj(oGetPisCofVal)    

Return
