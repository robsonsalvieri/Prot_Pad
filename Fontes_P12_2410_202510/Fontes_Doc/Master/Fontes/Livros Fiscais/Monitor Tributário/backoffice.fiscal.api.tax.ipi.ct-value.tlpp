#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"

Static oGetIPI := Nil

/*-------------------------------------------------------------
API de Valor de IPI por Código de Tributação.
@author eduardo.cirqueira
@parâmetros de entrada do endpoint
cbranches = filiais a serem consideradas (ex.: 'XIFIS26, DM1MG') )
cperiodStart = data inicial do período a ser filtrado
cperiodStart = data final do período a ser filtrado
@since 02/01/2025
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/ipi/cst-operation', description='API de Operação por CT do IPI')
Function MTValCTIPI()
Local oJsResponse            as json
Local cbranches        := oRest:getQueryRequest()['branches'] as character
Local cperiodStart     := oRest:getQueryRequest()['periodStart']  as character
Local cperiodEnd       := oRest:getQueryRequest()['periodEnd']  as character

//Todos os parâmetros são obrigatórios
if valtype(cbranches) != 'U' .and. !empty(cbranches) .and. valtype(cperiodStart) != 'U' .and. !empty(cperiodStart) .and. valtype(cperiodEnd) != 'U' .and. !empty(cperiodEnd)
   oJsResponse := CTIPIFilter(1, cbranches, cperiodStart, cperiodEnd)
endif    
oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)
Return 

/*-------------------------------------------------------------
API de Valor por Benefício Fiscal de IPI.
@author eduardo.cirqueira
@parâmetros de entrada do endpoint
cbranches = filiais a serem consideradas (ex.: 'XIFIS26, DM1MG') )
cperiodStart = data inicial do período a ser filtrado
cperiodStart = data final do período a ser filtrado
@since 02/01/2025
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/ipi/current-tax-benefits', description='API de Benefícios Fiscais Atuais de IPI')
Function MTValBenefIPI()
Local oJsResponse            as json
Local cbranches        := oRest:getQueryRequest()['branches'] as character
Local cperiodStart     := oRest:getQueryRequest()['periodStart']  as character
Local cperiodEnd       := oRest:getQueryRequest()['periodEnd']  as character

//Todos os parâmetros são obrigatórios
if valtype(cbranches) != 'U' .and. !empty(cbranches) .and. valtype(cperiodStart) != 'U' .and. !empty(cperiodStart) .and. valtype(cperiodEnd) != 'U' .and. !empty(cperiodEnd)
   oJsResponse := CTIPIFilter(2, cbranches, cperiodStart, cperiodEnd)
endif    
oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)
Return 

/*-------------------------------------------------------------------
{Protheus.doc} GET CTIPIFilter
Função responsável por preencher o Json com com informações referentes a Operações por CT do IPI
@author eduardo.cirqueira
@parâmetros de entrada
nCard = 1-Card Valor por CST; 2-Card Beneficios Fiscais do IPI; 
cbranches = filiais a serem consideradas (ex.: 'XIFIS26, DM1MG') )
cperiodStart = data inicial do período a ser filtrado
cperiodStart = data final do período a ser filtrado
@since 02/01/2025
-------------------------------------------------------------------*/
Static Function CTIPIFilter(nCard as numeric, cbranches as character, cperiodStart as character, cperiodEnd as character)
Local nPos         := 0                         as numeric
Local lHasNext     := .f.                       as logical
Local oJsonResp    := JsonObject():New()        as object
Local cAlias       := getNextAlias()            as character
Local aRetOrig     := {}                        as array

GetValCTIPI(nCard,@cAlias,cbranches,cperiodStart,cperiodEnd)

// Iniciando o While
If (cAlias)->(!Eof())
    While (cAlias)->(!Eof())
        
        if nPos == 0
            oJsonResp["items"] := {}
        endif
        
        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++
        oJsonResp["items"][nPos]["code"]         := AllTrim((cAlias)->CTIPI)
        oJsonResp["items"][nPos]["description"]  := AllTrim((cAlias)->DESCRI)
        oJsonResp["items"][nPos]["value"]        := (cAlias)->TOTAL_OPER
        
        (cAlias)->(DbSkip())
    EndDo
Else
    oJsonResp['code'] := 400
    oJsonResp['detailMessage'] := 'value not found'
    oJsonResp['message'] := "Dados não encontrados para o período e/ou filial informados."
EndIf

oJsonResp["hasNext"]      := lHasNext
oJsonResp["nextPageHref"] := ""

(cAlias)->(DBCloseArea())

aSize(aRetOrig,0)
Return oJsonResp:toJson()



/*---------------------------------------------------------------------
{Protheus.doc} GetValCTIPI()
(Responsável por executar a consulta aos campos da tabela CJX)
@author eduardo.cirqueira
@parâmetros de entrada
nCard = 1-Card Valor por CST; 2-Card Beneficios Fiscais do IPI
cAlias = tabela temporaria para a query
cbranches = filiais a serem consideradas (ex.: 'XIFIS26, DM1MG') )
cperiodStart = data inicial do período a ser filtrado.
cperiodStart = data final do período a ser filtrado.
@since 02/01/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetValCTIPI(nCard as numeric, cAlias as character, cbranches as character, cperiodStart as character, cperiodEnd as character)
Local cQuery    := ''  as character
Local cCampoSUM := 'CJX.CJX_VALIPI'  as character
Local cCTBenef  := ''  as character
Local nOrdBind  := 0 as numeric

If nCard == 2 
   cCTBenef := "01,02,03,04,05,51,52,53,54,55" // Códigos de Tributação do IPI referentes a Benefícios
   nOrdBind := 1
   cCampoSUM := 'CJX.CJX_VALCON'
EndIf

If oGetIPI == Nil
    cQuery := "SELECT distinct RTRIM(LTRIM(X5.X5_CHAVE)) as CTIPI"
    cQuery +=     " , RTRIM(LTRIM(X5.X5_DESCRI)) as DESCRI
    cQuery +=     " , SUM(?) AS TOTAL_OPER"
    cQuery +=  " FROM " + FIS319SqlName('CJX') + " CJX "
    cQuery +=  " INNER JOIN " + RetSQLName('SX5') + " X5 "
    cQuery +=          " ON X5_FILIAL = '" + xFilial("SX5") + "' "
    cQuery +=          " AND X5_TABELA = ? "
    If nCard == 2
       cQuery +=       " AND X5_CHAVE IN (?) "
    EndIf
    cQuery +=          " AND X5_CHAVE = CJX.CJX_CTIPI "
	cQuery +=          " AND X5.D_E_L_E_T_ = ? "
    cQuery += " WHERE CJX.CJX_FILIAL IN (?) "
    cQuery +=   " AND CJX.CJX_ENTRAD BETWEEN ? AND ? "
    cQuery +=   " AND ? > ?"
    cQuery +=   " AND CJX.D_E_L_E_T_ = ? "
    cQuery += " GROUP BY X5.X5_CHAVE, X5.X5_DESCRI"
    cQuery += " ORDER BY CTIPI"
    oGetIPI := FWExecStatement():New()
    oGetIPI:SetQuery(ChangeQuery(cQuery))
Endif

If oGetIPI != Nil
    oGetIPI:SetUnSafe(1, cCampoSUM)
    oGetIPI:SetString(2, 'S3')
    If nCard == 2 
        oGetIPI:SetIn(3, StrTokArr(cCTBenef, "," ))
    EndIf
    oGetIPI:SetString(3+nOrdBind, " ")
    oGetIPI:SetIn(4+nOrdBind, StrTokArr(cbranches, "," ))
    oGetIPI:SetString(5+nOrdBind, StrTran(cperiodStart, "-", ""))
    oGetIPI:SetString(6+nOrdBind, StrTran(cperiodEnd, "-", ""))
    oGetIPI:SetUnSafe(7+nOrdBind, cCampoSUM)
    oGetIPI:SetNumeric(8+nOrdBind, 0)
    oGetIPI:SetString(9+nOrdBind, " ")
    cAlias := oGetIPI:OpenAlias()
Endif

FWFreeObj(oGetIPI)
Return 

