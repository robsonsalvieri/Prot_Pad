#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"
#INCLUDE "BACKOFFICE.FISCAL.API.TAX.ICMS.SUMMARY.ch"

/*-------------------------------------------------------------
{Protheus.doc} GET MTApurIcmsFilterSummary
API com retorno de resumo de apuração de ICMS. 

- Valor Operacional
 -- Entradas (entries)
 -- Saidas   (exits)

- Total do Imposto
 -- Creditos (credits)
 -- Debitos  (debts)

@author Evandro Italo
@parametros de entrada do endpoint
    branches: Filiais
    periodStart: Data Inicial
    periodEnd: Data Final

@since 02/04/2025
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/icms/summary', description='Informações resumidas de ICMS no período selecionado') // STR0001 - Informações resumidas de ICMS no período selecionado
Function MTApurIcmsFilterSummary()
Local oJsResponse         as json
Local cBranches        := oRest:getQueryRequest()['branches'] as character
Local cPeriodStart     := oRest:getQueryRequest()['periodStart']  as character
Local cPeriodEnd       := oRest:getQueryRequest()['periodEnd']  as character

If !empty(cBranches) .and. !empty(cPeriodStart) .and. !empty(cPeriodEnd)
    oJsResponse := ApurIcmsFilterSummary(cBranches, cPeriodStart, cPeriodEnd)
Endif    

oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)

Return 

/*-------------------------------------------------------------------
{Protheus.doc} GET ApurIcmsFilterSummary

Função responsável por receber o parametros de entrada(filiais, 
Data Inicial e Data Final) e retornar o resumo de apuração de ICMS.
A função retorna um Json com os seguintes campos:

Json:

{
  "operationalValue": {
    "entries": 0,
    "exits": 0
  },
  "taxTotal": {
    "credits": 0,
    "debts": 0
  }
}

@author Evandro Italo
@parametros de entrada da função
    Filiais         as character  // Filiais
    Data Inicial    as character  // Data Inicial
    Data Final      as character  // Data Final

@since 18/03/2025
-------------------------------------------------------------------*/
Static Function ApurIcmsFilterSummary(cBranches as character, cPeriodStart as character, cPeriodEnd as character)

Local cAlias       := getNextAlias()            as character
Local cQuery       := ''                        as character
Local oJsonResp    := JsonObject():New()        as object
Local oGetApurIcms  := JsonObject():New()        as object

cQuery := " SELECT	ISNULL(SUM(CASE WHEN CJZ_TIPOMO = ? AND CJZ_VALICM > ? THEN ISNULL(CJZ_VALCON,0) ELSE ? END ),0) ENTRIES, "
cQuery += " 		    ISNULL(SUM(CASE WHEN CJZ_TIPOMO = ? AND CJZ_VALICM > ? THEN ISNULL(CJZ_VALCON,0) ELSE ? END),0)EXITS, "
cQuery += " 		    ISNULL(SUM(CASE WHEN CJZ_TIPOMO = ? AND CJZ_VALICM > ? THEN ISNULL(CJZ_VALICM,0) ELSE ? END),0)CREDITS, "
cQuery += " 		    ISNULL(SUM(CASE WHEN CJZ_TIPOMO = ? AND CJZ_VALICM > ? THEN ISNULL(CJZ_VALICM,0) ELSE ? END),0)DEBTS "
cQuery += " FROM ? "
cQuery += " WHERE D_E_L_E_T_ = ? "
cQuery += " 	  AND CJZ_FILIAL IN (?) "
cQuery += " 	  AND CJZ_ENTRAD BETWEEN ? AND ? "
cQuery += "     AND CJZ_TIPO != ? "

oGetApurIcms := FwExecStatement():New()
oGetApurIcms:SetQuery(ChangeQuery(cQuery))

oGetApurIcms:setString(1, 'E')
oGetApurIcms:SetNumeric(2, 0)
oGetApurIcms:setNumeric(3, 0)
oGetApurIcms:setString(4, 'S')
oGetApurIcms:SetNumeric(5, 0)
oGetApurIcms:SetNumeric(6, 0)
oGetApurIcms:setString(7, 'E')
oGetApurIcms:setNumeric(8, 0)
oGetApurIcms:setNumeric(9, 0)
oGetApurIcms:setString(10, 'S')
oGetApurIcms:SetNumeric(11,0)
oGetApurIcms:SetNumeric(12,0)
oGetApurIcms:SetUnsafe(13, FIS319SqlName("CJZ"))
oGetApurIcms:setString(14, Space(01))
oGetApurIcms:SetIn(15, StrTokArr(cBranches, "," ))
oGetApurIcms:setString(16, StrTran(cPeriodStart, "-", ""))
oGetApurIcms:setString(17, StrTran(cPeriodEnd, "-", ""))
oGetApurIcms:setString(18, 'S')
oGetApurIcms:CBASEQUERY := oGetApurIcms:GETFIXQUERY()
cAlias := oGetApurIcms:OpenAlias()

// Iniciando o While
If (cAlias)->(!Eof()) .AND. ((cAlias)->ENTRIES > 0 .OR. (cAlias)->EXITS > 0 .OR. (cAlias)->CREDITS > 0 .OR. (cAlias)->DEBTS > 0)
    oJsonResp["operationalValue"]            :=  JsonObject(): New()
    oJsonResp["operationalValue"]["entries"] := (cAlias)->ENTRIES
    oJsonResp["operationalValue"]["exits" ]  := (cAlias)->EXITS
    oJsonResp["taxTotal"        ]            :=  JsonObject(): New()
    oJsonResp["taxTotal"        ]["credits"] := (cAlias)->CREDITS
    oJsonResp["taxTotal"        ]["debts" ]  := (cAlias)->DEBTS    
Else
    oJsonResp['code'] := 400
    oJsonResp['detailMessage'] := STR0002 //'Valor não encontrado'
    oJsonResp['message'] := STR0003 //'Dados não encontrados para o perí­odo e/ou filial informados.'
EndIf       
(cAlias)->(DBCloseArea())

FWFreeObj(oGetApurIcms)

Return oJsonResp
