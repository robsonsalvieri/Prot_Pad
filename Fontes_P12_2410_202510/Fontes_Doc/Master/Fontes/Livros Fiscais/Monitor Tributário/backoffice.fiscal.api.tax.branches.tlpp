#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "protheus.ch"

/*/{Protheus.doc} MNTBGetBranches
Função responsável por retornar a consulta de todos as filiais

@type function
@author Caique Carlos
@since 12/02/2025
@version 1.0
@return Nil, Não há retorno 
/*/
@GET(endpoint="/api/fiscal/tax-monitor/v1/branches", description="Realiza a consulta de todas as filiais")
Function MNTBGetBranches()

	Local jQueryParams  := oRest:GetQueryRequest()      as json
	Local jResponse     := GetResponse(jQueryParams)    as json

	oRest:SetKeyHeaderResponse("Content-Type", "application/json")
	oRest:SetStatusCode(200)
	oRest:SetResponse(jResponse)

	FWFreeObj(jQueryParams)
	FWFreeObj(jResponse)

Return

/*/{Protheus.doc} GetResponse
Função responsável por retornar o JSON de resposta da consulta de filiais

@type static function
@author Caique Carlos
@since 13/02/2025
@version 1.0
@param jQueryParams, json, Objeto JSON contendo os query params
@return json, Retorna um objeto JSON com as filiais
/*/
Static Function GetResponse(jQueryParams as json) as json

	Local aItems	:= {}	as array
	Local nPage     := 0	as integer
	Local nPageSize := 0	as integer
	Local lHasNext  := .F.	as logical
	Local jResponse := Nil  as json

	Default jQueryParams := Nil

	nPage 		:= 1
	nPageSize 	:= 30

	If jQueryParams != Nil
		If jQueryParams:HasProperty("page")
			nPage := Val(jQueryParams["page"])
		EndIf
		If jQueryParams:HasProperty("pageSize")
			nPageSize := Val(jQueryParams["pageSize"])
		EndIf
	EndIf

	aItems 	  := GetBranches(nPage, nPageSize, @lHasNext)
	jResponse := JSONObject():New()

	If jQueryParams != Nil
		jResponse["items"]        := aItems
		jResponse["hasNext"]      := lHasNext
		jResponse["nextPageHref"] := IIf(lHasNext, "/api/fiscal/tax-monitor/v1/branches?" + "page" + "=" + cValToChar(nPage + 1) + "&" + "pageSize" +  "=" + cValToChar(nPageSize), "")
	endif

Return jResponse

/*/{Protheus.doc} GetBranches
Função responsável por retornar um alias contendo as filiais
@type static function
@author Caique Carlos
@since 12/02/2025
@version 1.0
@param nPage, integer, Página da consulta
@param nPageSize, integer, Quantidade de registros por página
@return array, Retorna um array de json com as filiais
/*/
Static Function GetBranches(nPage as integer, nPageSize as integer, lHasNext as logical) as array

    Local aFiliais  := {}   as array
	Local aEmpresas := {}   as array
    Local nIx        := 0    as integer    
    Local nEmpresas := 0    as integer
    Local nItems    := 0    as integer
    Local jItems    := Nil  as json

    Default nPage       := 1
    Default nPageSize   := 30
    Default lHasNext    := .F.

    aEmpresas := FWLoadSM0(.T., .T.)
    nEmpresas := Len(aEmpresas)

    If !Empty(aEmpresas)
        For nIx := IIf(nPage == 1, 1, ((nPage - 1) * nPageSize) + 1) To nEmpresas
            If nItems < nPageSize .And. !Empty(aEmpresas[nIx]) .And. aEmpresas[nIx][SM0_GRPEMP] == cEmpAnt .And. aEmpresas[nIx][SM0_USEROK]
                jItems := JSONObject():New()

                jItems["code"]          := Alltrim(aEmpresas[nIx][SM0_CODFIL])
                jItems["description"] := AllTrim(aEmpresas[nIx][SM0_NOMRED])

                AAdd(aFiliais, jItems)

                If ++nItems == nPageSize
                    Exit
                EndIf
            EndIf
        Next
		
		lHasNext := nIx < nEmpresas
    EndIf

    FWFreeArray(aEmpresas)

Return aFiliais
