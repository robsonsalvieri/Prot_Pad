#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"
#INCLUDE "backoffice.fiscal.api.tax.icms.cst.operation.ch"

Static oGetICMS := Nil

/*-------------------------------------------------------------
API de Valor de ICMS por Código de Situação Tributária.
@author eduardo.cirqueira
@parâmetros de entrada do endpoint
cbranches = filiais a serem consideradas (ex.: 'XIFIS26, DM1MG') )
cperiodStart = data inicial do período a ser filtrado
cperiodStart = data final do período a ser filtrado
@since 04/04/2025
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/icms/cst-operation', description='API de Operação por CST do ICMS')

Function MTValCSTICMS()
Local oJsResponse                                            as json
Local cBranches    := oRest:getQueryRequest()['branches']    as character
Local cPeriodStart := oRest:getQueryRequest()['periodStart'] as character
Local cPeriodEnd   := oRest:getQueryRequest()['periodEnd']   as character

//Todos os parâmetros são obrigatórios
if !Empty(cBranches) .and. !Empty(cPeriodStart) .and. !Empty(cPeriodEnd)
   oJsResponse := CSTICMSFilter(cBranches, cPeriodStart, cPeriodEnd)
endif    
oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)
Return 

/*-------------------------------------------------------------------
{Protheus.doc} GET CSTICMSFilter
Função responsável por preencher o Json com com informações referentes a Operações por CST do ICMS
@author eduardo.cirqueira
@parâmetros de entrada
cBranches = filiais a serem consideradas (ex.: 'XIFIS26, DM1MG') )
cPeriodStart = data inicial do período a ser filtrado
cPeriodStart = data final do período a ser filtrado
@since 04/04/2025
-------------------------------------------------------------------*/
Static Function CSTICMSFilter(cBranches as character, cPeriodStart as character, cPeriodEnd as character)
Local nPos         := 0                         as numeric
Local lHasNext     := .f.                       as logical
Local oJsonResp    := JsonObject():New()        as object
Local cAlias       := getNextAlias()            as character
Local aRetOrig     := {}                        as array

GetValCSTICMS(@cAlias,cBranches,cPeriodStart,cPeriodEnd)

// Iniciando o While
If (cAlias)->(!Eof())
    While (cAlias)->(!Eof())
        
        if nPos == 0
            oJsonResp["items"] := {}
        endif
        
        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++
        oJsonResp["items"][nPos]["code"]         := AllTrim((cAlias)->CSTICMS)
        oJsonResp["items"][nPos]["description"]  := AllTrim((cAlias)->DESCRI)
        oJsonResp["items"][nPos]["value"]        := (cAlias)->TOTAL_OPER
        
        (cAlias)->(DbSkip())
    EndDo
Else
    oJsonResp['code'] := 400
    oJsonResp['detailMessage'] := STR0001
    oJsonResp['message'] := STR0002
EndIf

oJsonResp["hasNext"]      := lHasNext
oJsonResp["nextPageHref"] := ""

(cAlias)->(DBCloseArea())

aSize(aRetOrig,0)
Return oJsonResp:toJson()



/*---------------------------------------------------------------------
{Protheus.doc} GetValCSTICMS()
(Responsável por executar a consulta aos campos da tabela CJZ)
@author eduardo.cirqueira
@parâmetros de entrada
cAlias = tabela temporaria para a query
cBranches = filiais a serem consideradas (ex.: 'XIFIS26, DM1MG') )
cPeriodStart = data inicial do período a ser filtrado
cPeriodStart = data final do período a ser filtrado
@since 04/04/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetValCSTICMS(cAlias as character, cBranches as character, cPeriodStart as character, cPeriodEnd as character)
Local cQuery    := ''       as character
Local cSpace    := Space(1) as character

If oGetICMS == Nil
    cQuery := "SELECT DISTINCT RTRIM(LTRIM(X5.X5_CHAVE)) AS CSTICMS"
    cQuery +=              " , RTRIM(LTRIM(X5.X5_DESCRI)) AS DESCRI
    cQuery +=              " , SUM(CJZ.CJZ_VALICM) AS TOTAL_OPER"
    cQuery +=  " FROM ? CJZ "
    cQuery +=  " INNER JOIN " + RetSqlName('SX5') + " X5 "
    cQuery +=          " ON X5_FILIAL = ? "
    cQuery +=         " AND X5_TABELA = ? "
    cQuery +=         " AND RTRIM(LTRIM(X5_CHAVE)) = SUBSTRING(CJZ_CLASFI,2,2) "
	cQuery +=         " AND X5.D_E_L_E_T_ = ? "
    cQuery += " WHERE CJZ.CJZ_FILIAL IN (?) "
    cQuery +=   " AND CJZ.CJZ_ENTRAD BETWEEN ? AND ? "
    cQuery +=   " AND CJZ.CJZ_VALICM > ?"
    cQuery +=   " AND CJZ.D_E_L_E_T_ = ? "
    cQuery += " GROUP BY X5.X5_CHAVE, X5.X5_DESCRI"
    cQuery += " ORDER BY CSTICMS"
    oGetICMS := FWExecStatement():New()
    oGetICMS:SetQuery(ChangeQuery(cQuery))
Endif

If oGetICMS != Nil
    oGetICMS:SetUnsafe(1, FIS319SqlName("CJZ"))
    oGetICMS:SetString(2, xFilial("SX5") )
    oGetICMS:SetString(3, 'S2')
    oGetICMS:SetString(4, cSpace)
    oGetICMS:SetIn(5, StrTokArr(cBranches, "," ))
    oGetICMS:SetString(6, StrTran(cPeriodStart, "-", ""))
    oGetICMS:SetString(7, StrTran(cPeriodEnd, "-", ""))
    oGetICMS:SetNumeric(8, 0)
    oGetICMS:SetString(9, cSpace)
    oGetICMS:CBASEQUERY := oGetICMS:GETFIXQUERY()
    cAlias := oGetICMS:OpenAlias()
Endif

FWFreeObj(oGetICMS)
Return 

