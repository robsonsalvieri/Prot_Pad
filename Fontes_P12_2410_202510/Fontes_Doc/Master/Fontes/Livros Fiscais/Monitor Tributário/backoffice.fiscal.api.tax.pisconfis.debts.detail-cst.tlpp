#INCLUDE "TLPP-CORE.TH"
#INCLUDE "TLPP-REST.TH"
#INCLUDE "TOTVS.CH"
#INCLUDE "FISA318.CH"

/*/{Protheus.doc} TITPisCofinsDebtsDetail
    Funcao responsavel por executar o metodo Get para retorno de API;
    A chamada desta funcao executara o detalhamento de debito de pis/cofins

    @type  Function
    @author Caique Carlos
    @since 14/05/2025
    @version 1.0
    @return Nil, nulo, não tem retorno

    @see https://tdn.totvs.com/pages/viewpage.action?pageId=553333507
/*/
@Get(endpoint='/api/fiscal/tax-monitor/v1/pis-cofins/debits', description='Obtém valores de Detalhamento de Débitos de PIS COFINS no período, para preenchimento da tabela de Detalhamento de Débitos de PIS COFINS.')
Function TITPisCofinsDebtsDetail()

    Local aRequiredProperties                                            as array
	Local oJsonResp           := JsonObject():new()                      as json
	Local oQryStr             := oRest:getQueryRequest()                 as object

    aRequiredProperties := {;
        {'branches',400,; //PROPERTY //ERROR_CODE
        STR0069,;         //"O parâmetro 'branches' é obrigatório e não foi informado."
        STR0070},;        //"Informar Código de Filiais separadas por vírgula."
        {'periodStart',400,;
        STR0071,;         //"Nenhuma Data foi informada no parâmetro 'periodStart'."
        STR0072},;        //"Informar a data desejada para o período"
        {'periodEnd',400,;
        STR0073,;         //"Nenhuma Data foi informada no parâmetro 'periodEnd'."
        STR0072};         //"Informar a data desejada para o período"
    }

    oRest:setKeyHeaderResponse('Content-Type', 'application/json')

    If FISA318F6HasApiRequiredProperties(aRequiredProperties, oQryStr, @oJsonResp)
        oJsonResp := PisCofDebtsDetailFilter(oQryStr["branches"], oQryStr["periodStart"], oQryStr["periodEnd"], oQryStr["page"], oQryStr["pageSize"])
	Endif

	oRest:setResponse(oJsonResp)

	FwFreeArray(aRequiredProperties)
	FWFreeObj(oJsonResp)
	FWFreeObj(oQryStr)

Return

/*/{Protheus.doc} TITPisCofinsCstOperation
    Funcao responsavel por executar o metodo Get para retorno de API;
    A chamada desta funcao executara a criacao do response de debito de pis/cofins, valor operacional e cst

    @type  Function
    @author Caique Carlos
    @since 14/05/2025
    @version 1.0
    @return Nil, nulo, não tem retorno.

    @see https://tdn.totvs.com/pages/viewpage.action?pageId=553333507
/*/
@Get(endpoint='/api/fiscal/tax-monitor/v1/pis-cofins/cst-operation', description='Obtém valores de Total de Valor Operacional, Total PIS e de COFINS apurados conforme Códigos de Situação Tributária, para preenchimento do gráfico de Operação por CST de PIS COFINS no período.')
Function TITPisCofinsCstOperation()

    Local aRequiredProperties                                            as array
	Local oJsonResp           := JsonObject():new()                      as json
	Local oQryStr             := oRest:getQueryRequest()                 as object

    aRequiredProperties := {;
        {'branches',400,; //PROPERTY //ERROR_CODE
        STR0069,;         //"O parâmetro 'branches' é obrigatório e não foi informado."
        STR0070},;        //"Informar Código de Filiais separadas por vírgula."
        {'periodStart',400,;
        STR0071,;         //"Nenhuma Data foi informada no parâmetro 'periodStart'."
        STR0072},;        //"Informar a data desejada para o período"
        {'periodEnd',400,;
        STR0073,;         //"Nenhuma Data foi informada no parâmetro 'periodEnd'."
        STR0072};         //"Informar a data desejada para o período"
    }

    oRest:setKeyHeaderResponse('Content-Type', 'application/json')

    If FISA318F6HasApiRequiredProperties(aRequiredProperties, oQryStr, @oJsonResp)
        oJsonResp := PisCofDebtCstFilter(oQryStr["branches"], oQryStr["periodStart"], oQryStr["periodEnd"], oQryStr["page"], oQryStr["pageSize"])
	Endif

	oRest:setResponse(oJsonResp)

    FwFreeArray(aRequiredProperties)
	FWFreeObj(oJsonResp)
	FWFreeObj(oQryStr)

Return

/*/{Protheus.doc} PisCofDebtsDetailFilter
    Funcao responsavel por executar a montagem do response body da 
    requisicao de detalhamento do pis/cofins de debito

    @type  Static Function

    @author Caique Carlos
    @since 14/05/2025
    @version 1.0

    @param cbranches, character, filiais usadas na requisicao
    @param cPeriodStart, character, perido inicial 
    @param cPeriodEnd, character, periodo final
    @return object, Objeto para response 
/*/
Static Function PisCofDebtsDetailFilter(cbranches as character, cPeriodStart as character, cPeriodEnd as character, cPage as character, cPageSize as character)

    Local oJsonResp := JsonObject():New() as object
    Local cAlias    := getNextAlias()     as character
    Local cAlias2   := getNextAlias()     as character
    Local cDbMs     := ''                 as character
    Local nPos      := 0                  as numeric
    Local nPageAux  := 0                  as numeric
    Local nPos2     := 0                  as numeric
    Local nPage     := 0                  as numeric
    Local nPageSize := 0                  as numeric
    Local lHasNext  := .f.                as logical

    Default cPage     := '1'
    Default cPageSize := '10'

    nPage     := Val(cPage)
    nPageSize := Val(cPageSize)

    cDbMs := TcGetDb()

    If 'MSSQL' $ cDbMs
        MssqlDebtsPeriodQry(@cAlias, cbranches, cPeriodStart, cPeriodEnd, nPage, nPageSize)
    ElseIf 'ORACLE' $ cDbMs
        OracleDebtsPeriodQry(@cAlias, cbranches, cPeriodStart, cPeriodEnd, nPage, nPageSize)
    ElseIf 'POSTGRES' $ cDbMs
        PostgresDebtsPeriodQry(@cAlias, cbranches, cPeriodStart, cPeriodEnd, nPage, nPageSize)
    EndIf

    If (cAlias)->(!Eof())
        While (cAlias)->(!Eof())
            nPageAux++

            If nPageAux == 1
                oJsonResp["items"] := {}
            Endif

            If nPageAux <= nPageSize
                aAdd( oJsonResp["items"], JsonObject():New() )
                nPos++
                oJsonResp["items"][nPos]["period"] := AllTrim((cAlias)->MESNOME)
                oJsonResp["items"][nPos]["branch"] := (cAlias)->FILIAL

                cBranchPisCofins  := (cAlias)->FILIAL
                cPeriodPisCofins := (cAlias)->PERIODO

                PisCofinsDetailQry(@cAlias2, cBranchPisCofins, cPeriodPisCofins, cDbMs)

                oJsonResp["items"][nPos]["detail"] := {}
                nPos2 := 0 
                While (cAlias2)->(!Eof())

                    nPos2++
                    Aadd(oJsonResp["items"][nPos]["detail"], JsonObject():New() )

                    oJsonResp["items"][nPos]["detail"][nPos2]["cst"]         := (cAlias2)->CSTPIS
                    oJsonResp["items"][nPos]["detail"][nPos2]["pisDebits"]    := (cAlias2)->PISTOTAL    
                    oJsonResp["items"][nPos]["detail"][nPos2]["cofinsDebits"] := (cAlias2)->COFINSTOTAL

                    (cAlias2)->(DbSkip())
                EndDo
            Else
                lHasNext := .t.
                Exit
            EndIf
            (cAlias)->(DbSkip())
        EndDo
        oJsonResp["hasNext"] := lHasNext

    Else
        oJsonResp["items"]   := {}
        oJsonResp["hasNext"] := 'false'
    EndIf

    (cAlias)->(DBCloseArea())

    If Select(cAlias2) > 0
        (cAlias2)->(DBCloseArea())
    EndIf

Return oJsonResp:toJson()

/*/{Protheus.doc} PisCofDebtCstFilter
    Funcao responsavel por executar a montagem do response body da 
    requisicao de detalhamento do pis/cofins de debito - o resultado da query
    realiza o detalhamanto por CST

    @type  Static Function
    @author Caique Carlos
    @since 14/05/2025
    @version 1.0

    @param cbranches, character, filiais usadas na requisicao
    @param cPeriodStart, character, perido inicial 
    @param cPeriodEnd, character, periodo final
    @return object, Objeto para response 
/*/
Static Function PisCofDebtCstFilter(cbranches as character, cPeriodStart as character, cPeriodEnd as character, cPage as character, cPageSize as character)

    Local oJsonResp := JsonObject():New() as object
    Local cAliasCst := getNextAlias()     as character
    Local nPos      := 0                  as numeric
    Local nPageAux  := 0                  as numeric
    Local nPage     := 0                  as numeric
    Local nPageSize := 0                  as numeric
    Local lHasNext  := .f.                as logical

    Default cPage     := '1'
    Default cPageSize := '10'

    nPage     := Val(cPage)
    nPageSize := Val(cPageSize)

    PisCofinsCstQry(@cAliasCst, cbranches, cPeriodStart, cPeriodEnd, nPage, nPageSize)

    If (cAliasCst)->(!Eof())
        While (cAliasCst)->(!Eof())
            nPageAux++
            If nPageAux == 1
                oJsonResp["items"] := {}
            Endif
            If nPageAux <= nPageSize
                aAdd( oJsonResp["items"], JsonObject():New() )
                nPos++
                oJsonResp["items"][nPos]["cstCode"]               := (cAliasCst)->CSTPIS
                oJsonResp["items"][nPos]["operationalTotalValue"] := (cAliasCst)->DEBTOPER
                oJsonResp["items"][nPos]["pisTotalValue"]         := (cAliasCst)->PISTOTAL
                oJsonResp["items"][nPos]["cofinsTotalValue"]      := (cAliasCst)->COFINSTOTAL
            Else
                lHasNext := .t.
                Exit
            EndIf
            (cAliasCst)->(DbSkip())
        EndDo
        oJsonResp["hasNext"] := lHasNext

    Else
        oJsonResp["items"]   := {}
        oJsonResp["hasNext"] := 'false'
    EndIf

    (cAliasCst)->(DBCloseArea())

Return oJsonResp:toJson()

/*/{Protheus.doc} MssqlDebtsPeriodQry
    Funcao responsavel por preencher o Alias com as informacoes 
    referentes aos valores de Pis e Cofins por periodo

    @type Static Function
    @author Caique Carlos
    @since 14/05/2025

    @param cAlias, character, alias de utilizacao para da query
    @param cbranches, character, filiais usadas na requisicao
    @param cPeriodStart, character, perido inicial 
    @param cPeriodEnd, character, periodo final
    @param nPage, numeric, numero da pagina a ser utilizado 
    @param nPageSize, numeric, quantidade de registros por pagina
    @return Nil, nulo, não tem retorno.

    @see https://tdn.totvs.com/display/public/framework/FWPreparedStatement
/*/
Static Function MssqlDebtsPeriodQry(cAlias as character, cbranches as character, cPeriodStart as character, cPeriodEnd as character, nPage as numeric, nPageSize as numeric)

    Local cQuery                as character
    Local oGetPisCofDebtsPeriod as object

    cQuery := "SELECT DISTINCT "
    cQuery +=   "CK0.MESNOME AS MESNOME, "
    cQuery +=   "CK0.FILIAL AS FILIAL, "
    cQuery +=   "CK0.MESNUMERO AS MESNUMERO, "
    cQuery +=   "CK0.ANONUM AS ANONUM,  "
    cQuery +=   "CK0.PERIODO AS PERIODO "
    cQuery += "FROM ("
    cQuery +=     "SELECT "
    cQuery +=       "CK0_FILIAL AS FILIAL, "
    cQuery +=       "MONTH(CK0_ENTRAD) AS MESNUMERO, "
    cQuery +=       "YEAR(CK0_ENTRAD) AS ANONUM, "
    cQuery +=       "SUBSTRING(CK0_ENTRAD,5,2) + LEFT(CK0_ENTRAD,4) AS PERIODO, "
    cQuery +=       "CASE "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD,5,2) = ? THEN 'Jan/' + LEFT(CK0_ENTRAD,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD,5,2) = ? THEN 'Fev/' + LEFT(CK0_ENTRAD,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD,5,2) = ? THEN 'Mar/' + LEFT(CK0_ENTRAD,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD,5,2) = ? THEN 'Abr/' + LEFT(CK0_ENTRAD,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD,5,2) = ? THEN 'Mai/' + LEFT(CK0_ENTRAD,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD,5,2) = ? THEN 'Jun/' + LEFT(CK0_ENTRAD,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD,5,2) = ? THEN 'Jul/' + LEFT(CK0_ENTRAD,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD,5,2) = ? THEN 'Ago/' + LEFT(CK0_ENTRAD,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD,5,2) = ? THEN 'Set/' + LEFT(CK0_ENTRAD,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD,5,2) = ? THEN 'Out/' + LEFT(CK0_ENTRAD,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD,5,2) = ? THEN 'Nov/' + LEFT(CK0_ENTRAD,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD,5,2) = ? THEN 'Dez/' + LEFT(CK0_ENTRAD,4) "
    cQuery +=       "ELSE ? END as MESNOME "
    cQuery +=      "FROM ? CK0 "
    cQuery +=       "WHERE "
    cQuery +=           "CK0_ENTRAD BETWEEN ? AND ? "
    cQuery +=           "AND D_E_L_E_T_ = ? "
    cQuery +=           "AND CK0_FILIAL IN (?) "
    cQuery +=           "AND (CK0_VALPIS > ? OR CK0_VALCOF > ?) "
    cQuery +=           "AND CK0_TIPOMO = ? "
    cQuery +=      "GROUP BY CK0_FILIAL, "
    cQuery +=      "SUBSTRING(CK0_ENTRAD, 5,2), LEFT(CK0_ENTRAD,4), MONTH(CK0_ENTRAD), YEAR(CK0_ENTRAD)) AS CK0 "
    cQuery += "GROUP BY CK0.MESNOME, CK0.MESNUMERO, CK0.ANONUM, CK0.FILIAL, CK0.PERIODO"
    cQuery += "ORDER BY CK0.ANONUM, CK0.MESNUMERO, CK0.FILIAL "
    cQuery += "OFFSET ( ? * ? ) ROWS "
    cQuery += "FETCH NEXT ? ROWS ONLY "

    oGetPisCofDebtsPeriod := FwExecStatement():New()
    oGetPisCofDebtsPeriod:SetQuery(ChangeQuery(cQuery))

    oGetPisCofDebtsPeriod:setString(1, "01")
    oGetPisCofDebtsPeriod:setString(2, "02")
    oGetPisCofDebtsPeriod:setString(3, "03")
    oGetPisCofDebtsPeriod:setString(4, "04")
    oGetPisCofDebtsPeriod:setString(5, "05")
    oGetPisCofDebtsPeriod:setString(6, "06")
    oGetPisCofDebtsPeriod:setString(7, "07")
    oGetPisCofDebtsPeriod:setString(8, "08")
    oGetPisCofDebtsPeriod:setString(9, "09")
    oGetPisCofDebtsPeriod:setString(10,"10")
    oGetPisCofDebtsPeriod:setString(11,"11")
    oGetPisCofDebtsPeriod:setString(12,"12")
    oGetPisCofDebtsPeriod:setString(13, "")
    oGetPisCofDebtsPeriod:setUnsafe(14, FIS319SqlName("CK0"))
    oGetPisCofDebtsPeriod:setString(15, StrTran(cPeriodStart, "-", ""))
    oGetPisCofDebtsPeriod:setString(16, StrTran(cPeriodEnd, "-", ""))
    oGetPisCofDebtsPeriod:setString(17, " ")
    oGetPisCofDebtsPeriod:setIn(18, StrTokArr(cbranches, "," ))
    oGetPisCofDebtsPeriod:setNumeric(19, 0)
    oGetPisCofDebtsPeriod:setNumeric(20, 0)
    oGetPisCofDebtsPeriod:setString(21, "S")
    oGetPisCofDebtsPeriod:setNumeric(22, cValToChar(nPage-1))
    oGetPisCofDebtsPeriod:setNumeric(23, cValToChar(nPageSize))
    oGetPisCofDebtsPeriod:setNumeric(24, cValToChar(nPageSize+1))

    oGetPisCofDebtsPeriod:cBaseQuery := oGetPisCofDebtsPeriod:getFixQuery()
    cAlias := oGetPisCofDebtsPeriod:openAlias()

    FWFreeObj(oGetPisCofDebtsPeriod)
Return

/*/{Protheus.doc} OraclelDebtsPeriodQry
    Funcao responsavel por preencher o Alias com as informacoes 
    referentes aos valores de Pis e Cofins por periodo

    @type Static Function
    @author Caique Carlos
    @since 14/05/2025

    @param cAlias, character, alias de utilizacao para da query
    @param cbranches, character, filiais usadas na requisicao
    @param cPeriodStart, character, perido inicial 
    @param cPeriodEnd, character, periodo final
    @param nPage, numeric, numero da pagina a ser utilizado 
    @param nPageSize, numeric, quantidade de registros por pagina
    @return Nil, nulo, não tem retorno.

    @see https://tdn.totvs.com/display/public/framework/FWPreparedStatement
/*/
Static Function OracleDebtsPeriodQry(cAlias as character, cbranches as character, cPeriodStart as character, cPeriodEnd as character, nPage as numeric, nPageSize as numeric)

    Local cQuery                as character
    Local oGetPisCofDebtsPeriod as object

    cQuery := "SELECT DISTINCT "
    cQuery +=   "CK0.MESNOME AS MESNOME, "
    cQuery +=   "CK0.FILIAL AS FILIAL, "
    cQuery +=   "CK0.MESNUMERO AS MESNUMERO, "
    cQuery +=   "CK0.ANONUM AS ANONUM, "
    cQuery +=   "CK0.PERIODO AS PERIODO "
    cQuery += "FROM ("
    cQuery +=     "SELECT "
    cQuery +=       "CK0_FILIAL AS FILIAL, "
    cQuery +=       "TO_CHAR(CAST(CK0_ENTRAD AS DATE), 'MM') AS MESNUMERO, "
    cQuery +=       "TO_CHAR(CAST(CK0_ENTRAD AS DATE), 'YYYY') AS ANONUM, "
    cQuery +=       "SUBSTR(CK0_ENTRAD, 5,2) || SUBSTR(CK0_ENTRAD, 1,4) AS PERIODO, "
    cQuery +=       "CASE "
    cQuery +=           "WHEN SUBSTR(CK0_ENTRAD,5,2) = ? THEN 'Jan/' || SUBSTR(CK0_ENTRAD,1,4) "
    cQuery +=           "WHEN SUBSTR(CK0_ENTRAD,5,2) = ? THEN 'Fev/' || SUBSTR(CK0_ENTRAD,1,4) "
    cQuery +=           "WHEN SUBSTR(CK0_ENTRAD,5,2) = ? THEN 'Mar/' || SUBSTR(CK0_ENTRAD,1,4) "
    cQuery +=           "WHEN SUBSTR(CK0_ENTRAD,5,2) = ? THEN 'Abr/' || SUBSTR(CK0_ENTRAD,1,4) "
    cQuery +=           "WHEN SUBSTR(CK0_ENTRAD,5,2) = ? THEN 'Mai/' || SUBSTR(CK0_ENTRAD,1,4) "
    cQuery +=           "WHEN SUBSTR(CK0_ENTRAD,5,2) = ? THEN 'Jun/' || SUBSTR(CK0_ENTRAD,1,4) "
    cQuery +=           "WHEN SUBSTR(CK0_ENTRAD,5,2) = ? THEN 'Jul/' || SUBSTR(CK0_ENTRAD,1,4) "
    cQuery +=           "WHEN SUBSTR(CK0_ENTRAD,5,2) = ? THEN 'Ago/' || SUBSTR(CK0_ENTRAD,1,4) "
    cQuery +=           "WHEN SUBSTR(CK0_ENTRAD,5,2) = ? THEN 'Set/' || SUBSTR(CK0_ENTRAD,1,4) "
    cQuery +=           "WHEN SUBSTR(CK0_ENTRAD,5,2) = ? THEN 'Out/' || SUBSTR(CK0_ENTRAD,1,4) "
    cQuery +=           "WHEN SUBSTR(CK0_ENTRAD,5,2) = ? THEN 'Nov/' || SUBSTR(CK0_ENTRAD,1,4) "
    cQuery +=           "WHEN SUBSTR(CK0_ENTRAD,5,2) = ? THEN 'Dez/' || SUBSTR(CK0_ENTRAD,1,4) "
    cQuery +=       "ELSE ? END AS MESNOME "
    cQuery +=      "FROM ? CK0 "
    cQuery +=       "WHERE "
    cQuery +=           "CK0_ENTRAD BETWEEN ? AND ? "
    cQuery +=           "AND D_E_L_E_T_ = ? "
    cQuery +=           "AND CK0_FILIAL IN (?) "
    cQuery +=           "AND (CK0_VALPIS > ? OR CK0_VALCOF > ?) "
    cQuery +=           "AND CK0_TIPOMO = ? "
    cQuery +=      "GROUP BY CK0_FILIAL, "
    cQuery +=       "SUBSTR(CK0_ENTRAD,5,2), SUBSTR(CK0_ENTRAD,1,4),TO_CHAR(CAST(CK0_ENTRAD AS DATE), 'MM'), TO_CHAR(CAST(CK0_ENTRAD AS DATE), 'YYYY')) CK0 "
    cQuery += "GROUP BY CK0.MESNOME, CK0.MESNUMERO, CK0.ANONUM, CK0.FILIAL, CK0.PERIODO "
    cQuery += "ORDER BY CK0.ANONUM, CK0.MESNUMERO, CK0.FILIAL "
    cQuery += "OFFSET ( ? * ? ) ROWS "
    cQuery += "FETCH NEXT ? ROWS ONLY "

    oGetPisCofDebtsPeriod := FwExecStatement():New()
    oGetPisCofDebtsPeriod:SetQuery(ChangeQuery(cQuery))

    oGetPisCofDebtsPeriod:setString(1, "01")
    oGetPisCofDebtsPeriod:setString(2, "02")
    oGetPisCofDebtsPeriod:setString(3, "03")
    oGetPisCofDebtsPeriod:setString(4, "04")
    oGetPisCofDebtsPeriod:setString(5, "05")
    oGetPisCofDebtsPeriod:setString(6, "06")
    oGetPisCofDebtsPeriod:setString(7, "07")
    oGetPisCofDebtsPeriod:setString(8, "08")
    oGetPisCofDebtsPeriod:setString(9, "09")
    oGetPisCofDebtsPeriod:setString(10,"10")
    oGetPisCofDebtsPeriod:setString(11,"11")
    oGetPisCofDebtsPeriod:setString(12,"12")
    oGetPisCofDebtsPeriod:setString(13, "")
    oGetPisCofDebtsPeriod:setUnsafe(14, FIS319SqlName("CK0"))
    oGetPisCofDebtsPeriod:setString(15, StrTran(cPeriodStart, "-", ""))
    oGetPisCofDebtsPeriod:setString(16, StrTran(cPeriodEnd, "-", ""))
    oGetPisCofDebtsPeriod:setString(17, " ")
    oGetPisCofDebtsPeriod:setIn(18, StrTokArr(cbranches, "," ))
    oGetPisCofDebtsPeriod:setNumeric(19, 0)
    oGetPisCofDebtsPeriod:setNumeric(20, 0)
    oGetPisCofDebtsPeriod:setString(21, "S")
    oGetPisCofDebtsPeriod:setNumeric(22, nPage-1)
    oGetPisCofDebtsPeriod:setNumeric(23, nPageSize)
    oGetPisCofDebtsPeriod:setNumeric(24, nPageSize+1)

    oGetPisCofDebtsPeriod:cBaseQuery := oGetPisCofDebtsPeriod:getFixQuery()
    cAlias := oGetPisCofDebtsPeriod:openAlias()

    FWFreeObj(oGetPisCofDebtsPeriod)

Return

/*/{Protheus.doc} PostgresDebtsPeriodQry
    Funcao responsavel por preencher o Alias com as informacoes 
    referentes aos valores de Pis e Cofins por periodo

    @type Static Function
    @author Caique Carlos
    @since 14/05/2025

    @param cAlias, character, alias de utilizacao para da query
    @param cbranches, character, filiais usadas na requisicao
    @param cPeriodStart, character, perido inicial 
    @param cPeriodEnd, character, periodo final
    @param nPage, numeric, numero da pagina a ser utilizado 
    @param nPageSize, numeric, quantidade de registros por pagina
    @return Nil, nulo, não tem retorno.

    @see https://tdn.totvs.com/display/public/framework/FWPreparedStatement
/*/
Static Function PostgresDebtsPeriodQry(cAlias as character, cbranches as character, cPeriodStart as character, cPeriodEnd as character, nPage as numeric, nPageSize as numeric)

    Local cQuery                as character
    Local oGetPisCofDebtsPeriod as object

    cQuery := "SELECT DISTINCT "
    cQuery +=   "CK0.FILIAL AS FILIAL, "
    cQuery +=   "CK0.MESNOME AS MESNOME, "
    cQuery +=   "TO_NUMBER(CK0.MESNUMERO,'99') AS MESNUMERO, "
    cQuery +=   "TO_NUMBER(CK0.ANONUM,'9999') AS ANONUM,  "
    cQuery +=   "CK0.PERIODO AS PERIODO "
    cQuery += "FROM ("
    cQuery +=     "SELECT "
    cQuery +=       "CK0_FILIAL AS FILIAL, "
    cQuery +=       "TO_CHAR(CK0_ENTRAD:: DATE, 'MM') AS MESNUMERO, "
    cQuery +=       "TO_CHAR(CK0_ENTRAD:: DATE, 'YYYY') AS ANONUM, "
    cQuery +=       "SUBSTRING(CK0_ENTRAD,5,2) || SUBSTRING(CK0_ENTRAD,1,4) AS PERIODO, "
    cQuery +=       "CASE "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD, 5,2) = ? THEN 'Jan/' || SUBSTRING(CK0_ENTRAD, 1,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD, 5,2) = ? THEN 'Fev/' || SUBSTRING(CK0_ENTRAD, 1,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD, 5,2) = ? THEN 'Mar/' || SUBSTRING(CK0_ENTRAD, 1,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD, 5,2) = ? THEN 'Abr/' || SUBSTRING(CK0_ENTRAD, 1,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD, 5,2) = ? THEN 'Mai/' || SUBSTRING(CK0_ENTRAD, 1,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD, 5,2) = ? THEN 'Jun/' || SUBSTRING(CK0_ENTRAD, 1,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD, 5,2) = ? THEN 'Jul/' || SUBSTRING(CK0_ENTRAD, 1,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD, 5,2) = ? THEN 'Ago/' || SUBSTRING(CK0_ENTRAD, 1,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD, 5,2) = ? THEN 'Set/' || SUBSTRING(CK0_ENTRAD, 1,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD, 5,2) = ? THEN 'Out/' || SUBSTRING(CK0_ENTRAD, 1,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD, 5,2) = ? THEN 'Nov/' || SUBSTRING(CK0_ENTRAD, 1,4) "
    cQuery +=           "WHEN SUBSTRING(CK0_ENTRAD, 5,2) = ? THEN 'Dez/' || SUBSTRING(CK0_ENTRAD, 1,4) "
    cQuery +=       "ELSE ? END as MESNOME "
    cQuery +=      "FROM ? CK0 "
    cQuery +=       "WHERE "
    cQuery +=           "CK0_ENTRAD BETWEEN ? AND ? "
    cQuery +=           "AND D_E_L_E_T_ = ? "
    cQuery +=           "AND CK0_FILIAL IN (?) "
    cQuery +=           "AND (CK0_VALPIS > ? OR CK0_VALCOF > ?) "
    cQuery +=           "AND CK0_TIPOMO = ? "
    cQuery +=      "GROUP BY CK0_FILIAL, "
    cQuery +=      "SUBSTRING(CK0_ENTRAD, 5,2), SUBSTRING(CK0_ENTRAD,1,4), TO_CHAR(CK0_ENTRAD:: DATE, 'MM'), TO_CHAR(CK0_ENTRAD:: DATE, 'YYYY')) AS CK0 "
    cQuery += "GROUP BY CK0.MESNOME, CK0.MESNUMERO, CK0.ANONUM, CK0.FILIAL, CK0.PERIODO "
    cQuery += "ORDER BY TO_NUMBER(CK0.ANONUM,'9999'), TO_NUMBER(CK0.MESNUMERO,'99'), CK0.FILIAL "
    cQuery += "OFFSET ( ? * ? ) ROWS "
    cQuery += "FETCH NEXT ? ROWS ONLY "

    oGetPisCofDebtsPeriod := FwExecStatement():New()
    oGetPisCofDebtsPeriod:SetQuery(ChangeQuery(cQuery))

    oGetPisCofDebtsPeriod:setString(1, "01")
    oGetPisCofDebtsPeriod:setString(2, "02")
    oGetPisCofDebtsPeriod:setString(3, "03")
    oGetPisCofDebtsPeriod:setString(4, "04")
    oGetPisCofDebtsPeriod:setString(5, "05")
    oGetPisCofDebtsPeriod:setString(6, "06")
    oGetPisCofDebtsPeriod:setString(7, "07")
    oGetPisCofDebtsPeriod:setString(8, "08")
    oGetPisCofDebtsPeriod:setString(9, "09")
    oGetPisCofDebtsPeriod:setString(10,"10")
    oGetPisCofDebtsPeriod:setString(11,"11")
    oGetPisCofDebtsPeriod:setString(12,"12")
    oGetPisCofDebtsPeriod:setString(13, "")
    oGetPisCofDebtsPeriod:setUnsafe(14, FIS319SqlName("CK0"))
    oGetPisCofDebtsPeriod:setString(15, StrTran(cPeriodStart, "-", ""))
    oGetPisCofDebtsPeriod:setString(16, StrTran(cPeriodEnd, "-", ""))
    oGetPisCofDebtsPeriod:setString(17, " ")
    oGetPisCofDebtsPeriod:setIn(18, StrTokArr(cbranches, "," ))
    oGetPisCofDebtsPeriod:setNumeric(19, 0)
    oGetPisCofDebtsPeriod:setNumeric(20, 0)
    oGetPisCofDebtsPeriod:setString(21, "S")
    oGetPisCofDebtsPeriod:setNumeric(22, nPage-1)
    oGetPisCofDebtsPeriod:setNumeric(23, nPageSize)
    oGetPisCofDebtsPeriod:setNumeric(24, nPageSize+1)

    oGetPisCofDebtsPeriod:cBaseQuery := oGetPisCofDebtsPeriod:getFixQuery()
    cAlias := oGetPisCofDebtsPeriod:openAlias()

    FWFreeObj(oGetPisCofDebtsPeriod)

Return

/*/{Protheus.doc} PisCofinsDetailQry
    Funcao responsavel por preencher o Alias2 com as informacoes 
    referentes aos valores de Pis e Cofins por periodo de forma detalhada

    @type Static Function
    @author Caique Carlos
    @since 14/05/2025

    @param cAlias2, character, alias de utilizacao para da query
    @param cBranchPisCofins, character, filial a ser consultada
    @param cPeriod, character, perido a consultar 
    @param cPeriodEnd, character, periodo final
    @param cDbType, character, tipo de banco de dados
    @return Nil, nulo, não tem retorno.

    @see https://tdn.totvs.com/display/public/framework/FWPreparedStatement
/*/
Static Function PisCofinsDetailQry(cAlias2 as character, cBranchPisCofins as character, cPeriod as character, cDbType as character)

    Local cQuery                as character
    Local oGetPisCofDebtsDetail as object

    cQuery := "SELECT DISTINCT "
    cQuery +=   "SUM(CK0.PIS) AS PISTOTAL, "
    cQuery +=   "SUM(CK0.COFINS) AS COFINSTOTAL, "
    cQuery +=   "CK0.CSTPIS AS CSTPIS, "
    cQuery +=   "CK0.CSTCOFINS AS CSTCOFINS "
    cQuery += "FROM ("
    cQuery +=   "SELECT "
    cQuery +=       "ISNULL(SUM(CK0_VALPIS),0) AS PIS, "
    cQuery +=       "ISNULL(SUM(CK0_VALCOF),0) AS COFINS, "
    cQuery +=       "CK0_CSTPIS AS CSTPIS, "
    cQuery +=       "CK0_CSTCOF AS CSTCOFINS "
    cQuery +=   "FROM ? CK0 "
    cQuery +=   "WHERE "

    If "MSSQL" $ cDbType
        cQuery += "SUBSTRING(CK0_ENTRAD,5,2) + LEFT(CK0_ENTRAD,4) = ? "
    ElseIf "ORACLE" $ cDbType
        cQuery += "SUBSTR(CK0_ENTRAD,5,2) || SUBSTR(CK0_ENTRAD,1,4) = ? "
    ElseIf "POSTGRES" $ cDbType
        cQuery += "SUBSTRING(CK0_ENTRAD,5,2) || SUBSTRING(CK0_ENTRAD,1,4) = ? "
    EndIf

    cQuery +=       "AND D_E_L_E_T_ = ? "
    cQuery +=       "AND CK0_FILIAL = ? "
    cQuery +=       "AND (CK0_VALPIS > ? OR CK0_VALCOF > ?) "
    cQuery +=       "AND CK0_TIPOMO = ? "
    cQuery +=   "GROUP BY CK0_CSTPIS, CK0_CSTCOF) CK0 "
    cQuery += "GROUP BY CK0.CSTCOFINS, CK0.CSTPIS"

    oGetPisCofDebtsDetail := FwExecStatement():New()
    oGetPisCofDebtsDetail:SetQuery(ChangeQuery(cQuery))

    oGetPisCofDebtsDetail:setUnsafe(1, FIS319SqlName("CK0"))
    oGetPisCofDebtsDetail:setString(2, cPeriod)
    oGetPisCofDebtsDetail:setString(3, " ")
    oGetPisCofDebtsDetail:setString(4, cBranchPisCofins)
    oGetPisCofDebtsDetail:setNumeric(5, 0)
    oGetPisCofDebtsDetail:setNumeric(6, 0)
    oGetPisCofDebtsDetail:setString(7, "S")

    oGetPisCofDebtsDetail:cBaseQuery := oGetPisCofDebtsDetail:getFixQuery()
    cAlias2 := oGetPisCofDebtsDetail:openAlias()

    FWFreeObj(oGetPisCofDebtsDetail)
Return

/*/{Protheus.doc} PisCofinsCstQry
    Funcao responsavel por preencher o Alias com as informacoes 
    referentes aos valores de Pis e Cofins por cst

    @type Static Function
    @author Caique Carlos
    @since 14/05/2025

    @param cAliasCst, character, alias de utilizacao para da query
    @param cbranches, character, filiais usadas na requisicao
    @param cPeriodStart, character, perido inicial 
    @param cPeriodEnd, character, periodo final
    @param nPage, numeric, numero da pagina a ser utilizado 
    @param nPageSize, numeric, quantidade de registros por pagina
    @return Nil, nulo, não tem retorno.

    @see https://tdn.totvs.com/display/public/framework/FWPreparedStatement
/*/
Static Function PisCofinsCstQry(cAliasCst as character, cbranches as character, cPeriodStart as character, cPeriodEnd as character, nPage as numeric, nPageSize as numeric)

    Local cQuery             as character
    Local oGetPisCofDebtsCst as object

    cQuery := "SELECT DISTINCT "
    cQuery +=   "SUM(CK0.PIS) AS PISTOTAL, "
    cQuery +=   "SUM(CK0.COFINS) AS COFINSTOTAL, "
    cQuery +=   "SUM(CK0.VALCONT) AS DEBTOPER, "
    cQuery +=   "CK0.CSTPIS AS CSTPIS, "
    cQuery +=   "CK0.CSTCOFINS AS CSTCOFINS "
    cQuery += "FROM ("
    cQuery +=   "SELECT "
    cQuery +=       "ISNULL(SUM(CK0_VALPIS),0) AS PIS, "
    cQuery +=       "ISNULL(SUM(CK0_VALCOF),0) AS COFINS, "
    cQuery +=       "ISNULL(SUM(CK0_VALCON),0) AS VALCONT, "
    cQuery +=       "CK0_CSTPIS AS CSTPIS, "
    cQuery +=       "CK0_CSTCOF AS CSTCOFINS "
    cQuery +=   "FROM ? CK0 "
    cQuery +=   "WHERE "
    cQuery +=       "CK0_ENTRAD BETWEEN ? AND ? "
    cQuery +=       "AND D_E_L_E_T_ = ? "
    cQuery +=       "AND CK0_FILIAL IN (?) "
    cQuery +=       "AND (CK0_VALPIS > ? OR CK0_VALCOF > ?) "
    cQuery +=       "AND CK0_TIPOMO = ? "
    cQuery +=   "GROUP BY CK0_CSTPIS, CK0_CSTCOF) CK0 "
    cQuery += "GROUP BY CK0.CSTCOFINS, CK0.CSTPIS "
    cQuery += "ORDER BY CK0.CSTPIS "
    cQuery += "OFFSET ( ? * ? ) ROWS "
    cQuery += "FETCH NEXT ? ROWS ONLY "

    oGetPisCofDebtsCst := FwExecStatement():New()
    oGetPisCofDebtsCst:SetQuery(ChangeQuery(cQuery))

    oGetPisCofDebtsCst:setUnsafe(1, FIS319SqlName("CK0"))
    oGetPisCofDebtsCst:setString(2, StrTran(cPeriodStart, "-", ""))
    oGetPisCofDebtsCst:setString(3, StrTran(cPeriodEnd, "-", ""))
    oGetPisCofDebtsCst:setString(4, ' ')
    oGetPisCofDebtsCst:setIn(5, StrTokArr(cbranches, "," ))
    oGetPisCofDebtsCst:setNumeric(6, 0)
    oGetPisCofDebtsCst:setNumeric(7, 0)
    oGetPisCofDebtsCst:setString(8, "S")
    oGetPisCofDebtsCst:setNumeric(9, (nPage-1))
    oGetPisCofDebtsCst:setNumeric(10,(nPageSize))
    oGetPisCofDebtsCst:setNumeric(11,(nPageSize+1))

    oGetPisCofDebtsCst:cBaseQuery := oGetPisCofDebtsCst:getFixQuery()
    cAliasCst := oGetPisCofDebtsCst:openAlias()

    FWFreeObj(oGetPisCofDebtsCst)
Return
