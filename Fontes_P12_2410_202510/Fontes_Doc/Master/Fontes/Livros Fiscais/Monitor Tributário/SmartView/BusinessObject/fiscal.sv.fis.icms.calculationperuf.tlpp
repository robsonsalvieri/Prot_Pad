#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "FISA318.CH"
    
namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} icmsCalculationPerUfBusinessObject
    Objeto de Negócio que abre no formato Visão de Dados da Tecnologia SmartView o Card de Apuração de ICMS por UF do TOTVS Inteligência Tributária
    
    @author Fábio Mendonça
    @since 24/04/2025
    @version 1.0

    @see https://tdn.totvs.com/pages/releaseview.action?pageId=625448935
/*/
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", name="icmsCalculationPerUfBusinessObject", country="BRA", initialRelease="12.1.2310")
Class IcmsCalculationPerUfBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    Public Method new()         as object
    Public Method getData()     as object
    Public Method getSchema()   as object
EndClass

/*{Protheus.doc} new
    Construtor da classe
    
    @author Fábio Mendonça
    @since 24/04/2025
    @version 1.0

    @return object: self
*/  
Method new() as object Class IcmsCalculationPerUfBusinessObject
    _Super:new()

    ::appendArea(STR0006) //"Monitor Tributário"
    ::setDisplayName(STR0061 + FISA318F4GetDisplayNameComplement()) //"CARD_ICMS_APURACAO_POR_UF_"
    ::setDescription(STR0062) //"Abertura dos dados do Card de Apuração por UF da Visão de ICMS do TOTVS Inteligência Tributária no formato Visão de Dados da Tecnologia SmartView"
Return self
 
/*/{Protheus.doc} getData
    Retorna dados do objeto de negócio
    
    @author Fábio Mendonça
    @since 24/04/2025
    @version 1.0
    @param nPage, numérico, Indica a página atual do relatório
    @param oFilter, objeto, Objeto com filtro do SmartView    
    @return object: self:oData

    @see https://tdn.totvs.com/pages/viewpage.action?pageId=274315041
    @see https://tdn.totvs.com/display/tec/DecodeUTF8
/*/  
Method getData(nPage as numeric, oFilter as object) as object Class IcmsCalculationPerUfBusinessObject
    Local nICalcPerUf        as numeric
    Local aCalcPerUfItems    as array
    Local oCalcItems         as object
    Local oFilterParams      as object
    Local oCalcPerUf         as object

    oFilterParams    := oFilter:getParameters()
    aCalcPerUfItems  := FISA318F1SVGetApiData(oFilterParams)

    For nICalcPerUf := 1 To Len(aCalcPerUfItems)
        oCalcPerUf                  := aCalcPerUfItems[nICalcPerUf] 
        oCalcPerUf["ufDescription"] :=  DecodeUTF8(oCalcPerUf["ufDescription"])

        oCalcItems  := oCalcPerUf["calculations"]
        aEval(;
            oCalcItems:getNames(),;
            {|calcPropertyName| oCalcPerUf[calcPropertyName] := oCalcItems[calcPropertyName] };
        )

        ::appendData(oCalcPerUf)
    Next nICalcPerUf

    FwFreeObj(oCalcItems)
    FwFreeObj(oFilterParams)
    FwFreeObj(oCalcPerUf)
    FwFreeArray(aCalcPerUfItems)
Return ::oData
 
/*/{Protheus.doc} getSchema
    Retorna estrutura dos campos

    @author Fábio Mendonça
    @since 24/04/2025
    @version 1.0
    @return object: self:oSchema
/*/  
Method getSchema() as object Class IcmsCalculationPerUfBusinessObject
    Local aSVParams   as array

    // Coleta parâmetros do ON
    aSVParams := FISA318F2SVGetParametersByDashboard("icms")
    aEval(aSVParams, {|aParam| ::addParameter(aParam[1], aParam[2], aParam[3], aParam[4])})

    // Colunas do ON
    ::addProperty("ufDescription"   , STR0056, "string", STR0056, "ufDescription"   ) //"Estado"
    ::addProperty("credits"         , STR0063, "number", STR0063, "credits"         ) //"Créditos"
    ::addProperty("debits"          , STR0064, "number", STR0064, "debits"          ) //"Débitos"
    ::addProperty("icmsStCredits"   , STR0058, "number", STR0058, "icmsStCredits"   ) //"ICMS ST (Créditos)"
    ::addProperty("icmsStDebits"    , STR0066, "number", STR0066, "icmsStDebits"    ) //"ICMS ST (Débitos)"
    ::addProperty("difal"           , STR0065, "number", STR0065, "difal"           ) //"Difal"

    FwFreeArray(aSVParams)
Return ::oSchema
