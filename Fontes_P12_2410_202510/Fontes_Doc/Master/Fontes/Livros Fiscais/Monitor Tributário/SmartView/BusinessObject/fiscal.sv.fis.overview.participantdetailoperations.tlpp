#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "FISA318.CH"
    
namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} overviewParticipantDetailOperationsBusinessObject
    Objeto de Negócio que abre no formato Visão de Dados da Tecnologia SmartView o Card de Detalhamento das Operações por Participante do TOTVS Inteligência Tributária
    
    @author Fábio Mendonça
    @since 10/03/2025
    @version 1.0

    @see https://tdn.totvs.com/pages/releaseview.action?pageId=625448935
/*/
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", name="overviewParticipantDetailOperationsBusinessObject", country="BRA", initialRelease="12.1.2310")
Class OverviewParticipantDetailOperationsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    Public Method new()         as object
    Public Method getData()     as object
    Public Method getSchema()   as object
EndClass

/*{Protheus.doc} new
    Construtor da classe
    
    @author Fábio Mendonça
    @since 10/03/2025
    @version 1.0

    @return object: self
*/  
Method new() as object Class OverviewParticipantDetailOperationsBusinessObject
    _Super:new()

    ::appendArea(STR0006) //"Monitor Tributário"
    ::setDisplayName(STR0020 + strTran(dToC(date()), "/", "-") + "_" + strTran(time(), ":", "-")) //"CARD_VISAO-GERAL_DETALHAMENTO-OPERACOES-POR-PARTICIPANTE_"
    ::setDescription(STR0021) //"Abertura dos dados do Card de Detalhamento das Operações por Participante da Visão Geral do TOTVS Inteligência Tributária no formato Visão de Dados da Tecnologia SmartView"
Return self
 
/*/{Protheus.doc} getData
    Retorna dados do objeto de negócio
    
    @author Fábio Mendonça
    @since 10/03/2025
    @version 1.0
    @param nPage, numérico, Indica a página atual do relatório
    @param oFilter, objeto, Objeto com filtro do SmartView    
    @return object: self:oData
/*/  
Method getData(nPage as numeric, oFilter as object) as object Class OverviewParticipantDetailOperationsBusinessObject
    Local nIParticipant     as numeric
    Local aParticipantItems as array
    Local oParticipant      as object
    Local oFilterParams     as object

    oFilterParams       := oFilter:getParameters()
    aParticipantItems   := FISA318F1SVGetApiData(oFilterParams)

    For nIParticipant := 1 To Len(aParticipantItems)
        oParticipant        := aParticipantItems[nIParticipant]        
        // Executa Parser da informação do Simples Nacional
        oParticipant["sn"]  := IIf(oParticipant["sn"] == "2", Upper(STR0037), Upper(STR0038)) //#"Não" ##"Sim"

        ::appendData(oParticipant)
    Next nIParticipant

    FwFreeObj(oParticipant)
    FwFreeObj(oFilterParams)
Return ::oData
 
/*/{Protheus.doc} getSchema
    Retorna estrutura dos campos

    @author Fábio Mendonça
    @since 10/03/2025
    @version 1.0
    @return object: self:oSchema
/*/  
Method getSchema() as object Class OverviewParticipantDetailOperationsBusinessObject
    Local aSVParams   as array

    // Coleta parâmetros do ON
    aSVParams := FISA318F2SVGetParametersByDashboard("overview")
    aEval(aSVParams, {|aParam| ::addParameter(aParam[1], aParam[2], aParam[3], aParam[4])})

    // Colunas do ON
    ::addProperty("branch"          , STR0010   , "string", STR0010 , "branch"          )   //"Filial"
    ::addProperty("participant"     , STR0022   , "string", STR0022 , "participant"     )   //"Participante"
    ::addProperty("sn"              , STR0023   , "string", STR0023 , "sn"              )   //"S.N."
    ::addProperty("country"         , STR0024   , "string", STR0024 , "country"         )   //"País"
    ::addProperty("uf"              , STR0025   , "string", STR0025 , "uf"              )   //"UF"
    ::addProperty("city"            , STR0026   , "string", STR0026 , "city"            )   //"Cidade"
    ::addProperty("operationalValue", STR0027   , "number", STR0027 , "operationalValue")   //"Valor Operacional"
    ::addProperty("icms"            , STR0028   , "number", STR0028 , "icms"            )   //"ICMS"
    ::addProperty("iss"             , STR0029   , "number", STR0029 , "iss"             )   //"ISS"
    ::addProperty("ipi"             , STR0017   , "number", STR0017 , "ipi"             )   //"IPI"
    ::addProperty("pis"             , STR0030   , "number", STR0030 , "pis"             )   //"PIS"
    ::addProperty("cofins"          , STR0031   , "number", STR0031 , "cofins"          )   //"COFINS"
Return ::oSchema
