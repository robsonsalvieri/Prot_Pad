#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "FISA318.CH"
    
namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} ipiCreditsBusinessObject
    Objeto de Negócio que abre no formato Visão de Dados da Tecnologia SmartView o Card de Créditos de IPI do TOTVS Inteligência Tributária
    
    @author Fábio Mendonça
    @since 13/01/2025
    @version 1.0

    @see https://tdn.totvs.com/pages/releaseview.action?pageId=625448935
/*/
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", name="ipiCreditsBusinessObject", country="BRA", initialRelease="12.1.2310")
Class IpiCreditsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    Public Method new()         as object
    Public Method getData()     as object
    Public Method getSchema()   as object
EndClass

/*{Protheus.doc} new
    Construtor da classe
    
    @author Fábio Mendonça
    @since 13/01/2025
    @version 1.0

    @return object: self
*/  
Method new() as object Class IpiCreditsBusinessObject
    _Super:new()

    ::appendArea(STR0006) //"Monitor Tributário"
    ::setDisplayName(STR0013 + strTran(dToC(date()), "/", "-") + "_" + strTran(time(), ":", "-")) //"CARD_IPI_CREDITOS_"
    ::setDescription(STR0014) //"Abertura dos dados do Card de Créditos de IPI do TOTVS Inteligência Tributária no formato Visão de Dados da Tecnologia SmartView"
Return self
 
/*/{Protheus.doc} getData
    Retorna dados do objeto de negócio
    
    @author Fábio Mendonça
    @since 13/01/2025
    @version 1.0
    @param nPage, numérico, Indica a página atual do relatório
    @param oFilter, objeto, Objeto com filtro do SmartView    
    @return object: self:oData
/*/  
Method getData(nPage as numeric, oFilter as object) as object Class IpiCreditsBusinessObject
    Local nIPeriod      as numeric
    Local nIBranch      as numeric
    Local nICfop        as numeric
    Local nIProduct     as numeric
    Local aCreditItems  as array
    Local oPeriod       as object
    Local oBranch       as object
    Local oCfop         as object
    Local oProduct      as object
    Local oFilterParams as object
    Local oCreditItems  as object

    oFilterParams   := oFilter:getParameters()
    aCreditItems    := FISA318F1SVGetApiData(oFilterParams)
    oCreditItems    := JsonObject():new()

    For nIPeriod := 1 To Len(aCreditItems)
        oPeriod                 := aCreditItems[nIPeriod]
        oCreditItems["period"]  := oPeriod["levelDescription"]

        For nIBranch := 1 To Len(oPeriod["detail"])
            oBranch                 := oPeriod["detail"][nIBranch]
            oCreditItems["branch"]  := oBranch["levelDescription"]

            For nICfop := 1 To Len(oBranch["detail"])
                oCfop                 := oBranch["detail"][nICfop]
                oCreditItems["cfop"]  := oCfop["levelDescription"]

                For nIProduct := 1 To Len(oCfop["detail"])
                    oProduct                := oCfop["detail"][nIProduct]
                    oCreditItems["product"] := oProduct["levelDescription"]
                    oCreditItems["value"]   := oProduct["value"]

                    ::appendData(oCreditItems)
                Next nIProduct
            Next nICfop
        Next nIBranch
    Next nIPeriod

    FwFreeObj(oPeriod)
    FwFreeObj(oBranch)
    FwFreeObj(oCfop)
    FwFreeObj(oProduct)
    FwFreeObj(oFilterParams)
    FwFreeObj(oCreditItems)
Return ::oData
 
/*/{Protheus.doc} getSchema
    Retorna estrutura dos campos

    @author Fábio Mendonça
    @since 13/01/2025
    @version 1.0
    @return object: self:oSchema
/*/  
Method getSchema() as object Class IpiCreditsBusinessObject
    Local aSVParams   as array

    // Coleta parâmetros do ON
    aSVParams := FISA318F2SVGetParametersByDashboard("ipi")
    aEval(aSVParams, {|aParam| ::addParameter(aParam[1], aParam[2], aParam[3], aParam[4])})

    // Colunas do ON
    ::addProperty("period"  , STR0009   , "string", STR0009 , "period"  ) //"Período"
    ::addProperty("branch"  , STR0010   , "string", STR0010 , "branch"  ) //"Filial"
    ::addProperty("cfop"    , STR0015   , "string", STR0015 , "cfop"    ) //"CFOP"
    ::addProperty("product" , STR0016   , "string", STR0016 , "product" ) //"Produto"
	::addProperty("value"   , STR0017   , "number", STR0017 , "value"   ) //"IPI"
Return ::oSchema
