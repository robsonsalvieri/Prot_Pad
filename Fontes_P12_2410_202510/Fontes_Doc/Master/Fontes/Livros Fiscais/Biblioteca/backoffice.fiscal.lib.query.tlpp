#include 'tlpp-core.th'

Namespace totvs.protheus.backoffice.fiscal.lib

/*/{Protheus.doc} FisQuery
    Classe responsável por executar queries.

    @type class
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
    @param jQuery, json, contém a query e o bind a serem executados.
/*/
Class FisQuery
    Data jQuery         as json
    Data oStatement     as object
    Data cAlsQry        as character
    Data cFinalQuery    as character

    //ProtheusDOC de métodos devem ser efetuados na implementação.
    Public Method new() CONSTRUCTOR
    Public Method destroy()

    Public Method setQuery()        //indica query a ser executada
    Public Method setBind()         //indica bind a ser incorporado na query
    Public Method executeQuery()    //executa a query via OpenAlias
    Public Method executeEscalar()  //executa a query via ExecEscalar
    Public Method getQuery()        //retorna a query
    Public Method getBind()         //retorna o tipo de varíavel do bind
    Public Method getQueryResult()  //retorna o resultado da query

    Private Method prepareQuery()   //realiza preparativos na query pré-execução
EndClass

/*/{Protheus.doc } Method new()
    Método construtor da classe
/*/
Method new() Class FisQuery
    ::jQuery := JsonObject():New()
Return

/*/{Protheus.doc } Method destroy()
    Método para desconstruir as variáveis inicializadas da classe
/*/
Method destroy() class FisQuery
    If (Valtype(self:jQuery) == "J")
        FwFreeVAR(self:jQuery)
    EndIf
    
    If Valtype(self:oStatement) == "O"
        ::oStatement:Destroy()
        ::oStatement := nil
    ENDIF

    if !Empty(self:cAlsQry) .and. select(self:cAlsQry) > 0
        (self:cAlsQry)->(dbCloseArea())
    EndIf

Return

/*/{Protheus.doc } Method setQuery(cQuery)
    Método responsável por indicar a query a ser executada. 

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
    @param cQuery, string, contém a query a ser executada.
/*/
Method setQuery(cQuery) class FisQuery
    ::jQuery["query_string"] := cQuery
Return


/*/{Protheus.doc } Method setBind(aBind)
    Método responsável por indicar o(s) bind(s) da query a ser executada. 

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
    @param aBind, array, contém o(s) bind(s) que será(ão) incrementado(s) à query.
/*/
Method setBind(aBind) class FisQuery
    ::jQuery["bind"] := aBind
Return

/*/{Protheus.doc } Method prepareQuery()
    Método responsável por executar a query. 

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
    @param lChangeQuery, logic, decide se executa changequery na string da query.
/*/
Method prepareQuery(lChangeQuery) class FisQuery
    local nX            as numeric

    Default lChangeQuery    := .F.

    IF ::jQuery:HasProperty('query_string')
        IF lChangeQuery
            ::jQuery["query_string"] := changequery(::jQuery["query_string"])
        EndIf

        ::oStatement := FwExecStatement():New(::jQuery["query_string"])
        IF ::jQuery:HasProperty('bind')
        
            For nX := 1 to Len(self:jQuery["bind"])
                ::getBind(nX, ::jQuery["bind"][nX][1],::jQuery["bind"][nX][2])
            Next nX
        EndIf
    EndIf
Return

/*/{Protheus.doc } Method executeQuery()
    Método responsável por executar a query. 

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
    @param lChangeQuery, logic, decide se executa changequery na string da query.
/*/
Method executeQuery(lChangeQuery) class FisQuery
    Default lChangeQuery    := .F.
    
    ::prepareQuery(lChangeQuery)
    ::cAlsQry	    := ::oStatement:OpenAlias()

Return ::cAlsQry


/*/{Protheus.doc } Method executeEscalar()
    Método responsável por executar executeEscalar. 

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
    @param lChangeQuery, logic, decide se executa changequery na string da query.
/*/
Method executeEscalar(lChangeQuery, cFieldName) class FisQuery
    Default lChangeQuery    := .F.
    Default cFieldName      := ""

    ::prepareQuery(lChangeQuery)

Return ::oStatement:ExecScalar(cFieldName)

/*/{Protheus.doc } Method getQuery()
    Método responsável por retornar a query gerada. 

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
/*/
Method getQuery() class FisQuery
    ::cFinalQuery := ::oStatement:GetFixQuery()
Return ::cFinalQuery


/*/{Protheus.doc } Method getBind(nParam,cType,cBind)
    Método responsável por retornar o tipo das variáveis do bind. 

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
    @param nParam, numeric, armazena a ordem do parâmetro de bind.
    @param cType, array, contém o tipo (valtype) da variável.
    @param cBind, array, contém a varíavel que será bindada na query.
/*/
Method getBind(nParam,cType,cBind) Class FisQuery
    
    DO CASE
        CASE cType == "C"  //seta a string caractere na condição WHERE da query
            ::oStatement:SetString(nParam, cBind)
        CASE cType == "N"  //seta a string numérica na condição WHERE da query
            ::oStatement:SetNumeric(nParam, cBind)
        CASE cType == "A"  //seta o array como IN () na condição WHERE da query
            ::oStatement:SetIn(nParam, cBind)
        CASE cType == "D"  // seta a data na condição WHERE da query
            ::oStatement:SetDate(nParam, cBind)
        OTHERWISE
            ::oStatement:SetUnsafe(nParam, cBind)
    ENDCASE
Return


/*/{Protheus.doc } Method getQueryResult()
    Método responsável incrementar JSON contendo o retorno da query. 

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
/*/
Method getQueryResult() class FisQuery
    Local nX            as numeric
    Local jQueryResult  := JsonObject():New() as json
    Local aQueryResult  := {} as array
    Local aStruAlsQry   := {} as array

    If !Empty(self:cAlsQry)
        dbSelectArea(self:cAlsQry)
        aStruAlsQry := (self:cAlsQry)->(dbStruct())
        While (self:cAlsQry)->(!Eof())
            For nX := 1 to Len(aStruAlsQry)
                aAdd(aQueryResult, {aStruAlsQry[nx][1],(self:cAlsQry)->&(aStruAlsQry[nx][1])})
            Next nX
            (self:cAlsQry)->( dbSkip())
        EndDo
        jQueryResult["query_result"] := aQueryResult
    EndIf
Return jQueryResult
