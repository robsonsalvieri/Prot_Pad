
#INCLUDE "PROTHEUS.CH"
#Include "FiveWin.ch"
#INCLUDE "Mata952.ch"
#Include "RwMake.ch"
#Include "FWCommand.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "MATA95xDef.ch"


STATIC aApurSX2		:=	LoadX2Apur()
STATIC aApurSX3		:=	LoadX3Apur()
STATIC aExistBloc	:=	LoadPEApur()
STATIC aFindFunc	:=  LoadXFFApur()	
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA952  ³ Autor ³ Juan Jose Pereira     ³ Data ³ 09/04/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Apuracao de IPI                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MATA952(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAFIS                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Marcos Simidu³15/07/98³Monica³Acerto var. nMoeda p/ nMoedTit para     ³±±
±±³              ³15/07/98³Monica³gravar SE2 e os Lanctos. Padronizados.  ³±±
±±³ Wagner       ³24/11/98³Polic.³Criar filial de / ate                   ³±±
±±³ Andreia      ³02/12/99³25406A³Aumentar o tamanho da pergunta ano da   ³±±
±±³              ³        ³      ³apuracao para 4 digitos.                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION MATA952(lAutomato,nOpcApu)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL nOpc			 := 0
Local oDlg
Local OldAlias 	 := alias()
LOCAL cTitulo		 := ""
Local cText1		 := ""
Local cText2		 := ""
LOCAL aCAP      	 := {STR0001,STR0002,STR0003} //"Confirma"###"Abandona"###"Parƒmetros"
LOCAL cSvScrMenu 	 := ""
Local cPerg		 := "MTA952"
Local cProgram	 := "MATA952"
Local lConfApur   := SuperGetMV("MV_CONFAPU",.F.,.F.)
Local cAlsIPIs		:= "IPIDEB"
Local cTempIPIs		:= "IPIDEBITO"+AllTrim(Str(ThreadID()))
Local cAlsIPIe		:= "IPICRD"
Local cTempIPIe		:= "IPICREDITO"+AllTrim(Str(ThreadID()))
Local cStart    := Time()

Default lAutomato	 :=	.F.
Default nOpcApu 	 :=	2

If ( !AMIIn(9) )
	Return
EndIf

if lConfApur
	DelTempIPI()
	/*CrTempApu ("IP", cAlsIPIs, cTempIPIs)
	CrTempApu ("IP", cAlsIPIe, cTempIPIe)*/
EndIf

dbSelectArea(OldAlias)

If lAutomato
	A952Apuracao(cPerg,cProgram,lAutomato,nOpcApu)
Else

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Janela Principal                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cTitulo:=STR0004 //"Apura‡„o de IPI"
	cText1:=STR0005 //"Este programa faz a Apura‡„o de IPI, conforme parƒmetros "
	cText2:=STR0006 //"informados pelo usu rio, dever  ser executado em modo "
	cText3:=STR0007 //"mono-us rio."

	While .t.
		nOpc	:=	0
		DEFINE MSDIALOG oDlg TITLE OemtoAnsi(cTitulo) FROM  165,145 TO 315,495 PIXEL OF oMainWnd
		@ 03, 10 TO 43, 165 LABEL "" OF oDlg  PIXEL
		@ 10, 15 SAY OemToAnsi(cText1) SIZE 150, 8 OF oDlg PIXEL
		@ 20, 15 SAY OemToAnsi(cText2) SIZE 150, 8 OF oDlg PIXEL
		@ 30, 15 SAY OemToAnsi(cText3) SIZE 150, 8 OF oDlg PIXEL
		DEFINE SBUTTON FROM 50, 082 TYPE 5 ACTION (nOpc:=3,oDlg:End()) ENABLE OF oDlg
		DEFINE SBUTTON FROM 50, 111 TYPE 1 ACTION (nOpc:=1,oDlg:End()) ENABLE OF oDlg
		DEFINE SBUTTON FROM 50, 140 TYPE 2 ACTION (nOpc:=2,oDlg:End()) ENABLE OF oDlg
		ACTIVATE MSDIALOG oDlg		
		
		//conout(Alltrim(Str(ThreadID())) + " Inicio do processamento MATA952 Alterado:  " + Time())	
		Do Case
			Case nOpc==1
				A952Apuracao(cPerg,cProgram,lAutomato,nOpcApu)
				nOpc :=0
			Case nOpc==3
				Pergunte(cPerg,.T.)
				nOpc :=0
			Loop
		EndCase
		Exit
	End
EndIf

if lConfApur
	DelTempIPI()
EndIf

//conout(Alltrim(Str(ThreadID())) + " Fim do processamento MATA952 Alterado:  " + Time())	
//conout(Alltrim(Str(ThreadID()))+  " Tempo do Processamento MATA952 Alterado :  " + ElapTime(cStart, Time()))

RETURN (Nil)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ FIM DA FUNCAO                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A952Apuracao³ Autor ³ Juan Jose Pereira   ³ Data ³ 09/04/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua Apuracao                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA952                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC FUNCTION A952Apuracao(cPerg,cProgram,lAutomato,nOpcApu)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aDatas	:={}
Local nI 		:= 0
Local aRotinas 	:= {}
Local lContinua	:= .F.
Local cSvScr 	:= ""
Local cArqApur 	:= ""        
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Lancamentos padronizado da apuracao                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cLcPadTit := Substr(GetMv("MV_LPADIPI"),1,3)
Local cLcPadExt := Substr(GetMv("MV_LPADIPI"),5,3)
Local nMes         := 0
Local nAno         := 0
Local cNrLivro     := ""
Local nApuracao    := 0
Local nPeriodo     := 0
Local cImp         := "IP"
Local cImposto     := "IPI"
Local cVenc        := GetMV("MV_IPIVENC")
Local cDia         := ""
Local dDtIni
Local dDtFim
Local dDtVenc
Local aAreaSE2
Local lUsaSped  	:= SuperGetMv("MV_USASPED",,.T.) .And. aApurSX2[AI_CDP] .And. aApurSX2[AI_CCK] 
Local aApuIPI      := {}
Local aMensIPI     := {}
Local cConsfil     := ""
Local aCDAIPI      := {}

Default nOpcApu    := 2

Private aApuracao  := {}
Private aGetApur   := {}
Private aObserv    := {}
Private cArqAnt    := ""
Private nVlrTitulo := 0
Private aDadIC     := {}
Private cCodRetIPI := ""
Private aLisFil    := {}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define parametros                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Parametros do Programa                                                    ³
//³ mv_par01 -> Mes da Apuracao ?                                             ³
//³ mv_par02 -> Ano da Apuracao ?                                             ³
//³ mv_par03 -> Livro Selecionado ?                                           ³
//³ mv_par04 -> Apuracao ? Decendial / Quinzenal / Mensal / Semestral / Anual ³
//³ mv_par05 -> Periodo ? 1o.Periodo / 2o.Periodo / 3o.Periodo                ³
//³ mv_par06 -> Arquivo com apuracao do periodo anterior ?                    ³
//³ mv_par07 -> Moeda do Titulo?                                              ³
//³ mv_par08 -> Gera Titulo ? Sim/Nao                                         ³
//³ mv_par09 -> Mostra Lancamento Contabil ? Sim/Nao                          ³
//³ mv_par10 -> Considera filiais          ? Sim/Nao                          ³
//³ mv_par11 -> Da Filial                                                     ³
//³ mv_par12 -> Ate a Filial                                                  ³
//³ mv_par13 -> Tipo de Apuracao ? Normal / Por NCM                           ³
//³ mv_par14 -> Percent. Cred.Pres.?           								  |
//| mv_par15 -> Código de Recolhimento de IPI                                 ³
//| mv_par16 -> Seleciona filiais para utilizacao da funcao MatFilCalc?Sim/Nao³
//| mv_par17 -> Aglutina por CNJP + IE ?Sim/Nao                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(cPerg,.f.)
nMes		:=mv_par01
nAno		:=mv_par02
cNrLivro	:=mv_par03
nApuracao	:=mv_par04
nPeriodo	:=mv_par05
cArqAnt		:=mv_par06
nMoedTit	:= If(Trim(Str(mv_par07))$"12345",mv_par07,1)
lTitulo		:=(mv_par08==1)
lContab		:=(mv_par09==1)
cConsfil		:= mv_par10
aDatas		:= DetDatas(nMes,nAno,nApuracao,nPeriodo)
dDtIni		:= aDatas[1]
dDtFim		:= aDatas[2]
dDtVenc		:= DataValida(aDatas[2]+1,.f.)                               
lNCM   		:=(mv_par13==2)
nPerCrd     := IIF(Empty(mv_par14),0,mv_par14)
cArqApur 	:= NmArqApur(cImp,nAno,nMes,nApuracao,nPeriodo,cNrLivro,lNCM) 
cCodRetIPI  := mv_par15
lFiliais    := If(mv_par16==1,.T.,.F.)
lFilAgl	    := If(mv_par17==1,.T.,.F.)  
aLisFil     := {}
  
If lFiliais //Seleciona Filial = Sim
	cConsfil :=1	 
	 If lFilAgl  //Aglutina Cnpj = Sim 
	    aLisFil := MatFilCalc(lFiliais,,,lFilAgl,,2)
	 Else
	    aLisFil := MatFilCalc(lFiliais)
	 Endif
Elseif !Empty(mv_par11) .And. !Empty(mv_par12)
	 aLisFil:={{.T.,mv_par11},{.T.,mv_par12}} 
Else
	 aLisFil:={{.T.,cFilAnt}}
EndIf
                        
If nAno >= 2004                                                               
	cDia    := Substr(cVenc,1,2)
	dDtVenc := DataValida(aDatas[2]+Val(cDia),.F.)                               
Else
	dDtVenc		:= DataValida(aDatas[2]+1,.f.)                               
Endif


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se ja foi feita apuracao para este periodo             ³
//³MV_USASPED - Indica se usa SPED                                 ³
//³se sim (.T.), deve buscar informacoes de apuracao da tabela CDH ³
//³se nao (.F.), busca dos arquivos de apuracao                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If lUsaSped

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Rotinas da apuracao                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 

	aRotinas:={	{||CkApurCDP(cImp,nAno,nMes,nApuracao,nPeriodo,cNrLivro,cImposto,aDadIC,nMoedTit,cLcPadExt,cArqApur,{},cProgram, ,lTitulo,dDtIni,nOpcApu,lAutomato)},;	// Verifica se existe apuracao
			{||ShowApur(cConsfil,mv_par11,mv_par12,cImposto,cImp,dDtIni,dDtFim,cNrLivro,lNCM,nApuracao,nPerCrd,,aLisFil,lFilAgl,@aMensIPI,lAutomato,aCDAIPI)},; 	// Efetua apuracao e exibe
			{||ViewResumo(cImposto,cArqAnt,cImp,dDtIni,dDtFim,cNrLivro,@aMensIPI,lAutomato,aCDAIPI)},;	// Exibe resumo da apuracao
			{||ApurObserv(cImposto,dDtFim,@dDtVenc,lAutomato)},;	// Entrada de observacoes e vencimento do titulo
			{||GravaTit(lTitulo,nVlrTitulo,cImposto,cImp,cLcPadTit,dDtIni,dDtFim,dDtVenc,nMoedTit,.F.,nMes,nAno,0,0,cProgram,lContab,,,,,,cCodRetIPI,,,,,,,,,,,,,,,,,,,,,,,,lAutomato)},;// Gera titulo a pagar e faz lancamento contabil
			{||GravaApu(cImp,nAno,nMes,nApuracao,nPeriodo,cNrLivro,dDtIni,dDtFim,nMoedTit,lNCM)},;  	// Grava arquivo de apuracao
			{||GrvApurCDP(nApuracao,nPeriodo,cNrLivro,dDtIni,dDtFim,nMoedTit,lNCM,mv_par10,mv_par11,mv_par12)}} 	  	// Grava tabela de apuracao
			
Else

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Rotinas da apuracao                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
	
	aRotinas:={	{||CheckApur(cImp,nAno,nMes,nApuracao,nPeriodo,cNrLivro,cImposto,aDadIC,nMoedTit,cLcPadExt,cArqApur,{},cProgram, ,lTitulo,,,,nOpcApu:=@nOpcApu,lAutomato)},;	// Verifica se existe apuracao
			{||ShowApur(cConsfil,mv_par11,mv_par12,cImposto,cImp,dDtIni,dDtFim,cNrLivro,lNCM,nApuracao,nPerCrd,,aLisFil,lFilAgl,,lAutomato)},; 	// Efetua apuracao e exibe	
			{||ViewResumo(cImposto,cArqAnt,cImp,dDtIni,dDtFim,cNrLivro,,lAutomato)},;	// Exibe resumo da apuracao						
			{||ApurObserv(cImposto,dDtFim,@dDtVenc,lAutomato)},;	// Entrada de observacoes e vencimento do titulo
			{||GravaTit(lTitulo,nVlrTitulo,cImposto,cImp,cLcPadTit,dDtIni,dDtFim,dDtVenc,nMoedTit,.F.,nMes,nAno,0,0,cProgram,lContab,,,,,,cCodRetIPI,,,,,,,,,,,,,,,,,,,,,,,,lAutomato)},;// Gera titulo a pagar e faz lancamento contabil
			{||GravaApu(cImp,nAno,nMes,nApuracao,nPeriodo,cNrLivro,dDtIni,dDtFim,nMoedTit,lNCM,,Iif(nOpcApu == 2 , 1 , Iif( nOpcApu == 3 , 2 , 1 )))}}	  	// Grava arquivo de apuracao
			
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa rotinas da apuracao em sequencia                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nI:=1 to Len(aRotinas)
	lContinua:=Eval(aRotinas[nI])
	If nI== 5 // GravaTit
		If len(mv_par15) == 4
			dbSelectArea("SX5")
			SX5->(dbSetOrder(1))
			SX5->(dbSeek(xFilial("SX5")+"53"+"IPI")) 
		    
			aAreaSE2 := SD2->(GetArea())
			dbSelectArea("SE2")
			SE2->(dbSetOrder(1))
			If dbSeek(TRIM(xFilial("SE2")+cImposto+ X5Descri()))
  		   		RecLock("SE2",.F.)
				SE2->E2_CODRET := mv_par15
				MsUnlock()
			EndIf
			RestArea(aAreaSE2)
		End If
	EndIf
	If !lContinua
		Exit
	Endif
Next nI

If aExistBloc[PE_A952CONT] 
    aApuIPI:= FisApur(cImp,nAno,nMes,nApuracao,nPeriodo,cNrLivro,.F.,aApuIPI,nMoedTit,.F.,"")
	Execblock("A952CONT",.F.,.F.,aApuIPI) 	
EndIf

RETURN (NIL)


/*Conferencia Apuração IPI*/
Function ConfApIPI()
	
Local lRet := .T.
Local cTitulo     := "Conferência de Apuração de IPI"
Local oDlgCNF     := NIL
Local oFont6   	  := NIL
Local lConfApur   := SuperGetMV("MV_CONFAPU",.F.,.F.)

If lConfApur
	// Define fonte para o MsDialog
	DEFINE FONT oFont6 NAME "Arial" BOLD
	
	//Apresenta as opcoes na tela do usuario
	DEFINE MSDIALOG oDlgCNF FROM 264,182 TO 441,712 TITLE cTitulo OF oDlgCNF PIXEL
	@ 004,010 TO 082,157 LABEL "" OF oDlgCNF PIXEL
	
	@ 015,035 SAY "APURAÇÃO de IPI                 "   OF oDlgCNF PIXEL Size 150,010 FONT oFont6 COLOR CLR_HBLUE
	@ 035,017 SAY "Consulta notas fiscais de debito e crédito "   OF oDlgCNF PIXEL Size 150,010 FONT oFont6 COLOR CLR_BLACK
	@ 045,017 SAY "para conferência dos valores apurados.     "   OF oDlgCNF PIXEL Size 150,010 FONT oFont6 COLOR CLR_BLACK

	@ 20,167 BUTTON "Consulta Débito " 	SIZE 045,012 ACTION FWMsgRun(, {|| ApurIPISai() }, "Processando", "Consultando Notas Fiscais...") OF oDlgCNF PIXEL // "Aguarde" ## "Consultando Notas Fiscais..."
	@ 20,215 BUTTON "Consulta Crédito" 	SIZE 045,012 ACTION FWMsgRun(, {|| ApurIPIEnt() }, "Processando", "Consultando Notas Fiscais...") OF oDlgCNF PIXEL
	@ 70,191 BUTTON "Sair"    			SIZE 045,012 ACTION Close(oDlgCNF) OF oDlgCNF PIXEL
	
	ACTIVATE MSDIALOG oDlgCNF CENTERED
Else
	MsgAlert("O parametro MV_CONFAPU esta desabilitado ou não existe na base de dados") //"O parametro MV_CONFAPU esta desabilitado"
	lRet := .F.
EndIf

Return .T.

Static Function ApurIPISai()

Local aEnableButtons := {{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.T.,Nil},{.T.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil}} //"Confirmar"###"Fechar"
Local lRet:=.T.
Local cAlsIPIs		:= "IPIDEB"
Local cTempIPIs		:= "IPIDEBITO"+AllTrim(Str(ThreadID()))

If TCCanOpen(cTempIPIs)
	dbUseArea( .T. ,__cRdd , cTempIPIs , cAlsIPIs , .T. , .F. )
	( cAlsIPIs )->( dbClearIndex() , dbSetIndex( cTempIPIs + '_01' ) )

	//Chama a view da conferencia de apuração
	If aFindFunc[FF_MATA952A]
		FWExecView("Conferência de Apuração de IPI: Débito do Imposto", 'mata952a', MODEL_OPERATION_VIEW, /*oDlg*/, {|| .T. } ,/*bOk*/ , /*nPercReducao*/, aEnableButtons, /*bCancel*/ , , /*cToolBar*/, /*oModel*/ ) // Apuração de IPI
	Elseif !aFindFunc[FF_MATA952A]
		MsgAlert("A rotina MATA952a não existe em seu ambiente") //"A rotina MATA952a não foi compilada em seu ambiente"
		lRet := .F.
	EndIf
	( cAlsIPIs )->(DbCloseArea())
EndIf

Return(lRet)
	
// Consulta valores de cr¨¦dito - Apura??o IPI
Static Function ApurIPIEnt()

Local aEnableButtons := {{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.T.,Nil},{.T.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil}} //"Confirmar"###"Fechar"
Local lRet:=.T.
Local cAlsIPIe		:= "IPICRD"
Local cTempIPIe		:= "IPICREDITO"+AllTrim(Str(ThreadID()))

If TCCanOpen(cTempIPIe)
	dbUseArea( .T. ,__cRdd , cTempIPIe , cAlsIPIe , .T. , .F. )
	( cAlsIPIe )->( dbClearIndex() , dbSetIndex( cTempIPIe + '_01' ) )

	//Chama a view da conferencia de apuração
	If aFindFunc[FF_MATA952A]
		FWExecView("Conferência de Apuração de IPI: Débito do Imposto", 'mata952a', MODEL_OPERATION_VIEW, /*oDlg*/, {|| .T. } ,/*bOk*/ , /*nPercReducao*/, aEnableButtons, /*bCancel*/ , , /*cToolBar*/, /*oModel*/ ) // Apuração de IPI
	Elseif !aFindFunc[FF_MATA952A]
		MsgAlert("A rotina MATA952a não existe em seu ambiente") //"A rotina MATA952a não foi compilada em seu ambiente"
		lRet := .F.
	EndIf
	( cAlsIPIe )->(DbCloseArea())
EndIf

Return(lRet)

//---------------------------------------------------------------------------------------------
/* {Protheus.doc} DelTempIPI
Deleta os arquivos temporarios criados (cApurCred e cApurDeb)

@author    Ronaldo Tapia
@version   12.1.17
@since     28/08/2017
*/
//---------------------------------------------------------------------------------------------

Function DeltempIPI()

Local cTempIPIs		:= "IPIDEBITO"+AllTrim(Str(ThreadID()))
Local cTempIPIe		:= "IPICREDITO"+AllTrim(Str(ThreadID()))

XApDelTempDB (cTempIPIs, .F. )
XApDelTempDB (cTempIPIe, .F. )

Return nil
