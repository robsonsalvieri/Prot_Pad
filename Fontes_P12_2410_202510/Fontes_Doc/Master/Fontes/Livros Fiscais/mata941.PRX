#INCLUDE "mata941.ch" 
#IFDEF WINDOWS
	#include "fivewin.ch"
#ENDIF
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA941  ³ Autor ³ Juan Jos‚ Pereira     ³ Data ³ 30/01/97 		³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Gera arquivo para pre-formatado para Secretarias de Fazenda		³±±                                                                                                           
±±³          ³ Estaduais (operacoes interestaduais)                       		³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ MATA941(void)                                              		³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   		³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     		³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   		³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Marcos Simidu³25/05/98³XXXXXX³ Ajustes no registro 70 e 71.           		³±±
±±³ Marcos Simidu³28/05/98³XXXXXX³ Acertos Invalid Order - AXS.           		³±±
±±³ Marcos Simidu³24/06/98³XXXXXX³ Acertos layout do registro tipo 54.   	 	³±±
±±³ Marcos Simidu³22/07/98³XXXXXX³Acerto Reg. tipo 90.                    		³±±
±±³ Marcos Simidu³22/07/98³XXXXXX³Acerto Reg. tipo 75.                    		³±±
±±³ Marcos Simidu³17/08/98³Hever.³Acertos na gravacao dos discos.         		³±±
±±³ Marcos Simidu³21/08/98³Monica³Reg 70 p/ emit/destin. NF frete.        		³±±
±±³ Wagner       ³21/09/98³Monica³Filtro datas regs 70/71.                		³±±
±±³ Marcos Simidu³16/11/98³18716A³Gerar reg 90 mesmo sem movimentacoes.   		³±±
±±³ Eduardo      ³13/01/98³18780A³Alteracao do Lay-Out Reg11.             		³±±
±±³ Andreia      ³15/01/99³xxxxxx³Filtragem do estado p/ Reg 71.          		³±±
±±³ Wagner       ³29/01/99³xxxxxx³Ponto de Entrada Mta940B                		³±±
±±³ Andreia      ³23/02/99³xxxxxx³inclusao do parametro(Entradas/Saidas)  		³±±
±±³ Andreia      ³16/04/99³15827A³Tratamento do campo telefone no registro		³±±
±±³              ³        ³      ³11, retirando formatacao.               		³±±
±±³ Andreia      ³16/04/99³xxxxxx³Acertos Protheus.                       		³±±
±±³ Andreia      ³21/05/99³21594a³Acerto no campo CGC zerado quando cliente	  	³±±
±±³              ³        ³      ³for consumidor final e possuir CGC e insc.est.³±±
±±³ Andreia      ³25/05/99³XXXXXX³Inclusao do parametro MV_PAR15 que define o  	³±±
±±³              ³        ³      ³o tipo de geracao(Normal/Retificacao)			³±±
±±³ Eduardo      ³10/06/99³21909A³Alteracao do Reg10                            ³±±
±±³ Andreia      ³09/08/99³XXXXXX³Acerto na gravacao do registro tipo 70. 		³±±
±±³              ³        ³      ³Total de registro 70 nao estava de acordo		³±±
±±³              ³        ³      ³com o total gravado no registro 90. Funcao	³±±
±±³              ³        ³      ³A941Grava().                            		³±±
±±³ Andreia      ³20/08/99³23233A³Inclusao do registro tipo 61.           		³±±
±±³ Andreia      ³03/09/99³xxxxxx³Acerto na filtragem do registro tipo 70 e 71	³±±
±±³ Andreia      ³09/11/99³24419A³Acerto na geracao do Registro tipo 54.		³±±
±±³ Andreia      ³12/11/99³xxxxxx³Troca do array aReg54 pelo arq. temp. creg54,	³±±
±±³              ³        ³      ³porque o no de reg.ultrapassa o limite do array.³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Mata941
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Salva a Integridade dos dados de Entrada                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aSave:={Alias(),IndexOrd(),Recno()}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nOpc:=0, oDlg
Local cTitulo,cText1,cText2,cText3
Local aCAP:={STR0001,STR0002,STR0003} //"Confirma"###"Abandona"###"Parƒmetros"

If ( !AMIIn(9) )
	Return
EndIf
Private cPerg:="MTA940"
Private lEnd:=.F.,cReg90:=""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Janela Principal                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cTitulo:=STR0004 //"Arquivo Magn‚tico"
cText1:=STR0005 //"Este programa gera arquivo pr‚-formatado dos lan‡amentos fiscais"
cText2:=STR0006 //"para entrega as Secretarias de Fazenda Estaduais, (opera‡”es"
cText3:=STR0007 //"interestaduais). Dever  ser executado em modo mono-usu rio."
While .T.
	#IFDEF WINDOWS
		DEFINE MSDIALOG oDlg TITLE OemtoAnsi(cTitulo) FROM  165,115 TO 315,525 PIXEL OF oMainWnd
		@ 03, 10 TO 43, 195 LABEL "" OF oDlg  PIXEL
		@ 10, 15 SAY OemToAnsi(cText1) SIZE 180, 8 OF oDlg PIXEL
		@ 20, 15 SAY OemToAnsi(cText2) SIZE 180, 8 OF oDlg PIXEL
		@ 30, 15 SAY OemToAnsi(cText3) SIZE 180, 8 OF oDlg PIXEL
		DEFINE SBUTTON FROM 50, 112 TYPE 5 ACTION (nOpc:=3,oDlg:End()) ENABLE OF oDlg
		DEFINE SBUTTON FROM 50, 141 TYPE 1 ACTION (nOpc:=1,oDlg:End()) ENABLE OF oDlg
		DEFINE SBUTTON FROM 50, 170 TYPE 2 ACTION (nOpc:=2,oDlg:End()) ENABLE OF oDlg
		ACTIVATE MSDIALOG oDlg
	#ELSE
		DrawAdvWindow(cTitulo,08,04,17,76)
		@ 11,08 Say cText1 Color "b/bg"
		@ 12,08 Say cText2 Color "b/bg"
		@ 13,08 Say cText3 Color "b/bg"
		Set Cursor Off
		cSvScrMenu:=SaveScreen(17,05,17,79)
		nOpc:=menuh(aCAP,17,05,"b/w,w+/n,r/w","CAP","",1)
		RestScreen(17,05,17,79,cSvScrMenu)
	#ENDIF
	Do Case
		Case nOpc==1
			#IFDEF WINDOWS
				Processa({||a941Processa()},,,@lEnd)
			#ELSE
				a941Processa()
			#ENDIF
		Case nOpc==3
			Pergunte(cPerg,.t.)
			Loop
	EndCase
	Exit
End
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura area                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea(aSave[1])
dbSetOrder(aSave[2])
dbGoto(aSave[3])

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A941Processa ³ Autor ³ Juan Jos‚ Pereira ³ Data ³ 30/01/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Processamento do MATA941                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC FUNCTION A941Processa
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cAlias:=Alias()
Private cArqTemp
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contadores de registros para gravar no registro 90           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private nReg50:=0,nReg51:=0,nReg53:=0,nReg54:=0,nReg55:=0,;
nReg60:=0,nReg61:=0,nReg70:=0,nReg71:=0,nReg75:=0

Private aReturn :={STR0008, 1,STR0009, 2, 2, 1, "",1 } //"Zebrado"###"Administra‡„o"
Private Nomeprog:="MATA941"
Private wnrel:=NomeProg
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Series utilizadas como cupons fiscais                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cMv_SerCup:=GetMv("MV_SERCUP")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Flag para determinar se ha' notas de conhecimento de frete   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private lConFrete:=.F.
Private cReg54
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega Parametros                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(cPerg,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ mv_par01 - Data Inicial                                      ³
//³ mv_par02 - Data Final                                        ³
//³ mv_par03 - Drive Destino                                     ³
//³ mv_par04 - Nome do Arquivo .TXT                              ³
//³ mv_par05 - Equipamento utilizado                             ³
//³ mv_par06 - Meio magnetico utilizado (FITA/DISQUETE)          ³
//³ mv_par07 - Densidade de gravacao                             ³
//³ mv_par08 - Fator de Bloco de gravacao                        ³
//³ mv_par09 - Estado de Orig./Destino                           ³
//³ mv_par10 - Listar Estado                                     ³
//³ mv_par11 - Nr.do Livro                                       ³
//³ mv_par12 - Cupons Fiscais                                    ³
//³ mv_par13 - Descontos em N.Fiscal                             ³
//³ mv_par14 - Entradas/Saidas/Ambos                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dDtIni:=mv_par01
dDtFim:=mv_par02
cDisco:=Upper(mv_par03)
cArqTxt:=Alltrim(mv_par04)
cEquip:=mv_par05
nMeioMag:=mv_par06
cDensGrv:=mv_par07
cFatBloc:=mv_par08
cEstDest:=mv_par09
lListEst:=(mv_par10==1)
cNrLivro:=mv_par11
lCupons:=(mv_par12==2)
lDesconto:=(mv_par13==2)
nNumReg:=A941NumReg(nMeioMag)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica Parametro MV_RESPFIS                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cContato:=" "
cContato:=GetMV("MV_RESPFIS")

dbSelectArea(cAlias)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria arquivo temporario para armazenar registros             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cArqTemp:=A941CriaTemp()
cReg54 := A941reg54Temp()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega valores a serem gravados                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
A941Apura()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada para casos particulares                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lEnd.and.(existblock("MATA940A"))
	ExecBlock("MATA940A",.F.,.F.)
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava Arquivo                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lEnd
	A941Grava()
Endif

If !lEnd
	A941Relat()
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura ambiente                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea(cArqtemp)
cArqTemp1:=Left(cArqTemp,7)+"A"+OrdBagExt()
cArqTemp2:=Left(cArqTemp,7)+"B"+OrdBagExt()
cArqTemp3:=Left(cArqTemp,7)+"C"+OrdBagExt()
cArqTemp4:=Left(cArqTemp,7)+"D"+OrdBagExt()
dbCloseArea()
Ferase(cArqTemp+".DBF")
Ferase(cArqTemp1)
Ferase(cArqTemp2)
Ferase(cArqTemp3)
Ferase(cArqTemp4)
dbSelectArea(cAlias)

Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A941Apura ³ Autor ³ Juan Jos‚ Pereira     ³ Data ³ 30/01/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava lancamentos em arquivo temporario                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A941Apura()

Local lMata940R:=(existblock("MATA940R"))
Local aTotReg61 := {}

Private lMta940Efs:=(existblock("A940EFS"))
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava registro inicial tipo 10                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SF3")
dbSetOrder(1)
dbSeek(xFilial()+DTOS(dDtIni),.T.)
ProcRegua(LastRec())

If !lEnd
	A941Reg10()
EndIf

If !lEnd
	A941Reg11()
EndIf

While !eof().and.F3_FILIAL==xFilial().and.F3_ENTRADA>=dDtIni.and.F3_ENTRADA<=dDtFim
	IncProc(STR0010) //"Processando registros..."
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Filtros                                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If mv_par14 == 1 						//entrada
		if Val(substr(SF3->F3_CFO,1,1)) >= 5
			SF3->( dbskip() )
			loop
		endif
	Elseif mv_par14 == 2 				//saida
		if Val(substr(SF3->F3_CFO,1,1)) < 5
			SF3->( dbskip() )
			loop
		Endif
	Endif

	If F3_TIPO$"SL".or.If(cNrLivro=="*",.F.,!(F3_NRLIVRO==cNrLivro)).or.;
		If(Empty(cEstDest),.F.,IIf(lListEst,!(F3_ESTADO==cEstDest),(F3_ESTADO==cEstDest))).or.;
			A941IsCupom(SerieNfId("SF3",2,"F3_SERIE"))
			dbSkip()
			Loop
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se foi escriturada nota de conhecimento de frete    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Substr(F3_CFO,2,2)$"61,62,63"
			lConfrete:=.T.
			dbSkip()
			Loop
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Apura registros                                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lEnd
			A941Reg50()
		Endif
		If !lEnd
			A941Reg51()
		Endif
		If !lEnd
			A941Reg53()
		Endif
		If !lEnd
			A941Reg54e75()
		Endif

		A941TotReg61(@aTotReg61)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ponto de entrada para outros tipos de registro               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lEnd.and.lMata940R
			ExecBlock("MATA940R",.F.,.F.)
		Endif
		dbSkip()
	End
	If !lEnd
		A941Reg55()
	Endif
	If !lEnd.and.lCupons
		A941Reg60()
	Endif
	If !lEnd
		A941Reg61(aTotReg61)
	Endif

	If !lEnd.and.lConFrete
		A941Reg70e71()
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Totaliza registros no registro 90                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lEnd
		A941Reg90()
	Endif

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941Grava ³ Autor ³ Juan Jos‚ Pereira     ³ Data ³ 31/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³ Grava arquivo pre-formatado                                ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static function A941Grava()

	Local nHandle := -1,cRegistro
	Local cTotDiscos:=""
	Local aReg90:={},nI,lReg54 := .f., lReg75 := .f.
	Local nRecOld, cReg

	Begin Sequence

	dbSelectArea(cArqTemp)
	dbSetOrder(1)
	If (RecCount() == 0)
		lEnd := .T. ; Break
	EndIf

	dbGoBottom()
	cTotDiscos:=A		// Avalia numero discos necessarios

	dbGotop()
	cCurrDSK := A

	ProcRegua(Lastrec())

	A941AvalDrive(@nHandle,384,Val(cCurrDSk),Val(cTotDiscos))

	While !eof().and.!lEnd
		IncProc(STR0011) //"Gravando registros..."
		If lEnd
			Exit
		Endif

		If (B == "75")
			If (IndexOrd() != 4)
				nRecOld := Recno()
				DbSetOrder(4)
				DbSeek(cCurrDsk+"75",.F.)
				lReg75 := .T.
			EndIf
		ElseIf ( B == "54" )
			If (IndexOrd() != 3)
				nRecOLd := Recno()
				dbSetOrder(3)
				dbSeek(cCurrDsk+"54",.F.)
				lReg54 := .T.
			Endif
		ElseIf ( B == "90" )
			AADD(aReg90,{(B+C+D+E+F+G+Chr(13)+Chr(10)),Recno()})
			dbSkip()
			Loop
		Else
			If (IndexOrd() != 1)
				DbSetOrder(1)
				dbgoTo(nRecOld)
				if (B =="54" .and. lReg54) .or. (B =="75" .and. lReg75)
					cReg := B
					while !eof() .and. cReg == B
						dbskip()
					end
				endif
			EndIf
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Linha a ser gravada                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cRegistro:=B+C+D+E+F+G+Chr(13)+Chr(10)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Grava registro no arquivo                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		FWrite(nHandle,cRegistro,Len(cRegistro))

		dbSkip()

		If !(cCurrDSK == A) .And. !Eof()
			cCurrDSK := A
			A941AvalDrive(@nHandle,Len(cRegistro),Val(cCurrDSK),Val(cTotDiscos))
		EndIf
	EndDo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava arquivo tipo 90.                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aReg90:=Asort(aReg90,,,{|x,y|x[2]<y[2]})

	For nI:=1 To Len(aReg90)
		FWrite(nHandle,aReg90[nI,1],Len(aReg90[nI,1]))
	Next
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fecha arquivo texto                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nHandle >= 0
		FClose(nHandle)
	Endif
	End Sequence

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³a941Relat   ³ Autor ³ Juan Jos‚ Pereira   ³ Data ³ 21/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Imprime relatorio de conferencia                            ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	STATIC FUNCTION a941Relat()
	Local cTela:=""

	Titulo:=STR0012 //"Listagem de acompanhamento"
	cDesc1:=STR0013 //"Listagem de acompanhamento do arquivo pr‚-formatado."
	cDesc2:=""
	cDesc3:=""
	Tamanho:="P"
	Limite:=80
	nTipo:=18
	cabec1:=''
	cabec2:=''
	cabec3:=''
	cbCont:=0
	cbtxt:=Space(10)
	m_pag:=1
	cPerg   :="MTA940"
	cString :="SF3"
	lEnd	:=.f.
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica as perguntas selecionadas                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	pergunte(cperg,.F.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia controle para a funcao SETPRINT                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nLastKey:=0

	wnrel:=SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.F.,"",.F.)
	If nLastKey==27
		Return
	Endif
	SetDefault(aReturn,cString)
	If nLastKey==27
		Return
	Endif

	#IFDEF WINDOWS
		RptStatus({|lEnd|A941RptDetail(@lEnd,wnRel,cString,Tamanho)},titulo)
	#ELSE
		a941RptDetail(@lEnd,wnRel,cString,Tamanho)
	#ENDIF

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941RptDetail³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 21/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Chamada para impressao da listagem                          ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	FUNCTION A941RptDetail(lEnd,wnRel,cString,Tamanho)

	Local nLin,nTotReg
	Local nCnt01:=1,nCnt02:=1
	Local cTipo:=""
	Local nReg90:=1

	dbSelectArea(cArqTemp)
	dbSetOrder(1)

	If dbSeek("90",.F.)
		While !Eof().And.B=="90"
			cReg := F+G
			For nCnt01:=1 To 10	// Nro registros
				If nCnt01>1
					nCnt02:=(nCnt01*10)+1		// Primeiro registro
				Endif
				cTipo:=Subs(cReg,nCnt02,2)
				nCnt02+=2	// Incrementa p/ pegar nro de registros.
				Do Case
					Case cTipo=="50"
						nReg50 := Val(SubSTr( cReg,nCnt02,08 ))
					Case cTipo=="51"
						nReg51 := Val(SubSTr( cReg,nCnt02,08 ))
					Case cTipo=="53"
						nReg53 := Val(SubSTr( cReg,nCnt02,08 ))
					Case cTipo=="54"
						nReg54 := Val(SubSTr( cReg,nCnt02,08 ))
					Case cTipo=="55"
						nReg55 := Val(SubSTr( cReg,nCnt02,08 ))
					Case cTipo=="60"
						nReg60 := Val(SubSTr( cReg,nCnt02,08 ))
					Case cTipo=="61"
						nReg61 := Val(SubSTr( cReg,nCnt02,08 ))
					Case cTipo=="70"
						nReg70 := Val(SubSTr( cReg,nCnt02,08 ))
					Case cTipo=="71"
						nReg71 := Val(SubSTr( cReg,nCnt02,08 ))
					Case cTipo=="75"
						nReg75 := Val(SubSTr( cReg,nCnt02,08 ))
				EndCase
			Next
			nCnt02:=1
			nReg90:=Val(Subs(G,88,1))		// Nro registros tipo 90
			dbSkip()
		EndDo
	EndIf

	cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
	nLin:=Prow()+1
	@ nLin,00 PSAY Replic("*",80)
	nLin++
	nLin++
	@ ++nLin,15 PSAY STR0014+Transform(SM0->M0_CGC,"@R! NN.NNN.NNN/NNNN-99") //"C.G.C.(MF)........: "
	@ ++nLin,15 PSAY STR0015+SM0->M0_INSC //"Inscricao Estadual: "
	@ ++nLin,15 PSAY STR0016+SM0->M0_NOMECOM //"Razao Social......: "
	@ ++nLin,15 PSAY STR0017+SM0->M0_ENDENT //"Endereco..........: "
	@ ++nLin,15 PSAY "                    "+SM0->M0_CIDENT+" - "+SM0->M0_ESTENT+STR0018+Transform(SM0->M0_CEPENT,"@R 99999-999") //" - CEP: "
	nLin++
	nLin++
	@++nLin,15 PSAY STR0019+cEquip //"Equipamento.......: "
	@++nLin,15 PSAY STR0020+If(nMeioMag<=4,STR0021,STR0022) //"Meio Magnetico....: "###"Disquete"###"Fita"
	@ ++nLin,15 PSAY STR0023+cFatBloc //"Fator de Bloco....: "
	nLin++
	nLin++
	@ ++nLin,15 PSAY STR0024+DTOC(dDtIni)+STR0025+DTOC(dDtFim) //"Periodo abrangido.: "###" a "
	nLin++
	@ ++nLin,15 PSAY STR0026+Str(1,8)	   +STR0027 //"Tipo 10...........: "###" Registro"
	@ ++nLin,15 PSAY STR0028+Str(1,8)	   +STR0027 //"Tipo 11...........: "###" Registro"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Imprime somente itens existentes no arq. magnetico           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nReg50>0
		@ ++nLin,15 PSAY STR0029+Str(nReg50,8)	+STR0030 //"Tipo 50...........: "###" Registro(s)"
	Endif
	If nReg51>0
		@ ++nLin,15 PSAY STR0031+Str(nReg51,8)	+STR0030 //"Tipo 51...........: "###" Registro(s)"
	Endif
	If nReg53>0
		@ ++nLin,15 PSAY STR0032+Str(nReg53,8)	+STR0030 //"Tipo 53...........: "###" Registro(s)"
	Endif
	If nReg54>0
		@ ++nLin,15 PSAY STR0033+Str(nReg54,8)	+STR0030 //"Tipo 54...........: "###" Registro(s)"
	Endif
	If nReg55>0
		@ ++nLin,15 PSAY STR0034+Str(nReg55,8)	+STR0030 //"Tipo 55...........: "###" Registro(s)"
	Endif
	If nReg60>0
		@ ++nLin,15 PSAY STR0035+Str(nReg60,8) +STR0030 //"Tipo 60...........: "###" Registro(s)"
	Endif
	If nReg61>0
		@ ++nLin,15 PSAY STR0036+Str(nReg61,8)	+STR0030 //"Tipo 61...........: "###" Registro(s)"
	Endif
	If nReg70>0
		@ ++nLin,15 PSAY STR0037+Str(nReg70,8)	+STR0030 //"Tipo 70...........: "###" Registro(s)"
	Endif
	If nReg71>0
		@ ++nLin,15 PSAY STR0038+Str(nReg71,8)	+STR0030 //"Tipo 71...........: "###" Registro(s)"
	Endif
	If nReg75>0
		@ ++nLin,15 PSAY STR0039+Str(nReg75,8)	+STR0030 //"Tipo 75...........: "###" Registro(s)"
	Endif
	@ ++nLin,15 PSAY STR0040+Str(nReg90,8)	   +STR0041 //"Tipo 90...........: "###" Registros"
	nLin++
	nTotReg:=2+nReg90+nReg50+nReg51+nReg53+nReg54+nReg55+nReg60+nReg61+nReg70+nReg71+nReg75
	@ ++nLin,15 PSAY STR0042+Str(nTotReg)+STR0041 //"Total de registros: "###" Registros"
	nLin++

	If nTotReg>0
		roda(cbcont,cbtxt,tamanho)
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Spool de Impressao                                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	#IFNDEF WINDOWS
		Set Device to Screen
	#ENDIF
	Set Printer to
	If aReturn[5] == 1
		dbcommitAll()
		ourspool(wnrel)
	Endif

	MS_FLUSH()

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941CriaTemp ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 21/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Cria arquivo temporario para armazenar registros            ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941CriaTemp()

	Local cArq01,cArq02,cArq03,cArq04,aCampos:={},cAlias:=Alias(),cArqDB:=""

	AADD(aCampos,{"A","C",002,0})
	AADD(aCampos,{"B","C",002,0})
	AADD(aCampos,{"C","C",014,0})
	AADD(aCampos,{"D","C",010,0})
	AADD(aCampos,{"E","C",004,0})
	AADD(aCampos,{"F","C",008,0})
	AADD(aCampos,{"G","C",088,0})
	AADD(aCampos,{"H","N",012,0})

	cArqDB:=CriaTrab(aCampos)
	cArq01:=Left(cArqDB,7)+"A"
	cArq02:=Left(cArqDB,7)+"B"
	cArq03:=Left(cArqDB,7)+"C"
	cArq04:=Left(cArqDB,7)+"D"
	dbUseArea(.T.,,cArqDB,cArqDB,.T.,.F.)
	IndRegua(cArqDB,cArq01,"B+F",,,STR0043) //"Indexando registros..."
	IndRegua(cArqDB,cArq02,"B+C+D",,,STR0043) //"Indexando registros..."
	IndRegua(cArqDB,cArq03,"A+B+Substr(D,1,8)",,,STR0043) //"Indexando registros..."
	IndRegua(cArqDB,cArq04,"A+B+C+D",,,STR0043) //"Indexando registros..."

	DbSelectArea(cArqDB)
	DbClearIndex()
	DbSetIndex(cArq01+OrdBagExt())
	DbSetIndex(cArq02+OrdBagExt())
	DbSetIndex(cArq03+OrdBagExt())
	DbSetIndex(cArq04+OrdBagExt())

	dbSelectArea(cAlias)

	Return (cArqDB)
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941reg54Temp³Autor ³ Andreia dos Santos  ³ Data ³ 12/11/99 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Cria arquivo temporario para armazenar registros            ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941reg54Temp()
	Local cArq01,aCampos:={},cAlias:=Alias(),cArqDB:=""

	AADD(aCampos,{"NOTA"   	,"C",006,0})
	AADD(aCampos,{"SERIE"  	,"C",TamSx3("F3_SERIE")[1],0})
	AADD(aCampos,{"CLIEFOR"	,"C",006,0})
	AADD(aCampos,{"LOJA"	,"C",002,0})
	AADD(aCampos,{"TIPO"	,"C",001,0})
	AADD(aCampos,{"SDOC"	,"C",003,0})

	cArqDB:=CriaTrab(aCampos)
	cArq01:=Left(cArqDB,7)+"E"
	dbUseArea(.T.,,cArqDB,cArqDB,.T.,.F.)
	IndRegua(cArqDB,cArq01,"NOTA+SERIE+CLIEFOR+LOJA+TIPO",,,STR0043) //"Indexando registros..."

	DbSelectArea(cArqDB)
	DbClearIndex()
	DbSetIndex(cArq01+OrdBagExt())

	dbSelectArea(cAlias)

	Return (cArqDB)

	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941PutTemp  ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Grava dados no arquivo temporario                           ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941PutTemp(cConteudo)

	Local cAlias   :=Alias()
	Local lInclude := .T.

	dbSelectArea(cArqTemp)

	If Left(cConteudo,2) == "75"
		DbSetOrder(2)
		lInclude := !DbSeek(Left(cConteudo,26),.F.)
		DbSetOrder(1)
	EndIf

	If lInclude
		RecLock(cArqTemp,.T.)
		REPLACE B WITH SubStr(cConteudo,01,002)
		REPLACE C WITH SubStr(cConteudo,03,016)
		REPLACE D WITH SubStr(cConteudo,17,026)
		REPLACE E WITH SubStr(cConteudo,27,030)
		REPLACE F WITH SubStr(cConteudo,31,038)
		REPLACE G WITH SubStr(cConteudo,39,126)
		MsUnlock()

		Do Case
			Case B=="50"
				nReg50++
			Case B=="51"
				nReg51++
			Case B=="53"
				nReg53++
			Case B=="54"
				nReg54++
			Case B=="55"
				nReg55++
			Case B=="60"
				nReg60++
			Case B=="61"
				nReg61++
			Case B=="70"
				nReg70++
			Case B=="71"
				nReg71++
			Case B=="75"
				nReg75++
		EndCase
	EndIf

	dbSelectArea(cAlias)

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941FILL     ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Encaixa conteudo em espaco especificado                     ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Function A941FILL(cConteudo,nTam)

	cConteudo:=If(cConteudo==NIL,"",cConteudo)
	If Len(cConteudo)>nTam
		cRetorno:=Substr(cConteudo,1,nTam)
	Else
		cRetorno:=cConteudo+Space(nTam-Len(cConteudo))
	Endif

	Return (cRetorno)
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941AvalDrive³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Avalia drive a ser gravado                                  ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941AvalDrive(nHandle,nBytes,nAtual,nTotal)

	Local nDisco:=0,nHdlCtl:=0,oDlg,cPath:="",cFile,nHdlChk:=0,cArqChk:="MATA941.CHK"
	Local cTitulo:=STR0044 //"Numero Discos"
	Local cMens1:=STR0045+StrZero(nTotal,2)+STR0046 //"Sera(„o) necessario(s) "###" disco(s) formatado(s) e limpo(s),"
	Local cMens2:=STR0047 //"para gerar o(s) arquivo(s) do(s) Livros em Disquete."
	Local cMens3:=STR0048 + StrZero(nAtual,2) + STR0049 + StrZero(nTotal,2) //"Insira o disco numero : "###" de "
	Local aCA:={STR0001,STR0002}, nOpcA:=2, nOpcB:=2 //"Confirma"###"Abandona"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fecha Arquivo Aberto Anteriormente                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nHandle >= 0
		FClose(nHandle)
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Mostra o Disco Atual                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	#IFDEF WINDOWS
		DEFINE MSDIALOG oDlg TITLE OemtoAnsi(cTitulo) FROM  165,115 TO 315,525 PIXEL OF oMainWnd
		@ 03, 10 TO 43, 195 LABEL "" OF oDlg  PIXEL
		@ 10, 15 SAY OemToAnsi(cMens1) SIZE 180, 8 OF oDlg PIXEL
		@ 20, 15 SAY OemToAnsi(cMens2) SIZE 180, 8 OF oDlg PIXEL
		@ 30, 15 SAY OemToAnsi(cMens3) SIZE 180, 8 OF oDlg PIXEL
		DEFINE SBUTTON FROM 50, 141 TYPE 1 ACTION (nOpcA:=1,oDlg:End()) ENABLE OF oDlg
		DEFINE SBUTTON FROM 50, 170 TYPE 2 ACTION (nOpcA:=2,oDlg:End()) ENABLE OF oDlg
		ACTIVATE MSDIALOG oDlg
	#ELSE
		DrawAdvWindow(cTitulo,08,04,17,76)
		@ 11,08 Say cMens1 Color "b/bg"
		@ 12,08 Say cMens2 Color "b/bg"
		@ 13,08 Say cMens3 Color "b/bg"
		Set Cursor Off
		cSvScrMenu:=SaveScreen(17,05,17,79)
		nOpcA:=menuh(aCA,17,05,"b/w,w+/n,r/w","CA","",1)
		RestScreen(17,05,17,79,cSvScrMenu)
	#ENDIF
	If !(nOpcA==1)
		lEnd:=.T.
		Break
	Endif

	While (nOpcA==1) .And. (nOpcB==2)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Define disco a ser gravado                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(cDisco)
			nDisco:=Asc(cDisco)-Asc("A")+1
			cPath:=cDisco+":\"
		Else
			cPath:=""
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se drive ‚ avaliavel                                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cArqChk:=cPath+cArqChk
		nHdlChk:=MsFCreate(cArqChk)
		If nHdlChk < 0
			nOpcB:=Aviso(STR0050,STR0051,{STR0002,STR0052}) //"Drive"###"Drive n„o est  dispon¡vel."###"Abandona"###"Continua"
			If nOpcB==2
				Loop
			Else
				lEnd := .T. ; Break
			Endif
		Else
			FClose(nHdlChk) ; Ferase(cArqChk)
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica espaco no disco                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If DiskSpace(nDisco)<nBytes
			cMsg := STR0053 //"N„o h  espa‡o no disco para grava‡„o;"
			cMsg += STR0054 //" Troque disquete para continuar..."
			nHdlChk := -1

			nOpcB:=Aviso(STR0050,cMsg,{STR0055,STR0056}) //"Drive"###"Abandonar"###"Continuar"
			If nOpcB==2
				Loop
			Else
				lEnd := .T. ; Break
			Endif
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Cria arquivo                                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cFile:=cPath+cArqTxt+".T"+Strzero(nAtual,2)
		nHandle:=MsFCreate(cFile)
		If nHandle < 0
			nOpcB:=Aviso(STR0050,STR0057,{STR0002,STR0052}) //"Drive"###"Drive n„o est  pronto."###"Abandona"###"Continua"
			If nOpcB==2
				Loop
			Else
				lEnd := .T. ; Break
			Endif
		EndIf
		Exit
	EndDo

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941IsCupom  ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Verifica se nota fiscal e' um cupom fiscal                  ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941IsCupom(cSerie)
	Return (Alltrim(cSerie)$cMv_SerCup)
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941Reg50    ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Grava registro tipo 50                                      ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941Reg50()

	Local cRegistro:="50",cClieFor

	If Empty(F3_ESPECIE).or.Alltrim(F3_ESPECIE)$"NFF,NF,NFE,NFCEE,NTST"
		cClieFor:=F3_CLIEFOR+F3_LOJA
		If F3_TIPO$"BD"
			If substr(F3_CFO,1,1)>="5"
				SA2->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("F",SA2->A2_CGC,SA2->A2_TIPO)
				cInsc:=SA2->A2_INSCR
			Else
				SA1->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("C",SA1->A1_CGC,SA1->A1_TIPO)
				cInsc:=SA1->A1_INSCR
			Endif
		Else
			If substr(F3_CFO,1,1)>="5"
				SA1->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("C",SA1->A1_CGC,SA1->A1_TIPO)
				cInsc:=SA1->A1_INSCR
			Else
				SA2->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("F",SA2->A2_CGC,SA2->A2_TIPO)
				cInsc:=SA2->A2_INSCR
			Endif
		Endif
		If Empty(cCGC)
			cCGC:=Replic("0",14)
		Endif
		If Empty(cInsc).or.F3_ESTADO=="EX"
			cInsc:="ISENTO"
		Else
			cInsc:=A941INSC(cInsc)
		Endif

		cRegistro+=A941FILL(A941NUM2CHR(Val(cCGC),14,0),14)
		cRegistro+=A941FILL(cInsc,14)
		cRegistro+=A941FILL(DTOS(F3_ENTRADA),8)
		cRegistro+=A941FILL(F3_ESTADO,2)
		cRegistro+=A941FILL(A941NUM2CHR(A941ModNot(F3_ESPECIE),2,0),2)
		cRegistro+=A941FILL(SerieNfId("SF3",2,"F3_SERIE"),3)
		cRegistro+=A941FILL(,2)
		cRegistro+=A941FILL(A941NUM2CHR(Val(F3_NFISCAL),6,0),6)
		cRegistro+=A941FILL(A941NUM2CHR(Val(substr(F3_CFO,1,3)),3,0),3)
		cRegistro+=A941FILL(A941NUM2CHR(F3_VALCONT,13),13)
		cRegistro+=A941FILL(A941NUM2CHR(F3_BASEICM,13),13)
		cRegistro+=A941FILL(A941NUM2CHR(F3_VALICM,13),13)
		cRegistro+=A941FILL(A941NUM2CHR(F3_ISENICM,13),13)
		cRegistro+=A941FILL(A941NUM2CHR(F3_OUTRICM,13),13)
		cRegistro+=A941FILL(A941NUM2CHR(F3_ALIQICM,4),4)
		cRegistro+=A941FILL(If("CANCELADA"$F3_OBSERV,"S","N"),1)

		A941PutTemp(cRegistro)

	Endif

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941ModNot   ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Retorna codigo do modelo da nota fiscal                     ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941ModNot(cEspecie)

	Local cCodigo:="01" // Default Nota Fiscal ou Nota Fiscal Fatura

	Do Case
		Case Alltrim(cEspecie)=="CA"	; cCodigo:="10" // Conh.Aereo
		Case Alltrim(cEspecie)=="CTA" 	; cCodigo:="09" // Conh.Transp.Aquaviario
		Case Alltrim(cEspecie)$"CTR" 	; cCodigo:="08" // Conh.Transp.Aereo
		Case Alltrim(cEspecie)=="NFCEE" ; cCodigo:="06" // Conta de Energia Eletrica
		Case Alltrim(cEspecie)=="NFE" 	; cCodigo:="01" // NF Entrada
		Case Alltrim(cEspecie)=="NFST" 	; cCodigo:="07" // NF Servico de Transporte
		Case Alltrim(cEspecie)=="NFSC" 	; cCodigo:="21" // NF Servico de Comunicacao
		Case Alltrim(cEspecie)=="NFST" 	; cCodigo:="22" // NF Servico de Telecomunicacoes
	EndCase

	Return (Val(cCodigo))
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941NUM2CHR  ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Converte numerico para formato binario                      ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Function A941NUM2CHR(nValor,nTam,nDec)

	Local nInteiro,nDecimal,cInteiro,cDecimal,cValor

	nDec:=If(nDec==NIL,2,nDec)

	nInteiro:=Int(nValor)
	nDecimal:=Abs(nValor-nInteiro)

	cInteiro:=StrZero(nInteiro,nTam-nDec)
	cDecimal:=StrZero(nDecimal*10^nDec,nDec)

	cValor:=cInteiro+cDecimal

	Return (cValor)
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941Reg10    ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Grava registro tipo 10                                      ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941Reg10()

	Local cRegistro:="10",cCGC:=SM0->M0_CGC,cInsc:=SM0->M0_INSC
	Local cCod:="03",cFaxAux:=SM0->M0_FAX,cFax:="",n:=1

	// Retira caracteres do campo cFax
	For n:=1 To Len(cFaxAux)
		If IsDigit(Subs(cFaxAux,n,1))
			cFax+=Subs(cFaxAux,n,1)
		Endif
	Next

	If Empty(cCGC)
		cCGC:=Replic("0",14)
	Endif
	If Empty(cInsc)
		cInsc:="ISENTO"
	Endif

	cRegistro+=A941FILL(A941NUM2CHR(Val(cCGC),14,0),14)			// CGC da Empresa
	cRegistro+=A941FILL(cInsc,14)			// Inscricao Estadual
	cRegistro+=A941FILL(SM0->M0_NOMECOM,35)	// Nome Comercial
	cRegistro+=A941FILL(SM0->M0_CIDENT,30)	// Municipio
	cRegistro+=A941FILL(SM0->M0_ESTENT,2)	// Estado
	cRegistro+=A941FILL(A941NUM2CHR(Val(cFax),10,0),10)		// Fax
	cRegistro+=A941FILL(DTOS(dDtIni),8)		// Data Inicial
	cRegistro+=A941FILL(DTOS(dDtFim),8)		// Data Final

	cCod:=(If(!Empty(cEstDest),"04",cCod))
	cRegistro+=A941FILL(cCod,2)
	cRegistro+=A941FILL(alltrim(str(MV_PAR15)),1)

	A941PutTemp(cRegistro)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Incrementa contador de registros                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	Return

	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941Reg11    ³Autor ³    Marcos Simidu    ³ Data ³ 28/10/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Grava registro tipo 11                                      ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941Reg11()

	Local cRegistro:="11"
	Local aArray:={}
	Local nVirgula:= Rat(",",SM0->M0_ENDENT)
	Local cNumero := AllTrim(SubStr(SM0->M0_ENDENT,nVirgula+1))
	Local nNumero := Val(AllTrim(cNumero))
	Local cEnderec:= PadR(If(nNumero!=0,SubStr(SM0->M0_ENDENT,1,nVirgula-1),SM0->M0_ENDENT),34)
	Local cTelEAux:= SM0->M0_TEL
	Local cTel:=""
	Local n :=0
	// Retira caracteres do campo cTeleAux
	For n:=1 To Len(cTeleAux)
		If IsDigit(Subs(cTeleAux,n,1))
			cTel+=Subs(cTeleAux,n,1)
		Endif
	Next

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Quando nao ha virgula no endereco procura-se o caracter branco³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( nVirgula == 0 )
		nVirgula := Rat(" ",AllTrim(SM0->M0_ENDENT))
		cNumero := AllTrim(SubStr(SM0->M0_ENDENT,nVirgula+1))
		nNumero := Val(AllTrim(cNumero))
		cEnderec:= PadR(If(nNumero!=0,SubStr(SM0->M0_ENDENT,1,nVirgula-1),SM0->M0_ENDENT),34)
	EndIf

	If lMta940Efs
		aArray:=ExecBlock("A940EFS",.F.,.F.)
	Endif

	If Empty(aArray) .Or. !(ValType(aArray) == "A")
		aArray := { cEnderec,;						//	[1]
		nNumero,;						//	[2]
		"000000",;						//	[3]
		PadR(SM0->M0_BAIRENT,15),;	//	[4]
		Val(SM0->M0_CEPENT),;		//	[5]
		cContato,;						//	[6]
		PadR(cTEL,12) }		//	[7]
	Else
		If (Len(aArray) # 7)
			aArray := { cEnderec,;						//	[1]
			nNumero,;						//	[2]
			"000000",;						//	[3]
			PadR(SM0->M0_BAIRENT,15),;	//	[4]
			Val(SM0->M0_CEPENT),;		//	[5]
			cContato,;						//	[6]
			PadR(cTEL,12) }		//	[7]
		Else
			If !(Valtype(aArray[1])=="C")
				aArray[1]:= cEnderec
			Endif
			If !(Valtype(aArray[2])=="N")
				aArray[2]:= nNumero
			Endif
			If !(Valtype(aArray[3])=="C")
				aArray[3]:=Space(22)
			Endif
			If !(Valtype(aArray[4])=="C")
				aArray[4]:=PadR(SM0->M0_BAIRENT,15)
			Endif
			If !(Valtype(aArray[5])=="N")
				aArray[5]:=Val(SM0->M0_CEPENT)
			Endif
			If !(Valtype(aArray[6])=="C")
				aArray[6]:=Space(28)
			Endif
			If !(Valtype(aArray[7])=="N")
				aArray[7]:=PadR(cTEL,12)
			Endif
		EndIf
	EndIf

	cRegistro+=A941FILL(aArray[1],34)							// Logradouro
	cRegistro+=A941FILL(A941NUM2CHR(aArray[2],5,0),05)		// Numero
	cRegistro+=A941FILL(aArray[3],22)							// Complemento
	cRegistro+=A941FILL(aArray[4],15)							// Bairro
	cRegistro+=A941FILL(A941NUM2CHR(aArray[5],8,0),08)		// CEP
	cRegistro+=A941FILL(aArray[6],28)							// Contato
	cRegistro+=A941FILL(A941NUM2CHR(Val(aArray[7]),12,0),12)	// Telefone

	A941PutTemp(cRegistro)

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941Reg51    ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Grava registro tipo 51                                      ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941Reg51()

	Local cRegistro:="51",cClieFor

	If (Empty(F3_ESPECIE).or.Alltrim(F3_ESPECIE)$"NFF,NF,NFE").And.;
		(F3_VALIPI+F3_ISENIPI+F3_OUTRIPI)>0

		cClieFor:=F3_CLIEFOR+F3_LOJA
		If F3_TIPO$"BD"
			If substr(F3_CFO,1,1)>="5"
				SA2->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("F",SA2->A2_CGC,SA2->A2_TIPO)
				cInsc:=SA2->A2_INSCR
			Else
				SA1->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("C",SA1->A1_CGC,SA1->A1_TIPO)
				cInsc:=SA1->A1_INSCR
			Endif
		Else
			If substr(F3_CFO,1,1)>="5"
				SA1->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("C",SA1->A1_CGC,SA1->A1_TIPO)
				cInsc:=SA1->A1_INSCR
			Else
				SA2->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("F",SA2->A2_CGC,SA2->A2_TIPO)
				cInsc:=SA2->A2_INSCR
			Endif
		Endif
		If Empty(cCGC)
			cCGC:=Replic("0",14)
		Endif
		If Empty(cInsc).or.F3_ESTADO=="EX"
			cInsc:="ISENTO"
		Else
			cInsc:=A941INSC(cInsc)
		Endif

		cRegistro+=A941FILL(A941NUM2CHR(Val(cCGC),14,0),14)
		cRegistro+=A941FILL(cInsc,14)
		cRegistro+=A941FILL(DTOS(F3_ENTRADA),8)
		cRegistro+=A941FILL(F3_ESTADO,2)
		cRegistro+=A941FILL(SerieNfId("SF3",2,"F3_SERIE"),2)
		cRegistro+=A941FILL(,2)
		cRegistro+=A941FILL(A941NUM2CHR(Val(F3_NFISCAL),6,0),6)
		cRegistro+=A941FILL(A941NUM2CHR(Val(substr(F3_CFO,1,3)),3,0),3)
		cRegistro+=A941FILL(A941NUM2CHR(F3_VALCONT,13),13)
		cRegistro+=A941FILL(A941NUM2CHR(F3_VALIPI,13),13)
		cRegistro+=A941FILL(A941NUM2CHR(F3_ISENIPI,13),13)
		cRegistro+=A941FILL(A941NUM2CHR(F3_OUTRIPI,13),13)
		cRegistro+=A941FILL(If(substr(F3_CFO,1,1)>="5",SubStr(GetMv("MV_SITFEDE"),01,5),),5)
		cRegistro+=A941FILL(If(substr(F3_CFO,1,1)>="5",SubStr(GetMv("MV_SITFEDE"),07,5),),5)
		cRegistro+=A941FILL(If(substr(F3_CFO,1,1)>="5",SubStr(GetMv("MV_SITFEDE"),13,5),),5)
		cRegistro+=A941FILL(If(substr(F3_CFO,1,1)>="5",SubStr(GetMv("MV_SITFEDE"),19,5),),5)
		cRegistro+=A941FILL(If("CANCELADA"$F3_OBSERV,"S","N"),1)

		A941PutTemp(cRegistro)

	Endif

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941Reg53    ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Grava registro tipo 53                                      ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941Reg53()

	Local cRegistro:="53",cClieFor

	If substr(F3_CFO,1,1)>="5".AND.F3_BASERET+F3_ICMSRET>0

		cClieFor:=F3_CLIEFOR+F3_LOJA
		If F3_TIPO$"BD"
			If substr(F3_CFO,1,1)>="5"
				SA2->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("F",SA2->A2_CGC,SA2->A2_TIPO)
				cInsc:=SA2->A2_INSCR
			Else
				SA1->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("C",SA1->A1_CGC,SA1->A1_TIPO)
				cInsc:=SA1->A1_INSCR
			Endif
		Else
			If substr(F3_CFO,1,1)>="5"
				SA1->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("C",SA1->A1_CGC,SA1->A1_TIPO)
				cInsc:=SA1->A1_INSCR
			Else
				SA2->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("F",SA2->A2_CGC,SA2->A2_TIPO)
				cInsc:=SA2->A2_INSCR
			Endif
		Endif
		If Empty(cCGC)
			cCGC:=Replic("0",14)
		Endif
		If Empty(cInsc).or.F3_ESTADO=="EX"
			cInsc:="ISENTO"
		Else
			cInsc:=A941INSC(cInsc)
		Endif
		cRegistro+=A941FILL(A941NUM2CHR(Val(cCGC),14,0),14)
		cRegistro+=A941FILL(cInsc,14)
		cRegistro+=A941FILL(DTOS(F3_ENTRADA),8)
		cRegistro+=A941FILL(F3_ESTADO,2)
		cRegistro+=A941FILL(A941NUM2CHR(A941ModNot(F3_ESPECIE),2,0),2)
		cRegistro+=A941FILL(SerieNfId("SF3",2,"F3_SERIE"),3)
		cRegistro+=A941FILL(,2)
		cRegistro+=A941FILL(A941NUM2CHR(Val(F3_NFISCAL),6,0),6)
		cRegistro+=A941FILL(A941NUM2CHR(Val(substr(F3_CFO,1,3)),3,0),3)
		cRegistro+=A941FILL(A941NUM2CHR(F3_BASERET,13),13)
		cRegistro+=A941FILL(A941NUM2CHR(F3_ICMSRET,13),13)
		cRegistro+=A941FILL(A941NUM2CHR(F3_DESPESA,13),13)
		cRegistro+=A941FILL(If("CANCELADA"$F3_OBSERV,"S","N"),1)
		cRegistro+=A941FILL(,30)

		A941PutTemp(cRegistro)

	Endif

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941Reg54e75 ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Grava registro tipo 54 e tipo 75                            ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941Reg54e75()

	Local cRegistro,nItem:=1,cClieFor,cCGC,cInsc,lFound

	If !lEnd .and. Alltrim(F3_ESPECIE)$"NFF,NF,NFE"
		if  (cReg54)->(!dbseek(SF3->F3_NFISCAL+SF3->F3_SERIE+SF3->F3_CLIEFOR+SF3->F3_LOJA+if(substr(SF3->F3_CFO,1,1)>='5',"S","E")))
			RecLock(cReg54,.T.)
			(cReg54)->NOTA		:= 	SF3->F3_NFISCAL
			(cReg54)->SERIE		:=	SF3->F3_SERIE
			(cReg54)->SDOC		:=	SerieNfId("SF3",2,"F3_SERIE")
			(cReg54)->CLIEFOR 	:=	SF3->F3_CLIEFOR
			(cReg54)->LOJA		:= 	SF3->F3_LOJA
			(cReg54)->TIPO		:=	if(substr(SF3->F3_CFO,1,1)>='5',"S","E")
			MsUnlock()
            dbSelectArea("SF3")
		else
			return
		Endif

		cClieFor:=F3_CLIEFOR+F3_LOJA
		If F3_TIPO$"BD"
			If substr(F3_CFO,1,1)>="5"
				SA2->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("F",SA2->A2_CGC,SA2->A2_TIPO)
				cInsc:=SA2->A2_INSCR
			Else
				SA1->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("C",SA1->A1_CGC,SA1->A1_TIPO)
				cInsc:=SA1->A1_INSCR
			Endif
		Else
			If substr(F3_CFO,1,1)>="5"
				SA1->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("C",SA1->A1_CGC,SA1->A1_TIPO)
				cInsc:=SA1->A1_INSCR
			Else
				SA2->(dbSeek(xFilial()+cClieFor))
				cCGC:=A941CGC("F",SA2->A2_CGC,SA2->A2_TIPO)
				cInsc:=SA2->A2_INSCR
			Endif
		Endif
		If Empty(cCGC)
			cCGC:=Replic("0",14)
		Endif
		If Empty(cInsc).or.F3_ESTADO=="EX"
			cInsc:="ISENTO"
		Else
			cInsc:=A941INSC(cInsc)
		Endif

		cSeek:=F3_NFISCAL+F3_SERIE+F3_CLIEFOR+F3_LOJA

		lFound:=.F.
		If substr(F3_CFO,1,1)<"5"
			dbSelectArea("SD1")
			dbSetOrder(1)
			dbSeek(xFilial()+cSeek)
			While !eof().and.D1_FILIAL+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA==xFilial()+cSeek
				SF4->(dbSeek(xFilial()+SD1->D1_TES))
				If SF4->F4_LFICM=="N"
					dbSkip()
					Loop
				Endif
				SB1->(dbSeek(xFilial()+SD1->D1_COD))
				cRegistro:="54"
				cRegistro+=A941FILL(A941NUM2CHR(Val(cCGC),14,0),14)
				cRegistro+=A941FILL(DTOS(SF3->F3_ENTRADA),8)
				cRegistro+=A941FILL(SF3->F3_ESTADO,2)
				cRegistro+=A941FILL(A941NUM2CHR(A941ModNot(SF3->F3_ESPECIE),2,0),2)
				cRegistro+=A941FILL(SerieNfId("SF3",2,"F3_SERIE"),3)
				cRegistro+=A941FILL(,2)
				cRegistro+=A941FILL(A941NUM2CHR(Val(SF3->F3_NFISCAL),6,0),6)

				cRegistro+=A941FILL(StrZero(nItem,2),2)
				cRegistro+=A941FILL((If(Empty(SB1->B1_POSIPI),Left(SB1->B1_COD,10),SB1->B1_POSIPI)),10)
				If Empty(SD1->D1_CLASFIS)
					cRegistro+=A941FILL(A941NUM2CHR(Val(Substr(SB1->B1_ORIGEM,1,1)+SB1->B1_CLASFIS),3,0),3)
				Else
					cRegistro+=A941FILL(A941NUM2CHR(Val(SD1->D1_CLASFIS),3,0),3)
				Endif
				cRegistro+=A941FILL(SD1->D1_UM,3)
				cRegistro+=A941FILL(A941NUM2CHR(D1_QUANT,13,3),13)
				cRegistro+=A941FILL(A941NUM2CHR(D1_TOTAL,13),13)
				cRegistro+=A941FILL(A941NUM2CHR(D1_BASEICM,13),13)
				cRegistro+=A941FILL(A941NUM2CHR(D1_BRICMS,13),13)
				cRegistro+=A941FILL(A941NUM2CHR(D1_PICM,4),4)
				cRegistro+=A941FILL(A941NUM2CHR(D1_VALIPI,13),13)

				A941PutTemp(cRegistro)

				If Empty(SB1->B1_POSIPI)
					cRegistro:="75"
					cRegistro+=A941FILL(A941NUM2CHR(Val(cCGC),14,0),14)
					cRegistro+=A941FILL(Left(SB1->B1_COD,10),10)
					cRegistro+=A941FILL(SB1->B1_DESC,75)
					cRegistro+=A941FILL(,25)
					dbSelectArea(cArqTemp)
					If !dbSeek("75"+cRegistro)
						A941PutTemp(cRegistro)
					Endif
				Endif

				dbSelectArea("SD1")
				dbSkip()
				nItem:=nItem+1
				lFound:=.T.
			End
		Else
			dbSelectArea("SD2")
			dbSetOrder(3)
			dbSeek(xFilial()+cSeek)
			While !eof().and.D2_FILIAL+D2_DOC+D2_SERIE+D2_CLIENTE+D2_LOJA==xFilial()+cSeek
				SF4->(dbSeek(xFilial()+SD2->D2_TES))
				If SD2->D2_TOTAL<0.Or.SF4->F4_LFICM=="N"
					dbSkip()
					Loop
				Endif
				SB1->(dbSeek(xFilial()+SD2->D2_COD))

				cRegistro:="54"
				cRegistro+=A941FILL(A941NUM2CHR(Val(cCGC),14,0),14)
				cRegistro+=A941FILL(DTOS(SF3->F3_ENTRADA),8)
				cRegistro+=A941FILL(SF3->F3_ESTADO,2)
				cRegistro+=A941FILL(A941NUM2CHR(A941ModNot(SF3->F3_ESPECIE),2,0),2)
				cRegistro+=A941FILL(SerieNfId("SF3",2,"F3_SERIE"),3)
				cRegistro+=A941FILL(,2)
				cRegistro+=A941FILL(A941NUM2CHR(Val(SF3->F3_NFISCAL),6,0),6)
				cRegistro+=A941FILL(StrZero(nItem,2),2)
				cRegistro+=A941FILL((If(Empty(SB1->B1_POSIPI),Left(SB1->B1_COD,10),SB1->B1_POSIPI)),10)
				If Empty(SD2->D2_CLASFIS)
					cRegistro+=A941FILL(A941NUM2CHR(Val(Left(SB1->B1_ORIGEM,1)+SB1->B1_CLASFIS),3,0),3)
				Else
					cRegistro+=A941FILL(A941NUM2CHR(Val(SD2->D2_CLASFIS),3,0),3)
				Endif
				cRegistro+=A941FILL(SD2->D2_UM,3)
				cRegistro+=A941FILL(A941NUM2CHR(D2_QUANT,13,3),13)
				cRegistro+=A941FILL(A941NUM2CHR(D2_TOTAL,13),13)
				cRegistro+=A941FILL(A941NUM2CHR(D2_BASEICM,13),13)
				cRegistro+=A941FILL(A941NUM2CHR(D2_BRICMS,13),13)
				cRegistro+=A941FILL(A941NUM2CHR(D2_PICM,4),4)
				cRegistro+=A941FILL(A941NUM2CHR(D2_VALIPI,13),13)

				A941PutTemp(cRegistro)

				If Empty(SB1->B1_POSIPI)
					cRegistro:="75"
					If Empty(SM0->M0_CGC)
						cRegistro+=A941FILL(Repl("0",14),14)
					Else
						cRegistro+=A941FILL(A941NUM2CHR(Val(SM0->M0_CGC),14,0),14)
					Endif
					cRegistro+=A941FILL(Left(SB1->B1_COD,10),10)
					cRegistro+=A941FILL(SB1->B1_DESC,75)
					cRegistro+=A941FILL(,25)
					dbSelectArea(cArqTemp)
					If !dbSeek("75"+cRegistro)
						A941PutTemp(cRegistro)
					Endif
				Endif

				dbSelectArea("SD2")

				dbSkip()
				nItem:=nItem+1
				lFound:=.T.
			End
		Endif

		dbSelectArea("SF3")
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Registro adicional para registrar desconto escriturado       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lFound.and.lDesconto.and.F3_VALOBSE>0
			cRegistro:="54"
			cRegistro+=A941FILL(A941NUM2CHR(Val(cCGC),14,0),14)
			cRegistro+=A941FILL(DTOS(SF3->F3_ENTRADA),8)
			cRegistro+=A941FILL(SF3->F3_ESTADO,2)
			cRegistro+=A941FILL(A941NUM2CHR(A941ModNot(SF3->F3_ESPECIE),2,0),2)
			cRegistro+=A941FILL(SerieNfId("SF3",2,"F3_SERIE"),3)
			cRegistro+=A941FILL(,2)
			cRegistro+=A941FILL(A941NUM2CHR(Val(SF3->F3_NFISCAL),6,0),6)
			cRegistro+="99"
			cRegistro+=A941FILL(Space(10),10)
			cRegistro+=A941FILL("000",3)
			cRegistro+=A941FILL(,3)
			cRegistro+=A941FILL(A941NUM2CHR(1,13,3),13)
			cRegistro+=A941FILL(A941NUM2CHR(SF3->F3_VALOBSE,13),13)
			cRegistro+=A941FILL(A941NUM2CHR(0,13),13)
			cRegistro+=A941FILL(A941NUM2CHR(0,13),13)
			cRegistro+=A941FILL(A941NUM2CHR(0,4),4)
			cRegistro+=A941FILL(A941NUM2CHR(0,13),13)

			A941PutTemp(cRegistro)
		Endif
	Endif

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941Reg55    ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Grava registro 55                                           ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941Reg55

	Local cRegistro,cAlias:=Alias(),cCGC,cInsc

	dbSelectArea("SF6")
	dbSetOrder(2)
	dbSeek(xFilial())

	ProcRegua(LastRec())
	While !eof() .And. xFilial()==SF6->F6_FILIAL
		IncProc(STR0010) //"Processando registros..."
		If lEnd
			Exit
		Endif
		If F6_DTARREC<dDtIni .Or. IIf(lListEst,!(F6_EST == cEstDest),(F6_EST==cEstDest))
			dbSkip()
			Loop
		Endif
		If F6_DTARREC>dDtFim
			dbSkip()
			Loop
		Endif

		cCGC:=SM0->M0_CGC
		cInsc:=F6_INSC
		If Empty(cCGC)
			cCGC:=Replic("0",14)
		Endif
		If Empty(cInsc)
			cInsc:="ISENTO"
		Else
			cInsc:=A941INSC(cInsc)
		Endif

		cRegistro:="55"
		cRegistro+=A941FILL(A941NUM2CHR(Val(cCGC),14,0),14)
		cRegistro+=A941FILL(cInsc,14)
		cRegistro+=A941FILL(DTOS(F6_DTARREC),8)
		cRegistro+=A941FILL(F6_EST,2)
		cRegistro+=A941FILL(A941NUM2CHR(Val(F6_BANCO),3,0),3)
		cRegistro+=A941FILL(A941NUM2CHR(Val(F6_AGENCIA),4,0),4)
		cRegistro+=A941FILL(A941NUM2CHR(Val(F6_NUMERO),12,0),12)
		cRegistro+=A941FILL(A941NUM2CHR(F6_VALOR,13),13)
		cRegistro+=A941FILL(DTOS(F6_DTVENC),8)
		cRegistro+=A941FILL((StrZero(F6_MESREF,2)+StrZero(F6_ANOREF,4)),6)
		cRegistro+=A941FILL(F6_NUMCONV,30)
		cRegistro+=A941FILL(,10)

		A941PutTemp(cRegistro)
		dbSkip()
	End

	dbSetOrder(1)

	dbSelectArea(cAlias)

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941Reg60    ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Grava registro 60                                           ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941Reg60

	Local cArqCupom,aCampos:=Array(7),cChave,cAlias:=Alias(),cEspDoc

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cria arquivo para armazenar cupons fiscais                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aCampos[1]:={"CUP_DTEMIS","D",8,0}
	aCampos[2]:={"CUP_MAQREG","C",TamSx3("F3_SERIE")[1],0}
	aCampos[3]:={"CUP_ESPDOC","C",2,0}
	aCampos[4]:={"CUP_NUMINI","C",6,0}
	aCampos[5]:={"CUP_NUMFIM","C",6,0}
	aCampos[6]:={"CUP_VALOR","N",14,2}
	aCampos[7]:={"CUP_VALICM","N",14,2}
	aCampos[2]:={"CUP_SDOC","C",3,0}
	cArqCupom:=CriaTrab(aCampos)
	dbUseArea(.T.,,cArqCupom,cArqCupom,.T.,.F.)
	cChave:="DTOS(CUP_DTEMIS)+CUP_MAQREG"
	IndRegua(cArqCupom,cArqCupom,cChave,,,STR0058) //"Indexando cupons fiscais..."

	dbSelectArea("SF3")
	dbSetOrder(1)
	dbSeek(xFilial()+DTOS(dDtIni),.T.)
	ProcRegua(LastRec())
	While !eof() .And. xFilial()==SF3->F3_FILIAL
		IncProc(STR0010) //"Processando registros..."
		If lEnd
			Exit
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Filtra dados                                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If F3_ENTRADA>dDtFim
			Exit
		Endif
		If (substr(F3_CFO,1,1)<"5").OR.!A941IsCupom(SerieNfId("SF3",2,"F3_SERIE")).OR.(F3_TIPO$"SL").OR.If(cNrLivro=="*",.F.,!(F3_NRLIVRO==cNrLivro)).OR.;
			Substr(F3_CFO,2,2)$"62,63"	.Or. If(Empty(cEstDest),.F.,IIf(lListEst,!(F3_ESTADO==cEstDest),(F3_ESTADO==cEstDest)))
			dbSkip()
			Loop
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Define tipo de cupom fiscal                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Substr(SerieNfId("SF3",2,"F3_SERIE"),1,2)=="CP"
			cEspDoc:="2C"
		Else
			cEspDoc:="2D"
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Totaliza cupons fiscais no arquivo temporario                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea(cArqCupom)

		If dbSeek(DTOS(SF3->F3_ENTRADA)+SF3->F3_SERIE)
			RecLock(cArqCupom,.F.)
		Else
			RecLock(cArqCupom,.T.)
			Replace CUP_DTEMIS with SF3->F3_ENTRADA
			Replace CUP_MAQREG	with A941MaqReg(SF3->F3_SERIE)
			Replace CUP_SDOC		with A941MaqReg(SerieNfId("SF3",2,"F3_SERIE"))
			Replace CUP_ESPDOC	with cEspDoc
		Endif
		If Val(SF3->F3_NFISCAL)==Max(Val(CUP_NUMINI),Val(SF3->F3_NFISCAL))
			If Val(CUP_NUMINI)>=Val(SF3->F3_NFISCAL) .Or. Empty(CUP_NUMINI)
				Replace CUP_NUMINI with StrZero(Val(SF3->F3_NFISCAL),6)
			Endif
			Replace CUP_NUMFIM with StrZero(Val(SF3->F3_NFISCAL),6)
		Else
			Replace CUP_NUMFIM with StrZero(Val(SF3->F3_NFISCAL),6)
		Endif
		Replace CUP_VALOR with CUP_VALOR+SF3->F3_VALCONT
		Replace CUP_VALICM with CUP_VALICM+SF3->F3_VALICM
		MsUnlock()

		dbSelectArea("SF3")
		dbSkip()
	End
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava registro 60                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea(cArqCupom)
	dbGotop()
	ProcRegua(LastRec())
	While !Eof().and.!lEnd
		IncProc(STR0010) //"Processando registros..."
		If lEnd
			Exit
		Endif
		cRegistro:="60"
		cRegistro+=A941FILL(,28)
		cRegistro+=A941FILL(DTOS(CUP_DTEMIS),8)
		cRegistro+=A941FILL(A941NUM2CHR(Val(CUP_SDOC),3,0),3)
		cRegistro+=A941FILL(CUP_ESPDOC,2)
		cRegistro+=A941FILL(CUP_NUMINI,6)
		cRegistro+=A941FILL(CUP_NUMFIM,6)
		cRegistro+=A941FILL(A941NUM2CHR(CUP_VALOR,14),14)
		cRegistro+=A941FILL(A941NUM2CHR(CUP_VALICM,13),13)
		cRegistro+=A941FILL(,44)

		A941PutTemp(cRegistro)

		dbSkip()
	End
	dbSelectArea(cArqCupom)
	dbCloseArea()
	Ferase(cArqCupom+".DBF")
	Ferase(cArqCupom+OrdBagExt())

	dbSelectArea(cAlias)

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941Reg61    ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Grava registro 61                                           ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941Reg61(aTotReg61)
	Local cRegistro, i

	For i := 1 to len( aTotReg61 )
		cRegistro:="61"
		cRegistro+=A941FILL(,28) 										// brancos
		cRegistro+=A941FILL(aTotReg61[i,3],08)						// Data de emissao dos doc. fiscais
		cRegistro+=A941FILL(aTotReg61[i,4],02)						// Modelo dos doc. Fiscais
		cRegistro+=A941FILL(aTotReg61[i,5],01)						// Serie dos doc. Fiscais
		cRegistro+=A941FILL(aTotReg61[i,6],03)						// Subserie dos documentos Fiscais
		cRegistro+=A941FILL(strzero(val(aTotReg61[i,7]),9),9)	// Numero do 1o. doc. Fiscal emitido no dia
		cRegistro+=A941FILL(strzero(val(aTotReg61[i,8]),9),9)	// Numero do ultimo doc. Fiscal emitido no dia
		cRegistro+=A941FILL(A941NUM2CHR(aTotReg61[i,9],16),16)// Somatorio diario das saidas p/doc. fiscais de mesma serie e subserie
		cRegistro+=A941FILL(,48)										// Brancos

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Incrementa contador de registros                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		A941PutTemp(cRegistro)
	Next

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941Reg70e71 ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Grava registro 70 e 71                                      ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941Reg70e71

	Local	cIndSF8,cFiltro,cChave,cAlias:=Alias(),cSeek,;
	cRegistro,cConhec:="X",cTranspCGC,cTranspIns,cTranspEst,;
	cEspDocCon,dEmissCon,cIndSF3,cIndSF3A
	Local nIndex
	Local lMta940b := (existblock("MTA940B"))

	cIndSF3:=CriaTrab(NIL,.F.)
	cIndSF3A:=Substr(cIndSF3,1,6)+"A"
	cIndSF8:=Substr(cIndSF3,1,6)+"B"

	dbSelectArea("SF3")
	dbSetOrder(4)
	cChave:=IndexKey()
	cFiltro:="F3_FILIAL=='"+xFilial()+"'"
	If cNrLivro!="*"
		cFiltro+=".And.F3_NRLIVRO=='"+cNrLivro+"'"
	Endif
	IndRegua("SF3",cIndSF3,cChave,,cFiltro,STR0059) //"Selec.Notas fiscais..."
	nIndex:=RetIndex()
	#IFNDEF TOP
		dbSetIndex(cIndSF3+OrdBagExt())
	#ENDIF
	dbSetOrder(nIndex+1)

	dbSelectArea("SF8")
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Filtra por data                                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cFiltro:="F8_FILIAL=='"+xFilial()+"'.AND."
	cFiltro+="F8_TIPO=='F'.AND."
	cFiltro+="DTOS(F8_DTDIGIT)>='"+DTOS(dDtIni)+"'.AND.DTOS(F8_DTDIGIT)<='"+DTOS(dDtFim)+"'"

	cChave:="DTOS(F8_DTDIGIT)+F8_SEDIFRE+F8_NFDIFRE"

	IndRegua("SF8",cIndSF8,cChave,,cFiltro,STR0060) //"Selec.Conhec.de Frete..."

	ProcRegua(LastRec())
	While !eof()
		IncProc(STR0010) //"Processando registros..."
		If lEnd
			Exit
		Endif

		cSeek:=xFilial("SF3")+F8_TRANSP+F8_LOJTRAN+F8_NFDIFRE+F8_SEDIFRE

		If !(cConhec==cSeek)
			dbSelectArea("SF3")
			dbSeek(cSeek)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Filtra por CFO - Para tomadores de servico.                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Val(substr(F3_CFO,1,1))>=5
				dbSelectArea("SF8")
				dbSkip()
				Loop
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Filtra por lancamento no SF3                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Found() .Or. If(Empty(cEstDest),.F.,IIf(lListEst,!(F3_ESTADO==cEstDest),(F3_ESTADO==cEstDest)))
				dbSelectArea("SF8")
				dbSkip()
				Loop
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Posiciona na transportadora                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SF3->F3_FORMUL$'N '
				SA2->(dbSeek(xFilial()+SF8->F8_TRANSP+SF8->F8_LOJTRAN))
				cTranspCGC:=If(Empty(SA2->A2_CGC),Replic("0",14),SA2->A2_CGC)
				cTranspIns:=If(Empty(SA2->A2_INSCR).or.F3_ESTADO=="EX","ISENTO",A941INSC(SA2->A2_INSCR))
				cTranspEst:=SA2->A2_EST
			Else
				cTranspCGC:=If(Empty(SM0->M0_CGC),Replic("0",14),SM0->M0_CGC)
				cTranspIns:=If(Empty(SM0->M0_INSC).or.SF3->F3_ESTADO=="EX","ISENTO",A941INSC(SM0->M0_INSC))
				cTranspEst:=If(Empty(SM0->M0_ESTENT),SM0->M0_ESTCOB,SM0->M0_ESTENT)
			Endif
			dbSelectArea("SF3")
			cEspDocCon:=A941NUM2CHR(A941ModNot(F3_ESPECIE),2,0)
			dEmissCon:=F3_ENTRADA

			While !eof().and.F3_FILIAL+F3_CLIEFOR+F3_LOJA+F3_NFISCAL+F3_SERIE==cSeek
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Grava registro 70                                            ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				#IFDEF WINDOWS
					SysRefresh()
				#ENDIF
				cRegistro:="70"
				cRegistro+=A941FILL(A941NUM2CHR(Val(cTranspCGC),14,0),14)
				cRegistro+=A941FILL(cTranspIns,14)
				cRegistro+=A941FILL(DTOS(dEmissCon),8)
				cRegistro+=A941FILL(cTranspEst,2)
				cRegistro+=A941FILL(cEspDocCon,2)
				cRegistro+=A941FILL(SerieNFId("SF3",2,"F3_SERIE"),3)
				cRegistro+=A941FILL(A941NUM2CHR(Val(F3_NFISCAL),6,0),6)
				cRegistro+=A941FILL(A941NUM2CHR(Val(substr(F3_CFO,1,3)),3,0),3)
				cRegistro+=A941FILL(A941NUM2CHR(F3_VALCONT,14),14)
				cRegistro+=A941FILL(A941NUM2CHR(F3_BASEICM,14),14)
				cRegistro+=A941FILL(A941NUM2CHR(F3_VALICM,14),14)
				cRegistro+=A941FILL(A941NUM2CHR(F3_ISENICM,14),14)
				cRegistro+=A941FILL(A941NUM2CHR(F3_OUTRICM,14),14)
				cRegistro+=A941FILL("1",1) // Sempre CIF
				cRegistro+=A941FILL(If("CANCELADA"$F3_OBSERV,"S","N"),1)

				A941PutTemp(cRegistro)
				dbSkip()
			End
			cConhec:=cSeek
		Endif

		dbSelectArea("SF8")
		dbSkip()
	End
                           
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Restaura indices e cria novo para verificar notas de frete de venda  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex("SF8")
	Ferase(cIndSF8+OrdBagExt())
	SF8->(dBSetOrder(1))
	cChave	:=	"F8_FILIAL+F8_NFDIFRE+F8_SEDIFRE+F8_TRANSP+F8_LOJTRAN"

	IndRegua("SF8",cIndSF8,cChave,,cFiltro,STR0060) //"Selec.Conhec.de Frete..."

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Prepara e grava registro 71                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SF3")
	dbSetOrder(4)
	cChave:=IndexKey()
	cFiltro:="F3_FILIAL=='"+xFilial()+"'"
	cFiltro+=".And.DTOS(F3_ENTRADA)>='"+DTOS(dDtIni)+"'.AND.DTOS(F3_ENTRADA)<='"+DTOS(dDtFim)+"'"
	cFiltro+=".And.Subs(F3_CFO,2,2)$'61/62/63/64/65'"
	cFiltro+=".And."+IIf(lListEst,"F3_ESTADO=='"+cEstDest+"'","F3_ESTADO!='"+cEstDest+"'")

	If cNrLivro!="*"
		cFiltro+=".And.F3_NRLIVRO=='"+cNrLivro+"'"
	Endif
	IndRegua("SF3",cIndSF3A,cChave,,cFiltro,STR0059) //"Selec.Notas fiscais..."
	nIndex:=RetIndex("SF3")
	#IFNDEF TOP
		dbSetIndex(cIndSF3+OrdBagExt())
		dbSetIndex(cIndSF3A+OrdBagExt())
	#ENDIF
	dbSetOrder(nIndex+2)
	dbGotop()

	While !Eof()
		#IFDEF WINDOWS
			SysRefresh()
		#ENDIF                                     
		
		if Substr(SF3->F3_CFO,1,1) < "5"
			if SF8->(dBSeek(xFilial("SF8")+SF3->F3_NFISCAL+SF3->F3_SERIE+SF3->F3_CLIEFOR+SF3->F3_LOJA))
				dbSkip()
				loop
			EndIf
		EndIF		
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Posiciona na transportadora                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SF3->F3_TIPO$'DB' .or. Substr(SF3->F3_CFO,1,1) < "5" 
			SA2->(dbSeek(xFilial()+SF3->F3_CLIEFOR+SF3->F3_LOJA))
			cTranspCGC:=If(Empty(SA2->A2_CGC),Replic("0",14),SA2->A2_CGC)
			cTranspIns:=If(Empty(SA2->A2_INSCR).or.F3_ESTADO=="EX","ISENTO",A941INSC(SA2->A2_INSCR))
			cTranspEst:=SA2->A2_EST
		Else
			SA1->(dbSeek(xFilial()+SF3->F3_CLIEFOR+SF3->F3_LOJA))
			cTranspCGC:=If(Empty(SA1->A1_CGC),Replic("0",14),SA1->A1_CGC)
			cTranspIns:=If(Empty(SA1->A1_INSCR).or.F3_ESTADO=="EX","ISENTO",A941INSC(SA1->A1_INSCR))
			cTranspEst:=SA1->A1_EST
		Endif

		dbSelectArea("SF3")
		cEspDocCon:=A941NUM2CHR(A941ModNot(F3_ESPECIE),2,0)
		dEmissCon:=F3_ENTRADA

		/*BEGINDOC
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Somente gera registro tipo 71 para      ³
		//³emitentes de conhecimento de transporte.³
		//³Portanto somente Notas Fiscais de Saida.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		ENDDOC*/
		if Substr(SF3->F3_CFO,1,1) >= "5"

			If lMta940B
				cRegistro := Execblock("MTA940B",.F.,.F.)
			Else
				cRegistro:="71"
				cRegistro+=A941FILL(A941NUM2CHR(Val(cTranspCGC),14,0),14)
				cRegistro+=A941FILL(If(Empty(cTranspIns).or.F3_ESTADO=="EX","ISENTO",cTranspIns),14)
				cRegistro+=A941FILL(DTOS(dEmissCon),8)
				cRegistro+=A941FILL(SF3->F3_ESTADO,2)
				cRegistro+=A941FILL(cEspDocCon,2)
				cRegistro+=A941FILL(SerieNFId("SF3",2,"F3_SERIE"),3)
				cRegistro+=A941FILL(A941NUM2CHR(Val(SF3->F3_NFISCAL),6,0),6)
				/*BEGINDOC
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Nos campos 11 e 12, nao temos condicoes de saber o CGC e Inscricao Estadual do 	³
				//³Destinatario no caso de o remetente ser o tomador do servico.                  	³
				//³Portanto tratamos como se o "Tomador" do servico fosse sempre o destinatario. 	³
				//³Se for necessario utilizar o primeiro caso, fazer via ponto de entrada MTA940B.  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				ENDDOC*/
				cRegistro+=A941FILL(SM0->M0_ESTENT,2)
				cRegistro+=A941FILL(A941NUM2CHR(Val(SM0->M0_CGC),14,0),14)
				cRegistro+=A941FILL(If(Empty(SM0->M0_INSC),"ISENTO",SM0->M0_INSC),14)
				cRegistro+=A941FILL(DTOS(F3_ENTRADA),8)
				cRegistro+=A941FILL(A941NUM2CHR(A941ModNot(F3_ESPECIE),2,0),2)
				cRegistro+=A941FILL(SerieNFId("SF3",2,"F3_SERIE"),4)
				cRegistro+=A941FILL(A941NUM2CHR(Val(F3_NFISCAL),6,0),6)
				cRegistro+=A941FILL(A941NUM2CHR(F3_VALCONT,14),14)
				cRegistro+=A941FILL(,11)
			Endif
	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Grava registro 71                                            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			A941PutTemp(cRegistro)
	   EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Grava registro 70                                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cRegistro:="70"
		cRegistro+=A941FILL(A941NUM2CHR(Val(cTranspCGC),14,0),14)
		cRegistro+=A941FILL(cTranspIns,14)
		cRegistro+=A941FILL(DTOS(dEmissCon),8)
		cRegistro+=A941FILL(cTranspEst,2)
		cRegistro+=A941FILL(cEspDocCon,2)
		cRegistro+=A941FILL(SerieNFId("SF3",2,"F3_SERIE"),3)
		cRegistro+=A941FILL(A941NUM2CHR(Val(F3_NFISCAL),6,0),6)
		cRegistro+=A941FILL(A941NUM2CHR(Val(substr(F3_CFO,1,1)),3,0),3)
		cRegistro+=A941FILL(A941NUM2CHR(F3_VALCONT,14),14)
		cRegistro+=A941FILL(A941NUM2CHR(F3_BASEICM,14),14)
		cRegistro+=A941FILL(A941NUM2CHR(F3_VALICM,14),14)
		cRegistro+=A941FILL(A941NUM2CHR(F3_ISENICM,14),14)
		cRegistro+=A941FILL(A941NUM2CHR(F3_OUTRICM,14),14)
		cRegistro+=A941FILL("1",1) // Sempre CIF
		cRegistro+=A941FILL(If("CANCELADA"$F3_OBSERV,"S","N"),1)

		A941PutTemp(cRegistro)

		dbSkip()
	End

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Restaura indices                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RetIndex("SF8")
	Ferase(cIndSF8+OrdBagExt())

	dbSelectArea("SF3")
	RetIndex("SF3")
	dbSetOrder(1)
	Ferase(cIndSF3+OrdBagExt())
	Ferase(cIndSF3A+OrdBagExt())

	dbSelectArea(cAlias)

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941Reg90    ³Autor ³ Juan Jos‚ Pereira   ³ Data ³ 30/01/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Grava registro tipo 90                                      ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941Reg90()

	Local cRegistro:="90"
	Local nRegNO	:= 0
	Local nDisco	:= 1
	Local nReg90	:= 1	// Nro registros tipo 90

	Private lGravou:=.F.

	If ((nReg50+nReg51+nReg53+nReg54+nReg55+nReg60+nReg61+nReg70+nReg71+nReg75) >= 0)

		cRegistro+=A941FILL(A941NUM2CHR(Val(SM0->M0_CGC),14,0),14)
		cRegistro+=A941FILL(If(Empty(SM0->M0_INSC),"ISENTO",SM0->M0_INSC),14)

		cRegistro+="10"
		cRegistro+=A941FILL(StrZero(1,8),8)	// Total de Registros "10"
		cRegistro+="11"
		cRegistro+=A941FILL(StrZero(1,8),8)	// Total de Registros "11"

		If nReg50>0
			// 58
			cRegistro+="50"
			cRegistro+=A941FILL(StrZero(nReg50,8),8)	// Total de Registros "50"
		Endif

		If nReg51>0
			// 68
			cRegistro+="51"
			cRegistro+=A941FILL(StrZero(nReg51,8),8)	// Total de Registros "51"
		Endif

		If nReg53>0
			// 78
			cRegistro+="53"
			cRegistro+=A941FILL(StrZero(nReg53,8),8)	// Total de Registros "53"
		Endif

		If nReg54>0
			//	88
			cRegistro+="54"
			cRegistro+=A941FILL(StrZero(nReg54,8),8)	// Total de Registros "54"
		Endif

		If nReg55>0
			// 98
			cRegistro+="55"
			cRegistro+=A941FILL(StrZero(nReg55,8),8)	// Total de Registros "55"
		Endif

		If nReg60>0
			//	108
			cRegistro+="60"
			cRegistro+=A941FILL(StrZero(nReg60,8),8)	// Total de Registros "60"
		Endif

		If nReg61>0
			cRegistro+="61"
			cRegistro+=A941FILL(StrZero(nReg61,8),8)	// Total de Registros "61"
		Endif

		If nReg70>0
			//	118
			cRegistro+="70"
			cRegistro+=A941FILL(StrZero(nReg70,8),8)	// Total de Registros "70"
		Endif

		// Imprime Reg. Tipo 90
		If Len(cRegistro)>=110
			A941IncReg90(@cRegistro,@nReg90,(nReg71+nReg75))
		Endif

		If nReg71>0
			cRegistro+="71"
			cRegistro+=A941FILL(StrZero(nReg71,8),8)	// Total de Registros "71"
		Endif

		// Imprime Reg. Tipo 90
		If Len(cRegistro)>=110.And.!lGravou
			A941IncReg90(@cRegistro,@nReg90,(nReg75))
		Endif

		If nReg75>0
			cRegistro+="75"
			cRegistro+=A941FILL(StrZero(nReg75,8),8)	// Total de Registros "75"
		Endif

		// Imprime Reg. Tipo 90
		If (Len(cRegistro)>=110.And.!lGravou).Or.!lGravou
			A941IncReg90(@cRegistro,@nReg90,0)
		Endif

		nReg50:=nReg51:=nReg53:=nReg54:=nReg55:=nReg61:=nReg70:=nReg71:=nReg75:=0
	EndIf

	dbSelectArea(cArqTemp)
	dbGoTop()
	ProcRegua(Reccount())
	While !Eof()
		IncProc()
		If (nRegNO == nNumReg)
			nDisco += 1
			nRegNO := 0
		EndIf

		RecLock(cArqTemp,.F.)
		(cArqTemp)->A := StrZero(nDisco,2)
		(cArqTemp)->H := ++nRegNO
		If (cArqTemp)->B=="90"
			(cArqTemp)->G:=(Subs((cArqTemp)->G,1,87)+Alltrim(Str(nReg90)))
		Endif
		MsUnLock()

		dbSkip()
	EndDo

	Return
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³ A941IncReg90 ³Autor ³  Marcos Simidu       ³Data³ 17/07/98 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³ Inclui registros tipo 90.                                  ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Static Function A941IncReg90(cRegistro,nReg90,nResto)

	If nResto<=0.And.(125-Len(cRegistro))>8
		// Nao possui mais registros a imprimir
		lGravou:=.T.
		cRegistro+=A941FILL(StrZero(2+nReg90+nReg50+nReg51+nReg53+nReg54+nReg55+nReg60+nReg61+nReg70+nReg71+nReg75,8),8)
		cRegistro+=A941FILL(,(125-Len(cRegistro)))	// Brancos ate registro final
		cRegistro+=Alltrim(Str(nReg90))		// Nro registro tipo 90
		A941PutTemp(cRegistro)					// Grava reg. tipo 90
	Endif

	If (125-Len(cRegistro))<=11	// Verifica se imprime mais um registro
		If !lGravou
			cRegistro+=A941FILL(,(125-Len(cRegistro)))	// Brancos ate registro final
			cRegistro+=Alltrim(Str(nReg90))		// Nro registro tipo 90
			A941PutTemp(cRegistro)					// Grava reg. tipo 90

			nReg90++				// Incrementa reg. tipo 90

			If nResto<=0.And.(125-Len(cRegistro))<9
				// Nao possui mais registros a imprimir
				lGravou:=.T.
				cRegistro:="90"	// Reinicializa segundo registro 90
				cRegistro+=A941FILL(A941NUM2CHR(Val(SM0->M0_CGC),14,0),14)
				cRegistro+=A941FILL(If(Empty(SM0->M0_INSC),"ISENTO",SM0->M0_INSC),14)
				cRegistro+=A941FILL(StrZero(2+nReg90+nReg50+nReg51+nReg53+nReg54+nReg55+nReg60+nReg61+nReg70+nReg71+nReg75,8),8)
				cRegistro+=A941FILL(,(125-Len(cRegistro)))	// Brancos ate registro final
				cRegistro+=Alltrim(Str(nReg90))		// Nro registro tipo 90
				A941PutTemp(cRegistro)					// Grava reg. tipo 90
			Else
				cRegistro:="90"	// Reinicializa segundo registro 90
				cRegistro+=A941FILL(A941NUM2CHR(Val(SM0->M0_CGC),14,0),14)
				cRegistro+=A941FILL(If(Empty(SM0->M0_INSC),"ISENTO",SM0->M0_INSC),14)
			Endif
		Endif
	Endif

	Return

	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³ R940NumReg   ³Autor ³Ben Hur M. de Castilho³Data³ 13/08/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³ Ajusta perguntas do SX1                                    ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Function A941NumReg(nSize)

	Return({1000000000,10800}[Max(nSize,1)])
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³  A941MaqReg  ³Autor ³    Marcos Simidu     ³Data³ 22/08/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³ Retorna numero de Maq. Registradora                        ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Function A941MaqReg(cSerie)
	Local cMaq:="",i:=0

	For i:=1 To Len(cSerie)
		If IsDigit(Subs(cSerie,i,1))
			cMaq+=Subs(cSerie,i,1)
		Endif
	Next

	Return(cMaq)

	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³   A941CGC    ³Autor ³    Marcos Simidu     ³Data³ 22/08/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³ Retorna numero de CGC                                      ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Function A941CGC(cCliFor,cCGC,cTipo)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Zera o campo de CGC para pessoas nao obrigadas a inscricao.  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cCliFor=="C"		// Cliente
		If ( cTipo=="X") .Or. (cTipo$"FL" .and. Len(Alltrim(cCGC)) < 14 )
			cCGC:=""
		Endif
	Else						// Fornecedor
		If ( cTipo$"FL" ) .Or. ( Len(Alltrim(cCGC)) < 14 )
			cCGC:=""
		Endif
	Endif

	Return(cCGC)
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³   A941INSC   ³Autor ³    Marcos Simidu     ³Data³ 22/08/97 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³ Retorna inscricao estadual                                 ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Function A941INSC(cInsc)
	Local cRet:="",i:=1

	For i:=1 To Len(cInsc)
		If Isdigit(Subs(cInsc,i,1)) .Or. IsAlpha(Subs(cInsc,i,1))
			cRet+=Subs(cInsc,i,1)
		Endif
	Next

	If Empty(cRet)
		cRet:="ISENTO"
	Endif

	Return(cRet)
	/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A941TOTREG61  ³Autor ³    Andreia dos Santos³Data³ 18/08/99 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³ Monta registro 61                                          ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
	*/
	Function A941TotReg61(aReg61)
	Local nPos
	/*
	01 - Chave: Data+serie+subserie
	02 - Tipo
	03 - Data de Emissao
	04 - Modelo
	05 - Serie
	06 - Subserie
	07 - Numero inicial NF
	08 - Numero Final NF
	09 - Valor
	*/
	if Alltrim(F3_ESPECIE) =="NFST" .OR. Alltrim(F3_ESPECIE) =="NTSC"  //NF Servico de Transporte e NF Servico de Comunicacao
		nPos:=Ascan(aReg61,{|x|x[1]== dTos(F3_EMISSAO)+F3_SERIE})
		if nPos == 0
			AADD( aReg61,array(9))
			nPos := len(aReg61)
			aReg61[nPos,01] := dTos(F3_EMISSAO)+F3_SERIE		// CHAVE: EMISSAO+SERIE
			aReg61[nPos,02] := "61" 						// TIPO DE REGISTRO
			aReg61[nPos,03] := dtos(F3_EMISSAO)			// Data de emissao
			aReg61[nPos,04] := A941FILL(A941NUM2CHR(A941ModNot(F3_ESPECIE),2,0),2)	// Modelo
			aReg61[nPos,05] := SerieNFId("SF3",2,"F3_SERIE")					// Serie
			aReg61[nPos,06] := ""							// Subserie
			aReg61[nPos,07] := F3_NFISCAL					// No. do doc. fiscal inicial
			aReg61[nPos,08] := F3_NFISCAL					// No. do ultimo doc. fiscal emitido
			aReg61[nPos,09] := F3_VALCONT					// VALOR
		Else
			aReg61[nPos,08] := F3_NFISCAL					// No. do ultimo doc. fiscal emitido
			aReg61[nPos,09] += F3_VALCONT					// VALOR
		Endif
	EndIF
