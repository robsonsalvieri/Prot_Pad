#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.fciSynthetic.ch"

namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", tables="SB1,CFD", name="fciSyntheticSmartviewBusinessObject", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*/{Protheus.doc} fciSyntheticSmartviewBusinessObject
Classe para criação do Objeto de Negócio de FCI
@author Evandro Italo
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class fciSyntheticSmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Evandro Italo
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method new() class fciSyntheticSmartviewBusinessObject
    Local oFisTools := fisSmartviewTools():new()
    _Super:new()
    //Define Area
    self:appendArea(STR0001) //"Livros Fiscais"
    //Define o nome do objeto de Negócio
    self:setDisplayName(STR0002) //"FCI - FCI Sintético"
    //Define a descrição do objeto de Negócio
    self:setDescription(STR0003) //"Objeto FCI - Ficha de Conteúdo de Importação - FCI Sintético (SB1/CFD)"

    oFisTools:validPerg(self:setPergunte("FISTRFCI02"))
    freeobj(oFisTools)
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
@return object: self:oData
@author Evandro Italo
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class fciSyntheticSmartviewBusinessObject
    Local cAlias    as character
    Local cQuery    as character
    Local oFiltQry  := oFilter:getParameters() as Json
    Local cProdDe   := oFiltQry['MV_PAR01',1] as character
    Local cProdAte  := oFiltQry['MV_PAR02',1] as character
    Local oPrepare  := nil as object
    
    cQuery := " SELECT "		
    cQuery += "	CFD.CFD_FILIAL,"
    cQuery += "	CFD.CFD_COD, "
    cQuery += "	CFD.CFD_PERCAL, "
    cQuery += "	CFD.CFD_PERVEN, "
    cQuery += "	CFD.CFD_VPARIM, "
    cQuery += "	CFD.CFD_VSAIIE, "
    cQuery += "	CFD.CFD_CONIMP, "
    cQuery += "	SB1.B1_DESC, "
    cQuery += "	SB1.B1_UM, "
    cQuery += "	SB1.B1_POSIPI, "
    cQuery += "	SB1.B1_CODBAR "
    cQuery += " FROM " + RetSqlName( 'CFD' ) + " CFD "
    cQuery += " INNER JOIN " + RetSqlName( 'SB1' ) + " SB1 "
    cQuery += "	ON SB1.B1_FILIAL   = ? " 
    cQuery += "		AND SB1.B1_COD = CFD.CFD_COD "
    cQuery += " 	AND SB1.D_E_L_E_T_  = ? " 
    cQuery += "     WHERE "
    cQuery += " 	CFD.CFD_FILIAL = ? "  
    cQuery += "     AND CFD.CFD_COD BETWEEN  ? AND ? " 
    cQuery += "     AND CFD.D_E_L_E_T_ = ? " 

    // Instancia a classe
    oPrepare := FwExecStatement():New(cQuery)

    oPrepare:setString( 1, xFilial("SB1") ) 
    oPrepare:setString( 2, ' ' )
    oPrepare:setString( 3, xFilial("CFD")  )
    oPrepare:setString( 4, cProdDe )
    oPrepare:setString( 5, cProdAte )
    oPrepare:setString( 6, ' ' )
    
    cAlias := oPrepare:OpenAlias()  

    While !(cAlias)->(Eof())

        self:oData:appendData({ "FILIAL" : (cAlias)->CFD_FILIAL,;
                                "CODIGO" : Alltrim((cAlias)->CFD_COD),;
                                "PERCAL" : transform((cAlias)->CFD_PERCAL, "@R 99-9999"),; 
                                "PERVEN" : transform((cAlias)->CFD_PERVEN, "@R 99-9999"),; 
                                "VPARIM" : ((cAlias)->CFD_VPARIM),;
                                "VSAIIE" : ((cAlias)->CFD_VSAIIE),;
                                "CONIMP" : ((cAlias)->CFD_CONIMP),;
                                "DESC"   : Alltrim((cAlias)->B1_DESC),;
                                "UM"     : ((cAlias)->B1_UM),;
                                "POSIPI" : ((cAlias)->B1_POSIPI),;
                                "CODBAR" : Alltrim((cAlias)->B1_CODBAR)   } )
        (cAlias)->( dbSkip() )
    Enddo

    (cAlias)->( DBCloseArea() )
    oPrepare:Destroy()
    oPrepare := nil
    freeobj(oFiltQry)

return self:oData
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
@return object: self:oSchema
@author Evandro Italo
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class fciSyntheticSmartviewBusinessObject

    //              ( Nome   ,  Descricao                   ,  Tipo   , Display                     , cRealName
    self:addProperty("FILIAL", "Filial"                     , "string", "Filial"                    , "CFD_FILIAL"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CODIGO", "Código do Produto"          , "string", "Código do Produto"         , "CFD_COD"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("PERCAL", "Período do Calculo"         , "string", "Período do Cálculo"        , "CFD_PERCAL"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("PERVEN", "Periodo de vendas"          , "string", "Período de vendas"         , "CFD_PERVEN"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VPARIM", "Valor da Parcela Importada" , "number", "Valor da Parcela Importada", "CFD_VPARIM"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VSAIIE", "Valor Saida Interestadual"  , "number", "Valor Saida Interestadual" , "CFD_VSAIIE"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CONIMP", "Conteudo de Importacao"     , "number", "Conteúdo de Importação"    , "CFD_CONIMP"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("DESC"  , "Descrição do Produto"       , "string", "Descrição do Produto"      , "B1_DESC"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("UM"    , "Unidade de Medida"          , "string", "Unidade de Medida"         , "B1_UM"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("POSIPI", "Nomenclatura Ext.Mercosul"  , "string", "Nomenclatura Ext.Mercosul" , "B1_POSIPI"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CODBAR", "Codigo de Barras"           , "string", "Código de Barras"          , "B1_CODBAR "  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )

return self:oSchema
