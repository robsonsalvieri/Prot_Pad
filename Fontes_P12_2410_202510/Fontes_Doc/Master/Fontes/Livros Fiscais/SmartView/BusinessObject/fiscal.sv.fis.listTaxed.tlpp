#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.listTaxed.ch"
    
namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAFIS", tables="SF3,SFT,SB1,SA1,SA2",name="Listagem de Entradas e Saídas", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*{Protheus.doc}  listTaxedSmartviewBusinessObject
Classe para criação do Objeto listtaxed - Notas fiscais de entrada e saída com ICMS, IPI ou ISS tributado
@author Raphael Ventura
@since 14/07/2023
*/
//-------------------------------------------------------------------  
class listTaxedSmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    data nPageSize as integer


    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author Raphael Ventura
@since 14/07/2023
*/
//-------------------------------------------------------------------   
method new() class listTaxedSmartviewBusinessObject
    Local oFisTools := fisSmartviewTools():new()
    _Super:new()
        //Define Area
    self:appendArea(STR0001)//"Livros Fiscais"
        //Define o nome do objeto de Negócio
    self:setDisplayName(STR0002)//"Objeto de Negócio Listagem de Entradas e Saídas"
        //Define a descrição do objeto de Negócio
    self:setDescription(STR0003)//"Objeto listtaxed - Objeto de Negócio Listagem de Entradas e Saídas (SF3/SFT/SB1/SA1/SA2)"

    self:nPageSize := oFisTools:paginacao(3000)
    oFisTools:validPerg(self:setPergunte("FISTRR9201"))
    freeobj(oFisTools)

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: self:oData 
@author Raphael Ventura
@since 14/07/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class listTaxedSmartviewBusinessObject

    Local cQuery                                                         as Character
    Local cAlias                                                         as Character
    Local oPrepare                                                       as object
    Local oFisTools  := fisSmartviewTools():New()                        as object
    Local oFiltQry   := oFilter:getParameters()                          as Json
    Local dStartDate := FwDateTimeToLocal(oFiltQry[ 'MV_PAR01' ,1])[1]   as date
    Local dEndDate   := FwDateTimeToLocal(oFiltQry[ 'MV_PAR02' ,1])[1]   as date
    Local cFilDe     := oFiltQry[ 'MV_PAR03' ,1]                         as Character
    Local cFilAte    := oFiltQry[ 'MV_PAR04' ,1]                         as Character        
    Local cTipoMov                                                       as Character    
    Local aFiliais   := {}                                               as array    
    Local cFils      := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character
    local aBind      := {}                                               as array
    Local nFilial                                                        as numeric
    local nlA                                                            as numeric    
    Local lOk        := .T.                                              as logical

    if cFils == 'Erro'
        oFisTools:validFil(.f.)
        lOk := .f.
    endif    
    
    if lOk

        cTipoMov := filterMoviment(oFiltQry)

        cQuery += "     SELECT  "
        cQuery += "             SF3.F3_FILIAL, "        
        cQuery += "             CASE "
        cQuery += "                 WHEN SF3.F3_CODRSEF = '00' THEN 'Documento Regular' "
        cQuery += "                 WHEN SF3.F3_CODRSEF = '01' THEN 'Documento Regular Extemporâneo' "
        cQuery += "                 WHEN SF3.F3_CODRSEF = '02' THEN 'Documento Cancelado' "
        cQuery += "                 WHEN SF3.F3_CODRSEF = '03' THEN 'Documento Cancelado Extemporâneo' "
        cQuery += "                 WHEN SF3.F3_CODRSEF = '04' THEN 'NFe ou CTe Denegada' "
        cQuery += "                 WHEN SF3.F3_CODRSEF = '05' THEN 'NFe ou CTe Numeração Inutilizada' "
        cQuery += "                 WHEN SF3.F3_CODRSEF = '06' THEN 'Documento Fiscal Complementar' "
        cQuery += "                 WHEN SF3.F3_CODRSEF = '07' THEN 'Documento Fiscal Complementar Extemporâneo' "
        cQuery += "                 WHEN SF3.F3_CODRSEF = '08' THEN 'Documento Fiscal emitido com base em Regime Especial ou Norma Específica' "
        cQuery += "                 WHEN SF3.F3_CODRSEF = '09' THEN 'Documento Fiscal Substituído' "
        cQuery += "                 ELSE '' "
        cQuery += "             END AS CODRSEF, "
        cQuery += "             SF3.F3_NFISCAL, "
        cQuery += "             SF3.F3_SERIE, "
        cQuery += "             SF3.F3_CLIEFOR, "
        cQuery += "             SF3.F3_LOJA, "
        cQuery += "             CASE "
        cQuery += "                 WHEN SFT.FT_TIPOMOV = 'E' AND SFT.FT_TIPO <> 'B' AND SFT.FT_TIPO <> 'D' THEN SA2.A2_NOME "
        cQuery += "                 WHEN SFT.FT_TIPOMOV = 'E' AND (SFT.FT_TIPO = 'B' OR SFT.FT_TIPO = 'D') THEN SA1.A1_NOME "
        cQuery += "                 WHEN SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO <> 'B' AND SFT.FT_TIPO <> 'D' THEN SA1.A1_NOME "
        cQuery += "                 WHEN SFT.FT_TIPOMOV = 'S' AND (SFT.FT_TIPO = 'B' OR SFT.FT_TIPO = 'D') THEN SA2.A2_NOME "
        cQuery += "             END AS NOME_PART, "
        cQuery += "             CASE "
        cQuery += "                 WHEN SFT.FT_TIPOMOV = 'E' AND SFT.FT_TIPO <> 'B' AND SFT.FT_TIPO <> 'D' THEN SA2.A2_CGC "
        cQuery += "                 WHEN SFT.FT_TIPOMOV = 'E' AND (SFT.FT_TIPO = 'B' OR SFT.FT_TIPO = 'D') THEN SA1.A1_CGC "
        cQuery += "                 WHEN SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO <> 'B' AND SFT.FT_TIPO <> 'D' THEN SA1.A1_CGC "
        cQuery += "                 WHEN SFT.FT_TIPOMOV = 'S' AND (SFT.FT_TIPO = 'B' OR SFT.FT_TIPO = 'D') THEN SA2.A2_CGC "        
        cQuery += "             END AS CNPJ, "
        cQuery += "             CASE "
        cQuery += "                 WHEN SFT.FT_TIPOMOV = 'E' AND SFT.FT_TIPO <> 'B' AND SFT.FT_TIPO <> 'D' THEN SA2.A2_CONTA "
        cQuery += "                 WHEN SFT.FT_TIPOMOV = 'E' AND (SFT.FT_TIPO = 'B' OR SFT.FT_TIPO = 'D') THEN SA1.A1_CONTA "
        cQuery += "                 WHEN SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO <> 'B' AND SFT.FT_TIPO <> 'D' THEN SA1.A1_CONTA "
        cQuery += "                 WHEN SFT.FT_TIPOMOV = 'S' AND (SFT.FT_TIPO = 'B' OR SFT.FT_TIPO = 'D') THEN SA2.A2_CONTA "
        cQuery += "             END AS CONTA, "
        cQuery += "             SF3.F3_TIPO, "
        cQuery += "             SF3.F3_EMISSAO, "
        cQuery += "             SF3.F3_ENTRADA, "        
        cQuery += "             SF3.F3_ESTADO AS UFORIGEM, "
        cQuery += "             SF3.F3_ESPECIE, "
        cQuery += "             SF3.F3_DTCANC, "
        cQuery += "             SFT.FT_ITEM, "
        cQuery += "             SFT.FT_PRODUTO, "
        cQuery += "             SFT.FT_POSIPI, "
        cQuery += "             SFT.FT_TES, "
        cQuery += "             CASE "
        cQuery += "	                WHEN SFT.FT_TIPOMOV = 'E' THEN 'Entrada' "
        cQuery += "	                WHEN SFT.FT_TIPOMOV = 'S' THEN 'Saída' "
        cQuery += "             END AS FT_TIPOMOV, "
        cQuery += "             CASE "
        cQuery += "	                WHEN SFT.FT_TIPO <> 'S' THEN 'Não' "
        cQuery += "	                WHEN SFT.FT_TIPO = 'S' THEN 'Sim' "
        cQuery += "             END AS SERVICO, "
        cQuery += "             SFT.FT_VALCONT, "
        cQuery += "             SFT.FT_CFOP, "
        cQuery += "             SFT.FT_CLASFIS AS CST_ICMS, "
        cQuery += "             CASE  "
        cQuery += "	                WHEN SFT.FT_TIPO = 'S' THEN 0 "
        cQuery += "	                ELSE SFT.FT_BASEICM "
        cQuery += "             END AS FT_BASEICM, "
        cQuery += "             CASE  "
        cQuery += "	                WHEN SFT.FT_TIPO = 'S' THEN 0 "
        cQuery += "	                ELSE SFT.FT_ALIQICM "
        cQuery += "             END AS FT_ALIQICM, "
        cQuery += "             CASE  "
        cQuery += "	                WHEN SFT.FT_TIPO = 'S' THEN 0 "
        cQuery += "	                ELSE SFT.FT_VALICM "
        cQuery += "             END AS FT_VALICM, "
        cQuery += "             CASE  "
        cQuery += "	                WHEN SFT.FT_TIPO = 'S' THEN 0 "
        cQuery += "	                ELSE SFT.FT_ISENICM "
        cQuery += "             END AS FT_ISENICM, "
        cQuery += "             CASE  "
        cQuery += "	                WHEN SFT.FT_TIPO = 'S' THEN 0 "
        cQuery += "	                ELSE SFT.FT_OUTRICM "
        cQuery += "             END AS FT_OUTRICM, "
        cQuery += "             SFT.FT_VALFECP, "
        cQuery += "             SFT.FT_CTIPI AS CST_IPI, "
        cQuery += "             SFT.FT_BASEIPI, "
        cQuery += "             SFT.FT_ALIQIPI, "
        cQuery += "             SFT.FT_VALIPI, "
        cQuery += "             SFT.FT_ISENIPI, "
        cQuery += "             SFT.FT_OUTRIPI, "
        cQuery += "             SFT.FT_CODISS AS CST_ISS, "
        cQuery += "             CASE "
        cQuery += "	                WHEN SFT.FT_TIPO = 'S' THEN SFT.FT_BASEICM "
        cQuery += "	                ELSE 0 "
        cQuery += "             END AS BASEISS, "
        cQuery += "             CASE  "
        cQuery += "	                WHEN SFT.FT_TIPO = 'S' THEN SFT.FT_ALIQICM "
        cQuery += "	                ELSE 0 "
        cQuery += "             END AS ALIQISS, "
        cQuery += "             CASE  "
        cQuery += "	                WHEN SFT.FT_TIPO = 'S' THEN SFT.FT_VALICM "
        cQuery += "	                ELSE 0 "
        cQuery += "             END AS VALISS, "
        cQuery += "             CASE  "
        cQuery += "	                WHEN SFT.FT_TIPO = 'S' THEN SFT.FT_ISENICM "
        cQuery += "	                ELSE 0 "
        cQuery += "             END AS ISENISS, "
        cQuery += "             CASE  "
        cQuery += "	                WHEN SFT.FT_TIPO = 'S' THEN SFT.FT_OUTRICM "
        cQuery += "	                ELSE 0 "
        cQuery += "             END AS OUTRISS, "
        cQuery += "             SFT.FT_OBSERV "
        If cTipoMov == 'E' // Se for tipo de movimento E, notas de entrada
            cQuery += "         ,SF1.F1_MENNOTA "
        EndIf
        cQuery += "         from "
        cQuery +=               RetSqlName("SF3") + " SF3 "
        cQuery += "             INNER JOIN " + RetSqlName("SFT") + " SFT    ON SFT.FT_FILIAL = ? " //1
        If !Empty(cTipoMov)
            cQuery += "                                                     AND SFT.FT_TIPOMOV = ? " //2
        Else
            cQuery += "                                                     AND SFT.FT_TIPOMOV =  (CASE WHEN SF3.F3_CFO >= '5' THEN 'S' ELSE 'E' END) "            
        EndIf        
        cQuery += "         							                    AND SFT.FT_NFISCAL = SF3.F3_NFISCAL "
        cQuery += "         							                    AND SFT.FT_SERIE = SF3.F3_SERIE "
        cQuery += "         							                    AND SFT.FT_CLIEFOR = SF3.F3_CLIEFOR "
        cQuery += "         							                    AND SFT.FT_LOJA = SF3.F3_LOJA "
        cQuery += "                                                         AND SFT.FT_IDENTF3	= SF3.F3_IDENTFT
        cQuery += "         							                    AND SFT.D_E_L_E_T_ = ? " //3
        If cTipoMov == 'E' 
            cQuery += "         INNER JOIN " + RetSqlName("SF1") + " SF1    ON SF1.F1_FILIAL = ? " //4
            cQuery += "         							                AND SF1.F1_DOC = SF3.F3_NFISCAL "
            cQuery += "         							                AND SF1.F1_SERIE = SF3.F3_SERIE "
            cQuery += "         							                AND SF1.F1_FORNECE = SF3.F3_CLIEFOR "
            cQuery += "         							                AND SF1.F1_LOJA = SF3.F3_LOJA "
            cQuery += "         							                AND SF1.D_E_L_E_T_ = ? " //5
        EndIf
        cQuery += "             LEFT JOIN " + RetSqlName("SA1") + " SA1     ON SA1.A1_FILIAL = ? " //6
        cQuery += "         							                    AND SA1.A1_COD = SF3.F3_CLIEFOR "
        cQuery += "         							                    AND SA1.A1_LOJA = SF3.F3_LOJA "
        cQuery += "         							                    AND SA1.D_E_L_E_T_ = ? " //7
        cQuery += "             LEFT JOIN " + RetSqlName("SA2") + " SA2     ON SA2.A2_FILIAL = ? " //8
        cQuery += "         							                    AND SA2.A2_COD = SF3.F3_CLIEFOR "
        cQuery += "         							                    AND SA2.A2_LOJA = SF3.F3_LOJA "
        cQuery += "         							                    AND SA2.D_E_L_E_T_ = ? " //9
        cQuery += "         WHERE "
        cQuery += "	            SF3.F3_ENTRADA BETWEEN ? AND ? " //10 //11
        cQuery += "             AND SF3.F3_FILIAL =  ? " //12
        cQuery += "	            AND SF3.D_E_L_E_T_ = ? "   //13     

        If !Empty(cTipoMov)
            If cTipoMov == 'E'
                cQuery += " AND SF3.F3_CFO < '5' "
            Else
                cQuery += " AND SF3.F3_CFO >= '5' " 
            Endif
        EndIf        
        
        if oFilter:hasFilter() // Filtros setados na interface do Smart View
            cQuery += " AND ? " //14
        endif

        oPrepare := FwExecStatement():New( ChangeQuery(cQuery) )        

        //Percorre as filiais
        For nFilial := 1 to Len(aFiliais)


            aadd(aBind, {'C', xFilial( 'SFT', aFiliais[nFilial] )}) //1

            iF !empty(cTipoMov)
                aadd(aBind, {'C', cTipoMov}) //2
            Endif
            aadd(aBind, {'C', ' '}) //3
            If cTipoMov == 'E' // Se for tipo de movimento E, notas de entrada
                aadd(aBind, {'C', xFilial( 'SF1', aFiliais[nFilial] )}) //4
                aadd(aBind, {'C', ' '}) //5
            EndIf
            aadd(aBind, {'C', xFilial( 'SA1', aFiliais[nFilial] )}) //6
            aadd(aBind, {'C', ' '}) //7
            aadd(aBind, {'C', xFilial( 'SA2', aFiliais[nFilial] )}) //8
            aadd(aBind, {'C', ' '}) //9
            aadd(aBind, {'D', dStartDate}) //10
            aadd(aBind, {'D', dEndDate}) //11
            aadd(aBind, {'C', xFilial( 'SF3', aFiliais[nFilial] )}) //12
            aadd(aBind, {'C', ' '}) //13

            if oFilter:hasFilter() // Filtros setados na interface do Smart View
                aadd(aBind, {'C', oFilter:getSQLExpression() }) //14
            endif

            For nlA := 1 to len(aBind)

                If aBind[nlA][1] == 'C'                    
                    oPrepare:setString( nlA, aBind[nlA][2] )
                Elseif aBind[nlA][1] == 'D'
                    oPrepare:setDate( nlA, aBind[nlA][2] )
                Endif

            next nlA

            cAlias := oPrepare:OpenAlias()

            If cTipoMov == 'E' // Se for tipo de movimento E, notas de entrada

                while !(cAlias)->(Eof())            

                    self:oData:appendData({ "F3_FILIAL"     : (cAlias)->F3_FILIAL,;
                                            "F3_NFISCAL"    : alltrim((cAlias)->F3_NFISCAL),;
                                            "F3_SERIE"      : (cAlias)->F3_SERIE,; 
                                            "F3_CLIEFOR"    : (cAlias)->F3_CLIEFOR,;
                                            "F3_LOJA"       : (cAlias)->F3_LOJA,;
                                            "NOME_PART"     : (cAlias)->NOME_PART,;
                                            "CNPJ"          : (cAlias)->CNPJ,;
                                            "CONTA"         : (cAlias)->CONTA,;
                                            "F3_TIPO"       : (cAlias)->F3_TIPO,;
                                            "F3_EMISSAO"    : totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->F3_EMISSAO)), ; 
                                            "F3_ENTRADA"    : totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->F3_ENTRADA)), ;
                                            "F3_DTCANC"     : totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->F3_DTCANC)), ; 
                                            "UFORIGEM"      : (cAlias)->UFORIGEM,; 
                                            "F3_ESPECIE"    : (cAlias)->F3_ESPECIE,;
                                            "FT_ITEM"       : (cAlias)->FT_ITEM,;
                                            "FT_PRODUTO"    : (cAlias)->FT_PRODUTO,;
                                            "FT_POSIPI"     : (cAlias)->FT_POSIPI,;
                                            "FT_TES"        : (cAlias)->FT_TES,;
                                            "FT_TIPOMOV"    : (cAlias)->FT_TIPOMOV,;
                                            "SERVICO"       : (cAlias)->SERVICO,;
                                            "FT_VALCONT"    : (cAlias)->FT_VALCONT,;
                                            "FT_CFOP"       : (cAlias)->FT_CFOP,;
                                            "CST_ICMS"      : (cAlias)->CST_ICMS,;
                                            "FT_BASEICM"    : (cAlias)->FT_BASEICM,;
                                            "FT_ALIQICM"    : (cAlias)->FT_ALIQICM,;
                                            "FT_VALICM"     : (cAlias)->FT_VALICM,;
                                            "FT_ISENICM"    : (cAlias)->FT_ISENICM,;
                                            "FT_OUTRICM"    : (cAlias)->FT_OUTRICM,;
                                            "FT_VALFECP"    : (cAlias)->FT_VALFECP,;
                                            "CST_IPI"       : (cAlias)->CST_IPI,;
                                            "FT_BASEIPI"    : (cAlias)->FT_BASEIPI,;
                                            "FT_ALIQIPI"    : (cAlias)->FT_ALIQIPI,;
                                            "FT_VALIPI"     : (cAlias)->FT_VALIPI,;
                                            "FT_ISENIPI"    : (cAlias)->FT_ISENIPI,;
                                            "FT_OUTRIPI"    : (cAlias)->FT_OUTRIPI,;
                                            "CST_ISS"       : (cAlias)->CST_ISS,;
                                            "BASEISS"       : (cAlias)->BASEISS,; 
                                            "ALIQISS"       : (cAlias)->ALIQISS,; 
                                            "VALISS"        : (cAlias)->VALISS,; 
                                            "ISENISS"       : (cAlias)->ISENISS,; 
                                            "OUTRISS"       : (cAlias)->OUTRISS,; 
                                            "CODRSEF"       : (cAlias)->CODRSEF,; 
                                            "FT_OBSERV"     : (cAlias)->FT_OBSERV,;
                                            "F1_MENNOTA"   : (cAlias)->F1_MENNOTA;
                                            })
                                     
                (cAlias)->(dbSkip())
                enddo

            Else

                while !(cAlias)->(Eof())            

                    self:oData:appendData({ "F3_FILIAL"     : (cAlias)->F3_FILIAL,;
                                            "F3_NFISCAL"    : alltrim((cAlias)->F3_NFISCAL),;
                                            "F3_SERIE"      : (cAlias)->F3_SERIE,; 
                                            "F3_CLIEFOR"    : (cAlias)->F3_CLIEFOR,;
                                            "F3_LOJA"       : (cAlias)->F3_LOJA,;
                                            "NOME_PART"     : (cAlias)->NOME_PART,;
                                            "CNPJ"          : (cAlias)->CNPJ,;
                                            "CONTA"         : (cAlias)->CONTA,;
                                            "F3_TIPO"       : (cAlias)->F3_TIPO,;
                                            "F3_EMISSAO"    : totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->F3_EMISSAO)), ; 
                                            "F3_ENTRADA"    : totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->F3_ENTRADA)), ;
                                            "F3_DTCANC"     : totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->F3_DTCANC)), ; 
                                            "UFORIGEM"      : (cAlias)->UFORIGEM,; 
                                            "F3_ESPECIE"    : (cAlias)->F3_ESPECIE,;
                                            "FT_ITEM"       : (cAlias)->FT_ITEM,;
                                            "FT_PRODUTO"    : (cAlias)->FT_PRODUTO,;
                                            "FT_POSIPI"     : (cAlias)->FT_POSIPI,;
                                            "FT_TES"        : (cAlias)->FT_TES,;
                                            "FT_TIPOMOV"    : (cAlias)->FT_TIPOMOV,;
                                            "SERVICO"       : (cAlias)->SERVICO,;
                                            "FT_VALCONT"    : (cAlias)->FT_VALCONT,;
                                            "FT_CFOP"       : (cAlias)->FT_CFOP,;
                                            "CST_ICMS"      : (cAlias)->CST_ICMS,;
                                            "FT_BASEICM"    : (cAlias)->FT_BASEICM,;
                                            "FT_ALIQICM"    : (cAlias)->FT_ALIQICM,;
                                            "FT_VALICM"     : (cAlias)->FT_VALICM,;
                                            "FT_ISENICM"    : (cAlias)->FT_ISENICM,;
                                            "FT_OUTRICM"    : (cAlias)->FT_OUTRICM,;
                                            "FT_VALFECP"    : (cAlias)->FT_VALFECP,;
                                            "CST_IPI"       : (cAlias)->CST_IPI,;
                                            "FT_BASEIPI"    : (cAlias)->FT_BASEIPI,;
                                            "FT_ALIQIPI"    : (cAlias)->FT_ALIQIPI,;
                                            "FT_VALIPI"     : (cAlias)->FT_VALIPI,;
                                            "FT_ISENIPI"    : (cAlias)->FT_ISENIPI,;
                                            "FT_OUTRIPI"    : (cAlias)->FT_OUTRIPI,;
                                            "CST_ISS"       : (cAlias)->CST_ISS,;
                                            "BASEISS"       : (cAlias)->BASEISS,; 
                                            "ALIQISS"       : (cAlias)->ALIQISS,; 
                                            "VALISS"        : (cAlias)->VALISS,; 
                                            "ISENISS"       : (cAlias)->ISENISS,; 
                                            "OUTRISS"       : (cAlias)->OUTRISS,; 
                                            "CODRSEF"       : (cAlias)->CODRSEF,; 
                                            "FT_OBSERV"     : (cAlias)->FT_OBSERV;
                                            })
                                     
                (cAlias)->(dbSkip())
                enddo
            EndIf
        
            (cAlias)->(DBCloseArea())
            
            aSize(aBind,0)            
        Next nFilial
    ENDIF
    
    freeobj(oFisTools)
    
    aSize(aBind,0)
    aBind    := Nil

    freeObj(oPrepare)
    freeobj(oFiltQry)
    
    aSize(aFiliais,0)
    aFiliais := Nil

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author Raphael Ventura
@since 14/07/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class listTaxedSmartviewBusinessObject

    //              ( Nome,             Descricao ,                                     Tipo ,      Display ,               cRealName ,     /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F3_FILIAL",       "Filial" ,                                      "string",   "Filial" ,              "F3_FILIAL" ,   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F3_NFISCAL",      "Número do documento fiscal" ,                  "string",   "Nota Fiscal" ,         "F3_NFISCAL",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F3_SERIE",        "Série do documento fiscal" ,                   "string",   "Série" ,               "F3_SERIE",     /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F3_CLIEFOR",      "Código do participante (cliente/fornecedor)" , "string",   "Cód. particip" ,       "F3_CLIEFOR",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F3_LOJA",         "Loja do participante" ,                        "string",   "Loja part." ,          "F3_LOJA",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("NOME_PART",       "Nome do participante" ,                        "string",   "Nome part." ,          "NOME_PART",    /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("CNPJ",            "CNPJ" ,                                        "string",   "CNPJ" ,                "CNPJ",         /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("CONTA",           "Conta Contábil" ,                              "string",   "Conta Ctb." ,          "CONTA",        /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F3_TIPO",         "Tipo" ,                                        "string",   "Tipo" ,                "F3_TIPO",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F3_EMISSAO",      "Data Emissão" ,                                "date" ,    "Data Emissão" ,        "F3_EMISSAO",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F3_ENTRADA",      "Data Entrada" ,                                "date" ,    "Data Entrada" ,        "F3_ENTRADA",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F3_DTCANC",       "Data Cancelamento" ,                           "date" ,    "Data Canc." ,          "F3_DTCANC",    /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("UFORIGEM",        "UF Origem" ,                                   "string",   "UF Orig." ,            "UFORIGEM",     /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F3_ESPECIE",      "Espécie do documento fiscal" ,                 "string",   "Espécie" ,             "F3_ESPECIE",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_ITEM",         "Item",                                         "string",   "Item",                 "FT_ITEM",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_PRODUTO",      "Codigo Produto",                               "string",   "Cod. Produto",         "FT_PRODUTO",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_POSIPI",       "NCM",                                          "string",   "NCM",                  "FT_POSIPI",    /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_TES",          "Codigo TES",                                   "string",   "Cod. TES",             "FT_TES",       /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )     
    self:addProperty("FT_TIPOMOV",      "Tipo de Movimentação" ,                        "string",   "Tipo Mov." ,           "FT_TIPOMOV",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("SERVICO",         "Serviço?" ,                                    "string",   "Serviço?" ,            "SERVICO",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_VALCONT",      "Valor Contábil",                               "number",   "Valor Contábil",       "FT_VALCONT",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_CFOP",         "CFOP" ,                                        "string",   "CFOP" ,                "FT_CFOP",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("CST_ICMS",        "CST ICMS" ,                                    "string",   "CST ICMS" ,            "CST_ICMS",     /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_BASEICM",      "Base ICMS",                                    "number",   "Base ICMS",            "FT_BASEICM",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_ALIQICM",      "Alíquota ICMS",                                "number",   "Alíq. ICMS",           "FT_ALIQICM",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_VALICM",       "Valor ICMS",                                   "number",   "Valor ICMS",           "FT_VALICM",    /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_ISENICM",      "Valor Isenção ICMS",                           "number",   "Val. Isen. ICMS",      "FT_ISENICM",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_OUTRICM",      "Valor Outros ICMS",                            "number",   "Val. Out. ICMS",       "FT_OUTRICM",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_VALFECP",      "Valor FECP",                                   "number",   "Val. FECP",            "FT_VALFECP",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("CST_IPI",         "CST IPI" ,                                     "string",   "CST IPI" ,             "CST_IPI",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_BASEIPI",      "Base IPI",                                     "number",   "Base IPI",             "FT_BASEIPI",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_ALIQIPI",      "Alíquota IPI",                                 "number",   "Alíq. IPI",            "FT_ALIQIPI",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_VALIPI",       "Valor IPI",                                    "number",   "Valor IPI",            "FT_VALIPI",    /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_ISENIPI",      "Valor Isenção IPI",                            "number",   "Val. Isen. IPI",       "FT_ISENIPI",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_OUTRIPI",      "Valor Outros IPI",                             "number",   "Val. Out. IPI",        "FT_OUTRIPI",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("CST_ISS",         "CST ISS" ,                                     "string",   "CST ISS" ,             "CST_ISS",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("BASEISS",         "Base ISS",                                     "number",   "Base ISS",             "BASEISS",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("ALIQISS",         "Alíquota ISS",                                 "number",   "Alíq. ISS",            "ALIQISS",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("VALISS",          "Valor ISS",                                    "number",   "Valor ISS",            "VALISS",       /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("ISENISS",         "Valor Isenção ISS",                            "number",   "Val. Isen. ISS",       "ISENISS",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("OUTRISS",         "Valor Outros ISS",                             "number",   "Val. Out. ISS",        "OUTRISS",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("CODRSEF",         "Situação" ,                                    "string",   "Situação" ,            "CODRSEF",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_OBSERV",       "Observação" ,                                  "string",   "Obs." ,                "FT_OBSERV",    /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F1_MENNOTA",      "Mensagem p/ nota" ,                            "string",   "Mensg p nota" ,        "F1_MENNOTA",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} filterMoviment
    (long_description)
    @type  Static Function
    @author Rafael Oliveira
    @since 20/08/2024
    @version 12.1.2310
    @param oFiltQry, json, contém o filtro do TReports
    @return cTipoMov, character, retorna o tipo de movimentação    
/*/
Static Function filterMoviment(oFiltQry)

    Local cTipoMov    as Character
    Local cParTipoMov as Character    

    If len(oFiltQry:Getnames()) >= 5 .and. !Empty(oFiltQry[ 'MV_PAR05' ,1])
        cParTipoMov := Alltrim(cValtoChar(oFiltQry[ 'MV_PAR05' ,1]))

        If cParTipoMov $ "1|2"

            If cParTipoMov == "1"
                cTipoMov := "E"
                
            ElseIf cParTipoMov == "2"
                cTipoMov := "S"                
            Endif

        EndIf
    EndIf

Return cTipoMov
