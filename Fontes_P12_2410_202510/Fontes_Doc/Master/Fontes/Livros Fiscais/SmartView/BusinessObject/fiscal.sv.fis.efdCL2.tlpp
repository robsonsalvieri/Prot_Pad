#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.efdCL2.ch"

namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAFIS", tables="CL2",;
	name="efdCL2SmartviewBusinessObject", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*{Protheus.doc} efdCL2SmartviewBusinessObject
@author eduardo.melo
@since 31/07/2023
*/
//-------------------------------------------------------------------  
class efdCL2SmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	data cDBMS as character

	public method new() as object
	public method getData() as object
	public method getSchema() as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author eduardo.melo
@since 31/07/2023
*/
//-------------------------------------------------------------------   
method new() class efdCL2SmartviewBusinessObject

	Local oFisTools := fisSmartviewTools():new()

	_Super:new()
	//Define Area
	self:appendArea(STR0001) //"Livros Fiscais"
	//Define o nome do objeto de Negócio
	self:setDisplayName(STR0002) //"Objeto de Negócio EFD Contribuiçoes - CL2"
	//Define a descrição do objeto de Negócio
	self:setDescription(STR0003) //"Objeto de negócio que fornece o detalhamento do EFD Contribuições (CL2)"
	//self:setIsCBoxLookup(.T., .F.)

	oFisTools:validPerg(::setPergunte("FISTRR1016"))

	self:cDBMS := UPPER(TcGetDb()) //verifica qual banco é utilizado

	freeobj(oFisTools)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: self:oData 
@author eduardo.melo
@since 31/07/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class efdCL2SmartviewBusinessObject

	Local cQuery     as Character
	Local cAlias     as Character
	Local oFisTools  := fisSmartviewTools():new()
	Local oFiltQry   := oFilter:getParameters() as Json
	Local dStartDate := FwDateTimeToLocal(oFiltQry['MV_PAR01',1])[1] as date
	Local dEndDate   := FwDateTimeToLocal(oFiltQry['MV_PAR02',1])[1] as date
	Local cFilDe     := oFiltQry['MV_PAR03',1] as Character
	Local cFilAte    := oFiltQry['MV_PAR04',1] as Character
	Local lOk		 := .T. as Logical
	Local aFiliais   := {} as array
	Local cFils      := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character
	Local oPrepare   := nil as object

	if cFils == 'Erro'
		oFisTools:validFil(.f.)
		lOk := .f.
	endif

	If lOk

		cQuery += " SELECT "
		cQuery += " 	CL2_PER,"
		cQuery += " 	CL2_NUMTIT,"
		cQuery += " 	CL2_VLOPER,"
		cQuery += " 	CL2_VLCOF,"
		cQuery += " 	CL2_PREFIX,"
		cQuery += " 	CASE WHEN CL2_REGIME = '1' THEN '1-Não Cumulativo' WHEN CL2_REGIME = '2' THEN '2-Cumulativo' ELSE '' END CL2_REGIME,"
		cQuery += " 	CL2_VLPIS,"
		cQuery += " 	CL2_INDOP,"
		cQuery += " 	CL2_PARC,"
		cQuery += " 	CL2_CODBCC,"
		cQuery += " 	CL2_CSTCOF,"
		cQuery += " 	CL2_FILIAL,"
		cQuery += " 	CL2_IDCF8,"
		cQuery += " 	CL2_CSTPIS,"
		cQuery += " 	CL2_BCCOF,"
		cQuery += " 	CL2_DTOPER,"
		cQuery += " 	CL2_ALQCOF,"
		cQuery += " 	CL2_CODATF,"
		cQuery += " 	CL2_DESATF,"
		cQuery += " 	CL2_ITATF,"
		cQuery += " 	CL2_CRTCRD,"
		cQuery += " 	CL2_BCPIS,"
		cQuery += " 	CL2_ALQPIS,"
		cQuery += " 	CL2_REG,"
		cQuery += "     CASE "
		cQuery += "         WHEN CL2.CL2_REG = 'F100' THEN 'F100-Demais Documentos e Operações geradoras de Contribuição e Créditos' "
		cQuery += "         WHEN CL2.CL2_REG = 'F120' THEN 'F120-Bens Incorporados ao Ativo Imobilizado - Operações geradoras de Créditos com base nos Encargos de Depreciação e Amortização' "
		cQuery += "         WHEN CL2.CL2_REG = 'F130' THEN 'F130-Bens Incorporados ao Ativo Imobilizado - Operações geradoras de Créditos com base no Valor de Aquisição' "
		cQuery += "         ELSE '' "
		cQuery += "     END AS CL2_REGDSC, "		
		cQuery += " '' AS TITULO"
		cQuery += " FROM "
		cQuery += RetSqlName("CL2")+" CL2 "
		cQuery += " WHERE CL2_FILIAL IN ( ? )"
		cQuery += " AND CL2_PER BETWEEN ? AND ? "
		cQuery += " AND D_E_L_E_T_ = ? "
		cQuery += " ORDER BY CL2_FILIAL,CL2_PER "

		oPrepare := FWExecStatement():New(ChangeQuery(cQuery))

		
		oPrepare:setIn(1,aFiliais)
		oPrepare:setDate(2,dStartDate )
		oPrepare:setDate(3,dEndDate)
		oPrepare:setString(4,' ')

		cAlias:= oPrepare:OpenAlias()

		while !(cAlias)->(Eof())

			self:oData:appendData({;
				"CL2_REG"		: (cAlias)->CL2_REG,;
				"CL2_REGDSC"    : (cAlias)->CL2_REGDSC,;
				"CL2_NUMTIT"    : (cAlias)->CL2_NUMTIT,;
				"CL2_VLOPER"    : (cAlias)->CL2_VLOPER,;
				"CL2_VLCOF"     : (cAlias)->CL2_VLCOF,;
				"CL2_PREFIX"    : (cAlias)->CL2_PREFIX,;
				"CL2_REGIME"    : (cAlias)->CL2_REGIME,;
				"CL2_VLPIS"     : (cAlias)->CL2_VLPIS,;
				"CL2_INDOP"     : (cAlias)->CL2_INDOP,;
				"CL2_PARC"      : (cAlias)->CL2_PARC,;
				"CL2_CODBCC"    : (cAlias)->CL2_CODBCC,;
				"CL2_CSTCOF"    : (cAlias)->CL2_CSTCOF,;
				"CL2_FILIAL"    : (cAlias)->CL2_FILIAL,;
				"CL2_IDCF8"     : (cAlias)->CL2_IDCF8,;
				"CL2_CSTPIS"    : (cAlias)->CL2_CSTPIS,;
				"CL2_BCCOF"     : (cAlias)->CL2_BCCOF,;
				"CL2_DTOPER"    : totvs.framework.treports.date.stringToTimeStamp((cAlias)->CL2_DTOPER),;
				"CL2_ALQCOF"    : (cAlias)->CL2_ALQCOF,;
				"CL2_CODATF"    : (cAlias)->CL2_CODATF,;
				"CL2_DESATF"    : (cAlias)->CL2_DESATF,;
				"CL2_ITATF"     : (cAlias)->CL2_ITATF,;
				"CL2_CRTCRD"    : (cAlias)->CL2_CRTCRD,;
				"CL2_BCPIS"     : (cAlias)->CL2_BCPIS,;
				"CL2_ALQPIS"    : (cAlias)->CL2_ALQPIS,;
				"CL2_PER"       : totvs.framework.treports.date.stringToTimeStamp((cAlias)->CL2_PER),;				
				"TITULO"        : (cAlias)->CL2_PREFIX+(cAlias)->CL2_NUMTIT+(cAlias)->CL2_PARC;
				})

			(cAlias)->(dbSkip())

		enddo
		(cAlias)->(DBCloseArea())

	Endif

	freeobj(oFisTools)
	freeObj(oPrepare)
	freeobj(oFiltQry)
	aSize(aFiliais,0)
	aFiliais := Nil

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author eduardo.melo
@since 31/07/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class efdCL2SmartviewBusinessObject

	self:addProperty("CL2_ALQPIS"      ,"Alíquota de PIS"              ,"number"   ,"Alq.PIS"      ,"CL2_ALQPIS"   )
	self:addProperty("CL2_ALQCOF"      ,"Alíquota de COFINS"           ,"number"   ,"Alq.COF"      ,"CL2_ALQCOF"   )
	self:addProperty("CL2_BCCOF"       ,"Base de Cálculo da COFINS"    ,"number"   ,"Bc. COF"      ,"CL2_BCCOF"    )
	self:addProperty("CL2_BCPIS"       ,"Base de Cálculo de PIS"       ,"number"   ,"Bc. PIS"      ,"CL2_BCPIS"    )
	self:addProperty("CL2_CODATF"      ,"Código do Ativo Fixo"         ,"string"   ,"Cod. Ativo"   ,"CL2_CODATF"   )
	self:addProperty("CL2_CODBCC"      ,"Cód Base de Crédito"          ,"string"   ,"Cod. BCC"     ,"CL2_CODBCC"   )
	self:addProperty("CL2_CRTCRD"      ,"Critério do Crédito"          ,"string"   ,"Crt. Cred"    ,"CL2_CRTCRD"   )
	self:addProperty("CL2_CSTCOF"      ,"CST COF"                      ,"string"   ,"CST COF"      ,"CL2_CSTCOF"   )
	self:addProperty("CL2_CSTPIS"      ,"CST PIS"                      ,"string"   ,"CST PIS"      ,"CL2_CSTPIS"   )
	self:addProperty("CL2_DESATF"      ,"Descrição do Ativo"           ,"string"   ,"Descr. Ativo" ,"CL2_DESATF"   )
	self:addProperty("CL2_DTOPER"      ,"Data da Operação"             ,"date"     ,"Dt.Oper"      ,"CL2_DTOPER"   )
	self:addProperty("CL2_FILIAL"      ,"Filial"                       ,"string"   ,"Filial"       ,"CL2_FILIAL"   )
	self:addProperty("CL2_IDCF8"       ,"Demais Documentos"            ,"string"   ,"Demais Docs." ,"CL2_IDCF8"    )
	self:addProperty("CL2_INDOP"       ,"Indicador da Operação"        ,"string"   ,"Ind.Oper."    ,"CL2_INDOP"    )
	self:addProperty("CL2_ITATF"       ,"Número do Item do Ativo"      ,"string"   ,"Num.Item ATF" ,"CL2_ITATF"    )
	self:addProperty("CL2_NUMTIT"      ,"Número do Título"             ,"string"   ,"Num.Título"   ,"CL2_NUMTIT"   )
	self:addProperty("CL2_PARC"        ,"Parcela do Título"            ,"string"   ,"Parcela"      ,"CL2_PARC"     )
	self:addProperty("CL2_PREFIX"      ,"Prefixo do Título"            ,"string"   ,"Prefixo"      ,"CL2_PREFIX"   )
	self:addProperty("CL2_REG"         ,"Registro que será proc."      ,"string"   ,"Registro"     ,"CL2_REG"      )
	self:addProperty("CL2_REGIME"      ,"Regime"                       ,"string"   ,"Regime"       ,"CL2_REGIME"   )
	self:addProperty("CL2_VLCOF"       ,"Valor de COFINS"              ,"number"   ,"Vl.COF"       ,"CL2_VLCOF"    )
	self:addProperty("CL2_VLOPER"      ,"Valor da Operação"            ,"number"   ,"Vl.Oper."     ,"CL2_VLOPER"   )
	self:addProperty("CL2_VLPIS"       ,"Valor de PIS"                 ,"number"   ,"Vl.PIS"       ,"CL2_VLPIS"    )
	self:addProperty("CL2_PER"         ,"Periodo de Apuracao"          ,"date"     ,"Per.Apur."    ,"CL2_PER"      )
	self:addProperty("TITULO"          ,"Titulo da Linha"              ,"string"   ,"Titulo",      "TITULO"        )

return self:oSchema
