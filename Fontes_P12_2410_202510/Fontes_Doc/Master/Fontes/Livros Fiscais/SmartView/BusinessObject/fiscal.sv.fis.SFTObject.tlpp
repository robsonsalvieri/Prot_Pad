#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.SFTObject.ch"
 
namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", tables="SFT,CDA,SA1,SA2,SF6,SF2", name="Conferencia de ICMS e IPI", country="BRA", initialRelease="12.1.2310")
//-------------------------------------------------------------------
/*{Protheus.doc} SFTSmartViewBusinessObject
Classe para criação do Objeto SFTSmartViewBusinessObject - Notas fiscais de entrada e saída com cálculo de ICMS Próprio, 
ICMS Difal (saídas), ICMS Complementar (Entradas), ICMS-ST, FECP e IPI, escriturados como Tributado, Isento e Outros
 
@author Adilson Roberto
@since 16/05/2023
*/
//-------------------------------------------------------------------  
class SFTSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    data nPageSize as integer
    data cDBMS as character  

    public method new() as object
    public method getData() as object
    public method getSchema() as object
 
endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Adilson Roberto
@since 16/05/2023
*/
//-------------------------------------------------------------------   
method new() class SFTSmartViewBusinessObject
Local oFisTools := fisSmartviewTools():new()
_Super:new()
    //Define Area
self:appendArea(STR0001)//"Livros Fiscais"
    //Define o nome do objeto de Negócio
self:setDisplayName(STR0002)//"Objeto de Negocio SFT"
    //Define a descrição do objeto de Negócio
self:setDescription(STR0003)//"Objeto SFTObject - Objeto de negócio da tabela SFT para conferência de tributos (SFT/CDA/SA1/SA2/SF6/SF2)"

self:nPageSize := oFisTools:paginacao(3000)
oFisTools:validPerg(self:setPergunte("FISTRSFT01")) //Indica o pergunte que será utilizado no relatório

self:cDBMS := UPPER(TcGetDb()) //verifica qual banco é utilizado

freeobj(oFisTools)


return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Adilson Roberto
@since 16/05/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class SFTSmartViewBusinessObject
local cQuery        as character
local cAlias        as character
Local oFisTools     := fisSmartviewTools():new()
Local oFiltQry      := oFilter:getParameters() as json
Local dStartDate    := FwDateTimeToLocal(oFiltQry['MV_PAR01',1])[1]
Local dEndDate      := FwDateTimeToLocal(oFiltQry['MV_PAR02',1])[1]
Local cFilDe        := oFiltQry['MV_PAR03',1]  as character
Local cFilAte       := oFiltQry['MV_PAR04',1] as character
Local cEst          := SuperGetMv('MV_ESTADO') as character
Local aFiliais      := {} as array
Local cFils         := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character
local aBind         := {}  as array
local nlA           := 0   as numeric
Local nFilial                                                       as numeric
Local oPrepare      := nil as object
Local lOk           := .T. as logical
Local cTpNota       := cValtoChar(oFiltQry['MV_PAR05',1]) //Tipo Nota? 1-Entrada, 2-Saida, 3-Todos
Local cMovimento    := cValtoChar(oFiltQry['MV_PAR06',1]) //Movimento? 1-Interestadual, 2-Local, 3-Todos
Local cTPEnt        := 'E' as character
Local cTPSai        := 'S' as character
Local cIniWith      := 'WITH QueryWithGuia AS ( ' as character
Local cEndWith      := " ) SELECT * FROM QueryWithGuia" as character
local cFilter       := '' as character
local cOrder        as character

if cFils == 'Erro'
    oFisTools:validFil(.f.)
    lOk := .f.
endif

If lOk  
        
    self:setPageSize(self:nPageSize)
        
    cQuery += " SELECT CASE "
    cQuery += " WHEN SFT.FT_TIPOMOV = 'E' THEN 'Entrada' "
    cQuery += " ELSE 'Saida' "
    cQuery += " END TIPOMOV, "
    cQuery += " CASE "
    cQuery += " WHEN SFT.FT_TIPO = 'D' THEN 'Devolucao' "
    cQuery += " WHEN SFT.FT_TIPO = 'S' THEN 'Servico' "
    cQuery += " WHEN SFT.FT_TIPO = 'L' THEN 'Em Lote' "
    cQuery += " WHEN SFT.FT_TIPO = 'C' THEN 'Complemento' "
    cQuery += " WHEN SFT.FT_TIPO = '' OR SFT.FT_TIPO = 'N' THEN 'Normal' " 
    cQuery += " ELSE SFT.FT_TIPO END TIPO, "
    cQuery += " (SFT.FT_ITEM || '  ' || SFT.FT_PRODUTO) ITEMPROD,"
    cQuery += " SFT.FT_FILIAL,SFT.FT_ENTRADA,SFT.FT_NFISCAL,SFT.FT_SERIE,SFT.FT_CLIEFOR,SFT.FT_LOJA,SFT.FT_ESTADO,"
    cQuery += " SFT.FT_CFOP,SFT.FT_ALIQICM,SFT.FT_VALCONT,SFT.FT_BASEICM,SFT.FT_VALICM,SFT.FT_ISENICM,SFT.FT_OUTRICM,SFT.FT_BASEIPI,"
    cQuery += " SFT.FT_VALIPI,SFT.FT_ISENIPI,SFT.FT_OUTRIPI,SFT.FT_BASERET,SFT.FT_ICMSRET,SFT.FT_ICMSCOM,SFT.FT_ICMSDIF,SFT.FT_DIFAL,"
    cQuery += " SFT.FT_TRFICM,SFT.FT_CRDEST,SFT.FT_IPIOBS,SFT.FT_OBSICM,SFT.FT_OBSSOL,SFT.FT_TIPO,SFT.FT_SOLTRIB,SFT.FT_ESPECIE,"
    cQuery += " SFT.FT_PRODUTO,SFT.FT_ITEM,SFT.FT_ALIQIPI,SFT.FT_CLASFIS,SFT.FT_CTIPI,SFT.FT_QUANT,SFT.FT_VFECPST,SFT.FT_OBSERV,SFT.FT_BSFCPST,"
    cQuery += " SFT.FT_ALFCPST,SFT.FT_BSFCCMP,SFT.FT_ALFCCMP,SFT.FT_BFCPANT,SFT.FT_VFCPANT,SFT.FT_AFCPANT,SFT.FT_VFCPDIF,"
    cQuery += " SFT.FT_ALQFECP,SFT.FT_VALFECP,SFT.FT_BASFECP,SFT.FT_DESPESA, CDA.CDA_CODLAN,CDA.CDA_BASE, CDA.CDA_ALIQ, "
    cQuery += " CDA.CDA_VALOR, CDA.CDA_VLOUTR, SUBSTRING(SF2.F2_GNRDIF, 4, 9) AS F2_NUMTIT,"
    cQuery += " COALESCE(SA1.A1_CGC, SA2.A2_CGC) CNPJ,COALESCE(SA1.A1_NOME, SA2.A2_NOME) NOMPAR,COALESCE(SA1.A1_INSCR, SA2.A2_INSCR) INCESC, "
    cQuery += " COALESCE(SF2.F2_GNRDIF, SF6.F6_NUMERO) GUIA,"
    cQuery += " CASE WHEN  (SFT.FT_VFCPDIF > 0 AND SF2.F2_GNRFECP <> ' ') THEN SF2.F2_GNRFECP "   
    cQuery += " WHEN  (SFT.FT_VFCPDIF > 0 AND SF2.F2_GNRFECP = ' ') THEN 
    cQuery += " (SELECT SF6A.F6_NUMERO FROM " +RetSqlName("SF6")+" SF6A WHERE SF6A.F6_FECP = '1' AND SF6A.F6_TIPOIMP = 'B' "
    cQuery += " AND SF6A.F6_FILIAL = ? " // 1
    cQuery += " AND SFT.FT_NFISCAL = SF6A.F6_DOC AND SFT.FT_SERIE = SF6A.F6_SERIE AND "
    cQuery += " SFT.FT_CLIEFOR = SF6A.F6_CLIFOR AND SFT.FT_LOJA = SF6A.F6_LOJA AND SF6A.D_E_L_E_T_ = ? )" // 2
    cQuery += " ELSE ' ' END GUIAFCP "  
    cQuery += " FROM " +RetSqlName("SFT")+" SFT "
    //CDA
    cQuery += " LEFT JOIN " +RetSqlName("CDA")+" CDA ON "
    cQuery += " CDA.CDA_FILIAL = ? "  // 3
    cQuery += " AND CDA.CDA_TPMOVI = SFT.FT_TIPOMOV AND"
    cQuery += " CDA.CDA_NUMERO = SFT.FT_NFISCAL AND CDA.CDA_SERIE = SFT.FT_SERIE AND CDA.CDA_CLIFOR = SFT.FT_CLIEFOR AND CDA.CDA_LOJA = SFT.FT_LOJA AND"
    cQuery += " CDA.CDA_NUMITE = SFT.FT_ITEM AND CDA.D_E_L_E_T_ = ? " // 4
    //SA2 Fornecedores
    cQuery += "  LEFT JOIN " +RetSqlName("SA2")+" SA2 ON "
    cQuery += " ((SFT.FT_TIPOMOV = 'E' AND SFT.FT_TIPO <> 'D') OR (SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO IN ('D','B'))) AND " 
    cQuery += " SA2.A2_FILIAL = ? "  // 5
    cQuery += " AND SA2.A2_COD = SFT.FT_CLIEFOR AND SA2.A2_LOJA = SFT.FT_LOJA AND SA2.D_E_L_E_T_ = ? " // 6
    //SA1 Clientes
    cQuery += "  LEFT JOIN " +RetSqlName("SA1")+" SA1 ON "
    cQuery += " ((SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO NOT IN ('D','B')) OR (SFT.FT_TIPOMOV = 'E' AND SFT.FT_TIPO = 'D')) AND " 
    cQuery += " SA1.A1_FILIAL = ? "  // 7
    cQuery += " AND SA1.A1_COD = SFT.FT_CLIEFOR AND SA1.A1_LOJA = SFT.FT_LOJA AND SA1.D_E_L_E_T_ = ? " // 8
    //SF6 Guia de Recolhimento
    cQuery += " LEFT JOIN " +RetSqlName("SF6")+" SF6 ON "
    cQuery += " SFT.FT_DIFAL > 0 AND (SFT.FT_TIPOMOV = 'E' AND SF6.F6_OPERNF = '1') AND " 
    cQuery += " SF6.F6_FILIAL = ? "  // 9
    cQuery += " AND SF6.F6_DOC = SFT.FT_NFISCAL AND SF6.F6_SERIE = SFT.FT_SERIE AND SF6.F6_TIPOIMP = 'B' "
    cQuery += " AND SF6.F6_CLIFOR = SFT.FT_CLIEFOR AND SF6.F6_LOJA = SFT.FT_LOJA AND SF6.D_E_L_E_T_ = ? " // 10
    //SF2 Cabeçalho das NF de Saída     
    cQuery += " LEFT JOIN " +RetSqlName("SF2")+" SF2 ON "
    cQuery += " SFT.FT_DIFAL > 0 AND SFT.FT_TIPOMOV = 'S' AND " 
    cQuery += " SF2.F2_FILIAL = ? "  // 11
    cQuery += " AND SF2.F2_DOC = SFT.FT_NFISCAL AND SF2.F2_SERIE = SFT.FT_SERIE AND SF2.F2_CLIENTE = SFT.FT_CLIEFOR AND " 
    cQuery += " SF2.F2_LOJA = SFT.FT_LOJA AND SF2.F2_EMISSAO = SFT.FT_ENTRADA AND SF2.D_E_L_E_T_ = ? " // 12
    // where
    cQuery += " WHERE SFT.FT_FILIAL = ? " // 13
    //Entradas, Saídas, todos MV_PAR05
    If cTpNota == "1"
        cQuery += " AND SFT.FT_TIPOMOV = ?" //AND SFT.FT_TIPOMOV = 'E'" 14
    ElseIf cTpNota == "2"
        cQuery += " AND SFT.FT_TIPOMOV = ?" //AND SFT.FT_TIPOMOV = 'S'" 14 
    Endif    
    cQuery += " AND SFT.FT_ENTRADA BETWEEN ? AND ? " // 15 E 16
    cQuery += " AND SFT.D_E_L_E_T_	= ?  " // 17
    
    // Somente interestadual, somente local, todas MV_PAR06
    If cMovimento == "1"
        cQuery += " AND SFT.FT_ESTADO <> ? " //AND SFT.FT_ESTADO <> '" + cEst + "'"iu 18

    Elseif cMovimento == "2"
        cQuery += " AND SFT.FT_ESTADO = ? " //AND SFT.FT_ESTADO = '" + cEst + "'" 18
    Endif    

    //incluído para considerar os filtros de tela 
    if oFilter:hasFilter()
        cFilter :=   " WHERE " + oFilter:getSQLExpression()
    endif

    cOrder := " ORDER BY FT_FILIAL, TIPOMOV, FT_SERIE, FT_NFISCAL, FT_CLIEFOR, FT_LOJA, FT_ITEM "

    // incluído WITH para considerar os filtros de tela que podem ser feitos utilizando qualquer campo do select, inclusive o "GUIA <> ' ' " que vem fixado    
    cQuery := cIniWith + ChangeQuery(cQuery) + cEndWith + cFilter + cOrder
    
    oPrepare := FwExecStatement():New( cQuery ) 

    //Percorre as filiais
    For nFilial := 1 to Len(aFiliais)
        
        Aadd( aBind, xFilial( 'SF6', aFiliais[nFilial] ) )  // 1
        aadd( aBind , ' ' )                                 // 2 
        Aadd( aBind, xFilial( 'CDA', aFiliais[nFilial] ) )  // 3
        aadd( aBind , ' ' )                                 // 4 
        Aadd( aBind, xFilial( 'SA2', aFiliais[nFilial] ) )  // 5
        aadd( aBind , ' ' )                                 // 6
        Aadd( aBind, xFilial( 'SA1', aFiliais[nFilial] ) )  // 7
        aadd( aBind , ' ' )                                 // 8
        Aadd( aBind, xFilial( 'SF6', aFiliais[nFilial] ) )  // 9
        aadd( aBind , ' ' )                                 // 10
        Aadd( aBind, xFilial( 'SF2', aFiliais[nFilial] ) )  // 11
        aadd( aBind , ' ' )                                 // 12
        Aadd( aBind, xFilial( 'SFT', aFiliais[nFilial] ) )  // 13

        If cTpNota == "1"
            aadd( aBind , cTPEnt ) // 14
        ElseIf cTpNota == "2"
            aadd( aBind , cTPSai ) // 14
        Endif    

        aadd( aBind , dStartDate ) // 15
        aadd( aBind , dEndDate )   // 16
        aadd( aBind , ' ' )        // 17
    
        // Somente interestadual, somente local, todas MV_PAR06
        If cMovimento == "1"
            aadd( aBind , cEst ) // 18
        Elseif cMovimento == "2"
            aadd( aBind , cEst ) // 18
        Endif 
        
        for nlA := 1 to len(aBind)            
            if Valtype(aBind[nlA]) == 'D'
                oPrepare:setDate( nlA, aBind[nlA] )
            else
                oPrepare:setString( nlA, aBind[nlA] )
            endif
        next nlA
        
        cQuery := oPrepare:getFixQuery()
        cAlias:= MPSysOpenQuery(cQuery) //executequery			

        while !(cAlias)->(Eof())
            self:oData:appendData({ "FT_FILIAL"    : (cAlias)->FT_FILIAL,;
                                    "FT_ENTRADA"   : totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->FT_ENTRADA)),; 
                                    "FT_NFISCAL"   : Alltrim((cAlias)->FT_NFISCAL),;
                                    "FT_SERIE"     : (cAlias)->FT_SERIE,; 
                                    "FT_CLIEFOR"   : (cAlias)->FT_CLIEFOR,;
                                    "FT_LOJA"      : ((cAlias)->FT_LOJA),;                                
                                    "NOMPAR"       : ((cAlias)->NOMPAR),;
                                    "CNPJ"         : ((cAlias)->CNPJ),;
                                    "INCESC"       : ((cAlias)->INCESC),;
                                    "FT_ESTADO"    : (cAlias)->FT_ESTADO,; 
                                    "FT_CFOP"      : (cAlias)->FT_CFOP,; 
                                    "FT_ALIQICM"   : (cAlias)->FT_ALIQICM,; 
                                    "FT_VALCONT"   : (cAlias)->FT_VALCONT,; 
                                    "FT_DESPESA"   : (cAlias)->FT_DESPESA,; 
                                    "FT_BASEICM"   : Iif((cAlias)->FT_TIPO<>"D",(cAlias)->FT_BASEICM,0),;
                                    "FT_BSICMDV"   : Iif((cAlias)->FT_TIPO<>"D",0,(cAlias)->FT_BASEICM),; 
                                    "FT_VALICM"    : Iif((cAlias)->FT_TIPO<>"D",(cAlias)->FT_VALICM,0),;
                                    "FT_VLICMDV"   : Iif((cAlias)->FT_TIPO<>"D",0,(cAlias)->FT_VALICM),;   
                                    "FT_ISENICM"   : (cAlias)->FT_ISENICM,; 
                                    "FT_OUTRICM"   : (cAlias)->FT_OUTRICM,; 
                                    "FT_VALFECP"   : (cAlias)->FT_VALFECP,; 
                                    "FT_VFECPST"   : (cAlias)->FT_VFECPST,; 
                                    "FT_BASEIPI"   : (cAlias)->FT_BASEIPI,; 
                                    "FT_VALIPI"    : (cAlias)->FT_VALIPI,; 
                                    "FT_ISENIPI"   : (cAlias)->FT_ISENIPI,; 
                                    "FT_OUTRIPI"   : (cAlias)->FT_OUTRIPI,; 
                                    "FT_BASERET"   : Iif((cAlias)->FT_TIPO<>"D",(cAlias)->FT_BASERET,0),;
                                    "FT_BSRETDV"   : Iif((cAlias)->FT_TIPO<>"D",0,(cAlias)->FT_BASERET),;  
                                    "FT_ICMSRET"   : Iif((cAlias)->FT_TIPO<>"D",(cAlias)->FT_ICMSRET,0),;
                                    "FT_ICMRETDV"  : Iif((cAlias)->FT_TIPO<>"D",0,(cAlias)->FT_ICMSRET),; 
                                    "FT_ICMSCOM"   : (cAlias)->FT_ICMSCOM,; 
                                    "FT_ICMSDIF"   : (cAlias)->FT_ICMSDIF,; 
                                    "FT_DIFAL"     : (cAlias)->FT_DIFAL,; 
                                    "GUIAFCP"      : (cAlias)->GUIAFCP,; 
                                    "GUIA"         : (cAlias)->GUIA,; 
                                    "F2_NUMTIT"    : (cAlias)->F2_NUMTIT,;
                                    "FT_TRFICM"    : (cAlias)->FT_TRFICM,; 
                                    "FT_CRDEST"    : (cAlias)->FT_CRDEST,; 
                                    "FT_IPIOBS"    : (cAlias)->FT_IPIOBS,; 
                                    "FT_OBSICM"    : (cAlias)->FT_OBSICM,; 
                                    "FT_OBSSOL"    : (cAlias)->FT_OBSSOL,; 
                                    "FT_SOLTRIB"   : (cAlias)->FT_SOLTRIB,; 
                                    "FT_ESPECIE"   : (cAlias)->FT_ESPECIE,; 
                                    "FT_PRODUTO"   : (cAlias)->FT_PRODUTO,; 
                                    "FT_ITEM"      : (cAlias)->FT_ITEM,; 
                                    "ITEMPROD"     : (cAlias)->ITEMPROD,;
                                    "FT_ALIQIPI"   : (cAlias)->FT_ALIQIPI,; 
                                    "FT_CLASFIS"   : (cAlias)->FT_CLASFIS,; 
                                    "FT_CTIPI"     : (cAlias)->FT_CTIPI,; 
                                    "FT_QUANT"     : (cAlias)->FT_QUANT,;                                                                 
                                    "TIPOMOV"      : (cAlias)->TIPOMOV,; 
                                    "TIPO"         : (cAlias)->TIPO,;  
                                    "FT_BSFCCMP"   : (cAlias)->FT_BSFCCMP,; 
                                    "FT_ALFCCMP"   : (cAlias)->FT_ALFCCMP,; 
                                    "FT_VFCPDIF"   : (cAlias)->FT_VFCPDIF,;
                                    "FT_BFCPANT"   : (cAlias)->FT_BFCPANT,; 
                                    "FT_VFCPANT"   : (cAlias)->FT_VFCPANT,; 
                                    "FT_AFCPANT"   : (cAlias)->FT_AFCPANT,; 
                                    "FT_BASFECP"   : (cAlias)->FT_BASFECP,;  
                                    "FT_ALQFECP"   : (cAlias)->FT_ALQFECP,; 
                                    "FT_VALFECP"   : (cAlias)->FT_VALFECP,; 
                                    "FT_BSFCPST"   : (cAlias)->FT_BSFCPST,; 
                                    "FT_ALFCPST"   : (cAlias)->FT_ALFCPST,;
                                    "FT_VFECPST"   : (cAlias)->FT_VFECPST,;
                                    "FT_OBSERV"    : (cAlias)->FT_OBSERV,;
                                    "CDA_CODLAN"   : (cAlias)->CDA_CODLAN,;
                                    "CDA_BASE"     : (cAlias)->CDA_BASE,;
                                    "CDA_ALIQ"     : (cAlias)->CDA_ALIQ,;
                                    "CDA_VLOUTR"   : (cAlias)->CDA_VLOUTR,;
                                    "CDA_VALOR"    : (cAlias)->CDA_VALOR} )

            (cAlias)->( dbSkip() )
        enddo    

        (cAlias)->( DBCloseArea() )
        aSize(aBind,0)
    Next nFilial    
EndIf

    freeobj(oFisTools)
    aSize(aBind,0)
    aBind := Nil
    freeObj(oPrepare)
    freeobj(oFiltQry)
    aSize(aFiliais,0)
    aFiliais := Nil

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Adilson Roberto
@since 16/05/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class SFTSmartViewBusinessObject

    //              ( Nome   ,  Descricao                   ,  Tipo   , Display                     , cRealName
    self:addProperty("FT_FILIAL" , "Filial"                 , "string", "Filial"                    , "FT_FILIAL"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_ENTRADA", "Data Entrada"           , "date"  , "Data Entrada"              , "FT_ENTRADA"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_ESPECIE", "Especie"                , "string", "Especie"                   , "FT_ESPECIE"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_NFISCAL", "Nota Fiscal"            , "string", "Nota Fiscal"               , "FT_NFISCAL"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_SERIE"  , "Serie"                  , "string", "Serie"                     , "FT_SERIE"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_CLIEFOR", "Cli/For"                , "string", "Cli/For"                   , "FT_CLIEFOR"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_LOJA"   , "Loja"                   , "string", "Loja"                      , "FT_LOJA"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("NOMPAR"    , "Nom. Participante"      , "string", "Nom. Participante"         , "NOMPAR"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CNPJ"      , "Cnpj/CPF"               , "string", "Cnpj/CPF"                  , "CNPJ"        , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("INCESC"    , "Insc. Est."             , "string", "Insc. Est."                , "INCESC"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_ESTADO" , "UF"                     , "string", "UF"                        , "FT_ESTADO"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_CFOP"   , "CFOP"                   , "string", "CFOP"                      , "FT_CFOP"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_VALCONT", "Vlr Contab."            , "number", "Vlr Contab."               , "FT_VALCONT"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_DESPESA", "Vlr Despesa"            , "number", "Vlr Despesa"               , "FT_DESPESA"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_ALIQICM", "Alq. ICMS"              , "number", "Alq. ICMS"                 , "FT_ALIQICM"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_BASEICM", "Base ICMS"              , "number", "Base ICMS"                 , "FT_BASEICM"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_BSICMDV", "Base ICMS Devolução"    , "number", "Base ICMS Devolução"       , "FT_BSICMDV"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )    
    self:addProperty("FT_VALICM" , "Vlr ICMS"               , "number", "Vlr ICMS"                  , "FT_VALICM"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_VLICMDV", "Vlr ICMS Devolução"     , "number", "Vlr ICMS Devolução"        , "FT_VLICMDV"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )    
    self:addProperty("FT_ISENICM", "Isen. ICMS"             , "number", "Isen. ICMS"                , "FT_ISENICM"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_OUTRICM", "Outr. ICMS"             , "number", "Outr. ICMS"                , "FT_OUTRICM"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_BASEIPI", "Base IPI"               , "number", "Base IPI"                  , "FT_BASEIPI"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_ALIQIPI", "Alq. IPI"               , "number", "Alq. IPI"                  , "FT_ALIQIPI"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_VALIPI" , "Vlr. IPI"               , "number", "Vlr. IPI"                  , "FT_VALIPI"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_ISENIPI", "Isen. IPI"              , "number", "Isen. IPI"                 , "FT_ISENIPI"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_OUTRIPI", "Outr. IPI"              , "number", "Outr. IPI"                 , "FT_OUTRIPI"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_BASERET", "Bas.Ret.ICM"            , "number", "Bas.Ret.ICM"               , "FT_BASERET"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_BSRETDV", "Bas.Ret.ICM Devolução"  , "number", "Bas.Ret.ICM Devolução"     , "FT_BSRETDV"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_ICMSRET", "Vlr ICMS Ret"           , "number", "Vlr ICMS Ret"              , "FT_ICMSRET"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_ICMRETDV","Vlr ICMS Ret Devolução" , "number", "Vlr ICMS Ret Devolução"    , "FT_ICMRETDV" , /*cCombOValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_ICMSCOM", "Vlr ICMS Compl"         , "number", "Vlr ICMS Compl"            , "FT_ICMSCOM"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_ICMSDIF", "Vlr ICMS Dif"           , "number", "Vlr ICMS Dif"              , "FT_ICMSDIF"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_DIFAL"  , "Vlr Difal"              , "number", "Vlr Difal"                 , "FT_DIFAL"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("GUIA"      , "Guia"                   , "string", "Guia"                      , "GUIA"        , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("GUIAFCP"   , "Guia"                   , "string", "Guia Fecp"                 , "GUIAFCP"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("F2_NUMTIT" , "Titulo"                 , "string", "Titulo"                    , "F2_NUMTIT"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_TRFICM" , "Vlr ICMS Trf"           , "number", "Vlr ICMS Trf"              , "FT_TRFICM"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_CRDEST" , "Vlr Créd Est"           , "number", "Vlr Créd Est"              , "FT_CRDEST"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_IPIOBS" , "Vlr IPI Obs"            , "number", "Vlr IPI Obs"               , "FT_IPIOBS"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_OBSICM" , "Vlr ICM Obs"            , "number", "Vlr ICM Obs"               , "FT_OBSICM"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_OBSSOL" , "Vlr Sol Obs"            , "number", "Vlr Sol Obs"               , "FT_OBSSOL"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_SOLTRIB", "ICMS Sol Tri"           , "number", "ICMS Sol Tri"              , "FT_SOLTRIB"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_PRODUTO", "Cod. Prod."             , "string", "Cod. Prod."                , "FT_PRODUTO"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_ITEM"   , "Item"                   , "string", "Item"                      , "FT_ITEM"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ITEMPROD"  , "Item + Produto"         , "string", "Item + Produto"            , "ITEMPROD"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_CLASFIS", "Sit.Tribut."            , "string", "Sit.Tribut."               , "FT_CLASFIS"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_CTIPI"  , "Trib. IPI"              , "string", "Trib. IPI"                 , "FT_CTIPI"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_QUANT"  , "Quant."                 , "number", "Quant."                    , "FT_QUANT"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("TIPOMOV"   , "Movimento"              , "string", "Movimento"                 , "TIPOMOV"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("TIPO"      , "tp. Nota"               , "string", "tp. Nota"                  , "TIPO"        , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_BSFCCMP", "B.FECP Compl"           , "number", "B.FECP Compl"              , "FT_BSFCCMP"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_ALFCCMP", "Al. FECP Cpl"           , "number", "Al. FECP Cpl"              , "FT_ALFCCMP"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_VFCPDIF", "Val FECP Dif"           , "number", "Val FECP Dif"              , "FT_VFCPDIF"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_BFCPANT", "Base FCP Ant"           , "number", "Base FCP Ant"              , "FT_BFCPANT"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_AFCPANT", "Alq. FCP Ant"           , "number", "Alq. FCP Ant"              , "FT_AFCPANT"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_VFCPANT", "Val. FCP Ant"           , "number", "Val. FCP Ant"              , "FT_VFCPANT"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_BASFECP", "Base FCP Pr."           , "number", "Base FCP Pr."              , "FT_BASFECP"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_ALQFECP", "Aliq FCP Pr."           , "number", "Aliq FCP Pr."              , "FT_ALQFECP"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_VALFECP", "Val FCP Pr."            , "number", "Val FCP Pr."               , "FT_VALFECP"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_BSFCPST", "Base FCP ST"            , "number", "Base FCP ST"               , "FT_BSFCPST"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_ALFCPST", "Aliq FCP ST"            , "number", "Aliq FCP ST"               , "FT_ALFCPST"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_VFECPST", "Val FCP ST"             , "number", "Val FCP ST"                , "FT_VFECPST"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("FT_OBSERV" , "Observacao"             , "string", "Observacao"                , "FT_OBSERV"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CDA_CODLAN", "Cod. Lanc."             , "string", "Cod. Lanc."                , "CDA_CODLAN"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CDA_BASE"  , "Base Cod. Lanc."        , "number", "Base Cod. Lanc."           , "CDA_BASE"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CDA_ALIQ"  , "Al. Cod. Lanc."         , "number", "Al. Cod. Lanc."            , "CDA_ALIQ"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CDA_VALOR" , "Vl. Cod. Lanc."         , "number", "Vl. Cod. Lanc."            , "CDA_VALOR"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CDA_VLOUTR", "Vl.Out.Cod.Lanc."       , "number", "Vl.Out.Cod.Lanc."          , "CDA_VLOUTR"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )


return self:oSchema
