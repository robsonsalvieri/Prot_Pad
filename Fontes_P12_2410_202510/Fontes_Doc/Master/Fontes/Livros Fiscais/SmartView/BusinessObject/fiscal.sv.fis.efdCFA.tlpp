#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.efdcfa.ch"

namespace totvs.protheus.fiscal.fis.treportsintegratedprovider

//-------------------------------------------------------------------
/*{Protheus.doc}  efdCFASmartviewBusinessObject
Classe para criação do objeto de Crédito Diferidos no Período  de processamento da Apuração de PIS e COFINS
@author Julia Mota
@since 14/11/2023
*/
//-------------------------------------------------------------------  
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAFIS", tables="CFA",;
	name="Objeto de Negócio EFD - CFA", country="BRA", initialRelease="12.1.2310")
class efdCFASmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	private data oPrepare as object 

	public method new() as object
	public method getData() as object
	public method getSchema() as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author Julia Mota
@since 14/11/2023
*/
//-------------------------------------------------------------------   
method new() class efdCFASmartviewBusinessObject

	Local oFisTools := fisSmartviewTools():new() as object

	_Super:new()
	::oPrepare := nil
	//Define Area
	self:appendArea(STR0001) //"Livros Fiscais"
	//Define o nome do objeto de Negócio
	self:setDisplayName(STR0002) //"Objeto de Negócio EFD Contribuiçoes - CFA"
	//Define a descrição do objeto de Negócio
	self:setDescription(STR0003) //"Objeto de negócio que fornece o detalhamento do EFD Contribuições (CFA)"
	self:setIsCBoxLookup(.T., .F.)

	oFisTools:validPerg(self:setPergunte("FISTRR1014"))
	freeobj(oFisTools)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: ::oData 
@author allef.souza
@since 31/07/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class efdCFASmartviewBusinessObject

	Local cQuery     as Character
	Local cAlias     as Character
	Local oFisTools  := fisSmartviewTools():new() as object
	Local oFiltQry   := oFilter:getParameters() as Json
	Local cFilDe     := oFiltQry['MV_PAR03',1] as Character
	Local cFilAte    := oFiltQry['MV_PAR04',1] as Character
	Local lOk		 := .T. as Logical
	Local aFiliais   := {} as array
	Local cFils      := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character


	if cFils == 'Erro'
		oFisTools:validFil(.f.) 
		lOk := .f.
	endif

	if lOk	

		cQuery += "SELECT "
		cQuery += " CFA.CFA_CNPJ, "
		cQuery += " CFA.CFA_CODCRE, "
		cQuery += " CFA.CFA_CREDIF, "
		cQuery += " CFA.CFA_TPCON, "
		cQuery += " CFA.CFA_CODCON, "
		cQuery += " CFA.CFA_TOTVEN, "
		cQuery += " CFA.CFA_TOTDIF, "
		cQuery += " CFA.CFA_CONDIF, "
		cQuery += " CFA.CFA_ALIQ, "
		cQuery += " CFA.CFA_CONREC, "
		cQuery += " CFA.CFA_VLRREC, "
		cQuery += " CFA.CFA_VLNREC, "
		cQuery += " CFA.CFA_PERAPU, "
		cQuery += " CFA.CFA_CONSOL, "
		cQuery += " CFA.CFA_FILIAL "
		cQuery +=  "FROM "
		cQuery += RetSQLName("CFA") + " CFA "
		cQuery += " WHERE "
		cQuery += " CFA.CFA_FILIAL IN ( ? ) "
		cQuery += " AND CFA.D_E_L_E_T_ = ? "

		::oPrepare := FWExecStatement():New(ChangeQuery(cQuery))
		::oPrepare:setIn(1,aFiliais)
		::oPrepare:setString(2,' ')

		cAlias:= ::oPrepare:OpenAlias()

		while !(cAlias)->(Eof())

			self:oData:appendData({;
				"CFA_CNPJ"    : (cAlias)->CFA_CNPJ,;
				"CFA_CODCRE"  : (cAlias)->CFA_CODCRE,;
				"CFA_CREDIF"  : (cAlias)->CFA_CREDIF,;
				"CFA_TPCON"   : (cAlias)->CFA_TPCON ,;
				"CFA_CODCON"  : (cAlias)->CFA_CODCON,;
				"CFA_TOTVEN"  : (cAlias)->CFA_TOTVEN,;
				"CFA_TOTDIF"  : (cAlias)->CFA_TOTDIF,;
				"CFA_CONDIF"  : (cAlias)->CFA_CONDIF,;
				"CFA_ALIQ"    : (cAlias)->CFA_ALIQ  ,;
				"CFA_CONREC"  : (cAlias)->CFA_CONREC,;
				"CFA_VLRREC"  : (cAlias)->CFA_VLRREC,;
				"CFA_VLNREC"  : (cAlias)->CFA_VLNREC,;
				"CFA_PERAPU"  : (cAlias)->CFA_PERAPU,;
				"CFA_CONSOL"  : (cAlias)->CFA_CONSOL,;
				"CFA_FILIAL"  : (cAlias)->CFA_FILIAL;
				})

			(cAlias)->(dbSkip())
		enddo

		(cAlias)->(DBCloseArea())

	EndIf
	freeobj(oFisTools)
	freeObj(::oPrepare)
	freeobj(oFiltQry)
	aSize(aFiliais,0)
	aFiliais := Nil

return self:oData
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: ::oSchema 
@author Julia Mota
@since 16/11/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class efdCFASmartviewBusinessObject

	self:addProperty("CFA_CNPJ",   "CNPJ do Cliente"           , "string", "CNPJ Cliente"           , "CFA_CNPJ"  )
	self:addProperty("CFA_CODCRE", "Codigo do Tipo de Credito" , "string", "Cod Tipo Crédito"       , "CFA_CODCRE")
	self:addProperty("CFA_CREDIF", "Credito Diferido"          , "number", "Crédito Diferido"       , "CFA_CREDIF")
	self:addProperty("CFA_TPCON",  "Tipo da Contribuicao"      , "string", "Tipo Contribuição"      , "CFA_TPCON" )
	self:addProperty("CFA_CODCON", "Codigo da Contribuicao"    , "string", "Cod. Contribuição"      , "CFA_CODCON")
	self:addProperty("CFA_TOTVEN", "Total de Vendas"           , "number", "Total Operações"        , "CFA_TOTVEN")
	self:addProperty("CFA_TOTDIF", "Total Diferido"            , "number", "Total Diferido"         , "CFA_TOTDIF")
	self:addProperty("CFA_CONDIF", "Contribuicao Diferida"     , "number", "Contribuição Diferida"  , "CFA_CONDIF")
	self:addProperty("CFA_ALIQ",   "Aliquota"                  , "number", "Alíquota"               , "CFA_ALIQ"  )
	self:addProperty("CFA_VLRREC", "Total Recebido"            , "number", "Total Recebido"         , "CFA_VLRREC")
	self:addProperty("CFA_VLNREC", "Total Nao Recebido"        , "number", "Tot Não Recebido"       , "CFA_VLNREC")
	self:addProperty("CFA_PERAPU", "Periodo de Apuracao"       , "string", "Periodo Apuração"       , "CFA_PERAPU")
	self:addProperty("CFA_CONSOL", "Consolid. visão Empresa "  , "string", "Consolidado"            , "CFA_CONSOL")

return self:oSchema
