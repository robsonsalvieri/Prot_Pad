#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.conftrib.ch"

namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAFIS", tables="SFT,SA1,SA2,F2B,F2D,F2E", name="ConfTribSmartviewBusinessObject", country="BRA", initialRelease="12.1.2210")
//-------------------------------------------------------------------
/*{Protheus.doc}  ConfTribSmartviewBusinessObject
Classe para criação do Objeto ConfTribSmartviewBusinessObject - Enquadramento Legal da Operação/Prestação Geradora de Crédito Acumulado do ICMS
@author Julia Mota
@since 15/01/2024
*/
//-------------------------------------------------------------------  
class ConfTribSmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	data nPageSize as integer

	public method new() as object
	public method getData() as object
	public method getSchema() as object
endclass
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author Julia Mota
@since 15/01/2024
*/
//-------------------------------------------------------------------   
method new() class ConfTribSmartviewBusinessObject
	Local oFisTools := fisSmartviewTools():new()
	_Super:new()
	//Define Area
	self:appendArea(STR0001)//"Livros Fiscais"
	//Define o nome do objeto de Negócio
	self:setDisplayName(STR0002)//"Objeto de Negocio Calculo via Configurador de Tributos"
	//Define a descrição do objeto de Negócio
	self:setDescription(STR0003)//"Objeto ConfTribSmartviewBusinessObject - Objeto de negócio para conferencia dos calculos feitos via Configurador de Tributos (SFT,SB1,F2B,F2D,F2E)"

	self:nPageSize := oFisTools:paginacao(3000)
	oFisTools:validPerg(::setPergunte("FISTRR1025"))
	freeobj(oFisTools)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: self:oData 
@author Julia Mota
@since 15/01/2024
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class ConfTribSmartviewBusinessObject

	Local cQuery    as Character
	Local cAlias    as Character
	Local oFisTools := fisSmartviewTools():new()
	Local oFiltQry  := oFilter:getParameters() as Json
	Local dStartDate:= FwDateTimeToLocal(oFiltQry['MV_PAR01',1])[1]
	Local dEndDate  := FwDateTimeToLocal(oFiltQry['MV_PAR02',1])[1]
	Local cFilDe    := oFiltQry['MV_PAR03',1] as Character
	Local cFilAte   := oFiltQry['MV_PAR04',1] as Character
	Local cCodTriDe   := oFiltQry['MV_PAR05',1] as Character
	Local cCodTriAte   := oFiltQry['MV_PAR06',1] as Character
	Local cTpNota   := cValtoChar(oFiltQry['MV_PAR07',1]) //Tipo Nota? 1-Entrada, 2-Saida, 3-Todos
	Local lOk       := .t. as Logical
	Local aFiliais  := {} as array
	Local cFils     := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character
	Local nFilial	:= 0   as numeric
	Local oPrepare  := nil as object

	if cFils == 'Erro'
		oFisTools:validFil(.f.)
		lOk := .f.
	endif

	if lOk

		//definindo a quantidade máxima por página (default 100)
		self:setPageSize(self:nPageSize)

		cQuery := " SELECT"
		cQuery += "         F2B.F2B_FILIAL,     "
		cQuery += "         F2B.F2B_TRIB,   "
		cQuery += "         F2B.F2B_REGRA,  "
		cQuery += "         F2B.F2B_DESC,   "
		cQuery += "         F2D.F2D_BASE,   "
		cQuery += "         F2D.F2D_ALIQ,   "
		cQuery += "         F2D.F2D_VALOR,  "
		cQuery += "         F2D.F2D_BASQTD,     "
		cQuery += "         F2D.F2D_MVA,    "
		cQuery += "         F2D.F2D_MAJORA,     "
		cQuery += "         F2D.F2D_ALQMAJ,     "
		cQuery += "         F2D.F2D_VALMAJ, "
		cQuery += "         F2D.F2D_PAUTA,  "
		cQuery += "         SFT.FT_NFISCAL,     "
		cQuery += "         SFT.FT_SERIE,   "
		cQuery += "         SFT.FT_ESPECIE,     "
		cQuery += "         SFT.FT_CLIEFOR, "
		cQuery += "         COALESCE(SA1.A1_CGC, SA2.A2_CGC) CNPJ,"
		cQuery += "         COALESCE(SA1.A1_NOME, SA2.A2_NOME) NOMECF, "
		cQuery += "         SFT.FT_LOJA,    "
		cQuery += "         SFT.FT_ENTRADA,     "
		cQuery += "         SFT.FT_EMISSAO,     "
		cQuery += "         SFT.FT_DTCANC ,     "
		cQuery += "         SFT.FT_CONTA,   "
		cQuery += "         SFT.FT_DIFAL  , "
		cQuery += "         SFT.FT_ITEM,    "
		cQuery += "         CASE WHEN SFT.FT_TIPOMOV = 'E' THEN 'Entrada' ELSE 'Saida' END TIPOMOV, "
		cQuery += "         CASE WHEN SFT.FT_TIPO = 'D' THEN 'Devolucao' "
		cQuery += "              WHEN SFT.FT_TIPO = 'S' THEN 'Servico' "
		cQuery += "              WHEN SFT.FT_TIPO = 'L' THEN 'Em Lote' "
		cQuery += "              WHEN SFT.FT_TIPO = 'C' THEN 'Complemento' "
		cQuery += "              WHEN SFT.FT_TIPO IN (' ','N' )  THEN 'Normal' "
		cQuery += "              WHEN SFT.FT_TIPO = 'B' THEN 'Beneficiamento' "
		cQuery += "              ELSE SFT.FT_TIPO "
		cQuery += "         END TIPO, "
		cQuery += "         SFT.FT_TIPOMOV, "
		cQuery += "         SFT.FT_TIPO,    "
		cQuery += "         SFT.FT_CFOP,    "
		cQuery += "         SFT.FT_CSTPIS,  "
		cQuery += "         SFT.FT_CSTCOF,  "
		cQuery += "         CJ2.CJ2_INCIDE, "
		cQuery += "         CJ2.CJ2_IREDBS, "
		cQuery += "         SFT.FT_PRODUTO  "
		cQuery += "    FROM " + RetSqlName("F2D") + " F2D "

		cQuery += "   INNER JOIN " + RetSqlName("SFT") + " SFT "		// (SFT) Indice D-FT_FILIAL+FT_IDTRIB
		cQuery += "      ON SFT.FT_FILIAL = ? "							// ?-01
		cQuery += "     AND SFT.FT_IDTRIB = F2D.F2D_IDREL "
		cQuery += "     AND SFT.D_E_L_E_T_ = ? "						// ?-02
		cQuery += "     AND SFT.FT_ENTRADA BETWEEN ? AND ?"				// ?-03 - 04		

		cQuery += "   INNER JOIN " + RetSqlName("F2B") + " F2B "		// (F2B) Indice 4-F2B_ID
		cQuery += "      ON F2B.F2B_FILIAL = ? "						// ?-05
		cQuery += "     AND F2B.F2B_ID = F2D.F2D_IDCAD "
		cQuery += "     AND F2B.D_E_L_E_T_ = ? "						// ?-06

		cQuery += "    LEFT JOIN " + RetSqlName("CJ2") + " CJ2 "		// (CJ2) Indice 1-CJ2_FILIAL+CJ2_CODIGO+CJ2_ALTERA
		cQuery += "      ON CJ2.CJ2_FILIAL = ? "						// ?-07
		cQuery += "     AND CJ2.CJ2_CODIGO = F2B.F2B_CODESC "
		cQuery += "     AND CJ2.D_E_L_E_T_ = ? "						// ?-08

		cQuery += "    LEFT JOIN " + RetSqlName("SA2") + " SA2 "		// (SA2) Indice 1-A2_FILIAL+A2_COD+A2_LOJA
		cQuery += "      ON SA2.A2_FILIAL = ? "							// ?-09
		cQuery += "     AND SA2.A2_LOJA = SFT.FT_LOJA "
		cQuery += "     AND SA2.A2_COD = SFT.FT_CLIEFOR "
		cQuery += "     AND SA2.D_E_L_E_T_ = ? "						// ?-10
		cQuery += "     AND ((SFT.FT_TIPOMOV = ? AND SFT.FT_TIPO NOT IN( ? ) )  OR  (SFT.FT_TIPOMOV = ? AND SFT.FT_TIPO IN( ? ) ) ) "	// ?-11 -12 -13 -14

		cQuery += "    LEFT JOIN " + RetSqlName("SA1") + " SA1 "		// (SA1) Indice 1-A1_FILIAL+A1_COD+A1_LOJA
		cQuery += "      ON SA1.A1_FILIAL = ? "							// ?-15
		cQuery += "     AND SA1.A1_LOJA = SFT.FT_LOJA "
		cQuery += "     AND SA1.A1_COD = SFT.FT_CLIEFOR "
		cQuery += "     AND SA1.D_E_L_E_T_ = ? "						// ?-16
		cQuery += "     AND ((SFT.FT_TIPOMOV = ? AND SFT.FT_TIPO IN( ? ) )  OR  (SFT.FT_TIPOMOV = ? AND SFT.FT_TIPO NOT IN( ? ) ) ) "	// ? 17 -18 -19 -20
		cQuery+= " WHERE "
		cQuery += "     F2D.F2D_FILIAL =  ?  "							// ?-21
		cQuery += " AND F2D.D_E_L_E_T_ = ? "							// ?-22
		cQuery += " AND F2B.F2B_TRIB BETWEEN ? AND ? "					// ?-23 -24
		cQuery += " AND SFT.FT_TIPOMOV IN ( ? ) "						// ?-25


		 oPrepare := FwExecStatement():New( ChangeQuery(cQuery) )        
		 
        //Percorre as filiais
        For nFilial := 1 to Len(aFiliais)

			oPrepare:SetString(01, xFilial("SFT",aFiliais[nFilial]) )
			oPrepare:SetString(02, ' '	)
			oPrepare:SetDate(03, dStartDate	)
			oPrepare:SetDate(04, dEndDate	)
			oPrepare:SetString(05, xFilial("F2B",aFiliais[nFilial]) )
			oPrepare:SetString(06, ' '	)
			oPrepare:SetString(07, xFilial("CJ2",aFiliais[nFilial]) )
			oPrepare:SetString(08, ' '	)
			oPrepare:SetString(09, xFilial("SA2",aFiliais[nFilial]) )
			oPrepare:SetString(10, ' '	)
			oPrepare:SetString(11, 'E'	)		// 11  	ENTRADA
			oPrepare:SetIn(12, {'D','B'})		// 12 	DEVOLUÇÃO/BENEFICIAMENTO
			oPrepare:SetString(13, 'S'	)		// 13 	SAÍDA
			oPrepare:SetIn(14, {'D','B'})		// 14 	DEVOLUÇÃO/BENEFICIAMENTO
			oPrepare:SetString(15, xFilial("SA1",aFiliais[nFilial]) )
			oPrepare:SetString(16, ' '	)
			oPrepare:SetString(17, 'E'	)		// 17 	ENTRADA
			oPrepare:SetIn(18, {'D','B'})		// 18 	DEVOLUÇÃO/BENEFICIAMENTO
			oPrepare:SetString(19, 'S'	)		// 19	SAÍDA
			oPrepare:SetIn(20, {'D','B'})		// 20 	DEVOLUÇÃO/BENEFICIAMENTO
			oPrepare:SetString(21, xFilial("F2D",aFiliais[nFilial]) )
			oPrepare:SetString(22, ' '	)
			oPrepare:SetString(23, cCodTriDe  )
			oPrepare:SetString(24, cCodTriAte )

			//Entradas, Saídas, todos MV_PAR07
			If cTpNota == "1"
				oPrepare:SetIn(25, {'E'	}	)	// 25	E-ENTRADA
			ElseIf cTpNota == "2"
				oPrepare:SetIn(25, {'S'	}	)	// 25	S-SAÍDA
			Else
				oPrepare:SetIn(25, {'E','S'})	// 25	E-ENTRADA/S-SAÍDA
			EndIf

			cQuery := oPrepare:getFixQuery()
        	cAlias:= MPSysOpenQuery(cQuery) //executequery			
			
			while !(cAlias)->(Eof())

				self:oData:appendData({ ;
					"F2B_FILIAL"    :       (cAlias)->F2B_FILIAL ,;
					"F2B_TRIB"      :      	(cAlias)->F2B_TRIB   ,;
					"F2B_REGRA"     :      	(cAlias)->F2B_REGRA  ,;
					"F2B_DESC"      :      	(cAlias)->F2B_DESC   ,;
					"F2D_BASE"      :      	(cAlias)->F2D_BASE   ,;
					"F2D_ALIQ"      :      	(cAlias)->F2D_ALIQ   ,;
					"F2D_VALOR"     :      	(cAlias)->F2D_VALOR  ,;
					"F2D_BASQTD"    :      	(cAlias)->F2D_BASQTD ,;
					"F2D_MVA"       :      	(cAlias)->F2D_MVA    ,;
					"F2D_MAJORA"    :      	(cAlias)->F2D_MAJORA ,;
					"F2D_ALQMAJ"    :      	(cAlias)->F2D_ALQMAJ ,;
					"F2D_VALMAJ"    :      	(cAlias)->F2D_VALMAJ ,;
					"F2D_PAUTA"     :      	(cAlias)->F2D_PAUTA  ,;
					"FT_NFISCAL"    :      	(cAlias)->FT_NFISCAL ,;
					"FT_SERIE"      :      	(cAlias)->FT_SERIE   ,;
					"FT_ESPECIE"    :      	(cAlias)->FT_ESPECIE ,;
					"FT_CLIEFOR"    :      	(cAlias)->FT_CLIEFOR ,;
					"CNPJ"          :      	(cAlias)->CNPJ       ,;
					"NOEMCF"        :      	(cAlias)->NOMECF     ,;
					"FT_LOJA"       :      	(cAlias)->FT_LOJA    ,;
					"FT_ENTRADA"    :      	totvs.framework.treports.date.stringToTimeStamp((cAlias)->FT_ENTRADA),; 
					"FT_EMISSAO"    :      	totvs.framework.treports.date.stringToTimeStamp((cAlias)->FT_EMISSAO),; 
					"FT_DTCANC"     :      	totvs.framework.treports.date.stringToTimeStamp((cAlias)->FT_DTCANC) ,;
					"FT_CONTA"      :      	(cAlias)->FT_CONTA   ,;
					"FT_DIFAL"      :      	(cAlias)->FT_DIFAL   ,;
					"FT_ITEM"       :      	(cAlias)->FT_ITEM    ,;
					"TIPOMOV"       :      	(cAlias)->TIPOMOV    ,;
					"TIPO"          :      	(cAlias)->TIPO       ,;
					"FT_CFOP"       :      	(cAlias)->FT_CFOP    ,;
					"FT_CSTPIS"     :      	(cAlias)->FT_CSTPIS  ,;
					"FT_CSTCOF"     :      	(cAlias)->FT_CSTCOF  ,;
					"CJ2_INCIDE"    :      	(cAlias)->CJ2_INCIDE ,;
					"CJ2_IREDBS"    :      	(cAlias)->CJ2_IREDBS ,;
					"FT_PRODUTO"    :      	(cAlias)->FT_PRODUTO ;
					})

				(cAlias)->(dbSkip())
			EndDo

			(cAlias)->(DBCloseArea())

        Next nFilial
	EndIf

	freeobj(oFisTools)
	freeObj(oPrepare)
	freeobj(oFiltQry)
	aSize(aFiliais,0)
	aFiliais := Nil

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author Julia Mota
@since 15/01/2024
*/
//-------------------------------------------------------------------   
method getSchema() as object class ConfTribSmartviewBusinessObject
//                   ( Nome       ,  Descricao                     ,  Tipo   , Display                    , cRealName
	self:addProperty("F2B_FILIAL" ,    "Filial"                    , "string", "Filial"                 , "F2B_FILIAL")
	self:addProperty("F2B_TRIB"   ,    "Cod.Tributo"               , "string", "Cod.Tributo"            , "F2B_TRIB"  )
	self:addProperty("F2B_REGRA"  ,    "Código da Regra Fiscal "   , "string", "Código Regra Fiscal"    , "F2B_REGRA" )
	self:addProperty("F2B_DESC"   ,    "Descrição da Regra"        , "string", "Descrição da Regra"     , "F2B_DESC"  )
	self:addProperty("F2D_BASE"   ,    "Base de Cálculo"           , "number", "Base de Cálculo"        , "F2D_BASE"  )
	self:addProperty("F2D_ALIQ"   ,    "Alíquota do Tributo"       , "number", "Alíquota Tributo"       , "F2D_ALIQ"  )
	self:addProperty("F2D_VALOR"  ,    "Valor do Tributo"          , "number", "Valor Tributo"          , "F2D_VALOR" )
	self:addProperty("F2D_BASQTD" ,    "Base Cálculo QTDE"         , "number", "Base Cálculo QTDE"      , "F2D_BASQTD")
	self:addProperty("F2D_MVA"    ,    "Marge Valor Agregado"      , "number", "MVA"                    , "F2D_MVA"   )
	self:addProperty("F2D_MAJORA" ,    "Majoração da alíquota"     , "number", "Majoração da alíquota"  , "F2D_MAJORA")
	self:addProperty("F2D_ALQMAJ" ,    "Alíquota Majorada"         , "number", "Alíquota Majorada"      , "F2D_ALQMAJ")
	self:addProperty("F2D_VALMAJ" ,    "Valor Majorado"            , "number", "Valor Majorado"         , "F2D_VALMAJ")
	self:addProperty("F2D_PAUTA"  ,    "Valor da Pauta"            , "number", "Valor da Pauta"         , "F2D_PAUTA" )
	self:addProperty("FT_NFISCAL" ,    "Número Documento Fiscal  " , "string", "Doc. Fiscal"            , "FT_NFISCAL")
	self:addProperty("FT_SERIE"   ,    "Série do Documento Fiscal ", "string", "Série NF"               , "FT_SERIE"  )
	self:addProperty("FT_ESPECIE" ,    "Espécie da NF"             , "string", "Espécie NF"             , "FT_ESPECIE")
	self:addProperty("FT_CLIEFOR" ,    "Cliente/Fornecedor"        , "string", "Cod.Cliente/Fornecedor" , "FT_CLIEFOR")
	self:addProperty("CNPJ"       ,"CNPJ/CPF do Cliente/Fornecedor", "string", "CNPJ/CPF do Cli/For"    , "CNPJ"    )
	self:addProperty("NOMECF"     ,    "Nome do Cliente/Fornecedor", "string", "Nome do Cli/For"        , "NOME"   )
	self:addProperty("FT_LOJA"    ,    "Codigo Loja Client/Forn"   , "string", "Cod. Loja Client/Forn"  , "FT_LOJA"   )
	self:addProperty("FT_ENTRADA" ,    "Data Entrada NF"           , "date"  , "Data Entrada"           , "FT_ENTRADA")
	self:addProperty("FT_EMISSAO" ,    "Data Emissão"              , "date"  , "Data Emissão"           , "FT_EMISSAO")
	self:addProperty("FT_DTCANC"  ,    "Data Cancelamento NF"      , "date"  , "Data Cancelamento"      , "FT_DTCANC" )
	self:addProperty("FT_CONTA"   ,    "Número da Conta Contábil"  , "string", "Conta Contábil"         , "FT_CONTA"  )
	self:addProperty("FT_DIFAL"   ,    "Difal Origem/Destino"      , "number", "Difal ICMS "            , "FT_DIFAL"  )
	self:addProperty("FT_ITEM"    ,    "Código do Item"            , "string", "Código Item "           , "FT_ITEM"   )
	self:addProperty("TIPOMOV"   , "Movimento"              , "string", "Movimento"                 , "TIPOMOV"     )
	self:addProperty("TIPO"      , "tp. Nota"               , "string", "tp. Nota"                  , "TIPO"         )
	self:addProperty("FT_CFOP"    ,    "Cod. Fiscal"               , "string", "Cod. Fiscal"            , "FT_CFOP"   )
	self:addProperty("FT_CSTPIS"  ,    "CST Pis"                   , "string", "CST Pis"                , "FT_CSTPIS" )
	self:addProperty("FT_CSTCOF"  ,    "CST COF"                   , "string", "CST COF"                , "FT_CSTCOF" )
	self:addProperty("CJ2_INCIDE" ,   "Incidência"                , "string", "Incidência"             , "CJ2_INCIDE" )
	self:addProperty("CJ2_IREDBS" ,   "Incidência Parcela Reduzida", "string", "Incid. Parcela Reduzida", "CJ2_IREDBS" )
	self:addProperty("FT_PRODUTO" ,    "Código do Produto"         , "string", "Cod. Produto"           , "FT_PRODUTO")

return self:oSchema
