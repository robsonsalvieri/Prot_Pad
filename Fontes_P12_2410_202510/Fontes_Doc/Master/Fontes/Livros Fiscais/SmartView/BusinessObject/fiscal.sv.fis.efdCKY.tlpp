#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.efdCKY.ch"

namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAFIS", tables="CKY", name="efdRetFontePCCSmartviewBusinessObject", country="BRA", initialRelease="12.1.2210")
//-------------------------------------------------------------------
/*{Protheus.doc}  efdRetFontePCCSmartviewBusinessObject
Classe para criação do Objeto Retenção na fonte PIS e COFINS
@author allef.souza
@since 31/07/2023
*/
//-------------------------------------------------------------------  
class efdRetFontePCCSmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	private Data oPrepare as object
	
	public method new() as object
	public method getData() as object
	public method getSchema() as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author allef.souza
@since 31/07/2023
*/
//-------------------------------------------------------------------   
method new() class efdRetFontePCCSmartviewBusinessObject
	Local oFisTools := fisSmartviewTools():new() as object
	_Super:new()
	::oPrepare := nil;
	//Define Area
	self:appendArea(STR0001) //"Livros Fiscais"
	//Define o nome do objeto de Negócio
	self:setDisplayName(STR0002) //"Retenção na fonte PIS e COFINS"
	//Define a descrição do objeto de Negócio
	self:setDescription(STR0003) //"Este objeto de negócio relaciona as retenções na fonte de PIS e COFINS para o período e filiais informados (CKY)"

	oFisTools:validPerg(self:setPergunte("FISTRR1012"))
	freeobj(oFisTools)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: self:oData 
@author allef.souza
@since 31/07/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class efdRetFontePCCSmartviewBusinessObject

	Local cQuery     as Character
	Local cAlias     as Character
	Local oFisTools  := fisSmartviewTools():new() as object
	Local jParams    := oFilter:getParameters() as Json
	Local dStartDate := FwDateTimeToLocal(jParams['MV_PAR01',1])[1] as Date
	Local dEndDate   := FwDateTimeToLocal(jParams['MV_PAR02',1])[1] as Date
	Local cFilDe     := jParams['MV_PAR03',1] as Character
	Local cFilAte    := jParams['MV_PAR04',1] as Character
	Local lOk        := .T. as Logical
	Local aFiliais   := {} as array
	Local cFils      := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character

	if cFils == 'Erro'
		oFisTools:validFil(.f.)
		lOk := .f.
	endif

	if lOk

		cQuery := "SELECT "
		cQuery += 	" CKY.CKY_NUMTIT, "
		cQuery += 	" CKY.CKY_PREFIX, "
		cQuery += 	" CKY.CKY_PARC, "
		cQuery += 	" CKY.CKY_DTEMIS, "
		cQuery += 	" CKY.CKY_ORIG, "
		cQuery += 	" CKY.CKY_DTRET, "
		cQuery += 	" CKY.CKY_CNPJ, "
		cQuery += 	" CKY.CKY_PISRET, "
		cQuery += 	" CKY.CKY_COFRET, "
		cQuery += 	" CKY.CKY_FILIAL "
		cQuery += " FROM "
		cQuery +=   RetSQLName("CKY") + " CKY "
		cQuery += " WHERE "
		cQuery +=  " CKY.CKY_FILIAL IN ( ? ) "
		cQuery +=  " AND CKY.CKY_PER BETWEEN ? AND ? "
		cQuery +=  " AND CKY.D_E_L_E_T_ = ? "
		
		::oPrepare := FWExecStatement():New(ChangeQuery(cQuery))

		::oPrepare:setIn(1,aFiliais)
		::oPrepare:setDate(2,dStartDate )
		::oPrepare:setDate(3,dEndDate)
		::oPrepare:setString(4,' ')

		cAlias:= ::oPrepare:OpenAlias()

		(cAlias)->(DbGoTop())

		while !(cAlias)->(Eof())
			
			self:oData:appendData({;
				"CKY_CNPJ"   : (cAlias)->CKY_CNPJ,;
				"CKY_COFRET" : (cAlias)->CKY_COFRET,;
				"CKY_DTEMIS" : totvs.framework.treports.date.stringToTimeStamp((cAlias)->CKY_DTEMIS),; 
				"CKY_DTRET"  : totvs.framework.treports.date.stringToTimeStamp((cAlias)->CKY_DTRET),;
				"CKY_FILIAL" : (cAlias)->CKY_FILIAL,;
				"CKY_NUMTIT" : (cAlias)->CKY_NUMTIT,;
				"CKY_ORIG"   : (cAlias)->CKY_ORIG,;
				"CKY_PARC"   : (cAlias)->CKY_PARC,;
				"CKY_PISRET" : (cAlias)->CKY_PISRET,;
				"CKY_PREFIX" : (cAlias)->CKY_PREFIX;
				})

			(cAlias)->(dbSkip())
		enddo
		(cAlias)->(DBCloseArea())
	endif

	freeobj(oFisTools)
	freeObj(::oPrepare)
	freeobj(jParams)
	aSize(aFiliais,0)
	aFiliais := Nil

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author allef.souza
@since 31/07/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class efdRetFontePCCSmartviewBusinessObject

	self:addProperty("CKY_CNPJ"  , "CNPJ da Pessoa Juridica", "string", "CNPJ"       , "CKY_CNPJ"  )
	self:addProperty("CKY_COFRET", "COFINS Retido"          , "number", "COFINS Ret.", "CKY_COFRET")
	self:addProperty("CKY_DTEMIS", "Data de Emissão"        , "date"  , "Emissão"    , "CKY_DTEMIS")
	self:addProperty("CKY_DTRET" , "Data da Retencao"       , "date"  , "Dt.Ret"     , "CKY_DTRET" )
	self:addProperty("CKY_FILIAL", "Filial do Sistema"      , "string", "Filial"     , "CKY_FILIAL")
	self:addProperty("CKY_NUMTIT", "Número do Título"       , "string", "Num.Título" , "CKY_NUMTIT")
	self:addProperty("CKY_ORIG"  , "Origem do Título"       , "string", "Origem"     , "CKY_ORIG"  )
	self:addProperty("CKY_PARC"  , "Parcela do Título"      , "string", "Parcela"    , "CKY_PARC"  )
	self:addProperty("CKY_PISRET", "PIS Retido"             , "number", "PIS Ret."   , "CKY_PISRET")
	self:addProperty("CKY_PREFIX", "Prefixo do Título"      , "string", "Prefixo"    , "CKY_PREFIX")

return self:oSchema
