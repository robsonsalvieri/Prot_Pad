#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.BPEObject.ch"

namespace totvs.protheus.fiscal.fis.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", tables="SFT", name="Conferencia fiscal de BP-e", country="BRA", initialRelease="12.1.2310")
//-------------------------------------------------------------------
/*{Protheus.doc} BPESmartViewBusinessObject
Classe para criação do Objeto BPESmartViewBusinessObject - conferência fiscal dos valores dos Bilhetes, ICMS e crédito presumido, para os fechamentos mensais
 
@author Juliana Mellao
@since 20/12/2024
*/
//-------------------------------------------------------------------  
class BPESmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	private data oPrepare as object

	public method new() as object
	public method getData() as object
	public method getSchema() as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Juliana Mellao
@since 20/12/2024
*/
//-------------------------------------------------------------------   
method new() class BPESmartViewBusinessObject
	Local oFisTools := fisSmartviewTools():new()
	_Super:new()
	self:oPrepare := FwExecStatement():New()
	//Define Area
	self:appendArea(STR0001)//"Livros Fiscais"
	//Define o nome do objeto de Negócio
	self:setDisplayName(STR0002)//"Objeto de Negócios BP-e"
	//Define a descrição do objeto de Negócio
	self:setDescription(STR0003)//"Objeto BPEObject - Objeto de negócios para conferência fiscal de BP-e"

	oFisTools:validPerg(self:setPergunte("FISTRBPE02")) //Indica o pergunte que será utilizado no relatório

	freeobj(oFisTools)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData 
 
@author Juliana Mellao
@since 20/12/2024
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class BPESmartViewBusinessObject

	Local cQuery        as character
	Local cAlias        as character
	Local oFisTools     := fisSmartviewTools():new() as object
	Local oFiltQry      := oFilter:getParameters() as json
	Local lOk           := .T. as logical
	Local cFilDe        := oFiltQry['MV_PAR01',1] as character
	Local cFilAte       := oFiltQry['MV_PAR02',1] as character
	Local cPeriodo      := oFiltQry['MV_PAR03',1] as character //Periodo no formato MMAAAA
	Local dStartDate    as date
	Local dEndDate      as date
	Local aFiliais      := {} as array
	Local cFils         := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character
	Local cEspecie      := "BPE" as character
	Local cDBMS         := TCGetDB() as character
	Local lOracle		:= .F. as logical

	if cFils == 'Erro'
		oFisTools:validFil(.f.)
		lOk := .f.
	endif

	if cDBMS == "ORACLE"
		lOracle := .T.
	endif

	If lOk

		dStartDate  := StoD(Substr(cPeriodo,3,4)+Substr(cPeriodo,1,2)+"01")
		dEndDate    := LastDay(dStartDate)

		cQuery := "WITH SFTBPE AS ("
		cQuery += " SELECT"
		cQuery += " 	SFT.FT_FILIAL,SFT.FT_ENTRADA,SFT.FT_NFISCAL,SFT.FT_SERIE,SFT.FT_ESPECIE,SFT.FT_CLIEFOR,SFT.FT_LOJA,"
		cQuery += " 	SFT.FT_CFOP,SFT.FT_ALIQICM,SFT.FT_VALCONT,SFT.FT_BASEICM,SFT.FT_VALICM,SFT.FT_ISENICM,SFT.FT_OUTRICM,"
		cQuery += " 	SFT.FT_ESTADO,SFT.FT_PRODUTO,SFT.FT_ITEM,SFT.FT_QUANT,"
		cQuery += "     SFT.FT_DESCONT,SFT.FT_PRCUNIT,SFT.FT_BASECPR,"
		cQuery += "     CASE WHEN SFT.FT_CRDPRES = '0' THEN SFT.FT_CREDPRE ELSE SFT.FT_CRDPRES END AS FT_CRDPRES,"
		cQuery += "     SFT.FT_CHVNFE,"
		if !lOracle
			cQuery += "     CASE WHEN SUBSTRING(SFT.FT_DTCANC,1,6) = SUBSTRING(SFT.FT_ENTRADA,1,6) THEN SFT.FT_DTCANC ELSE '' END AS FT_DTCANC,"
			cQuery += "     CASE WHEN SUBSTRING(SFT.FT_DTCANC,1,6) = SUBSTRING(SFT.FT_ENTRADA,1,6) THEN SFT.FT_OBSERV ELSE '' END AS FT_OBSERV,"
		else
			cQuery += "     CASE WHEN SUBSTR(SFT.FT_DTCANC,1,6) = SUBSTR(SFT.FT_ENTRADA,1,6) THEN SFT.FT_DTCANC ELSE '' END AS FT_DTCANC,"
			cQuery += "     CASE WHEN SUBSTR(SFT.FT_DTCANC,1,6) = SUBSTR(SFT.FT_ENTRADA,1,6) THEN SFT.FT_OBSERV ELSE '' END AS FT_OBSERV,"
		endif
		cQuery += "     SFT.FT_NFORI,SFT.FT_SERORI,SFT.FT_ITEMORI"
		cQuery += " FROM "+RetSqlName("SFT")+" SFT"
		cQuery += " WHERE"
		cQuery += " 	SFT.FT_FILIAL IN (?)"
		cQuery += " 	AND SFT.FT_TIPOMOV = ?"
		cQuery += " 	AND SFT.FT_ENTRADA BETWEEN ? AND ?"
		cQuery += " 	AND SFT.FT_ESPECIE = ?"
		cQuery += " 	AND SFT.D_E_L_E_T_ = ?"

		cQuery += " UNION ALL"

		cQuery += " SELECT"
		cQuery += " 	SFT.FT_FILIAL,SFT.FT_ENTRADA,SFT.FT_NFISCAL,SFT.FT_SERIE,SFT.FT_ESPECIE,SFT.FT_CLIEFOR,SFT.FT_LOJA,"
		cQuery += " 	SFT.FT_CFOP,SFT.FT_ALIQICM,SFT.FT_VALCONT,SFT.FT_BASEICM,SFT.FT_VALICM,SFT.FT_ISENICM,SFT.FT_OUTRICM,"
		cQuery += " 	SFT.FT_ESTADO,SFT.FT_PRODUTO,SFT.FT_ITEM,SFT.FT_QUANT,"
		cQuery += "     SFT.FT_DESCONT,SFT.FT_PRCUNIT,SFT.FT_BASECPR,"
		cQuery += "     CASE WHEN SFT.FT_CRDPRES = '0' THEN SFT.FT_CREDPRE ELSE SFT.FT_CRDPRES END AS FT_CRDPRES,"
		cQuery += "     SFT.FT_CHVNFE,SFT.FT_DTCANC,SFT.FT_OBSERV,"
		cQuery += "     SFT.FT_NFORI,SFT.FT_SERORI,SFT.FT_ITEMORI"
		cQuery += "	FROM "+RetSqlName("SFT")+" SFT"
		cQuery += " WHERE"
		cQuery += "     SFT.FT_FILIAL IN (?)"
		cQuery += " 	AND SFT.FT_TIPOMOV = ?"
		cQuery += "     AND (SFT.FT_ENTRADA < ? OR SFT.FT_ENTRADA > ?)"
		cQuery += "     AND SFT.FT_DTCANC BETWEEN ? AND ?"
		cQuery += "     AND SFT.FT_ESPECIE = ?"
		cQuery += "     AND SFT.D_E_L_E_T_ = ?"
		cQuery += " )"

		cQuery += " SELECT"
		cQuery += " 	SFTS.FT_FILIAL,SFTS.FT_ENTRADA,SFTS.FT_NFISCAL,SFTS.FT_SERIE,SFTS.FT_ESPECIE,SFTS.FT_CLIEFOR,SFTS.FT_LOJA,"
		cQuery += " 	SFTS.FT_CFOP,SFTS.FT_ALIQICM,SFTS.FT_VALCONT,SFTS.FT_BASEICM,SFTS.FT_VALICM,SFTS.FT_ISENICM,SFTS.FT_OUTRICM,"
		cQuery += " 	SFTS.FT_ESTADO,SFTS.FT_PRODUTO,SFTS.FT_ITEM,SFTS.FT_QUANT,SFTS.FT_DTCANC,SFTS.FT_DESCONT,SFTS.FT_PRCUNIT,"
		cQuery += " 	SFTS.FT_BASECPR,SFTS.FT_CRDPRES,SFTS.FT_CHVNFE,SFTS.FT_OBSERV,"
		cQuery += " CASE "
		cQuery += " 	WHEN SFTS.FT_DTCANC <> ' ' THEN"
		cQuery += " 		CASE"
		if !lOracle
			cQuery += " 		WHEN SUBSTRING(SFTS.FT_DTCANC, 1, 6) = SUBSTRING(SFTS.FT_ENTRADA, 1, 6) THEN 'D'"
			cQuery += " 		WHEN SUBSTRING(SFTS.FT_DTCANC, 1, 6) <> SUBSTRING(SFTS.FT_ENTRADA, 1, 6) THEN 'F'"        
		else
			cQuery += " 		WHEN SUBSTR(SFTS.FT_DTCANC, 1, 6) = SUBSTR(SFTS.FT_ENTRADA, 1, 6) THEN 'D'"
			cQuery += " 		WHEN SUBSTR(SFTS.FT_DTCANC, 1, 6) <> SUBSTR(SFTS.FT_ENTRADA, 1, 6) THEN 'F'"        
		endif
		cQuery += " 		END"
		cQuery += " 	ELSE ' '"
		cQuery += " END AS CANCELAMENTO,"
		cQuery += " CASE"
		cQuery += " 	WHEN SFTS.FT_NFORI <> ' ' THEN"
		cQuery += " 		CASE"
		if !lOracle
			cQuery += " 		WHEN SUBSTRING(SFTS.FT_ENTRADA, 1, 6) = SUBSTRING(S.FT_ENTRADA, 1, 6) THEN 'D'"
			cQuery += " 		WHEN SUBSTRING(SFTS.FT_ENTRADA, 1, 6) <> SUBSTRING(S.FT_ENTRADA, 1, 6) THEN 'F'"
		else
			cQuery += " 		WHEN SUBSTR(SFTS.FT_ENTRADA, 1, 6) = SUBSTR(S.FT_ENTRADA, 1, 6) THEN 'D'"
			cQuery += " 		WHEN SUBSTR(SFTS.FT_ENTRADA, 1, 6) <> SUBSTR(S.FT_ENTRADA, 1, 6) THEN 'F'"
		endif
		cQuery += " 		END"
		cQuery += " 	ELSE ' '"
		cQuery += " END AS SUBSTITUTO,"
		cQuery += "     SFTS.FT_NFORI,SFTS.FT_SERORI,SFTS.FT_ITEMORI,"
		cQuery += " 	S.FT_ENTRADA AS FTEMISSAO,S.FT_NFISCAL AS FTNFISCAL,S.FT_SERIE AS FTSERIE,S.FT_ITEM AS FTITEM,"
		cQuery += "		S.FT_ESPECIE AS FTESPECIE,S.FT_CHVNFE AS FTCHAVE,S.FT_CLIEFOR AS FTCLIFOR,S.FT_LOJA AS FTLOJA,"
		cQuery += " 	S.FT_CFOP AS FTCFOP,S.FT_ALIQICM AS FTALIQICM,S.FT_VALCONT AS FTVALCONT,S.FT_BASEICM AS FTBASEICM,"
		cQuery += "		S.FT_VALICM AS FTVALICM,S.FT_ISENICM AS FTISENICM,S.FT_OUTRICM AS FTOUTRICM,S.FT_BASECPR AS FTBASECPR,S.FT_CRDPRES AS FTCRDPRES"
		cQuery += " FROM SFTBPE SFTS"
		cQuery += " LEFT JOIN "+RetSqlName("SFT")+" S"
		cQuery += "		ON S.FT_FILIAL = SFTS.FT_FILIAL"
		cQuery += " 	AND S.FT_TIPOMOV = ?"
		cQuery += " 	AND S.FT_NFISCAL = SFTS.FT_NFORI"
		cQuery += " 	AND S.FT_SERIE = SFTS.FT_SERORI"
		cQuery += " 	AND S.FT_ITEM = SFTS.FT_ITEMORI"
		cQuery += "     AND S.D_E_L_E_T_ = ?"
		cQuery += " ORDER BY SFTS.FT_FILIAL, SFTS.FT_ENTRADA, SFTS.FT_NFISCAL, SFTS.FT_CLIEFOR, SFTS.FT_LOJA, SFTS.FT_ITEM"

		self:oPrepare:setQuery(cQuery)

		self:oPrepare:setIn(1,aFiliais)
		self:oPrepare:setString(2, 'S')
		self:oPrepare:setDate(3,dStartDate)
		self:oPrepare:setDate(4,dEndDate)
		self:oPrepare:setString(5, cEspecie)
		self:oPrepare:setString(6, ' ')

		self:oPrepare:setIn(7,aFiliais)
		self:oPrepare:setString(8, 'S')
		self:oPrepare:setDate(9,dStartDate)
		self:oPrepare:setDate(10,dEndDate)
		self:oPrepare:setDate(11,dStartDate)
		self:oPrepare:setDate(12,dEndDate)
		self:oPrepare:setString(13, cEspecie)
		self:oPrepare:setString(14, ' ')

		self:oPrepare:setString(15, 'S')
		self:oPrepare:setString(16, ' ')

		cAlias := self:oPrepare:OpenAlias()

		while !(cAlias)->(Eof())
			self:oData:appendData({;
				"FT_FILIAL"    : (cAlias)->FT_FILIAL,;
				"FT_ENTRADA"   : totvs.framework.treports.date.stringToTimeStamp((cAlias)->FT_ENTRADA),;
				"FT_NFISCAL"   : Alltrim((cAlias)->FT_NFISCAL),;
				"FT_SERIE"     : (cAlias)->FT_SERIE,;
				"FT_ESPECIE"   : (cAlias)->FT_ESPECIE,;
				"FT_CLIEFOR"   : (cAlias)->FT_CLIEFOR,;
				"FT_LOJA"      : (cAlias)->FT_LOJA,;
				"FT_CFOP"      : (cAlias)->FT_CFOP,;
				"FT_ALIQICM"   : (cAlias)->FT_ALIQICM,;
				"FT_VALCONT"   : (cAlias)->FT_VALCONT,;
				"FT_BASEICM"   : (cAlias)->FT_BASEICM,;
				"FT_VALICM"    : (cAlias)->FT_VALICM,;
				"FT_ISENICM"   : (cAlias)->FT_ISENICM,;
				"FT_OUTRICM"   : (cAlias)->FT_OUTRICM,;
				"FT_PRODUTO"   : (cAlias)->FT_PRODUTO,;
				"FT_ITEM"      : (cAlias)->FT_ITEM,;
				"FT_QUANT"     : (cAlias)->FT_QUANT,;
				"FT_DTCANC"    : totvs.framework.treports.date.stringToTimeStamp((cAlias)->FT_DTCANC),;
				"CANCELAMENTO" : Alltrim((cAlias)->CANCELAMENTO),;
				"FT_DESCONT"   : (cAlias)->FT_DESCONT,;
				"FT_PRCUNIT"   : (cAlias)->FT_PRCUNIT,;
				"FT_BASECPR"   : (cAlias)->FT_BASECPR,;
				"FT_CRDPRES"   : (cAlias)->FT_CRDPRES,;
				"FT_CHVNFE"    : (cAlias)->FT_CHVNFE,;
				"FT_OBSERV"    : (cAlias)->FT_OBSERV,;
				"SUBSTITUTO"   : Alltrim((cAlias)->SUBSTITUTO),;
				"FT_NFORI"     : (cAlias)->FT_NFORI,;
				"FT_SERORI"    : (cAlias)->FT_SERORI,;
				"FT_ITEMORI"   : (cAlias)->FT_ITEMORI,;
				"FTEMISSAO"    : totvs.framework.treports.date.stringToTimeStamp((cAlias)->FTEMISSAO),;
				"FTCHAVE"      : (cAlias)->FTCHAVE,;
				"FTALIQICM"    : (cAlias)->FTALIQICM,;
				"FTVALCONT"    : (cAlias)->FTVALCONT,;
				"FTBASEICM"    : (cAlias)->FTBASEICM,;
				"FTVALICM"     : (cAlias)->FTVALICM,;
				"FTISENICM"    : (cAlias)->FTISENICM,;
				"FTOUTRICM"    : (cAlias)->FTOUTRICM,;
				"FTBASECPR"    : (cAlias)->FTBASECPR,;
				"FTCRDPRES"    : (cAlias)->FTCRDPRES;
				})
			(cAlias)->( dbSkip() )
		enddo
		(cAlias)->( DBCloseArea() )
	EndIf

	FWfreeobj(oFisTools)
	self:oPrepare:Destroy()
	self:oPrepare := nil
	FWfreeobj(oFiltQry)
	aSize(aFiliais,0)
	aFiliais := Nil

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Juliana Mellao
@since 20/12/2024
*/
//-------------------------------------------------------------------   
method getSchema() as object class BPESmartViewBusinessObject

	//               ( Nome          , Descricao           		, Tipo    , Display             	  , cRealName       , cComboValues, lIsRequired, cRenameField)
	self:addProperty("FT_FILIAL"     , "Filial"            		, "string"  , "Filial"            	  , "FT_FILIAL"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_ENTRADA"    , "Data Emissao"      		, "date"    , "Data Emissao"      	  , "FT_ENTRADA"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_ESPECIE"    , "Especie"           		, "string"  , "Especie"           	  , "FT_ESPECIE"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_NFISCAL"    , "Nota Fiscal"       		, "string"  , "Nota Fiscal"       	  , "FT_NFISCAL"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_SERIE"      , "Serie"             		, "string"  , "Serie"             	  , "FT_SERIE"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_CLIEFOR"    , "Cliente/Fornec"    		, "string"  , "Cliente/Fornec"    	  , "FT_CLIEFOR"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_LOJA"       , "Loja"              		, "string"  , "Loja"                  , "FT_LOJA"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_PRODUTO"    , "Produto"           		, "string"  , "Produto"		          , "FT_PRODUTO"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_ITEM"       , "Item"              		, "string"  , "Item" 		          , "FT_ITEM"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_QUANT"      , "Quant"             		, "number"  , "Quant"		          , "FT_QUANT"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_PRCUNIT"    , "Vlr Unitario"      		, "number"  , "Vlr Unitario"	      , "FT_PRCUNIT"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_DESCONT"    , "Vlr Desconto"      		, "number"  , "Vlr Desconto"	      , "FT_DESCONT"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_VALCONT"    , "Vlr Contabil"      		, "number"  , "Vlr Contabil"          , "FT_VALCONT"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_CFOP"       , "CFOP"              		, "string"  , "CFOP"		          , "FT_CFOP"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_BASEICM"    , "Base ICMS"         		, "number"  , "Base ICMS"	          , "FT_BASEICM"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_ALIQICM"    , "Aliq ICMS"         		, "number"  , "Aliq ICMS"             , "FT_ALIQICM"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_VALICM"     , "Vlr ICMS"          		, "number"  , "Vlr ICMS"	          , "FT_VALICM"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_ISENICM"    , "Vlr Isen ICMS"     		, "number"  , "Vlr Isen ICMS"         , "FT_ISENICM"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_OUTRICM"    , "Vlr Out ICMS"      		, "number"  , "Vlr Out ICMS"          , "FT_OUTRICM"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_BASECPR"    , "Base Cred Presum"  		, "number"  , "Base Cred Presu"       , "FT_BASECPR"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_CRDPRES"    , "Vlr Cred Presum"   		, "number"  , "Vlr Cred Presum"       , "FT_CRDPRES"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_CHVNFE"     , "Chave NFe"         		, "string"  , "Chave NFe"	          , "FT_CHVNFE"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_OBSERV"     , "Observ"            		, "string"  , "Observ"		          , "FT_OBSERV"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_DTCANC"     , "Data Cancel"       		, "date"    , "Data Cancel"	          , "FT_DTCANC"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("CANCELAMENTO"  , "Per.Cancelamento"  		, "string"  , "Per.Cancelamento"      , "CANCELAMENTO"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("SUBSTITUTO"    , "Per.Substituicao"  		, "string"  , "Per.Substituicao"	  , "SUBSTITUTO"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_NFORI"      , "NF Original"       		, "string"  , "NF Original"	     	  , "FT_NFORI"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_SERORI"     , "Serie Original"    		, "string"  , "Serie Original"    	  , "FT_SERORI"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FT_ITEMORI"    , "Item Original"     		, "string"  , "Item Original"     	  , "FT_ITEMORI"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FTEMISSAO"     , "Data Emissao Sub"  		, "date"    , "Data Emissao Sub"  	  , "FTEMISSAO"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FTVALCONT"     , "Vlr Contabil Sub"  		, "number"  , "Vlr Contabil Sub"  	  , "FTVALCONT"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FTBASEICM"     , "Base ICMS Sub"     		, "number"  , "Base ICMS Sub"	      , "FTBASEICM"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FTALIQICM"     , "Aliq ICMS Sub"     		, "number"  , "Aliq ICMS Sub"     	  , "FTALIQICM"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FTVALICM"      , "Vlr ICMS Sub"      		, "number"  , "Vlr ICMS Sub"	      , "FTVALICM"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FTISENICM"     , "Vlr Isen ICMS Sub" 		, "number"  , "Vlr Isen ICMS Sub" 	  , "FTISENICM"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FTOUTRICM"     , "Vlr Out ICMS Sub"  		, "number"  , "Vlr Out ICMS Sub"  	  , "FTOUTRICM"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FTBASECPR"     , "Base Cred Presum Sub"   , "number"  , "Base Cred Presu Sub"   , "FTBASECPR"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FTCRDPRES"     , "Vlr Cred Presum Sub"    , "number"  , "Vlr Cred Presum Sub"   , "FTCRDPRES"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("FTCHAVE"       , "Chave NFe Sub"     		, "string"  , "Chave NFe Sub"     	  , "FTCHAVE"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
return self:oSchema
