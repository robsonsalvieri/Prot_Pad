#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.efdSFTInd.ch"

namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAFIS", tables="SFT,SA1,SB1,SF4",;
	name="efdSFTIndSmartviewBusinessObject", country="BRA", initialRelease="12.1.2310")
//-------------------------------------------------------------------
/*{Protheus.doc}  efdSFTIndSmartviewBusinessObject
@author eduardo.melo
@since 31/07/2023
*/
//-------------------------------------------------------------------  

class efdSFTIndSmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	data nPageSize as integer

	public method new() as object
	public method getData() as object
	public method getSchema() as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author eduardo.melo
@since 31/07/2023
*/
//-------------------------------------------------------------------   
method new() class efdSFTIndSmartviewBusinessObject

	Local oFisTools := fisSmartviewTools():new()

	_Super:new()
	//Define Area
	self:appendArea(STR0001) //"Livros Fiscais"
	//Define o nome do objeto de Negócio
	self:setDisplayName(STR0002) //"Objeto de Negócio EFD Contribuições - SFT"
	//Define a descrição do objeto de Negócio
	self:setDescription(STR0003) //"Objeto de negócio que fornece o detalhamento do EFD Contribuições (SFT)"
	//self:setIsCBoxLookup(.T., .F.)

	self:nPageSize := oFisTools:Paginacao(500)
	oFisTools:validPerg(::setPergunte("FISTRR1020"))
	freeobj(oFisTools)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: self:oData 
@author eduardo.melo
@since 27/10/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class efdSFTIndSmartviewBusinessObject

	Local cQuery     := "" as character
	Local cAlias     := "" as character
	Local cObserv	 := "" as character
	Local oFisTools  := fisSmartviewTools():new() as object
	Local oFiltQry   := oFilter:getParameters() as Json
	Local dStartDate := FwDateTimeToLocal(oFiltQry['MV_PAR01',1])[1] as date
	Local dEndDate   := FwDateTimeToLocal(oFiltQry['MV_PAR02',1])[1] as date
	Local cFilDe     := oFiltQry['MV_PAR03',1] as character
	Local cFilAte    := oFiltQry['MV_PAR04',1] as character
	Local lOk		 := .T. as logical
	Local aFiliais   := {} as array
	Local nFilial	 := 0   as numeric
	Local cFils      := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character
	Local oPrepare   := nil as object
	Local aCSTE      := {}  as array
	Local aCSTS      := {}  as array
    Local aCODRSEF   := {}  as array
	Local cFilDT6	 := "" as character
	Local cSeekDT6	 := "" as character

	static oJsonCTE	 := nil as json
    
	aCSTE := {"70", "71", "72", "73", "74", "98", "99"}
	aCSTS := {"01", "05", "07", "08", "09", "49"}
    aCODRSEF := { "110", "204", "205", "301", "302", "303", "304", "305", "306", "102"}

	if cFils == 'Erro'
		oFisTools:validFil(.f.)
		lOk := .f.
	endif

	If lOk

		//definindo a quantidade máxima por página (default 100)
		self:setPageSize(self:nPageSize)

		cQuery += "	SELECT SF4.F4_TPREG, "
		cQuery += " SFT.FT_TES,"
		cQuery += " SFT.FT_CFOP,"
		cQuery += " SFT.FT_ESPECIE,"
		cQuery += " SFT.FT_ALIQPIS,"
		cQuery += " SFT.FT_ALIQCOF,"
		cQuery += " SFT.FT_ALIQCF3,"
		cQuery += " SFT.FT_ALIQPS3,"
		cQuery += " SFT.FT_CSTPIS,"
		cQuery += " SFT.FT_CSTCOF,"
		cQuery += " SFT.FT_PAUTPIS,"
		cQuery += " SFT.FT_PAUTCOF,"
		cQuery += " SFT.FT_TNATREC,"
		cQuery += " SFT.FT_CNATREC,"
		cQuery += " SFT.FT_GRUPONC,"
		cQuery += " SFT.FT_DTFIMNT,"
		cQuery += " SFT.FT_CODBCC,"
		cQuery += " SFT.FT_CONTA,"
		cQuery += " SFT.FT_MALQCOF,"
		cQuery += " SFT.FT_MALQPIS,"
		cQuery += " SFT.FT_TIPO ,"
		cQuery += " SFT.FT_CREDST,"
		cQuery += " SFT.FT_ESTADO,"
		cQuery += " SFT.FT_SERSAT,"
		cQuery += " SFT.FT_ALIQCPB,"
		cQuery += " SFT.FT_ATIVCPB,"
		cQuery += " SFT.FT_FILIAL ,"
		cQuery += " SFT.FT_NFELETR ,"
		cQuery += " SFT.FT_NFISCAL ,"
		cQuery += " SFT.FT_SERIE ,"
		cQuery += " SFT.FT_LOJA ,"
		cQuery += " SFT.FT_CLIEFOR ,"
		cQuery += " SFT.FT_EMISSAO ,"
		cQuery += " SFT.FT_ENTRADA ,"
		cQuery += " SFT.FT_PRODUTO ,"
		cQuery += " SFT.FT_ITEM ,"
		cQuery += " SFT.FT_VALPIS ,"
		cQuery += " SFT.FT_VALCOF ,"
		cQuery += " SFT.FT_VALCONT ,"
		cQuery += " SFT.FT_TOTAL ,"
		cQuery += " SFT.FT_BASEPIS ,"
		cQuery += " SFT.FT_BASECOF ,"
		cQuery += " SFT.FT_BASECF3 ,"
		cQuery += " SFT.FT_BASEPS3 ,"
		cQuery += " SFT.FT_VALPS3 ,"
		cQuery += " SFT.FT_VALCF3 ,"
		cQuery += " SFT.FT_MVALCOF ,"
		cQuery += " SFT.FT_MVALPIS ,"
		cQuery += " SFT.FT_DTCANC ,"
		cQuery += " SFT.FT_VALIPI ,"
		cQuery += " SFT.FT_ICMSRET ,"
		cQuery += " SFT.FT_OBSERV ,"
		cQuery += " SFT.FT_TIPOMOV ,"
		cQuery += " SFT.FT_ISENRET ,"
		cQuery += " SFT.FT_OBSSOL ,"
		cQuery += " SFT.FT_OUTRRET ,"
		cQuery += " SFT.FT_QUANT ,"
		cQuery += " SFT.FT_DESCZFR ,"
		cQuery += " SFT.FT_DESCONT ,"
		cQuery += " SFT.FT_SOLTRIB ,"
		cQuery += " SFT.FT_BASECPB,"
		cQuery += " SFT.FT_VALCPB, "
		cQuery += " SFT.FT_NFORI,"
		cQuery += " SFT.FT_SERORI,"
		cQuery += " SF4.F4_ATUATF "
		cQuery += "	FROM "+ RetSqlName("SFT")+" SFT "  
		cQuery += "	LEFT JOIN "+RetSqlName("SF4")+" SF4 ON ( "  
		cQuery += " SF4.F4_FILIAL = ? "							// ? - 01
		cQuery += "	AND SF4.F4_CODIGO = SFT.FT_TES"
		cQuery += "	AND SF4.D_E_L_E_T_ = ? )" 					// ? - 02
		cQuery += "	WHERE "
		cQuery += " SFT.FT_FILIAL =  ?  " 						// ? - 03
		cQuery += "	AND SFT.FT_TIPOMOV = ? " 					// ? - 04
		cQuery += "	AND SFT.FT_ENTRADA >= ? "				    // ? - 05
		cQuery += "	AND SFT.FT_ENTRADA <= ? " 					// ? - 06
		cQuery += "	AND ( (SF4.F4_ATUATF IN ( ? ) "				// ? - 07
		cQuery += "	AND (((SFT.FT_BASEPIS > ? " 				// ? - 08
		cQuery += "	OR SFT.FT_BASEPS3 > ? )" 					// ? - 09
		cQuery += "	OR SFT.FT_CSTPIS IN ( ? ))" 				// ? - 10
		cQuery += "	OR ((SFT.FT_BASECOF > ? " 					// ? - 11
		cQuery += "	OR SFT.FT_BASECF3 > ?)" 					// ? - 12
		cQuery += "	OR SFT.FT_CSTCOF IN ( ? ))))" 				// ? - 13
		cQuery += "	OR (SF4.F4_ATUATF = ? "		 				// ? - 14
		cQuery += "	AND ((SFT.FT_CSTCOF IN ( ? ))" 				// ? - 15
		cQuery += "	OR (SFT.FT_CSTPIS IN ( ? )))) )" 			// ? - 16
		cQuery += "	AND SFT.D_E_L_E_T_ = ?"						// ? - 17
		cQuery += "	UNION ALL "
		cQuery += "	SELECT SF4.F4_TPREG, "
		cQuery += " SFT.FT_TES,"
		cQuery += "	SFT.FT_CFOP,"
		cQuery += "	SFT.FT_ESPECIE,"
		cQuery += "	SFT.FT_ALIQPIS,"
		cQuery += "	SFT.FT_ALIQCOF,"
		cQuery += "	SFT.FT_ALIQCF3,"
		cQuery += "	SFT.FT_ALIQPS3,"
		cQuery += "	SFT.FT_CSTPIS,"
		cQuery += "	SFT.FT_CSTCOF,"
		cQuery += "	SFT.FT_PAUTPIS,"
		cQuery += "	SFT.FT_PAUTCOF,"
		cQuery += "	SFT.FT_TNATREC,"
		cQuery += "	SFT.FT_CNATREC,"
		cQuery += "	SFT.FT_GRUPONC,"
		cQuery += "	SFT.FT_DTFIMNT,"
		cQuery += "	SFT.FT_CODBCC,"
		cQuery += "	SFT.FT_CONTA,"
		cQuery += "	SFT.FT_MALQCOF,"
		cQuery += "	SFT.FT_MALQPIS,"
		cQuery += "	SFT.FT_TIPO ,"
		cQuery += "	SFT.FT_CREDST,"
		cQuery += "	SFT.FT_ESTADO,"
		cQuery += "	SFT.FT_SERSAT,"
		cQuery += "	SFT.FT_ALIQCPB,"
		cQuery += "	SFT.FT_ATIVCPB,"
		cQuery += "	SFT.FT_FILIAL ,"
		cQuery += "	SFT.FT_NFELETR ,"
		cQuery += "	SFT.FT_NFISCAL ,"
		cQuery += "	SFT.FT_SERIE ,"
		cQuery += "	SFT.FT_LOJA ,"
		cQuery += "	SFT.FT_CLIEFOR ,"
		cQuery += "	SFT.FT_EMISSAO ,"
		cQuery += "	SFT.FT_ENTRADA ,"
		cQuery += "	SFT.FT_PRODUTO ,"
		cQuery += "	SFT.FT_ITEM ,"
		cQuery += "	SFT.FT_VALPIS ,"
		cQuery += "	SFT.FT_VALCOF ,"
		cQuery += "	SFT.FT_VALCONT ,"
		cQuery += "	SFT.FT_TOTAL ,"
		cQuery += "	SFT.FT_BASEPIS ,"
		cQuery += "	SFT.FT_BASECOF ,"
		cQuery += "	SFT.FT_BASECF3 ,"
		cQuery += "	SFT.FT_BASEPS3 ,"
		cQuery += "	SFT.FT_VALPS3 ,"
		cQuery += "	SFT.FT_VALCF3 ,"
		cQuery += "	SFT.FT_MVALCOF ,"
		cQuery += "	SFT.FT_MVALPIS ,"
		cQuery += "	SFT.FT_DTCANC ,"
		cQuery += "	SFT.FT_VALIPI ,"
		cQuery += "	SFT.FT_ICMSRET ,"
		cQuery += "	SFT.FT_OBSERV ,"
		cQuery += "	SFT.FT_TIPOMOV ,"
		cQuery += "	SFT.FT_ISENRET ,"
		cQuery += "	SFT.FT_OBSSOL ,"
		cQuery += "	SFT.FT_OUTRRET ,"
		cQuery += "	SFT.FT_QUANT ,"
		cQuery += "	SFT.FT_DESCZFR ,"
		cQuery += "	SFT.FT_DESCONT ,"
		cQuery += "	SFT.FT_SOLTRIB ,"
		cQuery += "	SFT.FT_BASECPB,"
		cQuery += "	SFT.FT_VALCPB, "
		cQuery += " SFT.FT_NFORI,"
		cQuery += " SFT.FT_SERORI,"
		cQuery += " SF4.F4_ATUATF "
		cQuery += "	FROM "+RetSqlName("SFT")+" SFT" 
		cQuery += "	INNER JOIN "+RetSqlName("SF3")+" SF3 ON ( " 
		cQuery += " SFT.FT_FILIAL = SF3.F3_FILIAL "
		cQuery += "	AND SF3.F3_SERIE = SFT.FT_SERIE"
		cQuery += "	AND SF3.F3_NFISCAL = SFT.FT_NFISCAL"
		cQuery += "	AND SF3.F3_CLIEFOR = SFT.FT_CLIEFOR"
		cQuery += "	AND SF3.F3_LOJA = SFT.FT_LOJA"
		cQuery += "	AND SF3.F3_IDENTFT = SFT.FT_IDENTF3"
		cQuery += "	AND SF3.D_E_L_E_T_ = ? " 					// ? - 18
		cQuery += "	AND SF3.F3_CODRSEF NOT IN ( ? ) " 			// ? - 19
		cQuery += "	AND SF3.F3_CFO = SFT.FT_CFOP)"
		cQuery += "	LEFT JOIN "+RetSqlName("SF4")+" SF4 ON ( " 
		cQuery += " SF4.F4_FILIAL = ? "							// ? - 20
		cQuery += "	AND SF4.F4_CODIGO = SFT.FT_TES"
		cQuery += "	AND SF4.D_E_L_E_T_ = ? )"					// ? - 21
		cQuery += "	WHERE "
		cQuery += " SFT.FT_FILIAL = ?   " 						// ? - 22
		cQuery += "	AND SFT.FT_TIPOMOV = ? "					// ? - 23
		cQuery += "	AND SFT.FT_ENTRADA >= ? " 					// ? - 24
		cQuery += "	AND SFT.FT_ENTRADA <= ? " 					// ? - 25
		cQuery += "	AND (( (SFT.FT_BASEPIS > ? " 				// ? - 26
		cQuery += "	OR SFT.FT_BASEPS3 > ? ) " 					// ? - 27
		cQuery += "	OR SFT.FT_CSTPIS IN ( ? )) "				// ? - 28
		cQuery += "	OR ( (SFT.FT_BASECOF > ? "					// ? - 29
		cQuery += "	OR SFT.FT_BASECF3 > ? ) " 					// ? - 30
		cQuery += "	OR SFT.FT_CSTCOF IN ( ? ))) "				// ? - 31
		cQuery += "	AND SFT.D_E_L_E_T_ = ? "					// ? - 32

		oPrepare := FWExecStatement():New(ChangeQuery(cQuery))
		
		oJsonCTE := JsonObject():new()
		oJsonCTE['filial'] := cFilDe

		DT6->(DbSetOrder(1)) // DT6_FILIAL, DT6_FILDOC, DT6_DOC, DT6_SERIE
		
		for nFilial := 1 to len(aFiliais)

			oPrepare:SetString(01, xFilial("SF4",aFiliais[nFilial]) )	//? - 01 - SF4.F4_FILIAL
			oPrepare:SetString(02, ' '	)			        			//? - 02
			oPrepare:SetString(03, xFilial("SFT",aFiliais[nFilial]) )	//? - 03
			oPrepare:SetString(04, 'E'	)								//? - 04
			oPrepare:SetDate(05, dStartDate )							//? - 05
			oPrepare:SetDate(06, dEndDate   ) 							//? - 06
			oPrepare:SetIn( 07, { ' ', 'N' }) 							//? - 07
			oPrepare:SetNumeric( 08, 0	)	 							//? - 08
			oPrepare:SetNumeric( 09, 0	)	 							//? - 09
			oPrepare:SetIn(10, aCSTE	) 								//? - 10
			oPrepare:SetNumeric( 11, 0	)	  							//? - 11
			oPrepare:SetNumeric( 12, 0	)	  							//? - 12
			oPrepare:SetIn(13, aCSTE	)  								//? - 13
			oPrepare:SetString(14, 'S'	)	 							//? - 14
			oPrepare:SetIn(15, aCSTE	) 								//? - 15
			oPrepare:SetIn(16, aCSTE	)								//? - 16
			oPrepare:SetString(17, ' '	)								//? - 17

			oPrepare:SetString(18, ' '	)								//? - 18
			oPrepare:SetIn(19, aCODRSEF	)								//? - 19
			oPrepare:SetString(20, xFilial("SF4",aFiliais[nFilial]) )	//? - 20 - SF4.F4_FILIAL
			oPrepare:SetString(21, ' '	)								//? - 21
			oPrepare:SetString(22, xFilial("SFT",aFiliais[nFilial]) )	//? - 22
			oPrepare:SetString(23, 'S'	)								//? - 23
			oPrepare:SetDate(24, dStartDate )							//? - 24
			oPrepare:SetDate(25, dEndDate   )	 						//? - 25
			oPrepare:SetNumeric(26, 0	) 								//? - 26
			oPrepare:SetNumeric(27, 0	)								//? - 27
			oPrepare:SetIn(28, aCSTS	)								//? - 28
			oPrepare:SetNumeric(29, 0 	)								//? - 29
			oPrepare:SetNumeric(30, 0	)								//? - 30
			oPrepare:SetIn(31, aCSTS 	)								//? - 31
			oPrepare:SetString(32, ' ' 	)								//? - 32

			cAlias := oPrepare:OpenAlias()
			
			cFilAnt := aFiliais[nFilial]
			cFilDT6	:= xFilial("DT6")

			while !(cAlias)->(Eof())

				cSeekDT6 := cFilDT6+(cAlias)->FT_FILIAL+(cAlias)->FT_NFISCAL+(cAlias)->FT_SERIE

				cObserv := vldMsgObserv((cAlias)->FT_OBSERV, (cAlias)->FT_TIPOMOV, Alltrim((cAlias)->FT_ESPECIE), (cAlias)->FT_NFORI, cSeekDT6, aFiliais[nFilial])

				self:oData:appendData({;
					"F4_ATUATF"     : (cAlias)->F4_ATUATF,;
					"F4_TPREG"      : (cAlias)->F4_TPREG,;
					"FT_FILIAL"     : (cAlias)->FT_FILIAL,;
					"FT_ENTRADA"    : totvs.framework.treports.date.stringToTimeStamp((cAlias)->FT_ENTRADA),;
					"FT_EMISSAO"    : totvs.framework.treports.date.stringToTimeStamp((cAlias)->FT_EMISSAO),;
					"FT_NFISCAL"    : (cAlias)->FT_NFISCAL,;
					"FT_SERIE"      : (cAlias)->FT_SERIE,;
					"FT_CLIEFOR"    : (cAlias)->FT_CLIEFOR,;
					"FT_LOJA"       : (cAlias)->FT_LOJA,;
					"FT_ESTADO"     : (cAlias)->FT_ESTADO,;
					"FT_CFOP"       : (cAlias)->FT_CFOP,;
					"FT_VALCONT"    : (cAlias)->FT_VALCONT,;
					"FT_VALIPI"     : (cAlias)->FT_VALIPI,;
					"FT_ICMSRET"    : (cAlias)->FT_ICMSRET,;
					"FT_OBSERV"     : cObserv,;
					"FT_TIPO"       : (cAlias)->FT_TIPO,;
					"FT_OBSSOL"     : (cAlias)->FT_OBSSOL,;
					"FT_SOLTRIB"    : (cAlias)->FT_SOLTRIB,;
					"FT_DTCANC"     : totvs.framework.treports.date.stringToTimeStamp((cAlias)->FT_DTCANC),;
					"FT_ESPECIE"    : (cAlias)->FT_ESPECIE,;
					"FT_CREDST"     : (cAlias)->FT_CREDST,;
					"FT_CONTA"      : (cAlias)->FT_CONTA,;
					"FT_PRODUTO"    : (cAlias)->FT_PRODUTO,;
					"FT_TIPOMOV"    : (cAlias)->FT_TIPOMOV,;
					"FT_ITEM"       : (cAlias)->FT_ITEM,;
					"FT_ISENRET"    : (cAlias)->FT_ISENRET,;
					"FT_OUTRRET"    : (cAlias)->FT_OUTRRET,;
					"FT_QUANT"      : (cAlias)->FT_QUANT,;
					"FT_DESCONT"    : (cAlias)->FT_DESCONT,;
					"FT_TOTAL"      : (cAlias)->FT_TOTAL,;
					"FT_BASEPIS"    : (cAlias)->FT_BASEPIS,;
					"FT_ALIQPIS"    : (cAlias)->FT_ALIQPIS,;
					"FT_VALPIS"     : (cAlias)->FT_VALPIS,;
					"FT_BASECOF"    : (cAlias)->FT_BASECOF,;
					"FT_ALIQCOF"    : (cAlias)->FT_ALIQCOF,;
					"FT_VALCOF"     : (cAlias)->FT_VALCOF,;
					"FT_BASEPS3"    : (cAlias)->FT_BASEPS3,;
					"FT_ALIQPS3"    : (cAlias)->FT_ALIQPS3,;
					"FT_VALPS3"     : (cAlias)->FT_VALPS3,;
					"FT_BASECF3"    : (cAlias)->FT_BASECF3,;
					"FT_ALIQCF3"    : (cAlias)->FT_ALIQCF3,;
					"FT_VALCF3"     : (cAlias)->FT_VALCF3,;
					"FT_NFELETR"    : (cAlias)->FT_NFELETR,;
					"FT_CSTPIS"     : (cAlias)->FT_CSTPIS,;
					"FT_CSTCOF"     : (cAlias)->FT_CSTCOF,;
					"FT_CODBCC"     : (cAlias)->FT_CODBCC,;
					"FT_PAUTPIS"    : (cAlias)->FT_PAUTPIS,;
					"FT_PAUTCOF"    : (cAlias)->FT_PAUTCOF,;
					"FT_TNATREC"    : (cAlias)->FT_TNATREC,;
					"FT_CNATREC"    : (cAlias)->FT_CNATREC,;
					"FT_GRUPONC"    : (cAlias)->FT_GRUPONC,;
					"FT_DTFIMNT"    : totvs.framework.treports.date.stringToTimeStamp((cAlias)->FT_DTFIMNT),;
					"FT_MALQCOF"    : (cAlias)->FT_MALQCOF,;
					"FT_MVALCOF"    : (cAlias)->FT_MVALCOF,;
					"FT_DESCZFR"    : (cAlias)->FT_DESCZFR,;
					"FT_MALQPIS"    : (cAlias)->FT_MALQPIS,;
					"FT_MVALPIS"    : (cAlias)->FT_MVALPIS,;
					"FT_ATIVCPB"    : (cAlias)->FT_ATIVCPB,;
					"FT_BASECPB"    : (cAlias)->FT_BASECPB,;
					"FT_SERSAT"     : (cAlias)->FT_SERSAT,;
					"FT_ALIQCPB"    : (cAlias)->FT_ALIQCPB,;
					"FT_VALCPB"     : (cAlias)->FT_VALCPB,;
					"FT_TES"        : (cAlias)->FT_TES;
					})

				(cAlias)->(dbSkip())

			enddo

			(cAlias)->(DBCloseArea())

		next nFilial

	Endif

	freeobj(oFisTools)
	freeObj(oPrepare)
	freeobj(oFiltQry)
	aSize(aFiliais,0)
	aFiliais := Nil

	If oJsonCTE <> Nil
		LimpaJson()
	EndIf

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author eduardo.melo
@since 27/10/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class efdSFTIndSmartviewBusinessObject

	self:addProperty("F4_ATUATF","Atualiza Ativo Fixo","string","Atual.Ativo","F4_ATUATF","S=Sim;N=Nao")
	self:addProperty("F4_TPREG","Tipo de Regime","string","Tp Reg","F4_TPREG","1=Não Cumulativo;2=Cumulativo;3=Ambos")
	self:addProperty("FT_FILIAL","Filial","string","Filial","FT_FILIAL")
	self:addProperty("FT_ENTRADA","Data Entrada NF","date","Data Entrada","FT_ENTRADA")
	self:addProperty("FT_EMISSAO","Data Emissão NF","date","Data Emissão","FT_EMISSAO")
	self:addProperty("FT_NFISCAL","Numero Documento Fiscal","string","Doc. Fiscal","FT_NFISCAL")
	self:addProperty("FT_SERIE","Série do Dcomento Fiscal","string","Série NF","FT_SERIE")
	self:addProperty("FT_CLIEFOR","Cliente/Fornecedor","string","Cli/Forn.","FT_CLIEFOR")
	self:addProperty("FT_LOJA","Codigo Loja Client/Forn","string","Codigo loja","FT_LOJA")
	self:addProperty("FT_ESTADO","Estado Referencia","string","Estado Ref","FT_ESTADO")
	self:addProperty("FT_CFOP","Codigo Fiscal Oper/Prest","string","Cod. Fiscal","FT_CFOP")
	self:addProperty("FT_VALCONT","Valor Contábil","number","Vlr Contábil","FT_VALCONT")
	self:addProperty("FT_VALIPI","Valor do IPI","number","Valor IPI","FT_VALIPI")
	self:addProperty("FT_ICMSRET","Valor do ICMS Retido ST","number","Vlr ICMS Ret","FT_ICMSRET")
	self:addProperty("FT_OBSERV","Menssagem Livro Fiscal","string","Obs Liv. Fis","FT_OBSERV")
	self:addProperty("FT_TIPO","Tipo Lançamento","string","Tipo Lanc","FT_TIPO","L=Nota em Lote;S=Nota com ISS;B=Beneficiamento;D=Devolucao")
	self:addProperty("FT_OBSSOL","Valor Solidário na Obs","number","Vlr Sol Obs","FT_OBSSOL")
	self:addProperty("FT_SOLTRIB","Vlr ICMS Solidário Tribut","number","ICMS Sol Tri","FT_SOLTRIB")
	self:addProperty("FT_DTCANC","Data Cancelamento NF","date","Data Cancel","FT_DTCANC")
	self:addProperty("FT_ESPECIE","Espécie da NF","string","Espécie NF","FT_ESPECIE")
	self:addProperty("FT_CREDST","Crédito/Débito ST","string","Crd/Deb ST","FT_CREDST","1=Credita;2=Não se aplica;3=Debita")
	self:addProperty("FT_CONTA","Número da Conta Contábil","string","Conta Contáb","FT_CONTA")
	self:addProperty("FT_PRODUTO","Código do Produto","string","Cod. Produto","FT_PRODUTO")
	self:addProperty("FT_TIPOMOV","Tipo do Movimento","string","Tipo Mov.","FT_TIPOMOV","E=Entrada;S=Saída")
	self:addProperty("FT_ITEM","Código do Item","string","Código Item","FT_ITEM")
	self:addProperty("FT_ISENRET","ST Retido Isento","number","Retido Isent","FT_ISENRET")
	self:addProperty("FT_OUTRRET","ST Retido Outros","number","Retido Outro","FT_OUTRRET")
	self:addProperty("FT_QUANT","Quantidade do item","number","Quantidade","FT_QUANT")
	self:addProperty("FT_DESCONT","Valor do Desconto item","number","Vlr Desconto","FT_DESCONT")
	self:addProperty("FT_TOTAL","Vlr Total Item","number","Vlr Total","FT_TOTAL")
	self:addProperty("FT_BASEPIS","Base calculo PIS","number","Base PIS","FT_BASEPIS")
	self:addProperty("FT_ALIQPIS","Alíquota de PIS","number","Aliq. PIS","FT_ALIQPIS")
	self:addProperty("FT_VALPIS","Valor de PIS","number","Valor PIS","FT_VALPIS")
	self:addProperty("FT_BASECOF","Base de COFINS","number","Base COFINS","FT_BASECOF")
	self:addProperty("FT_ALIQCOF","Alíquota de COFINS","number","Aliq. COFINS","FT_ALIQCOF")
	self:addProperty("FT_VALCOF","Valor de COFINS","number","Valor COFINS","FT_VALCOF")
	self:addProperty("FT_BASEPS3","Base Pis ST","number","Bs. Pis ST","FT_BASEPS3")
	self:addProperty("FT_ALIQPS3","Alíquota Pis ST","number","Al. Pis ST","FT_ALIQPS3")
	self:addProperty("FT_VALPS3","Valor Pis ST","number","Vl. Pis ST","FT_VALPS3")
	self:addProperty("FT_BASECF3","Base Cofins ST","number","Bs. Cof ST","FT_BASECF3")
	self:addProperty("FT_ALIQCF3","Alíquota Cofins ST","number","Al. Cof ST","FT_ALIQCF3")
	self:addProperty("FT_VALCF3","Valor Cofins ST","number","Vl. Cof ST","FT_VALCF3")
	self:addProperty("FT_NFELETR","Nota Fiscal Eletrônica","string","NF Eletr.","FT_NFELETR")
	self:addProperty("FT_CSTPIS","CST Pis","string","CST Pis","FT_CSTPIS")
	self:addProperty("FT_CSTCOF","CST Cofins","string","CST COF","FT_CSTCOF")
	self:addProperty("FT_CODBCC","Cod.BC Cred.","string","Cod.BC Cred.","FT_CODBCC")
	self:addProperty("FT_PAUTPIS","Valor do PIS de Pauta","number","PIS Pauta","FT_PAUTPIS")
	self:addProperty("FT_PAUTCOF","Valor do Cofins de Pauta","number","Cofins Pauta","FT_PAUTCOF")
	self:addProperty("FT_TNATREC","Tabela Nat. da Receita","string","Tab.Nat.Rec","FT_TNATREC")
	self:addProperty("FT_CNATREC","Cod. Natureza da Receita","string","Cod.Nat.Rec.","FT_CNATREC")
	self:addProperty("FT_GRUPONC","Grupo Natureza da Receita","string","Grp.Nat.Rec.","FT_GRUPONC")
	self:addProperty("FT_DTFIMNT","Data Fim Nat. Receita","date","Dt.Fim N. R","FT_DTFIMNT")
	self:addProperty("FT_MALQCOF","Aliquota COFINS Majorada","number","Aliq. COF M.","FT_MALQCOF")
	self:addProperty("FT_MVALCOF","Valor COFINS Majorada","number","Val. COF M.","FT_MVALCOF")
	self:addProperty("FT_DESCZFR","Desc. Zona Franca Manaus","number","Desc.ZFM","FT_DESCZFR")
	self:addProperty("FT_MALQPIS","Alíquota PIS Majorada","number","Aliq. PIS M.","FT_MALQPIS")
	self:addProperty("FT_MVALPIS","Valor do PIS Majorada","number","Val. PIS M.","FT_MVALPIS")
	self:addProperty("FT_ATIVCPB","Cód. Atividade da CPRB","string","Cód. Ativ.","FT_ATIVCPB")
	self:addProperty("FT_BASECPB","Base da CPRB","number","Base CPRB","FT_BASECPB")
	self:addProperty("FT_SERSAT","Série do SAT","string","Sér.SAT","FT_SERSAT")
	self:addProperty("FT_ALIQCPB","Valor da CPRB","number","Alíquota CPR","FT_ALIQCPB")
	self:addProperty("FT_VALCPB","Valor da CPRB","number","Valor CPRB","FT_VALCPB")
	self:addProperty("FT_TES","Tipo de entrada / saída","string","Tipo E/S","FT_TES")

return self:oSchema

/*/{Protheus.doc} vldMsgObserv
	Valida a mensagem que será exibida no FT_OBSERV
	@type  Static Function
	@author Matheus Bispo
	@since 25/04/2025
	@version 12.1.2410
	@param cFTObserv, character, observação vindo da tabela SFT
	@param cTipoMov, character, tipo de movimento (entrada ou saida)
	@param cEspecie, character, especie da nota
	@param cNotaOrigem, character, nota de origem (FT_NFORI)
	@param cSeekDT6, character, chave para fazer seek na DT6
	@return cObserv, character, Observação que será apresentada no relatorio
	@see (links_or_references)
/*/
static function vldMsgObserv(cFTObserv as character, cTipoMov as character, cEspecie as character, cNotaOrigem as character, cSeekDT6 as character, cFilAtu as character)
	Local cObserv := cFTObserv as character

	if !Empty(cNotaOrigem) .And. ((cTipoMov == "S" .And. cEspecie == "BPE") .Or. (cTipoMov == "E" .And. cEspecie == "CTE"))
		cObserv := cEspecie + " SUBST."+Alltrim(cNotaOrigem)
	elseIf cTipoMov == "S" .And. cEspecie == "CTE" .And. vldJsonDT6(cSeekDT6, cFilAtu)
		cObserv := "CTE SUBST."+Alltrim(oJsonCTE[cSeekDT6]['nota'])
	endIf

return cObserv

/*/{Protheus.doc} vldJsonDT6
	Valida Json da DT6 e faz cache para evitar seek
	@type  Static Function
	@author Matheus Bispo
	@since 25/04/2025
	@version 12.1.2410
	@param cSeekDT6, character, chave para seek na tabela DT6
	@return lRet, logical, informa se achou o documento de origem de DT6
/*/
static function vldJsonDT6(cSeekDT6 as character, cFilAtu as character)
	Local lRet := .T. as logical

	If oJsonCTE['filial'] != cFilAtu
		LimpaJson()
		oJsonCTE := JsonObject():new()
		oJsonCTE['filial'] := cFilAtu
	EndIf

	if !oJsonCTE:hasProperty(cSeekDT6)
		oJsonCTE[cSeekDT6] := JsonObject():New()
		oJsonCTE[cSeekDT6]['lSeekOri'] := DT6->(MsSeek(cSeekDT6))
		oJsonCTE[cSeekDT6]['nota'] := DT6->(DT6_DOCDCO)
	endif

	lRet := oJsonCTE[cSeekDT6]['lSeekOri'] .And. !Empty(oJsonCTE[cSeekDT6]['nota'])
return lRet

/*/{Protheus.doc} LimpaJson
	Função para liberar memória do Objeto Json e seus atributos utilizados.
	@type  Static Function
	@author Matheus Bispo
	@since 25/04/2025
	@version 12.1.2410
/*/
static function LimpaJson()
	Local aArray := oJsonCTE:getnames() as array
	Local nLen := Len(aArray) as numeric
	Local nX := 1 as numeric

	For nX := 1 to nLen
		oJsonCTE[aArray[nX]] := Nil
	Next

	FreeObj(oJsonCTE)

return
