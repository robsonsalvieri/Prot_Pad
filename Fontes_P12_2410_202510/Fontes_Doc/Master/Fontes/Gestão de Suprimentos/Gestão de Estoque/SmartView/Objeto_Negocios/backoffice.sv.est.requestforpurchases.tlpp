#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.est.requestforpurchases.ch"
#include "totvs.framework.treports.integratedprovider.th"

#define SX1GRUPO "ESTT015"

//NAMESPACE
namespace totvs.protheus.backoffice.est.requestforpurchases.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

//ANNOTATION
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SCP", customTables="SCP", name="Posição das Solicitações ao Armazém", country="ALL")

//-------------------------------------------------------------------
/*{Protheus.doc} RequestForPurchasesSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Posição das Solicitações ao Armazém

@author Breno Nogueira
@since 08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class RequestForPurchasesSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object

	Protected Method oNGetSchema()

	Protected data aFields	as array
	Protected data jItems	as json
	Protected data cWhere	as character
	Protected data lExistPergunte	as logical

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} RequestForPurchasesSmartViewBusinessObject
Método de instância da classe

@return object: self
@author Breno Nogueira
@since 08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class RequestForPurchasesSmartViewBusinessObject

	_Super:new()

	//Define a Área
	self:appendArea( STR0001 )// "Estoque/Custos"

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 ) // "Posição das Solicitações ao Armazém"

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 ) // "Este objeto de negocio apresenta uma relação da Posição das Solicitações ao Armazém"
	
	//Define o conjunto de perguntas e faz o teste se existe
	self:lExistPergunte := self:setPergunte( SX1GRUPO )
	If !self:lExistPergunte
		self:setErrorStatus(400,STR0005,i18n(STR0006,{SX1GRUPO}))//O Grupo de perguntas #1 não existe		
		FWLogMsg('WARN', , 'SmartView', , , , i18n(STR0006,{SX1GRUPO}), , , )//O Grupo de perguntas #1 não existe
	EndIf

	//Indica que o LookUp será do tipo padrão LookUp
	self:setIsLookUp(.T.)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do Smart View

@return object: self:oData

@author Breno Nogueira
@since 08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData( nPage as Numeric, oFilter as object) as object Class RequestForPurchasesSmartViewBusinessObject
	Local aCustom	as Array
	Local cQuery	as Character
	Local cAlias	as Character
	Local cFields	as Character
	Local cCustom	as Character
	Local nX		as Numeric
	Local nSaldo	as Numeric
	Local jParams	as Json
	Local dDataDe	as Date
	Local dDataAte	as Date
	Local oQuery	as object
	Local nFil		as numeric
	Local cBkpFil	as Character
	Local cFiltro	as character

	jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'])

	cFiltro := " AND "

	//Valida o pergunte
	If !self:lExistPergunte
		return self:oData
	Endif

	//Se tiver custom
	cCustom := self:getSQLFields(.f.,nil,.t.)
	if !empty(cCustom)	
		aCustom := StrTokArr(cCustom,",")
		For nX := 1 to len(aCustom)
			aadd(self:aFields,aCustom[nX])		
		Next
	EndIf
	
	cFields := StrTran(ArrTokStr(self:aFields),"|",",")	

	//--------------------------------------------------------------------------
	// Variaveis utilizadas para parametros no grupo de pergunta ESTT015
	//--------------------------------------------------------------------------
	//
	// MV_PAR01 - Solicitação inicial
	// MV_PAR02 - Solicitação Final
	// MV_PAR03 - Data Emissao Inicial
	// MV_PAR04 - Data Emissao Final


	dDataDe  := FwDateTimeToLocal( jParams['MV_PAR03'][1] )[1]
	dDataAte := FwDateTimeToLocal( jParams['MV_PAR04'][1] )[1]


	cQuery := " SELECT ? "
	cQuery += " FROM " + RetSQLName("SCP") + " SCP "
	cQuery += " WHERE SCP.CP_FILIAL = ? "
	cQuery += " AND SCP.CP_NUM >= ? "
	cQuery += " AND SCP.CP_NUM <= ? "
	cQuery += " AND SCP.CP_EMISSAO >= ? "
	cQuery += " AND SCP.CP_EMISSAO <= ? "
	cQuery += " AND  SCP.D_E_L_E_T_ = ? "

	cQuery += self:cWhere

	//Os filtros serão setados na interface do novo SmartView
	If !(oFilter:hasFilter())
		cFiltro := ""
	Endif

	cQuery += " ? "

	cQuery := changeQuery(cQuery)
	oQuery := FwExecStatement():New(cQuery)

	cBkpFil := cFilAnt

	//Colocar while ou for de filiais aqui
	For nFil :=1 to len(self:aFils)
		cFilAnt	:= self:aFils[nFil]
		nSaldo	:= 0

		//Binding
		oQuery:setUnsafe(1,cFields)
		oQuery:setString(2,FWxFilial("SCP"))
		oQuery:setString(3,jParams['MV_PAR01'][1])
		oQuery:setString(4,jParams['MV_PAR02'][1])
		oQuery:setString(5,Dtos(dDataDe))
		oQuery:setString(6,Dtos(dDataAte))
		oQuery:setString(7," ")
		oQuery:setUnsafe(8,cFiltro + oFilter:getSQLExpression())

		cAlias := oQuery:OpenAlias()

		(cAlias)->(dbGoTop())

		While !(cAlias)->(Eof())

			self:jItems := JsonObject():New()

			For nX := 1 To Len(self:aFields)			
				if FWSX3Util():GetFieldType(self:aFields[nX]) == "D" //Campo tipo data tem um tratamento diferente para envio
					If !Empty( (cAlias)->&(self:aFields[nX]) )
						self:jItems[self:aFields[nX]] := totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->&(self:aFields[nX])))
					EndIf
				Else
					self:jItems[self:aFields[nX]] := (cAlias)->&(self:aFields[nX])
				EndIf
			Next nX		

			nSaldo := (cAlias)->CP_QUANT - (cAlias)->CP_QUJE

			self:jItems["SALDO"] := nSaldo

			nSaldo := 0

			self:processData()
			self:oData:appendData(self:jItems)

			(cAlias)->(DBSkip())
		EndDo

		(cAlias)->(DBCloseArea())
	Next

	cFilAnt := cBkpFil

	oQuery:Destroy()

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} oNGetSchema
Retorna a estrutura dos campos

@return object: self:oSchema
@author Breno Nogueira
@since 08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method oNGetSchema() Class RequestForPurchasesSmartViewBusinessObject
	
	self:aFields :={"CP_FILIAL","CP_NUM","CP_ITEM","CP_PRODUTO","CP_DESCRI","CP_EMISSAO","CP_QUANT","CP_QUJE","CP_SALBLQ","CP_CC","CP_CODSOLI","CP_SOLICIT","CP_LOCAL"}
	self:AliasToSchema("SCP", self:aFields)

	self:addProperty( "SALDO"	, STR0004	, "number", STR0004	, "SALDO" 	) //"Saldo"

Return
