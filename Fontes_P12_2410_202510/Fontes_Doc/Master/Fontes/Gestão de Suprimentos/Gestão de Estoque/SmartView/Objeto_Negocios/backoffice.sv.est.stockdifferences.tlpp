#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.est.stockdifferences.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE SX1GRUPO "ESTT016"

using namespace totvs.protheus.backoffice.est.smartView.integratedProvider
namespace totvs.protheus.backoffice.est.stockdifferences.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SB1,SB2",name="Diferenças de Estoque", country="ALL")

//------------------------------------------------------------------- 
/*{Protheus.doc} StockDifferencesSmartViewBusinessObject 
Classe para criação do Objeto de Negócio de Diferenças de Estoque
@type Class
@author Breno Nogueira
@since 08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class StockDifferencesSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object

	Protected Method oNGetSchema()
	Protected data aFields	as array
	Protected data jItems	as json
	Protected data cWhere	as character
	Protected data lExistPergunte as Logical

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} StockDifferencesSmartViewBusinessObject
Método de instância da classe

@return object: self
@author Breno Nogueira
@since 08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class StockDifferencesSmartViewBusinessObject

	LOCAL cMsgSX1 as Character

	_Super:new()

	//Define a Área
	self:appendArea( STR0001 )// "Estoque/Custos"

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 ) // "Diferenças de Estoque"

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 ) // "Este relatório apresenta uma relação das Diferenças de Estoque"

	//Grupo de Perguntas
	self:lExistPergunte := self:SetPergunte(SX1GRUPO)
	If !self:lExistPergunte
		cMsgSX1 := OemToAnsi(I18N(STR0007,{SX1GRUPO})) //#Grupo de perguntas #1[SX1GRUPO]# nao encontrado. Verifique na tabela SX1, o grupo de perguntas informado
		self:setErrorStatus(400,STR0008,cMsgSX1)		//#Sem Pergunte
		FwLogMsg("WARN",, "SmartView ESTSV007",,,,cMsgSX1,,,)
	EndIf

	//Indica que o LookUp será do tipo padrão LookUp
	self:setIsLookUp(.T.)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author Breno Nogueira
@since 08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData( nPage as Numeric, oFilter as object) as object Class StockDifferencesSmartViewBusinessObject
	Local cFiltro       as Character
	Local cQuery     	as Character
	Local cAlias     	as Character
	Local nX         	as Numeric
	Local jParams    	as Json
	Local cFields    	as Character
	Local nF         	as Numeric
	Local nSaldoCalc 	as Numeric
	Local nSaldoAtual	as Numeric
	Local nDiferenca 	as Numeric
	Local dDataRef 		as Date
	Local aSaldo 		as Array
	Local oQuery		as object
	Local nFil          as Numeric 
	Local cBkpFil       as Character

	If !self:lExistPergunte
		Return self:oData
	EndIf

	self:setPageSize(50) //Define a quantidade máxima por página (Default 100)
	cAlias   := getNextAlias() // Define nome do alias disponível
	jParams  := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'])

	dDataRef := Ctod("31/12/49","ddmmyy") //dDataBase
	cFiltro  := " AND "
	For nF := 1 To Len( self:aFields )
		cFields += self:aFields[ nF ][ 01 ] + ","
	Next nF

	cFields := SubStr( cFields , 1 , Len( cFields ) -1 )

	//--------------------------------------------------------------------------
	// Variaveis utilizadas para parametros no grupo de pergunta ESTT012
	//--------------------------------------------------------------------------
	//
	// MV_PAR01 - Produto De?
	// MV_PAR02 - Produto Até?
	// MV_PAR03 - Tipo De?
	// MV_PAR04 - Tipo Ate?
	// MV_PAR05 - Armazém De?
	// MV_PAR06 - Armazém Até?
	// MV_PAR07 - Grupo De?
	// MV_PAR08 - Grupo Ate?

	cQuery := " Select B2_FILIAL, "
	cQuery += " B2_COD, "
	cQuery += " B2_LOCAL, "
	cQuery += " B2_QATU, ? "
	cQuery += " FROM " + RetSQLName("SB1") + " SB1 "
	cQuery += " JOIN " + RetSQLName("SB2") + " SB2 "
	cQuery += " ON B2_FILIAL = ? "
	cQuery += " AND B2_COD = B1_COD "
	cQuery += " AND B2_LOCAL >= ? "
	cQuery += " AND B2_LOCAL <= ? "
	cQuery += " AND SB2.D_E_L_E_T_ = ? "
	cQuery += " WHERE B1_FILIAL = ? "
	cQuery += " AND B1_COD   >= ? "
	cQuery += " AND B1_COD   <= ? "
	cQuery += " AND B1_TIPO  >= ? "
	cQuery += " AND B1_TIPO  <= ? "
	cQuery += " AND B1_GRUPO >= ? "
	cQuery += " AND B1_GRUPO <= ? "
	cQuery += " AND SB1.D_E_L_E_T_ = ? "

	//Os filtros serão setados na interface do Smart View
	If !(oFilter:hasFilter())
		cFiltro := ""  
	Endif
	cQuery += " ? "
	cQuery += self:cWhere
	cQuery += " ORDER BY B1_COD, B1_LOCPAD "

	cQuery := changeQuery(cQuery)
	oQuery := FwExecStatement():New(cQuery)

	cBkpFil := cFilant 

	//Colocar while ou for de filiais aqui 
	For nFil :=1 to len(self:aFils)

		cFilAnt     := self:aFils[nFil]

		//Seta os campos
		oQuery:SetUnsafe(1,cFields)
		oQuery:SetString(2,FWxFilial("SB2"))
		oQuery:SetString(3,jParams['MV_PAR05'][1])
		oQuery:SetString(4,jParams['MV_PAR06'][1])
		oQuery:SetString(5,' ')
		oQuery:SetString(6,FWxFilial("SB1"))
		oQuery:SetString(7,jParams['MV_PAR01'][1])
		oQuery:SetString(8,jParams['MV_PAR02'][1])
		oQuery:SetString(9,jParams['MV_PAR03'][1])
		oQuery:SetString(10,jParams['MV_PAR04'][1])
		oQuery:SetString(11,jParams['MV_PAR07'][1])
		oQuery:SetString(12,jParams['MV_PAR08'][1])
		oQuery:SetString(13,' ')
		oQuery:SetUnsafe(14,cFiltro + oFilter:getSQLExpression())
		//Retorna a query com os parâmetros já tratados e substituídos
		cAlias := oQuery:OpenAlias()

		While !(cAlias)->(Eof())

			self:jItems := JsonObject():New()

			For nX := 1 To Len(self:aFields)
				self:jItems[self:aFields[nX][ 01 ]] := (cAlias)->&(self:aFields[nX][ 01 ])
			Next nX

			aSaldo      := CalcEst( (cAlias)->B2_COD,(cAlias)->B2_LOCAL,dDataRef+1,(cAlias)->B2_FILIAL )
			nSaldoCalc  := aSaldo[1]
			nSaldoAtual := (cAlias)->B2_QATU
			nDiferenca  := ABS(nSaldoCalc - nSaldoAtual)

			If QtdComp(nDiferenca) <> QtdComp(0)

				self:jItems["SALDOCALC"] := nSaldoCalc
				self:jItems["SALDO"]     := nSaldoAtual
				self:jItems["DIFERENCA"] := nDiferenca
				self:processData()
				self:oData:appendData(self:jItems)

				nSaldoCalc := 0
				nSaldoAtual := 0
				nDiferenca := 0

			EndIf

			(cAlias)->(DBSkip())

		EndDo

		(cAlias)->(DBCloseArea())

	Next
	
	cFilAnt := cBkpFil
	oQuery:Destroy()

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema
@author Breno Nogueira
@since 08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method oNGetSchema() Class StockDifferencesSmartViewBusinessObject

	Local aFieldsCol as Array
	Local aFieldsSB2 as Array
	Local aStrAux    as Array
	Local aStrSB2    as Array
	Local nC         as Numeric
	Local nY		 as Numeric

	aFieldsCol   := {}
	aFieldsSB2   := {}
	self:aFields := {}

	aStrAux      := SB1->( DBStruct() )
	aStrSB2      := SB2->( DBStruct() )

	For nC := 1 To Len( aStrAux )
		If aStrAux[ nC , 02 ] != "M" .And. (Alltrim( aStrAux[ nC , 01 ] ) $ "B1_COD|B1_DESC|B1_TIPO|B1_GRUPO|B1_UM")
			aAdd( self:aFields , { Alltrim( aStrAux[ nC , 01 ] ) , aStrAux[ nC , 02 ] } )
		EndIf
	Next nC

	For nY := 1 To Len( aStrSB2 )
		If aStrSB2[ nY , 02 ] != "M" .And. (Alltrim( aStrSB2[ nY , 01 ] ) $ "B2_FILIAL|B2_LOCAL")
			aAdd( self:aFields , { Alltrim( aStrSB2[ nY , 01 ] ) , aStrSB2[ nY , 02 ] } )
		EndIf
	Next nY

	aadd(aFieldsCol, "B1_COD"   )
	aadd(aFieldsCol, "B1_DESC"  )
	aadd(aFieldsCol, "B1_TIPO"  )
	aadd(aFieldsCol, "B1_GRUPO" )
	aadd(aFieldsCol, "B1_UM"    )
	aadd(aFieldsSB2, "B2_FILIAL")
	aadd(aFieldsSB2, "B2_LOCAL")

	self:AliasToSchema("SB1", aFieldsCol)
	self:AliasToSchema("SB2", aFieldsSB2)

	self:oSchema:addProperty( "SALDOCALC" , STR0004 , "number", STR0004 , "SALDOCALC" ) //"Saldo Calculado"
	self:oSchema:addProperty( "SALDO"     , STR0005 , "number", STR0005 , "SALDOATUA" ) //"Saldo Atual"
	self:oSchema:addProperty( "DIFERENCA" , STR0006 , "number", STR0006 , "DIFERENCA" ) //"Diferenca"

Return
