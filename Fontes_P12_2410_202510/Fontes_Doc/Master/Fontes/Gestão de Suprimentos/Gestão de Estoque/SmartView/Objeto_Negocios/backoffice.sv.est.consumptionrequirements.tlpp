#include "TOTVS.ch"
#include "backoffice.sv.est.consumptionrequirements.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE SX1GRUPO "ESTT017"

namespace totvs.protheus.backoffice.est.consumptionrequirements.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SD3",name="Requisições para Consumo", country="ALL")

//-------------------------------------------------------------------
/*{Protheus.doc} ConsumptionRequirementsSmartViewBusinessObject 
Classe para criação do Objeto de Negócio de Requisições para Consumo
@type Class
@author Breno Nogueira
@since 08/2023 
@version 1.0
*/
//-------------------------------------------------------------------
Class ConsumptionRequirementsSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object

	Protected Method oNGetSchema()

	Protected data aFields	as array
	Protected data jItems	as json
	Protected data lExistPergunte as Logical

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} ConsumptionRequirementsSmartViewBusinessObject
Método de instáncia da classe

@return object: self
@author Breno Nogueira
@since 08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class ConsumptionRequirementsSmartViewBusinessObject

	LOCAL cMsgSX1 as Character

	_Super:new()

	//Define a area
	self:appendArea( STR0001 )// "Estoque/Custos"

	//Define o nome do Objeto de Negocio
	self:setDisplayName( STR0002 ) // "Requisições para Consumo"

	//Define a descrição do Objeto de Negocio
	self:setDescription( STR0003 ) // "Este relatório apresenta uma relação das Requisições para Consumo"

	//Grupo de Perguntas
	self:lExistPergunte := self:SetPergunte(SX1GRUPO)
	If !self:lExistPergunte
   	cMsgSX1 := OemToAnsi(I18N(STR0009,{SX1GRUPO})) //#Grupo de perguntas #1[SX1GRUPO]# nao encontrado. Verifique na tabela SX1, o grupo de perguntas informado
	   self:setErrorStatus(400,STR0010,cMsgSX1)		//#Sem Pergunte 
	   FwLogMsg("WARN",, "SmartView ESTSV003",,,,cMsgSX1,,,)
	EndIf 

	//Indica que o LookUp será do tipo padrão LookUp
	self:setIsLookUp(.T.)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negocio

@param nPage, numerico, indica a pagina atual do relatorio
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author Breno Nogueira
@since 08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData( nPage as Numeric, oFilter as object) as object Class ConsumptionRequirementsSmartViewBusinessObject
	Local aAllFields as Array
	Local nCustUni    as Numeric
	Local nQuant      as Numeric
	Local nCustUniT   as Numeric
	Local nCustTotT   as Numeric
	Local nQualMoeda  as Numeric
	Local nX          as Numeric
	Local cDescCC     as Character
	Local cDescPrd    as Character
	Local cCampoCus   as Character
	Local cProd       as Character
	Local cD3Local    as Character
	Local cCCAnt      as Character
	Local cGrupAnt    as Character
	Local CCodProduto as Character
	Local cCodCentroC as Character
	Local cCodCF      as Character
	Local cFiltro     as Character
	Local cQuery      as Character
	Local cAlias      as Character
	Local dDataDe     as Date
	Local dDataAte    as Date
	Local jParams     as Json
	Local lVeiculo    as logical
	Local oQuery      as Object
	Local nFil			as numeric
	Local cBkpFil		as Character

	If !self:lExistPergunte
	   Return self:oData 
	EndIf 

	jParams    := oFilter:getParameters() //metodo para retorno do json dos parámetros

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'])

	cBkpFil := cFilAnt

	aAllFields := self:getArrayFields()

	lVeiculo   := Upper(GetMv("MV_VEICULO"))=="S"
	dDataDe    := FwDateTimeToLocal( jParams['MV_PAR09'][1] )[1]
	dDataAte   := FwDateTimeToLocal( jParams['MV_PAR10'][1] )[1]
	nQualMoeda := jParams['MV_PAR15'][1]
	If nQualMoeda == 0 
	   nQualMoeda := 1 
	EndIf
	cCampoCus  := "D3_CUSTO"+Alltrim(str(nQualMoeda))
	cFiltro  := " AND "

	cQuery := " SELECT D3_CC,D3_COD,D3_GRUPO,D3_UM,D3_EMISSAO,D3_LOCAL,D3_QUANT,D3_OP,D3_CF,D3_TM,D3_FILIAL,D3_CUSTO1,D3_CUSTO2,D3_CUSTO3,D3_CUSTO4,D3_CUSTO5 "
	cQuery += " FROM " + RetSQLName("SD3") + " SD3 "
	cQuery += " WHERE SD3.D3_FILIAL = ? "
	cQuery += " AND SD3.D3_COD >= ? "
	cQuery += " AND SD3.D3_COD <= ? "
	cQuery += " AND SD3.D3_LOCAL >= ? "
	cQuery += " AND SD3.D3_LOCAL <= ? "
	cQuery += " AND SD3.D3_EMISSAO >= ? "
	cQuery += " AND SD3.D3_EMISSAO <= ? "
	cQuery += " AND SD3.D3_GRUPO >= ? "
	cQuery += " AND SD3.D3_GRUPO <= ? "
	cQuery += " AND SD3.D3_CONTA >= ? "
	cQuery += " AND SD3.D3_CONTA <= ? "
	cQuery += " AND SD3.D3_CC >= ? "
	cQuery += " AND SD3.D3_CC <= ? "
	cQuery += " AND SD3.D3_ESTORNO = ? "
	cQuery += " AND SD3.D3_TIPO  >= ? "
	cQuery += " AND SD3.D3_TIPO  <= ? "
	cQuery += " AND D3_CF <> ? "
	cQuery += " AND D3_CF <> ? "

	//Os filtros serão setados na interface do Smart View
	If !(oFilter:hasFilter())
		cFiltro := ""  
	Endif
	cQuery += " ? "
	cQuery += " AND SD3.D_E_L_E_T_ = ? "

	cQuery := changeQuery(cQuery)
	oQuery := FwExecStatement():New(cQuery)

	For nFil := 1 To len(self:aFils)
		cFilAnt	:= self:aFils[nFil]
		nQuant	:= 0

		//Binding
		oQuery:setString(1,FWxFilial("SD3"))
		oQuery:setString(2,jParams['MV_PAR13'][1])	
		oQuery:setString(3,jParams['MV_PAR14'][1])
		oQuery:setString(4,jParams['MV_PAR03'][1])
		oQuery:setString(5,jParams['MV_PAR04'][1])
		oQuery:setString(6,Dtos(dDataDe))
		oQuery:setString(7,Dtos(dDataAte))
		oQuery:setString(8,jParams['MV_PAR11'][1])
		oQuery:setString(9,jParams['MV_PAR12'][1])
		oQuery:setString(10,jParams['MV_PAR07'][1])
		oQuery:setString(11,jParams['MV_PAR08'][1])
		oQuery:setString(12,jParams['MV_PAR01'][1])
		oQuery:setString(13,jParams['MV_PAR02'][1])
		oQuery:setString(14,' ')
		oQuery:setString(15,jParams['MV_PAR05'][1])
		oQuery:setString(16,jParams['MV_PAR06'][1])
		oQuery:setString(17,'RE4')
		oQuery:setString(18,'DE4')
		oQuery:setUnsafe(19,cFiltro + oFilter:getSQLExpression())
		oQuery:setString(20,' ')

		cAlias := oQuery:OpenAlias()

		(cAlias)->(dbGoTop())

		While !(cAlias)->(Eof())

			self:jItems := JsonObject():New()

			For nX := 1 To Len(aAllFields)        	
				if  FWSX3Util():GetFieldType( aAllFields[nX] ) == "D"
					If !Empty( (cAlias)->&(aAllFields[nX]))
						self:jItems[aAllFields[nX]] := totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->&(aAllFields[nX])))			       			
					endif
				else
					self:jItems[aAllFields[nX]] := (cAlias)->&(aAllFields[nX])
				EndIf			
			Next nX

			cProd	 := (cAlias)->(D3_COD)
			cD3Local := (cAlias)->(D3_LOCAL)
			cCCAnt   := (cAlias)->(D3_CC)
			cGrupAnt := (cAlias)->(D3_GRUPO)
			CCodProduto := (cAlias)->(D3_COD)
			cCodCentroC := (cAlias)->(D3_CC)
			cCodCF := (cAlias)->(D3_CF)
			cDescCC  := Posicione('CTT',1,FWxFilial("CTT")+cCodCentroC,"CTT_DESC01")
			cDescPrd := Posicione('SB1',1,FWxFilial("SB1")+CCodProduto,"B1_DESC")

			Store 0 To nCustUniT,nCustTotT,nQuant,nCustUni

			While (cAlias)->(!Eof()) .And. (cAlias)->(D3_FILIAL)+;
					(cAlias)->(D3_CC)+;
					(cAlias)->(D3_GRUPO)+;
					(cAlias)->(D3_COD)+;
					(cAlias)->(D3_LOCAL) == FWxFilial("SD3")+cCCAnt+cGrupAnt+cProd+cD3Local

				If SubStr((cAlias)->D3_CF,2,1) != "E"
					(cAlias)->(DBSkip())
					Loop
				EndIf

				If SubStr((cAlias)->D3_OP,7,2) # "OS" .And. !Empty((cAlias)->D3_OP)
					(cAlias)->(DBSkip())
					Loop
				EndIf

				If (cAlias)->(D3_TM) <  '500'
					nQuant   -= (cAlias)->(D3_QUANT)
					nCustUni -= (cAlias)->(&(cCampoCus))
				Else
					nQuant   += (cAlias)->(D3_QUANT)
					nCustUni += (cAlias)->(&(cCampoCus))
				EndIf

				nCustUniT := nCustUni/nQuant
				nCustTotT := nQuant * nCustUniT

				(cAlias)->(DBSkip())
			EndDo

			If nQuant <> 0
				self:jItems["FILIAL"]	:= cFilAnt
				self:jItems["QUANT"]    := nQuant
				self:jItems["DESCCC"]   := cDescCC
				self:jItems["DESCPROD"] := cDescPrd
				self:jItems["CUSTOUNI"] := nCustUniT
				self:jItems["CUSTOTOT"] := nCustTotT

				self:processData()
				self:oData:appendData(self:jItems)
			Endif
		EndDo

		(cAlias)->(DBCloseArea())
	Next

	cFilAnt := cBkpFil

	oQuery:Destroy()

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} oNGetSchema
Retorna a estrutura dos campos

@return object: self:oSchema
@author Breno Nogueira
@since 08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method oNGetSchema() Class ConsumptionRequirementsSmartViewBusinessObject
	self:AliasToSchema("SD3", {"D3_CC","D3_COD","D3_GRUPO","D3_UM","D3_EMISSAO"})

	// ADICIONAR COLUNAS CUSTOMIZADAS
	self:oSchema:addProperty( "FILIAL"   , STR0011 , "string", STR0011 , "FILIAL"   ) //"Filial"
	self:oSchema:addProperty( "DESCCC"   , STR0004 , "string", STR0004 , "DESCCC"   ) //"Descrição Centro de Custo"
	self:oSchema:addProperty( "DESCPROD" , STR0005 , "string", STR0005 , "DESCPROD" ) //"Descrição Produto"
	self:oSchema:addProperty( "QUANT"    , STR0008 , "number", STR0008 , "QUANT"    ) //"Quanqtidade"
	self:oSchema:addProperty( "CUSTOUNI" , STR0006 , "number", STR0006 , "CUSTOUNI" ) //"Custo Unitário"
	self:oSchema:addProperty( "CUSTOTOT" , STR0007 , "number", STR0007 , "CUSTOTOT" ) //"Custo Total"

Return self:oSchema
