#include "protheus.ch"
#include "backoffice.sv.est.soldproducts.ch"
#include "totvs.framework.treports.integratedprovider.th"

//Grupo de perguntas  
#define SX1GRUPO "ESTT031"

using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

namespace totvs.protheus.backoffice.est.soldproducts.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SD2,SD1,SB1",name="Produtos Vendidos", country="ALL")

//-------------------------------------------------------------------
/*{Protheus.doc} ProdutosVendidosSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Produtos Vendidos 

@author Squad Entradas
@since 06/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class ProdutosVendidosSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object
	Public Method calcAbaVal() as Numeric
	
	Protected Method oNGetSchema()
	Protected data jItems		as json
	Protected data aFieldsSD2	as array
	Protected data aFieldsSD1	as array
	Protected data aImpSD2		as array
	Protected data cWhereSD2	as character
	Protected data cWhereSD1	as character
	Protected data cAliasTRB	as character
	Protected data lExistPerg	as logical

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self
 
@author Squad Entradas
@since 06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class ProdutosVendidosSmartViewBusinessObject

	_Super:new()

	//Define a Área
	self:appendArea( STR0001 ) //"Estoque/Custos"

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 ) //"Produtos Vendidos"

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 ) //"Este objeto de negócio apresenta o valor total das vendas de cada produto, calculando sua participação dentro do total das vendas. Mostra também o custo de cada venda e o custo de reposição do produto."

    //Indica o pergunte que será utilizado no relatório
	self:lExistPerg := self:setPergunte( SX1GRUPO )
	If !self:lExistPerg
		self:setErrorStatus(400,STR0024,i18n(STR0025,{SX1GRUPO})) //"Sem Pergunte" -- "Grupo de perguntas #1[SX1GRUPO]# nao encontrado. Verifique na tabela SX1, o grupo de perguntas informado"
		FWLogMsg('WARN', , 'SmartView', , , , i18n(STR0025,{SX1GRUPO}), , , ) //"Grupo de perguntas #1[SX1GRUPO]# nao encontrado. Verifique na tabela SX1, o grupo de perguntas informado"
	EndIf

	//Indica que o LookUp será do tipo padrão LookUp
    self:setIsLookUp(.T.)

	self:aFieldsSD2 := {}
	self:aFieldsSD1 := {}
	self:aImpSD2	:= {}

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Squad Entradas
@since 06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData( nPage as Numeric, oFilter as object) as object Class ProdutosVendidosSmartViewBusinessObject

	Local dDataDe		as Date
	Local dDataAte		as Date

	Local aTam			as Array
	Local aIndex		as Array
	Local aCpoTRB		as Array
	local aPrepared     as Array
	Local a2Prepared    as Array

	Local nF			as Numeric
	Local nAp1			as Numeric
	Local nAp2			as Numeric
	Local nAp3			as Numeric
	Local nAp4			as Numeric
	Local nAp5			as Numeric
	Local nSkip			as Numeric
	Local nMoeda		as Numeric
	Local nAp1Cus		as Numeric
	Local nValCus		as Numeric
	Local nTotLuc		as Numeric
	Local nTotFat		as Numeric
	Local nIndSD1		as Numeric
	Local nD1Total		as Numeric
	Local nD1ValIPI		as Numeric
	Local nD1ValICM		as Numeric
	Local nD1ValDesc	as Numeric
	Local nD2Total		as Numeric
	Local nD2ValIPI		as Numeric
	Local nD2ValICM		as Numeric
	Local nFil          as Numeric 
	local nPosPrepared  as numeric
	local n2PosPrepare  as Numeric	
	Local nBind         as Numeric
	Local nX            as Numeric

	Local lDevolucao	as Logical
	Local lAliqBra      as Logical

	local cMD5		    as Character
	Local cQuery		as Character
	Local cEstoq		as Character
	Local cDupli		as Character
	Local cArqSD1		as Character
	Local cFilSD1		as Character
	Local cKeySD1		as Character
	Local cFieldsD2		as Character
	Local cFieldsD1     as Character
	Local cCodAnt		as Character
	Local cTipant		as Character
	Local cEspSAT		as Character
	Local cEspNFCE		as Character
	Local cArqTrab		as Character
	Local cAliasTmp		as Character
	Local cEspecieLoja	as Character
	Local cBkpFil       as Character
	Local cFil          as Character 

	Local oTempTable	as Object

	Private jParams		as Json
	Private lVeic		as Logical
	Private cCampImp	as Character
	Private cAliasTRB	as Character
	Private cCpoCusD1	as Character
	Private cCpoCusD2	as Character

	//Valida o pergunte
	If !self:lExistPerg
		return self:oData
	Endif

	aPrepared    := {}
	a2Prepared   := {}
	n2PosPrepare := 0
	nPosPrepared := 0

	DbSelectArea("SF4")
	SF4->(DbSetOrder(1))

	DbSelectArea("SF2")
	SF2->(DbSetOrder(1))

	DbSelectArea("SF1")
	SF1->(DbSetOrder(1))

	DbSelectArea("SD2")
	SD2->(DbSetOrder(1))

	DbSelectArea("SD1")
	SD1->(DbSetOrder(1))

	DbSelectArea("SB1")
	SB1->(DbSetOrder(1))

	DbSelectArea("SBM")
	SBM->(DbSetOrder(1))

	//--------------------------------------------------------------------------
	// Variaveis utilizadas para parametros no grupo de pergunta ESTT031
	//--------------------------------------------------------------------------
	//
	// MV_PAR01 - Armazem Inicial
	// MV_PAR02 - Armazem Final
	// MV_PAR03 - Tipo Inicial
	// MV_PAR04 - Tipo Final
	// MV_PAR05 - Produto Inicial
	// MV_PAR06 - Produto Final
	// MV_PAR07 - Dt Digitacao Inicial
	// MV_PAR08 - Dt Digitacao Final
	// MV_PAR09 - Considera Devolucao
	// MV_PAR10 - TES Qto a Estoque
	// MV_PAR11 - TES Qto a Duplicata
	// MV_PAR12 - Abate Imp Nao Fat
	// MV_PAR13 - Perc. Abatimento
	// MV_PAR14 - Inclui Dev. Compra
	// MV_PAR15 - Moeda
	// MV_PAR16 - Qual Custo
	//--------------------------------------------------------------------------
	jParams		:= oFilter:getParameters() //metodo para retorno do json dos parâmetros

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'])

	If ValType(jParams['MV_PAR13'][1]) == "C"
		jParams['MV_PAR13'][1] := Val(jParams['MV_PAR13'][1])
	EndIf

	dDataDe		:= FwDateTimeToLocal( jParams['MV_PAR07'][1] )[1]
	dDataAte	:= FwDateTimeToLocal( jParams['MV_PAR08'][1] )[1]
	nMoeda		:= jParams['MV_PAR15'][1]
	cFieldsD1	:= ""
	cFieldsD2   := ""
	cCodAnt		:= ""
	cTipant		:= ""
	cEspSAT		:= "SATCE"
	aCpoTRB		:= {}
	cEspNFCE	:= "NFCE"
	cEspecieLoja:= "CF"
	cEstoq		:= If( (jParams['MV_PAR10'][1] == 1),"S",If( (jParams['MV_PAR10'][1] == 2),"N","SN" ) )
	cDupli 		:= If( (jParams['MV_PAR11'][1] == 1),"S",If( (jParams['MV_PAR11'][1] == 2),"N","SN" ) )
	nTotLuc		:= 0
	nTotFat		:= 0
	nValCus		:= 0
	lDevolucao	:= .F.
	lAliqBra    := SFB->(FieldPos("FB_ALIQ")) <= 0

	If jParams['MV_PAR16'][1] == 2
		cCpoCusD1 := 'D1_CUSFF' + cValToChar(nMoeda)
		cCpoCusD2 := 'D2_CUSFF' + cValToChar(nMoeda)
	Else
		If nMoeda == 1
			cCpoCusD1 := 'D1_CUSTO'
		Else
			cCpoCusD1 := 'D1_CUSTO' + cValToChar(nMoeda)
		EndIf

		cCpoCusD2 := 'D2_CUSTO' + cValToChar(nMoeda)
	EndIf

	For nF := 1 To Len( self:aFieldsSD2 )
		cFieldsD2 += self:aFieldsSD2[nF][1] + ","
		aTam	:= TamSx3(self:aFieldsSD2[nF][1])
		Aadd( aCpoTRB ,{self:aFieldsSD2[nF][1],self:aFieldsSD2[nF][2],aTam[1],aTam[2]})
	Next nF

	cFieldsD2 += cCpoCusD2

	aTam := TamSx3( cCpoCusD2 )
	Aadd( aCpoTRB , { "D2_CUSTO"  , "N" , aTam[1] , aTam[2] })
	Aadd( aCpoTRB , { "RECSMARTV" , "N" , 8 , 0	 })
	
	cAliasTRB := GetNextAlias()
	aIndex    :={"D2_FILIAL", "D2_TP", "D2_COD", "D2_DOC", "D2_SERIE"}

	self:cAliasTRB := cAliasTRB

	oTempTable := FWTemporaryTable():New( cAliasTRB )
	oTempTable:SetFields( aCpoTRB )
	oTempTable:AddIndex("indice1", aIndex )
	oTempTable:Create()

	cArqTrab := oTempTable:GetRealName()

	cBkpFil := cFilant 

	//Colocar while ou for de filiais aqui 
	For nFil :=1 to len(self:aFils)

		cFilAnt      := self:aFils[nFil]
		cFilExec     := AllTrim(FWFilialName())
		lVeic        := UPPER(GETMV("MV_VEICULO"))=="S"
		nBind        := 1
		nTotLuc      := 0
		nTotFat      := 0
		nValCus      := 0
	    nAp1Cus      := 0
		nAp1         := 0
		nAp2         := 0
		nAp3         := 0
		nAp4         := 0
		nAp5         := 0
		nD2Total     := 0
		nD2ValIPI    := 0
		nD2ValICM    := 0
		nD1TOTAL     := 0
		nD1VALIPI    := 0
		nD1VALICM    := 0
		nD1ValDesc   := 0
		lDevolucao   := .F.

		If jParams['MV_PAR09'][1] != 3

			DbSelectArea( "SD1" )
			cArqSD1 := CriaTrab( NIL,.F. )
			cKeySD1 := "D1_FILIAL+D1_COD+D1_SERIORI+D1_NFORI+D1_ITEMORI"

			cFilSD1 := "D1_FILIAL = '" + xFilial("SD1") + "'"
			cFilSD1 += " .And. D1_TIPO = 'D'"
			cFilSD1 += " .And. D1_COD >= '" + jParams['MV_PAR05'][1] + "'"
			cFilSD1 += " .And. D1_COD <= '" + jParams['MV_PAR06'][1] + "'"
			cFilSD1 += " .And. D1_LOCAL >= '" + jParams['MV_PAR01'][1] + "'"
			cFilSD1 += " .And. D1_LOCAL <= '" + jParams['MV_PAR02'][1] + "'"
			cFilSD1 += " .And. DTOS(D1_DTDIGIT) >= '" + DTOS(dDataDe) + "'"
			cFilSD1 += " .And. DTOS(D1_DTDIGIT) <= '" + DTOS(dDataAte) + "'"
			cFilSD1 += " .And. D1_TP >= '" + jParams['MV_PAR03'][1] + "'"
			cFilSD1 += " .And. D1_TP <= '" + jParams['MV_PAR04'][1] + "'"

			IndRegua("SD1",cArqSD1,cKeySD1,,cFilSD1)

			nIndSD1 := RetIndex("SD1")

			DbSetOrder(nIndSD1 + 1)
			DbGoTop()

		EndIf

		cQuery := " SELECT ? "
		cQuery += " FROM	" + RetSQLName( 'SD2' ) + " SD2 "
		cQuery += " JOIN	" + RetSQLName( 'SF2' ) + " SF2 "		
		cQuery += " ON		SF2.F2_FILIAL    = ? "
		cQuery += " AND		SF2.F2_DOC       = SD2.D2_DOC "
		cQuery += " AND		SF2.F2_SERIE     = SD2.D2_SERIE "
		cQuery += " AND		SF2.F2_LOJA      = SD2.D2_LOJA "
		cQuery += " AND		(NOT "
		cQuery += " (		SF2.F2_ESPECIE <> ? "
		cQuery += " AND		SF2.F2_ESPECIE <> ? "
		cQuery += " AND		SF2.F2_ESPECIE <> ? "
		cQuery += " AND		SF2.F2_NFCUPOM <> ? "
		cQuery += " )) "
		cQuery += " WHERE	SD2.D2_FILIAL    = ? "
		cQuery += " AND		SD2.D2_TP       >= ? "
		cQuery += " AND		SD2.D2_TP       <= ? "

		IF !lVeic
			cQuery += " AND D2_COD >= ? "
			cQuery += " AND D2_COD <= ? "
		Endif

		cQuery += " AND		SD2.D2_LOCAL    >= ? "
		cQuery += " AND		SD2.D2_LOCAL    <= ? "
		cQuery += " AND		SD2.D2_EMISSAO  >= ? "
		cQuery += " AND		SD2.D2_EMISSAO  <= ? "

		If jParams['MV_PAR14'][1] == 2
			cQuery += " AND D2_TIPO <> ? "
		Endif

		cQuery += " ? "
		cQuery += " AND	SD2.D_E_L_E_T_ = ? "
		cQuery += " AND	SF2.D_E_L_E_T_ = ? "
		cQuery += " ORDER BY 1,2,3
		cQuery := ChangeQuery(cQuery)
		
		cMD5 := MD5(cQuery)
		If (nPosPrepared := Ascan(aPrepared,{|x| x[2] == cMD5})) == 0
			cQuery := ChangeQuery(cQuery)
			Aadd(aPrepared,{FwExecStatement():New(cQuery),cMD5})
			nPosPrepared := Len(aPrepared)
		Endif

		//Binding
		aPrepared[nPosPrepared][1]:setUnsafe(nBind++, cFieldsD2 )
		aPrepared[nPosPrepared][1]:setString(nBind++, FWxFilial("SF2") )
		aPrepared[nPosPrepared][1]:setString(nBind++, cEspNFCE )
		aPrepared[nPosPrepared][1]:setString(nBind++, cEspSAT )
		aPrepared[nPosPrepared][1]:setString(nBind++, cEspecieLoja )
		aPrepared[nPosPrepared][1]:setString(nBind++,  ' ' )
		aPrepared[nPosPrepared][1]:setString(nBind++, FWxFilial("SD2") )
		aPrepared[nPosPrepared][1]:setString(nBind++, jParams['MV_PAR03'][1] )
		aPrepared[nPosPrepared][1]:setString(nBind++, jParams['MV_PAR04'][1] )

		IF !lVeic
			aPrepared[nPosPrepared][1]:setString(nBind++, jParams['MV_PAR05'][1] )
			aPrepared[nPosPrepared][1]:setString(nBind++, jParams['MV_PAR06'][1] )
			aPrepared[nPosPrepared][1]:setString(nBind++, jParams['MV_PAR01'][1] )
			aPrepared[nPosPrepared][1]:setString(nBind++, jParams['MV_PAR02'][1] )
			aPrepared[nPosPrepared][1]:setString(nBind++, Dtos(dDataDe))
			aPrepared[nPosPrepared][1]:setString(nBind++, Dtos(dDataAte))
			If jParams['MV_PAR14'][1] == 2
				aPrepared[nPosPrepared][1]:setString(nBind++, 'D')
			Endif
			aPrepared[nPosPrepared][1]:setUnsafe(nBind++, self:cWhereSD2 )
			aPrepared[nPosPrepared][1]:setString(nBind++, ' ')
			aPrepared[nPosPrepared][1]:setString(nBind++, ' ')
		Else
			aPrepared[nPosPrepared][1]:setString(nBind++, jParams['MV_PAR01'][1] )
			aPrepared[nPosPrepared][1]:setString(nBind++, jParams['MV_PAR02'][1] )
			aPrepared[nPosPrepared][1]:setString(nBind++, Dtos(dDataDe))
			aPrepared[nPosPrepared][1]:setString(nBind++, Dtos(dDataAte))
			If jParams['MV_PAR14'][1] == 2
				aPrepared[nPosPrepared][1]:setString(nBind++, 'D')
			Endif
			aPrepared[nPosPrepared][1]:setUnsafe(nBind++, self:cWhereSD2 )
			aPrepared[nPosPrepared][1]:setString(nBind++, ' ')
			aPrepared[nPosPrepared][1]:setString(nBind++, ' ')
		EndIf

		cAliasTmp := aPrepared[nPosPrepared][1]:OpenAlias()

		// Gera dados de saída
		GeraTrab("SD2",cAliasTmp,cAliasTRB,self:aFieldsSD2)
		(cAliasTmp)->( DBCloseArea() )

		If jParams['MV_PAR09'][1] != 3

			nBind     := 1
			cFieldsD1 := ""
			For nF  := 1 To Len( self:aFieldsSD1 )
				cFieldsD1 += self:aFieldsSD1[nF][1] + ","
			Next nF

			cFieldsD1 += cCpoCusD1

			cQuery := " SELECT ? "
			cQuery += " FROM	" + RetSQLName( 'SD1' ) + " SD1 "

			cQuery += " WHERE	D1_FILIAL   = ? "
			cQuery += " AND		D1_TP      >= ? "
			cQuery += " AND		D1_TP      <= ? "

			IF !lVeic
				cQuery += " AND D1_COD >= ? "
				cQuery += " AND D1_COD <= ? "
			Endif

			cQuery += " AND D1_LOCAL   >= ? "
			cQuery += " AND	D1_LOCAL   <= ? "
			cQuery += " AND D1_TIPO     = ? "

			If jParams['MV_PAR09'][1] = 1
				cQuery += " AND D1_DATORI >= ? "
				cQuery += " AND D1_DATORI <= ? "
			Else
				cQuery += " AND D1_DTDIGIT >= ? "
				cQuery += " AND D1_DTDIGIT <= ? "
			EndIf		

			cQuery += " ? "
			cQuery += " AND	SD1.D_E_L_E_T_ = ? "
			cQuery := ChangeQuery(cQuery)
			
			cMD5 := MD5(cQuery)
			If (n2PosPrepare := Ascan(a2Prepared,{|x| x[2] == cMD5})) == 0
				cQuery := ChangeQuery(cQuery)
				Aadd(a2Prepared,{FwExecStatement():New(cQuery),cMD5})
				n2PosPrepare := Len(a2Prepared)
			Endif

			a2Prepared[n2PosPrepare][1]:setUnsafe(nBind++, cFieldsD1 )
			a2Prepared[n2PosPrepare][1]:setString(nBind++, FWxFilial("SD1") )
			a2Prepared[n2PosPrepare][1]:setString(nBind++, jParams['MV_PAR03'][1] )
			a2Prepared[n2PosPrepare][1]:setString(nBind++, jParams['MV_PAR04'][1] )
			IF !lVeic
				a2Prepared[n2PosPrepare][1]:setString(nBind++, jParams['MV_PAR05'][1] )
				a2Prepared[n2PosPrepare][1]:setString(nBind++, jParams['MV_PAR06'][1] )
			EndIf
			a2Prepared[n2PosPrepare][1]:setString(nBind++, jParams['MV_PAR01'][1] )
			a2Prepared[n2PosPrepare][1]:setString(nBind++, jParams['MV_PAR02'][1] )
			a2Prepared[n2PosPrepare][1]:setString(nBind++, 'D' )
			a2Prepared[n2PosPrepare][1]:setString(nBind++, Dtos(dDataDe) )
			a2Prepared[n2PosPrepare][1]:setString(nBind++, Dtos(dDataAte) )
			a2Prepared[n2PosPrepare][1]:setUnsafe(nBind++, self:cWhereSD1 )
			a2Prepared[n2PosPrepare][1]:setString(nBind++, ' ' )

			cAliasTmp := a2Prepared[n2PosPrepare][1]:OpenAlias()

			// Gera dados de devolução
			GeraTrab("SD1",cAliasTmp,cAliasTRB,self:aFieldsSD1) 
			(cAliasTmp)->( DBCloseArea() )

		EndIf

		DbSelectArea(cAliasTRB)
		(cAliasTRB)->(DbGoTop())

		//Ajusta sequencia de acordo com a ordem da TRB para fazer a paginação corretamente.
		ajusteSeq()

		cFil := FWxFilial("SD2")
		(cAliasTRB)->(dbSeek(cFil))
		While !(cAliasTRB)->(Eof()) .And. (cAliasTRB)->D2_FILIAL == cFil

			If !(cCodAnt == (cAliasTRB)->D2_COD)
				cCodAnt    := (cAliasTRB)->D2_COD
				lDevolucao := .T.
			EndIf

			//------------------------------------------------------------------
			//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal    ³
			//³ Despreza Itens em que a TES NAO Se Ajusta a Selecao do Usuario ³
			//------------------------------------------------------------------
			If !((cAliasTRB)->D2_ORIGLAN == "LF") .And. AvalTes((cAliasTRB)->D2_TES,cEstoq,cDupli)

				If !(SB1->(MsSeek( xFilial("SB1") + (cAliasTRB)->D2_COD, .F. ))) // pode haver filtro de usuario na SB1
					dbSkip()
					Loop
				EndIf

				If (cAliasTRB)->D2_TIPO != "D"
					If (jParams['MV_PAR15'][1] > 1)
						nValCus := (cAliasTRB)->D2_CUSTO
						If nValCus > 0
							nTotLuc += nValCus
						Else
							nTotLuc += xMoeda( (cAliasTRB)->D2_CUSTO, Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, (cAliasTRB)->D2_EMISSAO )
						EndIf
					Else
						nTotLuc += (cAliasTRB)->D2_CUSTO
					EndIf

					nTotFat +=  valSD2()

				Else
					If (jParams['MV_PAR15'][1] > 1)
						nValCus := (cAliasTRB)->D2_CUSTO
						If nValCus > 0
							nTotLuc -= nValCus
						Else
							nTotLuc -= xMoeda( (cAliasTRB)->D2_CUSTO, Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, (cAliasTRB)->D2_EMISSAO )
						EndIf
					Else
						nTotLuc -= (cAliasTRB)->D2_CUSTO
					EndIf

					nTotFat -=  valSD2()

				Endif
				Do Case
				Case (jParams['MV_PAR09'][1] == 1)
					DbSelectArea( "SD1" )
					If DbSeek( xFilial("SD1") + (cAliasTRB)->D2_COD + (cAliasTRB)->D2_SERIE + (cAliasTRB)->D2_DOC + (cAliasTRB)->D2_ITEM,.F. )
						While !SD1->(Eof()) .And. (SD1->D1_COD == (cAliasTRB)->D2_COD) .And. ((cAliasTRB)->D2_SERIE+(cAliasTRB)->D2_DOC+(cAliasTRB)->D2_ITEM == SD1->D1_SERIORI+SD1->D1_NFORI+AllTrim(SD1->D1_ITEMORI))
							//------------------------------------------------------------------
							//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal    ³
							//³ Despreza Itens em que a TES NAO Se Ajusta a Selecao do Usuario ³
							//------------------------------------------------------------------
							If !(D1_ORIGLAN == "LF") .And. AvalTes(D1_TES,cEstoq,cDupli)

								If (jParams['MV_PAR15'][1] > 1)
									nValCus :=  &(cCpoCusD1)
									if nValCus > 0
										nTotLuc -= nValCus
									else
										nTotLuc -= xMoeda( D1_CUSTO, Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, D1_DTDIGIT )
									endif
								Else
									nTotLuc -=  &(cCpoCusD1)
								EndIf

								If (jParams['MV_PAR15'][1] > 1)
									nTotFat -= xMoeda( valSD1(), Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, D1_DTDIGIT )
								Else
									nTotFat -= valSD1()
								EndIf
							EndIf
							dbSelectArea('SD1')
							DbSkip()
						EndDo
					EndIf
				Case (jParams['MV_PAR09'][1] == 2) .And. lDevolucao
					DbSelectArea( "SD1" )
					If DbSeek( xFilial("SD1") + (cAliasTRB)->D2_COD + (cAliasTRB)->D2_SERIE + (cAliasTRB)->D2_DOC + (cAliasTRB)->D2_ITEM,.F. )
						While !SD1->(Eof()) .And. (SD1->D1_COD == (cAliasTRB)->D2_COD) .And. ((cAliasTRB)->D2_SERIE+(cAliasTRB)->D2_DOC+(cAliasTRB)->D2_ITEM  == SD1->D1_SERIORI+SD1->D1_NFORI+AllTrim(SD1->D1_ITEMORI))
							//------------------------------------------------------------------
							//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal    ³
							//³ Despreza Itens em que a TES NAO Se Ajusta a Selecao do Usuario ³
							//------------------------------------------------------------------
							If !(D1_ORIGLAN == "LF") .And. AvalTes(D1_TES,cEstoq,cDupli)

								If (jParams['MV_PAR15'][1] > 1)
									nValCus :=  &(cCpoCusD1)
									if nValCus > 0
										nTotLuc -= nValCus
									else
										nTotLuc -= xMoeda( D1_CUSTO, Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, D1_DTDIGIT )
									endif
								Else
									nTotLuc -=  &(cCpoCusD1)
								EndIf

								If (jParams['MV_PAR15'][1] > 1)
									nTotFat -= xMoeda( valSD1(), Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, D1_DTDIGIT )
								Else
									nTotFat -= valSD1()
								EndIf
							EndIf
							dbSelectArea('SD1')
							DbSkip()
						EndDo
					EndIf
				EndCase
			EndIf

			(cAliasTRB)->( DbSkip() )
			lDevolucao := !((cAliasTRB)->D2_TIPO == 'D')
		EndDo

		nTotLuc := self:calcAbaVal(nTotFat)-nTotLuc

		(cAliasTRB)->(DbGoTop())

		cCodAnt := ""

		(cAliasTRB)->(dbSeek(cFil))
		While !(cAliasTRB)->(Eof()) .and. (cAliasTRB)->D2_FILIAL == cFil

			cTipant := (cAliasTRB)->D2_TP

			While !(cAliasTRB)->(Eof()) .And. ((cAliasTRB)->D2_TP == cTipant) .And. ((cAliasTRB)->D2_FILIAL == cFil)

				If !(SB1->(MsSeek( xFilial("SB1") + (cAliasTRB)->D2_COD, .F. ))) // pode haver filtro de usuario na SB1
					(cAliasTRB)->( DbSkip() )
					Loop
				EndIf

				SBM->(MsSeek( xFilial("SBM") + SB1->B1_GRUPO, .F. ))

				nAp1 	:= 0
				nAp1Cus	:= 0
				nAp2	:= 0
				nAp3	:= 0
				nAp4	:= 0
				nAp5	:= 0

				nD2Total	:= 0
				nD2ValIPI	:= 0
				nD2ValICM	:= 0
				nD1TOTAL	:= 0
				nD1VALIPI	:= 0
				nD1VALICM	:= 0
				nD1ValDesc	:= 0

				For nF := 1 To Len( self:aImpSD2 )
					self:aImpSD2[ nF , 2 ] := 0
				Next nF

				If !(cCodAnt == (cAliasTRB)->D2_COD)
					cCodAnt    := (cAliasTRB)->D2_COD
					lDevolucao := !((cAliasTRB)->D2_TIPO == 'D')
				EndIf

				While !(cAliasTRB)->(Eof()) .And. (cAliasTRB)->D2_FILIAL + (cAliasTRB)->D2_TP + (cAliasTRB)->D2_COD == cFil + cTipant + cCodAnt

					//------------------------------------------------------------------
					//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal    ³
					//³ Despreza Itens em que a TES NAO Se Ajusta a Selecao do Usuario ³
					//------------------------------------------------------------------
					If !((cAliasTRB)->D2_ORIGLAN == "LF") .And. AvalTes((cAliasTRB)->D2_TES,cEstoq,cDupli)
						If !((cAliasTRB)->D2_TIPO == "D")
							If AvalTes((cAliasTRB)->D2_TES,"S")
								nAp1Cus+=(cAliasTRB)->D2_QUANT
							Endif
							nAp1 += (cAliasTRB)->D2_QUANT
							If (jParams['MV_PAR15'][1] > 1)
								nValCus := (cAliasTRB)->D2_CUSTO
								if nValCus > 0
									nAp2 += nValCus
								else
									nAp2 += xMoeda( (cAliasTRB)->D2_CUSTO, Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, (cAliasTRB)->D2_EMISSAO )
								endif
							Else
								nAp2 += (cAliasTRB)->D2_CUSTO
							EndIf

							nAp3 += valSD2()
							If lAliqBra
								nAp5 += xMoeda( (cAliasTRB)->D2_TOTAL+IF(jParams['MV_PAR09'][1] == 1 .And. (cAliasTRB)->D2_TIPO <> "P",(cAliasTRB)->D2_VALIPI,0), Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, (cAliasTRB)->D2_EMISSAO )
							Else
								nAp5 += SumTaxSD2()
							EndIf
							
							nD2Total	+= (cAliasTRB)->D2_TOTAL
							nD2ValIPI	+= (cAliasTRB)->D2_VALIPI
							nD2ValICM	+= (cAliasTRB)->D2_VALICM

							//Campos de impostos
							For nF := 1 To Len( self:aImpSD2 )
								self:aImpSD2[ nF , 2 ] += &("(cAliasTRB)->" + self:aImpSD2[ nF , 1 ] )
							Next nF

							If jParams['MV_PAR15'][1] > 1
								nAp4 += ((cAliasTRB)->D2_QUANT*xMoeda(RetFldProd(SB1->B1_COD,"B1_CUSTD"), Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, RetFldProd(SB1->B1_COD,"B1_DATREF") ))
							Else
								If RetFldProd(SB1->B1_COD,"B1_MCUSTD") > "1" .and. !Empty(RetFldProd(SB1->B1_COD,"B1_MCUSTD"))
									nAp4 += ((cAliasTRB)->D2_QUANT*(XMoeda(RetFldProd(SB1->B1_COD,"B1_CUSTD"),Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")),1,dDataBase)))
								Else
									nAp4 += ((cAliasTRB)->D2_QUANT*RetFldProd(SB1->B1_COD,"B1_CUSTD"))
								EndIf
							EndIF
						Else
							If AvalTes((cAliasTRB)->D2_TES,"S")
								nAp1Cus-=(cAliasTRB)->D2_QUANT
							Endif
							nAp1 -= (cAliasTRB)->D2_QUANT
							If (jParams['MV_PAR15'][1] > 1)
								nValCus := (cAliasTRB)->D2_CUSTO
								if nValCus > 0
									nAp2 -= nValCus
								else
									nAp2 -= xMoeda( (cAliasTRB)->D2_CUSTO, Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, (cAliasTRB)->D2_EMISSAO )
								endif
							Else
								nAp2 -= (cAliasTRB)->D2_CUSTO
							EndIf

							nAp3 -= valSD2()
							If lAliqBra
								nAp5 -= xMoeda( (cAliasTRB)->D2_TOTAL+IF(jParams['MV_PAR09'][1] == 1 .And. (cAliasTRB)->D2_TIPO <> "P",(cAliasTRB)->D2_VALIPI,0), Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, (cAliasTRB)->D2_EMISSAO )
							Else
								nAp5 -= SumTaxSD2()
							EndIf

							nD2Total	-= (cAliasTRB)->D2_TOTAL
							nD2ValIPI	-= (cAliasTRB)->D2_VALIPI
							nD2ValICM	-= (cAliasTRB)->D2_VALICM

							//Campos de impostos
							For nF := 1 To Len( self:aImpSD2 )
								self:aImpSD2[ nF , 2 ] -= &("(cAliasTRB)->" + self:aImpSD2[ nF , 1 ] )
							Next nF

							If jParams['MV_PAR15'][1] > 1
								If Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")) > 1
									nAp4 -= ((cAliasTRB)->D2_QUANT*(XMoeda(RetFldProd(SB1->B1_COD,"B1_CUSTD"),Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")),1,dDataBase)))
								Else
									nAp4 -= ((cAliasTRB)->D2_QUANT*RetFldProd(SB1->B1_COD,"B1_CUSTD"))
								EndIf
							Else
								If RetFldProd(SB1->B1_COD,"B1_MCUSTD") > "1" .and. !Empty(RetFldProd(SB1->B1_COD,"B1_MCUSTD"))
									nAp4 -= ((cAliasTRB)->D2_QUANT*(XMoeda(RetFldProd(SB1->B1_COD,"B1_CUSTD"),Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")),1,dDataBase)))
								Else
									nAp4 -= ((cAliasTRB)->D2_QUANT*RetFldProd(SB1->B1_COD,"B1_CUSTD"))
								EndIf
							EndIF
						Endif
						Do Case
						Case (jParams['MV_PAR09'][1] == 1)
							DbSelectArea( "SD1" )
							If DbSeek( xFilial("SD1") + (cAliasTRB)->D2_COD+(cAliasTRB)->D2_SERIE+(cAliasTRB)->D2_DOC+(cAliasTRB)->D2_ITEM,.F. )
								While !SD1->(Eof()) .And. (D1_COD == cCodAnt) .And. ((cAliasTRB)->D2_SERIE+(cAliasTRB)->D2_DOC+(cAliasTRB)->D2_ITEM == SD1->D1_SERIORI+SD1->D1_NFORI+AllTrim(SD1->D1_ITEMORI))
									//------------------------------------------------------------------
									//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal    ³
									//³ Despreza Itens em que a TES NAO Se Ajusta a Selecao do Usuario ³
									//------------------------------------------------------------------
									If !(SD1->D1_ORIGLAN == "LF") .And. AvalTes(SD1->D1_TES,cEstoq,cDupli)
										If AvalTes(D1_TES,"S")
											nAp1Cus -= D1_QUANT
										Endif
										nAp1 -= SD1->D1_QUANT
										If (jParams['MV_PAR15'][1] > 1)
											nValCus :=  &(cCpoCusD1)
											if nValCus > 0
												nAP2 -= nValCus
											else
												nAp2 -= xMoeda(SD1->D1_CUSTO, Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")),nMoeda, SD1->D1_DTDIGIT )
											endif
										Else
											nAp2 -=  &(cCpoCusD1)
										EndIf

										If (jParams['MV_PAR15'][1] > 1)

											nAp3 -= xMoeda( valSD1(), Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, SD1->D1_DTDIGIT )
											nAp5 -= SD1->D1_TOTAL-SD1->D1_VALDESC

										Else

											nAp3 -= valSD1()
											nAp5 -= SD1->D1_TOTAL-SD1->D1_VALDESC

										EndIF

										nD1Total	+= SD1->D1_TOTAL
										nD1ValIPI	+= SD1->D1_VALIPI
										nD1ValICM	+= SD1->D1_VALICM
										nD1ValDesc	+= SD1->D1_VALDESC

										If jParams['MV_PAR15'][1] > 1
											nAp4 -= (SD1->D1_QUANT*xMoeda(RetFldProd(SB1->B1_COD,"B1_CUSTD"), Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, RetFldProd(SB1->B1_COD,"B1_DATREF") ))
										Else
											If RetFldProd(SB1->B1_COD,"B1_MCUSTD") > "1" .and. !Empty(RetFldProd(SB1->B1_COD,"B1_MCUSTD"))
												nAp4 -= (SD1->D1_QUANT*(XMoeda(RetFldProd(SB1->B1_COD,"B1_CUSTD"),Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")),1,dDataBase)))
											Else
												nAp4 -= (SD1->D1_QUANT*RetFldProd(SB1->B1_COD,"B1_CUSTD"))
											EndIf
										EndIF
									EndIf
									dbSelectArea('SD1')
									DbSkip()
								EndDo
							EndIf
						Case (jParams['MV_PAR09'][1] == 2) .And. lDevolucao
							DbSelectArea( "SD1" )
							If DbSeek( xFilial("SD1") +(cAliasTRB)->D2_COD+(cAliasTRB)->D2_SERIE+(cAliasTRB)->D2_DOC + (cAliasTRB)->D2_ITEM,.F. )
								While !SD1->(Eof()) .And. (D1_COD == cCodAnt) .And. ((cAliasTRB)->D2_SERIE+(cAliasTRB)->D2_DOC+(cAliasTRB)->D2_ITEM == SD1->D1_SERIORI+SD1->D1_NFORI+AllTrim(SD1->D1_ITEMORI))
									//------------------------------------------------------------------
									//³ Despreza Notas Fiscais Lancadas Pelo Modulo do Livro Fiscal    ³
									//³ Despreza Itens em que a TES NAO Se Ajusta a Selecao do Usuario ³
									//------------------------------------------------------------------
									If !(D1_ORIGLAN == "LF") .And. AvalTes(D1_TES,cEstoq,cDupli)
										If AvalTes(D1_TES,"S")
											nAp1Cus -= D1_QUANT
										Endif
										nAp1 -= D1_QUANT
										If (jParams['MV_PAR15'][1] > 1)
											nValCus :=  &(cCpoCusD1)
											if nValCus > 0
												nAp2 -= nValCus
											else
												nAp2 -= xMoeda( D1_CUSTO, Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, D1_DTDIGIT )
											endif
										Else
											nAp2 -=  &(cCpoCusD1)
										EndIf

										If (jParams['MV_PAR15'][1] > 1)

											nAp3 -= xMoeda( valSD1(), Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, D1_DTDIGIT )
											nAp5 -= SD1->D1_TOTAL-SD1->D1_VALDESC

										Else

											nAp3 -= valSD1()
											nAp5 -= SD1->D1_TOTAL-SD1->D1_VALDESC

										EndIF

										nD1Total	-= SD1->D1_TOTAL
										nD1ValIPI	-= SD1->D1_VALIPI
										nD1ValICM	-= SD1->D1_VALICM
										nD1ValDesc	-= SD1->D1_VALDESC

										If jParams['MV_PAR15'][1] > 1
											nAp4 -= (D1_QUANT*xMoeda( RetFldProd(SB1->B1_COD,"B1_CUSTD"), Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")), nMoeda, RetFldProd(SB1->B1_COD,"B1_DATREF") ))
										Else
											If RetFldProd(SB1->B1_COD,"B1_MCUSTD") > "1" .and. !Empty(RetFldProd(SB1->B1_COD,"B1_MCUSTD"))
												nAp4 -= (D1_QUANT*(XMoeda(RetFldProd(SB1->B1_COD,"B1_CUSTD"),Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")),1,dDataBase)))
											Else
												nAp4 -= (D1_QUANT*RetFldProd(SB1->B1_COD,"B1_CUSTD"))
											EndIf
										EndIF
									EndIf
									dbSelectArea('SD1')
									DbSkip()
								EndDo
							EndIf
						EndCase

					EndIf
					
					(cAliasTRB)->( DbSkip() )
					lDevolucao := !((cAliasTRB)->D2_TIPO == 'D')

				EndDo

				If nAp1 != 0 .Or. nAp2 != 0 .Or. nAp3 != 0 .Or. nAp4 != 0 .And. lDevolucao

					self:jItems := JsonObject():New()

					self:jItems["cFIL"		] := cFil
					self:jItems["cTipant"	] := cTipant
					self:jItems["cCodAnt"	] := cCodAnt
					self:jItems["B1_DESC"	] := SB1->B1_DESC
					self:jItems["B1_GRUPO"	] := SB1->B1_GRUPO
					self:jItems["BM_DESC"	] := SBM->BM_DESC
					self:jItems["nAp1"		] := nAp1
					self:jItems["nMov"		] := nAp1Cus
					self:jItems["B1_UM"		] := SB1->B1_UM
					self:jItems["nAp2"		] := nAp2
					self:jItems["nCusto"	] := Iif(nAp1Cus<>0,nAp2/nAp1Cus,0)
					self:jItems["nAp5"		] := nAp5
					self:jItems["nAp3"		] := nAp3
					self:jItems["nAp4"		] := nAp4

					If nAp3 != 0
						self:jItems["nImpnFat"	] := (nAp5 - ( self:calcAbaVal(nAp5) ) )
						self:jItems["nValFat"	] := (nAp3 - ( nAp5 - ( self:calcAbaVal(nAp5) ) ) )
						self:jItems["nMargem"	] := (100  * ( self:calcAbaVal(nAp3) - nAp2) / self:calcAbaVal(nAp3) )
					Else
						self:jItems["nImpnFat"	] := 0
						self:jItems["nValFat"	] := 0
						self:jItems["nMargem"	] := 0
					EndIf
					If nTotFat != 0
						self:jItems["nMixMar"	] := (100  * self:calcAbaVal(nAp3) / self:calcAbaVal(nTotFat) )
					Else
						self:jItems["nMixMar"	] := 0
					EndIf
					self:jItems["cSinal"	] := IIF( nAp3 - nAp2 > 0 , "+" , "-" )
					If nTotLuc != 0
						self:jItems["nMix"		] := (100  * ( self:calcAbaVal(nAp3) - nAp2) / nTotLuc)
					Else
						self:jItems["nMix"		] := 0
					EndIf	
					
					If nAp2 != 0
						self:jItems["nVar"		] := ( (100 * nAp4 / nAp2) - 100 )
					Else
						self:jItems["nVar"		] := 0
					EndIf

					self:jItems["D1_TOTAL"	] := nD1TOTAL
					self:jItems["D1_VALIPI"	] := nD1VALIPI
					self:jItems["D1_VALICM"	] := nD1VALICM
					self:jItems["D1_VALDESC"] := nD1ValDesc

					self:jItems["D2_TOTAL"	] := nD2TOTAL
					self:jItems["D2_VALIPI"	] := nD2VALIPI
					self:jItems["D2_VALICM"	] := nD2VALICM

					For nF := 1 To Len( self:aImpSD2 )
						self:jItems[ self:aImpSD2[ nF , 1 ]	] := self:aImpSD2[ nF , 2 ]
					Next nF

					self:processData()
					self:oData:appendData( self:jItems )

				Endif

			EndDo

		EndDo

		If jParams['MV_PAR09'][1] != 3
			dbSelectArea( "SD1" )
			RetIndex("SD1")
			dbClearFilter()
		EndIf

	Next 

	If ValType(oTempTable) == "O"
		oTempTable:Delete()
		FreeObj(oTempTable)
		oTempTable := nil
	EndIf

	cFilant := cBkpFil 

	For nX := 1 To Len(aPrepared)
		aPrepared[nX][1]:Destroy()
	Next

	For nX := 1 To Len(a2Prepared)
		a2Prepared[nX][1]:Destroy()
	Next

	FWFreeArray(aPrepared)
	FWFreeArray(a2Prepared)
	
Return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Squad Entradas
@since 06/2023
@version 1.0
*/
//-------------------------------------------------------------------    
Method oNGetSchema() Class ProdutosVendidosSmartViewBusinessObject

	Local nI		As Numeric
	Local cImp		As Character
	Local cCpoImp	As Character
	Local aFields	As Array

	self:addProperty( "cFIL"	, STR0026 , "string" ,STR0026 , "cFIL"		) //"Filial"
	self:addProperty( "cTipant"	, STR0004 , "string" ,STR0004 , "cTipant"	) //"TP"
	self:addProperty( "cCodAnt"	, STR0005 , "string" ,STR0005 , "cCodAnt"	) //"Codigo"
	self:addProperty( "B1_DESC"	, STR0006 , "string" ,STR0006 , "B1_DESC"	) //"Descricao"
	self:addProperty( "B1_GRUPO", STR0022 , "string" ,STR0022 , "B1_GRUPO"	) //"Grupo"
	self:addProperty( "BM_DESC ", STR0023 , "string" ,STR0023 , "BM_DESC"	) //"Descrição Grupo"
	self:addProperty( "nAp1"	, STR0007 , "number" ,STR0007 , "nAp1"		) //"Quantidade"
	self:addProperty( "nMov"	, STR0008 , "number" ,STR0008 , "nMov"		) //"Qtd. Movimento"
	self:addProperty( "B1_UM"	, STR0009 , "string" ,STR0009 , "B1_UM"		) //"Unidade"
	self:addProperty( "nAp2"	, STR0010 , "number" ,STR0010 , "nAp2"		) //"Custo Total"
	self:addProperty( "nCusto"	, STR0011 , "number" ,STR0011 , "nCusto"	) //"Custo por Unidade"
	self:addProperty( "nAp5"	, STR0012 , "number" ,STR0012 , "nAp5"		) //"Val. Faturado Bruto"
	self:addProperty( "nAp3"	, STR0013 , "number" ,STR0013 , "nAp3"		) //"Valor Faturado"
	self:addProperty( "nImpnFat", STR0014 , "number" ,STR0014 , "nImpnFat"	) //"Impostos não Faturados"
	self:addProperty( "nValFat"	, STR0015 , "number" ,STR0015 , "nValFat"	) //"Val Faturado- Imp. não Fat."
	self:addProperty( "nMargem"	, STR0016 , "number" ,STR0016 , "nMargem"	) //"Margem"
	self:addProperty( "nMixMar"	, STR0017 , "number" ,STR0017 , "nMixMar"	) //"Mix *Mar"
	self:addProperty( "cSinal"	, STR0021 , "string" ,STR0021 , "cSinal"	) //"Sinal"
	self:addProperty( "nMix"	, STR0018 , "number" ,STR0018 , "nMix"		) //"Mix"
	self:addProperty( "nAp4"	, STR0019 , "number" ,STR0019 , "nAp4"		) //"Custo de Reposicao"
	self:addProperty( "nVar"	, STR0020 , "number" ,STR0020 , "nVar"		) //"Variacao"

	aAdd( self:aFieldsSD2 , { "D2_FILIAL"	, "C" } )
	aAdd( self:aFieldsSD2 , { "D2_COD"		, "C" } )
	aAdd( self:aFieldsSD2 , { "D2_LOCAL"	, "C" } )
	aAdd( self:aFieldsSD2 , { "D2_SERIE"	, "C" } )
	aAdd( self:aFieldsSD2 , { "D2_TES"		, "C" } )
	aAdd( self:aFieldsSD2 , { "D2_TP"		, "C" } )
	aAdd( self:aFieldsSD2 , { "D2_EMISSAO"	, "D" } )
	aAdd( self:aFieldsSD2 , { "D2_TIPO"		, "C" } )
	aAdd( self:aFieldsSD2 , { "D2_DOC"		, "C" } )
	aAdd( self:aFieldsSD2 , { "D2_QUANT"	, "N" } )
	aAdd( self:aFieldsSD2 , { "D2_TOTAL"	, "N" } )
	aAdd( self:aFieldsSD2 , { "D2_VALIPI"	, "N" } )
	aAdd( self:aFieldsSD2 , { "D2_VALICM"	, "N" } )
	aAdd( self:aFieldsSD2 , { "D2_ITEM"		, "C" } )
	aAdd( self:aFieldsSD2 , { "D2_ORIGLAN"	, "C" } )
	aAdd( self:aFieldsSD2 , { "D2_CLIENTE"	, "C" } )
	aAdd( self:aFieldsSD2 , { "D2_LOJA"		, "C" } )

	//carrega campos de impostos conforme existente.
	cImp	:= '1'
	cCpoImp := "D2_VALIMP" + cImp
	While SD2->( FieldPos( cCpoImp ) ) > 0
		aAdd( self:aFieldsSD2 , { cCpoImp , "N" } )
		aAdd( self:aImpSD2	, { cCpoImp , 0 })
		cImp	:= Soma1( cImp )
		cCpoImp := "D2_VALIMP" + cImp
	EndDo

	cImp	:= "1"
	cCpoImp := "D2_BASIMP" + cImp
	While SD2->( FieldPos( cCpoImp ) ) > 0
		aAdd( self:aFieldsSD2 , { cCpoImp , "N" } )
		aAdd( self:aImpSD2	, { cCpoImp , 0 } )
		cImp	:= Soma1( cImp )
		cCpoImp := "D2_BASIMP" + cImp
	EndDo

	aFields := {"D2_TOTAL","D2_VALIPI","D2_VALICM"	}
	For nI := 1 To Len( self:aImpSD2 )
		aAdd( aFields , self:aImpSD2[ nI, 1 ] )
	Next nI

	self:AliasToSchema( "SD2" , aFields )
	
	aAdd( self:aFieldsSD1 , {  "D1_FILIAL"	, "C" } )
	aAdd( self:aFieldsSD1 , {  "D1_TP"		, "C" } )
	aAdd( self:aFieldsSD1 , {  "D1_COD"		, "C" } )
	aAdd( self:aFieldsSD1 , {  "D1_DOC"		, "C" } )
	aAdd( self:aFieldsSD1 , {  "D1_SERIE"	, "C" } )
	aAdd( self:aFieldsSD1 , {  "D1_LOCAL"	, "C" } )
	aAdd( self:aFieldsSD1 , {  "D1_TES"		, "C" } )
	aAdd( self:aFieldsSD1 , {  "D1_EMISSAO"	, "D" } )
	aAdd( self:aFieldsSD1 , {  "D1_TIPO"	, "C" } )
	aAdd( self:aFieldsSD1 , {  "D1_QUANT"	, "N" } )
	aAdd( self:aFieldsSD1 , {  "D1_TOTAL"	, "N" } )
	aAdd( self:aFieldsSD1 , {  "D1_VALIPI"	, "N" } )
	aAdd( self:aFieldsSD1 , {  "D1_VALICM"	, "N" } )
	aAdd( self:aFieldsSD1 , {  "D1_ORIGLAN"	, "C" } )
	aAdd( self:aFieldsSD1 , {  "D1_ITEM"	, "C" } )
	aAdd( self:aFieldsSD1 , {  "D1_LOJA"	, "C" } )
	aAdd( self:aFieldsSD1 , {  "D1_SERIORI"	, "C" } )
	aAdd( self:aFieldsSD1 , {  "D1_ITEMORI"	, "C" } )
	aAdd( self:aFieldsSD1 , {  "D1_NFORI"	, "C" } )
	aAdd( self:aFieldsSD1 , {  "D1_VALDESC"	, "N" } )
	aAdd( self:aFieldsSD1 , {  "D1_CUSTO"	, "N" } )
	aAdd( self:aFieldsSD1 , {  "D1_FORNECE"	, "C" } )
	aAdd( self:aFieldsSD1 , {  "D1_CUSTO2"	, "N" } )

	self:AliasToSchema( "SD1" , {"D1_TOTAL","D1_VALIPI","D1_VALICM","D1_VALDESC" } )

Return


//-------------------------------------------------------------------
/*{Protheus.doc} self:calcAbaVal
Retorna valor baseado no percentual de abatimento.
 
@return nValor Valor calculado do abatimento
 
@author Marcio Lopes dos Santos
@since 06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method calcAbaVal( nValor ) as Numeric Class ProdutosVendidosSmartViewBusinessObject

	Local nPerc := jParams['MV_PAR13'][1]
	If jParams['MV_PAR12'][1] == 2
		nValor := nValor - (nValor * nPerc /100)
	EndIf
	
Return nValor

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    GeraTrab  ³ Autor ³Patricia A. Salomao    ³ Data ³ 11/01/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Gera Arquivo de Trabalho                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function GeraTrab(cAlias as character,cAliasTmp as character,cAliasTRB as character,aCampos as array)

	Local nPosFirst as Numeric
	Local nCpo 		as Numeric
	Local lT		as Logical

	nCpo := 0

	DbSelectArea(cAliasTmp)
	(cAliasTmp)->( DbGoTop() )
	While !Eof() .And. &(Subs(cAlias,2,2)+"_FILIAL") == xFilial(cAlias)

		//---------------------------------------------
		// CONTROLE para SIGAVEI, SIGAPEC e SIGAOFI
		//---------------------------------------------
		If lVeic
			lT := .T.
			DbSelectArea("SB1")
			If SB1->(MsSeek( xFilial("SB1") + &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_COD")))
				If !Eof()
					If (SB1->B1_CODITE >= jParams['MV_PAR05'][1] .and. SB1->B1_CODITE <= jParams['MV_PAR06'][1])
						lT := .F.
					EndIf
				EndIf
				dbSelectArea(cAliasTmp)
				If lT
					(cAliasTmp)->(DbSkip())
					Loop
				EndIf
			EndIf
		ENDIF

		//  So' deverao entrar no "Arquivo de Trabalho", as NF's de Devolucao, que nao tenham suas NF's Originais emitidas no mesmo periodo.
		If cAlias == "SD1" .And. (cAliasTRB)->(dbSeek((cAliasTmp)->D1_FILIAL+(cAliasTmp)->D1_TP+(cAliasTmp)->D1_COD+(cAliasTmp)->D1_NFORI+(cAliasTmp)->D1_SERIORI)) .And. (cAliasTRB)->D2_TIPO == "N"
			dbSkip()
			loop
		EndIf
		
		dbSelectArea(cAliasTRB)
		Reclock(cAliasTRB,.T.)
		Replace (cAliasTRB)->D2_FILIAL  With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_FILIAL")
		Replace (cAliasTRB)->D2_COD     With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_COD")
		Replace (cAliasTRB)->D2_LOCAL   With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_LOCAL")
		If cAlias == "SD2"
			Replace (cAliasTRB)->D2_SERIE   With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_SERIE")
		Else
			Replace (cAliasTRB)->D2_SERIE   With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_SERIORI")
			If cAlias == "SD1" .And. rtrim((cAliasTRB)->D2_SERIE)=='' .And. (cAliasTmp)->D1_TIPO == 'D'
				Replace (cAliasTRB)->D2_SERIE   With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_SERIE")
			EndIf
		Endif
		Replace (cAliasTRB)->D2_TES     With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_TES")
		Replace (cAliasTRB)->D2_TP      With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_TP")
		Replace (cAliasTRB)->D2_EMISSAO With sTod(&(cAliasTmp+"->"+Subs(cAlias,2,2)+"_EMISSAO"))
		Replace (cAliasTRB)->D2_TIPO    With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_TIPO")
		If cAlias == "SD2"
			Replace (cAliasTRB)->D2_DOC With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_DOC")
		Else
			Replace (cAliasTRB)->D2_DOC With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_NFORI")
			If cAlias == "SD1" .And.  rtrim((cAliasTRB)->D2_DOC)=='' .And. (cAliasTmp)->D1_TIPO == 'D'
				Replace (cAliasTRB)->D2_DOC With &(cAliasTmp+"->"+(Subs(cAlias,2,2)+"_DOC"))
			EndIf

		Endif
		Replace (cAliasTRB)->D2_QUANT   With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_QUANT")
		Replace (cAliasTRB)->D2_TOTAL   With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_TOTAL")
		If cAlias == "SD1" .And. jParams['MV_PAR09'][1] == 2 .And. (cAliasTmp)->D1_TIPO == 'D'
			Replace (cAliasTRB)->D2_TOTAL   With (cAliasTRB)->D2_TOTAL - &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_VALDESC")
		EndIf
		Replace (cAliasTRB)->D2_VALIPI  With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_VALIPI")
		Replace (cAliasTRB)->D2_VALICM  With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_VALICM")
		If cAlias == "SD2"
			Replace (cAliasTRB)->D2_ITEM With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_ITEM")
		Else
			Replace (cAliasTRB)->D2_ITEM With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_ITEMORI")
			If cAlias == "SD1" .And. rtrim((cAliasTRB)->D2_ITEM)=='' .And. (cAliasTmp)->D1_TIPO == 'D'
				Replace (cAliasTRB)->D2_ITEM With Subs(&(cAliasTmp+"->"+ Subs(cAlias,2,2)+"_ITEM"),3,2)
			EndIf
		Endif	
		Replace (cAliasTRB)->D2_ORIGLAN With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_ORIGLAN")
		Replace (cAliasTRB)->D2_LOJA    With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_LOJA")

		If cAlias == "SD2"
			Replace (cAliasTRB)->D2_CUSTO With &(cAliasTmp+"->"+ cCpoCusD2)
			Replace (cAliasTRB)->D2_CLIENTE With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_CLIENTE")
		Else
			Replace (cAliasTRB)->D2_CUSTO With &(cAliasTmp+"->"+ cCpoCusD1)
		EndIf

		If cAlias == "SD1" .And. rtrim((cAliasTRB)->D2_CLIENTE)=='' .And. (cAliasTmp)->D1_TIPO == 'D'
			Replace (cAliasTRB)->D2_CLIENTE With &(cAliasTmp+"->"+Subs(cAlias,2,2)+"_FORNECE")
		EndIf
		If cAlias == "SD2"
			nPosFirst := AScan(aCampos,{|x| "D2_VALIMP"$Alltrim(x[1]) .or. "D2_BASIMP"$AllTrim(x[1])})
			For nCpo := nPosFirst to Len(aCampos)
				If "D2_VALIMP"$aCampos[nCpo][1] .or. "D2_BASIMP"$aCampos[nCpo][1]
					Replace (cAliasTRB)->&(aCampos[nCpo][1]) With &(cAliasTmp+"->"+Substr(cAlias,2,2)+Substr(aCampos[nCpo][1],3,8))
				EndIf
			Next
		EndIf
		
		MsUnlock()
		dbSelectArea(cAliasTmp)
		dbSkip()
	EndDo
	(cAliasTRB)->(dbGoTop())
Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³  valSD2  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 20/05/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Soma os valores de acordo com os parametros no arquivo SD2 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR310			                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function valSD2()

	Local nValRet as Numeric
	
	nValRet	:=0

	nValRet := (cAliasTRB)->D2_TOTAL

	If (cAliasTRB)->D2_TIPO == "D" 
		nValRet := calcMoed("SF1",nValRet) //Calcula a taxa da moeda de acordo com a venda
	Else
		nValRet := calcMoed("SF2",nValRet) //Calcula a taxa da moeda de acordo com a venda
	EndIf

Return nValRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³  valSD1  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 20/05/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Soma os valores de acordo com os parametros no arquivo SD1 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR310                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function valSD1()

	Local nValRet as Numeric

	nValRet	:= 0

	nValRet:=D1_TOTAL-D1_VALDESC

	If D1_TIPO == "D" 
		nValRet := calcMoed("SF2",nValRet) //Calcula a taxa da moeda de acordo com a venda
	EndIf

RETURN nValRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³SumTaxSD2 ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 20/05/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Soma os valores de acordo com os parametros no arquivo SD2 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR310			                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function sumTaxSD2()

	Local nValRet	as Numeric
	Local nImpInc	as Numeric
	Local aImpostos	as Array
	Local nY		as Numeric

	nValRet   := 0
	nImpInc   := 0
	aImpostos := {}
	nY := 0

	aImpostos:=TesImpInf((cAliasTRB)->D2_TES)
	For nY:=1 to Len(aImpostos)
		cCampImp:="(cAliasTRB)->D2_"+(Substr(aImpostos[nY][2],4))
		If ( aImpostos[nY][3]=="1" )
			nImpInc  += &cCampImp
		EndIf
	Next

	nValRet:=(cAliasTRB)->D2_TOTAL+IF(jParams['MV_PAR10'][1] == 1,nImpInc,0)

	If (cAliasTRB)->D2_TIPO == "D"
		nValRet := calcMoed("SF1",nValRet)//Calcula a tasa de la noneda del documento 
	Else
		nValRet := calcMoed("SF2",nValRet)//Calcula a taxa da moeda de acordo com a venda.
	EndIf

Return nValRet

//-------------------------------------------------------------------
/*{Protheus.doc} ajusteSeq
Ajusta sequencia de acordo com a ordem da TRB para fazer a paginação corretamente.

@author Squad Entradas
@since 06/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Static Function ajusteSeq()

	Local cTipant as Character
	Local cCodAnt as Character
	Local nRec    as Numeric

	While !(cAliasTRB)->(Eof())

		cTipant := (cAliasTRB)->D2_TP
		cCodAnt	:= (cAliasTRB)->D2_COD
		nRec++

		While !(cAliasTRB)->(Eof()) .And. (cAliasTRB)->D2_TP + (cAliasTRB)->D2_COD == cTipant + cCodAnt

			Reclock(cAliasTRB,.F.)
				(cAliasTRB )->RECSMARTV := nRec
			(cAliasTRB)->( MsUnlock() )

			(cAliasTRB)->( DbSkip() ) 

		EndDo

	EndDo

	(cAliasTRB)->(DbGoTop())
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  calcMoed  ºRafael P. Rizzatto  ³Microsiga  º Data ³  07/10/04    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao de localizacoes que calcula a taxa da moeda de acordo    º±±
±±º          ³ com a  venda do produto. Nota de Credito/Cliente (Entrada)      º±±
±±º          ³ e Venda/Fatutamento (Saida).                                    º±±
±±º          ³                                                                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP7                                                             º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function calcMoed(cTab as character,nVal as numeric)

	Local aArea		as Array
	Local aAreaSF2	as Array
	Local aAreaSF1	as Array
	Local nValor	as Numeric
	Local nMoedaOri	as Numeric	

	If cTab == "SF2"
		aArea:=GetArea()
		aAreaSF2:=SF2->(GetArea())
		SF2->(dbSetOrder(1))
		SF2->(MsSeek(xFilial("SF2") + (cAliasTRB)->D2_DOC+(cAliasTRB)->D2_SERIE+(cAliasTRB)->D2_CLIENTE+(cAliasTRB)->D2_LOJA))
		nMoedaOri:=SF2->F2_MOEDA
		RestArea(aArea)
		SF2->(RestArea(aAreaSF2))
		nValor := xMoeda( nVal, nMoedaOri, jParams['MV_PAR15'][1], (cAliasTRB)->D2_EMISSAO )
	ElseIf cTab == "SF1"
		aArea:=GetArea()
		aAreaSF1:=SF1->(GetArea())
		SF1->(dbSetOrder(1))
		SF1->(MsSeek(xFilial("SF1")+(cAliasTRB)->D2_DOC+(cAliasTRB)->D2_SERIE+(cAliasTRB)->D2_CLIENTE+(cAliasTRB)->D2_LOJA+(cAliasTRB)->D2_TIPO,.F.))
		nMoedaOri:=SF1->F1_MOEDA
		RestArea(aArea)
		SF1->(RestArea(aAreaSF1))
		nValor := xMoeda( nVal, nMoedaOri, jParams['MV_PAR15'][1], (cAliasTRB)->D2_EMISSAO )
	EndIf

Return nValor
