#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.est.listofproductstoadress.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE SX1GRUPO "ESTT014"

namespace totvs.protheus.backoffice.est.listofproductstoadress.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SDA,SB1",name="Lista de Produtos a Endereçar", country="ALL")

//-------------------------------------------------------------------
/*{Protheus.doc} ListadeProdutosSmartViewBusinessObject  
Classe para criação do Objeto de Negócio de Prodotos
 
@author Squad Entradas
@since 08/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class ListofProductstoAdressSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object

	Protected Method oNGetSchema()

	Protected data aFieldsSDA as array
	Protected data aFieldsSB1 as array
	Protected data jItens  as json
	Protected data cWhere  as character
	Protected data cAlias  as character
	Protected data lExistPergunte as Logical

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@author Squad Entradas
@since 08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class ListofProductstoAdressSmartViewBusinessObject

	LOCAL cMsgSX1 as Character

	_Super:new()

	//Define a Área
	self:appendArea( STR0001)

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 )

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 )

	//Grupo de Perguntas
	self:lExistPergunte := self:SetPergunte(SX1GRUPO)
	If !self:lExistPergunte
		cMsgSX1 := OemToAnsi(I18N(STR0008,{SX1GRUPO})) //#Grupo de perguntas #1[SX1GRUPO]# nao encontrado. Verifique na tabela SX1, o grupo de perguntas informado
		self:setErrorStatus(400,STR0009,cMsgSX1)		//#Sem Pergunte
		FwLogMsg("WARN",, "SmartView ESTSV005",,,,cMsgSX1,,,)
	EndIf

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author BackOffice
@since 04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData( nPage as Numeric, oFilter as object) as object Class ListofProductstoAdressSmartViewBusinessObject

	local oQuery 	as object
	local cQuery	as character
	local cFields	as character
	local cDataDe	as character
	local cDataAte	as character
	local nX		as numeric
	local jParams	as json
	local cFiltro	as character
	local nFil		as numeric
	local cBkpFil	as Character

	If !self:lExistPergunte
		Return self:oData
	EndIf

	//metodo para retorno do json dos parâmetros
	jParams := oFilter:getParameters()

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'])

	//Retorna o próximo Alias disponível
	self:cAlias:= GetNextAlias()

	//Atualiza a variável cFields com os campos que farão parte do select
	cFields := ArrTokStr(self:aFieldsSDA,",") + ","
	cFields += ArrTokStr(self:aFieldsSB1,",")

	//Retorna o tipo do campos da SDA e SB1 convertidos para o Smart View
	self:aFieldsSDA := estGetCpo(self:aFieldsSDA)
	self:aFieldsSB1 := estGetCpo(self:aFieldsSB1)

	cDataDe  := FwDateTimeToLocal(jParams["MV_PAR05"][1])[1]
	cDataAte := FwDateTimeToLocal(jParams["MV_PAR06"][1])[1]

	cFiltro := " AND "

	cQuery := "SELECT ? "
	cQuery += "FROM " + RetSqlName("SDA") + " SDA "
	cQuery += "INNER JOIN " + RetSqlName("SB1") + " SB1 ON "
	cQuery += "SB1.B1_FILIAL= ? AND "
	cQuery += "SB1.B1_COD = SDA.DA_PRODUTO AND "
	cQuery += "SB1.D_E_L_E_T_ = ? "
	cQuery += "WHERE SDA.DA_FILIAL= ? AND "
	cQuery += "SDA.DA_PRODUTO >= ? AND "
	cQuery += "SDA.DA_PRODUTO <= ? AND "
	cQuery += "SDA.DA_LOCAL >= ? AND "
	cQuery += "SDA.DA_LOCAL <= ? AND "
	cQuery += "SDA.DA_DATA >= ? AND "
	cQuery += "SDA.DA_DATA <= ? AND "
	cQuery += "SDA.DA_SALDO > ? "

	//Os filtros serão setados na interface do novo TReports
	If !(oFilter:hasFilter())
		cFiltro := ""
	Endif

	cQuery += " AND SDA.D_E_L_E_T_ = ? "
	cQuery += self:cWhere
	cQuery += " ? "

	oQuery := FwExecStatement():New(cQuery)

	cBkpFil := cFilAnt

	//Colocar while ou for de filiais aqui
	For nFil :=1 to len(self:aFils)
		cFilAnt := self:aFils[nFil]

		oQuery:SetUnsafe(1, cFields)
		oQuery:SetString(2, FWxFilial("SB1"))
		oQuery:SetString(3, " ")
		oQuery:SetString(4, FWxFilial("SDA"))
		oQuery:SetString(5, jParams['MV_PAR01'][1])
		oQuery:SetString(6, jParams['MV_PAR02'][1])
		oQuery:SetString(7, jParams['MV_PAR03'][1])
		oQuery:SetString(8, jParams['MV_PAR04'][1])
		oQuery:SetString(9, Dtos(cDataDe))
		oQuery:SetString(10, Dtos(cDataAte))
		oQuery:SetNumeric(11, 0)
		oQuery:SetString(12, " ")
		oQuery:setUnsafe(13, cFiltro + oFilter:getSQLExpression())

		oQuery:OpenAlias(self:cAlias)

		(self:cAlias)->(dbGoTop())

		While !(self:cAlias)->(Eof())

			self:jItens := JsonObject():New()

			For nX := 1 To Len(self:aFieldsSB1)
				If self:aFieldsSB1[nX][ 02 ] == "date" //Campo tipo data tem um tratamento diferente para envio
					If !Empty( (self:cAlias)->&(self:aFieldsSB1[nX][ 01 ]) )
						self:jItens[self:aFieldsSB1[nX][ 01 ]] := totvs.framework.treports.date.dateToTimeStamp(StoD((self:cAlias)->&(self:aFieldsSB1[nX][01])))	
					EndIf
				Else
					self:jItens[self:aFieldsSB1[nX][ 01 ]] := (self:cAlias)->&(self:aFieldsSB1[nX][ 01 ])
				EndIf
			Next nX

			For nX := 1 To Len(self:aFieldsSDA)
				If self:aFieldsSDA[nX][ 02 ] == "date" //Campo tipo data tem um tratamento diferente para envio
					If !Empty( (self:cAlias)->&(self:aFieldsSDA[nX][ 01 ]) )	
						self:jItens[self:aFieldsSDA[nX][ 01 ]] := totvs.framework.treports.date.dateToTimeStamp(StoD((self:cAlias)->&(self:aFieldsSDA[nX][01])))
					EndIf
				Else
					self:jItens[self:aFieldsSDA[nX][ 01 ]] := (self:cAlias)->&(self:aFieldsSDA[nX][ 01 ])
				EndIf
			Next nX

			self:processData()
			self:appendData(self:jItens)

			(self:cAlias)->(DBSkip())
		EndDo

		(self:cAlias)->(DBCloseArea())
	Next

	cFilAnt := cBkpFil

	oQuery:Destroy()

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} oNGetSchema
Retorna a estrutura dos campos
@return object: self:oSchema
@author Squad Entradas
@since Agosto 28,2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method oNGetSchema() Class ListofProductstoAdressSmartViewBusinessObject

	self:aFieldsSDA := {"DA_FILIAL","DA_PRODUTO","DA_LOCAL","DA_QTDORI","DA_SALDO","DA_ORIGEM",;
		"DA_LOTECTL","DA_NUMLOTE","DA_DOC","DA_SERIE","DA_NUMSEQ","DA_DATA"}

	self:aFieldsSB1 := {"B1_DESC","B1_UCOM"}

	self:AliasToSchema("SDA",self:aFieldsSDA)
	self:AliasToSchema("SB1",self:aFieldsSB1)

Return

/*/{Protheus.doc} estGetCpo
Prepara a estrutura dos campos
@type  Função
@author Squad Entradas
@since  Agosto 28,2023
/*/
Static Function estGetCpo(aFields as array)

	local aDeParaCpo as array
	local aCpoTmp := {} as array
	local cTipR as character
	local cTipo as character
	local nPos as numeric
	local nX as numeric

	aDeParaCpo := {{"C", "string"}, {"D", "date"}, {"N", "number"}, {"L", "boolean"}}

	For nX := 1 To Len(aFields)
		cTipo := FWSX3Util():GetFieldType( aFields[nX] )
		cTipR := "string"
		If (nPos := aScan( aDeParaCpo, {|c| c[01] = cTipo } ) ) > 0
			cTipR := aDeParaCpo[nPos, 02]
		EndIf
		AAdd( aCpoTmp,;
			{aFields[nX],;
			cTipR})
	Next nX

Return (aCpoTmp)
