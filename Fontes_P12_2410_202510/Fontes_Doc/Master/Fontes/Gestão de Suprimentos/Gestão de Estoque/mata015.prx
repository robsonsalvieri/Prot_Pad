#INCLUDE "MATA015.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "msgraphi.ch"
#INCLUDE 'Colors.ch'
#INCLUDE 'FWMVCDEF.CH'
#INCLUDE "FWADAPTEREAI.CH"

PUBLISH MODEL REST NAME MATA015 SOURCE MATA015

//-----------------------------------------------------------------------------------------------------------
/* Browse da rotina de Cadastro de Endere็o
@author  	Jefferson Tomaz
@version 	P11 R11.7
@build		7.00.111010P
@since 	10/08/2012
@return 	*/
//-----------------------------------------------------------------------------------------------------------
Function MATA015( xRotAuto, nOpcAuto )

Local   cCadastro	:= OemToAnsi( STR0006 )	//"Localizao Fกsica"
Private aRotina	:= MenuDef()
Private aRotAuto	:= Nil

Default xRotAuto := Nil
Default nOpcAuto := 3
	
If xRotAuto == Nil
	oMBrowse:= FWMBrowse():New()	
	oMBrowse:SetAlias( "SBE" )
	oMBrowse:SetDescription( cCadastro )
	oMBrowse:Activate()
Else
	aRotAuto := xRotAuto
	FwMvcRotAuto( ModelDef(), "SBE", nOpcAuto, { { "MdFieldSBE", aRotAuto } } )  //Chamada da rotina automatica atrav้s do MVC
EndIf

Return

//-----------------------------------------------------------------------------------------------------------
/* Modelo de Dados 
@author  	Jefferson Tomaz
@version 	P11 R11.7
@build		7.00.111010P
@since 	10/08/2012
@return 	*/
//-----------------------------------------------------------------------------------------------------------
Static Function ModelDef()

Local oModel 		:= Nil
Local oStruSBE 	:= FwFormStruct( 1, "SBE" )
Local bPreValid	:= Nil // { |oMdl| A015Alert( , oMdl ) }
Local bPosValid	:= { |oModel| A015TOK( , oModel ) }
Local oEventWMS := MATA015EVWMS():New()

oModel := MpFormModel():New( "MATA015", /*bPre*/, bPosValid /*bPost*/, /*bCommit*/, /*bCancel*/ )
oModel:AddFields( "MdFieldSBE", Nil, oStruSBE, /*Pre-Validacao*/,/*Pos-Validacao*/,/*Carga*/ )
oModel:SetDescription( OemToAnsi( STR0006 ) ) //"Localizao Fกsica"
oModel:GetModel( "MdFieldSBE" ):SetDescription( OemToAnsi( STR0006 ) )
oModel:SetPrimaryKey( { "BE_FILIAL","BE_LOCAL","BE_LOCALIZ" } )

oModel:InstallEvent("WMSE015", , oEventWMS)

oModel:SetVldActivate( { |oModel| A015AVldA( oModel )  } )

Return( oModel )

//-----------------------------------------------------------------------------------------------------------
/* Retorna a View (tela) da rotina Cadastro de Endere็o 
@author  	Jefferson Tomaz
@version 	P11 R11.7
@build		7.00.111010P
@since 	10/08/2012
@return 	*/
//-----------------------------------------------------------------------------------------------------------
Static Function ViewDef()

Local oModel	:= FwLoadModel( "MATA015" )
Local oView	:= Nil
Local oStruSBE	:= FwFormStruct( 2,"SBE" )
Local lWmsNew  := SuperGetMv("MV_WMSNEW",.F.,.F.)
Local lIntWMS  := SuperGetMV('MV_INTWMS',.F.,.F.)

If !lWmsNew .And. !lIntWMS .And. WmsX312120("SBE","BE_NRUNIT") 
	oStruSBE:RemoveField('BE_NRUNIT')
EndIf

oView := FwFormView():New()

oView:SetModel( oModel )

oView:CreateHorizontalBox( 'Field'	, 100 )

If lIntWMS .And. X3Usado("BE_VALNV1")
	oStruSBE:SetProperty("BE_VALNV1",MVC_VIEW_CANCHANGE,.F.)
	oStruSBE:SetProperty("BE_VALNV2",MVC_VIEW_CANCHANGE,.F.)
	oStruSBE:SetProperty("BE_VALNV3",MVC_VIEW_CANCHANGE,.F.)
	oStruSBE:SetProperty("BE_VALNV4",MVC_VIEW_CANCHANGE,.F.)
	oStruSBE:SetProperty("BE_VALNV5",MVC_VIEW_CANCHANGE,.F.)
	oStruSBE:SetProperty("BE_VALNV6",MVC_VIEW_CANCHANGE,.F.)
EndIf

oView:AddField( 'VwFieldSBE', oStruSBE, 'MdFieldSBE'	)

oView:SetOwnerView( 'VwFieldSBE'	, 'Field'	)

Return( oView )

//===========================================================================================================
/* Retorna as opera็๕es disponiveis para o Cadastro de Endere็o.
@author  	Jefferson Tomaz
@version 	P11 R11.7
@build		7.00.111010P
@since 	10/08/2012
@return 	aRotina - Array com as op็oes de Menu */                                                                                                         
//===========================================================================================================
Static Function MenuDef()

Private aRotina := {}

aAdd( aRotina, { STR0001, "PesqBrw"          , 0, 1, 0, .T. } ) // Pesquisar
aAdd( aRotina, { STR0002, "VIEWDEF.MATA015"  , 0, 2, 0, .F. } ) // Visualizar
aAdd( aRotina, { STR0003, "VIEWDEF.MATA015"  , 0, 3, 0, Nil } ) // Incluir
aAdd( aRotina, { STR0004, "VIEWDEF.MATA015"  , 0, 4, 0, Nil } ) // Alterar
aAdd( aRotina, { STR0005, "VIEWDEF.MATA015"  , 0, 5, 3, Nil } ) // Excluir
aAdd( aRotina, { STR0017,  "A015Graph"		   , 0, 2, 0 ,Nil } ) // Ocupacao

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Ponto de entrada utilizado para inserir novas opcoes no array aRotina  ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If ExistBlock("MTA015MNU")
	ExecBlock("MTA015MNU",.F.,.F.)
EndIf

Return aRotina

//-----------------------------------------------------------------------------------------------------------
/* Executa validacao antes de excluir 
@author  	Jefferson Tomaz
@version 	P11 R11.7
@build		7.00.111010P
@since 	10/08/2012
@return 	*/
//-----------------------------------------------------------------------------------------------------------
Static Function A015AVldA( oMdl )

Local lRet := .T.
Local nOpc := oMdl:GetOperation()

If nOpc == 5
	If SBE->BE_STATUS $ "2|3|4|5|6"
		Help(" ",1,"MATA015001") //Este endere็o nใo pode ser excluํdo.
		lRet := .F.
	EndIf
	
	If lRet
		lRet := MA015CkSDB()
	Endif
EndIf
	
If lRet .And. FindFunction("WMSSBECpos")
	lRet :=	WMSSBECpos()
EndIf
	
Return( lRet ) 

//-----------------------------------------------------------------------------------------------------------
/* Executa validacao antes de incluir / alterar / excluir 
@author  	Jefferson Tomaz
@version 	P11 R11.7
@build		7.00.111010P
@since 	10/08/2012
@return 	*/
//-----------------------------------------------------------------------------------------------------------

Function A015TOK( nOpc, oMdl )

Local lRet   := .T.
Local lMTTOK := ExistBlock('MT015TOK')
Local cLocEnd := ""
Local lIntgMes := PCPIntgPPI()
Local lIntWms  := SuperGetMv("MV_WMSNEW",.F.,.F.) .Or. SuperGetMv("MV_INTWMS",.F.,.F.) // Valida integra็ใo com WMS ou Novo WMS

Default nOpc := oMdl:GetOperation()

If lIntgMes .And. lRet
	cLocEnd := oMdl:GetValue('MdFieldSBE','BE_LOCAL')+"+"+oMdl:GetValue('MdFieldSBE','BE_LOCALIZ')
	lRet := MATA015PPI(, cLocEnd, Iif(nOpc==5,.T.,.F.), .T., .F., oMdl)
EndIf

//-- Executa ponto de entrada para validacao
If	lMTTOK
	lRet := ExecBlock('MT015TOK',.F.,.F.,{nOpc})
	If	ValType(lRet) # "L"
		lRet := .T.
	EndIf
EndIf

Return( lRet )


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณA015Alert ณAutorณRodrigo de A. Sartorio ณ Data ณ 01/10/97 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ Pergunta se quer excluir local                           ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ MATA650                                                  ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function A015Alert(nOpc, oMdl)
Local lRet   := .F.
Local cLinha1:= STR0010+Alltrim(SBE->BE_LOCAL)+STR0011+Alltrim(SBE->BE_LOCALIZ)+STR0012	//"O almoxarifado "###" localizao "###" possui "
Local cLinha2:= STR0013	//"saldo em estoque . Excluir esta localizao impedir nova(s) "
Local cLinha3:= STR0014	//"distribuies para a mesma. "
Local cLinha4:= STR0015	//"Confirma excluso ?"

Default nOpc := oMdl:GetOperation()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Antes de deletar verificar se existe saldo na localizacao    ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If nOpc == 5
	dbSelectArea("SBF")
	If dbSeek(xFilial("SBF")+Alltrim(SBE->BE_LOCAL)+Alltrim(SBE->BE_LOCALIZ))
		TONE(3500,1)
		lRet := MsgYesNo(OemToAnsi(cLinha1+cLinha2+cLinha3+cLinha4),OemToAnsi(STR0016+Alltrim(SBE->BE_LOCAL)+" "+Alltrim(SBE->BE_LOCALIZ)))	//"Ateno - "
	Else
		lRet:=.T.
	EndIf
EndIf	

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณA015Graph บAutor  ณRodrigo Sartorio    บ Data ณ  09/01/02   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMonta Grafico de Preenchimento                              บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP6                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function A015Graph()

Local aAreaSBE   := SBE->(GetArea())
Local aAreaSBF   := SBF->(GetArea())
Local aObjects   := {}
Local aPosObj    := {}
Local aSize      := MsAdvSize(.F.)
Local aInfo      := {aSize[1], aSize[2], aSize[3], aSize[4], 3, 3}
Local cCorOcu    := ''
Local cCorDes    := ''
Local nTotOcu    := 0
Local nTotDes    := 0
Local oGraph
Local oPanelGr
Local oDlg

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณPadroniza as medidas da Janela ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
aAdd(aObjects, {100, 015, .T., .F., .F.})
aAdd(aObjects, {100, 100, .T., .T., .T.})
aAdd(aObjects, {100, 015, .T., .F., .F.})
aPosObj := MsObjSize(aInfo, aObjects)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณColeta dados para o Graficoณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
SBF->(dbSetOrder(1))
SBE->(dbSetOrder(1))
If SBE->(dbSeek(xFilial('SBE'), .F.))
	If Pergunte('MTA015', .T.)
		Processa({|lEnd| A015FiltGr(mv_par01, mv_par02, mv_par03, mv_par04, mv_par05, mv_par06, mv_par07, mv_par08, @nTotOcu, @nTotDes)}, STR0022) //'Aguarde, Coletando dados para o grafico...' //'Aguarde, Coletando dados para o grafico...'
		DEFINE MSDIALOG oDlg TITLE STR0018 OF oMainWnd PIXEL FROM aSize[7],0 TO aSize[6],aSize[5] //'Grafico de ocupacao dos enderecos'
		@ aPosObj[1,1],aPosObj[1,2] Say STR0019 OF oDlg PIXEL  //'Ocupacao dos Enderecos por Posicao'
		oPanelGr := TPanel():New(aPosObj[2,1],aPosObj[2,2],,oDlg,,,,,, aPosObj[2,3],  aPosObj[2,4])
		oGraph   := FWChartFactory():New()
		oGraph:SetOwner(oPanelGr)
		oGraph:AddSerie(STR0020,   nTotOcu ) //'Enderecos Ocupados'
		oGraph:AddSerie(STR0021,   nTotDes ) //'Enderecos Ocupados'
		oGraph:EnableMenu(.F.)
		oGraph:SetChartDefault(NEWPIECHART)
		oGraph:SetMask('*@*')
		oGraph:SetPicture("@E 999,999,999.99")
		oGraph:setLegend(CONTROL_ALIGN_RIGHT)
		oGraph:Activate()
		DEFINE SBUTTON FROM aPosObj[3,1],aPosObj[3,4]-30 ACTION oDlg:End() ENABLE OF oDlg PIXEL
		ACTIVATE MSDIALOG oDlg
	EndIf	
Else
	Aviso('MATA015', STR0023+xFilial('SBE')+STR0024, {'Ok'}) //'Nao existem registros da filial '###' no Cadastro de Enderecos.'
EndIf	

RestArea(aAreaSBF)
RestArea(aAreaSBE)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณA015FiltGrบAutor  ณFernando Joly       บ Data ณ  17/07/2002 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFiltra os dados para a montagem do Grafico                  บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP6                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function A015FiltGr(cArmDe, cArmAte, cZonaDe, cZonaAte, cEstFDe, cEstFAte, cEndDe, cEndAte, nTotOcu, nTotDes)

Local aAreaAnt   := GetArea()
Local aAreaSBE   := SBE->(GetArea())
Local lMT15FGRA  := ExistBlock('MT15FGRA')
Local lRetPE     := .F.
Local lWmsNew    := SuperGetMv("MV_WMSNEW",.F.,.F.)

Default cArmDe     := Space(TamSX3('BE_LOCAL')[1])
Default cArmAte    := Replicate(TamSX3('BE_LOCAL')[1], 'z')
Default cZonaDe    := Space(TamSX3('BE_CODZON')[1])
Default cZonaAte   := Replicate(TamSX3('BE_CODZON')[1], 'z')
Default cEstFDe    := Space(TamSX3('BE_ESTFIS')[1])
Default cEstFAte   := Replicate(TamSX3('BE_ESTFIS')[1], 'z')
Default cEndDe     := Space(TamSX3('BE_LOCALIZ')[1])
Default cEndAte    := Replicate(TamSX3('BE_LOCALIZ')[1], 'z')

dbSelectArea('SBE')
ProcRegua(LastRec())
dbSetOrder(3)
dbSeek(xFilial('SBE')+cArmDe+cZonaDe, .T.)
While !Eof() .And. BE_FILIAL == xFilial('SBE') .And. BE_LOCAL+BE_CODZON <= cArmAte+cZonaAte
	IncProc(STR0022) //'Aguarde, Coletando dados para o grafico...' //'Aguarde, Coletando dados para o grafico...'
	//-- Filtra Estrutura Fisica e Endereco
	If BE_ESTFIS < cEstFDe .Or. BE_ESTFIS > cEstFAte .Or. ;
		BE_LOCALIZ < cEndDe .Or. BE_LOCALIZ > cEndAte
		dbSkip()
		Loop	
	EndIf	
 	If SBF->(dbSeek(xFilial('SBF')+SBE->BE_LOCAL+SBE->BE_LOCALIZ, .F.))
		//-- Ponto de Entrada para Fitragem
		If lMT15FGRA .And. ValType(lRetPE:=ExecBlock('MT15FGRA', .F., .F.))=='L' .And. !lRetPE
			dbSkip()
			Loop
		EndIf
		nTotOcu++
	ElseIf lWmsNew .And. D14->(dbSeek(xFilial('D14')+SBE->BE_LOCAL+SBE->BE_LOCALIZ, .F.))
		//-- Ponto de Entrada para Fitragem
		If lMT15FGRA .And. ValType(lRetPE:=ExecBlock('MT15FGRA', .F., .F.))=='L' .And. !lRetPE
			dbSkip()
			Loop
		EndIf
		nTotOcu++	
	Else
		//-- Ponto de Entrada para Fitragem
		If lMT15FGRA .And. ValType(lRetPE:=ExecBlock('MT15FGRA', .F., .F.))=='L' .And. !lRetPE
			dbSkip()
			Loop
		EndIf
		nTotDes++
	EndIf

	dbSkip()
EndDo 

RestArea(aAreaSBE)
RestArea(aAreaAnt)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณMATVERCOR บAutor  ณFernando Joly       บ Data ณ  17/07/2002 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMostra uma tabela de cores com base na include COLOR.CH     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณRetorna um String com o Numero referente a posicao da cor   บฑฑ
ฑฑบ          ณno Array.                                                   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP6                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function MatVerCor(cCorAnt)

Local aCores     := {	{'CLR_BLACK'	, STR0025}, ; //'Preto          '
						{'CLR_BLUE'		, STR0026}, ; //'Azul escuro    '
						{'CLR_GREEN'	, STR0027}, ; //'Verde escuro   '
						{'CLR_CYAN'		, STR0028}, ; //'Ciano escuro   '
						{'CLR_RED'		, STR0029}, ; //'Vermelho escuro'
						{'CLR_MAGENTA'	, STR0030}, ; //'Magenta escuro '
						{'CLR_BROWN'	, STR0031}, ; //'Marrom         '
						{'CLR_HGRAY'	, STR0032}, ; //'Cinza          '
						{'CLR_LIGHTGRAY', STR0033}, ; //'Cinza claro    '
						{'CLR_GRAY'		, STR0034}, ; //'Cinza escuro   '
						{'CLR_HBLUE'	, STR0035}, ; //'Azul           '
						{'CLR_HGREEN'	, STR0036}, ; //'Verde          '
						{'CLR_HCYAN'	, STR0037}, ; //'Ciano          '
						{'CLR_HRED'		, STR0038}, ; //'Vermelho       '
						{'CLR_HMAGENTA'	, STR0039}, ; //'Magenta        '
						{'CLR_YELLOW'	, STR0040}, ; //'Amarelo        '
						{'CLR_WHITE'	, STR0041}}	  //'Branco         '
Local cRet       := ''
Local nPosAnt    := 0

Default cCorAnt  := '01'

nPosAnt := If((nPosAnt:=Val(cCorAnt))<1,1,If(nPosAnt>17,17,nPosAnt))

aCores := {	{'BR_PRETO'	 , STR0025}, ; //'Preto          '
				{'BR_AZUL'   , STR0026}, ; //'Azul escuro    '
				{'ENABLE'    , STR0027}, ; //'Verde escuro   '
				{'BR_PINK'   , STR0028}, ; //'Ciano escuro   '
				{'DISABLE'   , STR0029}, ; //'Vermelho escuro'
				{'BR_PINK'   , STR0030}, ; //'Magenta escuro '
				{'BR_MARRON' , STR0031}, ; //'Marrom         '
				{'BR_CINZA'  , STR0032}, ; //'Cinza          '
				{'BR_CINZA'  , STR0033}, ; //'Cinza claro    '
				{'BR_CINZA'  , STR0034}, ; //'Cinza escuro   '
				{'BR_AZUL'   , STR0035}, ; //'Azul           '
				{'BR_VERDE'  , STR0036}, ; //'Verde          '
				{'BR_PINK'   , STR0037}, ; //'Ciano          '
				{'DISABLE'   , STR0038}, ; //'Vermelho       '
				{'BR_PINK'   , STR0039}, ; //'Magenta        '
				{'BR_AMARELO', STR0040}, ; //'Amarelo        '
				{'DISABLE'   , STR0041}}   //'Branco         '

cRet := MTA015TCor(cCadastro, STR0042, aCores) //'Cores Disponiveis'

Return cRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณMATRETCOR บAutor  ณFernando Joly       บ Data ณ  17/07/2002 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบRetorno   ณRetorna o STR da cor com base na posicao do Array de cores  บฑฑ
ฑฑบ          ณgerado pela funcao MATVERCOR.                               บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP6                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function MatRetCor(cCor)

Local aCores     := {	CLR_BLACK		, ;	//-- 01 - Preto
						CLR_BLUE		, ;	//-- 02 - Azul
						CLR_GREEN		, ;	//-- 03 - Verde
						CLR_CYAN		, ;	//-- 04 - Ciano
						CLR_RED			, ;	//-- 05 - Vermelho
						CLR_MAGENTA		, ;	//-- 06 - Magenta
						CLR_BROWN		, ;	//-- 07 - Marrom
						CLR_HGRAY		, ;	//-- 08 - Cinza escuro
						CLR_LIGHTGRAY	, ;	//-- 09 - Cinza claro
						CLR_GRAY		, ;	//-- 10 - Cinza
						CLR_HBLUE		, ;	//-- 11 - Azul escuro
						CLR_HGREEN		, ;	//-- 12 - Verde escuro
						CLR_HCYAN		, ;	//-- 13 - Ciano escuro
						CLR_HRED		, ;	//-- 14 - Vermelho escuro
						CLR_HMAGENTA	, ;	//-- 15 - Magenta escuro
						CLR_YELLOW		, ;	//-- 16 - Amarelo
						CLR_WHITE}       	//-- 17 - Branco
Local cRet       := ''
Local nPos       := 0

Default cCor       := '01'

nPos    := If((nPos:=Val(cCor))<1,1,If(nPos>17,17,nPos))
cRet    := aCores[nPos]

Return cRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFunao    ณMTA015TCor   ณRev.  ณFernando Joly Siquiniณ Data ณ23/07/2002ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณJanela contendo uma legenda das cores para mBrowse.         ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณSintaxe   ณMTA015TCor(cTitulo,cMensagem,aCores)                        ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametrosณcTitulo    :Titulo da Janela                                ณฑฑ
ฑฑณ          ณcMensagem  :Mensagem exibida                                ณฑฑ
ฑฑณ          ณaCores     :Array contendo as cores                         ณฑฑ
ฑฑณ          ณnXSize     :Tamanho do Bitmap para ajuste da mensagem       ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function MTA015TCor(cTitulo,cMensagem,aCores,nXSize)

Local nY         := 0
Local nX         := 0
Local aBmp[Len(aCores)]
Local aSays[Len(aCores)]
Local cRet       := ''
Local oDlgLeg

Default  nXSize    := 14

DEFINE MSDIALOG oDlgLeg FROM 0,0 TO ((Len(aCores)*20)+50),304 TITLE cTitulo Of oMainWnd PIXEL
oDlgLeg:bLClicked := {||oDlgLeg:End()}
DEFINE FONT oBold NAME 'Arial' SIZE 0, -13 BOLD
@ 00, 00 BITMAP oBmp RESNAME 'PROJETOAP' oF oDlgLeg SIZE 35, 155 NOBORDER WHEN .F. PIXEL
@ 11, 35 TO 13, 400 LABEL '' OF oDlgLeg PIXEL
@ 03, 37 SAY cMensagem Of oDlgLeg PIXEL SIZE 35, 9 FONT oBold
For nX := 1 to Len(aCores)
	@ 19+((nX-1)*10), 44 BITMAP aBmp[nX] RESNAME aCores[nX, 1] of oDlgLeg SIZE 20, 20 NOBORDER WHEN .F. PIXEL
	@ 19+((nX-1)*10), ((nXSize/2)+47) SAY If((nY+=1)==nY, aCores[nY, 2]+If(nY==Len(aCores), If((nY:=0)==nY, '', ''), ''), '') of oDlgLeg PIXEL
Next nX
ACTIVATE MSDIALOG oDlgLeg CENTERED

Return cRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณMA015CkSDBบAutor  ณNereu Humberto Jr.  บ Data ณ  19/10/07   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Verifica se existe movimento com o endereco apos o fecha-  บฑฑ
ฑฑบ          ณ mento e nao deixa excluir.                                 บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ MATA015                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Static Function MA015CkSDB()
Local aArea     := GetArea()
Local cAliasSDB := ""
Local cQuery    := ""
Local dDataFec  := MVUlmes()
Local cLocal    := SBE->BE_LOCAL
Local cLocaliz  := SBE->BE_LOCALIZ
Local lRet      := .T.
Local lWmsNew   := SuperGetMv("MV_WMSNEW",.F.,.F.)

dbSelectArea("SDB")
dbSetOrder(1)

If !lWmsNew .Or. (Empty(SBE->BE_CODZON) .And. Empty(SBE->BE_ESTFIS))
	dbSelectArea("SDB")
	dbSetOrder(1)

	cAliasSDB := GetNextAlias()
		
	cQuery += "SELECT 1 "		
	cQuery += "FROM " + RetSqlName( "SDB" ) + " SDB "
	cQuery += "WHERE "
	cQuery += "DB_FILIAL ='" + xFilial( "SDB" ) + "' AND "
	cQuery += "DB_DATA > '" + DTOS(dDataFec) + "' AND "
	cQuery += "DB_LOCAL = '" + cLocal + "' AND "
	cQuery += "DB_LOCALIZ = '" + cLocaliz + "' AND "
	cQuery += "DB_ESTORNO = ' ' AND "
	cQuery += "SDB.D_E_L_E_T_=' ' "
		
	cQuery := ChangeQuery( cQuery )
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSDB,.F.,.T. )
		
	If (cAliasSDB)->(!Eof())
		lRet := .F.
		Help(" ",1,"MATA015E",,STR0051,1,0) //Este endere็o nใo pode ser excluido porque possui movimenta็๕es com data posterior ao ultimo fechamento.	
	Endif
			
	dbSelectArea(cAliasSDB)
	dbCloseArea()

	dbSelectArea("SDB")
	dbSetOrder(1)	
EndIf
	
RestArea(aArea)

Return(lRet)

//-------------------------------------------------------------------------------------------------
/*/{Protheus.doc} MATA015PPI

Realiza a integra็ใo com o PC-Factory - PPI Multitask

@param cXml      - XML que serแ enviado. Caso nใo seja passado esse parametro, serแ realizada
                   a chamada do Adapter para cria็ใo do XML.
                   Se for passado esse parโmetro, nใo serแ exibida a mensagem de erro caso exista,
                   nem serแ considerado o filtro da tabela SOE. 
@param cLocEnd   - Obrigat๓rio quando utilizado o parโmetro cXml. Cont้m o c๓digo do local e o c๓digo do endere็o. ('01+CODENDERECO')
@param lExclusao - Indica se estแ chamando para rotina de exclusใo de produto.
@param lFiltra   - Identifica se serแ realizado ou nใo o filtro do registro.
@param lPendAut  - Indica se serแ gerada a pend๊ncia sem realizar a pergunta para o usuแrio, caso ocorra algum erro.

@author  Lucas Konrad Fran็a
@version P118
@since   19/04/2016
@return  lRet  - Indica se a integra็ใo com o PC-Factory foi realizada. 
           .T. -> Integra็ใo Realizada
           .F. -> Integra็ใo nใo realizada.
/*/
//-------------------------------------------------------------------------------------------------
Function MATA015PPI(cXml, cLocEnd, lExclusao, lFiltra, lPendAut, oModel)
   Local aArea     := GetArea()
   Local lRet      := .T.
   Local aRetXML   := {}
   Local aRetWS    := {}
   Local aRetData  := {}
   Local aRetArq   := {}
   Local cNomeXml  := ""
   Local cGerouXml := ""
   Local cOperacao := ""
   
   Local cEndereco := ""
   Local cLocal    := ""
   
   //Variแvel utilizada para identificar que estแ sendo executada a integra็ใo para o PPI dentro do MATI200.
   Private lRunPPI := .T.
   //Variแvel utilizada no filtro
   Private oModelSBE
   
   Default cXml      := ""
   Default cLocEnd   := ""
   Default lExclusao := .F.
   Default lFiltra   := .T.
   Default lPendAut  := .F.
   Default oModel    := Nil

	If !Empty(oModel)
		oModelSBE := oModel:GetModel("MdFieldSBE")
	EndIf

   If Empty(cXml)
      If lExclusao 
         cOperacao := Lower(STR0005) //"excluir"
      Else
         If INCLUI
            cOperacao := Lower(STR0003) //"incluir"
         Else
            cOperacao := Lower(STR0004) //"alterar"
         EndIf
      EndIf
   Else
      If PCPEvntXml(cXml) == "delete"
         lExclusao := .T.
      EndIf
   EndIf

   If Empty(cXml)
      cEndereco := oModel:GetValue("MdFieldSBE","BE_LOCALIZ")
      cLocal    := oModel:GetValue("MdFieldSBE","BE_LOCAL")
   Else
      cLocal    := PadR(StrTokArr(cLocEnd,"+")[1], TAMSX3("BE_LOCAL")[1])
      cEndereco := PadR(StrTokArr(cLocEnd,"+")[2], TAMSX3("BE_LOCALIZ")[1])
   EndIf

   //Realiza filtro na tabela SOE, para verificar se o produto entra na integra็ใo.
   If !Empty(cXml) .Or. !lFiltra .Or. PCPFiltPPI("SBE", cLocal+"+"+cEndereco,"oModelSBE:GetValue('?')")
      //Adapter para cria็ใo do XML
      If Empty(cXml)
         MATI015MOD(oModel)
         //aRetXML := FwIntegDef("MATA015",,,,"MATA015")
		 aRetXML := MATI015("", TRANS_SEND, EAI_MESSAGE_BUSINESS)
         MATI015MOD(Nil)
      Else
         aRetXML := {.T.,cXml}
      EndIf
      /*
         aRetXML[1] - Status da cria็ใo do XML
         aRetXML[2] - String com o XML
      */
      If aRetXML[1]
         //Retira os caracteres especiais
         aRetXML[2] := EncodeUTF8(aRetXML[2])
      
         //Busca a data/hora de gera็ใo do XML
         aRetData := PCPxDtXml(aRetXML[2])
         /*
            aRetData[1] - Data de gera็ใo AAAAMMDD
            aRetData[1] - Hora de gera็ใo HH:MM:SS
         */
      
         //Envia o XML para o PCFactory
         aRetWS := PCPWebsPPI(aRetXML[2])
         /*
            aRetWS[1] - Status do envio (1 - OK, 2 - Pendente, 3 - Erro.)
            aRetWS[2] - Mensagem de retorno do PPI
         */
      
         If aRetWS[1] != "1" .And. Empty(cXml)
            If lPendAut
               lRet := .T.
            Else
	            //"Aten็ใo! Ocorreram erros na integra็ใo com o TOTVS MES. Erro: "
	            // XXXXXX
	            // XXXXXX
	            // "Deseja incluir/alterar/excluir o endere็o no protheus e gerar pend๊ncia para integra็ใo?"
	            If !MsgYesNo(STR0052 + AllTrim(aRetWS[2]) + CHR(10)+;
	                         STR0053 + AllTrim(cOperacao) + STR0054)
	               lRet := .F.
	            EndIf
	         EndIf
         EndIf
         
         If lRet
            //Cria o XML fisicamente no diret๓rio parametrizado
            aRetArq := PCPXmLPPI(aRetWS[1],"SBE",cLocal+"+"+cEndereco,aRetData[1],aRetData[2],aRetXML[2])
            /*
               aRetArq[1] Status da cria็ใo do arquivo. .T./.F.
               aRetArq[2] Nome do XML caso tenha criado. Mensagem de erro caso nใo tenha criado o XML.
            */
            If !aRetArq[1]
               If Empty(cXml) .And. !lPendAut
                  Alert(aRetArq[2])
               EndIf
            Else
               cNomeXml := aRetArq[2]
            EndIf
            If Empty(cNomeXml)
               cGerouXml := "2"
            Else
               cGerouXml := "1"
            EndIf
            //Cria a tabela SOF
            PCPCriaSOF("SBE",cLocal+"+"+cEndereco,aRetWS[1],cGerouXml,cNomeXml,aRetData[1],aRetData[2],__cUserId,aRetWS[2],aRetXML[2])
            //Array com os componentes que tiveram erro.
            If Type('aIntegPPI') == "A"
               If aRetWS[1] != "1"
                  aAdd(aIntegPPI,{cLocal+"+"+cEndereco,aRetWS[2]})
               EndIf
            EndIf
         EndIf
      EndIf
   EndIf
   //Tratativa para retornar .F. mesmo quando ้ pend๊ncia automแtica;
   //Utilizado apenas para o programa de sincroniza็ใo.
   If (AllTrim(FunName()) == "PCPA111") .And. Len(aRetWs) > 0 .And. aRetWS[1] != "1"
      lRet := .F.
   EndIf
   RestArea(aArea)
Return lRet	
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษอออออออออออัออออออออออหอออออออัออออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบ Function  ณ IntegDef บ Autor ณ Paulo V. Beraldo     บ Data ณ  Out/2018   บฑฑ
ฑฑฬอออออออออออุออออออออออสอออออออฯออออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบ Descricao ณ Funcao de tratamento para o recebimento/envio de mensagem    บฑฑ
ฑฑบ           ณ unica de cadastro de produtos.                               บฑฑ
ฑฑฬอออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ Uso       ณ MATA010                                                      บฑฑ
ฑฑศอออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function IntegDef( xEnt, nTypeTrans, cTypeMessage, cVersion, cTransac, lEAIObj )
Default xEnt := ""
Default nTypeTrans := ""
Default cTypeMessage := ""
Default cVersion := ""
Default cTransac := ""
Default lEAIObj := .F.
Return MATI015(xEnt,nTypeTrans,cTypeMessage, cVersion, cTransac, lEAIObj )
