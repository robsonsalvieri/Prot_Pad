#INCLUDE "msgraphi.ch"
#INCLUDE "DBTREE.CH"
#INCLUDE "MATC015.CH"
#INCLUDE "PROTHEUS.CH"

Static cRevEstru

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATC015  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 27/11/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consulta Ambiente de Producao                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION MATC015 
LOCAL aAlias:={}
Private l240:=.T. // Utilizado X3_RELACAO do campo D3_DESCRI
PRIVATE cCadastro:= STR0001 //"Consulta Ambiente de Producao"
PRIVATE aRotina := MenuDef()

pergunte("MTC015",.T.)
SetKey( VK_F12, { || pergunte("MTC015",.T.) } )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
mBrowse( 6, 1,22,75,"SB1",,,,,2)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Desativa tecla F12                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Set Key VK_F12	To

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Recupera a Ordem Original do arquivo principal               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB1")
dbSetOrder(1)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MC015Con  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 27/11/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta a dialog da consulta                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATC015                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MC015Con(cAlias,nReg,nOpc)
LOCAL oTree
LOCAL oDlg
LOCAL oFont
LOCAL oPanel
LOCAL oBmp
LOCAL aSize   := MsAdvSize()
LOCAL nTop    := 23
LOCAL nLeft   := 5
LOCAL nBottom := aSize[6]
LOCAL nRight  := aSize[5]
LOCAL bChange := {|| IIF(Val(SubStr(oTree:GetCargo(),6,12))<>0,Eval({||(SubStr(oTree:GetCargo(),3,3))->(MsGoto(Val(SubStr(oTree:GetCargo(),6,12)))),RegToMemory(SubStr(oTree:GetCargo(),3,3),.F.)}),Nil),MTC015DlgV(@oTree,{0,0,((nBottom-nTop)/2)-26,(nRight-nLeft)/2-160},@oPanel,aTotais) }
LOCAL aTotais :={}
LOCAL aTitles := {STR0005,STR0006}     //"Dados"###"Legenda"
LOCAL aPages  := {"HEADER","HEADER"}
LOCAL nOpcx   :=aRotina[nOpc,4]

If (nOpcx == 5.And. pergunte("MTC015",.T.)) .Or. nOpcx == 2
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Forca os parametros para utilizar somente produto posicionado³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nOpcx == 2
		mv_par01:=SB1->B1_COD
		mv_par02:=SB1->B1_COD
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica as perguntas selecionadas                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para parametros                   	      		³
	//³ mv_par01        // De  Produto                          	     	³
	//³ mv_par02        // Ate Produto                              	 	³
	//³ mv_par03        // De  Armazem                               		³
	//³ mv_par04        // Ate Armazem                               		³
	//³ mv_par05        // De  OP                                    		³
	//³ mv_par06        // Ate OP                                    		³
	//³ mv_par07        // De  Data Prev Inicio                      		³
	//³ mv_par08        // Ate Data Prev Inicio                      		³
	//³ mv_par09        // Considera OPs Firmes / Previstas / Ambos			³
	//³ mv_par10        // Mostra Roteiro Oper. Padrao / Todos       		³
	//³ mv_par11        // Apresenta dados da  Estrutura / Pre-estrutura    |
	//³ mv_par12        // Tipo de custo    Standard / Medio				³
	//³ mv_par13        // De Data Producao               					³
	//³ mv_par14        // Ate Data Producao               					³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Carrega as variaveis de memoria do SB1               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SB1")
	RegToMemory("SB1",.F.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta a tela para a consulta                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DEFINE FONT oFont NAME "Arial" SIZE 0, -10
	DEFINE MSDIALOG oDlg TITLE cCadastro OF oMainWnd PIXEL FROM nTop,nLeft TO nBottom,nRight
	//ÚÄÄÄÄÄÄ¿
	//³Folder³
	//ÀÄÄÄÄÄÄÙ
	oFolder := TFolder():New(12,0,aTitles,aPages,oDlg,,,,.T.,.F.,nRight-nLeft-420,nBottom-nTop-12)
	oFolder:aDialogs[1]:oFont := oDlg:oFont
	//ÚÄÄÄÄÄ¿
	//³Panel³
	//ÀÄÄÄÄÄÙ
	oPanel := TPanel():New(2,160,'',oFolder:aDialogs[1], oDlg:oFont, .T., .T.,, ,(nRight-nLeft)/2-160,((nBottom-nTop)/2)-25,.T.,.T. )
	lOneColumn := If((nRight-nLeft)/2-178>312,.F.,.T.)
	//ÚÄÄÄ¿
	//³Get³
	//ÀÄÄÄÙ
	MsMGet():New("SB1",SB1->(RecNo()),2,,,,,{0,0,((nBottom-nTop)/2)-25,(nRight-nLeft)/2-160},,3,,,,oPanel,,.T.,lOneColumn)
	//ÚÄÄÄÄ¿
	//³Tree³
	//ÀÄÄÄÄÙ
	oTree := dbTree():New(2, 2,((nBottom-nTop)/2)-24,159,oFolder:aDialogs[1],,,.T.)
	oTree:bChange := {|| Eval(bChange)}
	oTree:SetFont(oFont)
	oTree:lShowHint:= .F.
	MTC015Tree(@oTree,aTotais)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Informacoes da Legenda³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	@ 10,08 BITMAP oBmp RESNAME "PMSTASK6" Of oFolder:aDialogs[2] Size 9,9 Pixel NoBorder
	@ 10,23 SAY STR0007 Of oFolder:aDialogs[2] Size 50,50 Pixel //"Produtos"
	@ 23,08 BITMAP oBmp RESNAME "PMSTASK4" Of oFolder:aDialogs[2] Size 9,9 Pixel NoBorder
	@ 23,23 SAY STR0008 Of oFolder:aDialogs[2] Size 70,50 Pixel //"Componentes / Empenhos"
	@ 36,08 BITMAP oBmp RESNAME "PMSTASK1" Of oFolder:aDialogs[2] Size 9,9 Pixel NoBorder
	@ 36,23 SAY STR0025 Of oFolder:aDialogs[2] Size 70,50 Pixel //"Consumos"
	@ 49,08 BITMAP oBmp RESNAME "ROTEIRO" Of oFolder:aDialogs[2] Size 9,9 Pixel NoBorder
	@ 49,23 SAY STR0009 Of oFolder:aDialogs[2] Size 65,50 Pixel //"Roteiros de Operacao"
	@ 62,08 BITMAP oBmp RESNAME "CLOCK01" Of oFolder:aDialogs[2] Size 9,9 Pixel NoBorder
	@ 62,23 SAY STR0010 Of oFolder:aDialogs[2] Size 50,50 Pixel //"Operacoes"
	@ 75,08 BITMAP oBmp RESNAME "ENGRENAGEM" Of oFolder:aDialogs[2] Size 9,9 Pixel NoBorder
	@ 75,23 SAY STR0011 Of oFolder:aDialogs[2] Size 50,50 Pixel //"Recursos"
	@ 88,08 BITMAP oBmp RESNAME "INSTRUME" Of oFolder:aDialogs[2] Size 9,9 Pixel NoBorder
	@ 88,23 SAY STR0012 Of oFolder:aDialogs[2] Size 50,50 Pixel //"Ferramentas"
	@ 101,08 BITMAP oBmp RESNAME "PMSDOC" Of oFolder:aDialogs[2] Size 9,9 Pixel NoBorder
	@ 101,23 SAY STR0013 Of oFolder:aDialogs[2] Size 50,50 Pixel //"Ordens de Producao"
	@ 114,08 BITMAP oBmp RESNAME "relacionamento_direita" Of oFolder:aDialogs[2] Size 9,9 Pixel NoBorder
	@ 114,23 SAY STR0050 Of oFolder:aDialogs[2] Size 50,50 Pixel //"Estruturas"
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||oDlg:End()},{||oDlg:End()})
	Release Object oTree
EndIf
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MTC015DlgV³ Autor ³Rodrigo de A. Sartorio ³ Data ³ 27/11/03   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao que mostra as informacoes detalhadas da consulta       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATC015                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MtC015DlgV(oTree,aPos,oPanel,aTotais)
Local aDados    := {}
Local aArea		:= GetArea()
Local cAlias	:= SubStr(oTree:GetCargo(),3,3)
Local nRecView	:= Val(SubStr(oTree:GetCargo(),6,12))
Local cSeekTot  := Substr(oTree:GetCargo(),1,5)
Local cSeekComp := Substr(oTree:GetCargo(),18,60)
Local nPostotais:= Ascan(aTotais,{|x| x[1]== cSeekTot .And. x[2] == cSeekComp})
Local lOneColumn:= If(aPos[4]-aPos[2]>312,.F.,.T.)
Local nTop      := oMainWnd:nTop+23
Local nLeft     := oMainWnd:nLeft+5
Local nBottom   := oMainWnd:nBottom-60
Local nRight    := oMainWnd:nRight-10
Local oFWLayer  := Nil
Local oFWChart01:= Nil
Local oScroll,oGraphic,oBold,oTree2
Local nze,nSerie,nSerie2 
Local oPanelGr

cRevEstru := If(cRevEstru == NIL, CriaVar('G5_REVISAO'), cRevEstru)

oPanel:Hide()
MsFreeObj(@oPanel,.T.) 

If nRecView <> 0
	dbSelectArea(cAlias)
	MsGoto(nRecView)
	RegtoMemory(cAlias,.F.)
	MsMGet():New(cAlias,(cAlias)->(RecNo()),2,,,,,aPos,,3,,,,oPanel,,.T.,lOneColumn)
Else
	If nPosTotais > 0
		Do Case
			// Adiciona totalizador roteiros por produto
			Case cSeekTot == "20SG2"
				AADD(aDados,{STR0014}) //"Total de roteiros para esse produto"
				AADD(aDados,{Str(aTotais[nPosTotais,3])})
				C040MatScrDisp(aDados,@oScroll,@oPanel,aPos,,{{2,CLR_RED}})
			Case cSeektot == "21SG2"
				AADD(aDados,{STR0015}) //"Total de operacoes para esse roteiro"
				AADD(aDados,{Str(aTotais[nPosTotais,3])})
				C040MatScrDisp(aDados,@oScroll,@oPanel,aPos,,{{2,CLR_RED}})
			Case cSeektot == "21GRP" //"Painel de custos de operações para esse roteiro" 
				If !A015FWGrf(aTotais[nPosTotais][3],,oPanel,cSeekTot,aPos,,aTotais[nPostotais][4])	
					AADD(aDados,{STR0022}) //"Nao existem totais para essa consulta."
					C040MatScrDisp(aDados,@oScroll,@oPanel,aPos,{{1,CLR_RED}})
				EndIf
			Case cSeektot == "50SGF"
				AADD(aDados,{STR0016}) //"Total de componentes para essa operacao"
				AADD(aDados,{Str(aTotais[nPosTotais,3])})
				C040MatScrDisp(aDados,@oScroll,@oPanel,aPos,,{{2,CLR_RED}})
			Case cSeekTot == "60SC2"
				AADD(aDados,{STR0017,Str(aTotais[nPosTotais,3])}) //"Total de ordens de producao para esse produto"
				AADD(aDados,{"",""})
				AADD(aDados,{STR0018,Transform(aTotais[nPosTotais,4],PesqPict("SC2","C2_QUANT"))})  //"Quantidade Total"
				AADD(aDados,{STR0019,Transform(aTotais[nPosTotais,5],PesqPict("SC2","C2_QUANT"))})  //"Quantidade ja entregue"
				AADD(aDados,{STR0020,Transform(aTotais[nPosTotais,6],PesqPict("SC2","C2_QUANT"))})  //"Quantidade perdida"
				C040MatScrDisp(aDados,@oScroll,@oPanel,aPos,{{2,CLR_RED}})
			Case cSeekTot == "70SD3"
				AADD(aDados,{STR0052,Str(aTotais[nPosTotais,3])}) //"Total de itens consumidos"
				C040MatScrDisp(aDados,@oScroll,@oPanel,aPos,{{2,CLR_RED}})
			Case cSeekTot == "80SD4"
				AADD(aDados,{STR0021,Str(aTotais[nPosTotais,3])}) //"Total de itens empenhados para a OP"
				C040MatScrDisp(aDados,@oScroll,@oPanel,aPos,{{2,CLR_RED}})
			Case cSeekTot == "91GRP"
				oFWLayer := FWLayer():New()
    			oFWLayer:init( oPanel, .F. )
				oFWLayer:addCollumn( "Col02", 100,.T. )
				oFWLayer:addWindow( "Col02", "Win02", STR0027,70, .F., .F. ) //"Qtd Total X Perda X Apontamento"
			
				A015Chart(@oFWChart01,@oFWLayer, 02 /*PIECHART*/,"Col02","Win02",,"")
				// Monta o Grafico
				A650PieChart(@oFWChart01,aTotais[nPosTotais,3,1],aTotais[nPosTotais,3,3],aTotais[nPosTotais,3,2],aTotais[nPosTotais,3,4])
			Case cSeekTot == "92GRP"
				DEFINE FONT oBold NAME "Arial" SIZE 0,-14 BOLD
				
				oPanelGr := TPanel():New(aPos[1],aPos[2],,oPanel,,,,,, aPos[4]-aPos[2],(aPos[3]-aPos[1])-12)			
							
				oGraph   := FWChartFactory():New()
				oGraph:SetOwner(oPanelGr)
				aEval(aTotais[nPosTotais,3],{|x| (oGraph:AddSerie(x[1],x[4]),oGraph:AddSerie(x[1],x[3]))})
				oGraph:EnableMenu(.F.)
				oGraph:SetChartDefault(COLUMNCHART)
				oGraph:SetMask('*@*')
				oGraph:SetPicture("@E 999,999,999.99")
				oGraph:setLegend(CONTROL_ALIGN_RIGHT)
				oGraph:Activate()

				@ aPos[3]-11,030 SAY OemToAnsi(STR0029) OF oPanel COLOR CLR_HRED FONT oBold PIXEL
				@ aPos[3]-11,130 SAY OemToAnsi(STR0030) OF oPanel COLOR CLR_HBLUE FONT oBold PIXEL
			Case cSeekTot == "93GRP"
				A015FWGrf(aTotais[nPostotais][3],,oPanel,cSeekTot,aPos,SubStr(cSeekComp,1,FWSIZEFILIAL()+TAMSX3('D4_OP')[1]),aTotais[nPostotais][4])
			Case cSeekTot == "00SG1"
				AADD(aDados,{OemToAnsi(STR0048)}) //"Total de roteiros para esse produto"
				AADD(aDados,{Str(aTotais[nPosTotais,3])})
				C040MatScrDisp(aDados,@oScroll,@oPanel,aPos,,{{2,CLR_RED}})
			Case cSeekTot == "01SG1" 
				oTree2 := dbTree():New(aPos[1], aPos[2],aPos[3],aPos[4],oPanel,,,.T.)
				Private ldbTree := .T.
				Private nIndex  := 1
				//-- Estrutura
				If mv_par11 == 1
					Pergunte('MTA200', .F.)
					//-- Monta o Tree da estrutura do produto.
					Ma200Monta(oTree2, oPanel,aTotais[nPosTotais,3],nil,aTotais[nPosTotais,4],2,,,.T.)
			   	 //-- Pre-estrutura	
				Else  
					A015MTree(oTree2, oPanel, aTotais[nPosTotais,3],@cRevEstru)
					If !Empty(cRevEstru) .And. oTree2:Total()>0
						//-- Altera a descricao do item da arvore.
						oTree:ChangePrompt(STR0051 + " - " + cRevEstru, "01SG1"+StrZero(0,12)+A015CSeek(FWxFilial("SGG")+aTotais[nPosTotais,3])) //"Revisão"
					Else 
						//-- Altera a descricao do item da arvore.
						oTree:ChangePrompt(STR0051 + " - " + cRevEstru, "01SG1"+StrZero(0,12)+A015CSeek(FWxFilial("SGG")+aTotais[nPosTotais,3])) //"Revisão" 
						AADD(aDados,{STR0022}) //"Nao existem totais para essa consulta."
						C040MatScrDisp(aDados,@oScroll,@oPanel,aPos,{{1,CLR_RED}})
					EndIf	
				EndIf
				Pergunte('MTC015', .F.)
			//-- Apresenta o grafico da estrutura	 
			Case cSeekTot == "02GRP"
				If !A015FWGrf(aTotais[nPostotais][3],If(mv_par11==1,aTotais[nPostotais][4],cRevEstru),oPanel,cSeekTot,aPos)
					AADD(aDados,{STR0022}) //"Nao existem totais para essa consulta."
					C040MatScrDisp(aDados,@oScroll,@oPanel,aPos,{{1,CLR_RED}})
				EndIf
			//-- Apresenta o grafico do produto	
			Case cSeekTot == "12GRP"
				If !A015FWGrf(aTotais[nPostotais][3],,oPanel,cSeekTot,aPos)	
					AADD(aDados,{STR0022}) //"Nao existem totais para essa consulta."
					C040MatScrDisp(aDados,@oScroll,@oPanel,aPos,{{1,CLR_RED}})
                EndIf
		EndCase
	Else
		AADD(aDados,{STR0022}) //"Nao existem totais para essa consulta."
		C040MatScrDisp(aDados,@oScroll,@oPanel,aPos,{{1,CLR_RED}})
	EndIf
EndIf
// Mostra Painel
oPanel:Show()
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MTC015Tree³ Autor ³Rodrigo de A Sartorio  ³ Data ³ 27/11/03   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao que monta o Tree da consulta de produto                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATC015                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MTC015Tree(oTree,aTotais)
Local aArea			:= GetArea()
Local nRecSB1		:= 0
Local aDadosCon		:= {}
Local aDadosSC2		:= {0,0,0,0}
Local nCountSG2		:= 0
Local nCountOperac	:= 0
Local nCountOpSGF	:= 0
Local nCountSD3		:= 0
Local nCountSD4		:= 0
Local nCountSG5		:= 0
Local cRoteiro		:= ""	
Local cSeekAnt		:= ""
Local cSeekAnt2		:= ""
Local cRefGrd		:= ""
Local cRevisao		:= ""
Local lRefGrd		:= .F.
//Local lPreEstru		:= mv_par11 == 2 
Local lPreEstru		:= iif(valtype(MV_PAR11)=='N',iif(MV_PAR11 == 2,.T.,.F.),.F.)
Local nAcho			:= 0 
Local bBloco
Local bBlocoWh

cRevEstru := If(cRevEstru == NIL, CriaVar('G5_REVISAO'), cRevEstru)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                          		³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                   	      		³
//³ mv_par01        // De  Produto                          	     	³
//³ mv_par02        // Ate Produto                              	 	³
//³ mv_par03        // De  Armazem                               		³
//³ mv_par04        // Ate Armazem                               		³
//³ mv_par05        // De  OP                                    		³
//³ mv_par06        // Ate OP                                    		³
//³ mv_par07        // De  Data Prev Inicio                      		³
//³ mv_par08        // Ate Data Prev Inicio                      		³
//³ mv_par09        // Considera OPs Firmes / Previstas / Ambos			³
//³ mv_par10        // Mostra Roteiro Oper. Padrao / Todos       		³
//³ mv_par11        // Apresenta dados da  Estrutura / Pre-estrutura    |
//³ mv_par12        // Tipo de custo    Standard / Medio				³
//³ mv_par13        // De Data Producao               					³
//³ mv_par14        // Ate Data Producao               					³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

// Monta tree na primeira vez
oTree:BeginUpdate()
oTree:Reset()
oTree:EndUpdate()

dbSelectArea("SD3")
dbSetOrder(1)
dbSelectArea("SC2")
dbSetOrder(1)
dbSelectArea("SD4")
dbSetOrder(2)
dbSelectArea("SG2")
dbSetOrder(1)
dbSelectArea("SH1")
dbSetOrder(1)
dbSelectArea("SG1")
dbSetOrder(1)
DbSelectArea("SG5")
DbSetOrder(1)
DbSelectArea("SGG")
DbSetOrder(1)
dbSelectArea("SB1")
dbSeek(xFilial("SB1")+mv_par01,.T.)
While SB1->(!Eof()) .And. B1_FILIAL == xFilial("SB1") .And. B1_COD <= mv_par02
	nRecSB1:=SB1->(Recno())
	cSeekAnt:="";cSeekAnt2:=""
	// Adiciona TREE com Produto
	oTree:AddTree(AllTrim(SB1->B1_COD)+" - "+AllTrim(SB1->B1_DESC),.T.,,,"PMSTASK6","PMSTASK6","10SB1"+StrZero(SB1->(Recno()),12)+A015CSeek())
	// Abre totalizador de roteiro de operacoes
	nCountSG2:=0
	oTree:AddTree(STR0009,.T.,,,"ROTEIRO","ROTEIRO","20SG2"+StrZero(0,12)+A015CSeek(xFilial("SG2")+SB1->B1_COD))
	dbSelectArea("SG2")
	cRefGrd		:= SB1->B1_COD
	a630SeekSG2(1,@cRefGrd,xFilial("SG2")+SB1->B1_COD+IF(mv_par10==1,A680RotPad("",SB1->B1_COD),""),,@lRefGrd,.T.)
	
	If lRefGrd
		bBlocoWh:= {|| SG2->(G2_FILIAL+G2_REFGRD) == xFilial("SG2")+cRefGrd }
		bBloco	:= {|| SG2->(G2_FILIAL+G2_REFGRD+G2_CODIGO) }
	Else
		bBlocoWh:= {|| SG2->(G2_FILIAL+G2_PRODUTO) == xFilial("SG2")+SB1->B1_COD }
		bBloco	:= {|| SG2->(G2_FILIAL+G2_PRODUTO+G2_CODIGO) }
	EndIf
	
    While !EOF() .And. Eval(bBlocoWh) .And. IF(mv_par10==1,SG2->G2_CODIGO == SB1->B1_OPERPAD,.T.)
		cRoteiro:= SG2->G2_CODIGO
		If Eval(bBloco) # cSeekAnt
			nCountSG2++
			// Adiciona totalizador de operacoes por roteiro
			If !Empty(cSeekAnt)
				AADD(aTotais,{"21SG2",A015CSeek(cSeekAnt),nCountOperac})
			EndIf
			cSeekAnt := Eval(bBloco)
			// Abre Roteiro de Operacoes
			oTree:AddTree(AllTrim(SG2->G2_CODIGO),.T.,,,"ROTEIRO","ROTEIRO","21SG2"+StrZero(0,12)+A015CSeek(Eval(bBloco)))
			nCountOperac:=0
		EndIf
		// Abre Operacao
		nCountOperac++
		oTree:AddTree(AllTrim(SG2->G2_OPERAC) +" - " +AllTrim(SG2->G2_DESCRI),.T.,,,"CLOCK01","CLOCK01","22SG2"+StrZero(SG2->(Recno()),12)+A015CSeek())
		// Adiciona Recursos
		dbSelectArea("SH1")
		dbSetOrder(1)
		If dbSeek(xFilial("SH1")+SG2->G2_RECURSO)
			oTree:AddTreeItem(AllTrim(SH1->H1_CODIGO) +" - " +AllTrim(SH1->H1_DESCRI),"ENGRENAGEM","ENGRENAGEM","30SH1"+StrZero(SH1->(Recno()),12)+A015CSeek())
		EndIf
		// Adiciona Ferramentas
		dbSelectArea("SH4")
		dbSetOrder(1)
		If dbSeek(xFilial("SH4")+SG2->G2_FERRAM)
			oTree:AddTreeItem(AllTrim(SH4->H4_CODIGO) +" - " +AllTrim(SH4->H4_DESCRI),"INSTRUME","INSTRUME","40SH4"+StrZero(SH4->(Recno()),12)+A015CSeek())
		EndIf
		// Adiciona Componentes
		nCountOpSGF:=0
		dbSelectArea("SGF")
		dbSetOrder(1)
		dbSeek(xFilial("SGF")+SB1->B1_COD+SG2->G2_CODIGO+SG2->G2_OPERAC)
		While !EOF() .And. xFilial("SGF")+SB1->B1_COD+SG2->G2_CODIGO+SG2->G2_OPERAC == SGF->GF_FILIAL+SGF->GF_PRODUTO+SGF->GF_ROTEIRO+SGF->GF_OPERAC
			If SGF->GF_FILIAL+SGF->GF_PRODUTO+SGF->GF_ROTEIRO+SGF->GF_OPERAC # cSeekAnt2
				// Adiciona totalizador de componentes por operacao
				If !Empty(cSeekAnt2)
					AADD(aTotais,{"50SGF",A015CSeek(cSeekAnt2),nCountOpSGF})
				EndIf
				nCountOpSGF:=0
				cSeekAnt2:=SGF->GF_FILIAL+SGF->GF_PRODUTO+SGF->GF_ROTEIRO+SGF->GF_OPERAC
				// Abre Componentes
				oTree:AddTree(STR0023,.T.,,,"PMSTASK4","PMSTASK4","50SGF"+StrZero(0,12)+A015CSeek(SGF->GF_FILIAL+SGF->GF_PRODUTO+SGF->GF_ROTEIRO+SGF->GF_OPERAC))
			EndIf
			nCountOpSGF++
			nRecSB1:=SB1->(Recno())
			// Posiciona no produto
			SB1->(dbSeek(xFilial("SB1")+SGF->GF_COMP))
			oTree:AddTreeItem(AllTrim(SGF->GF_COMP) +" - " +AllTrim(SB1->B1_DESC) +If(!Empty(SGF->GF_TRT)," / " +AllTrim(SGF->GF_TRT),""),"PMSTASK4","PMSTASK4","50SGF"+StrZero(SGF->(Recno()),12)+A015CSeek())
			// Reposiciona produto
			SB1->(dbGoto(nRecSB1))
			dbSkip()
			If Eof() .Or. SGF->GF_FILIAL+SGF->GF_PRODUTO+SGF->GF_ROTEIRO+SGF->GF_OPERAC # cSeekAnt2
				// Fecha Componentes
				oTree:EndTree()
			EndIf
		End
		// Adiciona totalizador de componentes por operacao
		If !Empty(cSeekAnt2)
			AADD(aTotais,{"50SGF",A015CSeek(cSeekAnt2),nCountOpSGF})
		EndIf
		cSeekAnt2:=""
		dbSelectArea("SG2")
		dbSkip()
		// Fecha Operacao
		oTree:EndTree()
		If Eof() .Or. Eval(bBloco) # cSeekAnt
			//Adicina totalizador do grafico
			AADD(aTotais,{"21GRP",A015CSeek(FWxFilial("SG2")+SB1->B1_COD+cRoteiro),SB1->B1_COD,cRoteiro})
			//Abre grafico painel de custos por operacoes
			oTree:AddTreeItem(STR0041,"GRAF2D","GRAF2D","21GRP"+StrZero(0,12)+A015CSeek(FwxFilial("SG2")+SB1->B1_COD+cRoteiro)) //"Painel de custos"
			// Fecha Roteiro de Operacoes
			oTree:EndTree()
		EndIf
	End
	// Fecha totalizador de roteiro de operacoes
	oTree:EndTree()
	// Adiciona totalizador de operacoes por roteiro
	If !Empty(cSeekAnt)
		AADD(aTotais,{"21SG2",A015CSeek(cSeekAnt),nCountOperac})
	EndIf
	// Adiciona totalizador roteiros por produto
	AADD(aTotais,{"20SG2",A015CSeek(xFilial("SG2")+SB1->B1_COD),nCountSG2})
	cSeekAnt:="";cSeekAnt2:=""
	// Abre totalizador de roteiro de Ordens de Producao
	oTree:AddTree(STR0013,.T.,,,"PMSDOC","PMSDOC","60SC2"+StrZero(0,12)+A015CSeek(xFilial("SC2")+SB1->B1_COD))
	dbSelectArea("SC2")
	dbSetOrder(1)
	dbSeek(xFilial("SC2")+mv_par05,.T.)
	aDadosSC2:={0,0,0,0}
	While !EOF() .And. SC2->C2_FILIAL == xFilial("SC2") .And. SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD <= mv_par06
		//-- Valida se a OP deve ser CONSIDERADA ou nao
		If !MtrAValOP(mv_par09,'SC2')
			dbSkip()
			Loop
		EndIf
		If SC2->C2_LOCAL < mv_par03 .Or. SC2->C2_LOCAL > mv_par04 .Or. DTOS(SC2->C2_DATPRI) < DTOS(mv_par07) .Or. DTOS(SC2->C2_DATPRI) > DTOS(mv_par08) .Or. SC2->C2_PRODUTO # SB1->B1_COD
			dbSkip()
			Loop
		EndIf
		aDadosSC2[1]+=1
		aDadosSC2[2]+=SC2->C2_QUANT
		aDadosSC2[3]+=SC2->C2_QUJE
		aDadosSC2[4]+=SC2->C2_PERDA
		cSeekAnt:=SC2->C2_FILIAL+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_ITEMGRD
		While !Eof() .And. SC2->C2_FILIAL+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_ITEMGRD == cSeekAnt .And. SC2->C2_PRODUTO == SB1->B1_COD
			// Array com os consumos por ordem de producao
			aDadosCon:={}
			// Abre Ordem de Producao
			oTree:AddTree(SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD,.T.,,,"PMSDOC","PMSDOC","60SC2"+StrZero(SC2->(Recno()),12)+A015CSeek())
    		// Abre Consumos
			oTree:AddTree(STR0025,.T.,,,"PMSTASK1","PMSTASK1","70SD3"+StrZero(0,12)+A015CSeek(SC2->C2_FILIAL+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD)) //"Consumos"
			ncountSD3:=0		
			dbSelectArea("SD3")
			dbSeek(xFilial("SD3")+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD)
			While !EOF() .And. D3_FILIAL+D3_OP == xFilial("SD3")+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD
				If D3_ESTORNO == "S" .Or. Substr(SD3->D3_CF,1,1) = "P"
					dbSkip()
					Loop
				EndIf	
				nCountSD3++
				nRecSB1:=SB1->(Recno())
				// Posiciona no produto
				SB1->(dbSeek(xFilial("SB1")+SD3->D3_COD))
				oTree:AddTreeItem(AllTrim(SD3->D3_COD) +" - " +AllTrim(SB1->B1_DESC) +If(!Empty(SD3->D3_TRT)," / " +AllTrim(SD3->D3_TRT),""),"PMSTASK1","PMSTASK1","70SD3"+StrZero(SD3->(Recno()),12)+A015CSeek())
				// Reposiciona produto
				SB1->(dbGoto(nRecSB1))
				// Array com os consumos por ordem de producao - Adiciona consumo
				nAcho:=ASCAN(aDadosCon,{ |x| x[1] == SD3->D3_COD .And. x[2] == SD3->D3_TRT})
				If nAcho > 0
					aDadosCon[nAcho,4]+=SD3->D3_QUANT+SD3->D3_PERDA
				Else				
					AADD(aDadosCon,{SD3->D3_COD,SD3->D3_TRT,0,SD3->D3_QUANT+SD3->D3_PERDA})
				EndIf
				dbSkip()
			End				
			// Adiciona totalizador de Consumos por OP
			AADD(aTotais,{"70SD3",A015CSeek(SC2->C2_FILIAL+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD),nCountSD3})
			// Fecha Consumos
			oTree:EndTree()
			// Abre Empenhos
			oTree:AddTree(STR0024,.T.,,,"PMSTASK4","PMSTASK4","80SD4"+StrZero(0,12)+A015CSeek(SC2->C2_FILIAL+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD))
			// Adiciona Empenho
			nCountSD4:=0
			dbSelectArea("SD4")
			dbSeek(xFilial("SD4")+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD)
			While !EOF() .And. D4_FILIAL+D4_OP == xFilial("SD4")+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD
				If SD4->D4_LOCAL < mv_par03 .Or. SD4->D4_LOCAL > mv_par04 .Or. DTOS(SC2->C2_DATPRI) < DTOS(mv_par07) .Or. DTOS(SC2->C2_DATPRI) > DTOS(mv_par08)
					dbSkip()
					Loop
				EndIf
				nCountSD4++
				nRecSB1:=SB1->(Recno())
				// Posiciona no produto
				SB1->(dbSeek(xFilial("SB1")+SD4->D4_COD))
				oTree:AddTreeItem(AllTrim(SD4->D4_COD) +" - " +AllTrim(SB1->B1_DESC) +If(!Empty(AllTrim(SD4->D4_TRT))," / "+SD4->D4_TRT,""),"PMSTASK4","PMSTASK4","80SD4"+StrZero(SD4->(Recno()),12)+A015CSeek())
				// Reposiciona produto
				SB1->(dbGoto(nRecSB1))
				// Array com os consumos por ordem de producao - Adiciona saldo original
				nAcho:=ASCAN(aDadosCon,{ |x| x[1] == SD4->D4_COD .And. x[2] == SD4->D4_TRT})
				If nAcho > 0
					aDadosCon[nAcho,3]+=SD4->D4_QTDEORI
				Else				
					AADD(aDadosCon,{SD4->D4_COD,SD4->D4_TRT,D4_QTDEORI,0})
				EndIf
				dbSkip()
			End
			// Adiciona totalizador de Empenhos por OP
			AADD(aTotais,{"80SD4",A015CSeek(SC2->C2_FILIAL+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD),nCountSD4})
			// Fecha Empenhos
			oTree:EndTree()
			// Abre Graficos para essa OP
			oTree:AddTree(STR0026,.T.,,,"GRAF2D","GRAF2D","90GRP"+StrZero(0,12)+A015CSeek(SC2->C2_FILIAL+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD)) //"Graficos"
			// Grafico 1 - Qtd Total X Perda X Qtd Apontada
			oTree:AddTreeItem(STR0027,"GRAF2D","GRAF2D","91GRP"+StrZero(0,12)+A015CSeek(SC2->C2_FILIAL+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD)) //"Qtd Total X Perda X Apontamento"
			// Adiciona dados do grafico                  
			AADD(aTotais,{"91GRP",A015CSeek(SC2->C2_FILIAL+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD),{SC2->C2_QUANT-SC2->C2_QUJE-If(SuperGetMV("MV_PERDINF",.F.,.F.),0,SC2->C2_PERDA),;
			SC2->C2_PERDA,SC2->C2_QUJE,!Empty(SC2->C2_DATRF)}})
			// Grafico 2 - Qtd Total X Perda X Qtd Apontada
			oTree:AddTreeItem(STR0028,"GRAF2D","GRAF2D","92GRP"+StrZero(0,12)+A015CSeek(SC2->C2_FILIAL+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD)) //"Consumo Real X Consumo Standard"
			// Adiciona dados do grafico                  
			AADD(aTotais,{"92GRP",A015CSeek(SC2->C2_FILIAL+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD),aDadosCon})
			//Abre Grafico 3 - Painel de custos de Ordem de Produção (Componentes e Operações)
			oTree:AddTreeItem(STR0041,"GRAF2D","GRAF2D","93GRP"+StrZero(0,12)+A015CSeek(SC2->C2_FILIAL+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD)) //"Painel de custos"
			// Adiciona dados do grafico                  
			AADD(aTotais,{"93GRP",A015CSeek(SC2->C2_FILIAL+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD),SC2->C2_PRODUTO,SC2->C2_ROTEIRO}) 
			// Fecha Graficos para essa OP
			oTree:EndTree()
			dbSelectArea("SC2")
			dbSkip()
			// Fecha Ordem de Producao
			oTree:EndTree()
		End
	End
	// Adiciona totalizador de OPs por produto
	AADD(aTotais,{"60SC2",A015CSeek(xFilial("SC2")+SB1->B1_COD),aDadosSC2[1],aDadosSC2[2],aDadosSC2[3],aDadosSC2[4]})
	// Fecha totalizador de Ops
	oTree:EndTree()
	// Abre totalizador de estrturas
	oTree:AddTree(If(lPreEstru,STR0049,STR0050),.T.,,,"relacionamento_direita","relacionamento_direita","00SG1"+StrZero(0,12)+A015CSeek(FWxFilial(If(lPreEstru,"SGG","SG1"))+SB1->B1_COD))
	If lPreEstru
		// Abre o tree da pre-estrutura
	    If SGG->(DbSeek(FWxFilial("SGG")+SB1->B1_COD)) 
			AADD(aTotais,{"00SG1",A015CSeek(FWxFilial("SGG")+SB1->B1_COD),0})
			//Abre totalizador da estrtura sem revisoes
			oTree:AddTree(STR0051,.T.,,,"relacionamento_direita","relacionamento_direita","01SG1"+StrZero(0,12)+A015CSeek(FWxFilial("SGG")+SB1->B1_COD))
			//Grafico 1 - "Painel de custos da pre-estrutura"			
			oTree:AddTreeItem(STR0041,"GRAF2D","GRAF2D","02GRP"+StrZero(0,12)+A015CSeek(FWxFilial("SGG")+SB1->B1_COD)) //"Painel de custos"			
			// Adiciona dados do grafico                  
			AADD(aTotais,{"02GRP",A015CSeek(FWxFilial("SGG")+SB1->B1_COD),SB1->B1_COD})
			// Fecha totalizador da pre-estrtura
	   		oTree:EndTree()
			AADD(aTotais,{"01SG1",A015CSeek(FWxFilial("SGG")+SB1->B1_COD),SB1->B1_COD})
	    EndIf
	Else
		If SG5->(DbSeek(FWxFilial("SG5")+SB1->B1_COD))
			cRevisao:= SG5->G5_REVISAO
			While !SG5->(Eof()) .And. SB1->B1_COD == SG5->G5_PRODUTO
		    	cRevisao:= SG5->G5_REVISAO
		    	nCountSG5++
				//Abre totalizador das revisoes da estrturas
				oTree:AddTree(STR0051 +" - " +SG5->G5_REVISAO,.T.,,,"relacionamento_direita","relacionamento_direita","01SG1"+StrZero(0,12)+A015CSeek(FWxFilial("SG5")+SG5->G5_PRODUTO+SG5->G5_REVISAO))
				//Grafico 1 - "Painel de custos da estrutura"
				oTree:AddTreeItem(STR0041,"GRAF2D","GRAF2D","02GRP"+StrZero(0,12)+A015CSeek(FWxFilial("SG5")+SG5->G5_PRODUTO+SG5->G5_REVISAO)) //"Painel de custos da estrutura"			
				// Adiciona dados do grafico                  
				AADD(aTotais,{"02GRP",A015CSeek(FWxFilial("SG5")+SG5->G5_PRODUTO+SG5->G5_REVISAO),SG5->G5_PRODUTO,SG5->G5_REVISAO})
				// Fecha totalizador das revisoes da estrturas
				AADD(aTotais,{"01SG1",A015CSeek(FWxFilial("SG5")+SG5->G5_PRODUTO+SG5->G5_REVISAO),SG5->G5_PRODUTO,SG5->G5_REVISAO})
		   		oTree:EndTree() 
				SG5->(DbSkip())
	    	End
			AADD(aTotais,{"00SG1",A015CSeek(FWxFilial(If(lPreEstru,"SGG","SG1"))+SB1->B1_COD),nCountSG5})
		EndIf
		// Abre o tree da estrutura sem revisoes
	    If Empty(nCountSG5) .And. SG1->(DbSeek(FWxFilial("SG1")+SB1->B1_COD))
			AADD(aTotais,{"00SG1",A015CSeek(FWxFilial("SG1")+SB1->B1_COD),nCountSG5})
			//Abre totalizador da estrtura sem revisoes
			oTree:AddTree(STR0051,.T.,,,"relacionamento_direita","relacionamento_direita","01SG1"+StrZero(0,12)+A015CSeek(FWxFilial("SG1")+SB1->B1_COD))
			//Grafico 1 - "Painel de custos da estrutura"
			oTree:AddTreeItem(STR0041,"GRAF2D","GRAF2D","02GRP"+StrZero(0,12)+A015CSeek(FWxFilial("SG1")+SB1->B1_COD)) //"Painel de custos"
			// Adiciona dados do grafico                  
			AADD(aTotais,{"02GRP",A015CSeek(FWxFilial("SG1")+SB1->B1_COD),SB1->B1_COD,cRevEstru})
			// Fecha totalizador da estrtura sem revisoes
	   		oTree:EndTree()
			AADD(aTotais,{"01SG1",A015CSeek(FWxFilial("SG1")+SB1->B1_COD),SB1->B1_COD,cRevEstru})
	    EndIf
	EndIf	     
	nCountSG5:=0
	// Fecha totalizador de estruturas
	oTree:EndTree()
	// Reposiciona produto
	SB1->(dbGoto(nRecSB1))
	//Abre grafico Painel de custos do produto
	oTree:AddTreeItem(STR0041,"GRAF2D","GRAF2D","12GRP"+StrZero(0,12)+A015CSeek(FwxFilial("SB1")+SB1->B1_COD)) //"Painel de custos do produto"			
	AADD(aTotais,{"12GRP",A015CSeek(FwxFilial("SB1")+SB1->B1_COD),SB1->B1_COD})
    SB1->(DbSkip())
	// Fecha Produto
	oTree:EndTree()
End
oTree:EndUpdate()
oTree:Refresh()
RestArea(aArea)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A015CSeek ³ Autor ³Rodrigo de A. Sartorio³ Data ³ 01/12/03   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Monta texto de acordo com tamanho formatado (nTamOri)         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Generico                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A015CSeek(cTexto)
Local nTamOri:=60
Default cTexto:=""
Return (cTexto+Space(nTamOri-Len(cTexto)))

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Fabio Alves Silva     ³ Data ³09/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados         ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()     
PRIVATE aRotina	:= {{STR0002,"AxPesqui", 0 , 1, 0, .F.},; //"Pesquisar"
					{STR0004,"MC015Con", 0 , 2, 0, nil},; //"Consulta"
					{STR0003,"MC015Con", 0 , 5, 0, nil}	} //"pOsicionado"	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada utilizado para inserir novas opcoes no array aRotina  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MTC015MNU")
	ExecBlock("MTC015MNU",.F.,.F.)
EndIf
Return(aRotina)


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³A015FWGrf ºAutor  ³Aecio Ferreira Gomesº Data ³ 18/06/10    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Montagem do Layer para visualizacao dos graficos 			  º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do produto posicionado para consulta.         ³±±
±±³			 ³ExpC2: Revisão da estrutura do produto.					  ³±±
±±³			 ³Expo1: Panel onde será apresentado o Layer.			   	  ³±±
±±³			 ³ExpC3: Item do Tree posicionado.  					   	  ³±±
±±³			 ³ExpA1: Array com as dimessoes do Panel					  ³±±
±±³			 ³ExpC4: Numero da OP.										  ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATC015                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

Static Function A015FWGrf(cProduto,cRevisao,oPanel,cSeekTot,aPos,cSeekComp,cRoteiro)
Local aSerie  := {}   
Local aTitles := {STR0031,STR0032,STR0033} //"Componente" ###"Operacao"###"Custo de produção da OP"    
Local aPages  := {"HEADER","HEADER","HEADER"}
Local nX	  := 0
Local lRet    := .T.
Local oFWLayer,oFolder2 

oFWLayer := FWLayer():New()			
If cSeekTot == "02GRP" 
	aSerie:= MTCustEstr(cProduto,cRevisao)
	oFWLayer:init(oPanel, .F. )
	oFWLayer:addCollumn( "Col02",100,.F. )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//|Grafico "Custo por componente" |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ                   
	A015PCHART(oFWLayer,aSerie,"Col02","Win02",100,STR0034,,oPanel,aPos) //"Custo por componente"
ElseIf cSeekTot == "12GRP"
	aSerie:= MTCusProd(cProduto)
	aSerie:={{STR0035,aSerie}} //"Custo médio unitário"
	oFWLayer:init(oPanel, .F. )
	oFWLayer:addCollumn( "Col02",100,.F. )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//|Grafico "Custo medio unitario de produção" |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ                   
	A015LCHART(oFWLayer,aSerie,"Col02","Win02",100,STR0036,,oPanel,aPos) //"Custo médio unitário de produção"
ElseIf cSeekTot == "21GRP" 
	aSerie:=A015CMRot(cProduto,"1",cSeekComp,cRoteiro)
	oFWLayer:init(oPanel, .F. )
	oFWLayer:addCollumn( "Col02",100,.F. )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//|Grafico "Custo medio unitario por operação" 			  |	
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ                   
	A015PCHART(oFWLayer,aSerie[1][2],"Col02","Win02",100,STR0037,,oPanel,aPos) // "Custo medio unitario por operação"
ElseIf cSeekTot == "93GRP"  
	//ÚÄÄÄÄÄÄ¿
	//³Folder³
	//ÀÄÄÄÄÄÄÙ
	oFolder2 := TFolder():New(0,0,aTitles,aPages,oPanel,,,,.T.,.F.,aPos[4],aPos[3])
	oFolder2:oFont := oPanel:oFont

	For nX:= 1 To Len(oFolder2:ADIALOGS)
		oFWLayer := FWLayer():New()			
		If nX <> 3
			If nX == 1 //Custo por componente
				aSerie:=A015CmComp(cProduto,"1|2",cSeekComp)
			Else //Custo Por Roteiro de operação
				aSerie:=A015CMRot(cProduto, "1|2",cSeekComp,cRoteiro)
			EndIf
			oFWLayer:init(oFolder2:ADIALOGS[nX], .F. )
			//Cria linhas do Layer
			oFWLayer:addLine( "SUPERIOR",50,.F.)		
			oFWLayer:addLine( "INFERIOR",50,.F.)		
			//Cria as colunas do Layer
			oFWLayer:addCollumn( "ALL", 100,.F.,"SUPERIOR")
			oFWLayer:addCollumn( "ESQUERDA", 50,.F.,"INFERIOR")
			oFWLayer:addCollumn( "DIREITA", 50,.F.,"INFERIOR")
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|Grafico "Custo previsto" por roteiro de operacao ou componente |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ                   
			A015PCHART(oFWLayer,aSerie[1,2],"ESQUERDA","Win01",100,STR0038,"INFERIOR",oPanel,aPos) //"Custo Previsto" 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|Grafico "Custo Real" por roteiro de operacao ou componente | 
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			A015PCHART(oFWLayer,aSerie[2,2],"DIREITA","Win02",100,STR0039,"INFERIOR",oPanel,aPos) //"Custo Real" 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|Grafico "Custo Previsto x Real" por roteiro de operacao ou componente | 
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			A015BCHART(oFWLayer,aSerie,"ALL","Win03",100,STR0040,"SUPERIOR",oPanel,aPos) //"Custo Previsto x Real" 
		Else                                        
			// Custo Total da OP
			aSerie:=A015CustOp(cProduto, cSeekComp) 
			oFWLayer:init(oFolder2:ADIALOGS[nX], .F. )
			//Cria Coluna do Layer
			oFWLayer:addCollumn( "Col01", 100,.F.)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|Grafico "Custo total da OP" Custo Previsto x Real | 
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
 			A015BCHART(oFWLayer,aSerie,"Col01","Win01",100,STR0040,,oPanel,aPos) //"Custo Previsto x Real"
		EndIf	
	Next	
Else
	lRet:= .F.	
EndIf 	

Return (lRet)
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³A015MTree ºAutor  ³Aecio Ferreira Gomesº Data ³ 18/06/10    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Monta a visualizacao do tree da pre-estrutura de acordo com º±±
±±º			 ³a revisao informada.							              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATC015                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function A015MTree(oTree, oPanel, cProduto,cRevisao)
Local lExit   := .F.
Local cCabec  := STR0042 //"Revisao da pre-estrutura"
Local cString := STR0043 //"Informe codigo da revisao"
Local nOpcA   := 0
Local oDlg, oPanel1
Local cArqInd, cChaveInd, cQuery:= "" 

DbSelectArea("SGG")
DbSetOrder(1)

Do While .T.
	DEFINE MSDIALOG oDlg FROM 0, 0 TO 100, 243 TITLE cCabec PIXEL
	@ 10, 10 SAY AllTrim(cString)									OF oDlg PIXEL
	@ 20, 10 SAY AllTrim(RetTitle('G5_REVISAO'))+':'				OF oDlg PIXEL
	@ 20, 50 MSGET cRevisao VALID If(nOpcA==1,!Empty(cRevisao),.t.)OF oDlg PIXEL

	@ 000,000 MSPANEL oPanel1	OF oDlg SIZE 000,012
	oPanel1:Align    := CONTROL_ALIGN_BOTTOM
	oPanel1:nClrPane := RGB(234,241,246)
	oPanel1:ReadClientCoors()
	
	@ 000,000 BUTTON oBack PROMPT STR0044 SIZE 040,011 OF oPanel1 PIXEL; //"Cancela"
	 ACTION (nOpcA:=2,(lExit:=.T.,oDlg:End()))
	oBack:Align := CONTROL_ALIGN_RIGHT 

	@ 000,000 BUTTON oBack PROMPT STR0045 SIZE 040, 011 OF oPanel1 PIXEL; //"Confirma" 
	 ACTION (nOpcA:=1, If(!Empty(cRevisao), (lExit:=.T.,oDlg:End()), lExit:=.F.))
	oBack:Align := CONTROL_ALIGN_RIGHT 

	ACTIVATE MSDIALOG oDlg CENTERED

	If Empty(nOpcA) .Or. lExit
	    If nOpcA <> 1
		    cRevisao:= CriaVar('G5_REVISAO')
		EndIf    
		Exit
	EndIf	
EndDo
//-- Filtra somente os compontes da revisão
cArqInd   := CriaTrab(, .F.)
cChaveInd := INDEXKEY()
cQuery	  += " GG_FILIAL = '"		+ FWxFilial("SGG")	+ "'"
cQuery	  += " .AND. GG_COD = '"    + cProduto			+ "'"
cQuery	  += " .AND. GG_REVFIM >= '"+ cRevisao			+ "'"
IndRegua("SGG", cArqInd, cChaveInd, , cQuery) 
nIndice := RetIndex("SGG") + 1

dbSetOrder(nIndice)
SGG->(MsSeek(FWxFILIAL("SGG")))

//-- Monta o Tree da pre-estrutura do produto.
If !Empty(cRevisao) .And. nOpcA == 1
	Pergunte('MTA202', .F.)
	Ma202Monta(oTree, oPanel, cProduto ,nil,2)
	Pergunte('MTC015', .F.) 
EndIf

SGG->(DbClearFil())
RetIndex("SGG")

Return
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ A015PCHART ºAutor  ³Aecio Ferreira Gomesº Data ³ 18/06/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Montagem do Grafico de Pizza.								 º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Expo1: Layer onde será apresentado do Gafico.			     ³±±
±±³			 ³ExpA1: Serie do Grafico.									 ³±±
±±³			 ³ExpC1: ID da coluna conforme divisao do Layer.  			 ³±±
±±³			 ³ExpC2: ID da janela que será criada no Layer.			   	 ³±±
±±³			 ³ExpN1: Percentual de Altura da janela 					 ³±±
±±³			 ³ExpC4: Titulo da janela que esta sendo criada.    		 ³±±
±±³			 ³Expc3: ID da Linha conforme divisao do layer				 ³±±
±±³			 ³ExpC4: Panel onde será apresentado o Layer.				 ³±±
±±³			 ³ExpA2: Array com as dimessoes do Panel.		    		 ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATC015                                 	                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function A015PCHART(oFWLayer,aSerie,cId,cIDWindow,nPercHeight,cTitle,cIdLine,oPanel,aPos)
Local nX := 0
Local oFWChart01,oFWChart02,oFWLayer2,oFont,oScroll
Local aDados:= {}
Local lEmptySerie:= .T.

DEFINE FONT oFont NAME "Arial" SIZE 0, -10
oFWLayer:addWindow( cId, cIDWindow, cTitle, nPercHeight, .F., .F.,,cIdLine)

If cIDWindow  == "Win01"
	A015Chart(@oFWChart01,@oFWLayer,02 /*PIECHART*/,cId,cIDWindow,cIdLine,PesqPict('SB2','B2_CM1'))
    //Adiciona serie no grafico
	For nX:=1 to len(aSerie)
		oFWChart01:addSerie(aSerie[nx,1],aSerie[nX,2])
		lEmptySerie:= .F.
	Next 
	//Cria o Grafico
	If !lEmptySerie
		oFWChart01:build()
	Endif	
ElseIf cIDWindow == "Win02" 	
	A015Chart(@oFWChart02,@oFWLayer,02 /*PIECHART*/,cId,cIDWindow,cIdLine,PesqPict('SB2','B2_CM1'))
	//Adiciona serie no grafico
	For nX:=1 to len(aSerie)
		oFWChart02:addSerie(aSerie[nx,1],aSerie[nX,2])
		lEmptySerie:= .F.
	Next
	//Cria o Grafico
	If !lEmptySerie
		oFWChart02:build()
	Endif	
EndIf 

If lEmptySerie
	oFWLayer2:=oFWLayer:GetWinPanel(cId, cIDWindow,cIdLine)
	AADD(aDados,{STR0022}) //"Nao existem totais para essa consulta."
	C040MatScrDisp(aDados,@oScroll,@oFWLayer2,aPos,{{1,CLR_RED}})
EndIf

Return
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ A015LCHART ºAutor  ³Aecio Ferreira Gomesº Data ³ 18/06/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Montagem do Grafico de Lnhas.								 º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Expo1: Layer onde será apresentado do Gafico.			     ³±±
±±³			 ³ExpA1: Serie do Grafico.									 ³±±
±±³			 ³ExpC1: ID da coluna conforme divisao do Layer.  			 ³±±
±±³			 ³ExpC2: ID da janela que será criada no Layer.			   	 ³±±
±±³			 ³ExpN1: Percentual de Altura da janela 					 ³±±
±±³			 ³ExpC4: Titulo da janela que esta sendo criada.    		 ³±±
±±³			 ³Expc3: ID da Linha conforme divisao do layer				 ³±±
±±³			 ³ExpC4: Panel onde será apresentado o Layer.				 ³±±
±±³			 ³ExpA2: Array com as dimessoes do Panel.    		 		 ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATC015                                 	                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function A015LCHART(oFWLayer,aSerie,cId,cIDWindow,nPercHeight,cTitle,cIdLine,oPanel,aPos)
Local nX := 0
Local oFWChart,oFont,oScroll,oFWLayer2
Local aDados:= {}
Local lEmptySerie:= .T.

DEFINE FONT oFont NAME "Arial" SIZE 0, -10
oFWLayer:addWindow( cId, cIDWindow, cTitle, nPercHeight, .F., .F.,,cIdLine)
A015Chart(@oFWChart,@oFWLayer, 01 /*LINECHART*/,cId,cIDWindow,cIdLine,PesqPict('SB2','B2_CM1'))

// Adiciona serie no grafico
For nX:= 1 To Len(aSerie)
	If !Empty(aSerie[nX][2])
		oFWChart:addSerie(aSerie[nX][1],aSerie[nX][2])
		lEmptySerie:= .F.
	EndIf	
Next

If lEmptySerie
	oFWLayer2:=oFWLayer:GetWinPanel(cId, cIDWindow,cIdLine)
	AADD(aDados,{STR0022}) //"Nao existem totais para essa consulta."
	C040MatScrDisp(aDados,@oScroll,@oFWLayer2,aPos,{{1,CLR_RED}})
Else
	//Cria o Grafico
	oFWChart:build()
EndIf

Return
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ A015BCHART ºAutor  ³Aecio Ferreira Gomesº Data ³ 18/06/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Montagem do Grafico de Barras.							 º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Expo1: Layer onde será apresentado do Gafico.			     ³±±
±±³			 ³ExpA1: Serie do Grafico.									 ³±±
±±³			 ³ExpC1: ID da coluna conforme divisao do Layer.  			 ³±±
±±³			 ³ExpC2: ID da janela que será criada no Layer.			   	 ³±±
±±³			 ³ExpN1: Percentual de Altura da janela 					 ³±±
±±³			 ³ExpC4: Titulo da janela que esta sendo criada.    		 ³±±
±±³			 ³Expc3: ID da Linha conforme divisao do layer				 ³±±
±±³			 ³ExpC4: Panel onde será apresentado o Layer.				 ³±±
±±³			 ³ExpA2: Array com as dimessoes do Panel.		    		 ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATC015                                 	                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function A015BCHART(oFWLayer,aSerie,cId,cIDWindow,nPercHeight,cTitle,cIdLine,oPanel,aPos)
Local nX := 0
Local oFWChart,oFWLayer2,oFont,oScroll
Local aDados:= {} 
Local lEmptySerie:= .T.

DEFINE FONT oFont NAME "Arial" SIZE 0, -10
oFWLayer:addWindow( cId, cIDWindow, cTitle, nPercHeight, .F., .F.,,cIdLine)
A015Chart(@oFWChart,@oFWLayer, 03 /*BARCOMPCHART*/,cId,cIDWindow,cIdLine,PesqPict('SB2','B2_CM1'))
// Adiciona serie no grafico
For nX:= 1 To Len(aSerie)
	If !Empty(aSerie[nX][2])
		oFWChart:addSerie(aSerie[nX][1],aSerie[nX][2])
		lEmptySerie:= .F.
	EndIf	
Next

If lEmptySerie
	oFWLayer2:=oFWLayer:GetWinPanel(cId, cIDWindow,cIdLine)
	AADD(aDados,{STR0022}) //"Nao existem totais para essa consulta."
	C040MatScrDisp(aDados,@oScroll,@oFWLayer2,aPos,{{1,CLR_RED}})
Else
	//Cria o Grafico
	oFWChart:build()
EndIf

Return
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ A015CMRot  ºAutor  ³Aecio Ferreira Gomesº Data ³ 18/06/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna a operação e custo unitário Previsto e ou Real	 º±±	
±±º			 ³de produção conforme roteiro de operacao				   	 º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Produto posicionado para consulta.    				 ³±±
±±³			 ³ExpC2: Tipo de custo 1- Previsto; 2-Real.					 ³±±
±±³			 ³ExpC3: Ordem de producao posicionada.	   				   	 ³±±
±±³			 ³ExpC4: Roteiro de operação.        	   				   	 ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATC015                                 	                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function A015CMRot(cProduto,cTipo,cOP,cRoteiro)
Local aRet	   := {}
Local aRetPrev := {}
Local aRetReal := {}
Local aArea    := GetArea()
Local aAreaSG2 := SG2->(GetArea())
Local nCusMed  := 0
Local nCustSD3 := 0
Local nQtdSD3  := 0
Default cOP	   :=CriaVar("D3_OP") 

DbSelectArea("SD3")
SD3->(DbSetOrder(1))
DbSelectArea("SH6")
SH6->(DbSetOrder(1))
DbSelectArea("SG2")
SG2->(DbSetOrder(1))
//Custo Previsto	
If "1"  $ cTipo .And. SG2->(DbSeek(FWxFilial("SG2")+cProduto+cRoteiro))
	While SG2->(!Eof()) .And. SG2->G2_PRODUTO == cProduto .And. SG2->G2_CODIGO == cRoteiro
		// Posiciona no registro da MOD no cadastro de produtos
		If !SB1->(DbSeek(FwxFilial("SB1")+APrModRec(SG2->G2_RECURSO))) 
			SG2->(DbSkip())
			Loop
		EndIf                      
		// Recupera o custo medio da MOD
		If SB2->(DbSeek(FwxFilial("SB2")+SB1->B1_COD+SB1->B1_LOCPAD))
	 		nCusMed := ABS(SB2->B2_CM1)
			AADD(aRetPrev,{SG2->G2_OPERAC,nCusMed})
		EndIf
		SG2->(DbSkip())
	End
EndIf
//Custo Real 	
If "2"  $ cTipo .And. SG2->(DbSeek(FWxFilial("SG2")+cProduto+cRoteiro))
	While SG2->(!Eof()) .And. SG2->G2_PRODUTO == cProduto .And. SG2->G2_CODIGO == cRoteiro
		// Posiciona no registro da MOD no cadastro de produtos
		If !SB1->(DbSeek(FwxFilial("SB1")+APrModRec(SG2->G2_RECURSO))) 
			SG2->(DbSkip())
			Loop
		EndIf
		// Posiciona nas riquisicoes da MOD para a OP
		If !SD3->( DbSeek(cOP+SB1->B1_COD) )
			SG2->(DbSkip())
			Loop
		EndIf
		While SD3->(!Eof()) .And. (cOP+SB1->B1_COD) == SD3->(D3_FILIAL+D3_OP+D3_COD)
			If SD3->D3_ESTORNO == 'S' 
			   SD3->(DbSkip())
			   Loop
			EndIf
			// Posiciona nos apontamentos da operacao
			If !SH6->(Dbseek(cOP+cProduto+SG2->G2_OPERAC))
			   SD3->(DbSkip())
			   Loop
			EndIf
			While SH6->(!Eof()) .And. cProduto+SD3->D3_OP+SG2->G2_OPERAC == SH6->(H6_PRODUTO+H6_OP+H6_OPERAC)
				If SH6->H6_IDENT == SD3->D3_IDENT
					nCustSD3+= SD3->D3_CUSTO1							
			    	nQtdSD3 += SD3->D3_QUANT
			 	Endif
			 	SH6->(DbSkip())						 	
			End
		 	SD3->(DbSkip())
		End
		nCustSD3:= (nCustSD3/nQtdSD3)                    
		AADD(aRetReal,{SG2->G2_OPERAC,nCustSD3})
		nCustSD3:=nQtdSD3:=0
	 	SG2->(DbSkip())
	End
EndIf
AADD(aRet,{STR0038,aRetPrev}) //"Custo Previsto"
AADD(aRet,{STR0039,aRetReal}) //"Custo Real"
RestArea(aArea)
RestArea(aAreaSG2)
Return aRet
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ A015CmComp ºAutor  ³Aecio Ferreira Gomesº Data ³ 18/06/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna o custo medio Previsto e Real de produção     	 º±±
±±º			 ³com base nos componentes da estrutura do PA.               º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Produto posicionado para consulta.    				 ³±±
±±³			 ³ExpC2: Tipo de custo 1- Previsto; 2-Real.					 ³±±
±±³			 ³ExpC3: Ordem de producao posicionada.	   				   	 ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATC015                                 	                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function A015CmComp(cProduto,cTipo,cOP)
Local aRet	   := {}
Local aRetPrev := {}
Local aRetReal := {}
Local aArea    := GetArea()
Local aAreaSD4 := SD4->(GetArea())
Local aAreaSD3 := SD3->(GetArea())
Local cComp	   := ''	
Local nCustd   := 0
Local nCusto   := 0
Default cOP	   :=CriaVar("D3_OP") 

//Custo Previsto	
If "1" $ cTipo 
	DbSelectArea("SD4")
	SD4->(DbSetOrder(2))
	SD4->(DbSeek(cOP))
	While (SD4->(!Eof()) .And. cOP == SD4->D4_FILIAL+SD4->D4_OP)
		cComp:= SD4->D4_COD
		// Desconsidera produtos MOD
		If IsProdMod(cComp)
			SD4->(DbSkip())		
			Loop
		EndIf	          
		// Recupera custo Standard do produto
		If SB1->(DbSeek(FwxFilial("SB1")+cComp))
			nCustd := SB1->B1_CUSTD
		EndIf 
		nCusto += (SD4->D4_QTDEORI * nCustd)
		SD4->(DbSkip())
		If cComp <> SD4->D4_COD .Or. cOP <> SD4->D4_FILIAL+SD4->D4_OP
			AADD(aRetPrev,{cComp,nCusto})
			nCusto:=nCustd:= 0	
		EndIf
	End
EndIf
//Custo Real 			
If "2" $ cTipo 
	DbSelectArea("SD3")
	SD3->(DbSetOrder(1))
	SD3->(DbSeek(cOP))
	While (SD3->(!Eof()) .And. cOP == SD3->D3_FILIAL+SD3->D3_OP)
		cComp:= SD3->D3_COD
		// Desconsidera produtos MOD
		If IsProdMod(SD3->D3_COD)
			SD3->(DbSkip())		
			Loop
		EndIf
		// Permite somente requisicoes para OP.
		If !('RE' $ SD3->D3_CF .And. SD3->D3_ESTORNO # "S")
			SD3->(DbSkip())		
			Loop
		EndIf
		nCusto+= SD3->D3_CUSTO1							
		SD3->(DbSkip())		
		If cComp <> SD3->D3_COD .Or. cOP <> SD3->D3_FILIAL+SD3->D3_OP
			AADD(aRetReal,{cComp,nCusto})
			nCusto:= 0
		EndIf
	End
EndIf	
AADD(aRet,{STR0038,aRetPrev}) //"Custo Previsto"
AADD(aRet,{STR0039,aRetReal}) //"Custo Real"
RestArea(aArea)
RestArea(aAreaSD4)
RestArea(aAreaSD3)
Return aRet 

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ A015CustOp ºAutor  ³Aecio Ferreira Gomesº Data ³ 18/06/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna o custo medio Previsto e Real da produção		   	 º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Produto posicionado para consulta.    				 ³±±
±±³			 ³ExpC2: Ordem de producao posicionada.	   				   	 ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATC015                                 	                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function A015CustOp(cProduto,cOP)
Local aRet	   := {}
Local aRetPrev := {}
Local aRetReal := {}
Local aArea    := GetArea()
Local aAreaSD4 := SD4->(GetArea())
Local aAreaSD3 := SD3->(GetArea())
Local nCustd   := 0
Local nCustSD3 := 0	
Local nCusto   := 0
Default cOP	   :=CriaVar("D3_OP") 
//Custo Previsto
DbSelectArea("SD4")
SD4->(DbSetOrder(2))
SD4->(DbSeek(cOP))
While (SD4->(!Eof()) .And. cOP == SD4->D4_FILIAL+SD4->D4_OP)
	If SB1->(DbSeek(FwxFilial("SB1")+SD4->D4_COD))
		nCustd := SB1->B1_CUSTD
	EndIf 
	nCusto += (SD4->D4_QTDEORI * nCustd)
	SD4->(DbSkip())		
	If cOP <> SD4->D4_FILIAL+SD4->D4_OP
		AADD(aRetPrev,{cProduto,nCusto})
		nCusto:=nCustd:= 0	
	EndIf	
End
//Custo Real 			
DbSelectArea("SD3")
SD3->(DbSetOrder(1))
SD3->(DbSeek(cOP+cProduto))
While (SD3->(!Eof()) .And. cOP+cProduto == SD3->D3_FILIAL+SD3->D3_OP+SD3->D3_COD)
	If SD3->D3_ESTORNO == "S" 
		SD3->(DbSkip())		
		Loop
	EndIf
	If SD3->D3_CF == 'PR0' 
		nCustSD3+= SD3->D3_CUSTO1							
		SD3->(DbSkip())		
		If cOP+cProduto <> SD3->D3_FILIAL+SD3->D3_OP+SD3->D3_COD
			AADD(aRetReal,{cProduto,nCustSD3})
			nCustSD3:= 0
		EndIf
	Else
		SD3->(DbSkip())	
	EndIf			
End
AADD(aRet,{STR0038,aRetPrev}) //"Custo Previsto"
AADD(aRet,{STR0039,aRetReal}) //"Custo Real" 
RestArea(aArea)
RestArea(aAreaSD4)
RestArea(aAreaSD3)
Return aRet     
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ MTCusProd  ºAutor  ³Aecio Ferreira Gomesº Data ³ 18/06/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna o custo medio mensal de produção de um determinado º±±
±±º			 ³produto, conforme periodo Informado.		    			 º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Produto posicionado para consulta.    				 ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATC015                                 	                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function MTCusProd(cProduto)	 
Local aRet	   := {}
Local cArqInd  := ""	
Local cChaveInd:= "" 
Local cQuery   := ""
Local cAliasSD3:= "SD3"
Local cMes	   := "" 
Local cAno	   := substr(str(year(mv_par13),4),3)		
Local dDataEmis:= cTod("")
Local nCusmed  := 0  
Local nCusTot  := 0
Local nQuant   := 0 
Local nIndice  := 0

dbSelectArea("SD3")
dbSetOrder(6)
cArqInd   := CriaTrab(, .F.)
cChaveInd := INDEXKEY()
cQuery	  += " D3_FILIAL = '"				+ FWxFilial("SD3")	+ "'"
cQuery	  += " .AND. D3_COD = '"            + cProduto 			+ "'"
cQuery	  += " .AND. D3_CF = 'PR0' "
cQuery	  += " .AND. D3_ESTORNO # 'S' "
cQuery	  += " .AND. dTos(D3_EMISSAO) >= '" + Dtos(mv_par13) 	+ "'"
cQuery	  += " .AND. dTos(D3_EMISSAO) <= '" + Dtos(mv_par14) 	+ "'"
IndRegua(cAliasSD3, cArqInd, cChaveInd, , cQuery) 
nIndice := RetIndex("SD3") + 1

dbSetOrder(nIndice)
(cAliasSD3)->(MsSeek(FWxFILIAL("SD3"))) 
cMes:= strzero(month((cAliasSD3)->D3_EMISSAO),2)
While (cAliasSD3)->(!EOF() .And. (cAliasSD3)->D3_EMISSAO >= mv_par13 .And. (cAliasSD3)->D3_EMISSAO < mv_par14)
	nQuant		+= (cAliasSD3)->D3_QUANT
	nCusTot		+= (cAliasSD3)->D3_CUSTO1		                                    
	dDataEmis	:= (cAliasSD3)->D3_EMISSAO
	(cAliasSD3)->(DbSkip())
  	
  	If cMes <> strzero(month((cAliasSD3)->D3_EMISSAO),2)
		dDataEmis:= ctod("01/"+strzero(month(dDataEmis)+1,2)+"/"+cAno)
		cMes:= strzero(month(dDataEmis),2)
	   	nCusMed := (nCusTot/nQuant)
		AADD(aRet,{Dtoc( If(!Empty(dDataEmis) .And. dDataEmis < mv_par14,(dDataEmis-1),mv_par14)),nCusMed})		
		nCusTot:=nQuant:=nCusMed:=0  
	EndIf	
End	  
(cAliasSD3)->(DbClearFil())
RetIndex("SD3")

Return(aRet)
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ MTCustEstr ºAutor  ³Aecio Ferreira Gomesº Data ³ 18/06/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorno o codigo e custo unitario do componte da estru-  	 º±±
±±º			 ³ra ou pre-estrutura de acordo com a revisão.				 º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Produto posicionado para consulta.    				 ³±±
±±³			 ³ExpC2: Revisão da estrutua.			    				 ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATC015                                 	                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function MTCustEstr(cProduto,cRevisao)
Local aRet		:= {}
Local aArea		:= GetArea()
Local aAreaSB1	:= SB1->(GetArea())
Local cLocPad	:= ""
Local cCustd	:= ""
Local nQB		:= Posicione("SB1",1,xFilial("SB1")+cProduto,If(mv_par11 == 1,"B1_QB","B1_QBP")) //Quantidade base da estrutura do produto

If nQB <= 0
	nQB := 1
EndIf

If mv_par11 == 1
	DbSelectArea("SG1")
	DbSetOrder(1)
	If SG1->(DbSeek(FWxFilial("SG1")+cProduto))
		While SG1->(!Eof()) .And. SG1->G1_COD == cProduto
	        If !(SG1->G1_REVINI <= cRevisao .And. SG1->G1_REVFIM >= cRevisao)
	        	SG1->(DbSkip())
	        	Loop
	        EndIf
	        // Desconsidera produtos MOD
			If IsProdMod(SG1->G1_COD)
				SD3->(DbSkip())		
				Loop
			EndIf
	        //--Busca o local padrao e faz o calculo do custo medio do componente na estrura pelo custo standard
			If SB1->(DbSeek(FWxFilial("SB1")+SG1->G1_COMP))
				cLocPad := SB1->B1_LOCPAD
				cCustd  := ((SB1->B1_CUSTD * SG1->G1_QUANT)/nQB)
			EndIf
	        //--Busca o custo do produto
			If mv_par12 == 1 .And. !Empty(cCustd)// Custo stantard		 
				AADD(aRet,{SG1->G1_COMP,cCustd})	
			ElseIf mv_par12 == 2 .And. SB2->(DbSeek(FWxFilial("SB2")+SG1->G1_COMP+cLocPad)) // Custo medio
				AADD(aRet,{SG1->G1_COMP,((SB2->B2_CM1 * SG1->G1_QUANT)/nQB)})
			EndIf
			SG1->(DbSkip())
		End
	EndIf
Else 
	DbSelectArea("SGG")
	DbSetOrder(1)
	If SGG->(DbSeek(FWxFilial("SGG")+cProduto))
		While SGG->(!Eof()) .And. SGG->GG_COD == cProduto
	        If !(SGG->GG_REVFIM >= cRevisao)
	        	SGG->(DbSkip())
	        	Loop
	        EndIf	
	        // Desconsidera produtos MOD
			If IsProdMod(SGG->GG_COD)
				SD3->(DbSkip())		
				Loop
			EndIf
	        //--Busca o local padrao e faz o calculo do custo medio do componente na estrura pelo cuusto standard
			If SB1->(DbSeek(FWxFilial("SB1")+SGG->GG_COMP))
				cLocPad := SB1->B1_LOCPAD
				cCustd  := ((SB1->B1_CUSTD * SGG->GG_QUANT)/nQB)
			EndIf
	        //--Busca o custo do produto
			If mv_par12 == 1 .And. !Empty(cCustd)// Custo stantard		 
				AADD(aRet,{SGG->GG_COMP,cCustd})	
			ElseIf mv_par12 == 2 .And. SB2->(DbSeek(FWxFilial("SB2")+SGG->GG_COMP+cLocPad)) // Custo medio
				AADD(aRet,{SGG->GG_COMP,((SB2->B2_CM1 * SGG->GG_QUANT)/nQB)})
			EndIf
			SGG->(DbSkip())
		End
	EndIf
EndIf	

RestArea(aArea)
RestArea(aAreaSB1)
Return(aRet)   
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ A015Chart  ºAutor  ³Aecio Ferreira Gomesº Data ³ 18/06/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Instancia o Gráfico.									     º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Expo1: Objeto que será instanciado.    					 ³±±
±±³			 ³Expo2: Layer onde o grafico será criado.    				 ³±±
±±³			 ³ExpN1: ID do Grafico.										 ³±±
±±³			 ³ExpC1: ID da coluna conforme divisao do Layer.  			 ³±±
±±³			 ³ExpC2: ID da janela que será criada no Layer.			   	 ³±±
±±³			 ³ExpC3: ID da Linha conforme divisao do layer				 ³±±
±±³			 ³ExpC1: Picture dos valores do grafico.	   				 ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATC015                                 	                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function A015Chart(oFWChart,oFWLayer,nGrafico,cId,cIDWindow,cIdLine,cPicture)     

oFWChart:= FWChartFactory():New()
oFWChart:= oFWChart:getInstance( nGrafico )
oFWChart:init( oFWLayer:getWinPanel( cId, cIDWindow, cIdLine ) )
oFWChart:setLegend( CONTROL_ALIGN_BOTTOM )
oFWChart:setPicture( cPicture)

Return 

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ A015VlData ºAutor  ³Aecio Ferreira Gomesº Data ³ 18/06/10 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida as datas de produção informa pelo usuário.			 º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Produto posicionado para consulta.    				 ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATC015                                 	                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function A015VlData() 
Local cAnoIni  := ""
Local cAnoFim  := ""
Local lRet	   := .T. 

cAnoIni  := substr(str(year(mv_par13),4),3)
cAnoFim  := substr(str(year(mv_par14),4),3)

If (mv_par13 > mv_par14)
	Aviso("A015VLDATA",STR0046, { "OK" }) //"A data inicial de produção não pode ser maior que a data final, por favor, informe uma data válida!" 
	lRet := .F.
EndIf

If lRet .And. (cAnoFim <> cAnoIni)
	Aviso("A015VLDATA",STR0047, { "OK" }) //"O periodo informado deve pertencer ao mesmo ano, por favor, informe uma data válida!"
	lRet := .F.
EndIf

Return lRet
