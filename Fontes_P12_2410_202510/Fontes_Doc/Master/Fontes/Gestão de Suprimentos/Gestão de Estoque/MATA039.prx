#INCLUDE "MATA039.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"
PUBLISH MODEL REST NAME MATA039 SOURCE MATA039 RESOURCE OBJECT oRestMATA039
 
//-------------------------------------------------------------------
/*{Protheus.doc} MATA039
Correlação de produtos 
*/
//-------------------------------------------------------------------
Function MATA039()
Local oBrowse := BrowseDef() 
	oBrowse:Activate()
Return 

Static Function BrowseDef() 
Local oBrowse

	D4C->(dbSetOrder(1)) //D4C_FILIAL+D4C_PRODUT+D4C_PRDATO
	
	oBrowse := FWmBrowse():New()
	oBrowse:SetAlias('D4C')
	oBrowse:SetDescription(STR0001) //Correlação de Produtos
			
Return oBrowse

//-------------------------------------------------------------------
/*{Protheus.doc} MenuDef
Monta opcoes de rotina do programa
*/
//-------------------------------------------------------------------
Static Function MenuDef() 

Private aRotina := {}

ADD OPTION aRotina TITLE STR0002 ACTION "VIEWDEF.MATA039" OPERATION MODEL_OPERATION_VIEW	ACCESS 0 //"Visualizar"	
ADD OPTION aRotina TITLE STR0003 ACTION "VIEWDEF.MATA039" OPERATION MODEL_OPERATION_INSERT	ACCESS 0 //"Incluir"		
ADD OPTION aRotina TITLE STR0004 ACTION "VIEWDEF.MATA039" OPERATION MODEL_OPERATION_UPDATE	ACCESS 0 //"Alterar"		
ADD OPTION aRotina TITLE STR0005 ACTION "VIEWDEF.MATA039" OPERATION MODEL_OPERATION_DELETE	ACCESS 3 //"Excluir"	

Return aRotina

//-------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Modelo de dados de Corelação de Produtos
/*/
//-------------------------------------------------------------------
Static Function ModelDef()

Local oModel 	:= NIL
Local oStruD4C	:= FWFormStruct(1,'D4C',{|cCampo| AllTRim(cCampo) $ "D4C_PRODUT|D4C_DESPAI|D4C_UM"})
Local oStruGrid := FWFormStruct(1,'D4C',{|cCampo| (AllTRim(cCampo) $ "D4C_PRDATO|D4C_DESCAT|D4C_UMATO|D4C_QTPRAT")})
oModel := MPFormModel():New('MATA039', /*bPreValidacao*/, {|oModel|C039VldGrv(oModel)}/*bPosValidacao*/, /*bCommit*/, /*bCancel*/ )

oModel:AddFields('D4CMASTER',NIL,oStruD4C)
oModel:AddGrid('D4CDETAILS','D4CMASTER',oStruGrid,,{|oModel,nLin|M039VldLin(oModel,nLin)},,/*{|oModel,nLin|M039VldLin(oModel,nLin)}*/)

oModel:SetRelation('D4CDETAILS',{{'D4C_FILIAL','xFilial("D4C")'},{"D4C_PRODUT","D4C_PRODUT"}},D4C->(IndexKey(1)))
oModel:GetModel("D4CDETAILS"):SetDelAllLine(.T.)
oModel:GetModel("D4CDETAILS"):SetMaxLine(GetNewPar("MV_COMLMAX", 99999))
oModel:SetDescription(STR0001)	//"Correlação de Produtos"
oModel:SetPrimaryKey({"D4C_FILIAL","D4C_PRODUT","D4C_PRDATO"})
oModel:GetModel('D4CDETAILS'):SetUniqueLine({'D4C_PRDATO'}) //Obedece o critério de chave única

oStruD4C:SetProperty('D4C_PRODUT', MODEL_FIELD_OBRIGAT, .T.)
oStruGrid:SetProperty('D4C_PRDATO', MODEL_FIELD_OBRIGAT, .T.)
oStruGrid:SetProperty('D4C_QTPRAT', MODEL_FIELD_OBRIGAT, .T.)

oModel:SetPrimaryKey({"D4C_PRODUT"})

Return oModel

//-------------------------------------------------------------------
/*{Protheus.doc} ViewDef
Interface do Modelo de dados de Corelação de Produtos
*/
//-------------------------------------------------------------------
Static Function ViewDef()

Local oView		:= NIL
Local oModel	:= FWLoadModel('MATA039')
Local oStruD4C  := FWFormStruct(2,"D4C", {|cCampo|   AllTRim(cCampo) $ "D4C_PRODUT|D4C_DESPAI|D4C_UM"})
Local oStruGRID := FWFormStruct(2,"D4C", {|cCampo| !(AllTRim(cCampo) $ "D4C_PRODUT|D4C_DESPAI|D4C_UM")})

oStruD4C:SetNoFolder()

oView:= FWFormView():New() 
oView:SetModel(oModel)              

oView:AddField('VIEW_D4C', oStruD4C, 'D4CMASTER')
oView:AddGrid ('GRID_D4C', oStruGRID, 'D4CDETAILS' )

oView:CreateHorizontalBox("MAIN",25)
oView:CreateHorizontalBox("GRID",75)

oView:SetOwnerView('VIEW_D4C','MAIN')
oView:SetOwnerView('GRID_D4C','GRID')
oView:EnableControlBar(.T.)

Return oView

//-------------------------------------------------------------------
/*/{Protheus.doc} C039VldGrv
Validação total do modelo
/*/
//-------------------------------------------------------------------
Static Function C039VldGrv(oModel)

Local lRet	 	 := .T.

//Bloqueia inclusão de produto pai já cadastrado
If oModel:GetOperation() == MODEL_OPERATION_INSERT
	lRet := ExistChav('D4C', 'D4C_PRODUT')
EndIf

Return lRet


//-------------------------------------------------------------------
/*/{Protheus.doc} C039VldPrd
Valid do campo D4C_PRODUT
/*/
//-------------------------------------------------------------------
Function C039VldPrd()

Local lRet	 	 := .T.

If lRet  
    D4C->(dbSetOrder(1))
    D4C->(dbSeek(xFilial("D4C")+M->D4C_PRODUT))
    If D4C->(!EOF()) .and. D4C->D4C_PRODUT == M->D4C_PRODUT
        Help(" ",1,"JAGRAVADO")
        lRet := .F.
    EndIf
EndIf 
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} C039VldPrA
Validação do campo D4C_PRDATO
/*/
//-------------------------------------------------------------------
Function C039VldPrA()
	Local lRet	 	 := .T.

Return lRet

/*/{Protheus.doc} M039VldLin(oModel,nLin)
	Validação total da linha
/*/
Static Function M039VldLin(oModelGrid,nLin)
	Local lRet := .T.
	Local oModel := oModelGrid:GetModel()
	Local cProdP := oModel:GetValue('D4CMASTER','D4C_PRODUT')
	Local cProdF := oModelGrid:GetValue('D4C_PRDATO', nLin)

	If cProdP == cProdF
	    Help(,, "M039VldLin",, "O produto pai não pode ser informado como filho", 1, 0,,,,,,;
		{"Informe outro produto"})
        lRet := .F.
	EndIf

Return lRet

//Funcionalidades REST API

/*/{Protheus.doc} oRestMATA039
	Instancia do FwRestModel 
	@type  Class
	@author Vicente Gomes
	@since 11/03/2024
	@version 1.0	
/*/
Class oRestMATA039 From FwRestModel	
	
	Method Activate()
	Method DeActivate()
	Method Seek()
	Method Skip()
	Method SaveData()	
EndClass
/*/{Protheus.doc} 
	Activate
/*/
Method Activate() Class oRestMATA039
    dbSelectArea("D4C")   
	dbSetOrder(1)
Return _Super:Activate()
/*/{Protheus.doc} 
	DeActivate
/*/
Method DeActivate() Class oRestMATA039
    D4C->(dbCloseArea())    
Return _Super:DeActivate()


//-------------------------------------------------------------------
/*/{Protheus.doc} Seek
Método responsável buscar um registro em específico no alias selecionado.
Se o parametro cPK não for informaodo, indica que deve-se ser posicionado
no primeiro registro da tabela.
@param	cPK						PK do registro.
@return	lRet	Indica se foi encontrado algum registro.
@author Vicente Gomes
@since 11/03/2024
/*/
//-------------------------------------------------------------------
Method Seek(cPK) Class oRestMATA039
Local lRet := .F.

if empty(cPK)		
	D4C->(DbGotop())
    lRet := !D4C->(Eof())
elseif !Empty(cPK)    
	If D4C->(dbSeek(cPK)) //cPK == Filial + Codigo + UniCom
       lRet := .T. 
    EndIf	
EndIf

Return lRet

/*/{Protheus.doc} Skip
	Pula registro
	@author Vicente Gomes
	@since 11/03/2024
	@version 1.0
	@param nSkip
	@return lRet
/*/
Method Skip(nSkip) Class oRestMATA039
Local lRet := .F. 
    D4C->(DbSkip(nSkip))
    lRet := !D4C->(Eof()) 
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} SaveData
Método responsável por salvar o registro recebido pelo metodo PUT ou POST.
Se o parametro cPK não for informado, significa que é um POST.

@param	cPK			PK do registro.
@param	cData		Conteúdo a ser salvo
@param	@cError	Retorna o alguma mensagem de erro
@return	lRet		Indica se o registro foi salvo

@author Vicente Gomes
@since 11/03/2024
/*/
//-------------------------------------------------------------------
Method SaveData(cPK, cData, cError) Class oRestMATA039
local lRet := .T.
Local oJson  as oBject

Default cData	:= ""

	If Empty(cPk)
		self:oModel:SetOperation(MODEL_OPERATION_INSERT)
	Else
		self:oModel:SetOperation(MODEL_OPERATION_UPDATE)
		lRet := self:Seek(cPK)
	EndIf

	If lRet
		self:oModel:Activate()
		//Pega o texto e transforma em objeto
   		oJson := JsonObject():New()
    	oJson:FromJson(cData)
		cUnicom := oJson["models"][1]["models"][1]["items"][1]["fields"][2]["value"]		
		lRet := self:oModel:LoadJsonData(cData)			
		If lRet
            If self:oModel:lModify // Verifico se o modelo sofreu alguma alteração
                self:oModel:VldData()
				self:oModel:CommitData()
            EndIf
        Else
            cError := ErrorMessage(self:oModel:GetErrorMessage())
        EndIf
		Self:oModel:DeActivate()
	Else
		cError := i18n("Invalid record '#1' on table #2", {cPK, self:cAlias})
	EndIf
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} ErrorMessage
Funcao responsavel por retonar o erro do modelo.

@param aErroMsg Array de erro do modelo de dados

@return cRet Formato texto do array de erro do modelo de dados

@author Felipe Bonvicini Conti
@since 05/04/2016
@version P11, P12
/*/
//-------------------------------------------------------------------
Static Function ErrorMessage(aErroMsg)
Local cRet := CRLF + " --- Error on Model ---" + CRLF
	cRet += "Id submodel origin: [" + aErroMsg[1] + "]" + CRLF
	cRet += "Id field origin: [" + aErroMsg[2] + "]" + CRLF
	cRet += "Id submodel error: [" + aErroMsg[3] + "]" + CRLF
	cRet += "Id field error: [" + aErroMsg[4] + "]" + CRLF
	cRet += "Id error: [" + aErroMsg[5] + "]" + CRLF
	cRet += "Error menssage: [" + aErroMsg[6] + "]" + CRLF
	cRet += "Solution menssage: [" + aErroMsg[7] + "]" + CRLF
	cRet += "Assigned value: [" + cValToChar( aErroMsg[8] ) + "]" + CRLF
	cRet += "Previous value: [" + cValToChar( aErroMsg[9] ) + "]" + CRLF
	aErroMsg := aSize(aErroMsg, 0)
Return cRet
