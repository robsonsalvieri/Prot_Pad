#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "backoffice.stock.trackcosts.crud.ch"

namespace totvs.protheus.backoffice.stock.trackcosts.CRUD

class CRUD

    PUBLIC DATA STRUCT
    PUBLIC DATA ERROR

    PUBLIC METHOD NEW() 
    PUBLIC METHOD INSERT() 
    PUBLIC METHOD UPDATE() 
    PUBLIC METHOD PATCH() 
    PUBLIC METHOD DELETE()
    PUBLIC METHOD SEEK()
    PUBLIC METHOD version()

endclass

/*/{Protheus.doc} ClosingProfile:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class ClosingProfile
    @type  Metodo
    @author squad.entradas
    @since  Fevereiro 04, 2022
    @version 12.1.33
/*/

Method NEW(cAlias) class CRUD

If !AliasInDic(cAlias)
    self:ERROR := STR0001
Else 
    self:STRUCT := &(cAlias)->(dbStruct())
Endif

return Self

/*/{Protheus.doc} ClosingProfile:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class ClosingProfile
    @type  Metodo
    @author squad.entradas
    @since  Fevereiro 04, 2022
    @version 12.1.33
/*/

Method INSERT(cAlias, oObj) class CRUD

Local xAux := NIL
Local nX        as Numeric
Local nType := NIL
DEFAULT oObj := JsonObject():New()

RecLock(cAlias, .T.)

For nX := 1 to Len(self:STRUCT)
    If oObj:HasProperty(self:STRUCT[nX][1])
        xAux := oObj:GetJsonObject(self:STRUCT[nX][1])

        If (ValType(xAux) == self:STRUCT[nX][2]) .OR. (self:STRUCT[nX][2] == "M" .AND. VALTYPE(xAux) == "C")
            &(cAlias)->(&(AllTrim((self:STRUCT[nX][1])))) := xAux
        Else
            nType := self:STRUCT[nX][2]
            Do Case
                Case nType == "D"
                    &(cAlias)->(&(AllTrim((self:STRUCT[nX][1])))) := CtoD("  /  /    ")
                Case nType == "C"
                    &(cAlias)->(&(AllTrim((self:STRUCT[nX][1])))) := ""
                Case nType == "N"
                    &(cAlias)->(&(AllTrim((self:STRUCT[nX][1])))) := 0
                Case nType == "M"
                    &(cAlias)->(&(AllTrim((self:STRUCT[nX][1])))) := ""
            EndCase
        EndIf
    Else
        nType := self:STRUCT[nX][2]
        Do Case
            Case nType == "D"
                &(cAlias)->(&(AllTrim((self:STRUCT[nX][1])))) := CtoD("  /  /    ")
            Case nType == "C"
                &(cAlias)->(&(AllTrim((self:STRUCT[nX][1])))) := ""
            Case nType == "N"
                &(cAlias)->(&(AllTrim((self:STRUCT[nX][1])))) := 0
            Case nType == "M"
                &(cAlias)->(&(AllTrim((self:STRUCT[nX][1])))) := ""
        EndCase
    EndIf

Next

&(cAlias)->(MSUnlock())

return Self

/*/{Protheus.doc} ClosingProfile:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class ClosingProfile
    @type  Metodo
    @author squad.entradas
    @since  Fevereiro 04, 2022
    @version 12.1.33
/*/
Method UPDATE(cAlias, oObj) class CRUD

Local xAux      := NIL
Local nX        := 0
Default oObj    := JsonObject():New()

RecLock(cAlias, .F.)

For nX := 1 to Len(self:STRUCT)
    If oObj:HasProperty(self:STRUCT[nX][1])
        xAux := oObj:GetJsonObject(self:STRUCT[nX][1])

        Do Case
            Case self:STRUCT[nX][2] == "C"
                If !Empty(xAux) .and.  Valtype(xAux) == "C"
                    &(cAlias)->(&(self:STRUCT[nX][1])) := xAux
                Endif
            Case self:STRUCT[nX][2] == "M"
                If xAux <> nil .and. Valtype(xAux) == "C"
                    &(cAlias)->(&(self:STRUCT[nX][1])) := xAux
                Endif
            Case self:STRUCT[nX][2] == "D"
                If xAux <> nil .and. Valtype(xAux) == "D"
                    &(cAlias)->(&(self:STRUCT[nX][1])) := xAux
                Endif
            Case self:STRUCT[nX][2] == "N"
                If xAux <> nil .and. Valtype(xAux) == "N"
                    &(cAlias)->(&(self:STRUCT[nX][1])) := xAux
                Endif
        EndCase
    Endif
Next
        
&(cAlias)->(MSUnlock())

return Self

/*/{Protheus.doc} ClosingProfile:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class ClosingProfile
    @type  Metodo
    @author squad.entradas
    @since  Fevereiro 04, 2022
    @version 12.1.33
/*/

Method PATCH(cAlias, cField, cContent) class CRUD
    Local cTypeContent := VALTYPE(cContent)
    Local lField := .F.

    Default cField := ""

    cField := UPPER(Alltrim(cField))

    If !Empty(cField) .OR. VALTYPE(cField) == 'C'
       
        lField := aScan(self:STRUCT, {|x| AllTrim(Upper(x[1])) == cField}) > 0

        If lField 
            If (cTypeContent == VALTYPE(cContent)) .OR. (cTypeContent == "M" .AND. VALTYPE(cContent) == "C")
                RecLock(cAlias, .F.)
                &(cAlias)->(&(cField)) := cContent
                &(cAlias)->(MSUnlock())
            Endif
        Endif

    Endif

Return Self

/*/{Protheus.doc} ClosingProfile:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class ClosingProfile
    @type  Metodo
    @author squad.entradas
    @since  Fevereiro 04, 2022
    @version 12.1.33
/*/

Method DELETE(cAlias, cField, cId, lAdvpr) class CRUD

Local cQuery := ''
Local lRet := .T.
Local nPos := 0
Local nRet   := 0

Default cField := ""
Default cAlias := ""
Default lAdvpr := .F.

If !Empty(cField) .OR. !Empty(cAlias)

    nPos := aScan(self:STRUCT, {|x| AllTrim(Upper(x[1])) == cField})

    If nPos > 0
        If self:STRUCT[nPos][2] == Valtype(cId)

            cQuery := "DELETE FROM " + RetSQLName(cAlias) + " WHERE D_E_L_E_T_ = ' ' AND " + cField
            cQuery += " = '" + cId + "'"

            nRet := TCSQLExec(cQuery)

            If nRet < 0 .or. lAdvpr
                lRet := .F.
            Endif

        Endif 
    Endif
Endif
  
Return lRet

/*/{Protheus.doc} ClosingProfile:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class ClosingProfile
    @type  Metodo
    @author squad.entradas
    @since  Fevereiro 04, 2022
    @version 12.1.33
/*/

Method SEEK(cAlias, nIndex, cString) class CRUD

    DbSelectArea(cAlias)
    &(cAlias)->(DbSetOrder(nIndex))

Return &(cAlias)->(DbSeek( cString ) )

/*/{Protheus.doc} version
    Metodo responsavel por indicar a versao da classe
    @type  Method
    @author Adriano Vieira
    @since  15/02/2024
    @version 12.1.23.10
/*/
Method version() class CRUD
    Local nVersion := 200
Return nVersion
