#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace ac.BasicValidation.Controller
using namespace ac.BasicValidation.Service

class acBasicValidationController

    public  method New()
    public  method version()
    public  method GetDictionaryValid()

    @Get("api/stock/trackcosts/kardex/v1/ESTR900/access")
    public  method getRelease()

    @Get("api/stock/process/v1/fieldsstruct")
    public method getFieldsStruct()

endclass

/*/{Protheus.doc} acBasicValidationController:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class acBasicValidationController
    @type  Metodo
    @author felipe.suetoshi
    @since  Mai  06, 2021
    @version 12.1.27
/*/
Method new() class acBasicValidationController
return Self

/*/{Protheus.doc} acConfigProf:getConfigProfile()
    Metodo responsavel retornar sx1 ou profile do usuário
    @type  Metodo
    @author felipe.suetoshi
    @since  Mai  06, 2021
    @version 12.1.27
/*/

Method GetDictionaryValid(lSecond) class acBasicValidationController
    Local oResponse as Object
    Local oService  as Object

    Default lSecond := .F.

    oResponse   := JsonObject():New()
    oService    := ac.BasicValidation.Service.acBasicValidationService():new()
    oResponse   := oService:GetDictionaryValidator(lSecond) 
 
return oResponse

/*/{Protheus.doc} postInsertMovTotal()
    Recebe os dados da requisicao de POST para efetuar o INSERT na tabela D4B
    @type  Method
    @author Adriano Vieira
    @since 20/01/2023
    @version 1.0
    //Exemplo POSTMAN
    {
        "parameters": [
            {"parameter": "mv_par01","value": "pcpB0001001"}, // OP
            {"parameter": "mv_par02","value": 1}, // Moeda
            {"parameter": "mv_par03","value": "20140701"}, DATA DE 
            {"parameter": "mv_par04","value": "20220731"}, DATA ATE
            {"parameter": "mv_par05","value": 1}, //   QUANTIDADE TOTAL POR OP (VERIFICAR)
            {"parameter": "mv_par06","value": 1}, QUAL CUSTO IMPRIMIR
            {"parameter": "mv_par07","value": id(ALEATORIO)}
        ],
        "branches": [
            {
                "Code": "D MG 01 ",
                "Cgc": "53485215000106",
                "Description": "Filial BELO HOR                          "
            }
        ]
    }
/*/
method getRelease() class acBasicValidationController
    
    local jError := JsonObject():New()
    local jResponse := JsonObject():New()
    Local cResponse := ""
    Local oService  as Object
    Local oDictionaryValidator  as Object
    Local lSecond   := .T.
    
    oDictionaryValidator := ::GetDictionaryValid(lSecond)

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    oService := ac.BasicValidation.Service.acBasicValidationService():new()
    
    If !oDictionaryValidator['bIsValid']
        oRest:setStatusCode(oDictionaryValidator['nErrorNumber'])
        oRest:setResponse(oDictionaryValidator['cErrorMsg'])
    Else 
        jResponse["authorized"] := oService:getReleaseValid()
        cResponse := jResponse:toJson()
        oRest:setResponse(cResponse) 
    Endif

return Self

/*/{Protheus.doc} version
    Metodo responsavel por indicar a versao da classe
    @type  Method
    @author Adriano Vieira
    @since  15/02/2024
    @version 12.1.23.10
/*/
method version() class acBasicValidationController
    Local nVersion := 200
Return nVersion

/*/{Protheus.doc} getFieldsStruct
    Retorna estrutura dos campos
    @author Squad.Entradas
    @since 29/04/2024
    @version 1.0
    @return json com resposta requisicao
    /*/
Method getFieldsStruct() Class acBasicValidationController

    Local oDictionaryValidator as object
    Local oService as object
    Local jResponse := JsonObject():New() as json
    Local cResponse := "" as character
    Local lSecond   := .T. as logical

    oDictionaryValidator := ::GetDictionaryValid(lSecond)

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    oService := ac.BasicValidation.Service.acBasicValidationService():new()

    If !oDictionaryValidator['bIsValid']
        oRest:setStatusCode(oDictionaryValidator['nErrorNumber'])
        oRest:setResponse(oDictionaryValidator['cErrorMsg'])
    Else
        jResponse := oService:getFieldsStruct()
        cResponse := jResponse:toJson()
        oRest:setResponse(cResponse) 
    EndIf

Return
