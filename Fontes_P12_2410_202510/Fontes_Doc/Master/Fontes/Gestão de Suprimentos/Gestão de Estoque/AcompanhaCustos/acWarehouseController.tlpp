#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace ac.Warehouse.Controller

using namespace ac.acWarehouse.Repository
using namespace ac.acWarehouse.Service

class acWarehouseController
    public data  jBody as object
    public data  jParams as   Object
    public data  cData as character
    public data  cPage as Character
    public data  cPageSize as Character
    public data  cFilter as Character

    public method New()
    public method version()

    @get("api/stock/proccess/v1/warehouses")
    public method getWarehouses()

endclass

/*/{Protheus.doc} acWarehouseController:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class acWarehouseController
    @type  Metodo
    @author felipe.suetoshi
    @since  Abril  28, 2021
    @version 12.1.27
/*/

Method new() class acWarehouseController
    ::jBody     := JsonObject():New()
    ::cData     := ''
return Self

/*/{Protheus.doc} postWarehouses:New()
    Metodo responsavel por retornar uma lista de armazens em determinada filial
    @type  Metodo
    @author felipe.suetoshi
    @since  Abril  28, 2021
    @version 12.1.27
/*/

Method getWarehouses() class acWarehouseController
    Local jBodyResponse as Object
    Local aCode       as Array
    Local aBranch       as Array
    Local nPage         as Numeric     
    Local nPageSize     as Numeric
    Local cBranch       as Character
    Local cCode         as Character

    oRest:setKeyHeaderResponse("Content-Type", "application/json")
    ::jParams        := oRest:getQueryRequest()
    jBodyResponse    := JsonObject():New()

    ::cPage             := ::jParams['page']
    ::cPageSize         := ::jParams['pageSize']
    cBranch             := ::jParams['branch']
    cCode               := ::jParams['code']

    if empty(::cPage)
        nPage := 1
    else
        nPage := VAL(::cPage) 
    endif

    if empty(::cPageSize)
        nPageSize := 10
    else 
        nPageSize := Val(::cPageSize) 
    endif

    if Empty(cBranch) .or. (cBranch == 'null' .OR. cBranch == 'undefined') 
        aBranch := {{"FILTER", ''}}
    else 
        aBranch := {{"FILTER", cBranch}}
    endif

    if Empty(cCode) .or. (cCode == 'null' .OR. cCode == 'undefined') 
        aCode := {{"FILTER", ''}}
    else 
        aCode := {{"FILTER", cCode}}
    endif

    oService := ac.Warehouse.Service.acWarehouseService():new()
    oRest:setResponse( oService:getWarehouses(nPage, nPageSize, aBranch, aCode))
return .T.

/*/{Protheus.doc} version
    Metodo responsavel por indicar a versao da classe
    @type  Method
    @author Adriano Vieira
    @since  15/02/2024
    @version 12.1.23.10
/*/
Method version() class acWarehouseController
    Local nVersion := 200
Return nVersion
