#Include "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH" 
#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'BACKOFFICE.PGC.SUPPLIER.REPOSITORY.CH'

namespace pgc.supplierRepository
using namespace pgc.utils

/*/{Protheus.doc} pgcSupplierRepository
	Classe repository para o serviço de fornecedores
@author juan.felipe
@since 23/02/2022
/*/
Class pgcSupplierRepository FROM FWAdapterBaseV2
    public Data cSupplierCode As Character
    public Data cStore As Character
    public Data cAreaCode  As Character
    public Data cTelephone As Character
    public Data cEmail As Character
    public Data lOk As Logical
    public Data oJsonRequest As Object

    public Method New()
    public Method Get()
    public Method postSupplier()
    public Method putUpdateContacts()
	private Method TreatJSONResponse()
EndClass


/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 23/02/2022
/*/
Method New(cVerb) Class pgcSupplierRepository
    Default cVerb := "GET"

    Self:cSupplierCode := ""
    Self:cStore        := ""
    Self:cAreaCode     := ""
    Self:cTelephone    := ""
    Self:cEmail        := ""
    Self:lOk           := .F.
    Self:oJsonRequest       := Nil

    _Super:New(cVerb, .T.)
Return Self

/*/{Protheus.doc} Get
	Consulta fornecedores
@author juan.felipe
@since 23/02/2022
@param nPage, numeric, número da pagina.
@param nPageSize, numeric, tamanho da página.
@param cSupplierCode, character, código do fornecedor.
@param cStore, character, loja do fornecedor.
@param nOption, numeric, 1- Todos fornecedores, 2 - Últimos Fornecedores.
@param aProducts, array, Produtos para retorno de Últimos Fornecedores
@param nQuantitySuppliers, numeric, Quantidade de Últimos Fornecedores
@return Nil, nulo.
/*/
Method Get(nPage, nPageSize, cSupplierCode, cStore, nOption, aProducts, nQuantitySuppliers) class pgcSupplierRepository
    Local aArea  As Array
    Local aQueryRequest As Array
    Local cWhere As Character
    Local oUtils As Object
    
    Default nPage        := 1
	Default nPageSize    := 10
	Default cSupplierCode:= ""
    Default nOption      := 0
    Default aProducts    := {}
    Default nQuantitySuppliers := 0
    
    aArea := FwGetArea()
    aQueryRequest := {}
    cWhere := GetWhere(cSupplierCode, cStore, nOption, aProducts, nQuantitySuppliers)
    oUtils := pgcUtils():New()
    AddMapFields( self )

    Self:setPage(nPage)
    Self:setPageSize(nPageSize)
    Self:SetQuery(GetQuery())
    Self:SetWhere(cWhere)
    Self:SetOrder(SqlOrder(SA2->(IndexKey(1))))
    Self:SetUseSpaces(.T.)

    aQueryRequest := oUtils:queryRequestToArray(oRest:getQueryRequest())

    If Len(aQueryRequest) > 0
        Self:SetUrlFilter(aQueryRequest)
    EndIf

    Self:SetOrderQuery(oUtils:getQueryParam('ORDER'))
    Self:SetFields(oUtils:getQueryParam('FIELDS'))
    
    If Self:Execute()
        Self:FillGetResponse()
		Self:TreatJSONResponse(Self:oJsonObj:oJsonObj)
    EndIf

    FwRestArea(aArea)
    FwFreeArray(aArea)
    FreeObj(oUtils)
Return Nil

/*/{Protheus.doc} AddMapFields
	Cria o Mapa de campos Protheus x API para os fornecedores.
@author juan.felipe
@since 23/02/2022
@param oSelf, object, Objeto - Objeto com herança da classe FWAdapterBaseV2.
@return Nil, nulo.
/*/
Static Function AddMapFields(oSelf)
    oSelf:AddMapFields('branch'      , 'A2_FILIAL', .T., .T., {'A2_FILIAL' , 'C', TamSX3('A2_FILIAL')[1], 0})
    oSelf:AddMapFields('supplierCode', 'A2_COD'   , .T., .T., {'A2_COD'    , 'C', TamSX3('A2_COD'   )[1], 0})
    oSelf:AddMapFields('store'       , 'A2_LOJA'  , .T., .T., {'A2_LOJA'   , 'C', TamSX3('A2_LOJA'  )[1], 0})
    oSelf:AddMapFields('companyName' , 'A2_NOME'  , .T., .F., {'A2_NOME'   , 'C', TamSX3('A2_NOME'  )[1], 0})
    oSelf:AddMapFields('fantasyName' , 'A2_NREDUZ', .T., .F., {'A2_NREDUZ' , 'C', TamSX3('A2_NREDUZ')[1], 0})
    oSelf:AddMapFields('address'     , 'A2_END'   , .T., .F., {'A2_END'    , 'C', TamSX3('A2_END'   )[1], 0})
    oSelf:AddMapFields('district'    , 'A2_BAIRRO', .T., .F., {'A2_BAIRRO' , 'C', TamSX3('A2_BAIRRO')[1], 0})
    oSelf:AddMapFields('state'       , 'A2_EST'   , .T., .F., {'A2_EST'    , 'C', TamSX3('A2_EST'   )[1], 0})
    oSelf:AddMapFields('county'      , 'A2_MUN'   , .T., .F., {'A2_MUN'    , 'C', TamSX3('A2_MUN'   )[1], 0})
    oSelf:AddMapFields('zipCode'     , 'A2_CEP'   , .T., .F., {'A2_CEP'    , 'C', TamSX3('A2_CEP'   )[1], 0})
    oSelf:AddMapFields('type'        , 'A2_TIPO'  , .T., .F., {'A2_TIPO'   , 'C', TamSX3('A2_TIPO'  )[1], 0})
    oSelf:AddMapFields('cnpj'        , 'A2_CGC'   , .T., .F., {'A2_CGC'    , 'C', TamSX3('A2_CGC'   )[1], 0})
    oSelf:AddMapFields('countryCode' , 'A2_DDI'   , .T., .F., {'A2_DDI'    , 'C', TamSX3('A2_DDI'   )[1], 0})
    oSelf:AddMapFields('areaCode'    , 'A2_DDD'   , .T., .F., {'A2_DDD'    , 'C', TamSX3('A2_DDD'   )[1], 0})
    oSelf:AddMapFields('telephone'   , 'A2_TEL'   , .T., .F., {'A2_TEL'    , 'C', TamSX3('A2_TEL'   )[1], 0})
    oSelf:AddMapFields('email'       , 'A2_EMAIL' , .T., .F., {'A2_EMAIL'  , 'C', TamSX3('A2_EMAIL' )[1], 0})
Return Nil

/*/{Protheus.doc} GetQuery
	Monta a expressão SQL para consulta dos fornecedores.
@author juan.felipe
@since 23/02/2022
@return cQuery, string, query dos fornecedores.
/*/
Static Function GetQuery()
    Local cQuery As Character
    
    cQuery := "SELECT #QueryFields# "
    cQuery += "FROM " + RetSqlName( "SA2" ) + " SA2 "
    cQuery += "WHERE #QueryWhere#"
Return cQuery


/*/{Protheus.doc} GetWhere
	Monta a expressão SQL WHERE para consulta dos fornecedores
@author juan.felipe
@since 23/02/2022
@param cSupplierCode, character, código do fornecedor.
@param cStore, character, loja do fornecedor.
@param nOption, numeric, 1- Todos fornecedores, 2- Últimos Fornecedores., 3 - Fornecedores devolvidos pela execauto NFCFILFOR.
@param aProducts, array, Produtos para retorno de Últimos Fornecedores
@param nQuantitySuppliers, numeric, Quantidade de Últimos Fornecedores
@return cQuery, string, expressão where para consulta dos fornecedores.
/*/
Static Function GetWhere(cSupplierCode, cStore, nOption, aProducts, nQuantitySuppliers)
    Local cWhere As Character
    Local nP As Integer
    Local nX As Integer
    Local cUltForn As Character
    Local aUltForn As Array
    Local aLastSuppliers As Array
    Local lOracle As Logical
    Default cSupplierCode := ""
    Default nOption       := 0
    Default aProducts     := {}
    Default nQuantitySuppliers := 0

    lOracle := Upper(Alltrim(TcGetDb())) == "ORACLE"

    cWhere := "SA2.A2_FILIAL = '" + FWxFilial('SA2') + "' "

    If !Empty(cSupplierCode) .And. !Empty(cStore)
        cWhere += "AND SA2.A2_COD = '" + cSupplierCode + "' "
        cWhere += "AND SA2.A2_LOJA = '" + cStore + "' "
    EndIf

    If nOption == 2 .And. Len(aProducts) > 0 .or. (nOption == 3 .and. Existblock("NFCFILFOR"))
        cUltForn := "''"
        aUltForn := {}
        aLastSuppliers := {}
        
        If nQuantitySuppliers == 0
            Pergunte("MTA131",.F.)
            nQuantitySuppliers := MV_PAR02
        Endif

        if nOption == 2
            For nP := 1 To Len(aProducts)
                Aadd(aUltForn,a131UlForn(nQuantitySuppliers,aProducts[nP]))
            Next nP
        elseif nOption == 3
            aUltForn := ExecBlock("NFCFILFOR",.F.,.F.,{aProducts})
        endif

        For nP := 1 To Len(aUltForn) // Fornecedores por Produto
            For nX := 1 To Len(aUltForn[nP])
                If Ascan(aLastSuppliers,aUltForn[nP,nX,1]+aUltForn[nP,nX,2]) == 0
                    Aadd(aLastSuppliers,aUltForn[nP,nX,1]+aUltForn[nP,nX,2])
                    cUltForn += ",'"+aUltForn[nP,nX,1]+aUltForn[nP,nX,2]+"'"
                Endif
            Next nX
        Next nP

        If lOracle
            cWhere += "AND A2_COD || A2_LOJA IN ("+cUltForn+") "
        Else
            cWhere += "AND CONCAT(A2_COD,A2_LOJA) IN ("+cUltForn+") "
        Endif        
    Endif
        
    cWhere += "AND SA2.A2_MSBLQL IN(' ', '2') "
    cWhere += "AND SA2.D_E_L_E_T_ = ' '"
Return cWhere

/*/{Protheus.doc} TreatJSONResponse
	Trata retorno do JSON gerado pelo FWAdapter.
@author juan.felipe
@since 27/10/2022
@param nQuery, numeric, 1- Listagem de Fornecedores; 2- Listagem de itens; 3- Listagem de fornecedores ordenados por critério; 4- Listagem de SCs relacionadas.
@param oJsonResponse, object, JSON gerado pelo FWAdapter
@return Nil, nulo.
/*/
Method TreatJSONResponse(oJsonResponse) class pgcSupplierRepository
    Default oJsonResponse := Nil

    NFCSetContactList(oJsonResponse,1)
Return Nil

/*/{Protheus.doc} postSupplier
	Cadastra fornecedor.
@author juan.felipe
@since 12/07/2023
/*/
Method postSupplier() Class pgcSupplierRepository
    Local aAreas As Array
    Local aNames As Array
    Local aError As Array
    Local cBranchSC8 As Character
    Local cQuotationCode As Character
    Local cSupplier As Character
    Local cStore As Character
    Local cProposal As Character
    Local cCorporateName As Character
    Local cMessage As Character
    Local cField As Character
    Local xValue As Character
    Local cFieldType As Character
    Local cPicture As Character
    Local cKey As Character
    Local cLastKey As Character
    Local nX As Numeric
    Local nNoField As Numeric
    Local nCode As Numeric
    Local lHasRegistration As Logical
    Local oModel As Object
    Local oModelSA2 As Object
    Local oJsonRet As Object
    Local oUtils As Object
    
    oUtils := pgcUtils():New()
    oJsonRet := JsonObject():New()

    aAreas       := {SA2->(GetArea()), SC8->(GetArea()), GetArea()}
    aNames       := Self:oJsonRequest:GetNames() //-- Obtém nomes das propriedades do JSON
    aError       := {}
    cMessage     := ''
    cBranchSC8   := FWxFilial('SC8')
    lParticipant := Self:oJsonRequest['isparticipant']
    nNoField     := 0
    lHasRegistration := .F.
    
    If lParticipant
        cQuotationCode := PadR(Self:oJsonRequest['c8_num'    ], TamSX3('C8_NUM'    )[1], '')
        cSupplier      := PadR(Self:oJsonRequest['c8_fornece'], TamSX3('C8_FORNECE')[1], '')
        cStore         := PadR(Self:oJsonRequest['c8_loja'   ], TamSX3('C8_LOJA'   )[1], '')
        cProposal      := PadR(Self:oJsonRequest['c8_numpro' ], TamSX3('C8_NUMPRO' )[1], '')
        cCorporateName := PadR(Self:oJsonRequest['c8_fornome'], TamSX3('C8_FORNOME')[1], '')
        cCorporateName := Upper(cCorporateName)
    Endif
    
    oModel := FWLoadModel('MATA020M')
    //Verifico se está NIL, indicando que não existe localização. Se ocorrer, chamo a model MATA020, para gravar os dados
    if oModel == Nil
        oModel := FWLoadModel('MATA020')
    endif
    oModel:SetOperation(MODEL_OPERATION_INSERT)
    oModel:Activate()
    oModelSA2:= oModel:GetModel('SA2MASTER')

    For nX := 1 to Len(aNames)
        cField := aNames[nX]

        If 'A2_' $ Upper(cField) 
            xValue := Self:oJsonRequest[cField]
            
            If ValType(xValue) <> 'U'
                cField := Upper(cField)
                cFieldType := GetSx3Cache(cField, 'X3_TIPO')
                cPicture := AllTrim(GetSx3Cache(cField, 'X3_PICTURE'))

                If cFieldType == 'C' .And. cPicture $ '@!'
                    xValue := Upper(xValue)
                    
                    If lParticipant .And. cField == 'A2_NOME'
                        xValue := RTrim(cCorporateName)
                    EndIf
                ElseIf cFieldType == 'D'
                    xValue := SToD(xValue)
                EndIf

                If oModelSA2:CanSetValue(cField)
                    oModelSA2:SetValue(cField, xValue)
                EndIf

                Self:lOk := !oModel:HasErrorMessage()
                
                If !Self:lOk
                    Exit
                EndIf
            EndIf
        Else
            nNoField ++
        EndIf
    Next nX

    If nNoField == Len(aNames)
        cMessage := STR0005 //-- Não foram informados campos válidos para o cadastro do fornecedor.
        Self:lOk := .F.
    EndIf

    Begin Transaction
        Self:lOk := Self:lOk .And. oModel:VldData() .And. oModel:CommitData()

        If Self:lOk 
            If Self:oJsonRequest['isparticipant']
                SC8->(dbSetOrder(8)) //-- C8_FILIAL+C8_NUM+C8_FORNECE+C8_LOJA+C8_FORNOME

                If Self:lOk := SC8->(MsSeek(cBranchSC8+cQuotationCode+cSupplier+cStore+cCorporateName)) // Posiciona na Cotação do Fornecedor
                    A161AtuCot(SC8->C8_FORNOME, SA2->A2_COD, SA2->A2_LOJA) //-- Atualiza cotação do fornecedor para receber o código e loja
                    cMessage := STR0006 //-- O fornecedor foi cadastrado com sucesso.
                ElseIf Self:lOk := SC8->(MsSeek(cBranchSC8+cQuotationCode))
                    While SC8->(!Eof()) .And. SC8->(C8_FILIAL+C8_NUM+C8_FORNOME) <> cBranchSC8+cQuotationCode+cCorporateName
                        cLastKey := PadR(SC8->C8_FORNECE, TamSX3('A2_COD')[1]) + PadR(SC8->C8_LOJA, TamSX3('A2_LOJA')[1])
                        SC8->(DbSkip())
                    EndDo

                    If !Empty(SC8->(C8_FORNECE+C8_LOJA)) .Or. !Empty(cLastKey)
                        cMessage := STR0007 //-- Esta proposta já teve um fornecedor cadastrado.
                        Self:lOk := .F.
                        lHasRegistration := .T.

                        cKey := PadR(SC8->C8_FORNECE, TamSX3('A2_COD')[1]) + PadR(SC8->C8_LOJA, TamSX3('A2_LOJA')[1])
                        cKey := Iif(Empty(cLastKey), cKey, cLastKey)

                        SA2->(dbSetOrder(1)) //-- A2_FILIAL+A2_COD+A2_LOJA
                        SA2->(MsSeek(FWxFilial('SA2') + cKey)) //-- Posiciona na SA2 para retornar os dados do fornecedor
                    EndIf
                Else
                    cMessage := STR0008 + AllTrim(cQuotationCode) + STR0009 //-- A cotação XXX não foi localizada na base de dados.
                EndIf

                If !Self:lOk
                    DisarmTransaction() //-- Desarma transação em caso não encontre a cotação
                EndIf
            Else
                cMessage := STR0006 //-- O fornecedor foi cadastrado com sucesso.
            EndIf
        Else //-- Obtém o título do campo com erro
            aError := oModel:GetErrorMessage()
            cField := AllTrim(aError[4])

            If !Empty(cField)
                cMessage := STR0010 + AllTrim(GetSX3Cache(cField, 'X3_TITULO')) //-- Verifique o campo XXXX
            EndIf
        EndIf
    End Transaction
    
    nCode := Iif(Self:lOk, 200, 400)
    oJsonRet := oUtils:setClassResponse(nCode, cMessage, oModel) //-- Retorna mensagem da classe no formato json.

    oJsonRet['hasRegistration'] := lHasRegistration //-- Informa se a cotação já teve fornecedor cadastrato

    //-- Adiciona informações do fornecedor cadastrado no response
    oJsonRet['supplier'] := JsonObject():New()
    oJsonRet['supplier']['a2_cod' ] := SA2->A2_COD
    oJsonRet['supplier']['a2_loja'] := SA2->A2_LOJA
    oJsonRet['supplier']['a2_nome'] := SA2->A2_NOME
    oJsonRet['supplier']['a2_est' ] := SA2->A2_EST
    oJsonRet['supplier']['a2_mun' ] := SA2->A2_MUN

    oModel:Destroy()

    aEval(aAreas, {|x| RestArea(x), FwFreeArray(x)})
    FreeObj(oModel)
    FreeObj(oUtils)
    FwFreeArray(aNames)
    FwFreeArray(aError)
Return oJsonRet

/*/{Protheus.doc} putUpdateContacts
	Atualiza contatos do fornecedor.
@author juan.felipe
@since 08/03/2022
/*/
Method putUpdateContacts() Class pgcSupplierRepository
    Local aAreas As Array
    Local aError As Array
    Local cKeyReg As Character
    Local cMessage As Character
    Local nCode As Numeric
    Local oModel As Object
    Local oModelSA2 As Object
    Local oJsonRet As Object
    Local oUtils As Object
    
    aAreas   := {SA2->(GetArea()), GetArea()}
    aError   := {}
    cMessage := ''
    oJsonRet := JsonObject():New()
    oUtils := pgcUtils():New()
    
    SA2->(dbSetOrder(1))
    If Self:lOk := SA2->(MsSeek(xFilial('SA2') + Self:cSupplierCode + Self:cStore))
        
        cKeyReg := SA2->A2_COD + '-' + SA2->A2_LOJA

        If Self:lOk := !oUtils:isLocked('SA2', STR0004 /*Fornecedores*/, cKeyReg, @cMessage) //-- Valida se o registro está em uso
            oModel := FWLoadModel("MATA020M")
            //Verifico se está NIL, indicando que não existe localização. Se ocorrer, chamo a model MATA020, para gravar os dados
            if oModel == Nil
                oModel := FWLoadModel('MATA020')
            endif

            //-- Desativa campos obrigatórios do modelo MATA020M
            oModel:GetModel("SA2MASTER"):GetStruct():SetProperty("*", MODEL_FIELD_OBRIGAT, .F.)
            oModel:GetModel("BANCOS"   ):GetStruct():SetProperty("*", MODEL_FIELD_OBRIGAT, .F.)
            Iif(ChkFile("DHS"), oModel:GetModel("PROCREF"):GetStruct():SetProperty("*", MODEL_FIELD_OBRIGAT, .F.), Nil)
            Iif(ChkFile("DHT"), oModel:GetModel("SA2DEP" ):GetStruct():SetProperty("*", MODEL_FIELD_OBRIGAT, .F.), Nil)
            Iif(ChkFile("DKE"), oModel:GetModel("SA2DKE" ):GetStruct():SetProperty("*", MODEL_FIELD_OBRIGAT, .F.), Nil)

            oModel:SetOperation(4)
            oModel:Activate()

            oModelSA2:= oModel:GetModel("SA2MASTER")
            oModelSA2:SetValue("A2_DDD", Self:cAreaCode)         
            oModelSA2:SetValue("A2_TEL", Self:cTelephone) 
            oModelSA2:SetValue("A2_EMAIL", Self:cEmail) 
            
            If Self:lOk := oModel:VldData() .And. oModel:CommitData()
                cMessage := STR0001 //-- Contatos atualizados com sucesso.
            EndIf
            
            nCode := Iif(Self:lOk, 200, 400)
            oJsonRet := oUtils:setClassResponse(nCode, cMessage, oModel)
            
            If Self:lOk
                oJsonRet['areacode'] := SA2->A2_DDD
                oJsonRet['telephone'] := SA2->A2_TEL
                oJsonRet['email'] := SA2->A2_EMAIL
            EndIf

            oModel:Destroy()
        Else
            oJsonRet := oUtils:setClassResponse(400, cMessage)
        EndIf
    Else
        oJsonRet := oUtils:setClassResponse(400, STR0002 + Self:cSupplierCode + STR0003) //-- O fornecedor XXXX não foi localizado.
    EndIf

    FreeObj(oUtils)
    aEval(aAreas, {|x| RestArea(x), FwFreeArray(x) })
Return oJsonRet
