#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#Include 'FWMVCDef.ch'
#include 'BACKOFFICE.PGC.UTILS.REPOSITORY.CH'

namespace pgc.utilsRepository
using namespace tlpp.regex

/*/{Protheus.doc} pgcUtilsRepository
	Classe de serviços utilitários
@author juan.felipe
@since 18/11/2021
/*/
Class pgcUtilsRepository
    public Method New()

    public Method fieldsProperties()
    public Method companyInfo()
    public Method calcDiscount()
    public Method verifyMultiProtocol()
    public Method postWizardParamSv()
    public Method postWizardEmailSv()
    public Method existsWF7email()
    public Method TestEmailCreate()
    public Method createhtml()
    public Method setPostalBox()
    public Method getPurchaserList()
	public Method postMigrateLegacyQuote()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 22/12/2021
/*/
Method New() Class pgcUtilsRepository
Return Self

/*/{Protheus.doc} postFieldsProperties
	Consulta propriedades dos campos via SX3.
@author juan.felipe
@since 22/12/2021
@return oJsonResponse, object, resposta no formato json.
/*/
Method fieldsProperties(cAlias, aFields) Class pgcUtilsRepository
    Local aItems As Array
    Local nX As Numeric
    Local cField As Character
    Local oJsonResponse As Object
    Default aFields := {}
    Default cAlias  := ""

    aItems := {}
    oJsonResponse := JsonObject():New()
    For nX := 1 To Len(aFields)
        cField := AllTrim(aFields[nX])
        If AliasIndic(cAlias) .And. (cAlias)->(FieldPos(cField)) > 0
            aAdd(aItems, JsonObject():New())
            aItems[nX]['field'] := cField
            aItems[nX]['type'] := GetSx3Cache(cField, "X3_TIPO")
            aItems[nX]['length'] := GetSx3Cache(cField, "X3_TAMANHO")
            aItems[nX]['decimal'] := GetSx3Cache(cField, "X3_DECIMAL")
            aItems[nX]['picture'] := AllTrim(GetSx3Cache(cField, "X3_PICTURE"))
            aItems[nX]['combo'] := GetSx3Cache(cField, "X3_CBOX")
        EndIf
    Next nX

    oJsonResponse["items"] := aItems
Return oJsonResponse

/*/{Protheus.doc} companyInfo
	Consulta dados da empresa logada, tais como endereço de entrega e CNPJ
@author Leandro Fini
@since 13/12/2022
@return oJsonResponse, object, resposta no formato json.
/*/
Method companyInfo(cCompanyId, cBranchId) Class pgcUtilsRepository
    Local aItems As Array
    Local nX As Numeric
    Local oJsonResponse As Object
    Local aCompanyData As Array
    
    //Campos que serão enviados na resposta.
    Local aFields := { "M0_CODIGO","M0_CODFIL","M0_FILIAL","M0_NOME","M0_NOMECOM","M0_TEL","M0_CGC","M0_INSC","M0_INSCM","M0_ENDENT",;
                       "M0_COMPENT","M0_BAIRENT","M0_CIDENT","M0_ESTENT","M0_CEPENT","M0_CODMUN","M0_ENDCOB","M0_BAIRCOB","M0_CIDCOB","M0_ENDCOB","M0_CEPCOB"}
                

    Default cCompanyId := ""
    Default cBranchId  := ""

    aCompanyData := FWSM0Util():GetSM0Data(cCompanyId, cBranchId, aFields)

    oJsonResponse := JsonObject():New()
    aItems := {} 
    aAdd(aItems, JsonObject():New())
    For nX := 1 To Len(aCompanyData)
        cField := AllTrim(aCompanyData[nX][1])
        aItems[1][lower(cField)] := alltrim(aCompanyData[nX][2])
    Next nX

    oJsonResponse["items"] := aItems 

Return oJsonResponse

/*/{Protheus.doc} calcDiscount
	Realiza o cálculo de desconto de acordo com parâmetors enviados
    nPerc = Percentual de desconto
    nTotDiscount = Total do desconto em valor
    nTotValue = Total do documento
    aItems = Items que serão aplicados o desconto
@author Leandro Fini
@since 22/12/2022
@return oJsonResponse, object, resposta no formato json.
/*/
Method calcDiscount(nPerc, nTotDiscount, nTotValue, aItems) Class pgcUtilsRepository
    
    Local nRetTotDisc As Numeric //Total de desconto
    Local nRetTotValue As Numeric//Valor total descontado
    Local nRetPercDisc As Numeric//Percentual do valor de desconto
    Local nX           As Numeric
    Local oJsonResponse As Object
    Local aItemsRet     As Array

    Default nPerc := 0
    Default nTotDiscount := 0
    Default aItems := {}

    if nPerc > 0 
        nRetTotDisc := ( nTotValue * ( nPerc / 100 ) )
        nRetTotValue := nTotValue - nRetTotDisc
        nRetPercDisc := nPerc
    elseif nTotDiscount > 0
        nRetTotDisc := nTotDiscount
        nRetTotValue := nTotValue - nRetTotDisc
        nRetPercDisc := ( nTotDiscount / nTotValue )  * 100
    else 
        nRetTotDisc  := 0
        nRetTotValue := nTotValue
        nRetPercDisc := 0
    endif

    oJsonResponse := JsonObject():New()
    oJsonResponse["discount"]   := nRetTotDisc
    oJsonResponse["perc"]       := nRetPercDisc
    oJsonResponse["totalvalue"] := nRetTotValue
    oJsonResponse["items"] := {}

    aItemsRet := {}
    if !empty(aItems) .and. nRetPercDisc > 0
        for nX := 1 to len(aItems)
            aAdd(aItemsRet, JsonObject():New())
            aItemsRet[nX]["index"] := aItems[nX]["index"]
            aItemsRet[nX]["newitemvalue"] := aItems[nX]["itemvalue"] - ( aItems[nX]["itemvalue"] * ( nRetPercDisc / 100 ) )   
            aItemsRet[nX]["itemdiscount"] := ( aItems[nX]["itemvalue"] - aItemsRet[nX]["newitemvalue"] )
        next nX
       oJsonResponse["items"] := aItemsRet
    elseif !empty(aItems) .and. nRetPercDisc == 0
        for nX := 1 to len(aItems)
            aAdd(aItemsRet, JsonObject():New())
            aItemsRet[nX]["index"] := aItems[nX]["index"]
            aItemsRet[nX]["newitemvalue"] := aItems[nX]["itemvalue"]  
            aItemsRet[nX]["itemdiscount"] := 0
        next nX
       oJsonResponse["items"] := aItemsRet
    endif

Return oJsonResponse


/*/{Protheus.doc} verifyMultiProtocol
	Verifica se o MULTIPROTOCOLO está ativo no Appserver.ini do ambiente, verifica o parâmetro MV_PGCWF e se os arquivos html estão na pasta.
@author Renan Martins
@since 11/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method verifyMultiProtocol() Class pgcUtilsRepository
    local oJsonResponse As Object
    local lMultiProc    as logical
    local lFileWF       as logical
    local cWFDIR        as character
    local cNameFil1     as character
    local cNameFil2     as character
    local cAliasQry     as character

    lMultiProc := .f.
    lFileWF    := .t.
    cWFDIR     := "" 
    cNameFil1  := "\PGCA030_Mail001.HTML"
    cNameFil2  := "\PGCA030_Mail002.HTML"
    cAliasQry   := GetNextAlias()

    lMultiProc  := GetPvProfString("DRIVERS", "MULTIPROTOCOLPORT", "", GetSrvIniName()) == "1"
    cWFDIR      := AllTrim( WFGetMV( "MV_WFDIR", "" ) )

    if !( file(cWFDIR + cNameFil1) .or. file(cWFDIR + cNameFil2) )
       lFileWF := .f.
    endif
    
    oJsonResponse := JsonObject():New()
    oJsonResponse["activate"]       := lMultiProc
    oJsonResponse["fileWfexists"]   := lFileWF
    oJsonResponse["serversmtp"]     := alltrim(WFGetMV( "MV_PGCSMSR" , "" ))
    oJsonResponse["portsmtp"]       := WFGetMV( "MV_PGCSMPO", 0 )
    oJsonResponse["serverpop3imap"] := alltrim(WFGetMV( "MV_PGCPISV", "" ))
    oJsonResponse["portpop3imap"]   := WFGetMV( "MV_PGCPIPO", 0 )

Return oJsonResponse


/*/{Protheus.doc} postWizardParamSv
	Grava parâmetros na base e cria o Appserver.ini de modelo, com os dados imputados
    oJsonRequest = Body da requisição com os dados
@author Renan Martins
@since 11/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method postWizardParamSv(oJsonRequest) Class pgcUtilsRepository
    Local oJsonResponse     As Object
    local cPublicIP         as Character
    local cPortIP           as Character
    local cEnvirSrv         as Character
    local cParambrw         as Character
    local cDirWf            as Character
    local cAppSrvM          as Character
    local cSrtPath          as Character
    local cRootPath         as Character
    local cSrcPath          as Character
    local nErro             as Numeric
    local cCRLF             as Character
    local caddressSMTP      as Character
    local nportSMTP         as Numeric
    local caddressPopImp    as Character
    local nportPopImap      as Numeric
    local cTypeAuthent      as character 
    Default oJsonRequest := JsonObject():New()

    cAppSrvM        := ""
    cPublicIP       := alltrim(oJsonRequest:publicIP)
    cPortIP         := alltrim(oJsonRequest:portIP)
    cEnvirSrv       := alltrim(oJsonRequest:environmentSrv)
    cParambrw       := lower(oJsonRequest:paramBrw)
    cDirWf          := iif( left(alltrim(lower(oJsonRequest:paramWfDir)),1 ) != "\", "\", "" ) + alltrim(lower(oJsonRequest:paramWfDir)) //"\" + strtran(strtran(lower(oJsonRequest:paramWfDir), "\",""), "/", "")
    cSrtPath        := AllTrim(GetSrvProfString("StartPath", "\") )
    cRootPath       := alltrim(GetSrvProfString("ROOTPATH", ""))
    cSrcPath        := alltrim(GetSrvProfString("SourcePath", ""))
    cCRLF           := chr(13) + chr(10)
    nErro           := 0
    caddressSMTP    := alltrim(oJsonRequest:addressSMTP)
    nportSMTP       := oJsonRequest:portSMTP
    caddressPopImp  := alltrim(oJsonRequest:addressPopImap)
    nportPopImap    := oJsonRequest:portPopImap
    cTypeAuthent    := alltrim(oJsonRequest:protocolType)

    //Atualizando os MVs
    PutMV("MV_WFDIR"    , cDirWf)   //Diretório de trabalho do Workflow
    PutMV("MV_WFBRWSR"  , cParamBrw) //Servidor do workflow
    PutMV("MV_WFDHTTP"  , cDirWf) //Servidor do workflow

    PutMV("MV_PGCPISV"  , caddressPopImp )//caddressSMTP) //Servidor de e-mail POP 3/ IMAP dos compradores
    PutMV("MV_PGCPIPO"  , nportPopImap )//nportSMTP) //Porta de e-mail POP 3/ IMAP dos compradores

    PutMV("MV_PGCSMSR"  , caddressSMTP) //Servidor de e-mail SMTP dos compradores
    PutMV("MV_PGCSMPO"  , nportSMTP) //Porta de e-mail SMTP dos compradores
    PutMV("MV_PGCTAUT"  , cTypeAuthent) //Tipo de autenticação do servidor de e-mail: TLS, SSL ou nenhum

    //Verifico se a pasta informada no MV_WFDIR existe. Se não, crio a pasta
    if !( ExistDir(cDirWf) )
        if ( MakeDir(cDirWf) != 0 )
            nErro := ferror()
        endif
    endif

    //Modelo do AppServer
    cAppSrvM += " EXEMPLO WEBSERVICE PGC " + cCRLF

    cAppSrvM += " ;-------------------------------- WEBSERVICE - PGC -------------------------------------------- " + cCRLF

    cAppSrvM += "[" + cEnvirSrv + "]" + cCRLF
    cAppSrvM += "SOURCEPATH=" + cSrcPath  + cCRLF
    cAppSrvM += "ROOTHPATH="  + cRootPath + cCRLF
    cAppSrvM += "STARTPATH="  + cSrtPath  + cCRLF + cCRLF

    cAppSrvM += "[DRIVERS]" + cCRLF
    cAppSrvM += "MULTIPROTOCOLPORT=1" + cCRLF + cCRLF

    cAppSrvM += "[HTTP] " + cCRLF
    cAppSrvM += "ENABLE=1 " + cCRLF
    cAppSrvM += "PATH=" + cRootPath + "\http-root " + cCRLF
    cAppSrvM += "PORT=" + cPortIP + cCRLF
    cAppSrvM += "ENVIRONMENT=" + cEnvirSrv + cCRLF
    cAppSrvM += "INSTANCES=1,10 " + cCRLF
    cAppSrvM += "XFRAMEOPTIONS=ALLOW-FROM * " + cCRLF
    cAppSrvM += "UPLOADPATH=\web " + cCRLF + cCRLF

    cAppSrvM += "[" + cPublicIP + ":" + cPortIP + "/ws] " + cCRLF
    cAppSrvM += "ENABLE=1 " + cCRLF
    cAppSrvM += "PATH=" + cRootPath + "\web\ws " + cCRLF
    cAppSrvM += "ENVIRONMENT= " + cEnvirSrv + cCRLF
    cAppSrvM += "INSTANCENAME=WS " + cCRLF
    cAppSrvM += "RESPONSEJOB=JOB_WS_T1 " + cCRLF
    cAppSrvM += "DEFAULTPAGE=wsindex.apw " + cCRLF + cCRLF

    cAppSrvM += "[JOB_WS_T1] " + cCRLF
    cAppSrvM += "TYPE=WEBEX " + cCRLF
    cAppSrvM += "ENVIRONMENT=" + cEnvirSrv + cCRLF
    cAppSrvM += "INSTANCES=1,10 " + cCRLF
    cAppSrvM += "SIGAWEB=WS " + cCRLF
    cAppSrvM += "INSTANCENAME=WS " + cCRLF
    cAppSrvM += "ONSTART=__WSSTART " + cCRLF
    cAppSrvM += "ONCONNECT=__WSCONNECT " + cCRLF
    cAppSrvM += "PREPAREIN=ALL " + cCRLF + cCRLF

    cAppSrvM += "[" + cParamBrw + "] " + cCRLF
    cAppSrvM += "enable=1 " + cCRLF
    cAppSrvM += "port=" + cPortIP + cCRLF
    cAppSrvM += "PATH=" + cRootPath + cDirWf + cCRLF
    cAppSrvM += "environment=" + cEnvirSrv + cCRLF
    cAppSrvM += "responsejob=WF_INDEX " + cCRLF
    cAppSrvM += "DEFAULTPAGE=wsindex.apw " + cCRLF + cCRLF

    cAppSrvM += "[WF_INDEX]  " + cCRLF
    cAppSrvM += "TYPE=WEB " + cCRLF
    cAppSrvM += "environment= " + cEnvirSrv + cCRLF
    cAppSrvM += "INSTANCES=1,10 " + cCRLF
    cAppSrvM += "SIGAWEB=WF " + cCRLF
    cAppSrvM += "INSTANCENAME=WF " + cCRLF
    cAppSrvM += "ONSTART=STARTWEBEX " + cCRLF
    cAppSrvM += "ONCONNECT=CONNECTWEBEX " + cCRLF
    cAppSrvM += "ONEXIT=FINISHWEBEX " + cCRLF + cCRLF

    cAppSrvM += "[MAIL] " + cCRLF
    cAppSrvM += "Protocol=SMTP " + cCRLF
    cAppSrvM += "ExtendSMTP=1 " + cCRLF
    cAppSrvM += "SSLVersion=3 " + cCRLF
    cAppSrvM += "AuthLOGIN=1 " + cCRLF
    cAppSrvM += "AuthPLAIN=1 " + cCRLF
    cAppSrvM += "TLSVersion=3 " + cCRLF + cCRLF

    cAppSrvM += "[OnStart] " + cCRLF
    cAppSrvM += "jobs=JOB_WS_T1,WF_INDEX " + cCRLF
 
    oJsonResponse := JsonObject():New()
    oJsonResponse["ModelAppServer"] := cAppSrvM
    oJsonResponse["CreateFolder"]   := nErro
Return oJsonResponse


/*/{Protheus.doc} postWizardEmailSv
Realiza a gravação dos dados do Wizard de comprador.
@author Renan Martins
@since 11/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method postWizardEmailSv(oJsonRequest) Class pgcUtilsRepository
    local cProtocol         as character 
    local cErrorMsg         as character
    local cNameAjs          as Character
    local cFolder           as character
    local cNameTmp          as character
    local lSuccess          as logical
    local lNewNameBox       as logical
    local nOperation        as numeric
    local oModelWFC3        as Object
    local oObjWF	        as Object
    local oJsonResponse     As Object
    local oSearchRegx       as Object
    Default oJsonRequest    := JsonObject():New()

    lSuccess    := .t.
    cFolder     := ""
    cProtocol   := WFGetProtocol()[2]
    oSearchRegx := Regex():New("[^0-9a-zA-Z_]+")
    cNameTmp    := oJsonRequest:senderEmail
    if ( AttIsMemberOf(oJsonRequest, 'postalname') .and.  !empty(oJsonRequest:postalname) )
        cNameTmp := oJsonRequest:postalname
    endif
    cNameAjs    := alltrim( upper( cNameTmp ) )
    oSearchRegx:ReplaceAll(@cNameAjs,'')
    lNewNameBox := .f.

    //Verifico se á uma inclusão de caixa postal ou apenas alteração
    if (Self:existsWF7email(oJsonRequest:senderEmail, @cFolder, cNameAjs, "1"))
        
        /*Se inclusão, verifico se o nome informado da caixa pelo usuário existe na base, pois se sim, não pode ser usado novamente para a mesma filial.
        Essa alteração foi necessária, agora que o nome da caixa pode ser informado pelo usuário. Se existir, vai ser gravado o padrão do sistema, que é o e-mail do usuário.*/
        if !(Self:existsWF7email('', '', cNameAjs, "2"))
            cNameAjs := alltrim( upper(oJsonRequest:senderEmail) )
            oSearchRegx:ReplaceAll(@cNameAjs,'')
        endif
        nOperation  := 3
        cFolder     := substr(cNameAjs, 1, tamsx3("WF7_PASTA")[1])
    else
        if (upper(alltrim(cFolder)) != cNameAjs)
            if (Self:existsWF7email('', '', cNameAjs, "2"))
                lNewNameBox := .t.
            endif
        endif
        nOperation := 4
        WF7->(DbSetOrder(1)) //WF7_FILIAL+WF7_PASTA
        lSuccess := iif( WF7->(DbSeek(xFilial("WF7") + cFolder)), .t., .f.)
        cFolder := iif( lNewNameBox, substr(cNameAjs, 1, tamsx3("WF7_PASTA")[1]), cFolder) // Se editar o nome da caixa....
    endif

    if ( lSuccess )
        oModelWFC3 := FWLoadModel("WFC003")
        oObjWF	   := oModelWFC3:GetModel('FORMWF7')
        oModelWFC3:SetOperation(nOperation)
        oModelWFC3:Activate()

        if ( nOperation != 4 )
            oObjWF:setValue("WF7_FILIAL"	, xfilial("WF7"))
            oObjWF:setValue("WF7_PASTA"		, cFolder)
            oObjWF:setValue("WF7_ENDERE"	, oJsonRequest:senderEmail)
        elseif lNewNameBox
            /*O campo WF7_PASTA é chave. Ou seja, após inclusão, não é possível alterá-lo.
              Por isso, é necessário alterar o modelo, desativando a opção de chave do campo, alterar o valor e após, reativar o campo 
              com o valor de chave.*/
            oModelWFC3:GetModel("FORMWF7"):GetStruct():SetProperty('WF7_PASTA', MODEL_FIELD_KEY, .f.)
            oObjWF:setValue("WF7_PASTA"		, cFolder)
            oModelWFC3:GetModel("FORMWF7"):GetStruct():SetProperty('WF7_PASTA', MODEL_FIELD_KEY, .t.) 
        endif
        oObjWF:setValue("WF7_REMETE"	, substr(oJsonRequest:senderName, 1, tamsx3("WF7_REMETE")[1]))
        oObjWF:setValue("WF7_CONTA"		, oJsonRequest:authenticationEmail)
        oObjWF:setValue("WF7_SENHA"	    , oJsonRequest:authenticationPsw)
        oObjWF:setValue("WF7_SMTPSR"	, oJsonRequest:receiveServer)
        oObjWF:setValue("WF7_SMTPPR"	, oJsonRequest:receivePort)
        oObjWF:setValue("WF7_AUTUSU"	, oJsonRequest:authenticationEmail)
        oObjWF:setValue("WF7_AUTSEN"	, oJsonRequest:authenticationPsw) 
        oObjWF:setValue("WF7_ATIVO"	    , .t.) 
        oObjWF:setValue("WF7_SMTPSE"	, alltrim(WFGetMV( "MV_PGCTAUT", "" )))
        If (cProtocol == "IMAP")
            oObjWF:setValue('WF7_IMAPSR', oJsonRequest:senderServer)
            oObjWF:setValue('WF7_IMAPPR', oJsonRequest:senderPort )
        Else // Padrão - POP3		
            oObjWF:setValue('WF7_POP3SR' , oJsonRequest:senderServer) 
            oObjWF:setValue('WF7_POP3PR' , oJsonRequest:senderPort )			
        End

        if (oModelWFC3:VldData())
            oModelWFC3:commitData()
        else
            lSuccess := .f.
            cErrorMsg := oModelWFC3:GetErrorMessage()[6]
        endif
    endif
    
    if (lSuccess)
        cErrorMsg := self:TestEmailCreate( cFolder, oJsonRequest:senderEmail )
    endif

    oJsonResponse := JsonObject():New()
    oJsonResponse["sucesssave"] := lSuccess
    oJsonResponse["errorsave"]  := cErrorMsg
    oJsonResponse["createnewpostal"]  := iif(nOperation == 3, .t., .f.)
Return oJsonResponse


/*/{Protheus.doc} existsWF7email
Verifica se já existe o e-mail do comprador cadastrado no sistema
@author Renan Martins
@since 11/2023
@return lReturn, lógico, se já existe item.
/*/
Method existsWF7email(cEmail, cFileArq, cFolder, cType) Class pgcUtilsRepository
    local cAliasQry     as character
    local cRetFnc       as character
    local lReturn       as logical
    default cEmail      := ""
    default cFileArq    := ""
    default cFolder     := ""
    default cType       := "1"

    cAliasQry   := GetNextAlias()
    lReturn     := .t.
    
    if (cType == "1")

        cEmail      := upper(alltrim(cEmail))
        BeginSQL Alias cAliasQry
            SELECT
                WF7_PASTA
            FROM
                %Table:WF7% WF7
            WHERE
                WF7.WF7_FILIAL          = %xFilial:WF7% AND
                upper(WF7.WF7_ENDERE)   = %exp:cEmail% AND
                WF7.WF7_ATIVO           = %exp:'T'% AND
                WF7.%NotDel%
        EndSQL

        if !(cAliasQry)->(Eof())
            lReturn := .f.
            cFileArq := (cAliasQry)->WF7_PASTA
        endif
        
    else
        //Não pode existir duas caixas de e-mail com o mesmo nome
        cRetFnc     := substr(cFolder, 1, tamsx3("WF7_PASTA")[1])
        BeginSQL Alias cAliasQry
            SELECT
                WF7_PASTA
            FROM
                %Table:WF7% WF7
            WHERE
                WF7.WF7_FILIAL          = %xFilial:WF7% AND
                upper(WF7.WF7_PASTA)    = %exp:cRetFnc% AND
                WF7.%NotDel%
        EndSQL

        if !(cAliasQry)->(Eof())
            lReturn := .f.
        endif 
    endif
    (cAliasQry)->(DbCloseArea())
Return lReturn


/*/{Protheus.doc} TestEmailCreate
Função que realiza o envio do e-mail de teste para o e-mail recém-cadastrado.
@author Renan Martins
@since 12/2023
@return cMsg, string, mensagem de erro.
/*/
Method TestEmailCreate(cFolderEmail, cEmail)  Class pgcUtilsRepository
    local cMsg as character
    local oProcess as Object

    //Verifica pasta e cria HTMl se necessário
    cMsg := Self:createhtml()

    if empty(cMsg)
        //Seta a caixa postal criada para envio de e-mail teste
        cMsg := Self:setPostalBox(cFolderEmail)
    endif

    if empty(cMsg)
        oProcess := TWFProcess():New("000001", "Treinamento")

        //Seto a caixa recém-criada
        oProcess:oWf:cMailBox := alltrim(cFolderEmail)
        oProcess:cFrom        := alltrim(cFolderEmail)
        
        oProcess:NewTask("FORMULARIO", "\workflow\__WFORM.html")   
        oProcess:oHtml:ValByName("TEXT_TIME", Time() )

        oProcess:cTo 		:= "HTML" 
        oProcess:bTimeOut 	:= {{"__WFTimeout()", 0, 0, 5 }} //function no WFTESTE
        oProcess:bReturn 	:= "__WFRetorno()" //function no WFTESTE

        cMailID := oProcess:Start()     
        oProcess:NewTask("LINK", "\workflow\__wflink.html")

        oProcess:oHtml:ValByName("A_LINK", alltrim(WFGetMV("MV_WFBRWSR", '')) + "/messenger/emp" + cEmpAnt + "/html/" + cMailId + ".htm") 
        
        oProcess:cTo 		:= cEmail  
        oProcess:cSubject	:= "Workflow teste - cadastro/alteração de e-mail"
        oProcess:UserSiga   := "000000"          

        oProcess:Start()
    endif

return cMsg 


/*/{Protheus.doc} createhtml
Cria um arquivo HTML, para envio do e-mail
@author Renan Martins
@since 11/2023
@return cMsg, string, retorno.
/*/
method createhtml() Class pgcUtilsRepository
    local nHandle as numeric
    local cForm   as character
    local cMsg    as character  

    cMsg := ""
    cForm := ""
    nHandle := 0
    if !(ExistDir("\workflow"))
        MakeDir("\workflow")
    endif

    if !(file("\workflow\__WFORM.html"))
        nHandle := fcreate("\workflow\__WFORM.html")

        if (file("\workflow\__WFORM.html"))
            cForm += '<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN" "http://www.w3.org/TR/html4/strict.dtd">'
            cForm += '<html>'
            cForm += '    <head>'
            cForm += '      <meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1">'
            cForm += '		<title>Workflow por Link</title>'
            cForm += '    </head>'
            cForm += '<body><form action="mailto:%WFMailTo%" method="POST" name="formulario">'
            cForm += '			Processo gerado às !TEXT_TIME!'
            cForm += '			<br> Clique aqui para responder --> '
            cForm += '			<input type="submit" value="Enviar"/></form></body>'
            cForm += '</html>'                

            FWrite(nHandle, cForm)
            FClose(nHandle)
        endif
    endif

    if !(file("\workflow\__wflink.html"))
        nHandle := fcreate("\workflow\__wflink.html")

        if (file("\workflow\__wflink.html"))
            cForm := ''
            cForm += '<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN" "http://www.w3.org/TR/html4/strict.dtd">'
            cForm += '<html>'
            cForm += '    <head>'
            cForm += '      <meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1">'
            cForm += '		<title>Workflow por Link</title>'
            cForm += '    </head>'
            cForm += "<body>	<form name='form1' method='post' action=''>"
            cForm += "		<p>Clique no <a href='!A_LINK!'>link</a> para responder.</p>"
            cForm += '</form></body>'
            cForm += '</html>'                

            FWrite(nHandle, cForm)
            FClose(nHandle)
        endif
    endif

return cMsg


/*/{Protheus.doc} createhtml
Valida as configutrações de e-mail, como usuário e senha
@author Renan Martins
@since 12/2023
@return cError, string, retorno em caso de erro.
/*/
method setPostalBox(cFileEmail) Class pgcUtilsRepository
    local cError as character
    local cUser as character 
    local cPass as character 
    local oServer as Object
    local lContinue as logical
    local nRet as numeric

    cError := ""
    cUser := ""
    cPass := "" 
    lContinue := .t.
    WF7->(DbSetOrder(1))
    if ( WF7->(DbSeek(xFilial('WF7') + cFileEmail )) )    

        oServer := TMailManager():New()
        if ( alltrim(WF7->WF7_SMTPSE) == "SSL" )
            oServer:SetUseSSL( .T. )
        elseif ( alltrim(WF7->WF7_SMTPSE) == "TLS")
            oServer:SetUseTLS( .T. )
        endif

        cUser := alltrim(WF7->WF7_AUTUSU)
        cPass := alltrim(WF7->WF7_AUTSEN)
        
        nTimeout := WF7->WF7_TEMPO
        
        nRet := oServer:Init( "", alltrim(WF7->WF7_SMTPSR), cUser, cPass, ,WF7->WF7_SMTPPR )
        
        // Estabelece comunicação com o servidor SMTP
        if ( lContinue )
            nRet := oServer:SMTPConnect()
            if nRet <> 0
                cError := "Não foi possivel conectar ao servidor SMTP - Validar as configurações de SMTP, como porta e servidor."
                lContinue := .f.
            endif
        endif
        
        // Autentica no servidor SMTP
        if ( lContinue )
            nRet := oServer:SmtpAuth( cUser, cPass )
            if nRet <> 0
                cError := "Não foi possivel autenticar no servidor SMTP - Valide as configurações de usuário e senha."
                oServer:SMTPDisconnect()
            endif
        endif

    endif

return cError



/*/{Protheus.doc} getPurchaserList
Retorna lista de compradores, baseado na tabela SY1
oJsonRequest = Body da requisição com os dados
@author Renan Martins
@since 12/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method getPurchaserList(oJsonRequest) Class pgcUtilsRepository
    Local aItems        As Array
    Local oItem         As Object
    Local oJsonResponse As Object
    local cQuery        as character
    local oQuery        as object
    local cAliasTmp     as character
    local lExsCmp       as logical
    local aOpenArea     as array
    
    oJsonResponse := JsonObject():New()
    aItems := {}
    lExsCmp := .f.

    //Verifico se o campo Y1_MSBLQL existe na base
    aOpenArea := FwGetArea()
    DbSelectArea("SY1")
    if ( SY1->(FieldPos("Y1_MSBLQL")) > 0 )
        lExsCmp := .t.
    endif
    SY1->(DbCloseArea())
    FwRestArea(aOpenArea)
    
    cQuery := " SELECT SY1.Y1_USER USERCOD, SY1.Y1_NOME NOME FROM " + RetSQLName("SY1") + " SY1"
    cQuery += "   WHERE "
    cQuery += "     SY1.Y1_FILIAL = ? "
    if (lExsCmp)
        cQuery += "     AND SY1.Y1_MSBLQL <> ?"
    endif
    cQuery += "     AND SY1.D_E_L_E_T_ = ' ' "
    cQuery += "   ORDER BY UPPER(SY1.Y1_NOME) "

    oQuery := FWPreparedStatement():New(cQuery)
    oQuery:SetString(1, FWxFilial('SY1'))
    if (lExsCmp)
        oQuery:SetString(2, '1')
    endif
    
    cAliasTmp := GetNextAlias()
    cAliasTmp := MpSysOpenQuery(oQuery:getFixQuery())

    While !(cAliasTmp)->(eof())
        oItem := JsonObject():New()
        oItem["purchaserCode"]  := (cAliasTmp)->USERCOD
        oItem["purchasername"]  := upper(alltrim((cAliasTmp)->NOME))
        aAdd(aItems, oItem)
        (cAliasTmp)->(dbSkip())
    enddo

    (cAliasTmp)->(DbCloseArea())
    oJsonResponse["purchasers"] := aItems 
Return oJsonResponse

/*/{Protheus.doc} postMigrateLegacyQuote
Realiza a migração da cotação legada para estrutura do NFC

Etapas:
1 - Executa query na SC8 para verificar cotações a serem migradas.
2 - Compatibilizar e gravar tabela DHU
3 - Compatibilizar e gravar tabela DHV
4 - Executa query na SC7/CN9 para verificar pedidos vinculados a cotações e preencher CE_NUMPED/CE_NUMCTR

@author Leandro Fini
@since 02/2024
@return oJsonResponse, object, resposta no formato json.
/*/
Method postMigrateLegacyQuote(oJsonRequest) Class pgcUtilsRepository

    local oJsonResponse as Object
    local cInitialDate  as character
    local cEndDate      as character

    cInitialDate := oJsonRequest:initialdate 
    cEndDate     := oJsonRequest:enddate
    cEmail       := if(AttIsMemberOf(oJsonRequest,"email"),oJsonRequest:email,"")

    StartJob("migrateLegacyQuote",GetEnvServer(),.F.,cEmpAnt,cFilAnt,cInitialDate,cEndDate,cEmail)
    
    oJsonResponse := JsonObject():New()
    //"Processo de migração iniciado ..." # " ao término do processamento uma notificação será enviada para o e-mail informado."
    oJsonResponse["message"] := STR0001 + if(!empty(cEmail),STR0002,"")
    
Return oJsonResponse
