#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'BACKOFFICE.PGC.UTILS.CONTROLLER.CH'

namespace pgc.utilsController
using namespace pgc.utilsService
using namespace pgc.utils

/*/{Protheus.doc} pgcUtilsController
	API de serviços utilitários
@author juan.felipe
@since 18/11/2021
/*/
Class pgcUtilsController
    public Data cName As Character
    public Data cDescription As Character

    public Method New()

    @Post("/api/com/v1/pgcUtils/fieldsProperties")
    public Method postFieldsProperties()
    
    @Get("/api/com/v1/pgcUtils/userInfo/:login")
    public Method getUserInfo()
    
    @Post("/api/com/v1/pgcUtils/sendMetric")
    public Method postSendMetric()

	@Get("/api/com/v1/pgcUtils/companyInfo")
    public Method companyInfo()

    @Post("/api/com/v1/pgcUtils/calcDiscount")
    public Method postCalcDiscount()

    @Post("/api/com/v1/pgcUtils/verifyMultiProt")
    public Method postVerifyMultiProt()

    @Post("/api/com/v1/pgcUtils/postWizardParam")
    public Method postWizardParam()

    @Post("/api/com/v1/pgcUtils/postWizardEmail")
    public Method postWizEmail()

    @Get("/api/com/v1/pgcUtils/purchaserList")
    public Method getpurchaserList()

	@Post("/api/com/v1/pgcUtils/migrateLegacyQuote")
    public Method postMigrateLegacyQuote()

    @Get("/api/com/v1/pgcUtils/validateRoutine/:routine")
    public Method getValidateRoutine()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 18/11/2021
/*/
Method New() Class pgcUtilsController
Return Self

/*/{Protheus.doc} postFieldsProperties
	Consulta propriedades dos campos via SX3.
@author juan.felipe
@since 29/11/2021
@return .T., logical, consulta realizada corretamente.
/*/
Method postFieldsProperties() Class pgcUtilsController
    Local lOk   As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oUtilsService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonResponse := JsonObject():New()
    oJsonRequest := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oUtilsService := pgcUtilsService():New()
        oJsonResponse := oUtilsService:fieldsProperties(oJsonRequest:alias, oJsonRequest:fields)
    EndIf
    
    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getUserInfo
	Consulta ID e nome do usuário.
@author juan.felipe
@since 13/01/2022
@return .T., logical, consulta realizada corretamente.
/*/
Method getUserInfo() Class pgcUtilsController
    Local cLogin As Character
    Local lOk As Logical
    Local oJsonResponse As Object
    Local oJsonPath As Object
    Local oUtilsService As Object
    Local oUtils As Object

    oJsonPath := oRest:getPathParamsRequest()
    oUtils := pgcUtils():New()

    cLogin := oJsonPath["login"]
    oUtilsService := pgcUtilsService():New()
    oJsonResponse := oUtilsService:getUserInfo(cLogin)
    lOk := oUtilsService:lOk

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} postSendMetric
	Envia métrica.
@author juan.felipe
@since 22/11/2022
@return .T., logical, requisição realizada corretamente.
/*/
Method postSendMetric() Class pgcUtilsController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oUtilsService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oUtilsService := pgcUtilsService():New()
        oJsonResponse := oUtilsService:postSendMetric(oJsonRequest)
    EndIf
    
    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtilsService)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} companyInfo
	Consulta dados da empresa logada, tais como endereço de entrega e CNPJ
@author Leandro Fini
@since 13/12/2022
@return .T., logical, consulta realizada corretamente.
/*/
Method companyInfo() Class pgcUtilsController
    Local cCompany As Character
    Local cBranch As Character
    Local cJsonResponse As Character
    Local oJsonResponse As Object
    Local oUtilsService As Object

    cCompany := cEmpAnt
    cBranch  := cFilAnt
    
    oUtilsService := pgcUtilsService():New()
    oJsonResponse := oUtilsService:companyInfo(cCompany,cBranch) 

    cJsonResponse := FwJsonSerialize(oJsonResponse)
    oRest:SetResponse(cJsonResponse)
Return .T.

/*/{Protheus.doc} postCalcDiscount
	Realiza o cálculo de desconto de acordo com parâmetors enviados
    nPerc = Percentual de desconto
    nTotDiscount = Total do desconto em valor
    nTotValue = Total do documento
    aItems = Items que serão aplicados o desconto
@author Leandro Fini
@since 22/12/2022
@return oJsonResponse, object, resposta no formato json.
/*/
Method postCalcDiscount() Class pgcUtilsController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oUtilsService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonResponse := JsonObject():New()
    oJsonRequest := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oUtilsService := pgcUtilsService():New()
        oJsonResponse := oUtilsService:calcDiscount(oJsonRequest:perc, oJsonRequest:discount, oJsonRequest:totalvalue, oJsonRequest:items)
    EndIf
    
    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.


/*/{Protheus.doc} verifyMultiProt
Verifica se a porta multiprotocolo existe, bem como realzia verificação de parâmetros
@author Renan Martins 
@since 11/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method postVerifyMultiProt() Class pgcUtilsController
    Local lOk   As Logical
    Local cBody As Character
    Local oJsonResponse As Object
    Local oUtilsService As Object
    Local oUtils As Object

    lOk := .t.
    cBody := oRest:getBodyRequest()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    oUtilsService := pgcUtilsService():New()
    oJsonResponse := oUtilsService:verifyMultiProt()

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.


/*/{Protheus.doc} postWizardParam
Realiza a gravação dos dados do Wizard de administrador.
@author Renan Martins 
@since 11/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method postWizardParam() Class pgcUtilsController
    Local lOk   As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oUtilsService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonResponse := JsonObject():New()
    oJsonRequest := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oUtilsService := pgcUtilsService():New()
        oJsonResponse := oUtilsService:postWizardParam(oJsonRequest)
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.


/*/{Protheus.doc} postWizEmail
Realiza a gravação dos dados do Wizard de comprador.
@author Renan Martins
@since 11/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method postWizEmail() Class pgcUtilsController
    Local lOk   As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oUtilsService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonResponse := JsonObject():New()
    oJsonRequest := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oUtilsService := pgcUtilsService():New()
        oJsonResponse := oUtilsService:postWizardEmail(oJsonRequest)
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.


/*/{Protheus.doc} getPurchaserList
Retorna lista com os compradores cadastrados no sistema - tabela SY1
@author Renan Martins
@since 12/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method getPurchaserList() Class pgcUtilsController
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oUtilsService As Object

    oUtilsService := pgcUtilsService():New()
    oJsonResponse := oUtilsService:getpurchaserList(oJsonRequest)

    //oUtils:setAPIResponse(oRest, oJsonResponse, lOk)
    cJsonResponse := FwJsonSerialize(oJsonResponse)
    oRest:SetResponse(cJsonResponse)

    FreeObj(oUtilsService)
    FreeObj(oJsonResponse)

Return .T.


/*/{Protheus.doc} postMigrateLegacyQuote
Realiza a migração da cotação legada para estrutura do NFC
@author Leandro Fini
@since 02/2024
@return oJsonResponse, object, resposta no formato json.
/*/
Method postMigrateLegacyQuote() Class pgcUtilsController
    Local lOk           As Logical
    Local cBody         As Character
    Local cMessage      As Character
    Local cAliasTmp     as Character
    Local oJsonResponse As Object
    Local oJsonRequest  As Object
    Local oUtilsService As Object
    Local oUtils        As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonResponse := JsonObject():New()
    oJsonRequest := JsonObject():New()
    oUtils := pgcUtils():New()

    DbSelectArea("DHU")
    
    if DHU->(FieldPos("DHU_LEGACY")) > 0
        if lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
            if ( AttIsMemberOf(oJsonRequest,"initialdate") .and. AttIsMemberOf(oJsonRequest,"enddate") )
                cAliasTmp := getLegacyQuot(oJsonRequest:initialdate,oJsonRequest:enddate)
                
                //Update para trocar o 'S' para '2', do campo DHU_LEGACY
                NFCAdjDHUBase()

                if (cAliasTmp)->(eof())
                    //"|| NFC || -- Não há cotações a serem migradas com as datas informadas: "
                    FwLogMsg("INFO",,"NFC","BACKOFFICE.PGC.UTILS.CONTROLLER",,, STR0004 + DtoC(StoD(oJsonRequest:initialdate)) + " - " + DtoC(StoD(oJsonRequest:enddate)))
                    lOk := .F.
                    cMessage := STR0005 + DtoC(StoD(oJsonRequest:initialdate)) + " - " + DtoC(StoD(oJsonRequest:enddate))//"Não há cotações a serem migradas com as datas informadas: "
                    oUtils:setAPIResponse(oRest, oJsonResponse, lOk, 400, cMessage)
                else
                    oUtilsService := pgcUtilsService():New()
                    oJsonResponse := oUtilsService:postMigrateLegacyQuote(oJsonRequest)
                    oUtils:setAPIResponse(oRest, oJsonResponse, lOk, 200)
                endif
                (cAliasTmp)->(dbCloseArea())
            endif
        endif
    elseif DHU->(FieldPos("DHU_LEGACY")) == 0
        lOk := .F.
        cMessage := STR0006//"Necessário possuir o campo 'DHU_LEGACY' para o processo de migração de cotações. Atualize o dicionário de dados com a última expedição contínua do Compras."
        oUtils:setAPIResponse(oRest, oJsonResponse, lOk, 400, cMessage)
    endif

    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getValidateRoutine
    Valida rotina através da chamada da função PGCVldTables.
@author juan.felipe
@since 25/09/2025
@return .T., logical, consulta realizada corretamente.
/*/
Method getValidateRoutine() Class pgcUtilsController
    Local oJsonResponse As Object
    Local oJsonPath     As Object
    Local oUtilsService As Object
    Local oUtils        As Object

    oUtilsService := pgcUtilsService():New()
    oJsonPath := oRest:getPathParamsRequest()
    oJsonResponse := oUtilsService:getValidateRoutine(oJsonPath)
    oUtils := pgcUtils():New()

    oUtils:setAPIResponse(oRest, oJsonResponse, oUtilsService:lOk)

    FreeObj(oUtilsService)
    FreeObj(oJsonResponse)
Return .T.
