#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'BACKOFFICE.PGC.SUPPLIER.CONTROLLER.CH'

namespace pgc.supplierController
using namespace pgc.supplierService
using namespace pgc.utils

/*/{Protheus.doc} pgcSupplierController
	API para consultar fornecedores
@author juan.felipe
@since 23/02/2022
/*/
Class pgcSupplierController
    public Data nPage     As Integer
    public Data nPageSize As Integer

    public Method New()
    private Method setAnswer()

    @Get("/api/com/v1/pgcSupplier")
    public Method getSupplier()
       
    @Post("/api/com/v1/pgcSupplier")
    public Method postSupplier()

    @Get("/api/com/v1/pgcSupplier/:code/:store")
    public Method getSupplierCode()

    @Put("/api/com/v1/pgcSupplier/updateContacts")
    public Method putUpdateContacts()

    @Post("/api/com/v1/pgcSupplier/lastSuppliers")
    public Method postLastSuppliers()

    @Post("/api/com/v1/pgcSupplier/mySuppliers")
    public Method postMySuppliers()

    @Get("/api/com/v1/pgcSupplier/supplierInfo/:documentNumber")
    public Method supplierInfo()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 23/02/2022
/*/
Method New() Class pgcSupplierController
Return Self

/*/{Protheus.doc} getSupplier
	Consulta fornecedores
@author juan.felipe
@since 23/02/2022
@return .T., logical, consulta realizada corretamente.
/*/
Method getSupplier() Class pgcSupplierController
    Self:setAnswer() 
Return .T.

/*/{Protheus.doc} postSupplier
	Cadastra fornecedor
@author juan.felipe
@since 12/07/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method postSupplier() Class pgcSupplierController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oSupplierService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .F.)
        oSupplierService := pgcSupplierService():New()
        oJsonResponse := oSupplierService:postSupplier(oJsonRequest)
        lOk := oSupplierService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oSupplierService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getSupplierCode
	Consulta fornecedor de acordo com o código e loja.
@author juan.felipe
@since 23/02/2022
@return .T., logical, consulta realizada corretamente.
/*/
Method getSupplierCode() Class pgcSupplierController
    Local cCode As Character
    Local cStore As Character
    Local oJsonPath As Object

    cCode := ""
    cStore := ""
    oJsonPath := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("code") .And. oJsonPath:hasProperty("store")
        cCode := oJsonPath["code"]
        cStore := oJsonPath["store"]
    EndIf

    Self:setAnswer(cCode, cStore)
    
    FreeObj(oJsonPath)
Return .T.


/*/{Protheus.doc} setAnswer
	Monta resposta JSON para o retorno dos fornecedores
@author juan.felipe
@since 23/02/2022
@param cCode, character, código do fornecedor.
@param cStore, character, loja do fornecedor.
@param nOption, numeric, 1- Todos fornecedores, 2 - Últimos Fornecedores.
@param aProducts, array, Produtos para retorno de Últimos Fornecedores
@param nQuantitySuppliers, numeric, Quantidade de Últimos Fornecedores
@return Nil, nulo.
/*/
Method setAnswer(cCode, cStore, nOption, aProducts, nQuantitySuppliers) Class pgcSupplierController
    Local oSupplierService As Object
    Local oUtils As Object
    Default cCode   := ""
    Default cStore  := ""
    Default nOption := 0
    Default aProducts := {}
    Default nQuantitySuppliers := 0

    oSupplierService := pgcSupplierService():New()
    oUtils := pgcUtils():New()

    oUtils:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oSupplierService:getQueryResponse(Self:nPage, Self:nPageSize, cCode, cStore, nOption, aProducts, nQuantitySuppliers)

    oUtils:setAPIResponse(oRest, oSupplierService:oJsonResponse, oSupplierService:lOk, oSupplierService:nCode, oSupplierService:cMessage)

    FreeObj(oSupplierService)
    FreeObj(oUtils)
Return Nil

/*/{Protheus.doc} putUpdateContacts
	Atualiza contatos do fornecedor.
@author juan.felipe
@since 08/03/2022
@return .T., logical
/*/
Method putUpdateContacts() Class pgcSupplierController
    Local lOk As Logical
    Local cBody As Character
    Local cSupplierCode As Character
    Local cStore As Character
    Local cAreaCode As Character
    Local cTelephone As Character
    Local cEmail As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oSupplierService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oSupplierService := pgcSupplierService():New()
            
        cSupplierCode := oJsonRequest:supplierCode
        cStore := oJsonRequest:store
        cAreaCode := oJsonRequest:areaCode
        cTelephone := oJsonRequest:telephone
        cEmail := oJsonRequest:email

        oJsonResponse := oSupplierService:putUpdateContacts(cSupplierCode, cStore, cAreaCode, cTelephone, cEmail)
        lOk := oSupplierService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oSupplierService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} postLastSuppliers
	Consulta últimos fornecedores
@author rd.santos
@since 16/08/2022
@return .T., logical, consulta realizada corretamente.
/*/
Method postLastSuppliers() Class pgcSupplierController
    Local aProducts As Character
    Local nQuantitySuppliers As Integer
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        aProducts := oJsonRequest:products
        nQuantitySuppliers := oJsonRequest:quantity
                    
        Self:setAnswer(,,2,aProducts,nQuantitySuppliers) //-- Seta resposta Ok
    Else
        oUtils:setAPIResponse(oRest, oJsonResponse, lOk) //-- Seta resposta de erro
    EndIf

    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} postLastSuppliers
	Consulta últimos fornecedores
@author rd.santos
@since 16/08/2022
@return .T., logical, consulta realizada corretamente.
/*/
Method postMySuppliers() Class pgcSupplierController
    Local aProducts As Character
    Local nQuantitySuppliers As Integer
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        aProducts := oJsonRequest:products
        nQuantitySuppliers := oJsonRequest:quantity
                    
        Self:setAnswer(,,3,aProducts,nQuantitySuppliers) //-- Seta resposta Ok
    Else
        oUtils:setAPIResponse(oRest, oJsonResponse, lOk) //-- Seta resposta de erro
    EndIf

    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} supplierInfo
	Consulta dados do fornecedor através do TOTVS API Carol.
@author juan.felipe
@since 01/08/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method supplierInfo() Class pgcSupplierController
    Local oUtils As Object
    Local oSupplierService As Object
    Local oJsonPath As Object
    Local oJsonResponse As Object
    Local cSupplierAPI As Character

	cSupplierAPI := ''

	If oRest:getHeaderRequest():HasProperty('supplierAPI')
		cSupplierAPI := oRest:getHeaderRequest()['supplierAPI']
	EndIf

    oUtils        := pgcUtils():New()
    oSupplierService := pgcSupplierService():New()
    oJsonPath     := oRest:getPathParamsRequest()
    oJsonResponse := oSupplierService:supplierInfo(oJsonPath['documentNumber'], cSupplierAPI)

    oUtils:setAPIResponse(oRest, oJsonResponse, oSupplierService:lOk)

    FreeObj(oUtils)
    FreeObj(oSupplierService)
    FreeObj(oJsonPath)
    FreeObj(oJsonResponse)
Return .T.
