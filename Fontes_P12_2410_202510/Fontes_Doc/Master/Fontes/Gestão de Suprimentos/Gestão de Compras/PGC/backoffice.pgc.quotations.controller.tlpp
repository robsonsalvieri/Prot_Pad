#include 'tlpp-core.th'
#include 'tlpp-rest.th'

namespace pgc.quotationsController
using namespace pgc.quotationsService
using namespace pgc.utils

/*/{Protheus.doc} pgcQuotationsController
	API relacionadas a cotações
@author Leandro Fini
@since 09/01/2022
/*/
Class pgcQuotationsController
    public Data nPage     As Integer
    public Data nPageSize As Integer

    public Method New()
    private Method setAnswer()

    @Get("/api/com/v1/pgcQuotations/quotationList")
    public Method getQuotationList()

    @Delete("/api/com/v1/pgcQuotations/deleteQuotation/:code")
    public Method deleteQuotation()

    @Get("/api/com/v1/pgcPurchaseOrders/purchaseList")
    public Method getPurchaseList()

    @Get("/api/com/v1/pgcPurchaseOrders/contractsList")
    public Method getContractsList()

    @Put("/api/com/v1/pgcQuotations/quotationHeader")
    public Method putQuotationHeader()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 27/10/2022
/*/
Method New() Class pgcQuotationsController
Return Self

/*/{Protheus.doc} getQuotationList
	Consulta lista de cotações
@author Leandro Fini
@since 06/01/2022
@return .T., logical, consulta realizada corretamente.
/*/
Method getQuotationList() Class pgcQuotationsController
    Self:setAnswer(1) 
Return .T.

/*/{Protheus.doc} setAnswer
	Monta resposta JSON para o retorno de dados relacionados a cotações
@author juan.felipe
@since 27/10/2022
@param nQuery, numeric, 1- Listagem de cotações
@return .T., logical, consulta realizada corretamente.
/*/
Method setAnswer(nQuery) Class pgcQuotationsController
    Local lOk As Logical
    Local cMessage As Character
    Local nCode As Numeric
    Local oQuotationsService As Object
    Local oJsonResponse As Object
    Local oUtils As Object

    Default nQuery  := 1

    oQuotationsService := pgcQuotationsService():New()
    oUtils := pgcUtils():New()

    oUtils:getPageAndPageSize(@Self:nPage, @Self:nPageSize)

    oQuotationsService:getQueryResponse(Self:nPage, Self:nPageSize, nQuery)
    
    oJsonResponse := oQuotationsService:oJsonResponse
    lOk := oQuotationsService:lOk
    nCode := oQuotationsService:nCode
    cMessage := oQuotationsService:cMessage

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk, nCode, cMessage)

    FreeObj(oQuotationsService)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} deleteQuotation
	Deleta cotação.
@author juan.felipe
@since 16/01/2023
@return .T., logical, exclusão realizada corretamente.
/*/
Method deleteQuotation() Class pgcQuotationsController
    Local lOk As Logical
    Local cCode As Character
    Local oJsonResponse As Object
    Local oJsonPath As Object
    Local oQuotationsService As Object
    Local oUtils As Object

    lOk := .T.
    cCode := ''
    oJsonResponse := JsonObject():New()
    oJsonPath := oRest:getPathParamsRequest()
    oUtils := pgcUtils():New()

    If ValType(oJsonPath) == 'J' .And. oJsonPath:hasProperty('code')
        cCode := oJsonPath['code']
    EndIf

    oQuotationsService := pgcQuotationsService():New()
    oJsonResponse := oQuotationsService:deleteQuotation(cCode)
    lOk := oQuotationsService:lOk

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oQuotationsService)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.


/*/{Protheus.doc} getPurchaseList
Consulta lista de Pedidos de Compras
@author Renan Martins
@since 11/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method getPurchaseList() Class pgcQuotationsController
    Self:setAnswer(2)
Return .T.


/*/{Protheus.doc} getContractsList
Consulta lista de Contratos
@author Renan Martins
@since 11/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method getContractsList() Class pgcQuotationsController
    Self:setAnswer(3)
Return .T.


/*/{Protheus.doc} putQuotationHeader
	Realiza atualização do cabeçalho da cotação.
@author juan.felipe
@since 15/07/2025
@return .T., logical, alteração realizada com sucesso.
/*/
Method putQuotationHeader() Class pgcQuotationsController
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oQuotationsService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .F.)
        oQuotationsService := pgcQuotationsService():New()
        oJsonResponse := oQuotationsService:putQuotationHeader(oJsonRequest)
        lOk := oQuotationsService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oQuotationsService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.
