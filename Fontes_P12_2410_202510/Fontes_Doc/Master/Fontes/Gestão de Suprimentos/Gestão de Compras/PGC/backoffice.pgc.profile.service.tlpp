#include 'tlpp-core.th'
#include 'tlpp-rest.th'

namespace pgc.profileService
using namespace pgc.profileRepository

/*/{Protheus.doc} pgcProfileService
	Classe de serviço de perfil de usuário
@author juan.felipe
@since 29/12/2021
/*/
Class pgcProfileService
    public Method New()

    public Method getProfile()
    public Method getProfileRestore()
    public Method postProfile()
    public Method putProfile()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 29/12/2021
/*/
Method New() Class pgcProfileService
Return Self

/*/{Protheus.doc} getProfile
	Retorna profile do usuário.
@author juan.felipe
@since 29/12/2021
@param cUserId, character, código do usuário.
@param cBody, character, conteúdo a ser gravado no profile.
@param cCompany, character, empresa.
@param cBranch, character, filial.
@return oJsonProfile, object, profile no formato json.
/*/
Method getProfile(cUserId, cCompany, cBranch) Class pgcProfileService
    local oJsonProfile As Object
    Local oProfileRepository As Object
    Local cProfile As Character

    oJsonProfile := JsonObject():new()
    oProfileRepository := pgcProfileRepository():new()
    cProfile := oProfileRepository:getHaveProfile(cUserId, cCompany,cBranch) 
    
    If Empty(cProfile)
        oJsonProfile['profile'] := JsonObject():New()
        oJsonProfile['profile']['period'] := JsonObject():New()
        oJsonProfile['profile']['period']['startDate'] :=  ''
        oJsonProfile['profile']['period']['endDate'] :=  ''
    Else
        oJsonProfile:fromJson(AllTrim(cProfile))
    EndIf

Return oJsonProfile

/*/{Protheus.doc} getProfileRestore
	Retorna profile restaurado.
@author juan.felipe
@since 05/01/2021
@return oJsonProfile, object, profile no formato json.
/*/
Method getProfileRestore() Class pgcProfileService
    local oJsonProfile As Object

    oJsonProfile := JsonObject():new()
    oJsonProfile['profile'] := JsonObject():New()
    oJsonProfile['profile']['period'] := JsonObject():New()
    oJsonProfile['profile']['period']['startDate'] :=  ''
    oJsonProfile['profile']['period']['endDate'] :=  ''

Return oJsonProfile

/*/{Protheus.doc} postProfile
	Grava ou altera profile do usuário
@author juan.felipe
@since 30/12/2021
@param cUserId, character, código do usuário.
@param cBody, character, conteúdo a ser gravado no profile.
@param cCompany, character, empresa.
@param cBranch, character, filial.
@return lWrite, logical, profile gravado com sucesso.
/*/
Method postProfile(cUserId, cBody, cCompany, cBranch) Class pgcProfileService
    Local lWrite As Logical
    local cJsonProfile As Object 
    Local oProfileRepository As Object

    lWrite := .T.
    oProfileRepository := pgcProfileRepository():New()
    cJsonProfile := oProfileRepository:getHaveProfile(cUserId, cCompany, cBranch) 
        
    If Empty(cJsonProfile)
        lWrite := oProfileRepository:postProfile(cUserId, cBody,cCompany, cBranch)
    Else
        lWrite := oProfileRepository:putProfile(cUserId, cBody, cCompany, cBranch)
    EndIf
Return lWrite

/*/{Protheus.doc} postProfile
	Altera profile do usuário
@author juan.felipe
@since 30/12/2021
@param cUserId, character, código do usuário.
@param cBody, character, conteúdo a ser gravado no profile.
@param cCompany, character, empresa.
@param cBranch, character, filial.
@return lWrite, logical, profile gravado com sucesso.
/*/
Method putProfile(cUserId, cBody, cCompany, cBranch) Class pgcProfileService
    Local lWrite As Logical
    local cJsonProfile As Object 
    Local oProfileRepository As Object

    lWrite := .T.
    oProfileRepository := pgcProfileRepository():New()
    cJsonProfile := oProfileRepository:getHaveProfile(cUserId, cCompany, cBranch) 
        
    If !Empty(cJsonProfile)
        lWrite := oProfileRepository:putProfile(cUserId, cBody, cCompany, cBranch)
    EndIf
Return lWrite
