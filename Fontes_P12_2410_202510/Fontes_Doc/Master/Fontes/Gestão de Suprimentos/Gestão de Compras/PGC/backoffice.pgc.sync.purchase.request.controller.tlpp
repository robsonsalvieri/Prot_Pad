#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'BACKOFFICE.PGC.SYNC.PURCHASE.REQUEST.CONTROLLER.CH'

namespace pgc.syncPurchaseRequestController
using namespace pgc.syncPurchaseRequestService
using namespace pgc.documentsService
using namespace pgc.utils

/*/{Protheus.doc} pgcSyncPurchaseRequest
	API para consultar solicitações de compra.
@author juan.felipe
@since 02/09/2021
/*/
Class pgcSyncPurchaseRequest
    public Data nPage     As Integer
    public Data nPageSize As Integer

    public Method New()
    private Method setAnswer()

    @Get("/api/com/v1/pgcSyncPurchaseRequest")
    public Method getPurchaseRequest()
       
    @Get("/api/com/v1/pgcSyncPurchaseRequest/:code")
    public Method getPurchaseRequestCode()
    
    @Get("/api/com/v1/pgcSyncPurchaseRequest/open")
    public Method getPurchaseRequestOpen()
    
    @Get("/api/com/v1/pgcSyncPurchaseRequest/partial")
    public Method getPurchaseRequestPartial()

    @Get("/api/com/v1/pgcSyncPurchaseRequest/attended")
    public Method getPurchaseRequestAttended()
    
    @Post("/api/com/v1/pgcSyncPurchaseRequest/attachDocument")
    public Method postAttachDocument()
    
    @Get("/api/com/v1/pgcSyncPurchaseRequest/documentsList/:requestCode/:item")
    public Method getDocumentsList()
    
    @Get("/api/com/v1/pgcSyncPurchaseRequest/document/:document")
    public Method getDocument()
    
    @Delete("/api/com/v1/pgcSyncPurchaseRequest/document")
    public Method deleteDocument()

    @Post("/api/com/v1/pgcSyncPurchaseRequest/checkOpen")
    public Method postCheckOpen()

    //-- Agrupamento por solicitação (retorna apenas o cabeçalho da SC)
    @Get("/api/com/v1/pgcSyncPurchaseRequest/groupByRequest/open")
    public Method getGroupByRequestOpen()

    @Get("/api/com/v1/pgcSyncPurchaseRequest/groupByRequest/partial")
    public Method getGroupByRequestPartial()

    @Get("/api/com/v1/pgcSyncPurchaseRequest/groupByRequest/attended")
    public Method getGroupByRequestAttended()

    //-- Agrupamento por produto (retorna apenas os produtos que contenham SCs)
    @Get("/api/com/v1/pgcSyncPurchaseRequest/groupByProduct/open")
    public Method getGroupByProductOpen()

    @Get("/api/com/v1/pgcSyncPurchaseRequest/groupByProduct/partial")
    public Method getGroupByProductPartial()

    @Get("/api/com/v1/pgcSyncPurchaseRequest/groupByProduct/attended")
    public Method getGroupByProductAttended()

    @Get("/api/com/v1/pgcSyncPurchaseRequest/centralized")
    public Method getPurchaseRequestCentralized()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 02/09/2021
/*/
Method New() Class pgcSyncPurchaseRequest
Return Self

/*/{Protheus.doc} getPurchaseRequest
	Consulta solicitações de compra
@author juan.felipe
@since 02/09/2021
@return .T., logical, consulta realizada corretamente.
/*/
Method getPurchaseRequest() Class pgcSyncPurchaseRequest
    Self:setAnswer() 
Return .T.

/*/{Protheus.doc} getPurchaseRequestCode
	Consulta solicitação de compra de acordo com o código
@author juan.felipe
@since 02/09/2021
@return .T., logical, consulta realizada corretamente.
/*/
Method getPurchaseRequestCode() Class pgcSyncPurchaseRequest
    Local cCode As Character
    Local oJsonPath As Object

    cCode := ""
    oJsonPath := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("code")
        cCode := oJsonPath["code"]
    EndIf

    Self:setAnswer(cCode)
    
    FreeObj(oJsonPath)
Return .T.

/*/{Protheus.doc} getPurchaseRequestOpen
	Consulta solicitações de compra em aberto
@author juan.felipe
@since 27/09/2021
@return .T., logical, consulta realizada corretamente.
/*/
Method getPurchaseRequestOpen() Class pgcSyncPurchaseRequest
    Self:setAnswer("", 1) 
Return .T.

/*/{Protheus.doc} getPurchaseRequestOpen
	Consulta solicitações de compra parcialmente atendidas
@author juan.felipe
@since 28/09/2021
@return .T., logical, consulta realizada corretamente.
/*/
Method getPurchaseRequestPartial() Class pgcSyncPurchaseRequest
    Self:setAnswer("", 2) 
Return .T.

/*/{Protheus.doc} getPurchaseRequestOpen
	Consulta solicitações de compra atendidas
@author juan.felipe
@since 28/09/2021
@return .T., logical, consulta realizada corretamente.
/*/
Method getPurchaseRequestAttended() Class pgcSyncPurchaseRequest
    Self:setAnswer("", 3) 
Return .T.

/*/{Protheus.doc} setAnswer
	Monta resposta JSON para o retorno das solicitações de compra
@author juan.felipe
@since 02/09/2021
@param cCode, character, código da solicitação de compra
@param nRequestType, numeric, 1- Em aberto; 2- Parcialmente atendidas; 3- Atendidas.
@param nGroupBy, numeric, 0- Sem agrupamento; 1- Agrupamento por SC; 2- Agrupamento por Produto.
@return .T., logical, consulta realizada corretamente.
/*/
Method setAnswer(cCode, nRequestType, nGroupBy) Class pgcSyncPurchaseRequest
    Local oRequestService As Object
    Local oUtils As Object
    Default cCode        := ""
    Default nRequestType := 0
    Default nGroupBy     := 0

    oRequestService := pgcSyncPurchaseRequestService():New()
    oUtils := pgcUtils():New()

    oUtils:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oRequestService:getQueryResponse(Self:nPage, Self:nPageSize, cCode, nRequestType, nGroupBy)
    lOk := oRequestService:lOk

    oUtils:setAPIResponse(oRest, oRequestService:oJsonResponse, lOk, oRequestService:nCode, oRequestService:cMessage)

    FreeObj(oRequestService)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} postAttachDocument
	Anexa documento ao banco de conhecimento.
@author juan.felipe
@since 28/09/2021
@return .T., logical
/*/
Method postAttachDocument() Class pgcSyncPurchaseRequest
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oDocumentsService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oDocumentsService := pgcDocumentsService():New("POST")
        oJsonResponse := oDocumentsService:attachDocument(oJsonRequest, 'SC1')
        lOk := oDocumentsService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oDocumentsService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getDocumentsList
	Consulta documentos da base de conhecimento.
@author juan.felipe
@since 22/11/2021
@return lRet, logical, consulta realizada corretamente.
/*/
Method getDocumentsList() Class pgcSyncPurchaseRequest
    Local lOk As Logical
    Local cSeachKey As Character
    Local oJsonPath As Object
    Local oJsonResponse As Object
    Local oDocumentsService As Object
    Local oUtils As Object

    cSeachKey := ""
    oJsonPath := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("requestCode") .And. oJsonPath:hasProperty("item")
        cSeachKey := xFilial('SC1') + oJsonPath["requestCode"] + oJsonPath["item"]
    EndIf

    oDocumentsService := pgcDocumentsService():New()
    oUtils := pgcUtils():New()

    oUtils:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oJsonResponse := oDocumentsService:getDocumentsList(Self:nPage, Self:nPageSize, 'SC1', cSeachKey)
    lOk := oDocumentsService:lOk

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oJsonPath)
    FreeObj(oDocumentsService)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getDocument
	Retorna documento em base64 do banco de conhecimento.
@author juan.felipe
@since 22/11/2021
@return lRet, logical, consulta realizada corretamente.
/*/
Method getDocument() Class pgcSyncPurchaseRequest
    Local lOk As Logical
    Local cDocument As Character
    Local oJsonPath As Object
    Local oDocumentsService As Object
    Local oUtils As Object

    lOk := .T.
    cDocument := ""
    oJsonPath := oRest:getPathParamsRequest()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("document")
        cDocument := oJsonPath["document"]
    EndIf

    oDocumentsService := pgcDocumentsService():New()
    oJsonResponse := oDocumentsService:getDocument(cDocument)
    lOk := oDocumentsService:lOk
    
    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oDocumentsService)
    FreeObj(oJsonPath)
    FreeObj(oJsonResponse)
Return .T.

/*/{Protheus.doc} deleteDocument
	Deleta documento anexado a SC.
@author juan.felipe
@since 09/12/2021
@return .T., logical
/*/
Method deleteDocument() Class pgcSyncPurchaseRequest
    Local lOk As Logical
    Local cBody As Character
    Local cDocument As Character
    Local cKey As Character
    Local cTable As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oDocumentsService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        cDocument := oJsonRequest:document
        cKey := oJsonRequest:documentKey
        cTable := "SC1"

        oDocumentsService := pgcDocumentsService():New()
        oJsonResponse := oDocumentsService:deleteDocument(cDocument, cKey, cTable)
        lOk := oDocumentsService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oDocumentsService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} checkOpen
	Verifica se um array de solicitações está em aberto
@author juan.felipe
@since 14/02/2022
@return .T., logical
/*/
Method postCheckOpen() Class pgcSyncPurchaseRequest
    Local lOk As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oRequestService As Object
    Local oUtils As Object

    lOk := .T.
    cBody := oRest:getBodyRequest()
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .F.)
        oRequestService := pgcSyncPurchaseRequestService():New()
            
        oJsonResponse := oRequestService:postCheckOpen(oJsonRequest)
        lOk := oRequestService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oRequestService)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getGroupByRequestOpen
	Consulta solicitações de compra em aberto agrupadas por solicitação (retorna apenas o cabeçalho)
@author juan.felipe
@since 17/03/2022
@return lRet, logical, consulta realizada corretamente.
/*/
Method getGroupByRequestOpen() Class pgcSyncPurchaseRequest
    Self:setAnswer("", 1, 1)
Return .T.

/*/{Protheus.doc} getGroupByRequestPartial
	Consulta solicitações de compra parcialmente atendidas agrupadas por solicitação (retorna apenas o cabeçalho)
@author juan.felipe
@since 17/03/2022
@return lRet, logical, consulta realizada corretamente.
/*/
Method getGroupByRequestPartial() Class pgcSyncPurchaseRequest
    Self:setAnswer("", 2, 1)
Return .T.

/*/{Protheus.doc} getGroupByRequestAttended
	Consulta solicitações de compra atendidas agrupadas por solicitação (retorna apenas o cabeçalho)
@author juan.felipe
@since 17/03/2022
@return lRet, logical, consulta realizada corretamente.
/*/
Method getGroupByRequestAttended() Class pgcSyncPurchaseRequest
    Self:setAnswer("", 3, 1)
Return .T.

/*/{Protheus.doc} getGroupByProductOpen
	Consulta solicitações de compra em aberto agrupadas por produto (retorna apenas os produtos que contenham SCs)
@author juan.felipe
@since 17/03/2022
@return lRet, logical, consulta realizada corretamente.
/*/
Method getGroupByProductOpen() Class pgcSyncPurchaseRequest
    Self:setAnswer("", 1, 2)
Return .T.

/*/{Protheus.doc} getGroupByProductPartial
	Consulta solicitações de compra parcialmente atendidas agrupadas por produto (retorna apenas os produtos que contenham SCs)
@author juan.felipe
@since 17/03/2022
@return lRet, logical, consulta realizada corretamente.
/*/
Method getGroupByProductPartial() Class pgcSyncPurchaseRequest
    Self:setAnswer("", 2, 2)
Return .T.

/*/{Protheus.doc} getGroupByProductAttended
	Consulta solicitações de compra atendidas agrupadas por produto (retorna apenas os produtos que contenham SCs)
@author juan.felipe
@since 17/03/2022
@return lRet, logical, consulta realizada corretamente.
/*/
Method getGroupByProductAttended() Class pgcSyncPurchaseRequest
    Self:setAnswer("", 3, 2)
Return .T.


/*/{Protheus.doc} getPurchaseRequestCentralized
	Consulta solicitações de compra em aberto por todas as filiais
@author renan.martins
@since 03/2024
@return .T., logical, consulta realizada corretamente.
/*/
Method getPurchaseRequestCentralized() Class pgcSyncPurchaseRequest
    Self:setAnswer("", 4) 
Return .T.
