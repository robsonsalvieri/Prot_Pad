#INCLUDE "MATA150.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE 'FWLIBVERSION.CH'  

#DEFINE VALMERC 	1  // Valor total do mercadoria 
#DEFINE VALDESC 	2  // Valor total do desconto
#DEFINE VALIPI  	3  // Valor total do IPI
#DEFINE VALICM  	4  // Valor total do ICMS
#DEFINE FRETE   	5  // Valor total do Frete
#DEFINE VALDESP 	6  // Valor total da despesa
#DEFINE TOTF1		7  // Total de Despesas Folder 1
#DEFINE TOTPED		8  // Total do Pedido
#DEFINE BASEIPI 	9  // Base de IPI
#DEFINE BASEICM		10 // Base de ICMS
#DEFINE BASESOL		11 // Base do ICMS Sol.
#DEFINE VALSOL		12 // Valor do ICMS Sol.
#DEFINE VALCOMP 	13 // Valor do ICMS Com.
#DEFINE SEGURO		14 // Valor total do seguro
#DEFINE TOTF3		15 // Total utilizado no Folder 3

Static lLGPD := FindFunction("SuprLGPD") .And. SuprLGPD()
Static _lPGC := .F.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MATA150   ³Rev.   ³ Edson Maricate        ³ Data ³29.07.2000 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de atualizacao manual das cotacoes de compra          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³MV_PAR01: Sugere TE padrao                                   ³±±
±±³          ³MV_PAR02: Demonstra somente as cotacoes em aberto            ³±±
±±³          ³MV_PAR03: Demonstra somente as cotacoes do comprador         ³±±
±±³          ³MV_PAR04: Tipo de Exclusao ( Item/Cotacao)                   ³±±
±±³          ³MV_PAR05: Demonstrar impostos ( Aliquota/Imposto )           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³.T.	                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina permite as operacoes de pesquisa, visualizacao,  ³±±
±±³          ³Nova proposta, Novo participante ou exclusao da cotacao de   ³±±
±±³          ³compra                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³    2 - Simplesmente Mostra os Campos                         ³
//³    3 - Inclui registros no Bancos de Dados                   ³
//³    4 - Altera o registro corrente                            ³
//³    5 - Remove o registro corrente do Banco de Dados          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ PARAMETROS PARA ROTINA AUTOMATICA :                          ³
//³ ExpA1 : Array contendo os dados do cabecalho da Cotacao      ³
//³ ExpA2 : Array contendo os dados dos itens                    ³
//³ ExpN3 : Indica qual a operacao  a ser executada  -           ³
//³         2 : Inclui novo participante                         ³
//³         3 : Atualiza os dados de uma cotacao ja existente    ³
//³         4 : Inclui uma nova proposta em uma cotacao ja exis. ³
//³         5 : Exclui toda a cotacao                            ³
//³ ExpL1 : Filtra a Origem das Cotações para TRM			     ³
//³ ExpL2 : Indica se é uma simulacao						     ³
//³ ExpN4 : Item recebido pela p/ser excluido pela Exec.Auto	 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
FUNCTION MATA150(xAutoCab,xAutoItens,nOpcAuto,lTrm,lSimulacao,xRecno,lxRetImp,lSaveF2D)

	Local aArea			:= GetArea()
	Local aGrupo 		:= {}
	Local cGrupComp		:= ""
	Local cFiltraSc8	:= ""
	Local cFilUser		:= ""
	Local nX          	:= 0
	Local aIndexSC8  	:= {}
	Local aCores		:= {}
	Local lSolic		:= (GetMv("MV_RESTCOM")=="S")
	Local aFixe 		:= {{OemToAnsi(STR0001),"C8_NUM    " },;		//"Numero"
	{ OemToAnsi(STR0002),"C8_FORNECE" },;		//"Fornecedor"
	{ OemToAnsi(STR0003),"C8_LOJA   " },;		//"Loja"
	{ OemToAnsi(STR0004),"C8_NUMPRO"  },;		//"Proposta"
	{ OemToAnsi(STR0005),"C8_PRODUTO" },;		//"Cod.Produto"
	{ OemToAnsi(STR0006),"C8_PRECO"   },;		//"Preco"
	{ OemToAnsi(STR0007),"C8_VALIDA " }}		//"Validade "
	Local aCorUsr     	:= {}
	Local nCnt        	:= 0
	Local nCntFor		:= 0
	Local cCotACC		:= "(!Empty(SC8->C8_ACCNUM) .And. Empty(SC8->C8_NUMPED))"

	PRIVATE bFiltraBrw	:= {|| Nil}
	PRIVATE cCadastro	:= OemToAnsi(STR0056)			//"Atualizacao Precos da Cotacao"
	PRIVATE aRotina		:= MenuDef()
	PRIVATE l150Auto	:= (xAutoCab <> Nil)
	PRIVATE lOnUpdate  	:= .T.
	PRIVATE aAutoCab	:= {}
	PRIVATE aAutoItens	:= {}
	PRIVATE lGrade      := MaGrade()
	PRIVATE cFornBKP    := ""
	PRIVATE cLojBKP     := ""
	Private lAtudesc	:= .F.
	Private lAtuDesp	:= .f.
	Private cValDesp	:= 0
	Private lRetImp		:= .F.
	Private lF2DSave	:= .F.
	
	Default lTrm        := .F.
	Default xRecno      := ""
	Default lxRetImp	:= .F.
	Default lSaveF2D	:= .F.

	lRetImp  := lxRetImp //Define se a rotina foi executada apenas para carregar os impostos. (default = .F.)
	lF2DSave := lSaveF2D //Para gravar na F2D, via MAFISTG
	//-- Adiciona cores do usuario
	If ExistBlock("MT150LEG")
		aCorUsr := ExecBlock("MT150LEG",.F.,.F., { 1 })
		If ValType(aCorUsr) <> "A"
			aCorUsr := {}
		EndIf
		For nCnt := 1 To Len(aCorUsr)
			Aadd( aCores , { aCorUsr[nCnt,1],aCorUsr[nCnt,2] } )
		Next nCnt
	EndIf

	Aadd( aCores , { "Alltrim(C8_ORIGEM) $ 'PGCA020|NFCA020'",'BR_BRANCO'} )
	Aadd( aCores , { cCotACC,'BR_AZUL'})  //Cotação em compra através do portal ACC
	Aadd( aCores , { "!" +cCotACC +".And.Empty(C8_NUMPED).And.C8_PRECO<>0 .And. !Empty(C8_COND)",'ENABLE' } )    //Cotacao em aberto
	Aadd( aCores , { "!Empty(C8_NUMPED)",'DISABLE'} )				    //Cotacao Baixada
	Aadd( aCores , { '!' +cCotACC +'.And.(C8_PRECO==0 .Or. Empty(C8_COND)).And.Empty(C8_NUMPED)','BR_AMARELO'}) //Cotacao nao digitada
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ponto de entrada para alteracao da ordem dos campos do aFixe ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock("MT150FIX")
		aFixe := ExecBlock("MT150FIX",.F.,.F.,aFixe)
	Endif

	If !( l150Auto )
		If ( Pergunte("MTA150",.T.) )

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se o Parametro MV_GRADE estiver ativado a Pergunte mv_par06 "Unifica    ³
			//³Atualizacao de Precos"  sera modificada automaticamente para 1 - NAO    ³
			//³esse recurso nao deve ser usado em produtos grade,usar botao Replicar.  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lGrade
				MV_PAR06 := 1
			EndIf

			If ( MV_PAR02 == 1 .Or. (lSolic .And. MV_PAR03 == 1) .Or. lTrm )

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Avalia Ponto de Entrada e retorna condicao do filtro do usuario         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ExistBlock("MT150FIL")
					cFilUser := ExecBlock("MT150FIL",.F.,.F.)
				EndIf

				If lSolic
					aGrupo := UsrGrComp(RetCodUsr())
				Endif

				//Se houver ao menos 150 grupos é feito o filtro de acordo com o legado
				//caso ultrapassar 150 grupos de compra, 
				//é feito filtro de outra forma para prevenir erro SC8000: Filter greater than 2000 bytes.
				If Len(aGrupo) <= 150

					If Empty(MV_PAR07) .or. MV_PAR07 == 2
						cFiltraSc8 := "C8_FILIAL=='"+xFilial("SC8")+"'"
					Endif

					If MV_PAR02 == 1
						If !Empty(cFiltraSc8)
							cFiltraSc8 += " .AND. Empty(C8_NUMPED) "//.And. C8_PRECO == 0"
						Else
							cFiltraSc8 := " Empty(C8_NUMPED)"// .And. C8_PRECO == 0"
						Endif
					EndIf

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Avalia se ha necessidade de Filtrar Grupo de Compradores                ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If lSolic
						If Empty(aGrupo)
							aGrupo := UsrGrComp(RetCodUsr())
						EndIf
						If ( Ascan(aGrupo,"*") == 0 )
							If !Empty(cFiltraSC8)
								cGrupComp  := " .And. (C8_GRUPCOM=='"+Space(Len(SC8->C8_GRUPCOM))+"'"
							Else
								cGrupComp  := " (C8_GRUPCOM=='"+Space(Len(SC8->C8_GRUPCOM))+"'"
							Endif
							For nCntFor := 1 To Len(aGrupo)
								If nCntFor == 1
									cGrupComp += " .Or. C8_GRUPCOM $ '"+aGrupo[nCntFor]+""
								Else
									cGrupComp += ","+aGrupo[nCntFor]
								Endif
							Next nCntFor
							If Len(aGrupo) > 0
								cGrupComp  += "'"
							Endif
							cGrupComp  += ")"
						EndIf
					EndIf

					cFiltraSC8 += cGrupComp

					If lTrm
						If !Empty(cFiltraSC8)
							cFiltraSC8 += " .And. C8_ORIGEM == 'TRM     ' "
						Else
							cFiltraSC8 := " C8_ORIGEM == 'TRM     ' "
						Endif
					EndIf

					If !Empty(cFilUser)
						cFiltraSC8 += If(Empty(cFiltraSC8),AllTrim(cFilUser)," .And. "+AllTrim(cFilUser))
					EndIf
					
				Else

					If Empty(MV_PAR07) .or. MV_PAR07 == 2
						cFiltraSc8 := "@C8_FILIAL='"+xFilial("SC8")+"'"
					Endif

					If MV_PAR02 == 1
						If !Empty(cFiltraSc8)
							cFiltraSc8 += " AND C8_NUMPED = '' "
						Else
							cFiltraSc8 := "@C8_NUMPED = ''"
						Endif
					EndIf

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Avalia se ha necessidade de Filtrar Grupo de Compradores                ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If lSolic
						If ( Ascan(aGrupo,"*") == 0 )
							If !Empty(cFiltraSC8)
								cGrupComp  := " And ( C8_GRUPCOM='"+Space(Len(SC8->C8_GRUPCOM))+"'"
							Else
								cGrupComp  := "@(C8_GRUPCOM='"+Space(Len(SC8->C8_GRUPCOM))+"'"
							Endif
							For nCntFor := 1 To Len(aGrupo)
								If nCntFor == 1
									cGrupComp += " Or C8_GRUPCOM in ('"+aGrupo[nCntFor]+""
								Else
									cGrupComp += "','"+aGrupo[nCntFor]
								Endif
							Next nCntFor
							If Len(aGrupo) > 0
								cGrupComp  += "'))"
							Endif
						EndIf
					EndIf

					cFiltraSC8 += cGrupComp

					If !Empty(cFilUser)
						cFiltraSC8 += If(Empty(cFiltraSC8),AllTrim(cFilUser)," And "+AllTrim(cFilUser))
					EndIf 

				Endif

			Else
				If Empty(MV_PAR07) .or. MV_PAR07 == 2
					cFiltraSc8 := "C8_FILIAL=='"+xFilial("SC8")+"'"
				Endif
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Endereca a funcao de BROWSE                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SetKey(VK_F12,{|| Pergunte("MTA150",.T.) , If(lGrade,MV_PAR06 := 1, ) })
			mBrowse( 6, 1,22,75,"SC8",aFixe,,,,,aCores,,,,,,,,,,,,cFiltraSC8 )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Desativa tecla que aciona perguntas                            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SetKey(VK_F12,Nil)
		EndIf
	Else
		Pergunte("MTA150",.F.)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se o Parametro MV_GRADE estiver ativado a Pergunte mv_par06 "Unifica    ³
		//³Atualizacao de Precos"  sera modificada automaticamente para 1 - NAO    ³
		//³esse recurso nao deve ser usado em produtos grade,usar botao Replicar.  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lGrade
			MV_PAR06 := 1
		EndIf

		DEFAULT lSimulacao := .F.
		lOnUpdate  := !lSimulacao
		aAutoCab   := xAutoCab
		aAutoItens := xAutoItens
		For nX := 1 To Len(aRotina)
			aRotina[nX][4] := 0
		Next nX
		aRotina[nOpcAuto][4] := nOpcAuto
		// Caso tenha passado uma chave para pesquisa forca opcao 4 para que a rotina automatica entenda como alteracao
		If nOpcAuto == 3 .And. Upper(AllTrim(aAutoCab[Len(aAutoCab)][1])) == "INDEX"
			aRotina[nOpcAuto][4]:= 4
			aRotina[4]:= ACLONE(aRotina[3])
			nOpcAuto:= 4
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//|Filtra tabela para Eliminar um Item especifico      |
		//|quando o mesmo for enviado pela Rotina Automática   |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If xRecno!=Nil .And. ValType(xRecno)=="C" .And. nOpcAuto==5
			If !Empty(xRecno)
				DbSelectArea("SC8")
				DbSetOrder(1)
				Set Filter to c8_item = xRecno
			EndIf
		EndIf

		MBrowseAuto(nOpcAuto,Aclone(aAutoCab),"SC8")
		xAutoCab   := aAutoCab
		xAutoItens := aAutoItens
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Restaura a integridade da rotina                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SC8")
	RetIndex("SC8")
	dbClearFilter()
	aEval(aIndexSc8,{|x| Ferase(x[1]+OrdBagExt())})
	RestArea(aArea)
Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} A150Propos
Valida se pode gerar nova proposta.

@author douglas.nunes

@since 18/09/2014
@version 1.0
/*/
//-------------------------------------------------------------------
Function A150Propos(cAlias,nReg,nOpcX,cMessage)
	Local aAreas  := {SC8->(GetArea()), GetArea()}
	Local nOpcA   := 0
	Local lDigita := .F.
	Local lPossuiSaldo := .F.
	Local cRefNumPro   := ""
	Local cCodCot := ""
	Local cForn   := ""
	Local cLoja   := ""
	Local cNProp  := ''
    Default cMessage := '' //-- Retorna mensagens de validação por referência @cMessage
	
	If l150Auto
		cCodCot := aAutoCab[aScan(aAutoCab,{|x| AllTrim(x[1]) == "C8_NUM"})][2]
		cForn  	:= aAutoCab[aScan(aAutoCab,{|x| AllTrim(x[1]) == "C8_FORNECE"})][2]
		cLoja   := aAutoCab[aScan(aAutoCab,{|x| AllTrim(x[1]) == "C8_LOJA"})][2]
		If aScan(aAutoCab,{|x| AllTrim(x[1]) == "C8_FORNOME"}) > 0
			cA150FNome := aAutoCab[aScan(aAutoCab,{|x| AllTrim(x[1]) == "C8_FORNOME"})][2]
		Elseif !Empty(cForn)
			cA150FNome := Posicione("SA2",1,xFilial("SA2")+cForn+cLoja,"A2_NOME")
		EndIf
	Else
		cCodCot 	:= SC8->C8_NUM
		cForn   	:= SC8->C8_FORNECE
		cLoja   	:= SC8->C8_LOJA
		cA150FNome	:= SC8->C8_FORNOME
	EndIf


	If _lPGC //-- Verificar antes na cotação posicionada se algum item ainda possui saldo disponivel 	
		SC8->(DbSetOrder(1))
		SC8->(MsSeek(xFilial('SC8')+cCodCot+cForn+cLoja+Replicate('Z',TamSx3('C8_ITEM')[1])+Replicate('Z',TamSx3('C8_NUMPRO')[1]),.T.))
		
		SC8->(DbSkip(-1))
		cRefNumPro := SC8->C8_NUMPRO //Maior número de Proposta

		//Pega somente da proposta maior
		While (!SC8->(Bof()) .AND. SC8->C8_NUM == cCodCot .AND. cForn == SC8->C8_FORNECE .AND. cLoja == SC8->C8_LOJA)
			//Verifica se algum produto da última proposta possui preço preenchido
			If (cRefNumPro == SC8->C8_NUMPRO .And. SC8->C8_QTDISP > 0)
				lPossuiSaldo := .T.
				Exit
			EndIf
			SC8->(DbSkip(-1))
		EndDo

		//-- Retorna posição
		RestArea(aAreas[1])
	EndIf

	//Verifica se cotação não está encerrada
	If (Empty(SC8->C8_NUMPED)) .OR. ( _lPGC .AND. lPossuiSaldo ) 
	
		if !Empty(cForn+cLoja)
			SC8->(DbSetOrder(1))
			//Faz o Seek e Posiciona um registro depois do fim da chave xFilial('SC8')+cCodCot+cForn+cLoja
			SC8->(MsSeek(xFilial('SC8')+cCodCot+cForn+cLoja+Replicate('Z',TamSx3('C8_ITEM')[1])+Replicate('Z',TamSx3('C8_NUMPRO')[1]),.T.))
			//Posiciona no último registro da chave
			SC8->(DbSkip(-1))
			cNProp  := SC8->C8_NUMPRO //Maior número de Proposta
			While (!lDigita .AND. !SC8->(Bof()) .AND. SC8->C8_NUM == cCodCot .AND. cForn == SC8->C8_FORNECE .AND. cLoja == SC8->C8_LOJA)
				//Verifica se algum produto da última proposta possui preço preenchido
				If (SC8->C8_PRECO <> 0 .AND. cNProp == SC8->C8_NUMPRO) .Or. (A150GetPGC() .And. SC8->C8_SITUAC <> '1' .And. cNProp == SC8->C8_NUMPRO)
					lDigita := .T.
					Exit
				EndIf
				SC8->(DbSkip(-1))
			EndDo
		else
			//tratativa para quando for informado um fornecedor sem código ( melhoria que ocorreu na v12)
			cAliasSC8 := getNextAlias()
			cQuery := " SELECT C8_PRECO, C8_NUM, C8_FORNECE, C8_LOJA, C8_ITEM, C8_NUMPRO, C8_ITEMGRD, R_E_C_N_O_ "
			If A150GetPGC()
				cQuery += " , C8_SITUAC "
			EndIf
			cQuery += " FROM "+ RetSqlName("SC8")+" SC8 "
			cQuery += "WHERE C8_NUM = '"+ cCodCot +"' AND C8_FORNOME = '"+ cA150FNome +"' "
			cQuery += "AND C8_NUMPRO = '"+ SC8->C8_NUMPRO +"' AND C8_FILIAL = '"+SC8->C8_FILIAL+"'AND D_E_L_E_T_ = ' ' "
			cQuery += "ORDER BY C8_NUM, C8_NUMPRO, C8_FORNOME"

			cQuery := ChangeQuery(cQuery)

			dbUseArea( .T., "TOPCONN", TcGenQry( ,,cQuery ), cAliasSC8, .F., .F. )

			While !Eof()

				IF (cAliasSC8)->C8_PRECO <> 0 .Or. (A150GetPGC() .And. (cAliasSC8)->C8_SITUAC <> '1')

					SC8->(dbGoTo((cAliasSC8)->R_E_C_N_O_))
					lDigita := .T.
					Exit

				ENDIF

				dbSelectArea(cAliasSC8)
				dbSkip()
			EndDo

			(cAliasSC8)->(dbCloseArea())

		endif
        
		If lDigita
            If _lPGC
                nOpcA := 1
            Else
			    nOpcA := A150Digita(cAlias,nReg,nOpcX)  
            EndIf
		Else
			Help(" ",1,"A150PRO0")
            cMessage := STR0118 //-- Não é possível incluir uma nova proposta, pois a última proposta ainda não foi atualizada.
		EndIf
    Else
        cMessage := STR0119 //-- Não é possível incluir uma nova proposta, pois a cotação está finalizada.
	EndIf

    IIf(_lPGC, aEval(aAreas, {|x| RestArea(x), FwFreeArray(x)}), FwFreeArray(aAreas))
Return nOpcA
/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡„o    ³A150Digita³Rev.   ³ Edson Maricate        ³ Data ³29.07.2000 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³          ³Rotina de atualizacao manual das cotacoes de compra          ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150Digita(ExpC1,ExpN1,ExpN2)                                ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpC1: Alias do Arquivo                                      ³±±
	±±³          ³ExpN1: Numero do Registro                                    ³±±
	±±³          ³ExpN2: Opcao do MBrowse                                      ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³Nenhum                                                       ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡„o ³Esta rotina efetua a atualizacao das cotacoes de compra      ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso       ³ Materiais                                                   ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FUNCTION A150Digita(cAlias,nReg,nOpcX)

	Local aArea     	:= GetArea()
	Local aPages		:= {"HEADER"}
	Local aRecSC8		:= {}
	Local aSCs			:= {}
	Local aImpRef   	:= {}
	Local aTitles		:= {OemToAnsi(STR0014),; //"Totais"
							OemToAnsi(STR0015),; //"Inf. Fornecedor"
							OemToAnsi(STR0016),; //"Frete/Despesas"
							OemToAnsi(STR0017),; //"Descontos"
							OemToAnsi(STR0018),; //"Impostos"
							OemToAnsi(STR0019)}  //"Mensagem/Reajuste"	
	Local aObj      	:= {}
	Local aInfForn		:= {"","",Ctod(""),Ctod(""),"","",""}
	Local aCombo    	:= CarregaTipoFrete()
	Local abuttons  	:= {}
	Local aColsUsr  	:= {}
	Local bBlock	
	Local bRefCabOk 	:={||A150CabOk(nOpcX,aImpRef,aInfForn,@cDescMsg,@cDescFor,@a150Var)}
	Local cCadastro 	:= OemtoAnsi(STR0056)			//"Atualiza‡Æo Pre‡os da Cota‡Æo"
	Local cDescMsg		:= ""
	Local cDescFor		:= ""
	Local cAviso    	:= ""
	Local cHelp     	:= ""
	Local cNumCot		:= SC8->C8_NUM
	Local cFilSC8 		:= xFilial("SC8")
	Local l150Visual	:= .F.
	Local l150Altera	:= .F.
	Local lContinua 	:= .T.
	Local lValido   	:= .F.
	Local lAnalisada	:= .F.
	Local lIniTes   	:= MV_PAR01==1
	Local lDelCot   	:= MV_PAR04==2 .And. nOpcx == 5
	Local lDelProd  	:= MV_PAR04==3 .And. nOpcx == 5
	Local lMt150Scr 	:= ExistBlock("MT150SCR")
	Local lMT150OK  	:= ExistBlock("MT150OK")
	Local lClicB		:= A131VerInt()
    Local lPgc   		:= .F.
	Local nOpc      	:= 0
	Local nX        	:= 0
	Local nSaveSX8  	:= GetSX8Len()
	Local nLinMax		:= GetNewPar("MV_COMLMAX", 999)
	Local aInfo 		:= {}
	Local aPosGet		:= {}
	Local aPosObj   	:= {}
	Local aSizeAut  	:= {}
	Local nPosTes 		:= 0 
	Local oDlg
	Local oFolder
	Local oGetDados
	Local oForn
	Local oSize
	Local ocA150Loj
	Local oCond
	Local oGetMoeda
	Local oDescCond
	Local oDescMoed
	Local oGetTaxa
	Local lHelp 		:= .F.
	Local aImpItem 		:= {}
	Local nLoop	   		:= 1
	Local lExsC8CF		:= SC8->(FieldPos("C8_CF")) > 0
	Local nPosC8CF		:= 0
	Local nPosIpi   	:= 0
	Local nPosIcm   	:= 0
	Local nPosISS		:= 0
	Local nPosCmp   	:= 0
	Local lTrbGen		:= IIf(FindFunction("ChkTrbGen"), ChkTrbGen("SC8", "C8_IDTRIB"), .F.)
	Local nForLc		:= 0
	Local nPosIDTrb		:= 0
	Local cNumIDTrb 	:= ""

	Local aItens		:= {}
	Local aForns		:= {}
	Local aCots			:= {}
	/* aCots 	[x]
	[x,1] 			- Num. Cotação.
	[x,2] 			- Data Emissão.
	[x,3,y] 		- Array de Fornecedores (aForns).
	[x,3,y,1]		- Cod.  Fornecedor.
	[x,3,y,2]		- Loja  Fornecedor.
	[x,3,y,3]		- eMail Fornecedor.
	[x,3,y,4,z]	- Array de Itens por Fornecedor (aItens).
	[x,3,y,4,z,1]	- Cod. Produto.
	[x,3,y,4,z,2] - Quantidade.
	[x,3,y,4,z,3]	- Prazo de entrega.
	*/
	Local cBody 		:= ""
	Local oContato 		:= NIL
	Local oForNome		:= NIL
	Local oForMail		:= NIL
	Local lLock			:= .T.
	Local nPosFrete		:= 0
	Local cForncTmp		:= ""
	Local cLojaTmp		:= ""
	Local nForncTmp 	:= 0
	Local nLojaTmp		:= 0
	Local lGenTaxes		:= FindClass('backoffice.com.generictaxes.generictaxes')
	Local oGenTax		:= JsonObject():New()
	Local oJsonTax		:= Nil
	Local cRetGentx		:= ""
	Local aRetGenTx		:= {}
	Local lExsObjGen	:= type("oTribProcGen") == "J"

	If !l150Auto
		aSizeAut		:= MsAdvSize()
	EndIf

	PRIVATE l150Inclui  := .F.
	PRIVATE l150Propost := .F.
	PRIVATE l150Deleta  := .F.
	PRIVATE l150Atuliz  := .F.
	PRIVATE oFisTrbGen 	:= Nil

    lPgc := if(Alltrim(SC8->C8_ORIGEM) $ "PGCA020|NFCA020" .and. !A150GetPGC(),.T., .F.)//Verifica se cotação foi gerada pelo PGC.

	if lRetImp .And. lGenTaxes
		oGenTax := backoffice.com.generictaxes.generictaxes():New()
	endif

	if lTrbGen 
		Aadd(aTitles,OemToAnsi(STR0122)) //"Tributos Genéricos - Por item"
	endif
    
    If lPgc .and. !FwIsInCallStack("PUTQUOTE") // Veio do Portal do Fornecedor
        Help(,,"A150PGC",,STR0115, 1, 0,,,,,, {STR0116} ) //-- "Não é permitido realizar ações em cotações oriundas do Protheus Gestão de Compras.". Utilize a rotina Protheus Gestão de Compras.
    Else
        lLock := LockByName("COT_" + cFilSC8 + cNumCot, .T., .F.) 

        If lLock
            //Limpa variaveis de BKP //
            cFornBKP:=""
            cLojBkp:=""

            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
            //³ Avalia botoes do usuario                                     ³
            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
            If ExistBlock( "MA150BUT" )
                aButtons := ExecBlock( "MA150BUT", .F., .F. )
            EndIf
            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
            //³Identifica qual operacao esta sendo executada                 ³
            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
            Do Case
            Case nOpcX == 6
                l150Visual := .T.
            Case nOpcX == 2
                l150Inclui	:= .T.
                If ExistBlock("MT150NPA")
                    lContinua := ExecBlock("MT150NPA",.F.,.F.)
                    If ValType(lContinua) <> "L"
                        lContinua := .T.
                    EndIf
                EndIf
            Case nOpcX == 3 .Or. nOpcX == 4 
                If ExistBlock("MT150ENV")
                    lContinua := ExecBlock("MT150ENV",.F.,.F.)
                    If ValType(lContinua) <> "L"
                        lContinua := .T.
                    EndIf
                EndIf
                l150Altera	:= .T.
                If nOpcX == 4
                    l150Propost := .T.
                EndIf
                If nOpcX == 3
                    l150Atuliz := .T.
                EndIf
            Case nOpcX == 5
                l150Deleta	:= .T.
                l150Visual	:= .T.
            EndCase


			PRIVATE bTgRefresh    := {|| Iif(lTrbGen .and. !l150Auto,MaFisLinTG(oFisTrbGen,oGetDados:oBrowse:nAt),.T.)}
            PRIVATE cA150Num  := SC8->C8_NUM
            PRIVATE dA150Emis := SC8->C8_EMISSAO
            PRIVATE cA150Forn := If(l150Inclui,CriaVar("C8_FORNECE"),SC8->C8_FORNECE)
            PRIVATE cA150Loj  := If(l150Inclui,CriaVar("C8_LOJA"),SC8->C8_LOJA)

            PRIVATE cA150FNome:= IF(l150Inclui,CriaVar("C8_FORNOME"),SC8->C8_FORNOME)
            PRIVATE cA150FMail:= IF(l150Inclui,CriaVar("C8_FORMAIL"),SC8->C8_FORMAIL)

            PRIVATE cA150ProvEnt:= If(cPaisLoc == "ARG", If(l150Inclui, CriaVar("C8_PROVENT"),SC8->C8_PROVENT ), "")
            PRIVATE cCondicao := If(l150Inclui,CriaVar("C8_COND"),SC8->C8_COND)
            PRIVATE cDescCond := If(l150Inclui,CriaVar("E4_DESCRI"),SE4->E4_DESCRI)
            PRIVATE cContato  := If(l150Inclui,CriaVar("C8_CONTATO"),SC8->C8_CONTATO)
            PRIVATE cMsg      := If(l150Inclui,CriaVar("C8_MSG"),SC8->C8_MSG)
            PRIVATE cReajuste := If(l150Inclui,CriaVar("C8_REAJUST"),SC8->C8_REAJUST)
            PRIVATE c150Frete := If(l150Inclui,"C-CIF",RetTipoFrete(SC8->C8_TPFRETE))
            PRIVATE nDesc1	  := If(l150Inclui,CriaVar("C8_DESC1"),SC8->C8_DESC1)
            PRIVATE nDesc2    := If(l150Inclui,CriaVar("C8_DESC2"),SC8->C8_DESC2)
            PRIVATE nDesc3	  := If(l150Inclui,CriaVar("C8_DESC3"),SC8->C8_DESC3)
            PRIVATE a150Var   := {0,0,0,0,0,0,0,0,0,0,0,0,0,0,0}
            PRIVATE aCols     := {}
            PRIVATE aHeader   := {}
            PRIVATE aHeaderPro:= {}

            PRIVATE bFolderRefresh:= {|| A150RodOk(oFolder,aObj) }
            PRIVATE bGDRefresh    := {|| (oGetDados:oBrowse:Refresh()) }
            PRIVATE bZeraDesc	  := {|| (nDesc1:=0),(nDesc2:=0),(nDesc3:=0)}
            PRIVATE bCalcDesc	  := {|| (A150VDesc(nDesc1,nDesc2,nDesc3,@a150Var))}
            PRIVATE bRefresh      := {|| (A150Refresh(@a150Var)),(Eval(bFolderRefresh))}
            PRIVATE bFisToaCols   := {|| (A150FisToaCols()),Eval(bRefresh),Eval(bGDRefresh)}
            PRIVATE nMoedaCot     := If(l150Inclui,Iif(SC8->C8_MOEDA > 0, SC8->C8_MOEDA, 1),Max(SC8->C8_MOEDA,1)) // Pega a moeda informada na rotina Gera Cotacoes (MATA130).
            PRIVATE nTxMoeda      := If(l150Inclui,0,SC8->C8_TXMOEDA)
            PRIVATE cDescMoed     := SuperGetMv("MV_MOEDA"+AllTrim(Str(nMoedaCot,2)))
            PRIVATE cMoeda

            If !l150Auto .And. !Empty(SC8->C8_ACCNUM) .And. Empty(SC8->C8_NUMPED)
                If nOpcX # 5
                    Aviso(STR0086,STR0087,{"Ok"}) //"Portal ACC ## Esta cotação poderá ser manipulada somente via portal ACC."
                    lContinua := .F.
                ElseIf Aviso(STR0086,STR0088,{STR0107,STR0108}) == 2
                    lContinua := .F.
                EndIf
            EndIf

            If l150Auto
                nPosFrete := aScan(aAutoCab,{|x| AllTrim(x[1]) == "C8_TPFRETE"})
                If nPosFrete > 0
                    c150Frete  := aAutoCab[nPosFrete][2]
                EndIf
                cA150Forn  := aAutoCab[aScan(aAutoCab,{|x| AllTrim(x[1]) == "C8_FORNECE"})][2]
                cA150Loj   := aAutoCab[aScan(aAutoCab,{|x| AllTrim(x[1]) == "C8_LOJA"})][2]
                If aScan(aAutoCab,{|x| AllTrim(x[1]) == "C8_FORNOME"}) > 0
                    cA150FNome := aAutoCab[aScan(aAutoCab,{|x| AllTrim(x[1]) == "C8_FORNOME"})][2]
                Elseif !Empty(cA150Forn)
                    cA150FNome := Posicione("SA2",1,xFilial("SA2")+cA150Forn+cA150Loj,"A2_NOME")
                EndIf
            EndIf

            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
            //³ Verifica se a SC esta vinculada a um Edital              ³
            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
            SC1->(dbSetOrder(5))
            If SC1->( MsSeek(xFilial("SC1")+SC8->C8_NUM+SC8->C8_PRODUTO))
                If (SC1->C1_QUJE>0 .And. !Empty(SC1->C1_CODED) ) .And. !l150Auto
                    Aviso(STR0021,STR0090+SC1->C1_NUM+STR0091+Alltrim(SC1->C1_CODED)+STR0092,{"Ok"})
                    lContinua := .F.
                EndIf
            EndIf

            If lContinua
                //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                //³Botao de consulta ao Historico de Produtos                    ³
                //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                aadd(aButtons,{"S4WB005N" ,{|| A150ComView() },STR0064,STR0065})
                If ! __lPyme
                    aadd(aButtons,{"RPMCPO"   ,{|| A150Replace() },STR0074,STR0075})  //"Replicar dados" ### "Replicar"
                EndIf

                //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                //³Se for rotina automatica nao executar funcoes de atualizacoes³
                //³de janela                                                    ³
                //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                If l150Auto
                    bGDRefresh		:= {|| Nil }
                    bFolderRefresh	:= {|| Nil }
                    lDelCot 		:= (nOpcx==5) .And. MV_PAR04 == 2	// Exclusao por cotacao
                EndIf

                //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                //³Verifica se a Cotacao de compra pode ser alterada             ³
                //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                If ( l150Deleta )
                    bBlock := {|| l1:=MaCanDelCOT("SC8",.F.,@cAviso,@cHelp),lValido:=If(lValido,lValido,l1),l1}
                Else
                    bBlock := {|| l1:=MaCanAltCot("SC8",.F.,@cAviso,nOpcX),lValido:=If(lValido,lValido,l1),l1}
                EndIf

                //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                //³Efetua a montagem do Aheader e do Acols                       ³
                //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				/* Se NFC e novo participante já cadastrado, os dados que chegam aqui é do clone de outro registro já existente. Assim, é necessário
				passar o fornecedor e loja corretos, para inicializar as variáveis de tributos e tributos genéricos. */
				nForncTmp := aScan(aAutoCab,{|x| AllTrim(x[1]) == "FORNREALMD"}) //aAutoCab[aScan(aAutoCab,{|x| AllTrim(x[1]) == "FORNREAL"})][2]
				nLojaTmp  := aScan(aAutoCab,{|x| AllTrim(x[1]) == "LOJAREALMD"}) //aAutoCab[aScan(aAutoCab,{|x| AllTrim(x[1]) == "LOJAREAL"})][2]
	
				cForncTmp := iif( nForncTmp > 0 .and. nLojaTmp > 0, aAutoCab[nForncTmp][2], cA150Forn)
				cLojaTmp  := iif( nForncTmp > 0 .and. nLojaTmp > 0, aAutoCab[nLojaTmp][2], cA150Loj)

                MaFisIni(cForncTmp, cLojaTmp, "F", "N",,,, !l150Visual,,,,,,,,,,,,,,,,,,,,,,,,, lTrbGen)
                lContinua := A150Monta(nOpcX,bBlock,@aRecSc8,@aSCs,@aImpRef,lIniTes,lDelCot,lDelProd,@lAnalisada)
                //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                //³Ponto de entrada para alterar/inserir dados no Item                     ³
                //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                If l150Altera .and.!l150Propost
                    If ExistBlock("MT150GET")
                        aColsUsr := ExecBlock("MT150GET",.F.,.F.,{aCols})
                        If ValType(aColsUsr) == "A"
                            aCols:= ACLONE(aColsUsr)
                        EndIf
                    Endif
                EndIF

				//Ao Atualizar a proposta, força que o sistema busque os dados dos impostos conforme configurador de tributos
				if lExsC8CF .and. lTrbGen
					nPosTes := aScan(aHeader,{|x| AllTrim(x[2])=="C8_TES"})
					nPosC8CF := aScan(aHeader,{|x| AllTrim(x[2])=="C8_CF"})
					nPosIpi  := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_ALIIPI"})
					nPosIcm  := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_PICM"})
					nPosISS  := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_ALIQISS"})
					nPosCmp  := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_ALIQCMP"})

					For nForLc := 1 to Len(aCols)
						if !empty(aCols[nForLc, nPosTes]) .or. !empty(aCols[nForLc, nPosC8CF])
							MaFisLoad("IT_TES", "", nForLc)
							MaFisLoad("IT_CF", "", nForLc)

							MaFisAlt("IT_TES", aCols[nForLc, nPosTes] , nForLc)
							MaFisAlt("IT_CF" , aCols[nForLc, nPosC8CF], nForLc)

							if nPosIpi > 0
								MaFisAlt("IT_ALIQIPI" , aCols[nForLc, nPosIpi], nForLc)
							endif
							if nPosIcm > 0
								MaFisAlt("IT_ALIQICM" , aCols[nForLc, nPosIcm], nForLc)
							endif
							if nPosISS > 0
								MaFisAlt("IT_ALIQISS" , aCols[nForLc, nPosISS], nForLc)
							endif
							if nPosCmp > 0
								MaFisAlt("IT_ALIQCMP" , aCols[nForLc, nPosCmp], nForLc)
							endif
						endif
					Next	
				endif
				MaFisToCols(aHeader,aCols,,"MT150")

                If ( lContinua .And. lValido )
                    A150CabOk(nOpcX,aImpRef,aInfForn,@cDescMsg,@cDescFor,@a150Var)
                    A150DescCnd(cCondicao,,@cDescCond)
                    A150DescMoed(nMoedaCot,,@cDescMoed)
                    If !l150Auto
                        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                        //³Verifica o tipo de Exclusao                                   ³
                        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                        If nOpcX == 5 .And. (lDelCot .Or. lDelProd)
                            If MsgYesNo(IIF(lDelCot,STR0020,STR0069),STR0021) // ''Confirma exclusao da cotacao?'#'Atencao!'
                                nOpc := 1
                            EndIf
                        Else
                            aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 }
                            aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],305,;
                                {{10,40,95,140,200,222,268,240},;
                                {10,40,111,140,223,268,63},;
                                {5,70,160,205,295},;
                                {6,34,200,215},;
                                {6,34,80,113,153,178},;
                                {6,34,235,268,260},;
                                {10,50,150,190,120},;
                                {273,130,190},;
                                {8,45,73,103,139,167,190,225,253},;
                                {144,190,144,190,289,293},;
                                {142,293,140},;
                                {9,47,188,148,9,146}} )
                            DEFINE MSDIALOG oDlg FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] TITLE cCadastro Of oMainWnd PIXEL

                            oSize := FwDefSize():New(.T.,,,oDlg)

                            oSize:AddObject('HEADER',100,40,.T.,.F.)
                            oSize:AddObject('GRID'  ,100,10,.T.,.T.)
                            oSize:AddObject('FOOT'  ,100,90,.T.,.F.)

                            oSize:aMargins 	:= { 3, 3, 3, 3 }
                            oSize:Process()

                            aAdd(aPosObj,{oSize:GetDimension('HEADER', 'LININI'),oSize:GetDimension('HEADER', 'COLINI'),oSize:GetDimension('HEADER', 'LINEND'),oSize:GetDimension('HEADER', 'COLEND')})
                            aAdd(aPosObj,{oSize:GetDimension('GRID'  , 'LININI'),oSize:GetDimension('GRID'  , 'COLINI'),oSize:GetDimension('GRID'  , 'LINEND'),oSize:GetDimension('GRID'  , 'COLEND')})
                            aAdd(aPosObj,{oSize:GetDimension('FOOT'  , 'LININI'),oSize:GetDimension('FOOT'  , 'COLINI'),oSize:GetDimension('FOOT'  , 'LINEND'),oSize:GetDimension('FOOT'  , 'COLEND')})

                            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                            //³Cabecalho da cotacao de compra                                ³
                            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                            @ aPosObj[1][1],aPosObj[1][2] TO aPosObj[1][3],aPosObj[1][4] LABEL '' OF oDlg PIXEL
                            @ aPosObj[1][1]+3 ,aPosGet[1,1]  SAY OemToAnsi(STR0022) Of oDlg PIXEL SIZE 31 ,6  // "Numero"
                            @ aPosObj[1][1]+2 ,aPosGet[1,2]  MSGET cA150Num  Picture PesqPict('SC8','C8_NUM') F3 CpoRetF3('C8_NUM');
                                When .F. Valid CheckSX3('C8_NUM',cA150Num) ;
                                OF oDlg PIXEL SIZE 31 ,6
                            @ aPosObj[1][1]+3 ,aPosGet[2,3]  SAY OemToAnsi(STR0023) Of oDlg PIXEL SIZE 50 ,6 // "Data de Emissao"
                            @ aPosObj[1][1]+2 ,aPosGet[1,4]  MSGET dA150Emis Picture PesqPict('SC8','C8_EMISSAO') F3 CpoRetF3('C8_EMISSAO');
                                When .F. Valid CheckSX3('C8_EMISSAO',dA150Emis) ;
                                OF oDlg PIXEL SIZE 42 ,6
                            @ aPosObj[1][1]+3  ,aPosGet[1,5] SAY OemToAnsi(STR0024) Of oDlg PIXEL SIZE 36 ,6 // "Fornecedor"
                            @ aPosObj[1][1]+2  ,aPosGet[1,6] MSGET oForn  VAR cA150Forn SIZE 075, 009 Picture PesqPict('SC8','C8_FORNECE') F3 CpoRetF3('C8_FORNECE');
                                When l150Inclui  Valid A150Forn(cA150Forn,@cA150Loj,l150Inclui) .And. CheckSX3('C8_FORNECE',cA150Forn) .And. If(cPaisLoc == "ARG",a150ProEnt(),.T.) ;
                                .And. A150AtuRef('NF_CODCLIFOR',cA150Forn) .And. Eval(bRefCabOk) OF oDlg PIXEL SIZE 60 ,6

                            @ aPosObj[1][1]+3  ,aPosGet[1,7] SAY OemToAnsi(STR0003) Of oDlg PIXEL SIZE 36 ,6 // "Loja"
                            @ aPosObj[1][1]+2  ,aPosGet[1,7] + IIf(cPaisLoc == "ARG",30,15)  MSGET ocA150Loj VAR cA150Loj  Picture PesqPict('SC8','C8_LOJA') F3 CpoRetF3('C8_LOJA');
                                When l150Inclui Valid A150Forn(cA150Forn,@cA150Loj,l150Inclui) .And. CheckSX3('C8_LOJA',cA150Loj) ;
                                .And. A150AtuRef('NF_LOJA',cA150Loj) .And. Eval(bRefCabOk) OF oDlg PIXEL SIZE 19 ,6

                            @ aPosObj[1][1]+15 ,aPosGet[2,1]  SAY OemToAnsi(STR0025) Of oDlg PIXEL SIZE 30 ,6 // "Cond. Pagto"
                            @ aPosObj[1][1]+14 ,aPosGet[2,2]  MSGET oCond VAR cCondicao  Picture PesqPict('SC8','C8_COND') F3 CpoRetF3('C8_COND');
                                When !l150Visual .And. VisualSX3('C8_COND') Valid NaoVazio() .And. CheckSX3('C8_COND',cCondicao) .And. A150DescCnd(cCondicao,@oDescCond,@cDescCond,oGetDados) .And. A150ValCond(cCondicao) ;
                                OF oDlg PIXEL SIZE 25 ,6
                            @ aPosObj[1][1]+14,aPosGet[2,7] MSGET oDescCond VAR cDescCond PICTURE PesqPict('SE4','E4_DESCRI') ;
                                WHEN .F. OF oDlg PIXEL SIZE 055,006
                            
                            @ aPosObj[1][1]+15 ,aPosGet[2,3] SAY OemToAnsi(STR0026) Of oDlg PIXEL SIZE 38 ,6 // Contato
                            @ aPosObj[1][1]+14 ,aPosGet[2,4] MSGET oContato VAR cContato  Picture PesqPict('SC8','C8_CONTATO') F3 CpoRetF3('C8_CONTATO');
                                When !l150Visual .And. VisualSX3('C8_CONTATO') Valid CheckSX3('C8_CONTATO',cContato) ;
                                OF oDlg PIXEL SIZE 74 ,6
                            If(lLGPD,OfuscaLGPD(oContato,"C8_CONTATO"),.F.)

                            // novos campos nome email e wf
                            @ aPosObj[1][1]+15  ,aPosGet[1,5] SAY OemToAnsi("Nome") Of oDlg PIXEL SIZE 36 ,6 // "Nome"
                            @ aPosObj[1][1]+14  ,aPosGet[1,6] MSGET oForNome  VAR cA150Fnome SIZE IIf(cPaisLoc == "ARG",80,100) , 009 Picture PesqPict('SC8','C8_FORNOME');
                                When a150fWhen() .And. l150Inclui OF oDlg PIXEL SIZE 60 ,6
                            If(lLGPD,OfuscaLGPD(oForNome,"C8_FORNOME"),.F.)
                                
                            @ aPosObj[1][1]+27  ,aPosGet[1,5] SAY OemToAnsi("E-mail") Of oDlg PIXEL SIZE 36 ,6 // "Email"
                            @ aPosObj[1][1]+26  ,aPosGet[1,6] MSGET oForMail  VAR cA150FMail SIZE 100, 009 Picture PesqPict('SC8','C8_FORMAIL');
                                OF oDlg PIXE SIZE 60,6
                            If(lLGPD,OfuscaLGPD(oForMail,"C8_FORMAIL"),.F.)
                            /* 
                            Funcionalidade aguardando possibilidade de direcionar WF
                            @ aPosObj[1][1]+27  ,aPosGet[1,6]+100 CHECKBOX oForn VAR cA150WF PROMPT "WF" OF oDlg SIZE 050, 010 PIXEL /* ON CLICK (ft321Click(lTarefa,oComboTar,@dDtTarIni,@dDtTarFim)
                            */

                            If cPaisLoc == "ARG"
                                @ aPosObj[1][1]+15,aPosGet[1,7] SAY Alltrim(RetTitle("C8_PROVENT")) OF oDlg PIXEL SIZE 036,10 // Prov. Entrega
                                @ aPosObj[1][1]+14,aPosGet[1,7]+30 MSGET oca150PrvE VAR cA150ProvEnt PICTURE PesqPict('SC8','C8_PROVENT') F3 CpoRetF3('C8_PROVENT');
                                    WHEN VisualSX3('C8_PROVENT')  Valid  ProvEntCt() OF oDlg PIXEL SIZE 019,006  HASBUTTON
                            EndIf

                            @ aPosObj[1][1]+27,aPosGet[1,1] SAY OemToAnsi(STR0061) OF oDlg PIXEL SIZE 030,006 // "Moeda"
                            @ aPosObj[1][1]+26,aPosGet[2,2] MSGET oGetMoeda VAR nMoedaCot PICTURE PesqPict("SC8","C8_MOEDA") ;
                                VALID M->nMoedaCot <= MoedFin() .And. M->nMoedaCot <> 0 .And. A150DescMoed(nMoedaCot,@oDescMoed,@cDescMoed,oGetTaxa) ;
                                .And. Eval({|| IIf(oGetMoeda:lModified,nTxMoeda:=If(cPaisLoc=="BRA" .Or. GetNewPar("MV_MDLOCCT",.F.),RecMoeda(dDataBase,M->nMoedaCot),A150TxMoeda(nMoedaCot,oGetTaxa)),Nil),.T.} );
                                .And. A150RodOk(oFolder,aObj,(cPaisLoc!="BRA"));
                                WHEN !l150Visual .And. VisualSX3("C8_MOEDA") PIXEL SIZE 30,06 OF oDlg

                            @ aPosObj[1][1]+26,aPosGet[2,7] MSGET oDescMoed VAR cDescMoed WHEN .F. OF oDlg PIXEL SIZE 055,006
                            @ aPosObj[1][1]+27,aPosGet[2,3] SAY OemToAnsi(STR0062) OF oDlg PIXEL SIZE 030,006 // "Taxa da Moeda"
                            @ aPosObj[1][1]+26,aposget[1,4] MSGET oGetTaxa VAR nTxMoeda OF oDlg PICTURE PesqPict("SC8","C8_TXMOEDA",11) F3 CpoRetF3('C8_TXMOEDA');
                                WHEN !l150Visual .And. VisualSX3('C8_TXMOEDA') Valid CheckSX3('C8_TXMOEDA',nMoedaCot) ;
                                PIXEL SIZE 060,006
                            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                            //³Getdados                                                      ³
                            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                            oGetDados := MSGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcX,'A150LinOk','A150TudOk(,'+Str(nOpcX,1)+')','+C8_ITEM',.T.,,,,nLinMax,'A150FieldOk',,,'A150Del')

							If lTrbGen
								oGetDados:oBrowse:bChange := {|| Eval(bTgRefresh) }
							EndIf

                            oFolder := TFolder():New(aPosObj[3,1],aPosObj[3,2],aTitles,aPages,oDlg,,,, .T., .F.,aPosObj[3,4]-aPosObj[3,2],aPosObj[3,3]-aPosObj[3,1],)

							If lTrbGen
								oFolder:bSetOption := {|nDst| Iif(nDst == 7, Eval(bTgRefresh),.T.)} 
							EndIf
                            //acerto no folder para nao perder o foco
                            For nX := 1 to Len(oFolder:aDialogs)
                                DEFINE SBUTTON FROM 5000,5000 TYPE 5 ACTION Allwaystrue() ENABLE OF oFolder:aDialogs[nX]
                            Next nX
                            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                            //³Folder de totais                                              ³
                            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                            oFolder:aDialogs[1]:oFont := oDlg:oFont
                            aadd(aObj,Array(7))
                            @ 6,aPosGet[3,1] SAY OemToAnsi(STR0028) Of oFolder:aDialogs[1] PIXEL SIZE 55 ,9 // "Valor da Mercadoria"
                            @ 5,aPosGet[3,2] MSGET aObj[1][1] VAR a150Var[VALMERC] Picture PesqPict('SC8','C8_TOTAL',GetSx3Cache("C8_TOTAL","X3_TAMANHO"),nMoedaCot) OF oFolder:aDialogs[1] PIXEL When .F. SIZE 80 ,9
                            @ 6,aPosGet[3,3] SAY OemToAnsi(STR0029) Of oFolder:aDialogs[1] PIXEL SIZE 49 ,9 // "Descontos"
                            @ 5,aPosGet[3,4] MSGET aObj[1][2] VAR a150Var[VALDESC]  Picture PesqPict('SC8','C8_VLDESC',GetSx3Cache("C8_VLDESC","X3_TAMANHO"),nMoedaCot) OF oFolder:aDialogs[1] PIXEL When .F. SIZE 80 ,9
                            If cPaisLoc == "BRA"
                                @ 20,aPosGet[3,1] SAY OemToAnsi(STR0030) Of oFolder:aDialogs[1] PIXEL SIZE 31 ,9 // "Valor do IPI"
                                @ 19,aPosGet[3,2]  MSGET aObj[1][3] VAR a150Var[VALIPI]  Picture PesqPict('SC8','C8_VALIPI',14,nMoedaCot) OF oFolder:aDialogs[1] PIXEL When .F. SIZE 80 ,9
                                @ 20,aPosGet[3,3] SAY OemToAnsi(STR0031) Of oFolder:aDialogs[1] PIXEL SIZE 50 ,9 // "Valor do ICMS"
                                @ 19,aPosGet[3,4] MSGET aObj[1][4] VAR a150Var[VALICM]  Picture PesqPict('SC8','C8_VALICM',14,nMoedaCot) OF oFolder:aDialogs[1] PIXEL When .F. SIZE 80 ,9
                            EndIf
                            @ 34,aPosGet[3,1]   SAY OemToAnsi(STR0032) Of oFolder:aDialogs[1] PIXEL SIZE 50 ,9  // "Frete+Despesas"
                            @ 33,aPosGet[3,2]  MSGET aObj[1][5] VAR a150Var[TOTF3]  Picture PesqPict('SC8','C8_VALFRE',14,nMoedaCot) OF oFolder:aDialogs[1] PIXEL When .F.  SIZE 80 ,9
                            If cPaisLoc == "BRA"
                                @ 34,aPosGet[3,3] SAY OemToAnsi(STR0033) Of oFolder:aDialogs[1] PIXEL SIZE 50 ,9 // "Valor ICMS Ret."
                                @ 33,aPosGet[3,4] MSGET aObj[1][6] VAR a150Var[VALSOL]  Picture PesqPict('SC8','C8_TOTAL',GetSx3Cache("C8_TOTAL","X3_TAMANHO"),nMoedaCot) OF oFolder:aDialogs[1] PIXEL When .F. SIZE 80 ,9
                            EndIf
                            @ 51,aPosGet[3,3] SAY OemToAnsi(STR0034) Of oFolder:aDialogs[1] PIXEL SIZE 58 ,9 // "Total do Pedido"
                            @ 49,aPosGet[3,4] MSGET aObj[1][7] VAR a150Var[TOTPED]  Picture PesqPict('SC8','C8_TOTAL',GetSx3Cache("C8_TOTAL","X3_TAMANHO"),nMoedaCot) OF oFolder:aDialogs[1] PIXEL When .F. SIZE 80 ,9
                            @ 43 ,3   TO 46 ,aPosGet[3,5] LABEL '' OF oFolder:aDialogs[1] PIXEL
                            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                            //³Dados do Fornecedor                                           ³
                            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                            oFolder:aDialogs[2]:oFont := oDlg:oFont
                            aadd(aObj,Array(7))
                            @ 6  ,aPosGet[4,1]  SAY OemToAnsi(STR0035) Of oFolder:aDialogs[2] PIXEL SIZE 37 ,9 // "Nome"
                            @ 5  ,aPosGet[4,2]  MSGET aObj[2][1] VAR aInfForn[1] Picture PesqPict('SA2','A2_NOME');
                                When .F. OF oFolder:aDialogs[2] PIXEL SIZE 159,9
                            If(lLGPD,OfuscaLGPD(aObj[2][1],"A2_NOME"),.F.)
                            
                            @ 6  ,aPosGet[4,3] SAY OemToAnsi(STR0036) Of oFolder:aDialogs[2] PIXEL SIZE 23 ,9 // "Tel."
                            @ 5  ,aPosGet[4,4] MSGET aObj[2][2] VAR aInfForn[2];
                                When .F. OF oFolder:aDialogs[2] PIXEL SIZE 74 ,9
                            If(lLGPD,OfuscaLGPD(aObj[2][2],"A2_TEL"),.F.)	
                                
                            @ 43 ,aPosGet[5,1]  SAY OemToAnsi(STR0037) Of oFolder:aDialogs[2] PIXEL SIZE 32 ,9 // "1a Compra"
                            @ 42 ,aPosGet[5,2]  MSGET aObj[2][3] VAR aInfForn[3] Picture PesqPict('SA2','A2_PRICOM') ;
                                When .F. OF oFolder:aDialogs[2] PIXEL SIZE 40 ,9
                                
                            @ 43 ,aPosGet[5,3] SAY OemToAnsi(STR0038) Of oFolder:aDialogs[2] PIXEL SIZE 36 ,9 // "Ult. Compra"
                            @ 42 ,aPosGet[5,4] MSGET aObj[2][4] VAR aInfForn[4] Picture PesqPict('SA2','A2_ULTCOM');
                                When .F. OF oFolder:aDialogs[2] PIXEL SIZE 40 ,9
                                
                            @ 43 ,aPosGet[5,5] SAY RetTitle("A2_CGC") Of oFolder:aDialogs[2] PIXEL SIZE 50 ,9 // "cgc"
                            @ 42 ,aPosGet[5,6] MSGET aObj[2][7] VAR aInfForn[7] Picture PesqPict('SA2','A2_CGC');
                                When .F. OF oFolder:aDialogs[2] PIXEL SIZE 76 ,9
                            If(lLGPD,OfuscaLGPD(aObj[2][7],"A2_CGC"),.F.)
                                
                            @ 24 ,aPosGet[6,1]   SAY OemToAnsi(STR0039) Of oFolder:aDialogs[2] PIXEL SIZE 49 ,9 // "Endereco"
                            @ 23 ,aPosGet[6,2] MSGET aObj[2][5] VAR aInfForn[5]  Picture PesqPict('SA2','A2_END');
                                When .F. OF oFolder:aDialogs[2] PIXEL SIZE 205,9
                            If(lLGPD,OfuscaLGPD(aObj[2][5],"A2_END"),.F.)
                                
                            @ 24 ,aPosGet[6,3] SAY OemToAnsi(STR0040) Of oFolder:aDialogs[2] PIXEL SIZE 60 ,9 // "Estado"
                            @ 23 ,aPosGet[6,4] MSGET aObj[2][6] VAR aInfForn[6]  Picture PesqPict('SA2','A2_EST');
                                When .F. OF oFolder:aDialogs[2] PIXEL SIZE 21 ,9
                            If(lLGPD,OfuscaLGPD(aObj[2][6],"A2_EST"),.F.)	
                                
                            @42 ,aPosGet[6,5] BUTTON OemToAnsi(STR0041) SIZE 30 ,10  FONT oDlg:oFont ACTION A1502FC030()  OF oFolder:aDialogs[2] PIXEL // "Mais Inf."
                            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                            //³Folder de Frete e despesas acessorias                         ³
                            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                            oFolder:aDialogs[3]:oFont := oDlg:oFont
                            aadd(aObj,Array(5))
                            @ 09,aPosGet[7,1] SAY OemToAnsi(STR0042) Of oFolder:aDialogs[3] PIXEL SIZE 35 ,9 // "Tp. Frete"
                            @ 08,aPosGet[7,2] MSCOMBOBOX aObj[3][1] VAR c150Frete ITEMS aCombo ON CHANGE A150VldCombo(c150Frete,@a150Var) .And. A150AtuRef("NF_FRETE",a150Var[FRETE]) WHEN !l150Visual SIZE 78 ,28 OF oFolder:aDialogs[3] PIXEL
                            @ 09,aPosGet[7,3] SAY OemToAnsi(STR0043) Of oFolder:aDialogs[3] PIXEL SIZE 35 ,9 //"Valor do Frete"
                            @ 08,aPosGet[7,4] MSGET aObj[3][2] VAR a150Var[FRETE]  Picture PesqPict('SC8','C8_VALFRE',14,nMoedaCot) OF oFolder:aDialogs[3] PIXEL WHEN !l150Visual .And. c150Frete=="C-CIF" VALID Positivo() .AND. A150AtuRef("NF_FRETE",a150Var[FRETE]) SIZE 80,9
                            @ 26,aPosGet[7,1] SAY OemToAnsi(STR0044) Of oFolder:aDialogs[3] PIXEL SIZE 42 ,9 // "Despesas"
                            @ 25,aPosGet[7,2] MSGET aObj[3][3] VAR a150Var[VALDESP] Picture PesqPict('SC8','C8_VALFRE',14,nMoedaCot) OF oFolder:aDialogs[3] PIXEL WHEN !l150Visual VALID Positivo() .AND. A150AtuRef("NF_DESPESA",a150Var[VALDESP]) SIZE 80,9
                            @ 26,aPosGet[7,3] SAY OemToAnsi(STR0045) Of oFolder:aDialogs[3] PIXEL SIZE 35 ,9 // "Seguro"
                            @ 25,aPosGet[7,4] MSGET aObj[3][4] VAR a150Var[SEGURO]  Picture PesqPict('SC8','C8_VALFRE',14,nMoedaCot) OF oFolder:aDialogs[3] PIXEL WHEN !l150Visual VALID Positivo() .AND. A150AtuRef("NF_SEGURO",a150Var[SEGURO]) SIZE 80,9
                            @ 38,11  TO 40 ,aPosGet[8,1] LABEL '' OF oFolder:aDialogs[3] PIXEL
                            @ 48,aPosGet[7,5] SAY OemToAnsi(STR0046) Of oFolder:aDialogs[3] PIXEL SIZE 65 ,09 // "Total ( Frete+Despesas)"
                            @ 47,aPosGet[8,3] MSGET aObj[3][5] VAR a150Var[TOTF3]  Picture PesqPict('SC8','C8_VALFRE',14,nMoedaCot) OF oFolder:aDialogs[3] PIXEL WHEN .F. SIZE 80,9
                            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                            //³Folder de Descontos                                           ³
                            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                            oFolder:aDialogs[4]:oFont := oDlg:oFont
                            aadd(aObj,Array(5))
                            @ 9 ,aPosGet[9,1]  SAY OemToAnsi(STR0047) Of oFolder:aDialogs[4] PIXEL SIZE 40 ,9 // "Desconto 1"
                            @ 8 ,aPosGet[9,2]  MSGET aObj[4][1] VAR nDesc1  Picture PesqPict('SC8','C8_DESC1',6,nMoedaCot) OF oFolder:aDialogs[4] PIXEL WHEN !l150Visual VALID Positivo() .AND. A150VDesc(@nDesc1,@nDesc2,@nDesc3,@a150Var) SIZE 30 ,9
                            @ 9 ,aPosGet[9,3]  SAY '%' Of oFolder:aDialogs[4] PIXEL SIZE 11 ,9
                            @ 9 ,aPosGet[9,4] SAY OemToAnsi(STR0048) Of oFolder:aDialogs[4] PIXEL SIZE 36 ,9 // "Desconto 2"
                            @ 8 ,aPosGet[9,5] MSGET aObj[4][2] VAR nDesc2  Picture PesqPict('SC8','C8_DESC2',6,nMoedaCot) OF oFolder:aDialogs[4] PIXEL WHEN !l150Visual VALID Positivo() .AND. A150VDesc(@nDesc1,@nDesc2,@nDesc3,@a150Var) SIZE 30 ,9
                            @ 9 ,aPosGet[9,6] SAY '%' Of oFolder:aDialogs[4] PIXEL SIZE 9  ,9
                            @ 9 ,aPosGet[9,7] SAY OemToAnsi(STR0049) Of oFolder:aDialogs[4] PIXEL SIZE 42 ,9 // "Desconto 3"
                            @ 8 ,aPosGet[9,8] MSGET aObj[4][3] VAR nDesc3  Picture PesqPict('SC8','C8_DESC3',6,nMoedaCot) OF oFolder:aDialogs[4] PIXEL WHEN !l150Visual VALID Positivo() .AND. A150VDesc(@nDesc1,@nDesc2,@nDesc3,@a150Var) SIZE 30 ,9
                            @ 9 ,aPosGet[9,9] SAY '%' Of oFolder:aDialogs[4] PIXEL SIZE 12 ,9
                            @ 27 ,aPosGet[10,1] SAY OemToAnsi(STR0050) Of oFolder:aDialogs[4] PIXEL SIZE 48 ,9 //"Valor do Desconto"
                            @ 26 ,aPosGet[10,2] MSGET aObj[4][4] VAR a150Var[VALDESC]  Picture PesqPict('SC8','C8_VLDESC',GetSx3Cache("C8_VLDESC","X3_TAMANHO"),nMoedaCot) OF oFolder:aDialogs[4] PIXEL When !l150Visual .And.(nDesc1+nDesc2+nDesc3==0) VALID Positivo() .AND. A150AtuRef("NF_DESCONTO",a150Var[VALDESC]) SIZE 75 ,9
                            @ 49 ,aPosGet[10,3] SAY OemToAnsi(STR0051) Of oFolder:aDialogs[4] PIXEL SIZE 43 ,9 // "Total do Pedido"
                            @ 48 ,aPosGet[10,4] MSGET aObj[4][5] VAR a150Var[TOTPED]  Picture PesqPict('SC8','C8_TOTAL',GetSx3Cache("C8_TOTAL","X3_TAMANHO"),nMoedaCot) OF oFolder:aDialogs[4] PIXEL When .F. SIZE 75 ,9
                            @ 38 ,5   TO 40 ,aPosGet[10,5] LABEL '' OF oFolder:aDialogs[4] PIXEL
                            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                            //³Folder de Resumo dos impostos                                 ³
                            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                            oFolder:aDialogs[5]:oFont := oDlg:oFont
                            aadd(aObj,Array(1))
                            aObj[5][1] := MaFisRodape(mv_par05,oFolder:aDialogs[5],,{5,3,aPosGet[10,6],53},bFisToaCols,l150Visual)
                            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                            //³Folder de Formulas de reajuste e mensagem                     ³
                            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                            oFolder:aDialogs[6]:oFont := oDlg:oFont
                            @ 5  ,aPosGet[11,1] TO 55 ,aPosGet[11,2] LABEL OemToAnsi(STR0052) OF oFolder:aDialogs[6] PIXEL //"Reajuste"
                            @ 5  ,3   TO 55 ,aPosGet[11,3] LABEL OemToAnsi(STR0053) OF oFolder:aDialogs[6] PIXEL // "Mensagem"
                            @ 15 ,aPosGet[12,1]  SAY OemToAnsi(STR0054) Of oFolder:aDialogs[6] PIXEL SIZE 40 ,9 // "Cod. Formula"
                            @ 14 ,aPosGet[12,2]  MSGET cMsg Picture PesqPict('SC8','C8_MSG') F3 CpoRetF3('C8_MSG');
                                When !l150Visual .And.VisualSX3('C8_MSG') ;
                                Valid CheckSX3('C8_MSG',cMsg) .And. A120FormDesc(cMsg,@cDescMsg) .And.;
                                Eval(bFolderRefresh) ;
                                OF oFolder:aDialogs[6] PIXEL SIZE 23 ,9
                            @ 14 ,aPosGet[12,3] MSGET cReajuste Picture PesqPict('SC8','C8_REAJUST') F3 CpoRetF3('C8_REAJUST');
                                When !l150Visual .And.VisualSX3('C8_REAJUST') ;
                                Valid CheckSX3('C8_REAJUST',cReajuste) .And. A120FormReaj(cReajuste,@cDescFor) .And.;
                                Eval(bFolderRefresh) ;
                                OF oFolder:aDialogs[6] PIXEL SIZE 23 ,9
                            @ 15 ,aPosGet[12,4] SAY OemToAnsi(STR0054) Of oFolder:aDialogs[6] PIXEL SIZE 40 ,9 //"Cod. Formula"
                            @ 32 ,aPosGet[12,5] MSGET cDescMsg  Picture "@!" OF oFolder:aDialogs[6] When .F. PIXEL SIZE 124,9
                            @ 31 ,aPosGet[12,6] MSGET cDescFor  Picture "@!" OF oFolder:aDialogs[6] When .F. PIXEL SIZE 140,9

							// Criacao do Folder de tributos genericos por item
							If lTrbGen
								oFolder:aDialogs[7]:oFont := oDlg:oFont
								oFisTrbGen := MaFisBrwTG(oFolder:aDialogs[7],{5,4,( aPosObj[3,4]-aPosObj[3,2] ) - 10,53}, l150Visual)		
							EndIf

                            If lMt150Scr
                                ExecBlock("MT150SCR",.F.,.F.,@oDlg)
                            EndIf

                            ACTIVATE MSDIALOG oDlg ON INIT ;
                                EnchoiceBar(oDlg,{||If(oGetDados:TudoOk().And.A150CabOk(nOpcX,aImpRef,aInfForn,@cDescMsg,@cDescFor,@a150Var,.T.).And.A150OK(nOpcX,lMT150OK),(nOpc:=1,oDlg:End()),nOpc:=0)},{||nOpc:=0,oDlg:End()},,aButtons)
                        EndIf
                    Else
                        nOpc	:= 1
                        If !l150Deleta
                            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                            //³ Execucao da rotina automatica                               ³
                            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                            aValidGet := {}
                            If l150Inclui
                                AADD(aValidGet,{"cA150Forn"  ,aAutoCab[ProcH("C8_FORNECE"),2],"A150Forn(cA150Forn,@cA150Loj,l150Inclui) .And. CheckSX3('C8_FORNECE',cA150Forn).And. A150AtuRef('NF_CODCLIFOR',cA150Forn)" + If(cPaisLoc == "ARG"," .And. a150ProEnt()",""),.T.})
                                AADD(aValidGet,{"cA150Loj"   ,aAutoCab[ProcH("C8_LOJA"   ),2],"A150Forn(cA150Forn,@cA150Loj,l150Inclui) .And. CheckSX3('C8_LOJA',cA150Loj) .And. A150AtuRef('NF_LOJA',cA150Loj)",.T.})
                            EndIf
                            If cPaisLoc == "ARG" .And. ProcH("C8_PROVENT")<>0
                                AADD(aValidGet,{"cA150ProvEnt" ,aAutoCab[ProcH("C8_PROVENT" ),2],"ProvEntCt()",.T.})
                            EndIf
                            AADD(aValidGet,{"cCondicao"  ,aAutoCab[ProcH("C8_COND"   ),2],"NaoVazio() .And. CheckSX3('C8_COND',cCondicao)",.T.})
                            AADD(aValidGet,{"cContato"   ,aAutoCab[ProcH("C8_CONTATO"),2],"CheckSX3('C8_CONTATO',cContato)",.F.})
                            
                            If ProcH("C8_MOEDA")<>0
                                AADD(aValidGet,{"nMoedaCot"   ,aAutoCab[ProcH("C8_MOEDA"),2],"CheckSX3('C8_MOEDA',nMoedaCot)",.T.})
                            EndIf

                            If ProcH("C8_TXMOEDA")<>0
                                AADD(aValidGet,{"nTxMoeda"   ,aAutoCab[ProcH("C8_TXMOEDA"),2],"CheckSX3('C8_TXMOEDA',nMoedaCot)",.F.})
                            EndIf

                            If nOpc == 1 .And. !SC8->(MsVldGAuto(aValidGet))
                                nOpc := 0
                            EndIf
                            
                            If l150auto
                                If ProcH("C8_MOEDA")<>0 .And. ProcH("C8_TXMOEDA")==0
                                    nTxMoeda := RecMoeda(dDataBase,nMoedaCot)
                                Endif
                            Endif

                            If nOpc == 1 .And. !MsGetDAuto(aAutoItens,"A150LinOk",{|| A150TudOk(,nOpcX).And.A150CabOk(nOpcX,aImpRef,aInfForn,@cDescMsg,@cDescFor,@a150Var)},aAutoCab)
                                nOpc := 0
                            EndIf

                            //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                            //³Valida os Gets referentes ao rodape³
                            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                            aValidGet := {}
                            If ProcH("C8_TPFRETE") > 0
                                AADD(aValidGet,{"c150frete" ,aAutoCab[ProcH("C8_TPFRETE" ),2],  "A150VldCombo(c150Frete,@a150Var) .And. A150AtuRef('NF_FRETE',a150Var[5])",.T.})
                            EndIf
                            If ProcH("C8_TOTFRE" ) > 0
                                AADD(aValidGet,{"a150var[5]" ,aAutoCab[ProcH("C8_TOTFRE" ),2],  "A150AtuRef('NF_FRETE',a150var[5])",.T.})
                            EndIf
                            If ProcH("C8_VLDESC" ) > 0
                                AADD(aValidGet,{"a150var[2]" ,aAutoCab[ProcH("C8_VLDESC" ),2],"A150AtuRef('NF_DESCONTO',a150var[2])",.T.})
                            EndIf
                            If ProcH("C8_DESPESA" ) > 0
                                AADD(aValidGet,{"a150var[6]" ,aAutoCab[ProcH("C8_DESPESA" ),2],"A150AtuRef('NF_DESPESA',a150var[6])",.T.})
                            EndIf
                            If ProcH("C8_SEGURO" ) > 0
                                AADD(aValidGet,{"a150var[14]" ,aAutoCab[ProcH("C8_SEGURO" ),2],"A150AtuRef('NF_SEGURO',a150var[14])",.T.})
                            EndIf
                            If ProcH("C8_DESC1") > 0
                                AADD(aValidGet,{"nDesc1"     ,aAutoCab[ProcH("C8_DESC1"  ),2],  "A150VDesc(@nDesc1,@nDesc2,@nDesc3,@a150var)",.T.})
                            EndIf
                            If ProcH("C8_DESC2") > 0
                                AADD(aValidGet,{"nDesc2"     ,aAutoCab[ProcH("C8_DESC2"  ),2],  "A150VDesc(@nDesc1,@nDesc2,@nDesc3,@a150var)",.T.})
                            EndIf
                            If ProcH("C8_DESC3") > 0
                                AADD(aValidGet,{"nDesc3",aAutoCab[ProcH("C8_DESC3"  ),2],  "A150VDesc(@nDesc1,@nDesc2,@nDesc3,@a150var)",.T.})
                            EndIf
                            If ProcH("C8_MSG") > 0
                                AADD(aValidGet,{"cMsg" ,aAutoCab[ProcH("C8_MSG" ),2],"CheckSX3('C8_MSG',cMsg) .And. (Vazio() .Or. ExistCpo('SM4'))",.T.})
                            EndIf
                            If ProcH("C8_REAJUST") > 0
                                AADD(aValidGet,{"cReajuste"  ,aAutoCab[ProcH("C8_REAJUST"),2],"CheckSX3('C8_REAJUST',cReajuste) .And. (Vazio() .Or. ExistCpo('SM4'))",.T.})
                            EndIf
                            If nOpc == 1 .And. !SC8->(MsVldGAuto(aValidGet))
                                nOpc := 0
                            EndIf
                        EndIf
                    EndIf
                Else
                    If ( !lValido)
                        If lAnalisada
                            Help(NIL, NIL,STR0059, NIL, STR0104, 1, 0) //"Analisada"#"Cotação já foi analisada"#""					
                        EndIf
                    EndIf
                EndIf

				If type("aImpVal") == "A" .and. lRetImp//Retorna somente os valores dos impostos.
					nPosTes := aScan(aHeader,{|x| AllTrim(x[2])=="C8_TES"})
					nPosC8CF := aScan(aHeader,{|x| AllTrim(x[2])=="C8_CF"})

					For nLoop := 1 to Len( aCols )

						//Load e carregamentos dos impostos
						MaFisLoad("IT_TES","", nLoop)
						MaFisAlt("IT_TES", aCols[nLoop][nPosTes], nLoop)

						if (lExsC8CF .and. lTrbGen) //Se existir o campo de CFOP, calculo os valores genéricos
							MaFisLoad("IT_CF", "", nLoop)
							MaFisAlt("IT_CF" , aCols[nLoop, nPosC8CF], nLoop)
						endif

						//Verifico se existe ID de Tributo Genérico. Se sim e está preenchido, passo para finalizar o regitro.
						if (lTrbGen)
							nPosIDTrb := aScan(aHeader,{|x| AllTrim(x[2])=="C8_IDTRIB"})
							cNumIDTrb := aCols[nLoop][nPosIDTrb]

							if ( lF2DSave .And. !empty(cNumIDTrb) )
								MaFisTG(2,,,cNumIDTrb)
							endif
						endif

						//Conteudo, Campo, Referência Fiscal/Valor
						If cPaisLoc == "BRA"
							aAdd(aImpItem,{"difal"	    ,"C8_DIFAL"     ,MaFisRet(nLoop,"IT_DIFAL"  )})
							aAdd(aImpItem,{"difalpori"  ,"C8_PDORI"     ,MaFisRet(nLoop,"IT_PDORI"  )})
							aAdd(aImpItem,{"difalpdes"  ,"C8_PDDES"     ,MaFisRet(nLoop,"IT_PDDES"  )})
							aAdd(aImpItem,{"ipi"		,"C8_VALIPI"	,MaFisRet(nLoop,"IT_VALIPI")})
							aAdd(aImpItem,{"icms"		,"C8_VALICM"	,MaFisRet(nLoop,"IT_VALICM")})
							aAdd(aImpItem,{"iss"		,"C8_VALISS"	,MaFisRet(nLoop,"IT_VALISS")})
							aAdd(aImpItem,{"icmscomp"	,"C8_ICMSCOM"	,MaFisRet(nLoop,"IT_VALCMP")})
							aAdd(aImpItem,{"icmsst"		,"C8_VALSOL"	,MaFisRet(nLoop,"IT_VALSOL")})
							
							aAdd(aImpItem,{"aliqipi"	 ,"C8_ALIIPI"	,MaFisRet(nLoop,"IT_ALIQIPI")})
							aAdd(aImpItem,{"aliqicms"	 ,"C8_PICM"		,MaFisRet(nLoop,"IT_ALIQICM")})
							aAdd(aImpItem,{"aliqiss"	 ,"C8_ALIQISS"	,MaFisRet(nLoop,"IT_ALIQISS")})
							aAdd(aImpItem,{"aliqicmscomp","C8_ALIQCMP"	,MaFisRet(nLoop,"IT_ALIQCMP")})
							
							aAdd(aImpItem,{"difalbase"   ,"C8_BASEDES"  ,MaFisRet(nLoop,"IT_BASEDES")})
							aAdd(aImpItem,{"ipibase"     ,"C8_BASEIPI"  ,MaFisRet(nLoop,"IT_BASEIPI")})
							aAdd(aImpItem,{"icmsbase"    ,"C8_BASEICM"  ,MaFisRet(nLoop,"IT_BASEICM")})
							aAdd(aImpItem,{"icmssolbase" ,"C8_BASESOL"  ,MaFisRet(nLoop,"IT_BASESOL")})
							if ( lF2DSave )
								aAdd(aImpItem,{"idgenerico"  ,"C8_IDTRIB"   ,MaFisTG(1, "SC8", nLoop)})
							endif

							//Aciona a classe de tributos genéricos do compras
							if lExsObjGen .AND. lGenTaxes
								cRetGentx := oGenTax:getallRules(nLoop)
								oJsonTax := JsonObject():New()
								oJsonTax:FromJson(cRetGentx)
								aAdd(aRetGenTx, oJsonTax:GetJsonObject("dados_itens"))
							endif
						Else
							MaFisLoad("IT_TES","",nLoop)
							MaFisAlt("IT_TES",aCols[nLoop][nPosTes],nLoop)
							
							aAdd(aImpItem,{"BASIMP1" 	 ,"C8_BASIMP1"  ,MaFisRet(nLoop,"IT_BASEIV1")})						
							aAdd(aImpItem,{"BASIMP2" 	 ,"C8_BASIMP2"  ,MaFisRet(nLoop,"IT_BASEIV2")})												
							aAdd(aImpItem,{"BASIMP3" 	 ,"C8_BASIMP3"  ,MaFisRet(nLoop,"IT_BASEIV3")})												
							aAdd(aImpItem,{"BASIMP4" 	 ,"C8_BASIMP4"  ,MaFisRet(nLoop,"IT_BASEIV4")})						
							aAdd(aImpItem,{"BASIMP5" 	 ,"C8_BASIMP5"  ,MaFisRet(nLoop,"IT_BASEIV5")})						
							aAdd(aImpItem,{"BASIMP6" 	 ,"C8_BASIMP6"  ,MaFisRet(nLoop,"IT_BASEIV6")})	
							aAdd(aImpItem,{"BASIMP7" 	 ,"C8_BASIMP7"  ,MaFisRet(nLoop,"IT_BASEIV7")})	
							aAdd(aImpItem,{"BASIMP8" 	 ,"C8_BASIMP8"  ,MaFisRet(nLoop,"IT_BASEIV8")})	
							aAdd(aImpItem,{"BASIMP9" 	 ,"C8_BASIMP9"  ,MaFisRet(nLoop,"IT_BASEIV9")})	
							aAdd(aImpItem,{"BASIMPA" 	 ,"C8_BASIMPA"  ,MaFisRet(nLoop,"IT_BASEIVA")})	
							aAdd(aImpItem,{"BASIMPB" 	 ,"C8_BASIMPB"  ,MaFisRet(nLoop,"IT_BASEIVB")})	
							aAdd(aImpItem,{"BASIMPC" 	 ,"C8_BASIMPC"  ,MaFisRet(nLoop,"IT_BASEIVC")})	
							aAdd(aImpItem,{"BASIMPD" 	 ,"C8_BASIMPD"  ,MaFisRet(nLoop,"IT_BASEIVD")})	
							aAdd(aImpItem,{"BASIMPE" 	 ,"C8_BASIMPE"  ,MaFisRet(nLoop,"IT_BASEIVE")})	
							aAdd(aImpItem,{"BASIMPF" 	 ,"C8_BASIMPF"  ,MaFisRet(nLoop,"IT_BASEIVF")})	
							aAdd(aImpItem,{"BASIMPG" 	 ,"C8_BASIMPG"  ,MaFisRet(nLoop,"IT_BASEIVG")})	
							aAdd(aImpItem,{"BASIMPH" 	 ,"C8_BASIMPH"  ,MaFisRet(nLoop,"IT_BASEIVH")})	
							aAdd(aImpItem,{"BASIMPI" 	 ,"C8_BASIMPI"  ,MaFisRet(nLoop,"IT_BASEIVI")})	
							aAdd(aImpItem,{"BASIMPJ" 	 ,"C8_BASIMPJ"  ,MaFisRet(nLoop,"IT_BASEIVJ")})	
							aAdd(aImpItem,{"BASIMPK" 	 ,"C8_BASIMPK"  ,MaFisRet(nLoop,"IT_BASEIVK")})	
							aAdd(aImpItem,{"BASIMPL" 	 ,"C8_BASIMPL"  ,MaFisRet(nLoop,"IT_BASEIVL")})	
							aAdd(aImpItem,{"BASIMPM" 	 ,"C8_BASIMPM"  ,MaFisRet(nLoop,"IT_BASEIVM")})	
							aAdd(aImpItem,{"BASIMPN" 	 ,"C8_BASIMPN"  ,MaFisRet(nLoop,"IT_BASEIVN")})	
							aAdd(aImpItem,{"BASIMPO" 	 ,"C8_BASIMPO"  ,MaFisRet(nLoop,"IT_BASEIVO")})	
							aAdd(aImpItem,{"BASIMPP" 	 ,"C8_BASIMPP"  ,MaFisRet(nLoop,"IT_BASEIVP")})	
							aAdd(aImpItem,{"BASIMPQ" 	 ,"C8_BASIMPQ"  ,MaFisRet(nLoop,"IT_BASEIVQ")})	
							aAdd(aImpItem,{"BASIMPR" 	 ,"C8_BASIMPR"  ,MaFisRet(nLoop,"IT_BASEIVR")})	
							aAdd(aImpItem,{"BASIMPS" 	 ,"C8_BASIMPS"  ,MaFisRet(nLoop,"IT_BASEIVS")})	
							aAdd(aImpItem,{"BASIMPT" 	 ,"C8_BASIMPT"  ,MaFisRet(nLoop,"IT_BASEIVT")})	
							aAdd(aImpItem,{"BASIMPU" 	 ,"C8_BASIMPU"  ,MaFisRet(nLoop,"IT_BASEIVU")})	
							aAdd(aImpItem,{"BASIMPV" 	 ,"C8_BASIMPV"  ,MaFisRet(nLoop,"IT_BASEIVV")})	
							aAdd(aImpItem,{"BASIMPW" 	 ,"C8_BASIMPW"  ,MaFisRet(nLoop,"IT_BASEIVW")})	
							aAdd(aImpItem,{"BASIMPX" 	 ,"C8_BASIMPX"  ,MaFisRet(nLoop,"IT_BASEIVX")})	
							aAdd(aImpItem,{"BASIMPY" 	 ,"C8_BASIMPY"  ,MaFisRet(nLoop,"IT_BASEIVY")})	
							aAdd(aImpItem,{"BASIMPZ" 	 ,"C8_BASIMPZ"  ,MaFisRet(nLoop,"IT_BASEIVZ")})	

							aAdd(aImpItem,{"aliqimp1" 	 ,"C8_ALQIMP1"  ,MaFisRet(nLoop,"IT_ALIQIV1")})
							aAdd(aImpItem,{"aliqimp2" 	 ,"C8_ALQIMP2"  ,MaFisRet(nLoop,"IT_ALIQIV2")})						
							aAdd(aImpItem,{"aliqimp3" 	 ,"C8_ALQIMP3"  ,MaFisRet(nLoop,"IT_ALIQIV3")})
							aAdd(aImpItem,{"aliqimp4" 	 ,"C8_ALQIMP4"  ,MaFisRet(nLoop,"IT_ALIQIV4")})
							aAdd(aImpItem,{"aliqimp5" 	 ,"C8_ALQIMP5"  ,MaFisRet(nLoop,"IT_ALIQIV5")})
							aAdd(aImpItem,{"aliqimp6" 	 ,"C8_ALQIMP6"  ,MaFisRet(nLoop,"IT_ALIQIV6")})
							aAdd(aImpItem,{"aliqimp6" 	 ,"C8_ALQIMP6"  ,MaFisRet(nLoop,"IT_ALIQIV6")})
							aAdd(aImpItem,{"aliqimp7" 	 ,"C8_ALQIMP7"  ,MaFisRet(nLoop,"IT_ALIQIV7")})
							aAdd(aImpItem,{"aliqimp8" 	 ,"C8_ALQIMP8"  ,MaFisRet(nLoop,"IT_ALIQIV8")})
							aAdd(aImpItem,{"aliqimp9" 	 ,"C8_ALQIMP9"  ,MaFisRet(nLoop,"IT_ALIQIV9")})
							aAdd(aImpItem,{"aliqimpA" 	 ,"C8_ALQIMPA"  ,MaFisRet(nLoop,"IT_ALIQIVA")})	
							aAdd(aImpItem,{"aliqimpB" 	 ,"C8_ALQIMPB"  ,MaFisRet(nLoop,"IT_ALIQIVB")})	
							aAdd(aImpItem,{"aliqimpC" 	 ,"C8_ALQIMPC"  ,MaFisRet(nLoop,"IT_ALIQIVC")})	
							aAdd(aImpItem,{"aliqimpD" 	 ,"C8_ALQIMPD"  ,MaFisRet(nLoop,"IT_ALIQIVD")})	
							aAdd(aImpItem,{"aliqimpE" 	 ,"C8_ALQIMPE"  ,MaFisRet(nLoop,"IT_ALIQIVE")})	
							aAdd(aImpItem,{"aliqimpF" 	 ,"C8_ALQIMPF"  ,MaFisRet(nLoop,"IT_ALIQIVF")})	
							aAdd(aImpItem,{"aliqimpG" 	 ,"C8_ALQIMPG"  ,MaFisRet(nLoop,"IT_ALIQIVG")})	
							aAdd(aImpItem,{"aliqimpH" 	 ,"C8_ALQIMPH"  ,MaFisRet(nLoop,"IT_ALIQIVH")})	
							aAdd(aImpItem,{"aliqimpI" 	 ,"C8_ALQIMPI"  ,MaFisRet(nLoop,"IT_ALIQIVI")})	
							aAdd(aImpItem,{"aliqimpJ" 	 ,"C8_ALQIMPJ"  ,MaFisRet(nLoop,"IT_ALIQIVJ")})	
							aAdd(aImpItem,{"aliqimpK" 	 ,"C8_ALQIMPK"  ,MaFisRet(nLoop,"IT_ALIQIVK")})	
							aAdd(aImpItem,{"aliqimpL" 	 ,"C8_ALQIMPL"  ,MaFisRet(nLoop,"IT_ALIQIVL")})	
							aAdd(aImpItem,{"aliqimpM" 	 ,"C8_ALQIMPM"  ,MaFisRet(nLoop,"IT_ALIQIVM")})	
							aAdd(aImpItem,{"aliqimpN" 	 ,"C8_ALQIMPN"  ,MaFisRet(nLoop,"IT_ALIQIVN")})	
							aAdd(aImpItem,{"aliqimpO" 	 ,"C8_ALQIMPO"  ,MaFisRet(nLoop,"IT_ALIQIVO")})	
							aAdd(aImpItem,{"aliqimpP" 	 ,"C8_ALQIMPP"  ,MaFisRet(nLoop,"IT_ALIQIVP")})	
							aAdd(aImpItem,{"aliqimpQ" 	 ,"C8_ALQIMPQ"  ,MaFisRet(nLoop,"IT_ALIQIVQ")})	
							aAdd(aImpItem,{"aliqimpR" 	 ,"C8_ALQIMPR"  ,MaFisRet(nLoop,"IT_ALIQIVR")})	
							aAdd(aImpItem,{"aliqimpS" 	 ,"C8_ALQIMPS"  ,MaFisRet(nLoop,"IT_ALIQIVS")})	
							aAdd(aImpItem,{"aliqimpT" 	 ,"C8_ALQIMPT"  ,MaFisRet(nLoop,"IT_ALIQIVT")})	
							aAdd(aImpItem,{"aliqimpU" 	 ,"C8_ALQIMPU"  ,MaFisRet(nLoop,"IT_ALIQIVU")})	
							aAdd(aImpItem,{"aliqimpV" 	 ,"C8_ALQIMPV"  ,MaFisRet(nLoop,"IT_ALIQIVV")})	
							aAdd(aImpItem,{"aliqimpW" 	 ,"C8_ALQIMPW"  ,MaFisRet(nLoop,"IT_ALIQIVW")})	
							aAdd(aImpItem,{"aliqimpX" 	 ,"C8_ALQIMPX"  ,MaFisRet(nLoop,"IT_ALIQIVX")})	
							aAdd(aImpItem,{"aliqimpY" 	 ,"C8_ALQIMPY"  ,MaFisRet(nLoop,"IT_ALIQIVY")})	
							aAdd(aImpItem,{"aliqimpZ" 	 ,"C8_ALQIMPZ"  ,MaFisRet(nLoop,"IT_ALIQIVZ")})	

							aAdd(aImpItem,{"valimp1"	,"C8_VALIMP1"	,MaFisRet(nLoop,"IT_VALIV1")})
							aAdd(aImpItem,{"valimp2"	,"C8_VALIMP2"	,MaFisRet(nLoop,"IT_VALIV2")})
							aAdd(aImpItem,{"valimp3"	,"C8_VALIMP3"	,MaFisRet(nLoop,"IT_VALIV3")})
							aAdd(aImpItem,{"valimp4"	,"C8_VALIMP4"	,MaFisRet(nLoop,"IT_VALIV4")})
							aAdd(aImpItem,{"valimp5"	,"C8_VALIMP5"	,MaFisRet(nLoop,"IT_VALIV5")})
							aAdd(aImpItem,{"valimp6"	,"C8_VALIMP6"	,MaFisRet(nLoop,"IT_VALIV6")})
							aAdd(aImpItem,{"valimp7"	,"C8_VALIMP7"	,MaFisRet(nLoop,"IT_VALIV7")})
							aAdd(aImpItem,{"valimp8"	,"C8_VALIMP8"	,MaFisRet(nLoop,"IT_VALIV8")})
							aAdd(aImpItem,{"valimp9"	,"C8_VALIMP9"	,MaFisRet(nLoop,"IT_VALIV9")})
							aAdd(aImpItem,{"valimpA" 	,"C8_VALIMPA"   ,MaFisRet(nLoop,"IT_VALIVA")})	
							aAdd(aImpItem,{"valimpB" 	,"C8_VALIMPB"   ,MaFisRet(nLoop,"IT_VALIVB")})	
							aAdd(aImpItem,{"valimpC" 	,"C8_VALIMPC"   ,MaFisRet(nLoop,"IT_VALIVC")})	
							aAdd(aImpItem,{"valimpD" 	,"C8_VALIMPD"   ,MaFisRet(nLoop,"IT_VALIVD")})	
							aAdd(aImpItem,{"valimpE" 	,"C8_VALIMPE"   ,MaFisRet(nLoop,"IT_VALIVE")})	
							aAdd(aImpItem,{"valimpF" 	,"C8_VALIMPF"   ,MaFisRet(nLoop,"IT_VALIVF")})	
							aAdd(aImpItem,{"valimpG" 	,"C8_VALIMPG"   ,MaFisRet(nLoop,"IT_VALIVG")})	
							aAdd(aImpItem,{"valimpH" 	,"C8_VALIMPH"   ,MaFisRet(nLoop,"IT_VALIVH")})	
							aAdd(aImpItem,{"valimpI" 	,"C8_VALIMPI"   ,MaFisRet(nLoop,"IT_VALIVI")})	
							aAdd(aImpItem,{"valimpJ" 	,"C8_VALIMPJ"   ,MaFisRet(nLoop,"IT_VALIVJ")})	
							aAdd(aImpItem,{"valimpK" 	,"C8_VALIMPK"   ,MaFisRet(nLoop,"IT_VALIVK")})	
							aAdd(aImpItem,{"valimpL" 	,"C8_VALIMPL"   ,MaFisRet(nLoop,"IT_VALIVL")})	
							aAdd(aImpItem,{"valimpM" 	,"C8_VALIMPM"   ,MaFisRet(nLoop,"IT_VALIVM")})	
							aAdd(aImpItem,{"valimpN" 	,"C8_VALIMPN"   ,MaFisRet(nLoop,"IT_VALIVN")})	
							aAdd(aImpItem,{"valimpO" 	,"C8_VALIMPO"   ,MaFisRet(nLoop,"IT_VALIVO")})	
							aAdd(aImpItem,{"valimpP" 	,"C8_VALIMPP"   ,MaFisRet(nLoop,"IT_VALIVP")})	
							aAdd(aImpItem,{"valimpQ" 	,"C8_VALIMPQ"   ,MaFisRet(nLoop,"IT_VALIVQ")})	
							aAdd(aImpItem,{"valimpR" 	,"C8_VALIMPR"   ,MaFisRet(nLoop,"IT_VALIVR")})	
							aAdd(aImpItem,{"valimpS" 	,"C8_VALIMPS"   ,MaFisRet(nLoop,"IT_VALIVS")})	
							aAdd(aImpItem,{"valimpT" 	,"C8_VALIMPT"   ,MaFisRet(nLoop,"IT_VALIVT")})	
							aAdd(aImpItem,{"valimpU" 	,"C8_VALIMPU"   ,MaFisRet(nLoop,"IT_VALIVU")})	
							aAdd(aImpItem,{"valimpV" 	,"C8_VALIMPV"   ,MaFisRet(nLoop,"IT_VALIVV")})	
							aAdd(aImpItem,{"valimpW" 	,"C8_VALIMPW"   ,MaFisRet(nLoop,"IT_VALIVW")})	
							aAdd(aImpItem,{"valimpX" 	,"C8_VALIMPX"   ,MaFisRet(nLoop,"IT_VALIVX")})	
							aAdd(aImpItem,{"valimpY" 	,"C8_VALIMPY"   ,MaFisRet(nLoop,"IT_VALIVY")})	
							aAdd(aImpItem,{"valimpZ" 	,"C8_VALIMPZ"   ,MaFisRet(nLoop,"IT_VALIVZ")})	

						Endif

						aAdd(aImpVal, {aCols[nLoop,gdFieldPos("C8_ITEM")], aClone(aImpItem)})
                        aImpItem := {}
					Next

					if ( lExsObjGen .And. lGenTaxes .And. len(aRetGenTx) > 0 )
						oTribProcGen := aClone(aRetGenTx)
					endif
				Endif

                If nOpc == 1 .and. !lRetImp
                    Begin Transaction
                        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                        //³ Efetua a gravacao do Pedido (Inclusao/Alteracao/Exclusao  ³
                        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                        If l150Inclui .Or. l150Altera .Or. l150Deleta
                            If Type("lOnUpDate") == "U" .Or. lOnUpdate
                                //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                                //³ Inicializa a gravacao atraves das funcoes MATXFIS         ³
                                //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                                If ( !A150Grava(nOpcX,aRecSC8,aSCs,bBlock) )
                                    lHelp := .T.
                                Else
                                    //Monta Array para envio de e-mail ao fornecedor adicionado.
                                    If SuperGetMV("MV_ENVCOT") .AND. !Empty(SC8->C8_FORMAIL) .AND. l150Inclui
                                        aAdd(aItens,allTrim(SC8->C8_PRODUTO))
                                        aAdd(aItens,SC8->C8_QUANT)
                                        aAdd(aItens,SC8->C8_PRAZO)
										aAdd(aItens,SC8->C8_IDENT)
										aAdd(aItens,SC8->C8_DATPRF)		

                                        aAdd(aForns,allTrim(SC8->C8_FORNECE))
                                        aAdd(aForns,allTrim(SC8->C8_LOJA))
                                        aAdd(aForns,allTrim(SC8->C8_FORMAIL))
                                        aAdd(aForns,{})
                                        aAdd(aForns[4],aItens)

                                        aAdd(aCots,{})
                                        aAdd(aCots[1],allTrim(SC8->C8_NUM))
                                        aAdd(aCots[1],SC8->C8_EMISSAO)
                                        aAdd(aCots[1],{})
                                        aAdd(aCots[1][3],aForns)
                                    EndIf

                                    If nOpcX == 5
                                        If lClicB
                                            A311RegCot(cNumCot,3)
                                        Endif
                                    Endif
                                    EvalTrigger()
                                    While ( GetSX8Len() > nSaveSX8 )
                                        ConfirmSx8()
                                    EndDo
                                EndIf
                            Else
                                aAutoItens := MsAuto2Gd(aHeader,aCols)
                            EndIf
                        EndIf
                    End Transaction
                    If lHelp
                        Help(" ",1,"A150NAOREG")
                    Endif
                    If ExistBlock("MT150END")
                        ExecBlock("MT150END",.F.,.F.,{nOpcx})
                    EndIf
                Endif

				if !lRetImp
					If nOpc == 0
						While ( GetSX8Len() > nSaveSX8 )
							RollBackSX8()
						EndDo
					Else
						If SuperGetMV("MV_ENVCOT") .AND. Len(aCots) > 0 .AND. l150Inclui
							cBody := A131GerMail(aCots[1][1],aCots[1][2],aCots[1][3][1])
							MTSendMail({aCots[1][3][1][3]},OemToAnsi(STR0101),cBody)
						EndIf
					EndIf
				endif
                //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                //³ Destrava os registros na aletaracao e exclusao            ³
                //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                If l150Altera .Or. l150Deleta
                    MsUnlockAll()
                EndIf
                //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
                //³ Finaliza o uso das funcoes MATXFIS                        ³
                //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                MaFisEnd()
                RestArea(aArea)
                If !(SC8->(IndexOrd()) > 0) //ajuste para Oracle
                    dbSetOrder(1)
                EndIf 
                //Return(nOpc) 
            EndIf 
        Else
            Help(,, "A150LOCK",, STR0114, 1, 0) //-- "Cotação em uso por outro usuário!"
        Endif

        If lLock
            UnLockByName("COT_" + cFilSC8 + cNumCot, .T., .F.)
        Endif
    EndIf

	FwFreeArray(aRetGenTx)
	FwFreeObj(oJsonTax)
Return nOpc

/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡„o    ³ A150OK   ³Rev.   ³ Bruno Schmidt         ³ Data ³28.09.2010 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³          ³Funcao de fazer validacaoes no momento de confirmar          ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150OK(ExpC1,ExpX1,ExpL1)                                    ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpN1: Referencia do imposto	                               ³±±
	±±³          ³ExpL1: efetua o refresh (default = .T.)                      ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T./.F.	                                                   ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡„o ³Esta rotina executa o Ponto de Entrada MT150OK para          ³±±
	±±³          ³validacoes complementares quando se confirma a EnchoiceBar   ³±±
	±±³          ³todas as informacoes vinculadas.                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso       ³ Materiais                                                   ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150OK(nOpcX,lMT150OK)
	Local lRet  := .T.

	IF lMT150OK
		lRet := ExecBlock("MT150OK",.F.,.F.,{nOpcX})
		If ValType(lRet) <> "L"
			lRet:= .T.
		EndIf
	EndIf

Return lRet

/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡„o    ³A150VDesc ³Rev.   ³ Edson Maricate        ³ Data ³29.07.2000 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³          ³Rotina de calculo e validacao dos descontos da cotacao de    ³±±
	±±³          ³compra                                                       ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150VDesc(ExpN1,ExpN2,ExpN3,ExpA1)                           ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpN1: desconto 1 			                               ³±±
	±±³ 	     ³ExpN2: desconto 2     	       				               ³±±
	±±³ 	     ³ExpN3: desconto 3         	   				               ³±±
	±±³          ³ExpA1: Array com as variaveis do rodape 			           ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T. se validacao OK, caso contrario, .F.                     ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡„o ³Esta rotina efetua o calculo e a validacao dos descontos em  ³±±
	±±³          ³cascata da cotacao de compra                                 ³±±
	±±³          ³                                                             ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso       ³ Materiais                                                   ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150VDesc(nDesc1,nDesc2,nDesc3,a150Var)

	Local aArea	  	:= GetArea()
	Local nTotal  	:= MaFisRet(,"NF_VALMERC")
	Local nPosDesc	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C8_DESC"})
	Local lDescItem	:= .F.
	Local lRet 		:= .T.
	Local lOpc		:= .T.
	Local cReadVar  := ReadVar()

	If nDesc1+nDesc2+nDesc3 > 0
		aEval(aCols,{|x| lDescItem := If(!Empty(x[nPosDesc]),.T.,lDescItem)})
		If lDescItem .And. !l150Auto
			lOpc := MsgYesNo(OemToAnsi(STR0055),OemToAnsi(STR0053)) // "Quanto a exclusao da cotaca"#"Atencao"
		EndIf
		If lOpc
			aEval(aCols,{|x| x[nPosDesc]:= 0 })
			a150Var[VALDESC] := (CalcDesc(nTotal,nDesc1,nDesc2,nDesc3))
			A150AtuRef("NF_DESCONTO",a150Var[VALDESC])
		Else
			lRet := .F.
		EndIf
	ElseIf !l150Auto
		If "NDESC" $ cReadVar
			a150Var[VALDESC] := 0
			aEval(aCols,{|x| x[nPosDesc]:= 0 })
			A150AtuRef("NF_DESCONTO",a150Var[VALDESC])
		EndIf
		If nDesc1 < 0 .Or. nDesc2 < 0 .Or. nDesc3 < 0
			lRet := .F.
		EndIf
	EndIf
	RestArea(aArea)
Return lRet


/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡„o    ³A150AtuRef³Rev.   ³ Edson Maricate        ³ Data ³29.07.2000 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³          ³Rotina de calculo e atualizacao dos dados referentes ao      ³±±
	±±³          ³cabecalho e rodape da cotacao de compra                      ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150AtuRef(ExpC1,ExpX1,ExpL1)                                ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpC1: Referencia do imposto	                               ³±±
	±±³          ³ExpX1: Referencia do campo 	                               ³±±
	±±³          ³ExpL1: efetua o refresh (default = .T.)                      ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T.	                                                       ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡„o ³Esta rotina executa a funcao fiscal com base na atualizacao  ³±±
	±±³          ³efetua no rodape ou cabecalho da cotacao fiscal, atualizando ³±±
	±±³          ³todas as informacoes vinculadas.                             ³±±
	±±³          ³                                                             ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso       ³ Materiais                                                   ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150AtuRef(cReferencia,xValor,lRefre)

	Local aArea		:= GetArea()
	Local nX		:= 0
	Local nY		:= 0
	Local nPosRef   := 0
	Local cValid	:= ""
	Local cRefCols	:= ""
	DEFAULT lRefre := .T.

	If ( MaFisFound("NF") )
		MaFisAlt(cReferencia,xValor)
		For nY := 1 to Len(aCols)
			For nX	:= 1 to Len(aHeader)
				cValid	:= AllTrim(UPPER(aHeader[nX][6]))
				If "MAFISREF"$cValid
					nPosRef := AT('MAFISREF("',cValid) + 10
					cRefCols:=Substr(cValid,nPosRef,AT('","MT150",',cValid)-nPosRef )
					If MaFisFound("IT",nY)
						aCols[nY][nX]:= MaFisRet(nY,cRefCols)
					EndIf
				EndIf
			Next nX
		Next nY
		If ( lRefre )
			Eval(bRefresh)
			Eval(bGDRefresh)
		EndIf
	EndIf
	If ReadVar() == "A150VAR[2]" // não pode ser utilizado o A150VAR[VALDESC], pois o retorno da readVar utiliza a posiçaõ 2. 
		lAtudesc := .T.
	Endif
	If ReadVar() == "A150VAR[6]"  .and. cValDesp <> A150VAR[VALDESP] // não pode ser utilizado o A150VAR[VALDESP], pois o retorno da readVar utiliza a posiçaõ . 
		lAtuDesp := .T.
		cValDesp := A150VAR[VALDESP]
	Endif

	RestArea(aArea)
Return(.T.)


/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡„o    ³A150Refres³Rev.   ³ Edson Maricate        ³ Data ³29.07.2000 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³          ³Rotina de atualizacao das variaveis do rodape referente aos  ³±±
	±±³          ³impostos.                                                    ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150Refresh(ExpA1)			                               ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpA1: Array com os variaveis de impostos do rodape          ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T.	                                                       ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡„o ³Esta rotina recupera os valores contidos na funcao fiscal    ³±±
	±±³          ³apos o calculo, atualizando as variaveis do rodape.          ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso       ³ Materiais                                                   ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A150Refresh(a150Var)

	Local aArea	:= GetArea()
	Local nPFrete := 0
	
	If !l150Auto .OR. (l150Inclui .and. !l150Propost)
			a150Var[VALMERC]:= MaFisRet(,"NF_VALMERC")
			a150Var[VALDESC]:= MaFisRet(,"NF_DESCONTO")
			a150Var[VALIPI] := MaFisRet(,"NF_VALIPI")
			a150Var[VALICM] := MaFisRet(,"NF_VALICM")
			a150Var[FRETE]  := MaFisRet(,"NF_FRETE")
			a150Var[TOTPED] := MaFisRet(,"NF_TOTAL")
			a150Var[BASEIPI]:= MaFisRet(,"NF_BASEIPI")
			a150Var[BASEICM]:= MaFisRet(,"NF_BASEICM")
			a150Var[BASESOL]:= MaFisRet(,"NF_BASESOL")
			a150Var[VALSOL] := MaFisRet(,"NF_VALSOL")
			a150Var[VALCOMP]:= MaFisRet(,"NF_VALCMP")
			a150Var[SEGURO] := MaFisRet(,"NF_SEGURO")
			a150Var[VALDESP]:= MaFisRet(,"NF_DESPESA")
			a150Var[TOTF1]  := a150Var[VALDESP]+a150Var[SEGURO]
			a150Var[TOTF3]  := a150Var[FRETE]+a150Var[SEGURO]+a150Var[VALDESP]
	ElseIf l150Auto  .And. l150Atuliz .And. Substr(c150Frete,1,1) == 'C'
		nPFrete := aScan(aAutoItens[N],{|x| AllTrim(x[1]) == "C8_VALFRE"})
		if nPFrete > 0
			a150Var[TOTF3] := aAutoItens[N][nPFrete][2]
		endif
	EndIf
	
	If ReadVar() == "M->C8_DESPESA"  
		cValDesp := A150VAR[VALDESP]
	Endif
	
	RestArea(aArea)

Return(.T.)

/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡„o    ³A150Forn  ³Rev.   ³ Alexandre Inacio Lemes³ Data ³15/08/2001 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³          ³Rotina de validacao e atualizacao das informacoes referentes ³±±
	±±³          ³ao fornecedor.                                               ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150Forn(ExpC1,ExpC2,ExpL1)                                  ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpC1: Codigo do Fornecedor                                  ³±±
	±±³          ³ExpC2: Loja do Fornecedor                                    ³±±
	±±³          ³ExpL1: Indica se o cabecalho pode ser alterado               ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T. / .F.                                                    ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡„o ³Esta rotina efetua a validacao do fornecedor quanto a integri³±±
	±±³          ³dade referencia e quanto a chave primaria da cotacao de com- ³±±
	±±³          ³pra.                                                         ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso       ³ Materiais                                                   ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150Forn(cA150Forn,cA150Loj,l150inclui)

	Local lRetorno	:= .F.
	Local cGetvar   := ReadVar()
	Local nRecSC8	:= SC8->(Recno())
	Local nX        := 0
	Local nPPICM    := aScan(aHeader,{|x| AllTrim(x[2])=="C8_PICM"})

	If  !Empty(cA150Forn)
		dbSelectArea("SA2")
		If Empty(cA150Loj)
			SA2->(dbSetOrder(1))
			SA2->(MsSeek(xFilial("SA2")+cA150Forn))
			While !SA2->(Eof()) .AND. xFilial("SA2")+cA150Forn == SA2->A2_FILIAL+SA2->A2_COD .AND. Empty(cA150Loj)
				IF RegistroOk("SA2",.F.) .AND. Empty(GetAdvFval("SC8","C8_NUM",xFilial("SC8") + cA150Num+cA150Forn+SA2->A2_LOJA,8))
					cA150Loj := SA2->A2_LOJA
				Else
					SA2->(DbSkip())
				EndIf
			Enddo
		Endif
	Endif

		If (Empty(cA150Forn) .Or. Empty(cA150Loj))
			lRetorno := .T.
		ElseIf ( !Empty(cA150Forn) )
			dbSelectArea("SA2")
			dbSetOrder(1)
			If ( dbSeek(xFilial("SA2")+cA150Forn+cA150Loj) )
				cA150Loj := SA2->A2_LOJA
				cContato := SA2->A2_CONTATO
				cCondicao:= SA2->A2_COND
				cA150FNome := SA2->A2_NOME
				cA150FMail := SA2->A2_EMAIL
				lRetorno := .T.
				If cGetvar = "CA150LOJ"
					If l150inclui
						dbSelectArea("SC8")
						dbSetOrder(8)
						If !dbSeek(xFilial("SC8")+cA150Num+cA150Forn+cA150Loj+cA150FNome)
							SC8->(dbGoTo(nRecSC8))
							lRetorno := .T.
						Else
							Help(" ",1,"A150FOREXIST")
							lRetorno := .F.
						EndIf
					Endif
				Endif
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se o Registro esta Bloqueado.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lRetorno .And. !RegistroOk("SA2")
					lRetorno := .F.
				EndIf
			Else
				If !( ExistCpo("SA2",cA150Forn+cA150Loj,1) )
					lRetorno := .F.
				EndIf
			Endif
		EndIf

		If lRetorno .And. !Empty(cA150Forn)
			If cFornBKP<>cA150Forn .Or. cLojBKP<>cA150Loj
				If MaFisFound()
					MaFisAlt("NF_CODCLIFOR",cA150Forn)
					If nPPIcm > 0
						For nX := 1 To Len(aCols)
							aCols[nX][nPPICM] := 0
						Next nX
					EndIf
					MaColsToFis(aHeader,aCols)
					MaFisLoad("NF_LOJA","")
					MaFisAlt("NF_LOJA",cA150Loj)

					cFornBKP:=cA150Forn
					cLojBKP:=cA150Loj
				EndIf
			EndIf
		EndIf

		Return(lRetorno)

/*/
		±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
		±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
		±±³Fun‡„o    ³a150Monta ³ Autor ³ Eduardo Riera         ³ Data ³27.07.2000 ³±±
		±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
		±±³          ³Rotina de montagem do aHeader e do aCols da cotacao de compra³±±
		±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
		±±³Sintaxe   ³ExpL1 := A150Monta(ExpN1,ExpB1,ExpA1,ExpA2,ExpA3,ExpL1,	   ³±±
		±±³          ³                   ExpL2,ExpL3)                              ³±±
		±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
		±±³Parametros³ExpN1: [2] Novo participante                                 ³±±
		±±³          ³       [3] Atualiza cotacao                                  ³±±
		±±³          ³       [3] Nova propostaante                                 ³±±
		±±³          ³       [4] Exclusao                                          ³±±
		±±³          ³ExpB1: Code block que eh avaliado para cada registro do SC8  ³±±
		±±³          ³ExpA1: Registros do SC8 para atualizacao                     ³±±
		±±³          ³ExpA2: Identificador de amarracao com a cotacao              ³±±
		±±³          ³ExpA3: Impostos tratados pela rotina                         ³±±
		±±³          ³ExpL1: Indica se a TES deve ser inicializada                 ³±±
		±±³          ³ExpL2: Indica qdo delecao da cotacao  		               ³±±
		±±³          ³ExpL3: Indica qdo delecao do produto 			               ³±±
		±±³          ³ExpL4: Indica cotacao ja analisada 			               ³±±
		±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
		±±³Retorno   ³ ExpL1: Conforme indicado pelo CodeBlock em ExpB2, quando o  ³±±
		±±³          ³ este indicar .F., a execucao da funcao sera interrompida.   ³±±
		±±³          ³ Para o codeblock sera informado o alias do SC8.             ³±±
		±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
		±±³Descri‡„o ³Esta rotina tem como objetivo atualizar as variaveis de uti- ³±±
		±±³          ³lizada na modelo 2.                                          ³±±
		±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
		±±³Uso       ³ Materiais                                                   ³±±
		±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
		±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
		ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function a150Monta(nOpc,bBlock,aRecNo,aSCs,aRefImpos,lIniTes,lDelCot,lDelProd,lAnalisada)

	Local aSC8      := {}
	Local bWhile    := {|| !Eof() .And. SC8->C8_FILIAL == xFilial("SC8") .And.;
		SC8->C8_NUM == cA150Num .And.;
		SC8->C8_FORNECE == cFornece .And.;
		SC8->C8_LOJA == cLoja }
	Local nItensG  := 0
	Local nX	   := 0
	Local nY	   := 0
	Local nJ       := 0
	Local nPosTes  := 0
	Local nPosTotal:= 0
	Local nPNumPro := 0
	Local nPItem   := 0
	Local nPosRef1 := 0
	Local nPosRef2 := 0
	Local nTotal   := 0
	Local nPosIt   := 0
	Local nPosPro  := 0
	Local nAchou   := 0
	Local cFornece := ""
	Local cLoja    := ""
	Local cProposta:= SC8->C8_NUMPRO
	Local cNextPro := ""
	Local lItensG  := .F.
	Local lRetorno := .T.
	Local lMT150Lin:= ExistBlock("MT150LIN")
	Local cProduto := ""
	Local cProdDel := SC8->C8_PRODUTO
	Local cProdSC8 := SC8->C8_PRODUTO
	Local lZeraProp:= GetNewPar('MV_ZERPROP','1') == '1' //1=Sim  2=Nao - Zera os valores na inclusao de nova proposta
	Local lReItProp:= GetNewPar('MV_REITPRO','1') == '1' //1=Sim  2=Nao - Renumera os itens na inclusao de nova proposta, caso exista um item deletado na proposta origem.
	Local lDelForn := .F.
	Local lDelCotAc:= GetNewPar('MV_PERFORM',.T.) //Indica se na exclusao da cotacao inteira ou por produto sera carregado o acols, pois esses tipos de exclusoes nao e apresentada a tela
	Local lMT150Chv:= ExistBlock("MT150CHV")
	Local cChaveDel:= SC8->C8_PRODUTO
	Local cChaveAux:= ""
	Local bSoftlock:= {|| Iif(nOpc == 5 , .T. , SoftLock("SC8") ) }
	Local aNoFields:= {}
	Local cItem	:= StrZero(1,TamSx3("C8_ITEM")[1])
	Local nPosQuant := 0
	Local lOrgPGC   := .F.
	Local lPortFor  := .F.
	Local nHeader 	:= 1
	Local nPosRef	:= 0
	Local xValor	:= 0
	
	DEFAULT bBlock		:= {|| .T.}
	DEFAULT lIniTes		:= .F.
	DEFAULT lDelCot		:= .F.
	DEFAULT lDelProd	:= .F.
	DEFAULT lAnalisada	:= .F.
	DEFAULT aRecNo		:= {}
	DEFAULT aSCs		:= {}

	PRIVATE N

	lOrgPGC  := if(Alltrim(SC8->C8_ORIGEM) $ "PGCA020|NFCA020" .and. !A150GetPGC(),.T., .F.) //Verifica se cotação foi gerada pelo PGC.
	lPortFor := if(lOrgPGC .And. FwIsInCallStack("PUTQUOTE"), .T., .F.) //Verifica se foi chamado pelo Portal do Fornecedor

    If _lPGC .or. lPortFor
        aNoFields:= {"C8_FORNOME","C8_FORMAIL","C8_WF","C8_FORNECE","C8_LOJA","C8_CONTATO","C8_COND"}

		If lPortFor
			AAdd(aNoFields, "C8_MSUIDT")
		EndIf
    Else
        aNoFields:= {"C8_FORNOME","C8_FORMAIL","C8_WF","C8_FORNECE","C8_LOJA","C8_CONTATO","C8_COND","C8_SITUAC","C8_QTDISP","C8_DIFAL","C8_BASEDES","C8_PDORI","C8_PDDES","C8_IDTRIB"}
    EndIf

	If SC8->(FieldPos("C8_ITEMPCO")) > 0
		Aadd(aNoFields, "C8_ITEMPCO")
	Endif

	bWhile    := {|| !Eof() .And. SC8->C8_FILIAL == xFilial("SC8") .And.;
		SC8->C8_NUM == cA150Num .And.;
		SC8->C8_FORNECE == cFornece .And.;
		SC8->C8_LOJA == cLoja .And. ;
		AllTrim(SC8->C8_FORNOME) == AllTrim(cA150FNome) }

	lGrade := If(Type('lGrade')=="L",lGrade,MaGrade())

	MatGrdPrrf(@cProdDel, .T.)

	If cPaisLoc == "ARG"
		dbSelectArea("SA2")
		dbSetOrder(1)
		If ( MsSeek(xFilial("SA2")+cA150Forn+cA150Loj) )
			If Empty(cA150ProvEnt)
				cA150ProvEnt := SA2->A2_EST // Provincia de Entrega do Fornecedor
			Endif
		Endif
	EndIf

	If !lDelCot .And. !lDelProd
		lDelForn := .T.
	Endif
	If !lDelCotAc .And. nOpc <> 5
		lDelCotAc := .T.
	Endif
	If lDelProd .And. lMT150Chv
		cChaveAux := ExecBlock("MT150CHV",.F.,.F.)
		If ValType(cChaveAux) == "C" .And. !Empty(cChaveAux)
			cChaveDel += &(cChaveAux)
			cChaveAux := "SC8->C8_PRODUTO+"+cChaveAux
		Endif
	Endif

	If lDelCotAc .Or. (nOpc == 5 .And. lDelForn)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Faz a montagem do aHeader a partir dos campos SX3.           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aHeader  := {}
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Montagem do aHeader                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SX3")
		dbSetOrder(1)
		dbSeek("SC8")

		aHeader := A150HEADER('SC8',aNoFields,.T.)

		For nHeader := 1 to len(aHeader)

			/* Referência:
			aAdd(aRetHead,{ TRIM(X3Titulo()),; -- 1
				GetSx3Cache(aAllFields[nX,1],'X3_CAMPO'),; --2
				GetSx3Cache(aAllFields[nX,1],'X3_PICTURE'),; -- 3
				GetSx3Cache(aAllFields[nX,1],'X3_TAMANHO'),; -- 4
				GetSx3Cache(aAllFields[nX,1],'X3_DECIMAL'),; -- 5
				GetSx3Cache(aAllFields[nX,1],'X3_VALID'),; -- 6
				GetSx3Cache(aAllFields[nX,1],'X3_USADO'),; -- 7
				GetSx3Cache(aAllFields[nX,1],'X3_TIPO'),; -- 8 
				GetSx3Cache(aAllFields[nX,1],'X3_F3'),; -- 9 
				GetSx3Cache(aAllFields[nX,1],'X3_CONTEXT')}) -- 10
			*/

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Array dos campos de Usuario para posterior replicacao do aCols ³
			//³ na funcao a150AcolsSC8 em unificacao de precos = SIM           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aAdd(aHeaderPro,{aHeader[nHeader][2], GetSx3Cache(aHeader[nHeader][2],'X3_PROPRI')} )

			If ( Trim(aHeader[nHeader][2])=="C8_NUMPRO" )
				nPNumPro := nHeader
			ElseIf ( Trim(aHeader[nHeader][2])=="C8_ITEM" )
				nPItem := nHeader
			ElseIf ( Trim(aHeader[nHeader][2])=="C8_TES" )
				nPosTes := nHeader
			ElseIf ( Trim(aHeader[nHeader][2])=="C8_TOTAL" )
				nPosTotal := nHeader
			EndIf

			nPosRef1	:= At("MAFISREF(",Upper(aHeader[nHeader][6]))
			If ( nPosRef1 > 0 )
				nPosRef1    += 10
				nPosRef2    := At(",",SubStr(aHeader[nHeader][6],nPosRef1))-2
				aadd(aRefImpos,{"SC8",aHeader[nHeader][2],SubStr(aHeader[nHeader][6],nPosRef1,nPosRef2)})
			EndIf

		next nHeader		

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Aciona o Walk Thru somente quando nao for utilizada a unificacao de precos ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If mv_par06 == 1
			ADHeadRec("SC8",aHeader)
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Faz a montagem do aCols com os dados da Cotacao de Compra    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aCols := {}
	Endif
	Do Case
	Case ( nOpc == 5 .And. (lDelCot .Or. lDelProd) )
		dbSelectArea("SC8")
		If lDelCot
			dbSetOrder(1)
			dbSeek(xFilial("SC8")+cA150Num)
			bWhile := {|| !Eof() .And. SC8->C8_FILIAL == xFilial("SC8") .And.;
				SC8->C8_NUM == cA150Num }
		Else
			If SC8->C8_GRADE == "S"
				dbSetOrder(1)
				dbSeek(xFilial("SC8")+cA150Num)
				bWhile := {|| !Eof() .And. SC8->C8_FILIAL == xFilial("SC8") .And.;
					SC8->C8_NUM == cA150Num }
			Else
				dbSetOrder(3)
				dbSeek(xFilial("SC8")+cA150Num+cProdDel)
				bWhile := {|| !Eof() .And. SC8->C8_FILIAL == xFilial("SC8") .And.;
					SC8->C8_NUM == cA150Num .And. SC8->C8_PRODUTO == cProdDel }
			EndIf
		Endif
	Case ( nOpc <> 2 )
		cFornece := cA150Forn
		cLoja    := cA150Loj
		cProposta:= SC8->C8_NUMPRO
		dbSelectArea("SC8")
		If mv_par06==2
			dbSetOrder(10)
			dbSeek(xFilial("SC8")+cA150Num+cFornece+cLoja)
		Else
			dbSetOrder(8)
			dbSeek(xFilial("SC8")+cA150Num+cFornece+cLoja+cA150FNome)
		EndIf
	OtherWise
		bWhile := {|| !Eof() .And. SC8->C8_FILIAL == xFilial("SC8") .And.;
			SC8->C8_NUM == cA150Num }
		dbSelectArea("SC8")
		dbSetOrder(1)
		dbSeek(xFilial("SC8")+cA150Num)
	EndCase

	if l150Auto .and. Type("aAutoItens") == "A" .and. len(aAutoItens) > 0
		nPosIt	:= aScan(aAutoItens[1],{|x| AllTrim(x[1]) == "C8_ITEM"})
		nPosPro	:= aScan(aAutoItens[1],{|x| AllTrim(x[1]) == "C8_PRODUTO"})
	endif 

	While ( Eval(bWhile) )
		lAnalisada := .F.
		If (lDelCot .Or. lDelProd) .Or. aScan(aSC8,{|x| x[1] == SC8->C8_PRODUTO.And.x[2]==SC8->C8_IDENT})==0
			If ( (lDelCot .Or. lDelProd) .Or. SC8->C8_NUMPRO == cProposta )
				If nOpc==3 .And. !Empty(SC8->C8_NUMPED)
					SC8->(dbSkip())
					lAnalisada := .T.
					Loop
				Endif
				If ( Eval(bSoftlock) .And. Eval(bBlock,"SC8") )
					If lDelCotAc .Or. (nOpc == 5 .And. lDelForn)
						If mv_par06 == 1
							AADD(aCols,Array(Len(aHeader)+1))
						Endif
					Endif
					If lDelProd
						If !lMT150Chv
							cProdSC8 := SC8->C8_PRODUTO
							MatGrdPrrf(@cProdSC8, .T.)
							If cProdDel == cProdSC8
								AADD(aRecNo,SC8->(RecNo()))
							Endif
						Else
							If !Empty(cChaveAux) .And. !Empty(cChaveDel)
								If cChaveDel == &(cChaveAux)
									AADD(aRecNo,SC8->(RecNo()))
								Endif
							Else
								cProdSC8 := SC8->C8_PRODUTO
								MatGrdPrrf(@cProdSC8, .T.)
								If cProdDel == cProdSC8
									AADD(aRecNo,SC8->(RecNo()))
								Endif
							Endif
						Endif
					Else
						AADD(aRecNo,SC8->(RecNo()))
					Endif
					AADD(aScs,{SC8->C8_NUMSC,SC8->C8_ITEMSC,SC8->C8_IDENT,SC8->C8_GRUPCOM,alltrim(SC8->C8_PRODUTO),SC8->(RecNo())})

					If lDelCotAc .Or. (nOpc == 5 .And. lDelForn)
						If mv_par06 == 1
							nX++
						Else
							If cProduto <> SC8->C8_PRODUTO
								AADD(aCols,Array(Len(aHeader)+1))
								cProduto := SC8->C8_PRODUTO
								nX++
							EndIf
						Endif

						If l150Auto 
							
							nAchou := 0
							nJ 	   := 1	

							For nJ:= 1 to Len(aAutoItens)
								MaFisIniLoad(nJ)
								If ALLTRIM(aAutoItens[nJ][nPosIt][2]) == ALLTRIM(SC8->C8_ITEM) .AND. ALLTRIM(aAutoItens[nJ][nPosPro][2]) == ALLTRIM(SC8->C8_PRODUTO)
									nAchou := nJ
								EndIf
							Next
						Else 
							nAchou := Nx	
						EndIf

							If  nAchou > 0
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Inicia a Carga do item nas funcoes MATXFIS  ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								If !l150Auto
									MaFisIniLoad(nAchou)
								Endif

								For nY := 1 To Len(aHeader)
									dbSelectArea("SC8")
									If mv_par06 == 1
										If ( aHeader[nY][10] != "V" )
											If l150Auto 
												If !(AllTrim(aHeader[nY][2]) $ Iif(_lPGC,"|C8_QTDISP|C8_QTSEGUM","|C8_QUANT|C8_QTSEGUM") .AND. GetSx3Cache(aHeader[nY][2],"X3_VISUAL")=="V")
													aCols[nX][nY] := FieldGet(FieldPos(aHeader[nY][2]))
												Else
													nPosQuant := If (nAchou>0, aScan(aAutoItens[nAchou],{|x| AllTrim(x[1]) == Alltrim(aHeader[nY][2])}),0)

													//Atualiza aCols com Quantidade Original da SC8
													If SC8->&(aHeader[nY][2]) > 0
														aCols[nX][nY] := SC8->&(aHeader[nY][2])

													ElseIf  nPosQuant > 0 .AND. aAutoItens[nAchou][nPosQuant][2] > 0
														aCols[nX][nY] := aAutoItens[nAchou][nPosQuant][2]
													Else
														aCols[nX][nY] := FieldGet(FieldPos(aHeader[nY][2]))
													EndIf

													If nPosQuant > 0 .and. SC8->&(aHeader[nY][2])!=aAutoItens[nAchou][nPosQuant][2]
														//Regra padrão não permite alteração de quantidade, ExecAuto também desconsiderará a alteração
														//Atualiza aAutoItens com Quantidade Original para Validar Valor Total - Help A12003
														aAutoItens[nAchou][nPosQuant][2] := SC8->&(aHeader[nY][2])
													EndIf
											 EndIf
											Else
												If AllTrim(aHeader[nY][2]) $ "C8_BASEICM"
													If nPosTotal > 0
														aCols[nX][nY] := FieldGet(FieldPos(aHeader[nPosTotal][2]))
													EndIf
												ElseIf AllTrim(aHeader[nY][2]) $ "C8_BASEIPI"
													If nPosTotal > 0
														aCols[nX][nY] := FieldGet(FieldPos(aHeader[nPosTotal][2]))
													EndIf
												Else
													aCols[nX][nY] := FieldGet(FieldPos(aHeader[nY][2]))
												EndIf
											EndIf
										ElseIf IsHeadRec(aHeader[nY][2])
											aCols[nX][nY] := IIF(nOpc == 3 .Or. (nOpc == 5 .And. mv_par04 == 1) , SC8->(Recno()) , 0 )
										ElseIf IsHeadAlias(aHeader[nY][2])
											aCols[nX][nY] := "SC8"
										Else
											aCols[nX][nY] := CriaVar(aHeader[nY][2],.T.)
										EndIf


										aCols[nX][Len(aHeader)+1] := .F.
									Else
										//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
										//³Unifica Atualizacao de Precos por Produto                               ³
										//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
										If ( aHeader[nY][10] != "V")
											If !(Trim(aHeader[nY][2]) $ Iif(_lPGC, "C8_QTDISP/","C8_QUANT/")+"C8_QTSEGUM/C8_TOTAL/C8_VALFRE/C8_DESPESA/C8_SEGURO/C8_VLDESC/C8_VALIPI/C8_VALICM/C8_BASEIPI/C8_BASEICM")
												aCols[nX][nY] := FieldGet(FieldPos(aHeader[nY][2]))
											Else
												If aCols[nX][nY] == Nil
													aCols[nX][nY] := FieldGet(FieldPos(aHeader[nY][2]))
												Else
													If (Trim(aHeader[nY][2])=="C8_QUANT") .Or. (Trim(aHeader[nY][2])=="C8_QTSEGUM")
														If l150Auto
															//Atualiza aCols com Quantidade Original da SC8
															aCols[nX][nY] := SC8->&(aHeader[nY][2])

															nPosQuant := If (nAchou >0, aScan(aAutoItens[nAchou],{|x| AllTrim(x[1]) == AllTrim(aHeader[nY][2])}),0)
															If nPosQuant > 0 .and. SC8->&(aHeader[nY][2])!=aAutoItens[nAchou][nPosQuant][2]
																//Regra padrão não permite alteração de quantidade, ExecAuto também desconsiderará a alteração
																//Atualiza aAutoItens com Quantidade Original para Validar Valor Total - Help A12003
																aAutoItens[nAchou][nPosQuant][2] := SC8->&(aHeader[nY][2])
															EndIf
														Else
															If cProduto == SC8->C8_PRODUTO
																aCols[nX][nY] += FieldGet(FieldPos(aHeader[nY][2]))
															EndIf
														EndIf
													EndIf
													If ( Trim(aHeader[nY][2])=="C8_TOTAL" )
														If cProduto == SC8->C8_PRODUTO
															aCols[nX][nY] += FieldGet(FieldPos(aHeader[nY][2]))
														EndIf
													EndIf
													If ( Trim(aHeader[nY][2])=="C8_VALFRE" )
														If cProduto == SC8->C8_PRODUTO
															aCols[nX][nY] += FieldGet(FieldPos(aHeader[nY][2]))
														EndIf
													EndIf
													If ( Trim(aHeader[nY][2])=="C8_DESPESA" )
														If cProduto == SC8->C8_PRODUTO
															aCols[nX][nY] += FieldGet(FieldPos(aHeader[nY][2]))
														EndIf
													EndIf
													If ( Trim(aHeader[nY][2])=="C8_SEGURO" )
														If cProduto == SC8->C8_PRODUTO
															aCols[nX][nY] += FieldGet(FieldPos(aHeader[nY][2]))
														EndIf
													EndIf
													If ( Trim(aHeader[nY][2])=="C8_VLDESC" )
														If cProduto == SC8->C8_PRODUTO
															aCols[nX][nY] += FieldGet(FieldPos(aHeader[nY][2]))
														EndIf
													EndIf
													If ( Trim(aHeader[nY][2])=="C8_VALIPI" )
														If cProduto == SC8->C8_PRODUTO
															aCols[nX][nY] += FieldGet(FieldPos(aHeader[nY][2]))
														EndIf
													EndIf
													If ( Trim(aHeader[nY][2])=="C8_VALICM" )
														If cProduto == SC8->C8_PRODUTO
															aCols[nX][nY] += FieldGet(FieldPos(aHeader[nY][2]))
														EndIf
													EndIf
													If ( Trim(aHeader[nY][2])=="C8_BASEIPI" )
														If cProduto == SC8->C8_PRODUTO
															aCols[nX][nY] += FieldGet(FieldPos(aHeader[nY][2]))
														EndIf
													EndIf
													If ( Trim(aHeader[nY][2])=="C8_BASEICM" )
														If cProduto == SC8->C8_PRODUTO
															aCols[nX][nY] += FieldGet(FieldPos(aHeader[nY][2]))
														EndIf
													EndIf
												Endif
											Endif
										Else
											aCols[nX][nY] := CriaVar(aHeader[nY][2],.T.)
										EndIf
										aCols[nX][Len(aHeader)+1] := .F.
									Endif

									If Trim(aHeader[nY][02])=="C8_ITEM" .And. l150Inclui
										aCols[nX][nY] := cItem
										cItem := Soma1(cItem)
									EndIf

									If cPaisLoc <> "BRA"
										If ( Trim(aHeader[nY][2])=="C8_TAXAFIN" )
											aCols[nX][nY] := Iif(Empty(SC8->C8_TAXAFIN),GetMv("MV_JUROS"), SC8->C8_TAXAFIN)
										EndIf
									EndIf
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³Preenche a TES padrao                                                   ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									If ( Trim(aHeader[nY][2])=="C8_TES" .And. lIniTes )
										dbSelectArea("SB1")
										dbSetOrder(1)
										dbSeek(xFilial("SB1")+SC8->C8_PRODUTO,.F.)
										If ( Empty(aCols[nX][nPosTes]) )
											aCols[nX][nPosTes] := RetFldProd(SB1->B1_COD,"B1_TE")
										EndIf
									EndIf
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³Efetua tratamento para uma nova proposta                                ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									If nOpc == 2 .OR. (nOpc == 4 .And. lZeraProp) //Novo Participante ou Nova Proposta
										If ( Trim(aHeader[nY][02])$"C8_PICM/C8_PRECO/C8_TOTAL/C8_AVISTA/C8_VLDESC/C8_VALFRE/C8_PRAZO/C8_DESPESA/C8_SEGURO/C8_DESC/C8_VALEMB" )
											aCols[nX][nY] := 0
										Else
											If Trim(aHeader[nY][02])=="C8_COND"
												aCols[nX][nY] := ""
											EndIf
											If Trim(aHeader[nY][02])=="C8_CODTAB"
												aCols[nX][nY] := CriaVar(aHeader[nY][2],.T.)
											EndIf
											If Trim(aHeader[nY][02])=="C8_NUMPRO"
												aCols[nX][nY] := CriaVar(aHeader[nY][2],.T.)
											EndIf
										EndIf
									EndIf
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³Provincia de Entrega do Fornecedor                                      ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									If cPaisLoc == "ARG" .And. Trim(aHeader[nY][2])=="C8_PROVENT"
										aCols[nX][nY] := cA150Provent
									EndIf
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³Efetua tratamento para campos prazo e observação                        ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									If nOpc == 2 .And. AllTrim(aHeader[nY][2])$"C8_PRAZO"
										aCols[nX][nY] := CriaVar(aHeader[nY][2],.T.)
									EndIf

									nPosRef := aScan(aRefImpos,{|x| Alltrim(x[2]) == Alltrim(aHeader[nY][2])})
									if nPosRef > 0
										xValor := if((AllTrim(aRefImpos[nPosRef][2]) == "C8_TOTAL") .AND. (nOpc == 2 .OR. (nOpc == 4 .And. lZeraProp)),0,SC8->(FieldGet(FieldPos(aRefImpos[nPosRef][2]))))
										MaFisLoad(aRefImpos[nPosRef][3],xValor,nAchou)
									endif
								Next nY
							Endif


						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Zera valores na MATXFIS  					³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If (nOpc == 2 .OR. (nOpc == 4 .And. lZeraProp))
							MaFisAlt("NF_FRETE",0)
							MaFisAlt("NF_DESPESA",0)
							MaFisAlt("NF_SEGURO",0)
							MaFisAlt("NF_DESCONTO",0)
							MaFisAlt("NF_VALEMB",0)
							MaFisAlt("NF_TOTAL",0)
						EndIf

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Atualiza TES nas rotinas fiscais       ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If lIniTes .And. nPosTES > 0 .And. !Empty(aCols[nX][nPosTES])
							N := nX
							nTotal := IIf(nPosTotal>0,aCols[nX,nPosTotal],0)
							MaFisRef('IT_TES',"MT150",aCols[nX][nPosTES])
							// Protecao para Atualizacao Unificada
							If mv_par06==2 .And. nPosTotal > 0 .And. QtdComp(nTotal) <> QtdComp(aCols[nX,nPosTotal])
								aCols[nX,nPosTotal] := nTotal
							EndIf
						EndIf
					Endif

					aadd(aSC8,{SC8->C8_PRODUTO,SC8->C8_IDENT})

					If lMt150Lin
						N := nX
						ExecBlock("MT150LIN",.F.,.F.,{nOpc})
					Endif
				ElseIf !Empty(SC8->C8_NUMPED)
					lAnalisada := .T.
				EndIf
			EndIf
		EndIf
		cNextPro := If(cNextPro>SC8->C8_NUMPRO,cNextPro,SC8->C8_NUMPRO)
		dbSelectArea("SC8")
		dbSkip()
	EndDo

	If lDelCotAc .Or. (nOpc == 5 .And. lDelForn)

		If ( Len(aCols)==0 )
			lRetorno := .F.
		EndIf
		If ( nOpc == 2 .Or. (nOpc == 4 .And. lReItProp) ) .Or. (mv_par06 == 2) .Or. ( nOpc == 4 )
			lItensG := .F.
			// Caso MV_ReItPro=1(SIM), identifica se ha' itens de grade, anulando neste caso o param. p/ renumerar os itens
			If lGrade .And. nOpc == 4 .And. lReItProp
				For nX := Len(aCols) To 1 Step -1
					nItensG := (aScan(aCols,{|x|x[1] == aCols[nX][1]}))
					If nItensG <> nX
						lItensG := .T.
						Exit
					EndIf
				Next
			EndIf
			if nOpc == 4 .and. nPNumPro > 0
				For nX := 1 To Len(aCols)
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Atualiza o Campo Proposta                                               ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					aCols[nX][nPNumPro] := Soma1(cNextPro,1)
				Next
			endif
		Endif
	Endif

	If lRetorno .and. Len(aCols) > 0
		MaColsToFis(aHeader, aCols,, "MTA150",,,,,if(l150Auto,.F.,.T.))
	EndIf

Return(lRetorno)

/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡„o    ³A150Grava ³ Autor ³ Eduardo Riera         ³ Data ³31.07.2000 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³          ³Rotina de atualizacao da tabela de cotacoes de compra        ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150Grava(ExpN1,ExpA1,ExpA2,ExpB1)				  		   ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpN1: [2] Novo participante                                 ³±±
	±±³          ³       [3] Atualiza cotacao                                  ³±±
	±±³          ³       [4] Nova proposta                                     ³±±
	±±³          ³       [5] Exclusao                                          ³±±
	±±³          ³ExpA1: Array com os registros do SC8                         ³±±
	±±³          ³ExpA2: Array com os identificadores da cotacao               ³±±
	±±³          ³ExpB1: Code block que e' avaliado para cada registro do SC8  ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³ExpL1: .T.                                                   ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡„o ³Este rotina tem como objetivo efetuar a atualizacao das cota-³±±
	±±³          ³coes de compra.                                              ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso       ³ Materiais                                                   ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A150Grava(nOpcX,aRecNo,aSCs,bBlock)

	Static lAltC8TxFi := Nil
	Local nX		:= 0
	Local nY		:= 0
	Local nJ		:= 0
	Local lMT150GRV	:= ExistBlock("MT150GRV")
	Local lMT150DEL	:= ExistBlock("MT150DEL")
	Local lInclui   := .F.
	Local nUsado    := Len(aHeader)
	Local lDelCotAc := GetNewPar('MV_PERFORM',.T.) //Indica se na exclusao da cotacao inteira ou por produto sera carregado o acols, pois esses tipos de exclusoes nao e apresentada a tela, para melhorar a performance.
	Local lLog 		:= GetNewPar("MV_HABLOG",.F.)
	Local nPC8Total	:= aScan(aHeader,{|x| AllTrim(x[2])=="C8_TOTAL"})
	Local nPosItem  := aScan(aHeader,{|x| AllTrim(x[2])=="C8_ITEM"})
	Local aAux 		:= {}
	Local nLenRecno := 0
	Local nQtdItens := 0
	Local Cc8FilEnt	:= ""
	Local lTrbGen	:= IIf(FindFunction("ChkTrbGen"), ChkTrbGen("SC8", "C8_IDTRIB"), .F.)
	Local nValJuros	:= SuperGetMV("MV_JUROS", .F., 1)
	Local lPgc   	:= .F.
	Local lPortFor  := FwIsInCallStack("PUTQUOTE") //Chamado no Portal do Fornecedor

	PRIVATE lDel150	:= .F.

	lPgc := if(Alltrim(SC8->C8_ORIGEM) $ "PGCA020|NFCA020" .and. !A150GetPGC(),.T., .F.)//Verifica se cotação foi gerada pelo PGC.

	If !lDelCotAc .And. nOpcX <> 5
		lDelCotAc := .T.
	Endif

	Pergunte("MTA150",.F.)

	If (lDelCotAc .Or. (nOpcx == 5 .And. mv_par04 == 1))  
		If mv_par06 == 2 .Or. lPortFor // Veio do Portal do Fornecedor
			a150AcolsSC8(nOpcX,bBlock,aSCs,aRecNo)
		Endif
	Endif
	nLenRecno := len(aRecNo)
	If lDel150 .And. nOpcX != 2
		For nX := 1 to len(aSCs)
			If aSCs[nX][7] .And. !Empty(aSCs[nX][6])
				SC8->( dbGoto(aSCs[nX][6]) )
				RecLock("SC8",.F.)
				SC8->( dbDelete() )
				MaAvalCot("SC8",3)
				MsUnLock()
			Endif
		Next nX
		lDelCotAc := .F.
	EndIf

	//Recria array aSCs sem os itens deletados para impedir gravação erronea dos campos C8_NUMSC, C8_ITEMSC, C8_IDENT, C8_TAXAFIN, C8_GRUPCOM quando
	//algum deles está NÃO USADO no configurador
	IF !l150Deleta .AND. len(aSCs)>0
		If Len(aSCs[1])>6
			aAux := aClone(aSCs)
			aSCs := {}
			For nX := 1 To Len(aAux)
				If !aAux[nX][7]
					aAdd(aSCs,aAux[nX])
				EndIf
			Next nX
		EndIf
	EndIf

	If lAltC8TxFi == Nil
		lAltC8TxFi := ( GetSX3Cache("C8_TAXAFIN", "X3_VISUAL") $ ' A' )
	EndIf

	// Cuidado ao alterar o comando abaixo.
	ASort(aRecno) // Este comando corrigi erro de chave duplicada, pois o aRecno nao estava na mesma ordem do aCols.
	
	If lDelCotAc .Or. (nOpcx == 5 .And. mv_par04 == 1) .Or. nOpcx == 3
		dbSelectArea("SC8")
		dbSetOrder(1)
		
		For nX := 1 to Len(aCols)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Posiciona na cotacao correta                                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
			lInclui := .T.	
			If ( nX <= nLenRecno .And. nOpcX <> 2 .And. nOpcX <> 4 )	
				lInclui := .F.
				dbSelectArea("SC8")
				SC8->(dbGoto(aRecNo[nX]))
				If l150auto .and. acols[nx][nPosItem] <> SC8->C8_ITEM
					For nj := 1 to nLenRecno
						dbGoto(aRecNo[nJ])
						If  acols[nx][nPosItem] == SC8->C8_ITEM
						  	nj := nLenRecno
						Endif
					Next nJ
				Endif
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se o item deve ser deletado                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( aCols[nX,nUsado+1] .Or. nOpcX == 5 )
				If ( !lInclui )
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Ponto de Entrada para tratamento dos registros da Cotacao de compras.   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If lMT150DEL
						ExecBlock("MT150DEL",.f.,.f.)
					Endif

					//Chamada para exclusão do tributo generico (F2D_DTEXCL).
					If lTrbGen .AND. !Empty(SC8->C8_IDTRIB)
						MaFisTG(2,,,SC8->C8_IDTRIB)
					EndIf

					RecLock("SC8",.F.)
					dbDelete()
					MaAvalCot("SC8",3)
					MsUnLock()
				EndIf
			Else
				If ( lInclui )
					RecLock("SC8",.T.)
				Else
					Cc8FilEnt := SC8->C8_FILENT //tratamento para manter o conteudo do c8_filent, caso não seja enviado via execauto
					RecLock("SC8",.F.)
				EndIf
				If !lDel150 .Or. nOpcX == 2 .Or. nOpcx == 3
					For nY := 1 To Len(aHeader)
						If ( aHeader[nY][10] != "V" )
							If l150Auto
								If l150Inclui .OR. l150Propost .or. !(AllTrim(aHeader[nY][2])$ Iif(_lPGC,"|C8_QTDISP|","|C8_QUANT|")+"C8_QTSEGUM" .AND. GetSx3Cache(aHeader[nY][2],"X3_VISUAL")=="V")
                                    FieldPut(FieldPos(aHeader[nY][2]),aCols[nX][nY])
								EndIf
							Else
								FieldPut(FieldPos(aHeader[nY][2]),aCols[nX][nY])
							EndIf
						EndIf
					Next nY
				Endif
				/*Via Portal do Fornecedor, não carregava o aCols no Fiscal, gerando não conformidade ao chamar a função MaFisTG(1,"SC8",nX).
				Agora, o aCols é carregado corretamente no Fiscal, e na sequencia, atualizamos o "browser" com os valores corretos. */
				if ( l150auto .and. !(MafisFound("IT", nX)) )
					MaColsToFis(aHeader, aCols,,"MTA150")
					MaFisToCols(aHeader,aCols,,"MT150")
				endif
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza dados padroes da cotacao                        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				SC8->C8_FILIAL  := xFilial("SC8")
				SC8->C8_FORNECE := cA150Forn
				SC8->C8_LOJA    := cA150Loj
				SC8->C8_FORNOME := cA150FNome
				SC8->C8_FORMAIL := cA150Fmail

				if (lTrbGen)
					SC8->C8_IDTRIB	:= MaFisTG(1,"SC8",nX)
				endif

				SC8->C8_NUM     := cA150Num
				SC8->C8_COND    := cCondicao
				SC8->C8_CONTATO := cContato
				SC8->C8_MOEDA   := nMoedaCot
				SC8->C8_TxMoeda := nTxMoeda
				SC8->C8_REAJUST := cReajuste
				SC8->C8_MSG     := cMsg
				SC8->C8_DESC1   := nDesc1
				SC8->C8_DESC2   := nDesc2
				SC8->C8_DESC3   := nDesc3
				SC8->C8_TPFRETE := Substr(c150Frete,1,1)
				SC8->C8_TOTFRE  := MaFisRet(,"NF_FRETE")
				SC8->C8_EMISSAO := da150Emis

				If (lPgc .And. lPortFor)
					SC8->C8_ORIGEM := "PGCA020"
					SC8->C8_SITUAC := "8" //Identifica que foi gravado do Portal do Fornecedor
				EndIf

				If Empty(SC8->C8_NUMSC)
					SC8->C8_NUMSC   := aSCs[nX][1]
				EndIf
				If Empty(SC8->C8_ITEMSC)
					SC8->C8_ITEMSC  := aSCs[nX][2]
				EndIf
				If Empty(SC8->C8_IDENT)
					SC8->C8_IDENT   := aSCs[nX][3]
				EndIf
				If l150auto .AND. Empty(SC8->C8_FILENT)
					SC8->C8_FILENT   := Cc8FilEnt
				EndIf
				
				If !lAltC8TxFi //O campo so sera gravado com o MV_JUROS caso nao tenha sido liberado para digitacao
					SC8->C8_TAXAFIN := If(Empty(SC8->C8_TAXAFIN), nValJuros, SC8->C8_TAXAFIN)
				Endif
				If nOpcx == 2 .Or. nOpcx == 3 .Or. nOpcx == 4
					SC8->C8_GRUPCOM := aSCs[nX][4]
				EndIf
				MaFisWrite(2,"SC8",nX)
				MaAvalCOT("SC8",2,,,,,(Len(aCols) == nX))

				If SC8->C8_TOTAL == 0 .And. nPC8Total > 0
					FieldPut(FieldPos("C8_TOTAL"),aCols[nX][nPC8Total])
				Endif

				SC8->(MsUnlock())
				nQtdItens++

				//---------------------------------------------------------
				// Envia WF caso seja inclusao
				//---------------------------------------------------------
				/*
				If lInclui .And. cA150WF
					If MsgYesNo("Aviso","Confirma o Envio de WF?")
				// Chama função de Wf
					EndIf
				EndIf
				*/
				If lLog
					//Caio.Santos - 11/01/13 - Req.72
					RSTSCLOG("ATL",1,/*cUser*/)
				EndIf

				If lMT150GRV
					ExecBlock("MT150GRV",.F.,.F.,{nOpcx})
				EndIf
			EndIf	
		Next nX
	ElseIf !lDel150
		For nX := 1 To Len(aRecNo)
			dbSelectArea("SC8")
			dbGoto(aRecNo[nX])

			RecLock("SC8",.F.)
			dbDelete()
			MaAvalCot("SC8",3)
			MsUnLock()
		Next
	Endif

	If nQtdItens > 0
		ComMetric(nQtdItens)
	Endif

	//Atualiza o status da DHU caso a origem tenha vindo do NFC
	If (lPgc .And. lPortFor)
		setQuoteStatus(SC8->C8_NUM, SC8->C8_FILIAL)
	EndIf

Return(.T.)


/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡„o    ³A150CabOk ³Rev.   ³ Edson Maricate        ³ Data ³29.07.2000 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³          ³Rotina de validacao do cabecalho da cotacao de compra.       ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150CabOk(ExpN1,ExpA1,ExpA2,ExpC1,ExpC2,ExpA3)	  		   ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpN1: [2] Novo Participante                                 ³±±
	±±³          ³       [3] Atualizacao dao cotacao de compra                 ³±±
	±±³          ³       [4] Nova proposta                                     ³±±
	±±³          ³       [5] Exclusao                                          ³±±
	±±³          ³ExpA1: [1] Referencia do impostos                            ³±±
	±±³          ³       [2] Referencia do campo                               ³±±
	±±³          ³ExpA2: Informacoes do Fornecedor                             ³±±
	±±³          ³ExpC1: Descricao da Formula da Mensagem                      ³±±
	±±³          ³ExpC2: Descricao da Formula de reajuste                      ³±±
	±±³          ³ExpA3: Informacoes dos totais do rodape                      ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³ExpL1: .T. - Se o cabecalho for valido                       ³±±
	±±³          ³       .F. - Se o cabecalho for invalido                     ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡„o ³Esta rotina efetua a validacao no cabecalho da cotacao de    ³±±
	±±³          ³compra quanto a integridade referencial, inicializa a funcao ³±±
	±±³          ³fiscal e atualiza os dados do rodape vinculados ao cabecalho ³±±
	±±³          ³                                                             ³±±
	±±³          ³                                                             ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso       ³ Materiais                                                   ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A150CabOk(nOpcX,aImpRef,aInfForn,cDescMsg,cDescFor,a150Var,lTela150)

	Local lRetorno		:= .T.
	Local nRecSC8  		:= SC8->(Recno())
	Local lFoundForn	:= .F.
	Local lExisPart		:= .F.
	DEFAULT aInfForn	:= {}
	Default lTela150 	:= .F.

	If ( MaFisFound("NF") )
		MaFisAlt("NF_RELIMP",aImpRef)
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Dados do Fornecedor                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SA2")
	dbSetOrder(1)
	If ReadVar() == "CA150FORN" .And. !Empty(cA150Forn) .And. !( MsSeek(xFilial("SA2")+cA150Forn) )
		lFoundForn 	:= .F.
		lRetorno 	:= ExistCpo("SA2",xFilial("SA2")+cA150Forn,1)
	ElseIf ( MsSeek(xFilial("SA2")+cA150Forn+cA150Loj) )
		lFoundForn := .T.
		cCondicao 	:= If(Empty(cCondicao),SA2->A2_COND,cCondicao)  //Condicao
		aInfForn[1]	:= SA2->A2_NOME					// Nome
		aInfForn[2] := IIF(!Empty(SA2->A2_DDI),"( "+TransForm(SA2->A2_DDI,PesqPict("SA2","A2_DDI"))+") ","");
			+TransForm(alltrim(SA2->A2_DDD),PesqPict("SA2","A2_DDD"))+" "+TransForm(SA2->A2_TEL,PesqPict("SA2","A2_TEL")) // Telefone
		aInfForn[3]	:= SA2->A2_PRICOM	    			//Primeira Compra
		aInfForn[4] := SA2->A2_ULTCOM      				//Ultima Compra
		aInfForn[5]	:= Trim(SA2->A2_END)+" - "+Trim(SA2->A2_MUN)	//Endereco
		aInfForn[6]	:= SA2->A2_EST         				//Estado
		aInfForn[7]	:= SA2->A2_CGC         				//CNPJ
	ElseIf Empty(cA150FNome)
		cCondicao 	:= CriaVar("C8_COND")   //Condicao
		aInfForn[1]	:= ""	        	// Nome
		aInfForn[2]	:= "" 		        // Telefone
		aInfForn[3]	:= ""	        	// Primeira Compra
		aInfForn[4]     := ""           	// Ultima Compra
		aInfForn[5]	:= ""	        	// Endereco
		aInfForn[6]	:= ""                   // Estado
		aInfForn[7]	:= ""                   // CGC
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se ja existe o Fornecedor e loja  na Coatacao   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IF ReadVar()#"CA150LOJ"
		If nOpcX=2
			If Empty(cA150Forn) .And. Empty(cA150Loj)
				If !ExistChav("SC8",cA150Num+cA150Forn+cA150Loj+cA150FNome,8)
					SC8->(dbGoTo(nRecSC8))
					lExisPart := .T.
					lRetorno := .F.
				EndIf
			Else
				If !ExistChav("SC8",cA150Num+cA150Forn+cA150Loj,1)
					SC8->(dbGoTo(nRecSC8))
					lExisPart := .T.
					lRetorno := .F.
				EndIf
			EndIf
		EndIf
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se os dados do cabecalho foram preenchidos      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRetorno .And. nOpcX != 5
		If nOpcX == 2 //Novo Participante
			If lTela150 .And. (Empty(cA150Forn) .Or. Empty(cA150Loj)) .And. Empty(cA150FNome)
				lRetorno := .F.
			Endif

			If lRetorno .And. ((ReadVar() == "CCONDICAO" .And. Empty(cCondicao)) .Or. (lTela150 .And. Empty(cCondicao)))
				lRetorno := .F.
			Endif
		Elseif nOpcX == 3 .Or. nOpcX == 4 //3 - Atualiza / 4 - Proposta
			If (ReadVar() == "CCONDICAO" .And. Empty(cCondicao)) .Or. (lTela150 .And. Empty(cCondicao))
				lRetorno := .F.
			Endif
		Endif
	Endif

	If !lRetorno .And. !lExisPart .And. lTela150
		Help(1, " ", "OBRIGAT",, STR0105, 3, 0) // Informe os dados do fornecedor.
	EndIf

	If lRetorno
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiza o rodape com os dados do cabecalho              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Dados da Formula de Mensagem/Reajuste                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SM4")
		dbSetOrder(1)
		If ( MsSeek(xFilial("SM4")+cMsg) )
			cDescMsg := SM4->M4_DESCR
		Else
			cDescMsg := ""
		EndIf
		If ( MsSeek(xFilial("SM4")+cReajuste) )
			cDescFor := SM4->M4_DESCR
		Else
			cDescMsg := ""
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Dados do total da cotacao                                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nOpcX <> 4
			a150Var[VALMERC]:= MaFisRet(,"NF_VALMERC")
			a150Var[VALDESC]:= MaFisRet(,"NF_DESCONTO")
			a150Var[VALIPI] := MaFisRet(,"NF_VALIPI")
			a150Var[VALICM] := MaFisRet(,"NF_VALICM")
			a150Var[FRETE]  := MaFisRet(,"NF_FRETE")
			a150Var[TOTPED] := MaFisRet(,"NF_TOTAL")
			a150Var[BASEIPI]:= MaFisRet(,"NF_BASEIPI")
			a150Var[BASEICM]:= MaFisRet(,"NF_BASEICM")
			a150Var[BASESOL]:= MaFisRet(,"NF_BASESOL")
			a150Var[VALSOL] := MaFisRet(,"NF_VALSOL")
			a150Var[VALCOMP]:= MaFisRet(,"NF_VALCMP")
			a150Var[SEGURO] := MaFisRet(,"NF_SEGURO")
			a150Var[VALDESP]:= MaFisRet(,"NF_DESPESA")
			a150Var[TOTF1]  := a150Var[VALDESP]+a150Var[SEGURO]
			a150Var[TOTF3]  := a150Var[FRETE]+a150Var[SEGURO]+a150Var[VALDESP]
		EndIf
	EndIf
Return(lRetorno)


/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³a150ComView³ Autor ³Alexandre Inacio Lemes³ Data ³05/11/2003³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³ Cria uma tela de consulta de historico dos produtos        ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ Nenhum                                                     ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³ Nenhum                                                     ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³ MATA150                                                    ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150ComView()

	Local nPosCod	:= aScan(aHeader,{|x| AllTrim(x[2])=="C8_PRODUTO"})

	MaFisSave()
	MaFisEnd()
	If !AtIsRotina("MACOMVIEW")
		If !Empty(aCols[n][nPosCod])
			MaComView(aCols[n][nPosCod])
		EndIf
	EndIf
	MaFisRestore()

Return

/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³a150DescCnd³ Autor ³Alexandre Inacio Lemes³ Data ³05/11/2003³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³ Preenche a descricao da condicao de pagto.                 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³ A150DescCnd(ExpC1,ExpO1,ExpC2,ExpO2)                       ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ ExpC1: codigo da condicao pagto.			                  ³±±
	±±³          ³ ExpO1: obj da cond.pagto.  				                  ³±±
	±±³          ³ ExpC2: descricao da condicao pagto.		                  ³±±
	±±³          ³ ExpO2: obj da getdados	 				                  ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³ .T.		                                                  ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³ MATA150                                                    ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150DescCnd(cCondicao,oDescCond,cDescCond,oGetDados)

	dbSelectArea("SE4")
	dbSetOrder(1)
	If MsSeek(xFilial()+cCondicao)
		cDescCond := SE4->E4_DESCRI
		If oDescCond != Nil
			oDescCond:Refresh()
		EndIf
	ElseIf Empty(cCondicao)
		cDescCond := "        "
	EndIf

	If oGetDados <> Nil
		oGetDados:oBrowse:Refresh()
	EndIf

Return .T.


/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡„o    ³A150RodOk ³Rev.   ³ Edson Maricate        ³ Data ³29.07.2000 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³          ³Rotina de refresh do rodape da cotacao de compra             ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150RodOk(ExpO1,ExpA1)				                       ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpO1: obj.do folder do rodape	                           ³±±
	±±³          ³ExpA1: Array do rodape			                           ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T.									  	                   ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡„o ³Esta rotina efetua o refresh no rodape da cotacao de compra  ³±±
	±±³          ³quanto a integridade referencial, inicializa a funcao fiscal ³±±
	±±³          ³e atualiza os dados do rodape vinculados ao cabecalho        ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso       ³ Materiais                                                   ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A150RodOk(oFolder,aObjetos,lAtuPict)

	Local nX		:= 0
	Local nY	    := 0
	Local a150Bck   := a150Var

	DEFAULT lAtuPict := .F.

	If lAtuPict
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Armazena e zera os valores totalizadores para atualizacao da picture ³
		//³ pois o refresh e executado quando ha mudanca no conteudo da variavel,³
		//³ tratamento especifico para ambientes localizados onde as casas       ³
		//³ decimais podem variar de acordo com a moeda                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		a150Bck   := a150Var
		a150Var   := {0,0,0,0,0,0,0,0,0,0,0,0,0,0,0}

		aObjetos[1,1]:PICTURE:=PesqPict('SC8','C8_TOTAL',14,nMoedaCot)
		aObjetos[1,2]:PICTURE:=PesqPict('SC8','C8_VLDESC',14,nMoedaCot)
		aObjetos[1,5]:PICTURE:=PesqPict('SC8','C8_VALFRE',14,nMoedaCot)
		aObjetos[1,7]:PICTURE:=PesqPict('SC8','C8_TOTAL',14,nMoedaCot)
		aObjetos[3,2]:PICTURE:=PesqPict('SC8','C8_VALFRE',14,nMoedaCot)
		aObjetos[3,3]:PICTURE:=PesqPict('SC8','C8_VALFRE',14,nMoedaCot)
		aObjetos[3,4]:PICTURE:=PesqPict('SC8','C8_VALFRE',14,nMoedaCot)
		aObjetos[3,5]:PICTURE:=PesqPict('SC8','C8_VALFRE',14,nMoedaCot)
		aObjetos[4,4]:PICTURE:=PesqPict('SC8','C8_VLDESC',14,nMoedaCot)
		aObjetos[4,5]:PICTURE:=PesqPict('SC8','C8_TOTAL',14,nMoedaCot)

	EndIf

	For nY := 1 To Len(aObjetos)
		For nX := 1 To Len(aObjetos[nY])
			If aObjetos[nY][nX] <> Nil
				aObjetos[nY][nX]:Refresh()
			Endif
		Next nX
	Next nY

	If lAtuPict
		a150Var := a150Bck
	EndIf

Return(.T.)


/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡„o    ³A150Trigge³Rev.   ³ Edson Maricate        ³ Data ³29.07.2000 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³          ³Rotina de execucao das funcoes fiscais vinculas a um gatilho ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150Trigger(ExpC1)					                       ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpC1: Nome do campo de destino                              ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³ExpL1: .T. - Se a funcao foi executada com sucesso           ³±±
	±±³          ³       .F. - Se a funcao nao foi executada com sucesso       ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡„o ³Esta rotina efetua a execucao das funcoes fiscais vinculadas ³±±
	±±³          ³a um gatilho a fim de manter todos os impostos calculado.    ³±±
	±±³          ³                                                             ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso       ³ Materiais                                                   ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150Trigger(cCampo)

	Local aArea 	:= GetArea()
	Local lRet		:= .T.
	Local nX		:= 0
	Local nPosCpo	:= 0
	Local oModel    := FwModelActive()
	Local lNFC      := A150GetPGC() .And. ValType(oModel) == 'O' .And. oModel:GetId() == 'NFCA020' // Verifico se a origem é Legado ou NFC

	if !lNFC
		nPosCpo	:= aScan(aHeader,{|x| x[2]=cCampo})
		If ( nPosCpo > 0 )
			For nX := 1 to Len(aHeader)
				M->&(AllTrim(aHeader[nX][2])) := aCols[n][nX]
			Next
			If &(aHeader[nPosCpo][6])
				A150FieldOk()
			Else
				lRet := .F.
			EndIf
		Else
			lRet := .F.
		EndIf

		If "C8_TOTAL" $ cCampo
			A150ArrTot()
		EndIf
	endif

	RestArea(aArea)

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} A150ArrTot
Funcao utilizada para calcular o valor total do item

@author Luiz Henrique Bourscheid 
@since 29/05/2017
@version 1.0
/*/
//-------------------------------------------------------------------
Function A150ArrTot()

	Local nPosQtd   := aScan(aHeader,{|x| AllTrim(x[2]) == Iif(_lPGC,"C8_QTDISP","C8_QUANT")})
	Local nPosPrc   := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_PRECO"})
	Local nPosTot   := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_TOTAL"})
	Local nTotal    := 0
	Local nTamTot   := TamSX3("C8_TOTAL")[2]
	Local cRegra    := SuperGetMV("MV_ARRPEDC",.F.,"")

	If !Empty(cRegra)
		If AllTrim(cRegra) == "NOROUND"
			nTotal := NoRound(aCols[n][nPosQtd]*aCols[n][nPosPrc],nTamTot)
		ElseIf AllTrim(cRegra) == "ROUND"
			nTotal := Round(aCols[n][nPosQtd]*aCols[n][nPosPrc],nTamTot)
		EndIf

		If nTotal > 0
			aCols[n][nPosTot] := nTotal
			MaFisAlt("IT_VALMERC",nTotal,n)
			Eval(bRefresh)
		EndIf
	EndIf

Return

/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A150FieldOk ³Autor³ Edson Maricate        ³ Data ³06.01.2000³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Refresh da GetDados. 	 			                          ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³Nenhum                                                      ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³Nenhum                                                      ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³MATA150                                                     ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150FieldOk()
	Eval(bRefresh)
	Eval(bTgRefresh)
Return .T.

/*/


	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A150Preco³ Autor ³ TOTVS                 ³ Data ³30.06.2016³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Validacao do valor unitario digitado                       ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150Preco()                                                ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpNl:                                                     ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T. se valor unitario validado, caso contrario .F.         ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³ Dicionario de Dados - Campo:C8_PRECO                      ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150Preco()

	Local lRet   := .T.
	Local oModel := FwModelActive()
	local lNFC   := A150GetPGC() .And. ValType(oModel) == 'O' .And. oModel:GetId() == 'NFCA020' // Verifico se a origem é Legado ou NFC

	if !lNFC
		// Tratamento incluido para efetuar o calculo dos impostos na atualizacao da cotacao
		If l150Atuliz
			MaFisRef("IT_PRCUNI","MT150",M->C8_PRECO)
		EndIf
	else
		NF020SVlIt()
	endif

Return lRet

/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A150Total³ Autor ³ Edson Maricate        ³ Data ³06.01.2000³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Validacao do valor total digitado                          ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150Total(ExpN1)                                           ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpNl: total digitado				                         ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T. se total validado, caso contrario .F.                  ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³ Dicionario de Dados - Campo:C8_TOTAL                      ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150Total(nTotal)

	Local aArea		:= GetArea()
	Local oModel    := FwModelActive()
	Local lNFC      := A150GetPGC() .And. ValType(oModel) == 'O' .And. oModel:GetId() == 'NFCA020' // Verifico se a origem é Legado ou NFC
	Local nPosTES	:= iif( !lNFC, aScan(aHeader,{|x|x[2] = "C8_TES"}), 0 )
	Local nPosProv  := iif( !lNFC, Ascan(aHeader,{|x|x[2] = "C8_PROVENT"}), 0 )
	Local nPQtSegUm := iif( !lNFC, Ascan(aHeader,{|x|x[2] = "C8_QTSEGUM"}), 0 )
	Local nPPreco	:= iif( !lNFC, Ascan(aHeader,{|x|x[2] = "C8_PRECO"}), 0 )
	Local nPQuant	:= iif( !lNFC, Ascan(aHeader,{|x|x[2] = "C8_QUANT"}), 0 )
	Local nQuant	:= iif( !lNFC, aCols[n][aScan(aHeader,{|x|x[2] = Iif(_lPGC,"C8_QTDISP","C8_QUANT")})], 0 )
	Local nPreco	:= iif( !lNFC, aCols[n][GDFieldPos("C8_PRECO")], 0 )
	Local lRet 		:= .T.
	Local nDif		:= NoRound(nQuant*nPreco,2)-nTotal
	Local lCalc2Um  := SuperGetMV( "MV_CALC2UM", .F., .T. )

	if !lNFC
		If nDif < 0
			nDif := -(nDif)
		EndIf

		If lCalc2Um
			If nPQtSegUm > 0
				If aCols[n][nPQtSegUm] > 0 .And. nDif > 0.49
					aCols[n][nPPreco] := nTotal / aCols[n][nPQuant]
					If MaFisFound( "IT", n )
						MaFisAlt( "IT_PRCUNI", aCols[n][nPPreco], n )
					EndIf
					nDif := Abs( NoRound( aCols[n][nPQuant] * aCols[n][nPPreco], 2 ) - nTotal )
				EndIf
			EndIf 
		EndIf

		If nDif > 0.09
			Help(" ",1,"A12003")
			lRet := .F.
		EndIf

		If lRet
			MaFisAlt('IT_TES',aCols[n][nPosTES],n)
			If nPosProv > 0 .And. VerProEnIt(aCols[n][nPosProv],aCols[n][nPosTES],.T.,.F.)
				MaFisAlt('IT_PROVENT',aCols[n][nPosProv],n)
			EndIf
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica e Calcula o Desconto em cascata           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lRet 
			MaFisRef("IT_VALMERC","MT150",M->C8_TOTAL)
		Endif 

		if !l150Auto
			A150AtuRef("NF_FRETE"   ,a150Var[FRETE],.T.)
		Endif

		IF lAtuDesp
			A150AtuRef("NF_DESPESA" ,a150Var[VALDESP],.T.)
		Endif

		if ( l150Auto .and. Type("aAutoItens") == "A" .and. len(aAutoItens) > 0 .and. len(aCols) == len(aAutoItens) ) .or. !l150Auto
			A150AtuRef("NF_SEGURO"  ,a150Var[SEGURO],.T.)
		endif

		If lAtudesc
			A150AtuRef("NF_DESCONTO",a150Var[VALDESC],.T.)
		Endif
		
		Eval(bCalcDesc)
	endif
	RestArea(aArea)

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A150LinOk ³ Autor ³ Edson Maricate        ³ Data ³18.02.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Critica se a linha digitada esta' Ok                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum							                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³.T. / .F.										              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA150                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A150LinOk(o)

	Local nX
	Local lRet := .T.
	Local nDec
	Local nPosTotal
	Local nPosPreco
	Local nPosQuant
	Local lMt150lOkT:= ExistTemplate("MT150LOK")
	Local lMt150lOk := Existblock("MT150LOK")
	Local nPosProd	:= ASCAN ( AHEADER , { |X| X[2]=="C8_PRODUTO"})

	For nX = 1 To Len(aHeader)
		If Trim(aHeader[nX][2]) == "C8_TOTAL"
			nPosTotal := nX
			nDec      := aHeader[nX][5]
		ElseIf Trim(aHeader[nX][2]) == "C8_PRECO"
			nPosPreco := nX
		ElseIf Trim(aHeader[nX][2]) == Iif(_lPGC,"C8_QTDISP","C8_QUANT")
			nPosQuant := nX
		ElseIf Trim(aHeader[nX][2]) == "C8_TES"
			If aCols[n][nX] > "500"
				Help(" ",1,"A100INVTES")
				lRet := .F.
			EndIf
		Endif
	Next nX

	If lRet .and. valtype("_lPGC") == "L" .and. !_lPGC
		If Abs(aCols[n][nPosTotal] - ;
				Round(aCols[n][nPosQuant] * aCols[n][nPosPreco],nDec)) > 0.09
			Help(" ",1,"A150TOT")
			lRet := .F.
		EndIf
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Posiciono a SB1 para validar item correto          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	dbSelectArea("SB1")
	dbSetOrder(1)
	dbSeek(xFilial("SB1")+aCols[N,nPosProd],.F.)	

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Checa se produto está bloqueado                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !RegistroOk("SB1")
		lRet := .F.
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Pontos de Entrada                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRet	.And. lMt150lOkT	// Tratamento do usuario
		lRet := ExecTemplate("MT150LOK",.F.,.F.)
	Endif
	If lRet	.And. lMt150lOk	// Tratamento do usuario
		lRet := Execblock("MT150LOK",.F.,.F.)
	Endif

Return lRet


/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³a150FisToaCols³ Autor ³ Edson Maricate    ³ Data ³ 01.02.99 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Atualiza o aCols com os valores da funcao fiscal.           ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150FisToaCols(ExpN1)                                       ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpNl: no.do item a atualizar (OPC)		 				  ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T.                                                         ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³ MATA150                                                    ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A150FisToaCols(nItem)

	Local nX		:= 0
	Local nY		:= 0
	Local cValid	:= ""
	Local nPosRef	:= 0
	Local nItemIni	:= If(nItem==Nil,1,nItem)
	Local nItemAte	:= If(nItem==Nil,Len(aCols),nItem)

	For nY := nItemIni to nItemAte
		For nX	:= 1 to Len(aHeader)
			cValid	:= AllTrim(UPPER(aHeader[nX][6]))
			If "MAFISREF"$cValid
				nPosRef := AT('MAFISREF("',cValid) + 10
				cRefCols:=Substr(cValid,nPosRef,AT('","MT150",',cValid)-nPosRef )
				If MaFisFound("IT",nY)
					aCols[nY][nX]:= MaFisRet(nY,cRefCols)
				EndIf
			EndIf
		Next
	Next

Return .T.


/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³ A150Del  ³ Autor ³ Edson Maricate        ³ Data ³04.03.2000³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³ Critica a delecao da linha                                 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ ExpC1 = Objeto a ser verificado.                           ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T.                                                         ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³ MatA150                                                    ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150Del(o)

	If !MaFisFound("IT",n)
		MaFisAdd("","",0,0,0,"","",,0,0,0,0,0)
	EndIf
	MaFisDel(n,aCols[n][Len(aCols[n])])
	Eval(bRefresh)
Return .T.


/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A150TudOk ³ Autor ³ Edson Maricate        ³ Data ³04.03.2000³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³ Critica se toda cotacao esta Ok                            ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ ExpC1 = Objeto a ser verificado.                           ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T. / .F.                                                   ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³ MATA150                                                    ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150Tudok(o,nOpcx)

	Local lRet	     := .T.
	Local lRetPE	 := .T.
	Local nX         := 0
	Local nUsado     := Len(aHeader)
	Local nPosCodTab := aScan(aHeader,{|x| AllTrim(x[2])=="C8_CODTAB"})
	Local nPosProd   := aScan(aHeader,{|x| AllTrim(x[2])=="C8_PRODUTO"})
	Local nPosItem   := aScan(aHeader,{|x| AllTrim(x[2])=="C8_ITEM"})

	If Empty(cCondicao)
		lRet := .F.
		HELP("   ",1,"A150COND")
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Caso o fornecedor seja de alguma      ³
	//³cidade ou provincia que esteja no     ³
	//³parametro MV_RETIIBB valida para que  ³
	//|o mesmo seja obrigado a informar o    |
	//|campo PROVINCIA DE ENTREGA            |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cPaisLoc == "ARG"
		SA2->(DBSetOrder(1))
		SA2->(MSSeek(xFilial("SA2")+cA150Forn+cA150Loj))
		If Empty(cA150ProvEnt) .And. (SA2->A2_EST$GetMV("MV_RETIIBB",,"CF|BA"))
			Aviso( OemToAnsi(STR0021), OemToAnsi(STR0084),{OemToAnsi(STR0083)}) // "Atencao!"### "Es necesario que informe la ciudad o provincia de entrega del producto o prestacion del servicio"    ###  "OK"
			lRet := .F.
		EndIf
	EndIf

	For nX := 1 To Len(aCols)
		If !aCols[nX][nUsado+1]
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica a integridade com a tabela de preco                                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lRet .And. nPosCodTab > 0 .And. !Empty(aCols[nX][nPosCodTab])
				If ( !MaVldTabCom(cA150Forn,cA150Loj,aCols[nX][nPosCodTab],cCondicao,/*cHelp*/,dA150Emis) )
					lRet := .F.
					Exit
				EndIf
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se o produto x fornecedor foi bloqueado pela Qualidade.	                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !QieSitFornec(cA150Forn,cA150Loj,aCols[nX,nPosProd],.F.)
				Help(" ",1,"A150BLQFO",,OemToAnsi(STR0066)+Alltrim(aCols[nX,nPosProd])+OemToAnsi(STR0067)+Alltrim(aCols[nX,nPosItem])+OemToAnsi(STR0068),1)//"O Produto: " ### " no Item: " ### ", desta cotacao, foi bloqueado pela Qualidade para este Fornecedor."
				lRet := .F.
				Exit
			EndIF
		EndIf
	Next nX
	If lRet .And. ExistBlock("MT150OK")
		lRetPE := ExecBlock("MT150OK",.F.,.F.,{nOpcx})
		If ValType(lRetPE) == "L"
			lRet:= lRetPE
		EndIf
	EndIf

Return lRet


/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A150ZeraDesc³Autor³ Edson Maricate        ³ Data ³06.01.2000³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Zera os percentuais de desconto do Rodape.                  ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³Nenhum                                                      ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T.                                                         ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³MATA150                                                     ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150ZeraDesc()
	Local oModel := FwModelActive()
	Local lNFC   := A150GetPGC() .And. ValType(oModel) == 'O' .And. oModel:GetId() == 'NFCA020' // Verifico se a origem é Legado ou NFC

	if !lNFC
		Eval(bZeraDesc)
		Eval(bFolderRefresh)
	endif
Return .T.


/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A150VldCombo³Autor³ Edson Maricate        ³ Data ³06.01.2000³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Validacao do Combo utilizado para o tipo de Frete.          ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150VldCombo(ExpC1,ExpA1)                                   ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpC1: Conteudo do Combo	("FRETE")				          ³±±
	±±³          ³ExpA1: Array com os variaveis do rodape 			          ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T.                                                         ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³MATA150                                                     ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150VldCombo(c150Frete,a150Var)

	If Substr(c150Frete,1,1) != "C"
		a150Var[FRETE] := 0
	EndIf

Return .T.


/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡„o    ³A150Legend³Rev.   ³ Edson Maricate        ³ Data ³29.07.2000 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³          ³Demonstra a legenda das cores da mbrowse                     ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³Nenhum                                                       ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T.	                                                       ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡„o ³Esta rotina monta uma dialog com a descricao das cores da    ³±±
	±±³          ³Mbrowse.                                                     ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso       ³ Materiais                                                   ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150Legenda()

	Local aLegenda := {}
	Local aLegUsr  := {}
	Local nCnt     := 0

	Aadd( aLegenda, {"ENABLE",STR0058} )
	Aadd( aLegenda, {"DISABLE",STR0059} )
	Aadd( aLegenda, {"BR_AMARELO",STR0060} )
	Aadd( aLegenda, {"BR_BRANCO",STR0117} )//Cotação gerada no PGC

	//-- Adiciona cores do usuario
	If ExistBlock("MT150LEG")
		aLegUsr := ExecBlock("MT150LEG",.F.,.F., { 2 })
		If ValType(aLegUsr) <> "A"
			aLegUsr := {}
		EndIf
		For nCnt := 1 To Len(aLegUsr)
			Aadd( aLegenda , { aLegUsr[nCnt,1],aLegUsr[nCnt,2] } )
		Next nCnt
	EndIf

	BrwLegenda(cCadastro,STR0057,aLegenda)

Return(.T.)


/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³  ProcH	  ³ Autor ³                       ³Data³          ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³ Procura campos itens em aAutoCab (rotina automatica)		  ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³ ExpN1 := ProcH(ExpC1)                     				  ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ ExpC1 = Nome do campo                                      ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³ ExpN1 = Numero da posicao encontrada                       ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso       ³ MATA150                                                    ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ProcH(cCampo)
Return aScan(aAutoCab,{|x|Trim(x[1])== cCampo })


/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡„o    ³A150Totais³ Autor ³ Aline Correa do Vale  ³ Data ³29.09.2003 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³          ³Demonstra as quantidades totais considerando todas as filiais³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡„o ³Esta rotina monta uma dialog aglutinando todas os produtos e ³±±
	±±³          ³quantidades solicitadas de um mesmo fornecedor               ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150Totais(ExpC1,ExpN1,ExpN2)                                ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpC1: Alias do Arquivo                                      ³±±
	±±³          ³ExpN1: Numero do Registro                                    ³±±
	±±³          ³ExpN2: Opcao selecionada                                     ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T.	                                                       ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso       ³ Materiais                                                   ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150Totais(cAlias,nReg,nOpcX)

	Local aArea    := GetArea()
	Local aSCs	   := {}
	Local aStruSC8 := {}


	Local aHeader  := {}
	Local aCols    := {}
	Local cCadastro:= OemtoAnsi(STR0056) //"Atualiza‡Æo Pre‡os da Cota‡Æo"
	Local cAviso   := ""
	Local lContinua:= .T.
	Local nUsado   := 0
	Local nX       := 0
	Local nY       := 0
	Local nPosQtd  := 0
	Local nPosTot  := 0
	Local nPosPrd  := 0
	Local nPosAcols:= 0
	Local nPosPrc  := 0
	Local cProduto := ""
	Local cAliasSC8:= "SC8"
	Local cQuery   := ""
	Local lQuery   := .F.
	Local cA150Forn:= SC8->C8_FORNECE
	Local cA150Loj := SC8->C8_LOJA
	Local cArqInd  := ""
	Local oDlg
	Local oFont
	Local oQual
	Local oNomeFor := NIL
	Local lPgc		:= .F.

	lPgc := if(Alltrim(SC8->C8_ORIGEM)=="PGCA020",.T., .F.)//Verifica se cotação foi gerada pelo PGC.
    
    If lPgc
        Help(,,"A150PGC",,STR0115, 1, 0,,,,,, {STR0116} ) //-- "Não é permitido realizar ações em cotações oriundas do Protheus Gestão de Compras.". Utilize a rotina Protheus Gestão de Compras.
    EndIf
	
	lContinua := MaCanAltCot("SC8",.F.,@cAviso)

	If lContinua .and. !lPgc
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Efetua a montagem do Aheader e do Acols                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SX3")
		dbSetOrder(1)
		MsSeek("SC8")
		While !EOF() .And. (SX3->X3_ARQUIVO == cAlias)
			IF X3USO(SX3->X3_USADO) .And. cNivel >= SX3->X3_NIVEL .And. (ALLTRIM(X3_CAMPO) $;
					Iif(_lPGC,"C8_QTDISP/","C8_QUANT/")+'C8_PRODUTO/C8_TOTAL/C8_DATPRF')
				nUsado++
				AADD(aHeader,{ TRIM(X3Titulo()),;
					SX3->X3_CAMPO,;
					SX3->X3_PICTURE,;
					SX3->X3_TAMANHO,;
					SX3->X3_DECIMAL,;
					SX3->X3_VALID,;
					SX3->X3_USADO,;
					SX3->X3_TIPO,;
					SX3->X3_ARQUIVO,;
					SX3->X3_CONTEXT } )
			EndIf
			If ( Trim(SX3->X3_CAMPO)==Iif(_lPGC,"C8_QTDISP","C8_QUANT") )
				nPosQtd := nUsado
			EndIf
			If ( Trim(SX3->X3_CAMPO)=="C8_PRECO" )
				nPosPrc := nUsado
			EndIf
			If ( Trim(SX3->X3_CAMPO)=="C8_TOTAL" )
				nPosTot := nUsado
			EndIf
			If ( Trim(SX3->X3_CAMPO)=="C8_PRODUTO" )
				nPosPrd := nUsado
			EndIf
			dbSelectArea("SX3")
			dbSkip()
		EndDo
		nUsado  := Len(aHeader)

		lQuery := .T.
		cAliasSC8 := "SC8TMP"
		aStruSC8 := SC8->(dbStruct())
		cQuery := "SELECT C8_FILIAL, " + Iif(_lPGC,"C8_QTDISP","C8_QUANT") + ", C8_TOTAL, C8_DATPRF, C8_FORNECE, "
		cQuery += "C8_LOJA, C8_NUMPED, C8_PRODUTO, C8_NUM FROM "+ RetSqlName("SC8")+" SC8 "
		cQuery += "WHERE C8_FORNECE = '"+ca150Forn+"' AND C8_LOJA = '"+ca150Loj+"' "
		cQuery += "AND C8_NUMPED ='" + Space(Len(SC8->C8_NUMPED)) + "' "
		cQuery += "AND D_E_L_E_T_ = ' ' "

		cQuery := ChangeQuery(cQuery)

		dbUseArea( .T., "TOPCONN", TcGenQry( ,,cQuery ), cAliasSC8, .F., .F. )
		For nX := 1 To Len(aStruSC8)
			If aStruSC8[nX][2] <> "C" .And. FieldPos(aStruSC8[nX][1])<>0
				TcSetField(cAliasSC8,aStruSC8[nX][1],aStruSC8[nX][2],aStruSC8[nX][3],aStruSC8[nX][4])
			EndIf
		Next nX
		cProduto := ""
		While !Eof()
			If (cA150Forn == (cAliasSC8)->C8_FORNECE .And. cA150Loj==(cAliasSC8)->C8_LOJA;
					.And. Empty(C8_NUMPED))
				nPosAcols := aScan(aCols,{|x|x[nPosPrd] == cProduto})
				If nPosAcols > 0
					nX := nPosAcols
				Else
					AADD(aCols,Array(nUsado+1))
					nX := Len(aCols)
				EndIf
				For nY := 1 to Len(aHeader)
					If (Trim(aHeader[nY][2]) == Iif(_lPGC,"C8_QTDISP","C8_QUANT") .Or. Trim(aHeader[nY][2]) == "C8_TOTAL") .And. nPosAcols > 0
						aCols[nX][nY] += FieldGet(FieldPos(aHeader[nY][2]))
					ElseIf (Trim(aHeader[nY][2]) == "C8_PRECO") .And. nPosAcols > 0
						aCols[nX][nY] := If(aCols[nX][nY]>0,aCols[nX][nY],FieldGet(FieldPos(aHeader[nY][2])))
					Else
						aCols[nX][nY] := FieldGet(FieldPos(aHeader[nY][2]))
					EndIf
					aCols[nX][nUsado+1] := .F.
				Next nY
				If nX > Len(aSCS)
					aAdd(aSCS,{})
				EndIf

				aSCs[nX] := {Trans(aCols[nX][1],PesqPict("SC8","C8_PRODUTO"))+ " ",;
					Trans(aCols[nx][2],PesqPict("SC8",Iif(_lPGC,"C8_QTDISP","C8_QUANT")))+ " ",;
					Trans(aCols[nx][3],PesqPict("SC8","C8_TOTAL"))+ " ",;
					Trans(aCols[nx][4],PesqPict("SC8","C8_DATPRF"))}
			EndIf
			dbSelectArea(cAliasSC8)
			dbSkip()
			cProduto := (cAliasSC8)->C8_PRODUTO
		EndDo

		dbSelectArea("SA2")
		dbSetOrder(1)
		dbSeek(xFilial("SA2")+cA150Forn+cA150Loj)

		DEFINE MSDIALOG oDlg TITLE cCadastro + "/" + OemtoAnsi(STR0034) FROM 0,0 TO 300,550 OF oMainWnd	 PIXEL
		DEFINE FONT oFont SIZE 8,0 BOLD

		@ 05,05 Say OemToAnsi(STR0002)+": "+cA150Forn +" - "+cA150Loj OF oDlg PIXEL //"Fornecedor "
		@ 04,120 MSGET oNomeFor VAR SA2->A2_NOME When .F. SIZE 155,9 OF oDlg PIXEL
		If(lLGPD,OfuscaLGPD(oNomeFor,"A2_NOME"),.F.)

		@ 28,15 LISTBOX oQual FIELDS HEADER OemtoAnsi(STR0005),OemtoAnsi(STR0063),OemtoAnsi(STR0028),OemtoAnsi(STR0007) ;
			SIZE 250,100 PIXEL

		oQual:SetArray(aScs)
		oQual:bLine := { || {aScs[oQual:nAt,1], aScs[oQual:nAt,2], aScs[oQual:nAt,3], aScs[oQual:nAt,4]} }

		DEFINE SBUTTON FROM 135,125 TYPE 1 ACTION (oDlg:End()) ENABLE OF oDlg PIXEL
		ACTIVATE MSDIALOG oDlg CENTERED

	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Finaliza o uso das funcoes MATXFIS                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lQuery
		(cAliasSC8)->(dbCloseArea())
	EndIf
	dbSelectArea("SC8")
	dbSetOrder(1)
	If ( bFiltraBrw != Nil )
		Eval(bFiltraBrw)
	EndIf
	If File(cArqInd+OrdBagExt())
		FErase(cArqInd+OrdBagExt() )
	EndIf
	RestArea(aArea)
Return(.T.)


/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Funcao    ³A150Tabela³ Autor ³ Eduardo Riera         ³ Data ³16.10.2003³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Funcao que retorna o preco de compra                        ³±±
	±±³          ³                                                            ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150Tabela(ExpC1,ExpX1)		                              ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpC1:    Nome do Campo da tabela de preco a atualizar      ³±±
	±±³          ³ExpX1:    Conteudo do campo a atualizar                     ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T.						                                  ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³          ³               ³                                            ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150Tabela(cReadVar,xConteudo)

	Local aArea     := GetArea()
	Local aAreaSB1  := SB1->(GetArea())
	Local oModel    := FwModelActive()
	Local lNFC      := A150GetPGC() .And. ValType(oModel) == 'O' .And. oModel:GetId() == 'NFCA020' // Verifico se a origem é Legado ou NFC
	Local nPCodTab  := iif( !lNFC, aScan(aHeader,{|x| AllTrim(x[2])=="C8_CODTAB"}), 0 )
	Local nPProduto := iif( !lNFC, aScan(aHeader,{|x| AllTrim(x[2])=="C8_PRODUTO"}), 0 )
	Local nPQuant   := iif( !lNFC, aScan(aHeader,{|x| AllTrim(x[2])==Iif(_lPGC,"C8_QTDISP","C8_QUANT")}), 0 )
	Local nPPreco   := iif( !lNFC, aScan(aHeader,{|x| AllTrim(x[2])=="C8_PRECO"}), 0 )
	Local nPAVista  := iif( !lNFC, aScan(aHeader,{|x| AllTrim(x[2])=="C8_AVISTA"}), 0 )
	Local nPTxFor   := iif( !lNFC, aScan(aHeader,{|x| AllTrim(x[2])=="C8_TAXAFOR"}), 0 )
	Local nPTotal   := iif( !lNFC, aScan(aHeader,{|x| AllTrim(x[2])=="C8_TOTAL"}), 0 )
	Local nPFrete   := iif( !lNFC, aScan(aHeader,{|x| AllTrim(x[2])=="C8_VALFRE"}), 0 )
	Local nPrcCom	:= iif( !lNFC, aCols[N][nPPreco], 0 )
	Local nQtde     := iif( !lNFC, aCols[N][nPQuant], 0 )
	Local cProduto  := iif( !lNFC, aCols[N][nPProduto], "")
	Local cCodTab   := ""
	Local nValFrete := 0
	Local lAtuFrete := .F.

	DEFAULT cReadVar  := ReadVar()
	DEFAULT xConteudo := &(ReadVar())

	if !lNFC
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o campo de tabela de preco esta ativo            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nPCodTab > 0
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica qual o campo ativo na getdados                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Do Case
			Case "C8_CODTAB"$cReadVar
				cCodTab := xConteudo

				//Atualiza aCols
				aCols[n,nPCodTab] := xConteudo
			Case "C8_PRODUTO"$cReadVar
				cProduto := xConteudo
			Case "C8_QUANT"$cReadVar
				nQtde := xConteudo
			EndCase

			dbSelectArea("SB1")
			dbSetOrder(1)
			If ( !Empty(cProduto) .And. MsSeek(xFilial("SB1")+cProduto,.F.) .and. !Empty(cCodTab))
				nPrcCom := MaTabPrCom(cCodTab,cProduto,nQtde,cA150Forn,cA150Loj,nMoedaCot,dA150Emis,nTxMoeda,@nValFrete)
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza a funcao fiscal                                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If nPAVista > 0
				aCols[N][nPAVista] := nPrcCom
			EndIf
			If nPAVista > 0 .And. nPTxFor > 0 .And. aCols[n][nPTxFor] > 0
				nPrcCom	 := a150FV(nPrcCom,aCols[n][nPTxFor])
				nValFrete:= a150FV(nValFrete,aCols[n][nPTxFor])
			EndIf
			aCols[N][nPPreco] := nPrcCom
			If nPFrete > 0 .And. nValFrete != aCols[N][nPFrete]
				aCols[N][nPFrete] := nValFrete
				lAtuFrete          := .T.
			EndIf

			If MaFisFound("IT",N)
				MaFisRef("IT_PRCUNI","MT150",nPrcCom)
				If lAtuFrete
					MaFisRef("IT_FRETE","MT150",nValFrete)
				EndIf
			EndIf
			If Abs(aCols[n][nPTotal]-NoRound(aCols[n][nPPreco]*aCols[n][nPQuant],TamSx3("C8_TOTAL")[2]))<>0.09
				aCols[n][nPTotal] := NoRound(aCols[n][nPPreco]*aCols[n][nPQuant],TamSx3("C8_TOTAL")[2])
				If MaFisFound("IT",N)
					MaFisRef("IT_VALMERC","MT150",aCols[n][nPTotal])
				EndIf
			EndIf
			If ExistTrigger("C8_PRECO")
				RunTrigger(2,N)
			EndIf
			If ExistTrigger("C8_VALFRE")
				RunTrigger(2,N)
			EndIf
		EndIf
	endif

	RestArea(aAreaSB1)
	RestArea(aArea)
Return(.T.)


/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A150DescMoed|Autor ³Alexandre Inacio Lemes³ Data ³16/01/2004³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³ Preenche a descricao da Moeda                              ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³ A150DescMoed(ExpN1,ExpO1,ExpC1,Exp02)                      ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ ExpN1: numero da moeda					                  ³±±
	±±³          ³ ExpO1: obj da moeda		 				                  ³±±
	±±³          ³ ExpC1: descricao da moeda				                  ³±±
	±±³          ³ ExpO2: armazena a taxa da moeda                            ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³ .T.                                                        ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³ MATA150                                                    ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150DescMoed(nMoedaCot,oDescMoed,cDescMoed,oGetTaxa)

	Local lMoedLocTx  := GetNewPar("MV_MOEDLOC",.F.) //Param criado para atualizar a TX da moeda no PC em outros paises o DEFAULT nao atualiza a Taxa.
	Local lRet       := .T.
	Local lMoedAlt   := .T.
	Local lM150PESC  := (ExistBlock("MT150PESC") .AND. cPaisLoc!= "BRA")
	Local lM150Aux	 :=.F.

	If ExistBlock("MT150ALTMD")
		lMoedAlt := ExecBlock("MT150ALTMD",.F.,.F.)
		If ValType(lMoedAlt) == "L"
			lRet := lMoedAlt
		EndIf
	EndIf

	cDescMoed := SuperGetMv("MV_MOEDA"+AllTrim(Str(nMoedaCot,2)))
	If oDescMoed != Nil
		oDescMoed:Refresh()
	EndIf
	IF lM150PESC
		lM150Aux:=ExecBlock("MT150PESC",.F.,.F.,{lMoedLocTx})
		If ValType(lM150Aux) == "L"
			lMoedLocTx:=lM150Aux
		EndIF
	ENDIF
	If oGetTaxa != Nil .and. cPaisLoc != "BRA"
		oGetTaxa:PICTURE:=PesqPict('SC8','C8_TXMOEDA',11)
		oGetTaxa:Refresh()
	EndIf
	IF (cPaisLoc=="BRA" .Or. lMoedLocTx)
		nTxMoeda:=RecMoeda(dDataBase,nMoedaCot)
	EndIf

Return lRet

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A150AcolsSC8³Autor³Alexandre Inacio Lemes ³ Data ³16/09/2005 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Restaura o Acols original com base nos registros do SC8.     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³A150AcolsSC8(ExpN1,ExpB1)				                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: numero da opcao selecionada	  	                   ³±±
±±³          ³ExpB1: Code block que e' avaliado para cada registro do SC8  ³±±  
±±³          ³ExpA1: Array com as SC's carragadas no acols 				   ³±±  
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA150                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function a150AcolsSC8(nOpcX,bBlock,aSCSel,aRecNo)

	Local aColsLine:= aClone(aCols)
	Local aSC8     := {}
	Local aRecno2  := {}
	Local nX	   := 0
	Local nY	   := 0
	Local nH	   := 0   
	Local nSC      := 0
	Local nPos     := 0
	Local nSCPos   := 0
	Local nPosProd := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_PRODUTO"})
	Local nPosQuant:= aScan(aHeader,{|x| AllTrim(x[2]) == Iif(_lPGC,"C8_QTDISP","C8_QUANT")})
	Local nPosTab  := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_CODTAB"})
	Local nPosPreco:= aScan(aHeader,{|x| AllTrim(x[2]) == "C8_PRECO"})
	Local nPosTotal:= aScan(aHeader,{|x| AllTrim(x[2]) == "C8_TOTAL"})
	Local nPosPrazo:= aScan(aHeader,{|x| AllTrim(x[2]) == "C8_PRAZO"})
	Local nPosObser:= aScan(aHeader,{|x| AllTrim(x[2]) == "C8_OBS"})
	Local nPBaseIPI:= aScan(aHeader,{|x| AllTrim(x[2]) == "C8_BASEIPI"})
	Local nPosIpi  := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_ALIIPI"})
	Local nPValIPI := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_VALIPI"})
	Local nPosTes  := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_TES"})
	Local nPBaseIcm:= aScan(aHeader,{|x| AllTrim(x[2]) == "C8_BASEICM"})
	Local nPosIcm  := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_PICM"})
	Local nPValIcm := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_VALICM"})
	Local nPosEmb  := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_VALEMB"})
	Local nPosValid:= aScan(aHeader,{|x| AllTrim(x[2]) == "C8_VALIDA"})
	Local nPosNorc := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_ORCFOR"})
	Local nPosSeOr := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_SEQFOR"})
	Local nPosItOr := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_ITEFOR"})
	Local nPNumPro := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_NUMPRO"})
	Local nPItem   := aScan(aHeader,{|x| AllTrim(x[2]) == "C8_ITEM"})
	Local nPosDesc := aScan(aHeader,{|x| Alltrim(x[2]) == "C8_DESC"})
	Local nPosVlDesc:=aScan(aHeader,{|x| Alltrim(x[2]) == "C8_VLDESC"})
	Local nPosTipFre:=aScan(aHeader,{|x| Alltrim(x[2]) == "C8_TPFRETE"})
	Local nPosValFre:=aScan(aHeader,{|x| Alltrim(x[2]) == "C8_VALFRE"})
	Local nPosValDes:=aScan(aHeader,{|x| Alltrim(x[2]) == "C8_DESPESA"})
	Local nPosValSeg:=aScan(aHeader,{|x| Alltrim(x[2]) == "C8_SEGURO"})
	Local nPosBaseST:=aScan(aHeader,{|x| Alltrim(x[2]) == "C8_BASESOL"})
	Local nPosValIST:=aScan(aHeader,{|x| Alltrim(x[2]) == "C8_VALSOL"})
	Local nPosC8CF  :=aScan(aHeader,{|x| Alltrim(x[2]) == "C8_CF"})
	Local cFornece := ""
	Local cLoja    := ""
	Local cProposta:= if(nPNumPro > 0, aColsLine[1][nPNumPro], "")
	Local cNextPro := ""
	Local nTamItem := Len(SC8->C8_ITEM)
	Local cItem    := StrZero(0,nTamItem)
	Local lDelCot  := MV_PAR04==2 .And. nOpcx == 5
	Local lDelProd := MV_PAR04==3 .And. nOpcx == 5   
	Local lnSC     := .F.
	Local lnSCDel  := .F.
	Local bWhile   := {|| !Eof() .And. SC8->C8_FILIAL == xFilial("SC8") .And.;
							SC8->C8_NUM == cA150Num .And.;
							SC8->C8_FORNECE == cFornece .And.;
							SC8->C8_LOJA == cLoja }
	Local lPortFor := .F.
	Local aProp    := {}
	Local nProd	   := 0
	Local lTrbGen  := IIf(FindFunction("ChkTrbGen"), ChkTrbGen("SC8", "C8_IDTRIB"), .F.)
	DEFAULT bBlock	:= {|| .T.}     

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Adiciona a Nro Solicitacao/Item ao Array                      ³
	//|Somente se existir linhas excluidas no aCols 				 |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If aScan(aCols,{|x|(x[Len(aCols[1])])==.T.})>0
		lnSCDel:=.T.
		lDel150:=.T.
		If Len(aSCSel)>0
			nSC:=Len(aSCSel[1])+2
			For nX=1 To Len(aSCSel)
				aSize(aSCSel[nX],nSC) 
				nPos := aScan(aCols,{|x| AllTrim(x[nPosProd])==AllTrim(aSCSel[nX,5])})
				If nPos > 0
					aSCSel[nX][nSC-1]:= aCols[nPos][Len(aCols[1])] 	//Linha deletada ?  
					// O registro sera processado
					If !aCols[nPos][Len(aCols[1])]
						aadd(aRecno2,aRecno[nX]) //Inclui no array somente os itens que não foram deletados
					EndIf
				EndIf
				aSCSel[nX][nSC] := aSCSeL[nX][1]+aSCSeL[nX][2]		//Nr Sc + Item Sc
			Next nX
			If Len(aRecno2) > 0
				aRecno := aClone(aRecno2)
			EndIf
		EndIf
	EndIf

	aCols	:= {} // Recria o aCols detalhado

	Do Case
	Case ( (nOpcX == 5 .Or. nOpcX == 3) .And. (lDelCot .Or. lDelProd) )
		dbSelectArea("SC8")
		dbSetOrder(1)
		dbSeek(xFilial("SC8")+cA150Num)
		bWhile := {|| !Eof() .And. SC8->C8_FILIAL == xFilial("SC8") .And.;
		SC8->C8_NUM == cA150Num }
	Case ( nOpcX <> 2 )
		cFornece := cA150Forn
		cLoja    := cA150Loj
		dbSelectArea("SC8")
		dbSetOrder(1)
		dbSeek(xFilial("SC8")+cA150Num+cFornece+cLoja)
		cProposta:= SC8->C8_NUMPRO
	OtherWise
		bWhile := {|| !Eof() .And. SC8->C8_FILIAL == xFilial("SC8") .And.;
		SC8->C8_NUM == cA150Num }
		dbSelectArea("SC8")
		dbSetOrder(1)
		dbSeek(xFilial("SC8")+cA150Num)
	EndCase
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Efetua a montagem do Aheader e do Acols                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If MaFisFound()
		MaFisEnd()
	EndIf

	MaFisIni(cA150Forn,cA150Loj,"F","N",,,,,,,,,,,,,,,,,,,,,,,,,,,,,lTrbGen)

	nX :=0
	While ( Eval(bWhile) )
		If (lDelCot .Or. lDelProd) .Or. aScan(aSC8,{|x| x[1] == SC8->C8_PRODUTO.And.x[2]==SC8->C8_IDENT})==0
			If ((lDelCot .Or. lDelProd) .Or. SC8->C8_NUMPRO == cProposta )

				//Verifica se SC não foi marcada como deletada, se foi, não será considerada
				//Somente entrara na rotina se houver itens excluidos no acols
				If lnSCDel
					nSCPos:=aScan(aSCSel,{|x| AllTrim(x[nSC]) == (SC8->C8_NUMSC+SC8->C8_ITEMSC)})
					If nSCPos>0
						lnSC:=aSCSel[nSCPos][nSC-1]
					EndIf
				EndIf

				If ( SoftLock("SC8") .And. Eval(bBlock,"SC8")) .And. !lnSC
					AADD(aCols,Array(Len(aHeader)+1))
					nX++
					cItem := SC8->C8_ITEM				
					For nY := 1 To Len(aHeader)
						dbSelectArea("SC8")
						If IsHeadRec(aHeader[nY][2])
							aCols[nX][nY] := IIF(nOpcX == 3 .Or. (nOpcX == 5 .And. mv_par04 == 1) , SC8->(Recno()) , 0 )
						ElseIf IsHeadAlias(aHeader[nY][2])
							aCols[nX][nY] := "SC8"
						ElseIf ( aHeader[nY][10] != "V")
							If AllTrim(aHeader[nY][2]) == "C8_ITEM"
								aCols[nX][nY] := cItem 
							Else
								aCols[nX][nY] := FieldGet(FieldPos(aHeader[nY][2]))
							EndIf
						Else
							aCols[nX][nY] := CriaVar(aHeader[nY][2],.T.)
						EndIf
						aCols[nX][Len(aHeader)+1] := .F.

						If cPaisLoc <> "BRA"
							If ( Trim(aHeader[nY][2])=="C8_TAXAFIN" )
								aCols[nX][nY] := Iif(Empty(SC8->C8_TAXAFIN),GetMv("MV_JUROS"), SC8->C8_TAXAFIN)
							EndIf
						EndIf

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Replicar a TES digitada                                                 ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If ( Trim(aHeader[nY][2])=="C8_TES" )
							aCols[nX][nPosTes] := ""
						EndIf

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Provincia de Entrega do Fornecedor                                      ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If cPaisLoc == "ARG" .And. Trim(aHeader[nY][2])=="C8_PROVENT"
							aCols[nX][nY] := cA150Provent
						EndIf

					Next nY
					aadd(aSC8,{SC8->C8_PRODUTO,SC8->C8_IDENT})
				EndIf
			EndIf
		EndIf
		cNextPro := If(cNextPro>SC8->C8_NUMPRO,cNextPro,SC8->C8_NUMPRO)
		dbSelectArea("SC8")
		dbSkip()
	EndDo

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza o Campo Proposta                                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( nOpcX == 4 ) .and. nPNumPro > 0
		For nY := 1 To Len(aCols)
			aCols[nY][nPNumPro] := Soma1(cNextPro,1)
		Next nY
	EndIf

	//Classifica por item
	Asort(aCols,,,{|a,b| a[nPItem]<b[nPItem] })

	lPortFor := IsInCallStack("PUTQUOTE") // Veio do Portal do Fornecedor
	
	if lPortFor
		Asort(aColsLine,,,{|a,b| a[nPItem]<b[nPItem] })
	Endif 

	For nY := 1 to Len(aCols)

		If (nProd:= ascan(aProp,{|x| x[1] == aCols[nY][nPosProd]})) > 0
			aProp[nprod][2]++
		Else
			aadd(aProp,{aCols[nY][nPosProd],1})
		EndIf
	Next nY

	For nY := 1 to Len(aCols)
		nProd:= ascan(aProp,{|x| x[1] == aCols[nY][nPosProd]})
		//Ajusta o item, se necessário.Caso venha do portal do fornecedor, não ajustar o item 
		If lDel150
			cItem    := StrZero(nY,nTamItem)
			If !(aCols[nY,npItem] == cItem)
				aCols[nY,npItem] := cItem
			EndIf
		Endif

		nPosAcols := aScan(aColsLine,{|x|x[nPosProd] == aCols[nY][nPosProd]})
		If nPosAcols > 0
			If lPortFor
				nX := nY //caso venha do portal do fornecedor não tera agrupamento do aCols, então o nX deve ser igual ao contador
			Else
				nX := nPosAcols
			EndIf
			If aCols[nY][nPosProd] == aColsLine[nX][nPosProd]
				If nPosTab > 0
					aCols[nY][nPosTab]   := aColsLine[nX][nPosTab]
				Endif
				If nPosPreco > 0
					aCols[nY][nPosPreco] := aColsLine[nX][nPosPreco]
				Endif
				If nPosTotal > 0 .And. nPosQuant > 0 .And. nPosPreco > 0
					aCols[nY][nPosTotal] := aCols[nY][nPosQuant] * aColsLine[nX][nPosPreco]
				Endif
				If nPosPrazo > 0
					aCols[nY][nPosPrazo] := aColsLine[nX][nPosPrazo]
				Endif
				If nPosObser > 0 .And. ( Empty(aCols[nY][nPosObser]) .Or. lPortFor )
					aCols[nY][nPosObser] := aColsLine[nX][nPosObser]
				Endif
				If nPBaseIPI > 0
					aCols[nY][nPBaseIPI] := aColsLine[nX][nPBaseIPI] / aProp[nprod][2]
				EndIf
				If nPosIpi > 0
					aCols[nY][nPosIpi]   := aColsLine[nX][nPosIpi]
				Endif
				If nPValIPI > 0
					aCols[nY][nPValIPI] := aColsLine[nX][nPValIPI] / aProp[nprod][2]
				EndIf
				If nPosTes > 0
					aCols[nY][nPosTes]   := aColsLine[nX][nPosTes]
				Endif
				If nPosC8CF > 0 //para restaurar os impostos calculados pelo configurador de tributos.
					aCols[nY][nPosC8CF]   := aColsLine[nX][nPosC8CF]
				Endif
				If nPBaseIcm > 0
					aCols[nY][nPBaseIcm] := aColsLine[nX][nPBaseIcm] / aProp[nprod][2]
				EndIf
				If nPosIcm > 0
					aCols[nY][nPosIcm]   := aColsLine[nX][nPosIcm]
				Endif
				If nPValIcm > 0
					aCols[nY][nPValIcm] := aColsLine[nX][nPValIcm]  / aProp[nprod][2]
				EndIf
				If nPosValid > 0
					aCols[nY][nPosValid] := aColsLine[nX][nPosValid]
				Endif
				If nPosNorc > 0
					aCols[nY][nPosNorc] := aColsLine[nX][nPosNorc]
				Endif
				If nPosSeOr > 0
					aCols[nY][nPosSeOr] := aColsLine[nX][nPosSeOr]
				Endif
				If nPosItOr > 0
					aCols[nY][nPosItOr] := aColsLine[nX][nPosItOr]
				Endif
				If nPosEmb > 0
					aCols[nY][nPosEmb] := aColsLine[nX][nPosEmb]
				Endif
				If nPNumPro > 0
					aCols[nY][nPNumPro] := aColsLine[nX][nPNumPro]
				Endif
				If nPosDesc > 0
					aCols[nY][nPosDesc] := aColsLine[nX][nPosDesc]
				Endif
				If nPosVlDesc > 0
					aCols[nY][nPosVlDesc] := aColsLine[nX][nPosVlDesc]
				Endif
				If nPosTipFre > 0
					aCols[nY][nPosTipFre] := aColsLine[nX][nPosTipFre]
				EndIf
				If nPosValFre > 0
					aCols[nY][nPosValFre] := aColsLine[nX][nPosValFre]
				EndIf
				If nPosValDes > 0
					aCols[nY][nPosValDes] := aColsLine[nX][nPosValDes]
				EndIf
				If nPosValSeg > 0
					aCols[nY][nPosValSeg] := aColsLine[nX][nPosValSeg]
				EndIf
					If nPosValIST > 0
					aCols[nY][nPosValIST] := aColsLine[nX][nPosValIST]
				EndIf
				If nPosBaseST > 0
					aCols[nY][nPosBaseST] := aColsLine[nX][nPosBaseST] 
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Grava Campos Customizados do Usuario       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !(aHeaderPro == Nil) .And. Len(aHeaderPro) > 0
					For nH := 1 to Len(aHeader)
						If !(aHeader[nH, 10]=='V') .And. aHeaderPro[nH, 2] == 'U'
							If !lPortFor
								aCols[nY][nH] := aColsLine[nX][nH]
							ElseIf aScan(aAutoCab,{|x|Trim(x[1]) == AllTrim(aHeader[nH,2]) }) > 0 .Or.;	// Quando o campo de usuario nao era visivel no portal do fornecedor
							aScan(aAutoItens[1],{|x|Trim(x[1]) == AllTrim(aHeader[nH,2]) }) > 0		// estava apagando o conteudo do campo customizado ao gravar a cotacao
								aCols[nY][nH] := aColsLine[nX][nH]
							EndIf
						EndIf
					Next nH
				EndIf
			Endif
		Endif
	Next nY

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Atualizacao do array Fiscal                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Len(aCols) > 0 .And. !lPortFor
		MaColsToFis(aHeader,aCols,Nil,"MTA150",.F.,,,.T.)
	EndIf

	A150AtuRef("NF_FRETE"   ,a150Var[FRETE],.F.)
	A150AtuRef("NF_DESPESA" ,a150Var[VALDESP],.F.)
	A150AtuRef("NF_SEGURO"  ,a150Var[SEGURO],.F.)
	A150AtuRef("NF_DESCONTO",a150Var[VALDESC],.F.)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A150FV    ³ Autor ³ Eduardo Riera         ³ Data ³18.01.2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao que calcula o preco de compra com base no preco a vis³±±
±±³          ³ta e na taxa do fornecedor                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ExpN1 := A150FV(ExpN2,ExpN3)                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN2: Preco a Vista (OPC)                                  ³±±
±±³          ³ExpN3: Taxa do fornecedor (OPC)                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpN1: Preco futuro                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA150                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function a150FV(nPrcVis,nTaxaFor)

	Local aVencto  := {}
	Local nX       := 0
	Local nPPrcVis := aScan(aHeader,{|x| AllTrim(x[2])=="C8_AVISTA" })
	Local nPTxFor  := aScan(aHeader,{|x| AllTrim(x[2])=="C8_TAXAFOR" })
	Local nPPreco  := aScan(aHeader,{|x| AllTrim(x[2])=="C8_PRECO" })
	Local nValFV   := 0

	If nPPrcVis > 0 .And. nPTxFor > 0
		DEFAULT nPrcVis  := aCols[n][nPPrcVis]
		DEFAULT nTaxaFor := aCols[n][nPTxFor ]

		aVencto := Condicao(nPrcVis,cCondicao,0,dDataBase,0)

		For nX := 1 To Len(aVencto)
			nValFV += MaValPres(aVencto[nX][2],aVencto[nX][1],nTaxaFor,2)
		Next nX
		aCols[n][nPPreco] := nValFV
	EndIf
Return(nValFV)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A150PrcVis³ Autor ³ Eduardo Riera         ³ Data ³18.01.2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de validacao do preco a vista                        ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ExpL1 := A150PrcVis(ExpN1,ExpN2)                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Preco a Vista (OPC)                                  ³±±
±±³          ³ExpN2: Taxa do fornecedor (OPC)                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Sempre .T.                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA150                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function a150PrcVis(nPrcVis,nTaxaFor)

	Local aVencto  := {}
	Local nX       := 0
	Local nPTotal  := aScan(aHeader,{|x| AllTrim(x[2])=="C8_TOTAL" })
	Local nPQuant  := aScan(aHeader,{|x| AllTrim(x[2])=="C8_QUANT" })
	Local nPPrcVis := aScan(aHeader,{|x| AllTrim(x[2])=="C8_AVISTA" })
	Local nPTxFor  := aScan(aHeader,{|x| AllTrim(x[2])=="C8_TAXAFOR" })
	Local nPPreco  := aScan(aHeader,{|x| AllTrim(x[2])=="C8_PRECO" })
	Local nPVlDesc := aScan(aHeader,{|x| AllTrim(x[2])=="C8_VLDESC" })
	Local nPDesc   := aScan(aHeader,{|x| AllTrim(x[2])=="C8_DESC" })
	Local nValFV   := 0

	If nPPrcVis > 0 .And. nPTxFor > 0
		DEFAULT nPrcVis  := aCols[n][nPPrcVis]
		DEFAULT nTaxaFor := aCols[n][nPTxFor ]

		If nPrcVis > 0
			aVencto := Condicao(nPrcVis,cCondicao,0,dDataBase,0)

			For nX := 1 To Len(aVencto)
				nValFV += MaValPres(aVencto[nX][2],aVencto[nX][1],nTaxaFor,2)
			Next nX
			aCols[n][nPPreco] := NoRound(nValFV,TamSx3("C8_PRECO")[2])
			If MaFisFound("IT",N)
				MaFisRef("IT_PRCUNI","MT150",nValFV)
			EndIf
			If Abs(aCols[n][nPTotal]-NoRound(aCols[n][nPPreco]*aCols[n][nPQuant],TamSx3("C8_TOTAL")[2]))<>0.09
				aCols[n][nPTotal] := NoRound(aCols[n][nPPreco]*aCols[n][nPQuant],TamSx3("C8_TOTAL")[2])
				If MaFisFound("IT",N)
					MaFisRef("IT_VALMERC","MT150",aCols[n][nPTotal])
				EndIf
			EndIf
			If aCols[n][nPDesc]>0
				aCols[n][nPvlDesc] := NoRound(aCols[n][nPTotal]*aCols[n][nPDesc]/100,TamSx3("C8_VLDESC")[2])
				If MaFisFound("IT",N)
					MaFisRef("IT_DESCONTO","MT150",aCols[n][nPVlDesc])
				EndIf
			Else
				Eval(bCalcDesc)
			EndIf
		EndIf
	EndIf
Return(.T.)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A150Recal ³ Autor ³ Eduardo Riera         ³ Data ³18.01.2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de recalculo dos itens da cotacao                    ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ExpL1 := A150Recal()			                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Sempre .T.                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA150                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function a150Recal()

	Local nSavN := N
	Local nX    := 0

	For nX := 1 To Len(aCols)
		N := nX
		a150PrcVis()
	Next nX

	N := nSavN

Return(.T.)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³CalcDesc  ³ Autor ³ Cristina Ogura    	³ Data ³ 24/05/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Calcula descontos em cascatas de um valor.	              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ExpN1 := CalcDesc(ExpN2,ExpN3,ExpN4,ExpN5)       	          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN2: valor base a ser calculado o desconto           	  ³±±
±±³ 	     ³ExpN3: desconto 1                           				  ³±±
±±³ 	     ³ExpN4: desconto 2     	       				              ³±±
±±³ 	     ³ExpN5: desconto 3         	   				              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpN1: valor do desconto   			                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA150					                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function CalcDesc( nValor, nDesc1, nDesc2, nDesc3)
	Local nTotDesc:= 0

	nTotDesc := NoROUND(nValor*nDesc1/100,2)
	nTotDesc := nTotDesc + ROUND((nValor-nTotDesc)*nDesc2/100,2)
	nTotDesc := nTotDesc + ROUND((nValor-nTotDesc)*nDesc3/100,2)

Return (nTotDesc)

/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Programa  ³MenuDef   ³ Autor ³ Fabio Alves Silva     ³ Data ³06/11/2006³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³Parametros do array a Rotina:                               ³±±
	±±³          ³1. Nome a aparecer no cabecalho                             ³±±
	±±³          ³2. Nome da Rotina associada                                 ³±±
	±±³          ³3. Reservado                                                ³±±
	±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
	±±³          ³	  1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
	±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
	±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
	±±³          ³    4 - Altera o registro corrente                          ³±±
	±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
	±±³          ³5. Nivel de acesso                                          ³±±
	±±³          ³6. Habilita Menu Funcional                                  ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
	Local lClicB	:= A131VerInt()
	Local aClicB	:={}
	Local aConhec 	:= {} 

	Aadd(aConhec, { STR0102 , "MsDocument", 0, 4, 0, .F.}) // "Conhecimento Fornecedor"
	Aadd(aConhec, { STR0103 , "A150MsDoc", 0, 4, 0, .F.}) // "Conhecimento Portal"

	PRIVATE aRotina	:= {{STR0008, "PesqBrw"  , 0 , 6, 0, .F.},;    //"Pesquisar" 
	{ STR0009 , "A150Digita" , 0 , 6, 0, Nil},;   //"Novo Particip."
	{ STR0010 , "A150Digita" , 0 , 6, 0, Nil},;   //"Atualizar"
	{ STR0011 , "A150Propos" , 0 , 6, 0, Nil},;   //"pRoposta"
	{ STR0012 , "A150Digita" , 0 , 5, 0, Nil},;   //"Excluir"
	{ STR0034 , "A150Totais" , 0 , 2, 0, Nil},;   //"Total do Pedido"
	{ STR0100 , aConhec	     , 0 , 8, 0, Nil},;	  //"Conhecimento"
	{ STR0057 , "A150Legenda", 0 , 1, 0, .F.}}    //"Legenda"

	If lClicB
		AAdd(aClicB,{ STR0094, "A131CalCom"  , 0 , 6, 0, .F.})//"Portal ClicBusiness"
		AAdd(aClicB,{ STR0095, "A150ExcCli"  , 0 , 6, 0, .F.})//"Exclui ClicBusiness"
		AAdd(aRotina,	{ STR0096 , aClicB, 0 , 6, 0, nil})  		   //"ClicBusiness"
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ponto de entrada para inclusão de novos itens no menu aRotina³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock("MT150ROT")
		aRotina := ExecBlock("MT150ROT",.F.,.F.,aRotina)
	Endif

Return(aRotina)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³A150Replace³ Autor ³Alexandre Inacio Lemes³ Data ³03/07/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Replica o preco unitario entre os demais produtos da mesma  ³±±
±±³          ³grade quando o produto for um produto de grade C8_GRADE ='S'³±±
±±³          ³O recurso e um facilitador assim como o Grid de Grade, a    ³±±
±±³          ³replica atingira todos os produtos com o mesmo Item C8_ITEM ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Exp1 :                                                 	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Exp1 :                     			                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA150					                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A150Replace()

	Local aArea		:= GetArea()
	Local nPosItem  := aScan(aHeader,{|x| AllTrim(x[2])=="C8_ITEM"})
	Local nPosProd	:= aScan(aHeader,{|x| AllTrim(x[2])=="C8_PRODUTO"})
	Local nPosQuant	:= aScan(aHeader,{|x| AllTrim(x[2])==Iif(_lPGC,"C8_QTDISP","C8_QUANT")})
	Local nPosPreco	:= aScan(aHeader,{|x| AllTrim(x[2])=="C8_PRECO"})
	Local nPosTotal	:= aScan(aHeader,{|x| AllTrim(x[2])=="C8_TOTAL"})
	Local nPosGrade	:= aScan(aHeader,{|x| AllTrim(x[2])=="C8_GRADE"})
	Local nX		:= 0
	Local nSaveN    := N
	Local cFamilyPrd:= ""
	Local cItFamily := aCols[n][nPosItem]
	Local oDlg
	Local lReplicar := .F.

	PRIVATE nPrcRepl := 0

	If aCols[n][nPosGrade] == "S"
		cFamilyPrd := aCols[n][nPosProd]
		MatGrdPrrf(@cFamilyPrd, .T.)

		DEFINE FONT oBold NAME "Arial" SIZE 000,-12 BOLD
		DEFINE MSDIALOG oDlg FROM 000,000 TO 200,355 TITLE STR0076 OF oMainWnd PIXEL //"Replica de Preços do Produto de Grade"
		@ 020,010 SAY STR0077            OF oDlg PIXEL SIZE 245,009 //"Familia de Produtos - "
		@ 020,108 SAY STR0078            OF oDlg PIXEL SIZE 245,009 //"Item da Cotação - "
		@ 020,063 SAY cFamilyPrd         OF oDlg PIXEL SIZE 245,009 FONT oBold
		@ 020,153 SAY aCols[n][nPosItem] OF oDlg PIXEL SIZE 245,009 FONT oBold
		@ 040,010 SAY STR0079             OF oDlg PIXEL SIZE 245,009 //"Preço Unitário a ser Replicado "
		@ 040,095 MSGET nPrcRepl PICTURE PesqPict("SC8","C8_PRECO") Valid CheckSX3("C8_PRECO",nPrcRepl) OF oDlg PIXEL SIZE 074,006

		@ 070,125 BUTTON STR0080 SIZE 045,010 FONT oDlg:oFont ACTION (lReplicar :=.T.,oDlg:End()) OF oDlg PIXEL  //"Replicar"
		@ 070,070 BUTTON STR0081 SIZE 045,010 FONT oDlg:oFont ACTION (oDlg:End()) OF oDlg PIXEL  //"Cancelar"

		ACTIVATE MSDIALOG oDlg CENTERED

		If lReplicar

			For nX :=1 to Len(aCols)

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Se o Item do aCols for Igual ao Item da Familia Selecionada  ³
				//³ atualiza o aCols com o novo Preco e a Funcao Fiscal.         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If aCols[nX][nPosItem] == cItFamily

					aCols[nX][nPosPreco]:= nPrcRepl

					N := nX

					If MaFisFound("IT",nX)
						MaFisRef("IT_PRCUNI","MT150",nPrcRepl)
					EndIf

					If Abs(aCols[nX][nPosTotal]-NoRound(aCols[nX][nPosPreco]*aCols[nX][nPosQuant],TamSx3("C8_TOTAL")[2]))<>0.09
						aCols[nX][nPosTotal] := NoRound(aCols[nX][nPosPreco]*aCols[nX][nPosQuant],TamSx3("C8_TOTAL")[2])
						If MaFisFound("IT",nX)
							MaFisRef("IT_VALMERC","MT150",aCols[nX][nPosTotal])
						EndIf
					EndIf

					If ExistTrigger("C8_PRECO")
						RunTrigger(2,nX)
					EndIf

				EndIf

			Next nX

			Eval(bRefresh)
			Eval(bGDRefresh)

		EndIf
	Else
		Aviso(STR0021,STR0082,{STR0083},2) //"Atencao"###"O produto selecionado não é um produto de grade, Este recurso deve ser executado em produtos com Grade"
	EndIf

	N := nSaveN

	RestArea(aArea)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ProvEntCt ³ Autor ³ Ricardo Berti         ³ Data ³ 09/09/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Atualiza os itens da Cotacao e Valida Provincia  			  ³±±
±±³          ³ informada no cabecalho.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lRet                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ProvEntCt()
	Local aRefImpos:= {}
	Local lRet	   := .T.
	Local nX	   := 0
	Local nPosProv := Ascan(aHeader,{|x| Alltrim(x[2]) == "C8_PROVENT"})
	Local nPosTes  := Ascan(aHeader,{|x| Alltrim(x[2]) == "C8_TES"})
	Local cCpo     := ""
	Local cProv    := ""
	Local cTes     := ""
	Local nProv    := 0
	Local lXFIS	   := MaFisFound("NF")

	If !lXFIS
		aRefImpos	:= MaFisRelImp('MT100',{"SC8"})
		MaFisIni(ca150Forn,ca150Loj,"F","N",Nil,aRefImpos,,.T.)
	EndIf

	lRet := Vazio() .Or. cA150ProvEnt == "99" .Or. ExistCpo("SX5","12"+cA150ProvEnt)
	If lRet .And. nPosProv > 0
		cCpo  := ReadVar()
		cProv := &cCpo
		For nX := 1 to Len(aCols)
			cTes := aCols[n,nPosTes]
			If VerProEnIt(cProv,cTes,.F.,.F.)
				aCols[nX,nPosProv]:= cProv
				If lXFIS
					MaFisAlt("IT_PROVENT",cProv,nX)
				Else
					MaColsToFis(aHeader,aCols,nX)
					lXFIS := .T.
				Endif
			Else
				nProv++
			Endif
		Next
		Eval(bGDRefresh)
		If nProv > 0
			MsgAlert(STR0085,STR0021)//("Alguns itens não tiveram a província alterada pois possuem impostos gravados em um mesmo campo.","Atenção")
		Endif
	Endif
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³a150ProEnt³ Autor ³ Ricardo Berti         ³ Data ³ 09/09/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Atualiza os itens da Cotacao e Valida Provincia             ³±±
±±³          ³ informada no cabecalho.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lRet                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function a150ProEnt()
	Local aRefImpos	:= {}
	Local cProvAnt	:= ""
	Local nPosProv	:= Ascan(aHeader,{|x| Alltrim(x[2]) == "C8_PROVENT"})
	Local nPosTes	:= Ascan(aHeader,{|x| Alltrim(x[2]) == "C8_TES"})
	Local nX		:= 0
	Local nProv		:= 0
	Local lXFIS		:= MaFisFound("NF")

	If !lXFIS
		aRefImpos	:= MaFisRelImp('MT100',{"SC8"})
		MaFisIni(ca150Forn,ca150Loj,"F","N",Nil,aRefImpos,,.T.)
	EndIf

	If cPaisLoc == "ARG"
		SA2->(dbSetOrder(1))
		If SA2->(MsSeek(xFilial("SA2")+cA150Forn))
			cProvAnt	 := cA150ProvEnt
			cA150ProvEnt := SA2->A2_EST // Provincia de Entrega do Fornecedor
			If nPosProv > 0
				If cProvAnt <> cA150ProvEnt
					For nX := 1 to Len(aCols)
						cTes := aCols[n,nPosTes]
						If VerProEnIt(cA150ProvEnt,cTes,.F.,.F.)
							aCols[nX,nPosProv]:= cA150ProvEnt
							If lXFIS
								MaFisAlt("IT_PROVENT",cA150ProvEnt,nX)
							Else
								MaColsToFis(aHeader,aCols,nX)
								lXFIS := .T.
							Endif
						Else
							nProv++
						Endif
					Next
					Eval(bGDRefresh)
					If nProv > 0
						MsgAlert(STR0085,STR0021)//("Alguns itens não tiveram a província alterada pois possuem impostos gravados em um mesmo campo.","Atenção")
					Endif
				Endif
			Endif
		Endif
	Endif
Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ProEntItCt³ Autor ³ Ricardo Berti         ³ Data ³ 09/09/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Atualiza os itens da Cotacao e Valida Provincia   		  ³±±
±±³          ³ informada no cabecalho.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lRet                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ProEntItCt()
	Local aRefImpos:= {}
	Local lRet	   := .T.
	Local nX	   := 0
	Local nPosTes  := Ascan(aHeader,{|x| Alltrim(x[2]) == "C8_TES"})
	Local nPosProv := Ascan(aHeader,{|x| Alltrim(x[2]) == "C8_PROVENT"})
	Local cCpo     := ""
	Local cProv    := ""
	Local lXFIS	   := MaFisFound("NF")

	If !lXFIS
		aRefImpos  := MaFisRelImp('MT100',{"SC8"})
		MaFisIni(ca150Forn,ca150Loj,"F","N",Nil,aRefImpos,,.T.)
	EndIf

	If nPosTes > 0
		cCpo  := ReadVar()
		cProv := &cCpo
		lRet  := ValProvEnt(cProv,aCols[n,nPosTes])
		If lRet .And. nPosProv > 0
			aCols[n,nPosProv]:= cProv
			If lXFIS
				MaFisAlt("IT_PROVENT",cProv,n)
			Else
				MaColsToFis(aHeader,aCols,n)
				lXFIS := .T.
			Endif
		Endif
		Eval(bGDRefresh)
	Endif
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ProvTesCt ³ Autor ³ Ricardo Berti         ³ Data ³ 09/09/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida o TES na cotacao de compras						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lRet                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ProvTesCt()
	Local lRet		:= .T.
	Local nPosProv  := Ascan(aHeader,{|x| Alltrim(x[2]) == "C8_PROVENT"})
	Local cCpo		:= ""
	Local cProv		:= ""

	cCpo  := ReadVar()
	cTes := &cCpo
	If nPosProv > 0
		cProv := aCols[n,nPosProv]
		lRet := VerProEnIt(cProv,cTes,.T.,.F.)
		If lRet
			MaFisAlt("IT_PROVENT",cProv,n)
		EndIf
	Endif
Return lRet


/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A1502FC030  ³Autor³ Ricardo Berti         ³ Data ³13.10.2008³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Compatibilizacao de variaveis utilizadas no FINC030.        ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³Nenhum                                                      ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³MATA150                                                     ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A1502FC030()
	A120ToFC030()
	Pergunte("MTA150",.F.)
Return .T.

/*/
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡„o    ³A150ValCond  ³ Rev ³ Totvs S/A            ³ Data ³24.05.2010 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³          ³Funcao para validar a Cond.Pagamento                         ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Parametros³ExpC1: Condicao de Pagamento                                 ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³Valor Logico .T. / .F.                                       ³±±
	±±³          ³                                                             ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡„o ³Validar a Condicao de pagamento							   ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Uso       ³ Materiais                                                   ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150ValCond(cCondicao)
	Local aAreaAnt := GetArea()
	Local aAreaSE4 := SE4->(GetArea())
	Local lRet     := .F.
	Local lRet1    := .F.

	If ExistBlock("MT120VCP9")
		lRet1 := ExecBlock("MT120VCP9",.F.,.F.)
		If ValType(lRet) <> "L"
			lRet1 := .F.
		EndIf
	EndIf

	dbSelectArea("SE4")
	dbSetOrder(1)
	If ( !Empty(cCondicao) .And. ExistCpo("SE4",cCondicao) )
		MsSeek(xFilial("SE4")+cCondicao)
		If SE4->E4_TIPO == "9"
			If !lRet1
				Help(" ",1,"A150VCOND")
				lRet := .F.
			Else
				lRet := .T.
			EndIf
		Else
			lRet := .T.
		EndIf
	EndIf

	RestArea(aAreaSE4)
	RestArea(aAreaAnt)
Return(lRet)

/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A150TxMoeda ³Autor³ TOTVS			        ³ Data ³14.10.2010³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Retorna Taxa da Moeda									      ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A150TxMoeda(nMoedaCot,oGetTaxa)
	Local nValTX :=0
	If oGetTaxa != Nil .And. cPaisLoc != "BRA"
		If nTxMoeda==0
			nValTx:=RecMoeda(dDataBase,nMoedaCot)
		EndIf
	EndIf

Return nValTX

/*/
	ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
	±±³Fun‡…o    ³A150Quant³ Autor ³ Aline S Damasceno     ³ Data ³25.07.2012³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Descri‡…o ³Validacao da quantidade digitada no Portal do Fornecedor   ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Sintaxe   ³A150Quant(ExpN1)                                           ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³Retorno   ³.T. se total validado, caso contrario .F.                  ³±±
	±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
	±±³ Uso      ³ Dicionario de Dados - Campo:C8_TOTAL                      ³±±
	±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
	±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
	ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A150Quant(nQuant)

	Local aArea		:= GetArea()
	Local oModel    := FwModelActive()
	Local lNFC      := A150GetPGC() .And. ValType(oModel) == 'O' .And. oModel:GetId() == 'NFCA020' // Verifico se a origem é Legado ou NFC
	Local nPosItem	:= iif( !lNFC, aScan(aHeader,{|x|x[2] = "C8_ITEM"}), 0 )
	Local lRet 		:= .T.
	Local lAuto     := Type("l150Auto")=="L"

	if !lNFC
		If lAuto .And. IsInCallStack("PUTQUOTE") .And. Type("aAutoCab") == "A"
			DbSelectArea("SC8")
			DbSetOrder(1)
			MsSeek(xFilial("SC8")+aAutoCab[ProcH("C8_NUM"),2]+aAutoCab[ProcH("C8_FORNECE"),2]+aAutoCab[ProcH("C8_LOJA"),2]+aCols[n][nPosItem])

			If QtdComp(nQuant)<> QtdComp(SC8->C8_QUANT)
				Help(" ",1,"A150001")
				lRet := .F.
			EndIf

		EndIf
	else
		lRet := NF020QtDisp()
	endif
	RestArea(aArea)
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} a150FWhen
Função para ativar ou desativar o campo Nome e Email do Fornecedor.

@author alexandre.gimenez
@since 21/10/2013
@return lRet
/*/
//-------------------------------------------------------------------
Function a150FWhen()
	Local lRet 	:= .T.
	Local oModel 	:= FWModelActive()

	If  IsInCallStack("A150Digita")
		lRet :=  Empty(cA150Forn)
	EndIf

	If  IsInCallStack("A130Proces")
		lRet :=  Empty(oModel:GetModel("SC8DETAIL"):GetValue("C8_FORNECE"))
	EndIf


Return lRet

	//-------------------------------------------------------------------
	/*/{Protheus.doc} A150ExcCli()
	Chamada da Exclusão da cotação no ClicBusiness

	@author flavio.lopes
	@since 23/04/2014

	/*/
//-------------------------------------------------------------------
Function A150ExcCli()
	If MSGYESNO(STR0097, STR0098)//"Deseja realmente excluir a cotação do portal ClicBusiness?"#"Exclusão ClicBusiness"
		A311RegCot(SC8->C8_NUM,3,.T.)
	Endif
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} A150ValFre
Função para validação do tipo de frete

@author Manuela Cavalcante 
@since 18/08/2016
@version
/*/
//-------------------------------------------------------------------
Function A150ValFre()

	Local nValFrete := &(readvar())
	Local nPosFrete := 0
	Local lRet      := .T.
	Local lPortal   := IsInCallStack("PUTQUOTE")
	Local lTpInvalid:= .F.
	Local oModel    := FwModelActive()
	Local lNFC      := A150GetPGC() .And. ValType(oModel) == 'O' .And. oModel:GetId() == 'NFCA020' // Verifico se a origem é Legado ou NFC

	if !lNFC
		If lPortal
			If ( nPosFrete := aScan(aAutoItens[N],{|x| AllTrim(x[1]) == "C8_TPFRETE"}) ) > 0
				If Substr(aAutoItens[N][nPosFrete][2],1,1) != "C"
					lTpInvalid := .T.
				EndIf
			EndIf
		EndIf

		If ( ( !lPortal .And. Substr(c150Frete,1,1) <> 'C' ) .Or. ( lPortal .And. lTpInvalid ) ) .And. nValFrete <> 0
			Help(1, " ", "A150VALFRE",, STR0106, 3, 0)
			lRet := .F.
			MaFisAlt("NF_FRETE",0)
			Eval(bRefresh)
		EndIf
	endif

Return (lRet)

/*/{Protheus.doc} A150INICPAD
Inicilizador padrão e browse do campo C8_DESCRI

@author Rodrigo Pontes
@since 23/06/18
@version
/*/

Function A150INICPAD(cOpc)

	Local cRet	:= ""

	If cOpc == "P" //Inicializador Padrao
		cRet := IF(!INCLUI,POSICIONE("SC1",1,SC8->C8_FILIAL+SC8->C8_NUMSC+SC8->C8_ITEMSC,"SC1->C1_DESCRI")  ,"")
	Elseif cOpc == "B" //Inicializador Browse
		cRet := POSICIONE("SC1",1,SC8->C8_FILIAL+SC8->C8_NUMSC+SC8->C8_ITEMSC,"SC1->C1_DESCRI")
	Endif

Return cRet

/*/{Protheus.doc} A150Doc
Função ponte para chamada da MsDocument com parâmetros

@author leonardo.magalhaes
@since 07/12/20
@version 1.0
/*/
Function A150MsDoc(cAlias, nRecno, nOpc)
	
	MsDocument("SC8P", nRecno, nOpc)

Return Nil

/*/{Protheus.doc} ComMetric
	Média de itens utilizados na Cotação via <FWCustomMetrics>
@author rd.santos
@since 28/05/2021
@return Nil, indefinido
/*/
Static Function ComMetric(nQuant)
Local cIdMetric		:= "compras-protheus_media-itens-cotacao_average"
Local cRotina		:= "mata150"
Local cSubRoutine	:= cRotina+'-media'
Local lContinua		:= (FWLibVersion() >= "20210517") .And. FindClass('FWCustomMetrics')

If lContinua
	FWCustomMetrics():setAverageMetric(cSubRoutine, cIdMetric, nQuant, /*dDateSend*/, /*nLapTime*/,cRotina)
Endif

Return

/*/{Protheus.doc} A150SetPGC
	Seta se a chamada da rotina é oriunda do PGC.
@author juan.felipe
@since 09/02/2023
@return Nil, indefinido.
/*/
Function A150SetPGC(lPGC)
    _lPGC := lPGC
Return Nil

/*/{Protheus.doc} A150GetPGC
	Obtém conteúdo da variável _lPGC
@author juan.felipe
@since 09/02/2023
@return Nil, indefinido.
/*/
Function A150GetPGC()
Return _lPGC

/*/{Protheus.doc} A150HEADER
	Montagem do aHeader da SC8 considerando campos virtuais.
@author Leandro Fini
@since 03/2025
@return Nil, indefinido.
/*/
Static Function A150HEADER(cAliasTmp,aNotField,lVirtual)   

Local aRetHead		:= {}
Local aAllFields    := {}
Local lInclui		:= .T.
Local nX			:= 0
Local aAreaSX3		:= SX3->(GetArea())

Default	aNotField	:= {}
Default lVirtual    := .F.

aAllFields	:= FWSX3Util():GetListFieldsStruct(cAliasTmp, lVirtual)

SX3->(DbSetOrder(2))
For nX := 1 To Len(aAllFields)
	If X3Uso(GetSx3Cache(aAllFields[nX,1],'X3_USADO'))
		lInclui := .T.
		
		if Len(aNotField) > 0
			lInclui := aScan(aNotField,{|x| AllTrim(x) == AllTrim(aAllFields[nX,1])}) == 0
		Endif
		
		If lInclui
			SX3->(DbSeek(aAllFields[nX,1]))
			aAdd(aRetHead,{ TRIM(X3Titulo()),;
							GetSx3Cache(aAllFields[nX,1],'X3_CAMPO'),;
							GetSx3Cache(aAllFields[nX,1],'X3_PICTURE'),;
							GetSx3Cache(aAllFields[nX,1],'X3_TAMANHO'),;
							GetSx3Cache(aAllFields[nX,1],'X3_DECIMAL'),;
							GetSx3Cache(aAllFields[nX,1],'X3_VALID'),;
							GetSx3Cache(aAllFields[nX,1],'X3_USADO'),;
							GetSx3Cache(aAllFields[nX,1],'X3_TIPO'),;
							GetSx3Cache(aAllFields[nX,1],'X3_F3'),;
							GetSx3Cache(aAllFields[nX,1],'X3_CONTEXT')})
		Endif
	Endif
Next nX

RestArea(aAreaSX3)
FwFreeArray(aAreaSX3)

Return aRetHead
