#include "totvs.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include 'fwlibversion.ch'
#include "totvs.framework.treports.integratedprovider.th"
#include 'backoffice.sv.com.integratedprovider.ch'

namespace totvs.protheus.backoffice.com.smartView.integratedProvider

//-------------------------------------------------------------------
/*/{Protheus.doc} ComIntegratedProvider
    Classe de Integrated Provider que incorpora as principais soluções  de compras.
    Será a Super classe dos objetos de negócio do módulo.
    @author SQUAD SUPRIMENTOS
    @since 31/08/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------

class ComIntegratedProvider from totvs.framework.treports.integratedprovider.IntegratedProvider

	public method new()					as object
	public method getData()				as object

	protected data aField				as array
	protected data aFieldCustom			as array
	protected data aStruct				as array

	protected data cAlias				as character
	protected data cMinimumLibLabel		as character
	protected data cMinimumLibLookUp	as character
	protected data cSelectComplement	as character
	protected data cSX1Group			as character
	protected data cWhereComplement		as character

	protected data jCustomPar			as json
	protected data jData				as json
	protected data jStatement			as json

	protected data lPergunteOK			as logical
	protected data lLibOK				as logical
	protected data lLibLookUpOK	    	as logical

	protected data oLGPD				as object
	protected data oCurrentFilter		as object

	protected method addLGPDCalcField()
	protected method addToData()
	protected method comAddProperty()
	protected method comAddNestedProperty()
    protected method enableFilterByBranch()
	protected method getAllFieldsToSQL()	as character
	protected method getCustomFieldsToSQL() as character
	protected method getCustomParam()
	protected method getInfoEmp()
    protected method getPictureCGC()
	protected method getStructCustom()
	protected method loadLGPDFields()
	protected method setAliasToData()
	protected method processAndAppend()
	protected method processCustomFields()
	protected method processSQLStatement()
    protected method setContentToData()
	protected method setCustomInfoToData()
    protected method setSchema()
    protected method setErrorInvalidPergunte()
    protected method setErrorInvalidLib()
    protected method setEmptyContent()
	protected method setFilter()
	protected method setFilterOnWhere()
	protected method setMVPAR()
	protected method setPergunte()			as logical
	protected method setValueMVPAR()
    protected method validEnvironment()
	protected method varToTimeStamp()		as variant

endclass


//-------------------------------------------------------------------
/*/{Protheus.doc} new
    @author SQUAD SUPRIMENTOS
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method New() as object class ComIntegratedProvider

	_Super:New()

	self:appendArea(STR0001) //"Compras"

	self:aStruct				:= {}
	self:aField					:= {}
	self:aFieldCustom			:= {}

	self:cAlias					:= ""
	self:cMinimumLibLabel		:= "20230920"
    self:cMinimumLibLookUp      := "20231121"
	self:cSelectComplement		:= ""
	self:cWhereComplement		:= ""

	self:jCustomPar				:= JsonObject():New()
	self:jData					:= JsonObject():New()
	self:jStatement				:= JsonObject():New()

	self:lLibOK					:= FwLibVersion() >= self:cMinimumLibLabel
	self:lLibLookUpOK			:= FwLibVersion() >= self:cMinimumLibLookUp
	self:lPergunteOK			:= .T.

	self:oLGPD					:= SmartViewComLGPD():New()    

return self


//-------------------------------------------------------------------
/*/{Protheus.doc} setPergunte
    Utiliza polimorfismo para guardar o grupo de perguntas que o 
    objeto de negócio tentou usar, e guardar o resultado do setPergunte original, 
    sendo usado depois na validEnvironment.
    @author SQUAD SUPRIMENTOS
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method setPergunte(cPergunte as character) as logical class ComIntegratedProvider

	self:cSX1Group		:= cPergunte
	self:lPergunteOK	:= _Super:setPergunte(cPergunte)

return self:lPergunteOK


//-------------------------------------------------------------------
/*/{Protheus.doc} setMVPAR
    Atualiza as variáveis públicas MV_PAR de acordo com os parâmetros
    informados pelo usuário na interface do Smart View.
    @author SQUAD SUPRIMENTOS
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method setMVPAR(oFilter as object) class ComIntegratedProvider    

default oFilter := self:oCurrentFilter

	if !(oFilter == NIL)
		self:setValueMVPAR(oFilter:getParameters(), self:cPergunte)
	endIf

return


//-------------------------------------------------------------------
/*/{Protheus.doc} setSchema
    Devido aos campos dos ON localizados serem apresentados antes
    dos campos do ON genérico, esta classe resolverá essa questão de
    ordenação. Onde o método aliasToSchema será invocado primeiro
    para os campos do genérico, e se exisitir o mesmo alias no ON
    localizado, o mesmo será carregado posteriormente.
@author SQUAD SUPRIMENTOS
@since 06/05/2024
@version 12.1.2210
*/
//-------------------------------------------------------------------
method setSchema() class ComIntegratedProvider

local cAlias  as character
local nPos  as numeric
local nX    as numeric
    
    for nX := 1 to len(self:aField)
        cAlias := self:aField[nX,1]
        self:aliasToSchema(cAlias, self:aField[nX,2])

        nPos := aScan(self:aFieldCustom,{|x| x[1] == cAlias })
        if nPos > 0
            self:aliasToSchema(cAlias, self:aFieldCustom[nPos,2])
        endif
    next nX
    
return


//-------------------------------------------------------------------
/*/{Protheus.doc} processAndAppend
    Faz o processamento dos dados do JData e o append no objeto oData.
    Chama os métodos responsáveis pelo complemento dos dados no MI,
    no objeto extendido do compras, o ofuscamento de dados protegidos/sendiveis
    conforme LGPD e qualquer outro tratamento que deve ser feito nos dados,
    antes de entregar para a classe do Framework montar o JSON que será entregue
    ao Smart View.
    @author SQUAD SUPRIMENTOS
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method processAndAppend() class ComIntegratedProvider
	
	self:processData()			// Manipula jData antes do append, criado pelo Framework para uso do MI
	self:processCustomFields()	// Manipula jData antes do append, incluindo o conteúdo dos campos personalizados
	self:loadLGPDFields()
	self:jData := self:oLGPD:handleDataWithLGPD(self:jData)
	self:oData:appendData(self:jData)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} varToTimeStamp
    Chama a função do frame que faz o tratamento de campos tipo data
    para o formato adequado à API. Campos vazios devem preencher com NULO 
    o JSON do getData().
    @author SQUAD SUPRIMENTOS
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------

method varToTimeStamp(xDate as variant) as variant class ComIntegratedProvider

local cRet  as character

	if valType(xDate) == "D"
		cRet :=  totvs.framework.treports.date.dateToTimeStamp( xDate )
	elseif valType(xDate) == "C"
		cRet :=  totvs.framework.treports.date.stringToTimeStamp( xDate )
	endif

return cRet

//-------------------------------------------------------------------
/*/{Protheus.doc} loadLGPDFields
    Usa os campos do esquema para analisar quais campos serão ofuscados
    no objeto de negócio, dependendo do usuário ativo.
    Os campos calculados, adicionados pelo método addLGPDCalcFields também
    serão analisados aqui.
    @author SQUAD SUPRIMENTOS
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method loadLGPDFields() class ComIntegratedProvider
    
	self:oLGPD:loadFields(self:oSchema:getProperties())

return

//-------------------------------------------------------------------
/*/{Protheus.doc} addLGPDCalcField
    Adiciona campos calculados à classe de tratamento LGPD, pois esses
    campos precisam ser vinculados a algum campo do dicionário de dados
    para que o sistema saiba se deve ofuscar ou não.
    @author SQUAD SUPRIMENTOS
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------

method addLGPDCalcField(cCalcFieldName as character, cRealName as character) class ComIntegratedProvider
    self:oLGPD:addCalcField(cCalcFieldName, cRealName)
return


/*/{Protheus.doc} setValueMVPAR()
    Seta os valores para os MV_PAR publicas.
    @author SQUAD SUPRIMENTOS
    @since 20/04/2023
    @version 1.0
    @return Nil
/*/
method setValueMVPAR(oJsonMvPar As Json, cPergunte as Character) class ComIntegratedProvider

local oJson     as json
local nY        as numeric
local nJson     as numeric

	Pergunte(cPergunte,.F.)

	oJson := oJsonMvPar:GetNames()
	nJson := len(oJson)

	for nY := 1 To nJson
		do case
            case UPPER( oJson[nY] ) == "SV_MULTBRANCH"
                self:jCustomPar[UPPER(oJson[nY])] := oJsonMvPar[oJson[nY]]
			case VALTYPE(&( UPPER( oJson[nY] ) )) == "D"
                If !Empty(oJsonMvPar[ oJson[nY] ][1])
				    &( UPPER( oJson[nY] ) ) := StoD( SubStr(StrTran(oJsonMvPar[ oJson[nY] ][1],"-",""),1,8) )
                Else
                    &( UPPER( oJson[nY] ) ) := StoD("") 
                EndIf
			case VALTYPE(&( UPPER( oJson[nY] ) )) $ "N|C"
				&( UPPER( oJson[nY] ) ) := oJsonMvPar[ oJson[nY] ][1]
			otherwise
				self:jCustomPar[UPPER(oJson[nY])] := oJsonMvPar[oJson[nY]]
		endcase
	next nY

Return


//-------------------------------------------------------------------
/*/{Protheus.doc} getCustomFieldsToSQL
    Retorna string de personalização de campos pronta para ser adicionada à query.
    @author SQUAD SUPRIMENTOS
    @since 20/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getCustomFieldsToSQL() as character class ComIntegratedProvider

local cCustomFields		:= "" as character
local lContactTableName := .T.
local aTables			:= NIL
local lOnlyCustomFields := .T.

    cCustomFields := self:getSQLFields(lContactTableName, aTables, lOnlyCustomFields)
    cCustomFields := if(!empty(cCustomFields), ", " + cCustomFields, '')

return cCustomFields


//-------------------------------------------------------------------
/*/{Protheus.doc} processCustomFields
    Complementa o JSON jData com o conteúdo dos campos personalizados.
    @author SQUAD SUPRIMENTOS
    @since 19/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method processCustomFields() class ComIntegratedProvider

local aCustomFields	:= {}	as array
local nX			:= 0	as numeric
local cFieldName	:= ""	as character
local cType			:= ""	as character
local cPropertyId	:= ""	as character

    aCustomFields := self:getCustomFields()

    for nX := 1 To len(aCustomFields)
       
	    cPropertyId := cFieldName := aCustomFields[nX][1]
        cType		:= aCustomFields[nX][3]        
        self:addToData(cPropertyId, cType, cFieldName)

    next nX

return


//-------------------------------------------------------------------
/*/{Protheus.doc} addToData
    Adiciona algum conteúdo no jData
    @author SQUAD SUPRIMENTOS
    @since 20/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method addToData(cPropertyId as character, cType as character, cFieldName as character) class ComIntegratedProvider

local cComboContent := "" as character

    if cType == "date"
        self:jData[cPropertyId] := self:varToTimeStamp((self:cAlias)->&(cFieldName))
    elseif cType == "string" .and. !Empty(cComboContent := x3Combo(cFieldName, (self:cAlias)->&(cFieldName)))
        self:jData[cPropertyId] := cComboContent
    else
        self:jData[cPropertyId] := (self:cAlias)->&(cFieldName)
    endif

return


//-------------------------------------------------------------------
/*/{Protheus.doc} setFilter
    Adiciona algum conteúdo no jData
    @author SQUAD SUPRIMENTOS
    @since 20/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method setFilter(oFilter as object) class ComIntegratedProvider

    self:oCurrentFilter := oFilter

return


//-------------------------------------------------------------------
/*/{Protheus.doc} getData
    Método getData padrão para todos os objetos de negócio.
    @author SQUAD SUPRIMENTOS
    @since 24/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ComIntegratedProvider     

	if self:validEnvironment()
	
        self:setFilter(oFilter)
        
        if oFilter <> NIL
            self:setMVPAR(oFilter)
        endIf
        
		self:jStatement := JsonObject():New()
        self:cAlias := MPSysOpenQuery(self:getQuery(), NIL, self:aStruct)

        self:setAliasToData()
    
        (self:cAlias)->( DBCloseArea() )
    
    endif

return self:oData


//-------------------------------------------------------------------
/*/{Protheus.doc} setAliasToData
    Método responsável em carregar o self:jData com os atributos padrões
    do Alias. Caso o ON tenha conteúdos específicos ou condicionais, o mesmo
    deve utilizar o método setCustomInfoToData.
    @author SQUAD SUPRIMENTOS
    @since 24/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method setAliasToData() class ComIntegratedProvider

	self:aStruct := self:getStructFields()

	(self:cAlias)->(dbGoTop())

    if (self:cAlias)->(Eof())
        self:setEmptyContent()
        self:processAndAppend()
    endif

    while !(self:cAlias)->(Eof())
        self:setContentToData()
        self:setCustomInfoToData()
        self:processAndAppend()
        (self:cAlias)->(DbSkip())
    enddo
	
return


//-------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
    Método definido para não ocorrer errorLog nos processamentos dos 
    ON's, quando o mesmo não conter este método definido em sua classe.
    @author SQUAD SUPRIMENTOS
    @since 24/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method setCustomInfoToData() class ComIntegratedProvider
return

//-------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
    Método definido para não ocorrer errorLog nos processamentos dos 
    ON's, quando o mesmo não conter este método definido em sua classe.
    @author SQUAD SUPRIMENTOS
    @since 24/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method setEmptyContent() class ComIntegratedProvider
return


//-------------------------------------------------------------------
/*/{Protheus.doc} getAllFieldsToSQL
    Retorna todos os campos usados no objeto de negócio para compor
    a query. Inclui todos os campos do schema padrão, da personalização de campos
    e da internacionalização.
    É bom dar preferência para esse método em vez de montar os campos da query manualmente
    no objeto de negócio porque se houver algum campo na consulta que não existe no schema,
    será permitido ao usuário incluir esse campo no objeto de negócio e ele ficaria duplicado
    na query, correndo um risco alto de gerar erros em tempo de execução.
    @author SQUAD SUPRIMENTOS
    @since 26/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getAllFieldsToSQL() as character class ComIntegratedProvider

local cFields := "" as character    
local lContactTableName := .T. as logical

    cFields := self:getSQLFields(lContactTableName) + " "
    cFields += self:getCustomFieldsToSQL() + " "
    cFields += self:cSelectComplement + " "
    
return cFields

//-------------------------------------------------------------------
/*{Protheus.doc} getStructCustom
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Leandro Nishihata
@since 25/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getStructCustom(aCpos as array) class ComIntegratedProvider

local aField	:= {}	as array
local cTitle			as character
local cType				as character
local nX				as numeric
local nY				as numeric
local uField

	aTypeAux := {{"C", "string"}, {"D", "date"}, {"N", "number"}, {"L", "boolean"}}

	for nX := 1	to len(aCpos)
		
		uField := aCpos[nX]

		if valtype(uField) == "A"
			AADD(aField, AEval(uField, {|x| x }) )
		else
			cTitle := FWSX3Util():GetDescription(uField)
			nY := aScan(aTypeAux, {|x| x[1] == FWSX3Util():GetFieldType(uField)})
			cType := if(nY > 0, aTypeAux[nY, 2], "string")
			AADD(aField,{uField, cTitle, cType, cTitle})
		endif
	
	next nX

return aField


/*/{Protheus.doc} setFilterOnWhere
	Tratamento do filtro do objeto de negócio para tratamento no statement da query
@author SQUAD SUPRIMENTOS
@since 13/12/2023
@version version
@return cRet, character, retorna script SQL concatenado
/*/
method setFilterOnWhere() class ComIntegratedProvider

local cRet	:= ""	as character

	cRet := if( self:oCurrentFilter:hasFilter(), ' AND ' + self:oCurrentFilter:getSQLExpression(), '' )

return cRet


/*/{Protheus.doc} getCustomParam
	Em caso de parâmetro customizado este método retornará o valor do parâmetro desejado
@author SQUAD SUPRIMENTOS
@since 15/12/2023
@version version
@return xRet, character, retorna o valor do parâmetro fornecido pelo ON.
/*/
method getCustomParam(cParam as character, lIsBranch as logical, cTable as character) class ComIntegratedProvider

local xRet  := Nil

default cTable      := ""
default cParam      := ""
default lIsBranch   := .F.

    if !empty(cParam)
		if lIsBranch .and. ( !(self:lLibLookUpOK) .Or. empty(self:jCustomPar["SV_MULTBRANCH"]) )
			xRet := FWxFilial(cTable)
		else
			xRet := self:jCustomPar[cParam]
		endif
    endif

return xRet


//-------------------------------------------------------------------
/*/{Protheus.doc} getInfoEmp
    Método para retornar informações/atributos do cadastro de empresa
        - Existência do grupo de perguntas (SX1)
        - Versão mínima da LIB
    @author SQUAD SUPRIMENTOS
    @since 16/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getInfoEmp(cFldSM0 as character, cCodFil as character) class ComIntegratedProvider

local aAux	as array
local cRet	as character
local nX	as numeric

	aAux := FWSM0Util():GetSM0Data(, cCodFil,)
	nX	 := aScan(aAux,{|x| alltrim(x[1]) == upper(alltrim(cFldSM0)) })

	do case
		case nX > 0 .and. "_CGC" $ upper(alltrim(cFldSM0))
			cRet := self:getPictureCGC(alltrim(aAux[nX][2]))
		case nX > 0
			cRet := alltrim(aAux[nX][2])
	endcase

return cRet


//-------------------------------------------------------------------
/*/{Protheus.doc} comAddProperty
    Método para a criação de propriedades/campos custom no ON.
    @author SQUAD SUPRIMENTOS
    @since 16/01/2024
    @version 12.1.2310
*/
//------------------------------------------------------------------
method comAddProperty(cPropertyName as character, cDescription as character, cType as character, cRealNameForLGPD as character, cRenameField as character, lCanFilter as logical) class ComIntegratedProvider

default cRealNameForLGPD    := ""
default cRenameField        := ""
default lCanFilter          := .F.

    
	if !empty(cRealNameForLGPD)
		self:addProperty(cPropertyName, cDescription, cType, cDescription, cRealNameForLGPD, , , cRenameField, lCanFilter)
		self:addLGPDCalcField(cPropertyName, cRealNameForLGPD)
	else
		self:addProperty(cPropertyName, cDescription, cType, cDescription, cPropertyName, , , cRenameField, lCanFilter)
	endif
	
	self:loadLGPDFields()

return


//-------------------------------------------------------------------
/*/{Protheus.doc} comAddNestedProperty
    Método para a criação de objetos de negócios aninhados
    @since 16/01/2024
    @version 12.1.2310
*/
//------------------------------------------------------------------
method comAddNestedProperty(cPropertyId as character, cDescription as character, cDisplayname as character, cAlias as character, aField as array) class ComIntegratedProvider

	aField := self:getStructCustom(aField)
    self:addNestedProperty(cPropertyId, cDescription, cDisplayName, cAlias, aField)

return


/*/{Protheus.doc} processSQLStatement
    
	Método para processamento da classe FWPreparedStatement para todos os
	ON's pertinentes ao compras, com o objeto de centralizar a utilização dessa classe
	numa possível manutenção/troca, conforme definição do Framework.

    @author SQUAD SUPRIMENTOS
    @since 10/01/2024
    @version version
/*/
method processSQLStatement(cQuery as character) class ComIntegratedProvider

local cTipoPar		as character
local cRetQry       as character
local nX	 := 0	as numeric
local oJson			as json
local oQuery		as object

default cQuery	        := ''

	oQuery := FWPreparedStatement():New(cQuery)
	oJson := self:jStatement:GetNames()

	for nX := 1 To len(oJson)
		if 'UNSAFE_' + strZero(nX,3) == oJson[nX]
			oQuery:setUnsafe(nX, self:jStatement[oJson[nX]])
		else
			cTipoPar := valtype( self:jStatement[oJson[nX]] )
			if cTipoPar == "C"
				oQuery:setString(nX, self:jStatement[oJson[nX]])
			elseif cTipoPar == "D"
				oQuery:setDate(nX, self:jStatement[oJson[nX]])
			elseif cTipoPar == "N"
				oQuery:setNumeric(nX, self:jStatement[oJson[nX]])
			elseif cTipoPar == "A"
				oQuery:setIn(nX, self:jStatement[oJson[nX]])
			endif
		endif
	next nX
	
	cRetQry := oQuery:GetFixQuery()

return cRetQry


//-------------------------------------------------------------------
/*/{Protheus.doc} setContentToData
	Método para carregar o jData antes do append
	@since 19/01/2024
	@version 12.1.2310
*/
//-------------------------------------------------------------------
method setContentToData() class ComIntegratedProvider

local cProperty	as character
local cRealName	as character
local cField    as character
local lCombo	as logical
local nX		as numeric

    self:jData := JSonObject():New()

	for nX := 1 to len(self:aStruct)
        
        cProperty := self:aStruct[nX]:getName()
        cRealName := self:aStruct[nX]:getRealName()

        cField := if( cProperty == cRealName, cProperty, cRealName )
    
		if (self:cAlias)->(fieldPos(cProperty)) > 0
            lCombo := At('_', cField) > 0 .and. !empty( x3Combo(cField, (self:cAlias)->&(cField)) )
			do case	
				case self:aStruct[nX]:getType() == "date" 
					self:jData[cProperty] := self:varToTimeStamp((self:cAlias)->&(cField))
				case "_CGC" $ cProperty
                    self:jData[cProperty] := self:getPictureCGC(alltrim((self:cAlias)->&(cProperty)))
                case lCombo
					self:jData[cProperty] := x3Combo(cField, (self:cAlias)->&(cField))
				otherwise
					self:jData[cProperty] := (self:cAlias)->&(cField)
			endcase
		endif
	next nX

return

//-------------------------------------------------------------------
/*/{Protheus.doc} getPictureCGC
    Método para validação do Ambiente.
        - Existência do grupo de perguntas (SX1)
        - Versão mínima da LIB
    @author SQUAD SUPRIMENTOS
    @since 16/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getPictureCGC(cContent as character) class ComIntegratedProvider

local cRet      as character

    do case
        case !empty(cContent) .and. len(cContent) == 14
            cRet := transform(cContent, "@R! NN.NNN.NNN/NNNN-99")
        case !empty(cContent) .and. len(cContent) == 11
            cRet := transform(cContent, "@R 999.999.999-99")
        otherwise
            cRet := ""
    endcase       

return cRet

//-------------------------------------------------------------------
/*/{Protheus.doc} validEnvironment
    Método para validação do Ambiente.
        - Existência do grupo de perguntas (SX1)
        - Versão mínima da LIB
    @author SQUAD SUPRIMENTOS
    @since 16/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method validEnvironment() class ComIntegratedProvider

	if !(self:lLibOK)
		self:setErrorInvalidLib()
	endif

    if !(self:lPergunteOK)
		self:setErrorInvalidPergunte()
	endif

return self:lLibOK .and. self:lPergunteOK


//-------------------------------------------------------------------
/*/{Protheus.doc} setErrorInvalidLib
    Método para 'setar' o erro 400 HTTP
        - Notificar o usuário sobre a versão mínima da LIB
    @since 16/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method setErrorInvalidLib() class ComIntegratedProvider
    
	self:setErrorStatus(400, STR0005, STR0006 + self:cMinimumLibLabel)  //"Versão de Lib inválida", "Necessário Lib Label igual ou superior a "
	FwLogMsg("WARN",,STR0005,,,,STR0006 + self:cMinimumLibLabel,,,)     //"Versão de Lib inválida", "Necessário Lib Label igual ou superior a " 

return


//-------------------------------------------------------------------
/*/{Protheus.doc} setErrorInvalidPergunte
    Método para 'setar' o erro 400 HTTP
        - Notificar o usuário sobre a ausência do grupo de 
        perguntas no dicionário (SX1)
    @since 16/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method setErrorInvalidPergunte() class ComIntegratedProvider
    
	self:setErrorStatus(400, STR0002, STR0003 + self:cSX1Group )    //"Sem grupo de perguntas", "Grupo de perguntas não encontrado: "
	FwLogMsg("WARN",, STR0004,,, , STR0003 + self:cSX1Group , , ,)  //"Smart View, "Grupo de perguntas não encontrado: "

return


/*/{Protheus.doc} enableFilterByBranch
		Método utilizado para habilitar a utilização de filtro por Filial
		sem a necessidade de criação do parâmetro no dicionário de perguntas (SX1)
	@since 23/10/2024
	@version 12.1.2310
	/*/
Method enableFilterByBranch(cAlsBase as character) class ComIntegratedProvider

default cAlsBase := ""

    if self:lLibLookUpOK
        self:oSchema:addParameter("SV_MULTBRANCH", STR0007, "string", .T.)	// "Qual Filial/Filiais?"
        self:setCustomURL("SV_MULTBRANCH","/api/com/smartview/v1/options/getBranches/" + cAlsBase,2)
    endif

Return
