#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.invoicelist.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SF1", name="Relação de Notas Fiscais", country="ALL", customTables="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} invoicelistReportsBusinessObject
Classe para criação do Objeto de Negócio 
Relacao de notas fiscais

	MATR080 - Relação de Notas Fiscais
	Invoice List

@author Everton Fregonezi Diniz
@since 03/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class invoicelistReportsBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object
	
	protected method getQuery() as character
	protected method setCustomInfoToData() as object 
     
endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Everton Fregonezi Diniz
@since 03/11/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class invoicelistReportsBusinessObject

	_Super:new()
	self:setDisplayName(STR0002) 	// "Notas Fiscais"
	self:setDescription(STR0003) 	// "Relação de Notas Fiscais"
	self:setPergunte("COMT000003")
	
return self



//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 03/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getSchema() as object class invoicelistReportsBusinessObject

	self:aliasToSchema("SF1", {	"F1_FILIAL", "F1_TIPO", "F1_DOC", "F1_SERIE", "F1_FORNECE", "F1_LOJA", "F1_ESPECIE", "F1_DTDIGIT",;
								"F1_MOEDA", "F1_TXMOEDA", "F1_VALMERC", "F1_VALBRUT", "F1_COND", "F1_FRETE", "F1_DESPESA", "F1_DESCONT" })

	self:comAddProperty("RAZAO", STR0004, "string")  // "Razão Social"
	self:comAddProperty("cMoeda", STR0005, "string")	// "Moeda"
	
	self:enableFilterByBranch("SF1")

return self:oSchema


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 03/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class invoicelistReportsBusinessObject

local cQuery	:= ""	as character
local nX		:= 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL() + ", A2_NOME AS  RAZAO, A2_EST AS ESTADO"
 	cQuery += " FROM " + RetSQLName("SF1") + " SF1 "
	cQuery += " "
 	cQuery += "     INNER JOIN " + RetSQLName("SA2") + " SA2 "
 	cQuery += "         ON	" + FWJoinFilial("SA2","SF1")
 	cQuery += "			AND	A2_COD          = F1_FORNECE "
 	cQuery += "			AND A2_LOJA         = F1_LOJA "
 	cQuery += "			AND SA2.D_E_L_E_T_  = ? "
	cQuery += " "
 	cQuery += "     INNER JOIN " + RetSQLName("SD1") + " SD1 "
 	cQuery += "         ON	" + FWJoinFilial("SD1","SF1")
 	cQuery += "			AND	D1_DOC			= F1_DOC "
 	cQuery += "			AND	D1_SERIE        = F1_SERIE "
 	cQuery += "			AND	D1_FORNECE      = F1_FORNECE "
 	cQuery += "			AND	D1_LOJA         = F1_LOJA "
 	cQuery += "			AND	D1_TIPO         = F1_TIPO "
	cQuery += "			AND D1_FORMUL		= F1_FORMUL "
	cQuery += "			AND D1_TIPODOC		= F1_TIPODOC "
 	cQuery += "			AND SD1.D_E_L_E_T_  = ? "
	cQuery += " "
 	cQuery += " WHERE "
 	cQuery += "         F1_FILIAL		IN (?) "
 	cQuery += "		AND F1_DOC          >= ? "
 	cQuery += "		AND F1_DOC          <= ? "
	cQuery += "		AND " + SerieNfId("SF1",3,"F1_SERIE") + " >= ? "
	cQuery += "		AND " + SerieNfId("SF1",3,"F1_SERIE") + " <= ? "
 	cQuery += "		AND F1_FORNECE      >= ? "
 	cQuery += "		AND F1_FORNECE      <= ? "
 	cQuery += "		AND F1_TIPO			NOT IN (?) "
	cQuery += "		AND NOT ("+IsRemito(3,'SF1.F1_TIPODOC')+ ") " 
 	cQuery += "		AND F1_DTDIGIT      >= ? "
 	cQuery += "		AND F1_DTDIGIT      <= ? "
 	cQuery += "		AND SF1.D_E_L_E_T_  = ?
 	cQuery += "		AND D1_COD          >= ? "
 	cQuery += "		AND D1_COD          <= ? "
	cQuery += "		AND D1_TES			>= ? "
	cQuery += "		AND D1_TES			<= ? "
 	cQuery += "		AND D1_LOCAL        >= ? "
 	cQuery += "		AND D1_LOCAL        <= ? "
 	cQuery += "		AND D1_GRUPO        >= ? "
 	cQuery += "		AND D1_GRUPO        <= ? "
	cQuery += self:setFilterOnWhere()
 	cQuery += " GROUP BY " + self:getAllFieldsToSQL() + ", A2_NOME, A2_EST "
 	
	cQuery += " UNION ALL "
 	
	cQuery += " SELECT " + self:getAllFieldsToSQL() + ", A1_NOME AS  RAZAO, A1_EST AS ESTADO"
 	cQuery += " FROM
 	cQuery += "     " + RetSQLName("SF1") + " SF1 "
	cQuery += " "
 	cQuery += "     INNER JOIN " + RetSQLName("SA1") + " SA1 "
 	cQuery += "         ON	" + FWJoinFilial("SA1","SF1")
 	cQuery += "			AND	A1_COD          = F1_FORNECE "
 	cQuery += "			AND	A1_LOJA         = F1_LOJA "
 	cQuery += "			AND	SA1.D_E_L_E_T_  = ? "
	cQuery += " "
 	cQuery += "     INNER JOIN " + RetSQLName("SD1") + " SD1
 	cQuery += "         ON	" + FWJoinFilial("SD1","SF1")
 	cQuery += "			AND	D1_DOC          = F1_DOC "
 	cQuery += "			AND	D1_SERIE        = F1_SERIE "
 	cQuery += "			AND	D1_TIPO         = F1_TIPO "
 	cQuery += "			AND	D1_FORNECE      = F1_FORNECE "
 	cQuery += "			AND	D1_LOJA         = F1_LOJA "
	cQuery += "			AND D1_FORMUL		= F1_FORMUL "
	cQuery += "			AND D1_TIPODOC		= F1_TIPODOC "
 	cQuery += "			AND	SD1.D_E_L_E_T_  = ? "
	cQuery += " "
 	cQuery += " WHERE "
 	cQuery += "         F1_FILIAL		IN (?) "
	cQuery += "		AND F1_DOC          >= ? "
 	cQuery += "		AND F1_DOC          <= ? "
	cQuery += "		AND " + SerieNfId("SF1",3,"F1_SERIE") + " >= ? "
	cQuery += "		AND " + SerieNfId("SF1",3,"F1_SERIE") + " <= ? "
 	cQuery += "		AND F1_FORNECE      >= ? "
 	cQuery += "		AND F1_FORNECE      <= ? "
 	cQuery += "		AND F1_TIPO			IN (?) "
	cQuery += "		AND NOT ("+IsRemito(3,'SF1.F1_TIPODOC')+ ") "
	cQuery += "		AND F1_DTDIGIT      >= ? "
 	cQuery += "		AND F1_DTDIGIT      <= ? "
 	cQuery += "     AND SF1.D_E_L_E_T_  = ? "
 	cQuery += "		AND D1_COD          >= ? "
 	cQuery += "		AND D1_COD          <= ? "
	cQuery += "		AND D1_TES			>= ? "
	cQuery += "		AND D1_TES			<= ? "
 	cQuery += "		AND D1_LOCAL        >= ? "
 	cQuery += "		AND D1_LOCAL        <= ? "
 	cQuery += "		AND D1_GRUPO        >= ? "
 	cQuery += "		AND D1_GRUPO        <= ? "
	cQuery += self:setFilterOnWhere()
	cQuery += " GROUP BY " + self:getAllFieldsToSQL() + ", A1_NOME, A1_EST"

	cQuery += " ORDER BY " + SqlOrder(SF1->(IndexKey(1)))

	// 1º Bloco SQL
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SF1")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR11
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR12
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR13
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR14
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := strToKarr2("D|B", "|")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR15
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR16
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06

	// 2º Bloco SQL
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SF1")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR11
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR12
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR13
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR14
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := strToKarr2("D|B", "|")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR15
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR16
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	
    cQuery := self:processSQLStatement(cQuery)

return cQuery


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 03/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class invoicelistReportsBusinessObject

	self:jData["F1_FILIAL"] := (self:cAlias)->(alltrim(F1_FILIAL) + " - " + self:getInfoEmp("M0_FILIAL", F1_FILIAL))

	DbSelectArea("CTO")
	self:jData["cMoeda"]	:= (self:cAlias)->(str((self:cAlias)->F1_MOEDA) + ' - ' + alltrim(GetAdvFVal( "CTO", "CTO_DESC", FWxFilial("CTO") + strZero(F1_MOEDA,2), 1 )))

	DbSelectArea("SE4")
	self:jData["F1_COND"]	:= (self:cAlias)->(alltrim(F1_COND) + ' - ' + alltrim(GetAdvFVal( "SE4", "E4_DESCRI", FWxFilial("SE4") + F1_COND, 1 )))
	

return
