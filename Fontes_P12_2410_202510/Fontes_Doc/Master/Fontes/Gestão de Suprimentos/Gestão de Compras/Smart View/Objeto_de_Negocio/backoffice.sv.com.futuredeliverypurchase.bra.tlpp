#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.futuredeliverypurchase.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SB1,SD1,DHQ,SA2", customTables="ALL", name="Listagem de Compra com Entrega Futura", country="BRA")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} futureDeliveryPurchaseSmartviewBusinessObjects
	Classe para criação do Objeto de Negócio 
	Lista de Compras C/ Entrega Futura

@author Everton Fregonezi Diniz
@since 22/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class futureDeliveryPurchaseSmartviewBusinessObjects from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object
	
	protected method getQuery() as character
	protected method setCustomInfoToData() as object
     
endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Everton Fregonezi Diniz
@since 22/11/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class futureDeliveryPurchaseSmartviewBusinessObjects

	_Super:new()
	self:setDisplayName(STR0002) 	// "Relação de Compras com Entrega Futura"
	self:setDescription(STR0003) 	// "Objeto de negócio responsável em apresentar uma lista de mercadorias em poder do fornecedor para entrega futura"
	self:setPergunte("COMT000012")
	self:enableFilterByBranch("DHQ")

return self



//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 22/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getSchema() as object class futureDeliveryPurchaseSmartviewBusinessObjects
	
	self:aliasToSchema("DHQ", {"DHQ_FILIAL","DHQ_TIPO","DHQ_IDENT","DHQ_DOC","DHQ_SERIE","DHQ_ITEM","DHQ_FORNEC","DHQ_LOJA","DHQ_DTREC","DHQ_DESFAZ","DHQ_QTORI","DHQ_QTREC"})
	self:aliasToSchema("SA2", {"A2_NOME","A2_CGC"})
	self:aliasToSchema("SB1", {"B1_DESC"})
	self:aliasToSchema("SD1", {"D1_PEDIDO","D1_ITEMPC","D1_COD","D1_UM","D1_TES","D1_VUNIT","D1_TOTAL","D1_CF"})
	
	self:comAddProperty("nSaldo", STR0004, "number")	//"Saldo"
	self:comAddProperty("nCusto", STR0005, "number")	//"Custo"
	
return self:oSchema


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 22/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class futureDeliveryPurchaseSmartviewBusinessObjects

local cQuery	:= ""	as character
local nX		:= 1	as numeric

	cQuery := " SELECT	" + self:getAllFieldsToSQL()
	cQuery += "		,	?	AS nCusto"
	cQuery += " FROM  " +  RetSQLName("DHQ") + " DHQ "
	
	cQuery += " INNER JOIN " +  RetSQLName("SD1") + " SD1 "
	cQuery += " 	ON	" + FWJoinFilial("SD1", "DHQ")
	cQuery += " 	AND D1_DOC     = DHQ_DOC	"
	cQuery += " 	AND D1_SERIE   = DHQ_SERIE	"
	cQuery += " 	AND D1_FORNECE = DHQ_FORNEC	"
	cQuery += " 	AND D1_LOJA    = DHQ_LOJA	"
	cQuery += " 	AND D1_ITEM    = DHQ_ITEM	"
	cQuery += " 	AND SD1.D_E_L_E_T_ = ?		"
	
	cQuery += " INNER JOIN " +  RetSQLName("SB1") + " SB1 "
	cQuery += " 	ON	" + FWJoinFilial("SB1", "DHQ")
	cQuery += " 	AND B1_COD     = DHQ_COD	"
	cQuery += " 	AND SB1.D_E_L_E_T_ = ?		"
	
	cQuery += " INNER JOIN " +  RetSQLName("SA2") + " SA2 "
	cQuery += " 	ON	" + FWJoinFilial("SA2", "DHQ")
	cQuery += " 	AND A2_COD		= DHQ_FORNEC	"
	cQuery += " 	AND A2_LOJA		= DHQ_LOJA		"
	cQuery += " 	AND SA2.D_E_L_E_T_ = ?			"
	
	cQuery += " WHERE "
	cQuery += " 		DHQ_FILIAL	IN  (?)	"
	cQuery += " 	AND	DHQ_FORNEC	>= ?	"
	cQuery += " 	AND	DHQ_FORNEC	<= ?	"
	cQuery += " 	AND	DHQ_LOJA	>= ?	"
	cQuery += " 	AND	DHQ_LOJA	<= ?	"
	cQuery += " 	AND	DHQ_COD		>= ?	"
	cQuery += " 	AND	DHQ_COD		<= ?	"
	cQuery += " 	AND	DHQ_DTREC	>= ?	"
	cQuery += " 	AND	DHQ_DTREC	<= ?	"

	IF MV_PAR09 == 2		// Somente em aberto.
		cQuery += "		AND	DHQ_STATUS	= ?	"
		cQuery += "		AND	DHQ_QTORI	> DHQ_QTREC	"
		cQuery += "		AND	DHQ_DESFAZ	= ?	"
	elseif MV_PAR09 == 3	// Encerrado
		cQuery += "		AND	DHQ_STATUS	= ? "
		cQuery += "		AND	DHQ_QTORI	<= DHQ_QTREC "
	endif
	cQuery += " 	AND	DHQ.D_E_L_E_T_ = ? "
	
	cQuery += self:setFilterOnWhere()
	
	cQuery += " ORDER BY " + SqlOrder(DHQ->(IndexKey(1)))

	self:jStatement['UNSAFE_'+strZero(nX++,3)]	:= "D1_CUSTO" + if( MV_PAR13 > 1, alltrim(str(MV_PAR13)), '' )
    self:jStatement['PARAM_'+strZero(nX++,3)]	:= ' '
    self:jStatement['PARAM_'+strZero(nX++,3)]	:= ' '
    self:jStatement['PARAM_'+strZero(nX++,3)]	:= ' '
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= self:getCustomParam('SV_MULTBRANCH', .T., "DHQ")
    self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR01
    self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR03
    self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR02
    self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR04
    self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR05
    self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR06
    self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR07
    self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR08
	
	if MV_PAR09 == 2		// Somente em aberto.
	    self:jStatement['PARAM_'+strZero(nX++,3)]	:= '1'
    	self:jStatement['PARAM_'+strZero(nX++,3)]	:= ' '
	elseif MV_PAR09 == 3	// Encerrado		
		self:jStatement['PARAM_'+strZero(nX++,3)]	:= '9'
	endif
    
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= ' '

    cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 22/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class futureDeliveryPurchaseSmartviewBusinessObjects

local cCodPro	as character
local nTamSB1	as numeric

	nTamSB1	:= GetSX3Cache("B1_COD", "X3_TAMANHO")
	
	self:jData["DHQ_FILIAL"]	:= alltrim((self:cAlias)->DHQ_FILIAL) + " - " + alltrim(self:getInfoEmp("M0_FILIAL", (self:cAlias)->DHQ_FILIAL))	
	self:jData["nSaldo"] := (self:cAlias)->( If(DHQ_TIPO == "1" .and. empty(DHQ_DESFAZ), DHQ_QTORI - DHQ_QTREC, 0) )
	self:jData["nCusto"] := if(MV_PAR12 == 1, (self:cAlias)->nCusto, 0)
	
	//	Não lista pedidos de compras
	if MV_PAR10 == 2
		self:jData["D1_PEDIDO"]	:= ""
		self:jData["D1_ITEMPC"]	:= ""
	endif

	// Lista a qtde na 2ª Unid. Medida
	if MV_PAR11 == 1
		cCodPro 				:= PadR( (self:cAlias)->D1_COD, nTamSB1 )
		self:jData["D1_UM"]		:= GetAdvFval("SB1", "B1_SEGUM", (self:cAlias)->DHQ_FILIAL + cCodPro, 1, "")
		self:jData["DHQ_QTORI"]	:= (self:cAlias)->( ConvUm(cCodPro, DHQ_QTORI, 0, 2) )
		self:jData["DHQ_QTREC"]	:= (self:cAlias)->( ConvUm(cCodPro, DHQ_QTREC, 0, 2) )
		self:jData["nSaldo"]	:= (self:cAlias)->( ConvUm(cCodPro, If(DHQ_TIPO == "1" .and. empty(DHQ_DESFAZ), DHQ_QTORI - DHQ_QTREC, 0), 0, 2) )
	endif
    
return
