#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.requestdivergencesxpurchaseorder.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SA2,SB1,SC1,SC7", customTables="SA2,SB1,SC1,SC7", name="Divergência entre solicitação e pedido de compras", country="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} requestdivergencesxpurchaseorderReportsBusinessObject
	Classe para criação do Objeto de Negócio da divergencia entre solicitação e pedido de compras
@author Everton Fregonezi Diniz
@since 03/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class requestdivergencesxpurchaseorderReportsBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
	protected method setCustomInfoToData() as object
     
endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Everton Fregonezi Diniz
@since 03/11/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class requestdivergencesxpurchaseorderReportsBusinessObject

	_Super:new()
	self:setDisplayName(STR0002) 	// "Divergência SC x PC"
	self:setDescription(STR0003) 	// "Divergência de Solicitação de Compras x Pedido de Compras"
	self:setPergunte("COMT000025")
	
return self
 

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 03/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getSchema() as object class requestdivergencesxpurchaseorderReportsBusinessObject

	self:comAddProperty("DIVERG", STR0004, "string")	// "Divergência"

	self:aliasToSchema("SA2", { "A2_NOME" } )
	self:aliasToSchema("SB1", { "B1_UM" } )
    self:aliasToSchema("SC1", { "C1_FILIAL","C1_NUM","C1_ITEM","C1_EMISSAO","C1_FORNECE","C1_LOJA","C1_PRODUTO","C1_QUANT","C1_QUJE","C1_CC","C1_CONTA","C1_DATPRF" } )
	self:aliasToSchema("SC7", { "C7_NUM","C7_ITEM","C7_EMISSAO","C7_FORNECE","C7_LOJA","C7_PRODUTO","C7_QUANT","C7_CC","C7_CONTA","C7_DATPRF" } )

	self:enableFilterByBranch("SC1")

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 03/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class requestdivergencesxpurchaseorderReportsBusinessObject

	local cQuery	as character
	local nX	 := 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += " FROM    " + RetSQLName("SC1") + " SC1 "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1", "SC1")	
	cQuery += "			AND	B1_COD			= C1_PRODUTO "
	cQuery += "			AND	SB1.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		INNER JOIN " + RetSQLName("SC7") + " SC7 "
	cQuery += "			ON	" + FWJoinFilial("SC7", "SC1")	
	cQuery += "			AND	C7_PRODUTO		= C1_PRODUTO "
	cQuery += "			AND	C7_NUM			= C1_PEDIDO	"
	cQuery += "			AND	C7_ITEM			= C1_ITEMPED "
	cQuery += "			AND	C7_RESIDUO		= ? "
	cQuery += "			AND	SC7.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		INNER JOIN " + RetSQLName("SA2") + " SA2  "
	cQuery += "			ON	" + FWJoinFilial("SA2", "SC1")	
	cQuery += "			AND	(	A2_COD		= C1_FORNECE "
	cQuery += "				OR	A2_COD		= C7_FORNECE ) "
	cQuery += "			AND	(	A2_LOJA		= C1_LOJA "
	cQuery += "				OR	A2_LOJA		= C7_LOJA ) "
	cQuery += "			AND	SA2.D_E_L_E_T_ 	= ?	"
	cQuery += "	"
	cQuery += " WHERE  "
	cQuery += "			C1_FILIAL		IN (?) "
	cQuery += "		AND	C1_PRODUTO		>= ? "
	cQuery += "		AND	C1_PRODUTO		<= ? "
	cQuery += "		AND	C1_NUM			>= ? "
	cQuery += "		AND	C1_NUM			<= ? "
	cQuery += "		AND	(	C1_FORNECE		>= ? "
	cQuery += "				OR	C7_FORNECE	>= ? ) "
	cQuery += "		AND	(	C1_LOJA		>= ? "
	cQuery += "				OR	C7_LOJA	>= ? ) "
	cQuery += "		AND	(	C1_FORNECE		<= ? "
	cQuery += "				OR	C7_FORNECE	<= ? ) "
	cQuery += "		AND	(	C1_LOJA		<= ? "
	cQuery += "				OR	C7_LOJA	<= ? ) "
	if MV_PAR15 == 1		// Todas as Divergências
		cQuery += "		AND (	C1_QUANT		<> C1_QUJE "
		cQuery += "			OR	C1_DATPRF		<> C7_DATPRF ) "
	elseif MV_PAR15 == 2	// Por Quantidade
		cQuery += "		AND		C1_QUANT		<> C1_QUJE "
	elseif MV_PAR15 == 3	// Por Dt. Entrega
		cQuery += "		AND		C1_DATPRF		<> C7_DATPRF "
	endif
	cQuery += "		AND	C1_EMISSAO		>= ? "
	cQuery += "		AND	C1_EMISSAO		<= ? "
	cQuery += "		AND	C1_PEDIDO		>= ? "
	cQuery += "		AND	C1_PEDIDO		<= ? "
	cQuery += "		AND	C1_RESIDUO		= ? "
	cQuery += "		AND	SC1.D_E_L_E_T_	= ?	"
	cQuery += "		AND	C7_EMISSAO		>= ? "
	cQuery += "		AND	C7_EMISSAO		<= ? "
	
	cQuery += self:setFilterOnWhere()
	cQuery += " ORDER BY " + SqlOrder(SC1->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SC1")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR13
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR14
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR11
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR11
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR12
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR12
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	
	cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 01/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method setCustomInfoToData(oFilter as object) as object class requestdivergencesxpurchaseorderReportsBusinessObject

	self:jData["DIVERG"]	:= (self:cAlias)->( if( C1_QUANT == C1_QUJE, STR0005, STR0006) )	// "Quantidade" # "Data de Entrega"
	self:jData["A2_NOME"]	:= (self:cAlias)->( if( !empty(C1_FORNECE), alltrim(C1_FORNECE) + '/' + alltrim(C1_LOJA) + ' - ', alltrim(C7_FORNECE) + '/' + alltrim(C7_LOJA) + ' - '  )  +  alltrim(A2_NOME) )
	self:jData["C1_CC"]		:= (self:cAlias)->( if( !empty(C1_CC),		alltrim( C1_CC )	+ ' - ' + GetAdvFVal( "CTT", "CTT_DESC01", FWxFilial("CTT") + PadR(C1_CC, FwTamSX3("CTT_CUSTO")[1]), 1 ), "" ))
	self:jData["C7_CC"]		:= (self:cAlias)->( if( !empty(C7_CC),		alltrim( C7_CC )	+ ' - ' + GetAdvFVal( "CTT", "CTT_DESC01", FWxFilial("CTT") + PadR(C7_CC, FwTamSX3("CTT_CUSTO")[1]), 1 ), "" ))
	self:jData["C1_CONTA"]	:= (self:cAlias)->( if( !empty(C1_CONTA),	alltrim( C1_CONTA ) + ' - ' + GetAdvFVal( "CT1", "CT1_DESC01", FWxFilial("CT1") + PadR(C1_CONTA, FwTamSX3("CT1_CONTA")[1]), 1 ), "" ))
	self:jData["C7_CONTA"]	:= (self:cAlias)->( if( !empty(C7_CONTA),	alltrim( C7_CONTA ) + ' - ' + GetAdvFVal( "CT1", "CT1_DESC01", FWxFilial("CT1") + PadR(C7_CONTA, FwTamSX3("CT1_CONTA")[1]), 1 ), "" ))

return
