#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.purchaseorderxsupplierlist.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.backoffice.com.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SC7,SA2", customTables="ALL", name="Fornecedores x Pedidos de Compras", country="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} purchaseorderxsupplierlistSmartViewBusinessObject
Classe para criação do Objeto de Negócio Relação de Pedidos de Compras x Fornecedores

@author Everton Fregonezi Diniz
@since 18/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  

class purchaseorderxsupplierlistSmartViewBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character

endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//----------------------------------------------------------------------------------------------------------------------------------   

method new() class purchaseorderxsupplierlistSmartViewBusinessObject

    _Super:new()
    self:setDisplayName(STR0002)  // "Pedido de Compras x Fornecedor"
    self:setDescription(STR0003)  // "Listagem de pedido x fornecedores"
    self:setPergunte("COMT000011")

return self

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getSchema() as object class purchaseorderxsupplierlistSmartViewBusinessObject

	self:aliasToSchema("SB1", {	"B1_DESC","B1_TIPO","B1_GRUPO"})
	self:aliasToSchema("SA2", {	"A2_COD","A2_LOJA","A2_NOME","A2_END","A2_BAIRRO","A2_EST","A2_MUN","A2_CEP","A2_CGC","A2_INSCR","A2_TEL","A2_DDD"})
	self:aliasToSchema("SC7", {	"C7_FILIAL","C7_FILENT","C7_EMISSAO","C7_NUM","C7_ITEM","C7_UM","C7_QUANT","C7_PRODUTO","C7_PRECO","C7_TOTAL",;
								"C7_DATPRF","C7_OP","C7_DESC1","C7_DESC2","C7_DESC3","C7_VLDESC","C7_DT_EMB","C7_CODTAB","C7_SEGURO","C7_VALFRE","C7_DESPESA"})

	self:enableFilterByBranch("SC7")

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class purchaseorderxsupplierlistSmartViewBusinessObject

	local cQuery		as character
	local nX		:= 1	as numeric

	cQuery := "	SELECT " + self:getAllFieldsToSQL()
	cQuery += " "
	cQuery += "	FROM  " + RetSQLName("SC7") + " SC7 "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1", "SC7")
	cQuery += "			AND	B1_COD			= C7_PRODUTO "
	cQuery += "			AND SB1.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2", "SC7")
	cQuery += "			AND	A2_COD			= C7_FORNECE "
	cQuery += "			AND	A2_LOJA			= C7_LOJA "
	cQuery += "			AND SA2.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += " WHERE "
	cQuery += " "
	cQuery += " 		C7_FILIAL		IN (?) "
	cQuery += "		AND	C7_NUM			>= ? "
	cQuery += "		AND	C7_NUM			<= ? "
	cQuery += "		AND	C7_TIPO			>= ? "
	cQuery += "		AND	C7_FORNECE		>= ? "
	cQuery += "		AND	C7_FORNECE		<= ? "
	cQuery += "		AND	C7_LOJA			>= ? "
	cQuery += "		AND	C7_LOJA			<= ? "
	cQuery += "		AND	C7_EMISSAO		>= ? "
	cQuery += "		AND	C7_EMISSAO		<= ? "
	cQuery += "		AND	C7_RESIDUO		!= ? "
	cQuery += " 	AND	SC7.D_E_L_E_T_	= ? "
	cQuery += self:setFilterOnWhere()

	cQuery += " ORDER BY " + SqlOrder(SC7->(IndexKey(5))) 

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SC7")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := 1
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := 'S'
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	
	cQuery := self:processSQLStatement(cQuery)

return cQuery
