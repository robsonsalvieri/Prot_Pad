#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#INCLUDE "backoffice.sv.com.origininvoicexfreightinvoice.ch"

namespace totvs.protheus.backoffice.com.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SF8,SF1,SA2", customTables="ALL", name="Relação de Notas Fiscais x Notas de Frete", country="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} origininvoicexfreightinvoiceReportsBusinessObject
Classe para criação do Objeto de Negócio da Listagem de Notas Fiscais x Notas de Frete
 matr015 - Listagem de notas fiscais x notas de frete
         - Origin Invoice X Freight Invoice     
@author Everton Fregonezi Diniz
@since 15/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class origininvoicexfreightinvoiceReportsBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
	protected method setCustomInfoToData() as object

endclass
 

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Everton Fregonezi Diniz
@since 15/12/2023
@version 1.0
*/
//----------------------------------------------------------------------------------------------------------------------------------   
method new() class origininvoicexfreightinvoiceReportsBusinessObject

    _Super:new()
    self:setDisplayName(STR0002)	// "Nota Fiscal Origem x Nota Fiscal de Frete"
    self:setDescription(STR0003)	// "Este relatório permite a verificação cruzada das informações das faturas originais com os conhecimentos de frete e despesas de importação geradas pelo sistema, de acordo com a parametrização do usuário."
    self:setPergunte("COMT000022")
	self:enableFilterByBranch("SF8")

return self


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 15/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   
method getSchema() as object class origininvoicexfreightinvoiceReportsBusinessObject

	self:comAddProperty("NFGROUP" , STR0006, "string")	// "Nota GRUPO"

	self:aliasToSchema("SF8", {"F8_FILIAL","F8_TIPO","F8_DTDIGIT","F8_NFDIFRE","F8_SEDIFRE","F8_TRANSP","F8_LOJTRAN","F8_NFORIG","F8_SERORIG","F8_FORNECE","F8_LOJA"} )
    self:aliasToSchema("SA2", {"A2_NOME"} )

return self:oSchema


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 15/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class origininvoicexfreightinvoiceReportsBusinessObject

local cQuery	:= ""	as character
local nX		:= 1	as numeric
	
	cQuery := "	SELECT " + self:getAllFieldsToSQL()
	cQuery += " "
	cQuery += "	FROM  " + RetSQLName("SF8") + " SF8 "
	cQuery += " "
	cQuery += "		LEFT JOIN " + RetSQLName("SF1") + " SF1 "
	cQuery += "			ON	" + FWJoinFilial("SF1", "SF8")
	cQuery += "			AND	F1_DOC			= F8_NFORIG	"
	cQuery += "			AND F1_SERIE		= F8_SERORIG "
	cQuery += "			AND	F1_FORNECE		= F8_FORNECE "
	cQuery += "			AND F1_LOJA			= F8_LOJA "
	cQuery += "			AND SF1.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2", "SF8")
	cQuery += "			AND	A2_COD			= F8_TRANSP "
	cQuery += "			AND A2_LOJA			= F8_LOJTRAN "
	cQuery += "			AND SA2.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "	WHERE "
	cQuery += "			F8_FILIAL		IN (?) " 
	cQuery += "		AND	?				>= ? AND ?			<= ? "
	cQuery += "		AND ?				>= ? AND ?			<= ? "
	cQuery += "		AND	F8_TRANSP		>= ? AND F8_TRANSP	<= ? "
	cQuery += "		AND F8_LOJTRAN		>= ? AND F8_LOJTRAN	<= ? "
	cQuery += "		AND F8_DTDIGIT		>= ? AND F8_DTDIGIT	<= ? "
	cQuery += "		AND F8_TIPO			IN (?) "
	cQuery += "		AND SF8.D_E_L_E_T_	= ? "  
	cQuery += self:setFilterOnWhere()

	cQuery += " ORDER BY " + SqlOrder(SF8->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)]	:= ' '
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= ' '
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= self:getCustomParam('SV_MULTBRANCH', .T., "SF8")
	self:jStatement['UNSAFE_'+strZero(nX++,3)]	:= if( MV_PAR01 == 1 .or. MV_PAR01 == 2, "F8_NFDIFRE", "F8_NFORIG")
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR04
	self:jStatement['UNSAFE_'+strZero(nX++,3)]	:= if( MV_PAR01 == 1 .or. MV_PAR01 == 2, "F8_NFDIFRE", "F8_NFORIG")
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR06
	self:jStatement['UNSAFE_'+strZero(nX++,3)]	:= if( MV_PAR01 == 1 .or. MV_PAR01 == 2, SerieNfId("SF8",3,"F8_SEDIFRE"), SerieNfId("SF8",3,"F8_SERORIG"))
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR05
	self:jStatement['UNSAFE_'+strZero(nX++,3)]	:= if( MV_PAR01 == 1 .or. MV_PAR01 == 2, SerieNfId("SF8",3,"F8_SEDIFRE"), SerieNfId("SF8",3,"F8_SERORIG"))
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR10
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR11
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= if(MV_PAR01 == 1, StrToKarr2('F','|'), if( MV_PAR01 == 2, StrToKarr2('D','|'), StrToKarr2('F|D','|'))) 
	self:jStatement['PARAM_'+strZero(nX++,3)]	:= ' '

	cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 15/12/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method setCustomInfoToData(oFilter as object) as object class origininvoicexfreightinvoiceReportsBusinessObject

	self:jData["F8_FILIAL"]	:= alltrim((self:cAlias)->F8_FILIAL) + " - " + alltrim(self:getInfoEmp("M0_FILIAL", (self:cAlias)->F8_FILIAL))
	self:jData["NFGROUP"]	:= (self:cAlias)->(alltrim(F8_NFORIG) + ' / ' + alltrim(F8_SERORIG))
	self:jData["F8_TIPO"]	:= if( (self:cAlias)->F8_TIPO == "F", STR0004, STR0005)  // Frete # Desp. Import.

return
