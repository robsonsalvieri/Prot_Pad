#include "protheus.ch"
#include "backoffice.sv.com.returninvoices.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SF2,SD2,SA2", customTables="All", name="Relação de notas fiscais de devolução", country="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} returninvoicesReportsBusinessObject
Classe para criação do Objeto de Negócio da Listagem de Notas Fiscais de Devolução

MATR180 - Nota Fiscal de Devolução
Return Invoices 

@author Everton Fregonezi Diniz
@since 31/10/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class returninvoicesReportsBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
     
endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Everton Fregonezi Diniz
@since 31/10/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class returninvoicesReportsBusinessObject

	_Super:new()
	self:setDisplayName(STR0002) 	// "Notas Fiscais de Devolução" 
	self:setDescription(STR0003) 	// "Relação de Notas Fiscais de Devolução"
	self:setPergunte("COMT000020")
	
return self
 

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 31/10/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getSchema() as object class returninvoicesReportsBusinessObject

	self:comAddProperty("cCliFor", STR0004, "string", "F2_CLIENTE")	// "Fornecedor" 

	self:aliasToSchema("SF2", { "F2_FILIAL", "F2_EMISSAO", "F2_TIPO", "F2_DOC", "F2_SERIE", "F2_LOJA", "F2_VALBRUT" } )
	self:aliasToSchema("SD2", { "D2_ITEM", "D2_COD", "D2_QUANT", "D2_PRCVEN", "D2_TOTAL", "D2_TES", "D2_TP", "D2_CUSTO1", "D2_NFORI", "D2_SERIORI", "D2_ITEMORI"} )
	self:aliasToSchema("SB1", { "B1_DESC", "B1_UM" } )
	self:aliasToSchema("SA2", { "A2_NOME" } )

	self:enableFilterByBranch("SF2")

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 31/10/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class returninvoicesReportsBusinessObject

	local cQuery		as character
	local nX	 := 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL() 
	cQuery += " "
	cQuery += "	FROM " + RetSQLName("SF2") +" SF2 "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SD2") + " SD2 "
	cQuery += "			ON	" + FWJoinFilial("SD2", "SF2")	
	cQuery += "			AND	D2_DOC			= F2_DOC "
	cQuery += "			AND	D2_SERIE		= F2_SERIE "
	cQuery += "			AND	D2_CLIENTE 		= F2_CLIENTE "
	cQuery += "			AND	D2_LOJA			= F2_LOJA "
	cQuery += "			AND	SD2.D_E_L_E_T_ 	= ? "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1", "SF2")	
	cQuery += "			AND	B1_COD			= D2_COD "
	cQuery += "			AND	SB1.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2", "SF2")	
	cQuery += "			AND	A2_COD			= SD2.D2_CLIENTE "
	cQuery += "			AND	A2_LOJA			= SD2.D2_LOJA "
	cQuery += "			AND	SA2.D_E_L_E_T_ 	= ? "
	cQuery += " "
	cQuery += "	WHERE "
	cQuery += "			F2_FILIAL		IN (?) " 
	cQuery += "		AND	F2_SERIE		>= ? "
	cQuery += "		AND	F2_SERIE		<= ? "
	cQuery += "		AND	F2_EMISSAO		>= ? "
	cQuery += "		AND	F2_EMISSAO		<= ? "
	cQuery += "		AND	F2_DOC			>= ? "
	cQuery += "		AND	F2_DOC			<= ? "
	cQuery += "		AND	F2_CLIENTE		>= ? "
	cQuery += "		AND	F2_CLIENTE		<= ? "
	cQuery += "		AND	F2_TIPO			= ? "	
	cQuery += "		AND	SF2.D_E_L_E_T_	= ? "
	cQuery += "		AND	D2_COD			>= ? "
	cQuery += "		AND	D2_COD			<= ? "
	cQuery += self:setFilterOnWhere()
	cQuery += " ORDER BY " + SqlOrder(SA2->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SF2")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := 'D'
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10
    
	cQuery := self:processSQLStatement(cQuery)

return cQuery
