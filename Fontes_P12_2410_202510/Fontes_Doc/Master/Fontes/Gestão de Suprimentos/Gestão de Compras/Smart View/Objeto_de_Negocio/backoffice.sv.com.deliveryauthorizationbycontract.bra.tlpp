#include "msobject.ch"
#include "protheus.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGACOM", tables="SC3", name="Autorizacao de Entrega Por Contrato - Localizado", country="BRA")
 
class deliveryauthorizationbycontractReportsBusinessObjectsBRA from totvs.protheus.backoffice.com.smartView.integratedProvider.deliveryauthorizationbycontractReportsBusinessObjects
    
	public method new() as object

	protected method preSchema() as object
	protected method preData() as object
	protected method processData() as object

	private data aFldSC3 as array

endclass


method New() as object class deliveryauthorizationbycontractReportsBusinessObjectsBRA

    _Super:new()
	
	::aFldSC3 := {"C3_IPI", "C3_BASEIPI", "C3_VALIPI"}

return self


method preSchema() as object class deliveryauthorizationbycontractReportsBusinessObjectsBRA

	self:oSchema:aliasToSchema("SC3", ::aFldSC3)
	
return self:oSchema


method preData() as object class deliveryauthorizationbycontractReportsBusinessObjectsBRA

	Local nx as numeric

	For nx:= 1 to Len(::aFldSC3)
		::cSelectComplement += ", " + ::aFldSC3[nx]
	Next

return self:oData


method processData() as object class deliveryauthorizationbycontractReportsBusinessObjectsBRA

local nX		as numeric

	if (self:cAlias)->(!EOF())
		for nX := 1 to len(::aFldSC3)
			if At("_",::aFldSC3[nX]) > 0
				self:jData[::aFldSC3[nX]] := (self:cAlias)->&(::aFldSC3[nX])
			endif
		next nX
	endif
	
return self:oData
