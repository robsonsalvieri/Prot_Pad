#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "rh.sv.taf.esocialconference.ch"

namespace totvs.protheus.esocial.taf.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATAF", tables="TAFST2, TAFXERP",;
name="EsocialConference", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*/{Protheus.doc} EsocialConferenceSmartViewBusinessObject

@author Evandro Italo
@since 08/11/2023
@version 1.0

*/
//-------------------------------------------------------------------  

class EsocialConferenceSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new()       as object
    public method getData()   as object
    public method getSchema() as object 
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Evandro Italo
@since 08/11/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method new() class EsocialConferenceSmartViewBusinessObject

local oTafTools := Nil as object

oTafTools := totvs.protheus.fiscal.taf.treportsintegratedprovider.tafSmartviewTools():new() //Classe para auxiliar no coverage 100% e automacao.

_Super:new()
    //Define Area
self:appendArea(STR0001)     //"TAF eSocial"
    //Define o nome do objeto de negócio
self:setDisplayName(STR0002) //"Conferência dos eventos do eSocial"
    //Define a descrição do objeto de negócio
self:setDescription(STR0003) //"Objeto EsocialConference - Conferência dos eventos do eSocial (TAFST2, TAFXERP)"
    //Define as perguntas, arquivo da SX1

if !::setPergunte("ESOCIAL000")
    FwLogMsg("WARN",,"SmartView",,,,STR0004,,,)
endif //"Grupo de perguntas nao encontrado"

freeobj(oTafTools)

return nil 

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Evandro Italo
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------   

method getData(nPage as numeric, oFilter as object) as object class EsocialConferenceSmartViewBusinessObject

    Local cAlias            as character
    Local cQuery            as character
    Local cTafErr           as character
    Local cIdEvento	        as character
    Local cFilEvento        as character
    Local cTafTicket        as character
    Local cTafKey           as character
    Local cOfDate           as character
    Local cToDate           as character 
    Local oFiltQry          as json
    Local aBind         := {}  as array
    Local nI            := 0   as numeric
    Local nTamBind      := 0   as numeric
    Local cCodErr           as character 
    
    oFiltQry := oFilter:getParameters()

    cTafTicket      := Alltrim(oFiltQry['MV_PAR01',1]) //TAFTICKET ?
    cTafKey         := Alltrim(oFiltQry['MV_PAR02',1]) //TafKey ?
    cOfDate         := subStr(StrTran( oFiltQry['MV_PAR03',1] , "-", ""),1,8) //Data De ?
    cToDate         := subStr(StrTran( oFiltQry['MV_PAR04',1] , "-", ""),1,8) //Data Até ?

    cQuery := " SELECT  "
    cQuery += " ST2.TAFFIL, ST2.TAFTICKET, ST2.TAFKEY, ST2.TAFTPREG, ST2.TAFDATA, ST2.TAFHORA, ST2.TAFCODMSG, "
    cQuery += " CASE "
	cQuery += " WHEN XERP.TAFSTATUS = ? THEN 'Inclusão'   " 
	cQuery += " WHEN XERP.TAFSTATUS = ? THEN 'Alteração'  "
	cQuery += " WHEN XERP.TAFSTATUS = ? THEN 'Exclusão'   "
	cQuery += " WHEN XERP.TAFSTATUS = ? THEN 'Fila' "
	cQuery += " WHEN XERP.TAFSTATUS = ? THEN 'Erro' "
	cQuery += " WHEN XERP.TAFSTATUS = ? THEN 'Erro' "
	cQuery += " WHEN XERP.TAFSTATUS = ? THEN 'Erro' " 
	cQuery += " ELSE '-' "
	cQuery += " END TAFSTATUS "
    cQuery += " , XERP.TAFCODERR, XERP.TAFRECNO, XERP.TAFALIAS "
    cQuery += " FROM TAFST2 ST2  "
    cQuery += " LEFT JOIN TAFXERP XERP  ON XERP.TAFKEY = ST2.TAFKEY AND XERP.TAFTICKET = ST2.TAFTICKET AND XERP.D_E_L_E_T_ = ? "
    cQuery += " WHERE ST2.D_E_L_E_T_ = ? AND ST2.TAFCODMSG = ? "
    cQuery += " AND ST2.TAFDATA BETWEEN ? AND ? "
    If !EMPTY( cTafTicket )
        cQuery += " AND ST2.TAFTICKET = ? "
    EndIf
    If !EMPTY( cTafKey )
        cQuery += " AND ST2.TAFKEY = ? "
    EndIf
    
    // Instancia a classe
    oPrepare := FwExecStatement():New( ChangeQuery(cQuery) ) 

    // Monta array com os bind
    Aadd( aBind , {'C','1'})				
    Aadd( aBind , {'C','2'})				
    Aadd( aBind , {'C','3'})				
    Aadd( aBind , {'C','4'})				
    Aadd( aBind , {'C','7'})				
    Aadd( aBind , {'C','8'})				
    Aadd( aBind , {'C','9'}) 				
    Aadd( aBind , {'C',' '}) 				
    Aadd( aBind , {'C',' '}) 				
    Aadd( aBind , {'C','2'})						
    Aadd( aBind , {'C',cOfDate } )			
    Aadd( aBind , {'C',cToDate } )		   
    If !EMPTY( cTafTicket )
        Aadd( aBind , {'C', AllTrim(cTafTicket)})			
    EndIf
    If !EMPTY( cTafKey )
        Aadd( aBind , {'C', AllTrim(cTafKey) })
    EndIf

    nTamBind := Len(aBind)
    
    For nI := 1 to nTamBind
        If  aBind[nI][1] == 'C'
            oPrepare:setString(nI, aBind[nI][2])
        Endif
    Next 

    cAlias := oPrepare:OpenAlias() 

    While !(cAlias)->(Eof())

        cIdEvento	:= "-"
        cFilEvento  := "-"
        cCodErr     := "-"
        cTafErr     := "-"

        cTafErr := getErrXERP(Alltrim(( cAlias )->TAFCODERR ))
            
        If !EMPTY( (cAlias)->TAFALIAS ) .And. !EMPTY( (cAlias)->TAFRECNO )
            ((cAlias)->TAFALIAS )->( DbGoTo( (cAlias)->TAFRECNO ) )

			cIdEvento	:= ( (cAlias)->TAFALIAS )->&( (cAlias)->TAFALIAS + "_ID" )
			cFilEvento  := ( (cAlias)->TAFALIAS )->&( (cAlias)->TAFALIAS + "_FILIAL" )

        EndIf

        If !EMPTY( ( cAlias )->TAFCODERR )
            cCodErr := ( cAlias )->TAFCODERR
        Else
            cTafErr := '-'
        Endif

        self:oData:appendData({ "_TAFFIL"    : (cAlias)->TAFFIL                                                  ,;
                                "_TAFTICKET" : Alltrim(( cAlias )->TAFTICKET)                                    ,;
                                "_TAFKEY"    : Alltrim(( cAlias )->TAFKEY   )                                    ,;
                                "_TAFTPREG"  : Alltrim(( cAlias )->TAFTPREG )                                    ,;
                                "_TAFSTATUS" : Alltrim(( cAlias )->TAFSTATUS )                                   ,;
                                "_IDEVENTO"  : cIdEvento                                                         ,;
                                "_FILEVENTO" : cFilEvento                                                        ,;
                                "_TAFCODERR" : cCodErr                                                           ,;
                                "_CTAFERR"   : cTafErr                                                           ,;
                                "_TAFDATA"   : DtoC(Stod(StrTran(SubStr(FwTimeStamp(5,sToD((cAlias) ->TAFDATA),"00:00:00"),1,10 ), "-","")))  ,;
                                "_TAFHORA"   : (cAlias) ->TAFHORA                                                })

        (cAlias)->( dbSkip() )  

    Enddo

    (cAlias)->( DBCloseArea() )
    oPrepare:Destroy()

    aSize(aBind,0) 
	aBind := Nil
    oPrepare := nil 
	freeobj(oFiltQry)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Evandro Italo
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getSchema() as object class EsocialConferenceSmartViewBusinessObject

    regMiddLS()
    //              ( Nome               ,  Descricao               ,  Tipo   , Display                 , cRealName
    self:addProperty("_TAFFIL"           , "Filial"                 , "string", "Filial"                , "TAFFIL"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("_TAFTICKET"        , "Taf Ticket"             , "string", "Taf Ticket"            , "TAFTICKET"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("_TAFKEY"           , "Taf Key"                , "string", "Taf Key"               , "TAFKEY"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("_TAFTPREG"         , "Evento e-Social"        , "string", "Evento"                , "TAFTPREG"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("_TAFSTATUS"        , "Status"                 , "string", "Status"                , "TAFSTATUS"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("_IDEVENTO"         , "ID Evento e-Social"     , "string", "ID Evento"             , "IDEVENTO"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("_FILEVENTO"        , "Filial do Evento"       , "string", "Filial do Evento"      , "FILEVENTO"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("_TAFCODERR"        , "Código de Erro"         , "string", "Cod. Erro"             , "TAFCODERR"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("_CTAFERR"          , "Descrição do Erro"      , "string", "Descrição do Erro"     , "CTAFERR"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("_TAFDATA"          , "Data"                   , "string", "Data"                  , "TAFDATA"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("_TAFHORA"          , "Hora"                   , "string", "Hora"                  , "TAFHORA"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )

return self:oSchema

/*/{Protheus.doc} getErrXERP

Retorna a descrição do erro conforme array aTafErro alimentado pela função
TAFCodErr.

@param cCodErr 		- Código do Erro

@return cDescErr	- Descrição do Erro.

@author Evandro dos Santos Oliveira
@Alteração Evandro Italo
@since 14/06/2018 - Alteração em 16/11/2023

@version 1.0

/*/

Static Function getErrXERP(cCodErr as character)

    Local aTafErro := TAFCodErr() as array
	Local nPos     := 0           as numeric
	Local cDescErr := ""          as character

	nPos :=  aScan(aTafErro,{|x|x[1] == AllTrim(cCodErr)})

	If nPos > 0
		cDescErr := aTafErro[nPos][2]
	EndIf
    cDescErr := removeLineBreak(cDescErr)

Return (cDescErr)

//----------------------------------------------------------------------------
/*/{Protheus.doc} removeLineBreak

Troca a quebra de linha CRLF para \n

@param cString - String a ser normalizada

@author Evandro dos Santos Oliveira
@Alteração Evandro Italo
@since 14/06/2018 - Alteração em 16/11/2023
@version 1.0
/*/
//---------------------------------------------------------------------------
Static Function removeLineBreak(cString as character)

	Local cDescRet := "" as character

	cDescRet := StrTran(cString,Chr(13)+Chr(10),"\n")

Return (cDescRet) 

//---------------------------------------------------------------------
/*/{Protheus.doc} regMiddLS
@type			function
@description	Registra o Uso da Rotina no License Server
@author			Evandro dos Santos Oliveira
@since			17/09/2021
@param			Nil 
@return			Nil 
/*/
//---------------------------------------------------------------------
Static Function regMiddLS()

	Local cCode		:= ""
	Local cUser		:= "000000"
	Local cModule	:= ""
	Local cRoutine  := ""
    Local lLogLS    := .F. 
    Local lSecurity := Nil  

	If !lLogLS 
		cCode 		:= "LS006"
		cModule		:= "84"
		cRoutine  	:= ProcName(-1)

		If lSecurity == Nil 
			lSecurity := ( GetPvProfString("HTTPREST","Security","0",getAdv97()) == "1" )
		EndIf		

		If lSecurity
			cUser := RetCodUsr()
		EndIf 		

		//Função para gravar o uso de rotinas e enviar ao LS (License Server)
		Iif(FindFunction('FWLsPutAsyncInfo'),FWLsPutAsyncInfo(cCode,cUser,cModule,cRoutine),)
		lLogLS := .T.
	EndIf

Return Nil
