#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "rh.sv.taf.EsocialTransmition.ch"

namespace totvs.protheus.esocial.taf.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATAF",;
tables="C1E,C92,C8R,C99,C1G,C91,T62,T3P,T1M,T2A,T3V,T1S,CUO,T3A,C9V,T1U,T1V,CM0,C8B,CM6,V72,CM9,T3B,CMF,CMD,T0F,T92,V73,V75,V76,V77,V78,V9U,V7C,CMJ,V7J,T2M,T2G,V2P,T2V,V2Z,V7K",;
name="EsocialTransmition", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*/{Protheus.doc} EsocialTransmitionSmartViewBusinessObject

@author Denis Souza
@since 12/12/2023
@version 1.0

*/
//-------------------------------------------------------------------
class EsocialTransmitionSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()       as object
    public method getData()   as object
    public method getSchema() as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Denis Souza
@since 12/12/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method new() class EsocialTransmitionSmartViewBusinessObject
local oTafTools := Nil as object

oTafTools := totvs.protheus.fiscal.taf.treportsintegratedprovider.tafSmartviewTools():new() 

_Super:new()
self:appendArea(STR0001) //"TAF eSocial"
self:setDisplayName(STR0002) //"Eventos transmitidos do eSocial"
self:setDescription(STR0003 + " - " + STR0002 + "( " + STR0004 + " )") //"Objeto EsocialTransmition"##"Eventos transmitidos do eSocial"##"C1E,C92,C8R,C99,C1G,C91,T62,T3P,T1M,T2A,T3V,T1S,CUO,T3A,C9V,T1U,T1V,CM0,C8B,CM6,V72,CM9,T3B,CMF,CMD,T0F,T92,V73,V75,V76,V77,V78,V9U,V7C,CMJ,V7J,T2M,T2G,V2P,T2V,V2Z,V7K"

oTafTools:validPerg(self:setPergunte("ESOCIAL002"))

freeobj(oTafTools)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
@return object: self:oData

@author Denis Souza
@since 12/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class EsocialTransmitionSmartViewBusinessObject

local oFiltQry   := oFilter:getParameters() as json
local cQryFil    := Alltrim(oFiltQry['MV_PAR01',1]) as character //Filial ?
local cEvent     := Alltrim(oFiltQry['MV_PAR02',1]) as character //Evento(S-9999) ?
local cPeriod    := Alltrim(oFiltQry['MV_PAR03',1]) as character //Periodo
local cStatus    := Alltrim(oFiltQry['MV_PAR04',1]) as character //Status ?
local cOfDate    := "" as character
local cToDate    := "" as character
local cCompC8E   := "" as character
local cMothYear  := "" as character
local cEvtDate   := "" as character
local cSystDate  := "" as character
local cYear      := "" as character
local cName      := "" as character
local aInfEUF    := {} as array
local nEscopo    := 2  as numeric //e-social
local cAlias     := {} as array

aInfEUF := TAFTamEUF(Upper(AllTrim(SM0->M0_LEIAUTE)))

if !Empty(cPeriod)

    cOfDate   := Stod(cPeriod + "01")
    cToDate   := LastDay( cOfDate )
    cMothYear := cPeriod
    cCompC8E  := Upper(AllTrim(FWModeAccess("C8E",1)+FWModeAccess("C8E",2)+FWModeAccess("C8E",3)))

    cAlias := TAFSVeSocial(cQryFil,cStatus,nEscopo,cEvent,cPeriod,cOfDate,cToDate,cCompC8E,aInfEUF)

    if !Empty(cAlias)

        while !(cAlias)->(Eof())

            cName := ''
            cCGC  := ''

            aSM0 := FWSM0Util():GetSM0Data(cEmpAnt,(cAlias)->FIL ,{"M0_FILIAL","M0_CGC"})
            if len(aSM0) >= 2
                cName := SubStr(Alltrim(aSM0[1][2]),1,30)
                cCGC := Alltrim(aSM0[2][2])
            endif
            if len(cCGC) == 14
                cCGC := Transform(cCGC,"@R! NN.NNN.NNN/NNNN-99")
            endif

            cSystDate := Alltrim((cAlias)->DIS)
            cEvtDate  := Alltrim((cAlias)->DTE)
            cYear     := ''

            if len(cEvtDate) == 6 //C-Carga Inicial,M-Mensal,E-Eventual,T-Totalizador(nao gera conforme cEvtType <> "T")
                if (cAlias)->TIP == 'M' .Or. (cAlias)->TIP == 'E'
                    if (substr(cEvtDate,1,4) >= '2001' .And. substr(cEvtDate,1,4) <= '2099') .And. !Empty(substr(cEvtDate,5,2)) //062023
                        cEvtDate += '01'
                    endif
                ElseIf (cAlias)->TIP == 'C'
                    if (cAlias)->EVT == "S-1000"
                        cEvtDate += '01'
                    else
                        if (substr(cEvtDate,3,4) >= '2001' .And. substr(cEvtDate,3,4) <= '2099') //062023
                            cYear := substr(cEvtDate,3,4)
                        endif
                        if !Empty(cYear)
                            cEvtDate := cYear + substr(cEvtDate,1,2) + '01'
                        endif
                    endif
                endif
            endif

            cEvtDate := IIF(!Empty(cEvtDate), DtoC(Stod(StrTran(SubStr(totvs.framework.treports.date.dateToTimeStamp(sToD(cEvtDate),"00:00:00"),1,10), "-",""))), "  /  /    ")
            cSystDate  := IIF(!Empty(cSystDate), DtoC(Stod(StrTran(SubStr(totvs.framework.treports.date.dateToTimeStamp(sToD(cSystDate),"00:00:00"),1,10), "-",""))), "  /  /    ")

            self:oData:appendData({ "_FIL"   : (cAlias)->FIL ,; //01 "Filial"
                                    "_CGC"   : cCGC          ,; //02 "CGC"
                                    "_NMFIL" : cName         ,; //03 "Nome"
                                    "_EVENTO": (cAlias)->EVT ,; //04 "Cod.Evento"
                                    "_DESEVT": (cAlias)->DEVT,; //05 "Descrição Evt."
                                    "_ID"    : (cAlias)->ID  ,; //06 "Id do Registro"
                                    "_DESCRI": (cAlias)->DSC ,; //07 "Descrição Reg."
                                    "_PERIOD": cMothYear     ,; //08 "Período"
                                    "_DTEVEN": cEvtDate      ,; //09 "Dt.Evento"
                                    "_DTINCS": cSystDate     ,; //10 "Dt.Incl.Sis."
                                    "_RECIBO": (cAlias)->PRT ,; //11 "Recibo"
                                    "_STATUS": (cAlias)->STS }) //12 "Status"

            (cAlias)->( dbSkip() )
        enddo
        (cAlias)->( DBCloseArea() )              
    endif
endif

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Denis Souza
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class EsocialTransmitionSmartViewBusinessObject

self:addProperty( "_FIL"    , "Filial"            , "string", "Filial"            , "FIL"   , , , ) //01
self:addProperty( "_CGC"    , "CGC"               , "string", "CGC"               , "CGC"   , , , ) //02
self:addProperty( "_NMFIL"  , "Nome"              , "string", "Nome"              , "NMFIL" , , , ) //03
self:addProperty( "_EVENTO" , "Cod.Evento"        , "string", "Cod.Evento"        , "EVENTO", , , ) //04
self:addProperty( "_DESEVT" , "Descrição Evento"  , "string", "Descrição Evento"  , "DESEVT", , , ) //05
self:addProperty( "_ID"     , "Id do Registro"    , "string", "Id do Registro"    , "ID"    , , , ) //06
self:addProperty( "_DESCRI" , "Descrição Registro", "string", "Descrição Registro", "DESCRI", , , ) //07
self:addProperty( "_PERIOD" , "Período"           , "string", "Período"           , "PERIOD", , , ) //08
self:addProperty( "_DTEVEN" , "Data Evento"       , "string", "Data Evento"       , "DTEVEN", , , ) //09
self:addProperty( "_DTINCS" , "Inclusão Sistêmica", "string", "Inclusão Sistêmica", "DTINCS", , , ) //10
self:addProperty( "_RECIBO" , "Recibo"            , "string", "Recibo"            , "RECIBO", , , ) //11
self:addProperty( "_STATUS" , "Status"            , "string", "Status"            , "STATUS", , , ) //12

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} TAFSVeSocial
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Denis Souza
@since 12/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Static Function TAFSVeSocial(cQryFil,cStatus,nEscopo,cEvent,cPeriod,dDataIni,dDataFim,cCompC8E,aInfEUF)

local cQry	    := "" as character
local cQuery    := "" as character
local cAliasTb  := "" as character
local cLayout   := "" as character
local cDescrip  := "" as character
local cDtEvt    := "" as character
local cEvtType  := "" as character
local cTagXml   := "" as character
local cBancoDB  := "" as character
local cColDsc   := "" as character
local cColDtE   := "" as character
local cColDtIs  := "" as character
local nX	    := 0  as numeric
local nTMDtIncS := 0  as numeric
local nDscSize  := 0  as numeric
local nDtESize  := 0  as numeric
local nDtIS     := 0  as numeric
local nPrtSize  := 0  as numeric
local lDtIncSis := .F. as logical
local lActive   := .F. as logical
local aBind     := {}  as array
local aBindEvt  := {}  as array
local aBindSts  := {}  as array
local aBindLeft := {}  as array
local nI        := 0   as numeric
local nTamBind  := 0   as numeric
local oPrepare  := nil as object
local cAliasQry := {}  as array

default cQryFil	  := ""
default cStatus	  := ""
default nEscopo   := 2 //esocial
default cEvent    := ""
default cPeriod   := ""
default dDataIni  := ""
default dDataFim  := ""
default cCompC8E  := ""
default aInfEUF   := {}

cBancoDB := Upper(Alltrim(TcGetDb()))
aEventos := TAFRotinas( ,, .T., nEscopo )

nDscSize := GetSX3Cache("C9V_NOMEV ","X3_TAMANHO") 
nDtESize := GetSX3Cache("C9V_DTINIV","X3_TAMANHO") 
nDtIS    := GetSX3Cache("C9V_DINSIS","X3_TAMANHO") 
nPrtSize := GetSX3Cache("C9V_PROTUL","X3_TAMANHO") 
cColDsc  := ",CAST(' ' AS CHAR("+Str(nDscSize)+")) DSC"
cColDtE  := ",CAST(' ' AS CHAR("+Str(nDtESize)+")) DTE"
cColDtIs := ",CAST(' ' AS CHAR("+Str(nDtIS)+")) DIS"

For nX := 1 To Len(aEventos)

    //Desconsiderar os eventos que tiverem a posicao 19 do fonte TAFROTINAS que estiverem com false (eventos obsoletos)
    //Desconsiderar os eventos que tiverem a posicao 09 do fonte TAFROTINAS vazia (tag evento)
    //Desconsiderar os eventos Totalizadores (posicao 12 do fonte TAFROTINAS)    
    //[4] Layout Totvs [9] Tag do Evento eSocial [12] Tipo do Evento (C-Carga Inicial,M-Mensal,E-Eventual,T-Totalizador) [19] Evento ativo com a versão corrente do E-Social?

    cAliasTb := aEventos[nX][3]  //Nome da Tabela
    cLayout	 := aEventos[nX][4]  //Layout Totvs
    cDescrip := aEventos[nX][14] //Descrição do evento, ex: Nome do trabalhador, Desc. Rubrica etc..
    cDtEvt   := aEventos[nX][6]  //Data do Evento
    cEvtType := aEventos[nX][12] //Tipo do Evento C,M,E,T  ver{ (cAlias)->TIP == 'C' }
    cTagXml  := aEventos[nX][9]  //Tag do Evento eSocial
    lActive  := aEventos[nX][19] //Evento ativo com a versão corrente do E-Social?

    If !Empty(cLayout) .And. !Empty(cTagXml) .And. !Empty(cEvtType) .And. lActive
        if Empty(cEvent) .Or. (cEvent == cLayout)
            if !(cAliasTb $ cQry) //ex: S-1200 e S-1202 utilizam a mesma tabela entao nao eh necessario fazer o union 2 x na tabela "C91"
                //Protecao para utilizar a tabela se a mesma existir no banco de dados
                If TAFAlsInDic(cAliasTb) .And. !Empty(cLayout) .And. cEvtType <> "T" .And. !(cLayout $ "TAUTO")
                    If !Empty(cQry)
                        cQry += " UNION ALL "
                    EndIf

                    cQry += " SELECT "
                    If cAliasTb == "C1E"
                        cQry += cAliasTb + "_FILTAF FIL,"
                    Else
                        cQry += cAliasTb + "_FILIAL FIL,"
                    Endif

                    cQry += cAliasTb + "_ID ID,"
                    cQry += "'" + cLayout + "' EVT,"
                    cQry += "'" + cEvtType + "' TIP"

                    //Descrição (este campo é dinâmico e varia de acordo com o evento, posição 14 do TAFROTINAS)
                    if !Empty(cDescrip) .And. GetSX3Cache(cDescrip,"X3_TIPO") == 'C'
                        cQry += "," + cDescrip + " DSC"
                    else
                        if !("POSTGRES" $ cBancoDB)
                            cColDsc := ",' ' DSC"
                        endif
                        cQry += cColDsc
                    endif

                    //Data do Evento (Em caso de não periodicos, este campo é dinâmico e varia de acordo com o evento)
                    //Se o campo não tiver data do evento não mostrar a data. Deixar em branco.
                    if !Empty(cDtEvt) .And. GetSX3Cache(cDtEvt,"X3_TIPO") $ ('CD')
                        cQry += "," + cDtEvt + " DTE"
                    else
                        if !("POSTGRES" $ cBancoDB)
                            cColDtE := ",' ' DTE"
                        endif
                        cQry += cColDtE
                    endif

                    //Data de Inclusão (Data Sistêmica - _DINSIS )
                    lDtIncSis := TafColumnPos(cAliasTb + "_DINSIS") .And. GetSX3Cache((cAliasTb + "_DINSIS"),"X3_TIPO") $ ('CD')
                    nTMDtIncS := 0
                    If lDtIncSis
                        nTMDtIncS := GetSX3Cache((cAliasTb + "_DINSIS"),"X3_TAMANHO")
                        cQry += "," + cAliasTb + "_DINSIS DIS"
                    Else
                        if !("POSTGRES" $ cBancoDB)
                            cColDtIs := ",' ' DIS"
                        endif
                        cQry += cColDtIs
                    EndIf

                    If TafColumnPos(cAliasTb + "_STATUS")
                        cQry += "," + cAliasTb + "_STATUS STS"
                    endif

                    If TafColumnPos( cAliasTb + "_PROTUL")
                        cQry += "," + cAliasTb + "_PROTUL PRT"
                    EndIf

                    cQry += " FROM " + RetSqlName((cAliasTb)) + " WHERE "

                    if !Empty( cQryFil ) //Filtro de Filial
                        If cAliasTb == "C1E"
                            cQry += cAliasTb + "_FILTAF = ? AND "
                        Else
                            cQry += cAliasTb + "_FILIAL = ? AND "                        
                        Endif
                            Aadd( aBindEvt , xFilial(cAliasTb, cQryFil) ) 
                    endif

                    cQry += cAliasTb + "_STATUS = ? AND "
                    Aadd( aBindEvt , cStatus) 

                    if cAliasTb == "CM6" //apenas CM6 ira filtrar de forma diferente, a prioridade para os demais _DINSIS e se nao possuir filtra de forma dinamica por aEventos[nX][6]
                        cQry += "(" + TafMonPAfast("",DtoS(dDataIni),DtoS(dDataFim),.F.) + ") AND "
                    else
                        //Informe a Data De para filtrar a data sistêmica a partir dessa data e a Data até para filtrar a data sistêmica até essa data.
                        if lDtIncSis
                            if nTMDtIncS == 8
                                cQry += cAliasTb + "_DINSIS BETWEEN ? AND ? AND "
                                Aadd( aBindEvt , DtoS(dDataIni)) 
                                Aadd( aBindEvt , DtoS(dDataFim))
                            endif
                        else
                            If cEvtType == "C"
                                If !Empty(cPeriod) .AND. !Empty(cDtEvt)
                                    cQry += "(" + cDtEvt + " = '" + cPeriod + "' OR " + cDtEvt + " = '" + Right(cPeriod, 2) + Left(cPeriod, 4) + "') AND "
                                EndIf
                            EndIf
                        endif
                    endif
                    cQry += cAliasTb + "_ATIVO = ? AND D_E_L_E_T_ = ? "
                    Aadd( aBindEvt , '1')
                    Aadd( aBindEvt , ' ')                    
                Endif
            EndIf
        endif
    EndIf
Next nX

if !Empty(cQry)

    cQuery := "SELECT FIL, ID, EVT, TIP, DEVT, DSC, DTE, DIS, PRT, STS FROM ( "
    cQuery += "SELECT  FIL, ID, EVT, TIP, DSC, DTE, DIS, PRT, C8E.C8E_DESCRI DEVT " //Ordenação: Filial, Evento e Data Sistêmica
    cQuery += ",CASE WHEN TMP.STS = ? THEN 'Aguard.Retorno RET' WHEN TMP.STS = ? THEN 'Inconsistente' WHEN TMP.STS = ? THEN 'Transmitido' "
    cQuery += "WHEN TMP.STS = ? THEN 'Aguard.Exclusão' WHEN TMP.STS = ? THEN 'Reg.Excluído' ELSE 'Pendente Transmissão' END STS " 
    Aadd( aBindSts ,'2') //Aguard.Retorno RET				
    Aadd( aBindSts ,'3') //Inconsistente				
    Aadd( aBindSts ,'4') //Transmitido				
    Aadd( aBindSts ,'6') //Aguard.Exclusão				
    Aadd( aBindSts ,'7') //Reg.Excluído	
    cQuery += "FROM (" + cQry + ") TMP ""
    cQuery += "LEFT JOIN " + RetSqlName("C8E") + " C8E ON C8E.C8E_FILIAL = ? AND C8E.C8E_CODIGO = TMP.EVT AND C8E.D_E_L_E_T_ = ? ) TAB "
    Aadd( aBindLeft ,xFilial("C8E",cQryfil) )
    Aadd( aBindLeft ,' ')  

    For nI := 1 to Len(aBindSts) 
        aAdd( aBind , aBindSts[nI] )
    Next nI

    For nI := 1 to Len(aBindEvt) 
        aAdd( aBind , aBindEvt[nI] )
    Next nI

    For nI := 1 to Len(aBindLeft) 
        aAdd( aBind , aBindLeft[nI] )
    Next nI
       
    nTamBind := Len(aBind) 

    oPrepare := FwExecStatement():New( ChangeQuery(cQuery) ) 
    
    For nI := 1 to nTamBind
        oPrepare:setString( nI, aBind[nI] )
    Next 

    cAliasQry := oPrepare:OpenAlias() 

    oPrepare:Destroy()
    aSize(aBind,0)
    aBind := Nil
    oPrepare := nil 

endif

Return cAliasQry
