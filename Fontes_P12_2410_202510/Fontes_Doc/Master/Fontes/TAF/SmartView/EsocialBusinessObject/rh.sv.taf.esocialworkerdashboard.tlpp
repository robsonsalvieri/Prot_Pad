#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "rh.sv.taf.esocialworkerdashboard.ch"

namespace totvs.protheus.esocial.taf.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATAF", tables="C91,T62,T3P,C9V,T1U,T1V,CM0,C8B,CM6,V72,CM9,CMF,CMD,T0F,T92,V73,V75,V76,V77,V78,V9U,V7C,CMJ",name="EsocialWorkerDashboard", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*/{Protheus.doc} EsocialWorkerDashboardSmartViewBusinessObject

@author Evandro Italo / Denis Souza
@since 15/12/2023
@version 1.0

*/
//-------------------------------------------------------------------

class EsocialWorkerDashboardSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new()       as object
    public method getData()   as object
    public method getSchema() as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Evandro Italo / Denis Souza   
@since 15/12/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method new() class EsocialWorkerDashboardSmartViewBusinessObject

_Super:new()
self:appendArea(STR0001) //"TAF eSocial"
self:setDisplayName(STR0002) //"eSocial - Painel do trabalhador"
self:setDescription(STR0003 + " - (C91,T62,T3P,C9V,T1U,T1V,CM0,C8B,CM6,V72,CM9,CMF,CMD,T0F,T92,V73,V75,V76,V77,V78,V9U,V7C,CMJ)") //"Objeto esocialworkerdashboard - Painel do trabalhador"
if !::setPergunte("ESOCIAL001")
    FwLogMsg("WARN",,"SmartView",,,,STR0004,,,) //"Grupo de perguntas nao encontrado"
endif

return nil

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Evandro Italo / Denis Souza
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------   

method getData(nPage as numeric, oFilter as object) as object class EsocialWorkerDashboardSmartViewBusinessObject

    Local aEventos    := {}  as array
    Local aSM0        := {}  as array
    Local cQry        := ""  as character
    Local cEvtTab     := ""  as character
    Local cC9VTab     := ""  as character
    Local cEvent 	  := ""  as character
    Local cParEvent   := ""  as character
    Local cOfDate     := ""  as character
    Local cToDate     := ""  as character
    Local cCPFWorker  := ""  as character
    Local cParCPF     := ""  as character
    Local cNameWorker := ""  as character
    Local cIdWorker   := ""  as character
    Local cMatric     := ""  as character
    Local cFil        := ""  as character
    local cName       := ""  as character
    local cCGC        := ""  as character
    Local cDtIncSis   := ""  as character
    Local cDtEvent    := ""  as character
    Local cStatus     := ""  as character
    Local cProtocol   := ""  as character
    Local cID         := ""  as character
    Local cVersion    := ""  as character
    Local cDateType   := "0" as character
    local nEscopo     := 2   as numeric //esocial
    local cBanco      := Upper( AllTrim( TCGetDB() ) )
    Local lHasNext    := .F. as logical
    Local oFiltQry    := nil as json
    local iCont       := 1	as integer
    Local oQueryC9V   := Nil as object 

    Default nPage   := 0
    Default oFilter := nil

    If Len(aEventos) == 0
        aEventos := TAFRotinas( ,, .T., nEscopo )
    EndIf

    oFiltQry := oFilter:getParameters()

    cParCPF    := Alltrim(oFiltQry['MV_PAR01',1]) //CPF?
    cParEvent  := Alltrim(oFiltQry['MV_PAR02',1]) //Evento?
    cDateType  := cValtoChar(oFiltQry['MV_PAR03',1]) //Tipo de data? Combo Opcao Numerica default 1-Sistemica 2-Evento
    cPeriod    := subStr(StrTran( oFiltQry['MV_PAR04',1] , "-", ""),1,8) //Data do período?

    if Empty(cParEvent)
        cParEvent := ' '
    endif

    If Empty(cPeriod)
        cPeriod := ' '
    endif

    If Empty(cDateType) .Or. cDateType == "0"
        cDateType := "1" //Default 1-Sistemica
    endif

    /*
    Importante: como se inspira no painel do trabalhador, o uso do CPF se torna primordial, como no grupo de perguntas nao eh possivel deixar o campo obrigatorio,
    para realizar a consulta sera necessario no minimo o preenchimento do CPF e periodo, com ele sera possivel criar uma visao de eventos do eSocial por trabalhador,
    nesse caso, nao havera paginacao, ja que o CPF e periodo ira restringir muito o filtro e mesmo vinculado a mais de 30 eventos o retorno nao havera grande
    quantidade de registros (de forma expressiva), por isso o lHasNext sempre sera false e havera um loop no CPF, caso haja mais de um vinculo no CPF, inclusive em outra filial.
    */
    lHasNext := .F.

    if !Empty( cParCPF ) .And. !Empty(cPeriod)

        cOfDate := Stod(cPeriod + "01")
        cToDate := LastDay( cOfDate )

        //O Trabalhador pode possuir registros em filiais diferentes, nesta situação a matricula PODE ser diferente
        cQry := "SELECT C9V.C9V_FILIAL,C9V.C9V_CPF,C9V.C9V_MATRIC,C9V.C9V_NOME,C9V.C9V_ID, "
        cQry += "C9V.C9V_VERSAO FROM " + RetSqlName("C9V") + " C9V "
        cQry += "WHERE C9V.D_E_L_E_T_ = ? AND "
        cQry += "C9V.C9V_CPF = ? "
        cQry += "ORDER BY C9V.C9V_FILIAL, C9V.C9V_CPF "
        
        oQueryC9V := FwExecStatement():New( ChangeQuery(cQry) ) 
        
        oQueryC9V:setString(iCont++, ' ' )
        oQueryC9V:setString(iCont++, cParCPF )

        cC9VTab := oQueryC9V:OpenAlias() 

        while (cC9VTab)->(!Eof())

            cFil        := (cC9VTab)->C9V_FILIAL
            cIdWorker   := (cC9VTab)->C9V_ID
            cNameWorker := (cC9VTab)->C9V_NOME
            cMatric     := (cC9VTab)->C9V_MATRIC

            cEvtTab := TafSvETrab(cFil, cBanco, cParCPF, cIdWorker, nEscopo, cParEvent, cDateType, cPeriod, cOfDate, cToDate)
            if !Empty( cEvtTab )
                while !(cEvtTab)->(Eof())
                    cName := ''
                    cCGC  := ''
                    cCPFWorker := ''
                    aSM0  := FWSM0Util():GetSM0Data(cEmpAnt,cFil,{"M0_FILIAL","M0_CGC"})
                    if len(aSM0) >= 2
                        cName := SubStr(Alltrim(aSM0[1][2]),1,30)
                        cCGC := Alltrim(aSM0[2][2])
                    endif
                    if len(cCGC) == 14
                        cCGC := Transform(cCGC,"@R! NN.NNN.NNN/NNNN-99")
                    endif
                    if len(cParCPF) == 11
                        cCPFWorker := Transform(cParCPF,"@R 999.999.999-99")
                    endif

                    cEvent    := Alltrim((cEvtTab)->EVT)
                    cDtIncSis := (cEvtTab)->DIS
                    cDtEvent  := (cEvtTab)->DTE
                    cStatus   := Alltrim((cEvtTab)->STS)
                    cProtocol := Alltrim((cEvtTab)->PRT)
                    cID       := Alltrim((cEvtTab)->ID)
                    cVersion  := Alltrim((cEvtTab)->VRS)
                    cDscEvt   := Alltrim((cEvtTab)->DEVT)

                    cDtIncSis := DtoC(Stod(StrTran(SubStr(FwTimeStamp(5,sToD(cDtIncSis),"00:00:00"),1,10), "-","")))
                    cDtEvent := DtoC(Stod(StrTran(SubStr(FwTimeStamp(5,sToD(cDtEvent),"00:00:00"),1,10), "-","")))

                    self:oData:appendData({ "_FIL"   : cFil        ,; //01 _FIL    "Filial"
                                            "_CGC"   : cCGC        ,; //02 _CGC    "CGC"
                                            "_NMFIL" : cName       ,; //03 _NMFIL  "Razão Social"
                                            "_NOME"  : cNameWorker ,; //04 _NOME   "Nome Trabalhador"
                                            "_CPF"   : cCPFWorker  ,; //05 _CPF    "CPF"
                                            "_MATRIC": cMatric     ,; //06 _MATRIC "Matrícula"
                                            "_EVENTO": cEvent      ,; //07 _EVENTO "Evento"
                                            "_DSCEVT": cDscEvt     ,; //08 cDscEvt "Descrição Evento"
                                            "_DTINCS": cDtIncSis   ,; //09 _DTINCS "Inclusão Sistêmica"
                                            "_DTPER" : cDtEvent    ,; //10 _DTPER  "Data Período"
                                            "_STATUS": cStatus     ,; //11 _STATUS "Status"
                                            "_RECIBO": cProtocol   ,; //12 _RECIBO "Recibo"
                                            "_ID"    : cID         ,; //13 _ID     "Identificador"
                                            "_VERSAO": cVersion    }) //14 _VERSAO "Versão"

                    (cEvtTab)->( dbSkip() )
                enddo
                (cEvtTab)->( DBCloseArea() )
            endif
            (cC9VTab)->( dbSkip() )
        EndDo
        (cC9VTab)->( DBCloseArea() )
    endif

    self:setHasNext( lHasNext )

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Evandro Italo / Denis Souza
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class EsocialWorkerDashboardSmartViewBusinessObject

    regMiddLS()

    //              ( Nome     ,  Descricao          , Tipo    , Display             , cRealName
    self:addProperty( "_FIL"   , "Filial"            , "string", "Filial"            , "FIL"   , , , ) //01 _FIL    "Filial"
    self:addProperty( "_CGC"   , "CGC"               , "string", "CGC"               , "CGC"   , , , ) //02 _CGC    "CGC"
    self:addProperty( "_NMFIL" , "Razão Social"      , "string", "Razão Social"      , "NMFIL" , , , ) //03 _NMFIL  "Razão Social"
    self:addProperty( "_NOME"  , "Nome Trabalhador"  , "string", "Nome Trabalhador"  , "NOME"  , , , ) //04 _NOME   "Nome Trabalhador"
    self:addProperty( "_CPF"   , "CPF"               , "string", "CPF"               , "CPF"   , , , ) //05 _CPF    "CPF"
    self:addProperty( "_MATRIC", "Matrícula"         , "string", "Matrícula"         , "MATRIC", , , ) //06 _MATRIC "Matrícula"
    self:addProperty( "_EVENTO", "Evento"            , "string", "Evento"            , "EVENTO", , , ) //07 _EVENTO "Evento"
    self:addProperty( "_DSCEVT", "Descrição Evento"  , "string", "Descrição Evento"  , "DSCEVT", , , ) //08 _DSCEVT "Descrição Evento"
    self:addProperty( "_DTINCS", "Inclusão Sistêmica", "string", "Inclusão Sistêmica", "DTINCS", , , ) //09 _DTINCS "Inclusão Sistêmica"
    self:addProperty( "_DTPER" , "Data Período"      , "string", "Data Período"      , "DTPER" , , , ) //10 _DTPER" "Data Período"
    self:addProperty( "_STATUS", "Status"            , "string", "Status"            , "STATUS", , , ) //11 _STATUS "Status"
    self:addProperty( "_RECIBO", "Recibo"            , "string", "Recibo"            , "RECIBO", , , ) //12 _RECIBO "Recibo"
    self:addProperty( "_ID"    , "Identificador"     , "string", "Identificador"     , "ID"    , , , ) //13 _ID"    "Identificador"
    self:addProperty( "_VERSAO", "Versão"            , "string", "Versão"            , "VERSAO", , , ) //14 _VERSAO "Versão"

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} TafSvETrab

Cria Query dinâmica com base na regra do painel do trabalhador
 
@return object: self:oSchema
 
@author Evandro Italo / Denis Souza
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Static Function TafSvETrab(cFil, cBancoDB, cCPF, cId, nEscopo, cParEvent, cDateType, cPeriod, dDataIni, dDataFim )

    local cQry      := ""  as character
    local cAlias    := ""  as character
    local cEvtTab   := ""  as character
    local cLayout   := ""  as character
    local cCmpPer   := ""  as character
    local cDtEvt    := ""  as character
    local cEvtType  := ""  as character
    local cTagXml   := ""  as character
    local cKey      := ""  as character
    local cColDtE   := ""  as character
    local cCompC8E  := ""  as character
    local cQuery    := ""  as character
    local nTMDtEvt  := 0   as numeric
    local nTMDtIncS := 0   as numeric
    local nDtESize  := 0   as numeric
    local nX		:= 0   as numeric
    local nlA       := 0   as numeric
    local aEventos  := {}  as array
    local aBind     := {}  as array
    local aBindEvt  := {}  as array
    local aBindJoin := {}  as array    
    local aBindSts  := {}  as array  
    local aBindDel  := {}  as array      
    local aInfEUF   := {}  as array
    local lActive   := .F. as logical
    local lDtIncSis := .F. as logical
    local oPrepare  := nil as object
    local nI        := 0   as numeric
    local nTamBind  := 0   as numeric    

    default cFil      := ""
    default	cBancoDB  := "" //tcGetDb()
    default cCPF	  := ""
    default cId       := ""
    default nEscopo   := 2
    default cParEvent := ""
    default cDateType := "1"
    default cPeriod   := ""
    default dDataIni  := ""
    default dDataFim  := ""

    aEventos := TAFRotinas( ,, .T., nEscopo )


    nDtESize := GetSX3Cache("C9V_DTINIV","X3_TAMANHO")
    cColDtE  := ",CAST(' ' AS CHAR("+Str(nDtESize)+")) DTE"

    cCompC8E  := Upper(AllTrim(FWModeAccess("C8E",1)+FWModeAccess("C8E",2)+FWModeAccess("C8E",3)))

    aInfEUF := TAFTamEUF(Upper(AllTrim(SM0->M0_LEIAUTE)))

    For nX := 1 To Len(aEventos) //laco se repete conforme tafrotinas aprox 30-40 vezes

        cEvtTab  := aEventos[nX][3]
        cLayout	 := aEventos[nX][4]
        cCmpPer  := aEventos[nX][6]
        cDtEvt   := aEventos[nX][6]  //Data do Evento
        cTagXml  := aEventos[nX][9]  //Tag do Evento eSocial
        cKey     := aEventos[nX][11] //Se Busca Por CPF ou ID //Considerar a 11° posição no TAFROTINAS para o escopo esocial deverá estar preenchido.
        cEvtType := aEventos[nX][12] //Tipo do Evento C,M,E,T
        lActive  := aEventos[nX][19] //Evento ativo com a versão corrente do E-Social?

        If !Empty(cLayout) .And. !Empty(cTagXml) .And. !Empty(cKey) .And. aEventos[nX][19] == .T.

            if Empty(cParEvent) .Or. (cParEvent == cLayout) //regra para filtrar o evento que pode vir por parametro

                if !(cEvtTab $ cQry) //ex: S-1200 e S-1202 utilizam a mesma tabela entao nao eh necessario fazer o union 2 x na tabela "C91"

                    If TAFAlsInDic(cEvtTab) .And. !Empty(cLayout) .And. cEvtType <> "T" .And. !(cLayout $ "TAUTO")
                        If !Empty(cQry)
                            cQry += " UNION ALL "
                        EndIf
                        cQry += " SELECT "  + cEvtTab + "_FILIAL FIL "
                        cQry += ", '" + cLayout + "' EVT "
                        cQry += ", " + cEvtTab + "_STATUS STS "
                        cQry += ", " + cEvtTab + "_PROTUL PRT "
                        cQry += ", " + cEvtTab + "_ID ID "
                        cQry += ", " + cEvtTab + "_VERSAO VRS "

                        //Data de Inclusão (Data Sistêmica - _DINSIS )
                        lDtIncSis := TafColumnPos(cEvtTab + "_DINSIS") .And. GetSX3Cache((cEvtTab + "_DINSIS"),"X3_TIPO") $ ('CD')
                        nTMDtIncS := 0
                        If lDtIncSis
                            nTMDtIncS := GetSX3Cache((cEvtTab + "_DINSIS"),"X3_TAMANHO")
                            cQry += "," + cEvtTab + "_DINSIS DIS"
                        EndIf

                        //Data do Evento (Em caso de não periodicos, este campo é dinâmico e varia de acordo com o evento). Se o campo não tiver data do evento não mostrar a data. Deixar em branco.
                        lDtEvt := !Empty(cDtEvt) .And. TafColumnPos(cDtEvt) .And. GetSX3Cache((cDtEvt),"X3_TIPO") $ ('CD')
                        nTMDtEvt := 0
                        if lDtEvt
                            nTMDtEvt := GetSX3Cache(cDtEvt,"X3_TAMANHO")
                            cQry += "," + cDtEvt + " DTE"
                        else
                            if !("POSTGRES" $ cBancoDB)
                                cColDtE := ",' ' DTE"
                            endif
                            cQry += cColDtE
                        endif

                        cQry += " FROM " + RetSqlName((cEvtTab)) + " WHERE "

                        if !Empty( cFil )
                            cQry += cEvtTab + "_FILIAL = ? AND "
                            aadd( aBindEvt , cFil )
                        endif

                        If GetSX3Cache(cKey,"X3_TAMANHO") == 11
                            cQry += cKey + " = ? AND "
                            aadd( aBindEvt , cCPF )
                        Else
                            cQry += cKey + " = ? AND "
                            aadd( aBindEvt , cId )
                        EndIf

                        lFilDate := .F.
                        if cEvtTab == "CM6" //apenas CM6 ira filtrar de forma diferente, a prioridade para os demais _DINSIS e se nao possuir filtra de forma dinamica por aEventos[nX][6]
                            cQry += "( (CM6_DTAFAS >= ? AND CM6_DTAFAS <= ?) OR (CM6_DTFAFA >= ? AND CM6_DTFAFA <= ?) ) AND "
                            aadd( aBindEvt , DtoS(dDataIni) )
                            aadd( aBindEvt , DtoS(dDataFim) )
                            aadd( aBindEvt , DtoS(dDataIni) )
                            aadd( aBindEvt , DtoS(dDataFim) )
                            lFilDate := .T.
                        else                    
                            if cDateType == "2" .And. lDtEvt //Dt Evento
                                if nTMDtEvt == 8
                                    lFilDate := .T.
                                    cQry += cDtEvt + " BETWEEN ? AND ? AND "
                                    aadd( aBindEvt , DtoS(dDataIni) )
                                    aadd( aBindEvt , DtoS(dDataFim) )
                                elseif nTMDtEvt == 6
                                    lFilDate := .T.
                                    cQry += "(" + cDtEvt + " = ? OR " + cDtEvt + " = ? ) AND "
                                    aadd( aBindEvt , cPeriod )
                                    aadd( aBindEvt , (Right(cPeriod,2) + Left(cPeriod,4)) )
                                endif
                            endif
                            if !lFilDate //se nao existir o filtro acima, ira aplicar o default que eh por Data Sistemica se Existir
                                if lDtIncSis .And. nTMDtIncS == 8 //Informe a Data De para filtrar a data sistêmica a partir dessa data e a Data até para filtrar a data sistêmica até essa data.
                                    lFilDate := .T.
                                    cQry += cEvtTab + "_DINSIS BETWEEN ? AND ? AND "
                                    aadd( aBindEvt , DtoS(dDataIni) )
                                    aadd( aBindEvt , DtoS(dDataFim) )
                                endif
                            endif
                        endif
                        cQry += cEvtTab + "_ATIVO = ? AND D_E_L_E_T_ = ? "
                        aadd( aBindEvt , '1' )
                        aadd( aBindEvt , ' ' )
                    endif
                endif
            Endif
        EndIf
    Next nX

    if !Empty(cQry)

        cQuery := "SELECT TAB.FIL,TAB.EVT,TAB.PRT,TAB.ID,TAB.VRS,TAB.DIS,TAB.DTE,TAB.STS,TAB.DEVT FROM ( "
        cQuery += "SELECT TMP.FIL,TMP.EVT,TMP.PRT,TMP.ID,TMP.VRS,TMP.DIS,TMP.DTE,C8E.C8E_DESCRI DEVT "
        cQuery += ",CASE WHEN TMP.STS = ? THEN 'Aguard.Retorno RET' WHEN TMP.STS = ? THEN 'Inconsistente' "
        cQuery += "WHEN TMP.STS = ? THEN 'Transmitido' WHEN TMP.STS = ? THEN 'Aguard.Exclusão' "
        cQuery += "WHEN TMP.STS = ? THEN 'Reg.Excluído' ELSE 'Pendente Transmissão' END STS "
        aadd( aBindSts ,'2') //Aguard.Retorno RET	
        aadd( aBindSts ,'3') //Inconsistente	
        aadd( aBindSts ,'4') //Transmitido	
        aadd( aBindSts ,'6') //Aguard.Exclusão
        aadd( aBindSts ,'7') //Reg.Excluído
        cQuery += "FROM (" + cQry + ") TMP "
        cQuery += "LEFT JOIN " + RetSqlName("C8E") + " C8E ON C8E.C8E_FILIAL = ?  "    
        if cCompC8E == "CCC" .Or. (cCompC8E == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
            Aadd( aBindJoin , xFilial("C8E")  )  
        else
            Aadd( aBindJoin , StrTran( FWJoinFilial("C8E","TMP","C8E","TMP",.T.,cBancoDB,.T.),"TMP.TMP_FILIAL","TMP.FIL") )
        endif
        cQuery += " AND C8E.C8E_CODIGO = TMP.EVT AND C8E.D_E_L_E_T_ = ? ) TAB "
        aadd( aBindDel , ' ' )
        cQuery += "ORDER BY TAB.FIL,TAB.EVT,TAB.STS "
    
        For nI := 1 to Len(aBindSts) 
            aAdd( aBind , aBindSts[nI] )
        Next nI

        For nI := 1 to Len(aBindEvt) 
            aAdd( aBind , aBindEvt[nI] )
        Next nI

        For nI := 1 to Len(aBindJoin) 
            aAdd( aBind , aBindJoin[nI] )
        Next nI    

        For nI := 1 to Len(aBindDel) 
            aAdd( aBind , aBindDel[nI] )
        Next nI     
        
        nTamBind := Len(aBind) 
        oPrepare := FwExecStatement():New( ChangeQuery(cQuery) ) 

        for nlA := 1 to nTamBind
            oPrepare:setString( nlA, aBind[nlA] )
        next nlA

        cAlias:= oPrepare:OpenAlias() 
    
        oPrepare:Destroy()
        aSize(aBind,0)
        aBind := Nil
        aSize(aBindSts,0)
        aBindSts := Nil      
        aSize(aBindJoin,0)
        aBindJoin := Nil   
        aSize(aBindEvt,0)
        aBindEvt := Nil    
        aSize(aBindDel,0)
        aBindDel := Nil                         
        oPrepare := nil 
    
    endif


Return cAlias

//---------------------------------------------------------------------
/*/{Protheus.doc} regMiddLS
@type			function
@description	Registra o Uso da Rotina no License Server
@author			Evandro dos Santos Oliveira
@since			17/09/2021
@param			Nil
@return			Nil
/*/
//---------------------------------------------------------------------
Static Function regMiddLS()

	Local cCode		:= ""
	Local cUser		:= "000000"
	Local cModule	:= ""
	Local cRoutine  := ""
    Local lLogLS    := .F.
    Local lSecurity := Nil

	If !lLogLS
		cCode 	 := "LS006"
		cModule	 := "84"
		cRoutine := ProcName(-1)
		If lSecurity == Nil
			lSecurity := ( GetPvProfString("HTTPREST","Security","0",getAdv97()) == "1" )
		EndiF
		If lSecurity
			cUser := RetCodUsr()
		EndIf
		//Função para gravar o uso de rotinas e enviar ao LS (License Server)
		Iif(FindFunction('FWLsPutAsyncInfo'),FWLsPutAsyncInfo(cCode,cUser,cModule,cRoutine),)
		lLogLS := .T.
	EndIf

Return Nil
