#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.taf.calculationIPI.ch"
    
namespace totvs.protheus.fiscal.taf.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGATAF", tables="V8W, C20, C2F", name="CalculationIPISmartViewBusinessObject", country="BRA", initialRelease="12.1.2310") 

//-------------------------------------------------------------------
/*{Protheus.doc}  CalculationIPISmartViewBusinessObject
Classe para criação do Objeto de Apuração de IPI
@author Juliana Mellão
@since 10/06/2024
*/
//-------------------------------------------------------------------  
class CalculationIPISmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    data nPageSize as integer
    data cDBMS as character

    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author Juliana Mellão
@since 10/06/2024
*/
//-------------------------------------------------------------------   
method new() class CalculationIPISmartViewBusinessObject

    Local oTafTools := tafSmartviewTools():new()
    
    _Super:new()
    self:appendArea(STR0001) //"TAF Fiscal"
    self:setDisplayName(STR0002) //"Apuração de IPI"
    self:setDescription(STR0003) //"Objeto de Negocios para conferência da Apuração de IPI"

    oTafTools:validPerg(self:setPergunte("TAFTR00011"))

    self:cDBMS := UPPER(TcGetDb()) //verifica qual banco é utilizado

    freeobj(oTafTools)

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: self:oData 
@author Juliana Mellão
@since 10/06/2024
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class CalculationIPISmartViewBusinessObject
    Local cQuery      as Character
    Local oTafTools   := tafSmartviewTools():new()
    Local oFiltQry    := oFilter:getParameters() as Json
    Local cFilDe      := oFiltQry['MV_PAR01',1] as Character
    Local cFilAte     := oFiltQry['MV_PAR02',1] as Character
    Local dStartDate  := FwDateTimeToLocal(oFiltQry['MV_PAR03',1])[1]
    Local dEndDate    := FwDateTimeToLocal(oFiltQry['MV_PAR04',1])[1]
    Local cCFOP       := oFiltQry['MV_PAR05',1] as Character
    Local cCST        := oFiltQry['MV_PAR06',1] as Character
    Local lOk         := .t. as logical
    Local aFiliais    := {} as array
    Local cFils       := oTafTools:montaFilial(cFilDe, cFilAte, aFiliais) as character
    Local oPrepare    := nil as object
 
    //Define a quantidade máxima por página (Default 100)
    self:setPageSize(200)
 
    if cFils == 'Erro'
        oTafTools:validFil(.f.)
        lOk := .f.
    endif

    If lOk

        cQuery := " SELECT " 
        cQuery += "     V8W.V8W_FILIAL, "
        cQuery += "     C20.C20_NUMDOC, "
        cQuery += "     C20.C20_SERIE, "
        cQuery += "     C20.C20_ESPECI, "
        cQuery += "     C20.C20_CLIFOR, "
        cQuery += "     C1H.C1H_NOME, "
        cQuery += "     C20.C20_DTDOC, " 
        cQuery += "     C20.C20_VLDOC, "
        cQuery += "     V8W.V8W_ALIQ, "
        cQuery += "     C2F.C2F_BASE, "
        cQuery += "     V8W.V8W_VALOR, "
        cQuery += "     C0Y.C0Y_CODIGO, "
        cQuery += "     C0Y.C0Y_DESCRI, "

        cQuery += "     C0Y.C0Y_CODIGO + ' - ' + C0Y.C0Y_DESCRI CFOP, " 

        cQuery += "     C15.C15_CODIGO, "    
        cQuery += "     C15.C15_DESCRI,

        cQuery += "     C15.C15_CODIGO + ' - ' + C15.C15_DESCRI CST "  

        cQuery += " FROM "
        cQuery +=       RetSQLName("V8W") + " V8W "
        cQuery += " INNER JOIN " + RetSqlName('C20') + " C20 ON " + FwJoinFilial("C20","V8W")
        cQuery += "    AND C20.C20_CHVNF = V8W.V8W_CHVNF "
        cQuery += "    AND C20.D_E_L_E_T_ = ' '"
        cQuery += " INNER JOIN " +RetSqlName("C2F")+" C2F ON " + FwJoinFilial("C2F","V8W") 
        cQuery += "    AND C2F.C2F_CHVNF = V8W.V8W_CHVNF "
        cQuery += "    AND C2F.C2F_CODTRI = '000005' "
        cQuery += "    AND C2F.D_E_L_E_T_ = ' '"
        cQuery += " INNER JOIN " +RetSqlName("C1H")+" C1H ON " + FwJoinFilial("C1H","C20") 
        cQuery += "    AND C20.C20_CODPAR = C1H.C1H_ID "
        cQuery += "    AND C1H.D_E_L_E_T_ = ' '"
        cQuery += " INNER JOIN " +RetSqlName("C0Y")+" C0Y ON "  
        cQuery += "    V8W.V8W_CFOP = C0Y.C0Y_ID "
        cQuery += "    AND C0Y.D_E_L_E_T_ = ' '"
        cQuery += " INNER JOIN " +RetSqlName("C15")+" C15 ON " 
        cQuery += "    V8W.V8W_CST = C15.C15_ID "
        cQuery += "    AND C15.D_E_L_E_T_ = ' '"
        cQuery += " WHERE " 
        cQuery += "    V8W.V8W_FILIAL IN ( ? ) "
        cQuery += "    AND C20.C20_DTDOC BETWEEN ? AND ? " 
        If !Empty(cCFOP)
            cQuery += " AND V8W.V8W_CFOP = ? "
        EndIf
        If !Empty(cCST)
            cQuery += " AND V8W.V8W_CST = ?" 
        EndIf
        cQuery += "    AND V8W.D_E_L_E_T_ = ' ' "  
        self:setOrder("V8W.V8W_FILIAL, V8W.V8W_IDPER")

        cQuery := ChangeQuery( cQuery )

        oPrepare := FWPreparedStatement():New(cQuery)
        oPrepare:SetIn(1 , aFiliais)
        oPrepare:SetDate(2 , dStartDate)
        oPrepare:SetDate(3 , dEndDate)
        If !Empty(cCFOP) .and. Empty(cCST)
            oPrepare:SetString(4 , cCFOP)
        EndIf
        If Empty(cCFOP) .and. !Empty(cCST)
            oPrepare:SetString(4 , cCST)
        EndIf
        If !Empty(cCFOP) .and. !Empty(cCST)
            oPrepare:SetString(4 , cCFOP)
            oPrepare:SetString(5 , cCST)
        EndIf

        cQuery := oPrepare:GetFixQuery()
        self:setQuery(cQuery) 
    EndIf
    
    freeobj(oTafTools)
    freeObj(oPrepare)
    freeobj(oFiltQry)
    aSize(aFiliais,0)
    aFiliais := Nil

return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author Juliana Mellão
@since 10/06/2024
*/
//-------------------------------------------------------------------   
method getSchema() as object class CalculationIPISmartViewBusinessObject

//                 ( Nome            ,  Descricao            , Tipo      , Display          , cRealName     , cComboValues, lIsRequired, cRenameField, lCanFilter)
   self:addProperty("V8W_FILIAL "    , "Filial"              , "string"  , "Filial"         , "V8W_FILIAL " ,             ,            ,             ,           )
   self:addProperty("C20_NUMDOC "    , "Nota Fiscal"         , "string"  , "Nt Fiscal"      , "C20_NUMDOC " ,             ,            ,             ,           )
   self:addProperty("C20_SERIE  "    , "Serie"               , "string"  , "Serie"          , "C20_SERIE  " ,             ,            ,             ,           )
   self:addProperty("C20_ESPECI "    , "Especie"             , "string"  , "Especie"        , "C20_ESPECI " ,             ,            ,             ,           )
   self:addProperty("C20_CLIFOR "    , "Cod Cliente/Forn"    , "string"  , "Cod Cli/For"    , "C20_CLIFOR " ,             ,            ,             ,           )
   self:addProperty("C1H_NOME   "    , "Cliente/Forn"        , "string"  , "Cliente/Forn"   , "C1H_NOME   " ,             ,            ,             ,           )
   self:addProperty("C20_DTDOC  "    , "Data do Documento"   , "date"    , "Dt Documento"   , "C20_DTDOC  " ,             ,            ,             ,           )
   self:addProperty("C20_VLDOC  "    , "Valor do Documento"  , "number"  , "Vl Documento"   , "C20_VLDOC  " ,             ,            ,             ,           )
   self:addProperty("V8W_ALIQ   "    , "Aliquota do IPI"     , "number"  , "% Aliq IPI"     , "V8W_ALIQ   " ,             ,            ,             ,           )
   self:addProperty("C2F_BASE   "    , "Valor Base"          , "number"  , "Vl Base"        , "C2F_BASE   " ,             ,            ,             ,           )
   self:addProperty("V8W_VALOR  "    , "Valor do IPI"        , "number"  , "Vl IPI"         , "V8W_VALOR  " ,             ,            ,             ,           )
   self:addProperty("C0Y_CODIGO "    , "Cod CFOP"            , "string"  , "Cod CFOP"       , "C0Y_CODIGO " ,             ,            ,             ,           )
   self:addProperty("C0Y_DESCRI "    , "Desc CFOP"           , "string"  , "Desc CFOP"      , "C0Y_DESCRI " ,             ,            ,             ,           )
   self:addProperty("CFOP       "    , "CFOP"                , "string"  , "CFOP"           , "CFOP       " ,             ,            ,             ,           )
   self:addProperty("C15_CODIGO "    , "Cod CST"             , "string"  , "Cod CST"        , "C15_CODIGO " ,             ,            ,             ,           )
   self:addProperty("C15_DESCRI "    , "Desc CST"            , "string"  , "Desc CST"       , "C15_DESCRI " ,             ,            ,             ,           )
   self:addProperty("CST        "    , "CST"                 , "string"  , "CST"            , "CST        " ,             ,            ,             ,           )

return self:oSchema

