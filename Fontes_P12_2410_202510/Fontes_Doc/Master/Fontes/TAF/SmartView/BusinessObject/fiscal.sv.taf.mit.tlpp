#include "protheus.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.taf.mit.ch"
 

namespace totvs.protheus.fiscal.taf.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATAF", tables="T1A,T1B,C3S", name="TaxIntegrationModule", country="BRA", initialRelease="12.1.2310")
 

//-------------------------------------------------------------------
/*/{Protheus.doc} MitSmartViewBusinessObject
Classe para criação do Objeto de Negócio

@author Vogas / Wesley Matos
@since 24/02/2025
@version 1.0
*/
//-------------------------------------------------------------------  
class MitSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object 
    private method execQuery()  as character

endclass


//-------------------------------------------------------------------
/*/{Protheus.doc} new
Método de instância da Classe

@author	 Vogas 
@since	 24/02/2025
@version 1.0
/*/ 
//-------------------------------------------------------------------
method new() as object class MitSmartViewBusinessObject

_Super:new()
self:appendArea(STR0002) //TAF Fiscal
self:setDisplayName(STR0001) //MIT - Relatório Smart View
self:setDescription(STR0003) //MIT módulo de inclusão de tributos relatório smart view
if !::setPergunte("TAFTR00015")
    FwLogMsg(STR0004,,STR0004,,,,STR0005,,,)//"WARN"##"SmartView"##"Grupo de perguntas nao encontrado"
endIf
self:setPageSize(10000)

return self


//-------------------------------------------------------------------
/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio. 

@author	 Vogas
@since	 24/02/2025
@version 1.0
/*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class MitSmartViewBusinessObject

local cAlias        := ''   as character
Local oPrepare      := nil  as object

cAlias := self:execQuery(oPrepare, oFilter)

while !(cAlias)->(eof())

    self:oData:appendData({ "COD_RECEITA"  : (cAlias)->CODEREC       ,;
                            "CODIGO_TRIB"  : (cAlias)->C3SCODIGO       ,;
                            "DESCRI_TRIB"  : (cAlias)->C3SDESCRI       ,;
                            "PERIODO_APU"  : (cAlias)->T1APERAPU       ,;
                            "VAL_TRIBUTO"  : (cAlias)->T1AVALTRI       ,;
                            "TA1FILIAL"    : (cAlias)->T1AFILIAL       ,;
                            "TA1FILORI"    : (cAlias)->T1AFILORI       ,;
                            "T1AANOPOS"    : (cAlias)->T1AANOPOS       ,;
                            "T1ATRIPOS"    : (cAlias)->T1ATRIPOS       ,;
                            "T1APERIOD"    : (cAlias)->PERIODICIDADE    ,;
                            "T1ACPNEST"    : (cAlias)->T1ACPNEST       ,;
                            "T1ACNPINC"    : (cAlias)->T1ACNPINC       ,;
                            "T1ACODMUN"    : (cAlias)->T1ACODMUN       ,;
                            "T1AUF"        : (cAlias)->C09DESCRI       ,;
                            "T1ADTDEBI"    : DToC(Stod((cAlias)->T1ADTDEBI)) ,;
                            "T1BFILIAL"    : (cAlias)->T1BFILIAL       ,;
                            "T1BIDPROC"    : (cAlias)->T1BIDPROC       ,;
                            "T1BTERCEI"    : (cAlias)->TERCEIRO         ,;
                            "T1BVALSUS"    : (cAlias)->T1BVALSUS       ,;
                            "V3XCNPJ"      : (cAlias)->V3XCNPJ         ,;
                            "T5LCODSUS"    : (cAlias)->T5LCODSUS       ,;
                            "C07DESCRI"    : (cAlias)->C07DESCRI       ,;
                            "NOMEFILIAL"   : (cAlias)->NOMEFIL          })

    (cAlias)->(dbskip())

enddo

(cAlias)->(DbCloseArea())
freeObj(oPrepare)

return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} execQuery
Execução da query com paginação.

@param oPrepare, objeto, preparação da query
@param oFilter, objeto, contém o filtro do SmartView
 
@return string

@author Vogas / Wesley Matos
@since 12/03/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method execQuery(oPrepare as object, oFilter as object) as character class MitSmartViewBusinessObject

local cFilReport    := ''   as character
local cNomeFilial   := ''   as character
local cPeriod       := ''   as character
local cQuery        := ''   as character
local aBind         := {}   as array
Local nI            := 0    as numeric
Local cCompT5L      := ''   as character
Local cCompV3X      := ''   as character
Local aInfEUF       := {}   as array

Default oPrepare := Nil
Default oFilter := Nil

cPeriod     := oFilter:getParameters()["MV_PAR01"][1]
cFilReport  := FWSM0Util( ):GetSM0Data( , cFilAnt , { 'M0_CODFIL' } )[1][2]
cNomeFilial := FWSM0Util( ):GetSM0Data( , cFilReport , { 'M0_FILIAL' } )[1][2]
cCompT5L    := Upper(AllTrim(FWModeAccess("T5L",1)+FWModeAccess("T5L",2)+FWModeAccess("T5L",3)))
cCompV3X    := Upper(AllTrim(FWModeAccess("V3X",1)+FWModeAccess("V3X",2)+FWModeAccess("V3X",3)))
aInfEUF     := TAFTamEUF(Upper(AllTrim(SM0->M0_LEIAUTE)))

cQuery += " SELECT"
cQuery += " T1A.T1A_CODREC CODEREC,"
cQuery += " C3S.C3S_CODIGO C3SCODIGO,"
cQuery += " C3S.C3S_DESCRI C3SDESCRI,"
cQuery += " T1A.T1A_PERAPU T1APERAPU,"
cQuery += " T1A.T1A_VALTRI T1AVALTRI,"
cQuery += " T1A.T1A_FILIAL T1AFILIAL,"
cQuery += " T1A.T1A_FILORI T1AFILORI,"
cQuery += " T1A.T1A_ANOPOS T1AANOPOS,"
cQuery += " C07.C07_DESCRI C07DESCRI,"
cQuery += " T1A.T1A_TRIPOS T1ATRIPOS,"
cQuery += " T1A.T1A_CPNEST T1ACPNEST,"
cQuery += " T1A.T1A_CNPINC T1ACNPINC,"
cQuery += " T1A.T1A_CODMUN T1ACODMUN,"
cQuery += " V3X.V3X_CNPJ   V3XCNPJ,  "
cQuery += " C09.C09_DESCRI C09DESCRI,"
cQuery += " T1A.T1A_DTDEBI T1ADTDEBI,"
cQuery += " T1B.T1B_FILIAL T1BFILIAL,"
cQuery += " T1B.T1B_IDPROC T1BIDPROC,"
cQuery += " T1B.T1B_VALSUS T1BVALSUS,"
cQuery += " T5L.T5L_CODSUS T5LCODSUS,"
cQuery += " '"+cNomeFilial+"' NOMEFIL,"
cQuery += " CASE"
cQuery += "     WHEN T1B.T1B_TERCEI = ? "
cQuery += "         THEN 'Sim' "
cQuery += "     WHEN T1B.T1B_TERCEI = ? "
cQuery += "         THEN 'Nao' "
cQuery += "     ELSE 'Nao Informado' "
cQuery += "     END TERCEIRO, "
aAdd(aBind, '1')
aAdd(aBind, '2')
cQuery += " CASE"
cQuery += "     WHEN T1A.T1A_PERIOD = ?"
cQuery += "         THEN 'Diaria' "
cQuery += "     WHEN T1A.T1A_PERIOD = ?"
cQuery += "         THEN 'Decendial' "
cQuery += "     WHEN T1A.T1A_PERIOD = ?"
cQuery += "         THEN 'Quinzenal' "
cQuery += "     WHEN T1A.T1A_PERIOD = ?"
cQuery += "         THEN 'Mensal' "
cQuery += "     ELSE 'Nao Informado'"
cQuery += "     END PERIODICIDADE "
aAdd(aBind, '1')
aAdd(aBind, '2')
aAdd(aBind, '3')
aAdd(aBind, '4')

cQuery += " FROM " + RetSqlName("T1A") + " T1A "
cQuery += " INNER JOIN " + RetSqlName("C3S") + " C3S ON "
cQuery += " C3S.C3S_FILIAL = ? "
cQuery += " AND C3S.C3S_ID = T1A.T1A_IDTRIB "
cQuery += " AND C3S.D_E_L_E_T_ = ? "
aAdd(aBind, xFilial("C3S") )
aAdd(aBind, Space(1))

cQuery += " LEFT JOIN " + RetSqlName("C09") + " C09 ON "
cQuery += " C09.C09_FILIAL = ? "
cQuery += " AND C09.C09_ID = T1A.T1A_UF "
cQuery += " AND C09.D_E_L_E_T_ = ? "
aAdd(aBind, xFilial("C09") )
aAdd(aBind, Space(1))

cQuery += " LEFT JOIN " + RetSqlName("T1B") + " T1B ON "
cQuery += " T1B.T1B_FILIAL = ? "
cQuery += " AND T1B.T1B_ID = T1A.T1A_ID "
cQuery += " AND T1B.D_E_L_E_T_ = ? "
aAdd(aBind, xFilial("T1B") )
aAdd(aBind, Space(1))

cQuery += " LEFT JOIN " + RetSqlName("V3X") + " V3X ON "
if cCompV3X == "CCC" .or. (cCompV3X == "EEC" .And. aInfEUF[1] + aInfEUF[2] == 0)
    cQuery += " V3X.V3X_FILIAL = ? "
    aAdd(aBind, xFilial("V3X") )
else
    cQuery += FwJoinFilial("V3X","T1A")
endIf
cQuery += " AND V3X.V3X_ID = T1A.T1A_IDSCP "
cQuery += " AND V3X.D_E_L_E_T_ = ? "
aAdd(aBind, Space(1))

cQuery += " LEFT JOIN " + RetSqlName("T5L") + " T5L ON "
if cCompT5L == "CCC" .or. (cCompT5L == "EEC" .And. aInfEUF[1] + aInfEUF[2] == 0)
    cQuery += " T5L.T5L_FILIAL = ? "
    aAdd(aBind, xFilial("T5L") )
else
    cQuery += FwJoinFilial("T5L","T1B")
endIf
cQuery += " AND T5L.T5L_ID || T5L.T5L_VERSAO || T5L.T5L_CODSUS = T1B.T1B_IDSUSP "
cQuery += " AND T5L.D_E_L_E_T_ = ? "
aAdd(aBind, Space(1))

cQuery += " LEFT JOIN " + RetSqlName("C07") + " C07 ON "
cQuery += " C07.C07_FILIAL = ? "
cQuery += " AND C07.C07_ID = T1A.T1A_CODMUN "
cQuery  += " AND C07.D_E_L_E_T_ = ? " 
aAdd(aBind, xFilial("C07") )
aAdd(aBind, Space(1))

cQuery += " WHERE "

cQuery += " T1A.T1A_FILIAL = ? AND "
aAdd(aBind, cFilReport)

cQuery += " T1A.T1A_PERAPU = ? AND "
aAdd(aBind, cPeriod)

cQuery += " T1A.D_E_L_E_T_ = ? AND "
aAdd(aBind, Space(1))

cQuery += " C3S.D_E_L_E_T_ = ? "
aAdd(aBind, Space(1))

cQuery := ChangeQuery(cQuery)
oPrepare := FwExecStatement():New(cQuery)

for nI := 1 to len(aBind)
    oPrepare:setString( nI, aBind[nI] )
next nI

cAlias := GetNextAlias()
oPrepare:OpenAlias(cAlias)

return cAlias

//_____________________________________________________________________
/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos.

@author	 Vogas
@since	 24/02/2025
@version 1.0
/*/
//_____________________________________________________________________
method getSchema() as object class MitSmartViewBusinessObject
local aFieldsT1A := {}  as array
local aFieldsT1B := {}  as array
local aFieldsC3S := {}  as array
local aFieldsC09 := {}  as array
local aFieldsV3X := {}  as array
local aFieldsT5L := {}  as array
local aFieldsC07 := {}  as array

aFieldsT1A := { "T1A_CODREC", "T1A_IDTRIB", "T1A_PERAPU", "T1A_VALTRI", "TA1_FILIAL", "TA1_FILORI", "T1A_ID",;
                "T1A_ANOPOS", "T1A_TRIPOS", "T1A_PERIOD", "T1A_CPNEST", "T1A_CNPINC", "T1A_CODMUN",;
                "T1A_IDSCP" , "T1A_DTDEBI", "NOMEFIL" }
aFieldsT1B := { "T1B_FILIAL", "T1B_ID"    , "T1B_IDPROC", "T1B_TERCEI", "T1B_VALSUS", "T1B_IDSUSP" }
aFieldsC3S := { "C3S_CODIGO", "C3S_DESCRI"}
aFieldsC09 := {"C09_DESCRI"}
aFieldsC07 := {"C07_DESCRI"}
aFieldsV3X := {"V3X_CNPJ"}
aFieldsT5L := {"T5L_CODSUS"}

self:oSchema:addProperty( "COD_RECEITA"  , "Código Receita" , "string"  , "Cod. Receita"            , "T1A_CODREC"  ) 
self:oSchema:addProperty( "CODIGO_TRIB"  , "Código Tributo" , "string"  , "Código Tributo"          , "C3S_CODIGO"  ) 
self:oSchema:addProperty( "DESCRI_TRIB"  , "Descrição"      , "string"  , "Descrição"               , "C3S_DESCRI"  ) 
self:oSchema:addProperty( "PERIODO_APU"  , "Per. Apuração"  , "string"  , "Per. Apuração."          , "T1A_PERAPU"  ) 
self:oSchema:addProperty( "VAL_TRIBUTO"  , "Valor Tributo"  , "number"  , "Valor Tributo"           , "T1A_VALTRI"  ) 
self:oSchema:addProperty( "T1BVALSUS"    , "Val. Susp."     , "number"  , "Valor Suspensão"         , "T1B_VALSUS"  )
self:oSchema:addProperty( "T1ADTDEBI"    , "Dt. Debito"     , "string"  , "Data do Debito"          , "T1A_DTDEBI"  )
self:oSchema:addProperty( "T1AANOPOS"    , "Ano Pos."       , "string"  , "Ano Postergado"          , "T1A_ANOPOS"  )
self:oSchema:addProperty( "T1ATRIPOS"    , "Trim. Poster"   , "string"  , "Trimestre Postergado"    , "T1A_TRIPOS"  )   
self:oSchema:addProperty( "T1APERIOD"    , "Period."        , "string"  , "Periodicidade"           , "T1A_PERIOD"  )
self:oSchema:addProperty( "T1ACPNEST"    , "CNPJ Estab."    , "string"  , "CNPJ Estabelecimento"    , "T1A_CPNEST"  )  
self:oSchema:addProperty( "T1ACNPINC"    , "CNPJ Incorp."   , "string"  , "CNPJ Incorporação"       , "T1A_CNPINC"  )   
self:oSchema:addProperty( "T1ACODMUN"    , "Cod. Mun."      , "string"  , "Código municipio"        , "T1A_CODMUN"  )   
self:oSchema:addProperty( "C07DESCRI"    , "Desc. Mun."     , "string"  , "Descrição municipio"     , "C07_DESCRI"  )   
self:oSchema:addProperty( "T1AUF"        , "UF"             , "string"  , "Unidade Federativa"      , "C09_DESCRI"  ) 
self:oSchema:addProperty( "T1BTERCEI"    , "Terceiro"       , "string"  , "Terceiro"                , "T1B_TERCEI"  )
self:oSchema:addProperty( "TA1FILIAL"    , "Filial"         , "string"  , "Filial Matriz"           , "TA1_FILIAL"  )
self:oSchema:addProperty( "TA1FILORI"    , "Filial Docum"   , "string"  , "Filial Documento"        , "TA1_FILORI"  )
self:oSchema:addProperty( "T1BFILIAL"    , "Filial"         , "string"  , "Filial T1B"              , "T1B_FILIAL"  )
self:oSchema:addProperty( "T1BIDPROC"    , "ID Proc"        , "string"  , "ID Processo"             , "T1B_IDPROC"  )
self:oSchema:addProperty( "V3XCNPJ"      , "V3X CNPJ"       , "string"  , "SCP CNPJ"                , "V3X_CNPJ"    )
self:oSchema:addProperty( "T5LCODSUS"    , "T5L_CODSUS"     , "string"  , "Cód Suspensão"           , "T5L_CODSUS"  )
self:oSchema:addProperty( "NOMEFILIAL"   , "Nome Filial"    , "string"  , "Nome Filial"             , "NOMEFIL"     )

self:oSchema:aliasToSchema("T1A",aFieldsT1A)
self:oSchema:aliasToSchema("T1B",aFieldsT1B)
self:oSchema:aliasToSchema("C3S",aFieldsC3S)
self:oSchema:aliasToSchema("C09",aFieldsC09)
self:oSchema:aliasToSchema("V3X",aFieldsV3X)
self:oSchema:aliasToSchema("T5L",aFieldsT5L)
self:oSchema:aliasToSchema("C07",aFieldsc07)

return self:oSchema
