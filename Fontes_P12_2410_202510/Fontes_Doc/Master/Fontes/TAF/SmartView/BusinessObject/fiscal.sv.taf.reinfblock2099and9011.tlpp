#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.taf.reinfblock2099and9011.ch"

namespace totvs.protheus.fiscal.taf.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider( active=.T., team="SIGATAF",tables="V0C,V0E,V0F,V0G,V0H,V0I,V6C,V0J",name="ReinfBlock2099and9011", country="BRA", initialRelease="12.1.2310" )

//-------------------------------------------------------------------
/*/{Protheus.doc} ReinfBlock2099SmartViewBusinessObject
Classe para criação do Objeto de Negócio
@author Denis Souza
@since 19/09/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class ReinfBlock2099and9011SmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

public method new()        as object
public method getData()    as object
public method getSchema()  as object
private method execQuery() as character

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Denis Souza
@since 19/09/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class ReinfBlock2099and9011SmartViewBusinessObject

_Super:new()
self:appendArea(STR0001)		//"TAF Fiscal Contábil"
self:setDisplayName(STR0002) 	//"Relatório REINF(R2099/R9011) Fechamento do Bloco 20 x Retorno Totalizador Governo "
self:setDescription(STR0003 + STR0004) //"Objeto ReinfBlock2099and9011 Fechamento Bloco 20 x Retorno Governo"##" (V0C,V0E,V0F,V0G,V0H,V0I,V6C,V0J)"
if !::setPergunte("TAFTR00006");FwLogMsg("WARN", , "SmartView", , , , "Grupo de perguntas nao encontrado", , ,);endif
self:setPageSize(10000)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório 
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Denis Souza
@since 19/09/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method getData( nPage as numeric, oFilter as object ) as object class ReinfBlock2099and9011SmartViewBusinessObject

local oFiltQry        := Nil as json
local cAlias          := " " as character
local cYearMonth      := " " as character
local cName           := " " as character
local cTaxNbBranch    := " " as character
local cTaxNbPartipant := " " as character
local cCompetence     := " " as character
local cProcessDate    := " " as character
local cProcessHour    := " " as character
local nStart          := 0   as numeric
local nEnd            := 0   as numeric
local aSM0            := {}  as array

default nPage   := 1
default oFilter := Nil

oFiltQry    := oFilter:getParameters()
cYearMonth  := AllTrim(oFiltQry['MV_PAR01',1]) //Ano+Mes (AAAAMM)
cCompetence := substr(cYearMonth,5,2) + "-" + substr(cYearMonth,1,4)
nStart      := ((nPage - 1) * self:getPageSize()) + 1 //Seta o primeiro item
nEnd        := nPage * self:getPageSize()             //Seta último da página atual

cAlias := self:execQuery( nStart, nEnd, cYearMonth )

while !(cAlias)->(Eof())

    cName := " "
    cTaxNbBranch := " "

    aSM0 := FWSM0Util():GetSM0Data(cEmpAnt,(cAlias)->FILIAL,{"M0_FILIAL","M0_CGC"})

    if len(aSM0) >= 2
        cName := SubStr(Alltrim(aSM0[1][2]),1,30)
        if len(Alltrim(aSM0[2][2])) == 14
            cTaxNbBranch := Transform(Alltrim(aSM0[2][2]),"@R! NN.NNN.NNN/NNNN-99")
        endif
    endif

    cTaxNbPartipant := " "
    if len(Alltrim((cAlias)->NRINSC)) == 14
        cTaxNbPartipant := Transform(Alltrim((cAlias)->NRINSC),"@R! NN.NNN.NNN/NNNN-99")
    endif

    cProcessDate := StrTran( SubStr( FwTimeStamp(5,sToD((cAlias)->DTPROC) ,"00:00:00"),1,10 ) , "-", "")

    cProcessHour := PadL(Alltrim((cAlias)->HRPROC),4,"0")
    cProcessHour := SubStr(cProcessHour,1,2) + ":" + SubStr(cProcessHour,3,2)

    self:oData:appendData( { "_FILIAL"  : Alltrim((cAlias)->FILIAL) ,;
                             "_RAZSOC"  : cName                     ,;
                             "_CNPJFIL" : cTaxNbBranch              ,;
                             "_EVENTO"  : Alltrim((cAlias)->EVENTO) ,;
                             "_COMP"    : cCompetence               ,;
                             "_NRREC"   : Alltrim((cAlias)->NRREC)  ,;
                             "_CODREC"  : Alltrim((cAlias)->CODREC) ,;
                             "_NRINSC"  : cTaxNbPartipant           ,;
                             "_BSCAL"   : (cAlias)->BSCAL           ,;
                             "_VLTRIB"  : (cAlias)->VLTRIB          ,;
                             "_IMPSUS"  : (cAlias)->IMPSUS          ,;
                             "_ADIC"    : (cAlias)->ADIC       	    ,;
                             "_ADISUS"  : (cAlias)->ADISUS          ,;
                             "_HRPROC"  : cProcessHour              ,;
                             "_DTPROC"  : DtoC(Stod(cProcessDate))  } )
    
    (cAlias)->(dbSkip())

enddo

(cAlias)->(DBCloseArea())


return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Denis Souza
@since 19/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class ReinfBlock2099and9011SmartViewBusinessObject

self:oSchema:addProperty( "_FILIAL" , "Filial"	              , "string", "Filial"	              , "FILIAL" , , , )
self:oSchema:addProperty( "_RAZSOC" , "Razão Social"          , "string", "Razão Social"          , "RAZSOC" , , , )
self:oSchema:addProperty( "_CNPJFIL", "CNPJ Filial"	          , "string", "CNPJ Filial"	          , "CNPJFIL", , , )
self:oSchema:addProperty( "_EVENTO" , "Evento"                , "string", "Evento"	              , "EVENTO" , , , )
self:oSchema:addProperty( "_COMP"   , "Competência"           , "string", "Competência"	          , "COMP"   , , , )
self:oSchema:addProperty( "_NRREC"  , "Nº de Recibo"          , "string", "Nº de Recibo"	      , "NRREC"  , , , )
self:oSchema:addProperty( "_CODREC" , "Cod. Receita"          , "string", "Cod. Receita"          , "CODREC" , , , )
self:oSchema:addProperty( "_NRINSC" , "Nr.Insc.Participante"  , "string", "Nr.Insc.Participante"  , "NRINSC" , , , )
self:oSchema:addProperty( "_BSCAL"  , "Base cálculo"          , "number", "Base cálculo"	      , "BSCAL"  , , , )
self:oSchema:addProperty( "_VLTRIB" , "Valor Tributo"         , "number", "Valor Tributo"	      , "VLTRIB" , , , )
self:oSchema:addProperty( "_IMPSUS" , "Tributo Suspenso"      , "number", "Tributo Suspenso"	  , "IMPSUS" , , , )
self:oSchema:addProperty( "_ADIC"   , "Valor Ret. Adic."      , "number", "Valor Ret. Adic."      , "ADIC"   , , , )
self:oSchema:addProperty( "_ADISUS" , "Valor Ret. Adic. Susp.", "number", "Valor Ret. Adic. Susp.", "ADISUS" , , , )
self:oSchema:addProperty( "_HRPROC" , "Hora Processamento"    , "string", "Hora Processamento"    , "HRPROC" , , , )
self:oSchema:addProperty( "_DTPROC" , "Data Processamento"    , "string", "Data Processamento"    , "DTPROC" , , , )

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} execQuery

Execução da query com paginação.

@param oFilter, objeto, contém o filtro do SmartView
@param nStart, numérico, indica o inicío da paginação
@param nEnd, numérico, indica o fim da paginação
 
@return string

@author Denis Souza
@since 19/09/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method execQuery( nStart as numeric, nEnd as numeric, cYearMonth as character ) as character class ReinfBlock2099and9011SmartViewBusinessObject

local cQry      := " " as character
local aBind     := {}  as array
local nI        := 0   as numeric
local nX        := 0   as numeric
local aRootBranch    := {}  as array
local aBranch 	   := {}  as array
local oPrepare          as object

default nStart   	:= 0
default nEnd   		:= 0
default cYearMonth 	:= ""

aBranch := wsLoadFil()
For nX := 1 to len(aBranch)
    aadd(aRootBranch, aBranch[nX][3] )
Next nX

cQry += "SELECT V0F.V0F_FILIAL FILIAL,'R-2010' EVENTO,V0F.V0F_NRREC NRREC,V0F.V0F_CRTOM CODREC,' ' NRINSC,V0F.V0F_VLRBRE BSCAL,V0F.V0F_VLRTOM VLTRIB,V0F.V0F_VLRSUS IMPSUS,0 ADIC,0 ADISUS, V0C.V0C_HRPROC HRPROC, V0C.V0C_DTPROC DTPROC FROM "
cQry += RetSqlName("V0C") + " V0C INNER JOIN " + RetSqlName("V0E") + " V0E ON " + FwJoinFilial("V0E","V0C") + " AND V0E.V0E_ID = V0C.V0C_ID AND V0E.V0E_VERSAO = V0C.V0C_VERSAO AND V0E.D_E_L_E_T_= ? "
cQry += "INNER JOIN " + RetSqlName("V0F") + " V0F ON " + FwJoinFilial("V0F","V0E") + " AND V0F.V0F_ID = V0C.V0C_ID AND V0F.V0F_VERSAO = V0C.V0C_VERSAO AND V0F.V0F_NRREC = V0C.V0C_PROTUL AND V0F.D_E_L_E_T_= ? "
cQry += "WHERE V0C.V0C_FILIAL IN (?) AND V0C.V0C_ATIVO = ? AND V0C.V0C_CODRET = ? AND V0C.V0C_PERAPU = ? AND V0C.D_E_L_E_T_ = ? "
aadd(aBind,space(1))    
aadd(aBind,space(1))   
aadd(aBind,aRootBranch)  
aadd(aBind,"1")  
aadd(aBind,"0")  
aadd(aBind,substr(cYearMonth,5,2) + substr(cYearMonth,1,4))    
aadd(aBind,space(1))   


cQry += "UNION ALL SELECT V0G.V0G_FILIAL FILIAL,'R-2020' EVENTO,V0G.V0G_NRREC NRREC,' ' CODREC,V0G.V0G_NRINST NRINSC,V0G.V0G_VLRBRE BSCAL,V0G.V0G_VLRPRI VLTRIB,V0G.V0G_VLRNPR IMPSUS,V0G.V0G_VLRNPR ADIC,V0G.V0G_VLRNAD ADISUS, V0C.V0C_HRPROC HRPROC, V0C.V0C_DTPROC DTPROC FROM "
cQry += RetSqlName("V0C") + " V0C INNER JOIN " + RetSqlName("V0E") + " V0E ON " + FwJoinFilial("V0E","V0C") + " AND V0E.V0E_ID = V0C.V0C_ID AND V0E.V0E_VERSAO = V0C.V0C_VERSAO AND V0E.D_E_L_E_T_= ? "
cQry += "INNER JOIN " + RetSqlName("V0G") + " V0G ON " + FwJoinFilial("V0G","V0E") + " AND V0G.V0G_ID = V0C.V0C_ID AND V0G.V0G_VERSAO = V0C.V0C_VERSAO AND V0G.V0G_NRREC = V0C.V0C_PROTUL AND V0G.D_E_L_E_T_= ? "
cQry += "WHERE V0C.V0C_FILIAL IN (?) AND V0C.V0C_ATIVO = ? AND V0C.V0C_CODRET = ? AND V0C.V0C_PERAPU = ? AND V0C.D_E_L_E_T_ = ? "
aadd(aBind,space(1))  
aadd(aBind,space(1))  
aadd(aBind,aRootBranch)  
aadd(aBind,"1")  
aadd(aBind,"0")  
aadd(aBind,substr(cYearMonth,5,2) + substr(cYearMonth,1,4))    
aadd(aBind,space(1))   


cQry += "UNION ALL SELECT V0H.V0H_FILIAL FILIAL,'R-2040' EVENTO, V0H.V0H_NRREC NRREC,V0H.V0H_CRRECR CODREC,' ' NRINSC,V0H.V0H_VLRREP BSCAL,V0H.V0H_VLRRET VLTRIB,V0H.V0H_VLRNRE IMPSUS,0 ADIC,0 ADISUS, V0C.V0C_HRPROC HRPROC, V0C.V0C_DTPROC DTPROC FROM "
cQry += RetSqlName("V0C") + " V0C INNER JOIN " + RetSqlName("V0E") + " V0E ON " + FwJoinFilial("V0E","V0C") + " AND V0E.V0E_ID = V0C.V0C_ID AND V0E.V0E_VERSAO = V0C.V0C_VERSAO AND V0E.D_E_L_E_T_= ? "
cQry += "INNER JOIN " + RetSqlName("V0H") + " V0H ON " + FwJoinFilial("V0H","V0E") + " AND V0H.V0H_ID = V0C.V0C_ID AND V0H.V0H_VERSAO = V0C.V0C_VERSAO AND V0H.V0H_NRREC = V0C.V0C_PROTUL AND V0H.D_E_L_E_T_= ? "
cQry += "WHERE V0C.V0C_FILIAL IN (?) AND V0C.V0C_ATIVO = ? AND V0C.V0C_CODRET = ? AND V0C.V0C_PERAPU = ? AND V0C.D_E_L_E_T_ = ? "
aadd(aBind,space(1))  
aadd(aBind,space(1))  
aadd(aBind,aRootBranch)  
aadd(aBind,"1")  
aadd(aBind,"0")  
aadd(aBind,substr(cYearMonth,5,2) + substr(cYearMonth,1,4))    
aadd(aBind,space(1))   


cQry += "UNION ALL SELECT V0I.V0I_FILIAL FILIAL,'R-2050' EVENTO,V0I.V0I_NRREC NRREC,V0I.V0I_CRCOML CODREC,' ' NRINSC,0 BSCAL,V0I.V0I_VRCOML VLTRIB,V0I.V0I_VRCOMS IMPSUS,0 ADIC,0 ADISUS, V0C.V0C_HRPROC HRPROC, V0C.V0C_DTPROC DTPROC FROM "
cQry += RetSqlName("V0C") + " V0C INNER JOIN " + RetSqlName("V0E") + " V0E ON " + FwJoinFilial("V0E","V0C") + " AND V0E.V0E_ID = V0C.V0C_ID AND V0E.V0E_VERSAO = V0C.V0C_VERSAO AND V0E.D_E_L_E_T_= ? "
cQry += "INNER JOIN " + RetSqlName("V0I") + " V0I ON " + FwJoinFilial("V0I","V0E") + " AND V0I.V0I_ID = V0C.V0C_ID AND V0I.V0I_VERSAO = V0C.V0C_VERSAO AND V0I.V0I_NRREC = V0C.V0C_PROTUL AND V0I.D_E_L_E_T_= ? "
cQry += "WHERE V0C.V0C_FILIAL IN (?) AND V0C.V0C_ATIVO = ? AND V0C.V0C_CODRET = ? AND V0C.V0C_PERAPU = ? AND V0C.D_E_L_E_T_ = ? "
aadd(aBind,space(1))  
aadd(aBind,space(1))  
aadd(aBind,aRootBranch)  
aadd(aBind,"1")  
aadd(aBind,"0")  
aadd(aBind,substr(cYearMonth,5,2) + substr(cYearMonth,1,4))    
aadd(aBind,space(1))   


cQry += "UNION ALL SELECT V6C.V6C_FILIAL FILIAL,'R-2055' EVENTO,V6C.V6C_NRREC NRREC,V6C.V6C_CODREC CODREC,' ' NRINSC ,0 BSCAL,V6C.V6C_VRAQUI VLTRIB,V6C.V6C_VLSUSP IMPSUS,0 ADIC,0 ADISUS, V0C.V0C_HRPROC HRPROC, V0C.V0C_DTPROC DTPROC FROM "
cQry += RetSqlName("V0C") + " V0C INNER JOIN " + RetSqlName("V0E") + " V0E ON " + FwJoinFilial("V0E","V0C") + " AND V0E.V0E_ID = V0C.V0C_ID AND V0E.V0E_VERSAO = V0C.V0C_VERSAO AND V0E.D_E_L_E_T_= ? "
cQry += "INNER JOIN " + RetSqlName("V6C") + " V6C ON " + FwJoinFilial("V6C","V0E") + " AND V6C.V6C_ID = V0C.V0C_ID AND V6C.V6C_VERSAO = V0C.V0C_VERSAO AND V6C.V6C_NRREC = V0C.V0C_PROTUL AND V6C.D_E_L_E_T_= ? "
cQry += "WHERE V0C.V0C_FILIAL IN (?) AND V0C.V0C_ATIVO = ? AND V0C.V0C_CODRET = ? AND V0C.V0C_PERAPU = ? AND V0C.D_E_L_E_T_ = ? "
aadd(aBind,space(1))  
aadd(aBind,space(1))  
aadd(aBind,aRootBranch)  
aadd(aBind,"1")  
aadd(aBind,"0")  
aadd(aBind,substr(cYearMonth,5,2) + substr(cYearMonth,1,4))    
aadd(aBind,space(1))  


cQry += "UNION ALL SELECT V0J.V0J_FILIAL FILIAL, 'R-2060' EVENTO,V0J.V0J_NRREC NRREC,V0J.V0J_CODREC CODREC,' ' NRINSC,0 BSCAL,V0J.V0J_VLRCPT VLTRIB,V0J.V0J_VLRCPS IMPSUS,0 ADIC,0 ADISUS, V0C.V0C_HRPROC HRPROC, V0C.V0C_DTPROC DTPROC FROM "
cQry += RetSqlName("V0C") + " V0C INNER JOIN " + RetSqlName("V0E") + " V0E ON " + FwJoinFilial("V0E","V0C") + " AND V0E.V0E_ID = V0C.V0C_ID AND V0E.V0E_VERSAO = V0C.V0C_VERSAO AND V0E.D_E_L_E_T_= ? "
cQry += "INNER JOIN " + RetSqlName("V0J") + " V0J ON " + FwJoinFilial("V0J","V0E") + " AND V0J.V0J_ID = V0C.V0C_ID AND V0J.V0J_VERSAO = V0C.V0C_VERSAO AND V0J.V0J_NRREC = V0C.V0C_PROTUL AND V0J.D_E_L_E_T_= ? "
cQry += "WHERE V0C.V0C_FILIAL IN (?) AND V0C.V0C_ATIVO = ? AND V0C.V0C_CODRET = ? AND V0C.V0C_PERAPU = ? AND V0C.D_E_L_E_T_ = ? "
aadd(aBind,space(1))  
aadd(aBind,space(1))  
aadd(aBind,aRootBranch)  
aadd(aBind,"1")  
aadd(aBind,"0")  
aadd(aBind,substr(cYearMonth,5,2) + substr(cYearMonth,1,4))    
aadd(aBind,space(1))  

cQry += " ORDER BY FILIAL, NRREC, EVENTO, CODREC, NRINSC "

cQry := ChangeQuery(cQry)

oPrepare := FwExecStatement():New( cQry )
for nI := 1 to len(aBind)
    if Valtype(aBind[nI]) == 'A'
        oPrepare:setIn( nI, aBind[nI] )
    else
        oPrepare:setString( nI, aBind[nI] )
    endif
next nI

cAlias := GetNextAlias()
oPrepare:OpenAlias( cAlias )

freeObj(oPrepare)

Return cAlias
