#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.taf.rulesIPI.ch"
    
namespace totvs.protheus.fiscal.taf.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGATAF", tables="V8W, C20, C2F", name="RulesIPISmartViewBusinessObject", country="BRA", initialRelease="12.1.2310") 

//-------------------------------------------------------------------
/*{Protheus.doc}  RulesIPISmartViewBusinessObject
Classe para criação do Objeto de Apuração de IPI
@author Juliana Mellão
@since 10/06/2024
*/
//-------------------------------------------------------------------  
class RulesIPISmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    data nPageSize as integer
    data cDBMS as character

    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author Juliana Mellão
@since 10/06/2024
*/
//-------------------------------------------------------------------   
method new() class RulesIPISmartViewBusinessObject

    Local oTafTools := tafSmartviewTools():new()
    
    _Super:new()
    self:appendArea(STR0001) //"TAF Fiscal"
    self:setDisplayName(STR0002) //"Apuração de IPI por regra"
    self:setDescription(STR0003) //"Objeto de Negocios para conferência da Apuração de IPI por regra"

    oTafTools:validPerg(self:setPergunte("TAFTR00012"))
    
    self:cDBMS := UPPER(TcGetDb()) //verifica qual banco é utilizado

    freeobj(oTafTools)

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: self:oData 
@author Juliana Mellão
@since 10/06/2024
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class RulesIPISmartViewBusinessObject

    Local cQuery      as Character
    Local oTafTools   := tafSmartviewTools():new()
    Local oFiltQry    := oFilter:getParameters() as Json
    Local lOk         := .t. as logical
    Local cFil        := "" as character
    Local oPrepare    := nil as object
    Local cAlias      := "" as character
    Local cRuleId     := oFiltQry['MV_PAR01',1] as Character
    Local cPeriod     := oFiltQry['MV_PAR02',1] as Character

    cAlias := getFiliais(cRuleId, cPeriod)
    If (cAlias)->(!Eof())
        cFil := (cAlias)->V11_FILANT
    EndIf

    //Define a quantidade máxima por página (Default 100)
    self:setPageSize(200)
 
    If lOk

        cQuery := " SELECT " 
        cQuery += "     V8W.V8W_FILIAL, "
        cQuery += "     C20.C20_NUMDOC, "
        cQuery += "     C20.C20_SERIE, "
        cQuery += "     C20.C20_ESPECI, "
        cQuery += "     C20.C20_CLIFOR, "
        cQuery += "     C1H.C1H_NOME, "
        cQuery += "     C20.C20_DTDOC, " 
        cQuery += "     C20.C20_VLDOC, "
        cQuery += "     V8W.V8W_ALIQ, "
        cQuery += "     C2F.C2F_BASE, "
        cQuery += "     C2F.C2F_VLOPE, "
        cQuery += "     V8W.V8W_VALOR, "
        cQuery += "     C0Y.C0Y_CODIGO, "
        cQuery += "     C0Y.C0Y_DESCRI, "

        cQuery += "     C0Y.C0Y_CODIGO || ' - ' || C0Y.C0Y_DESCRI CFOP, "

        cQuery += "     C15.C15_CODIGO, "    
        cQuery += "     C15.C15_DESCRI,

        cQuery += "     C15.C15_CODIGO || ' - ' || C15.C15_DESCRI CST "

        cQuery += " FROM "
        cQuery +=       RetSQLName("V8W") + " V8W "
        cQuery += " INNER JOIN " + RetSqlName('C20') + " C20 ON " + FwJoinFilial("C20","V8W") + " AND C20.C20_CHVNF = V8W.V8W_CHVNF  AND C20.D_E_L_E_T_ = ?"
        
        cQuery += " INNER JOIN " + RetSqlName("C2F") + " C2F ON " + FwJoinFilial("C2F","V8W") + " AND C2F.C2F_CHVNF = V8W.V8W_CHVNF AND C2F.C2F_CODTRI = '000005' AND C2F.C2F_ALIQ = V8W.V8W_ALIQ AND C2F.D_E_L_E_T_ = ?"
        
        cQuery += " INNER JOIN " + RetSqlName("C1H") + " C1H ON " + FwJoinFilial("C1H","C20") + " AND C20.C20_CODPAR = C1H.C1H_ID AND C1H.D_E_L_E_T_ = ?"
        
        cQuery += " INNER JOIN " + RetSqlName("C0Y") + " C0Y ON V8W.V8W_CFOP = C0Y.C0Y_ID AND C0Y.D_E_L_E_T_ = ?"
        
        cQuery += " INNER JOIN " + RetSqlName("C15") + " C15 ON V8W.V8W_CST = C15.C15_ID AND C15.D_E_L_E_T_ = ?"
        
        cQuery += " INNER JOIN " + RetSqlName("V13") + " V13 ON " + FwJoinFilial("V8W","V13") + " AND V8W.V8W_IDPER = V13.V13_ID AND V13.D_E_L_E_T_ = ?"
        
        cQuery += " WHERE " 
        cQuery += "    V8W.V8W_FILIAL = ? "
        cQuery += "    AND V13.V13_ID = ? " 
        cQuery += "    AND V13.V13_REGID = ? " 
        cQuery += "    AND V8W.D_E_L_E_T_ = ? "

        self:setOrder("V8W.V8W_FILIAL, V8W.V8W_IDPER, C20.C20_NUMDOC")

        cQuery := ChangeQuery( cQuery ) 

        oPrepare := FWPreparedStatement():New(cQuery)

        oPrepare:SetString( 01 , Space(01)  )
        oPrepare:SetString( 02 , Space(01)  )
        oPrepare:SetString( 03 , Space(01)  )
        oPrepare:SetString( 04 , Space(01)  )
        oPrepare:SetString( 05 , Space(01)  )
        oPrepare:SetString( 06 , Space(01)  )    
        oPrepare:SetString( 07 , cFil       )
        oPrepare:SetString( 08 , cPeriod    )
        oPrepare:SetString( 09 , cRuleId    )
        oPrepare:SetString( 10 , Space(01)  )

        cQuery := oPrepare:GetFixQuery()

        self:setQuery(cQuery) 

    EndIf
    
    freeobj(oTafTools)
    freeObj(oPrepare)

return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author Juliana Mellão
@since 10/06/2024
*/
//-------------------------------------------------------------------   
method getSchema() as object class RulesIPISmartViewBusinessObject

//                 ( Nome            ,  Descricao            , Tipo      , Display          , cRealName     )
   self:addProperty("V8W_FILIAL "    , "Filial"              , "string"  , "Filial"         , "V8W_FILIAL " )
   self:addProperty("C20_NUMDOC "    , "Nota Fiscal"         , "string"  , "Nt Fiscal"      , "C20_NUMDOC " )
   self:addProperty("C20_SERIE  "    , "Serie"               , "string"  , "Serie"          , "C20_SERIE  " )
   self:addProperty("C20_ESPECI "    , "Especie"             , "string"  , "Especie"        , "C20_ESPECI " )
   self:addProperty("C20_CLIFOR "    , "Cod Cliente/Forn"    , "string"  , "Cod Cli/For"    , "C20_CLIFOR " )
   self:addProperty("C1H_NOME   "    , "Cliente/Forn"        , "string"  , "Cliente/Forn"   , "C1H_NOME   " )
   self:addProperty("C20_DTDOC  "    , "Data do Documento"   , "date"    , "Dt Documento"   , "C20_DTDOC  " )
   self:addProperty("C20_VLDOC  "    , "Valor do Documento"  , "number"  , "Vl Documento"   , "C20_VLDOC  " )
   self:addProperty("V8W_ALIQ   "    , "Aliquota do IPI"     , "number"  , "% Aliq IPI"     , "V8W_ALIQ   " )
   self:addProperty("C2F_BASE   "    , "Valor Base"          , "number"  , "Vl Base"        , "C2F_BASE   " )
   self:addProperty("C2F_VLOPE  "    , "Valor Operacao"      , "number"  , "Vl Oper"        , "C2F_VLOPE  " )
   self:addProperty("V8W_VALOR  "    , "Valor do IPI"        , "number"  , "Vl IPI"         , "V8W_VALOR  " )
   self:addProperty("C0Y_CODIGO "    , "Cod CFOP"            , "string"  , "Cod CFOP"       , "C0Y_CODIGO " )
   self:addProperty("C0Y_DESCRI "    , "Desc CFOP"           , "string"  , "Desc CFOP"      , "C0Y_DESCRI " )
   self:addProperty("CFOP       "    , "CFOP"                , "string"  , "CFOP"           , "CFOP       " )
   self:addProperty("C15_CODIGO "    , "Cod CST"             , "string"  , "Cod CST"        , "C15_CODIGO " )
   self:addProperty("C15_DESCRI "    , "Desc CST"            , "string"  , "Desc CST"       , "C15_DESCRI " )
   self:addProperty("CST        "    , "CST"                 , "string"  , "CST"            , "CST        " )

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author Juliana Mellão
@since 10/06/2024
*/
//-------------------------------------------------------------------   

Static Function getFiliais(cRuleId as character, cPeriod as character) as character

    Local cQry        := ""             as character
    Local cRetFil     := GetNextAlias() as character
    Local aBind       := {}             as array

    cQry := " SELECT  "
    cQry += " V11.V11_FILANT    "
    cQry += " FROM " + RetSqlName('V11') + " V11 "
    cQry += " INNER JOIN " + RetSqlName('V13') + " V13 ON " + FwJoinFilial("V13","V11") 
    cQry += " AND V11.V11_CODIGO = V13.V13_REGID AND V13.V13_IDFIL = V11.V11_ID AND V13.D_E_L_E_T_ = ' ' "
    cQry += " WHERE V11.D_E_L_E_T_ = ' ' "
    cQry += " AND V11.V11_CODIGO = ? " 
    cQry += " AND V13.V13_ID = ? " 

    AADD(aBind, cRuleId)
    AADD(aBind, cPeriod)

    dbUseArea(.T., "TOPCONN", TCGenQry2(, , cQry, aBind), cRetFil, .F., .T.)

return cRetFil
