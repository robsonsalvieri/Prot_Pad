#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.taf.reinfblock4099and9015.ch"

namespace totvs.protheus.fiscal.taf.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider( active=.T., team="SIGATAF", tables="V3W,V9F,V9Q", name="ReinfBlock4099and9015", country="BRA", initialRelease="12.1.2310" )

//-------------------------------------------------------------------
/*/{Protheus.doc} ReinfBlock4099and9015SmartViewBusinessObject
Classe para criação do Objeto de Negócio
@author Denis Souza
@since 26/10/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class ReinfBlock4099and9015SmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

public method new()        as object
public method getData()    as object
public method getSchema()  as object
private method execQuery() as character

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Denis Souza
@since 26/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class ReinfBlock4099and9015SmartViewBusinessObject

_Super:new()
self:appendArea(STR0001) //"TAF Fiscal Contábil"
self:setDisplayName(STR0002) //"Relatório REINF(R4099/R9015) Fech. Bloco 40 x Retorno Totalizador Governo"
self:setDescription(STR0003 + STR0004) //"Objeto ReinfBlock4099and9015 Fech Bloco 40 x Retorno Governo"##" (V3W,V9F,V9Q)"
if !::setPergunte("TAFTR00007")
    FwLogMsg(STR0005,,STR0006,,,,STR0007,,,) //STR0005##STR0006##STR0007
endif
self:setPageSize(10000)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório 
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Denis Souza
@since 26/10/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method getData( nPage as numeric, oFilter as object ) as object class ReinfBlock4099and9015SmartViewBusinessObject

local oFiltQry     := Nil as json
local cAlias       := " " as character
local cYearMonth   := " " as character
local cName        := " " as character
local cTaxNbBranch := " " as character
local cTpPer       := " " as character
local cProcessDate := " " as character
local cProcessHour := " " as character
local cPerApu      := " " as character
local nStart       := 0   as numeric
local nEnd         := 0   as numeric
local nX           := 0   as numeric
local nVlMen       := 0   as numeric
local nVMenSus     := 0   as numeric
local nVlQui       := 0   as numeric
local nVQuiSus     := 0   as numeric
local nVlDen       := 0   as numeric
local nVDenSus     := 0   as numeric
local nVlSem       := 0   as numeric
local nVSemSus     := 0   as numeric
local nVlDay       := 0   as numeric
local nVDaySus     := 0   as numeric
local aBranch      := {}  as array
local aRootBranch  := {}  as array
local aSM0         := {}  as array

default nPage   := 1
default oFilter := Nil

oFiltQry   := oFilter:getParameters()
cYearMonth := AllTrim(oFiltQry['MV_PAR01',1])        //Ano+Mes (AAAAMM)
nStart     := ((nPage - 1) * self:getPageSize()) + 1 //Seta o primeiro item
nEnd       := nPage * self:getPageSize()             //Seta último da página atual

aBranch := wsLoadFil()
For nX := 1 to len(aBranch)
    aadd(aRootBranch, { aBranch[nX][1], aBranch[nX][3] } )
Next nX

cAlias := self:execQuery( aRootBranch, nStart, nEnd, cYearMonth )

while !(cAlias)->(Eof())

    cName := " "
    cTaxNbBranch := " "

    aSM0 := FWSM0Util():GetSM0Data(cEmpAnt,(cAlias)->FILIAL,{"M0_FILIAL","M0_CGC"})

    if len(aSM0) >= 2
        cName := SubStr(Alltrim(aSM0[1][2]),1,30)
        if len(Alltrim(aSM0[2][2])) == 14
            cTaxNbBranch := Transform(Alltrim(aSM0[2][2]),"@R! NN.NNN.NNN/NNNN-99")
        endif
    endif

    cProcessDate := StrTran( SubStr( FwTimeStamp(5,sToD((cAlias)->DTPROC) ,"00:00:00"),1,10 ) , "-", "")

    cProcessHour := PadL(Alltrim((cAlias)->HRPROC),4,"0")
    cProcessHour := SubStr(cProcessHour,1,2) + ":" + SubStr(cProcessHour,3,2)

    nVlMen   := 0
    nVMenSus := 0
    nVlQui   := 0
    nVQuiSus := 0
    nVlDen   := 0
    nVDenSus := 0
    nVlSem   := 0
    nVSemSus := 0
    nVlDay   := 0
    nVDaySus := 0
    cTpPer   := Alltrim((cAlias)->TPPER)
    cPerApu  := " "

    Do Case
        Case cTpPer == '1' //Mensal
            nVlMen   := (cAlias)->VALTRIB
            nVMenSus := (cAlias)->VLTRIBSUSP
            cPerApu  := 'Mês ' + SubStr(cYearMonth,5,2)
        Case cTpPer == '2' //Quinzenal
            nVlQui   := (cAlias)->VALTRIB
            nVQuiSus := (cAlias)->VLTRIBSUSP
            cPerApu  := Alltrim((cAlias)->PERTRB) + "º Quinzena"
        Case cTpPer == '3' //Decendial
            nVlDen   := (cAlias)->VALTRIB
            nVDenSus := (cAlias)->VLTRIBSUSP
            cPerApu  := Alltrim((cAlias)->PERTRB) + "º Decendio"
        Case cTpPer == '4' //Semanal
            nVlSem   := (cAlias)->VALTRIB
            nVSemSus := (cAlias)->VLTRIBSUSP
            cPerApu  := Alltrim((cAlias)->PERTRB) + "º Semana"
        Case cTpPer == '5' //Diario
            nVlDay   := (cAlias)->VALTRIB
            nVDaySus := (cAlias)->VLTRIBSUSP
            cPerApu  := "Dia " + Alltrim((cAlias)->PERTRB)
    EndCase

    self:oData:appendData( { "_FILIAL" : Alltrim((cAlias)->FILIAL)  ,; //01
                             "_RAZSOC" : cName                      ,; //02
                             "_CNPJFIL": cTaxNbBranch               ,; //03
                             "_EVENTO" : Alltrim((cAlias)->TPEVEN)  ,; //04
                             "_COMP"   : Alltrim((cAlias)->PERAPU)  ,; //05
                             "_TPPER"  : cTpPer                     ,; //06
                             "_PERTRB" : cPerApu                    ,; //07
                             "_CODREC" : Alltrim((cAlias)->CR)      ,; //08
                             "_NATREN" : Alltrim((cAlias)->NATREN)  ,; //09
                             "_VLMEN"  : nVlMen                     ,; //10
                             "_VMENSUS": nVMenSus                   ,; //11
                             "_VLQUI"  : nVlQui                     ,; //12
                             "_VQUISUS": nVQuiSus                   ,; //13
                             "_VLDEN"  : nVlDen                     ,; //14
                             "_VDENSUS": nVDenSus                   ,; //15
                             "_VLSEM"  : nVlSem                     ,; //16
                             "_VSEMSUS": nVSemSus                   ,; //17
                             "_VLDIA"  : nVlDay                     ,; //18
                             "_VDIASUS": nVDaySus                   ,; //19
                             "_NRREC"  : Alltrim((cAlias)->NRRECB)  ,; //20
                             "_HRPROC" : cProcessHour               ,; //21
                             "_DTPROC" : DtoC(Stod(cProcessDate))   }) //22

    (cAlias)->(dbSkip())
enddo
(cAlias)->(DBCloseArea())

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Denis Souza
@since 26/10/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class ReinfBlock4099and9015SmartViewBusinessObject

self:oSchema:addProperty( "_FILIAL" , "Filial"	                    , "string", "Filial"	                    , "FILIAL"  , , , ) //01
self:oSchema:addProperty( "_RAZSOC" , "Razão Social"                , "string", "Razão Social"                  , "RAZSOC"  , , , ) //02
self:oSchema:addProperty( "_CNPJFIL", "CNPJ Filial"	                , "string", "CNPJ Filial"	                , "CNPJFIL" , , , ) //03
self:oSchema:addProperty( "_EVENTO" , "Evento"                      , "string", "Evento"	                    , "EVENTO"  , , , ) //04
self:oSchema:addProperty( "_COMP"   , "Competência"                 , "string", "Competência"	                , "COMP"    , , , ) //05
self:oSchema:addProperty( "_TPPER"  , "Tipo Per."                   , "string", "Tipo Per."	                    , "TPPER"   , , , ) //06
self:oSchema:addProperty( "_PERTRB" , "Per. Apur CR"                , "string", "Per. Apur CR"	                , "PERTRB"  , , , ) //07
self:oSchema:addProperty( "_CODREC" , "Cod. Receita"                , "string", "Cod. Receita"                  , "CODREC"  , , , ) //08
self:oSchema:addProperty( "_NATREN" , "Natureza Rendimento"         , "string", "Natureza Rendimento"           , "NATREN"  , , , ) //09
self:oSchema:addProperty( "_VLMEN"  , "Valor Tributo Mensal"        , "number", "Valor Tributo Mensal"          , "VLMEN"   , , , ) //10
self:oSchema:addProperty( "_VMENSUS", "Vl. Trib. Mensal Suspenso"   , "number", "Vl. Trib. Mensal Suspenso"     , "VMENSUS" , , , ) //11
self:oSchema:addProperty( "_VLQUI"  , "Valor Tributo Quinzenal"     , "number", "Valor Tributo Quinzenal"       , "VLQUI"   , , , ) //12
self:oSchema:addProperty( "_VQUISUS", "Vl. Trib. Quinzenal Suspenso", "number", "Vl. Trib. Quinzenal Suspenso"  , "VQUISUS" , , , ) //13
self:oSchema:addProperty( "_VLDEN"  , "Valor Tributo Decendial"     , "number", "Valor Tributo Decendial"       , "VLDEN"   , , , ) //14
self:oSchema:addProperty( "_VDENSUS", "Vl. Trib. Decendial Suspenso", "number", "Vl. Trib. Decendial Suspenso"  , "VDENSUS" , , , ) //15
self:oSchema:addProperty( "_VLSEM"  , "Valor Tributo Semanal"       , "number", "Valor Tributo Semanal"         , "VLSEM"   , , , ) //16
self:oSchema:addProperty( "_VSEMSUS", "Vl. Trib. Semanal Suspenso"  , "number", "Vl. Trib. Semanal Suspenso"    , "VSEMSUS" , , , ) //17
self:oSchema:addProperty( "_VLDIA"  , "Valor Tributo Diário"        , "number", "Valor Tributo Diário"          , "VLDIA"   , , , ) //18
self:oSchema:addProperty( "_VDIASUS", "Vl. Trib. Diário Suspenso"   , "number", "Vl. Trib. Diário Suspenso"     , "VDIASUS" , , , ) //19
self:oSchema:addProperty( "_NRREC"  , "Nº de Recibo"                , "string", "Nº de Recibo"	                , "NRREC"   , , , ) //20
self:oSchema:addProperty( "_HRPROC" , "Hora Processamento"          , "string", "Hora Processamento"            , "HRPROC"  , , , ) //21
self:oSchema:addProperty( "_DTPROC" , "Data Processamento"          , "string", "Data Processamento"            , "DTPROC"  , , , ) //22

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} execQuery

Execução da query com paginação.

@param oFilter, objeto, contém o filtro do SmartView
@param nStart, numérico, indica o inicío da paginação
@param nEnd, numérico, indica o fim da paginação
 
@return string

@author Denis Souza
@since 26/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method execQuery(  aRootBranch as array, nStart as numeric, nEnd as numeric, cYearMonth as character ) as character class ReinfBlock4099and9015SmartViewBusinessObject

local cQry     := " " as character
local cFilsV3W := " " as character
local nlA          := 0   as numeric
local aBind             := {}  as array
local oPrepare          := Nil as object
local aRoot        := {}  as array

default aRootBranch := {}
default nStart   	:= 0
default nEnd   		:= 0
default cYearMonth 	:= ""

cFilsV3W := TafRetFilC("V3W", aRootBranch )

cFilsV3W := StrTran(StrTran(StrTran(cFilsV3W,"(",""),")",""),"'","")

aSize(aRoot ,0)
aRoot  := Separa( cFilsV3W, "," )

cQry += " SELECT ROW_NUMBER() OVER (ORDER BY V9Q.V9Q_FILIAL, V9Q.V9Q_TPPER, V9Q.V9Q_PERTRB, V9Q.V9Q_CR, V9Q.V9Q_NATREN ) LINHA, "
cQry += " V9Q.V9Q_FILIAL FILIAL, TMP.V9F_DTPROC DTPROC, TMP.V9F_HRPROC HRPROC, TMP.V9F_TPEVEN TPEVEN, TMP.V9F_PERAPU PERAPU, TMP.V9F_NRRECB NRRECB, "
cQry += " V9Q.V9Q_TPPER TPPER, V9Q.V9Q_PERTRB PERTRB, V9Q.V9Q_CR CR, V9Q.V9Q_NATREN NATREN, V9Q.V9Q_VTRIBI VALTRIB, V9Q.V9Q_VSUSPI VLTRIBSUSP "
cQry += " FROM " + RetSqlName("V9Q") + " V9Q INNER JOIN "
cQry += " ( SELECT V9F.V9F_FILIAL, V9F.V9F_ID, V9F.V9F_VERSAO, V9F.V9F_PERAPU, V9F.V9F_DTPROC, V9F.V9F_HRPROC, V9F.V9F_TPEVEN, V9F.V9F_NRRECB "
cQry += " FROM " + RetSqlName("V3W") + " V3W "
cQry += " INNER JOIN " + RetSqlName("V9F") + " V9F ON " + FwJoinFilial("V3W","V9F") + " AND V3W.V3W_XMLID = V9F.V9F_IDEVEN "
cQry += " AND RTRIM(LTRIM(V3W.V3W_PROTUL)) = RTRIM(LTRIM(V9F.V9F_PROTUL)) AND V3W.V3W_PERAPU = V9F.V9F_PERAPU AND V9F.V9F_ATIVO = ? AND V9F.V9F_TPEVEN = ? AND V9F.D_E_L_E_T_ = ? "
aadd(aBind, "1")
aadd(aBind, "4099")
aadd(aBind, space(1))

cQry += " WHERE "

If "TMPFIL" $ aRoot[1]
    cQry += " V3W.V3W_FILIAL IN (" + aRoot[1] + ") "
Else
    cQry += " V3W.V3W_FILIAL IN (?) "
    aadd(aBind, aRoot)
EndIf

cQry += " AND V3W.V3W_ATIVO = ? AND V3W.V3W_PERAPU = ? AND V3W.D_E_L_E_T_ = ? "
cQry += " ) TMP ON " + FwJoinFilial("V9Q","V9F","V9Q","TMP") + " AND V9Q.V9Q_ID = TMP.V9F_ID AND V9Q.V9Q_VERSAO = TMP.V9F_VERSAO WHERE V9Q.D_E_L_E_T_ = ? "
aadd(aBind, "1")
aadd(aBind, substr(cYearMonth,5,2) + substr(cYearMonth,1,4))
aadd(aBind, space(1))
aadd(aBind, space(1))

cQry := ChangeQuery(cQry)
oPrepare := FwExecStatement():New( cQry )
for nlA := 1 to len(aBind)
    if Valtype(aBind[nlA]) == 'A'
        oPrepare:setIn( nlA, aBind[nlA] )
    else
        oPrepare:setString( nlA, aBind[nlA] )
    endif
next nlA

cAlias := GetNextAlias()
oPrepare:OpenAlias( cAlias )

Return cAlias
