#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.taf.reinfblock3010.ch"

namespace totvs.protheus.fiscal.taf.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATAF", tables="T9F,C1E,T9G,T9H,T9J,C1G,C07,C09", name="ReinfBlock3010", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*/{Protheus.doc} ReinfBlock3010SmartViewBusinessObject
Classe para criação do Objeto de Negócio
@author Denis Souza
@since 20/12/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class ReinfBlock3010SmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

public method new()        as object
public method getData()    as object
public method getSchema()  as object
private method execQuery() as character

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Denis Souza
@since 20/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class ReinfBlock3010SmartViewBusinessObject

local oTafTools := Nil as object

oTafTools := tafSmartviewTools():new() //Classe para auxiliar no coverage 100% e automacao.

_Super:new()
self:appendArea(STR0001) //"TAF Fiscal Contábil"
self:setDisplayName(STR0002) //"Relatório REINF R-3010 Movimentos TAF x Retorno Governo"
self:setDescription(STR0003 + " - (T9F,C1E,T9G,T9H,T9J,C1G,C07,C09)") //"Objeto ReinfBlock3010 - Movimentos TAF x Retorno Governo"
if !::setPergunte("TAFTR00010")
    FwLogMsg(STR0004,,STR0005,,,,STR0006,,,) //"WARN"##"SmartView"##"Grupo de perguntas nao encontrado"
endif

self:setPageSize( oTafTools:paginacao(10000) )

freeobj(oTafTools)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Denis Souza
@since 20/12/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method getData( nPage as numeric, oFilter as object ) as object class ReinfBlock3010SmartViewBusinessObject

local oFiltQry    := Nil as json
local cYearMonth  := ""  as character
local cStartDate  := ""  as character
local cEndDate    := ""  as character
local cRootBranch := ""  as character
local cBranchId   := ""  as character
local cName       := ""  as character
local cCNPJ       := ""  as character
local cDtEsp      := ""  as character
local cCodProc    := ""  as character
local aBranch     := {}  as array
local aInfEUF     := {}  as array
local aRootBranch := {}  as array
local nStart      := 0   as numeric
local nEnd        := 0   as numeric
local nX          := 0   as numeric
local nVlSusp     := 0   as numeric

default nPage   := 1
default oFilter := Nil

oFiltQry := oFilter:getParameters()

cYearMonth := AllTrim(oFiltQry['MV_PAR01',1]) //Ano+Mes (AAAAMM)

aInfEUF := TAFTamEUF(Upper(AllTrim(SM0->M0_LEIAUTE)))

aBranch := wsLoadFil()
For nX := 1 to len(aBranch)
    aadd(aRootBranch, { aBranch[nX][1], aBranch[nX][3] } )
Next nX
cRootBranch := TafRetFilC("T9F", aRootBranch )
cRootBranch := StrTran(StrTran(StrTran(cRootBranch,"(",""),")",""),"'","")
aSize(aRootBranch,0)

aRootBranch := Separa( cRootBranch, "," )

nStart := ((nPage - 1) * self:getPageSize()) + 1 //Seta o primeiro item
nEnd   := nPage * self:getPageSize()             //Seta último da página atual
if !Empty(cYearMonth)
    cStartDate := cYearMonth + "01"               //20100101 ex: "20100101"
    cEndDate   := DtoS(LastDay(StoD(cStartDate))) //20240101 ex: "20100131"
endif
cAlias := self:execQuery( @oFilter, nStart, nEnd, cStartDate, cEndDate, aRootBranch, aInfEUF )

while !(cAlias)->(Eof())

    cBranchId := Alltrim((cAlias)->FILIAL)
    cCNPJ     := ''
    cName     := ''

    aSM0 := FWSM0Util():GetSM0Data(cEmpAnt,(cAlias)->FILIAL,{"M0_FILIAL","M0_CGC"})
    if len(aSM0) >= 2
        cName := SubStr(Alltrim(aSM0[1][2]),1,40)
        cCNPJ := Alltrim(aSM0[2][2])
        if len(cCNPJ) == 14
            cCNPJ := Transform(cCNPJ,"@R! NN.NNN.NNN/NNNN-99")                       
        endif
    endif

    cDtEsp := FWTimeStamp(5,stod((cAlias)->DTESP),"00:00:00")

    cCodProc := 'Não Possui'
    nVlSusp  := 0
    If !Empty((cAlias)->NUMPRO)
        cCodProc := (cAlias)->NUMPRO
        nVlSusp  := (cAlias)->VLRSUS
    endif

    self:oData:appendData({ "_FIL"  : cBranchId                     ,; //01 "_FIL"   "Filial"
                            "_CNPJ" : cCNPJ                         ,; //02 "_CNPJ"  "CNPJ"
                            "_RAZAO": cName                         ,; //03 "_RAZAO" "Razão Social"
                            "_DTESP": cDtEsp                        ,; //04 "_DTESP" "Dt Espetacul"
                            "_NRBOL": Alltrim((cAlias)->BOLETIM)    ,; //05 "_NRBOL" "Número Boletim"
                            "_TPCOM": Alltrim((cAlias)->TIPO)       ,; //06 "_TPCOM" "Tipo Competição"
                            "_CAEVT": Alltrim((cAlias)->CATEGORIA)  ,; //07 "_CAEVT" "Categoria Evento"
                            "_MODES": Alltrim((cAlias)->MODALIDADE) ,; //08 "_MODES" "Modalidade Esportiva"
                            "_NMCOM": Alltrim((cAlias)->COMPETICAO) ,; //09 "_NMCOM" "Nome Competição"
                            "_CNPJM": Alltrim((cAlias)->CNPJMAND)   ,; //10 "_CNPJM" "CNPJ Clube Mandante"
                            "_CNPJV": Alltrim((cAlias)->CNPJVI)     ,; //11 "_CNPJV" "CNPJ Clube Visitante"
                            "_NMVIS": Alltrim((cAlias)->VISITANTE)  ,; //12 "_NMVIS" "Nome Clube Visitante"
                            "_PDESP": Alltrim((cAlias)->PRACA)      ,; //13 "_PDESP" "Praça Desportiva"
                            "_ESTEV": Alltrim((cAlias)->ESTADO)     ,; //14 "_ESTEV" "Estado Evento"
                            "_MUNEV": Alltrim((cAlias)->CIDADE)     ,; //15 "_MUNEV" "Municipio Evento"
                            "_QTPGT": (cAlias)->PAGANTES            ,; //16 "_QTPGT" "Qtd Pagantes"
                            "_QTNPG": (cAlias)->NAOPAGANTES         ,; //17 "_QTNPG" "Qtd Não Pagantes"
                            "_TTREC": (cAlias)->TOTALBRUTO          ,; //18 "_TTREC" "Total Receita"
                            "_TTCRP": (cAlias)->TRIBUTO             ,; //19 "_TTCRP" "Total Contr. Prev."
                            "_TTRCC": (cAlias)->RECEITA             ,; //20 "_TTRCC" "Total Receita Clube"
                            "_TTRET": (cAlias)->VALORRETIDO         ,; //21 "_TTRET" "Total Retido"
                            "_CPROC": cCodProc                      ,; //22 "_CPROC" "Numero Processo Ref."
                            "_VLSUS": nVlSusp                       }) //23 "_VLSUS" "Valor Suspenso"

    (cAlias)->( dbSkip() )
enddo

(cAlias)->( DBCloseArea() )



return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Denis Souza
@since 20/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class ReinfBlock3010SmartViewBusinessObject

self:oSchema:addProperty( "_FIL"  , "Filial"              , "string", "Filial"              , "FIL"  , , , ) //01 "_FIL"   "Filial"
self:oSchema:addProperty( "_CNPJ" , "CNPJ"                , "string", "CNPJ"                , "CNPJ" , , , ) //02 "_CNPJ"  "CNPJ"
self:oSchema:addProperty( "_RAZAO", "Razão Social"        , "string", "Razão Social"        , "RAZAO", , , ) //03 "_RAZAO" "Razão Social"
self:oSchema:addProperty( "_DTESP", "Data Espetáculo"     , "date"  , "Data Espetáculo"     , "DTESP", , , ) //04 "_DTESP" "Dt Espetacul"
self:oSchema:addProperty( "_NRBOL", "Número Boletim"      , "string", "Número Boletim"      , "NRBOL", , , ) //05 "_NRBOL" "Número Boletim"
self:oSchema:addProperty( "_TPCOM", "Tipo Competição"     , "string", "Tipo Competição"     , "TPCOM", , , ) //06 "_TPCOM" "Tipo Competição"
self:oSchema:addProperty( "_CAEVT", "Categoria Evento"    , "string", "Categoria Evento"    , "CAEVT", , , ) //07 "_CAEVT" "Categoria Evento"
self:oSchema:addProperty( "_MODES", "Modalidade Esportiva", "string", "Modalidade Esportiva", "MODES", , , ) //08 "_MODES" "Modalidade Esportiva"
self:oSchema:addProperty( "_NMCOM", "Nome Competição"     , "string", "Nome Competição"     , "NMCOM", , , ) //09 "_NMCOM" "Nome Competição"
self:oSchema:addProperty( "_CNPJM", "CNPJ Clube Mandante" , "string", "CNPJ Clube Mandante" , "CNPJM", , , ) //10 "_CNPJM" "CNPJ Clube Mandante"
self:oSchema:addProperty( "_CNPJV", "CNPJ Clube Visitante", "string", "CNPJ Clube Visitante", "CNPJV", , , ) //11 "_CNPJV" "CNPJ Clube Visitante"
self:oSchema:addProperty( "_NMVIS", "Nome Clube Visitante", "string", "Nome Clube Visitante", "NMVIS", , , ) //12 "_NMVIS" "Nome Clube Visitante"
self:oSchema:addProperty( "_PDESP", "Praça Desportiva"    , "string", "Praça Desportiva"    , "PDESP", , , ) //13 "_PDESP" "Praça Desportiva"
self:oSchema:addProperty( "_ESTEV", "Estado Evento"       , "string", "Estado Evento"       , "ESTEV", , , ) //14 "_ESTEV" "Estado Evento"
self:oSchema:addProperty( "_MUNEV", "Município Evento"    , "string", "Município Evento"    , "MUNEV", , , ) //15 "_MUNEV" "Municipio Evento"
self:oSchema:addProperty( "_QTPGT", "Qtd Pagantes"        , "number", "Qtd Pagantes"        , "QTPGT", , , ) //16 "_QTPGT" "Qtd Pagantes"
self:oSchema:addProperty( "_QTNPG", "Qtd Não Pagantes"    , "number", "Qtd Não Pagantes"    , "QTNPG", , , ) //17 "_QTNPG" "Qtd Não Pagantes"
self:oSchema:addProperty( "_TTREC", "Total Receita"       , "number", "Total Receita"       , "TTREC", , , ) //18 "_TTREC" "Total Receita"
self:oSchema:addProperty( "_TTCRP", "Total Contr. Prev."  , "number", "Total Contr. Prev."  , "TTCRP", , , ) //19 "_TTCRP" "Total Contr. Prev."
self:oSchema:addProperty( "_TTRCC", "Total Receita Clube" , "number", "Total Receita Clube" , "TTRCC", , , ) //20 "_TTRCC" "Total Receita Clube"
self:oSchema:addProperty( "_TTRET", "Total Retido"        , "number", "Total Retido"        , "TTRET", , , ) //21 "_TTRET" "Total Retido"
self:oSchema:addProperty( "_CPROC", "Número Processo Ref.", "string", "Numero Processo Ref.", "CPROC", , , ) //22 "_CPROC" "Numero Processo Ref."
self:oSchema:addProperty( "_VLSUS", "Valor Suspenso"      , "number", "Valor Suspenso"      , "VLSUS", , , ) //23 "_VLSUS" "Valor Suspenso"

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} execQuery

Execução da query com paginação.

@param oFilter, objeto, contém o filtro do SmartView
@param nStart, numérico, indica o inicío da paginação
@param nEnd, numérico, indica o fim da paginação
 
@return string

@author Denis Souza
@since 20/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method execQuery( oFilter as object, nStart as numeric, nEnd as numeric, cStartDate as character, cEndDate as character, aRootBranch as array, aInfEUF as array ) as character class ReinfBlock3010SmartViewBusinessObject

local cAlias   := ""  as character
local cQry     := ""  as character
local cCompC1G := ""  as character
local nlA      := 0   as numeric
local aBind    := {}  as array
local oPrepare          as object

default oFilter     := nil
default nStart      := 0
default nEnd        := 0
default cStartDate  := ""
default cEndDate    := ""
default aRootBranch := {}
default aInfEUF     := {}

cCompC1G := Upper(AllTrim(FWModeAccess("C1G",1)+FWModeAccess("C1G",2)+FWModeAccess("C1G",3)))

cQry += "SELECT DISTINCT T9F.T9F_FILIAL FILIAL,T9F.T9F_DTAPUR DTESP,T9F.T9F_VALCLU RECEITA,"
cQry += "CASE WHEN T9G.T9G_TPCOMP = ? THEN 'OFICIAL' ELSE 'NAO OFICIAL' END TIPO,"
cQry += "CASE WHEN T9G.T9G_CATEVE = ? THEN 'INTERNACIONAL' WHEN T9G.T9G_CATEVE = ? THEN 'INTERESTADUAL' WHEN T9G.T9G_CATEVE = ? THEN 'ESTADUAL'"
cQry += "WHEN T9G.T9G_CATEVE = ? THEN 'LOCAL' ELSE 'NAO INFORMADO' END CATEGORIA,"
cQry += "T9G.T9G_NUMBOL BOLETIM,T9G.T9G_MODDES MODALIDADE,T9G.T9G_NOMCOM COMPETICAO,T9G.T9G_CNPJMA CNPJMAND,T9G.T9G_CNPJVI CNPJVI,T9G.T9G_NOMVIS VISITANTE,"
cQry += "T9G.T9G_PRADES PRACA,T9G.T9G_PAGANT PAGANTES,T9G.T9G_NPAGAN NAOPAGANTES,"
cQry += "T9F.T9F_VALTOT TOTALBRUTO,T9F.T9F_VALPRE TRIBUTO,T9F.T9F_VALRET VALORRETIDO,"
cQry += "C07.C07_DESCRI CIDADE,C09.C09_DESCRI ESTADO,C1G.C1G_NUMPRO NUMPRO,T9J.T9J_VLRSUS VLRSUS"
aadd(aBind, "1")
aadd(aBind, "1")
aadd(aBind, "2")
aadd(aBind, "3")
aadd(aBind, "4")

cQry += "FROM " + RetSqlName("T9F") + " T9F "
cQry += "INNER JOIN " + RetSqlName("C1E") + " C1E ON " + FwJoinFilial("C1E","T9F") + " AND C1E.C1E_ATIVO IN (?) AND C1E.D_E_L_E_T_ = ? "
aadd(aBind, {' ','1'})
aadd(aBind, space(1))

cQry += "INNER JOIN " + RetSqlName("T9G") + " T9G ON " + FwJoinFilial("T9G","T9F") + " AND T9G.T9G_ID = T9F.T9F_ID AND T9G.D_E_L_E_T_ = ? "
aadd(aBind, space(1))

cQry += "INNER JOIN " + RetSqlName("T9H") + " T9H ON " + FwJoinFilial("T9H","T9G") + " AND T9H.T9H_ID = T9G.T9G_ID "
cQry += "AND T9H.T9H_NUMBOL = T9G.T9G_NUMBOL AND T9H.T9H_DTAPUR = T9G.T9G_DTAPUR AND T9H.D_E_L_E_T_ = ? "
cQry += "LEFT JOIN " + RetSqlName("T9J") + " T9J ON " + FwJoinFilial("T9J","T9F") + " AND T9J.T9J_ID = T9F.T9F_ID "
cQry += "AND T9J.T9J_DTAPUR = T9F.T9F_DTAPUR AND T9J.D_E_L_E_T_ = ? "
aadd(aBind, space(1))
aadd(aBind, space(1))

cQry += "LEFT JOIN " + RetSqlName("C1G") + " C1G ON " 
if cCompC1G == "CCC" .Or. (cCompC1G == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
    cQry += " C1G.C1G_FILIAL = ? "
    AADD(aBind,xFilial("C1G")) //C1G_FILIAL
else
    cQry += FwJoinFilial("C1G","T9J")
endif

cQry += " AND C1G.C1G_ID = T9J.T9J_IDPROC AND C1G.C1G_ATIVO = ? AND C1G.D_E_L_E_T_ = ? " 
aadd(aBind, "1")
aadd(aBind, space(1))

cQry += "INNER JOIN " + RetSqlName("C07") + " C07 ON C07.C07_FILIAL = ? "
aadd(aBind,xFilial("C07")) //C07_FILIAL

cQry += " AND C07.C07_ID = T9G.T9G_CODMUN AND C07.D_E_L_E_T_ = ? "
aadd(aBind, space(1))

cQry += "INNER JOIN " + RetSqlName("C09") + " C09 ON C09.C09_FILIAL = ? "
aadd(aBind,xFilial("C09")) //C09_FILIAL

cQry += " AND C09.C09_ID = T9G.T9G_UF AND C09.D_E_L_E_T_ = ? "
aadd(aBind, space(1))

cQry += " WHERE "

If "TMPFIL" $ aRootBranch[1]
    cQry += " T9F.T9F_FILIAL IN (" + aRootBranch[1] + ") "
Else
    cQry += " T9F.T9F_FILIAL IN ( ? ) "
    aadd(aBind,aRootBranch)
EndIf

if !Empty( cStartDate ) .And. !Empty( cEndDate )
    cQry += " AND T9F.T9F_DTAPUR BETWEEN ? AND ? "
    aadd(aBind,cStartDate)
    aadd(aBind,cEndDate)
endif

cQry += " AND T9F.D_E_L_E_T_ = ? "
aadd(aBind, space(1))

cQry += " ORDER BY FILIAL, DTESP, TIPO "

cQry := ChangeQuery(cQry)

oPrepare := FwExecStatement():New( cQry )
for nlA := 1 to len(aBind)
    if Valtype(aBind[nlA]) == 'A'
        oPrepare:setIn( nlA, aBind[nlA] )
    else
        oPrepare:setString( nlA, aBind[nlA] )
    endif
next nlA

cAlias := GetNextAlias()
oPrepare:OpenAlias( cAlias )

aSize(aBind,0)

    if oPrepare != Nil
      FreeObj(oPrepare)
      oPrepare  := Nil
    endif
    
Return cAlias
