#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.taf.reinfblock2050and2055.ch"

namespace totvs.protheus.fiscal.taf.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATAF", tables="C20,C30,C35,T9Q,C1G,T5L,LEM,T9E,C1H,V0X,V0W,V6B,V5S", name="ReinfBlock2050and2055", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*/{Protheus.doc} ReinfBlock2050and2055SmartViewBusinessObject
Classe para criação do Objeto de Negócio
@author Denis Souza
@since 05/07/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class ReinfBlock2050and2055SmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

public method new()        as object
public method getData()    as object
public method getSchema()  as object
private method execQuery() as character
private method indAq2055() as character
private method totalRet()

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Denis Souza
@since 05/07/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class ReinfBlock2050and2055SmartViewBusinessObject

_Super:new()
self:appendArea(STR0001)     //"TAF Fiscal Contábil"
self:setDisplayName(STR0002) //"Relatório REINF(R2050/R2055) Movimentos TAF x Retorno Governo"
self:setDescription(STR0003 + STR0004)//"Objeto ReinfBlock2050and2055 - Movimentos TAF x Retorno Governo"##" (C20,C30,C35,T9Q,C1G,T5L,LEM,T9E,C1H,V0X,V0W,V6B,V5S)"
if !::setPergunte("TAFTR00002")
    FwLogMsg(STR0005,,STR0006,,,,STR0007,,,) //"WARN"##"SmartView"##"Grupo de perguntas nao encontrado"
endif
self:setPageSize(10000)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Denis Souza
@since 05/07/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method getData( nPage as numeric, oFilter as object ) as object class ReinfBlock2050and2055SmartViewBusinessObject

local oFiltQry         := Nil as json
local oHashTot		   := Nil as object
local cMonthYear       := " " as character
local cYearMonth       := " " as character
local cReinfEvt        := " " as character
local cAlias           := " " as character
local cStartDate       := " " as character
local cEndDate         := " " as character
local cTaxNumberBranch := " " as character
local cEmssionDate     := " " as character
local cName            := " " as character
local cPartName        := " " as character
local cRoutine         := " " as character
local cTypeInsc        := " " as character
local cBranch          := " " as character
local cDocumentCode    := " " as character
local cSeries          := " " as character
local cDocumentNumber  := " " as character
local cItemNumber      := " " as character 
local cIndBusiness     := " " as character
local cProcess         := " " as character
local cINSSCodSus      := " " as character
local cGilRCodSus      := " " as character
local cSenarCodSus     := " " as character
local cIndRural        := " " as character
local cGpEpFil	       := " " as character
local cKey             := " " as character
local cProtocol        := " " as character
local cDbType          := " " as character
local cMV205XTRB       := " " as character
local cV5STpInsc       := " " as character
local cV5SNrInsc       := " " as character
local aCmpsC1E         := {}  as array
local aSM0             := {}  as array
local aTotalizer	   := {}  as array
local lColPAA          := .F. as logical
local lColNCPF         := .F. as logical
local lIPI             := .F. as logical
local lICMSST          := .F. as logical
local lICMSFreight     := .F. as logical
local nTamFil          := 0   as numeric
local nStart           := 0   as numeric
local nEnd             := 0   as numeric
local nGrossIncome     := 0   as numeric
local nVLINSS          := 0   as numeric
local nVLGilR          := 0   as numeric
local nVLSenar         := 0   as numeric
local nSuspINSS        := 0   as numeric
local nSuspGILR        := 0   as numeric
local nSuspSena        := 0   as numeric
local nGovINSS         := 0   as numeric
local nGovGILR         := 0   as numeric
local nGovSena         := 0   as numeric
local nC20DocNumSize   := 0   as numeric
local nC1GProcessSize  := 0   as numeric
local aInfEUF          := {}  as array
local oPrepare         := Nil as object
default nPage   := 1
default oFilter := Nil

nC20DocNumSize  := GetSx3Cache( 'C20_NUMDOC', 'X3_TAMANHO' )
nC1GProcessSize := GetSx3Cache( 'C1G_NUMPRO', 'X3_TAMANHO' )

aInfEUF := TAFTamEUF(Upper(AllTrim(SM0->M0_LEIAUTE)))

oFiltQry := oFilter:getParameters()
oHashTot := tHashMap():New()

cMonthYear := SubStr(AllTrim(oFiltQry['MV_PAR02',1]),5,2)+SubStr(AllTrim(oFiltQry['MV_PAR02',1]),1,4) //Mes+Ano

if (valtype(oFiltQry['MV_PAR01',1]) == "N" .And. oFiltQry['MV_PAR01',1] == 1) .Or. (valtype(oFiltQry['MV_PAR01',1]) == "C" .And. oFiltQry['MV_PAR01',1] == '1')
    cReinfEvt := "R-2050"
elseif (valtype(oFiltQry['MV_PAR01',1]) == "N" .And. oFiltQry['MV_PAR01',1] == 2) .Or. (valtype(oFiltQry['MV_PAR01',1]) == "C" .And. oFiltQry['MV_PAR01',1] == '2')
    cReinfEvt := "R-2055"
    lColPAA  := TAFColumnPos("C1E_PAA")
    lColNCPF := TAFColumnPos("C1E_NRCPF")
    nTamFil  := GetSx3Cache("C1E_FILTAF","X3_TAMANHO")
    dbSelectArea("C1E")
    C1E->(dbSetOrder(3))
endif

if !Empty(cReinfEvt)
    //Parâmetro criado para que o usuário informe se quer ou não que o tributo seja abatido do valor contábil
    if cReinfEvt == "R-2050"
        cMV205XTRB := SuperGetMV("MV_2050TRB",," ")
    elseif cReinfEvt == "R-2055"
        cMV205XTRB := SuperGetMV("MV_2055TRB",," ")
    endif
    lIPI	 := Iif( "1" $ cMV205XTRB, .T., .F. ) // 1 - IPI
    lICMSST	 := Iif( "2" $ cMV205XTRB, .T., .F. ) // 2 - ICMS ST
    If TAFColumnPos("C30_ICMSFT")
        lICMSFreight := Iif( "3" $ cMV205XTRB, .T., .F. ) // 3 - ICMS ST do frete
    EndIf

    self:totalRet(@oPrepare,cReinfEvt,cMonthYear,@oHashTot)
endif

cYearMonth := AllTrim(oFiltQry['MV_PAR02',1]) //Ano+Mes (AAAAMM)

nStart := ((nPage - 1) * self:getPageSize()) + 1 //Seta o primeiro item
nEnd   := nPage * self:getPageSize()             //Seta último da página atual

cStartDate := cYearMonth + "01"                      //20100101 ex: "20100101"
cEndDate   := DtoS( LastDay( StoD( cStartDate ) ) )  //20240101 ex: "20100131"

if !Empty(cReinfEvt)
    cDbType := Upper(Alltrim(TCGetDB()))
    cAlias := self:execQuery( @oPrepare, @oFilter, nStart, nEnd, cYearMonth, cStartDate, cEndDate, cReinfEvt, cDbType, aInfEUF )

    while !(cAlias)->(Eof())

        cEmssionDate := DtoC(Stod(StrTran(SubStr(FwTimeStamp(5,sToD((cAlias)->DTEMISSAONF),"00:00:00"),1,10 ), "-","")))

        aSM0 := FWSM0Util():GetSM0Data(cEmpAnt,(cAlias)->FIL,{"M0_FILIAL","M0_CGC"})
        if len(aSM0) >= 1
            cName := SubStr(Alltrim(aSM0[1][2]),1,30)
        endif

        if cReinfEvt == "R-2050"
            cTypeInsc := "1-CNPJ" //CNPJ
            if len(aSM0) >= 2
                cTaxNumberBranch := Alltrim(aSM0[2][2])
            endif
            nGovINSS := nGovGILR := nGovSena := 0
            cProtocol := "Não possui"
            aSize(aTotalizer,0)
            cKey := "1"+Alltrim(cTaxNumberBranch)
            If HMGet(oHashTot, cKey, @aTotalizer)
                if len(aTotalizer) >= 1 .and. len(aTotalizer[1]) == 5
                    cProtocol := aTotalizer[1][2]
                    nGovINSS  := aTotalizer[1][3]
                    nGovGILR  := aTotalizer[1][4]
                    nGovSena  := aTotalizer[1][5]
                endif
            EndIf
            if len(cTaxNumberBranch) == 14
                cTaxNumberBranch := Transform(cTaxNumberBranch,"@R! NN.NNN.NNN/NNNN-99")
            endif
            if (cAlias)->INDCOM == "1"
                cIndBusiness :=  "Comer.Prod. Rural PJ/Agroindústria Exceto PAA;"
            elseIf (cAlias)->INDCOM == "7"
                cIndBusiness :=  "Comer.Prod.Isen.Cont.Prev.Lei n°13.606/2018;"
            elseIf (cAlias)->INDCOM == "8"
                cIndBusiness :=  "Comer.Prod.Prog. Aquisição Alimentos PAA;"
            elseIf (cAlias)->INDCOM == "9"
                cIndBusiness :=  "Comer. Direta Prod. Mercado Externo"
            endif
        elseif cReinfEvt == "R-2055"
            if len(aSM0) >= 2
                cV5SNrInsc := Alltrim(aSM0[2][2])
                if len(cV5SNrInsc) == 14
                    cV5STpInsc := "1" //CNPJ
                else
                    cV5STpInsc := "2" //CPF
                endif
            endif
            If len(Alltrim((cAlias)->PARTI)) == 14
                cTypeInsc := "1 - CNPJ"
                cTaxNumberBranch := Transform(Alltrim((cAlias)->PARTI),"@R! NN.NNN.NNN/NNNN-99")
            else
                cTypeInsc := "2 - CPF"
                cTaxNumberBranch := Transform(Alltrim((cAlias)->PARTI),"@R 999.999.999-99")
            endif
            cPartName := SubStr(Alltrim((cAlias)->NOME),1,45)
            nGovINSS := nGovGILR := nGovSena := 0
            cProtocol := "Não possui"
            aSize(aTotalizer,0)
            if !Empty((cAlias)->CPFPR)
                cTypeInsc := "2 - CPF"
                cTaxNumberBranch := Transform(Alltrim((cAlias)->CPFPR),"@R 999.999.999-99")
                cKey := cV5STpInsc + cV5SNrInsc + SubStr(Alltrim(cTypeInsc),1,1)+Alltrim((cAlias)->CPFPR)
            else
                cKey := cV5STpInsc + cV5SNrInsc + SubStr(Alltrim(cTypeInsc),1,1)+Alltrim((cAlias)->PARTI)
            endif
            If HMGet(oHashTot, cKey, @aTotalizer)
                if len(aTotalizer) >= 1 .and. len(aTotalizer[1]) == 5
                    cProtocol := aTotalizer[1][2]
                    nGovINSS  := aTotalizer[1][3]
                    nGovGILR  := aTotalizer[1][4]
                    nGovSena  := aTotalizer[1][5]
                endif
            EndIf
            cIndBusiness := self:indAq2055(cAlias,cGpEpFil,@aCmpsC1E,lColPAA,lColNCPF,nTamFil)
            cIndRural    := iif(Alltrim((cAlias)->INDCP) == '2', "2 - Sobre a folha de pagamento", "1 - Sobre a comercializacao da sua producao")            
        endif

        cGpEpFil        := cEmpAnt+(cAlias)->FIL
        cRoutine        := (cAlias)->ROTINA
        cBranch         := (cAlias)->FIL
        cDocumentCode   := Alltrim((cAlias)->CHVNF)
        cSeries         := SubStr(Alltrim((cAlias)->SERIE),1,8)
        cDocumentNumber := SubStr(Alltrim((cAlias)->NUMDOC),1,nC20DocNumSize)
        cItemNumber     := Alltrim((cAlias)->NUMITE)
        nVLINSS         := (cAlias)->VLINSS
        nVLGilR         := (cAlias)->VLGILR
        nVLSenar        := (cAlias)->VLSENA
        cProcess        := SubStr(Alltrim((cAlias)->NUMPRO),1,nC1GProcessSize)
        cINSSCodSus     := Alltrim((cAlias)->CODSUSINSS)
        nSuspINSS       := (cAlias)->VLSUSINSS
        cGilRCodSus     := Alltrim((cAlias)->CODSUSGILR)
        nSuspGILR       := (cAlias)->VLSUSGILR
        cSenarCodSus    := Alltrim((cAlias)->CODSUSSEN)
        nSuspSena       := (cAlias)->VLSUSSENA
        cIndBusiness    := SubStr(Alltrim(cIndBusiness),1,50)
        nGrossIncome := (cAlias)->VLRRECBRUTA
        if lIPI
            nGrossIncome -= (cAlias)->IPI
        endif
        if lICMSST
            nGrossIncome -= (cAlias)->ICMSST
        endif
        If lICMSFreight
            nGrossIncome -= (cAlias)->ICMSFRT
        EndIf

        self:oData:appendData({ "_ROTINA"     : cRoutine        ,; //01
                                "_FIL"        : cBranch         ,; //02
                                "_RAZAOSOCIAL": cName           ,; //03
                                "_TPINSC"     : cTypeInsc       ,; //04
                                "_NRINSC"     : cTaxNumberBranch,; //05
                                "_CODDOC"     : cDocumentCode   ,; //06
                                "_SERDOC"     : cSeries         ,; //07
                                "_NUMDOC"     : cDocumentNumber ,; //08
                                "_NRITEM"     : cItemNumber     ,; //09
                                "_DTEMISSAONF": cEmssionDate    ,; //10
                                "_INDCOMERC"  : cIndBusiness    ,; //11
                                "_VLRECBRT"   : nGrossIncome    ,; //12
                                "_VLINSS"     : nVLINSS         ,; //13
                                "_VLGILR"     : nVLGilR         ,; //14
                                "_VLSENA"     : nVLSenar        ,; //15
                                "_NRPROC"     : cProcess        ,; //16
                                "_CDSUSINSS"  : cINSSCodSus     ,; //17
                                "_VLNRETINSS" : nSuspINSS       ,; //18
                                "_CDSUSGILRA" : cGilRCodSus     ,; //19
                                "_VLNRETGILRA": nSuspGILR       ,; //20
                                "_CDSUSSENAR" : cSenarCodSus    ,; //21
                                "_VLNRETSENAR": nSuspSena       ,; //22
                                "_INDRUR"     : cIndRural       ,; //23
                                "_PROTOCOL"   : cProtocol       ,; //24
                                "_TOTINSS"    : nGovINSS        ,; //25
                                "_TOTGILR"    : nGovGILR        ,; //26
                                "_TOTSENA"    : nGovSena        ,; //27
                                "_NOMEPAR"    : cPartName       }) //28

        (cAlias)->( dbSkip() )
    enddo
endif


(cAlias)->( DBCloseArea() )

freeobj(oHashTot)
freeObj(oPrepare)
oHashTot := Nil
aSize(aTotalizer,0)


return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Denis Souza
@since 05/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class ReinfBlock2050and2055SmartViewBusinessObject

self:oSchema:addProperty( "_ROTINA"     , "Rotina"                , "string", "Rotina"                  , "ROTINA"     ,,, )
self:oSchema:addProperty( "_FIL"        , "Filial"                , "string", "Filial"                  , "FIL"        ,,, )
self:oSchema:addProperty( "_RAZAOSOCIAL", "Razão Social"          , "string", "Razão Social"            , "RAZAOSOCIAL",,, )
self:oSchema:addProperty( "_TPINSC"     , "Tipo.Insc(Comerc/Adiq)", "string", "Tipo.Insc(Comerc/Adiq)"  , "TPINSC"     ,,, )
self:oSchema:addProperty( "_NRINSC"     , "Nr.Insc.(Comerc/Adiq)" , "string", "Nr.Insc(Comerc/Adiq)"    , "NRINSC"     ,,, )
self:oSchema:addProperty( "_CODDOC"     , "Cod.Documento"         , "string", "Cod.Documento"           , "CODDOC"     ,,, )
self:oSchema:addProperty( "_SERDOC"     , "Série Documento"       , "string", "Série Documento"         , "SERDOC"     ,,, )
self:oSchema:addProperty( "_NUMDOC"     , "Nr.Documento"          , "string", "Nr.Documento"            , "NUMDOC"     ,,, )
self:oSchema:addProperty( "_NRITEM"     , "Nr.Item"               , "string", "Nr.Item"                 , "NRITEM"     ,,, )
self:oSchema:addProperty( "_DTEMISSAONF", "Data Emissão"          , "string", "Data Emissão"            , "DTEMISSAONF",,, )
self:oSchema:addProperty( "_INDCOMERC"  , "Ind.Comerc/Aquisic."   , "string", "Ind.Comerc/Aquisic."     , "INDCOMERC"  ,,, )
self:oSchema:addProperty( "_VLRECBRT"   , "Vl.Receita Bruta"      , "number", "Vl.Receita Bruta"        , "VLRECBRT"   ,,, )
self:oSchema:addProperty( "_VLINSS"     , "Vl.INSS"               , "number", "Vl.INSS"                 , "VLINSS"     ,,, )
self:oSchema:addProperty( "_VLGILR"     , "Vl.GILRAT"             , "number", "Vl.GILRAT"               , "VLGILR"     ,,, )
self:oSchema:addProperty( "_VLSENA"     , "Vl.SENAR"              , "number", "Vl.SENAR"                , "VLSENA"     ,,, )
self:oSchema:addProperty( "_NRPROC"     , "Nr.Processo"           , "string", "Nr.Processo"             , "NRPROC"     ,,, )
self:oSchema:addProperty( "_CDSUSINSS"  , "Cod.Susp.INSS"         , "string", "Cod.Susp.INSS"           , "CDSUSINSS"  ,,, )
self:oSchema:addProperty( "_VLNRETINSS" , "Vl.N.Ret.INSS"         , "number", "Vl.N.Ret.INSS"           , "VLNRETINSS" ,,, )
self:oSchema:addProperty( "_CDSUSGILRA" , "Cod.Susp.GILRAT"       , "string", "Cod.Susp.GILRAT"         , "CDSUSGILRA" ,,, )
self:oSchema:addProperty( "_VLNRETGILRA", "Vl.N.Ret.GILRAT"       , "number", "Vl.N.Ret.GILRAT"         , "VLNRETGILRA",,, )
self:oSchema:addProperty( "_CDSUSSENAR" , "Cod.Susp.SENAR"        , "string", "Cod.Susp.SENAR"          , "CDSUSSENAR" ,,, )
self:oSchema:addProperty( "_VLNRETSENAR", "Vl.N.Ret.SENAR"        , "number", "Vl.N.Ret.SENAR"          , "VLNRETSENAR",,, )
self:oSchema:addProperty( "_INDRUR"     , "Ind.Rural"             , "string", "Ind.Rural"               , "INDRUR"     ,,, )
self:oSchema:addProperty( "_PROTOCOL"   , "Nr°Recibo"             , "string", "Nr°Recibo"               , "PROTOCOL"   ,,, )
self:oSchema:addProperty( "_TOTINSS"    , "Gov.Total INSS"        , "number", "Gov.Total INSS"          , "TOTINSS"    ,,, )
self:oSchema:addProperty( "_TOTGILR"    , "Gov.Total GILRAT"      , "number", "Gov.Total GILRAT"        , "TOTGILR"    ,,, )
self:oSchema:addProperty( "_TOTSENA"    , "Gov.Total SENAR"       , "number", "Gov.Total SENAR"         , "TOTSENA"    ,,, )
self:oSchema:addProperty( "_NOMEPAR"    , "Nome Participante"     , "string", "Nome Participante"       , "NOMEPAR"    ,,, )

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} execQuery

Execução da query com paginação.

@param oFilter, objeto, contém o filtro do SmartView
@param nStart, numérico, indica o inicío da paginação
@param nEnd, numérico, indica o fim da paginação
 
@return string

@author Denis Souza
@since 05/07/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method execQuery( oPrepare as object, oFilter as object, nStart as numeric, nEnd as numeric, cYearMonth as character, cStartDate as character, cEndDate as character, cReinfEvt as character, cDbType as character, aInfEUF as array ) as character class ReinfBlock2050and2055SmartViewBusinessObject

local nC30IdtSerSize := 0   as numeric
local nSrvMunSize    := 0   as numeric
local nCodSerSize    := 0   as numeric
local nTpRepaSize    := 0   as numeric
local nT9QIdSuspSize := 0   as numeric
local nT9EIdSuspSize := 0   as numeric
local nT5MIdtSerSize := 0   as numeric
local nT5MTpRepaSize := 0   as numeric
local nC0YCFOPSize   := 0   as numeric
local nI             := 0   as numeric
local nX             := 0   as numeric
local cIsNullSQL     := " " as character
local cConcatenate   := " " as character
local cSubtrSQL      := " " as character
local cCompC1G	 	 := " " as character
local cCompT5L 	     := " " as character
local cCompC1H       := " " as character
local cAlias         := " " as character
local cQry           := " " as character
local cSrvType       := " " as character
local cTopRow        := " " as character
local cSelect1       := " " as character
local cSelect2       := " " as character
local cSelect3       := " " as character
local cSelect4       := " " as character
local aBind          := {}  as array
local aCODSIT        := {}  as array
local aRootBranch    := {}  as array
local aBranch 	   := {}  as array

default oFilter     := Nil
Default oPrepare := Nil
default nStart      := 0
default nEnd        := 0
default cYearMonth  := " "
default cStartDate  := " "
default cEndDate    := " "
default cReinfEvt   := " "
default cDbType     := Upper(Alltrim(TCGetDB())) //ver xFunExpSql xFunFisTaf.prw
default aInfEUF     := {}

aCODSIT := {"000001","000002","000007","000008","000009"}

nC30IdtSerSize  := GetSx3Cache( 'C30_IDTSER', 'X3_TAMANHO' )
nSrvMunSize     := GetSx3Cache( 'C30_SRVMUN', 'X3_TAMANHO' )
nCodSerSize     := GetSx3Cache( 'C30_CODSER', 'X3_TAMANHO' )
nTpRepaSize     := GetSx3Cache( 'C30_TPREPA', 'X3_TAMANHO' )
nT9QIdSuspSize  := GetSx3Cache( 'T9Q_IDSUSP', 'X3_TAMANHO' )
nT9EIdSuspSize  := GetSx3Cache( 'T9E_IDSUSP', 'X3_TAMANHO' )
nT5MIdtSerSize  := GetSx3Cache( 'T5M_IDTSER', 'X3_TAMANHO' )
nT5MTpRepaSize  := GetSx3Cache( 'T5M_TPREPA', 'X3_TAMANHO' )
nC0YCFOPSize    := GetSx3Cache( 'C0Y_CODIGO', 'X3_TAMANHO' )

cSrvType     := TcSrvType()
cConcatenate := iif(cDbType $ "ORACLE,POSTGRES,DB2,INFORMIX","||","+")
cIsNullSQL   := iif(cDbType $ "INFORMIX|ORACLE","NVL",iif(cDbType $ "DB2|POSTGRES" .Or. (cDbType == "DB2/400" .And. Upper(cSrvType) == "ISERIES"),"COALESCE","ISNULL"))
cSubtrSQL    := iif(cDbType $ "ORACLE|POSTGRES|DB2","SUBSTR","SUBSTRING")
cTopRow      := iif(cDbType $ "ORACLE"," AND ROWNUM <= 1 ",iif(cDbType $ "POSTGRES"," LIMIT 1 ",iif(cDbType $ "DB2|"," FETCH FIRST 1 ROWS ONLY "," ")))

cCompC1G     := Upper(AllTrim(FWModeAccess("C1G",1)+FWModeAccess("C1G",2)+FWModeAccess("C1G",3)))
cCompT5L     := Upper(AllTrim(FWModeAccess("T5L",1)+FWModeAccess("T5L",2)+FWModeAccess("T5L",3)))
cCompC1H     := Upper(AllTrim(FWModeAccess("C1H",1)+FWModeAccess("C1H",2)+FWModeAccess("C1H",3)))

cSelect1 := " C1G.C1G_NUMPRO "
cSelect2 := " T9Q.T9Q_NUMPRO "
cSelect3 := " C1G.C1G_NUMPRO "
cSelect4 := " T9E.T9E_NUMPRO "

cSelect1 := iif( cDbType $ "INFORMIX|"," FIRST 1 " + cSelect1, iif( cDbType $ "MSSQL|MSSQL7"," TOP 1 " + cSelect1, cSelect1 ) )
cSelect2 := iif( cDbType $ "INFORMIX|"," FIRST 1 " + cSelect2, iif( cDbType $ "MSSQL|MSSQL7"," TOP 1 " + cSelect2, cSelect2 ) )
cSelect3 := iif( cDbType $ "INFORMIX|"," FIRST 1 " + cSelect3, iif( cDbType $ "MSSQL|MSSQL7"," TOP 1 " + cSelect3, cSelect3 ) )
cSelect4 := iif( cDbType $ "INFORMIX|"," FIRST 1 " + cSelect4, iif( cDbType $ "MSSQL|MSSQL7"," TOP 1 " + cSelect4, cSelect4 ) )

aBranch := wsLoadFil()
For nX := 1 to len(aBranch)
    aadd(aRootBranch, aBranch[nX][3] )
Next nX

cQry += " SELECT DISTINCT 'NF' ROTINA " //01 ROTINA
cQry += " ,C20.C20_FILIAL FIL "          //02 FIL
cQry += " ,C20.C20_CHVNF CHVNF "         //03 CHVNF
cQry += " ,C20.C20_DTDOC DTEMISSAONF "   //04 DTEMISSAONF
cQry += " ,C20.C20_SERIE SERIE "         //05 SERIE
cQry += " ,C20.C20_NUMDOC NUMDOC "       //06 NUMDOC
cQry += " ,C30.C30_VLOPER VLRRECBRUTA "  //07 VLRRECBRUTA
cQry += " ,C30.C30_NUMITE NUMITE "       //08 NUMITE
cQry += " ,(SELECT SUM(C35.C35_VALOR) FROM " + RetSqlName("C35") + " C35 WHERE " + FwJoinFilial("C35","C30") + " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE AND C35.D_E_L_E_T_ = ? AND C35.C35_CODTRI = ? ) VLINSS " //09
cQry += " ,(SELECT SUM(C35.C35_VALOR) FROM " + RetSqlName("C35") + " C35 WHERE " + FwJoinFilial("C35","C30") + " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE AND C35.D_E_L_E_T_ = ? AND C35.C35_CODTRI = ? ) VLGILR " //10
cQry += " ,(SELECT SUM(C35.C35_VALOR) FROM " + RetSqlName("C35") + " C35 WHERE " + FwJoinFilial("C35","C30") + " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE AND C35.D_E_L_E_T_ = ? AND C35.C35_CODTRI = ? ) VLSENA " //11
cQry += " ,(SELECT SUM(T9Q.T9Q_VALSUS) FROM " + RetSqlName("T9Q") + " T9Q WHERE " + FwJoinFilial("T9Q","C30") + " AND T9Q.T9Q_CHVNF = C30.C30_CHVNF AND T9Q.T9Q_NUMITE = C30.C30_NUMITE AND T9Q.T9Q_ID = C30.C30_CODITE AND T9Q.D_E_L_E_T_ = ? AND T9Q.T9Q_CODTRI = ? ) VLSUSINSS " //12
cQry += " ,(SELECT SUM(T9Q.T9Q_VALSUS) FROM " + RetSqlName("T9Q") + " T9Q WHERE " + FwJoinFilial("T9Q","C30") + " AND T9Q.T9Q_CHVNF = C30.C30_CHVNF AND T9Q.T9Q_NUMITE = C30.C30_NUMITE AND T9Q.T9Q_ID = C30.C30_CODITE AND T9Q.D_E_L_E_T_ = ? AND T9Q.T9Q_CODTRI = ? ) VLSUSGILR " //13
cQry += " ,(SELECT SUM(T9Q.T9Q_VALSUS) FROM " + RetSqlName("T9Q") + " T9Q WHERE " + FwJoinFilial("T9Q","C30") + " AND T9Q.T9Q_CHVNF = C30.C30_CHVNF AND T9Q.T9Q_NUMITE = C30.C30_NUMITE AND T9Q.T9Q_ID = C30.C30_CODITE AND T9Q.D_E_L_E_T_ = ? AND T9Q.T9Q_CODTRI = ? ) VLSUSSENA " //14
AADD(aBind, Space(1))  
AADD(aBind, '000013')  
AADD(aBind, Space(1))  
AADD(aBind, '000024')  
AADD(aBind, Space(1))  
AADD(aBind, '000025')  
AADD(aBind, Space(1))  
AADD(aBind, '000013') 
AADD(aBind, Space(1))  
AADD(aBind, '000024')  
AADD(aBind, Space(1))  
AADD(aBind, '000025')   


cQry += " ,(SELECT " + cSelect1 + " FROM " + RetSqlName("C1G") + " C1G WHERE "
if cCompC1G == "CCC" .Or. (cCompC1G == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
    cQry += " C1G.C1G_FILIAL = ? "
    AADD(aBind,xFilial("C1G")) //C1G_FILIAL
else
    cQry += FwJoinFilial("C1G","C20")
endif

cQry += " AND C1G.C1G_ID = "
cQry += " (SELECT " + cSelect2 + " FROM " + RetSqlName("T9Q") + " T9Q WHERE " + FwJoinFilial("T9Q","C30") + " AND T9Q.T9Q_CHVNF = C30.C30_CHVNF AND T9Q.T9Q_NUMITE = C30.C30_NUMITE AND T9Q.T9Q_ID = C30.C30_CODITE "

cQry += " AND T9Q.D_E_L_E_T_ = ? AND T9Q.T9Q_CODTRI IN (?) " + cTopRow + ") AND C1G.D_E_L_E_T_ = ? " + cTopRow + ") NUMPRO " //15
AADD(aBind, Space(1))  
AADD(aBind, {'000013','000024','000025'})  
AADD(aBind, Space(1))  

cSelect1 := " T5L.T5L_CODSUS "
cSelect2 := " T9Q.T9Q_IDSUSP "

cSelect1 := iif( cDbType $ "INFORMIX|"," FIRST 1 " + cSelect1, iif( cDbType $ "MSSQL|MSSQL7"," TOP 1 " + cSelect1, cSelect1 ) )
cSelect2 := iif( cDbType $ "INFORMIX|"," FIRST 1 " + cSelect2, iif( cDbType $ "MSSQL|MSSQL7"," TOP 1 " + cSelect2, cSelect2 ) )

cQry += " ,(SELECT " + cSelect1 + " FROM " + RetSqlName("T5L") + " T5L WHERE "

if cCompT5L == "CCC" .Or. (cCompT5L == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
    cQry += " T5L.T5L_FILIAL = ? "
    AADD(aBind,xFilial("T5L")) //T5L_FILIAL
else
    cQry += FwJoinFilial("T5L","C20")
endif


if "POSTGRES" $ cDbType
    cQry += " AND " + cSubtrSQL + "(T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS,1," + cValToChar(nT9QIdSuspSize) + ") = "
elseif "INFORMIX" $ cDbType //ver ExtFisxTaf.prw function RegT003
    cQry += " AND (T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS)[1]["+ cValToChar(nT9QIdSuspSize) +"] = "
else
    cQry += " AND T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS = "
endif

cQry += " (SELECT " + cSelect2 + " FROM " + RetSqlName("T9Q") + " T9Q WHERE " + FwJoinFilial("T9Q","C30") + " AND T9Q.T9Q_CHVNF = C30.C30_CHVNF AND T9Q.T9Q_NUMITE = C30.C30_NUMITE AND T9Q.T9Q_ID = C30.C30_CODITE AND T9Q.D_E_L_E_T_ = ? AND T9Q.T9Q_CODTRI = ? " + cTopRow + ") "
AADD(aBind, Space(1))  
AADD(aBind, '000013')  
  

cQry += " AND T5L.D_E_L_E_T_ = ? " + cTopRow + ") CODSUSINSS " //16 CODSUSINSS
AADD(aBind, Space(1)) 

cQry += " ,(SELECT " + cSelect1 + " FROM " + RetSqlName("T5L") + " T5L WHERE "

if cCompT5L == "CCC" .Or. (cCompT5L == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
    cQry += " T5L.T5L_FILIAL = ? "
    AADD(aBind,xFilial("T5L")) //T5L_FILIAL
else
    cQry += FwJoinFilial("T5L","C20")
endif


if "POSTGRES" $ cDbType
    cQry += " AND " + cSubtrSQL + "(T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS,1," + cValToChar(nT9QIdSuspSize) + ") = "
elseif "INFORMIX" $ cDbType //ver ExtFisxTaf.prw function RegT003
    cQry += " AND (T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS)[1]["+ cValToChar(nT9QIdSuspSize) +"] = "
else
    cQry += " AND T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS = "
endif

cQry += " (SELECT " + cSelect2 + " FROM " + RetSqlName("T9Q") + " T9Q WHERE " + FwJoinFilial("T9Q","C30") + " AND T9Q.T9Q_CHVNF = C30.C30_CHVNF AND T9Q.T9Q_NUMITE = C30.C30_NUMITE AND T9Q.T9Q_ID = C30.C30_CODITE AND T9Q.D_E_L_E_T_ = ? AND T9Q.T9Q_CODTRI = ? " + cTopRow + ") "
AADD(aBind, Space(1)) 
AADD(aBind, '000024') 

cQry += " AND T5L.D_E_L_E_T_ = ? " + cTopRow + ") CODSUSGILR " //17 CODSUSGILR
AADD(aBind, Space(1)) 

cQry += " ,(SELECT " + cSelect1 + " FROM " + RetSqlName("T5L") + " T5L WHERE "
if cCompT5L == "CCC" .Or. (cCompT5L == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
    cQry += " T5L.T5L_FILIAL = ? "
    AADD(aBind,xFilial("T5L")) //T5L_FILIAL
else
    cQry += FwJoinFilial("T5L","C20")
endif


if "POSTGRES" $ cDbType
    cQry += " AND " + cSubtrSQL + "(T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS,1," + cValToChar(nT9QIdSuspSize) + ") = "
elseif "INFORMIX" $ cDbType //ver ExtFisxTaf.prw function RegT003
    cQry += " AND (T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS)[1]["+ cValToChar(nT9QIdSuspSize) +"] = "
else
    cQry += " AND T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS = "
endif

cQry += " (SELECT " + cSelect2 + " FROM " + RetSqlName("T9Q") + " T9Q WHERE " + FwJoinFilial("T9Q","C30") + " AND T9Q.T9Q_CHVNF = C30.C30_CHVNF AND T9Q.T9Q_NUMITE = C30.C30_NUMITE AND T9Q.T9Q_ID = C30.C30_CODITE AND T9Q.D_E_L_E_T_ = ? AND T9Q.T9Q_CODTRI = ? " + cTopRow + ") "
AADD(aBind, Space(1)) 
AADD(aBind, '000025') 

cQry += " AND T5L.D_E_L_E_T_ = ? " + cTopRow + ") CODSUSSEN " //18 CODSUSSEN
AADD(aBind, Space(1)) 


if "INFORMIX" $ cDbType
    cQry += " ,CASE WHEN C1H.C1H_PAA = ? THEN '8' WHEN C1H.C1H_PAA <> ? AND C0Y.C0Y_CODIGO[1][1] = ? THEN '9' WHEN C1H.C1H_PAA <> ? AND C0Y.C0Y_CODIGO[1][1] <> ? THEN '1' END INDCOM " //19
    AADD(aBind, '1') 
    AADD(aBind, '1') 
    AADD(aBind, '7')
    AADD(aBind, '1') 
    AADD(aBind, '7')
else
    cQry += " ,CASE WHEN C1H.C1H_PAA = ? THEN '8' WHEN C1H.C1H_PAA <> ? AND " + cSubtrSQL + "(C0Y.C0Y_CODIGO,1,1) = ? THEN '9' WHEN C1H.C1H_PAA <> ? AND " + cSubtrSQL + "(C0Y.C0Y_CODIGO,1,1) <> ? THEN '1' END INDCOM " //19
    AADD(aBind, '1') 
    AADD(aBind, '1') 
    AADD(aBind, '7')
    AADD(aBind, '1') 
    AADD(aBind, '7')

endif

if cReinfEvt == "R-2055"
    cQry += " ,C1H.C1H_NOME NOME "      //20 NOME
    cQry += " ,C1H.C1H_INDCP INDCP "    //21 INDCP
    cQry += " ,C1H.C1H_CPF CPFPR "      //22 CPFPR
    cQry += " ,C1H.C1H_CNPJ CNPJPR "    //23 CNPJPR
    cQry += " ,CASE WHEN C1H.C1H_CPF <> ? THEN C1H.C1H_CPF ELSE C1H.C1H_CNPJ END PARTI " //24 PARTI
    cQry += " ,C30.C30_INDISE INDISE "  //25 INDISE
    cQry += " ,C0Y.C0Y_CODIGO CFOP "    //26 CFOP
    AADD(aBind, Space(1)) 
endif

cQry += " ,C30.C30_ICMSFT ICMSFRT "

cQry += " ,(SELECT SUM(C35.C35_VALOR) FROM " + RetSqlName("C35") + " C35 WHERE " + FwJoinFilial("C35","C30") + " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE AND C35.D_E_L_E_T_ = ? AND C35.C35_CODTRI = ? ) ICMSST "
AADD(aBind, Space(1)) 
AADD(aBind, '000004') 

cQry += " ,(SELECT SUM(C35.C35_VALOR) FROM " + RetSqlName("C35") + " C35 WHERE " + FwJoinFilial("C35","C30") + " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE AND C35.D_E_L_E_T_ = ? AND C35.C35_CODTRI = ? ) IPI "
AADD(aBind, Space(1)) 
AADD(aBind, '000005') 

cQry += " FROM " + RetSqlName("C20") + " C20 "

cQry += " INNER JOIN " + RetSqlName("C30") + " C30 ON " + FwJoinFilial("C30","C20") + " AND C30.C30_CHVNF = C20.C20_CHVNF AND C30.D_E_L_E_T_ = ? "
AADD(aBind, Space(1)) 

if cReinfEvt == "R-2050"
    cQry += " AND C30.C30_IDTSER = ? AND C30.C30_SRVMUN = ? AND C30.C30_CODSER = ? AND C30.C30_TPREPA = ? "
    AADD(aBind, Padr(" ",nC30IdtSerSize)) 
    AADD(aBind, Padr(" ",nSrvMunSize)) 
    AADD(aBind, Padr(" ",nCodSerSize)) 
    AADD(aBind, Padr(" ",nTpRepaSize)) 
endif


cQry += " INNER JOIN " + RetSqlName("C1H") + " C1H ON " 

if cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
    cQry += " C1H.C1H_FILIAL = ? "
    AADD(aBind,xFilial("C1H")) //C1H_FILIAL
else
    cQry += FwJoinFilial("C1H","C20")
endif

cQry += " AND C1H.C1H_ID = C20.C20_CODPAR AND C1H.C1H_INDDES <> ? AND C1H.D_E_L_E_T_ = ? "
AADD(aBind, '1') 
AADD(aBind, Space(1)) 


if cReinfEvt == "R-2055"
    cQry += " AND C1H.C1H_RAMO = ? "
    AADD(aBind, '4') 
endif

cQry += " INNER JOIN " + RetSqlName("C0Y") + " C0Y ON C0Y.C0Y_FILIAL = ? AND C0Y.C0Y_ID = C30.C30_CFOP AND C0Y.D_E_L_E_T_ = ? "
AADD(aBind, xFilial("C0Y"))
AADD(aBind, Space(1)) 


cQry += " WHERE C20.C20_FILIAL IN (?) AND C20.C20_CODSIT IN (?) "
AADD(aBind ,aRootBranch) 
AADD(aBind ,aCODSIT) 


if cReinfEvt == "R-2050"
    cQry += " AND C20.C20_INDOPE = ? "
    AADD(aBind, '1') 
elseif cReinfEvt == "R-2055"
    cQry += " AND C20.C20_INDOPE = ? AND C20.C20_TPDOC <> ? "
    cQry += " AND NOT EXISTS ( SELECT C30.C30_CHVNF FROM " + RetSqlName("C30") + " C30 WHERE " + FwJoinFilial("C30","C20") + " AND C30.C30_CHVNF = C20.C20_CHVNF "
    cQry += " AND ( C30.C30_IDTSER <> ? OR C30.C30_SRVMUN <> ? OR C30.C30_CODSER <> ? OR C30.C30_TPREPA <> ? ) "
    cQry += " AND C30.D_E_L_E_T_ = ? ) "
    AADD(aBind, '0')
    AADD(aBind, '000002') 
    AADD(aBind, Padr(" ",nC30IdtSerSize))
    AADD(aBind, Padr(" ",nSrvMunSize))
    AADD(aBind, Padr(" ",nCodSerSize))
    AADD(aBind, Padr(" ",nTpRepaSize))
    AADD(aBind, Space(1)) 
endif

cQry += " AND C20.C20_DTDOC BETWEEN ? AND ? AND C20.D_E_L_E_T_ = ? "
AADD(aBind, cStartDate)
AADD(aBind, cEndDate)
AADD(aBind, Space(1)) 

if cReinfEvt == "R-2055"
    cQry += " UNION ALL "

    cQry += " SELECT 'FAT' ROTINA "         //01 ROTINA
    cQry += " ,LEM.LEM_FILIAL FIL "         //02 FIL
    cQry += " ,LEM.LEM_ID CHVNF "           //03 CHVNF
    cQry += " ,LEM.LEM_DTEMIS DTEMISSAONF " //04 DTEMISSAONF
    cQry += " ,LEM.LEM_PREFIX SERIE "       //05 SERIE
    cQry += " ,LEM.LEM_NUMERO NUMDOC "      //06 NUMDOC
    cQry += " ,LEM.LEM_VLBRUT VLRRECBRUTA " //07 VLRRECBRUTA
    cQry += " ,' ' NUMITE "                 //08 NUMITE
    cQry += " ,LEM.LEM_VLRCP VLINSS "       //09 VLINSS
    cQry += " ,LEM.LEM_VLRGIL VLGILR "      //10 VLGILR
    cQry += " ,LEM.LEM_VLRSEN VLSENA "      //11 VLSENA

    cQry += " ,(SELECT SUM(T9E.T9E_VALSUS) FROM " + RetSqlName("T9E") + " T9E WHERE " + FwJoinFilial("T9E","LEM") + " AND T9E.T9E_ID = LEM.LEM_ID AND T9E.D_E_L_E_T_ = ? AND T9E.T9E_CODTRI = ?) VLSUSINSS " //12 VLSUSINSS
    cQry += " ,(SELECT SUM(T9E.T9E_VALSUS) FROM " + RetSqlName("T9E") + " T9E WHERE " + FwJoinFilial("T9E","LEM") + " AND T9E.T9E_ID = LEM.LEM_ID AND T9E.D_E_L_E_T_ = ? AND T9E.T9E_CODTRI = ?) VLSUSGILR " //13 VLSUSGILR
    cQry += " ,(SELECT SUM(T9E.T9E_VALSUS) FROM " + RetSqlName("T9E") + " T9E WHERE " + FwJoinFilial("T9E","LEM") + " AND T9E.T9E_ID = LEM.LEM_ID AND T9E.D_E_L_E_T_ = ? AND T9E.T9E_CODTRI = ?) VLSUSSENA " //14 VLSUSSENA
    AADD(aBind, Space(1)) 
    AADD(aBind, '000013') 
    AADD(aBind, Space(1)) 
    AADD(aBind, '000024') 
    AADD(aBind, Space(1)) 
    AADD(aBind, '000025') 


    cQry += " ,(SELECT " + cSelect3 + " FROM " + RetSqlName("C1G") + " C1G WHERE "
    if cCompC1G == "CCC" .Or. (cCompC1G == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
       cQry += " C1G.C1G_FILIAL = ? "
       AADD(aBind, xFilial("C1G")) //C1G_FILIAL
    else
       cQry += FwJoinFilial("C1G","LEM")
    endif

    cQry += " AND C1G.C1G_ID = "

    cQry += " (SELECT " + cSelect4 + " FROM " + RetSqlName("T9E") + " T9E WHERE " + FwJoinFilial("T9E","LEM") + " AND T9E.T9E_ID = LEM.LEM_ID AND T9E.D_E_L_E_T_ = ? AND T9E.T9E_CODTRI IN (?) " + cTopRow + ") "
    AADD(aBind, Space(1))
    AADD(aBind, {'000013','000024','000025'})

    cQry += " AND C1G.D_E_L_E_T_ = ? " + cTopRow + ") NUMPRO " //15 NUMPRO
    AADD(aBind, Space(1))

    cSelect3 := " T5L.T5L_CODSUS "
    cSelect4 := " T9E.T9E_IDSUSP "

    cSelect3 := iif( cDbType $ "INFORMIX|"," FIRST 1 " + cSelect3, iif( cDbType $ "MSSQL|MSSQL7"," TOP 1 " + cSelect3, cSelect3 ) )
    cSelect4 := iif( cDbType $ "INFORMIX|"," FIRST 1 " + cSelect4, iif( cDbType $ "MSSQL|MSSQL7"," TOP 1 " + cSelect4, cSelect4 ) )

    cQry += " ,(SELECT " + cSelect3 + " FROM " + RetSqlName("T5L") + " T5L WHERE "
    if cCompT5L == "CCC" .Or. (cCompT5L == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
       cQry += " T5L.T5L_FILIAL = ? "
       AADD(aBind,xFilial("T5L")) //T5L_FILIAL
    else
       cQry += FwJoinFilial("T5L","LEM")
    endif

    if "POSTGRES" $ cDbType
        cQry += " AND " + cSubtrSQL + "(T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS,1," + cValToChar(nT9EIdSuspSize) + ") = "
    elseif "INFORMIX" $ cDbType //ver ExtFisxTaf.prw function RegT003
        cQry += " AND (T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS)[1]["+ cValToChar(nT9EIdSuspSize) +"] = "
    else
        cQry += " AND T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS = "
    endif  

    cQry += " (SELECT " + cSelect4 + " FROM " + RetSqlName("T9E") + " T9E WHERE " + FwJoinFilial("T9E","LEM") + " AND T9E.T9E_ID = LEM.LEM_ID AND T9E.D_E_L_E_T_ = ? AND T9E.T9E_CODTRI = ? ) "
    AADD(aBind, Space(1)) 
    AADD(aBind, '000013')

    cQry += " AND T5L.D_E_L_E_T_ = ? ) CODSUSSINSS " //16 CODSUSSINSS
    AADD(aBind, Space(1)) 

    cQry += " ,(SELECT " + cSelect3 + " FROM " + RetSqlName("T5L") + " T5L WHERE "
    if cCompT5L == "CCC" .Or. (cCompT5L == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
       cQry += " T5L.T5L_FILIAL = ? "
       AADD(aBind,xFilial("T5L")) //T5L_FILIAL
    else
       cQry += FwJoinFilial("T5L","LEM")
    endif

    if "POSTGRES" $ cDbType
        cQry += " AND " + cSubtrSQL + "(T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS,1," + cValToChar(nT9EIdSuspSize) + ") = "
    elseif "INFORMIX" $ cDbType //ver ExtFisxTaf.prw function RegT003
        cQry += " AND (T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS)[1]["+ cValToChar(nT9EIdSuspSize) +"] = "
    else
        cQry += " AND T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS = "
    endif    

    cQry += " (SELECT " + cSelect4 + " FROM " + RetSqlName("T9E") + " T9E WHERE " + FwJoinFilial("T9E","LEM") + " AND T9E.T9E_ID = LEM.LEM_ID AND T9E.D_E_L_E_T_ = ? AND T9E.T9E_CODTRI = ? ) "
    AADD(aBind, Space(1)) 
    AADD(aBind, '000024')


    cQry += " AND T5L.D_E_L_E_T_ = ? ) CODSUSGILR " //17 CODSUSGILR
    AADD(aBind, Space(1))


    cQry += " ,(SELECT " + cSelect3 + " FROM " + RetSqlName("T5L") + " T5L WHERE "
    if cCompT5L == "CCC" .Or. (cCompT5L == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
       cQry += " T5L.T5L_FILIAL = ? "
       AADD(aBind,xFilial("T5L")) //T5L_FILIAL
    else
       cQry += FwJoinFilial("T5L","LEM")
    endif

    if "POSTGRES" $ cDbType
        cQry += " AND " + cSubtrSQL + "(T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS,1," + cValToChar(nT9EIdSuspSize) + ") = "
    elseif "INFORMIX" $ cDbType //ver ExtFisxTaf.prw function RegT003
        cQry += " AND (T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS)[1]["+ cValToChar(nT9EIdSuspSize) +"] = "
    else
        cQry += " AND T5L.T5L_ID" + cConcatenate + "T5L.T5L_VERSAO" + cConcatenate + "T5L.T5L_CODSUS = "
    endif    


    cQry += " (SELECT " + cSelect4 + " FROM " + RetSqlName("T9E") + " T9E WHERE " + FwJoinFilial("T9E","LEM") + " AND T9E.T9E_ID = LEM.LEM_ID AND T9E.D_E_L_E_T_ = ? AND T9E.T9E_CODTRI = ?) "
    AADD(aBind, Space(1)) 
    AADD(aBind, '000025')

    cQry += " AND T5L.D_E_L_E_T_ = ? ) CODSUSSEN " //18 CODSUSSEN
    AADD(aBind, Space(1)) 

    cQry += " ,' ' INDCOM "                 //19 INDCOM
    cQry += " ,C1H.C1H_NOME NOME "          //20 NOME
    cQry += " ,C1H.C1H_INDCP INDCP "        //21 INDCP
    cQry += " ,C1H.C1H_CPF CPFPR "          //22 CPFPR
    cQry += " ,C1H.C1H_CNPJ CNPJPR "        //23 CNPJPR
    cQry += " ,CASE WHEN C1H.C1H_CPF <> ? THEN C1H.C1H_CPF ELSE C1H.C1H_CNPJ END PARTI " //24 PARTI
    cQry += " ,LEM_INDISE INDISE "          //25 INDISE
    cQry += " ,'"+Padr(" ", nC0YCFOPSize) +"' CFOP " //26 CFOP
    cQry += " ,0 ICMSFRT " //nao realiza abatimento na fatura, funcoes GetICMSFRT e GetValTRB consideram apenas C30 e C35
    cQry += " ,0 ICMSST "
    cQry += " ,0 IPI "
    AADD(aBind, Space(1)) 

    cQry += " FROM " + RetSqlName("LEM") + " LEM "

    cQry += " INNER JOIN " + RetSqlName("C1H") + " C1H ON "
    if cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
       cQry += " C1H.C1H_FILIAL = ? "
       AADD(aBind,xFilial("C1H")) //C1H_FILIAL
    else
       cQry += FwJoinFilial("C1H","LEM")
    endif

    cQry += " AND C1H.C1H_ID = LEM.LEM_IDPART AND C1H.C1H_INDDES <> ? AND C1H.C1H_RAMO = ? AND C1H.D_E_L_E_T_ = ? "
    AADD(aBind, '1') 
    AADD(aBind, '4') 
    AADD(aBind, Space(1)) 

    cQry += " WHERE LEM.LEM_FILIAL IN (?) AND LEM.LEM_NATTIT = ? "
    AADD(aBind ,aRootBranch) 
    AADD(aBind, '0') 

	cQry += " AND LEM.LEM_DOCORI = ? AND LEM.LEM_DTEMIS BETWEEN ? AND ? AND ( LEM.LEM_VLRGIL > ? OR LEM.LEM_VLRSEN > ? OR LEM.LEM_VLRCP > ? ) AND LEM.D_E_L_E_T_ = ? "
    AADD(aBind, Space(1)) 
    AADD(aBind, cStartDate) 
    AADD(aBind, cEndDate) 
    AADD(aBind, '0')
    AADD(aBind, '0')
    AADD(aBind, '0')
    AADD(aBind, Space(1)) 

    // Tratamento feito para retornar registros da tabela T5M em que os campos T5M_IDTSER e T5M_TPREPA sejam diferentes de branco.
	// Caso seja retornado algum registro, essa fatura não deverá estar disponível para apuração no evento R-2055 e sim R-2010.
    cQry += " AND NOT EXISTS ( SELECT T5M.T5M_NUMFAT FROM " + RetSqlName("T5M") + " T5M WHERE " + FwJoinFilial("T5M","LEM") + " AND T5M.T5M_ID = LEM.LEM_ID AND T5M.T5M_IDPART = LEM.LEM_IDPART AND T5M.T5M_NUMFAT = LEM.LEM_NUMERO "
    cQry += " AND ( T5M.T5M_IDTSER <> ? OR T5M.T5M_TPREPA <> ? AND T5M.D_E_L_E_T_ = ? ) ) "
    AADD(aBind, Padr(" ", nT5MIdtSerSize))
    AADD(aBind, Padr(" ", nT5MTpRepaSize))
    AADD(aBind, Space(1)) 
endif

//Importante devido o controle dentro do while a ordenacao deve permanecer na seguinte ordem.: 02 FIL + 05 SERIE + 06 NUMDOC + 08 NUMITE
cQry += " ORDER BY 2,5,6,8 " //filial+Serie+NumDoc+NumIte

cQry := ChangeQuery(cQry)


oPrepare := FwExecStatement():New( cQry )
for nI := 1 to len(aBind)
    if Valtype(aBind[nI]) == 'A'
        oPrepare:setIn( nI, aBind[nI] )
    else
        oPrepare:setString( nI, aBind[nI] )
    endif
next nI

cAlias := GetNextAlias()
oPrepare:OpenAlias( cAlias )

Return cAlias

//-------------------------------------------------------------------
/*{Protheus.doc} indAq2055

Retorna o indicicativo de comercializacao

@return string

@author Denis Souza
@since 15/05/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method indAq2055(cAlias as character, cGpEpFil as character, aCmpsC1E as array, lColPAA as logical, lColNCPF as logical, nTamFil as numeric) as character class ReinfBlock2050and2055SmartViewBusinessObject

local nPos 	    := 0   as numeric
local cPAA 	    := " " as character
local cRet 	    := " " as character
local cIndAquis := " " as character
local cTpInsc   := " " as character
local cInsc	    := " " as character
local aAreaC1E  := {}  as array
local aAreaSM0  := {}  as array
local lFind     := .F. as logical

nPos := aScan( aCmpsC1E, {|x| Upper(AllTrim(x[3])) == cGpEpFil } ) //Cache para Tp e Nr Insc, senao executaria seek na C1E ou posicione na SM0 a cada registro
if nPos <= 0
    aAreaC1E := GetArea()
    If C1E->(MSSeek(XFilial("C1E")+PadR((cAlias)->FIL,nTamFil)+"1")) //dbSetOrder(3)
        If lColNCPF .And. !Empty(C1E->C1E_NRCPF)
            cTpInsc := "3"
            cInsc := C1E->C1E_NRCPF
        Else
            cTpInsc := "1"
            cInsc := Posicione("SM0", 1, cGpEpFil, "M0_CGC")
        EndIf
    EndIf
    RestArea(aAreaC1E)
    Aadd(aCmpsC1E,{cTpInsc,cInsc,cGpEpFil,cPAA})
    If cTpInsc == '1' //CNPJ
        ASize(aAreaC1E,0); aAreaC1E := Nil
        aAreaSM0 := SM0->(GetArea())
        aAreaC1E := C1E->(GetArea())
        dbSelectArea("SM0")
        SM0->(dbGoTop())
        While !SM0->(Eof()) .And. !lFind //Posiciona na SM0 com a raiz do CNPJ do estabelecimento
            If !Empty(cInsc)
                If AllTrim(SM0->M0_CGC) == AllTrim(cInsc)
                    lFind := .T.
                    cPAA := Posicione("C1E", 3, XFilial("C1E")+PadR(SM0->M0_CODFIL, nTamFil)+"1", "C1E_PAA" )
                EndIf
            EndIf
            SM0->(DbSkip())
        EndDo
        RestArea(aAreaSM0)
        RestArea(aAreaC1E)
    ElseIf cTpInsc == '3' //CAEPF
        ASize(aAreaC1E,0); aAreaC1E := Nil
        aAreaC1E := C1E->(GetArea())
        dbSelectArea("C1E")
        C1E->(dbGoTop())
        While !C1E->(Eof()) .And. !lFind
            If !Empty(cInsc)
                If SubStr(AllTrim(C1E->C1E_NRCPF), 1, 9) == SubStr(AllTrim(cInsc),1,9)
                    lFind := .T.
                    cPAA := C1E->C1E_PAA
                EndIf
            EndIf
            C1E->(DbSkip())
        EndDo
        RestArea(aAreaC1E)
    EndIf
endif
cIndAquis := ""
If Empty((cAlias)->CPFPR) .And. !Empty((cAlias)->CNPJPR) //Pessoa jurídica
    If lColPAA
        If cPAA == "1" .And. (cAlias)->INDISE <> "1"
            cIndAquis := "3"
        Else
            If cPAA == "1" .And. (cAlias)->INDISE == "1"  
                cIndAquis := "6"
            EndIf
        EndIf
    Else
        If cPAA == "1" .And. (cAlias)->INDISE == "1" 
            cIndAquis := "6"	
        EndIf
    EndIf
    If cPAA <> "1"
        cIndAquis := "3"
    EndIf
Else //Produtor pessoa física
    If cPAA == "1" .And. (cAlias)->INDISE <> "1"
        cIndAquis := "2"
    ElseIf cPAA != "1" .And. (cAlias)->INDISE <> "1"
        cIndAquis := "1"
    ElseIf cPAA <> "1" .And. (cAlias)->INDISE == "1" 
        cIndAquis := "4"
    ElseIf cPAA == "1" .And. (cAlias)->INDISE == "1"
        cIndAquis := "5"		 				
    EndIf
    If (cAlias)->CFOP == "1501" .or. (cAlias)->CFOP == "2501"
        cIndAquis := "7"
    EndIf
EndIf
if cIndAquis == "1"
    cRet :=  "Aquisic. Prod. Produtor Rural P.Física ou Seg. Especial Geral"
elseIf cIndAquis == "2"
    cRet :=  "Aquisic. Prod. Produtor Rural P.Física ou Seg. Especial Entidade PAA"
elseIf cIndAquis == "3"
    cRet :=  "Aquisic. Prod. Produtor Rural P.Jurídica Entidade PAA"
elseif cIndAquis == "4"
    cRet :=  "Aquisic. Prod. Produtor Rural P.Física ou Seg. Especial Geral - Prod. Isenta (Lei 13.606/2018)"
elseIf cIndAquis == "5"
    cRet :=  "Aquisic. Prod. Produtor Rural P.Física ou Seg."
elseIf cIndAquis == "6"
    cRet :=  "Aquisic. Prod. Produtor Rural P.Jurídica Entidade PAA - Prod. Isenta (Lei 13.606/2018)"
elseIf cIndAquis == "7"
    cRet :=  "Aquisic. Prod. Produtor Rural P.Física ou Seg. Especial Fins Exportação"
Else
    cRet := ""
endIf
ASize(aAreaC1E,0); aAreaC1E := Nil
ASize(aAreaSM0,0); aAreaSM0 := Nil

Return cRet

//-------------------------------------------------------------------
/*{Protheus.doc} totalRet

total retorno governo

@return null

@author Denis Souza
@since 12/07/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method totalRet(oPrepare as object, cReinfEvt as character, cMonthYear as character, oHashTot as object) class ReinfBlock2050and2055SmartViewBusinessObject

local cAliasQry := " " as character
local cQuery    := " " as character
local aTot      := {}  as array
local aBranch   := {}  as array
local aRootBranch  := {}  as array
local aBind        := {}  as array
local nX           := 0   as numeric
local nlA          := 0   as numeric

default cReinfEvt   := ""
default oHashTot    := Nil
default cMonthYear  := ""
Default oPrepare := Nil

aBranch := wsLoadFil()
For nX := 1 to len(aBranch)
    aadd(aRootBranch, aBranch[nX][3] )
Next nX

if cReinfEvt == "R-2050"
    cQuery := " SELECT V0W.V0W_FILIAL, V0W.V0W_ID, V0W.V0W_VERSAO, V0W.V0W_NRRECB, V0W.V0W_TPINSE, V0W.V0W_NRINSE "    
    cQuery += " ,(SELECT SUM(V0X.V0X_VLCOML) FROM " + RetSqlName("V0X") + " V0X WHERE " + FwJoinFilial("V0X","V0W") + " AND V0X.V0X_ID = V0W.V0W_ID AND V0X.V0X_VERSAO = V0W.V0W_VERSAO "
    cQuery += " AND V0X.D_E_L_E_T_ = ? AND V0X.V0X_CRCOML BETWEEN ? AND ? ) TOTINSS " //final 01 ou 02
    cQuery += " ,(SELECT SUM(V0X.V0X_VLCOML) FROM " + RetSqlName("V0X") + " V0X WHERE " + FwJoinFilial("V0X","V0W") + " AND V0X.V0X_ID = V0W.V0W_ID AND V0X.V0X_VERSAO = V0W.V0W_VERSAO "
    cQuery += " AND V0X.D_E_L_E_T_ = ? AND V0X.V0X_CRCOML BETWEEN ? AND ? ) TOTGILR " //final 05 ou 06
    cQuery += " ,(SELECT SUM(V0X.V0X_VLCOML) FROM " + RetSqlName("V0X") + " V0X WHERE " + FwJoinFilial("V0X","V0W") + " AND V0X.V0X_ID = V0W.V0W_ID AND V0X.V0X_VERSAO = V0W.V0W_VERSAO "
    cQuery += " AND V0X.D_E_L_E_T_ = ? AND V0X.V0X_CRCOML BETWEEN ? AND ? ) TOTSENA " //final 02 ou 04
    cQuery += " FROM " + RetSqlName("V0W") + " V0W "
    cQuery += " WHERE V0W.V0W_FILIAL IN (?) AND V0W.V0W_ATIVO = ? AND V0W.V0W_CODRET = ? AND V0W.V0W_TPEVEN = ? AND V0W.D_E_L_E_T_ = ? AND V0W.V0W_PERAPU = ? " //MMAAAA
    cQuery += " GROUP BY V0W.V0W_FILIAL, V0W.V0W_ID, V0W.V0W_VERSAO, V0W.V0W_NRRECB, V0W.V0W_TPINSE, V0W.V0W_NRINSE "
    AADD(aBind, Space(1)) 
    AADD(aBind, '165701') 
    AADD(aBind, '165799') 
    AADD(aBind, Space(1)) 
    AADD(aBind, '164601') 
    AADD(aBind, '164699')
    AADD(aBind, Space(1)) 
    AADD(aBind, '121301') 
    AADD(aBind, '121399') 
    AADD(aBind, aRootBranch)
    AADD(aBind, '1') 
    AADD(aBind, '0') 
    AADD(aBind, '2050')
    AADD(aBind, Space(1)) 
    AADD(aBind, cMonthYear) 

elseif cReinfEvt == "R-2055"
    cQuery := " SELECT TAB1.V5S_FILIAL, TAB1.V5S_TPINSC, TAB1.V5S_NRINSC, TAB1.V5S_TPINSP, TAB1.V5S_NRINSP, TAB1.V5S_PROTUL, TAB1.V5S_XMLID "
    cQuery += " ,( SELECT SUM(V6B.V6B_VLRCRA) FROM " + RetSqlName("V6B") + " V6B WHERE (V6B.V6B_FILIAL||V6B.V6B_ID||V6B.V6B_VERSAO) = TAB1.FLIDVS AND V6B.V6B_CRAQUI BETWEEN ? AND ? AND V6B.D_E_L_E_T_ = ? ) TOTINSS "
    cQuery += " ,( SELECT SUM(V6B.V6B_VLRCRA) FROM " + RetSqlName("V6B") + " V6B WHERE (V6B.V6B_FILIAL||V6B.V6B_ID||V6B.V6B_VERSAO) = TAB1.FLIDVS AND V6B.V6B_CRAQUI BETWEEN ? AND ? AND V6B.D_E_L_E_T_ = ? ) TOTGILR "
    cQuery += " ,( SELECT SUM(V6B.V6B_VLRCRA) FROM " + RetSqlName("V6B") + " V6B WHERE (V6B.V6B_FILIAL||V6B.V6B_ID||V6B.V6B_VERSAO) = TAB1.FLIDVS AND V6B.V6B_CRAQUI BETWEEN ? AND ? AND V6B.D_E_L_E_T_ = ? ) TOTSENA "
    cQuery += " FROM ( "    
    cQuery += " SELECT V5S.V5S_FILIAL,V5S.V5S_TPINSC,V5S.V5S_NRINSC,V5S.V5S_TPINSP,V5S.V5S_NRINSP,V5S.V5S_PROTUL,V5S.V5S_XMLID "
    cQuery += " ,( SELECT V0W.V0W_FILIAL||V0W.V0W_ID||V0W.V0W_VERSAO FROM " + RetSqlName("V0W") + " V0W WHERE " + FwJoinFilial("V0W","V5S")    
    cQuery += " AND V0W.V0W_IDEVEN = V5S.V5S_XMLID AND V0W.V0W_PERAPU = V5S.V5S_PERAPU AND V0W.V0W_NRRECB = V5S.V5S_PROTUL "
    cQuery += " AND V0W_TPEVEN = ? AND V0W.V0W_ATIVO = ? AND V0W.V0W_CODRET = ? AND V0W.D_E_L_E_T_ = ? ) FLIDVS "    
    cQuery += " FROM " + RetSqlName("V5S") + " V5S "
    cQuery += " WHERE V5S.V5S_FILIAL IN (?) AND V5S.V5S_STATUS = ? AND V5S.V5S_PROTUL <> ? AND V5S.V5S_PERAPU = ? AND V5S.V5S_ATIVO = ? AND V5S.D_E_L_E_T_ = ? "
    cQuery += " ) TAB1 GROUP BY TAB1.V5S_FILIAL, TAB1.V5S_TPINSC, TAB1.V5S_NRINSC, TAB1.V5S_TPINSP, TAB1.V5S_NRINSP, TAB1.V5S_PROTUL, TAB1.V5S_XMLID, TAB1.FLIDVS "
    AADD(aBind, '165601')
    AADD(aBind, '165699')
    AADD(aBind, Space(1))
    AADD(aBind, '164601')
    AADD(aBind, '164699')
    AADD(aBind, Space(1))
    AADD(aBind, '121301')
    AADD(aBind, '121399')
    AADD(aBind, Space(1))
    AADD(aBind, '2055')
    AADD(aBind, '1')
    AADD(aBind, '0')
    AADD(aBind, Space(1)) 
    AADD(aBind, aRootBranch)
    AADD(aBind, '4')
    AADD(aBind, Space(1)) 
    AADD(aBind, cMonthYear) 
    AADD(aBind, '1')
    AADD(aBind, Space(1)) 
endif

cQuery := ChangeQuery(cQuery)

oPrepare := FwExecStatement():New( cQuery )
for nlA := 1 to len(aBind)
    if Valtype(aBind[nlA]) == 'A'
        oPrepare:setIn( nlA, aBind[nlA] )
    else
        oPrepare:setString( nlA, aBind[nlA] )
    endif
next nlA 

cAliasQry := GetNextAlias()

oPrepare:OpenAlias( cAliasQry )

if cReinfEvt == "R-2050"
    while (cAliasQry)->(!Eof())
        AAdd(aTot,{(Alltrim((cAliasQry)->V0W_TPINSE) + Alltrim((cAliasQry)->V0W_NRINSE)),;
        Alltrim((cAliasQry)->V0W_NRRECB),(cAliasQry)->TOTINSS,(cAliasQry)->TOTGILR,(cAliasQry)->TOTSENA})
        (cAliasQry)->(DbSkip())
    enddo
elseif cReinfEvt == "R-2055"
    while (cAliasQry)->(!Eof())
        AAdd(aTot,{(Alltrim((cAliasQry)->V5S_TPINSC) + Alltrim((cAliasQry)->V5S_NRINSC)) + Alltrim((cAliasQry)->V5S_TPINSP) + Alltrim((cAliasQry)->V5S_NRINSP),;
        Alltrim((cAliasQry)->V5S_PROTUL),(cAliasQry)->TOTINSS,(cAliasQry)->TOTGILR,(cAliasQry)->TOTSENA})
        (cAliasQry)->(DbSkip())
    enddo
endif
(cAliasQry)->(DbCloseArea())

oHashTot := AToHM(aTot)

freeObj(oPrepare)

Return Nil
