#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.taf.reinfblock2010and2020.ch"

namespace totvs.protheus.fiscal.taf.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATAF", tables="C20,C30,C35,C8C,T9C,C1H,LEM,T5M,V0W", name="ReinfBlock2010and2020", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*/{Protheus.doc} ReinfBlock2010and2020SmartViewBusinessObject
Classe para criação do Objeto de Negócio

@author Denis Souza
@since 18/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class ReinfBlock2010and2020SmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

public method new()         as object
public method getData()     as object
public method getSchema()   as object
private method execQuery()  as character

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Denis Souza
@since 18/05/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class ReinfBlock2010and2020SmartViewBusinessObject

_Super:new()
self:appendArea(STR0001)        //"TAF Fiscal Contábil"
self:setDisplayName(STR0002)    //"Relatório REINF(R2010/R2020) Movimentos TAF x Retorno Governo"
self:setDescription(STR0003 + STR0004) //"Objeto ReinfBlock2010and2020 - Movimentos TAF x Retorno Governo"##" (C20,C30,C35,C8C,T9C,C1H,LEM,T5M,V0W)"
if !::setPergunte("TAFTR00001") 
    FwLogMsg(STR0005,,STR0006,,,,STR0007,,,)//"WARN"##"SmartView"##"Grupo de perguntas nao encontrado"
endif
self:setPageSize(5000)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Denis Souza
@since 18/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method getData( nPage as numeric, oFilter as object ) as object class ReinfBlock2010and2020SmartViewBusinessObject

local oFiltQry          := Nil as json
local cYearMonth        := " " as character
local cReinfEvt         := " " as character
local cAlias            := " " as character
local cStartDate        := " " as character
local cEndDate          := " " as character
Local cIssuanceDate     := " " as character
local cTaxNumberBranch  := " " as character
local cReceiptNumber    := " " as character
local cDescServ         := " " as character
local cCprb             := " " as character
local cTypepInsc	    := " " as character
local cIndConstruction  := " " as character
local cDescConstruction := " " as character
local cObservation      := " " as character
local cCNPJCNO          := " " as character
local cName             := " " as character
local cCorporateName    := " " as character
local cPartipantCode    := " " as character
local aSM0              := {}  as array
local nStart            := 0   as numeric
local nEnd              := 0   as numeric
local aBind             := {}  as array
local oPrepare          := Nil as object
//incluido comentario para geração de novo pacote para cliente piloto Nutrii Liffe
default nPage    := 1
default oFilter  := Nil

oFiltQry := oFilter:getParameters()

if (valtype(oFiltQry['MV_PAR01',1]) == "N" .And. oFiltQry['MV_PAR01',1] == 1) .Or. (valtype(oFiltQry['MV_PAR01',1]) == "C" .And. oFiltQry['MV_PAR01',1] == '1')
    cReinfEvt  := "R-2010"
elseif (valtype(oFiltQry['MV_PAR01',1]) == "N" .And. oFiltQry['MV_PAR01',1] == 2) .Or. (valtype(oFiltQry['MV_PAR01',1]) == "C" .And. oFiltQry['MV_PAR01',1] == '2')
    cReinfEvt  := "R-2020"
endif

cYearMonth  := AllTrim(oFiltQry['MV_PAR02',1]) //Ano+Mes (AAAAMM)

nStart := ((nPage - 1) * self:getPageSize()) + 1 //Seta o primeiro item
nEnd   := nPage * self:getPageSize()             //Seta último da página atual

cStartDate := cYearMonth + "01"                   //20100101 ex: "20100101"
cEndDate := DtoS( LastDay( StoD( cStartDate ) ) ) //20240101 ex: "20100131"

if !Empty(cReinfEvt)

    cAlias := self:execQuery(@oPrepare, @oFilter, @aBind, nStart, nEnd, cYearMonth, cStartDate, cEndDate, cReinfEvt )

    while !(cAlias)->(Eof())

        cIssuanceDate := StrTran( SubStr( FwTimeStamp(5,sToD((cAlias)->DTEMISSAONF) ,"00:00:00"),1,10 ) , "-", "")

        aSM0 := FWSM0Util():GetSM0Data( cEmpAnt , (cAlias)->FIL , { "M0_FILIAL", "M0_CGC" } )
        cCorporateName := SubStr(Alltrim(aSM0[1][2]),1,30)
        cTaxNumberBranch := Alltrim(aSM0[2][2])
        if len(cTaxNumberBranch) == 14
            cTaxNumberBranch := Transform(cTaxNumberBranch,"@R! NN.NNN.NNN/NNNN-99")
        endif

        cReceiptNumber := iif(Empty((cAlias)->NRRECB),"Não Possui",Alltrim((cAlias)->NRRECB))

        cDescServ := SubStr(Alltrim((cAlias)->C8CDESCRI),1,25)

        cCprb := Iif((cAlias)->CPRB $ "0|' '", "Não Contrib. 11%", "Contrib. 3,5%")

        cTypepInsc := ' '

        cName := SubStr(Alltrim((cAlias)->C1HNOME),1,30)

        cPartipantCode := SubStr(Alltrim((cAlias)->C1HCODPAR),1,25)

        //Caso seja uma obra apresenta o CNO informado no NRINSC (conforme renderizacao no painel reinf po ui)
        If !Empty((cAlias)->NRINSC)	.And. AllTrim(Upper((cAlias)->NRINSC)) <> "SEM CODIGO"
            cTypepInsc := iif( Alltrim((cAlias)->TPINSC) == '1' , '1 - CNPJ' , iif( Alltrim((cAlias)->TPINSC) == '4', '4 - CNO' ,' ') ) //TPINSC=T9C.T9C_TPINSC 1=CNPJ;4=CNO
            cCNPJCNO := Alltrim((cAlias)->NRINSC)
            if len(cCNPJCNO) == 12 
                cCNPJCNO := Transform(cCNPJCNO,"@R 99.999.99999/99") //02.043.11375/74
            endif
            cIndConstruction  := iif((cAlias)->INDOBRA == '0','0 - Não é obra',iif((cAlias)->INDOBRA == '1','1 - Obra - Empreit.Total','2 - Obra - Empreit.Parcial'))
            cDescConstruction := SubStr(Alltrim((cAlias)->DSCOBRA),1,30)
            cObservation      := SubStr(Alltrim((cAlias)->OBS),1,20)
        Else
            cTypepInsc := '1 - CNPJ'
            cCNPJCNO   := Alltrim((cAlias)->CNPJ)
            if len(cCNPJCNO) == 14
                cCNPJCNO := Transform(cCNPJCNO,"@R! NN.NNN.NNN/NNNN-99")
            endif
            cIndConstruction  := '0 - Não é obra'
            cDescConstruction := ''
            cObservation      := ''
        EndIf

        self:oData:appendData({ "_ROTINA" : (cAlias)->ROTINA ,; //01 Rotina
            "_FIL"          : (cAlias)->FIL                  ,; //02 Filial
            "_CGC"          : cTaxNumberBranch               ,; //03 Nr.Inscri.Contrib
            "_NUMDOCTO"     : Alltrim((cAlias)->NUMDOCTO)    ,; //04 Nr.Documento
            "_SERIE"        : Alltrim((cAlias)->SERIE)       ,; //05 Série
            "_DTEMISSAONF"  : DtoC(Stod(cIssuanceDate))      ,; //06 Dt.Emissão
            "_VLRBRUTO"     : (cAlias)->VLRBRUTO             ,; //07 Vlr.Bruto.Doc
            "_OBS"          : cObservation                   ,; //08 Obs
            "_TPINSC"       : cTypepInsc                     ,; //09 Tp.Inscrição
            "_NRINSC"       : Alltrim((cAlias)->NRINSC)      ,; //10 Nr.Inscrição
            "_IDOBRA"       : (cAlias)->IDOBRA               ,; //11 Id.Obra
            "_DSCOBRA"      : cDescConstruction              ,; //12 Descrição.Obra
            "_INDOBRA"      : cIndConstruction               ,; //13 Ind.Obra
            "_CNPJ"         : cCNPJCNO                       ,; //14 CNPJ.Part.
            "_CODOBR"       : Alltrim((cAlias)->CODOBR)      ,; //15 Cod.Obra
            "_VLRBASERET"   : (cAlias)->VLRBASERET           ,; //16 Vlr Base Ret
            "_VLRRETENCAO"  : (cAlias)->VLRRETENCAO          ,; //17 Vlr Retenção Tributo
            "_VLRRETSUB"    : (cAlias)->VLRRETSUB            ,; //18 Vlr Ret Sub
            "_VLRNRETPRINC" : (cAlias)->VLRNRETPRINC         ,; //19 Vlr N Ret Princ
            "_VLR15SERVICOS": (cAlias)->VLR15SERVICOS        ,; //20 Vlr 15 Serv
            "_VLR20SERVICOS": (cAlias)->VLR20SERVICOS        ,; //21 Vlr 20 Serv
            "_VLR25SERVICOS": (cAlias)->VLR25SERVICOS        ,; //22 Vlr 25 Serv
            "_VLRADICIONAL" : (cAlias)->VLRADICIONAL         ,; //23 Vlr Adicional
            "_VLRNRETADIC"  : (cAlias)->VLRNRETADIC          ,; //24 Vlr N Ret Adic
            "_ALIQ"         : (cAlias)->ALIQ                 ,; //25 Alíquota
            "_CPRB"         : cCprb                          ,; //26 CPRB
            "_C1HCODPAR"    : cPartipantCode                 ,; //27 Cod Part.
            "_C1HNOME"      : cName                          ,; //28 Nome Part.
            "_CODSERVIC"    : Alltrim((cAlias)->CODSERVIC)   ,; //29 Cod Serviço
            "_C8CDESCRI"    : cDescServ                      ,; //30 Descrição Serviço
            "_NRRECB"       : cReceiptNumber                 ,; //31 Nr.Recibo
            "_RETBRUT"      : (cAlias)->RETBRUT              ,; //32 Retorno Vlr Bruto
            "_RETTRIB"      : (cAlias)->RETTRIB              ,; //33 Retorno Vlr Trib
            "_RAZAOSOCIAL"  : cCorporateName                 }) //34 Razão Social

        (cAlias)->( dbSkip() )
    enddo
    
    (cAlias)->( DBCloseArea() )

endif

aSize(aBind,0)
freeObj(oPrepare)


return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Denis Souza
@since 18/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method getSchema() as object class ReinfBlock2010and2020SmartViewBusinessObject

//self:oSchema:addProperty(Nome TAG       , Descricao             , Tipo    , Display               , cRealName
self:oSchema:addProperty( "_ROTINA"       , "Rotina"              , "string", "Rotina"              , "ROTINA"        ,,, ) //01 (cAlias)->ROTINA
self:oSchema:addProperty( "_FIL"          , "Filial"              , "string", "Filial"              , "FIL"           ,,, ) //02 (cAlias)->FIL
self:oSchema:addProperty( "_CGC"          , "Nr.Inscri.Contrib"   , "string", "Nr.Inscri.Contrib"   , "CGC"           ,,, ) //03 cTaxNumberBranch
self:oSchema:addProperty( "_NUMDOCTO"     , "Nr.Documento"        , "string", "Nr.Documento"        , "NUMDOCTO"      ,,, ) //04 Alltrim((cAlias)->NUMDOCTO)
self:oSchema:addProperty( "_SERIE"        , "Série"               , "string", "Série"               , "SERIE"         ,,, ) //05 Alltrim((cAlias)->SERIE)
self:oSchema:addProperty( "_DTEMISSAONF"  , "Dt.Emissão"          , "string", "Dt.Emissão"          , "DTEMISSAONF"   ,,, ) //06 DtoC(Stod(cIssuanceDate))
self:oSchema:addProperty( "_VLRBRUTO"     , "Vlr.Bruto.Doc"       , "number", "Vlr.Bruto.Doc"       , "VLRBRUTO"      ,,, ) //07 (cAlias)->VLRBRUTO
self:oSchema:addProperty( "_OBS"          , "Obs"                 , "string", "Obs"                 , "OBS"           ,,, ) //08 cObservation
self:oSchema:addProperty( "_TPINSC"       , "Tp.Inscrição"        , "string", "Tp.Inscrição"        , "TPINSC"        ,,, ) //09 cTypepInsc
self:oSchema:addProperty( "_NRINSC"       , "Nr.Inscrição"        , "string", "Nr.Inscrição"        , "NRINSC"        ,,, ) //10 Alltrim((cAlias)->NRINSC)
self:oSchema:addProperty( "_IDOBRA"       , "Id.Obra"             , "string", "Id.Obra"             , "IDOBRA"        ,,, ) //11 (cAlias)->IDOBRA
self:oSchema:addProperty( "_DSCOBRA"      , "Descrição.Obra"      , "string", "Descrição.Obra"      , "DSCOBRA"       ,,, ) //12 cDescConstruction
self:oSchema:addProperty( "_INDOBRA"      , "Ind.Obra"            , "string", "Ind.Obra"            , "INDOBRA"       ,,, ) //13 cIndConstruction
self:oSchema:addProperty( "_CNPJ"         , "CNPJ.Part."          , "string", "CNPJ.Part."          , "CNPJ"          ,,, ) //14 cCNPJCNO
self:oSchema:addProperty( "_CODOBR"       , "Cod.Obra"            , "string", "Cod.Obra"            , "CODOBR"        ,,, ) //15 (cAlias)->CODOBR
self:oSchema:addProperty( "_VLRBASERET"   , "Vlr Base Ret"        , "number", "Vlr Base Ret"        , "VLRBASERET"    ,,, ) //16 (cAlias)->VLRBASERET
self:oSchema:addProperty( "_VLRRETENCAO"  , "Vlr Retenção Tributo", "number", "Vlr Retenção Tributo", "VLRRETENCAO"   ,,, ) //17 (cAlias)->VLRRETENCAO
self:oSchema:addProperty( "_VLRRETSUB"    , "Vlr Ret Sub"         , "number", "Vlr Ret Sub"         , "VLRRETSUB"     ,,, ) //18 (cAlias)->VLRRETSUB
self:oSchema:addProperty( "_VLRNRETPRINC" , "Vlr N Ret Princ"     , "number", "Vlr N Ret Princ"     , "VLRNRETPRINC"  ,,, ) //19 (cAlias)->VLRNRETPRINC
self:oSchema:addProperty( "_VLR15SERVICOS", "Vlr 15 Serv"         , "number", "Vlr 15 Serv"         , "VLR15SERVICOS" ,,, ) //20 (cAlias)->VLR15SERVICOS
self:oSchema:addProperty( "_VLR20SERVICOS", "Vlr 20 Serv"         , "number", "Vlr 20 Serv"         , "VLR20SERVICOS" ,,, ) //21 (cAlias)->VLR20SERVICOS
self:oSchema:addProperty( "_VLR25SERVICOS", "Vlr 25 Serv"         , "number", "Vlr 25 Serv"         , "VLR25SERVICOS" ,,, ) //22 (cAlias)->VLR25SERVICOS
self:oSchema:addProperty( "_VLRADICIONAL" , "Vlr Adicional"       , "number", "Vlr Adicional"       , "VLRADICIONAL"  ,,, ) //23 (cAlias)->VLRADICIONAL
self:oSchema:addProperty( "_VLRNRETADIC"  , "Vlr N Ret Adic"      , "number", "Vlr N Ret Adic"      , "VLRNRETADIC"   ,,, ) //24 (cAlias)->VLRNRETADIC
self:oSchema:addProperty( "_ALIQ"         , "Alíquota"            , "number", "Alíquota"            , "ALIQ"          ,,, ) //25 (cAlias)->ALIQ
self:oSchema:addProperty( "_CPRB"         , "CPRB"                , "string", "CPRB"                , "CPRB"          ,,, ) //26 cCprb
self:oSchema:addProperty( "_C1HCODPAR"    , "Cod Part."           , "string", "Cod Part."           , "C1HCODPAR"     ,,, ) //27 (cAlias)->C1HCODPAR
self:oSchema:addProperty( "_C1HNOME"      , "Nome Part."          , "string", "Nome Part."          , "C1HNOME"       ,,, ) //28 cName
self:oSchema:addProperty( "_CODSERVIC"    , "Cod Serviço"         , "string", "Cod Serviço"         , "CODSERVIC"     ,,, ) //29 (cAlias)->CODSERVIC
self:oSchema:addProperty( "_C8CDESCRI"    , "Descrição Serviço"   , "string", "Descrição Serviço"   , "C8CDESCRI"     ,,, ) //30 cDescServ
self:oSchema:addProperty( "_NRRECB"       , "Nr.Recibo"           , "string", "Nr.Recibo"           , "NRRECB"        ,,, ) //31 cReceiptNumber
self:oSchema:addProperty( "_RETBRUT"      , "Retorno Vlr Bruto"   , "number", "Retorno Vlr Bruto"   , "RETBRUT"       ,,, ) //32 (cAlias)->RETBRUT
self:oSchema:addProperty( "_RETTRIB"      , "Retorno Vlr Trib"    , "number", "Retorno Vlr Trib"    , "RETTRIB"       ,,, ) //33 (cAlias)->RETTRIB
self:oSchema:addProperty( "_RAZAOSOCIAL"  , "Razão Social"        , "string", "Razão Social"        , "RAZAOSOCIAL"   ,,, )

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} execQuery

Execução da query com paginação.

@param oFilter, objeto, contém o filtro do SmartView
@param nStart, numérico, indica o inicío da paginação
@param nEnd, numérico, indica o fim da paginação
 
@return string

@author Denis Souza
@since 15/05/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method execQuery(oPrepare as object, oFilter as object, aBind as array, nStart as numeric, nEnd as numeric, cYearMonth as character, cStartDate as character, cEndDate as character, cReinfEvt as character ) as character class ReinfBlock2010and2020SmartViewBusinessObject

local cDbType      := " " as character
local cIsNullSQL   := " " as character
local cAlias       := " " as character
local cQry         := " " as character
local cSrvType     := " " as character
local cCompC1H     := " " as character
local cCompT9C     := " " as character
local aBranch 	   := {}  as array
local aRootBranch  := {}  as array
local nX           := 0   as numeric
local aInfEUF      := {}  as array
local aCODSIT      := {}  as array
local cDOCORI      := ' ' as character
local nlA          := 0   as numeric

aCODSIT := {"000001","000002","000007","000008","000009"}

Default oPrepare := Nil
Default aBind    := {}
default oFilter    := Nil
default nStart     := 0
default nEnd       := 0
default cYearMonth := " "
default cStartDate := " "
default cEndDate   := " "
default cReinfEvt  := " "

cCompC1H := Upper(AllTrim(FWModeAccess("C1H",1)+FWModeAccess("C1H",2)+FWModeAccess("C1H",3)))
cCompT9C := Upper(AllTrim(FWModeAccess("T9C",1)+FWModeAccess("T9C",2)+FWModeAccess("T9C",3)))
aInfEUF  := TAFTamEUF(Upper(AllTrim(SM0->M0_LEIAUTE)))

cSrvType := TcSrvType()
cDbType := Upper(Alltrim(TCGetDB()))
cIsNullSQL := iif(cDbType $ "INFORMIX|ORACLE","NVL",iif(cDbType $ "DB2|POSTGRES" .Or. (cDbType == "DB2/400" .And. Upper(cSrvType) == "ISERIES"),"COALESCE","ISNULL"))

aBranch := wsLoadFil()
For nX := 1 to len(aBranch)
    aadd(aRootBranch, aBranch[nX][3] )
Next nX

cQry += " SELECT 'NF' ROTINA, '0' ID, C20.C20_FILIAL FIL, C20.C20_NUMDOC NUMDOCTO, C20.C20_SERIE SERIE, C20.C20_DTDOC DTEMISSAONF, C20.C20_VLDOC VLRBRUTO, ' ' OBS, "
cQry += " C20.R_E_C_N_O_ RECNO, T9C.T9C_TPINSC TPINSC, T9C.T9C_NRINSC NRINSC, T9C.T9C_ID IDOBRA, T9C.T9C_DSCOBR DSCOBRA, T9C.T9C_INDOBR INDOBRA, C1H.C1H_CNPJ CNPJ, C20.C20_CODPAR CODPAR, "
cQry += " C20.C20_IDOBR CODOBR, C30.C30_IDTSER CODSER, C35.C35_BASE VLRBASERET, C35.C35_VALOR VLRRETENCAO, C35.C35_VLSCON VLRRETSUB, C35.C35_VLRNPG VLRNRETPRINC, C35.C35_VLCE15 VLR15SERVICOS, "
cQry += " C35.C35_VLCE20 VLR20SERVICOS, C35.C35_VLCE25 VLR25SERVICOS, C35.C35_VLRADI VLRADICIONAL, C35.C35_VLRANP VLRNRETADIC, C20.C20_CHVNF CHVNF, C35.C35_ALIQ ALIQ, "
cQry += iif(TAFColumnPos( "T9C_CPRB" )," CASE WHEN C20.C20_IDOBR = ? OR T9C.T9C_CPRB IS NULL OR T9C.T9C_CPRB = ? THEN C1H.C1H_CPRB ELSE T9C.T9C_CPRB END CPRB, "," C1H.C1H_CPRB CPRB, ")
AADD(aBind, Space(1))  
AADD(aBind, Space(1))  

cQry += " C1H.C1H_ID IDPART, C1H.C1H_CODPAR C1HCODPAR, C1H.C1H_NOME C1HNOME, C8C.C8C_CREINF CODSERVIC, C8C.C8C_DESCRI C8CDESCRI, C20.C20_PROCID PROCID, "
cQry += iif(cReinfEvt == "R-2010"," V0W.V0W_VLTTBR RETBRUT, V0W.V0W_VLTTRP RETTRIB ", " V0W.V0W_VLPTBR RETBRUT, V0W.V0W_VLPTRP RETTRIB " ) + ", V0W.V0W_NRRECB NRRECB "
cQry += " FROM " + RetSqlName("C20") + " C20 "
cQry += " INNER JOIN " + RetSqlName("C30") + " C30 ON " + FwJoinFilial("C30","C20") + " AND C30.C30_CHVNF = C20.C20_CHVNF AND C30.C30_IDTSER <> ? AND C30.C30_TPREPA = ? AND C30.D_E_L_E_T_ = ? "
cQry += " INNER JOIN " + RetSqlName("C35") + " C35 ON " + FwJoinFilial("C35","C30") + " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE AND C35.C35_CODTRI = ? AND C35.D_E_L_E_T_ = ? "
cQry += " INNER JOIN " + RetSqlName("C8C") + " C8C ON C8C.C8C_FILIAL = ? AND C8C.C8C_ID = C30.C30_IDTSER AND C8C.D_E_L_E_T_ = ? "
AADD(aBind, Space(1))  
AADD(aBind, Space(1))  
AADD(aBind, Space(1))  
AADD(aBind, '000013')  
AADD(aBind, Space(1)) 
AADD(aBind, xFilial("C8C")) 
AADD(aBind, Space(1)) 

cQry  += " LEFT JOIN " + RetSqlName("T9C") + " T9C ON "
if cCompT9C == "CCC" .Or. (cCompT9C == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
    cQry += " T9C.T9C_FILIAL = ? "
    AADD(aBind,xFilial("T9C")) 
else
    cQry += FwJoinFilial("T9C","C20")
endif
cQry += " AND T9C.T9C_ID = C20.C20_IDOBR AND T9C.D_E_L_E_T_ = ? "
AADD(aBind, Space(1))

cQry  += " INNER JOIN " + RetSqlName("C1H") + " C1H ON "
if cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
    cQry += " C1H.C1H_FILIAL = ? "
    AADD(aBind,xFilial("C1H")) //C1H_FILIAL
else
    cQry += FwJoinFilial("C1H","C20")
endif

cQry += " AND C1H.C1H_ID = C20.C20_CODPAR AND C1H.C1H_PPES = ? AND C1H.C1H_CNPJ <> ? AND C1H.C1H_INDDES IN (?) AND C1H.D_E_L_E_T_ = ? "
AADD(aBind, '2')  
AADD(aBind, Space(1))  
AADD(aBind,{Space(1),'0','2'})
AADD(aBind, Space(1))  

cQry += " LEFT JOIN " + RetSqlName("V0W") + " V0W ON " + FwJoinFilial("V0W","C20") + " AND V0W.V0W_ATIVO = ? AND V0W.V0W_CODRET = ? AND V0W.V0W_PERAPU = ? "
AADD(aBind, '1')  
AADD(aBind, '0')  
AADD(aBind, substr(cYearMonth,5,2) + substr(cYearMonth,1,4))  

if cReinfEvt == "R-2010"
    cQry += " AND V0W.V0W_TPEVEN = ? AND V0W.V0W_CNPJ10 = C1H.C1H_CNPJ "
    AADD(aBind, '2010')  

elseif cReinfEvt == "R-2020"
    cQry += " AND V0W.V0W_TPEVEN = ? "
    AADD(aBind, '2020') 
    cQry += " AND V0W.V0W_TPINST = (CASE WHEN T9C.T9C_TPINSC = ? AND T9C.T9C_NRINSC <> ? THEN '4' ELSE '1' END) "
    AADD(aBind, '4')
    AADD(aBind, Space(1)) 
    cQry += " AND V0W.V0W_NRINST = (CASE WHEN T9C.T9C_TPINSC = ? AND T9C.T9C_NRINSC <> ? THEN T9C.T9C_NRINSC ELSE C1H.C1H_CNPJ END) "
    AADD(aBind, '4')
    AADD(aBind, Space(1)) 

endif

cQry += " AND V0W.D_E_L_E_T_ = ? "
AADD(aBind, Space(1)) 

cQry += " WHERE C20.C20_FILIAL IN (?) AND C20.D_E_L_E_T_ = ? AND C20.C20_CODSIT IN (?) AND " // WHERE C20.C20_FILIAL IN " + cRootBranch + " AND C20.D_E_L_E_T_ = ' ' AND C20.C20_CODSIT NOT IN ('000003','000004','000005','000006') AND "
AADD( aBind ,aRootBranch) 
AADD( aBind ,Space(1)) 
AADD( aBind ,aCODSIT) 

cQry += iif(cReinfEvt == "R-2010"," C20.C20_INDOPE = ? AND ", " C20.C20_INDOPE = ? AND " ) 

If cReinfEvt == "R-2010"
  AADD( aBind , '0') 
Else  
  AADD( aBind , '1') 
Endif

cQry += " C20.C20_DTDOC BETWEEN ? AND ? " //C20.C20_DTDOC BETWEEN '" + cStartDate + "' AND '" + cEndDate + "' "
AADD( aBind , cStartDate )
AADD( aBind , cEndDate )

cQry += " UNION ALL "

cQry += " SELECT 'FAT' ROTINA, LEM.LEM_ID ID, LEM.LEM_FILIAL FIL, LEM.LEM_NUMERO NUMDOCTO, LEM.LEM_PREFIX SERIE, LEM.LEM_DTEMIS DTEMISSAONF, LEM.LEM_VLBRUT VLRBRUTO, LEM.LEM_OBSERV OBS, "
cQry += " LEM.R_E_C_N_O_ RECNO, T9C.T9C_TPINSC TPINSC, T9C.T9C_NRINSC NRINSC, T9C.T9C_ID IDOBRA, T9C.T9C_DSCOBR DSCOBRA, T9C.T9C_INDOBR INDOBRA, C1H.C1H_CNPJ CNPJ, LEM_IDPART CODPAR, "
cQry += " '0' CODOBR, T5M.T5M_IDTSER CODSER, T5M.T5M_BSINSS VLRBASERET, T5M.T5M_VLINSS VLRRETENCAO, T5M.T5M_VLRETS VLRRETSUB, T5M.T5M_VLRETP VLRNRETPRINC, T5M.T5M_VLSV15 VLR15SERVICOS, "
cQry += " T5M.T5M_VLSV20 VLR20SERVICOS, T5M.T5M_VLSV25 VLR25SERVICOS, T5M.T5M_VLADIC VLRADICIONAL, T5M.T5M_VLADNE VLRNRETADIC, '0' CHVNF, 0 ALIQ, "
cQry += iif(TAFColumnPos( "T9C_CPRB" )," CASE WHEN T9C.T9C_INDOBR = ? OR T9C.T9C_CPRB IS NULL OR T9C.T9C_CPRB = ? THEN C1H.C1H_CPRB ELSE T9C.T9C_CPRB END CPRB, "," C1H.C1H_CPRB CPRB, ")
AADD(aBind, Space(1))  
AADD(aBind, Space(1))  

cQry += " C1H.C1H_ID IDPART, C1H.C1H_CODPAR C1HCODPAR, C1H.C1H_NOME C1HNOME, C8C.C8C_CREINF CODSERVIC, C8C.C8C_DESCRI C8CDESCRI, LEM.LEM_PROCID PROCID, "
cQry += iif(cReinfEvt == "R-2010"," V0W.V0W_VLTTBR RETBRUT, V0W.V0W_VLTTRP RETTRIB ", " V0W.V0W_VLPTBR RETBRUT, V0W.V0W_VLPTRP RETTRIB " ) + ", V0W.V0W_NRRECB NRRECB "
cQry += " FROM " + RetSqlName("LEM") + " LEM "
cQry += " INNER JOIN " + RetSqlName("T5M") + " T5M ON " + FwJoinFilial("T5M","LEM") + " AND T5M.T5M_ID = LEM.LEM_ID AND T5M.T5M_IDPART = LEM.LEM_IDPART AND T5M.T5M_IDTSER <> ? AND T5M.T5M_TPREPA = ? AND T5M.T5M_NUMFAT = LEM.LEM_NUMERO AND T5M.T5M_BSINSS > ? AND T5M.D_E_L_E_T_ = ? "
AADD(aBind, Space(1)) 
AADD(aBind, Space(1)) 
AADD(aBind, '0') 
AADD(aBind, Space(1)) 

cQry += " INNER JOIN " + RetSqlName("C8C") + " C8C ON C8C.C8C_FILIAL = ? AND C8C.C8C_ID = T5M.T5M_IDTSER AND C8C.D_E_L_E_T_ = ? "
AADD(aBind, xFilial("C8C")) 
AADD(aBind, Space(1)) 

cQry  += " LEFT JOIN " + RetSqlName("T9C") + " T9C ON "
if cCompT9C == "CCC" .Or. (cCompT9C == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
    cQry += " T9C.T9C_FILIAL = ? "
    AADD(aBind,xFilial("T9C")) 
else
    cQry += FwJoinFilial("T9C","LEM")
endif

cQry += " AND T9C.T9C_ID = LEM.LEM_IDOBRA AND T9C.D_E_L_E_T_ = ? "
AADD(aBind, Space(1))

cQry  += " INNER JOIN " + RetSqlName("C1H") + " C1H ON "
if cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
    cQry += " C1H.C1H_FILIAL = ? "
    AADD(aBind,xFilial("C1H")) //C1H_FILIAL
else
    cQry += FwJoinFilial("C1H","LEM")
endif

cQry += " AND C1H.C1H_ID = LEM.LEM_IDPART AND C1H.C1H_PPES = ? AND C1H.C1H_CNPJ <> ? AND C1H.C1H_INDDES IN (?) AND C1H.D_E_L_E_T_ = ? "
AADD(aBind, '2')
AADD(aBind, Space(1))
AADD(aBind,{Space(1),'0','2'})
AADD(aBind, Space(1))

cQry += " LEFT JOIN "  + RetSqlName("V0W") + " V0W ON " + FwJoinFilial("V0W","LEM") + " AND V0W.V0W_ATIVO = ? AND V0W.V0W_CODRET = ? AND V0W.V0W_PERAPU = ? "
AADD(aBind, '1')
AADD(aBind, '0')
AADD(aBind, substr(cYearMonth,5,2) + substr(cYearMonth,1,4))

if cReinfEvt == "R-2010"
    cQry += " AND V0W.V0W_TPEVEN = ? AND V0W.V0W_CNPJ10 = C1H.C1H_CNPJ "
    AADD(aBind, '2010')
elseif cReinfEvt == "R-2020"
    cQry += " AND V0W.V0W_TPEVEN = ? "
    AADD(aBind, '2020')
    cQry += " AND V0W.V0W_TPINST = (CASE WHEN T9C.T9C_TPINSC = ? AND T9C.T9C_NRINSC <> ? THEN '4' ELSE '1' END) "
    AADD(aBind, '4')
    AADD(aBind, Space(1)) 
    cQry += " AND V0W.V0W_NRINST = (CASE WHEN T9C.T9C_TPINSC = ? AND T9C.T9C_NRINSC <> ? THEN T9C.T9C_NRINSC ELSE C1H.C1H_CNPJ END) "
    AADD(aBind, '4')
    AADD(aBind, Space(1)) 
endif
cQry += " AND V0W.D_E_L_E_T_ = ? "
AADD(aBind, Space(1)) 

cQry += " WHERE LEM.LEM_FILIAL IN (?) AND LEM.LEM_DOCORI = ? AND " // WHERE LEM.LEM_FILIAL IN " + cRootBranch + " AND LEM.LEM_DOCORI = ' ' AND LEM.D_E_L_E_T_ = ' ' AND "
AADD( aBind , aRootBranch ) 
AADD( aBind , cDOCORI ) 

cQry += iif(cReinfEvt == "R-2010"," LEM.LEM_NATTIT = ? AND ", " LEM.LEM_NATTIT = ? AND " ) 

If cReinfEvt == "R-2010"
   AADD( aBind , '0'  ) 
Else
   AADD( aBind , '1' ) 
Endif

cQry += " LEM.LEM_DTEMIS BETWEEN ? AND ? " //LEM.LEM_DTEMIS BETWEEN '" + cStartDate + "' AND '" + cEndDate + "' "
cQry += " AND LEM.D_E_L_E_T_ = ? "
AADD( aBind , cStartDate )
AADD( aBind , cEndDate )
AADD( aBind , Space(1)) 

cQry += " ORDER BY FIL, ROTINA, CODPAR, CNPJ, TPINSC, NRINSC, DTEMISSAONF, NUMDOCTO, SERIE "

cQry := ChangeQuery(cQry)

oPrepare := FwExecStatement():New( cQry )
for nlA := 1 to len(aBind)
    if Valtype(aBind[nlA]) == 'A'
        oPrepare:setIn( nlA, aBind[nlA] )
    else
        oPrepare:setString( nlA, aBind[nlA] )
    endif
next nlA

cAlias := GetNextAlias()
oPrepare:OpenAlias( cAlias )


Return cAlias
