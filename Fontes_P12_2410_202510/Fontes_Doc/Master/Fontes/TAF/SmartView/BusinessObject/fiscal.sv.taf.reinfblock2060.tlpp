#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "fiscal.sv.taf.reinfblock2060.ch"

Static _cQryMD5 := "" as character

namespace totvs.protheus.fiscal.taf.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATAF", tables="C5M,C5V,V48,T9T,T9C,V0W,V0Z", name="ReinfBlock2060", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*/{Protheus.doc} ReinfBlock2060SmartViewBusinessObject


@author Evandro Italo
@since 26/07/2023
@version 1.0

*/
//-------------------------------------------------------------------  
class ReinfBlock2060SmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()       as object
    public method getData()   as object
    public method getSchema() as object 
    private method totalRet()

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Evandro Italo
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method new() class ReinfBlock2060SmartViewBusinessObject

_Super:new()
    //Define Area
self:appendArea(STR0001)     //"TAF Fiscal Contábil"
    //Define o nome do objeto de negócio
self:setDisplayName(STR0002) //"Relatório REINF(R2060) Movimentos TAF x Retorno Governo"
    //Define a descrição do objeto de negócio
self:setDescription(STR0003 + STR0004) //"Objeto ReinfBlock2060 - Movimentos TAF x Retorno Governo"##" (C5M,C5V,V48,T9T,T9C,V0W,V0Z)"
    //Define as perguntas, arquivo da SX1
if !::setPergunte("TAFTR00003");FwLogMsg("WARN",,"SmartView",,,,"Grupo de perguntas nao encontrado",,,);endif
self:setPageSize(10000)

return nil 

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Evandro Italo
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------   

method getData(nPage as numeric, oFilter as object) as object class ReinfBlock2060SmartViewBusinessObject

    local aSM0              as array 
    local aBranch           as array
    local aRootBranch       as array
    local aTotalizer        as array
    local aInfEUF           as array
    local aBind             as array
    local cName             as character
    local cTaxNumberBranch  as character
    local cAlias            as character
    local cQuery            as character
    local cEndDate          as character
    local cStartDate        as character
    local cYearMonth        as character
    local cRootBranch       as character
    local cProtocol         as character
    local cCompT9C          as character
    local cCompC1G          as character
    local cNrCNO            as character
    local cBd               as character
    local lHasNext          as logical
    local nStart            as numeric
    local nEnd              as numeric
    local nX                as numeric
    local nlA               as numeric
    local nTotGov           as numeric
    Local oFiltQry          as json
    local oHashTot		    as object
    local oPrepare          as object

    oFiltQry := oFilter:getParameters()

    aBranch     := {}
    aRootBranch := {} 
    aTotalizer  := {}
    aBind       := {}
    cYearMonth  := AllTrim(oFiltQry['MV_PAR01',1])
	cAlias      := ""
    cRootBranch := ""
    cProtocol   := ""
    cNrCNO      := ""
    cBd         := Upper(Alltrim(TCGetDB()))
	nlA		    := 0
    nX          := 0
    nTotGov     := 0
    oHashTot    := tHashMap():New()
	oPrepare	:= nil
    aInfEUF     := TAFTamEUF(Upper(AllTrim(SM0->M0_LEIAUTE)))
    cCompT9C    := Upper(AllTrim(FWModeAccess("T9C",1)+FWModeAccess("T9C",2)+FWModeAccess("T9C",3)))
    cCompC1G    := Upper(AllTrim(FWModeAccess("C1G",1)+FWModeAccess("C1G",2)+FWModeAccess("C1G",3)))

    cStartDate := cYearMonth + "01"                      //20100101 ex: "20100101"
    cEndDate   := DtoS( LastDay( StoD( cStartDate ) ) )  //20240101 ex: "20100131"

    lHasNext := .F.

    //Seta o primeiro item e o último da página atual
    nStart := ((nPage - 1) * self:getPageSize()) + 1
    nEnd   := nPage * self:getPageSize()

    aBranch := wsLoadFil()
    For nX := 1 to len(aBranch)
        aadd(aRootBranch, { aBranch[nX][1], aBranch[nX][3] } )
    Next nX
    cRootBranch := TafRetFilC("C5M", aRootBranch )

    self:totalRet(cYearMonth,@oHashTot)

    cRootBranch := StrTran(StrTran(StrTran(cRootBranch,"(",""),")",""),"'","")
    aSize(aRootBranch,0)
    aRootBranch := Separa( cRootBranch, "," )

    cQuery :=   " SELECT TAB.C5M_FILIAL, TAB.C5M_ID, TAB.V48_IDCNO, TAB.T9C_NRINSC, TAB.C5M_CODATI, TAB.C5V_DESCRI, TAB.C5MVTOT, TAB.C5MVLRATIV, "
    cQuery +=   " TAB.C5MBASE, TAB.C5MVCON, TAB.V48_ALQCON, TAB.C5M_ALQCON, TAB.V48VATIV, TAB.V48_BASE, TAB.V48VCON, TAB.REDUCAO, TAB.ACRESCIMO, TAB.C1GNUMPRO "
    cQuery +=   "FROM ( "
    cQuery +=   " SELECT TMP.C5M_FILIAL, TMP.C5M_ID, TMP.V48_IDCNO, TMP.T9C_NRINSC, "
    cQuery +=   " TMP.C5M_CODATI, TMP.C5V_DESCRI, TMP.C5MVTOT, TMP.C5MVLRATIV, TMP.C5MVCON, "
    cQuery +=   " TMP.C5MBASE, TMP.V48_ALQCON, TMP.C5M_ALQCON, TMP.V48VATIV, TMP.V48_BASE, TMP.V48VCON, TMP.REDUCAO, TMP.ACRESCIMO, TMP.C1GNUMPRO "
    cQuery +=   "FROM "
    cQuery +=   " ( SELECT "
    cQuery +=   " C5M.C5M_FILIAL, C5M.C5M_ID, '' V48_IDCNO, 0 V48_ALQCON,  '' T9C_NRINSC,C5M.C5M_CODATI, C5V.C5V_DESCRI, C5M.C5M_ALQCON "
    cQuery +=   " ,SUM(C5M.C5M_VTOT) C5MVTOT "
    cQuery +=   " ,SUM(C5M.C5M_VATIV) C5MVLRATIV "  //RECEITA BRUTA 
    cQuery +=   " ,SUM(C5M.C5M_BASE) C5MBASE "      //VALOR BASE 
    cQuery +=   " ,SUM(C5M.C5M_VCON) C5MVCON "      //VALOR CONTRIB 
    cQuery +=   " ,0 V48VATIV "                     //RECEITA BRUTA OBRA
    cQuery +=   " ,0 V48_BASE "                     //VALOR BASE OBRA
    cQuery +=   " ,0 V48VCON "                      //VALOR CONTRIB OBRA

    cQuery +=   " ,COALESCE( "
    cQuery +=   " (SELECT SUM(T9T.T9T_VLRAJU) FROM " + RetSqlName("T9T") + " T9T WHERE " + FwJoinFilial("T9T","C5M")
    cQuery +=   " AND T9T.T9T_ID IN ( SELECT TMP.C5M_ID FROM " + RetSqlName("C5M") + " TMP WHERE TMP.D_E_L_E_T_ = ? "
    aadd(aBind,space(1))    //TMP.D_E_L_E_T_

    If "TMPFIL" $ aRootBranch[1]
        cQuery +=   " AND TMP.C5M_FILIAL IN (" + aRootBranch[1] + ") "
    Else
        cQuery +=   " AND TMP.C5M_FILIAL IN (?) "
        aadd(aBind,aRootBranch) //C5M_FILIAL
    EndIf
    cQuery +=   " AND TMP.C5M_DTINI >= ? AND TMP.C5M_DTFIM <= ? "
    cQuery +=   " AND TMP.C5M_CODATI = C5M.C5M_CODATI  ) "
    cQuery +=   " AND T9T.T9T_TPAJUS = ? "
    cQuery +=   " AND T9T.D_E_L_E_T_ = ?) "
    cQuery +=   " ,0) REDUCAO "
    aadd(aBind,cStartDate)  //C5M_DTINI
    aadd(aBind,cEndDate)    //C5M_DTFIM
    aadd(aBind,'0')         //T9T_TPAJUS
    aadd(aBind,space(1))    //T9T.D_E_L_E_T_

    cQuery +=   " ,COALESCE( "
    cQuery +=   " (SELECT SUM(T9T.T9T_VLRAJU) FROM " + RetSqlName("T9T") + " T9T WHERE " + FwJoinFilial("T9T","C5M")
    cQuery +=   " AND T9T.T9T_ID IN ( SELECT TMP.C5M_ID FROM " + RetSqlName("C5M") + " TMP WHERE TMP.D_E_L_E_T_ = ? "
    aadd(aBind,space(1))    //TMP.D_E_L_E_T_

    If "TMPFIL" $ aRootBranch[1]
        cQuery +=   " AND TMP.C5M_FILIAL IN (" + aRootBranch[1] + ") "
    Else
        cQuery +=   " AND TMP.C5M_FILIAL IN (?) "
        aadd(aBind,aRootBranch) //C5M_FILIAL
    EndIf
    cQuery +=   " AND TMP.C5M_DTINI >= ? AND TMP.C5M_DTFIM <= ? "
    cQuery +=   " AND TMP.C5M_CODATI = C5M.C5M_CODATI ) "
    cQuery +=   " AND T9T.T9T_TPAJUS = ? "
    cQuery +=   " AND T9T.D_E_L_E_T_ = ? ) "
    cQuery +=   " ,0) ACRESCIMO, "
    aadd(aBind,cStartDate)  //C5M_DTINI
    aadd(aBind,cEndDate)    //C5M_DTFIM
    aadd(aBind,'1')         //T9T_TPAJUS
    aadd(aBind,space(1))    //T9T.D_E_L_E_T_

    cQuery +=  iif( !( cBd $ ( "INFORMIX|ORACLE|OPENEDGE|POSTGRES|DB2" ) ), " ( SELECT TOP 1 C1G.C1G_NUMPRO ", " ( SELECT C1G.C1G_NUMPRO ")
    cQuery += " FROM " + RetSqlName("C5O") + " C5O INNER JOIN " + RetSqlName("C1G") + " C1G ON "
    if(cCompC1G == "CCC" .Or. (cCompC1G == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0))
        cQuery += "C1G.C1G_FILIAL = ? "
        aadd(aBind,xFilial("C1G"))
    else
        cQuery += FwJoinFilial("C1G","C5O")
    endif
    cQuery += " AND C1G.C1G_ID = C5O.C5O_NUMPRO AND C1G.D_E_L_E_T_ = ? "
    cQuery += " WHERE " + FwJoinFilial("C5O","C5M") + " AND C5O.C5O_ID = C5M.C5M_ID AND C5O.D_E_L_E_T_ = ? " + iif( cBd == "ORACLE", "AND ROWNUM <= 1", iif(cBd $ "POSTGRES|DB2","LIMIT 1","") ) + ") C1GNUMPRO "
    cQuery += " FROM " + RetSqlName("C5M") + " C5M "
    cQuery += " INNER JOIN " + RetSqlName("C5V") + " C5V ON C5V.C5V_FILIAL = ? AND C5V.C5V_ID = C5M.C5M_CODATI AND C5V.D_E_L_E_T_ = ? "
    aadd(aBind,space(1))       //C1G.D_E_L_E_T_
    aadd(aBind,space(1))       //C5O.D_E_L_E_T_
    aadd(aBind,xFilial("C5V")) //C5V.C5V_FILIAL
    aadd(aBind,space(1))       //C5V.D_E_L_E_T_
    
    cQuery += " WHERE "

    If "TMPFIL" $ aRootBranch[1]
        cQuery += " C5M.C5M_FILIAL IN (" + aRootBranch[1] + ") "
    Else
        cQuery += " C5M.C5M_FILIAL IN (?) "
        aadd(aBind,aRootBranch)    //C5M_FILIAL
    EndIf
    cQuery += " AND C5M.C5M_DTINI >= ? AND C5M.C5M_DTFIM <= ? AND C5M.D_E_L_E_T_ = ? "
    aadd(aBind,cStartDate)     //C5M_DTINI
    aadd(aBind,cEndDate)       //C5M_DTFIM
    aadd(aBind,space(1))       //C5M.D_E_L_E_T_

    cQuery += " AND NOT EXISTS ( "
    cQuery += " SELECT TAB1.C5M_ID "
    cQuery += " FROM " + RetSqlName("C5M") + " TAB1 "
    cQuery += " INNER JOIN " + RetSqlName("V48") + " V48 ON " + FwJoinFilial("V48","C5M") + " AND C5M.C5M_ID = V48.V48_ID "
    cQuery += " AND V48.V48_IDCNO <> ? AND V48.D_E_L_E_T_ = ? "
    cQuery += " ) "
    aadd(aBind,space(1)) //V48_IDCNO
    aadd(aBind,space(1)) //V48.D_E_L_E_T_

    if oFilter:hasFilter()
        cQuery +=   " AND " + oFilter:getSQLExpression()
    endif

    cQuery +=   " GROUP BY "
    cQuery +=   " C5M.C5M_FILIAL, C5M.C5M_ID, C5M.C5M_CODATI, C5M.C5M_ALQCON, C5V.C5V_DESCRI "

    cQuery +=   " union all "

    cQuery +=   " SELECT  "
    cQuery +=   " C5M.C5M_FILIAL "
    cQuery +=   " ,C5M.C5M_ID "
    cQuery +=   " ,V48.V48_IDCNO "
    cQuery +=   " ,V48.V48_ALQCON "    
    cQuery +=   " ,T9C.T9C_NRINSC "
    cQuery +=   " ,C5M.C5M_CODATI "
    cQuery +=   " ,C5V.C5V_DESCRI "
    cQuery +=   " ,0 C5MVTOT "
    cQuery +=   " ,0 C5MVLRATIV "
    cQuery +=   " ,0 C5MBASE "
    cQuery +=   " ,0 C5MVCON "
    cQuery +=   " ,0 C5MALQCON"
    cQuery +=   " ,SUM(V48.V48_VATIV) V48VATIV "
    cQuery +=   " ,SUM(V48.V48_BASE)  V48_BASE "
    cQuery +=   " ,SUM(V48.V48_VCON)  V48VCON "
    cQuery +=   " ,0 REDUCAO "
    cQuery +=   " ,0 ACRESCIMO, "
    cQuery +=  iif( !( cBd $ ( "INFORMIX|ORACLE|OPENEDGE|POSTGRES|DB2" ) ), " ( SELECT TOP 1 C1G.C1G_NUMPRO ", " ( SELECT C1G.C1G_NUMPRO ")
    cQuery += " FROM " + RetSqlName("C5O") + " C5O INNER JOIN " + RetSqlName("C1G") + " C1G ON "
    if(cCompC1G == "CCC" .Or. (cCompC1G == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0))
        cQuery += "C1G.C1G_FILIAL = ? "
        aadd(aBind,xFilial("C1G"))
    else
        cQuery += FwJoinFilial("C1G","C5O")
    endif
    cQuery += " AND C1G.C1G_ID = C5O.C5O_NUMPRO AND C1G.D_E_L_E_T_ = ? "
    cQuery += " WHERE " + FwJoinFilial("C5O","C5M") + " AND C5O.C5O_ID = C5M.C5M_ID AND C5O.D_E_L_E_T_ = ? " + iif(cBd == "ORACLE", " AND ROWNUM <= 1 ", iif(cBd $ "POSTGRES|DB2"," LIMIT 1 ","")) + ") C1GNUMPRO "
    cQuery += " FROM " + RetSqlName("C5M") + " C5M "
    cQuery += " INNER JOIN " + RetSqlName("C5V") + " C5V ON C5V.C5V_FILIAL = ? AND C5V.C5V_ID = C5M.C5M_CODATI AND C5V.D_E_L_E_T_ = ? "
    cQuery += " INNER JOIN " + RetSqlName("V48") + " V48 ON " + FwJoinFilial("V48","C5M") + " AND C5M.C5M_ID = V48.V48_ID "
    cQuery += " AND V48.V48_IDCNO <> ? AND V48.D_E_L_E_T_ = ? "
    aadd(aBind,space(1))       //C1G.D_E_L_E_T_
    aadd(aBind,space(1))       //C5O.D_E_L_E_T_
    aadd(aBind,xFilial("C5V")) //C5V_FILIAL
    aadd(aBind,space(1))       //C5V.D_E_L_E_T_
    aadd(aBind,space(1))       //V48_IDCNO
    aadd(aBind,space(1))       //V48.D_E_L_E_T_

    cQuery += " LEFT JOIN " + RetSqlName("T9C") + " T9C ON "
    if(cCompT9C == "CCC" .Or. (cCompT9C == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0))
        cQuery += "T9C.T9C_FILIAL = ? "
        aadd(aBind,xFilial("T9C"))
    else
        cQuery += FwJoinFilial("T9C","V48")
    endif
    cQuery += " AND V48_IDCNO = T9C.T9C_ID AND T9C.D_E_L_E_T_ = ? "
    aadd(aBind,space(1))    //T9C.D_E_L_E_T_
    
    cQuery += " WHERE "

    If "TMPFIL" $ aRootBranch[1]
        cQuery += " C5M.C5M_FILIAL IN (" + aRootBranch[1] + ") "
    Else
        cQuery += " C5M.C5M_FILIAL IN (?) "
        aadd(aBind,aRootBranch) //C5M_FILIAL
    EndIf
    
    cQuery += " AND C5M.C5M_DTINI >= ? AND C5M.C5M_DTFIM <= ? AND C5M.D_E_L_E_T_ = ? "
    aadd(aBind,cStartDate)  //C5M_DTINI
    aadd(aBind,cEndDate)    //C5M_DTFIM
    aadd(aBind,space(1))    //C5M.D_E_L_E_T_

    if oFilter:hasFilter()
        cQuery +=   " AND " + oFilter:getSQLExpression()
    endif

    cQuery  +=   " GROUP BY C5M.C5M_FILIAL, C5M.C5M_ID, V48.V48_IDCNO, T9C.T9C_NRINSC, C5M.C5M_CODATI, V48.V48_ALQCON, C5V.C5V_DESCRI "
    cQuery  +=   ") TMP) TAB " 

    If !Md5(cQuery) == _cQryMD5
        cQuery := ChangeQuery(cQuery)
        _cQryMD5 := Md5(cQuery)    
    endif

    oPrepare := FwExecStatement():New( cQuery )
    for nlA := 1 to len(aBind)
        if Valtype(aBind[nlA]) == 'A'
            oPrepare:setIn( nlA, aBind[nlA] )
        else
            oPrepare:setString( nlA, aBind[nlA] )
        endif
    next nlA

    cAlias := GetNextAlias()
    oPrepare:OpenAlias( cAlias )

    while !(cAlias)->(Eof())

        aSM0 := FWSM0Util():GetSM0Data(cEmpAnt,(cAlias)->C5M_FILIAL,{"M0_FILIAL","M0_CGC"})
        if len(aSM0) >= 1
            cName := SubStr(Alltrim(aSM0[1][2]),1,30)
        endif
        if len(aSM0) >= 2
                cTaxNumberBranch := Alltrim(aSM0[2][2])
        endif
        nTotGov := 0
        cProtocol := "Não possui"
        aSize(aTotalizer,0)
        cKey := IIF(!EMPTY((cAlias)->V48_IDCNO),"4" + AllTrim((cAlias)->T9C_NRINSC),"1" + Alltrim(aSM0[2][2]))
        If HMGet(oHashTot, cKey, @aTotalizer)
            if len(aTotalizer) >= 1 .and. len(aTotalizer[1]) == 3
                nTotGov   := aTotalizer[1][2]
                cProtocol := aTotalizer[1][3]
            endif
        EndIf
        cNrCNO := "Não Possui"
        if !Empty((cAlias)->T9C_NRINSC)
            cNrCNO := AllTrim((cAlias)->T9C_NRINSC)
        endif
        
        self:oData:appendData({ "FILIAL"          : (cAlias)->C5M_FILIAL                                                                                  ,;
                                "NAME"            : Alltrim(cName)                                                                                        ,;
                                "cTaxNumberBranch": Alltrim(cTaxNumberBranch)                                                                             ,;
                                "C5M_ID"          : Alltrim((cAlias)->C5M_ID)                                                                             ,;
                                "V48_IDCNO"       : (cAlias)->V48_IDCNO                                                                                   ,;
                                "T9C_NRINSC"      : cNrCNO                                                                                                ,;
                                "C5M_CODATI"      : (cAlias)->C5M_CODATI                                                                                  ,;
                                "C5V_DESCRI"      : SubStr((cAlias)->C5V_DESCRI,1,50)                                                                     ,;
                                "C5MVTOT"         : (cAlias)->C5MVTOT                                                                                     ,;
                                "C5MVLRATIV"      : (cAlias)->C5MVLRATIV                                                                                  ,;
                                "C5MBASE"         : ((cAlias)->C5MVLRATIV +  (cAlias)->ACRESCIMO -  (cAlias)->REDUCAO)                                    ,;
                                "C5M_ALQCON"      : (cAlias)->C5M_ALQCON                                                                                  ,;
                                "V48_ALQCON"      : (cAlias)->V48_ALQCON                                                                                  ,;
                                "C5MVCON"         : ((((cAlias)->C5MVLRATIV +  (cAlias)->ACRESCIMO -  (cAlias)->REDUCAO) * (cAlias)->C5M_ALQCON) / 100 )  ,;
                                "V48VATIV"        : (cAlias)->V48VATIV                                                                                    ,;
                                "V48_BASE"        : (cAlias)->V48_BASE                                                                                    ,;
                                "V48VCON"         : (cAlias)->V48VCON                                                                                     ,;
                                "REDUCAO"         : (cAlias)->REDUCAO                                                                                     ,;
                                "ACRESCIMO"       : (cAlias)->ACRESCIMO                                                                                   ,;
                                "PROTOCOL"        :  AllTrim(cProtocol)                                                                                   ,;
                                "TOTGOV"          :  nTotGov                                                                                              ,;
                                "NRPROCES"        : Alltrim((cAlias)->C1GNUMPRO) } )

        (cAlias)->( dbSkip() )
    enddo

    aSize(aBind,0)

    if oPrepare != Nil
      FreeObj(oPrepare)
      oPrepare  := Nil
    endif
    
    if oHashTot != Nil
      FreeObj(oHashTot)
      oHashTot  := Nil
    endif

    aSize(aTotalizer,0)
 
    (cAlias)->( DBCloseArea() )

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Evandro Italo
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getSchema() as object class ReinfBlock2060SmartViewBusinessObject

    //              ( Nome              ,  Descricao                    ,  Tipo   , Display                         , cRealName
    self:addProperty("FILIAL"           , "Filial"                      , "string", "Filial"                        , "C5M_FILIAL"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("NAME"             , "Razão Social"                , "string", "Razão Social"                  , "NAME"            , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("cTaxNumberBranch" , "Inscrição"                   , "string", "Inscrição"                     , "INSCRICAO"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("C5M_ID"           , "Identificador do Registro"   , "string", "Identificador"                 , "C5M_ID"          , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("V48_IDCNO"        , "Id do Cadastro de CNO"       , "string", "ID CNO"                        , "V48_IDCNO"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("T9C_NRINSC"       , "Número CNO"                  , "string", "Número CNO"                    , "T9C_NRINSC"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("C5M_CODATI"       , "Código de Atividade"         , "string", "Código de Atividade"           , "C5M_CODATI"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("C5V_DESCRI"       , "Descrição da atividade"      , "string", "Descrição da atividade"        , "C5V_DESCRI"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("C5MVTOT"          , "Valor Total"                 , "number", "Valor Total"                   , "C5MVTOT"         , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("TOTVLCPAT"        , "Valor Total Governo"         , "number", "Valor Total Governo"           , "TOTVLCPAT"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("C5MVLRATIV"       , "Receita Bruta"               , "number", "Receita Bruta"                 , "C5MVLRATIV"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("C5M_ALQCON"       , "Aliquota Contribuição(%)"    , "number", "Aliquota Contribuição(%)"      , "C5M_ALQCON"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("V48_ALQCON"       , "Aliquota Contribuição(%) CNO", "number", "Aliquota Contribuição(%) CNO"  , "V48_ALQCON"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("C5MBASE"          , "Valor Base"                  , "number", "Valor Base"                    , "C5MBASE"         , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("C5MVCON"          , "Valor da Contribuição"       , "number", "Valor da Contribuição"         , "C5MVCON"         , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("V48VATIV"         , "Receita Bruta - CNO"         , "number", "Receita Bruta - CNO"           , "V48VATIV"        , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("V48_BASE"         , "Valor Base - CNO"            , "number", "Valor Base - CNO"              , "V48_BASE "       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("V48VCON"          , "Valor da Contribuição - CNO" , "number", "Valor da Contribuição - CNO"   , "V48VCON"         , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("REDUCAO"          , "Exclusão"                    , "number", "Exclusão"                      , "REDUCAO"         , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ACRESCIMO"        , "Adição"                      , "number", "Adição"                        , "ACRESCIMO"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("PROTOCOL"         , "Recibo do governo"           , "string", "Rec Governo"                   , "PROTOCOL"        , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("TOTGOV"           , "Total do Governo"            , "number", "Total do Governo"              , "TOTGOV"          , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("NRPROCES"         , "Nr Processo"                 , "string", "Nr Processo"                   , "NRPROCES"        , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/  )

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} totalRet
Retorna Total Governo
 
@return object: self:oSchema
 
@author Evandro Italo
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
method totalRet(cYearMonth as character, oHashTot as object) class ReinfBlock2060SmartViewBusinessObject

local cAliasQry := " " as character
local cQuery    := " " as character
local aTot      := {}  as array
local cBd       := " " as character
local oPrepare         := Nil as object
local aBranch   := {}  as array
local aRootBranch  := {}  as array
local nX           := 0   as numeric
local nlA          := 0   as numeric
local aBind        := {}  as array

default oHashTot    := Nil
default cYearMonth  := ""


aBranch := wsLoadFil()
For nX := 1 to len(aBranch)
    aadd(aRootBranch, aBranch[nX][3] )
Next nX

cBd := Upper(Alltrim(TCGetDB()))

cQuery := " SELECT V0W.V0W_TPINSE,	V0W.V0W_NRINSE, SUM(V0Z.V0Z_VLCPAT) VLCPAT, "
cQuery +=  iif( !( cBd $ ( "INFORMIX|ORACLE|OPENEDGE|POSTGRES|DB2" ) ), " ( SELECT TOP 1 V0W2.V0W_PROTUL ", " ( SELECT V0W2.V0W_PROTUL ")
cQuery += " FROM " + RetSqlName("V0W") + " V0W2 WHERE V0W.V0W_FILIAL = V0W2.V0W_FILIAL AND V0W.V0W_TPINSE = V0W2.V0W_TPINSE AND V0W.V0W_NRINSE = V0W2.V0W_NRINSE AND V0W2.D_E_L_E_T_ = ? "
cQuery += iif( cBd == "ORACLE", " AND ROWNUM <= 1 ", iif(cBd $ "POSTGRES|DB2"," LIMIT 1 ","") ) + " ) PROTOCOL "
cQuery += " FROM " + RetSqlName("V0W") + " V0W "
cQuery += " LEFT JOIN " + RetSqlName("V0Z") + " V0Z ON " + FwJoinFilial("V0Z","V0W") + " AND V0Z.V0Z_ID = V0W.V0W_ID AND V0Z.V0Z_VERSAO = V0W.V0W_VERSAO AND V0Z.D_E_L_E_T_ = ? "
AADD(aBind, Space(1)) 
AADD(aBind, Space(1)) 

cQuery += " WHERE "
cQuery += " V0W.V0W_FILIAL IN (?) AND "
cQuery += " V0W.D_E_L_E_T_ = ? AND V0W.V0W_TPEVEN = ? "
cQuery += " AND V0W.V0W_CODRET = ? AND V0W.V0W_PERAPU = ? AND V0W_ATIVO = ? "
cQuery += " GROUP BY V0W.V0W_FILIAL, V0W.V0W_TPINSE, V0W.V0W_NRINSE "
cQuery += " ORDER BY V0W.V0W_FILIAL, V0W.V0W_TPINSE, V0W.V0W_NRINSE "
AADD(aBind, aRootBranch)
AADD(aBind, Space(1)) 
AADD(aBind, '2060') 
AADD(aBind, '0') 
AADD(aBind, substr(cYearMonth,5,2) + substr(cYearMonth,1,4)) 
AADD(aBind, '1') 

cQuery := ChangeQuery(cQuery)

oPrepare := FwExecStatement():New( cQuery )
for nlA := 1 to len(aBind)
    if Valtype(aBind[nlA]) == 'A'
        oPrepare:setIn( nlA, aBind[nlA] )
    else
        oPrepare:setString( nlA, aBind[nlA] )
    endif
next nlA 

cAliasQry := GetNextAlias()

oPrepare:OpenAlias( cAliasQry )


while (cAliasQry)->(!Eof())
    AAdd(aTot,{Alltrim((cAliasQry)->V0W_TPINSE) + ; 
    Alltrim((cAliasQry)->V0W_NRINSE),;
    (cAliasQry)->VLCPAT, ;
    AllTrim((cAliasQry)->PROTOCOL) })

    (cAliasQry)->(DbSkip())
enddo

(cAliasQry)->(DbCloseArea())

oHashTot := AToHM(aTot)

freeObj(oPrepare)

Return Nil
