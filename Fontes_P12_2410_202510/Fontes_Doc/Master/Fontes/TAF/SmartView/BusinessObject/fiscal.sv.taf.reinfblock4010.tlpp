#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.taf.reinfblock4010.ch"

namespace totvs.protheus.fiscal.taf.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider( active=.T., team="SIGATAF",;
	tables="C1H,T9A,V3O,V3X,C1G,V4F,V4G,C20,C30,C35,V4C,V84,V4D,T9Q,V88,LEM,V3S,V47,V4L,V83,V4M,T9E,V89,V3U,V3V,V46,V4I,V85,V4J,V4H,V90,V4Q,V9D,V9G",;
	name="ReinfBlock4010", country="BRA", initialRelease="12.1.2310" )

//-------------------------------------------------------------------
/*/{Protheus.doc} ReinfBlock4010SmartViewBusinessObject
Classe para criação do Objeto de Negócio
@author Denis Souza
@since 03/08/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class ReinfBlock4010SmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

public method new()        as object
public method getData()    as object
public method getSchema()  as object
private method execQuery() as character
private method totalRet()

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Denis Souza
@since 03/08/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class ReinfBlock4010SmartViewBusinessObject

_Super:new()
self:appendArea(STR0001)     //"TAF Fiscal Contábil"
self:setDisplayName(STR0002) //"Relatório REINF(R4010) Movimentos TAF x Retorno Governo"
self:setDescription(STR0003 + STR0004) //"Objeto ReinfBlock4010 - Movimentos TAF x Retorno Governo"##" (C1H,T9A,V3O,V3X,C1G,V4F,V4G,C20,C30,C35,V4C,V84,V4D,T9Q,V88,LEM,V3S,V47,V4L,V83,V4M,T9E,V89,V3U,V3V,V46,V4I,V85,V4J,V4H,V90,V4Q,V9D,V9G)"
if !::setPergunte("TAFTR00004");FwLogMsg("WARN", , "SmartView", , , , "Grupo de perguntas nao encontrado", , ,);endif
self:setPageSize(10000)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório 
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Denis Souza
@since 03/08/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method getData( nPage as numeric, oFilter as object ) as object class ReinfBlock4010SmartViewBusinessObject

local oTabTemp          := nil as object
local oHashTot		    := Nil as object
local oFiltQry          := nil as json
local cAlias            := " " as character
local cName             := " " as character
local cTaxNumberBranch  := " " as character
local cCPF              := " " as character
local cEmssionDate      := " " as character
local cBd               := " " as character
local cYearMonth        := " " as character
local cProtocol         := " " as character
local cKey              := " " as character
local cV4QTpInsc        := " " as character
local cV4QNrInsc        := " " as character
local cV4QNrInsB        := " " as character
local cNrNif            := " " as character
local cNrDoc            := " " as character
local aSM0              := {}  as array
local aTotalizer        := {}  as array
local nStart            := 0   as numeric
local nEnd              := 0   as numeric
local nX                := 0   as numeric
local nTotGov           := 0   as numeric
local nC20DocNumSize    := 0   as numeric
local aBranch           := {}  as array
local aRootBranch       := {}  as array

default nPage   := 1
default oFilter := Nil

cBd    := Upper(Alltrim(TCGetDB()))

nStart := ((nPage - 1) * self:getPageSize()) + 1 //Seta o primeiro item
nEnd   := nPage * self:getPageSize()             //Seta último da página atual

nC20DocNumSize := GetSx3Cache( 'C20_NUMDOC', 'X3_TAMANHO' )

oFiltQry   := oFilter:getParameters()
cYearMonth := AllTrim(oFiltQry['MV_PAR01',1]) //Ano+Mes (AAAAMM)

oHashTot := tHashMap():New()

aBranch := wsLoadFil()
For nX := 1 to len(aBranch)
    aadd(aRootBranch, { aBranch[nX][1], aBranch[nX][3] } )
Next nX

self:totalRet(cBd,aRootBranch,cYearMonth,@oHashTot)

cAlias := self:execQuery(cBd,aRootBranch,cYearMonth,nStart,nEnd,@oTabTemp)

while !(cAlias)->(Eof())

    aSM0 := FWSM0Util():GetSM0Data(cEmpAnt,(cAlias)->FILIAL,{"M0_FILIAL","M0_CGC"})
    if len(aSM0) >= 1
        cName := SubStr(Alltrim(aSM0[1][2]),1,30)
    endif
    if len(aSM0) >= 2
        cV4QNrInsc := Alltrim(aSM0[2][2])
    endif
    if len(cV4QNrInsc) == 14
        cV4QTpInsc := "1"
        cTaxNumberBranch := Transform(cV4QNrInsc,"@R! NN.NNN.NNN/NNNN-99")
    endif
    cV4QNrInsB := Alltrim((cAlias)->CPF)
    
    if len(cV4QNrInsB) == 11
        cCPF := Transform(cV4QNrInsB,"@R 999.999.999-99")
    else
        cCPF := "Não Informado"
    endif
    cEmssionDate := DtoC(Stod(StrTran(SubStr(FwTimeStamp(5,sToD((cAlias)->DTEMISPGT),"00:00:00"),1,10 ), "-","")))

    cNrNif := "Não Informado"
    if !Empty( (cAlias)->NIF )
        cNrNif := Alltrim((cAlias)->NIF)
    endif

    cProtocol := "Não possui"
    nTotGov := 0
    aSize(aTotalizer,0)
    //ex: 1+53113791000122+22064958720 + Se CPF Vazio + ( 225236332514(NIF) ou FF4018101(CODPAR) se NIF Vazio)
    cKey := cV4QTpInsc+"|"+cV4QNrInsc+"|"+cV4QNrInsB+"|"+iif(Empty(cV4QNrInsB),iif(!Empty(Alltrim((cAlias)->NIF)),Alltrim((cAlias)->NIF),Alltrim((cAlias)->CODPAR)),"")

    If HMGet(oHashTot, cKey, @aTotalizer)
        if len(aTotalizer) >= 1 .and. len(aTotalizer[1]) >= 3
            nTotGov   := aTotalizer[1][2]
            cProtocol := Alltrim(aTotalizer[1][3])
        endif
    EndIf

    cNrDoc := SubStr(Alltrim((cAlias)->DOCTO),1,nC20DocNumSize)

    self:oData:appendData({;
    "_FILIAL"    : Alltrim((cAlias)->FILIAL)                ,; //01 "Filial"
    "_RAZSOC"    : Alltrim(cName)                           ,; //02 "Razão Social"
    "_CNPJFIL"   : Alltrim(cTaxNumberBranch)                ,; //03 "CNPJ da filial"
    "_CPF"       : cCPF                                     ,; //04 "CPF Beneficiário"
    "_CODPAR"    : Alltrim((cAlias)->CODPAR)                ,; //05 "Código participante"
    "_INDNIF"    : Alltrim((cAlias)->INDNIF)                ,; //06 "Indicativo NIF"
    "_NIF"       : cNrNif                                   ,; //07 "N° NIF"
    "_FRMTRIB"   : Alltrim((cAlias)->FRMTRIB)               ,; //08 "Forma Tributação"
    "_NOME"      : SubStr(Alltrim((cAlias)->NOME),1,40)     ,; //09 "Nome Beneficiário"
    "_TIPO"      : Alltrim((cAlias)->ROTINA)                ,; //10 "Tipo"
    "_SERIEPARC" : SubStr(Alltrim((cAlias)->SERIEPARC),1,8) ,; //11 "Série / Parcela"
    "_DOCTO"     : cNrDoc                                   ,; //12 "N° Documento"
    "_NUMITE"    : Alltrim((cAlias)->NUMITE)                ,; //13 "N° do Item"
    "_DTEMISPGT" : Alltrim(cEmssionDate)                    ,; //14 "Data emissão/pagamento"
    "_ID"        : Alltrim((cAlias)->ID)                    ,; //15 "ID do documento"
    "_CODNAT"    : Alltrim((cAlias)->CODNAT)                ,; //16 "Natureza de Rendimento"
    "_INDFCISCP" : Alltrim((cAlias)->INDFCISCP)             ,; //17 "Indicativo de FCI/SCP"
    "_CNPJFCISCP": Alltrim((cAlias)->CNPJFCISCP)            ,; //18 "CNPJ da FCI/SCP"
    "_NRRRAPJD"  : Alltrim((cAlias)->NRRRAPJD)              ,; //19 "Info. de RRA ou Processo Judicial"
    "_INDRRA"    : Alltrim((cAlias)->INDRRA)                ,; //20 "N° Inscrição processo RRA/Processo Judicial"
    "_CNPJOR"    : Alltrim((cAlias)->CNPJOR)                ,; //21 "CNPJ Origem Recurso"
    "_CUSTADV"   : (cAlias)->CUSTADV                        ,; //22 "Total de Custas com advogados"
    "_CUSTJUD"   : (cAlias)->CUSTJUD                        ,; //23 "Total de Custas Judiciais"
    "_VALBRUTO"  : (cAlias)->VALBRUTO                       ,; //24 "Vl do documento"
    "_BASEIR"    : (cAlias)->BASEIR                         ,; //25 "Base de Cálculo IR"
    "_ALIQIR"    : (cAlias)->ALIQIR                         ,; //26 "Alíquota do IR"
    "_VALIR"     : (cAlias)->VALIR                          ,; //27 "Vl do IR"
    "_TOTDEDU"   : (cAlias)->TOTDEDU                        ,; //28 "Total de deduções"
    "_TDEDUDEP"  : (cAlias)->TDEDUDEP                       ,; //29 "Vl total das deduções de dependentes"
    "_TOTISEN"   : (cAlias)->TOTISEN                        ,; //30 "Total de Rendimentos Isentos"
    "_VBSIR"     : (cAlias)->VBSIR                          ,; //31 "Vl Rendimento Suspenso por processo judicial"
    "_VRSIR"     : (cAlias)->VRSIR                          ,; //32 "Vl Retencão não efetuada Processo Referenciado"
    "_TDEDUSUSP" : (cAlias)->TDEDUSUSP                      ,; //33 "Vl dedução suspensa"
    "_NPROCREF"  : Alltrim((cAlias)->NPROCREF)              ,; //34 "N° Processo Referenciado"
    "_CNPJPLS"   : Alltrim((cAlias)->CNPJPLS)               ,; //35 "CNPJ PLS"
    "_BENEPLS"   : Alltrim((cAlias)->BENEPLS)               ,; //36 "Beneficiário PLS"
    "_CPFDEPE"   : Alltrim((cAlias)->CPFDEPE)               ,; //37 "CPF Dependente"
    "_TIPPGTO"   : Alltrim((cAlias)->TIPPGTO)               ,; //38 "Tipo Pagamento"
    "_VPGTPLS"   : (cAlias)->VPGTPLS                        ,; //39 "Vl pago PLS"
    "_REEMBAN"   : (cAlias)->REEMBAN                        ,; //40 "Reembolso ano anterior"
    "_TPMEDIC"   : Alltrim((cAlias)->TPMEDIC)               ,; //41 "Tipo de Inscrição Médico"
    "_NINSMED"   : Alltrim((cAlias)->NINSMED)               ,; //42 "Nr. Inscrição Médico"
    "_PROTOCOL"  : cProtocol                                ,; //43 Protocolo Governo
    "_TOTGOV"    : nTotGov                                  }) //44 Total IR  Governo

    (cAlias)->( dbSkip() )
enddo

if oTabTemp <> Nil
    oTabTemp:Delete()
    oTabTemp := Nil
    freeobj(oTabTemp)
endif

    freeobj(oHashTot)
    oHashTot := Nil
    aSize(aTotalizer,0)

(cAlias)->( DBCloseArea() )

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Denis Souza
@since 03/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class ReinfBlock4010SmartViewBusinessObject

self:oSchema:addProperty( "_FILIAL"    , "Filial"	                                     , "string", "Filial"	                                     , "FILIAL"    , , , ) //01
self:oSchema:addProperty( "_RAZSOC"    , "Razão Social"                                  , "string", "Razão Social"                                  , "RAZSOC"    , , , ) //02
self:oSchema:addProperty( "_CNPJFIL"   , "CNPJ da filial"	                             , "string", "CNPJ da filial"	                             , "CNPJFIL"   , , , ) //03
self:oSchema:addProperty( "_CPF"       , "CPF Beneficiário"	                             , "string", "CPF Beneficiário"	                             , "CPF"       , , , ) //04
self:oSchema:addProperty( "_CODPAR"    , "Código participante"	                         , "string", "Código participante"	                         , "CODPAR"    , , , ) //05
self:oSchema:addProperty( "_INDNIF"    , "Indicativo NIF"	                             , "string", "Indicativo NIF"	                             , "INDNIF"    , , , ) //06
self:oSchema:addProperty( "_NIF"       , "N° NIF"	                                     , "string", "N° NIF"	                                     , "NIF"       , , , ) //07
self:oSchema:addProperty( "_FRMTRIB"   , "Forma Tributação"	                             , "string", "Forma Tributação"	                             , "FRMTRIB"   , , , ) //08
self:oSchema:addProperty( "_NOME"      , "Nome Beneficiário"	                         , "string", "Nome Beneficiário"	                         , "NOME"      , , , ) //09
self:oSchema:addProperty( "_TIPO"      , "Tipo"	                                         , "string", "Tipo"	                                         , "TIPO"      , , , ) //10
self:oSchema:addProperty( "_SERIEPARC" , "Série / Parcela"	                             , "string", "Série / Parcela"	                             , "SERIEPARC" , , , ) //11
self:oSchema:addProperty( "_DOCTO"     , "N° Documento"	                                 , "string", "N° Documento"	                                 , "DOCTO"     , , , ) //12
self:oSchema:addProperty( "_NUMITE"    , "N° do Item"	                                 , "string", "N° do Item"	                                 , "NUMITE"    , , , ) //13
self:oSchema:addProperty( "_DTEMISPGT" , "Data emissão/pagamento"	                     , "string", "Data emissão/pagamento"	                     , "DTEMISPGT" , , , ) //14
self:oSchema:addProperty( "_ID"        , "ID do documento"	                             , "string", "ID do documento"	                             , "ID"        , , , ) //15
self:oSchema:addProperty( "_CODNAT"    , "Natureza de Rendimento"	                     , "string", "Natureza de Rendimento"	                     , "CODNAT"    , , , ) //16
self:oSchema:addProperty( "_INDFCISCP" , "Indicativo de FCI/SCP"	                     , "string", "Indicativo de FCI/SCP"	                     , "INDFCISCP" , , , ) //17
self:oSchema:addProperty( "_CNPJFCISCP", "CNPJ da FCI/SCP"	                             , "string", "CNPJ da FCI/SCP"	                             , "CNPJFCISCP", , , ) //18
self:oSchema:addProperty( "_NRRRAPJD"  , "Info. de RRA ou Processo Judicial"	         , "string", "Info. de RRA ou Processo Judicial"	         , "NRRRAPJD"  , , , ) //19
self:oSchema:addProperty( "_INDRRA"    , "N° Inscrição processo RRA/Processo Judicial"   , "string", "N° Inscrição processo RRA/Processo Judicial"   , "INDRRA"    , , , ) //20
self:oSchema:addProperty( "_CNPJOR"    , "CNPJ Origem Recurso"	                         , "string", "CNPJ Origem Recurso"	                         , "CNPJOR"    , , , ) //21
self:oSchema:addProperty( "_CUSTADV"   , "Total de Custas com advogados"	             , "number", "Total de Custas com advogados"	             , "CUSTADV"   , , , ) //22
self:oSchema:addProperty( "_CUSTJUD"   , "Total de Custas Judiciais"	                 , "number", "Total de Custas Judiciais"	                 , "CUSTJUD"   , , , ) //23
self:oSchema:addProperty( "_VALBRUTO"  , "Vl do documento"	                             , "number", "Vl do documento"	                             , "VALBRUTO"  , , , ) //24
self:oSchema:addProperty( "_BASEIR"    , "Base de Cálculo IR"	                         , "number", "Base de Cálculo IR"	                         , "BASEIR"    , , , ) //25
self:oSchema:addProperty( "_ALIQIR"    , "Alíquota do IR"	                             , "number", "Alíquota do IR"	                             , "ALIQIR"    , , , ) //26
self:oSchema:addProperty( "_VALIR"     , "Vl do IR"	                                     , "number", "Vl do IR"	                                     , "VALIR"     , , , ) //27
self:oSchema:addProperty( "_TOTDEDU"   , "Total de deduções"                             , "number", "Total de deduções"                             , "TOTDEDU"   , , , ) //28
self:oSchema:addProperty( "_TDEDUDEP"  , "Vl total das deduções de dependentes"	         , "number", "Vl total das deduções de dependentes"	         , "TDEDUDEP"  , , , ) //29
self:oSchema:addProperty( "_TOTISEN"   , "Total de Rendimentos Isentos"	                 , "number", "Total de Rendimentos Isentos"	                 , "TOTISEN"   , , , ) //30
self:oSchema:addProperty( "_VBSIR"     , "Vl Rendimento Suspenso por processo judicial"  , "number", "Vl Rendimento Suspenso por processo judicial"  , "VBSIR"     , , , ) //31
self:oSchema:addProperty( "_VRSIR"     , "Vl Retencão não efetuada Processo Referenciado", "number", "Vl Retencão não efetuada Processo Referenciado", "VRSIR"     , , , ) //32
self:oSchema:addProperty( "_TDEDUSUSP" , "Vl dedução suspensa"                           , "number", "Vl dedução suspensa"                           , "TDEDUSUSP" , , , ) //33
self:oSchema:addProperty( "_NPROCREF"  , "N° Processo Referenciado"                      , "string", "N° Processo Referenciado"                      , "NPROCREF"  , , , ) //34
self:oSchema:addProperty( "_CNPJPLS"   , "CNPJ PLS"	                                     , "string", "CNPJ PLS"	                                     , "CNPJPLS"   , , , ) //35
self:oSchema:addProperty( "_BENEPLS"   , "Beneficiário PLS"	                             , "string", "Beneficiário PLS"	                             , "BENEPLS"   , , , ) //36
self:oSchema:addProperty( "_CPFDEPE"   , "CPF Dependente"                                , "string", "CPF Dependente"                                , "CPFDEPE"   , , , ) //37
self:oSchema:addProperty( "_TIPPGTO"   , "Tipo Pagamento"                                , "string", "Tipo Pagamento"                                , "TIPPGTO"   , , , ) //38
self:oSchema:addProperty( "_VPGTPLS"   , "Vl pago PLS"	                                 , "number", "Vl pago PLS"	                                 , "VPGTPLS"   , , , ) //39
self:oSchema:addProperty( "_REEMBAN"   , "Reembolso ano anterior"	                     , "number", "Reembolso ano anterior"	                     , "REEMBAN"   , , , ) //40
self:oSchema:addProperty( "_TPMEDIC"   , "Tipo de Inscrição Médico"	                     , "string", "Tipo de Inscrição Médico"	                     , "TPMEDIC"   , , , ) //41
self:oSchema:addProperty( "_NINSMED"   , "Nr. Inscrição Médico"                          , "string", "Nr. Inscrição Médico"                          , "NINSMED"   , , , ) //42
self:oSchema:addProperty( "_PROTOCOL"  , "Protocolo"                                     , "string", "Protocolo"                                     , "PROTOCOL"  , , , ) //43
self:oSchema:addProperty( "_TOTGOV"    , "Total Governo"                                 , "number", "Total Governo"                                 , "TOTGOV"    , , , ) //44

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} execQuery

Execução da query com paginação.
 
@return string

@author Denis Souza
@since 03/08/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method execQuery( cBd as character, aRootBranch as array, cYearMonth as character, nStart as numeric, nEnd as numeric, oTabTemp as object) as character class ReinfBlock4010SmartViewBusinessObject

local cAlias      := " " as character
local cQry        := " " as character
local cCompC1G    := " " as character
local cCompC1H    := " " as character
local cCompV3X    := " " as character
local cCompV4F    := " " as character
local cCompV3R    := " " as character
local aInfEUF     := {}  as array
local nlA         := 0  as numeric
local oPrepare          as object

Private aBind:= {}  as array

default cBd         := " "
default aRootBranch := {}
default cYearMonth  := " "
default nStart      := 0
default nEnd        := 0
default oTabTemp    := Nil

cCompC1G   := Upper(AllTrim(FWModeAccess("C1G",1)+FWModeAccess("C1G",2)+FWModeAccess("C1G",3)))
cCompC1H   := Upper(AllTrim(FWModeAccess("C1H",1)+FWModeAccess("C1H",2)+FWModeAccess("C1H",3)))
cCompV3X   := Upper(AllTrim(FWModeAccess("V3X",1)+FWModeAccess("V3X",2)+FWModeAccess("V3X",3)))
cCompV4F   := Upper(AllTrim(FWModeAccess("V4F",1)+FWModeAccess("V4F",2)+FWModeAccess("V4F",3)))
cCompV3R   := Upper(AllTrim(FWModeAccess("V3R",1)+FWModeAccess("V3R",2)+FWModeAccess("V3R",3)))
aInfEUF    := TAFTamEUF(Upper(AllTrim(SM0->M0_LEIAUTE)))

cFilsC20 := TafRetFilC("C20", aRootBranch )
cFilsLEM := TafRetFilC("LEM", aRootBranch )
cFilsV3U := TafRetFilC("V3U", aRootBranch )
cFilsV4B := TafRetFilC("V4B", aRootBranch )

cQry := SvRel4010(cBd,nStart,nEnd,cYearMonth,cCompC1G,cCompC1H,cCompV3X,cCompV4F,cCompV3R,cFilsC20,cFilsLEM,cFilsV3U,cFilsV4B,aInfEUF,@oTabTemp) 


oPrepare := FwExecStatement():New( cQry )
for nlA := 1 to len(aBind)
    if Valtype(aBind[nlA]) == 'A'
        oPrepare:setIn( nlA, aBind[nlA] )
    else
        oPrepare:setString( nlA, aBind[nlA] )
    endif
next nlA

cAlias := GetNextAlias()
oPrepare:OpenAlias( cAlias )

Return cAlias

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} SvRel4010
Retorna a query para geração da visao

@author Denis Souza
@since 08/08/2023
@version 1.0 
/*/
//---------------------------------------------------------------------------------------------------------------
Static function SvRel4010(cBd,nStart,nEnd,cYearMonth,cCompC1G,cCompC1H,cCompV3X,cCompV4F,cCompV3R,cFilsC20,cFilsLEM,cFilsV3U,cFilsV4B,aInfEUF,oTabTemp)

	local cQry       := " " as character
	local cStartDate := " " as character
	local cEndDate   := " " as character
	local nV3SIdNatR := 0   as numeric
	local nC30CNatRe := 0   as numeric
	local nV3VCNatRe := 0   as numeric

	default cBd        := ''
	Default oPrepare := Nil
	default nStart     := 0
	default nEnd       := 0
	default cYearMonth := ''
	default cCompC1G   := ''
	default cCompC1H   := ''
	default cCompV3X   := ''
	default cCompV4F   := ''
	default cCompV3R   := ''
	default cFilsC20   := ''
	default cFilsLEM   := ''
	default cFilsV3U   := ''
	default cFilsV4B   := ''
	default aInfEUF    := {}
	default oTabTemp   := nil

	cStartDate := cYearMonth + "01" //ex: 20220201
	cEndDate   := DtoS( LastDay( StoD( cStartDate ) ) )
	nV3SIdNatR := GetSx3Cache( "V3S_IDNATR", 'X3_TAMANHO' )
	nC30CNatRe := GetSx3Cache( "C30_CNATRE", 'X3_TAMANHO' )
	nV3VCNatRe := GetSx3Cache( "V3V_CNATRE", 'X3_TAMANHO' )

//Bloco com campos provenientes das notas
	cQry += SvTafNota(cBd,cStartDate,cEndDate,aInfEUF,cCompC1G,cCompC1H,cCompV3X,nC30CNatRe,cFilsC20) + " UNION ALL "
//Bloco com campos provenientes das faturas
	cQry += SvTafFat(cBd,cStartDate,cEndDate,aInfEUF,cCompC1G,cCompC1H,cCompV3X,cCompV4F,nV3SIdNatR,cFilsC20,cFilsLEM) + " UNION ALL "
//Bloco com campos provenientes dos pagamentos
	cQry += SvTafPgt(cBd,cStartDate,cEndDate,aInfEUF,cCompC1G,cCompC1H,cCompV3X,cCompV4F,nV3VCNatRe,cFilsV3U,cFilsC20)
//Bloco com campos provenientes dos reembolsos \ plano de saude
	cQry += " UNION ALL " + SvTafPls(@oTabTemp,cQry,cBd,cStartDate,cEndDate,aInfEUF,cCompC1H,cCompV3R,cFilsV4B)

	cQry := iif(!("DB2" $ cBd), ChangeQuery(cQry), StrTran(cQry,"  "," "))

Return cQry

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} SvTafNota

@author Denis Souza
@since 08/08/2023
@version 1.0 
/*/
//---------------------------------------------------------------------------------------------------------------
Static Function SvTafNota(cBd,cStartDate,cEndDate,aInfEUF,cCompC1G,cCompC1H,cCompV3X,nC30CNatRe,cFilsC20)

	local cQry := " " as character
	local aRoot        := {}  as array

	default cBd        := " "
	default cStartDate := " "
	default cEndDate   := " "
	default aInfEUF    := {}
	default cCompC1G   := " "
	default cCompC1H   := " "
	default cCompV3X   := " "
	default nC30CNatRe := " "
	default cFilsC20   := " "

	cFilsC20 := StrTran(StrTran(StrTran(cFilsC20,"(",""),")",""),"'","")

	aSize(aRoot,0)
	aRoot := Separa( cFilsC20, "," )

	cQry := "SELECT C20.C20_FILIAL FILIAL, C1H.C1H_CPF CPF, C1H.C1H_CODPAR CODPAR "
	cQry += ",CASE WHEN C1H.C1H_INDNIF = ? THEN 'Possui' WHEN C1H.C1H_INDNIF = ? THEN 'Dispensado' WHEN C1H.C1H_INDNIF = ? THEN 'Pais nao exige' ELSE 'Nao Informado' END INDNIF "
	cQry += ",C1H.C1H_NIF NIF, " + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(T9A.T9A_DESCRI,'') FRMTRIB, C1H.C1H_NOME NOME, 'NFS' ROTINA, C20.C20_SERIE SERIEPARC, C20.C20_NUMDOC DOCTO "
	cQry += ",C30.C30_NUMITE NUMITE, C20.C20_DTDOC DTEMISPGT, C20.C20_CHVNF ID, V3O.V3O_CODIGO CODNAT, CASE WHEN C20.C20_FCISCP = ? THEN 'FCI' WHEN C20.C20_FCISCP = ? THEN 'SCP' ELSE 'Nao Informado' END INDFCISCP "
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V3X.V3X_CNPJ, '') CNPJFCISCP, '' NRRRAPJD, '' INDRRA, '' CNPJOR, 0 CUSTADV, 0 CUSTJUD "
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(SUM(C30.C30_TOTAL), 0) VALBRUTO " //22 Valor do documento
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(SUM(C35.C35_BASE), 0) BASEIR "	  //23 Base de Cálculo IR
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(C35.C35_ALIQ, 0) ALIQIR "		  //24 Alíquota do IR
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(SUM(C35.C35_VALOR), 0) VALIR "	  //25 Valor do IR
	aadd(aBind, '1')
	aadd(aBind, '2')
	aadd(aBind, '3')
	aadd(aBind, '1')
	aadd(aBind, '2')

//Detalhamento das Deduções
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V4C.V4C_VLRDED) FROM " + RetSqlName("V4C") + " V4C WHERE " + FwJoinFilial("V4C","C20") + " AND V4C.V4C_CHVNF = C20.C20_CHVNF "
	cQry += "AND V4C.V4C_NUMITE = C30.C30_NUMITE AND V4C.V4C_CODITE = C30.C30_CODITE AND V4C.V4C_CODTRI = C35.C35_CODTRI "
	cQry += "AND V4C.V4C_CODTRI = ? AND V4C.D_E_L_E_T_ = ?),0) TOTDEDU " //Total de deduções
	aadd(aBind, '000012')
	aadd(aBind, space(1))

//Beneficiários da Pensão
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V84.V84_VALDED) FROM " + RetSqlName("V84") + " V84 WHERE " + FwJoinFilial("V84","C20") + " AND V84.V84_CHVNF = C20.C20_CHVNF "
	cQry += "AND V84.V84_IDPART = C20.C20_CODPAR AND V84.V84_NUMITE = C30.C30_NUMITE AND V84.V84_CODITE = C30.C30_CODITE AND V84.V84_CODTRI = C35.C35_CODTRI "
	cQry += "AND V84.V84_CODTRI = ? AND V84.D_E_L_E_T_ = ?),0) TDEDUDEP " //Valor total das deduções de dependentes
	aadd(aBind, '000012')
	aadd(aBind, space(1))

//Isenções
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V4D.V4D_VLRISE) FROM " + RetSqlName("V4D") + " V4D WHERE " + FwJoinFilial("V4D","C20") + " AND V4D.V4D_CHVNF = C20.C20_CHVNF "
	cQry += "AND V4D.V4D_NUMITE = C30.C30_NUMITE AND V4D.V4D_CODITE = C30.C30_CODITE AND V4D.V4D_CODTRI = C35.C35_CODTRI "
	cQry += "AND V4D.V4D_CODTRI = ? AND V4D.D_E_L_E_T_ = ?),0) TOTISEN " //Total de Rendimentos Isentos
	aadd(aBind, '000012')
	aadd(aBind, space(1))

//Ind. Sus. Proc. Judicial / Adm
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(T9Q.T9Q_BSSUSP) FROM " + RetSqlName("T9Q") + " T9Q WHERE " + FwJoinFilial("T9Q","C20") + " AND T9Q.T9Q_CHVNF = C20.C20_CHVNF "
	cQry += "AND T9Q.T9Q_NUMITE = C30.C30_NUMITE AND T9Q.T9Q_CODTRI = ? AND T9Q.D_E_L_E_T_ = ?),0) VBSIR " //Valor do Rendimento Suspenso por processo judicial
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(T9Q.T9Q_VALSUS) FROM " + RetSqlName("T9Q") + " T9Q WHERE " + FwJoinFilial("T9Q","C20") + " AND T9Q.T9Q_CHVNF = C20.C20_CHVNF "
	cQry += "AND T9Q.T9Q_NUMITE = C30.C30_NUMITE AND T9Q.T9Q_CODTRI = ? AND T9Q.D_E_L_E_T_ = ?),0) VRSIR " //Valor da retenção nao efetuada devido Processo Ref
	aadd(aBind, '000012')
	aadd(aBind, space(1))
	aadd(aBind, '000012')
	aadd(aBind, space(1))

//Det. deducoes Exig. Susp
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V88.V88_VALDED) FROM " + RetSqlName("V88") + " V88 WHERE " + FwJoinFilial("V88","C20") + " AND V88.V88_CHVNF = C20.C20_CHVNF "
	cQry += "AND V88.V88_NUMITE = C30.C30_NUMITE AND V88.V88_IDPROC = C30.C30_CODITE AND V88.V88_CODTRI = ? AND V88.D_E_L_E_T_ = ?),0) TDEDUSUSP " //Valor da dedução suspensa
	aadd(aBind, '000012')
	aadd(aBind, space(1))

//Processos referenciados
	cQry += iif(!("INFORMIX" $ cBd) , ",COALESCE(( " , ",NVL(( ")
	cQry += iif( !( cBd $ ( "INFORMIX|ORACLE|OPENEDGE|POSTGRES|DB2" ) ), "SELECT TOP 1 C1G.C1G_NUMPRO " , "SELECT C1G.C1G_NUMPRO " )
	cQry += " FROM " + RetSqlName("T9Q") + " T9Q INNER JOIN " + RetSqlName("C1G") + " C1G ON "

	if cCompC1G == "CCC" .Or. (cCompC1G == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " C1G.C1G_FILIAL = ? "
		AADD(aBind,xFilial("C1G")) //C1G_FILIAL
	else
		cQry += FwJoinFilial("C1G","T9Q")
	endif

	cQry += " AND C1G.C1G_ID = T9Q.T9Q_NUMPRO AND C1G.D_E_L_E_T_ = ? "
	cQry += "WHERE " + FwJoinFilial("T9Q","C20") + " AND T9Q.T9Q_CHVNF = C20.C20_CHVNF AND T9Q.T9Q_NUMITE = C30.C30_NUMITE AND T9Q.T9Q_ID = C30.C30_CODITE AND T9Q.T9Q_CODTRI = ? AND T9Q.D_E_L_E_T_ = ? "
	cQry += iif(cBd == "ORACLE"," AND ROWNUM <= 1 ",iif(cBd $ "POSTGRES|DB2"," LIMIT 1 ",""))
	cQry += " ),'') NPROCREF, ' ' CNPJPLS, ' ' BENEPLS, ' ' CPFDEPE, ' ' TIPPGTO, 0 VPGTPLS, 0 REEMBAN, ' ' TPMEDIC, ' ' NINSMED "
	aadd(aBind, space(1))
	aadd(aBind, '000012')
	aadd(aBind, space(1))

//Documento Fiscal
	cQry += " FROM " + RetSqlName("C20") + " C20 "
//participante
	cQry += " INNER JOIN " + RetSqlName("C1H") + " C1H ON "

	if cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " C1H.C1H_FILIAL = ? "
		aadd(aBind,xFilial("C1H")) //C1H_FILIAL
	else
		cQry += FwJoinFilial("C1H","C20")
	endif

	cQry += " AND C1H.C1H_ID = C20.C20_CODPAR AND ((C1H.C1H_CPF <> ? AND C1H.C1H_PPES = ?)  OR (C1H.C1H_PEEXTE = ? AND C1H.C1H_PAISEX <> ?) ) AND C1H.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))
	aadd(aBind, "1")
	aadd(aBind, "1")
	aadd(aBind, space(1))
	aadd(aBind, space(1))

//forma de tributacao
	cQry += " LEFT JOIN " + RetSqlName("T9A") + " T9A ON T9A.T9A_FILIAL = ? AND T9A.T9A_ID = C1H.C1H_IDTRIB AND T9A.D_E_L_E_T_ = ? "
	aadd(aBind, xFilial("T9A"))
	aadd(aBind, space(1))

//Itens da nota fiscal natureza rendimento
	cQry += " INNER JOIN " + RetSqlName("C30") + " C30 ON " +FwJoinFilial("C30","C20")+ " AND C30.C30_CHVNF = C20.C20_CHVNF AND C30.C30_CNATRE <> ? AND C30.D_E_L_E_T_ = ? "
	aadd(aBind, Space(nC30CNatRe))
	aadd(aBind, space(1))

//Natureza de operacao
	cQry += " INNER JOIN " + RetSqlName("V3O") + " V3O ON V3O.V3O_FILIAL = ? AND V3O.V3O_ID = C30.C30_CNATRE AND V3O.D_E_L_E_T_ = ? "
	aadd(aBind, xFilial("V3O"))
	aadd(aBind, space(1))

//FCI e SCP
	cQry += " LEFT JOIN " + RetSqlName("V3X") + " V3X ON "
	if cCompV3X == "CCC" .Or. (cCompV3X == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " V3X.V3X_FILIAL = ? "
		aadd(aBind,xFilial("V3X")) //V3X_FILIAL
	else
		cQry += FwJoinFilial("V3X","C20")
	endif
	cQry += " AND V3X.V3X_ID = C30.C30_IFCISC AND V3X.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))

//Tributos do Item
	cQry += " INNER JOIN " + RetSqlName("C35") + " C35 ON " + FwJoinFilial("C35","C30") + " AND C35.C35_CHVNF = C30.C30_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODITE = C30.C30_CODITE AND C35.C35_CODTRI = ? AND C35.D_E_L_E_T_ = ? "
	aadd(aBind, "000012")
	aadd(aBind, space(1))

//Fatura
	cQry += " LEFT JOIN " + RetSqlName("LEM") + " LEM ON " + FwJoinFilial("LEM","C20") + " AND LEM.LEM_DOCORI = C20.C20_CHVNF AND LEM.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))

	cQry += " WHERE "

	If "TMPFIL" $ aRoot[1]
		cQry += " C20.C20_FILIAL IN (" + aRoot[1] + ") "
	Else
		cQry += " C20.C20_FILIAL IN (?) "
		aadd(aBind, aRoot)
	EndIf

	cQry += " AND C20.C20_INDOPE = ? AND C20.C20_DTDOC BETWEEN ? AND ? AND LEM.LEM_DOCORI IS NULL AND C20.D_E_L_E_T_ = ? "
	cQry += " GROUP BY C20.C20_FILIAL, C20.C20_CHVNF, C20.C20_NUMDOC, C20.C20_SERIE, C20.C20_DTDOC, C20.C20_FCISCP, C20.C20_CODPAR, C1H.C1H_CPF, C1H.C1H_CODPAR, C1H.C1H_INDNIF, C1H.C1H_NIF, C1H.C1H_NOME, C1H.C1H_PAISEX, "
	cQry += " T9A.T9A_DESCRI, V3X.V3X_CNPJ, C30.C30_NUMITE, C30.C30_CODITE, V3O.V3O_CODIGO, C35.C35_CODTRI, C35.C35_ALIQ "
	aadd(aBind, "0")
	aadd(aBind, cStartDate)
	aadd(aBind, cEndDate)
	aadd(aBind, space(1))

Return cQry

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} SvTafFat

@author Denis Souza
@since 08/08/2023
@version 1.0 
/*/
//---------------------------------------------------------------------------------------------------------------
Static Function SvTafFat(cBd,cStartDate,cEndDate,aInfEUF,cCompC1G,cCompC1H,cCompV3X,cCompV4F,nV3SIdNatR,cFilsC20,cFilsLEM)

	local cQry    := " " as character
	local cConcat := " " as character
	local aRoot        := {}  as array

	default cBd        := " "
	default cStartDate := " "
	default cEndDate   := " "
	default aInfEUF    := {}
	default cCompC1G   := " "
	default cCompC1H   := " "
	default cCompV3X   := " "
	default cCompV4F   := " "
	default nV3SIdNatR := 0
	default cFilsC20   := " "
	default cFilsLEM   := " "

	cFilsLEM := StrTran(StrTran(StrTran(cFilsLEM,"(",""),")",""),"'","")

	aSize(aRoot,0)
	aRoot := Separa( cFilsLEM, "," )

	cConcat := iif( !( cBd $ ( "INFORMIX|ORACLE|OPENEDGE|POSTGRES|DB2" ) ), ' + ', ' || ' ) //tratamento do concat devido tcsqlexec nao converte com o padrao || no sql

	cQry := " SELECT FAT.FILIAL, FAT.CPF, FAT.CODPAR, FAT.INDNIF, FAT.NIF, FAT.FRMTRIB, FAT.NOME, FAT.ROTINA, FAT.SERIEPARC, FAT.DOCTO, FAT.NUMITE, FAT.DTEMISPGT, FAT.ID, FAT.CODNAT, FAT.INDFCISCP, FAT.CNPJFCISCP, FAT.NRRRAPJD, FAT.INDRRA, FAT.CNPJOR "
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V4GADV.V4G_VLDESP) FROM " + RetSqlName("V4F") + " V4F INNER JOIN " + RetSqlName("V4G") + " V4GADV ON " + FwJoinFilial("V4G","V4F","V4GADV","V4F")
	cQry += " AND V4GADV.V4G_ID = V4F.V4F_ID AND V4GADV.V4G_TPDESP = ? AND V4GADV.V4G_DTDESP BETWEEN ? AND ? AND V4GADV.D_E_L_E_T_ = ? WHERE "
	cQry += SvTmpBranch(cBd,cCompV4F,aInfEUF,"V4F","V4F.V4F_FILIAL","FAT.FILIAL"," V4F.V4F_ID = FAT.IDRRAPJUD ",.T.) + " ), 0) CUSTADV " //Total de Custas com advogados
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V4GJUD.V4G_VLDESP) FROM " + RetSqlName("V4F") + " V4F INNER JOIN " + RetSqlName("V4G") + " V4GJUD ON " + FwJoinFilial("V4G","V4F","V4GJUD","V4F")
	cQry += " AND V4GJUD.V4G_ID = V4F.V4F_ID AND V4GJUD.V4G_TPDESP = ? AND V4GJUD.V4G_DTDESP BETWEEN ? AND ? AND V4GJUD.D_E_L_E_T_ = ? WHERE "
	cQry += SvTmpBranch(cBd,cCompV4F,aInfEUF,"V4F","V4F.V4F_FILIAL","FAT.FILIAL"," V4F.V4F_ID = FAT.IDRRAPJUD ",.T.) + " ), 0) CUSTJUD " //Total de Custas Judiciais
	cQry += " ,FAT.VALBRUTO, FAT.BASEIR, FAT.ALIQIR, FAT.VALIR, FAT.TOTDEDU, FAT.TDEDUDEP, FAT.TOTISEN, FAT.VBSIR, FAT.VRSIR, FAT.TDEDUSUSP, FAT.NPROCREF, FAT.CNPJPLS, FAT.BENEPLS, FAT.CPFDEPE, FAT.TIPPGTO, FAT.VPGTPLS, FAT.REEMBAN, FAT.TPMEDIC, FAT.NINSMED "
	aadd(aBind, "1")
	aadd(aBind, cStartDate)
	aadd(aBind, cEndDate)
	aadd(aBind, space(1))
	aadd(aBind, "2")
	aadd(aBind, cStartDate)
	aadd(aBind, cEndDate)
	aadd(aBind, space(1))

	cQry += " FROM ( "
	cQry += " SELECT LEM.LEM_FILIAL FILIAL, C1H.C1H_CPF CPF, C1H.C1H_CODPAR CODPAR, CASE WHEN C1H.C1H_INDNIF = ? THEN 'Possui' WHEN C1H.C1H_INDNIF = ? THEN 'Dispensado' WHEN C1H.C1H_INDNIF = ? THEN 'Pais nao exige' ELSE 'Nao Informado' END INDNIF "
	cQry += " ,C1H.C1H_NIF NIF, " + iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(T9A.T9A_DESCRI, '') FRMTRIB, C1H.C1H_NOME NOME, 'FAT' ROTINA, ' ' SERIEPARC, LEM.LEM_NUMERO DOCTO, ' ' NUMITE, LEM.LEM_DTEMIS DTEMISPGT, LEM.LEM_ID ID, V3O.V3O_CODIGO CODNAT "
	cQry += " ,CASE WHEN LEM.LEM_FCISCP = ? THEN 'FCI' WHEN LEM.LEM_FCISCP = ? THEN 'SCP' ELSE 'Nao Informado' END INDFCISCP, " + iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V3X.V3X_CNPJ, '') CNPJFCISCP "
	cQry += " ,V3S.V3S_IDPROC IDRRAPJUD "
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V4F.V4F_NRPROC, '') NRRRAPJD "
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "( CASE WHEN V4F.V4F_INDRRA = ? THEN 'Sim' ELSE 'Nao' END , '') INDRRA "
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V4F.V4F_CNPJOR, '') CNPJOR "
	cQry += ",LEM.LEM_VLBRUT VALBRUTO "                                                         //Valor do documento
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(SUM(V47.V47_BASECA),0) BASEIR "	//Base de Cálculo IR
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V47.V47_ALIQ, 0) ALIQIR "		//Alíquota do IR
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(SUM(V47.V47_VLTRIB),0) VALIR "	//Valor do IR
	aadd(aBind, "1")
	aadd(aBind, "2")
	aadd(aBind, "3")
	aadd(aBind, "1")
	aadd(aBind, "2")
	aadd(aBind, "1")

//Detalhamento das Deduções
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V4L.V4L_VLRDED) FROM " + RetSqlName("V4L") + " V4L WHERE " + FwJoinFilial("V4L","LEM") + " AND V4L.V4L_ID = LEM.LEM_ID "
	cQry += " AND V4L.V4L_IDPART = LEM.LEM_IDPART AND V4L.V4L_NUMFAT = LEM.LEM_NUMERO AND V4L.V4L_IDNATR = V3S.V3S_IDNATR "
	cQry += " AND V4L.V4L_DECTER = V3S.V3S_DECTER AND V4L.V4L_IDTRIB = V47.V47_IDTRIB AND V4L.V4L_IDTRIB = ? AND V4L.D_E_L_E_T_ = ?),0) TOTDEDU " //Total de deduções
	aadd(aBind, "000012")
	aadd(aBind, space(1))

//Beneficiários da pensão alimen
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V83.V83_VALDED) FROM " + RetSqlName("V83") + " V83 WHERE " + FwJoinFilial("V83","LEM") + " AND V83.V83_ID = LEM.LEM_ID "
	cQry += " AND V83.V83_IDPART = LEM.LEM_IDPART AND V83.V83_NUMFAT = LEM.LEM_NUMERO AND V83.V83_IDNATR = V3S.V3S_IDNATR AND V83.V83_DECTER = V3S.V3S_DECTER "
	cQry += " AND V83.V83_IDTRIB = V47.V47_IDTRIB AND V83.V83_IDTRIB = ? AND V83.D_E_L_E_T_ = ?),0) TDEDUDEP " //Valor total das deduções de dependentes
	aadd(aBind, "000012")
	aadd(aBind, space(1))

//Detalhamento das Isenções
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V4M.V4M_VLRISE) FROM " + RetSqlName("V4M") + " V4M WHERE " + FwJoinFilial("V4M","LEM") + " AND V4M.V4M_ID = LEM.LEM_ID "
	cQry += " AND V4M.V4M_IDPART = LEM.LEM_IDPART AND V4M.V4M_NUMFAT = LEM.LEM_NUMERO AND V4M.V4M_IDNATR = V3S.V3S_IDNATR "
	cQry += " AND V4M.V4M_DECTER = V3S.V3S_DECTER AND V4M.V4M_IDTRIB = V47.V47_IDTRIB AND V4M.V4M_IDTRIB = ? AND V4M.D_E_L_E_T_ = ?),0) TOTISEN " //Total de Rendimentos Isentos
	aadd(aBind, "000012")
	aadd(aBind, space(1))

//Indicativo Suspensão Processo
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(T9E.T9E_BSSUSP) FROM " + RetSqlName("T9E") + " T9E WHERE " + FwJoinFilial("T9E","LEM") + " AND T9E.T9E_ID = LEM.LEM_ID AND T9E.T9E_IDPART = LEM.LEM_IDPART "
	cQry += " AND T9E.T9E_NUMFAT = LEM.LEM_NUMERO AND T9E.T9E_CNATRE = V3S.V3S_IDNATR AND T9E.T9E_CODTRI = ? AND T9E.D_E_L_E_T_ = ?),0) VBSIR " //Valor do Rendimento Suspenso por processo judicial
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(T9E.T9E_VALSUS) FROM " + RetSqlName("T9E") + " T9E WHERE " + FwJoinFilial("T9E","LEM") + " AND T9E.T9E_ID = LEM.LEM_ID AND T9E.T9E_IDPART = LEM.LEM_IDPART "
	cQry += " AND T9E.T9E_NUMFAT = LEM.LEM_NUMERO AND T9E.T9E_CNATRE = V3S.V3S_IDNATR AND T9E.T9E_CODTRI = ? AND T9E.D_E_L_E_T_ = ?),0) VRSIR " //Valor da retenção nao efetuada devido Processo Ref
	aadd(aBind, "000012")
	aadd(aBind, space(1))
	aadd(aBind, "000012")
	aadd(aBind, space(1))

//Detalhamento das deduções
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V89.V89_VALDED) FROM " + RetSqlName("V89") + " V89 WHERE  " + FwJoinFilial("V89","LEM") + " AND V89.V89_ID = LEM.LEM_ID "
	cQry += " AND V89.V89_IDPT9E = LEM.LEM_IDPART AND V89.V89_NFAT9E = LEM.LEM_NUMERO AND V89.V89_CODTRI = ? AND V89.V89_TPPROC"+cConcat+"V89.V89_NUMPRO"+cConcat+"V89.V89_IDSUSP IN ( "
	cQry += "SELECT DISTINCT T9E.T9E_TPPROC"+cConcat+"T9E.T9E_NUMPRO"+cConcat+"T9E.T9E_IDSUSP "
	cQry += "FROM " + RetSqlName("T9E") + " T9E WHERE " + FwJoinFilial("T9E","LEM") + " AND T9E.T9E_ID = LEM.LEM_ID AND T9E.T9E_IDPART = LEM.LEM_IDPART "
	cQry += "AND T9E.T9E_NUMFAT = LEM.LEM_NUMERO AND T9E.T9E_CNATRE = V3S.V3S_IDNATR AND T9E.T9E_CODTRI = ? AND T9E.D_E_L_E_T_ = ?) "
	cQry += "AND V89.D_E_L_E_T_ = ?),0) TDEDUSUSP " //Valor da dedução suspensa (Diferente da nota, onde V88 está por item, do pagamento, onde V90 está por natureza a V89 da fatura nao possui vinculo direto com a natureza)
	aadd(aBind, "000012")
	aadd(aBind, "000012")
	aadd(aBind, space(1))
	aadd(aBind, space(1))

//Processos Referenciados
	cQry += iif(!("INFORMIX" $ cBd),",COALESCE(( " , ",NVL(( ")
	cQry += iif( !( cBd $ ( "INFORMIX|ORACLE|OPENEDGE|POSTGRES|DB2" ) ), " SELECT TOP 1 C1G.C1G_NUMPRO ", " SELECT C1G.C1G_NUMPRO ")

	cQry += " FROM " + RetSqlName("T9E") + " T9E INNER JOIN " + RetSqlName("C1G") + " C1G ON "
	if cCompC1G == "CCC" .Or. (cCompC1G == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " C1G.C1G_FILIAL = ? "
		AADD(aBind, xFilial("C1G")) //C1G_FILIAL
	else
		cQry += FwJoinFilial("C1G","T9E")
	endif
	cQry += " AND C1G.C1G_ID = T9E.T9E_NUMPRO AND C1G.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))

	cQry += " WHERE " + FwJoinFilial("T9E","LEM") + " AND T9E.T9E_ID = LEM.LEM_ID AND T9E.T9E_IDPART = LEM.LEM_IDPART AND T9E.T9E_NUMFAT = LEM.LEM_NUMERO AND T9E.T9E_CNATRE = V3S.V3S_IDNATR AND T9E.T9E_CODTRI = ? AND T9E.D_E_L_E_T_ = ? "
	cQry += iif( cBd == "ORACLE", " AND ROWNUM <= 1 ", iif(cBd $ "POSTGRES|DB2"," LIMIT 1 ","") ) + " ),'') NPROCREF "
	cQry += ",' ' CNPJPLS, ' ' BENEPLS, ' ' CPFDEPE, ' ' TIPPGTO, 0 VPGTPLS, 0 REEMBAN, ' ' TPMEDIC, ' ' NINSMED "
	aadd(aBind, "000012")
	aadd(aBind, space(1))

//Fatura
	cQry += " FROM " + RetSqlName("LEM") + " LEM "

//Participante
	cQry += " INNER JOIN " + RetSqlName("C1H") + " C1H ON "
	if cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " C1H.C1H_FILIAL = ? "
		AADD(aBind, xFilial("C1H")) //C1H_FILIAL
	else
		cQry += FwJoinFilial("C1H","LEM")
	endif

	cQry += " AND C1H.C1H_ID = LEM.LEM_IDPART AND ((C1H.C1H_CPF <> ? AND C1H.C1H_PPES = ?)  OR (C1H.C1H_PEEXTE = ? AND C1H.C1H_PAISEX <> ?) ) AND C1H.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))
	aadd(aBind, "1")
	aadd(aBind, "1")
	aadd(aBind, space(1))
	aadd(aBind, space(1))

	//forma de tributacao
	cQry += " LEFT JOIN " + RetSqlName("T9A") + " T9A ON T9A.T9A_FILIAL = ? AND T9A.T9A_ID = C1H.C1H_IDTRIB AND T9A.D_E_L_E_T_ = ? "
	aadd(aBind, xFilial("T9A"))
	aadd(aBind, space(1))

//Tributos X Natureza Rendimento
	cQry += " INNER JOIN " + RetSqlName("V3S") + " V3S ON " + FwJoinFilial("V3S","LEM") + " AND V3S.V3S_ID = LEM.LEM_ID AND V3S.V3S_IDPART = LEM.LEM_IDPART AND V3S.V3S_NUMFAT = LEM.LEM_NUMERO AND V3S.V3S_IDNATR <> ? AND V3S.D_E_L_E_T_ = ? "
	aadd(aBind, Space(nV3SIdNatR))
	aadd(aBind, space(1))

//Natureza de rendimento
	cQry += " INNER JOIN " + RetSqlName("V3O") + " V3O ON V3O.V3O_FILIAL = ? AND V3O.V3O_ID = V3S.V3S_IDNATR AND V3O.D_E_L_E_T_ = ? "
	aadd(aBind, xFilial("V3O"))
	aadd(aBind, space(1))

//Despesas Processuais e RRA
	cQry += " LEFT JOIN " + RetSqlName("V4F") + " V4F ON "
	if cCompV4F == "CCC" .Or. (cCompV4F == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " V4F.V4F_FILIAL = ? "
		AADD(aBind, xFilial("V4F")) //V4F_FILIAL
	else
		cQry += FwJoinFilial("V4F","V3S")
	endif
	cQry += " AND V4F.V4F_ID = V3S.V3S_IDPROC AND V4F.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))

//FCI e SCP
	cQry += " LEFT JOIN " + RetSqlName("V3X") + " V3X ON "
	if cCompV3X == "CCC" .Or. (cCompV3X == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " V3X.V3X_FILIAL = ? "
		AADD(aBind, xFilial("V3X")) //V3X_FILIAL
	else
		cQry += FwJoinFilial("V3X","V3S")
	endif

	cQry += " AND V3X.V3X_ID = V3S.V3S_IFCISC AND V3X.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))

//Tributos
	cQry += " INNER JOIN " + RetSqlName("V47") + " V47 ON " + FwJoinFilial("V47","LEM") + " AND V47.V47_ID = LEM.LEM_ID AND V47.V47_IDPART = LEM.LEM_IDPART AND V47.V47_NUMFAT = LEM.LEM_NUMERO AND V47.V47_IDNATR = V3S.V3S_IDNATR AND V47.V47_DECTER = V3S.V3S_DECTER AND V47.V47_IDTRIB = ? AND V47.D_E_L_E_T_ = ? "
	aadd(aBind, "000012")
	aadd(aBind, space(1))

	cQry += " WHERE "
	
	If "TMPFIL" $ aRoot[1]
		cQry += " LEM.LEM_FILIAL IN (" + aRoot[1] + ") "
	Else
		cQry += " LEM.LEM_FILIAL IN (?) "
		aadd(aBind, aRoot)
	EndIF

	cQry += " AND LEM.LEM_DTEMIS BETWEEN ? AND ? AND LEM.LEM_NATTIT = ? AND LEM.D_E_L_E_T_ = ? "
	cQry += " GROUP BY LEM.LEM_FILIAL, LEM.LEM_ID, LEM.LEM_NUMERO, LEM.LEM_DTEMIS, LEM.LEM_FCISCP, LEM.LEM_VLBRUT, LEM.LEM_IDPART, C1H.C1H_CPF, C1H.C1H_CODPAR, C1H.C1H_INDNIF, C1H.C1H_NIF, C1H.C1H_NOME, C1H.C1H_PAISEX, "
	cQry += " T9A.T9A_DESCRI, V3X.V3X_CNPJ, V3S.V3S_IDPROC, V3S.V3S_IDNATR, V3S.V3S_DECTER, V4F.V4F_NRPROC, V4F.V4F_INDRRA, V4F.V4F_CNPJOR, V3O.V3O_CODIGO, V47.V47_IDTRIB, V47.V47_ALIQ ) FAT "
	aadd(aBind, cStartDate)
	aadd(aBind, cEndDate)
	aadd(aBind, "0")
	aadd(aBind, space(1))

Return cQry

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} SvTafPgt

@author Denis Souza
@since 08/08/2023
@version 1.0
/*/
//---------------------------------------------------------------------------------------------------------------
Static Function SvTafPgt(cBd,cStartDate,cEndDate,aInfEUF,cCompC1G,cCompC1H,cCompV3X,cCompV4F,nV3VCNatRe,cFilsV3U,cFilsC20)

	local cQry := " " as character
	local aRoot        := {}  as array

	default cBd         := " "
	default cStartDate  := " "
	default cEndDate    := " "
	default aInfEUF     := { }
	default cCompC1G    := " "
	default cCompC1H    := " "
	default cCompV3X    := " "
	default cCompV4F    := " "
	default nV3VCNatRe  := 0
	default cFilsV3U    := " "
	default cFilsC20    := " "

	cFilsV3U := StrTran(StrTran(StrTran(cFilsV3U,"(",""),")",""),"'","")

	aSize(aRoot,0)
	aRoot := Separa( cFilsV3U, "," )

	cQry := "SELECT PGT.FILIAL, PGT.CPF, PGT.CODPAR, PGT.INDNIF, PGT.NIF, PGT.FRMTRIB, PGT.NOME, PGT.ROTINA, PGT.SERIEPARC, PGT.DOCTO, PGT.NUMITE, PGT.DTEMISPGT, PGT.ID, PGT.CODNAT, PGT.INDFCISCP, PGT.CNPJFCISCP, PGT.NRRRAPJD, PGT.INDRRA, PGT.CNPJOR "

	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(( SELECT SUM(V4GADV.V4G_VLDESP) FROM " + RetSqlName("V4F") + " V4F INNER JOIN " + RetSqlName("V4G") + " V4GADV ON " + FwJoinFilial("V4G","V4F","V4GADV","V4F")
	cQry += " AND V4GADV.V4G_ID = V4F.V4F_ID AND V4GADV.V4G_TPDESP = ? AND V4GADV.V4G_DTDESP BETWEEN ? AND ? AND V4GADV.D_E_L_E_T_ = ? WHERE "
	cQry += SvTmpBranch(cBd,cCompV4F,aInfEUF,"V4F","V4F.V4F_FILIAL","PGT.FILIAL"," V4F.V4F_ID = PGT.IDRRAPJUD ",.T.) + " ), 0) CUSTADV " //Total de Custas com advogados
	aadd(aBind, "1")
	aadd(aBind, cStartDate)
	aadd(aBind, cEndDate)
	aadd(aBind, space(1))

	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V4GJUD.V4G_VLDESP) FROM " + RetSqlName("V4F") + " V4F INNER JOIN " + RetSqlName("V4G") + " V4GJUD ON " + FwJoinFilial("V4G","V4F","V4GJUD","V4F")
	cQry += " AND V4GJUD.V4G_ID = V4F.V4F_ID AND V4GJUD.V4G_TPDESP = ? AND V4GJUD.V4G_DTDESP BETWEEN ? AND ? AND V4GJUD.D_E_L_E_T_ = ? WHERE "
	cQry += SvTmpBranch(cBd,cCompV4F,aInfEUF,"V4F","V4F.V4F_FILIAL","PGT.FILIAL"," V4F.V4F_ID = PGT.IDRRAPJUD ",.T.) + " ), 0) CUSTJUD " //Total de Custas Judiciais
	aadd(aBind, "2")
	aadd(aBind, cStartDate)
	aadd(aBind, cEndDate)
	aadd(aBind, space(1))

	cQry += ",PGT.VALBRUTO, PGT.BASEIR, PGT.ALIQIR, PGT.VALIR, PGT.TOTDEDU, PGT.TDEDUDEP, PGT.TOTISEN, PGT.VBSIR, PGT.VRSIR, PGT.TDEDUSUSP, PGT.NPROCREF, PGT.CNPJPLS, PGT.BENEPLS, PGT.CPFDEPE, PGT.TIPPGTO, PGT.VPGTPLS, PGT.REEMBAN, PGT.TPMEDIC, PGT.NINSMED "

	cQry += " FROM ( "
	cQry += "SELECT V3U.V3U_FILIAL FILIAL, C1H.C1H_CPF CPF, C1H.C1H_CODPAR CODPAR, CASE WHEN C1H.C1H_INDNIF = ? THEN 'Possui' WHEN C1H.C1H_INDNIF = ? THEN 'Dispensado' WHEN C1H.C1H_INDNIF = ? THEN 'Pais nao exige' ELSE 'Nao Informado' END INDNIF, C1H.C1H_NIF NIF "
	cQry += "," + iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(T9A.T9A_DESCRI,'') FRMTRIB "
	cQry += ",C1H.C1H_NOME NOME, 'PGT' ROTINA, V3U.V3U_PARCEL SERIEPARC, V3U.V3U_NUMERO DOCTO, ' ' NUMITE, V3U.V3U_DTPAGT DTEMISPGT, V3U.V3U_ID ID, V3O.V3O_CODIGO CODNAT "
	cQry += ",CASE WHEN V3U.V3U_FCISCP = ? THEN 'FCI' WHEN V3U.V3U_FCISCP = ? THEN 'SCP' ELSE 'Nao Informado' END INDFCISCP "
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V3X.V3X_CNPJ,'') CNPJFCISCP "
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V4F.V4F_NRPROC,'') NRRRAPJD "
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "( CASE WHEN V4F.V4F_INDRRA = ? THEN 'Sim' ELSE 'Nao' END , '') INDRRA "
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V4F.V4F_CNPJOR,'') CNPJOR "
	cQry += ",V3V.V3V_IDPROC IDRRAPJUD, V3V.V3V_VALOR VALBRUTO " //Valor do documento
	aadd(aBind, "1")
	aadd(aBind, "2")
	aadd(aBind, "3")
	aadd(aBind, "1")
	aadd(aBind, "2")
	aadd(aBind, "1")

//Tributos sobre Pag por Nat Ren
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V46.V46_BASE) FROM " + RetSqlName("V46") + " V46 WHERE " + FwJoinFilial("V46","V3U") + " AND V46.V46_ID = V3U.V3U_ID "
	cQry += " AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ?),0) BASEIR " //Base de Cálculo IR
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT V46.V46_ALIQ FROM " + RetSqlName("V46") + " V46 WHERE " + FwJoinFilial("V46","V3U") + " AND V46.V46_ID = V3U.V3U_ID "
	cQry += " AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ?),0) ALIQIR " //Alíquota do IR
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V46.V46_VALOR) FROM " + RetSqlName("V46") + " V46 WHERE " + FwJoinFilial("V46","V3U") + " AND V46.V46_ID = V3U.V3U_ID "
	cQry += " AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ?),0) VALIR " //Valor do IR
	aadd(aBind, "000028")
	aadd(aBind, space(1))
	aadd(aBind, "000028")
	aadd(aBind, space(1))
	aadd(aBind, "000028")
	aadd(aBind, space(1))

//Dedução Pgto Parc Fatura \ Rec
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V4I.V4I_VLRDED) FROM " + RetSqlName("V4I") + " V4I WHERE " + FwJoinFilial("V4I","V3U") + " AND V4I.V4I_ID = V3U.V3U_ID "
	cQry += " AND V4I.V4I_IDNAT = V3V.V3V_CNATRE AND V4I.V4I_IDTRIB = ? AND V4I.D_E_L_E_T_ = ?),0) TOTDEDU " //Total de deduções
	aadd(aBind, "000028")
	aadd(aBind, space(1))

//Beneficiarios de pensão
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V85.V85_VALDED) FROM " + RetSqlName("V85") + " V85 WHERE " + FwJoinFilial("V85","V3U") + " AND V85.V85_ID = V3U.V3U_ID "
	cQry += " AND V85.V85_IDPART = V3U.V3U_IDPART AND V85.V85_IDNAT = V3V.V3V_CNATRE AND V85.V85_IDTRIB = ? AND V85.D_E_L_E_T_ = ?),0) TDEDUDEP " //Valor total das deduções de dependentes
	aadd(aBind, "000028")
	aadd(aBind, space(1))

//Isenção Pgto Parc Fatura / Rec
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V4J.V4J_VLRISE) FROM " + RetSqlName("V4J") + " V4J WHERE " + FwJoinFilial("V4J","V3U") + " AND V4J.V4J_ID = V3U.V3U_ID "
	cQry += " AND V4J.V4J_IDNAT = V3V.V3V_CNATRE AND V4J.V4J_IDTRIB = ? AND V4J.D_E_L_E_T_ = ?),0) TOTISEN " //Total de Rendimentos Isentos
	aadd(aBind, "000028")
	aadd(aBind, space(1))

//Suspensão Pgto Parc Fatura Rec
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V4H.V4H_VBASSU) FROM " + RetSqlName("V4H") + " V4H WHERE " + FwJoinFilial("V4H","V3U") + " AND V4H.V4H_ID = V3U.V3U_ID "
	cQry += " AND V4H.V4H_CNATRE = V3V.V3V_CNATRE AND V4H.V4H_IDTRIB = ? AND V4H.D_E_L_E_T_ = ?),0) VBSIR " //Valor do Rendimento Suspenso por processo judicial
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V4H.V4H_VALSUS) FROM " + RetSqlName("V4H") + " V4H WHERE " + FwJoinFilial("V4H","V3U") + " AND V4H.V4H_ID = V3U.V3U_ID "
	cQry += " AND V4H.V4H_CNATRE = V3V.V3V_CNATRE AND V4H.V4H_IDTRIB = ? AND V4H.D_E_L_E_T_ = ?),0) VRSIR " //Valor da retenção nao efetuada devido Processo Ref
	aadd(aBind, "000028")
	aadd(aBind, space(1))
	aadd(aBind, "000028")
	aadd(aBind, space(1))

//Det Deducoes Exig. Susp
	cQry += "," + Iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "((SELECT SUM(V90.V90_VALDED) FROM " + RetSqlName("V90") + " V90 WHERE " + FwJoinFilial("V90","V3U") + " AND V90.V90_ID = V3U.V3U_ID "
	cQry += " AND V90.V90_CNATRE = V3V.V3V_CNATRE AND V90.V90_IDTRIB = ? AND V90.D_E_L_E_T_ = ?),0) TDEDUSUSP " //Valor da dedução suspensa
	aadd(aBind, "000028")
	aadd(aBind, space(1))

//Processo Referenciado
	cQry += iif( !("INFORMIX" $ cBd), ",COALESCE(( ", ",NVL(( " )
	cQry += iif( !(cBd $ ("INFORMIX|ORACLE|OPENEDGE|POSTGRES|DB2"))," SELECT TOP 1 C1G.C1G_NUMPRO ", " SELECT C1G.C1G_NUMPRO " )
	cQry += " FROM " + RetSqlName("V4H") + " V4H INNER JOIN "  + RetSqlName("C1G") + " C1G ON "

	if cCompC1G == "CCC" .Or. (cCompC1G == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " C1G.C1G_FILIAL = ? "
		AADD(aBind, xFilial("C1G")) //C1G_FILIAL
	else
		cQry += FwJoinFilial("C1G","V4H")
	endif

	cQry += " AND C1G.C1G_ID = V4H.V4H_IDPROC AND C1G.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))

	cQry += " WHERE " + FwJoinFilial("V4H","V3U") + " AND V4H.V4H_ID = V3U.V3U_ID AND V4H.V4H_CNATRE = V3V.V3V_CNATRE AND V4H.V4H_IDTRIB = ? AND V4H.D_E_L_E_T_ = ? "
	aadd(aBind, "000028")
	aadd(aBind, space(1))

	cQry += iif(cBd == "ORACLE"," AND ROWNUM <= 1 ",iif(cBd $ "POSTGRES|DB2"," LIMIT 1 ","")) + " ),'') NPROCREF "
	cQry += ",' ' CNPJPLS, ' ' BENEPLS,' ' CPFDEPE,' ' TIPPGTO, 0 VPGTPLS, 0 REEMBAN,' ' TPMEDIC,' ' NINSMED "

//Pagamento das Parcelas
	cQry += " FROM " + RetSqlName("V3U") + " V3U "

//Natureza Rendimento
	cQry += " INNER JOIN " + RetSqlName("V3V") + " V3V ON "+FwJoinFilial("V3V","V3U")+" AND V3V.V3V_ID = V3U.V3U_ID AND V3V.V3V_CNATRE <> ? AND V3V.D_E_L_E_T_ = ?"
	aadd(aBind, Space(nV3VCNatRe))
	aadd(aBind, space(1))

//Despesas Processuais e RRA
	cQry += " LEFT JOIN " + RetSqlName("V4F") + " V4F ON "
	if cCompV4F == "CCC" .Or. (cCompV4F == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " V4F.V4F_FILIAL = ? "
		aadd(aBind, xFilial("V4F")) //V4F_FILIAL
	else
		cQry += FwJoinFilial("V4F","V3V")
	endif

	cQry += " AND V4F.V4F_ID = V3V.V3V_IDPROC AND V4F.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))

//FCI e SCP
	cQry += " LEFT JOIN " + RetSqlName("V3X") + " V3X ON "
	if cCompV3X == "CCC" .Or. (cCompV3X == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " V3X.V3X_FILIAL = ? "
		aadd(aBind, xFilial("V3X")) //V3X_FILIAL
	else
		cQry += FwJoinFilial("V3X","V3V")
	endif

	cQry += " AND V3X.V3X_ID = V3V.V3V_IFCISC AND V3X.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))

//Natureza
	cQry += " INNER JOIN " + RetSqlName("V3O") + " V3O ON V3O.V3O_FILIAL = ? AND V3O.V3O_ID = V3V.V3V_CNATRE AND V3O.D_E_L_E_T_ = ? "
	aadd(aBind, xFilial("V3O"))
	aadd(aBind, space(1))

//Participantes
	cQry += " INNER JOIN " + RetSqlName("C1H") + " C1H ON "
	if cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)
		cQry += " C1H.C1H_FILIAL = ? "
		aadd(aBind, xFilial("C1H")) //C1H_FILIAL
	else
		cQry += FwJoinFilial("C1H","V3U")
	endif

	cQry += " AND C1H.C1H_ID = V3U.V3U_IDPART AND ((C1H.C1H_CPF <> ? AND C1H.C1H_PPES = ?) OR (C1H.C1H_PEEXTE = ? AND C1H.C1H_PAISEX <> ?) ) AND C1H.D_E_L_E_T_ = ? "
	aadd(aBind, space(1))
	aadd(aBind, "1")
	aadd(aBind, "1")
	aadd(aBind, space(1))
	aadd(aBind, space(1))

//Forma de tributacao
	cQry += " LEFT JOIN " + RetSqlName("T9A") + " T9A ON T9A.T9A_FILIAL = ? AND T9A.T9A_ID = C1H.C1H_IDTRIB AND T9A.D_E_L_E_T_ = ? "
	aadd(aBind, xFilial("T9A"))
	aadd(aBind, space(1))

//T158AB ( V46 ) Tributos Pagamento
	cQry += " LEFT JOIN " + RetSqlName("V46") + " V46 ON " + FwJoinFilial("V46","V3V") + " AND V46.V46_ID = V3U.V3U_ID AND V46.V46_IDNAT = V3V.V3V_CNATRE AND V46.V46_IDTRIB = ? AND V46.D_E_L_E_T_ = ? "
	aadd(aBind, "000028")
	aadd(aBind, space(1))

	cQry += " WHERE "
	
	If "TMPFIL" $ aRoot[1]
		cQry += " V3U.V3U_FILIAL IN (" + aRoot[1] + ") "
	Else
		cQry += " V3U.V3U_FILIAL IN (?) "
		aadd(aBind, aRoot)
	EndIf
	
	cQry += " AND V3U.V3U_DTPAGT BETWEEN ? AND ? AND V3U.D_E_L_E_T_ = ? "
	cQry += " AND (V46.V46_IDTRIB IS NOT NULL OR (V46.V46_IDTRIB IS NULL AND V3O.V3O_TRIB = ?)) "
	
	aadd(aBind, cStartDate)
	aadd(aBind, cEndDate)
	aadd(aBind, space(1))
	aadd(aBind, "8")

	cQry += " GROUP BY V3U.V3U_FILIAL, V3U.V3U_NUMERO, V3U.V3U_PARCEL, V3U.V3U_DTPAGT, V3U.V3U_ID, V3U.V3U_FCISCP, V3U.V3U_IDPART, C1H.C1H_CPF, C1H.C1H_CODPAR, C1H.C1H_INDNIF, C1H.C1H_NIF, C1H.C1H_NOME, "
	cQry += " T9A.T9A_DESCRI, V3X.V3X_CNPJ, V3V.V3V_CNATRE, V3V.V3V_IDPROC, V3V.V3V_VALOR, V3O.V3O_CODIGO, V4F.V4F_INDRRA, V4F.V4F_NRPROC, V4F.V4F_CNPJOR ) PGT "

Return cQry

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} SvTmpBranch
Relacionamento da V4F com a temporaria FAT (por esse motivo nao foi utilizado FwJoinFilial)

@author Denis Souza
@since 08/08/2023
@version 1.0 
/*/
//---------------------------------------------------------------------------------------------------------------
Static Function SvTmpBranch(cBd, cCompTab, aInfEUF, cTab, cFromField, cToField, cAndFilter, lDel )

	local cFilter   := " " as character
	local cSubtrSQL := " " as character

	default cBd         := " "
	default cCompTab    := " "
	default aInfEUF     := {}
	default cTab        := " "
	default cFromField  := " "
	default cToField    := " "
	default cAndFilter  := " "
	default lDel        := .F.

	cSubtrSQL := iif(cBd $ "ORACLE|POSTGRES|DB2","SUBSTR","SUBSTRING")

	If cCompTab == "EEE"
		cFilter += cFromField + " = " + cToField
	Else
		If cCompTab == "EEC" .And. aInfEUF[1]+aInfEUF[2] > 0
			cFilter += "LTRIM(RTRIM("+cSubtrSQL+"("+cFromField+",1," + cValToChar(aInfEUF[1]+aInfEUF[2]) + "))) " + " = LTRIM(RTRIM("+cSubtrSQL+"("+cToField+",1," + cValToChar(aInfEUF[1]+aInfEUF[2]) + "))) "
		ElseIf cCompTab == 'ECC' .And. aInfEUF[1]+aInfEUF[2] > 0
			cFilter += "LTRIM(RTRIM("+cSubtrSQL+"("+cFromField+",1," + cValToChar(aInfEUF[1]) + "))) " + " = LTRIM(RTRIM("+cSubtrSQL+"("+cToField+",1," + cValToChar(aInfEUF[1]) + "))) "
		ElseIf cCompTab == "CCC" .Or. ( cCompTab == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0 )
			cFilter += cFromField + " = '" + xFilial(cTab) + "' "
		EndIf
	EndIf
	if !Empty(cAndFilter)
		cFilter += " AND " + cAndFilter
	endif
	if lDel
		cFilter += " AND " + cTab + ".D_E_L_E_T_ = ' ' "
	endif

Return cFilter

//-------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} SvTafPls

@author Denis Souza
@since 08/08/2023
@version 1.0 
/*/
//---------------------------------------------------------------------------------------------------------------
Static Function SvTafPls(oTabTemp,cQry4010,cBd,cStartDate,cEndDate,aInfEUF,cCompC1H,cCompV3R,cFilsV4B)

	local cQry 	   := " " as character
	local cInto    := " " as character
	local cTabName := " " as character
	local cFields  := " " as character
	local aStru    := {}  as array
	local nlA         := 0  as numeric
	local oPrepare          as object
	local cAlias      := " " as character

	default oTabTemp   := Nil
	default cQry4010   := ''
	default cBd        := ''
	default cStartDate := ''
	default cEndDate   := ''
	default aInfEUF    := {}
	default cCompC1H   := ''
	default cCompV3R   := ''
	default cFilsV4B   := ''

	aadd(aStru,{'FILIAL' 	,GetSx3Cache('V4B_FILIAL','X3_TIPO'),GetSx3Cache('V4B_FILIAL','X3_TAMANHO'),GetSx3Cache('V4B_FILIAL','X3_DECIMAL')})
	aadd(aStru,{'CPF' 	  	,GetSx3Cache('C1H_CPF'   ,'X3_TIPO'),GetSx3Cache('C1H_CPF'   ,'X3_TAMANHO'),GetSx3Cache('C1H_CPF'   ,'X3_DECIMAL')})
	aadd(aStru,{'CODPAR' 	,GetSx3Cache('C1H_CODPAR','X3_TIPO'),GetSx3Cache('C1H_CODPAR','X3_TAMANHO'),GetSx3Cache('C1H_CODPAR','X3_DECIMAL')})
	aadd(aStru,{'INDNIF' 	,"C",15,0})
	aadd(aStru,{'NIF'    	,GetSx3Cache('C1H_NIF'   ,'X3_TIPO'),GetSx3Cache('C1H_NIF'   ,'X3_TAMANHO'),GetSx3Cache('C1H_NIF'   ,'X3_DECIMAL')})
	aadd(aStru,{'FRMTRIB'	,GetSx3Cache('T9A_DESCRI','X3_TIPO'),GetSx3Cache('T9A_DESCRI','X3_TAMANHO'),GetSx3Cache('T9A_DESCRI','X3_DECIMAL')})
	aadd(aStru,{'NOME'		,GetSx3Cache('C1H_NOME'	 ,'X3_TIPO'),GetSx3Cache('C1H_NOME'	 ,'X3_TAMANHO'),GetSx3Cache('C1H_NOME'	,'X3_DECIMAL')})
	aadd(aStru,{'ROTINA'    ,"C",3,0 })
	aadd(aStru,{'SERIEPARC' ,"C",1,0 })
	aadd(aStru,{'DOCTO'	    ,"C",1,0 })
	aadd(aStru,{'NUMITE'	,"C",1,0 })
	aadd(aStru,{'DTEMISPGT' ,GetSx3Cache('V4B_DTPGTO','X3_TIPO'),GetSx3Cache('V4B_DTPGTO','X3_TAMANHO'),GetSx3Cache('V4B_DTPGTO','X3_DECIMAL')})
	aadd(aStru,{'ID' 		,GetSx3Cache('V4B_ID'	 ,'X3_TIPO'),GetSx3Cache('V4B_ID'	 ,'X3_TAMANHO'),GetSx3Cache('V4B_ID'	,'X3_DECIMAL')})
	aadd(aStru,{'CODNAT' 	,"C",1,0 })
	aadd(aStru,{'INDFCISCP' ,"C",15,0})
	aadd(aStru,{'CNPJFCISCP',"C",1,0 })
	aadd(aStru,{'INDRRA' 	,"C",3,0 })
	aadd(aStru,{'NRRRAPJD'  ,"C",1,0 })
	aadd(aStru,{'CNPJOR'    ,"C",1,0 })
	aadd(aStru,{'CUSTADV'   ,"N",1,0 })
	aadd(aStru,{'CUSTJUD'   ,"N",1,0 })
	aadd(aStru,{'VALBRUTO'  ,"N",1,0 })
	aadd(aStru,{'BASEIR'    ,"N",1,0 })
	aadd(aStru,{'ALIQIR'    ,"N",1,0 })
	aadd(aStru,{'VALIR'     ,"N",1,0 })
	aadd(aStru,{'TOTDEDU'   ,"N",1,0 })
	aadd(aStru,{'TDEDUDEP'  ,"N",1,0 })
	aadd(aStru,{'TOTISEN'   ,"N",1,0 })
	aadd(aStru,{'VBSIR'   	,"N",1,0 })
	aadd(aStru,{'VRSIR'   	,"N",1,0 })
	aadd(aStru,{'TDEDUSUSP' ,"N",1,0 })
	aadd(aStru,{'NPROCREF'  ,"C",1,0 })
	aadd(aStru,{'CNPJPLS'   ,GetSx3Cache('V4B_CNPJ'  ,'X3_TIPO'),GetSx3Cache('V4B_CNPJ'	 ,'X3_TAMANHO'),GetSx3Cache('V4B_CNPJ'	,'X3_DECIMAL')})
	aadd(aStru,{'BENEPLS'   ,"C",10,0})
	aadd(aStru,{'CPFDEPE'   ,GetSx3Cache('V3R_CPF'   ,'X3_TIPO'),GetSx3Cache('V3R_CPF'	 ,'X3_TAMANHO'),GetSx3Cache('V3R_CPF'	,'X3_DECIMAL')})
	aadd(aStru,{'TIPPGTO'   ,"C",15,0})
	aadd(aStru,{'VPGTPLS'   ,GetSx3Cache('V4B_VLRPGT','X3_TIPO'),GetSx3Cache('V4B_VLRPGT','X3_TAMANHO'),GetSx3Cache('V4B_VLRPGT','X3_DECIMAL')})
	aadd(aStru,{'REEMBAN'   ,GetSx3Cache('V4B_VRBANT','X3_TIPO'),GetSx3Cache('V4B_VRBANT','X3_TAMANHO'),GetSx3Cache('V4B_VRBANT','X3_DECIMAL')})
	aadd(aStru,{'TPMEDIC'   ,"C",15,0})
	aadd(aStru,{'NINSMED'   ,GetSx3Cache('V4B_NRINSC','X3_TIPO'),GetSx3Cache('V4B_NRINSC','X3_TAMANHO'),GetSx3Cache('V4B_NRINSC','X3_DECIMAL')})

	oTabTemp := FWTemporaryTable():New(,aStru) //Necessario Criar tabela temporaria (TCGenQry Error - Query greater than 32768 bytes)
	oTabTemp:Create()
	cTabName := oTabTemp:GetRealName()

	oPrepare := FwExecStatement():New( cQry4010 )
	for nlA := 1 to len(aBind)
		if Valtype(aBind[nlA]) == 'A'
			oPrepare:setIn( nlA, aBind[nlA] )
		else
			oPrepare:setString( nlA, aBind[nlA] )
		endif
	next nlA

	cAlias := GetNextAlias()
	oPrepare:OpenAlias( cAlias )

	cQry4010 := oPrepare:getFixQuery()

	cFields := " FILIAL,CPF,CODPAR,INDNIF,NIF,FRMTRIB,NOME,ROTINA,SERIEPARC,DOCTO,NUMITE,DTEMISPGT,ID,CODNAT,INDFCISCP,CNPJFCISCP,INDRRA,NRRRAPJD,CNPJOR,CUSTADV,CUSTJUD "
	cFields += " ,VALBRUTO,BASEIR,ALIQIR,VALIR,TOTDEDU,TDEDUDEP,TOTISEN,VBSIR,VRSIR,TDEDUSUSP,NPROCREF,CNPJPLS,BENEPLS,CPFDEPE,TIPPGTO,VPGTPLS,REEMBAN,TPMEDIC,NINSMED "

	cInto := " INSERT INTO " + cTabName + "( " + cFields + " ) ( "
	cInto += " SELECT DISTINCT V4B.V4B_FILIAL FILIAL, C1H.C1H_CPF CPF, C1H.C1H_CODPAR CODPAR, "
	cInto += " CASE WHEN C1H.C1H_INDNIF = '1' THEN 'Possui' WHEN C1H.C1H_INDNIF = '2' THEN 'Dispensado' WHEN C1H.C1H_INDNIF = '3' THEN 'Pais nao exige' ELSE 'Nao Informado' END INDNIF "
	cInto += " ,C1H.C1H_NIF NIF, " + iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(T9A.T9A_DESCRI,'') FRMTRIB "
	cInto += " ,C1H.C1H_NOME NOME, 'PLS' ROTINA, ' ' SERIEPARC, ' ' DOCTO, ' ' NUMITE, V4B.V4B_DTPGTO DTEMISPGT, V4B.V4B_ID ID, ' ' CODNAT, ' ' INDFCISCP, ' ' CNPJFCISCP, ' ' INDRRA, ' ' NRRRAPJD "
	cInto += " ,' ' CNPJOR, 0 CUSTADV, 0 CUSTJUD, 0 VALBRUTO, 0 BASEIR, 0 ALIQIR, 0 VALIR, 0 TOTDEDU, 0 TDEDUDEP, 0 TOTISEN, 0 VBSIR, 0 VRSIR, 0 TDEDUSUSP,' ' NPROCREF, V4B.V4B_CNPJ CNPJPLS "
	cInto += " ,CASE WHEN V4B.V4B_INDBEN = '1' THEN 'Titular' ELSE 'Dependente' END BENEPLS "
	cInto += " ," + iif(!("INFORMIX" $ cBd),"COALESCE","NVL") + "(V3R.V3R_CPF,'') CPFDEPE "
	cInto += " ,CASE WHEN V4B.V4B_TPPGTO = '1' THEN 'Co Participacao' ELSE 'Reembolso' END TIPPGTO "
	cInto += " ,V4B.V4B_VLRPGT VPGTPLS, V4B.V4B_VRBANT REEMBAN, CASE WHEN V4B.V4B_TPINSC = '1' THEN 'Pessoa Juridica' ELSE 'Pessoa Fisica' END TPMEDIC, V4B.V4B_NRINSC NINSMED "
//Pagamento Reembolso PLS
	cInto += " FROM " + RetSqlName("V4B") + " V4B "
//Participante
	cInto += " INNER JOIN " + RetSqlName("C1H") + " C1H ON " + iif(cCompC1H == "CCC" .Or. (cCompC1H == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)," C1H.C1H_FILIAL = '" +xFilial("C1H")+ "' ", FwJoinFilial("C1H","V4B") )
	cInto += " AND C1H.C1H_ID = V4B.V4B_IDPART AND ((C1H.C1H_CPF <> ' ' AND C1H.C1H_PPES = '1') OR (C1H.C1H_PEEXTE = '1' AND C1H.C1H_PAISEX <> ' ') ) AND C1H.D_E_L_E_T_ = ' ' "
//forma de tributacao
	cInto += " LEFT JOIN " + RetSqlName("T9A") + " T9A ON T9A.T9A_FILIAL = '" + xFilial("T9A") + "' AND T9A.T9A_ID = C1H.C1H_IDTRIB AND T9A.D_E_L_E_T_ = ' ' "
//Dependentes
	cInto += " LEFT JOIN " + RetSqlName("V3R") + " V3R ON " + iif(cCompV3R == "CCC" .Or. (cCompV3R == "EEC" .And. aInfEUF[1]+aInfEUF[2] == 0)," V3R.V3R_FILIAL = '" + xFilial("V3R") + "' ", FwJoinFilial("V3R","V4B") )
	cInto += " AND V3R.V3R_ID = V4B.V4B_IDPART AND V3R.V3R_CODIGO = V4B.V4B_CODDEP AND V3R.D_E_L_E_T_ = ' ' "
	cInto += " WHERE V4B.V4B_FILIAL IN " + cFilsV4B + " AND V4B.V4B_DTPGTO BETWEEN '" + cStartDate + "' AND '" + cEndDate + "' AND V4B.D_E_L_E_T_ = ' ' "
	cInto += " AND C1H.C1H_CPF IN ( SELECT DISTINCT TB2.CPF FROM ( " + cQry4010 + " ) TB2 ) ) "

	cInto := StrTran(iif("ORA" $ cBd,StrTran(cInto,"SUBSTRING","SUBSTR"),cInto),"  "," ") //invalid identifier necessario pois nao existe changequery para tcsqlexec
	tcSqlExec( cInto ) //Inclui registros na tabela temporária

	cQry := "SELECT " + cFields + " FROM " + cTabName

	aSize(aStru,0)

Return cQry

//-------------------------------------------------------------------
/*{Protheus.doc} totalRet
Total retorno governo

@return null

@author Denis Souza
@since 15/08/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method totalRet(cBd as character, aRootBranch as array, cYearMonth as character, oHashTot as object) class ReinfBlock4010SmartViewBusinessObject

	local cTotQry   := " " as character
	local cQry      := " " as character
	local cFilsV4Q  := " " as character
	local aTot      := {}  as array
	local oPrepare          as object
	local aBind        := {}  as array
	local aRoot        := {}  as array
	local nlA          := 0   as numeric

	default cBd         := ""
	default aRootBranch := {}
	default cYearMonth  := ""
	default oHashTot    := Nil

	cFilsV4Q := TafRetFilC("V4Q", aRootBranch )

	cFilsV4Q := StrTran(StrTran(StrTran(cFilsV4Q,"(",""),")",""),"'","")

	aSize(aRoot,0)
	aRoot := Separa( cFilsV4Q, "," )

	cQry := " SELECT TAB1.V4Q_FILIAL,TAB1.V4Q_TPINSC,TAB1.V4Q_NRINSC,TAB1.V4Q_CPF,TAB1.V4Q_IDEXTE,TAB1.V4Q_PROTUL "
	cQry += " ,(SELECT SUM(V9G.V9G_VINFO) FROM " + RetSqlName("V9G") + " V9G WHERE V9G.V9G_FILIAL||V9G.V9G_ID||V9G.V9G_VERSAO = TAB1.FLIDVS) V9G_VINFO FROM ( "
	cQry += " SELECT V4Q.V4Q_FILIAL,V4Q.V4Q_TPINSC,V4Q.V4Q_NRINSC,V4Q.V4Q_CPF,V4Q.V4Q_IDEXTE,V4Q.V4Q_PROTUL "
	cQry += " ,(SELECT V9D.V9D_FILIAL||V9D.V9D_ID||V9D.V9D_VERSAO FROM " + RetSqlName("V9D") + " V9D "
	cQry += " WHERE " + FwJoinFilial("V9D","V4Q") + " AND V9D.V9D_IDEVEN = V4Q.V4Q_XMLID AND V9D.V9D_PERAPU = V4Q.V4Q_PERAPU AND V9D.V9D_NRRECB = V4Q.V4Q_PROTUL "
	cQry += " AND V9D.V9D_TPEVEN = ? AND V9D.V9D_ATIVO = ? AND V9D.V9D_CODRET = ? AND V9D.D_E_L_E_T_ = ? ) FLIDVS "
	AADD(aBind, "4010")
	AADD(aBind, "1")
	AADD(aBind, "0")
	AADD(aBind, Space(1))

	cQry += " FROM  " + RetSqlName("V4Q") + " V4Q WHERE "
	
	If "TMPFIL" $ aRoot[1]
		cQry += " V4Q.V4Q_FILIAL IN (" + aRoot[1] + ") "
	Else
		cQry += " V4Q.V4Q_FILIAL IN (?) "
		AADD(aBind, aRoot)
	EndIF
	cQry += " AND V4Q.V4Q_STATUS = ? AND V4Q.V4Q_PROTUL <> ? AND V4Q.V4Q_PERAPU = ? "
	cQry += " AND V4Q.V4Q_ATIVO = ? AND V4Q.D_E_L_E_T_ = ? ) TAB1 ORDER BY TAB1.V4Q_FILIAL,TAB1.V4Q_TPINSC,TAB1.V4Q_NRINSC,TAB1.V4Q_CPF,TAB1.V4Q_IDEXTE "
	AADD(aBind, "4")
	AADD(aBind, Space(1))
	AADD(aBind, substr(cYearMonth,5,2) + substr(cYearMonth,1,4))
	AADD(aBind, "1")
	AADD(aBind, Space(1))

	cQry := ChangeQuery(cQry)

	oPrepare := FwExecStatement():New( cQry )
	for nlA := 1 to len(aBind)
		if Valtype(aBind[nlA]) == 'A'
			oPrepare:setIn( nlA, aBind[nlA] )
		else
			oPrepare:setString( nlA, aBind[nlA] )
		endif
	next nlA

	cTotQry := GetNextAlias()

	oPrepare:OpenAlias( cTotQry )

	while (cTotQry)->(!Eof()) //ex: [1]1|53113791000122|22064958720|( 225236332514(NIF) ou FF4018101(CODPAR) ) [2]V9G_VINFO [3]V9D_PROTUL
		AAdd(aTot,{(Alltrim((cTotQry)->V4Q_TPINSC)+"|"+Alltrim((cTotQry)->V4Q_NRINSC)+"|"+Alltrim((cTotQry)->V4Q_CPF))+"|"+Alltrim((cTotQry)->V4Q_IDEXTE),;
			(cTotQry)->V9G_VINFO, Alltrim((cTotQry)->V4Q_PROTUL)})
		(cTotQry)->(DbSkip())
	enddo
	(cTotQry)->(DbCloseArea())

	oHashTot := AToHM(aTot)

	freeObj(oPrepare)

Return Nil
