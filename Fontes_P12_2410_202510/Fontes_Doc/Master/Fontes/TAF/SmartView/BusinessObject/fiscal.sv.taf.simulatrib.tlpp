#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.taf.simulatrib.ch"

#DEFINE ISS                     "000001"
#DEFINE ICMS                    "000002"
#DEFINE ICMSST                  "000004"
#DEFINE IPI                     "000005"
#DEFINE PIS                     "000006"
#DEFINE CONFINS                 "000007"
#DEFINE DOCUMENTO_CANCELADO     "000003"
#DEFINE ESCRITURACAO_CANCELADA  "000004"
#DEFINE NUMERACAO_INUTILIZADA   "000005"
#DEFINE DOCUMENTO_COMPLEMENTAR  "000006"

namespace totvs.protheus.fiscal.taf.treportsintegratedprovider

//-------------------------------------------------------------------
/*{Protheus.doc}  simulatribSmartViewBusinessObject
Classe para criação do Objeto Simulador de Tributos

@author Juliana Mellão
@since 12/08/2024
*/
//-------------------------------------------------------------------  
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGATAF", tables="C20, C30, C35", name="simulatribSmartViewBusinessObject", country="BRA", initialRelease="12.1.2310") 
class simulatribSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    data nPageSize as integer

    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author Juliana Mellão  
@since 12/08/2024
*/
//-------------------------------------------------------------------   
method new() class simulatribSmartViewBusinessObject

    Local oTafTools := tafSmartviewTools():new()
    
    _Super:new()
    self:appendArea(STR0001) //"TAF Fiscal"
    self:setDisplayName(STR0002) //"Simulador de Tributos"
    self:setDescription(STR0003) //"Objeto de Negocios para simulação de novos tributos referentes à Reforma Tributária"

    oTafTools:validPerg(self:setPergunte("TAFTR00014"))
    freeobj(oTafTools)

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do Smart View 
@return object: self:oData 
@author Juliana Mellão
@since 12/08/2024
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class simulatribSmartViewBusinessObject
    Local cQuery        as Character
    Local cAlias        as Character
    Local oTafTools     := tafSmartviewTools():new()
    Local oFiltQry      := oFilter:getParameters() as Json
    Local cFilDe        := oFiltQry['MV_PAR01',1] as Character
    Local cFilAte       := oFiltQry['MV_PAR02',1] as Character
    Local cPeriodo      := oFiltQry['MV_PAR03',1] as Character
    Local cProdDe       := oFiltQry['MV_PAR04',1] as Character
    Local cProdAte      := oFiltQry['MV_PAR05',1] as Character
    Local nPercCBS      := oFiltQry['MV_PAR06',1] as numeric
    Local nPercIBS      := oFiltQry['MV_PAR07',1] as numeric
    Local nPercIS       := oFiltQry['MV_PAR08',1] as numeric
    Local cPercRedu     := oFiltQry['MV_PAR09',1] as numeric //combo com opções 0, 30, 60 e 100
    Local dStartDate    as date
    Local dEndDate      as date
    Local nCBS          := 0 as numeric
    Local nIBS          := 0 as numeric
    Local nIS           := 0 as numeric
    Local nAux1         := 0 as numeric
    Local nAux2         := 0 as numeric
    Local nValorBase    := 0 as numeric
    Local nValorCBS     := 0 as numeric
    Local nValorIBS     := 0 as numeric
    Local nValorIS      := 0 as numeric
    Local nValorReforma := 0 as numeric
    Local lOk           := .t. as logical
    Local lHasNext      := .F. as Logical
    Local aFiliais      := {} as array
    Local cFils         := oTafTools:montaFilial(cFilDe, cFilAte, aFiliais) as character
    Local iBind         := 0  as integer
    Local aBind         := {} as array
    Local oStatement    := Nil as object
 
    If cFils == 'Erro'
        oTafTools:validFil(.F.)
        lOk := .F.
    Endif

    If Empty(oFiltQry['MV_PAR06',1]) //proteção caso % dos tributos não seja informado
        nPercCBS := 0
    EndIf
    If Empty(oFiltQry['MV_PAR07',1])
        nPercIBS := 0
    EndIf
    If Empty(oFiltQry['MV_PAR08',1])
        nPercIBS := 0
    EndIf

    If Empty(oFiltQry['MV_PAR03',1]) //proteção caso periodo não seja informado
        dStartDate := dDatabase
        dEndDate   := dDatabase
    EndIf

    dStartDate  := StoD(Substr(cPeriodo,3,4)+Substr(cPeriodo,1,2)+"01")
    dEndDate    := LastDay(dStartDate)

    Do Case
        Case cPercRedu == 0
            nCBS    := nPercCBS / 100
            nIBS    := nPercIBS / 100

        Case cPercRedu == 1
            nAux1   := (nPercCBS / 100) * 0.3 
            nCBS    := (nPercCBS / 100) - nAux1
            nAux2   := (nPercIBS / 100) * 0.3 
            nIBS    := (nPercIBS / 100) - nAux2

        Case cPercRedu == 2
            nAux1   := (nPercCBS / 100) * 0.6 
            nCBS    := (nPercCBS / 100) - nAux1
            nAux2   := (nPercIBS / 100) * 0.6 
            nIBS    := (nPercIBS / 100) - nAux2
            
        Case cPercRedu == 3
            nCBS    := 0
            nIBS    := 0

    EndCase

    nIS := nPercIS / 100 //IS não terá redução

    If lOk
        cQuery := " WITH C35_CALC AS ("
        cQuery += "     SELECT" 
        cQuery += "         C35.C35_FILIAL," 
        cQuery += "         C35.C35_CHVNF," 
        cQuery += "         C35.C35_NUMITE," 
        cQuery += "         C35.C35_CODITE,"
        cQuery += " 	    C35.C35_CODTRI,"
        cQuery += "         CASE WHEN C35.C35_CODTRI = ? THEN C35.C35_ALIQ ELSE 0 END ALIQ_ISS,"
        cQuery += "         CASE WHEN C35.C35_CODTRI = ? THEN C35.C35_VALOR ELSE 0 END VLR_ISS,"
        cQuery += "         CASE WHEN C35.C35_CODTRI = ? THEN C35.C35_ALIQ ELSE 0 END ALIQ_ICMS,"
        cQuery += "         CASE WHEN C35.C35_CODTRI = ? THEN C35.C35_VALOR ELSE 0 END VLR_ICMS,"
        cQuery += "         CASE WHEN C35.C35_CODTRI = ? THEN C35.C35_ALIQ ELSE 0 END ALIQ_ICMSST,"
        cQuery += "         CASE WHEN C35.C35_CODTRI = ? THEN C35.C35_VALOR ELSE 0 END VLR_ICMSST,"
        cQuery += "         CASE WHEN C35.C35_CODTRI = ? THEN C35.C35_ALIQ ELSE 0 END ALIQ_IPI,"
        cQuery += "         CASE WHEN C35.C35_CODTRI = ? THEN C35.C35_VALOR ELSE 0 END VLR_IPI,"
        cQuery += "         CASE WHEN C35.C35_CODTRI = ? THEN C35.C35_ALIQ ELSE 0 END ALIQ_PIS,"
        cQuery += "         CASE WHEN C35.C35_CODTRI = ? THEN C35.C35_VALOR ELSE 0 END VLR_PIS,"
        cQuery += "         CASE WHEN C35.C35_CODTRI = ? THEN C35.C35_ALIQ ELSE 0 END ALIQ_COFINS,"
        cQuery += "         CASE WHEN C35.C35_CODTRI = ? THEN C35.C35_VALOR ELSE 0 END VLR_COFINS,"
        cQuery += "         C35.C35_VALOR TOTAL_TRIBUTOS"
        cQuery += "     FROM " + RetSQLName("C35") + " C35"
        cQuery += "     WHERE C35.D_E_L_E_T_ = ?"
        cQuery += " )"
        cQuery += " SELECT"
        cQuery += "     C20.C20_FILIAL,"
        cQuery += "     C20.C20_DTDOC,"
        cQuery += "     C20.C20_NUMDOC,"
        cQuery += "     C20.C20_SERIE,"
        cQuery += "     C20.C20_ESPECI,"
        cQuery += "     C20.C20_LOJA,"
        cQuery += "     C20.C20_VLDOC,"
        cQuery += "     C20.C20_VLRFRT,"
        cQuery += "     C20.C20_VLRDA,"
        cQuery += "     C20.C20_VLRSEG,"
        cQuery += "     C30.C30_NUMITE,"
        cQuery += "     C30.C30_QUANT,"
        cQuery += "     C30.C30_VLOPER,"
        cQuery += "     C30.C30_VLRITE,"
        cQuery += "     C1H.C1H_NOME,"
        cQuery += "     C09.C09_UF,"
        cQuery += "     C0Y.C0Y_CODIGO,"
        cQuery += "     C1L.C1L_CODIGO,"
        cQuery += "     C1L.C1L_DESCRI,"
        cQuery += "     C35.C35_VLOUTR,"
        cQuery += "     SUM(C35A.ALIQ_ISS) ALIQ_ISS,"
        cQuery += "     SUM(C35A.VLR_ISS) VLR_ISS,"
        cQuery += "     SUM(C35A.ALIQ_ICMS) ALIQ_ICMS,"
        cQuery += "     SUM(C35A.VLR_ICMS) VLR_ICMS,"
        cQuery += "     SUM(C35A.ALIQ_ICMSST) ALIQ_ICMSST,"
        cQuery += "     SUM(C35A.VLR_ICMSST) VLR_ICMSST,"
        cQuery += "     SUM(C35A.ALIQ_IPI) ALIQ_IPI,"
        cQuery += "     SUM(C35A.VLR_IPI) VLR_IPI,"
        cQuery += "     SUM(C35A.ALIQ_PIS) ALIQ_PIS,"
        cQuery += "     SUM(C35A.VLR_PIS) VLR_PIS,"
        cQuery += "     SUM(C35A.ALIQ_COFINS) ALIQ_COFINS,"
        cQuery += "     SUM(C35A.VLR_COFINS) VLR_COFINS,"
        cQuery += "     SUM(C35A.TOTAL_TRIBUTOS) TOTAL_TRIBUTOS"
        cQuery += " FROM " + RetSQLName("C20") + " C20"
        cQuery += "     INNER JOIN " + RetSQLName("C1H") + " C1H ON C1H.C1H_FILIAL = ? AND C1H.C1H_ID = C20.C20_CODPAR AND C1H.D_E_L_E_T_ = ?"
        cQuery += "     INNER JOIN " + RetSQLName("C30") + " C30 ON C30.C30_FILIAL = ? AND C30.C30_CHVNF = C20.C20_CHVNF AND C30.D_E_L_E_T_ = ?"
        cQuery += "     LEFT JOIN C35_CALC C35A ON C35A.C35_FILIAL = ? AND C35A.C35_CHVNF = C20.C20_CHVNF AND C35A.C35_NUMITE = C30.C30_NUMITE"
        cQuery += "     LEFT JOIN " + RetSQLName("C35") + " C35 ON C35.C35_FILIAL = ? AND C35.C35_CHVNF = C20.C20_CHVNF AND C35.C35_NUMITE = C30.C30_NUMITE AND C35.C35_CODTRI = ? AND C35.D_E_L_E_T_ = ?"
        cQuery += "     LEFT JOIN " + RetSQLName("C0Y") + " C0Y ON C0Y.C0Y_FILIAL = ? AND C0Y.C0Y_ID = C30.C30_CFOP AND C0Y.D_E_L_E_T_ = ?"
        cQuery += "     LEFT JOIN " + RetSQLName("C09") + " C09 ON C09.C09_FILIAL = ? AND C09.C09_ID = C20.C20_UF AND C09.D_E_L_E_T_ = ?"
        cQuery += "     LEFT JOIN " + RetSQLName("C1L") + " C1L ON C1L.C1L_FILIAL = ? AND C1L.C1L_ID = C35.C35_CODITE AND C1L.D_E_L_E_T_ = ?"
        cQuery += " WHERE"
        cQuery += "     C20.C20_FILIAL IN (SELECT FILIAIS.FILIAL FROM ? FILIAIS) "
        cQuery += "     AND C20.C20_DTDOC BETWEEN ? AND ? "

        If !Empty(cProdDe)
            cQuery += "     AND C1L.C1L_CODIGO >= ? "
        EndIf

        If !Empty(cProdAte)
            cQuery += "     AND C1L.C1L_CODIGO <= ? "
        EndIf
        
        cQuery += "     AND C20.C20_CODSIT NOT IN (?)"
        cQuery += "     AND C20.D_E_L_E_T_ = ?" 
        cQuery += " GROUP BY" 
        cQuery += "     C20.C20_FILIAL,"
        cQuery += "     C20.C20_DTDOC,"
        cQuery += "     C20.C20_NUMDOC,"
        cQuery += "     C20.C20_SERIE,"
        cQuery += "     C30.C30_NUMITE,"
        cQuery += "     C30.C30_QUANT,"
        cQuery += "     C1H.C1H_NOME,"
        cQuery += "     C20.C20_LOJA,"
        cQuery += "     C09.C09_UF,"
        cQuery += "     C30.C30_VLOPER,"
        cQuery += "     C20.C20_ESPECI,"
        cQuery += "     C0Y.C0Y_CODIGO,"
        cQuery += "     C1L.C1L_CODIGO,"                        
        cQuery += "     C1L.C1L_DESCRI,"
        cQuery += "     C30.C30_VLRITE,"
        cQuery += "     C20.C20_VLDOC,"
        cQuery += "     C20.C20_VLRFRT,"
        cQuery += "     C35.C35_VLOUTR,"
        cQuery += "     C20_VLRDA,"
        cQuery += "     C20_VLRSEG"
        cQuery += " ORDER BY 
        cQuery += "     C20.C20_FILIAL, 
        cQuery += "     C20.C20_DTDOC, 
        cQuery += "     C20.C20_NUMDOC, 
        cQuery += "     C20.C20_SERIE"

        oStatement := FwExecStatement():New(cQuery)

        oStatement:SetString(++iBind, ISS)
        oStatement:SetString(++iBind, ISS)
        oStatement:SetString(++iBind, ICMS)
        oStatement:SetString(++iBind, ICMS)
        oStatement:SetString(++iBind, ICMSST)
        oStatement:SetString(++iBind, ICMSST)
        oStatement:SetString(++iBind, IPI)
        oStatement:SetString(++iBind, IPI)
        oStatement:SetString(++iBind, PIS)
        oStatement:SetString(++iBind, PIS)
        oStatement:SetString(++iBind, CONFINS)
        oStatement:SetString(++iBind, CONFINS)
        oStatement:SetString(++iBind, Space(1))
        oStatement:SetString(++iBind, xFilial("C1H"))
        oStatement:SetString(++iBind, Space(1))
        oStatement:SetString(++iBind, xFilial("C30"))
        oStatement:SetString(++iBind, Space(1))
        oStatement:SetString(++iBind, xFilial("C35"))
        oStatement:SetString(++iBind, xFilial("C35"))
        oStatement:SetString(++iBind, IPI)
        oStatement:SetString(++iBind, Space(1))
        oStatement:SetString(++iBind, xFilial("C0Y"))
        oStatement:SetString(++iBind, Space(1))
        oStatement:SetString(++iBind, xFilial("C09"))
        oStatement:SetString(++iBind, Space(1))
        oStatement:SetString(++iBind, xFilial("C1L"))
        oStatement:SetString(++iBind, Space(1))
        oStatement:SetUnsafe(++iBind, TAFCacheFil(Nil, aFiliais, .T.))
        oStatement:SetDate(++iBind, dStartDate)
        oStatement:SetDate(++iBind, dEndDate)

        If !Empty(cProdDe)
            oStatement:SetString(++iBind, cProdDe)
        EndIf

        If !Empty(cProdAte)
            oStatement:SetString(++iBind, cProdAte)
        EndIf

        oStatement:SetIn(++iBind, {DOCUMENTO_CANCELADO, ESCRITURACAO_CANCELADA, NUMERACAO_INUTILIZADA, DOCUMENTO_COMPLEMENTAR})
        oStatement:SetString(++iBind, Space(1))

        cAlias := oStatement:OpenAlias()

        (cAlias)->(DbGoTop())        

        While !(cAlias)->(Eof())
            nValorBase      := (cAlias)->(((C30_VLRITE * C30_QUANT) - VLR_ICMS) + C20_VLRDA + C20_VLRFRT + C20_VLRSEG)
            nValorCBS       := nValorBase * nCBS
            nValorIBS       := nValorBase * nIBS
            nValorIS        := nValorBase * nIS
            nValorReforma   := nValorCBS + nValorIBS + nValorIS

            self:oData:appendData({ "C20_FILIAL"    : (cAlias)->C20_FILIAL,;
                                    "C20_DTDOC"     : FWTimeStamp(6, stod((cAlias)->C20_DTDOC)),;
                                    "C20_NUMDOC"    : (cAlias)->C20_NUMDOC,; 
                                    "C20_SERIE"     : (cAlias)->C20_SERIE,;
                                    "C30_NUMITE"    : (cAlias)->C30_NUMITE,; 
                                    "C30_QUANT"     : (cAlias)->C30_QUANT,;  
                                    "C1H_NOME"      : (cAlias)->C1H_NOME,;   
                                    "C20_LOJA"      : (cAlias)->C20_LOJA,;   
                                    "C09_UF"        : (cAlias)->C09_UF,;     
                                    "C30_VLOPER"    : (cAlias)->C30_VLOPER,; 
                                    "C20_ESPECI"    : (cAlias)->C20_ESPECI,;
                                    "C0Y_CODIGO"    : (cAlias)->C0Y_CODIGO,;
                                    "C1L_CODIGO"    : (cAlias)->C1L_CODIGO,;  
                                    "C1L_DESCRI"    : (cAlias)->C1L_DESCRI,; 
                                    "C30_VLRITE"    : (cAlias)->C30_VLRITE,; 
                                    "C20_VLDOC"     : (cAlias)->C20_VLDOC,;  
                                    "C20_VLRFRT"    : (cAlias)->C20_VLRFRT,; 
                                    "C35_VLOUTR"    : (cAlias)->C35_VLOUTR,; 
                                    "ALIQ_ISS"      : (cAlias)->ALIQ_ISS,;
                                    "VLR_ISS"       : (cAlias)->VLR_ISS,;  
                                    "ALIQ_ICMS"     : (cAlias)->ALIQ_ICMS,;      
                                    "VLR_ICMS"      : (cAlias)->VLR_ICMS,;   
                                    "ALIQ_ICMSST"   : (cAlias)->ALIQ_ICMSST,;   
                                    "VLR_ICMSST"    : (cAlias)->VLR_ICMSST,;
                                    "ALIQ_IPI"      : (cAlias)->ALIQ_IPI,;       
                                    "VLR_IPI"       : (cAlias)->VLR_IPI,;        
                                    "ALIQ_PIS"      : (cAlias)->ALIQ_PIS,;       
                                    "VLR_PIS"       : (cAlias)->VLR_PIS,;        
                                    "ALIQ_COFINS"   : (cAlias)->ALIQ_COFINS,;    
                                    "VLR_COFINS"    : (cAlias)->VLR_COFINS,;  
                                    "TOTAL_TRIBUTOS": (cAlias)->TOTAL_TRIBUTOS,; 
                                    "VLR_BASE"      : nValorBase,;     
                                    "CBS"           : nValorCBS,;       
                                    "IBS"           : nValorIBS,;      
                                    "IS"            : nValorIS,;      
                                    "TOTAL_REFORMA" : nValorReforma})

            (cAlias)->(dbSkip())
        EndDo

        self:setHasNext(lHasNext)
        (cAlias)->(DBCloseArea())
    
    EndIf
    
    freeobj(oTafTools)
    freeObj(oStatement)
    freeobj(oFiltQry)
    aSize(aFiliais,0)
    aFiliais := Nil
    aSize(aBind,0)
    aBind := Nil

return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author Juliana Mellão
@since 12/08/2024
*/
//-------------------------------------------------------------------   
method getSchema() as object class simulatribSmartViewBusinessObject

//                 ( Nome           ,  Descricao        , Tipo      , Display           , cRealName         , cComboValues, lIsRequired, cRenameField, lCanFilter)
   self:addProperty("C20_FILIAL"    , STR0004           , "string"  , STR0004           , "C20_FILIAL"      ,             ,            ,             ,           ) // Filial
   self:addProperty("C20_DTDOC"     , STR0005           , "date"    , STR0006           , "C20_DTDOC"       ,             ,            ,             ,           ) // Data documento // Dt documento
   self:addProperty("C20_NUMDOC"    , STR0007           , "string"  , STR0007           , "C20_NUMDOC"      ,             ,            ,             ,           ) // Nota fiscal
   self:addProperty("C20_SERIE"     , STR0008           , "string"  , STR0008           , "C20_SERIE"       ,             ,            ,             ,           ) // Série
   self:addProperty("C30_NUMITE"    , STR0009           , "string"  , STR0009           , "C30_NUMITE"      ,             ,            ,             ,           ) // Item
   self:addProperty("C30_QUANT"     , STR0010           , "number"  , STR0010           , "C30_QUANT"       ,             ,            ,             ,           ) // Quantidade
   self:addProperty("C1H_NOME"      , STR0011           , "string"  , STR0011           , "C1H_NOME"        ,             ,            ,             ,           ) // Participante
   self:addProperty("C20_LOJA"      , STR0012           , "string"  , STR0012           , "C20_LOJA"        ,             ,            ,             ,           ) // Loja
   self:addProperty("C09_UF"        , STR0013           , "string"  , STR0013           , "C09_UF"          ,             ,            ,             ,           ) // Estado
   self:addProperty("C30_VLOPER"    , STR0014           , "number"  , STR0015           , "C30_VLOPER"      ,             ,            ,             ,           ) // Valor operação // Vlr operação
   self:addProperty("C20_ESPECI"    , STR0016           , "string"  , STR0016           , "C20_ESPECI"      ,             ,            ,             ,           ) // Espécie
   self:addProperty("C0Y_CODIGO"    , STR0017           , "string"  , STR0017           , "C0Y_CODIGO"      ,             ,            ,             ,           ) // CFOP
   self:addProperty("C1L_CODIGO"    , STR0020           , "string"  , STR0020           , "C1L_CODIGO"      ,             ,            ,             ,           ) // Produto
   self:addProperty("C1L_DESCRI"    , STR0021           , "string"  , STR0022           , "C1L_DESCRI"      ,             ,            ,             ,           ) // Descrição produto // Desc produto
   self:addProperty("C30_VLRITE"    , STR0023           , "number"  , STR0024           , "C30_VLRITE"      ,             ,            ,             ,           ) // Preço unitário // Prç unit
   self:addProperty("C20_VLDOC"     , STR0025           , "number"  , STR0026           , "C20_VLDOC"       ,             ,            ,             ,           ) // Valor total // Vlr total
   self:addProperty("C20_VLRFRT"    , STR0027           , "number"  , STR0027           , "C20_VLRFRT"      ,             ,            ,             ,           ) // Frete
   self:addProperty("C35_VLOUTR"    , STR0028           , "number"  , STR0029           , "C35_VLOUTR"      ,             ,            ,             ,           ) // Valor outros // Vlr outros
   self:addProperty("ALIQ_ISS"      , STR0030           , "number"  , STR0031           , "ALIQ_ISS"        ,             ,            ,             ,           ) // Alíquota ISS // Alíq ISS
   self:addProperty("VLR_ISS"       , STR0032           , "number"  , STR0033           , "VLR_ISS"         ,             ,            ,             ,           ) // Valor ISS // Vlr ISS
   self:addProperty("ALIQ_ICMS"     , STR0034           , "number"  , STR0035           , "ALIQ_ICMS"       ,             ,            ,             ,           ) // Alíquota ICMS // Alíq ICMS
   self:addProperty("VLR_ICMS"      , STR0036           , "number"  , STR0037           , "VLR_ICMS"        ,             ,            ,             ,           ) // Valor ICMS // Vlr ICMS
   self:addProperty("ALIQ_ICMSST"   , STR0038           , "number"  , STR0039           , "ALIQ_ICMSST"     ,             ,            ,             ,           ) // Alíquota ICMS-ST // Alíq ICMS-ST
   self:addProperty("VLR_ICMSST"    , STR0040           , "number"  , STR0041           , "VLR_ICMSST"      ,             ,            ,             ,           ) // Valor ICMS-ST // Vlr ICMS-ST
   self:addProperty("ALIQ_IPI"      , STR0042           , "number"  , STR0043           , "ALIQ_IPI"        ,             ,            ,             ,           ) // Alíquota IPI // Alíq IPI
   self:addProperty("VLR_IPI"       , STR0044           , "number"  , STR0045           , "VLR_IPI"         ,             ,            ,             ,           ) // Valor IPI //Vlr IPI
   self:addProperty("ALIQ_PIS"      , STR0046           , "number"  , STR0047           , "ALIQ_PIS"        ,             ,            ,             ,           ) // Alíquota PIS // Alíq PIS
   self:addProperty("VLR_PIS"       , STR0048           , "number"  , STR0049           , "VLR_PIS"         ,             ,            ,             ,           ) // Valor PIS // Vlr PIS
   self:addProperty("ALIQ_COFINS"   , STR0050           , "number"  , STR0051           , "ALIQ_COFINS"     ,             ,            ,             ,           ) // Alíquota COFINS // Alíq COFINS
   self:addProperty("VLR_COFINS"    , STR0052           , "number"  , STR0053           , "VLR_COFINS"      ,             ,            ,             ,           ) // Valor COFINS // Vlr COFINS
   self:addProperty("TOTAL_TRIBUTOS", STR0054           , "number"  , STR0054           , "TOTAL_TRIBUTOS"  ,             ,            ,             ,           ) // Total tributos
   self:addProperty("VLR_BASE"      , STR0056           , "number"  , STR0057           , "VLR_BASE"        ,             ,            ,             ,           ) // Valor base // Vlr base
   self:addProperty("CBS"           , STR0058           , "number"  , STR0059           , "CBS"             ,             ,            ,             ,           ) // Valor CBS // Vlr CBS
   self:addProperty("IBS"           , STR0060           , "number"  , STR0061           , "IBS"             ,             ,            ,             ,           ) // Valor IBS // Vlr IBS
   self:addProperty("IS"            , STR0062           , "number"  , STR0063           , "IS"              ,             ,            ,             ,           ) // Valor IS // Vlr IS
   self:addProperty("TOTAL_REFORMA" , STR0064           , "number"  , STR0064           , "TOTAL_REFORMA"   ,             ,            ,             ,           ) // Total reforma
 
return self:oSchema
