#include "tlpp-core.th"
#include "tlpp-rest.th"
#DEFINE TOKEN_JWT_TSS 'TokenAuthTSS'

/* --------------------------------------- */
/*/{Protheus.doc} className
    (long_description)
    @author user
    @since 02/09/2025
    @version version
    /*/
Class WSTAFCREDTRAB
    public Method New() as object

    @GET("/api/rh/esocial/v1/wstafcredtrab/tafFull")
    public Method TafFullConsult()

    @GET("/api/rh/esocial/v1/wstafcredtrab/middleware")
    public Method MidConsult()

EndClass

Method New() as object Class WSTAFCREDTRAB; Return

/*/{Protheus.doc} MidConsult
    Metodo de consulta do crédito do trabalhador para o Middleware
    @author lucas.passos
    @since 02/09/2025
    @version 1.0
    /*/
Method MidConsult() Class WSTAFCREDTRAB
    Local cEntidade     as Character
    Local cPeriod       as Character
    Local cURL          as Character
    Local cForceRequest as Character
    Local cBearerToken  as Character
    Local cMsgErro      as Character
    local jQueryString  as json
    Local jHeader       as json
    Local oRet          as object

    jQueryString := oRest:getQueryRequest()
    jHeader      := oRest:getHeaderRequest()

    If ValType(jHeader) != "U" 
        cBearerToken := jHeader[TOKEN_JWT_TSS]
    EndIf

    cEntidade       := jQueryString["sourceBranch"] 
    cPeriod         := jQueryString["period"] 
    cForceRequest   := jQueryString["forceRequest"] 
    cURL            := jQueryString["urlTSS"] 
    cTpAmbe         := jQueryString["environment"]

    If Empty(cEntidade)
        oRest:setStatusCode(400)
        oRest:SetResponse("Entidade do TSS não informada no paramêtro sourceBranch")

    ElseIf Empty(cURL)
        oRest:setStatusCode(400)
        oRest:SetResponse("Url do TSS não informada no paramêtro urlTSS")

    ElseIf Empty(cPeriod)
        oRest:setStatusCode(400)
        oRest:SetResponse("Competencia não informada no paramêtro period")

    ElseIf Empty(cForceRequest)
        oRest:setStatusCode(400)
        oRest:SetResponse("ForceRequest não informada no paramêtro forceRequest")

    ElseIf Empty(cTpAmbe)
        oRest:setStatusCode(400)
        oRest:SetResponse("Tipo de ambiente a ser consultado não informado no paramêtro environment")
    Else 

        If RAT("/",cUrl) != Len(cUrl)
            cUrl += "/"
        EndIf 
    
        If TSSOnAir(cURL)
            oRet :=  ConsultTss(cEntidade, cPeriod, cForceRequest, cTpAmbe, cURL, cBearerToken)

            // Seta no REST
            oRest:setResponse(buildResponse(oRet))
        Else
            oRest:setStatusCode(400)
            cMsgErro := IIf(Empty(GetWscError(3)),GetWscError(1),GetWscError(3))
            oRest:SetResponse( "Não foi possível obter a resposta do TSS. Detalhe: " + cMsgErro ) 

        EndIf

    EndIf

Return 


/*/{Protheus.doc} TafFullConsult
    Metodo de consulta do crédito do trabalhador para o TAFFUL
    @author lucas.passos
    @since 02/09/2025
    @version 1.0
    /*/
Method TafFullConsult() Class WSTAFCREDTRAB

    Local cEntidade     as character
    Local cPeriod       as character
    Local cURL          as character
    Local cForceRequest as character
    Local cBearerToken  as character
    Local cEmpTaf       as character
    Local cFilTAF       as character
    Local cUUIDFil		as character
	Local cChaveFil		as character
    Local cMsgErro      as character
    local jQueryString  as json
    Local aEmpresas		as array
    Local oRet          as object

    jQueryString    := oRest:getQueryRequest()
    cBearerToken    := ""
    cEmpTaf         := ""
    cFilTAF         := ""
    cMsgErro        := ""
    cUUIDFil		:= "tafJobID"
	cChaveFil		:= "keyTafJob"
    cEntidade       := jQueryString["sourceBranch"] 
    cPeriod         := jQueryString["period"] 
    cForceRequest   := jQueryString["forceRequest"] 
    aEmpresas		:= {}

    If Empty(cEntidade)
        oRest:setStatusCode(400)
        oRest:SetResponse("Filial não informada no paramêtro sourceBranch")

    ElseIf Empty(cPeriod)
        oRest:setStatusCode(400)
        oRest:SetResponse("Competencia não informada no paramêtro period")

    ElseIf Empty(cForceRequest)
        oRest:setStatusCode(400)
        oRest:SetResponse("ForceRequest não informada no paramêtro forceRequest")
    Else 

        If VarGetA(cUUIDFil,cChaveFil,@aEmpresas)
            WsSearchDePara(cEntidade,aEmpresas,@cEmpTaf,.T.,@cFilTAF)

            If !Empty(cFilTAF).And. PrepEnv(cEmpTaf, cFilTAF, "TAF", "WSTAFCREDTRAB" )

                SM0->(MsSeek(cEmpTaf+AllTrim(cFilTAF)))
                cFilAnt := SM0->M0_CODFIL
                cTpAmbe := SuperGetMv("MV_TAFAMBE", .F., "2")
                cURL    := AllTrim(TafGetUrlTSS(cEntidade))

                If RAT("/",cUrl) != Len(cUrl)
                    cUrl += "/"
                EndIf 

                If TSSOnAir(cURL)
                    
                    cEntidade := TAFRIdEnt(.T.)

                    If Empty(cEntidade)
                        oRest:setStatusCode(400)
                        oRest:SetResponse("Entidade não retornada pelo TSS. Verifique se sourceBranch informado é da filial Matriz.")
                    Else
                        oRet :=  ConsultTss(cEntidade, cPeriod, cForceRequest, cTpAmbe, cURL, cBearerToken)
                        oRest:setResponse(buildResponse(oRet))
                    EndIf 
                Else
                    oRest:setStatusCode(400)

                    cMsgErro := IIf(Empty(GetWscError(3)),GetWscError(1),GetWscError(3))
                    oRest:SetResponse( "Não foi possível obter a resposta do TSS. Detalhe: " + cMsgErro ) 

                EndIf
            Else
                oRest:setStatusCode(400)
                oRest:SetResponse("Não foi possivel localizar a Filial do TAF para o retorno das informações. ")
            EndIf 
        EndIf 

        VarCleanX(cUUIDFil)
    EndIf 

Return 



/*/{Protheus.doc} ConsultTss
    Consulta client do TSS WSTSSWSCREDITOTRABALHADOR pare retorno do credito trabalhador
    @type  Static Function
    @author lucas.passos
    @since 02/09/2025
    @version 1.0
/*/
Static Function ConsultTss(cEntidade as character, cPeriod as character, cForceRequest as character, cTpAmbe as character, cURL as character, cBearerToken as character)
    
    Local lRequest   as logical
    Local oConsulTss as object
    Local oResponse  as object

    Default cEntidade       := ""        
    Default cPeriod         := ""     
    Default cForceRequest   := ""           
    Default cTpAmbe         := ""      
    Default cURL            := ""          
    Default cBearerToken    := ""            

    oConsulTss := TSSWSCONSIGNADO():New(cBearerToken)

    If oConsulTss <> Nil
        oConsulTss:_Url 						:= cUrl + "/TSSWSCONSIGNADO.apw"  
        oConsulTss:oWSCONSULTADOC:cUSERTOKEN 	:= "TOTVS"
        oConsulTss:oWSCONSULTADOC:cENTIDADE    	:= cEntidade
        oConsulTss:oWSCONSULTADOC:cAMBIENTE     := cTpAmbe
        oConsulTss:oWSCONSULTADOC:cCOMPETENCIA  := cPeriod
        oConsulTss:oWSCONSULTADOC:lFORCEREQUEST := Upper(cForceRequest) == "TRUE"

        lRequest := oConsulTss:CONSULTARCONSIGNACOES()

        If oConsulTss:oWSRETORNODOC <> Nil
            oResponse := oConsulTss:oWSRETORNODOC
        EndIf

    EndIf 

Return oResponse


/*/{Protheus.doc} buildResponse
    Função para formatar o JSON a ser devolvido para a API
    @type  Function
    @author lucas.passos
    @since 08/09/2025
    @version 1.0
    /*/
Function buildResponse(oRet as object)

    Local cAmbiente        as character
    Local cArquivo         as character
    Local cCodrequisicao   as character
    Local cCompetencia     as character
    Local cDescrerro       as character
    Local cHrsolicitacao   as character
    Local cId              as character
    Local cId_ent          as character
    Local cStatus          as character
    Local cDtsolicitacao   as character

    Default oRet := Nil

    cAmbiente           := oRet:CAMBIENTE 
    If ValType(oRet:CARQUIVO ) != "U" 
        cArquivo := Encode64(oRet:CARQUIVO)
    EndIf 
    cCodrequisicao      := oRet:CCODREQUISICAO 
    cCompetencia        := oRet:CCOMPETENCIA 
    cDescrerro          := oRet:CDESCRERRO
    cHrsolicitacao      := oRet:CHRSOLICITACAO 
    cId                 := oRet:CID 
    cId_ent             := oRet:cENTIDADE 
    cStatus             := oRet:CSTATUS 
    cDtsolicitacao      := DTOS(oRet:DDTSOLICITACAO)

    oResponse := JsonObject():New()
    oResponse["Ambiente" ]      := cAmbiente       
    oResponse["Arquivo" ]       := cArquivo        
    oResponse["CodRequisicao"]  := cCodrequisicao  
    oResponse["Competencia"]    := cCompetencia    
    oResponse["DescrErro"]      := cDescrerro      
    oResponse["HrSolicitacao"]  := cHrsolicitacao  
    oResponse["Id" ]            := cId             
    oResponse["Id_ent"]         := cId_ent         
    oResponse["Status"]         := cStatus         
    oResponse["DtSolicitacao"]  := cDtsolicitacao  
    
Return oResponse
