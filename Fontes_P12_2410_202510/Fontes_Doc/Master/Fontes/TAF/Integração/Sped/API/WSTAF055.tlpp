#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"

//status do Periodo V13
#DEFINE StatusEmAberto		'1'
#DEFINE StatusApurado  		'2'
#DEFINE StatusFechado	    '3'
#DEFINE StatusEmApuracao 	'4'

#DEFINE CodeRespCreated 201
#DEFINE CodeNoContent 204
#DEFINE CodeRespBadRequest 400

Static __oSelectV11 := Nil
Static __oInsert    := Nil
Static __oUpdateV11 := Nil
Static cOperConcat  := iif(TcGetDb() $ 'ORACLE|POSTGRES', '||', '+') as character

/*-------------------------------------------------------------------
{Protheus.doc} Get calculationRules
Função Responsável por retornar todas as regras de apuração.

@author	adilson.roberto
@since	19/01/2024
-------------------------------------------------------------------*/
//Retorna lista dos itens de cadastro de perfil de apuração
@get(endpoint='api/taf/fiscal/v1/calculationRules', description='API de consulta das Configurações de Apuração')
Function TAFCalculationRules()
Local oJsResponse as json
Local cTribute := oRest:getQueryRequest()['tribute'] as character

oJsResponse := DescCalculationRules(Nil ,cTribute)  

oRest:setResponse( (oJsResponse ))

Return

/*-------------------------------------------------------------------
{Protheus.doc} GET getcalculationRules
Retorna os registros do docadastro de perfil de tributos

@author	Carlos Eduardo Boy
@since	22/09/2022
-------------------------------------------------------------------*/
@get(endpoint='api/taf/fiscal/v1/calculationRules/:id', description='API de consulta das Configurações de Apuração por ID')
Function TAFGetCalculationRules() 
Local oJsResponse := JsonObject():new() as json
Local cRegraParam := oRest:getPathParamsRequest()['id'] as character

//O parâmetro ruleId é obrigatório
GetRespCalCulationRules(cRegraParam, @oJsResponse)

oRest:setResponse( (oJsResponse ))   

Return 

/*-------------------------------------------------------------------
{Protheus.doc} Post postCalculationRules
Grava os registros do cadastro de regra de apuração

@author	adilson.roberto
@since	19/01/2024
-------------------------------------------------------------------*/
@POST(endpoint='/api/taf/fiscal/v1/calculationRules', description='API de inclusão das Configurações de Apuração') 
Function TAFPostCalculationRules()
Local oJsBody     := JsonObject():new() as Json
Local oJsResponse := JsonObject():new() as object
Local cRegraParam := "" as character
Local cRegIns     := "" as character
Local cBranch     := xFilial('V10') as character
Local cBranchV11  := xFilial('V11') as character
Local lRetV10     := .F. as logical
Local lRetV11     := .F. as logical
Local lRetJson    := .T. as logical
Local nItem as numeric
Local nPass as numeric
Local nPV11 as numeric
Local nFil  as numeric

//Recupera o conteudo do body
oJsBody:fromJson( oRest:GetBodyRequest() )
nItem := Len(oJsBody)
If Valtype(oJsBody) == "J" .And. nItem > 0  
    Begin transaction
        For nPass := 1 To nItem
            
            lRetV10 := .f.
            lRetV11 := .f.
            
            If (Empty(AllTrim(oJsBody[nPass]["tribute"])) .Or. Empty(AllTrim(oJsBody[nPass]["expirationRuleStart"])) .Or.;
                Empty(AllTrim(oJsBody[nPass]["expirationRuleEnd"])) .Or.;
                (Alltrim(oJsBody[nPass]["tribute"]) != '5' .And. Empty(AllTrim(oJsBody[nPass]["fiscalRegime"]))) .Or.;
                (Alltrim(oJsBody[nPass]["tribute"]) != '5' .And. Empty(AllTrim(oJsBody[nPass]["calculationSystem"]))) .Or.;
                (Alltrim(oJsBody[nPass]["tribute"]) != '5' .And. Empty(AllTrim(oJsBody[nPass]["taxRegime"]))) )
                lRetJson := .F.
            Endif     
            If lRetJson .And. V10->(RecLock('V10',.T.))
                GetLastCode(@cRegraParam)
                cRegraParam := StrZero(Val(__soma1( cRegraParam )),6)
                V10->V10_FILIAL := cBranch
                V10->V10_CODIGO := cRegraParam
                V10->V10_DESCRI := oJsBody[nPass]["description"]
                V10->V10_PRFIL  := oJsBody[nPass]["processBranch"]
                V10->V10_TRIBUT := oJsBody[nPass]["tribute"]
                If Alltrim(oJsBody[nPass]["tribute"]) != '5'
                    V10->V10_REGFIS := oJsBody[nPass]["fiscalRegime"]
                    V10->V10_REGAPU := oJsBody[nPass]["calculationSystem"]
                    V10->V10_REGTRI := oJsBody[nPass]["taxRegime"]
                Endif    
                V10->V10_VIGINI := Stod(Left(oJsBody[nPass]["expirationRuleStart"],4) + SubStr(oJsBody[nPass]["expirationRuleStart"],6,2)+ Right(oJsBody[nPass]["expirationRuleStart"],2))
                V10->V10_VIGFIN := Stod(Left(oJsBody[nPass]["expirationRuleEnd"],4) + SubStr(oJsBody[nPass]["expirationRuleEnd"],6,2)+ Right(oJsBody[nPass]["expirationRuleEnd"],2))
                V10->(MsUnLock())
                lRetV10 := .T.
                If nPass = 1
                    cRegIns := "'"+cRegraParam+"'"
                Else 
                    cRegIns := cRegIns + " , '"+cRegraParam+"'"
                Endif       
            Endif
            If lRetV10
                nFil := Len(oJsBody[nPass]["branches"])
                For nPV11 := 1 To nFil 
                    If V11->(RecLock('V11',.T.))
                        V11->V11_FILIAL := cBranchV11
                        V11->V11_ID := FwUUIDV4(.T.)
                        V11->V11_CODIGO := cRegraParam
                        V11->V11_FILANT := oJsBody[nPass]["branches"][nPV11]
                        V11->(MsUnLock())
                        lRetV11 := .T.
                    Endif
					
					If !lRetV11; Exit; EndIf
				Next    
            Endif

            If !lRetV10 .or. !lRetV11
                DisarmTransaction()
                Exit  
            Endif

        NEXT
    end transaction    

    If lRetJson .And. lRetV10 .And. lRetV11
        oJsResponse := DescCalculationRules(cRegIns)
        oRest:setStatusCode(CodeRespCreated)
    elseIf !lRetJson
        oJsResponse['code'] := CodeRespBadRequest
        oJsResponse['detailMessage'] := ''
        oJsResponse['message'] := "O tributo ou  Data Inicio ou Data Expiracao ou Regime Fiscal ou Sistema de Calculo"
        oJsResponse['message'] += " ou Regime de Taxa nao foi informado."
        oRest:setStatusCode(CodeRespBadRequest)
    Else
        oJsResponse['code'] := CodeRespBadRequest
        oJsResponse['detailMessage'] := ''
        oJsResponse['message'] := "O codigo do tributo nao foi informado ou nao existe no cadastro ou não foram informados Filiais."
        oRest:setStatusCode(CodeRespBadRequest)
    endif       
else
    oJsResponse['code'] := CodeRespBadRequest
    oJsResponse['detailMessage'] := ''
    oJsResponse['message'] := "Nao foi localizado nenhum registro para ser gravado."
    oRest:setStatusCode(CodeRespBadRequest)
Endif        

oRest:setResponse( (oJsResponse )) 

return 

/*-------------------------------------------------------------------
{Protheus.doc} Post getCalculationProfileScreen
Grava os registros do cadastro de perfil de tributos

@author	adilson.roberto
@since	19/01/2024
-------------------------------------------------------------------*/
@PUT(endpoint='/api/taf/fiscal/v1/calculationRules/:id', description='API de alteração das Configurações de Apuração') 
Function TAFPutCalculationRules()

    Local aFiliais      := {} as array
    Local oJsBody       := JsonObject():new() as Json
    Local oJsResponse   := JsonObject():new() as json
    Local cBranch       := xFilial('V10') as character
    Local cBranchV11    := xFilial('V11') as character
    Local cRegraParam   := oRest:getPathParamsRequest()['id'] as character
    Local cFiliais      as character
    Local lRetV10       as logical
    Local lRetV11       as logical
    Local iFil          as integer
    Local cErrorMessage as character
    Local cErrorDetail  as character    
    Local aIdPeriodos   as array 
    Local nStatus       := CodeRespCreated as integer

    if V10->(MsSeek(cBranch+cRegraParam))
        oJsBody:fromJson( oRest:GetBodyRequest() )
        //Valida se alteração pode ser feita
        if ValidRuleChange(cRegraParam, oJsBody, @aIdPeriodos, @cErrorMessage, @cErrorDetail)
            if TafDesfazApuracao(aIdPeriodos)
                Begin transaction
                    If V10->(RecLock('V10',.F.))
                        V10->V10_DESCRI := oJsBody["description"]
                        V10->V10_PRFIL  := oJsBody["processBranch"]
                        V10->V10_TRIBUT := oJsBody["tribute"]
                        V10->V10_REGFIS := oJsBody["fiscalRegime"]
                        V10->V10_REGAPU := oJsBody["calculationSystem"]
                        V10->V10_REGTRI := oJsBody["taxRegime"]
                        V10->V10_VIGINI := stod(strtran(oJsBody['expirationRuleStart'],'-',''))
                        V10->V10_VIGFIN := stod(strtran(oJsBody['expirationRuleEnd'  ],'-',''))
                        V10->(MsUnLock())
                        lRetV10 := .T.
                    Endif

                    For iFil := 1 To Len(oJsBody["branches"])
                        If iFil == 1
                            cFiliais := "'" + oJsBody["branches"][iFil] + "'"
                        Else 
                            cFiliais := cFiliais + " ,'" + oJsBody["branches"][iFil] + "'"
                        Endif 

                        AaDd(aFiliais, {oJsBody["branches"][iFil], 0})
                    Next

                    If lRetV10 
                        ProcFilUpd(cBranchV11, cRegraParam, cFiliais, @lRetV11)
                        if lRetV11
                            ProcFilIns(cBranchV11, cRegraParam, aFiliais, @lRetV11)
                        endif    
                    Endif

                    //Caso a alteração das regra tenha sido bem sucedida
                    If lRetV10 .And. lRetV11; GetRespCalCulationRules(cRegraParam, @oJsResponse, @cErrorMessage); else; cErrorMessage := "Não foi possível alterar a regra, aguarda alguns minutos e tente novamente! "; DisarmTransaction(); endif
                end transaction
            endif
        else
            if empty(cErrorMessage); cErrorMessage := 'Para alterar uma regra, é obrigatório que os períodos existentes estejam abertos!'; endif
        endif
    else
        cErrorMessage := "O codigo da regra nao foi informado ou nao existe no cadastro."
    endif

    if !empty(cErrorMessage)
        nStatus := CodeRespBadRequest
        oJsResponse['code'] := nStatus
        oJsResponse['detailMessage'] := cErrorDetail
        oJsResponse['message'] := cErrorMessage
    endif   

    oRest:setStatusCode(nStatus)
    oRest:setResponse(oJsResponse) 

    FWFreeObj(oJsResponse)
    oJsResponse := nil

return 

/*-------------------------------------------------------------------
{Protheus.doc} DEL DelCalculationRules
Faz a Delete Logico nos registros de Regra de apuração

@author	adilson.roberto
@since	19/01/2024
-------------------------------------------------------------------*/
@DELETE(endpoint='/api/taf/fiscal/v1/calculationRules/:id', description='API de delete das Configurações de Apuração') 
Function TAFDelCalculationRules()

Local lDeleteOk := .f. as logical
Local lHasClosePeriods := .f. as logical
Local oJsResponse := JsonObject():new() as json
Local cBranch     := xFilial('V10') as character
Local cBranchV11  := xFilial('V11') as character
Local cFilialV8Z  := xFilial('V8Z')
Local cRegraParam := oRest:getPathParamsRequest()['id'] as character
Local cErrorMessage as character
Local aIdPeriodos as array 
Local nStatus := CodeNoContent as integer
Local oVldDelete := FwExecStatement():New() as object

//Valida se tem algum periodo encerrado para a regra\filial
cQuery := " SELECT COUNT(V13_ID) REGS FROM " + RetSqlName('V13')
cQuery += " WHERE D_E_L_E_T_ = ' ' "
cQuery += " 	AND V13_FILIAL = '" + xFilial('V13') + "' "
cQuery += " 	AND V13_REGID = ? "
cQuery += " 	AND V13_STATUS = ? "
cQuery := ChangeQuery(cQuery)

oVldDelete:SetQuery(cQuery)
oVldDelete:SetString(1, cRegraParam)
oVldDelete:SetString(2, StatusFechado)
lHasClosePeriods := oVldDelete:ExecScalar('REGS') != 0

//Se não existem periodos encerrados para a regra
if !lHasClosePeriods

    //Retorna todos os ids de periodo da regra 
    aIdPeriodos := GetIdPeriods(cRegraParam,, .t.)

    //Caso o periodo já esteja apurado, desfaz a apuração antes de deletar a regra
    If !TafDesfazApuracao(aIdPeriodos); cErrorMessage := "Não foi possível desfazer as apurações da regra, tente novamente mais tarde!"; EndIf

    If Empty(cErrorMessage)
        Begin transaction
            V10->(DBSetOrder(1))   

            //Deleta a regra
            If V10->(MsSeek(cBranch + cRegraParam))
                lDeleteOk := V10->(RecLock('V10',.F.))
                
                If lDeleteOk
                    V10->(dbDelete())
                    V10->(MsUnLock())
                EndIf
            Endif

            //Deleta as filiais vinculadas a regra
            if lDeleteOk
                V11->(DBSetOrder(1))

                If V11->(MsSeek(cBranchV11 + cRegraParam))
                    While V11->(V11_FILIAL+V11_CODIGO) == (cBranchV11+cRegraParam) .And. V11->(!Eof())
                        If lDeleteOk
                            lDeleteOk := V11->(RecLock('V11',.F.))
                            
                            If lDeleteOk
                                V11->(dbDelete())
                                V11->(MsUnLock())
                            EndIf
                        EndIf
                        
                        V11->(DbSkip())
                    Enddo
                Endif
            endif 

            //Deleta os perfis vinculados a regra               
            if lDeleteOk
                V8Z->(DBSetOrder(1))

                If V8Z->(MsSeek(cFilialV8Z + cRegraParam))
                    while V8Z->(!eof()) .and. V8Z->(V8Z_FILIAL+V8Z_CODREG) == cFilialV8Z + cRegraParam
                        If lDeleteOk
                            lDeleteOk := V8Z->(RecLock('V8Z',.f.))
                            
                            If lDeleteOk
                                V8Z->(DbDelete())
                                V8Z->(MsUnLock())
                            EndIf
                        EndIf

                        V8Z->(DbSkip())
                    enddo
                Endif
            Endif   

            If !lDeleteOk; cErrorMessage := "Nao foi possivel deletar o registro."; DisarmTransaction(); EndIf
        end transaction
    endif    
else                
    cErrorMessage := 'Para deletar uma regra, é obrigatório que os períodos existentes estejam abertos!'
endif
    
//Caso tenha ocorrido erro na deleção dos dados.
if !empty(cErrorMessage)
    nStatus := CodeRespBadRequest
    oJsResponse['code'] := CodeRespBadRequest
    oJsResponse['detailMessage'] := ''
    oJsResponse['message'] := cErrorMessage
endif

oRest:setStatusCode(nStatus)
oRest:setResponse( (oJsResponse )) 

oVldDelete:Destroy()
oVldDelete := nil

Return 

/*-------------------------------------------------------------------
{Protheus.doc} Post getCalculationProfileScreen
Grava os registros do cadastro de perfil de tributos

@author	adilson.roberto
@since	19/01/2024
-------------------------------------------------------------------*/
Static Function DescCalculationRules(cRegIns as character,cTribute as character) as object

    Local nPos         := 0                       as numeric
    Local nPageAux     := 0                       as numeric
    Local lHasNext     := .F.                     as logical
    Local nPage        := 1                       as numeric
    Local nPageSize    := 10                      as numeric
    Local aBranches                               as array
    Local cRule                                   as character
    Local cStartPeriod                            as character
    Local cEndPeriod                              as character
    Local oJsonResp    := JsonObject():New()      as object
    Local cAlias                                  as character
    Local cDataIni     := ""                      as character
    Local cDataFin     := ""                      as character
    Local oJsParams    := oRest:getQueryRequest() as json //Json com os parâmetros (query param)    
    Local aTributo      := StrTokArr(GetSx3Cache('V10_TRIBUT','X3_CBOX'),';') as array
    Local aRegFiscal    := StrTokArr(GetSx3Cache('V10_REGFIS','X3_CBOX'),';') as array
    Local aRegApuracao  := StrTokArr(GetSx3Cache('V10_REGAPU','X3_CBOX'),';') as array
    Local aRegTributario:= StrTokArr(GetSx3Cache('V10_REGTRI','X3_CBOX'),';') as array
    Local aFilProcess   := StrTokArr(GetSx3Cache('V10_PRFIL' ,'X3_CBOX'),';') as array
    Local aFilApu       := {}                     as array 
    Local nFil          := 1                      as numeric

    Default cRegIns  := ""
    Default cTribute := ""

    if valtype( oJsParams['page'] ) != 'U' .and. Val(oJsParams['page']) > 0
        nPage := Val(oJsParams['page'])
    endif    
    if valtype( oJsParams['pageSize'] ) != 'U' .and. Val(oJsParams['pageSize']) > 0
        nPageSize := Val(oJsParams['pageSize'])
    endif

    if valtype( oJsParams['branches'] ) != 'U' 
        aBranches := StrTokarr2(oJsParams['branches'], ",")
    endif
    if valtype( oJsParams['rule'] ) != 'U' 
        cRule := oJsParams['rule']
    endif
    if valtype( oJsParams['startPeriod'] ) != 'U'
        cStartPeriod := StrTran(oJsParams['startPeriod'],"-","")
    endif
    if valtype( oJsParams['endPeriod'] ) != 'U' 
        cEndPeriod := StrTran(oJsParams['endPeriod'],"-","")
    endif

    cAlias := GetItensRules(nPage, nPageSize, , cRegIns,cTribute,aBranches, cRule, cStartPeriod, cEndPeriod)

    oJsonResp["items"] := {}
    oJsonResp["total"] := 0

    While (cAlias)->(!Eof())
        nPageAux++
        
        if nPageAux <= nPageSize

            cDataIni := RIGHT((cAlias)->V10_VIGINI,2)+"/"+SUBSTR((cAlias)->V10_VIGINI,5,2)+"/"+LEFT((cAlias)->V10_VIGINI,4)        
            cDataFin := RIGHT((cAlias)->V10_VIGFIN,2)+"/"+SUBSTR((cAlias)->V10_VIGFIN,5,2)+"/"+LEFT((cAlias)->V10_VIGFIN,4)
            //Filial por regra
            GetFiliais((cAlias)->V10_CODIGO,@aFilApu, ,1)
            //
            aAdd( oJsonResp["items"], JsonObject():New() )
            nPos++
            oJsonResp["items"][nPos]["id"]                                 := (cAlias)->V10_CODIGO
            oJsonResp["items"][nPos]["description"]                        := DecodeUTF8(AllTrim((cAlias)->V10_DESCRI))

            oJsonResp["items"][nPos]["processBranch"]                      :=  JsonObject():New()
            oJsonResp["items"][nPos]["processBranch"]["id"]                := AllTrim((cAlias)->V10_PRFIL)
            oJsonResp["items"][nPos]["processBranch"]["description"]       := alltrim(substr( aFilProcess[ val(AllTrim((cAlias)->V10_PRFIL)) ], 3))

            oJsonResp["items"][nPos]["tribute"]                            :=  JsonObject():New() 
            oJsonResp["items"][nPos]["tribute"]["id"]                      := alltrim((cAlias)->V10_TRIBUT)
            oJsonResp["items"][nPos]["tribute"]["description"]             := alltrim(substr( aTributo[ val((cAlias)->V10_TRIBUT) ], 4))         
            //
            oJsonResp["items"][nPos]["branches"]                           := {}
            For nFil := 1 to Len(aFilApu)
                Aadd(oJsonResp["items"][nPos]["branches"], JsonObject():New() )
                oJsonResp["items"][nPos]["branches"][nFil]["id"]           := aFilApu[nFil,1]
                oJsonResp["items"][nPos]["branches"][nFil]["description"]  := aFilApu[nFil,2]
            Next 
            //
            oJsonResp["items"][nPos]["fiscalRegime"]                        :=  JsonObject():New() 
            oJsonResp["items"][nPos]["fiscalRegime"]["id"]                  := alltrim((cAlias)->V10_REGFIS)
            oJsonResp["items"][nPos]["fiscalRegime"]["description"]         := Iif(!Empty(alltrim((cAlias)->V10_REGFIS)), alltrim(substr( aRegFiscal[ val((cAlias)->V10_REGFIS) ], 4)),"")        
            //
            oJsonResp["items"][nPos]["calculationSystem"]                   :=  JsonObject():New() 
            oJsonResp["items"][nPos]["calculationSystem"]["id"]             := alltrim((cAlias)->V10_REGAPU)
            oJsonResp["items"][nPos]["calculationSystem"]["description"]    := Iif(!Empty(alltrim((cAlias)->V10_REGAPU)), alltrim(substr( aRegApuracao[ val((cAlias)->V10_REGAPU) ], 4)),"")        
            //
            oJsonResp["items"][nPos]["taxRegime"]                           :=  JsonObject():New() 
            oJsonResp["items"][nPos]["taxRegime"]["id"]                     := alltrim((cAlias)->V10_REGTRI)
            oJsonResp["items"][nPos]["taxRegime"]["description"]            := Iif(!Empty(alltrim((cAlias)->V10_REGTRI)), alltrim(substr( aRegTributario[ val((cAlias)->V10_REGTRI) ], 4)),"")        
            //
            oJsonResp["items"][nPos]["lastPeriodCalculated"]                := JsonObject():New() //Iif(!Empty(alltrim((cAlias)->V10_PERAPU)), Right((cAlias)->V10_PERAPU,2)+"/"+Left((cAlias)->V10_PERAPU,4), "")
            oJsonResp["items"][nPos]["lastPeriodCalculated"]["id"]          := ""
            oJsonResp["items"][nPos]["lastPeriodCalculated"]["description"] := ""
            //
            oJsonResp["items"][nPos]["calculationBalance"]                  :=  JsonObject():New() 
            oJsonResp["items"][nPos]["calculationBalance"]["id"]            := Iif(!Empty(alltrim((cAlias)->V10_IDSLD)), Alltrim((cAlias)->V10_IDSLD), "")
            oJsonResp["items"][nPos]["calculationBalance"]["description"]   := Iif(!Empty(alltrim((cAlias)->V10_DCSLD)), Alltrim((cAlias)->V10_DCSLD), "")               
            //
            oJsonResp["items"][nPos]["ruleValidity"]                        := cDataIni + " - " + cDataFin
        Else
            lHasNext := .t.
            Exit
        EndIf

        (cAlias)->(DbSkip())

    EndDo

    If !Empty(oJsonResp["items"])
        oJsonResp["total"] := GetTotal(cRegIns, cTribute)
    EndIf

    oJsonResp["hasNext"] := lHasNext

    If lHasNext
        If Empty(cTribute)
            oJsonResp["nextPageHref"] := "/api/taf/fiscal/v1/calculationRules?page=" + cValtoChar(nPage + 1) + "&pageSize=" + cValtoChar(nPageSize)
        Else
            oJsonResp["nextPageHref"] := "/api/taf/fiscal/v1/calculationRules?page=" + cValtoChar(nPage + 1) + "&pageSize=" + cValtoChar(nPageSize) + "&tribute="+cTribute 
        Endif    
    Else
        oJsonResp["nextPageHref"] := ""
    EndIf

    (cAlias)->(DBCloseArea())

Return oJsonResp


/*--------------------------------------------------------------------
{Protheus.doc} GetItensRules()
(Responsável por executar a consulta aos campos da tabela V10)
@author adilson.roberto
@since 06/02/2024
//-------------------------------------------------------------------*/
Static Function GetItensRules(nPage as integer, nPageSize as integer, cTrib as character, cRegIns as character,cTribute as character, aBranches as array, cRule as character,cStartPeriod as character, cEndPeriod as character)
    
    Local cRet    := GetNextAlias() as character
    Local cQuery  := ''  as character

    DEFAULT nPage        := 1
    DEFAULT nPageSize    := 30
    Default cRegIns      := ""
    Default cTrib        := ""
    Default cTribute     := ""
    Default aBranches    := {}
    Default cRule        := ""
    Default cStartPeriod := ""
    Default cEndPeriod   := ""

    cQuery := " SELECT              "
    cQuery += " 	V10.V10_FILIAL, "
    cQuery += " 	V10.V10_CODIGO, "
    cQuery += " 	V10.V10_DESCRI, "
    cQuery += " 	V10.V10_TRIBUT, "
    cQuery += " 	V10.V10_REGFIS, "
    cQuery += " 	V10.V10_REGAPU, "
    cQuery += " 	V10.V10_REGTRI, "
    cQuery += " 	V10.V10_VIGINI, "
    cQuery += " 	V10.V10_VIGFIN, "
    cQuery += " 	V10.V10_IDSLD, "
    cQuery += " 	V10.V10_DCSLD, "
    cQuery += " 	V10.V10_PERAPU, "
    cQuery += " 	V10.V10_PRFIL   "
    cQuery += " FROM " + RetSqlName('V10') + " V10 "

    If !Empty(aBranches)
        cQuery += " INNER JOIN " + RetSQLName("V11") + " V11 "
        cQuery += "     ON V11.D_E_L_E_T_ = ' ' "
        cQuery += "         AND " + FWJoinFilial("V11", "V10")
        cQuery += "         AND V11.V11_CODIGO = V10.V10_CODIGO "
        cQuery += "         AND V11.V11_FILANT IN (SELECT FILIAIS.FILIAL FROM " + TAFCacheFil(Nil, aBranches, .T.) + " FILIAIS) "
    Endif

    cQuery += " WHERE V10.D_E_L_E_T_ = ' ' "
    cQuery += "	AND V10.V10_FILIAL = '" + xFilial('V10') + "' "
    
    If !Empty(cTrib)
        cQuery += " AND V10.V10_CODIGO = '" + cTrib + "' "
    Endif

    If !Empty(cRule)
        cQuery += " AND V10.V10_CODIGO = '" + cRule + "' "
    Endif

    If !Empty(cStartPeriod)
        cQuery += " AND V10.V10_VIGINI >= '" + cStartPeriod + "' "
    EndIf

    If !Empty(cEndPeriod)
        cQuery += " AND V10.V10_VIGFIN <= '" + cEndPeriod + "' "
    EndIf

    //If abaixo para o WHERE geral da query
    If !Empty(cRegIns)
        cQuery += " AND V10.V10_CODIGO IN (" + cRegIns +") "
    Endif

    If !Empty(cTribute)
        cQuery += " AND V10.V10_TRIBUT = '" + cTribute + "' "
    Endif

    cQuery += " ORDER BY V10.V10_CODIGO  "
    cQuery += " OFFSET ( " + cValToChar(nPage-1) + " * " + cValToChar(nPageSize) + " ) ROWS "
    cQuery += "     FETCH NEXT " + cValToChar(nPageSize+1) + " ROWS ONLY "

    dbUseArea(.T., "TOPCONN", TCGenQry2(, , cQuery,{}), cRet, .F., .T.)

Return cRet

/*--------------------------------------------------------------------
{Protheus.doc} GetFiliais()
(Responsável por executar a consulta aos campos da tabela V11)
@author adilson.roberto
@since 19/01/2024
//-------------------------------------------------------------------*/
Static Function GetFiliais(cRegra as character, aFilApu as array, aFilProc as array, nPass as numeric)

    Local cQuerFil      := ""   as character  
    Local cFilReg       := ""   as character  
    Local iPos          := 0    as integer

    Default aFilApu     := {}
    Default aFilProc    := {}
    Default cRegra      := ""
    Default nPass       := 1

    If !Empty(cRegra)
        If __oSelectV11 == Nil 
            cQuerFil := " SELECT V11.V11_FILANT "
            cQuerFil += "   FROM " + RetSqlName("V11") + " V11 "
            cQuerFil += "   WHERE V11.D_E_L_E_T_ = ' ' "
            cQuerFil += " 	    AND V11.V11_FILIAL = ?"
            cQuerFil += " 	    AND V11.V11_CODIGO = ? "

            cQuerFil := ChangeQuery(cQuerFil)

            __oSelectV11 := FwExecStatement():New(cQuerFil)
        EndIf

		If __oSelectV11 != Nil
            aFilApu := {}

			__oSelectV11:SetString(1, xFilial("V11"))
			__oSelectV11:SetString(2, cRegra)

            cFilReg := __oSelectV11:OpenAlias()
            
            While !(cFilReg)->(EOF())
                If nPass == 1
                    AAdd(aFilApu, {(cFilReg)->V11_FILANT, (cFilReg)->V11_FILANT + " - " + AllTrim(FWSM0Util():GetSM0Data(, (cFilReg)->V11_FILANT, {"M0_FILIAL"})[1][2])})
                ElseIf nPass == 2            
                    iPos := AScan(aFilProc, {|x| AllTrim(x[1]) == AllTrim((cFilReg)->V11_FILANT)})

                    If iPos > 0
                        aFilProc[iPos][2] := 1
                    Endif    
                Endif

                (cFilReg)->(DbSkip())
            Enddo

            (cFilReg)->(DBCloseArea())
		EndIf
    EndIf

Return 

/*-------------------------------------------------------------------
{Protheus.doc} GetRespCalCulationRules()
Monta o retorno dos metodos Get e Put do cadastro de regra de apuração

@author	adilson.roberto
@since	19/01/2024
@parametro cTrib = código da regra de apuração
-------------------------------------------------------------------*/
Static Function GetRespCalCulationRules(cTrib as character, oJsonResp as json, cMsgResponse as character)
Local cAlias        as character
Local cDataIni      := "" as character
Local cDataFin      := "" as character
Local aFilApu       := {} as array
Local aTributo      := StrTokArr(GetSx3Cache('V10_TRIBUT','X3_CBOX'),';') as array
Local aRegFiscal    := StrTokArr(GetSx3Cache('V10_REGFIS','X3_CBOX'),';') as array
Local aRegApuracao  := StrTokArr(GetSx3Cache('V10_REGAPU','X3_CBOX'),';') as array
Local aRegTributario:= StrTokArr(GetSx3Cache('V10_REGTRI','X3_CBOX'),';') as array
Local aFilProcess   := StrTokArr(GetSx3Cache('V10_PRFIL' ,'X3_CBOX'),';') as array
Local nFil          := 0 as numeric

DEFAULT cTrib := ""

cAlias := GetItensRules( , , cTrib) 

// Iniciando o While
If (cAlias)->(!Eof())

    //Filial por regra
    GetFiliais((cAlias)->V10_CODIGO,@aFilApu, ,1)

    cDataIni := RIGHT((cAlias)->V10_VIGINI,2)+"/"+SUBSTR((cAlias)->V10_VIGINI,5,2)+"/"+LEFT((cAlias)->V10_VIGINI,4)        
    cDataFin := RIGHT((cAlias)->V10_VIGFIN,2)+"/"+SUBSTR((cAlias)->V10_VIGFIN,5,2)+"/"+LEFT((cAlias)->V10_VIGFIN,4)
    
    oJsonResp["id"]                                     := (cAlias)->V10_CODIGO
    oJsonResp["description"]                            := DecodeUTF8(AllTrim((cAlias)->V10_DESCRI))
    //
    oJsonResp["processBranch"]                          := JsonObject():New()
    oJsonResp["processBranch"]["id"]                    := AllTrim((cAlias)->V10_PRFIL)
    oJsonResp["processBranch"]["description"]           := alltrim(substr( aFilProcess[ val(AllTrim((cAlias)->V10_PRFIL)) ], 3))
    //
    oJsonResp["tribute"]                                := JsonObject():New() 
    oJsonResp["tribute"]["id"]                          := alltrim((cAlias)->V10_TRIBUT)
    oJsonResp["tribute"]["description"]                 := alltrim(substr( aTributo[ val((cAlias)->V10_TRIBUT) ], 4)) 
    //
    oJsonResp["branches"]                         := {}
    For nFil := 1 to Len(aFilApu)
        Aadd(oJsonResp["branches"], JsonObject():New() )
        oJsonResp["branches"][nFil]["id"]           := aFilApu[nFil,1]
        oJsonResp["branches"][nFil]["description"]  := aFilApu[nFil,2]
    Next         
    //
    oJsonResp["fiscalRegime"]                       :=  JsonObject():New() 
    oJsonResp["fiscalRegime"]["id"]                 := alltrim((cAlias)->V10_REGFIS)
    oJsonResp["fiscalRegime"]["description"]        := Iif(!Empty(alltrim((cAlias)->V10_REGFIS)), alltrim(substr( aRegFiscal[ val((cAlias)->V10_REGFIS) ], 4)),"")        
    //
    oJsonResp["calculationSystem"]                  :=  JsonObject():New() 
    oJsonResp["calculationSystem"]["id"]            := alltrim((cAlias)->V10_REGAPU)
    oJsonResp["calculationSystem"]["description"]   := Iif(!Empty(alltrim((cAlias)->V10_REGAPU)), alltrim(substr( aRegApuracao[ val((cAlias)->V10_REGAPU) ], 4)),"")                
    //
    oJsonResp["taxRegime"]                          :=  JsonObject():New() 
    oJsonResp["taxRegime"]["id"]                    := alltrim((cAlias)->V10_REGTRI)
    oJsonResp["taxRegime"]["description"]           := Iif(!Empty(alltrim((cAlias)->V10_REGTRI)), alltrim(substr( aRegTributario[ val((cAlias)->V10_REGTRI) ], 4)),"")
    //
    oJsonResp["lastPeriodCalculated"]               := JsonObject():New() //Iif(!Empty(alltrim((cAlias)->V10_PERAPU)), Right((cAlias)->V10_PERAPU,2)+"/"+Left((cAlias)->V10_PERAPU,4), "")
    oJsonResp["lastPeriodCalculated"]["id"]         := ""
    oJsonResp["lastPeriodCalculated"]["description"]:= ""
    //
    oJsonResp["calculationBalance"]                 :=  JsonObject():New() 
    oJsonResp["calculationBalance"]["id"]           := Iif(!Empty(alltrim((cAlias)->V10_IDSLD)), Alltrim((cAlias)->V10_IDSLD), "")
    oJsonResp["calculationBalance"]["description"]  := Iif(!Empty(alltrim((cAlias)->V10_DCSLD)), Alltrim((cAlias)->V10_DCSLD), "")       
    //
    oJsonResp["ruleValidity"]  := cDataIni + " - " + cDataFin

Else

    cMsgResponse := 'Nenhuma regra encontrada ou ID invalido informado no parametro id.'

Endif
(cAlias)->(DBCloseArea())

Return 

/*-------------------------------------------------------------------
{Protheus.doc} GetNextCode()
Retorna o ultimo código do perfil para a regra passada por parâmetro

@author	Carlos Eduardo Boy
@since	26/12/23
-------------------------------------------------------------------*/
Static Function GetLastCode( cRegraParam as character) as logical
Local lReturn as logical
Local cAlias := GetNextAlias() as character
Local cQuery as character

cQuery := " SELECT "
cQuery += " 	COUNT(DISTINCT V10.V10_CODIGO) FLAG_REGRA, "
cQuery += " 	COALESCE(MAX(V10.V10_CODIGO),'000000') ULT_CODIGO"
cQuery += " FROM " + RetSqlName('V10') + " V10 "
cQuery += " WHERE V10.V10_FILIAL = '" + xFilial('V10') + "' "

dbUseArea(.T., "TOPCONN", TCGenQry2(, , cQuery,{}), cAlias, .F., .T.)

cRegraParam := alltrim((cAlias)->ULT_CODIGO)
lReturn    := (cAlias)->FLAG_REGRA > 0

(cAlias)->(DbCloseArea())

return lReturn

/*---------------------------------------------------------------------------
{Protheus.doc} ProcFilUpd
Apaga os registros da tabela(V11) que vincula a regra à filial caso alguma 
filial tenha sido desmarcada(excluida) no momento da alteração

@author	adilson.roberto
@since	19/01/2024
Parâmetros
cBranchV11  = Filial a ser processada
cRegraParam = Regra de apuração a ser processada
cFilProc    = Array contendo lista de filiais contidas na regra
lRetV11     = Informa se foi feita a alteração do registro na tabela V11
-----------------------------------------------------------------------------*/
Static Function ProcFilUpd(cBranchV11 as character, cRegraParam as character, cFilProc as character, lRetV11 as logical)

    Local cQryFil       := ""  as character
    Local cError	    := ""  as character

    Default cBranchV11  := ""
    Default cRegraParam := ""
    Default cFilProc    := ""
    Default lRetV11     := .F.

    If !Empty(cRegraParam)
        If __oUpdateV11 == Nil 
            cQryFil := " UPDATE " + RetSqlName("V11")
            cQryFil += "    SET D_E_L_E_T_ = '*', "
            cQryFil += "        R_E_C_D_E_L_ = R_E_C_N_O_ "
            cQryFil += "    WHERE D_E_L_E_T_ = ' ' "
            cQryFil += "        AND V11_FILIAL = ? "
            cQryFil += "        AND V11_CODIGO = ? "
            cQryFil += "        AND V11_FILANT NOT IN (?) "
            
            __oUpdateV11 := FwExecStatement():New(cQryFil)
        EndIf

		If __oUpdateV11 != Nil
			__oUpdateV11:SetString(1, cBranchV11)
			__oUpdateV11:SetString(2, cRegraParam)
            __oUpdateV11:SetUnsafe(3, cFilProc)

			cQryFil := __oUpdateV11:GetFixQuery()
            
			If !TAFExecQuery(cQryFil, .T., @cError); TAFConOut(cError); EndIf
            
            lRetV11 := Empty(cError)
		EndIf
    EndIf

Return

/*------------------------------------------------------------------------
{Protheus.doc} ProcFilIns
Grava os registros da tabela(V11) que vincula a regra à filial caso alguma 
filial tenha sido marcada(incluida) no momento da alteração

@author	adilson.roberto
@since	19/01/2024
Parâmetros
cBranchV11  = Filial a ser processada
cRegraParam = Regra de apuração a ser processada
aFilProc    = Array contendo lista de filiais contidas na regra
lRetV11     = Informa se foi feita a inclusão do registro na tabela V11
--------------------------------------------------------------------------*/
Static Function ProcFilIns(cBranchV11 as character, cRegraParam as character, aFilProc as array, lRetV11 as logical)

    Local aCampos       := {}   as array
    Local iProc         := 0    as integer
    Local jCampos       := Nil  as json
    Local lInsert       := .T.  as logical

    Default aFilProc    := {}
    Default cBranchV11  := ""
    Default cRegraParam := ""
    Default lRetV11     := .F.

    If !Empty(cRegraParam)
        GetFiliais(cRegraParam,, @aFilProc, 2)

        If __oInsert == Nil .Or. !Empty(__oInsert:GetError()) .Or. Empty(__oInsert:cFields)
            __oInsert := FwBulk():New(RetSQLName("V11"), 30)

            __oInsert:SetFields(V11->(DbStruct()))
        EndIf

        If __oInsert != Nil
            aCampos := StrTokArr2(__oInsert:cFields, ",")
            jCampos := JSONObject():New()

            For iProc := 1 To Len(aFilProc)
                If lInsert .And. aFilProc[iProc][2] == 0
                    jCampos["V11_FILIAL"]   := cBranchV11
                    jCampos["V11_CODIGO"]   := cRegraParam
                    jCampos["V11_FILANT"]   := aFilProc[iProc][1]
                    jCampos["V11_ID"]       := FwUUIDV4(.T.)

                    lInsert := __oInsert:AddData({	jCampos[aCampos[1]],;
                                                    jCampos[aCampos[2]],;
                                                    jCampos[aCampos[3]],;
                                                    jCampos[aCampos[4]] })
                EndIf
            Next    

            lRetV11 := .T.

            If !lInsert .Or. !Empty(__oInsert:GetError()) .Or. !__oInsert:Flush(); lRetV11 := .F.; TAFConOut(__oInsert:GetError()); EndIf
            
            FWFreeObj(jCampos)
            FWFreeArray(aCampos)
        EndIf
    EndIf

Return

/*
{Protheus.doc} GetTotal
@description Retorna o total dos registros na tabela V10 de acordo com os filtros
@params cRegIns - IDs das regras de apuração 
@params cTribute - Código do tributo
@return iTotal - Total de registros existentes na tabela V10
@author	Melkz Siqueira
@since	24/04/2024
@version 1.0
*/
Static Function GetTotal(cRegIns as character, cTribute as character) as integer

	Local cColumn       := ""   as character
	Local cQuery        := ""   as character
	Local iBind         := 0    as integer
	Local iTotal        := 0    as integer
	Local oTotal        := Nil  as object

	Default cRegIns     := ""
	Default cTribute	:= ""

	cColumn := "TOTAL"

	cQuery := " SELECT COUNT(V10.R_E_C_N_O_) " + cColumn
	cQuery += "     FROM " + RetSqlName("V10") + " V10 "
	cQuery += "     WHERE V10.D_E_L_E_T_ = ' ' "
	cQuery += "         AND V10.V10_FILIAL = ? "

	If !Empty(cRegIns)
		cQuery += "         AND V10.V10_CODIGO IN (?) "
	EndIf

	If !Empty(cTribute)
		cQuery += "         AND V10.V10_TRIBUT = ? "
	EndIf

	oTotal := FwExecStatement():New(cQuery)

	If oTotal != Nil
		oTotal:SetString(++iBind, xFilial("V10"))

		If !Empty(cRegIns)
			oTotal:SetUnsafe(++iBind, cRegIns)
		EndIf

		If !Empty(cTribute)
			oTotal:SetString(++iBind, cTribute)
		EndIf

		iTotal := oTotal:ExecScalar(cColumn)
	EndIf

Return iTotal

/*----------------------------------------------------------------------
{Protheus.doc} ValidRuleChange()
@description Retorna se a regra passada por parâmetro pode ser alterada.

@params cIdRule

@return lRet 
@author	Carlos Eduardo Boy
@since	24/04/2024
@version 1.0
-----------------------------------------------------------------------*/
Static Function ValidRuleChange(cIdRule as character, oJsonRegra as json, aIdsReturn as array, cMsgValid as character, cMsgDetail as character) as logical
Local lRet := .t.   as logical
Local lValid        as logical
Local cQryBrachs    as character
Local cQryDelete    as character
Local cQryVldPeriod as character
Local cQryVldRule   as character
Local oQryBrachsRule := FwExecStatement():New() as object 
Local oQryDelete     := FwExecStatement():New() as object
Local oQryVldPeriod  := FwExecStatement():New() as object
Local oQryVldRule    := FwExecStatement():New() as object
Local aBranchsQry    := {} as array
Local aBranchsAtu    := oJsonRegra['branches'] as array
Local aAllBranchs    := {} as array
Local aFilDetail     := {} as array   
Local i as integer
Local cIdFilial as character
Local nPosFilial as integer
Local cVigInicialAtu := strtran(oJsonRegra['expirationRuleStart'],'-','') as character
Local cVigFinalAtu   := strtran(oJsonRegra['expirationRuleEnd'  ],'-','') as character

Default cMsgValid  := ''
Default cMsgDetail := ''

/*--------------------------
Estrutura do array principal
----------------------------
aAllBranchs [x][1] Id da filial
aAllBranchs [x][2] Filial
aAllBranchs [x][3] Se é uma filial nova, que foi incluida na alteração corrente
aAllBranchs [x][4] Se esta deletando uma filial existente
aAllBranchs [x][5] Vigencia Inicial alterada
aAllBranchs [x][6] Vigencia Final Alterada*/

//Retorna todas as filiais da regra em array
cQryBrachs += " SELECT V11_ID, V11_FILANT FROM " + RetSqlName('V11')
cQryBrachs += " WHERE D_E_L_E_T_ = ' ' "
cQryBrachs += " 	AND V11_FILIAL = '" + xFilial('V11') + "' "
cQryBrachs += " 	AND V11_CODIGO = ? "
cQryBrachs := ChangeQuery(cQryBrachs)
oQryBrachsRule:SetQuery(cQryBrachs)
oQryBrachsRule:SetString(1, cIdRule)
cQryBrachs := oQryBrachsRule:GetFixQuery()
TcSqlToArr(cQryBrachs, @aBranchsQry,,)

//Valida se tem algum periodo encerrado para a regra\filial
cQryDelete := " SELECT COUNT(V13_ID) REGS FROM " + RetSqlName('V13')
cQryDelete += " WHERE D_E_L_E_T_ = ' ' "
cQryDelete += " 	AND V13_FILIAL  = '" + xFilial('V13') + "' "
cQryDelete += " 	AND V13_REGID  = ? "
cQryDelete += " 	AND V13_STATUS = ? "
cQryDelete += " 	AND V13_IDFIL  = ? "
cQryDelete := ChangeQuery(cQryDelete)
oQryDelete:SetQuery(cQryDelete)

//Valida se tem algum periodo encerrado para a regra\filial\periodo 
cQryVldPeriod := cQryDelete

cQryVldPeriod += "      AND SUBSTRING(V13_PERIOD,3,6) ? SUBSTRING(V13_PERIOD,1,2) ? ? "
cQryVldPeriod := ChangeQuery(cQryVldPeriod)
oQryVldPeriod:SetQuery(cQryVldPeriod)

//Valida regra conforme alteração da data de vigencia
cQryVldRule += " SELECT "
cQryVldRule += " 	COUNT(V11.V11_CODIGO) REGS"
cQryVldRule += " FROM " + RetSqlName('V11') + " V11"
cQryVldRule += " 	INNER JOIN " + RetSqlName('V10') + " V10 ON V10.V10_FILIAL = '" + xFilial('V10') + "' "
cQryVldRule += " 		AND V10.V10_CODIGO = V11.V11_CODIGO "
cQryVldRule += " 		AND V10.D_E_L_E_T_ = ' ' "
cQryVldRule += "    	AND ( LEFT(V10.V10_VIGINI, 6) BETWEEN ? AND ? "
cQryVldRule += "    		OR LEFT(V10.V10_VIGFIN, 6) BETWEEN ? AND ? ) "
cQryVldRule += " WHERE V11.D_E_L_E_T_ = ' ' "
cQryVldRule += " 	AND V11.V11_FILIAL = '" + xFilial('V11') + "' "
cQryVldRule += " 	AND V11.V11_FILANT =  ? "
cQryVldRule += " 	AND V11.V11_CODIGO != ? "
cQryVldRule := ChangeQuery(cQryVldRule)
oQryVldRule:SetQuery(cQryVldRule)

//Carrega as filiais de retorno da query(Todas cadastradas até então)
for i := 1 to len(aBranchsQry)
    aadd(aAllBranchs, { aBranchsQry[i][1], aBranchsQry[i][2], .f., .t.,'',''})
next

//Carrega as filiais de retorno da API(Deletadas e Incluidas)
for i := 1 to len(aBranchsAtu)
    nPosFilial := aScan(aAllBranchs, {|x| x[2] == aBranchsAtu[i]} )
    if nPosFilial = 0
        cIdFilial := GetAdvFval('V11','V11_ID', xFilial('V11')+cIdRule+aBranchsAtu[i], 1)
        aadd(aAllBranchs, {cIdFilial, aBranchsAtu[i],.t.,.f.,'',''} )
    else
        aAllBranchs[nPosFilial][4] := .f.
    endif    
next

//Caso tenham filiais desmarcadas(retiradas da regra), serão as ultimas a serem validadas
aSort(aAllBranchs,,, {|x,y| x[4] < y[4]} )

lDetail := .T.

//Passa por todas as filiais da regra, deletadas, incluídas e mantidas
for i := 1 to len(aAllBranchs)

    lValid := .t.

    //Grava no array a data de vigencia antes da alteracao
    aAllBranchs[i][5] := dtos(V10->V10_VIGINI)
    aAllBranchs[i][6] := dtos(V10->V10_VIGFIN)
    
    //Se desmarcou alguma filial da regra, a validação deve verificar todos os periodos existentes 
    if aAllBranchs[i][4]
        oQryDelete:SetString(1, cIdRule         )
        oQryDelete:SetString(2, StatusFechado   )
        oQryDelete:SetString(3, aAllBranchs[i][1])
        if oQryDelete:ExecScalar('REGS') > 0
            cMsgValid += ' - Filial: ' + aAllBranchs[i][2] + CRLF
            lValid := .f.
        endif
    else

        //Se é uma filial nova, que foi incluida na alteração corrente
            if lValid .And. aAllBranchs[i][3]
            oQryVldRule:SetString(1, left(cVigInicialAtu,6) )
            oQryVldRule:SetString(2, left(cVigFinalAtu  ,6) )
            oQryVldRule:SetString(3, left(cVigInicialAtu,6) )
            oQryVldRule:SetString(4, left(cVigFinalAtu  ,6) )            
            oQryVldRule:SetString(5, aAllBranchs[i][2])
            oQryVldRule:SetString(6, cIdRule ) 
            if oQryVldRule:ExecScalar('REGS') > 0
                lValid  := .f.
                lDetail := .f.
                aAdd(aFilDetail,  aAllBranchs[i][2])
            endif    
        Else        
            //Se foi alterado a vigencia inicial para uma data anterior ou é uma filial nova
            if lValid .And. (SToD(cVigInicialAtu) < V10->V10_VIGINI .Or. SToD(cVigInicialAtu) > V10->V10_VIGINI)
                oQryVldRule:SetString(1, left(cVigInicialAtu,6) )
                oQryVldRule:SetString(2, left(cVigFinalAtu  ,6) )
                oQryVldRule:SetString(3, left(cVigInicialAtu,6) )
                oQryVldRule:SetString(4, left(cVigFinalAtu  ,6) )            
                oQryVldRule:SetString(5, aAllBranchs[i][2])
                oQryVldRule:SetString(6, cIdRule )
                if oQryVldRule:ExecScalar('REGS') > 0
                    lValid := .f.
                    lDetail := .f.
                    aAdd(aFilDetail,  aAllBranchs[i][2])
                endif
            endif

            //Se foi alterado a vigencia Final para uma data posterior ou é uma filial nova
            if lValid .and. stod(cVigFinalAtu) > V10->V10_VIGFIN 
                oQryVldRule:SetString(1, left(dtos(V10->V10_VIGINI) ,6) )
                oQryVldRule:SetString(2, left(cVigFinalAtu          ,6) )
                oQryVldRule:SetString(3, left(dtos(V10->V10_VIGINI) ,6) )
                oQryVldRule:SetString(4, left(cVigFinalAtu          ,6) )            
                oQryVldRule:SetString(5, aAllBranchs[i][2])
                oQryVldRule:SetString(6, cIdRule )
                if oQryVldRule:ExecScalar('REGS') > 0
                    lValid := .f.
                    lDetail := .f.
                    aAdd(aFilDetail,  aAllBranchs[i][2])
                endif
            endif

            //Se foi alterado a vigencia inicial para uma data posterior 
            if lValid .and. stod(cVigInicialAtu) > V10->V10_VIGINI 
                //Passa os valores do parâmetro
                oQryVldPeriod:SetString(1, cIdRule         )
                oQryVldPeriod:SetString(2, StatusFechado   )
                oQryVldPeriod:SetString(3, aAllBranchs[i][1])
                oQryVldPeriod:SetUnsafe(4, cOperConcat)
                oQryVldPeriod:SetUnsafe(5, '<')
                oQryVldPeriod:SetString(6, left(cVigInicialAtu,6))

                if oQryVldPeriod:ExecScalar('REGS') > 0
                    cMsgValid += ' - Filial: ' + aAllBranchs[i][2] + CRLF
                    lValid := .f.
                else
                    aAllBranchs[i][5] := cVigInicialAtu
                endif
            endif

            //Se foi alterado a vigencia final para uma data anterior
            if lValid .and. stod(cVigFinalAtu) < V10->V10_VIGFIN 
                //Passa os valores do parâmetro
                oQryVldPeriod:SetString(1, cIdRule         )
                oQryVldPeriod:SetString(2, StatusFechado   )            
                oQryVldPeriod:SetString(3, aAllBranchs[i][1])
                oQryVldPeriod:SetUnsafe(4, cOperConcat)            
                oQryVldPeriod:SetUnsafe(5, '>' )
                oQryVldPeriod:SetString(6, left(cVigFinalAtu,6))

                if oQryVldPeriod:ExecScalar('REGS') > 0
                    cMsgValid += ' - Filial: ' + aAllBranchs[i][2] + CRLF
                    lValid := .f.
                else
                    aAllBranchs[i][6] := cVigFinalAtu
                endif
            endif
        EndIf
    endif
next

//Se não passou por alguma validação, monta mensagem conforme alteração.
if !lValid .and. empty(cMsgValid) .or. !lDetail
    cMsgValid := 'Existem outras regras com essas mesmas configurações(Vigência x Filial), verifique seu cadastro!'
    if len(aFilDetail) > 0
        for i := 1 to len(aFilDetail)
            cMsgDetail += 'A Filial ' + aFilDetail[i] + 'já consta em outra regra com a mesma vigência! ' + '\n'
        next
    EndIf
    lRet := .f.
elseif !empty(cMsgValid)
    cMsgValid := 'Existem periodos fechados que impedem essa alteração, verifique o painel de apuração!' + CRLF + cMsgValid
    lRet := .f.
endif

//Só retorna os ids para alteração se passaram por todas as validações sem erro.
if lRet; aIdsReturn := GetIdPeriods(cIdRule, aAllBranchs); endif    

//Apaga objetos da memoria
oQryDelete:destroy()
oQryDelete := nil
oQryVldPeriod:destroy()
oQryVldPeriod := nil
oQryVldRule:destroy()
oQryVldRule := nil

return lRet 

/*-------------------------------------------------------------------
{Protheus.doc} GetIdPeriods()
@description Retorna um array com o id dos periodos da regra passada 
             por parametro 

@params cIdRule - Código da regra
@params aDadosAltera - Dados de alteração da regra
@params lDelRegra - Se esta deletando a regra inteira

@return lRet 
@author	Carlos Eduardo Boy
@since	24/04/2024
@version 1.0
--------------------------------------------------------------------*/
Static Function GetIdPeriods(cIdRule as character, aDadosAltera as array, lDelRegra as logical) as array
Local aReturn := {} as array
Local i as integer 
Local cQuery    as character
Local cAliasQry as character
Local oQryIds       := FwExecStatement():New() as object
Local oQryIdsFiltro := FwExecStatement():New() as object

Default aDadosAltera := {}
Default lDelRegra := .f.

/*------------------------------------
|           Observacao                |
--------------------------------------
Esse tratamento é feito com foco de que só é possível a alteração dos campos vigencia e/ou filiais.
Com isso só vai desfazer a puração dos periodos que estiverem dentro dos parâmetros de alteração, 
se formos pensar que quaisquer outros campos foram alterados, o correto seria reabrir todos os períodos 
ja fechado da regra em questão. */

cQuery += " SELECT "
cQuery += " 	V13_ID  "
cQuery += " FROM " + RetSqlName('V13')
cQuery += " WHERE D_E_L_E_T_ = ' ' "
cQuery += " 	AND V13_FILIAL = '" + xFilial('V13') + "' "
cQuery += " 	AND V13_REGID = ? "

if !lDelRegra
    cQuery += " 	AND V13_IDFIL = ? "
    cQuery := ChangeQuery(cQuery)
    oQryIds:SetQuery(cQuery)

    cQuery += " 	AND  "
    cQuery += " 		( "
    cQuery += " 			SUBSTRING(V13_PERIOD,3,6) ? SUBSTRING(V13_PERIOD,1,2) < ? OR "
    cQuery += " 			SUBSTRING(V13_PERIOD,3,6) ? SUBSTRING(V13_PERIOD,1,2) > ?    "
    cQuery += " 		)  "

    cQuery := ChangeQuery(cQuery)
    oQryIdsFiltro:SetQuery(cQuery)


    for i := 1 to len(aDadosAltera)

        if aDadosAltera[i][4]
            oQryIds:SetString(1, cIdRule )
            oQryIds:SetString(2, aDadosAltera[i][1])
            cAliasQry := oQryIds:OpenAlias()
        else    
            oQryIdsFiltro:SetString(1, cIdRule )
            oQryIdsFiltro:SetString(2, aDadosAltera[i][1])
            oQryIdsFiltro:SetUnsafe(3, cOperConcat )
            oQryIdsFiltro:SetString(4, left(aDadosAltera[i][5],6) )
            oQryIdsFiltro:SetUnsafe(5, cOperConcat )            
            oQryIdsFiltro:SetString(6, left(aDadosAltera[i][6],6) )
            cAliasQry := oQryIdsFiltro:OpenAlias()
        endif    
    
        //Se foi encontrado periodo com os parâemtros passados
        while (cAliasQry)->(!eof())
            aadd(aReturn,{(cAliasQry)->V13_ID, .t. })
            (cAliasQry)->(DbSkip())
        enddo

        (cAliasQry)->(DbCloseArea())
        cAliasQry := ''

    next
else

    cQuery := ChangeQuery(cQuery)
    oQryIds:SetQuery(cQuery)
    oQryIds:SetString(1, cIdRule)    
    cAliasQry := oQryIds:OpenAlias()

    while (cAliasQry)->(!eof())
        aadd(aReturn,{(cAliasQry)->V13_ID, .t. })
        (cAliasQry)->(DbSkip())
    enddo

    (cAliasQry)->(DbCloseArea())

endif

oQryIds:Destroy()
oQryIds := nil

oQryIdsFiltro:Destroy()
oQryIdsFiltro := nil

return aReturn
