#INCLUDE 'tlpp-core.th'
#INCLUDE 'tlpp-rest.th'

/*-------------------------------------------------------------------
{Protheus.doc} GET getCalculationProfileScreen
Retorna os registros do docadastro de perfil de tributos
@author	Carlos Eduardo Boy
@since	22/09/2022
-------------------------------------------------------------------*/
@GET(endpoint='/api/taf/fiscal/v1/calculationRules/:ruleId/calculationRulesProfilesIPI', description="Consulta registros do cadastro de perfil de apuração")   
Function TAFGetCalculationProfileScreen() as logical 
Local oJsResponse                                            as json
Local cRegraParam := oRest:getPathParamsRequest()['ruleId']  as character

//O parâmetro ruleId é obrigatório
if valtype(cRegraParam) != 'U' .and. !empty(cRegraParam)
    oJsResponse := GetResponseProfile(cRegraParam)
else
    oJsResponse := JsonObject():new()
    oJsResponse['code'] := 400
    oJsResponse['detailMessage'] := ''
    oJsResponse['message'] := "O id. da regra relacionada não foi informada no parâmetro ruleId."
    oRest:setStatusCode(400)
endif     

oRest:setResponse( (oJsResponse ))  

Return 

//-------------------------------------------------------------------
/*/{Protheus.doc} GET by ID
Retorna o perfil de tributo filtrado por ID

@author Evandro Italo
@since 08/01/2024
@version 12.1.2410
/*/
//-------------------------------------------------------------------
@GET(endpoint="/api/taf/fiscal/v1/calculationRules/:ruleId/calculationRulesProfilesIPI/:id", description="Consulta registros do cadastro de perfil de apuração por ID")
Function TAFCalculationRulesProfileId()
    Local cId         as character
    Local cRegraParam as character
    Local oJsResponse as json
    
    cRegraParam := oRest:getPathParamsRequest()['ruleId'] 
    cId         := oRest:getPathParamsRequest()['id']   

    if valtype(cRegraParam) != 'U' .and. !empty(cRegraParam) .and. !empty(cId)
            oJsResponse := GetResponseProfile(cRegraParam, cId, .T.)
    else
        oJsResponse := JsonObject():new()
        oJsResponse['code'] := 400
        oJsResponse['detailMessage'] := ''
        oJsResponse['message'] := "O id. da regra relacionada não foi informada no parâmetro ruleId."
    oRest:setStatusCode(400)    
    endif  

    oRest:setResponse( (oJsResponse ))  

Return

/*-------------------------------------------------------------------
{Protheus.doc} Post getCalculationProfileScreen

Grava os registros do cadastro de perfil de tributos.
Foi definido que o post fará a função de ( POST, PUT, DELETE ).

Funções:
POST    - postV8Z
PUT     - putV8Z
Delete  - deleteV8Z


@author	Evandro Italo
@since	13/01/2024
-------------------------------------------------------------------*/
@POST(endpoint='/api/taf/fiscal/v1/calculationRules/:ruleId/calculationRulesProfilesIPI', description="Grava registros do cadastro de perfil de apuração") 
Function TAFPostCalculationProfileScreen()
Local i                     as       integer
Local oJsBody               :=       JsonObject():new() as Json
Local oJsResponse           :=       JsonObject():new() as json
Local cRegraParam           :=       oRest:getPathParamsRequest()['ruleId'] as character
Local cOperation            := "" as character
Local cProfileId            := "" as character
Local cCodigoPerfil         as       character
Local cPerfilFiltroResponse as       character
Local cPerfilEdit           as       character   
Local oResponse             as       json

Begin Transaction
//Verifica se existe a regra passada por parâmetro e retorna o ultimo codigo do perfil 
if !empty(cRegraParam)

   //Recupera o conteudo do body
    oJsBody:fromJson( oRest:GetBodyRequest() )

    for i := 1 to len(oJsBody)  
        cOperation := oJsBody[i]['operation']

        If cOperation == "add"
            cCodigoPerfil := postV8Z(cRegraParam, oJsBody[i])  

            If !Empty(cCodigoPerfil)
                if i == 1
                    cPerfilFiltroResponse := "'" + cCodigoPerfil + "'"
                else
                    cPerfilFiltroResponse := cPerfilFiltroResponse  + ", " + "'" + cCodigoPerfil + "'"
                EndIF
            EndIf
        Elseif cOperation == "edit"
            cCodigoPerfil := oJsBody[i]['id']
            cPerfilEdit   := putV8Z(cRegraParam, cCodigoPerfil, oJsBody[i])
            
            if i == 1
                cPerfilFiltroResponse := "'" + cPerfilEdit + "'"
            else
                cPerfilFiltroResponse := cPerfilFiltroResponse  + ", " + "'" + cPerfilEdit + "'"
            EndIF

        Elseif cOperation == "delete"
            cProfileId := oJsBody[i]['id']
            deleteV8Z(cRegraParam, cProfileId) 
        EndIf         
    next

    If !EMPTY( cPerfilFiltroResponse )
        oResponse := GetResponseProfile(cRegraParam,cPerfilFiltroResponse) 
        oJsResponse := oResponse
    EndIf
        
    oRest:setStatusCode(201)
else
    oJsResponse := JsonObject():new()
    oJsResponse['code'] := 400
    oJsResponse['detailMessage'] := ''
    oJsResponse['message'] := "O código da regra não foi informado ou não existe no cadastro"
    oRest:setStatusCode(400)
endif       

End Transaction

oRest:setResponse( (oJsResponse )) 

return 

/*-------------------------------------------------------------------
{Protheus.doc} GetResponseProfile()
Monta o retorno dos metodos Get e Put do cadastro de perfil

@author	Carlos Eduardo Boy
@since	26/12/23
-------------------------------------------------------------------*/
Static function GetResponseProfile(cRegra as character, cCodPerfil as character, lById as logical) 

Local cAlias           := getNextAlias()                                     as character
Local cQuery                                                                 as character
Local cQryCount                                                              as character
Local cBranch          := xFilial('V8Z')                                     as character //Formata a string para somar com a query
Local oJsResponse      := JsonObject():new()                                 as json
Local nPosItem                                                               as integer
Local nTotalRegs                                                             as integer
Local lHasNext                                                               as logical
Local nPage            := 1                                                  as integer
Local nPageSize        := 10                                                 as integer
Local cPageUrnCurrent                                                        as character
Local cPageUrnNext                                                           as character
Local cNextPageHref                                                          as character
Local cCodDescRecolhi                                                        as character
Local cMetodoRequest   := oRest:getMethodRequest()                           as character
Local aPeriodoApuracao := StrTokArr(GetSx3Cache('V8Z_PERAPU','X3_CBOX'),';') as array
Local aTipoVencimento  := StrTokArr(GetSx3Cache('V8Z_TPVENC','X3_CBOX'),';') as array
Local aDecad           := StrTokArr(GetSx3Cache('V8Z_DECAD' ,'X3_CBOX'),';') as array

Default cRegra      := ""
Default cCodPerfil  := ""
Default lById       := .F.

cQryCount := " SELECT COUNT(V8Z.V8Z_CODREG) TOTALREGS"
cQryCount += " FROM " + RetSqlName('V8Z') + ' V8Z '
cQryCount += " WHERE V8Z.D_E_L_E_T_ = ' ' "
cQryCount += "     AND V8Z.V8Z_CODREG = '" + cRegra + "' "

cQuery := " SELECT "
cQuery += "     V8Z.V8Z_CODIGO, "
cQuery += "     V8Z.V8Z_PERAPU, "
cQuery += " 	V8Z.V8Z_TPVENC, "
cQuery += " 	V8Z.V8Z_DTVENC, "
cQuery += " 	V8Z.V8Z_CODOBR, "
cQuery += " 	V8Z.V8Z_CODREC, "
cQuery += " 	COALESCE(V8Z.V8Z_DECAD, ' ') V8Z_DECAD, "
cQuery += " 	C6R.C6R_CODIGO, "
cQuery += " 	C6R.C6R_DESCRI, "
cQuery += " 	C6R.C6R_ID      "
cQuery += " FROM " + RetSqlName('V8Z') + " V8Z "
cQuery += "     LEFT JOIN " + RetSqlName('C6R') + " C6R ON C6R.C6R_FILIAL = '" + xFilial('C6R') + "' AND C6R.C6R_ID = V8Z.V8Z_CODREC AND C6R.D_E_L_E_T_ = ' ' "
cQuery += " WHERE V8Z.D_E_L_E_T_ = ' ' "
cQuery += "     AND V8Z.V8Z_CODREG = '" + cRegra + "' "

If !empty(cCodPerfil) .AND. cMetodoRequest == 'GET'
    cQuery += "     AND V8Z.V8Z_CODIGO IN ('" + cCodPerfil + "') "
EndIf

if cMetodoRequest == 'POST'
    cQuery += "     AND V8Z.V8Z_CODIGO IN (" + cCodPerfil + ") "
    cQuery += "     AND V8Z.V8Z_FILIAL = '" + cBranch + "' "
    cQuery += " ORDER BY V8Z.V8Z_CODREG, V8Z.V8Z_CODIGO "

elseif cMetodoRequest == 'GET'

    //Verifico se foi passado os parametros page e pagesize 
    oJParams := oRest:getQueryRequest()

    if valtype(oJParams['page']) != 'U' .and. val(oJParams['page']) > 0
        nPage := val(oJParams['page'])
    endif        

    if valtype(oJParams['pageSize']) != 'U' .and. val(oJParams['pageSize']) > 0
        nPageSize := val(oJParams['pageSize'])
    endif 

    //Filtro Filial
        cQuery += "     AND V8Z.V8Z_FILIAL = '" + cBranch + "' "
        cQryCount += "     AND V8Z.V8Z_FILIAL = '" + cBranch + "' "

    //Abro a query com o contador de registros e armazeno na variavel nTotalRegs
    DbUseArea(.T., "TOPCONN", TCGenQry2(, , cQryCount, {}), cAlias, .F., .T.)
    nTotalRegs := (cAlias)->TOTALREGS
    (cAlias)->(DbCloseArea())

    cQuery += " ORDER BY V8Z.V8Z_CODREG, V8Z.V8Z_CODIGO "

    cQuery += " OFFSET ( " + cValToChar(nPage-1) + " * " + cValToChar(nPageSize) + " ) ROWS "
    cQuery += " FETCH NEXT " + cValToChar(nPageSize+1) + " ROWS ONLY "    

endif

//Abro a query com os dados que serão retornados pelo GET
dbUseArea(.T., "TOPCONN", TCGenQry2(, , cQuery, {}), cAlias, .F., .T.)

oJsResponse['items'] := {}

while (cAlias)->(!eof())

    nPosItem++

    if nPosItem <= nPageSize .OR. cMetodoRequest == 'POST'
        
        cCodDescRecolhi := (cAlias)->(alltrim(C6R_CODIGO) + ' - ' + alltrim(C6R_DESCRI))
        
        aadd(oJsResponse['items'], JsonObject():New())
        oJsResponse['items'][nPosItem]['id'                  ]                  := (cAlias)->V8Z_CODIGO

        oJsResponse['items'][nPosItem]['calculationPeriod'   ]                  := JsonObject():New()                
        oJsResponse['items'][nPosItem]['calculationPeriod'   ]['id']            := ALLTRIM((cAlias)->V8Z_PERAPU)
        oJsResponse['items'][nPosItem]['calculationPeriod'   ]['description']   := alltrim(substr( aPeriodoApuracao[ val((cAlias)->V8Z_PERAPU) ], 3))

        oJsResponse['items'][nPosItem]['expirationDateType'  ]                  := JsonObject():New()  
        oJsResponse['items'][nPosItem]['expirationDateType'  ]['id']            := ALLTRIM((cAlias)->V8Z_TPVENC)
        oJsResponse['items'][nPosItem]['expirationDateType'  ]['description']   := alltrim(substr( aTipoVencimento[ val((cAlias)->V8Z_TPVENC) ], 3))

        oJsResponse['items'][nPosItem]['expirationDay'       ]                  := (cAlias)->V8Z_DTVENC

        oJsResponse['items'][nPosItem]['collectionCode'      ]                  := JsonObject():New()  
        oJsResponse['items'][nPosItem]['collectionCode'      ]['id']            := ALLTRIM((cAlias)->C6R_ID)
        oJsResponse['items'][nPosItem]['collectionCode'      ]['description']   := cCodDescRecolhi

        oJsResponse['items'][nPosItem]['decad'               ]                  := JsonObject():New() 
        oJsResponse['items'][nPosItem]['decad'               ]['id']            := ALLTRIM((cAlias)->V8Z_DECAD)
        oJsResponse['items'][nPosItem]['decad'               ]['description']   := IIf(Empty((cAlias)->V8Z_DECAD), "", Alltrim(SubStr(aDecad[Val((cAlias)->V8Z_DECAD)], 3)))

    else
        lHasNext := .t.
        exit
    endif    

    (cAlias)->(DbSkip())
enddo
(cAlias)->(DbCloseArea())    

If lById
    If !Empty(oJsResponse['items']) .And. Len(oJsResponse['items']) == 1
        oJsResponse := oJsResponse['items'][1]
    EndIf

    Return oJsResponse
EndIf

oJsResponse['total'] := iif(cMetodoRequest == 'POST', nPosItem, nTotalRegs )

if cMetodoRequest == 'GET'
    //Monta a URL de referência para a próxima página caso exista
    if lHasNext
        cPageUrnCurrent := 'page='+cValToChar(nPage)
        cPageUrnNext    := 'page='+cValToChar(nPage+1)
        if at('page=', oRest:URN ) > 0
            cNextPageHref := strtran( oRest:URN, cPageUrnCurrent, cPageUrnNext )
        else
            cNextPageHref := rtrim(oRest:URN)+'&'+cPageUrnNext
        endif

        if  at('pageSize=', oRest:URN) = 0
            cNextPageHref += '&pageSize=' + cValToChar(nPageSize)
        endif 

    endif    

    oJsResponse['hasNext'] := lHasNext
    oJsResponse['nextPageHref'] := cNextPageHref     
endif

return oJsResponse


//-------------------------------------------------------------------
/*/{Protheus.doc} postV8Z
@description Função de POST add, irá adicionar o registro no BD
@param cRegraParam - Regra de apuração
@param jBody - Objeto JSON contendo os parâmetros do corpo da requisição
@author Evandro Italo   
@since 13/01/2024
@version 2.0
/*/
//-------------------------------------------------------------------
Static Function postV8Z(cRegraParam as character, jBody as json)

    Local cCodigoPerfil := "" as character
    Local cBranch       := "" as character

    Default cRegraParam := ""
    Default jBody       := Nil

    If !Empty(cRegraParam) .And. jBody != Nil
        cBranch         := xFilial("V8Z")
        cCodigoPerfil   := FwUUIDV4(.T.)

        //Guardo o primeiro codigo do perfil cadastrado
        If V8Z->(RecLock("V8Z", .T.))
            V8Z->V8Z_FILIAL := cBranch
            V8Z->V8Z_CODREG := cRegraParam
            V8Z->V8Z_CODIGO := cCodigoPerfil
            V8Z->V8Z_PERAPU := jBody["calculationPeriod"]
            V8Z->V8Z_TPVENC := jBody["expirationDateType"]
            V8Z->V8Z_DTVENC := jBody["expirationDay"]
            V8Z->V8Z_CODREC := jBody["collectionCode"]
            V8Z->V8Z_DECAD  := jBody["decad"]

            V8Z->(MsUnLock())
        EndIf
    EndIf

Return cCodigoPerfil

//-------------------------------------------------------------------
/*/{Protheus.doc} putV8Z


Função de POST editar, irá adicionar o registro no BD

@author Evandro Italo   
@since 13/01/2024
@version 12.1.2410
/*/
//-------------------------------------------------------------------

Static Function putV8Z(cRuleId as character, cIdProfile as character, oJsBody as json)
    

    V8Z->(MsSeek(FWxFilial("V8Z") + cRuleId + cIdProfile )) 
    RecLock('V8Z',.F.)
    V8Z->V8Z_PERAPU := oJsBody['calculationPeriod'   ]
    V8Z->V8Z_TPVENC := oJsBody['expirationDateType'  ]
    V8Z->V8Z_DTVENC := oJsBody['expirationDay'       ]
    V8Z->V8Z_CODREC := oJsBody['collectionCode'      ]
    V8Z->V8Z_DECAD  := oJsBody['decad'               ]
    V8Z->(MsUnLock())

Return cIdProfile

//-------------------------------------------------------------------
/*/{Protheus.doc} deleteV8Z


Função de POST Delete, irá deletar o registro no BD

@author Evandro Italo   
@since 13/01/2024
@version 12.1.2410
/*/
//-------------------------------------------------------------------

Static Function deleteV8Z(cRuleId as character, cProfileId as character)

    V8Z->(MsSeek(FWxFilial("V8Z") + cRuleId + cProfileId ))
    RecLock('V8Z',.F.)             
    DbDelete()
    V8Z->(MsUnLock())

Return 
