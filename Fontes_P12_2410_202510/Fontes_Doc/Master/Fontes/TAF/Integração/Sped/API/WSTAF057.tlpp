#INCLUDE 'tlpp-core.th'
#INCLUDE 'tlpp-rest.th'

#DEFINE OBLIGATION_CODES ""

/*-------------------------------------------------------------------
{Protheus.doc} getObligation()
Monta o retorno dos metodos Get e GetID

@author	Evandro Italo
@since  19/01/2024
-------------------------------------------------------------------*/

@GET(endpoint='/api/taf/fiscal/v1/obligationCodes', description='Consulta os códigos da obrigação')
Function TAFGetObligationCode() as logical

Local oJsResponse as json
Local oJParams    as json

oJParams := oRest:getHeaderRequest()

oJsResponse := getObligation(OBLIGATION_CODES)

oRest:setResponse( (oJsResponse ))

Return

/*-------------------------------------------------------------------
{Protheus.doc} getObligation()
Monta o retorno dos metodos Get e GetID

@author	Evandro Italo
@since  19/01/2024
-------------------------------------------------------------------*/

@GET(endpoint="/api/taf/fiscal/v1/obligationCodes/:id", description='Consulta os códigos da obrigação por ID')
Function TAFGetObligationCodeByID()

	Local cOblig      as character
	Local oJsResponse as json

	cOblig := oRest:getPathParamsRequest()['id']

	oJsResponse := getObligation(cOblig)

	oRest:setResponse( (oJsResponse ))

Return


/*-------------------------------------------------------------------
{Protheus.doc} getObligation()
Monta o retorno dos metodos Get e GetID

@author	Evandro Italo
@since  19/01/2024
-------------------------------------------------------------------*/
Static Function getObligation(cOblig as character)

Local cAlias          as character
Local cQry            as character
Local cPageUrnCurrent as character
Local cPageUrnNext    as character
Local cNextPageHref   as character
Local cDescription    as character
Local lHasNext        as logical
Local nPosItem        as Numeric
Local nPage           as Numeric
Local nPageSize       as Numeric
Local oJsResponse     as json
Local oJParams        as json

cQry            := ""
cAlias          := getNextAlias()
cDescription    := ""
nPage           := 1
nPageSize       := 10
oJsResponse     := JsonObject():new()

cQry := " SELECT "
cQry += " C3E.C3E_ID, C3E.C3E_CODIGO, C3E.C3E_DESCRI "
cQry += " FROM " + RetSqlName('C3E') + " C3E " 
cQry += " WHERE C3E.D_E_L_E_T_ = '' "

If !EMPTY(cOblig)
    cQry += " AND C3E.C3E_ID = '" + cOblig + "' "
EndIf

cQry += " ORDER BY C3E.C3E_ID, C3E_CODIGO, C3E_DESCRI "

//Verifico se foi passado os parametros page e pagesize 
oJParams := oRest:getQueryRequest()
if valtype(oJParams['page']) != 'U' .and. val(oJParams['page']) > 0
    nPage := val(oJParams['page'])
endif        
If valtype(oJParams['pageSize']) != 'U' .and. val(oJParams['pageSize']) > 0
    nPageSize := val(oJParams['pageSize'])
EndIf 

cQry += " OFFSET ( " + cValToChar(nPage-1) + " * " + cValToChar(nPageSize) + " ) ROWS "
cQry += " FETCH NEXT " + cValToChar(nPageSize+1) + " ROWS ONLY "    


dbUseArea(.T., "TOPCONN", TCGenQry2(, , cQry, {}), cAlias, .F., .T.)

While (cAlias)->(!EOF())
    nPosItem++

    //Cria a porpriedade items como array
    If empty(cOblig) 
        If nPosItem = 1; oJsResponse['items'] := {}; Endif
    EndIf

    If nPosItem <= nPageSize
        // Concatenar string de descrição
        cDescription := (cAlias)->(alltrim(C3E_CODIGO) + ' - ' + alltrim(C3E_DESCRI))
        If empty(cOblig) 
            Aadd(oJsResponse['items'], JsonObject():New())
            oJsResponse['items'][nPosItem]['id'         ] := ALLTRIM((cAlias)->C3E_ID)
            oJsResponse['items'][nPosItem]['description'] := cDescription
        Else 
            oJsResponse :=  JsonObject():New()
            oJsResponse['id'         ] := ALLTRIM((cAlias)->C3E_ID)
            oJsResponse['description'] := cDescription     
        EndIf       
    Else
        lHasNext := .t.
        exit
    EndIf
    (cAlias)->(DbSkip())

Enddo

(cAlias)->(DbCloseArea()) 

If lHasNext
    cPageUrnCurrent := 'page='+cValToChar(nPage)
    cPageUrnNext    := 'page='+cValToChar(nPage+1)
    If at('page=', oRest:URN ) > 0
        cNextPageHref   := strtran( oRest:URN, cPageUrnCurrent, cPageUrnNext )
    Else
        cNextPageHref := rtrim(oRest:URN)+'&'+cPageUrnNext
    EndIf
    If  at('pageSize=', oRest:URN) = 0
        cNextPageHref += '&pageSize=' + cValToChar(nPageSize)
    EndIf    
EndIf 

If EMPTY(cOblig)
    oJsResponse['hasNext'     ] := lHasNext
    oJsResponse['nextPageHref'] := cNextPageHref       
EndIf

Return oJsResponse
