#INCLUDE 'tlpp-core.th'
#INCLUDE 'tlpp-rest.th'

/*-------------------------------------------------------------------
{Protheus.doc} getCodesCollection()
Monta o retorno dos metodos Get e GetID

@author	Evandro Italo
@since  19/01/2024
-------------------------------------------------------------------*/

@GET(endpoint='/api/taf/fiscal/v1/collectionCodes', description='Consulta os códigos de Recolhimento')
Function TAFGetCodesCollection()

Local cFilter     as character
Local oJsResponse as json
Local oJParams    as json

oJParams := oRest:getHeaderRequest()
oJsResponse := JsonObject():new()

cFilter     := oRest:getQueryRequest()['filter']
oJsResponse := getCollectionCodes(Nil, cFilter)

oRest:setResponse( (oJsResponse ))

Return

/*-------------------------------------------------------------------
{Protheus.doc} getByIDCollection()
Monta o retorno dos metodos Get e GetID

@author	Evandro Italo
@since  19/01/2024
-------------------------------------------------------------------*/

@GET(endpoint="/api/taf/fiscal/v1/collectionCodes/:id", description='Consulta os códigos de Recolhimento por ID')
Function TAFGetByIDCollection()

	Local cOblig         as character
	Local oJsResponse    as json
    Local oJParams       as json

    oJsResponse := JsonObject():new()
    oJParams    := oRest:getHeaderRequest()

    cOblig      := oRest:getPathParamsRequest()['id']
    oJsResponse := getCollectionCodes(cOblig)

	oRest:setResponse( (oJsResponse ))

Return


/*-------------------------------------------------------------------
{Protheus.doc} getCollectionCodes()
Monta o retorno dos metodos Get e GetID

@author	Evandro Italo
@since  19/01/2024
-------------------------------------------------------------------*/
Static Function getCollectionCodes(cOblig as character, cFilter as character)

Local cAlias          as character
Local cQry            as character
Local cPageUrnCurrent as character
Local cPageUrnNext    as character
Local cNextPageHref   as character
Local cDescription    as character
Local cDescTributo    as character
Local aDescTributo    as array
Local lHasNext        as logical
Local nPosItem        as Numeric
Local nPage           as Numeric
Local nPageSize       as Numeric
Local cTributo        as character
Local oJsResponse     as json
Local oJParams        as json

Default cOblig     := ""
Default cFilter := ""

cQry            := ""
cAlias          := getNextAlias()
cDescription    := ""
cDescTributo    := GetSX3Cache("C6R_TRIBUT","X3_CBOX") 
aDescTributo    := StrTokArr(cDescTributo,';') 
nPage           := 1
nPageSize       := 10
cTributo        := ""
oJsResponse     := JsonObject():new()
oJParams        := JsonObject():new()

oJParams := oRest:getQueryRequest()
cTributo := oJParams['tribute']

cQry := " SELECT "
cQry += " C6R.C6R_ID, C6R.C6R_CODIGO, C6R.C6R_DESCRI, C6R.C6R_TRIBUT "
cQry += " FROM " + RetSqlName('C6R') + " C6R " 
cQry += " WHERE C6R.D_E_L_E_T_ = '' "

If !EMPTY(cOblig)
    cQry += " AND (C6R.C6R_ID = '" + cOblig + "' "
    cQry += " OR C6R.C6R_CODIGO = '" + cOblig + "') "
EndIf

If !EMPTY(cTributo)
    cQry += " AND C6R.C6R_TRIBUT = '" + cTributo + "' "
EndIf

If !EMPTY(cFilter)
    cQry += " AND (C6R.C6R_CODIGO LIKE '%" + cFilter + "%' "
    cQry += "   OR C6R.C6R_DESCRI LIKE '%" + Upper(cFilter) + "%') "
EndIf

cQry += " ORDER BY C6R.C6R_ID, C6R_CODIGO, C6R_DESCRI "
 

if valtype(oJParams['page']) != 'U' .and. val(oJParams['page']) > 0
    nPage := val(oJParams['page'])
endif        
If valtype(oJParams['pageSize']) != 'U' .and. val(oJParams['pageSize']) > 0
    nPageSize := val(oJParams['pageSize'])
EndIf 

cQry += " OFFSET ( " + cValToChar(nPage-1) + " * " + cValToChar(nPageSize) + " ) ROWS "
cQry += " FETCH NEXT " + cValToChar(nPageSize+1) + " ROWS ONLY "

dbUseArea(.T., "TOPCONN", TCGenQry2(, , cQry, {}), cAlias, .F., .T.)

While (cAlias)->(!EOF())
    nPosItem++

    //Cria a porpriedade items como array
    If empty(cOblig) 
        If nPosItem = 1; oJsResponse['items'] := {}; Endif
    EndIf

    If nPosItem <= nPageSize
        // Concatenar string de descrição
        cDescription := (cAlias)->(alltrim(C6R_CODIGO) + ' - ' + alltrim(C6R_DESCRI))
        If empty(cOblig) 
            Aadd(oJsResponse['items'], JsonObject():New())
            oJsResponse['items'][nPosItem]['id'         ] := ALLTRIM((cAlias)->C6R_ID)
            oJsResponse['items'][nPosItem]['description'] := cDescription 
            oJsResponse['items'][nPosItem]['tribute']     :=  JsonObject():New()
            oJsResponse['items'][nPosItem]['tribute']['id'         ] := ALLTRIM((cAlias)->C6R_TRIBUT)
            If !empty(ALLTRIM((cAlias)->C6R_TRIBUT))
                oJsResponse['items'][nPosItem]['tribute']['description'] := alltrim(substr( aDescTributo[ val((cAlias)->C6R_TRIBUT) ], 4,Len(aDescTributo[1])))
            Else
                oJsResponse['items'][nPosItem]['tribute']['description'] := ""
            EndIf
        Else 
            oJsResponse :=  JsonObject():New()
            oJsResponse['id'         ] := ALLTRIM((cAlias)->C6R_ID)
            oJsResponse['description'] := cDescription
            oJsResponse['tribute'] :=  JsonObject():New()
            oJsResponse['tribute']['id'         ] := ALLTRIM((cAlias)->C6R_TRIBUT)
            If !empty(ALLTRIM((cAlias)->C6R_TRIBUT))
                oJsResponse['tribute']['description'] := alltrim(substr( aDescTributo[ val((cAlias)->C6R_TRIBUT) ], 4,Len(aDescTributo[1])))   
            Else
                oJsResponse['tribute']['description'] := ""
            EndIf  
        EndIf
        
    Else
        lHasNext := .t.
        exit
    EndIf
    (cAlias)->(DbSkip())

Enddo

(cAlias)->(DbCloseArea()) 

If lHasNext
    cPageUrnCurrent := 'page='+cValToChar(nPage)
    cPageUrnNext    := 'page='+cValToChar(nPage+1)
    If at('page=', oRest:URN ) > 0
        cNextPageHref   := StrTran( oRest:URN, cPageUrnCurrent, cPageUrnNext )
    Else
        cNextPageHref := Rtrim(oRest:URN)+'&'+cPageUrnNext
    EndIf
    If  at('pageSize=', oRest:URN) = 0
        cNextPageHref += '&pageSize=' + cValToChar(nPageSize)
    EndIf    
EndIf 

If EMPTY(cOblig)
    oJsResponse['hasNext'     ] := lHasNext
    oJsResponse['nextPageHref'] := cNextPageHref       
EndIf

Return oJsResponse
