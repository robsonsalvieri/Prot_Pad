#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"

/*-------------------------------------------------------------
API de consulta a lista de fornecedores.
@author daniel.aguilar
@since 11/01/2024
--------------------------------------------------------------*/
@get(endpoint='api/taf/fiscal/v1/suppliers', description='API de consulta a lista de fornecedores fiscais')
Function TAFGetCodesSuppliers() as logical

Local oJsResponse        as json

Local lRet        := .t. as logical
Local cFiltro     := oRest:getQueryRequest()['filter']



    oJsResponse := suppliers(cFiltro)
    oRest:setResponse( (oJsResponse ))


Return lRet

/*-------------------------------------------------------------
API de consulta a lista de fornecedores pelo id
@author daniel.aguilar
@since 17/01/2024
--------------------------------------------------------------*/
@get(endpoint='api/taf/fiscal/v1/suppliers/:id', description='API de consulta a lista de fornecedores fiscais pelo id')
Function TAFGetCodesIdSuppliers() as logical

Local oJsResponse        as json

Local lRet        := .t. as logical
Local cId         := oRest:getPathParamsRequest()['id']

//O parâmetro id é obrigatório
if valtype(cId) != 'U' .and. !empty(cId)
    oJsResponse := suppliersId(cId)
endif     


Return lRet

/*-------------------------------------------------------------------
{Protheus.doc} GET suppliers
Metodo responsável por preencher o Json com com informações referente a lista de fornecedores fiscais
@author Daniel Aguilar
@since 11/01/2024
-------------------------------------------------------------------*/
Static Function suppliers(cFiltro)

Local nPos         := 0                         as numeric
Local nPageAux     := 0                         as numeric
Local nPageSize    := 10                        as numeric
Local lHasNext     := .f.                       as logical
Local nPage        := 1                         as numeric
Local oJsonResp    := JsonObject():New()        as object
Local cAlias       := getNextAlias()            as character
Local oJParams     := oRest:getQueryRequest()   as json //Json com os parâmetros (query param)    

Default cFiltro := ""

if valtype(oJParams) == 'J'

    if valtype( oJParams['page'] ) != 'U' .and. Val(oJParams['page']) > 0
        nPage := Val(oJParams['page'])
    endif
    
    if valtype( oJParams['pageSize'] ) != 'U' .and. Val(oJParams['pageSize']) > 0
        nPageSize := Val(oJParams['pageSize'])
    endif
endif


GetSuppItens(@cAlias,cFiltro,nPage,nPageSize)

// Iniciando o While
While (cAlias)->(!Eof())
    nPageAux++
    
    if nPageAux == 1
        oJsonResp["items"] := {}
    endif
    if nPageAux <= nPageSize
        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++

        oJsonResp["items"][nPos]["id"]                  :=   AllTrim((cAlias)->C1H_ID)

        If  Empty(Alltrim((cAlias)->C1H_CPF))
            oJsonResp["items"][nPos]["cpfCNPJ"]         :=   AllTrim((cAlias)->C1H_CNPJ)
        Else
            oJsonResp["items"][nPos]["cpfCNPJ"]         :=   AllTrim((cAlias)->C1H_CPF)
        EndIf

        oJsonResp["items"][nPos]["uf"]                  :=   AllTrim((cAlias)->C1H_UF)
        oJsonResp["items"][nPos]["description"]         :=   AllTrim((cAlias)->C1H_NOME)
    Else
        lHasNext := .t.
        Exit
    EndIf

    (cAlias)->(DbSkip())

EndDo

    oJsonResp["hasNext"]    := lHasNext

    If lHasNext
        If !Empty(cFiltro)
            oJsonResp["nextPageHref"] := "/api/taf/fiscal/v1/suppliers?page=" + cValtoChar(nPage + 1) + "&pageSize=" + cValtoChar(nPageSize) + "&filter=" + cfiltro
        Else
            oJsonResp["nextPageHref"] := "/api/taf/fiscal/v1/suppliers?page=" + cValtoChar(nPage + 1) + "&pageSize=" + cValtoChar(nPageSize)
        EndIf

    Else

        oJsonResp["nextPageHref"] := ""

    EndIf
    

    oRest:SetResponse(oJsonResp:toJson())
    (cAlias)->(DBCloseArea())

Return

/*-------------------------------------------------------------------
{Protheus.doc} GET suppliers
Metodo responsável por preencher o Json com com informações referente a lista de fornecedores fiscais
@author Daniel Aguilar
@since 22/01/2024
-------------------------------------------------------------------*/
Static Function suppliersId(cId)

Local lRet         := .t.                       as logical
Local nPageSize    := 10                        as numeric
Local nPage        := 1                         as numeric
Local oJsonResp    := JsonObject():New()        as object
Local cAlias       := getNextAlias()            as character

Default cId        := ""


GetSuppItens(@cAlias, ,nPage,nPageSize,cId)

If !EMPTY(AllTrim((cAlias)->C1H_ID)) .AND. (cAlias)->(!Eof())

        oJsonResp["id"]                  :=   AllTrim((cAlias)->C1H_ID)

        If  Empty(Alltrim((cAlias)->C1H_CPF))
            oJsonResp["cpfCNPJ"]         :=   AllTrim((cAlias)->C1H_CNPJ)
        Else
            oJsonResp["cpfCNPJ"]         :=   AllTrim((cAlias)->C1H_CPF)
        EndIf

        oJsonResp["uf"]                  :=   AllTrim((cAlias)->C1H_UF)
        
        If Empty(Alltrim((cAlias)->C1H_CPF))
            oJsonResp["description"]     :=   IIf(Empty((cAlias)->C1H_CNPJ), AllTrim((cAlias)->C1H_NOME), AllTrim((cAlias)->C1H_CNPJ) + " - " +  AllTrim((cAlias)->C1H_NOME))
        Else
            oJsonResp["description"]     :=   IIf(Empty((cAlias)->C1H_CPF), AllTrim((cAlias)->C1H_NOME), AllTrim((cAlias)->C1H_CPF) + " - " +  AllTrim((cAlias)->C1H_NOME))
        EndIf

Else

    oJsonResp['code'] := 400
    oJsonResp['detailMessage'] := ''
    oJsonResp['message'] := "O id do fornecedor não foi encontrado."

EndIf
    
    oRest:SetResponse(oJsonResp:toJson())
    (cAlias)->(DBCloseArea())

Return lRet

/*--------------------------------------------------------------------
{Protheus.doc} GetSuppItens()
(Responsável por executar a consulta aos campos da tabela C1H)
@author Daniel Aguilar
@since 12/01/2024
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetSuppItens(cAlias,cFiltro,nPage,nPageSize,cId)

Local cQuery    := ''  as character

Default cId     := ""
Default cFiltro := ""


cQuery := " SELECT          "
cQuery += " C1H.C1H_ID,     "
cQuery += " C1H.C1H_CNPJ,   "
cQuery += " C1H.C1H_CPF,    "
cQuery += " C1H.C1H_NOME,   "
cQuery += " C1H.C1H_UF      "
cQuery += " FROM " + RetSqlName('C1H') + " C1H "
cQuery += " WHERE C1H.D_E_L_E_T_ = ' '
cQuery += " 	AND C1H.C1H_FILIAL = '" + xFilial('C1H') + "' "
If Empty( cId )
    If !Empty(cFiltro)
        If !FwIsNumeric(cFiltro)
            cQuery += " AND (C1H.C1H_NOME LIKE ('%" +cFiltro+ "%')) "
        Else
            cQuery += "AND (C1H.C1H_NOME LIKE ('%" +cFiltro+ "%') OR C1H.C1H_CPF LIKE ('%" +cFiltro+ "%') OR C1H.C1H_CNPJ LIKE ('%" +cFiltro+ "%'))"
        Endif
    EndIf
    cQuery += " ORDER BY C1H.C1H_ID "
    cQuery += " OFFSET ( " + cValToChar(nPage-1) + " * " + cValToChar(nPageSize) + " ) ROWS "
    cQuery += " FETCH NEXT " + cValToChar(nPageSize+1) + " ROWS ONLY "
Else

    cQuery += " 	AND C1H.C1H_ID = '" + cId + "' "

EndIf


dbUseArea(.T., "TOPCONN", TCGenQry2(, , cQuery,{}), cAlias, .F., .T.)


Return
